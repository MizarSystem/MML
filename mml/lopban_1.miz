:: Banach Space of Bounded Linear Operators
::  by Yasunari Shidama
::
:: Received December 22, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies PRE_TOPC, NORMSP_1, RLVECT_1, QC_LANG1, FUNCT_1, ARYTM, ARYTM_1,
      ARYTM_3, RELAT_1, ABSVALUE, ORDINAL2, BINOP_1, GRCAT_1, UNIALG_1,
      LATTICES, FUNCOP_1, FUNCSDOM, FUNCT_2, RLSUB_1, SEQ_1, SEQ_2, SEQM_3,
      BHSP_3, RSSPACE, RSSPACE3, PARTFUN1, LOPBAN_1, FRAENKEL, MONOID_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_2,
      FUNCT_3, PRE_TOPC, DOMAIN_1, FUNCOP_1, BINOP_1, STRUCT_0, XREAL_0,
      ORDINAL1, NUMBERS, REAL_1, NAT_1, MONOID_0,
      PSCOMP_1, ALG_1, RLVECT_1, FRAENKEL,
      COMPLEX1, RLSUB_1, NORMSP_1, RSSPACE, RSSPACE3, SEQ_1, SEQ_2, SEQM_3,
      SEQ_4, FUNCSDOM;
 constructors MCART_1, FUNCT_3, BINOP_1, ARYTM_0, REAL_1, XREAL_0, NAT_1,
      SQUARE_1, FUNCT_2, XCMPLX_0, SUBSET_1, MEMBERED, DOMAIN_1, SEQ_1, SEQ_2,
      SERIES_1, SEQ_4, PSCOMP_1, FUNCOP_1, FUNCSDOM, PREPOWER, RLVECT_1,
      VECTSP_1, FRAENKEL, PARTFUN1, RLSUB_1, NORMSP_1, BHSP_2, BHSP_3, RSSPACE,
      COMPLEX1, RSSPACE3, MONOID_0;
 registrations SUBSET_1, RELSET_1, STRUCT_0, ORDINAL2, FUNCOP_1, RLVECT_1,
      NORMSP_1, XREAL_0, MEMBERED, SEQ_1, RSSPACE3, SEQM_3, FRAENKEL,
      MONOID_0;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions RLVECT_1, SEQ_4, FRAENKEL;
 theorems XBOOLE_0, REAL_2, AXIOMS, TARSKI, ABSVALUE, REAL_1, RLVECT_1,
      BINOP_1, FUNCOP_1, FUNCSDOM, XREAL_0, XCMPLX_0, SEQ_1, SEQ_2, SEQM_3,
      FUNCT_1, NAT_1, FUNCT_2, RLSUB_1, NORMSP_1, SEQ_4, FUNCT_3, RSSPACE,
      RSSPACE3, MONOID_0;
 schemes BINOP_1, SEQ_1, FUNCT_2, XBOOLE_0;

begin :: Real Vector Space of Operators

definition
  let X be set;
  let Y be non empty set;
  let F be Function of [:REAL, Y:], Y;
  let a be real number, f be Function of X, Y;
  redefine func F[;](a,f) -> Element of Funcs(X, Y);
coherence
proof
A1:a in REAL by XREAL_0:def 1;
     dom f = X by FUNCT_2:def 1;
   then dom f --> a is Function of X,REAL by A1,FUNCOP_1:57;
then A2:<:dom f --> a, f:> is Function of X,[:REAL, Y:] by FUNCT_3:78;
     F[;](a,f) = F * <:dom f --> a, f:> by FUNCOP_1:def 5;
   then F[;](a,f) is Function of X, Y by A2,FUNCT_2:19;
   hence thesis by FUNCT_2:11;
end;
end;

theorem Th1:
for X be non empty set for Y be non empty LoopStr
ex ADD be BinOp of Funcs(X,the carrier of Y) st
  for f,g being Element of Funcs(X,the carrier of Y)
    holds ADD.(f,g)=(the add of Y).:(f,g)
proof
let X be non empty set;
let Y be non empty LoopStr;
deffunc F(Element of Funcs(X,the carrier of Y),
 Element of Funcs(X,the carrier of Y)) = (the add of Y).:($1,$2);
consider ADD being BinOp of Funcs(X,the carrier of Y) such that
A1:  for f,g being Element of Funcs(X,the carrier of Y) holds
    ADD.(f,g) = F(f,g) from BINOP_1:sch 2;
 take ADD;
thus thesis by A1;
end;

theorem Th2:
for X be non empty set, Y be RealLinearSpace
 ex MULT be Function of
  [:REAL, Funcs(X,the carrier of Y):], Funcs(X,the carrier of Y) st
    for r be Real, f be Element of Funcs(X,the carrier of Y) holds
     for s be Element of X holds (MULT.[r,f]).s = r*(f.s)
proof
   let X be non empty set;
   let Y be RealLinearSpace;
   deffunc F(Real,Element of Funcs(X,the carrier of Y)) =
    @((the Mult of Y)[;]($1,$2));
   consider F being Function of
    [:REAL,Funcs(X,the carrier of Y):], Funcs(X,the carrier of Y) such that
A1:for a be Real, f being Element of Funcs(X,the carrier of Y)
    holds F.[a,f] = F(a,f) from FUNCT_2:sch 8;
   take F;
   let a be Real,f be Element of Funcs(X,the carrier of Y),
       x be Element of X;
A2:dom (F.[a,f]) = X by FUNCT_2:169;
     F.[a,f] = @((the Mult of Y)[;](a,f)) by A1
          .= (the Mult of Y) [;](a,f) by FUNCSDOM:def 1;
   hence (F.[a,f]).x = (the Mult of Y).(a,f.x) by A2,FUNCOP_1:42
                    .= (the Mult of Y).[a,f.x] by BINOP_1:def 1
                    .= a*(f.x) by RLVECT_1:def 4;
end;

definition
  let X be non empty set;
  let Y be non empty LoopStr;
  func FuncAdd(X,Y) -> BinOp of Funcs(X,the carrier of Y) means :Def1:
    for f,g being Element of Funcs(X,the carrier of Y) holds
    it.(f,g) = (the add of Y).:(f,g);
 existence by Th1;
 uniqueness
  proof let it1,it2 be BinOp of Funcs(X,the carrier of Y) such that
   A1: for f,g being Element of Funcs(X,the carrier of Y)
                   holds it1.(f,g) = (the add of Y).:(f,g) and
   A2: for f,g being Element of Funcs(X,the carrier of Y)
                   holds it2.(f,g) = (the add of Y).:(f,g);
     now let f,g be Element of Funcs(X,the carrier of Y);
    thus it1.(f,g) = (the add of Y).:(f,g)
          by A1 .= it2.(f,g) by A2; end;
   hence thesis by BINOP_1:2;
  end;
end;

definition
  let X be non empty set;
  let Y be RealLinearSpace;
  func FuncExtMult(X,Y) -> Function of [:REAL,Funcs(X,the carrier of Y):],
     Funcs(X,the carrier of Y) means :Def2:
     for a being Real,
    f being Element of Funcs(X,the carrier of Y),
      x being Element of X holds
      (it.[a,f]).x = a*(f.x);
 existence by Th2;
 uniqueness
  proof
   let it1,it2 be Function of [:REAL,Funcs(X,the carrier of Y):],
        Funcs(X,the carrier of Y) such that
   A1: for a being Real, f being Element of Funcs(X,the carrier of Y),
      x being Element of X holds (it1.[a,f]).x = a*(f.x) and
   A2:  for a being Real, f being Element of Funcs(X,the carrier of Y),
      x being Element of X holds (it2.[a,f]).x = a*(f.x);
     now let a be Element of REAL, f be Element of Funcs(X,the carrier of Y);
      now let x be Element of X;
     thus (it1.[a,f]).x = a*(f.x) by A1 .= (it2.[a,f]).x by A2;
    end;
    hence it1.[a,f] = it2.[a,f] by FUNCT_2:113;
   end;
   hence thesis by FUNCT_2:120;
 end;
end;

definition
  let X be set;
  let Y be non empty ZeroStr;
  func FuncZero(X,Y) -> Element of Funcs (X,the carrier of Y) equals
:Def3:   X --> 0.Y;
 coherence
  proof X --> 0.Y is Function of X,the carrier of Y by FUNCOP_1:58;
   hence thesis by FUNCT_2:11; end;
end;

 reserve X,A,B for non empty set;
 reserve Y for RealLinearSpace;
 reserve f,g,h for Element of Funcs(X,the carrier of Y);

Lm1:
  for A,B be non empty set
  for x being Element of A,
        f being Function of A,B holds x in dom f
  proof
    let A,B be non empty set;
    let x be Element of A, f be Function of A,B;
      x in A; hence x in dom f by FUNCT_2:def 1;
  end;

theorem
   Th3:for Y being non empty LoopStr,
         f,g,h being Element of Funcs(X,the carrier of Y) holds
   h = (FuncAdd(X,Y)).(f,g) iff
           for x being Element of X holds h.x = f.x + g.x
 proof
  let Y be non empty LoopStr,
      f,g,h be Element of Funcs(X,the carrier of Y);
A1: now assume A2: h = (FuncAdd(X,Y)).(f,g);
     let x be Element of X; A3: x in dom ((the add of Y).:(f,g)) by Lm1;
     thus h.x = ((the add of Y).:(f,g)).x by A2,Def1
             .= (the add of Y).(f.x,g.x) by A3,FUNCOP_1:28
             .= (the add of Y).[f.x,g.x] by BINOP_1:def 1
             .= f.x + g.x by RLVECT_1:def 3;
    end;
    now assume A4: for x being Element of X holds h.x=f.x + g.x;
    now let x be Element of X; A5: x in dom ((the add of Y).:(f,g)) by Lm1;
   thus ((FuncAdd(X,Y)).(f,g)).x = ((the add of Y).:(f,g)).x by Def1
                              .= (the add of Y).(f.x,g.x) by A5,FUNCOP_1:28
                              .= (the add of Y).[f.x,g.x] by BINOP_1:def 1
                              .= f.x + g.x by RLVECT_1:def 3
                              .= h.x by A4; end;
  hence h = (FuncAdd(X,Y)).(f,g) by FUNCT_2:113;  end;
 hence thesis by A1; end;

theorem
   Th4: for x being Element of X holds (FuncZero(X,Y)).x = 0.Y
 proof let x be Element of X;
  thus (FuncZero(X,Y)).x = (X --> 0.Y).x by Def3
                          .= 0.Y by FUNCOP_1:13;
 end;

reserve a,b for Real;

theorem
 Th5: h = (FuncExtMult(X,Y)).[a,f] iff
  for x being Element of X holds h.x = a*(f.x)
 proof
 thus h = (FuncExtMult(X,Y)).[a,f] implies
  (for x being Element of X holds h.x = a*(f.x)) by Def2;
    now assume A1: for x being Element of X holds h.x = a*(f.x);
    for x being Element of X holds
                    h.x = ((FuncExtMult(X,Y)).[a,f]).x
     proof let x be Element of X;
    thus h.x = a*(f.x) by A1 .= ((FuncExtMult(X,Y)).[a,f]).x
 by Def2; end;
  hence h = (FuncExtMult(X,Y)).[a,f] by FUNCT_2:113; end;
 hence thesis; end;

reserve u,v,w for VECTOR of RLSStruct(#Funcs(X,the carrier of Y),
            (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#);

theorem
Th6: (FuncAdd(X,Y)).(f,g) = (FuncAdd(X,Y)).(g,f)
 proof
     now let x be Element of X;
  thus ((FuncAdd(X,Y)).(f,g)).x = g.x + f.x by Th3
                             .= ((FuncAdd(X,Y)).(g,f)).x by Th3;
      end;
  hence thesis by FUNCT_2:113;
 end;

theorem
Th7: (FuncAdd(X,Y)).(f,(FuncAdd(X,Y)).(g,h)) =
                    (FuncAdd(X,Y)).((FuncAdd(X,Y)).(f,g),h)
 proof
     now let x be Element of X;
  thus ((FuncAdd(X,Y)).(f,(FuncAdd(X,Y)).(g,h))).x =
                 f.x + ((FuncAdd(X,Y)).(g,h)).x by Th3
     .= f.x + (g.x + h.x) by Th3
     .= (f.x + g.x) + h.x by RLVECT_1:def 6
     .= ((FuncAdd(X,Y)).(f,g)).x + h.x by Th3
     .= ((FuncAdd(X,Y)).((FuncAdd(X,Y)).(f,g),h)).x by Th3; end;
 hence thesis by FUNCT_2:113; end;

theorem
Th8: (FuncAdd(X,Y)).(FuncZero(X,Y),f) = f
 proof
    now let x be Element of X;
   thus ((FuncAdd(X,Y)).(FuncZero(X,Y),f)).x =
    (FuncZero(X,Y)).x + f.x by Th3
    .= 0.Y + f.x by Th4
    .= f.x by RLVECT_1:def 7;
  end;
  hence thesis by FUNCT_2:113;
 end;

theorem
Th9: (FuncAdd(X,Y)).(f,(FuncExtMult(X,Y)).[-1,f]) = FuncZero(X,Y)
 proof
    now let x be Element of X;
   set y=f.x;
   thus ((FuncAdd(X,Y)).(f,(FuncExtMult(X,Y)).[-1,f])).x =
     f.x + ((FuncExtMult(X,Y)).[-1,f]).x by Th3
     .= f.x + ((-1)*y) by Th5
     .= f.x + (-y) by RLVECT_1:29
     .= 0.Y by RLVECT_1:16
     .= (FuncZero(X,Y)).x by Th4;
   end;
  hence thesis by FUNCT_2:113;
  end;

theorem
Th10: (FuncExtMult(X,Y)).[1,f] = f
 proof
    now let x be Element of X;
   thus ((FuncExtMult(X,Y)).[1 qua Real,f]).x = 1*(f.x) by Th5
                                     .=  f.x by RLVECT_1:def 9;
  end;
  hence thesis by FUNCT_2:113;
 end;

theorem
Th11: (FuncExtMult(X,Y)).[a,(FuncExtMult(X,Y)).[b,f]] =
  (FuncExtMult(X,Y)).[a*b,f]
 proof
     now let x be Element of X;
    thus ((FuncExtMult(X,Y)).[a,(FuncExtMult(X,Y)).[b,f]]).x
      = a*(((FuncExtMult(X,Y)).[b,f]).x) by Th5
     .= a*(b*(f.x)) by Th5 .= (a*b)*(f.x) by RLVECT_1:def 9
     .= ((FuncExtMult(X,Y)).[a*b,f]).x by Th5;
   end;
  hence thesis by FUNCT_2:113;
 end;

theorem
Th12: (FuncAdd(X,Y)).
      ((FuncExtMult(X,Y)).[a,f],(FuncExtMult(X,Y)).[b,f])
                                = (FuncExtMult(X,Y)).[a+b,f]
 proof
    now let x be Element of X;
   thus ((FuncAdd(X,Y)).
      ((FuncExtMult(X,Y)).[a,f],(FuncExtMult(X,Y)).[b,f])).x =
      ((FuncExtMult(X,Y)).[a,f]).x  +
                  ((FuncExtMult(X,Y)).[b,f]).x by Th3
     .= a*(f.x)  + ((FuncExtMult(X,Y)).[b,f]).x by Th5
     .= a*(f.x) + b*(f.x) by Th5
     .= (a+b)*(f.x) by RLVECT_1:def 9
     .= ((FuncExtMult(X,Y)).[a+b,f]).x by Th5;
   end;
  hence thesis by FUNCT_2:113;
 end;

Lm2: (FuncAdd(X,Y)).
        ((FuncExtMult(X,Y)).[a,f],(FuncExtMult(X,Y)).[a,g])
   = (FuncExtMult(X,Y)).[a,(FuncAdd(X,Y)).(f,g)]
 proof
    now let x be Element of X;
   thus ((FuncAdd(X,Y)).
    ((FuncExtMult(X,Y)).[a,f],(FuncExtMult(X,Y)).[a,g])).x
   = ((FuncExtMult(X,Y)).[a,f]).x + ((FuncExtMult(X,Y)).[a,g]).x by Th3
   .= a*(f.x) + ((FuncExtMult(X,Y)).[a,g]).x by Th5
   .= a*(f.x) + a*(g.x) by Th5
   .= a*(f.x + g.x) by RLVECT_1:def 9
   .= a*(((FuncAdd(X,Y)).(f,g)).x) by Th3
   .= ((FuncExtMult(X,Y)).[a,(FuncAdd(X,Y)).(f,g)]).x by Th5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
Th13: RLSStruct(#Funcs(X,the carrier of Y),
     (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#)
     is RealLinearSpace
 proof
     set IT = RLSStruct(#Funcs(X,the carrier of Y),
              (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#);
       IT is Abelian add-associative right_zeroed right_complementable
       RealLinearSpace-like
 proof
  thus u+v = v+u proof
   reconsider v' = v, u' = u as Element of Funcs(X,the carrier of Y);
   thus u+v = (FuncAdd(X,Y)).[u',v'] by RLVECT_1:def 3
           .= (FuncAdd(X,Y)).(u',v') by BINOP_1:def 1
           .= (FuncAdd(X,Y)).(v',u') by Th6
           .= (FuncAdd(X,Y)).[v',u'] by BINOP_1:def 1
           .= v+u by RLVECT_1:def 3;
   end;
  thus (u+v)+w = u+(v+w) proof
    reconsider v'=v, u'=u, w'=w as Element of Funcs(X,the carrier of Y);
    thus (u+v)+w = (FuncAdd(X,Y)).[u+v,w'] by RLVECT_1:def 3
            .= (FuncAdd(X,Y)).(u+v,w') by BINOP_1:def 1
            .= (FuncAdd(X,Y)).((FuncAdd(X,Y)).[u',v'],w') by RLVECT_1:def 3
            .= (FuncAdd(X,Y)).((FuncAdd(X,Y)).(u',v'),w') by BINOP_1:def 1
            .= (FuncAdd(X,Y)).(u',(FuncAdd(X,Y)).(v',w')) by Th7
            .= (FuncAdd(X,Y)).(u',(FuncAdd(X,Y)).[v',w']) by BINOP_1:def 1
            .= (FuncAdd(X,Y)).[u',(FuncAdd(X,Y)).[v',w']] by BINOP_1:def 1
            .= (FuncAdd(X,Y)).[u',v+w] by RLVECT_1:def 3
            .= u+(v+w) by RLVECT_1:def 3; end;
  thus u+0.IT = u proof
    reconsider u'=u as Element of Funcs(X,the carrier of Y);
   thus u+0.IT = (FuncAdd(X,Y)).[u',0.IT] by RLVECT_1:def 3
              .= (FuncAdd(X,Y)).[u',FuncZero(X,Y)]
                                         by RLVECT_1:def 2
              .= (FuncAdd(X,Y)).(u',FuncZero(X,Y)) by BINOP_1:def 1
              .= (FuncAdd(X,Y)).(FuncZero(X,Y),u') by Th6
              .= u by Th8;
  end;
  thus ex w st u+w = 0.IT
   proof
    reconsider u' = u as Element of Funcs(X,the carrier of Y);
    reconsider w = (FuncExtMult(X,Y)).[-1,u'] as VECTOR of IT;
    take w;
    thus u+w = (FuncAdd(X,Y)).[u',(FuncExtMult(X,Y)).[-1,u']]
           by RLVECT_1:def 3
            .= (FuncAdd(X,Y)).(u',(FuncExtMult(X,Y)).[-1,u'])
                                                  by BINOP_1:def 1
            .= FuncZero(X,Y) by Th9
            .= 0.IT by RLVECT_1:def 2;
   end;
  thus a*(u+v) = a*u + a *v
   proof
    reconsider v' = v, u' = u as Element of Funcs(X,the carrier of Y);
    reconsider w = (FuncExtMult(X,Y)).[a,u'],w' = (FuncExtMult(X,Y)).[a,v']
 as VECTOR of IT;
    thus a*(u+v) = (FuncExtMult(X,Y)).[a,u+v] by RLVECT_1:def 4
                .=(FuncExtMult(X,Y)).[a,(FuncAdd(X,Y)).[u',v']]
                                                by RLVECT_1:def 3
                .= (FuncExtMult(X,Y)).[a,(FuncAdd(X,Y)).(u',v')]
                                                   by BINOP_1:def 1
                .=(FuncAdd(X,Y)).(w,w') by Lm2
                .= (FuncAdd(X,Y)).[w,w'] by BINOP_1:def 1
                .= w + w' by RLVECT_1:def 3
                .= w + (a*v) by RLVECT_1:def 4
                .= (a*u) + (a*v) by RLVECT_1:def 4;
   end;
  thus (a+b)*v = a*v + b*v
   proof
    reconsider v' = v as Element of Funcs(X,the carrier of Y);
    reconsider w = (FuncExtMult(X,Y)).[a,v'],w' = (FuncExtMult(X,Y)).[b,v']
 as VECTOR of IT;
    thus (a+b)*v = (FuncExtMult(X,Y)).[a+b,v'] by RLVECT_1:def 4
                .= (FuncAdd(X,Y)).(w,w') by Th12
                .= (FuncAdd(X,Y)).[w,w'] by BINOP_1:def 1
                .= w + w' by RLVECT_1:def 3
                .= w + (b*v) by RLVECT_1:def 4
                .= (a*v) + (b*v) by RLVECT_1:def 4;
   end;
  thus (a*b)*v = a*(b*v)
   proof
    reconsider v'=v as Element of Funcs(X,the carrier of Y);
    thus (a*b)*v = (FuncExtMult(X,Y)).[a*b,v'] by RLVECT_1:def 4
                .= (FuncExtMult(X,Y)).[a,(FuncExtMult(X,Y)).[b,v']] by Th11
                .= (FuncExtMult(X,Y)).[a,b*v] by RLVECT_1:def 4
                .= a*(b*v) by RLVECT_1:def 4;
   end;
  thus 1*v = v
   proof
     reconsider v'=v as Element of Funcs(X,the carrier of Y);
     thus 1*v = (FuncExtMult(X,Y)).[1,v'] by RLVECT_1:def 4
              .= v by Th10;
   end;
 end;
hence thesis;
end;

definition
  let X be non empty set;
  let Y be RealLinearSpace;
  func RealVectSpace(X,Y) -> RealLinearSpace equals :Def4:
   RLSStruct(#Funcs(X,the carrier of Y),
              (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#);
  coherence by Th13;
end;

registration
  let X be non empty set;
  let Y be RealLinearSpace;
  cluster RealVectSpace(X,Y) -> strict;
  coherence
proof
    RealVectSpace(X,Y) =
   RLSStruct(#Funcs(X,the carrier of Y),
              (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#) by Def4;
   hence thesis;
 end;
end;

registration
  let X be non empty set;
  let Y be RealLinearSpace;
  cluster RealVectSpace(X,Y) -> constituted-Functions;
  coherence
   proof
     RealVectSpace(X,Y) =
      RLSStruct(#Funcs(X,the carrier of Y),
              (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#) by Def4;
    hence thesis by MONOID_0:89;
   end;
end;

definition
 let X be non empty set;
 let Y be RealLinearSpace;
 let f be VECTOR of RealVectSpace(X,Y);
 let x be Element of X;
 redefine func f.x -> VECTOR of Y;
coherence
proof
    RealVectSpace(X,Y) =
   RLSStruct(#Funcs(X,the carrier of Y),
              (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#) by Def4;
  then consider g being Function such that
A1: f = g and
A2: dom g = X and
A3: rng g c= the carrier of Y by FUNCT_2:def 2;
    f.x in rng g by A1,A2,FUNCT_1:def 5;
  hence thesis by A3;
end;
end;

theorem Th14:
  for X be non empty set
  for Y be RealLinearSpace
  for f,g,h be VECTOR of RealVectSpace(X,Y) holds
      h = f+g iff for x be Element of X holds h.x = f.x + g.x
   proof
     let X be non empty set;
     let Y be RealLinearSpace;
     let f,g,h be VECTOR of RealVectSpace(X,Y);
      A1:  RealVectSpace(X, Y) =
          RLSStruct(#Funcs(X,the carrier of Y),
                     FuncZero(X,Y),
                     FuncAdd(X,Y),
                     FuncExtMult(X,Y)#) by Def4;
     then reconsider f'=f,g'=g,h'=h as Element of Funcs(X, the carrier of Y);
hereby assume
     A2: h = f+g;
     let x be Element of X;
        h = (FuncAdd(X,Y)).[f',g'] by A1,A2,RLVECT_1:def 3
       .= (FuncAdd(X,Y)).(f',g') by BINOP_1:def 1;
     hence h.x = f.x+g.x by Th3;
    end;
    assume for x be Element of X holds h.x=f.x+g.x;
    then A3:  for x be Element of X holds h'.x=f'.x+g'.x;
    f+g = (FuncAdd(X,Y)).[f',g'] by A1,RLVECT_1:def 3
    .= (FuncAdd(X,Y)).(f',g') by BINOP_1:def 1;
    hence h = f+g by A3,Th3;
 end;

theorem Th15:
  for X be non empty set
  for Y be RealLinearSpace
  for f,h be VECTOR of RealVectSpace(X,Y)
  for a be Real holds
      h = a*f iff for x be Element of X holds h.x = a * f.x
      proof
      let X be non empty set;
      let Y be RealLinearSpace;
      let f,h be VECTOR of RealVectSpace(X,Y);
      let a be Real;
       A1:  RealVectSpace(X , Y) =
           RLSStruct(#Funcs(X,the carrier of Y),
                      FuncZero(X,Y),
                      FuncAdd(X,Y),
                      FuncExtMult(X,Y)#) by Def4;
      then reconsider f'=f,h'=h as Element of Funcs(X, the carrier of Y);
hereby assume
       A2: h = a*f;
       let x be Element of X;
         h= (FuncExtMult(X, Y)).[a,f'] by A1,A2,RLVECT_1:def 4;
       hence h.x = a*f.x by Th5;
     end;
  assume for x be Element of X holds h.x=a*f.x;
  then h'=(FuncExtMult(X, Y)).[a,f'] by Th5;
  hence h =a*f by A1,RLVECT_1:def 4;
 end;

theorem Th16:
  for X be non empty set
  for Y be RealLinearSpace holds  0.RealVectSpace(X,Y) = X --> 0.Y
    proof
    let X be non empty set;
    let Y be RealLinearSpace;
    set z = X -->0.Y;
      RealVectSpace(X,Y) = RLSStruct(#Funcs(X,the carrier of Y),
              FuncZero(X,Y),FuncAdd(X,Y),FuncExtMult(X,Y)#) by Def4;
    then 0.RealVectSpace(X,Y) = FuncZero(X,Y) by RLVECT_1:def 2;
    hence thesis by Def3;
    end;

begin :: Real Vector Space of Linear Operators

definition
  let X be non empty RLSStruct;
  let Y be non empty LoopStr;
  let IT be Function of X, Y;
  attr IT is additive means :Def5:
  for x,y being VECTOR of X holds IT.(x+y) = IT.x+IT.y;
end;

definition
  let X, Y be non empty RLSStruct;
  let IT be Function of X,Y;
  attr IT is homogeneous means  :Def6:
   for x being VECTOR of X, r being Real holds IT.(r*x) = r*IT.x;
end;

registration
  let X be non empty RLSStruct;
  let Y be RealLinearSpace;
  cluster additive homogeneous Function of X,Y;
  existence
  proof
    set f = (the carrier of X) --> 0.Y;
     reconsider f as Function of X,Y by FUNCOP_1:58;
     take f;
     hereby let x,y be VECTOR of X;
      thus f.(x+y) = 0.Y by FUNCOP_1:13
                     .= 0.Y+0.Y by RLVECT_1:10
                     .= f.x+0.Y by FUNCOP_1:13
                     .= f.x+f.y by FUNCOP_1:13;
      end;
     hereby let x be VECTOR of X, r be Real;
     thus f.(r*x) =0.Y by FUNCOP_1:13
                    .=r*0.Y by RLVECT_1:23
        .= r*f.x by FUNCOP_1:13;
     end;
    end;
end;

definition
  let X, Y be RealLinearSpace;
  mode LinearOperator of X,Y is additive homogeneous Function of X,Y;
end;

definition
  let X, Y be RealLinearSpace;
  func LinearOperators(X,Y) -> Subset of
     RealVectSpace(the carrier of X, Y) means :Def7:
   for x being set holds x in it
     iff
   x is LinearOperator of X,Y;
existence
proof
  defpred P[set] means $1 is LinearOperator of X,Y;
   consider IT being set such that
A1:for x being set holds x in IT
    iff x in Funcs(the carrier of X,the carrier of Y)  & P[x] from
XBOOLE_0:sch 1;
A2:  IT is Subset of Funcs(the carrier of X,the carrier of Y)
   proof
       for x be set st x in IT holds x
     in Funcs(the carrier of X,the carrier of Y) by A1;
     hence thesis by TARSKI:def 3;
   end;
  A3: the carrier of RealVectSpace(the carrier of X , Y)
    = Funcs(the carrier of X, the carrier of Y)
  proof
    RealVectSpace(the carrier of X , Y) =
  RLSStruct(#Funcs(the carrier of X,the carrier of Y),
                FuncZero(the carrier of X,Y),
                FuncAdd(the carrier of X,Y),
                FuncExtMult(the carrier of X,Y)#) by Def4;
  hence thesis;
  end;
  take IT;
  thus IT is Subset of RealVectSpace(the carrier of X, Y) by A2,A3;
  let x be set;
  thus x in IT implies x is LinearOperator of X,Y by A1;
 assume
A4:  x is LinearOperator of X,Y;
  then x in Funcs(the carrier of X,the carrier of Y) by FUNCT_2:11;
  hence thesis by A1,A4;
end;
uniqueness
proof
   let X1,X2 be Subset of RealVectSpace(the carrier of X, Y);
 assume that
A5: for x being set holds x in X1 iff
     x is LinearOperator of X,Y and
A6: for x being set holds x in X2 iff
     x is LinearOperator of X,Y;
      for x being set st x in X1 holds x in X2
    proof
      let x be set;
    assume x in X1;
      then x is LinearOperator of X,Y by A5;
      hence thesis by A6;
    end;
then A7: X1 c= X2 by TARSKI:def 3;
      for x being set st x in X2 holds x in X1
    proof
      let x be set;
      assume x in X2;
      then x is LinearOperator of X,Y by A6;
      hence thesis by A5;
    end;
    then X2 c= X1 by TARSKI:def 3;
    hence thesis by A7,XBOOLE_0:def 10;
  end;
end;

registration
  let X, Y be RealLinearSpace;
  cluster LinearOperators(X,Y)  -> non empty functional;
  coherence
   proof
    set f = (the carrier of X) --> 0.Y;
        reconsider f as Function of X,Y by FUNCOP_1:58;
    A1:  f is additive
     proof
       let x,y be VECTOR of X;
       thus f.(x+y) = 0.Y by FUNCOP_1:13
                            .=0.Y+0.Y by RLVECT_1:10
                            .= f.x+0.Y by FUNCOP_1:13
                     .= f.x+f.y by FUNCOP_1:13;
     end;
       f is homogeneous
     proof
        let x be VECTOR of X, r be Real;
        thus f.(r*x) =0.Y by FUNCOP_1:13
                    .=r*0.Y by RLVECT_1:23
        .= r*f.x by FUNCOP_1:13;
      end;
  hence LinearOperators(X,Y) is non empty by A1,Def7;
  let x be set;
  assume x in LinearOperators(X,Y);
  hence thesis by Def7;
end;
end;

theorem Th17:
  for X, Y be RealLinearSpace holds LinearOperators(X,Y) is lineary-closed
proof
  let X, Y be RealLinearSpace;
  set W = LinearOperators(X,Y);
A1:  RealVectSpace(the carrier of X, Y) =
          RLSStruct(#Funcs(the carrier of X,the carrier of Y),
                     FuncZero(the carrier of X,Y),
                     FuncAdd(the carrier of X,Y),
                     FuncExtMult(the carrier of X,Y)#) by Def4;
A2:for v,u be VECTOR of RealVectSpace(the carrier of X, Y) st
    v in LinearOperators(X,Y) &
    u in LinearOperators(X,Y)
    holds v + u in LinearOperators(X,Y)
   proof
     let v,u be VECTOR of RealVectSpace(the carrier of X ,Y) such that
A3: v in W & u in W;
    v+u is LinearOperator of X,Y
      proof
         reconsider f=v+u as Function of X,Y by A1,FUNCT_2:121;
           f is LinearOperator of X,Y
          proof
            A4:  f is additive
             proof
                let x,y be VECTOR of X;
                reconsider v' = v, u' = u as Element of
                Funcs(the carrier of X,the carrier of Y) by A1;
                A5: f=
                (FuncAdd(the carrier of X,Y)).[u',v'] by A1,RLVECT_1:def 3
                 .= (FuncAdd(the carrier of X,Y)).(u',v') by BINOP_1:def 1;
                A6: u' is LinearOperator of X,Y by A3,Def7;
                A7: v' is LinearOperator of X,Y by A3,Def7;
                thus f.(x+y) =u'.(x+y)+v'.(x+y) by A5,Th3
               .=(u'.x+u'.y)+v'.(x+y) by A6,Def5
               .=u'.x+u'.y+(v'.x+v'.y) by A7,Def5
               .=u'.x+u'.y+v'.x+v'.y by RLVECT_1:def 6
               .=u'.x+v'.x+u'.y+v'.y by RLVECT_1:def 6
              .= f.x+ u'.y+v'.y by A5,Th3
               .= f.x+ (u'.y+v'.y) by RLVECT_1:def 6
               .= f.x+f.y by A5,Th3;
              end;
               f is homogeneous
               proof
                 let x be VECTOR of X, s be Real;
                reconsider v' = v, u' = u as Element of
                Funcs(the carrier of X,the carrier of Y) by A1;
                A8: f=
                 (FuncAdd(the carrier of X,Y)).[u',v'] by A1,RLVECT_1:def 3
                 .= (FuncAdd(the carrier of X,Y)).(u',v') by BINOP_1:def 1;
                A9: u' is LinearOperator of X,Y by A3,Def7;
                A10: v' is LinearOperator of X,Y by A3,Def7;
                thus f.(s*x) =u'.(s*x)+v'.(s*x) by A8,Th3
               .=(s*(u'.x))+v'.(s*x) by A9,Def6
               .=s*u'.x+s*v'.x by A10,Def6
               .=s*(u'.x+v'.x) by RLVECT_1:def 9
               .= s*f.x by A8,Th3;
               end;
          hence thesis by A4;
     end;
      hence thesis;
     end;
    hence v+u in W by Def7;
  end;

  for a be Real for v be VECTOR of RealVectSpace(the carrier of X,Y)
    st v in W holds a * v in W
    proof
     let a be Real;
     let v be VECTOR of RealVectSpace(the carrier of X,Y) such that
    A11: v in W;
         a*v is LinearOperator of X,Y
         proof
         reconsider f=a*v as Function of X,Y by A1,FUNCT_2:121;
           f is LinearOperator of X,Y
          proof
            A12:  f is additive
             proof
                let x,y be VECTOR of X;
                reconsider v' = v as Element of
                Funcs(the carrier of X,the carrier of Y) by A1;
                A13: f= (FuncExtMult(the carrier of X,Y)).[a,v']
             by A1,RLVECT_1:def 4;
                A14: v' is LinearOperator of X,Y by A11,Def7;
                thus f.(x+y) = a*(v'.(x+y)) by A13,Th5
               .=a*(v'.x+v'.y) by A14,Def5
               .=a*v'.x+a*v'.y by RLVECT_1:def 9
               .=f.x+a*v'.y by A13,Th5
               .=f.x+ f.y by A13,Th5;
              end;
              f is homogeneous
               proof
                let x be VECTOR of X, s be Real;
                reconsider v' = v as Element of
                Funcs(the carrier of X,the carrier of Y) by A1;
                A15: f= (FuncExtMult(the carrier of X,Y)).[a,v']
             by A1,RLVECT_1:def 4;
                A16: v' is LinearOperator of X,Y by A11,Def7;
                thus f.(s*x) =a*v'.(s*x) by A15,Th5
               .=a*(s*(v'.x)) by A16,Def6
               .=a*s*v'.x by RLVECT_1:def 9
               .=s*(a*v'.x) by RLVECT_1:def 9
               .=s*f.x by A15,Th5;
               end;
       hence thesis by A12;
     end;
   hence thesis;
  end;
  hence thesis by Def7;
 end;
 hence thesis by A2,RLSUB_1:def 1;
end;

theorem Th18:
  for X, Y be RealLinearSpace holds
RLSStruct (# LinearOperators(X,Y),
        Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
        Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
        Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
is Subspace of RealVectSpace(the carrier of X,Y)
proof
  let X, Y be RealLinearSpace;
    LinearOperators(X,Y) is lineary-closed by Th17;
  hence thesis by RSSPACE:13;
end;

registration
  let X, Y be RealLinearSpace;
  cluster RLSStruct (# LinearOperators(X,Y),
        Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
        Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
        Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
 -> Abelian add-associative right_zeroed right_complementable
        RealLinearSpace-like;
  coherence by Th18;
end;

canceled;

definition
  let X, Y be RealLinearSpace;
  func R_VectorSpace_of_LinearOperators(X,Y) -> RealLinearSpace equals:Def8:
  RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #);
  coherence;
end;

registration
  let X, Y be RealLinearSpace;
  cluster R_VectorSpace_of_LinearOperators(X,Y) -> strict;
  coherence
proof
    R_VectorSpace_of_LinearOperators(X,Y) =
  RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
       by Def8;
   hence thesis;
 end;
end;

registration
 let X,Y be RealLinearSpace;
 cluster R_VectorSpace_of_LinearOperators(X,Y) -> constituted-Functions;
 coherence
  proof
    R_VectorSpace_of_LinearOperators(X,Y) =
  RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
       by Def8;
   hence thesis by MONOID_0:89;
  end;
end;

definition
 let X,Y be RealLinearSpace;
 let f be Element of R_VectorSpace_of_LinearOperators(X,Y);
 let v be VECTOR of X;
 redefine func f.v -> VECTOR of Y;
coherence
proof
    R_VectorSpace_of_LinearOperators(X,Y) =
  RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
       by Def8;
  then reconsider f as LinearOperator of X,Y by Def7;
    f.v is VECTOR of Y;
  hence thesis;
end;
end;

theorem Th20:
  for X, Y be RealLinearSpace
  for f,g,h be VECTOR of R_VectorSpace_of_LinearOperators(X,Y) holds
      h = f+g iff for x be VECTOR of X holds h.x = f.x + g.x
proof
     let X, Y be RealLinearSpace;
     let f,g,h be VECTOR of R_VectorSpace_of_LinearOperators(X,Y);
A1:      R_VectorSpace_of_LinearOperators(X,Y) =
      RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
       by Def8;
       then reconsider f'=f,g'=g,h'=h as LinearOperator of X,Y by Def7;
      A2:R_VectorSpace_of_LinearOperators(X,Y) is
           Subspace of RealVectSpace(the carrier of X,Y) by A1,Th18;
        then reconsider f1=f as VECTOR
                         of RealVectSpace(the carrier of X,Y) by RLSUB_1:18;
       reconsider g1=g as VECTOR
                  of RealVectSpace(the carrier of X,Y) by A2,RLSUB_1:18;
       reconsider h1=h as VECTOR
                   of RealVectSpace(the carrier of X,Y) by A2,RLSUB_1:18;
       reconsider f1'=f' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
       reconsider g1'=g' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
       reconsider h1'=h' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
      A3: now assume
            A4: h = f+g;
             let x be Element of X;
               h1=f1+g1 by A2,A4,RLSUB_1:21;
             hence h'.x=f'.x+g'.x by Th14;
    end;
       now assume
              for x be Element of X holds h'.x=f'.x+g'.x;
           then h1=f1+g1 by Th14;
           hence h =f+g by A2,RLSUB_1:21;
     end;
     hence thesis by A3;
 end;

theorem Th21:
  for X, Y be RealLinearSpace
  for f,h be VECTOR of R_VectorSpace_of_LinearOperators(X,Y)
  for a be Real holds
      h = a*f iff for x be VECTOR of X holds h.x = a * f.x
  proof
     let X, Y be RealLinearSpace;
     let f,h be VECTOR of R_VectorSpace_of_LinearOperators(X,Y);
     let a be Real;
A1:    R_VectorSpace_of_LinearOperators(X,Y) =
      RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
       by Def8;
       then reconsider f'=f,h'=h as LinearOperator of X,Y by Def7;
      A2:R_VectorSpace_of_LinearOperators(X,Y) is
           Subspace of RealVectSpace(the carrier of X,Y)
        by A1,Th18;
        then reconsider f1=f as VECTOR
                         of RealVectSpace(the carrier of X,Y) by RLSUB_1:18;
       reconsider h1=h as VECTOR
                  of RealVectSpace(the carrier of X,Y) by A2,RLSUB_1:18;
       reconsider f1'=f' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
       reconsider h1'=h' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
      A3: now assume
            A4: h = a*f;
             let x be Element of X;
               h1=a*f1 by A2,A4,RLSUB_1:22;
             hence h'.x=a*f'.x by Th15;
    end;
       now assume
              for x be Element of X holds h'.x=a*f'.x;
           then h1=a*f1 by Th15;
           hence h =a*f by A2,RLSUB_1:22;
     end;
     hence thesis by A3;
 end;

theorem Th22:
  for X, Y be RealLinearSpace holds
  0.R_VectorSpace_of_LinearOperators(X,Y) = (the carrier of X) -->0.Y
     proof
     let X, Y be RealLinearSpace;
        R_VectorSpace_of_LinearOperators(X,Y) =
      RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
       by Def8;
      then A1:R_VectorSpace_of_LinearOperators(X,Y) is
           Subspace of RealVectSpace(the carrier of X,Y) by Th18;
       0.RealVectSpace(the carrier of X,Y)
          =((the carrier of X) -->0.Y) by Th16;
     hence thesis by A1,RLSUB_1:19;
    end;

theorem Th23:
  for X,Y be RealLinearSpace holds
       (the carrier of X) --> 0.Y is LinearOperator of X,Y
  proof
    let X,Y be RealLinearSpace;
    set f=(the carrier of X) --> 0.Y;
            reconsider f as Function of X,Y by FUNCOP_1:58;
                A1:  f is additive
                proof
                let x,y be VECTOR of X;
                thus f.(x+y) = 0.Y by FUNCOP_1:13
                      .=0.Y+0.Y by RLVECT_1:10
                     .= f.x+0.Y by FUNCOP_1:13
                     .= f.x+f.y by FUNCOP_1:13;
                  end;
                  f is homogeneous
                   proof
                     let x be VECTOR of X, r be Real;
                     thus f.(r*x) =0.Y by FUNCOP_1:13
                       .=r*0.Y by RLVECT_1:23
                       .= r*f.x by FUNCOP_1:13;
                   end;
     hence thesis by A1;
 end;

begin :: Real Normed Linear Space of Bounded Linear Operators

theorem Th24:
  for X be RealNormSpace
   for seq be sequence of X
    for g be Point of X holds
   seq is convergent & lim seq = g implies
      ||.seq.|| is convergent & lim ||.seq.|| = ||.g.||
proof
   let X be RealNormSpace;
   let seq be sequence of X;
   let g be Point of X;
 assume that
A1:  seq is convergent and
A2:  lim seq = g;
A3:   ||.seq.|| is convergent by A1,NORMSP_1:39;
         now let r be real number;
A4:    r is Real by XREAL_0:def 1;
     assume r > 0;
          then consider m1 be Nat such that
     A5:   for n be Nat st n >= m1 holds ||.(seq.n) - g.|| < r
      by A1,A2,A4,NORMSP_1:def 11;
          take k = m1;
            now let n be Nat; assume n >= k;
          then A6: ||.(seq.n) - g.|| < r by A5;
                   abs(||.(seq.n).|| - ||.g.||) <= ||.(seq.n) - g.||
 by NORMSP_1:13;
               then abs(||.(seq.n).|| - ||.g.||) < r by A6,AXIOMS:22;
               hence abs(||.seq.||.n - ||.g.||) < r by NORMSP_1:def 10;
               end;
          hence for n be Nat st k <= n holds abs(||.seq.||.n - ||.g.||) < r;
          end;
    hence thesis by A3,SEQ_2:def 7;
end;

definition
  let X, Y be RealNormSpace;
  let IT be LinearOperator of X,Y;
  attr IT is bounded means :Def9:
  ex K being Real st 0 <= K &
   for x being VECTOR of X holds
       ||. IT.x .|| <= K * ||. x .||;
end;

theorem Th25:
  for X, Y be RealNormSpace holds
      for f be LinearOperator of X,Y
         st (for x be VECTOR of X holds f.x=0.Y)
        holds f is bounded
 proof
    let X, Y be RealNormSpace;
    let f be LinearOperator of X,Y such that
        A1: for x be VECTOR of X holds f.x=0.Y;
    A2:
      now
        let x be VECTOR of X;
        thus ||. f.x .|| = ||. 0.Y .|| by A1
         .=0*||. x .|| by NORMSP_1:5;
       end;
     take 0;
     thus thesis by A2;
  end;

registration
  let X, Y be RealNormSpace;
  cluster bounded LinearOperator of X,Y;
existence
proof
  set f=(the carrier of X) --> 0.Y;
  reconsider f as LinearOperator of X,Y by Th23;
  take f;
    for x be VECTOR of X holds f.x =0.Y by FUNCOP_1:13;
  hence thesis by Th25;
 end;
end;

definition
  let X, Y be RealNormSpace;
  func BoundedLinearOperators(X,Y) ->
    Subset of R_VectorSpace_of_LinearOperators(X,Y) means :Def10:
   for x being set holds x in it
     iff
   x is bounded LinearOperator of X,Y;
existence
proof
  defpred P[set] means $1 is bounded LinearOperator of X,Y;
  consider IT being set such that
  A1:for x being set holds x in IT
    iff x in LinearOperators(X,Y) & P[x] from XBOOLE_0:sch 1;
A2:  IT is Subset of LinearOperators(X,Y)
   proof
       for x be set st x in IT holds x
     in LinearOperators(X,Y) by A1;
     hence thesis by TARSKI:def 3;
   end;
  A3: the carrier of R_VectorSpace_of_LinearOperators(X,Y)
    = LinearOperators(X,Y)
  proof
    R_VectorSpace_of_LinearOperators(X,Y) =
  RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
 by Def8;
  hence thesis;
  end;
  take IT;
  thus IT is Subset of R_VectorSpace_of_LinearOperators(X,Y) by A2,A3;
  let x be set;
  thus x in IT implies x is bounded LinearOperator of X,Y by A1;
 assume
A4:  x is bounded LinearOperator of X,Y;
  then x in LinearOperators(X,Y) by Def7;
  hence thesis by A1,A4;
end;
uniqueness
proof
   let X1,X2 be Subset of R_VectorSpace_of_LinearOperators(X,Y);
 assume that
A5: for x being set holds x in X1 iff
     x is bounded LinearOperator of X,Y and
A6: for x being set holds x in X2 iff
     x is bounded LinearOperator of X,Y;
      for x being set st x in X1 holds x in X2
    proof
      let x be set;
    assume x in X1;
      then x is bounded LinearOperator of X,Y by A5;
      hence thesis by A6;
    end;
then A7: X1 c= X2 by TARSKI:def 3;
      for x being set st x in X2 holds x in X1
    proof
      let x be set;
      assume x in X2;
      then x is bounded LinearOperator of X,Y by A6;
      hence thesis by A5;
    end;
    then X2 c= X1 by TARSKI:def 3;
    hence thesis by A7,XBOOLE_0:def 10;
  end;
end;

registration
  let X, Y be RealNormSpace;
  cluster BoundedLinearOperators(X,Y) -> non empty;
  coherence
   proof
     set f=(the carrier of X) --> 0.Y;
     reconsider f as LinearOperator of X,Y by Th23;
       f is bounded
     proof
         for x be VECTOR of X holds f.x =0.Y by FUNCOP_1:13;
       hence thesis by Th25;
     end;
     hence thesis by Def10;
   end;
end;

theorem Th26:
  for X, Y be RealNormSpace holds
  BoundedLinearOperators(X,Y) is lineary-closed
proof
  let X, Y be RealNormSpace;
  set W = BoundedLinearOperators(X,Y);
A1:  R_VectorSpace_of_LinearOperators(X,Y) =
  RLSStruct (# LinearOperators(X,Y),
             Zero_(LinearOperators(X,Y),RealVectSpace(the carrier of X,Y)),
           Add_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)),
           Mult_(LinearOperators(X,Y), RealVectSpace(the carrier of X,Y)) #)
 by Def8;
A2:for v,u be VECTOR of R_VectorSpace_of_LinearOperators(X, Y) st
    v in W & u in W
    holds v + u in W
   proof
     let v,u be VECTOR of R_VectorSpace_of_LinearOperators(X, Y) such that
     A3: v in W & u in W;
     reconsider f=v+u as LinearOperator of X,Y by A1,Def7;
        f is bounded
     proof
     reconsider u1=u as bounded LinearOperator of X,Y by A3,Def10;
     reconsider v1=v as bounded LinearOperator of X,Y by A3,Def10;
     consider K1 be Real such that
       A4: 0 <= K1 &
     for x be VECTOR of X holds ||. u1.x .|| <= K1*||. x .|| by Def9;
     consider K2 be Real such that
       A5: 0 <= K2 &
     for x be VECTOR of X holds ||. v1.x .|| <= K2*||. x .|| by Def9;
     take K3=K1+K2;
A6:         0 + 0 <= K1+K2 by A4,A5,REAL_1:55;
         now let x be VECTOR of X;
        A7: ||. f.x .|| =||. u1.x+v1.x .|| by Th20;
        A8: ||. u1.x+v1.x .|| <= ||. u1.x .||+ ||. v1.x .|| by NORMSP_1:def 2;
        A9:||. u1.x .|| <= K1*||. x .|| by A4;
         ||. v1.x .|| <= K2*||. x .|| by A5;
        then ||. u1.x .|| + ||. v1.x .|| <= K1*||. x .|| +K2*||. x .||
      by A9,REAL_1:55;
       hence ||. f.x .|| <= K3*||. x .|| by A7,A8,AXIOMS:22;
        end;
     hence thesis by A6;
     end;
     hence thesis by Def10;
  end;

  for a be Real for v be VECTOR of R_VectorSpace_of_LinearOperators(X,Y)
    st v in W holds a * v in W
    proof
     let a be Real;
     let v be VECTOR of R_VectorSpace_of_LinearOperators(X,Y) such that
           A10: v in W;
          reconsider f=a*v as LinearOperator of X,Y by A1,Def7;
            f is bounded
          proof
     reconsider v1=v as bounded LinearOperator of X,Y by A10,Def10;
     consider K be Real such that
A11: 0 <= K &
        for x be VECTOR of X holds ||. v1.x .|| <= K*||. x .|| by Def9;
      take K1=abs(a)*K;
A12:      0 <= K & 0 <=abs(a) by A11,ABSVALUE:5;
        now let x be VECTOR of X;
        A13: ||. a*v1.x .|| = abs(a)* ||. v1.x .|| by NORMSP_1:def 2;
        A14:||. v1.x .|| <= K*||. x .|| by A11;
         0 <=abs(a) by ABSVALUE:5;
         then abs(a)* ||. v1.x .|| <= abs(a)* (K*||. x .||) by A14,AXIOMS:25;
       hence ||. f.x .|| <= abs(a)* K*||. x .|| by A13,Th21;
     end;
     hence thesis by A12,REAL_2:121;
     end;
     hence thesis by Def10;
     end;
 hence thesis by A2,RLSUB_1:def 1;
end;

theorem Th27:
  for X, Y be RealNormSpace holds
RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
        Mult_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)) #)
is Subspace of R_VectorSpace_of_LinearOperators(X,Y)
proof
  let X, Y be RealNormSpace;
    BoundedLinearOperators(X,Y) is lineary-closed by Th26;
  hence thesis by RSSPACE:13;
end;

registration
  let X, Y be RealNormSpace;
  cluster RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
        Mult_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)) #)
 -> Abelian add-associative right_zeroed right_complementable
        RealLinearSpace-like;
  coherence by Th27;
end;

canceled;

definition
  let X, Y be RealNormSpace;
  func R_VectorSpace_of_BoundedLinearOperators(X,Y) -> RealLinearSpace equals
  :Def11:
  RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
        Mult_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)) #);
  coherence;
end;

registration
  let X, Y be RealNormSpace;
  cluster R_VectorSpace_of_BoundedLinearOperators(X,Y) -> strict;
  coherence
proof
    R_VectorSpace_of_BoundedLinearOperators(X,Y) =
  RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
   hence thesis;
 end;
end;

registration
 let X,Y be RealNormSpace;
 cluster -> Function-like Relation-like Element of
            R_VectorSpace_of_BoundedLinearOperators(X,Y);
 coherence
proof
    R_VectorSpace_of_BoundedLinearOperators(X,Y) =
  RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
  hence thesis by Def10;
end;
end;

definition
 let X,Y be RealNormSpace;
 let f be Element of R_VectorSpace_of_BoundedLinearOperators(X,Y);
 let v be VECTOR of X;
 redefine func f.v -> VECTOR of Y;
coherence
proof
    R_VectorSpace_of_BoundedLinearOperators(X,Y) =
  RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
  then reconsider f as LinearOperator of X,Y by Def10;
    f.v is VECTOR of Y;
  hence thesis;
end;
end;

theorem Th29:
  for X, Y be RealNormSpace
  for f,g,h be VECTOR of R_VectorSpace_of_BoundedLinearOperators(X,Y) holds
      h = f+g iff for x be VECTOR of X holds h.x = f.x + g.x
proof
     let X, Y be RealNormSpace;
     let f,g,h be VECTOR of R_VectorSpace_of_BoundedLinearOperators(X,Y);
A1:  R_VectorSpace_of_BoundedLinearOperators(X,Y) =
  RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
      then reconsider f'=f,g'=g,h'=h as bounded LinearOperator of X,Y by Def10;
      A2:R_VectorSpace_of_BoundedLinearOperators(X,Y) is
           Subspace of R_VectorSpace_of_LinearOperators(X,Y) by A1,Th27;
        then reconsider f1=f as VECTOR
           of R_VectorSpace_of_LinearOperators(X,Y) by RLSUB_1:18;
       reconsider g1=g as VECTOR
          of R_VectorSpace_of_LinearOperators(X,Y) by A2,RLSUB_1:18;
       reconsider h1=h as VECTOR
         of R_VectorSpace_of_LinearOperators(X,Y) by A2,RLSUB_1:18;
       reconsider f1'=f' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
       reconsider g1'=g' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
       reconsider h1'=h' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
      hereby assume
            A3: h = f+g;
             let x be Element of X;
                    h1=f1+g1 by A2,A3,RLSUB_1:21;
                       hence h.x=f.x+g.x by Th20;
    end;
    assume
              for x be Element of X holds h.x=f.x+g.x;
           then h1=f1+g1 by Th20;
           hence h =f+g by A2,RLSUB_1:21;
 end;

theorem Th30:
  for X, Y be RealNormSpace
  for f,h be VECTOR of R_VectorSpace_of_BoundedLinearOperators(X,Y)
  for a be Real holds
      h = a*f iff for x be VECTOR of X holds h.x = a * f.x
  proof
     let X, Y be RealNormSpace;
     let f,h be VECTOR of R_VectorSpace_of_BoundedLinearOperators(X,Y);
     let a be Real;
A1:  R_VectorSpace_of_BoundedLinearOperators(X,Y) =
  RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
     then reconsider f'=f,h'=h as bounded LinearOperator of X,Y by Def10;
      A2:R_VectorSpace_of_BoundedLinearOperators(X,Y) is
           Subspace of R_VectorSpace_of_LinearOperators(X,Y)
        by A1,Th27;
        then reconsider f1=f as VECTOR
                   of R_VectorSpace_of_LinearOperators(X,Y) by RLSUB_1:18;
       reconsider h1=h as VECTOR
                of R_VectorSpace_of_LinearOperators(X,Y) by A2,RLSUB_1:18;
       reconsider f1'=f' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
       reconsider h1'=h' as Element
            of Funcs(the carrier of X, the carrier of Y) by FUNCT_2:11;
      hereby assume
            A3: h = a*f;
             let x be Element of X;
               h1=a*f1 by A2,A3,RLSUB_1:22;
             hence h.x=a*f.x by Th21;
    end;
    assume
              for x be Element of X holds h.x=a*f.x;
           then h1=a*f1 by Th21;
           hence h =a*f by A2,RLSUB_1:22;
 end;

theorem Th31:
     for X, Y be RealNormSpace holds
          0.R_VectorSpace_of_BoundedLinearOperators(X,Y)
          = (the carrier of X) --> 0.Y
     proof
     let X, Y be RealNormSpace;
       R_VectorSpace_of_BoundedLinearOperators(X,Y) =
    RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
      then A1:R_VectorSpace_of_BoundedLinearOperators(X,Y) is
           Subspace of R_VectorSpace_of_LinearOperators(X,Y) by Th27;
       0.R_VectorSpace_of_LinearOperators(X,Y)
          =((the carrier of X) -->0.Y) by Th22;
     hence thesis by A1,RLSUB_1:19;
    end;

definition
  let X, Y be RealNormSpace;
  let f be set such that
  A1: f in BoundedLinearOperators(X,Y);
  func modetrans(f,X,Y) -> bounded LinearOperator of X,Y equals:Def12:
    f;
  coherence by A1,Def10;
end;

definition
   let X, Y be RealNormSpace;
   let u be LinearOperator of X,Y;
   func PreNorms(u) -> non empty Subset of REAL equals :Def13:
  {||.u.t.|| where t is VECTOR of X : ||.t.|| <= 1 };
 coherence
 proof
   A1: now let x be set;
           now assume
             x in {||.u.t.|| where t is VECTOR of X : ||.t.|| <= 1 };
           then consider t be VECTOR of X such that
            A2:   x=||.u.t.|| & ||.t.|| <= 1;
          thus x in REAL by A2;
         end;
         hence
           x in {||.u.t.|| where t is VECTOR of X : ||.t.|| <= 1 } implies
        x in REAL;
       end;
      ||.0.X.|| = 0 by NORMSP_1:5;
    then ||.u.(0.X).|| in {||.u.t.|| where t is VECTOR of X : ||.t.|| <= 1 };
    hence thesis by A1,TARSKI:def 3;
  end;
end;

theorem Th32:
    for X, Y be RealNormSpace
    for g be bounded LinearOperator of X,Y
      holds PreNorms(g) is non empty bounded_above
   proof
       let X, Y be RealNormSpace;
       let g be bounded LinearOperator of X,Y;
     PreNorms(g) is bounded_above
   proof
  consider K be Real such that
    A1: 0 <= K and
    A2: for x be VECTOR of X holds
       ||. g.x .|| <= K*||. x .|| by Def9;
     A3:now let r be Real such that
        A4:  r in  PreNorms(g);
           r in  {||.g.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A4,Def13;
       then consider t be VECTOR of X such that
          A5:   r=||.g.t.|| & ||.t.|| <= 1;
          A6:   ||.g.t.|| <= K*||. t .|| by A2;
            K*||. t .|| <= K *1 by A1,A5,AXIOMS:25;
        hence r <=K by A5,A6,AXIOMS:22;
      end;
   take K;
   thus thesis by A3;
   end;
 hence thesis;
 end;

theorem
      for X, Y be RealNormSpace
    for g be LinearOperator of X,Y
      holds g is bounded iff PreNorms(g) is bounded_above
   proof
       let X, Y be RealNormSpace;
       let g be LinearOperator of X,Y;

   now assume
    A1: PreNorms(g) is bounded_above;
         reconsider K=sup PreNorms(g) as Real;
    A2:  0 <= K
    proof
    A3:now let r be Real such that
        A4: r in PreNorms(g);
          r in {||.g.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A4,Def13;
        then consider t be VECTOR of X such that
          A5: r=||.g.t.|| & ||.t.|| <= 1;
          thus 0 <= r by A5,NORMSP_1:8;
    end;
  consider r0 be set such that
  A6: r0 in PreNorms(g) by XBOOLE_0:def 1;
  reconsider r0 as Real by A6;
    0 <= r0 by A3,A6;
    hence thesis by A1,A6,SEQ_4:def 4;
    end;
    A7: now let t be VECTOR of X;
         now per cases;
         case A8: t = 0.X;
             then A9: g.t = g.(0*0.X) by RLVECT_1:23
                   .=0*g.(0.X) by Def6
                   .=0.Y by RLVECT_1:23;
                    ||.t.|| = 0 by A8,NORMSP_1:def 2;
            hence ||.g.t.|| <= K*||.t.|| by A9,NORMSP_1:def 2;
       end;
       case t <> 0.X;
          then A10:||.t.|| <> 0 by NORMSP_1:def 2;
          then A11: ||.t.|| > 0 by NORMSP_1:8;
          A12:abs( ||.t.||") = abs( 1*||.t.||")
          .=abs( 1/||.t.||) by XCMPLX_0:def 9
          .=1/abs( ||.t.||) by ABSVALUE:15
          .=1/||.t.|| by A11,ABSVALUE:def 1
          .=1*||.t.||" by XCMPLX_0:def 9
          .=||.t.||";
          reconsider t1= ( ||.t.||")*t as VECTOR of X;
          A13: ||.t1.|| =abs( ||.t.||")*||.t.|| by NORMSP_1:def 2
                      .=1 by A10,A12,XCMPLX_0:def 7;
         A14:  ||.g.t.||/||.t.||
                           = ||.g.t.||*||.t.||" by XCMPLX_0:def 9
                          .=||. ||.t.||"*g.t.|| by A12,NORMSP_1:def 2
                          .=||.g.t1.|| by Def6;
          ||.g.t1.|| in
              {||.g.s.|| where s is VECTOR of X : ||.s.|| <= 1 } by A13;
          then ||.g.t.||/||.t.|| in PreNorms(g) by A14,Def13;
          then A15: ||.g.t.||/||.t.|| <= K by A1,SEQ_4:def 4;
              ||.g.t.||/||.t.||*||.t.||
             = ||.g.t.||*||.t.||"*||.t.|| by XCMPLX_0:def 9
            .=||.g.t.||*(||.t.||"*||.t.||)
            .=||.g.t.||*1 by A10,XCMPLX_0:def 7
            .=||.g.t.||;
           hence ||.g.t.|| <= K *||.t.|| by A11,A15,AXIOMS:25;
           end;
           end;
     hence ||.g.t.|| <= K*||.t.||;
    end;
 take K;
 thus g is bounded by A2,A7,Def9;
 end;
hence thesis by Th32;
end;

theorem Th34:
   for X, Y be RealNormSpace
    ex NORM be Function of BoundedLinearOperators(X,Y),REAL st
          for f be set st f in BoundedLinearOperators(X,Y) holds
        NORM.f = sup PreNorms(modetrans(f,X,Y))
proof
   let X, Y be RealNormSpace;
  deffunc F(set) = sup PreNorms(modetrans($1,X,Y));
A1:for z be set st z in BoundedLinearOperators(X,Y) holds F(z) in REAL;
     ex f being Function of BoundedLinearOperators(X,Y),REAL st
     for x being set st x in BoundedLinearOperators(X,Y) holds
       f.x = F(x) from FUNCT_2:sch 2(A1);
   hence thesis;
end;

definition
   let X, Y be RealNormSpace;
   func BoundedLinearOperatorsNorm(X,Y)
     -> Function of BoundedLinearOperators(X,Y), REAL means :Def14:
  for x be set st x in BoundedLinearOperators(X,Y) holds
    it.x = sup PreNorms(modetrans(x,X,Y));
existence by Th34;
uniqueness
proof
   let NORM1,NORM2 be Function of BoundedLinearOperators(X,Y), REAL
   such that
A1: (for x be set st x in BoundedLinearOperators(X,Y) holds
        NORM1.x = sup PreNorms(modetrans(x,X,Y))) and
A2:(for x be set st x in BoundedLinearOperators(X,Y) holds
     NORM2.x = sup PreNorms(modetrans(x,X,Y)));
A3:dom NORM1 = BoundedLinearOperators(X,Y) &
     dom NORM2 = BoundedLinearOperators(X,Y) by FUNCT_2:def 1;
     for z be set st z in BoundedLinearOperators(X,Y) holds NORM1.z = NORM2.z
   proof
     let z be set such that
A4:  z in BoundedLinearOperators(X,Y);
       NORM1.z = sup PreNorms(modetrans(z,X,Y)) by A1,A4;
     hence thesis by A2,A4;
  end;
  hence thesis by A3,FUNCT_1:9;
end;
end;

theorem Th35:
   for X, Y be RealNormSpace
   for f be bounded LinearOperator of X,Y holds modetrans(f,X,Y)=f
  proof
     let X, Y be RealNormSpace;
     let f be bounded LinearOperator of X,Y;
       f in BoundedLinearOperators(X,Y) by Def10;
     hence thesis by Def12;
  end;

theorem Th36:
   for X, Y be RealNormSpace
   for f be bounded LinearOperator of X,Y holds
   BoundedLinearOperatorsNorm(X,Y).f = sup PreNorms(f)
  proof
       let X, Y be RealNormSpace;
       let f be bounded LinearOperator of X,Y;
       A1: f in BoundedLinearOperators(X,Y) by Def10;
      reconsider f'=f as set;
      thus BoundedLinearOperatorsNorm(X,Y).f
        = sup PreNorms(modetrans(f',X,Y)) by A1,Def14
       .= sup PreNorms(f) by Th35;
  end;

definition let X, Y be RealNormSpace;
 func R_NormSpace_of_BoundedLinearOperators(X,Y) -> non empty NORMSTR equals
 :Def15: NORMSTR (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
          BoundedLinearOperatorsNorm(X,Y) #);
  coherence;
end;

theorem Th37:
    for X, Y be RealNormSpace
       holds (the carrier of X) --> 0.Y =
        0.R_NormSpace_of_BoundedLinearOperators(X,Y)
 proof
     let X, Y be RealNormSpace;
    A1: R_NormSpace_of_BoundedLinearOperators(X,Y)
     = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y) #) by Def15;
A2: R_VectorSpace_of_BoundedLinearOperators(X,Y) =
    RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
            ((the carrier of X) --> 0.Y)
            =0.R_VectorSpace_of_BoundedLinearOperators(X,Y) by Th31
            .=Zero_(BoundedLinearOperators(X,Y),
            R_VectorSpace_of_LinearOperators(X,Y)) by A2,RLVECT_1:def 2
            .=0.R_NormSpace_of_BoundedLinearOperators(X,Y)
        by A1,RLVECT_1:def 2;
  hence thesis;
end;

theorem Th38:
    for X, Y be RealNormSpace
    for f being Point of
       R_NormSpace_of_BoundedLinearOperators(X,Y)
    for g be bounded LinearOperator of X,Y st g=f holds
      for t be VECTOR of X holds ||.g.t.|| <= ||.f.|| * ||.t.||
   proof
       let X, Y be RealNormSpace;
       let f being Point of
          R_NormSpace_of_BoundedLinearOperators(X,Y);
      let g be bounded LinearOperator of X,Y such that
     A1:g=f;
A2: R_NormSpace_of_BoundedLinearOperators(X,Y)
     = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y) #) by Def15;
 A3: PreNorms(g) is non empty bounded_above by Th32;
      now let t be VECTOR of X;
         now per cases;
         case A4: t  = 0.X;
             then A5: g.t  =g.(0*0.X) by RLVECT_1:23
                   .=0*g.(0.X) by Def6
                   .=0.Y by RLVECT_1:23;
                    ||.t.|| = 0 by A4,NORMSP_1:def 2;
            hence ||.g.t.|| <= ||.f.||*||.t.|| by A5,NORMSP_1:def 2;
       end;
       case t <> 0.X;
          then A6:||.t.|| <> 0 by NORMSP_1:def 2;
          then A7: ||.t.|| > 0 by NORMSP_1:8;
          A8:abs( ||.t.||")
          =abs( 1*||.t.||")
          .=abs( 1/||.t.||) by XCMPLX_0:def 9
          .=1/abs( ||.t.||) by ABSVALUE:15
          .=1/||.t.|| by A7,ABSVALUE:def 1
          .=1*||.t.||" by XCMPLX_0:def 9
          .=||.t.||";
          reconsider t1= ( ||.t.||")*t as VECTOR of X;
          A9: ||.t1.|| =abs( ||.t.||")*||.t.|| by NORMSP_1:def 2
                      .=1 by A6,A8,XCMPLX_0:def 7;
           ||.g.t.||/||.t.|| = ||.g.t.||*||.t.||" by XCMPLX_0:def 9
                          .=||. ||.t.||"*g.t.|| by A8,NORMSP_1:def 2
                          .=||.g.t1.|| by Def6;
        then ||.g.t.||/||.t.|| in
              {||.g.s.|| where s is VECTOR of X : ||.s.|| <= 1 } by A9;
          then ||.g.t.||/||.t.|| in PreNorms(g) by Def13;
          then ||.g.t.||/||.t.||
              <= sup PreNorms(g) by A3,SEQ_4:def 4;
          then ||.g.t.||/||.t.||
              <= BoundedLinearOperatorsNorm(X,Y).g by Th36;
           then A10: ||.g.t.||/||.t.|| <= ||.f.|| by A1,A2,NORMSP_1:def 1;
              ||.g.t.||/||.t.||*||.t.||
             = ||.g.t.||*||.t.||"*||.t.|| by XCMPLX_0:def 9
            .=||.g.t.||*(||.t.||"*||.t.||)
            .=||.g.t.||*1 by A6,XCMPLX_0:def 7
            .=||.g.t.||;
           hence ||.g.t.|| <= ||.f.||*||.t.|| by A7,A10,AXIOMS:25;
        end;
        end;
     hence ||.g.t.|| <= ||.f.||*||.t.||;
    end;
    hence thesis;
end;

theorem Th39:
    for X, Y be RealNormSpace
    for f being Point of
       R_NormSpace_of_BoundedLinearOperators(X,Y)
    holds 0 <= ||.f.||
proof
       let X, Y be RealNormSpace;
       let f being Point of
          R_NormSpace_of_BoundedLinearOperators(X,Y);
A1: R_NormSpace_of_BoundedLinearOperators(X,Y)
  = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y)
      #) by Def15;
    then reconsider g=f as bounded LinearOperator of X,Y by Def10;
 A2: now let r be Real such that
        A3:  r in PreNorms(g);
           r in {||.g.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A3,Def13;
        then consider t be VECTOR of X such that
          A4: r=||.g.t.|| & ||.t.|| <= 1;
          thus 0 <= r by A4,NORMSP_1:8;
 end;
A5:PreNorms(g) is non empty bounded_above by Th32;
 consider r0 be set such that
  A6: r0 in PreNorms(g) by XBOOLE_0:def 1;
  reconsider r0 as Real by A6;
    0 <= r0 by A2,A6;
  then A7:  0 <=sup PreNorms(g) by A5,A6,SEQ_4:def 4;
    BoundedLinearOperatorsNorm(X,Y).f = sup PreNorms(g) by Th36;
    hence thesis by A1,A7,NORMSP_1:def 1;
  end;

theorem Th40:
    for X, Y be RealNormSpace
    for f being Point of
       R_NormSpace_of_BoundedLinearOperators(X,Y)
       st f = 0.R_NormSpace_of_BoundedLinearOperators(X,Y)
    holds 0 = ||.f.||
proof
    let X, Y be RealNormSpace;
    let f being Point of
       R_NormSpace_of_BoundedLinearOperators(X,Y) such that
A1:       f = 0.R_NormSpace_of_BoundedLinearOperators(X,Y);
A2: R_NormSpace_of_BoundedLinearOperators(X,Y)
  = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y) #) by Def15;
  thus ||.f.|| = 0
    proof
        set z = (the carrier of X) --> 0.Y;
        reconsider z as Function of the carrier of X, the carrier of Y
    by FUNCOP_1:58;
        reconsider g=f as bounded LinearOperator of X,Y by A2,Def10;
       A3: z=g by A1,Th37;
       A4:
      now let r be Real such that
        A5:  r in PreNorms(g);
           r in {||.g.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A5,Def13;
        then consider t be VECTOR of X such that
          A6:   r=||.g.t.|| & ||.t.|| <= 1;
            ||.g.t.|| = ||.0.Y.|| by A3,FUNCOP_1:13
                  .= 0 by NORMSP_1:def 2;
          hence 0 <= r & r <=0 by A6;
      end;
     A7:PreNorms(g) is non empty bounded_above by Th32;
     consider r0 be set such that
      A8: r0 in PreNorms(g) by XBOOLE_0:def 1;
     reconsider r0 as Real by A8;
     A9: 0 <= r0 & r0<=0 by A4,A8;
       (for s be real number st s in PreNorms(g) holds s <= 0)
         implies sup PreNorms(g) <= 0 by SEQ_4:62;
      then sup PreNorms(g) = 0 by A4,A7,A8,A9,SEQ_4:def 4;
       then BoundedLinearOperatorsNorm(X,Y).f=0 by Th36;
      hence thesis by A2,NORMSP_1:def 1;
      end;
end;

registration
 let X,Y be RealNormSpace;
 cluster -> Function-like Relation-like
   Element of R_NormSpace_of_BoundedLinearOperators(X,Y);
coherence
proof
    R_NormSpace_of_BoundedLinearOperators(X,Y) =
  NORMSTR (# BoundedLinearOperators(X,Y),
    Zero_(BoundedLinearOperators(X,Y),
    R_VectorSpace_of_LinearOperators(X,Y)),
    Add_(BoundedLinearOperators(X,Y),
    R_VectorSpace_of_LinearOperators(X,Y)),
    Mult_(BoundedLinearOperators(X,Y),
    R_VectorSpace_of_LinearOperators(X,Y)),
    BoundedLinearOperatorsNorm(X,Y) #) by Def15;
  hence thesis by Def10;
end;
end;

definition
 let X,Y be RealNormSpace;
 let f be Element of R_NormSpace_of_BoundedLinearOperators(X,Y);
 let v be VECTOR of X;
 redefine func f.v -> VECTOR of Y;
coherence
proof
    R_NormSpace_of_BoundedLinearOperators(X,Y) =
  NORMSTR (# BoundedLinearOperators(X,Y),
    Zero_(BoundedLinearOperators(X,Y),
    R_VectorSpace_of_LinearOperators(X,Y)),
    Add_(BoundedLinearOperators(X,Y),
    R_VectorSpace_of_LinearOperators(X,Y)),
    Mult_(BoundedLinearOperators(X,Y),
    R_VectorSpace_of_LinearOperators(X,Y)),
    BoundedLinearOperatorsNorm(X,Y) #) by Def15;
  then reconsider f as LinearOperator of X,Y by Def10;
    f.v is VECTOR of Y;
  hence thesis;
end;
end;

theorem Th41:
  for X, Y be RealNormSpace
  for f,g,h be Point of R_NormSpace_of_BoundedLinearOperators(X,Y) holds
      h = f+g iff for x be VECTOR of X holds h.x = f.x + g.x
proof
     let X, Y be RealNormSpace;
     let f,g,h be Point of R_NormSpace_of_BoundedLinearOperators(X,Y);
A1: R_NormSpace_of_BoundedLinearOperators(X,Y)
  = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y) #) by Def15;
    then reconsider f'=f,g'=g,h'=h as bounded LinearOperator of X,Y by Def10;
A2: R_VectorSpace_of_BoundedLinearOperators(X,Y) =
    RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
 then reconsider f1=f, g1=g, h1=h as VECTOR of
 R_VectorSpace_of_BoundedLinearOperators(X,Y) by A1;
   h=f+g iff h1=f1+g1
proof
A3: now
assume h=f+g;
hence h1=Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)).[f,g] by A1,RLVECT_1:def 3
          .=f1+g1 by A2,RLVECT_1:def 3;
end;
  now
assume h1=f1+g1;
hence h=Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y))
         .[f1,g1] by A2,RLVECT_1:def 3
      .=f+g by A1,RLVECT_1:def 3;
end;
hence thesis by A3;
end;
hence thesis by Th29;
end;

theorem Th42:
  for X, Y be RealNormSpace
  for f,h be Point of R_NormSpace_of_BoundedLinearOperators(X,Y)
  for a be Real holds
      h = a*f iff for x be VECTOR of X holds h.x = a * f.x
proof
     let X, Y be RealNormSpace;
     let f,h be Point of
     R_NormSpace_of_BoundedLinearOperators(X,Y);
     let a be Real;
A1: R_NormSpace_of_BoundedLinearOperators(X,Y)
  = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y) #) by Def15;
    then reconsider f'=f,h'=h as bounded LinearOperator of X,Y by Def10;
A2: R_VectorSpace_of_BoundedLinearOperators(X,Y) =
    RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
 then reconsider f1=f as VECTOR of
 R_VectorSpace_of_BoundedLinearOperators(X,Y) by A1;
 reconsider h1=h as VECTOR of
 R_VectorSpace_of_BoundedLinearOperators(X,Y) by A1,A2;
  h=a*f iff h1=a*f1
proof
A3: now
assume h=a*f;
hence h1=Mult_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)).[a,f] by A1,RLVECT_1:def 4
          .=a*f1 by A2,RLVECT_1:def 4;
end;
  now
assume h1=a*f1;
hence
       h=Mult_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y))
         .[a,f1] by A2,RLVECT_1:def 4
      .=a*f by A1,RLVECT_1:def 4;
end;
hence thesis by A3;
end;
hence thesis by Th30;
end;

theorem Th43:
    for X be RealNormSpace
    for Y be RealNormSpace
    for f, g being Point of
       R_NormSpace_of_BoundedLinearOperators(X,Y)
       for a be Real holds
  ( ||.f.|| = 0 iff
  f = 0.R_NormSpace_of_BoundedLinearOperators(X,Y) ) &
  ||.a*f.|| = abs(a) * ||.f.|| &
  ||.f+g.|| <= ||.f.|| + ||.g.||
proof
    let X be RealNormSpace;
    let Y be RealNormSpace;
    let f, g being Point of R_NormSpace_of_BoundedLinearOperators(X,Y);
    let a be Real;
A1: R_NormSpace_of_BoundedLinearOperators(X,Y)
  = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y) #) by Def15;
A2: R_VectorSpace_of_BoundedLinearOperators(X,Y) =
    RLSStruct (# BoundedLinearOperators(X,Y),
        Zero_(BoundedLinearOperators(X,Y),
        R_VectorSpace_of_LinearOperators(X,Y)),
         Add_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Mult_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) #) by Def11;
A3:
now assume
   A4: ||.f.|| = 0;
   reconsider g=f as bounded LinearOperator of X,Y by A1,Def10;
   set z = (the carrier of X) --> 0.Y;
   reconsider z as Function of the carrier of X, the carrier of Y
  by FUNCOP_1:58;
    now let t be VECTOR of X;
               ||.g.t.|| <= ||.f.|| *||.t.|| by Th38;
              then ||.g.t.|| = 0 by A4,NORMSP_1:8;
           hence g.t =0.Y by NORMSP_1:def 2
                 .=z.t by FUNCOP_1:13;
       end;
     then g=z by FUNCT_2:113;
   hence f=0.R_NormSpace_of_BoundedLinearOperators(X,Y) by Th37;
end;

A5:
now assume
   A6: f = 0.R_NormSpace_of_BoundedLinearOperators(X,Y);
  thus ||.f.|| = 0
  proof
     set z = (the carrier of X) --> 0.Y;
     reconsider z as Function of the carrier of X, the carrier of Y
    by FUNCOP_1:58;
    reconsider g=f as bounded LinearOperator of X,Y by A1,Def10;
      A7: z=g by A6,Th37;
      A8:
      now let r be Real such that
        A9:  r in PreNorms(g);
           r in {||.g.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A9,Def13;
        then consider t be VECTOR of X such that
          A10:   r=||.g.t.|| & ||.t.|| <= 1;
            ||.g.t.|| = ||.0.Y.|| by A7,FUNCOP_1:13
                  .= 0 by NORMSP_1:def 2;
          hence 0 <= r & r <=0 by A10;
    end;
A11:PreNorms(g) is non empty bounded_above by Th32;
 consider r0 be set such that
  A12: r0 in PreNorms(g) by XBOOLE_0:def 1;
  reconsider r0 as Real by A12;
  A13: 0<=r0 by A8,A12;
  (for s be real number st s in PreNorms(g) holds s <= 0)
implies sup PreNorms(g) <= 0 by SEQ_4:62;
    then sup PreNorms(g) = 0 by A8,A11,A12,A13,SEQ_4:def 4;
    then BoundedLinearOperatorsNorm(X,Y).f =0 by Th36;
    hence thesis by A1,NORMSP_1:def 1;
  end;
end;

A14:  ||.a*f.|| = abs(a) * ||.f.||
proof
reconsider f1=f, h1=a*f as bounded LinearOperator of X,Y by A1,Def10;
A15:
now let t be VECTOR of X such that
  A16: ||.t.|| <= 1;
 A17:  ||.h1.t.||= ||.a*f1.t.|| by Th42;
 A18:  ||.a*f1.t.|| =abs(a)*||.f1.t.|| by NORMSP_1:def 2;
 A19:   ||.f1.t.||<= ||.f.||*||.t.|| by Th38;
     0 <= ||.f.|| by Th39;
   then ||.f.||*||.t.|| <= ||.f.||*1 by A16,AXIOMS:25;
 then A20: ||.f1.t.||  <= ||.f.|| by A19,AXIOMS:22;
   0<= abs(a) by ABSVALUE:5;
    hence ||.h1.t.|| <= abs(a)*||.f.|| by A17,A18,A20,AXIOMS:25;
end;
A21:
     now let r be Real such that
        A22:  r in PreNorms(h1);
           r in {||.h1.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A22,Def13;
        then consider t be VECTOR of X such that
          A23: r=||.h1.t.|| & ||.t.|| <= 1;
          thus r <= abs(a)*||.f.|| by A15,A23;
  end;

A24:
(for s be real number st s in PreNorms(h1) holds s <= abs(a)*||.f.|| )
implies sup PreNorms(h1) <=  abs(a)*||.f.|| by SEQ_4:62;
  BoundedLinearOperatorsNorm(X,Y).(a*f)
    = sup PreNorms(h1) by Th36;
then A25:     ||.a*f.|| <= abs(a)*||.f.|| by A1,A21,A24,NORMSP_1:def 1;
  now per cases;
case A26: a <> 0;
 A27: now let t be VECTOR of X such that
  A28: ||.t.|| <= 1;
    h1.t=a*f1.t by Th42;
  then A29: a"*h1.t =( a"* a)*f1.t by RLVECT_1:def 9
             .=1*f1.t by A26,XCMPLX_0:def 7
             .=f1.t by RLVECT_1:def 9;
 A30:  ||.a"*h1.t.|| =abs(a")*||.h1.t.|| by NORMSP_1:def 2;
 A31:   ||.h1.t.||<= ||.a*f.||*||.t.|| by Th38;
     0 <= ||.a*f.|| by Th39;
   then ||.a*f.||*||.t.|| <= ||.a*f.||*1 by A28,AXIOMS:25;
   then A32: ||.h1.t.|| <= ||.a*f.|| by A31,AXIOMS:22;
 A33: 0<= abs(a") by ABSVALUE:5;
     abs(a") =abs(1*a") .=abs( 1/a) by XCMPLX_0:def 9
                 .=1/abs(a) by ABSVALUE:15
                 .=1*abs(a)" by XCMPLX_0:def 9
                 .=abs(a)";
  hence ||.f1.t.|| <= abs(a)"*||.a*f.|| by A29,A30,A32,A33,AXIOMS:25;
end;
A34:
     now let r be Real such that
        A35:  r in PreNorms(f1);
           r in {||.f1.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A35,Def13;
        then consider t be VECTOR of X such that
          A36:  r=||.f1.t.|| & ||.t.|| <= 1;
          thus r <= abs(a)"*||.a*f.|| by A27,A36;
  end;
A37: abs(a) <>0 by A26,ABSVALUE:6;
 A38:
(for s be real number st s in PreNorms(f1) holds s <= abs(a)"*||.a*f.|| )
implies sup PreNorms(f1) <= abs(a)"*||.a*f.|| by SEQ_4:62;
   BoundedLinearOperatorsNorm(X,Y).(f)
    = sup PreNorms(f1) by Th36;
 then A39: ||.f.|| <=abs(a)"*||.a*f.|| by A1,A34,A38,NORMSP_1:def 1;
   0 <= abs(a) by ABSVALUE:5;
 then abs(a)*||.f.|| <=abs(a)*(abs(a)"*||.a*f.||) by A39,AXIOMS:25;
 then abs(a)*||.f.|| <=(abs(a)*abs(a)")*||.a*f.||;
 then abs(a)*||.f.|| <=1*||.a*f.|| by A37,XCMPLX_0:def 7;
 hence abs(a)* ||.f.|| <=||.a*f.||;
 end;
 case A40: a=0;
  reconsider fz=f as VECTOR
     of R_VectorSpace_of_BoundedLinearOperators(X,Y)
 by A1,A2;
     A41: a*f =Mult_(BoundedLinearOperators(X,Y),
            R_VectorSpace_of_LinearOperators(X,Y)).[a,f] by A1,RLVECT_1:def 4
        .=a*fz by A2,RLVECT_1:def 4
        .=0.R_VectorSpace_of_BoundedLinearOperators(X,Y)
 by A40,RLVECT_1:23
        .=Zero_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)) by A2,RLVECT_1:def 2
        .=0.R_NormSpace_of_BoundedLinearOperators(X,Y) by A1,RLVECT_1:def 2;
  thus abs(a)* ||.f.|| =0 * ||.f.|| by A40,ABSVALUE:7
                           .=||.a*f.|| by A41,Th40;
end;
end;
hence thesis by A25,AXIOMS:21;
end;
    ||.f+g.|| <= ||.f.|| +  ||.g.||
proof
reconsider f1=f, g1=g, h1=f+g as bounded LinearOperator of X,Y by A1,Def10;
A42:
now let t be VECTOR of X such that
  A43: ||.t.|| <= 1;
 A44:  ||.h1.t.||= ||.f1.t+g1.t.|| by Th41;
 A45:  ||.f1.t+g1.t.|| <=||.f1.t.||+||.g1.t.|| by NORMSP_1:def 2;
 A46:   ||.f1.t.||<= ||.f.||*||.t.|| by Th38;
     ||.g1.t.||<= ||.g.||*||.t.|| by Th38;
 then A47:  ||.f1.t.||+||.g1.t.|| <= ||.f.||*||.t.|| +  ||.g.||*||.t.||
 by A46,REAL_1:55;
     0 <= ||.f.|| by Th39;
 then A48:  ||.f.||*||.t.|| <= ||.f.||*1 by A43,AXIOMS:25;
     0 <= ||.g.|| by Th39;
 then ||.g.||*||.t.|| <= ||.g.||*1 by A43,AXIOMS:25;
    then ||.f.||*||.t.|| + ||.g.||*||.t.|| <= ||.f.||*1 + ||.g.||*1
 by A48,REAL_1:55;
   then ||.f1.t.||+||.g1.t.|| <= ||.f.|| + ||.g.|| by A47,AXIOMS:22;
    hence ||.h1.t.|| <= ||.f.|| + ||.g.|| by A44,A45,AXIOMS:22;
end;
A49:
     now let r be Real such that
        A50:  r in PreNorms(h1);
           r in {||.h1.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A50,Def13;
        then consider t be VECTOR of X such that
          A51:   r=||.h1.t.|| & ||.t.|| <= 1;
          thus r <= ||.f.|| + ||.g.|| by A42,A51;
  end;
A52:
(for s be real number st s in PreNorms(h1) holds s <= ||.f.|| + ||.g.||)
implies sup PreNorms(h1) <= ||.f.|| + ||.g.|| by SEQ_4:62;
  BoundedLinearOperatorsNorm(X,Y).(f+g) = sup PreNorms(h1) by Th36;
hence ||.f+g.|| <=||.f.|| + ||.g.|| by A1,A49,A52,NORMSP_1:def 1;
end;
hence thesis by A3,A5,A14;
end;

theorem Th44:
  for X,Y be RealNormSpace holds
  R_NormSpace_of_BoundedLinearOperators(X,Y) is RealNormSpace-like
      proof
       let X,Y be RealNormSpace;
      for x, y being Point of R_NormSpace_of_BoundedLinearOperators(X,Y)
       for a be Real holds
  ( ||.x.|| = 0 iff
  x = 0.R_NormSpace_of_BoundedLinearOperators(X,Y) ) &
  ||.a*x.|| = abs(a) * ||.x.|| &
  ||.x+y.|| <= ||.x.|| +  ||.y.|| by Th43;
   hence thesis by NORMSP_1:def 2;
  end;

theorem Th45:
  for X, Y be RealNormSpace holds
    R_NormSpace_of_BoundedLinearOperators(X,Y) is RealNormSpace
     proof
    let X, Y be RealNormSpace;
A1: R_NormSpace_of_BoundedLinearOperators(X,Y)
  = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y) #) by Def15;
      RLSStruct (# BoundedLinearOperators(X,Y),
            Zero_(BoundedLinearOperators(X,Y),
            R_VectorSpace_of_LinearOperators(X,Y)),
             Add_(BoundedLinearOperators(X,Y),
             R_VectorSpace_of_LinearOperators(X,Y)),
            Mult_(BoundedLinearOperators(X,Y),
            R_VectorSpace_of_LinearOperators(X,Y)) #) is RealLinearSpace;
      hence thesis by A1,Th44,RSSPACE3:4;
     end;

registration
   let X, Y be RealNormSpace;
   cluster R_NormSpace_of_BoundedLinearOperators(X,Y) ->
               RealNormSpace-like RealLinearSpace-like
               Abelian add-associative right_zeroed right_complementable;
   coherence by Th45;
end;

theorem Th46:
  for X, Y be RealNormSpace
  for f,g,h be Point of R_NormSpace_of_BoundedLinearOperators(X,Y) holds
      h = f-g iff for x be VECTOR of X holds h.x = f.x - g.x
proof
     let X, Y be RealNormSpace;
     let f,g,h be Point of R_NormSpace_of_BoundedLinearOperators(X,Y);
   R_NormSpace_of_BoundedLinearOperators(X,Y)
  = NORMSTR (# BoundedLinearOperators(X,Y),
         Zero_(BoundedLinearOperators(X,Y),
         R_VectorSpace_of_LinearOperators(X,Y)),
          Add_(BoundedLinearOperators(X,Y),
          R_VectorSpace_of_LinearOperators(X,Y)),
           Mult_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
           BoundedLinearOperatorsNorm(X,Y) #) by Def15;
      then reconsider f'=f,g'=g,h'=h as bounded LinearOperator of X,Y by Def10;
hereby assume h=f-g;
     then h+g=f-(g-g) by RLVECT_1:43;
     then h+g=f-0.R_NormSpace_of_BoundedLinearOperators(X,Y) by RLVECT_1:28;
     then A1: h+g=f by RLVECT_1:26;
       now let x be VECTOR of X;
         f'.x=h'.x +  g'.x by A1,Th41;
       then f'.x-g'.x=h'.x + (g'.x-g'.x) by RLVECT_1:42;
       then f'.x-g'.x=h'.x + 0.Y by RLVECT_1:28;
       hence f'.x-g'.x=h'.x by RLVECT_1:10;
     end;
     hence for x be VECTOR of X holds h.x = f.x - g.x;
end;
assume
     A2: for x be VECTOR of X holds h.x = f.x - g.x;
       now let x be VECTOR of X;
         h'.x = f'.x - g'.x by A2;
       then h'.x + g'.x= f'.x - (g'.x- g'.x) by RLVECT_1:43;
       then h'.x + g'.x= f'.x - 0.Y by RLVECT_1:28;
       hence h'.x + g'.x= f'.x by RLVECT_1:26;
     end;
     then f=h+g by Th41;
     then f-g=h+(g-g) by RLVECT_1:42;
     then f-g=h+0.R_NormSpace_of_BoundedLinearOperators(X,Y) by RLVECT_1:28;
     hence f-g=h by RLVECT_1:10;
end;

begin :: Real Banach Space of Bounded Linear Operators

definition let X be RealNormSpace;
  attr X is complete means :Def16:
    for seq be sequence of X holds
    seq is Cauchy_sequence_by_Norm implies seq is convergent;
end;

registration
  cluster complete RealNormSpace;
  existence
  proof
    take l1_Space;
    thus thesis by Def16,RSSPACE3:11;
  end;
end;

definition
  mode RealBanachSpace is complete RealNormSpace;
end;

Lm3:
for e be Real, seq be Real_Sequence st ( seq is convergent &
 ex k be Nat st (for i be Nat st k <= i holds seq.i <=e ) ) holds
  lim seq <=e
proof
   let e be Real;
   let seq be Real_Sequence such that
A1:seq is convergent and
A2:ex k be Nat st (for i be Nat st k <= i holds seq.i <=e );
   reconsider cseq = NAT --> e as Real_Sequence by FUNCOP_1:57;
A3:for n be Nat holds cseq.n=e by FUNCOP_1:13;
   reconsider e1=e as real number;
A5:cseq is convergent by SEQ_4:39;
   consider k be Nat such that
A6:for i be Nat st k <= i holds seq.i <=e by A2;
A7:(seq ^\k) is convergent by A1,SEQ_4:33;
A8:lim (seq ^\k)=lim seq by A1,SEQ_4:33;
A9:now let i be Nat;
A10: (seq ^\k).i = seq.(k+i) by SEQM_3:def 9;
       k <= k+i by NAT_1:29;
     then seq.(k+i) <=e by A6;
     hence (seq ^\k) .i <= cseq.i by A3,A10;
  end;
    lim cseq = cseq.0 by SEQ_4:41 .= e by A3;
  hence thesis by A5,A7,A8,A9,SEQ_2:32;
end;

theorem Th47:
    for X be RealNormSpace
      for seq be sequence of X
        st seq is convergent
       holds ||.seq.|| is convergent & lim ||.seq.|| = ||.lim seq.||
proof
 let X be RealNormSpace;
 let seq be sequence of X such that
 A1:  seq is convergent;
 A2: now let e1 be real number such that
   A3: e1 >0;
   reconsider e =e1 as Real by XREAL_0:def 1;
   consider k be Nat such that
   A4:      for n be Nat st n >= k holds ||.seq.n - lim seq.|| < e
              by A1,A3,NORMSP_1:def 11;
   A5:
   now let m be Nat such that
      A6:   m >= k;
          ||.seq.m.||= ||.seq.||.m by NORMSP_1:def 10;
      then A7:    abs( ||.seq.||.m - ||.lim seq.||  ) <= ||.seq.m - lim seq.||
            by NORMSP_1:13;
           ||.seq.m - lim seq.||  <e by A4,A6;
      hence abs( ||.seq.||.m - ||.lim seq.||  ) <e1
      by A7,AXIOMS:22;
   end;
   take k;
   thus for m be Nat st m >= k holds
             abs(||.seq.||.m - ||.lim seq.|| ) < e1 by A5;
 end;
 then ||.seq.|| is convergent by SEQ_2:def 6;
 hence thesis by A2,SEQ_2:def 7;
end;

theorem Th48:
    for X, Y be RealNormSpace st Y is complete
      for seq be sequence of R_NormSpace_of_BoundedLinearOperators(X,Y)
               st seq is Cauchy_sequence_by_Norm
 holds seq is convergent
proof
    let X, Y be RealNormSpace such that
 A1:   Y is complete;
    let vseq be sequence of R_NormSpace_of_BoundedLinearOperators(X,Y)
     such that
A2: vseq is Cauchy_sequence_by_Norm;
A3:  ||.vseq.|| is convergent
proof
   now let e1 be real number such that
   A4: e1 >0;
   reconsider e =e1 as Real by XREAL_0:def 1;
          consider k be Nat such that
   A5:      for n, m be Nat st ( n >= k & m >= k ) holds
                ||.(vseq.n) - (vseq.m).|| < e
              by A2,A4,RSSPACE3:10;
A6: now
      let m be Nat such that
      A7:   m >= k;
      A8:  abs( ||.vseq.m.||- ||.vseq.k.|| )
         <= ||.(vseq.m) - (vseq.k).|| by NORMSP_1:13;
      A9:   ||.vseq.k.||= ||.vseq.||.k by NORMSP_1:def 10;
      A10:   ||.vseq.m.||= ||.vseq.||.m by NORMSP_1:def 10;
           ||.(vseq.m) - (vseq.k).|| <e by A5,A7;
      hence abs( ||.vseq.||.m - ||.vseq.||.k  ) <e1
      by A8,A9,A10,AXIOMS:22;
   end;
   take k;
   thus for m be Nat st m >= k holds
             abs(||.vseq.||.m - ||.vseq.||.k ) < e1 by A6;
 end;
 hence ||.vseq.|| is convergent by SEQ_4:58;
end;
    defpred P[set, set] means ex xseq be sequence of Y st
    (for n be Nat holds xseq.n=modetrans((vseq.n),X,Y).$1)
    & xseq is convergent & $2= lim xseq;
A11: R_NormSpace_of_BoundedLinearOperators(X,Y)
    = NORMSTR (# BoundedLinearOperators(X,Y),
           Zero_(BoundedLinearOperators(X,Y),
           R_VectorSpace_of_LinearOperators(X,Y)),
            Add_(BoundedLinearOperators(X,Y),
            R_VectorSpace_of_LinearOperators(X,Y)),
             Mult_(BoundedLinearOperators(X,Y),
             R_VectorSpace_of_LinearOperators(X,Y)),
             BoundedLinearOperatorsNorm(X,Y) #) by Def15;
A12: for x be Element of X ex y be Element of Y st  P[x,y]
   proof
       let x be Element of X;
       deffunc F(Nat) = modetrans((vseq.$1),X,Y).x;
       consider xseq be sequence of Y such that
A13:  for n be Nat holds xseq.n = F(n) from FUNCT_2:sch 4;
A14: for m,k be Nat holds
             ||.xseq.m-xseq.k.|| <= ||.vseq.m - vseq.k.|| * ||.x.||
      proof
      let m,k be Nat;
           vseq.m is bounded LinearOperator of X,Y by A11,Def10;
then A15:         modetrans((vseq.m),X,Y)=vseq.m by Th35;
            vseq.k is bounded LinearOperator of X,Y by A11,Def10;
then A16:         modetrans((vseq.k),X,Y)=vseq.k by Th35;
         reconsider h1=vseq.m-vseq.k as bounded LinearOperator of X,Y by A11,
Def10;
           A17:  xseq.m =modetrans((vseq.m),X,Y).x by A13;
              xseq.k =modetrans((vseq.k),X,Y).x by A13;
           then xseq.m - xseq.k = h1.x by A15,A16,A17,Th46;
          hence thesis by Th38;
         end;
A18: xseq is convergent
     proof
          now
            let e be Real such that
A19:          e > 0;
               thus ex k be Nat st
                for n, m be Nat st ( n >= k & m >= k )
                holds ||.xseq.n -xseq.m.|| < e
                proof
                   now per cases;
                   case A20:x=0.X;
                   take k=0;
                   thus for n, m be Nat st ( n >= k & m >= k ) holds
                   ||.xseq.n -xseq.m.|| < e
                   proof
                     let n, m be Nat such that n >= k & m >= k;
                     A21: xseq.n=modetrans((vseq.n),X,Y).x by A13
                        .=modetrans((vseq.n),X,Y).(0*x) by A20,RLVECT_1:23
                        .=0*modetrans((vseq.n),X,Y).x by Def6
                        .=0.Y by RLVECT_1:23;
                       xseq.m=modetrans((vseq.m),X,Y).x by A13
                        .=modetrans((vseq.m),X,Y).(0*x) by A20,RLVECT_1:23
                        .=0*modetrans((vseq.m),X,Y).x by Def6
                        .=0.Y by RLVECT_1:23;
                     then ||.xseq.n -xseq.m.|| = ||.0.Y.|| by A21,RLVECT_1:26
                        .=0 by NORMSP_1:def 2;
                     hence ||.xseq.n -xseq.m.|| < e by A19;
                   end;
                   end;
                   case x <>0.X;
                   then A22:  ||.x.|| <> 0 by NORMSP_1:def 2;
                   then A23:  ||.x.|| > 0 by NORMSP_1:8;
                   then e/||.x.||>0 by A19,SEQ_2:6;
                   then consider k be Nat such that
A24:                for n, m be Nat st n >= k & m >= k holds
                     ||.(vseq.n) - (vseq.m).|| < e/||.x.||
                        by A2,RSSPACE3:10;
                   take k;
                   thus for n, m be Nat st ( n >= k & m >= k )
                     holds ||.xseq.n-xseq.m.|| < e
                   proof
                     let n,m be Nat such that
A25:                  n >=k & m >= k;
A26:                 ||.xseq.n-xseq.m.||  <=
                     ||.(vseq.n) - (vseq.m).|| * ||.x.|| by A14;
                       ||.(vseq.n) - (vseq.m).|| < e/||.x.|| by A24,A25;
then A27:                 ||.(vseq.n) - (vseq.m).|| * ||.x.||
                       < e/||.x.|| * ||.x.|| by A23,REAL_1:70;
                       e/||.x.|| * ||.x.|| = e*||.x.||"* ||.x.||
                            by XCMPLX_0:def 9
                         .= e*(||.x.||"* ||.x.||)
                         .= e*1 by A22,XCMPLX_0:def 7
                         .=e;
                     hence ||.xseq.n-xseq.m.|| < e by A26,A27,AXIOMS:22;
                   end;
                 end;
                 end;
                 hence thesis;
               end;
            end;
            then xseq is Cauchy_sequence_by_Norm by RSSPACE3:10;
         hence thesis by A1,Def16;
         end;
  take y = lim xseq;
  thus thesis by A13,A18;
  end;
   consider f be Function of the carrier of X,the carrier of Y such that
A28:for x be Element of X holds P[x,f.x] from FUNCT_2:sch 3(A12);
   reconsider tseq=f as Function of X,Y;
  tseq is LinearOperator of X,Y
proof
A29: now let x,y be VECTOR of X;
  consider xseq be sequence of Y such that
  A30: (for n be Nat holds xseq.n=modetrans((vseq.n),X,Y).x)
       & xseq is convergent
       & tseq.x = lim xseq by A28;
  consider yseq be sequence of Y such that
  A31: (for n be Nat holds yseq.n=modetrans((vseq.n),X,Y).y)
       & yseq is convergent
       & tseq.y = lim yseq by A28;
  consider zseq be sequence of Y such that
  A32: (for n be Nat holds zseq.n=modetrans((vseq.n),X,Y).(x+y))
       & zseq is convergent
       & tseq.(x+y) = lim zseq by A28;
    zseq=xseq+yseq
  proof
       now let n be Nat;
     thus zseq.n=modetrans((vseq.n),X,Y).(x+y) by A32
         .= modetrans((vseq.n),X,Y).x+modetrans((vseq.n),X,Y).y
          by Def5
         .= xseq.n + modetrans((vseq.n),X,Y).y by A30
         .= xseq.n +yseq.n by A31;
      end;
    hence thesis by NORMSP_1:def 5;
  end;
 hence tseq.(x+y)=tseq.x+tseq.y by A30,A31,A32,NORMSP_1:42;
end;
  now
       let x be VECTOR of X;
       let a be Real;
  consider xseq be sequence of Y such that
  A33: (for n be Nat holds xseq.n=modetrans((vseq.n),X,Y).x)
       & xseq is convergent
       & tseq.x = lim xseq by A28;
  consider zseq be sequence of Y such that
  A34: (for n be Nat holds zseq.n=modetrans((vseq.n),X,Y).(a*x))
       & zseq is convergent
       & tseq.(a*x) = lim zseq by A28;
    zseq=a*xseq
  proof
       now let n be Nat;
     thus zseq.n=modetrans((vseq.n),X,Y).(a*x) by A34
         .= a*modetrans((vseq.n),X,Y).x by Def6
         .= a*xseq.n by A33;
      end;
    hence thesis by NORMSP_1:def 8;
  end;
 hence tseq.(a*x)=a*tseq.x by A33,A34,NORMSP_1:45;
end;
hence thesis by A29,Def5,Def6;
end;
then reconsider tseq as LinearOperator of X,Y;
A35: tseq is bounded
proof
A36: now
       let x be VECTOR of X;
       consider xseq be sequence of Y such that
  A37: (for n be Nat holds  xseq.n=modetrans((vseq.n),X,Y).x)
       & xseq is convergent
       & tseq.x = lim xseq by A28;
A38: for m be Nat holds
             ||.xseq.m.|| <= ||.vseq.m.|| * ||.x.||
      proof
      let m be Nat;
      vseq.m is bounded LinearOperator of X,Y by A11,Def10;
then A39:         modetrans((vseq.m),X,Y)=vseq.m by Th35;
         reconsider h1=vseq.m as bounded LinearOperator of X,Y by A11,Def10;
            xseq.m =modetrans((vseq.m),X,Y).x by A37;
          hence thesis by A39,Th38;
         end;
  A40:     ||.xseq.|| is convergent by A37,Th24;
  A41:     ||.tseq.x.|| = lim ||.xseq.|| by A37,Th24;
  A42:    for n be Nat holds ||.xseq.||.n  <=( ||.x.||(#)||.vseq.||).n
            proof
            let n be Nat;
             A43: ||.xseq.||.n = ||.(xseq.n).|| by NORMSP_1:def 10;
             A44: ||.(xseq.n).|| <= ||.vseq.n.|| * ||.x.|| by A38;
               ||.vseq.n.|| = ||.vseq.||.n by NORMSP_1:def 10;
             hence thesis by A43,A44,SEQ_1:13;
            end;
   A45:  ||.x.||(#)||.vseq.|| is convergent by A3,SEQ_2:21;
      lim ( ||.x.||(#)||.vseq.|| ) = lim (||.vseq.|| )* ||.x.||
 by A3,SEQ_2:22;
   hence ||.tseq.x.|| <= lim (||.vseq.|| )* ||.x.|| by A40,A41,A42,A45,SEQ_2:32
;
    end;
   take K= lim (||.vseq.|| );
      K >=0
    proof
       now let n be Nat;
           ||.vseq.n.|| >=0 by NORMSP_1:8;
         hence ||.vseq.||.n >=0 by NORMSP_1:def 10;
      end;
     hence thesis by A3,SEQ_2:31;
    end;
    hence thesis by A36;
end;

A46:
   for e be Real st
    e > 0 ex k be Nat st for n be Nat st
     n >= k holds
        for x be VECTOR of X holds
            ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e* ||.x.||
   proof
     let e be Real such that
      A47:e > 0;
      consider k be Nat such that
      A48:  for n, m be Nat st ( n >= k & m >= k ) holds
             ||.(vseq.n) - (vseq.m).|| < e
                        by A2,A47,RSSPACE3:10;
      A49: now let n be Nat such that
         A50: n >= k;
             now let x be VECTOR of X;
              consider xseq be sequence of Y such that
              A51:
              (for n be Nat holds  xseq.n=modetrans((vseq.n),X,Y).x)
                & xseq is convergent
                & tseq.x = lim xseq by A28;
               A52:
                for m,k be Nat holds
                 ||.xseq.m-xseq.k.|| <= ||.vseq.m - vseq.k.|| * ||.x.||
                proof
                let m,k be Nat;
                  vseq.m is bounded LinearOperator of X,Y by A11,Def10;
                 then A53:           modetrans((vseq.m),X,Y)=vseq.m by Th35;
                  vseq.k is bounded LinearOperator of X,Y by A11,Def10;
                then A54:            modetrans((vseq.k),X,Y)=vseq.k by Th35;
                reconsider h1=vseq.m-vseq.k
              as bounded LinearOperator of X,Y by A11,Def10;
                A55:  xseq.m =modetrans((vseq.m),X,Y).x by A51;
                   xseq.k =modetrans((vseq.k),X,Y).x by A51;
                 then xseq.m - xseq.k =h1.x by A53,A54,A55,Th46;
                 hence thesis by Th38;
                 end;
           A56:  for m be Nat st m >=k holds
                    ||.xseq.n-xseq.m.|| <= e * ||.x.||
                    proof
                      let m be Nat such that
                      A57:  m >=k;
                      A58:  ||.xseq.n-xseq.m.||
                      <=  ||.vseq.n - vseq.m.|| * ||.x.|| by A52;
                      A59:  ||.vseq.n - vseq.m.|| <e by A48,A50,A57;
                         0 <= ||.x.|| by NORMSP_1:8;
                      then ||.vseq.n - vseq.m.|| * ||.x.|| <= e* ||.x.||
                  by A59,AXIOMS:25;
                      hence thesis by A58,AXIOMS:22;
                    end;
                    ||.xseq.n-tseq.x.|| <= e * ||.x.||
                  proof
                    deffunc F(Nat) = ||.xseq.$1 - xseq.n.||;
                    consider rseq be Real_Sequence such that
  A60:              for m be Nat holds rseq.m = F(m) from SEQ_1:sch 1;
  A61:            rseq is convergent & lim rseq = ||.tseq.x-xseq.n.||
                    proof
                        A62: xseq - xseq.n is convergent by A51,NORMSP_1:36;
                        A63: lim (xseq-xseq.n)= tseq.x - xseq.n
                    by A51,NORMSP_1:44;
                            rseq = ||.xseq - xseq.n.||
                         proof
                            now let x be set such that
                           A64:  x in NAT;
                           reconsider k=x as Nat by A64;
                           thus rseq.x = ||.xseq.k - xseq.n.|| by A60
                             .= ||.(xseq - xseq.n).k.|| by NORMSP_1:def 7
                             .= ||.(xseq - xseq.n).||.x by NORMSP_1:def 10;
                         end;
                         hence thesis by SEQ_1:8;
                         end;
                         hence thesis by A62,A63,Th47;
                     end;
              for m be Nat st m >= k holds rseq.m <= e * ||.x.||
                    proof
                      let m be Nat such that
                      A65:  m >=k;
                        rseq.m = ||.xseq.m-xseq.n.|| by A60
                                     .= ||.xseq.n-xseq.m.|| by NORMSP_1:11;
                      hence thesis by A56,A65;
                    end;
                   then lim rseq <= e * ||.x.|| by A61,Lm3;
                   hence thesis by A61,NORMSP_1:11;
                  end;
                 hence ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e* ||.x.||
                      by A51;
           end;
         hence for x be VECTOR of X holds
          ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e* ||.x.||;
     end;
     take k;
     thus thesis by A49;
   end;
  reconsider tseq as bounded LinearOperator of X,Y by A35;
  reconsider tv=tseq as Point of
   R_NormSpace_of_BoundedLinearOperators(X,Y) by A11,Def10;

A66:
   for e be Real st
    e > 0 ex k be Nat st for n be Nat st
     n >= k holds ||.vseq.n - tv.|| <= e
   proof
   let e be Real such that
      A67: e > 0;
      consider k be Nat such that
      A68: for n be Nat st n >= k holds
          for x be VECTOR of X holds
  ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e* ||.x.|| by A46,A67;
        now let n be Nat such that
        A69: n >= k;
        set f1=modetrans((vseq.n),X,Y);
        set g1=tseq;
        reconsider h1=vseq.n-tv as bounded LinearOperator of X,Y by A11,Def10;
       A70: now let t be VECTOR of X such that
       A71: ||.t.|| <= 1;
           vseq.n is bounded LinearOperator of X,Y by A11,Def10;
         then modetrans((vseq.n),X,Y)=vseq.n by Th35;
       then A72:  ||.h1.t.||= ||.f1.t-g1.t.|| by Th46;
       A73:  ||.f1.t-g1.t.|| <=e* ||.t.|| by A68,A69;
         e*||.t.|| <= e*1 by A67,A71,AXIOMS:25;
       hence ||.h1.t.|| <=e by A72,A73,AXIOMS:22;
      end;
     A74:
       now let r be Real such that
        A75:  r in PreNorms(h1);
           r in {||.h1.t.|| where t is VECTOR of X : ||.t.|| <= 1 }
    by A75,Def13;
        then consider t be VECTOR of X such that
          A76: r=||.h1.t.|| & ||.t.|| <= 1;
          thus r <=e by A70,A76;
      end;
     A77:
     (for s be real number st s in PreNorms(h1) holds s <= e)
      implies sup PreNorms(h1) <=e by SEQ_4:62;
       BoundedLinearOperatorsNorm(X,Y).(vseq.n-tv)
      = sup PreNorms(h1) by Th36;
     hence ||.vseq.n-tv.|| <=e by A11,A74,A77,NORMSP_1:def 1;
      end;
     hence thesis;
end;
    for e be Real st
    e > 0 ex m be Nat st for n be Nat st
     n >= m holds ||.(vseq.n) - tv.|| < e
   proof
    let e be Real such that
      A78: e > 0;
      A79: e/2 > 0 by A78,SEQ_2:3;
      A80: e/2<e by A78,SEQ_2:4;
      consider m be Nat such that
          A81: for n be Nat st
          n >= m holds ||.(vseq.n) - tv.|| <= e/2 by A66,A79;
            now let n be Nat such that
        A82: n >= m;
              ||.(vseq.n) - tv.|| <= e/2 by A81,A82;
            hence ||.(vseq.n) - tv.|| < e by A80,AXIOMS:22;
          end;
    hence thesis;
   end;
   hence thesis by NORMSP_1:def 9;
end;

theorem Th49:
  for X be RealNormSpace
  for Y be RealBanachSpace holds
     R_NormSpace_of_BoundedLinearOperators(X,Y) is RealBanachSpace
proof
    let X be RealNormSpace;
    let Y be RealBanachSpace;
      for seq be sequence of R_NormSpace_of_BoundedLinearOperators(X,Y)
      st seq is Cauchy_sequence_by_Norm holds seq is convergent by Th48;
    hence thesis by Def16;
end;

registration
  let X be RealNormSpace;
  let Y be RealBanachSpace;
  cluster R_NormSpace_of_BoundedLinearOperators (X,Y) -> complete;
  coherence by Th49;
end;

