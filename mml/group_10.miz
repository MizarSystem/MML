:: The Sylow Theorems
::  by Marco Riccardi
::
:: Received August 13, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies ARYTM_1, ARYTM_3, FINSEQ_1, RELAT_1, FUNCT_1, RLVECT_1, BOOLE,
      FINSEQ_2, FINSEQ_4, COMPLEX1, BINOP_1, VECTSP_1, GROUP_1, REALSET1,
      NAT_1, TARSKI, CARD_1, GROUP_2, FUNCOP_1, FUNCT_2, FINSET_1, RLSUB_1,
      SETFAM_1, EQREL_1, GROUP_3, GRAPH_1, ARYTM, ORDINAL2, GROUP_4, NEWTON,
      GR_CY_1, CARD_FIN, FILTER_0, NAT_3, GROUP_9, GROUP_10;
 notations TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, NUMBERS, XCMPLX_0, ZFMISC_1,
      XXREAL_0, INT_2, NAT_1, NAT_D, FINSET_1, RELAT_1, REALSET1, FUNCT_1,
      RELSET_1, FUNCT_2, FINSEQ_1, BINARITH, RVSUM_1, CARD_1, STRUCT_0,
      GROUP_1, GROUP_2, GROUP_3, EQREL_1, FUNCOP_1, WSIERP_1, NEWTON, DOMAIN_1,
      BINOP_2, GR_CY_1, ORDINAL1, XREAL_0, GROUP_4, CARD_FIN, PARTFUN1, NAT_3,
      GROUP_9, TOPGRP_1;
 constructors SETFAM_1, WELLORD2, XXREAL_0, REAL_1, NAT_D, EQREL_1, BINARITH,
      WSIERP_1, REALSET2, UPROOTS, GR_CY_1, GROUP_4, CARD_FIN, NAT_3, TOPGRP_1;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, ORDINAL1, RELSET_1, FINSET_1,
      XXREAL_0, XREAL_0, NAT_1, INT_1, CARD_1, FINSEQ_1, SEQ_1, NEWTON,
      STRUCT_0, GROUP_2, FSM_1, DYNKIN, GROUP_1, GROUP_3, GR_CY_1, FUNCT_2,
      SEQM_3, GROUP_6, FRAENKEL, NAT_3, REALSET1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions GROUP_2, TARSKI, REALSET1, RELAT_1, WELLORD2;
 theorems FINSEQ_1, GROUP_2, GROUP_3, TARSKI, GROUP_6, FINSEQ_2, FUNCT_1,
      FUNCT_2, RELAT_1, XBOOLE_0, XBOOLE_1, NAT_1, GROUP_1, XREAL_1, RELSET_1,
      PARTFUN1, FINSEQ_3, INT_1, ZFMISC_1, CARD_1, CARD_2, FINSET_1, XCMPLX_1,
      ORDINAL1, FINSEQ_4, XXREAL_0, STRUCT_0, NAT_D, GR_CY_1, NEWTON, CARD_FIN,
      WEDDWITT, WELLORD2, EQREL_1, FUNCOP_1, RVSUM_1, PEPIN, NAT_4, SEQ_1,
      GROUP_8, NAT_3, BINARITH, GROUP_9, GR_CY_2, TOPGRP_1;
 schemes FUNCT_1, FINSEQ_1, NAT_1, FUNCT_2, SUBSET_1;

begin :: Group Operating on a Set

notation
  let S be non empty 1-sorted;
  let E be set;
  let A be Action of (the carrier of S), E;
  let s be Element of S;
  synonym A^s for A.s;
end;

definition
  let S be non empty 1-sorted;
  let E be set;
  let A be Action of (the carrier of S), E;
  let s be Element of S;
  redefine func A^s -> Function of E, E;
  correctness
  proof
    consider f be Function such that
A1: A.s = f & dom f = E & rng f c= E by FUNCT_2:def 2;
    reconsider f as Function of E,E by A1,FUNCT_2:4;
    now
      take f;
      thus f = A.s by A1;
    end;
    hence thesis;
  end;
end;

definition
  let S be unital (non empty HGrStr);
  let E be set;
  let A be Action of (the carrier of S), E;
  attr A is being_left_operation means :Def1:
  A^(1_S) = id E &
    for s1,s2 being Element of S holds A^(s1*s2) = A^s1 * (A^s2);
end;

registration
  let S be unital (non empty HGrStr);
  let E be set;
  cluster being_left_operation Action of (the carrier of S), E;
  correctness
  proof
    reconsider IT=[:the carrier of S,{id E}:]
      as Action of (the carrier of S),E by GROUP_9:62;
    take IT;
A1: now
      let s be Element of S;
      now let x be set;
        assume A2: x in the carrier of S;
        consider y be set such that
        A3: y=id E;
        take y;
        y in {id E} by A3,TARSKI:def 1;
        hence [x,y] in [:the carrier of S,{id E}:] by A2,ZFMISC_1:def 2;
      end; then
      dom IT = the carrier of S by RELSET_1:22; then
      A4: IT.s in rng IT by FUNCT_1:12;
      reconsider IT'=IT as Relation of the carrier of S,{id E}
        by RELSET_1:def 1;
      rng IT' c= {id E};
      hence IT.s = id E by A4,TARSKI:def 1;
    end;
    thus IT^(1_S) = id E by A1;
    let s1,s2 be Element of S;
    IT^s1 = id E & IT^s2 = id E by A1; then
    IT^s1 * (IT^s2) = id E by PARTFUN1:36;
    hence IT^(s1*s2) = IT^s1 * (IT^s2) by A1;
  end;
end;

:: ALG I.5.1 DEF 1
definition
  let S be unital (non empty HGrStr);
  let E be set;
  mode LeftOperation of S, E is
    being_left_operation Action of (the carrier of S), E;
end;

scheme ExLeftOperation {E()->set, S()->Group-like (non empty HGrStr),
                        f(Element of S())->Function of E(),E()}:
  ex T being LeftOperation of S(), E()
    st for s being Element of S() holds T.s = f(s)
provided
A1: f(1_S()) = id E() and
A2: for s1,s2 being Element of S() holds f(s1*s2) = f(s1) * f(s2)
proof
  set T = {[s,f(s)] where s is Element of S(): not contradiction};
  now let x be set;
    assume x in T; then
    consider s be Element of S() such that
    A3: x = [s, f(s)];
    f(s) in Funcs(E(),E()) by FUNCT_2:12;
    hence x in [:the carrier of S(),Funcs(E(),E()):] by A3,ZFMISC_1:def 2;
  end; then
A4: T c= [: the carrier of S(), Funcs(E(),E()):] by TARSKI:def 3;
  now
    let x,y1,y2 be set;
    assume [x,y1] in T; then
    consider s1 be Element of S() such that
    A5: [x,y1] = [s1,f(s1)];
    assume [x,y2] in T; then
    consider s2 be Element of S() such that
    A6: [x,y2] = [s2,f(s2)];
    x=s1 & y1=f(s1) & x=s2 & y2=f(s2) by A5,A6,ZFMISC_1:33;
    hence y1 = y2;
  end; then
  reconsider T as Function-like Relation of
    (the carrier of S()), Funcs(E(),E()) by A4,FUNCT_1:def 1,RELSET_1:def 1;
  T is quasi_total
  proof
    now
      let x be set;
      hereby
        assume x in the carrier of S(); then
        reconsider s=x as Element of S();
        reconsider y=f(s) as set;
        take y;
        thus [x,y] in T;
      end;
      given y be set such that
      A7: [x,y] in T;
      consider s be Element of S() such that
      A8: [x,y] = [s,f(s)] by A7;
      x=s by A8,ZFMISC_1:33;
      hence x in the carrier of S();
    end; then
    the carrier of S() = dom T by RELAT_1:def 4;
    hence thesis by FUNCT_2:def 1;
  end; then
  reconsider T as Action of (the carrier of S()), E();
A9: dom T = the carrier of S() by FUNCT_2:def 1; then
  [1_S(),T.(1_S())] in T by FUNCT_1:8; then
  consider s' be Element of S() such that
A10: [1_S(),T.(1_S())] = [s',f(s')];
A11:  1_S() = s' & T.(1_S()) = f(s') by A10,ZFMISC_1:33;
  now
    let s1,s2 be Element of S();
    s1 in the carrier of S() & s2 in the carrier of S() &
    s1*s2 in the carrier of S(); then
    s1 in dom T & s2 in dom T & s1*s2 in dom T by FUNCT_2:def 1; then
    A12: [s1,T.s1] in T & [s2,T.s2] in T &
    [s1*s2,T.(s1*s2)] in T by FUNCT_1:8;
    consider s1' be Element of S() such that
    A13: [s1,T.s1] = [s1',f(s1')] by A12;
    consider s2' be Element of S() such that
    A14: [s2,T.s2] = [s2',f(s2')] by A12;
    consider s12' be Element of S() such that
    A15: [s1*s2,T.(s1*s2)] = [s12',f(s12')] by A12;
    A16: s1=s1' & f(s1')=T.s1 by A13,ZFMISC_1:33;
    A17: s2=s2' & f(s2')=T.s2 by A14,ZFMISC_1:33;
    thus T^(s1*s2) = f(s12') by A15,ZFMISC_1:33
    .= f(s1*s2) by A15,ZFMISC_1:33
    .= T^s1 * (T^s2) by A2,A16,A17;
  end; then
  reconsider T as LeftOperation of S(), E() by A1,A11,Def1;
  take T;
  thus for s being Element of S() holds T.s = f(s)
  proof
    let s be Element of S();
    [s,T.s] in T by A9,FUNCT_1:8; then
    consider s' be Element of S() such that
    A18: [s,T.s] = [s',f(s')];
    s=s' & T.s=f(s') by A18,ZFMISC_1:33;
    hence T.s = f(s);
  end;
end;

theorem Th1:
  for E being non empty set, S being Group-like (non empty HGrStr),
      s being Element of S, LO being LeftOperation of S, E
  holds LO^s is one-to-one
proof
  let E be non empty set;
  let S be Group-like (non empty HGrStr);
  let s be Element of S;
  let LO be LeftOperation of S, E;
  consider e be Element of S such that
A1: for h being Element of S holds h * e = h & e * h = h &
    ex g being Element of S st h * g = e & g * h = e by GROUP_1:def 3;
  consider s' be Element of S such that
A2: s * s' = e & s' * s = e by A1;
A3: id E = LO^(1_S) by Def1
  .= LO^(s' * s) by A1,A2,GROUP_1:10
  .= (LO^s') * (LO^s) by Def1;
  LO^s in Funcs(E,E) by FUNCT_2:12; then
A4: ex f being Function st LO^s = f & dom f = E & rng f c= E by FUNCT_2:def 2;
  LO^s' in Funcs(E,E) by FUNCT_2:12; then
  ex f being Function st LO^s' = f & dom f = E & rng f c= E by FUNCT_2:def 2;
  hence LO^s is one-to-one by A3,A4,FUNCT_1:47;
end;

:: left translation
:: ALG I.3.1
notation
  let S be non empty HGrStr;
  let s be Element of S;
  synonym the_left_translation_of s for s*;
end;

definition
  let S be Group-like associative (non empty HGrStr);
  func the_left_operation_of S -> LeftOperation of S, the carrier of S
    means :Def2:
    for s being Element of S holds it.s = the_left_translation_of s;
  existence
  proof
    set E = the carrier of S;
    deffunc f(Element of S) = the_left_translation_of $1;
    A1: for s1,s2 being Element of S holds f(s1*s2)=f(s1)*f(s2)
    proof
      let s1,s2 be Element of S;
      set f12 = the_left_translation_of(s1*s2);
      set f1 = the_left_translation_of(s1);
      set f2 = the_left_translation_of(s2);
      A2: f12 in Funcs(E,E) & f1 in Funcs(E,E) &
      f2 in Funcs(E,E) by FUNCT_2:12;
      A3: ex f being Function st f1=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A4: ex f being Function st f2=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A5: ex f being Function st f12=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A6: now
        let x be set;
        hereby
          assume A7: x in dom f12;
          hence x in dom f2 by A4;
          f2.x in rng f2 by A4,A7,FUNCT_1:12;
          hence f2.x in dom f1 by A3;
        end;
        assume x in dom f2 & f2.x in dom f1;
        hence x in dom f12 by A5;
      end;
      now
        let x be set;
        assume A8: x in dom f12;
        reconsider s1'=x as Element of S by A8;
        f2.x in rng f2 by A4,A8,FUNCT_1:12; then
        reconsider s1''=f2.x as Element of S;
        thus f12.x = (s1*s2) * s1' by TOPGRP_1:def 1
        .= s1 * (s2*s1') by GROUP_1:def 4
        .= s1 * s1'' by TOPGRP_1:def 1
        .= f1.(f2.x) by TOPGRP_1:def 1;
      end;
      hence thesis by A6,FUNCT_1:20;
    end;
    A9: f(1_S) = id E
    proof
      set f = the_left_translation_of(1_S);
      f in Funcs(E,E) by FUNCT_2:12; then
      A10: ex f' being Function st f=f' & dom f' = E & rng f' c= E
        by FUNCT_2:def 2;
      now
        let x be set;
        assume x in E; then
        reconsider s1=x as Element of S;
        f.s1 = 1_S * s1 by TOPGRP_1:def 1;
        hence f.x = x by GROUP_1:def 5;
      end;
      hence thesis by A10,FUNCT_1:34;
    end;
    ex T being LeftOperation of S, E st
      for s being Element of S holds T.s = f(s) from ExLeftOperation(A9,A1);
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of S, the carrier of S such that
A11: for s being Element of S holds T1.s = the_left_translation_of s and
A12: for s being Element of S holds T2.s = the_left_translation_of s;
    now
      let x be set;
      assume x in the carrier of S;
      then reconsider s = x as Element of S;
      thus T1.x = the_left_translation_of s by A11 .= T2.x by A12;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

definition
  let E be set;
  let n be set;
  func the_subsets_of_card(n, E) -> Subset-Family of E equals
  {X where X is Subset of E: Card X = n};
  correctness
  proof
    set SF = {X where X is Subset of E: Card X = n};
    now let x be set;
      assume x in SF; then
      ex X being Subset of E st X = x & Card X = n;
      hence x in bool E;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;

registration
  let E be finite set;
  let n be set;
  cluster the_subsets_of_card(n, E) -> finite;
  correctness
  proof
    bool E is finite by FINSET_1:24;
    hence thesis by FINSET_1:13;
  end;
end;

theorem Th2:
  for n being natural number, E being non empty set
  st Card n <=` Card E holds the_subsets_of_card(n, E) is non empty
proof
  let n be natural number;
  let E be non empty set;
  assume Card n <=` Card E; then
  consider f be Function such that
  A1: f is one-to-one & dom f = n & rng f c= E by CARD_1:26;
  set X = f .: n;
  reconsider n'=n as Element of NAT by ORDINAL1:def 13;
  A2: rng f = X by A1,RELAT_1:146; then
  n,X are_equipotent by A1,WELLORD2:def 4; then
  Card X = Card n' by CARD_1:21; then
  A3: Card X = n' by CARD_1:66;
  X in {X' where X' is Subset of E: Card X' = n} by A1,A2,A3;
  hence the_subsets_of_card(n, E) is non empty;
end;

theorem Th3:
  for E being non empty finite set, k being Element of NAT,
      x1,x2 being set st x1<>x2 holds
  card Choose(E,k,x1,x2) = card the_subsets_of_card(k,E)
proof
  let E be non empty finite set;
  let k be Element of NAT;
  let x1,x2 be set;
  assume A1: x1<>x2;
  set f = {[fX,X] where fX is Function of E,{x1,x2}, X is Subset of E:
    Card (fX"{x1}) = k & fX"{x1} = X};
  now
    let x be set;
    assume x in f; then
    consider y be Function of E,{x1,x2}, z be Subset of E such that
    A2: x = [y,z] & Card (y"{x1}) = k & y"{x1} = z;
    reconsider y,z as set;
    take y,z;
    thus x = [y,z] by A2;
  end; then
  reconsider f as Relation-like set by RELAT_1:def 1;
  now
    let x,y1,y2 be set;
    assume [x,y1] in f; then
    consider fX1 be Function of E,{x1,x2}, X1 be Subset of E such that
    A3: [x,y1]=[fX1,X1] & Card (fX1"{x1}) = k & fX1"{x1} = X1;
    assume [x,y2] in f; then
    consider fX2 be Function of E,{x1,x2}, X2 be Subset of E such that
    A4: [x,y2]=[fX2,X2] & Card (fX2"{x1}) = k & fX2"{x1} = X2;
    A5: x=fX1 & y1=X1 by A3,ZFMISC_1:33;
    x=fX2 & y2=X2 by A4,ZFMISC_1:33;
    hence y1 = y2 by A3,A4,A5;
  end; then
  reconsider f as Function by FUNCT_1:def 1;
  now
    let y1,y2 be set;
    assume A6: y1 in dom f & y2 in dom f;
    assume A7: f.y1 = f.y2;
    [y1,f.y1] in f by A6,FUNCT_1:8; then
    consider fX1 be Function of E,{x1,x2}, X1 be Subset of E such that
    A8: [y1,f.y1]=[fX1,X1] & Card (fX1"{x1}) = k & fX1"{x1} = X1;
    [y2,f.y2] in f by A6,FUNCT_1:8; then
    consider fX2 be Function of E,{x1,x2}, X2 be Subset of E such that
    A9: [y2,f.y2]=[fX2,X2] & Card (fX2"{x1}) = k & fX2"{x1} = X2;
    A10: y1=fX1 & f.y1=X1 by A8,ZFMISC_1:33;
    A11: y2=fX2 & f.y2=X2 by A9,ZFMISC_1:33;
    A12: fX1"{x1} = fX2"{x1} by A7,A8,A9,A10,ZFMISC_1:33;
    dom fX1 = E by FUNCT_2:def 1; then
    A13: dom fX1 = dom fX2 by FUNCT_2:def 1;
    for z being set st z in dom fX1 holds fX1.z = fX2.z
    proof
      assume ex z being set st z in dom fX1 & fX1.z <> fX2.z; then
      consider z be set such that
      A14: z in dom fX1 & fX1.z <> fX2.z;
      A15: fX1.z in {x1,x2} by A14,FUNCT_2:7;
      fX2.z in {x1,x2} by A14,FUNCT_2:7; then
      A16: fX2.z = x1 or fX2.z = x2 by TARSKI:def 2;
      per cases by A14,A15,A16,TARSKI:def 2;
      suppose A17: fX1.z = x1 & fX2.z = x2;
        A18: [z,fX1.z] in fX1 by A14,FUNCT_1:8;
        fX1.z in {x1} by A17,TARSKI:def 1; then
        z in fX2"{x1} by A12,A18,RELAT_1:def 14; then
        consider z' be set such that
        A19: [z,z'] in fX2 & z' in {x1} by RELAT_1:def 14;
        z' = fX2.z by A19,FUNCT_1:8;
        hence contradiction by A1,A17,A19,TARSKI:def 1;
      end;
      suppose A20: fX1.z = x2 & fX2.z = x1;
        A21: [z,fX2.z] in fX2 by A13,A14,FUNCT_1:8;
        fX2.z in {x1} by A20,TARSKI:def 1; then
        z in fX1"{x1} by A12,A21,RELAT_1:def 14; then
        consider z' be set such that
        A22: [z,z'] in fX1 & z' in {x1} by RELAT_1:def 14;
        z' = fX1.z by A22,FUNCT_1:8;
        hence contradiction by A1,A20,A22,TARSKI:def 1;
      end;
    end;
    hence y1 = y2 by A10,A11,A13,FUNCT_1:9;
  end; then
A23: f is one-to-one by FUNCT_1:def 8;
  for x being set holds
  x in Choose(E,k,x1,x2) iff ex y being set st [x,y] in f
  proof
    let x be set;
    thus x in Choose(E,k,x1,x2) implies ex y being set st [x,y] in f
    proof
      assume x in Choose(E,k,x1,x2); then
      consider fX be Function of E,{x1,x2} such that
      A24: fX = x & Card (fX"{x1})=k by CARD_FIN:def 1;
      set y = fX"{x1};
      take y;
      thus [x,y] in f by A24;
    end;
    thus (ex y being set st [x,y] in f) implies x in Choose(E,k,x1,x2)
    proof
      given y be set such that
      A25: [x,y] in f;
      consider fX be Function of E,{x1,x2}, X be Subset of E such that
      A26: [x,y]=[fX,X] & Card (fX"{x1}) = k & fX"{x1} = X by A25;
      x=fX by A26,ZFMISC_1:33;
      hence x in Choose(E,k,x1,x2) by A26,CARD_FIN:def 1;
    end;
  end; then
A27: dom f = Choose(E,k,x1,x2) by RELAT_1:def 4;
  for y being set holds
    y in the_subsets_of_card(k,E) iff ex x being set st [x,y] in f
  proof
    let y be set;
    hereby
      assume y in the_subsets_of_card(k,E); then
      consider X be Subset of E such that
      A28: y=X & card X = k;
      defpred P[set,set] means
        ($1 in X & $2=x1) or (not $1 in X & $2=x2);
      A29: for z1 being Element of E ex z2 being Element of {x1,x2} st P[z1,z2]
      proof
        let z1 be Element of E;
        per cases;
        suppose A30: z1 in X;
          reconsider z2=x1 as Element of {x1,x2} by TARSKI:def 2;
          take z2;
          thus P[z1,z2] by A30;
        end;
        suppose A31: not z1 in X;
          reconsider z2=x2 as Element of {x1,x2} by TARSKI:def 2;
          take z2;
          thus P[z1,z2] by A31;
        end;
      end;
      ex f being Function of E,{x1,x2} st
        for z being Element of E holds P[z,f.z] from FUNCT_2:sch 3(A29); then
      consider f' be Function of E,{x1,x2} such that
      A32: for z being Element of E holds P[z,f'.z];
      reconsider x=f' as set;
      take x;
      now
        let z be set;
        hereby
          assume A33: z in X;
          z in E by A33; then
          z in dom f' by FUNCT_2:def 1; then
          A34: [z,f'.z] in f' by FUNCT_1:8;
          reconsider z'=z as Element of E by A33;
          A35: f'.z'=x1 by A32,A33;
          set z''=f'.z';
          z'' in {x1} by A35,TARSKI:def 1;
          hence z in f'"{x1} by A34,RELAT_1:def 14;
        end;
        assume z in f'"{x1}; then
        consider z' be set such that
        A36: [z,z'] in f' & z' in {x1} by RELAT_1:def 14;
        A37: z'=x1 by A36,TARSKI:def 1;
        z in dom f' by A36,RELAT_1:def 4; then
        reconsider z''=z as Element of E;
        f'.z'' = x1 by A36,A37,FUNCT_1:8;
        hence z in X by A1,A32;
      end; then
      X = f'"{x1} by TARSKI:2;
      hence [x,y] in f by A28;
    end;
    given x be set such that
    A38: [x,y] in f;
    consider fX be Function of E,{x1,x2}, X be Subset of E such that
    A39: [x,y]=[fX,X] & Card (fX"{x1}) = k & fX"{x1} = X by A38;
    Card X = k & y = X by A39,ZFMISC_1:33;
    hence y in the_subsets_of_card(k,E);
  end; then
  rng f = the_subsets_of_card(k,E) by RELAT_1:def 5; then
  Choose(E,k,x1,x2), the_subsets_of_card(k,E) are_equipotent
    by A23,A27,WELLORD2:def 4;
  hence thesis by CARD_1:81;
end;

definition
  let E be non empty set;
  let n be natural number;
  let S be Group-like (non empty HGrStr);
  let s be Element of S;
  let LO be LeftOperation of S, E;
  assume A1: Card n <=` Card E;
  func the_extension_of_left_translation_of(n,s,LO) ->
    Function of the_subsets_of_card(n, E), the_subsets_of_card(n, E)
  means :Def4:
    for X being Element of the_subsets_of_card(n, E) holds it.X = (LO^s) .: X;
  existence
  proof
    set EE = the_subsets_of_card(n, E);
    set f = {[X,(LO^s) .: X] where X is Element of EE: not contradiction};
      A2: now
        let x,y1,y2 be set;
        assume [x,y1] in f; then consider X1 be Element of EE such that
        A3: [x,y1] = [X1,(LO^s) .: X1];
        assume [x,y2] in f; then consider X2 be Element of EE such that
        A4: [x,y2] = [X2,(LO^s) .: X2];
        A5: x = X1 & y1 = (LO^s) .: X1 by A3,ZFMISC_1:33;
        x = X2 & y2 = (LO^s) .: X2 by A4,ZFMISC_1:33;
        hence y1 = y2 by A5;
      end;
      now
        let x be set;
        assume x in f; then consider X be Element of EE such that
        A6: x = [X,(LO^s) .: X];
        reconsider y=X,z=(LO^s) .: X as set;
        take y,z;
        thus x = [y,z] by A6;
      end; then
      reconsider f as Function by A2,FUNCT_1:def 1,RELAT_1:def 1;
      now
        let x be set;
        hereby
          assume x in EE; then
          reconsider X = x as Element of EE;
          reconsider y = (LO^s) .: X as set;
          take y;
          thus [x,y] in f;
        end;
        given y be set such that
        A7: [x,y] in f;
        consider X be Element of EE such that
        A8: [x,y] = [X,(LO^s) .: X] by A7;
        A9: x = X by A8,ZFMISC_1:33;
        EE is not empty by A1,Th2;
        hence x in EE by A9;
      end; then
A10:  dom f = EE by RELAT_1:def 4;
      now let Y be set;
        assume Y in rng f; then
        consider X1 be set such that
        A11: [X1,Y] in f by RELAT_1:def 5;
        consider X2 be Element of EE such that
        A12: [X1,Y] = [X2,(LO^s) .: X2] by A11;
        EE is not empty by A1,Th2; then
        A13: X2 in EE; then
        A14: ex X being Subset of E st X=X2 & Card X = n;
        A15: Y = (LO^s) .: X2 by A12,ZFMISC_1:33;
        LO^s in Funcs(E,E) by FUNCT_2:12; then
        A16: ex f being Function st LO^s = f & dom f = E & rng f c= E
          by FUNCT_2:def 2;
        set fe = (LO^s)|X2;
        LO^s is one-to-one by Th1; then
        A17: fe is one-to-one by FUNCT_1:84;
        A18: dom fe = X2 by A13,A16,RELAT_1:91;
        A19: rng fe = Y by A15,RELAT_1:148;
        reconsider n'=n as Element of NAT by ORDINAL1:def 13;
        X2,Y are_equipotent by A17,A18,A19,WELLORD2:def 4; then
        Card Y = n by A14,CARD_1:21;
        hence Y in EE by A15;
      end; then
      rng f c= EE by TARSKI:def 3; then
      reconsider f as Function of EE,EE by A10,FUNCT_2:4;
      take f;
    thus for X being Element of EE holds f.X = (LO^s) .: X
    proof
      let X be Element of EE;
      EE is non empty by A1,Th2; then
      [X,f.X] in f by A10,FUNCT_1:8; then
      consider X' be Element of EE such that
      A20: [X,f.X] = [X',(LO^s) .: X'];
      X = X' & f.X = (LO^s) .: X' by A20,ZFMISC_1:33;
      hence f.X = (LO^s) .: X;
    end;
  end;
  uniqueness
  proof
    set EE = the_subsets_of_card(n, E);
    let IT1, IT2 be Function of EE,EE such that
A21: for X being Element of EE holds IT1.X = (LO^s) .: X and
A22: for X being Element of EE holds IT2.X = (LO^s) .: X;
    now
      let x be set;
      assume x in EE;
      then reconsider X = x as Element of EE;
      thus IT1.x = (LO^s) .: X by A21 .= IT2.x by A22;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

definition
  let E be non empty set;
  let n be natural number;
  let S be Group-like (non empty HGrStr);
  let LO be LeftOperation of S, E;
  assume A1: Card n <=` Card E;
  func the_extension_of_left_operation_of(n,LO) ->
    LeftOperation of S, the_subsets_of_card(n, E) means :Def5:
    for s being Element of S
      holds it.s = the_extension_of_left_translation_of(n,s,LO);
  existence
  proof
    set EE=the_subsets_of_card(n, E);
    deffunc f(Element of S)=the_extension_of_left_translation_of(n,$1,LO);
    A2: for s1,s2 being Element of S holds f(s1*s2)=f(s1)*f(s2)
    proof
      let s1,s2 be Element of S;
      set f12 = the_extension_of_left_translation_of(n,s1*s2,LO);
      set f1 = the_extension_of_left_translation_of(n,s1,LO);
      set f2 = the_extension_of_left_translation_of(n,s2,LO);
      A3: f12 in Funcs(EE,EE) & f1 in Funcs(EE,EE) &
        f2 in Funcs(EE,EE) by FUNCT_2:12;
      A4: ex f being Function st f1=f & dom f = EE & rng f c= EE
        by A3,FUNCT_2:def 2;
      A5: ex f being Function st f2=f & dom f = EE & rng f c= EE
        by A3,FUNCT_2:def 2;
      A6: ex f being Function st f12=f & dom f = EE & rng f c= EE
        by A3,FUNCT_2:def 2;
      A7: now
        let x be set;
        hereby
          assume A8: x in dom f12;
          hence x in dom f2 by A5;
          f2.x in rng f2 by A5,A8,FUNCT_1:12;
          hence f2.x in dom f1 by A4;
        end;
        assume x in dom f2 & f2.x in dom f1;
        hence x in dom f12 by A6;
      end;
      now
        let x be set;
        assume A9: x in dom f12; then
        reconsider X1=x as Element of EE;
        f2.x in rng f2 by A5,A9,FUNCT_1:12; then
        reconsider X2=f2.x as Element of EE;
        reconsider X3=x as Element of EE by A9;
        thus f12.x = (LO^(s1*s2)) .: X1 by A1,Def4
        .= ((LO^s1)*(LO^s2)) .: X1 by Def1
        .= (LO^s1) .: ((LO^s2) .: X1) by RELAT_1:159
        .= (LO^s1) .: X2 by A1,Def4
        .= f1.(f2.x) by A1,Def4;
      end;
      hence thesis by A7,FUNCT_1:20;
    end;
    A10: f(1_S) = id EE
    proof
      set f = the_extension_of_left_translation_of(n,1_S,LO);
      f in Funcs(EE,EE) by FUNCT_2:12; then
      A11: ex f' being Function st f=f' & dom f' = EE & rng f' c= EE
        by FUNCT_2:def 2;
      now
        let x be set;
        assume x in EE; then
        reconsider X=x as Element of EE;
        A12: f.X=(LO^1_S) .: X by A1,Def4;
        EE is non empty by A1,Th2; then
        X in EE; then
        consider X' be Subset of E such that
        A13: X'=X & Card X' = n;
        f.x = (id E) .: X' by A12,A13,Def1
        .= X' by FUNCT_1:162;
        hence f.x = x by A13;
      end;
      hence thesis by A11,FUNCT_1:34;
    end;
    ex T being LeftOperation of S, EE st
      for s being Element of S holds T.s = f(s) from ExLeftOperation(A10,A2);
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of S, the_subsets_of_card(n, E) such that
A14: for s being Element of S
      holds T1.s = the_extension_of_left_translation_of(n,s,LO) and
A15: for s being Element of S
      holds T2.s = the_extension_of_left_translation_of(n,s,LO);
    now
      let x be set;
      assume x in the carrier of S;
      then reconsider s = x as Element of S;
      thus T1.x = the_extension_of_left_translation_of(n,s,LO) by A14
      .= T2.x by A15;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

definition
  let S be non empty HGrStr;
  let s be Element of S;
  let Z be non empty set;
  func the_left_translation_of(s,Z) ->
    Function of [:the carrier of S,Z:],[:the carrier of S,Z:]
    means :Def6:
    for z1 being Element of [:the carrier of S,Z:] holds
      ex z2 being Element of [:the carrier of S,Z:], s1,s2 being Element of S,
         z being Element of Z
      st z2 = it.z1 & s2 = s * s1 & z1=[s1,z] & z2=[s2,z];
  existence
  proof
      set f = {[z1,z2] where z1,z2 is Element of [:the carrier of S,Z:]:
                 ex s1,s2 being Element of S, z being Element of Z st
                   s2 = s * s1 & z1=[s1,z] & z2=[s2,z]};
      set E = [:the carrier of S,Z:];
      A1: now
        let x,y1,y2 be set;
        assume [x,y1] in f; then consider z1',z2' be Element of E such that
        A2: [x,y1] = [z1',z2'] & ex s1,s2 being Element of S,
          z being Element of Z st s2 = s * s1 & z1'=[s1,z] & z2'=[s2,z];
        consider s1',s2' be Element of S, z' be Element of Z such that
        A3: s2' = s * s1' & z1'=[s1',z'] & z2'=[s2',z'] by A2;
        assume [x,y2] in f; then consider z1'',z2'' be Element of E such that
        A4: [x,y2] = [z1'',z2''] & ex s1,s2 being Element of S,
          z being Element of Z st s2 = s * s1 & z1''=[s1,z] & z2''=[s2,z];
        consider s1'',s2'' be Element of S, z'' be Element of Z such that
        A5: s2'' = s * s1'' & z1''=[s1'',z''] & z2''=[s2'',z''] by A4;
        x = z1'' & y2 = z2'' by A4,ZFMISC_1:33; then
        [s1',z'] = [s1'',z''] by A2,A3,A5,ZFMISC_1:33; then
        s1'=s1'' & z'=z'' by ZFMISC_1:33;
        hence y1 = y2 by A2,A3,A4,A5,ZFMISC_1:33;
      end;
      now
        let x be set;
        assume x in f; then consider z1,z2 be Element of E such that
        A6: x = [z1,z2] & ex s1,s2 being Element of S,
          z being Element of Z st s2 = s * s1 & z1=[s1,z] & z2=[s2,z];
        reconsider y=z1,z=z2 as set;
        take y,z;
        thus x = [y,z] by A6;
      end; then
      reconsider f as Function by A1,FUNCT_1:def 1,RELAT_1:def 1;
      now
        let x be set;
        hereby
          assume x in E; then
          consider s1,z be set such that
          A7: s1 in the carrier of S & z in Z & x = [s1,z] by ZFMISC_1:def 2;
          reconsider s1 as Element of S by A7;
          reconsider z as Element of Z by A7;
          set s2 = s * s1;
          reconsider y = [s2,z] as set;
          take y;
          s2 = s * s1 & x=[s1,z] & y=[s2,z] by A7;
          hence [x,y] in f;
        end;
        given y be set such that
        A8: [x,y] in f;
        consider z1,z2 be Element of E such that
        A9: [x,y] = [z1,z2] & ex s1,s2 being Element of S,
          z being Element of Z st s2 = s * s1 & z1=[s1,z] & z2=[s2,z] by A8;
        x = z1 by A9,ZFMISC_1:33;
        hence x in E;
      end; then
A10:   dom f = E by RELAT_1:def 4;
      now let y be set;
        assume y in rng f; then
        consider x be set such that
        A11: [x,y] in f by RELAT_1:def 5;
        consider z1,z2 be Element of E such that
        A12: [x,y] = [z1,z2] & ex s1,s2 being Element of S,
          z being Element of Z st s2 = s * s1 & z1=[s1,z] & z2=[s2,z] by A11;
        y = z2 by A12,ZFMISC_1:33;
        hence y in E;
      end; then
      rng f c= E by TARSKI:def 3; then
      reconsider f as Function of E,E by A10,FUNCT_2:4;
      take f;
      thus for z1 being Element of [:the carrier of S,Z:] holds
      ex z2 being Element of [:the carrier of S,Z:], s1,s2 being Element of S,
         z being Element of Z
      st z2 = f.z1 & s2 = s * s1 & z1=[s1,z] & z2=[s2,z]
      proof
        let z1 be Element of [:the carrier of S,Z:];
        set z2 = f.z1;
        [z1,z2] in f by A10,FUNCT_1:8; then
        consider z1',z2' be Element of [:the carrier of S,Z:] such that
        A13: [z1,z2]=[z1',z2'] & ex s1,s2 being Element of S,
          z being Element of Z st s2 = s * s1 & z1'=[s1,z] & z2'=[s2,z];
        consider s1,s2 be Element of S, z be Element of Z such that
        A14: s2 = s * s1 & z1'=[s1,z] & z2'=[s2,z] by A13;
        take z2,s1,s2,z;
        thus z2 = f.z1;
        thus s2 = s * s1 & z1=[s1,z] & z2=[s2,z] by A13,A14,ZFMISC_1:33;
      end;
  end;
  uniqueness
  proof
    let IT1, IT2 be Function of [:the carrier of S,Z:],[:the carrier of S,Z:]
    such that
A15: for z1 being Element of [:the carrier of S,Z:] holds
      ex z2 being Element of [:the carrier of S,Z:], s1,s2 being Element of S,
         z being Element of Z
      st z2 = IT1.z1 & s2 = s * s1 & z1=[s1,z] & z2=[s2,z] and
A16: for z1 being Element of [:the carrier of S,Z:] holds
      ex z2 being Element of [:the carrier of S,Z:], s1,s2 being Element of S,
         z being Element of Z
      st z2 = IT2.z1 & s2 = s * s1 & z1=[s1,z] & z2=[s2,z];
    now
      let x be set;
      assume x in [:the carrier of S,Z:];
      then reconsider z1 = x as Element of [:the carrier of S,Z:];
      consider z12 be Element of [:the carrier of S,Z:],
        s11,s12 be Element of S, z' be Element of Z such that
      A17: z12 = IT1.z1 & s12 = s * s11 & z1=[s11,z'] & z12=[s12,z'] by A15;
      consider z22 be Element of [:the carrier of S,Z:],
        s21,s22 be Element of S, z'' be Element of Z such that
      A18: z22 = IT2.z1 & s22 = s * s21 & z1=[s21,z''] & z22=[s22,z''] by A16;
      A19: s11=s21 & z'=z'' by A17,A18,ZFMISC_1:33;
      thus IT1.x = IT2.x by A17,A18,A19;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

definition
  let S be Group-like associative (non empty HGrStr);
  let Z be non empty set;
  func the_left_operation_of(S,Z) -> LeftOperation of S,[:the carrier of S,Z:]
    means :Def7:
    for s being Element of S holds it.s = the_left_translation_of(s,Z);
  existence
  proof
    set E = [:the carrier of S,Z:];
    deffunc f(Element of S)=the_left_translation_of($1,Z);
    A1: for s1,s2 being Element of S holds f(s1*s2)=f(s1)*f(s2)
    proof
      let s1,s2 be Element of S;
      set f12 = the_left_translation_of(s1*s2,Z);
      set f1 = the_left_translation_of(s1,Z);
      set f2 = the_left_translation_of(s2,Z);
      A2: f12 in Funcs(E,E) & f1 in Funcs(E,E) &
      f2 in Funcs(E,E) by FUNCT_2:12;
      A3: ex f being Function st f1=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A4: ex f being Function st f2=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A5: ex f being Function st f12=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A6: now
        let x be set;
        hereby
          assume A7: x in dom f12;
          hence x in dom f2 by A4;
          f2.x in rng f2 by A4,A7,FUNCT_1:12;
          hence f2.x in dom f1 by A3;
        end;
        assume x in dom f2 & f2.x in dom f1;
        hence x in dom f12 by A5;
      end;
      now
        let x be set;
        assume A8: x in dom f12; then
        reconsider z1'=x as Element of E;
        consider z2' be Element of E, s1',s2' be Element of S,
          z' be Element of Z such that
        A9: z2'=f2.z1' & s2'=s2 * s1' & z1'=[s1',z'] & z2'=[s2',z'] by Def6;
        f2.x in rng f2 by A4,A8,FUNCT_1:12; then
        reconsider z1''=f2.x as Element of E;
        consider z2'' be Element of E, s1'',s2'' be Element of S,
          z'' be Element of Z such that
        A10: z2''=f1.z1'' & s2''=s1*s1'' & z1''=[s1'',z''] & z2''=[s2'',z'']
          by Def6;
        reconsider z1'''=x as Element of E by A8;
        consider z2''' be Element of E, s1''',s2''' be Element of S,
          z''' be Element of Z such that
        A11: z2'''=f12.z1''' & s2''' = (s1*s2) * s1''' & z1'''=[s1''',z'''] &
          z2'''=[s2''',z'''] by Def6;
        A12: s1'' = s2' & z'' = z' by A9,A10,ZFMISC_1:33;
        A13: s2''' = (s1*s2) * s1' by A9,A11,ZFMISC_1:33
        .= s1 * (s2*s1') by GROUP_1:def 4
        .= s2'' by A9,A10,ZFMISC_1:33;
        thus f12.x = f1.(f2.x) by A9,A10,A11,A12,A13,ZFMISC_1:33;
      end;
      hence thesis by A6,FUNCT_1:20;
    end;
    A14: f(1_S) = id E
    proof
      set f = the_left_translation_of(1_S,Z);
      f in Funcs(E,E) by FUNCT_2:12; then
      A15: ex f' being Function st f=f' & dom f' = E & rng f' c= E
        by FUNCT_2:def 2;
      now
        let x be set;
        assume x in E; then
        reconsider z1=x as Element of E;
        consider z2 be Element of E, s1,s2 be Element of S,
          z be Element of Z such that
        A16: z2=f.z1 & s2 = 1_S * s1 & z1=[s1,z] & z2=[s2,z] by Def6;
        thus f.x = x by A16,GROUP_1:def 5;
      end;
      hence thesis by A15,FUNCT_1:34;
    end;
    consider T be LeftOperation of S, E such that
    A17: for s being Element of S holds T.s=f(s) from ExLeftOperation(A14,A1);
    thus thesis by A17;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of S, [:the carrier of S,Z:] such that
A18: for s being Element of S holds T1.s = the_left_translation_of(s,Z) and
A19: for s being Element of S holds T2.s = the_left_translation_of(s,Z);
    now
      let x be set;
      assume x in the carrier of S;
      then reconsider s = x as Element of S;
      thus T1.x = the_left_translation_of(s,Z) by A18
      .= T2.x by A19;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

definition
  let G be Group;
  let H,P be Subgroup of G;
  let h be Element of H;
  func the_left_translation_of(h,P) -> Function of Left_Cosets P, Left_Cosets P
  means :Def8:
    for P1 being Element of Left_Cosets P holds
      ex P2 being Element of Left_Cosets P, A1,A2 being Subset of G,
         g being Element of G
      st P2 = it.P1 & A2 = g * A1 & A1=P1 & A2=P2 & g=h;
  existence
  proof
    set f = {[P1,P2] where P1,P2 is Element of Left_Cosets P:
        ex A1,A2 being Subset of G, g being Element of G
        st A2 = g * A1 & A1=P1 & A2=P2 & g=h};
      A1: now
        let x,y1,y2 be set;
        assume [x,y1] in f; then
        consider P1',P2' be Element of Left_Cosets P such that
        A2: [x,y1] = [P1',P2'] &
          ex A1,A2 being Subset of G,g being Element of G
          st A2 = g * A1 & A1=P1' & A2=P2' & g=h;
        assume [x,y2] in f; then
        consider P1'',P2'' be Element of Left_Cosets P such that
        A3: [x,y2] = [P1'',P2''] &
          ex A1,A2 being Subset of G,g being Element of G
          st A2 = g * A1 & A1=P1'' & A2=P2'' & g=h;
        A4: x = P1'' & y2 = P2'' by A3,ZFMISC_1:33;
        x = P1' & y1 = P2' by A2,ZFMISC_1:33;
        hence y1 = y2 by A2,A3,A4;
      end;
      now
        let x be set;
        assume x in f; then
        consider P1,P2 be Element of Left_Cosets P such that
        A5: x=[P1,P2] & ex A1,A2 being Subset of G,g being Element of G
        st A2=g*A1 & A1=P1 & A2=P2 & g=h;
        reconsider y=P1,z=P2 as set;
        take y,z;
        thus x = [y,z] by A5;
      end; then
      reconsider f as Function by A1,FUNCT_1:def 1,RELAT_1:def 1;
      now
        let x be set;
        hereby
          assume x in Left_Cosets P; then
          consider a be Element of G such that
          A6: x = a * P by GROUP_2:def 15;
          reconsider g = h as Element of G by GROUP_2:51;
          reconsider y = g * (a * P) as set;
          take y;
          reconsider P1=x as Element of Left_Cosets P by A6,GROUP_2:def 15;
          g * (a * P) = (g*a) * P by GROUP_2:127; then
          reconsider P2=y as Element of Left_Cosets P by GROUP_2:def 15;
          ex A1,A2 being Subset of G st A2=g*A1 & A1=P1 & A2=P2 by A6;
          hence [x,y] in f;
        end;
        given y be set such that
        A7: [x,y] in f;
        consider P1,P2 be Element of Left_Cosets P such that
        A8: [x,y]=[P1,P2] &
          ex A1,A2 being Subset of G, g being Element of G
          st A2=g*A1 & A1=P1 & A2=P2 & g=h by A7;
        x = P1 & Left_Cosets P is not empty by A8,GROUP_2:165,ZFMISC_1:33;
        hence x in Left_Cosets P;
      end; then
A9:  dom f = Left_Cosets P by RELAT_1:def 4;
      now let y be set;
        assume y in rng f; then
        consider x be set such that
        A10: x in dom f & y = f.x by FUNCT_1:def 5;
        [x,y] in f by A10,FUNCT_1:8; then
        consider P1,P2 be Element of Left_Cosets P such that
        A11: [x,y]=[P1,P2] &
          ex A1,A2 being Subset of G, g being Element of G
          st A2=g*A1 & A1=P1 & A2=P2 & h=g;
        y = P2 & Left_Cosets P is not empty by A11,GROUP_2:165,ZFMISC_1:33;
        hence y in Left_Cosets P;
      end; then
      rng f c= Left_Cosets P by TARSKI:def 3; then
      reconsider f as Function of Left_Cosets P, Left_Cosets P
        by A9,FUNCT_2:4;
      take f;
    thus for P1 being Element of Left_Cosets P holds
      ex P2 being Element of Left_Cosets P, A1,A2 being Subset of G,
         g being Element of G
      st P2 = f.P1 & A2 = g * A1 & A1=P1 & A2=P2 & g=h
     proof
       let P1 be Element of Left_Cosets P;
       set P2=f.P1;
       Left_Cosets P is non empty by GROUP_2:165; then
       [P1,P2] in f by A9,FUNCT_1:8; then
       consider P1',P2' be Element of Left_Cosets P such that
       A12: [P1,P2]=[P1',P2'] &
          ex A1,A2 being Subset of G, g being Element of G
          st A2=g*A1 & A1=P1' & A2=P2' & h=g;
       consider A1,A2 be Subset of G, g be Element of G such that
       A13: A2=g*A1 & A1=P1' & A2=P2' & h=g by A12;
       reconsider P2 as Element of Left_Cosets P by A12,ZFMISC_1:33;
       take P2, A1,A2, g;
       thus P2 = f.P1;
       thus A2 = g * A1 & A1=P1 & A2=P2 & g=h by A12,A13,ZFMISC_1:33;
     end;

  end;
  uniqueness
  proof
    let IT1, IT2 be Function of Left_Cosets P, Left_Cosets P such that
A14: for P1 being Element of Left_Cosets P holds
      ex P2 being Element of Left_Cosets P, A1,A2 being Subset of G,
         g being Element of G
      st P2 = IT1.P1 & A2 = g * A1 & A1=P1 & A2=P2 & g=h and
A15: for P1 being Element of Left_Cosets P holds
      ex P2 being Element of Left_Cosets P, A1,A2 being Subset of G,
         g being Element of G
      st P2 = IT2.P1 & A2 = g * A1 & A1=P1 & A2=P2 & g=h;
    now
      let x be set;
      assume x in Left_Cosets P;
      then reconsider P1 = x as Element of Left_Cosets P;
      consider P12 be Element of Left_Cosets P, A11,A12 be Subset of G,
         g1 be Element of G such that
      A16: P12 = IT1.P1 & A12 = g1 * A11 & A11=P1 & A12=P12 & g1=h by A14;
      consider P22 be Element of Left_Cosets P, A21,A22 be Subset of G,
         g2 be Element of G such that
      A17: P22 = IT2.P1 & A22 = g2 * A21 & A21=P1 & A22=P22 & g2=h by A15;
      thus IT1.x = IT2.x by A16,A17;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

definition
  let G be Group;
  let H,P be Subgroup of G;
  func the_left_operation_of(H,P) -> LeftOperation of H, Left_Cosets P
    means :Def9:
    for h being Element of H holds it.h = the_left_translation_of(h,P);
  existence
  proof
    deffunc f(Element of H)=the_left_translation_of($1,P);
    A1: for h1,h2 being Element of H holds f(h1*h2)=f(h1)*f(h2)
    proof
      let h1,h2 be Element of H;
      set f12 = the_left_translation_of(h1*h2,P);
      set f1 = the_left_translation_of(h1,P);
      set f2 = the_left_translation_of(h2,P);
      A2: f12 in Funcs(Left_Cosets P ,Left_Cosets P) &
      f1 in Funcs(Left_Cosets P ,Left_Cosets P) &
      f2 in Funcs(Left_Cosets P ,Left_Cosets P) by FUNCT_2:12;
      A3: ex f being Function st f1=f & dom f = Left_Cosets P
        & rng f c= Left_Cosets P by A2,FUNCT_2:def 2;
      A4: ex f being Function st f2=f & dom f = Left_Cosets P
        & rng f c= Left_Cosets P by A2,FUNCT_2:def 2;
      A5: ex f being Function st f12=f & dom f = Left_Cosets P
        & rng f c= Left_Cosets P by A2,FUNCT_2:def 2;
      A6: now
        let x be set;
        hereby
          assume A7: x in dom f12;
          hence x in dom f2 by A4;
          f2.x in rng f2 by A4,A7,FUNCT_1:12;
          hence f2.x in dom f1 by A3;
        end;
        assume x in dom f2 & f2.x in dom f1;
        hence x in dom f12 by A5;
      end;
      now
        let x be set;
        assume A8: x in dom f12; then
        reconsider P1'=x as Element of Left_Cosets P;
        consider P2' be Element of Left_Cosets P,
          A1',A2' be Subset of G, g' be Element of G such that
        A9: P2' = f2.P1' & A2' = g' * A1' & A1'=P1' & A2'=P2' & g'=h2 by Def8;
        f2.x in rng f2  by A4,A8,FUNCT_1:12; then
        reconsider P1''=f2.x as Element of Left_Cosets P;
        consider P2'' be Element of Left_Cosets P,
          A1'',A2'' be Subset of G, g'' be Element of G such that
        A10: P2''=f1.P1'' & A2'' = g'' * A1'' & A1''=P1'' & A2''=P2'' & g''=h1
          by Def8;
        reconsider P1'''=x as Element of Left_Cosets P by A8;
        consider P2''' be Element of Left_Cosets P,
          A1''',A2''' be Subset of G, g''' be Element of G such that
        A11: P2'''=f12.P1''' & A2'''=g''' * A1''' & A1'''=P1''' & A2'''=P2''' &
          g'''=h1*h2 by Def8;
        g'''=g''*g' by A9,A10,A11,GROUP_2:52;
        hence f12.x = f1.(f2.x) by A9,A10,A11,GROUP_2:38;
      end;
      hence thesis by A6,FUNCT_1:20;
    end;
    A12: f(1_H) = id Left_Cosets P
    proof
      set f = the_left_translation_of(1_H,P);
      f in Funcs(Left_Cosets P,Left_Cosets P) by FUNCT_2:12; then
      A13: ex f' being Function st f=f' & dom f' = Left_Cosets P
        & rng f' c= Left_Cosets P by FUNCT_2:def 2;
      now
        let x be set;
        assume x in Left_Cosets P; then
        reconsider P1=x as Element of Left_Cosets P;
        consider P2 be Element of Left_Cosets P,
          A1,A2 be Subset of G, g be Element of G such that
        A14: P2=f.P1 & A2 = g * A1 & A1=P1 & A2=P2 & g=1_H by Def8;
        1_H=1_G by GROUP_2:53;
        hence f.x = x by A14,GROUP_2:43;
      end;
      hence thesis by A13,FUNCT_1:34;
    end;
    ex T being LeftOperation of H, Left_Cosets P st
      for h being Element of H holds T.h = f(h) from ExLeftOperation(A12,A1);
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of H, Left_Cosets P such that
A15: for h being Element of H holds T1.h = the_left_translation_of(h,P) and
A16: for h being Element of H holds T2.h = the_left_translation_of(h,P);
    now
      let x be set;
      assume x in the carrier of H;
      then reconsider h = x as Element of H;
      thus T1.x = the_left_translation_of(h,P) by A15
      .= T2.x by A16;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

begin :: Stabilizer and Orbits

:: stabilizer
:: ALG I.5.2 Definition 3
definition
  let G be Group;
  let E be non empty set;
  let T be LeftOperation of G,E;
  let A be Subset of E;
  func the_strict_stabilizer_of(A,T) -> strict Subgroup of G means :Def10:
  the carrier of it = {g where g is Element of G: (T^g) .: A = A};
  existence
  proof
    set X={g where g is Element of G: (T^g) .: A = A};
    now let x be set;
      assume x in X; then
      ex g being Element of G st x=g & (T^g) .: A = A;
      hence x in the carrier of G;
    end; then
    reconsider X as Subset of G by TARSKI:def 3;
A1: id E .: A = A by FUNCT_1:162; then
    (T^1_G) .: A = A by Def1; then
A2: 1_G in X;
A3: for g1,g2 being Element of G st g1 in X & g2 in X holds g1 * g2 in X
    proof
      let g1,g2 be Element of G;
      assume g1 in X; then
      A4: ex g being Element of G st g=g1 & (T^g) .: A = A;
      assume g2 in X; then
      A5: ex g being Element of G st g=g2 & (T^g) .: A = A;
      (T^(g1*g2)) .: A = (T^g1 * (T^g2)) .: A by Def1
      .= A by A4,A5,RELAT_1:159;
      hence g1 * g2 in X;
    end;
    for g being Element of G st g in X holds g" in X
    proof
      let g be Element of G;
      assume g in X; then
      ex g' being Element of G st g'=g & (T^g') .: A = A; then
      (T^g") .: A = (T^g" * (T^g)) .: A by RELAT_1:159
      .= (T^(g"*g)) .: A by Def1
      .= (T^1_G) .: A by GROUP_1:def 6
      .= A by A1,Def1;
      hence g" in X;
    end;
    hence thesis by A2,A3,GROUP_2:61;
  end;
  uniqueness by GROUP_2:68;
end;

definition
  let G be Group;
  let E be non empty set;
  let T be LeftOperation of G,E;
  let x be Element of E;
  func the_strict_stabilizer_of(x,T) -> strict Subgroup of G equals
  the_strict_stabilizer_of({x},T);
  correctness;
end;

definition
  let S be unital (non empty HGrStr);
  let E be set;
  let T be LeftOperation of S, E;
  let x be Element of E;
  pred x is_fixed_under T means :Def12:
  for s being Element of S holds x = (T^s).x;
end;

definition
  let S be unital (non empty HGrStr);
  let E be set;
  let T be LeftOperation of S, E;
  func the_fixed_points_of T -> Subset of E equals :Def13:
    {x where x is Element of E: x is_fixed_under T} if E is non empty
    otherwise {}E;
  correctness
  proof
    not E is empty implies
      {x where x is Element of E:x is_fixed_under T} is Subset of E
    proof
      assume A1: not E is empty;
      defpred P[set] means for x being Element of E st x=$1
        holds x is_fixed_under T;
      consider X be Subset of E such that
      A2: for x being set holds x in X iff x in E & P[x] from SUBSET_1:sch 1;
      set Y = {x where x is Element of E: x is_fixed_under T};
      now
        let y be set;
        hereby
          assume A3: y in X; then
          reconsider y'=y as Element of E;
          y' is_fixed_under T by A2,A3;
          hence y in Y;
        end;
        assume y in Y; then
        consider y' be Element of E such that
        A4: y=y' & y' is_fixed_under T;
        y in E & P[y] by A1,A4;
        hence y in X by A2;
      end;
      hence {x where x is Element of E:x is_fixed_under T} is Subset of E
        by TARSKI:2;
    end;
    hence thesis;
  end;
end;

:: ALG I.5.4 Definition 5
definition
  let S be unital (non empty HGrStr);
  let E be set;
  let T be LeftOperation of S, E;
  let x,y be Element of E;
  pred x,y are_conjugated_under T means :Def14:
  ex s being Element of S st y = (T^s).x;
end;

theorem Th4:
  for S being unital (non empty HGrStr),
      E being non empty set, x being Element of E,
      T being LeftOperation of S, E
  holds x,x are_conjugated_under T
proof
  let S be unital (non empty HGrStr);
  let E be non empty set;
  let x be Element of E;
  let T be LeftOperation of S, E;
  x = (id E).x by FUNCT_1:35
  .= (T^1_S).x by Def1;
  hence x,x are_conjugated_under T by Def14;
end;

theorem Th5:
  for G being Group,
      E being non empty set, x,y being Element of E,
      T being LeftOperation of G, E
  st x,y are_conjugated_under T holds y,x are_conjugated_under T
proof
  let G be Group;
  let E be non empty set;
  let x,y be Element of E;
  let T be LeftOperation of G, E;
  assume x,y are_conjugated_under T; then
  consider g be Element of G such that
  A1: y = (T^g).x by Def14;
  x in E; then
  A2: x in dom(T^g) by FUNCT_2:def 1;
  (T^g").y = ((T^g")*(T^g)).x by A1,A2,FUNCT_1:23
  .= (T^(g"*g)).x by Def1
  .= (T^1_G).x by GROUP_1:def 6
  .= (id E).x by Def1
  .= x by FUNCT_1:35;
  hence y,x are_conjugated_under T by Def14;
end;

theorem Th6:
  for S being unital (non empty HGrStr),
      E being non empty set, x,y,z being Element of E,
      T being LeftOperation of S, E
  st x,y are_conjugated_under T & y,z are_conjugated_under T
  holds x,z are_conjugated_under T
proof
  let S be unital (non empty HGrStr);
  let E be non empty set;
  let x,y,z be Element of E;
  let T be LeftOperation of S, E;
  assume x,y are_conjugated_under T; then
  consider s1 be Element of S such that
  A1: y = (T^s1).x by Def14;
  assume y,z are_conjugated_under T; then
  consider s2 be Element of S such that
  A2: z = (T^s2).y by Def14;
  x in E; then
  A3: x in dom(T^s1) by FUNCT_2:def 1;
  z = ((T^s2)*(T^s1)).x by A1,A2,A3,FUNCT_1:23
  .= (T^(s2*s1)).x by Def1;
  hence x,z are_conjugated_under T by Def14;
end;

definition
  let S be unital (non empty HGrStr);
  let E be non empty set;
  let T be LeftOperation of S, E;
  let x be Element of E;
  func the_orbit_of(x,T) -> Subset of E equals
  {y where y is Element of E: x,y are_conjugated_under T};
  correctness
  proof
    set Y = {y where y is Element of E: x,y are_conjugated_under T};
    defpred P[set] means for y being Element of E st y=$1
      holds x,y are_conjugated_under T;
    consider X be Subset of E such that
A1: for y being Element of E holds y in X iff P[y] from SUBSET_1:sch 3;
    now
      let y be set;
      hereby
        assume A2: y in X; then
        reconsider y'=y as Element of E;
        y'=y & x,y' are_conjugated_under T by A1,A2;
        hence y in Y;
      end;
      assume y in Y; then
      ex y' being Element of E st y'=y & x,y' are_conjugated_under T; then
      y in E & P[y];
      hence y in X by A1;
    end;
    hence thesis by TARSKI:2;
  end;
end;

theorem Th7:
  for S being unital (non empty HGrStr),
      E being non empty set, x being Element of E,
      T being LeftOperation of S, E holds
  the_orbit_of(x,T) is non empty
proof
  let S be unital (non empty HGrStr);
  let E be non empty set;
  let x be Element of E;
  let T be LeftOperation of S, E;
  x,x are_conjugated_under T by Th4; then
  x in {y where y is Element of E: x,y are_conjugated_under T};
  hence the_orbit_of(x,T) is non empty;
end;

theorem Th8:
  for G being Group,
      E being non empty set, x,y being Element of E,
      T being LeftOperation of G, E holds
  the_orbit_of(x,T) misses the_orbit_of(y,T) or
  the_orbit_of(x,T)=the_orbit_of(y,T)
proof
  let G be Group;
  let E be non empty set;
  let x,y be Element of E;
  let T be LeftOperation of G, E;
  assume not the_orbit_of(x,T) misses the_orbit_of(y,T); then
  the_orbit_of(x,T) /\ the_orbit_of(y,T) <> {} by XBOOLE_0:def 7; then
  consider z1 be set such that
A1: z1 in the_orbit_of(x,T) /\ the_orbit_of(y,T) by XBOOLE_0:def 1;
A2: z1 in the_orbit_of(x,T) & z1 in the_orbit_of(y,T) by A1,XBOOLE_0:def 3;
  consider z1' be Element of E such that
A3: z1=z1' & x,z1' are_conjugated_under T by A2;
  consider z1'' be Element of E such that
A4: z1=z1'' & y,z1'' are_conjugated_under T by A2;
  now
    let z2 be set;
    hereby
      assume z2 in the_orbit_of(x,T); then
      consider z2' be Element of E such that
      A5: z2=z2' & x,z2' are_conjugated_under T;
      z2',x are_conjugated_under T by A5,Th5; then
      z2',z1'' are_conjugated_under T by A3,A4,Th6; then
      z1'',z2' are_conjugated_under T by Th5; then
      y,z2' are_conjugated_under T by A4,Th6;
      hence z2 in the_orbit_of(y,T) by A5;
    end;
    assume z2 in the_orbit_of(y,T); then
    consider z2' be Element of E such that
    A6: z2=z2' & y,z2' are_conjugated_under T;
    z2',y are_conjugated_under T by A6,Th5; then
    z2',z1' are_conjugated_under T by A3,A4,Th6; then
    z1',z2' are_conjugated_under T by Th5; then
    x,z2' are_conjugated_under T by A3,Th6;
    hence z2 in the_orbit_of(x,T) by A6;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th9:
  for S being unital (non empty HGrStr),
      E being non empty finite set, x being Element of E,
      T being LeftOperation of S, E
  st x is_fixed_under T holds the_orbit_of(x,T) = {x}
proof
  let S be unital (non empty HGrStr);
  let E be non empty finite set;
  let x be Element of E;
  let T be LeftOperation of S, E;
  assume A1: x is_fixed_under T;
  set X=the_orbit_of(x,T);
  now X<>{} by Th7; then
    A2: ex x' being set st x' in X by XBOOLE_0:def 1;
    assume X<>{x}; then
    not (for x' being set holds x' in X iff x'=x) by TARSKI:def 1; then
    consider x'' be set such that
    A3: x''<>x & x'' in X by A2;
    consider y' be Element of E such that
    A4: x''=y' & x,y' are_conjugated_under T by A3;
    ex s being Element of S st y' = (T^s).x by A4,Def14;
    hence contradiction by A1,A3,A4,Def12;
  end;
  hence the_orbit_of(x,T) = {x};
end;

:: the orbit-stabilizer theorem
theorem Th10:
  for G being Group, E being non empty set, a being Element of E,
      T being LeftOperation of G, E holds
  Card the_orbit_of(a,T) = Index the_strict_stabilizer_of(a,T)
proof
  let G be Group;
  let E be non empty set;
  let a be Element of E;
  let T be LeftOperation of G, E;
  set H = the_strict_stabilizer_of(a,T);
  set f = {[b,A] where b is Element of E, A is Subset of G:
    ex g being Element of G st b = (T^g).a & A = g * H};
  reconsider A' = {a} as Subset of E;
A1: now let x be set;
    assume x in f; then
    consider b be Element of E, A be Subset of G such that
    A2: [b,A]=x & ex g being Element of G st b = (T^g).a & A = g * H;
    reconsider b,A as set;
    take b,A;
    thus x=[b,A] by A2;
  end;
  now let x,y1,y2 be set;
    assume [x,y1] in f; then
    consider b1 be Element of E, A1 be Subset of G such that
    A3: [b1,A1]=[x,y1] & ex g being Element of G st b1 = (T^g).a & A1 = g * H;
    assume [x,y2] in f; then
    consider b2 be Element of E, A2 be Subset of G such that
    A4: [b2,A2]=[x,y2] & ex g being Element of G st b2 = (T^g).a & A2 = g * H;
    A5: x=b1 & y1=A1 by A3,ZFMISC_1:33;
    A6: x=b2 & y2=A2 by A4,ZFMISC_1:33;
    consider g1 be Element of G such that
    A7: b1 = (T^g1).a & A1 = g1 * H by A3;
    consider g2 be Element of G such that
    A8: b2 = (T^g2).a & A2 = g2 * H by A4;
    A9: dom(T^g1)=E & dom(T^g2)=E by FUNCT_2:def 1;
    (T^g1).a = (T^g2).a by A3,A6,A7,A8,ZFMISC_1:33; then
    Im(T^g1,a) = {(T^g2).a} by A9,FUNCT_1:117; then
    A10: Im(T^g1,a) = Im(T^g2,a) by A9,FUNCT_1:117;
    set g = g2"*g1;
    reconsider g as Element of G;
    A11: the carrier of the_strict_stabilizer_of(A',T) =
    {g' where g' is Element of G: (T^g') .: A' = A'} by Def10;
    (T^g) .: A' = (T^g2" * (T^g1)) .: A' by Def1
    .= (T^g2").:((T^g1) .: A') by RELAT_1:159
    .= (T^g2" *(T^g2)) .: A' by A10,RELAT_1:159
    .= (T^(g2"*g2)) .: A' by Def1
    .= (T^1_G) .: A' by GROUP_1:def 6
    .= (id E) .: A' by Def1
    .= A' by FUNCT_1:162; then
    g in {g' where g' is Element of G: (T^g') .: A' = A'}; then
    g in the_strict_stabilizer_of(A',T) by A11,STRUCT_0:def 5;
    hence y1=y2 by A5,A6,A7,A8,GROUP_2:137;
  end; then
  reconsider f as Function by A1,FUNCT_1:def 1,RELAT_1:def 1;
  now
    let x1,x2 be set;
    assume x1 in dom f; then
    [x1,f.x1] in f by FUNCT_1:8; then
    consider b1 be Element of E, A1 be Subset of G such that
A12: [b1,A1]=[x1,f.x1] & ex g being Element of G st b1 = (T^g).a & A1=g * H;
    assume x2 in dom f; then
    [x2,f.x2] in f by FUNCT_1:8; then
    consider b2 be Element of E, A2 be Subset of G such that
A13: [b2,A2]=[x2,f.x2] & ex g being Element of G st b2 = (T^g).a & A2=g * H;
    A14: x1=b1 & f.x1=A1 by A12,ZFMISC_1:33;
    A15: x2=b2 & f.x2=A2 by A13,ZFMISC_1:33;
    consider g1 be Element of G such that
    A16: b1 = (T^g1).a & A1 = g1 * H by A12;
    consider g2 be Element of G such that
    A17: b2 = (T^g2).a & A2 = g2 * H by A13;
    A18: the carrier of the_strict_stabilizer_of(A',T) =
    {g' where g' is Element of G: (T^g') .: A' = A'} by Def10;
    set g = g2"*g1;
    assume f.x1 = f.x2; then
    g1 * H = g2 * H by A12,A15,A16,A17,ZFMISC_1:33; then
    g in H by GROUP_2:137; then
    g in the carrier of the_strict_stabilizer_of(A',T) by STRUCT_0:def 5; then
    A19: ex g' being Element of G st g=g' & (T^g') .: A' = A' by A18;
    A20: dom(T^g1)=E & dom(T^g2)=E by FUNCT_2:def 1;
    (T^g1).:A' = (id E).:((T^g1).:A') by FUNCT_1:162
    .= (T^1_G).:((T^g1).:A') by Def1
    .= (T^(g2*g2")).:((T^g1).:A') by GROUP_1:def 6
    .= (T^(g2*g2")*(T^g1)).:A' by RELAT_1:159
    .= (T^((g2*g2")*g1)).:A' by Def1
    .= (T^(g2*g)).:A' by GROUP_1:def 4
    .= ((T^g2)*(T^g)).:A' by Def1
    .= (T^g2).:A' by A19,RELAT_1:159; then
    Im(T^g1,a) = Im(T^g2,a); then
    Im(T^g1,a) = {(T^g2).a} by A20,FUNCT_1:117; then
    {(T^g1).a} = {(T^g2).a} by A20,FUNCT_1:117;
    hence x1 = x2 by A14,A15,A16,A17,ZFMISC_1:24;
  end; then
A21: f is one-to-one by FUNCT_1:def 8;
  for x being set holds x in the_orbit_of(a,T) iff ex y being set st [x,y] in f
  proof
    let x be set;
    hereby
      assume x in the_orbit_of(a,T); then
      consider b be Element of E such that
      A22: b=x & a,b are_conjugated_under T;
      consider g be Element of G such that
      A23: b = (T^g).a by A22,Def14;
      reconsider y=g * H as set;
      take y;
      thus [x,y] in f by A22,A23;
    end;
    given y be set such that
    A24: [x,y] in f;
    consider b be Element of E, A be Subset of G such that
    A25: [b,A]=[x,y] & ex g being Element of G st b = (T^g).a & A = g * H
      by A24;
    A26: b=x & A=y by A25,ZFMISC_1:33;
    a,b are_conjugated_under T by A25,Def14;
    hence x in the_orbit_of(a,T) by A26;
  end; then
A27: dom f = the_orbit_of(a,T) by RELAT_1:def 4;
  for y being set holds y in Left_Cosets H iff ex x being set st [x,y] in f
  proof
    let y be set;
    hereby
      assume A28: y in Left_Cosets H; then
      reconsider A=y as Subset of G;
      consider g be Element of G such that
      A29: A = g * H by A28,GROUP_2:def 15;
      reconsider x=(T^g).a as set;
      take x;
      thus [x,y] in f by A29;
    end;
    given x be set such that
    A30: [x,y] in f;
    consider b be Element of E, A be Subset of G such that
    A31: [b,A]=[x,y] & ex g being Element of G st b = (T^g).a & A = g * H
      by A30;
    b=x & A=y by A31,ZFMISC_1:33;
    hence y in Left_Cosets H by A31,GROUP_2:def 15;
  end; then
  rng f = Left_Cosets H by RELAT_1:def 5; then
  the_orbit_of(a,T), Left_Cosets H are_equipotent by A21,A27,WELLORD2:def 4;
  hence thesis by CARD_1:21;
end;

definition
  let G be Group;
  let E be non empty set;
  let T be LeftOperation of G, E;
  func the_orbits_of T -> a_partition of E equals
  {X where X is Subset of E: ex x being Element of E st X = the_orbit_of(x,T)};
  correctness
  proof
    set P = {X where X is Subset of E:
      ex x being Element of E st X = the_orbit_of(x,T)};
    now let x be set;
      assume x in P; then
      ex X being Subset of E st x=X &
        ex x being Element of E st X = the_orbit_of(x,T);
      hence x in bool E;
    end; then
    reconsider P as Subset-Family of E by TARSKI:def 3;
    for x being set holds x in E iff ex Y being set st x in Y & Y in P
    proof
      let x be set;
      hereby
        assume x in E; then
        reconsider x'=x as Element of E;
        reconsider Y = the_orbit_of(x',T) as set;
        take Y;
        x',x' are_conjugated_under T by Th4;
        hence x in Y;
        thus Y in P;
      end;
      given Y be set such that
      A1: x in Y & Y in P;
      thus x in E by A1;
    end; then
A2: union P = E by TARSKI:def 4;
    for A being Subset of E st A in P holds A<>{} &
      for B being Subset of E st B in P holds A = B or A misses B
    proof
      let A be Subset of E;
      assume A in P; then
      A3: ex X being Subset of E st A=X &
        ex x' being Element of E st X = the_orbit_of(x',T);
      hence A<>{} by Th7;
      let B be Subset of E;
      assume B in P; then
      A4: ex X being Subset of E st B=X &
        ex x' being Element of E st X = the_orbit_of(x',T);
      assume not A = B;
      hence A misses B by A3,A4,Th8;
    end;
    hence thesis by A2,EQREL_1:def 6;
  end;
end;

begin :: p-groups

:: ALG I.6.5 Definition 9
definition
  let p be prime (natural number);
  let G be Group;
  pred G is_p-group_of_prime p means :Def17:
  ex r being natural number st ord G = p |^ r;
end;

definition
  let p be prime (natural number);
  let G be Group;
  let P be Subgroup of G;
  pred P is_p-group_of_prime p means :Def18:
  ex H being finite Group st P = H & H is_p-group_of_prime p;
end;

:: like WEDDWITT:39
:: ALG I.6.5 PRO 11
theorem Th11:
  for E being non empty finite set, G being finite Group,
      p being prime (natural number), T being LeftOperation of G, E
  st G is_p-group_of_prime p
  holds card the_fixed_points_of T mod p = card E mod p
proof
  let E be non empty finite set;
  let G be finite Group;
  let p be prime (natural number);
  let T be LeftOperation of G, E;
  assume A1: G is_p-group_of_prime p;
  set O1 = {X where X is Subset of E: card X = 1 &
    ex x being Element of E st X = the_orbit_of(x,T)};
  set O2 = {X where X is Subset of E: card X > 1 &
    ex x being Element of E st X = the_orbit_of(x,T)};
  set O = O1 \/ O2;
  A2: now
    let y be set;
    hereby
      assume y in the_orbits_of T; then
      consider X be Subset of E such that
      A3: y=X & ex x being Element of E st X = the_orbit_of(x,T);
      X is non empty by A3,Th7; then
      card X <> 0 by CARD_2:59; then
      0 < card X; then
      0+1 < card X+1 by XREAL_1:8; then
      A4: 1 <= card X by NAT_1:13;
      per cases by A4,XXREAL_0:1;
      suppose 1=card X; then
        y in O1 by A3;
        hence y in O1 \/ O2 by XBOOLE_0:def 2;
      end;
      suppose 1<card X; then
        y in O2 by A3;
        hence y in O1 \/ O2 by XBOOLE_0:def 2;
      end;
    end;
    assume A5: y in O1 \/ O2;
    per cases by A5,XBOOLE_0:def 2;
    suppose y in O1; then
      ex X being Subset of E st y=X & card X = 1 &
        ex x being Element of E st X = the_orbit_of(x,T);
      hence y in the_orbits_of T;
    end;
    suppose y in O2; then
      ex X being Subset of E st y=X & card X > 1 &
        ex x being Element of E st X = the_orbit_of(x,T);
      hence y in the_orbits_of T;
    end;
  end; then
A6: O = the_orbits_of T by TARSKI:2;
  reconsider O as a_partition of E by A2,TARSKI:2;
  O1 c= the_orbits_of T by A6,XBOOLE_1:7; then
  O1 is finite by FINSET_1:13; then
  consider f1 be FinSequence such that
A7: rng f1 = O1 & f1 is one-to-one by FINSEQ_4:73;
  O2 c= the_orbits_of T by A6,XBOOLE_1:7; then
  O2 is finite by FINSET_1:13; then
  consider f2 be FinSequence such that
A8: rng f2 = O2 & f2 is one-to-one by FINSEQ_4:73;
  set f = f1 ^ f2;
  now given x be set such that
    A9: x in O1 /\ O2;
    x in O1 & x in O2 by A9,XBOOLE_0:def 3; then
    (ex X being Subset of E st x=X & card X=1 &
      ex x' being Element of E st X=the_orbit_of(x',T)) &
    (ex X being Subset of E st x=X & card X>1 &
      ex x' being Element of E st X=the_orbit_of(x',T));
    hence contradiction;
  end; then
  O1 /\ O2 = {} by XBOOLE_0:def 1; then
  rng f1 misses rng f2 by A7,A8,XBOOLE_0:def 7; then
A10: f is one-to-one by A7,A8,FINSEQ_3:98;
A11: rng f = O by A7,A8,FINSEQ_1:44; then
  reconsider f as FinSequence of O by FINSEQ_1:def 4;
  deffunc F(set) = Card(f1.$1);
  consider p1 being FinSequence such that
A12: len p1 = len f1 & for i being Nat
    st i in Seg len f1 holds p1.i = F(i) from FINSEQ_1:sch 2;
A13: dom p1 = Seg len f1 by A12,FINSEQ_1:def 3;
  for x being set st x in rng p1 holds x in NAT
  proof
    let x be set;
    assume A14: x in rng p1;
    consider i be Nat such that
    A15: i in dom p1 & p1.i = x by A14,FINSEQ_2:11;
    A16: x = Card(f1.i) by A12,A13,A15;
    i in dom f1 by A12,A15,FINSEQ_3:31; then
    f1.i in O1 by A7,FUNCT_1:12; then
    ex X being Subset of E st f1.i=X & card X = 1 &
      ex x being Element of E st X=the_orbit_of(x,T);
    hence thesis by A16;
  end; then
  rng p1 c= NAT by TARSKI:def 3; then
  reconsider c1=p1 as FinSequence of NAT by FINSEQ_1:def 4;
A17: len c1 = card(rng f1) by A7,A12,FINSEQ_4:77;
A18: for i being Element of NAT st i in dom c1 holds c1.i = 1
  proof
    let i be Element of NAT;
    assume A19: i in dom c1;
    i in dom f1 by A12,A19,FINSEQ_3:31; then
    f1.i in O1 by A7,FUNCT_1:12; then
    ex X being Subset of E st f1.i = X & card X = 1 &
      ex x being Element of E st X=the_orbit_of(x,T);
    hence thesis by A12,A13,A19;
  end;
  for x being set st x in rng c1 holds x in {1}
  proof
    let x be set; assume x in rng c1; then
    ex i being Nat st i in dom c1 & x = c1.i by FINSEQ_2:11; then
    x = 1 by A18;
    hence thesis by TARSKI:def 1;
  end; then
  A20: rng c1 c= {1} by TARSKI:def 3;
A21: Sum c1 = len c1
  proof
    per cases;
    suppose len c1=0; hence thesis by FINSEQ_1:25,RVSUM_1:102; end;
    suppose A22: len c1<>0;
      for x being set st x in {1} holds x in rng c1
      proof
        let x be set such that
        A23: x in {1};
        Seg len c1 = dom c1 by FINSEQ_1:def 3; then
        A24: len c1 in dom c1 by A22,FINSEQ_1:5; then
        c1.(len c1) = 1 by A18; then c1.(len c1) = x by A23,TARSKI:def 1;
        hence thesis by A24,FUNCT_1:12;
      end; then
      {1}c= rng c1 by TARSKI:def 3; then
      rng c1={1} by A20,XBOOLE_0:def 10; then
      c1 = (dom c1) --> 1 by FUNCOP_1:15; then
      c1 = Seg (len c1) --> 1 by FINSEQ_1:def 3; then
      c1 = (len c1) |-> 1 by FINSEQ_2:def 2; then
      Sum c1 = (len c1)*1 by RVSUM_1:110;
      hence thesis;
    end;
  end;
  defpred P[set,set] means
    ex x being Element of E, X being Subset of E st $1=x & $2=X &
    X = the_orbit_of(x,T) & x is_fixed_under T;
A25: for x,y1,y2 being set st x in the_fixed_points_of T & P[x,y1] & P[x,y2]
     holds y1 = y2;
A26: for x being set st x in the_fixed_points_of T ex y being set st P[x,y]
  proof
    let x be set;
    assume A27: x in the_fixed_points_of T; then
    reconsider x' = x as Element of E;
    reconsider y = the_orbit_of(x',T) as set;
    take y;
    now
      set x=x'; set X=y;
      take x,X;
      thus x'=x & y=X & X = the_orbit_of(x,T);
      the_fixed_points_of T =
        {x'' where x'' is Element of E:x'' is_fixed_under T} by Def13; then
      ex e being Element of E st x=e & e is_fixed_under T by A27;
      hence x is_fixed_under T;
    end;
    hence P[x,y];
  end;
  consider FX be Function such that
A28: dom FX = the_fixed_points_of T &
    for x being set st x in the_fixed_points_of T
    holds P[x,FX.x] from FUNCT_1:sch 2(A25,A26);
  now
    let y be set;
    hereby
      assume y in O1; then
      consider X be Subset of E such that
      A29: y=X & card X = 1 & ex x being Element of E st X = the_orbit_of(x,T);
      consider x' be Element of E such that
      A30: X = the_orbit_of(x',T) by A29;
      reconsider x=x' as set;
      take x;
      Card X = Card {x} by A29,CARD_1:50; then
      consider x'' be set such that
      A31: X = {x''} by CARD_1:49;
      now given g be Element of G such that
        A32: x' <> (T^g).x';
        set y' = (T^g).x';
        x',y' are_conjugated_under T by Def14; then
        y' in {y'' where y'' is Element of E: x',y'' are_conjugated_under T};
        then A33: y'=x'' by A30,A31,TARSKI:def 1;
        x',x' are_conjugated_under T by Th4; then
        x' in {y'' where y'' is Element of E: x',y'' are_conjugated_under T};
        hence contradiction by A30,A31,A32,A33,TARSKI:def 1;
      end; then
      x' is_fixed_under T by Def12; then
      x in {x''' where x''' is Element of E:x''' is_fixed_under T}; then
      A34: x in the_fixed_points_of T by Def13; then
      ex x'' being Element of E, X' being Subset of E st x''=x & X'=FX.x &
      X' = the_orbit_of(x'',T) & x'' is_fixed_under T by A28;
      hence [x,y] in FX by A28,A29,A30,A34,FUNCT_1:8;
    end;
    given x be set such that
    A35: [x,y] in FX;
    x in dom FX & y = FX.x by A35,FUNCT_1:8; then
    consider x' be Element of E, X' be Subset of E such that
    A36: x'=x & X'=y & X' = the_orbit_of(x',T) & x' is_fixed_under T by A28;
    X' = {x'} by A36,Th9; then
    card X' = 1 by CARD_1:79;
    hence y in O1 by A36;
  end; then
A37: rng FX = O1 by RELAT_1:def 5;
  now
    let x1,x2 be set;
    assume x1 in dom FX; then
    consider x1' be Element of E, X1 be Subset of E such that
    A38: x1'=x1 & X1=FX.x1 & X1 = the_orbit_of(x1',T) & x1' is_fixed_under T
      by A28;
    assume x2 in dom FX; then
    consider x2' be Element of E, X2 be Subset of E such that
    A39: x2'=x2 & X2=FX.x2 & X2 = the_orbit_of(x2',T) & x2' is_fixed_under T
      by A28;
    assume FX.x1 = FX.x2; then
    {x1'} = the_orbit_of(x2',T) by A38,A39,Th9
    .= {x2'} by A39,Th9;
    hence x1 = x2 by A38,A39,ZFMISC_1:6;
  end; then
  FX is one-to-one by FUNCT_1:def 8; then
A40: the_fixed_points_of T,O1 are_equipotent by A28,A37,WELLORD2:def 4;
  deffunc P2(set) = Card(f2.$1);
  consider p2 being FinSequence such that
  A41: len p2 = len f2 & for i being Nat
    st i in Seg len f2 holds p2.i = P2(i) from FINSEQ_1:sch 2;
  A42: dom p2 = Seg len f2 by A41,FINSEQ_1:def 3;
  for x being set st x in rng p2 holds x in NAT
  proof
    let x be set;
    assume x in rng p2; then
    consider i being Nat such that
    A43: i in dom p2 & p2.i = x by FINSEQ_2:11;
    A44: x = Card(f2.i) by A41,A42,A43;
    i in dom f2 by A41,A43,FINSEQ_3:31; then
    f2.i in O2 by A8,FUNCT_1:12; then
    ex X being Subset of E st f2.i=X & card X > 1 &
      ex x being Element of E st X=the_orbit_of(x,T);
    hence thesis by A44;
  end; then rng p2 c= NAT by TARSKI:def 3;
  then reconsider c2=p2 as FinSequence of NAT by FINSEQ_1:def 4;
  set c = c1^c2;
  reconsider c as FinSequence of NAT;
  len c = len f1 + len f2 by A12,A41,FINSEQ_1:35; then
  A45: len c = len f by FINSEQ_1:35;
  for i being Element of NAT st i in dom c holds c.i = Card (f.i)
  proof
    let i be Element of NAT such that
    A46: i in dom c;
    now per cases by A46,FINSEQ_1:38;
      suppose A47: i in dom c1;
        then A48: i in dom f1 by A12,FINSEQ_3:31;
        c.i = c1.i by A47,FINSEQ_1:def 7 .= Card(f1.i) by A12,A13,A47
        .= Card(f.i) by A48,FINSEQ_1:def 7;
        hence thesis;
      end;
      suppose ex j being Nat st j in dom c2 & i = len c1 + j;
        then consider j being Nat such that
        A49: j in dom c2 and A50: i = len c1 + j;
        A51: j in dom f2 by A41,A49,FINSEQ_3:31;
        c.i = c2.j by A49,A50,FINSEQ_1:def 7 .= Card(f2.j) by A41,A42,A49
        .= Card(f.i) by A12,A50,A51,FINSEQ_1:def 7;
        hence thesis;
      end;
    end;
    hence thesis;
  end; then
A52: card E = Sum c by A10,A11,A45,WEDDWITT:6
  .= Sum c1 + Sum c2 by RVSUM_1:105;
  reconsider p'=p as Element of NAT by ORDINAL1:def 13;
  for i being Element of NAT st i in dom c2 holds p' divides c2/.i
  proof
    let i be Element of NAT;
    assume A53: i in dom c2;
    c2.i = Card (f2.i) by A41,A42,A53; then
    A54: c2/.i = Card (f2.i) by A53,PARTFUN1:def 8;
    i in dom f2 by A41,A53,FINSEQ_3:31; then
    f2.i in O2 by A8,FUNCT_1:12; then
    consider X be Subset of E such that
    A55: f2.i=X & card X > 1 & ex x being Element of E st X=the_orbit_of(x,T);
    consider x be Element of E such that
    A56: X = the_orbit_of(x,T) by A55;
    set H = the_strict_stabilizer_of(x,T);
    A57: Card the_orbit_of(x,T) = Index H by Th10;
    A58: c2/.i = Index H by A54,A55,A56,Th10;
    A59: ex B being finite set st B = Left_Cosets H & c2/.i = card B
    proof
      reconsider B=Left_Cosets H as finite set by GROUP_2:164;
      take B;
      thus B = Left_Cosets H;
      thus c2/.i = card B by A58;
    end; then
    c2/.i = index H by GROUP_2:def 18; then
    A60: ord G = ord H * c2/.i by GROUP_2:177;
    consider r be natural number such that
    A61: ord G = p |^ r by A1,Def17;
    A62: ord G <> 0 by GROUP_1:90;
    A63: now assume ord H = ord G; then
      ord G = ord G * index H by GROUP_2:177; then
      1 = ord G * index H / ord G by A62,XCMPLX_1:60; then
      1 = (ord G / ord G) * index H by XCMPLX_1:75; then
      A64: 1 * index H = 1 by A62,XCMPLX_1:60;
      consider B be finite set such that
      A65: B = Left_Cosets H & c2/.i = card B by A59;
      thus contradiction by A55,A56,A57,A64,A65,GROUP_2:def 18;
    end;
    set i'=c2/.i;
    reconsider r'=r as Element of NAT by ORDINAL1:def 13;
    A66: i' divides p'|^r' by A60,A61,NAT_D:def 3;
    per cases by A66,PEPIN:33;
    suppose i' = 1; hence thesis by A60,A63;
    end;
    suppose ex k being Element of NAT st i' = p * k;
      hence thesis by NAT_D:def 3;
    end;
  end; then
  p divides Sum c2 by WEDDWITT:5; then
  ex t being Nat st Sum c2 = p * t by NAT_D:def 3; then
A67: Sum c2 mod p = 0 by NAT_D:13;
  card E mod p = (Sum c1 +(Sum c2 mod p)) mod p by A52,NAT_D:23
  .= Sum c1 mod p by A67;
  hence thesis by A7,A17,A21,A40,CARD_1:21;
end;

begin :: The Sylow Theorems

:: ALG I.6.6 Definition 10
definition
  let p be prime (natural number);
  let G be Group;
  let P be Subgroup of G;
  pred P is_Sylow_p-subgroup_of_prime p means :Def19:
  P is_p-group_of_prime p & not p divides index P;
end;

Lm1:
  for n being natural number, p being prime (natural number) st n<>0
  holds ex m,r being natural number st n = p |^ r * m & not p divides m
proof
  let n be natural number;
  let p be prime (natural number);
  assume A1: n <> 0;
  set r = p |-count n;
  p <> 1 by NAT_4:12; then
A2: p |^ r divides n & not p |^ (r+1) divides n by A1,NAT_3:def 7; then
  consider m be natural number such that
A3: n = p |^ r * m by NAT_D:def 3;
  take m,r;
  thus n = p |^ r * m by A3;
  thus not p divides m
  proof
    assume p divides m; then
    consider t be natural number such that
    A4: m = p * t by NAT_D:def 3;
    n = p |^ r * p * t by A3,A4
    .= p |^(r+1) * t by NEWTON:11;
    hence contradiction by A2,NAT_D:def 3;
  end;
end;

Lm2:
  for n being natural number st n > 0 holds ord INT.Group(n) = n
proof
  let n be natural number;
  assume A1: n > 0;
  set G = INT.Group(n);
A2: HGrStr(#Segm(n),addint(n)#) = G by A1,GR_CY_1:def 6;
  reconsider k = n as Element of NAT by ORDINAL1:def 13;
A3: Segm k is finite; then
  reconsider G as finite Group by A2,STRUCT_0:def 11;
  now
    reconsider B = Segm n as finite set by A3;
    take B;
    thus B = the carrier of G by A2;
    Card B = Card k by CARD_1:def 12;
    hence n = card B by CARD_1:66;
  end;
  hence ord INT.Group(n) = n by GROUP_1:def 15;
end;

Lm3:
  for X,Y being non empty set holds
  Card {[:X,{y}:] where y is Element of Y: not contradiction} = Card Y
proof
  let X,Y be non empty set;
  set Z = {[:X,{y}:] where y is Element of Y: not contradiction};
  deffunc F(set) = [: X, {$1} :];
  ex f being Function st dom f = Y &
    for x being set st x in Y holds f.x = F(x) from FUNCT_1:sch 3; then
  consider f be Function such that
  A1: dom f = Y & for x being set st x in Y holds f.x = F(x);
  for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1 = f.x2 holds x1 = x2
  proof
    let x1,x2 be set;
    assume x1 in dom f & x2 in dom f; then
    A2: f.x1 = F(x1) & f.x2 = F(x2) by A1;
    assume f.x1 = f.x2; then
    {x1} = {x2} by A2,ZFMISC_1:134;
    hence x1 = x2 by ZFMISC_1:6;
  end; then
  A3: f is one-to-one by FUNCT_1:def 8;
  for y being set holds y in Z iff ex x being set st x in dom f & y = f.x
  proof
    let y be set;
    hereby
      assume y in Z; then
      consider y' be Element of Y such that
      A4: y = [:X,{y'}:];
      reconsider x=y' as set;
      take x;
      thus x in dom f by A1;
      thus y = f.x by A1,A4;
    end;
    given x be set such that
    A5: x in dom f & y = f.x;
    f.x = [:X,{x}:] by A1,A5;
    hence y in Z by A1,A5;
  end; then
  rng f = Z by FUNCT_1:def 5; then
  Z,Y are_equipotent by A1,A3,WELLORD2:def 4;
  hence thesis by CARD_1:21;
end;

Lm4:
  for G being finite Group, E,T being non empty set,
      LO being LeftOperation of G,E,
      n being natural number
  st LO = the_left_operation_of(G,T) & n = ord G &
     E = [:the carrier of G,T:] &
     Card n <=` Card E
  holds
  the_fixed_points_of(the_extension_of_left_operation_of(n,LO)) =
    {[:the carrier of G,{t}:] where t is Element of T: not contradiction}
proof
  let G be finite Group;
  let E,T be non empty set;
  let LO be LeftOperation of G,E;
  let n be natural number;
  assume A1: LO = the_left_operation_of(G,T);
  assume A2: n = ord G;
  assume A3: E = [:the carrier of G,T:];
  assume A4: Card n <=` Card E;
  set LO' = the_extension_of_left_operation_of(n,LO);
  set CG = the carrier of G;
  set EE = the_subsets_of_card(n, E);
  now
    let z be set;
    hereby
      assume z in the_fixed_points_of LO'; then
      z in {x where x is Element of EE: x is_fixed_under LO'} by Def13; then
      consider e be Element of EE such that
      A5: z=e & e is_fixed_under LO';
      A6: for g being Element of G holds e = the_left_translation_of(g,T) .: e
      proof
        let g be Element of G;
        LO'.g = the_extension_of_left_translation_of(n,g,LO) by A4,Def5;
        then
        A7: (LO'^g).e = (LO^g) .: e by A4,Def4;
        e = (LO'^g).e by A5,Def12;
        hence thesis by A1,A7,Def7;
      end;
      EE is non empty by A4,Th2; then
      z in EE by A5; then
      consider X be Subset of E such that
      A8: z=X & Card X = n;
      ex t being Element of T st [:the carrier of G,{t}:] c= z
      proof
        X is not empty by A2,A8,CARD_1:47,GROUP_1:90; then
        consider x1 be set such that
        A9: x1 in X by XBOOLE_0:def 1;
        consider g1,t be set such that
        A10: g1 in the carrier of G & t in T & x1=[g1,t]
          by A3,A9,ZFMISC_1:def 2;
        reconsider g1 as Element of G by A10;
        reconsider t as Element of T by A10;
        take t;
        now let x2 be set;
          assume x2 in [:the carrier of G,{t}:]; then
          consider g2,t' be set such that
          A11: g2 in the carrier of G & t' in {t} & x2=[g2,t']
            by ZFMISC_1:def 2;
          reconsider g2 as Element of G by A11;
          set g=g2*g1";
          A12: dom the_left_translation_of(g,T) = E by A3,FUNCT_2:def 1;
          reconsider z1'=x1 as Element of [: the carrier of G,T :] by A3,A9;
          consider z2' be Element of [: the carrier of G,T :],
            g1', g2' be Element of G, t'' be Element of T such that
          A13: z2'=the_left_translation_of(g,T).z1' &
            g2' = g * g1' & z1' = [g1',t''] & z2' = [g2',t''] by Def6;
          A14: g1=g1' & t=t'' by A10,A13,ZFMISC_1:33;
          g2'=g2*g1"*g1 by A10,A13,ZFMISC_1:33
          .= g2*(g1"*g1) by GROUP_1:def 4
          .= g2*1_G by GROUP_1:def 6 .= g2 by GROUP_1:def 5; then
          A15: z2' = x2 by A11,A13,A14,TARSKI:def 1;
          the_left_translation_of(g,T).x1 in the_left_translation_of(g,T) .: e
            by A5,A8,A9,A12,FUNCT_1:def 12;
          hence x2 in z by A5,A6,A13,A15;
        end;
        hence [:the carrier of G,{t}:] c= z by TARSKI:def 3;
      end; then
      consider t be Element of T such that
      A16: [:the carrier of G,{t}:] c= z;
      A17: card [:the carrier of G,{t}:] = card the carrier of G by CARD_2:13
      .= n by A2,GROUP_1:def 15;
      n,X are_equipotent by A8,CARD_1:def 5; then
      consider f be Function such that
      A18: f is one-to-one & dom f = n & rng f = X by WELLORD2:def 4;
      dom f in omega by A18,ORDINAL1:def 13; then
      reconsider X as finite set by A18,FINSET_1:def 1;
      [:the carrier of G,{t}:] = X by A8,A16,A17,CARD_FIN:1;
      hence z in {[:the carrier of G,{t'}:] where t' is Element of T:
        not contradiction} by A8;
    end;
    assume z in {[:the carrier of G,{t}:] where t is Element of T:
      not contradiction}; then
    consider  t be Element of T such that
    A19: z = [:the carrier of G,{t}:];
    A20: now let z' be set;
      assume z' in z; then
      ex x,y being set st x in the carrier of G & y in {t} & z'=[x,y]
        by A19,ZFMISC_1:def 2;
      hence z' in [:the carrier of G,T:] by ZFMISC_1:def 2;
    end; then
    A21: z c= [:the carrier of G,T:] by TARSKI:def 3;
    reconsider X=z as Subset of E by A3,A20,TARSKI:def 3;
    A22: Card X = Card the carrier of G by A19,CARD_2:13;
    A23: ex B being finite set st B = the carrier of G & n = card B
      by A2,GROUP_1:def 15;
    z in EE by A22,A23; then
    reconsider e=z as Element of EE;
    A24: for g being Element of G holds e = (LO^g) .: e
    proof
      let g be Element of G;
      A25: LO^g = the_left_translation_of(g,T) by A1,Def7;
      now
        let e2 be set;
        hereby
          assume e2 in e; then
          consider g2,t2 be set such that
          A26: g2 in the carrier of G & t2 in {t} & e2=[g2,t2]
            by A19,ZFMISC_1:def 2;
          reconsider g2 as Element of G by A26;
          reconsider e1=[g"*g2,t2] as set;
          take e1;
          A27: e1 in e by A19,A26,ZFMISC_1:def 2;
          z c= dom(LO^g) by A3,A21,FUNCT_2:def 1;
          hence e1 in dom(LO^g) & e1 in e by A27; then
          reconsider z1=e1 as Element of [:the carrier of G,T:] by A3;
          consider z2 be Element of [:the carrier of G,T:],
            g1',g2' be Element of G, t' be Element of T such that
          A28: z2=(LO^g).z1 & g2'=g * g1' & z1=[g1',t'] & z2=[g2',t']
            by A25,Def6;
          A29: g2' = g*(g"*g2) by A28,ZFMISC_1:33
          .= g*g"*g2 by GROUP_1:def 4
          .= 1_G*g2 by GROUP_1:def 6
          .= g2 by GROUP_1:def 5;
          thus e2 = (LO^g).e1 by A26,A28,A29,ZFMISC_1:33;
        end;
        given e1 be set such that
        A30: e1 in dom (LO^g) & e1 in e & e2 = (LO^g).e1;
        consider g1,t1 be set such that
        A31: g1 in the carrier of G & t1 in {t} & e1=[g1,t1]
            by A19,A30,ZFMISC_1:def 2;
        reconsider z1=e1 as Element of [:the carrier of G,T:] by A3,A30;
        consider z2 be Element of [:the carrier of G,T:],
          g1',g2' be Element of G, t' be Element of T such that
        A32: z2=(LO^g).z1 & g2'=g * g1' & z1=[g1',t'] & z2=[g2',t']
          by A25,Def6;
        A33: t1 = t by A31,TARSKI:def 1;
        A34: t'=t by A31,A32,A33,ZFMISC_1:33;
        t' in {t'} by TARSKI:def 1;
        hence e2 in e by A19,A30,A32,A34,ZFMISC_1:def 2;
      end;
      hence e = (LO^g) .: e by FUNCT_1:def 12;
    end;
    for g being Element of G holds e = (LO'^g).e
    proof
      let g be Element of G;
      LO'.g = the_extension_of_left_translation_of(n,g,LO) by A4,Def5;
      then
      A35: (LO'^g).e = (LO^g) .: e by A4,Def4;
      consider Y be Element of EE such that
      A36: [Y,(LO^g).:Y] = [e,(LO'^g).e] by A35;
      Y=e & (LO^g).:Y=(LO'^g).e by A36,ZFMISC_1:33;
      hence e = (LO'^g).e by A24;
    end; then
    A37: e is_fixed_under LO' by Def12;
    A38: EE is non empty by A4,Th2;
    z in {x where x is Element of EE: x is_fixed_under LO'} by A37;
    hence z in the_fixed_points_of LO' by A38,Def13;
  end;
  hence thesis by TARSKI:2;
end;

:: ALG I.6.6 Lemma
Lm5:
  for n,m,r being natural number, p being prime (natural number)
  st n = p |^ r * m & not p divides m
  holds (n choose p|^r) mod p <> 0
proof
  let n,m,r be natural number;
  let p be prime (natural number);
  assume A1: n = p |^ r * m;
  assume A2: not p divides m;
  reconsider p' = p as prime Element of NAT by ORDINAL1:def 13;
  reconsider k1 = p'|^r as Element of NAT by ORDINAL1:def 13;
  set S = INT.Group(k1);
A3: HGrStr(#Segm(k1),addint(k1)#) = S by GR_CY_1:def 6;
A4: ord S = k1 by Lm2;
  reconsider CS = the carrier of S as finite set by A3;
  reconsider S as finite Group by A3,STRUCT_0:def 11;
  Card CS = Card k1 by A3,CARD_1:def 12; then
A5: card CS = p|^r by CARD_1:66;
  reconsider k2 = m as Element of NAT by ORDINAL1:def 13;
  set T = Segm k2;
  Card T = Card k2 by CARD_1:def 12; then
A6: card T = m by CARD_1:66;
  reconsider x1 = 1,x2 = 0 as set;
A7: k2<>0 by A2,NAT_D:6;
  reconsider T as non empty finite set by A7,CARD_1:def 12;
  set X = [: CS, T :];
A8: card X = n by A1,A5,A6,CARD_2:65;
A9: x1<>x2 & X is non empty set by ZFMISC_1:113;
  reconsider X'=X as non empty finite set by ZFMISC_1:113;
A10: m > 0 by A7;
  reconsider n'=n as real number;
A11: n <> 0 by A8,A9,CARD_2:59;
  now assume p |^ r > n; then
    p |^ r * m > n * m by A10,XREAL_1:70; then
    (n' / n') > n' * m / n' by A1,XREAL_1:76; then
    1 > (n' * m / n') by A11,XCMPLX_1:60; then
    1 > (n' / n') * m by XCMPLX_1:75; then
    1 > 1 * m by A11,XCMPLX_1:60; then
    m < 0 + 1;
    hence contradiction by A10,NAT_1:13;
  end; then
  Card k1 <=` Card card X by A8,CARD_1:72; then
A12: Card k1 <=` Card X';
  set E = the_subsets_of_card(k1,X');
  set LO = the_left_operation_of(S,T);
  reconsider E'=E as non empty finite set by A12,Th2;
A13: card Choose(X,k1,x1,x2) = card E' by Th3;
A14: card E' = n choose k1 by A8,A13,CARD_FIN:18;
  set LO' = the_extension_of_left_operation_of(k1,LO);
  Card the_fixed_points_of LO' =
    Card {[:CS,{t}:] where t is Element of T: not contradiction}
    by A4,A12,Lm4
  .= Card T by Lm3
  .= Card k2 by CARD_1:def 12; then
A15: card the_fixed_points_of LO' = m by CARD_1:66;
A16:  S is_p-group_of_prime p by A4,Def17;
  now assume m mod p' = 0; then
    m = p * (m div p) + 0 by NAT_D:2;
    hence contradiction by A2,NAT_D:def 3;
  end;
  hence (n choose p|^r) mod p <> 0 by A14,A15,A16,Th11;
end;

Lm6:
  for r,n,m1,m2 being natural number, p being prime (natural number) st
  p|^r * n = m1 * m2 & not p divides n & not p divides m2
  holds p|^r divides m1
proof
  defpred P[Nat] means for n,m1,m2 be natural number,
    p being prime (natural number)
    st p|^$1 * n = m1 * m2 & not p divides n & not p divides m2
    holds p|^$1 divides m1;
A1: P[0]
  proof
    let n,m1,m2 be natural number;
    let p be prime (natural number);
    assume p|^0 * n = m1 * m2 & not p divides n & not p divides m2;
    p|^0 = 1 by NEWTON:9;
    hence p|^0 divides m1 by NAT_D:6;
  end;
A2: for k be Nat st P[k] holds P[k + 1]
  proof
    let k be Nat;
    assume A3: P[k];
    for n,m1,m2 be natural number, p being prime (natural number)
      st p|^(k+1) * n = m1 * m2 & not p divides n & not p divides m2
      holds p|^(k+1) divides m1
    proof
      let n,m1,m2 be natural number;
      let p be prime (natural number);
      assume A4: p|^(k+1) * n = m1 * m2;
      assume A5: not p divides n;
      assume A6: not p divides m2;
      reconsider p'=p as prime Element of NAT by ORDINAL1:def 13;
      A7: p|^(k+1) * n = p|^k * p * n by NEWTON:11
      .= p|^k * n * p; then
      p divides m1 * m2 by A4,NAT_D:def 3; then
      p divides m1 by A6,NEWTON:98; then
      consider m1' be natural number such that
      A8: m1 = p * m1' by NAT_D:def 3;
      p|^k * n * p' = m1' * m2 * p' by A4,A7,A8; then
      p|^k * n = m1' * m2 by XCMPLX_1:5; then
      p|^k divides m1' by A3,A5,A6; then
      consider m1'' be natural number such that
      A9: m1' = p|^k * m1'' by NAT_D:def 3;
      m1 = p|^k * p * m1'' by A8,A9
      .= p|^(k+1) * m1'' by NEWTON:11;
      hence p|^(k+1) divides m1 by NAT_D:def 3;
    end;
    hence thesis;
  end;
  for k being Nat holds P[k] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

:: like GROUP_8:16
Lm7:
  for G being Group, A being non empty Subset of G, x being Element of G holds
  Card A = Card (A * x)
proof
  let G be Group;
  let A be non empty Subset of G;
  let x be Element of G;
  set B = A * x;
  A is non empty & B is non empty
  proof consider a being Element of A;
    a * x in B by GROUP_2:34;
    hence thesis;
  end;
  then reconsider B as non empty Subset of G;
  deffunc F(Element of G) = $1*x;
  consider f being Function of A, the carrier of G such that
  A1: for a being Element of A holds f.a = F(a) from FUNCT_2:sch 4;
  A2: dom f = A by FUNCT_2:def 1;
  A3:rng f = B
  proof
    A4: rng f c= B
    proof
      let s be set; assume s in rng f; then
      consider a being set such that
      A5:  a in A & s = f.a by A2,FUNCT_1:def 5;
      reconsider a as Element of A by A5;
      s = F(a) by A1,A5;
      hence thesis by GROUP_2:34;
    end;
    B c= rng f
    proof
      let s be set;
      assume s in B; then
      consider a being Element of G such that
      A6: s = a * x & a in A by GROUP_2:34;
      ex x being set st x in dom f & s = f.x
      proof
        take x = a;
        thus thesis by A1,A6,FUNCT_2:def 1;
      end;
      hence thesis by FUNCT_1:def 5;
    end;
    hence thesis by A4,XBOOLE_0:def 10;
  end; then
  reconsider f as Function of A,B by A2,FUNCT_2:4;
  A7: for a,b being Element of A st f.a = f.b holds a = b
  proof
    let a,b be Element of A;
    assume f.a = f.b; then
    F(a) = f.b by A1; then
    a*x = b*x by A1;
    hence a = b by GROUP_1:14;
  end;
  A,B are_equipotent
  proof take f;
    thus thesis by A3,A7,FUNCT_2:def 1,GROUP_6:1;
  end;
  hence thesis by CARD_1:21;
end;

:: ALG I.6.6 Theorem 2
:: The first Sylow theorem
theorem Th12:
  for G being finite Group, p being prime (natural number)
  holds ex P being Subgroup of G st P is_Sylow_p-subgroup_of_prime p
proof
  let G be finite Group;
  let p be prime (natural number);
  reconsider p'=p as prime Element of NAT by ORDINAL1:def 13;
A1: ord G > 0 by GROUP_1:90; then
  consider m,r be natural number such that
A2: ord G = p |^ r * m & not p divides m by Lm1;
  set n = ord G;
  reconsider k1 = p'|^r as Element of NAT by ORDINAL1:def 13;
  reconsider x1 = 1,x2 = 0 as set;
A3: card the carrier of G = n by GROUP_1:def 15;
  k1 divides n by A2,NAT_D:def 3; then
  k1 <= n by A1,NAT_D:7; then
  card k1 <= card the carrier of G by A3,CARD_1:66; then
A4: Card k1 <=` Card the carrier of G by CARD_2:57;
  set SF = the_subsets_of_card(k1, the carrier of G);
  card SF = card Choose(the carrier of G,k1,x1,x2) by Th3; then
A5: card SF = n choose k1 by A3,CARD_FIN:18; then
A6: card SF mod p <> 0 by A2,Lm5;
  set LO = the_left_operation_of G;
  set LO' = the_extension_of_left_operation_of(k1,LO);
  reconsider E = SF as non empty finite set by A6,CARD_1:78,NAT_D:26;
  reconsider T = LO' as LeftOperation of G, E;
A7: ex X being Element of E st card the_orbit_of(X,T) mod p <> 0
  proof
    assume A8: for X being Element of E holds card the_orbit_of(X,T) mod p = 0;
    consider f be FinSequence such that
    A9: rng f = the_orbits_of T & f is one-to-one by FINSEQ_4:73;
    reconsider f as FinSequence of the_orbits_of T by A9,FINSEQ_1:def 4;
    deffunc F(set) = Card(f.$1);
    consider pf being FinSequence such that
    A10: len pf = len f & for i being Nat
    st i in Seg len f holds pf.i = F(i) from FINSEQ_1:sch 2;
    A11: dom pf = Seg len f by A10,FINSEQ_1:def 3;
A12: for i being Element of NAT st i in Seg len f holds pf.i = F(i) by A10;
    for x being set st x in rng pf holds x in NAT
    proof
      let x be set;
      assume A13: x in rng pf;
      consider i be Nat such that
      A14: i in dom pf & pf.i = x by A13,FINSEQ_2:11;
      i in dom f by A10,A14,FINSEQ_3:31; then
      f.i in the_orbits_of T by A9,FUNCT_1:12; then
      consider X be Subset of E such that
      A15: f.i=X & ex x' being Element of E st X=the_orbit_of(x',T);
      x = card X by A10,A11,A14,A15;
      hence thesis;
    end; then
    rng pf c= NAT by TARSKI:def 3; then
    reconsider c = pf as FinSequence of NAT by FINSEQ_1:def 4;
    A16: card E = Sum c by A9,A10,A11,A12,WEDDWITT:6;
    deffunc F'(set) = c.($1) / p;
    consider c' being FinSequence such that
    A17: len c' = len c & for i being Nat
      st i in Seg len c holds c'.i = F'(i) from FINSEQ_1:sch 2;
    for x being set st x in rng c' holds x in NAT
    proof
      let x be set;
      assume A18: x in rng c';
      consider i be Nat such that
      A19: i in dom c' & c'.i = x by A18,FINSEQ_2:11;
      reconsider i as Element of NAT by ORDINAL1:def 13;
      A20: i in Seg len c by A17,A19,FINSEQ_1:def 3;
      A21: dom pf = Seg len c by FINSEQ_1:def 3
      .= dom c' by A17,FINSEQ_1:def 3;
      i in dom f by A10,A19,A21,FINSEQ_3:31; then
      f.i in the_orbits_of T by A9,FUNCT_1:12; then
      consider X be Subset of E such that
      A22: f.i=X & ex x' being Element of E st X=the_orbit_of(x',T);
      consider x' be Element of E such that
      A23: X=the_orbit_of(x',T) by A22;
      A24: c.i = card X by A10,A11,A19,A21,A22;
      A25: c.i mod p = 0 by A8,A23,A24;
      set q = c.i div p';
      c.i = p' * q + (c.i mod p') by NAT_D:2
      .= p' * q by A25; then
      consider q be natural number such that
      A26: c.i = p * q;
      reconsider p''=p' as real number;
      x = p'' * q / p'' by A17,A19,A20,A26
      .= p'' / p'' * q by XCMPLX_1:75
      .= 1 * q by XCMPLX_1:60;
      hence thesis by ORDINAL1:def 13;
    end; then
    rng c' c= NAT by TARSKI:def 3; then
    reconsider c' as FinSequence of NAT by FINSEQ_1:def 4;
    A27: dom c = Seg len c' by A17,FINSEQ_1:def 3
    .= dom c' by FINSEQ_1:def 3
    .= dom(p'*c') by SEQ_1:def 6;
    for k being Nat st k in dom c holds c.k = (p'*c').k
    proof
      let k be Nat;
      assume A28: k in dom c;
      A29: k in Seg len c by A28,FINSEQ_1:def 3;
      reconsider p''=p' as real number;
      p * c'.k = p * (c.k / p') by A17,A29
      .= p'' * c.k / p'' by XCMPLX_1:75
      .= (p'' / p'') * c.k by XCMPLX_1:75
      .= 1 * c.k by XCMPLX_1:60;
      hence c.k = (p'*c').k by A27,A28,SEQ_1:def 6;
    end; then
    A30: c = p' * c' by A27,FINSEQ_1:17;
    card E mod p = p'*(Sum c') mod p' by A16,A30,RVSUM_1:117
    .= 0 by NAT_D:13;
    hence contradiction by A2,A5,Lm5;
  end;
  consider X be Element of E such that
A31: card the_orbit_of(X,T) mod p <> 0 by A7;
  set HX = the_strict_stabilizer_of(X,T);
A32: Card the_orbit_of(X,T) = Index HX by Th10;
  Left_Cosets HX is finite by GROUP_2:164; then
A33: index HX mod p <> 0 by A31,A32,GROUP_2:def 18;
A34: p|^r * m = ord HX * index HX by A2,GROUP_2:177;
  take HX;
  reconsider H=HX as finite Group by GROUP_2:48;
A35: ord H <> 0 by GROUP_1:90;
A36: ord H > 0 by GROUP_1:90;
  now assume p divides index HX; then
    consider t be Nat such that
    A37: index HX = p * t by NAT_D:def 3;
    thus contradiction by A33,A37,NAT_D:13;
  end; then
  p|^r divides ord H by A2,A34,Lm6; then
A38: p|^r <= ord H by A36,NAT_D:7;
A39: the carrier of HX={g where g is Element of G: (T^g).:{X} = {X}} by Def10;
  X in {X' where X' is Subset of the carrier of G: Card X' = k1}; then
A40: ex X' being Subset of the carrier of G st X'=X & Card X' = k1;
  reconsider X as non empty Subset of G by A40,CARD_1:47;
  consider x be Element of X;
  reconsider x as Element of G;
  now let z be set;
    assume z in the carrier of H; then
    consider g be Element of G such that
    A41: z=g & (T^g).:{X}={X} by A39;
    A42: dom(T^g) = E by FUNCT_2:def 1;
    Im(T^g,X) = {(T^g).X} by A42,FUNCT_1:117; then
    A43: (T^g).X = X by A41,ZFMISC_1:6;
    reconsider X1=X as Element of E;
    T.g = the_extension_of_left_translation_of(k1,g,LO) by A4,Def5; then
    A44: (T^g).X1 = (LO^g) .: X1 by A4,Def4;
    reconsider LO as LeftOperation of G,the carrier of G;
    reconsider g'' = g as Element of G;
    A45: LO^g = the_left_translation_of g by Def2;
    ex x' being set st x' in dom(LO^g) & x' in X & g * x = (LO^g).x'
    proof
      reconsider x'=x as set;
      take x';
      dom(LO^g) = the carrier of G by FUNCT_2:def 1;
      hence x' in dom(LO^g) & x' in X;
      thus g * x = (LO^g).x' by A45,TOPGRP_1:def 1;
    end; then
    A46: g * x in (LO^g) .: X by FUNCT_1:def 12;
    set h = g * x;
    h * x" = g * (x*x") by GROUP_1:def 4
    .= g * 1_G by GROUP_1:def 6
    .= z by A41,GROUP_1:def 5;
    hence z in X * x" by A43,A44,A46,GROUP_2:34;
  end; then
  the carrier of H c= X * x" by TARSKI:def 3; then
  card (the carrier of H) <= card (X * x") by CARD_1:80; then
A47: card (the carrier of H) <= card X by Lm7;
A48: ord H <= p|^r by A40,A47,GROUP_1:def 15;
  ord H = p|^r by A38,A48,XXREAL_0:1; then
  H is_p-group_of_prime p by Def17; then
A49: HX is_p-group_of_prime p by Def18;
  index HX = index HX * ord HX * ( 1 / ord HX) by A35,XCMPLX_1:108
  .= p|^r * m * (1 / ord HX) by A2,GROUP_2:177
  .= p'|^r * m * (1 / p'|^r) by A38,A48,XXREAL_0:1
  .= m * (p'|^r * (1/p'|^r))
  .= m * (p'|^r / p'|^r) by XCMPLX_1:100
  .= m by XCMPLX_1:89;
  hence HX is_Sylow_p-subgroup_of_prime p by A2,A49,Def19;
end;

Lm8:
for n,r being natural number, p being prime (natural number)
st n divides p|^r & n > 1 holds p divides n
proof
  let n,r be natural number;
  let p be prime (natural number);
  assume A1: n divides p|^r;
  assume A2: n > 1;
  reconsider n'=n,r'=r,p'=p as Element of NAT by ORDINAL1:def 13;
  A3: p' is prime & n' divides p'|^r' by A1;
  per cases by A3,PEPIN:33;
  suppose n' = 1;
    hence p divides n by A2;
  end;
  suppose ex k being Element of NAT st n' = p*k;
    hence p divides n by NAT_D:def 3;
  end;
end;

:: ALG I.6.6 Corollary
:: The Cauchy theorem
theorem
  for G being finite Group, p being prime (natural number) st p divides ord G
  holds ex g being Element of G st ord g = p
proof
  let G be finite Group;
  let p be prime (natural number);
  assume A1: p divides ord G;
  consider P be Subgroup of G such that
  A2: P is_Sylow_p-subgroup_of_prime p by Th12;
  P is_p-group_of_prime p & not p divides index P by A2,Def19; then
  consider H be finite Group such that
  A3: P = H & H is_p-group_of_prime p by Def18;
  consider r be natural number such that
  A4: ord H = p |^ r by A3,Def17;
  A5: now assume r=0; then
    ord H = 1 by A4,NEWTON:9; then
    ord G = 1 * index P by A3,GROUP_2:177;
    hence contradiction by A1,A2,Def19;
  end;
  0 < r by A5; then
  0 + 1 < r + 1 by XREAL_1:8; then
  1 <= r by NAT_1:13; then
  1 - 1 <= r - 1 by XREAL_1:11; then
  reconsider r' = r - 1 as Element of NAT by INT_1:16;
  reconsider p' = p as prime Element of NAT by ORDINAL1:def 13;
  set H' = (Omega).H;
  A6: ord H = ord (Omega).H * index (Omega).H by GROUP_2:177
  .= ord (Omega).H * 1 by GROUP_2:180;
  A7: p |^ r = p |^(r'+1)
  .= p|^r' * p by NEWTON:11;
  0 + 1 < p'|^r' + 1 by XREAL_1:8; then
  1 <= p|^r' by NAT_1:13; then
  A8: 1*p <= p|^r'*p by XREAL_1:66;
  1 < p' by NAT_4:12; then
  ord H' > 1 by A4,A6,A7,A8,XXREAL_0:2; then
  consider g be Element of H' such that
  A9: g <> 1_H' by GR_CY_1:31;
  reconsider H'' = gr {g} as strict Group;
  A10: H'' is cyclic Group by GR_CY_2:10;
  A11: H' is finite by GROUP_2:48; then
  A12: H'' is finite by GROUP_2:48;
  set n = ord H'';
  A13: H' is finite by GROUP_2:48;
  A14: n >= 1 by A12,GROUP_1:90;
  now assume n = 1; then
    ord g = 1 by A11,GR_CY_1:27;
    hence contradiction by A9,GROUP_1:85;
  end; then
  A15: n > 1 by A14,XXREAL_0:1;
  ord H'' divides ord H' by A13,GROUP_2:178; then
  A16: p divides n by A4,A6,A15,Lm8;
  consider H''' be strict Subgroup of H'' such that
  A17: ord H''' = p' &
    for G2 being strict Subgroup of H'' st ord G2=p' holds G2=H'''
    by A10,A12,A16,GR_CY_2:28;
  H''' is finite by A12,GROUP_2:48; then
  consider h' be Element of H''' such that
  A18: ord h' = p by A17,GROUP_8:1;
  H' is Subgroup of G by A3,GROUP_2:65; then
  H'' is Subgroup of G by GROUP_2:65; then
  reconsider H''' as strict Subgroup of G by GROUP_2:65;
  reconsider h' as Element of H''';
  reconsider h=h' as Element of G by GROUP_2:51;
  take h;
  thus thesis by A18,GROUP_8:5;
end;

:: ALG I.6.6 Theorem 3
:: The second Sylow theorem
theorem Th14:
  for G being finite Group, p being prime (natural number) holds
    (for H being Subgroup of G st H is_p-group_of_prime p holds
      ex P being Subgroup of G st
      P is_Sylow_p-subgroup_of_prime p & H is Subgroup of P) &
    (for P1,P2 being Subgroup of G
      st P1 is_Sylow_p-subgroup_of_prime p & P2 is_Sylow_p-subgroup_of_prime p
      holds P1,P2 are_conjugated)
proof
  let G be finite Group;
  let p be prime (natural number);
  reconsider p'=p as prime Element of NAT by ORDINAL1:def 13;
A1: for H,P being Subgroup of G
    st H is_p-group_of_prime p & P is_Sylow_p-subgroup_of_prime p
    holds ex g being Element of G st carr H c= carr(P |^ g)
  proof
    let H,P be Subgroup of G;
    assume A2: H is_p-group_of_prime p;
    assume A3: P is_Sylow_p-subgroup_of_prime p;
    set H'= H;
    set E = Left_Cosets P;
    set T = the_left_operation_of(H',P);
    reconsider H' as finite Group by GROUP_2:48;
A4: ex I being finite Group st H = I & I is_p-group_of_prime p by A2,Def18;
    reconsider E as non empty finite set by GROUP_2:164,165;
    reconsider T as LeftOperation of H', E;
A5: card the_fixed_points_of T mod p = card E mod p by A4,Th11;
A6: not p divides index P by A3,Def19;
    now assume A7: card E mod p = 0;
      (ex t being Nat st card E = p * t + 0 & 0<p)
      or (0=0 & p=0) by A7,NAT_D:def 2; then
      p' divides card E by NAT_D:def 3;
      hence contradiction by A6,GROUP_2:def 18;
    end; then
    card the_fixed_points_of T <> 0 by A5,NAT_D:26; then
    consider x be set such that
A8: x in the_fixed_points_of T by CARD_1:78,XBOOLE_0:def 1;
    x in {x' where x' is Element of E: x' is_fixed_under T} by A8,Def13; then
    consider x' be Element of E such that
A9: x=x' & x' is_fixed_under T;
    x' in Left_Cosets P; then
    consider g' be Element of G such that
A10: x' = g' * P by GROUP_2:def 15;
    set g=g'";
    take g;
    now let y be set;
      assume y in carr H; then
      reconsider h=y as Element of H';
      reconsider h'=h as Element of H;
      A11: the_left_operation_of(H,P).h' =
        the_left_translation_of(h',P) by Def9;
      reconsider P1=x' as Element of Left_Cosets P;
      consider P2 be Element of Left_Cosets P,
        A1,A2 be Subset of G, g'' be Element of G such that
      A12: P2 = the_left_translation_of(h',P).P1 & A2 = g'' * A1 & A1 = P1 &
        A2 = P2 & g'' = h' by Def8;
      A13: g' * P * g'" = g'' * (g' * P) * g'" by A9,A10,A11,A12,Def12
      .= g'' * (g' * P * g'") by GROUP_2:39;
      g'' in g' * P * g'"
      proof
        assume A14: not g'' in g' * P * g'";
        A15: g'' = g'' * 1_G by GROUP_1:def 5;
        A16: g' * g'" = 1_G by GROUP_1:def 6;
        1_G in P by GROUP_2:55; then
        A17: 1_G in carr P by STRUCT_0:def 5;
        g' = g' * 1_G by GROUP_1:def 5; then
        g' in g' * P by A17,GROUP_2:33; then
        1_G in (g' * P * g'") by A16,GROUP_2:34;
        hence contradiction by A13,A14,A15,GROUP_2:33;
      end;
      hence y in g' * P * g'" by A12;
    end; then
    A18: carr H c= g' * P * g'" by TARSKI:def 3;
    carr H c= g" * P * g by A18,GROUP_1:19;
    hence carr H c= carr(P |^ g) by GROUP_3:71;
  end;
  thus for H being Subgroup of G st H is_p-group_of_prime p holds
         ex P being Subgroup of G st P is_Sylow_p-subgroup_of_prime p &
         H is Subgroup of P
  proof
    let H be Subgroup of G;
    assume A19: H is_p-group_of_prime p;
    consider P' be Subgroup of G such that
    A20: P' is_Sylow_p-subgroup_of_prime p by Th12;
    consider g be Element of G such that
    A21: carr H c= carr(P' |^ g) by A1,A19,A20;
    set P = P' |^ g;
    take P;
    A22: P' is_p-group_of_prime p & not p divides index P' by A20,Def19;
    consider H1 be finite Group such that
    A23: P' = H1 & H1 is_p-group_of_prime p by A22,Def18;
    consider r be natural number such that
    A24: ord H1 = p |^ r by A23,Def17;
    set H2 = P;
    reconsider H2 as finite Group by GROUP_2:48;
    P' is finite by GROUP_2:48; then
    A25: ord P' = ord P by GROUP_3:79; then
    H2 is_p-group_of_prime p by A23,A24,Def17; then
    A26: P is_p-group_of_prime p by Def18;
    P is finite by GROUP_2:48; then
    A27: ord P <> 0 by GROUP_1:90;
    ord P * index P = ord G by GROUP_2:177
    .= ord P * index P' by A25,GROUP_2:177; then
    not p divides index P by A22,A27,XCMPLX_1:5;
    hence P is_Sylow_p-subgroup_of_prime p by A26,Def19;
    thus H is Subgroup of P by A21,GROUP_2:66;
  end;
  let P1,P2 be Subgroup of G;
  assume P1 is_Sylow_p-subgroup_of_prime p; then
  A28: P1 is_p-group_of_prime p & not p divides index P1 by Def19; then
  consider H1 be finite Group such that
  A29: P1 = H1 & H1 is_p-group_of_prime p by Def18;
  consider r1 be natural number such that
  A30: ord H1 = p |^ r1 by A29,Def17;
  assume A31: P2 is_Sylow_p-subgroup_of_prime p; then
  consider g be Element of G such that
  A32: carr P1 c= carr(P2 |^ g) by A1,A28;
  A33: P2 is finite by GROUP_2:48;
  A34: P2 is_p-group_of_prime p & not p divides index P2 by A31,Def19; then
  consider H2 be finite Group such that
  A35: P2 = H2 & H2 is_p-group_of_prime p by Def18;
  consider r2 be natural number such that
  A36: ord H2 = p |^ r2 by A35,Def17;
  A37: ord G = ord P2 * index P2 by GROUP_2:177;
  A38: p|^r1 * index P1=p|^r2 * index P2 by A29,A30,A35,A36,A37,GROUP_2:177;
  now assume A39: r1<>r2;
    per cases by A39,XXREAL_0:1;
    suppose r1 > r2; then
      A40: r1 - r2 > r2 - r2 by XREAL_1:11;
      set r = r1 -' r2;
      A41: r = r1 - r2 by A40,BINARITH:def 3; then
      r1 = r2 + r; then
      p' |^ r1 = p' |^r2 * p' |^ r by NEWTON:13; then
      p' |^ r2 * (p' |^ r * index P1) = p' |^ r2 * index P2 by A38; then
      A42: p' |^ r * index P1 = index P2 by XCMPLX_1:5;
      consider k be Nat such that
      A43: r = k + 1 by A40,A41,NAT_1:6;
      p |^ k * p * index P1 = index P2 by A42,A43,NEWTON:11; then
      p * (p |^ k * index P1) = index P2;
      hence contradiction by A34,NAT_D:def 3;
    end;
    suppose r1<r2; then
      A44: r2 - r1 > r1 - r1 by XREAL_1:11;
      set r = r2 -' r1;
      A45: r = r2 - r1 by A44,BINARITH:def 3; then
      r2 = r1 + r; then
      p' |^ r2 = p' |^r1 * p' |^ r by NEWTON:13; then
      p' |^ r1 * (p' |^ r * index P2) = p' |^ r1 * index P1
        by A29,A30,A35,A36,A37,GROUP_2:177; then
      A46: p' |^ r * index P2 = index P1 by XCMPLX_1:5;
      consider k be Nat such that
      A47: r = k + 1 by A44,A45,NAT_1:6;
      p |^ k * p * index P2 = index P1 by A46,A47,NEWTON:11; then
      p * (p |^ k * index P2) = index P1;
      hence contradiction by A28,NAT_D:def 3;
    end;
  end; then
  A48: card carr P1 = ord P2 by A29,A30,A35,A36,GROUP_1:def 15
  .= card carr P2 by A33,GROUP_1:def 15;
  A49: P2 |^ g is finite by GROUP_2:48;
  card carr P2 = ord P2 by A33,GROUP_1:def 15
  .= ord(P2 |^ g) by A33,GROUP_3:79
  .= card carr(P2 |^ g) by A49,GROUP_1:def 15; then
  carr P1 = carr (P2 |^ g) by A32,A48,CARD_FIN:1; then
  the HGrStr of P1 = the HGrStr of P2 |^ g by GROUP_2:68;
  hence P1,P2 are_conjugated by GROUP_3:def 11;
end;

definition
  let G be Group;
  let p be prime (natural number);
  func the_sylow_p-subgroups_of_prime(p,G) -> Subset of Subgroups G
  equals {H where H is Element of Subgroups G: ex P being strict Subgroup of G
    st P = H & P is_Sylow_p-subgroup_of_prime p};
  correctness
  proof
    set X = {H where H is Element of Subgroups G:
      ex P being strict Subgroup of G st P = H &
      P is_Sylow_p-subgroup_of_prime p};
    now let x be set;
      assume x in X; then
      ex H being Element of Subgroups G st x = H &
        ex P being strict Subgroup of G st
          P = H & P is_Sylow_p-subgroup_of_prime p;
      hence x in Subgroups G;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;

registration
  let G be finite Group;
  let p be prime (natural number);
  cluster the_sylow_p-subgroups_of_prime(p,G) -> non empty finite;
  correctness
  proof
    consider P be Subgroup of G such that
A1: P is_Sylow_p-subgroup_of_prime p by Th12;
    set P' = (Omega).P;
    the carrier of P c= the carrier of G &
      the mult of P = (the mult of G)||the carrier of P by GROUP_2:def 5; then
    reconsider P' as strict Subgroup of G by GROUP_2:def 5;
    reconsider H'=P' as Element of Subgroups G by GROUP_3:def 1;
    P is_p-group_of_prime p & not p divides index P by A1,Def19; then
    consider H be finite Group such that
    A2: P = H & H is_p-group_of_prime p by Def18;
    A3: ord P <> 0 by A2,GROUP_1:90;
    P is finite by GROUP_2:48; then
    A4: ex B being finite set st B = the carrier of P & ord P = card B
      by GROUP_1:def 15;
    P' is finite by GROUP_2:48; then
    A5: ex B being finite set st B = the carrier of P' & ord P' = card B
      by GROUP_1:def 15;
    ord P * index P = ord G by GROUP_2:177
    .= ord P * index P' by A4,A5,GROUP_2:177; then
    A6: index P' = index P by A3,XCMPLX_1:5;
    ex H being finite Group st P' = H & H is_p-group_of_prime p
    proof
      set H'' = (Omega).H;
      reconsider H'' as finite Group by GROUP_2:48;
      take H'';
      thus P' = H'' by A2;
      A7: ex B being finite set st B = the carrier of H & ord H = card B
        by GROUP_1:def 15;
      A8: ex B being finite set st B = the carrier of H'' & ord H'' = card B
        by GROUP_1:def 15;
      consider r be natural number such that
      A9: ord H = p |^ r by A2,Def17;
      thus H'' is_p-group_of_prime p by A7,A8,A9,Def17;
    end; then
    P' is_p-group_of_prime p & not p divides index P'
      by A1,A6,Def18,Def19; then
    H'=P' & P' is_Sylow_p-subgroup_of_prime p by Def19; then
A10: H' in the_sylow_p-subgroups_of_prime(p,G);
    Subgroups G is finite by GROUP_3:19;
    hence thesis by A10,FINSET_1:13;
  end;
end;

definition
  let G be finite Group;
  let p be prime (natural number);
  let H be Subgroup of G;
  let h be Element of H;
  func the_left_translation_of(h,p) ->
  Function of the_sylow_p-subgroups_of_prime(p,G),
              the_sylow_p-subgroups_of_prime(p,G)
    means :Def21:
    for P1 being Element of the_sylow_p-subgroups_of_prime(p,G) holds
      ex P2 being Element of the_sylow_p-subgroups_of_prime(p,G),
         H1,H2 being strict Subgroup of G, g being Element of G st
      P2 = it.P1 & P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g;
  existence
  proof
    set f = {[P1,P2] where P1,P2 is
        Element of the_sylow_p-subgroups_of_prime(p,G):
        ex H1,H2 being strict Subgroup of G, g being Element of G
        st P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g};
      set E = the_sylow_p-subgroups_of_prime(p,G);
      A1: now
        let x,y1,y2 be set;
        assume [x,y1] in f; then
        consider P1',P2' be Element of E such that
        A2: [x,y1] = [P1',P2'] &
          ex H1,H2 being strict Subgroup of G, g being Element of G
          st P1'=H1 & P2'=H2 & h"=g & H2 = H1 |^ g;
        assume [x,y2] in f; then
        consider P1'',P2'' be Element of E such that
        A3: [x,y2] = [P1'',P2''] &
          ex H1,H2 being strict Subgroup of G, g being Element of G
          st P1''=H1 & P2''=H2 & h"=g & H2 = H1 |^ g;
        A4: x = P1' & y1 = P2' by A2,ZFMISC_1:33;
        x = P1'' & y2 = P2'' by A3,ZFMISC_1:33;
        hence y1 = y2 by A2,A3,A4;
      end;
      now
        let x be set;
        assume x in f; then
        consider P1,P2 be Element of E such that
        A5: x = [P1,P2] &
          ex H1,H2 being strict Subgroup of G, g being Element of G
          st P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g;
        reconsider y=P1,z=P2 as set;
        take y,z;
        thus x = [y,z] by A5;
      end; then
      reconsider f as Function by A1,FUNCT_1:def 1,RELAT_1:def 1;
      now
        let x be set;
        hereby
          assume x in E; then
          reconsider P1 = x as Element of E;
          P1 in E; then
          consider H1 be Element of Subgroups G such that
          A6: P1=H1 & ex P being strict Subgroup of G
            st P = H1 & P is_Sylow_p-subgroup_of_prime p;
          reconsider H1 as strict Subgroup of G by A6;
          A7: H1 is_p-group_of_prime p & not p divides index H1
            by A6,Def19; then
          consider H' be finite Group such that
          A8: H1 = H' & H' is_p-group_of_prime p by Def18;
          consider r be natural number such that
          A9: ord H' = p |^ r by A8,Def17;
          reconsider g=h" as Element of G by GROUP_2:51;
          set H2 = H1 |^ g;
          reconsider H2 as strict Subgroup of G;
          A10: H1 is finite by GROUP_2:48; then
          A11: ord H1 <> 0 by GROUP_1:90;
          A12: ord H1 = ord H2 by A10,GROUP_3:79;
          A13: ord H1 * index H1 = ord G by GROUP_2:177
          .= ord H1 * index H2 by A12,GROUP_2:177;
          reconsider H''=H2 as finite Group by GROUP_2:48;
          ord H' = ord H'' by A8,GROUP_3:79; then
          H'' is_p-group_of_prime p by A9,Def17; then
          H2 is_p-group_of_prime p & not p divides index H2
            by A7,A11,A13,Def18,XCMPLX_1:5; then
          A14: H2 is_Sylow_p-subgroup_of_prime p by Def19;
          reconsider H2'=H2 as Element of Subgroups G by GROUP_3:def 1;
          H2' in E by A14; then
          reconsider P2=H2 as Element of E;
          reconsider y = P2 as set;
          take y;
          thus [x,y] in f by A6;
        end;
        given y be set such that
        A15: [x,y] in f;
        consider P1,P2 be Element of E such that
        A16: [x,y] = [P1,P2] &
          ex H1,H2 being strict Subgroup of G, g being Element of G
          st P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g by A15;
        x = P1 by A16,ZFMISC_1:33;
        hence x in E;
      end; then
A17:   dom f = E by RELAT_1:def 4;
      now let y be set;
        assume y in rng f; then
        consider x be set such that
        A18: [x,y] in f by RELAT_1:def 5;
        consider P1,P2 be Element of E such that
        A19: [x,y] = [P1,P2] &
          ex H1,H2 being strict Subgroup of G, g being Element of G
          st P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g by A18;
        y = P2 by A19,ZFMISC_1:33;
        hence y in E;
      end; then
      rng f c= E by TARSKI:def 3; then
      reconsider f as Function of E,E by A17,FUNCT_2:4;
      take f;
     thus for P1 being Element of E holds
      ex P2 being Element of E,
        H1,H2 being strict Subgroup of G, g being Element of G st
        P2 = f.P1 & P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g
     proof
       let P1 be Element of E;
       set P2=f.P1;
       [P1,P2] in f by A17,FUNCT_1:8; then
       consider P1',P2' be Element of E such that
       A20: [P1,P2]=[P1',P2'] &
          ex H1,H2 being strict Subgroup of G, g being Element of G st
          P1'=H1 & P2'=H2 & h"=g & H2 = H1 |^ g;
       consider H1,H2 be strict Subgroup of G, g be Element of G such that
       A21: P1'=H1 & P2'=H2 & h"=g & H2 = H1 |^ g by A20;
       reconsider P2 as Element of E;
       take P2, H1,H2, g;
       thus P2 = f.P1;
       thus P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g  by A20,A21,ZFMISC_1:33;
     end;
  end;
  uniqueness
  proof
    let IT1, IT2 be Function of the_sylow_p-subgroups_of_prime(p,G),
              the_sylow_p-subgroups_of_prime(p,G) such that
A22: for P1 being Element of the_sylow_p-subgroups_of_prime(p,G) holds
      ex P2 being Element of the_sylow_p-subgroups_of_prime(p,G),
         H1,H2 being strict Subgroup of G,
         g being Element of G
      st P2 = IT1.P1 & P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g and
A23: for P1 being Element of the_sylow_p-subgroups_of_prime(p,G) holds
      ex P2 being Element of the_sylow_p-subgroups_of_prime(p,G),
         H1,H2 being strict Subgroup of G,
         g being Element of G
      st P2 = IT2.P1 & P1=H1 & P2=H2 & h"=g & H2 = H1 |^ g;
    now
      let x be set;
      assume x in the_sylow_p-subgroups_of_prime(p,G);
      then reconsider P1 = x as Element of the_sylow_p-subgroups_of_prime(p,G);
      consider P12 be Element of the_sylow_p-subgroups_of_prime(p,G),
         H11,H12 be strict Subgroup of G,
         g1 be Element of G such that
      A24: P12 = IT1.P1 & P1=H11 & P12=H12 & h"=g1 & H12 = H11 |^ g1 by A22;
      consider P22 be Element of the_sylow_p-subgroups_of_prime(p,G),
         H21,H22 be strict Subgroup of G,
         g2 be Element of G such that
      A25: P22 = IT2.P1 & P1=H21 & P22=H22 & h"=g2 & H22 = H21 |^ g2 by A23;
      thus IT1.x = IT2.x by A24,A25;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

definition
  let G be finite Group;
  let p be prime (natural number);
  let H be Subgroup of G;
  func the_left_operation_of(H,p) ->
    LeftOperation of H, the_sylow_p-subgroups_of_prime(p,G)
  means :Def22:
  for h being Element of H holds it.h = the_left_translation_of(h,p);
  existence
  proof
    set E = the_sylow_p-subgroups_of_prime(p,G);
    deffunc f(Element of H)=the_left_translation_of($1,p);
    A1: for h1,h2 being Element of H holds f(h1*h2)=f(h1)*f(h2)
    proof
      let h1,h2 be Element of H;
      set f12 = the_left_translation_of(h1*h2,p);
      set f1 = the_left_translation_of(h1,p);
      set f2 = the_left_translation_of(h2,p);
      A2: f12 in Funcs(E,E) & f1 in Funcs(E,E) &
      f2 in Funcs(E,E) by FUNCT_2:12;
      A3: ex f being Function st f1=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A4: ex f being Function st f2=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A5: ex f being Function st f12=f & dom f = E & rng f c= E
        by A2,FUNCT_2:def 2;
      A6: now
        let x be set;
        hereby
          assume A7: x in dom f12;
          hence x in dom f2 by A4;
          f2.x in rng f2 by A4,A7,FUNCT_1:12;
          hence f2.x in dom f1 by A3;
        end;
        assume x in dom f2 & f2.x in dom f1;
        hence x in dom f12 by A5;
      end;
      now
        let x be set;
        assume A8: x in dom f12; then
        reconsider P1'=x as Element of E;
        consider P2' be Element of E,
          H1',H2' be strict Subgroup of G, g2 be Element of G such that
        A9: P2'=f2.P1' & P1'=H1' & P2'=H2' & h2"=g2 & H2' = H1' |^ g2
          by Def21;
        f2.x in rng f2 by A4,A8,FUNCT_1:12; then
        reconsider P1''=f2.x as Element of E;
        consider P2'' be Element of E,
          H1'',H2'' be strict Subgroup of G,
          g1 be Element of G such that
        A10: P2''=f1.P1'' & P1''=H1'' & P2''=H2'' & h1"=g1 & H2'' = H1'' |^ g1
          by Def21;
        reconsider P1'''=x as Element of E by A8;
        consider P2''' be Element of E,
          H1''',H2''' be strict Subgroup of G,
          g3 be Element of G such that
        A11: P2'''=f12.P1''' & P1'''=H1''' & P2'''=H2''' & (h1*h2)"=g3 &
          H2'''=H1''' |^ g3 by Def21;
        A12: g3 = h2" * h1" by A11,GROUP_1:25;
        A13: P2''' = H1''' |^ (g2 * g1) by A9,A10,A11,A12,GROUP_2:52
        .= P2'' by A9,A10,A11,GROUP_3:72;
        thus f12.x = f1.(f2.x) by A10,A11,A13;
      end;
      hence thesis by A6,FUNCT_1:20;
    end;
    A14: f(1_H) = id E
    proof
      set f = the_left_translation_of(1_H,p);
      f in Funcs(E,E) by FUNCT_2:12; then
      A15: ex f' being Function st f=f' & dom f' = E & rng f' c= E
        by FUNCT_2:def 2;
      now
        let x be set;
        assume x in E; then
        reconsider P1=x as Element of E;
        consider P2 be Element of E,
          H1,H2 be strict Subgroup of G, g be Element of G such that
        A16: P2=f.P1 & P1=H1 & P2=H2 & (1_H)"=g & H2 = H1 |^ g by Def21;
        (1_H)" = 1_H by GROUP_1:16; then
        g = 1_G by A16,GROUP_2:53;
        hence f.x = x by A16,GROUP_3:73;
      end;
      hence thesis by A15,FUNCT_1:34;
    end;
    ex T being LeftOperation of H, E st
      for h being Element of H holds T.h = f(h)
      from ExLeftOperation(A14,A1);
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of H, the_sylow_p-subgroups_of_prime(p,G)
    such that
A17: for h being Element of H holds T1.h = the_left_translation_of(h,p) and
A18: for h being Element of H holds T2.h = the_left_translation_of(h,p);
    now
      let x be set;
      assume x in the carrier of H;
      then reconsider h = x as Element of H;
      thus T1.x = the_left_translation_of(h,p) by A17 .= T2.x by A18;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

Lm9:
for G,H being finite Group, p being prime (natural number),
    I being Subgroup of G,
    P being Subgroup of H
st I = P & I is_Sylow_p-subgroup_of_prime p & H is Subgroup of G
holds P is_Sylow_p-subgroup_of_prime p
proof
  let G,H be finite Group;
  let p be prime (natural number);
  let I be Subgroup of G;
  let P be Subgroup of H;
  assume A1: I = P;
  assume I is_Sylow_p-subgroup_of_prime p; then
  A2: I is_p-group_of_prime p & not p divides index I by Def19;
  assume H is Subgroup of G; then
  reconsider H'=H as Subgroup of G;
  ex I' being finite Group st I = I' & I' is_p-group_of_prime p by A2,Def18;
  then A3: P is_p-group_of_prime p by A1,Def18;
  index I = index P * index H' by A1,GROUP_2:179; then
  not p divides index P by A2,NAT_D:9;
  hence P is_Sylow_p-subgroup_of_prime p by A3,Def19;
end;

:: ALG I.6.6 Theorem 3
:: The third Sylow theorem
theorem
  for G being finite Group, p being prime (natural number) holds
    card the_sylow_p-subgroups_of_prime(p,G) mod p = 1 &
    card the_sylow_p-subgroups_of_prime(p,G) divides ord G
proof
  let G be finite Group;
  let p be prime (natural number);
  consider P be Subgroup of G such that
  A1: P is_Sylow_p-subgroup_of_prime p by Th12;
  set T = the_left_operation_of(P,p);
  set E = the_sylow_p-subgroups_of_prime(p,G);
  P is_p-group_of_prime p & not p divides index P by A1,Def19; then
  consider H be finite Group such that
  A2: P = H & H is_p-group_of_prime p by Def18;
  set P' = (Omega).P;
  the carrier of P c= the carrier of G &
    the mult of P = (the mult of G)||the carrier of P by GROUP_2:def 5; then
  reconsider P' as strict Subgroup of G by GROUP_2:def 5;
  reconsider H'=P' as Element of Subgroups G by GROUP_3:def 1;
  A3: ord P <> 0 by A2,GROUP_1:90;
  P is finite by GROUP_2:48; then
  A4: ex B being finite set st B = the carrier of P & ord P = card B
    by GROUP_1:def 15;
  P' is finite by GROUP_2:48; then
  A5: ex B being finite set st B = the carrier of P' & ord P' = card B
    by GROUP_1:def 15;
  ord P * index P = ord G by GROUP_2:177
  .= ord P * index P' by A4,A5,GROUP_2:177; then
  A6: index P' = index P by A3,XCMPLX_1:5;
  ex H being finite Group st P' = H & H is_p-group_of_prime p
  proof
    set H'' = (Omega).H;
    reconsider H'' as finite Group by GROUP_2:48;
    take H'';
    thus P' = H'' by A2;
    A7: ex B being finite set st B = the carrier of H & ord H = card B
      by GROUP_1:def 15;
    A8: ex B being finite set st B = the carrier of H'' & ord H'' = card B
      by GROUP_1:def 15;
    consider r be natural number such that
    A9: ord H = p |^ r by A2,Def17;
    thus H'' is_p-group_of_prime p by A7,A8,A9,Def17;
  end; then
  P' is_p-group_of_prime p & not p divides index P'
    by A1,A6,Def18,Def19; then
  A10: H'=P' & P' is_Sylow_p-subgroup_of_prime p by Def19; then
  A11: P' in E;
  reconsider T as LeftOperation of P, E;
  reconsider P' as Element of E by A11;
  for x being set holds x in the_fixed_points_of T iff x=P'
  proof
    let x be set;
    hereby
      assume x in the_fixed_points_of T; then
      x in {x' where x' is Element of E: x' is_fixed_under T} by Def13; then
      consider Q be Element of E such that
      A12: x=Q & Q is_fixed_under T;
      Q in {H'' where H'' is Element of Subgroups G:
        ex P being strict Subgroup of G
        st P = H'' & P is_Sylow_p-subgroup_of_prime p}; then
      consider H'' be  Element of Subgroups G such that
      A13: Q=H'' & ex P being strict Subgroup of G
        st P = H'' & P is_Sylow_p-subgroup_of_prime p;
      consider Q' be strict Subgroup of G such that
      A14: Q' = H'' & Q' is_Sylow_p-subgroup_of_prime p by A13;
      set N = Normalizator Q';
      now let y be set;
        assume A15: y in the carrier of P;
        ex h being Element of G st y = h & Q' |^ h = Q'
        proof
          set h = y;
          the carrier of P c= the carrier of G by GROUP_2:def 5; then
          reconsider h as Element of G by A15;
          take h;
          thus y = h;
          reconsider h'=h as Element of P by A15;
          A16: Q' = (T^h').Q' by A12,A13,A14,Def12;
          A17: T.h' = the_left_translation_of(h',p) by Def22;
          dom(T^h') = E by FUNCT_2:def 1; then
          Q' in dom(T^h') by A14; then
          reconsider Q1=Q' as Element of E;
          consider Q2 be Element of E,
            H1,H2 be strict Subgroup of G, g be Element of G such that
          A18: Q2=the_left_translation_of(h',p).Q1 & Q1=H1 & Q2=H2 & h'"=g &
            H2 = H1 |^ g by Def21;
          A19: h"*h = 1_G by GROUP_1:def 6;
          Q' |^ h" = Q' by A16,A17,A18,GROUP_2:57; then
          Q' |^ (h"*h) = Q' |^ h by GROUP_3:72;
          hence Q' |^ h = Q' by A19,GROUP_3:73;
        end; then
        y in N by GROUP_3:160;
        hence y in the carrier of N by STRUCT_0:def 5;
      end; then
      A20: the carrier of P c= the carrier of N by TARSKI:def 3;
      A21: now let y be set;
        assume A22: y in the carrier of Q';
        ex h being Element of G st y = h & Q' |^ h = Q'
        proof
          set h = y;
          the carrier of Q' c= the carrier of G by GROUP_2:def 5; then
          reconsider h as Element of G by A22;
          take h;
          thus y = h;
          for g being Element of G holds g in Q' iff g in Q' |^ h
          proof
            let g be Element of G;
            hereby
              assume A23: g in Q';
              ex g' being Element of G st g = g' |^ h & g' in Q'
              proof
                set g' = h * g * h";
                take g';
                thus g' |^ h = h" * g' * h by GROUP_3:def 2
                .= h" * (h * (g * h")) * h by GROUP_1:def 4
                .= (h" * h) * (g * h") * h by GROUP_1:def 4
                .= 1_G * (g * h") * h by GROUP_1:def 6
                .= g * h" * h by GROUP_1:def 5
                .= g * (h" * h) by GROUP_1:def 4
                .= g * 1_G by GROUP_1:def 6
                .= g by GROUP_1:def 5;
                A24: h in Q' by A22,STRUCT_0:def 5; then
                A25: h" in Q' by GROUP_2:60;
                h * g in Q' by A23,A24,GROUP_2:59;
                hence g' in Q' by A25,GROUP_2:59;
              end;
              hence g in Q' |^ h by GROUP_3:70;
            end;
            assume g in Q' |^ h; then
            consider g' be Element of G such that
            A26: g = g' |^ h & g' in Q' by GROUP_3:70;
            A27: h in Q' by A22,STRUCT_0:def 5; then
            h" in Q' by GROUP_2:60; then
            h" * g' in Q' by A26,GROUP_2:59; then
            h" * g' * h  in Q' by A27,GROUP_2:59;
            hence g in Q' by A26,GROUP_3:def 2;
          end;
          hence Q' |^ h = Q' by GROUP_2:def 6;
        end; then
        y in N by GROUP_3:160;
        hence y in the carrier of N by STRUCT_0:def 5;
      end; then
      A28: the carrier of Q' c= the carrier of N by TARSKI:def 3;
      reconsider N as finite Group by GROUP_2:48;
      reconsider P''=P' as strict Subgroup of N by A20,GROUP_2:66;
      reconsider Q''=Q' as strict Subgroup of N by A28,GROUP_2:66;
      A29: P'' is_Sylow_p-subgroup_of_prime p by A10,Lm9;
      A30: Q'' is_Sylow_p-subgroup_of_prime p by A14,Lm9;
      P'',Q'' are_conjugated by A29,A30,Th14; then
      consider n be Element of N such that
      A31: P'' = Q'' |^ n by GROUP_3:def 11;
      n in Normalizator Q' by STRUCT_0:def 5; then
      consider n' be Element of G such that
      A32: n = n' & Q' |^ n' = Q' by GROUP_3:160;
      A33: now
        let y be set;
        hereby
          assume y in the carrier of Q' |^ n'; then
          y in carr(Q') |^ n' by GROUP_3:def 6; then
          consider q' be Element of G such that
          A34: y = q' |^ n' & q' in carr Q' by GROUP_3:50;
          reconsider q''=q' as Element of N by A21,A34;
          n'" = n" by A32,GROUP_2:57; then
          n'" * q' = n" * q'' by GROUP_2:52; then
          A35: n'" * q' * n' = n" * q'' * n by A32,GROUP_2:52;
          q' |^ n' = n'" * q' * n' by GROUP_3:def 2
          .= q'' |^ n by A35,GROUP_3:def 2; then
          y in carr(Q'') |^ n by A34,GROUP_3:50;
          hence y in the carrier of Q'' |^ n by GROUP_3:def 6;
        end;
        assume y in the carrier of Q'' |^ n; then
        y in carr(Q'') |^ n by GROUP_3:def 6; then
        consider q'' be Element of N such that
        A36: y = q'' |^ n & q'' in carr Q'' by GROUP_3:50;
        A37: the carrier of Q'' c= the carrier of G by GROUP_2:def 5;
        reconsider q'=q'' as Element of G by A36,A37;
        n'" = n" by A32,GROUP_2:57; then
        n'" * q' = n" * q'' by GROUP_2:52; then
        A38: n'" * q' * n' = n" * q'' * n by A32,GROUP_2:52;
        q' |^ n' = n'" * q' * n' by GROUP_3:def 2
        .= q'' |^ n by A38,GROUP_3:def 2; then
        y in carr(Q') |^ n' by A36,GROUP_3:50;
        hence y in the carrier of Q' |^ n' by GROUP_3:def 6;
      end; then
      A39: the carrier of Q' |^ n' = the carrier of Q'' |^ n by TARSKI:2;
      the carrier of Q'' |^ n c= the carrier of N by GROUP_2:def 5; then
      A40: [:the carrier of Q'' |^ n,the carrier of Q'' |^ n:] c=
        [:the carrier of N,the carrier of N:] by ZFMISC_1:119;
      A41: (the mult of G)||the carrier of Q'' |^ n
      = ((the mult of G)||the carrier of N)||the carrier of Q'' |^ n
        by A40,RELAT_1:103
      .= (the mult of N)||the carrier of Q'' |^ n by GROUP_2:def 5;
      the mult of Q' |^ n' = (the mult of G)||the carrier of Q'' |^ n
        by A39,GROUP_2:def 5
      .= the mult of Q'' |^ n by A41,GROUP_2:def 5;
      hence x=P' by A12,A13,A14,A31,A32,A33,TARSKI:2;
    end;
    assume A42: x=P';
    for h being Element of P holds P' = (T^h).P'
    proof
      let h be Element of P;
      reconsider P1=P' as Element of E;
      consider P2 be Element of E,
        H1,H2 be strict Subgroup of G, g be Element of G such that
      A43: P2=the_left_translation_of(h,p).P1 & P1=H1 & P2=H2 & h"=g &
        H2 = H1 |^ g by Def21;
      A44: P'=P1 & (T^h).P'=P2 by A43,Def22;
      A45: H1 is finite by GROUP_2:48;
      A46: H1 |^ g is finite by GROUP_2:48;
      A47: carr H1 c= carr (H1 |^ g)
      proof
        A48: g in H1 by A43,STRUCT_0:def 5;
        now let y be set;
          assume A49: y in carr H1; then
          reconsider h'=y as Element of G;
          A50: h' in H1 by A49,STRUCT_0:def 5;
          ex h being Element of G st y = h * g & h in g" * H1
          proof
            set h'' = h'*g";
            g" in H1 by A48,GROUP_2:60; then
            A51: h'' in H1 by A50,GROUP_2:59;
            take h'';
            thus h'' * g = h' * (g" * g) by GROUP_1:def 4
            .= h' * 1_G by GROUP_1:def 6
            .= y by GROUP_1:def 5;
            ex h being Element of G st h'' = g" * h & h in H1
            proof
              set h''' = g*h'';
              take h''';
              thus g" * h''' = g" * g * h'' by GROUP_1:def 4
              .= 1_G * h'' by GROUP_1:def 6
              .= h'' by GROUP_1:def 5;
              thus h''' in H1 by A48,A51,GROUP_2:59;
            end;
            hence h'' in g" * H1 by GROUP_2:125;
          end;
          hence y in g" * H1 * g by GROUP_2:34;
        end; then
        carr H1 c= g" * H1 * g by TARSKI:def 3;
        hence thesis by GROUP_3:71;
      end;
      card carr H1 = ord H1 by A45,GROUP_1:def 15
      .= ord(H1 |^ g) by A45,GROUP_3:79
      .= card carr(H1 |^ g) by A46,GROUP_1:def 15; then
      carr H1 = carr (H1 |^ g) by A47,CARD_FIN:1;
      hence P' = (T^h).P' by A43,A44,GROUP_2:68;
    end; then
    P' is_fixed_under T by Def12; then
    x in {x' where x' is Element of E: x' is_fixed_under T} by A42;
    hence x in the_fixed_points_of T by Def13;
  end; then
  A52: the_fixed_points_of T = {P'} by TARSKI:def 1;
  p is prime Element of NAT by ORDINAL1:def 13; then
  A53: p > 1 by NAT_4:12;
  card E mod p = card the_fixed_points_of T mod p by A2,Th11
  .= 1 mod p by A52,CARD_1:79;
  hence card the_sylow_p-subgroups_of_prime(p,G) mod p = 1 by A53,NAT_D:14;
  set G' = (Omega).G;
  set T' = the_left_operation_of(G',p);
  set K = the_strict_stabilizer_of(P',T');
  A54: Card the_orbit_of(P',T') = Index K by Th10;
  A55: now
    let y be set;
    thus y in the_orbit_of(P',T') implies y in E;
    assume y in E; then
    consider Q be Element of Subgroups G such that
    A56: y=Q & ex P being strict Subgroup of G
      st P = Q & P is_Sylow_p-subgroup_of_prime p;
    consider Q' be strict Subgroup of G such that
    A57: Q' = Q & Q' is_Sylow_p-subgroup_of_prime p by A56;
    reconsider P''=P' as strict Subgroup of G;
    P'',Q' are_conjugated by A10,A57,Th14; then
    consider g be Element of G such that
    A58: Q' = P'' |^ g by GROUP_3:def 11;
    reconsider g'=g" as Element of G';
    reconsider P1=P' as Element of E;
    consider P2 be Element of E,
      H1,H2 be strict Subgroup of G, g''' be Element of G such that
    A59: P2=the_left_translation_of(g',p).P1 & P1=H1 & P2=H2 & g'"=g''' &
      H2 = H1 |^ g''' by Def21;
    g'" = (g")" by GROUP_2:57 .= g by GROUP_1:19; then
    A60: Q' = (T'^g').P' by A58,A59,Def22;
    Q' in E by A57; then
    reconsider Q''=Q' as Element of E;
    y=Q'' & P',Q'' are_conjugated_under T' by A56,A57,A60,Def14;
    hence y in the_orbit_of(P',T');
  end;
  A61: G' is finite by GROUP_2:48;
  reconsider K as Subgroup of G';
  Left_Cosets K is finite by A61,GROUP_2:164; then
  A62: ex B being finite set st B = Left_Cosets K & index K = card B
    by GROUP_2:176;
  A63: card E = index K by A54,A55,A62,TARSKI:2;
  ord G = ord G' * index G' by GROUP_2:177
  .= ord G' * 1 by GROUP_2:180; then
  ord G = ord K * index K by A61,GROUP_2:177;
  hence card the_sylow_p-subgroups_of_prime(p,G) divides ord G
    by A63,NAT_D:def 3;
end;

begin :: Appendix

theorem
  for X,Y being non empty set holds
  Card {[:X,{y}:] where y is Element of Y: not contradiction} = Card Y by Lm3;

theorem
  for n,m,r being natural number, p being prime (natural number)
  st n = p |^ r * m & not p divides m
  holds (n choose p|^r) mod p <> 0 by Lm5;

theorem
  for n being natural number st n > 0 holds ord INT.Group(n) = n by Lm2;

theorem
  for G being Group, A being non empty Subset of G, g being Element of G holds
  Card A = Card (A * g) by Lm7;
