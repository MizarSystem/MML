:: The Sylow Theorems
::  by Marco Riccardi
::
:: Received August 13, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies ARYTM_1, ARYTM_3, FINSEQ_1, RELAT_1, FUNCT_1, RLVECT_1, BOOLE,
      FINSEQ_2, FINSEQ_4, COMPLEX1, BINOP_1, GROUP_1, REALSET1, NAT_1, TARSKI,
      CARD_1, GROUP_2, FUNCOP_1, FUNCT_2, FINSET_1, RLSUB_1, SETFAM_1, EQREL_1,
      GROUP_3, GRAPH_1, ARYTM, ORDINAL2, GROUP_4, NEWTON, GR_CY_1, CARD_FIN,
      FILTER_0, NAT_3, GROUP_9, GROUP_10, ALGSTR_0, XREAL_0, VECTSP_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, CARD_1, NUMBERS, XCMPLX_0,
      ZFMISC_1, XXREAL_0, XREAL_0, INT_2, NAT_1, NAT_D, FINSET_1, RELAT_1,
      REALSET1, FUNCT_1, RELSET_1, FUNCT_2, FINSEQ_1, RVSUM_1, STRUCT_0,
      ALGSTR_0, GROUP_1, GROUP_2, GROUP_3, EQREL_1, FUNCOP_1, WSIERP_1, NEWTON,
      DOMAIN_1, GR_CY_1, ORDINAL1, GROUP_4, CARD_FIN, PARTFUN1, NAT_3, GROUP_9,
      TOPGRP_1;
 constructors SETFAM_1, WELLORD2, XXREAL_0, NAT_D, EQREL_1, BINARITH, WSIERP_1,
      REALSET2, UPROOTS, GR_CY_1, GROUP_4, CARD_FIN, NAT_3, TOPGRP_1, SEQ_1,
      DYNKIN, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, ORDINAL1, RELSET_1, FINSET_1,
      XXREAL_0, XREAL_0, NAT_1, INT_1, CARD_1, FINSEQ_1, NEWTON, STRUCT_0,
      GROUP_2, GROUP_1, GROUP_3, GR_CY_1, FUNCT_2, NAT_3, REALSET1, VALUED_0,
      DYNKIN;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions GROUP_1, GROUP_2, TARSKI, REALSET1, RELAT_1, WELLORD2, STRUCT_0,
      FUNCT_2;
 theorems FINSEQ_1, GROUP_2, GROUP_3, TARSKI, GROUP_6, FINSEQ_2, FUNCT_1,
      FUNCT_2, RELAT_1, XBOOLE_0, XBOOLE_1, NAT_1, GROUP_1, XREAL_1, RELSET_1,
      PARTFUN1, FINSEQ_3, INT_1, ZFMISC_1, CARD_1, CARD_2, FINSET_1, XCMPLX_1,
      ORDINAL1, FINSEQ_4, XXREAL_0, STRUCT_0, NAT_D, GR_CY_1, NEWTON, CARD_FIN,
      WEDDWITT, WELLORD2, EQREL_1, FUNCOP_1, RVSUM_1, PEPIN, NAT_4, GROUP_8,
      NAT_3, GROUP_9, GR_CY_2, TOPGRP_1, VALUED_1, XREAL_0;
 schemes FUNCT_1, FINSEQ_1, NAT_1, FUNCT_2, SUBSET_1, CLASSES1;

begin :: Group Operating on a Set

notation
  let S be non empty 1-sorted;
  let E be set;
  let A be Action of (the carrier of S), E;
  let s be Element of S;
  synonym A^s for A.s;
end;

definition
  let S be non empty 1-sorted;
  let E be set;
  let A be Action of (the carrier of S), E;
  let s be Element of S;
  redefine func A^s -> Function of E, E;
  correctness
  proof
    consider f be Function such that
A1: A.s = f and
A2: dom f = E & rng f c= E by FUNCT_2:def 2;
    reconsider f as Function of E,E by A2,FUNCT_2:4;
    now
      take f;
      thus f = A.s by A1;
    end;
    hence thesis;
  end;
end;

definition
  let S be unital (non empty multMagma);
  let E be set;
  let A be Action of (the carrier of S), E;
  attr A is being_left_operation means
  :Def1:
  A^(1_S) = id E & for s1,s2 being
  Element of S holds A^(s1*s2) = A^s1 * (A^s2);
end;

registration
  let S be unital (non empty multMagma);
  let E be set;
  cluster being_left_operation Action of (the carrier of S), E;
  correctness
  proof
    reconsider IT=[:the carrier of S,{id E}:] as Action of (the carrier of S),
    E by GROUP_9:62;
    take IT;
A1: now
      let s be Element of S;
      IT c= [:the carrier of S,{id E}:];
      then reconsider IT'=IT as Relation of the carrier of S,{id E};
      now
        let x be set;
        assume
A2:     x in the carrier of S;
        consider y be set such that
A3:     y=id E;
        take y;
        y in {id E} by A3,TARSKI:def 1;
        hence [x,y] in [:the carrier of S,{id E}:] by A2,ZFMISC_1:def 2;
      end;
      then dom IT = the carrier of S by RELSET_1:22;
      then
A4:   IT.s in rng IT by FUNCT_1:12;
      rng IT' c= {id E};
      hence IT.s = id E by A4,TARSKI:def 1;
    end;
    hence IT^(1_S) = id E;
    let s1,s2 be Element of S;
    IT^s1 = id E & IT^s2 = id E by A1;
    then IT^s1 * (IT^s2) = id E by PARTFUN1:36;
    hence thesis by A1;
  end;
end;

:: ALG I.5.1 DEF 1

definition
  let S be unital (non empty multMagma);
  let E be set;
  mode LeftOperation of S, E is being_left_operation Action of (the carrier of
    S), E;
end;

scheme
  ExLeftOperation {E()->set, S()->Group-like (non empty multMagma), f(Element
  of S())->Function of E(),E()}: ex T being LeftOperation of S(), E() st for s
  being Element of S() holds T.s = f(s)
provided
A1: f(1_S()) = id E() and
A2: for s1,s2 being Element of S() holds f(s1*s2) = f(s1) * f(s2)
proof
  set T = {[s,f(s)] where s is Element of S(): not contradiction};
A3: now
    let x be set;
    assume
    x in T;
    then consider s be Element of S() such that
A4: x = [s, f(s)];
    f(s) in Funcs(E(),E()) by FUNCT_2:12;
    hence x in [:the carrier of S(),Funcs(E(),E()):] by A4,ZFMISC_1:def 2;
  end;
  now
    let x,y1,y2 be set;
    assume
    [x,y1] in T;
    then consider s1 be Element of S() such that
A5: [x,y1] = [s1,f(s1)];
A6: x=s1 by A5,ZFMISC_1:33;
    assume
    [x,y2] in T;
    then consider s2 be Element of S() such that
A7: [x,y2] = [s2,f(s2)];
    x=s2 by A7,ZFMISC_1:33;
    hence y1 = y2 by A5,A7,A6,ZFMISC_1:33;
  end;
  then reconsider
  T as Function-like Relation of (the carrier of S()), Funcs(E(),E(
  )) by A3,FUNCT_1:def 1,TARSKI:def 3;
  now
    let x be set;
    hereby
      assume
      x in the carrier of S();
      then reconsider s=x as Element of S();
      reconsider y=f(s) as set;
      take y;
      thus [x,y] in T;
    end;
    given y be set such that
A8: [x,y] in T;
    consider s be Element of S() such that
A9: [x,y] = [s,f(s)] by A8;
    x=s by A9,ZFMISC_1:33;
    hence x in the carrier of S();
  end;
  then the carrier of S() = dom T by RELAT_1:def 4;
  then reconsider T as Action of (the carrier of S()), E() by FUNCT_2:def 1;
A10: now
    let s1,s2 be Element of S();
    s1 in the carrier of S();
    then s1 in dom T by FUNCT_2:def 1;
    then [s1,T.s1] in T by FUNCT_1:8;
    then consider s1' be Element of S() such that
A11: [s1,T.s1] = [s1',f(s1')];
A12: s1=s1' & f(s1')=T.s1 by A11,ZFMISC_1:33;
    s2 in the carrier of S();
    then s2 in dom T by FUNCT_2:def 1;
    then [s2,T.s2] in T by FUNCT_1:8;
    then consider s2' be Element of S() such that
A13: [s2,T.s2] = [s2',f(s2')];
    s1*s2 in the carrier of S();
    then s1*s2 in dom T by FUNCT_2:def 1;
    then [s1*s2,T.(s1*s2)] in T by FUNCT_1:8;
    then consider s12' be Element of S() such that
A14: [s1*s2,T.(s1*s2)] = [s12',f(s12')];
A15: s2=s2' & f(s2')=T.s2 by A13,ZFMISC_1:33;
    thus T^(s1*s2) = f(s12') by A14,ZFMISC_1:33
      .= f(s1*s2) by A14,ZFMISC_1:33
      .= T^s1 * (T^s2) by A2,A12,A15;
  end;
A16: dom T = the carrier of S() by FUNCT_2:def 1;
  then [1_S(),T.(1_S())] in T by FUNCT_1:8;
  then consider s' be Element of S() such that
A17: [1_S(),T.(1_S())] = [s',f(s')];
  1_S() = s' & T.(1_S()) = f(s') by A17,ZFMISC_1:33;
  then reconsider T as LeftOperation of S(), E() by A1,A10,Def1;
  take T;
  thus for s being Element of S() holds T.s = f(s)
  proof
    let s be Element of S();
    [s,T.s] in T by A16,FUNCT_1:8;
    then consider s' be Element of S() such that
A18: [s,T.s] = [s',f(s')];
    s=s' by A18,ZFMISC_1:33;
    hence thesis by A18,ZFMISC_1:33;
  end;
end;

theorem Th1:
  for E being non empty set, S being Group-like (non empty
multMagma), s being Element of S, LO being LeftOperation of S, E holds LO^s is
  one-to-one
proof
  let E be non empty set;
  let S be Group-like (non empty multMagma);
  let s be Element of S;
  let LO be LeftOperation of S, E;
  consider e be Element of S such that
A1: for h being Element of S holds h * e = h & e * h = h & ex g being
  Element of S st h * g = e & g * h = e by GROUP_1:def 3;
  consider s' be Element of S such that
  s * s' = e and
A2: s' * s = e by A1;
  LO^s' in Funcs(E,E) by FUNCT_2:12;
  then
A3: ex f being Function st LO^s' = f & dom f = E & rng f c= E by
  FUNCT_2
  :def 2;
  LO^s in Funcs(E,E) by FUNCT_2:12;
  then
A4: ex f being Function st LO^s = f & dom f = E & rng f c= E by
  FUNCT_2:
  def 2;
  id E = LO^(1_S) by Def1
    .= LO^(s' * s) by A1,A2,GROUP_1:10
    .= (LO^s') * (LO^s) by Def1;
  hence thesis by A4,A3,FUNCT_1:47;
end;

:: left translation
:: ALG I.3.1

notation
  let S be non empty multMagma;
  let s be Element of S;
  synonym the_left_translation_of s for s*;
end;

definition
  let S be Group-like associative (non empty multMagma);
  func the_left_operation_of S -> LeftOperation of S, the carrier of S means
  :
  Def2: for s being Element of S holds it.s = the_left_translation_of s;
  existence
  proof
    deffunc f(Element of S) = the_left_translation_of $1;
    set E = the carrier of S;
A1: for s1,s2 being Element of S holds f(s1*s2)=f(s1)*f(s2)
    proof
      let s1,s2 be Element of S;
      set f12 = the_left_translation_of(s1*s2);
      set f1 = the_left_translation_of(s1);
      set f2 = the_left_translation_of(s2);
      f1 in Funcs(E,E) by FUNCT_2:12;
      then
A2:   ex f being Function st f1=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
      f2 in Funcs(E,E) by FUNCT_2:12;
      then
A3:   ex f being Function st f2=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
      f12 in Funcs(E,E) by FUNCT_2:12;
      then
A4:   ex f being Function st f12=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
A5:   now
        let x be set;
        hereby
          assume
A6:       x in dom f12;
          hence x in dom f2 by A3;
          f2.x in rng f2 by A3,A6,FUNCT_1:12;
          hence f2.x in dom f1 by A2;
        end;
        assume that
A7:     x in dom f2 and
        f2.x in dom f1;
        thus x in dom f12 by A4,A7;
      end;
      now
        let x be set;
        assume
A8:     x in dom f12;
        then reconsider s1'=x as Element of S;
        f2.x in rng f2 by A3,A8,FUNCT_1:12;
        then reconsider s1''=f2.x as Element of S;
        thus f12.x = (s1*s2) * s1' by TOPGRP_1:def 1
          .= s1 * (s2*s1') by GROUP_1:def 4
          .= s1 * s1'' by TOPGRP_1:def 1
          .= f1.(f2.x) by TOPGRP_1:def 1;
      end;
      hence thesis by A5,FUNCT_1:20;
    end;
A9: f(1_S) = id E
    proof
      set f = the_left_translation_of(1_S);
A10:  now
        let x be set;
        assume
        x in E;
        then reconsider s1=x as Element of S;
        f.s1 = 1_S * s1 by TOPGRP_1:def 1;
        hence f.x = x by GROUP_1:def 5;
      end;
      f in Funcs(E,E) by FUNCT_2:12;
      then ex f' being Function st f=f' & dom f' = E & rng f' c= E by
      FUNCT_2:def 2;
      hence thesis by A10,FUNCT_1:34;
    end;
    ex T being LeftOperation of S, E st for s being Element of S holds T.
    s = f(s) from ExLeftOperation(A9,A1);
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of S, the carrier of S such that
A11: for s being Element of S holds T1.s = the_left_translation_of s and
A12: for s being Element of S holds T2.s = the_left_translation_of s;
    let x be Element of S;
    thus T1.x = the_left_translation_of x by A11
      .= T2.x by A12;
  end;
end;

definition
  let E be set;
  let n be set;
  func the_subsets_of_card(n, E) -> Subset-Family of E equals
  {X where X is
  Subset of E: card X = n};
  correctness
  proof
    set SF = {X where X is Subset of E: card X = n};
    now
      let x be set;
      assume
      x in SF;
      then ex X being Subset of E st X = x & card X = n;
      hence x in bool E;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;

registration
  let E be finite set;
  let n be set;
  cluster the_subsets_of_card(n, E) -> finite;
  correctness;
end;

theorem Th2:
  for n being natural number, E being non empty set st card n c=
  card E holds the_subsets_of_card(n, E) is non empty
proof
  let n be natural number;
  let E be non empty set;
  reconsider n'=n as Element of NAT by ORDINAL1:def 13;
  assume
  card n c= card E;
  then consider f be Function such that
A1: f is one-to-one and
A2: dom f = n and
A3: rng f c= E by CARD_1:26;
  set X = f .: n;
A4: rng f = X by A2,RELAT_1:146;
  then n,X are_equipotent by A1,A2,WELLORD2:def 4;
  then card X = n' by CARD_1:def 5;
  then X in {X' where X' is Subset of E: card X' = n} by A3,A4;
  hence thesis;
end;

theorem Th3:
  for E being non empty finite set, k being Element of NAT, x1,x2
being set st x1<>x2 holds card Choose(E,k,x1,x2) = card the_subsets_of_card(k,E
  )
proof
  let E be non empty finite set;
  let k be Element of NAT;
  let x1,x2 be set;
  set f = {[fX,X] where fX is Function of E,{x1,x2}, X is Subset of E: card (
  fX"{x1}) = k & fX"{x1} = X};
  now
    let x be set;
    assume
    x in f;
    then consider y be Function of E,{x1,x2}, z be Subset of E such that
A1: x = [y,z] and
    card (y"{x1}) = k and
    y"{x1} = z;
    reconsider y,z as set;
    take y,z;
    thus x = [y,z] by A1;
  end;
  then reconsider f as Relation-like set by RELAT_1:def 1;
  now
    let x,y1,y2 be set;
    assume
    [x,y1] in f;
    then consider fX1 be Function of E,{x1,x2}, X1 be Subset of E such that
A2: [x,y1]=[fX1,X1] and
    card (fX1"{x1}) = k and
A3: fX1"{x1} = X1;
A4: x=fX1 by A2,ZFMISC_1:33;
    assume
    [x,y2] in f;
    then consider fX2 be Function of E,{x1,x2}, X2 be Subset of E such that
A5: [x,y2]=[fX2,X2] and
    card (fX2"{x1}) = k and
A6: fX2"{x1} = X2;
    x=fX2 by A5,ZFMISC_1:33;
    hence y1 = y2 by A2,A3,A5,A6,A4,ZFMISC_1:33;
  end;
  then reconsider f as Function by FUNCT_1:def 1;
  assume
A7: x1<>x2;
  now
    let y1,y2 be set;
    assume that
A8: y1 in dom f and
A9: y2 in dom f;
    assume
A10: f.y1 = f.y2;
    [y2,f.y2] in f by A9,FUNCT_1:8;
    then consider fX2 be Function of E,{x1,x2}, X2 be Subset of E such that
A11: [y2,f.y2]=[fX2,X2] and
    card (fX2"{x1}) = k and
A12: fX2"{x1} = X2;
A13: y2=fX2 by A11,ZFMISC_1:33;
    [y1,f.y1] in f by A8,FUNCT_1:8;
    then consider fX1 be Function of E,{x1,x2}, X1 be Subset of E such that
A14: [y1,f.y1]=[fX1,X1] and
    card (fX1"{x1}) = k and
A15: fX1"{x1} = X1;
    dom fX1 = E by FUNCT_2:def 1;
    then
A16: dom fX1 = dom fX2 by FUNCT_2:def 1;
    f.y1=X1 by A14,ZFMISC_1:33;
    then
A17: fX1"{x1} = fX2"{x1} by A10,A15,A11,A12,ZFMISC_1:33;
A18: for z being set st z in dom fX1 holds fX1.z = fX2.z
    proof
      assume
      ex z being set st z in dom fX1 & fX1.z <> fX2.z;
      then consider z be set such that
A19:  z in dom fX1 and
A20:  fX1.z <> fX2.z;
A21:  fX1.z in {x1,x2} by A19,FUNCT_2:7;
      fX2.z in {x1,x2} by A19,FUNCT_2:7;
      then
A22:  fX2.z = x1 or fX2.z = x2 by TARSKI:def 2;
      per cases by A20,A21,A22,TARSKI:def 2;
      suppose
A23:    fX1.z = x1 & fX2.z = x2;
        then
A24:    fX1.z in {x1} by TARSKI:def 1;
        [z,fX1.z] in fX1 by A19,FUNCT_1:8;
        then z in fX2"{x1} by A17,A24,RELAT_1:def 14;
        then consider z' be set such that
A25:    [z,z'] in fX2 and
A26:    z' in {x1} by RELAT_1:def 14;
        z' = fX2.z by A25,FUNCT_1:8;
        hence contradiction by A7,A23,A26,TARSKI:def 1;
      end;
      suppose
A27:    fX1.z = x2 & fX2.z = x1;
        then
A28:    fX2.z in {x1} by TARSKI:def 1;
        [z,fX2.z] in fX2 by A16,A19,FUNCT_1:8;
        then z in fX1"{x1} by A17,A28,RELAT_1:def 14;
        then consider z' be set such that
A29:    [z,z'] in fX1 and
A30:    z' in {x1} by RELAT_1:def 14;
        z' = fX1.z by A29,FUNCT_1:8;
        hence contradiction by A7,A27,A30,TARSKI:def 1;
      end;
    end;
    y1=fX1 by A14,ZFMISC_1:33;
    hence y1 = y2 by A13,A16,A18,FUNCT_1:9;
  end;
  then
A31: f is one-to-one by FUNCT_1:def 8;
  for y being set holds y in the_subsets_of_card(k,E) iff ex x being set
  st [x,y] in f
  proof
    let y be set;
    hereby
      assume
      y in the_subsets_of_card(k,E);
      then consider X be Subset of E such that
A32:  y=X & card X = k;
      defpred P[set,set] means ($1 in X & $2=x1) or (not $1 in X & $2=x2);
A33:  for z1 being Element of E ex z2 being Element of {x1,x2} st P[z1,z2
      ]
      proof
        let z1 be Element of E;
        per cases;
        suppose
A34:      z1 in X;
          reconsider z2=x1 as Element of {x1,x2} by TARSKI:def 2;
          take z2;
          thus thesis by A34;
        end;
        suppose
A35:      not z1 in X;
          reconsider z2=x2 as Element of {x1,x2} by TARSKI:def 2;
          take z2;
          thus thesis by A35;
        end;
      end;
      ex f being Function of E,{x1,x2} st for z being Element of E holds
      P[z,f.z] from FUNCT_2:sch 3(A33);
      then consider f' be Function of E,{x1,x2} such that
A36:  for z being Element of E holds P[z,f'.z];
      reconsider x=f' as set;
      take x;
      now
        let z be set;
        hereby
          assume
A37:      z in X;
          then reconsider z'=z as Element of E;
          set z''=f'.z';
          z in E by A37;
          then z in dom f' by FUNCT_2:def 1;
          then
A38:      [z,f'.z] in f' by FUNCT_1:8;
          f'.z'=x1 by A36,A37;
          then z'' in {x1} by TARSKI:def 1;
          hence z in f'"{x1} by A38,RELAT_1:def 14;
        end;
        assume
        z in f'"{x1};
        then consider z' be set such that
A39:    [z,z'] in f' and
A40:    z' in {x1} by RELAT_1:def 14;
        z in dom f' by A39,RELAT_1:def 4;
        then reconsider z''=z as Element of E;
        z'=x1 by A40,TARSKI:def 1;
        then f'.z'' = x1 by A39,FUNCT_1:8;
        hence z in X by A7,A36;
      end;
      then X = f'"{x1} by TARSKI:2;
      hence [x,y] in f by A32;
    end;
    given x be set such that
A41: [x,y] in f;
    consider fX be Function of E,{x1,x2}, X be Subset of E such that
A42: [x,y]=[fX,X] and
A43: card (fX"{x1}) = k & fX"{x1} = X by A41;
    y = X by A42,ZFMISC_1:33;
    hence thesis by A43;
  end;
  then
A44: rng f = the_subsets_of_card(k,E) by RELAT_1:def 5;
  for x being set holds x in Choose(E,k,x1,x2) iff ex y being set st [x,y
  ] in f
  proof
    let x be set;
    thus x in Choose(E,k,x1,x2) implies ex y being set st [x,y] in f
    proof
      assume
      x in Choose(E,k,x1,x2);
      then consider fX be Function of E,{x1,x2} such that
A45:  fX = x & card (fX"{x1})=k by CARD_FIN:def 1;
      set y = fX"{x1};
      take y;
      thus thesis by A45;
    end;
    thus (ex y being set st [x,y] in f) implies x in Choose(E,k,x1,x2)
    proof
      given y be set such that
A46:  [x,y] in f;
      consider fX be Function of E,{x1,x2}, X be Subset of E such that
A47:  [x,y]=[fX,X] and
A48:  card (fX"{x1}) = k and
      fX"{x1} = X by A46;
      x=fX by A47,ZFMISC_1:33;
      hence thesis by A48,CARD_FIN:def 1;
    end;
  end;
  then dom f = Choose(E,k,x1,x2) by RELAT_1:def 4;
  then Choose(E,k,x1,x2), the_subsets_of_card(k,E) are_equipotent by A31,
  A44,WELLORD2:def 4;
  hence thesis by CARD_1:21;
end;

definition
  let E be non empty set;
  let n be natural number;
  let S be Group-like (non empty multMagma);
  let s be Element of S;
  let LO be LeftOperation of S, E;
  assume
A1: card n c= card E;
  func the_extension_of_left_translation_of(n,s,LO) -> Function of
  the_subsets_of_card(n, E), the_subsets_of_card(n, E) means
  :Def4:
  for X being
  Element of the_subsets_of_card(n, E) holds it.X = (LO^s) .: X;
  existence
  proof
    set EE = the_subsets_of_card(n, E);
    set f = {[X,(LO^s) .: X] where X is Element of EE: not contradiction};
A2: now
      let x,y1,y2 be set;
      assume
      [x,y1] in f;
      then consider X1 be Element of EE such that
A3:   [x,y1] = [X1,(LO^s) .: X1];
A4:   x = X1 by A3,ZFMISC_1:33;
      assume
      [x,y2] in f;
      then consider X2 be Element of EE such that
A5:   [x,y2] = [X2,(LO^s) .: X2];
      x = X2 by A5,ZFMISC_1:33;
      hence y1 = y2 by A3,A5,A4,ZFMISC_1:33;
    end;
    now
      let x be set;
      assume
      x in f;
      then consider X be Element of EE such that
A6:   x = [X,(LO^s) .: X];
      reconsider y=X,z=(LO^s) .: X as set;
      take y,z;
      thus x = [y,z] by A6;
    end;
    then reconsider f as Function by A2,FUNCT_1:def 1,RELAT_1:def 1;
    now
      let x be set;
      hereby
        assume
        x in EE;
        then reconsider X = x as Element of EE;
        reconsider y = (LO^s) .: X as set;
        take y;
        thus [x,y] in f;
      end;
      given y be set such that
A7:   [x,y] in f;
      consider X be Element of EE such that
A8:   [x,y] = [X,(LO^s) .: X] by A7;
A9:   x = X by A8,ZFMISC_1:33;
      EE is not empty by A1,Th2;
      hence x in EE by A9;
    end;
    then
A10: dom f = EE by RELAT_1:def 4;
    now
      let Y be set;
      assume
      Y in rng f;
      then consider X1 be set such that
A11:  [X1,Y] in f by RELAT_1:def 5;
      consider X2 be Element of EE such that
A12:  [X1,Y] = [X2,(LO^s) .: X2] by A11;
      set fe = (LO^s)|X2;
      LO^s is one-to-one by Th1;
      then
A13:  fe is one-to-one by FUNCT_1:84;
      EE is not empty by A1,Th2;
      then
A14:  X2 in EE;
      then
A15:  ex X being Subset of E st X=X2 & card X = n;
      LO^s in Funcs(E,E) by FUNCT_2:12;
      then ex f being Function st LO^s = f & dom f = E & rng f c= E by
      FUNCT_2:def 2;
      then
A16:  dom fe = X2 by A14,RELAT_1:91;
A17:  Y = (LO^s) .: X2 by A12,ZFMISC_1:33;
      then rng fe = Y by RELAT_1:148;
      then X2,Y are_equipotent by A13,A16,WELLORD2:def 4;
      then card Y = n by A15,CARD_1:21;
      hence Y in EE by A17;
    end;
    then rng f c= EE by TARSKI:def 3;
    then reconsider f as Function of EE,EE by A10,FUNCT_2:4;
    take f;
    thus for X being Element of EE holds f.X = (LO^s) .: X
    proof
      let X be Element of EE;
      EE is non empty by A1,Th2;
      then [X,f.X] in f by A10,FUNCT_1:8;
      then consider X' be Element of EE such that
A18:  [X,f.X] = [X',(LO^s) .: X'];
      X = X' by A18,ZFMISC_1:33;
      hence thesis by A18,ZFMISC_1:33;
    end;
  end;
  uniqueness
  proof
    set EE = the_subsets_of_card(n, E);
    let IT1, IT2 be Function of EE,EE such that
A19: for X being Element of EE holds IT1.X = (LO^s) .: X and
A20: for X being Element of EE holds IT2.X = (LO^s) .: X;
    let x be Element of EE;
    thus IT1.x = (LO^s) .: x by A19
      .= IT2.x by A20;
  end;
end;

definition
  let E be non empty set;
  let n be natural number;
  let S be Group-like (non empty multMagma);
  let LO be LeftOperation of S, E;
  assume
A1: card n c= card E;
  func the_extension_of_left_operation_of(n,LO) -> LeftOperation of S,
  the_subsets_of_card(n, E) means
  :Def5:
  for s being Element of S holds it.s =
  the_extension_of_left_translation_of(n,s,LO);
  existence
  proof
    deffunc f(Element of S)=the_extension_of_left_translation_of(n,$1,LO);
    set EE=the_subsets_of_card(n, E);
A2: for s1,s2 being Element of S holds f(s1*s2)=f(s1)*f(s2)
    proof
      let s1,s2 be Element of S;
      set f12 = the_extension_of_left_translation_of(n,s1*s2,LO);
      set f1 = the_extension_of_left_translation_of(n,s1,LO);
      set f2 = the_extension_of_left_translation_of(n,s2,LO);
      f1 in Funcs(EE,EE) by FUNCT_2:12;
      then
A3:   ex f being Function st f1=f & dom f = EE & rng f c= EE by
      FUNCT_2:def 2;
      f2 in Funcs(EE,EE) by FUNCT_2:12;
      then
A4:   ex f being Function st f2=f & dom f = EE & rng f c= EE by
      FUNCT_2
      :def 2;
      f12 in Funcs(EE,EE) by FUNCT_2:12;
      then
A5:   ex f being Function st f12=f & dom f = EE & rng f c= EE by
      FUNCT_2:def 2;
A6:   now
        let x be set;
        hereby
          assume
A7:       x in dom f12;
          hence x in dom f2 by A4;
          f2.x in rng f2 by A4,A7,FUNCT_1:12;
          hence f2.x in dom f1 by A3;
        end;
        assume that
A8:     x in dom f2 and
        f2.x in dom f1;
        thus x in dom f12 by A5,A8;
      end;
      now
        let x be set;
        assume
A9:     x in dom f12;
        then reconsider X1=x as Element of EE;
        f2.x in rng f2 by A4,A9,FUNCT_1:12;
        then reconsider X2=f2.x as Element of EE;
        thus f12.x = (LO^(s1*s2)) .: X1 by A1,Def4
          .= ((LO^s1)*(LO^s2)) .: X1 by Def1
          .= (LO^s1) .: ((LO^s2) .: X1) by RELAT_1:159
          .= (LO^s1) .: X2 by A1,Def4
          .= f1.(f2.x) by A1,Def4;
      end;
      hence thesis by A6,FUNCT_1:20;
    end;
A10: f(1_S) = id EE
    proof
      set f = the_extension_of_left_translation_of(n,1_S,LO);
A11:  now
        let x be set;
        assume
        x in EE;
        then reconsider X=x as Element of EE;
        EE is non empty by A1,Th2;
        then X in EE;
        then consider X' be Subset of E such that
A12:    X'=X and
        card X' = n;
        f.X=(LO^1_S) .: X by A1,Def4;
        then f.x = (id E) .: X' by A12,Def1
          .= X' by FUNCT_1:162;
        hence f.x = x by A12;
      end;
      f in Funcs(EE,EE) by FUNCT_2:12;
      then ex f' being Function st f=f' & dom f' = EE & rng f' c= EE by
      FUNCT_2:def 2;
      hence thesis by A11,FUNCT_1:34;
    end;
    ex T being LeftOperation of S, EE st for s being Element of S holds T
    .s = f(s) from ExLeftOperation(A10,A2);
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of S, the_subsets_of_card(n, E) such that
A13: for s being Element of S holds T1.s =
    the_extension_of_left_translation_of(n,s,LO) and
A14: for s being Element of S holds T2.s =
    the_extension_of_left_translation_of(n,s,LO);
    let x be Element of S;
    thus T1.x = the_extension_of_left_translation_of(n,x,LO) by A13
      .= T2.x by A14;
  end;
end;

definition
  let S be non empty multMagma;
  let s be Element of S;
  let Z be non empty set;
  func the_left_translation_of(s,Z) -> Function of [:the carrier of S,Z:],[:
  the carrier of S,Z:] means
  :Def6:
  for z1 being Element of [:the carrier of S,Z
:] holds ex z2 being Element of [:the carrier of S,Z:], s1,s2 being Element of
  S, z being Element of Z st z2 = it.z1 & s2 = s * s1 & z1=[s1,z] & z2=[s2,z];
  existence
  proof
    set E = [:the carrier of S,Z:];
    set f = {[z1,z2] where z1,z2 is Element of [:the carrier of S,Z:]: ex s1,
s2 being Element of S, z being Element of Z st s2 = s * s1 & z1=[s1,z] & z2=[s2
    ,z]};
A1: now
      let x,y1,y2 be set;
      assume
      [x,y1] in f;
      then consider z1',z2' be Element of E such that
A2:   [x,y1] = [z1',z2'] and
A3:   ex s1,s2 being Element of S, z being Element of Z st s2 = s * s1
      & z1'=[s1,z] & z2'=[s2,z];
      consider s1',s2' be Element of S, z' be Element of Z such that
A4:   s2' = s * s1' and
A5:   z1'=[s1',z'] and
A6:   z2'=[s2',z'] by A3;
      assume
      [x,y2] in f;
      then consider z1'',z2'' be Element of E such that
A7:   [x,y2] = [z1'',z2''] and
A8:   ex s1,s2 being Element of S, z being Element of Z st s2 = s *
      s1 & z1''=[s1,z] & z2''=[s2,z];
      consider s1'',s2'' be Element of S, z'' be Element of Z such that
A9:   s2'' = s * s1'' and
A10:  z1''=[s1'',z''] and
A11:  z2''=[s2'',z''] by A8;
      x = z1'' by A7,ZFMISC_1:33;
      then [s1',z'] = [s1'',z''] by A2,A5,A10,ZFMISC_1:33;
      then s1'=s1'' & z'=z'' by ZFMISC_1:33;
      hence y1 = y2 by A2,A4,A5,A6,A7,A9,A10,A11,ZFMISC_1:33;
    end;
    now
      let x be set;
      assume
      x in f;
      then consider z1,z2 be Element of E such that
A12:  x = [z1,z2] and
      ex s1,s2 being Element of S, z being Element of Z st s2 = s * s1 &
      z1=[s1,z] & z2=[s2,z];
      reconsider y=z1,z=z2 as set;
      take y,z;
      thus x = [y,z] by A12;
    end;
    then reconsider f as Function by A1,FUNCT_1:def 1,RELAT_1:def 1;
    now
      let x be set;
      hereby
        assume
        x in E;
        then consider s1,z be set such that
A13:    s1 in the carrier of S and
A14:    z in Z and
A15:    x = [s1,z] by ZFMISC_1:def 2;
        reconsider z as Element of Z by A14;
        reconsider s1 as Element of S by A13;
        set s2 = s * s1;
        reconsider y = [s2,z] as set;
        take y;
        x=[s1,z] by A15;
        hence [x,y] in f;
      end;
      given y be set such that
A16:  [x,y] in f;
      consider z1,z2 be Element of E such that
A17:  [x,y] = [z1,z2] and
      ex s1,s2 being Element of S, z being Element of Z st s2 = s * s1 &
      z1=[s1,z] & z2=[s2,z] by A16;
      x = z1 by A17,ZFMISC_1:33;
      hence x in E;
    end;
    then
A18: dom f = E by RELAT_1:def 4;
    now
      let y be set;
      assume
      y in rng f;
      then consider x be set such that
A19:  [x,y] in f by RELAT_1:def 5;
      consider z1,z2 be Element of E such that
A20:  [x,y] = [z1,z2] and
      ex s1,s2 being Element of S, z being Element of Z st s2 = s * s1 &
      z1=[s1,z] & z2=[s2,z] by A19;
      y = z2 by A20,ZFMISC_1:33;
      hence y in E;
    end;
    then rng f c= E by TARSKI:def 3;
    then reconsider f as Function of E,E by A18,FUNCT_2:4;
    take f;
    thus for z1 being Element of [:the carrier of S,Z:] holds ex z2 being
Element of [:the carrier of S,Z:], s1,s2 being Element of S, z being Element of
    Z st z2 = f.z1 & s2 = s * s1 & z1=[s1,z] & z2=[s2,z]
    proof
      let z1 be Element of [:the carrier of S,Z:];
      set z2 = f.z1;
      [z1,z2] in f by A18,FUNCT_1:8;
      then consider z1',z2' be Element of [:the carrier of S,Z:] such that
A21:  [z1,z2]=[z1',z2'] and
A22:  ex s1,s2 being Element of S, z being Element of Z st s2 = s *
      s1 & z1'=[s1,z] & z2'=[s2,z];
      consider s1,s2 be Element of S, z be Element of Z such that
A23:  s2 = s * s1 & z1'=[s1,z] & z2'=[s2,z] by A22;
      take z2,s1,s2,z;
      thus z2 = f.z1;
      thus thesis by A21,A23,ZFMISC_1:33;
    end;
  end;
  uniqueness
  proof
    let IT1, IT2 be Function of [:the carrier of S,Z:],[:the carrier of S,Z:]
    such that
A24: for z1 being Element of [:the carrier of S,Z:] holds ex z2 being
Element of [:the carrier of S,Z:], s1,s2 being Element of S, z being Element of
    Z st z2 = IT1.z1 & s2 = s * s1 & z1=[s1,z] & z2=[s2,z] and
A25: for z1 being Element of [:the carrier of S,Z:] holds ex z2 being
Element of [:the carrier of S,Z:], s1,s2 being Element of S, z being Element of
    Z st z2 = IT2.z1 & s2 = s * s1 & z1=[s1,z] & z2=[s2,z];
    let x be Element of [:the carrier of S,Z:];
    consider z12 be Element of [:the carrier of S,Z:], s11,s12 be Element of S
    , z' be Element of Z such that
A26: z12 = IT1.x & s12 = s * s11 and
A27: x=[s11,z'] and
A28: z12=[s12,z'] by A24;
    consider z22 be Element of [:the carrier of S,Z:], s21,s22 be Element of S
    , z'' be Element of Z such that
A29: z22 = IT2.x & s22 = s * s21 and
A30: x=[s21,z''] and
A31: z22=[s22,z''] by A25;
    s11=s21 by A27,A30,ZFMISC_1:33;
    hence IT1.x = IT2.x by A26,A27,A28,A29,A30,A31,ZFMISC_1:33;
  end;
end;

definition
  let S be Group-like associative (non empty multMagma);
  let Z be non empty set;
  func the_left_operation_of(S,Z) -> LeftOperation of S,[:the carrier of S,Z:]
  means
  :Def7:
  for s being Element of S holds it.s = the_left_translation_of(s,Z);
  existence
  proof
    deffunc f(Element of S)=the_left_translation_of($1,Z);
    set E = [:the carrier of S,Z:];
A1: for s1,s2 being Element of S holds f(s1*s2)=f(s1)*f(s2)
    proof
      let s1,s2 be Element of S;
      set f12 = the_left_translation_of(s1*s2,Z);
      set f1 = the_left_translation_of(s1,Z);
      set f2 = the_left_translation_of(s2,Z);
      f1 in Funcs(E,E) by FUNCT_2:12;
      then
A2:   ex f being Function st f1=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
      f2 in Funcs(E,E) by FUNCT_2:12;
      then
A3:   ex f being Function st f2=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
A4:   now
        let x be set;
        assume
A5:     x in dom f12;
        then reconsider z1'=x as Element of E;
        reconsider z1'''=x as Element of E by A5;
        consider z2' be Element of E, s1',s2' be Element of S, z' be Element
        of Z such that
A6:     z2'=f2.z1' and
A7:     s2'=s2 * s1' and
A8:     z1'=[s1',z'] and
A9:     z2'=[s2',z'] by Def6;
        f2.x in rng f2 by A3,A5,FUNCT_1:12;
        then reconsider z1''=f2.x as Element of E;
        consider z2'' be Element of E, s1'',s2'' be Element of S, z'' be
        Element of Z such that
A10:    z2''=f1.z1'' and
A11:    s2''=s1*s1'' and
A12:    z1''=[s1'',z''] and
A13:    z2''=[s2'',z''] by Def6;
A14:    z'' = z' by A6,A9,A12,ZFMISC_1:33;
        consider z2''' be Element of E, s1''',s2''' be Element of S, z''' be
        Element of Z such that
A15:    z2'''=f12.z1''' and
A16:    s2''' = (s1*s2) * s1''' and
A17:    z1'''=[s1''',z'''] and
A18:    z2'''=[s2''',z'''] by Def6;
        s2''' = (s1*s2) * s1' by A8,A16,A17,ZFMISC_1:33
          .= s1 * (s2*s1') by GROUP_1:def 4
          .= s2'' by A6,A7,A9,A11,A12,ZFMISC_1:33;
        hence f12.x = f1.(f2.x) by A8,A10,A13,A15,A17,A18,A14,ZFMISC_1:33;
      end;
      f12 in Funcs(E,E) by FUNCT_2:12;
      then
A19:  ex f being Function st f12=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
      now
        let x be set;
        hereby
          assume
A20:      x in dom f12;
          hence x in dom f2 by A3;
          f2.x in rng f2 by A3,A20,FUNCT_1:12;
          hence f2.x in dom f1 by A2;
        end;
        assume that
A21:    x in dom f2 and
        f2.x in dom f1;
        thus x in dom f12 by A19,A21;
      end;
      hence thesis by A4,FUNCT_1:20;
    end;
A22: f(1_S) = id E
    proof
      set f = the_left_translation_of(1_S,Z);
A23:  now
        let x be set;
        assume
        x in E;
        then reconsider z1=x as Element of E;
        ex z2 be Element of E, s1,s2 be Element of S, z be Element of Z st
        z2=f.z1 & s2 = 1_S * s1 & z1=[s1,z] & z2=[s2,z] by Def6;
        hence f.x = x by GROUP_1:def 5;
      end;
      f in Funcs(E,E) by FUNCT_2:12;
      then ex f' being Function st f=f' & dom f' = E & rng f' c= E by
      FUNCT_2:def 2;
      hence thesis by A23,FUNCT_1:34;
    end;
    ex T be LeftOperation of S, E st for s being Element of S holds T.s=f(
    s) from ExLeftOperation(A22,A1 );
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of S, [:the carrier of S,Z:] such that
A24: for s being Element of S holds T1.s = the_left_translation_of(s,Z
    ) and
A25: for s being Element of S holds T2.s = the_left_translation_of(s,Z
    );
    let x be Element of S;
    thus T1.x = the_left_translation_of(x,Z) by A24
      .= T2.x by A25;
  end;
end;

definition
  let G be Group;
  let H,P be Subgroup of G;
  let h be Element of H;
  func the_left_translation_of(h,P) -> Function of Left_Cosets P, Left_Cosets
  P means
  :Def8:
  for P1 being Element of Left_Cosets P holds ex P2 being Element
of Left_Cosets P, A1,A2 being Subset of G, g being Element of G st P2 = it.P1 &
  A2 = g * A1 & A1=P1 & A2=P2 & g=h;
  existence
  proof
    set f = {[P1,P2] where P1,P2 is Element of Left_Cosets P: ex A1,A2 being
    Subset of G, g being Element of G st A2 = g * A1 & A1=P1 & A2=P2 & g=h};
A1: now
      let x,y1,y2 be set;
      assume
      [x,y1] in f;
      then consider P1',P2' be Element of Left_Cosets P such that
A2:   [x,y1] = [P1',P2'] and
A3:   ex A1,A2 being Subset of G,g being Element of G st A2 = g * A1 &
      A1= P1' & A2=P2' & g=h;
A4:   x = P1' by A2,ZFMISC_1:33;
      assume
      [x,y2] in f;
      then consider P1'',P2'' be Element of Left_Cosets P such that
A5:   [x,y2] = [P1'',P2''] and
A6:   ex A1,A2 being Subset of G,g being Element of G st A2 = g * A1 &
      A1= P1'' & A2=P2'' & g=h;
      x = P1'' by A5,ZFMISC_1:33;
      hence y1 = y2 by A2,A3,A5,A6,A4,ZFMISC_1:33;
    end;
    now
      let x be set;
      assume
      x in f;
      then consider P1,P2 be Element of Left_Cosets P such that
A7:   x=[P1,P2] and
      ex A1,A2 being Subset of G,g being Element of G st A2=g*A1 & A1=P1 &
      A2=P2 & g=h;
      reconsider y=P1,z=P2 as set;
      take y,z;
      thus x = [y,z] by A7;
    end;
    then reconsider f as Function by A1,FUNCT_1:def 1,RELAT_1:def 1;
    now
      let x be set;
      hereby
        reconsider g = h as Element of G by GROUP_2:51;
        assume
        x in Left_Cosets P;
        then consider a be Element of G such that
A8:     x = a * P by GROUP_2:def 15;
        reconsider P1=x as Element of Left_Cosets P by A8,GROUP_2:def 15;
        reconsider y = g * (a * P) as set;
        take y;
        g * (a * P) = (g*a) * P by GROUP_2:127;
        then reconsider P2=y as Element of Left_Cosets P by GROUP_2:def 15;
        ex A1,A2 being Subset of G st A2=g*A1 & A1=P1 & A2=P2 by A8;
        hence [x,y] in f;
      end;
      given y be set such that
A9:   [x,y] in f;
      consider P1,P2 be Element of Left_Cosets P such that
A10:  [x,y]=[P1,P2] and
      ex A1,A2 being Subset of G, g being Element of G st A2=g*A1 & A1=P1
      & A2=P2 & g=h by A9;
A11:  Left_Cosets P is not empty by GROUP_2:165;
      x = P1 by A10,ZFMISC_1:33;
      hence x in Left_Cosets P by A11;
    end;
    then
A12: dom f = Left_Cosets P by RELAT_1:def 4;
    now
      let y be set;
A13:  Left_Cosets P is not empty by GROUP_2:165;
      assume
      y in rng f;
      then consider x be set such that
A14:  x in dom f & y = f.x by FUNCT_1:def 5;
      [x,y] in f by A14,FUNCT_1:8;
      then consider P1,P2 be Element of Left_Cosets P such that
A15:  [x,y]=[P1,P2] and
      ex A1,A2 being Subset of G, g being Element of G st A2=g*A1 & A1=P1
      & A2=P2 & h=g;
      y = P2 by A15,ZFMISC_1:33;
      hence y in Left_Cosets P by A13;
    end;
    then rng f c= Left_Cosets P by TARSKI:def 3;
    then reconsider f as Function of Left_Cosets P, Left_Cosets P by A12,
    FUNCT_2:4;
    take f;
    thus for P1 being Element of Left_Cosets P holds ex P2 being Element of
Left_Cosets P, A1,A2 being Subset of G, g being Element of G st P2 = f.P1 & A2
    = g * A1 & A1=P1 & A2=P2 & g=h
    proof
      let P1 be Element of Left_Cosets P;
      set P2=f.P1;
      Left_Cosets P is non empty by GROUP_2:165;
      then [P1,P2] in f by A12,FUNCT_1:8;
      then consider P1',P2' be Element of Left_Cosets P such that
A16:  [P1,P2]=[P1',P2'] and
A17:  ex A1,A2 being Subset of G, g being Element of G st A2=g*A1 &
      A1= P1' & A2=P2' & h=g;
      reconsider P2 as Element of Left_Cosets P by A16,ZFMISC_1:33;
      consider A1,A2 be Subset of G, g be Element of G such that
A18:  A2=g*A1 & A1=P1' and
      A2=P2' and
A19:  h=g by A17;
      take P2, A1,A2, g;
      thus P2 = f.P1;
      thus thesis by A16,A17,A18,A19,ZFMISC_1:33;
    end;
  end;
  uniqueness
  proof
    let IT1, IT2 be Function of Left_Cosets P, Left_Cosets P such that
A20: ( for P1 being Element of Left_Cosets P holds ex P2 being Element
of Left_Cosets P, A1,A2 being Subset of G, g being Element of G st P2 = IT1.P1
    & A2 = g * A1 & A1=P1 & A2=P2 & g=h)& for P1 being Element of Left_Cosets P
holds ex P2 being Element of Left_Cosets P, A1,A2 being Subset of G, g being
    Element of G st P2 = IT2.P1 & A2 = g * A1 & A1=P1 & A2=P2 & g=h;
    let x be Element of Left_Cosets P;
    (ex P12 be Element of Left_Cosets P, A11,A12 be Subset of G, g1 be
Element of G st P12 = IT1.x & A12 = g1 * A11 & A11=x & A12=P12 & g1 =h )& ex
P22 be Element of Left_Cosets P, A21,A22 be Subset of G, g2 be Element of G st
    P22 = IT2.x & A22 = g2 * A21 & A21=x & A22=P22 & g2 =h by A20;
    hence thesis;
  end;
end;

definition
  let G be Group;
  let H,P be Subgroup of G;
  func the_left_operation_of(H,P) -> LeftOperation of H, Left_Cosets P means
  :
  Def9: for h being Element of H holds it.h = the_left_translation_of(h,P);
  existence
  proof
    deffunc f(Element of H)=the_left_translation_of($1,P);
A1: for h1,h2 being Element of H holds f(h1*h2)=f(h1)*f(h2)
    proof
      let h1,h2 be Element of H;
      set f12 = the_left_translation_of(h1*h2,P);
      set f1 = the_left_translation_of(h1,P);
      set f2 = the_left_translation_of(h2,P);
      f1 in Funcs(Left_Cosets P,Left_Cosets P) by FUNCT_2:12;
      then
A2:   ex f being Function st f1=f & dom f = Left_Cosets P & rng f c=
      Left_Cosets P by FUNCT_2:def 2;
      f2 in Funcs(Left_Cosets P,Left_Cosets P) by FUNCT_2:12;
      then
A3:   ex f being Function st f2=f & dom f = Left_Cosets P & rng f c=
      Left_Cosets P by FUNCT_2:def 2;
A4:   now
        let x be set;
        assume
A5:     x in dom f12;
        then reconsider P1'=x as Element of Left_Cosets P;
        f2.x in rng f2 by A3,A5,FUNCT_1:12;
        then reconsider P1''=f2.x as Element of Left_Cosets P;
        consider P2'' be Element of Left_Cosets P, A1'',A2'' be Subset of G,
        g'' be Element of G such that
A6:     P2''=f1.P1'' & A2'' = g'' * A1'' & A1''=P1'' & A2''=P2'' and
A7:     g''=h1 by Def8;
        reconsider P1'''=x as Element of Left_Cosets P by A5;
        consider P2' be Element of Left_Cosets P, A1',A2' be Subset of G, g'
        be Element of G such that
A8:     P2' = f2.P1' & A2' = g' * A1' & A1'=P1' & A2'=P2' and
A9:     g'=h2 by Def8;
        consider P2''' be Element of Left_Cosets P, A1''',A2''' be Subset of G
        , g''' be Element of G such that
A10:    P2'''=f12.P1''' & A2'''=g''' * A1''' & A1'''=P1''' & A2'''=
        P2''' and
A11:    g'''=h1*h2 by Def8;
        g'''=g''*g' by A9,A7,A11,GROUP_2:52;
        hence f12.x = f1.(f2.x) by A8,A6,A10,GROUP_2:38;
      end;
      f12 in Funcs(Left_Cosets P,Left_Cosets P) by FUNCT_2:12;
      then
A12:  ex f being Function st f12=f & dom f = Left_Cosets P & rng f c=
      Left_Cosets P by FUNCT_2:def 2;
      now
        let x be set;
        hereby
          assume
A13:      x in dom f12;
          hence x in dom f2 by A3;
          f2.x in rng f2 by A3,A13,FUNCT_1:12;
          hence f2.x in dom f1 by A2;
        end;
        assume that
A14:    x in dom f2 and
        f2.x in dom f1;
        thus x in dom f12 by A12,A14;
      end;
      hence thesis by A4,FUNCT_1:20;
    end;
A15: f(1_H) = id Left_Cosets P
    proof
      set f = the_left_translation_of(1_H,P);
A16:  now
        let x be set;
        assume
        x in Left_Cosets P;
        then reconsider P1=x as Element of Left_Cosets P;
        (ex P2 be Element of Left_Cosets P, A1,A2 be Subset of G, g be
Element of G st P2=f.P1 & A2 = g * A1 & A1=P1 & A2=P2 & g=1_H )& 1_H=1_G by
        Def8, GROUP_2:53;
        hence f.x = x by GROUP_2:43;
      end;
      f in Funcs(Left_Cosets P,Left_Cosets P) by FUNCT_2:12;
      then ex f' being Function st f=f' & dom f' = Left_Cosets P & rng f' c=
      Left_Cosets P by FUNCT_2:def 2;
      hence thesis by A16,FUNCT_1:34;
    end;
    ex T being LeftOperation of H, Left_Cosets P st for h being Element
    of H holds T.h = f(h) from ExLeftOperation(A15,A1);
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of H, Left_Cosets P such that
A17: for h being Element of H holds T1.h = the_left_translation_of(h,P
    ) and
A18: for h being Element of H holds T2.h = the_left_translation_of(h,P
    );
    let x be Element of H;
    thus T1.x = the_left_translation_of(x,P) by A17
      .= T2.x by A18;
  end;
end;

begin :: Stabilizer and Orbits

:: stabilizer
:: ALG I.5.2 Definition 3

definition
  let G be Group;
  let E be non empty set;
  let T be LeftOperation of G,E;
  let A be Subset of E;
  func the_strict_stabilizer_of(A,T) -> strict Subgroup of G means
  :Def10:
  the
  carrier of it = {g where g is Element of G: (T^g) .: A = A};
  existence
  proof
    set X={g where g is Element of G: (T^g) .: A = A};
    now
      let x be set;
      assume
      x in X;
      then ex g being Element of G st x=g & (T^g) .: A = A;
      hence x in the carrier of G;
    end;
    then reconsider X as Subset of G by TARSKI:def 3;
A1: for g1,g2 being Element of G st g1 in X & g2 in X holds g1 * g2 in X
    proof
      let g1,g2 be Element of G;
      assume
      g1 in X;
      then
A2:   ex g being Element of G st g=g1 & (T^g) .: A = A;
      assume
      g2 in X;
      then
A3:   ex g being Element of G st g=g2 & (T^g) .: A = A;
      (T^(g1*g2)) .: A = (T^g1 * (T^g2)) .: A by Def1
        .= A by A2,A3,RELAT_1:159;
      hence thesis;
    end;
A4: id E .: A = A by FUNCT_1:162;
A5: for g being Element of G st g in X holds g" in X
    proof
      let g be Element of G;
      assume
      g in X;
      then ex g' being Element of G st g'=g & (T^g') .: A = A;
      then (T^g") .: A = (T^g" * (T^g)) .: A by RELAT_1:159
        .= (T^(g"*g)) .: A by Def1
        .= (T^1_G) .: A by GROUP_1:def 6
        .= A by A4,Def1;
      hence thesis;
    end;
    (T^1_G) .: A = A by A4,Def1;
    then 1_G in X;
    hence thesis by A1,A5,GROUP_2:61;
  end;
  uniqueness by GROUP_2:68;
end;

definition
  let G be Group;
  let E be non empty set;
  let T be LeftOperation of G,E;
  let x be Element of E;
  func the_strict_stabilizer_of(x,T) -> strict Subgroup of G equals
  the_strict_stabilizer_of({x},T);
  correctness;
end;

definition
  let S be unital (non empty multMagma);
  let E be set;
  let T be LeftOperation of S, E;
  let x be Element of E;
  pred x is_fixed_under T means
  :Def12:
  for s being Element of S holds x = (T^
  s).x;
end;

definition
  let S be unital (non empty multMagma);
  let E be set;
  let T be LeftOperation of S, E;
  func the_fixed_points_of T -> Subset of E equals
  :Def13:
  {x where x is
  Element of E: x is_fixed_under T} if E is non empty otherwise {}E;
  correctness
  proof
    not E is empty implies {x where x is Element of E:x is_fixed_under T}
    is Subset of E
    proof
      set Y = {x where x is Element of E: x is_fixed_under T};
      defpred P[set] means for x being Element of E st x=$1 holds x
      is_fixed_under T;
      consider X be Subset of E such that
A1:   for x being set holds x in X iff x in E & P[x] from SUBSET_1:sch
      1;
      assume
A2:   not E is empty;
      now
        let y be set;
        hereby
          assume
A3:       y in X;
          then reconsider y'=y as Element of E;
          y' is_fixed_under T by A1,A3;
          hence y in Y;
        end;
        assume
        y in Y;
        then
A4:     ex y' be Element of E st y=y' & y' is_fixed_under T;
        then P[y];
        hence y in X by A2,A1,A4;
      end;
      hence thesis by TARSKI:2;
    end;
    hence thesis;
  end;
end;

:: ALG I.5.4 Definition 5

definition
  let S be unital (non empty multMagma);
  let E be set;
  let T be LeftOperation of S, E;
  let x,y be Element of E;
  pred x,y are_conjugated_under T means
  :Def14:
  ex s being Element of S st y =
  (T^s).x;
end;

theorem Th4:
  for S being unital (non empty multMagma), E being non empty set,
  x being Element of E, T being LeftOperation of S, E holds x,x
  are_conjugated_under T
proof
  let S be unital (non empty multMagma);
  let E be non empty set;
  let x be Element of E;
  let T be LeftOperation of S, E;
  x = (id E).x by FUNCT_1:35
    .= (T^1_S).x by Def1;
  hence thesis by Def14;
end;

theorem Th5:
  for G being Group, E being non empty set, x,y being Element of E,
  T being LeftOperation of G, E st x,y are_conjugated_under T holds y,x
  are_conjugated_under T
proof
  let G be Group;
  let E be non empty set;
  let x,y be Element of E;
  let T be LeftOperation of G, E;
  assume
  x,y are_conjugated_under T;
  then consider g be Element of G such that
A1: y = (T^g).x by Def14;
  x in E;
  then x in dom(T^g) by FUNCT_2:def 1;
  then (T^g").y = ((T^g")*(T^g)).x by A1,FUNCT_1:23
    .= (T^(g"*g)).x by Def1
    .= (T^1_G).x by GROUP_1:def 6
    .= (id E).x by Def1
    .= x by FUNCT_1:35;
  hence thesis by Def14;
end;

theorem Th6:
  for S being unital (non empty multMagma), E being non empty set,
  x,y,z being Element of E, T being LeftOperation of S, E st x,y
  are_conjugated_under T & y,z are_conjugated_under T holds x,z
  are_conjugated_under T
proof
  let S be unital (non empty multMagma);
  let E be non empty set;
  let x,y,z be Element of E;
  let T be LeftOperation of S, E;
  assume
  x,y are_conjugated_under T;
  then consider s1 be Element of S such that
A1: y = (T^s1).x by Def14;
  assume
  y,z are_conjugated_under T;
  then consider s2 be Element of S such that
A2: z = (T^s2).y by Def14;
  x in E;
  then x in dom(T^s1) by FUNCT_2:def 1;
  then z = ((T^s2)*(T^s1)).x by A1,A2,FUNCT_1:23
    .= (T^(s2*s1)).x by Def1;
  hence thesis by Def14;
end;

definition
  let S be unital (non empty multMagma);
  let E be non empty set;
  let T be LeftOperation of S, E;
  let x be Element of E;
  func the_orbit_of(x,T) -> Subset of E equals
  {y where y is Element of E: x,y
  are_conjugated_under T};
  correctness
  proof
    defpred P[set] means for y being Element of E st y=$1 holds x,y
    are_conjugated_under T;
    set Y = {y where y is Element of E: x,y are_conjugated_under T};
    consider X be Subset of E such that
A1: for y being Element of E holds y in X iff P[y] from SUBSET_1:sch 3;
    now
      let y be set;
      hereby
        assume
A2:     y in X;
        then reconsider y'=y as Element of E;
        x,y' are_conjugated_under T by A1,A2;
        hence y in Y;
      end;
      assume
      y in Y;
      then
A3:   ex y' being Element of E st y'=y & x,y' are_conjugated_under T;
      then P[y];
      hence y in X by A1,A3;
    end;
    hence thesis by TARSKI:2;
  end;
end;

theorem Th7:
  for S being unital (non empty multMagma), E being non empty set,
x being Element of E, T being LeftOperation of S, E holds the_orbit_of(x,T) is
  non empty
proof
  let S be unital (non empty multMagma);
  let E be non empty set;
  let x be Element of E;
  let T be LeftOperation of S, E;
  x,x are_conjugated_under T by Th4;
  then x in {y where y is Element of E: x,y are_conjugated_under T};
  hence thesis;
end;

theorem Th8:
  for G being Group, E being non empty set, x,y being Element of E,
T being LeftOperation of G, E holds the_orbit_of(x,T) misses the_orbit_of(y,T)
  or the_orbit_of(x,T)=the_orbit_of(y,T)
proof
  let G be Group;
  let E be non empty set;
  let x,y be Element of E;
  let T be LeftOperation of G, E;
  assume
  not the_orbit_of(x,T) misses the_orbit_of(y,T);
  then the_orbit_of(x,T) /\ the_orbit_of(y,T) <> {} by XBOOLE_0:def 7;
  then consider z1 be set such that
A1: z1 in the_orbit_of(x,T) /\ the_orbit_of(y,T) by XBOOLE_0:def 1;
  z1 in the_orbit_of(y,T) by A1,XBOOLE_0:def 4;
  then consider z1'' be Element of E such that
A2: z1=z1'' and
A3: y,z1'' are_conjugated_under T;
  z1 in the_orbit_of(x,T) by A1,XBOOLE_0:def 4;
  then consider z1' be Element of E such that
A4: z1=z1' and
A5: x,z1' are_conjugated_under T;
  now
    let z2 be set;
    hereby
      assume
      z2 in the_orbit_of(x,T);
      then consider z2' be Element of E such that
A6:   z2=z2' and
A7:   x,z2' are_conjugated_under T;
      z2',x are_conjugated_under T by A7,Th5;
      then z2',z1'' are_conjugated_under T by A4,A5,A2,Th6;
      then z1'',z2' are_conjugated_under T by Th5;
      then y,z2' are_conjugated_under T by A3,Th6;
      hence z2 in the_orbit_of(y,T) by A6;
    end;
    assume
    z2 in the_orbit_of(y,T);
    then consider z2' be Element of E such that
A8: z2=z2' and
A9: y,z2' are_conjugated_under T;
    z2',y are_conjugated_under T by A9,Th5;
    then z2',z1' are_conjugated_under T by A4,A2,A3,Th6;
    then z1',z2' are_conjugated_under T by Th5;
    then x,z2' are_conjugated_under T by A5,Th6;
    hence z2 in the_orbit_of(x,T) by A8;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th9:
  for S being unital (non empty multMagma), E being non empty
  finite set, x being Element of E, T being LeftOperation of S, E st x
  is_fixed_under T holds the_orbit_of(x,T) = {x}
proof
  let S be unital (non empty multMagma);
  let E be non empty finite set;
  let x be Element of E;
  let T be LeftOperation of S, E;
  set X=the_orbit_of(x,T);
  assume
A1: x is_fixed_under T;
  now
    assume
    X<>{x};
    then
A2: not (for x' being set holds x' in X iff x'=x) by TARSKI:def 1;
    X<>{} by Th7;
    then ex x' being set st x' in X by XBOOLE_0:def 1;
    then consider x'' be set such that
A3: x''<>x and
A4: x'' in X by A2;
    consider y' be Element of E such that
A5: x''=y' and
A6: x,y' are_conjugated_under T by A4;
    ex s being Element of S st y' = (T^s).x by A6,Def14;
    hence contradiction by A1,A3,A5,Def12;
  end;
  hence thesis;
end;
:: the orbit-stabilizer theorem

theorem Th10:
  for G being Group, E being non empty set, a being Element of E,
  T being LeftOperation of G, E holds card the_orbit_of(a,T) = Index
  the_strict_stabilizer_of(a,T)
proof
  let G be Group;
  let E be non empty set;
  let a be Element of E;
  let T be LeftOperation of G, E;
  set H = the_strict_stabilizer_of(a,T);
  set f = {[b,A] where b is Element of E, A is Subset of G: ex g being Element
  of G st b = (T^g).a & A = g * H};
  reconsider A' = {a} as Subset of E;
A1: now
    let x be set;
    assume
    x in f;
    then consider b be Element of E, A be Subset of G such that
A2: [b,A]=x and
    ex g being Element of G st b = (T^g).a & A = g * H;
    reconsider b,A as set;
    take b,A;
    thus x=[b,A] by A2;
  end;
  now
    let x,y1,y2 be set;
    assume
    [x,y1] in f;
    then consider b1 be Element of E, A1 be Subset of G such that
A3: [b1,A1]=[x,y1] and
A4: ex g being Element of G st b1 = (T^g).a & A1 = g * H;
    assume
    [x,y2] in f;
    then consider b2 be Element of E, A2 be Subset of G such that
A5: [b2,A2]=[x,y2] and
A6: ex g being Element of G st b2 = (T^g).a & A2 = g * H;
A7: y2=A2 by A5,ZFMISC_1:33;
    consider g1 be Element of G such that
A8: b1 = (T^g1).a and
A9: A1 = g1 * H by A4;
    consider g2 be Element of G such that
A10: b2 = (T^g2).a and
A11: A2 = g2 * H by A6;
    x=b2 by A5,ZFMISC_1:33;
    then dom(T^g1)=E & (T^g1).a = (T^g2).a by A3,A8,A10,FUNCT_2:def 1 ,
    ZFMISC_1:33;
    then dom(T^g2)=E & Im(T^g1,a) = {(T^g2).a} by FUNCT_1:117,FUNCT_2:def
    1;
    then
A12: Im(T^g1,a) = Im(T^g2,a) by FUNCT_1:117;
    set g = g2"*g1;
    reconsider g as Element of G;
A13: the carrier of the_strict_stabilizer_of(A',T) = {g' where g' is
    Element of G: (T^g') .: A' = A'} by Def10;
    (T^g) .: A' = (T^g2" * (T^g1)) .: A' by Def1
      .= (T^g2").:((T^g1) .: A') by RELAT_1:159
      .= (T^g2" *(T^g2)) .: A' by A12,RELAT_1:159
      .= (T^(g2"*g2)) .: A' by Def1
      .= (T^1_G) .: A' by GROUP_1:def 6
      .= (id E) .: A' by Def1
      .= A' by FUNCT_1:162;
    then g in {g' where g' is Element of G: (T^g') .: A' = A'};
    then
A14: g in the_strict_stabilizer_of(A',T) by A13,STRUCT_0:def 5;
    y1=A1 by A3,ZFMISC_1:33;
    hence y1=y2 by A7,A9,A11,A14,GROUP_2:137;
  end;
  then reconsider f as Function by A1,FUNCT_1:def 1,RELAT_1:def 1;
  for y being set holds y in Left_Cosets H iff ex x being set st [x,y] in
  f
  proof
    let y be set;
    hereby
      assume
A15:  y in Left_Cosets H;
      then reconsider A=y as Subset of G;
      consider g be Element of G such that
A16:  A = g * H by A15,GROUP_2:def 15;
      reconsider x=(T^g).a as set;
      take x;
      thus [x,y] in f by A16;
    end;
    given x be set such that
A17: [x,y] in f;
    consider b be Element of E, A be Subset of G such that
A18: [b,A]=[x,y] and
A19: ex g being Element of G st b = (T^g).a & A = g * H by A17;
    A=y by A18,ZFMISC_1:33;
    hence thesis by A19,GROUP_2:def 15;
  end;
  then
A20: rng f = Left_Cosets H by RELAT_1:def 5;
  now
    let x1,x2 be set;
A21: the carrier of the_strict_stabilizer_of(A',T) = {g' where g' is
    Element of G: (T^g') .: A' = A'} by Def10;
    assume
    x1 in dom f;
    then [x1,f.x1] in f by FUNCT_1:8;
    then consider b1 be Element of E, A1 be Subset of G such that
A22: [b1,A1]=[x1,f.x1] and
A23: ex g being Element of G st b1 = (T^g).a & A1=g * H;
    assume
    x2 in dom f;
    then [x2,f.x2] in f by FUNCT_1:8;
    then consider b2 be Element of E, A2 be Subset of G such that
A24: [b2,A2]=[x2,f.x2] and
A25: ex g being Element of G st b2 = (T^g).a & A2=g * H;
    consider g2 be Element of G such that
A26: b2 = (T^g2).a and
A27: A2 = g2 * H by A25;
    assume
A28: f.x1 = f.x2;
    consider g1 be Element of G such that
A29: b1 = (T^g1).a and
A30: A1 = g1 * H by A23;
    set g = g2"*g1;
    f.x2=A2 by A24,ZFMISC_1:33;
    then g1 * H = g2 * H by A22,A30,A27,A28,ZFMISC_1:33;
    then g in H by GROUP_2:137;
    then g in the carrier of the_strict_stabilizer_of(A',T) by STRUCT_0:
    def 5;
    then
A31: ex g' being Element of G st g=g' & (T^g') .: A' = A' by A21;
    (T^g1).:A' = (id E).:((T^g1).:A') by FUNCT_1:162
      .= (T^1_G).:((T^g1).:A') by Def1
      .= (T^(g2*g2")).:((T^g1).:A') by GROUP_1:def 6
      .= (T^(g2*g2")*(T^g1)).:A' by RELAT_1:159
      .= (T^((g2*g2")*g1)).:A' by Def1
      .= (T^(g2*g)).:A' by GROUP_1:def 4
      .= ((T^g2)*(T^g)).:A' by Def1
      .= (T^g2).:A' by A31,RELAT_1:159;
    then dom(T^g2)=E & Im(T^g1,a) = Im(T^g2,a) by FUNCT_2:def 1;
    then dom(T^g1)=E & Im(T^g1,a) = {(T^g2).a} by FUNCT_1:117,FUNCT_2:def
    1;
    then
A32: {(T^g1).a} = {(T^g2).a} by FUNCT_1:117;
A33: x2=b2 by A24,ZFMISC_1:33;
    x1=b1 by A22,ZFMISC_1:33;
    hence x1 = x2 by A33,A29,A26,A32,ZFMISC_1:24;
  end;
  then
A34: f is one-to-one by FUNCT_1:def 8;
  for x being set holds x in the_orbit_of(a,T) iff ex y being set st [x,y
  ] in f
  proof
    let x be set;
    hereby
      assume
      x in the_orbit_of(a,T);
      then consider b be Element of E such that
A35:  b=x and
A36:  a,b are_conjugated_under T;
      consider g be Element of G such that
A37:  b = (T^g).a by A36,Def14;
      reconsider y=g * H as set;
      take y;
      thus [x,y] in f by A35,A37;
    end;
    given y be set such that
A38: [x,y] in f;
    consider b be Element of E, A be Subset of G such that
A39: [b,A]=[x,y] & ex g being Element of G st b = (T^g).a & A = g * H
    by A38;
    b=x & a,b are_conjugated_under T by A39,Def14,ZFMISC_1:33;
    hence thesis;
  end;
  then dom f = the_orbit_of(a,T) by RELAT_1:def 4;
  then the_orbit_of(a,T), Left_Cosets H are_equipotent by A34,A20,
  WELLORD2:def 4;
  hence thesis by CARD_1:21;
end;

definition
  let G be Group;
  let E be non empty set;
  let T be LeftOperation of G, E;
  func the_orbits_of T -> a_partition of E equals
  {X where X is Subset of E:
  ex x being Element of E st X = the_orbit_of(x,T)};
  correctness
  proof
    set P = {X where X is Subset of E: ex x being Element of E st X =
    the_orbit_of(x,T)};
    now
      let x be set;
      assume
      x in P;
      then ex X being Subset of E st x=X & ex x being Element of E st X =
      the_orbit_of(x,T);
      hence x in bool E;
    end;
    then reconsider P as Subset-Family of E by TARSKI:def 3;
    for x being set holds x in E iff ex Y being set st x in Y & Y in P
    proof
      let x be set;
      hereby
        assume
        x in E;
        then reconsider x'=x as Element of E;
        reconsider Y = the_orbit_of(x',T) as set;
        take Y;
        x',x' are_conjugated_under T by Th4;
        hence x in Y;
        thus Y in P;
      end;
      given Y be set such that
A1:   x in Y & Y in P;
      thus thesis by A1;
    end;
    then
A2: union P = E by TARSKI:def 4;
    for A being Subset of E st A in P holds A<>{} & for B being Subset of
    E st B in P holds A = B or A misses B
    proof
      let A be Subset of E;
      assume
      A in P;
      then
A3:   ex X being Subset of E st A=X & ex x' being Element of E st X =
      the_orbit_of(x',T);
      hence A<>{} by Th7;
      let B be Subset of E;
      assume
      B in P;
      then
A4:   ex X being Subset of E st B=X & ex x' being Element of E st X =
      the_orbit_of(x',T);
      assume
      not A = B;
      hence thesis by A3,A4,Th8;
    end;
    hence thesis by A2,EQREL_1:def 6;
  end;
end;

begin :: p-groups

:: ALG I.6.5 Definition 9

definition
  let p be prime (natural number);
  let G be Group;
  pred G is_p-group_of_prime p means
  :Def17:
  ex r being natural number st card
  G = p |^ r;
end;

definition
  let p be prime (natural number);
  let G be Group;
  let P be Subgroup of G;
  pred P is_p-group_of_prime p means
  :Def18:
  ex H being finite Group st P = H
  & H is_p-group_of_prime p;
end;

:: like WEDDWITT:39
:: ALG I.6.5 PRO 11

theorem Th11:
  for E being non empty finite set, G being finite Group, p being
prime (natural number), T being LeftOperation of G, E st G is_p-group_of_prime
  p holds card the_fixed_points_of T mod p = card E mod p
proof
  let E be non empty finite set;
  let G be finite Group;
  let p be prime (natural number);
  let T be LeftOperation of G, E;
  set O1 = {X where X is Subset of E: card X = 1 & ex x being Element of E st
  X = the_orbit_of(x,T)};
  set O2 = {X where X is Subset of E: card X > 1 & ex x being Element of E st
  X = the_orbit_of(x,T)};
  set O = O1 \/ O2;
  defpred P[set,set] means ex x being Element of E, X being Subset of E st $1=
  x & $2=X & X = the_orbit_of(x,T) & x is_fixed_under T;
  reconsider p'=p as Element of NAT by ORDINAL1:def 13;
A1: for x being set st x in the_fixed_points_of T ex y being set st P[x,y]
  proof
    let x be set;
    assume
A2: x in the_fixed_points_of T;
    then reconsider x' = x as Element of E;
    reconsider y = the_orbit_of(x',T) as set;
    take y;
    now
      set X=y;
      set x=x';
      take x,X;
      thus x'=x & y=X & X = the_orbit_of(x,T);
      the_fixed_points_of T = {x'' where x'' is Element of E:x''
      is_fixed_under T} by Def13;
      then ex e being Element of E st x=e & e is_fixed_under T by A2;
      hence x is_fixed_under T;
    end;
    hence thesis;
  end;
  consider FX be Function such that
A3: dom FX = the_fixed_points_of T & for x being set st x in
  the_fixed_points_of T holds P[x,FX.x] from CLASSES1:sch 1(A1);
  now
    let y be set;
    hereby
      assume
      y in O1;
      then consider X be Subset of E such that
A4:   y=X and
A5:   card X = 1 and
A6:   ex x being Element of E st X = the_orbit_of(x,T);
      consider x' be Element of E such that
A7:   X = the_orbit_of(x',T) by A6;
      reconsider x=x' as set;
      card X = card {x} by A5,CARD_1:50;
      then consider x'' be set such that
A8:   X = {x''} by CARD_1:49;
      take x;
      now
        x',x' are_conjugated_under T by Th4;
        then
A9:     x' in {y'' where y'' is Element of E: x',y'' are_conjugated_under
        T};
        given g be Element of G such that
A10:    x' <> (T^g).x';
        set y' = (T^g).x';
        x',y' are_conjugated_under T by Def14;
        then
        y' in {y'' where y'' is Element of E: x',y'' are_conjugated_under
        T};
        then y'=x'' by A7,A8,TARSKI:def 1;
        hence contradiction by A7,A8,A10,A9,TARSKI:def 1;
      end;
      then x' is_fixed_under T by Def12;
      then x in {x''' where x''' is Element of E:x''' is_fixed_under T};
      then
A11:  x in the_fixed_points_of T by Def13;
      then ex x'' being Element of E, X' being Subset of E st x''=x & X'=FX.x
      & X' = the_orbit_of(x'',T) & x'' is_fixed_under T by A3;
      hence [x,y] in FX by A3,A4,A7,A11,FUNCT_1:8;
    end;
    given x be set such that
A12: [x,y] in FX;
    x in dom FX & y = FX.x by A12,FUNCT_1:8;
    then consider x' be Element of E, X' be Subset of E such that
    x'=x and
A13: X'=y and
A14: X' = the_orbit_of(x',T) and
A15: x' is_fixed_under T by A3;
    X' = {x'} by A14,A15,Th9;
    then card X' = 1 by CARD_1:50;
    hence y in O1 by A13,A14;
  end;
  then
A16: rng FX = O1 by RELAT_1:def 5;
  now
    let x1,x2 be set;
    assume
    x1 in dom FX;
    then consider x1' be Element of E, X1 be Subset of E such that
A17: x1'=x1 and
A18: X1=FX.x1 & X1 = the_orbit_of(x1',T) & x1' is_fixed_under T by
    A3;
    assume
    x2 in dom FX;
    then consider x2' be Element of E, X2 be Subset of E such that
A19: x2'=x2 and
A20: X2=FX.x2 & X2 = the_orbit_of(x2',T) and
A21: x2' is_fixed_under T by A3;
    assume
    FX.x1 = FX.x2;
    then {x1'} = the_orbit_of(x2',T) by A18,A20,Th9
      .= {x2'} by A21,Th9;
    hence x1 = x2 by A17,A19,ZFMISC_1:6;
  end;
  then FX is one-to-one by FUNCT_1:def 8;
  then
A22: the_fixed_points_of T,O1 are_equipotent by A3,A16,WELLORD2:def
  4;
A23: now
    let y be set;
    hereby
      assume
      y in the_orbits_of T;
      then consider X be Subset of E such that
A24:  y=X and
A25:  ex x being Element of E st X = the_orbit_of(x,T);
      X is non empty by A25,Th7;
      then 0+1 < card X+1 by XREAL_1:8;
      then
A26:  1 <= card X by NAT_1:13;
      per cases by A26,XXREAL_0:1;
      suppose
        1=card X;
        then y in O1 by A24,A25;
        hence y in O1 \/ O2 by XBOOLE_0:def 3;
      end;
      suppose
        1<card X;
        then y in O2 by A24,A25;
        hence y in O1 \/ O2 by XBOOLE_0:def 3;
      end;
    end;
    assume
A27: y in O1 \/ O2;
    per cases by A27,XBOOLE_0:def 3;
    suppose
      y in O1;
      then ex X being Subset of E st y=X & card X = 1 & ex x being Element of
      E st X = the_orbit_of(x,T);
      hence y in the_orbits_of T;
    end;
    suppose
      y in O2;
      then ex X being Subset of E st y=X & card X > 1 & ex x being Element of
      E st X = the_orbit_of(x,T);
      hence y in the_orbits_of T;
    end;
  end;
  then
A28: O = the_orbits_of T by TARSKI:2;
  then O1 is finite by FINSET_1:13,XBOOLE_1:7;
  then consider f1 be FinSequence such that
A29: rng f1 = O1 and
A30: f1 is one-to-one by FINSEQ_4:73;
  O2 is finite by A28,FINSET_1:13,XBOOLE_1:7;
  then consider f2 be FinSequence such that
A31: rng f2 = O2 and
A32: f2 is one-to-one by FINSEQ_4:73;
  set f = f1 ^ f2;
  reconsider O as a_partition of E by A23,TARSKI:2;
A33: rng f = O by A29,A31,FINSEQ_1:44;
  now
    given x be set such that
A34: x in O1 /\ O2;
    x in O2 by A34,XBOOLE_0:def 4;
    then
A35: ex X being Subset of E st x=X & card X>1 & ex x' being Element of E
    st X=the_orbit_of(x',T);
    x in O1 by A34,XBOOLE_0:def 4;
    then ex X being Subset of E st x=X & card X=1 & ex x' being Element of E
    st X=the_orbit_of(x',T);
    hence contradiction by A35;
  end;
  then O1 /\ O2 = {} by XBOOLE_0:def 1;
  then rng f1 misses rng f2 by A29,A31,XBOOLE_0:def 7;
  then
A36: f is one-to-one by A30,A32,FINSEQ_3:98;
  reconsider f as FinSequence of O by A33,FINSEQ_1:def 4;
  deffunc F(set) = card(f1.$1);
  consider p1 being FinSequence such that
A37: len p1 = len f1 & for i being Nat st i in dom p1 holds p1.i = F(i)
  from FINSEQ_1:sch 2;
  for x being set st x in rng p1 holds x in NAT
  proof
    let x be set;
    assume
    x in rng p1;
    then consider i be Nat such that
A38: i in dom p1 and
A39: p1.i = x by FINSEQ_2:11;
    i in dom f1 by A37,A38,FINSEQ_3:31;
    then f1.i in O1 by A29,FUNCT_1:12;
    then
A40: ex X being Subset of E st f1.i=X & card X = 1 & ex x being Element of
    E st X=the_orbit_of(x,T);
    x = card(f1.i) by A37,A38,A39;
    hence thesis by A40;
  end;
  then rng p1 c= NAT by TARSKI:def 3;
  then reconsider c1=p1 as FinSequence of NAT by FINSEQ_1:def 4;
  deffunc P2(set) = card(f2.$1);
  consider p2 being FinSequence such that
A41: len p2 = len f2 & for i being Nat st i in dom p2 holds p2.i = P2(i
  ) from FINSEQ_1:sch 2;
  for x being set st x in rng p2 holds x in NAT
  proof
    let x be set;
    assume
    x in rng p2;
    then consider i being Nat such that
A42: i in dom p2 and
A43: p2.i = x by FINSEQ_2:11;
    i in dom f2 by A41,A42,FINSEQ_3:31;
    then f2.i in O2 by A31,FUNCT_1:12;
    then
A44: ex X being Subset of E st f2.i=X & card X > 1 & ex x being Element
    of E st X=the_orbit_of(x,T);
    x = card(f2.i) by A41,A42,A43;
    hence thesis by A44;
  end;
  then rng p2 c= NAT by TARSKI:def 3;
  then reconsider c2=p2 as FinSequence of NAT by FINSEQ_1:def 4;
  set c = c1^c2;
  reconsider c as FinSequence of NAT;
A45: for i being Element of NAT st i in dom c holds c.i = card (f.i)
  proof
    let i be Element of NAT such that
A46: i in dom c;
    now
      per cases by A46,FINSEQ_1:38;
      suppose
A47:    i in dom c1;
        then
A48:    i in dom f1 by A37,FINSEQ_3:31;
        c.i = c1.i by A47,FINSEQ_1:def 7
          .= card(f1.i) by A37,A47
          .= card(f.i) by A48,FINSEQ_1:def 7;
        hence thesis;
      end;
      suppose
        ex j being Nat st j in dom c2 & i = len c1 + j;
        then consider j being Nat such that
A49:    j in dom c2 and
A50:    i = len c1 + j;
A51:    j in dom f2 by A41,A49,FINSEQ_3:31;
        c.i = c2.j by A49,A50,FINSEQ_1:def 7
          .= card(f2.j) by A41,A49
          .= card(f.i) by A37,A50,A51,FINSEQ_1:def 7;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  assume
A52: G is_p-group_of_prime p;
  for i being Element of NAT st i in dom c2 holds p' divides c2/.i
  proof
    let i be Element of NAT;
    set i'=c2/.i;
    consider r be natural number such that
A53: card G = p |^ r by A52,Def17;
    reconsider r'=r as Element of NAT by ORDINAL1:def 13;
    assume
A54: i in dom c2;
    then i in dom f2 by A41,FINSEQ_3:31;
    then f2.i in O2 by A31,FUNCT_1:12;
    then consider X be Subset of E such that
A55: f2.i=X and
A56: card X > 1 and
A57: ex x being Element of E st X=the_orbit_of(x,T);
    consider x be Element of E such that
A58: X = the_orbit_of(x,T) by A57;
    set H = the_strict_stabilizer_of(x,T);
    c2.i = card (f2.i) by A41,A54;
    then c2/.i = card (f2.i) by A54,PARTFUN1:def 8;
    then c2/.i = Index H by A55,A58,Th10;
    then c2/.i = index H by GROUP_2:def 18;
    then
A59: card G = card H * c2/.i by GROUP_2:177;
    then
A60: i' divides p'|^r' by A53,NAT_D:def 3;
A61: card the_orbit_of(x,T) = Index H by Th10;
A62: now
      consider B be finite set;
      assume
      card H = card G;
      then card G = card G * index H by GROUP_2:177;
      then 1 = card G * index H / card G by XCMPLX_1:60;
      then 1 = (card G / card G) * index H by XCMPLX_1:75;
      then 1 * index H = 1 by XCMPLX_1:60;
      hence contradiction by A56,A58,A61,GROUP_2:def 18;
    end;
    per cases by A60,PEPIN:33;
    suppose
      i' = 1;
      hence thesis by A59,A62;
    end;
    suppose
      ex k being Element of NAT st i' = p * k;
      hence thesis by NAT_D:def 3;
    end;
  end;
  then p divides Sum c2 by WEDDWITT:5;
  then ex t being Nat st Sum c2 = p * t by NAT_D:def 3;
  then
A63: Sum c2 mod p = 0 by NAT_D:13;
  len c = len f1 + len f2 by A37,A41,FINSEQ_1:35;
  then len c = len f by FINSEQ_1:35;
  then card E = Sum c by A36,A33,A45,WEDDWITT:6
    .= Sum c1 + Sum c2 by RVSUM_1:105;
  then
A64: card E mod p = (Sum c1 +(Sum c2 mod p)) mod p by NAT_D:23
    .= Sum c1 mod p by A63;
A65: for i being Element of NAT st i in dom c1 holds c1.i = 1
  proof
    let i be Element of NAT;
    assume
A66: i in dom c1;
    then i in dom f1 by A37,FINSEQ_3:31;
    then f1.i in O1 by A29,FUNCT_1:12;
    then ex X being Subset of E st f1.i = X & card X = 1 & ex x being Element
    of E st X=the_orbit_of(x,T);
    hence thesis by A37,A66;
  end;
  for x being set st x in rng c1 holds x in {1}
  proof
    let x be set;
    assume
    x in rng c1;
    then ex i being Nat st i in dom c1 & x = c1.i by FINSEQ_2:11;
    then x = 1 by A65;
    hence thesis by TARSKI:def 1;
  end;
  then
A67: rng c1 c= {1} by TARSKI:def 3;
A68: Sum c1 = len c1
  proof
    per cases;
    suppose
      len c1=0;
      then c1 = {};
      hence thesis by RVSUM_1:102;
    end;
    suppose
A69:  len c1<>0;
      for x being set st x in {1} holds x in rng c1
      proof
        let x be set such that
A70:    x in {1};
A71:    Seg len c1 = dom c1 by FINSEQ_1:def 3;
        then c1.(len c1) = 1 by A65,A69,FINSEQ_1:5;
        then c1.(len c1) = x by A70,TARSKI:def 1;
        hence thesis by A69,A71,FINSEQ_1:5,FUNCT_1:12;
      end;
      then {1}c= rng c1 by TARSKI:def 3;
      then rng c1={1} by A67,XBOOLE_0:def 10;
      then c1 = (dom c1) --> 1 by FUNCOP_1:15;
      then c1 = Seg (len c1) --> 1 by FINSEQ_1:def 3;
      then c1 = (len c1) |-> 1 by FINSEQ_2:def 2;
      then Sum c1 = (len c1)*1 by RVSUM_1:110;
      hence thesis;
    end;
  end;
  len c1 = card(rng f1) by A30,A37,FINSEQ_4:77;
  hence thesis by A29,A68,A22,A64,CARD_1:21;
end;

begin :: The Sylow Theorems

:: ALG I.6.6 Definition 10

definition
  let p be prime (natural number);
  let G be Group;
  let P be Subgroup of G;
  pred P is_Sylow_p-subgroup_of_prime p means
  :Def19:
  P is_p-group_of_prime p
  & not p divides index P;
end;

Lm1: for n being natural number, p being prime (natural number) st n<>0 holds
ex m,r being natural number st n = p |^ r * m & not p divides m

proof
  let n be natural number;
  let p be prime (natural number);
  set r = p |-count n;
A1: p <> 1 by NAT_4:12;
  assume
A2: n <> 0;
  then p |^ r divides n by A1,NAT_3:def 7;
  then consider m be natural number such that
A3: n = p |^ r * m by NAT_D:def 3;
  take m,r;
  thus n = p |^ r * m by A3;
A4: not p |^ (r+1) divides n by A2,A1,NAT_3:def 7;
  thus not p divides m
  proof
    assume
    p divides m;
    then consider t be natural number such that
A5: m = p * t by NAT_D:def 3;
    n = p |^ r * p * t by A3,A5
      .= p |^(r+1) * t by NEWTON:11;
    hence contradiction by A4,NAT_D:def 3;
  end;
end;

Lm2: for n being natural number st n > 0 holds card INT.Group(n) = n
proof
  let n be natural number;
  set G = INT.Group(n);
  reconsider k = n as Element of NAT by ORDINAL1:def 13;
  assume
A1: n > 0;
  then
A2: multMagma(#Segm(n),addint(n)#) = G by GR_CY_1:def 6;
  reconsider G as finite Group by A1;
A3: Segm k is finite;
  now
    reconsider B = Segm n as finite set by A3;
    take B;
    thus B = the carrier of G by A2;
    card B = card k by CARD_1:def 12;
    hence n = card B by CARD_1:def 5;
  end;
  hence thesis;
end;

Lm3: for X,Y being non empty set holds card {[:X,{y}:] where y is Element of Y
: not contradiction} = card Y

proof
  let X,Y be non empty set;
  set Z = {[:X,{y}:] where y is Element of Y: not contradiction};
  deffunc F(set) = [: X, {$1} :];

  ex f being Function st dom f = Y & for x being set st x in Y holds f.x =
  F(x) from FUNCT_1:sch 3;

  then consider f be Function such that
A1: dom f = Y and
A2: for x being set st x in Y holds f.x = F(x);
  for y being set holds y in Z iff ex x being set st x in dom f & y = f.x
  proof
    let y be set;
    hereby
      assume
      y in Z;
      then consider y' be Element of Y such that
A3:   y = [:X,{y'}:];
      reconsider x=y' as set;
      take x;
      thus x in dom f by A1;
      thus y = f.x by A2,A3;
    end;
    given x be set such that
A4: x in dom f and
A5: y = f.x;
    f.x = [:X,{x}:] by A1,A2,A4;
    hence thesis by A1,A4,A5;
  end;
  then
A6: rng f = Z by FUNCT_1:def 5;

  for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1 = f.x2 holds x1
  = x2

  proof
    let x1,x2 be set;
    assume
    x1 in dom f & x2 in dom f;
    then
A7: f.x1 = F(x1) & f.x2 = F(x2) by A1,A2;
    assume
    f.x1 = f.x2;
    then {x1} = {x2} by A7,ZFMISC_1:134;
    hence thesis by ZFMISC_1:6;
  end;
  then f is one-to-one by FUNCT_1:def 8;
  then Z,Y are_equipotent by A1,A6,WELLORD2:def 4;
  hence thesis by CARD_1:21;
end;

Lm4: for G being finite Group, E,T being non empty set, LO being LeftOperation
of G,E, n being natural number st LO = the_left_operation_of(G,T) & n = card G
& E = [:the carrier of G,T:] & card n c= card E holds the_fixed_points_of(
the_extension_of_left_operation_of(n,LO)) = {[:the carrier of G,{t}:] where t
is Element of T: not contradiction}

proof
  let G be finite Group;
  let E,T be non empty set;
  let LO be LeftOperation of G,E;
  let n be natural number;
  assume
A1: LO = the_left_operation_of(G,T);
  set EE = the_subsets_of_card(n, E);
  set LO' = the_extension_of_left_operation_of(n,LO);
  assume
A2: n = card G;
  assume
A3: E = [:the carrier of G,T:];
  assume
A4: card n c= card E;
  now
    let z be set;
    hereby
      assume
      z in the_fixed_points_of LO';
      then z in {x where x is Element of EE: x is_fixed_under LO'} by
      Def13;
      then consider e be Element of EE such that
A5:   z=e and
A6:   e is_fixed_under LO';
      EE is non empty by A4,Th2;
      then z in EE by A5;
      then consider X be Subset of E such that
A7:   z=X and
A8:   card X = n;

A9:   for g being Element of G holds e = the_left_translation_of(g,T) .:
      e

      proof
        let g be Element of G;
        LO'.g = the_extension_of_left_translation_of(n,g,LO) by A4,Def5;
        then
A10:    (LO'^g).e = (LO^g) .: e by A4,Def4;
        e = (LO'^g).e by A6,Def12;
        hence thesis by A1,A10,Def7;
      end;
      ex t being Element of T st [:the carrier of G,{t}:] c= z
      proof
        X is not empty by A2,A8;
        then consider x1 be set such that
A11:    x1 in X by XBOOLE_0:def 1;
        consider g1,t be set such that
A12:    g1 in the carrier of G and
A13:    t in T and
A14:    x1=[g1,t] by A3,A11,ZFMISC_1:def 2;
        reconsider t as Element of T by A13;
        reconsider g1 as Element of G by A12;
        take t;
        now
          reconsider z1'=x1 as Element of [: the carrier of G,T :] by A3,A11;
          let x2 be set;
          assume
          x2 in [:the carrier of G,{t}:];
          then consider g2,t' be set such that
A15:      g2 in the carrier of G and
A16:      t' in {t} & x2=[g2,t'] by ZFMISC_1:def 2;
          reconsider g2 as Element of G by A15;
          set g=g2*g1";

          consider z2' be Element of [: the carrier of G,T :], g1', g2' be
          Element of G, t'' be Element of T such that

A17:      z2'=the_left_translation_of(g,T).z1' and
A18:      g2' = g * g1' and
A19:      z1' = [g1',t''] and
A20:      z2' = [g2',t''] by Def6;
A21:      t=t'' by A14,A19,ZFMISC_1:33;
          dom the_left_translation_of(g,T) = E by A3,FUNCT_2:def 1;

          then
A22:      the_left_translation_of(g,T).x1 in the_left_translation_of(g,T)
          .: e by A5,A7,A11,FUNCT_1:def 12;

          g2'=g2*g1"*g1 by A14,A18,A19,ZFMISC_1:33
            .= g2*(g1"*g1) by GROUP_1:def 4
            .= g2*1_G by GROUP_1:def 6
            .= g2 by GROUP_1:def 5;
          then z2' = x2 by A16,A20,A21,TARSKI:def 1;
          hence x2 in z by A5,A9,A17,A22;
        end;
        hence thesis by TARSKI:def 3;
      end;
      then consider t be Element of T such that
A23:  [:the carrier of G,{t}:] c= z;
      n,X are_equipotent by A8,CARD_1:def 5;
      then consider f be Function such that
      f is one-to-one and
A24:  dom f = n and
A25:  rng f = X by WELLORD2:def 4;
      dom f in omega by A24,ORDINAL1:def 13;
      then reconsider X as finite set by A25,FINSET_1:def 1;
      card [:the carrier of G,{t}:] = card the carrier of G by CARD_2:13
        .= n by A2;
      then [:the carrier of G,{t}:] = X by A7,A8,A23,CARD_FIN:1;

      hence z in {[:the carrier of G,{t'}:] where t' is Element of T: not
      contradiction} by A7;

    end;
    assume

    z in {[:the carrier of G,{t}:] where t is Element of T: not
    contradiction};

    then consider t be Element of T such that
A26: z = [:the carrier of G,{t}:];
A27: now
      let z' be set;
      assume
      z' in z;

      then ex x,y being set st x in the carrier of G & y in {t} & z'=[x,y] by
      A26,ZFMISC_1:def 2;

      hence z' in [:the carrier of G,T:] by ZFMISC_1:def 2;
    end;
    then reconsider X=z as Subset of E by A3,TARSKI:def 3;
    card X = card the carrier of G by A26,CARD_2:13;
    then z in EE by A2;
    then reconsider e=z as Element of EE;
A28: z c= [:the carrier of G,T:] by A27,TARSKI:def 3;
A29: for g being Element of G holds e = (LO^g) .: e
    proof
      let g be Element of G;
A30:  LO^g = the_left_translation_of(g,T) by A1,Def7;
      now
        let e2 be set;
        hereby
          assume
          e2 in e;
          then consider g2,t2 be set such that
A31:      g2 in the carrier of G and
A32:      t2 in {t} and
A33:      e2=[g2,t2] by A26,ZFMISC_1:def 2;
          reconsider g2 as Element of G by A31;
          reconsider e1=[g"*g2,t2] as set;
          take e1;
A34:      z c= dom(LO^g) by A3,A28,FUNCT_2:def 1;
          e1 in e by A26,A32,ZFMISC_1:def 2;
          hence e1 in dom(LO^g) & e1 in e by A34;
          then reconsider z1=e1 as Element of [:the carrier of G,T:] by A3;

          consider z2 be Element of [:the carrier of G,T:], g1',g2' be Element
          of G, t' be Element of T such that

A35:      z2=(LO^g).z1 and
A36:      g2'=g * g1' and
A37:      z1=[g1',t'] and
A38:      z2=[g2',t'] by A30,Def6;
          g2' = g*(g"*g2) by A36,A37,ZFMISC_1:33
            .= g*g"*g2 by GROUP_1:def 4
            .= 1_G*g2 by GROUP_1:def 6
            .= g2 by GROUP_1:def 5;
          hence e2 = (LO^g).e1 by A33,A35,A37,A38,ZFMISC_1:33;
        end;
        given e1 be set such that
A39:    e1 in dom (LO^g) and
A40:    e1 in e and
A41:    e2 = (LO^g).e1;
        reconsider z1=e1 as Element of [:the carrier of G,T:] by A3,A39;

        consider z2 be Element of [:the carrier of G,T:], g1',g2' be Element
        of G, t' be Element of T such that

A42:    z2=(LO^g).z1 and
        g2'=g * g1' and
A43:    z1=[g1',t'] and
A44:    z2=[g2',t'] by A30,Def6;
A45:    t' in {t'} by TARSKI:def 1;
        consider g1,t1 be set such that
        g1 in the carrier of G and
A46:    t1 in {t} and
A47:    e1=[g1,t1] by A26,A40,ZFMISC_1:def 2;
        t1 = t by A46,TARSKI:def 1;
        then t'=t by A47,A43,ZFMISC_1:33;
        hence e2 in e by A26,A41,A42,A44,A45,ZFMISC_1:def 2;
      end;
      hence thesis by FUNCT_1:def 12;
    end;
    for g being Element of G holds e = (LO'^g).e
    proof
      let g be Element of G;
      LO'.g = the_extension_of_left_translation_of(n,g,LO) by A4,Def5;
      then (LO'^g).e = (LO^g) .: e by A4,Def4;
      then consider Y be Element of EE such that
A48:  [Y,(LO^g).:Y] = [e,(LO'^g).e];
      Y=e & (LO^g).:Y=(LO'^g).e by A48,ZFMISC_1:33;
      hence thesis by A29;
    end;
    then e is_fixed_under LO' by Def12;
    then
A49: z in {x where x is Element of EE: x is_fixed_under LO'};
    EE is non empty by A4,Th2;
    hence z in the_fixed_points_of LO' by A49,Def13;
  end;
  hence thesis by TARSKI:2;
end;

:: ALG I.6.6 Lemma

Lm5: for n,m,r being natural number, p being prime (natural number) st n = p
|^ r * m & not p divides m holds (n choose p|^r) mod p <> 0

proof
  reconsider x1 = 1,x2 = 0 as set;
  let n,m,r be natural number;
  let p be prime (natural number);
  assume
A1: n = p |^ r * m;
  reconsider n'=n as real number;
  reconsider k2 = m as Element of NAT by ORDINAL1:def 13;
  reconsider p' = p as prime Element of NAT by ORDINAL1:def 13;
  reconsider k1 = p'|^r as non zero Element of NAT by ORDINAL1:def 13;
  set S = INT.Group(k1);
  set CS = the carrier of S;
  set T = Segm k2;
  card T = card k2 by CARD_1:def 12;
  then
A2: card T = m by CARD_1:def 5;
  assume
A3: not p divides m;
  then
A4: k2<>0 by NAT_D:6;
  then reconsider T as non empty finite set;
  set X = [: CS, T :];
  reconsider X'=X as non empty finite set;
  set E = the_subsets_of_card(k1,X');
  multMagma(#Segm(k1),addint(k1)#) = S by GR_CY_1:def 6;
  then card CS = card k1 by CARD_1:def 12;
  then card CS = p|^r by CARD_1:def 5;
  then
A5: card X = n by A1,A2,CARD_2:65;
  now
    assume
    p |^ r > n;
    then p |^ r * m > n * m by A4,XREAL_1:70;
    then (n' / n') > n' * m / n' by A1,XREAL_1:76;
    then 1 > (n' * m / n') by A5,XCMPLX_1:60;
    then 1 > (n' / n') * m by XCMPLX_1:75;
    then 1 > 1 * m by A5,XCMPLX_1:60;
    then m < 0 + 1;
    hence contradiction by A4,NAT_1:13;
  end;
  then
A6: card k1 c= card card X by A5,NAT_1:41;
  then reconsider E'=E as non empty finite set by Th2;
  card Choose(X,k1,x1,x2) = card E' by Th3;
  then
A7: card E' = n choose k1 by A5,CARD_FIN:18;
  set LO = the_left_operation_of(S,T);
  set LO' = the_extension_of_left_operation_of(k1,LO);
A8: now
    assume
    m mod p' = 0;
    then m = p * (m div p) + 0 by NAT_D:2;
    hence contradiction by A3,NAT_D:def 3;
  end;
A9: card S = k1 by Lm2;

  then
  card the_fixed_points_of LO' = card {[:CS,{t}:] where t is Element of T
  : not contradiction} by A6,Lm4

    .= card T by Lm3
    .= card k2 by CARD_1:def 12;
  then
A10: card the_fixed_points_of LO' = m by CARD_1:def 5;
  S is_p-group_of_prime p by A9,Def17;
  hence thesis by A7,A10,A8,Th11;
end;

Lm6: for r,n,m1,m2 being natural number, p being prime (natural number) st p|^
r * n = m1 * m2 & not p divides n & not p divides m2 holds p|^r divides m1

proof

  defpred P[Nat] means for n,m1,m2 be natural number, p being prime (natural

number) st p|^$1 * n = m1 * m2 & not p divides n & not p divides m2 holds p|^$1
  divides m1;

A1: for k be Nat st P[k] holds P[k + 1]
  proof
    let k be Nat;
    assume
A2: P[k];

    for n,m1,m2 be natural number, p being prime (natural number) st p|^(k

+1) * n = m1 * m2 & not p divides n & not p divides m2 holds p|^(k+1) divides
    m1

    proof
      let n,m1,m2 be natural number;
      let p be prime (natural number);
      assume
A3:   p|^(k+1) * n = m1 * m2;
      reconsider p'=p as prime Element of NAT by ORDINAL1:def 13;
      assume
A4:   not p divides n;
      assume
A5:   not p divides m2;
A6:   p|^(k+1) * n = p|^k * p * n by NEWTON:11
        .= p|^k * n * p;
      then p divides m1 * m2 by A3,NAT_D:def 3;
      then p divides m1 by A5,NEWTON:98;
      then consider m1' be natural number such that
A7:   m1 = p * m1' by NAT_D:def 3;
      p|^k * n * p' = m1' * m2 * p' by A3,A6,A7;
      then p|^k divides m1' by A2,A4,A5,XCMPLX_1:5;
      then consider m1'' be natural number such that
A8:   m1' = p|^k * m1'' by NAT_D:def 3;
      m1 = p|^k * p * m1'' by A7,A8
        .= p|^(k+1) * m1'' by NEWTON:11;
      hence thesis by NAT_D:def 3;
    end;
    hence thesis;
  end;
A9: P[0]
  proof
    let n,m1,m2 be natural number;
    let p be prime (natural number);
    assume that
    p|^0 * n = m1 * m2 and
    not p divides n and
    not p divides m2;
    p|^0 = 1 by NEWTON:9;
    hence thesis by NAT_D:6;
  end;
  for k being Nat holds P[k] from NAT_1:sch 2(A9,A1);
  hence thesis;
end;

:: like GROUP_8:16

Lm7: for G being Group, A being non empty Subset of G, x being Element of G
holds card A = card (A * x)

proof
  let G be Group;
  let A be non empty Subset of G;
  let x be Element of G;
  set B = A * x;
  A is non empty & B is non empty
  proof
    consider a being Element of A;
    a * x in B by GROUP_2:34;
    hence thesis;
  end;
  then reconsider B as non empty Subset of G;
  deffunc F(Element of G) = $1*x;
  consider f being Function of A, the carrier of G such that
A1: for a being Element of A holds f.a = F(a) from FUNCT_2:sch 4;
A2: B c= rng f
  proof
    let s be set;
    assume
    s in B;
    then consider a being Element of G such that
A3: s = a * x & a in A by GROUP_2:34;
    ex x being set st x in dom f & s = f.x
    proof
      take x = a;
      thus thesis by A1,A3,FUNCT_2:def 1;
    end;
    hence thesis by FUNCT_1:def 5;
  end;
A4: dom f = A by FUNCT_2:def 1;
A5: rng f c= B
  proof
    let s be set;
    assume
    s in rng f;
    then consider a being set such that
A6: a in A and
A7: s = f.a by A4,FUNCT_1:def 5;
    reconsider a as Element of A by A6;
    s = F(a) by A1,A7;
    hence thesis by GROUP_2:34;
  end;
  then reconsider f as Function of A,B by A4,FUNCT_2:4;
A8: for a,b being Element of A st f.a = f.b holds a = b
  proof
    let a,b be Element of A;
    assume
    f.a = f.b;
    then F(a) = f.b by A1;
    then a*x = b*x by A1;
    hence thesis by GROUP_1:14;
  end;
  A,B are_equipotent
  proof
    take f;
    thus thesis by A5,A2,A8,FUNCT_2:def 1,GROUP_6:1,XBOOLE_0:def 10;
  end;
  hence thesis by CARD_1:21;
end;

:: ALG I.6.6 Theorem 2
:: The first Sylow theorem

theorem Th12:
  for G being finite Group, p being prime (natural number) ex P
  being strict Subgroup of G st P is_Sylow_p-subgroup_of_prime p
proof
  reconsider x1 = 1,x2 = 0 as set;
  let G be finite Group;
  let p be prime (natural number);
  reconsider p'=p as prime Element of NAT by ORDINAL1:def 13;
  set n = card G;
  set LO = the_left_operation_of G;
  consider m,r be natural number such that
A1: card G = p |^ r * m and
A2: not p divides m by Lm1;
  reconsider k1 = p'|^r as Element of NAT by ORDINAL1:def 13;
  set SF = the_subsets_of_card(k1, the carrier of G);
  card SF = card Choose(the carrier of G,k1,x1,x2) by Th3;
  then
A3: card SF = n choose k1 by CARD_FIN:18;
  then card SF mod p <> 0 by A1,A2,Lm5;
  then reconsider E = SF as non empty finite set by CARD_1:47,NAT_D:26;
  set LO' = the_extension_of_left_operation_of(k1,LO);
  reconsider T = LO' as LeftOperation of G, E;
  ex X being Element of E st card the_orbit_of(X,T) mod p <> 0
  proof
    consider f be FinSequence such that
A4: rng f = the_orbits_of T and
A5: f is one-to-one by FINSEQ_4:73;
    reconsider f as FinSequence of the_orbits_of T by A4,FINSEQ_1:def 4;
    deffunc F(set) = card(f.$1);
    consider pf being FinSequence such that
A6: len pf = len f & for i being Nat st i in dom pf holds pf.i = F(i)
    from FINSEQ_1:sch 2;
    for x being set st x in rng pf holds x in NAT
    proof
      let x be set;
      assume
      x in rng pf;
      then consider i be Nat such that
A7:   i in dom pf and
A8:   pf.i = x by FINSEQ_2:11;
      i in dom f by A6,A7,FINSEQ_3:31;
      then f.i in the_orbits_of T by A4,FUNCT_1:12;
      then consider X be Subset of E such that
A9:   f.i=X and
      ex x' being Element of E st X=the_orbit_of(x',T);
      x = card X by A6,A7,A8,A9;
      hence thesis;
    end;
    then rng pf c= NAT by TARSKI:def 3;
    then reconsider c = pf as FinSequence of NAT by FINSEQ_1:def 4;
    deffunc F'(set) = c.($1) / p;
    consider c' being FinSequence such that
A10: len c' = len c & for i being Nat st i in dom c' holds c'.i = F'(i
    ) from FINSEQ_1:sch 2;
    assume
A11: for X being Element of E holds card the_orbit_of(X,T) mod p = 0;
    for x being set st x in rng c' holds x in NAT
    proof
      reconsider p''=p' as real number;
      let x be set;
      assume
      x in rng c';
      then consider i be Nat such that
A12:  i in dom c' and
A13:  c'.i = x by FINSEQ_2:11;
      reconsider i as Element of NAT by ORDINAL1:def 13;
      i in dom f by A6,A10,A12,FINSEQ_3:31;
      then f.i in the_orbits_of T by A4,FUNCT_1:12;
      then consider X be Subset of E such that
A14:  f.i=X and
A15:  ex x' being Element of E st X=the_orbit_of(x',T);
      dom pf = dom c' by A10,FINSEQ_3:31;
      then c.i = card X by A6,A12,A14;
      then
A16:  c.i mod p = 0 by A11,A15;
      set q = c.i div p';
      c.i = p' * q + (c.i mod p') by NAT_D:2
        .= p' * q by A16;
      then consider q be natural number such that
A17:  c.i = p * q;
      x = p'' * q / p'' by A10,A12,A13,A17
        .= p'' / p'' * q by XCMPLX_1:75
        .= 1 * q by XCMPLX_1:60;
      hence thesis;
    end;
    then rng c' c= NAT by TARSKI:def 3;
    then reconsider c' as FinSequence of NAT by FINSEQ_1:def 4;
A18: dom c = Seg len c' by A10,FINSEQ_1:def 3
      .= dom c' by FINSEQ_1:def 3
      .= dom(p'*c') by VALUED_1:def 5;
    for k being Nat st k in dom c holds c.k = (p'*c').k
    proof
      reconsider p''=p' as real number;
      let k be Nat;
      assume
A19:  k in dom c;
      then k in dom c' by A10,FINSEQ_3:31;
      then p * c'.k = p * (c.k / p') by A10
        .= p'' * c.k / p'' by XCMPLX_1:75
        .= (p'' / p'') * c.k by XCMPLX_1:75
        .= 1 * c.k by XCMPLX_1:60;
      hence thesis by A18,A19,VALUED_1:def 5;
    end;
    then
A20: c = p' * c' by A18,FINSEQ_1:17;
    for i being Element of NAT st i in dom pf holds pf.i = F(i) by A6;
    then card E = Sum c by A4,A5,A6,WEDDWITT:6;
    then card E mod p = p'*(Sum c') mod p' by A20,RVSUM_1:117
      .= 0 by NAT_D:13;
    hence contradiction by A1,A2,A3,Lm5;
  end;
  then consider X be Element of E such that
A21: card the_orbit_of(X,T) mod p <> 0;
  set HX = the_strict_stabilizer_of(X,T);
  card the_orbit_of(X,T) = Index HX by Th10;
  then
A22: index HX mod p <> 0 by A21,GROUP_2:def 18;
A23: now
    assume
    p divides index HX;
    then ex t be Nat st index HX = p * t by NAT_D:def 3;
    hence contradiction by A22,NAT_D:13;
  end;
  take HX;
  X in {X' where X' is Subset of the carrier of G: card X' = k1};
  then
A24: ex X' being Subset of the carrier of G st X'=X & card X' = k1;
  reconsider H=HX as finite Group;
A25: the carrier of HX={g where g is Element of G: (T^g).:{X} = {X}} by
  Def10;
  reconsider X as non empty Subset of G by A24;
  consider x be Element of X;
  reconsider x as Element of G;
  k1 divides n by A1,NAT_D:def 3;
  then k1 <= n by NAT_D:7;
  then card k1 <= card the carrier of G by CARD_1:def 5;
  then
A26: card k1 c= card the carrier of G by NAT_1:40;
  now
    reconsider X1=X as Element of E;
    let z be set;
    assume
    z in the carrier of H;
    then consider g be Element of G such that
A27: z=g and
A28: (T^g).:{X}={X} by A25;
    dom(T^g) = E by FUNCT_2:def 1;
    then Im(T^g,X) = {(T^g).X} by FUNCT_1:117;
    then
A29: (T^g).X = X by A28,ZFMISC_1:6;
    set h = g * x;
    T.g = the_extension_of_left_translation_of(k1,g,LO) by A26,Def5;
    then
A30: (T^g).X1 = (LO^g) .: X1 by A26,Def4;
    reconsider LO as LeftOperation of G,the carrier of G;
A31: LO^g = the_left_translation_of g by Def2;
    ex x' being set st x' in dom(LO^g) & x' in X & g * x = (LO^g).x'
    proof
      reconsider x'=x as set;
      take x';
      dom(LO^g) = the carrier of G by FUNCT_2:def 1;
      hence x' in dom(LO^g) & x' in X;
      thus thesis by A31,TOPGRP_1:def 1;
    end;
    then
A32: g * x in (LO^g) .: X by FUNCT_1:def 12;
    h * x" = g * (x*x") by GROUP_1:def 4
      .= g * 1_G by GROUP_1:def 6
      .= z by A27,GROUP_1:def 5;
    hence z in X * x" by A29,A30,A32,GROUP_2:34;
  end;
  then the carrier of H c= X * x" by TARSKI:def 3;
  then card (the carrier of H) <= card (X * x") by NAT_1:44;
  then
A33: card H <= p|^r by A24,Lm7;
  p|^r * m = card HX * index HX by A1,GROUP_2:177;
  then p|^r divides card H by A2,A23,Lm6;
  then
A34: p|^r <= card H by NAT_D:7;
  then card H = p|^r by A33,XXREAL_0:1;
  then H is_p-group_of_prime p by Def17;
  then
A35: HX is_p-group_of_prime p by Def18;
  index HX = index HX * card HX * ( 1 / card HX) by XCMPLX_1:108
    .= p|^r * m * (1 / card HX) by A1,GROUP_2:177
    .= p'|^r * m * (1 / p'|^r) by A34,A33,XXREAL_0:1
    .= m * (p'|^r * (1/p'|^r))
    .= m * (p'|^r / p'|^r) by XCMPLX_1:100
    .= m by XCMPLX_1:89;
  hence thesis by A2,A35,Def19;
end;

Lm8: for n,r being natural number, p being prime (natural number) st n divides
p|^r & n > 1 holds p divides n

proof
  let n,r be natural number;
  let p be prime (natural number);
  assume
A1: n divides p|^r;
  reconsider n'=n as Element of NAT by ORDINAL1:def 13;
  assume
A2: n > 1;
  per cases by A1,PEPIN:33;
  suppose
    n' = 1;
    hence thesis by A2;
  end;
  suppose
    ex k being Element of NAT st n' = p*k;
    hence thesis by NAT_D:def 3;
  end;
end;

:: ALG I.6.6 Corollary
:: The Cauchy theorem

theorem
  for G being finite Group, p being prime (natural number) st p divides
  card G ex g being Element of G st ord g = p
proof
  let G be finite Group;
  let p be prime (natural number);
  reconsider p' = p as prime Element of NAT by ORDINAL1:def 13;
A1: 1 < p' by NAT_4:12;
  consider P be strict Subgroup of G such that
A2: P is_Sylow_p-subgroup_of_prime p by Th12;
  P is_p-group_of_prime p by A2,Def19;
  then consider H be finite Group such that
A3: P = H and
A4: H is_p-group_of_prime p by Def18;
  consider r be natural number such that
A5: card H = p |^ r by A4,Def17;
  assume
A6: p divides card G;
  now
    assume
    r=0;
    then card H = 1 by A5,NEWTON:9;
    then card G = 1 * index P by A3,GROUP_2:177;
    hence contradiction by A6,A2,Def19;
  end;
  then 0 + 1 < r + 1 by XREAL_1:8;
  then 1 <= r by NAT_1:13;
  then 1 - 1 <= r - 1 by XREAL_1:11;
  then reconsider r' = r - 1 as Element of NAT by INT_1:16;
  0 + 1 < p'|^r' + 1 by XREAL_1:8;
  then 1 <= p|^r' by NAT_1:13;
  then
A7: 1*p <= p|^r'*p by XREAL_1:66;
  set H' = (Omega).H;
A8: card H = card (Omega).H * index (Omega).H by GROUP_2:177
    .= card (Omega).H * 1 by GROUP_2:180;
  p |^ r = p |^(r'+1)
    .= p|^r' * p by NEWTON:11;
  then card H' > 1 by A5,A7,A1,XXREAL_0:2;
  then consider g be Element of H' such that
A9: g <> 1_H' by GR_CY_1:31;
  reconsider H'' = gr {g} as strict Group;
A10: H'' is cyclic Group by GR_CY_2:10;
  reconsider H'' as finite strict Group;
  set n = card H'';
A11: now
    assume
    n = 1;
    then ord g = 1 by GR_CY_1:27;
    hence contradiction by A9,GROUP_1:85;
  end;
  n >= 1 by GROUP_1:90;
  then n > 1 by A11,XXREAL_0:1;
  then p divides n by A5,A8,Lm8,GROUP_2:178;
  then consider H''' be strict Subgroup of H'' such that
A12: card H''' = p' and
  for G2 being strict Subgroup of H'' st card G2=p' holds G2=H''' by A10,
  GR_CY_2:28;
  consider h' be Element of H''' such that
A13: ord h' = p by A12,GROUP_8:1;
  H'' is Subgroup of G by A3,GROUP_2:65;
  then reconsider H''' as strict Subgroup of G by GROUP_2:65;
  reconsider h' as Element of H''';
  reconsider h=h' as Element of G by GROUP_2:51;
  take h;
  thus thesis by A13,GROUP_8:5;
end;

:: ALG I.6.6 Theorem 3
:: The second Sylow theorem

theorem Th14:
  for G being finite Group, p being prime (natural number) holds (
for H being Subgroup of G st H is_p-group_of_prime p holds ex P being Subgroup
  of G st P is_Sylow_p-subgroup_of_prime p & H is Subgroup of P) & (for P1,P2
  being Subgroup of G st P1 is_Sylow_p-subgroup_of_prime p & P2
  is_Sylow_p-subgroup_of_prime p holds P1,P2 are_conjugated)
proof
  let G be finite Group;
  let p be prime (natural number);
  reconsider p'=p as prime Element of NAT by ORDINAL1:def 13;
A1: for H,P being Subgroup of G st H is_p-group_of_prime p & P
is_Sylow_p-subgroup_of_prime p holds ex g being Element of G st carr H c= carr(
  P |^ g)
  proof
    let H,P be Subgroup of G;
    set H'= H;
    set E = Left_Cosets P;
    set T = the_left_operation_of(H',P);
    reconsider E as non empty finite set by GROUP_2:165;
    reconsider T as LeftOperation of H', E;
    assume
    H is_p-group_of_prime p;
    then ex I being finite Group st H = I & I is_p-group_of_prime p by
    Def18;
    then
A2: card the_fixed_points_of T mod p = card E mod p by Th11;
    assume
    P is_Sylow_p-subgroup_of_prime p;
    then
A3: not p divides index P by Def19;
    now
      assume
      card E mod p = 0;
      then (ex t being Nat st card E = p * t + 0 & 0<p) or 0=0 & p=0 by
      NAT_D:def 2;
      then p' divides card E by NAT_D:def 3;
      hence contradiction by A3,GROUP_2:def 18;
    end;
    then card the_fixed_points_of T <> 0 by A2,NAT_D:26;
    then consider x be set such that
A4: x in the_fixed_points_of T by CARD_1:47,XBOOLE_0:def 1;
    x in {x' where x' is Element of E: x' is_fixed_under T} by A4,Def13;
    then consider x' be Element of E such that
    x=x' and
A5: x' is_fixed_under T;
    x' in Left_Cosets P;
    then consider g' be Element of G such that
A6: x' = g' * P by GROUP_2:def 15;
    set g=g'";
    take g;
    now
      reconsider P1=x' as Element of Left_Cosets P;
      let y be set;
      assume
      y in carr H;
      then reconsider h=y as Element of H';
      reconsider h'=h as Element of H;
      consider P2 be Element of Left_Cosets P, A1,A2 be Subset of G, g'' be
      Element of G such that
A7:   P2 = the_left_translation_of(h',P).P1 & A2 = g'' * A1 & A1 = P1
      & A2 = P2 and
A8:   g'' = h' by Def8;
      the_left_operation_of(H,P).h' = the_left_translation_of(h',P) by
      Def9;
      then
A9:   g' * P * g'" = g'' * (g' * P) * g'" by A5,A6,A7,Def12
        .= g'' * (g' * P * g'") by GROUP_2:39;
      g'' in g' * P * g'"
      proof
        1_G in P by GROUP_2:55;
        then
A10:    1_G in carr P by STRUCT_0:def 5;
        g' = g' * 1_G by GROUP_1:def 5;
        then g' * g'" = 1_G & g' in g' * P by A10,
        GROUP_1:def 6,GROUP_2:33;
        then
A11:    g'' = g'' * 1_G & 1_G in (g' * P * g'") by GROUP_1:def 5,
        GROUP_2:34;
        assume
        not g'' in g' * P * g'";
        hence contradiction by A9,A11,GROUP_2:33;
      end;
      hence y in g' * P * g'" by A8;
    end;
    then carr H c= g" * P * g by TARSKI:def 3;
    hence thesis by GROUP_3:71;
  end;
  thus for H being Subgroup of G st H is_p-group_of_prime p holds ex P being
  Subgroup of G st P is_Sylow_p-subgroup_of_prime p & H is Subgroup of P
  proof
    let H be Subgroup of G;
    consider P' be strict Subgroup of G such that
A12: P' is_Sylow_p-subgroup_of_prime p by Th12;
    assume
    H is_p-group_of_prime p;
    then consider g be Element of G such that
A13: carr H c= carr(P' |^ g) by A1,A12;
    set P = P' |^ g;
    take P;
    set H2 = P;
    reconsider H2 as finite Group;
A14: card P' = card P by GROUP_3:79;
    P' is_p-group_of_prime p by A12,Def19;
    then consider H1 be finite Group such that
A15: P' = H1 and
A16: H1 is_p-group_of_prime p by Def18;
    ex r be natural number st card H1 = p |^ r by A16,Def17;
    then H2 is_p-group_of_prime p by A15,A14,Def17;
    then
A17: P is_p-group_of_prime p by Def18;
A18: not p divides index P' by A12,Def19;
    card P * index P = card G by GROUP_2:177
      .= card P * index P' by A14,GROUP_2:177;
    then not p divides index P by A18,XCMPLX_1:5;
    hence P is_Sylow_p-subgroup_of_prime p by A17,Def19;
    thus thesis by A13,GROUP_2:66;
  end;
  let P1,P2 be Subgroup of G;
  assume
A19: P1 is_Sylow_p-subgroup_of_prime p;
  then
A20: P1 is_p-group_of_prime p by Def19;
  then consider H1 be finite Group such that
A21: P1 = H1 and
A22: H1 is_p-group_of_prime p by Def18;
A23: not p divides index P1 by A19,Def19;
  consider r1 be natural number such that
A24: card H1 = p |^ r1 by A22,Def17;
  assume
A25: P2 is_Sylow_p-subgroup_of_prime p;
  then
A26: not p divides index P2 by Def19;
  P2 is_p-group_of_prime p by A25,Def19;
  then consider H2 be finite Group such that
A27: P2 = H2 and
A28: H2 is_p-group_of_prime p by Def18;
  consider r2 be natural number such that
A29: card H2 = p |^ r2 by A28,Def17;
A30: card G = card P2 * index P2 by GROUP_2:177;
  then
A31: p|^r1 * index P1=p|^r2 * index P2 by A21,A24,A27,A29,GROUP_2:177;
  now
    assume
A32: r1<>r2;
    per cases by A32,XXREAL_0:1;
    suppose
A33:  r1 > r2;
      set r = r1 -' r2;
A34:  r1 - r2 > r2 - r2 by A33,XREAL_1:11;
      then
A35:  r = r1 - r2 by XREAL_0:def 2;
      then consider k be Nat such that
A36:  r = k + 1 by A34,NAT_1:6;
      r1 = r2 + r by A35;
      then p' |^ r1 = p' |^r2 * p' |^ r by NEWTON:13;
      then p' |^ r2 * (p' |^ r * index P1) = p' |^ r2 * index P2 by A31;
      then p' |^ r * index P1 = index P2 by XCMPLX_1:5;
      then p |^ k * p * index P1 = index P2 by A36,NEWTON:11;
      then p * (p |^ k * index P1) = index P2;
      hence contradiction by A26,NAT_D:def 3;
    end;
    suppose
A37:  r1<r2;
      set r = r2 -' r1;
A38:  r2 - r1 > r1 - r1 by A37,XREAL_1:11;
      then
A39:  r = r2 - r1 by XREAL_0:def 2;
      then consider k be Nat such that
A40:  r = k + 1 by A38,NAT_1:6;
      r2 = r1 + r by A39;
      then p' |^ r2 = p' |^r1 * p' |^ r by NEWTON:13;
      then p' |^ r1 * (p' |^ r * index P2) = p' |^ r1 * index P1 by A21,A24,
      A27,A29,A30,GROUP_2:177;
      then p' |^ r * index P2 = index P1 by XCMPLX_1:5;
      then p |^ k * p * index P2 = index P1 by A40,NEWTON:11;
      then p * (p |^ k * index P2) = index P1;
      hence contradiction by A23,NAT_D:def 3;
    end;
  end;
  then
A41: card carr P1 = card P2 by A21,A24,A27,A29
    .= card carr P2;
  consider g be Element of G such that
A42: carr P1 c= carr(P2 |^ g) by A1,A20,A25;
  card carr P2 = card P2
    .= card(P2 |^ g) by GROUP_3:79
    .= card carr(P2 |^ g);
  then the multMagma of P1 = the multMagma of P2 |^ g by A42,A41,
  CARD_FIN:
  1 ,GROUP_2:68;
  hence thesis by GROUP_3:def 11;
end;

definition
  let G be Group;
  let p be prime (natural number);
  func the_sylow_p-subgroups_of_prime(p,G) -> Subset of Subgroups G equals
  {H
where H is Element of Subgroups G: ex P being strict Subgroup of G st P = H & P
  is_Sylow_p-subgroup_of_prime p};
  correctness
  proof
    set X = {H where H is Element of Subgroups G: ex P being strict Subgroup
    of G st P = H & P is_Sylow_p-subgroup_of_prime p};
    now
      let x be set;
      assume
      x in X;
      then ex H being Element of Subgroups G st x = H & ex P being strict
      Subgroup of G st P = H & P is_Sylow_p-subgroup_of_prime p;
      hence x in Subgroups G;
    end;
    hence thesis by TARSKI:def 3;
  end;
end;

registration
  let G be finite Group;
  let p be prime (natural number);
  cluster the_sylow_p-subgroups_of_prime(p,G) -> non empty finite;
  correctness
  proof
    consider P be strict Subgroup of G such that
A1: P is_Sylow_p-subgroup_of_prime p by Th12;
    set P' = (Omega).P;
    reconsider H'=P' as Element of Subgroups G by GROUP_3:def 1;
    H' in the_sylow_p-subgroups_of_prime(p,G) by A1;
    hence thesis by FINSET_1:13,GROUP_3:19;
  end;
end;

definition
  let G be finite Group;
  let p be prime (natural number);
  let H be Subgroup of G;
  let h be Element of H;
  func the_left_translation_of(h,p) -> Function of
the_sylow_p-subgroups_of_prime(p,G), the_sylow_p-subgroups_of_prime(p,G) means
  :Def21:
  for P1 being Element of the_sylow_p-subgroups_of_prime(p,G) holds ex P2
  being Element of the_sylow_p-subgroups_of_prime(p,G), H1,H2 being strict
Subgroup of G, g being Element of G st P2 = it.P1 & P1=H1 & P2=H2 & h"=g & H2 =
  H1 |^ g;
  existence
  proof
    set E = the_sylow_p-subgroups_of_prime(p,G);
    set f = {[P1,P2] where P1,P2 is Element of the_sylow_p-subgroups_of_prime(
p,G): ex H1,H2 being strict Subgroup of G, g being Element of G st P1=H1 & P2=
    H2 & h"=g & H2 = H1 |^ g};
A1: now
      let x,y1,y2 be set;
      assume
      [x,y1] in f;
      then consider P1',P2' be Element of E such that
A2:   [x,y1] = [P1',P2'] and
A3:   ex H1,H2 being strict Subgroup of G, g being Element of G st P1'
      =H1 & P2'=H2 & h"=g & H2 = H1 |^ g;
A4:   x = P1' by A2,ZFMISC_1:33;
      assume
      [x,y2] in f;
      then consider P1'',P2'' be Element of E such that
A5:   [x,y2] = [P1'',P2''] and
A6:   ex H1,H2 being strict Subgroup of G, g being Element of G st
      P1''=H1 & P2''=H2 & h"=g & H2 = H1 |^ g;
      x = P1'' by A5,ZFMISC_1:33;
      hence y1 = y2 by A2,A3,A5,A6,A4,ZFMISC_1:33;
    end;
    now
      let x be set;
      assume
      x in f;
      then consider P1,P2 be Element of E such that
A7:   x = [P1,P2] and
      ex H1,H2 being strict Subgroup of G, g being Element of G st P1=H1 &
      P2=H2 & h"=g & H2 = H1 |^ g;
      reconsider y=P1,z=P2 as set;
      take y,z;
      thus x = [y,z] by A7;
    end;
    then reconsider f as Function by A1,FUNCT_1:def 1,RELAT_1:def 1;
    now
      let y be set;
      assume
      y in rng f;
      then consider x be set such that
A8:   [x,y] in f by RELAT_1:def 5;
      consider P1,P2 be Element of E such that
A9:   [x,y] = [P1,P2] and
      ex H1,H2 being strict Subgroup of G, g being Element of G st P1=H1 &
      P2=H2 & h"=g & H2 = H1 |^ g by A8;
      y = P2 by A9,ZFMISC_1:33;
      hence y in E;
    end;
    then
A10: rng f c= E by TARSKI:def 3;
    now
      let x be set;
      hereby
        reconsider g=h" as Element of G by GROUP_2:51;
        assume
        x in E;
        then reconsider P1 = x as Element of E;
        P1 in E;
        then consider H1 be Element of Subgroups G such that
A11:    P1=H1 and
A12:    ex P being strict Subgroup of G st P = H1 & P
        is_Sylow_p-subgroup_of_prime p;
        reconsider H1 as strict Subgroup of G by A12;
        H1 is_p-group_of_prime p by A12,Def19;
        then consider H' be finite Group such that
A13:    H1 = H' & H' is_p-group_of_prime p by Def18;
        set H2 = H1 |^ g;
        reconsider H2 as strict Subgroup of G;
        reconsider H''=H2 as finite Group;
        (ex r be natural number st card H' = p |^ r )& card H' = card H''
        by A13, Def17,GROUP_3:79;
        then H'' is_p-group_of_prime p by Def17;
        then
A14:    H2 is_p-group_of_prime p by Def18;
        reconsider H2'=H2 as Element of Subgroups G by GROUP_3:def 1;
A15:    card H1 = card H2 by GROUP_3:79;
A16:    card H1 * index H1 = card G by GROUP_2:177
          .= card H1 * index H2 by A15,GROUP_2:177;
        not p divides index H1 by A12,Def19;
        then not p divides index H2 by A16,XCMPLX_1:5;
        then H2 is_Sylow_p-subgroup_of_prime p by A14,Def19;
        then H2' in E;
        then reconsider P2=H2 as Element of E;
        reconsider y = P2 as set;
        take y;
        thus [x,y] in f by A11;
      end;
      given y be set such that
A17:  [x,y] in f;
      consider P1,P2 be Element of E such that
A18:  [x,y] = [P1,P2] and
      ex H1,H2 being strict Subgroup of G, g being Element of G st P1=H1 &
      P2=H2 & h"=g & H2 = H1 |^ g by A17;
      x = P1 by A18,ZFMISC_1:33;
      hence x in E;
    end;
    then
A19: dom f = E by RELAT_1:def 4;
    then reconsider f as Function of E,E by A10,FUNCT_2:4;
    take f;
    thus for P1 being Element of E holds ex P2 being Element of E, H1,H2 being
strict Subgroup of G, g being Element of G st P2 = f.P1 & P1=H1 & P2=H2 & h"=g
    & H2 = H1 |^ g
    proof
      let P1 be Element of E;
      set P2=f.P1;
      [P1,P2] in f by A19,FUNCT_1:8;
      then consider P1',P2' be Element of E such that
A20:  [P1,P2]=[P1',P2'] and
A21:  ex H1,H2 being strict Subgroup of G, g being Element of G st
      P1'=H1 & P2'=H2 & h"=g & H2 = H1 |^ g;
      reconsider P2 as Element of E;
      consider H1,H2 be strict Subgroup of G, g be Element of G such that
A22:  P1'=H1 & P2'=H2 & h"=g and
      H2 = H1 |^ g by A21;
      take P2, H1,H2, g;
      thus P2 = f.P1;
      thus thesis by A20,A21,A22,ZFMISC_1:33;
    end;
  end;
  uniqueness
  proof
    let IT1, IT2 be Function of the_sylow_p-subgroups_of_prime(p,G),
    the_sylow_p-subgroups_of_prime(p,G) such that
A23: ( for P1 being Element of the_sylow_p-subgroups_of_prime(p,G)
holds ex P2 being Element of the_sylow_p-subgroups_of_prime(p,G), H1,H2 being
strict Subgroup of G, g being Element of G st P2 = IT1.P1 & P1=H1 & P2=H2 & h"=
g & H2 = H1 |^ g)& for P1 being Element of the_sylow_p-subgroups_of_prime(p,G)
holds ex P2 being Element of the_sylow_p-subgroups_of_prime(p,G), H1,H2 being
strict Subgroup of G, g being Element of G st P2 = IT2.P1 & P1=H1 & P2=H2 & h"=
    g & H2 = H1 |^ g;
    let x be Element of the_sylow_p-subgroups_of_prime(p,G);
    (ex P12 be Element of the_sylow_p-subgroups_of_prime(p,G), H11,H12 be
strict Subgroup of G, g1 be Element of G st P12 = IT1.x & x=H11 & P12=H12 & h"=
g1 & H12 = H11 |^ g1 )& ex P22 be Element of the_sylow_p-subgroups_of_prime(p,
G), H21,H22 be strict Subgroup of G, g2 be Element of G st P22 = IT2.x & x= H21
    & P22=H22 & h"=g2 & H22 = H21 |^ g2 by A23;
    hence IT1.x = IT2.x;
  end;
end;

definition
  let G be finite Group;
  let p be prime (natural number);
  let H be Subgroup of G;
  func the_left_operation_of(H,p) -> LeftOperation of H,
  the_sylow_p-subgroups_of_prime(p,G) means
  :Def22:
  for h being Element of H
  holds it.h = the_left_translation_of(h,p);
  existence
  proof
    deffunc f(Element of H)=the_left_translation_of($1,p);
    set E = the_sylow_p-subgroups_of_prime(p,G);
A1: for h1,h2 being Element of H holds f(h1*h2)=f(h1)*f(h2)
    proof
      let h1,h2 be Element of H;
      set f12 = the_left_translation_of(h1*h2,p);
      set f1 = the_left_translation_of(h1,p);
      set f2 = the_left_translation_of(h2,p);
      f1 in Funcs(E,E) by FUNCT_2:12;
      then
A2:   ex f being Function st f1=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
      f2 in Funcs(E,E) by FUNCT_2:12;
      then
A3:   ex f being Function st f2=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
A4:   now
        let x be set;
        assume
A5:     x in dom f12;
        then reconsider P1'=x as Element of E;
        reconsider P1'''=x as Element of E by A5;
        consider P2' be Element of E, H1',H2' be strict Subgroup of G, g2 be
        Element of G such that
A6:     P2'=f2.P1' & P1'=H1' & P2'=H2' and
A7:     h2"=g2 and
A8:     H2' = H1' |^ g2 by Def21;
        f2.x in rng f2 by A3,A5,FUNCT_1:12;
        then reconsider P1''=f2.x as Element of E;
        consider P2'' be Element of E, H1'',H2'' be strict Subgroup of G, g1
        be Element of G such that
A9:     P2''=f1.P1'' and
A10:    P1''=H1'' & P2''=H2'' and
A11:    h1"=g1 and
A12:    H2'' = H1'' |^ g1 by Def21;
        consider P2''' be Element of E, H1''',H2''' be strict Subgroup of G,
        g3 be Element of G such that
A13:    P2'''=f12.P1''' and
A14:    P1'''=H1''' and
A15:    P2'''=H2''' and
A16:    (h1*h2)"=g3 and
A17:    H2'''=H1''' |^ g3 by Def21;
        g3 = h2" * h1" by A16,GROUP_1:25;
        then P2''' = H1''' |^ (g2 * g1) by A7,A11,A15,A17,GROUP_2:52
          .= P2'' by A6,A8,A10,A12,A14,GROUP_3:72;
        hence f12.x = f1.(f2.x) by A9,A13;
      end;
      f12 in Funcs(E,E) by FUNCT_2:12;
      then
A18:  ex f being Function st f12=f & dom f = E & rng f c= E by
      FUNCT_2:
      def 2;
      now
        let x be set;
        hereby
          assume
A19:      x in dom f12;
          hence x in dom f2 by A3;
          f2.x in rng f2 by A3,A19,FUNCT_1:12;
          hence f2.x in dom f1 by A2;
        end;
        assume that
A20:    x in dom f2 and
        f2.x in dom f1;
        thus x in dom f12 by A18,A20;
      end;
      hence thesis by A4,FUNCT_1:20;
    end;
A21: f(1_H) = id E
    proof
      set f = the_left_translation_of(1_H,p);
A22:  now
        let x be set;
        assume
        x in E;
        then reconsider P1=x as Element of E;
        consider P2 be Element of E, H1,H2 be strict Subgroup of G, g be
        Element of G such that
A23:    P2=f.P1 & P1=H1 & P2=H2 and
A24:    (1_H)"=g and
A25:    H2 = H1 |^ g by Def21;
        (1_H)" = 1_H by GROUP_1:16;
        then g = 1_G by A24,GROUP_2:53;
        hence f.x = x by A23,A25,GROUP_3:73;
      end;
      f in Funcs(E,E) by FUNCT_2:12;
      then ex f' being Function st f=f' & dom f' = E & rng f' c= E by
      FUNCT_2:def 2;
      hence thesis by A22,FUNCT_1:34;
    end;
    ex T being LeftOperation of H, E st for h being Element of H holds T.
    h = f(h) from ExLeftOperation(A21,A1 );
    hence thesis;
  end;
  uniqueness
  proof
    let T1, T2 be LeftOperation of H, the_sylow_p-subgroups_of_prime(p,G) such
    that
A26: for h being Element of H holds T1.h = the_left_translation_of(h,p
    ) and
A27: for h being Element of H holds T2.h = the_left_translation_of(h,p
    );
    let x be Element of H;
    thus T1.x = the_left_translation_of(x,p) by A26
      .= T2.x by A27;
  end;
end;

Lm9: for G,H being finite Group, p being prime (natural number), I being
Subgroup of G, P being Subgroup of H st I = P & I is_Sylow_p-subgroup_of_prime
p & H is Subgroup of G holds P is_Sylow_p-subgroup_of_prime p

proof
  let G,H be finite Group;
  let p be prime (natural number);
  let I be Subgroup of G;
  let P be Subgroup of H;
  assume
A1: I = P;
  assume
A2: I is_Sylow_p-subgroup_of_prime p;
  then I is_p-group_of_prime p by Def19;

  then ex I' being finite Group st I = I' & I' is_p-group_of_prime p by
  Def18;

  then
A3: P is_p-group_of_prime p by A1,Def18;
  assume
  H is Subgroup of G;
  then reconsider H'=H as Subgroup of G;
A4: index I = index P * index H' by A1,GROUP_2:179;
  not p divides index I by A2,Def19;
  then not p divides index P by A4,NAT_D:9;
  hence thesis by A3,Def19;
end;

:: ALG I.6.6 Theorem 3
:: The third Sylow theorem

theorem
  for G being finite Group, p being prime (natural number) holds card
  the_sylow_p-subgroups_of_prime(p,G) mod p = 1 & card
  the_sylow_p-subgroups_of_prime(p,G) divides card G
proof
  let G be finite Group;
  let p be prime (natural number);
  set E = the_sylow_p-subgroups_of_prime(p,G);
A1: p > 1 by NAT_4:12;
  consider P be strict Subgroup of G such that
A2: P is_Sylow_p-subgroup_of_prime p by Th12;
  set P' = (Omega).P;
  reconsider P' as strict Subgroup of G;
  reconsider H'=P' as Element of Subgroups G by GROUP_3:def 1;
  reconsider P' as strict finite Subgroup of G;
  H'=P';
  then P' in E by A2;
  then reconsider P' as Element of E;
  set T = the_left_operation_of(P,p);
  P is_p-group_of_prime p by A2,Def19;
  then
A3: ex H be finite Group st P = H & H is_p-group_of_prime p by Def18;
  set G' = (Omega).G;
  set T' = the_left_operation_of(G',p);
  set K = the_strict_stabilizer_of(P',T');
A4: now
    reconsider P1=P' as Element of E;
    reconsider P''=P' as strict Subgroup of G;
    let y be set;
    thus y in the_orbit_of(P',T') implies y in E;
    assume
    y in E;
    then consider Q be Element of Subgroups G such that
A5: y=Q and
A6: ex P being strict Subgroup of G st P = Q & P
    is_Sylow_p-subgroup_of_prime p;
    consider Q' be strict Subgroup of G such that
A7: Q' = Q and
A8: Q' is_Sylow_p-subgroup_of_prime p by A6;
    P'',Q' are_conjugated by A2,A8,Th14;
    then consider g be Element of G such that
A9: Q' = P'' |^ g by GROUP_3:def 11;
    Q' in E by A7,A8;
    then reconsider Q''=Q' as Element of E;
    reconsider g'=g" as Element of G';
A10: g'" = (g")" by GROUP_2:57
      .= g;
    ex P2 be Element of E, H1,H2 be strict Subgroup of G, g''' be Element
of G st P2=the_left_translation_of(g',p).P1 & P1=H1 & P2=H2 & g'"=g''' & H2 =
    H1 |^ g''' by Def21;
    then Q' = (T'^g').P' by A9,A10,Def22;
    then P',Q'' are_conjugated_under T' by Def14;
    hence y in the_orbit_of(P',T') by A5,A7;
  end;
  reconsider P as finite Subgroup of G;
  reconsider T as LeftOperation of P, E;
  for x being set holds x in the_fixed_points_of T iff x=P'
  proof
    let x be set;
    for h being Element of P holds P' = (T^h).P'
    proof
      reconsider P1=P' as Element of E;
      let h be Element of P;
      consider P2 be Element of E, H1,H2 be strict Subgroup of G, g be Element
      of G such that
A11:  P2=the_left_translation_of(h,p).P1 and
A12:  P1=H1 and
A13:  P2=H2 and
A14:  h"=g and
A15:  H2 = H1 |^ g by Def21;
A16:  g in H1 by A12,A14,STRUCT_0:def 5;
      now
        let y be set;
        assume
A17:    y in carr H1;
        then reconsider h'=y as Element of G;
A18:    h' in H1 by A17,STRUCT_0:def 5;
        ex h being Element of G st y = h * g & h in g" * H1
        proof
          set h'' = h'*g";
          take h'';
          thus h'' * g = h' * (g" * g) by GROUP_1:def 4
            .= h' * 1_G by GROUP_1:def 6
            .= y by GROUP_1:def 5;
          g" in H1 by A16,GROUP_2:60;
          then
A19:      h'' in H1 by A18,GROUP_2:59;
          ex h being Element of G st h'' = g" * h & h in H1
          proof
            set h''' = g*h'';
            take h''';
            thus g" * h''' = g" * g * h'' by GROUP_1:def 4
              .= 1_G * h'' by GROUP_1:def 6
              .= h'' by GROUP_1:def 5;
            thus thesis by A16,A19,GROUP_2:59;
          end;
          hence thesis by GROUP_2:125;
        end;
        hence y in g" * H1 * g by GROUP_2:34;
      end;
      then carr H1 c= g" * H1 * g by TARSKI:def 3;
      then
A20:  carr H1 c= carr (H1 |^ g) by GROUP_3:71;
A21:  card carr H1 = card H1
        .= card(H1 |^ g) by GROUP_3:79
        .= card carr(H1 |^ g);
      (T^h).P'=P2 by A11,Def22;
      hence thesis by A12,A13,A15,A20,A21,CARD_FIN:1,GROUP_2:68;
    end;
    then
A22: P' is_fixed_under T by Def12;
    hereby
      assume
      x in the_fixed_points_of T;
      then x in {x' where x' is Element of E: x' is_fixed_under T} by
      Def13;
      then consider Q be Element of E such that
A23:  x=Q and
A24:  Q is_fixed_under T;
      Q in {H'' where H'' is Element of Subgroups G: ex P being strict
      Subgroup of G st P = H'' & P is_Sylow_p-subgroup_of_prime p};
      then consider H'' be Element of Subgroups G such that
A25:  Q=H'' and
A26:  ex P being strict Subgroup of G st P = H'' & P
      is_Sylow_p-subgroup_of_prime p;
      consider Q' be strict Subgroup of G such that
A27:  Q' = H'' and
A28:  Q' is_Sylow_p-subgroup_of_prime p by A26;
      set N = Normalizator Q';
      now
        let y be set;
        assume
A29:    y in the carrier of P;
        ex h being Element of G st y = h & Q' |^ h = Q'
        proof
          set h = y;
          the carrier of P c= the carrier of G by GROUP_2:def 5;
          then reconsider h as Element of G by A29;
          reconsider h'=h as Element of P by A29;
          take h;
          thus y = h;
          dom(T^h') = E by FUNCT_2:def 1;
          then Q' in dom(T^h') by A27,A28;
          then reconsider Q1=Q' as Element of E;
A30:      ex Q2 be Element of E, H1,H2 be strict Subgroup of G, g be
Element of G st Q2=the_left_translation_of(h',p).Q1 & Q1=H1 & Q2=H2 & h'"= g &
          H2 = H1 |^ g by Def21;
          Q' = (T^h').Q' & T.h' = the_left_translation_of(h',p) by A24,
          A25,A27,Def12,Def22;
          then Q' |^ h" = Q' by A30,GROUP_2:57;
          then h"*h = 1_G & Q' |^ (h"*h) = Q' |^ h by GROUP_1:def 6,
          GROUP_3:72;
          hence thesis by GROUP_3:73;
        end;
        then y in N by GROUP_3:160;
        hence y in the carrier of N by STRUCT_0:def 5;
      end;
      then
A31:  the carrier of P c= the carrier of N by TARSKI:def 3;
A32:  now
        let y be set;
        assume
A33:    y in the carrier of Q';
        ex h being Element of G st y = h & Q' |^ h = Q'
        proof
          set h = y;
          the carrier of Q' c= the carrier of G by GROUP_2:def 5;
          then reconsider h as Element of G by A33;
          take h;
          thus y = h;
          for g being Element of G holds g in Q' iff g in Q' |^ h
          proof
            let g be Element of G;
            hereby
              assume
A34:          g in Q';
              ex g' being Element of G st g = g' |^ h & g' in Q'
              proof
                set g' = h * g * h";
                take g';
                thus g' |^ h = h" * g' * h by GROUP_3:def 2
                  .= h" * (h * (g * h")) * h by GROUP_1:def 4
                  .= (h" * h) * (g * h") * h by GROUP_1:def 4
                  .= 1_G * (g * h") * h by GROUP_1:def 6
                  .= g * h" * h by GROUP_1:def 5
                  .= g * (h" * h) by GROUP_1:def 4
                  .= g * 1_G by GROUP_1:def 6
                  .= g by GROUP_1:def 5;
                h in Q' by A33,STRUCT_0:def 5;
                then h" in Q' & h * g in Q' by A34,GROUP_2:59,60;
                hence thesis by GROUP_2:59;
              end;
              hence g in Q' |^ h by GROUP_3:70;
            end;
            assume
            g in Q' |^ h;
            then consider g' be Element of G such that
A35:        g = g' |^ h and
A36:        g' in Q' by GROUP_3:70;
A37:        h in Q' by A33,STRUCT_0:def 5;
            then h" in Q' by GROUP_2:60;
            then h" * g' in Q' by A36,GROUP_2:59;
            then h" * g' * h in Q' by A37,GROUP_2:59;
            hence thesis by A35,GROUP_3:def 2;
          end;
          hence thesis by GROUP_2:def 6;
        end;
        then y in N by GROUP_3:160;
        hence y in the carrier of N by STRUCT_0:def 5;
      end;
      then
A38:  the carrier of Q' c= the carrier of N by TARSKI:def 3;
      reconsider N as finite Group;
      reconsider Q''=Q' as strict Subgroup of N by A38,GROUP_2:66;
A39:  Q'' is_Sylow_p-subgroup_of_prime p by A28,Lm9;
      reconsider P''=P' as strict Subgroup of N by A31,GROUP_2:66;
      P'' is_Sylow_p-subgroup_of_prime p by A2,Lm9;
      then P'',Q'' are_conjugated by A39,Th14;
      then consider n be Element of N such that
A40:  P'' = Q'' |^ n by GROUP_3:def 11;
      the carrier of Q'' |^ n c= the carrier of N by GROUP_2:def 5;
      then
A41:  (the multF of G)||the carrier of Q'' |^ n = ((the multF of G)||the
      carrier of N)||the carrier of Q'' |^ n by RELAT_1:103,ZFMISC_1:119
        .= (the multF of N)||the carrier of Q'' |^ n by GROUP_2:def 5;
      n in Normalizator Q' by STRUCT_0:def 5;
      then consider n' be Element of G such that
A42:  n = n' and
A43:  Q' |^ n' = Q' by GROUP_3:160;
A44:  now
        let y be set;
        hereby
          assume
          y in the carrier of Q' |^ n';
          then y in carr(Q') |^ n' by GROUP_3:def 6;
          then consider q' be Element of G such that
A45:      y = q' |^ n' and
A46:      q' in carr Q' by GROUP_3:50;
          reconsider q''=q' as Element of N by A32,A46;
          n'" = n" by A42,GROUP_2:57;
          then n'" * q' = n" * q'' by GROUP_2:52;
          then
A47:      n'" * q' * n' = n" * q'' * n by A42,GROUP_2:52;
          q' |^ n' = n'" * q' * n' by GROUP_3:def 2
            .= q'' |^ n by A47,GROUP_3:def 2;
          then y in carr(Q'') |^ n by A45,A46,GROUP_3:50;
          hence y in the carrier of Q'' |^ n by GROUP_3:def 6;
        end;
        assume
        y in the carrier of Q'' |^ n;
        then y in carr(Q'') |^ n by GROUP_3:def 6;
        then consider q'' be Element of N such that
A48:    y = q'' |^ n and
A49:    q'' in carr Q'' by GROUP_3:50;
        the carrier of Q'' c= the carrier of G by GROUP_2:def 5;
        then reconsider q'=q'' as Element of G by A49;
        n'" = n" by A42,GROUP_2:57;
        then n'" * q' = n" * q'' by GROUP_2:52;
        then
A50:    n'" * q' * n' = n" * q'' * n by A42,GROUP_2:52;
        q' |^ n' = n'" * q' * n' by GROUP_3:def 2
          .= q'' |^ n by A50,GROUP_3:def 2;
        then y in carr(Q') |^ n' by A48,A49,GROUP_3:50;
        hence y in the carrier of Q' |^ n' by GROUP_3:def 6;
      end;
      then the carrier of Q' |^ n' = the carrier of Q'' |^ n by TARSKI:2;
      then the multF of Q' |^ n' = (the multF of G)||the carrier of Q'' |^ n
      by GROUP_2:def 5
        .= the multF of Q'' |^ n by A41,GROUP_2:def 5;
      hence x=P' by A23,A25,A27,A40,A43,A44,TARSKI:2;
    end;
    assume
    x=P';
    then x in {x' where x' is Element of E: x' is_fixed_under T} by A22;
    hence thesis by Def13;
  end;
  then
A51: the_fixed_points_of T = {P'} by TARSKI:def 1;
  card E mod p = card the_fixed_points_of T mod p by A3,Th11
    .= 1 mod p by A51,CARD_1:50;
  hence card the_sylow_p-subgroups_of_prime(p,G) mod p = 1 by A1,NAT_D:14;
A52: card the_orbit_of(P',T') = Index K by Th10;
  reconsider K as Subgroup of G';
  card G = card G' * index G' by GROUP_2:177
    .= card G' * 1 by GROUP_2:180;
  then
A53: card G = card K * index K by GROUP_2:177;
  ex B being finite set st B = Left_Cosets K & index K = card B by
  GROUP_2:176;
  then card E = index K by A52,A4,TARSKI:2;
  hence thesis by A53,NAT_D:def 3;
end;

begin :: Appendix

theorem
  for X,Y being non empty set holds card {[:X,{y}:] where y is Element
  of Y: not contradiction} = card Y by Lm3;

theorem
  for n,m,r being natural number, p being prime (natural number) st n =
  p |^ r * m & not p divides m holds (n choose p|^r) mod p <> 0 by Lm5;

theorem
  for n being non zero natural number holds card INT.Group(n) = n by Lm2;

theorem
  for G being Group, A being non empty Subset of G, g being Element of G
  holds card A = card (A * g) by Lm7;

