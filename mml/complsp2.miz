:: The Inner Product and Conjugate of Finite Sequences of Complex Numbers
::  by Wenpai Chang , Hiroshi Yamazaki and Yatsuka Nakamura
:: 
:: Received April 25, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FINSEQ_1, FINSEQ_2, ARYTM_1, FUNCT_1, RELAT_1, BINOP_1, FUNCOP_1,
      RVSUM_1, COMPLSP1, COMPLEX1, EUCLID_2, ARYTM_3, XCMPLX_0, VECTSP_1,
      ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, ARYTM_0, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, RELAT_1, FUNCT_1, FUNCT_2, ZFMISC_1, NAT_1, PARTFUN1, BINOP_1,
      FUNCOP_1, FINSEQ_1, FINSEQOP, COMPLSP1, COMPLEX1, RVSUM_1, FINSEQ_2,
      BINOP_2, EUCLID, EUCLID_2, ORDINAL1, MATRIX_5;
 constructors REAL_1, NAT_1, BINOP_1, SETWISEO, FINSEQOP, SEQ_2, MEMBERED,
      PARTFUN1, XBOOLE_0, BINOP_2, COMPLSP1, FINSOP_1, COMPLEX1, ARYTM_0,
      FUNCT_4, EUCLID, RELSET_1, XREAL_0, EUCLID_2, RVSUM_1, RELAT_1, FUNCOP_1,
      MATRIX_5;
 registrations RELSET_1, FINSEQ_2, NAT_1, MEMBERED, ZFMISC_1, FUNCT_1, FUNCT_2,
      XBOOLE_0, BINOP_2, COMPLSP1, COMPLEX1, XREAL_0, XCMPLX_0, EUCLID,
      NUMBERS, SETWISEO;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions TARSKI, FINSEQOP, RELAT_1;
 theorems FUNCT_1, FUNCT_2, FINSEQ_1, FUNCOP_1, FINSEQ_2, FINSEQOP, RELAT_1,
      BINOP_2, COMPLSP1, COMPLEX1, XCMPLX_0, XREAL_0, FINSEQ_3, EUCLID_2,
      RVSUM_1;
 schemes FINSEQ_1;

begin :: Preliminaries

 reserve i,j,k for Nat,
         x,y,z for FinSequence of COMPLEX,
         c for Element of COMPLEX,
         R,R1,R2 for Element of i-tuples_on COMPLEX;

definition let z be FinSequence of COMPLEX;
  func z*' -> FinSequence of COMPLEX means
:AA10: len it = len z &
  (for i being Nat st 1<=i & i<=len z holds it.i = ((z.i)*'));
 existence
  proof
    deffunc F(Nat)=(z.$1)*';
       ex p being FinSequence st len p = len z
   & for k being Nat st k in Seg len z holds p.k=F(k) from FINSEQ_1:sch 2;
   then consider p being FinSequence such that
 A1: len p = len z &
 for k st k in Seg len z holds p.k=F(k);
 A2: for i st 1<=i & i<=len z holds ((z.i)*')=p.i
  proof let i;
   assume 1<=i & i<=len z;
   then i in Seg len z by FINSEQ_1:3;
   hence (z.i)*'=p.i by A1;
  end;
  rng p c= COMPLEX
  proof let s2 be set; assume s2 in rng p;then
    consider s1 being set such that
A3: s1 in dom p & s2=p.s1 by FUNCT_1:def 5;
A4: s1 in Seg len p by A3,FINSEQ_1:def 3;
    reconsider nx=s1 as Nat by A3;
    p.nx = (z.nx)*' by A1,A4;
   hence s2 in COMPLEX by A3;
  end;then
  p is FinSequence of COMPLEX by FINSEQ_1:def 4;
 hence thesis by A1,A2;
end;
uniqueness
 proof
  thus for p1,p2 being FinSequence of COMPLEX st
  (len p1=len z & for i st 1<=i & i<=len z holds
  ((z.i)*')=p1.i)&
  (len p2=len z & for i st 1<=i & i<=len z holds
  ((z.i)*')=p2.i) holds p1=p2
  proof let p1,p2 be FinSequence of COMPLEX;
   assume A8: (len p1=len z & for i st 1<=i & i<=len z holds (z.i)*'=p1.i)&
      (len p2=len z & for i st 1<=i & i<=len z holds (z.i)*'=p2.i);
      for i st 1<=i & i<=len p1 holds p1.i=p2.i
      proof let i;
       assume A9: 1<=i & i<=len p1;then
        (z.i)*'=p1.i by A8;
       hence p1.i=p2.i by A8,A9;
      end;
    hence p1=p2 by A8,FINSEQ_1:18;
  end;
 end;
end;

Lm2: c*x = (multcomplex[;](c,id COMPLEX))*x
 proof c multcomplex = multcomplex[;](c,id COMPLEX) by COMPLSP1:def 5;
  hence thesis by COMPLSP1:def 10;
 end;

theorem Th4: 
  i in dom (x+y) implies (x+y).i = x.i + y.i
 proof assume that
  A1: i in dom (x+y);
  set r1 = x.i, r2 = y.i;
     x + y = addcomplex.:(x,y) & i in dom(x+y) by A1,COMPLSP1:def 7;
  then (x + y).i = addcomplex.(r1,r2) by FUNCOP_1:28;
  hence thesis by BINOP_2:def 3;
 end;

theorem Th5: 
  i in dom (x-y) implies (x-y).i = x.i - y.i
 proof assume that
  A1: i in dom (x-y);
  set r1 = x.i, r2 = y.i;
  x - y = diffcomplex.:(x,y) & i in dom(x-y) by A1,COMPLSP1:def 8;
  then (x - y).i = diffcomplex.(r1,r2) by FUNCOP_1:28;
  hence thesis by BINOP_2:def 4;
 end;

definition let i,R1,R2;
  redefine func R1 - R2 -> Element of i-tuples_on COMPLEX;
  coherence
  proof R1 - R2 = diffcomplex.:(R1,R2) by COMPLSP1:def 8;
    hence thesis by FINSEQ_2:140;
  end;
end;

definition let i,R1,R2;
  redefine func R1 + R2 -> Element of i-tuples_on COMPLEX;
  coherence
  proof R1 + R2 = addcomplex.:(R1,R2) by COMPLSP1:def 7;
   hence thesis by FINSEQ_2:140; end;
end;

definition let i; let r be complex number; let R;
  redefine func r*R -> Element of i-tuples_on COMPLEX;
  coherence
  proof
    reconsider q = r as Element of COMPLEX by XCMPLX_0:def 2;
    q*R = (multcomplex[;](q,id COMPLEX))*R by Lm2;
    hence thesis by FINSEQ_2:133;
  end;
end;

theorem Th14:
  for a being complex number, x being FinSequence of COMPLEX holds
    len (a*x)=len x
proof let a be complex number, x be FinSequence of COMPLEX;
  set n=len x;
  reconsider z=x as Element of n-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider a' = a as Element of COMPLEX by XCMPLX_0:def 2;
  len (a'*z)=n by FINSEQ_2:109;
  hence thesis;
end;

theorem Th15: 
  for x being FinSequence of COMPLEX holds 
    dom x = dom -x
proof let x be FinSequence of COMPLEX;
  dom compcomplex = COMPLEX by FUNCT_2:def 1;
  then rng x c= dom compcomplex by FINSEQ_1:def 4;
  hence dom x = dom(compcomplex*x) by RELAT_1:46
             .= dom -x by COMPLSP1:def 9;
end;

theorem Th16: 
  for x being FinSequence of COMPLEX holds 
    len (-x)=len x
proof let x be FinSequence of COMPLEX;
  dom (-x)=dom x by Th15;
  then Seg len(-x)=dom x by FINSEQ_1:def 3;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th17:
  for x1,x2 being FinSequence of COMPLEX st len x1=len x2 holds
    len (x1+x2)=len x1
proof let x1,x2 be FinSequence of COMPLEX;
  assume A1: len x1=len x2;
  set n=len x1;
  reconsider z1=x1 as Element of (len x1)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider z2=x2 as Element of n-tuples_on COMPLEX by A1,FINSEQ_2:110;
  dom (z1+z2)=Seg n by FINSEQ_2:144;
 hence thesis by FINSEQ_1:def 3;
end;

theorem Th18:
  for x1,x2 being FinSequence of COMPLEX st len x1=len x2 holds
    len (x1-x2)=len x1
proof let x1,x2 be FinSequence of COMPLEX;
  assume A1: len x1=len x2;
  set n=len x1;
  reconsider z1=x1 as Element of (len x1)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider z2=x2 as Element of n-tuples_on COMPLEX by A1,FINSEQ_2:110;
  dom (z1-z2)=Seg n by FINSEQ_2:144;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th19:
  for f being FinSequence of COMPLEX holds 
    f is Element of COMPLEX (len f)
proof let f be FinSequence of COMPLEX;
  f is Element of COMPLEX* by FINSEQ_1:def 11;
  then f in { s where s is Element of COMPLEX*: len s = len f };
  then f in ((len f)-tuples_on COMPLEX) by FINSEQ_2:def 4;
  hence thesis by COMPLSP1:def 12;
end;

theorem Th20:
  R1 - R2 = R1 + - R2
proof
  thus R1 - R2 
   = diffcomplex.:(R1,R2) by COMPLSP1:def 8 
   .= addcomplex.:(R1,compcomplex*R2) by COMPLSP1:def 3,FINSEQOP:89 
   .= addcomplex.:(R1,-R2) by COMPLSP1:def 9
   .= R1 + -R2 by COMPLSP1:def 7;
 end;

theorem Th21:
  for x,y being FinSequence of COMPLEX st len x=len y holds 
    x-y=x+(-y)
proof let x,y be FinSequence of COMPLEX;
  assume len x=len y;
  then reconsider z = x, z2 = y as Element of COMPLEX len x by Th19;
  set n=len x;
  reconsider w = z, u = z2 as Element of n-tuples_on COMPLEX
    by COMPLSP1:def 12;
  w-u=w+(-u) by Th20;
  hence thesis;
end;

theorem Th22:
  (-1)*R = -R
 proof
   reconsider r = 1 as Element of COMPLEX by XCMPLX_0:def 2;
A1:compcomplex.r = -r by BINOP_2:def 1;
   thus (-1)*R = multcomplex[;](-r,id COMPLEX)*R by Lm2
     .= compcomplex*R by A1,COMPLSP1:8,COMPLSP1:7,
     COMPLSP1:15,BINOP_2:6,FINSEQOP:72
     .= -R by COMPLSP1:def 9;
 end;

theorem Th23:
  for x being FinSequence of COMPLEX holds 
    (-1)*x = -x
proof let x be FinSequence of COMPLEX;
  reconsider z = x as Element of COMPLEX len x by Th19;
  reconsider w = z as Element of (len x)-tuples_on COMPLEX by COMPLSP1:def 12;
  -w=(-1)*w by Th22;
  hence thesis;
end;

theorem Th24:
  for x being FinSequence of COMPLEX holds 
    (-x).i = -x.i
proof let x be FinSequence of COMPLEX;
  per cases;
  suppose
A1:  not i in dom -x;
   then A2:   not i in dom x by Th15;
   thus (-x).i = -0 by A1,FUNCT_1:def 4
      .= -x.i by A2,FUNCT_1:def 4;
  end;
  suppose
A3:  i in dom -x;
  set r = x.i;
  -x = compcomplex*x by COMPLSP1:def 9;
  then (-x).i = compcomplex.r by A3,FUNCT_1:22;
  hence thesis by BINOP_2:def 1;
 end;
end;

definition let i,R;
  redefine func -R -> Element of i-tuples_on COMPLEX;
  coherence
  proof -R = compcomplex*R by COMPLSP1:def 9;
    hence thesis by FINSEQ_2:133; end;
end;

theorem
  c = R.j implies (-R).j = -c by Th24;

theorem Th26:
  for a being complex number holds dom(a*x) = dom x
  proof
    let a be complex number;
    dom(a multcomplex) = COMPLEX by FUNCT_2:def 1;
    then rng x c= dom(a multcomplex) by FINSEQ_1:def 4;
   hence dom x = dom((a multcomplex)*x) by RELAT_1:46
              .= dom(a*x) by COMPLSP1:def 10;
  end;

theorem Th27:
  for a being complex number holds (a*x).i = a*(x.i)
proof
  let a be complex number;
  reconsider aa = a as Element of COMPLEX by XCMPLX_0:def 2;
  per cases;
  suppose
A1: not i in dom (a*x);
   then A2:  not i in dom x by Th26;
   thus (a*x).i = a*0 by A1,FUNCT_1:def 4
      .= a*(x.i) by A2,FUNCT_1:def 4;
  end;
  suppose
A3:  i in dom (a*x);
  set a' = x.i;
A4:  i in dom(a*x) & a*x = (multcomplex[;](aa,id COMPLEX))*x by A3,Lm2;
  then A5:  a' in dom(multcomplex[;](aa,id COMPLEX)) by FUNCT_1:21;
  thus (a*x).i = (multcomplex[;](aa,id COMPLEX)).a' by A4,FUNCT_1:22
              .= multcomplex.(a,(id COMPLEX).a') by A5,FUNCOP_1:42
              .= multcomplex.(aa,a') by FUNCT_1:35
              .= a*(x.i) by BINOP_2:def 5;
 end;
end;

theorem Th28: 
  for a being complex number holds (a*x)*' = a*'*(x*')
proof
  let a be complex number;
  reconsider aa = a as Element of COMPLEX by XCMPLX_0:def 2;
A1: len (a*x) = len x by Th14;
A2: len ((a*x)*') = len (a*x) by AA10;
    len (a*'*(x*')) = len (x*') by Th14;then
    len (a*'*(x*')) = len x by AA10;then
A3: len ((a*x)*')=len (a*'*(x*')) by Th14,A2;
  now let i; assume
  1 <= i & i <= len ((a*x)*');then
A4: 1 <= i & i <= len (a*x) by AA10;
  hence (a*x)*'.i = (((a*x).i)*') by AA10
         .= (aa*(x.i))*' by Th27
         .= aa*'*(x.i)*' by COMPLEX1:121
         .= a*'*((x*').i) by AA10,A4,A1
         .= (a*'*(x*')).i by Th27; 
  end;
  hence thesis by FINSEQ_1:18,A3; 
end;

theorem Th30:
  (R1+R2).j = R1.j + R2.j
proof per cases;
  suppose
A1:  not j in Seg i;
   then A2:  not j in dom R1 by FINSEQ_2:144;
A3:  not j in dom R2 by A1,FINSEQ_2:144;
     not j in dom(R1+R2) by A1,FINSEQ_2:144;
   hence (R1+R2).j = 0+0 by FUNCT_1:def 4
       .= R1.j + 0 by A2,FUNCT_1:def 4
       .= R1.j + R2.j by A3,FUNCT_1:def 4;
  end;
  suppose j in Seg i;
   then j in dom (R1 + R2) by FINSEQ_2:144;
   hence thesis by Th4;
  end;
end;

theorem Th31:
  for x1,x2 being FinSequence of COMPLEX st len x1=len x2 holds 
    (x1 + x2)*' = x1*' + x2*'
proof let x1,x2 be FinSequence of COMPLEX;
  assume A1: len x1=len x2;then
  A2: len (x1 + x2) = len x1 by Th17;
  reconsider x'=x1 as Element of (len x1)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y'=x2 as Element of (len x2)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider x3=x1*' as Element of (len (x1*'))-tuples_on COMPLEX
    by FINSEQ_2:110;
  reconsider x4=x2*' as Element of (len (x2*'))-tuples_on COMPLEX
    by FINSEQ_2:110;
  A3: len x1=len (x1*') by AA10;
  A4: len x2=len (x2*') by AA10;then
  len (x1*' + x2*') = len x1 by A3,Th17,A1;then
  A5: len ((x1 + x2)*')=len (x1*' + x2*') by A2,AA10;
  now let i; assume 1 <= i & i <= len ((x1 + x2)*');then
  A6: 1 <= i & i <= len (x1 + x2) by AA10;
  hence (x1 + x2)*'.i = (((x1 + x2).i)*') by AA10 
          .= (x'.i + y'.i)*' by Th30,A1
          .= (x1.i)*'+(x2.i)*' by COMPLEX1:118
          .= ((x1*').i)+(x2.i)*' by AA10,A6,A2
          .= ((x1*').i)+((x2*').i) by AA10,A6,A2,A1
          .= (x3+x4).i by Th30,A1,A3,A4;
  end;
  hence thesis by FINSEQ_1:18,A5; 
end;

theorem Th32:
  (R1-R2).j = R1.j - R2.j
 proof per cases;
  suppose
A1:  not j in Seg i;
   then A2:  not j in dom R1 by FINSEQ_2:144;
A3:  not j in dom R2 by A1,FINSEQ_2:144;
     not j in dom(R1-R2) by A1,FINSEQ_2:144;
   hence (R1-R2).j = 0-0 by FUNCT_1:def 4
       .= R1.j - 0 by A2,FUNCT_1:def 4
       .= R1.j - R2.j by A3,FUNCT_1:def 4;
  end;
  suppose j in Seg i;
   then j in dom (R1 - R2) by FINSEQ_2:144;
  hence thesis by Th5;
 end;
 end;

theorem Th34:
  for x1,x2 being FinSequence of COMPLEX st len x1=len x2 holds 
    (x1 - x2)*' = x1*' - x2*'
proof let x1,x2 be FinSequence of COMPLEX;
   assume A1: len x1=len x2;then
A2:len (x1 - x2) = len x1 by Th18;
   reconsider x'=x1 as Element of (len x1)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider y'=x2 as Element of (len x2)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider x3=x1*' as Element of (len (x1*'))-tuples_on COMPLEX
     by FINSEQ_2:110;
   reconsider x4=x2*' as Element of (len (x2*'))-tuples_on COMPLEX
     by FINSEQ_2:110;
  A3: len x1=len (x1*') by AA10;
  A4: len x2=len (x2*') by AA10;then
      len (x1*' - x2*') = len x1 by A3,Th18,A1;then
  A5: len ((x1 - x2)*')=len (x1*' - x2*') by A2,AA10;
  now let i; assume 1 <= i & i <= len ((x1 - x2)*');then
  A6: 1 <= i & i <= len (x1 - x2) by AA10;
  hence (x1 - x2)*'.i = (((x1 - x2).i)*') by AA10 
          .= (x'.i - y'.i)*' by Th32,A1
          .= (x1.i)*'-(x2.i)*' by COMPLEX1:120
          .= ((x1*').i)-(x2.i)*' by AA10,A6,A2
          .= ((x1*').i)-((x2*').i) by AA10,A6,A2,A1
          .= (x3-x4).i by Th32,A1,A3,A4;
  end;
  hence thesis by FINSEQ_1:18,A5; 
end;

theorem
  for z being FinSequence of COMPLEX holds
    (z*')*' = z
proof let z be FinSequence of COMPLEX;
  A1: len ((z*')*') = len (z*') by AA10;
  A2: len (z*') = len z by AA10;
  now let i; assume 1 <= i & i <= len ((z*')*');then
  A3: 1 <= i & i <= len (z*') by AA10;
  z*'.i = (z.i)*' by AA10,A2,A3;
  hence ((z*')*').i = ((z.i)*')*' by A3,AA10
     .= z.i;
  end;
  hence thesis by FINSEQ_1:18,A2,A1; 
end;

theorem
  for z being FinSequence of COMPLEX holds
    (-z)*' = -(z*')
proof let z be FinSequence of COMPLEX;
  (-z)*'=((-1)*z)*' by Th23
       .= ((-1r)*')*(z*') by COMPLEX1:def 7,Th28
       .= (-1)*(z*') by COMPLEX1:def 7,COMPLEX1:115,119
       .= -z*' by Th23;
  hence thesis;
end;

theorem Th47:
  for z being complex number holds
    z+z*' = 2*(Re z)
proof let z be complex number;
  z is Element of COMPLEX by XCMPLX_0:def 2; then
  z+z*'=Re z + Re (z*')+(Im z + Im (z*'))*<i> by COMPLEX1:def 9
      .=Re z + Re (z*')+(Im z + -Im z)*<i> by COMPLEX1:112
      .=Re z + Re z by COMPLEX1:112
      .=2*(Re z);
  hence thesis;
end;

theorem Th48: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    (x-y).i = x.i - y.i
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A2: len (x - y) = len x by Th18,A1;
  reconsider x2=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y2=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y2'=(x2 - y2) as Element of (len (x2 - y2))-tuples_on COMPLEX
    by FINSEQ_2:110;
  per cases;
  suppose
   A3: not i in Seg (len x);then
   A4: not i in dom x2 by FINSEQ_2:144;
   A5: not i in dom y2 by A3,FINSEQ_2:144,A1;
   A6: not i in dom(y2') by A3,FINSEQ_2:144,A2;
   (x2-y2).i = 0-0 by FUNCT_1:def 4,A6
       .= x2.i - 0 by A4,FUNCT_1:def 4
       .= x2.i - y2.i by A5,FUNCT_1:def 4;
       hence thesis;
  end;
  suppose i in Seg (len x);then
   i in dom (y2') by FINSEQ_2:144,A2;
   hence thesis by Th5;
  end;
 end;

theorem Th49: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    (x+y).i = x.i + y.i
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A2: len (x + y) = len x by Th17,A1;
  reconsider x2=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y2=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y2'=(x2 + y2) as Element of (len (x2 + y2))-tuples_on COMPLEX
    by FINSEQ_2:110;
  per cases;
  suppose
   A3: not i in Seg (len x);then 
   A4: not i in dom x2 by FINSEQ_2:144;
   A5: not i in dom y2 by A3,FINSEQ_2:144,A1;
   A6: not i in dom(y2') by A3,FINSEQ_2:144,A2;
   (x2+y2).i = 0+0 by FUNCT_1:def 4,A6
       .= x2.i + 0 by A4,FUNCT_1:def 4
       .= x2.i + y2.i by A5,FUNCT_1:def 4;
   hence thesis;
  end;
  suppose i in Seg (len x);then
   i in dom (y2') by FINSEQ_2:144,A2;
   hence thesis by Th4;
  end;
 end;

definition let z be FinSequence of COMPLEX;
  func Re z -> FinSequence of REAL equals
 :AA99: 1/2*(z+z*');
 coherence
proof
A1: len z = len (z*') by AA10;
A2: len (1/2*(z+z*')) = len (z+z*') by Th14
       .= len z by Th17,A1;
  rng (1/2*(z+z*')) c= REAL
  proof let w be set;
    assume w in rng (1/2*(z+z*'));then
    consider x being set such that
A3:   x in dom (1/2*(z+z*')) & w=(1/2*(z+z*')).x by FUNCT_1:def 5;
A4:   x in Seg len (1/2*(z+z*')) by A3,FINSEQ_1:def 3;
      reconsider i=x as Nat by A3;
A5:   1<=i & i<=len z by FINSEQ_1:3,A4,A2;
      (1/2*(z+z*')).i = 1/2*((z+z*').i) by Th27
        .=1/2*((z.i)+(z*').i) by Th49,A1
        .=1/2*((z.i)+(z.i)*') by AA10,A5
        .=(1/2)*(2*(Re (z.i))) by Th47;
      hence w in REAL by A3;
    end;
   hence thesis by FINSEQ_1:def 4;
  end;
end;

theorem Th50:
  for z being complex number holds
    z-z*' = 2*(Im z) * <i>
proof let z be complex number;
  z is Element of COMPLEX by XCMPLX_0:def 2; then
  z-z*'= Re z - Re (z*') + (Im z - Im (z*'))*<i> by COMPLEX1:def 12
      .= Re z - Re (z*')+(Im z - -Im z)*<i> by COMPLEX1:112
      .= Re z - Re z + (Im z - -Im z)*<i> by COMPLEX1:112
      .= 2*(Im z)*<i>;
  hence thesis;
end;

definition let z be FinSequence of COMPLEX;
  func Im z -> FinSequence of REAL equals
 :AA99b: (-1/2*<i>)*(z-z*');
 coherence
proof
  A1: len z = len (z*') by AA10;
  A2: len ((-1/2*<i>)*(z-z*'))
      = len (z-z*') by Th14
     .= len z by Th18,A1;
  rng ((-1/2*<i>)*(z-z*')) c= REAL
  proof let w be set;
    assume w in rng ((-1/2*<i>)*(z-z*'));then
    consider x being set such that 
A3: x in dom ((-1/2*<i>)*(z-z*')) &
      w=((-1/2*<i>)*(z-z*')).x by FUNCT_1:def 5;
A4: x in Seg len ((-1/2*<i>)*(z-z*')) by A3,FINSEQ_1:def 3;
    reconsider i=x as Nat by A3;
A5: 1<=i & i<=len z by FINSEQ_1:3,A4,A2;
      ((-1/2*<i>)*(z-z*')).i
         =(-1/2*<i>)*((z-z*').i) by Th27
        .=(-1/2*<i>)*((z.i)-(z*').i) by Th48,A1
        .=(-1/2*<i>)*((z.i)-(z.i)*') by AA10,A5
        .=(-1/2*<i>)*((2*(Im (z.i))) * <i>) by Th50;
      hence w in REAL by A3;
    end;
   hence thesis by FINSEQ_1:def 4;
  end;
end;

definition let x,y be FinSequence of COMPLEX;
  func |( x,y )| -> Element of COMPLEX equals
:AA40: |(Re x,Re y)| - <i>*(|(Re x,Im y)|)
       + <i>*(|(Im x,Re y)|) + |(Im x,Im y)|;
 coherence by XCMPLX_0:def 2;
end;

theorem Th51:
  for x,y,z being FinSequence of COMPLEX st len x=len y & len y=len z holds
    x + (y + z) = x + y + z
proof let x,y,z be FinSequence of COMPLEX;
  assume A1: len x=len y & len y=len z;
   reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider y'=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
   reconsider z'=z as Element of (len z)-tuples_on COMPLEX by FINSEQ_2:110;
   x + (y + z) = addcomplex.:(x,y+z) by COMPLSP1:def 7
        .= addcomplex.:(x,addcomplex.:(y,z)) by COMPLSP1:def 7
        .= addcomplex.:(addcomplex.:(x',y'),z') by FINSEQOP:29,A1
        .= addcomplex.:(x+y,z) by COMPLSP1:def 7
        .= x + y + z by COMPLSP1:def 7;
   hence thesis;
 end;

theorem Th52: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    x + y = y + x
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y'=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
  x + y = addcomplex.:(x,y) by COMPLSP1:def 7
       .= addcomplex.:(y',x') by FINSEQOP:34,A1
       .= y + x by COMPLSP1:def 7;
  hence thesis;
 end;

theorem Th53: 
  for c being complex number,
      x,y being FinSequence of COMPLEX st len x=len y holds
    c*(x+y) = c*x + c*y
proof let c be complex number, x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y'=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
  set cM = c multcomplex;
  c is Element of COMPLEX by XCMPLX_0:def 2; then
  A2: cM is_distributive_wrt addcomplex by COMPLSP1:17;
  c*(x+y) = cM*(x + y) by COMPLSP1:def 10
         .= cM*(addcomplex.:(x,y)) by COMPLSP1:def 7
         .= addcomplex.:(cM*x',cM*y') by A1,A2,FINSEQOP:52
         .= cM*x + cM*y by COMPLSP1:def 7
         .= c*x + cM*y by COMPLSP1:def 10
         .= c*x + c*y by COMPLSP1:def 10;
  hence thesis;
 end;

theorem Th54: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    x - y = x + - y
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y'=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
  thus x - y = diffcomplex.:(x,y) by COMPLSP1:def 8
            .= addcomplex.:(x',compcomplex*y') by A1,COMPLSP1:def 3,FINSEQOP:89
            .= addcomplex.:(x,-y) by COMPLSP1:def 9
            .= x + -y by COMPLSP1:def 7;
 end;

definition let i,c;
  redefine func i |-> c -> Element of i-tuples_on COMPLEX;
  coherence by FINSEQ_2:132;
end;

theorem Th62: 
  for x,y being FinSequence of COMPLEX 
  st len x=len y & x + y = 0c (len x) holds x = -y & y = -x 
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y & x + y = 0c (len x);
  reconsider y'=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  A3: x + y = addcomplex.:(x,y) by COMPLSP1:def 7;
  A4: -x = compcomplex*x by COMPLSP1:def 9;
  A5: -y = compcomplex*y by COMPLSP1:def 9;
  A6: x + y = (len x) |-> 0c by COMPLSP1:def 13,A1;
  x' = -y' by COMPLSP1:7,COMPLSP1:8,BINOP_2:1,FINSEQOP:78,A1,A3,A5,A6;
  hence thesis by COMPLSP1:7,COMPLSP1:8,BINOP_2:1,FINSEQOP:78,A1,A3,A4,A6;
end;

theorem Th56: 
  for x being FinSequence of COMPLEX holds
  x + 0c (len x) = x
 proof let x be FinSequence of COMPLEX;
  reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  x + 0c (len x) = x+(len x) |-> 0c by COMPLSP1:def 13 
     .= addcomplex.:(x,(len x) |-> 0c) by COMPLSP1:def 7 
     .= x' by BINOP_2:1,FINSEQOP:57;
   hence thesis;
 end;

theorem Th57:
  for x being FinSequence of COMPLEX holds
  x + -x = 0c (len x)
 proof let x be FinSequence of COMPLEX;
  reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  x + -x = addcomplex.:(x,-x) by COMPLSP1:def 7 
    .= addcomplex.:(x,compcomplex*x) by COMPLSP1:def 9
    .= (len x') |-> 0c by COMPLSP1:7,COMPLSP1:8,BINOP_2:1,FINSEQOP:77
    .= 0c (len x') by COMPLSP1:def 13;
   hence thesis;
 end;

theorem Th58: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    -(x + y) = -x + -y
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
   A2: len (-y) = len y by Th16;
   A3: len (-x) = len x by Th16;
   A5: len (x + y) = len x by Th17,A1;
   A8: len (-x + -y) = len (-x) by Th17,A3,A1,A2;
   (x + y) + (-x + -y) = x + y + -x + -y by Th51,A1,A2,A3,A5
    .= y + x + -x + -y by Th52,A1 
    .= y + (x + -x) + -y by Th51,A1,A3 
    .= y + 0c (len x) + -y by Th57 
    .= y + -y by Th56,A1 
    .= 0c (len y) by Th57;
   hence thesis by Th62,A1,A3,A5,A8;
 end;

theorem Th60: 
  for x,y,z being FinSequence of COMPLEX st len x=len y & len y=len z holds
    x - y - z = x - (y + z)
proof let x,y,z be FinSequence of COMPLEX;
  assume A1: len x=len y & len y=len z;
A2: len (y+z)=len y by Th17,A1;
A3: len (-y) = len y by Th16;
A4: len (-z) = len z by Th16;
A5: len (x - y) = len x by Th18,A1;
    x - y - z = x - y + - z by Th54,A1,A5
             .= x + - y + - z by Th54,A1
             .= x + (- y + - z) by Th51,A1,A3,A4 
             .= x + -(y + z) by Th58,A1 
             .= x - (y + z) by Th54,A1,A2;
  hence thesis;
end;

theorem Th61:
  for x,y,z being FinSequence of COMPLEX st len x=len y & len y=len z holds
    x + (y - z) = x + y - z
proof let x,y,z be FinSequence of COMPLEX;
  assume A1: len x=len y & len y=len z;
   A2: len (x+y)=len x by Th17,A1;
   A3: len (-z) = len z by Th16;
    x + (y - z) = x + (y + - z) by Th54,A1
               .= x + y + - z by Th51,A1,A3
               .= x + y - z by Th54,A1,A2;
  hence thesis;
end;

theorem Th63: 
  for x being FinSequence of COMPLEX holds
    --x = x
proof let x be FinSequence of COMPLEX;
A1: len x=len (-x) by Th16;
  x + -x = 0c (len x) by Th57; 
  hence thesis by Th62,A1;
end;

theorem Th64: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    -(x - y) = -x + y
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A2: len (-y) = len y by Th16;
  -(x - y) = -(x+ -y) by Th54,A1
          .= -x +--y by Th58,A1,A2
          .= -x + y by Th63;
  hence thesis;
end;

theorem Th65: 
  for x,y,z being FinSequence of COMPLEX  
  st len x=len y & len y=len z holds
    x - (y - z) = x - y + z
proof let x,y,z be FinSequence of COMPLEX;
  assume A1: len x=len y & len y=len z;
   A2: len (-y) = len y by Th16;
   A3: len (y - z) = len y by Th18,A1;
   x - (y - z) = x + - (y - z) by Th54,A1,A3
              .= x + (- y + z) by Th64,A1 
              .= x + - y + z by Th51,A1,A2 
              .= x - y + z by Th54,A1;
  hence thesis;
end;

theorem Th66:
  for c being complex number holds
    c*(0c (len x)) = 0c (len x)
proof
  let c be complex number;
  reconsider cc = c as Element of COMPLEX by XCMPLX_0:def 2;
  reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
A1:  rng (0c (len x)) c= COMPLEX by FINSEQ_1:def 4;
   c*(0c (len x)) = multcomplex[;](cc,id COMPLEX)*(0c (len x)) by Lm2
          .= multcomplex[;](cc,(id COMPLEX)*(0c (len x))) by FUNCOP_1:44
          .= multcomplex[;](cc,(0c (len x))) by A1,RELAT_1:79
          .= multcomplex[;](cc,(len x |-> 0c)) by COMPLSP1:def 13
          .= (len x) |->(multcomplex.(cc,0c)) by FINSEQOP:19
          .= (len x) |->(cc*0c) by BINOP_2:def 5
          .= (0c (len x)) by COMPLSP1:def 13,BINOP_2:1;
   hence thesis;
end;

theorem Th67:
  for c being complex number holds
    -c*x = c*(-x)
 proof
   let c be complex number;
   A1: len x=len (-x) by Th16;
   A2: len (c*(-x))=len (-x) by Th14;
   A3: len (c*x)=len x by Th14;
   c*(-x) + c*x = c*(-x+x) by Th53,A1
               .= c*(x+-x) by Th52,A1
               .= c*(0c (len x)) by Th57
               .= 0c (len x) by Th66;
   hence thesis by Th62,A1,A2,A3;
 end;

theorem Th68:
  for c being complex number,
      x,y being FinSequence of COMPLEX st len x=len y holds
    c*(x-y) = c*x - c*y
proof let c be complex number, x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A2: len (c*y)=len y by Th14;
  A3: len (c*x)=len x by Th14;
  reconsider x'=x as Element of (len x)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider y'=y as Element of (len y)-tuples_on COMPLEX by FINSEQ_2:110;
  reconsider cc = c as Element of COMPLEX by XCMPLX_0:def 2;
  set cM = cc multcomplex;
  A4: cM is_distributive_wrt addcomplex by COMPLSP1:17;
  c*(x-y) = c*(x+(-y)) by Th54,A1
    .= cM*(x +(- y)) by COMPLSP1:def 10
    .= cM*(addcomplex.:(x,(-y))) by COMPLSP1:def 7
    .= addcomplex.:(cM*x',cM*(-y')) by A1,A4,FINSEQOP:52
    .= cM*x + cM*(-y) by COMPLSP1:def 7
    .= c*x + cM*(-y) by COMPLSP1:def 10
    .= c*x + c*(-y) by COMPLSP1:def 10
    .= c*x +- c*y by Th67
    .= c*x - c*y by Th54,A1,A2,A3;
  hence thesis;
end;

theorem
  for x1,y1 being Element of COMPLEX
  for x2,y2 being Real st x1 = x2 & y1 = y2 holds
    addcomplex.(x1,y1) = addreal.(x2,y2)
proof
  let x1,y1 be Element of COMPLEX;
  let x2,y2 be Real;
  assume A1: x1 = x2 & y1 = y2;
  addcomplex.(x1,y1)= x1+y1 by BINOP_2:def 3;
  hence thesis by A1,BINOP_2:def 9;
end;

 reserve C for Function of [:COMPLEX,COMPLEX:],COMPLEX;
 reserve G for Function of [:REAL,REAL:],REAL;

theorem Th70:
  for x1,y1 being FinSequence of COMPLEX
  for x2,y2 being FinSequence of REAL 
  st x1 = x2 & y1 = y2 & len x1=len y2 &
  (for i st i in dom x1 holds C.(x1.i,y1.i)=G.(x2.i,y2.i))
    holds C.:(x1,y1)=G.:(x2,y2)
proof
  let x1,y1 be FinSequence of COMPLEX;
  let x2,y2 be FinSequence of REAL;
  assume A1: x1 = x2 & y1 = y2 & len x1=len y2 &
    (for i st i in dom x1 holds C.(x1.i,y1.i)=G.(x2.i,y2.i));
  A2: len (C.:(x1,y1)) = len x1 by FINSEQ_2:86,A1;
  A3: len (G.:(x2,y2)) = len x1 by FINSEQ_2:86,A1;
  now let i; assume
    A4: 1 <= i & i <= len (C.:(x1,y1));then
    A5: 1 <= i & i <= len x1 by FINSEQ_2:86,A1;then
    A6: i in dom x1 by FINSEQ_3:27;
    A7: i in dom (G.:(x2,y2)) by FINSEQ_3:27,A5,A3;
    i in dom (C.:(x1,y1)) by FINSEQ_3:27,A4;
    hence (C.:(x1,y1)).i = C.(x1.i,y1.i) by FUNCOP_1:28
           .= G.(x2.i,y2.i) by A1,A6
           .= (G.:(x2,y2)).i by FUNCOP_1:28,A7;
  end;
  hence thesis by FINSEQ_1:18,A2,A3;
end;

Lm1:
  0 = 0c;

definition let z be FinSequence of REAL, i be set;
  redefine func z.i -> Element of REAL;
  coherence
  proof
    per cases;
    suppose i in dom z; hence thesis by FINSEQ_2:13;
    end;
    suppose not i in dom z;
      hence z.i is Element of REAL by Lm1,FUNCT_1:def 4;
    end;
  end;
end;

theorem Th71: 
  for x1,y1 being FinSequence of COMPLEX
  for x2,y2 being FinSequence of REAL 
  st x1 = x2 & y1 = y2 & len x1=len y2 holds
    addcomplex.:(x1,y1) = addreal.:(x2,y2) 
proof
  let x1,y1 be FinSequence of COMPLEX;
  let x2,y2 be FinSequence of REAL;
  assume A1: x1 = x2 & y1 = y2 & len x1=len y2;
  for i st i in dom x1 holds
    addcomplex.(x1.i,y1.i)=addreal.(x2.i,y2.i)
  proof 
    let i;
    x1.i + y1.i = addcomplex.(x1.i,y1.i) by BINOP_2:def 3;
    hence thesis by A1,BINOP_2:def 9;
  end;
  hence thesis by A1,Th70;
end;

theorem Th72: 
  for x1,y1 being FinSequence of COMPLEX
  for x2,y2 being FinSequence of REAL 
  st x1 = x2 & y1 = y2 & len x1=len y2 holds
    x1 + y1 = x2 + y2
proof
  let x1,y1 be FinSequence of COMPLEX;
  let x2,y2 be FinSequence of REAL;
  assume A1: x1 = x2 & y1 = y2 & len x1=len y2;
  x1 + y1 = addcomplex.:(x1,y1) by COMPLSP1:def 7
         .= addreal.:(x2,y2) by A1,Th71
         .= x2 + y2 by RVSUM_1:def 4;
  hence thesis;
end;

theorem Th73A: 
  for x being FinSequence of COMPLEX holds 
    len (Re x)=len x & len (Im x)=len x 
proof let x be FinSequence of COMPLEX;
A1: len x=len (x*') by AA10;
A2: len (Re x) =len (1/2*(x+x*')) by AA99
       .=len (x+x*') by Th14
       .=len x by Th17,A1;
    len (Im x) =len ((-1/2*<i>)*(x-x*')) by AA99b
       .=len (x-x*') by Th14
       .=len x by Th18,A1;
    hence thesis by A2;
end;

theorem Th73:  
  for x,y being FinSequence of COMPLEX st len x=len y holds 
    Re (x+y) = Re x + Re y & Im(x + y) = Im x + Im y
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A2: len y=len (y*') by AA10;
  A3: len x=len (x*') by AA10;
  A4: len (x+y)=len x by Th17,A1;
  A5: len (x+(x*'))=len x by Th17,A3;
  A6: len (y+(y*'))=len y by Th17,A2;
  A7: len (-x*')=len (x*') by Th16;
  A8: len (x-(x*'))=len x by Th18,A3;
  A9: len (y-(y*'))=len y by Th18,A2;
  A12: len (Re x) = len x by Th73A;
  A13: len (Re y) = len y by Th73A;
  A14: len (Im x) = len x by Th73A;
  A15: len (Im y) = len y by Th73A;
  A16: 1/2*(x+x*')=Re x by AA99;
  A17: 1/2*(y+y*')=Re y by AA99;
  thus Re (x+y)=(1/2*((x+y)+(x+y)*')) by AA99
         .=1/2*((x+y)+(x*'+y*')) by A1,Th31
         .=1/2*((x+y+x*')+y*') by Th51,A1,A2,A3,A4
         .=1/2*((x+(y+x*'))+y*') by Th51,A1,A3
         .=1/2*((x+(x*'+y))+y*') by Th52,A1,A3
         .=1/2*((x+x*'+y)+y*') by Th51,A1,A3
         .=1/2*((x+x*')+(y+y*')) by Th51,A1,A2,A5
         .=1/2*(x+x*')+(1/2)*(y+y*') by Th53,A1,A5,A6
         .= Re x + Re y by Th72,A16,A17,A12,A13,A1;
  A18: (-1/2*<i>)*(x-x*')=Im x by AA99b;
  A19: (-1/2*<i>)*(y-y*')=Im y by AA99b;
  thus Im (x+y)=((-1/2*<i>)*((x+y)-(x+y)*')) by AA99b
         .=(-1/2*<i>)*((x+y)-(x*'+y*')) by A1,Th31 
         .=(-1/2*<i>)*((x+y)-x*'-y*') by Th60,A1,A2,A3,A4
         .=(-1/2*<i>)*((x+y)+-x*'-y*') by Th54,A3,A4
         .=(-1/2*<i>)*(x+(y+-x*')-y*') by Th51,A1,A3,A7
         .=(-1/2*<i>)*(x+(-x*'+y)-y*') by Th52,A1,A3,A7
         .=(-1/2*<i>)*(x+-x*'+y-y*') by Th51,A1,A3,A7
         .=(-1/2*<i>)*((x-x*')+y-y*') by Th54,A3
         .=(-1/2*<i>)*((x-x*')+(y-y*')) by Th61,A1,A2,A8
         .=(-1/2*<i>)*(x-x*')+(-1/2*<i>)*(y-y*') by Th53,A1,A8,A9
         .=Im x + Im y by Th72,A18,A19,A14,A15,A1;
end;

theorem Th74: 
  for x1,y1 being FinSequence of COMPLEX
  for x2,y2 being FinSequence of REAL st
    x1 = x2 & y1 = y2 & len x1=len y2 holds
      diffcomplex.:(x1,y1) = diffreal.:(x2,y2)
proof
  let x1,y1 be FinSequence of COMPLEX,
      x2,y2 be FinSequence of REAL;
  assume A1: x1 = x2 & y1 = y2 & len x1=len y2;
  for i st i in dom x1 holds 
    diffcomplex.(x1.i,y1.i)=diffreal.(x2.i,y2.i)
  proof 
    let i;
    x1.i - y1.i = diffcomplex.(x1.i,y1.i) by BINOP_2:def 4;
    hence thesis by A1,BINOP_2:def 10;
  end;
  hence thesis by A1,Th70;
end;

theorem Th75: 
  for x1,y1 being FinSequence of COMPLEX
  for x2,y2 being FinSequence of REAL 
  st x1 = x2 & y1 = y2 & len x1=len y2 holds
    x1 - y1 = x2 - y2
proof
  let x1,y1 be FinSequence of COMPLEX;
  let x2,y2 be FinSequence of REAL;
  assume A1: x1 = x2 & y1 = y2 & len x1=len y2;
  x1 - y1 = diffcomplex.:(x1,y1) by COMPLSP1:def 8
         .= diffreal.:(x2,y2) by A1,Th74
         .= x2 - y2 by RVSUM_1:def 6;
  hence thesis;
end;

theorem Th76:
  for x,y being FinSequence of COMPLEX 
  st len x=len y holds 
    Re(x - y) = Re x - Re y & Im(x - y) = Im x - Im y
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A2: len y=len (y*') by AA10;
  A3: len x=len (x*') by AA10;
  A5: len (x+(x*'))=len x by Th17,A3;
  A6: len (y+(y*'))=len y by Th17,A2;
  A7: len (x-y)=len x by Th18,A1;
  A8: len (x-(x*'))=len x by Th18,A3;
  A9: len (y-(y*'))=len y by Th18,A2;
  A12: len (Re x) = len x by Th73A;
  A13: len (Re y) = len y by Th73A;
  A14: len (Im x) = len x by Th73A;
  A15: len (Im y) = len y by Th73A;
  A16: 1/2*(x+x*')=Re x by AA99;
  A17: 1/2*(y+y*')=Re y by AA99;
  thus Re (x-y)=(1/2*((x-y)+(x-y)*')) by AA99
       .=(1/2)*((x-y)+(x*'-y*')) by A1,Th34
       .=(1/2)*((x-y)+x*'-y*') by Th61,A1,A2,A3,A7
       .=(1/2)*(x*'+(x-y)-y*') by Th52,A3,A7
       .=(1/2)*(x*'+((x-y)-y*')) by Th61,A1,A2,A3,A7
       .=(1/2)*(x*'+(x-(y+y*'))) by Th60,A1,A2
       .=(1/2)*(x*'+x-(y+y*')) by Th61,A1,A3,A6
       .=(1/2)*(x+x*'-(y+y*')) by Th52,A3
       .=(1/2)*(x+x*')-1/2*(y+y*') by Th68,A1,A5,A6
       .= Re x - Re y by Th75,A16,A17,A12,A13,A1;
  A18: (-1/2*<i>)*(x-x*')=Im x by AA99b;
  A19: (-1/2*<i>)*(y-y*')=Im y by AA99b;
  thus Im (x-y)=((-1/2*<i>)*((x-y)-(x-y)*')) by AA99b
       .=(-1/2*<i>)*((x-y)-(x*'-y*')) by A1,Th34
       .=(-1/2*<i>)*((x-y)-x*'+y*') by Th65,A1,A2,A3,A7
       .=(-1/2*<i>)*(x-(y+x*')+y*') by Th60,A1,A3
       .=(-1/2*<i>)*(x-(x*'+y)+y*') by Th52,A1,A3
       .=(-1/2*<i>)*(x-x*'-y+y*') by Th60,A1,A3
       .=(-1/2*<i>)*((x-x*')-(y-y*')) by Th65,A1,A2,A8
       .=(-1/2*<i>)*(x-x*')-(-1/2*<i>)*(y-y*') by Th68,A1,A8,A9
       .=Im x - Im y by Th75,A18,A19,A14,A15,A1;
end;

theorem Th80:
  for a, b being complex number holds
    a*(b*z) = (a*b)*z
 proof
  let a, b be complex number;
  reconsider aa = a, bb = b as Element of COMPLEX by XCMPLX_0:def 2;
  thus (a*b)*z = (multcomplex[;](aa*bb,id COMPLEX))*z by Lm2
     .= multcomplex[;](multcomplex.(aa,bb),id COMPLEX)*z by BINOP_2:def 5
     .= multcomplex[;](aa,multcomplex[;](bb,id COMPLEX))*z by FUNCOP_1:77
     .= (multcomplex[;](aa,id COMPLEX)*multcomplex[;](bb,id COMPLEX))*z
         by FUNCOP_1:69
     .= (multcomplex[;](aa,id COMPLEX))*(multcomplex[;](bb,id COMPLEX)*z)
         by RELAT_1:55
     .= (multcomplex[;](aa,id COMPLEX))*(b*z) by Lm2
     .= a*(b*z) by Lm2;
 end;

Lm3: -(0c (len x)) = 0c (len x)
 proof
    -(0c (len x)) = -((len x) |-> 0c) by COMPLSP1:def 13
    .= compcomplex*((len x) |-> 0c) by COMPLSP1:def 9
    .= len x |->(compcomplex.0c) by FINSEQOP:17
    .= (len x) |-> -0c by BINOP_2:def 1
    .= (0c (len x)) by COMPLSP1:def 13,COMPLEX1:def 6;
  hence thesis;
 end;

theorem Th81:
  for c being complex number holds
    (-c)*x = -(c*x)
 proof
   let c be complex number;
   A2: len ((-c)*x)=len x by Th14;
   A3: len (c*x)=len x by Th14;
   (-c)*x + c*x = (-1)*c*x + c*x
     .= (-1)*(c*x)+ c*x by Th80
     .= -(c*x)+ c*x by Th23
     .= -(c*x - c*x) by Th64,A3
     .= -(c*x + - c*x) by Th54,A3
     .= -(0c (len x)) by A3,Th57
     .= 0c (len x) by Lm3;
   hence thesis by Th62,A2,A3;  
 end;

 reserve h for Function of COMPLEX,COMPLEX,
         g for Function of REAL,REAL;

theorem Th82: 
  for y1 being FinSequence of COMPLEX
  for y2 being FinSequence of REAL 
  st len y1=len y2 & (for i st i in dom y1 holds h.(y1.i)=g.(y2.i)) holds
    h*y1 = g*y2
proof
  let y1 be FinSequence of COMPLEX;
  let y2 be FinSequence of REAL;
  assume A1: len y1=len y2 & (for i st i in dom y1 holds h.(y1.i)=g.(y2.i));
  A2: len (h*y1) = len y1 by FINSEQ_2:37;
  A3: len (g*y2) = len y1 by FINSEQ_2:37,A1;
  now let i; assume
    A4: 1 <= i & i <= len (h*y1);then 
    A5: 1 <= i & i <= len y1 by FINSEQ_2:37;
    A6: i in dom y1 by FINSEQ_3:27,A5;
    A7: i in dom (g*y2) by FINSEQ_3:27,A5,A3;
    i in dom (h*y1) by FINSEQ_3:27,A4;
    hence (h*y1).i = h.(y1.i) by FUNCT_1:22
            .=  g.(y2.i) by A1,A6
            .= (g*y2).i by FUNCT_1:22,A7;
  end;
  hence thesis by FINSEQ_1:18,A2,A3; 
end;

theorem Th83: 
  for y1 being FinSequence of COMPLEX
  for y2 being FinSequence of REAL 
  st y1 = y2 & len y1=len y2 holds
    compcomplex*y1 = compreal*y2 
proof
  let y1 be FinSequence of COMPLEX;
  let y2 be FinSequence of REAL;
  assume A1: y1 = y2 & len y1=len y2;
  for i st i in dom y1 holds compcomplex.(y1.i)=compreal.(y2.i)
  proof 
    let i;
    assume i in dom y1;
    - y1.i = compcomplex.(y1.i) by BINOP_2:def 1;
    hence thesis by A1,BINOP_2:def 7;
  end;
  hence thesis by A1,Th82;
end;

theorem Th84: 
  for y1 being FinSequence of COMPLEX
  for y2 being FinSequence of REAL 
    st y1 = y2 & len y1=len y2 holds
    - y1 = - y2
proof
  let y1 be FinSequence of COMPLEX;
  let y2 be FinSequence of REAL;
  assume A1: y1 = y2 & len y1=len y2;
  - y1 = compcomplex*y1 by COMPLSP1:def 9
      .= compreal*y2 by A1,Th83
      .= - y2 by RVSUM_1:def 5;
  hence thesis;
end;

theorem Th85: 
  for x being FinSequence of COMPLEX holds
    Re (<i>*x) = -(Im x) & Im (<i>*x) = Re x 
proof let x be FinSequence of COMPLEX;
  A1: len (x*') = len x by AA10;
  A2: len (<i>*x) = len x by Th14;
  A3: len (<i>*(x*')) = len (x*') by Th14;
  A4: len (-(<i>*(x*'))) = len (<i>*(x*')) by Th16;
  A6: len (Im x) = len x by Th73A;
  A7: (((-1/2)*<i>))*(x-x*')=Im x by AA99b;
  A8: Re (<i>*x) = 1/2*(<i>*x+(<i>*x)*') by AA99
   .=(1/2)*(<i>*x+((-<i>)*(x*'))) by COMPLEX1:116,Th28
   .=(1/2)*(<i>*x+((-<i>*x*'))) by Th81
   .=(1/2)*(<i>*x-(<i>*((x*')))) by Th54,A1,A2,A3
   .=(1/2)*(<i>*(x-(x*'))) by Th68,A1
   .=(1/2*<i>)*(x-x*') by Th80
   .=((-1)*((-1/2)*<i>))*(x-x*')
   .=(-1)*(((-1/2)*<i>)*(x-x*')) by Th80
   .=-(((-1/2)*<i>)*(x-x*')) by Th23
   .=-(Im x) by Th84,A7,A6;
  Im (<i>*x) = (((-1/2)*<i>))*(<i>*x-(<i>*x)*') by AA99b
  .=(((-1/2)*<i>))*(<i>*x-((-<i>)*(x*'))) by COMPLEX1:116,Th28
  .=(((-1/2)*<i>))*(<i>*x-(-<i>*x*')) by Th81
  .=(((-1/2)*<i>))*(<i>*x+-(-(<i>*x*'))) by Th54,A1,A2,A3,A4
  .=(((-1/2)*<i>))*(<i>*x+(<i>*x*')) by Th63
  .=(((-1/2)*<i>))*(<i>*(x+x*')) by Th53,A1
  .=(((-1/2)*<i>)*<i>)*(x+x*') by Th80
  .=Re x by AA99;
  hence thesis by A8;
end;

theorem Th86: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    |(<i>*x,y)| = <i>*(|(x,y)|)
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A4: len (Re y) = len y by Th73A;
  A5: len (Im x) = len x by Th73A;
  A6: len (Im y) = len y by Th73A;
  |(<i>*x,y)| 
   =|(Re (<i>*x),Re y)| - <i>*(|(Re (<i>*x),Im y)|)
    + <i>*(|(Im (<i>*x),Re y)|) + |(Im (<i>*x),Im y)| by AA40
  .=|(-(Im x),Re y)| - <i>*(|(Re (<i>*x),Im y)|)
    + <i>*(|(Im (<i>*x),Re y)|) + |(Im (<i>*x),Im y)| by Th85
  .=|(-(Im x),Re y)| - <i>*(|(-(Im x),Im y)|)
    + <i>*(|(Im (<i>*x),Re y)|) + |(Im (<i>*x),Im y)| by Th85
  .=|(-(Im x),Re y)| - <i>*(|(-(Im x),Im y)|)
    + <i>*(|(Re x,Re y)|) + |(Im (<i>*x),Im y)| by Th85
  .=|(-(Im x),Re y)| - <i>*(|(-(Im x),Im y)|)
    + <i>*(|(Re x,Re y)|) + |(Re x,Im y)| by Th85
  .=-|((Im x),Re y)| - <i>*(|(-(Im x),Im y)|)
    + <i>*(|(Re x,Re y)|) + |(Re x,Im y)| by EUCLID_2:22,A4,A5,A1
  .=-|((Im x),Re y)| - <i>*(-|((Im x),Im y)|)
    + <i>*(|(Re x,Re y)|) + |(Re x,Im y)| by EUCLID_2:22,A5,A1,A6
  .=-|((Im x),Re y)| + <i>*(|((Im x),Im y)|)
    + <i>*(|(Re x,Re y)|) + |(Re x,Im y)|
  .=-(1r)*(|((Im x),Re y)|) + <i>*(|((Im x),Im y)|)
    + <i>*(|(Re x,Re y)|) + |(Re x,Im y)| by COMPLEX1:def 7
  .=(<i>*<i>)*(|((Im x),Re y)|) + <i>*(|((Im x),Im y)|)
    + <i>*(|(Re x,Re y)|) + |(Re x,Im y)| by COMPLEX1:37
  .=(-1)*(|((Im x),Re y)|) + <i>*(|((Im x),Im y)|)
    + <i>*(|(Re x,Re y)|) + (1)*(|(Re x,Im y)|)
  .=<i>*(|(Re x,Re y)|) - <i>*<i>*(|(Re x,Im y)|)
    + <i>*<i>*(|(Im x,Re y)|) + <i>*(|(Im x,Im y)|) 
    by COMPLEX1:37,COMPLEX1:def 7 
  .=<i>*(|(Re x,Re y)| - <i>*(|(Re x,Im y)|)
    + <i>*(|(Im x,Re y)|) + |(Im x,Im y)|)
  .=<i>*(|(x,y)|) by AA40;
  hence thesis;
end;

theorem Th87: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    |(x,<i>*y)| = -<i>*(|(x,y)|)
proof let x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A4: len (Re x) = len x by Th73A;
  A5: len (Im x) = len x by Th73A;
  A6: len (Im y) = len y by Th73A;
  |(x,<i>*y)| = |(Re x,Re (<i>*y))| - <i>*(|(Re x,Im (<i>*y))|)
    + <i>*(|(Im x,Re (<i>*y))|) + |(Im x,Im (<i>*y))| by AA40
  .=|(Re x,-(Im y))| - <i>*(|(Re x,Im (<i>*y))|)
    + <i>*(|(Im x,Re (<i>*y))|) + |(Im x,Im (<i>*y))| by Th85
  .=|(Re x,-(Im y))| - <i>*(|(Re x,Re y)|)
    + <i>*(|(Im x,Re (<i>*y))|) + |(Im x,Im (<i>*y))| by Th85
  .=|(Re x,-(Im y))| - <i>*(|(Re x,Re y)|)
    + <i>*(|(Im x,-Im y)|) + |(Im x,Im (<i>*y))| by Th85
  .=|(Re x,-(Im y))| - <i>*(|(Re x,Re y)|)
    + <i>*(|(Im x,-Im y)|) + |(Im x,Re y)| by Th85
  .=-|(Re x,Im y)| - <i>*(|(Re x,Re y)|)
    + <i>*(|(Im x,-Im y)|) + |(Im x,Re y)| by EUCLID_2:23,A4,A6,A1
  .=-|(Re x,Im y)| - <i>*(|(Re x,Re y)|)
    + <i>*(-(|(Im x,Im y)|)) + |(Im x,Re y)| by EUCLID_2:23,A5,A1,A6
  .=-<i>*(|(Re x,Re y)| - <i>*(|(Re x,Im y)|)
    + <i>*(|(Im x,Re y)|) + |(Im x,Im y)|) 
  .=-<i>*(|(x,y)|) by AA40;
  hence thesis;
end;

theorem Th100C: 
  for a1 being Element of COMPLEX,y1 being FinSequence of COMPLEX
  for a2 being Element of REAL,y2 being FinSequence of REAL 
  st a1 = a2 & y1 = y2 & len y1=len y2 holds
    (a1 multcomplex)*y1 = (a2 multreal)*y2
proof
  let a1 be Element of COMPLEX,y1 be FinSequence of COMPLEX;
  let a2 be Element of REAL,y2 be FinSequence of REAL;
  assume A1: a1=a2 & y1 = y2 & len y1=len y2;
    for i st i in dom y1 holds
      (a1 multcomplex).(y1.i)=(a2 multreal).(y2.i)
    proof 
      let i;
      assume i in dom y1;
      A2: (a1*y1).i = a1*(y1.i) by Th27
                 .= multcomplex.(a1,(y1.i)) by BINOP_2:def 5
                 .= multcomplex.(a1,(id COMPLEX).(y1.i)) by FUNCT_1:35
                 .= (multcomplex[;](a1,id COMPLEX)).(y1.i) by FUNCOP_1:66
                 .= (a1 multcomplex).(y1.i) by COMPLSP1:def 5;
      A3: (a2*y2).i = a2*(y2.i) by RVSUM_1:66
                 .= multreal.(a2,(y2.i)) by BINOP_2:def 11
                 .= multreal.(a2,(id REAL).(y2.i)) by FUNCT_1:35
                 .= (multreal[;](a2,id REAL)).(y2.i) by FUNCOP_1:66
                 .= (a2 multreal).(y2.i) by RVSUM_1:def 3;
      a1*y1.i=(a1*y1).i by Th27;
      hence thesis by A1,A2,A3,RVSUM_1:66;
    end;
  hence thesis by A1,Th82;
end;

theorem Th100A: 
  for a1 being complex number, y1 being FinSequence of COMPLEX
  for a2 being Element of REAL,y2 being FinSequence of REAL
  st a1 = a2 & y1 = y2 & len y1=len y2 holds
    a1*y1 = a2*y2
proof
  let a1 be complex number, y1 be FinSequence of COMPLEX;
  let a2 be Element of REAL,y2 be FinSequence of REAL;
  reconsider aa = a1 as Element of COMPLEX by XCMPLX_0:def 2;
  assume A1: a1=a2 & y1 = y2 & len y1=len y2;
  a1*y1 = (aa multcomplex)*y1 by COMPLSP1:def 10
      .= (a2 multreal)*y2 by A1,Th100C
      .= a2*y2 by RVSUM_1:def 7;
  hence thesis;
end;

theorem Th100:
  for a, b being complex number holds
    (a+b)*z = a*z + b*z 
 proof
  let a, b be complex number;
  reconsider aa = a, bb = b as Element of COMPLEX by XCMPLX_0:def 2;
  set c1M = multcomplex[;](aa,id COMPLEX), c2M = multcomplex[;](bb,id COMPLEX);
  thus (a + b)*z
     = (multcomplex[;](aa + bb,id COMPLEX))*z by Lm2
    .= multcomplex[;](addcomplex.(aa,bb),id COMPLEX)*z by BINOP_2:def 3
    .= addcomplex.:(c1M,c2M)*z by COMPLSP1:15,FINSEQOP:36
    .= addcomplex.:(c1M*z,c2M*z) by FUNCOP_1:31
    .= c1M*z + c2M*z by COMPLSP1:def 7
    .= a*z + c2M*z by Lm2
    .= a*z + b*z by Lm2;
 end;

theorem Th100B:
  for a, b being complex number holds
    (a-b)*z = a*z - b*z 
 proof
  let a, b be complex number;
  reconsider aa = a, bb = b as Element of COMPLEX by XCMPLX_0:def 2;
    A1: len (a*z) = len z by Th14;
    A2: len (b*z) = len z by Th14;
    (a - b)*z=(a+(-b))*z
     .= aa*z +(-bb)*z by Th100
     .= aa*z + -(bb*z) by Th81
     .= a*z - b*z by A1,A2,Th54;
  hence thesis;    
 end;

theorem Th101: 
  for a being Element of COMPLEX,x being FinSequence of COMPLEX
  holds Re (a*x) = Re a * Re x - Im a * Im x &
        Im (a*x) = Im a * Re x + Re a * Im x 
proof let a be Element of COMPLEX, x be FinSequence of COMPLEX;
  A1: len (x*') = len x by AA10;
  A3: len Re x = len x by Th73A;
  A4: len Im x = len x by Th73A;
  A5: Re x=(1/2)*(x+x*') by AA99;
  A6: Im x=((-1/2*<i>))*(x-x*') by AA99b;
  reconsider z1=(a-<i>*(Im a)), z2=-<i>*(a-(Re a))
    as Element of COMPLEX by XCMPLX_0:def 2;
  reconsider z3=((Re a) + <i>*(Im a)), z4=(Re a * (1/2))
    as Element of COMPLEX by XCMPLX_0:def 2;
  reconsider z5=Re a, z6=Im a as Element of COMPLEX by XCMPLX_0:def 2;
  reconsider z7=<i>*Im a, z8=(-1/2) * <i>
    as Element of COMPLEX by XCMPLX_0:def 2;
  A7: len (1/2*z5*x) = len x by Th14;
  A8: len (1/2*(x+x*')) = len (x+x*') by Th14;
  A9: len (((-1/2*<i>))*(x-x*'))=len (x-x*') by Th14;
  A10: Re a * Re x = z5 * ((1/2)*(x+x*')) by A5,Th100A,A8
       .=(z5 * (1/2))*(x+x*') by Th80
       .=(z5*1/2)*x+(z5*1/2)*(x*') by Th53,A1;
  A15: Im a * Im x = z6 * (((-1/2*<i>))*(x-x*')) by A6,Th100A,A9
       .=(z6 * (-1/2*<i>))*(x-x*') by Th80
       .=((-1/2*<i>)*z6)*x -((-1/2*<i>)*z6)*(x*') by Th68,A1;
  A16: len (1/2*z5*x) = len x by Th14;
  A17: len (1/2*z5*(x*')) = len (x*') by Th14;
  A18: len (Re a * Re x)=len (Re x) by EUCLID_2:8;
  A19: len (Im a * Im x)=len (Im x) by EUCLID_2:8;
  A20: len (Im a * Re x)=len (Re x) by EUCLID_2:8;
  A21: len (Re a * Im x)=len (Im x) by EUCLID_2:8;
  A22: len (((-1/2*<i>)*z6)*x) = len x by Th14;
  A23: len (((-1/2*<i>)*z6)*(x*')) = len (x*') by Th14;
  A24: len (((1/2*<i>)*z6)*x) = len x by Th14; then
  A25: len (1/2*z5*x + (1/2*<i>)*z6*x)
        =len (1/2*z5*x) by Th17,A16;
  A26: len (1/2*z5*(x*') + (-1/2*<i>)*z6*(x*'))
        =len (1/2*z5*(x*')) by Th17,A17,A23;
  A27: len ((1/2*<i>)*z6*x+1/2*z5*x)
        =len ((1/2*<i>)*z6*x) by Th17,A16,A24;
  A28: len ((z5-z6*<i>)*x*') = len (x*') by Th14;
  A29: len ((z5 + <i>*z6)*x) = len x by Th14;
  A30: len (z5*x) = len x by Th14;
  A31: len ((<i>*z6)*x) = len x by Th14;
  A32: len (z5*(x*')) = len (x*') by Th14;
  A33: len ((z6*<i>)*(x*')) = len (x*') by Th14;
  A34: len ((<i>*z6)*(x*')) = len (x*') by Th14;
  A35: len ((a*x)*') = len (a*x) by AA10;    
  A36: len (1/2*(z5*(x*')))=len (z5*(x*')) by Th14;
  A37: len ((1/2*<i>)*z6*(x*')) = len (x*') by Th14;
  A38: len (z6*(x*')) = len (x*') by Th14;
  A39: len ((-z5)*(x*')) = len (x*') by Th14;
  A40: len (((-1/2*<i>)*z5)*x) = len x by Th14;
  A41: len (((-1/2*<i>)*z5)*(x*')) = len (x*') by Th14;
  A42: len (z6*x)=len x by Th14;
  A43: len ((1/2)*(z6*x)) = len (z6*x) by Th14;
  A44: len (((-1/2*<i>)*z5)*x) = len x by Th14; 
  A45: len ((((-1/2*<i>)*z5)*x + (1/2)*(z6*x)))
       = len (((-1/2*<i>)*z5)*x) by Th17,A44,A43,A42;
  A46: len ((1/2)*(z6*(x*')))=len (z6*(x*')) by Th14;
  A47: len (((1/2*<i>)*z5)*(x*'))=len (x*') by Th14;
  A48: len (((1/2*<i>)*z5)*(x*')+(1/2)*(z6*(x*')))
       = len (((1/2*<i>)*z5)*(x*')) by Th17,A47,A46,A38;
  A49: len ((1/2)*(z6*x)+((-1/2*<i>)*z5)*x)
       = len ((1/2)*(z6*x)) by Th17,A43,A42,A44;
  A50: len ((1/2)*(z6*(x*'))) = len (z6*(x*')) by Th14;
  A51: len ((1/2)*(z6*x)+(1/2)*(z6*(x*')))
       = len ((1/2)*(z6*x)) by Th17,A43,A42,A46,A38,A1;
  A52: len (-(a*x)*') = len ((a*x)*') by Th16;
  A53: Re (a*x) = (1/2)*(a*x+(a*x)*') by AA99
   .= (1/2)*((a*x)*'+(a*x)) by Th52,A35
   .= (1/2)*((a*'*x*')+a*x) by Th28
   .= (1/2)*((a*'*x*')+((z5) + <i>*z6)*x) by COMPLEX1:29
   .= (1/2)*(((z5-z6*<i>)*x*')+(z5 + <i>*z6)*x) by COMPLEX1:def 15
   .= (1/2)*((z5-z6*<i>)*x*')+(1/2)*((z5 + <i>*z6)*x)
       by Th53,A1,A29,A28
   .= (1/2)*((z5-z6*<i>)*x*')+(1/2)*(z5*x + (<i>*z6)*x) by Th100
   .= (1/2)*(z5*(x*')-(z6*<i>)*(x*'))+(1/2)*(z5*x + (<i>*z6)*x) by Th100B
   .= 1/2*(z5*(x*')-(z6*<i>)*(x*'))
      +(1/2*(z5*x) + 1/2*((<i>*z6)*x)) by Th53,A30,A31
   .= ((1/2*(z5*(x*'))-1/2*((z6*<i>)*(x*'))))
      +(1/2*(z5*x) + 1/2*((<i>*z6)*x)) by Th68,A32,A33
   .= 1/2*(z5*(x*'))-1/2*((<i>*z6)*(x*'))
      +(1/2*(z5*x) + 1/2*(<i>*z6)*x) by Th80
   .= 1/2*(z5*(x*'))-(1/2*<i>)*z6*(x*')
      +(1/2*(z5*x) + (1/2*<i>)*z6*x) by Th80
   .= (1/2*(z5*(x*'))+(-(1/2*<i>)*z6*(x*')))
      +((1/2*(z5*x) + (1/2*<i>)*z6*x)) by Th54,A32,A36,A37
   .= (1/2*(z5*(x*'))+((-1/2*<i>*z6)*(x*')))
      +((1/2*(z5*x) + (1/2*<i>)*z6*x)) by Th81
   .= ((1/2*z5)*(x*')+((-1/2*<i>)*z6)*(x*'))
      +((1/2*(z5*x) + (1/2*<i>)*z6*x)) by Th80
   .= ((1/2*z5)*(x*')+((-1/2*<i>)*z6)*(x*'))
      +((1/2*z5)*x + (1/2*<i>)*z6*x) by Th80
   .= (1/2*z5*x + (1/2*<i>)*z6*x)
      + (1/2*z5*(x*') + (-1/2*<i>)*z6*(x*')) by Th52,A7,A25,A26,A17,A1
   .= (1/2*<i>)*z6*x+1/2*z5*x
      + (1/2*z5*(x*') + (-1/2*<i>)*z6*(x*')) by Th52,A7,A24
   .= ((1/2*<i>)*z6*x+1/2*z5*x)
      + 1/2*z5*(x*') + (-1/2*<i>)*z6*(x*') by Th51,A17,A1,A23,A24,A27
   .= (1/2*<i>)*z6*x + (1/2*z5*x
      + 1/2*z5*(x*')) + (-1/2*<i>)*z6*(x*') by Th51,A7,A17,A1,A24
   .= (1/2*z5*x+1/2*z5*(x*'))
      +((-(-1/2*<i>)*z6)*x) +((-1/2*<i>)*z6*(x*')) by A10,Th52,A18,A3,A24
   .= (1/2*z5*x+1/2*z5*(x*'))
      +(-((-1/2*<i>)*z6*x)) +((-1/2*<i>)*z6*(x*')) by Th81
   .= (1/2*z5*x+1/2*z5*(x*'))
      -((-1/2*<i>)*z6*x) +((-1/2*<i>)*z6*(x*')) by A10,Th54,A18,A3,A22
   .= ((1/2)*z5*x+(1/2)*z5*(x*'))
      -(((-1/2*<i>)*z6)*x -((-1/2*<i>)*z6)*(x*')) by A10,Th65,A18,A3,A1,A22,A23
   .= Re a * Re x - Im a * Im x by A10,A15,Th75,A18,A19,A3,A4;
  A54: Im a * Re x = z6 * ((1/2)*(x+x*')) by A5,Th100A,A8
   .= (z6 * (1/2))*(x+x*') by Th80
   .= (1/2*z6)*x+(1/2*z6)*(x*') by Th53,A1;
  A55: Re a * Im x = z5*((-1/2*<i>)*(x-x*')) by A6,Th100A,A9
   .= (z5 * (-1/2*<i>))*(x-x*') by Th80
   .= ((-1/2*<i>)*z5)*x-((-1/2*<i>)*z5)*(x*') by Th68,A1;
  Im (a*x) = (-1/2*<i>)*(a*x-(a*x)*') by AA99b
   .= (-1/2*<i>)*(a*x+(-(a*x)*')) by Th54,A35
   .= (-1/2*<i>)*((-((a*x)*'))+(a*x)) by Th52,A35,A52
   .= (-1/2*<i>)*((-((a*x)*')))+(-1/2*<i>)*(a*x) by Th53,A35,A52
   .= (-1/2*<i>)*(-((a*')*(x*')))+(-1/2*<i>)*(a*x) by Th28 
   .= (-1/2*<i>)*(-((a*')*(x*')))
      +(-1/2*<i>)*((z5 + <i>*z6)*x) by COMPLEX1:29
   .= (-1/2*<i>)*(-((z5-z6*<i>)*x*'))
      +(-1/2*<i>)*((z5 + <i>*z6)*x) by COMPLEX1:def 15 
   .= (-1/2*<i>)*((-(z5-z6*<i>))*x*')
      +(-1/2*<i>)*((z5 + <i>*z6)*x) by Th81 
   .= (-1/2*<i>)*((-z5+z6*<i>)*(x*')) +(-1/2*<i>)*((z5 + <i>*z6)*x)
   .= (-1/2*<i>)*((-z5)*(x*')+(<i>*z6)*(x*'))
      +(-1/2*<i>)*((z5 + <i>*z6)*x) by Th100
   .= (-1/2*<i>)*((-z5)*(x*')+(<i>*z6)*(x*'))
      +(-1/2*<i>)*(z5*x + (<i>*z6)*x) by Th100
   .= (-1/2*<i>)*((-z5)*(x*')+(<i>*z6)*(x*'))
      +((-1/2*<i>)*(z5*x) + (-1/2*<i>)*((<i>*z6)*x)) by Th53,A30,A31
   .= (-1/2*<i>)*((-z5)*(x*'))+(-1/2*<i>)*((<i>*z6)*(x*'))
      +((-1/2*<i>)*(z5*x) + (-1/2*<i>)*((<i>*z6)*x)) by Th53,A39,A34
   .= ((-1/2*<i>)*(-z5))*(x*')+(-1/2*<i>)*((<i>*z6)*(x*'))
      +((-1/2*<i>)*(z5*x) + (-1/2*<i>)*((<i>*z6)*x)) by Th80
   .= ((-1/2*<i>)*(-z5))*(x*')+(-1/2*<i>)*(<i>*(z6*(x*')))
      +((-1/2*<i>)*(z5*x) + (-1/2*<i>)*((<i>*z6)*x)) by Th80
   .= ((-1/2*<i>)*(-z5))*(x*')+((-1/2*<i>)*<i>)*(z6*(x*'))
      +((-1/2*<i>)*(z5*x) + (-1/2*<i>)*((<i>*z6)*x)) by Th80
   .= ((-1/2*<i>)*(-z5))*(x*')+((-1/2*<i>)*<i>)*(z6*(x*'))
      +(((-1/2*<i>)*z5)*x + (-1/2*<i>)*((<i>*z6)*x)) by Th80
   .= ((-1/2*<i>)*(-z5))*(x*')+((-1/2*<i>)*<i>)*(z6*(x*'))
      +(((-1/2*<i>)*z5)*x + (-1/2*<i>)*(<i>*(z6*x))) by Th80
   .= ((1/2*<i>)*z5)*(x*')+(1/2)*(z6*(x*'))
      +(((-1/2*<i>)*z5)*x + (1/2)*(z6*x)) by Th80
   .= (((-1/2*<i>)*z5)*x + (1/2)*(z6*x))
      +(((1/2*<i>)*z5)*(x*')+(1/2)*(z6*(x*'))) by Th52,A45,A44,A48,A47,A1
   .= (1/2)*(z6*x)+((-1/2*<i>)*z5)*x
      +(((1/2*<i>)*z5)*(x*')+(1/2)*(z6*(x*'))) by Th52,A44,A43,A42
   .= (1/2)*(z6*x)+((-1/2*<i>)*z5)*x
      +((1/2)*(z6*(x*'))+((1/2*<i>)*z5)*(x*')) by Th52,A47,A46,A38
   .= (1/2)*(z6*x)+(((-1/2*<i>)*z5)*x)
     +(1/2)*(z6*(x*'))+((1/2*<i>)*z5)*(x*') by Th51,A43,A42,A47,A1,A46,A38,A49
   .= (1/2)*(z6*x)+((((-1/2*<i>)*z5)*x)
      +(1/2)*(z6*(x*')))+((1/2*<i>)*z5)*(x*') by Th51,A44,A43,A42,A1,A46,A38
   .= (1/2)*(z6*x)+((1/2)*(z6*(x*'))
      +(((-1/2*<i>)*z5)*x))+((1/2*<i>)*z5)*(x*') by Th52,A44,A1,A38,A50
   .= (1/2)*(z6*x)+(1/2)*(z6*(x*')) + (((-1/2*<i>)*z5)*x)+((1/2*<i>)*z5)*(x*')
       by Th51,A44,A43,A42,A1,A38,A50
   .= (1/2*(z6*x)+(1/2)*(z6*(x*')))+((((-1/2*<i>)*z5)*x)+((1/2*<i>)*z5)*(x*'))
       by Th51,A44,A43,A42,A47,A1,A51
   .= (1/2*z6)*x+(1/2)*(z6*(x*'))
      +((((-1/2*<i>)*z5)*x)+((1/2*<i>)*z5)*(x*')) by Th80
   .= (1/2*z6)*x+(1/2*z6)*(x*')
      +(((-1/2*<i>)*z5)*x+((-((-1/2*<i>)*z5))*(x*'))) by Th80
   .= (1/2*z6)*x+(1/2*z6)*(x*')
      +(((-1/2*<i>)*z5)*x+(-((-1/2*<i>)*z5)*(x*'))) by Th81
   .= (1/2*z6)*x+(1/2*z6)*(x*')
      +(((-1/2*<i>)*z5)*x-((-1/2*<i>)*z5)*(x*')) by Th54,A40,A41,A1
   .= Im a * Re x + Re a * Im x by A54,A55,Th72,A20,A21,A3,A4;
  hence thesis by A53;
end;

begin :: The Inner Product and Conjugate of Finite Sequences 

theorem Th88: 
  for x1,x2,y being FinSequence of COMPLEX st len x1=len x2 & len x2=len y 
    holds |((x1+x2),y)| = |(x1,y)| + |(x2,y)|
proof let x1,x2,y be FinSequence of COMPLEX;
  assume A1: len x1=len x2 & len x2=len y;
  A5: len (Re x1) = len x1 by Th73A;
  A6: len (Re x2) = len x2 by Th73A;
  A7: len (Re y) = len y by Th73A;
  A8: len (Im x1) = len x1 by Th73A;
  A9: len (Im x2) = len x2 by Th73A;
  A10: len (Im y) = len y by Th73A;
  |((x1+x2),y)|
   = |(Re (x1+x2),Re y)| - <i>*(|(Re (x1+x2),Im y)|)
     + <i>*(|(Im (x1+x2),Re y)|) + |(Im (x1+x2),Im y)| by AA40
  .= |((Re x1+ Re x2),Re y)| - <i>*(|(Re (x1+x2),Im y)|)
     + <i>*(|(Im (x1+x2),Re y)|) + |(Im (x1+x2),Im y)| by Th73,A1
  .= |((Re x1+ Re x2),Re y)| - <i>*(|((Re x1+Re x2),Im y)|)
     + <i>*(|(Im (x1+x2),Re y)|) + |(Im (x1+x2),Im y)| by Th73,A1
  .= |((Re x1+ Re x2),Re y)| - <i>*(|((Re x1+Re x2),Im y)|)
     + <i>*(|((Im x1+ Im x2),Re y)|) + |(Im (x1+x2),Im y)| by Th73,A1
  .= |((Re x1+ Re x2),Re y)| - <i>*(|((Re x1+Re x2),Im y)|)
     + <i>*(|((Im x1+ Im x2),Re y)|) + |((Im x1+ Im x2),Im y)| by Th73,A1
  .= |(Re x1,Re y)|+ |(Re x2,Re y)| - <i>*(|((Re x1+Re x2),Im y)|)
     + <i>*(|((Im x1+ Im x2),Re y)|) + |((Im x1+ Im x2),Im y)|
     by EUCLID_2:19,A1,A5,A6,A7
  .= |(Re x1,Re y)|+ |(Re x2,Re y)| - <i>*(|(Re x1,Im y)| + |(Re x2,Im y)|)
     + <i>*(|((Im x1+ Im x2),Re y)|) + |((Im x1+ Im x2),Im y)|
     by EUCLID_2:19,A1,A5,A6,A10
  .= |(Re x1,Re y)|+ |(Re x2,Re y)| - <i>*(|(Re x1,Im y)| + |(Re x2,Im y)|)
     + <i>*(|(Im x1,Re y)|+ |(Im x2,Re y)|) + |((Im x1+ Im x2),Im y)|
     by EUCLID_2:19,A1,A7,A8,A9
  .= |(Re x1,Re y)| + |(Re x2,Re y)| 
     - <i>*(|(Re x1,Im y)| + |(Re x2,Im y)|)
     + <i>*(|(Im x1,Re y)| + |(Im x2,Re y)|)
     + (|(Im x1,Im y)|+ |(Im x2,Im y)|) by EUCLID_2:19,A1,A8,A9,A10
  .= (|(Re x1,Re y)| - <i>*(|(Re x1,Im y)|)
     + <i>*(|(Im x1,Re y)|) + |(Im x1,Im y)|)
     + (|(Re x2,Re y)| - <i>*(|(Re x2,Im y)|)
     + <i>*(|(Im x2,Re y)|) + |(Im x2,Im y)|)
  .= |(x1,y)|+ (|(Re x2,Re y)| - <i>*(|(Re x2,Im y)|)
     + <i>*(|(Im x2,Re y)|) + |(Im x2,Im y)|) by AA40
  .= |(x1,y)| + |(x2,y)| by AA40;
  hence thesis;
end;

theorem Th89: 
  for x1,x2 being FinSequence of COMPLEX st len x1=len x2 holds
    |(-x1, x2)| = -|(x1, x2)|
  proof let x1,x2 be FinSequence of COMPLEX;
    assume A1: len x1=len x2;
A3: len (<i>*x1)=len x1 by Th14;
    |(-x1, x2)| = |(<i>*<i>*x1, x2)| by Th23
      .= |(<i>*(<i>*x1), x2)| by Th80
      .= <i>*(|(<i>*x1, x2)|) by Th86,A1,A3
      .= <i>*(<i>*(|(x1, x2)|)) by Th86,A1
      .= (-1)*|(x1, x2)|;
    hence thesis;
  end;

theorem Th90: 
  for x1,x2 being FinSequence of COMPLEX st len x1=len x2 holds
    |(x1, -x2)| = -|(x1, x2)| 
proof let x1,x2 be FinSequence of COMPLEX;
    assume A1: len x1=len x2;
A3: len (<i>*x2)=len x2 by Th14;
       |(x1, -x2)| = |(x1, <i>*<i>*x2)| by Th23
      .= |(x1, <i>*(<i>*x2))| by Th80
      .= -<i>*(|(x1, <i>*x2)|) by Th87,A1,A3
      .= -<i>*(-<i>*(|(x1, x2)|)) by Th87,A1
      .= -(|(x1, x2)|);
    hence thesis;
end;

theorem
  for x1,x2 being FinSequence of COMPLEX st len x1=len x2 holds
    |(-x1, -x2)| = |(x1, x2)|
proof let x1,x2 be FinSequence of COMPLEX;
  assume A1: len x1=len x2;
  then len (-x2)=len x1 by Th16;
  then |(-x1, -x2)| = -|(x1, -x2)| by Th89
    .= --|(x1, x2)| by A1,Th90;
  hence thesis;
end;

theorem Th92: 
  for x1,x2,x3 being FinSequence of COMPLEX st len x1=len x2 & len x2=len x3
    holds |(x1-x2, x3)| = |(x1, x3)| - |(x2, x3)|
  proof let x1,x2,x3 be FinSequence of COMPLEX;
    assume A1: len x1=len x2 & len x2=len x3;
A2: len (-x2)=len x2 by Th16;
    |(x1 - x2, x3)| = |(x1 + -x2, x3)| by A1,Th21
      .= |(x1, x3)| + |(-x2, x3)| by A1,A2,Th88
      .= |(x1, x3)| + - |(x2, x3)| by A1,Th89;
    hence thesis;
  end;

theorem Th93: 
  for x,y1,y2 being FinSequence of COMPLEX st len x=len y1 & len y1=len y2 
    holds |(x, y1+y2)| = |(x, y1)| + |(x, y2)|
proof let x,y1,y2 be FinSequence of COMPLEX;
  assume A1:  len x=len y1 &len y1=len y2;
  A5: len (Re y1) = len y1 by Th73A;
  A6: len (Re y2) = len y2 by Th73A;
  A7: len (Re x) = len x by Th73A;
  A8: len (Im y1) = len y1 by Th73A;
  A9: len (Im y2) = len y2 by Th73A;
  A10: len (Im x) = len x by Th73A;
  |(x,(y1+y2))|
   = |(Re x,Re (y1+y2))| - <i>*(|(Re x,Im (y1+y2))|)
     + <i>*(|(Im x,Re (y1+y2))|) + |(Im x,Im (y1+y2))| by AA40
  .= |(Re x,(Re y1+ Re y2))| - <i>*(|(Re x,Im (y1+y2))|)
     + <i>*(|(Im x,Re (y1+y2))|) + |(Im x,Im (y1+y2))| by Th73,A1
  .= |(Re x,(Re y1+ Re y2))| - <i>*(|(Re x,Im (y1+y2))|)
     + <i>*(|(Im x,Re (y1+y2))|) + |(Im x,(Im y1+Im y2))| by Th73,A1
  .= |(Re x,(Re y1+ Re y2))| - <i>*(|(Re x,(Im y1+Im y2))|)
     + <i>*(|(Im x,Re (y1+y2))|) + |(Im x,(Im y1+Im y2))| by Th73,A1
  .= |(Re x,(Re y1+ Re y2))| - <i>*(|(Re x,(Im y1+Im y2))|)
     + <i>*(|(Im x,(Re y1+Re y2))|) + |(Im x,(Im y1+Im y2))| by Th73,A1
  .= |(Re x,Re y1)|+ |(Re x,Re y2)| - <i>*(|(Re x,(Im y1+Im y2))|)
     + <i>*(|(Im x,(Re y1+Re y2))|) + |(Im x,(Im y1+Im y2))|
     by EUCLID_2:27,A1,A5,A6,A7
  .= |(Re x,Re y1)|+ |(Re x,Re y2)| - <i>*(|(Re x,(Im y1+Im y2))|)
     + <i>*(|(Im x,(Re y1+Re y2))|) + (|(Im x,Im y1)|+|(Im x,Im y2)|)
     by EUCLID_2:27,A1,A8,A9,A10
  .= |(Re x,Re y1)|+ |(Re x,Re y2)| - <i>*(|(Re x,Im y1)|+|(Re x,Im y2)|)
     + <i>*(|(Im x,(Re y1+Re y2))|) + (|(Im x,Im y1)|+|(Im x,Im y2)|)
     by EUCLID_2:27,A1,A7,A8,A9
  .= |(Re x,Re y1)|+ |(Re x,Re y2)| - <i>*(|(Re x,Im y1)|+|(Re x,Im y2)|)
     + <i>*(|(Im x,Re y1)|+|(Im x,Re y2)|) + (|(Im x,Im y1)|+|(Im x,Im y2)|)
     by EUCLID_2:27,A1,A5,A6,A10
  .= (|(Re x,Re y1)| - <i>*(|(Re x,Im y1)|)+
      <i>*(|(Im x,Re y1)|)+ |(Im x,Im y1)|)
    +(|(Re x,Re y2)| - <i>*(|(Re x,Im y2)|)+
      <i>*(|(Im x,Re y2)|)+ |(Im x,Im y2)|)
  .= |(x,y1)| + (|(Re x,Re y2)| - <i>*(|(Re x,Im y2)|)+
      <i>*(|(Im x,Re y2)|)+ |(Im x,Im y2)|) by AA40
  .= |(x,y1)| + |(x,y2)| by AA40;
 hence thesis;
end;

theorem Th94:
  for x,y1,y2 being FinSequence of COMPLEX st len x=len y1 & len y1=len y2
    holds |(x, y1-y2)| = |(x, y1)| - |(x, y2)|
proof let x,y1,y2 be FinSequence of COMPLEX;
  assume A1:  len x=len y1 &len y1=len y2;
  A5: len (Re y1) = len y1 by Th73A;
  A6: len (Re y2) = len y2 by Th73A;
  A7: len (Re x) = len x by Th73A;
  A8: len (Im y1) = len y1 by Th73A;
  A9: len (Im y2) = len y2 by Th73A;
  A10: len (Im x) = len x by Th73A;
  |(x,(y1-y2))|
   = |(Re x,Re (y1-y2))| - <i>*(|(Re x,Im (y1-y2))|)
     + <i>*(|(Im x,Re (y1-y2))|) + |(Im x,Im (y1-y2))| by AA40
  .= |(Re x,(Re y1- Re y2))| - <i>*(|(Re x,Im (y1-y2))|)
     + <i>*(|(Im x,Re (y1-y2))|) + |(Im x,Im (y1-y2))| by Th76,A1
  .= |(Re x,(Re y1- Re y2))| - <i>*(|(Re x,Im (y1-y2))|)
     + <i>*(|(Im x,Re (y1-y2))|) + |(Im x,(Im y1-Im y2))| by Th76,A1
  .= |(Re x,(Re y1- Re y2))| - <i>*(|(Re x,(Im y1-Im y2))|)
     + <i>*(|(Im x,Re (y1-y2))|) + |(Im x,(Im y1-Im y2))| by Th76,A1
  .= |(Re x,(Re y1- Re y2))| - <i>*(|(Re x,(Im y1-Im y2))|)
     + <i>*(|(Im x,(Re y1-Re y2))|) + |(Im x,(Im y1-Im y2))| by Th76,A1
  .= |(Re x,Re y1)|- |(Re x,Re y2)| - <i>*(|(Re x,(Im y1-Im y2))|)
     + <i>*(|(Im x,(Re y1-Re y2))|) + |(Im x,(Im y1-Im y2))|
     by EUCLID_2:28,A1,A5,A6,A7
  .= |(Re x,Re y1)|- |(Re x,Re y2)| - <i>*(|(Re x,(Im y1-Im y2))|)
     + <i>*(|(Im x,(Re y1-Re y2))|) + (|(Im x,Im y1)|-|(Im x,Im y2)|)
     by EUCLID_2:28,A1,A8,A9,A10
  .= |(Re x,Re y1)|- |(Re x,Re y2)| - <i>*(|(Re x,Im y1)|-|(Re x,Im y2)|)
     + <i>*(|(Im x,(Re y1-Re y2))|) + (|(Im x,Im y1)|-|(Im x,Im y2)|)
     by EUCLID_2:28,A1,A7,A8,A9
  .= |(Re x,Re y1)|- |(Re x,Re y2)| - <i>*(|(Re x,Im y1)|-|(Re x,Im y2)|)
     + <i>*(|(Im x,Re y1)|-|(Im x,Re y2)|) + (|(Im x,Im y1)|-|(Im x,Im y2)|)
     by EUCLID_2:28,A1,A5,A6,A10
  .= (|(Re x,Re y1)| - <i>*(|(Re x,Im y1)|)+
      <i>*(|(Im x,Re y1)|)+ |(Im x,Im y1)|)
    -(|(Re x,Re y2)| - <i>*(|(Re x,Im y2)|)+
      <i>*(|(Im x,Re y2)|)+ |(Im x,Im y2)|)
  .= |(x,y1)| - (|(Re x,Re y2)| - <i>*(|(Re x,Im y2)|)+
      <i>*(|(Im x,Re y2)|)+ |(Im x,Im y2)|) by AA40
  .= |(x,y1)| - |(x,y2)| by AA40;
 hence thesis;
end;

theorem Th95: 
  for x1,x2,y1,y2 being FinSequence of COMPLEX
  st len x1=len x2 & len x2=len y1 & len y1=len y2 holds
  |(x1+x2, y1+y2)| = |(x1, y1)| + |(x1, y2)| + |(x2, y1)| + |(x2, y2)|
proof let x1,x2,y1,y2 be FinSequence of COMPLEX;
   assume A1: len x1=len x2 & len x2=len y1 & len y1=len y2;then
   |(x1+x2, y2)| = |(x1, y2)| + |(x2, y2)| by Th88;then
A2:|(x1+x2, y1)| + |(x1+x2, y2)|
      = (|(x1, y1)|+|(x2, y1)|) + (|(x1, y2)| + |(x2, y2)|) by A1,Th88;
   len (x1+x2)=len x1 by A1,Th17;
   hence thesis by A1,A2,Th93;
end;

theorem Th96:  
  for x1,x2,y1,y2 being FinSequence of COMPLEX 
  st len x1=len x2 & len x2=len y1 & len y1=len y2 holds
  |(x1-x2, y1-y2)| = |(x1, y1)| - |(x1, y2)| - |(x2, y1)| + |(x2, y2)|
proof let x1,x2,y1,y2 be FinSequence of COMPLEX;
    assume A1: len x1=len x2 & len x2=len y1 & len y1=len y2;
    then |(x1,y1-y2)| = |(x1,y1)| - |(x1,y2)| by Th94; 
    then A2: |(x1,y1-y2)| - |(x2,y1-y2)|
      = (|(x1,y1)|-|(x1,y2)|)-(|(x2,y1)|-|(x2,y2)|) by A1,Th94;
    len (y1 - y2)=len y1 by A1,Th18;
    hence thesis by A1,A2,Th92;
end;

theorem Th97: 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    |(x,y)| = (|(y,x)|)*'
  proof let x,y be FinSequence of COMPLEX;
   assume len x=len y;
   set x1=|(Re y,Re x)|,x2=|(Re y,Im x)|,y1=|(Im y,Re x)|, y2=|(Im y,Im x)|;
   reconsider x1'=x1, x2'=x2, y1'=y1, y2'=y2 as Element of REAL
     by XREAL_0:def 1;
   A3: Im x1'=0 by COMPLEX1:def 3;
   A5: Im x2'=0 by COMPLEX1:def 3;
   A7: Im y1'=0 by COMPLEX1:def 3;
   A9: Im y2'=0 by COMPLEX1:def 3;
   reconsider x1'=x1, x2'=x2, y1'=y1, y2'=y2 as Element of COMPLEX
     by XCMPLX_0:def 2;
   (|(y,x)|)*' = (|(Re y,Re x)| - <i>*(|(Re y,Im x)|)
    + <i>*(|(Im y,Re x)|) + |(Im y,Im x)|)*' by AA40
  .=(x1' + y2' + <i>*((y1') - (x2')))*'
  .=(x1' + y2')*' + (<i>*((y1') - (x2')))*' by COMPLEX1:118
  .=(x1')*' + (y2')*' + (<i>*((y1') - (x2')))*' by COMPLEX1:118
  .=x1' + (y2')*' + (<i>*((y1') - (x2')))*' by COMPLEX1:124,A3
  .=x1' + y2' + (<i>*((y1') - (x2')))*' by COMPLEX1:124,A9
  .=x1' + y2' + (-<i>)*((y1') - (x2'))*' by COMPLEX1:116,121
  .=x1' + y2' + (-<i>)*((y1')*' - (x2')*') by COMPLEX1:120
  .=x1' + y2' + (-<i>)*((y1') - (x2')*') by COMPLEX1:124,A7
  .=x1' + y2' + (-<i>)*((y1') - (x2')) by COMPLEX1:124,A5
  .=|(Re x,Re y)| - <i>*(|(Re x,Im y)|)
    + <i>*(|(Im x,Re y)|) + |(Im x,Im y)|
  .=|(x, y)| by AA40;
    hence thesis;
  end;

theorem 
  for x,y being FinSequence of COMPLEX st len x=len y holds
    |(x+y,x+y)| = |(x,x)| + 2*Re(|(x,y)|) + |(y,y)|
  proof let x,y be FinSequence of COMPLEX;
   assume A1: len x=len y;
   set z=|(x, y)|;
   |(x+y, x+y)|
     =|(x, x)| + |(x, y)| + |(y, x)| + |(y, y)| by Th95,A1
    .=|(x, x)| + |(x, y)| + (|(x, y)|)*' + |(y, y)| by Th97,A1
    .=|(x, x)| + (z+z*') + |(y, y)|
    .=|(x, x)| + 2*Re(|(x, y)|) + |(y, y)| by Th47;
    hence thesis;
  end;

theorem
  for x,y being FinSequence of COMPLEX st len x=len y holds
    |(x-y, x-y)| = |(x, x)| - 2*Re(|(x, y)|) + |(y, y)|
  proof let x,y be FinSequence of COMPLEX;
   assume A1: len x=len y;
   set z=|(x, y)|;
   |(x-y, x-y)|
     =|(x, x)| - |(x, y)| - |(y, x)| + |(y, y)| by Th96,A1
    .=|(x, x)| - |(x, y)| - (|(x, y)|)*' + |(y, y)| by Th97,A1
    .=|(x, x)| - (z + z*') + |(y, y)|
    .=|(x, x)| - 2*Re(|(x, y)|) + |(y, y)| by Th47;
    hence thesis;
  end;

theorem Th102:
  for a being Element of COMPLEX, x,y being FinSequence of COMPLEX
  st len x=len y holds |(a*x,y)| = a*(|(x,y)|)
proof let a be Element of COMPLEX, x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
  A5: len (Re x) = len x by Th73A;
  A7: len (Re y) = len y by Th73A;
  A8: len (Im x) = len x by Th73A;
  A10: len (Im y) = len y by Th73A;
  A13: len (Re a * Re x) = len (Re x) by EUCLID_2:8;
  A14: len (Im a * Im x) = len (Im x) by EUCLID_2:8;
  A15: len (Re a * Im x) = len (Im x) by EUCLID_2:8;
  A16: len (Im a * Re x) = len (Re x) by EUCLID_2:8;
  |(a*x,y)|
   = |(Re (a*x),Re y)| - <i>*(|(Re (a*x),Im y)|)
     + <i>*(|(Im (a*x),Re y)|) + |(Im (a*x),Im y)| by AA40
  .= |((Re a * Re x - Im a * Im x),Re y)| - <i>*(|(Re (a*x),Im y)|)
     + <i>*(|(Im (a*x),Re y)|) + |(Im (a*x),Im y)| by Th101
  .= |((Re a * Re x - Im a * Im x),Re y)| 
     - <i>*(|((Re a * Re x - Im a * Im x),Im y)|)
     + <i>*(|(Im (a*x),Re y)|) + |(Im (a*x),Im y)| by Th101
  .= |((Re a * Re x - Im a * Im x),Re y)|
     - <i>*(|((Re a * Re x - Im a * Im x),Im y)|)
     + <i>*(|((Im a * Re x + Re a * Im x),Re y)|)
     + |(Im (a*x),Im y)| by Th101
  .= |((Re a * Re x - Im a * Im x),Re y)|
     - <i>*(|((Re a * Re x - Im a * Im x),Im y)|)
     + <i>*(|((Im a * Re x + Re a * Im x),Re y)|)
     + (|((Im a * Re x + Re a * Im x),Im y)|) by Th101
  .= |(Re a * Re x,Re y)|-|(Im a * Im x,Re y)|
     - <i>*(|((Re a * Re x - Im a * Im x),Im y)|)
     + <i>*(|((Im a * Re x + Re a * Im x),Re y)|)
     + (|((Im a * Re x + Re a * Im x),Im y)|)
     by EUCLID_2:25,A5,A8,A1,A13,A14,A7
  .= |(Re a * Re x,Re y)|-|(Im a * Im x,Re y)|
     - <i>*((|(Re a * Re x,Im y)|)-(|(Im a * Im x,Im y)|))
     + <i>*(|((Im a * Re x + Re a * Im x),Re y)|)
     + (|((Im a * Re x + Re a * Im x),Im y)|) 
     by EUCLID_2:25,A5,A8,A1,A13,A14,A10
  .= |(Re a * Re x,Re y)|-|(Im a * Im x,Re y)|
     - <i>*((|(Re a * Re x,Im y)|)-(|(Im a * Im x,Im y)|))
     + <i>*((|(Im a * Re x,Re y)|)+(|(Re a * Im x,Re y)|))
     + (|((Im a * Re x + Re a * Im x),Im y)|) 
     by EUCLID_2:19,A5,A8,A1,A15,A7,A16
  .= |(Re a * Re x,Re y)|-|(Im a * Im x,Re y)|
     - <i>*((|(Re a * Re x,Im y)|)-(|(Im a * Im x,Im y)|))
     + <i>*((|(Im a * Re x,Re y)|)+(|(Re a * Im x,Re y)|))
     + ((|(Im a * Re x,Im y)|)+(|(Re a * Im x,Im y)|)) 
     by EUCLID_2:19,A5,A8,A1,A15,A10,A16
  .= |(Re a * Re x,Re y)|-|(Im a * Im x,Re y)|
     - <i>*((Re a * (|(Re x,Im y)|))-(|(Im a * Im x,Im y)|))
     + <i>*((|(Im a * Re x,Re y)|)+(|(Re a * Im x,Re y)|))
     + ((|(Im a * Re x,Im y)|)+(|(Re a * Im x,Im y)|)) 
     by EUCLID_2:20,A5,A1,A10
  .= |(Re a * Re x,Re y)|-|(Im a * Im x,Re y)|
     - <i>*((Re a * (|(Re x,Im y)|))-Im a * (|(Im x,Im y)|))
     + <i>*((|(Im a * Re x,Re y)|)+(|(Re a * Im x,Re y)|))
     + ((|(Im a * Re x,Im y)|)+(|(Re a * Im x,Im y)|)) 
     by EUCLID_2:20,A8,A1,A10
  .= Re a * (|(Re x,Re y)|)-|(Im a * Im x,Re y)|
     - <i>*((Re a * (|(Re x,Im y)|))-Im a * (|(Im x,Im y)|))
     + <i>*((|(Im a * Re x,Re y)|)+(|(Re a * Im x,Re y)|))
     + ((|(Im a * Re x,Im y)|)+(|(Re a * Im x,Im y)|)) 
     by EUCLID_2:20,A5,A1,A7
  .= Re a * (|(Re x,Re y)|)-Im a * (|(Im x,Re y)|)
     - <i>*((Re a * (|(Re x,Im y)|))-Im a * (|(Im x,Im y)|))
     + <i>*((|(Im a * Re x,Re y)|)+(|(Re a * Im x,Re y)|))
     + ((|(Im a * Re x,Im y)|)+(|(Re a * Im x,Im y)|)) 
     by EUCLID_2:20,A8,A1,A7
  .= Re a * (|(Re x,Re y)|)-Im a * (|(Im x,Re y)|)
     - <i>*((Re a * (|(Re x,Im y)|))-Im a * (|(Im x,Im y)|))
     + <i>*(Im a * (|(Re x,Re y)|)+(|(Re a * Im x,Re y)|))
     + ((|(Im a * Re x,Im y)|)+(|(Re a * Im x,Im y)|)) 
     by EUCLID_2:20,A5,A1,A7
  .= Re a * (|(Re x,Re y)|)-Im a * (|(Im x,Re y)|)
     - <i>*((Re a * (|(Re x,Im y)|))-Im a * (|(Im x,Im y)|))
     + <i>*(Im a * (|(Re x,Re y)|)+Re a * (|(Im x,Re y)|))
     + ((|(Im a * Re x,Im y)|)+(|(Re a * Im x,Im y)|)) 
     by EUCLID_2:20,A8,A1,A7
  .= Re a * (|(Re x,Re y)|)-Im a * (|(Im x,Re y)|)
     - <i>*((Re a * (|(Re x,Im y)|))-Im a * (|(Im x,Im y)|))
     + <i>*(Im a * (|(Re x,Re y)|)+Re a * (|(Im x,Re y)|))
     + (Im a * (|(Re x,Im y)|)+(|(Re a * Im x,Im y)|)) 
     by EUCLID_2:20,A5,A1,A10
  .= Re a * (|(Re x,Re y)|)-Im a * (|(Im x,Re y)|)
     - <i>*((Re a * (|(Re x,Im y)|))-Im a * (|(Im x,Im y)|))
     + <i>*(Im a * (|(Re x,Re y)|)+Re a * (|(Im x,Re y)|))
     + (Im a * (|(Re x,Im y)|)+Re a * (|(Im x,Im y)|)) 
     by EUCLID_2:20,A8,A1,A10
  .= Re a*(|(Re x,Re y)|) + <i>*(Im a)*(|(Re x,Re y)|)
    - Re a*<i>*(|(Re x,Im y)|) + (Im a)*(|(Re x,Im y)|)
    + (Re a*(<i>*(|(Im x,Re y)|)) - (Im a)*(|(Im x,Re y)|))
    + (Re a + <i>*(Im a))*(|(Im x,Im y)|)
  .= Re a*(|(Re x,Re y)|) + <i>*(Im a)*(|(Re x,Re y)|)
    - Re a*<i>*(|(Re x,Im y)|) + (Im a)*(|(Re x,Im y)|)
    + (Re a + <i>*(Im a))*(<i>*(|(Im x,Re y)|))
    + a*(|(Im x,Im y)|) by COMPLEX1:29
  .= Re a*(|(Re x,Re y)|) + <i>*(Im a)*(|(Re x,Re y)|)
    - (Re a + <i>*(Im a))*(<i>*(|(Re x,Im y)|))
    + a*(<i>*(|(Im x,Re y)|)) + a*(|(Im x,Im y)|) by COMPLEX1:29
  .= (Re a + <i>*(Im a))*(|(Re x,Re y)|) - a*(<i>*(|(Re x,Im y)|))
    + a*(<i>*(|(Im x,Re y)|)) + a*(|(Im x,Im y)|) by COMPLEX1:29
  .= a*(|(Re x,Re y)|) - a*(<i>*(|(Re x,Im y)|))
    + a*(<i>*(|(Im x,Re y)|)) + a*(|(Im x,Im y)|) by COMPLEX1:29
  .= a*(|(Re x,Re y)| - <i>*(|(Re x,Im y)|)
    + <i>*(|(Im x,Re y)|) + |(Im x,Im y)|)
  .= a*(|(x,y)|) by AA40;
  hence thesis;
end;  

theorem Th103:
  for a being Element of COMPLEX, x,y being FinSequence of COMPLEX
  st len x=len y holds |(x,a*y)| = (a*')*(|(x,y)|)
proof let a be Element of COMPLEX, x,y be FinSequence of COMPLEX;
  assume A1: len x=len y;
A2: len (a*y) = len y by Th14;
  |(x,a*y)|=(|(a*y,x)|)*' by Th97,A1,A2
          .=(a*|(y,x)|)*' by Th102,A1
          .=(a*')*(|(y,x)|)*' by COMPLEX1:121
          .=(a*')*(|(x,y)|) by Th97,A1;
  hence thesis;
end;

theorem 
  for a,b being Element of COMPLEX,x,y,z being FinSequence of COMPLEX
     st len x=len y & len y=len z holds
    |((a*x+b*y), z)| = a*|(x,z)| + b*|(y,z)|
proof let a,b be Element of COMPLEX,x,y,z be FinSequence of COMPLEX;
    assume A1: len x=len y & len y=len z;
A2: len (a*x)=len x by Th14;
    len (b*y)=len y by Th14;
    then |(a*x + b*y, z)| = |(a*x, z)| + |(b*y, z)| by A1,A2,Th88
        .= a*|(x,z)| + |(b*y,z)| by A1,Th102
        .= a*|(x,z)| + b*|(y,z)| by A1,Th102;
    hence thesis;
end;

theorem
  for a,b being Element of COMPLEX,x,y,z being FinSequence of COMPLEX 
  st len x=len y & len y=len z holds
    |(x,a*y+b*z)| = (a*')*|(x,y)| + (b*')*|(x,z)|
  proof let a,b be Element of COMPLEX,x,y,z be FinSequence of COMPLEX;
    assume A1: len x=len y & len y=len z;
A2: len (a*y)=len y by Th14;
len (b*z)=len z by Th14;
    then |(x,a*y+b*z)| = |(x, a*y)| + |(x, b*z)| by A1,A2,Th93
        .= (a*')*|(x,y)| + |(x,b*z)| by A1,Th103
        .= (a*')*|(x,y)| + (b*')*|(x,z)| by A1,Th103;
    hence thesis;
  end;
