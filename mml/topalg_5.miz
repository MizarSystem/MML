:: The Fundamental Group of the Circle
::  by Artur Korni{\l}owicz
::
:: Received February 22, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, EUCLID, RELAT_1, ARYTM, SUBSET_1, TOPALG_1, WELLORD1,
      GR_CY_1, FINSET_1, METRIC_1, INT_1, ALGSTR_1, BORSUK_1, FUNCT_1, SIN_COS,
      WAYBEL20, EQREL_1, BORSUK_2, ORDINAL2, ARYTM_1, MCART_1, COMPLEX1,
      SQUARE_1, GROUP_6, T_0TOPSP, TOPS_2, ARYTM_3, RCOMP_1, TEX_2, ASYMPT_0,
      FUNCOP_1, TOPMETR, JGRAPH_2, GRAPH_1, TOPALG_2, JORDAN1, BORSUK_6,
      SGRAPH1, PCOMPS_1, FUNCT_4, BOOLE, SEQM_3, SETFAM_1, TARSKI, COMPTS_1,
      TOPREAL2, TAXONOM2, FINSEQ_1, RELAT_2, RCOMP_3, TOPREALB, TOPS_1,
      CONNSP_1, CONNSP_2, CONNSP_3, RLVECT_3, CANTOR_1, CARD_4, PBOOLE, PROB_1,
      TOPALG_5, ALGSTR_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, ORDINAL1, RELAT_1,
      EQREL_1, FUNCT_1, CARD_3, CARD_4, RELSET_1, PARTFUN1, SEQ_1, FUNCT_2,
      BINOP_1, PBOOLE, FINSET_1, NUMBERS, XCMPLX_0, XREAL_0, REAL_1, SQUARE_1,
      NAT_1, INT_1, SEQM_3, FINSEQ_1, FUNCT_4, RCOMP_1, RCOMP_2, DOMAIN_1,
      STRUCT_0, ALGSTR_0, PRE_TOPC, TOPS_1, GR_CY_1, GROUP_6, METRIC_1,
      CANTOR_1, YELLOW_8, BORSUK_1, TOPS_2, COMPTS_1, CONNSP_1, CONNSP_2,
      CONNSP_3, TEX_2, TSEP_1, TOPMETR, EUCLID, PCOMPS_1, BORSUK_2, SIN_COS,
      BORSUK_3, JORDAN16, TAXONOM2, BORSUK_6, TOPALG_1, TOPREAL9, TOPALG_2,
      TOPREALB, RCOMP_3, XXREAL_0;
 constructors SQUARE_1, NAT_1, CARD_3, SEQM_3, RCOMP_2, PBOOLE, REALSET1,
      CARD_4, BINARITH, SIN_COS, TOPS_1, CONNSP_1, COMPTS_1, GRCAT_1, TSEP_1,
      TEX_2, CANTOR_1, MONOID_0, PSCOMP_1, CONNSP_3, YELLOW_8, TAXONOM2,
      BORSUK_3, JCT_MISC, BORSUK_4, JORDAN16, TOPALG_1, TOPREAL9, TOPREALB,
      RCOMP_3, SEQ_1, BINOP_2;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, RELSET_1, FUNCT_2, NUMBERS,
      XXREAL_0, XREAL_0, NAT_1, INT_1, MEMBERED, FINSEQ_1, REALSET1, STRUCT_0,
      PRE_TOPC, TOPS_1, COMPTS_1, METRIC_1, BORSUK_1, TSEP_1, GR_CY_1,
      MONOID_0, EUCLID, TOPMETR, BORSUK_2, YELLOW12, YELLOW13, BORSUK_4,
      BORSUK_5, TOPALG_1, TOPALG_2, TOPALG_3, TOPREALB, RCOMP_3, VALUED_0;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions FUNCT_1, FUNCT_2, GROUP_6, TOPS_2, TSEP_1, BORSUK_2, BORSUK_6,
      COMPTS_1, PRE_TOPC, TARSKI, XBOOLE_0, CONNSP_1, T_0TOPSP, CONNSP_2,
      RCOMP_3, BINOP_1, GR_CY_1, STRUCT_0, BORSUK_1, ALGSTR_0, SETFAM_1;
 theorems GROUP_6, PRE_TOPC, XREAL_0, FUNCT_2, BORSUK_1, INT_1, SIN_COS,
      EUCLID, JGRAPH_1, SQUARE_1, TOPREAL9, TOPALG_1, JORDAN2C, TOPS_2,
      BORSUK_3, FUNCT_1, TOPMETR, JGRAPH_2, BORSUK_4, RELAT_1, FUNCOP_1,
      JORDAN16, BORSUK_6, BORSUK_2, TOPALG_2, BINOP_1, TARSKI, ZFMISC_1,
      FUNCT_4, TSEP_1, XBOOLE_1, COMPLEX2, COMPTS_1, SETFAM_1, XBOOLE_0,
      TAXONOM2, REAL_1, FINSEQ_3, NAT_1, RCOMP_1, SEQM_3, FINSEQ_5, HEINE,
      TEX_2, DOMAIN_1, TOPS_1, TOPMETR2, GOBOARD9, CONNSP_1, TOPALG_3,
      TOPREALA, TOPREALB, FINSET_1, RCOMP_2, TOPS_3, CONNSP_3, CONNSP_2,
      TOPGRP_1, XREAL_1, CANTOR_1, FRECHET, TOPGEN_2, CARD_3, PBOOLE, RCOMP_3,
      XXREAL_0, CARD_1;
 schemes FUNCT_2, NAT_1, PBOOLE, BINOP_1;

begin :: Preliminaries
set o = |[0,0]|;
set I = the carrier of I[01];
set R = the carrier of R^1;

Lm1: 0 in INT by INT_1:def 1;

Lm2: 0 in I by BORSUK_1:86;
then

Lm3: {0} c= I by ZFMISC_1:37;

Lm4: 0 in {0} by TARSKI:def 1;

Lm5: the carrier of [:I[01],I[01]:] = [:I,I:] by BORSUK_1:def 5;
reconsider j0 = 0, j1 = 1 as Point of I[01] by BORSUK_1:def 17,def 18;

Lm6: [#]I[01] = I;

Lm7: I[01] | [#]I[01] = I[01] by TSEP_1:3;

Lm8: 1-0 <= 1;

Lm9: 3/2-1/2 <= 1;

registration
  cluster INT.Group -> infinite;
  coherence
  proof
    thus the carrier of INT.Group is infinite;
  end;
end;

reserve a, r, s for real number;

theorem Th1:
  r <= s & 0 < a implies
  for p being Point of Closed-Interval-MSpace(r,s) holds Ball(p,a) = [.r,s.] or
  Ball(p,a) = [.r,p+a.[ or Ball(p,a) = ].p-a,s.] or Ball(p,a) = ].p-a,p+a.[
proof
  set M = Closed-Interval-MSpace(r,s);
  assume that
A1: r <= s and
A2: 0 < a;
  let p be Point of M;
  set B = Ball(p,a);
A3: the carrier of M = [.r,s.] by A1,TOPMETR:14;
  reconsider p1 = p as Point of RealSpace by TOPMETR:12;
  set B1 = Ball(p1,a);
A4: B = B1 /\ the carrier of M by TOPMETR:13;
  a is Real & p1 is Real by XREAL_0:def 1;
  then
A5: B1 = ].p1-a, p1+a.[ by A2,FRECHET:7;
  per cases;
  suppose that
A6: p1+a <= s and
A7: p1-a < r;
    B = [.r,p1+a.[
    proof
      thus B c= [.r,p1+a.[
      proof
        let b be set;
        assume
A8:     b in B;
        then reconsider b as Element of B;
        b in B1 by A4,A8,XBOOLE_0:def 3;
        then
A9:     b < p1+a by A5,RCOMP_1:47;
        r <= b by A3,A8,RCOMP_1:48;
        hence thesis by A9,RCOMP_2:3;
      end;
      let b be set;
      assume
A10:  b in [.r,p1+a.[;
      then reconsider b as Real;
A11:  r <= b by A10,RCOMP_2:3;
A12:  b < p1+a by A10,RCOMP_2:3;
      then b <= s by A6,XXREAL_0:2;
      then
A13:  b in [.r,s.] by A11,RCOMP_1:48;
      p1-a < b by A7,A11,XXREAL_0:2;
      then b in B1 by A5,A12,RCOMP_1:47;
      hence thesis by A3,A4,A13,XBOOLE_0:def 3;
    end;
    hence thesis;
  end;
  suppose that
A14: p1+a <= s and
A15: p1-a >= r;
    B = ].p1-a,p1+a.[
    proof
      thus B c= ].p1-a,p1+a.[ by A4,A5,XBOOLE_1:17;
      let b be set;
      assume
A16:  b in ].p1-a,p1+a.[;
      then reconsider b as Real;
      p1-a <= b by A16,RCOMP_1:47;
      then
A17:  r <= b by A15,XXREAL_0:2;
      b < p1+a by A16,RCOMP_1:47;
      then b <= s by A14,XXREAL_0:2;
      then b in [.r,s.] by A17,RCOMP_1:48;
      hence thesis by A3,A4,A5,A16,XBOOLE_0:def 3;
    end;
    hence thesis;
  end;
  suppose that
A18: p1+a > s and
A19: p1-a < r;
    B = [.r,s.]
    proof
      thus B c= [.r,s.] by A3;
      let b be set;
      assume
A20:  b in [.r,s.];
      then reconsider b as Real;
A21:  r <= b by A20,RCOMP_1:48;
      b <= s by A20,RCOMP_1:48;
      then
A22:  b < p1+a by A18,XXREAL_0:2;
      p1-a < b by A19,A21,XXREAL_0:2;
      then b in B1 by A5,A22,RCOMP_1:47;
      hence thesis by A3,A4,A20,XBOOLE_0:def 3;
    end;
    hence thesis;
  end;
  suppose that
A23: p1+a > s and
A24: p1-a >= r;
    B = ].p1-a,s.]
    proof
      thus B c= ].p1-a,s.]
      proof
        let b be set;
        assume
A25:    b in B;
        then reconsider b as Element of B;
        b in B1 by A4,A25,XBOOLE_0:def 3;
        then
A26:    p1-a < b by A5,RCOMP_1:47;
        b <= s by A3,A25,RCOMP_1:48;
        hence thesis by A26,RCOMP_2:4;
      end;
      let b be set;
      assume
A27:  b in ].p1-a,s.];
      then reconsider b as Real;
A28:  b <= s by A27,RCOMP_2:4;
A29:  p1-a < b by A27,RCOMP_2:4;
      then r <= b by A24,XXREAL_0:2;
      then
A30:  b in [.r,s.] by A28,RCOMP_1:48;
      b < p1+a by A23,A28,XXREAL_0:2;
      then b in B1 by A5,A29,RCOMP_1:47;
      hence thesis by A3,A4,A30,XBOOLE_0:def 3;
    end;
    hence thesis;
  end;
end;

theorem Th2:
  r <= s implies ex B being Basis of Closed-Interval-TSpace(r,s) st
  (ex f being ManySortedSet of Closed-Interval-TSpace(r,s) st
  for y being Point of Closed-Interval-MSpace(r,s) holds
  f.y = {Ball(y,1/n) where n is Element of NAT: n <> 0} & B = Union f) &
  for X being Subset of Closed-Interval-TSpace(r,s) st X in B holds
  X is connected
proof
  set L = Closed-Interval-TSpace(r,s), M = Closed-Interval-MSpace(r,s);
A1: L = TopSpaceMetr(M) by TOPMETR:def 8;
  assume
A2: r <= s;
  defpred P[set,set] means ex x being Point of L, y being Point of M,
  B being Basis of x st $1 = x & x = y & $2 = B &
  B = {Ball(y,1/n) where n is Element of NAT: n <> 0};
A3: for i being set st i in the carrier of L ex j being set st P[i,j]
  proof
    let i be set;
    assume i in the carrier of L;
    then reconsider i as Point of L;
    reconsider m = i as Point of M by A1,TOPMETR:16;
    consider B being Basis of i such that
A4: B = {Ball(m,1/n) where n is Element of NAT: n <> 0} and B is countable &
    ex f being Function of NAT,B st for n being set st n in NAT holds
    ex n' being Element of NAT st n=n' & f.n = Ball(m,1/(n'+1))
    by A1,FRECHET:11;
    take B, i, m, B;
    thus thesis by A4;
  end;
  consider f being ManySortedSet of the carrier of L such that
A5: for i being set st i in the carrier of L holds P[i,f.i]
  from PBOOLE:sch 3(A3);
  for x being Element of L holds f.x is Basis of x
  proof
    let x be Element of L;
    P[x,f.x] by A5;
    hence thesis;
  end;
  then reconsider B = Union f as Basis of L by TOPGEN_2:2;
  take B;
  hereby
    take f;
    let x be Point of M;
    the carrier of M = [.r,s.] by A2,TOPMETR:14
      .= the carrier of L by A2,TOPMETR:25;
    then P[x,f.x] by A5;
    hence f.x = {Ball(x,1/n) where n is Element of NAT: n <> 0} & B = Union f;
  end;
  let X be Subset of L;
  assume X in B;
  then X in union rng f by CARD_3:def 4;
  then consider Z being set such that
A6: X in Z and
A7: Z in rng f by TARSKI:def 4;
  consider x being set such that
A8: x in dom f and
A9: f.x = Z by A7,FUNCT_1:def 5;
  dom f = the carrier of L by PBOOLE:def 3;
  then consider x1 being Point of L, y being Point of M,
  B1 being Basis of x1 such that
A10: x = x1 & x1 = y & f.x = B1 &
  B1 = {Ball(y,1/n) where n is Element of NAT: n <> 0} by A5,A8;
  consider n being Element of NAT such that
A11: X = Ball(y,1/n) and
A12: n <> 0 by A6,A9,A10;
  reconsider X1 = X as Subset of R^1 by PRE_TOPC:39;
  0 < n by A12;
  then 1/n > 0 by XREAL_1:141;
  then Ball(y,1/n) = [.r,s.] or Ball(y,1/n) = [.r,y+1/n.[ or
  Ball(y,1/n) = ].y-1/n,s.] or Ball(y,1/n) = ].y-1/n,y+1/n.[ by A2,Th1;
  then X1 is connected by A11,RCOMP_3:52;
  hence thesis by CONNSP_1:24;
end;

theorem Th3:
  for T being TopStruct, A being Subset of T, t be Point of T st t in A
  holds Component_of(t,A) c= A
proof
  let T be TopStruct, A be Subset of T, t be Point of T;
  assume
A1: t in A;
  then Down(t,A) = t by CONNSP_3:def 3;
  then
A2: Component_of(t,A) = Component_of Down(t,A) by A1,CONNSP_3:def 7;
  the carrier of (T|A) = A by PRE_TOPC:29;
  hence thesis by A2;
end;

registration
  let T be TopSpace, A be open Subset of T;
  cluster T|A -> open;
  coherence
  proof
    let X being Subset of T;
    thus thesis by PRE_TOPC:29;
  end;
end;

theorem Th4:
  for T being TopSpace, S being SubSpace of T,
  A being Subset of T, B being Subset of S st A = B holds T|A = S|B
proof
  let T be TopSpace, S be SubSpace of T,
  A be Subset of T, B be Subset of S such that
A1: A = B;
A2: S|B is SubSpace of T by TSEP_1:7;
  [#](S|B) = A by A1,PRE_TOPC:def 10;
  hence T|A = S|B by A2,PRE_TOPC:def 10;
end;

theorem Th5:
  for S, T being TopSpace, A, B being Subset of T, C, D being Subset of S
  st the TopStruct of S = the TopStruct of T & A = C & B = D &
  A,B are_separated holds C,D are_separated
proof
  let S, T be TopSpace;
  let A, B be Subset of T;
  let C, D be Subset of S;
  assume
A1: the TopStruct of S = the TopStruct of T;
  assume
A2: A = C & B = D;
  then
A3: Cl A = Cl C & Cl B = Cl D by A1,TOPS_3:80;
  assume A,B are_separated;
  then Cl A misses B & A misses Cl B by CONNSP_1:def 1;
  hence C,D are_separated by A2,A3,CONNSP_1:def 1;
end;

theorem
  for S, T being TopSpace st the TopStruct of S = the TopStruct of T &
  S is connected holds T is connected
proof
  let S, T be TopSpace such that
A1: the TopStruct of S = the TopStruct of T and
A2: S is connected;
  let A, B be Subset of T such that
A3: [#]T = A \/ B and
A4: A,B are_separated;
  reconsider A1 = A, B1 = B as Subset of S by A1;
A5: [#]S = the carrier of S & [#]T = the carrier of T;
  A1,B1 are_separated by A1,A4,Th5;
  then A1 = {}S or B1 = {}S by A1,A2,A3,A5,CONNSP_1:def 2;
  hence A = {}T or B = {}T;
end;

theorem Th7:
  for S, T being TopSpace, A being Subset of S, B being Subset of T
  st the TopStruct of S = the TopStruct of T & A = B &
  A is connected holds B is connected
proof
  let S, T be TopSpace, A be Subset of S, B be Subset of T such that
A1: the TopStruct of S = the TopStruct of T and
A2: A = B and
A3: A is connected;
  now
    let P, Q be Subset of T such that
A4: B = P \/ Q and
A5: P,Q are_separated;
    reconsider P1 = P, Q1 = Q as Subset of S by A1;
    P1,Q1 are_separated by A1,A5,Th5;
    then P1 = {}S or Q1 = {}S by A2,A3,A4,CONNSP_1:16;
    hence P = {}T or Q = {}T;
  end;
  hence thesis by CONNSP_1:16;
end;

theorem Th8:
  for S, T being non empty TopSpace, s being Point of S, t being Point of T,
  A being a_neighborhood of s
  st the TopStruct of S = the TopStruct of T & s = t holds
  A is a_neighborhood of t
proof
  let S, T be non empty TopSpace, s be Point of S, t be Point of T,
  A be a_neighborhood of s such that
A1: the TopStruct of S = the TopStruct of T and
A2: s = t;
  reconsider B = A as Subset of T by A1;
A3: Int A = Int B by A1,TOPS_3:77;
  s in Int A by CONNSP_2:def 1;
  hence thesis by A2,A3,CONNSP_2:def 1;
end;

theorem
  for S, T being non empty TopSpace, A being Subset of S, B being Subset of T
  , N being a_neighborhood of A
  st the TopStruct of S = the TopStruct of T & A = B holds
  N is a_neighborhood of B
proof
  let S, T be non empty TopSpace, A be Subset of S, B be Subset of T,
  N be a_neighborhood of A such that
A1: the TopStruct of S = the TopStruct of T and
A2: A = B;
  reconsider M = N as Subset of T by A1;
A3: Int M = Int N by A1,TOPS_3:77;
  A c= Int N by CONNSP_2:def 2;
  hence thesis by A2,A3,CONNSP_2:def 2;
end;

theorem Th10:
  for S, T being non empty TopSpace, A, B being Subset of T,
  f being Function of S,T st f is_homeomorphism & A is_a_component_of B
  holds f"A is_a_component_of f"B
proof
  let S, T be non empty TopSpace, A, B being Subset of T,
  f be Function of S,T such that
A1: f is_homeomorphism;
  given X being Subset of T|B such that
A2: X = A and
A3: X is_a_component_of T|B;
  set Y = f"A;
A4: the carrier of (T|B) = B by PRE_TOPC:29;
  then f"X c= f"B by RELAT_1:178;
  then reconsider Y as Subset of S|(f"B) by A2,PRE_TOPC:29;
  take Y;
  thus Y = f"A;
  X is connected by A3,CONNSP_1:def 5;
  then A is connected by A2,CONNSP_1:24;
  then f"A is connected by A1,TOPS_2:76;
  hence Y is connected by CONNSP_1:24;
  let Z being Subset of S|(f"B) such that
A5: Z is connected and
A6: Y c= Z;
  reconsider Z1 = Z as Subset of S by PRE_TOPC:39;
A7: Z1 is connected by A5,CONNSP_1:24;
  the carrier of S|(f"B) = f"B by PRE_TOPC:29;
  then
A8: f.:Z c= f.:(f"B) by RELAT_1:156;
A9: rng f = [#]T by A1,TOPS_2:def 5
    .= the carrier of T;
  then reconsider R = f.:Z as Subset of T|B by A4,A8,FUNCT_1:147;
  f is continuous by A1,TOPS_2:def 5;
  then f.:Z1 is connected by A7,TOPS_2:75;
  then
A10: R is connected by CONNSP_1:24;
A11: f is one-to-one by A1,TOPS_2:def 5;
A12: X = f.:Y by A2,A9,FUNCT_1:147;
  f.:Y c= f.:Z by A6,RELAT_1:156;
  then
A13: X = R by A3,A10,A12,CONNSP_1:def 5;
  dom f = the carrier of S by FUNCT_2:def 1;
  then Z1 c= dom f;
  hence Y = Z by A2,A11,A13,FUNCT_1:164;
end;

begin :: Locally connectedness

theorem Th11:
  for T being non empty TopSpace, S being non empty SubSpace of T,
  A being non empty Subset of T, B being non empty Subset of S st A = B &
  A is locally_connected holds B is locally_connected
proof
  let T be non empty TopSpace, S be non empty SubSpace of T,
  A be non empty Subset of T, B be non empty Subset of S such that
A1: A = B and
A2: A is locally_connected;
  T|A = S|B by A1,Th4;
  hence S|B is locally_connected by A2,CONNSP_2:def 6;
end;

theorem Th12:
  for S, T being non empty TopSpace st
  the TopStruct of S = the TopStruct of T &
  S is locally_connected holds T is locally_connected
proof
  let S, T be non empty TopSpace such that
A1: the TopStruct of S = the TopStruct of T and
A2: S is locally_connected;
  let t be Point of T;
  let U be Subset of T;
  reconsider U1 = U as Subset of S by A1;
  reconsider s = t as Point of S by A1;
A3: S is_locally_connected_in s by A2,CONNSP_2:def 4;
  assume U is a_neighborhood of t;
  then U1 is a_neighborhood of s by A1,Th8;
  then consider V1 being Subset of S such that
A4: V1 is a_neighborhood of s and
A5: V1 is connected and
A6: V1 c= U1 by A3,CONNSP_2:def 3;
  reconsider V = V1 as Subset of T by A1;
  take V;
  thus V is a_neighborhood of t by A1,A4,Th8;
  thus V is connected by A1,A5,Th7;
  thus V c= U by A6;
end;

theorem Th13:
  for T being non empty TopSpace holds
  T is locally_connected iff [#]T is locally_connected
proof
  let T be non empty TopSpace;
  T is SubSpace of T by TSEP_1:2;
  then
A1: the TopStruct of T = the TopStruct of (T| [#]T) by PRE_TOPC:29,TSEP_1:5;
  hereby
    assume T is locally_connected;
    then T| [#]T is locally_connected by A1,Th12;
    hence [#]T is locally_connected by CONNSP_2:def 6;
  end;
  assume [#]T is locally_connected;
  then T| [#]T is locally_connected by CONNSP_2:def 6;
  hence T is locally_connected by A1,Th12;
end;

Lm10: for T being non empty TopSpace, S being non empty open SubSpace of T st
T is locally_connected holds the TopStruct of S is locally_connected
proof
  let T be non empty TopSpace;
  let S be non empty open SubSpace of T;
  assume
A1: T is locally_connected;
  reconsider A = [#]S as non empty Subset of T by TSEP_1:1;
  A is open by TSEP_1:def 1;
  then [#]S is locally_connected by A1,Th11,CONNSP_2:23;
  then S is locally_connected by Th13;
  then the TopStruct of S is locally_connected by Th12;
  then [#]the TopStruct of S is locally_connected by Th13;
  then (the TopStruct of S)| [#]the TopStruct of S is locally_connected
  by CONNSP_2:def 6;
  hence thesis by TSEP_1:3;
end;

theorem Th14:
  for T being non empty TopSpace, S being non empty open SubSpace of T st
  T is locally_connected holds S is locally_connected
proof
  let T be non empty TopSpace;
  let S be non empty open SubSpace of T;
  assume T is locally_connected;
  then the TopStruct of S is locally_connected by Lm10;
  hence S is locally_connected by Th12;
end;

theorem
  for S, T being non empty TopSpace st S,T are_homeomorphic &
  S is locally_connected holds T is locally_connected
proof
  let S, T be non empty TopSpace;
  given f being Function of S,T such that
A1: f is_homeomorphism;
  assume
A2: S is locally_connected;
  now
    let A be non empty Subset of T, B being Subset of T such that
A3: A is open and
A4: B is_a_component_of A;
    rng f = [#]T by A1,TOPS_2:def 5;
    then
A5: f"A is non empty by RELAT_1:174;
A6: f"A is open by A1,A3,TOPGRP_1:26;
    f"B is_a_component_of f"A by A1,A4,Th10;
    then f"B is open by A2,A5,A6,CONNSP_2:24;
    hence B is open by A1,TOPGRP_1:26;
  end;
  hence thesis by CONNSP_2:24;
end;

theorem Th16:
  for T being non empty TopSpace st
  ex B being Basis of T st for X being Subset of T st X in B holds
  X is connected holds T is locally_connected
proof
  let T be non empty TopSpace;
  given B being Basis of T such that
A1: for X being Subset of T st X in B holds X is connected;
  let x be Point of T;
  let U be Subset of T such that
A2: x in Int U;
A3: Int U in the topology of T by PRE_TOPC:def 5;
  the topology of T c= UniCl B by CANTOR_1:def 2;
  then consider Y being Subset-Family of T such that
A4: Y c= B and
A5: Int U = union Y by A3,CANTOR_1:def 1;
  consider V being set such that
A6: x in V and
A7: V in Y by A2,A5,TARSKI:def 4;
  reconsider V as Subset of T by A7;
  take V;
A8: B c= the topology of T by CANTOR_1:def 2;
  V in B by A4,A7;
  then V is open by A8,PRE_TOPC:def 5;
  hence x in Int V by A6,TOPS_1:55;
  thus V is connected by A1,A4,A7;
A9: V c= union Y by A7,ZFMISC_1:92;
  Int U c= U by TOPS_1:44;
  hence V c= U by A5,A9,XBOOLE_1:1;
end;

theorem Th17:
  r <= s implies Closed-Interval-TSpace(r,s) is locally_connected
proof
  assume r <= s;
  then ex B being Basis of Closed-Interval-TSpace(r,s) st
  (ex f being ManySortedSet of Closed-Interval-TSpace(r,s) st
  for y being Point of Closed-Interval-MSpace(r,s) holds
  f.y = {Ball(y,1/n) where n is Element of NAT: n <> 0} & B = Union f) &
  for X being Subset of Closed-Interval-TSpace(r,s) st X in B holds
  X is connected by Th2;
  hence thesis by Th16;
end;

registration
  cluster I[01] -> locally_connected;
  coherence by Th17,TOPMETR:27;
end;

registration
  let A be non empty open Subset of I[01];
  cluster I[01] | A -> locally_connected;
  coherence by Th14;
end;

begin :: Some useful functions

definition
  let r be real number;
  func ExtendInt(r) -> Function of I[01], R^1 means
  :Def1:
  for x being Point of I[01] holds it.x = r*x;
  existence
  proof
    defpred P[real number,set] means $2 = r*$1;
A1: for x being Element of I[01] ex y being Element of R st P[x,y]
    proof
      let x be Element of I[01];
      take r*x;
      thus thesis by TOPMETR:24,XREAL_0:def 1;
    end;
    ex f being Function of I, R st
    for x being Element of I[01] holds P[x,f.x] from FUNCT_2:sch 3(A1);
    hence thesis;
  end;
  uniqueness
  proof
    let f, g be Function of I[01], R^1 such that
A2: for x being Point of I[01] holds f.x = r*x and
A3: for x being Point of I[01] holds g.x = r*x;
    for x being Point of I[01] holds f.x = g.x
    proof
      let x be Point of I[01];
      thus f.x = r*x by A2
        .= g.x by A3;
    end;
    hence f = g by FUNCT_2:113;
  end;
end;

registration
  let r be real number;
  cluster ExtendInt(r) -> continuous;
  coherence
  proof
    reconsider f1 = id I[01] as Function of I[01],R^1
    by BORSUK_1:83,FUNCT_2:9,TOPMETR:24;
    f1 is continuous by TOPMETR:7;
    then consider g being Function of I[01],R^1 such that
A1: for p being Point of I[01], r1 being real number st f1.p=r1 holds g.p=r*r1
    and
A2: g is continuous by JGRAPH_2:33;
    for x being Point of I[01] holds g.x = (ExtendInt(r)).x
    proof
      let x be Point of I[01];
      thus g.x = r*f1.x by A1
        .= r*x by FUNCT_1:35
        .= (ExtendInt(r)).x by Def1;
    end;
    hence thesis by A2,FUNCT_2:113;
  end;
end;

definition
  let r be real number;
  redefine func ExtendInt(r) -> Path of R^1(0),R^1(r);
  coherence
  proof
    thus ExtendInt(r) is continuous;
    thus (ExtendInt(r)).0 = r * 0 by Def1,BORSUK_1:def 17
      .= R^1(0) by TOPREALB:def 2;
    thus (ExtendInt(r)).1 = r * 1 by Def1,BORSUK_1:def 18
      .= R^1(r) by TOPREALB:def 2;
  end;
end;

definition
  let S, T, Y be non empty TopSpace, H be Function of [:S,T:],Y,
  t be Point of T;
  func Prj1(t,H) -> Function of S,Y means
  :Def2:
  for s being Point of S holds it.s = H.(s,t);
  existence
  proof
    deffunc F(Point of S) = H. [$1,t];
    consider f being Function of the carrier of S, the carrier of Y such that
A1: for x being Element of S holds f.x = F(x) from FUNCT_2:sch 4;
    take f;
    let x be Point of S;
    thus f.x = H.(x,t) by A1;
  end;
  uniqueness
  proof
    let f, g be Function of S,Y such that
A2: for s being Point of S holds f.s = H.(s,t) and
A3: for s being Point of S holds g.s = H.(s,t);
    now
      let s be Point of S;
      thus f.s = H.(s,t) by A2
        .= g.s by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let S, T, Y be non empty TopSpace, H be Function of [:S,T:],Y,
  s be Point of S;
  func Prj2(s,H) -> Function of T,Y means
  :Def3:
  for t being Point of T holds it.t = H.(s,t);
  existence
  proof
    deffunc F(Point of T) = H. [s,$1];
    consider f being Function of the carrier of T, the carrier of Y such that
A1: for x being Element of T holds f.x = F(x) from FUNCT_2:sch 4;
    take f;
    let x be Point of T;
    thus f.x = H.(s,x) by A1;
  end;
  uniqueness
  proof
    let f, g be Function of T,Y such that
A2: for t being Point of T holds f.t = H.(s,t) and
A3: for t being Point of T holds g.t = H.(s,t);
    now
      let t be Point of T;
      thus f.t = H.(s,t) by A2
        .= g.t by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

registration
  let S, T, Y be non empty TopSpace, H be continuous Function of [:S,T:],Y,
  t be Point of T;
  cluster Prj1(t,H) -> continuous;
  coherence
  proof
    for p being Point of S, V being Subset of Y
    st Prj1(t,H).p in V & V is open holds
    ex W being Subset of S st p in W & W is open & Prj1(t,H).:W c= V
    proof
      let p be Point of S, V be Subset of Y such that
A1:   Prj1(t,H).p in V and
A2:   V is open;
      Prj1(t,H).p = H.(p,t) by Def2;
      then consider W being Subset of [:S,T:] such that
A3:   [p,t] in W and
A4:   W is open and
A5:   H.:W c= V by A1,A2,JGRAPH_2:20;
      consider A being Subset-Family of [:S,T:] such that
A6:   W = union A and
A7:   for
      e being set st e in A ex X1 being Subset of S, Y1 being Subset of T st
      e = [:X1,Y1:] & X1 is open & Y1 is open by A4,BORSUK_1:45;
      consider e being set such that
A8:   [p,t] in e and
A9:   e in A by A3,A6,TARSKI:def 4;
      consider X1 being Subset of S, Y1 being Subset of T such that
A10:  e = [:X1,Y1:] and
A11:  X1 is open and Y1 is open by A7,A9;
      take X1;
      thus p in X1 by A8,A10,ZFMISC_1:106;
      thus X1 is open by A11;
      let x be set;
      assume x in Prj1(t,H).:X1;
      then consider c being Point of S such that
A12:  c in X1 and
A13:  x = Prj1(t,H).c by FUNCT_2:116;
A14:  Prj1(t,H).c = H.(c,t) by Def2
        .= H. [c,t];
      t in Y1 by A8,A10,ZFMISC_1:106;
      then [c,t] in [:X1,Y1:] by A12,ZFMISC_1:106;
      then [c,t] in W by A6,A9,A10,TARSKI:def 4;
      then H. [c,t] in H.:W by FUNCT_2:43;
      hence x in V by A5,A13,A14;
    end;
    hence thesis by JGRAPH_2:20;
  end;
end;

registration
  let S, T, Y be non empty TopSpace, H be continuous Function of [:S,T:],Y,
  s be Point of S;
  cluster Prj2(s,H) -> continuous;
  coherence
  proof
    for p being Point of T, V being Subset of Y
    st Prj2(s,H).p in V & V is open holds
    ex W being Subset of T st p in W & W is open & Prj2(s,H).:W c= V
    proof
      let p be Point of T, V be Subset of Y such that
A1:   Prj2(s,H).p in V and
A2:   V is open;
      Prj2(s,H).p = H.(s,p) by Def3;
      then consider W being Subset of [:S,T:] such that
A3:   [s,p] in W and
A4:   W is open and
A5:   H.:W c= V by A1,A2,JGRAPH_2:20;
      consider A being Subset-Family of [:S,T:] such that
A6:   W = union A and
A7:   for
      e being set st e in A ex X1 being Subset of S, Y1 being Subset of T st
      e = [:X1,Y1:] & X1 is open & Y1 is open by A4,BORSUK_1:45;
      consider e being set such that
A8:   [s,p] in e and
A9:   e in A by A3,A6,TARSKI:def 4;
      consider X1 being Subset of S, Y1 being Subset of T such that
A10:  e = [:X1,Y1:] and X1 is open and
A11:  Y1 is open by A7,A9;
      take Y1;
      thus p in Y1 by A8,A10,ZFMISC_1:106;
      thus Y1 is open by A11;
      let x be set;
      assume x in Prj2(s,H).:Y1;
      then consider c being Point of T such that
A12:  c in Y1 and
A13:  x = Prj2(s,H).c by FUNCT_2:116;
A14:  Prj2(s,H).c = H.(s,c) by Def3
        .= H. [s,c];
      s in X1 by A8,A10,ZFMISC_1:106;
      then [s,c] in [:X1,Y1:] by A12,ZFMISC_1:106;
      then [s,c] in W by A6,A9,A10,TARSKI:def 4;
      then H. [s,c] in H.:W by FUNCT_2:43;
      hence x in V by A5,A13,A14;
    end;
    hence thesis by JGRAPH_2:20;
  end;
end;

theorem
  for T being non empty TopSpace, a, b being Point of T,
  P, Q being Path of a,b, H being Homotopy of P,Q,
  t being Point of I[01] st H is continuous holds Prj1(t,H) is continuous
proof
  let T be non empty TopSpace;
  let a, b be Point of T;
  let P, Q be Path of a,b;
  let H be Homotopy of P,Q;
  let t be Point of I[01];
  for H being continuous Function of [:I[01],I[01]:], T holds
  Prj1(t,H) is continuous;
  hence thesis;
end;

theorem
  for T being non empty TopSpace, a, b being Point of T,
  P, Q being Path of a,b, H being Homotopy of P,Q,
  s being Point of I[01] st H is continuous holds Prj2(s,H) is continuous
proof
  let T be non empty TopSpace;
  let a, b be Point of T;
  let P, Q be Path of a,b;
  let H be Homotopy of P,Q;
  let s be Point of I[01];
  for H being continuous Function of [:I[01],I[01]:], T holds
  Prj2(s,H) is continuous;
  hence thesis;
end;
set TUC = Tunit_circle(2);
set cS1 = the carrier of TUC;

Lm11: now
  TUC = Tcircle(0.REAL 2,1) by TOPREALB:def 7;
  hence cS1 = Sphere(|[0,0]|,1) by EUCLID:58,TOPREALB:9;
end;

Lm12: dom CircleMap = REAL by FUNCT_2:def 1,TOPMETR:24;

definition
  let r be real number;
  func cLoop(r) -> Function of I[01], Tunit_circle(2) means
  :Def4:
  for x being Point of I[01] holds it.x = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]|;
  existence
  proof
    defpred P[real number,set] means $2 = |[ cos(2*PI*r*$1), sin(2*PI*r*$1) ]|;
A1: for x being Element of I[01] ex y being Element of cS1 st P[x,y]
    proof
      let x be Element of I[01];
      set y = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]|;
      |. y - o .| = |. y .| by EUCLID:58,JORDAN2C:13
        .= sqrt((y`1)^2+(y`2)^2) by JGRAPH_1:47
        .= sqrt((cos(2*PI*r*x))^2+(y`2)^2) by EUCLID:56
        .= sqrt((cos(2*PI*r*x))^2+(sin(2*PI*r*x))^2) by EUCLID:56
        .= 1 by SIN_COS:32,SQUARE_1:83;
      then reconsider y as Element of cS1 by Lm11,TOPREAL9:9;
      take y;
      thus P[x,y];
    end;
    ex f being Function of I, cS1 st
    for x being Element of I[01] holds P[x,f.x] from FUNCT_2:sch 3(A1);
    hence thesis;
  end;
  uniqueness
  proof
    let f, g be Function of I[01], TUC such that
A2: for x being Point of I[01] holds f.x = |[cos(2*PI*r*x),sin(2*PI*r*x)]| and
A3: for x being Point of I[01] holds g.x = |[cos(2*PI*r*x),sin(2*PI*r*x)]|;
    for x being Point of I[01] holds f.x = g.x
    proof
      let x be Point of I[01];
      thus f.x = |[cos(2*PI*r*x),sin(2*PI*r*x)]| by A2
        .= g.x by A3;
    end;
    hence f = g by FUNCT_2:113;
  end;
end;

theorem Th20:
  cLoop(r) = CircleMap * ExtendInt(r)
proof
  for x being Point of I[01] holds (cLoop(r)).x = (CircleMap * ExtendInt(r)).
  x
  proof
    let x be Point of I[01];
A1: (ExtendInt(r)).x = r*x by Def1;
    thus (cLoop(r)).x = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]| by Def4
      .= |[ cos(2*PI*(ExtendInt(r)).x), sin(2*PI*(ExtendInt(r)).x) ]| by A1
      .= CircleMap.((ExtendInt(r)).x) by TOPREALB:def 11
      .= (CircleMap * ExtendInt(r)).x by FUNCT_2:21;
  end;
  hence thesis by FUNCT_2:113;
end;

definition
  let n be Integer;
  redefine func cLoop(n) -> Loop of c[10];
  coherence
  proof
    set f = cLoop(n);
    f = CircleMap * ExtendInt(n) by Th20;
    hence f is continuous by TOPS_2:58;
    thus f.0 = |[ cos(2*PI*n*j0), sin(2*PI*n*j0) ]| by Def4
      .= c[10] by SIN_COS:34,TOPREALB:def 8;
    thus f.1 = |[ cos(2*PI*n*j1), sin(2*PI*n*j1) ]| by Def4
      .= |[ cos(0), sin(2*PI*n+0) ]| by COMPLEX2:10
      .= c[10] by COMPLEX2:9,SIN_COS:34,TOPREALB:def 8;
  end;
end;

begin :: Main theorems

Lm13: ex F being Subset-Family of Tunit_circle(2) st
F is Cover of Tunit_circle(2) & F is open &
for U being Subset of Tunit_circle(2) st U in F
ex D being mutually-disjoint open Subset-Family of R^1
st union D = (CircleMap)"U & for d being Subset of R^1 st d in D
for f being Function of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
holds f is_homeomorphism
proof
  consider F being Subset-Family of Tunit_circle(2) such that
A1: F = { (CircleMap).:].0,1.[, (CircleMap).:].1/2,3/2.[ } and
A2: F is Cover of Tunit_circle(2) & F is open and
A3: for U being Subset of Tunit_circle(2) holds
  ( U = (CircleMap).:].0,1.[ implies union IntIntervals(0,1) = (CircleMap)"U &
  for d being Subset of R^1 st d in IntIntervals(0,1)
  for f being Function of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
  holds f is_homeomorphism ) & ( U = (CircleMap).:].1/2,3/2.[ implies
  union IntIntervals(1/2,3/2) = (CircleMap)"U &
  for d being Subset of R^1 st d in IntIntervals(1/2,3/2)
  for f being Function of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
  holds f is_homeomorphism ) by TOPREALB:49;
  take F;
  thus F is Cover of Tunit_circle(2) & F is open by A2;
  let U be Subset of Tunit_circle(2);
  assume
A4: U in F;
  per cases by A1,A4,TARSKI:def 2;
  suppose
A5: U = (CircleMap).:].0,1.[;
    reconsider D = IntIntervals(0,1) as
    mutually-disjoint open Subset-Family of R^1 by Lm8,TOPREALB:4;
    take D;
    thus thesis by A3,A5;
  end;
  suppose
A6: U = (CircleMap).:].1/2,3/2.[;
    reconsider D = IntIntervals(1/2,3/2) as
    mutually-disjoint open Subset-Family of R^1 by Lm9,TOPREALB:4;
    take D;
    thus thesis by A3,A6;
  end;
end;

Lm14: [#]Sspace(0[01]) = {0} by TEX_2:def 4;

Lm15: for F being Subset-Family of Closed-Interval-TSpace(r,s),
C being IntervalCover of F, G being IntervalCoverPts of C holds
F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
implies G is non empty
proof
  let F be Subset-Family of Closed-Interval-TSpace(r,s),
  C be IntervalCover of F, G be IntervalCoverPts of C;
  assume F is Cover of Closed-Interval-TSpace(r,s) & F is open connected &
  r <= s;
  then len G = len C + 1 by RCOMP_3:def 3;
  hence thesis by CARD_1:47;
end;

theorem Th21:
  for UL being Subset-Family of Tunit_circle(2) st
  UL is Cover of Tunit_circle(2) & UL is open
  for Y being non empty TopSpace,
  F being continuous Function of [:Y,I[01]:], Tunit_circle(2),
  y being Point of Y ex T being non empty FinSequence of REAL st
  T.1 = 0 & T.len T = 1 & T is increasing &
  ex N being open Subset of Y st y in N &
  for i being natural number st i in dom T & i+1 in dom T
  ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
  F.:[:N,[.T.i,T.(i+1).]:] c= Ui
proof
  let UL be Subset-Family of TUC such that
A1: UL is Cover of TUC and
A2: UL is open;
  let Y be non empty TopSpace, F be continuous Function of [:Y,I[01]:], TUC,
  y be Point of Y;
  set L = Closed-Interval-TSpace(0,1);
  set C = the carrier of Y;
A3: [#]TUC = union UL by A1,SETFAM_1:60;
A4: for i being Point of I[01]
  ex U being non empty open Subset of TUC st F. [y,i] in U & U in UL
  proof
    let i be Point of I[01];
    consider U being set such that
A5: F. [y,i] in U & U in UL by A3,TARSKI:def 4;
    reconsider U as non empty open Subset of TUC by A2,A5,TOPS_2:def 1;
    take U;
    thus thesis by A5;
  end;
  then ex U being non empty open Subset of TUC st F. [y,0[01]] in U & U in UL;
  then reconsider UL1 = UL as non empty set;
  defpred I0[set,set] means
  ex V being open Subset of TUC st V in UL1 & F. [y,$1] in V & $2 = V;
A6: for i being Element of I ex z being Element of UL1 st I0[i,z]
  proof
    let i be Element of I;
    ex U being non empty open Subset of TUC st F. [y,i] in U & U in UL by A4;
    hence ex z being Element of UL1 st I0[i,z];
  end;
  consider I0 being Function of I, UL1 such that
A7: for i being Element of I holds I0[i,I0.i] from FUNCT_2:sch 3(A6);
  defpred I1[set,set] means
  ex M being open Subset of Y, O being open connected Subset of I[01] st
  y in M & $1 in O & F.:[:M,O:] c= I0.$1 & $2 = [:M,O:];
A8: for i being Element of I ex z being Subset of [:C,I:] st I1[i,z]
  proof
    let i be Element of I;
    consider V being open Subset of TUC such that V in UL1 and
A9: F. [y,i] in V and
A10: I0.i = V by A7;
    consider W being Subset of [:Y,I[01]:] such that
A11: [y,i] in W and
A12: W is open and
A13: F.:W c= V by A9,JGRAPH_2:20;
    consider Q being Subset-Family of [:Y,I[01]:] such that
A14: W = union Q and
A15: for e being set st e in Q
    ex A being Subset of Y, B being Subset of I[01] st
    e = [:A,B:] & A is open & B is open by A12,BORSUK_1:45;
    consider Z being set such that
A16: [y,i] in Z and
A17: Z in Q by A11,A14,TARSKI:def 4;
    consider A being Subset of Y, B being Subset of I[01] such that
A18: Z = [:A,B:] and
A19: A is open and
A20: B is open by A15,A17;
    reconsider A as open Subset of Y by A19;
A21: i in B by A16,A18,ZFMISC_1:106;
    reconsider B as non empty open Subset of I[01] by A16,A18,A20,ZFMISC_1:106;
    reconsider i1 = i as Point of I[01]|B by A21,PRE_TOPC:29;
A22: Component_of(i,B) = Component_of i1 by A21,CONNSP_3:def 7;
    Component_of i1 is_a_component_of I[01]|B by CONNSP_1:43;
    then Component_of i1 is open by CONNSP_2:21;
    then reconsider D = Component_of(i,B) as open connected Subset of I[01]
    by A21,A22,CONNSP_3:35,TSEP_1:17;
    reconsider z = [:A,D:] as Subset of [:C,I:] by BORSUK_1:def 5;
    take z, A, D;
    thus y in A by A16,A18,ZFMISC_1:106;
    thus i in D by A21,CONNSP_3:26;
    D c= B by A21,Th3;
    then
A23: z c= [:A,B:] by ZFMISC_1:118;
    [:A,B:] c= W by A14,A17,A18,ZFMISC_1:92;
    then z c= W by A23,XBOOLE_1:1;
    then F.:z c= F.:W by RELAT_1:156;
    hence F.:[:A,D:] c= I0.i by A10,A13,XBOOLE_1:1;
    thus z = [:A,D:];
  end;
  consider I1 being Function of I, bool [:C,I:] such that
A24: for i being Element of I holds I1[i,I1.i] from FUNCT_2:sch 3(A8);
A25: dom I1 = I by FUNCT_2:def 1;
  reconsider C1 = rng I1 as Subset-Family of [:Y,I[01]:] by BORSUK_1:def 5;
  [:{y},[#]I[01]:] c= union C1
  proof
    let a be set;
    assume a in [:{y},[#]I[01]:];
    then consider a1, a2 being set such that
A26: a1 in {y} and
A27: a2 in [#]I[01] and
A28: a = [a1,a2] by ZFMISC_1:def 2;
    reconsider a2 as Point of I[01] by A27;
    consider M being open Subset of Y,
    O being open connected Subset of I[01] such that
A29: y in M & a2 in O and F.:[:M,O:] c= I0.a2 and
A30: I1.a2 = [:M,O:] by A24;
A31: a1 = y by A26,TARSKI:def 1;
A32: [y,a2] in [:M,O:] by A29,ZFMISC_1:106;
    [:M,O:] in C1 by A25,A30,FUNCT_1:def 5;
    hence thesis by A28,A31,A32,TARSKI:def 4;
  end;
  then
A33: C1 is Cover of [:{y},[#]I[01]:] by SETFAM_1:def 12;
A34: C1 is open
  proof
    let P be Subset of [:Y,I[01]:];
    assume P in C1;
    then consider i being set such that
A35: i in dom I1 and
A36: I1.i = P by FUNCT_1:def 5;
    I1[i,I1.i] by A24,A35;
    hence P is open by A36,BORSUK_1:46;
  end;
  [:{y},[#]I[01]:] is compact by BORSUK_3:27;
  then consider G being Subset-Family of [:Y,I[01]:] such that
A37: G c= C1 and
A38: G is Cover of [:{y},[#]I[01]:] and
A39: G is finite by A33,A34,COMPTS_1:def 7;
A40: [:{y},[#]I[01]:] c= union G by A38,SETFAM_1:def 12;
A41: y in {y} by TARSKI:def 1;
  set ICOV = {O where O is open connected Subset of I[01]:
  ex M being open Subset of Y st [:M,O:] in G};
  ICOV c= bool I
  proof
    let a be set;
    assume a in ICOV;
    then ex O being open connected Subset of I[01] st a = O &
    ex M being open Subset of Y st [:M,O:] in G;
    hence thesis;
  end;
  then reconsider ICOV as Subset-Family of L by TOPMETR:27;
A42: [#]L c= union ICOV
  proof
    let a be set;
    assume a in [#]L;
    then reconsider a as Point of I[01] by TOPMETR:27;
    [y,a] in [:{y},[#]I[01]:] by A41,ZFMISC_1:def 2;
    then consider Z being set such that
A43: [y,a] in Z and
A44: Z in G by A40,TARSKI:def 4;
    consider i being set such that
A45: i in dom I1 and
A46: I1.i = Z by A37,A44,FUNCT_1:def 5;
    consider M being open Subset of Y,
    O being open connected Subset of I[01] such that
    y in M and i in O and F.:[:M,O:] c= I0.i and
A47: I1.i = [:M,O:] by A24,A45;
A48: a in O by A43,A46,A47,ZFMISC_1:106;
    O in ICOV by A44,A46,A47;
    hence thesis by A48,TARSKI:def 4;
  end;
A49: ICOV is Cover of L
  proof
    thus the carrier of L c= union ICOV by A42;
  end;
A50: ICOV is open
  proof
    let a be Subset of L;
    assume a in ICOV;
    then ex O being open connected Subset of I[01] st a = O &
    ex M being open Subset of Y st [:M,O:] in G;
    hence thesis by TOPMETR:27;
  end;
  consider NCOV being IntervalCover of ICOV;
  consider T being IntervalCoverPts of NCOV;
A51: ICOV is connected
  proof
    let X be Subset of L;
    assume X in ICOV;
    then ex O being open connected Subset of I[01] st
    X = O & ex M being open Subset of Y st [:M,O:] in G;
    hence thesis by TOPMETR:27;
  end;
  then reconsider T as non empty FinSequence of REAL by A49,A50,Lm15;
  take T;
  thus T.1 = 0 & T.len T = 1 by A49,A50,A51,RCOMP_3:def 3;
  thus T is increasing by A49,A50,A51,RCOMP_3:79;
  set NN = {M where M is open Subset of Y: y in M &
  ex O being open Subset of I[01] st [:M,O:] in G};
  NN c= bool C
  proof
    let a be set;
    assume a in NN;
    then ex M being open Subset of Y st a = M & y in M &
    ex O being open Subset of I[01] st [:M,O:] in G;
    hence thesis;
  end;
  then reconsider NN as Subset-Family of Y;
  [y,0[01]] in [:{y},[#]I[01]:] by A41,ZFMISC_1:def 2;
  then consider Z being set such that [y,0[01]] in Z and
A52: Z in G by A40,TARSKI:def 4;
  consider i being set such that
A53: i in dom I1 and
A54: I1.i = Z by A37,A52,FUNCT_1:def 5;
  consider M being open Subset of Y,
  O being open connected Subset of I[01] such that
A55: y in M and i in O & F.:[:M,O:] c= I0.i and
A56: I1.i = [:M,O:] by A24,A53;
A57: M in NN by A52,A54,A55,A56;
A58: NN is open
  proof
    let a be Subset of Y;
    assume a in NN;
    then ex M being open Subset of Y st a = M & y in M &
    ex O being open Subset of I[01] st [:M,O:] in G;
    hence thesis;
  end;
  consider p being Function such that
A59: rng p = G and
A60: dom p in omega by A39,FINSET_1:def 1;
  defpred F[set,set] means
  ex M being open Subset of Y, O being non empty open Subset of I[01] st
  y in M & p.$1 = [:M,O:] & $2 = M;
A61: for x being set st x in dom p ex y being set st y in NN & F[x,y]
  proof
    let x be set;
    assume x in dom p;
    then
A62: p.x in rng p by FUNCT_1:def 5;
    then consider i being set such that
A63: i in dom I1 and
A64: I1.i = p.x by A37,A59,FUNCT_1:def 5;
    consider M being open Subset of Y, O being open connected Subset of I[01]
    such that
A65: y in M & i in O & F.:[:M,O:] c= I0.i & I1.i = [:M,O:] by A24,A63;
    take M;
    thus thesis by A59,A62,A64,A65;
  end;
  consider p1 being Function of dom p, NN such that
A66: for x being set st x in dom p holds F[x,p1.x] from FUNCT_2:sch 1(A61);
A67: dom p1 = dom p by A57,FUNCT_2:def 1;
  rng p1 = NN
  proof
    thus rng p1 c= NN;
    let a be set;
    assume a in NN;
    then consider M being open Subset of Y such that
A68: a = M and y in M and
A69: ex O being open Subset of I[01] st [:M,O:] in G;
    consider O being open Subset of I[01] such that
A70: [:M,O:] in G by A69;
    consider b being set such that
A71: b in dom p and
A72: p.b = [:M,O:] by A59,A70,FUNCT_1:def 5;
    consider M1 being open Subset of Y,
    O1 being non empty open Subset of I[01] such that
A73: y in M1 and
A74: p.b = [:M1,O1:] and
A75: p1.b = M1 by A66,A71;
    M1 = M by A72,A73,A74,ZFMISC_1:134;
    hence a in rng p1 by A67,A68,A71,A75,FUNCT_1:def 5;
  end;
  then NN is finite by A60,A67,FINSET_1:def 1;
  then reconsider N = meet NN as open Subset of Y by A58,TOPS_2:27;
  take N;
  now
    let Z be set;
    assume Z in NN;
    then ex M being open Subset of Y st Z = M & y in M &
    ex O being open Subset of I[01] st [:M,O:] in G;
    hence y in Z;
  end;
  hence y in N by A57,SETFAM_1:def 1;
  let i be natural number;
  assume that
A76: i in dom T and
A77: i+1 in dom T;
A78: 1 <= i by A76,FINSEQ_3:27;
A79: i+1 <= len T by A77,FINSEQ_3:27;
  then
A80: i < len T by NAT_1:13;
A81: rng NCOV c= ICOV by A49,A50,A51,RCOMP_3:def 2;
  len T = len NCOV + 1 by A49,A50,A51,RCOMP_3:def 3;
  then i <= len NCOV by A79,XREAL_1:8;
  then i in dom NCOV by A78,FINSEQ_3:27;
  then NCOV.i in rng NCOV by FUNCT_1:def 5;
  then NCOV.i in ICOV by A81;
  then consider O being open connected Subset of I[01] such that
A82: NCOV.i = O and
A83: ex M being open Subset of Y st [:M,O:] in G;
  consider M being open Subset of Y such that
A84: [:M,O:] in G by A83;
  consider j being set such that
A85: j in dom I1 and
A86: I1.j = [:M,O:] by A37,A84,FUNCT_1:def 5;
  consider M1 being open Subset of Y, O1 being open connected Subset of I[01]
  such that
A87: y in M1 and
A88: j in O1 and
A89: F.:[:M1,O1:] c= I0.j and
A90: I1.j = [:M1,O1:] by A24,A85;
A91: M = M1 & O = O1 by A86,A87,A88,A90,ZFMISC_1:134;
  consider V being open Subset of TUC such that
A92: V in UL1 and
A93: F. [y,j] in V and
A94: I0.j = V by A7,A85;
  reconsider V as non empty open Subset of TUC by A93;
  take V;
  thus V in UL by A92;
A95: [.T.i,T.(i+1).] c= O by A49,A50,A51,A78,A80,A82,RCOMP_3:80;
  M in NN by A84,A87,A91;
  then N c= M by SETFAM_1:4;
  then [:N,[.T.i,T.(i+1).]:] c= [:M1,O1:] by A86,A90,A95,ZFMISC_1:119;
  then F.:[:N,[.T.i,T.(i+1).]:] c= F.:[:M1,O1:] by RELAT_1:156;
  hence F.:[:N,[.T.i,T.(i+1).]:] c= V by A89,A94,XBOOLE_1:1;
end;

theorem Th22:
  for Y being non empty TopSpace,
  F being Function of [:Y,I[01]:], Tunit_circle(2),
  Ft being Function of [:Y,Sspace(0[01]):], R^1 st
  F is continuous & Ft is continuous &
  F | [:the carrier of Y,{0}:] = CircleMap*Ft ex G being Function
  of [:Y,I[01]:], R^1 st G is continuous & F = CircleMap*G &
  G | [:the carrier of Y,{0}:] = Ft &
  for H being Function of [:Y,I[01]:], R^1 st
  H is continuous & F = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft
  holds G = H
proof
  let Y be non empty TopSpace, F be Function of [:Y,I[01]:], TUC,
  Ft be Function of [:Y,Sspace(0[01]):], R^1 such that
A1: F is continuous and
A2: Ft is continuous and
A3: F | [:the carrier of Y,{0}:] = CircleMap*Ft;
A4: the carrier of [:Y,I[01]:] = [:the carrier of Y,I:] by BORSUK_1:def 5;
A5: the carrier of [:Y,Sspace(0[01]):] =
  [:the carrier of Y, the carrier of Sspace(0[01]):] by BORSUK_1:def 5;
  then
A6: dom Ft = [:the carrier of Y,{0}:] by Lm14,FUNCT_2:def 1;
  consider UL being Subset-Family of TUC such that
A7: UL is Cover of TUC and
A8: UL is open and
A9: for U being Subset of TUC st U in UL
  ex D being mutually-disjoint open Subset-Family of R^1
  st union D = (CircleMap)"U & for d being Subset of R^1 st d in D
  for f being Function of R^1 | d, TUC | U st f = CircleMap|d
  holds f is_homeomorphism by Lm13;
A10: dom F = the carrier of [:Y,I[01]:] by FUNCT_2:def 1
    .= [:the carrier of Y,I:] by BORSUK_1:def 5;
  defpred A[set,set] means ex y being Point of Y, t being Point of I[01],
  N being non empty open Subset of Y, Fn being Function
  of [:Y|N,I[01]:], R^1 st
  $1 = [y,t] & $2 = Fn.$1 & y in N & Fn is continuous &
  F | [:N,I:] = CircleMap*Fn & Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] &
  for H being Function of [:Y|N,I[01]:], R^1 st
  H is continuous & F | [:N,I:] = CircleMap*H &
  H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H;
A11: for x being Point of [:Y,I[01]:] ex z being Point of R^1 st A[x,z]
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y, t being Point of I[01] such that
A12: x = [y,t] by BORSUK_1:50;
    consider TT being non empty FinSequence of REAL such that
A13: TT.1 = 0 and
A14: TT.len TT = 1 and
A15: TT is increasing and
A16: ex N being open Subset of Y st y in N &
    for i being natural number st i in dom TT & i+1 in dom TT
    ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
    F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A1,A7,A8,Th21;
    consider N being open Subset of Y such that
A17: y in N and
A18: for i being natural number st i in dom TT & i+1 in dom TT
    ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
    F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A16;
    reconsider N as non empty open Subset of Y by A17;
A19: 1 in dom TT by FINSEQ_5:6;
A20: len TT in dom TT by FINSEQ_5:6;
A21: now
      let i be Element of NAT such that
A22:  i in dom TT;
      1 <= i by A22,FINSEQ_3:27;
      then 1 = i or 1 < i by REAL_1:def 5;
      hence
A23:  0 <= TT.i by A13,A15,A19,A22,SEQM_3:def 1;
      assume
A24:  i+1 in dom TT;
A25:  i+0 < i+1 by XREAL_1:10;
      hence
A26:  TT.i < TT.(i+1) by A15,A22,A24,SEQM_3:def 1;
      i+1 <= len TT by A24,FINSEQ_3:27;
      then i+1 = len TT or i+1 < len TT by REAL_1:def 5;
      hence TT.(i+1) <= 1 by A14,A15,A20,A24,SEQM_3:def 1;
      hence TT.i < 1 by A26,XXREAL_0:2;
      thus 0 < TT.(i+1) by A15,A22,A23,A24,A25,SEQM_3:def 1;
    end;
A27: now
      let i be Element of NAT such that
A28:  0 <= TT.i & TT.(i+1) <= 1;
      thus [.TT.i,TT.(i+1).] c= I
      proof
        let a be set;
        assume
A29:    a in [.TT.i,TT.(i+1).];
        then reconsider a as Real;
        TT.i <= a & a <= TT.(i+1) by A29,RCOMP_1:48;
        then 0 <= a & a <= 1 by A28,XXREAL_0:2;
        hence thesis by BORSUK_1:86;
      end;
    end;
    defpred N[Element of NAT] means $1 in dom TT implies
    ex N2 being non empty open Subset of Y, S being non empty Subset of I[01],
    Fn being Function of [:Y|N2,I[01]|S:], R^1 st
    S = [.0,TT.$1.] & y in N2 & N2 c= N & Fn is continuous &
    F | [:N2,S:] = CircleMap*Fn &
    Fn | [:the carrier of Y,{0}:] = Ft | [:N2,I:];
A30: N[0] by FINSEQ_3:26;
A31: for i being Element of NAT st N[i] holds N[i+1]
    proof
      let i be Element of NAT;
      assume that
A32:  N[i] and
A33:  i+1 in dom TT;
      per cases by A33,TOPREALA:23;
      suppose
A34:    i = 0;
        set S = [.0,TT.(i+1).];
A35:    S = {0} by A13,A34,RCOMP_1:14;
        reconsider S as non empty Subset of I[01] by A13,A34,Lm3,RCOMP_1:14;
        take N2 = N;
A36:    the carrier of (Y|N2) = N2 by PRE_TOPC:29;
A37:    the carrier of (I[01]|S) = S by PRE_TOPC:29;
A38:    the carrier of [:Y|N2,I[01]|S:]

= [:the carrier of (Y|N2), the carrier of (I[01]|S):] by BORSUK_1:def 5;
        set Fn = Ft | [:N2,{0}:];
A39:    [:N2,S:] c= dom Ft by A6,A35,ZFMISC_1:119;
A40:    dom Fn = [:N2,S:] by A6,A35,RELAT_1:91,ZFMISC_1:119;
        rng Fn c= R;
        then reconsider Fn as Function of [:Y|N2,I[01]|S:], R^1
        by A36,A37,A38,A40,FUNCT_2:4;
        take S, Fn;
        thus S = [.0,TT.(i+1).];
        thus y in N2 by A17;
        thus N2 c= N;
        reconsider K0 = [:N2,S:] as non empty Subset of [:Y,Sspace(0[01]):]
        by A5,A35,Lm14,ZFMISC_1:119;
        reconsider S1 = S as non empty Subset of Sspace(0[01]) by A13,A34,Lm14,
        RCOMP_1:14;
        I[01]|S = Sspace(0[01]) by A35,TOPALG_3:6
          .= (Sspace(0[01]))|S1 by A35,Lm14,TSEP_1:3;
        then [:Y|N2,I[01]|S:] = [:Y,Sspace(0[01]):]|K0 by BORSUK_3:26;
        hence Fn is continuous by A2,A35,TOPMETR:10;
A41:    dom (F | [:N2,S:]) = [:N2,S:] by A10,RELAT_1:91,ZFMISC_1:119;
        rng Fn c= dom CircleMap by Lm12,TOPMETR:24;
        then
A42:    dom (CircleMap*Fn) = dom Fn by RELAT_1:46;
        for x being set st x in dom (F | [:N2,S:]) holds
        (F | [:N2,S:]).x = (CircleMap*Fn).x
        proof
          let x be set such that
A43:      x in dom (F | [:N2,S:]);
          thus (F | [:N2,S:]).x = F.x by A41,A43,FUNCT_1:72
            .= (CircleMap*Ft).x by A3,A5,A40,A41,A43,Lm14,FUNCT_1:72
            .= CircleMap.(Ft.x) by A39,A41,A43,FUNCT_1:23
            .= CircleMap.(Fn.x) by A35,A41,A43,FUNCT_1:72
            .= (CircleMap*Fn).x by A40,A41,A43,FUNCT_1:23;
        end;
        hence F | [:N2,S:] = CircleMap*Fn by A40,A41,A42,FUNCT_1:9;
A44:    dom (Fn | [:the carrier of Y,{0}:])
        = [:N2,S:] /\ [:the carrier of Y,{0}:] by A40,RELAT_1:90;
A45:    [:N2,S:] /\ [:the carrier of Y,{0}:] = [:N2,S:] by A35,ZFMISC_1:124;
A46:    dom (Ft | [:N2,I:]) = [:the carrier of Y,{0}:] /\ [:N2,I:]
        by A6,RELAT_1:90
          .= [:N2,S:] by A35,ZFMISC_1:124;
        for x being set st x in dom (Fn | [:the carrier of Y,{0}:]) holds
        (Fn | [:the carrier of Y,{0}:]).x = (Ft | [:N2,I:]).x
        proof
          let x be set such that
A47:      x in dom (Fn | [:the carrier of Y,{0}:]);
A48:      x in [:N2,{0}:] by A35,A44,A47,XBOOLE_0:def 3;
A49:      [:N2,{0}:] c= [:N2,I:] by Lm3,ZFMISC_1:118;
          x in [:the carrier of Y,{0}:] by A44,A47,XBOOLE_0:def 3;
          hence (Fn | [:the carrier of Y,{0}:]).x = Fn.x by FUNCT_1:72
            .= Ft.x by A48,FUNCT_1:72
            .= (Ft | [:N2,I:]).x by A48,A49,FUNCT_1:72;
        end;
        hence Fn | [:the carrier of Y,{0}:] = Ft | [:N2,I:]
        by A44,A45,A46,FUNCT_1:9;
      end;
      suppose
A50:    i in dom TT;
        then consider N2 being open Subset of Y,
        S being non empty Subset of I[01],
        Fn being Function of [:Y|N2,I[01]|S:], R^1 such that
A51:    S = [.0,TT.i.] and
A52:    y in N2 and
A53:    N2 c= N and
A54:    Fn is continuous and
A55:    F | [:N2,S:] = CircleMap*Fn and
A56:    Fn | [:the carrier of Y,{0}:] = Ft | [:N2,I:] by A32;
        reconsider N2 as non empty open Subset of Y by A52;
A57:    the carrier of I[01]|S = S by PRE_TOPC:29;
A58:    the carrier of [:Y|N2,I[01]|S:]

= [:the carrier of (Y|N2), the carrier of (I[01]|S):] by BORSUK_1:def 5;
        set SS = [.TT.i,TT.(i+1).];
        consider Ui being non empty Subset of TUC such that
A59:    Ui in UL and
A60:    F.:[:N,SS:] c= Ui by A18,A33,A50;
A61:    the carrier of (TUC | Ui) = Ui by PRE_TOPC:29;
        consider D being mutually-disjoint open Subset-Family of R^1 such that
A62:    union D = (CircleMap)"Ui and
A63:    for d being Subset of R^1 st d in D
        for f being Function of R^1 | d, TUC | Ui st f = CircleMap|d
        holds f is_homeomorphism by A9,A59;
A64:    the carrier of (Y|N2) = N2 by PRE_TOPC:29;
A65:    0 <= TT.i by A21,A50;
        then
A66:    TT.i in S by A51,RCOMP_1:15;
        then
A67:    [y,TT.i] in [:N2,S:] by A52,ZFMISC_1:106;
        then
A68:    F. [y,TT.i] = (CircleMap*Fn). [y,TT.i] by A55,FUNCT_1:72
          .= CircleMap.(Fn. [y,TT.i]) by A57,A58,A64,A67,FUNCT_2:21;
A69:    [y,TT.i] in dom F by A10,A66,ZFMISC_1:106;
A70:    TT.i < TT.(i+1) by A21,A33,A50;
        then TT.i in SS by RCOMP_1:15;
        then [y,TT.i] in [:N,SS:] by A17,ZFMISC_1:106;
        then F. [y,TT.i] in F.:[:N,SS:] by A69,FUNCT_2:43;
        then Fn. [y,TT.i] in (CircleMap)"Ui by A60,A68,FUNCT_2:46,TOPMETR:24;
        then consider Uit being set such that
A71:    Fn. [y,TT.i] in Uit and
A72:    Uit in D by A62,TARSKI:def 4;
        reconsider Uit as non empty Subset of R^1 by A71,A72;
A73:    dom Fn = the carrier of [:Y|N2,I[01]|S:] by FUNCT_2:def 1;
A74:    TT.i in {TT.i} by TARSKI:def 1;
        then
A75:    [y,TT.i] in [:N2,{TT.i}:] by A52,ZFMISC_1:def 2;
A76:    {TT.i} c= S by A66,ZFMISC_1:37;
        reconsider Ti = {TT.i} as non empty Subset of I[01] by A66,ZFMISC_1:37;
A77:    the carrier of I[01]|Ti = Ti by PRE_TOPC:29;
A78:    the carrier of [:Y|N2,I[01]|Ti:]
        = [:the carrier of Y|N2, the carrier of I[01]|Ti:] by BORSUK_1:def 5;
        set FnT = Fn | [:N2,Ti:];
A79:    dom FnT = [:N2,{TT.i}:] by A57,A58,A64,A73,A76,RELAT_1:91,ZFMISC_1:119;
        rng FnT c= REAL by TOPMETR:24;
        then reconsider FnT as Function of [:Y|N2,I[01]|Ti:],R^1
        by A64,A77,A78,A79,FUNCT_2:4;
        N2 c= N2;
        then reconsider N7 = N2 as non empty Subset of Y|N2 by PRE_TOPC:29;

reconsider Ti2 = Ti as non empty Subset of I[01]|S by A57,A66,ZFMISC_1:37;
A80:    Y|N2|N7 = Y|N2 & I[01]|S|Ti2 = I[01]|Ti by GOBOARD9:4;
        [:Y|N2|N7,I[01]|S|Ti2:] = [:Y|N2,I[01]|S:] | [:N7,Ti2:] by BORSUK_3:26;
        then
A81:    FnT is continuous by A54,A80,TOPMETR:10;
A82:    [#]R^1 <> {};
        Uit is open by A72,TOPS_2:def 1;
        then FnT"Uit is open by A81,A82,TOPS_2:55;
        then consider SF being Subset-Family of [:Y|N2,I[01]|Ti:] such that
A83:    FnT"Uit = union SF and
A84:    for e being set st e in SF
        ex X1 being Subset of Y|N2, Y1 being Subset of I[01]|Ti st
        e = [:X1,Y1:] & X1 is open & Y1 is open by BORSUK_1:45;
        FnT. [y,TT.i] in Uit by A71,A75,FUNCT_1:72;
        then [y,TT.i] in FnT"Uit by A75,A79,FUNCT_1:def 13;
        then consider N5 being set such that
A85:    [y,TT.i] in N5 and
A86:    N5 in SF by A83,TARSKI:def 4;
        consider X1 being Subset of Y|N2, Y1 being Subset of I[01]|Ti such that
A87:    N5 = [:X1,Y1:] and
A88:    X1 is open and Y1 is open by A84,A86;
        consider NY being Subset of Y such that
A89:    NY is open and
A90:    NY /\ [#](Y|N2) = X1 by A88,TOPS_2:32;
        consider y1, y2 being set such that
A91:    y1 in X1 and
A92:    y2 in Y1 and
A93:    [y,TT.i] = [y1,y2] by A85,A87,ZFMISC_1:def 2;
        set N1 = NY /\ N2;
A94:    Y1 = Ti
        proof
          thus Y1 c= Ti by A77;
          let a be set;
          assume a in Ti;
          then a = TT.i by TARSKI:def 1;
          hence thesis by A92,A93,ZFMISC_1:33;
        end;
        y = y1 by A93,ZFMISC_1:33;
        then
A95:    y in NY by A90,A91,XBOOLE_0:def 3;
        then reconsider N1 as non empty open Subset of Y
        by A52,A89,TOPS_1:38,XBOOLE_0:def 3;
A96:    N1 c= N2 by XBOOLE_1:17;
A97:    Fn.:[:N1,{TT.i}:] c= Uit
        proof
          let b be set;
          assume b in Fn.:[:N1,{TT.i}:];
          then consider a being Point of [:Y|N2,I[01]|S:] such that
A98:      a in [:N1,{TT.i}:] and
A99:      Fn.a = b by FUNCT_2:116;
          a in N5 by A87,A90,A94,A98,PRE_TOPC:def 10;
          then
A100:     a in union SF by A86,TARSKI:def 4;
          then a in dom FnT by A83,FUNCT_1:def 13;
          then Fn.a = FnT.a by FUNCT_1:70;
          hence b in Uit by A83,A99,A100,FUNCT_1:def 13;
        end;
A101:   the carrier of (Y|N1) = N1 by PRE_TOPC:29;
A102:   [:N2,SS:] c= [:N,SS:] by A53,ZFMISC_1:119;
        [:N1,SS:] c= [:N2,SS:] by A96,ZFMISC_1:119;
        then [:N1,SS:] c= [:N,SS:] by A102,XBOOLE_1:1;
        then
A103:   F.:[:N1,SS:] c= F.:[:N,SS:] by RELAT_1:156;
        set f = CircleMap|Uit;
A104:   the carrier of (R^1 | Uit) = Uit by PRE_TOPC:29;
A105:   dom f = Uit by Lm12,RELAT_1:91,TOPMETR:24;
        rng f c= Ui
        proof
          let b be set;
          assume b in rng f;
          then consider a being set such that
A106:     a in dom f and
A107:     f.a = b by FUNCT_1:def 5;
          a in union D by A72,A105,A106,TARSKI:def 4;
          then CircleMap.a in Ui by A62,FUNCT_2:46;
          hence b in Ui by A105,A106,A107,FUNCT_1:72;
        end;
        then reconsider f as Function of R^1 | Uit, TUC | Ui
        by A61,A104,A105,FUNCT_2:4;
A108:   f is_homeomorphism by A63,A72;
        TT.(i+1) <= 1 by A21,A33,A50;

then reconsider SS as non empty Subset of I[01] by A27,A65,A70,RCOMP_1:15;
        take N1;
        take S1 = S \/ SS;
        set Fni1 = f"*(F| [:N1,SS:]);
        reconsider ff = f as Function;
A109:   [:N1,SS:] c= dom F by A10,ZFMISC_1:119;
        f" is_homeomorphism by A63,A72,TOPS_2:70;
        then
A110:   dom (f") = [#](TUC | Ui) by TOPS_2:def 5;
A111:   rng f = [#](TUC | Ui) by A108,TOPS_2:def 5;
A112:   f is one-to-one by A108,TOPS_2:def 5;
        then
A113:   f" = ff" by A111,TOPS_2:def 4;
A114:   the carrier of (I[01]|SS) = SS by PRE_TOPC:29;
A115:   the carrier of (I[01]|S1) = S1 by PRE_TOPC:29;
A116:   dom (F| [:N1,SS:]) = [:N1,SS:] by A10,RELAT_1:91,ZFMISC_1:119;
A117:   rng (F| [:N1,SS:]) c= dom (f")
        proof
          let b be set;
          assume b in rng (F| [:N1,SS:]);
          then consider a being set such that
A118:     a in dom (F| [:N1,SS:]) and
A119:     (F| [:N1,SS:]).a = b by FUNCT_1:def 5;
          b = F.a by A116,A118,A119,FUNCT_1:72;
          then b in F.:[:N1,SS:] by A116,A118,FUNCT_2:43;
          then b in F.:[:N,SS:] by A103;
          then b in Ui by A60;
          hence thesis by A110,PRE_TOPC:29;
        end;
        then
A120:   dom Fni1 = dom (F| [:N1,SS:]) by RELAT_1:46;
A121:   [:N1,SS:] = the carrier of [:Y|N1,I[01]|SS:] by A101,A114,
        BORSUK_1:def 5;
A122:   [:N1,S1:] = the carrier of [:Y|N1,I[01]|S1:] by A101,A115,
        BORSUK_1:def 5;
A123:   [:N1,S:] = the carrier of [:Y|N1,I[01]|S:] by A57,A101,BORSUK_1:def 5;
        the carrier of (R^1 | Uit) is Subset of R^1 by TSEP_1:1;
        then rng Fni1 c= R by XBOOLE_1:1;
        then reconsider Fni1 as Function of [:Y|N1,I[01]|SS:], R^1
        by A116,A120,A121,FUNCT_2:4;
        set Fn2 = Fn | [:N1,S:];
        set Fn1 = Fn2 +* Fni1;
A124:   [:N1,S:] \/ [:N1,SS:] = [:N1,S1:] by ZFMISC_1:120;
A125:   [:N1,S:] c= [:N2,S:] by A96,ZFMISC_1:119;
        dom (Fn | [:N1,S:]) = [:N1,S:] by A57,A58,A64,A73,A96,RELAT_1:91
        ,ZFMISC_1:119;
        then
A126:   dom Fn1 = [:N1,S:] \/ [:N1,SS:] by A116,A120,FUNCT_4:def 1;
        rng Fn1 c= rng (Fn | [:N1,S:]) \/ rng Fni1 by FUNCT_4:18;
        then reconsider Fn1 as Function of [:Y|N1,I[01]|S1:], R^1
        by A122,A124,A126,FUNCT_2:4,XBOOLE_1:1;
        take Fn1; thus
A127:   S1 = [.0,TT.(i+1).] by A51,A65,A70,HEINE:2;
        thus y in N1 by A52,A95,XBOOLE_0:def 3;
        thus N1 c= N by A53,A96,XBOOLE_1:1;
A128:   S c= S1 by XBOOLE_1:7;
A129:   SS c= S1 by XBOOLE_1:7;
A130:   dom Fn2 = the carrier of [:Y|N1,I[01]|S:]
        by A57,A58,A64,A73,A96,A123,RELAT_1:91,ZFMISC_1:119;
        rng Fn2 c= R;
        then reconsider Fn2 as Function
        of [:Y|N1,I[01]|S:],R^1 by A130,FUNCT_2:4;
A131:   [#](I[01]|S1) = the carrier of (I[01]|S1);
        reconsider F1 = [#](I[01]|S), F2 = [#](I[01]|SS) as
        Subset of I[01]|S1 by A115,A128,A129,PRE_TOPC:29;
A132:   I[01]|S is SubSpace of I[01]|S1 by A57,A115,A128,TSEP_1:4;
A133:   I[01]|SS is SubSpace of I[01]|S1 by A114,A115,A129,TSEP_1:4;
        reconsider hS = F1, hSS = F2 as Subset of I[01] by PRE_TOPC:29;
        hS is closed by A51,BORSUK_4:48,PRE_TOPC:29;
        then
A134:   F1 is closed by TSEP_1:8;
        hSS is closed by BORSUK_4:48,PRE_TOPC:29;
        then
A135:   F2 is closed by TSEP_1:8;
        reconsider K0 = [:N1,S:] as Subset of [:Y|N2,I[01]|S:]
        by A57,A58,A125,PRE_TOPC:29;
        reconsider aN1 = N1 as non empty Subset of Y|N2 by A96,PRE_TOPC:29;
        S c= S;
        then reconsider aS = S as non empty Subset of I[01]|S by PRE_TOPC:29;
        [:Y|N2,I[01]|S:] | K0 = [:Y|N2|aN1,I[01]|S|aS:] by BORSUK_3:26
          .= [:Y|N1,I[01]|S|aS:] by GOBOARD9:4
          .= [:Y|N1,I[01]|S:] by GOBOARD9:4;
        then
A136:   Fn2 is continuous by A54,TOPMETR:10;
        reconsider fF = F| [:N1,SS:] as
        Function of [:Y|N1,I[01]|SS:], TUC | Ui
        by A110,A116,A117,A121,FUNCT_2:4;
        reconsider gF = F| [:N1,SS:] as Function of [:Y|N1,I[01]|SS:], TUC
        by A116,A117,A121,FUNCT_2:4;
        [:Y|N1,I[01]|SS:] = [:Y,I[01]:] | [:N1,SS:] by BORSUK_3:26;
        then gF is continuous by A1,TOPMETR:10;
        then
A137:   fF is continuous by TOPMETR:9;
        f" is continuous by A108,TOPS_2:def 5;
        then f"*fF is continuous by A137,TOPS_2:58;
        then
A138:   Fni1 is continuous by TOPMETR:7;
        for p being set st p in ([#][:Y|N1,I[01]|S:]) /\ [#][:Y|N1,I[01]|SS:]
        holds Fn2.p = Fni1.p
        proof
          let p be set such that
A139:     p in ([#][:Y|N1,I[01]|S:]) /\ [#][:Y|N1,I[01]|SS:];
A140:     [:N1,S:] /\ [:N1,SS:] = [:N1,S /\ SS:] by ZFMISC_1:122;
A141:     S /\ SS = {TT.i} by A51,A65,A70,TOPMETR2:1;
A142:     p in ([#][:Y|N1,I[01]|SS:]) /\ [#][:Y|N1,I[01]|S:] by A139;
A143:     p in [:N1,{TT.i}:] by A51,A65,A70,A121,A123,A139,A140,TOPMETR2:1;
          then consider p1 being Element of N1,
          p2 being Element of {TT.i} such that
A144:     p = [p1,p2] by DOMAIN_1:9;
A145:     the carrier of (Y|N2) = N2 by PRE_TOPC:29;
A146:     Fn.p = Fn2.p by A123,A142,FUNCT_1:72;
A147:     p2 in S by A141,XBOOLE_0:def 3;
          p1 in N1;
          then
A148:     p in [:N2,S:] by A96,A144,A147,ZFMISC_1:def 2;
          then
A149:     Fn.p in Fn.:[:N1,{TT.i}:] by A57,A58,A64,A143,FUNCT_2:43;
          (F| [:N1,SS:]).p = F.p by A121,A139,FUNCT_1:72
            .= (F | [:N2,S:]).p by A148,FUNCT_1:72
            .= CircleMap.(Fn.p) by A55,A57,A58,A73,A145,A148,FUNCT_1:23
            .= (CircleMap|Uit).(Fn.p) by A97,A149,FUNCT_1:72
            .= ff.(Fn2.p) by A123,A142,FUNCT_1:72;
          hence Fn2.p = ff".((F| [:N1,SS:]).p) by A97,A105,A112,A146,A149,
          FUNCT_1:54
            .= Fni1.p by A113,A116,A121,A139,FUNCT_1:23;
        end;
        then ex h being Function of [:Y|N1,I[01]|S1:],R^1 st
        h = Fn2+*Fni1 & h is continuous
        by A57,A114,A115,A131,A132,A133,A134,A135,A136,A138,TOPALG_3:20;
        hence Fn1 is continuous;
        rng Fn1 c= dom CircleMap by Lm12,TOPMETR:24;
        then
A150:   dom (CircleMap*Fn1) = dom Fn1 by RELAT_1:46;
A151:   dom Fn1 = dom F /\ [:N1,S1:] by A10,A124,A126,XBOOLE_1:28,ZFMISC_1:119;
        for a being set st a in dom (CircleMap*Fn1) holds (CircleMap*Fn1).a =
        F . a
        proof
          let a be set such that
A152:     a in dom (CircleMap*Fn1);
          per cases;
          suppose
A153:       a in dom Fni1;
            then
A154:       a in [:N1,SS:] by A10,A120,RELAT_1:91,ZFMISC_1:119;
A155:       [:N1,SS:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            then F.a in F.:[:N1,SS:] by A10,A154,FUNCT_1:def 12;
            then
A156:       F.a in F.:[:N,SS:] by A103;
            then a in F"(dom (ff")) by A10,A60,A61,A110,A113,A154,A155,
            FUNCT_1:def 13;
            then
A157:       a in dom (ff"*F) by RELAT_1:182;
            thus (CircleMap*Fn1).a = CircleMap.(Fn1.a) by A152,FUNCT_2:21
              .= CircleMap.(Fni1.a) by A153,FUNCT_4:14
              .= CircleMap.(f".((F| [:N1,SS:]).a)) by A120,A153,FUNCT_1:23
              .= CircleMap.(f".(F.a)) by A116,A120,A153,FUNCT_1:72
              .= CircleMap.((ff"*F).a) by A109,A113,A116,A120,A153,FUNCT_1:23
              .= (CircleMap*(ff"*F)).a by A157,FUNCT_1:23
              .= (CircleMap*ff"*F).a by RELAT_1:55
              .= (CircleMap*ff").(F.a) by A109,A116,A120,A153,FUNCT_1:23
              .= F.a by A60,A61,A111,A112,A156,TOPALG_3:2;
          end;
          suppose
A158:       not a in dom Fni1;
            then
A159:       a in [:N1,S:] by A116,A120,A126,A150,A152,XBOOLE_0:def 2;
            thus (CircleMap*Fn1).a = CircleMap.(Fn1.a) by A152,FUNCT_2:21
              .= CircleMap.(Fn | [:N1,S:].a) by A158,FUNCT_4:12
              .= CircleMap.(Fn.a) by A159,FUNCT_1:72
              .= (CircleMap*Fn).a by A57,A58,A64,A125,A159,FUNCT_2:21
              .= F.a by A55,A125,A159,FUNCT_1:72;
          end;
        end;
        hence F | [:N1,S1:] = CircleMap*Fn1 by A150,A151,FUNCT_1:68;
        0 <= TT.(i+1) by A21,A33;
        then 0 in S1 by A127,RCOMP_1:48;
        then
A160:   {0} c= S1 by ZFMISC_1:37;
A161:   dom (Fn1 | [:the carrier of Y,{0}:])
        = dom Fn1 /\ [:the carrier of Y,{0}:] by RELAT_1:90;
        then
A162:   dom (Fn1 | [:the carrier of Y,{0}:])
        = [:N1 /\ the carrier of Y,S1 /\ {0}:] by A124,A126,ZFMISC_1:123
          .= [:N1,S1 /\ {0}:] by XBOOLE_1:28
          .= [:N1,{0}:] by A160,XBOOLE_1:28;
A163:   dom (Ft | [:N1,I:]) = dom Ft /\ [:N1,I:] by RELAT_1:90
          .= [:(the carrier of Y) /\ N1,{0} /\ I:] by A6,ZFMISC_1:123
          .= [:N1,{0} /\ I:] by XBOOLE_1:28
          .= [:N1,{0}:] by Lm3,XBOOLE_1:28;
        for a being set st a in dom (Fn1 | [:the carrier of Y,{0}:]) holds
        (Fn1 | [:the carrier of Y,{0}:]).a = (Ft | [:N1,I:]).a
        proof
          let a be set;
          assume
A164:     a in dom (Fn1 | [:the carrier of Y,{0}:]);
          then
A165:     a in [:the carrier of Y,{0}:] by A161,XBOOLE_0:def 3;
          then consider a1, a2 being set such that a1 in the carrier of Y and
A166:     a2 in {0} and
A167:     a = [a1,a2] by ZFMISC_1:def 2;
A168:     a2 = 0 by A166,TARSKI:def 1;
          0 in S by A51,A65,RCOMP_1:48;
          then {0} c= S by ZFMISC_1:37;
          then
A169:     [:N1,{0}:] c= [:N1,S:] by ZFMISC_1:119;
          then
A170:     a in [:N1,S:] by A162,A164;
A171:     [:N1,S:] c= [:N1,I:] by ZFMISC_1:119;
          then
A172:     a in [:N1,I:] by A170;
A173:     [:N1,I:] c= [:N2,I:] by A96,ZFMISC_1:119;
          per cases;
          suppose
A174:       not a in dom Fni1;
            thus (Fn1 | [:the carrier of Y,{0}:]).a = Fn1.a by A165,FUNCT_1:72
              .= (Fn | [:N1,S:]).a by A174,FUNCT_4:12
              .= Fn.a by A162,A164,A169,FUNCT_1:72
              .= (Ft | [:N2,I:]).a by A56,A165,FUNCT_1:72
              .= Ft.a by A172,A173,FUNCT_1:72
              .= (Ft | [:N1,I:]).a by A170,A171,FUNCT_1:72;
          end;
          suppose
A175:       a in dom Fni1;
            then a in [:N1,SS:] by A10,A120,RELAT_1:91,ZFMISC_1:119;
            then consider b1, b2 being set such that
A176:       b1 in N1 and
A177:       b2 in SS and
A178:       a = [b1,b2] by ZFMISC_1:def 2;
            set e = (Ft | [:N1,I:]).a;
A179:       e = Ft.a by A170,A171,FUNCT_1:72
              .= (Ft | [:N2,I:]).a by A172,A173,FUNCT_1:72
              .= Fn.a by A56,A165,FUNCT_1:72;
            a2 = b2 by A167,A178,ZFMISC_1:33;
            then
A180:       a2 = TT.i by A65,A168,A177,RCOMP_1:48;
A181:       a1 = b1 by A167,A178,ZFMISC_1:33;
            then
A182:       [a1,TT.i] in [:N1,S:] by A66,A176,ZFMISC_1:106;
            [a1,TT.i] in [:N1,{TT.i}:] by A74,A176,A181,ZFMISC_1:106;
            then
A183:       e in Fn.:[:N1,{TT.i}:] by A57,A58,A64,A73,A125,A167,A179,A180,A182,
            FUNCT_1:def 12;
            then
A184:       ff.e = CircleMap.e by A97,FUNCT_1:72
              .= CircleMap.(Ft.a) by A170,A171,FUNCT_1:72
              .= (CircleMap*Ft).a by A6,A165,FUNCT_1:23
              .= F.a by A3,A165,FUNCT_1:72;
            thus (Fn1 | [:the carrier of Y,{0}:]).a = Fn1.a by A165,FUNCT_1:72
              .= Fni1.a by A175,FUNCT_4:14
              .= ff".((F| [:N1,SS:]).a) by A113,A120,A175,FUNCT_1:23
              .= ff".(F.a) by A116,A120,A175,FUNCT_1:72
              .= (Ft | [:N1,I:]).a by A97,A105,A112,A183,A184,FUNCT_1:54;
          end;
        end;
        hence Fn1 | [:the carrier of Y,{0}:] = Ft | [:N1,I:] by A162,A163,
        FUNCT_1:9;
      end;
    end;
    for i being Element of NAT holds N[i] from NAT_1:sch 1(A30,A31);
    then consider N2 being non empty open Subset of Y,
    S being non empty Subset of I[01],
    Fn1 being Function of [:Y|N2,I[01]|S:], R^1 such that
A185: S = [.0,TT.len TT.] and
A186: y in N2 and
A187: N2 c= N and
A188: Fn1 is continuous and
A189: F | [:N2,S:] = CircleMap*Fn1 and
A190: Fn1 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] by A20;
    reconsider z = Fn1.x as Point of R^1 by TOPMETR:24;
    take z, y, t, N2;
A191: I[01]|S = I[01] by A14,A185,Lm6,BORSUK_1:83,TSEP_1:3;
    then reconsider Fn1 as Function of [:Y|N2,I[01]:], R^1;
    take Fn1;
    thus x = [y,t] & z = Fn1.x & y in N2 & Fn1 is continuous &
    F | [:N2,I:] = CircleMap*Fn1 &
    Fn1 | [:the carrier of Y,{0}:] = Ft | [:N2,I:]
    by A12,A14,A185,A186,A188,A189,A190,A191,BORSUK_1:83;
    let H be Function of [:Y|N2,I[01]:], R^1 such that
A192: H is continuous and
A193: F | [:N2,I:] = CircleMap*H and
A194: H | [:the carrier of Y,{0}:] = Ft | [:N2,I:];
A195: dom H = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
A196: the carrier of [:Y|N2,I[01]:] = [:the carrier of Y|N2,I:]
    by BORSUK_1:def 5;
A197: the carrier of Y|N2 = N2 by PRE_TOPC:29;
A198: dom Fn1 = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
    defpred M[Element of NAT] means $1 in dom TT implies
    ex Z being non empty Subset of I[01] st
    Z = [.0,TT.$1.] & Fn1 | [:N2,Z:] = H | [:N2,Z:];
A199: M[0] by FINSEQ_3:26;
A200: for i being Element of NAT st M[i] holds M[i+1]
    proof
      let i be Element of NAT such that
A201: M[i] and
A202: i+1 in dom TT;
      per cases by A202,TOPREALA:23;
      suppose
A203:   i = 0;
        set Z = [.0,TT.(i+1).];
A204:   Z = {0} by A13,A203,RCOMP_1:14;
        reconsider Z as non empty Subset of I[01] by A13,A203,Lm3,RCOMP_1:14;
        take Z;
        thus Z = [.0,TT.(i+1).];
A205:   [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
        then
A206:   dom (Fn1 | [:N2,Z:]) = [:N2,Z:] by A196,A197,A198,RELAT_1:91;
A207:   dom (H | [:N2,Z:]) = [:N2,Z:] by A195,A196,A197,A205,RELAT_1:91;
        for x being set st x in dom (Fn1 | [:N2,Z:]) holds
        (Fn1 | [:N2,Z:]).x = (H | [:N2,Z:]).x
        proof
          let x be set;
          assume
A208:     x in dom (Fn1 | [:N2,Z:]);
A209:     [:N2,Z:] c= [:the carrier of Y,Z:] by ZFMISC_1:118;
          thus (Fn1 | [:N2,Z:]).x = Fn1.x by A206,A208,FUNCT_1:72

.= (Fn1 | [:the carrier of Y,{0}:]).x by A204,A206,A208,A209,FUNCT_1:72
            .= H.x by A190,A194,A204,A206,A208,A209,FUNCT_1:72
            .= (H | [:N2,Z:]).x by A206,A208,FUNCT_1:72;
        end;
        hence Fn1 | [:N2,Z:] = H | [:N2,Z:] by A206,A207,FUNCT_1:9;
      end;
      suppose
A210:   i in dom TT;
        then consider Z being non empty Subset of I[01] such that
A211:   Z = [.0,TT.i.] and
A212:   Fn1 | [:N2,Z:] = H | [:N2,Z:] by A201;
        set ZZ = [.TT.i,TT.(i+1).];
A213:   0 <= TT.i by A21,A210;
A214:   TT.(i+1) <= 1 by A21,A202,A210;
        then reconsider ZZ as Subset of I[01] by A27,A213;
        take Z1 = Z \/ ZZ;
A215:   TT.i < TT.(i+1) by A21,A202,A210;
        hence Z1 = [.0,TT.(i+1).] by A211,A213,HEINE:2;
A216:   [:N2,Z1:] c= [:N2,I:] by ZFMISC_1:118;
        then
A217:   dom (Fn1 | [:N2,Z1:]) = [:N2,Z1:] by A196,A197,A198,RELAT_1:91;
A218:   dom (H | [:N2,Z1:]) = [:N2,Z1:] by A195,A196,A197,A216,RELAT_1:91;
        for x being set st x in dom (Fn1 | [:N2,Z1:]) holds
        (Fn1 | [:N2,Z1:]).x = (H | [:N2,Z1:]).x
        proof
          let x be set such that
A219:     x in dom (Fn1 | [:N2,Z1:]);
          consider x1, x2 being set such that
A220:     x1 in N2 and
A221:     x2 in Z1 and
A222:     x = [x1,x2] by A217,A219,ZFMISC_1:def 2;
          reconsider xy = {x1} as non empty Subset of Y by A220,ZFMISC_1:37;
A223:     xy c= N2 by A220,ZFMISC_1:37;
          then reconsider xZZ = [:xy,ZZ:] as Subset of [:Y|N2,I[01]:]
          by A196,A197,ZFMISC_1:119;
          consider Ui being non empty Subset of TUC such that
A224:     Ui in UL and
A225:     F.:[:N,ZZ:] c= Ui by A18,A202,A210;
A226:     the carrier of (TUC | Ui) = Ui by PRE_TOPC:29;

consider D being mutually-disjoint open Subset-Family of R^1 such that
A227:     union D = (CircleMap)"Ui and
A228:     for d being Subset of R^1 st d in D
          for f being Function of R^1 | d, TUC | Ui st f = CircleMap|d
          holds f is_homeomorphism by A9,A224;
          TT.i in Z by A211,A213,RCOMP_1:15;
          then
A229:     [x1,TT.i] in [:N2,Z:] by A220,ZFMISC_1:106;
A230:     [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
          then
A231:     F. [x1,TT.i] = (CircleMap*Fn1). [x1,TT.i]
          by A14,A185,A189,A229,BORSUK_1:83,FUNCT_1:72

.= CircleMap.(Fn1. [x1,TT.i]) by A196,A197,A198,A229,A230,FUNCT_1:23;
A232:     TT.i in ZZ by A215,RCOMP_1:15;
          then [x1,TT.i] in [:N,ZZ:] by A187,A220,ZFMISC_1:106;
          then
A233:     F. [x1,TT.i] in F.:[:N,ZZ:] by FUNCT_2:43;

then Fn1. [x1,TT.i] in (CircleMap)"Ui by A225,A231,FUNCT_2:46,TOPMETR:24;
          then consider Uit being set such that
A234:     Fn1. [x1,TT.i] in Uit and
A235:     Uit in D by A227,TARSKI:def 4;
          reconsider Uit as non empty Subset of R^1 by A234,A235;
          0 <= TT.i & TT.i <= 1 by A21,A202,A210;
          then
A236:     TT.i is Point of I[01] by BORSUK_1:86;
          0 <= TT.(i+1) by A21,A202;
          then TT.(i+1) is Point of I[01] by A214,BORSUK_1:86;
          then
A237:     ZZ is connected by A215,A236,BORSUK_4:49;
          xy is connected by A220,CONNSP_1:29;
          then
A238:     [:xy,ZZ:] is connected by A237,TOPALG_3:17;
          I[01] is SubSpace of I[01] by TSEP_1:2;
          then [:Y|N2,I[01]:] is SubSpace of [:Y,I[01]:] by BORSUK_3:25;
          then
A239:     xZZ is connected by A238,CONNSP_1:24;
A240:     x1 in xy by TARSKI:def 1;
          then
A241:     [x1,TT.i] in xZZ by A232,ZFMISC_1:106;
          then Fn1. [x1,TT.i] in Fn1.:xZZ by FUNCT_2:43;
          then
A242:     Uit meets Fn1.:xZZ by A234,XBOOLE_0:3;
          D is Cover of Fn1.:xZZ
          proof
            let b be set;
            assume b in Fn1.:xZZ;
            then consider a being Point of [:Y|N2,I[01]:] such that
A243:       a in xZZ and
A244:       Fn1.a = b by FUNCT_2:116;
A245:       CircleMap.(Fn1.a) = (CircleMap*Fn1).a by FUNCT_2:21
              .= F.a by A14,A185,A189,A196,A197,BORSUK_1:83,FUNCT_1:72;
            xy c= N by A187,A220,ZFMISC_1:37;
            then [:xy,ZZ:] c= [:N,ZZ:] by ZFMISC_1:118;
            then
A246:       a in [:N,ZZ:] by A243;
            [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            then F.a in F.:[:N,ZZ:] by A10,A246,FUNCT_1:def 12;
            hence b in union D by A225,A227,A244,A245,Lm12,FUNCT_1:def 13
            ,TOPMETR:24;
          end;
          then
A247:     Fn1.:xZZ c= Uit by A188,A191,A235,A239,A242,TOPALG_3:13,TOPS_2:75;
          F. [x1,TT.i] = (CircleMap*H). [x1,TT.i] by A193,A229,A230,FUNCT_1:72
            .= CircleMap.(H. [x1,TT.i]) by A195,A196,A197,A229,A230,FUNCT_1:23;

then H. [x1,TT.i] in (CircleMap)"Ui by A225,A233,FUNCT_2:46,TOPMETR:24;
          then consider Uith being set such that
A248:     H. [x1,TT.i] in Uith and
A249:     Uith in D by A227,TARSKI:def 4;
          reconsider Uith as non empty Subset of R^1 by A248,A249;
          H. [x1,TT.i] in H.:xZZ by A241,FUNCT_2:43;
          then
A250:     Uith meets H.:xZZ by A248,XBOOLE_0:3;
          D is Cover of H.:xZZ
          proof
            let b be set;
            assume b in H.:xZZ;
            then consider a being Point of [:Y|N2,I[01]:] such that
A251:       a in xZZ and
A252:       H.a = b by FUNCT_2:116;
A253:       CircleMap.(H.a) = (CircleMap*H).a by FUNCT_2:21
              .= F.a by A193,A196,A197,FUNCT_1:72;
            xy c= N by A187,A220,ZFMISC_1:37;
            then [:xy,ZZ:] c= [:N,ZZ:] by ZFMISC_1:118;
            then
A254:       a in [:N,ZZ:] by A251;
            [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            then F.a in F.:[:N,ZZ:] by A10,A254,FUNCT_1:def 12;
            hence b in union D by A225,A227,A252,A253,Lm12,FUNCT_1:def 13
            ,TOPMETR:24;
          end;
          then
A255:     H.:xZZ c= Uith by A192,A239,A249,A250,TOPALG_3:13,TOPS_2:75;
A256:     H. [x1,TT.i] in H.:xZZ by A195,A241,FUNCT_1:def 12;
          Fn1. [x1,TT.i] = (Fn1 | [:N2,Z:]). [x1,TT.i] by A229,FUNCT_1:72
            .= H. [x1,TT.i] by A212,A229,FUNCT_1:72;
          then Uit meets Uith by A234,A255,A256,XBOOLE_0:3;
          then
A257:     Uit = Uith by A235,A249,TAXONOM2:def 5;
          set f = CircleMap|Uit;
A258:     the carrier of (R^1 | Uit) = Uit by PRE_TOPC:29;
A259:     dom f = Uit by Lm12,RELAT_1:91,TOPMETR:24;
          rng f c= Ui
          proof
            let b be set;
            assume b in rng f;
            then consider a being set such that
A260:       a in dom f and
A261:       f.a = b by FUNCT_1:def 5;
            a in union D by A235,A259,A260,TARSKI:def 4;
            then CircleMap.a in Ui by A227,FUNCT_2:46;
            hence b in Ui by A259,A260,A261,FUNCT_1:72;
          end;
          then reconsider f as Function of R^1 | Uit, TUC | Ui
          by A226,A258,A259,FUNCT_2:4;
          f is_homeomorphism by A228,A235;
          then
A262:     f is one-to-one by TOPS_2:def 5;
A263:     dom (Fn1 | [:xy,ZZ:]) = [:xy,ZZ:] by A196,A197,A198,A223,RELAT_1:91
          ,ZFMISC_1:119;
A264:     dom (H | [:xy,ZZ:]) = [:xy,ZZ:] by A195,A196,A197,A223,RELAT_1:91
          ,ZFMISC_1:119;
A265:     rng (Fn1 | [:xy,ZZ:]) c= dom f
          proof
            let b be set;
            assume b in rng (Fn1 | [:xy,ZZ:]);
            then consider a being set such that
A266:       a in dom (Fn1 | [:xy,ZZ:]) and
A267:       (Fn1 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
            Fn1.a = b by A263,A266,A267,FUNCT_1:72;
            then b in Fn1.:xZZ by A198,A263,A266,FUNCT_1:def 12;
            hence b in dom f by A247,A259;
          end;
A268:     rng (H | [:xy,ZZ:]) c= dom f
          proof
            let b be set;
            assume b in rng (H | [:xy,ZZ:]);
            then consider a being set such that
A269:       a in dom (H | [:xy,ZZ:]) and
A270:       (H | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
            H.a = b by A264,A269,A270,FUNCT_1:72;
            then b in H.:xZZ by A195,A264,A269,FUNCT_1:def 12;
            hence b in dom f by A255,A257,A259;
          end;
A271:     dom (f*(Fn1 | [:xy,ZZ:])) = dom (Fn1 | [:xy,ZZ:]) by A265,RELAT_1:46;
A272:     dom (f*(H | [:xy,ZZ:])) = dom (H | [:xy,ZZ:]) by A268,RELAT_1:46;
          for x being set st x in dom (f*(Fn1 | [:xy,ZZ:])) holds
          (f*(Fn1 | [:xy,ZZ:])).x = (f*(H | [:xy,ZZ:])).x
          proof
            let x be set such that
A273:       x in dom (f*(Fn1 | [:xy,ZZ:]));
A274:       Fn1.x in Fn1.:[:xy,ZZ:] by A198,A263,A271,A273,FUNCT_1:def 12;
A275:       H.x in H.:[:xy,ZZ:] by A195,A263,A271,A273,FUNCT_1:def 12;

thus (f*(Fn1 | [:xy,ZZ:])).x = ((f*Fn1) | [:xy,ZZ:]).x by RELAT_1:112
              .= (f*Fn1).x by A263,A271,A273,FUNCT_1:72
              .= f.(Fn1.x) by A198,A273,FUNCT_1:23
              .= CircleMap.(Fn1.x) by A247,A274,FUNCT_1:72
              .= (CircleMap*Fn1).x by A198,A273,FUNCT_1:23
              .= CircleMap.(H.x) by A14,A185,A189,A193,A195,A273,BORSUK_1:83
            ,FUNCT_1:23
              .= f.(H.x) by A255,A257,A275,FUNCT_1:72
              .= (f*H).x by A195,A273,FUNCT_1:23
              .= ((f*H) | [:xy,ZZ:]).x by A263,A271,A273,FUNCT_1:72
              .= (f*(H | [:xy,ZZ:])).x by RELAT_1:112;
          end;
          then
A276:     f*(Fn1 | [:xy,ZZ:]) = f*(H | [:xy,ZZ:]) by A263,A264,A271,A272,
          FUNCT_1:9;
          per cases;
          suppose x2 in ZZ;
            then
A277:       x in [:xy,ZZ:] by A222,A240,ZFMISC_1:106;
            thus (Fn1 | [:N2,Z1:]).x = Fn1.x by A217,A219,FUNCT_1:72
              .= (Fn1 | [:xy,ZZ:]).x by A277,FUNCT_1:72
              .= (H | [:xy,ZZ:]).x by A262,A263,A264,A265,A268,A276,FUNCT_1:49
              .= H.x by A277,FUNCT_1:72
              .= (H | [:N2,Z1:]).x by A217,A219,FUNCT_1:72;
          end;
          suppose not x2 in ZZ;
            then x2 in Z by A221,XBOOLE_0:def 2;
            then
A278:       x in [:N2,Z:] by A220,A222,ZFMISC_1:106;
            thus (Fn1 | [:N2,Z1:]).x = Fn1.x by A217,A219,FUNCT_1:72
              .= (Fn1 | [:N2,Z:]).x by A278,FUNCT_1:72
              .= H.x by A212,A278,FUNCT_1:72
              .= (H | [:N2,Z1:]).x by A217,A219,FUNCT_1:72;
          end;
        end;
        hence Fn1 | [:N2,Z1:] = H | [:N2,Z1:] by A217,A218,FUNCT_1:9;
      end;
    end;
    for i being Element of NAT holds M[i] from NAT_1:sch 1(A199,A200);
    then consider Z being non empty Subset of I[01] such that
A279: Z = [.0,TT.len TT.] and
A280: Fn1 | [:N2,Z:] = H | [:N2,Z:] by A20;
    thus Fn1 = Fn1 | [:N2,Z:] by A14,A196,A197,A198,A279,BORSUK_1:83,RELAT_1:98
      .= H by A14,A195,A196,A197,A279,A280,BORSUK_1:83,RELAT_1:98;
  end;
  consider G being Function of [:Y,I[01]:],R^1 such that
A281: for x being Point of [:Y,I[01]:] holds A[x,G.x] from FUNCT_2:sch 3(A11);
  take G;
A282: dom G = [:the carrier of Y,I:] by A4,FUNCT_2:def 1;
A283: for x being Point of [:Y,I[01]:] holds F.x = (CircleMap*G).x
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y, t being Point of I[01],
    N being non empty open Subset of Y,
    Fn being Function of [:Y|N,I[01]:], R^1 such that
A284: x = [y,t] and
A285: G.x = Fn.x and
A286: y in N and Fn is continuous and
A287: F | [:N,I:] = CircleMap*Fn and
    Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
    for H being Function of [:Y|N,I[01]:], R^1 st
    H is continuous & F | [:N,I:] = CircleMap*H &
    H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H by A281;
A288: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by
    BORSUK_1:def 5
      .= [:N,I:] by PRE_TOPC:29;
    then
A289: x in the carrier of [:Y|N,I[01]:] by A284,A286,ZFMISC_1:106;
    thus (CircleMap*G).x = CircleMap.(G.x) by FUNCT_2:21
      .= (CircleMap*Fn).x by A285,A289,FUNCT_2:21
      .= F.x by A287,A288,A289,FUNCT_1:72;
  end;
A290: now
    let N be Subset of Y, F be Function of [:Y|N,I[01]:],R^1;
    thus dom F = the carrier of [:Y|N,I[01]:] by FUNCT_2:def 1
      .= [:the carrier of Y|N,I:] by BORSUK_1:def 5
      .= [:N,I:] by PRE_TOPC:29;
  end;
A291: for p being Point of [:Y,I[01]:],
  y being Point of Y, t being Point of I[01],
  N1, N2 being non empty open Subset of Y,
  Fn1 being Function of [:Y|N1,I[01]:],R^1,
  Fn2 being Function of [:Y|N2,I[01]:],R^1 st p = [y,t] & y in N1 & y in N2 &
  Fn2 is continuous & Fn1 is continuous & F | [:N2,I:] = CircleMap*Fn2 &
  Fn2 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] &
  F | [:N1,I:] = CircleMap*Fn1 &
  Fn1 | [:the carrier of Y,{0}:] = Ft | [:N1,I:]
  holds Fn1 | [:{y},I:] = Fn2 | [:{y},I:]
  proof
    let p be Point of [:Y,I[01]:], y be Point of Y, t be Point of I[01],
    N1, N2 be non empty open Subset of Y,
    Fn1 be Function of [:Y|N1,I[01]:],R^1,
    Fn2 be Function of [:Y|N2,I[01]:],R^1 such that p = [y,t] and
A292: y in N1 and
A293: y in N2 and
A294: Fn2 is continuous and
A295: Fn1 is continuous and
A296: F | [:N2,I:] = CircleMap*Fn2 and
A297: Fn2 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] and
A298: F | [:N1,I:] = CircleMap*Fn1 and
A299: Fn1 | [:the carrier of Y,{0}:] = Ft | [:N1,I:];
A300: {y} c= N1 by A292,ZFMISC_1:37;
A301: dom Fn1 = [:N1,I:] by A290;
A302: {y} c= N2 by A293,ZFMISC_1:37;
A303: dom Fn2 = [:N2,I:] by A290;
    consider TT being non empty FinSequence of REAL such that
A304: TT.1 = 0 and
A305: TT.len TT = 1 and
A306: TT is increasing and
A307: ex N being open Subset of Y st y in N &
    for i being natural number st i in dom TT & i+1 in dom TT
    ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
    F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A1,A7,A8,Th21;
    consider N being open Subset of Y such that
A308: y in N and
A309: for i being natural number st i in dom TT & i+1 in dom TT
    ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
    F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A307;
    reconsider N as non empty open Subset of Y by A308;
A310: 1 in dom TT by FINSEQ_5:6;
A311: len TT in dom TT by FINSEQ_5:6;
A312: dom Fn2 = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
A313: the carrier of [:Y|N2,I[01]:] = [:the carrier of Y|N2,I:]
    by BORSUK_1:def 5;
A314: the carrier of Y|N2 = N2 by PRE_TOPC:29;
A315: the carrier of [:Y|N1,I[01]:] = [:the carrier of Y|N1,I:]
    by BORSUK_1:def 5;
A316: the carrier of Y|N1 = N1 by PRE_TOPC:29;
A317: dom Fn1 = the carrier of [:Y|N1,I[01]:] by FUNCT_2:def 1;
    defpred M[Element of NAT] means $1 in dom TT implies
    ex Z being non empty Subset of I[01] st
    Z = [.0,TT.$1.] & Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:];
A318: M[0] by FINSEQ_3:26;
A319: for i being Element of NAT st M[i] holds M[i+1]
    proof
      let i be Element of NAT such that
A320: M[i] and
A321: i+1 in dom TT;
      per cases by A321,TOPREALA:23;
      suppose
A322:   i = 0;
        set Z = [.0,TT.(i+1).];
A323:   Z = {0} by A304,A322,RCOMP_1:14;
        reconsider Z as non empty Subset of I[01] by A304,A322,Lm3,RCOMP_1:14;
        take Z;
        thus Z = [.0,TT.(i+1).];
A324:   [:{y},Z:] c= [:N1,I:] by A300,ZFMISC_1:119;
A325:   dom (Fn1 | [:{y},Z:]) = [:{y},Z:] by A300,A301,RELAT_1:91,ZFMISC_1:119;
A326:   [:{y},Z:] c= [:N2,I:] by A302,ZFMISC_1:119;
A327:   dom (Fn2 | [:{y},Z:]) = [:{y},Z:] by A302,A303,RELAT_1:91,ZFMISC_1:119;
        for x being set st x in dom (Fn1 | [:{y},Z:]) holds
        (Fn1 | [:{y},Z:]).x = (Fn2 | [:{y},Z:]).x
        proof
          let x be set;
          assume
A328:     x in dom (Fn1 | [:{y},Z:]);
A329:     [:{y},Z:] c= [:the carrier of Y,Z:] by ZFMISC_1:118;
          thus (Fn1 | [:{y},Z:]).x = Fn1.x by A325,A328,FUNCT_1:72

.= (Fn1 | [:the carrier of Y,{0}:]).x by A323,A325,A328,A329,FUNCT_1:72
            .= Ft.x by A299,A324,A325,A328,FUNCT_1:72
            .= (Ft | [:N2,I:]).x by A325,A326,A328,FUNCT_1:72
            .= Fn2.x by A297,A323,A325,A328,A329,FUNCT_1:72
            .= (Fn2 | [:{y},Z:]).x by A325,A328,FUNCT_1:72;
        end;
        hence Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:] by A325,A327,FUNCT_1:9;
      end;
      suppose
A330:   i in dom TT;
        then consider Z being non empty Subset of I[01] such that
A331:   Z = [.0,TT.i.] and
A332:   Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:] by A320;
        set ZZ = [.TT.i,TT.(i+1).];
A333:   now
          let i be Element of NAT such that
A334:     i in dom TT;
          1 <= i by A334,FINSEQ_3:27;
          then 1 = i or 1 < i by REAL_1:def 5;
          hence
A335:     0 <= TT.i by A304,A306,A310,A334,SEQM_3:def 1;
          assume
A336:     i+1 in dom TT;
A337:     i+0 < i+1 by XREAL_1:10;
          hence
A338:     TT.i < TT.(i+1) by A306,A334,A336,SEQM_3:def 1;
          i+1 <= len TT by A336,FINSEQ_3:27;
          then i+1 = len TT or i+1 < len TT by REAL_1:def 5;
          hence TT.(i+1) <= 1 by A305,A306,A311,A336,SEQM_3:def 1;
          hence TT.i < 1 by A338,XXREAL_0:2;
          thus 0 < TT.(i+1) by A306,A334,A335,A336,A337,SEQM_3:def 1;
        end;
A339:   now
          let i be Element of NAT such that
A340:     0 <= TT.i & TT.(i+1) <= 1;
          thus [.TT.i,TT.(i+1).] c= I
          proof
            let a be set;
            assume
A341:       a in [.TT.i,TT.(i+1).];
            then reconsider a as Real;
            TT.i <= a & a <= TT.(i+1) by A341,RCOMP_1:48;
            then 0 <= a & a <= 1 by A340,XXREAL_0:2;
            hence thesis by BORSUK_1:86;
          end;
        end;
A342:   0 <= TT.i by A330,A333;
A343:   TT.(i+1) <= 1 by A321,A330,A333;
        then reconsider ZZ as Subset of I[01] by A339,A342;
        take Z1 = Z \/ ZZ;
A344:   TT.i < TT.(i+1) by A321,A330,A333;
        hence Z1 = [.0,TT.(i+1).] by A331,A342,HEINE:2;
A345:   dom (Fn1 | [:{y},Z1:]) = [:{y},Z1:] by A300,A301,RELAT_1:91
        ,ZFMISC_1:119;
A346:   dom (Fn2 | [:{y},Z1:]) = [:{y},Z1:] by A302,A312,A313,A314,RELAT_1:91
        ,ZFMISC_1:119;
        for x being set st x in dom (Fn1 | [:{y},Z1:]) holds
        (Fn1 | [:{y},Z1:]).x = (Fn2 | [:{y},Z1:]).x
        proof
          let x be set such that
A347:     x in dom (Fn1 | [:{y},Z1:]);
          consider x1, x2 being set such that
A348:     x1 in {y} and
A349:     x2 in Z1 and
A350:     x = [x1,x2] by A345,A347,ZFMISC_1:def 2;
          reconsider xy = {x1} as non empty Subset of Y by A348,ZFMISC_1:37;
A351:     xy c= N2 by A302,A348,ZFMISC_1:37;
A352:     xy c= N1 by A300,A348,ZFMISC_1:37;
A353:     x1 = y by A348,TARSKI:def 1;
          then
A354:     xy c= N by A308,ZFMISC_1:37;
          reconsider xZZ = [:xy,ZZ:] as Subset of [:Y|N1,I[01]:]
          by A315,A316,A352,ZFMISC_1:119;
          consider Ui being non empty Subset of TUC such that
A355:     Ui in UL and
A356:     F.:[:N,ZZ:] c= Ui by A309,A321,A330;
A357:     the carrier of (TUC | Ui) = Ui by PRE_TOPC:29;

consider D being mutually-disjoint open Subset-Family of R^1 such that
A358:     union D = (CircleMap)"Ui and
A359:     for d being Subset of R^1 st d in D
          for f being Function of R^1 | d, TUC | Ui st f = CircleMap|d
          holds f is_homeomorphism by A9,A355;
A360:     TT.i in Z by A331,A342,RCOMP_1:15;
          then
A361:     [x1,TT.i] in [:{y},Z:] by A348,ZFMISC_1:106;
A362:     [x1,TT.i] in [:N2,Z:] by A302,A348,A360,ZFMISC_1:106;
A363:     [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
A364:     [:{y},Z:] c= [:N1,I:] by A300,ZFMISC_1:119;
          then
A365:     F. [x1,TT.i] = (CircleMap*Fn1). [x1,TT.i] by A298,A361,FUNCT_1:72
            .= CircleMap.(Fn1. [x1,TT.i]) by A301,A361,A364,FUNCT_1:23;
A366:     TT.i in ZZ by A344,RCOMP_1:15;
          then [x1,TT.i] in [:N,ZZ:] by A308,A353,ZFMISC_1:106;
          then
A367:     F. [x1,TT.i] in F.:[:N,ZZ:] by FUNCT_2:43;

then Fn1. [x1,TT.i] in (CircleMap)"Ui by A356,A365,FUNCT_2:46,TOPMETR:24;
          then consider Uit being set such that
A368:     Fn1. [x1,TT.i] in Uit and
A369:     Uit in D by A358,TARSKI:def 4;
          reconsider Uit as non empty Subset of R^1 by A368,A369;
          0 <= TT.i & TT.i <= 1 by A321,A330,A333;
          then
A370:     TT.i is Point of I[01] by BORSUK_1:86;
          0 <= TT.(i+1) by A321,A333;
          then TT.(i+1) is Point of I[01] by A343,BORSUK_1:86;
          then
A371:     ZZ is connected by A344,A370,BORSUK_4:49;
          xy is connected by A348,CONNSP_1:29;
          then
A372:     [:xy,ZZ:] is connected by A371,TOPALG_3:17;
A373:     I[01] is SubSpace of I[01] by TSEP_1:2;
          then [:Y|N1,I[01]:] is SubSpace of [:Y,I[01]:] by BORSUK_3:25;
          then
A374:     xZZ is connected by A372,CONNSP_1:24;
A375:     x1 in xy by TARSKI:def 1;
          then
A376:     [x1,TT.i] in xZZ by A366,ZFMISC_1:106;
          then Fn1. [x1,TT.i] in Fn1.:xZZ by FUNCT_2:43;
          then
A377:     Uit meets Fn1.:xZZ by A368,XBOOLE_0:3;
          D is Cover of Fn1.:xZZ
          proof
            let b be set;
            assume b in Fn1.:xZZ;
            then consider a being Point of [:Y|N1,I[01]:] such that
A378:       a in xZZ and
A379:       Fn1.a = b by FUNCT_2:116;
A380:       CircleMap.(Fn1.a) = (CircleMap*Fn1).a by FUNCT_2:21
              .= F.a by A298,A315,A316,FUNCT_1:72;
            [:xy,ZZ:] c= [:N,ZZ:] by A354,ZFMISC_1:118;
            then
A381:       a in [:N,ZZ:] by A378;
            [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            then F.a in F.:[:N,ZZ:] by A10,A381,FUNCT_1:def 12;
            hence b in union D by A356,A358,A379,A380,Lm12,FUNCT_1:def 13
            ,TOPMETR:24;
          end;
          then
A382:     Fn1.:xZZ c= Uit by A295,A369,A374,A377,TOPALG_3:13,TOPS_2:75;

F. [x1,TT.i] = (CircleMap*Fn2). [x1,TT.i] by A296,A362,A363,FUNCT_1:72

.= CircleMap.(Fn2. [x1,TT.i]) by A312,A313,A314,A362,A363,FUNCT_1:23;

then Fn2. [x1,TT.i] in (CircleMap)"Ui by A356,A367,FUNCT_2:46,TOPMETR:24;
          then consider Uith being set such that
A383:     Fn2. [x1,TT.i] in Uith and
A384:     Uith in D by A358,TARSKI:def 4;
          reconsider Uith as non empty Subset of R^1 by A383,A384;
          reconsider XZZ = [:xy,ZZ:] as Subset of [:Y|N2,I[01]:]
          by A313,A314,A351,ZFMISC_1:119;
          [:Y|N2,I[01]:] is SubSpace of [:Y,I[01]:] by A373,BORSUK_3:25;
          then
A385:     XZZ is connected by A372,CONNSP_1:24;
A386:     [:xy,ZZ:] c= [:N2,I:] by A351,ZFMISC_1:119;
          [x1,TT.i] in [:xy,ZZ:] by A348,A353,A366,ZFMISC_1:106;
          then [x1,TT.i] in dom Fn2 by A303,A386;
          then
A387:     Fn2. [x1,TT.i] in Fn2.:xZZ by A376,FUNCT_2:43;
          then
A388:     Uith meets Fn2.:xZZ by A383,XBOOLE_0:3;
          D is Cover of Fn2.:xZZ
          proof
            let b be set;
            assume b in Fn2.:xZZ;
            then consider a being Point of [:Y|N2,I[01]:] such that
A389:       a in xZZ and
A390:       Fn2.a = b by FUNCT_2:116;
A391:       CircleMap.(Fn2.a) = (CircleMap*Fn2).a by FUNCT_2:21
              .= F.a by A296,A313,A314,FUNCT_1:72;
            [:xy,ZZ:] c= [:N,ZZ:] by A354,ZFMISC_1:118;
            then
A392:       a in [:N,ZZ:] by A389;
            [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            then F.a in F.:[:N,ZZ:] by A10,A392,FUNCT_1:def 12;
            hence b in union D by A356,A358,A390,A391,Lm12,FUNCT_1:def 13
            ,TOPMETR:24;
          end;
          then
A393:     Fn2.:xZZ c= Uith by A294,A384,A385,A388,TOPALG_3:13,TOPS_2:75;
          Fn1. [x1,TT.i] = (Fn1 | [:{y},Z:]). [x1,TT.i] by A361,FUNCT_1:72
            .= Fn2. [x1,TT.i] by A332,A361,FUNCT_1:72;
          then Uit meets Uith by A368,A387,A393,XBOOLE_0:3;
          then
A394:     Uit = Uith by A369,A384,TAXONOM2:def 5;
          set f = CircleMap|Uit;
A395:     the carrier of (R^1 | Uit) = Uit by PRE_TOPC:29;
A396:     dom f = Uit by Lm12,RELAT_1:91,TOPMETR:24;
          rng f c= Ui
          proof
            let b be set;
            assume b in rng f;
            then consider a being set such that
A397:       a in dom f and
A398:       f.a = b by FUNCT_1:def 5;
            a in union D by A369,A396,A397,TARSKI:def 4;
            then CircleMap.a in Ui by A358,FUNCT_2:46;
            hence b in Ui by A396,A397,A398,FUNCT_1:72;
          end;
          then reconsider f as Function of R^1 | Uit, TUC | Ui
          by A357,A395,A396,FUNCT_2:4;
          f is_homeomorphism by A359,A369;
          then
A399:     f is one-to-one by TOPS_2:def 5;
A400:     dom (Fn1 | [:xy,ZZ:]) = [:xy,ZZ:] by A315,A316,A317,A352,RELAT_1:91
          ,ZFMISC_1:119;
A401:     dom (Fn2 | [:xy,ZZ:]) = [:xy,ZZ:] by A312,A313,A314,A351,RELAT_1:91
          ,ZFMISC_1:119;
A402:     rng (Fn1 | [:xy,ZZ:]) c= dom f
          proof
            let b be set;
            assume b in rng (Fn1 | [:xy,ZZ:]);
            then consider a being set such that
A403:       a in dom (Fn1 | [:xy,ZZ:]) and
A404:       (Fn1 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
            Fn1.a = b by A400,A403,A404,FUNCT_1:72;
            then b in Fn1.:xZZ by A317,A400,A403,FUNCT_1:def 12;
            hence b in dom f by A382,A396;
          end;
A405:     rng (Fn2 | [:xy,ZZ:]) c= dom f
          proof
            let b be set;
            assume b in rng (Fn2 | [:xy,ZZ:]);
            then consider a being set such that
A406:       a in dom (Fn2 | [:xy,ZZ:]) and
A407:       (Fn2 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
            Fn2.a = b by A401,A406,A407,FUNCT_1:72;
            then b in Fn2.:xZZ by A312,A401,A406,FUNCT_1:def 12;
            hence b in dom f by A393,A394,A396;
          end;
A408:     dom (f*(Fn1 | [:xy,ZZ:])) = dom (Fn1 | [:xy,ZZ:]) by A402,RELAT_1:46;
A409:     dom (f*(Fn2 | [:xy,ZZ:])) = dom (Fn2 | [:xy,ZZ:]) by A405,RELAT_1:46;
          for x being set st x in dom (f*(Fn1 | [:xy,ZZ:])) holds
          (f*(Fn1 | [:xy,ZZ:])).x = (f*(Fn2 | [:xy,ZZ:])).x
          proof
            let x be set such that
A410:       x in dom (f*(Fn1 | [:xy,ZZ:]));
A411:       Fn1.x in Fn1.:[:xy,ZZ:] by A317,A400,A408,A410,FUNCT_1:def 12;
            dom (Fn1 | [:xy,ZZ:]) = dom Fn1 /\ [:xy,ZZ:] by RELAT_1:90;
            then
A412:       x in [:xy,ZZ:] by A408,A410,XBOOLE_0:def 3;
A413:       Fn2.x in Fn2.:[:xy,ZZ:] by A303,A386,A400,A408,A410,FUNCT_1:def 12;
A414:       dom (Fn1 | [:xy,ZZ:]) c= dom Fn1 by RELAT_1:89;

thus (f*(Fn1 | [:xy,ZZ:])).x = ((f*Fn1) | [:xy,ZZ:]).x by RELAT_1:112
              .= (f*Fn1).x by A400,A408,A410,FUNCT_1:72
              .= f.(Fn1.x) by A317,A410,FUNCT_1:23
              .= CircleMap.(Fn1.x) by A382,A411,FUNCT_1:72
              .= (CircleMap*Fn1).x by A317,A410,FUNCT_1:23
              .= F.x by A298,A301,A408,A410,A414,FUNCT_1:72
              .= (CircleMap*Fn2).x by A296,A386,A412,FUNCT_1:72
              .= CircleMap.(Fn2.x) by A303,A386,A412,FUNCT_1:23
              .= f.(Fn2.x) by A393,A394,A413,FUNCT_1:72
              .= (f*Fn2).x by A303,A386,A412,FUNCT_1:23
              .= ((f*Fn2) | [:xy,ZZ:]).x by A400,A408,A410,FUNCT_1:72
              .= (f*(Fn2 | [:xy,ZZ:])).x by RELAT_1:112;
          end;
          then
A415:     f*(Fn1 | [:xy,ZZ:]) = f*(Fn2 | [:xy,ZZ:]) by A400,A401,A408,A409,
          FUNCT_1:9;
          per cases;
          suppose x2 in ZZ;
            then
A416:       x in [:xy,ZZ:] by A350,A375,ZFMISC_1:106;
            thus (Fn1 | [:{y},Z1:]).x = Fn1.x by A345,A347,FUNCT_1:72
              .= (Fn1 | [:xy,ZZ:]).x by A416,FUNCT_1:72

.= (Fn2 | [:xy,ZZ:]).x by A399,A400,A401,A402,A405,A415,FUNCT_1:49
              .= Fn2.x by A416,FUNCT_1:72
              .= (Fn2 | [:{y},Z1:]).x by A345,A347,FUNCT_1:72;
          end;
          suppose not x2 in ZZ;
            then x2 in Z by A349,XBOOLE_0:def 2;
            then
A417:       x in [:{y},Z:] by A348,A350,ZFMISC_1:106;
            thus (Fn1 | [:{y},Z1:]).x = Fn1.x by A345,A347,FUNCT_1:72
              .= (Fn1 | [:{y},Z:]).x by A417,FUNCT_1:72
              .= Fn2.x by A332,A417,FUNCT_1:72
              .= (Fn2 | [:{y},Z1:]).x by A345,A347,FUNCT_1:72;
          end;
        end;
        hence Fn1 | [:{y},Z1:] = Fn2 | [:{y},Z1:] by A345,A346,FUNCT_1:9;
      end;
    end;
    for i being Element of NAT holds M[i] from NAT_1:sch 1(A318,A319);
    then ex Z being non empty Subset of I[01] st
    Z = [.0,TT.len TT.] & Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:] by A311;
    hence Fn1 | [:{y},I:] = Fn2 | [:{y},I:] by A305,BORSUK_1:83;
  end;
  for p being Point of [:Y,I[01]:], V being Subset of R^1
  st G.p in V & V is open holds
  ex W being Subset of [:Y,I[01]:] st p in W & W is open & G.:W c= V
  proof
    let p be Point of [:Y,I[01]:], V be Subset of R^1 such that
A418: G.p in V and
A419: V is open;
    consider y being Point of Y, t being Point of I[01],
    N being non empty open Subset of Y,
    Fn being Function of [:Y|N,I[01]:], R^1 such that
A420: p = [y,t] and
A421: G.p = Fn.p and
A422: y in N and
A423: Fn is continuous and
A424: F | [:N,I:] = CircleMap*Fn and
A425: Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
    for H being Function of [:Y|N,I[01]:], R^1 st H is continuous &
    F | [:N,I:] = CircleMap*H &
    H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H by A281;
A426: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by
    BORSUK_1:def 5
      .= [:N,I:] by PRE_TOPC:29;
    then p in the carrier of [:Y|N,I[01]:] by A420,A422,ZFMISC_1:106;
    then consider W being Subset of [:Y|N,I[01]:] such that
A427: p in W and
A428: W is open and
A429: Fn.:W c= V by A418,A419,A421,A423,JGRAPH_2:20;
A430: dom Fn = [:N,I:] by A426,FUNCT_2:def 1;
    [:Y|N,I[01]:] = [:Y,I[01]:] | [:N,[#]I[01]:] by Lm7,BORSUK_3:26;
    then consider C being Subset of [:Y,I[01]:] such that
A431: C is open and
A432: C /\ [#][:Y|N,I[01]:] = W by A428,TOPS_2:32;
    take WW = C /\ [:N,[#]I[01]:];
A433: [#](Y|N) = N by PRE_TOPC:def 10;
    then
A434: [#][:Y|N,I[01]:] = [:N,[#]I[01]:] by BORSUK_3:1;
    thus p in WW by A427,A432,A433,BORSUK_3:1;
    [:N,[#]I[01]:] is open by BORSUK_1:46;
    hence WW is open by A431,TOPS_1:38;
    let y be set;
    assume y in G.:WW;
    then consider x being Point of [:Y,I[01]:] such that
A435: x in WW and
A436: y = G.x by FUNCT_2:116;
    consider y0 being Point of Y, t0 being Point of I[01],
    N0 being non empty open Subset of Y,
    Fn0 being Function of [:Y|N0,I[01]:], R^1 such that
A437: x = [y0,t0] and
A438: G.x = Fn0.x and
A439: y0 in N0 and
A440: Fn0 is continuous and
A441: F | [:N0,I:] = CircleMap*Fn0 and
A442: Fn0 | [:the carrier of Y,{0}:] = Ft | [:N0,I:] and
    for H being Function of [:Y|N0,I[01]:], R^1 st H is continuous &
    F | [:N0,I:] = CircleMap*H &
    H | [:the carrier of Y,{0}:] = Ft | [:N0,I:] holds Fn0 = H by A281;
    x in [:N,[#]I[01]:] by A435,XBOOLE_0:def 3;
    then
A443: y0 in N by A437,ZFMISC_1:106;
A444: x in [:{y0},I:] by A437,ZFMISC_1:128;
    then Fn.x = (Fn | [:{y0},I:]).x by FUNCT_1:72
      .= (Fn0 | [:{y0},I:]).x by A291,A423,A424,A425,A437,A439,A440,A441,A442
    ,A443
      .= Fn0.x by A444,FUNCT_1:72;
    then y in Fn.:W by A430,A432,A434,A435,A436,A438,FUNCT_1:def 12;
    hence y in V by A429;
  end;
  hence G is continuous by JGRAPH_2:20;
  thus F = CircleMap*G by A283,FUNCT_2:113;
A445: [:the carrier of Y,{0}:] c= [:the carrier of Y,I:] by Lm3,ZFMISC_1:118;
  then
A446: dom Ft = dom G /\ [:the carrier of Y,{0}:] by A6,A282,XBOOLE_1:28;
  for x being set st x in dom Ft holds Ft.x = G.x
  proof
    let x be set;
    assume
A447: x in dom Ft;
    then x in dom G by A6,A282,A445;
    then consider y being Point of Y, t being Point of I[01],
    N being non empty open Subset of Y,
    Fn being Function of [:Y|N,I[01]:], R^1 such that
A448: x = [y,t] and
A449: G.x = Fn.x and
A450: y in N and Fn is continuous & F | [:N,I:] = CircleMap*Fn and
A451: Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
    for H being Function of [:Y|N,I[01]:], R^1 st H is continuous &
    F | [:N,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] =
    Ft | [:N,I:] holds Fn = H by A281;
    x in [:N,I:] by A448,A450,ZFMISC_1:106;
    hence Ft.x = (Ft | [:N,I:]).x by FUNCT_1:72
      .= G.x by A5,A447,A449,A451,Lm14,FUNCT_1:72;
  end;
  hence G | [:the carrier of Y,{0}:] = Ft by A446,FUNCT_1:68;
  let H be Function of [:Y,I[01]:], R^1 such that
A452: H is continuous and
A453: F = CircleMap*H and
A454: H | [:the carrier of Y,{0}:] = Ft;
  for x being Point of [:Y,I[01]:] holds G.x = H.x
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y, t being Point of I[01],
    N being non empty open Subset of Y,
    Fn being Function of [:Y|N,I[01]:], R^1 such that
A455: x = [y,t] and
A456: G.x = Fn.x and
A457: y in N and Fn is continuous & F | [:N,I:] = CircleMap*Fn and
    Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
A458: for H being Function of [:Y|N,I[01]:], R^1 st H is continuous &
    F | [:N,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft | [:N,I:]
    holds Fn = H by A281;
A459: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by
    BORSUK_1:def 5
      .= [:N,I:] by PRE_TOPC:29;
    then
A460: x in the carrier of [:Y|N,I[01]:] by A455,A457,ZFMISC_1:106;
    dom H = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
    then [:N,I:] c= dom H by A4,ZFMISC_1:118;
    then
A461: dom (H | [:N,I:]) = [:N,I:] by RELAT_1:91;
    rng (H | [:N,I:]) c= R;
    then reconsider H1 = H | [:N,I:] as Function of [:Y|N,I[01]:], R^1
    by A459,A461,FUNCT_2:4;
A462: H1 is continuous by A452,TOPALG_3:18;
A463: H | [:N,I:] | [:the carrier of Y,{0}:]
    = H | ([:the carrier of Y,{0}:] /\ [:N,I:]) by RELAT_1:100
      .= Ft | [:N,I:] by A454,RELAT_1:100;
    F | [:N,I:] = CircleMap*(H| [:N,I:]) by A453,RELAT_1:112;
    hence G.x = (H| [:N,I:]).x by A456,A458,A462,A463
      .= H.x by A459,A460,FUNCT_1:72;
  end;
  hence G = H by FUNCT_2:113;
end;

theorem Th23:
  for x0, y0 being Point of Tunit_circle(2), xt being Point of R^1,
  f being Path of x0,y0 st xt in (CircleMap)"{x0}
  ex ft being Function of I[01], R^1 st ft.0 = xt &
  f = CircleMap*ft & ft is continuous & for f1 being Function
  of I[01], R^1 st f1 is continuous & f = CircleMap*f1 &
  f1.0 = xt holds ft = f1
proof
  let x0, y0 be Point of TUC;
  let xt be Point of R^1;
  let f be Path of x0,y0;
  assume xt in (CircleMap)"{x0};
  then
A1: CircleMap.xt in {x0} by FUNCT_2:46;
  set Y = 1TopSp{1};
A2: [#]Y = the carrier of Y;
  deffunc F(set,Element of I) = f.$2;
  consider F being Function of [:the carrier of Y,I:], cS1 such that
A3: for y being Element of Y, i being Element of I
  holds F.(y,i) = F(y,i) from BINOP_1:sch 4;
A4: the carrier of [:Y,I[01]:] = [:the carrier of Y, I:] by BORSUK_1:def 5;
  then reconsider F as Function of [:Y,I[01]:], TUC;
  reconsider j = 1 as Point of Y by TARSKI:def 1;
  for p being Point of [:Y,I[01]:], V being Subset of TUC
  st F.p in V & V is open holds
  ex W being Subset of [:Y,I[01]:] st p in W & W is open & F.:W c= V
  proof
    let p be Point of [:Y,I[01]:], V be Subset of TUC such that
A5: F.p in V and
A6: V is open;
    consider p1 being Point of Y, p2 being Point of I[01] such that
A7: p = [p1,p2] by BORSUK_1:50;
    F.(p1,p2) = f.p2 by A3;
    then consider S being Subset of I[01] such that
A8: p2 in S and
A9: S is open and
A10: f.:S c= V by A5,A6,A7,JGRAPH_2:20;
    set W = [:{1},S:];
    W c= [:the carrier of Y,I:] by ZFMISC_1:118;
    then reconsider W as Subset of [:Y,I[01]:] by BORSUK_1:def 5;
    take W;
    thus p in W by A7,A8,ZFMISC_1:106;
    thus W is open by A2,A9,BORSUK_1:46;
    let y be set;
    assume y in F.:W;
    then consider x being set such that
A11: x in the carrier of [:Y,I[01]:] and
A12: x in W and
A13: y = F.x by FUNCT_2:115;
    consider x1 being Point of Y, x2 being Point of I[01] such that
A14: x = [x1,x2] by A11,BORSUK_1:50;
A15: y = F.(x1,x2) by A13,A14
      .= f.x2 by A3;
    x2 in S by A12,A14,ZFMISC_1:106;
    then f.x2 in f.:S by FUNCT_2:43;
    hence y in V by A10,A15;
  end;
  then
A16: F is continuous by JGRAPH_2:20;
A17: dom F = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
  then
A18: [:the carrier of Y,{0}:] c= dom F by A4,Lm3,ZFMISC_1:118;
  then
A19: dom (F | [:the carrier of Y,{0}:]) = [:the carrier of Y,{0}:]
  by RELAT_1:91;
A20: the carrier of [:Y,Sspace(0[01]):] =
  [:the carrier of Y, the carrier of Sspace(0[01]):] by BORSUK_1:def 5;
  set Ft = [:Y,Sspace(0[01]):] --> xt;
A21: dom (CircleMap*Ft) = [:the carrier of Y,{0}:] by A20,Lm14,FUNCT_2:def 1;
A22: for y being Element of Y, i being Element of {0} holds
  Ft. [y,i] = xt by A20,Lm14,FUNCOP_1:13;
  for x being set st x in dom (CircleMap*Ft) holds
  (F | [:the carrier of Y,{0}:]).x = (CircleMap*Ft).x
  proof
    let x be set such that
A23: x in dom (CircleMap*Ft);
    consider x1, x2 being set such that
A24: x1 in the carrier of Y and
A25: x2 in {0} and
A26: x = [x1,x2] by A20,A23,Lm14,ZFMISC_1:def 2;
A27: x2 = 0 by A25,TARSKI:def 1;
    thus (F | [:the carrier of Y,{0}:]).x
    = F.(x1,x2) by A20,A23,A26,Lm14,FUNCT_1:72
      .= f.x2 by A3,A24,A27,Lm2
      .= x0 by A27,BORSUK_2:def 4
      .= CircleMap.xt by A1,TARSKI:def 1
      .= CircleMap.(Ft.x) by A22,A24,A25,A26
      .= (CircleMap*Ft).x by A23,FUNCT_1:22;
  end;
  then F | [:the carrier of Y,{0}:] = CircleMap*Ft by A19,A21,FUNCT_1:9;
  then consider G being Function of [:Y,I[01]:], R^1 such that
A28: G is continuous and
A29: F = CircleMap*G and
A30: G | [:the carrier of Y,{0}:] = Ft and
A31: for H being Function
  of [:Y,I[01]:], R^1 st H is continuous & F = CircleMap*H &
  H | [:the carrier of Y,{0}:] = Ft holds G = H by A16,Th22;
  take ft = Prj2(j,G);
A32: [j,j0] in [:the carrier of Y,{0}:] by Lm4,ZFMISC_1:106;
  thus ft.0 = G.(j,j0) by Def3
    .= Ft. [j,j0] by A30,A32,FUNCT_1:72
    .= xt by A22,Lm4;
  for i being Point of I[01] holds f.i = (CircleMap*ft).i
  proof
    let i be Point of I[01];
A33: the carrier of [:Y,I[01]:] = [:the carrier of Y, the carrier of I[01]:]
    by BORSUK_1:def 5;
    thus (CircleMap*ft).i = CircleMap.(ft.i) by FUNCT_2:21
      .= CircleMap.(G.(j,i)) by Def3
      .= (CircleMap*G).(j,i) by A33,BINOP_1:30
      .= f.i by A3,A29;
  end;
  hence f = CircleMap*ft by FUNCT_2:113;
  for H being continuous Function of [:Y,I[01]:], R^1 holds
  Prj2(j,H) is continuous;
  hence ft is continuous by A28;
  let f1 be Function of I[01], R^1;
  assume that
A34: f1 is continuous and
A35: f = CircleMap*f1 and
A36: f1.0 = xt;
  deffunc H(set,Element of I) = f1.$2;
  consider H being Function of [:the carrier of Y,I:], R such that
A37: for y being Element of Y, i being Element of I
  holds H.(y,i) = H(y,i) from BINOP_1:sch 4;
  reconsider H as Function of [:Y,I[01]:], R^1 by A4;
  for x being Point of [:Y,I[01]:] holds F.x = (CircleMap*H).x
  proof
    let x be Point of [:Y,I[01]:];
    consider x1 being Point of Y, x2 being Point of I[01] such that
A38: x = [x1,x2] by BORSUK_1:50;
    thus (CircleMap*H).x = CircleMap.(H.(x1,x2)) by A38,FUNCT_2:21
      .= CircleMap.(f1.x2) by A37
      .= (CircleMap*f1).x2 by FUNCT_2:21
      .= F.(x1,x2) by A3,A35
      .= F.x by A38;
  end;
  then
A39: F = CircleMap*H by FUNCT_2:113;
  for p being Point of [:Y,I[01]:], V being Subset of R^1
  st H.p in V & V is open holds
  ex W being Subset of [:Y,I[01]:] st p in W & W is open & H.:W c= V
  proof
    let p be Point of [:Y,I[01]:], V be Subset of R^1 such that
A40: H.p in V and
A41: V is open;
    consider p1 being Point of Y, p2 being Point of I[01] such that
A42: p = [p1,p2] by BORSUK_1:50;
    H.p = H.(p1,p2) by A42
      .= f1.p2 by A37;
    then consider W being Subset of I[01] such that
A43: p2 in W and
A44: W is open and
A45: f1.:W c= V by A34,A40,A41,JGRAPH_2:20;
    take W1 = [:[#]Y,W:];
    thus p in W1 by A42,A43,ZFMISC_1:106;
    thus W1 is open by A44,BORSUK_1:46;
    let y be set;
    assume y in H.:W1;
    then consider c being Element of [:Y,I[01]:] such that
A46: c in W1 and
A47: y = H.c by FUNCT_2:116;
    consider c1 being set, c2 being set such that
A48: c1 in [#]Y and
A49: c2 in W and
A50: c = [c1,c2] by A46,ZFMISC_1:def 2;
A51: H.c = H.(c1,c2) by A50
      .= f1.c2 by A37,A48,A49;
    f1.c2 in f1.:W by A49,FUNCT_2:43;
    hence y in V by A45,A47,A51;
  end;
  then
A52: H is continuous by JGRAPH_2:20;
  for i being Point of I[01] holds ft.i = f1.i
  proof
    let i be Point of I[01];
    dom H = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
    then
A53: dom (H | [:the carrier of Y,{0}:]) = [:the carrier of Y,{0}:]
    by A17,A18,RELAT_1:91;
A54: dom Ft = [:the carrier of Y,{0}:] by A20,Lm14,FUNCT_2:def 1;
    for x being set st x in dom (H | [:the carrier of Y,{0}:]) holds
    (H | [:the carrier of Y,{0}:]).x = Ft.x
    proof
      let x be set;
      assume
A55:  x in dom (H | [:the carrier of Y,{0}:]);
      then consider x1, x2 being set such that
A56:  x1 in the carrier of Y and
A57:  x2 in {0} and
A58:  x = [x1,x2] by A53,ZFMISC_1:def 2;
A59:  x2 = j0 by A57,TARSKI:def 1;
      thus (H | [:the carrier of Y,{0}:]).x = H.(x1,x2) by A55,A58,FUNCT_1:70
        .= f1.x2 by A37,A56,A59
        .= Ft.x by A22,A36,A56,A57,A58,A59;
    end;
    then
A60: H | [:the carrier of Y,{0}:] = Ft by A53,A54,FUNCT_1:9;
    thus ft.i = G.(j,i) by Def3
      .= H.(j,i) by A31,A39,A52,A60
      .= f1.i by A37;
  end;
  hence ft = f1 by FUNCT_2:113;
end;

theorem Th24:
  for x0, y0 being Point of Tunit_circle(2), P, Q being Path of x0,y0,
  F being Homotopy of P,Q,
  xt being Point of R^1 st P,Q are_homotopic & xt in (CircleMap)"{x0}
  ex yt being Point of R^1, Pt, Qt being Path of xt,yt,
  Ft being Homotopy of Pt,Qt st Pt,Qt are_homotopic & F = CircleMap*Ft &
  yt in (CircleMap)"{y0} &
  for F1 being Homotopy of Pt,Qt st F = CircleMap*F1 holds Ft = F1
proof
  let x0, y0 be Point of TUC;
  let P, Q be Path of x0,y0;
  let F be Homotopy of P,Q;
  let xt be Point of R^1;
  assume
A1: P,Q are_homotopic;
  assume
A2: xt in (CircleMap)"{x0};
  then consider ft being Function of I[01], R^1 such that
A3: ft.0 = xt and
A4: P = CircleMap*ft and
A5: ft is continuous and for f1 being Function
  of I[01], R^1 st f1 is continuous & P = CircleMap*f1 &
  f1.0 = xt holds ft = f1 by Th23;
A6: F is continuous by A1,BORSUK_6:def 13;
  CircleMap.xt in {x0} by A2,FUNCT_2:46;
  then
A7: CircleMap.xt = x0 by TARSKI:def 1;
  defpred P[set,set,set] means $3 = ft.$1;
A8: for x being Element of I for y being Element of {0}
  ex z being Element of REAL st P[x,y,z];
  consider Ft being Function of [:I,{0}:], REAL such that
A9: for y being Element of I, i being Element of {0} holds
  P[y,i, Ft.(y,i)] from BINOP_1:sch 3(A8);
A10: dom F = the carrier of [:I[01],I[01]:] by FUNCT_2:def 1;
  then
A11: [:I,{0}:] c= dom F by Lm3,Lm5,ZFMISC_1:118;
  then
A12: dom (F | [:I,{0}:]) = [:I,{0}:] by RELAT_1:91;
A13: dom Ft = [:I,{0}:] by FUNCT_2:def 1;
  rng Ft c= REAL;
  then
A14: dom (CircleMap*Ft) = dom Ft by Lm12,RELAT_1:46;
A15: the carrier of [:I[01],Sspace(0[01]):] =
  [:I, the carrier of Sspace(0[01]):] by BORSUK_1:def 5;
  then reconsider Ft as Function
  of [:I[01],Sspace(0[01]):], R^1 by Lm14,TOPMETR:24;
  for p being Point of [:I[01],Sspace(0[01]):], V being Subset of R^1
  st Ft.p in V & V is open holds
  ex W being Subset of [:I[01],Sspace(0[01]):] st
  p in W & W is open & Ft.:W c= V
  proof
    let p be Point of [:I[01],Sspace(0[01]):], V be Subset of R^1 such that
A16: Ft.p in V and
A17: V is open;
    consider p1 being Point of I[01],
    p2 being Point of Sspace(0[01]) such that
A18: p = [p1,p2] by A15,DOMAIN_1:9;
    P[p1,p2,Ft.(p1,p2)] by A9,Lm14;
    then consider W1 being Subset of I[01] such that
A19: p1 in W1 and
A20: W1 is open and
A21: ft.:W1 c= V by A5,A16,A17,A18,JGRAPH_2:20;
    reconsider W1 as non empty Subset of I[01] by A19;
    take W = [:W1,[#]Sspace(0[01]):];
    thus p in W by A18,A19,ZFMISC_1:def 2;
    thus W is open by A20,BORSUK_1:46;
    let y be set;
    assume y in Ft.:W;
    then consider x being Element of [:I[01],Sspace(0[01]):] such that
A22: x in W and
A23: y = Ft.x by FUNCT_2:116;
    consider x1 being Element of W1, x2 being Point of Sspace(0[01]) such that
A24: x = [x1,x2] by A22,DOMAIN_1:9;
A25: P[x1,x2,Ft.(x1,x2)] by A9,Lm14;
    ft.x1 in ft.:W1 by FUNCT_2:43;
    hence y in V by A21,A23,A24,A25;
  end;
  then
A26: Ft is continuous by JGRAPH_2:20;
  for x being set st x in dom (CircleMap*Ft) holds (F | [:I,{0}:]).x =
  (CircleMap*Ft).x
  proof
    let x be set such that
A27: x in dom (CircleMap*Ft);
    consider x1, x2 being set such that
A28: x1 in I and
A29: x2 in {0} and
A30: x = [x1,x2] by A15,A27,Lm14,ZFMISC_1:def 2;
    x2 = 0 by A29,TARSKI:def 1;
    hence (F | [:I,{0}:]).x = F.(x1,0) by A15,A27,A30,Lm14,FUNCT_1:72
      .= (CircleMap*ft).x1 by A1,A4,A28,BORSUK_6:def 13
      .= CircleMap.(ft.x1) by A28,FUNCT_2:21
      .= CircleMap.(Ft.(x1,x2)) by A9,A28,A29
      .= (CircleMap*Ft).x by A27,A30,FUNCT_1:22;
  end;
  then F | [:I,{0}:] = CircleMap*Ft by A12,A13,A14,FUNCT_1:9;
  then consider G being Function of [:I[01],I[01]:], R^1 such that
A31: G is continuous and
A32: F = CircleMap*G and
A33: G | [:I,{0}:] = Ft and
A34: for H being Function of [:I[01],I[01]:], R^1 st H is continuous &
  F = CircleMap*H &
  H | [:the carrier of I[01],{0}:] = Ft holds G = H by A6,A26,Th22;
  take yt = ft.j1;
  reconsider ft as Path of xt,yt by A3,A5,BORSUK_2:def 4;
  set sM0 = Prj2(j0,G);
  set sM1 = Prj2(j1,G);
A35: [j0,j0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
A36: [j1,j0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
A37: sM0.0 = G.(j0,j0) by Def3
    .= Ft.(j0,j0) by A33,A35,FUNCT_1:72
    .= xt by A3,A9,Lm4;
A38: for H being continuous Function of [:I[01],I[01]:], R^1 holds
  Prj2(j0,H) is continuous;
A39: sM1.0 = G.(j1,j0) by Def3
    .= Ft.(j1,j0) by A33,A36,FUNCT_1:72
    .= yt by A9,Lm4;
A40: for H being continuous Function of [:I[01],I[01]:], R^1 holds
  Prj2(j1,H) is continuous;
  consider cP1 being constant Loop of x0;
  consider ft1 being Function of I[01], R^1 such that
  ft1.0 = xt & cP1 = CircleMap*ft1 & ft1 is continuous and
A41: for f1 being Function
  of I[01], R^1 st f1 is continuous & cP1 = CircleMap*f1 &
  f1.0 = xt holds ft1 = f1 by A2,Th23;
  for x being Point of I[01] holds cP1.x = (CircleMap*sM0).x
  proof
    let x be Point of I[01];
    thus (CircleMap*sM0).x = CircleMap.(sM0.x) by FUNCT_2:21
      .= CircleMap.(G.(j0,x)) by Def3
      .= (CircleMap*G).(j0,x) by Lm5,BINOP_1:30
      .= x0 by A1,A32,BORSUK_6:def 13
      .= cP1.x by TOPALG_3:22;
  end;
  then cP1 = CircleMap*sM0 by FUNCT_2:113;
  then
A42: ft1 = sM0 by A31,A37,A38,A41;
  set g1 = I[01] --> xt;
A43: g1.j0 = xt by TOPALG_3:5;
  for x being Point of I[01] holds cP1.x = (CircleMap*g1).x
  proof
    let x be Point of I[01];
    thus cP1.x = x0 by TOPALG_3:22
      .= CircleMap.(g1.x) by A7,TOPALG_3:5
      .= (CircleMap*g1).x by FUNCT_2:21;
  end;
  then cP1 = CircleMap*g1 by FUNCT_2:113;
  then
A44: ft1 = g1 by A41,A43;
  consider cP2 being constant Loop of y0;
A45: CircleMap.yt = P.j1 by A4,FUNCT_2:21
    .= y0 by BORSUK_2:def 4;
  then
A46: CircleMap.yt in {y0} by TARSKI:def 1;
  then yt in (CircleMap)"{y0} by FUNCT_2:46;
  then consider ft2 being Function of I[01], R^1 such that
  ft2.0 = yt & cP2 = CircleMap*ft2 & ft2 is continuous and
A47: for f1 being Function
  of I[01], R^1 st f1 is continuous & cP2 = CircleMap*f1 &
  f1.0 = yt holds ft2 = f1 by Th23;
  for x being Point of I[01] holds cP2.x = (CircleMap*sM1).x
  proof
    let x be Point of I[01];
    thus (CircleMap*sM1).x = CircleMap.(sM1.x) by FUNCT_2:21
      .= CircleMap.(G.(j1,x)) by Def3
      .= (CircleMap*G).(j1,x) by Lm5,BINOP_1:30
      .= y0 by A1,A32,BORSUK_6:def 13
      .= cP2.x by TOPALG_3:22;
  end;
  then cP2 = CircleMap*sM1 by FUNCT_2:113;
  then
A48: ft2 = sM1 by A31,A39,A40,A47;
  set g2 = I[01] --> yt;
A49: g2.j0 = yt by TOPALG_3:5;
  for x being Point of I[01] holds cP2.x = (CircleMap*g2).x
  proof
    let x be Point of I[01];
    thus cP2.x = y0 by TOPALG_3:22
      .= CircleMap.(g2.x) by A45,TOPALG_3:5
      .= (CircleMap*g2).x by FUNCT_2:21;
  end;
  then cP2 = CircleMap*g2 by FUNCT_2:113;
  then
A50: ft2 = g2 by A47,A49;
  set Qt = Prj1(j1,G);
  Qt is Path of xt,yt
  proof
    for H being continuous Function of [:I[01],I[01]:], R^1 holds
    Prj1(j1,H) is continuous;
    hence Qt is continuous by A31;
    thus Qt.0 = G.(j0,j1) by Def2
      .= sM0.j1 by Def3
      .= xt by A42,A44,TOPALG_3:5;
    thus Qt.1 = G.(j1,j1) by Def2
      .= sM1.j1 by Def3
      .= yt by A48,A50,TOPALG_3:5;
  end;
  then reconsider Qt as Path of xt,yt;
  take ft, Qt;
A51: now
    let s be Point of I[01];
    [s,0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
    hence G.(s,0) = Ft.(s,j0) by A33,FUNCT_1:72
      .= ft.s by A9,Lm4;
    thus G.(s,1) = Qt.s by Def2;
    let t be Point of I[01];
    thus G.(0,t) = sM0.t by Def3
      .= xt by A42,A44,TOPALG_3:5;
    thus G.(1,t) = sM1.t by Def3
      .= yt by A48,A50,TOPALG_3:5;
  end;
  G is Homotopy of ft,Qt
  proof
    thus ft,Qt are_homotopic by A31,A51,BORSUK_2:def 7;
    thus thesis by A31,A51;
  end;
  then reconsider G as Homotopy of ft,Qt;
  take G; thus
A52: ft,Qt are_homotopic by A31,A51,BORSUK_2:def 7;
  thus F = CircleMap*G by A32;
  thus yt in (CircleMap)"{y0} by A46,FUNCT_2:46;
  let F1 be Homotopy of ft,Qt such that
A53: F = CircleMap*F1;
  dom F1 = the carrier of [:I[01],I[01]:] by FUNCT_2:def 1;
  then
A54: dom (F1 | [:I,{0}:]) = [:I,{0}:] by A10,A11,RELAT_1:91;
  for x being set st x in dom (F1 | [:I,{0}:]) holds (F1 | [:I,{0}:]).x = Ft.x
  proof
    let x be set;
    assume
A55: x in dom (F1 | [:I,{0}:]);
    then consider x1, x2 being set such that
A56: x1 in I and
A57: x2 in {0} and
A58: x = [x1,x2] by A54,ZFMISC_1:def 2;
A59: x2 = 0 by A57,TARSKI:def 1;
    thus (F1 | [:I,{0}:]).x = F1.(x1,x2) by A55,A58,FUNCT_1:70
      .= ft.x1 by A52,A56,A59,BORSUK_6:def 13
      .= Ft.(x1,x2) by A9,A56,A57
      .= Ft.x by A58;
  end;
  then F1 | [:I,{0}:] = Ft by A13,A54,FUNCT_1:9;
  hence G = F1 by A34,A53;
end;

definition
  func Ciso -> Function of INT.Group, pi_1(Tunit_circle(2),c[10]) means
  :Def5:
  for n being Integer holds
  it.n = Class(EqRel(Tunit_circle(2),c[10]),cLoop(n));
  existence
  proof
    defpred P[Integer,set] means $2 = Class(EqRel(TUC,c[10]),cLoop($1));
A1: for x being Element of INT ex y being Element of pi_1(TUC,c[10]) st P[x,y]
    proof
      let x be Element of INT;
      reconsider y = Class(EqRel(TUC,c[10]),cLoop(x))
      as Element of pi_1(TUC,c[10]) by TOPALG_1:48;
      take y;
      thus P[x,y];
    end;
    consider f being Function of INT,the carrier of pi_1(TUC,c[10]) such that
A2: for x being Element of INT holds P[x,f.x] from FUNCT_2:sch 3(A1);
    reconsider f as Function of INT.Group, pi_1(TUC,c[10]);
    take f;
    let n be Integer;
    n in INT by INT_1:def 2;
    hence thesis by A2;
  end;
  uniqueness
  proof
    let f, g be Function of INT.Group, pi_1(TUC,c[10]) such that
A3: for n being Integer holds f.n = Class(EqRel(TUC,c[10]),cLoop(n)) and
A4: for n being Integer holds g.n = Class(EqRel(TUC,c[10]),cLoop(n));
    for x being Element of INT.Group holds f.x = g.x
    proof
      let x be Element of INT.Group;
      thus f.x = Class(EqRel(TUC,c[10]),cLoop(x)) by A3
        .= g.x by A4;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem Th25:
  for i being Integer, f being Path of R^1(0),R^1(i) holds
  Ciso.i = Class(EqRel(Tunit_circle(2),c[10]),CircleMap*f)
proof
  let i be Integer;
  let f be Path of R^1(0),R^1(i);
  set P = CircleMap*f;
  P is Loop of c[10]
  proof
    thus P is continuous by TOPS_2:58;
    thus P.0 = CircleMap.(f.j0) by FUNCT_2:21
      .= CircleMap.R^1(0) by BORSUK_2:def 4
      .= CircleMap.0 by TOPREALB:def 2
      .= |[ cos(2*PI*0), sin(2*PI*0) ]| by TOPREALB:def 11
      .= c[10] by SIN_COS:34,TOPREALB:def 8;
    thus P.1 = CircleMap.(f.j1) by FUNCT_2:21
      .= CircleMap.R^1(i) by BORSUK_2:def 4
      .= CircleMap.i by TOPREALB:def 2
      .= |[ cos(2*PI*i+0), sin(2*PI*i+0) ]| by TOPREALB:def 11
      .= |[ cos(0), sin(2*PI*i+0) ]| by COMPLEX2:10
      .= c[10] by COMPLEX2:9,SIN_COS:34,TOPREALB:def 8;
  end;
  then reconsider P as Loop of c[10];
A1: cLoop(i) = CircleMap * ExtendInt(i) by Th20;
A2: cLoop(i),P are_homotopic
  proof
    reconsider J = R^1 as non empty convex SubSpace of R^1;
    reconsider r0 = R^1(0), ri = R^1(i) as Point of J;
    reconsider O = ExtendInt(i), ff = f as Path of r0,ri;
    reconsider G = R1Homotopy(O,ff) as Function of [:I[01],I[01]:],R^1;
    take F = CircleMap*G;
    thus F is continuous by TOPS_2:58;
    let s be Point of I[01];
    thus F.(s,0) = CircleMap.(G.(s,j0)) by Lm5,BINOP_1:30
      .= CircleMap.((1-j0) * (ExtendInt(i)).s + j0 * f.s) by TOPALG_2:def 5
      .= (cLoop(i)).s by A1,FUNCT_2:21;
    thus F.(s,1) = CircleMap.(G.(s,j1)) by Lm5,BINOP_1:30
      .= CircleMap.((1-j1) * (ExtendInt(i)).s + j1 * f.s) by TOPALG_2:def 5
      .= P.s by FUNCT_2:21;
    thus F.(0,s) = CircleMap.(G.(j0,s)) by Lm5,BINOP_1:30
      .= CircleMap.((1-s) * (ExtendInt(i)).j0 + s * f.j0) by TOPALG_2:def 5
      .= CircleMap.((1-s) * R^1(0) + s * f.j0) by BORSUK_2:def 4
      .= CircleMap.((1-s) * R^1(0) + s * R^1(0)) by BORSUK_2:def 4
      .= CircleMap.((1-s) * 0 + s * 0) by TOPREALB:def 2
      .= c[10] by TOPREALB:33;
    thus F.(1,s) = CircleMap.(G.(j1,s)) by Lm5,BINOP_1:30
      .= CircleMap.((1-s) * (ExtendInt(i)).j1 + s * f.j1) by TOPALG_2:def 5
      .= CircleMap.((1-s) * R^1(i) + s * f.j1) by BORSUK_2:def 4
      .= CircleMap.((1-s) * R^1(i) + s * R^1(i)) by BORSUK_2:def 4
      .= CircleMap.i by TOPREALB:def 2
      .= c[10] by TOPREALB:33;
  end;
  thus Ciso.i = Class(EqRel(TUC,c[10]),cLoop(i)) by Def5
    .= Class(EqRel(TUC,c[10]),CircleMap*f) by A2,TOPALG_1:47;
end;

definition
  redefine func Ciso -> Homomorphism of INT.Group, pi_1(Tunit_circle(2),c[10]);
  coherence
  proof
    set f = Ciso;
    now
      let x, y be Element of INT.Group;
      consider fX, fY being Loop of c[10] such that
A1:   f.x = Class(EqRel(TUC,c[10]),fX) and
A2:   f.y = Class(EqRel(TUC,c[10]),fY) and
A3:   (the multF of pi_1(TUC,c[10])).(f.x,f.y) =
      Class(EqRel(TUC,c[10]),fX+fY) by TOPALG_1:def 3;
      reconsider tx = AffineMap(1,x) as Function of R^1,R^1 by TOPMETR:24;
      set p = tx*ExtendInt(y);
      p is Path of R^1(x),R^1(x+y)
      proof
        x is Real by XREAL_0:def 1;
        then tx is_homeomorphism by JORDAN16:35;
        then tx is continuous by TOPS_2:def 5;
        hence p is continuous by TOPS_2:58;
        thus p.0 = tx.((ExtendInt(y)).j0) by FUNCT_2:21
          .= tx.(y*j0) by Def1
          .= 1*0+x by JORDAN16:def 3
          .= R^1(x) by TOPREALB:def 2;
        thus p.1 = tx.((ExtendInt(y)).j1) by FUNCT_2:21
          .= tx.(y*j1) by Def1
          .= 1*y+x by JORDAN16:def 3
          .= R^1(x+y) by TOPREALB:def 2;
      end;
      then reconsider p as Path of R^1(x),R^1(x+y);
      f.x = Class(EqRel(TUC,c[10]),cLoop(x)) by Def5;
      then
A4:   fX,cLoop(x) are_homotopic by A1,TOPALG_1:47;
      f.y = Class(EqRel(TUC,c[10]),cLoop(y)) by Def5;
      then fY,cLoop(y) are_homotopic by A2,TOPALG_1:47;
      then
A5:   fX+fY,cLoop(x)+cLoop(y) are_homotopic by A4,BORSUK_6:84;
A6:   for a being Point of I[01] holds
      (CircleMap * (ExtendInt(x)+p)).a = (cLoop(x)+cLoop(y)).a
      proof
        let a be Point of I[01];
        per cases;
        suppose
A7:       a <= 1/2;
          then
A8:       2*a is Point of I[01] by BORSUK_6:6;

thus (CircleMap * (ExtendInt(x)+p)).a = CircleMap.((ExtendInt(x)+p).a)
          by FUNCT_2:21
            .= CircleMap.((ExtendInt(x)).(2*a)) by A7,BORSUK_6:def 4
            .= CircleMap.(x*(2*a)) by A8,Def1

.= |[ cos(2*PI*(x*(2*a))), sin(2*PI*(x*(2*a))) ]| by TOPREALB:def 11
            .= |[ cos(2*PI*x*(2*a)), sin(2*PI*x*(2*a)) ]|
            .= (cLoop(x)).(2*a) by A8,Def4
            .= (cLoop(x)+cLoop(y)).a by A7,BORSUK_6:def 4;
        end;
        suppose
A9:       1/2 <= a;
          then
A10:      2*a-1 is Point of I[01] by BORSUK_6:7;

thus (CircleMap * (ExtendInt(x)+p)).a = CircleMap.((ExtendInt(x)+p).a)
          by FUNCT_2:21
            .= CircleMap.(p.(2*a-1)) by A9,BORSUK_6:def 4
            .= CircleMap.(tx.((ExtendInt(y)).(2*a-1))) by A10,FUNCT_2:21
            .= CircleMap.(tx.(y*(2*a-1))) by A10,Def1
            .= CircleMap.(1*(y*(2*a-1))+x) by JORDAN16:def 3
            .= |[ cos(2*PI*((y*(2*a-1))+x)), sin(2*PI*((y*(2*a-1))+x)) ]|
          by TOPREALB:def 11
            .= |[ cos(2*PI*(y*(2*a-1))), sin(2*PI*(y*(2*a-1))+2*PI*x) ]|
          by COMPLEX2:10
            .= |[ cos(2*PI*y*(2*a-1)), sin(2*PI*y*(2*a-1)) ]| by COMPLEX2:9
            .= (cLoop(y)).(2*a-1) by A10,Def4
            .= (cLoop(x)+cLoop(y)).a by A9,BORSUK_6:def 4;
        end;
      end;
      thus f.(x*y) = Class(EqRel(TUC,c[10]),CircleMap*(ExtendInt(x)+p)) by Th25
        .= Class(EqRel(TUC,c[10]),cLoop(x)+cLoop(y)) by A6,FUNCT_2:113
        .= f.x * f.y by A3,A5,TOPALG_1:47;
    end;
    hence thesis by GROUP_6:def 7;
  end;
end;

registration
  cluster Ciso -> one-to-one onto;
  coherence
  proof
A1: dom Ciso = INT by FUNCT_2:def 1;
    thus Ciso is one-to-one
    proof
      let m, n be set;
      assume that
A2:   m in dom Ciso & n in dom Ciso and
A3:   Ciso.m = Ciso.n;
      reconsider m, n as Integer by A2,INT_1:def 2;
      Ciso.m = Class(EqRel(TUC,c[10]),cLoop(m)) &
      Ciso.n = Class(EqRel(TUC,c[10]),cLoop(n)) by Def5;
      then
A4:   cLoop(m),cLoop(n) are_homotopic by A3,TOPALG_1:47;
      then consider F being Function of [:I[01],I[01]:], TUC such that
A5:   F is continuous and
A6:   for s being Point of I[01] holds
      F.(s,0) = (cLoop(m)).s & F.(s,1) = (cLoop(n)).s &
      for t being Point of I[01] holds F.(0,t) = c[10] & F.(1,t) = c[10]
      by BORSUK_2:def 7;
A7:   F is Homotopy of cLoop(m),cLoop(n) by A4,A5,A6,BORSUK_6:def 13;
      set xt = R^1(0);
A8:   xt in (CircleMap)"{c[10]} by Lm1,TOPREALB:34,def 2;
      then consider yt being Point of R^1, Pt, Qt being Path of xt,yt,
      Ft being Homotopy of Pt,Qt such that
A9:   Pt,Qt are_homotopic and
A10:  F = CircleMap*Ft and yt in (CircleMap)"{c[10]} and
      for F1 being Homotopy of Pt,Qt st F = CircleMap*F1 holds Ft = F1
      by A4,A7,Th24;
      consider ftm being Function of I[01], R^1 such that ftm.0 = xt and
      cLoop(m) = CircleMap*ftm and ftm is continuous and
A11:  for f1 being Function of I[01], R^1 st f1 is continuous &
      cLoop(m) = CircleMap*f1 & f1.0 = xt holds ftm = f1 by A8,Th23;
      consider ftn being Function of I[01], R^1 such that ftn.0 = xt and
      cLoop(n) = CircleMap*ftn and ftn is continuous and
A12:  for f1 being Function of I[01], R^1 st f1 is continuous &
      cLoop(n) = CircleMap*f1 & f1.0 = xt holds ftn = f1 by A8,Th23;
      set ft0 = Prj1(j0,Ft);
      set ft1 = Prj1(j1,Ft);
A13:  (ExtendInt(m)).0 = m*j0 by Def1
        .= xt by TOPREALB:def 2;
A14:  ft0.0 = Ft.(j0,j0) by Def2
        .= xt by A9,BORSUK_6:def 13;
      cLoop(m) = CircleMap*ExtendInt(m) by Th20;
      then
A15:  ExtendInt(m) = ftm by A11,A13;
      now
        let x be Point of I[01];
        thus (CircleMap*ft0).x = CircleMap.(ft0.x) by FUNCT_2:21
          .= CircleMap.(Ft.(x,j0)) by Def2
          .= F.(x,j0) by A10,Lm5,BINOP_1:30
          .= (cLoop(m)).x by A6;
      end;
      then cLoop(m) = CircleMap*ft0 by FUNCT_2:113;
      then ft0 = ftm by A11,A14;
      then
A16:  ft0.j1 = m*1 by A15,Def1;
A17:  (ExtendInt(n)).0 = n*j0 by Def1
        .= xt by TOPREALB:def 2;
      cLoop(n) = CircleMap*ExtendInt(n) by Th20;
      then
A18:  ExtendInt(n) = ftn by A12,A17;
A19:  ft1.0 = Ft.(j0,j1) by Def2
        .= xt by A9,BORSUK_6:def 13;
      now
        let x be Point of I[01];
        thus (CircleMap*ft1).x = CircleMap.(ft1.x) by FUNCT_2:21
          .= CircleMap.(Ft.(x,j1)) by Def2
          .= F.(x,j1) by A10,Lm5,BINOP_1:30
          .= (cLoop(n)).x by A6;
      end;
      then cLoop(n) = CircleMap*ft1 by FUNCT_2:113;
      then ft1 = ftn by A12,A19;
      then
A20:  ft1.j1 = n*1 by A18,Def1;
      ft0.j1 = Ft.(j1,j0) by Def2
        .= yt by A9,BORSUK_6:def 13
        .= Ft.(j1,j1) by A9,BORSUK_6:def 13
        .= ft1.j1 by Def2;
      hence thesis by A16,A20;
    end;
    thus rng Ciso c= the carrier of pi_1(TUC,c[10]);
    let q be set;
    assume q in the carrier of pi_1(TUC,c[10]);
    then consider f being Loop of c[10] such that
A21: q = Class(EqRel(TUC,c[10]),f) by TOPALG_1:48;
    R^1(0) in (CircleMap)"{c[10]} by Lm1,TOPREALB:34,def 2;
    then consider ft being Function of I[01], R^1 such that
A22: ft.0 = R^1(0) and
A23: f = CircleMap*ft and
A24: ft is continuous and for f1 being Function
    of I[01], R^1 st f1 is continuous & f = CircleMap*f1 &
    f1.0 = R^1(0) holds ft = f1 by Th23;
    CircleMap.(ft.j1) = (CircleMap*ft).j1 by FUNCT_2:21
      .= c[10] by A23,BORSUK_2:def 4;
    then CircleMap.(ft.1) in {c[10]} by TARSKI:def 1;
    then
A25: ft.1 in INT by Lm12,FUNCT_1:def 13,TOPREALB:34;
    then
A26: ft.1 is Integer by INT_1:def 2;
    ft is Path of R^1(0),R^1(ft.1)
    proof
      thus ft is continuous by A24;
      thus ft.0 = R^1(0) by A22;
      thus ft.1 = R^1(ft.1) by TOPREALB:def 2;
    end;
    then Ciso.(ft.1) = Class(EqRel(TUC,c[10]),CircleMap*ft) by A26,Th25;
    hence q in rng Ciso by A1,A21,A23,A25,FUNCT_1:def 5;
  end;
end;

theorem Th26:
  Ciso is being_isomorphism
proof
  thus rng Ciso = the carrier of pi_1(TUC,c[10]) by FUNCT_2:def 3;
  thus Ciso is one-to-one;
end;

Lm16: for r being positive (real number),
o being Point of TOP-REAL 2, x being Point of Tcircle(o,r) holds
INT.Group,pi_1(Tcircle(o,r),x) are_isomorphic
proof
  let r be positive (real number), o be Point of TOP-REAL 2,
  x be Point of Tcircle(o,r);
  TUC = Tcircle(0.REAL 2,1) by TOPREALB:def 7;
  then pi_1(TUC,c[10]),pi_1(Tcircle(o,r),x) are_isomorphic
  by TOPALG_3:35,TOPREALB:20;
  then consider h being Homomorphism of pi_1(TUC,c[10]),
  pi_1(Tcircle(o,r),x) such that
A1: h is_isomorphism by GROUP_6:def 15;
  take h*Ciso;
  thus thesis by A1,Th26,GROUP_6:74;
end;

theorem Th27:
  for S being being_simple_closed_curve SubSpace of TOP-REAL 2,
  x being Point of S holds INT.Group, pi_1(S,x) are_isomorphic
proof
  let S be being_simple_closed_curve SubSpace of TOP-REAL 2, x be Point of S;
  consider r being positive (real number), o being Point of TOP-REAL 2,
  y being Point of Tcircle(o,r);
A1: INT.Group,pi_1(Tcircle(o,r),y) are_isomorphic by Lm16;
  pi_1(Tcircle(o,r),y), pi_1(S,x) are_isomorphic by TOPALG_3:35,TOPREALB:11;
  hence thesis by A1,GROUP_6:78;
end;

registration
  let S be being_simple_closed_curve SubSpace of TOP-REAL 2, x be Point of S;
  cluster pi_1(S,x) -> infinite;
  coherence
  proof
    INT.Group, pi_1(S,x) are_isomorphic by Th27;
    hence thesis by GROUP_6:85;
  end;
end;

