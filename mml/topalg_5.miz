:: The Fundamental Group of the Circle
::  by Artur Korni{\l}owicz
::
:: Received February 22, 2005
:: Copyright (c) 2005 Association of Mizar Users
 
environ

 vocabularies NUMBERS, EUCLID, CARD_1, STRUCT_0, BORSUK_1, TOPMETR, TARSKI,
      ZFMISC_1, PRE_TOPC, SUBSET_1, RELAT_1, ARYTM_1, XXREAL_0, ARYTM_3,
      GR_CY_1, FINSET_1, XREAL_0, ORDINAL1, METRIC_1, XXREAL_1, XBOOLE_0,
      REAL_1, RLVECT_3, PBOOLE, FUNCT_1, CARD_3, RELAT_2, PCOMPS_1, FRECHET,
      CONNSP_1, CONNSP_3, RCOMP_1, CONNSP_2, TOPS_1, TOPS_2, ORDINAL2,
      T_0TOPSP, CANTOR_1, SETFAM_1, GRAPH_1, BORSUK_6, TOPREALB, SUPINF_2,
      SIN_COS, COMPLEX1, SQUARE_1, MCART_1, INT_1, ALGSTR_1, TAXONOM2, TEX_2,
      RCOMP_3, FINSEQ_1, FUNCT_4, COMPTS_1, FUNCOP_1, BORSUK_2, TOPALG_1,
      EQREL_1, WAYBEL20, CONVEX1, TOPALG_2, GROUP_6, ALGSTR_0, JGRAPH_2,
      FUNCT_2, WELLORD1, TOPREAL2, TOPALG_5, MSSUBFAM, MEASURE5;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, ORDINAL1, RELAT_1,
      EQREL_1, FUNCT_1, PBOOLE, CARD_3, RELSET_1, PARTFUN1, SEQ_1, FUNCT_2,
      BINOP_1, FINSET_1, NUMBERS, XCMPLX_0, XXREAL_2, XREAL_0, REAL_1,
      SQUARE_1, NAT_1, INT_1, SEQM_3, FINSEQ_1, FUNCT_4, RCOMP_1, DOMAIN_1,
      STRUCT_0, ALGSTR_0, PRE_TOPC, TOPS_1, GR_CY_1, GROUP_6, METRIC_1,
      CANTOR_1, YELLOW_8, BORSUK_1, TOPS_2, COMPTS_1, CONNSP_1, CONNSP_2,
      CONNSP_3, TEX_2, TSEP_1, TOPMETR, EUCLID, PCOMPS_1, FRECHET, BORSUK_2,
      SIN_COS, BORSUK_3, JORDAN16, TAXONOM2, BORSUK_6, TOPALG_1, TOPREAL9,
      TOPALG_2, TOPREALB, RCOMP_3, XXREAL_0;
 constructors SQUARE_1, NAT_1, CARD_3, SEQM_3, PBOOLE, REALSET1, BINARITH,
      SIN_COS, TOPS_1, CONNSP_1, COMPTS_1, GRCAT_1, TSEP_1, TEX_2, CANTOR_1,
      MONOID_0, PSCOMP_1, CONNSP_3, YELLOW_8, TAXONOM2, BORSUK_3, BORSUK_4,
      JORDAN16, TOPALG_1, TOPREAL9, TOPREALB, RCOMP_3, SEQ_1, BINOP_2,
      BORSUK_6, XXREAL_2, SEQ_4, FRECHET, GROUP_6, TOPS_2;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, RELSET_1, FUNCT_2, NUMBERS,
      XXREAL_0, XREAL_0, NAT_1, INT_1, MEMBERED, FINSEQ_1, STRUCT_0, PRE_TOPC,
      TOPS_1, COMPTS_1, METRIC_1, BORSUK_1, TSEP_1, GR_CY_1, MONOID_0, EUCLID,
      TOPMETR, BORSUK_2, YELLOW13, BORSUK_4, BORSUK_5, TOPALG_1, TOPALG_2,
      TOPALG_3, TOPREALB, RCOMP_3, VALUED_0, XXREAL_2, BORSUK_6, FRECHET,
      CONNSP_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions FUNCT_1, FUNCT_2, GROUP_6, TOPS_2, TSEP_1, BORSUK_2, BORSUK_6,
      COMPTS_1, PRE_TOPC, TARSKI, XBOOLE_0, CONNSP_1, T_0TOPSP, CONNSP_2,
      RCOMP_3, BINOP_1, GR_CY_1, STRUCT_0, BORSUK_1, ALGSTR_0, SETFAM_1,
      TOPALG_1;
 theorems GROUP_6, PRE_TOPC, XREAL_0, FUNCT_2, BORSUK_1, INT_1, SIN_COS,
      EUCLID, JGRAPH_1, SQUARE_1, TOPREAL9, TOPALG_1, TOPS_2, BORSUK_3,
      FUNCT_1, TOPMETR, JGRAPH_2, BORSUK_4, RELAT_1, FUNCOP_1, JORDAN16,
      BORSUK_6, BORSUK_2, TOPALG_2, BINOP_1, TARSKI, ZFMISC_1, FUNCT_4, TSEP_1,
      XBOOLE_1, COMPLEX2, COMPTS_1, SETFAM_1, XBOOLE_0, TAXONOM2, FINSEQ_3,
      NAT_1, SEQM_3, FINSEQ_5, TEX_2, DOMAIN_1, TOPS_1, GOBOARD9, CONNSP_1,
      TOPALG_3, TOPREALA, TOPREALB, FINSET_1, TOPS_3, CONNSP_3, CONNSP_2,
      TOPGRP_1, XREAL_1, CANTOR_1, FRECHET, TOPGEN_2, CARD_3, RCOMP_3,
      XXREAL_0, CARD_1, XXREAL_1, PARTFUN1, RLVECT_1;
 schemes FUNCT_2, NAT_1, PBOOLE, BINOP_1;

begin :: Preliminaries

set o = |[0,0]|;
set I = the carrier of I[01];
set R = the carrier of R^1;

Lm1: 0 in INT by INT_1:def 1;

Lm2: 0 in I by BORSUK_1:86;
then

Lm3: {0} c= I by ZFMISC_1:37;

Lm4: 0 in {0} by TARSKI:def 1;

Lm5: the carrier of [:I[01],I[01]:] = [:I,I:] by BORSUK_1:def 5;

reconsider j0 = 0, j1 = 1 as Point of I[01] by BORSUK_1:def 17,def 18;

Lm6: [#]I[01] = I;

Lm7: I[01] | [#]I[01] = I[01] by TSEP_1:3;

Lm8: 1-0 <= 1;

Lm9: 3/2-1/2 <= 1;

registration
  cluster INT.Group -> infinite;
  coherence;
end;

reserve a, r, s for real number;

theorem Th1:
  r <= s implies for p being Point of Closed-Interval-MSpace(r,s)
holds Ball(p,a) = [.r,s.] or Ball(p,a) = [.r,p+a.[ or Ball(p,a) = ].p-a,s.] or
  Ball(p,a) = ].p-a,p+a.[
proof
  set M = Closed-Interval-MSpace(r,s);
  assume r <= s;
  then
A1: the carrier of M = [.r,s.] by TOPMETR:14;
  let p be Point of M;
  set B = Ball(p,a);
  reconsider p1 = p as Point of RealSpace by TOPMETR:12;
  set B1 = Ball(p1,a);
A2: B = B1 /\ the carrier of M by TOPMETR:13;
  a is Real & p1 is Real by XREAL_0:def 1;
  then
A3: B1 = ].p1-a, p1+a.[ by FRECHET:7;
  per cases;
  suppose that
A4: p1+a <= s and
A5: p1-a < r;
    B = [.r,p1+a.[
    proof
      thus B c= [.r,p1+a.[
      proof
        let b be set;
        assume
A6:     b in B;
        then reconsider b as Element of B;
        b in B1 by A2,A6,XBOOLE_0:def 4;
        then
A7:     b < p1+a by A3,XXREAL_1:4;
        r <= b by A1,A6,XXREAL_1:1;
        hence thesis by A7,XXREAL_1:3;
      end;
      let b be set;
      assume
A8:   b in [.r,p1+a.[;
      then reconsider b as Real;
A9:   r <= b by A8,XXREAL_1:3;
A10:  b < p1+a by A8,XXREAL_1:3;
      then b <= s by A4,XXREAL_0:2;
      then
A11:  b in [.r,s.] by A9,XXREAL_1:1;
      p1-a < b by A5,A9,XXREAL_0:2;
      then b in B1 by A3,A10,XXREAL_1:4;
      hence thesis by A1,A2,A11,XBOOLE_0:def 4;
    end;
    hence thesis;
  end;
  suppose that
A12: p1+a <= s and
A13: p1-a >= r;
    B = ].p1-a,p1+a.[
    proof
      thus B c= ].p1-a,p1+a.[ by A2,A3,XBOOLE_1:17;
      let b be set;
      assume
A14:  b in ].p1-a,p1+a.[;
      then reconsider b as Real;
      b < p1+a by A14,XXREAL_1:4;
      then
A15:  b <= s by A12,XXREAL_0:2;
      p1-a <= b by A14,XXREAL_1:4;
      then r <= b by A13,XXREAL_0:2;
      then b in [.r,s.] by A15,XXREAL_1:1;
      hence thesis by A1,A2,A3,A14,XBOOLE_0:def 4;
    end;
    hence thesis;
  end;
  suppose that
A16: p1+a > s and
A17: p1-a < r;
    B = [.r,s.]
    proof
      thus B c= [.r,s.] by A1;
      let b be set;
      assume
A18:  b in [.r,s.];
      then reconsider b as Real;
      b <= s by A18,XXREAL_1:1;
      then
A19:  b < p1+a by A16,XXREAL_0:2;
      r <= b by A18,XXREAL_1:1;
      then p1-a < b by A17,XXREAL_0:2;
      then b in B1 by A3,A19,XXREAL_1:4;
      hence thesis by A1,A2,A18,XBOOLE_0:def 4;
    end;
    hence thesis;
  end;
  suppose that
A20: p1+a > s and
A21: p1-a >= r;
    B = ].p1-a,s.]
    proof
      thus B c= ].p1-a,s.]
      proof
        let b be set;
        assume
A22:    b in B;
        then reconsider b as Element of B;
        b in B1 by A2,A22,XBOOLE_0:def 4;
        then
A23:    p1-a < b by A3,XXREAL_1:4;
        b <= s by A1,A22,XXREAL_1:1;
        hence thesis by A23,XXREAL_1:2;
      end;
      let b be set;
      assume
A24:  b in ].p1-a,s.];
      then reconsider b as Real;
A25:  b <= s by A24,XXREAL_1:2;
A26:  p1-a < b by A24,XXREAL_1:2;
      then r <= b by A21,XXREAL_0:2;
      then
A27:  b in [.r,s.] by A25,XXREAL_1:1;
      b < p1+a by A20,A25,XXREAL_0:2;
      then b in B1 by A3,A26,XXREAL_1:4;
      hence thesis by A1,A2,A27,XBOOLE_0:def 4;
    end;
    hence thesis;
  end;
end;

theorem Th2:
  r <= s implies ex B being Basis of Closed-Interval-TSpace(r,s) st
  (ex f being ManySortedSet of Closed-Interval-TSpace(r,s) st for y being Point
  of Closed-Interval-MSpace(r,s) holds f.y = {Ball(y,1/n) where n is Element of
NAT: n <> 0} & B = Union f) & for X being Subset of Closed-Interval-TSpace(r,s)
  st X in B holds X is connected
proof
  set L = Closed-Interval-TSpace(r,s), M = Closed-Interval-MSpace(r,s);
  assume
A1: r <= s;
  defpred P[set,set] means ex x being Point of L, y being Point of M, B being
Basis of x st $1 = x & x = y & $2 = B & B = {Ball(y,1/n) where n is Element of
  NAT: n <> 0};
A2: L = TopSpaceMetr(M) by TOPMETR:def 8;
A3: for i being set st i in the carrier of L ex j being set st P[i,j]
  proof
    let i be set;
    assume i in the carrier of L;
    then reconsider i as Point of L;
    reconsider m = i as Point of M by A2,TOPMETR:16;
    reconsider j = i as Element of TopSpaceMetr(M) by A2;
    set B = Balls(j);
A4: ex y being Point of M st y = j &
    B = { Ball(y,1/n) where n is Element of NAT: n <> 0 } by FRECHET:def 1;
    reconsider B1 = B as Basis of i by A2;
    take B, i, m, B1;
    thus thesis by A4;
  end;
  consider f being ManySortedSet of the carrier of L such that
A5: for i being set st i in the carrier of L holds P[i,f.i] from PBOOLE:
  sch 3(A3);
  for x being Element of L holds f.x is Basis of x
  proof
    let x be Element of L;
    P[x,f.x] by A5;
    hence thesis;
  end;
  then reconsider B = Union f as Basis of L by TOPGEN_2:2;
  take B;
  hereby
    take f;
    let x be Point of M;
    the carrier of M = [.r,s.] by A1,TOPMETR:14
      .= the carrier of L by A1,TOPMETR:25;
    then P[x,f.x] by A5;
    hence f.x = {Ball(x,1/n) where n is Element of NAT: n <> 0} & B = Union f;
  end;
  let X be Subset of L;
  assume X in B;
  then X in union rng f by CARD_3:def 4;
  then consider Z being set such that
A6: X in Z and
A7: Z in rng f by TARSKI:def 4;
  consider x being set such that
A8: x in dom f and
A9: f.x = Z by A7,FUNCT_1:def 5;
  dom f = the carrier of L by PARTFUN1:def 4;
  then consider
  x1 being Point of L, y being Point of M, B1 being Basis of x1 such
  that
  x = x1 and
  x1 = y and
A10: f.x = B1 & B1 = {Ball(y,1/n) where n is Element of NAT: n <> 0} by A5,A8;
  consider n being Element of NAT such that
A11: X = Ball(y,1/n) and
  n <> 0 by A6,A9,A10;
  reconsider X1 = X as Subset of R^1 by PRE_TOPC:39;
  Ball(y,1/n) = [.r,s.] or Ball(y,1/n) = [.r,y+1/n.[ or Ball(y,1/n) = ].y
  -1/n,s.] or Ball(y,1/n) = ].y-1/n,y+1/n.[ by A1,Th1;
  then X1 is connected by A11;
  hence thesis by CONNSP_1:24;
end;

theorem Th3:
  for T being TopStruct, A being Subset of T, t be Point of T st t
  in A holds Component_of(t,A) c= A
proof
  let T be TopStruct, A be Subset of T, t be Point of T;
  assume
A1: t in A;
  then Down(t,A) = t by CONNSP_3:def 3;
  then
A2: Component_of(t,A) = Component_of Down(t,A) by A1,CONNSP_3:def 7;
  the carrier of (T|A) = A by PRE_TOPC:29;
  hence thesis by A2;
end;

registration
  let T be TopSpace, A be open Subset of T;
  cluster T|A -> open;
  coherence
  proof
    let X being Subset of T;
    thus thesis by PRE_TOPC:29;
  end;
end;

theorem Th4:
  for T being TopSpace, S being SubSpace of T, A being Subset of T,
  B being Subset of S st A = B holds T|A = S|B
proof
  let T be TopSpace, S be SubSpace of T, A be Subset of T, B be Subset of S;
  assume A = B;
  then S|B is SubSpace of T & [#](S|B) = A by PRE_TOPC:def 10,TSEP_1:7;
  hence thesis by PRE_TOPC:def 10;
end;

theorem Th5:
  for S, T being TopSpace, A, B being Subset of T, C, D being
  Subset of S st the TopStruct of S = the TopStruct of T & A = C & B = D & A,B
  are_separated holds C,D are_separated
proof
  let S, T be TopSpace;
  let A, B be Subset of T;
  let C, D be Subset of S;
  assume
A1: the TopStruct of S = the TopStruct of T;
  assume
A2: A = C & B = D;
  assume A,B are_separated;
  then
A3: Cl A misses B & A misses Cl B by CONNSP_1:def 1;
  Cl A = Cl C & Cl B = Cl D by A1,A2,TOPS_3:80;
  hence thesis by A2,A3,CONNSP_1:def 1;
end;

theorem
  for S, T being TopSpace st the TopStruct of S = the TopStruct of T & S
  is connected holds T is connected
proof
  let S, T be TopSpace such that
A1: the TopStruct of S = the TopStruct of T and
A2: S is connected;
  let A, B be Subset of T such that
A3: [#]T = A \/ B and
A4: A,B are_separated;
  reconsider A1 = A, B1 = B as Subset of S by A1;
  [#]S = the carrier of S & A1,B1 are_separated by A1,A4,Th5;
  then A1 = {}S or B1 = {}S by A1,A2,A3,CONNSP_1:def 2;
  hence thesis;
end;

theorem Th7:
  for S, T being TopSpace, A being Subset of S, B being Subset of T
st the TopStruct of S = the TopStruct of T & A = B & A is connected holds B is
  connected
proof
  let S, T be TopSpace, A be Subset of S, B be Subset of T such that
A1: the TopStruct of S = the TopStruct of T and
A2: A = B & A is connected;
  now
    let P, Q be Subset of T such that
A3: B = P \/ Q and
A4: P,Q are_separated;
    reconsider P1 = P, Q1 = Q as Subset of S by A1;
    P1,Q1 are_separated by A1,A4,Th5;
    then P1 = {}S or Q1 = {}S by A2,A3,CONNSP_1:16;
    hence P = {}T or Q = {}T;
  end;
  hence thesis by CONNSP_1:16;
end;

theorem Th8:
  for S, T being non empty TopSpace, s being Point of S, t being
  Point of T, A being a_neighborhood of s st the TopStruct of S = the TopStruct
  of T & s = t holds A is a_neighborhood of t
proof
  let S, T be non empty TopSpace, s be Point of S, t be Point of T, A be
  a_neighborhood of s such that
A1: the TopStruct of S = the TopStruct of T and
A2: s = t;
  reconsider B = A as Subset of T by A1;
A3: s in Int A by CONNSP_2:def 1;
  Int A = Int B by A1,TOPS_3:77;
  hence thesis by A2,A3,CONNSP_2:def 1;
end;

theorem
  for S, T being non empty TopSpace, A being Subset of S, B being Subset
of T, N being a_neighborhood of A st the TopStruct of S = the TopStruct of T &
  A = B holds N is a_neighborhood of B
proof
  let S, T be non empty TopSpace, A be Subset of S, B be Subset of T, N be
  a_neighborhood of A such that
A1: the TopStruct of S = the TopStruct of T and
A2: A = B;
  reconsider M = N as Subset of T by A1;
A3: A c= Int N by CONNSP_2:def 2;
  Int M = Int N by A1,TOPS_3:77;
  hence thesis by A2,A3,CONNSP_2:def 2;
end;

theorem Th10:
  for S, T being non empty TopSpace, A, B being Subset of T, f
being Function of S,T st f is being_homeomorphism & A is_a_component_of B holds
  f"A is_a_component_of f"B
proof
  let S, T be non empty TopSpace, A, B being Subset of T, f be Function of S,T
  such that
A1: f is being_homeomorphism;
A2: rng f = [#]T by A1,TOPS_2:def 5
    .= the carrier of T;
  set Y = f"A;
  given X being Subset of T|B such that
A3: X = A and
A4: X is a_component;
A5: the carrier of (T|B) = B by PRE_TOPC:29;
  then f"X c= f"B by RELAT_1:178;
  then reconsider Y as Subset of S|(f"B) by A3,PRE_TOPC:29;
  take Y;
  thus Y = f"A;
  X is connected by A4;
  then A is connected by A3,CONNSP_1:24;
  then f"A is connected by A1,TOPS_2:76;
  hence Y is connected by CONNSP_1:24;
  let Z being Subset of S|(f"B) such that
A6: Z is connected and
A7: Y c= Z;
A8: f.:Y c= f.:Z by A7,RELAT_1:156;
A9: f is one-to-one by A1,TOPS_2:def 5;
A10: f is continuous by A1,TOPS_2:def 5;
  the carrier of S|(f"B) = f"B by PRE_TOPC:29;
  then f.:Z c= f.:(f"B) by RELAT_1:156;
  then reconsider R = f.:Z as Subset of T|B by A5,A2,FUNCT_1:147;
  reconsider Z1 = Z as Subset of S by PRE_TOPC:39;
  dom f = the carrier of S by FUNCT_2:def 1;
  then
A11: Z1 c= dom f;
  Z1 is connected by A6,CONNSP_1:24;
  then f.:Z1 is connected by A10,TOPS_2:75;
  then
A12: R is connected by CONNSP_1:24;
  X = f.:Y by A3,A2,FUNCT_1:147;
  then X = R by A4,A12,A8,CONNSP_1:def 5;
  hence thesis by A3,A9,A11,FUNCT_1:164;
end;

begin :: Locally connectedness

theorem Th11:
  for T being non empty TopSpace, S being non empty SubSpace of T,
  A being non empty Subset of T, B being non empty Subset of S st A = B & A is
  locally_connected holds B is locally_connected
proof
  let T be non empty TopSpace, S be non empty SubSpace of T, A be non empty
  Subset of T, B be non empty Subset of S such that
A1: A = B and
A2: A is locally_connected;
  T|A = S|B by A1,Th4;
  hence S|B is locally_connected by A2,CONNSP_2:def 6;
end;

theorem Th12:
  for S, T being non empty TopSpace st the TopStruct of S = the
  TopStruct of T & S is locally_connected holds T is locally_connected
proof
  let S, T be non empty TopSpace such that
A1: the TopStruct of S = the TopStruct of T and
A2: S is locally_connected;
  let t be Point of T;
  reconsider s = t as Point of S by A1;
  let U be Subset of T;
  reconsider U1 = U as Subset of S by A1;
  assume U is a_neighborhood of t;
  then
A3: U1 is a_neighborhood of s by A1,Th8;
  S is_locally_connected_in s by A2,CONNSP_2:def 4;
  then consider V1 being Subset of S such that
A4: V1 is a_neighborhood of s and
A5: V1 is connected and
A6: V1 c= U1 by A3,CONNSP_2:def 3;
  reconsider V = V1 as Subset of T by A1;
  take V;
  thus V is a_neighborhood of t by A1,A4,Th8;
  thus V is connected by A1,A5,Th7;
  thus thesis by A6;
end;

theorem Th13:
  for T being non empty TopSpace holds T is locally_connected iff
  [#]T is locally_connected
proof
  let T be non empty TopSpace;
  T is SubSpace of T by TSEP_1:2;
  then
A1: the TopStruct of T = the TopStruct of (T| [#]T) by PRE_TOPC:29,TSEP_1:5;
  hereby
    assume T is locally_connected;
    then T| [#]T is locally_connected by A1,Th12;
    hence [#]T is locally_connected by CONNSP_2:def 6;
  end;
  assume [#]T is locally_connected;
  then T| [#]T is locally_connected by CONNSP_2:def 6;
  hence thesis by A1,Th12;
end;

Lm10: for T being non empty TopSpace, S being non empty open SubSpace of T st
T is locally_connected holds the TopStruct of S is locally_connected
proof
  let T be non empty TopSpace;
  let S be non empty open SubSpace of T;
  reconsider A = [#]S as non empty Subset of T by TSEP_1:1;
A1: A is open by TSEP_1:def 1;
  assume T is locally_connected;
  then [#]S is locally_connected by A1,Th11,CONNSP_2:23;
  then S is locally_connected by Th13;
  then the TopStruct of S is locally_connected by Th12;
  then [#]the TopStruct of S is locally_connected by Th13;
  then (the TopStruct of S)| [#]the TopStruct of S is locally_connected by
CONNSP_2:def 6;
  hence thesis by TSEP_1:3;
end;

theorem Th14:
  for T being non empty TopSpace, S being non empty open SubSpace
  of T st T is locally_connected holds S is locally_connected
proof
  let T be non empty TopSpace;
  let S be non empty open SubSpace of T;
  assume T is locally_connected;
  then the TopStruct of S is locally_connected by Lm10;
  hence thesis by Th12;
end;

theorem
  for S, T being non empty TopSpace st S,T are_homeomorphic & S is
  locally_connected holds T is locally_connected
proof
  let S, T be non empty TopSpace;
  given f being Function of S,T such that
A1: f is being_homeomorphism;
  assume
A2: S is locally_connected;
  now
    let A be non empty Subset of T, B being Subset of T;
    assume A is open & B is_a_component_of A;
    then
A3: f"A is open & f"B is_a_component_of f"A by A1,Th10,TOPGRP_1:26;
    rng f = [#]T by A1,TOPS_2:def 5;
    then f"A is non empty by RELAT_1:174;
    then f"B is open by A2,A3,CONNSP_2:24;
    hence B is open by A1,TOPGRP_1:26;
  end;
  hence thesis by CONNSP_2:24;
end;

theorem Th16:
  for T being non empty TopSpace st ex B being Basis of T st for X
  being Subset of T st X in B holds X is connected holds T is locally_connected
proof
  let T be non empty TopSpace;
  given B being Basis of T such that
A1: for X being Subset of T st X in B holds X is connected;
  let x be Point of T;
  let U be Subset of T such that
A2: x in Int U;
  Int U in the topology of T & the topology of T c= UniCl B by CANTOR_1:def 2
,PRE_TOPC:def 5;
  then consider Y being Subset-Family of T such that
A3: Y c= B and
A4: Int U = union Y by CANTOR_1:def 1;
  consider V being set such that
A5: x in V and
A6: V in Y by A2,A4,TARSKI:def 4;
  reconsider V as Subset of T by A6;
  take V;
  B c= the topology of T & V in B by A3,A6,TOPS_2:78;
  then V is open by PRE_TOPC:def 5;
  hence x in Int V by A5,TOPS_1:55;
  thus V is connected by A1,A3,A6;
A7: Int U c= U by TOPS_1:44;
  V c= union Y by A6,ZFMISC_1:92;
  hence thesis by A4,A7,XBOOLE_1:1;
end;

theorem Th17:
  r <= s implies Closed-Interval-TSpace(r,s) is locally_connected
proof
  assume r <= s;
  then ex B being Basis of Closed-Interval-TSpace(r,s) st (ex f being
  ManySortedSet of Closed-Interval-TSpace(r,s) st for y being Point of
Closed-Interval-MSpace(r,s) holds f.y = {Ball(y,1/n) where n is Element of NAT:
n <> 0} & B = Union f) & for X being Subset of Closed-Interval-TSpace(r,s) st X
  in B holds X is connected by Th2;
  hence thesis by Th16;
end;

registration
  cluster I[01] -> locally_connected;
  coherence by Th17,TOPMETR:27;
end;

registration
  let A be non empty open Subset of I[01];
  cluster I[01] | A -> locally_connected;
  coherence by Th14;
end;

begin :: Some useful functions

definition
  let r be real number;
  func ExtendInt(r) -> Function of I[01], R^1 means
  :Def1:
  for x being Point of I[01] holds it.x = r*x;
  existence
  proof
    defpred P[real number,set] means $2 = r*$1;
A1: for x being Element of I[01] ex y being Element of R st P[x,y]
    proof
      let x be Element of I[01];
      take r*x;
      thus thesis by TOPMETR:24,XREAL_0:def 1;
    end;
    ex f being Function of I, R st for x being Element of I[01] holds P[x,
    f.x] from FUNCT_2:sch 3(A1);
    hence thesis;
  end;
  uniqueness
  proof
    let f, g be Function of I[01], R^1 such that
A2: for x being Point of I[01] holds f.x = r*x and
A3: for x being Point of I[01] holds g.x = r*x;
    for x being Point of I[01] holds f.x = g.x
    proof
      let x be Point of I[01];
      thus f.x = r*x by A2
        .= g.x by A3;
    end;
    hence f = g by FUNCT_2:113;
  end;
end;

registration
  let r be real number;
  cluster ExtendInt(r) -> continuous;
  coherence
  proof
    reconsider f1 = id I[01] as Function of I[01],R^1 by BORSUK_1:83,FUNCT_2:9
,TOPMETR:24;
    f1 is continuous by PRE_TOPC:56;
    then consider g being Function of I[01],R^1 such that
A1: for p being Point of I[01], r1 being real number st f1.p=r1 holds
    g.p=r*r1 and
A2: g is continuous by JGRAPH_2:33;
    for x being Point of I[01] holds g.x = (ExtendInt(r)).x
    proof
      let x be Point of I[01];
      thus g.x = r*f1.x by A1
        .= r*x by FUNCT_1:35
        .= (ExtendInt(r)).x by Def1;
    end;
    hence thesis by A2,FUNCT_2:113;
  end;
end;

definition
  let r be real number;
  redefine func ExtendInt(r) -> Path of R^1(0),R^1(r);
  coherence
  proof
    thus ExtendInt(r) is continuous;
    thus (ExtendInt(r)).0 = r * 0 by Def1,BORSUK_1:def 17
      .= R^1(0) by TOPREALB:def 2;
    thus (ExtendInt(r)).1 = r * 1 by Def1,BORSUK_1:def 18
      .= R^1(r) by TOPREALB:def 2;
  end;
end;

definition
  let S, T, Y be non empty TopSpace, H be Function of [:S,T:],Y, t be Point of
  T;
  func Prj1(t,H) -> Function of S,Y means
  :Def2:
  for s being Point of S holds it.s = H.(s,t);
  existence
  proof
    deffunc F(Point of S) = H. [$1,t];
    consider f being Function of the carrier of S, the carrier of Y such that
A1: for x being Element of S holds f.x = F(x) from FUNCT_2:sch 4;
    take f;
    let x be Point of S;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f, g be Function of S,Y such that
A2: for s being Point of S holds f.s = H.(s,t) and
A3: for s being Point of S holds g.s = H.(s,t);
    now
      let s be Point of S;
      thus f.s = H.(s,t) by A2
        .= g.s by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

definition
  let S, T, Y be non empty TopSpace, H be Function of [:S,T:],Y, s be Point of
  S;
  func Prj2(s,H) -> Function of T,Y means
  :Def3:
  for t being Point of T holds it.t = H.(s,t);
  existence
  proof
    deffunc F(Point of T) = H. [s,$1];
    consider f being Function of the carrier of T, the carrier of Y such that
A1: for x being Element of T holds f.x = F(x) from FUNCT_2:sch 4;
    take f;
    let x be Point of T;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f, g be Function of T,Y such that
A2: for t being Point of T holds f.t = H.(s,t) and
A3: for t being Point of T holds g.t = H.(s,t);
    now
      let t be Point of T;
      thus f.t = H.(s,t) by A2
        .= g.t by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

registration
  let S, T, Y be non empty TopSpace, H be continuous Function of [:S,T:],Y, t
  be Point of T;
  cluster Prj1(t,H) -> continuous;
  coherence
  proof
    for p being Point of S, V being Subset of Y st Prj1(t,H).p in V & V is
    open holds ex W being Subset of S st p in W & W is open & Prj1(t,H).:W c= V
    proof
      let p be Point of S, V be Subset of Y such that
A1:   Prj1(t,H).p in V & V is open;
      Prj1(t,H).p = H.(p,t) by Def2;
      then consider W being Subset of [:S,T:] such that
A2:   [p,t] in W and
A3:   W is open and
A4:   H.:W c= V by A1,JGRAPH_2:20;
      consider A being Subset-Family of [:S,T:] such that
A5:   W = union A and
A6:   for e being set st e in A ex X1 being Subset of S, Y1 being
      Subset of T st e = [:X1,Y1:] & X1 is open & Y1 is open by A3,BORSUK_1:45;
      consider e being set such that
A7:   [p,t] in e and
A8:   e in A by A2,A5,TARSKI:def 4;
      consider X1 being Subset of S, Y1 being Subset of T such that
A9:   e = [:X1,Y1:] and
A10:  X1 is open and
      Y1 is open by A6,A8;
      take X1;
      thus p in X1 by A7,A9,ZFMISC_1:106;
      thus X1 is open by A10;
      let x be set;
      assume x in Prj1(t,H).:X1;
      then consider c being Point of S such that
A11:  c in X1 and
A12:  x = Prj1(t,H).c by FUNCT_2:116;
      t in Y1 by A7,A9,ZFMISC_1:106;
      then [c,t] in [:X1,Y1:] by A11,ZFMISC_1:106;
      then [c,t] in W by A5,A8,A9,TARSKI:def 4;
      then
A13:  H. [c,t] in H.:W by FUNCT_2:43;
      Prj1(t,H).c = H.(c,t) by Def2
        .= H. [c,t];
      hence thesis by A4,A12,A13;
    end;
    hence thesis by JGRAPH_2:20;
  end;
end;

registration
  let S, T, Y be non empty TopSpace, H be continuous Function of [:S,T:],Y, s
  be Point of S;
  cluster Prj2(s,H) -> continuous;
  coherence
  proof
    for p being Point of T, V being Subset of Y st Prj2(s,H).p in V & V is
    open holds ex W being Subset of T st p in W & W is open & Prj2(s,H).:W c= V
    proof
      let p be Point of T, V be Subset of Y such that
A1:   Prj2(s,H).p in V & V is open;
      Prj2(s,H).p = H.(s,p) by Def3;
      then consider W being Subset of [:S,T:] such that
A2:   [s,p] in W and
A3:   W is open and
A4:   H.:W c= V by A1,JGRAPH_2:20;
      consider A being Subset-Family of [:S,T:] such that
A5:   W = union A and
A6:   for e being set st e in A ex X1 being Subset of S, Y1 being
      Subset of T st e = [:X1,Y1:] & X1 is open & Y1 is open by A3,BORSUK_1:45;
      consider e being set such that
A7:   [s,p] in e and
A8:   e in A by A2,A5,TARSKI:def 4;
      consider X1 being Subset of S, Y1 being Subset of T such that
A9:   e = [:X1,Y1:] and
      X1 is open and
A10:  Y1 is open by A6,A8;
      take Y1;
      thus p in Y1 by A7,A9,ZFMISC_1:106;
      thus Y1 is open by A10;
      let x be set;
      assume x in Prj2(s,H).:Y1;
      then consider c being Point of T such that
A11:  c in Y1 and
A12:  x = Prj2(s,H).c by FUNCT_2:116;
      s in X1 by A7,A9,ZFMISC_1:106;
      then [s,c] in [:X1,Y1:] by A11,ZFMISC_1:106;
      then [s,c] in W by A5,A8,A9,TARSKI:def 4;
      then
A13:  H. [s,c] in H.:W by FUNCT_2:43;
      Prj2(s,H).c = H.(s,c) by Def3
        .= H. [s,c];
      hence thesis by A4,A12,A13;
    end;
    hence thesis by JGRAPH_2:20;
  end;
end;

theorem
  for T being non empty TopSpace, a, b being Point of T, P, Q being Path
  of a,b, H being Homotopy of P,Q, t being Point of I[01] st H is continuous
  holds Prj1(t,H) is continuous;

theorem
  for T being non empty TopSpace, a, b being Point of T, P, Q being Path
  of a,b, H being Homotopy of P,Q, s being Point of I[01] st H is continuous
  holds Prj2(s,H) is continuous;

set TUC = Tunit_circle(2);
set cS1 = the carrier of TUC;

Lm11: now
  TUC = Tcircle(0.TOP-REAL 2,1) by TOPREALB:def 7;
  hence cS1 = Sphere(|[0,0]|,1) by EUCLID:58,TOPREALB:9;
end;

Lm12: dom CircleMap = REAL by FUNCT_2:def 1,TOPMETR:24;

definition
  let r be real number;
  func cLoop(r) -> Function of I[01], Tunit_circle(2) means
  :Def4:
  for x being Point of I[01] holds it.x = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]|;
  existence
  proof
    defpred P[real number,set] means $2 = |[ cos(2*PI*r*$1), sin(2*PI*r*$1) ]|;
A1: for x being Element of I[01] ex y being Element of cS1 st P[x,y]
    proof
      let x be Element of I[01];
      set y = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]|;
      |. y - o .| = |. y .| by EUCLID:58,RLVECT_1:26
        .= sqrt((y`1)^2+(y`2)^2) by JGRAPH_1:47
        .= sqrt((cos(2*PI*r*x))^2+(y`2)^2) by EUCLID:56
        .= sqrt((cos(2*PI*r*x))^2+(sin(2*PI*r*x))^2) by EUCLID:56
        .= 1 by SIN_COS:32,SQUARE_1:83;
      then reconsider y as Element of cS1 by Lm11,TOPREAL9:9;
      take y;
      thus thesis;
    end;
    ex f being Function of I, cS1 st for x being Element of I[01] holds P[
    x,f.x] from FUNCT_2:sch 3(A1);
    hence thesis;
  end;
  uniqueness
  proof
    let f, g be Function of I[01], TUC such that
A2: for x being Point of I[01] holds f.x = |[cos(2*PI*r*x),sin(2*PI*r* x)]| and
A3: for x being Point of I[01] holds g.x = |[cos(2*PI*r*x),sin(2*PI*r* x)]|;
    for x being Point of I[01] holds f.x = g.x
    proof
      let x be Point of I[01];
      thus f.x = |[cos(2*PI*r*x),sin(2*PI*r*x)]| by A2
        .= g.x by A3;
    end;
    hence f = g by FUNCT_2:113;
  end;
end;

theorem Th20:
  cLoop(r) = CircleMap * ExtendInt(r)
proof
  for x being Point of I[01] holds (cLoop(r)).x = (CircleMap * ExtendInt(r
  )). x
  proof
    let x be Point of I[01];
A1: (ExtendInt(r)).x = r*x by Def1;
    thus (cLoop(r)).x = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]| by Def4
      .= |[ cos(2*PI*(ExtendInt(r)).x), sin(2*PI*(ExtendInt(r)).x) ]| by A1
      .= CircleMap.((ExtendInt(r)).x) by TOPREALB:def 11
      .= (CircleMap * ExtendInt(r)).x by FUNCT_2:21;
  end;
  hence thesis by FUNCT_2:113;
end;

definition
  let n be Integer;
  redefine func cLoop(n) -> Loop of c[10];
  coherence
  proof
    set f = cLoop(n);
    f = CircleMap * ExtendInt(n) by Th20;
    hence f is continuous;
    thus f.0 = |[ cos(2*PI*n*j0), sin(2*PI*n*j0) ]| by Def4
      .= c[10] by SIN_COS:34,TOPREALB:def 8;
    thus f.1 = |[ cos(2*PI*n*j1), sin(2*PI*n*j1) ]| by Def4
      .= |[ cos(0), sin(2*PI*n+0) ]| by COMPLEX2:10
      .= c[10] by COMPLEX2:9,SIN_COS:34,TOPREALB:def 8;
  end;
end;

begin :: Main theorems

Lm13: ex F being Subset-Family of Tunit_circle(2) st F is Cover of
Tunit_circle(2) & F is open & for U being Subset of Tunit_circle(2) st U in F
ex D being mutually-disjoint open Subset-Family of R^1 st union D = (CircleMap)
"U & for d being Subset of R^1 st d in D for f being Function of R^1 | d, (
Tunit_circle(2)) | U st f = CircleMap|d holds f is being_homeomorphism
proof
  consider F being Subset-Family of Tunit_circle(2) such that
A1: F = { (CircleMap).:].0,1.[, (CircleMap).:].1/2,3/2.[ } and
A2: F is Cover of Tunit_circle(2) & F is open and
A3: for U being Subset of Tunit_circle(2) holds ( U = (CircleMap).:].0,1
.[ implies union IntIntervals(0,1) = (CircleMap)"U & for d being Subset of R^1
st d in IntIntervals(0,1) for f being Function of R^1 | d, (Tunit_circle(2)) |
U st f = CircleMap|d holds f is being_homeomorphism ) & ( U = (CircleMap).:].1/
  2,3/2.[ implies union IntIntervals(1/2,3/2) = (CircleMap)"U & for d being
Subset of R^1 st d in IntIntervals(1/2,3/2) for f being Function of R^1 | d, (
  Tunit_circle(2)) | U st f = CircleMap|d holds f is being_homeomorphism ) by
TOPREALB:49;
  take F;
  thus F is Cover of Tunit_circle(2) & F is open by A2;
  let U be Subset of Tunit_circle(2);
  assume
A4: U in F;
  per cases by A1,A4,TARSKI:def 2;
  suppose
A5: U = (CircleMap).:].0,1.[;
    reconsider D = IntIntervals(0,1) as mutually-disjoint open Subset-Family
    of R^1 by Lm8,TOPREALB:4;
    take D;
    thus thesis by A3,A5;
  end;
  suppose
A6: U = (CircleMap).:].1/2,3/2.[;
    reconsider D = IntIntervals(1/2,3/2) as mutually-disjoint open
    Subset-Family of R^1 by Lm9,TOPREALB:4;
    take D;
    thus thesis by A3,A6;
  end;
end;

Lm14: [#]Sspace(0[01]) = {0} by TEX_2:def 4;

Lm15: for F being Subset-Family of Closed-Interval-TSpace(r,s), C being
IntervalCover of F, G being IntervalCoverPts of C holds F is Cover of
Closed-Interval-TSpace(r,s) & F is open connected & r <= s implies G is non
empty
proof
  let F be Subset-Family of Closed-Interval-TSpace(r,s), C be IntervalCover of
  F, G be IntervalCoverPts of C;
  assume
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s;
  then len G = len C + 1 by RCOMP_3:def 3;
  hence thesis by CARD_1:47;
end;

theorem Th21:
  for UL being Subset-Family of Tunit_circle(2) st UL is Cover of
Tunit_circle(2) & UL is open for Y being non empty TopSpace, F being continuous
  Function of [:Y,I[01]:], Tunit_circle(2), y being Point of Y ex T being non
  empty FinSequence of REAL st T.1 = 0 & T.len T = 1 & T is increasing & ex N
being open Subset of Y st y in N & for i being natural number st i in dom T & i
+1 in dom T ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL & F.:[:
  N,[.T.i,T.(i+1).]:] c= Ui
proof
  set L = Closed-Interval-TSpace(0,1);
  let UL be Subset-Family of TUC such that
A1: UL is Cover of TUC and
A2: UL is open;
  let Y be non empty TopSpace, F be continuous Function of [:Y,I[01]:], TUC, y
  be Point of Y;
A3: [#]TUC = union UL by A1,SETFAM_1:60;
A4: for i being Point of I[01] ex U being non empty open Subset of TUC st F.
  [y,i] in U & U in UL
  proof
    let i be Point of I[01];
    consider U being set such that
A5: F. [y,i] in U & U in UL by A3,TARSKI:def 4;
    reconsider U as non empty open Subset of TUC by A2,A5,TOPS_2:def 1;
    take U;
    thus thesis by A5;
  end;
  then ex U being non empty open Subset of TUC st F. [y,0[01]] in U & U in UL;
  then reconsider UL1 = UL as non empty set;
  set C = the carrier of Y;
  defpred I0[set,set] means ex V being open Subset of TUC st V in UL1 & F. [y,
  $1] in V & $2 = V;
A6: for i being Element of I ex z being Element of UL1 st I0[i,z]
  proof
    let i be Element of I;
    ex U being non empty open Subset of TUC st F. [y,i] in U & U in UL by A4;
    hence thesis;
  end;
  consider I0 being Function of I, UL1 such that
A7: for i being Element of I holds I0[i,I0.i] from FUNCT_2:sch 3(A6);
  defpred I1[set,set] means ex M being open Subset of Y, O being open
connected Subset of I[01] st y in M & $1 in O & F.:[:M,O:] c= I0.$1 & $2 = [:M,
  O:];
A8: for i being Element of I ex z being Subset of [:C,I:] st I1[i,z]
  proof
    let i be Element of I;
    consider V being open Subset of TUC such that
    V in UL1 and
A9: F. [y,i] in V and
A10: I0.i = V by A7;
    consider W being Subset of [:Y,I[01]:] such that
A11: [y,i] in W and
A12: W is open and
A13: F.:W c= V by A9,JGRAPH_2:20;
    consider Q being Subset-Family of [:Y,I[01]:] such that
A14: W = union Q and
A15: for e being set st e in Q ex A being Subset of Y, B being Subset
    of I[01] st e = [:A,B:] & A is open & B is open by A12,BORSUK_1:45;
    consider Z being set such that
A16: [y,i] in Z and
A17: Z in Q by A11,A14,TARSKI:def 4;
    consider A being Subset of Y, B being Subset of I[01] such that
A18: Z = [:A,B:] and
A19: A is open and
A20: B is open by A15,A17;
    reconsider A as open Subset of Y by A19;
A21: i in B by A16,A18,ZFMISC_1:106;
    reconsider B as non empty open Subset of I[01] by A16,A18,A20,ZFMISC_1:106;
    reconsider i1 = i as Point of I[01]|B by A21,PRE_TOPC:29;
    Component_of i1 is a_component by CONNSP_1:43;
    then
A22: Component_of i1 is open by CONNSP_2:21;
    Component_of(i,B) = Component_of i1 by A21,CONNSP_3:def 7;
    then reconsider
    D = Component_of(i,B) as open connected Subset of I[01] by A21,A22,
CONNSP_3:35,TSEP_1:17;
    reconsider z = [:A,D:] as Subset of [:C,I:] by BORSUK_1:def 5;
    take z, A, D;
    thus y in A by A16,A18,ZFMISC_1:106;
    thus i in D by A21,CONNSP_3:26;
    D c= B by A21,Th3;
    then
A23: z c= [:A,B:] by ZFMISC_1:118;
    [:A,B:] c= W by A14,A17,A18,ZFMISC_1:92;
    then z c= W by A23,XBOOLE_1:1;
    then F.:z c= F.:W by RELAT_1:156;
    hence F.:[:A,D:] c= I0.i by A10,A13,XBOOLE_1:1;
    thus thesis;
  end;
  consider I1 being Function of I, bool [:C,I:] such that
A24: for i being Element of I holds I1[i,I1.i] from FUNCT_2:sch 3(A8);
  reconsider C1 = rng I1 as Subset-Family of [:Y,I[01]:] by BORSUK_1:def 5;
A25: C1 is open
  proof
    let P be Subset of [:Y,I[01]:];
    assume P in C1;
    then consider i being set such that
A26: i in dom I1 and
A27: I1.i = P by FUNCT_1:def 5;
    I1[i,I1.i] by A24,A26;
    hence thesis by A27,BORSUK_1:46;
  end;
A28: dom I1 = I by FUNCT_2:def 1;
  [:{y},[#]I[01]:] c= union C1
  proof
    let a be set;
    assume a in [:{y},[#]I[01]:];
    then consider a1, a2 being set such that
A29: a1 in {y} and
A30: a2 in [#]I[01] and
A31: a = [a1,a2] by ZFMISC_1:def 2;
A32: a1 = y by A29,TARSKI:def 1;
    reconsider a2 as Point of I[01] by A30;
    consider M being open Subset of Y, O being open connected Subset of I[01]
    such that
A33: y in M & a2 in O and
    F.:[:M,O:] c= I0.a2 and
A34: I1.a2 = [:M,O:] by A24;
    [y,a2] in [:M,O:] & [:M,O:] in C1 by A28,A33,A34,FUNCT_1:def 5,ZFMISC_1:106
;
    hence thesis by A31,A32,TARSKI:def 4;
  end;
  then
A35: C1 is Cover of [:{y},[#]I[01]:] by SETFAM_1:def 12;
  [:{y},[#]I[01]:] is compact by BORSUK_3:27;
  then consider G being Subset-Family of [:Y,I[01]:] such that
A36: G c= C1 and
A37: G is Cover of [:{y},[#]I[01]:] and
A38: G is finite by A35,A25,COMPTS_1:def 7;
  set NN = {M where M is open Subset of Y: y in M & ex O being open Subset of
  I[01] st [:M,O:] in G};
  NN c= bool C
  proof
    let a be set;
    assume a in NN;
    then ex M being open Subset of Y st a = M & y in M & ex O being open
    Subset of I[01] st [:M,O:] in G;
    hence thesis;
  end;
  then reconsider NN as Subset-Family of Y;
  consider p being Function such that
A39: rng p = G and
A40: dom p in omega by A38,FINSET_1:def 1;
  defpred F[set,set] means ex M being open Subset of Y, O being non empty open
  Subset of I[01] st y in M & p.$1 = [:M,O:] & $2 = M;
A41: for x being set st x in dom p ex y being set st y in NN & F[x,y]
  proof
    let x be set;
    assume x in dom p;
    then
A42: p.x in rng p by FUNCT_1:def 5;
    then consider i being set such that
A43: i in dom I1 and
A44: I1.i = p.x by A36,A39,FUNCT_1:def 5;
    consider M being open Subset of Y, O being open connected Subset of I[01]
    such that
A45: y in M & i in O and
    F.:[:M,O:] c= I0.i and
A46: I1.i = [:M,O:] by A24,A43;
    take M;
    thus thesis by A39,A42,A44,A45,A46;
  end;
  consider p1 being Function of dom p, NN such that
A47: for x being set st x in dom p holds F[x,p1.x] from FUNCT_2:sch 1(
  A41);
  set ICOV = {O where O is open connected Subset of I[01]: ex M being open
  Subset of Y st [:M,O:] in G};
A48: [:{y},[#]I[01]:] c= union G by A37,SETFAM_1:def 12;
A49: y in {y} by TARSKI:def 1;
  then [y,0[01]] in [:{y},[#]I[01]:] by ZFMISC_1:def 2;
  then consider Z being set such that
  [y,0[01]] in Z and
A50: Z in G by A48,TARSKI:def 4;
  consider i being set such that
A51: i in dom I1 and
A52: I1.i = Z by A36,A50,FUNCT_1:def 5;
  consider M being open Subset of Y, O being open connected Subset of I[01]
  such that
A53: y in M and
  i in O and
  F.:[:M,O:] c= I0.i and
A54: I1.i = [:M,O:] by A24,A51;
A55: M in NN by A50,A52,A53,A54;
  then
A56: dom p1 = dom p by FUNCT_2:def 1;
  rng p1 = NN
  proof
    thus rng p1 c= NN;
    let a be set;
    assume a in NN;
    then consider M being open Subset of Y such that
A57: a = M and
    y in M and
A58: ex O being open Subset of I[01] st [:M,O:] in G;
    consider O being open Subset of I[01] such that
A59: [:M,O:] in G by A58;
    consider b being set such that
A60: b in dom p and
A61: p.b = [:M,O:] by A39,A59,FUNCT_1:def 5;
    consider M1 being open Subset of Y, O1 being non empty open Subset of
    I[01] such that
A62: y in M1 & p.b = [:M1,O1:] and
A63: p1.b = M1 by A47,A60;
    M1 = M by A61,A62,ZFMISC_1:134;
    hence thesis by A56,A57,A60,A63,FUNCT_1:def 5;
  end;
  then
A64: NN is finite by A40,A56,FINSET_1:def 1;
  NN is open
  proof
    let a be Subset of Y;
    assume a in NN;
    then ex M being open Subset of Y st a = M & y in M & ex O being open
    Subset of I[01] st [:M,O:] in G;
    hence thesis;
  end;
  then reconsider N = meet NN as open Subset of Y by A64,TOPS_2:27;
  ICOV c= bool I
  proof
    let a be set;
    assume a in ICOV;
    then ex O being open connected Subset of I[01] st a = O & ex M being open
    Subset of Y st [:M,O:] in G;
    hence thesis;
  end;
  then reconsider ICOV as Subset-Family of L by TOPMETR:27;
  [#]L c= union ICOV
  proof
    let a be set;
    assume a in [#]L;
    then reconsider a as Point of I[01] by TOPMETR:27;
    [y,a] in [:{y},[#]I[01]:] by A49,ZFMISC_1:def 2;
    then consider Z being set such that
A65: [y,a] in Z and
A66: Z in G by A48,TARSKI:def 4;
    consider i being set such that
A67: i in dom I1 and
A68: I1.i = Z by A36,A66,FUNCT_1:def 5;
    consider M being open Subset of Y, O being open connected Subset of I[01]
    such that
    y in M and
    i in O and
    F.:[:M,O:] c= I0.i and
A69: I1.i = [:M,O:] by A24,A67;
    a in O & O in ICOV by A65,A66,A68,A69,ZFMISC_1:106;
    hence thesis by TARSKI:def 4;
  end;
  then
A70: ICOV is Cover of L by SETFAM_1:def 12;
  consider NCOV being IntervalCover of ICOV;
  consider T being IntervalCoverPts of NCOV;
A71: ICOV is connected
  proof
    let X be Subset of L;
    assume X in ICOV;
    then ex O being open connected Subset of I[01] st X = O & ex M being open
    Subset of Y st [:M,O:] in G;
    hence thesis by TOPMETR:27;
  end;
A72: ICOV is open
  proof
    let a be Subset of L;
    assume a in ICOV;
    then ex O being open connected Subset of I[01] st a = O & ex M being open
    Subset of Y st [:M,O:] in G;
    hence thesis by TOPMETR:27;
  end;
  then reconsider T as non empty FinSequence of REAL by A70,A71,Lm15;
  take T;
  thus T.1 = 0 & T.len T = 1 by A70,A72,A71,RCOMP_3:def 3;
  thus T is increasing by A70,A72,A71,RCOMP_3:79;
  take N;
  now
    let Z be set;
    assume Z in NN;
    then ex M being open Subset of Y st Z = M & y in M & ex O being open
    Subset of I[01] st [:M,O:] in G;
    hence y in Z;
  end;
  hence y in N by A55,SETFAM_1:def 1;
  let i be natural number;
  assume that
A73: i in dom T and
A74: i+1 in dom T;
A75: 1 <= i by A73,FINSEQ_3:27;
A76: i+1 <= len T by A74,FINSEQ_3:27;
  len T = len NCOV + 1 by A70,A72,A71,RCOMP_3:def 3;
  then i <= len NCOV by A76,XREAL_1:8;
  then i in dom NCOV by A75,FINSEQ_3:27;
  then
A77: NCOV.i in rng NCOV by FUNCT_1:def 5;
  rng NCOV c= ICOV by A70,A72,A71,RCOMP_3:def 2;
  then NCOV.i in ICOV by A77;
  then consider O being open connected Subset of I[01] such that
A78: NCOV.i = O and
A79: ex M being open Subset of Y st [:M,O:] in G;
  consider M being open Subset of Y such that
A80: [:M,O:] in G by A79;
  i < len T by A76,NAT_1:13;
  then
A81: [.T.i,T.(i+1).] c= O by A70,A72,A71,A75,A78,RCOMP_3:80;
  consider j being set such that
A82: j in dom I1 and
A83: I1.j = [:M,O:] by A36,A80,FUNCT_1:def 5;
  consider V being open Subset of TUC such that
A84: V in UL1 and
A85: F. [y,j] in V and
A86: I0.j = V by A7,A82;
  reconsider V as non empty open Subset of TUC by A85;
  take V;
  thus V in UL by A84;
  consider M1 being open Subset of Y, O1 being open connected Subset of I[01]
  such that
A87: y in M1 and
A88: j in O1 and
A89: F.:[:M1,O1:] c= I0.j and
A90: I1.j = [:M1,O1:] by A24,A82;
  M = M1 by A83,A87,A88,A90,ZFMISC_1:134;
  then M in NN by A80,A87;
  then N c= M by SETFAM_1:4;
  then [:N,[.T.i,T.(i+1).]:] c= [:M1,O1:] by A83,A90,A81,ZFMISC_1:119;
  then F.:[:N,[.T.i,T.(i+1).]:] c= F.:[:M1,O1:] by RELAT_1:156;
  hence thesis by A89,A86,XBOOLE_1:1;
end;

theorem Th22:
  for Y being non empty TopSpace, F being Function of [:Y,I[01]:],
  Tunit_circle(2), Ft being Function of [:Y,Sspace(0[01]):], R^1 st F is
continuous & Ft is continuous & F | [:the carrier of Y,{0}:] = CircleMap*Ft ex
G being Function of [:Y,I[01]:], R^1 st G is continuous & F = CircleMap*G & G |
  [:the carrier of Y,{0}:] = Ft & for H being Function of [:Y,I[01]:], R^1 st H
is continuous & F = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft holds G = H
proof
  consider UL being Subset-Family of TUC such that
A1: UL is Cover of TUC & UL is open and
A2: for U being Subset of TUC st U in UL ex D being mutually-disjoint
  open Subset-Family of R^1 st union D = (CircleMap)"U & for d being Subset of
R^1 st d in D for f being Function of R^1 | d, TUC | U st f = CircleMap|d holds
  f is being_homeomorphism by Lm13;
  let Y be non empty TopSpace, F be Function of [:Y,I[01]:], TUC, Ft be
  Function of [:Y,Sspace(0[01]):], R^1 such that
A3: F is continuous and
A4: Ft is continuous and
A5: F | [:the carrier of Y,{0}:] = CircleMap*Ft;
  defpred A[set,set] means ex y being Point of Y, t being Point of I[01], N
being non empty open Subset of Y, Fn being Function of [:Y|N,I[01]:], R^1 st $1
= [y,t] & $2 = Fn.$1 & y in N & Fn is continuous & F | [:N,I:] = CircleMap*Fn &
  Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] & for H being Function of [:Y|N,
I[01]:], R^1 st H is continuous & F | [:N,I:] = CircleMap*H & H | [:the carrier
  of Y,{0}:] = Ft | [:N,I:] holds Fn = H;
A6: dom F = the carrier of [:Y,I[01]:] by FUNCT_2:def 1
    .= [:the carrier of Y,I:] by BORSUK_1:def 5;
A7: the carrier of [:Y,Sspace(0[01]):] = [:the carrier of Y, the carrier of
  Sspace(0[01]):] by BORSUK_1:def 5;
  then
A8: dom Ft = [:the carrier of Y,{0}:] by Lm14,FUNCT_2:def 1;
A9: for x being Point of [:Y,I[01]:] ex z being Point of R^1 st A[x,z]
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y, t being Point of I[01] such that
A10: x = [y,t] by BORSUK_1:50;
    consider TT being non empty FinSequence of REAL such that
A11: TT.1 = 0 and
A12: TT.len TT = 1 and
A13: TT is increasing and
A14: ex N being open Subset of Y st y in N & for i being natural
    number st i in dom TT & i+1 in dom TT ex Ui being non empty Subset of
Tunit_circle(2) st Ui in UL & F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A3,A1,Th21;
    consider N being open Subset of Y such that
A15: y in N and
A16: for i being natural number st i in dom TT & i+1 in dom TT ex Ui
being non empty Subset of Tunit_circle(2) st Ui in UL & F.:[:N,[.TT.i,TT.(i+1)
    .]:] c= Ui by A14;
    reconsider N as non empty open Subset of Y by A15;
    defpred N[Element of NAT] means $1 in dom TT implies ex N2 being non empty
open Subset of Y, S being non empty Subset of I[01], Fn being Function of [:Y|
N2,I[01]|S:], R^1 st S = [.0,TT.$1.] & y in N2 & N2 c= N & Fn is continuous & F
    | [:N2,S:] = CircleMap*Fn & Fn | [:the carrier of Y,{0}:] = Ft | [:N2,I:];
A17: len TT in dom TT by FINSEQ_5:6;
A18: 1 in dom TT by FINSEQ_5:6;
A19: now
      let i be Element of NAT such that
A20:  i in dom TT;
      1 <= i by A20,FINSEQ_3:27;
      then 1 = i or 1 < i by XXREAL_0:1;
      hence
A21:  0 <= TT.i by A11,A13,A18,A20,SEQM_3:def 1;
      assume
A22:  i+1 in dom TT;
A23:  i+0 < i+1 by XREAL_1:10;
      hence
A24:  TT.i < TT.(i+1) by A13,A20,A22,SEQM_3:def 1;
      i+1 <= len TT by A22,FINSEQ_3:27;
      then i+1 = len TT or i+1 < len TT by XXREAL_0:1;
      hence TT.(i+1) <= 1 by A12,A13,A17,A22,SEQM_3:def 1;
      hence TT.i < 1 by A24,XXREAL_0:2;
      thus 0 < TT.(i+1) by A13,A20,A21,A22,A23,SEQM_3:def 1;
    end;
A25: now
      let i be Element of NAT such that
A26:  0 <= TT.i and
A27:  TT.(i+1) <= 1;
      thus [.TT.i,TT.(i+1).] c= I
      proof
        let a be set;
        assume
A28:    a in [.TT.i,TT.(i+1).];
        then reconsider a as Real;
        a <= TT.(i+1) by A28,XXREAL_1:1;
        then
A29:    a <= 1 by A27,XXREAL_0:2;
        0 <= a by A26,A28,XXREAL_1:1;
        hence thesis by A29,BORSUK_1:86;
      end;
    end;
A30: for i being Element of NAT st N[i] holds N[i+1]
    proof
      let i be Element of NAT;
      assume that
A31:  N[i] and
A32:  i+1 in dom TT;
      per cases by A32,TOPREALA:23;
      suppose
A33:    i = 0;
        take N2 = N;
        set Fn = Ft | [:N2,{0}:];
        set S = [.0,TT.(i+1).];
A34:    S = {0} by A11,A33,XXREAL_1:17;
        reconsider S as non empty Subset of I[01] by A11,A33,Lm3,XXREAL_1:17;
A35:    dom Fn = [:N2,S:] by A8,A34,RELAT_1:91,ZFMISC_1:119;
        reconsider K0 = [:N2,S:] as non empty Subset of [:Y,Sspace(0[01]):] by
A7,A34,Lm14,ZFMISC_1:119;
A36:    the carrier of [:Y|N2,I[01]|S:] = [:the carrier of (Y|N2), the
        carrier of ( I[01]|S):] & rng Fn c= R by BORSUK_1:def 5;
        the carrier of (Y|N2) = N2 & the carrier of (I[01]|S) = S by
PRE_TOPC:29;
        then reconsider Fn as Function of [:Y|N2,I[01]|S:], R^1 by A35,A36,
FUNCT_2:4;
A37:    dom (F | [:N2,S:]) = [:N2,S:] by A6,RELAT_1:91,ZFMISC_1:119;
        reconsider S1 = S as non empty Subset of Sspace(0[01]) by A11,A33,Lm14,
XXREAL_1:17;
        take S, Fn;
        thus S = [.0,TT.(i+1).];
        thus y in N2 by A15;
        thus N2 c= N;
        I[01]|S = Sspace(0[01]) by A34,TOPALG_3:6
          .= (Sspace(0[01]))|S1 by A34,Lm14,TSEP_1:3;
        then [:Y|N2,I[01]|S:] = [:Y,Sspace(0[01]):]|K0 by BORSUK_3:26;
        hence Fn is continuous by A4,A34,TOPMETR:10;
        rng Fn c= dom CircleMap by Lm12,TOPMETR:24;
        then
A38:    dom (CircleMap*Fn) = dom Fn by RELAT_1:46;
A39:    [:N2,S:] c= dom Ft by A8,A34,ZFMISC_1:119;
        for x being set st x in dom (F | [:N2,S:]) holds (F | [:N2,S:]).x
        = (CircleMap*Fn).x
        proof
          let x be set such that
A40:      x in dom (F | [:N2,S:]);
          thus (F | [:N2,S:]).x = F.x by A37,A40,FUNCT_1:72
            .= (CircleMap*Ft).x by A5,A7,A35,A37,A40,Lm14,FUNCT_1:72
            .= CircleMap.(Ft.x) by A39,A37,A40,FUNCT_1:23
            .= CircleMap.(Fn.x) by A34,A37,A40,FUNCT_1:72
            .= (CircleMap*Fn).x by A35,A37,A40,FUNCT_1:23;
        end;
        hence F | [:N2,S:] = CircleMap*Fn by A35,A37,A38,FUNCT_1:9;
A41:    dom (Fn | [:the carrier of Y,{0}:]) = [:N2,S:] /\ [:the carrier
        of Y,{0}:] by A35,RELAT_1:90;
A42:    for x being set st x in dom (Fn | [:the carrier of Y,{0}:]) holds
        (Fn | [:the carrier of Y,{0}:]).x = (Ft | [:N2,I:]).x
        proof
A43:      [:N2,{0}:] c= [:N2,I:] by Lm3,ZFMISC_1:118;
          let x be set such that
A44:      x in dom (Fn | [:the carrier of Y,{0}:]);
A45:      x in [:N2,{0}:] by A34,A41,A44,XBOOLE_0:def 4;
          x in [:the carrier of Y,{0}:] by A41,A44,XBOOLE_0:def 4;
          hence (Fn | [:the carrier of Y,{0}:]).x = Fn.x by FUNCT_1:72
            .= Ft.x by A45,FUNCT_1:72
            .= (Ft | [:N2,I:]).x by A45,A43,FUNCT_1:72;
        end;
        dom (Ft | [:N2,I:]) = [:the carrier of Y,{0}:] /\ [:N2,I:] by A8,
RELAT_1:90
          .= [:N2,S:] by A34,ZFMISC_1:124;
        hence thesis by A34,A41,A42,FUNCT_1:9,ZFMISC_1:124;
      end;
      suppose
A46:    i in dom TT;
        set SS = [.TT.i,TT.(i+1).];
        consider Ui being non empty Subset of TUC such that
A47:    Ui in UL and
A48:    F.:[:N,SS:] c= Ui by A16,A32,A46;
        consider D being mutually-disjoint open Subset-Family of R^1 such that
A49:    union D = (CircleMap)"Ui and
A50:    for d being Subset of R^1 st d in D for f being Function of
R^1 | d, TUC | Ui st f = CircleMap|d holds f is being_homeomorphism by A2,A47;
A51:    the carrier of (TUC | Ui) = Ui by PRE_TOPC:29;
A52:    TT.i < TT.(i+1) by A19,A32,A46;
        then TT.i in SS by XXREAL_1:1;
        then
A53:    [y,TT.i] in [:N,SS:] by A15,ZFMISC_1:106;
        consider N2 being open Subset of Y, S being non empty Subset of I[01],
        Fn being Function of [:Y|N2,I[01]|S:], R^1 such that
A54:    S = [.0,TT.i.] and
A55:    y in N2 and
A56:    N2 c= N and
A57:    Fn is continuous and
A58:    F | [:N2,S:] = CircleMap*Fn and
A59:    Fn | [:the carrier of Y,{0}:] = Ft | [:N2,I:] by A31,A46;
        reconsider N2 as non empty open Subset of Y by A55;
A60:    the carrier of [:Y|N2,I[01]|S:] = [:the carrier of (Y|N2), the
        carrier of (I[01]|S):] by BORSUK_1:def 5;
        N2 c= N2;
        then reconsider N7 = N2 as non empty Subset of Y|N2 by PRE_TOPC:29;
A61:    dom Fn = the carrier of [:Y|N2,I[01]|S:] by FUNCT_2:def 1;
A62:    0 <= TT.i by A19,A46;
        then
A63:    TT.i in S by A54,XXREAL_1:1;
        then reconsider Ti = {TT.i} as non empty Subset of I[01] by ZFMISC_1:37
;
A64:    the carrier of I[01]|S = S by PRE_TOPC:29;
        then reconsider Ti2 = Ti as non empty Subset of I[01]|S by A63,
ZFMISC_1:37;
        set FnT = Fn | [:N2,Ti:];
A65:    the carrier of [:Y|N2,I[01]|Ti:] = [:the carrier of Y|N2, the
        carrier of I[01]|Ti:] & rng FnT c= REAL by BORSUK_1:def 5,TOPMETR:24;
A66:    [:N2,SS:] c= [:N,SS:] by A56,ZFMISC_1:119;
A67:    the carrier of (Y|N2) = N2 by PRE_TOPC:29;
        {TT.i} c= S by A63,ZFMISC_1:37;
        then
A68:    dom FnT = [:N2,{TT.i}:] by A64,A60,A67,A61,RELAT_1:91,ZFMISC_1:119;
A69:    [:Y|N2|N7,I[01]|S|Ti2:] = [:Y|N2,I[01]|S:] | [:N7,Ti2:] by BORSUK_3:26;
A70:    the carrier of I[01]|Ti = Ti by PRE_TOPC:29;
        then reconsider FnT as Function of [:Y|N2,I[01]|Ti:],R^1 by A67,A68,A65
,FUNCT_2:4;
        Y|N2|N7 = Y|N2 & I[01]|S|Ti2 = I[01]|Ti by GOBOARD9:4;
        then
A71:    FnT is continuous by A57,A69,TOPMETR:10;
        [y,TT.i] in dom F by A6,A63,ZFMISC_1:106;
        then
A72:    F. [y,TT.i] in F.:[:N,SS:] by A53,FUNCT_2:43;
A73:    [y,TT.i] in [:N2,S:] by A55,A63,ZFMISC_1:106;
        then F. [y,TT.i] = (CircleMap*Fn). [y,TT.i] by A58,FUNCT_1:72
          .= CircleMap.(Fn. [y,TT.i]) by A64,A60,A67,A73,FUNCT_2:21;
        then Fn. [y,TT.i] in (CircleMap)"Ui by A48,A72,FUNCT_2:46,TOPMETR:24;
        then consider Uit being set such that
A74:    Fn. [y,TT.i] in Uit and
A75:    Uit in D by A49,TARSKI:def 4;
        reconsider Uit as non empty Subset of R^1 by A74,A75;
        [#]R^1 <> {} & Uit is open by A75,TOPS_2:def 1;
        then FnT"Uit is open by A71,TOPS_2:55;
        then consider SF being Subset-Family of [:Y|N2,I[01]|Ti:] such that
A76:    FnT"Uit = union SF and
A77:    for e being set st e in SF ex X1 being Subset of Y|N2, Y1
        being Subset of I[01]|Ti st e = [:X1,Y1:] & X1 is open & Y1 is open by
BORSUK_1:45;
A78:    TT.i in {TT.i} by TARSKI:def 1;
        then
A79:    [y,TT.i] in [:N2,{TT.i}:] by A55,ZFMISC_1:def 2;
        then FnT. [y,TT.i] in Uit by A74,FUNCT_1:72;
        then [y,TT.i] in FnT"Uit by A79,A68,FUNCT_1:def 13;
        then consider N5 being set such that
A80:    [y,TT.i] in N5 and
A81:    N5 in SF by A76,TARSKI:def 4;
        set f = CircleMap|Uit;
A82:    dom f = Uit by Lm12,RELAT_1:91,TOPMETR:24;
A83:    rng f c= Ui
        proof
          let b be set;
          assume b in rng f;
          then consider a being set such that
A84:      a in dom f and
A85:      f.a = b by FUNCT_1:def 5;
          a in union D by A75,A82,A84,TARSKI:def 4;
          then CircleMap.a in Ui by A49,FUNCT_2:46;
          hence thesis by A82,A84,A85,FUNCT_1:72;
        end;
        consider X1 being Subset of Y|N2, Y1 being Subset of I[01]|Ti such
        that
A86:    N5 = [:X1,Y1:] and
A87:    X1 is open and
        Y1 is open by A77,A81;
        the carrier of (R^1 | Uit) = Uit by PRE_TOPC:29;
        then reconsider f as Function of R^1 | Uit, TUC | Ui by A51,A82,A83,
FUNCT_2:4;
        consider NY being Subset of Y such that
A88:    NY is open and
A89:    NY /\ [#](Y|N2) = X1 by A87,TOPS_2:32;
        consider y1, y2 being set such that
A90:    y1 in X1 and
A91:    y2 in Y1 and
A92:    [y,TT.i] = [y1,y2] by A80,A86,ZFMISC_1:def 2;
        set N1 = NY /\ N2;
        y = y1 by A92,ZFMISC_1:33;
        then
A93:    y in NY by A89,A90,XBOOLE_0:def 4;
        then reconsider N1 as non empty open Subset of Y by A55,A88,
XBOOLE_0:def 4;
A94:    N1 c= N2 by XBOOLE_1:17;
        then [:N1,SS:] c= [:N2,SS:] by ZFMISC_1:119;
        then [:N1,SS:] c= [:N,SS:] by A66,XBOOLE_1:1;
        then
A95:    F.:[:N1,SS:] c= F.:[:N,SS:] by RELAT_1:156;
        TT.(i+1) <= 1 by A19,A32,A46;
        then reconsider SS as non empty Subset of I[01] by A25,A62,A52,
XXREAL_1:1;
A96:    dom (F| [:N1,SS:]) = [:N1,SS:] by A6,RELAT_1:91,ZFMISC_1:119;
        set Fni1 = f"*(F| [:N1,SS:]);
        f" is being_homeomorphism by A50,A75,TOPS_2:70;
        then
A97:    dom (f") = [#](TUC | Ui) by TOPS_2:def 5;
A98:    rng (F| [:N1,SS:]) c= dom (f")
        proof
          let b be set;
          assume b in rng (F| [:N1,SS:]);
          then consider a being set such that
A99:      a in dom (F| [:N1,SS:]) and
A100:     (F| [:N1,SS:]).a = b by FUNCT_1:def 5;
          b = F.a by A96,A99,A100,FUNCT_1:72;
          then b in F.:[:N1,SS:] by A96,A99,FUNCT_2:43;
          then b in F.:[:N,SS:] by A95;
          then b in Ui by A48;
          hence thesis by A97,PRE_TOPC:29;
        end;
        then
A101:   dom Fni1 = dom (F| [:N1,SS:]) by RELAT_1:46;
        set Fn2 = Fn | [:N1,S:];
A102:   the carrier of (Y|N1) = N1 by PRE_TOPC:29;
        then
A103:   [:N1,S:] = the carrier of [:Y|N1,I[01]|S:] by A64,BORSUK_1:def 5;
        then
A104:   dom Fn2 = the carrier of [:Y|N1,I[01]|S:] by A64,A60,A67,A61,A94,
RELAT_1:91,ZFMISC_1:119;
        reconsider ff = f as Function;
A105:   f is being_homeomorphism by A50,A75;
        then
A106:   f is one-to-one by TOPS_2:def 5;
A107:   rng Fn2 c= R;
        the carrier of (R^1 | Uit) is Subset of R^1 by TSEP_1:1;
        then
A108:   rng Fni1 c= R by XBOOLE_1:1;
A109:   the carrier of (I[01]|SS) = SS by PRE_TOPC:29;
        then
A110:   [:N1,SS:] = the carrier of [:Y|N1,I[01]|SS:] by A102,BORSUK_1:def 5;
        then reconsider
        Fni1 as Function of [:Y|N1,I[01]|SS:], R^1 by A96,A101,A108,FUNCT_2:4;
        set Fn1 = Fn2 +* Fni1;
        reconsider Fn2 as Function of [:Y|N1,I[01]|S:],R^1 by A104,A107,
FUNCT_2:4;
A111:   rng Fn1 c= rng (Fn | [:N1,S:]) \/ rng Fni1 by FUNCT_4:18;
        dom (Fn | [:N1,S:]) = [:N1,S:] by A64,A60,A67,A61,A94,RELAT_1:91
,ZFMISC_1:119;
        then
A112:   dom Fn1 = [:N1,S:] \/ [:N1,SS:] by A96,A101,FUNCT_4:def 1;
A113:   rng f = [#](TUC | Ui) by A105,TOPS_2:def 5;
        then
A114:   f" = ff" by A106,TOPS_2:def 4;
A115:   Y1 = Ti
        proof
          thus Y1 c= Ti by A70;
          let a be set;
          assume a in Ti;
          then a = TT.i by TARSKI:def 1;
          hence thesis by A91,A92,ZFMISC_1:33;
        end;
A116:   Fn.:[:N1,{TT.i}:] c= Uit
        proof
          let b be set;
          assume b in Fn.:[:N1,{TT.i}:];
          then consider a being Point of [:Y|N2,I[01]|S:] such that
A117:     a in [:N1,{TT.i}:] and
A118:     Fn.a = b by FUNCT_2:116;
          a in N5 by A86,A89,A115,A117,PRE_TOPC:def 10;
          then
A119:     a in union SF by A81,TARSKI:def 4;
          then a in dom FnT by A76,FUNCT_1:def 13;
          then Fn.a = FnT.a by FUNCT_1:70;
          hence thesis by A76,A118,A119,FUNCT_1:def 13;
        end;
A120:   for p being set st p in ([#][:Y|N1,I[01]|S:]) /\ [#][:Y|N1,I[01]
        |SS:] holds Fn2.p = Fni1.p
        proof
A121:     the carrier of (Y|N2) = N2 by PRE_TOPC:29;
          let p be set such that
A122:     p in ([#][:Y|N1,I[01]|S:]) /\ [#][:Y|N1,I[01]|SS:];
A123:     p in ([#][:Y|N1,I[01]|SS:]) /\ [#][:Y|N1,I[01]|S:] by A122;
          then
A124:     Fn.p = Fn2.p by A103,FUNCT_1:72;
          [:N1,S:] /\ [:N1,SS:] = [:N1,S /\ SS:] by ZFMISC_1:122;
          then
A125:     p in [:N1,{TT.i}:] by A54,A62,A52,A110,A103,A122,XXREAL_1:418;
          then consider
          p1 being Element of N1, p2 being Element of {TT.i} such
          that
A126:     p = [p1,p2] by DOMAIN_1:9;
A127:     p1 in N1;
          S /\ SS = {TT.i} by A54,A62,A52,XXREAL_1:418;
          then p2 in S by XBOOLE_0:def 4;
          then
A128:     p in [:N2,S:] by A94,A126,A127,ZFMISC_1:def 2;
          then
A129:     Fn.p in Fn.:[:N1,{TT.i}:] by A64,A60,A67,A125,FUNCT_2:43;
          (F| [:N1,SS:]).p = F.p by A110,A122,FUNCT_1:72
            .= (F | [:N2,S:]).p by A128,FUNCT_1:72
            .= CircleMap.(Fn.p) by A58,A64,A60,A61,A121,A128,FUNCT_1:23
            .= (CircleMap|Uit).(Fn.p) by A116,A129,FUNCT_1:72
            .= ff.(Fn2.p) by A103,A123,FUNCT_1:72;
          hence Fn2.p = ff".((F| [:N1,SS:]).p) by A116,A82,A106,A124,A129,
FUNCT_1:54
            .= Fni1.p by A114,A96,A110,A122,FUNCT_1:23;
        end;
A130:   [:N1,S:] c= [:N2,S:] by A94,ZFMISC_1:119;
        then reconsider
        K0 = [:N1,S:] as Subset of [:Y|N2,I[01]|S:] by A64,A60,PRE_TOPC:29;
A131:   [:N1,SS:] c= dom F by A6,ZFMISC_1:119;
        reconsider gF = F| [:N1,SS:] as Function of [:Y|N1,I[01]|SS:], TUC by
A96,A98,A110,FUNCT_2:4;
        reconsider fF = F| [:N1,SS:] as Function of [:Y|N1,I[01]|SS:], TUC |
        Ui by A97,A96,A98,A110,FUNCT_2:4;
        [:Y|N1,I[01]|SS:] = [:Y,I[01]:] | [:N1,SS:] by BORSUK_3:26;
        then gF is continuous by A3,TOPMETR:10;
        then
A132:   fF is continuous by TOPMETR:9;
        f" is continuous by A105,TOPS_2:def 5;
        then f"*fF is continuous by A132;
        then
A133:   Fni1 is continuous by PRE_TOPC:56;
        reconsider aN1 = N1 as non empty Subset of Y|N2 by A94,PRE_TOPC:29;
        S c= S;
        then reconsider aS = S as non empty Subset of I[01]|S by PRE_TOPC:29;
        [:Y|N2,I[01]|S:] | K0 = [:Y|N2|aN1,I[01]|S|aS:] by BORSUK_3:26
          .= [:Y|N1,I[01]|S|aS:] by GOBOARD9:4
          .= [:Y|N1,I[01]|S:] by GOBOARD9:4;
        then
A134:   Fn2 is continuous by A57,TOPMETR:10;
        take N1;
        take S1 = S \/ SS;
A135:   [:N1,S:] \/ [:N1,SS:] = [:N1,S1:] by ZFMISC_1:120;
A136:   the carrier of (I[01]|S1) = S1 by PRE_TOPC:29;
        then [:N1,S1:] = the carrier of [:Y|N1,I[01]|S1:] by A102,
BORSUK_1:def 5;
        then reconsider Fn1 as Function of [:Y|N1,I[01]|S1:], R^1 by A135,A112
,A111,FUNCT_2:4,XBOOLE_1:1;
        take Fn1;
        thus
A137:   S1 = [.0,TT.(i+1).] by A54,A62,A52,XXREAL_1:165;
        0 <= TT.(i+1) by A19,A32;
        then 0 in S1 by A137,XXREAL_1:1;
        then
A138:   {0} c= S1 by ZFMISC_1:37;
A139:   dom (Fn1 | [:the carrier of Y,{0}:]) = dom Fn1 /\ [:the carrier
        of Y,{0}:] by RELAT_1:90;
        then
A140:   dom (Fn1 | [:the carrier of Y,{0}:]) = [:N1 /\ the carrier of Y,
        S1 /\ {0}:] by A135,A112,ZFMISC_1:123
          .= [:N1,S1 /\ {0}:] by XBOOLE_1:28
          .= [:N1,{0}:] by A138,XBOOLE_1:28;
A141:   for a being set st a in dom (Fn1 | [:the carrier of Y,{0}:])
        holds (Fn1 | [:the carrier of Y,{0}:]).a = (Ft | [:N1,I:]).a
        proof
          let a be set;
A142:     [:N1,I:] c= [:N2,I:] by A94,ZFMISC_1:119;
          assume
A143:     a in dom (Fn1 | [:the carrier of Y,{0}:]);
          then
A144:     a in [:the carrier of Y,{0}:] by A139,XBOOLE_0:def 4;
          then consider a1, a2 being set such that
          a1 in the carrier of Y and
A145:     a2 in {0} and
A146:     a = [a1,a2] by ZFMISC_1:def 2;
A147:     a2 = 0 by A145,TARSKI:def 1;
          0 in S by A54,A62,XXREAL_1:1;
          then {0} c= S by ZFMISC_1:37;
          then
A148:     [:N1,{0}:] c= [:N1,S:] by ZFMISC_1:119;
          then
A149:     a in [:N1,S:] by A140,A143;
A150:     [:N1,S:] c= [:N1,I:] by ZFMISC_1:119;
          then
A151:     a in [:N1,I:] by A149;
          per cases;
          suppose
A152:       not a in dom Fni1;
            thus (Fn1 | [:the carrier of Y,{0}:]).a = Fn1.a by A144,FUNCT_1:72
              .= (Fn | [:N1,S:]).a by A152,FUNCT_4:12
              .= Fn.a by A140,A143,A148,FUNCT_1:72
              .= (Ft | [:N2,I:]).a by A59,A144,FUNCT_1:72
              .= Ft.a by A151,A142,FUNCT_1:72
              .= (Ft | [:N1,I:]).a by A149,A150,FUNCT_1:72;
          end;
          suppose
A153:       a in dom Fni1;
            set e = (Ft | [:N1,I:]).a;
            a in [:N1,SS:] by A6,A101,A153,RELAT_1:91,ZFMISC_1:119;
            then consider b1, b2 being set such that
A154:       b1 in N1 and
A155:       b2 in SS and
A156:       a = [b1,b2] by ZFMISC_1:def 2;
            a2 = b2 by A146,A156,ZFMISC_1:33;
            then
A157:       a2 = TT.i by A62,A147,A155,XXREAL_1:1;
            a1 = b1 by A146,A156,ZFMISC_1:33;
            then
A158:       [a1,TT.i] in [:N1,S:] & [a1,TT.i] in [:N1,{TT.i}:] by A63,A78,A154,
ZFMISC_1:106;
            e = Ft.a by A149,A150,FUNCT_1:72
              .= (Ft | [:N2,I:]).a by A151,A142,FUNCT_1:72
              .= Fn.a by A59,A144,FUNCT_1:72;
            then
A159:       e in Fn.:[:N1,{TT.i}:] by A64,A60,A67,A61,A130,A146,A157,A158,
FUNCT_1:def 12;
            then
A160:       ff.e = CircleMap.e by A116,FUNCT_1:72
              .= CircleMap.(Ft.a) by A149,A150,FUNCT_1:72
              .= (CircleMap*Ft).a by A8,A144,FUNCT_1:23
              .= F.a by A5,A144,FUNCT_1:72;
            thus (Fn1 | [:the carrier of Y,{0}:]).a = Fn1.a by A144,FUNCT_1:72
              .= Fni1.a by A153,FUNCT_4:14
              .= ff".((F| [:N1,SS:]).a) by A114,A101,A153,FUNCT_1:23
              .= ff".(F.a) by A96,A101,A153,FUNCT_1:72
              .= (Ft | [:N1,I:]).a by A116,A82,A106,A159,A160,FUNCT_1:54;
          end;
        end;
A161:   rng Fn1 c= dom CircleMap by Lm12,TOPMETR:24;
        then
A162:   dom (CircleMap*Fn1) = dom Fn1 by RELAT_1:46;
A163:   for a being set st a in dom (CircleMap*Fn1) holds (CircleMap*Fn1
        ).a = F . a
        proof
          let a be set such that
A164:     a in dom (CircleMap*Fn1);
          per cases;
          suppose
A165:       a in dom Fni1;
A166:       [:N1,SS:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
A167:       a in [:N1,SS:] by A6,A101,A165,RELAT_1:91,ZFMISC_1:119;
            then F.a in F.:[:N1,SS:] by A6,A166,FUNCT_1:def 12;
            then
A168:       F.a in F.:[:N,SS:] by A95;
            then a in F"(dom (ff")) by A6,A48,A51,A97,A114,A167,A166,
FUNCT_1:def 13;
            then
A169:       a in dom (ff"*F) by RELAT_1:182;
            thus (CircleMap*Fn1).a = CircleMap.(Fn1.a) by A164,FUNCT_2:21
              .= CircleMap.(Fni1.a) by A165,FUNCT_4:14
              .= CircleMap.(f".((F| [:N1,SS:]).a)) by A101,A165,FUNCT_1:23
              .= CircleMap.(f".(F.a)) by A96,A101,A165,FUNCT_1:72
              .= CircleMap.((ff"*F).a) by A131,A114,A96,A101,A165,FUNCT_1:23
              .= (CircleMap*(ff"*F)).a by A169,FUNCT_1:23
              .= (CircleMap*ff"*F).a by RELAT_1:55
              .= (CircleMap*ff").(F.a) by A131,A96,A101,A165,FUNCT_1:23
              .= F.a by A48,A51,A113,A106,A168,TOPALG_3:2;
          end;
          suppose
A170:       not a in dom Fni1;
            then
A171:       a in [:N1,S:] by A96,A101,A112,A162,A164,XBOOLE_0:def 3;
            thus (CircleMap*Fn1).a = CircleMap.(Fn1.a) by A164,FUNCT_2:21
              .= CircleMap.(Fn | [:N1,S:].a) by A170,FUNCT_4:12
              .= CircleMap.(Fn.a) by A171,FUNCT_1:72
              .= (CircleMap*Fn).a by A64,A60,A67,A130,A171,FUNCT_2:21
              .= F.a by A58,A130,A171,FUNCT_1:72;
          end;
        end;
A172:   S c= S1 by XBOOLE_1:7;
        then
A173:   [#](I[01]|S1) = the carrier of (I[01]|S1) & I[01]|S is SubSpace
        of I[01]|S1 by A64,A136,TSEP_1:4;
A174:   SS c= S1 by XBOOLE_1:7;
        then reconsider
        F1 = [#](I[01]|S), F2 = [#](I[01]|SS) as Subset of I[01]|S1
        by A136,A172,PRE_TOPC:29;
        reconsider hS = F1, hSS = F2 as Subset of I[01] by PRE_TOPC:29;
        hS is closed by A54,BORSUK_4:48,PRE_TOPC:29;
        then
A175:   F1 is closed by TSEP_1:8;
        thus y in N1 by A55,A93,XBOOLE_0:def 4;
        thus N1 c= N by A56,A94,XBOOLE_1:1;
        hSS is closed by BORSUK_4:48,PRE_TOPC:29;
        then
A176:   F2 is closed by TSEP_1:8;
        I[01]|SS is SubSpace of I[01]|S1 by A109,A136,A174,TSEP_1:4;
        then ex h being Function of [:Y|N1,I[01]|S1:],R^1 st h = Fn2+*Fni1 &
h is continuous by A64,A109,A136,A173,A175,A176,A134,A133,A120,TOPALG_3:20;
        hence Fn1 is continuous;
        dom Fn1 = dom F /\ [:N1,S1:] by A6,A135,A112,XBOOLE_1:28,ZFMISC_1:119;
        hence F | [:N1,S1:] = CircleMap*Fn1 by A161,A163,FUNCT_1:68,RELAT_1:46;
        dom (Ft | [:N1,I:]) = dom Ft /\ [:N1,I:] by RELAT_1:90
          .= [:(the carrier of Y) /\ N1,{0} /\ I:] by A8,ZFMISC_1:123
          .= [:N1,{0} /\ I:] by XBOOLE_1:28
          .= [:N1,{0}:] by Lm3,XBOOLE_1:28;
        hence thesis by A140,A141,FUNCT_1:9;
      end;
    end;
A177: N[ 0 ] by FINSEQ_3:26;
    for i being Element of NAT holds N[i] from NAT_1:sch 1(A177,A30);
    then consider
    N2 being non empty open Subset of Y, S being non empty Subset of
    I[01], Fn1 being Function of [:Y|N2,I[01]|S:], R^1 such that
A178: S = [.0,TT.len TT.] and
A179: y in N2 and
A180: N2 c= N and
A181: Fn1 is continuous and
A182: F | [:N2,S:] = CircleMap*Fn1 and
A183: Fn1 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] by A17;
    reconsider z = Fn1.x as Point of R^1 by TOPMETR:24;
A184: I[01]|S = I[01] by A12,A178,Lm6,BORSUK_1:83,TSEP_1:3;
    then reconsider Fn1 as Function of [:Y|N2,I[01]:], R^1;
    take z, y, t, N2;
    take Fn1;
    thus x = [y,t] & z = Fn1.x & y in N2 & Fn1 is continuous & F | [:N2,I:] =
CircleMap*Fn1 & Fn1 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] by A10,A12,A178
,A179,A181,A182,A183,A184,BORSUK_1:83;
    let H be Function of [:Y|N2,I[01]:], R^1 such that
A185: H is continuous and
A186: F | [:N2,I:] = CircleMap*H and
A187: H | [:the carrier of Y,{0}:] = Ft | [:N2,I:];
    defpred M[Element of NAT] means $1 in dom TT implies ex Z being non empty
    Subset of I[01] st Z = [.0,TT.$1.] & Fn1 | [:N2,Z:] = H | [:N2,Z:];
A188: dom Fn1 = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
A189: the carrier of [:Y|N2,I[01]:] = [:the carrier of Y|N2,I:] & the
    carrier of Y |N2 = N2 by BORSUK_1:def 5,PRE_TOPC:29;
A190: dom H = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
A191: for i being Element of NAT st M[i] holds M[i+1]
    proof
      let i be Element of NAT such that
A192: M[i] and
A193: i+1 in dom TT;
      per cases by A193,TOPREALA:23;
      suppose
A194:   i = 0;
        set Z = [.0,TT.(i+1).];
A195:   Z = {0} by A11,A194,XXREAL_1:17;
        reconsider Z as non empty Subset of I[01] by A11,A194,Lm3,XXREAL_1:17;
A196:   [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
        then
A197:   dom (Fn1 | [:N2,Z:]) = [:N2,Z:] by A189,A188,RELAT_1:91;
A198:   for x being set st x in dom (Fn1 | [:N2,Z:]) holds (Fn1 | [:N2,Z
        :]).x = (H | [:N2,Z:]).x
        proof
          let x be set;
A199:     [:N2,Z:] c= [:the carrier of Y,Z:] by ZFMISC_1:118;
          assume
A200:     x in dom (Fn1 | [:N2,Z:]);
          hence (Fn1 | [:N2,Z:]).x = Fn1.x by A197,FUNCT_1:72
            .= (Fn1 | [:the carrier of Y,{0}:]).x by A195,A197,A200,A199,
FUNCT_1:72
            .= H.x by A183,A187,A195,A197,A200,A199,FUNCT_1:72
            .= (H | [:N2,Z:]).x by A197,A200,FUNCT_1:72;
        end;
        take Z;
        thus Z = [.0,TT.(i+1).];
        dom (H | [:N2,Z:]) = [:N2,Z:] by A190,A189,A196,RELAT_1:91;
        hence thesis by A188,A198,FUNCT_1:9,RELAT_1:91;
      end;
      suppose
A201:   i in dom TT;
        set ZZ = [.TT.i,TT.(i+1).];
A202:   0 <= TT.i by A19,A201;
A203:   TT.(i+1) <= 1 by A19,A193,A201;
        then reconsider ZZ as Subset of I[01] by A25,A202;
        consider Z being non empty Subset of I[01] such that
A204:   Z = [.0,TT.i.] and
A205:   Fn1 | [:N2,Z:] = H | [:N2,Z:] by A192,A201;
        take Z1 = Z \/ ZZ;
A206:   TT.i < TT.(i+1) by A19,A193,A201;
        hence Z1 = [.0,TT.(i+1).] by A204,A202,XXREAL_1:165;
A207:   [:N2,Z1:] c= [:N2,I:] by ZFMISC_1:118;
        then
A208:   dom (Fn1 | [:N2,Z1:]) = [:N2,Z1:] by A189,A188,RELAT_1:91;
A209:   for x being set st x in dom (Fn1 | [:N2,Z1:]) holds (Fn1 | [:N2,
        Z1:]).x = (H | [:N2,Z1:]).x
        proof
          0 <= TT.(i+1) by A19,A193;
          then
A210:     TT.(i+1) is Point of I[01] by A203,BORSUK_1:86;
          0 <= TT.i & TT.i <= 1 by A19,A193,A201;
          then TT.i is Point of I[01] by BORSUK_1:86;
          then
A211:     ZZ is connected by A206,A210,BORSUK_4:49;
          consider Ui being non empty Subset of TUC such that
A212:     Ui in UL and
A213:     F.:[:N,ZZ:] c= Ui by A16,A193,A201;
          consider D being mutually-disjoint open Subset-Family of R^1 such
          that
A214:     union D = (CircleMap)"Ui and
A215:     for d being Subset of R^1 st d in D for f being Function
of R^1 | d, TUC | Ui st f = CircleMap|d holds f is being_homeomorphism by A2
,A212;
          let x be set such that
A216:     x in dom (Fn1 | [:N2,Z1:]);
          consider x1, x2 being set such that
A217:     x1 in N2 and
A218:     x2 in Z1 and
A219:     x = [x1,x2] by A208,A216,ZFMISC_1:def 2;
A220:     TT.i in ZZ by A206,XXREAL_1:1;
          then [x1,TT.i] in [:N,ZZ:] by A180,A217,ZFMISC_1:106;
          then
A221:     F. [x1,TT.i] in F.:[:N,ZZ:] by FUNCT_2:43;
          reconsider xy = {x1} as non empty Subset of Y by A217,ZFMISC_1:37;
A222:     xy c= N2 by A217,ZFMISC_1:37;
          then reconsider xZZ = [:xy,ZZ:] as Subset of [:Y|N2,I[01]:] by A189,
ZFMISC_1:119;
A223:     dom (H | [:xy,ZZ:]) = [:xy,ZZ:] by A190,A189,A222,RELAT_1:91
,ZFMISC_1:119;
A224:     D is Cover of Fn1.:xZZ
          proof
            let b be set;
A225:       [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            assume b in Fn1.:xZZ;
            then consider a being Point of [:Y|N2,I[01]:] such that
A226:       a in xZZ and
A227:       Fn1.a = b by FUNCT_2:116;
            xy c= N by A180,A217,ZFMISC_1:37;
            then [:xy,ZZ:] c= [:N,ZZ:] by ZFMISC_1:118;
            then a in [:N,ZZ:] by A226;
            then
A228:       F.a in F.:[:N,ZZ:] by A6,A225,FUNCT_1:def 12;
            CircleMap.(Fn1.a) = (CircleMap*Fn1).a by FUNCT_2:21
              .= F.a by A12,A178,A182,A189,BORSUK_1:83,FUNCT_1:72;
            hence b in union D by A213,A214,A227,A228,Lm12,FUNCT_1:def 13
,TOPMETR:24;
          end;
A229:     D is Cover of H.:xZZ
          proof
            let b be set;
A230:       [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            assume b in H.:xZZ;
            then consider a being Point of [:Y|N2,I[01]:] such that
A231:       a in xZZ and
A232:       H.a = b by FUNCT_2:116;
A233:       CircleMap.(H.a) = (CircleMap*H).a by FUNCT_2:21
              .= F.a by A186,A189,FUNCT_1:72;
            xy c= N by A180,A217,ZFMISC_1:37;
            then [:xy,ZZ:] c= [:N,ZZ:] by ZFMISC_1:118;
            then a in [:N,ZZ:] by A231;
            then F.a in F.:[:N,ZZ:] by A6,A230,FUNCT_1:def 12;
            hence b in union D by A213,A214,A232,A233,Lm12,FUNCT_1:def 13
,TOPMETR:24;
          end;
          TT.i in Z by A204,A202,XXREAL_1:1;
          then
A234:     [x1,TT.i] in [:N2,Z:] by A217,ZFMISC_1:106;
          then
A235:     Fn1. [x1,TT.i] = (Fn1 | [:N2,Z:]). [x1,TT.i] by FUNCT_1:72
            .= H. [x1,TT.i] by A205,A234,FUNCT_1:72;
A236:     [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
          then F. [x1,TT.i] = (CircleMap*H). [x1,TT.i] by A186,A234,FUNCT_1:72
            .= CircleMap.(H. [x1,TT.i]) by A190,A189,A234,A236,FUNCT_1:23;
          then H. [x1,TT.i] in (CircleMap)"Ui by A213,A221,FUNCT_2:46
,TOPMETR:24;
          then consider Uith being set such that
A237:     H. [x1,TT.i] in Uith and
A238:     Uith in D by A214,TARSKI:def 4;
          F. [x1,TT.i] = (CircleMap*Fn1). [x1,TT.i] by A12,A178,A182,A234,A236,
BORSUK_1:83,FUNCT_1:72
            .= CircleMap.(Fn1. [x1,TT.i]) by A189,A188,A234,A236,FUNCT_1:23;
          then Fn1. [x1,TT.i] in (CircleMap)"Ui by A213,A221,FUNCT_2:46
,TOPMETR:24;
          then consider Uit being set such that
A239:     Fn1. [x1,TT.i] in Uit and
A240:     Uit in D by A214,TARSKI:def 4;
          I[01] is SubSpace of I[01] by TSEP_1:2;
          then
A241:     [:Y|N2,I[01]:] is SubSpace of [:Y,I[01]:] by BORSUK_3:25;
          xy is connected by A217;
          then [:xy,ZZ:] is connected by A211,TOPALG_3:17;
          then
A242:     xZZ is connected by A241,CONNSP_1:24;
          reconsider Uith as non empty Subset of R^1 by A237,A238;
A243:     x1 in xy by TARSKI:def 1;
          then
A244:     [x1,TT.i] in xZZ by A220,ZFMISC_1:106;
          then H. [x1,TT.i] in H.:xZZ by FUNCT_2:43;
          then Uith meets H.:xZZ by A237,XBOOLE_0:3;
          then
A245:     H.:xZZ c= Uith by A185,A242,A238,A229,TOPALG_3:13,TOPS_2:75;
          reconsider Uit as non empty Subset of R^1 by A239,A240;
          set f = CircleMap|Uit;
A246:     dom f = Uit by Lm12,RELAT_1:91,TOPMETR:24;
A247:     rng f c= Ui
          proof
            let b be set;
            assume b in rng f;
            then consider a being set such that
A248:       a in dom f and
A249:       f.a = b by FUNCT_1:def 5;
            a in union D by A240,A246,A248,TARSKI:def 4;
            then CircleMap.a in Ui by A214,FUNCT_2:46;
            hence thesis by A246,A248,A249,FUNCT_1:72;
          end;
          the carrier of (TUC | Ui) = Ui & the carrier of (R^1 | Uit) =
          Uit by PRE_TOPC:29;
          then reconsider f as Function of R^1 | Uit, TUC | Ui by A246,A247,
FUNCT_2:4;
A250:     dom (Fn1 | [:xy,ZZ:]) = [:xy,ZZ:] by A189,A188,A222,RELAT_1:91
,ZFMISC_1:119;
          H. [x1,TT.i] in H.:xZZ by A190,A244,FUNCT_1:def 12;
          then Uit meets Uith by A239,A245,A235,XBOOLE_0:3;
          then
A251:     Uit = Uith by A240,A238,TAXONOM2:def 5;
A252:     rng (H | [:xy,ZZ:]) c= dom f
          proof
            let b be set;
            assume b in rng (H | [:xy,ZZ:]);
            then consider a being set such that
A253:       a in dom (H | [:xy,ZZ:]) and
A254:       (H | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
            H.a = b by A223,A253,A254,FUNCT_1:72;
            then b in H.:xZZ by A190,A223,A253,FUNCT_1:def 12;
            hence thesis by A245,A251,A246;
          end;
          Fn1. [x1,TT.i] in Fn1.:xZZ by A244,FUNCT_2:43;
          then Uit meets Fn1.:xZZ by A239,XBOOLE_0:3;
          then
A255:     Fn1.:xZZ c= Uit by A181,A184,A240,A242,A224,TOPALG_3:13,TOPS_2:75;
A256:     rng (Fn1 | [:xy,ZZ:]) c= dom f
          proof
            let b be set;
            assume b in rng (Fn1 | [:xy,ZZ:]);
            then consider a being set such that
A257:       a in dom (Fn1 | [:xy,ZZ:]) and
A258:       (Fn1 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
            Fn1.a = b by A250,A257,A258,FUNCT_1:72;
            then b in Fn1.:xZZ by A188,A250,A257,FUNCT_1:def 12;
            hence thesis by A255,A246;
          end;
          then
A259:     dom (f*(Fn1 | [:xy,ZZ:])) = dom (Fn1 | [:xy,ZZ:]) by RELAT_1:46;
A260:     for x being set st x in dom (f*(Fn1 | [:xy,ZZ:])) holds (f*(
          Fn1 | [:xy,ZZ:])).x = (f*(H | [:xy,ZZ:])).x
          proof
            let x be set such that
A261:       x in dom (f*(Fn1 | [:xy,ZZ:]));
A262:       Fn1.x in Fn1.:[:xy,ZZ:] by A188,A250,A259,A261,FUNCT_1:def 12;
A263:       H.x in H.:[:xy,ZZ:] by A190,A250,A259,A261,FUNCT_1:def 12;
            thus (f*(Fn1 | [:xy,ZZ:])).x = ((f*Fn1) | [:xy,ZZ:]).x by
RELAT_1:112
              .= (f*Fn1).x by A250,A259,A261,FUNCT_1:72
              .= f.(Fn1.x) by A188,A261,FUNCT_1:23
              .= CircleMap.(Fn1.x) by A255,A262,FUNCT_1:72
              .= (CircleMap*Fn1).x by A188,A261,FUNCT_1:23
              .= CircleMap.(H.x) by A12,A178,A182,A186,A190,A261,BORSUK_1:83
,FUNCT_1:23
              .= f.(H.x) by A245,A251,A263,FUNCT_1:72
              .= (f*H).x by A190,A261,FUNCT_1:23
              .= ((f*H) | [:xy,ZZ:]).x by A250,A259,A261,FUNCT_1:72
              .= (f*(H | [:xy,ZZ:])).x by RELAT_1:112;
          end;
          f is being_homeomorphism by A215,A240;
          then
A264:     f is one-to-one by TOPS_2:def 5;
          dom (f*(H | [:xy,ZZ:])) = dom (H | [:xy,ZZ:]) by A252,RELAT_1:46;
          then
A265:     f*(Fn1 | [:xy,ZZ:]) = f*(H | [:xy,ZZ:]) by A250,A223,A256,A260,
FUNCT_1:9,RELAT_1:46;
          per cases;
          suppose
            x2 in ZZ;
            then
A266:       x in [:xy,ZZ:] by A219,A243,ZFMISC_1:106;
            thus (Fn1 | [:N2,Z1:]).x = Fn1.x by A208,A216,FUNCT_1:72
              .= (Fn1 | [:xy,ZZ:]).x by A266,FUNCT_1:72
              .= (H | [:xy,ZZ:]).x by A264,A250,A223,A256,A252,A265,FUNCT_1:49
              .= H.x by A266,FUNCT_1:72
              .= (H | [:N2,Z1:]).x by A208,A216,FUNCT_1:72;
          end;
          suppose
            not x2 in ZZ;
            then x2 in Z by A218,XBOOLE_0:def 3;
            then
A267:       x in [:N2,Z:] by A217,A219,ZFMISC_1:106;
            thus (Fn1 | [:N2,Z1:]).x = Fn1.x by A208,A216,FUNCT_1:72
              .= (Fn1 | [:N2,Z:]).x by A267,FUNCT_1:72
              .= H.x by A205,A267,FUNCT_1:72
              .= (H | [:N2,Z1:]).x by A208,A216,FUNCT_1:72;
          end;
        end;
        dom (H | [:N2,Z1:]) = [:N2,Z1:] by A190,A189,A207,RELAT_1:91;
        hence thesis by A188,A209,FUNCT_1:9,RELAT_1:91;
      end;
    end;
A268: M[ 0 ] by FINSEQ_3:26;
    for i being Element of NAT holds M[i] from NAT_1:sch 1(A268,A191);
    then consider Z being non empty Subset of I[01] such that
A269: Z = [.0,TT.len TT.] and
A270: Fn1 | [:N2,Z:] = H | [:N2,Z:] by A17;
    thus Fn1 = Fn1 | [:N2,Z:] by A12,A189,A188,A269,BORSUK_1:83,RELAT_1:98
      .= H by A12,A190,A189,A269,A270,BORSUK_1:83,RELAT_1:98;
  end;
  consider G being Function of [:Y,I[01]:],R^1 such that
A271: for x being Point of [:Y,I[01]:] holds A[x,G.x] from FUNCT_2:sch 3
  (A9 );
  take G;
A272: now
    let N be Subset of Y, F be Function of [:Y|N,I[01]:],R^1;
    thus dom F = the carrier of [:Y|N,I[01]:] by FUNCT_2:def 1
      .= [:the carrier of Y|N,I:] by BORSUK_1:def 5
      .= [:N,I:] by PRE_TOPC:29;
  end;
A273: for p being Point of [:Y,I[01]:], y being Point of Y, t being Point of
  I[01], N1, N2 being non empty open Subset of Y, Fn1 being Function of [:Y|N1,
I[01]:],R^1, Fn2 being Function of [:Y|N2,I[01]:],R^1 st p = [y,t] & y in N1 &
y in N2 & Fn2 is continuous & Fn1 is continuous & F | [:N2,I:] = CircleMap*Fn2
& Fn2 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] & F | [:N1,I:] = CircleMap*Fn1
& Fn1 | [:the carrier of Y,{0}:] = Ft | [:N1,I:] holds Fn1 | [:{y},I:] = Fn2 |
  [:{y},I:]
  proof
    let p be Point of [:Y,I[01]:], y be Point of Y, t be Point of I[01], N1,
N2 be non empty open Subset of Y, Fn1 be Function of [:Y|N1,I[01]:],R^1, Fn2 be
    Function of [:Y|N2,I[01]:],R^1 such that
    p = [y,t] and
A274: y in N1 and
A275: y in N2 and
A276: Fn2 is continuous and
A277: Fn1 is continuous and
A278: F | [:N2,I:] = CircleMap*Fn2 and
A279: Fn2 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] and
A280: F | [:N1,I:] = CircleMap*Fn1 and
A281: Fn1 | [:the carrier of Y,{0}:] = Ft | [:N1,I:];
A282: {y} c= N1 by A274,ZFMISC_1:37;
    consider TT being non empty FinSequence of REAL such that
A283: TT.1 = 0 and
A284: TT.len TT = 1 and
A285: TT is increasing and
A286: ex N being open Subset of Y st y in N & for i being natural
    number st i in dom TT & i+1 in dom TT ex Ui being non empty Subset of
Tunit_circle(2) st Ui in UL & F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A3,A1,Th21;
    consider N being open Subset of Y such that
A287: y in N and
A288: for i being natural number st i in dom TT & i+1 in dom TT ex Ui
being non empty Subset of Tunit_circle(2) st Ui in UL & F.:[:N,[.TT.i,TT.(i+1)
    .]:] c= Ui by A286;
    reconsider N as non empty open Subset of Y by A287;
    defpred M[Element of NAT] means $1 in dom TT implies ex Z being non empty
    Subset of I[01] st Z = [.0,TT.$1.] & Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:];
A289: len TT in dom TT by FINSEQ_5:6;
A290: dom Fn2 = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
A291: dom Fn2 = [:N2,I:] by A272;
A292: {y} c= N2 by A275,ZFMISC_1:37;
A293: the carrier of [:Y|N1,I[01]:] = [:the carrier of Y|N1,I:] & the
    carrier of Y |N1 = N1 by BORSUK_1:def 5,PRE_TOPC:29;
A294: the carrier of [:Y|N2,I[01]:] = [:the carrier of Y|N2,I:] & the
    carrier of Y |N2 = N2 by BORSUK_1:def 5,PRE_TOPC:29;
A295: dom Fn1 = [:N1,I:] by A272;
A296: dom Fn1 = the carrier of [:Y|N1,I[01]:] by FUNCT_2:def 1;
A297: 1 in dom TT by FINSEQ_5:6;
A298: for i being Element of NAT st M[i] holds M[i+1]
    proof
      let i be Element of NAT such that
A299: M[i] and
A300: i+1 in dom TT;
      per cases by A300,TOPREALA:23;
      suppose
A301:   i = 0;
        set Z = [.0,TT.(i+1).];
A302:   Z = {0} by A283,A301,XXREAL_1:17;
        reconsider Z as non empty Subset of I[01] by A283,A301,Lm3,XXREAL_1:17;
A303:   [:{y},Z:] c= [:N2,I:] by A292,ZFMISC_1:119;
A304:   dom (Fn1 | [:{y},Z:]) = [:{y},Z:] by A282,A295,RELAT_1:91,ZFMISC_1:119;
A305:   [:{y},Z:] c= [:N1,I:] by A282,ZFMISC_1:119;
A306:   for x being set st x in dom (Fn1 | [:{y},Z:]) holds (Fn1 | [:{y}
        ,Z:]).x = (Fn2 | [:{y},Z:]).x
        proof
          let x be set;
A307:     [:{y},Z:] c= [:the carrier of Y,Z:] by ZFMISC_1:118;
          assume
A308:     x in dom (Fn1 | [:{y},Z:]);
          hence (Fn1 | [:{y},Z:]).x = Fn1.x by A304,FUNCT_1:72
            .= (Fn1 | [:the carrier of Y,{0}:]).x by A302,A304,A308,A307,
FUNCT_1:72
            .= Ft.x by A281,A305,A304,A308,FUNCT_1:72
            .= (Ft | [:N2,I:]).x by A304,A303,A308,FUNCT_1:72
            .= Fn2.x by A279,A302,A304,A308,A307,FUNCT_1:72
            .= (Fn2 | [:{y},Z:]).x by A304,A308,FUNCT_1:72;
        end;
        take Z;
        thus Z = [.0,TT.(i+1).];
        dom (Fn2 | [:{y},Z:]) = [:{y},Z:] by A292,A291,RELAT_1:91,ZFMISC_1:119;
        hence thesis by A304,A306,FUNCT_1:9;
      end;
      suppose
A309:   i in dom TT;
A310:   now
          let i be Element of NAT such that
A311:     i in dom TT;
          1 <= i by A311,FINSEQ_3:27;
          then 1 = i or 1 < i by XXREAL_0:1;
          hence
A312:     0 <= TT.i by A283,A285,A297,A311,SEQM_3:def 1;
          assume
A313:     i+1 in dom TT;
A314:     i+0 < i+1 by XREAL_1:10;
          hence
A315:     TT.i < TT.(i+1) by A285,A311,A313,SEQM_3:def 1;
          i+1 <= len TT by A313,FINSEQ_3:27;
          then i+1 = len TT or i+1 < len TT by XXREAL_0:1;
          hence TT.(i+1) <= 1 by A284,A285,A289,A313,SEQM_3:def 1;
          hence TT.i < 1 by A315,XXREAL_0:2;
          thus 0 < TT.(i+1) by A285,A311,A312,A313,A314,SEQM_3:def 1;
        end;
        then
A316:   0 <= TT.i by A309;
A317:   TT.(i+1) <= 1 by A300,A309,A310;
        set ZZ = [.TT.i,TT.(i+1).];
        consider Z being non empty Subset of I[01] such that
A318:   Z = [.0,TT.i.] and
A319:   Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:] by A299,A309;
        now
          let i be Element of NAT such that
A320:     0 <= TT.i and
A321:     TT.(i+1) <= 1;
          thus [.TT.i,TT.(i+1).] c= I
          proof
            let a be set;
            assume
A322:       a in [.TT.i,TT.(i+1).];
            then reconsider a as Real;
            a <= TT.(i+1) by A322,XXREAL_1:1;
            then
A323:       a <= 1 by A321,XXREAL_0:2;
            0 <= a by A320,A322,XXREAL_1:1;
            hence thesis by A323,BORSUK_1:86;
          end;
        end;
        then reconsider ZZ as Subset of I[01] by A316,A317;
        take Z1 = Z \/ ZZ;
A324:   TT.i < TT.(i+1) by A300,A309,A310;
        hence Z1 = [.0,TT.(i+1).] by A318,A316,XXREAL_1:165;
A325:   dom (Fn1 | [:{y},Z1:]) = [:{y},Z1:] by A282,A295,RELAT_1:91
,ZFMISC_1:119;
A326:   for x being set st x in dom (Fn1 | [:{y},Z1:]) holds (Fn1 | [:{y
        },Z1:]).x = (Fn2 | [:{y},Z1:]).x
        proof
          0 <= TT.(i+1) by A300,A310;
          then
A327:     TT.(i+1) is Point of I[01] by A317,BORSUK_1:86;
          0 <= TT.i & TT.i <= 1 by A300,A309,A310;
          then TT.i is Point of I[01] by BORSUK_1:86;
          then
A328:     ZZ is connected by A324,A327,BORSUK_4:49;
A329:     TT.i in ZZ by A324,XXREAL_1:1;
          consider Ui being non empty Subset of TUC such that
A330:     Ui in UL and
A331:     F.:[:N,ZZ:] c= Ui by A288,A300,A309;
          consider D being mutually-disjoint open Subset-Family of R^1 such
          that
A332:     union D = (CircleMap)"Ui and
A333:     for d being Subset of R^1 st d in D for f being Function
of R^1 | d, TUC | Ui st f = CircleMap|d holds f is being_homeomorphism by A2
,A330;
          let x be set such that
A334:     x in dom (Fn1 | [:{y},Z1:]);
          consider x1, x2 being set such that
A335:     x1 in {y} and
A336:     x2 in Z1 and
A337:     x = [x1,x2] by A325,A334,ZFMISC_1:def 2;
          reconsider xy = {x1} as non empty Subset of Y by A335,ZFMISC_1:37;
A338:     xy c= N2 by A292,A335,ZFMISC_1:37;
          then
A339:     [:xy,ZZ:] c= [:N2,I:] by ZFMISC_1:119;
A340:     x1 = y by A335,TARSKI:def 1;
          then [x1,TT.i] in [:N,ZZ:] by A287,A329,ZFMISC_1:106;
          then
A341:     F. [x1,TT.i] in F.:[:N,ZZ:] by FUNCT_2:43;
A342:     xy c= N1 by A282,A335,ZFMISC_1:37;
          then reconsider xZZ = [:xy,ZZ:] as Subset of [:Y|N1,I[01]:] by A293,
ZFMISC_1:119;
          xy is connected by A335;
          then
A343:     [:xy,ZZ:] is connected by A328,TOPALG_3:17;
A344:     xy c= N by A287,A340,ZFMISC_1:37;
A345:     D is Cover of Fn1.:xZZ
          proof
            let b be set;
A346:       [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            assume b in Fn1.:xZZ;
            then consider a being Point of [:Y|N1,I[01]:] such that
A347:       a in xZZ and
A348:       Fn1.a = b by FUNCT_2:116;
A349:       CircleMap.(Fn1.a) = (CircleMap*Fn1).a by FUNCT_2:21
              .= F.a by A280,A293,FUNCT_1:72;
            [:xy,ZZ:] c= [:N,ZZ:] by A344,ZFMISC_1:118;
            then a in [:N,ZZ:] by A347;
            then F.a in F.:[:N,ZZ:] by A6,A346,FUNCT_1:def 12;
            hence b in union D by A331,A332,A348,A349,Lm12,FUNCT_1:def 13
,TOPMETR:24;
          end;
A350:     I[01] is SubSpace of I[01] by TSEP_1:2;
          then [:Y|N1,I[01]:] is SubSpace of [:Y,I[01]:] by BORSUK_3:25;
          then
A351:     xZZ is connected by A343,CONNSP_1:24;
          reconsider XZZ = [:xy,ZZ:] as Subset of [:Y|N2,I[01]:] by A294,A338,
ZFMISC_1:119;
          [:Y|N2,I[01]:] is SubSpace of [:Y,I[01]:] by A350,BORSUK_3:25;
          then
A352:     XZZ is connected by A343,CONNSP_1:24;
A353:     D is Cover of Fn2.:xZZ
          proof
            let b be set;
A354:       [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
            assume b in Fn2.:xZZ;
            then consider a being Point of [:Y|N2,I[01]:] such that
A355:       a in xZZ and
A356:       Fn2.a = b by FUNCT_2:116;
A357:       CircleMap.(Fn2.a) = (CircleMap*Fn2).a by FUNCT_2:21
              .= F.a by A278,A294,FUNCT_1:72;
            [:xy,ZZ:] c= [:N,ZZ:] by A344,ZFMISC_1:118;
            then a in [:N,ZZ:] by A355;
            then F.a in F.:[:N,ZZ:] by A6,A354,FUNCT_1:def 12;
            hence b in union D by A331,A332,A356,A357,Lm12,FUNCT_1:def 13
,TOPMETR:24;
          end;
A358:     TT.i in Z by A318,A316,XXREAL_1:1;
          then
A359:     [x1,TT.i] in [:{y},Z:] by A335,ZFMISC_1:106;
A360:     [:{y},Z:] c= [:N1,I:] by A282,ZFMISC_1:119;
          then F. [x1,TT.i] = (CircleMap*Fn1). [x1,TT.i] by A280,A359,
FUNCT_1:72
            .= CircleMap.(Fn1. [x1,TT.i]) by A295,A359,A360,FUNCT_1:23;
          then Fn1. [x1,TT.i] in (CircleMap)"Ui by A331,A341,FUNCT_2:46
,TOPMETR:24;
          then consider Uit being set such that
A361:     Fn1. [x1,TT.i] in Uit and
A362:     Uit in D by A332,TARSKI:def 4;
          reconsider Uit as non empty Subset of R^1 by A361,A362;
          set f = CircleMap|Uit;
A363:     dom f = Uit by Lm12,RELAT_1:91,TOPMETR:24;
A364:     rng f c= Ui
          proof
            let b be set;
            assume b in rng f;
            then consider a being set such that
A365:       a in dom f and
A366:       f.a = b by FUNCT_1:def 5;
            a in union D by A362,A363,A365,TARSKI:def 4;
            then CircleMap.a in Ui by A332,FUNCT_2:46;
            hence thesis by A363,A365,A366,FUNCT_1:72;
          end;
          the carrier of (TUC | Ui) = Ui & the carrier of (R^1 | Uit) =
          Uit by PRE_TOPC:29;
          then reconsider f as Function of R^1 | Uit, TUC | Ui by A363,A364,
FUNCT_2:4;
A367:     [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
A368:     [x1,TT.i] in [:N2,Z:] by A292,A335,A358,ZFMISC_1:106;
          then F. [x1,TT.i] = (CircleMap*Fn2). [x1,TT.i] by A278,A367,
FUNCT_1:72
            .= CircleMap.(Fn2. [x1,TT.i]) by A290,A294,A368,A367,FUNCT_1:23;
          then Fn2. [x1,TT.i] in (CircleMap)"Ui by A331,A341,FUNCT_2:46
,TOPMETR:24;
          then consider Uith being set such that
A369:     Fn2. [x1,TT.i] in Uith and
A370:     Uith in D by A332,TARSKI:def 4;
          reconsider Uith as non empty Subset of R^1 by A369,A370;
A371:     dom (Fn1 | [:xy,ZZ:]) = [:xy,ZZ:] by A293,A296,A342,RELAT_1:91
,ZFMISC_1:119;
A372:     x1 in xy by TARSKI:def 1;
          then
A373:     [x1,TT.i] in xZZ by A329,ZFMISC_1:106;
          then Fn1. [x1,TT.i] in Fn1.:xZZ by FUNCT_2:43;
          then Uit meets Fn1.:xZZ by A361,XBOOLE_0:3;
          then
A374:     Fn1.:xZZ c= Uit by A277,A362,A351,A345,TOPALG_3:13,TOPS_2:75;
A375:     rng (Fn1 | [:xy,ZZ:]) c= dom f
          proof
            let b be set;
            assume b in rng (Fn1 | [:xy,ZZ:]);
            then consider a being set such that
A376:       a in dom (Fn1 | [:xy,ZZ:]) and
A377:       (Fn1 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
            Fn1.a = b by A371,A376,A377,FUNCT_1:72;
            then b in Fn1.:xZZ by A296,A371,A376,FUNCT_1:def 12;
            hence thesis by A374,A363;
          end;
          then
A378:     dom (f*(Fn1 | [:xy,ZZ:])) = dom (Fn1 | [:xy,ZZ:]) by RELAT_1:46;
          [x1,TT.i] in [:xy,ZZ:] by A335,A340,A329,ZFMISC_1:106;
          then [x1,TT.i] in dom Fn2 by A291,A339;
          then
A379:     Fn2. [x1,TT.i] in Fn2.:xZZ by A373,FUNCT_2:43;
          then Uith meets Fn2.:xZZ by A369,XBOOLE_0:3;
          then
A380:     Fn2.:xZZ c= Uith by A276,A370,A352,A353,TOPALG_3:13,TOPS_2:75;
          Fn1. [x1,TT.i] = (Fn1 | [:{y},Z:]). [x1,TT.i] by A359,FUNCT_1:72
            .= Fn2. [x1,TT.i] by A319,A359,FUNCT_1:72;
          then Uit meets Uith by A361,A379,A380,XBOOLE_0:3;
          then
A381:     Uit = Uith by A362,A370,TAXONOM2:def 5;
A382:     for x being set st x in dom (f*(Fn1 | [:xy,ZZ:])) holds (f*(
          Fn1 | [:xy,ZZ:])).x = (f*(Fn2 | [:xy,ZZ:])).x
          proof
A383:       dom (Fn1 | [:xy,ZZ:]) c= dom Fn1 by RELAT_1:89;
            let x be set such that
A384:       x in dom (f*(Fn1 | [:xy,ZZ:]));
A385:       Fn1.x in Fn1.:[:xy,ZZ:] by A296,A371,A378,A384,FUNCT_1:def 12;
A386:       Fn2.x in Fn2.:[:xy,ZZ:] by A291,A339,A371,A378,A384,FUNCT_1:def 12;
            dom (Fn1 | [:xy,ZZ:]) = dom Fn1 /\ [:xy,ZZ:] by RELAT_1:90;
            then
A387:       x in [:xy,ZZ:] by A378,A384,XBOOLE_0:def 4;
            thus (f*(Fn1 | [:xy,ZZ:])).x = ((f*Fn1) | [:xy,ZZ:]).x by
RELAT_1:112
              .= (f*Fn1).x by A371,A378,A384,FUNCT_1:72
              .= f.(Fn1.x) by A296,A384,FUNCT_1:23
              .= CircleMap.(Fn1.x) by A374,A385,FUNCT_1:72
              .= (CircleMap*Fn1).x by A296,A384,FUNCT_1:23
              .= F.x by A280,A295,A378,A384,A383,FUNCT_1:72
              .= (CircleMap*Fn2).x by A278,A339,A387,FUNCT_1:72
              .= CircleMap.(Fn2.x) by A291,A339,A387,FUNCT_1:23
              .= f.(Fn2.x) by A380,A381,A386,FUNCT_1:72
              .= (f*Fn2).x by A291,A339,A387,FUNCT_1:23
              .= ((f*Fn2) | [:xy,ZZ:]).x by A371,A378,A384,FUNCT_1:72
              .= (f*(Fn2 | [:xy,ZZ:])).x by RELAT_1:112;
          end;
A388:     dom (Fn2 | [:xy,ZZ:]) = [:xy,ZZ:] by A290,A294,A338,RELAT_1:91
,ZFMISC_1:119;
A389:     rng (Fn2 | [:xy,ZZ:]) c= dom f
          proof
            let b be set;
            assume b in rng (Fn2 | [:xy,ZZ:]);
            then consider a being set such that
A390:       a in dom (Fn2 | [:xy,ZZ:]) and
A391:       (Fn2 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
            Fn2.a = b by A388,A390,A391,FUNCT_1:72;
            then b in Fn2.:xZZ by A290,A388,A390,FUNCT_1:def 12;
            hence thesis by A380,A381,A363;
          end;
          then dom (f*(Fn2 | [:xy,ZZ:])) = dom (Fn2 | [:xy,ZZ:]) by RELAT_1:46;
          then
A392:     f*(Fn1 | [:xy,ZZ:]) = f*(Fn2 | [:xy,ZZ:]) by A371,A388,A375,A382,
FUNCT_1:9,RELAT_1:46;
          f is being_homeomorphism by A333,A362;
          then
A393:     f is one-to-one by TOPS_2:def 5;
          per cases;
          suppose
            x2 in ZZ;
            then
A394:       x in [:xy,ZZ:] by A337,A372,ZFMISC_1:106;
            thus (Fn1 | [:{y},Z1:]).x = Fn1.x by A325,A334,FUNCT_1:72
              .= (Fn1 | [:xy,ZZ:]).x by A394,FUNCT_1:72
              .= (Fn2 | [:xy,ZZ:]).x by A393,A371,A388,A375,A389,A392,
FUNCT_1:49
              .= Fn2.x by A394,FUNCT_1:72
              .= (Fn2 | [:{y},Z1:]).x by A325,A334,FUNCT_1:72;
          end;
          suppose
            not x2 in ZZ;
            then x2 in Z by A336,XBOOLE_0:def 3;
            then
A395:       x in [:{y},Z:] by A335,A337,ZFMISC_1:106;
            thus (Fn1 | [:{y},Z1:]).x = Fn1.x by A325,A334,FUNCT_1:72
              .= (Fn1 | [:{y},Z:]).x by A395,FUNCT_1:72
              .= Fn2.x by A319,A395,FUNCT_1:72
              .= (Fn2 | [:{y},Z1:]).x by A325,A334,FUNCT_1:72;
          end;
        end;
        dom (Fn2 | [:{y},Z1:]) = [:{y},Z1:] by A292,A290,A294,RELAT_1:91
,ZFMISC_1:119;
        hence thesis by A325,A326,FUNCT_1:9;
      end;
    end;
A396: M[ 0 ] by FINSEQ_3:26;
    for i being Element of NAT holds M[i] from NAT_1:sch 1(A396,A298);
    then ex Z being non empty Subset of I[01] st Z = [.0,TT.len TT.] & Fn1 |
    [:{y},Z:] = Fn2 | [:{y},Z:] by A289;
    hence thesis by A284,BORSUK_1:83;
  end;
  for p being Point of [:Y,I[01]:], V being Subset of R^1 st G.p in V &
  V is open holds ex W being Subset of [:Y,I[01]:] st p in W & W is open & G.:W
  c= V
  proof
    let p be Point of [:Y,I[01]:], V be Subset of R^1 such that
A397: G.p in V & V is open;
    consider y being Point of Y, t being Point of I[01], N being non empty
    open Subset of Y, Fn being Function of [:Y|N,I[01]:], R^1 such that
A398: p = [y,t] and
A399: G.p = Fn.p and
A400: y in N and
A401: Fn is continuous and
A402: F | [:N,I:] = CircleMap*Fn & Fn | [:the carrier of Y,{0}:] = Ft
    | [:N,I:] and
    for H being Function of [:Y|N,I[01]:], R^1 st H is continuous & F | [:
N,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H
    by A271;
A403: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by
BORSUK_1:def 5
      .= [:N,I:] by PRE_TOPC:29;
    then p in the carrier of [:Y|N,I[01]:] by A398,A400,ZFMISC_1:106;
    then consider W being Subset of [:Y|N,I[01]:] such that
A404: p in W and
A405: W is open and
A406: Fn.:W c= V by A397,A399,A401,JGRAPH_2:20;
A407: dom Fn = [:N,I:] by A403,FUNCT_2:def 1;
A408: [#](Y|N) = N by PRE_TOPC:def 10;
    then
A409: [#][:Y|N,I[01]:] = [:N,[#]I[01]:] by BORSUK_3:1;
    [:Y|N,I[01]:] = [:Y,I[01]:] | [:N,[#]I[01]:] by Lm7,BORSUK_3:26;
    then consider C being Subset of [:Y,I[01]:] such that
A410: C is open and
A411: C /\ [#][:Y|N,I[01]:] = W by A405,TOPS_2:32;
    take WW = C /\ [:N,[#]I[01]:];
    thus p in WW by A404,A411,A408,BORSUK_3:1;
    [:N,[#]I[01]:] is open by BORSUK_1:46;
    hence WW is open by A410;
    let y be set;
    assume y in G.:WW;
    then consider x being Point of [:Y,I[01]:] such that
A412: x in WW and
A413: y = G.x by FUNCT_2:116;
    consider y0 being Point of Y, t0 being Point of I[01], N0 being non empty
    open Subset of Y, Fn0 being Function of [:Y|N0,I[01]:], R^1 such that
A414: x = [y0,t0] and
A415: G.x = Fn0.x and
A416: y0 in N0 & Fn0 is continuous & F | [:N0,I:] = CircleMap*Fn0 &
    Fn0 | [:the carrier of Y,{0}:] = Ft | [:N0,I :] and
    for H being Function of [:Y|N0,I[01]:], R^1 st H is continuous & F |
[:N0,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft | [:N0,I:] holds Fn0
    = H by A271;
    x in [:N,[#]I[01]:] by A412,XBOOLE_0:def 4;
    then
A417: y0 in N by A414,ZFMISC_1:106;
A418: x in [:{y0},I:] by A414,ZFMISC_1:128;
    then Fn.x = (Fn | [:{y0},I:]).x by FUNCT_1:72
      .= (Fn0 | [:{y0},I:]).x by A273,A401,A402,A414,A416,A417
      .= Fn0.x by A418,FUNCT_1:72;
    then y in Fn.:W by A407,A411,A409,A412,A413,A415,FUNCT_1:def 12;
    hence thesis by A406;
  end;
  hence G is continuous by JGRAPH_2:20;
  for x being Point of [:Y,I[01]:] holds F.x = (CircleMap*G).x
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y, t being Point of I[01], N being non empty
    open Subset of Y, Fn being Function of [:Y|N,I[01]:], R^1 such that
A419: x = [y,t] and
A420: G.x = Fn.x and
A421: y in N and
    Fn is continuous and
A422: F | [:N,I:] = CircleMap*Fn and
    Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
    for H being Function of [:Y|N,I[01]:], R^1 st H is continuous & F | [:
N,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H
    by A271;
A423: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by
BORSUK_1:def 5
      .= [:N,I:] by PRE_TOPC:29;
    then
A424: x in the carrier of [:Y|N,I[01]:] by A419,A421,ZFMISC_1:106;
    thus (CircleMap*G).x = CircleMap.(G.x) by FUNCT_2:21
      .= (CircleMap*Fn).x by A420,A424,FUNCT_2:21
      .= F.x by A422,A423,A424,FUNCT_1:72;
  end;
  hence F = CircleMap*G by FUNCT_2:113;
A425: [:the carrier of Y,{0}:] c= [:the carrier of Y,I:] by Lm3,ZFMISC_1:118;
A426: the carrier of [:Y,I[01]:] = [:the carrier of Y,I:] by BORSUK_1:def 5;
  then
A427: dom G = [:the carrier of Y,I:] by FUNCT_2:def 1;
A428: for x being set st x in dom Ft holds Ft.x = G.x
  proof
    let x be set;
    assume
A429: x in dom Ft;
    then x in dom G by A8,A427,A425;
    then consider
    y being Point of Y, t being Point of I[01], N being non empty
    open Subset of Y, Fn being Function of [:Y|N,I[01]:], R^1 such that
A430: x = [y,t] and
A431: G.x = Fn.x and
A432: y in N and
    Fn is continuous and
    F | [:N,I:] = CircleMap*Fn and
A433: Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
    for H being Function of [:Y|N,I[01]:], R^1 st H is continuous & F | [:
N,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H
    by A271;
    x in [:N,I:] by A430,A432,ZFMISC_1:106;
    hence Ft.x = (Ft | [:N,I:]).x by FUNCT_1:72
      .= G.x by A7,A429,A431,A433,Lm14,FUNCT_1:72;
  end;
  dom Ft = dom G /\ [:the carrier of Y,{0}:] by A8,A427,A425,XBOOLE_1:28;
  hence G | [:the carrier of Y,{0}:] = Ft by A428,FUNCT_1:68;
  let H be Function of [:Y,I[01]:], R^1 such that
A434: H is continuous & F = CircleMap*H and
A435: H | [:the carrier of Y,{0}:] = Ft;
  for x being Point of [:Y,I[01]:] holds G.x = H.x
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y, t being Point of I[01], N being non empty
    open Subset of Y, Fn being Function of [:Y|N,I[01]:], R^1 such that
A436: x = [y,t] and
A437: G.x = Fn.x and
A438: y in N and
    Fn is continuous and
    F | [:N,I:] = CircleMap*Fn and
    Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
A439: for H being Function of [:Y|N,I[01]:], R^1 st H is continuous &
F | [:N,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds
    Fn = H by A271;
A440: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by
BORSUK_1:def 5
      .= [:N,I:] by PRE_TOPC:29;
    then
A441: x in the carrier of [:Y|N,I[01]:] by A436,A438,ZFMISC_1:106;
    dom H = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
    then [:N,I:] c= dom H by A426,ZFMISC_1:118;
    then
A442: dom (H | [:N,I:]) = [:N,I:] by RELAT_1:91;
    rng (H | [:N,I:]) c= R;
    then reconsider
    H1 = H | [:N,I:] as Function of [:Y|N,I[01]:], R^1 by A440,A442,FUNCT_2:4;
A443: H | [:N,I:] | [:the carrier of Y,{0}:] = H | ([:the carrier of Y,{0}
    :] /\ [:N,I:]) by RELAT_1:100
      .= Ft | [:N,I:] by A435,RELAT_1:100;
    H1 is continuous & F | [:N,I:] = CircleMap*(H| [:N,I:]) by A434,RELAT_1:112
,TOPALG_3:18;
    hence G.x = (H| [:N,I:]).x by A437,A439,A443
      .= H.x by A440,A441,FUNCT_1:72;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th23:
  for x0, y0 being Point of Tunit_circle(2), xt being Point of R^1
, f being Path of x0,y0 st xt in (CircleMap)"{x0} ex ft being Function of I[01]
  , R^1 st ft.0 = xt & f = CircleMap*ft & ft is continuous & for f1 being
Function of I[01], R^1 st f1 is continuous & f = CircleMap*f1 & f1.0 = xt holds
  ft = f1
proof
  set Y = 1TopSp{1};
  let x0, y0 be Point of TUC;
  let xt be Point of R^1;
  let f be Path of x0,y0;
  deffunc F(set,Element of I) = f.$2;
  consider F being Function of [:the carrier of Y,I:], cS1 such that
A1: for y being Element of Y, i being Element of I holds F.(y,i) = F(y,i
  ) from BINOP_1:sch 4;
  reconsider j = 1 as Point of Y by TARSKI:def 1;
A2: [j,j0] in [:the carrier of Y,{0}:] by Lm4,ZFMISC_1:106;
A3: the carrier of [:Y,I[01]:] = [:the carrier of Y, I:] by BORSUK_1:def 5;
  then reconsider F as Function of [:Y,I[01]:], TUC;
  set Ft = [:Y,Sspace(0[01]):] --> xt;
A4: the carrier of [:Y,Sspace(0[01]):] = [:the carrier of Y, the carrier of
  Sspace(0[01]):] by BORSUK_1:def 5;
  then
A5: for y being Element of Y, i being Element of {0} holds Ft. [y,i] = xt
  by Lm14,FUNCOP_1:13;
A6: [#]Y = the carrier of Y;
  for p being Point of [:Y,I[01]:], V being Subset of TUC st F.p in V & V
is open holds ex W being Subset of [:Y,I[01]:] st p in W & W is open & F.:W c=
  V
  proof
    let p be Point of [:Y,I[01]:], V be Subset of TUC such that
A7: F.p in V & V is open;
    consider p1 being Point of Y, p2 being Point of I[01] such that
A8: p = [p1,p2] by BORSUK_1:50;
    F.(p1,p2) = f.p2 by A1;
    then consider S being Subset of I[01] such that
A9: p2 in S and
A10: S is open and
A11: f.:S c= V by A7,A8,JGRAPH_2:20;
    set W = [:{1},S:];
    W c= [:the carrier of Y,I:] by ZFMISC_1:118;
    then reconsider W as Subset of [:Y,I[01]:] by BORSUK_1:def 5;
    take W;
    thus p in W by A8,A9,ZFMISC_1:106;
    thus W is open by A6,A10,BORSUK_1:46;
    let y be set;
    assume y in F.:W;
    then consider x being set such that
A12: x in the carrier of [:Y,I[01]:] and
A13: x in W and
A14: y = F.x by FUNCT_2:115;
    consider x1 being Point of Y, x2 being Point of I[01] such that
A15: x = [x1,x2] by A12,BORSUK_1:50;
    x2 in S by A13,A15,ZFMISC_1:106;
    then
A16: f.x2 in f.:S by FUNCT_2:43;
    y = F.(x1,x2) by A14,A15
      .= f.x2 by A1;
    hence thesis by A11,A16;
  end;
  then
A17: F is continuous by JGRAPH_2:20;
  assume xt in (CircleMap)"{x0};
  then
A18: CircleMap.xt in {x0} by FUNCT_2:46;
A19: for x being set st x in dom (CircleMap*Ft) holds (F | [:the carrier of
  Y,{0}:]).x = (CircleMap*Ft).x
  proof
    let x be set such that
A20: x in dom (CircleMap*Ft);
    consider x1, x2 being set such that
A21: x1 in the carrier of Y and
A22: x2 in {0} and
A23: x = [x1,x2] by A4,A20,Lm14,ZFMISC_1:def 2;
A24: x2 = 0 by A22,TARSKI:def 1;
    thus (F | [:the carrier of Y,{0}:]).x = F.(x1,x2) by A4,A20,A23,Lm14,
FUNCT_1:72
      .= f.x2 by A1,A21,A24,Lm2
      .= x0 by A24,BORSUK_2:def 4
      .= CircleMap.xt by A18,TARSKI:def 1
      .= CircleMap.(Ft.x) by A5,A21,A22,A23
      .= (CircleMap*Ft).x by A20,FUNCT_1:22;
  end;
A25: dom (CircleMap*Ft) = [:the carrier of Y,{0}:] by A4,Lm14,FUNCT_2:def 1;
A26: dom F = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
  then
A27: [:the carrier of Y,{0}:] c= dom F by A3,Lm3,ZFMISC_1:118;
  then dom (F | [:the carrier of Y,{0}:]) = [:the carrier of Y,{0}:] by
RELAT_1:91;
  then consider G being Function of [:Y,I[01]:], R^1 such that
A28: G is continuous and
A29: F = CircleMap*G and
A30: G | [:the carrier of Y,{0}:] = Ft and
A31: for H being Function of [:Y,I[01]:], R^1 st H is continuous & F =
  CircleMap*H & H | [:the carrier of Y,{0}:] = Ft holds G = H by A17,A25,A19
,Th22,FUNCT_1:9;
  take ft = Prj2(j,G);
  thus ft.0 = G.(j,j0) by Def3
    .= Ft. [j,j0] by A30,A2,FUNCT_1:72
    .= xt by A5,Lm4;
  for i being Point of I[01] holds f.i = (CircleMap*ft).i
  proof
    let i be Point of I[01];
A32: the carrier of [:Y,I[01]:] = [:the carrier of Y, the carrier of I[01]
    :] by BORSUK_1:def 5;
    thus (CircleMap*ft).i = CircleMap.(ft.i) by FUNCT_2:21
      .= CircleMap.(G.(j,i)) by Def3
      .= (CircleMap*G).(j,i) by A32,BINOP_1:30
      .= f.i by A1,A29;
  end;
  hence f = CircleMap*ft by FUNCT_2:113;
  thus ft is continuous by A28;
  let f1 be Function of I[01], R^1;
  deffunc H(set,Element of I) = f1.$2;
  consider H being Function of [:the carrier of Y,I:], R such that
A33: for y being Element of Y, i being Element of I holds H.(y,i) = H(y,
  i) from BINOP_1:sch 4;
  reconsider H as Function of [:Y,I[01]:], R^1 by A3;
  assume that
A34: f1 is continuous and
A35: f = CircleMap*f1 and
A36: f1.0 = xt;
  for p being Point of [:Y,I[01]:], V being Subset of R^1 st H.p in V & V
is open holds ex W being Subset of [:Y,I[01]:] st p in W & W is open & H.:W c=
  V
  proof
    let p be Point of [:Y,I[01]:], V be Subset of R^1 such that
A37: H.p in V & V is open;
    consider p1 being Point of Y, p2 being Point of I[01] such that
A38: p = [p1,p2] by BORSUK_1:50;
    H.p = H.(p1,p2) by A38
      .= f1.p2 by A33;
    then consider W being Subset of I[01] such that
A39: p2 in W and
A40: W is open and
A41: f1.:W c= V by A34,A37,JGRAPH_2:20;
    take W1 = [:[#]Y,W:];
    thus p in W1 by A38,A39,ZFMISC_1:106;
    thus W1 is open by A40,BORSUK_1:46;
    let y be set;
    assume y in H.:W1;
    then consider c being Element of [:Y,I[01]:] such that
A42: c in W1 and
A43: y = H.c by FUNCT_2:116;
    consider c1 being set, c2 being set such that
A44: c1 in [#]Y and
A45: c2 in W and
A46: c = [c1,c2] by A42,ZFMISC_1:def 2;
A47: f1.c2 in f1.:W by A45,FUNCT_2:43;
    H.c = H.(c1,c2) by A46
      .= f1.c2 by A33,A44,A45;
    hence thesis by A41,A43,A47;
  end;
  then
A48: H is continuous by JGRAPH_2:20;
  for x being Point of [:Y,I[01]:] holds F.x = (CircleMap*H).x
  proof
    let x be Point of [:Y,I[01]:];
    consider x1 being Point of Y, x2 being Point of I[01] such that
A49: x = [x1,x2] by BORSUK_1:50;
    thus (CircleMap*H).x = CircleMap.(H.(x1,x2)) by A49,FUNCT_2:21
      .= CircleMap.(f1.x2) by A33
      .= (CircleMap*f1).x2 by FUNCT_2:21
      .= F.(x1,x2) by A1,A35
      .= F.x by A49;
  end;
  then
A50: F = CircleMap*H by FUNCT_2:113;
  for i being Point of I[01] holds ft.i = f1.i
  proof
    let i be Point of I[01];
A51: dom H = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
    then
A52: dom (H | [:the carrier of Y,{0}:]) = [:the carrier of Y,{0}:] by A26,A27,
RELAT_1:91;
A53: for x being set st x in dom (H | [:the carrier of Y,{0}:]) holds (H |
    [:the carrier of Y,{0}:]).x = Ft.x
    proof
      let x be set;
      assume
A54:  x in dom (H | [:the carrier of Y,{0}:]);
      then consider x1, x2 being set such that
A55:  x1 in the carrier of Y and
A56:  x2 in {0} and
A57:  x = [x1,x2] by A52,ZFMISC_1:def 2;
A58:  x2 = j0 by A56,TARSKI:def 1;
      thus (H | [:the carrier of Y,{0}:]).x = H.(x1,x2) by A54,A57,FUNCT_1:70
        .= f1.x2 by A33,A55,A58
        .= Ft.x by A5,A36,A55,A56,A57,A58;
    end;
    dom Ft = [:the carrier of Y,{0}:] by A4,Lm14,FUNCT_2:def 1;
    then
A59: H | [:the carrier of Y,{0}:] = Ft by A26,A27,A51,A53,FUNCT_1:9,RELAT_1:91;
    thus ft.i = G.(j,i) by Def3
      .= H.(j,i) by A31,A50,A48,A59
      .= f1.i by A33;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th24:
  for x0, y0 being Point of Tunit_circle(2), P, Q being Path of x0
  ,y0, F being Homotopy of P,Q, xt being Point of R^1 st P,Q are_homotopic & xt
  in (CircleMap)"{x0} ex yt being Point of R^1, Pt, Qt being Path of xt,yt, Ft
  being Homotopy of Pt,Qt st Pt,Qt are_homotopic & F = CircleMap*Ft & yt in (
CircleMap)"{y0} & for F1 being Homotopy of Pt,Qt st F = CircleMap*F1 holds Ft =
  F1
proof
  let x0, y0 be Point of TUC;
  let P, Q be Path of x0,y0;
  let F be Homotopy of P,Q;
  let xt be Point of R^1;
  consider cP1 being constant Loop of x0;
  set g1 = I[01] --> xt;
  consider cP2 being constant Loop of y0;
  assume
A1: P,Q are_homotopic;
  then
A2: F is continuous by BORSUK_6:def 13;
  assume
A3: xt in (CircleMap)"{x0};
  then consider ft being Function of I[01], R^1 such that
A4: ft.0 = xt and
A5: P = CircleMap*ft and
A6: ft is continuous and
  for f1 being Function of I[01], R^1 st f1 is continuous & P = CircleMap*
  f1 & f1.0 = xt holds ft = f1 by Th23;
  defpred P[set,set,set] means $3 = ft.$1;
A7: for x being Element of I for y being Element of {0} ex z being Element
  of REAL st P[x,y,z];
  consider Ft being Function of [:I,{0}:], REAL such that
A8: for y being Element of I, i being Element of {0} holds P[y,i, Ft.(y
  ,i)] from BINOP_1:sch 3(A7);
  CircleMap.xt in {x0} by A3,FUNCT_2:46;
  then
A9: CircleMap.xt = x0 by TARSKI:def 1;
A10: for x being Point of I[01] holds cP1.x = (CircleMap*g1).x
  proof
    let x be Point of I[01];
    thus cP1.x = x0 by TOPALG_3:22
      .= CircleMap.(g1.x) by A9,TOPALG_3:5
      .= (CircleMap*g1).x by FUNCT_2:21;
  end;
  consider ft1 being Function of I[01], R^1 such that
  ft1.0 = xt and
  cP1 = CircleMap*ft1 and
  ft1 is continuous and
A11: for f1 being Function of I[01], R^1 st f1 is continuous & cP1 =
  CircleMap*f1 & f1.0 = xt holds ft1 = f1 by A3,Th23;
  g1.j0 = xt by TOPALG_3:5;
  then
A12: ft1 = g1 by A11,A10,FUNCT_2:113;
A13: rng Ft c= REAL;
A14: dom Ft = [:I,{0}:] by FUNCT_2:def 1;
A15: the carrier of [:I[01],Sspace(0[01]):] = [:I, the carrier of Sspace(
  0[01]):] by BORSUK_1:def 5;
  then reconsider Ft as Function of [:I[01],Sspace(0[01]):], R^1 by Lm14,
TOPMETR:24;
A16: for x being set st x in dom (CircleMap*Ft) holds (F | [:I,{0}:]).x = (
  CircleMap*Ft).x
  proof
    let x be set such that
A17: x in dom (CircleMap*Ft);
    consider x1, x2 being set such that
A18: x1 in I and
A19: x2 in {0} and
A20: x = [x1,x2] by A15,A17,Lm14,ZFMISC_1:def 2;
    x2 = 0 by A19,TARSKI:def 1;
    hence (F | [:I,{0}:]).x = F.(x1,0) by A15,A17,A20,Lm14,FUNCT_1:72
      .= (CircleMap*ft).x1 by A1,A5,A18,BORSUK_6:def 13
      .= CircleMap.(ft.x1) by A18,FUNCT_2:21
      .= CircleMap.(Ft.(x1,x2)) by A8,A18,A19
      .= (CircleMap*Ft).x by A17,A20,FUNCT_1:22;
  end;
  for p being Point of [:I[01],Sspace(0[01]):], V being Subset of R^1 st
  Ft.p in V & V is open holds ex W being Subset of [:I[01],Sspace(0[01]):] st p
  in W & W is open & Ft.:W c= V
  proof
    let p be Point of [:I[01],Sspace(0[01]):], V be Subset of R^1 such that
A21: Ft.p in V & V is open;
    consider p1 being Point of I[01], p2 being Point of Sspace(0[01]) such
    that
A22: p = [p1,p2] by A15,DOMAIN_1:9;
    P[p1,p2,Ft.(p1,p2)] by A8,Lm14;
    then consider W1 being Subset of I[01] such that
A23: p1 in W1 and
A24: W1 is open and
A25: ft.:W1 c= V by A6,A21,A22,JGRAPH_2:20;
    reconsider W1 as non empty Subset of I[01] by A23;
    take W = [:W1,[#]Sspace(0[01]):];
    thus p in W by A22,A23,ZFMISC_1:def 2;
    thus W is open by A24,BORSUK_1:46;
    let y be set;
    assume y in Ft.:W;
    then consider x being Element of [:I[01],Sspace(0[01]):] such that
A26: x in W and
A27: y = Ft.x by FUNCT_2:116;
    consider x1 being Element of W1, x2 being Point of Sspace(0[01]) such that
A28: x = [x1,x2] by A26,DOMAIN_1:9;
    ( P[x1,x2,Ft.(x1,x2)])& ft.x1 in ft.:W1 by A8,Lm14,FUNCT_2:43;
    hence thesis by A25,A27,A28;
  end;
  then
A29: Ft is continuous by JGRAPH_2:20;
  take yt = ft.j1;
A30: [j1,j0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
  reconsider ft as Path of xt,yt by A4,A6,BORSUK_2:def 4;
A31: [j0,j0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
A32: dom F = the carrier of [:I[01],I[01]:] by FUNCT_2:def 1;
  then
A33: [:I,{0}:] c= dom F by Lm3,Lm5,ZFMISC_1:118;
  then dom (F | [:I,{0}:]) = [:I,{0}:] by RELAT_1:91;
  then F | [:I,{0}:] = CircleMap*Ft by A14,A13,A16,Lm12,FUNCT_1:9,RELAT_1:46;
  then consider G being Function of [:I[01],I[01]:], R^1 such that
A34: G is continuous and
A35: F = CircleMap*G and
A36: G | [:I,{0}:] = Ft and
A37: for H being Function of [:I[01],I[01]:], R^1 st H is continuous & F
  = CircleMap*H & H | [:the carrier of I[01],{0}:] = Ft holds G = H by A2,A29
,Th22;
  set sM0 = Prj2(j0,G);
A38: for x being Point of I[01] holds cP1.x = (CircleMap*sM0).x
  proof
    let x be Point of I[01];
    thus (CircleMap*sM0).x = CircleMap.(sM0.x) by FUNCT_2:21
      .= CircleMap.(G.(j0,x)) by Def3
      .= (CircleMap*G).(j0,x) by Lm5,BINOP_1:30
      .= x0 by A1,A35,BORSUK_6:def 13
      .= cP1.x by TOPALG_3:22;
  end;
  set g2 = I[01] --> yt;
A39: CircleMap.yt = P.j1 by A5,FUNCT_2:21
    .= y0 by BORSUK_2:def 4;
A40: for x being Point of I[01] holds cP2.x = (CircleMap*g2).x
  proof
    let x be Point of I[01];
    thus cP2.x = y0 by TOPALG_3:22
      .= CircleMap.(g2.x) by A39,TOPALG_3:5
      .= (CircleMap*g2).x by FUNCT_2:21;
  end;
A41: CircleMap.yt in {y0} by A39,TARSKI:def 1;
  then yt in (CircleMap)"{y0} by FUNCT_2:46;
  then consider ft2 being Function of I[01], R^1 such that
  ft2.0 = yt and
  cP2 = CircleMap*ft2 and
  ft2 is continuous and
A42: for f1 being Function of I[01], R^1 st f1 is continuous & cP2 =
  CircleMap*f1 & f1.0 = yt holds ft2 = f1 by Th23;
  g2.j0 = yt by TOPALG_3:5;
  then
A43: ft2 = g2 by A42,A40,FUNCT_2:113;
  set sM1 = Prj2(j1,G);
A44: for x being Point of I[01] holds cP2.x = (CircleMap*sM1).x
  proof
    let x be Point of I[01];
    thus (CircleMap*sM1).x = CircleMap.(sM1.x) by FUNCT_2:21
      .= CircleMap.(G.(j1,x)) by Def3
      .= (CircleMap*G).(j1,x) by Lm5,BINOP_1:30
      .= y0 by A1,A35,BORSUK_6:def 13
      .= cP2.x by TOPALG_3:22;
  end;
  sM1.0 = G.(j1,j0) by Def3
    .= Ft.(j1,j0) by A36,A30,FUNCT_1:72
    .= yt by A8,Lm4;
  then
A45: ft2 = sM1 by A34,A42,A44,FUNCT_2:113;
  sM0.0 = G.(j0,j0) by Def3
    .= Ft.(j0,j0) by A36,A31,FUNCT_1:72
    .= xt by A4,A8,Lm4;
  then
A46: ft1 = sM0 by A34,A11,A38,FUNCT_2:113;
  set Qt = Prj1(j1,G);
A47: Qt.0 = G.(j0,j1) by Def2
    .= sM0.j1 by Def3
    .= xt by A46,A12,TOPALG_3:5;
  Qt.1 = G.(j1,j1) by Def2
    .= sM1.j1 by Def3
    .= yt by A45,A43,TOPALG_3:5;
  then reconsider Qt as Path of xt,yt by A34,A47,BORSUK_2:def 4;
A48: now
    let s be Point of I[01];
    [s,0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
    hence G.(s,0) = Ft.(s,j0) by A36,FUNCT_1:72
      .= ft.s by A8,Lm4;
    thus G.(s,1) = Qt.s by Def2;
    let t be Point of I[01];
    thus G.(0,t) = sM0.t by Def3
      .= xt by A46,A12,TOPALG_3:5;
    thus G.(1,t) = sM1.t by Def3
      .= yt by A45,A43,TOPALG_3:5;
  end;
  then ft,Qt are_homotopic by A34,BORSUK_2:def 7;
  then reconsider G as Homotopy of ft,Qt by A34,A48,BORSUK_6:def 13;
  take ft, Qt;
  take G;
  thus
A49: ft,Qt are_homotopic by A34,A48,BORSUK_2:def 7;
  thus F = CircleMap*G by A35;
  thus yt in (CircleMap)"{y0} by A41,FUNCT_2:46;
  let F1 be Homotopy of ft,Qt such that
A50: F = CircleMap*F1;
A51: dom F1 = the carrier of [:I[01],I[01]:] by FUNCT_2:def 1;
  then
A52: dom (F1 | [:I,{0}:]) = [:I,{0}:] by A32,A33,RELAT_1:91;
  for x being set st x in dom (F1 | [:I,{0}:]) holds (F1 | [:I,{0}:]).x = Ft.x
  proof
    let x be set;
    assume
A53: x in dom (F1 | [:I,{0}:]);
    then consider x1, x2 being set such that
A54: x1 in I and
A55: x2 in {0} and
A56: x = [x1,x2] by A52,ZFMISC_1:def 2;
A57: x2 = 0 by A55,TARSKI:def 1;
    thus (F1 | [:I,{0}:]).x = F1.(x1,x2) by A53,A56,FUNCT_1:70
      .= ft.x1 by A49,A54,A57,BORSUK_6:def 13
      .= Ft.(x1,x2) by A8,A54,A55
      .= Ft.x by A56;
  end;
  then F1 | [:I,{0}:] = Ft by A32,A33,A14,A51,FUNCT_1:9,RELAT_1:91;
  hence thesis by A37,A50;
end;

definition
  func Ciso -> Function of INT.Group, pi_1(Tunit_circle(2),c[10]) means
  :Def5:
for n being Integer holds it.n = Class(EqRel(Tunit_circle(2),c[10]),cLoop(n));
  existence
  proof
    defpred P[Integer,set] means $2 = Class(EqRel(TUC,c[10]),cLoop($1));
A1: for x being Element of INT ex y being Element of pi_1(TUC,c[10]) st P[ x,y]
    proof
      let x be Element of INT;
      reconsider y = Class(EqRel(TUC,c[10]),cLoop(x)) as Element of pi_1(TUC,
      c[10]) by TOPALG_1:48;
      take y;
      thus thesis;
    end;
    consider f being Function of INT,the carrier of pi_1(TUC,c[10]) such that
A2: for x being Element of INT holds P[x,f.x] from FUNCT_2:sch 3(A1);
    reconsider f as Function of INT.Group, pi_1(TUC,c[10]);
    take f;
    let n be Integer;
    n in INT by INT_1:def 2;
    hence thesis by A2;
  end;
  uniqueness
  proof
    let f, g be Function of INT.Group, pi_1(TUC,c[10]) such that
A3: for n being Integer holds f.n = Class(EqRel(TUC,c[10]),cLoop(n)) and
A4: for n being Integer holds g.n = Class(EqRel(TUC,c[10]),cLoop(n));
    for x being Element of INT.Group holds f.x = g.x
    proof
      let x be Element of INT.Group;
      thus f.x = Class(EqRel(TUC,c[10]),cLoop(x)) by A3
        .= g.x by A4;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem Th25:
  for i being Integer, f being Path of R^1(0),R^1(i) holds Ciso.i
  = Class(EqRel(Tunit_circle(2),c[10]),CircleMap*f)
proof
  let i be Integer;
  let f be Path of R^1(0),R^1(i);
  set P = CircleMap*f;
A1: P.0 = CircleMap.(f.j0) by FUNCT_2:21
    .= CircleMap.R^1(0) by BORSUK_2:def 4
    .= CircleMap.0 by TOPREALB:def 2
    .= |[ cos(2*PI*0), sin(2*PI*0) ]| by TOPREALB:def 11
    .= c[10] by SIN_COS:34,TOPREALB:def 8;
  P.1 = CircleMap.(f.j1) by FUNCT_2:21
    .= CircleMap.R^1(i) by BORSUK_2:def 4
    .= CircleMap.i by TOPREALB:def 2
    .= |[ cos(2*PI*i+0), sin(2*PI*i+0) ]| by TOPREALB:def 11
    .= |[ cos(0), sin(2*PI*i+0) ]| by COMPLEX2:10
    .= c[10] by COMPLEX2:9,SIN_COS:34,TOPREALB:def 8;
  then reconsider P as Loop of c[10] by A1,BORSUK_2:def 4;
A2: cLoop(i) = CircleMap * ExtendInt(i) by Th20;
A3: cLoop(i),P are_homotopic
  proof
    reconsider J = R^1 as non empty interval SubSpace of R^1;
    reconsider r0 = R^1(0), ri = R^1(i) as Point of J;
    reconsider O = ExtendInt(i), ff = f as Path of r0,ri;
    reconsider G = R1Homotopy(O,ff) as Function of [:I[01],I[01]:],R^1;
    take F = CircleMap*G;
    thus F is continuous;
    let s be Point of I[01];
    thus F.(s,0) = CircleMap.(G.(s,j0)) by Lm5,BINOP_1:30
      .= CircleMap.((1-j0) * (ExtendInt(i)).s + j0 * f.s) by TOPALG_2:def 5
      .= (cLoop(i)).s by A2,FUNCT_2:21;
    thus F.(s,1) = CircleMap.(G.(s,j1)) by Lm5,BINOP_1:30
      .= CircleMap.((1-j1) * (ExtendInt(i)).s + j1 * f.s) by TOPALG_2:def 5
      .= P.s by FUNCT_2:21;
    thus F.(0,s) = CircleMap.(G.(j0,s)) by Lm5,BINOP_1:30
      .= CircleMap.((1-s) * (ExtendInt(i)).j0 + s * f.j0) by TOPALG_2:def 5
      .= CircleMap.((1-s) * R^1(0) + s * f.j0) by BORSUK_2:def 4
      .= CircleMap.((1-s) * R^1(0) + s * R^1(0)) by BORSUK_2:def 4
      .= CircleMap.((1-s) * 0 + s * 0) by TOPREALB:def 2
      .= c[10] by TOPREALB:33;
    thus F.(1,s) = CircleMap.(G.(j1,s)) by Lm5,BINOP_1:30
      .= CircleMap.((1-s) * (ExtendInt(i)).j1 + s * f.j1) by TOPALG_2:def 5
      .= CircleMap.((1-s) * R^1(i) + s * f.j1) by BORSUK_2:def 4
      .= CircleMap.((1-s) * R^1(i) + s * R^1(i)) by BORSUK_2:def 4
      .= CircleMap.i by TOPREALB:def 2
      .= c[10] by TOPREALB:33;
  end;
  thus Ciso.i = Class(EqRel(TUC,c[10]),cLoop(i)) by Def5
    .= Class(EqRel(TUC,c[10]),CircleMap*f) by A3,TOPALG_1:47;
end;

registration
  cluster Ciso -> multiplicative;
  coherence
  proof
    set f = Ciso;
    let x, y be Element of INT.Group;
    consider fX, fY being Loop of c[10] such that
A1: f.x = Class(EqRel(TUC,c[10]),fX) and
A2: f.y = Class(EqRel(TUC,c[10]),fY) and
A3: (the multF of pi_1(TUC,c[10])).(f.x,f.y) = Class(EqRel(TUC,c[10]
    ),fX+fY) by TOPALG_1:def 5;
    f.y = Class(EqRel(TUC,c[10]),cLoop(y)) by Def5;
    then
A4: fY,cLoop(y) are_homotopic by A2,TOPALG_1:47;
    reconsider tx = AffineMap(1,x) as Function of R^1,R^1 by TOPMETR:24;
    set p = tx*ExtendInt(y);
A5: p.0 = tx.((ExtendInt(y)).j0) by FUNCT_2:21
    .= tx.(y*j0) by Def1
    .= 1*0+x by JORDAN16:def 3
    .= R^1(x) by TOPREALB:def 2;
A6: p.1 = tx.((ExtendInt(y)).j1) by FUNCT_2:21
    .= tx.(y*j1) by Def1
    .= 1*y+x by JORDAN16:def 3
    .= R^1(x+y) by TOPREALB:def 2;
    x is Real by XREAL_0:def 1;
    then tx is being_homeomorphism by JORDAN16:35;
    then tx is continuous by TOPS_2:def 5;
    then reconsider p as Path of R^1(x),R^1(x+y) by A5,A6,BORSUK_2:def 4;
A7: for a being Point of I[01] holds (CircleMap * (ExtendInt(x)+p)).a =
    (cLoop(x)+cLoop(y)).a
    proof
      let a be Point of I[01];
      per cases;
      suppose
A8:     a <= 1/2;
        then
A9:     2*a is Point of I[01] by BORSUK_6:6;
        thus (CircleMap * (ExtendInt(x)+p)).a = CircleMap.((ExtendInt(x)+p).
        a) by FUNCT_2:21
        .= CircleMap.((ExtendInt(x)).(2*a)) by A8,BORSUK_6:def 4
        .= CircleMap.(x*(2*a)) by A9,Def1
        .= |[ cos(2*PI*(x*(2*a))), sin(2*PI*(x*(2*a))) ]| by
TOPREALB: def 11
          .= |[ cos(2*PI*x*(2*a)), sin(2*PI*x*(2*a)) ]|
          .= (cLoop(x)).(2*a) by A9,Def4
          .= (cLoop(x)+cLoop(y)).a by A8,BORSUK_6:def 4;
        end;
        suppose
A10:      1/2 <= a;
          then
A11:      2*a-1 is Point of I[01] by BORSUK_6:7;
          thus (CircleMap * (ExtendInt(x)+p)).a = CircleMap.((ExtendInt(x)+p).
          a) by FUNCT_2:21
          .= CircleMap.(p.(2*a-1)) by A10,BORSUK_6:def 4
          .= CircleMap.(tx.((ExtendInt(y)).(2*a-1))) by A11,FUNCT_2:21
          .= CircleMap.(tx.(y*(2*a-1))) by A11,Def1
          .= CircleMap.(1*(y*(2*a-1))+x) by JORDAN16:def 3
          .= |[ cos(2*PI*((y*(2*a-1))+x)), sin(2*PI*((y*(2*a-1))+x)) ]| by
TOPREALB: def 11
          .= |[ cos(2*PI*(y*(2*a-1))), sin(2*PI*(y*(2*a-1))+2*PI*x) ]| by
COMPLEX2: 10
          .= |[ cos(2*PI*y*(2*a-1)), sin(2*PI*y*(2*a-1)) ]| by COMPLEX2:9
          .= (cLoop(y)).(2*a-1) by A11,Def4
          .= (cLoop(x)+cLoop(y)).a by A10,BORSUK_6:def 4;
        end;
      end;
      f.x = Class(EqRel(TUC,c[10]),cLoop(x)) by Def5;
      then fX,cLoop(x) are_homotopic by A1,TOPALG_1:47;
      then
A12:  fX+fY,cLoop(x)+cLoop(y) are_homotopic by A4,BORSUK_6:84;
      thus f.(x*y) = Class(EqRel(TUC,c[10]),CircleMap*(ExtendInt(x)+p)) by Th25
      .= Class(EqRel(TUC,c[10]),cLoop(x)+cLoop(y)) by A7,FUNCT_2:113
      .= f.x * f.y by A3,A12,TOPALG_1:47;
    end;
  end;

registration
  cluster Ciso -> one-to-one onto;
  coherence
  proof
    thus Ciso is one-to-one
    proof
      set xt = R^1(0);
      let m, n be set;
      assume that
A1:   m in dom Ciso & n in dom Ciso and
A2:   Ciso.m = Ciso.n;
      reconsider m, n as Integer by A1;
      Ciso.m = Class(EqRel(TUC,c[10]),cLoop(m)) & Ciso.n = Class(EqRel(TUC
      ,c[10]), cLoop(n)) by Def5;
      then
A3:   cLoop(m),cLoop(n) are_homotopic by A2,TOPALG_1:47;
      then consider F being Function of [:I[01],I[01]:], TUC such that
A4:   F is continuous and
A5:   for s being Point of I[01] holds F.(s,0) = (cLoop(m)).s & F.(s,1
) = (cLoop(n)).s & for t being Point of I[01] holds F.(0,t) = c[10] & F.(1,t) =
      c[10] by BORSUK_2:def 7;
A6:   xt in (CircleMap)"{c[10]} by Lm1,TOPREALB:34,def 2;
      then consider ftm being Function of I[01], R^1 such that
      ftm.0 = xt and
      cLoop(m) = CircleMap*ftm and
      ftm is continuous and
A7:   for f1 being Function of I[01], R^1 st f1 is continuous & cLoop
      (m) = CircleMap*f1 & f1.0 = xt holds ftm = f1 by Th23;
      F is Homotopy of cLoop(m),cLoop(n) by A3,A4,A5,BORSUK_6:def 13;
      then consider
      yt being Point of R^1, Pt, Qt being Path of xt,yt, Ft being
      Homotopy of Pt,Qt such that
A8:   Pt,Qt are_homotopic and
A9:   F = CircleMap*Ft and
      yt in (CircleMap)"{c[10]} and
      for F1 being Homotopy of Pt,Qt st F = CircleMap*F1 holds Ft = F1 by A3,A6
,Th24;
A10:  cLoop(n) = CircleMap*ExtendInt(n) by Th20;
      set ft0 = Prj1(j0,Ft);
A11:  now
        let x be Point of I[01];
        thus (CircleMap*ft0).x = CircleMap.(ft0.x) by FUNCT_2:21
          .= CircleMap.(Ft.(x,j0)) by Def2
          .= F.(x,j0) by A9,Lm5,BINOP_1:30
          .= (cLoop(m)).x by A5;
      end;
      ft0.0 = Ft.(j0,j0) by Def2
        .= xt by A8,BORSUK_6:def 13;
      then
A12:  ft0 = ftm by A7,A11,FUNCT_2:113;
      set ft1 = Prj1(j1,Ft);
A13:  now
        let x be Point of I[01];
        thus (CircleMap*ft1).x = CircleMap.(ft1.x) by FUNCT_2:21
          .= CircleMap.(Ft.(x,j1)) by Def2
          .= F.(x,j1) by A9,Lm5,BINOP_1:30
          .= (cLoop(n)).x by A5;
      end;
      consider ftn being Function of I[01], R^1 such that
      ftn.0 = xt and
      cLoop(n) = CircleMap*ftn and
      ftn is continuous and
A14:  for f1 being Function of I[01], R^1 st f1 is continuous & cLoop
      (n) = CircleMap*f1 & f1.0 = xt holds ftn = f1 by A6,Th23;
A15:  cLoop(m) = CircleMap*ExtendInt(m) by Th20;
      ft1.0 = Ft.(j0,j1) by Def2
        .= xt by A8,BORSUK_6:def 13;
      then
A16:  ft1 = ftn by A14,A13,FUNCT_2:113;
      (ExtendInt(n)).0 = n*j0 by Def1
        .= xt by TOPREALB:def 2;
      then ExtendInt(n) = ftn by A14,A10;
      then
A17:  ft1.j1 = n*1 by A16,Def1;
      (ExtendInt(m)).0 = m*j0 by Def1
        .= xt by TOPREALB:def 2;
      then ExtendInt(m) = ftm by A7,A15;
      then
A18:  ft0.j1 = m*1 by A12,Def1;
      ft0.j1 = Ft.(j1,j0) by Def2
        .= yt by A8,BORSUK_6:def 13
        .= Ft.(j1,j1) by A8,BORSUK_6:def 13
        .= ft1.j1 by Def2;
      hence thesis by A18,A17;
    end;
    thus rng Ciso c= the carrier of pi_1(TUC,c[10]);
    let q be set;
    assume q in the carrier of pi_1(TUC,c[10]);
    then consider f being Loop of c[10] such that
A19: q = Class(EqRel(TUC,c[10]),f) by TOPALG_1:48;
    R^1(0) in (CircleMap)"{c[10]} by Lm1,TOPREALB:34,def 2;
    then consider ft being Function of I[01], R^1 such that
A20: ft.0 = R^1(0) and
A21: f = CircleMap*ft and
A22: ft is continuous and
    for f1 being Function of I[01], R^1 st f1 is continuous & f =
    CircleMap*f1 & f1.0 = R^1(0) holds ft = f1 by Th23;
    CircleMap.(ft.j1) = (CircleMap*ft).j1 by FUNCT_2:21
      .= c[10] by A21,BORSUK_2:def 4;
    then CircleMap.(ft.1) in {c[10]} by TARSKI:def 1;
    then
A23: ft.1 in INT by Lm12,FUNCT_1:def 13,TOPREALB:34;
    ft.1 = R^1(ft.1) by TOPREALB:def 2;
    then ft is Path of R^1(0),R^1(ft.1) by A20,A22,BORSUK_2:def 4;
    then dom Ciso = INT & Ciso.(ft.1) = Class(EqRel(TUC,c[10]),CircleMap*ft)
    by A23,Th25,FUNCT_2:def 1;
    hence thesis by A19,A21,A23,FUNCT_1:def 5;
  end;
end;

theorem
  Ciso is bijective;

Lm16: for r being positive (real number), o being Point of TOP-REAL 2, x being
Point of Tcircle(o,r) holds INT.Group,pi_1(Tcircle(o,r),x) are_isomorphic
proof
  let r be positive (real number), o be Point of TOP-REAL 2, x be Point of
  Tcircle(o,r);
  TUC = Tcircle(0.TOP-REAL 2,1) by TOPREALB:def 7;
  then pi_1(TUC,c[10]),pi_1(Tcircle(o,r),x) are_isomorphic by TOPALG_3:35
,TOPREALB:20;
  then consider
  h being Homomorphism of pi_1(TUC,c[10]), pi_1(Tcircle(o,r),x) such
  that
A1: h is bijective by GROUP_6:def 15;
  take h*Ciso;
  thus thesis by A1,GROUP_6:74;
end;

theorem Th27:
  for S being being_simple_closed_curve SubSpace of TOP-REAL 2, x
  being Point of S holds INT.Group, pi_1(S,x) are_isomorphic
proof
  consider r being positive (real number), o being Point of TOP-REAL 2, y
  being Point of Tcircle(o,r);
  let S be being_simple_closed_curve SubSpace of TOP-REAL 2, x be Point of S;
  INT.Group,pi_1(Tcircle(o,r),y) are_isomorphic & pi_1(Tcircle(o,r),y),
  pi_1(S,x) are_isomorphic by Lm16,TOPALG_3:35,TOPREALB:11;
  hence thesis by GROUP_6:78;
end;

registration
  let S be being_simple_closed_curve SubSpace of TOP-REAL 2, x be Point of S;
  cluster pi_1(S,x) -> infinite;
  coherence
  proof
    INT.Group, pi_1(S,x) are_isomorphic by Th27;
    hence thesis by GROUP_6:85;
  end;
end;
