:: The Fundamental Group of the Circle
::  by Artur Korni{\l}owicz
:: 
:: Received February 22, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, EUCLID, RELAT_1, ARYTM, SUBSET_1, TOPALG_1, WELLORD1,
      GR_CY_1, FINSET_1, METRIC_1, INT_1, ALGSTR_1, BORSUK_1, FUNCT_1, SIN_COS,
      WAYBEL20, EQREL_1, BORSUK_2, ORDINAL2, ARYTM_1, MCART_1, COMPLEX1,
      SQUARE_1, GROUP_6, VECTSP_1, T_0TOPSP, TOPS_2, ARYTM_3, RCOMP_1, TEX_2,
      ASYMPT_0, FUNCOP_1, TOPMETR, JGRAPH_2, GRAPH_1, TOPALG_2, JORDAN1,
      BORSUK_6, SGRAPH1, PCOMPS_1, FUNCT_4, BOOLE, SEQM_3, SETFAM_1, TARSKI,
      COMPTS_1, TOPREAL2, TAXONOM2, FINSEQ_1, RELAT_2, RCOMP_3, TOPREALB,
      TOPS_1, CONNSP_1, CONNSP_2, CONNSP_3, RLVECT_3, CANTOR_1, CARD_4, PBOOLE,
      PROB_1, TOPALG_5;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, RELAT_1, EQREL_1,
      FUNCT_1, CARD_3, CARD_4, RELSET_1, PARTFUN1, SEQ_1, FUNCT_2, BINOP_1,
      PBOOLE, YELLOW18, FINSET_1, ORDINAL2, NUMBERS, XCMPLX_0, XREAL_0, REAL_1,
      SQUARE_1, NAT_1, INT_1, SEQM_3, FINSEQ_1, FUNCT_4, RCOMP_1, RCOMP_2,
      STRUCT_0, PRE_TOPC, TOPS_1, GROUP_1, GR_CY_1, GROUP_6, GRCAT_1, METRIC_1,
      CANTOR_1, YELLOW_8, BORSUK_1, TOPS_2, COMPTS_1, CONNSP_1, CONNSP_2,
      CONNSP_3, TEX_2, TSEP_1, TOPMETR, EUCLID, TOPRNS_1, PCOMPS_1, BORSUK_2,
      SIN_COS, BORSUK_3, JORDAN16, TAXONOM2, BORSUK_6, TOPALG_1, TOPREAL9,
      TOPALG_2, TOPREALB, RCOMP_3;
 constructors TOPRNS_1, GRCAT_1, SIN_COS, JORDAN16, TOPREAL9, BORSUK_3,
      CONNSP_1, COMPTS_1, TAXONOM2, REALSET2, TEX_2, TOPALG_3, COMPUT_1,
      PSCOMP_1, MONOID_0, TOPREALB, FINSEQ_4, TRIANG_1, BINARITH, JCT_MISC,
      RCOMP_2, TOPS_1, CARD_4, CONNSP_3, TSEP_1, TOPGEN_2, WAYBEL35, RCOMP_3;
 registrations STRUCT_0, RELSET_1, XREAL_0, BORSUK_1, PRE_TOPC, SUBSET_1,
      XBOOLE_0, FUNCT_1, TOPALG_1, BORSUK_2, SCMRING3, INT_1, SIN_COS,
      SQUARE_1, TOPMETR, TOPALG_2, PCOMPS_1, TOPS_1, YELLOW13, GOBOARD1,
      TOPREAL6, TOPALG_3, TOPREALB, NAT_1, MEMBERED, BORSUK_5, TSEP_1,
      YELLOW12, REALSET1, METRIC_1, RCOMP_3;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions FUNCT_1, FUNCT_2, GROUP_1, GROUP_6, TOPS_2, TSEP_1, BORSUK_2,
      BORSUK_6, COMPTS_1, PRE_TOPC, TARSKI, XBOOLE_0, CONNSP_1, T_0TOPSP,
      CONNSP_2, RCOMP_3;
 theorems GR_CY_1, GROUP_1, GROUP_6, PRE_TOPC, XREAL_0, FUNCT_2, BORSUK_1,
      INT_1, SIN_COS, EUCLID, JGRAPH_1, SQUARE_1, TOPREAL9, TOPALG_1, JORDAN2C,
      TOPS_2, GRCAT_1, BORSUK_3, FUNCT_1, TOPMETR, JGRAPH_2, BORSUK_4, RELAT_1,
      FUNCOP_1, JORDAN16, BORSUK_6, BORSUK_2, TOPALG_2, BINOP_1, TARSKI,
      PCOMPS_1, ZFMISC_1, JORDAN5A, FUNCT_4, TSEP_1, FINSEQ_1, XBOOLE_1,
      COMPLEX2, COMPTS_1, JORDAN1, SETFAM_1, XBOOLE_0, TAXONOM2, REAL_1,
      FINSEQ_3, NAT_1, RCOMP_1, SEQM_3, FINSEQ_5, HEINE, TOPREAL5, TEX_2,
      DOMAIN_1, TOPS_1, TOPMETR2, GOBOARD9, CONNSP_1, TOPALG_3, TOPREALA,
      TOPREALB, FINSET_1, JORDAN6, RCOMP_2, TOPS_3, CONNSP_3, CONNSP_2,
      TOPGRP_1, JORDAN18, MSAFREE3, XREAL_1, CANTOR_1, FRECHET, TOPGEN_2,
      CARD_3, PBOOLE, RCOMP_3;
 schemes FUNCT_2, NAT_1, PBOOLE;

begin :: Preliminaries

set o = |[0,0]|;
set I = the carrier of I[01];
set R = the carrier of R^1;
Lm1: 0 in INT by INT_1:def 1;
Lm2: 0 in I by JORDAN5A:2;
  then
Lm3: {0} c= I by ZFMISC_1:37;
Lm4: 0 in {0} by TARSKI:def 1;
Lm5: the carrier of [:I[01],I[01]:] = [:I,I:] by BORSUK_1:def 5;
reconsider j0 = 0, j1 = 1 as Point of I[01] by BORSUK_1:def 17,def 18;
Lm6: [#]I[01] = I by PRE_TOPC:12;
Lm7: I[01] | [#]I[01] = I[01] by TSEP_1:3;

Lm9: 1-0 <= 1;
Lm10: 3/2-1/2 <= 1;

registration
  cluster -> integer Element of INT.Group;
coherence by INT_1:def 2,GR_CY_1:def 4;
end;

registration
  cluster INT.Group -> infinite;
coherence
proof 
  thus the carrier of INT.Group is infinite by GR_CY_1:def 4;
end;
end;

registration
  let S be infinite 1-sorted;
  cluster the carrier of S -> infinite;
coherence by GROUP_1:def 14;
end;

reserve a, r, s for real number;

theorem Th89:
  r <= s & 0 < a implies
  for p being Point of Closed-Interval-MSpace(r,s) holds Ball(p,a) = [.r,s.] or
  Ball(p,a) = [.r,p+a.[ or Ball(p,a) = ].p-a,s.] or Ball(p,a) = ].p-a,p+a.[
  proof 
    set M = Closed-Interval-MSpace(r,s);
    assume that
A1: r <= s and
A2: 0 < a;
    let p be Point of M;
    set B = Ball(p,a);
A3: the carrier of M = [.r,s.] by A1,TOPMETR:14;
    reconsider p1 = p as Point of RealSpace by TOPMETR:12;
    set B1 = Ball(p1,a);
A4: B = B1 /\ the carrier of M by TOPMETR:13;
    a is Real & p1 is Real by XREAL_0:def 1;
    then
A5: B1 = ].p1-a, p1+a.[ by A2,FRECHET:7;
    per cases;
    suppose that
A6:   p1+a <= s and
A7:   p1-a < r;
      B = [.r,p1+a.[
      proof
        thus B c= [.r,p1+a.[
        proof
          let b be set;
          assume
A8:       b in B;
          then reconsider b as Element of B;
          b in B1 by A8,A4,XBOOLE_0:def 3;
          then
A9:       b < p1+a by A5,JORDAN6:45;
          r <= b by A8,A3,TOPREAL5:1;
          hence thesis by A9,RCOMP_2:3;
        end;
        let b be set;
        assume
A10:     b in [.r,p1+a.[;
        then reconsider b as Real;
A11:     r <= b by A10,RCOMP_2:3;
A12:     b < p1+a by A10,RCOMP_2:3;
        then b <= s by A6,XREAL_1:2;
        then
A13:     b in [.r,s.] by A11,TOPREAL5:1;
        p1-a < b by A7,A11,XREAL_1:2;
        then b in B1 by A5,A12,JORDAN6:45;
        hence thesis by A13,A3,A4,XBOOLE_0:def 3;
      end;
      hence thesis;
    end;
    suppose that
A14:   p1+a <= s and
A15:   p1-a >= r;
      B = ].p1-a,p1+a.[
      proof
        thus B c= ].p1-a,p1+a.[ by A4,A5,XBOOLE_1:17;
        let b be set;
        assume
A16:     b in ].p1-a,p1+a.[;
        then reconsider b as Real;
        p1-a <= b by A16,JORDAN6:45;
        then
A17:     r <= b by A15,XREAL_1:2;
        b < p1+a by A16,JORDAN6:45;
        then b <= s by A14,XREAL_1:2;
        then b in [.r,s.] by A17,TOPREAL5:1;
        hence thesis by A16,A5,A3,A4,XBOOLE_0:def 3;
      end;
      hence thesis;
    end;
    suppose that
A18:   p1+a > s and
A19:   p1-a < r;
      B = [.r,s.]
      proof
        thus B c= [.r,s.] by A3;
        let b be set;
        assume
A20:     b in [.r,s.];
        then reconsider b as Real;
A21:     r <= b by A20,TOPREAL5:1;
A22:     b <= s by A20,TOPREAL5:1;
A23:     b < p1+a by A18,A22,XREAL_1:2;
        p1-a < b by A19,A21,XREAL_1:2;
        then b in B1 by A5,A23,JORDAN6:45;
        hence thesis by A20,A3,A4,XBOOLE_0:def 3;
      end;
      hence thesis;
    end;
    suppose that
A24:   p1+a > s and
A25:   p1-a >= r;
      B = ].p1-a,s.]
      proof
        thus B c= ].p1-a,s.]
        proof
          let b be set;
          assume
A26:       b in B;
          then reconsider b as Element of B;
          b in B1 by A26,A4,XBOOLE_0:def 3;
          then
A27:       p1-a < b by A5,JORDAN6:45;
          b <= s by A26,A3,TOPREAL5:1;
          hence thesis by A27,RCOMP_2:4;
        end;
        let b be set;
        assume
A28:     b in ].p1-a,s.];
        then reconsider b as Real;
A29:     b <= s by A28,RCOMP_2:4;
A30:     p1-a < b by A28,RCOMP_2:4;
        then r <= b by A25,XREAL_1:2;
        then
A31:     b in [.r,s.] by A29,TOPREAL5:1;
        b < p1+a by A24,A29,XREAL_1:2;
        then b in B1 by A5,A30,JORDAN6:45;
        hence thesis by A31,A3,A4,XBOOLE_0:def 3;
      end;
      hence thesis;
    end;
  end;

theorem Th90:
  r <= s implies
  ex B being Basis of Closed-Interval-TSpace(r,s) st
   (ex f being ManySortedSet of Closed-Interval-TSpace(r,s) st
    for y being Point of Closed-Interval-MSpace(r,s) holds
     f.y = {Ball(y,1/n) where n is Nat: n <> 0} & B = Union f) &
  for X being Subset of Closed-Interval-TSpace(r,s) st X in B holds
  X is connected
  proof 
    set L = Closed-Interval-TSpace(r,s),
    M = Closed-Interval-MSpace(r,s);
A1: L = TopSpaceMetr(M) by TOPMETR:def 8;
    assume
A2: r <= s;
    defpred P[set,set] means
    ex x being Point of L, y being Point of M,
    B being Basis of x st $1 = x & x = y & $2 = B &
    B = {Ball(y,1/n) where n is Nat: n <> 0};
A3: for i being set st i in the carrier of L ex j being set st P[i,j]
    proof
      let i be set;
      assume i in the carrier of L;
      then reconsider i as Point of L;
      reconsider m = i as Point of M by A1,TOPMETR:16;
      consider B being Basis of i such that
A4:   B = {Ball(m,1/n) where n is Nat: n <> 0} and
      B is countable &
      ex f being Function of NAT,B st for n being set st n in NAT holds
      ex n' being Nat st n=n' & f.n = Ball(m,1/(n'+1)) by A1,FRECHET:11;
      take B, i, m, B;
      thus thesis by A4;
    end;
    consider f being ManySortedSet of the carrier of L such that
A5: for i being set st i in the carrier of L holds P[i,f.i]
    from PBOOLE:sch 3(A3);
    for x being Element of L holds f.x is Basis of x
    proof
      let x be Element of L;
      P[x,f.x] by A5;
      hence thesis;
    end;
    then reconsider B = Union f as Basis of L by TOPGEN_2:2;
    take B;
    hereby
      take f;
      let x be Point of M;
      the carrier of M = [.r,s.] by A2,TOPMETR:14
      .= the carrier of L by A2,TOPMETR:25;
      then P[x,f.x] by A5;
      hence f.x = {Ball(x,1/n) where n is Nat: n <> 0} & B = Union f;
    end;
    let X be Subset of L;
    assume X in B;
    then X in union rng f by CARD_3:def 4;
    then consider Z being set such that
A6: X in Z and
A7: Z in rng f by TARSKI:def 4;
    consider x being set such that
A8: x in dom f and
A9: f.x = Z by A7,FUNCT_1:def 5;
    dom f = the carrier of L by PBOOLE:def 3;
    then consider x1 being Point of L, y being Point of M,
    B1 being Basis of x1 such that
A10: x = x1 & x1 = y & f.x = B1 &
    B1 = {Ball(y,1/n) where n is Nat: n <> 0} by A5,A8;
    consider n being Nat such that
A11: X = Ball(y,1/n) and
A12: n <> 0 by A6,A9,A10;
    reconsider X1 = X as Subset of R^1 by PRE_TOPC:39;
    0 < n by A12,NAT_1:19;
    then
A13: 1/n > 0 by XREAL_1:141;
    Ball(y,1/n) = [.r,s.] or Ball(y,1/n) = [.r,y+1/n.[ or
    Ball(y,1/n) = ].y-1/n,s.] or Ball(y,1/n) = ].y-1/n,y+1/n.[ by A2,A13,Th89;
    then X1 is connected by A11,RCOMP_3:52;
    hence thesis by CONNSP_1:24;
  end;

theorem Th81:
  for T being TopStruct, A being Subset of T, t be Point of T st t in A
  holds skl(t,A) c= A
  proof 
    let T be TopStruct, A be Subset of T, t be Point of T;
    assume
A1: t in A;
    then Down(t,A) = t by CONNSP_3:def 3;
    then
A2: skl(t,A) = skl Down(t,A) by A1,CONNSP_3:def 7;
    the carrier of (T|A) = A by JORDAN1:1;
    hence thesis by A2;
  end;

registration
  let T be TopSpace, A be open Subset of T;
  cluster T|A -> open;
  coherence
  proof 
    let X being Subset of T;
    thus thesis by JORDAN1:1;
  end;
end;

theorem Th82:
  for T being TopSpace, S being SubSpace of T,
  A being Subset of T, B being Subset of S st A = B holds
  T|A = S|B
  proof 
    let T be TopSpace, S be SubSpace of T,
    A be Subset of T, B be Subset of S such that
A1: A = B;
A2: S|B is SubSpace of T by TSEP_1:7;
    [#](S|B) = A by A1,PRE_TOPC:def 10;
    hence T|A = S|B by A2,PRE_TOPC:def 10;
  end;

theorem Th83:
  for S, T being TopSpace, A, B being Subset of T, C, D being Subset of S
  st the TopStruct of S = the TopStruct of T & A = C & B = D &
  A,B are_separated holds C,D are_separated
  proof 
    let S, T be TopSpace;
    let A, B be Subset of T;
    let C, D be Subset of S;
    assume
A1: the TopStruct of S = the TopStruct of T;
    assume
A2: A = C & B = D;
    then
A3: Cl A = Cl C & Cl B = Cl D by A1,TOPS_3:80;
    assume A,B are_separated;
    then Cl A misses B & A misses Cl B by CONNSP_1:def 1;
    hence C,D are_separated by A2,A3,CONNSP_1:def 1;
  end;

theorem
  for S, T being TopSpace st the TopStruct of S = the TopStruct of T &
  S is connected holds T is connected
  proof 
    let S, T be TopSpace such that
A1: the TopStruct of S = the TopStruct of T and
A2: S is connected;
    let A, B be Subset of T such that
A3: [#]T = A \/ B and
A4: A,B are_separated;
    reconsider A1 = A, B1 = B as Subset of S by A1;
A5: [#]S = the carrier of S & [#]T = the carrier of T by PRE_TOPC:12;
    A1,B1 are_separated by A1,A4,Th83;
    then A1 = {}S or B1 = {}S by A3,A5,A2,A1,CONNSP_1:def 2;
    hence A = {}T or B = {}T;
  end;

theorem Th85:
  for S, T being TopSpace, A being Subset of S, B being Subset of T
  st the TopStruct of S = the TopStruct of T & A = B &
  A is connected holds B is connected
  proof 
    let S, T be TopSpace, A be Subset of S, B be Subset of T such that
A1: the TopStruct of S = the TopStruct of T and
A2: A = B and
A3: A is connected;
    now
      let P, Q be Subset of T such that
A4:   B = P \/ Q and
A5:   P,Q are_separated;
      reconsider P1 = P, Q1 = Q as Subset of S by A1;
      P1,Q1 are_separated by A1,A5,Th83;
      then P1 = {}S or Q1 = {}S by A2,A3,A4,CONNSP_1:16;
      hence P = {}T or Q = {}T;
    end;
    hence thesis by CONNSP_1:16;
  end;

theorem Th86:
  for S, T being non empty TopSpace, s being Point of S, t being Point of T,
  A being a_neighborhood of s
  st the TopStruct of S = the TopStruct of T & s = t holds
  A is a_neighborhood of t
  proof 
    let S, T be non empty TopSpace, s be Point of S, t be Point of T,
    A be a_neighborhood of s such that
A1: the TopStruct of S = the TopStruct of T and
A2: s = t;
    reconsider B = A as Subset of T by A1;
A3: Int A = Int B by A1,TOPS_3:77;
    s in Int A by CONNSP_2:def 1;
    hence thesis by A2,A3,CONNSP_2:def 1;
  end;

theorem
  for S, T being non empty TopSpace, A being Subset of S, B being Subset of T,
  N being a_neighborhood of A
  st the TopStruct of S = the TopStruct of T & A = B holds
  N is a_neighborhood of B
  proof 
    let S, T be non empty TopSpace, A be Subset of S, B be Subset of T,
    N be a_neighborhood of A such that
A1: the TopStruct of S = the TopStruct of T and
A2: A = B;
    reconsider M = N as Subset of T by A1;
A3: Int M = Int N by A1,TOPS_3:77;
    A c= Int N by CONNSP_2:def 2;
    hence thesis by A2,A3,CONNSP_2:def 2;
  end;

theorem Th88:
  for S, T being non empty TopSpace, A, B being Subset of T,
  f being map of S,T st f is_homeomorphism & A is_a_component_of B
  holds f"A is_a_component_of f"B
  proof 
    let S, T be non empty TopSpace, A, B being Subset of T,
    f be map of S,T such that
A1: f is_homeomorphism;
    given X being Subset of T|B such that
A2: X = A and
A3: X is_a_component_of T|B;
    set Y = f"A;
A4: the carrier of (T|B) = B by JORDAN1:1;
    then f"X c= f"B by RELAT_1:178;
    then reconsider Y as Subset of S|(f"B) by A2,JORDAN1:1;
    take Y;
    thus Y = f"A;
    X is connected by A3,CONNSP_1:def 5;
    then A is connected by A2,CONNSP_1:24;
    then f"A is connected by A1,JORDAN18:2;
    hence Y is connected by CONNSP_1:24;
    let Z being Subset of S|(f"B) such that
A5: Z is connected and
A6: Y c= Z;
    reconsider Z1 = Z as Subset of S by PRE_TOPC:39;
A7: Z1 is connected by A5,CONNSP_1:24;
    the carrier of S|(f"B) = f"B by JORDAN1:1;
    then
A8: f.:Z c= f.:(f"B) by RELAT_1:156;
A9: rng f = [#]T by A1,TOPS_2:def 5
    .= the carrier of T by PRE_TOPC:12;
    then reconsider R = f.:Z as Subset of T|B by A4,A8,FUNCT_1:147;
    f is continuous by A1,TOPS_2:def 5;
    then f.:Z1 is connected by A7,TOPREAL5:5;
    then
A10: R is connected by CONNSP_1:24;
A11: f is one-to-one by A1,TOPS_2:def 5;
A12: X = f.:Y by A9,A2,FUNCT_1:147;
    f.:Y c= f.:Z by A6,RELAT_1:156;
    then
A13: X = R by A12,A3,A10,CONNSP_1:def 5;
    dom f = the carrier of S by FUNCT_2:def 1;
    then Z1 c= dom f;
    hence Y = Z by A2,A13,A11,MSAFREE3:1;
  end;

begin :: Locally connectedness

theorem Th91:
  for T being non empty TopSpace, S being non empty SubSpace of T,
  A being non empty Subset of T, B being non empty Subset of S st A = B &
  A is locally_connected holds B is locally_connected
  proof 
    let T be non empty TopSpace, S be non empty SubSpace of T,
    A be non empty Subset of T, B be non empty Subset of S such that
A1: A = B and
A2: A is locally_connected;
    T|A = S|B by A1,Th82;
    hence S|B is locally_connected by A2,CONNSP_2:def 6;
  end;

theorem Th92:
  for S, T being non empty TopSpace st
  the TopStruct of S = the TopStruct of T &
  S is locally_connected holds T is locally_connected
  proof 
    let S, T be non empty TopSpace such that
A1: the TopStruct of S = the TopStruct of T and
A2: S is locally_connected;
    let t be Point of T;
    let U be Subset of T;
    reconsider U1 = U as Subset of S by A1;
    reconsider s = t as Point of S by A1;
A3: S is_locally_connected_in s by A2,CONNSP_2:def 4;
    assume U is a_neighborhood of t;
    then U1 is a_neighborhood of s by A1,Th86;
    then consider V1 being Subset of S such that
A4: V1 is a_neighborhood of s and
A5: V1 is connected and
A6: V1 c= U1 by A3,CONNSP_2:def 3;
    reconsider V = V1 as Subset of T by A1;
    take V;
    thus V is a_neighborhood of t by A1,A4,Th86;
    thus V is connected by A1,A5,Th85;
    thus V c= U by A6;
  end;

theorem Th93:
  for T being non empty TopSpace holds
  T is locally_connected iff [#]T is locally_connected
  proof 
    let T be non empty TopSpace;
A1: the carrier of T =  [#]T by PRE_TOPC:12
    .= the carrier of (T| [#]T) by JORDAN1:1;
    T is SubSpace of T by TSEP_1:2;
    then
A2: the TopStruct of T = the TopStruct of (T| [#]T) by A1,TSEP_1:5;
    hereby
      assume T is locally_connected;
      then T| [#]T is locally_connected by A2,Th92;
      hence [#]T is locally_connected by CONNSP_2:def 6;
    end;
    assume [#]T is locally_connected;
    then T| [#]T is locally_connected by CONNSP_2:def 6;
    hence T is locally_connected by A2,Th92;
  end;

Lm15:
  for T being non empty TopSpace, S being non empty open SubSpace of T st
  T is locally_connected holds the TopStruct of S is locally_connected
  proof 
    let T be non empty TopSpace;
    let S be non empty open SubSpace of T;
    assume
A1: T is locally_connected;
A2: [#]S = the carrier of S by PRE_TOPC:12;
    then reconsider A = [#]S as non empty Subset of T by TSEP_1:1;
    A is open by A2,TSEP_1:def 1;
    then A is locally_connected by A1,CONNSP_2:23;
    then [#]S is locally_connected by Th91;
    then S is locally_connected by Th93;
    then the TopStruct of S is locally_connected by Th92;
    then [#]the TopStruct of S is locally_connected by Th93;
    then (the TopStruct of S)| [#]the TopStruct of S is locally_connected
    by CONNSP_2:def 6;
    hence thesis by TSEP_1:3;
  end;

theorem Th94:
  for T being non empty TopSpace, S being non empty open SubSpace of T st
  T is locally_connected holds S is locally_connected
  proof 
    let T be non empty TopSpace;
    let S be non empty open SubSpace of T;
    assume T is locally_connected;
    then the TopStruct of S is locally_connected by Lm15;
    hence S is locally_connected by Th92;
  end;

theorem
  for S, T being non empty TopSpace st S,T are_homeomorphic &
  S is locally_connected holds T is locally_connected
  proof 
    let S, T be non empty TopSpace;
    given f being map of S,T such that
A1: f is_homeomorphism;
    assume
A2: S is locally_connected;
    now
      let A be non empty Subset of T, B being Subset of T such that
A3:   A is open and
A4:   B is_a_component_of A;
A5:   the carrier of T = [#]T by PRE_TOPC:12;
      rng f = [#]T by A1,TOPS_2:def 5;
      then
A6:   f"A is non empty by A5,RELAT_1:174;
A7:   f"A is open by A1,A3,TOPGRP_1:26;
      f"B is_a_component_of f"A by A1,A4,Th88;
      then f"B is open by A2,A6,A7,CONNSP_2:24;
      hence B is open by A1,TOPGRP_1:26;
    end;
    hence thesis by CONNSP_2:24;
  end;

theorem Th96:
  for T being non empty TopSpace st
  ex B being Basis of T st for X being Subset of T st X in B holds
  X is connected holds
  T is locally_connected
  proof 
    let T be non empty TopSpace;
    given B being Basis of T such that
A1: for X being Subset of T st X in B holds X is connected;
    let x be Point of T;
    let U be Subset of T such that
A2: x in Int U;
A3: Int U in the topology of T by PRE_TOPC:def 5;
    the topology of T c= UniCl B by CANTOR_1:def 2;
    then consider Y being Subset-Family of T such that
A4: Y c= B and
A5: Int U = union Y by A3,CANTOR_1:def 1;
    consider V being set such that
A6: x in V and
A7: V in Y by A2,A5,TARSKI:def 4;
    reconsider V as Subset of T by A7;
    take V;
A8: B c= the topology of T by CANTOR_1:def 2;
    V in B by A4,A7;
    then V is open by A8,PRE_TOPC:def 5;
    hence x in Int V by A6,TOPS_1:55;
    thus V is connected by A1,A4,A7;
A9: V c= union Y by A7,ZFMISC_1:92;
    Int U c= U by TOPS_1:44;
    hence V c= U by A5,A9,XBOOLE_1:1;
  end;

theorem Th97:
  r <= s implies Closed-Interval-TSpace(r,s) is locally_connected
  proof 
    assume r <= s;
    then ex B being Basis of Closed-Interval-TSpace(r,s) st
    (ex f being ManySortedSet of Closed-Interval-TSpace(r,s) st
    for y being Point of Closed-Interval-MSpace(r,s) holds
    f.y = {Ball(y,1/n) where n is Nat: n <> 0} & B = Union f) &
    for X being Subset of Closed-Interval-TSpace(r,s) st X in B holds
    X is connected by Th90;
    hence thesis by Th96;
  end;

registration
  cluster I[01] -> locally_connected;
  coherence by Th97,TOPMETR:27;
end;

registration
  let A be non empty open Subset of I[01];
  cluster I[01] | A -> locally_connected;
coherence by Th94;
end;

begin :: Some useful functions

definition
  let r be real number;
  func ExtendInt(r) -> map of I[01], R^1 means :Def4:
  for x being Point of I[01] holds it.x = r*x;
existence
proof 
  defpred P[real number,set] means $2 = r*$1;
A1: for x being Element of I[01] ex y being Element of R st P[x,y]
  proof
    let x be Element of I[01];
    take r*x;
    thus thesis by TOPMETR:24,XREAL_0:def 1;
  end;
  ex f being Function of I, R st
   for x being Element of I[01] holds P[x,f.x] from FUNCT_2:sch 3(A1);
  hence thesis;
end;
uniqueness
proof 
  let f, g be map of I[01], R^1 such that
A2: for x being Point of I[01] holds f.x = r*x and
A3: for x being Point of I[01] holds g.x = r*x;
  for x being Point of I[01] holds f.x = g.x
  proof
    let x be Point of I[01];
    thus f.x = r*x by A2
      .= g.x by A3;
  end;
  hence f = g by FUNCT_2:113;
end;
end;

registration
  let r be real number;
  cluster ExtendInt(r) -> continuous;
coherence
proof 
  reconsider f1 = id I[01] as map of I[01],R^1
    by FUNCT_2:9,TOPMETR:24,BORSUK_1:83;
  f1 is continuous by TOPMETR:7;
  then consider g being map of I[01],R^1 such that
A1: for p being Point of I[01], r1 being real number st f1.p=r1 holds g.p=r*r1
  and
A2: g is continuous by JGRAPH_2:33;
  for x being Point of I[01] holds g.x = (ExtendInt(r)).x
  proof
    let x be Point of I[01];
    thus g.x = r*f1.x by A1
      .= r*x by GRCAT_1:11
      .= (ExtendInt(r)).x by Def4;
  end;
  hence thesis by A2,FUNCT_2:113;
end;
end;

definition
  let r be real number;
  redefine func ExtendInt(r) -> Path of R^1(0),R^1(r);
coherence
proof 
  thus ExtendInt(r) is continuous;
  thus (ExtendInt(r)).0 = r * 0 by Def4,BORSUK_1:def 17
    .= R^1(0) by TOPREALB:def 2;
  thus (ExtendInt(r)).1 = r * 1 by Def4,BORSUK_1:def 18
    .= R^1(r) by TOPREALB:def 2;
end;
end;

definition
  let S, T, Y be non empty TopSpace,
      H be map of [:S,T:],Y,
      t be Point of T;
  func Prj1(t,H) -> map of S,Y means :Def5:
  for s being Point of S holds it.s = H.(s,t);
existence
proof 
  deffunc F(Point of S) = H. [$1,t];
  consider f being Function of the carrier of S, the carrier of Y such that
A1:  for x being Element of the carrier of S holds f.x = F(x)
       from FUNCT_2:sch 4;
  take f;
  let x be Point of S;
  thus f.x = H. [x,t] by A1
    .= H.(x,t) by BINOP_1:def 1;
end;
uniqueness
proof 
  let f, g be map of S,Y such that
A2: for s being Point of S holds f.s = H.(s,t) and
A3: for s being Point of S holds g.s = H.(s,t);
  now
    let s be Point of S;
    thus f.s = H.(s,t) by A2
      .= g.s by A3;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

definition
  let S, T, Y be non empty TopSpace,
      H be map of [:S,T:],Y,
      s be Point of S;
  func Prj2(s,H) -> map of T,Y means :Def6:
  for t being Point of T holds it.t = H.(s,t);
existence
proof 
  deffunc F(Point of T) = H. [s,$1];
  consider f being Function of the carrier of T, the carrier of Y such that
A1:  for x being Element of the carrier of T holds f.x = F(x)
       from FUNCT_2:sch 4;
  take f;
  let x be Point of T;
  thus f.x = H. [s,x] by A1
    .= H.(s,x) by BINOP_1:def 1;
end;
uniqueness
proof 
  let f, g be map of T,Y such that
A2: for t being Point of T holds f.t = H.(s,t) and
A3: for t being Point of T holds g.t = H.(s,t);
  now
    let t be Point of T;
    thus f.t = H.(s,t) by A2
      .= g.t by A3;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

registration
  let S, T, Y be non empty TopSpace,
      H be continuous map of [:S,T:],Y,
      t be Point of T;
  cluster Prj1(t,H) -> continuous;
coherence
proof 
  for p being Point of S, V being Subset of Y
  st Prj1(t,H).p in V & V is open holds
  ex W being Subset of S st p in W & W is open & Prj1(t,H).:W c= V
  proof
    let p be Point of S, V be Subset of Y such that
A1:   Prj1(t,H).p in V and
A2:   V is open;
A3: Prj1(t,H).p = H.(p,t) by Def5;
    H. [p,t] = H.(p,t) by BINOP_1:def 1;
    then consider W being Subset of [:S,T:] such that
A4:   [p,t] in W and
A5:   W is open and
A6:   H.:W c= V by A1,A2,A3,JGRAPH_2:20;
    consider A being Subset-Family of [:S,T:] such that
A7:  W = union A and
A8:  for e being set st e in A ex X1 being Subset of S, Y1 being Subset of T st
       e = [:X1,Y1:] & X1 is open & Y1 is open by A5,BORSUK_1:45;
    consider e being set such that
A9:   [p,t] in e and
A10:   e in A by A4,A7,TARSKI:def 4;
    consider X1 being Subset of S, Y1 being Subset of T such that
A11:   e = [:X1,Y1:] and
A12:   X1 is open and
      Y1 is open by A8,A10;
    take X1;
    thus p in X1 by A9,A11,ZFMISC_1:106;
    thus X1 is open by A12;
    let x be set;
    assume x in Prj1(t,H).:X1;
    then consider c being Point of S such that
A13:   c in X1 and
A14:   x = Prj1(t,H).c by FUNCT_2:116;
A15: Prj1(t,H).c = H.(c,t) by Def5
      .= H. [c,t] by BINOP_1:def 1;
    t in Y1 by A9,A11,ZFMISC_1:106;
    then [c,t] in [:X1,Y1:] by A13,ZFMISC_1:106;
    then [c,t] in W by A7,A10,A11,TARSKI:def 4;
    then H. [c,t] in H.:W by FUNCT_2:43;
    hence x in V by A6,A14,A15;
  end;
  hence thesis by JGRAPH_2:20;
end;
end;

registration
  let S, T, Y be non empty TopSpace,
      H be continuous map of [:S,T:],Y,
      s be Point of S;
  cluster Prj2(s,H) -> continuous;
coherence
proof 
  for p being Point of T, V being Subset of Y
  st Prj2(s,H).p in V & V is open holds
  ex W being Subset of T st p in W & W is open & Prj2(s,H).:W c= V
  proof
    let p be Point of T, V be Subset of Y such that
A1:   Prj2(s,H).p in V and
A2:   V is open;
A3: Prj2(s,H).p = H.(s,p) by Def6;
    H. [s,p] = H.(s,p) by BINOP_1:def 1;
    then consider W being Subset of [:S,T:] such that
A4:   [s,p] in W and
A5:   W is open and
A6:   H.:W c= V by A1,A2,A3,JGRAPH_2:20;
    consider A being Subset-Family of [:S,T:] such that
A7:  W = union A and
A8:  for e being set st e in A ex X1 being Subset of S, Y1 being Subset of T st
       e = [:X1,Y1:] & X1 is open & Y1 is open by A5,BORSUK_1:45;
    consider e being set such that
A9:   [s,p] in e and
A10:   e in A by A4,A7,TARSKI:def 4;
    consider X1 being Subset of S, Y1 being Subset of T such that
A11:   e = [:X1,Y1:] and
      X1 is open and
A12:   Y1 is open by A8,A10;
    take Y1;
    thus p in Y1 by A9,A11,ZFMISC_1:106;
    thus Y1 is open by A12;
    let x be set;
    assume x in Prj2(s,H).:Y1;
    then consider c being Point of T such that
A13:   c in Y1 and
A14:   x = Prj2(s,H).c by FUNCT_2:116;
A15: Prj2(s,H).c = H.(s,c) by Def6
      .= H. [s,c] by BINOP_1:def 1;
    s in X1 by A9,A11,ZFMISC_1:106;
    then [s,c] in [:X1,Y1:] by A13,ZFMISC_1:106;
    then [s,c] in W by A7,A10,A11,TARSKI:def 4;
    then H. [s,c] in H.:W by FUNCT_2:43;
    hence x in V by A6,A14,A15;
  end;
  hence thesis by JGRAPH_2:20;
end;
end;

theorem
  for T being non empty TopSpace, a, b being Point of T,
      P, Q being Path of a,b,
      H being Homotopy of P,Q,
      t being Point of I[01] st H is continuous
  holds Prj1(t,H) is continuous
proof 
  let T be non empty TopSpace;
  let a, b be Point of T;
  let P, Q be Path of a,b;
  let H be Homotopy of P,Q;
  let t be Point of I[01];
  for H being continuous map of [:I[01],I[01]:], T holds
    Prj1(t,H) is continuous;
  hence thesis;
end;

theorem
  for T being non empty TopSpace, a, b being Point of T,
      P, Q being Path of a,b,
      H being Homotopy of P,Q,
      s being Point of I[01] st H is continuous
  holds Prj2(s,H) is continuous
proof 
  let T be non empty TopSpace;
  let a, b be Point of T;
  let P, Q be Path of a,b;
  let H be Homotopy of P,Q;
  let s be Point of I[01];
  for H being continuous map of [:I[01],I[01]:], T holds
    Prj2(s,H) is continuous;
  hence thesis;
end;

set TUC = Tunit_circle(2);
set cS1 = the carrier of TUC;
Lm16: now
  TUC = Tcircle(0.REAL 2,1) by TOPREALB:def 7;
  hence cS1 = Sphere(|[0,0]|,1) by TOPREALB:9,EUCLID:58;
end;

Lm17: dom CircleMap = REAL by FUNCT_2:def 1,TOPMETR:24;

definition
  let r be real number;
  func cLoop(r) -> map of I[01], Tunit_circle(2) means :Def7:
  for x being Point of I[01] holds it.x = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]|;
existence
proof 
  defpred P[real number,set] means $2 = |[ cos(2*PI*r*$1), sin(2*PI*r*$1) ]|;
A1: for x being Element of I[01]
   ex y being Element of cS1 st P[x,y]
  proof
    let x be Element of I[01];
    set y = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]|;
    |. y - o .| = |. y .| by JORDAN2C:13,EUCLID:58
      .= sqrt((y`1)^2+(y`2)^2) by JGRAPH_1:47
      .= sqrt((cos(2*PI*r*x))^2+(y`2)^2) by EUCLID:56
      .= sqrt((cos(2*PI*r*x))^2+(sin(2*PI*r*x))^2) by EUCLID:56
      .= 1 by SIN_COS:32,SQUARE_1:83;
    then reconsider y as Element of cS1 by Lm16,TOPREAL9:9;
    take y;
    thus P[x,y];
  end;
  ex f being Function of I, cS1 st
    for x being Element of I[01] holds P[x,f.x] from FUNCT_2:sch 3(A1);
  hence thesis;
end;
uniqueness
proof 
  let f, g be map of I[01], TUC such that
A2: for x being Point of I[01] holds f.x = |[cos(2*PI*r*x),sin(2*PI*r*x)]| and
A3: for x being Point of I[01] holds g.x = |[cos(2*PI*r*x),sin(2*PI*r*x)]|;
  for x being Point of I[01] holds f.x = g.x
  proof
    let x be Point of I[01];
    thus f.x = |[cos(2*PI*r*x),sin(2*PI*r*x)]| by A2
      .= g.x by A3;
  end;
  hence f = g by FUNCT_2:113;
end;
end;

theorem Th100:
  cLoop(r) = CircleMap * ExtendInt(r)
proof 
  for x being Point of I[01] holds (cLoop(r)).x = (CircleMap * ExtendInt(r)).x
  proof
    let x be Point of I[01];
A1: (ExtendInt(r)).x = r*x by Def4;
    thus (cLoop(r)).x = |[ cos(2*PI*r*x), sin(2*PI*r*x) ]| by Def7
      .= |[ cos(2*PI*(ExtendInt(r)).x), sin(2*PI*(ExtendInt(r)).x) ]| by A1
      .= CircleMap.((ExtendInt(r)).x) by TOPREALB:def 11
      .= (CircleMap * ExtendInt(r)).x by FUNCT_2:21;
  end;
  hence thesis by FUNCT_2:113;
end;

definition
  let n be Integer;
  redefine func cLoop(n) -> Loop of c[10];
coherence
proof 
  set f = cLoop(n);
  f = CircleMap * ExtendInt(n) by Th100;
  hence f is continuous;
  thus f.0 = |[ cos(2*PI*n*j0), sin(2*PI*n*j0) ]| by Def7
    .= c[10] by TOPREALB:def 8,SIN_COS:34;
  thus f.1 = |[ cos(2*PI*n*j1), sin(2*PI*n*j1) ]| by Def7
    .= |[ cos(0), sin(2*PI*n+0) ]| by COMPLEX2:10
    .= c[10] by TOPREALB:def 8,COMPLEX2:9,SIN_COS:34;
end;
end;

begin :: Main theorems

Lm18:
  ex F being Subset-Family of Tunit_circle(2) st
   F is_a_cover_of Tunit_circle(2) & F is open &
   for U being Subset of Tunit_circle(2) st U in F
   ex D being mutually-disjoint open Subset-Family of R^1
    st union D = (CircleMap)"U &
     for d being Subset of R^1 st d in D
     for f being map of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
      holds f is_homeomorphism
proof 
  consider F being Subset-Family of Tunit_circle(2) such that
A1: F = { (CircleMap).:].0,1.[, (CircleMap).:].1/2,3/2.[ } and
A2: F is_a_cover_of Tunit_circle(2) & F is open and
A3: for U being Subset of Tunit_circle(2) holds
   ( U = (CircleMap).:].0,1.[ implies
     union IntIntervals(0,1) = (CircleMap)"U &
     for d being Subset of R^1 st d in IntIntervals(0,1)
      for f being map of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
       holds f is_homeomorphism ) &
   ( U = (CircleMap).:].1/2,3/2.[ implies
     union IntIntervals(1/2,3/2) = (CircleMap)"U &
     for d being Subset of R^1 st d in IntIntervals(1/2,3/2)
      for f being map of R^1 | d, (Tunit_circle(2)) | U st f = CircleMap|d
       holds f is_homeomorphism ) by TOPREALB:49;
  take F;
  thus F is_a_cover_of Tunit_circle(2) & F is open by A2;
  let U be Subset of Tunit_circle(2);
  assume
A4: U in F;
  per cases by A1,A4,TARSKI:def 2;
  suppose
A5: U = (CircleMap).:].0,1.[;
  reconsider D = IntIntervals(0,1) as
   mutually-disjoint open Subset-Family of R^1 by Lm9,TOPREALB:4;
  take D;
  thus thesis by A3,A5;
  end;
  suppose
A6: U = (CircleMap).:].1/2,3/2.[;
  reconsider D = IntIntervals(1/2,3/2) as
   mutually-disjoint open Subset-Family of R^1 by Lm10,TOPREALB:4;
  take D;
  thus thesis by A3,A6;
  end;
end;

Lm19: the carrier of Sspace(0[01]) = {0} by BORSUK_1:def 17,TEX_2:def 4;
   then
Lm20: [#]Sspace(0[01]) = {0} by PRE_TOPC:12;

Lm14:
  for F being Subset-Family of Closed-Interval-TSpace(r,s),
  C being IntervalCover of F,
  G being IntervalCoverPts of C holds
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies G is non empty
proof 
  let F be Subset-Family of Closed-Interval-TSpace(r,s),
  C be IntervalCover of F,
  G be IntervalCoverPts of C;
  assume F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected &
  r <= s;
  then len G = len C + 1 by RCOMP_3:def 3;
  hence thesis by FINSEQ_1:25;
end;

theorem Th101:
  for UL being Subset-Family of Tunit_circle(2) st
    UL is_a_cover_of Tunit_circle(2) & UL is open
  for Y being non empty TopSpace,
      F being continuous map of [:Y,I[01]:], Tunit_circle(2),
      y being Point of Y
   ex T being non empty FinSequence of REAL st
      T.1 = 0 & T.len T = 1 & T is increasing &
      ex N being open Subset of Y st y in N &
      for i being natural number st i in dom T & i+1 in dom T
       ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
        F.:[:N,[.T.i,T.(i+1).]:] c= Ui
proof 
  let UL be Subset-Family of TUC such that
A1:  UL is_a_cover_of TUC and
A2:  UL is open;
  let Y be non empty TopSpace,
      F be continuous map of [:Y,I[01]:], TUC,
      y be Point of Y;
  set L = Closed-Interval-TSpace(0,1);
  set C = the carrier of Y;
A3:the carrier of [:Y,I[01]:] = [:C,I:] by BORSUK_1:def 5;
A4: [#]TUC = cS1 by PRE_TOPC:12;
A5: [#]TUC = union UL by A1,PRE_TOPC:def 8;
A6: for i being Point of I[01]
     ex U being non empty open Subset of TUC st F. [y,i] in U & U in UL
  proof
    let i be Point of I[01];
    consider U being set such that
A7:   F. [y,i] in U & U in UL by A5,A4,TARSKI:def 4;
    reconsider U as non empty open Subset of TUC by A2,A7,TOPS_2:def 1;
    take U;
    thus thesis by A7;
  end;
  ex U being non empty open Subset of TUC st F. [y,0[01]] in U & U in UL by A6;
  then reconsider UL1 = UL as non empty set;
  defpred I0[set,set] means
    ex V being open Subset of TUC st V in UL1 & F. [y,$1] in V & $2 = V;
A8: for i being Element of I ex z being Element of UL1 st I0[i,z]
  proof
    let i be Element of I;
    ex U being non empty open Subset of TUC st F. [y,i] in U & U in UL by A6;
    hence ex z being Element of UL1 st I0[i,z];
  end;
  consider I0 being Function of I, UL1 such that
A9:  for i being Element of I holds I0[i,I0.i] from FUNCT_2:sch 3(A8);
  defpred I1[set,set] means
    ex M being open Subset of Y, O being open connected Subset of I[01] st
       y in M & $1 in O & F.:[:M,O:] c= I0.$1 & $2 = [:M,O:];
A10: for i being Element of I ex z being Element of bool [:C,I:] st I1[i,z]
  proof
    let i be Element of I;
    consider V being open Subset of TUC such that
      V in UL1 and
A11:   F. [y,i] in V and
A12:   I0.i = V by A9;
    consider W being Subset of [:Y,I[01]:] such that
A13:   [y,i] in W and
A14:   W is open and
A15:   F.:W c= V by A11,JGRAPH_2:20;
    consider Q being Subset-Family of [:Y,I[01]:] such that
A16:   W = union Q and
A17:   for e being set st e in Q
        ex A being Subset of Y, B being Subset of I[01] st
         e = [:A,B:] & A is open & B is open by A14,BORSUK_1:45;
    consider Z being set such that
A18:   [y,i] in Z and
A19:   Z in Q by A13,A16,TARSKI:def 4;
    consider A being Subset of Y, B being Subset of I[01] such that
A20:   Z = [:A,B:] and
A21:   A is open and
A22:   B is open by A17,A19;
    reconsider A as open Subset of Y by A21;
A23: i in B by A18,A20,ZFMISC_1:106;
    reconsider B as non empty open Subset of I[01] by A22,A18,A20,ZFMISC_1:106;
    reconsider i1 = i as Point of I[01]|B by A23,JORDAN1:1;
A24: skl(i,B) = skl i1 by A23,CONNSP_3:def 7;
    skl i1 is_a_component_of I[01]|B by CONNSP_1:43;
    then skl i1 is open by CONNSP_2:21;
    then reconsider D = skl(i,B) as open connected Subset of I[01]
    by A23,A24,TSEP_1:17,CONNSP_3:35;
    reconsider z = [:A,D:] as Element of bool [:C,I:] by BORSUK_1:def 5;
    take z, A, D;
    thus y in A by A18,A20,ZFMISC_1:106;
    thus i in D by A23,CONNSP_3:26;
    D c= B by A23,Th81;
    then
A25: z c= [:A,B:] by ZFMISC_1:118;
    [:A,B:] c= W by A19,A16,A20,ZFMISC_1:92;
    then z c= W by A25,XBOOLE_1:1;
    then
    F.:z c= F.:W by RELAT_1:156;
    hence F.:[:A,D:] c= I0.i by A12,A15,XBOOLE_1:1;
    thus z = [:A,D:];
  end;
  consider I1 being Function of I, bool [:C,I:] such that
A26:  for i being Element of I holds I1[i,I1.i] from FUNCT_2:sch 3(A10);
A27: dom I1 = I by FUNCT_2:def 1;
  reconsider C1 = rng I1 as Subset-Family of [:Y,I[01]:] by A3,SETFAM_1:def 7;
  [:{y},[#]I[01]:] c= union C1
  proof
    let a be set;
    assume a in [:{y},[#]I[01]:];
    then consider a1, a2 being set such that
A28:   a1 in {y} and
A29:   a2 in [#]I[01] and
A30:   a = [a1,a2] by ZFMISC_1:def 2;
    reconsider a2 as Point of I[01] by A29;
    consider M being open Subset of Y,
             O being open connected Subset of I[01] such that
A31:  y in M & a2 in O and F.:[:M,O:] c= I0.a2 and
A32:  I1.a2 = [:M,O:] by A26;
A33: a1 = y by A28,TARSKI:def 1;
A34: [y,a2] in [:M,O:] by A31,ZFMISC_1:106;
    [:M,O:] in C1 by A27,A32,FUNCT_1:def 5;
    hence thesis by A34,A30,A33,TARSKI:def 4;
  end;
  then
A35: C1 is_a_cover_of [:{y},[#]I[01]:] by COMPTS_1:def 1;
A36: C1 is open
  proof
    let P be Subset of [:Y,I[01]:];
    assume P in C1;
    then consider i being set such that
A37:   i in dom I1 and
A38:   I1.i = P by FUNCT_1:def 5;
    I1[i,I1.i] by A26,A37;
    hence P is open by A38,BORSUK_1:46;
  end;
  [:{y},[#]I[01]:] is compact by BORSUK_3:27;
  then consider G being Subset-Family of [:Y,I[01]:] such that
A39: G c= C1 and
A40: G is_a_cover_of [:{y},[#]I[01]:] and
A41: G is finite by A35,A36,COMPTS_1:def 7;
A42: [:{y},[#]I[01]:] c= union G by A40,COMPTS_1:def 1;
A43: y in {y} by TARSKI:def 1;
  set ICOV = {O where O is open connected Subset of I[01]:
    ex M being open Subset of Y st [:M,O:] in G};
  ICOV c= bool I
  proof
    let a be set;
    assume a in ICOV;
    then ex O being open connected Subset of I[01] st a = O &
      ex M being open Subset of Y st [:M,O:] in G;
    hence thesis;
  end;
  then reconsider ICOV as Subset-Family of L by TOPMETR:27,SETFAM_1:def 7;
A44: [#]L c= union ICOV
  proof
    let a be set;
    assume a in [#]L;
    then reconsider a as Point of I[01] by TOPMETR:27;
    [y,a] in [:{y},[#]I[01]:] by A43,Lm6,ZFMISC_1:def 2;
    then consider Z being set such that
A45:   [y,a] in Z and
A46:   Z in G by A42,TARSKI:def 4;
    consider i being set such that
A47:   i in dom I1 and
A48:   I1.i = Z by A46,A39,FUNCT_1:def 5;
    consider M being open Subset of Y,
             O being open connected Subset of I[01] such that
      y in M and i in O and F.:[:M,O:] c= I0.i and
A49:   I1.i = [:M,O:] by A47,A26;
A50: a in O by A45,A48,A49,ZFMISC_1:106;
    O in ICOV by A49,A46,A48;
    hence thesis by A50,TARSKI:def 4;
  end;
A51: ICOV is_a_cover_of L
  proof
    thus [#]L c= union ICOV by A44;
    thus thesis by Lm6,TOPMETR:27;
  end;
A52: ICOV is open
  proof
    let a be Subset of L;
    assume a in ICOV;
    then ex O being open connected Subset of I[01] st a = O &
      ex M being open Subset of Y st [:M,O:] in G;
    hence thesis by TOPMETR:27;
  end;
  consider NCOV being IntervalCover of ICOV;
  consider T being IntervalCoverPts of NCOV;
A53: ICOV is connected
  proof
    let X be Subset of L;
    assume X in ICOV;
    then ex O being open connected Subset of I[01] st
    X = O & ex M being open Subset of Y st [:M,O:] in G;
    hence thesis by TOPMETR:27;
  end;
  then reconsider T as non empty FinSequence of REAL by A52,A51,Lm14;
  take T;
  thus T.1 = 0 & T.len T = 1 by A52,A51,RCOMP_3:def 3,A53;
  thus T is increasing by A52,A51,RCOMP_3:79,A53;
  set NN = {M where M is open Subset of Y: y in M &
    ex O being open Subset of I[01] st [:M,O:] in G};
  NN c= bool C
  proof
    let a be set;
    assume a in NN;
    then ex M being open Subset of Y st a = M & y in M &
      ex O being open Subset of I[01] st [:M,O:] in G;
    hence thesis;
  end;
  then reconsider NN as Subset-Family of Y by SETFAM_1:def 7;
  [y,0[01]] in [:{y},[#]I[01]:] by A43,Lm6,ZFMISC_1:def 2;
  then consider Z being set such that
    [y,0[01]] in Z and
A54: Z in G by A42,TARSKI:def 4;
  consider i being set such that
A55: i in dom I1 and
A56: I1.i = Z by A54,A39,FUNCT_1:def 5;
  consider M being open Subset of Y,
           O being open connected Subset of I[01] such that
A57: y in M and i in O & F.:[:M,O:] c= I0.i and
A58: I1.i = [:M,O:] by A26,A55;
A59: M in NN by A57,A58,A54,A56;
A60: NN is open
  proof
    let a be Subset of Y;
    assume a in NN;
    then ex M being open Subset of Y st a = M & y in M &
      ex O being open Subset of I[01] st [:M,O:] in G;
    hence thesis;
  end;
  consider p being Function such that
A61:  rng p = G and
A62:  dom p in omega by A41,FINSET_1:def 1;
  defpred F[set,set] means
    ex M being open Subset of Y, O being non empty open Subset of I[01] st
       y in M & p.$1 = [:M,O:] & $2 = M;
A63: for x being set st x in dom p ex y being set st y in NN & F[x,y]
  proof
    let x be set;
    assume x in dom p;
    then
A64: p.x in rng p by FUNCT_1:def 5;
    then consider i being set such that
A65:   i in dom I1 and
A66:   I1.i = p.x by A39,A61,FUNCT_1:def 5;
    consider M being open Subset of Y, O being open connected Subset of I[01]
      such that
A67:    y in M & i in O & F.:[:M,O:] c= I0.i & I1.i = [:M,O:] by A26,A65;
    take M;
    thus thesis by A66,A67,A61,A64;
  end;
  consider p1 being Function of dom p, NN such that
A68:  for x being set st x in dom p holds F[x,p1.x] from FUNCT_2:sch 1(A63);
A69: dom p1 = dom p by A59,FUNCT_2:def 1;
A70: rng p1 = NN
  proof
    thus rng p1 c= NN;
    let a be set;
    assume a in NN;
    then consider M being open Subset of Y such that
A71:   a = M and
      y in M and
A72:   ex O being open Subset of I[01] st [:M,O:] in G;
    consider O being open Subset of I[01] such that
A73:   [:M,O:] in G by A72;
    consider b being set such that
A74:   b in dom p and
A75:   p.b = [:M,O:] by A61,A73,FUNCT_1:def 5;
    consider M1 being open Subset of Y,
             O1 being non empty open Subset of I[01] such that
A76:   y in M1 and
A77:   p.b = [:M1,O1:] and
A78:   p1.b = M1 by A74,A68;
    M1 = M by A75,A77,A76,ZFMISC_1:134;
    hence a in rng p1 by A71,A69,A74,A78,FUNCT_1:def 5;
  end;
  NN is finite by A62,A70,A69,FINSET_1:def 1;
  then reconsider N = meet NN as open Subset of Y by A60,TOPS_2:27;
  take N;
  now
    let Z be set;
    assume Z in NN;
    then ex M being open Subset of Y st Z = M & y in M &
      ex O being open Subset of I[01] st [:M,O:] in G;
    hence y in Z;
  end;
  hence y in N by A59,SETFAM_1:def 1;
  let i be natural number;
  assume that
A79: i in dom T and
A80: i+1 in dom T;
A81: 1 <= i by A79,FINSEQ_3:27;
A82: i+1 <= len T by A80,FINSEQ_3:27;
  then
A83: i < len T by NAT_1:38;
A84: rng NCOV c= ICOV by A52,A51,RCOMP_3:def 2,A53;
  len T = len NCOV + 1 by A52,A51,RCOMP_3:def 3,A53;
  then i <= len NCOV by A82,XREAL_1:8;
  then i in dom NCOV by A81,FINSEQ_3:27;
  then NCOV.i in rng NCOV by FUNCT_1:def 5;
  then NCOV.i in ICOV by A84;
  then consider O being open connected Subset of I[01] such that
A85:  NCOV.i = O and
A86:  ex M being open Subset of Y st [:M,O:] in G;
  consider M being open Subset of Y such that
A87:  [:M,O:] in G by A86;
  consider j being set such that
A88:  j in dom I1 and
A89:  I1.j = [:M,O:] by A87,A39,FUNCT_1:def 5;
  consider M1 being open Subset of Y, O1 being open connected Subset of I[01]
   such that
A90: y in M1 and
A91: j in O1 and
A92: F.:[:M1,O1:] c= I0.j and
A93: I1.j = [:M1,O1:] by A26,A88;
A94: M = M1 & O = O1 by A93,A89,A90,A91,ZFMISC_1:134;
  consider V being open Subset of TUC such that
A95: V in UL1 and
A96: F. [y,j] in V and
A97: I0.j = V by A9,A88;
  reconsider V as non empty open Subset of TUC by A96;
  take V;
  thus V in UL by A95;
A98: [.T.i,T.(i+1).] c= O by A83,A85,A52,A51,A81,RCOMP_3:80,A53;
  M in NN by A90,A94,A87;
  then N c= M by SETFAM_1:4;
  then [:N,[.T.i,T.(i+1).]:] c= [:M1,O1:] by A98,A93,A89,ZFMISC_1:119;
  then F.:[:N,[.T.i,T.(i+1).]:] c= F.:[:M1,O1:] by RELAT_1:156;
  hence F.:[:N,[.T.i,T.(i+1).]:] c= V by A97,A92,XBOOLE_1:1;
end;

theorem Th102:
  for Y being non empty TopSpace,
      F being map of [:Y,I[01]:], Tunit_circle(2),
      Ft being map of [:Y,Sspace(0[01]):], R^1 st
    F is continuous & Ft is continuous &
    F | [:the carrier of Y,{0}:] = CircleMap*Ft
  ex G being map of [:Y,I[01]:], R^1 st G is continuous & F = CircleMap*G &
    G | [:the carrier of Y,{0}:] = Ft &
    for H being map of [:Y,I[01]:], R^1 st
        H is continuous & F = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft
        holds G = H
proof 
  let Y be non empty TopSpace,
      F be map of [:Y,I[01]:], TUC,
      Ft be map of [:Y,Sspace(0[01]):], R^1 such that
A1:  F is continuous and
A2:  Ft is continuous and
A3:  F | [:the carrier of Y,{0}:] = CircleMap*Ft;
A4: the carrier of [:Y,I[01]:] = [:the carrier of Y,I:] by BORSUK_1:def 5;
A5: the carrier of [:Y,Sspace(0[01]):] =
    [:the carrier of Y, the carrier of Sspace(0[01]):] by BORSUK_1:def 5;
    then
A6: dom Ft = [:the carrier of Y,{0}:] by Lm19,FUNCT_2:def 1;
  consider UL being Subset-Family of TUC such that
A7: UL is_a_cover_of TUC and
A8: UL is open and
A9: for U being Subset of TUC st U in UL
    ex D being mutually-disjoint open Subset-Family of R^1
     st union D = (CircleMap)"U &
     for d being Subset of R^1 st d in D
     for f being map of R^1 | d, TUC | U st f = CircleMap|d
      holds f is_homeomorphism by Lm18;
A10: dom F = the carrier of [:Y,I[01]:] by FUNCT_2:def 1
     .= [:the carrier of Y,I:] by BORSUK_1:def 5;
  defpred A[set,set] means
   ex y being Point of Y, t being Point of I[01],
    N being non empty open Subset of Y, Fn being map of [:Y|N,I[01]:], R^1 st
    $1 = [y,t] & $2 = Fn.$1 & y in N & Fn is continuous &
    F | [:N,I:] = CircleMap*Fn & Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] &
    for H being map of [:Y|N,I[01]:], R^1 st
        H is continuous & F | [:N,I:] = CircleMap*H &
        H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H;
A11: for x being Point of [:Y,I[01]:]
     ex z being Point of R^1 st A[x,z]
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y, t being Point of I[01] such that
A12:  x = [y,t] by BORSUK_1:50;
    consider TT being non empty FinSequence of REAL such that
A13:   TT.1 = 0 and
A14:   TT.len TT = 1 and
A15:   TT is increasing and
A16:   ex N being open Subset of Y st y in N &
      for i being natural number st i in dom TT & i+1 in dom TT
       ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
        F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A1,A7,A8,Th101;
    consider N being open Subset of Y such that
A17:   y in N and
A18:   for i being natural number st i in dom TT & i+1 in dom TT
       ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
        F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A16;
    reconsider N as non empty open Subset of Y by A17;
A19: 1 in dom TT by FINSEQ_5:6;
A20: len TT in dom TT by FINSEQ_5:6;
A21: now
      let i be Nat such that
A22:     i in dom TT;
      1 <= i by A22,FINSEQ_3:27;
      then 1 = i or 1 < i by REAL_1:def 5;
      hence
A23:     0 <= TT.i by A13,A15,A22,A19,SEQM_3:def 1;
      assume
A24:     i+1 in dom TT;
A25:   i+0 < i+1 by XREAL_1:10;
      hence
A26:     TT.i < TT.(i+1) by A15,A22,A24,SEQM_3:def 1;
      i+1 <= len TT by A24,FINSEQ_3:27;
      then i+1 = len TT or i+1 < len TT by REAL_1:def 5;
      hence TT.(i+1) <= 1 by A14,A15,A24,A20,SEQM_3:def 1;
      hence TT.i < 1 by A26,XREAL_1:2;
      thus 0 < TT.(i+1) by A23,A25,A15,A22,A24,SEQM_3:def 1;
    end;
A27: now
    let i be Nat such that
A28:   0 <= TT.i & TT.(i+1) <= 1;
    thus [.TT.i,TT.(i+1).] c= I
      proof
        let a be set;
        assume
A29:       a in [.TT.i,TT.(i+1).];
        then reconsider a as Real;
        TT.i <= a & a <= TT.(i+1) by A29,TOPREAL5:1;
        then 0 <= a & a <= 1 by A28,XREAL_1:2;
        hence thesis by JORDAN5A:2;
      end;
    end;

    defpred N[Nat] means
     $1 in dom TT implies
     ex N2 being non empty open Subset of Y,
        S being non empty Subset of I[01],
        Fn being map of [:Y|N2,I[01]|S:], R^1 st
      S = [.0,TT.$1.] & y in N2 & N2 c= N &
      Fn is continuous &
      F | [:N2,S:] = CircleMap*Fn &
      Fn | [:the carrier of Y,{0}:] = Ft | [:N2,I:];
A30: N[0] by FINSEQ_3:26;
A31: for i being Nat st N[i] holds N[i+1]
    proof
      let i be Nat;
      assume that
A32:     N[i] and
A33:     i+1 in dom TT;
      per cases by A33,TOPREALA:23;
      suppose
A34:    i = 0;
      set S = [.0,TT.(i+1).];
A35:  S = {0} by A13,A34,RCOMP_1:14;
      reconsider S as non empty Subset of I[01] by Lm3,A13,A34,RCOMP_1:14;
      take N2 = N;
A36:   the carrier of (Y|N2) = N2 by JORDAN1:1;
A37:   the carrier of (I[01]|S) = S by JORDAN1:1;
A38:   the carrier of [:Y|N2,I[01]|S:]
       = [:the carrier of (Y|N2), the carrier of (I[01]|S):] by BORSUK_1:def 5;
      set Fn = Ft | [:N2,{0}:];
A39:  [:N2,S:] c= dom Ft by A6,A35,ZFMISC_1:119;
      then
A40:  dom Fn = [:N2,S:] by A35,RELAT_1:91;
      rng Fn c= R;
      then reconsider Fn as map of [:Y|N2,I[01]|S:], R^1
        by A40,A37,A38,A36,FUNCT_2:4;
      take S, Fn;
      thus S = [.0,TT.(i+1).];
      thus y in N2 by A17;
      thus N2 c= N;
      reconsider K0 = [:N2,S:] as non empty Subset of [:Y,Sspace(0[01]):]
        by Lm19,A5,A35,ZFMISC_1:119;
      S c= the carrier of Sspace(0[01]) by Lm19,A13,A34,RCOMP_1:14;
      then reconsider S1 = S as non empty Subset of Sspace(0[01]);
      I[01]|S = Sspace(0[01]) by A35,TOPALG_3:6,BORSUK_1:def 17
         .= (Sspace(0[01]))|S1 by A35,Lm20,TSEP_1:3;
      then [:Y|N2,I[01]|S:] = [:Y,Sspace(0[01]):]|K0 by BORSUK_3:26;
      hence Fn is continuous by A2,A35,TOPMETR:10;
      [:N2,S:] c= dom F by A10,ZFMISC_1:119;
      then
A41:  dom (F | [:N2,S:]) = [:N2,S:] by RELAT_1:91;
      rng Fn c= dom CircleMap by Lm17,TOPMETR:24;
      then
A42:  dom (CircleMap*Fn) = dom Fn by RELAT_1:46;
      for x being set st x in dom (F | [:N2,S:]) holds
        (F | [:N2,S:]).x = (CircleMap*Fn).x
      proof
        let x be set such that
A43:       x in dom (F | [:N2,S:]);
        thus (F | [:N2,S:]).x = F.x by A43,A41,FUNCT_1:72
          .= (CircleMap*Ft).x by A3,A43,A40,A41,Lm19,A5,FUNCT_1:72
          .= CircleMap.(Ft.x) by A43,A41,A39,FUNCT_1:23
          .= CircleMap.(Fn.x) by A43,A35,A41,FUNCT_1:72
          .= (CircleMap*Fn).x by A40,A41,A43,FUNCT_1:23;
      end;
      hence F | [:N2,S:] = CircleMap*Fn by A40,A41,A42,FUNCT_1:9;
A44:  dom (Fn | [:the carrier of Y,{0}:])
        = [:N2,S:] /\ [:the carrier of Y,{0}:] by A40,RELAT_1:90;
A45:  [:N2,S:] /\ [:the carrier of Y,{0}:] = [:N2,S:] by A35,ZFMISC_1:124;
A46:  dom (Ft | [:N2,I:]) = [:the carrier of Y,{0}:] /\ [:N2,I:]
          by A6,RELAT_1:90
         .= [:N2,S:] by A35,ZFMISC_1:124;
      for x being set st x in dom (Fn | [:the carrier of Y,{0}:]) holds
        (Fn | [:the carrier of Y,{0}:]).x = (Ft | [:N2,I:]).x
      proof
        let x be set such that
A47:       x in dom (Fn | [:the carrier of Y,{0}:]);
A48:     x in [:N2,{0}:] by A47,A44,A35,XBOOLE_0:def 3;
A49:     [:N2,{0}:] c= [:N2,I:] by Lm3,ZFMISC_1:118;
        x in [:the carrier of Y,{0}:] by A47,A44,XBOOLE_0:def 3;
        hence (Fn | [:the carrier of Y,{0}:]).x = Fn.x by FUNCT_1:72
           .= Ft.x by A48,FUNCT_1:72
           .= (Ft | [:N2,I:]).x by A48,A49,FUNCT_1:72;
      end;
      hence Fn | [:the carrier of Y,{0}:] = Ft | [:N2,I:]
        by A44,A46,A45,FUNCT_1:9;
      end;
      suppose
A50:     i in dom TT;
      then consider N2 being open Subset of Y,
                    S being non empty Subset of I[01],
                    Fn being map of [:Y|N2,I[01]|S:], R^1 such that
A51:      S = [.0,TT.i.] and
A52:      y in N2 and
A53:      N2 c= N and
A54:      Fn is continuous and
A55:      F | [:N2,S:] = CircleMap*Fn and
A56:      Fn | [:the carrier of Y,{0}:] = Ft | [:N2,I:] by A32;
      reconsider N2 as non empty open Subset of Y by A52;
A57:   the carrier of I[01]|S = S by JORDAN1:1;
A58:   the carrier of [:Y|N2,I[01]|S:]
       = [:the carrier of (Y|N2), the carrier of (I[01]|S):] by BORSUK_1:def 5;
      set SS = [.TT.i,TT.(i+1).];
      consider Ui being non empty Subset of TUC such that
A59:     Ui in UL and
A60:     F.:[:N,SS:] c= Ui by A18,A33,A50;
A61:   the carrier of (TUC | Ui) = Ui by JORDAN1:1;
      consider D being mutually-disjoint open Subset-Family of R^1 such that
A62:     union D = (CircleMap)"Ui and
A63:     for d being Subset of R^1 st d in D
        for f being map of R^1 | d, TUC | Ui st f = CircleMap|d
          holds f is_homeomorphism by A59,A9;
A64:   the carrier of (Y|N2) = N2 by JORDAN1:1;
A65:   0 <= TT.i by A50,A21;
      then
A66:   TT.i in S by A51,RCOMP_1:15;
      then
A67:   [y,TT.i] in [:N2,S:] by A52,ZFMISC_1:106;
      then
A68:   F. [y,TT.i] = (CircleMap*Fn). [y,TT.i] by A55,FUNCT_1:72
        .= CircleMap.(Fn. [y,TT.i]) by A64,A57,A67,A58,FUNCT_2:21;
A69:   [y,TT.i] in dom F by A66,A10,ZFMISC_1:106;
A70:   TT.i < TT.(i+1) by A33,A50,A21;
      then TT.i in SS by RCOMP_1:15;
      then [y,TT.i] in [:N,SS:] by A17,ZFMISC_1:106;
      then
A71:   F. [y,TT.i] in F.:[:N,SS:] by A69,FUNCT_2:43;
      Fn. [y,TT.i] in (CircleMap)"Ui by A71,A60,A68,FUNCT_2:46,TOPMETR:24;
      then consider Uit being set such that
A72:     Fn. [y,TT.i] in Uit and
A73:     Uit in D by A62,TARSKI:def 4;
      reconsider Uit as non empty Subset of R^1 by A72,A73;
A74:   dom Fn = the carrier of [:Y|N2,I[01]|S:] by FUNCT_2:def 1;
A75:  TT.i in {TT.i} by TARSKI:def 1;
      then
A76:  [y,TT.i] in [:N2,{TT.i}:] by A52,ZFMISC_1:def 2;
A77:  {TT.i} c= S by A66,ZFMISC_1:37;
      reconsider Ti = {TT.i} as non empty Subset of I[01] by A66,ZFMISC_1:37;
A78:  the carrier of I[01]|Ti = Ti by JORDAN1:1;
A79:  the carrier of [:Y|N2,I[01]|Ti:]
       = [:the carrier of Y|N2, the carrier of I[01]|Ti:] by BORSUK_1:def 5;
      set FnT = Fn | [:N2,Ti:];
      [:N2,{TT.i}:] c= dom Fn by A74,A64,A57,A58,A77,ZFMISC_1:119;
      then
A80:  dom FnT = [:N2,{TT.i}:] by RELAT_1:91;
A81:  rng FnT c= REAL by TOPMETR:24;
      reconsider FnT as map of [:Y|N2,I[01]|Ti:],R^1
        by A64,A81,A78,A79,A80,FUNCT_2:4;
      N2 c= N2;
      then reconsider N7 = N2 as non empty Subset of Y|N2 by JORDAN1:1;
      reconsider Ti2 = Ti as non empty Subset of I[01]|S by A57,A66
,ZFMISC_1:37;
A82:  Y|N2|N7 = Y|N2 & I[01]|S|Ti2 = I[01]|Ti by GOBOARD9:4;
      [:Y|N2|N7,I[01]|S|Ti2:] = [:Y|N2,I[01]|S:] | [:N7,Ti2:] by BORSUK_3:26;
      then
A83:  FnT is continuous by A82,A54,TOPMETR:10;
      Uit is open by A73,TOPS_2:def 1;
      then FnT"Uit is open by A83,TOPS_2:55;
      then consider SF being Subset-Family of [:Y|N2,I[01]|Ti:] such that
A84:    FnT"Uit = union SF and
A85:    for e being set st e in SF
          ex X1 being Subset of Y|N2, Y1 being Subset of I[01]|Ti st
          e = [:X1,Y1:] & X1 is open & Y1 is open by BORSUK_1:45;
      FnT. [y,TT.i] in Uit by A72,A76,FUNCT_1:72;
      then [y,TT.i] in FnT"Uit by A76,A80,FUNCT_1:def 13;
      then consider N5 being set such that
A86:    [y,TT.i] in N5 and
A87:    N5 in SF by A84,TARSKI:def 4;
      consider X1 being Subset of Y|N2, Y1 being Subset of I[01]|Ti such that
A88:     N5 = [:X1,Y1:] and
A89:     X1 is open and Y1 is open by A85,A87;
      consider NY being Subset of Y such that
A90:    NY is open and
A91:    NY /\ [#](Y|N2) = X1 by A89,TOPS_2:32;
      consider y1, y2 being set such that
A92:    y1 in X1 and
A93:    y2 in Y1 and
A94:    [y,TT.i] = [y1,y2] by A86,A88,ZFMISC_1:def 2;
      set N1 = NY /\ N2;
A95:  [#](Y|N2) = N2 by PRE_TOPC:def 10;
A96:  Y1 = Ti
      proof
        thus Y1 c= Ti by A78;
        let a be set;
        assume a in Ti;
        then a = TT.i by TARSKI:def 1;
        hence thesis by A93,A94,ZFMISC_1:33;
      end;
      y = y1 by A94,ZFMISC_1:33;
      then
A97:   y in NY by A91,A92,XBOOLE_0:def 3;
      then reconsider N1 as non empty open Subset of Y
        by A90,TOPS_1:38,A52,XBOOLE_0:def 3;
A98:   N1 c= N2 by XBOOLE_1:17;
A99:   Fn.:[:N1,{TT.i}:] c= Uit
      proof
        let b be set;
        assume b in Fn.:[:N1,{TT.i}:];
        then consider a being Point of [:Y|N2,I[01]|S:] such that
A100:       a in [:N1,{TT.i}:] and
A101:       Fn.a = b by FUNCT_2:116;
A102:       ex a1, a2 being set st a1 in N1 & a2 in {TT.i} &
        a = [a1,a2] by A100,ZFMISC_1:def 2;
        a in N5 by A91,A95,A102,A88,A96,ZFMISC_1:def 2;
        then
A105:    a in union SF by A87,TARSKI:def 4;
        then a in dom FnT by A84,FUNCT_1:def 13;
        then Fn.a = FnT.a by FUNCT_1:70;
        hence b in Uit by A101,A105,A84,FUNCT_1:def 13;
      end;
A106:   the carrier of (Y|N1) = N1 by JORDAN1:1;
A107:   [:N2,SS:] c= [:N,SS:] by A53,ZFMISC_1:119;
      [:N1,SS:] c= [:N2,SS:] by A98,ZFMISC_1:119;
      then [:N1,SS:] c= [:N,SS:] by A107,XBOOLE_1:1;
      then
A108:   F.:[:N1,SS:] c= F.:[:N,SS:] by RELAT_1:156;
      set f = CircleMap|Uit;
A109:   the carrier of (R^1 | Uit) = Uit by JORDAN1:1;
A110:   dom f = Uit by Lm17,TOPMETR:24,RELAT_1:91;
      rng f c= Ui
      proof
        let b be set;
        assume b in rng f;
        then consider a being set such that
A111:       a in dom f and
A112:       f.a = b by FUNCT_1:def 5;
        a in union D by A111,A110,A73,TARSKI:def 4;
        then CircleMap.a in Ui by A62,FUNCT_2:46;
        hence b in Ui by A111,A112,A110,FUNCT_1:72;
      end;
      then reconsider f as map of R^1 | Uit, TUC | Ui
        by A110,A109,A61,FUNCT_2:4;
A113:   f is_homeomorphism by A63,A73;
      TT.(i+1) <= 1 by A33,A50,A21;
      then reconsider SS as non empty Subset of I[01] by A70,A65,A27
,RCOMP_1:15;
      take N1;
      take S1 = S \/ SS;
      set Fni1 = f"*(F| [:N1,SS:]);
      reconsider ff = f as Function;
A114:   [:N1,SS:] c= dom F by A10,ZFMISC_1:119;
      f" is_homeomorphism by A113,TOPS_2:70;
      then
A115:   dom (f") = [#](TUC | Ui) by TOPS_2:def 5;
A116:   [#](TUC | Ui) = the carrier of (TUC | Ui)
        by PRE_TOPC:12;
A117:   rng f = [#](TUC | Ui) by A113,TOPS_2:def 5;
A118:   f is one-to-one by A113,TOPS_2:def 5;
      then
A119:   f" = ff" by A117,TOPS_2:def 4;
A120:   the carrier of (I[01]|SS) = SS by JORDAN1:1;
A121:   the carrier of (I[01]|S1) = S1 by JORDAN1:1;
A122:   dom (F| [:N1,SS:]) = [:N1,SS:] by A114,RELAT_1:91;
A123:  rng (F| [:N1,SS:]) c= dom (f")
      proof
        let b be set;
        assume b in rng (F| [:N1,SS:]);
        then consider a being set such that
A124:       a in dom (F| [:N1,SS:]) and
A125:       (F| [:N1,SS:]).a = b by FUNCT_1:def 5;
        b = F.a by A124,A125,A122,FUNCT_1:72;
        then b in F.:[:N1,SS:] by A124,A122,FUNCT_2:43;
        then b in F.:[:N,SS:] by A108;
        then b in Ui by A60;
        hence thesis by A115,A116,JORDAN1:1;
      end;
      then
A126:   dom Fni1 = dom (F| [:N1,SS:]) by RELAT_1:46;
A127:   [:N1,SS:] = the carrier of [:Y|N1,I[01]|SS:] by A106,A120
,BORSUK_1:def 5;
A128:   [:N1,S1:] = the carrier of [:Y|N1,I[01]|S1:] by A106,A121
,BORSUK_1:def 5;
A129:  [:N1,S:] = the carrier of [:Y|N1,I[01]|S:] by A106,A57,BORSUK_1:def 5;
      the carrier of (R^1 | Uit) is Subset of R^1 by TSEP_1:1;
      then rng Fni1 c= R by XBOOLE_1:1;
      then reconsider Fni1 as map of [:Y|N1,I[01]|SS:], R^1
        by A122,A126,A127,FUNCT_2:4;
      set Fn2 = Fn | [:N1,S:];
      set Fn1 = Fn2 +* Fni1;
A130:   [:N1,S:] \/ [:N1,SS:] = [:N1,S1:] by ZFMISC_1:120;
A131:  [:N1,S:] c= [:N2,S:] by A98,ZFMISC_1:119;
      then dom (Fn | [:N1,S:]) = [:N1,S:] by A64,A57,A58,A74,RELAT_1:91;
      then
A132:   dom Fn1 = [:N1,S:] \/ [:N1,SS:] by A122,A126,FUNCT_4:def 1;
      rng Fn1 c= rng (Fn | [:N1,S:]) \/ rng Fni1 by FUNCT_4:18;
      then rng Fn1 c= R by XBOOLE_1:1;
      then reconsider Fn1 as map of [:Y|N1,I[01]|S1:], R^1
        by A132,A130,A128,FUNCT_2:4;
      take Fn1;
      thus
A133:     S1 = [.0,TT.(i+1).] by A51,A65,A70,HEINE:2;
      thus y in N1 by A97,A52,XBOOLE_0:def 3;
      thus N1 c= N by A98,A53,XBOOLE_1:1;
A134:  S c= S1 by XBOOLE_1:7;
A135:  SS c= S1 by XBOOLE_1:7;
A136:  dom Fn2 = the carrier of [:Y|N1,I[01]|S:]
      by A131,A129,A64,A57,A58,A74,RELAT_1:91;
      rng Fn2 c= R;
      then reconsider Fn2 as map of [:Y|N1,I[01]|S:],R^1 by A136,FUNCT_2:4;
A137:  [#](I[01]|S) = the carrier of (I[01]|S) by PRE_TOPC:12;
A138:  [#](I[01]|SS) = the carrier of (I[01]|SS) by PRE_TOPC:12;
A139:  [#](I[01]|S1) = the carrier of (I[01]|S1) by PRE_TOPC:12;
      reconsider F1 = [#](I[01]|S), F2 = [#](I[01]|SS) as
        Subset of I[01]|S1 by A120,A57,A121,A134,A135,PRE_TOPC:12;
A140:  I[01]|S is SubSpace of I[01]|S1 by A57,A121,A134,TSEP_1:4;
A141:  I[01]|SS is SubSpace of I[01]|S1 by A120,A121,A135,TSEP_1:4;
      reconsider hS = F1, hSS = F2 as Subset of I[01] by A120,A57,PRE_TOPC:12;
      hS is closed by A51,A57,A137,BORSUK_4:48;
      then
A142:  F1 is closed by TSEP_1:8;
      hSS is closed by A120,A138,BORSUK_4:48;
      then
A143:  F2 is closed by TSEP_1:8;
      reconsider K0 = [:N1,S:] as Subset of [:Y|N2,I[01]|S:]
        by A57,A58,A131,JORDAN1:1;
      reconsider aN1 = N1 as non empty Subset of Y|N2 by A98,JORDAN1:1;
      S c= S;
      then reconsider aS = S as non empty Subset of I[01]|S by JORDAN1:1;
      [:Y|N2,I[01]|S:] | K0 = [:Y|N2|aN1,I[01]|S|aS:] by BORSUK_3:26
        .= [:Y|N1,I[01]|S|aS:] by GOBOARD9:4
        .= [:Y|N1,I[01]|S:] by GOBOARD9:4;
      then
A144:  Fn2 is continuous by A54,TOPMETR:10;
      reconsider fF = F| [:N1,SS:] as
        map of [:Y|N1,I[01]|SS:], TUC | Ui
          by A115,A116,A122,A123,A127,FUNCT_2:4;
      reconsider gF = F| [:N1,SS:] as map of [:Y|N1,I[01]|SS:], TUC
        by A122,A123,A127,FUNCT_2:4;
      [:Y|N1,I[01]|SS:] = [:Y,I[01]:] | [:N1,SS:] by BORSUK_3:26;
      then gF is continuous by A1,TOPMETR:10;
      then
A145:  fF is continuous by TOPMETR:9;
      f" is continuous by A113,TOPS_2:def 5;
      then f"*fF is continuous by A145,TOPS_2:58;
      then
A146:  Fni1 is continuous by TOPMETR:7;
      for p being set st p in ([#][:Y|N1,I[01]|S:]) /\ [#][:Y|N1,I[01]|SS:]
        holds Fn2.p = Fni1.p
      proof
        let p be set such that
A147:      p in ([#][:Y|N1,I[01]|S:]) /\ [#][:Y|N1,I[01]|SS:];
A148:    [:N1,S:] /\ [:N1,SS:] = [:N1,S /\ SS:] by ZFMISC_1:122;
A149:    S /\ SS = {TT.i} by A51,A65,A70,TOPMETR2:1;
A150:    p in [#][:Y|N1,I[01]|SS:] by A147,XBOOLE_0:def 3;
A151:    p in [#][:Y|N1,I[01]|S:] by A147,XBOOLE_0:def 3;
        then
A152:    p in [:N1,{TT.i}:] by A148,A150,A127,A149,A129,XBOOLE_0:def 3;
        then consider p1 being Element of N1,
                      p2 being Element of {TT.i} such that
A153:      p = [p1,p2] by DOMAIN_1:9;
A154:    the carrier of (Y|N2) = N2 by JORDAN1:1;
A155:    Fn.p = Fn2.p by A151,A129,FUNCT_1:72;
A156:    p2 in S by A149,XBOOLE_0:def 3;
        p1 in N1;
        then
A157:    p in [:N2,S:] by A153,A156,A98,ZFMISC_1:def 2;
        then
A158:    Fn.p in Fn.:[:N1,{TT.i}:] by A152,A64,A57,A58,FUNCT_2:43;
        (F| [:N1,SS:]).p = F.p by A150,A127,FUNCT_1:72
           .= (F | [:N2,S:]).p by A157,FUNCT_1:72
           .= CircleMap.(Fn.p) by A55,A74,A154,A58,A57,A157,FUNCT_1:23
           .= (CircleMap|Uit).(Fn.p) by A158,A99,FUNCT_1:72
           .= ff.(Fn2.p) by A151,A129,FUNCT_1:72;
        hence Fn2.p = ff".((F| [:N1,SS:]).p) by A158,A99,A155,A110,A118
,FUNCT_1:54
           .= Fni1.p by A150,A122,A127,A119,FUNCT_1:23;
      end;
      then ex h being map of [:Y|N1,I[01]|S1:],R^1 st
      h = Fn2+*Fni1 & h is continuous
        by A142,A143,A144,A146,A140,A141,A120,A57,A121,A137,A138,A139
,TOPALG_3:20;
      hence Fn1 is continuous;
A159:   [:N1,S1:] c= dom F by A10,ZFMISC_1:119;
      rng Fn1 c= dom CircleMap by Lm17,TOPMETR:24;
      then
A160:   dom (CircleMap*Fn1) = dom Fn1 by RELAT_1:46;
A161:   dom Fn1 = dom F /\ [:N1,S1:] by A130,A132,A159,XBOOLE_1:28;
      for a being set st a in dom (CircleMap*Fn1) holds (CircleMap*Fn1).a = F.a
      proof
        let a be set such that
A162:       a in dom (CircleMap*Fn1);
        per cases;
        suppose
A163:       a in dom Fni1;
        then
A164:     a in [:N1,SS:] by A126,A114,RELAT_1:91;
A165:     [:N1,SS:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
        then F.a in F.:[:N1,SS:] by A10,A164,FUNCT_1:def 12;
        then
A166:     F.a in F.:[:N,SS:] by A108;
        then a in F"(dom (ff")) by A164,A165,A10,A115,A60,A116,A119,A61
,FUNCT_1:def 13;
        then
A167:     a in dom (ff"*F) by RELAT_1:182;
        thus (CircleMap*Fn1).a = CircleMap.(Fn1.a) by A162,FUNCT_2:21
          .= CircleMap.(Fni1.a) by A163,FUNCT_4:14
          .= CircleMap.(f".((F| [:N1,SS:]).a)) by A126,A163,FUNCT_1:23
          .= CircleMap.(f".(F.a)) by A122,A126,A163,FUNCT_1:72
          .= CircleMap.((ff"*F).a) by A114,A122,A126,A163,A119,FUNCT_1:23
          .= (CircleMap*(ff"*F)).a by A167,FUNCT_1:23
          .= (CircleMap*ff"*F).a by RELAT_1:55
          .= (CircleMap*ff").(F.a) by A114,A122,A126,A163,FUNCT_1:23
          .= F.a by A118,A166,A60,A117,A116,A61,TOPALG_3:2;
        end;
        suppose
A168:       not a in dom Fni1;
        then
A169:     a in [:N1,S:] by A132,A126,A122,A162,A160,XBOOLE_0:def 2;
        thus (CircleMap*Fn1).a = CircleMap.(Fn1.a) by A162,FUNCT_2:21
          .= CircleMap.(Fn | [:N1,S:].a) by A168,FUNCT_4:12
          .= CircleMap.(Fn.a) by A169,FUNCT_1:72
          .= (CircleMap*Fn).a by A64,A57,A58,A169,A131,FUNCT_2:21
          .= F.a by A169,A55,A131,FUNCT_1:72;
        end;
      end;
      hence F | [:N1,S1:] = CircleMap*Fn1 by A160,A161,FUNCT_1:68;
      0 <= TT.(i+1) by A33,A21;
      then 0 in S1 by A133,TOPREAL5:1;
      then
A170:   {0} c= S1 by ZFMISC_1:37;
A171:   dom (Fn1 | [:the carrier of Y,{0}:])
         = dom Fn1 /\ [:the carrier of Y,{0}:] by RELAT_1:90;
      then
A172:   dom (Fn1 | [:the carrier of Y,{0}:])
         = [:N1 /\ the carrier of Y,S1 /\ {0}:] by A130,A132,ZFMISC_1:123
        .= [:N1,S1 /\ {0}:] by XBOOLE_1:28
        .= [:N1,{0}:] by A170,XBOOLE_1:28;
A173:   dom (Ft | [:N1,I:]) = dom Ft /\ [:N1,I:] by RELAT_1:90
        .= [:(the carrier of Y) /\ N1,{0} /\ I:] by A6,ZFMISC_1:123
        .= [:N1,{0} /\ I:] by XBOOLE_1:28
        .= [:N1,{0}:] by Lm3,XBOOLE_1:28;
      for a being set st a in dom (Fn1 | [:the carrier of Y,{0}:]) holds
      (Fn1 | [:the carrier of Y,{0}:]).a = (Ft | [:N1,I:]).a
      proof
        let a be set;
        assume
A174:      a in dom (Fn1 | [:the carrier of Y,{0}:]);
        then
A175:     a in [:the carrier of Y,{0}:] by A171,XBOOLE_0:def 3;
        then consider a1, a2 being set such that
          a1 in the carrier of Y and
A176:       a2 in {0} and
A177:       a = [a1,a2] by ZFMISC_1:def 2;
A178:     a2 = 0 by A176,TARSKI:def 1;
        0 in S by A65,A51,TOPREAL5:1;
        then {0} c= S by ZFMISC_1:37;
        then
A179:    [:N1,{0}:] c= [:N1,S:] by ZFMISC_1:119;
        then
A180:    a in [:N1,S:] by A174,A172;
A181:    [:N1,S:] c= [:N1,I:] by ZFMISC_1:119;
        then
A182:    a in [:N1,I:] by A180;
A183:    [:N1,I:] c= [:N2,I:] by A98,ZFMISC_1:119;
        per cases;
        suppose
A184:       not a in dom Fni1;
        thus (Fn1 | [:the carrier of Y,{0}:]).a = Fn1.a by A175,FUNCT_1:72
          .= (Fn | [:N1,S:]).a by A184,FUNCT_4:12
          .= Fn.a by A179,A174,A172,FUNCT_1:72
          .= (Ft | [:N2,I:]).a by A56,A175,FUNCT_1:72
          .= Ft.a by A183,A182,FUNCT_1:72
          .= (Ft | [:N1,I:]).a by A181,A180,FUNCT_1:72;
        end;
        suppose
A185:       a in dom Fni1;
        then a in [:N1,SS:] by A126,A114,RELAT_1:91;
        then consider b1, b2 being set such that
A186:       b1 in N1 and
A187:       b2 in SS and
A188:       a = [b1,b2] by ZFMISC_1:def 2;
        set e = (Ft | [:N1,I:]).a;
A189:     e = Ft.a by A180,A181,FUNCT_1:72
         .= (Ft | [:N2,I:]).a by A183,A182,FUNCT_1:72
         .= Fn.a by A56,A175,FUNCT_1:72;
        a2 = b2 by A177,A188,ZFMISC_1:33;
        then
A190:     a2 = TT.i by A65,A178,A187,TOPREAL5:1;
A191:     a1 = b1 by A177,A188,ZFMISC_1:33;
        then
A192:    [a1,TT.i] in [:N1,S:] by A186,A66,ZFMISC_1:106;
        [a1,TT.i] in [:N1,{TT.i}:] by A186,A191,A75,ZFMISC_1:106;
        then
A193:     e in Fn.:[:N1,{TT.i}:] by A64,A189,A74,A58,A57,A190,A177,A131,A192
,FUNCT_1:def 12;
A194:     ff.e = CircleMap.e by A193,A99,FUNCT_1:72
          .= CircleMap.(Ft.a) by A180,A181,FUNCT_1:72
          .= (CircleMap*Ft).a by A6,A175,FUNCT_1:23
          .= F.a by A3,A175,FUNCT_1:72;
        thus (Fn1 | [:the carrier of Y,{0}:]).a = Fn1.a by A175,FUNCT_1:72
          .= Fni1.a by A185,FUNCT_4:14
          .= ff".((F| [:N1,SS:]).a) by A126,A185,A119,FUNCT_1:23
          .= ff".(F.a) by A122,A126,A185,FUNCT_1:72
          .= (Ft | [:N1,I:]).a by A110,A193,A99,A194,A118,FUNCT_1:54;
        end;
      end;
      hence Fn1 | [:the carrier of Y,{0}:] = Ft | [:N1,I:] by A172,A173
,FUNCT_1:9;
      end;
    end;
A195: for i being Nat holds N[i] from NAT_1:sch 1(A30,A31);

    consider N2 being non empty open Subset of Y,
             S being non empty Subset of I[01],
             Fn1 being map of [:Y|N2,I[01]|S:], R^1 such that
A196:   S = [.0,TT.len TT.] and
A197:   y in N2 and
A198:  N2 c= N and
A199:  Fn1 is continuous and
A200:  F | [:N2,S:] = CircleMap*Fn1 and
A201:  Fn1 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] by A195,A20;
    reconsider z = Fn1.x as Point of R^1 by TOPMETR:24;
    take z, y, t, N2;
A202: I[01]|S = I[01] by A196,A14,Lm6,BORSUK_1:83,TSEP_1:3;
    then reconsider Fn1 as map of [:Y|N2,I[01]:], R^1;
    take Fn1;
    thus x = [y,t] & z = Fn1.x & y in N2 & Fn1 is continuous  &
      F | [:N2,I:] = CircleMap*Fn1 &
      Fn1 | [:the carrier of Y,{0}:] = Ft | [:N2,I:]
        by A12,A197,A202,A196,A14,A199,A200,A201,BORSUK_1:83;
    let H be map of [:Y|N2,I[01]:], R^1 such that
A203:  H is continuous and
A204:  F | [:N2,I:] = CircleMap*H and
A205:  H | [:the carrier of Y,{0}:] = Ft | [:N2,I:];
A206: dom H = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
A207: the carrier of [:Y|N2,I[01]:] = [:the carrier of Y|N2,I:]
       by BORSUK_1:def 5;
A208: the carrier of Y|N2 = N2 by JORDAN1:1;
A209: dom Fn1 = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
    defpred M[Nat] means
     $1 in dom TT implies
     ex Z being non empty Subset of I[01] st
      Z = [.0,TT.$1.] & Fn1 | [:N2,Z:] = H | [:N2,Z:];
A210: M[0] by FINSEQ_3:26;
A211: for i being Nat st M[i] holds M[i+1]
  proof
    let i be Nat such that
A212:   M[i] and
A213:   i+1 in dom TT;
    per cases by A213,TOPREALA:23;
    suppose
A214:  i = 0;
    set Z = [.0,TT.(i+1).];
A215:  Z = {0} by A13,A214,RCOMP_1:14;
    reconsider Z as non empty Subset of I[01] by Lm3,A13,A214,RCOMP_1:14;
    take Z;
    thus Z = [.0,TT.(i+1).];
A216: [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
    then
A217:  dom (Fn1 | [:N2,Z:]) = [:N2,Z:] by A209,A207,A208,RELAT_1:91;
A218:  dom (H | [:N2,Z:]) = [:N2,Z:] by A206,A207,A208,A216,RELAT_1:91;
    for x being set st x in dom (Fn1 | [:N2,Z:]) holds
      (Fn1 | [:N2,Z:]).x = (H | [:N2,Z:]).x
    proof
      let x be set;
      assume
A219:     x in dom (Fn1 | [:N2,Z:]);
A220:  [:N2,Z:] c= [:the carrier of Y,Z:] by ZFMISC_1:118;
      thus (Fn1 | [:N2,Z:]).x = Fn1.x by A219,A217,FUNCT_1:72
        .= (Fn1 | [:the carrier of Y,{0}:]).x by A215,A219,A217,A220,FUNCT_1:72
        .= H.x by A215,A201,A205,A220,A219,A217,FUNCT_1:72
        .= (H | [:N2,Z:]).x by A217,A219,FUNCT_1:72;
    end;
    hence Fn1 | [:N2,Z:] = H | [:N2,Z:] by A217,A218,FUNCT_1:9;
    end;
    suppose
A221:   i in dom TT;
    then consider Z being non empty Subset of I[01] such that
A222:   Z = [.0,TT.i.] and
A223:  Fn1 | [:N2,Z:] = H | [:N2,Z:] by A212;
    set ZZ = [.TT.i,TT.(i+1).];
A224: 0 <= TT.i by A221,A21;
A225: TT.(i+1) <= 1 by A213,A221,A21;
    then reconsider ZZ as Subset of I[01] by A224,A27;
    take Z1 = Z \/ ZZ;
A226: TT.i < TT.(i+1) by A213,A221,A21;
    hence Z1 = [.0,TT.(i+1).] by A222,A224,HEINE:2;
A227: [:N2,Z1:] c= [:N2,I:] by ZFMISC_1:118;
    then
A228: dom (Fn1 | [:N2,Z1:]) = [:N2,Z1:] by A209,A207,A208,RELAT_1:91;
A229: dom (H | [:N2,Z1:]) = [:N2,Z1:] by A206,A207,A208,A227,RELAT_1:91;
    for x being set st x in dom (Fn1 | [:N2,Z1:]) holds
      (Fn1 | [:N2,Z1:]).x = (H | [:N2,Z1:]).x
    proof
      let x be set such that
A230:    x in dom (Fn1 | [:N2,Z1:]);
      consider x1, x2 being set such that
A231:    x1 in N2 and
A232:    x2 in Z1 and
A233:    x = [x1,x2] by A228,A230,ZFMISC_1:def 2;
      reconsider xy = {x1} as non empty Subset of Y by A231,ZFMISC_1:37;
A234:  xy c= N2 by A231,ZFMISC_1:37;
      then
A235:  [:xy,ZZ:] c= [:N2,I:] by ZFMISC_1:119;
      reconsider xZZ = [:xy,ZZ:] as Subset of [:Y|N2,I[01]:]
        by A234,A207,A208,ZFMISC_1:119;
      consider Ui being non empty Subset of TUC such that
A236:     Ui in UL and
A237:     F.:[:N,ZZ:] c= Ui by A18,A213,A221;
A238:   the carrier of (TUC | Ui) = Ui by JORDAN1:1;
      consider D being mutually-disjoint open Subset-Family of R^1 such that
A239:     union D = (CircleMap)"Ui and
A240:     for d being Subset of R^1 st d in D
        for f being map of R^1 | d, TUC | Ui st f = CircleMap|d
          holds f is_homeomorphism by A236,A9;
      TT.i in Z by A222,A224,RCOMP_1:15;
      then
A241:   [x1,TT.i] in [:N2,Z:] by A231,ZFMISC_1:106;
A242:  [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
      then
A243:   F. [x1,TT.i] = (CircleMap*Fn1). [x1,TT.i]
           by A241,A196,A14,A200,BORSUK_1:83,FUNCT_1:72
        .= CircleMap.(Fn1. [x1,TT.i]) by A242,A209,A207,A208,A241,FUNCT_1:23;
A244:   TT.i in ZZ by A226,RCOMP_1:15;
      then [x1,TT.i] in [:N,ZZ:] by A231,A198,ZFMISC_1:106;
      then
A245:   F. [x1,TT.i] in F.:[:N,ZZ:] by FUNCT_2:43;
      then Fn1. [x1,TT.i] in (CircleMap)"Ui by A237,A243
,FUNCT_2:46,TOPMETR:24;
      then consider Uit being set such that
A246:     Fn1. [x1,TT.i] in Uit and
A247:     Uit in D by A239,TARSKI:def 4;
      reconsider Uit as non empty Subset of R^1 by A246,A247;
      0 <= TT.i & TT.i <= 1 by A213,A221,A21;
      then
A248:  TT.i is Point of I[01] by JORDAN5A:2;
      0 <= TT.(i+1) by A213,A21;
      then
      TT.(i+1) is Point of I[01] by A225,JORDAN5A:2;
      then
A249:  ZZ is connected by A248,A226,BORSUK_4:49;
      xy is connected by A231,CONNSP_1:29;
      then
A250:  [:xy,ZZ:] is connected by A249,TOPALG_3:17;
      I[01] is SubSpace of I[01] by TSEP_1:2;
      then [:Y|N2,I[01]:] is SubSpace of [:Y,I[01]:] by BORSUK_3:25;
      then
A251:  xZZ is connected by A250,CONNSP_1:24;
      then
A252:  Fn1.:xZZ is connected by A199,A202,TOPREAL5:5;
A253:  x1 in xy by TARSKI:def 1;
      then
A254:  [x1,TT.i] in xZZ by A244,ZFMISC_1:106;
      then
      Fn1. [x1,TT.i] in Fn1.:xZZ by FUNCT_2:43;
      then
A255:  Uit meets Fn1.:xZZ by A246,XBOOLE_0:3;
      D is_a_cover_of Fn1.:xZZ
      proof
        let b be set;
        assume b in Fn1.:xZZ;
        then consider a being Point of [:Y|N2,I[01]:] such that
A256:      a in xZZ and
A257:      Fn1.a = b by FUNCT_2:116;
A258:    CircleMap.(Fn1.a) = (CircleMap*Fn1).a by FUNCT_2:21
          .= F.a by A207,A208,A196,A14,A200,BORSUK_1:83,FUNCT_1:72;
        xy c= N by A198,A231,ZFMISC_1:37;
        then [:xy,ZZ:] c= [:N,ZZ:] by ZFMISC_1:118;
        then
A259:    a in [:N,ZZ:] by A256;
        [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
        then F.a in F.:[:N,ZZ:] by A10,A259,FUNCT_1:def 12;
        hence b in union D by A237,A257,A258,A239,Lm17
,TOPMETR:24,FUNCT_1:def 13;
      end;
      then
A260:  Fn1.:xZZ c= Uit by A247,A252,A255,TOPALG_3:13;
  F. [x1,TT.i] = (CircleMap*H). [x1,TT.i] by A204,A242,A241,FUNCT_1:72
        .= CircleMap.(H. [x1,TT.i]) by A206,A242,A207,A208,A241,FUNCT_1:23;
      then H. [x1,TT.i] in (CircleMap)"Ui by A245,A237
,FUNCT_2:46,TOPMETR:24;
      then consider Uith being set such that
A262:    H. [x1,TT.i] in Uith and
A263:    Uith in D by A239,TARSKI:def 4;
      reconsider Uith as non empty Subset of R^1 by A262,A263;
A264:  H.:xZZ is connected by A251,A203,TOPREAL5:5;
      H. [x1,TT.i] in H.:xZZ by A254,FUNCT_2:43;
      then
A265:  Uith meets H.:xZZ by A262,XBOOLE_0:3;
      D is_a_cover_of H.:xZZ
      proof
        let b be set;
        assume b in H.:xZZ;
        then consider a being Point of [:Y|N2,I[01]:] such that
A266:      a in xZZ and
A267:      H.a = b by FUNCT_2:116;
A268:    CircleMap.(H.a) = (CircleMap*H).a by FUNCT_2:21
          .= F.a by A204,A207,A208,FUNCT_1:72;
        xy c= N by A198,A231,ZFMISC_1:37;
        then [:xy,ZZ:] c= [:N,ZZ:] by ZFMISC_1:118;
        then
A269:    a in [:N,ZZ:] by A266;
        [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
        then F.a in F.:[:N,ZZ:] by A10,A269,FUNCT_1:def 12;
        hence b in union D by A237,A267,A268,A239,Lm17
,TOPMETR:24,FUNCT_1:def 13;
      end;
      then
A270:  H.:xZZ c= Uith by A263,A264,A265,TOPALG_3:13;
A271:  H. [x1,TT.i] in H.:xZZ by A206,A254,FUNCT_1:def 12;
      Fn1. [x1,TT.i] = (Fn1 | [:N2,Z:]). [x1,TT.i] by A241,FUNCT_1:72
        .= H. [x1,TT.i] by A241,A223,FUNCT_1:72;
      then Uit meets Uith by A246,A270,A271,XBOOLE_0:3;
      then
A272:  Uit = Uith by A247,A263,TAXONOM2:def 5;
      set f = CircleMap|Uit;
A273:   the carrier of (R^1 | Uit) = Uit by JORDAN1:1;
A274:   dom f = Uit by Lm17,TOPMETR:24,RELAT_1:91;
      rng f c= Ui
      proof
        let b be set;
        assume b in rng f;
        then consider a being set such that
A275:       a in dom f and
A276:       f.a = b by FUNCT_1:def 5;
        a in union D by A275,A274,A247,TARSKI:def 4;
        then CircleMap.a in Ui by A239,FUNCT_2:46;
        hence b in Ui by A275,A276,A274,FUNCT_1:72;
      end;
      then reconsider f as map of R^1 | Uit, TUC | Ui
        by A274,A273,A238,FUNCT_2:4;
A277:   f is_homeomorphism by A240,A247;
A278:  f is one-to-one by A277,TOPS_2:def 5;
A279:  dom (Fn1 | [:xy,ZZ:]) = [:xy,ZZ:] by A235,A209,A207,A208,RELAT_1:91;
A280:  dom (H | [:xy,ZZ:]) = [:xy,ZZ:] by A235,A206,A207,A208,RELAT_1:91;
A281:  rng (Fn1 | [:xy,ZZ:]) c= dom f
      proof
        let b be set;
        assume b in rng (Fn1 | [:xy,ZZ:]);
        then consider a being set such that
A282:      a in dom (Fn1 | [:xy,ZZ:]) and
A283:      (Fn1 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
        Fn1.a = b by A279,A283,A282,FUNCT_1:72;
        then b in Fn1.:xZZ by A209,A279,A282,FUNCT_1:def 12;
        hence b in dom f by A274,A260;
      end;
A284:  rng (H | [:xy,ZZ:]) c= dom f
      proof
        let b be set;
        assume b in rng (H | [:xy,ZZ:]);
        then consider a being set such that
A285:      a in dom (H | [:xy,ZZ:]) and
A286:      (H | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
        H.a = b by A280,A286,A285,FUNCT_1:72;
        then b in H.:xZZ by A206,A280,A285,FUNCT_1:def 12;
        hence b in dom f by A274,A270,A272;
      end;
A287:  dom (f*(Fn1 | [:xy,ZZ:])) = dom (Fn1 | [:xy,ZZ:]) by A281,RELAT_1:46;
A288:  dom (f*(H | [:xy,ZZ:])) = dom (H | [:xy,ZZ:]) by A284,RELAT_1:46;
      for x being set st x in dom (f*(Fn1 | [:xy,ZZ:])) holds
        (f*(Fn1 | [:xy,ZZ:])).x = (f*(H | [:xy,ZZ:])).x
      proof
        let x be set such that
A289:      x in dom (f*(Fn1 | [:xy,ZZ:]));
A290:    Fn1.x in Fn1.:[:xy,ZZ:] by A287,A289,A279,A209,FUNCT_1:def 12;
A291:    H.x in H.:[:xy,ZZ:] by A206,A287,A289,A279,FUNCT_1:def 12;
        thus (f*(Fn1 | [:xy,ZZ:])).x = ((f*Fn1) | [:xy,ZZ:]).x by RELAT_1:112
          .= (f*Fn1).x by A279,A287,A289,FUNCT_1:72
          .= f.(Fn1.x) by A209,A289,FUNCT_1:23
          .= CircleMap.(Fn1.x) by A290,A260,FUNCT_1:72
          .= (CircleMap*Fn1).x by A289,A209,FUNCT_1:23
          .= CircleMap.(H.x) by A204,A289,A206,A196,A14,A200
,BORSUK_1:83,FUNCT_1:23
          .= f.(H.x) by A291,A270,A272,FUNCT_1:72
          .= (f*H).x by A206,A289,FUNCT_1:23
          .= ((f*H) | [:xy,ZZ:]).x by A279,A287,A289,FUNCT_1:72
          .= (f*(H | [:xy,ZZ:])).x by RELAT_1:112;
      end;
      then
A292:   f*(Fn1 | [:xy,ZZ:]) = f*(H | [:xy,ZZ:]) by A279,A280,A287,A288
,FUNCT_1:9;
      per cases;
      suppose x2 in ZZ;
      then
A293:  x in [:xy,ZZ:] by A233,A253,ZFMISC_1:106;
      thus (Fn1 | [:N2,Z1:]).x = Fn1.x by A228,A230,FUNCT_1:72
        .= (Fn1 | [:xy,ZZ:]).x by A293,FUNCT_1:72
        .= (H | [:xy,ZZ:]).x by A292,A278,A281,A284,A279,A280,FUNCT_1:49
        .= H.x by A293,FUNCT_1:72
        .= (H | [:N2,Z1:]).x by A228,A230,FUNCT_1:72;
      end;
      suppose not x2 in ZZ;
      then x2 in Z by A232,XBOOLE_0:def 2;
      then
A294:  x in [:N2,Z:] by A231,A233,ZFMISC_1:106;
      thus (Fn1 | [:N2,Z1:]).x = Fn1.x by A228,A230,FUNCT_1:72
        .= (Fn1 | [:N2,Z:]).x by A294,FUNCT_1:72
        .= H.x by A294,A223,FUNCT_1:72
        .= (H | [:N2,Z1:]).x by A228,A230,FUNCT_1:72;
      end;
    end;
    hence Fn1 | [:N2,Z1:] = H | [:N2,Z1:] by A228,A229,FUNCT_1:9;
    end;
  end;
  for i being Nat holds M[i] from NAT_1:sch 1(A210,A211);
  then consider Z being non empty Subset of I[01] such that
A295:  Z = [.0,TT.len TT.] and
A296:  Fn1 | [:N2,Z:] = H | [:N2,Z:] by A20;
    thus Fn1 = Fn1 | [:N2,Z:] by A209,A207,A208,A14,A295,BORSUK_1:83,RELAT_1:98
       .= H by A206,A207,A208,A296,A14,A295,BORSUK_1:83,RELAT_1:98;
  end;
  consider G being map of [:Y,I[01]:],R^1 such that
A297: for x being Point of [:Y,I[01]:] holds A[x,G.x] from FUNCT_2:sch 3(A11);
  take G;
A298: dom G = [:the carrier of Y,I:] by A4,FUNCT_2:def 1;
A299: for x being Point of [:Y,I[01]:] holds F.x = (CircleMap*G).x
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y,
             t being Point of I[01],
             N being non empty open Subset of Y,
             Fn being map of [:Y|N,I[01]:], R^1 such that
A300: x = [y,t] and
A301: G.x = Fn.x and
A302: y in N and
    Fn is continuous and
A303: F | [:N,I:] = CircleMap*Fn and
    Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
    for H being map of [:Y|N,I[01]:], R^1 st
        H is continuous & F | [:N,I:] = CircleMap*H &
        H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H by A297;
A304
: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by BORSUK_1:def 5
      .= [:N,I:] by JORDAN1:1;
    then
A305: x in the carrier of [:Y|N,I[01]:] by A300,A302,ZFMISC_1:106;
    thus (CircleMap*G).x = CircleMap.(G.x) by FUNCT_2:21
      .= (CircleMap*Fn).x by A301,A305,FUNCT_2:21
      .= F.x by A303,A304,A305,FUNCT_1:72;
  end;
A306: now
    let N be Subset of Y,
        F be map of [:Y|N,I[01]:],R^1;
    thus dom F = the carrier of [:Y|N,I[01]:] by FUNCT_2:def 1
      .= [:the carrier of Y|N,I:] by BORSUK_1:def 5
      .= [:N,I:] by JORDAN1:1;
  end;
A307: for p being Point of [:Y,I[01]:],
      y being Point of Y, t being Point of I[01],
      N1, N2 being non empty open Subset of Y,
      Fn1 being map of [:Y|N1,I[01]:],R^1,
      Fn2 being map of [:Y|N2,I[01]:],R^1
    st p = [y,t] & y in N1 & y in N2 &
    Fn2 is continuous &  Fn1 is continuous &
    F | [:N2,I:] = CircleMap*Fn2 &
    Fn2 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] &
    F | [:N1,I:] = CircleMap*Fn1 &
    Fn1 | [:the carrier of Y,{0}:] = Ft | [:N1,I:]
    holds Fn1 | [:{y},I:] = Fn2 | [:{y},I:]
   proof
     let p be Point of [:Y,I[01]:],
         y be Point of Y, t be Point of I[01],
         N1, N2 be non empty open Subset of Y,
         Fn1 be map of [:Y|N1,I[01]:],R^1,
         Fn2 be map of [:Y|N2,I[01]:],R^1 such that
       p = [y,t] and
A308:   y in N1 and
A309:   y in N2 and
A310:   Fn2 is continuous and
A311:   Fn1 is continuous and
A312:   F | [:N2,I:] = CircleMap*Fn2 and
A313:   Fn2 | [:the carrier of Y,{0}:] = Ft | [:N2,I:] and
A314:   F | [:N1,I:] = CircleMap*Fn1 and
A315:   Fn1 | [:the carrier of Y,{0}:] = Ft | [:N1,I:];
A316: {y} c= N1 by A308,ZFMISC_1:37;
A317: dom Fn1 = [:N1,I:] by A306;
A318: {y} c= N2 by A309,ZFMISC_1:37;
A319: dom Fn2 = [:N2,I:] by A306;
    consider TT being non empty FinSequence of REAL such that
A320:   TT.1 = 0 and
A321:   TT.len TT = 1 and
A322:   TT is increasing and
A323:   ex N being open Subset of Y st y in N &
      for i being natural number st i in dom TT & i+1 in dom TT
       ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
        F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A1,A7,A8,Th101;
    consider N being open Subset of Y such that
A324:  y in N and
A325:   for i being natural number st i in dom TT & i+1 in dom TT
       ex Ui being non empty Subset of Tunit_circle(2) st Ui in UL &
        F.:[:N,[.TT.i,TT.(i+1).]:] c= Ui by A323;
    reconsider N as non empty open Subset of Y by A324;
A326: 1 in dom TT by FINSEQ_5:6;
A327: len TT in dom TT by FINSEQ_5:6;
A328: dom Fn2 = the carrier of [:Y|N2,I[01]:] by FUNCT_2:def 1;
A329: the carrier of [:Y|N2,I[01]:] = [:the carrier of Y|N2,I:]
       by BORSUK_1:def 5;
A330: the carrier of Y|N2 = N2 by JORDAN1:1;
A331: the carrier of [:Y|N1,I[01]:] = [:the carrier of Y|N1,I:]
       by BORSUK_1:def 5;
A332: the carrier of Y|N1 = N1 by JORDAN1:1;
A333: dom Fn1 = the carrier of [:Y|N1,I[01]:] by FUNCT_2:def 1;
    defpred M[Nat] means
     $1 in dom TT implies
     ex Z being non empty Subset of I[01] st
      Z = [.0,TT.$1.] & Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:];
A334: M[0] by FINSEQ_3:26;
A335: for i being Nat st M[i] holds M[i+1]
  proof
    let i be Nat such that
A336:   M[i] and
A337:   i+1 in dom TT;
    per cases by A337,TOPREALA:23;
    suppose
A338:  i = 0;
    set Z = [.0,TT.(i+1).];
A339:  Z = {0} by A320,A338,RCOMP_1:14;
    reconsider Z as non empty Subset of I[01] by Lm3,A320,A338,RCOMP_1:14;
    take Z;
    thus Z = [.0,TT.(i+1).];
A340: [:{y},Z:] c= [:N1,I:] by A316,ZFMISC_1:119;
    then
A341:  dom (Fn1 | [:{y},Z:]) = [:{y},Z:] by A317,RELAT_1:91;
A342: [:{y},Z:] c= [:N2,I:] by A318,ZFMISC_1:119;
    then
A343:  dom (Fn2 | [:{y},Z:]) = [:{y},Z:] by A319,RELAT_1:91;
    for x being set st x in dom (Fn1 | [:{y},Z:]) holds
      (Fn1 | [:{y},Z:]).x = (Fn2 | [:{y},Z:]).x
    proof
      let x be set;
      assume
A344:     x in dom (Fn1 | [:{y},Z:]);
A345:  [:{y},Z:] c= [:the carrier of Y,Z:] by ZFMISC_1:118;
      thus (Fn1 | [:{y},Z:]).x = Fn1.x by A344,A341,FUNCT_1:72
        .= (Fn1 | [:the carrier of Y,{0}:]).x by A339,A344,A341,A345,FUNCT_1:72
        .= Ft.x by A315,A344,A341,A340,FUNCT_1:72
        .= (Ft | [:N2,I:]).x by A344,A341,A342,FUNCT_1:72
        .= Fn2.x by A339,A313,A345,A344,A341,FUNCT_1:72
        .= (Fn2 | [:{y},Z:]).x by A341,A344,FUNCT_1:72;
    end;
    hence Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:] by A341,A343,FUNCT_1:9;
    end;
    suppose
A346:   i in dom TT;
    then consider Z being non empty Subset of I[01] such that
A347:   Z = [.0,TT.i.] and
A348:  Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:] by A336;
    set ZZ = [.TT.i,TT.(i+1).];
A349: now
      let i be Nat such that
A350:     i in dom TT;
      1 <= i by A350,FINSEQ_3:27;
      then 1 = i or 1 < i by REAL_1:def 5;
      hence
A351:     0 <= TT.i by A320,A322,A350,A326,SEQM_3:def 1;
      assume
A352:     i+1 in dom TT;
A353:   i+0 < i+1 by XREAL_1:10;
      hence
A354:     TT.i < TT.(i+1) by A322,A350,A352,SEQM_3:def 1;
      i+1 <= len TT by A352,FINSEQ_3:27;
      then i+1 = len TT or i+1 < len TT by REAL_1:def 5;
      hence TT.(i+1) <= 1 by A321,A322,A352,A327,SEQM_3:def 1;
      hence TT.i < 1 by A354,XREAL_1:2;
      thus 0 < TT.(i+1) by A353,A351,A322,A350,A352,SEQM_3:def 1;
    end;
A355: now
    let i be Nat such that
A356:   0 <= TT.i & TT.(i+1) <= 1;
    thus [.TT.i,TT.(i+1).] c= I
      proof
        let a be set;
        assume
A357:       a in [.TT.i,TT.(i+1).];
        then reconsider a as Real;
        TT.i <= a & a <= TT.(i+1) by A357,TOPREAL5:1;
        then 0 <= a & a <= 1 by A356,XREAL_1:2;
        hence thesis by JORDAN5A:2;
      end;
    end;
A358: 0 <= TT.i by A346,A349;
A359: TT.(i+1) <= 1 by A337,A346,A349;
    then reconsider ZZ as Subset of I[01] by A358,A355;
    take Z1 = Z \/ ZZ;
A360: TT.i < TT.(i+1) by A337,A346,A349;
    hence Z1 = [.0,TT.(i+1).] by A347,A358,HEINE:2;
    [:{y},Z1:] c= [:N1,I:] by A316,ZFMISC_1:119;
    then
A361: dom (Fn1 | [:{y},Z1:]) = [:{y},Z1:] by A317,RELAT_1:91;
    [:{y},Z1:] c= [:N2,I:] by A318,ZFMISC_1:119;
    then
A362: dom (Fn2 | [:{y},Z1:]) = [:{y},Z1:] by A328,A329,A330,RELAT_1:91;
    for x being set st x in dom (Fn1 | [:{y},Z1:]) holds
      (Fn1 | [:{y},Z1:]).x = (Fn2 | [:{y},Z1:]).x
    proof
      let x be set such that
A363:    x in dom (Fn1 | [:{y},Z1:]);
      consider x1, x2 being set such that
A364:    x1 in {y} and
A365:    x2 in Z1 and
A366:    x = [x1,x2] by A361,A363,ZFMISC_1:def 2;
      reconsider xy = {x1} as non empty Subset of Y by A364,ZFMISC_1:37;
A367:  xy c= N2 by A318,A364,ZFMISC_1:37;
A368:  xy c= N1 by A316,A364,ZFMISC_1:37;
      then
A369:  [:xy,ZZ:] c= [:N1,I:] by ZFMISC_1:119;
A370: x1 = y by A364,TARSKI:def 1;
      then
A371:  xy c= N by A324,ZFMISC_1:37;
      reconsider xZZ = [:xy,ZZ:] as Subset of [:Y|N1,I[01]:]
        by A368,A331,A332,ZFMISC_1:119;
      consider Ui being non empty Subset of TUC such that
A372:     Ui in UL and
A373:     F.:[:N,ZZ:] c= Ui by A325,A337,A346;
A374:   the carrier of (TUC | Ui) = Ui by JORDAN1:1;
      consider D being mutually-disjoint open Subset-Family of R^1 such that
A375:     union D = (CircleMap)"Ui and
A376:     for d being Subset of R^1 st d in D
        for f being map of R^1 | d, TUC | Ui st f = CircleMap|d
          holds f is_homeomorphism by A372,A9;
A377:  TT.i in Z by A347,A358,RCOMP_1:15;
      then
A378:   [x1,TT.i] in [:{y},Z:] by A364,ZFMISC_1:106;
A379:  [x1,TT.i] in [:N2,Z:] by A377,A318,A364,ZFMISC_1:106;
A380: [:N2,Z:] c= [:N2,I:] by ZFMISC_1:118;
A381:  [:{y},Z:] c= [:N1,I:] by A316,ZFMISC_1:119;
      then
A382:   F. [x1,TT.i] = (CircleMap*Fn1). [x1,TT.i] by A314,A378,FUNCT_1:72
        .= CircleMap.(Fn1. [x1,TT.i]) by A317,A381,A378,FUNCT_1:23;
A383:   TT.i in ZZ by A360,RCOMP_1:15;
      then [x1,TT.i] in [:N,ZZ:] by A370,A324,ZFMISC_1:106;
      then
A384:   F. [x1,TT.i] in F.:[:N,ZZ:] by FUNCT_2:43;
      then Fn1. [x1,TT.i] in (CircleMap)"Ui by A373,A382
,FUNCT_2:46,TOPMETR:24;
      then consider Uit being set such that
A385:     Fn1. [x1,TT.i] in Uit and
A386:     Uit in D by A375,TARSKI:def 4;
      reconsider Uit as non empty Subset of R^1 by A385,A386;
      0 <= TT.i & TT.i <= 1 by A337,A346,A349;
      then
A387:  TT.i is Point of I[01] by JORDAN5A:2;
      0 <= TT.(i+1) by A337,A349;
      then
      TT.(i+1) is Point of I[01] by A359,JORDAN5A:2;
      then
A388:  ZZ is connected by A387,A360,BORSUK_4:49;
      xy is connected by A364,CONNSP_1:29;
      then
A389:  [:xy,ZZ:] is connected by A388,TOPALG_3:17;
A390:  I[01] is SubSpace of I[01] by TSEP_1:2;
      then [:Y|N1,I[01]:] is SubSpace of [:Y,I[01]:] by BORSUK_3:25;
      then xZZ is connected by A389,CONNSP_1:24;
      then
A391:  Fn1.:xZZ is connected by A311,TOPREAL5:5;
A392:  x1 in xy by TARSKI:def 1;
      then
A393:  [x1,TT.i] in xZZ by A383,ZFMISC_1:106;
      Fn1. [x1,TT.i] in Fn1.:xZZ by A393,FUNCT_2:43;
      then
A394:  Uit meets Fn1.:xZZ by A385,XBOOLE_0:3;
      D is_a_cover_of Fn1.:xZZ
      proof
        let b be set;
        assume b in Fn1.:xZZ;
        then consider a being Point of [:Y|N1,I[01]:] such that
A395:      a in xZZ and
A396:      Fn1.a = b by FUNCT_2:116;
A397:    CircleMap.(Fn1.a) = (CircleMap*Fn1).a by FUNCT_2:21
          .= F.a by A331,A332,A314,FUNCT_1:72;
        [:xy,ZZ:] c= [:N,ZZ:] by A371,ZFMISC_1:118;
        then
A398:    a in [:N,ZZ:] by A395;
        [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
        then F.a in F.:[:N,ZZ:] by A10,A398,FUNCT_1:def 12;
        hence b in union D by A373,A396,A397,A375,Lm17
,TOPMETR:24,FUNCT_1:def 13;
      end;
      then
A399:  Fn1.:xZZ c= Uit by A386,A391,A394,TOPALG_3:13;
  F. [x1,TT.i] = (CircleMap*Fn2). [x1,TT.i] by A312,A379,A380,FUNCT_1:72
        .= CircleMap.(Fn2. [x1,TT.i]) by A328,A329,A330,A380,A379,FUNCT_1:23;
      then Fn2. [x1,TT.i] in (CircleMap)"Ui by A384,A373
,FUNCT_2:46,TOPMETR:24;
      then consider Uith being set such that
A401:    Fn2. [x1,TT.i] in Uith and
A402:    Uith in D by A375,TARSKI:def 4;
      reconsider Uith as non empty Subset of R^1 by A401,A402;
      reconsider XZZ = [:xy,ZZ:] as Subset of [:Y|N2,I[01]:]
        by A367,A329,A330,ZFMISC_1:119;
      [:Y|N2,I[01]:] is SubSpace of [:Y,I[01]:] by A390,BORSUK_3:25;
      then XZZ is connected by A389,CONNSP_1:24;
      then
A403:  Fn2.:XZZ is connected by A310,TOPREAL5:5;
A404: [:xy,ZZ:] c= [:N2,I:] by A367,ZFMISC_1:119;
      [x1,TT.i] in [:xy,ZZ:] by A364,A370,A383,ZFMISC_1:106;
      then [x1,TT.i] in dom Fn2 by A319,A404;
      then
A405:  Fn2. [x1,TT.i] in Fn2.:xZZ by A393,FUNCT_2:43;
      then
A406:  Uith meets Fn2.:xZZ by A401,XBOOLE_0:3;
      D is_a_cover_of Fn2.:xZZ
      proof
        let b be set;
        assume b in Fn2.:xZZ;
        then consider a being Point of [:Y|N2,I[01]:] such that
A407:      a in xZZ and
A408:      Fn2.a = b by FUNCT_2:116;
A409:    CircleMap.(Fn2.a) = (CircleMap*Fn2).a by FUNCT_2:21
          .= F.a by A312,A329,A330,FUNCT_1:72;
        [:xy,ZZ:] c= [:N,ZZ:] by A371,ZFMISC_1:118;
        then
A410:    a in [:N,ZZ:] by A407;
        [:N,ZZ:] c= [:the carrier of Y,I:] by ZFMISC_1:119;
        then F.a in F.:[:N,ZZ:] by A10,A410,FUNCT_1:def 12;
        hence b in union D by A373,A408,A409,A375,Lm17
,TOPMETR:24,FUNCT_1:def 13;
      end;
      then
A411:  Fn2.:xZZ c= Uith by A402,A403,A406,TOPALG_3:13;
      Fn1. [x1,TT.i] = (Fn1 | [:{y},Z:]). [x1,TT.i] by A378,FUNCT_1:72
        .= Fn2. [x1,TT.i] by A378,A348,FUNCT_1:72;
      then Uit meets Uith by A385,A411,A405,XBOOLE_0:3;
      then
A412:  Uit = Uith by A386,A402,TAXONOM2:def 5;
      set f = CircleMap|Uit;
A413:   the carrier of (R^1 | Uit) = Uit by JORDAN1:1;
A414:   dom f = Uit by Lm17,TOPMETR:24,RELAT_1:91;
      rng f c= Ui
      proof
        let b be set;
        assume b in rng f;
        then consider a being set such that
A415:       a in dom f and
A416:       f.a = b by FUNCT_1:def 5;
        a in union D by A415,A414,A386,TARSKI:def 4;
        then CircleMap.a in Ui by A375,FUNCT_2:46;
        hence b in Ui by A415,A416,A414,FUNCT_1:72;
      end;
      then reconsider f as map of R^1 | Uit, TUC | Ui
        by A414,A413,A374,FUNCT_2:4;
A417:   f is_homeomorphism by A376,A386;
A418:  f is one-to-one by A417,TOPS_2:def 5;
A419:  dom (Fn1 | [:xy,ZZ:]) = [:xy,ZZ:] by A369,A333,A331,A332,RELAT_1:91;
      [:xy,ZZ:] c= [:N2,I:] by A367,ZFMISC_1:119;
      then
A420:  dom (Fn2 | [:xy,ZZ:]) = [:xy,ZZ:] by A328,A329,A330,RELAT_1:91;
A421:  rng (Fn1 | [:xy,ZZ:]) c= dom f
      proof
        let b be set;
        assume b in rng (Fn1 | [:xy,ZZ:]);
        then consider a being set such that
A422:      a in dom (Fn1 | [:xy,ZZ:]) and
A423:      (Fn1 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
        Fn1.a = b by A419,A423,A422,FUNCT_1:72;
        then b in Fn1.:xZZ by A333,A419,A422,FUNCT_1:def 12;
        hence b in dom f by A414,A399;
      end;
A424:  rng (Fn2 | [:xy,ZZ:]) c= dom f
      proof
        let b be set;
        assume b in rng (Fn2 | [:xy,ZZ:]);
        then consider a being set such that
A425:      a in dom (Fn2 | [:xy,ZZ:]) and
A426:      (Fn2 | [:xy,ZZ:]).a = b by FUNCT_1:def 5;
        Fn2.a = b by A420,A426,A425,FUNCT_1:72;
        then b in Fn2.:xZZ by A328,A420,A425,FUNCT_1:def 12;
        hence b in dom f by A414,A411,A412;
      end;
A427:  dom (f*(Fn1 | [:xy,ZZ:])) = dom (Fn1 | [:xy,ZZ:]) by A421,RELAT_1:46;
A428:  dom (f*(Fn2 | [:xy,ZZ:])) = dom (Fn2 | [:xy,ZZ:]) by A424,RELAT_1:46;
      for x being set st x in dom (f*(Fn1 | [:xy,ZZ:])) holds
        (f*(Fn1 | [:xy,ZZ:])).x = (f*(Fn2 | [:xy,ZZ:])).x
      proof
        let x be set such that
A429:      x in dom (f*(Fn1 | [:xy,ZZ:]));
A430:    Fn1.x in Fn1.:[:xy,ZZ:] by A427,A429,A419,A333,FUNCT_1:def 12;
        dom (Fn1 | [:xy,ZZ:]) = dom Fn1 /\ [:xy,ZZ:] by RELAT_1:90;
        then
A431:   x in [:xy,ZZ:] by A429,A427,XBOOLE_0:def 3;
A432:    Fn2.x in Fn2.:[:xy,ZZ:] by A427,A429,A419,A319,A404,FUNCT_1:def 12;
A433:   dom (Fn1 | [:xy,ZZ:]) c= dom Fn1 by RELAT_1:89;
        thus (f*(Fn1 | [:xy,ZZ:])).x = ((f*Fn1) | [:xy,ZZ:]).x by RELAT_1:112
          .= (f*Fn1).x by A419,A427,A429,FUNCT_1:72
          .= f.(Fn1.x) by A333,A429,FUNCT_1:23
          .= CircleMap.(Fn1.x) by A430,A399,FUNCT_1:72
          .= (CircleMap*Fn1).x by A429,A333,FUNCT_1:23
          .= F.x by A433,A429,A427,A317,A314,FUNCT_1:72
          .= (CircleMap*Fn2).x by A312,A431,A404,FUNCT_1:72
          .= CircleMap.(Fn2.x) by A431,A319,A404,FUNCT_1:23
          .= f.(Fn2.x) by A432,A411,A412,FUNCT_1:72
          .= (f*Fn2).x by A431,A319,A404,FUNCT_1:23
          .= ((f*Fn2) | [:xy,ZZ:]).x by A419,A427,A429,FUNCT_1:72
          .= (f*(Fn2 | [:xy,ZZ:])).x by RELAT_1:112;
      end;
      then
A434:   f*(Fn1 | [:xy,ZZ:]) = f*(Fn2 | [:xy,ZZ:]) by A419,A420,A427,A428
,FUNCT_1:9;
      per cases;
      suppose x2 in ZZ;
      then
A435:  x in [:xy,ZZ:] by A366,A392,ZFMISC_1:106;
      thus (Fn1 | [:{y},Z1:]).x = Fn1.x by A361,A363,FUNCT_1:72
        .= (Fn1 | [:xy,ZZ:]).x by A435,FUNCT_1:72
        .= (Fn2 | [:xy,ZZ:]).x by A434,A418,A421,A424,A419,A420,FUNCT_1:49
        .= Fn2.x by A435,FUNCT_1:72
        .= (Fn2 | [:{y},Z1:]).x by A361,A363,FUNCT_1:72;
      end;
      suppose not x2 in ZZ;
      then x2 in Z by A365,XBOOLE_0:def 2;
      then
A436:  x in [:{y},Z:] by A364,A366,ZFMISC_1:106;
      thus (Fn1 | [:{y},Z1:]).x = Fn1.x by A361,A363,FUNCT_1:72
        .= (Fn1 | [:{y},Z:]).x by A436,FUNCT_1:72
        .= Fn2.x by A436,A348,FUNCT_1:72
        .= (Fn2 | [:{y},Z1:]).x by A361,A363,FUNCT_1:72;
      end;
    end;
    hence Fn1 | [:{y},Z1:] = Fn2 | [:{y},Z1:] by A361,A362,FUNCT_1:9;
    end;
  end;
  for i being Nat holds M[i] from NAT_1:sch 1(A334,A335);
  then ex Z being non empty Subset of I[01] st
  Z = [.0,TT.len TT.] & Fn1 | [:{y},Z:] = Fn2 | [:{y},Z:] by A327;
    hence Fn1 | [:{y},I:] = Fn2 | [:{y},I:] by A321,BORSUK_1:83;
    end;
  for p being Point of [:Y,I[01]:], V being Subset of R^1
    st G.p in V & V is open holds
    ex W being Subset of [:Y,I[01]:] st p in W & W is open & G.:W c= V
  proof
    let p be Point of [:Y,I[01]:],
        V be Subset of R^1 such that
A437:  G.p in V and
A438:  V is open;
    consider y being Point of Y,
             t being Point of I[01],
             N being non empty open Subset of Y,
             Fn being map of [:Y|N,I[01]:], R^1 such that
A439:  p = [y,t] and
A440:  G.p = Fn.p and
A441:  y in N and
A442:  Fn is continuous and
A443:  F | [:N,I:] = CircleMap*Fn and
A444:  Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
      for H being map of [:Y|N,I[01]:], R^1 st H is continuous &
          F | [:N,I:] = CircleMap*H &
          H | [:the carrier of Y,{0}:] = Ft | [:N,I:] holds Fn = H by A297;
A445
: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by BORSUK_1:def 5
      .= [:N,I:] by JORDAN1:1;
    then p in the carrier of [:Y|N,I[01]:] by A439,A441,ZFMISC_1:106;
    then consider W being Subset of [:Y|N,I[01]:] such that
A446:  p in W and
A447:  W is open and
A448:  Fn.:W c= V by A437,A438,A442,A440,JGRAPH_2:20;
A449: dom Fn = [:N,I:] by A445,FUNCT_2:def 1;
    [:Y|N,I[01]:] = [:Y,I[01]:] | [:N,[#]I[01]:] by Lm7,BORSUK_3:26;
    then consider C being Subset of [:Y,I[01]:] such that
A450:  C is open and
A451:  C /\ [#][:Y|N,I[01]:] = W by A447,TOPS_2:32;
    take WW = C /\ [:N,[#]I[01]:];
A452: [#](Y|N) = N by PRE_TOPC:def 10;
    then
A453: [#][:Y|N,I[01]:] = [:N,[#]I[01]:] by BORSUK_3:1;
    thus p in WW by A451,A446,A452,BORSUK_3:1;
    [:N,[#]I[01]:] is open by BORSUK_1:46;
    hence WW is open by A450,TOPS_1:38;
    let y be set;
    assume y in G.:WW;
    then consider x being Point of [:Y,I[01]:] such that
A454:  x in WW and
A455:  y = G.x by FUNCT_2:116;
    consider y0 being Point of Y,
             t0 being Point of I[01],
             N0 being non empty open Subset of Y,
             Fn0 being map of [:Y|N0,I[01]:], R^1 such that
A456:  x = [y0,t0] and
A457:  G.x = Fn0.x and
A458:  y0 in N0 and
A459:  Fn0 is continuous and
A460:  F | [:N0,I:] = CircleMap*Fn0 and
A461:  Fn0 | [:the carrier of Y,{0}:] = Ft | [:N0,I:] and
      for H being map of [:Y|N0,I[01]:], R^1 st H is continuous &
          F | [:N0,I:] = CircleMap*H &
          H | [:the carrier of Y,{0}:] = Ft | [:N0,I:] holds Fn0 = H by A297;
A462: x in [:N,[#]I[01]:] by A454,XBOOLE_0:def 3;
    then
A463: y0 in N by A456,ZFMISC_1:106;
A464: x in [:{y0},I:] by A456,ZFMISC_1:128;
    then Fn.x = (Fn | [:{y0},I:]).x by FUNCT_1:72
        .= (Fn0 | [:{y0},I:]).x by A463,A456,A442,A458,A459,A443,A444,A460,A461
,A307
        .= Fn0.x by A464,FUNCT_1:72;
    then y in Fn.:W by A455,A457,A454,A451,A453,A462,A449,Lm6,FUNCT_1:def 12;
    hence y in V by A448;
  end;
  hence G is continuous by JGRAPH_2:20;
  thus F = CircleMap*G by A299,FUNCT_2:113;
A465: [:the carrier of Y,{0}:] c= [:the carrier of Y,I:] by Lm3,ZFMISC_1:118;
A466: dom Ft = dom G /\ [:the carrier of Y,{0}:] by A298,A465,A6,XBOOLE_1:28;
  for x being set st x in dom Ft holds Ft.x = G.x
  proof
    let x be set;
    assume
A467:   x in dom Ft;
    then x in dom G by A298,A465,A6;
    then consider y being Point of Y, t being Point of I[01],
         N being non empty open Subset of Y,
         Fn being map of [:Y|N,I[01]:], R^1 such that
A468:   x = [y,t] and
A469:   G.x = Fn.x and
A470:   y in N and Fn is continuous & F | [:N,I:] = CircleMap*Fn and
A471:   Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
      for H being map of [:Y|N,I[01]:], R^1 st H is continuous &
          F | [:N,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] =
          Ft | [:N,I:] holds Fn = H by A297;
    x in [:N,I:] by A468,A470,ZFMISC_1:106;
    hence Ft.x = (Ft | [:N,I:]).x by FUNCT_1:72
       .= G.x by Lm19,A5,A467,A471,A469,FUNCT_1:72;
  end;
  hence G | [:the carrier of Y,{0}:] = Ft by A466,FUNCT_1:68;
  let H be map of [:Y,I[01]:], R^1 such that
A472: H is continuous and
A473: F = CircleMap*H and
A474: H | [:the carrier of Y,{0}:] = Ft;
  for x being Point of [:Y,I[01]:] holds G.x = H.x
  proof
    let x be Point of [:Y,I[01]:];
    consider y being Point of Y,
             t being Point of I[01],
             N being non empty open Subset of Y,
             Fn being map of [:Y|N,I[01]:], R^1 such that
A475: x = [y,t] and
A476: G.x = Fn.x and
A477: y in N and
    Fn is continuous & F | [:N,I:] = CircleMap*Fn and
    Fn | [:the carrier of Y,{0}:] = Ft | [:N,I:] and
A478: for H being map of [:Y|N,I[01]:], R^1 st H is continuous &
        F | [:N,I:] = CircleMap*H & H | [:the carrier of Y,{0}:] = Ft | [:N,I:]
        holds Fn = H by A297;
A479
: the carrier of [:Y|N,I[01]:] = [:the carrier of (Y|N),I:] by BORSUK_1:def 5
      .= [:N,I:] by JORDAN1:1;
    then
A480: x in the carrier of [:Y|N,I[01]:] by A475,A477,ZFMISC_1:106;
    dom H = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
    then [:N,I:] c= dom H by A4,ZFMISC_1:118;
    then
A481: dom (H | [:N,I:]) = [:N,I:] by RELAT_1:91;
    rng (H | [:N,I:]) c= R;
    then reconsider H1 = H | [:N,I:] as map of [:Y|N,I[01]:], R^1
      by A481,A479,FUNCT_2:4;
A482: H1 is continuous by A472,TOPALG_3:18;
A483:  H | [:N,I:] | [:the carrier of Y,{0}:]
       = H | ([:the carrier of Y,{0}:] /\ [:N,I:]) by RELAT_1:100
      .= Ft | [:N,I:] by A474,RELAT_1:100;
    F | [:N,I:] = CircleMap*(H| [:N,I:]) by A473,RELAT_1:112;
    hence G.x = (H| [:N,I:]).x by A478,A476,A482,A483
       .= H.x by A479,A480,FUNCT_1:72;
  end;
  hence G = H by FUNCT_2:113;
end;

theorem Th103:
  for x0, y0 being Point of Tunit_circle(2),
      xt being Point of R^1,
      f being Path of x0,y0 st xt in (CircleMap)"{x0}
   ex ft being map of I[01], R^1 st ft.0 = xt &
    f = CircleMap*ft & ft is continuous &
    for f1 being map of I[01], R^1 st f1 is continuous & f = CircleMap*f1 &
        f1.0 = xt holds ft = f1
proof 
  let x0, y0 be Point of TUC;
  let xt be Point of R^1;
  let f be Path of x0,y0;
  assume xt in (CircleMap)"{x0};
  then
A1: CircleMap.xt in {x0} by FUNCT_2:46;
  set Y = 1TopSp{1};
A2: [#]Y = the carrier of Y by PRE_TOPC:12;
  deffunc F(set,Element of I) = f.$2;
  consider F being Function of [:the carrier of Y,I:], cS1 such that
A3: for y being Element of the carrier of Y, i being Element of I
      holds F. [y,i] = F(y,i) from FUNCT_2:sch 8;
A4: the carrier of [:Y,I[01]:] = [:the carrier of Y, I:] by BORSUK_1:def 5;
  then reconsider F as map of [:Y,I[01]:], TUC;
A5: the carrier of Y = {1} by PCOMPS_1:8;
  then reconsider j = 1 as Point of Y by TARSKI:def 1;
  for p being Point of [:Y,I[01]:], V being Subset of TUC
    st F.p in V & V is open holds
    ex W being Subset of [:Y,I[01]:] st p in W & W is open & F.:W c= V
  proof
    let p be Point of [:Y,I[01]:],
        V be Subset of TUC such that
A6:   F.p in V and
A7:   V is open;
    consider p1 being Point of Y, p2 being Point of I[01] such that
A8:   p = [p1,p2] by BORSUK_1:50;
    F. [p1,p2] = f.p2 by A3;
    then consider S being Subset of I[01] such that
A9:   p2 in S and
A10:   S is open and
A11:   f.:S c= V by A6,A7,A8,JGRAPH_2:20;
    set W = [:{1},S:];
    W c= [:the carrier of Y,I:] by A5,ZFMISC_1:118;
    then reconsider W as Subset of [:Y,I[01]:] by BORSUK_1:def 5;
    take W;
    thus p in W by A8,A9,A5,ZFMISC_1:106;
    thus W is open by A5,A2,A10,BORSUK_1:46;
    let y be set;
    assume y in F.:W;
    then consider x being set such that
A12:   x in the carrier of [:Y,I[01]:] and
A13:   x in W and
A14:   y = F.x by FUNCT_2:115;
    consider x1 being Point of Y, x2 being Point of I[01] such that
A15:   x = [x1,x2] by A12,BORSUK_1:50;
A16: y = f.x2 by A14,A15,A3;
    x2 in S by A13,A15,ZFMISC_1:106;
    then f.x2 in f.:S by FUNCT_2:43;
    hence y in V by A11,A16;
  end;
  then
A17: F is continuous by JGRAPH_2:20;
A18: dom F = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
A19: [:the carrier of Y,{0}:] c= dom F by A18,A4,Lm3,ZFMISC_1:118;
  then
A20: dom (F | [:the carrier of Y,{0}:]) = [:the carrier of Y,{0}:]
     by RELAT_1:91;
A21: the carrier of [:Y,Sspace(0[01]):] =
    [:the carrier of Y, the carrier of Sspace(0[01]):] by BORSUK_1:def 5;
  set Ft = [:Y,Sspace(0[01]):] --> xt;
A22: dom (CircleMap*Ft) = [:the carrier of Y,{0}:] by Lm19,A21,FUNCT_2:def 1;
A23: for y being Element of the carrier of Y, i being Element of {0} holds
      Ft. [y,i] = xt
    proof
      let y be Element of the carrier of Y,
          i be Element of {0};
A24:  Ft = (the carrier of [:Y,Sspace(0[01]):]) --> xt by BORSUK_1:def 3;
      [y,i] in [:the carrier of Y, the carrier of Sspace(0[01]):]
        by Lm19,ZFMISC_1:def 2;
      hence Ft. [y,i] = xt by A24,A21,FUNCOP_1:13;
    end;
  for x being set st x in dom (CircleMap*Ft) holds
   (F | [:the carrier of Y,{0}:]).x = (CircleMap*Ft).x
  proof
    let x be set such that
A25:   x in dom (CircleMap*Ft);
    consider x1, x2 being set such that
A26:   x1 in the carrier of Y and
A27:   x2 in {0} and
A28:   x = [x1,x2] by A25,Lm19,A21,ZFMISC_1:def 2;
A29: x2 = 0 by A27,TARSKI:def 1;
    thus (F | [:the carrier of Y,{0}:]).x = F.x by A25,Lm19,A21,FUNCT_1:72
      .= f.x2 by A3,A26,A28,A29,Lm2
      .= x0 by A29,BORSUK_2:def 4
      .= CircleMap.xt by A1,TARSKI:def 1
      .= CircleMap.(Ft.x) by A23,A26,A28,A27
      .= (CircleMap*Ft).x by A25,FUNCT_1:22;
  end;
  then F | [:the carrier of Y,{0}:] = CircleMap*Ft by A20,A22,FUNCT_1:9;
  then consider G being map of [:Y,I[01]:], R^1 such that
A30: G is continuous and
A31: F = CircleMap*G and
A32: G | [:the carrier of Y,{0}:] = Ft and
A33
: for H being map  of [:Y,I[01]:], R^1 st H is continuous & F = CircleMap*H &
    H | [:the carrier of Y,{0}:] = Ft holds G = H by A17,Th102;
  take ft = Prj2(j,G);
A34: [j,j0] in [:the carrier of Y,{0}:] by Lm4,ZFMISC_1:106;
  thus ft.0 = G.(j,j0) by Def6
    .= G. [j,j0] by BINOP_1:def 1
    .= Ft. [j,j0] by A32,A34,FUNCT_1:72
    .= xt by A23,Lm4;
  for i being Point of I[01] holds f.i = (CircleMap*ft).i
  proof
    let i be Point of I[01];
    thus (CircleMap*ft).i = CircleMap.(ft.i) by FUNCT_2:21
      .= CircleMap.(G.(j,i)) by Def6
      .= CircleMap.(G. [j,i]) by BINOP_1:def 1
      .= (CircleMap*G). [j,i] by FUNCT_2:21
      .= f.i by A31,A3;
  end;
  hence f = CircleMap*ft by FUNCT_2:113;
  for H being continuous map of [:Y,I[01]:], R^1 holds
  Prj2(j,H) is continuous;
  hence ft is continuous by A30;
  let f1 be map of I[01], R^1;
  assume that
A35: f1 is continuous and
A36:  f = CircleMap*f1 and
A37: f1.0 = xt;
  deffunc H(set,Element of I) = f1.$2;
  consider H being Function of [:the carrier of Y,I:], R such that
A38: for y being Element of the carrier of Y, i being Element of I
      holds H. [y,i] = H(y,i) from FUNCT_2:sch 8;
  reconsider H as map of [:Y,I[01]:], R^1 by A4;
  for x being Point of [:Y,I[01]:] holds F.x = (CircleMap*H).x
  proof
    let x be Point of [:Y,I[01]:];
    consider x1 being Point of Y,
             x2 being Point of I[01] such that
A39:   x = [x1,x2] by BORSUK_1:50;
    thus (CircleMap*H).x = CircleMap.(H.x) by FUNCT_2:21
      .= CircleMap.(f1.x2) by A39,A38
      .= (CircleMap*f1).x2 by FUNCT_2:21
      .= F.x by A39,A36,A3;
  end;
  then
A40: F = CircleMap*H by FUNCT_2:113;
  for p being Point of [:Y,I[01]:], V being Subset of R^1
    st H.p in V & V is open holds
    ex W being Subset of [:Y,I[01]:] st p in W & W is open & H.:W c= V
  proof
    let p be Point of [:Y,I[01]:],
        V be Subset of R^1 such that
A41:  H.p in V and
A42:  V is open;
    consider p1 being Point of Y,
             p2 being Point of I[01] such that
A43:  p = [p1,p2] by BORSUK_1:50;
    H.p = f1.p2 by A38,A43;
    then consider W being Subset of I[01] such that
A44:  p2 in W and
A45:  W is open and
A46:  f1.:W c= V by A41,A42,A35,JGRAPH_2:20;
    take W1 = [:[#]Y,W:];
    thus p in W1 by A2,A43,A44,ZFMISC_1:106;
    thus W1 is open by A45,BORSUK_1:46;
    let y be set;
    assume y in H.:W1;
    then consider c being Element of [:Y,I[01]:] such that
A47:  c in W1 and
A48:  y = H.c by FUNCT_2:116;
    consider c1 being set,
             c2 being set such that
A49:  c1 in [#]Y and
A50:  c2 in W and
A51:  c = [c1,c2] by A47,ZFMISC_1:def 2;
A52: H.c = f1.c2 by A38,A49,A50,A51;
    f1.c2 in f1.:W by A50,FUNCT_2:43;
    hence y in V by A46,A48,A52;
  end;
  then
A53: H is continuous by JGRAPH_2:20;
  for i being Point of I[01] holds ft.i = f1.i
  proof
    let i be Point of I[01];
    dom H = the carrier of [:Y,I[01]:] by FUNCT_2:def 1;
    then
A54: dom (H | [:the carrier of Y,{0}:]) = [:the carrier of Y,{0}:]
    by A18,A19,RELAT_1:91;
A55: dom Ft = [:the carrier of Y,{0}:] by Lm19,A21,FUNCT_2:def 1;
   for x being set st x in dom (H | [:the carrier of Y,{0}:]) holds
      (H | [:the carrier of Y,{0}:]).x = Ft.x
    proof
      let x be set;
      assume
A56:     x in dom (H | [:the carrier of Y,{0}:]);
      consider x1, x2 being set such that
A57:     x1 in the carrier of Y and
A58:     x2 in {0} and
A59:     x = [x1,x2] by A56,A54,ZFMISC_1:def 2;
A60:   x2 = j0 by A58,TARSKI:def 1;
      thus (H | [:the carrier of Y,{0}:]).x = H.x by A56,FUNCT_1:70
        .= f1.x2 by A38,A57,A60,A59
        .= Ft.x by A57,A58,A59,A23,A60,A37;
    end;
    then
A61: H | [:the carrier of Y,{0}:] = Ft by A54,A55,FUNCT_1:9;
    thus ft.i = G.(j,i) by Def6
      .= G. [j,i] by BINOP_1:def 1
      .= H. [j,i] by A53,A40,A33,A61
      .= f1.i by A38;
  end;
  hence ft = f1 by FUNCT_2:113;
end;

theorem Th104:
  for x0, y0 being Point of Tunit_circle(2),
      P, Q being Path of x0,y0,
      F being Homotopy of P,Q,
      xt being Point of R^1 st P,Q are_homotopic & xt in (CircleMap)"{x0}
  ex yt being Point of R^1, Pt, Qt being Path of xt,yt,
     Ft being Homotopy of Pt,Qt st Pt,Qt are_homotopic & F = CircleMap*Ft &
     yt in (CircleMap)"{y0} &
     for F1 being Homotopy of Pt,Qt st F = CircleMap*F1 holds Ft = F1
proof 
  let x0, y0 be Point of TUC;
  let P, Q be Path of x0,y0;
  let F be Homotopy of P,Q;
  let xt be Point of R^1;
  assume
A1: P,Q are_homotopic;
  assume
A2: xt in (CircleMap)"{x0};
  then consider ft being map of I[01], R^1 such that
A3: ft.0 = xt and
A4: P = CircleMap*ft and
A5: ft is continuous and
    for f1 being map of I[01], R^1 st f1 is continuous & P = CircleMap*f1 &
        f1.0 = xt holds ft = f1 by Th103;
A6: F is continuous by A1,BORSUK_6:def 13;
  CircleMap.xt in {x0} by A2,FUNCT_2:46;
  then
A7: CircleMap.xt = x0 by TARSKI:def 1;
  defpred P[set,set,set] means $3 = ft.$1;
A8:for x being Element of I for y being Element of {0}
      ex z being Element of REAL st P[x,y,z];
  consider Ft being Function of [:I,{0}:], REAL such that
A9: for y being Element of I, i being Element of {0} holds
      P[y,i, Ft. [y,i]] from FUNCT_2:sch 7(A8);
A10: dom F = the carrier of [:I[01],I[01]:] by FUNCT_2:def 1;
  then
A11: [:I,{0}:] c= dom F by Lm5,Lm3,ZFMISC_1:118;
  then
A12: dom (F | [:I,{0}:]) = [:I,{0}:] by RELAT_1:91;
A13: dom Ft = [:I,{0}:] by FUNCT_2:def 1;
  rng Ft c= REAL;
  then
A14: dom (CircleMap*Ft) = dom Ft by Lm17,RELAT_1:46;
A15: the carrier of [:I[01],Sspace(0[01]):] =
    [:I, the carrier of Sspace(0[01]):] by BORSUK_1:def 5;
  then reconsider Ft as map of [:I[01],Sspace(0[01]):], R^1 by Lm19,TOPMETR:24;
  for p being Point of [:I[01],Sspace(0[01]):], V being Subset of R^1
    st Ft.p in V & V is open holds
    ex W being Subset of [:I[01],Sspace(0[01]):] st
    p in W & W is open & Ft.:W c= V
  proof
    let p be Point of [:I[01],Sspace(0[01]):],
        V be Subset of R^1 such that
A16:  Ft.p in V and
A17:  V is open;
    consider p1 being Point of I[01],
             p2 being Point of Sspace(0[01]) such that
A18:  p = [p1,p2] by A15,DOMAIN_1:9;
    P[p1,p2,Ft. [p1,p2]] by Lm19,A9;
    then consider W1 being Subset of I[01] such that
A19:  p1 in W1 and
A20:  W1 is open and
A21:  ft.:W1 c= V by A5,A16,A17,A18,JGRAPH_2:20;
    reconsider W1 as non empty Subset of I[01] by A19;
    take W = [:W1,[#]Sspace(0[01]):];
    thus p in W by Lm19,Lm20,A18,A19,ZFMISC_1:def 2;
    thus W is open by A20,BORSUK_1:46;
    let y be set;
    assume y in Ft.:W;
    then consider x being Element of [:I[01],Sspace(0[01]):] such that
A22:  x in W and
A23:  y = Ft.x by FUNCT_2:116;
    consider x1 being Element of W1,
             x2 being Point of Sspace(0[01]) such that
A24:  x = [x1,x2] by Lm19,A22,Lm20,DOMAIN_1:9;
A25: P[x1,x2,Ft. [x1,x2]] by Lm19,A9;
    ft.x1 in ft.:W1 by FUNCT_2:43;
    hence y in V by A21,A23,A24,A25;
  end;
  then
A26: Ft is continuous by JGRAPH_2:20;
  for x being set st x in dom (CircleMap*Ft) holds (F | [:I,{0}:]).x =
   (CircleMap*Ft).x
  proof
    let x be set such that
A27:   x in dom (CircleMap*Ft);
    consider x1, x2 being set such that
A28:   x1 in I and
A29:   x2 in {0} and
A30:   x = [x1,x2] by A27,Lm19,A15,ZFMISC_1:def 2;
A31: x2 = 0 by A29,TARSKI:def 1;
    thus (F | [:I,{0}:]).x = F.x by A27,Lm19,A15,FUNCT_1:72
      .= F.(x1,0) by A31,A30,BINOP_1:def 1
      .= (CircleMap*ft).x1 by A4,A1,A28,BORSUK_6:def 13
      .= CircleMap.(ft.x1) by A28,FUNCT_2:21
      .= CircleMap.(Ft.x) by A9,A28,A30,A29
      .= (CircleMap*Ft).x by A27,FUNCT_1:22;
  end;
  then F | [:I,{0}:] = CircleMap*Ft by A12,A14,A13,FUNCT_1:9;
  then consider G being map of [:I[01],I[01]:], R^1 such that
A32: G is continuous and
A33: F = CircleMap*G and
A34: G | [:I,{0}:] = Ft and
A35: for H being map of [:I[01],I[01]:], R^1 st H is continuous &
    F = CircleMap*H &
    H | [:the carrier of I[01],{0}:] = Ft holds G = H by A6,A26,Th102;
  take yt = ft.j1;
  reconsider ft as Path of xt,yt by A3,A5,BORSUK_2:def 4;
  set sM0 = Prj2(j0,G);
  set sM1 = Prj2(j1,G);
A36: [j0,j0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
A37: [j1,j0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
A38: sM0.0 = G.(j0,j0) by Def6
      .= G. [j0,j0] by BINOP_1:def 1
      .= Ft. [j0,j0] by A36,A34,FUNCT_1:72
      .= xt by A3,A9,Lm4;
  for H being continuous map of [:I[01],I[01]:], R^1 holds
     Prj2(j0,H) is continuous;
  then
A39: sM0 is continuous by A32;
A40: sM1.0 = G.(j1,j0) by Def6
      .= G. [j1,j0] by BINOP_1:def 1
      .= Ft. [j1,j0] by A37,A34,FUNCT_1:72
      .= yt by A9,Lm4;
  for H being continuous map of [:I[01],I[01]:], R^1 holds
    Prj2(j1,H) is continuous;
  then
A41: sM1 is continuous by A32;
  consider cP1 being constant Loop of x0;
  consider ft1 being map of I[01], R^1 such that
    ft1.0 = xt & cP1 = CircleMap*ft1 & ft1 is continuous and
A42: for f1 being map of I[01], R^1 st f1 is continuous & cP1 = CircleMap*f1 &
      f1.0 = xt holds ft1 = f1 by A2,Th103;
  for x being Point of I[01] holds cP1.x = (CircleMap*sM0).x
  proof
    let x be Point of I[01];
    thus (CircleMap*sM0).x = CircleMap.(sM0.x) by FUNCT_2:21
      .= CircleMap.(G.(j0,x)) by Def6
      .= (CircleMap*G).(j0,x) by Lm5,TOPALG_3:3
      .= x0 by A33,A1,BORSUK_6:def 13
      .= cP1.x by TOPALG_3:22;
  end;
  then cP1 = CircleMap*sM0 by FUNCT_2:113;
  then
A43: ft1 = sM0 by A38,A39,A42;
  set g1 = I[01] --> xt;
A44: g1.j0 = xt by TOPALG_3:5;
  for x being Point of I[01] holds cP1.x = (CircleMap*g1).x
  proof
    let x be Point of I[01];
    thus cP1.x = x0 by TOPALG_3:22
      .= CircleMap.(g1.x) by A7,TOPALG_3:5
      .= (CircleMap*g1).x by FUNCT_2:21;
  end;
  then cP1 = CircleMap*g1 by FUNCT_2:113;
  then
A45: ft1 = g1 by A44,A42;
  consider cP2 being constant Loop of y0;
A46: CircleMap.yt = P.j1 by A4,FUNCT_2:21
    .= y0 by BORSUK_2:def 4;
  then
A47: CircleMap.yt in {y0} by TARSKI:def 1;
  then yt in (CircleMap)"{y0} by FUNCT_2:46;
  then consider ft2 being map of I[01], R^1 such that
    ft2.0 = yt & cP2 = CircleMap*ft2 & ft2 is continuous and
A48: for f1 being map of I[01], R^1 st f1 is continuous & cP2 = CircleMap*f1 &
      f1.0 = yt holds ft2 = f1 by Th103;
  for x being Point of I[01] holds cP2.x = (CircleMap*sM1).x
  proof
    let x be Point of I[01];
    thus (CircleMap*sM1).x = CircleMap.(sM1.x) by FUNCT_2:21
      .= CircleMap.(G.(j1,x)) by Def6
      .= (CircleMap*G).(j1,x) by Lm5,TOPALG_3:3
      .= y0 by A33,A1,BORSUK_6:def 13
      .= cP2.x by TOPALG_3:22;
  end;
  then cP2 = CircleMap*sM1 by FUNCT_2:113;
  then
A49: ft2 = sM1 by A40,A41,A48;
  set g2 = I[01] --> yt;
A50: g2.j0 = yt by TOPALG_3:5;
  for x being Point of I[01] holds cP2.x = (CircleMap*g2).x
  proof
    let x be Point of I[01];
    thus cP2.x = y0 by TOPALG_3:22
      .= CircleMap.(g2.x) by A46,TOPALG_3:5
      .= (CircleMap*g2).x by FUNCT_2:21;
  end;
  then cP2 = CircleMap*g2 by FUNCT_2:113;
  then
A51: ft2 = g2 by A50,A48;
  set Qt = Prj1(j1,G);
  Qt is Path of xt,yt
  proof
    for H being continuous map of [:I[01],I[01]:], R^1 holds
      Prj1(j1,H) is continuous;
    hence Qt is continuous by A32;
    thus Qt.0 = G.(j0,j1) by Def5
      .= sM0.j1 by Def6
      .= xt by A45,A43,TOPALG_3:5;
    thus Qt.1 = G.(j1,j1) by Def5
      .= sM1.j1 by Def6
      .= yt by A51,A49,TOPALG_3:5;
  end;
  then reconsider Qt as Path of xt,yt;
  take ft, Qt;
A52: now
    let s be Point of I[01];
A53: [s,0] in [:I,{0}:] by Lm4,ZFMISC_1:106;
    thus G.(s,0) = G. [s,0] by BINOP_1:def 1
      .= Ft. [s,j0] by A34,A53,FUNCT_1:72
      .= ft.s by A9,Lm4;
    thus G.(s,1) = Qt.s by Def5;
    let t be Point of I[01];
    thus G.(0,t) = sM0.t by Def6
      .= xt by A45,A43,TOPALG_3:5;
    thus G.(1,t) = sM1.t by Def6
      .= yt by A51,A49,TOPALG_3:5;
  end;
  G is Homotopy of ft,Qt
  proof
    thus ft,Qt are_homotopic by A32,A52,BORSUK_2:def 7;
    thus thesis by A32,A52;
  end;
  then reconsider G as Homotopy of ft,Qt;
  take G;
  thus
A54: ft,Qt are_homotopic by A32,A52,BORSUK_2:def 7;
  thus F = CircleMap*G by A33;
  thus yt in (CircleMap)"{y0} by A47,FUNCT_2:46;
  let F1 be Homotopy of ft,Qt such that
A55: F = CircleMap*F1;
   dom F1 = the carrier of [:I[01],I[01]:] by FUNCT_2:def 1;
   then
A56: dom (F1 | [:I,{0}:]) = [:I,{0}:] by A11,A10,RELAT_1:91;
  for x being set st x in dom (F1 | [:I,{0}:]) holds
    (F1 | [:I,{0}:]).x = Ft.x
  proof
    let x be set;
    assume
A57:   x in dom (F1 | [:I,{0}:]);
    consider x1, x2 being set such that
A58:   x1 in I and
A59:   x2 in {0} and
A60:   x = [x1,x2] by A57,A56,ZFMISC_1:def 2;
A61: x2 = 0 by A59,TARSKI:def 1;
    thus (F1 | [:I,{0}:]).x = F1.x by A57,FUNCT_1:70
      .= F1.(x1,x2) by A60,BINOP_1:def 1
      .= ft.x1 by A54,A58,A61,BORSUK_6:def 13
      .= Ft.x by A58,A59,A60,A9;
  end;
  then F1 | [:I,{0}:] = Ft by A56,A13,FUNCT_1:9;
  hence G = F1 by A55,A35;
end;

definition
  func Ciso -> map of INT.Group, pi_1(Tunit_circle(2),c[10]) means :Def8:
  for n being Integer holds
   it.n = Class(EqRel(Tunit_circle(2),c[10]),cLoop(n));
existence
proof 
  defpred P[Integer,set] means
    $2 = Class(EqRel(TUC,c[10]),cLoop($1));
A1: for x being Element of INT
   ex y being Element of the carrier of pi_1(TUC,c[10]) st P[x,y]
  proof
    let x be Element of INT;
    reconsider y = Class(EqRel(TUC,c[10]),cLoop(x))
     as Element of the carrier of pi_1(TUC,c[10]) by TOPALG_1:48;
    take y;
    thus P[x,y];
  end;
  consider f being Function of INT,the carrier of pi_1(TUC,c[10])
   such that
A2: for x being Element of INT holds P[x,f.x] from FUNCT_2:sch 3(A1);
  reconsider f as map of INT.Group, pi_1(TUC,c[10]) by GR_CY_1:def 4;
  take f;
  let n be Integer;
  n in INT by INT_1:def 2;
  hence thesis by A2;
end;
uniqueness
proof 
  let f, g be map of INT.Group, pi_1(TUC,c[10]) such that
A3: for n being Integer holds f.n = Class(EqRel(TUC,c[10]),cLoop(n))
   and
A4: for n being Integer holds g.n = Class(EqRel(TUC,c[10]),cLoop(n));
  for x being Element of the carrier of INT.Group holds f.x = g.x
  proof
    let x be Element of the carrier of INT.Group;
    thus f.x = Class(EqRel(TUC,c[10]),cLoop(x)) by A3
      .= g.x by A4;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

theorem Th105:
  for i being Integer, f being Path of R^1(0),R^1(i) holds
  Ciso.i = Class(EqRel(Tunit_circle(2),c[10]),CircleMap*f)
proof
  let i be Integer;
  let f be Path of R^1(0),R^1(i);
  set P = CircleMap*f;
  P is Loop of c[10]
  proof
    thus P is continuous;
    thus P.0 = CircleMap.(f.j0) by FUNCT_2:21
      .= CircleMap.R^1(0) by BORSUK_2:def 4
      .= CircleMap.0 by TOPREALB:def 2
      .= |[ cos(2*PI*0), sin(2*PI*0) ]| by TOPREALB:def 11
      .= c[10] by TOPREALB:def 8,SIN_COS:34;
    thus P.1 = CircleMap.(f.j1) by FUNCT_2:21
      .= CircleMap.R^1(i) by BORSUK_2:def 4
      .= CircleMap.i by TOPREALB:def 2
      .= |[ cos(2*PI*i+0), sin(2*PI*i+0) ]| by TOPREALB:def 11
      .= |[ cos(0), sin(2*PI*i+0) ]| by COMPLEX2:10
      .= c[10] by TOPREALB:def 8,COMPLEX2:9,SIN_COS:34;
  end;
  then reconsider P as Loop of c[10];
A1: cLoop(i) = CircleMap * ExtendInt(i) by Th100;
A2: cLoop(i),P are_homotopic
  proof
    reconsider J = R^1 as non empty convex SubSpace of R^1;
    reconsider r0 = R^1(0), ri = R^1(i) as Point of J;
    reconsider O = ExtendInt(i), ff = f as Path of r0,ri;
    reconsider G = R1Homotopy(O,ff) as map of [:I[01],I[01]:],R^1;
    take F = CircleMap*G;
    thus F is continuous by TOPS_2:58;
    let s be Point of I[01];
    thus F.(s,0) = CircleMap.(G.(s,j0)) by Lm5,TOPALG_3:3
      .= CircleMap.((1-j0) * (ExtendInt(i)).s + j0 * f.s) by TOPALG_2:def 5
      .= (cLoop(i)).s by A1,FUNCT_2:21;
    thus F.(s,1) = CircleMap.(G.(s,j1)) by Lm5,TOPALG_3:3
      .= CircleMap.((1-j1) * (ExtendInt(i)).s + j1 * f.s) by TOPALG_2:def 5
      .= P.s by FUNCT_2:21;
    let t be Point of I[01];
    thus F.(0,t) = CircleMap.(G.(j0,t)) by Lm5,TOPALG_3:3
      .= CircleMap.((1-t) * (ExtendInt(i)).j0 + t * f.j0) by TOPALG_2:def 5
      .= CircleMap.((1-t) * R^1(0) + t * f.j0) by BORSUK_2:def 4
      .= CircleMap.((1-t) * R^1(0) + t * R^1(0)) by BORSUK_2:def 4
      .= CircleMap.((1-t) * 0 + t * 0) by TOPREALB:def 2
      .= c[10] by TOPREALB:33;
    thus F.(1,t) = CircleMap.(G.(j1,t)) by Lm5,TOPALG_3:3
      .= CircleMap.((1-t) * (ExtendInt(i)).j1 + t * f.j1) by TOPALG_2:def 5
      .= CircleMap.((1-t) * R^1(i) + t * f.j1) by BORSUK_2:def 4
      .= CircleMap.((1-t) * R^1(i) + t * R^1(i)) by BORSUK_2:def 4
      .= CircleMap.i by TOPREALB:def 2
      .= c[10] by TOPREALB:33;
  end;
  thus Ciso.i = Class(EqRel(TUC,c[10]),cLoop(i)) by Def8
    .= Class(EqRel(TUC,c[10]),CircleMap*f) by A2,TOPALG_1:47;
end;

definition
  redefine func Ciso -> Homomorphism of INT.Group, pi_1(Tunit_circle(2),c[10]);
coherence
proof 
  set f = Ciso;
  let x, y be Element of INT.Group;
  consider fX, fY being Loop of c[10] such that
A1:  f.x = Class(EqRel(TUC,c[10]),fX) and
A2:  f.y = Class(EqRel(TUC,c[10]),fY) and
A3:  (the mult of pi_1(TUC,c[10])).(f.x,f.y) =
   Class(EqRel(TUC,c[10]),fX+fY) by TOPALG_1:def 3;
  reconsider tx = AffineMap(1,x) as map of R^1,R^1 by TOPMETR:24;
  set p = tx*ExtendInt(y);
  p is Path of R^1(x),R^1(x+y)
  proof
    x is Real by XREAL_0:def 1;
    then tx is_homeomorphism by JORDAN16:35;
    then tx is continuous by TOPS_2:def 5;
    hence p is continuous by TOPS_2:58;
    thus p.0 = tx.((ExtendInt(y)).j0) by FUNCT_2:21
      .= tx.(y*j0) by Def4
      .= 1*0+x by JORDAN16:def 3
      .= R^1(x) by TOPREALB:def 2;
    thus p.1 = tx.((ExtendInt(y)).j1) by FUNCT_2:21
      .= tx.(y*j1) by Def4
      .= 1*y+x by JORDAN16:def 3
      .= R^1(x+y) by TOPREALB:def 2;
  end;
  then reconsider p as Path of R^1(x),R^1(x+y);
  f.x = Class(EqRel(TUC,c[10]),cLoop(x)) by Def8;
  then
A4: fX,cLoop(x) are_homotopic by A1,TOPALG_1:47;
  f.y = Class(EqRel(TUC,c[10]),cLoop(y)) by Def8;
  then fY,cLoop(y) are_homotopic by A2,TOPALG_1:47;
  then
A5: fX+fY,cLoop(x)+cLoop(y) are_homotopic by A4,BORSUK_6:84;
A6: for a being Point of I[01] holds
     (CircleMap * (ExtendInt(x)+p)).a = (cLoop(x)+cLoop(y)).a
  proof
    let a be Point of I[01];
    per cases;
    suppose
A7:   a <= 1/2;
    then
A8: 2*a is Point of I[01] by BORSUK_6:6;
    thus (CircleMap * (ExtendInt(x)+p)).a = CircleMap.((ExtendInt(x)+p).a)
    by FUNCT_2:21
      .= CircleMap.((ExtendInt(x)).(2*a)) by A7,BORSUK_6:def 4
      .= CircleMap.(x*(2*a)) by A8,Def4
      .= |[ cos(2*PI*(x*(2*a))), sin(2*PI*(x*(2*a))) ]| by TOPREALB:def 11
      .= |[ cos(2*PI*x*(2*a)), sin(2*PI*x*(2*a)) ]|
      .= (cLoop(x)).(2*a) by A8,Def7
      .= (cLoop(x)+cLoop(y)).a by A7,BORSUK_6:def 4;
    end;
    suppose
A9:   1/2 <= a;
    then
A10: 2*a-1 is Point of I[01] by BORSUK_6:7;
    thus (CircleMap * (ExtendInt(x)+p)).a = CircleMap.((ExtendInt(x)+p).a)
    by FUNCT_2:21
      .= CircleMap.(p.(2*a-1)) by A9,BORSUK_6:def 4
      .= CircleMap.(tx.((ExtendInt(y)).(2*a-1))) by A10,FUNCT_2:21
      .= CircleMap.(tx.(y*(2*a-1))) by A10,Def4
      .= CircleMap.(1*(y*(2*a-1))+x) by JORDAN16:def 3
      .= |[ cos(2*PI*((y*(2*a-1))+x)), sin(2*PI*((y*(2*a-1))+x)) ]|
      by TOPREALB:def 11
      .= |[ cos(2*PI*(y*(2*a-1))), sin(2*PI*(y*(2*a-1))+2*PI*x) ]|
         by COMPLEX2:10
      .= |[ cos(2*PI*y*(2*a-1)), sin(2*PI*y*(2*a-1)) ]| by COMPLEX2:9
      .= (cLoop(y)).(2*a-1) by A10,Def7
      .= (cLoop(x)+cLoop(y)).a by A9,BORSUK_6:def 4;
    end;
  end;
  thus f.(x*y) = f.(addint.(x,y)) by GR_CY_1:def 4,GROUP_1:def 1
    .= f.(x+y) by GR_CY_1:14
    .= Class(EqRel(TUC,c[10]),CircleMap*(ExtendInt(x)+p)) by Th105
    .= Class(EqRel(TUC,c[10]),cLoop(x)+cLoop(y)) by A6,FUNCT_2:113
    .= Class(EqRel(TUC,c[10]),fX+fY) by A5,TOPALG_1:47
    .= f.x * f.y by A3,GROUP_1:def 1;
end;
end;

registration
  cluster Ciso -> one-to-one onto;
coherence
proof 
A1: dom Ciso = INT by FUNCT_2:def 1,GR_CY_1:def 4;
  thus Ciso is one-to-one
  proof
    let m, n be set;
    assume that
A2:   m in dom Ciso & n in dom Ciso and
A3:   Ciso.m = Ciso.n;
    reconsider m, n as Integer by A2,INT_1:def 2,GR_CY_1:def 4;
    Ciso.m = Class(EqRel(TUC,c[10]),cLoop(m)) &
    Ciso.n = Class(EqRel(TUC,c[10]),cLoop(n)) by Def8;
    then
A4: cLoop(m),cLoop(n) are_homotopic by A3,TOPALG_1:47;
    then consider F being map of [:I[01],I[01]:], TUC such that
A5:   F is continuous and
A6:   for s being Point of I[01] holds
         F.(s,0) = (cLoop(m)).s & F.(s,1) = (cLoop(n)).s &
      for t being Point of I[01] holds F.(0,t) = c[10] & F.(1,t) = c[10]
        by BORSUK_2:def 7;
A7: F is Homotopy of cLoop(m),cLoop(n) by A5,A6,A4,BORSUK_6:def 13;
    set xt = R^1(0);
A8: xt in (CircleMap)"{c[10]} by TOPREALB:def 2,Lm1,TOPREALB:34;
    then consider yt being Point of R^1,
             Pt, Qt being Path of xt,yt,
             Ft being Homotopy of Pt,Qt such that
A9:  Pt,Qt are_homotopic and
A10:  F = CircleMap*Ft and
     yt in (CircleMap)"{c[10]} and
     for F1 being Homotopy of Pt,Qt st F = CircleMap*F1 holds Ft = F1
       by A4,A7,Th104;
    consider ftm being map of I[01], R^1 such that
     ftm.0 = xt and
     cLoop(m) = CircleMap*ftm and
     ftm is continuous and
A11:  for f1 being map of I[01], R^1 st f1 is continuous &
     cLoop(m) = CircleMap*f1 & f1.0 = xt holds ftm = f1 by A8,Th103;
    consider ftn being map of I[01], R^1 such that
     ftn.0 = xt and
     cLoop(n) = CircleMap*ftn and
     ftn is continuous and
A12:  for f1 being map of I[01], R^1 st f1 is continuous &
     cLoop(n) = CircleMap*f1 & f1.0 = xt holds ftn = f1 by A8,Th103;
    set ft0 = Prj1(j0,Ft);
    set ft1 = Prj1(j1,Ft);
A13: (ExtendInt(m)).0 = m*j0 by Def4
      .= xt by TOPREALB:def 2;
A14: ft0.0 = Ft.(j0,j0) by Def5
      .= xt by A9,BORSUK_6:def 13;
    cLoop(m) = CircleMap*ExtendInt(m) by Th100;
    then
A15: ExtendInt(m) = ftm by A11,A13;
    now
      let x be Point of I[01];
      thus (CircleMap*ft0).x = CircleMap.(ft0.x) by FUNCT_2:21
        .= CircleMap.(Ft.(x,j0)) by Def5
        .= F.(x,j0) by A10,Lm5,TOPALG_3:3
        .= (cLoop(m)).x by A6;
    end;
    then cLoop(m) = CircleMap*ft0 by FUNCT_2:113;
    then ft0 = ftm by A11,A14;
    then
A16: ft0.j1 = m*1 by A15,Def4;
A17: (ExtendInt(n)).0 = n*j0 by Def4
      .= xt by TOPREALB:def 2;
    cLoop(n) = CircleMap*ExtendInt(n) by Th100;
    then
A18: ExtendInt(n) = ftn by A12,A17;
A19: ft1.0 = Ft.(j0,j1) by Def5
      .= xt by A9,BORSUK_6:def 13;
    now
      let x be Point of I[01];
      thus (CircleMap*ft1).x = CircleMap.(ft1.x) by FUNCT_2:21
        .= CircleMap.(Ft.(x,j1)) by Def5
        .= F.(x,j1) by A10,Lm5,TOPALG_3:3
        .= (cLoop(n)).x by A6;
    end;
    then cLoop(n) = CircleMap*ft1 by FUNCT_2:113;
    then ft1 = ftn by A12,A19;
    then
A20: ft1.j1 = n*1 by A18,Def4;
    ft0.j1 = Ft.(j1,j0) by Def5
      .= yt by A9,BORSUK_6:def 13
      .= Ft.(j1,j1) by A9,BORSUK_6:def 13
      .= ft1.j1 by Def5;
    hence thesis by A16,A20;
  end;
  thus rng Ciso c= the carrier of pi_1(TUC,c[10]);
  let q be set;
  assume q in the carrier of pi_1(TUC,c[10]);
  then consider f being Loop of c[10] such that
A21: q = Class(EqRel(TUC,c[10]),f) by TOPALG_1:48;
  R^1(0) in (CircleMap)"{c[10]} by TOPREALB:def 2,Lm1,TOPREALB:34;
  then consider ft being map of I[01], R^1 such that
A22: ft.0 = R^1(0) and
A23: f = CircleMap*ft and
A24: ft is continuous and
    for f1 being map of I[01], R^1 st f1 is continuous & f = CircleMap*f1 &
      f1.0 = R^1(0) holds ft = f1 by Th103;
A25: ft.j1 in dom CircleMap by Lm17,TOPMETR:24;
   CircleMap.(ft.j1) = (CircleMap*ft).j1 by FUNCT_2:21
     .= c[10] by A23,BORSUK_2:def 4;
   then CircleMap.(ft.1) in {c[10]} by TARSKI:def 1;
   then
A26: ft.1 in INT by A25,TOPREALB:34,FUNCT_1:def 13;
   then
A27: ft.1 is Integer by INT_1:def 2;
  ft is Path of R^1(0),R^1(ft.1)
  proof
    thus ft is continuous by A24;
    thus ft.0 = R^1(0) by A22;
    thus ft.1 = R^1(ft.1) by TOPREALB:def 2;
  end;
  then Ciso.(ft.1) = Class(EqRel(TUC,c[10]),CircleMap*ft) by A27,Th105;
  hence q in rng Ciso by A26,A1,A21,A23,FUNCT_1:def 5;
end;
end;

theorem Th106:
  Ciso is being_isomorphism
proof 
  thus rng Ciso = the carrier of pi_1(TUC,c[10]) by FUNCT_2:def 3;
  thus Ciso is one-to-one;
end;

Lm21:
  for r being positive (real number),
      o being Point of TOP-REAL 2, x being Point of Tcircle(o,r) holds
   INT.Group,pi_1(Tcircle(o,r),x) are_isomorphic
proof 
  let r be positive (real number),
      o be Point of TOP-REAL 2,
      x be Point of Tcircle(o,r);
  TUC = Tcircle(0.REAL 2,1) by TOPREALB:def 7;
  then Tcircle(o,r),TUC are_homeomorphic by TOPREALB:20;
  then pi_1(TUC,c[10]),pi_1(Tcircle(o,r),x) are_isomorphic
    by TOPALG_3:35;
  then consider h being Homomorphism of pi_1(TUC,c[10]),
    pi_1(Tcircle(o,r),x) such that
A1: h is_isomorphism by GROUP_6:def 15;
  take h*Ciso;
  thus thesis by A1,Th106,GROUP_6:74;
end;

theorem Th107:
  for S being being_simple_closed_curve SubSpace of TOP-REAL 2,
      x being Point of S holds
   INT.Group, pi_1(S,x) are_isomorphic
proof 
  let S be being_simple_closed_curve SubSpace of TOP-REAL 2,
      x be Point of S;
  consider r being positive (real number),
           o being Point of TOP-REAL 2,
           y being Point of Tcircle(o,r);
A1: INT.Group,pi_1(Tcircle(o,r),y) are_isomorphic by Lm21;
  Tcircle(o,r), S are_homeomorphic by TOPREALB:11;
  then pi_1(Tcircle(o,r),y), pi_1(S,x) are_isomorphic by TOPALG_3:35;
  hence thesis by A1,GROUP_6:78;
end;

registration
  let S be being_simple_closed_curve SubSpace of TOP-REAL 2,
      x be Point of S;
  cluster pi_1(S,x) -> infinite;
coherence
proof 
  INT.Group, pi_1(S,x) are_isomorphic by Th107;
  hence thesis by GROUP_6:85;
end;
end;
