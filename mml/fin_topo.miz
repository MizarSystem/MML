:: Finite Topological Spaces. Finite Topology Concepts and Neighbourhoods
::  by Hiroshi Imura and Masayoshi Eguchi
::
:: Received November 27, 1992
:: Copyright (c) 1992 Association of Mizar Users

environ

 vocabularies FINSEQ_1, FINSET_1, BOOLE, FUNCT_1, RELAT_1, ABSVALUE, ARYTM_1,
      CAT_1, FUNCT_2, SUBSET_1, PRE_TOPC, TARSKI, RELAT_2, SETFAM_1, CARD_1,
      FIN_TOPO, HAHNBAN, FINSEQ_4, ORDERS_1, NAT_1, EQREL_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0,
      FINSET_1, NAT_1, SETFAM_1, RELAT_1, EQREL_1, FUNCT_1, FUNCT_2, FUNCOP_1,
      DOMAIN_1, RELSET_1, FINSEQ_1, FINSEQ_4, CARD_1, INT_2, XXREAL_0,
      STRUCT_0, ORDERS_2;
 constructors SETFAM_1, DOMAIN_1, FUNCOP_1, XXREAL_0, REAL_1, NAT_1, INT_2,
      EQREL_1, FINSEQ_4, ORDERS_2;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, FUNCOP_1, ARYTM_3,
      XXREAL_0, XREAL_0, NAT_1, INT_1, FINSEQ_1, STRUCT_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, STRUCT_0, ORDINAL1, XBOOLE_0, SUBSET_1, RELAT_2, ORDERS_2,
      RELAT_1, FUNCOP_1;
 theorems NAT_1, TARSKI, FUNCOP_1, SUBSET_1, ZFMISC_1, FINSET_1, INT_1, CARD_1,
      FINSEQ_4, FUNCT_1, FUNCT_2, FINSEQ_1, ABSVALUE, RELSET_1,
      XBOOLE_0, XBOOLE_1, MEASURE2, XREAL_1, XXREAL_0, ORDINAL1, RELAT_1,
      EQREL_1, ORDERS_2;
 schemes RECDEF_1, NAT_1, FINSEQ_2, DOMAIN_1;

begin

theorem Th1:
  for A being set,
      f being FinSequence of bool A st
    (for i being Element of NAT st 1 <= i & i < len f holds f/.i c= f/.(i+1))
    for i, j being Element of NAT st
      i <= j & 1 <= i & j <= len f holds f/.i c= f/.j
proof
  let A be set;
  let f be FinSequence of bool A such that
A1:for i being Element of NAT st 1 <= i & i < len f holds f/.i c= f/.(i+1);
  let i, j be Element of NAT such that
    A2:i <= j & 1 <= i & j <= len f;
  defpred P[Element of NAT] means i+$1 <= j implies f/.i c= f/.(i+$1);
  A3:P[0];
  A4:now let k be Element of NAT;
    A5:i+k+1 = i+(k+1);
    assume A6:P[k];
    thus P[k+1]
    proof
    assume A7: i+(k+1) <= j;
    then A8:i+k < j by A5,NAT_1:13;
     i+k >= i by NAT_1:11;
    then i+k >= 1 & i+k < len f by A2,A8,XXREAL_0:2;
    then f/.(i+k) c= f/.(i+(k+1)) by A1,A5;
    hence f/.i c= f/.(i+(k+1)) by A5,A6,A7,NAT_1:13,XBOOLE_1:1;
    end;
  end;
  A9:for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A4);
  consider k be Nat such that
A10:i+k = j by A2,NAT_1:10;
   k in NAT by ORDINAL1:def 13;
  hence thesis by A9,A10;
end;

theorem Th2:
  for A being set,
      f being FinSequence of bool A st
    (for i being Element of NAT st 1 <= i & i < len f holds f/.i c= f/.(i+1))
    for i, j being Element of NAT st
      i < j & 1 <= i & j <= len f & f/.j c= f/.i
    for k being Element of NAT st i <= k & k <= j holds f/.j = f/.k
proof
  let A be set;
  let f be FinSequence of bool A such that
A1:for i being Element of NAT st 1 <= i & i < len f holds f/.i c= f/.(i+1);
  let i, j be Element of NAT; assume
A2:i < j & 1 <= i & j <= len f & f/.j c= f/.i;
  defpred P[Element of NAT] means i+$1 <= j implies f/.j c= f/.(i+$1);
  A3:P[0] by A2;
  A4:now let k be Element of NAT;
    A5:i+k+1 = i+(k+1);
    assume A6:P[k];
    thus P[k+1]
    proof
    assume A7: i+(k+1) <= j;
    then A8:i+k < j by A5,NAT_1:13;
        i+k >= i by NAT_1:11;
    then i+k >= 1 & i+k < len f by A2,A8,XXREAL_0:2;
    then f/.(i+k) c= f/.(i+k+1) by A1;
    hence f/.j c= f/.(i+(k+1)) by A6,A7,NAT_1:13,XBOOLE_1:1;
    end;
  end;
  A9:for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A4);
  let k be Element of NAT; assume A10:i <= k & k <= j;
  then consider m be Nat such that
    A11:k = i + m by NAT_1:10;
    reconsider m as Element of NAT by ORDINAL1:def 13;
     k=i+m by A11;
  then A12:f/.j c= f/.k by A9,A10;
      1 <= k by A2,A10,XXREAL_0:2;
  then f/.k c= f/.j by A1,A2,A10,Th1;
  hence thesis by A12,XBOOLE_0:def 10;
end;

scheme MaxFinSeqEx {X() -> non empty set,
                    A,B() -> Subset of X(),
                    F(Subset of X()) -> Subset of X()}:
  ex f being FinSequence of bool X() st
    len f > 0 &
    f/.1=B() &
    (for i being Element of NAT st i > 0 & i < len f holds f/.(i+1)=F(f/.i)) &
    F(f/.len f)=f/.len f &
    (for i, j being Element of NAT st i > 0 & i < j & j <= len f holds
      f/.i c= A() & f/.i c< f/.j)
  provided
    A1: A() is finite and
    A2: B() c= A() and
    A3: for A being Subset of X() st A c= A() holds A c= F(A) & F(A) c= A()
proof
  deffunc F(Element of NAT,Subset of X()) = F($2);
  consider f being Function of NAT,bool X() such that
    A4:f.0 = B() and
A5:for n being Element of NAT holds f.(n+1) = F(n,f.n) from RECDEF_1:sch 4;
    defpred P[Element of NAT] means f.$1 c= A();
  A6:P[0] by A2,A4;
  A7:now
    let n be Element of NAT such that A8:P[n];
        f.(n+1) = F(f.n) by A5;
    hence P[n+1] by A3,A8;
  end;
  A9:for n being Element of NAT holds P[n] from NAT_1:sch 1(A6,A7);
  A10:rng f c= bool A()
    proof
      let x be set; assume x in rng f;
      then x in f.:NAT by FUNCT_2:45;
      then ex k being Element of NAT st k in NAT & f.k=x by FUNCT_2:116;
      then x c= A() by A9;
      hence x in bool A();
    end;
      bool A() is finite by A1,FINSET_1:24;
  then A11:rng f is finite by A10,FINSET_1:13;
  A12:dom f = NAT by FUNCT_2:def 1;
  then A13:rng f <> {} by A4,FUNCT_1:def 5;
  A14:for i being Element of NAT holds f.i c= f.(i+1)
    proof
      let i be Element of NAT;
      A15:f.(i+1) = F(f.i) by A5;
       f.i c= A() by A9;
      hence f.i c= f.(i+1) by A3,A15;
    end;
      rng f is c=-linear
    proof
      let B,C be set;
      assume B in rng f;
      then consider i being set such that
  A16:i in NAT & B = f.i by A12,FUNCT_1:def 5;
      assume C in rng f;
      then consider j being set such that
  A17:j in NAT & C = f.j by A12,FUNCT_1:def 5;
      reconsider i as Element of NAT by A16;
      reconsider j as Element of NAT by A17;
        i <= j or j <= i;
      hence B c= C or C c= B by A14,A16,A17,MEASURE2:22;
    end;
  then consider m being set such that
    A18:m in rng f and
    A19:for C being set st C in rng f holds C c= m
      by A11,A13,FINSET_1:31;
    defpred P[Element of NAT] means $1 in NAT & f.$1=m;
      m in f.:NAT by A18,FUNCT_2:45;
  then A20:ex k being Element of NAT st P[k] by FUNCT_2:116;
  consider k being Element of NAT such that
    A21:P[k] and
    A22:for n being Element of NAT st P[n] holds k <= n from NAT_1:sch 5(A20);
A23:  k+1 in Seg(k+1) by FINSEQ_1:6;
  deffunc G(Element of NAT) = f.(abs($1-1));
  consider z being FinSequence of bool X() such that
A24:  len z = k+1 and
A25:  for j being Element of NAT st j in Seg(k+1) holds
        z.j = G(j) from FINSEQ_2:sch 1;
  take z;
  thus 0 < len z by A24;
  A26: 0+1 <= len z by NAT_1:13,A24;
  then A27:1 in Seg(k+1) by A24,FINSEQ_1:3;
  thus z/.1 = z.1 by A26,FINSEQ_4:24
               .= f.(abs(1-1)) by A25,A27
               .= B() by A4,ABSVALUE:7;
  thus A28:for i being Element of NAT st i > 0 & i < len z
   holds z/.(i+1)=F(z/.i)
    proof
      let i be Element of NAT; assume
      A29:i > 0 & i < len z;
      then A30: 0+1 <= i & i <= k+1 by A24,NAT_1:13;
      then A31:i in Seg(k+1) by FINSEQ_1:3;
      then A32:z.i = f.(abs(i-1)) by A25;
       0+1 < i+1 by A29,XREAL_1:8;
      then A33:1 <= i+1 & i+1 <= k+1 by A24,A29,NAT_1:13;
      then A34:i+1 in Seg(k+1) by FINSEQ_1:3;
       1-1 <= i-1 by A30,XREAL_1:11;
      then A35:0 <= (i-1)*1;
A36:   i in dom z by A24,A31,FINSEQ_1:def 3;
      thus z/.(i+1) = z.(i+1) by A24,A33,FINSEQ_4:24
                   .= f.(abs(i+1-1)) by A25,A34
                   .= f.(abs(i-1+1))
                   .= f.(abs(i-1)+abs(1)) by A35,ABSVALUE:24
                   .= f.(abs(i-1)+1) by ABSVALUE:def 1
                   .= F(f.(abs(i-1))) by A5
                   .= F(z/.i) by A32,A36,FINSEQ_4:def 4;
    end;
  thus F(z/.len z)=z/.len z
    proof
      A37:f.k c= f.(k+1) by A14;
       k+1 in NAT;
      then k+1 in dom f by FUNCT_2:def 1;
      then f.(k+1) in rng f by FUNCT_1:def 5;
      then A38: f.(k+1) c= f.k by A19,A21;
A39:   len z = 0 or len z in Seg(len z) by FINSEQ_1:5;
then A40:   len z in dom z by A24,FINSEQ_1:def 3;
A41:  z.len z = f.(abs(k+1-1)) by A24,A25,A39
                        .= f.k by ABSVALUE:def 1;
      hence F(z/.len z) = F(f.k) by A40,FINSEQ_4:def 4
                         .= f.(k+1) by A5
                         .= z.len z by A37,A38,A41,XBOOLE_0:def 10
                         .= z/.len z by A40,FINSEQ_4:def 4;
    end;
  let i, j be Element of NAT; assume A42:0 < i & i < j & j <= len z;
  then A43: 0+1 <= i by NAT_1:13;
  A44:i <= len z by A42,XXREAL_0:2;
  A45:i < len z by A42,XXREAL_0:2;
  reconsider l = i-1 as Element of NAT by A43,INT_1:18;
  A46:i in Seg(k+1) by A24,A43,A44,FINSEQ_1:3;
  A47:z/.i = z.i by A43,A44,FINSEQ_4:24
            .= f.(abs(i-1)) by A25,A46
            .= f.l by ABSVALUE:def 1;
  hence z/.i c= A() by A9;
  A48:for i being Element of NAT st 1 <= i & i < len z holds z/.i c= z/.(i+1)
    proof
      let i be Element of NAT; assume
      A49:1 <= i & i < len z;
      then A50:i in Seg(k+1) by A24,FINSEQ_1:3;
A51:  1-1 <= i-1 by A49,XREAL_1:11;
      A52:z/.i = z.i by A49,FINSEQ_4:24
                  .= f.(abs(i-1)) by A25,A50
                  .= f.(i-1) by A51,ABSVALUE:def 1;
      A53:1 <= i+1 & i+1 <= len z by A49,NAT_1:13;
      then A54:i+1 in Seg(k+1) by A24,FINSEQ_1:3;
      A55:z/.(i+1) = z.(i+1) by A53,FINSEQ_4:24
                    .= f.(abs(i+1-1)) by A25,A54
                    .= f.(i-1+1) by ABSVALUE:def 1;
          i-1 is Element of NAT by A51,INT_1:16;
      hence z/.i c= z/.(i+1) by A14,A52,A55;
    end;
  hence z/.i c= z/.j by A42,A43,Th1;
  assume A56: z/.i = z/.j;
      i <= i+1 & i+1 <= j by A42,NAT_1:13;
  then A57:z/.i = z/.(i+1) by A42,A43,A48,A56,Th2
             .= F(z/.i) by A28,A42,A45;
  defpred P[Element of NAT] means i+$1 <= len z implies z/.i = z/.(i+$1);
  A58:P[0];
  A59:now
      let n be Element of NAT such that A60:P[n];
      thus P[n+1]
      proof
  A61:i+n > 0 by A42,NAT_1:11;
      assume i+(n+1) <= len z;
      then i+n+1 <= len z;
      then i+n < len z by NAT_1:13;
      hence z/.i = z/.(i+n+1) by A28,A57,A60,A61
                   .= z/.(i+(n+1));
      end;
  end;
  A62:for n being Element of NAT holds P[n] from NAT_1:sch 1(A58,A59);
  consider n0 being Nat such that A63:i+n0 = len z by A44,NAT_1:10;
  reconsider n0 as Element of NAT by ORDINAL1:def 13;
A64:  k+1 in dom z by A23,A24,FINSEQ_1:def 3;
      i+n0=len z by A63;
     then f.l = z/.(k+1) by A24,A47,A62
           .= z.(k+1) by A64,FINSEQ_4:def 4
           .= f.(abs(k+1-1)) by A23,A25
           .= m by A21,ABSVALUE:def 1;
  then k <= l by A22;
  then len z <= l + 1 by A24,XREAL_1:8;
  hence contradiction by A42,XXREAL_0:2;
end;

registration
 cluster non empty strict RelStr;
 existence
  proof consider D being non empty set,f being Relation of D;
    take RelStr(#D,f#);
    thus the carrier of RelStr(#D,f#) is non empty;
    thus thesis;
  end;
end;

 reserve FT for non empty RelStr;
 reserve x, y, z for Element of FT;

definition
  let FT be RelStr;
  let x be Element of FT;
  func U_FT x -> Subset of FT equals
     Class(the InternalRel of FT,x);
  coherence;
end;

definition
  let x be set,
      y be Subset of {x};

  redefine func x.-->y -> Function of {x}, bool {x};
  coherence
    proof
A1:   dom ( x.-->y ) = {x} by FUNCOP_1:19;
       rng ( x.-->y ) = {y} by FUNCOP_1:14;
      then reconsider R = x.-->y as Relation of {x}, bool {x}
        by A1,RELSET_1:11;
       R is quasi_total by A1,FUNCT_2:def 1;
     hence thesis;
    end;
end;

definition let x be set;
 func SinglRel x -> Relation of {x} equals
   {[x,x]};
 coherence
  proof
     x in {x} by TARSKI:def 1;
   hence {[x,x]} is Relation of {x} by RELSET_1:8;
  end;
end;

definition
  func FT{0} -> strict RelStr equals
     RelStr (#{0},SinglRel 0#);
  coherence;
end;

registration
  cluster FT{0} -> non empty;
  coherence
  proof
   thus the carrier of FT{0} is non empty;
  end;
end;

notation let IT be non empty RelStr;
 synonym IT is filled for IT is reflexive;
end;

definition let IT be non empty RelStr;
 redefine
 attr IT is filled means :Def4:
  for x being Element of IT holds x in U_FT x;
 compatibility
  proof
   thus IT is filled implies for x being Element of IT holds x in U_FT x
    proof assume
A1:    IT is filled;
     let x be Element of IT;
       x <= x by A1,ORDERS_2:24;
      then [x,x] in the InternalRel of IT by ORDERS_2:def 9;
     hence x in U_FT x by EQREL_1:26;
    end;
   assume
A2:  for x being Element of IT holds x in U_FT x;
   let x be set;
   assume x in the carrier of IT;
    then reconsider x as Element of IT;
     x in U_FT x by A2;
   hence thesis by EQREL_1:26;
  end;
end;

canceled 4;

theorem Th7:
  FT{0} is filled
proof
 let x be Element of FT{0};
A1: x = 0 by TARSKI:def 1;
   [0,0] in SinglRel 0 by TARSKI:def 1;
 hence x in U_FT x by A1,RELAT_1:def 13;
end;

theorem Th8:
  FT{0} is finite
proof
   rng <*0*> = {0} by FINSEQ_1:55;
  hence the carrier of FT{0} is finite;
end;

registration
  cluster FT{0} -> filled finite;
  coherence by Th7,Th8;
end;

registration
  cluster finite filled strict (non empty RelStr);
  existence by Th7;
end;

definition let T be 1-sorted, F be set;
 canceled;

  pred F is_a_cover_of T means
     the carrier of T c= union F;
end;

theorem
   for FT being filled (non empty RelStr) holds
    {U_FT x where x is Element of FT : not contradiction}
     is_a_cover_of FT
proof
  let FT be filled (non empty RelStr);
  let a be set;
  assume a in the carrier of FT;
  then reconsider b = a as Element of FT;
  A1:b in U_FT b by Def4;
   U_FT b in {U_FT x where x is Element of FT : not contradiction};
  hence thesis by A1,TARSKI:def 4;
end;

 reserve A for Subset of FT;

definition let FT; let A be Subset of FT;
  func A^delta -> Subset of FT equals
     {x:U_FT x meets A & U_FT x meets A` };
  coherence
    proof
      defpred P[Element of FT] means
        U_FT $1 meets A & U_FT $1 meets A`;
        { x :P[x]} is Subset of FT from DOMAIN_1:sch 7;
      hence thesis;
    end;
end;

theorem Th10:
  x in A^delta iff U_FT x meets A & U_FT x meets A`
proof
  thus x in A^delta implies U_FT x meets A & U_FT x meets A`
    proof
      assume x in A^delta;
      then ex y st y=x & U_FT y meets A & U_FT y meets A`;
      hence thesis;
    end;
  assume U_FT x meets A & U_FT x meets A`;
  hence thesis;
end;

definition let FT; let A be Subset of FT;
  func A^deltai -> Subset of FT equals
    A /\ (A^delta);
  coherence;

  func A^deltao -> Subset of FT equals
    A` /\ (A^delta);
  coherence;
end;

theorem
   A^delta = A^deltai \/ A^deltao
proof
    for x being set holds x in A^delta iff x in A^deltai \/ A^deltao
    proof
      let x be set;
      thus x in A^delta implies x in A^deltai \/ A^deltao
      proof
        assume x in A^delta;
        then x in [#](the carrier of FT) /\ (A^delta) by XBOOLE_1:28;
        then x in (A \/ A`) /\ (A^delta) by SUBSET_1:25;
        hence x in (A^deltai) \/ (A^deltao) by XBOOLE_1:23;
      end;
      assume x in A^deltai \/ A^deltao;
      then x in (A \/ A`) /\ (A^delta) by XBOOLE_1:23;
      then x in [#](the carrier of FT) /\ (A^delta) by SUBSET_1:25;
      hence x in A^delta by XBOOLE_1:28;
    end;
  hence thesis by TARSKI:2;
end;

definition let FT; let A be Subset of FT;
  func A^i -> Subset of FT equals
    {x:U_FT x c= A};
  coherence
    proof
      defpred P[Element of FT] means U_FT $1 c= A;
       { x:P[x]} is Subset of FT from DOMAIN_1:sch 7;
      hence thesis;
    end;

  func A^b -> Subset of FT equals
    {x:U_FT x meets A};
  coherence
    proof
      defpred P[Element of FT] means U_FT $1 meets A;
       { x :P[x]} is Subset of FT from DOMAIN_1:sch 7;
      hence thesis;
    end;

  func A^s -> Subset of FT equals
    {x:x in A & U_FT x \ {x} misses A };
  coherence
  proof
    defpred P[Element of FT] means
      $1 in A & U_FT $1 \ {$1} misses A;
     {x:P[x]} is Subset of FT from DOMAIN_1:sch 7;
    hence thesis;
  end;
end;

definition let FT; let A be Subset of FT;
  func A^n -> Subset of FT equals
    A \ A^s;
  coherence;

  func A^f -> Subset of FT equals
    {x:ex y st y in A & x in U_FT y};
  coherence
  proof
    defpred P[Element of FT] means
      ex y st y in A & $1 in U_FT y;
     { x :P[x]} is Subset of FT from DOMAIN_1:sch 7;
    hence thesis;
  end;
end;

definition let IT be non empty RelStr;
 attr IT is symmetric means
:Def15: for x, y being Element of IT holds
    y in U_FT x implies x in U_FT y;
end;

theorem Th12:
  x in A^i iff U_FT x c= A
proof
  thus x in A^i implies U_FT x c= A
  proof
    assume x in A^i;
    then ex y st y=x & U_FT y c= A;
    hence thesis;
  end;
  assume U_FT x c= A;
  hence thesis;
end;

theorem Th13:
  x in A^b iff U_FT x meets A
proof
  thus x in A^b implies U_FT x meets A
  proof
    assume x in A^b;
    then ex y st y = x & U_FT y meets A;
    hence thesis;
  end;
  assume U_FT x meets A;
  hence thesis;
end;

theorem Th14:
  x in A^s iff x in A & U_FT x \ {x} misses A
proof
  thus x in A^s implies x in A & U_FT x \ {x} misses A
  proof
    assume x in A^s;
    then ex y st y = x & y in A & U_FT y \ {y} misses A;
    hence thesis;
  end;
  assume x in A & U_FT x \ {x} misses A;
  hence thesis;
end;

theorem
   x in A^n iff x in A & U_FT x \ {x} meets A
proof
  thus x in A^n implies x in A & U_FT x \ {x} meets A
  proof
    assume x in A^n;
    then x in A & not x in A^s by XBOOLE_0:def 4;
    hence x in A & U_FT x \ {x} meets A;
  end;
  assume x in A & U_FT x \ {x} meets A;
  then x in A & not x in A^s by Th14;
  hence thesis by XBOOLE_0:def 4;
end;

theorem Th16:
  x in A^f iff ex y st y in A & x in U_FT y
proof
  thus x in A^f implies ex y st y in A & x in U_FT y
  proof
    assume x in A^f;
    then ex y st y = x & ex z st z in A & y in U_FT z;
    hence thesis;
  end;
  assume ex z st z in A & x in U_FT z;
  hence x in A^f;
end;

theorem
   FT is symmetric iff for A holds A^b = A^f
proof
  thus FT is symmetric implies for A holds A^b = A^f
  proof
    assume A1:FT is symmetric;
    let A be Subset of FT;
    thus A^b c= A^f
    proof
      let x be set;
      assume A2:x in A^b;
      then reconsider y = x as Element of FT;
       U_FT y meets A by A2,Th13;
      then consider z be set such that A3:z in U_FT y /\ A by XBOOLE_0:4;
      reconsider z as Element of FT by A3;
      A4:z in U_FT y & z in A by A3,XBOOLE_0:def 3;
      then y in U_FT z by A1,Def15;
      hence x in A^f by A4;
    end;
    let x be set;
    assume A5:x in A^f;
    then reconsider y = x as Element of FT;
    consider z such that A6:z in A & y in U_FT z by A5,Th16;
     z in U_FT y by A1,A6,Def15;
    then U_FT y meets A by A6,XBOOLE_0:3;
    hence x in A^b;
  end;
  assume A7:for A being Subset of FT holds A^b = A^f;
  let x, y be Element of FT;
  assume y in U_FT x;
  then U_FT x meets {y} by ZFMISC_1:54;
  then x in {y}^b;
  then x in {y}^f by A7;
  then consider z such that A8:z in {y} & x in U_FT z by Th16;
  thus x in U_FT y by A8,TARSKI:def 1;
end;

reserve F for Subset of FT;

definition let FT; let IT be Subset of FT;
  attr IT is open means :Def16:
  IT = IT^i;

  attr IT is closed means :Def17:
  IT = IT^b;

  attr IT is connected means :Def18:
  for B,C being Subset of FT st
    IT = B \/ C & B <> {} & C <> {} & B misses C holds B^b meets C;
end;

definition let FT; let A be Subset of FT;
  func A^fb -> Subset of FT equals
     meet{F:A c= F & F is closed};
  coherence
  proof
    set FF = {F:A c= F & F is closed};
     FF c= bool the carrier of FT
    proof
      let x be set;
      assume x in FF;
      then ex F st x = F & A c= F & F is closed;
      hence x in bool the carrier of FT;
    end;
    then reconsider FF as Subset-Family of FT;
     meet FF is Subset of FT;
    hence thesis;
  end;

  func A^fi -> Subset of FT equals
     union{F:A c= F & F is open};
  coherence
  proof
    set FF = {F:A c= F & F is open};
     FF c= bool the carrier of FT
    proof
      let x be set;
      assume x in FF;
      then ex F st x = F & A c= F & F is open;
      hence x in bool the carrier of FT;
    end;
    then reconsider FF as Subset-Family of FT;
     union FF is Subset of FT;
    hence thesis;
  end;
end;

theorem Th18:
  for FT being filled (non empty RelStr),
      A being Subset of FT holds
    A c= A^b
proof
  let FT be filled (non empty RelStr);
  let A be Subset of FT;
  let x be set;
  assume A1:x in A;
  then reconsider y=x as Element of FT;
   y in U_FT y by Def4;
  then U_FT y meets A by A1,XBOOLE_0:3;
  hence thesis;
end;

theorem Th19:
  for FT being non empty RelStr,
      A, B being Subset of FT holds
    A c= B implies A^b c= B^b
proof
  let FT be non empty RelStr;
  let A, B be Subset of FT;
  assume A1:A c= B;
  let x be set;
  assume A2:x in A^b;
  then reconsider y=x as Element of FT;
   U_FT y meets A by A2,Th13;
  then consider w being set such that
  A3:w in U_FT y & w in A by XBOOLE_0:3;
   U_FT y meets B by A1,A3,XBOOLE_0:3;
  hence x in B^b;
end;

theorem
   for FT being filled finite (non empty RelStr),
      A being Subset of FT holds
    A is connected iff for x being Element of FT st x in A
    ex S being FinSequence of bool the carrier of FT st
      len S > 0 &
      S/.1 = {x} &
      (for i being Element of NAT st i > 0 & i < len S
       holds S/.(i+1) = (S/.i)^b /\ A) &
      A c= S/.len S
proof
  let FT be filled finite (non empty RelStr),
      A be Subset of FT;
  thus A is connected implies for x being Element of FT st x in A
       ex S being FinSequence of bool the carrier of FT st
         len S > 0 &
         S/.1 = {x} &
         (for i being Element of NAT st i > 0 & i < len S holds S/.(i+1) =
(S/.i)^b /\ A) & A c= S/.len S
  proof
    assume A1:A is connected;
    let x be Element of FT such that A2:x in A;
    deffunc F(Subset of FT) = $1^b /\ A;
    A3: A is finite by FINSET_1:13;
      A4: {x} c= A by A2,ZFMISC_1:37;
      A5:for B being Subset of FT st B c= A
         holds B c= F(B) & F(B) c= A
      proof
        let B be Subset of FT such that A6:B c= A;
         B c= B^b by Th18;
        hence B c= B^b /\ A by A6,XBOOLE_1:19;
        thus B^b /\ A c= A by XBOOLE_1:17;
      end;
    consider S being FinSequence of bool the carrier of FT such that
A7: len S > 0 and
A8: S/.1={x} and
A9: (for i being Element of NAT st i > 0 & i < len S
       holds S/.(i+1)=F(S/.i)) and
A10:F(S/.len S) = S/.len S and
A11:(for i, j being Element of NAT st i > 0 & i < j & j <= len S holds
           S/.i c= A & S/.i c< S/.j) from MaxFinSeqEx(A3,A4,A5);
    take S;
    thus len S > 0 by A7;
    thus S/.1 = {x} by A8;
    thus for i being Element of NAT st
    i > 0 & i < len S holds S/.(i+1) = S/.i^b /\ A by A9;
    set B = S/.len S;
    set C = A \ B;
    A12:B c= A by A10,XBOOLE_1:17;
    A13:B \/ C = B \/ A by XBOOLE_1:39
              .= A by A12,XBOOLE_1:12;
    assume not A c= S/.len S;
    then A14:C <> {} by XBOOLE_1:37;
    defpred P[Element of NAT] means $1 < len S implies S/.($1+1) <> {};
    A15:P[0] by A8;
    A16:now let i be Element of NAT;
         assume A17:P[i];
         thus P[i+1]
         proof
         assume A18:i + 1 < len S;
         A19:i+1 < i+1+1 by NAT_1:13;
          i+1+1 <= len S by A18,NAT_1:13;
         then S/.(i+1) c< S/.(i+1+1) by A11,A19;
         then S/.(i+1) c= S/.(i+1+1) by XBOOLE_0:def 8;
         hence S/.(i+1+1) <> {} by A17,A18,NAT_1:13,XBOOLE_1:3;
         end;
       end;
    A20:for i being Element of NAT holds P[i] from NAT_1:sch 1(A15,A16);
    consider k being Nat such that
      A21:len S = k+1 by A7,NAT_1:6;
    reconsider k as Element of NAT by ORDINAL1:def 13;
        k < len S by A21,NAT_1:13;
    then A22:B <> {} by A20,A21;
    A23:B misses C by XBOOLE_1:79;
A24:  B misses ( A \ B ) by XBOOLE_1:79;
        A \ B c= A by XBOOLE_1:36;
    then B^b /\ C = B^b /\ ( A /\ ( A \ B ) ) by XBOOLE_1:28
            .= B /\ ( A \ B ) by A10,XBOOLE_1:16
            .= {} by A24,XBOOLE_0:def 7;
    then B^b misses C by XBOOLE_0:def 7;
    hence contradiction by A1,A13,A14,A22,A23,Def18;
  end;
  assume A25:for x being Element of FT st x in A
            ex S being FinSequence of bool the carrier of FT st
              len S > 0 &
              S/.1 = {x} &
              (for i being Element of NAT st i > 0 & i < len S holds
                S/.(i+1) = S/.i^b /\ A) &
              A c= S/.len S;
  given B, C being Subset of FT such that
A26:A = B \/ C and
A27:B <> {} and
A28:C <> {} and
A29:B misses C and
A30:B^b misses C;
A31: B c= B^b by Th18;
A32:B^b /\ C = {} by A30,XBOOLE_0:def 7;
A33:B /\ C = {} by A29,XBOOLE_0:def 7;
  A34: B^b /\ A = B^b /\ B \/ {} by A26,A32,XBOOLE_1:23
             .= B by A31,XBOOLE_1:28;
  consider x being Element of B;
   x in A by A26,A27,XBOOLE_0:def 2;
  then consider S being FinSequence of bool the carrier of FT such that
    A35:len S > 0 and
    A36:S/.1 = {x} and
    A37:for i being Element of NAT st i > 0 & i < len S holds S/.(i+1) =
(S/.i)^b /\ A and
    A38:A c= S/.len S by A25;
  A39:B c= A by A26,XBOOLE_1:7;
  defpred P[Element of NAT] means $1 < len S implies S/.($1+1) c= B;
A40:P[0] by A27,A36,ZFMISC_1:37;
A41:now let i be Element of NAT;
      assume A42:P[i];
      thus P[i+1]
      proof
      assume A43:i+1 < len S;
      then S/.(i+1)^b c= B^b by A42,Th19,NAT_1:13;
      then S/.(i+1)^b /\ A c= B^b /\ A by XBOOLE_1:26;
      hence S/.(i+1+1) c= B by A34,A37,A43;
      end;
    end;
  A44:for i being Element of NAT holds P[i] from NAT_1:sch 1(A40,A41);
  consider k being Nat such that A45:len S = k + 1 by A35,NAT_1:6;
  reconsider k as Element of NAT by ORDINAL1:def 13;
   k < len S by A45,NAT_1:13;
  then A46:S/.len S c= B by A44,A45;
  then S/.len S c= A by A39,XBOOLE_1:1;
  then S/.len S = A by A38,XBOOLE_0:def 10;
  then A = B by A39,A46,XBOOLE_0:def 10;
  then C c= B by A26,XBOOLE_1:7;
  hence contradiction by A28,A33,XBOOLE_1:28;
end;

canceled;

theorem Th22:
  ((A`)^i)` = A^b
proof
     for x being set holds x in ((A`)^i)` iff x in A^b
    proof
      let x be set;
      thus x in ((A`)^i)` implies x in A^b
      proof
        assume A1:x in ((A`)^i)`;
        then reconsider y=x as Element of FT;
         not y in (A`)^i by A1,XBOOLE_0:def 4;
        then not U_FT y c= A`;
        then consider z being set such that
     A2:not(z in U_FT y implies z in A`) by TARSKI:def 3;
         z in U_FT y & z in A by A2,SUBSET_1:50;
        then U_FT y meets A by XBOOLE_0:3;
        hence x in A^b;
      end;
      assume A3:x in A^b;
      then reconsider y=x as Element of FT;
       U_FT y meets A by A3,Th13;
      then consider z being set such that
   A4:z in U_FT y & z in A by XBOOLE_0:3;
       not U_FT y c= A` by A4,XBOOLE_0:def 4;
      then not y in (A`)^i by Th12;
      hence x in ((A`)^i)` by SUBSET_1:50;
    end;
  hence thesis by TARSKI:2;
end;

theorem Th23:
  ((A`)^b)` = A^i
proof
   for x being set holds x in ((A`)^b)` iff x in A^i
  proof
    let x be set;
    thus x in ((A`)^b)` implies x in A^i
    proof
      assume A1:x in ((A`)^b)`;
      then reconsider y=x as Element of FT;
       not y in (A`)^b by A1,XBOOLE_0:def 4;
      then U_FT y misses A`;
      then U_FT y /\ A` = {} by XBOOLE_0:def 7;
      then U_FT y \ A = {} by SUBSET_1:32;
      then U_FT y c= A by XBOOLE_1:37;
      hence x in A^i;
    end;
    assume A2:x in A^i;
    then reconsider y=x as Element of FT;
     U_FT y c= A by A2,Th12;
    then U_FT y \ A = {} by XBOOLE_1:37;
    then U_FT y /\ A` = {} by SUBSET_1:32;
    then U_FT y misses A` by XBOOLE_0:def 7;
    then not y in (A`)^b by Th13;
    hence x in ((A`)^b)` by SUBSET_1:50;
  end;
  hence thesis by TARSKI:2;
end;

theorem
   A^delta = (A^b) /\ ((A`)^b)
proof
     for x being set holds x in A^delta iff x in (A^b) /\ ((A`)^b)
    proof
      let x be set;
      thus x in A^delta implies x in (A^b) /\ ((A`)^b)
      proof
        assume A1:x in A^delta;
        then reconsider y=x as Element of FT;
         U_FT y meets A & U_FT y meets A` by A1,Th10;
        then x in A^b & x in (A`)^b;
        hence x in (A^b) /\ ((A`)^b) by XBOOLE_0:def 3;
      end;
      assume A2: x in (A^b) /\ ((A`)^b);
      then A3:x in A^b & x in ((A`)^b) by XBOOLE_0:def 3;
      reconsider y=x as Element of FT by A2;
       U_FT y meets A & U_FT y meets A` by A3,Th13;
      hence x in A^delta;
    end;
  hence thesis by TARSKI:2;
end;

theorem
      (A`)^delta = A^delta
proof
   for x being set holds x in (A`)^delta iff x in A^delta
  proof
    let x be set;
    thus x in (A`)^delta implies x in A^delta
    proof
      assume A1:x in (A`)^delta;
      then reconsider y=x as Element of FT;
       U_FT y meets (A`) & U_FT y meets (A`)` by A1,Th10;
      hence x in A^delta;
    end;
    assume A2:x in A^delta;
    then reconsider y=x as Element of FT;
     U_FT y meets (A`)` & U_FT y meets A` by A2,Th10;
    hence x in (A`)^delta;
  end;
  hence thesis by TARSKI:2;
end;

theorem
   x in A^s implies not x in (A \ {x})^b
proof
  assume x in A^s;
  then A misses (U_FT x \ {x}) by Th14;
  then A /\ (U_FT x \ {x}) = {} by XBOOLE_0:def 7;
  then A1: (A /\ U_FT x) \ {x} = {} by XBOOLE_1:49;
   now per cases by A1,ZFMISC_1:66;
    suppose A /\ U_FT x = {};
then A2:   A misses U_FT x by XBOOLE_0:def 7;
       A \ {x} c= A by XBOOLE_1:36;
      hence (A \ {x}) misses U_FT x by A2,XBOOLE_1:63;
    end;
    suppose A /\ U_FT x = {x};
      then (U_FT x /\ A) \ {x} = {} by XBOOLE_1:37;
      then U_FT x /\ (A \ {x}) = {} by XBOOLE_1:49;
      hence (A \ {x}) misses U_FT x by XBOOLE_0:def 7;
  end;
  end;
  hence thesis by Th13;
end;

theorem
   A^s <> {} & Card A <> 1 implies A is not connected
proof
  assume A1:A^s <> {} & Card A <> 1;
  then consider z being Element of FT such that
A2:z in A^s by SUBSET_1:10;
A3:z in A & (U_FT z \ {z}) misses A by A2,Th14;
  set B = A \ {z};
  set C = {z};
      {z} is Subset of A by A3,SUBSET_1:63;
  then A4:A = B \/ C by XBOOLE_1:45;
A5:  Card {z} = 1 by CARD_1:50;
      A <> {} by A2,Th14;
  then A6:B <> {} by A1,A5,ZFMISC_1:66;
  A7:B misses C by XBOOLE_1:79;
      B^b misses C
    proof
      assume B^b meets C;
      then consider x being set such that
      A8:x in B^b & x in C by XBOOLE_0:3;
      reconsider x as Element of FT by A8;
       U_FT x meets B by A8,Th13;
      then consider y being set such that
   A9:y in U_FT x & y in B by XBOOLE_0:3;
      A10:x = z by A8,TARSKI:def 1;
      A11:B c= A by XBOOLE_1:36;
       not x in B by A8,XBOOLE_0:def 4;
      then y in U_FT x \ {x} by A9,ZFMISC_1:64;
      then (U_FT z \ {z}) meets B by A9,A10,XBOOLE_0:3;
      then consider w being set such that
  A12:w in (U_FT z \ {z}) /\ B by XBOOLE_0:4;
       (U_FT z \ {z}) /\ B c= (U_FT z \ {z}) /\ A by A11,XBOOLE_1:26;
      hence contradiction by A3,A12,XBOOLE_0:4;
    end;
  hence thesis by A4,A6,A7,Def18;
end;

theorem
      for FT being filled (non empty RelStr),
      A being Subset of FT holds
    A^i c= A
proof
  let FT be filled (non empty RelStr);
  let A be Subset of FT;
  let x be set;
  assume A1:x in A^i;
  then reconsider y=x as Element of FT;
  A2:y in U_FT y by Def4;
   U_FT y c= A by A1,Th12;
  hence x in A by A2;
end;

canceled;

theorem
   A is open implies A` is closed
proof
  assume A is open;
  then A1:A = A^i by Def16;
   A^i = ((A`)^b)` by Th23;
  hence thesis by A1,Def17;
end;

theorem
   A is closed implies A` is open
proof
  assume A is closed;
  then A1:A = A^b by Def17;
   A^b = ((A`)^i)` by Th22;
  hence thesis by A1,Def16;
end;
