:: Sequences of Metric Spaces and an Abstract Intermediate Value Theorem
::  by Yatsuka Nakamura and Andrzej Trybulec
::
:: Received September 11, 2002
:: Copyright (c) 2002 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, XREAL_0, ORDINAL1, XXREAL_0, SEQ_4,
      XXREAL_2, ARYTM_1, CARD_1, ARYTM_3, METRIC_1, NAT_1, PCOMPS_1, SEQ_2,
      FUNCT_1, RCOMP_1, ORDINAL2, TARSKI, REAL_1, STRUCT_0, PRE_TOPC, RELAT_1,
      SEQ_1, COMPLEX1, XXREAL_1, TOPMETR, VALUED_0, BORSUK_1, EUCLID, TOPREAL1,
      TOPREAL2, PSCOMP_1, JORDAN6, TOPS_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      XXREAL_2, XREAL_0, COMPLEX1, REAL_1, RELAT_1, FUNCT_1, RELSET_1,
      PARTFUN1, FUNCT_2, BINOP_1, NAT_1, STRUCT_0, METRIC_1, PRE_TOPC, TOPS_2,
      COMPTS_1, PCOMPS_1, RCOMP_1, EUCLID, PSCOMP_1, TOPMETR, SEQ_1, SEQ_2,
      SEQM_3, SEQ_4, TBSP_1, JORDAN6, TOPREAL1, TOPREAL2;
 constructors REAL_1, NAT_1, COMPLEX1, SEQM_3, RCOMP_1, TOPS_2, COMPTS_1,
      TBSP_1, TOPMETR, TOPREAL1, PSCOMP_1, JORDAN5C, JORDAN6, SEQ_1, XXREAL_2,
      SEQ_4, BINOP_2, RVSUM_1, SEQ_2, PCOMPS_1;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      MEMBERED, STRUCT_0, PRE_TOPC, METRIC_1, PCOMPS_1, EUCLID, TOPMETR,
      COMPTS_1, VALUED_0, SEQ_4;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, STRUCT_0, XXREAL_2;
 theorems PRE_TOPC, TOPMETR, XREAL_0, TBSP_1, XBOOLE_0, METRIC_1, FUNCT_1,
      XBOOLE_1, FUNCT_2, JGRAPH_2, GOBOARD6, SEQ_2, TREAL_1, SEQ_4, NAT_1,
      SEQM_3, UNIFORM1, BORSUK_1, JORDAN6, TOPREAL1, TOPS_2, TARSKI, RELAT_1,
      TOPREAL3, JORDAN5B, PREPOWER, XCMPLX_1, XREAL_1, XXREAL_0, XXREAL_1,
      EUCLID, XXREAL_2;
 schemes FUNCT_2, RECDEF_1, NAT_1;

begin

theorem Th1:
  for R being non empty Subset of REAL,r0 being real number st for
  r being real number st r in R holds r <= r0 holds upper_bound R <= r0
proof
  let R be non empty Subset of REAL,r0 be real number;
  assume
A1: for r being real number st r in R holds r<=r0;
  then for r being ext-real number st r in R holds r<=r0;
  then r0 is UpperBound of R by XXREAL_2:def 1;
  then
A2: R is bounded_above by XXREAL_2:def 10;
  now
    set r1=(upper_bound R) -r0;
    assume upper_bound R >r0;
    then r1>0 by XREAL_1:52;
    then ex r being real number st r in R & (upper_bound R)-r1<r by A2,
SEQ_4:def 4;
    hence contradiction by A1;
  end;
  hence thesis;
end;

theorem Th2:
  for X being non empty MetrSpace, S being sequence of X, F being
  Subset of TopSpaceMetr(X) st S is convergent & (for n being Element of NAT
  holds S.n in F) & F is closed holds lim S in F
proof
  let X be non empty MetrSpace, S be sequence of X, F be Subset of
  TopSpaceMetr(X);
  assume that
A1: S is convergent and
A2: for n being Element of NAT holds S.n in F and
A3: F is closed;
A4: for G being Subset of TopSpaceMetr(X) st G is open holds lim S in G
  implies F meets G
  proof
    let G be Subset of TopSpaceMetr(X);
    assume
A5: G is open;
    now
      assume lim S in G;
      then consider r1 being real number such that
A6:   r1>0 and
A7:   Ball(lim S,r1) c= G by A5,TOPMETR:22;
      reconsider r2=r1 as Real by XREAL_0:def 1;
      consider n being Element of NAT such that
A8:   for m being Element of NAT st m>=n holds dist(S.m,lim S)<r2 by A1,A6,
TBSP_1:def 4;
      dist(S.n,lim S)<r2 by A8;
      then
A9:   S.n in Ball(lim S,r1) by METRIC_1:12;
      S.n in F by A2;
      then S.n in F /\ G by A7,A9,XBOOLE_0:def 4;
      hence F meets G by XBOOLE_0:def 7;
    end;
    hence thesis;
  end;
  reconsider F0=F as Subset of TopSpaceMetr(X);
  lim S in the carrier of X;
  then lim S in the carrier of TopSpaceMetr(X) by TOPMETR:16;
  then lim S in Cl F0 by A4,PRE_TOPC:def 13;
  hence thesis by A3,PRE_TOPC:52;
end;

theorem Th3:
  for X,Y being non empty MetrSpace, f being Function of
TopSpaceMetr(X),TopSpaceMetr(Y),S being sequence of X holds f*S is sequence of
  Y
proof
  let X,Y be non empty MetrSpace, f be Function of TopSpaceMetr(X),
  TopSpaceMetr(Y),S be sequence of X;
A1: dom f=the carrier of TopSpaceMetr(X) by FUNCT_2:def 1
    .=the carrier of X by TOPMETR:16;
  dom S=NAT & rng S c= the carrier of X by FUNCT_2:def 1;
  then dom (f*S)=NAT by A1,RELAT_1:46;
  hence thesis by FUNCT_2:def 1,TOPMETR:16;
end;

theorem Th4:
  for X,Y being non empty MetrSpace, f being Function of
TopSpaceMetr(X),TopSpaceMetr(Y),S being sequence of X, T being sequence of Y st
  S is convergent & T= f*S & f is continuous holds T is convergent
proof
  let X,Y be non empty MetrSpace, f be Function of TopSpaceMetr(X),
  TopSpaceMetr(Y),S be sequence of X,T be sequence of Y;
  assume that
A1: S is convergent and
A2: T= f*S and
A3: f is continuous;
  set z0=lim S;
  reconsider p=z0 as Point of TopSpaceMetr(X) by TOPMETR:16;
A4: dom f=the carrier of TopSpaceMetr(X) by FUNCT_2:def 1
    .=the carrier of X by TOPMETR:16;
  then f.(lim S) in rng f by FUNCT_1:def 5;
  then reconsider x0=f.(lim S) as Element of Y by TOPMETR:16;
  for r being Real st r>0 ex n being Element of NAT st for m being Element
  of NAT st n<=m holds dist(T.m,x0)<r
  proof
    let r be Real;
    reconsider V=Ball(x0,r) as Subset of TopSpaceMetr(Y) by TOPMETR:16;
    assume r>0;
    then V is open & x0 in V by GOBOARD6:4,TOPMETR:21;
    then consider W being Subset of TopSpaceMetr(X) such that
A5: p in W & W is open and
A6: f.:W c= V by A3,JGRAPH_2:20;
    consider r0 being real number such that
A7: r0>0 and
A8: Ball(z0,r0) c= W by A5,TOPMETR:22;
    reconsider r00=r0 as Real by XREAL_0:def 1;
    consider n0 being Element of NAT such that
A9: for m being Element of NAT st n0<=m holds dist(S.m,z0)<r00 by A1,A7,
TBSP_1:def 4;
    for m being Element of NAT st n0<=m holds dist(T.m,x0)<r
    proof
      let m be Element of NAT;
      assume n0<=m;
      then dist(S.m,z0)<r0 by A9;
      then S.m in Ball(z0,r0) by METRIC_1:12;
      then
A10:  f.(S.m) in f.:W by A4,A8,FUNCT_1:def 12;
      dom T=NAT by FUNCT_2:def 1;
      then T.m in f.:W by A2,A10,FUNCT_1:22;
      hence thesis by A6,METRIC_1:12;
    end;
    hence thesis;
  end;
  hence thesis by TBSP_1:def 3;
end;

canceled;

theorem Th6:
  for s being Real_Sequence,S being sequence of RealSpace st s=S
  holds (s is convergent iff S is convergent) & (s is convergent implies lim s=
  lim S)
proof
  let s be Real_Sequence,S be sequence of RealSpace;
  assume
A1: s=S;
A2: s is convergent implies S is convergent
  proof
    assume s is convergent;
    then consider g being real number such that
A3: for p being real number st 0<p ex n being Element of NAT st for m
    being Element of NAT st n<=m holds abs (s.m-g) < p by SEQ_2:def 6;
    reconsider g0=g as Real by XREAL_0:def 1;
    reconsider x0=g as Point of RealSpace by METRIC_1:def 14,XREAL_0:def 1;
    for r being Real st r>0 ex n being Element of NAT st for m being
    Element of NAT st n<=m holds dist(S.m,x0)<r
    proof
      let r be Real;
      assume r>0;
      then consider n0 being Element of NAT such that
A4:   for m being Element of NAT st n0<=m holds abs (s.m-g) < r by A3;
      for m being Element of NAT st n0<=m holds dist(S.m,x0)<r
      proof
        let m be Element of NAT;
        assume
A5:     n0<=m;
        reconsider t=s.m as Real;
        dist(S.m,x0)=real_dist.(t,g) by A1,METRIC_1:def 1,def 14
          .=abs(t-g0) by METRIC_1:def 13;
        hence thesis by A4,A5;
      end;
      hence thesis;
    end;
    hence thesis by TBSP_1:def 3;
  end;
  S is convergent implies s is convergent
  proof
    assume S is convergent;
    then consider x being Element of RealSpace such that
A6: for r being Real st r>0 ex n being Element of NAT st for m being
    Element of NAT st n<=m holds dist(S.m,x)<r by TBSP_1:def 3;
    reconsider g0=x as Real by METRIC_1:def 14;
    for p being real number st 0<p ex n being Element of NAT st for m
    being Element of NAT st n<=m holds abs (s.m-g0) < p
    proof
      let p be real number;
      reconsider p0=p as Real by XREAL_0:def 1;
      assume 0<p;
      then consider n0 being Element of NAT such that
A7:   for m being Element of NAT st n0<=m holds dist(S.m,x)<p0 by A6;
      for m being Element of NAT st n0<=m holds abs (s.m-g0) < p
      proof
        let m be Element of NAT;
        assume
A8:     n0<=m;
        reconsider t=s.m as Real;
        dist(S.m,x)=real_dist.(t,g0) by A1,METRIC_1:def 1,def 14
          .=abs(t-g0) by METRIC_1:def 13;
        hence thesis by A7,A8;
      end;
      hence thesis;
    end;
    hence thesis by SEQ_2:def 6;
  end;
  hence s is convergent iff S is convergent by A2;
  thus s is convergent implies lim s=lim S
  proof
    reconsider g0=lim S as Real by METRIC_1:def 14;
    assume
A9: s is convergent;
    for r being real number st 0 < r ex m being Element of NAT st for n
    being Element of NAT st m <= n holds abs(s.n-g0) < r
    proof
      let r be real number;
      reconsider r0=r as Real by XREAL_0:def 1;
      assume 0 < r;
      then consider m0 being Element of NAT such that
A10:  for n being Element of NAT st m0<=n holds dist(S.n,lim S)<r0 by A2,A9,
TBSP_1:def 4;
      for n being Element of NAT st m0 <= n holds abs(s.n -g0) < r
      proof
        let n be Element of NAT;
        assume
A11:    m0 <= n;
        dist(S.n,lim S)=real_dist.(s.n,g0) by A1,METRIC_1:def 1,def 14
          .=abs(s.n-g0) by METRIC_1:def 13;
        hence thesis by A10,A11;
      end;
      hence thesis;
    end;
    hence thesis by A9,SEQ_2:def 7;
  end;
end;

theorem Th7:
  for a,b being real number,s being Real_Sequence st rng s c= [.a,b
  .] holds s is sequence of Closed-Interval-MSpace(a,b)
proof
  let a,b be real number,s be Real_Sequence;
  assume
A1: rng s c= [.a,b.];
  reconsider t=s.0 as Real;
A2: dom s=NAT by FUNCT_2:def 1;
  then s.0 in rng s by FUNCT_1:def 5;
  then a<=t & t<=b by A1,XXREAL_1:1;
  then the carrier of Closed-Interval-MSpace(a,b)=[.a,b.] by TOPMETR:14
,XXREAL_0:2;
  hence thesis by A1,A2,FUNCT_2:4;
end;

theorem Th8:
  for a,b being real number, S being sequence of
  Closed-Interval-MSpace(a,b) st a<=b holds S is sequence of RealSpace
proof
  let a,b be real number, S be sequence of Closed-Interval-MSpace(a,b);
  assume a<=b;
  then the carrier of Closed-Interval-MSpace(a,b)=[.a,b.] by TOPMETR:14;
  then dom S=NAT & rng S c= the carrier of RealSpace by FUNCT_2:def 1
,METRIC_1:def 14,XBOOLE_1:1;
  hence thesis by FUNCT_2:4;
end;

theorem Th9:
  for a,b being real number, S1 being sequence of
Closed-Interval-MSpace(a,b), S being sequence of RealSpace st S=S1 & a<=b holds
(S is convergent iff S1 is convergent)& (S is convergent implies lim S=lim S1)
proof
  let a,b be real number, S1 be sequence of Closed-Interval-MSpace(a,b), S be
  sequence of RealSpace;
  assume that
A1: S=S1 and
A2: a<=b;
  reconsider P=[.a,b.] as Subset of R^1 by TOPMETR:24;
A3: the carrier of Closed-Interval-MSpace(a,b)=[.a,b.] by A2,TOPMETR:14;
A4: S is convergent implies S1 is convergent
  proof
A5: for m being Element of NAT holds S.m in [.a,b.]
    proof
      let m be Element of NAT;
      dom S1=NAT by FUNCT_2:def 1;
      then S1.m in rng S1 by FUNCT_1:def 5;
      hence thesis by A1,A3;
    end;
A6: P is closed by TREAL_1:4;
    assume
A7: S is convergent;
    then consider x0 being Element of RealSpace such that
A8: for r being Real st r>0 ex n being Element of NAT st for m being
    Element of NAT st n<=m holds dist(S.m,x0)<r by TBSP_1:def 3;
    x0=lim S by A7,A8,TBSP_1:def 4;
    then reconsider x1=x0 as Element of Closed-Interval-MSpace(a,b) by A3,A7,A5
,A6,Th2,TOPMETR:def 7;
    for r being Real st r>0 ex n being Element of NAT st for m being
    Element of NAT st n<=m holds dist(S1.m,x1)<r
    proof
      let r be Real;
      assume r>0;
      then consider n0 being Element of NAT such that
A9:   for m being Element of NAT st n0<=m holds dist(S.m,x0)<r by A8;
      for m being Element of NAT st n0<=m holds dist(S1.m,x1)<r
      proof
        let m be Element of NAT;
        assume
A10:    n0<=m;
        dist(S1.m,x1) =(the distance of Closed-Interval-MSpace(a,b)).(S1.
        m,x1) by METRIC_1:def 1
          .=(the distance of RealSpace).(S1.m,x1) by TOPMETR:def 1
          .=dist(S.m,x0) by A1,METRIC_1:def 1;
        hence thesis by A9,A10;
      end;
      hence thesis;
    end;
    hence thesis by TBSP_1:def 3;
  end;
  S1 is convergent implies S is convergent
  proof
    assume S1 is convergent;
    then consider x0 being Element of Closed-Interval-MSpace(a,b) such that
A11: for r being Real st r>0 ex n being Element of NAT st for m being
    Element of NAT st n<=m holds dist(S1.m,x0)<r by TBSP_1:def 3;
    x0 in [.a,b.] by A3;
    then reconsider x1=x0 as Element of RealSpace by METRIC_1:def 14;
    for r being Real st r>0 ex n being Element of NAT st for m being
    Element of NAT st n<=m holds dist(S.m,x1)<r
    proof
      let r be Real;
      assume r>0;
      then consider n0 being Element of NAT such that
A12:  for m being Element of NAT st n0<=m holds dist(S1.m,x0)<r by A11;
      for m being Element of NAT st n0<=m holds dist(S.m,x1)<r
      proof
        let m be Element of NAT;
        assume
A13:    n0<=m;
        dist(S1.m,x0) =(the distance of Closed-Interval-MSpace(a,b)).(S1.
        m,x0) by METRIC_1:def 1
          .=(the distance of RealSpace).(S1.m,x0) by TOPMETR:def 1
          .=dist(S.m,x1) by A1,METRIC_1:def 1;
        hence thesis by A12,A13;
      end;
      hence thesis;
    end;
    hence thesis by TBSP_1:def 3;
  end;
  hence S is convergent iff S1 is convergent by A4;
  thus S is convergent implies lim S=lim S1
  proof
    lim S1 in the carrier of Closed-Interval-MSpace(a,b);
    then reconsider t0=lim S1 as Point of RealSpace by A3,METRIC_1:def 14;
    assume
A14: S is convergent;
    for r1 being Real st 0 < r1 ex m1 being Element of NAT st for n1
    being Element of NAT st m1 <= n1 holds dist(S.n1,t0) < r1
    proof
      let r1 being Real;
      assume 0<r1;
      then consider m1 being Element of NAT such that
A15:  for n1 being Element of NAT st m1 <= n1 holds dist(S1.n1,lim S1
      ) < r1 by A4,A14,TBSP_1:def 4;
      for n1 being Element of NAT st m1 <= n1 holds dist(S.n1,t0) < r1
      proof
        let n1 be Element of NAT;
        assume
A16:    m1 <= n1;
        dist(S1.n1,lim S1) =(the distance of Closed-Interval-MSpace(a,b))
        .(S1.n1,lim S1) by METRIC_1:def 1
          .=(the distance of RealSpace).(S1.n1,lim S1) by TOPMETR:def 1
          .=dist(S.n1,t0) by A1,METRIC_1:def 1;
        hence thesis by A15,A16;
      end;
      hence thesis;
    end;
    hence thesis by A14,TBSP_1:def 4;
  end;
end;

theorem Th10:
  for a,b being real number,s being Real_Sequence, S being
sequence of Closed-Interval-MSpace(a,b) st S=s & a<=b & s is convergent holds S
  is convergent & lim s=lim S
proof
  let a,b be real number,s be Real_Sequence, S be sequence of
  Closed-Interval-MSpace(a,b);
  assume that
A1: S=s and
A2: a<=b and
A3: s is convergent;
  reconsider S0=S as sequence of RealSpace by A2,Th8;
A4: S0 is convergent by A1,A3,Th6;
  hence S is convergent by A2,Th9;
  lim S0=lim S by A2,A4,Th9;
  hence thesis by A1,A3,Th6;
end;

theorem
  for a,b being real number,s being Real_Sequence, S being sequence of
  Closed-Interval-MSpace(a,b) st S=s & a<=b & s is non-decreasing holds S is
  convergent
proof
  let a,b be real number,s be Real_Sequence, S be sequence of
  Closed-Interval-MSpace(a,b);
  assume that
A1: S=s and
A2: a<=b and
A3: s is non-decreasing;
  for n being Element of NAT holds s.n<b+1
  proof
    let n be Element of NAT;
A4: b<b+1 by XREAL_1:31;
    dom S=NAT by FUNCT_2:def 1;
    then
A5: s.n in rng S by A1,FUNCT_1:def 5;
    the carrier of Closed-Interval-MSpace(a,b)=[.a,b.] by A2,TOPMETR:14;
    then s.n <=b by A5,XXREAL_1:1;
    hence thesis by A4,XXREAL_0:2;
  end;
  then s is bounded_above by SEQ_2:def 3;
  hence thesis by A1,A2,A3,Th10;
end;

theorem
  for a,b being real number,s being Real_Sequence, S being sequence of
  Closed-Interval-MSpace(a,b) st S=s & a<=b & s is non-increasing holds S is
  convergent
proof
  let a,b be real number,s be Real_Sequence, S be sequence of
  Closed-Interval-MSpace(a,b);
  assume that
A1: S=s and
A2: a<=b and
A3: s is non-increasing;
  for n being Element of NAT holds s.n>a-1
  proof
    let n be Element of NAT;
    a<a+1 by XREAL_1:31;
    then
A4: a-1<a by XREAL_1:21;
    dom S=NAT by FUNCT_2:def 1;
    then
A5: s.n in rng S by A1,FUNCT_1:def 5;
    the carrier of Closed-Interval-MSpace(a,b)=[.a,b.] by A2,TOPMETR:14;
    then a<=s.n by A5,XXREAL_1:1;
    hence thesis by A4,XXREAL_0:2;
  end;
  then s is bounded_below by SEQ_2:def 4;
  hence thesis by A1,A2,A3,Th10;
end;

canceled 2;

theorem Th15:
  for R being non empty Subset of REAL st R is bounded_above holds
ex s being Real_Sequence st s is non-decreasing convergent & rng s c= R & lim s
  =upper_bound R
proof
  let R be non empty Subset of REAL;
  reconsider rs=upper_bound R as real number;
  defpred X[Element of NAT, real number] means ($2 in R & (for r00 being real
  number st r00=$2 holds (rs)-(1/($1+1) qua real number)<r00));
  assume
A1: R is bounded_above;
A2: for n being Element of NAT ex r being Real st X[n,r]
  proof
    let n be Element of NAT;
    (n+1)">0;
    then 1/(n+1)>0 by XCMPLX_1:217;
    then consider r0 being real number such that
A3: r0 in R and
A4: (rs) -(1/(n+1) qua real number)<r0 by A1,SEQ_4:def 4;
    for r00 being real number st r00=r0 holds (rs)-(1/(n+1) qua real
    number)<r00 by A4;
    hence thesis by A3;
  end;
  ex s1 being Function of NAT, REAL st for n being Element of NAT holds X[
  n,s1.n] from FUNCT_2:sch 3(A2);
  then consider s1 being Function of NAT, REAL such that
A5: for n being Element of NAT holds s1.n in R & for r0 being real
  number st r0=s1.n holds (rs)-(1/(n+1) qua real number)<r0;
  defpred P[set,set,set] means ($2 is real number implies (for r1,r2 being
real number,n1 being Nat st r1=$2 & r2=s1.(n1+1) & n1=$1 holds (r1>=r2 implies
  $3=$2)&(r1<r2 implies $3=s1.(n1+1)))) & (not $2 is real number implies $3=1);
A6: for n being Element of NAT for x being set ex y being set st P[n,x,y]
  proof
    let n be Element of NAT;
    thus for x being set ex y being set st P[n,x,y]
    proof
      let x be set;
      now
        per cases;
        case
          not x is real number;
          hence thesis;
        end;
        case
A7:       x is real number;
          then reconsider r10=x as real number;
          reconsider r20=s1.(n+1) as real number;
          now
            per cases;
            case
              r10>=r20;
              then for r1,r2 being real number,n1 being Nat st r1=x & r2=s1.(
n1+1) & n1=n holds (r1>=r2 implies x=x)&(r1<r2 implies x=s1.(n1+1));
              hence thesis by A7;
            end;
            case
              r10<r20;
              then for r1,r2 being real number,n1 being Nat st r1=x & r2=s1.(
n1+1) & n1=n holds (r1>=r2 implies s1.(n+1)=x)& (r1<r2 implies s1.(n+1)=s1.(n1+
              1));
              hence thesis by A7;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
  end;
  ex f being Function st dom f = NAT & f.0 = s1.0 & for n being Element
  of NAT holds P[n,f.n,f.(n+1)] from RECDEF_1:sch 1(A6);
  then consider s2 being Function such that
A8: dom s2 = NAT and
A9: s2.0 = s1.0 and
A10: for n being Element of NAT holds P[n,s2.n,s2.(n+1)];
A11: rng s2 c= REAL
  proof
    defpred X[Nat] means s2.$1 in REAL;
    let y be set;
    assume y in rng s2;
    then consider x being set such that
A12: x in dom s2 and
A13: y=s2.x by FUNCT_1:def 5;
    reconsider n=x as Element of NAT by A8,A12;
A14: for k being Element of NAT st X[k] holds X[k+1]
    proof
      let k be Element of NAT;
      reconsider r2=s1.(k+1) as Real;
      assume
A15:  s2.k in REAL;
      then reconsider r1=s2.k as Real;
      now
        per cases;
        case
          r1>=r2;
          hence thesis by A10,A15;
        end;
        case
          r1<r2;
          then s2.(k+1)=s1.(k+1) by A10;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
A16: X[0] by A9;
    for m being Element of NAT holds X[m] from NAT_1:sch 1(A16,A14);
    then s2.n in REAL;
    hence thesis by A13;
  end;
  then reconsider s3=s2 as Real_Sequence by A8,FUNCT_2:4;
  defpred X[Nat] means s1.$1<=s3.$1;
A17: for k being Element of NAT st X[k] holds X[k+1]
  proof
    let k be Element of NAT;
    assume s1.k<=s3.k;
    reconsider r2=s1.(k+1) as real number;
    s2.k in rng s2 by A8,FUNCT_1:def 5;
    then reconsider r1=s2.k as real number by A11;
    now
      per cases;
      case
        r1>=r2;
        hence thesis by A10;
      end;
      case
        r1<r2;
        hence thesis by A10;
      end;
    end;
    hence thesis;
  end;
A18: X[0] by A9;
A19: for n4 being Element of NAT holds X[n4] from NAT_1:sch 1(A18,A17);
  defpred X[Nat] means 0<=$1 implies s3.0<=s3.$1;
A20: for n4 being Element of NAT holds s3.n4<=s3.(n4+1)
  proof
    let n4 be Element of NAT;
    reconsider r2=s1.(n4+1) as real number;
    dom s3=NAT by FUNCT_2:def 1;
    then reconsider r1=s2.n4 as real number;
    now
      per cases;
      case
        r1>=r2;
        hence thesis by A10;
      end;
      case
        r1<r2;
        hence thesis by A10;
      end;
    end;
    hence thesis;
  end;
A21: for k being Element of NAT st X[k] holds X[k+1]
  proof
    let k be Element of NAT;
    assume
A22: 0<=k implies s3.0<=s3.k;
    now
      assume 0<=k+1;
A23:  s3.k<=s3.(k+1) by A20;
      now
        per cases;
        case
          0<k;
          thus s3.0<=s3.(k+1) by A22,A23,XXREAL_0:2;
        end;
        case
          0=k;
          hence s3.0<=s3.(k+1) by A20;
        end;
      end;
      hence s3.0<=s3.(k+1);
    end;
    hence thesis;
  end;
  defpred Y[Nat] means for n4 being Element of NAT st $1<=n4 holds s3.$1<=s3.
  n4;
A24: for k being Element of NAT st Y[k] holds Y[k+1]
  proof
    let k be Element of NAT;
    assume for n5 being Element of NAT st k<=n5 holds s3.k<=s3.n5;
    defpred Z[Nat] means k+1<=$1 implies s3.(k+1)<=s3.$1;
A25: for i being Element of NAT st Z[i] holds Z[i+1]
    proof
      let i be Element of NAT;
      reconsider r2=s1.(i+1) as real number;
      s2.i in rng s2 by A8,FUNCT_1:def 5;
      then reconsider r1=s2.i as real number by A11;
A26:  now
        per cases;
        case
          r1>=r2;
          hence s3.i<=s3.(i+1) by A10;
        end;
        case
          r1<r2;
          hence s3.i<=s3.(i+1) by A10;
        end;
      end;
      assume
A27:  k+1<=i implies s3.(k+1)<=s3.i;
      now
        assume
A28:    k+1<=i+1;
        now
          per cases by A28,XXREAL_0:1;
          case
            k+1< i+1;
            hence s3.(k+1)<=s3.(i+1) by A27,A26,NAT_1:13,XXREAL_0:2;
          end;
          case
            k+1=i+1;
            hence s3.(k+1)<=s3.(i+1);
          end;
        end;
        hence s3.(k+1)<=s3.(i+1);
      end;
      hence thesis;
    end;
A29: Z[0];
    for n4 being Element of NAT holds Z[n4] from NAT_1:sch 1(A29,A25);
    hence thesis;
  end;
A30: X[0];
  for n4 being Element of NAT holds X[n4] from NAT_1:sch 1(A30,A21);
  then
A31: Y[0];
  for m4 being Element of NAT holds Y[m4] from NAT_1:sch 1(A31,A24);
  then for m4,n4 being Element of NAT st m4 in dom s3 & n4 in dom s3 & m4 <=
  n4 holds s3.m4 <= s3.n4;
  then
A32: s3 is non-decreasing by SEQM_3:def 3;
A33: rng s3 c= R
  proof
    defpred X[set] means s3.$1 in R;
    let y be set;
    assume y in rng s3;
    then
A34: ex x being set st x in dom s3 & y=s3.x by FUNCT_1:def 5;
A35: for k being Element of NAT st X[k] holds X[k+1]
    proof
      let k be Element of NAT;
      reconsider r2=s1.(k+1) as real number;
      s2.k in rng s2 by A8,FUNCT_1:def 5;
      then reconsider r1=s2.k as real number by A11;
      assume
A36:  s3.k in R;
      now
        per cases;
        case
          r1>=r2;
          hence thesis by A10,A36;
        end;
        case
          r1<r2;
          then s2.(k+1)=s1.(k+1) by A10;
          hence thesis by A5;
        end;
      end;
      hence thesis;
    end;
A37: X[0] by A5,A9;
    for nn being Element of NAT holds X[nn] from NAT_1:sch 1(A37,A35);
    hence thesis by A34;
  end;
  for n being Element of NAT holds s3.n < upper_bound R +1
  proof
    let n be Element of NAT;
    s3.n in rng s3 by A8,FUNCT_1:def 5;
    then
A38: s3.n <=upper_bound R by A1,A33,SEQ_4:def 4;
    upper_bound R < upper_bound R +1 by XREAL_1:31;
    hence thesis by A38,XXREAL_0:2;
  end;
  then
A39: s3 is bounded_above by SEQ_2:def 3;
A40: for r being Real st r>0 holds upper_bound R -r<lim s3
  proof
    let r be Real;
    assume
A41: r>0;
    consider n2 being Element of NAT such that
A42: 1/r<n2 by SEQ_4:10;
    n2<n2+1 by XREAL_1:31;
    then 1/r<n2+1 by A42,XXREAL_0:2;
    then 1/r*r<(n2+1)*r by A41,XREAL_1:70;
    then 1<(n2+1)*r by A41,XCMPLX_1:107;
    then 1/(n2+1)<(n2+1)*r/(n2+1) by XREAL_1:76;
    then 1/(n2+1)<r by XCMPLX_1:90;
    then rs-1/(n2+1)>rs-r by XREAL_1:12;
    then
A43: rs-r<s1.n2 by A5,XXREAL_0:2;
A44: s3.n2<=lim s3 by A32,A39,SEQ_4:54;
    s1.n2<=s3.n2 by A19;
    then rs-r<s3.n2 by A43,XXREAL_0:2;
    hence thesis by A44,XXREAL_0:2;
  end;
A45: now
    reconsider r=upper_bound R -lim s3 as Real;
    assume upper_bound R>lim s3;
    then r>0 by XREAL_1:52;
    then upper_bound R -r<lim s3 by A40;
    hence contradiction;
  end;
A46: for n being Element of NAT holds s3.n <=upper_bound R
  proof
    let n be Element of NAT;
    dom s3=NAT by FUNCT_2:def 1;
    then s3.n in rng s3 by FUNCT_1:def 5;
    hence thesis by A1,A33,SEQ_4:def 4;
  end;
  for n being Element of NAT holds s3.n < upper_bound R +1
  proof
    let n be Element of NAT;
    upper_bound R<upper_bound R +1 & s3.n<=upper_bound R by A46,XREAL_1:31;
    hence thesis by XXREAL_0:2;
  end;
  then
A47: s3 is bounded_above by SEQ_2:def 3;
  then lim s3<=upper_bound R by A32,A46,PREPOWER:3;
  hence thesis by A32,A33,A47,A45,XXREAL_0:1;
end;

theorem Th16:
  for R being non empty Subset of REAL st R is bounded_below holds
ex s being Real_Sequence st s is non-increasing convergent & rng s c= R & lim s
  =lower_bound R
proof
  let R be non empty Subset of REAL;
  reconsider rs=lower_bound R as real number;
  defpred X[Element of NAT, real number] means ($2 in R & (for r00 being real
  number st r00=$2 holds (rs)+(1/($1+1) qua real number)>r00));
  assume
A1: R is bounded_below;
A2: for n being Element of NAT ex r being Real st X[n,r]
  proof
    let n be Element of NAT;
    (n+1)">0;
    then 1/(n+1)>0 by XCMPLX_1:217;
    then consider r0 being real number such that
A3: r0 in R and
A4: (rs) +(1/(n+1) qua real number)>r0 by A1,SEQ_4:def 5;
    for r00 being real number st r00=r0 holds (rs)+(1/(n+1) qua real
    number)>r00 by A4;
    hence thesis by A3;
  end;
  ex s1 being Function of NAT, REAL st for n being Element of NAT holds X[
  n,s1.n] from FUNCT_2:sch 3(A2);
  then consider s1 being Function of NAT, REAL such that
A5: for n being Element of NAT holds s1.n in R & for r0 being real
  number st r0=s1.n holds (rs)+(1/(n+1) qua real number)>r0;
  defpred P[set,set,set] means ($2 is real number implies (for r1,r2 being
real number,n1 being Nat st r1=$2 & r2=s1.(n1+1) & n1=$1 holds (r1<=r2 implies
  $3=$2)&(r1>r2 implies $3=s1.(n1+1)))) & (not $2 is real number implies $3=1);
A6: for n being Element of NAT for x being set ex y being set st P[n,x,y]
  proof
    let n be Element of NAT;
    thus for x being set ex y being set st P[n,x,y]
    proof
      let x be set;
      now
        per cases;
        case
          not x is real number;
          hence ex y being set st (x is real number implies for r1,r2 being
real number,n1 being Nat st r1=x & r2=s1.(n1+1) & n1=n holds (r1>=r2 implies y=
          x)&(r1<r2 implies y=s1.(n1+1)) ) &(not x is real number implies y=1);
        end;
        case
A7:       x is real number;
          then reconsider r10=x as real number;
          reconsider r20=s1.(n+1) as real number;
          now
            per cases;
            case
              r10<=r20;
              then for r1,r2 being real number,n1 being Nat st r1=x & r2=s1.(
n1+1) & n1=n holds (r1<=r2 implies x=x)&(r1>r2 implies x=s1.(n1+1));
              hence thesis by A7;
            end;
            case
              r10>r20;
              then for r1,r2 being real number,n1 being Nat st r1=x & r2=s1.(
n1+1) & n1=n holds (r1<=r2 implies s1.(n+1)=x)& (r1>r2 implies s1.(n+1)=s1.(n1+
              1));
              hence thesis by A7;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
  end;
  ex f being Function st dom f = NAT & f.0 = s1.0 & for n being Element
  of NAT holds P[n,f.n,f.(n+1)] from RECDEF_1:sch 1(A6);
  then consider s2 being Function such that
A8: dom s2 = NAT and
A9: s2.0 = s1.0 and
A10: for n being Element of NAT holds P[n,s2.n,s2.(n+1)];
A11: rng s2 c= REAL
  proof
    defpred X[Nat] means s2.$1 in REAL;
    let y be set;
    assume y in rng s2;
    then consider x being set such that
A12: x in dom s2 and
A13: y=s2.x by FUNCT_1:def 5;
    reconsider n=x as Element of NAT by A8,A12;
A14: for k being Element of NAT st X[k] holds X[k+1]
    proof
      let k be Element of NAT;
      reconsider r2=s1.(k+1) as Real;
      assume
A15:  s2.k in REAL;
      then reconsider r1=s2.k as Real;
      now
        per cases;
        case
          r1<=r2;
          hence thesis by A10,A15;
        end;
        case
          r1>r2;
          then s2.(k+1)=s1.(k+1) by A10;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
A16: X[0] by A9;
    for m being Element of NAT holds X[m] from NAT_1:sch 1(A16,A14);
    then s2.n in REAL;
    hence thesis by A13;
  end;
  then reconsider s3=s2 as Real_Sequence by A8,FUNCT_2:4;
  defpred X[Nat] means s1.$1>=s3.$1;
A17: for k being Element of NAT st X[k] holds X[k+1]
  proof
    let k be Element of NAT;
    assume s1.k>=s3.k;
    reconsider r2=s1.(k+1) as real number;
    s2.k in rng s2 by A8,FUNCT_1:def 5;
    then reconsider r1=s2.k as real number by A11;
    now
      per cases;
      case
        r1<=r2;
        hence thesis by A10;
      end;
      case
        r1>r2;
        hence thesis by A10;
      end;
    end;
    hence thesis;
  end;
A18: X[0] by A9;
A19: for n4 being Element of NAT holds X[n4] from NAT_1:sch 1(A18,A17);
  defpred X[Nat] means 0<=$1 implies s3.0>=s3.$1;
A20: for n4 being Element of NAT holds s3.n4>=s3.(n4+1)
  proof
    let n4 be Element of NAT;
    reconsider r2=s1.(n4+1) as real number;
    dom s3=NAT by FUNCT_2:def 1;
    then reconsider r1=s2.n4 as real number;
    now
      per cases;
      case
        r1<=r2;
        hence thesis by A10;
      end;
      case
        r1>r2;
        hence thesis by A10;
      end;
    end;
    hence thesis;
  end;
A21: for k being Element of NAT st X[k] holds X[k+1]
  proof
    let k be Element of NAT;
    assume
A22: 0<=k implies s3.0>=s3.k;
    now
      assume 0<=k+1;
A23:  s3.k>=s3.(k+1) by A20;
      now
        per cases;
        case
          0<k+1;
          thus s3.0>=s3.(k+1) by A22,A23,XXREAL_0:2;
        end;
        case
          0=k+1;
          hence s3.0>=s3.(k+1);
        end;
      end;
      hence s3.0>=s3.(k+1);
    end;
    hence thesis;
  end;
  defpred Y[Nat] means for n4 being Element of NAT st $1<=n4 holds s3.$1>=s3.
  n4;
A24: for k being Element of NAT st Y[k] holds Y[k+1]
  proof
    let k be Element of NAT;
    assume for n5 being Element of NAT st k<=n5 holds s3.k>=s3.n5;
    defpred X[Nat] means k+1<=$1 implies s3.(k+1)>=s3.$1;
A25: for i being Element of NAT st X[i] holds X[i+1]
    proof
      let i be Element of NAT;
      reconsider r2=s1.(i+1) as real number;
      s2.i in rng s2 by A8,FUNCT_1:def 5;
      then reconsider r1=s2.i as real number by A11;
A26:  now
        per cases;
        case
          r1<=r2;
          hence s3.i>=s3.(i+1) by A10;
        end;
        case
          r1>r2;
          hence s3.i>=s3.(i+1) by A10;
        end;
      end;
      assume
A27:  k+1<=i implies s3.(k+1)>=s3.i;
      now
        assume
A28:    k+1<=i+1;
        now
          per cases by A28,XXREAL_0:1;
          case
            k+1<i+1;
            hence s3.(k+1)>=s3.(i+1) by A27,A26,NAT_1:13,XXREAL_0:2;
          end;
          case
            k+1=i+1;
            hence s3.(k+1)>=s3.(i+1);
          end;
        end;
        hence s3.(k+1)>=s3.(i+1);
      end;
      hence thesis;
    end;
A29: X[0];
    thus for n4 being Element of NAT holds X[n4] from NAT_1:sch 1(A29, A25);
  end;
A30: X[0];
  for n4 being Element of NAT holds X[n4] from NAT_1:sch 1(A30,A21);
  then
A31: Y[0];
  for m4 being Element of NAT holds Y[m4] from NAT_1:sch 1(A31,A24);
  then for m4,n4 being Element of NAT st m4 in dom s3 & n4 in dom s3 & m4 <=
  n4 holds s3.m4 >= s3.n4;
  then
A32: s3 is non-increasing by SEQM_3:def 4;
A33: rng s3 c= R
  proof
    defpred Z[Nat] means s3.$1 in R;
    let y be set;
    assume y in rng s3;
    then
A34: ex x being set st x in dom s3 & y=s3.x by FUNCT_1:def 5;
A35: for k being Element of NAT st Z[k] holds Z[k+1]
    proof
      let k be Element of NAT;
      reconsider r2=s1.(k+1) as real number;
      s2.k in rng s2 by A8,FUNCT_1:def 5;
      then reconsider r1=s2.k as real number by A11;
      assume
A36:  s3.k in R;
      now
        per cases;
        case
          r1<=r2;
          hence thesis by A10,A36;
        end;
        case
          r1>r2;
          then s2.(k+1)=s1.(k+1) by A10;
          hence thesis by A5;
        end;
      end;
      hence thesis;
    end;
A37: Z[0] by A5,A9;
    for nn being Element of NAT holds Z[nn] from NAT_1:sch 1(A37,A35);
    hence thesis by A34;
  end;
  for n being Element of NAT holds s3.n > lower_bound R -1
  proof
    let n be Element of NAT;
    lower_bound R < lower_bound R +1 by XREAL_1:31;
    then
A38: lower_bound R > lower_bound R -1 by XREAL_1:21;
    s3.n in rng s3 by A8,FUNCT_1:def 5;
    then s3.n >=lower_bound R by A1,A33,SEQ_4:def 5;
    hence thesis by A38,XXREAL_0:2;
  end;
  then
A39: s3 is bounded_below by SEQ_2:def 4;
A40: for r being Real st r>0 holds lower_bound R +r>lim s3
  proof
    let r be Real;
    assume
A41: r>0;
    consider n2 being Element of NAT such that
A42: 1/r<n2 by SEQ_4:10;
    n2<n2+1 by XREAL_1:31;
    then 1/r<n2+1 by A42,XXREAL_0:2;
    then 1/r*r<(n2+1)*r by A41,XREAL_1:70;
    then 1<(n2+1)*r by A41,XCMPLX_1:107;
    then 1/(n2+1)<(n2+1)*r/(n2+1) by XREAL_1:76;
    then 1/(n2+1)<r by XCMPLX_1:90;
    then rs+1/(n2+1)<rs+r by XREAL_1:10;
    then
A43: rs+r>s1.n2 by A5,XXREAL_0:2;
A44: s3.n2>=lim s3 by A32,A39,SEQ_4:55;
    s1.n2>=s3.n2 by A19;
    then rs+r>s3.n2 by A43,XXREAL_0:2;
    hence thesis by A44,XXREAL_0:2;
  end;
A45: now
    reconsider r=(lim s3) -lower_bound R as Real;
    assume lower_bound R<lim s3;
    then r>0 by XREAL_1:52;
    then lower_bound R +((lim s3)+-lower_bound R)>lim s3 by A40;
    hence contradiction;
  end;
A46: for n being Element of NAT holds s3.n >=lower_bound R
  proof
    let n be Element of NAT;
    dom s3=NAT by FUNCT_2:def 1;
    then s3.n in rng s3 by FUNCT_1:def 5;
    hence thesis by A1,A33,SEQ_4:def 5;
  end;
  for n being Element of NAT holds s3.n > lower_bound R -1
  proof
    let n be Element of NAT;
    lower_bound R<lower_bound R +1 by XREAL_1:31;
    then
A47: lower_bound R>lower_bound R -1 by XREAL_1:21;
    s3.n>=lower_bound R by A46;
    hence thesis by A47,XXREAL_0:2;
  end;
  then
A48: s3 is bounded_below by SEQ_2:def 4;
  then lim s3>=lower_bound R by A32,A46,PREPOWER:2;
  hence thesis by A32,A33,A48,A45,XXREAL_0:1;
end;

theorem Th17:
  :: An Abstract Intermediate Value Theorem for Closed Sets
  for X being non empty MetrSpace, f being Function of I[01],
TopSpaceMetr(X), F1,F2 being Subset of TopSpaceMetr(X),r1,r2 being Real st 0<=
r1 & r2<=1 & r1<=r2 & f.r1 in F1 & f.r2 in F2 & F1 is closed & F2 is closed & f
is continuous & F1 \/ F2 =the carrier of X ex r being Real st r1<=r & r<=r2 & f
  .r in F1 /\ F2
proof
  let X be non empty MetrSpace, f be Function of I[01],TopSpaceMetr(X), F1,F2
  be Subset of TopSpaceMetr(X),r1,r2 be Real;
  assume that
A1: 0<=r1 and
A2: r2<=1 and
A3: r1<=r2 & f.r1 in F1 and
A4: f.r2 in F2 and
A5: F1 is closed and
A6: F2 is closed and
A7: f is continuous and
A8: F1 \/ F2 =the carrier of X;
A9: r1 in {r3 where r3 is Real: r1<=r3 & r3<=r2 & f.r3 in F1} by A3;
  {r3 where r3 is Real: r1<=r3 & r3<=r2 & f.r3 in F1} c= REAL
  proof
    let x be set;
    assume x in {r3 where r3 is Real: r1<=r3 & r3<=r2 & f.r3 in F1};
    then ex r3 being Real st r3=x & r1<=r3 & r3<=r2 & f.r3 in F1;
    hence thesis;
  end;
  then reconsider R={r3 where r3 is Real: r1<=r3 & r3<=r2 & f.r3 in F1} as non
  empty Subset of REAL by A9;
A10: for r being real number st r in R holds r<=r2
  proof
    let r be real number;
    assume r in R;
    then ex r3 being Real st r3=r & r1<=r3 & r3<=r2 & f.r3 in F1;
    hence thesis;
  end;
 r2 is UpperBound of R
  proof
    let r be ext-real number;
    assume r in R;
    then ex r3 being Real st r3=r & r1<=r3 & r3<=r2 & f.r3 in F1;
    hence thesis;
  end;
  then
A11: R is bounded_above by XXREAL_2:def 10;
  then consider s1 being Real_Sequence such that
A12: s1 is non-decreasing convergent and
A13: rng s1 c= R and
A14: lim s1=upper_bound R by Th15;
  {r3 where r3 is Real: r1<=r3 & r3<=r2 & f.r3 in F1} c= [.0,1.]
  proof
    let x be set;
    assume x in {r3 where r3 is Real: r1<=r3 & r3<=r2 & f.r3 in F1};
    then consider r3 being Real such that
A15: r3=x & r1<=r3 and
A16: r3<=r2 and
    f.r3 in F1;
    r3<=1 by A2,A16,XXREAL_0:2;
    hence thesis by A1,A15,XXREAL_1:1;
  end;
  then reconsider S1=s1 as sequence of Closed-Interval-MSpace(0,1) by A13,Th7,
XBOOLE_1:1;
A17: I[01]=TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:27,def 8;
  then reconsider S00=f*S1 as sequence of X by Th3;
A18: S00 is convergent by A7,A12,A17,Th4,Th10;
  dom f=the carrier of I[01] by FUNCT_2:def 1
    .=the carrier of Closed-Interval-MSpace(0,1) by A17,TOPMETR:16;
  then f.(lim S1) in rng f by FUNCT_1:12;
  then reconsider t1=f.(lim S1) as Point of X by TOPMETR:16;
  reconsider S2=s1 as sequence of RealSpace by METRIC_1:def 14;
A19: S1 is convergent by A12,Th10;
  then S2 is convergent by Th9;
  then lim S2=lim S1 by Th9;
  then
A20: lim s1=lim S1 by A12,Th6;
A21: for r being Real st r>0 ex n being Element of NAT st for m being
  Element of NAT st m>=n holds dist(S00.m,t1)<r
  proof
    let r be Real;
    assume r>0;
    then consider r7 being Real such that
A22: r7>0 and
A23: for w being Point of Closed-Interval-MSpace(0,1), w1 being Point
    of X st w1=f.w & dist(lim S1,w)<r7 holds dist(t1,w1)<r by A7,A17,UNIFORM1:5
    ;
    consider n0 being Element of NAT such that
A24: for m being Element of NAT st m>=n0 holds dist(S1.m,lim S1)<r7 by A19,A22,
TBSP_1:def 4;
    for m being Element of NAT st m>=n0 holds dist(S00.m,t1)<r
    proof
      let m be Element of NAT;
      dom S00=NAT by TBSP_1:5;
      then
A25:  S00.m=f.(S1.m) by FUNCT_1:22;
      assume m>=n0;
      then dist(lim S1,S1.m)<r7 by A24;
      hence thesis by A23,A25;
    end;
    hence thesis;
  end;
A26: r2>=upper_bound R by A10,Th1;
  then
A27: r2 in {r3 where r3 is Real: upper_bound R <=r3 & r3<=r2 & f.r3 in F2}
  by A4;
  {r3 where r3 is Real: upper_bound R<=r3 & r3<=r2 & f.r3 in F2} c= REAL
  proof
    let x be set;
    assume x in {r3 where r3 is Real: upper_bound R<=r3 & r3<=r2 & f.r3 in F2};
    then ex r3 being Real st r3=x & upper_bound R<=r3 & r3<=r2 & f.r3 in F2;
    hence thesis;
  end;
  then reconsider
  R2={r3 where r3 is Real: upper_bound R<=r3 & r3<=r2 & f.r3 in F2}
  as non empty Subset of REAL by A27;
A28: for r being real number st r in R2 holds r>=upper_bound R
  proof
    let r be real number;
    assume r in R2;
    then ex r3 being Real st r3=r & upper_bound R<=r3 & r3<=r2 & f.r3 in F2;
    hence thesis;
  end;
  upper_bound R is LowerBound of R2
  proof
    let r be ext-real number;
    assume r in R2;
    then ex r3 being Real st r3=r & upper_bound R<=r3 & r3<=r2 & f.r3 in F2;
    hence thesis;
  end;
  then
A29: R2 is bounded_below by XXREAL_2:def 9;
  then consider s2 being Real_Sequence such that
A30: s2 is non-increasing convergent and
A31: rng s2 c= R2 and
A32: lim s2=lower_bound R2 by Th16;
A33: 0<=upper_bound R by A1,A9,A11,SEQ_4:def 4;
  {r3 where r3 is Real: upper_bound R <=r3 & r3<=r2 & f.r3 in F2} c= [.0, 1.]
  proof
    let x be set;
    assume x in {r3 where r3 is Real: upper_bound R<=r3 & r3<=r2 & f.r3 in F2};
    then consider r3 being Real such that
A34: r3=x & upper_bound R<=r3 and
A35: r3<=r2 and
    f.r3 in F2;
    r3<=1 by A2,A35,XXREAL_0:2;
    hence thesis by A33,A34,XXREAL_1:1;
  end;
  then reconsider S1=s2 as sequence of Closed-Interval-MSpace(0,1) by A31,Th7,
XBOOLE_1:1;
  reconsider S2=s2 as sequence of RealSpace by METRIC_1:def 14;
A36: S1 is convergent by A30,Th10;
  then S2 is convergent by Th9;
  then lim S2=lim S1 by Th9;
  then
A37: lim s2=lim S1 by A30,Th6;
  for n4 being Element of NAT holds S00.n4 in F1
  proof
    let n4 be Element of NAT;
    dom s1=NAT by FUNCT_2:def 1;
    then s1.n4 in rng s1 by FUNCT_1:def 5;
    then s1.n4 in R by A13;
    then dom S00=NAT & ex r3 being Real st r3=s1.n4 & r1<=r3 & r3<=r2 & f.r3
    in F1 by TBSP_1:5;
    hence thesis by FUNCT_1:22;
  end;
  then lim S00 in F1 by A5,A7,A19,A17,Th2,Th4;
  then
A38: f.(lim s1) in F1 by A20,A18,A21,TBSP_1:def 4;
A39: I[01]=TopSpaceMetr(Closed-Interval-MSpace(0,1)) by TOPMETR:27,def 8;
  then reconsider S00=f*S1 as sequence of X by Th3;
  dom f=the carrier of I[01] by FUNCT_2:def 1
    .=the carrier of Closed-Interval-MSpace(0,1) by A39,TOPMETR:16;
  then f.(lim S1) in rng f by FUNCT_1:12;
  then reconsider t1=f.(lim S1) as Point of X by TOPMETR:16;
A40: for r being Real st r>0 ex n being Element of NAT st for m being
  Element of NAT st m>=n holds dist(S00.m,t1)<r
  proof
    let r be Real;
    assume r>0;
    then consider r7 being Real such that
A41: r7>0 and
A42: for w being Point of Closed-Interval-MSpace(0,1), w1 being Point
    of X st w1=f.w & dist(lim S1,w)<r7 holds dist(t1,w1)<r by A7,A39,UNIFORM1:5
;
    consider n0 being Element of NAT such that
A43: for m being Element of NAT st m>=n0 holds dist(S1.m,lim S1)<r7
    by A36,A41,TBSP_1:def 4;
    for m being Element of NAT st m>=n0 holds dist(S00.m,t1)<r
    proof
      let m be Element of NAT;
      dom S00=NAT by TBSP_1:5;
      then
A44:  S00.m=f.(S1.m) by FUNCT_1:22;
      assume m>=n0;
      then dist(lim S1,S1.m)<r7 by A43;
      hence thesis by A42,A44;
    end;
    hence thesis;
  end;
A45: r1<= upper_bound R by A9,A11,SEQ_4:def 4;
  for s being real number st 0<s ex r being real number st r in R2 & r<
  upper_bound R+s
  proof
    let s be real number;
    assume
A46: 0<s;
    now
      assume
A47:  for r being real number st r<upper_bound R+s holds not r in R2;
      now
        per cases by A26,XREAL_1:50;
        case
          r2-upper_bound R=0;
          hence contradiction by A27,A46,A47,XREAL_1:31;
        end;
        case
A48:      r2-upper_bound R>0;
          set rs0=min(r2-upper_bound R,s);
          set r0=upper_bound R + rs0/2;
A49:      rs0>0 by A46,A48,XXREAL_0:21;
          then
A50:      upper_bound R<r0 by XREAL_1:31,217;
          rs0<=r2-upper_bound R by XXREAL_0:17;
          then
A51:      upper_bound R +rs0<=upper_bound R +(r2-upper_bound R) by XREAL_1:9;
A52:      rs0/2<rs0 by A49,XREAL_1:218;
          then upper_bound R +rs0/2<upper_bound R +rs0 by XREAL_1:10;
          then
A53:      r0<=r2 by A51,XXREAL_0:2;
          then r0<=1 by A2,XXREAL_0:2;
          then r0 in the carrier of I[01] by A1,A45,A49,BORSUK_1:83,XXREAL_1:1;
          then r0 in dom f by FUNCT_2:def 1;
          then f.r0 in rng f by FUNCT_1:def 5;
          then f.r0 in the carrier of TopSpaceMetr(X);
          then f.r0 in the carrier of X by TOPMETR:16;
          then
A54:      f.r0 in F1 or f.r0 in F2 by A8,XBOOLE_0:def 3;
          upper_bound R <= upper_bound R + rs0/2 by A49,XREAL_1:31,217;
          then
A55:      r1<=r0 by A45,XXREAL_0:2;
          rs0<=s by XXREAL_0:17;
          then rs0/2<s by A52,XXREAL_0:2;
          then r0<upper_bound R+s by XREAL_1:10;
          then
A56:      not r0 in R2 by A47;
          upper_bound R <r0 by A49,XREAL_1:31,217;
          then r0 in R by A56,A55,A53,A54;
          hence contradiction by A11,A50,SEQ_4:def 4;
        end;
      end;
      hence contradiction;
    end;
    hence thesis;
  end;
  then
A57: upper_bound R=lower_bound R2 by A28,A29,SEQ_4:def 5;
  S00 is convergent by A7,A30,A39,Th4,Th10;
  then
A58: f.(lim S1)=lim S00 by A40,TBSP_1:def 4;
  for n4 being Element of NAT holds S00.n4 in F2
  proof
    let n4 be Element of NAT;
    dom s2=NAT by FUNCT_2:def 1;
    then s2.n4 in rng s2 by FUNCT_1:def 5;
    then s2.n4 in R2 by A31;
    then dom S00=NAT & ex r3 being Real st r3=s2.n4 & upper_bound R<=r3 & r3
    <= r2 & f.r3 in F2 by TBSP_1:5;
    hence thesis by FUNCT_1:22;
  end;
  then lim S00 in F2 by A6,A7,A36,A39,Th2,Th4;
  then f.(lim s1) in F1 /\ F2 by A57,A14,A38,A32,A37,A58,XBOOLE_0:def 4;
  hence thesis by A26,A45,A14;
end;

theorem Th18:
  for n being Element of NAT,p1,p2 being Point of TOP-REAL n, P,P1
being non empty Subset of TOP-REAL n st P is_an_arc_of p1,p2 & P1 is_an_arc_of
  p2,p1 & P1 c= P holds P1=P
proof
  let n be Element of NAT,p1,p2 be Point of TOP-REAL n, P,P1 be non empty
  Subset of TOP-REAL n;
  assume that
A1: P is_an_arc_of p1,p2 and
A2: P1 is_an_arc_of p2,p1 and
A3: P1 c= P;
  P1 is_an_arc_of p1,p2 by A2,JORDAN5B:14;
  hence thesis by A1,A3,JORDAN6:59;
end;

theorem
  for P,P1 being compact non empty Subset of TOP-REAL 2 st P is
  being_simple_closed_curve & P1 is_an_arc_of W-min(P),E-max(P) & P1 c= P holds
  P1=Upper_Arc(P) or P1=Lower_Arc(P)
proof
  let P,P1 be compact non empty Subset of TOP-REAL 2;
  assume that
A1: P is being_simple_closed_curve and
A2: P1 is_an_arc_of W-min(P),E-max(P) and
A3: P1 c= P;
A4: Upper_Arc(P) is_an_arc_of W-min(P),E-max(P) by A1,JORDAN6:def 8;
A5: the carrier of (TOP-REAL 2)|P=[#]((TOP-REAL 2)|P) .=P by PRE_TOPC:def 10;
  then reconsider U2=Upper_Arc(P) as Subset of (TOP-REAL 2)|P by A1,JORDAN6:76;
  reconsider U3=Lower_Arc(P) as Subset of (TOP-REAL 2)|P by A1,A5,JORDAN6:76;
A6: Lower_Arc(P) is_an_arc_of E-max(P),W-min(P) by A1,JORDAN6:def 9;
  U2=Upper_Arc(P) /\ P by A1,JORDAN6:76,XBOOLE_1:28;
  then
A7: U2 is closed by A4,JORDAN6:3,12;
A8: Upper_Arc(P) \/ Lower_Arc(P)=P by A1,JORDAN6:def 9;
  U3=Lower_Arc(P) /\ P by A1,JORDAN6:76,XBOOLE_1:28;
  then
A9: U3 is closed by A6,JORDAN6:3,12;
A10: Upper_Arc(P) /\ Lower_Arc(P)={W-min(P),E-max(P)} by A1,JORDAN6:def 9;
  then
A11: E-max(P) in Upper_Arc(P) /\ Lower_Arc(P) by TARSKI:def 2;
A12: W-min(P) in Upper_Arc(P) /\ Lower_Arc(P) by A10,TARSKI:def 2;
  consider f being Function of I[01], (TOP-REAL 2)|P1 such that
A13: f is being_homeomorphism and
A14: f.0 = W-min(P) and
A15: f.1 = E-max(P) by A2,TOPREAL1:def 2;
A16: f is one-to-one by A13,TOPS_2:def 5;
A17: dom f=[#](I[01]) by A13,TOPS_2:def 5;
A18: rng f=[#]((TOP-REAL 2)|P1) by A13,TOPS_2:def 5
    .=P1 by PRE_TOPC:def 10;
A19: f is continuous by A13,TOPS_2:def 5;
  now
    per cases;
    case
A20:  for r being Real st 0<r & r<1 holds f.r in Lower_Arc(P);
      P1 c= Lower_Arc(P)
      proof
        let y be set;
        assume y in P1;
        then consider x being set such that
A21:    x in dom f and
A22:    y=f.x by A18,FUNCT_1:def 5;
        reconsider r=x as Real by A17,A21,BORSUK_1:83;
A23:    r<=1 by A21,BORSUK_1:83,XXREAL_1:1;
        now
          per cases by A21,A23,BORSUK_1:83,XXREAL_0:1,XXREAL_1:1;
          case
            0<r & r<1;
            hence thesis by A20,A22;
          end;
          case
            r=0;
            hence thesis by A12,A14,A22,XBOOLE_0:def 4;
          end;
          case
            r=1;
            hence thesis by A11,A15,A22,XBOOLE_0:def 4;
          end;
        end;
        hence thesis;
      end;
      hence thesis by A2,A6,Th18;
    end;
    case
A24:  ex r being Real st 0<r & r<1 & not f.r in Lower_Arc(P);
      now
        per cases;
        case
A25:      for r being Real st 0<r & r<1 holds f.r in Upper_Arc(P);
          P1 c= Upper_Arc(P)
          proof
            let y be set;
            assume y in P1;
            then consider x being set such that
A26:        x in dom f and
A27:        y=f.x by A18,FUNCT_1:def 5;
            reconsider r=x as Real by A17,A26,BORSUK_1:83;
A28:        r<=1 by A26,BORSUK_1:83,XXREAL_1:1;
            now
              per cases by A26,A28,BORSUK_1:83,XXREAL_0:1,XXREAL_1:1;
              case
                0<r & r<1;
                hence thesis by A25,A27;
              end;
              case
                r=0;
                hence thesis by A12,A14,A27,XBOOLE_0:def 4;
              end;
              case
                r=1;
                hence thesis by A11,A15,A27,XBOOLE_0:def 4;
              end;
            end;
            hence thesis;
          end;
          hence thesis by A2,A4,JORDAN6:59;
        end;
        case
          ex r being Real st 0<r & r<1 & not f.r in Upper_Arc(P);
          then consider r2 being Real such that
A29:      0<r2 and
A30:      r2<1 and
A31:      not f.r2 in Upper_Arc(P);
          r2 in [.0,1.] by A29,A30,XXREAL_1:1;
          then f.r2 in rng f by A17,BORSUK_1:83,FUNCT_1:def 5;
          then
A32:      f.r2 in Lower_Arc(P) by A3,A8,A18,A31,XBOOLE_0:def 3;
          consider r1 being Real such that
A33:      0<r1 and
A34:      r1<1 and
A35:      not f.r1 in Lower_Arc(P) by A24;
          r1 in [.0,1.] by A33,A34,XXREAL_1:1;
          then
A36:      f.r1 in rng f by A17,BORSUK_1:83,FUNCT_1:def 5;
          then
A37:      f.r1 in Upper_Arc(P) by A3,A8,A18,A35,XBOOLE_0:def 3;
          now
            per cases;
            case
A38:          r1<=r2;
              now
                per cases by A38,XXREAL_0:1;
                case
                  r1=r2;
                  hence contradiction by A3,A8,A18,A31,A35,A36,XBOOLE_0:def 3;
                end;
                case
A39:              r1<r2;
                  reconsider Q=P as non empty Subset of Euclid 2 by TOPREAL3:13
;
A40:              the carrier of (TOP-REAL 2)|P1=[#]((TOP-REAL 2)|P1)
                    .=P1 by PRE_TOPC:def 10;
                  the carrier of (TOP-REAL 2)|P=[#]((TOP-REAL 2)|P)
                    .=P by PRE_TOPC:def 10;
                  then rng f c= the carrier of (TOP-REAL 2)|P by A3,A40,
XBOOLE_1:1;
                  then reconsider
                  g=f as Function of I[01],(TOP-REAL 2)|P by A17,FUNCT_2:4;
                  P=P1 \/ P by A3,XBOOLE_1:12;
                  then
A41:              (TOP-REAL 2)|P1 is SubSpace of (TOP-REAL 2)|P by TOPMETR:5;
                  U2 \/ U3 =the carrier of ((Euclid 2)|Q) & (TOP-REAL 2)|
                  P=TopSpaceMetr(( Euclid 2)|Q) by A8,EUCLID:67,TOPMETR:def 2;
                  then consider r0 being Real such that
A42:              r1<=r0 and
A43:              r0<=r2 and
A44:              g.r0 in U2 /\ U3 by A19,A7,A9,A30,A33,A32,A37,A39,A41,Th17,
PRE_TOPC:56;
                  r0<1 by A30,A43,XXREAL_0:2;
                  then
A45:              r0 in dom f by A17,A33,A42,BORSUK_1:83,XXREAL_1:1;
A46:              1 in dom f by A17,BORSUK_1:83,XXREAL_1:1;
A47:              0 in dom f by A17,BORSUK_1:83,XXREAL_1:1;
                  now
                    per cases by A10,A44,TARSKI:def 2;
                    case
                      g.r0=W-min(P);
                      hence contradiction by A14,A16,A33,A42,A45,A47,
FUNCT_1:def 8;
                    end;
                    case
                      g.r0=E-max(P);
                      hence contradiction by A15,A16,A30,A43,A45,A46,
FUNCT_1:def 8;
                    end;
                  end;
                  hence contradiction;
                end;
              end;
              hence contradiction;
            end;
            case
A48:          r1>r2;
              reconsider Q=P as non empty Subset of Euclid 2 by TOPREAL3:13;
A49:          the carrier of (TOP-REAL 2)|P1=[#]((TOP-REAL 2)|P1)
                .=P1 by PRE_TOPC:def 10;
              the carrier of (TOP-REAL 2)|P=[#]((TOP-REAL 2)|P)
                .=P by PRE_TOPC:def 10;
              then rng f c= the carrier of (TOP-REAL 2)|P by A3,A49,XBOOLE_1:1;
              then reconsider g=f as Function of I[01],(TOP-REAL 2)|P by A17,
FUNCT_2:4;
              P=P1 \/ P by A3,XBOOLE_1:12;
              then
A50:          (TOP-REAL 2)|P1 is SubSpace of (TOP-REAL 2)|P by TOPMETR:5;
              U2 \/ U3 =the carrier of ((Euclid 2)|Q) & (TOP-REAL 2)|P=
              TopSpaceMetr(( Euclid 2)|Q) by A8,EUCLID:67,TOPMETR:def 2;
              then consider r0 being Real such that
A51:          r2<=r0 and
A52:          r0<=r1 and
A53:          g.r0 in U2 /\ U3 by A19,A7,A9,A29,A34,A32,A37,A48,A50,Th17,
PRE_TOPC:56;
              r0<1 by A34,A52,XXREAL_0:2;
              then
A54:          r0 in dom f by A17,A29,A51,BORSUK_1:83,XXREAL_1:1;
A55:          1 in dom f by A17,BORSUK_1:83,XXREAL_1:1;
A56:          0 in dom f by A17,BORSUK_1:83,XXREAL_1:1;
              now
                per cases by A10,A53,TARSKI:def 2;
                case
                  g.r0=W-min(P);
                  hence contradiction by A14,A16,A29,A51,A54,A56,FUNCT_1:def 8;
                end;
                case
                  g.r0=E-max(P);
                  hence contradiction by A15,A16,A34,A52,A54,A55,FUNCT_1:def 8;
                end;
              end;
              hence contradiction;
            end;
          end;
          hence contradiction;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;
