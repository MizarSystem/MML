:: On Some Points of a Simple Closed Curve. {P}art {II}
::  by Artur Korni{\l}owicz and Adam Grabowski
::
:: Received October 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies BOOLE, RELAT_1, RELAT_2, PRE_TOPC, COMPTS_1, EUCLID, TOPREAL2,
      JORDAN2C, TREES_1, JORDAN8, TOPREAL1, ORDINAL2, ARYTM, JORDAN6, JORDAN9,
      FINSEQ_1, FUNCT_1, PSCOMP_1, MCART_1, ARYTM_3, SEQ_1, SUBSET_1, METRIC_1,
      PCOMPS_1, GOBOARD5, FINSEQ_6, FINSEQ_4, GOBOARD2, ARYTM_1, MATRIX_1,
      GOBOARD1, GROUP_1, JORDAN10, TARSKI, GOBOARD9, SPPOL_1, JORDAN3,
      WEIERSTR, ABSVALUE, FUNCT_5, SEQ_2, LATTICES, JORDAN21, COMPLEX1,
      JORDAN19, KURATO_2, JORDAN1A, JORDAN1, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      XREAL_0, REAL_1, COMPLEX1, NAT_1, RELSET_1, FUNCT_2, SEQ_1, SEQ_4,
      NEWTON, FINSEQ_1, MATRIX_1, DOMAIN_1, STRUCT_0, PRE_TOPC, COMPTS_1,
      FINSEQ_4, FINSEQ_6, BINARITH, CONNSP_1, METRIC_1, PCOMPS_1, PSCOMP_1,
      EUCLID, TOPREAL1, TOPREAL2, GOBOARD1, GOBOARD2, WEIERSTR, SPPOL_1,
      GOBOARD5, TOPRNS_1, JORDAN2C, JORDAN1, JORDAN6, GOBOARD9, JORDAN8,
      JORDAN9, GOBRD13, TOPREAL6, JORDAN10, JORDAN1K, JORDAN1A, KURATO_2,
      JORDAN19, JORDAN21;
 constructors SETFAM_1, REAL_1, NAT_1, RCOMP_1, FINSEQ_4, NEWTON, REALSET1,
      BINARITH, CONNSP_1, NORMSP_1, MONOID_0, TOPRNS_1, TOPREAL4, GOBOARD2,
      JORDAN1, PSCOMP_1, GOBOARD9, JORDAN5C, JORDAN6, JORDAN2C, TOPREAL6,
      JORDAN1K, JORDAN21, JORDAN8, GOBRD13, JORDAN9, JORDAN10, JORDAN1A,
      KURATO_2, JORDAN19;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      INT_1, MEMBERED, FINSEQ_6, STRUCT_0, COMPTS_1, MONOID_0, EUCLID,
      TOPREAL1, TOPREAL2, GOBOARD2, SPPOL_2, TOPREAL5, SPRECT_1, SPRECT_2,
      JORDAN6, SPRECT_3, JORDAN2C, REVROT_1, TOPREAL6, JORDAN21, JORDAN8,
      JORDAN10;
 requirements SUBSET, BOOLE, REAL, NUMERALS, ARITHM;
 definitions TARSKI, SEQ_4, TOPRNS_1, JORDAN6, JORDAN21, EUCLID;
 theorems XBOOLE_1, JORDAN1A, JORDAN6, XBOOLE_0, JORDAN1K, EUCLID, TOPREAL1,
      TARSKI, JORDAN16, METRIC_1, REAL_2, SPRECT_3, TOPMETR, JORDAN7, GOBRD14,
      JORDAN1I, INT_1, BINARITH, GOBOARD5, FINSEQ_6, REVROT_1, SPRECT_2,
      GOBOARD7, JGRAPH_1, JORDAN1H, JORDAN9, GOBRD13, REAL_1, NAT_1, SEQ_2,
      JORDAN8, SUBSET_1, JORDAN2C, JORDAN10, PSCOMP_1, SPPOL_2, SPPOL_1, SEQ_4,
      FUNCT_2, WEIERSTR, JORDAN1G, TOPRNS_1, ABSVALUE, JORDAN19, KURATO_2,
      JORDAN21, XREAL_1, JORDAN1J, JORDAN1B, RELAT_1, RCOMP_1, JORDAN5D,
      TOPREAL3, JORDAN1, JORDAN1F, NEWTON, SPRECT_1, XXREAL_0, PEPIN, TOPREAL6,
      JCT_MISC;
 schemes TOPRNS_1;

begin :: Properties of the Approximations

 reserve C for Simple_closed_curve,
         i for Element of NAT;
Lm1:
  for r being real number, X being Subset of TOP-REAL 2 st r in proj2.:X
    ex x being Point of TOP-REAL 2 st x in X & proj2.x = r
    proof
      let r be real number,
          X be Subset of TOP-REAL 2;
      assume r in proj2.:X;
      then ex x being set st x in the carrier of TOP-REAL 2 &
       x in X & proj2.x = r by FUNCT_2:115;
      hence thesis;
    end;

theorem Th1:
  (Upper_Appr C).i c= Cl RightComp Cage(C,0)
proof
A1: (Upper_Appr C).i = Upper_Arc L~Cage (C,i) by JORDAN19:def 1;
A2: Upper_Arc L~Cage (C,i) c= L~Cage (C,i) by JORDAN6:76;
  L~Cage (C,i) c= Cl RightComp Cage(C,0) by JORDAN1H:54;
  hence thesis by A1,A2,XBOOLE_1:1;
end;

theorem Th2:
  (Lower_Appr C).i c= Cl RightComp Cage(C,0)
proof
A1: (Lower_Appr C).i = Lower_Arc L~Cage (C,i) by JORDAN19:def 2;
A2: Lower_Arc L~Cage (C,i) c= L~Cage (C,i) by JORDAN6:76;
  L~Cage (C,i) c= Cl RightComp Cage(C,0) by JORDAN1H:54;
  hence thesis by A1,A2,XBOOLE_1:1;
end;

registration
  let C be Simple_closed_curve;
  cluster Upper_Arc C -> connected;
coherence
proof
  Upper_Arc C is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
  hence thesis by JORDAN6:11;
end;
  cluster Lower_Arc C -> connected;
coherence
proof
  Lower_Arc C is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
  hence thesis by JORDAN6:11;
end;
end;

theorem
  (Upper_Appr C).i is compact connected
proof
  Upper_Arc L~Cage(C,i) is compact;
  hence (Upper_Appr C).i is compact by JORDAN19:def 1;
  Upper_Arc L~Cage(C,i) is connected;
  hence (Upper_Appr C).i is connected by JORDAN19:def 1;
end;

theorem
  (Lower_Appr C).i is compact connected
proof
  Lower_Arc L~Cage(C,i) is compact;
  hence (Lower_Appr C).i is compact by JORDAN19:def 2;
  Lower_Arc L~Cage(C,i) is connected;
  hence (Lower_Appr C).i is connected by JORDAN19:def 2;
end;

registration
  let C be Simple_closed_curve;
  cluster North_Arc C -> compact;
  coherence
  proof
    for i being Element of NAT
    holds (Upper_Appr C).i c= Cl RightComp Cage(C,0) by Th1;
    then Lim_inf Upper_Appr C is compact by KURATO_2:63;
   hence thesis by JORDAN19:def 3;
  end;
  cluster South_Arc C -> compact;
  coherence
  proof
    for i being Element of NAT
    holds (Lower_Appr C).i c= Cl RightComp Cage(C,0) by Th2;
    then Lim_inf Lower_Appr C is compact by KURATO_2:63;
   hence thesis by JORDAN19:def 4;
  end;
end;

:: Moved from JORDAN21, AK, 22.02.2006

reserve
  R for non empty Subset of TOP-REAL 2,
  j, k, m, n for Element of NAT;

Lm2: dom proj2 = the carrier of TOP-REAL 2 by FUNCT_2:def 1;

Lm3:
  1 <= len Gauge(R,n)
  proof
    4 <= len Gauge(R,n) by JORDAN8:13;
    hence 1 <= len Gauge(R,n) by XXREAL_0:2;
  end;

theorem Th5:
  [1,1] in Indices Gauge(R,n)
  proof
A1: len Gauge(R,n) = width Gauge(R,n) by JORDAN8:def 1;
    1 <= len Gauge(R,n) by Lm3;
    hence [1,1] in Indices Gauge(R,n) by A1,GOBOARD7:10;
  end;

theorem Th6:
  [1,2] in Indices Gauge(R,n)
  proof
A1: len Gauge(R,n) = width Gauge(R,n) by JORDAN8:def 1;
    4 <= len Gauge(R,n) by JORDAN8:13;
    then 1 <= len Gauge(R,n) & 2 <= len Gauge(R,n) by XXREAL_0:2;
    hence [1,2] in Indices Gauge(R,n) by A1,GOBOARD7:10;
  end;

theorem Th7:
  [2,1] in Indices Gauge(R,n)
  proof
A1: len Gauge(R,n) = width Gauge(R,n) by JORDAN8:def 1;
    4 <= len Gauge(R,n) by JORDAN8:13;
    then 1 <= len Gauge(R,n) & 2 <= len Gauge(R,n) by XXREAL_0:2;
    hence [2,1] in Indices Gauge(R,n) by A1,GOBOARD7:10;
  end;

theorem Th8:
  for C being non vertical non horizontal compact Subset of TOP-REAL 2 holds
  m > k & [i,j] in Indices Gauge(C,k) & [i,j+1] in Indices Gauge(C,k)
  implies
    dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i,j+1)) <
    dist(Gauge(C,k)*(i,j),Gauge(C,k)*(i,j+1))
  proof
    let C be non vertical non horizontal compact Subset of TOP-REAL 2;
    assume that
A1:   m > k and
A2:   [i,j] in Indices Gauge(C,k) and
A3:   [i,j+1] in Indices Gauge(C,k);
A4: 2|^m > 2|^k by A1,PEPIN:71;
A5: dist(Gauge(C,k)*(i,j),Gauge(C,k)*(i,j+1))
        = (N-bound C - S-bound C)/2|^k by A2,A3,GOBRD14:19;
A6: len Gauge(C,k) = width Gauge(C,k) by JORDAN8:def 1;
A7: len Gauge(C,m) = width Gauge(C,m) by JORDAN8:def 1;
A8: 1 <= i & 1 <= j by A2,GOBOARD5:1;
A9: len Gauge(C,k) < len Gauge(C,m) by A1,JORDAN1A:50;
    i <= len Gauge(C,k) by A2,GOBOARD5:1;
    then
A10: i <= len Gauge(C,m) by A9,XXREAL_0:2;
    j <= width Gauge(C,k) by A2,GOBOARD5:1;
    then
    j <= width Gauge(C,m) by A6,A7,A9,XXREAL_0:2;
    then
A11: [i,j] in Indices Gauge(C,m) by A8,A10,GOBOARD7:10;
A12: 1 <= j+1 by NAT_1:11;
    j+1 <= width Gauge(C,k) by A3,GOBOARD5:1;
    then
    j+1 <= width Gauge(C,m) by A6,A7,A9,XXREAL_0:2;
    then
A13: [i,j+1] in Indices Gauge(C,m) by A8,A10,A12,GOBOARD7:10;
A14: dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i,j+1))
        = (N-bound C - S-bound C)/2|^m by A11,A13,GOBRD14:19;
A15: 2|^k > 0 by NEWTON:102;
    N-bound C > S-bound C by SPRECT_1:34;
    then N-bound C - S-bound C > 0 by XREAL_1:52;
    hence thesis by A4,A5,A14,A15,REAL_2:200;
  end;

theorem Th9:
  for C being non vertical non horizontal compact Subset of TOP-REAL 2 holds
  m > k implies
    dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
    dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2))
  proof
    let C be non vertical non horizontal compact Subset of TOP-REAL 2;
    assume
A1:   m > k;
    [1,1] in Indices Gauge(C,k) & [1,1+1] in Indices Gauge(C,k) by Th5,Th6;
    hence thesis by A1,Th8;
  end;

theorem Th10:
  for C being non vertical non horizontal compact Subset of TOP-REAL 2 holds
  m > k & [i,j] in Indices Gauge(C,k) & [i+1,j] in Indices Gauge(C,k)
  implies
    dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i+1,j)) <
    dist(Gauge(C,k)*(i,j),Gauge(C,k)*(i+1,j))
  proof
    let C be non vertical non horizontal compact Subset of TOP-REAL 2;
    assume that
A1:   m > k and
A2:   [i,j] in Indices Gauge(C,k) and
A3:   [i+1,j] in Indices Gauge(C,k);
A4: 2|^m > 2|^k by A1,PEPIN:71;
A5: dist(Gauge(C,k)*(i,j),Gauge(C,k)*(i+1,j))
        = (E-bound C - W-bound C)/2|^k by A2,A3,GOBRD14:20;
A6: len Gauge(C,k) = width Gauge(C,k) by JORDAN8:def 1;
A7: len Gauge(C,m) = width Gauge(C,m) by JORDAN8:def 1;
A8: 1 <= i & 1 <= j by A2,GOBOARD5:1;
A9: len Gauge(C,k) < len Gauge(C,m) by A1,JORDAN1A:50;
    i <= len Gauge(C,k) by A2,GOBOARD5:1;
    then
A10: i <= len Gauge(C,m) by A9,XXREAL_0:2;
    j <= width Gauge(C,k) by A2,GOBOARD5:1;
    then
A11: j <= width Gauge(C,m) by A6,A7,A9,XXREAL_0:2;
    then
A12: [i,j] in Indices Gauge(C,m) by A8,A10,GOBOARD7:10;
A13: 1 <= i+1 by NAT_1:11;
    i+1 <= len Gauge(C,k) by A3,GOBOARD5:1;
    then
    i+1 <= len Gauge(C,m) by A9,XXREAL_0:2;
    then
A14: [i+1,j] in Indices Gauge(C,m) by A8,A11,A13,GOBOARD7:10;
A15: dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i+1,j))
        = (E-bound C - W-bound C)/2|^m by A12,A14,GOBRD14:20;
A16: 2|^k > 0 by NEWTON:102;
    E-bound C > W-bound C by SPRECT_1:33;
    then E-bound C - W-bound C > 0 by XREAL_1:52;
    hence thesis by A4,A5,A15,A16,REAL_2:200;
  end;

theorem Th11:
  for C being non vertical non horizontal compact Subset of TOP-REAL 2 holds
  m > k implies
    dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
    dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1))
  proof
    let C be non vertical non horizontal compact Subset of TOP-REAL 2;
    assume
A1:   m > k;
    [1,1] in Indices Gauge(C,k) & [1+1,1] in Indices Gauge(C,k) by Th5,Th7;
    hence thesis by A1,Th10;
  end;

theorem
  for r, t being real number holds r > 0 & t > 0 implies
  ex n being Element of NAT st i < n &
    dist(Gauge(C,n)*(1,1),Gauge(C,n)*(1,2)) < r &
    dist(Gauge(C,n)*(1,1),Gauge(C,n)*(2,1)) < t
  proof
    let r, t be real number;
    assume r > 0 & t > 0;
    then consider  n being Element of NAT such that
      1 < n and
A1:   dist(Gauge(C,n)*(1,1),Gauge(C,n)*(1,2)) < r &
      dist(Gauge(C,n)*(1,1),Gauge(C,n)*(2,1)) < t by GOBRD14:21;
    per cases;
    suppose
A2:   i < n;
    take n;
    thus thesis by A1,A2;
    end;
    suppose
A3:   i >= n;
    take i+1;
    thus
A4:   i < i+1 by NAT_1:13;
    then
A5: dist(Gauge(C,i+1)*(1,1),Gauge(C,i+1)*(2,1)) <
      dist(Gauge(C,i)*(1,1),Gauge(C,i)*(2,1)) by Th11;
A6: i > n or i = n by A3,REAL_1:def 5;
    then dist(Gauge(C,i)*(1,1),Gauge(C,i)*(2,1)) <=
      dist(Gauge(C,n)*(1,1),Gauge(C,n)*(2,1)) by Th11;
    then
A7: dist(Gauge(C,i+1)*(1,1),Gauge(C,i+1)*(2,1)) <
      dist(Gauge(C,n)*(1,1),Gauge(C,n)*(2,1)) by A5,XXREAL_0:2;
A8: dist(Gauge(C,i+1)*(1,1),Gauge(C,i+1)*(1,2)) <
      dist(Gauge(C,i)*(1,1),Gauge(C,i)*(1,2)) by A4,Th9;
    dist(Gauge(C,i)*(1,1),Gauge(C,i)*(1,2)) <=
      dist(Gauge(C,n)*(1,1),Gauge(C,n)*(1,2)) by A6,Th9;
    then dist(Gauge(C,i+1)*(1,1),Gauge(C,i+1)*(1,2)) <
      dist(Gauge(C,n)*(1,1),Gauge(C,n)*(1,2)) by A8,XXREAL_0:2;
    hence dist(Gauge(C,i+1)*(1,1),Gauge(C,i+1)*(1,2)) < r &
      dist(Gauge(C,i+1)*(1,1),Gauge(C,i+1)*(2,1)) < t by A1,A7,XXREAL_0:2;
  end;
end;

theorem Th13:
  0 < n implies
  sup (proj2.:(L~Cage(C,n) /\
       LSeg(Gauge(C,n)*(Center Gauge(C,n),1),
            Gauge(C,n)*(Center Gauge(C,n),len Gauge(C,n)))))
  =
  sup (proj2.:(L~Cage(C,n) /\
       Vertical_Line ((E-bound L~Cage(C,n) + W-bound L~Cage(C,n)) / 2)))
  proof
    assume
A1:   0 < n;
    set f = Cage(C,n),
        G = Gauge(C,n),
        c = Center G;
    set Y = proj2.:(L~f /\ Vertical_Line ((E-bound L~f + W-bound L~f) / 2));
    set X = proj2.:(L~f /\ LSeg(G*(c,1),G*(c,len G)));
A2: len G = width G by JORDAN8:def 1;
A3: 1 <= len G by Lm3;
A4: 1 <= c & c <= len G by JORDAN1B:12,14;
    then
A5: G*(c,1)`1 = G*(c,len G)`1 by A2,A3,GOBOARD5:3;
A6: G*(c,1)`1 = (W-bound C + E-bound C) / 2 by A1,A2,A3,JORDAN1G:43
             .= (W-bound L~f + E-bound L~f) / 2 by JORDAN1G:41;
    then G*(c,1) in Vertical_Line ((E-bound L~f + W-bound L~f) / 2) &
      G*(c,len G) in Vertical_Line ((E-bound L~f + W-bound L~f) / 2)
        by A5;
    then
    LSeg(G*(c,1),G*(c,len G)) c=
      Vertical_Line ((E-bound L~f + W-bound L~f) / 2) by JORDAN1A:23;
    then L~f /\ L~f /\ LSeg(G*(c,1),G*(c,len G)) c=
      L~f /\ Vertical_Line ((E-bound L~f + W-bound L~f) / 2) by XBOOLE_1:26;
    then
A7: X c= Y by RELAT_1:156;
A8: Y is bounded_above by JORDAN21:13;
    then
A9: X is bounded_above by A7,RCOMP_1:4;
A10: LSeg(G*(c,1),G*(c,len G)) meets Upper_Arc L~f by JORDAN1B:34;
    Upper_Arc L~f c= L~f by JORDAN6:76;
    then
    LSeg(G*(c,1),G*(c,len G)) meets L~f by A10,XBOOLE_1:63;
    then
    L~f /\ LSeg(G*(c,1),G*(c,len G)) is non empty by XBOOLE_0:def 7;
    then
A11: X is non empty by Lm2,RELAT_1:152;
    then
A12: Y is non empty by A7,XBOOLE_1:3;
A13: for r being real number st r in X holds r<=sup Y by A7,A8,SEQ_4:def 4;
    for s being real number st 0<s ex r being real number st r in X & sup Y-s<r
    proof
      let s be real number;
      assume 0<s;
      then consider r being real number such that
A14:     r in Y and
A15:     sup Y-s<r by A8,A12,SEQ_4:def 4;
      take r;
      consider x being Point of TOP-REAL 2 such that
A16:     x in L~f /\ Vertical_Line ((E-bound L~f + W-bound L~f) / 2) and
A17:     proj2.x = r by A14,Lm1;
A18:   x in L~f by A16,XBOOLE_0:def 3;
      x in Vertical_Line ((E-bound L~f + W-bound L~f)/2) by A16,XBOOLE_0:def 3;
      then
A19:   G*(c,1)`1 = x`1 by A6,JORDAN6:34;
      GoB f = G by JORDAN1H:52;
      then G*(c,1)`2 <= x`2 & x`2 <= G*(c,len G)`2
        by A2,A4,A18,JORDAN5D:35,36;
      then x in LSeg(G*(c,1),G*(c,len G)) by A5,A19,GOBOARD7:8;
      then x in L~f /\ LSeg(G*(c,1),G*(c,len G)) by A18,XBOOLE_0:def 3;
      hence r in X by A17,FUNCT_2:43;
      thus thesis by A15;
    end;
    hence thesis by A9,A11,A13,SEQ_4:def 4;
  end;

theorem Th14:
  0 < n implies
  inf (proj2.:(L~Cage(C,n) /\
       LSeg(Gauge(C,n)*(Center Gauge(C,n),1),
            Gauge(C,n)*(Center Gauge(C,n),len Gauge(C,n)))))
  =
  inf (proj2.:(L~Cage(C,n) /\
       Vertical_Line ((E-bound L~Cage(C,n) + W-bound L~Cage(C,n)) / 2)))
  proof
    assume
A1:   0 < n;
    set f = Cage(C,n),
        G = Gauge(C,n),
        c = Center G;
    set Y = proj2.:(L~f /\ Vertical_Line ((E-bound L~f + W-bound L~f) / 2));
    set X = proj2.:(L~f /\ LSeg(G*(c,1),G*(c,len G)));
A2: len G = width G by JORDAN8:def 1;
A3: 1 <= len G by Lm3;
A4: 1 <= c & c <= len G by JORDAN1B:12,14;
    then
A5: G*(c,1)`1 = G*(c,len G)`1 by A2,A3,GOBOARD5:3;
A6: G*(c,1)`1 = (W-bound C + E-bound C) / 2 by A1,A2,A3,JORDAN1G:43
             .= (W-bound L~f + E-bound L~f) / 2 by JORDAN1G:41;
    then G*(c,1) in Vertical_Line ((E-bound L~f + W-bound L~f) / 2) &
      G*(c,len G) in Vertical_Line ((E-bound L~f + W-bound L~f) / 2)
        by A5;
    then
    LSeg(G*(c,1),G*(c,len G)) c=
      Vertical_Line ((E-bound L~f + W-bound L~f) / 2) by JORDAN1A:23;
    then L~f /\ L~f /\ LSeg(G*(c,1),G*(c,len G)) c=
      L~f /\ Vertical_Line ((E-bound L~f + W-bound L~f) / 2) by XBOOLE_1:26;
    then
A7: X c= Y by RELAT_1:156;
A8: Y is bounded_below by JORDAN21:13;
    then
A9: X is bounded_below by A7,RCOMP_1:3;
A10: LSeg(G*(c,1),G*(c,len G)) meets Upper_Arc L~f by JORDAN1B:34;
    Upper_Arc L~f c= L~f by JORDAN6:76;
    then
    LSeg(G*(c,1),G*(c,len G)) meets L~f by A10,XBOOLE_1:63;
    then
    L~f /\ LSeg(G*(c,1),G*(c,len G)) is non empty by XBOOLE_0:def 7;
    then
A11: X is non empty by Lm2,RELAT_1:152;
    then
A12: Y is non empty by A7,XBOOLE_1:3;
A13: for r being real number st r in X holds inf Y<=r by A7,A8,SEQ_4:def 5;
    for s being real number st 0<s ex r being real number st r in X & r<inf Y+s
    proof
      let s be real number;
      assume 0<s;
      then consider r being real number such that
A14:     r in Y and
A15:     r<inf Y+s by A8,A12,SEQ_4:def 5;
      take r;
      consider x being Point of TOP-REAL 2 such that
A16:     x in L~f /\ Vertical_Line ((E-bound L~f + W-bound L~f) / 2) and
A17:     proj2.x = r by A14,Lm1;
A18:   x in L~f by A16,XBOOLE_0:def 3;
      x in Vertical_Line ((E-bound L~f + W-bound L~f)/2) by A16,XBOOLE_0:def 3;
      then
A19:   G*(c,1)`1 = x`1 by A6,JORDAN6:34;
      GoB f = G by JORDAN1H:52;
      then G*(c,1)`2 <= x`2 & x`2 <= G*(c,len G)`2
        by A2,A4,A18,JORDAN5D:35,36;
      then x in LSeg(G*(c,1),G*(c,len G)) by A5,A19,GOBOARD7:8;
      then x in L~f /\ LSeg(G*(c,1),G*(c,len G)) by A18,XBOOLE_0:def 3;
      hence r in X by A17,FUNCT_2:43;
      thus thesis by A15;
    end;
    hence thesis by A9,A11,A13,SEQ_4:def 5;
  end;

theorem Th15:
  0 < n implies
  UMP L~Cage(C,n) =
    |[ (E-bound L~Cage(C,n) + W-bound L~Cage(C,n)) / 2,
      sup (proj2.:(L~Cage(C,n) /\
           LSeg(Gauge(C,n)*(Center Gauge(C,n),1),
                Gauge(C,n)*(Center Gauge(C,n),len Gauge(C,n))))) ]|
                by Th13;

theorem Th16:
  0 < n implies
  LMP L~Cage(C,n) =
    |[ (E-bound L~Cage(C,n) + W-bound L~Cage(C,n)) / 2,
      inf (proj2.:(L~Cage(C,n) /\
           LSeg(Gauge(C,n)*(Center Gauge(C,n),1),
                Gauge(C,n)*(Center Gauge(C,n),len Gauge(C,n))))) ]|
          by Th14;

theorem Th17:
  (UMP C)`2 < (UMP L~Cage(C,n))`2
  proof
    set p = UMP L~Cage(C,n),
        u = UMP C,
        w = (W-bound C + E-bound C) / 2;
    assume
A1:   not thesis;
A2: u`1 = w by EUCLID:56;
A3: w = (W-bound L~Cage(C,n) + E-bound L~Cage(C,n)) / 2 by JORDAN1G:41;
    then
A4: p`1 = w by EUCLID:56;
A5: p = |[p`1,p`2]| & u = |[u`1,u`2]| by EUCLID:57;
A6: C misses L~Cage(C,n) by JORDAN10:5;
A7: u in C by JORDAN21:43;
A8: p in L~Cage(C,n) by JORDAN21:43;
    per cases by A1,REAL_1:def 5;
    suppose u`2 = p`2;
    hence thesis by A2,A4,A5,A6,A7,A8,XBOOLE_0:3;
    end;
    suppose
A9:   u`2 > p`2;
A10: p`2 = sup (proj2.:(L~Cage(C,n) /\ Vertical_Line w)) by A3,EUCLID:56;
A11: proj2.:(L~Cage(C,n) /\ Vertical_Line w) is non empty bounded_above
      by A3,JORDAN21:12,13;
    north_halfline p \ {p} is convex by JORDAN21:6;
    then
A12: north_halfline p \ {p} is connected by JORDAN1:9;
    north_halfline p \ {p} misses L~Cage(C,n)
    proof
      assume north_halfline p \ {p} meets L~Cage(C,n);
      then consider x being set such that
A13:     x in north_halfline p \ {p} and
A14:     x in L~Cage(C,n) by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A14;
A15:   x in north_halfline p by A13,XBOOLE_0:def 4;
      then
A16:   x`2 >= p`2 by TOPREAL1:def 12;
A17:   x`1 = w by A4,A15,TOPREAL1:def 12;
      not x in {p} by A13,XBOOLE_0:def 4;
      then x <> p by TARSKI:def 1;
      then
A18:   x`2 <> p`2 by A4,A17,TOPREAL3:11;
      x in Vertical_Line w by A17;
      then
A19:   x in L~Cage(C,n) /\ Vertical_Line w by A14,XBOOLE_0:def 3;
      proj2.x = x`2 by PSCOMP_1:def 29;
      then x`2 in proj2.:(L~Cage(C,n) /\ Vertical_Line w) by A19,FUNCT_2:43;
      then x`2 <= p`2 by A10,A11,SEQ_4:def 4;
      hence contradiction by A16,A18,REAL_1:def 5;
    end;
    then
A20: north_halfline p \ {p} c= UBD L~Cage(C,n) or
      north_halfline p \ {p} c= BDD L~Cage(C,n) by A12,JORDAN1K:19;
A21: north_halfline p is not Bounded by JORDAN2C:130;
A22: BDD L~Cage(C,n) is Bounded by JORDAN2C:114;
    {p} is Bounded by JORDAN21:1;
    then
A23: north_halfline p \ {p} is not Bounded by A21,TOPREAL6:99;
A24: UBD L~Cage(C,n) c= UBD C by JORDAN10:13;
A25: u in north_halfline p by A2,A4,A9,TOPREAL1:def 12;
    not u in {p} by A9,TARSKI:def 1;
    then u in north_halfline p \ {p} by A25,XBOOLE_0:def 4;
    then
A26: u in UBD L~Cage(C,n) by A20,A22,A23,JORDAN2C:16;
    UBD C misses C by JORDAN21:10;
    hence thesis by A7,A24,A26,XBOOLE_0:3;
  end;
  end;

theorem Th18:
  (LMP C)`2 > (LMP L~Cage(C,n))`2
  proof
    set p = LMP L~Cage(C,n),
        u = LMP C,
        w = (W-bound C + E-bound C) / 2;
    assume
A1:   not thesis;
A2: u`1 = w by EUCLID:56;
A3: w = (W-bound L~Cage(C,n) + E-bound L~Cage(C,n)) / 2 by JORDAN1G:41;
    then
A4: p`1 = w by EUCLID:56;
A5: p = |[p`1,p`2]| & u = |[u`1,u`2]| by EUCLID:57;
A6: C misses L~Cage(C,n) by JORDAN10:5;
A7: u in C by JORDAN21:44;
A8: p in L~Cage(C,n) by JORDAN21:44;
    per cases by A1,REAL_1:def 5;
    suppose u`2 = p`2;
    hence thesis by A2,A4,A5,A6,A7,A8,XBOOLE_0:3;
    end;
    suppose
A9:   u`2 < p`2;
A10: p`2 = inf (proj2.:(L~Cage(C,n) /\ Vertical_Line w)) by A3,EUCLID:56;
A11: proj2.:(L~Cage(C,n) /\ Vertical_Line w) is non empty bounded_below
      by A3,JORDAN21:12,13;
    south_halfline p \ {p} is convex by JORDAN21:7;
    then
A12: south_halfline p \ {p} is connected by JORDAN1:9;
    south_halfline p \ {p} misses L~Cage(C,n)
    proof
      assume south_halfline p \ {p} meets L~Cage(C,n);
      then consider x being set such that
A13:     x in south_halfline p \ {p} and
A14:     x in L~Cage(C,n) by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A14;
A15:   x in south_halfline p by A13,XBOOLE_0:def 4;
      then
A16:   x`2 <= p`2 by TOPREAL1:def 14;
A17:   x`1 = w by A4,A15,TOPREAL1:def 14;
      not x in {p} by A13,XBOOLE_0:def 4;
      then x <> p by TARSKI:def 1;
      then
A18:   x`2 <> p`2 by A4,A17,TOPREAL3:11;
      x in Vertical_Line w by A17;
      then
A19:   x in L~Cage(C,n) /\ Vertical_Line w by A14,XBOOLE_0:def 3;
      proj2.x = x`2 by PSCOMP_1:def 29;
      then x`2 in proj2.:(L~Cage(C,n) /\ Vertical_Line w) by A19,FUNCT_2:43;
      then x`2 >= p`2 by A10,A11,SEQ_4:def 5;
      hence contradiction by A16,A18,REAL_1:def 5;
    end;
    then
A20: south_halfline p \ {p} c= UBD L~Cage(C,n) or
      south_halfline p \ {p} c= BDD L~Cage(C,n) by A12,JORDAN1K:19;
A21: south_halfline p is not Bounded by JORDAN2C:131;
A22: BDD L~Cage(C,n) is Bounded by JORDAN2C:114;
    {p} is Bounded by JORDAN21:1;
    then
A23: south_halfline p \ {p} is not Bounded by A21,TOPREAL6:99;
A24: UBD L~Cage(C,n) c= UBD C by JORDAN10:13;
A25: u in south_halfline p by A2,A4,A9,TOPREAL1:def 14;
    not u in {p} by A9,TARSKI:def 1;
    then u in south_halfline p \ {p} by A25,XBOOLE_0:def 4;
    then
A26: u in UBD L~Cage(C,n) by A20,A22,A23,JORDAN2C:16;
    UBD C misses C by JORDAN21:10;
    hence thesis by A7,A24,A26,XBOOLE_0:3;
  end;
  end;

 Lm4:
  UMP Upper_Arc L~Cage(C,n) in Upper_Arc L~Cage(C,n) by JORDAN21:43;

 Lm5:
  LMP Lower_Arc L~Cage(C,n) in Lower_Arc L~Cage(C,n) by JORDAN21:44;

canceled 2;

theorem Th21:
  0 < n implies ex i being Element of NAT st 1 <= i & i <= len Gauge(C,n) &
    UMP L~Cage(C,n) = Gauge(C,n)*(Center Gauge(C,n),i)
  proof
    assume
A1:   0 < n;
    set f = Cage(C,n),
        G = Gauge(C,n),
        w = Center G;
A2: UMP L~f =
    |[ (E-bound L~f + W-bound L~f) / 2,
       sup (proj2.:(L~f /\ LSeg(G*(w,1),G*(w,len G)))) ]| by A1,Th15;
A3: f is_sequence_on G by JORDAN9:def 1;
A4: LSeg(G*(w,1),G*(w,len G)) meets Upper_Arc L~f by JORDAN1B:34;
    Upper_Arc L~f c= L~f by JORDAN6:76;
    then
A5: LSeg(G*(w,1),G*(w,len G)) meets L~f by A4,XBOOLE_1:63;
A6: len G = width G by JORDAN8:def 1;
A7: 1 <= len G by Lm3;
    1 <= w & w <= len G by JORDAN1B:12,14;
    then
    [w,1] in Indices G & [w,len G] in Indices G by A6,A7,GOBOARD7:10;
    then consider i such that
A8:   1 <= i & i <= len G and
A9:   G*(w,i)`2 = sup(proj2.:(LSeg(G*(w,1),G*(w,len G)) /\ L~f))
        by A3,A5,A7,JORDAN1F:2;
    take i;
    thus 1 <= i & i <= len G by A8;
A10: (UMP L~f)`2 = sup (proj2.:(L~f /\ LSeg(G*(w,1),G*(w,len G))))
      by A2,EUCLID:56;
A11: (UMP L~f)`1 = (E-bound L~f + W-bound L~f) / 2 by EUCLID:56;
    G*(w,i)`1 = (W-bound C + E-bound C) / 2 by A1,A6,A8,JORDAN1G:43;
    then (UMP L~f)`1 = G*(w,i)`1 by A11,JORDAN1G:41;
    hence thesis by A9,A10,TOPREAL3:11;
  end;

theorem Th22:
  0 < n implies ex i being Element of NAT st 1 <= i & i <= len Gauge(C,n) &
    LMP L~Cage(C,n) = Gauge(C,n)*(Center Gauge(C,n),i)
  proof
    assume
A1:   0 < n;
    set f = Cage(C,n),
        G = Gauge(C,n),
        w = Center G;
A2: LMP L~f =
    |[ (E-bound L~f + W-bound L~f) / 2,
       inf (proj2.:(L~f /\ LSeg(G*(w,1),G*(w,len G)))) ]| by A1,Th16;
A3: f is_sequence_on G by JORDAN9:def 1;
A4: LSeg(G*(w,1),G*(w,len G)) meets Upper_Arc L~f by JORDAN1B:34;
    Upper_Arc L~f c= L~f by JORDAN6:76;
    then
A5: LSeg(G*(w,1),G*(w,len G)) meets L~f by A4,XBOOLE_1:63;
A6: len G = width G by JORDAN8:def 1;
A7: 1 <= len G by Lm3;
    1 <= w & w <= len G by JORDAN1B:12,14;
    then
    [w,1] in Indices G & [w,len G] in Indices G by A6,A7,GOBOARD7:10;
    then consider i such that
A8:   1 <= i & i <= len G and
A9:   G*(w,i)`2 = inf(proj2.:(LSeg(G*(w,1),G*(w,len G)) /\ L~f))
        by A3,A5,A7,JORDAN1F:1;
    take i;
    thus 1 <= i & i <= len G by A8;
A10: (LMP L~f)`2 = inf (proj2.:(L~f /\ LSeg(G*(w,1),G*(w,len G))))
      by A2,EUCLID:56;
A11: (LMP L~f)`1 = (E-bound L~f + W-bound L~f) / 2 by EUCLID:56;
    G*(w,i)`1 = (W-bound C + E-bound C) / 2 by A1,A6,A8,JORDAN1G:43;
    then (LMP L~f)`1 = G*(w,i)`1 by A11,JORDAN1G:41;
    hence thesis by A9,A10,TOPREAL3:11;
  end;

theorem Th23:
  0 < n implies UMP L~Cage(C,n) = UMP Upper_Arc L~Cage(C,n)
  proof
    set f = Cage(C,n);
    set w = (E-bound C + W-bound C) / 2;
    assume
A1:   0 < n;
    then
A2: 0+1 <= n by NAT_1:13;
    then
A3: n-'1+1 = n by BINARITH:53;
A4: Upper_Arc L~f c= L~f by JORDAN6:76;
A5: W-bound C + E-bound C = W-bound L~f + E-bound L~f by JORDAN1G:41;
    then
A6: proj2.:(L~f /\ Vertical_Line w) is bounded_above by JORDAN21:13;
A7: W-bound L~f = W-bound Upper_Arc L~f &
      E-bound L~f = E-bound Upper_Arc L~f by JORDAN21:26,27;
    then
A8: Upper_Arc L~f /\ Vertical_Line
     ((W-bound Upper_Arc L~f + E-bound Upper_Arc L~f) / 2) is non empty
       by JORDAN21:30;
    now
      assume
A9:     not UMP L~f in Upper_Arc L~f;
A10:   UMP L~f in L~f by JORDAN21:43;
      Upper_Arc L~f \/ Lower_Arc L~f = L~f by JORDAN6:def 9;
      then
A11:   UMP L~f in Lower_Arc L~f by A9,A10,XBOOLE_0:def 2;
      consider j being Element of NAT such that
A12:     1 <= j & j <= len Gauge(C,n) and
A13:     UMP L~f = Gauge(C,n)*(Center Gauge(C,n),j) by A1,Th21;
      set a = Gauge(C,1)*(Center Gauge(C,1),len Gauge(C,1)),
          b = Gauge(C,n)*(Center Gauge(C,n),j),
          L = LSeg(a,b);
      len Gauge(C,1) = width Gauge(C,1) &
      len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
      then L meets Upper_Arc L~f by A3,A11,A12,A13,JORDAN19:5;
      then consider x being set such that
A14:     x in L and
A15:     x in Upper_Arc L~f by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A14;
A16:   1 <= len Gauge(C,1) by Lm3;
      then
A17:   a`1 = w by JORDAN1A:59;
A18:   b`1 = w by A1,A12,A16,A17,JORDAN1A:57;
      then
A19:   L is vertical by A17,SPPOL_1:37;
A20:   1 <= Center Gauge(C,n) & Center Gauge(C,n) <= len Gauge(C,n)
        by JORDAN1B:12,14;
      1 <= Center Gauge(C,1) & Center Gauge(C,1) <= len Gauge(C,1)
        by JORDAN1B:12,14; then
A21:   Gauge(C,1)*(Center Gauge(C,1),len Gauge(C,1))`2 >=
       Gauge(C,n)*(Center Gauge(C,n),len Gauge(C,n))`2
        by A2,A20,JORDAN1A:61;
      len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
      then Gauge(C,n)*(Center Gauge(C,n),len Gauge(C,n))`2 >=
       Gauge(C,n)*(Center Gauge(C,n),j)`2 by A12,A20,SPRECT_3:24;
      then a`2 >= b`2 by A21,XXREAL_0:2;
      then
A22:   x`2 >= b`2 by A14,TOPREAL1:10;
      a in L by TOPREAL1:6;
      then
A23:   x`1 = w by A14,A17,A19,SPPOL_1:def 3;
      then x in Vertical_Line w;
      then
A24:   x in Upper_Arc L~f /\ Vertical_Line w by A15,XBOOLE_0:def 3;
      then
A25:   (UMP Upper_Arc L~f)`2 >= x`2 by A5,A7,JORDAN21:41;
      (UMP L~f)`2 >= (UMP Upper_Arc L~f)`2 by A4,A5,A6,A7,A24,JORDAN21:56;
      then b`2 >= x`2 by A13,A25,XXREAL_0:2;
      then b`2 = x`2 by A22,XXREAL_0:1;
      hence contradiction by A9,A13,A15,A18,A23,TOPREAL3:11;
    end;
    hence thesis by A4,A5,A6,A7,A8,JORDAN21:58;
  end;

theorem Th24:
  0 < n implies LMP L~Cage(C,n) = LMP Lower_Arc L~Cage(C,n)
  proof
    set f = Cage(C,n);
    set w = (E-bound C + W-bound C) / 2;
    assume
A1:   0 < n;
    then
A2: 0+1 <= n by NAT_1:13;
    then
A3: n-'1+1 = n by BINARITH:53;
A4: Lower_Arc L~f c= L~f by JORDAN6:76;
A5: W-bound C + E-bound C = W-bound L~f + E-bound L~f by JORDAN1G:41;
    then
A6: proj2.:(L~f /\ Vertical_Line w) is bounded_below by JORDAN21:13;
A7: W-bound L~f = W-bound Lower_Arc L~f &
      E-bound L~f = E-bound Lower_Arc L~f by JORDAN21:28,29;
    then
A8: Lower_Arc L~f /\ Vertical_Line
     ((W-bound Lower_Arc L~f + E-bound Lower_Arc L~f) / 2) is non empty
       by JORDAN21:31;
    now
      assume
A9:     not LMP L~f in Lower_Arc L~f;
A10:   LMP L~f in L~f by JORDAN21:44;
      Upper_Arc L~f \/ Lower_Arc L~f = L~f by JORDAN6:def 9;
      then
A11:   LMP L~f in Upper_Arc L~f by A9,A10,XBOOLE_0:def 2;
      consider j being Element of NAT such that
A12:     1 <= j & j <= len Gauge(C,n) and
A13:     LMP L~f = Gauge(C,n)*(Center Gauge(C,n),j) by A1,Th22;
      set a = Gauge(C,1)*(Center Gauge(C,1),1),
          b = Gauge(C,n)*(Center Gauge(C,n),j),
          L = LSeg(a,b);
A14:   len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
      then L meets Lower_Arc L~f by A3,A11,A12,A13,JORDAN1J:62;
      then consider x being set such that
A15:     x in L and
A16:     x in Lower_Arc L~f by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A15;
A17:   1 <= len Gauge(C,1) by Lm3;
      then
A18:   a`1 = w by JORDAN1A:59;
A19:   b`1 = w by A1,A12,A17,A18,JORDAN1A:57;
      then
A20:   L is vertical by A18,SPPOL_1:37;
A21:   1 <= Center Gauge(C,n) & Center Gauge(C,n) <= len Gauge(C,n)
        by JORDAN1B:12,14;
      1 <= Center Gauge(C,1) & Center Gauge(C,1) <= len Gauge(C,1)
        by JORDAN1B:12,14; then
A22:   Gauge(C,1)*(Center Gauge(C,1),1)`2 <= Gauge(C,n)*(Center Gauge(C,n),1)`2
        by A2,A21,JORDAN1A:64;
      Gauge(C,n)*(Center Gauge(C,n),1)`2 <= Gauge(C,n)*(Center Gauge(C,n),j)`2
        by A12,A14,A21,SPRECT_3:24;
      then a`2 <= b`2 by A22,XXREAL_0:2;
      then
A23:   x`2 <= b`2 by A15,TOPREAL1:10;
      a in L by TOPREAL1:6;
      then
A24:   x`1 = w by A15,A18,A20,SPPOL_1:def 3;
      then x in Vertical_Line w;
      then
A25:   x in Lower_Arc L~f /\ Vertical_Line w by A16,XBOOLE_0:def 3;
      then
A26:   (LMP Lower_Arc L~f)`2 <= x`2 by A5,A7,JORDAN21:42;
      (LMP L~f)`2 <= (LMP Lower_Arc L~f)`2 by A4,A5,A6,A7,A25,JORDAN21:57;
      then b`2 <= x`2 by A13,A26,XXREAL_0:2;
      then b`2 = x`2 by A23,XXREAL_0:1;
      hence contradiction by A9,A13,A16,A19,A24,TOPREAL3:11;
    end;
    hence thesis by A4,A5,A6,A7,A8,JORDAN21:59;
  end;

theorem Th25:
  0 < n implies (UMP C)`2 < (UMP Upper_Arc L~Cage(C,n))`2
  proof
    assume 0 < n;
    then UMP Upper_Arc L~Cage(C,n) = UMP L~Cage(C,n) by Th23;
    hence thesis by Th17;
  end;

theorem Th26:
  0 < n implies (LMP Lower_Arc L~Cage(C,n))`2 < (LMP C)`2
  proof
    assume 0 < n;
    then LMP Lower_Arc L~Cage(C,n) = LMP L~Cage(C,n) by Th24;
    hence thesis by Th18;
  end;

theorem Th27:
  i <= j implies (UMP L~Cage(C,j))`2 <= (UMP L~Cage(C,i))`2
  proof
    assume
A1:   i <= j;
    set w = (E-bound C + W-bound C) / 2,
        ui = UMP L~Cage(C,i),
        uj = UMP L~Cage(C,j);
A2: W-bound L~Cage(C,j) + E-bound L~Cage(C,j) =
     W-bound C + E-bound C by JORDAN1G:41;
    then
A3: uj`2 = sup (proj2.:(L~Cage(C,j) /\ Vertical_Line w)) by EUCLID:56;
A4: W-bound L~Cage(C,i) + E-bound L~Cage(C,i) =
     W-bound C + E-bound C by JORDAN1G:41;
    then
A5: ui`2 = sup (proj2.:(L~Cage(C,i) /\ Vertical_Line w)) by EUCLID:56;
A6: proj2.:(L~Cage(C,j) /\ Vertical_Line w) is non empty bounded_above
      by A2,JORDAN21:12,13;
A7: proj2.:(L~Cage(C,i) /\ Vertical_Line w) is non empty bounded_above
      by A4,JORDAN21:12,13;
    assume (UMP L~Cage(C,j))`2 > (UMP L~Cage(C,i))`2;
    then uj`2 - ui`2 > 0 by XREAL_1:52;
    then consider r being real number such that
A8:   r in proj2.:(L~Cage(C,j) /\ Vertical_Line w) and
A9:   sup (proj2.:(L~Cage(C,j) /\ Vertical_Line w)) - (uj`2 - ui`2) < r
        by A6,SEQ_4:def 4;
    consider x being Point of TOP-REAL 2 such that
A10:   x in L~Cage(C,j) /\ Vertical_Line w and
A11:   proj2.x = r by A8,Lm1;
A12: LeftComp(Cage(C,i)) c= LeftComp(Cage(C,j)) by A1,JORDAN1H:55;
A13: x`2 = r by A11,PSCOMP_1:def 29;
    north_halfline x misses L~Cage(C,i)
    proof
      assume north_halfline x meets L~Cage(C,i);
      then consider y being set such that
A14:     y in north_halfline x and
A15:     y in L~Cage(C,i) by XBOOLE_0:3;
      reconsider y as Point of TOP-REAL 2 by A15;
A16:   y`2 >= x`2 by A14,TOPREAL1:def 12;
A17:   x in Vertical_Line w by A10,XBOOLE_0:def 3;
      y`1 = x`1 by A14,TOPREAL1:def 12
         .= w by A17,JORDAN6:34;
      then y in Vertical_Line w;
      then
A18:   y in L~Cage(C,i) /\ Vertical_Line w by A15,XBOOLE_0:def 3;
      proj2.y = y`2 by PSCOMP_1:def 29;
      then y`2 in proj2.:(L~Cage(C,i) /\ Vertical_Line w) by A18,FUNCT_2:43;
      then y`2 <= ui`2 by A5,A7,SEQ_4:def 4;
      hence contradiction by A3,A9,A13,A16,XXREAL_0:2;
    end;
    then
A19: north_halfline x c= UBD L~Cage(C,i) by JORDAN2C:137;
    x in north_halfline x by TOPREAL1:45;
    then x in UBD L~Cage(C,i) by A19;
    then
A20: x in LeftComp Cage(C,i) by GOBRD14:46;
    x in L~Cage(C,j) by A10,XBOOLE_0:def 3;
    then LeftComp Cage(C,j) meets L~Cage(C,j) by A12,A20,XBOOLE_0:3;
    hence thesis by SPRECT_3:43;
  end;

theorem Th28:
  i <= j implies (LMP L~Cage(C,i))`2 <= (LMP L~Cage(C,j))`2
  proof
    assume
A1:   i <= j;
    set w = (E-bound C + W-bound C) / 2,
        ui = LMP L~Cage(C,i),
        uj = LMP L~Cage(C,j);
A2: W-bound L~Cage(C,j) + E-bound L~Cage(C,j) =
     W-bound C + E-bound C by JORDAN1G:41;
    then
A3: uj`2 = inf (proj2.:(L~Cage(C,j) /\ Vertical_Line w)) by EUCLID:56;
A4: W-bound L~Cage(C,i) + E-bound L~Cage(C,i) =
     W-bound C + E-bound C by JORDAN1G:41;
    then
A5: ui`2 = inf (proj2.:(L~Cage(C,i) /\ Vertical_Line w)) by EUCLID:56;
A6: proj2.:(L~Cage(C,j) /\ Vertical_Line w) is non empty bounded_below
      by A2,JORDAN21:12,13;
A7: proj2.:(L~Cage(C,i) /\ Vertical_Line w) is non empty bounded_below
      by A4,JORDAN21:12,13;
    assume (LMP L~Cage(C,i))`2 > (LMP L~Cage(C,j))`2;
    then ui`2 - uj`2 > 0 by XREAL_1:52;
    then consider r being real number such that
A8:   r in proj2.:(L~Cage(C,j) /\ Vertical_Line w) and
A9:   r < inf (proj2.:(L~Cage(C,j) /\ Vertical_Line w)) + (ui`2 - uj`2)
        by A6,SEQ_4:def 5;
    consider x being Point of TOP-REAL 2 such that
A10:   x in L~Cage(C,j) /\ Vertical_Line w and
A11:   proj2.x = r by A8,Lm1;
A12: LeftComp(Cage(C,i)) c= LeftComp(Cage(C,j)) by A1,JORDAN1H:55;
A13: x`2 = r by A11,PSCOMP_1:def 29;
    south_halfline x misses L~Cage(C,i)
    proof
      assume south_halfline x meets L~Cage(C,i);
      then consider y being set such that
A14:     y in south_halfline x and
A15:     y in L~Cage(C,i) by XBOOLE_0:3;
      reconsider y as Point of TOP-REAL 2 by A15;
A16:   y`2 <= x`2 by A14,TOPREAL1:def 14;
A17:   x in Vertical_Line w by A10,XBOOLE_0:def 3;
      y`1 = x`1 by A14,TOPREAL1:def 14
         .= w by A17,JORDAN6:34;
      then y in Vertical_Line w;
      then
A18:   y in L~Cage(C,i) /\ Vertical_Line w by A15,XBOOLE_0:def 3;
      proj2.y = y`2 by PSCOMP_1:def 29;
      then y`2 in proj2.:(L~Cage(C,i) /\ Vertical_Line w) by A18,FUNCT_2:43;
      then y`2 >= ui`2 by A5,A7,SEQ_4:def 5;
      hence contradiction by A3,A9,A13,A16,XXREAL_0:2;
    end;
    then
A19: south_halfline x c= UBD L~Cage(C,i) by JORDAN2C:136;
    x in south_halfline x by TOPREAL1:45;
    then x in UBD L~Cage(C,i) by A19;
    then
A20: x in LeftComp Cage(C,i) by GOBRD14:46;
    x in L~Cage(C,j) by A10,XBOOLE_0:def 3;
    then LeftComp Cage(C,j) meets L~Cage(C,j) by A12,A20,XBOOLE_0:3;
    hence thesis by SPRECT_3:43;
  end;

theorem Th29:
  0 < i & i <= j implies
  (UMP Upper_Arc L~Cage(C,j))`2 <= (UMP Upper_Arc L~Cage(C,i))`2
  proof
    assume that
A1:   0 < i and
A2:   i <= j;
      (UMP Upper_Arc L~Cage(C,i))`2 = (UMP L~Cage(C,i))`2 &
      (UMP Upper_Arc L~Cage(C,j))`2 = (UMP L~Cage(C,j))`2 by A1,A2,Th23;
    hence thesis by A2,Th27;
  end;

theorem Th30:
  0 < i & i <= j implies
  (LMP Lower_Arc L~Cage(C,i))`2 <= (LMP Lower_Arc L~Cage(C,j))`2
  proof
    assume that
A1:   0 < i and
A2:   i <= j;
      (LMP Lower_Arc L~Cage(C,i))`2 = (LMP L~Cage(C,i))`2 &
      (LMP Lower_Arc L~Cage(C,j))`2 = (LMP L~Cage(C,j))`2 by A1,A2,Th24;
    hence thesis by A2,Th28;
  end;

begin :: On Special Points of Approximations

theorem Th31:
  W-min C in North_Arc C
  proof
A1: North_Arc C = Lim_inf Upper_Appr C by JORDAN19:def 3;
    reconsider w = W-min C as Point of Euclid 2 by TOPMETR:16;
    for r being real number st r > 0
      ex k being Element of NAT st
     for m being Element of NAT st m > k holds (Upper_Appr C).m meets Ball(w,r)
    proof
      let r be real number;
      assume r > 0;
      then r/2 > 0 by SEQ_2:3;
      then consider k being Element of NAT such that
        1 < k and
A2:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < r/2 and
A3:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < r/2 by GOBRD14:21;
      take k;
      let m be Element of NAT such that
A4:     m > k;
      reconsider p = W-min L~Cage(C,m) as Point of Euclid 2 by TOPMETR:16;
A5:   (Upper_Appr C).m = Upper_Arc L~Cage(C,m) by JORDAN19:def 1;
A6:   W-min L~Cage(C,m) in Upper_Arc L~Cage(C,m) by JORDAN7:1;
A7:   W-min L~Cage(C,m) in rng Cage(C,m) by SPRECT_2:47;
      len Cage(C,m) > 4 by GOBOARD7:36;
      then
A8:   1+1 <= len Cage(C,m) by XXREAL_0:2;
A9:   W-min L~Cage(C,m) = Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
        by A7,FINSEQ_6:98;
A10:   1+1 <= len Rotate(Cage(C,m),W-min L~Cage(C,m)) by A8,REVROT_1:14; then
A11:   Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
        in LSeg(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) by TOPREAL1:27;
      left_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) /\
        right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) =
        LSeg(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
          by A10,GOBOARD5:32;
      then
A12:   W-min L~Cage(C,m) in right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
        by A9,A11,XBOOLE_0:def 3;
A13:   W-min C in right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
        by JORDAN1I:8;
A14:   right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
       = right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,
                GoB Rotate(Cage(C,m),W-min L~Cage(C,m))) by A10,JORDAN1H:29
      .= right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,GoB Cage(C,m))
         by REVROT_1:28
      .= right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,Gauge(C,m))
         by JORDAN1H:52;
      Cage(C,m) is_sequence_on Gauge(C,m) by JORDAN9:def 1;
      then
A15:   Rotate(Cage(C,m),W-min L~Cage(C,m)) is_sequence_on Gauge(C,m)
       by REVROT_1:34;
      Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
       = W-min L~Rotate(Cage(C,m),W-min L~Cage(C,m)) by A9,REVROT_1:33;
      then
      consider i, j being Element of NAT such that
A16:     [i,j] in Indices Gauge(C,m) &
        [i,j+1] in Indices Gauge(C,m) and
A17:     Rotate(Cage(C,m),W-min L~Cage(C,m))/.1 = Gauge(C,m)*(i,j) &
        Rotate(Cage(C,m),W-min L~Cage(C,m))/.(1+1) = Gauge(C,m)*(i,j+1)
          by A10,A15,JORDAN1I:23;
A18:   right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
       = cell(Gauge(C,m),i,j) by A10,A14,A15,A16,A17,GOBRD13:23;
A19:   1 <= i & 1 <= j & j+1 <= width Gauge(C,m) by A16,GOBOARD5:1;
A20:   1 <= i+1 by NAT_1:11;
A21:   j <= width Gauge(C,m) by A16,GOBOARD5:1;
      i < len Gauge(C,m) by A10,A15,A16,A17,JORDAN1I:16;
      then
A22:   i+1 <= len Gauge(C,m) by NAT_1:13;
      then
A23:   [i+1,j] in Indices Gauge(C,m) by A19,A20,A21,GOBOARD7:10;
      Gauge(C,m)*(i,j)`1 <= (W-min C)`1 &
        (W-min C)`1 <= Gauge(C,m)*(i+1,j)`1 &
        Gauge(C,m)*(i,j)`2 <= (W-min C)`2 &
        (W-min C)`2 <= Gauge(C,m)*(i,j+1)`2 &
        Gauge(C,m)*(i,j)`1 <= (W-min L~Cage(C,m))`1 &
        (W-min L~Cage(C,m))`1 <= Gauge(C,m)*(i+1,j)`1 &
        Gauge(C,m)*(i,j)`2 <= (W-min L~Cage(C,m))`2 &
        (W-min L~Cage(C,m))`2 <= Gauge(C,m)*(i,j+1)`2
          by A12,A13,A18,A19,A22,JORDAN9:19;
      then
A24:   dist(W-min C,W-min L~Cage(C,m)) <=
       (Gauge(C,m)*(i+1,j)`1-Gauge(C,m)*(i,j)`1) +
       (Gauge(C,m)*(i,j+1)`2-Gauge(C,m)*(i,j)`2) by TOPREAL6:104;
A25:   [1,1] in Indices Gauge(C,m) by Th5;
A26:   [1,1+1] in Indices Gauge(C,m) by Th6;
A27:   [1+1,1] in Indices Gauge(C,m) by Th7;
A28:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
          = (E-bound C - W-bound C)/2|^m by A25,A27,GOBRD14:20;
A29:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
          = (N-bound C - S-bound C)/2|^m by A25,A26,GOBRD14:19;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) by A4,Th9;
      then
A30:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) < r/2 by A2,XXREAL_0:2;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) by A4,Th11;
      then
A31:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) < r/2 by A3,XXREAL_0:2;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
         = dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i,j+1)) by A16,A29,GOBRD14:19;
      then
A32:   Gauge(C,m)*(i,j+1)`2 - Gauge(C,m)*(i,j)`2 < r/2 by A16,A30,GOBRD14:16;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
         = dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i+1,j)) by A16,A23,A28,GOBRD14:20;
      then
      Gauge(C,m)*(i+1,j)`1 - Gauge(C,m)*(i,j)`1 < r/2 by A16,A23,A31,GOBRD14:15
;
      then
      (Gauge(C,m)*(i+1,j)`1-Gauge(C,m)*(i,j)`1) +
       (Gauge(C,m)*(i,j+1)`2-Gauge(C,m)*(i,j)`2) < r/2 + r/2
        by A32,XREAL_1:10;
      then dist(W-min C,W-min L~Cage(C,m)) < r by A24,XXREAL_0:2;
      then dist(w,p) < r by TOPREAL6:def 1;
      then p in Ball(w,r) by METRIC_1:12;
      hence (Upper_Appr C).m meets Ball(w,r) by A5,A6,XBOOLE_0:3;
    end;
    hence W-min C in North_Arc C by A1,KURATO_2:50;
  end;

theorem Th32:
  E-max C in North_Arc C
  proof
A1: North_Arc C = Lim_inf Upper_Appr C by JORDAN19:def 3;
    reconsider w = E-max C as Point of Euclid 2 by TOPMETR:16;
    for r being real number st r > 0
      ex k being Element of NAT st
     for m being Element of NAT st m > k holds (Upper_Appr C).m meets Ball(w,r)
    proof
      let r be real number;
      assume r > 0;
      then r/2 > 0 by SEQ_2:3;
      then consider k being Element of NAT such that
        1 < k and
A2:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < r/2 and
A3:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < r/2 by GOBRD14:21;
      take k;
      let m be Element of NAT such that
A4:     m > k;
      reconsider p = E-max L~Cage(C,m) as Point of Euclid 2 by TOPMETR:16;
A5:   (Upper_Appr C).m = Upper_Arc L~Cage(C,m) by JORDAN19:def 1;
A6:   E-max L~Cage(C,m) in Upper_Arc L~Cage(C,m) by JORDAN7:1;
A7:   E-max L~Cage(C,m) in rng Cage(C,m) by SPRECT_2:50;
      len Cage(C,m) > 4 by GOBOARD7:36;
      then
A8:   1+1 <= len Cage(C,m) by XXREAL_0:2;
A9:   E-max L~Cage(C,m) = Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
        by A7,FINSEQ_6:98;
A10:   1+1 <= len Rotate(Cage(C,m),E-max L~Cage(C,m)) by A8,REVROT_1:14; then
A11:   Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
        in LSeg(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) by TOPREAL1:27;
      left_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) /\
        right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) =
        LSeg(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
          by A10,GOBOARD5:32;
      then
A12:   E-max L~Cage(C,m) in right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
        by A9,A11,XBOOLE_0:def 3;
A13:   E-max C in right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
        by JORDAN1I:9;
A14:   right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
       = right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,
                GoB Rotate(Cage(C,m),E-max L~Cage(C,m))) by A10,JORDAN1H:29
      .= right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,GoB Cage(C,m))
         by REVROT_1:28
      .= right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,Gauge(C,m))
         by JORDAN1H:52;
      Cage(C,m) is_sequence_on Gauge(C,m) by JORDAN9:def 1;
      then
A15:   Rotate(Cage(C,m),E-max L~Cage(C,m)) is_sequence_on Gauge(C,m)
       by REVROT_1:34;
      Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
       = E-max L~Rotate(Cage(C,m),E-max L~Cage(C,m)) by A9,REVROT_1:33;
      then
      consider i, j being Element of NAT such that
A16:     [i,j+1] in Indices Gauge(C,m) &
        [i,j] in Indices Gauge(C,m) and
A17:     Rotate(Cage(C,m),E-max L~Cage(C,m))/.1 = Gauge(C,m)*(i,j+1) &
        Rotate(Cage(C,m),E-max L~Cage(C,m))/.(1+1) = Gauge(C,m)*(i,j)
          by A10,A15,JORDAN1I:25;
A18:   right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
       = cell(Gauge(C,m),i-'1,j) by A10,A14,A15,A16,A17,GOBRD13:29;
A19:   1 <= i & 1 <= j & j+1 <= width Gauge(C,m) by A16,GOBOARD5:1;
      i > 1 by A10,A15,A16,A17,JORDAN1I:18;
      then
A20:   i - 1 > 1 - 1 by REAL_1:92;
      then i - 1 is Element of NAT by INT_1:16;
      then
A21:   i - 1 >= 0 + 1 by A20,NAT_1:13;
      then
A22:   i-'1+1 = i by BINARITH:61;
A23:   i <= len Gauge(C,m) by A16,GOBOARD5:1;
A24:   i-'1 = i-1 by A20,BINARITH:def 3;
      i-1 <= i by XREAL_1:148;
      then
A25:   i-'1 <= len Gauge(C,m) & j <= width Gauge(C,m)
        by A16,A23,A24,XXREAL_0:2,GOBOARD5:1;
      then
A26:   [i-'1,j] in Indices Gauge(C,m) by A19,A21,A24,GOBOARD7:10;
      1 <= j+1 by NAT_1:11;
      then
A27:   [i-'1,j+1] in Indices Gauge(C,m) by A19,A21,A24,A25,GOBOARD7:10;
      Gauge(C,m)*(i-'1,j)`1 <= (E-max C)`1 &
        (E-max C)`1 <= Gauge(C,m)*(i-'1+1,j)`1 &
        Gauge(C,m)*(i-'1,j)`2 <= (E-max C)`2 &
        (E-max C)`2 <= Gauge(C,m)*(i-'1,j+1)`2 &
        Gauge(C,m)*(i-'1,j)`1 <= (E-max L~Cage(C,m))`1 &
        (E-max L~Cage(C,m))`1 <= Gauge(C,m)*(i-'1+1,j)`1 &
        Gauge(C,m)*(i-'1,j)`2 <= (E-max L~Cage(C,m))`2 &
        (E-max L~Cage(C,m))`2 <= Gauge(C,m)*(i-'1,j+1)`2
          by A12,A13,A18,A19,A21,A22,A23,JORDAN9:19;
      then
A28:   dist(E-max C,E-max L~Cage(C,m)) <=
       (Gauge(C,m)*(i-'1+1,j)`1-Gauge(C,m)*(i-'1,j)`1) +
       (Gauge(C,m)*(i-'1,j+1)`2-Gauge(C,m)*(i-'1,j)`2) by TOPREAL6:104;
A29:   [1,1] in Indices Gauge(C,m) by Th5;
A30:   [1,1+1] in Indices Gauge(C,m) by Th6;
A31:   [1+1,1] in Indices Gauge(C,m) by Th7;
A32:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
          = (E-bound C - W-bound C)/2|^m by A29,A31,GOBRD14:20;
A33:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
          = (N-bound C - S-bound C)/2|^m by A29,A30,GOBRD14:19;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) by A4,Th9;
      then
A34:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) < r/2 by A2,XXREAL_0:2;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) by A4,Th11;
      then
A35:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) < r/2 by A3,XXREAL_0:2;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
         = dist(Gauge(C,m)*(i-'1,j),Gauge(C,m)*(i-'1,j+1))
           by A26,A27,A33,GOBRD14:19;
      then
A36:   Gauge(C,m)*(i-'1,j+1)`2 - Gauge(C,m)*(i-'1,j)`2 < r/2
        by A26,A27,A34,GOBRD14:16;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
         = dist(Gauge(C,m)*(i-'1,j),Gauge(C,m)*(i-'1+1,j))
           by A16,A22,A26,A32,GOBRD14:20;
      then
      Gauge(C,m)*(i-'1+1,j)`1 - Gauge(C,m)*(i-'1,j)`1 < r/2
        by A16,A22,A26,A35,GOBRD14:15;
      then
      (Gauge(C,m)*(i-'1+1,j)`1-Gauge(C,m)*(i-'1,j)`1) +
       (Gauge(C,m)*(i-'1,j+1)`2-Gauge(C,m)*(i-'1,j)`2) < r/2 + r/2
        by A36,XREAL_1:10;
      then dist(E-max C,E-max L~Cage(C,m)) < r by A28,XXREAL_0:2;
      then dist(w,p) < r by TOPREAL6:def 1;
      then p in Ball(w,r) by METRIC_1:12;
      hence (Upper_Appr C).m meets Ball(w,r) by A5,A6,XBOOLE_0:3;
    end;
    hence E-max C in North_Arc C by A1,KURATO_2:50;
  end;

theorem Th33:
  W-min C in South_Arc C
  proof
A1: South_Arc C = Lim_inf Lower_Appr C by JORDAN19:def 4;
    reconsider w = W-min C as Point of Euclid 2 by TOPMETR:16;
    for r being real number st r > 0
      ex k being Element of NAT st
     for m being Element of NAT st m > k holds (Lower_Appr C).m meets Ball(w,r)
    proof
      let r be real number;
      assume r > 0;
      then r/2 > 0 by SEQ_2:3;
      then consider k being Element of NAT such that
        1 < k and
A2:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < r/2 and
A3:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < r/2 by GOBRD14:21;
      take k;
      let m be Element of NAT such that
A4:     m > k;
      reconsider p = W-min L~Cage(C,m) as Point of Euclid 2 by TOPMETR:16;
A5:   (Lower_Appr C).m = Lower_Arc L~Cage(C,m) by JORDAN19:def 2;
A6:   W-min L~Cage(C,m) in Lower_Arc L~Cage(C,m) by JORDAN7:1;
A7:   W-min L~Cage(C,m) in rng Cage(C,m) by SPRECT_2:47;
      len Cage(C,m) > 4 by GOBOARD7:36;
      then
A8:   1+1 <= len Cage(C,m) by XXREAL_0:2;
A9:   W-min L~Cage(C,m) = Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
        by A7,FINSEQ_6:98;
A10:   1+1 <= len Rotate(Cage(C,m),W-min L~Cage(C,m)) by A8,REVROT_1:14; then
A11:   Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
        in LSeg(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) by TOPREAL1:27;
      left_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) /\
        right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) =
        LSeg(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
          by A10,GOBOARD5:32;
      then
A12:   W-min L~Cage(C,m) in right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
        by A9,A11,XBOOLE_0:def 3;
A13:   W-min C in right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
        by JORDAN1I:8;
A14:   right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
       = right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,
                GoB Rotate(Cage(C,m),W-min L~Cage(C,m))) by A10,JORDAN1H:29
      .= right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,GoB Cage(C,m))
         by REVROT_1:28
      .= right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,Gauge(C,m))
         by JORDAN1H:52;
      Cage(C,m) is_sequence_on Gauge(C,m) by JORDAN9:def 1;
      then
A15:   Rotate(Cage(C,m),W-min L~Cage(C,m)) is_sequence_on Gauge(C,m)
       by REVROT_1:34;
      Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
       = W-min L~Rotate(Cage(C,m),W-min L~Cage(C,m)) by A9,REVROT_1:33;
      then
      consider i, j being Element of NAT such that
A16:     [i,j] in Indices Gauge(C,m) &
        [i,j+1] in Indices Gauge(C,m) and
A17:     Rotate(Cage(C,m),W-min L~Cage(C,m))/.1 = Gauge(C,m)*(i,j) &
        Rotate(Cage(C,m),W-min L~Cage(C,m))/.(1+1) = Gauge(C,m)*(i,j+1)
          by A10,A15,JORDAN1I:23;
A18:   right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
       = cell(Gauge(C,m),i,j) by A10,A14,A15,A16,A17,GOBRD13:23;
A19:   1 <= i & 1 <= j & j+1 <= width Gauge(C,m) by A16,GOBOARD5:1;
A20:   1 <= i+1 by NAT_1:11;
A21:   j <= width Gauge(C,m) by A16,GOBOARD5:1;
      i < len Gauge(C,m) by A10,A15,A16,A17,JORDAN1I:16;
      then
A22:   i+1 <= len Gauge(C,m) by NAT_1:13;
      then
A23:   [i+1,j] in Indices Gauge(C,m) by A19,A20,A21,GOBOARD7:10;
      Gauge(C,m)*(i,j)`1 <= (W-min C)`1 &
        (W-min C)`1 <= Gauge(C,m)*(i+1,j)`1 &
        Gauge(C,m)*(i,j)`2 <= (W-min C)`2 &
        (W-min C)`2 <= Gauge(C,m)*(i,j+1)`2 &
        Gauge(C,m)*(i,j)`1 <= (W-min L~Cage(C,m))`1 &
        (W-min L~Cage(C,m))`1 <= Gauge(C,m)*(i+1,j)`1 &
        Gauge(C,m)*(i,j)`2 <= (W-min L~Cage(C,m))`2 &
        (W-min L~Cage(C,m))`2 <= Gauge(C,m)*(i,j+1)`2
          by A12,A13,A18,A19,A22,JORDAN9:19;
      then
A24:   dist(W-min C,W-min L~Cage(C,m)) <=
       (Gauge(C,m)*(i+1,j)`1-Gauge(C,m)*(i,j)`1) +
       (Gauge(C,m)*(i,j+1)`2-Gauge(C,m)*(i,j)`2) by TOPREAL6:104;
A25:   [1,1] in Indices Gauge(C,m) by Th5;
A26:   [1,1+1] in Indices Gauge(C,m) by Th6;
A27:   [1+1,1] in Indices Gauge(C,m) by Th7;
A28:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
          = (E-bound C - W-bound C)/2|^m by A25,A27,GOBRD14:20;
A29:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
          = (N-bound C - S-bound C)/2|^m by A25,A26,GOBRD14:19;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) by A4,Th9;
      then
A30:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) < r/2 by A2,XXREAL_0:2;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) by A4,Th11;
      then
A31:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) < r/2 by A3,XXREAL_0:2;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
         = dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i,j+1)) by A16,A29,GOBRD14:19;
      then
A32:   Gauge(C,m)*(i,j+1)`2 - Gauge(C,m)*(i,j)`2 < r/2 by A16,A30,GOBRD14:16;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
         = dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i+1,j)) by A16,A23,A28,GOBRD14:20;
      then
      Gauge(C,m)*(i+1,j)`1 - Gauge(C,m)*(i,j)`1 < r/2 by A16,A23,A31,GOBRD14:15
;
      then
      (Gauge(C,m)*(i+1,j)`1-Gauge(C,m)*(i,j)`1) +
       (Gauge(C,m)*(i,j+1)`2-Gauge(C,m)*(i,j)`2) < r/2 + r/2
        by A32,XREAL_1:10;
      then dist(W-min C,W-min L~Cage(C,m)) < r by A24,XXREAL_0:2;
      then dist(w,p) < r by TOPREAL6:def 1;
      then p in Ball(w,r) by METRIC_1:12;
      hence (Lower_Appr C).m meets Ball(w,r) by A5,A6,XBOOLE_0:3;
    end;
    hence W-min C in South_Arc C by A1,KURATO_2:50;
  end;

theorem Th34:
  E-max C in South_Arc C
  proof
A1: South_Arc C = Lim_inf Lower_Appr C by JORDAN19:def 4;
    reconsider w = E-max C as Point of Euclid 2 by TOPMETR:16;
    for r being real number st r > 0
      ex k being Element of NAT st
     for m being Element of NAT st m > k holds (Lower_Appr C).m meets Ball(w,r)
    proof
      let r be real number;
      assume r > 0;
      then r/2 > 0 by SEQ_2:3;
      then consider k being Element of NAT such that
        1 < k and
A2:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < r/2 and
A3:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < r/2 by GOBRD14:21;
      take k;
      let m be Element of NAT such that
A4:     m > k;
      reconsider p = E-max L~Cage(C,m) as Point of Euclid 2 by TOPMETR:16;
A5:   (Lower_Appr C).m = Lower_Arc L~Cage(C,m) by JORDAN19:def 2;
A6:   E-max L~Cage(C,m) in Lower_Arc L~Cage(C,m) by JORDAN7:1;
A7:   E-max L~Cage(C,m) in rng Cage(C,m) by SPRECT_2:50;
      len Cage(C,m) > 4 by GOBOARD7:36;
      then
A8:   1+1 <= len Cage(C,m) by XXREAL_0:2;
A9:   E-max L~Cage(C,m) = Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
        by A7,FINSEQ_6:98;
A10:   1+1 <= len Rotate(Cage(C,m),E-max L~Cage(C,m)) by A8,REVROT_1:14; then
A11:   Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
        in LSeg(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) by TOPREAL1:27;
      left_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) /\
        right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) =
        LSeg(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
          by A10,GOBOARD5:32;
      then
A12:   E-max L~Cage(C,m) in right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
        by A9,A11,XBOOLE_0:def 3;
A13:   E-max C in right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
        by JORDAN1I:9;
A14:   right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
       = right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,
                GoB Rotate(Cage(C,m),E-max L~Cage(C,m))) by A10,JORDAN1H:29
      .= right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,GoB Cage(C,m))
         by REVROT_1:28
      .= right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,Gauge(C,m))
         by JORDAN1H:52;
      Cage(C,m) is_sequence_on Gauge(C,m) by JORDAN9:def 1;
      then
A15:   Rotate(Cage(C,m),E-max L~Cage(C,m)) is_sequence_on Gauge(C,m)
       by REVROT_1:34;
      Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
       = E-max L~Rotate(Cage(C,m),E-max L~Cage(C,m)) by A9,REVROT_1:33;
      then
      consider i, j being Element of NAT such that
A16:     [i,j+1] in Indices Gauge(C,m) &
        [i,j] in Indices Gauge(C,m) and
A17:     Rotate(Cage(C,m),E-max L~Cage(C,m))/.1 = Gauge(C,m)*(i,j+1) &
        Rotate(Cage(C,m),E-max L~Cage(C,m))/.(1+1) = Gauge(C,m)*(i,j)
          by A10,A15,JORDAN1I:25;
A18:   right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
       = cell(Gauge(C,m),i-'1,j) by A10,A14,A15,A16,A17,GOBRD13:29;
A19:   1 <= i & 1 <= j & j+1 <= width Gauge(C,m) by A16,GOBOARD5:1;
      i > 1 by A10,A15,A16,A17,JORDAN1I:18;
      then
A20:   i - 1 > 1 - 1 by REAL_1:92;
      then i - 1 is Element of NAT by INT_1:16;
      then
A21:   i - 1 >= 0 + 1 by A20,NAT_1:13;
      then
A22:   i-'1+1 = i by BINARITH:61;
A23:   i <= len Gauge(C,m) by A16,GOBOARD5:1;
A24:   i-'1 = i-1 by A20,BINARITH:def 3;
      i-1 <= i by XREAL_1:148;
      then
A25:   i-'1 <= len Gauge(C,m) & j <= width Gauge(C,m)
        by A16,A23,A24,XXREAL_0:2,GOBOARD5:1;
      then
A26:   [i-'1,j] in Indices Gauge(C,m) by A19,A21,A24,GOBOARD7:10;
      1 <= j+1 by NAT_1:11;
      then
A27:   [i-'1,j+1] in Indices Gauge(C,m) by A19,A21,A24,A25,GOBOARD7:10;
      Gauge(C,m)*(i-'1,j)`1 <= (E-max C)`1 &
        (E-max C)`1 <= Gauge(C,m)*(i-'1+1,j)`1 &
        Gauge(C,m)*(i-'1,j)`2 <= (E-max C)`2 &
        (E-max C)`2 <= Gauge(C,m)*(i-'1,j+1)`2 &
        Gauge(C,m)*(i-'1,j)`1 <= (E-max L~Cage(C,m))`1 &
        (E-max L~Cage(C,m))`1 <= Gauge(C,m)*(i-'1+1,j)`1 &
        Gauge(C,m)*(i-'1,j)`2 <= (E-max L~Cage(C,m))`2 &
        (E-max L~Cage(C,m))`2 <= Gauge(C,m)*(i-'1,j+1)`2
          by A12,A13,A18,A19,A21,A22,A23,JORDAN9:19;
      then
A28:   dist(E-max C,E-max L~Cage(C,m)) <=
       (Gauge(C,m)*(i-'1+1,j)`1-Gauge(C,m)*(i-'1,j)`1) +
       (Gauge(C,m)*(i-'1,j+1)`2-Gauge(C,m)*(i-'1,j)`2) by TOPREAL6:104;
A29:   [1,1] in Indices Gauge(C,m) by Th5;
A30:   [1,1+1] in Indices Gauge(C,m) by Th6;
A31:   [1+1,1] in Indices Gauge(C,m) by Th7;
A32:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
          = (E-bound C - W-bound C)/2|^m by A29,A31,GOBRD14:20;
A33:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
          = (N-bound C - S-bound C)/2|^m by A29,A30,GOBRD14:19;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) by A4,Th9;
      then
A34:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) < r/2 by A2,XXREAL_0:2;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) by A4,Th11;
      then
A35:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) < r/2 by A3,XXREAL_0:2;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
         = dist(Gauge(C,m)*(i-'1,j),Gauge(C,m)*(i-'1,j+1))
           by A26,A27,A33,GOBRD14:19;
      then
A36:   Gauge(C,m)*(i-'1,j+1)`2 - Gauge(C,m)*(i-'1,j)`2 < r/2
        by A26,A27,A34,GOBRD14:16;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
         = dist(Gauge(C,m)*(i-'1,j),Gauge(C,m)*(i-'1+1,j))
           by A16,A22,A26,A32,GOBRD14:20;
      then
      Gauge(C,m)*(i-'1+1,j)`1 - Gauge(C,m)*(i-'1,j)`1 < r/2
        by A16,A22,A26,A35,GOBRD14:15;
      then
      (Gauge(C,m)*(i-'1+1,j)`1-Gauge(C,m)*(i-'1,j)`1) +
       (Gauge(C,m)*(i-'1,j+1)`2-Gauge(C,m)*(i-'1,j)`2) < r/2 + r/2
        by A36,XREAL_1:10;
      then dist(E-max C,E-max L~Cage(C,m)) < r by A28,XXREAL_0:2;
      then dist(w,p) < r by TOPREAL6:def 1;
      then p in Ball(w,r) by METRIC_1:12;
      hence (Lower_Appr C).m meets Ball(w,r) by A5,A6,XBOOLE_0:3;
    end;
    hence E-max C in South_Arc C by A1,KURATO_2:50;
  end;

theorem Th35:
  UMP C in North_Arc C
  proof
    set w = (W-bound C + E-bound C) / 2;
    set p = UMP C;
    set U = {UMP Upper_Arc L~Cage(C,n) where n is Element of NAT: 0 < n};
    {UMP Upper_Arc L~Cage(C,n) where n is Element of NAT: 0 < n} c=
      the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in {UMP Upper_Arc L~Cage(C,n) where n is Element of NAT: 0 < n};
      then ex n being Element of NAT st x = UMP Upper_Arc L~Cage(C,n) & 0 < n;
      hence thesis;
    end;
    then reconsider U as Subset of TOP-REAL 2;
    set n0 = N-bound L~Cage(C,1);
    set H = LSeg(p,|[w,n0]|);
    set q = inf(proj2.:(H /\ U));
    set S = LSeg(|[w,q]|, |[w,n0]|);
A1: |[w,N-bound C]|`1 = w by EUCLID:56;
A2: |[w,N-bound C]|`2 = N-bound C by EUCLID:56;
A3: |[w,n0]|`1 = w by EUCLID:56;
A4: |[w,n0]|`2 = n0 by EUCLID:56;
A5: |[w,q]|`1 = w by EUCLID:56;
A6: |[w,q]|`2 = q by EUCLID:56;
A7: |[w,q]| in S by TOPREAL1:6;
A8: p`1 = w by EUCLID:56;
A9: S is vertical by A3,A5,SPPOL_1:37;
A10: LSeg(p, |[w, N-bound C]|) /\ C = {p} by JORDAN21:47;
A11: for n being Element of NAT holds (UMP Upper_Arc L~Cage(C,n))`1 = w
    proof
      let n be Element of NAT;
A12:   W-bound L~Cage(C,n) + E-bound L~Cage(C,n) = W-bound C + E-bound C
        by JORDAN1G:41;
      thus (UMP Upper_Arc L~Cage(C,n))`1
         = (W-bound Upper_Arc L~Cage(C,n) + E-bound Upper_Arc L~Cage(C,n))/2
           by EUCLID:56
        .= (W-bound L~Cage(C,n) + E-bound Upper_Arc L~Cage(C,n))/2
           by JORDAN21:26
        .= w by A12,JORDAN21:27;
    end;
A13: for n being Element of NAT st 0 < n
       holds (UMP Upper_Arc L~Cage(C,n))`2 in proj2.:(H /\ U)
    proof
      let n be Element of NAT;
      set f = Cage(C,n);
      set s = UMP Upper_Arc L~f;
A14:   s`2 <= N-bound L~f by JORDAN21:60;
      assume
A15:     0 < n;
      then 0+1 <= n by NAT_1:13;
      then 1 < n or n = 1 by REAL_1:def 5;
      then N-bound L~Cage(C,n) <= N-bound L~Cage(C,1) by JORDAN10:7;
      then
A16:   s`2 <= n0 by A14,XXREAL_0:2;
A17:   s`1 = w by A11;
      p`2 <= s`2 by A15,Th25;
      then
A18:   s in H by A3,A4,A8,A16,A17,GOBOARD7:8;
      s in U by A15;
      then
A19:   s in H /\ U by A18,XBOOLE_0:def 3;
      s`2 = proj2.s by PSCOMP_1:def 29;
      hence thesis by A19,FUNCT_2:43;
    end;
    H /\ U is Bounded by TOPREAL6:98;
    then proj2.:(H /\ U) is bounded by JCT_MISC:23;
    then
A20: proj2.:(H /\ U) is bounded_below by SEQ_4:def 3;
A21: (UMP Upper_Arc L~Cage(C,1))`2 <= n0 by JORDAN21:60;
A22: p`2 <= (UMP Upper_Arc L~Cage(C,1))`2 by Th25;
A23: (UMP Upper_Arc L~Cage(C,1))`2 in proj2.:(H /\ U) by A13;
    then q <= (UMP Upper_Arc L~Cage(C,1))`2 by A20,SEQ_4:def 5;
    then
A24: q <= n0 by A21,XXREAL_0:2;
    per cases;
    suppose
A25:   p <> |[w,q]|;
A26: now
      assume
A27:     p`2 >= q;
      per cases by A27,REAL_1:def 5;
      suppose p`2 = q;
      hence contradiction by A25,EUCLID:56;
      end;
      suppose p`2 > q;
      then 0 < p`2 - q by XREAL_1:52;
      then consider r being real number such that
A28:     r in proj2.:(H /\ U) and
A29:     r < q+(p`2 - q) by A20,A23,SEQ_4:def 5;
      consider t being Point of TOP-REAL 2 such that
A30:     t in H /\ U and
A31:     proj2.t = r by A28,Lm1;
A32:   t`2 = r by A31,PSCOMP_1:def 29;
A33:   p`2 <= n0 by A21,A22,XXREAL_0:2;
      t in H by A30,XBOOLE_0:def 3;
      hence contradiction by A4,A29,A32,A33,TOPREAL1:10;
    end;
    end;
    consider S',C' being Subset of TopSpaceMetr Euclid 2 such that
A34:   S = S' & C = C' and
A35:   dist_min(S,C) = min_dist_min(S',C') by JORDAN1K:def 1;
    S misses C
    proof
      assume S meets C;
      then consider x being set such that
A36:     x in S and
A37:     x in C by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A36;
A38:   x`2 <= N-bound C by A37,PSCOMP_1:71;
A39:   q <= x`2 by A4,A6,A24,A36,TOPREAL1:10;
      then
A40:   p`2 < x`2 by A26,XXREAL_0:2;
      x`1 = w by A5,A7,A9,A36,SPPOL_1:def 3;
      then
      x in LSeg(p, |[w, N-bound C]|) by A1,A2,A8,A38,A40,GOBOARD7:8;
      then x in {p} by A10,A37,XBOOLE_0:def 3;
      hence contradiction by A26,A39,TARSKI:def 1;
    end;
    then dist_min(S,C) > 0 by A34,A35,JGRAPH_1:55;
    then dist_min(S,C)/2 > 0 by SEQ_2:3;
    then consider k being Element of NAT such that
A41:   1 < k and
A42:   dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < dist_min(S,C)/2 &
      dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < dist_min(S,C)/2 by GOBRD14:21;
    set f = Cage(C,k),
        G = Gauge(C,k);
A43: dist(G*(1,1),G*(1+1,1)) + dist(G*(1,1),G*(1,1+1))
      < dist_min(S,C)/2 + dist_min(S,C)/2 by A42,XREAL_1:10;
    set s = UMP Upper_Arc L~f;
A44: s`1 = w by A11;
A45: s`2 <= N-bound L~f by JORDAN21:60;
    N-bound L~Cage(C,k) <= N-bound L~Cage(C,1) by A41,JORDAN10:7;
    then
A46: s`2 <= n0 by A45,XXREAL_0:2;
    0 < k by A41;
    then s`2 in proj2.:(H /\ U) by A13;
    then
A47: q <= s`2 by A20,SEQ_4:def 5;
A48: s in S by A3,A4,A5,A6,A44,A46,A47,GOBOARD7:8;
A49: Upper_Arc L~f c= L~f by JORDAN6:76;
    s in Upper_Arc L~f by Lm4;
    then consider i being Element of NAT such that
A50:   1 <= i & i+1 <= len f and
A51:   s in LSeg(f,i) by A49,SPPOL_2:13;
    right_cell(f,i,G) meets C by A50,JORDAN9:33;
    then consider c being set such that
A52:   c in right_cell(f,i,G) and
A53:   c in C by XBOOLE_0:3;
    reconsider c as Point of TOP-REAL 2 by A53;
A54: f is_sequence_on G by JORDAN9:def 1;
    consider i1,j1,i2,j2 being Element of NAT such that
A55:   [i1,j1] in Indices G and
A56:   f/.i = G*(i1,j1) and
A57:   [i2,j2] in Indices G and
A58:   f/.(i+1) = G*(i2,j2) and
A59:   i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
        i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A50,A54,JORDAN8:6;
    left_cell(f,i,G) /\ right_cell(f,i,G) = LSeg(f,i) by A50,A54,GOBRD13:30;
    then
A60: s in right_cell(f,i,G) by A51,XBOOLE_0:def 3;
A61: i1+1<>i1+0;
A62: i2+1+1<>i2;
A63: j2+1+1<>j2;
A64: 1 <= i1 by A55,GOBOARD5:1;
A65: 1 <= j1 by A55,GOBOARD5:1;
A66: 1 <= i2 by A57,GOBOARD5:1;
A67: 1 <= j2 by A57,GOBOARD5:1;
A68: 1 <= i1+1 by NAT_1:11;
A69: 1 <= j1+1 by NAT_1:11;
A70: i1 <= len G by A55,GOBOARD5:1;
A71: j1 <= width G by A55,GOBOARD5:1;
A72: i2 <= len G by A57,GOBOARD5:1;
A73: j2 <= width G by A57,GOBOARD5:1;
    reconsider s' = s, c' = c as Point of Euclid 2 by TOPMETR:16;
A74: min_dist_min(S',C') <= dist(s',c') by A34,A48,A53,WEIERSTR:40;
A75: dist(s',c') = dist(s,c) by TOPREAL6:def 1;
A76: [1,1] in Indices G by Th5;
    [1,1+1] in Indices G by Th6;
    then
A77: dist(G*(1,1),G*(1,1+1)) = (N-bound C - S-bound C)/2|^k by A76,GOBRD14:19;
    [1+1,1] in Indices G by Th7;
    then
A78: dist(G*(1,1),G*(1+1,1)) = (E-bound C - W-bound C)/2|^k by A76,GOBRD14:20;
    now
      per cases by A59;
      suppose
A79:     i1 = i2 & j1+1 = j2;
      then
A80:   j1+1 <= width G by A57,GOBOARD5:1;
      i1 < len G by A50,A55,A56,A57,A58,A79,JORDAN10:1;
      then
A81:   i1+1 <= len G by NAT_1:13;
      right_cell(f,i,G) = cell(G,i1,j1)
        by A50,A54,A55,A56,A57,A58,A61,A63,A79,GOBRD13:def 2;
      then
      G*(i1,j1)`1 <= c`1 & c`1 <= G*(i1+1,j1)`1 &
      G*(i1,j1)`2 <= c`2 & c`2 <= G*(i1,j1+1)`2 &
      G*(i1,j1)`1 <= s`1 & s`1 <= G*(i1+1,j1)`1 &
      G*(i1,j1)`2 <= s`2 & s`2 <= G*(i1,j1+1)`2
        by A52,A60,A64,A65,A80,A81,JORDAN9:19;
      then
A82:   dist(s,c) <=
        (G*(i1+1,j1)`1-G*(i1,j1)`1) + (G*(i1,j1+1)`2-G*(i1,j1)`2)
          by TOPREAL6:104;
A83:   [i1+1,j1] in Indices G by A65,A68,A71,A81,GOBOARD7:10;
      then
A84:   dist(G*(i1,j1),G*(i1+1,j1)) = G*(i1+1,j1)`1 - G*(i1,j1)`1
        by A55,GOBRD14:15;
A85:   dist(G*(i1,j1),G*(i1,j1+1)) = G*(i1,j1+1)`2 - G*(i1,j1)`2
        by A55,A57,A79,GOBRD14:16;
A86:   dist(G*(i1,j1),G*(i1,j1+1)) = (N-bound C - S-bound C)/2|^k
        by A55,A57,A79,GOBRD14:19;
      dist(G*(i1,j1),G*(i1+1,j1)) = (E-bound C - W-bound C)/2|^k
        by A55,A83,GOBRD14:20;
      hence contradiction by A35,A43,A74,A75,A77,A78,A82,A84,A85,A86,XXREAL_0:2
;
      end;
      suppose
A87:     i1+1 = i2 & j1 = j2;
      then
A88:   i1+1 <= len G by A57,GOBOARD5:1;
      1 < j1 by A50,A55,A56,A57,A58,A87,JORDAN10:3;
      then
A89:   1 <= j1-'1 by BINARITH:67;
A90:   j1-'1 <= width G by A71,BINARITH:62;
A91:   j1-'1+1 = j1 by A89,BINARITH:61;
      right_cell(f,i,G) = cell(G,i1,j1-'1)
        by A50,A54,A55,A56,A57,A58,A61,A62,A87,GOBRD13:def 2;
      then
      G*(i1,j1-'1)`1 <= c`1 & c`1 <= G*(i1+1,j1-'1)`1 &
      G*(i1,j1-'1)`2 <= c`2 & c`2 <= G*(i1,j1-'1+1)`2 &
      G*(i1,j1-'1)`1 <= s`1 & s`1 <= G*(i1+1,j1-'1)`1 &
      G*(i1,j1-'1)`2 <= s`2 & s`2 <= G*(i1,j1-'1+1)`2
        by A52,A60,A64,A71,A88,A89,A91,JORDAN9:19;
      then
A92:   dist(s,c) <=
        (G*(i1+1,j1-'1)`1-G*(i1,j1-'1)`1) + (G*(i1,j1-'1+1)`2-G*(i1,j1-'1)`2)
          by TOPREAL6:104;
A93:   [i1,j1-'1] in Indices G by A64,A70,A89,A90,GOBOARD7:10;
A94:   [i1+1,j1-'1] in Indices G by A66,A72,A87,A89,A90,GOBOARD7:10;
      then
A95:   dist(G*(i1,j1-'1),G*(i1+1,j1-'1)) = G*(i1+1,j1-'1)`1 - G*(i1,j1-'1)`1
        by A93,GOBRD14:15;
A96:   dist(G*(i1,j1-'1),G*(i1,j1-'1+1)) = G*(i1,j1-'1+1)`2 - G*(i1,j1-'1)`2
        by A55,A91,A93,GOBRD14:16;
A97:   dist(G*(i1,j1-'1),G*(i1,j1-'1+1)) = (N-bound C - S-bound C)/2|^k
        by A55,A91,A93,GOBRD14:19;
      dist(G*(i1,j1-'1),G*(i1+1,j1-'1)) = (E-bound C - W-bound C)/2|^k
        by A93,A94,GOBRD14:20;
      hence contradiction by A35,A43,A74,A75,A77,A78,A92,A95,A96,A97,XXREAL_0:2
;
      end;
      suppose
A98:     i1 = i2+1 & j1 = j2;
      then
A99:   i2+1 <= len G by A55,GOBOARD5:1;
      j1 < width G by A50,A55,A56,A57,A58,A98,JORDAN10:4;
      then
A100:   j1+1 <= width G by NAT_1:13;
      right_cell(f,i,G) = cell(G,i2,j2)
        by A50,A54,A55,A56,A57,A58,A61,A62,A98,GOBRD13:def 2;
      then
      G*(i2,j2)`1 <= c`1 & c`1 <= G*(i2+1,j2)`1 &
      G*(i2,j2)`2 <= c`2 & c`2 <= G*(i2,j2+1)`2 &
      G*(i2,j2)`1 <= s`1 & s`1 <= G*(i2+1,j2)`1 &
      G*(i2,j2)`2 <= s`2 & s`2 <= G*(i2,j2+1)`2
        by A52,A60,A65,A66,A98,A99,A100,JORDAN9:19;
      then
A101:   dist(s,c) <=
        (G*(i2+1,j2)`1-G*(i2,j2)`1) + (G*(i2,j2+1)`2-G*(i2,j2)`2)
          by TOPREAL6:104;
A102:   dist(G*(i2,j2),G*(i2+1,j2)) = G*(i2+1,j2)`1 - G*(i2,j2)`1
        by A55,A57,A98,GOBRD14:15;
A103:   [i2,j2+1] in Indices G by A66,A69,A72,A98,A100,GOBOARD7:10;
      then
A104:   dist(G*(i2,j2),G*(i2,j2+1)) = G*(i2,j2+1)`2 - G*(i2,j2)`2
        by A57,GOBRD14:16;
A105:   dist(G*(i2,j2),G*(i2,j2+1)) = (N-bound C - S-bound C)/2|^k
        by A57,A103,GOBRD14:19;
      dist(G*(i2,j2),G*(i2+1,j2)) = (E-bound C - W-bound C)/2|^k
        by A55,A57,A98,GOBRD14:20;
      hence contradiction
        by A35,A43,A74,A75,A77,A78,A101,A102,A104,A105,XXREAL_0:2;
      end;
      suppose
A106:     i1 = i2 & j1 = j2+1;
      then
A107:   j2+1 <= width G by A55,GOBOARD5:1;
      1 < i1 by A50,A55,A56,A57,A58,A106,JORDAN10:2;
      then
A108:   1 <= i1-'1 by BINARITH:67;
A109:   i1-'1 <= len G by A70,BINARITH:62;
A110:   i1-'1+1 = i1 by A108,BINARITH:61;
      right_cell(f,i,G) = cell(G,i1-'1,j2)
        by A50,A54,A55,A56,A57,A58,A61,A63,A106,GOBRD13:def 2;
      then
      G*(i1-'1,j2)`1 <= c`1 & c`1 <= G*(i1-'1+1,j2)`1 &
      G*(i1-'1,j2)`2 <= c`2 & c`2 <= G*(i1-'1,j2+1)`2 &
      G*(i1-'1,j2)`1 <= s`1 & s`1 <= G*(i1-'1+1,j2)`1 &
      G*(i1-'1,j2)`2 <= s`2 & s`2 <= G*(i1-'1,j2+1)`2
        by A52,A60,A67,A70,A107,A108,A110,JORDAN9:19;
      then
A111:   dist(s,c) <=
        (G*(i1-'1+1,j2)`1-G*(i1-'1,j2)`1) + (G*(i1-'1,j2+1)`2-G*(i1-'1,j2)`2)
          by TOPREAL6:104;
A112:   [i1-'1,j2] in Indices G by A67,A73,A108,A109,GOBOARD7:10;
A113:   [i1-'1+1,j2] in Indices G by A64,A67,A70,A73,A110,GOBOARD7:10;
      then
A114:   dist(G*(i1-'1,j2),G*(i1-'1+1,j2)) = G*(i1-'1+1,j2)`1 - G*(i1-'1,j2)`1
        by A112,GOBRD14:15;
A115:   [i1-'1,j2+1] in Indices G by A65,A71,A106,A108,A109,GOBOARD7:10;
      then
A116:   dist(G*(i1-'1,j2),G*(i1-'1,j2+1)) = G*(i1-'1,j2+1)`2 - G*(i1-'1,j2)`2
        by A112,GOBRD14:16;
A117:   dist(G*(i1-'1,j2),G*(i1-'1,j2+1)) = (N-bound C - S-bound C)/2|^k
        by A112,A115,GOBRD14:19;
      dist(G*(i1-'1,j2),G*(i1-'1+1,j2)) = (E-bound C - W-bound C)/2|^k
        by A112,A113,GOBRD14:20;
      hence contradiction
        by A35,A43,A74,A75,A77,A78,A111,A114,A116,A117,XXREAL_0:2;
    end;
    end;
    hence thesis;
    end;

    suppose p = |[w,q]|;
    then
A118: p`2 = q by EUCLID:56;
A119: North_Arc C = Lim_inf Upper_Appr C by JORDAN19:def 3;
    reconsider e = p as Point of Euclid 2 by TOPMETR:16;

    ex S being Real_Sequence of 2 st S is convergent &
      (for x being Element of NAT holds S.x in (Upper_Appr C).x) & p = lim S
    proof
      set R = {(UMP Upper_Arc L~Cage(C,n))`2 where n is Element of NAT: 0 < n};
      R c= REAL
      proof
        let x be set;
        assume x in R;
        then ex n being Element of NAT
        st x = (UMP Upper_Arc L~Cage(C,n))`2 & 0 < n;
        hence thesis;
      end;
      then reconsider R as Subset of REAL;
A120:   (UMP Upper_Arc L~Cage(C,1))`2 in R;
A121:   R is bounded_below
      proof
        take q;
        let r be real number;
        assume r in R;
        then ex n being Element of NAT
        st r = (UMP Upper_Arc L~Cage(C,n))`2 & 0 < n;
        then r in proj2.:(H /\ U) by A13;
        hence q<=r by A20,SEQ_4:def 5;
      end;
      deffunc f(Element of NAT) = UMP Upper_Arc L~Cage(C,$1);
      consider S being Real_Sequence of 2 such that
A122:     for n being Element of NAT holds S.n = f(n) from TOPRNS_1:sch 1;
      take S;
A123:   for r being Real st 0<r
       ex n being Element of NAT st for m being Element of NAT st n<=m holds
        |. S.m-p .| < r
      proof
        let r be Real;
        assume
A124:       0 < r;
A125:     for r being real number st r in R holds q<=r
        proof
          let r be real number;
          assume r in R;
          then ex n being Element of NAT
          st r = (UMP Upper_Arc L~Cage(C,n))`2 & 0 < n;
          then r in proj2.:(H /\ U) by A13;
          hence thesis by A20,SEQ_4:def 5;
        end;
        for s being real number st 0<s
         ex r being real number st r in R & r<q+s
        proof
          let s be real number;
          assume 0<s;
          then consider r being real number such that
A126:         r in proj2.:(H /\ U) and
A127:         r<q+s by A20,A23,SEQ_4:def 5;
          take r;
          consider x being Point of TOP-REAL 2 such that
A128:         x in H /\ U and
A129:         proj2.x = r by A126,Lm1;
          x in U by A128,XBOOLE_0:def 3;
          then consider n being Element of NAT such that
A130:         x = UMP Upper_Arc L~Cage(C,n) and
A131:         0 < n;
          r = (UMP Upper_Arc L~Cage(C,n))`2 by A129,A130,PSCOMP_1:def 29;
          hence thesis by A127,A131;
        end;
        then
A132:     inf R = q by A120,A121,A125,SEQ_4:def 5;
        consider v being real number such that
A133:       v in R and
A134:       v < inf R + r by A120,A121,A124,SEQ_4:def 5;
A135:   v - inf R < inf R + r - inf R by A134,REAL_1:92;
        consider n being Element of NAT such that
A136:       v = (UMP Upper_Arc L~Cage(C,n))`2 and
A137:       0 < n by A133;
        take n;
        let m be Element of NAT;
A138:     S.m = UMP Upper_Arc L~Cage(C,m) by A122;
        assume
A139:       n <= m;
        then (UMP Upper_Arc L~Cage(C,m))`2 <= (UMP Upper_Arc L~Cage(C,n))`2
          by A137,Th29;
        then (S.m)`2 <= v by A122,A136;
        then (S.m)`2 - p`2 <= v - p`2 by REAL_1:92;
        then
A140:     (S.m)`2-p`2 < r by A118,A132,A135,XXREAL_0:2;
        0 < m by A137,A139;
        then (UMP C)`2 < (UMP Upper_Arc L~Cage(C,m))`2 by Th25;
        then
A141:     0 < (S.m)`2-p`2 by A138,XREAL_1:52;
        (S.m)`1 = w by A11,A138;
        then abs((S.m)`1-p`1) = 0 by A8,ABSVALUE:def 1;
        then |. S.m-p .|<=0+abs((S.m)`2-p`2) by JGRAPH_1:49;
        then |. S.m-p .|<=(S.m)`2-p`2 by A141,ABSVALUE:def 1;
        hence |. S.m-p .| < r by A140,XXREAL_0:2;
      end;
      thus
A142:     S is convergent
      proof
        take p;
        thus thesis by A123;
      end;
      hereby
        let x be Element of NAT;
A143:     S.x = UMP Upper_Arc L~Cage(C,x) by A122;
        (Upper_Appr C).x = Upper_Arc L~Cage(C,x) by JORDAN19:def 1;
        hence S.x in (Upper_Appr C).x by A143,Lm4;
      end;
      thus p = lim S by A123,A142,TOPRNS_1:def 10;
    end;
    hence thesis by A119,KURATO_2:58;
  end;
  end;

theorem Th36:
  LMP C in South_Arc C
  proof
    set w = (W-bound C + E-bound C) / 2;
    set p = LMP C;
    set U = {LMP Lower_Arc L~Cage(C,n) where n is Element of NAT: 0 < n};
    {LMP Lower_Arc L~Cage(C,n) where n is Element of NAT: 0 < n} c=
      the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in {LMP Lower_Arc L~Cage(C,n) where n is Element of NAT: 0 < n};
      then ex n being Element of NAT st x = LMP Lower_Arc L~Cage(C,n) & 0 < n;
      hence thesis;
    end;
    then reconsider U as Subset of TOP-REAL 2;
    set n0 = S-bound L~Cage(C,1);
    set H = LSeg(p,|[w,n0]|);
    set q = sup(proj2.:(H /\ U));
    set S = LSeg(|[w,q]|, |[w,n0]|);
A1: |[w,S-bound C]|`1 = w by EUCLID:56;
A2: |[w,S-bound C]|`2 = S-bound C by EUCLID:56;
A3: |[w,n0]|`1 = w by EUCLID:56;
A4: |[w,n0]|`2 = n0 by EUCLID:56;
A5: |[w,q]|`1 = w by EUCLID:56;
A6: |[w,q]|`2 = q by EUCLID:56;
A7: |[w,q]| in S by TOPREAL1:6;
A8: p`1 = w by EUCLID:56;
A9: S is vertical by A3,A5,SPPOL_1:37;
A10: LSeg(p, |[w, S-bound C]|) /\ C = {p} by JORDAN21:48;
A11: for n being Element of NAT holds (LMP Lower_Arc L~Cage(C,n))`1 = w
    proof
      let n be Element of NAT;
A12:   W-bound L~Cage(C,n) + E-bound L~Cage(C,n) = W-bound C + E-bound C
        by JORDAN1G:41;
      thus (LMP Lower_Arc L~Cage(C,n))`1
         = (W-bound Lower_Arc L~Cage(C,n) + E-bound Lower_Arc L~Cage(C,n))/2
           by EUCLID:56
        .= (W-bound L~Cage(C,n) + E-bound Lower_Arc L~Cage(C,n))/2
           by JORDAN21:28
        .= w by A12,JORDAN21:29;
    end;
A13: for n being Element of NAT st 0 < n holds
      (LMP Lower_Arc L~Cage(C,n))`2 in proj2.:(H /\ U)
    proof
      let n be Element of NAT;
      set f = Cage(C,n);
      set s = LMP Lower_Arc L~f;
A14:   S-bound L~f <= s`2 by JORDAN21:61;
      assume
A15:     0 < n;
      then 0+1 <= n by NAT_1:13;
      then n = 1 or n > 1 by REAL_1:def 5;
      then S-bound L~Cage(C,n) >= S-bound L~Cage(C,1) by JORDAN1A:90;
      then
A16:   s`2 >= n0 by A14,XXREAL_0:2;
A17:   s`1 = w by A11;
      p`2 >= s`2 by A15,Th26;
      then
A18:   s in H by A3,A4,A8,A16,A17,GOBOARD7:8;
      s in U by A15;
      then
A19:   s in H /\ U by A18,XBOOLE_0:def 3;
      s`2 = proj2.s by PSCOMP_1:def 29;
      hence thesis by A19,FUNCT_2:43;
    end;
    H /\ U is Bounded by TOPREAL6:98;
    then proj2.:(H /\ U) is bounded by JCT_MISC:23;
    then
A20: proj2.:(H /\ U) is bounded_above by SEQ_4:def 3;
A21: (LMP Lower_Arc L~Cage(C,1))`2 >= n0 by JORDAN21:61;
A22: p`2 >= (LMP Lower_Arc L~Cage(C,1))`2 by Th26;
A23: (LMP Lower_Arc L~Cage(C,1))`2 in proj2.:(H /\ U) by A13;
    then q >= (LMP Lower_Arc L~Cage(C,1))`2 by A20,SEQ_4:def 4;
    then
A24: q >= n0 by A21,XXREAL_0:2;
    per cases;
    suppose
A25:   p <> |[w,q]|;
A26: now
      assume
A27:     p`2 <= q;
      per cases by A27,REAL_1:def 5;
      suppose p`2 = q;
      hence contradiction by A25,EUCLID:56;
      end;
      suppose p`2 < q;
      then 0 < q - p`2 by XREAL_1:52;
      then consider r being real number such that
A28:     r in proj2.:(H /\ U) and
A29:     q-(q-p`2) < r by A20,A23,SEQ_4:def 4;
      consider t being Point of TOP-REAL 2 such that
A30:     t in H /\ U and
A31:     proj2.t = r by A28,Lm1;
A32:   t`2 = r by A31,PSCOMP_1:def 29;
A33:   p`2 >= n0 by A21,A22,XXREAL_0:2;
      t in H by A30,XBOOLE_0:def 3;
      hence contradiction by A4,A29,A32,A33,TOPREAL1:10;
    end;
    end;
    consider S',C' being Subset of TopSpaceMetr Euclid 2 such that
A34:   S = S' & C = C' and
A35:   dist_min(S,C) = min_dist_min(S',C') by JORDAN1K:def 1;
    S misses C
    proof
      assume S meets C;
      then consider x being set such that
A36:     x in S and
A37:     x in C by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A36;
A38:   x`2 >= S-bound C by A37,PSCOMP_1:71;
A39:   q >= x`2 by A4,A6,A24,A36,TOPREAL1:10;
      then
A40:   p`2 > x`2 by A26,XXREAL_0:2;
      x`1 = w by A5,A7,A9,A36,SPPOL_1:def 3;
      then
      x in LSeg(p, |[w, S-bound C]|) by A1,A2,A8,A38,A40,GOBOARD7:8;
      then x in {p} by A10,A37,XBOOLE_0:def 3;
      hence contradiction by A26,A39,TARSKI:def 1;
    end;
    then dist_min(S,C) > 0 by A34,A35,JGRAPH_1:55;
    then dist_min(S,C)/2 > 0 by SEQ_2:3;
    then consider k being Element of NAT such that
A41:   1 < k and
A42:   dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < dist_min(S,C)/2 &
      dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < dist_min(S,C)/2 by GOBRD14:21;
    set f = Cage(C,k),
        G = Gauge(C,k);
A43: dist(G*(1,1),G*(1+1,1)) + dist(G*(1,1),G*(1,1+1))
      < dist_min(S,C)/2 + dist_min(S,C)/2 by A42,XREAL_1:10;
    set s = LMP Lower_Arc L~f;
A44: s`1 = w by A11;
A45: s`2 >= S-bound L~f by JORDAN21:61;
    S-bound L~Cage(C,k) >= S-bound L~Cage(C,1) by A41,JORDAN1A:90;
    then
A46: s`2 >= n0 by A45,XXREAL_0:2;
    0 < k by A41;
    then s`2 in proj2.:(H /\ U) by A13;
    then
A47: q >= s`2 by A20,SEQ_4:def 4;
A48: s in S by A3,A4,A5,A6,A44,A46,A47,GOBOARD7:8;
A49: Lower_Arc L~f c= L~f by JORDAN6:76;
    s in Lower_Arc L~f by Lm5;
    then consider i being Element of NAT such that
A50:   1 <= i & i+1 <= len f and
A51:   s in LSeg(f,i) by A49,SPPOL_2:13;
    right_cell(f,i,G) meets C by A50,JORDAN9:33;
    then consider c being set such that
A52:   c in right_cell(f,i,G) and
A53:   c in C by XBOOLE_0:3;
    reconsider c as Point of TOP-REAL 2 by A53;
A54: f is_sequence_on G by JORDAN9:def 1;
    consider i1,j1,i2,j2 being Element of NAT such that
A55:   [i1,j1] in Indices G and
A56:   f/.i = G*(i1,j1) and
A57:   [i2,j2] in Indices G and
A58:   f/.(i+1) = G*(i2,j2) and
A59:   i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
        i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A50,A54,JORDAN8:6;
    left_cell(f,i,G) /\ right_cell(f,i,G) = LSeg(f,i) by A50,A54,GOBRD13:30;
    then
A60: s in right_cell(f,i,G) by A51,XBOOLE_0:def 3;
A61: i1+1<>i1+0;
A62: i2+1+1<>i2;
A63: j2+1+1<>j2;
A64: 1 <= i1 by A55,GOBOARD5:1;
A65: 1 <= j1 by A55,GOBOARD5:1;
A66: 1 <= i2 by A57,GOBOARD5:1;
A67: 1 <= j2 by A57,GOBOARD5:1;
A68: 1 <= i1+1 by NAT_1:11;
A69: 1 <= j1+1 by NAT_1:11;
A70: i1 <= len G by A55,GOBOARD5:1;
A71: j1 <= width G by A55,GOBOARD5:1;
A72: i2 <= len G by A57,GOBOARD5:1;
A73: j2 <= width G by A57,GOBOARD5:1;
    reconsider s' = s, c' = c as Point of Euclid 2 by TOPMETR:16;
A74: min_dist_min(S',C') <= dist(s',c') by A34,A48,A53,WEIERSTR:40;
A75: dist(s',c') = dist(s,c) by TOPREAL6:def 1;
A76: [1,1] in Indices G by Th5;
    [1,1+1] in Indices G by Th6;
    then
A77: dist(G*(1,1),G*(1,1+1)) = (N-bound C - S-bound C)/2|^k by A76,GOBRD14:19;
    [1+1,1] in Indices G by Th7;
    then
A78: dist(G*(1,1),G*(1+1,1)) = (E-bound C - W-bound C)/2|^k by A76,GOBRD14:20;
    now
      per cases by A59;
      suppose
A79:     i1 = i2 & j1+1 = j2;
      then
A80:   j1+1 <= width G by A57,GOBOARD5:1;
      i1 < len G by A50,A55,A56,A57,A58,A79,JORDAN10:1;
      then
A81:   i1+1 <= len G by NAT_1:13;
      right_cell(f,i,G) = cell(G,i1,j1)
        by A50,A54,A55,A56,A57,A58,A61,A63,A79,GOBRD13:def 2;
      then
      G*(i1,j1)`1 <= c`1 & c`1 <= G*(i1+1,j1)`1 &
      G*(i1,j1)`2 <= c`2 & c`2 <= G*(i1,j1+1)`2 &
      G*(i1,j1)`1 <= s`1 & s`1 <= G*(i1+1,j1)`1 &
      G*(i1,j1)`2 <= s`2 & s`2 <= G*(i1,j1+1)`2
        by A52,A60,A64,A65,A80,A81,JORDAN9:19;
      then
A82:   dist(s,c) <=
        (G*(i1+1,j1)`1-G*(i1,j1)`1) + (G*(i1,j1+1)`2-G*(i1,j1)`2)
          by TOPREAL6:104;
A83:   [i1+1,j1] in Indices G by A65,A68,A71,A81,GOBOARD7:10;
      then
A84:   dist(G*(i1,j1),G*(i1+1,j1)) = G*(i1+1,j1)`1 - G*(i1,j1)`1
        by A55,GOBRD14:15;
A85:   dist(G*(i1,j1),G*(i1,j1+1)) = G*(i1,j1+1)`2 - G*(i1,j1)`2
        by A55,A57,A79,GOBRD14:16;
A86:   dist(G*(i1,j1),G*(i1,j1+1)) = (N-bound C - S-bound C)/2|^k
        by A55,A57,A79,GOBRD14:19;
      dist(G*(i1,j1),G*(i1+1,j1)) = (E-bound C - W-bound C)/2|^k
        by A55,A83,GOBRD14:20;
      hence contradiction by A35,A43,A74,A75,A77,A78,A82,A84,A85,A86,XXREAL_0:2
;
      end;
      suppose
A87:     i1+1 = i2 & j1 = j2;
      then
A88:   i1+1 <= len G by A57,GOBOARD5:1;
      1 < j1 by A50,A55,A56,A57,A58,A87,JORDAN10:3;
      then
A89:   1 <= j1-'1 by BINARITH:67;
A90:   j1-'1 <= width G by A71,BINARITH:62;
A91:   j1-'1+1 = j1 by A89,BINARITH:61;
      right_cell(f,i,G) = cell(G,i1,j1-'1)
        by A50,A54,A55,A56,A57,A58,A61,A62,A87,GOBRD13:def 2;
      then
      G*(i1,j1-'1)`1 <= c`1 & c`1 <= G*(i1+1,j1-'1)`1 &
      G*(i1,j1-'1)`2 <= c`2 & c`2 <= G*(i1,j1-'1+1)`2 &
      G*(i1,j1-'1)`1 <= s`1 & s`1 <= G*(i1+1,j1-'1)`1 &
      G*(i1,j1-'1)`2 <= s`2 & s`2 <= G*(i1,j1-'1+1)`2
        by A52,A60,A64,A71,A88,A89,A91,JORDAN9:19;
      then
A92:   dist(s,c) <=
        (G*(i1+1,j1-'1)`1-G*(i1,j1-'1)`1) + (G*(i1,j1-'1+1)`2-G*(i1,j1-'1)`2)
          by TOPREAL6:104;
A93:   [i1,j1-'1] in Indices G by A64,A70,A89,A90,GOBOARD7:10;
A94:   [i1+1,j1-'1] in Indices G by A66,A72,A87,A89,A90,GOBOARD7:10;
      then
A95:   dist(G*(i1,j1-'1),G*(i1+1,j1-'1)) = G*(i1+1,j1-'1)`1 - G*(i1,j1-'1)`1
        by A93,GOBRD14:15;
A96:   dist(G*(i1,j1-'1),G*(i1,j1-'1+1)) = G*(i1,j1-'1+1)`2 - G*(i1,j1-'1)`2
        by A55,A91,A93,GOBRD14:16;
A97:   dist(G*(i1,j1-'1),G*(i1,j1-'1+1)) = (N-bound C - S-bound C)/2|^k
        by A55,A91,A93,GOBRD14:19;
      dist(G*(i1,j1-'1),G*(i1+1,j1-'1)) = (E-bound C - W-bound C)/2|^k
        by A93,A94,GOBRD14:20;
      hence contradiction by A35,A43,A74,A75,A77,A78,A92,A95,A96,A97,XXREAL_0:2
;
      end;
      suppose
A98:     i1 = i2+1 & j1 = j2;
      then
A99:   i2+1 <= len G by A55,GOBOARD5:1;
      j1 < width G by A50,A55,A56,A57,A58,A98,JORDAN10:4;
      then
A100:   j1+1 <= width G by NAT_1:13;
      right_cell(f,i,G) = cell(G,i2,j2)
        by A50,A54,A55,A56,A57,A58,A61,A62,A98,GOBRD13:def 2;
      then
      G*(i2,j2)`1 <= c`1 & c`1 <= G*(i2+1,j2)`1 &
      G*(i2,j2)`2 <= c`2 & c`2 <= G*(i2,j2+1)`2 &
      G*(i2,j2)`1 <= s`1 & s`1 <= G*(i2+1,j2)`1 &
      G*(i2,j2)`2 <= s`2 & s`2 <= G*(i2,j2+1)`2
        by A52,A60,A65,A66,A98,A99,A100,JORDAN9:19;
      then
A101:   dist(s,c) <=
        (G*(i2+1,j2)`1-G*(i2,j2)`1) + (G*(i2,j2+1)`2-G*(i2,j2)`2)
          by TOPREAL6:104;
A102:   dist(G*(i2,j2),G*(i2+1,j2)) = G*(i2+1,j2)`1 - G*(i2,j2)`1
        by A55,A57,A98,GOBRD14:15;
A103:   [i2,j2+1] in Indices G by A66,A69,A72,A98,A100,GOBOARD7:10;
      then
A104:   dist(G*(i2,j2),G*(i2,j2+1)) = G*(i2,j2+1)`2 - G*(i2,j2)`2
        by A57,GOBRD14:16;
A105:   dist(G*(i2,j2),G*(i2,j2+1)) = (N-bound C - S-bound C)/2|^k
        by A57,A103,GOBRD14:19;
      dist(G*(i2,j2),G*(i2+1,j2)) = (E-bound C - W-bound C)/2|^k
        by A55,A57,A98,GOBRD14:20;
      hence contradiction
        by A35,A43,A74,A75,A77,A78,A101,A102,A104,A105,XXREAL_0:2;
      end;
      suppose
A106:     i1 = i2 & j1 = j2+1;
      then
A107:   j2+1 <= width G by A55,GOBOARD5:1;
      1 < i1 by A50,A55,A56,A57,A58,A106,JORDAN10:2;
      then
A108:   1 <= i1-'1 by BINARITH:67;
A109:   i1-'1 <= len G by A70,BINARITH:62;
A110:   i1-'1+1 = i1 by A108,BINARITH:61;
      right_cell(f,i,G) = cell(G,i1-'1,j2)
        by A50,A54,A55,A56,A57,A58,A61,A63,A106,GOBRD13:def 2;
      then
      G*(i1-'1,j2)`1 <= c`1 & c`1 <= G*(i1-'1+1,j2)`1 &
      G*(i1-'1,j2)`2 <= c`2 & c`2 <= G*(i1-'1,j2+1)`2 &
      G*(i1-'1,j2)`1 <= s`1 & s`1 <= G*(i1-'1+1,j2)`1 &
      G*(i1-'1,j2)`2 <= s`2 & s`2 <= G*(i1-'1,j2+1)`2
        by A52,A60,A67,A70,A107,A108,A110,JORDAN9:19;
      then
A111:   dist(s,c) <=
        (G*(i1-'1+1,j2)`1-G*(i1-'1,j2)`1) + (G*(i1-'1,j2+1)`2-G*(i1-'1,j2)`2)
          by TOPREAL6:104;
A112:   [i1-'1,j2] in Indices G by A67,A73,A108,A109,GOBOARD7:10;
A113:   [i1-'1+1,j2] in Indices G by A64,A67,A70,A73,A110,GOBOARD7:10;
      then
A114:   dist(G*(i1-'1,j2),G*(i1-'1+1,j2)) = G*(i1-'1+1,j2)`1 - G*(i1-'1,j2)`1
        by A112,GOBRD14:15;
A115:   [i1-'1,j2+1] in Indices G by A65,A71,A106,A108,A109,GOBOARD7:10;
      then
A116:   dist(G*(i1-'1,j2),G*(i1-'1,j2+1)) = G*(i1-'1,j2+1)`2 - G*(i1-'1,j2)`2
        by A112,GOBRD14:16;
A117:   dist(G*(i1-'1,j2),G*(i1-'1,j2+1)) = (N-bound C - S-bound C)/2|^k
        by A112,A115,GOBRD14:19;
      dist(G*(i1-'1,j2),G*(i1-'1+1,j2)) = (E-bound C - W-bound C)/2|^k
        by A112,A113,GOBRD14:20;
      hence contradiction
        by A35,A43,A74,A75,A77,A78,A111,A114,A116,A117,XXREAL_0:2;
    end;
    end;
    hence thesis;
    end;

    suppose p = |[w,q]|;
    then
A118: p`2 = q by EUCLID:56;
A119: South_Arc C = Lim_inf Lower_Appr C by JORDAN19:def 4;
    reconsider e = p as Point of Euclid 2 by TOPMETR:16;

    ex S being Real_Sequence of 2 st S is convergent &
      (for x being Element of NAT holds S.x in (Lower_Appr C).x) & p = lim S
    proof
      set R = {(LMP Lower_Arc L~Cage(C,n))`2 where n is Element of NAT: 0 < n};
      R c= REAL
      proof
        let x be set;
        assume x in R;
        then ex n being Element of NAT
        st x = (LMP Lower_Arc L~Cage(C,n))`2 & 0 < n;
        hence thesis;
      end;
      then reconsider R as Subset of REAL;
A120:   (LMP Lower_Arc L~Cage(C,1))`2 in R;
A121:   R is bounded_above
      proof
        take q;
        let r be real number;
        assume r in R;
        then ex n being Element of NAT
        st r = (LMP Lower_Arc L~Cage(C,n))`2 & 0 < n;
        then r in proj2.:(H /\ U) by A13;
        hence q>=r by A20,SEQ_4:def 4;
      end;
      deffunc g(Element of NAT) = LMP Lower_Arc L~Cage(C,$1);
      consider S being Real_Sequence of 2 such that
A122:     for n being Element of NAT holds S.n = g(n) from TOPRNS_1:sch 1;
      take S;
A123:   for r being Real st 0<r
       ex n being Element of NAT st for m being Element of NAT st n<=m holds
        |. S.m-p .| < r
      proof
        let r be Real;
        assume
A124:       0 < r;
A125:     for r being real number st r in R holds q>=r
        proof
          let r be real number;
          assume r in R;
          then ex n being Element of NAT
          st r = (LMP Lower_Arc L~Cage(C,n))`2 & 0 < n;
          then r in proj2.:(H /\ U) by A13;
          hence thesis by A20,SEQ_4:def 4;
        end;
        for s being real number st 0<s
         ex r being real number st r in R & q-s<r
        proof
          let s be real number;
          assume 0<s;
          then consider r being real number such that
A126:         r in proj2.:(H /\ U) and
A127:         q-s<r by A20,A23,SEQ_4:def 4;
          take r;
          consider x being Point of TOP-REAL 2 such that
A128:         x in H /\ U and
A129:         proj2.x = r by A126,Lm1;
          x in U by A128,XBOOLE_0:def 3;
          then consider n being Element of NAT such that
A130:         x = LMP Lower_Arc L~Cage(C,n) and
A131:         0 < n;
          r = (LMP Lower_Arc L~Cage(C,n))`2 by A129,A130,PSCOMP_1:def 29;
          hence thesis by A127,A131;
        end;
        then
A132:     sup R = q by A120,A121,A125,SEQ_4:def 4;
        consider v being real number such that
A133:       v in R and
A134:       sup R - r < v by A120,A121,A124,SEQ_4:def 4;
        sup R - r + r < v + r by A134,XREAL_1:8;
        then
A135:   sup R - v < v + r - v by REAL_1:92;
        consider n being Element of NAT such that
A136:       v = (LMP Lower_Arc L~Cage(C,n))`2 and
A137:       0 < n by A133;
        take n;
        let m be Element of NAT;
A138:     S.m = LMP Lower_Arc L~Cage(C,m) by A122;
        assume
A139:       n <= m;
        then (LMP Lower_Arc L~Cage(C,n))`2 <= (LMP Lower_Arc L~Cage(C,m))`2
          by A137,Th30;
        then (S.m)`2 >= v by A122,A136;
        then p`2 - (S.m)`2 <= p`2 - v by REAL_1:92;
        then
A140:     p`2-(S.m)`2 < r by A118,A132,A135,XXREAL_0:2;
        0 < m by A137,A139;
        then (LMP C)`2 > (LMP Lower_Arc L~Cage(C,m))`2 by Th26;
        then
A141:     0 > (S.m)`2-p`2 by A138,REAL_2:105;
        (S.m)`1 = w by A11,A138;
        then abs((S.m)`1-p`1) = 0 by A8,ABSVALUE:def 1;
        then |. S.m-p .|<=0+abs((S.m)`2-p`2) by JGRAPH_1:49;
        then |. S.m-p .|<=-((S.m)`2-p`2) by A141,ABSVALUE:def 1;
        hence |. S.m-p .| < r by A140,XXREAL_0:2;
      end;
      thus
A142:   S is convergent
      proof
        take p;
        thus thesis by A123;
      end;
      hereby
        let x be Element of NAT;
A143:     S.x = LMP Lower_Arc L~Cage(C,x) by A122;
        (Lower_Appr C).x = Lower_Arc L~Cage(C,x) by JORDAN19:def 2;
        hence S.x in (Lower_Appr C).x by A143,Lm5;
      end;
      thus p = lim S by A123,A142,TOPRNS_1:def 10;
    end;
    hence thesis by A119,KURATO_2:58;
  end;
  end;

theorem Th37:
  North_Arc C c= C
  proof
    let x be set;
    assume
A1:   x in North_Arc C;
    assume
A2:   not x in C;
    reconsider x as Point of TOP-REAL 2 by A1;
    reconsider e = x as Point of Euclid 2 by TOPMETR:16;
A3: North_Arc C = Lim_inf Upper_Appr C by JORDAN19:def 3;
A4: x in C` by A2,SUBSET_1:50;
A5: (BDD C) \/ (UBD C) = C` by JORDAN2C:31;
    per cases by A4,A5,XBOOLE_0:def 2;
    suppose x in BDD C;
    then consider r being real number such that
A6:   r > 0 and
A7:   Ball(e,r) c= BDD C by TOPMETR:22;
    consider k being Element of NAT such that
A8:   for m being Element of NAT st m > k
holds (Upper_Appr C).m meets Ball(e,r)
        by A1,A3,A6,KURATO_2:50;
A9: k+0 < k+1 by XREAL_1:10;
    BDD C misses L~Cage(C,k+1) by JORDAN10:19;
    then
A10: Ball(e,r) misses L~Cage(C,k+1) by A7,XBOOLE_1:63;
A11: (Upper_Appr C).(k+1) = Upper_Arc L~Cage(C,k+1) by JORDAN19:def 1;
    Upper_Arc L~Cage(C,k+1) c= L~Cage(C,k+1) by JORDAN6:76;
    then
    (Upper_Appr C).(k+1) misses Ball(e,r) by A10,A11,XBOOLE_1:63;
    hence thesis by A8,A9;
    end;
    suppose
A12:   x in UBD C;
    union UBD-Family C = UBD C by JORDAN10:14;
    then consider A being set such that
A13:   x in A and
A14:   A in UBD-Family C by A12,TARSKI:def 4;
    UBD-Family C =
     {UBD L~Cage(C,m) where m is Element of NAT : not contradiction}
      by JORDAN10:def 1;
    then consider m being Element of NAT such that
A15:   A = UBD L~Cage(C,m) by A14;
    x in LeftComp Cage(C,m) by A13,A15,GOBRD14:46;
    then consider r being real number such that
A16:   r > 0 and
A17:   Ball(e,r) c= LeftComp Cage(C,m) by TOPMETR:22;
    consider k being Element of NAT such that
A18:   for m being Element of NAT st m > k
holds (Upper_Appr C).m meets Ball(e,r)
        by A1,A3,A16,KURATO_2:50;
    thus thesis
    proof
      per cases;
      suppose
A19:     m > k;
      LeftComp Cage(C,m) misses L~Cage(C,m) by SPRECT_3:43;
      then
A20:   Ball(e,r) misses L~Cage(C,m) by A17,XBOOLE_1:63;
A21:   (Upper_Appr C).m = Upper_Arc L~Cage(C,m) by JORDAN19:def 1;
      Upper_Arc L~Cage(C,m) c= L~Cage(C,m) by JORDAN6:76;
      then
      (Upper_Appr C).m misses Ball(e,r) by A20,A21,XBOOLE_1:63;
      hence thesis by A18,A19;
      end;
      suppose m <= k;
      then
A22:   LeftComp Cage(C,m) c= LeftComp Cage(C,k) by JORDAN1H:55;
A23:   k+0 < k+1 by XREAL_1:10;
      then
A24:   LeftComp Cage(C,k) c= LeftComp Cage(C,k+1) by JORDAN1H:55;
      Ball(e,r) c= LeftComp Cage(C,k) by A17,A22,XBOOLE_1:1;
      then
A25:   Ball(e,r) c= LeftComp Cage(C,k+1) by A24,XBOOLE_1:1;
      LeftComp Cage(C,k+1) misses L~Cage(C,k+1) by SPRECT_3:43;
      then
A26:   Ball(e,r) misses L~Cage(C,k+1) by A25,XBOOLE_1:63;
A27:   (Upper_Appr C).(k+1) = Upper_Arc L~Cage(C,k+1) by JORDAN19:def 1;
      Upper_Arc L~Cage(C,k+1) c= L~Cage(C,k+1) by JORDAN6:76;
      then
      (Upper_Appr C).(k+1) misses Ball(e,r) by A26,A27,XBOOLE_1:63;
      hence thesis by A18,A23;
    end;
  end;
  end;
  end;

theorem Th38:
  South_Arc C c= C
  proof
    let x be set;
    assume
A1:   x in South_Arc C;
    assume
A2:   not x in C;
    reconsider x as Point of TOP-REAL 2 by A1;
    reconsider e = x as Point of Euclid 2 by TOPMETR:16;
A3: South_Arc C = Lim_inf Lower_Appr C by JORDAN19:def 4;
A4: x in C` by A2,SUBSET_1:50;
A5: (BDD C) \/ (UBD C) = C` by JORDAN2C:31;
    per cases by A4,A5,XBOOLE_0:def 2;
    suppose x in BDD C;
    then consider r being real number such that
A6:   r > 0 and
A7:   Ball(e,r) c= BDD C by TOPMETR:22;
    consider k being Element of NAT such that
A8:   for m being Element of NAT st m > k
holds (Lower_Appr C).m meets Ball(e,r)
        by A1,A3,A6,KURATO_2:50;
A9: k+0 < k+1 by XREAL_1:10;
    BDD C misses L~Cage(C,k+1) by JORDAN10:19;
    then
A10: Ball(e,r) misses L~Cage(C,k+1) by A7,XBOOLE_1:63;
A11: (Lower_Appr C).(k+1) = Lower_Arc L~Cage(C,k+1) by JORDAN19:def 2;
    Lower_Arc L~Cage(C,k+1) c= L~Cage(C,k+1) by JORDAN6:76;
    then
    (Lower_Appr C).(k+1) misses Ball(e,r) by A10,A11,XBOOLE_1:63;
    hence thesis by A8,A9;
    end;
    suppose
A12:   x in UBD C;
    union UBD-Family C = UBD C by JORDAN10:14;
    then consider A being set such that
A13:   x in A and
A14:   A in UBD-Family C by A12,TARSKI:def 4;
    UBD-Family C =
    {UBD L~Cage(C,m) where m is Element of NAT : not contradiction}
      by JORDAN10:def 1;
    then consider m being Element of NAT such that
A15:   A = UBD L~Cage(C,m) by A14;
    x in LeftComp Cage(C,m) by A13,A15,GOBRD14:46;
    then consider r being real number such that
A16:   r > 0 and
A17:   Ball(e,r) c= LeftComp Cage(C,m) by TOPMETR:22;
    consider k being Element of NAT such that
A18:   for m being Element of NAT st m > k
holds (Lower_Appr C).m meets Ball(e,r)
        by A1,A3,A16,KURATO_2:50;
    thus thesis
    proof
      per cases;
      suppose
A19:     m > k;
      LeftComp Cage(C,m) misses L~Cage(C,m) by SPRECT_3:43;
      then
A20:   Ball(e,r) misses L~Cage(C,m) by A17,XBOOLE_1:63;
A21:   (Lower_Appr C).m = Lower_Arc L~Cage(C,m) by JORDAN19:def 2;
      Lower_Arc L~Cage(C,m) c= L~Cage(C,m) by JORDAN6:76;
      then
      (Lower_Appr C).m misses Ball(e,r) by A20,A21,XBOOLE_1:63;
      hence thesis by A18,A19;
      end;
      suppose m <= k;
      then
A22:   LeftComp Cage(C,m) c= LeftComp Cage(C,k) by JORDAN1H:55;
A23:   k+0 < k+1 by XREAL_1:10;
      then
A24:   LeftComp Cage(C,k) c= LeftComp Cage(C,k+1) by JORDAN1H:55;
      Ball(e,r) c= LeftComp Cage(C,k) by A17,A22,XBOOLE_1:1;
      then
A25:   Ball(e,r) c= LeftComp Cage(C,k+1) by A24,XBOOLE_1:1;
      LeftComp Cage(C,k+1) misses L~Cage(C,k+1) by SPRECT_3:43;
      then
A26:   Ball(e,r) misses L~Cage(C,k+1) by A25,XBOOLE_1:63;
A27:   (Lower_Appr C).(k+1) = Lower_Arc L~Cage(C,k+1) by JORDAN19:def 2;
      Lower_Arc L~Cage(C,k+1) c= L~Cage(C,k+1) by JORDAN6:76;
      then
      (Lower_Appr C).(k+1) misses Ball(e,r) by A26,A27,XBOOLE_1:63;
      hence thesis by A18,A23;
      end;
    end;
  end;
  end;

theorem
  LMP C in Lower_Arc C & UMP C in Upper_Arc C or
  UMP C in Lower_Arc C & LMP C in Upper_Arc C
proof
A1: South_Arc C c= C by Th38;
A2: LMP C in South_Arc C by Th36;
A3: North_Arc C c= C by Th37;
A4: UMP C in North_Arc C by Th35;
  now
    per cases by A1,A2,A3,A4,JORDAN16:11;
    case LE LMP C, UMP C, C;
    then LMP C in Upper_Arc C & UMP C in Lower_Arc C
      or LMP C in Lower_Arc C & UMP C in Lower_Arc C
      or LMP C in Upper_Arc C & UMP C in Upper_Arc C by JORDAN6:def 10;
    hence UMP C in Lower_Arc C & LMP C in Upper_Arc C by JORDAN21:62,
63;
    end;
    case LE UMP C, LMP C, C;
    then UMP C in Upper_Arc C & LMP C in Lower_Arc C
      or LMP C in Lower_Arc C & UMP C in Lower_Arc C
      or LMP C in Upper_Arc C & UMP C in Upper_Arc C by JORDAN6:def 10;
    hence LMP C in Lower_Arc C & UMP C in Upper_Arc C by JORDAN21:62,
63;
    end;
  end;
  hence thesis;
end;

:: Moved from JORDAN, AG 20.01.2006

theorem
  W-bound C = W-bound North_Arc C
  proof
A1: North_Arc C c= C by Th37;
    W-min C in North_Arc C by Th31;
    hence thesis by A1,JORDAN1J:65;
end;

theorem
  E-bound C = E-bound North_Arc C
  proof
A1: North_Arc C c= C by Th37;
    E-max C in North_Arc C by Th32;
    hence thesis by A1,JORDAN1J:66;
end;

theorem
  W-bound C = W-bound South_Arc C
  proof
A1: South_Arc C c= C by Th38;
    W-min C in South_Arc C by Th33;
    hence thesis by A1,JORDAN1J:65;
end;

theorem
  E-bound C = E-bound South_Arc C
  proof
A1: South_Arc C c= C by Th38;
    E-max C in South_Arc C by Th34;
    hence thesis by A1,JORDAN1J:66;
end;

