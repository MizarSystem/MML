:: On Some Points of a Simple Closed Curve. {P}art {II}
::  by Artur Korni{\l}owicz and Adam Grabowski
::
:: Received October 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies BOOLE, RELAT_1, RELAT_2, PRE_TOPC, COMPTS_1, EUCLID, TOPREAL2,
      JORDAN2C, TREES_1, JORDAN8, TOPREAL1, ORDINAL2, ARYTM, JORDAN6, JORDAN9,
      FINSEQ_1, FUNCT_1, PSCOMP_1, MCART_1, ARYTM_3, SEQ_1, SUBSET_1, METRIC_1,
      PCOMPS_1, GOBOARD5, FINSEQ_6, FINSEQ_4, GOBOARD2, ARYTM_1, MATRIX_1,
      GOBOARD1, GROUP_1, JORDAN10, TARSKI, GOBOARD9, SPPOL_1, JORDAN3,
      WEIERSTR, ABSVALUE, FUNCT_5, SEQ_2, LATTICES, JORDAN21, COMPLEX1,
      JORDAN19, KURATO_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, COMPLEX1, NAT_1, RELAT_1, FUNCT_2, SEQ_1, SEQ_4, NEWTON,
      FINSEQ_1, MATRIX_1, STRUCT_0, PRE_TOPC, COMPTS_1, FINSEQ_4, FINSEQ_6,
      BINARITH, CONNSP_1, NORMSP_1, METRIC_1, PCOMPS_1, PSCOMP_1, EUCLID,
      TOPREAL1, TOPREAL2, GOBOARD1, GOBOARD2, WEIERSTR, SPPOL_1, GOBOARD5,
      TOPRNS_1, JORDAN2C, JORDAN6, GOBOARD9, JORDAN8, JORDAN9, GOBRD13,
      GOBRD14, JORDAN10, JORDAN1K, KURATO_2, JORDAN19, JORDAN21;
 constructors CARD_4, CONNSP_1, JORDAN2C, GOBOARD9, BINARITH, JORDAN1H, REAL_1,
      REALSET1, TOPREAL4, INT_1, JORDAN8, NAT_1, JORDAN6, JORDAN9, PSCOMP_1,
      JORDAN5C, JORDAN1K, GOBRD14, FINSEQ_4, GOBOARD2, NORMSP_1, JORDAN10,
      TOPRNS_1, KURATO_2, JORDAN19, COMPLEX1, JORDAN21;
 registrations SPRECT_1, TOPREAL6, JORDAN8, REVROT_1, INT_1, RELSET_1, XREAL_0,
      SPRECT_3, GOBOARD2, EUCLID, GOBRD14, SPRECT_4, BORSUK_2, BORSUK_4,
      MEMBERED, STRUCT_0, JORDAN1A, JORDAN10, SPPOL_2;
 requirements SUBSET, BOOLE, REAL, NUMERALS, ARITHM;
 definitions TARSKI, SEQ_4, TOPRNS_1;
 theorems XBOOLE_1, JORDAN1A, JORDAN6, XBOOLE_0, JORDAN1K, EUCLID, TOPREAL1,
      TARSKI, JORDAN16, METRIC_1, REAL_2, JORDAN3, SPRECT_3, TOPMETR, JORDAN7,
      GOBRD14, AXIOMS, JORDAN1I, INT_1, BINARITH, GOBOARD5, FINSEQ_6, REVROT_1,
      SPRECT_2, GOBOARD7, JGRAPH_1, JORDAN1H, JORDAN9, GOBRD13, REAL_1,
      SQUARE_1, NAT_1, SEQ_2, JORDAN8, SUBSET_1, JORDAN2C, JORDAN10, PSCOMP_1,
      SPPOL_2, SPPOL_1, SEQ_4, FUNCT_2, WEIERSTR, JORDAN1G, TOPRNS_1, ABSVALUE,
      JORDAN19, XCMPLX_1, KURATO_2, JORDAN21;
 schemes TOPRNS_1;

begin :: Properties of the Approximations

 reserve C for Simple_closed_curve,
         i for Nat;

Lm1: TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8;
Lm2:
  for r being real number, X being Subset of TOP-REAL 2 st r in proj2.:X
    ex x being Point of TOP-REAL 2 st x in X & proj2.x = r
    proof
      let r be real number,
          X be Subset of TOP-REAL 2;
      assume r in proj2.:X;
      then ex x being set st x in the carrier of TOP-REAL 2 &
       x in X & proj2.x = r by FUNCT_2:115;
      hence thesis;
    end;

Lm3:
  for n being real number holds n+1+1 <> n
  proof
    let n be real number;
    assume n+1+1 = n;
    then n+(1+1) = n+0 by XCMPLX_1:1;
    hence contradiction by REAL_1:53;
  end;

theorem Th1:
  (Upper_Appr C).i c= Cl RightComp Cage(C,0)
proof
A1: (Upper_Appr C).i = Upper_Arc L~Cage (C,i) by JORDAN19:def 1;
A2: Upper_Arc L~Cage (C,i) c= L~Cage (C,i) by JORDAN1A:16;
  0 <= i by NAT_1:18; then
  L~Cage (C,i) c= Cl RightComp Cage(C,0) by JORDAN1H:54;
  hence thesis by A1,A2,XBOOLE_1:1;
end;

theorem Th2:
  (Lower_Appr C).i c= Cl RightComp Cage(C,0)
proof
A1: (Lower_Appr C).i = Lower_Arc L~Cage (C,i) by JORDAN19:def 2;
A2: Lower_Arc L~Cage (C,i) c= L~Cage (C,i) by JORDAN1A:16;
  0 <= i by NAT_1:18; then
  L~Cage (C,i) c= Cl RightComp Cage(C,0) by JORDAN1H:54;
  hence thesis by A1,A2,XBOOLE_1:1;
end;

registration
  let C be Simple_closed_curve;
  cluster Upper_Arc C -> connected;
coherence
proof
  Upper_Arc C is_an_arc_of W-min C,E-max C by JORDAN6:def 8;
  hence thesis by JORDAN6:11;
end;
  cluster Lower_Arc C -> connected;
coherence
proof
  Lower_Arc C is_an_arc_of E-max C,W-min C by JORDAN6:def 9;
  hence thesis by JORDAN6:11;
end;
end;

theorem
  (Upper_Appr C).i is compact connected
proof
  Upper_Arc L~Cage(C,i) is compact;
  hence (Upper_Appr C).i is compact by JORDAN19:def 1;
  Upper_Arc L~Cage(C,i) is connected;
  hence (Upper_Appr C).i is connected by JORDAN19:def 1;
end;

theorem
  (Lower_Appr C).i is compact connected
proof
  Lower_Arc L~Cage(C,i) is compact;
  hence (Lower_Appr C).i is compact by JORDAN19:def 2;
  Lower_Arc L~Cage(C,i) is connected;
  hence (Lower_Appr C).i is connected by JORDAN19:def 2;
end;

registration
  let C be Simple_closed_curve;
  cluster North_Arc C -> compact;
  coherence
  proof
A1: Cl RightComp Cage(C,0) is Bounded by JORDAN2C:73;
    for i being Nat holds (Upper_Appr C).i c= Cl RightComp Cage(C,0) by Th1;
    then Lim_inf Upper_Appr C is compact by A1,KURATO_2:63;
   hence thesis by JORDAN19:def 3;
  end;
  cluster South_Arc C -> compact;
  coherence
  proof
A2: Cl RightComp Cage(C,0) is Bounded by JORDAN2C:73;
    for i being Nat holds (Lower_Appr C).i c= Cl RightComp Cage(C,0) by Th2;
    then Lim_inf Lower_Appr C is compact by A2,KURATO_2:63;
   hence thesis by JORDAN19:def 4;
  end;
end;

begin :: On Special Points of Approximations

theorem
  W-min C in North_Arc C
  proof
A1: North_Arc C = Lim_inf Upper_Appr C by JORDAN19:def 3;
    reconsider w = W-min C as Point of Euclid 2 by Lm1,TOPMETR:16;
    for r being real number st r > 0
      ex k being Nat st
     for m being Nat st m > k holds (Upper_Appr C).m meets Ball(w,r)
    proof
      let r be real number;
      assume r > 0;
      then r/2 > 0 by SEQ_2:3;
      then consider k being Nat such that
        1 < k and
A2:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < r/2 and
A3:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < r/2 by GOBRD14:21;
      take k;
      let m be Nat such that
A4:     m > k;
      reconsider p = W-min L~Cage(C,m) as Point of Euclid 2 by Lm1,TOPMETR:16;
A5:   (Upper_Appr C).m = Upper_Arc L~Cage(C,m) by JORDAN19:def 1;
A6:   W-min L~Cage(C,m) in Upper_Arc L~Cage(C,m) by JORDAN7:1;
A7:   W-min L~Cage(C,m) in rng Cage(C,m) by SPRECT_2:47;
      len Cage(C,m) > 4 by GOBOARD7:36;
      then
A8:   1+1 <= len Cage(C,m) by AXIOMS:22;
A9:   W-min L~Cage(C,m) = Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
        by A7,FINSEQ_6:98;
A10:   1+1 <= len Rotate(Cage(C,m),W-min L~Cage(C,m)) by A8,REVROT_1:14; then
A11:   Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
        in LSeg(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) by TOPREAL1:27;
      left_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) /\
        right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) =
        LSeg(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
          by A10,GOBOARD5:32;
      then
A12:   W-min L~Cage(C,m) in right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
        by A9,A11,XBOOLE_0:def 3;
A13:   W-min C in right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
        by JORDAN1I:8;
A14:   right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
       = right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,
                GoB Rotate(Cage(C,m),W-min L~Cage(C,m))) by A10,JORDAN1H:29
      .= right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,GoB Cage(C,m))
         by REVROT_1:28
      .= right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,Gauge(C,m))
         by JORDAN1H:52;
      Cage(C,m) is_sequence_on Gauge(C,m) by JORDAN9:def 1;
      then
A15:   Rotate(Cage(C,m),W-min L~Cage(C,m)) is_sequence_on Gauge(C,m)
       by REVROT_1:34;
      Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
       = W-min L~Rotate(Cage(C,m),W-min L~Cage(C,m)) by A9,REVROT_1:33;
      then
      consider i, j being Nat such that
A16:     [i,j] in Indices Gauge(C,m) &
        [i,j+1] in Indices Gauge(C,m) and
A17:     Rotate(Cage(C,m),W-min L~Cage(C,m))/.1 = Gauge(C,m)*(i,j) &
        Rotate(Cage(C,m),W-min L~Cage(C,m))/.(1+1) = Gauge(C,m)*(i,j+1)
          by A10,A15,JORDAN1I:23;
A18:   right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
       = cell(Gauge(C,m),i,j) by A10,A14,A15,A16,A17,GOBRD13:23;
A19:   1 <= i & 1 <= j & j+1 <= width Gauge(C,m) by A16,GOBOARD5:1;
A20:   1 <= i+1 by NAT_1:29;
A21:   j <= width Gauge(C,m) by A16,GOBOARD5:1;
      i < len Gauge(C,m) by A10,A15,A16,A17,JORDAN1I:16;
      then
A22:   i+1 <= len Gauge(C,m) by NAT_1:38;
      then
A23:   [i+1,j] in Indices Gauge(C,m) by A19,A20,A21,GOBOARD7:10;
      Gauge(C,m)*(i,j)`1 <= (W-min C)`1 &
        (W-min C)`1 <= Gauge(C,m)*(i+1,j)`1 &
        Gauge(C,m)*(i,j)`2 <= (W-min C)`2 &
        (W-min C)`2 <= Gauge(C,m)*(i,j+1)`2 &
        Gauge(C,m)*(i,j)`1 <= (W-min L~Cage(C,m))`1 &
        (W-min L~Cage(C,m))`1 <= Gauge(C,m)*(i+1,j)`1 &
        Gauge(C,m)*(i,j)`2 <= (W-min L~Cage(C,m))`2 &
        (W-min L~Cage(C,m))`2 <= Gauge(C,m)*(i,j+1)`2
          by A12,A13,A18,A19,A22,JORDAN9:19;
      then
A24:   dist(W-min C,W-min L~Cage(C,m)) <=
       (Gauge(C,m)*(i+1,j)`1-Gauge(C,m)*(i,j)`1) +
       (Gauge(C,m)*(i,j+1)`2-Gauge(C,m)*(i,j)`2) by GOBRD14:12;
A25:   [1,1] in Indices Gauge(C,m) by JORDAN21:14;
A26:   [1,1+1] in Indices Gauge(C,m) by JORDAN21:15;
A27:   [1+1,1] in Indices Gauge(C,m) by JORDAN21:16;
A28:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
          = (E-bound C - W-bound C)/2|^m by A25,A27,GOBRD14:20;
A29:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
          = (N-bound C - S-bound C)/2|^m by A25,A26,GOBRD14:19;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) by A4,JORDAN21:18;
      then
A30:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) < r/2 by A2,AXIOMS:22;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) by A4,JORDAN21:20;
      then
A31:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) < r/2 by A3,AXIOMS:22;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
         = dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i,j+1)) by A16,A29,GOBRD14:19;
      then
A32:   Gauge(C,m)*(i,j+1)`2 - Gauge(C,m)*(i,j)`2 < r/2 by A16,A30,GOBRD14:16;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
         = dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i+1,j)) by A16,A23,A28,GOBRD14:20;
      then
      Gauge(C,m)*(i+1,j)`1 - Gauge(C,m)*(i,j)`1 < r/2 by A16,A23,A31,GOBRD14:15
;
      then
      (Gauge(C,m)*(i+1,j)`1-Gauge(C,m)*(i,j)`1) +
       (Gauge(C,m)*(i,j+1)`2-Gauge(C,m)*(i,j)`2) < r/2 + r/2
        by A32,REAL_1:67;
      then dist(W-min C,W-min L~Cage(C,m)) < r by A24,AXIOMS:22;
      then dist(w,p) < r by GOBRD14:def 1;
      then p in Ball(w,r) by METRIC_1:12;
      hence (Upper_Appr C).m meets Ball(w,r) by A5,A6,XBOOLE_0:3;
    end;
    hence W-min C in North_Arc C by A1,KURATO_2:50;
  end;

theorem
  E-max C in North_Arc C
  proof
A1: North_Arc C = Lim_inf Upper_Appr C by JORDAN19:def 3;
    reconsider w = E-max C as Point of Euclid 2 by Lm1,TOPMETR:16;
    for r being real number st r > 0
      ex k being Nat st
     for m being Nat st m > k holds (Upper_Appr C).m meets Ball(w,r)
    proof
      let r be real number;
      assume r > 0;
      then r/2 > 0 by SEQ_2:3;
      then consider k being Nat such that
        1 < k and
A2:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < r/2 and
A3:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < r/2 by GOBRD14:21;
      take k;
      let m be Nat such that
A4:     m > k;
      reconsider p = E-max L~Cage(C,m) as Point of Euclid 2 by Lm1,TOPMETR:16;
A5:   (Upper_Appr C).m = Upper_Arc L~Cage(C,m) by JORDAN19:def 1;
A6:   E-max L~Cage(C,m) in Upper_Arc L~Cage(C,m) by JORDAN7:1;
A7:   E-max L~Cage(C,m) in rng Cage(C,m) by SPRECT_2:50;
      len Cage(C,m) > 4 by GOBOARD7:36;
      then
A8:   1+1 <= len Cage(C,m) by AXIOMS:22;
A9:   E-max L~Cage(C,m) = Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
        by A7,FINSEQ_6:98;
A10:   1+1 <= len Rotate(Cage(C,m),E-max L~Cage(C,m)) by A8,REVROT_1:14; then
A11:   Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
        in LSeg(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) by TOPREAL1:27;
      left_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) /\
        right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) =
        LSeg(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
          by A10,GOBOARD5:32;
      then
A12:   E-max L~Cage(C,m) in right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
        by A9,A11,XBOOLE_0:def 3;
A13:   E-max C in right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
        by JORDAN1I:9;
A14:   right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
       = right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,
                GoB Rotate(Cage(C,m),E-max L~Cage(C,m))) by A10,JORDAN1H:29
      .= right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,GoB Cage(C,m))
         by REVROT_1:28
      .= right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,Gauge(C,m))
         by JORDAN1H:52;
      Cage(C,m) is_sequence_on Gauge(C,m) by JORDAN9:def 1;
      then
A15:   Rotate(Cage(C,m),E-max L~Cage(C,m)) is_sequence_on Gauge(C,m)
       by REVROT_1:34;
      Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
       = E-max L~Rotate(Cage(C,m),E-max L~Cage(C,m)) by A9,REVROT_1:33;
      then
      consider i, j being Nat such that
A16:     [i,j+1] in Indices Gauge(C,m) &
        [i,j] in Indices Gauge(C,m) and
A17:     Rotate(Cage(C,m),E-max L~Cage(C,m))/.1 = Gauge(C,m)*(i,j+1) &
        Rotate(Cage(C,m),E-max L~Cage(C,m))/.(1+1) = Gauge(C,m)*(i,j)
          by A10,A15,JORDAN1I:25;
A18:   right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
       = cell(Gauge(C,m),i-'1,j) by A10,A14,A15,A16,A17,GOBRD13:29;
A19:   1 <= i & 1 <= j & j+1 <= width Gauge(C,m) by A16,GOBOARD5:1;
      i > 1 by A10,A15,A16,A17,JORDAN1I:18;
      then
A20:   i - 1 > 1 - 1 by REAL_1:92;
      then i - 1 is Nat by INT_1:16;
      then
A21:   i - 1 >= 0 + 1 by A20,NAT_1:38;
      then
A22:   i-'1+1 = i by JORDAN3:6;
A23:   i <= len Gauge(C,m) by A16,GOBOARD5:1;
A24:   i-'1 = i-1 by A20,BINARITH:def 3;
      i-1 <= i by INT_1:26;
      then
A25:   i-'1 <= len Gauge(C,m) & j <= width Gauge(C,m)
        by A16,A23,A24,AXIOMS:22,GOBOARD5:1;
      then
A26:   [i-'1,j] in Indices Gauge(C,m) by A19,A21,A24,GOBOARD7:10;
      1 <= j+1 by NAT_1:29;
      then
A27:   [i-'1,j+1] in Indices Gauge(C,m) by A19,A21,A24,A25,GOBOARD7:10;
      Gauge(C,m)*(i-'1,j)`1 <= (E-max C)`1 &
        (E-max C)`1 <= Gauge(C,m)*(i-'1+1,j)`1 &
        Gauge(C,m)*(i-'1,j)`2 <= (E-max C)`2 &
        (E-max C)`2 <= Gauge(C,m)*(i-'1,j+1)`2 &
        Gauge(C,m)*(i-'1,j)`1 <= (E-max L~Cage(C,m))`1 &
        (E-max L~Cage(C,m))`1 <= Gauge(C,m)*(i-'1+1,j)`1 &
        Gauge(C,m)*(i-'1,j)`2 <= (E-max L~Cage(C,m))`2 &
        (E-max L~Cage(C,m))`2 <= Gauge(C,m)*(i-'1,j+1)`2
          by A12,A13,A18,A19,A21,A22,A23,JORDAN9:19;
      then
A28:   dist(E-max C,E-max L~Cage(C,m)) <=
       (Gauge(C,m)*(i-'1+1,j)`1-Gauge(C,m)*(i-'1,j)`1) +
       (Gauge(C,m)*(i-'1,j+1)`2-Gauge(C,m)*(i-'1,j)`2) by GOBRD14:12;
A29:   [1,1] in Indices Gauge(C,m) by JORDAN21:14;
A30:   [1,1+1] in Indices Gauge(C,m) by JORDAN21:15;
A31:   [1+1,1] in Indices Gauge(C,m) by JORDAN21:16;
A32:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
          = (E-bound C - W-bound C)/2|^m by A29,A31,GOBRD14:20;
A33:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
          = (N-bound C - S-bound C)/2|^m by A29,A30,GOBRD14:19;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) by A4,JORDAN21:18;
      then
A34:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) < r/2 by A2,AXIOMS:22;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) by A4,JORDAN21:20;
      then
A35:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) < r/2 by A3,AXIOMS:22;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
         = dist(Gauge(C,m)*(i-'1,j),Gauge(C,m)*(i-'1,j+1))
           by A26,A27,A33,GOBRD14:19;
      then
A36:   Gauge(C,m)*(i-'1,j+1)`2 - Gauge(C,m)*(i-'1,j)`2 < r/2
        by A26,A27,A34,GOBRD14:16;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
         = dist(Gauge(C,m)*(i-'1,j),Gauge(C,m)*(i-'1+1,j))
           by A16,A22,A26,A32,GOBRD14:20;
      then
      Gauge(C,m)*(i-'1+1,j)`1 - Gauge(C,m)*(i-'1,j)`1 < r/2
        by A16,A22,A26,A35,GOBRD14:15;
      then
      (Gauge(C,m)*(i-'1+1,j)`1-Gauge(C,m)*(i-'1,j)`1) +
       (Gauge(C,m)*(i-'1,j+1)`2-Gauge(C,m)*(i-'1,j)`2) < r/2 + r/2
        by A36,REAL_1:67;
      then dist(E-max C,E-max L~Cage(C,m)) < r by A28,AXIOMS:22;
      then dist(w,p) < r by GOBRD14:def 1;
      then p in Ball(w,r) by METRIC_1:12;
      hence (Upper_Appr C).m meets Ball(w,r) by A5,A6,XBOOLE_0:3;
    end;
    hence E-max C in North_Arc C by A1,KURATO_2:50;
  end;

theorem
  W-min C in South_Arc C
  proof
A1: South_Arc C = Lim_inf Lower_Appr C by JORDAN19:def 4;
    reconsider w = W-min C as Point of Euclid 2 by Lm1,TOPMETR:16;
    for r being real number st r > 0
      ex k being Nat st
     for m being Nat st m > k holds (Lower_Appr C).m meets Ball(w,r)
    proof
      let r be real number;
      assume r > 0;
      then r/2 > 0 by SEQ_2:3;
      then consider k being Nat such that
        1 < k and
A2:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < r/2 and
A3:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < r/2 by GOBRD14:21;
      take k;
      let m be Nat such that
A4:     m > k;
      reconsider p = W-min L~Cage(C,m) as Point of Euclid 2 by Lm1,TOPMETR:16;
A5:   (Lower_Appr C).m = Lower_Arc L~Cage(C,m) by JORDAN19:def 2;
A6:   W-min L~Cage(C,m) in Lower_Arc L~Cage(C,m) by JORDAN7:1;
A7:   W-min L~Cage(C,m) in rng Cage(C,m) by SPRECT_2:47;
      len Cage(C,m) > 4 by GOBOARD7:36;
      then
A8:   1+1 <= len Cage(C,m) by AXIOMS:22;
A9:   W-min L~Cage(C,m) = Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
        by A7,FINSEQ_6:98;
A10:   1+1 <= len Rotate(Cage(C,m),W-min L~Cage(C,m)) by A8,REVROT_1:14; then
A11:   Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
        in LSeg(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) by TOPREAL1:27;
      left_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) /\
        right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1) =
        LSeg(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
          by A10,GOBOARD5:32;
      then
A12:   W-min L~Cage(C,m) in right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
        by A9,A11,XBOOLE_0:def 3;
A13:   W-min C in right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
        by JORDAN1I:8;
A14:   right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
       = right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,
                GoB Rotate(Cage(C,m),W-min L~Cage(C,m))) by A10,JORDAN1H:29
      .= right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,GoB Cage(C,m))
         by REVROT_1:28
      .= right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1,Gauge(C,m))
         by JORDAN1H:52;
      Cage(C,m) is_sequence_on Gauge(C,m) by JORDAN9:def 1;
      then
A15:   Rotate(Cage(C,m),W-min L~Cage(C,m)) is_sequence_on Gauge(C,m)
       by REVROT_1:34;
      Rotate(Cage(C,m),W-min L~Cage(C,m))/.1
       = W-min L~Rotate(Cage(C,m),W-min L~Cage(C,m)) by A9,REVROT_1:33;
      then
      consider i, j being Nat such that
A16:     [i,j] in Indices Gauge(C,m) &
        [i,j+1] in Indices Gauge(C,m) and
A17:     Rotate(Cage(C,m),W-min L~Cage(C,m))/.1 = Gauge(C,m)*(i,j) &
        Rotate(Cage(C,m),W-min L~Cage(C,m))/.(1+1) = Gauge(C,m)*(i,j+1)
          by A10,A15,JORDAN1I:23;
A18:   right_cell(Rotate(Cage(C,m),W-min L~Cage(C,m)),1)
       = cell(Gauge(C,m),i,j) by A10,A14,A15,A16,A17,GOBRD13:23;
A19:   1 <= i & 1 <= j & j+1 <= width Gauge(C,m) by A16,GOBOARD5:1;
A20:   1 <= i+1 by NAT_1:29;
A21:   j <= width Gauge(C,m) by A16,GOBOARD5:1;
      i < len Gauge(C,m) by A10,A15,A16,A17,JORDAN1I:16;
      then
A22:   i+1 <= len Gauge(C,m) by NAT_1:38;
      then
A23:   [i+1,j] in Indices Gauge(C,m) by A19,A20,A21,GOBOARD7:10;
      Gauge(C,m)*(i,j)`1 <= (W-min C)`1 &
        (W-min C)`1 <= Gauge(C,m)*(i+1,j)`1 &
        Gauge(C,m)*(i,j)`2 <= (W-min C)`2 &
        (W-min C)`2 <= Gauge(C,m)*(i,j+1)`2 &
        Gauge(C,m)*(i,j)`1 <= (W-min L~Cage(C,m))`1 &
        (W-min L~Cage(C,m))`1 <= Gauge(C,m)*(i+1,j)`1 &
        Gauge(C,m)*(i,j)`2 <= (W-min L~Cage(C,m))`2 &
        (W-min L~Cage(C,m))`2 <= Gauge(C,m)*(i,j+1)`2
          by A12,A13,A18,A19,A22,JORDAN9:19;
      then
A24:   dist(W-min C,W-min L~Cage(C,m)) <=
       (Gauge(C,m)*(i+1,j)`1-Gauge(C,m)*(i,j)`1) +
       (Gauge(C,m)*(i,j+1)`2-Gauge(C,m)*(i,j)`2) by GOBRD14:12;
A25:   [1,1] in Indices Gauge(C,m) by JORDAN21:14;
A26:   [1,1+1] in Indices Gauge(C,m) by JORDAN21:15;
A27:   [1+1,1] in Indices Gauge(C,m) by JORDAN21:16;
A28:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
          = (E-bound C - W-bound C)/2|^m by A25,A27,GOBRD14:20;
A29:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
          = (N-bound C - S-bound C)/2|^m by A25,A26,GOBRD14:19;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) by A4,JORDAN21:18;
      then
A30:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) < r/2 by A2,AXIOMS:22;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) by A4,JORDAN21:20;
      then
A31:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) < r/2 by A3,AXIOMS:22;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
         = dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i,j+1)) by A16,A29,GOBRD14:19;
      then
A32:   Gauge(C,m)*(i,j+1)`2 - Gauge(C,m)*(i,j)`2 < r/2 by A16,A30,GOBRD14:16;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
         = dist(Gauge(C,m)*(i,j),Gauge(C,m)*(i+1,j)) by A16,A23,A28,GOBRD14:20;
      then
      Gauge(C,m)*(i+1,j)`1 - Gauge(C,m)*(i,j)`1 < r/2 by A16,A23,A31,GOBRD14:15
;
      then
      (Gauge(C,m)*(i+1,j)`1-Gauge(C,m)*(i,j)`1) +
       (Gauge(C,m)*(i,j+1)`2-Gauge(C,m)*(i,j)`2) < r/2 + r/2
        by A32,REAL_1:67;
      then dist(W-min C,W-min L~Cage(C,m)) < r by A24,AXIOMS:22;
      then dist(w,p) < r by GOBRD14:def 1;
      then p in Ball(w,r) by METRIC_1:12;
      hence (Lower_Appr C).m meets Ball(w,r) by A5,A6,XBOOLE_0:3;
    end;
    hence W-min C in South_Arc C by A1,KURATO_2:50;
  end;

theorem
  E-max C in South_Arc C
  proof
A1: South_Arc C = Lim_inf Lower_Appr C by JORDAN19:def 4;
    reconsider w = E-max C as Point of Euclid 2 by Lm1,TOPMETR:16;
    for r being real number st r > 0
      ex k being Nat st
     for m being Nat st m > k holds (Lower_Appr C).m meets Ball(w,r)
    proof
      let r be real number;
      assume r > 0;
      then r/2 > 0 by SEQ_2:3;
      then consider k being Nat such that
        1 < k and
A2:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < r/2 and
A3:     dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < r/2 by GOBRD14:21;
      take k;
      let m be Nat such that
A4:     m > k;
      reconsider p = E-max L~Cage(C,m) as Point of Euclid 2 by Lm1,TOPMETR:16;
A5:   (Lower_Appr C).m = Lower_Arc L~Cage(C,m) by JORDAN19:def 2;
A6:   E-max L~Cage(C,m) in Lower_Arc L~Cage(C,m) by JORDAN7:1;
A7:   E-max L~Cage(C,m) in rng Cage(C,m) by SPRECT_2:50;
      len Cage(C,m) > 4 by GOBOARD7:36;
      then
A8:   1+1 <= len Cage(C,m) by AXIOMS:22;
A9:   E-max L~Cage(C,m) = Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
        by A7,FINSEQ_6:98;
A10:   1+1 <= len Rotate(Cage(C,m),E-max L~Cage(C,m)) by A8,REVROT_1:14; then
A11:   Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
        in LSeg(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) by TOPREAL1:27;
      left_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) /\
        right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1) =
        LSeg(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
          by A10,GOBOARD5:32;
      then
A12:   E-max L~Cage(C,m) in right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
        by A9,A11,XBOOLE_0:def 3;
A13:   E-max C in right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
        by JORDAN1I:9;
A14:   right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
       = right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,
                GoB Rotate(Cage(C,m),E-max L~Cage(C,m))) by A10,JORDAN1H:29
      .= right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,GoB Cage(C,m))
         by REVROT_1:28
      .= right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1,Gauge(C,m))
         by JORDAN1H:52;
      Cage(C,m) is_sequence_on Gauge(C,m) by JORDAN9:def 1;
      then
A15:   Rotate(Cage(C,m),E-max L~Cage(C,m)) is_sequence_on Gauge(C,m)
       by REVROT_1:34;
      Rotate(Cage(C,m),E-max L~Cage(C,m))/.1
       = E-max L~Rotate(Cage(C,m),E-max L~Cage(C,m)) by A9,REVROT_1:33;
      then
      consider i, j being Nat such that
A16:     [i,j+1] in Indices Gauge(C,m) &
        [i,j] in Indices Gauge(C,m) and
A17:     Rotate(Cage(C,m),E-max L~Cage(C,m))/.1 = Gauge(C,m)*(i,j+1) &
        Rotate(Cage(C,m),E-max L~Cage(C,m))/.(1+1) = Gauge(C,m)*(i,j)
          by A10,A15,JORDAN1I:25;
A18:   right_cell(Rotate(Cage(C,m),E-max L~Cage(C,m)),1)
       = cell(Gauge(C,m),i-'1,j) by A10,A14,A15,A16,A17,GOBRD13:29;
A19:   1 <= i & 1 <= j & j+1 <= width Gauge(C,m) by A16,GOBOARD5:1;
      i > 1 by A10,A15,A16,A17,JORDAN1I:18;
      then
A20:   i - 1 > 1 - 1 by REAL_1:92;
      then i - 1 is Nat by INT_1:16;
      then
A21:   i - 1 >= 0 + 1 by A20,NAT_1:38;
      then
A22:   i-'1+1 = i by JORDAN3:6;
A23:   i <= len Gauge(C,m) by A16,GOBOARD5:1;
A24:   i-'1 = i-1 by A20,BINARITH:def 3;
      i-1 <= i by INT_1:26;
      then
A25:   i-'1 <= len Gauge(C,m) & j <= width Gauge(C,m)
        by A16,A23,A24,AXIOMS:22,GOBOARD5:1;
      then
A26:   [i-'1,j] in Indices Gauge(C,m) by A19,A21,A24,GOBOARD7:10;
      1 <= j+1 by NAT_1:29;
      then
A27:   [i-'1,j+1] in Indices Gauge(C,m) by A19,A21,A24,A25,GOBOARD7:10;
      Gauge(C,m)*(i-'1,j)`1 <= (E-max C)`1 &
        (E-max C)`1 <= Gauge(C,m)*(i-'1+1,j)`1 &
        Gauge(C,m)*(i-'1,j)`2 <= (E-max C)`2 &
        (E-max C)`2 <= Gauge(C,m)*(i-'1,j+1)`2 &
        Gauge(C,m)*(i-'1,j)`1 <= (E-max L~Cage(C,m))`1 &
        (E-max L~Cage(C,m))`1 <= Gauge(C,m)*(i-'1+1,j)`1 &
        Gauge(C,m)*(i-'1,j)`2 <= (E-max L~Cage(C,m))`2 &
        (E-max L~Cage(C,m))`2 <= Gauge(C,m)*(i-'1,j+1)`2
          by A12,A13,A18,A19,A21,A22,A23,JORDAN9:19;
      then
A28:   dist(E-max C,E-max L~Cage(C,m)) <=
       (Gauge(C,m)*(i-'1+1,j)`1-Gauge(C,m)*(i-'1,j)`1) +
       (Gauge(C,m)*(i-'1,j+1)`2-Gauge(C,m)*(i-'1,j)`2) by GOBRD14:12;
A29:   [1,1] in Indices Gauge(C,m) by JORDAN21:14;
A30:   [1,1+1] in Indices Gauge(C,m) by JORDAN21:15;
A31:   [1+1,1] in Indices Gauge(C,m) by JORDAN21:16;
A32:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
          = (E-bound C - W-bound C)/2|^m by A29,A31,GOBRD14:20;
A33:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
          = (N-bound C - S-bound C)/2|^m by A29,A30,GOBRD14:19;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) by A4,JORDAN21:18;
      then
A34:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,2)) < r/2 by A2,AXIOMS:22;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) <
        dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) by A4,JORDAN21:20;
      then
A35:   dist(Gauge(C,m)*(1,1),Gauge(C,m)*(2,1)) < r/2 by A3,AXIOMS:22;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1,1+1))
         = dist(Gauge(C,m)*(i-'1,j),Gauge(C,m)*(i-'1,j+1))
           by A26,A27,A33,GOBRD14:19;
      then
A36:   Gauge(C,m)*(i-'1,j+1)`2 - Gauge(C,m)*(i-'1,j)`2 < r/2
        by A26,A27,A34,GOBRD14:16;
      dist(Gauge(C,m)*(1,1),Gauge(C,m)*(1+1,1))
         = dist(Gauge(C,m)*(i-'1,j),Gauge(C,m)*(i-'1+1,j))
           by A16,A22,A26,A32,GOBRD14:20;
      then
      Gauge(C,m)*(i-'1+1,j)`1 - Gauge(C,m)*(i-'1,j)`1 < r/2
        by A16,A22,A26,A35,GOBRD14:15;
      then
      (Gauge(C,m)*(i-'1+1,j)`1-Gauge(C,m)*(i-'1,j)`1) +
       (Gauge(C,m)*(i-'1,j+1)`2-Gauge(C,m)*(i-'1,j)`2) < r/2 + r/2
        by A36,REAL_1:67;
      then dist(E-max C,E-max L~Cage(C,m)) < r by A28,AXIOMS:22;
      then dist(w,p) < r by GOBRD14:def 1;
      then p in Ball(w,r) by METRIC_1:12;
      hence (Lower_Appr C).m meets Ball(w,r) by A5,A6,XBOOLE_0:3;
    end;
    hence E-max C in South_Arc C by A1,KURATO_2:50;
  end;

theorem Th9:
  UMP C in North_Arc C
  proof
    set w = (W-bound C + E-bound C) / 2;
    set p = UMP C;
    set U = {UMP Upper_Arc L~Cage(C,n) where n is Nat: 0 < n};
    {UMP Upper_Arc L~Cage(C,n) where n is Nat: 0 < n} c=
      the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in {UMP Upper_Arc L~Cage(C,n) where n is Nat: 0 < n};
      then ex n being Nat st x = UMP Upper_Arc L~Cage(C,n) & 0 < n;
      hence thesis;
    end;
    then reconsider U as Subset of TOP-REAL 2;
    set n0 = N-bound L~Cage(C,1);
    set H = LSeg(p,|[w,n0]|);
    set q = inf(proj2.:(H /\ U));
    set S = LSeg(|[w,q]|, |[w,n0]|);
A1: |[w,N-bound C]|`1 = w by EUCLID:56;
A2: |[w,N-bound C]|`2 = N-bound C by EUCLID:56;
A3: |[w,n0]|`1 = w by EUCLID:56;
A4: |[w,n0]|`2 = n0 by EUCLID:56;
A5: |[w,q]|`1 = w by EUCLID:56;
A6: |[w,q]|`2 = q by EUCLID:56;
A7: |[w,q]| in S by TOPREAL1:6;
A8: p`1 = w by JORDAN21:33;
A9: p = |[p`1,p`2]| by EUCLID:57;
A10: S is vertical by A3,A5,SPPOL_1:37;
A11: LSeg(p, |[w, N-bound C]|) /\ C = {p} by JORDAN21:47;
A12: for n being Nat holds (UMP Upper_Arc L~Cage(C,n))`1 = w
    proof
      let n be Nat;
A13:   W-bound L~Cage(C,n) + E-bound L~Cage(C,n) = W-bound C + E-bound C
        by JORDAN1G:41;
      thus (UMP Upper_Arc L~Cage(C,n))`1
         = (W-bound Upper_Arc L~Cage(C,n) + E-bound Upper_Arc L~Cage(C,n))/2
           by JORDAN21:33
        .= (W-bound L~Cage(C,n) + E-bound Upper_Arc L~Cage(C,n))/2
           by JORDAN21:26
        .= w by A13,JORDAN21:27;
    end;
A14: for n being Nat st 0 < n
       holds (UMP Upper_Arc L~Cage(C,n))`2 in proj2.:(H /\ U)
    proof
      let n be Nat;
      set f = Cage(C,n);
      set s = UMP Upper_Arc L~f;
A15:   s`2 <= N-bound L~f by JORDAN21:60;
      assume
A16:     0 < n;
      then 0+1 <= n by NAT_1:38;
      then 1 < n or n = 1 by REAL_1:def 5;
      then N-bound L~Cage(C,n) <= N-bound L~Cage(C,1) by JORDAN10:7;
      then
A17:   s`2 <= n0 by A15,AXIOMS:22;
A18:   s`1 = w by A12;
      p`2 <= s`2 by A16,JORDAN21:76;
      then
A19:   s in H by A3,A4,A8,A17,A18,GOBOARD7:8;
      s in U by A16;
      then
A20:   s in H /\ U by A19,XBOOLE_0:def 3;
      s`2 = proj2.s by PSCOMP_1:def 29;
      hence thesis by A20,FUNCT_2:43;
    end;
    H is Bounded by JORDAN2C:73;
    then H /\ U is Bounded by JORDAN1I:1;
    then proj2.:(H /\ U) is bounded by JORDAN1A:27;
    then
A21: proj2.:(H /\ U) is bounded_below by SEQ_4:def 3;
A22: (UMP Upper_Arc L~Cage(C,1))`2 <= n0 by JORDAN21:60;
A23: p`2 <= (UMP Upper_Arc L~Cage(C,1))`2 by JORDAN21:76;
A24: (UMP Upper_Arc L~Cage(C,1))`2 in proj2.:(H /\ U) by A14;
    then q <= (UMP Upper_Arc L~Cage(C,1))`2 by A21,SEQ_4:def 5;
    then
A25: q <= n0 by A22,AXIOMS:22;
    per cases;
    suppose
A26:   p <> |[w,q]|;
A27: now
      assume
A28:     p`2 >= q;
      per cases by A28,REAL_1:def 5;
      suppose p`2 = q;
      hence contradiction by A9,A26,JORDAN21:33;
      end;
      suppose p`2 > q;
      then 0 < p`2 - q by SQUARE_1:11;
      then consider r being real number such that
A29:     r in proj2.:(H /\ U) and
A30:     r < q+(p`2 - q) by A21,A24,SEQ_4:def 5;
      consider t being Point of TOP-REAL 2 such that
A31:     t in H /\ U and
A32:     proj2.t = r by A29,Lm2;
A33:   t`2 = r by A32,PSCOMP_1:def 29;
A34:   p`2 <= n0 by A22,A23,AXIOMS:22;
      t in H by A31,XBOOLE_0:def 3;
      hence contradiction by A4,A30,A33,A34,TOPREAL1:10;
    end;
    end;
    consider S',C' being Subset of TopSpaceMetr Euclid 2 such that
A35:   S = S' & C = C' and
A36:   dist_min(S,C) = min_dist_min(S',C') by JORDAN1K:def 1;
    S misses C
    proof
      assume S meets C;
      then consider x being set such that
A37:     x in S and
A38:     x in C by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A37;
A39:   x`2 <= N-bound C by A38,PSCOMP_1:71;
A40:   q <= x`2 by A4,A6,A25,A37,TOPREAL1:10;
      then
A41:   p`2 < x`2 by A27,AXIOMS:22;
      x`1 = w by A5,A7,A10,A37,SPPOL_1:def 3;
      then
      x in LSeg(p, |[w, N-bound C]|) by A1,A2,A8,A39,A41,GOBOARD7:8;
      then x in {p} by A11,A38,XBOOLE_0:def 3;
      hence contradiction by A27,A40,TARSKI:def 1;
    end;
    then dist_min(S,C) > 0 by A35,A36,Lm1,JGRAPH_1:55;
    then dist_min(S,C)/2 > 0 by SEQ_2:3;
    then consider k being Nat such that
A42:   1 < k and
A43:   dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < dist_min(S,C)/2 &
      dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < dist_min(S,C)/2 by GOBRD14:21;
    set f = Cage(C,k),
        G = Gauge(C,k);
A44: dist(G*(1,1),G*(1+1,1)) + dist(G*(1,1),G*(1,1+1))
      < dist_min(S,C)/2 + dist_min(S,C)/2 by A43,REAL_1:67;
    set s = UMP Upper_Arc L~f;
A45: s`1 = w by A12;
A46: s`2 <= N-bound L~f by JORDAN21:60;
    N-bound L~Cage(C,k) <= N-bound L~Cage(C,1) by A42,JORDAN10:7;
    then
A47: s`2 <= n0 by A46,AXIOMS:22;
    0 < k by A42;
    then s`2 in proj2.:(H /\ U) by A14;
    then
A48: q <= s`2 by A21,SEQ_4:def 5;
A49: s in S by A3,A4,A5,A6,A45,A47,A48,GOBOARD7:8;
A50: Upper_Arc L~f c= L~f by JORDAN1A:16;
    s in Upper_Arc L~f by JORDAN21:70;
    then consider i being Nat such that
A51:   1 <= i & i+1 <= len f and
A52:   s in LSeg(f,i) by A50,SPPOL_2:13;
    right_cell(f,i,G) meets C by A51,JORDAN9:33;
    then consider c being set such that
A53:   c in right_cell(f,i,G) and
A54:   c in C by XBOOLE_0:3;
    reconsider c as Point of TOP-REAL 2 by A54;
A55: f is_sequence_on G by JORDAN9:def 1;
    consider i1,j1,i2,j2 being Nat such that
A56:   [i1,j1] in Indices G and
A57:   f/.i = G*(i1,j1) and
A58:   [i2,j2] in Indices G and
A59:   f/.(i+1) = G*(i2,j2) and
A60:   i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
        i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A51,A55,JORDAN8:6;
    left_cell(f,i,G) /\ right_cell(f,i,G) = LSeg(f,i) by A51,A55,GOBRD13:30;
    then
A61: s in right_cell(f,i,G) by A52,XBOOLE_0:def 3;
A62: i1+1<>i1+0 by NAT_1:38;
A63: i2+1+1<>i2 by Lm3;
A64: j2+1+1<>j2 by Lm3;
A65: 1 <= i1 by A56,GOBOARD5:1;
A66: 1 <= j1 by A56,GOBOARD5:1;
A67: 1 <= i2 by A58,GOBOARD5:1;
A68: 1 <= j2 by A58,GOBOARD5:1;
A69: 1 <= i1+1 by NAT_1:29;
A70: 1 <= j1+1 by NAT_1:29;
A71: i1 <= len G by A56,GOBOARD5:1;
A72: j1 <= width G by A56,GOBOARD5:1;
A73: i2 <= len G by A58,GOBOARD5:1;
A74: j2 <= width G by A58,GOBOARD5:1;
    reconsider s' = s, c' = c as Point of Euclid 2 by Lm1,TOPMETR:16;
A75: min_dist_min(S',C') <= dist(s',c') by A35,A49,A54,Lm1,WEIERSTR:40;
A76: dist(s',c') = dist(s,c) by GOBRD14:def 1;
A77: [1,1] in Indices G by JORDAN21:14;
    [1,1+1] in Indices G by JORDAN21:15;
    then
A78: dist(G*(1,1),G*(1,1+1)) = (N-bound C - S-bound C)/2|^k by A77,GOBRD14:19;
    [1+1,1] in Indices G by JORDAN21:16;
    then
A79: dist(G*(1,1),G*(1+1,1)) = (E-bound C - W-bound C)/2|^k by A77,GOBRD14:20;
    now
      per cases by A60;
      suppose
A80:     i1 = i2 & j1+1 = j2;
      then
A81:   j1+1 <= width G by A58,GOBOARD5:1;
      i1 < len G by A51,A56,A57,A58,A59,A80,JORDAN10:1;
      then
A82:   i1+1 <= len G by NAT_1:38;
      right_cell(f,i,G) = cell(G,i1,j1)
        by A51,A55,A56,A57,A58,A59,A62,A64,A80,GOBRD13:def 2;
      then
      G*(i1,j1)`1 <= c`1 & c`1 <= G*(i1+1,j1)`1 &
      G*(i1,j1)`2 <= c`2 & c`2 <= G*(i1,j1+1)`2 &
      G*(i1,j1)`1 <= s`1 & s`1 <= G*(i1+1,j1)`1 &
      G*(i1,j1)`2 <= s`2 & s`2 <= G*(i1,j1+1)`2
        by A53,A61,A65,A66,A81,A82,JORDAN9:19;
      then
A83:   dist(s,c) <=
        (G*(i1+1,j1)`1-G*(i1,j1)`1) + (G*(i1,j1+1)`2-G*(i1,j1)`2)
          by GOBRD14:12;
A84:   [i1+1,j1] in Indices G by A66,A69,A72,A82,GOBOARD7:10;
      then
A85:   dist(G*(i1,j1),G*(i1+1,j1)) = G*(i1+1,j1)`1 - G*(i1,j1)`1
        by A56,GOBRD14:15;
A86:   dist(G*(i1,j1),G*(i1,j1+1)) = G*(i1,j1+1)`2 - G*(i1,j1)`2
        by A56,A58,A80,GOBRD14:16;
A87:   dist(G*(i1,j1),G*(i1,j1+1)) = (N-bound C - S-bound C)/2|^k
        by A56,A58,A80,GOBRD14:19;
      dist(G*(i1,j1),G*(i1+1,j1)) = (E-bound C - W-bound C)/2|^k
        by A56,A84,GOBRD14:20;
      hence contradiction by A36,A44,A75,A76,A78,A79,A83,A85,A86,A87,AXIOMS:22;
      end;
      suppose
A88:     i1+1 = i2 & j1 = j2;
      then
A89:   i1+1 <= len G by A58,GOBOARD5:1;
      1 < j1 by A51,A56,A57,A58,A59,A88,JORDAN10:3;
      then
A90:   1 <= j1-'1 by JORDAN3:12;
A91:   j1-'1 <= width G by A72,JORDAN3:7;
A92:   j1-'1+1 = j1 by A90,JORDAN3:6;
      right_cell(f,i,G) = cell(G,i1,j1-'1)
        by A51,A55,A56,A57,A58,A59,A62,A63,A88,GOBRD13:def 2;
      then
      G*(i1,j1-'1)`1 <= c`1 & c`1 <= G*(i1+1,j1-'1)`1 &
      G*(i1,j1-'1)`2 <= c`2 & c`2 <= G*(i1,j1-'1+1)`2 &
      G*(i1,j1-'1)`1 <= s`1 & s`1 <= G*(i1+1,j1-'1)`1 &
      G*(i1,j1-'1)`2 <= s`2 & s`2 <= G*(i1,j1-'1+1)`2
        by A53,A61,A65,A72,A89,A90,A92,JORDAN9:19;
      then
A93:   dist(s,c) <=
        (G*(i1+1,j1-'1)`1-G*(i1,j1-'1)`1) + (G*(i1,j1-'1+1)`2-G*(i1,j1-'1)`2)
          by GOBRD14:12;
A94:   [i1,j1-'1] in Indices G by A65,A71,A90,A91,GOBOARD7:10;
A95:   [i1+1,j1-'1] in Indices G by A67,A73,A88,A90,A91,GOBOARD7:10;
      then
A96:   dist(G*(i1,j1-'1),G*(i1+1,j1-'1)) = G*(i1+1,j1-'1)`1 - G*(i1,j1-'1)`1
        by A94,GOBRD14:15;
A97:   dist(G*(i1,j1-'1),G*(i1,j1-'1+1)) = G*(i1,j1-'1+1)`2 - G*(i1,j1-'1)`2
        by A56,A92,A94,GOBRD14:16;
A98:   dist(G*(i1,j1-'1),G*(i1,j1-'1+1)) = (N-bound C - S-bound C)/2|^k
        by A56,A92,A94,GOBRD14:19;
      dist(G*(i1,j1-'1),G*(i1+1,j1-'1)) = (E-bound C - W-bound C)/2|^k
        by A94,A95,GOBRD14:20;
      hence contradiction by A36,A44,A75,A76,A78,A79,A93,A96,A97,A98,AXIOMS:22;
      end;
      suppose
A99:     i1 = i2+1 & j1 = j2;
      then
A100:   i2+1 <= len G by A56,GOBOARD5:1;
      j1 < width G by A51,A56,A57,A58,A59,A99,JORDAN10:4;
      then
A101:   j1+1 <= width G by NAT_1:38;
      right_cell(f,i,G) = cell(G,i2,j2)
        by A51,A55,A56,A57,A58,A59,A62,A63,A99,GOBRD13:def 2;
      then
      G*(i2,j2)`1 <= c`1 & c`1 <= G*(i2+1,j2)`1 &
      G*(i2,j2)`2 <= c`2 & c`2 <= G*(i2,j2+1)`2 &
      G*(i2,j2)`1 <= s`1 & s`1 <= G*(i2+1,j2)`1 &
      G*(i2,j2)`2 <= s`2 & s`2 <= G*(i2,j2+1)`2
        by A53,A61,A66,A67,A99,A100,A101,JORDAN9:19;
      then
A102:   dist(s,c) <=
        (G*(i2+1,j2)`1-G*(i2,j2)`1) + (G*(i2,j2+1)`2-G*(i2,j2)`2)
          by GOBRD14:12;
A103:   dist(G*(i2,j2),G*(i2+1,j2)) = G*(i2+1,j2)`1 - G*(i2,j2)`1
        by A56,A58,A99,GOBRD14:15;
A104:   [i2,j2+1] in Indices G by A67,A70,A73,A99,A101,GOBOARD7:10;
      then
A105:   dist(G*(i2,j2),G*(i2,j2+1)) = G*(i2,j2+1)`2 - G*(i2,j2)`2
        by A58,GOBRD14:16;
A106:   dist(G*(i2,j2),G*(i2,j2+1)) = (N-bound C - S-bound C)/2|^k
        by A58,A104,GOBRD14:19;
      dist(G*(i2,j2),G*(i2+1,j2)) = (E-bound C - W-bound C)/2|^k
        by A56,A58,A99,GOBRD14:20;
      hence contradiction
        by A36,A44,A75,A76,A78,A79,A102,A103,A105,A106,AXIOMS:22;
      end;
      suppose
A107:     i1 = i2 & j1 = j2+1;
      then
A108:   j2+1 <= width G by A56,GOBOARD5:1;
      1 < i1 by A51,A56,A57,A58,A59,A107,JORDAN10:2;
      then
A109:   1 <= i1-'1 by JORDAN3:12;
A110:   i1-'1 <= len G by A71,JORDAN3:7;
A111:   i1-'1+1 = i1 by A109,JORDAN3:6;
      right_cell(f,i,G) = cell(G,i1-'1,j2)
        by A51,A55,A56,A57,A58,A59,A62,A64,A107,GOBRD13:def 2;
      then
      G*(i1-'1,j2)`1 <= c`1 & c`1 <= G*(i1-'1+1,j2)`1 &
      G*(i1-'1,j2)`2 <= c`2 & c`2 <= G*(i1-'1,j2+1)`2 &
      G*(i1-'1,j2)`1 <= s`1 & s`1 <= G*(i1-'1+1,j2)`1 &
      G*(i1-'1,j2)`2 <= s`2 & s`2 <= G*(i1-'1,j2+1)`2
        by A53,A61,A68,A71,A108,A109,A111,JORDAN9:19;
      then
A112:   dist(s,c) <=
        (G*(i1-'1+1,j2)`1-G*(i1-'1,j2)`1) + (G*(i1-'1,j2+1)`2-G*(i1-'1,j2)`2)
          by GOBRD14:12;
A113:   [i1-'1,j2] in Indices G by A68,A74,A109,A110,GOBOARD7:10;
A114:   [i1-'1+1,j2] in Indices G by A65,A68,A71,A74,A111,GOBOARD7:10;
      then
A115:   dist(G*(i1-'1,j2),G*(i1-'1+1,j2)) = G*(i1-'1+1,j2)`1 - G*(i1-'1,j2)`1
        by A113,GOBRD14:15;
A116:   [i1-'1,j2+1] in Indices G by A66,A72,A107,A109,A110,GOBOARD7:10;
      then
A117:   dist(G*(i1-'1,j2),G*(i1-'1,j2+1)) = G*(i1-'1,j2+1)`2 - G*(i1-'1,j2)`2
        by A113,GOBRD14:16;
A118:   dist(G*(i1-'1,j2),G*(i1-'1,j2+1)) = (N-bound C - S-bound C)/2|^k
        by A113,A116,GOBRD14:19;
      dist(G*(i1-'1,j2),G*(i1-'1+1,j2)) = (E-bound C - W-bound C)/2|^k
        by A113,A114,GOBRD14:20;
      hence contradiction
        by A36,A44,A75,A76,A78,A79,A112,A115,A117,A118,AXIOMS:22;
    end;
    end;
    hence thesis;
    end;

    suppose p = |[w,q]|;
    then
A119: p`2 = q by EUCLID:56;
A120: North_Arc C = Lim_inf Upper_Appr C by JORDAN19:def 3;
    reconsider e = p as Point of Euclid 2 by Lm1,TOPMETR:16;

    ex S being Real_Sequence of 2 st S is convergent &
      (for x being Nat holds S.x in (Upper_Appr C).x) & p = lim S
    proof
      set R = {(UMP Upper_Arc L~Cage(C,n))`2 where n is Nat: 0 < n};
      R c= REAL
      proof
        let x be set;
        assume x in R;
        then ex n being Nat st x = (UMP Upper_Arc L~Cage(C,n))`2 & 0 < n;
        hence thesis;
      end;
      then reconsider R as Subset of REAL;
A121:   (UMP Upper_Arc L~Cage(C,1))`2 in R;
A122:   R is bounded_below
      proof
        take q;
        let r be real number;
        assume r in R;
        then ex n being Nat st r = (UMP Upper_Arc L~Cage(C,n))`2 & 0 < n;
        then r in proj2.:(H /\ U) by A14;
        hence q<=r by A21,SEQ_4:def 5;
      end;
      deffunc f(Nat) = UMP Upper_Arc L~Cage(C,$1);
      consider S being Real_Sequence of 2 such that
A123:     for n being Nat holds S.n = f(n) from TOPRNS_1:sch 1;
      take S;
A124:   for r being Real st 0<r
       ex n being Nat st for m being Nat st n<=m holds
        |. S.m-p .| < r
      proof
        let r be Real;
        assume
A125:       0 < r;
A126:     for r being real number st r in R holds q<=r
        proof
          let r be real number;
          assume r in R;
          then ex n being Nat st r = (UMP Upper_Arc L~Cage(C,n))`2 & 0 < n;
          then r in proj2.:(H /\ U) by A14;
          hence thesis by A21,SEQ_4:def 5;
        end;
        for s being real number st 0<s
         ex r being real number st r in R & r<q+s
        proof
          let s be real number;
          assume 0<s;
          then consider r being real number such that
A127:         r in proj2.:(H /\ U) and
A128:         r<q+s by A21,A24,SEQ_4:def 5;
          take r;
          consider x being Point of TOP-REAL 2 such that
A129:         x in H /\ U and
A130:         proj2.x = r by A127,Lm2;
          x in U by A129,XBOOLE_0:def 3;
          then consider n being Nat such that
A131:         x = UMP Upper_Arc L~Cage(C,n) and
A132:         0 < n;
          r = (UMP Upper_Arc L~Cage(C,n))`2 by A130,A131,PSCOMP_1:def 29;
          hence thesis by A128,A132;
        end;
        then
A133:     inf R = q by A121,A122,A126,SEQ_4:def 5;
        consider v being real number such that
A134:       v in R and
A135:       v < inf R + r by A121,A122,A125,SEQ_4:def 5;
A136:   v - inf R < inf R + r - inf R by A135,REAL_1:92;
        consider n being Nat such that
A137:       v = (UMP Upper_Arc L~Cage(C,n))`2 and
A138:       0 < n by A134;
        take n;
        let m be Nat;
A139:     S.m = UMP Upper_Arc L~Cage(C,m) by A123;
        assume
A140:       n <= m;
        then (UMP Upper_Arc L~Cage(C,m))`2 <= (UMP Upper_Arc L~Cage(C,n))`2
          by A138,JORDAN21:80;
        then (S.m)`2 <= v by A123,A137;
        then (S.m)`2 - p`2 <= v - p`2 by REAL_1:92;
        then
A141:     (S.m)`2-p`2 < r by A119,A133,A136,AXIOMS:22;
        0 < m by A138,A140;
        then (UMP C)`2 < (UMP Upper_Arc L~Cage(C,m))`2 by JORDAN21:76;
        then
A142:     0 < (S.m)`2-p`2 by A139,SQUARE_1:11;
        (S.m)`1 = w by A12,A139;
        then abs((S.m)`1-p`1) = 0 by A8,ABSVALUE:def 1;
        then |. S.m-p .|<=0+abs((S.m)`2-p`2) by JGRAPH_1:49;
        then |. S.m-p .|<=(S.m)`2-p`2 by A142,ABSVALUE:def 1;
        hence |. S.m-p .| < r by A141,AXIOMS:22;
      end;
      thus
A143:     S is convergent
      proof
        take p;
        thus thesis by A124;
      end;
      hereby
        let x be Nat;
A144:     S.x = UMP Upper_Arc L~Cage(C,x) by A123;
        (Upper_Appr C).x = Upper_Arc L~Cage(C,x) by JORDAN19:def 1;
        hence S.x in (Upper_Appr C).x by A144,JORDAN21:70;
      end;
      thus p = lim S by A124,A143,TOPRNS_1:def 10;
    end;
    hence thesis by A120,KURATO_2:58;
  end;
  end;

theorem Th10:
  LMP C in South_Arc C
  proof
    set w = (W-bound C + E-bound C) / 2;
    set p = LMP C;
    set U = {LMP Lower_Arc L~Cage(C,n) where n is Nat: 0 < n};
    {LMP Lower_Arc L~Cage(C,n) where n is Nat: 0 < n} c=
      the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in {LMP Lower_Arc L~Cage(C,n) where n is Nat: 0 < n};
      then ex n being Nat st x = LMP Lower_Arc L~Cage(C,n) & 0 < n;
      hence thesis;
    end;
    then reconsider U as Subset of TOP-REAL 2;
    set n0 = S-bound L~Cage(C,1);
    set H = LSeg(p,|[w,n0]|);
    set q = sup(proj2.:(H /\ U));
    set S = LSeg(|[w,q]|, |[w,n0]|);
A1: |[w,S-bound C]|`1 = w by EUCLID:56;
A2: |[w,S-bound C]|`2 = S-bound C by EUCLID:56;
A3: |[w,n0]|`1 = w by EUCLID:56;
A4: |[w,n0]|`2 = n0 by EUCLID:56;
A5: |[w,q]|`1 = w by EUCLID:56;
A6: |[w,q]|`2 = q by EUCLID:56;
A7: |[w,q]| in S by TOPREAL1:6;
A8: p`1 = w by JORDAN21:35;
A9: p = |[p`1,p`2]| by EUCLID:57;
A10: S is vertical by A3,A5,SPPOL_1:37;
A11: LSeg(p, |[w, S-bound C]|) /\ C = {p} by JORDAN21:48;
A12: for n being Nat holds (LMP Lower_Arc L~Cage(C,n))`1 = w
    proof
      let n be Nat;
A13:   W-bound L~Cage(C,n) + E-bound L~Cage(C,n) = W-bound C + E-bound C
        by JORDAN1G:41;
      thus (LMP Lower_Arc L~Cage(C,n))`1
         = (W-bound Lower_Arc L~Cage(C,n) + E-bound Lower_Arc L~Cage(C,n))/2
           by JORDAN21:35
        .= (W-bound L~Cage(C,n) + E-bound Lower_Arc L~Cage(C,n))/2
           by JORDAN21:28
        .= w by A13,JORDAN21:29;
    end;
A14: for n being Nat st 0 < n holds
      (LMP Lower_Arc L~Cage(C,n))`2 in proj2.:(H /\ U)
    proof
      let n be Nat;
      set f = Cage(C,n);
      set s = LMP Lower_Arc L~f;
A15:   S-bound L~f <= s`2 by JORDAN21:61;
      assume
A16:     0 < n;
      then 0+1 <= n by NAT_1:38;
      then n = 1 or n > 1 by REAL_1:def 5;
      then S-bound L~Cage(C,n) >= S-bound L~Cage(C,1) by JORDAN1A:90;
      then
A17:   s`2 >= n0 by A15,AXIOMS:22;
A18:   s`1 = w by A12;
      p`2 >= s`2 by A16,JORDAN21:77;
      then
A19:   s in H by A3,A4,A8,A17,A18,GOBOARD7:8;
      s in U by A16;
      then
A20:   s in H /\ U by A19,XBOOLE_0:def 3;
      s`2 = proj2.s by PSCOMP_1:def 29;
      hence thesis by A20,FUNCT_2:43;
    end;
    H is Bounded by JORDAN2C:73;
    then H /\ U is Bounded by JORDAN1I:1;
    then proj2.:(H /\ U) is bounded by JORDAN1A:27;
    then
A21: proj2.:(H /\ U) is bounded_above by SEQ_4:def 3;
A22: (LMP Lower_Arc L~Cage(C,1))`2 >= n0 by JORDAN21:61;
A23: p`2 >= (LMP Lower_Arc L~Cage(C,1))`2 by JORDAN21:77;
A24: (LMP Lower_Arc L~Cage(C,1))`2 in proj2.:(H /\ U) by A14;
    then q >= (LMP Lower_Arc L~Cage(C,1))`2 by A21,SEQ_4:def 4;
    then
A25: q >= n0 by A22,AXIOMS:22;
    per cases;
    suppose
A26:   p <> |[w,q]|;
A27: now
      assume
A28:     p`2 <= q;
      per cases by A28,REAL_1:def 5;
      suppose p`2 = q;
      hence contradiction by A9,A26,JORDAN21:35;
      end;
      suppose p`2 < q;
      then 0 < q - p`2 by SQUARE_1:11;
      then consider r being real number such that
A29:     r in proj2.:(H /\ U) and
A30:     q-(q-p`2) < r by A21,A24,SEQ_4:def 4;
      consider t being Point of TOP-REAL 2 such that
A31:     t in H /\ U and
A32:     proj2.t = r by A29,Lm2;
A33:   t`2 = r by A32,PSCOMP_1:def 29;
A34:   p`2 >= n0 by A22,A23,AXIOMS:22;
      t in H by A31,XBOOLE_0:def 3;
      hence contradiction by A4,A30,A33,A34,TOPREAL1:10;
    end;
    end;
    consider S',C' being Subset of TopSpaceMetr Euclid 2 such that
A35:   S = S' & C = C' and
A36:   dist_min(S,C) = min_dist_min(S',C') by JORDAN1K:def 1;
    S misses C
    proof
      assume S meets C;
      then consider x being set such that
A37:     x in S and
A38:     x in C by XBOOLE_0:3;
      reconsider x as Point of TOP-REAL 2 by A37;
A39:   x`2 >= S-bound C by A38,PSCOMP_1:71;
A40:   q >= x`2 by A4,A6,A25,A37,TOPREAL1:10;
      then
A41:   p`2 > x`2 by A27,AXIOMS:22;
      x`1 = w by A5,A7,A10,A37,SPPOL_1:def 3;
      then
      x in LSeg(p, |[w, S-bound C]|) by A1,A2,A8,A39,A41,GOBOARD7:8;
      then x in {p} by A11,A38,XBOOLE_0:def 3;
      hence contradiction by A27,A40,TARSKI:def 1;
    end;
    then dist_min(S,C) > 0 by A35,A36,Lm1,JGRAPH_1:55;
    then dist_min(S,C)/2 > 0 by SEQ_2:3;
    then consider k being Nat such that
A42:   1 < k and
A43:   dist(Gauge(C,k)*(1,1),Gauge(C,k)*(1,2)) < dist_min(S,C)/2 &
      dist(Gauge(C,k)*(1,1),Gauge(C,k)*(2,1)) < dist_min(S,C)/2 by GOBRD14:21;
    set f = Cage(C,k),
        G = Gauge(C,k);
A44: dist(G*(1,1),G*(1+1,1)) + dist(G*(1,1),G*(1,1+1))
      < dist_min(S,C)/2 + dist_min(S,C)/2 by A43,REAL_1:67;
    set s = LMP Lower_Arc L~f;
A45: s`1 = w by A12;
A46: s`2 >= S-bound L~f by JORDAN21:61;
    S-bound L~Cage(C,k) >= S-bound L~Cage(C,1) by A42,JORDAN1A:90;
    then
A47: s`2 >= n0 by A46,AXIOMS:22;
    0 < k by A42;
    then s`2 in proj2.:(H /\ U) by A14;
    then
A48: q >= s`2 by A21,SEQ_4:def 4;
A49: s in S by A3,A4,A5,A6,A45,A47,A48,GOBOARD7:8;
A50: Lower_Arc L~f c= L~f by JORDAN1A:16;
    s in Lower_Arc L~f by JORDAN21:71;
    then consider i being Nat such that
A51:   1 <= i & i+1 <= len f and
A52:   s in LSeg(f,i) by A50,SPPOL_2:13;
    right_cell(f,i,G) meets C by A51,JORDAN9:33;
    then consider c being set such that
A53:   c in right_cell(f,i,G) and
A54:   c in C by XBOOLE_0:3;
    reconsider c as Point of TOP-REAL 2 by A54;
A55: f is_sequence_on G by JORDAN9:def 1;
    consider i1,j1,i2,j2 being Nat such that
A56:   [i1,j1] in Indices G and
A57:   f/.i = G*(i1,j1) and
A58:   [i2,j2] in Indices G and
A59:   f/.(i+1) = G*(i2,j2) and
A60:   i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
        i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A51,A55,JORDAN8:6;
    left_cell(f,i,G) /\ right_cell(f,i,G) = LSeg(f,i) by A51,A55,GOBRD13:30;
    then
A61: s in right_cell(f,i,G) by A52,XBOOLE_0:def 3;
A62: i1+1<>i1+0 by NAT_1:38;
A63: i2+1+1<>i2 by Lm3;
A64: j2+1+1<>j2 by Lm3;
A65: 1 <= i1 by A56,GOBOARD5:1;
A66: 1 <= j1 by A56,GOBOARD5:1;
A67: 1 <= i2 by A58,GOBOARD5:1;
A68: 1 <= j2 by A58,GOBOARD5:1;
A69: 1 <= i1+1 by NAT_1:29;
A70: 1 <= j1+1 by NAT_1:29;
A71: i1 <= len G by A56,GOBOARD5:1;
A72: j1 <= width G by A56,GOBOARD5:1;
A73: i2 <= len G by A58,GOBOARD5:1;
A74: j2 <= width G by A58,GOBOARD5:1;
    reconsider s' = s, c' = c as Point of Euclid 2 by Lm1,TOPMETR:16;
A75: min_dist_min(S',C') <= dist(s',c') by A35,A49,A54,Lm1,WEIERSTR:40;
A76: dist(s',c') = dist(s,c) by GOBRD14:def 1;
A77: [1,1] in Indices G by JORDAN21:14;
    [1,1+1] in Indices G by JORDAN21:15;
    then
A78: dist(G*(1,1),G*(1,1+1)) = (N-bound C - S-bound C)/2|^k by A77,GOBRD14:19;
    [1+1,1] in Indices G by JORDAN21:16;
    then
A79: dist(G*(1,1),G*(1+1,1)) = (E-bound C - W-bound C)/2|^k by A77,GOBRD14:20;
    now
      per cases by A60;
      suppose
A80:     i1 = i2 & j1+1 = j2;
      then
A81:   j1+1 <= width G by A58,GOBOARD5:1;
      i1 < len G by A51,A56,A57,A58,A59,A80,JORDAN10:1;
      then
A82:   i1+1 <= len G by NAT_1:38;
      right_cell(f,i,G) = cell(G,i1,j1)
        by A51,A55,A56,A57,A58,A59,A62,A64,A80,GOBRD13:def 2;
      then
      G*(i1,j1)`1 <= c`1 & c`1 <= G*(i1+1,j1)`1 &
      G*(i1,j1)`2 <= c`2 & c`2 <= G*(i1,j1+1)`2 &
      G*(i1,j1)`1 <= s`1 & s`1 <= G*(i1+1,j1)`1 &
      G*(i1,j1)`2 <= s`2 & s`2 <= G*(i1,j1+1)`2
        by A53,A61,A65,A66,A81,A82,JORDAN9:19;
      then
A83:   dist(s,c) <=
        (G*(i1+1,j1)`1-G*(i1,j1)`1) + (G*(i1,j1+1)`2-G*(i1,j1)`2)
          by GOBRD14:12;
A84:   [i1+1,j1] in Indices G by A66,A69,A72,A82,GOBOARD7:10;
      then
A85:   dist(G*(i1,j1),G*(i1+1,j1)) = G*(i1+1,j1)`1 - G*(i1,j1)`1
        by A56,GOBRD14:15;
A86:   dist(G*(i1,j1),G*(i1,j1+1)) = G*(i1,j1+1)`2 - G*(i1,j1)`2
        by A56,A58,A80,GOBRD14:16;
A87:   dist(G*(i1,j1),G*(i1,j1+1)) = (N-bound C - S-bound C)/2|^k
        by A56,A58,A80,GOBRD14:19;
      dist(G*(i1,j1),G*(i1+1,j1)) = (E-bound C - W-bound C)/2|^k
        by A56,A84,GOBRD14:20;
      hence contradiction by A36,A44,A75,A76,A78,A79,A83,A85,A86,A87,AXIOMS:22;
      end;
      suppose
A88:     i1+1 = i2 & j1 = j2;
      then
A89:   i1+1 <= len G by A58,GOBOARD5:1;
      1 < j1 by A51,A56,A57,A58,A59,A88,JORDAN10:3;
      then
A90:   1 <= j1-'1 by JORDAN3:12;
A91:   j1-'1 <= width G by A72,JORDAN3:7;
A92:   j1-'1+1 = j1 by A90,JORDAN3:6;
      right_cell(f,i,G) = cell(G,i1,j1-'1)
        by A51,A55,A56,A57,A58,A59,A62,A63,A88,GOBRD13:def 2;
      then
      G*(i1,j1-'1)`1 <= c`1 & c`1 <= G*(i1+1,j1-'1)`1 &
      G*(i1,j1-'1)`2 <= c`2 & c`2 <= G*(i1,j1-'1+1)`2 &
      G*(i1,j1-'1)`1 <= s`1 & s`1 <= G*(i1+1,j1-'1)`1 &
      G*(i1,j1-'1)`2 <= s`2 & s`2 <= G*(i1,j1-'1+1)`2
        by A53,A61,A65,A72,A89,A90,A92,JORDAN9:19;
      then
A93:   dist(s,c) <=
        (G*(i1+1,j1-'1)`1-G*(i1,j1-'1)`1) + (G*(i1,j1-'1+1)`2-G*(i1,j1-'1)`2)
          by GOBRD14:12;
A94:   [i1,j1-'1] in Indices G by A65,A71,A90,A91,GOBOARD7:10;
A95:   [i1+1,j1-'1] in Indices G by A67,A73,A88,A90,A91,GOBOARD7:10;
      then
A96:   dist(G*(i1,j1-'1),G*(i1+1,j1-'1)) = G*(i1+1,j1-'1)`1 - G*(i1,j1-'1)`1
        by A94,GOBRD14:15;
A97:   dist(G*(i1,j1-'1),G*(i1,j1-'1+1)) = G*(i1,j1-'1+1)`2 - G*(i1,j1-'1)`2
        by A56,A92,A94,GOBRD14:16;
A98:   dist(G*(i1,j1-'1),G*(i1,j1-'1+1)) = (N-bound C - S-bound C)/2|^k
        by A56,A92,A94,GOBRD14:19;
      dist(G*(i1,j1-'1),G*(i1+1,j1-'1)) = (E-bound C - W-bound C)/2|^k
        by A94,A95,GOBRD14:20;
      hence contradiction by A36,A44,A75,A76,A78,A79,A93,A96,A97,A98,AXIOMS:22;
      end;
      suppose
A99:     i1 = i2+1 & j1 = j2;
      then
A100:   i2+1 <= len G by A56,GOBOARD5:1;
      j1 < width G by A51,A56,A57,A58,A59,A99,JORDAN10:4;
      then
A101:   j1+1 <= width G by NAT_1:38;
      right_cell(f,i,G) = cell(G,i2,j2)
        by A51,A55,A56,A57,A58,A59,A62,A63,A99,GOBRD13:def 2;
      then
      G*(i2,j2)`1 <= c`1 & c`1 <= G*(i2+1,j2)`1 &
      G*(i2,j2)`2 <= c`2 & c`2 <= G*(i2,j2+1)`2 &
      G*(i2,j2)`1 <= s`1 & s`1 <= G*(i2+1,j2)`1 &
      G*(i2,j2)`2 <= s`2 & s`2 <= G*(i2,j2+1)`2
        by A53,A61,A66,A67,A99,A100,A101,JORDAN9:19;
      then
A102:   dist(s,c) <=
        (G*(i2+1,j2)`1-G*(i2,j2)`1) + (G*(i2,j2+1)`2-G*(i2,j2)`2)
          by GOBRD14:12;
A103:   dist(G*(i2,j2),G*(i2+1,j2)) = G*(i2+1,j2)`1 - G*(i2,j2)`1
        by A56,A58,A99,GOBRD14:15;
A104:   [i2,j2+1] in Indices G by A67,A70,A73,A99,A101,GOBOARD7:10;
      then
A105:   dist(G*(i2,j2),G*(i2,j2+1)) = G*(i2,j2+1)`2 - G*(i2,j2)`2
        by A58,GOBRD14:16;
A106:   dist(G*(i2,j2),G*(i2,j2+1)) = (N-bound C - S-bound C)/2|^k
        by A58,A104,GOBRD14:19;
      dist(G*(i2,j2),G*(i2+1,j2)) = (E-bound C - W-bound C)/2|^k
        by A56,A58,A99,GOBRD14:20;
      hence contradiction
        by A36,A44,A75,A76,A78,A79,A102,A103,A105,A106,AXIOMS:22;
      end;
      suppose
A107:     i1 = i2 & j1 = j2+1;
      then
A108:   j2+1 <= width G by A56,GOBOARD5:1;
      1 < i1 by A51,A56,A57,A58,A59,A107,JORDAN10:2;
      then
A109:   1 <= i1-'1 by JORDAN3:12;
A110:   i1-'1 <= len G by A71,JORDAN3:7;
A111:   i1-'1+1 = i1 by A109,JORDAN3:6;
      right_cell(f,i,G) = cell(G,i1-'1,j2)
        by A51,A55,A56,A57,A58,A59,A62,A64,A107,GOBRD13:def 2;
      then
      G*(i1-'1,j2)`1 <= c`1 & c`1 <= G*(i1-'1+1,j2)`1 &
      G*(i1-'1,j2)`2 <= c`2 & c`2 <= G*(i1-'1,j2+1)`2 &
      G*(i1-'1,j2)`1 <= s`1 & s`1 <= G*(i1-'1+1,j2)`1 &
      G*(i1-'1,j2)`2 <= s`2 & s`2 <= G*(i1-'1,j2+1)`2
        by A53,A61,A68,A71,A108,A109,A111,JORDAN9:19;
      then
A112:   dist(s,c) <=
        (G*(i1-'1+1,j2)`1-G*(i1-'1,j2)`1) + (G*(i1-'1,j2+1)`2-G*(i1-'1,j2)`2)
          by GOBRD14:12;
A113:   [i1-'1,j2] in Indices G by A68,A74,A109,A110,GOBOARD7:10;
A114:   [i1-'1+1,j2] in Indices G by A65,A68,A71,A74,A111,GOBOARD7:10;
      then
A115:   dist(G*(i1-'1,j2),G*(i1-'1+1,j2)) = G*(i1-'1+1,j2)`1 - G*(i1-'1,j2)`1
        by A113,GOBRD14:15;
A116:   [i1-'1,j2+1] in Indices G by A66,A72,A107,A109,A110,GOBOARD7:10;
      then
A117:   dist(G*(i1-'1,j2),G*(i1-'1,j2+1)) = G*(i1-'1,j2+1)`2 - G*(i1-'1,j2)`2
        by A113,GOBRD14:16;
A118:   dist(G*(i1-'1,j2),G*(i1-'1,j2+1)) = (N-bound C - S-bound C)/2|^k
        by A113,A116,GOBRD14:19;
      dist(G*(i1-'1,j2),G*(i1-'1+1,j2)) = (E-bound C - W-bound C)/2|^k
        by A113,A114,GOBRD14:20;
      hence contradiction
        by A36,A44,A75,A76,A78,A79,A112,A115,A117,A118,AXIOMS:22;
    end;
    end;
    hence thesis;
    end;

    suppose p = |[w,q]|;
    then
A119: p`2 = q by EUCLID:56;
A120: South_Arc C = Lim_inf Lower_Appr C by JORDAN19:def 4;
    reconsider e = p as Point of Euclid 2 by Lm1,TOPMETR:16;

    ex S being Real_Sequence of 2 st S is convergent &
      (for x being Nat holds S.x in (Lower_Appr C).x) & p = lim S
    proof
      set R = {(LMP Lower_Arc L~Cage(C,n))`2 where n is Nat: 0 < n};
      R c= REAL
      proof
        let x be set;
        assume x in R;
        then ex n being Nat st x = (LMP Lower_Arc L~Cage(C,n))`2 & 0 < n;
        hence thesis;
      end;
      then reconsider R as Subset of REAL;
A121:   (LMP Lower_Arc L~Cage(C,1))`2 in R;
A122:   R is bounded_above
      proof
        take q;
        let r be real number;
        assume r in R;
        then ex n being Nat st r = (LMP Lower_Arc L~Cage(C,n))`2 & 0 < n;
        then r in proj2.:(H /\ U) by A14;
        hence q>=r by A21,SEQ_4:def 4;
      end;
      deffunc g(Nat) = LMP Lower_Arc L~Cage(C,$1);
      consider S being Real_Sequence of 2 such that
A123:     for n being Nat holds S.n = g(n) from TOPRNS_1:sch 1;
      take S;
A124:   for r being Real st 0<r
       ex n being Nat st for m being Nat st n<=m holds
        |. S.m-p .| < r
      proof
        let r be Real;
        assume
A125:       0 < r;
A126:     for r being real number st r in R holds q>=r
        proof
          let r be real number;
          assume r in R;
          then ex n being Nat st r = (LMP Lower_Arc L~Cage(C,n))`2 & 0 < n;
          then r in proj2.:(H /\ U) by A14;
          hence thesis by A21,SEQ_4:def 4;
        end;
        for s being real number st 0<s
         ex r being real number st r in R & q-s<r
        proof
          let s be real number;
          assume 0<s;
          then consider r being real number such that
A127:         r in proj2.:(H /\ U) and
A128:         q-s<r by A21,A24,SEQ_4:def 4;
          take r;
          consider x being Point of TOP-REAL 2 such that
A129:         x in H /\ U and
A130:         proj2.x = r by A127,Lm2;
          x in U by A129,XBOOLE_0:def 3;
          then consider n being Nat such that
A131:         x = LMP Lower_Arc L~Cage(C,n) and
A132:         0 < n;
          r = (LMP Lower_Arc L~Cage(C,n))`2 by A130,A131,PSCOMP_1:def 29;
          hence thesis by A128,A132;
        end;
        then
A133:     sup R = q by A121,A122,A126,SEQ_4:def 4;
        consider v being real number such that
A134:       v in R and
A135:       sup R - r < v by A121,A122,A125,SEQ_4:def 4;
        sup R - r + r < v + r by A135,REAL_1:53;
        then
A136:   sup R - v < v + r - v by REAL_1:92;
        consider n being Nat such that
A137:       v = (LMP Lower_Arc L~Cage(C,n))`2 and
A138:       0 < n by A134;
        take n;
        let m be Nat;
A139:     S.m = LMP Lower_Arc L~Cage(C,m) by A123;
        assume
A140:       n <= m;
        then (LMP Lower_Arc L~Cage(C,n))`2 <= (LMP Lower_Arc L~Cage(C,m))`2
          by A138,JORDAN21:81;
        then (S.m)`2 >= v by A123,A137;
        then p`2 - (S.m)`2 <= p`2 - v by REAL_1:92;
        then
A141:     p`2-(S.m)`2 < r by A119,A133,A136,AXIOMS:22;
        0 < m by A138,A140;
        then (LMP C)`2 > (LMP Lower_Arc L~Cage(C,m))`2 by JORDAN21:77;
        then
A142:     0 > (S.m)`2-p`2 by A139,REAL_2:105;
        (S.m)`1 = w by A12,A139;
        then abs((S.m)`1-p`1) = 0 by A8,ABSVALUE:def 1;
        then |. S.m-p .|<=0+abs((S.m)`2-p`2) by JGRAPH_1:49;
        then |. S.m-p .|<=-((S.m)`2-p`2) by A142,ABSVALUE:def 1;
        hence |. S.m-p .| < r by A141,AXIOMS:22;
      end;
      thus
A143:   S is convergent
      proof
        take p;
        thus thesis by A124;
      end;
      hereby
        let x be Nat;
A144:     S.x = LMP Lower_Arc L~Cage(C,x) by A123;
        (Lower_Appr C).x = Lower_Arc L~Cage(C,x) by JORDAN19:def 2;
        hence S.x in (Lower_Appr C).x by A144,JORDAN21:71;
      end;
      thus p = lim S by A124,A143,TOPRNS_1:def 10;
    end;
    hence thesis by A120,KURATO_2:58;
  end;
  end;

theorem Th11:
  North_Arc C c= C
  proof
    let x be set;
    assume
A1:   x in North_Arc C;
    assume
A2:   not x in C;
    reconsider x as Point of TOP-REAL 2 by A1;
    reconsider e = x as Point of Euclid 2 by Lm1,TOPMETR:16;
A3: North_Arc C = Lim_inf Upper_Appr C by JORDAN19:def 3;
A4: x in C` by A2,SUBSET_1:50;
A5: (BDD C) \/ (UBD C) = C` by JORDAN2C:31;
    per cases by A4,A5,XBOOLE_0:def 2;
    suppose x in BDD C;
    then consider r being real number such that
A6:   r > 0 and
A7:   Ball(e,r) c= BDD C by Lm1,TOPMETR:22;
    consider k being Nat such that
A8:   for m being Nat st m > k holds (Upper_Appr C).m meets Ball(e,r)
        by A1,A3,A6,KURATO_2:50;
A9: k+0 < k+1 by REAL_1:67;
    BDD C misses L~Cage(C,k+1) by JORDAN10:19;
    then
A10: Ball(e,r) misses L~Cage(C,k+1) by A7,XBOOLE_1:63;
A11: (Upper_Appr C).(k+1) = Upper_Arc L~Cage(C,k+1) by JORDAN19:def 1;
    Upper_Arc L~Cage(C,k+1) c= L~Cage(C,k+1) by JORDAN1A:16;
    then
    (Upper_Appr C).(k+1) misses Ball(e,r) by A10,A11,XBOOLE_1:63;
    hence thesis by A8,A9;
    end;
    suppose
A12:   x in UBD C;
    union UBD-Family C = UBD C by JORDAN10:14;
    then consider A being set such that
A13:   x in A and
A14:   A in UBD-Family C by A12,TARSKI:def 4;
    UBD-Family C = {UBD L~Cage(C,m) where m is Nat : not contradiction}
      by JORDAN10:def 1;
    then consider m being Nat such that
A15:   A = UBD L~Cage(C,m) by A14;
    x in LeftComp Cage(C,m) by A13,A15,GOBRD14:46;
    then consider r being real number such that
A16:   r > 0 and
A17:   Ball(e,r) c= LeftComp Cage(C,m) by Lm1,TOPMETR:22;
    consider k being Nat such that
A18:   for m being Nat st m > k holds (Upper_Appr C).m meets Ball(e,r)
        by A1,A3,A16,KURATO_2:50;
    thus thesis
    proof
      per cases;
      suppose
A19:     m > k;
      LeftComp Cage(C,m) misses L~Cage(C,m) by SPRECT_3:43;
      then
A20:   Ball(e,r) misses L~Cage(C,m) by A17,XBOOLE_1:63;
A21:   (Upper_Appr C).m = Upper_Arc L~Cage(C,m) by JORDAN19:def 1;
      Upper_Arc L~Cage(C,m) c= L~Cage(C,m) by JORDAN1A:16;
      then
      (Upper_Appr C).m misses Ball(e,r) by A20,A21,XBOOLE_1:63;
      hence thesis by A18,A19;
      end;
      suppose m <= k;
      then
A22:   LeftComp Cage(C,m) c= LeftComp Cage(C,k) by JORDAN1H:55;
A23:   k+0 < k+1 by REAL_1:67;
      then
A24:   LeftComp Cage(C,k) c= LeftComp Cage(C,k+1) by JORDAN1H:55;
      Ball(e,r) c= LeftComp Cage(C,k) by A17,A22,XBOOLE_1:1;
      then
A25:   Ball(e,r) c= LeftComp Cage(C,k+1) by A24,XBOOLE_1:1;
      LeftComp Cage(C,k+1) misses L~Cage(C,k+1) by SPRECT_3:43;
      then
A26:   Ball(e,r) misses L~Cage(C,k+1) by A25,XBOOLE_1:63;
A27:   (Upper_Appr C).(k+1) = Upper_Arc L~Cage(C,k+1) by JORDAN19:def 1;
      Upper_Arc L~Cage(C,k+1) c= L~Cage(C,k+1) by JORDAN1A:16;
      then
      (Upper_Appr C).(k+1) misses Ball(e,r) by A26,A27,XBOOLE_1:63;
      hence thesis by A18,A23;
    end;
  end;
  end;
  end;

theorem Th12:
  South_Arc C c= C
  proof
    let x be set;
    assume
A1:   x in South_Arc C;
    assume
A2:   not x in C;
    reconsider x as Point of TOP-REAL 2 by A1;
    reconsider e = x as Point of Euclid 2 by Lm1,TOPMETR:16;
A3: South_Arc C = Lim_inf Lower_Appr C by JORDAN19:def 4;
A4: x in C` by A2,SUBSET_1:50;
A5: (BDD C) \/ (UBD C) = C` by JORDAN2C:31;
    per cases by A4,A5,XBOOLE_0:def 2;
    suppose x in BDD C;
    then consider r being real number such that
A6:   r > 0 and
A7:   Ball(e,r) c= BDD C by Lm1,TOPMETR:22;
    consider k being Nat such that
A8:   for m being Nat st m > k holds (Lower_Appr C).m meets Ball(e,r)
        by A1,A3,A6,KURATO_2:50;
A9: k+0 < k+1 by REAL_1:67;
    BDD C misses L~Cage(C,k+1) by JORDAN10:19;
    then
A10: Ball(e,r) misses L~Cage(C,k+1) by A7,XBOOLE_1:63;
A11: (Lower_Appr C).(k+1) = Lower_Arc L~Cage(C,k+1) by JORDAN19:def 2;
    Lower_Arc L~Cage(C,k+1) c= L~Cage(C,k+1) by JORDAN1A:16;
    then
    (Lower_Appr C).(k+1) misses Ball(e,r) by A10,A11,XBOOLE_1:63;
    hence thesis by A8,A9;
    end;
    suppose
A12:   x in UBD C;
    union UBD-Family C = UBD C by JORDAN10:14;
    then consider A being set such that
A13:   x in A and
A14:   A in UBD-Family C by A12,TARSKI:def 4;
    UBD-Family C = {UBD L~Cage(C,m) where m is Nat : not contradiction}
      by JORDAN10:def 1;
    then consider m being Nat such that
A15:   A = UBD L~Cage(C,m) by A14;
    x in LeftComp Cage(C,m) by A13,A15,GOBRD14:46;
    then consider r being real number such that
A16:   r > 0 and
A17:   Ball(e,r) c= LeftComp Cage(C,m) by Lm1,TOPMETR:22;
    consider k being Nat such that
A18:   for m being Nat st m > k holds (Lower_Appr C).m meets Ball(e,r)
        by A1,A3,A16,KURATO_2:50;
    thus thesis
    proof
      per cases;
      suppose
A19:     m > k;
      LeftComp Cage(C,m) misses L~Cage(C,m) by SPRECT_3:43;
      then
A20:   Ball(e,r) misses L~Cage(C,m) by A17,XBOOLE_1:63;
A21:   (Lower_Appr C).m = Lower_Arc L~Cage(C,m) by JORDAN19:def 2;
      Lower_Arc L~Cage(C,m) c= L~Cage(C,m) by JORDAN1A:16;
      then
      (Lower_Appr C).m misses Ball(e,r) by A20,A21,XBOOLE_1:63;
      hence thesis by A18,A19;
      end;
      suppose m <= k;
      then
A22:   LeftComp Cage(C,m) c= LeftComp Cage(C,k) by JORDAN1H:55;
A23:   k+0 < k+1 by REAL_1:67;
      then
A24:   LeftComp Cage(C,k) c= LeftComp Cage(C,k+1) by JORDAN1H:55;
      Ball(e,r) c= LeftComp Cage(C,k) by A17,A22,XBOOLE_1:1;
      then
A25:   Ball(e,r) c= LeftComp Cage(C,k+1) by A24,XBOOLE_1:1;
      LeftComp Cage(C,k+1) misses L~Cage(C,k+1) by SPRECT_3:43;
      then
A26:   Ball(e,r) misses L~Cage(C,k+1) by A25,XBOOLE_1:63;
A27:   (Lower_Appr C).(k+1) = Lower_Arc L~Cage(C,k+1) by JORDAN19:def 2;
      Lower_Arc L~Cage(C,k+1) c= L~Cage(C,k+1) by JORDAN1A:16;
      then
      (Lower_Appr C).(k+1) misses Ball(e,r) by A26,A27,XBOOLE_1:63;
      hence thesis by A18,A23;
      end;
    end;
  end;
  end;

theorem
  LMP C in Lower_Arc C & UMP C in Upper_Arc C or
  UMP C in Lower_Arc C & LMP C in Upper_Arc C
proof
A1: South_Arc C c= C by Th12;
A2: LMP C in South_Arc C by Th10;
A3: North_Arc C c= C by Th11;
A4: UMP C in North_Arc C by Th9;
  now
    per cases by A1,A2,A3,A4,JORDAN16:11;
    case LE LMP C, UMP C, C;
    then LMP C in Upper_Arc C & UMP C in Lower_Arc C
      or LMP C in Lower_Arc C & UMP C in Lower_Arc C
      or LMP C in Upper_Arc C & UMP C in Upper_Arc C by JORDAN6:def 10;
    hence UMP C in Lower_Arc C & LMP C in Upper_Arc C by JORDAN21:62,63;
    end;
    case LE UMP C, LMP C, C;
    then UMP C in Upper_Arc C & LMP C in Lower_Arc C
      or LMP C in Lower_Arc C & UMP C in Lower_Arc C
      or LMP C in Upper_Arc C & UMP C in Upper_Arc C by JORDAN6:def 10;
    hence LMP C in Lower_Arc C & UMP C in Upper_Arc C by JORDAN21:62,63;
    end;
  end;
  hence thesis;
end;
