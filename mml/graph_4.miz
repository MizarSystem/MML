:: Oriented Simple Chains Included in Oriented Chains
::  by Yatsuka Nakamura and Piotr Rudnicki
::
:: Received August 19, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies FINSEQ_1, GRAPH_1, ARYTM_1, FUNCT_1, ORDERS_1, BOOLE, GRAPH_2,
      RELAT_1, FINSET_1, CARD_1, FINSEQ_2, GRAPH_4, FINSEQ_4, ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, PARTFUN1, FINSEQ_1,
      FINSEQ_2, FINSEQ_4, CARD_1, FINSET_1, NUMBERS, XCMPLX_0, NAT_1, GRAPH_1,
      GRAPH_2, BINARITH, XXREAL_0;
 constructors PARTFUN1, XXREAL_0, XREAL_0, NAT_1, RECDEF_1, FINSEQ_4, BINARITH,
      GRAPH_2, VALUED_1, SEQ_1;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, XXREAL_0, XREAL_0, NAT_1, FINSEQ_1,
      GRAPH_2, ORDINAL1, VALUED_1, FUNCT_2;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FINSEQ_2, VALUED_1;
 theorems TARSKI, NAT_1, REAL_1, FINSEQ_1, FINSEQ_2, GRAPH_1, RELAT_1, FUNCT_1,
      GRFUNC_1, FUNCT_2, BINARITH, REAL_2, FINSET_1, FINSEQ_3, FINSEQ_4,
      GRAPH_2, XBOOLE_0, XBOOLE_1, XREAL_1, CARD_1, XXREAL_0, ORDINAL1;
 schemes NAT_1, FINSEQ_1, FUNCT_1;

begin   ::Oriented vertex sequences and V-sets

reserve p, q for FinSequence,
  y1,y2,e,X for set,
  i, j, k, m, n for Element of NAT,
  G for Graph;

reserve x,y,v,v1,v2,v3,v4 for Element of the Vertices of G;

definition
  let G;
  let x, y;
  let e;
  pred e orientedly_joins x, y means
  :Def1:
  (the Source of G).e = x & (the Target of G).e = y;
end;

theorem Th1:
  e orientedly_joins v1, v2 implies e joins v1, v2
proof
  assume e orientedly_joins v1, v2;
  then ((the Source of G).e = v1 & (the Target of G).e = v2) by Def1;
  hence e joins v1, v2 by GRAPH_1:def 9;
end;

definition
  let G;
  let x,y be Element of (the Vertices of G);
  pred x,y are_orientedly_incident means
  ex v being set st v in the Edges of G & v orientedly_joins x, y;
end;

theorem Th2:
  e orientedly_joins v1,v2 & e orientedly_joins v3,v4 implies v1=v3 & v2=v4
proof
  assume e orientedly_joins v1,v2 & e orientedly_joins v3,v4;
  then ((the Source of G).e = v1 & (the Target of G).e = v2) &
  ((the Source of G).e = v3 & (the Target of G).e = v4) by Def1;
  hence v1=v3 & v2=v4;
end;

reserve vs, vs1, vs2 for FinSequence of the Vertices of G,
  c, c1, c2 for oriented Chain of G;

registration
  let G;
  cluster empty oriented Chain of G;
  existence
  proof {} is oriented Chain of G by GRAPH_1:14;
    hence thesis;
  end;
end;

definition
  let G, X;
  func G-SVSet X -> set equals
  { v: ex e being Element of the Edges of G st e in X &
  v = (the Source of G).e };
  correctness;
end;

definition
  let G, X;
  func G-TVSet X -> set equals
  { v: ex e being Element of the Edges of G st e in X &
  v = (the Target of G).e };
  correctness;
end;

canceled;

theorem
  G-SVSet {} = {} & G-TVSet {} = {}
proof
  set X = {};
A1: now
    assume
A2: G-SVSet X<>{};
    consider x being Element of G-SVSet X;
    x in G-SVSet X by A2;
    then consider v such that
A3: v=x & (ex e being Element of the Edges of G st e in X &
    (v = (the Source of G).e));
    consider e being Element of the Edges of G such that
A4: e in X & (v = (the Source of G).e) by A3;
    thus contradiction by A4;
  end;
  now
    assume
A5: G-TVSet X<>{};
    consider x being Element of G-TVSet X;
    x in G-TVSet X by A5;
    then consider v such that
A6: v=x & (ex e being Element of the Edges of G st e in X &
    (v = (the Target of G).e));
    consider e being Element of the Edges of G such that
A7: e in X & (v = (the Target of G).e) by A6;
    thus contradiction by A7;
  end;
  hence thesis by A1;
end;

definition
  let G, vs;
  let c be FinSequence;
  pred vs is_oriented_vertex_seq_of c means
  :Def5:
  len vs = len c + 1 &
  for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1);
end;

theorem Th5:
  vs is_oriented_vertex_seq_of c implies vs is_vertex_seq_of c
proof
  assume
A1: vs is_oriented_vertex_seq_of c;
  then
A2: len vs = len c + 1 &
  for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
  by Def5;
  for n st 1<=n & n<=len c holds c.n joins vs/.n, vs/.(n+1)
  proof
    let n;
    assume 1<=n & n<=len c;
    then c.n orientedly_joins vs/.n, vs/.(n+1) by A1,Def5;
    hence c.n joins vs/.n, vs/.(n+1) by Th1;
  end;
  hence vs is_vertex_seq_of c by A2,GRAPH_2:def 7;
end;

theorem
  vs is_oriented_vertex_seq_of c implies G-SVSet rng c c= rng vs
proof
  assume
A1: vs is_oriented_vertex_seq_of c;
  then
A2: len vs = len c + 1 by Def5;
  G-SVSet rng c c= rng vs
  proof
    let y be set;
    assume y in G-SVSet rng c;
    then consider v being Element of the Vertices of G such that
A3: v=y & ex e being Element of the Edges of G st e in rng c &
    v = (the Source of G).e;
    consider e being Element of the Edges of G such that
A4: e in rng c & (v = (the Source of G).e) by A3;
    consider x being set such that
A5: x in dom c & e = c.x by A4,FUNCT_1:def 5;
    reconsider x as Element of NAT by A5;
A6: 1<=x & x<=len c by A5,FINSEQ_3:27;
    then
A7: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
    set v1 = vs/.x;
    set v2 = vs/.(x+1);
A8: v1 = vs.x & v2 = vs.(x+1) by A6,A7,FINSEQ_4:24;
    c.x orientedly_joins v1, v2 by A1,A6,Def5;
    then
A9: v = v1 by A4,A5,Def1;
    x in dom vs & x+1 in dom vs by A6,A7,FINSEQ_3:27;
    hence y in rng vs by A3,A8,A9,FUNCT_1:def 5;
  end;
  hence thesis;
end;

theorem
  vs is_oriented_vertex_seq_of c implies G-TVSet rng c c= rng vs
proof
  assume
A1: vs is_oriented_vertex_seq_of c;
  then
A2: len vs = len c + 1 by Def5;
  let y be set;
  assume y in G-TVSet rng c;
  then consider v being Element of the Vertices of G such that
A3: v=y & ex e being Element of the Edges of G st e in rng c &
  v = (the Target of G).e;
  consider e being Element of the Edges of G such that
A4: e in rng c & (v = (the Target of G).e) by A3;
  consider x being set such that
A5: x in dom c & e = c.x by A4,FUNCT_1:def 5;
  reconsider x as Element of NAT by A5;
A6: 1<=x & x<=len c by A5,FINSEQ_3:27;
  then
A7: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
  set v1 = vs/.x;
  set v2 = vs/.(x+1);
A8: v1 = vs.x & v2 = vs.(x+1) by A6,A7,FINSEQ_4:24;
  c.x orientedly_joins v1, v2 by A1,A6,Def5;
  then
A9: v = v2 by A4,A5,Def1;
  x in dom vs & x+1 in dom vs by A6,A7,FINSEQ_3:27;
  hence y in rng vs by A3,A8,A9,FUNCT_1:def 5;
end;

theorem
  c <>{} & vs is_oriented_vertex_seq_of c implies
  rng vs c= G-SVSet rng c \/ G-TVSet rng c
proof
  assume
A1: c <>{} & vs is_oriented_vertex_seq_of c;
  then
A2: len vs = len c + 1 by Def5;
  let y be set;
  assume y in rng vs;
  then consider x being set such that
A3: x in dom vs & y = vs.x by FUNCT_1:def 5;
  reconsider x as Element of NAT by A3;
A4: 1<=x & x<=len vs by A3,FINSEQ_3:27;
  per cases by A2,A4,NAT_1:8;
  suppose
A5: x<=len c;
    then
A6: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
    x in dom c by A4,A5,FINSEQ_3:27;
    then c.x in rng c & rng c c= the Edges of G by FINSEQ_1:def 4,FUNCT_1:def
    5;
    then reconsider e = c.x as Element of the Edges of G;
    set v1 = vs/.x;
    set v2 = vs/.(x+1);
A7: v1 = vs.x & v2 = vs.(x+1) by A4,A6,FINSEQ_4:24;
    c.x orientedly_joins v1, v2 by A1,A4,A5,Def5;
    then
A8: v1 = (the Source of G).e & v2 = (the Target of G).e by Def1;
    x in dom c by A4,A5,FINSEQ_3:27;
    then e in rng c by FUNCT_1:def 5;
    then y in G-SVSet rng c by A3,A7,A8;
    hence y in G-SVSet rng c \/ G-TVSet rng c by XBOOLE_0:def 2;
  end;
  suppose
A9: x=len c+1;
    set l = len c;
    l <> 0 by A1,FINSEQ_1:25;
    then 0<l & 0+1=1;
    then
A10: 1<=l & l<=l+1 by NAT_1:13;
    then l in dom c by FINSEQ_3:27;
    then c.l in rng c & rng c c= the Edges of G by FINSEQ_1:def 4,FUNCT_1:def
    5;
    then reconsider e = c.l as Element of the Edges of G;
    set v1 = vs/.l;
    set v2 = vs/.(l+1);
A11: v1 = vs.l & v2 = vs.(l+1) by A2,A4,A9,A10,FINSEQ_4:24;
    c.l orientedly_joins v1, v2 by A1,A10,Def5;
    then
A12: v1 = (the Source of G).e & v2 = (the Target of G).e by Def1;
    l in dom c by A10,FINSEQ_3:27;
    then e in rng c by FUNCT_1:def 5;
    then y in G-TVSet rng c by A3,A9,A11,A12;
    hence y in G-SVSet rng c \/ G-TVSet rng c by XBOOLE_0:def 2;
  end;
end;

begin :: Cutting and glueing of oriented chains

theorem
  <*v*> is_oriented_vertex_seq_of {}
proof
  set c ={};
  len c = 0 by FINSEQ_1:25;
  hence len <*v*> = len c +1 by FINSEQ_1:57;
  let n;
  assume 1<=n & n<=len c;
  hence thesis by FINSEQ_1:25;
end;

theorem Th10:
  ex vs st vs is_oriented_vertex_seq_of c
proof
  now per cases;
    case c <>{};
      then
A1:   len c <>0 by FINSEQ_1:25;
      defpred P[Nat,set] means
      (($1=len c+1 implies $2=(the Target of G).(c.len c)) &
      ($1<>len c+1 implies $2=(the Source of G).(c.$1)));
A2:   for k be Nat,y1,y2 st k in Seg (len c +1) & P[k,y1] & P[k,y2]
      holds y1=y2;
A3:   for k be Nat st k in Seg (len c+1) ex x being set st P[k,x]
      proof
        let k be Nat;
        assume k in Seg (len c+1);
        now per cases;
          case k=len c+1;
            hence ex x being set st
            ((k=len c+1 implies x=(the Target of G).(c.len c)) &
            (k<>len c+1 implies x=(the Source of G).(c.k)));
          end;
          case k<>len c+1;
            hence ex x being set st
            ((k=len c+1 implies x=(the Target of G).(c.len c)) &
            (k<>len c+1 implies x=(the Source of G).(c.k)));
          end;
        end;
        hence ex x being set st
        ((k=len c+1 implies x=(the Target of G).(c.len c)) &
        (k<>len c+1 implies x=(the Source of G).(c.k)));
      end;
      ex p st dom p = Seg (len c+1) & for k be Nat st k in Seg (len c+1) holds
      P[k,p.k] from FINSEQ_1:sch 1(A2,A3);
      then consider p such that
A4:   dom p = Seg (len c+1) & for k be Nat st k in Seg (len c+1) holds
      ((k=len c+1 implies p.k=(the Target of G).(c.len c)) &
      (k<>len c+1 implies p.k=(the Source of G).(c.k)));
A5:   len p=len c+1 by A4,FINSEQ_1:def 3;
      rng p c= (the Vertices of G)
      proof
        let y be set;
        assume y in rng p;
        then consider x being set such that
A6:     x in dom p & y=p.x by FUNCT_1:def 5;
        reconsider k=x as Element of NAT by A6;
A7:     1<=k & k<=len c+1 by A4,A6,FINSEQ_1:3;
        now per cases;
          case k=len c+1;
            then
A8:         y=(the Target of G).(c.len c) by A4,A6;
            len c >0 by A1;
            then len c>=0+1 by NAT_1:13;
            then len c in Seg len c by FINSEQ_1:3;
            then len c in dom c by FINSEQ_1:def 3;
            then
A9:         c.len c in rng c by FUNCT_1:def 5;
            rng c c= the Edges of G by FINSEQ_1:def 4;
            hence y in (the Vertices of G) by A8,A9,FUNCT_2:7;
          end;
          case
A10:        k<>len c+1;
            then
A11:        y=(the Source of G).(c.k) by A4,A6;
            k<len c+1 by A7,A10,REAL_1:def 5;
            then k<=len c by NAT_1:13;
            then k in Seg len c by A7,FINSEQ_1:3;
            then k in dom c by FINSEQ_1:def 3;
            then
A12:        c.k in rng c by FUNCT_1:def 5;
            rng c c= the Edges of G by FINSEQ_1:def 4;
            hence y in (the Vertices of G) by A11,A12,FUNCT_2:7;
          end;
        end;
        hence y in (the Vertices of G);
      end;

then reconsider vs=p as FinSequence of (the Vertices of G) by FINSEQ_1:def 4;
      for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
      proof
        let n;
        assume
A13:    1<=n & n<=len c;
        per cases;
        suppose
A14:      n=len c;
          then
A15:      n<len c+1 by NAT_1:13;
          then n in Seg (len c +1) by A13,FINSEQ_1:3;
          then
A16:      p.n=(the Source of G).(c.n) by A4,A15;
          1<n+1 by A13,NAT_1:13;
          then n+1 in Seg (len c +1) by A14,FINSEQ_1:3;
          then
A17:      p.(n+1)=(the Target of G).(c.len c) by A4,A14;
A18:      (the Source of G).(c.n) = vs/.n by A5,A13,A15,A16,FINSEQ_4:24;
          1<n+1 by A13,NAT_1:13;
          then (the Target of G).(c.n) = vs/.(n+1) by A5,A14,A17,FINSEQ_4:24;
          hence thesis by A18,Def1;
        end;
        suppose n<>len c;
          then
A19:      n<len c by A13,REAL_1:def 5;
          then n+1<=len c by NAT_1:13;
          then
A20:      n+1<len c+1 by NAT_1:13;
A21:      n<len c+1 by A13,NAT_1:13;
          then n in Seg (len c +1) by A13,FINSEQ_1:3;
          then
A22:      p.n=(the Source of G).(c.n) by A4,A21;
          1<n+1 by A13,NAT_1:13;
          then n+1 in Seg (len c +1) by A20,FINSEQ_1:3;
          then
A23:      p.(n+1)=(the Source of G).(c.(n+1)) by A4,A20;
A24:      (the Source of G).(c.n) = vs/.n by A5,A13,A21,A22,FINSEQ_4:24;
          1<n+1 by A13,NAT_1:13;
          then vs/.(n+1)=p.(n+1) by A5,A20,FINSEQ_4:24;

then (the Target of G).(c.n) = vs/.(n+1) by A13,A19,A23,GRAPH_1:def 12;
          hence thesis by A24,Def1;
        end;
      end;
      then vs is_oriented_vertex_seq_of c by A5,Def5;
      hence thesis;
    end;
    case
A25:  c ={};
      consider x being Element of the Vertices of G;
      set vs=<*x*>;
      len c =0 by A25,FINSEQ_1:25;
      then
A26:  len vs = len c + 1 by FINSEQ_1:57;
      for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
      by A25,FINSEQ_1:25;
      then vs is_oriented_vertex_seq_of c by A26,Def5;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th11:
  c <> {} & vs1 is_oriented_vertex_seq_of c & vs2 is_oriented_vertex_seq_of c
  implies vs1 = vs2
proof
  assume
A1: c <>{} & vs1 is_oriented_vertex_seq_of c &
  vs2 is_oriented_vertex_seq_of c;
  then
A2: len vs1 = len c + 1 &
  for n st 1<=n & n<=len c holds c.n orientedly_joins vs1/.n, vs1/.(n+1)
  by Def5;
A3: len vs2 = len c + 1 &
  for n st 1<=n & n<=len c holds c.n orientedly_joins vs2/.n, vs2/.(n+1)
  by A1,Def5;
  for n be Nat st 1<=n & n<=len vs1 holds vs1.n=vs2.n
  proof
    let n be Nat;
    assume
A4: 1<=n & n<=len vs1;
    then
A5: n<=len c +1 by A1,Def5;
A6: n in NAT by ORDINAL1:def 13;
    per cases;
    suppose n<len c +1;
      then
A7:   n<=len c by NAT_1:13;
      then c.n orientedly_joins vs1/.n, vs1/.(n+1) by A1,A4,A6,Def5;
      then
A8:   (the Source of G).(c.n) = vs1/.n
      & (the Target of G).(c.n) = vs1/.(n+1) by Def1;
      c.n orientedly_joins vs2/.n, vs2/.(n+1) by A1,A4,A6,A7,Def5;
      then
A9:   (the Source of G).(c.n) = vs2/.n
      & (the Target of G).(c.n) = vs2/.(n+1) by Def1;
      vs1.n=vs1/.n by A4,FINSEQ_4:24;
      hence thesis by A2,A3,A4,A8,A9,FINSEQ_4:24;
    end;
    suppose n>=len c+1;
      then
A10:  n=len c+1 by A5,XXREAL_0:1;
      then
A11:  n-1=len c;
      0<>len c by A1,FINSEQ_1:25;
      then 0<len c;
      then
A12:  0+1<=len c by NAT_1:13;
      then
A13:  n-1=n-'1 by A10,BINARITH:57;
A14:  1<=n-'1 & n-'1=len c by A11,A12,BINARITH:57;
      then c.(n-'1) orientedly_joins vs1/.(n-'1), vs1/.((n-'1)+1) by A1,Def5;
      then
A15:  (the Source of G).(c.(n-'1)) = vs1/.(n-'1)
      & (the Target of G).(c.(n-'1)) = vs1/.((n-'1)+1) by Def1;
      c.(n-'1) orientedly_joins vs2/.(n-'1), vs2/.((n-'1)+1) by A1,A14,Def5;
      then
A16:  (the Source of G).(c.(n-'1)) = vs2/.(n-'1)
      & (the Target of G).(c.(n-'1)) = vs2/.((n-'1)+1) by Def1;
      vs1.n=vs1/.n by A4,FINSEQ_4:24;
      hence thesis by A2,A3,A4,A13,A15,A16,FINSEQ_4:24;
    end;
  end;
  hence thesis by A2,A3,FINSEQ_1:18;
end;

definition
  let G, c;
  assume
A1: c <>{};
  func oriented-vertex-seq c -> FinSequence of the Vertices of G means
  it is_oriented_vertex_seq_of c;
  existence by Th10;
  uniqueness by A1,Th11;
end;

theorem
  vs is_oriented_vertex_seq_of c & c1 = c|Seg n & vs1= vs|Seg (n+1)
  implies vs1 is_oriented_vertex_seq_of c1
proof
  assume
A1: vs is_oriented_vertex_seq_of c;
  then
A2: len vs = len c + 1 &
  for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n,vs/.(n+1) by Def5;
  assume
A3: c1 = c|Seg n & vs1= vs|Seg (n+1);
  now
    per cases;
    suppose
A4:   n<=len c;
      then n+1<=len vs by A2,XREAL_1:8;
      then
A5:   len c1 = n & len vs1 = n+1 by A3,A4,FINSEQ_1:21;
      hence len vs1 = len c1 + 1;
      let k be Element of NAT;
      assume
A6:   1<=k & k<=len c1;
      then
A7:   k<=len c by A4,A5,XXREAL_0:2;
      then k in dom c & k in Seg n by A5,A6,FINSEQ_1:3,FINSEQ_3:27;
      then
A8:   c1.k = c.k by A3,FUNCT_1:72;
A9:   k<=len vs by A2,A7,NAT_1:12;
A10:  k<=n+1 by A5,A6,NAT_1:12;
      then
A11:  k in dom vs & k in Seg (n+1) by A6,A9,FINSEQ_1:3,FINSEQ_3:27;
A12:  k+1<=len vs by A2,A7,XREAL_1:9;
A13:  1<=k+1 & k+1<=len c1 +1 by A6,NAT_1:12,XREAL_1:9;
      then k+1 in dom vs & k+1 in Seg (n+1) by A5,A12,FINSEQ_1:3,FINSEQ_3:27;
      then
A14:  vs1.k=vs.k & vs1.(k+1)=vs.(k+1) by A3,A11,FUNCT_1:72;
A15:  vs/.k=vs.k & vs/.(k+1)=vs.(k+1) by A6,A9,A12,A13,FINSEQ_4:24;
      vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) by A5,A6,A10,A13,FINSEQ_4:24;
      hence c1.k orientedly_joins vs1/.k,vs1/.(k+1)
      by A1,A6,A7,A8,A14,A15,Def5;
    end;
    suppose
A16:  len c <=n;
      then len vs<=n+1 by A2,XREAL_1:8;
      then c1 = c & vs1 = vs by A3,A16,FINSEQ_2:23;
      hence len vs1 = len c1 + 1 &
      for k being Element of NAT st 1<=k & k<=len c1
      holds c1.k orientedly_joins vs1/.k,vs1/.(k+1) by A1,Def5;
    end;
  end;
  hence thesis by Def5;
end;

theorem Th13:
  1<=m & m<=n & n<=len c & q = (m,n)-cut c implies q is oriented Chain of G
proof
  assume
A1: 1<=m & m<=n & n<=len c & q = (m,n)-cut c;
  consider vs such that
A2: vs is_oriented_vertex_seq_of c by Th10;
A3: len q +m-m=n+1-m by A1,GRAPH_2:def 1;
  reconsider qq=q as Chain of G by A1,GRAPH_2:44;
  for n st 1 <= n & n < len q holds
  (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
  proof
    let k be Element of NAT;
    assume
A4: 1<=k & k<len q;
    1-1<=m-1 by A1,XREAL_1:11;
    then m-1 = m-'1 by BINARITH:def 3;
    then reconsider m1= m-1 as Element of NAT;
    reconsider i = m1+k as Element of NAT;
    set v1 = vs/.i;
    set v2 = vs/.(i+1);
    0+1<=k by A4;
    then consider j such that
A5: 0<=j & j<len q & k=j+1 by A4,GRAPH_2:1;
A6: i= m+j by A5;
    i+1= m+(j+1) by A5;
    then
A7: c.(i+1)=q.(k+1) & c.i=q.k by A1,A4,A5,A6,GRAPH_2:def 1;
    1-1<=m-1 by A1,XREAL_1:11;
    then
A8: 0+1<=m-1+k by A4,XREAL_1:9;
    i<=(m-1)+(n-(m-1)) by A3,A4,XREAL_1:8;
    then 1<=i & i<=len c by A1,A8,XXREAL_0:2;
    then c.i orientedly_joins v1, v2 by A2,Def5;
    then
A9: (the Source of G).(c.i) = v1 & (the Target of G).(c.i) = v2 by Def1;
A10: 1<i+1 by A8,NAT_1:13;
    k+m<n+1 by A3,A4,XREAL_1:8;
    then k+m<=n by NAT_1:13;
    then m+k<=len c by A1,XXREAL_0:2;
    then c.(i+1) orientedly_joins v2, vs/.(i+1+1) by A2,A10,Def5;
    hence (the Source of G).(q.(k+1)) = (the Target of G).(q.k) by A7,A9,Def1;
  end;
  then qq is oriented by GRAPH_1:def 12;
  hence thesis;
end;

theorem Th14:
  1<=m & m<=n & n<=len c & c1 = (m,n)-cut c &
  vs is_oriented_vertex_seq_of c & vs1 = (m,n+1)-cut vs
  implies vs1 is_oriented_vertex_seq_of c1
proof
  assume
A1: 1<=m & m<=n & n<=len c;
  assume
A2: c1 = (m,n)-cut c;
  assume
A3: vs is_oriented_vertex_seq_of c & vs1 = (m,n+1)-cut vs;
  then
A4: len vs = len c + 1 by Def5;
  then
A5: m<=n+1 & n+1<=len vs by A1,NAT_1:12,XREAL_1:8;
A6: len c1 +m= n+1 by A1,A2,GRAPH_2:def 1;
  len vs1 +m = n+1+1 by A1,A3,A5,GRAPH_2:def 1;
  hence
A7: len vs1 = len c1 + 1 by A6;
  let k be Element of NAT;
  assume
A8: 1<=k & k<=len c1;
  then 0+1<=k;
  then consider j such that
A9: 0<=j & j<len c1 & k=j+1 by A8,GRAPH_2:1;
  set i = m+j;
  set v1 = vs/.i;
  set v2 = vs/.(i+1);
  m+k<=len c1 +m by A8,XREAL_1:9;
  then m+j+1-1<=len c1 +m-1 by A9,XREAL_1:11;
  then
A10: 1<=i & i<=len c by A1,A6,NAT_1:12,XXREAL_0:2;
  then
A11: c.i orientedly_joins v1, v2 by A3,Def5;
  j<len vs1 by A7,A9,NAT_1:13;
  then
A12: vs1.k = vs.i by A1,A3,A5,A9,GRAPH_2:def 1;
A13: j+1<len vs1 by A7,A9,XREAL_1:8;
  m+j+1 = m+(j+1);
  then
A14: vs1.(k+1) = vs.(i+1) by A1,A3,A5,A9,A13,GRAPH_2:def 1;
  i<=len vs & 1<=i+1 & i+1<=len vs by A4,A10,NAT_1:12,XREAL_1:9;
  then
A15: vs/.i=vs.i & vs/.(i+1)=vs.(i+1) by A10,FINSEQ_4:24;
  0+1=1;
  then
A16: 1<=k & k<=len c1 by A9,NAT_1:13;
  then k<=len vs1 & 1<=k+1 & k+1<=len vs1 by A7,NAT_1:12,XREAL_1:9;
  then vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) by A16,FINSEQ_4:24;
  hence c1.k orientedly_joins vs1/.k, vs1/.(k+1)
  by A1,A2,A9,A11,A12,A14,A15,GRAPH_2:def 1;
end;

theorem Th15:
  vs1 is_oriented_vertex_seq_of c1
  & vs2 is_oriented_vertex_seq_of c2 & vs1.len vs1 = vs2.1
  implies c1^c2 is oriented Chain of G
proof
  assume
A1: vs1 is_oriented_vertex_seq_of c1
  & vs2 is_oriented_vertex_seq_of c2 & vs1.len vs1 = vs2.1;
  then
A2: vs1 is_vertex_seq_of c1
  & vs2 is_vertex_seq_of c2 & vs1.len vs1 = vs2.1 by Th5;
A3: len vs1 = len c1 + 1 & len vs2 = len c2 + 1 by A1,Def5;
  set q=c1^c2;
  set p=vs1^'vs2;
  thus c1^c2 is oriented Chain of G
  proof
    reconsider cc=c1^c2 as Chain of G by A2,GRAPH_2:46;
    for n st 1 <= n & n < len q holds
    (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
    proof
      let n;
      assume
A4:   1 <= n & n < len q;
      then
A5:   n in dom q by FINSEQ_3:27;
A6:   n+1<=len q by A4,NAT_1:13;
A7:   1<n+1 by A4,NAT_1:13;
      now per cases by A5,FINSEQ_1:38;
        case
A8:       n in dom c1;
          then
A9:       q.n = c1.n by FINSEQ_1:def 7;
          set v1=vs1/.n;
          set v2=vs1/.(n+1);
A10:      1<=n & n<=len c1 by A8,FINSEQ_3:27;
          then
A11:      c1.n orientedly_joins v1, v2 by A1,Def5;
A12:      n<=len vs1 by A3,A10,NAT_1:12;
          1<=n+1 & n+1<=len c1+1 by A10,NAT_1:12,XREAL_1:8;
          then
A13:      1<=n+1 & n+1<=len vs1 by A1,Def5;
          then
A14:      vs1/.n=vs1.n & vs1/.(n+1)=vs1.(n+1) by A10,A12, FINSEQ_4:24;
          p.n = vs1.n & p.(n+1) = vs1.(n+1) by A10,A12,A13,GRAPH_2:14;
          hence ex v1, v2 being Element of the Vertices of G st
          v1 = p.n & v2 = p.(n+1) & q.n orientedly_joins v1, v2 by A9,A11,A14;
        end;
        case ex k being Nat st k in dom c2 & n=len c1 + k;
          then consider k being Nat such that
A15:      k in dom c2 & n = len c1 + k;
          reconsider k as Element of NAT by ORDINAL1:def 13;
A16:      q.n = c2.k by A15,FINSEQ_1:def 7;
A17:      1<=k & k<=len c2 by A15,FINSEQ_3:27;
          then
A18:      1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12, XREAL_1:9;
          set v1 = vs2/.k;
          set v2 = vs2/.(k+1);
A19:      vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A17,A18,FINSEQ_4:24;
A20:      c2.k orientedly_joins v1, v2 by A1,A17,Def5;
A21:      k<=len vs2 by A3,A17,NAT_1:12;
          0+1<=k by A15,FINSEQ_3:27;
          then consider j such that
A22:      0<=j & j<len vs2 & k=j+1 by A21,GRAPH_2:1;
A23:      p.n = vs2.k
          proof per cases by A17,REAL_1:def 5;
            suppose
A24:          1=k;
A25:          0+1<=len vs1 by A3,NAT_1:13;
              thus p.n = p.(len vs1) by A1,A15,A24,Def5
                .= vs2.k by A1,A24,A25,GRAPH_2:14;
            end;
            suppose 1<k;
              then
A26:          1<=j by A22,NAT_1:13;
              thus p.n = p.(len c1 +1+j) by A15,A22
                .= p.(len vs1 +j) by A1,Def5
                .= vs2.k by A22,A26,GRAPH_2:15;
            end;
          end;
A27:      k<len vs2 by A3,A17,NAT_1:13;
          p.(n+1) = p.(len c1 +1+k) by A15
            .= p.(len vs1+k) by A1,Def5
            .= vs2.(k+1) by A17,A27,GRAPH_2:15;
          hence ex v1, v2 being Element of the Vertices of G st
          v1 = p.n & v2 = p.(n+1)
          & q.n orientedly_joins v1, v2 by A16,A19,A20,A23;
        end;
      end;
      then consider v1,v2 being Element of the Vertices of G such that
A28:  v1=p.n & v2=p.(n+1) & q.n orientedly_joins v1,v2;
A29:  n+1 in dom q by A6,A7,FINSEQ_3:27;
      now per cases by A29,FINSEQ_1:38;
        case
A30:      n+1 in dom c1;
          then
A31:      q.(n+1) = c1.(n+1) by FINSEQ_1:def 7;
          set v2'=vs1/.(n+1),v3=vs1/.(n+1+1);
A32:      1<=n+1 & n+1<=len c1 by A30,FINSEQ_3:27;
          then
A33:      c1.(n+1) orientedly_joins v2', v3 by A1,Def5;
A34:      n+1<=len vs1 by A3,A32,NAT_1:12;
          1<=n+1+1 & n+1+1<=len c1+1 by A32,NAT_1:12,XREAL_1:8;
          then
A35:      1<=n+1+1 & n+1+1<=len vs1 by A1,Def5;
          then
A36:      vs1/.(n+1)=vs1.(n+1) &
          vs1/.(n+1+1)=vs1.(n+1+1) by A32,A34,FINSEQ_4:24;
          p.(n+1) = vs1.(n+1) & p.(n+1+1) = vs1.(n+1+1) by A32,A34,A35,
          GRAPH_2:14;
          hence ex v2'',v3' being Element of the Vertices of G st
          v2'' = p.(n+1) & v3' = p.(n+1+1)
          & q.(n+1) orientedly_joins v2'', v3' by A31,A33,A36;
        end;
        case ex k being Nat st k in dom c2 & (n+1)=len c1 + k;
          then consider k being Nat such that
A37:      k in dom c2 & (n+1) = len c1 + k;
          reconsider k as Element of NAT by ORDINAL1:def 13;
A38:      q.(n+1) = c2.k by A37,FINSEQ_1:def 7;
A39:      1<=k & k<=len c2 by A37,FINSEQ_3:27;
          then
A40:      1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12, XREAL_1:9;
          set v2'=vs2/.k, v3 = vs2/.(k+1);
A41:      vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A39,A40,FINSEQ_4:24;
A42:      c2.k orientedly_joins v2', v3 by A1,A39,Def5;
A43:      k<=len vs2 by A3,A39,NAT_1:12;
          0+1<=k by A37,FINSEQ_3:27;
          then consider j such that
A44:      0<=j & j<len vs2 & k=j+1 by A43,GRAPH_2:1;
A45:      p.(n+1) = vs2.k
          proof per cases by A39,REAL_1:def 5;
            suppose
A46:          1=k;
A47:          0+1<=len vs1 by A3,NAT_1:13;
              thus p.(n+1) = p.(len vs1) by A1,A37,A46,Def5
                .= vs2.k by A1,A46,A47,GRAPH_2:14;
            end;
            suppose 1<k;
              then
A48:          1<=j by A44,NAT_1:13;
              thus p.(n+1) = p.(len c1 +1+j) by A37,A44
                .= p.(len vs1 +j) by A1,Def5
                .= vs2.k by A44,A48,GRAPH_2:15;
            end;
          end;
A49:      k<len vs2 by A3,A39,NAT_1:13;
          p.((n+1)+1) = p.(len c1 +1+k) by A37
            .= p.(len vs1+k) by A1,Def5
            .= vs2.(k+1) by A39,A49,GRAPH_2:15;
          hence ex v2'',v3' being Element of the Vertices of G st
          v2'' = p.(n+1) & v3' = p.((n+1)+1)
          & q.(n+1) orientedly_joins v2'', v3' by A38,A41,A42,A45;
        end;
      end;
      then consider v2',v3 being Element of the Vertices of G such that
A50:  v2'=p.(n+1) & v3=p.(n+1+1) & q.(n+1) orientedly_joins v2',v3;
      (the Source of G).(q.n) = v1 & (the Target of G).(q.n) = v2 by A28,Def1;
      hence (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
      by A28,A50,Def1;
    end;
    then cc is oriented by GRAPH_1:def 12;
    hence thesis;
  end;
end;

theorem Th16:
  vs1 is_oriented_vertex_seq_of c1 & vs2 is_oriented_vertex_seq_of c2
  & vs1.len vs1 = vs2.1 & c = c1^c2 & vs = vs1^'vs2
  implies vs is_oriented_vertex_seq_of c
proof
  assume
A1: vs1 is_oriented_vertex_seq_of c1 & vs2 is_oriented_vertex_seq_of c2
  & vs1.len vs1 = vs2.1;
  assume
A2: c = c1^c2 & vs = vs1^'vs2;
A3: len vs1 = len c1 + 1 & len vs2 = len c2 + 1 by A1,Def5;
  then
A4: vs1 <> {} & vs2 <> {} by FINSEQ_1:25;
  set q=c1^c2;
  set p=vs1^'vs2;
  len p +1 = len vs1 +len vs2 by A4,GRAPH_2:13;
  then
A5: len p = len c1 + len c2 + 1 by A3
    .= len q + 1 by FINSEQ_1:35;
  reconsider p as FinSequence of the Vertices of G;
  now
    let n be Element of NAT;
    assume
A6: 1<=n & n<=len q;
    then n<=len p & 1<=n+1 & n+1<=len p by A5,NAT_1:12,XREAL_1:9;
    then
A7: p/.n=p.n & p/.(n+1)=p.(n+1) by A6,FINSEQ_4:24;
A8: n in dom q by A6,FINSEQ_3:27;
    per cases by A8,FINSEQ_1:38;
    suppose
A9:   n in dom c1;
      set v1=vs1/.n;
      set v2=vs1/.(n+1);
A10:  1<=n & n<=len c1 by A9,FINSEQ_3:27;
      then
A11:  c1.n orientedly_joins v1, v2 by A1,Def5;
A12:  n<=len vs1 by A3,A10,NAT_1:12;
      1<=n+1 & n+1<=len c1+1 by A10,NAT_1:12,XREAL_1:8;
      then
A13:  1<=n+1 & n+1<=len vs1 by A1,Def5;
      then
A14:  vs1/.n=vs1.n & vs1/.(n+1)=vs1.(n+1) by A10,A12,FINSEQ_4:24;
      p.n = vs1.n & p.(n+1) = vs1.(n+1) by A10,A12,A13,GRAPH_2:14;
      hence q.n orientedly_joins p/.n, p/.(n+1)
      by A7,A9,A11,A14,FINSEQ_1:def 7;
    end;
    suppose ex k being Nat st k in dom c2 & n=len c1 + k;
      then consider k being Nat such that
A15:  k in dom c2 & n = len c1 + k;
      reconsider k as Element of NAT by ORDINAL1:def 13;
A16:  q.n = c2.k by A15,FINSEQ_1:def 7;
A17:  1<=k & k<=len c2 by A15,FINSEQ_3:27;
      then 1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12,XREAL_1:9;
      then
A18:  vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A17,FINSEQ_4:24;
A19:  k<=len vs2 by A3,A17,NAT_1:12;
      0+1<=k by A15,FINSEQ_3:27;
      then consider j such that
A20:  0<=j & j<len vs2 & k=j+1 by A19,GRAPH_2:1;
A21:  p.n = vs2.k
      proof per cases by A17,REAL_1:def 5;
        suppose
A22:      1=k;
A23:      0+1<=len vs1 by A3,NAT_1:13;
          thus p.n = p.(len vs1) by A1,A15,A22,Def5
            .= vs2.k by A1,A22,A23,GRAPH_2:14;
        end;
        suppose 1<k;
          then
A24:      1<=j by A20,NAT_1:13;
          thus p.n = p.(len c1 +1+j) by A15,A20
            .= p.(len vs1 +j) by A1,Def5
            .= vs2.k by A20,A24,GRAPH_2:15;
        end;
      end;
A25:  k<len vs2 by A3,A17,NAT_1:13;
      p.(n+1) = p.(len c1 +1+k) by A15
        .= p.(len vs1+k) by A1,Def5
        .= vs2.(k+1) by A17,A25,GRAPH_2:15;
      hence q.n orientedly_joins p/.n,p/.(n+1) by A1,A7,A16,A17,A18,A21,Def5;
    end;
  end;
  hence thesis by A2,A5,Def5;
end;

begin :: Oriented simple chains in oriented chains

Lm1: <*v*> is_oriented_vertex_seq_of {}
proof
  set p = <*v*>, ec = {};
A1: len p = 0 + 1 by FINSEQ_1:56
    .= len ec + 1 by FINSEQ_1:25;
  for n st 1<=n & n<=len ec holds ec.n orientedly_joins p/.n,p/.(n+1)
  by FINSEQ_1:25;
  hence thesis by A1,Def5;
end;

definition
  let G;
  let IT be oriented Chain of G;
  attr IT is Simple means
  :Def7:
  ex vs st vs is_oriented_vertex_seq_of IT &
  (for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs);
end;

registration
  let G;
  cluster Simple (oriented Chain of G);
  existence
  proof consider q being empty oriented Chain of G;
    consider x being Element of the Vertices of G;
    reconsider p =<*x*> as FinSequence of the Vertices of G;
A1: p is_oriented_vertex_seq_of q by Lm1;
    for n,m st 1<=n & n<m & m<=len p & p.n=p.m holds n=1 & m=len p
    proof
      let n,m;
      assume 1<=n & n<m & m<=len p & p.n = p.m;
      then 1<m & m<=1 by FINSEQ_1:56,XXREAL_0:2;
      hence n =1 & m = len p;
    end;
    then q is Simple by A1,Def7;
    hence thesis;
  end;
end;

registration
  let G;
  cluster oriented simple Chain of G;
  existence
  proof consider q being empty oriented Chain of G;
    consider x being Element of the Vertices of G;
    reconsider p =<*x*> as FinSequence of the Vertices of G;
A1: p is_vertex_seq_of q by Lm1,Th5;
    reconsider q'=q as Chain of G;
    for n,m st 1<=n & n<m & m<=len p & p.n=p.m holds n=1 & m=len p
    proof
      let n,m;
      assume 1<=n & n<m & m<=len p & p.n = p.m;
      then 1<m & m<=1 by FINSEQ_1:56,XXREAL_0:2;
      hence n =1 & m = len p;
    end;
    then q' is simple by A1,GRAPH_2:def 10;
    hence thesis;
  end;
end;

canceled;

theorem Th18:
  for q being oriented Chain of G holds q|(Seg n) is oriented Chain of G
proof
  let q be oriented Chain of G;
  reconsider q'= q|(Seg n) as FinSequence by FINSEQ_1:19;
  for i st 1 <= i & i < len q' holds (the Source of G).((q|(Seg n)).(i+1))
  = (the Target of G).((q|(Seg n)).i)
  proof
    let i;
    assume
A1: 1 <= i & i < len q';
    per cases;
    suppose n>=len q;
      then q|(Seg n)=q by FINSEQ_3:55;
      hence thesis by A1,GRAPH_1:def 12;
    end;
    suppose
A2:   n<len q;
      then
A3:   len q'=n by FINSEQ_1:21;
      then
A4:   i<len q by A1,A2,XXREAL_0:2;
      i in Seg n by A1,A3,FINSEQ_1:3;
      then
A5:   (q|(Seg n)).i=q.i by FUNCT_1:72;
A6:   i+1<=n by A1,A3,NAT_1:13;
      1<i+1 by A1,NAT_1:13;
      then i+1 in Seg n by A6,FINSEQ_1:3;
      then (q|(Seg n)).(i+1)=q.(i+1) by FUNCT_1:72;
      hence thesis by A1,A4,A5,GRAPH_1:def 12;
    end;
  end;
  hence thesis by GRAPH_1:4,def 12;
end;

reserve sc for oriented simple Chain of G;

theorem
  sc|(Seg n) is oriented simple Chain of G by Th18,GRAPH_2:49;

theorem Th20:
  for sc' being oriented Chain of G st sc'=sc holds sc' is Simple
proof
  let sc' be oriented Chain of G;
  assume
A1: sc'=sc;
  consider vs such that
A2: vs is_oriented_vertex_seq_of sc' by Th10;
  vs is_vertex_seq_of sc by A1,A2,Th5;
  then for n, m st 1<=n & n<m & m<=len vs & vs.n = vs.m holds n=1 & m=len vs
  by GRAPH_2:51;
  hence thesis by A2,Def7;
end;

theorem Th21:
  for sc' being Simple (oriented Chain of G)
  holds sc' is oriented simple Chain of G
proof
  let sc' be Simple (oriented Chain of G);
  consider vs such that
A1: vs is_oriented_vertex_seq_of sc' &
  (for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs)
  by Def7;
  vs is_vertex_seq_of sc' by A1,Th5;
  hence thesis by A1,GRAPH_2:def 10;
end;

reserve x,y for set;

Lm2: for p be FinSequence, m, n be Element of NAT st 1<=m & m<=n+1 & n<=len p
holds len (m, n)-cut p +m = n+1 &
for i being Element of NAT st i<len (m, n)-cut p
holds ((m, n)-cut p).(i+1)=p.(m+i)
proof
  let p be FinSequence, m, n be Element of NAT such that
A1: 1<=m & m<=n+1 & n<=len p;
A2: m = n+1 or m < n+1 by A1,XXREAL_0:1;
  per cases by A2,NAT_1:13;
  suppose
A3: m = n+1;
    then
A4: n < m by XREAL_1:31;
A5: not(1<=m & m<=n & n<=len p) by A3,XREAL_1:31;
    (m, n)-cut p = {} by A4,GRAPH_2:def 1;
    hence len (m, n)-cut p +m = n+1 by A3,CARD_1:47;
    thus thesis by A5,CARD_1:47,GRAPH_2:def 1;
  end;
  suppose m<=n;
    hence thesis by A1,GRAPH_2:def 1;
  end;
end;

theorem Th22:
  not c is Simple & vs is_oriented_vertex_seq_of c implies
  ex fc being Subset of c, fvs being Subset of vs, c1, vs1
  st len c1 < len c & vs1 is_oriented_vertex_seq_of c1 & len vs1 < len vs &
  vs.1 = vs1.1 & vs.len vs = vs1.len vs1 & Seq fc = c1 & Seq fvs = vs1
proof
  assume
A1: not c is Simple & vs is_oriented_vertex_seq_of c;
  then consider n, m being Element of NAT such that
A2: 1<=n & n<m & m<=len vs & vs.n=vs.m & (n<>1 or m<>len vs) by Def7;
A3: len vs = len c + 1 by A1,Def5;
A4: m-n>n-n by A2,XREAL_1:11;
  reconsider n1 = n-'1 as Element of NAT;
A5: 1-1<=n-1 by A2,XREAL_1:11;
  then
A6: n-1 = n-'1 by BINARITH:def 3;
A7: n1+1 = n-1+1 by A5,BINARITH:def 3
    .= n;
  per cases by A2;
  suppose
A8: n<>1 & m <> len vs;
    then 1 < n by A2,REAL_1:def 5;
    then 1 + 1<=n by NAT_1:13;
    then
A9: 1+1-1<=n-1 by XREAL_1:11;
    n < len vs by A2,XXREAL_0:2;
    then
A10: n-1 < len c +1-1 by A3,XREAL_1:11;
A11: 1<=n1+1 by NAT_1:12;
A12: m < len vs by A2,A8,REAL_1:def 5;
    then
A13: m<=len c by A3,NAT_1:13;
A14: 1<=m & m<=len c & len c <=len c by A2,A3,A12,NAT_1:13,XXREAL_0:2;
    reconsider c1 = (1,n1)-cut c as oriented Chain of G by A6,A9,A10,Th13;
    reconsider c2 = (m,len c)-cut c as oriented Chain of G by A14,Th13;
    set pp = (1,n)-cut vs;
    set p2 = (m,len c+1)-cut vs;
    set p2' = (m+1, len c +1)-cut vs;
    reconsider pp as FinSequence of the Vertices of G;
    reconsider p2 as FinSequence of the Vertices of G;
A15: 1<=n & n<=len vs by A2,XXREAL_0:2;
A16: 1<=m & m<=len c + 1 & len c + 1<=len vs by A1,A2,Def5,XXREAL_0:2;
A17: pp is_oriented_vertex_seq_of c1 by A1,A7,A9,A10,Th14;
A18: p2 is_oriented_vertex_seq_of c2 by A1,A14,Th14;
A19: len pp = len c1 + 1 by A17,Def5;
A20: len p2 = len c2 + 1 by A18,Def5;
    1-1<=m-1 by A14,XREAL_1:11;
    then m-'1 = m-1 by BINARITH:def 3;
    then reconsider m1 = m-1 as Element of NAT;
A21: m = m1 +1;
A22: len c2 +m = len c +1 by A14,GRAPH_2:def 1;
A23: m-m<=len c -m by A13,XREAL_1:11;
    then len c2 -'1 = len c2 -1 by A22,BINARITH:def 3;
    then reconsider lc21 = len c2 -1 as Element of NAT;
A24: m+lc21 = m1+len c2;
    0+1=1 & 0<len p2 by A20;
    then
A25: 0+1=1 & 0<=1 & 1<=len p2 by NAT_1:13;
A26: p2 = (0+1, len p2)-cut p2 by GRAPH_2:7
      .= (0+1, 1)-cut p2 ^ (1+1, len p2)-cut p2 by A25,GRAPH_2:8;
    m1<=m by A21,NAT_1:12;
    then
A27: p2 = (m1+1, m)-cut vs ^ (m+1, len c +1)-cut vs by A2,A3,GRAPH_2:8;
    (1,1)-cut p2 = <*p2.(0+1)*> by A25,GRAPH_2:6
      .= <*vs.(m+0)*> by A16,A20,GRAPH_2:def 1
      .= (m,m)-cut vs by A2,A14,GRAPH_2:6;
    then
A28: p2' = (2, len p2)-cut p2 by A26,A27,FINSEQ_1:46;
    set domfc = {k: 1<=k & k<=n1 or m<=k & k<= len c};
    deffunc F(set) = c.$1;
    consider fc being Function such that
A29: dom fc = domfc and
A30: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
    domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider kk being Element of NAT such that
A31:  x = kk & (1<=kk & kk<=n1 or m<=kk & kk<= len c);
      1<=kk & kk<=len c by A6,A10,A14,A31,XXREAL_0:2;
      hence x in Seg len c by A31,FINSEQ_1:3;
    end;
    then reconsider fc as FinSubsequence by A29,FINSEQ_1:def 12;
    fc c= c
    proof
      let p be set;
      assume
A32:  p in fc;
      then consider x, y being set such that
A33:  [x,y] = p by RELAT_1:def 1;
A34:  x in dom fc & y = fc.x by A32,A33,FUNCT_1:8;
      then consider kk being Element of NAT such that
A35:  x = kk & (1<=kk & kk<=n1 or m<=kk & kk<= len c) by A29;
      1<=kk & kk<=len c by A6,A10,A14,A35,XXREAL_0:2;
      then
A36:  x in dom c by A35,FINSEQ_3:27;
      y = c.kk by A29,A30,A34,A35;
      hence p in c by A33,A35,A36,FUNCT_1:8;
    end;
    then reconsider fc as Subset of c;
    take fc;
    set domfvs = {k: 1<=k & k<=n or m+1<=k & k<= len vs};
    deffunc F(set) = vs.$1;
    consider fvs being Function such that
A37: dom fvs = domfvs and
A38: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
    domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider kk being Element of NAT such that
A39:  x = kk & (1<=kk & kk<=n or m+1<=kk & kk<= len vs);
      1<=m+1 by NAT_1:12;
      then 1<=kk & kk<=len vs by A15,A39,XXREAL_0:2;
      hence x in Seg len vs by A39,FINSEQ_1:3;
    end;
    then reconsider fvs as FinSubsequence by A37,FINSEQ_1:def 12;
    fvs c= vs
    proof
      let p be set;
      assume
A40:  p in fvs;
      then consider x, y being set such that
A41:  [x,y] = p by RELAT_1:def 1;
A42:  x in dom fvs & y = fvs.x by A40,A41,FUNCT_1:8;
      then consider kk being Element of NAT such that
A43:  x = kk & (1<=kk & kk<=n or m+1<=kk & kk<= len vs) by A37;
      1<=m+1 by NAT_1:12;
      then 1<=kk & kk<=len vs by A15,A43,XXREAL_0:2;
      then
A44:  x in dom vs by A43,FINSEQ_3:27;
      y = vs.kk by A37,A38,A42,A43;
      hence p in vs by A41,A43,A44,FUNCT_1:8;
    end;
    then reconsider fvs as Subset of vs;
    take fvs;
A45: p2.1 = vs.m & pp.len pp = vs.n by A15,A16,GRAPH_2:12;
    then reconsider c' = c1^c2 as oriented Chain of G by A2,A17,A18,Th15;
    take c';
    take p1=pp^'p2;
A46: p1 = pp^p2' by A28,GRAPH_2:def 2;
A47: len c1 +1 = n1+1 by A6,A10,A11,Lm2;
    -(-(m-n)) = m-n;
    then
A48: -(m-n) < 0 by A4,XREAL_1:60;
    len c' = (n-1)+(len c -m+1) by A6,A22,A47,FINSEQ_1:35
      .= len c + (n+ -m );
    hence
A49: len c' < len c by A48,REAL_2:173;
    thus p1 is_oriented_vertex_seq_of c' by A2,A17,A18,A45,Th16;
    then len p1 = len c' + 1 by Def5;
    hence len p1 < len vs by A3,A49,XREAL_1:8;
    1<=1 + len c1 by NAT_1:12;
    then 1<=len pp by A17,Def5;
    then p1.1 = pp.1 by GRAPH_2:14;
    hence vs.1 = p1.1 by A15,GRAPH_2:12;
A50: p2.len p2 = vs.(len c + 1) by A16,GRAPH_2:12;
    m-m<=len c -m by A14,XREAL_1:11;
    then 0+1<=len c -m+1 by XREAL_1:8;
    then 1<len p2 by A20,A22,NAT_1:13;
    hence vs.len vs = p1.len p1 by A3,A50,GRAPH_2:16;
    set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n1};
    set DR = {kk where kk is Element of NAT: m<=kk & kk<= len c};
    now
      let x be set;
      hereby
        assume x in domfc;
        then consider k being Element of NAT such that
A51:    x=k & (1<=k & k<=n1 or m<=k & k<= len c);
        x in DL or x in DR by A51;
        hence x in DL \/ DR by XBOOLE_0:def 2;
      end;
      assume
A52:  x in DL \/ DR;
      per cases by A52,XBOOLE_0:def 2;
      suppose x in DL;
        then consider k being Element of NAT such that
A53:    x=k & 1<=k & k<=n1;
        thus x in domfc by A53;
      end;
      suppose x in DR;
        then consider k being Element of NAT such that
A54:    x=k & m<=k & k<=len c;
        thus x in domfc by A54;
      end;
    end;
    then
A55: domfc = DL \/ DR by TARSKI:2;
A56: DL c= Seg len c & DR c= Seg len c
    proof
      hereby
        let x be set;
        assume x in DL;
        then consider k being Element of NAT such that
A57:    x=k & 1<=k & k<=n1;
        k<=len c by A6,A10,A57,XXREAL_0:2;
        hence x in Seg len c by A57,FINSEQ_1:3;
      end;
      let x be set;
      assume x in DR;
      then consider k being Element of NAT such that
A58:  x=k & m<=k & k<=len c;
      1<=k by A16,A58,XXREAL_0:2;
      hence x in Seg len c by A58,FINSEQ_1:3;
    end;
    then reconsider DL as finite set by FINSET_1:13;
    reconsider DR as finite set by A56,FINSET_1:13;
    now
      let i,j;
      assume i in DL;
      then consider k being Element of NAT such that
A59:  k=i & 1<=k & k<=n1;
      assume j in DR;
      then consider l being Element of NAT such that
A60:  l=j & m<=l & l<=len c;
      n1<m by A2,A7,NAT_1:13;
      then k<m by A59,XXREAL_0:2;
      hence i < j by A59,A60,XXREAL_0:2;
    end;
    then
A61: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A56,FINSEQ_3:48;
    m+lc21 = len c by A22;
    then
A62: card DR = len c2 + -1 +1 by GRAPH_2:4
      .= len c2;
A63: len Sgm DL = card DL & len Sgm DR = card DR by A56,FINSEQ_3:44;
    DL = Seg n1 by FINSEQ_1:def 1;
    then
A64: Sgm DL = idseq n1 by FINSEQ_3:54;
    then
A65: dom Sgm DL = Seg n1 by RELAT_1:71;
    rng Sgm DL = DL by A56,FINSEQ_1:def 13;
    then
A66: rng Sgm DL c= dom fc by A29,A55,XBOOLE_1:7;
    rng Sgm DR = DR by A56,FINSEQ_1:def 13;
    then
A67: rng Sgm DR c= dom fc by A29,A55,XBOOLE_1:7;
    set SL = Sgm DL;
    set SR = Sgm DR;
    now
      let p be set;
      hereby
        assume
A68:    p in c1;
        then consider x, y being set such that
A69:    p=[x,y] by RELAT_1:def 1;
A70:    x in dom c1 & y=c1.x by A68,A69,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A71:    1<=k & k<=len c1 by A70,FINSEQ_3:27;
        then
A72:    x in dom SL by A47,A65,FINSEQ_1:3;
        then
A73:    k=SL.k by A64,A65,FUNCT_1:35;
A74:    k in domfc by A47,A71;
        then
A75:    x in dom (fc*SL) by A29,A72,A73,FUNCT_1:21;
        0+1<=k by A70,FINSEQ_3:27;
        then consider i being Element of NAT such that
A76:    0<=i & i<n1 & k=i+1 by A47,A71,GRAPH_2:1;
        (fc*SL).x = fc.k by A73,A75,FUNCT_1:22
          .= c.(1+i) by A29,A74,A76,
        GRFUNC_1:8
          .= y by A6,A10,A11,A47,A70,A76,Lm2;
        hence p in fc*(Sgm DL) by A69,A75,FUNCT_1:8;
      end;
      assume
A77:  p in fc*(Sgm DL);
      then consider x,y being set such that
A78:  p=[x,y] by RELAT_1:def 1;
A79:  x in dom (fc*SL) & y = (fc*SL).x by A77,A78,FUNCT_1:8;
      then
A80:  (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A81:  x in dom SL & SL.x in dom fc by A79,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n1}
      by A65,FINSEQ_1:def 1;
      then consider k such that
A82:  k=x & 1<=k & k<=n1;
A83:  k in dom fc by A29,A82;
A84:  k=SL.k by A64,A65,A81,A82,FUNCT_1:35;
A85:  k in dom c1 by A47,A82,FINSEQ_3:27;
      0+1<=k by A82;
      then consider i being Element of NAT such that
A86:  0<=i & i<n1 & k=i+1 by A82,GRAPH_2:1;
      c1.k = c.k by A6,A10,A11,A47,A86,Lm2
        .= y by A79,A80,A82,A83,A84,GRFUNC_1:8;
      hence p in c1 by A78,A82,A85,FUNCT_1:8;
    end;
    then
A87: c1 = fc*(Sgm DL) by TARSKI:2;
    now
      let p be set;
      hereby
        assume
A88:    p in c2;
        then consider x, y being set such that
A89:    p=[x,y] by RELAT_1:def 1;
A90:    x in dom c2 & y=c2.x by A88,A89,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A91:    1<=k & k<=len c2 by A90,FINSEQ_3:27;
A92:    x in dom SR by A62,A63,A90,FINSEQ_3:31;
A93:    m1+k=SR.k by A21,A22,A24,A91,GRAPH_2:5;
A94:    SR.k in rng SR by A92,FUNCT_1:def 5;
        then
A95:    x in dom (fc*SR) by A67,A92,FUNCT_1:21;
        0+1<=k by A90,FINSEQ_3:27;
        then consider i being Element of NAT such that
A96:    0<=i & i<len c2 & k=i+1 by A91,GRAPH_2:1;
        (fc*SR).x = fc.(m1+k) by A93,A95,FUNCT_1:22
          .= c.(m+i) by A67,A93, A94
        ,A96,GRFUNC_1:8
          .= y by A14,A90,A96,GRAPH_2:def 1;
        hence p in fc*(Sgm DR) by A89,A95,FUNCT_1:8;
      end;
      assume
A97:  p in fc*(Sgm DR);
      then consider x,y being set such that
A98:  p=[x,y] by RELAT_1:def 1;
A99:  x in dom (fc*SR) & y = (fc*SR).x by A97,A98,FUNCT_1:8;
      then
A100: x in dom SR & SR.x in dom fc by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A101: k in dom c2 by A62,A63,A100,FINSEQ_3:31;
A102: 1<=k & k<=len c2 by A62,A63,A100,FINSEQ_3:27;
      then
A103: m1+k=SR.k by A21,A22,A24,GRAPH_2:5;
      0+1<=k by A100,FINSEQ_3:27;
      then consider i being Element of NAT such that
A104: 0<=i & i<len c2 & k=i+1 by A102,GRAPH_2:1;
      c2.k = c.(m1+1+i) by A14,A104,GRAPH_2:def 1
        .= fc.(SR.k) by A100,A103,A104,GRFUNC_1:8
        .= y by A99,A100,FUNCT_1:23;
      hence p in c2 by A98,A101,FUNCT_1:8;
    end;
    then
A105: c2 = fc*(Sgm DR) by TARSKI:2;
    Seq fc= fc*((Sgm DL)^(Sgm DR))by A29,A55,A61,FINSEQ_1:def 14;
    hence Seq fc = c' by A66,A67,A87,A105,GRAPH_2:26;
    set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n};
    set DR = {kk where kk is Element of NAT: m+1<=kk & kk<= len vs};
    now
      let x be set;
      hereby
        assume x in domfvs;
        then consider k being Element of NAT such that
A106:   x=k & (1<=k & k<=n or m+1<=k & k<= len vs);
        x in DL or x in DR by A106;
        hence x in DL \/ DR by XBOOLE_0:def 2;
      end;
      assume
A107: x in DL \/ DR;
      per cases by A107,XBOOLE_0:def 2;
      suppose x in DL;
        then consider k being Element of NAT such that
A108:   x=k & 1<=k & k<=n;
        thus x in domfvs by A108;
      end;
      suppose x in DR;
        then consider k being Element of NAT such that
A109:   x=k & m+1<=k & k<=len vs;
        thus x in domfvs by A109;
      end;
    end;
    then
A110: domfvs = DL \/ DR by TARSKI:2;
A111: DL c= Seg len vs & DR c= Seg len vs
    proof
      hereby
        let x be set;
        assume x in DL;
        then consider k being Element of NAT such that
A112:   x=k & 1<=k & k<=n;
        k<=len vs by A15,A112,XXREAL_0:2;
        hence x in Seg len vs by A112,FINSEQ_1:3;
      end;
      let x be set;
      assume x in DR;
      then consider k being Element of NAT such that
A113: x=k & m+1<=k & k<=len vs;
      1<=m+1 by NAT_1:12;
      then 1<=k by A113,XXREAL_0:2;
      hence x in Seg len vs by A113,FINSEQ_1:3;
    end;
    then reconsider DL as finite set by FINSET_1:13;
    reconsider DR as finite set by A111,FINSET_1:13;
    now
      let i,j;
      assume i in DL;
      then consider k being Element of NAT such that
A114: k=i & 1<=k & k<=n;
      assume j in DR;
      then consider l being Element of NAT such that
A115: l=j & m+1<=l & l<=len vs;
      m<=m+1 by NAT_1:12;
      then
A116: m<=l by A115,XXREAL_0:2;
      k<m by A2,A114,XXREAL_0:2;
      hence i < j by A114,A115,A116,XXREAL_0:2;
    end;
    then
A117: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A111,FINSEQ_3:48;
    1<=len p2 by A20,NAT_1:12;
    then 1-1<=len p2 -1 by XREAL_1:11;
    then len p2 -'1 = len p2 -1 by BINARITH:def 3;
    then reconsider lp21 = len p2 -1 as Element of NAT;
    lp21 -'1 = lp21 -1 by A20,A22,A23,BINARITH:def 3;
    then reconsider lp22 = lp21 -1 as Element of NAT;
A118: m+1+lp22 = m+((lp21 -1)+1)
      .= m+(len c -m +1 +1 -1) by A18,A22,Def5
      .= len c +1;
    then
A119: card DR = lp22 + 1 by A3,GRAPH_2:4
      .= lp21;
A120: 1<=m+1 & m+1<=len c +1+1 by A16,NAT_1:12,XREAL_1:8;
A121: len p2 +m = len c +1+1 by A16,GRAPH_2:def 1
      .= len p2' +(m+1) by A16,A120,Lm2
      .= (len p2'+1)+m;
A122: len Sgm DL = card DL & len Sgm DR = card DR by A111,FINSEQ_3:44;
    DL = Seg n by FINSEQ_1:def 1;
    then
A123: Sgm DL = idseq n by FINSEQ_3:54;
    then
A124: dom Sgm DL = Seg n by RELAT_1:71;
    rng Sgm DL = DL by A111,FINSEQ_1:def 13;
    then
A125: rng Sgm DL c= dom fvs by A37,A110,XBOOLE_1:7;
    rng Sgm DR = DR by A111,FINSEQ_1:def 13;
    then
A126: rng Sgm DR c= dom fvs by A37,A110,XBOOLE_1:7;
    set SL = Sgm DL;
    set SR = Sgm DR;
    now
      let p be set;
      hereby
        assume
A127:   p in pp;
        then consider x, y being set such that
A128:   p=[x,y] by RELAT_1:def 1;
A129:   x in dom pp & y=pp.x by A127,A128,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A130:   1<=k & k<=len pp by A129,FINSEQ_3:27;
        then
A131:   x in dom SL by A6,A19,A47,A124,FINSEQ_1:3;
        then
A132:   k=SL.k by A123,A124,FUNCT_1:35;
A133:   k in domfvs by A6,A19,A47,A130;
        then
A134:   x in dom (fvs*SL) by A37,A131,A132,FUNCT_1:21;
        0+1<=k by A129,FINSEQ_3:27;
        then consider i being Element of NAT such that
A135:   0<=i & i<n & k=i+1 by A6,A19,A47,A130,GRAPH_2:1;
        (fvs*SL).x = fvs.k by A132,A134,FUNCT_1:22
          .= vs.(1+i) by A37,A133
        ,A135,GRFUNC_1:8
          .= y by A6,A15,A19,A47,A129,A135,GRAPH_2:def 1;
        hence p in fvs*(Sgm DL) by A128,A134,FUNCT_1:8;
      end;
      assume
A136: p in fvs*(Sgm DL);
      then consider x,y being set such that
A137: p=[x,y] by RELAT_1:def 1;
A138: x in dom (fvs*SL) & y = (fvs*SL).x by A136,A137,FUNCT_1:8;
      then
A139: (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A140: x in dom SL & SL.x in dom fvs by A138,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n}
      by A124,FINSEQ_1:def 1;
      then consider k such that
A141: k=x & 1<=k & k<=n;
A142: k in dom fvs by A37,A141;
A143: k=SL.k by A123,A124,A140,A141,FUNCT_1:35;
A144: k in dom pp by A6,A19,A47,A141,FINSEQ_3:27;
      0+1<=k by A141;
      then consider i being Element of NAT such that
A145: 0<=i & i<n & k=i+1 by A141,GRAPH_2:1;
      pp.k = vs.k by A6,A15,A19,A47,A145,GRAPH_2:def 1
        .= y by A138,A139,A141,A142,A143,GRFUNC_1:8;
      hence p in pp by A137,A141,A144,FUNCT_1:8;
    end;
    then
A146: pp = fvs*(Sgm DL) by TARSKI:2;
A147: m+1+lp22 = m+lp21;
A148: 1<=m+1 & m+1<=len c +1+1 by A2,A3,NAT_1:12,XREAL_1:9;
    now
      let p be set;
      hereby
        assume
A149:   p in p2';
        then consider x, y being set such that
A150:   p=[x,y] by RELAT_1:def 1;
A151:   x in dom p2' & y=p2'.x by A149,A150,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A152:   1<=k & k<=len p2' by A151,FINSEQ_3:27;
A153:   x in dom SR by A119,A121,A122,A151,FINSEQ_3:31;
A154:   m+k=SR.k by A3,A118,A121,A147,A152,GRAPH_2:5;
A155:   SR.k in rng SR by A153,FUNCT_1:def 5;
        then
A156:   x in dom (fvs*SR) by A126,A153,FUNCT_1:21;
        0+1<=k by A151, FINSEQ_3:27;
        then consider i being Element of NAT such that
A157:   0<=i & i<len p2' & k=i+1 by A152,GRAPH_2:1;
        (fvs*SR).x = fvs.(m+k) by A154,A156,FUNCT_1:22
          .= vs.(m+1+i) by A126
        ,A154,A155,A157,GRFUNC_1:8
          .= y by A3,A148,A151,A157,Lm2;
        hence p in fvs*(Sgm DR) by A150,A156,FUNCT_1:8;
      end;
      assume
A158: p in fvs*(Sgm DR);
      then consider x,y being set such that
A159: p=[x,y] by RELAT_1:def 1;
A160: x in dom (fvs*SR) & y = (fvs*SR).x by A158,A159,FUNCT_1:8;
      then
A161: x in dom SR & SR.x in dom fvs by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A162: k in dom p2' by A119,A121,A122,A161,FINSEQ_3:31;
A163: 1<=k & k<=len p2' by A119,A121,A122,A161,FINSEQ_3:27;
      then
A164: m+k=SR.k by A3,A118,A121,A147,GRAPH_2:5;
      0+1<=k by A161, FINSEQ_3:27;
      then consider i being Element of NAT such that
A165: 0<=i & i<len p2' & k=i+1 by A163,GRAPH_2:1;
      p2'.k = vs.(m+1+i) by A3,A148,A165,Lm2
        .= fvs.(SR.k) by A161,A164,A165,GRFUNC_1:8
        .= y by A160,A161,
      FUNCT_1:23;
      hence p in p2' by A159,A162,FUNCT_1:8;
    end;
    then
A166: p2' = fvs*(Sgm DR) by TARSKI:2;
    Seq fvs = fvs*((Sgm DL)^(Sgm DR)) by A37,A110,A117,FINSEQ_1:def 14;
    hence Seq fvs = p1 by A46,A125,A126,A146,A166,GRAPH_2:26;
  end;
  suppose
A167: n=1 & m <> len vs;
    then
A168: m < len vs by A2,REAL_1:def 5;
    then
A169: m<=len c by A3,NAT_1:13;
A170: 1 < m by A2,XXREAL_0:2;
A171: 1<=m & m<=len c & len c <=len c by A2,A3,A168,NAT_1:13,XXREAL_0:2;
    reconsider c2 = (m,len c)-cut c as oriented Chain of G by A169,A170,Th13;
    set p2 = (m,len c+1)-cut vs;
A172: p2 is_oriented_vertex_seq_of c2 by A1,A169,A170,Th14;
    set domfc = {k: m<=k & k<= len c};
    deffunc F(set) = c.$1;
    consider fc being Function such that
A173: dom fc = domfc and
A174: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
    domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider kk being Element of NAT such that
A175: x = kk & m<=kk & kk<= len c;
      1<=kk & kk<=len c by A170,A175,XXREAL_0:2;
      hence x in Seg len c by A175,FINSEQ_1:3;
    end;
    then reconsider fc as FinSubsequence by A173,FINSEQ_1:def 12;
    fc c= c
    proof
      let p be set;
      assume
A176: p in fc;
      then consider x, y being set such that
A177: [x,y] = p by RELAT_1:def 1;
A178: x in dom fc & y = fc.x by A176,A177,FUNCT_1:8;
      then consider kk being Element of NAT such that
A179: x = kk & m<=kk & kk<= len c by A173;
      1<=kk & kk<=len c by A170,A179,XXREAL_0:2;
      then
A180: x in dom c by A179,FINSEQ_3:27;
      y = c.kk by A173,A174,A178,A179;
      hence p in c by A177,A179,A180,FUNCT_1:8;
    end;
    then reconsider fc as Subset of c;
    take fc;
    set domfvs = {k: m<=k & k<= len vs};
    deffunc F(set) = vs.$1;
    consider fvs being Function such that
A181: dom fvs = domfvs and
A182: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
    domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider kk being Element of NAT such that
A183: x = kk & m<=kk & kk<= len vs;
      1<=kk & kk<=len vs by A170,A183,XXREAL_0:2;
      hence x in Seg len vs by A183,FINSEQ_1:3;
    end;
    then reconsider fvs as FinSubsequence by A181,FINSEQ_1:def 12;
    fvs c= vs
    proof
      let p be set;
      assume
A184: p in fvs;
      then consider x, y such that
A185: [x,y] = p by RELAT_1:def 1;
A186: x in dom fvs & y = fvs.x by A184,A185,FUNCT_1:8;
      then consider kk being Element of NAT such that
A187: x = kk & m<=kk & kk<= len vs by A181;
      1<=kk & kk<=len vs by A170,A187,XXREAL_0:2;
      then
A188: x in dom vs by A187,FINSEQ_3:27;
      y = vs.kk by A181,A182,A186,A187;
      hence p in vs by A185,A187,A188,FUNCT_1:8;
    end;
    then reconsider fvs as Subset of vs;
    take fvs;
    take c1 = c2;
    take p1 = p2;
A189: len c2 +m = len c +1 by A2,A3,A167,Lm2;
A190: m-m<=len c -m by A169,XREAL_1:11;
    1-1<=m-1 by A170,XREAL_1:11;
    then m-'1 = m-1 by BINARITH:def 3;
    then reconsider m1 = m-1 as Element of NAT;
A191: m = m1 +1;
    len c2 -'1 = len c2 -1 by A189,A190,BINARITH:def 3;
    then reconsider lc21 = len c2 -1 as Element of NAT;
A192: m+lc21 = m1+len c2;
A193: 1<=m & m<=len c +1+1 & len c +1<=len vs by A2,A3,A167,NAT_1:12;
    then
A194: len p2 +m = len c +1 +1 by Lm2;
    then len p2 = len c -m +1+1;
    then
A195: 1<=len p2 by A189,NAT_1:12;
A196: len c2 = len c +(-m+1) by A189;
    1-1 < m-1 by A170,XREAL_1:11;
    then 0 < -(-(m-1));
    then -(m-1) < 0 by XREAL_1:60;
    hence
A197: len c1 < len c by A196,REAL_2:173;
    thus p1 is_oriented_vertex_seq_of c1 by A1,A171,Th14;
    len p1 = len c1 + 1 by A172,Def5;
    hence len p1 < len vs by A3,A197,XREAL_1:8;
    thus vs.1 = p1.1 by A2,A3,A167,GRAPH_2:12;
    thus vs.len vs = p1.len p1 by A2,A3,A167,GRAPH_2:12;
A198: domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider k being Element of NAT such that
A199: x=k & m<=k & k<=len c;
      1<=k by A170,A199,XXREAL_0:2;
      hence x in Seg len c by A199,FINSEQ_1:3;
    end;
    then reconsider domfc as finite set by FINSET_1:13;
    len c2 -'1 = len c2 -1 by A189,A190,BINARITH:def 3;
    then reconsider lc21 = len c2 -1 as Element of NAT;
    m+lc21 = len c by A189;
    then
A200: card domfc = len c2 + -1 +1 by GRAPH_2:4
      .= len c2;
A201: len Sgm domfc = card domfc by A198,FINSEQ_3:44;
A202: rng Sgm domfc c= dom fc by A173,A198,FINSEQ_1:def 13;
    set SR = Sgm domfc;
    now
      let p be set;
      hereby
        assume
A203:   p in c2;
        then consider x, y being set such that
A204:   p=[x,y] by RELAT_1:def 1;
A205:   x in dom c2 & y=c2.x by A203,A204,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A206:   1<=k & k<=len c2 by A205,FINSEQ_3:27;
A207:   x in dom SR by A200,A201,A205,FINSEQ_3:31;
A208:   m1+k=SR.k by A189,A191,A192,A206,GRAPH_2:5;
A209:   SR.k in rng SR by A207,FUNCT_1:def 5;
        then
A210:   x in dom (fc*SR) by A202,A207,FUNCT_1:21;
        0+1<=k by A205, FINSEQ_3:27;
        then consider i being Element of NAT such that
A211:   0<=i & i<len c2 & k=i+1 by A206,GRAPH_2:1;
        (fc*SR).x = fc.(m1+k) by A208,A210,FUNCT_1:22
          .= c.(m+i) by A202, A208
        ,A209,A211,GRFUNC_1:8
          .= y by A2,A3,A167,A205,A211,Lm2;
        hence p in fc*(Sgm domfc) by A204,A210,FUNCT_1:8;
      end;
      assume
A212: p in fc*(Sgm domfc);
      then consider x,y being set such that
A213: p=[x,y] by RELAT_1:def 1;
A214: x in dom (fc*SR) & y = (fc*SR).x by A212,A213,FUNCT_1:8;
      then
A215: x in dom SR & SR.x in dom fc by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A216: k in dom c2 by A200,A201,A215,FINSEQ_3:31;
A217: 1<=k & k<=len c2 by A200,A201,A215,FINSEQ_3:27;
      then
A218: m1+k=SR.k by A189,A191,A192,GRAPH_2:5;
      0+1<=k by A215, FINSEQ_3:27;
      then consider i being Element of NAT such that
A219: 0<=i & i<len c2 & k=i+1 by A217,GRAPH_2:1;
      c2.k = c.(m1+1+i) by A2,A3,A167,A219,Lm2
        .= fc.(SR.k) by A215,A218,A219,GRFUNC_1:8
        .= y by A214,A215,FUNCT_1:23;
      hence p in c2 by A213,A216,FUNCT_1:8;
    end;
    then c2 = fc*(Sgm domfc) by TARSKI:2;
    hence Seq fc = c1 by A173,FINSEQ_1:def 14;
A220: domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider k being Element of NAT such that
A221: x=k & m<=k & k<=len vs;
      1<=k by A170,A221,XXREAL_0:2;
      hence x in Seg len vs by A221,FINSEQ_1:3;
    end;
    then reconsider domfvs as finite set by FINSET_1:13;
    1-1<=len p2 -1 by A195,XREAL_1:11;
    then len p2 -'1 = len p2 -1 by BINARITH:def 3;
    then reconsider lp21 = len p2 -1 as Element of NAT;
A222: m+lp21 = len c+1 by A194;
A223: m+lp21 = m1+len p2;
A224: card domfvs = len p2 + -1 +1 by A3,A222,GRAPH_2:4
      .= len p2;
A225: len Sgm domfvs = card domfvs by A220,FINSEQ_3:44;
A226: rng Sgm domfvs c= dom fvs by A181,A220,FINSEQ_1:def 13;
    set SR = Sgm domfvs;
    now
      let p be set;
      hereby
        assume
A227:   p in p2;
        then consider x, y being set such that
A228:   p=[x,y] by RELAT_1:def 1;
A229:   x in dom p2 & y=p2.x by A227,A228,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A230:   1<=k & k<=len p2 by A229,FINSEQ_3:27;
A231:   x in dom SR by A224,A225,A229,FINSEQ_3:31;
A232:   m1+k=SR.k by A3,A191,A194,A223,A230,GRAPH_2:5;
A233:   SR.k in rng SR by A231,FUNCT_1:def 5;
        then
A234:   x in dom (fvs*SR) by A226,A231,FUNCT_1:21;
        0+1<=k by A229, FINSEQ_3:27;
        then consider i being Element of NAT such that
A235:   0<=i & i<len p2 & k=i+1 by A230,GRAPH_2:1;
        (fvs*SR).x = fvs.(m1+k) by A232,A234,FUNCT_1:22
          .= vs.(m+i) by A226
        ,A232,A233,A235,GRFUNC_1:8
          .= y by A193,A229,A235,Lm2;
        hence p in fvs*(Sgm domfvs) by A228,A234,FUNCT_1:8;
      end;
      assume
A236: p in fvs*(Sgm domfvs);
      then consider x,y being set such that
A237: p=[x,y] by RELAT_1:def 1;
A238: x in dom (fvs*SR) & y = (fvs*SR).x by A236,A237,FUNCT_1:8;
      then
A239: x in dom SR & SR.x in dom fvs by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A240: k in dom p2 by A224,A225,A239,FINSEQ_3:31;
A241: 1<=k & k<=len p2 by A224,A225,A239,FINSEQ_3:27;
      then
A242: m1+k=SR.k by A3,A191,A194,A223,GRAPH_2:5;
      0+1<=k by A239, FINSEQ_3:27;
      then consider i being Element of NAT such that
A243: 0<=i & i<len p2 & k=i+1 by A241,GRAPH_2:1;
      p2.k = vs.(m1+1+i) by A193,A243,Lm2
        .= fvs.(SR.k) by A239,A242,A243,GRFUNC_1:8
        .= y
      by A238,A239,FUNCT_1:23;
      hence p in p2 by A237,A240,FUNCT_1:8;
    end;
    then p2 = fvs*(Sgm domfvs) by TARSKI:2;
    hence Seq fvs = p1 by A181,FINSEQ_1:def 14;
  end;
  suppose
A244: n<>1 & m = len vs;
    then 1 < n by A2,REAL_1:def 5;
    then 1 + 1<=n by NAT_1:13;
    then
A245: 1+1-1<=n-1 by XREAL_1:11;
    n < len vs by A2,XXREAL_0:2;
    then
A246: n-1 < len c +1-1 by A3, XREAL_1:11;
A247: 1<=n1+1 by NAT_1:12;
    reconsider c1 = (1,n1)-cut c as oriented Chain of G by A6,A245,A246,Th13;
    set pp = (1,n)-cut vs;
A248: 1<=n & n<=len vs by A2,XXREAL_0:2;
A249: pp is_oriented_vertex_seq_of c1 by A1,A7,A245,A246,Th14;
    then
A250: len pp = len c1 + 1 by Def5;
    set domfc = {k: 1<=k & k<=n1};
    deffunc F(set) = c.$1;
    consider fc being Function such that
A251: dom fc = domfc and
A252: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
    domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider kk being Element of NAT such that
A253: x = kk & 1<=kk & kk<=n1;
      1<=kk & kk<=len c by A6,A246,A253, XXREAL_0:2;
      hence x in Seg len c by A253,FINSEQ_1:3;
    end;
    then reconsider fc as FinSubsequence by A251,FINSEQ_1:def 12;
    fc c= c
    proof
      let p be set;
      assume
A254: p in fc;
      then consider x, y being set such that
A255: [x,y] = p by RELAT_1:def 1;
A256: x in dom fc & y = fc.x by A254,A255,FUNCT_1:8;
      then consider kk being Element of NAT such that
A257: x = kk & 1<=kk & kk<=n1 by A251;
      1<=kk & kk<=len c by A6,A246,A257,XXREAL_0:2;
      then
A258: x in dom c by A257,FINSEQ_3:27;
      y = c.kk by A251,A252,A256,A257;
      hence p in c by A255,A257,A258,FUNCT_1:8;
    end;
    then reconsider fc as Subset of c;
    take fc;
    set domfvs = { k : 1 <= k & k <= n};
    deffunc F(set) = vs.$1;
    consider fvs being Function such that
A259: dom fvs = domfvs and
A260: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
    domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider kk being Element of NAT such that
A261: x = kk & 1<=kk & kk<=n;
      1<=kk & kk<=len vs by A248,A261,XXREAL_0:2;
      hence x in Seg len vs by A261,FINSEQ_1:3;
    end;
    then reconsider fvs as FinSubsequence by A259,FINSEQ_1:def 12;
    fvs c= vs
    proof
      let p be set;
      assume
A262: p in fvs;
      then consider x, y being set such that
A263: [x,y] = p by RELAT_1:def 1;
A264: x in dom fvs & y = fvs.x by A262,A263,FUNCT_1:8;
      then consider kk being Element of NAT such that
A265: x = kk & 1<=kk & kk<=n by A259;
      1<=kk & kk<=len vs by A248,A265,XXREAL_0:2;
      then
A266: x in dom vs by A265,FINSEQ_3:27;
      y = vs.kk by A259,A260,A264, A265;
      hence p in vs by A263,A265,A266,FUNCT_1:8;
    end;
    then reconsider fvs as Subset of vs;
    take fvs;
    take c' = c1;
    take p1 = pp;
A267: len c1+1=n1+1 by A6,A246,A247,Lm2;
    then
A268: len c1 = n - 1 by A5,BINARITH:def 3; thus
    len c' < len c by A5,A246,A267,BINARITH:def 3;
    thus p1 is_oriented_vertex_seq_of c' by A1,A7,A245,A246,Th14;
    len p1 = len c1 + 1 by A249,Def5;
    hence len p1 < len vs by A2,A268,XXREAL_0:2;
    thus vs.1 = p1.1 by A248,GRAPH_2:12;
    thus vs.len vs = p1.len p1 by A2,A244,GRAPH_2:12;
    domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider k being Element of NAT such that
A269: x=k & 1<=k & k<=n1;
      k<=len c by A6,A246,A269,XXREAL_0:2;
      hence x in Seg len c by A269,FINSEQ_1:3;
    end;
    then reconsider domfc as finite set by FINSET_1:13;
    domfc = Seg n1 by FINSEQ_1:def 1;
    then
A270: Sgm domfc = idseq n1 by FINSEQ_3:54;
    then
A271: dom Sgm domfc = Seg n1 by RELAT_1:71;
    set SL = Sgm domfc;
    now
      let p be set;
      hereby
        assume
A272:   p in c1;
        then consider x, y being set such that
A273:   p=[x,y] by RELAT_1:def 1;
A274:   x in dom c1 & y=c1.x by A272,A273,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A275:   1<=k & k<=len c1 by A274,FINSEQ_3:27;
        then
A276:   x in dom SL by A267,A271,FINSEQ_1:3;
        then
A277:   k=SL.k by A270,A271,FUNCT_1:35;
A278:   k in domfc by A267,A275;
        then
A279:   x in dom (fc*SL) by A251,A276,A277,FUNCT_1:21;
        0+1<=k by A274,FINSEQ_3:27;
        then consider i being Element of NAT such that
A280:   0<=i & i<n1 & k=i+1 by A267,A275,GRAPH_2:1;
        (fc*SL).x = fc.k by A277,A279,FUNCT_1:22
          .= c.(1+i) by A251,A278, A280
        ,GRFUNC_1:8
          .= y by A6,A246,A247,A267,A274,A280,Lm2;
        hence p in fc*(Sgm domfc) by A273,A279,FUNCT_1:8;
      end;
      assume
A281: p in fc*(Sgm domfc);
      then consider x,y being set such that
A282: p=[x,y] by RELAT_1:def 1;
A283: x in dom (fc*SL) & y = (fc*SL).x by A281,A282,FUNCT_1:8;
      then
A284: (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A285: x in dom SL & SL.x in dom fc by A283,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n1}
      by A271,FINSEQ_1:def 1;
      then consider k such that
A286: k=x & 1<=k & k<=n1;
A287: k in dom fc by A251,A286;
A288: k=SL.k by A270,A271,A285,A286,FUNCT_1:35;
A289: k in dom c1 by A267,A286,FINSEQ_3:27;
      0+1<=k by A286;
      then consider i being Element of NAT such that
A290: 0<=i & i<n1 & k=i+1 by A286,GRAPH_2:1;
      c1.k = c.k by A6,A246,A247,A267,A290,Lm2
        .= y by A283,A284,A286,A287,A288,GRFUNC_1:8;
      hence p in c1 by A282,A286,A289,FUNCT_1:8;
    end;
    then c1 = fc*(Sgm domfc) by TARSKI:2;
    hence Seq fc = c' by A251,FINSEQ_1:def 14;
    domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider k being Element of NAT such that
A291: x=k & 1<=k & k<=n;
      k<=len vs by A248,A291,XXREAL_0:2;
      hence x in Seg len vs by A291,FINSEQ_1:3;
    end;
    then reconsider domfvs as finite set by FINSET_1:13;
    domfvs = Seg n by FINSEQ_1:def 1;
    then
A292: Sgm domfvs = idseq n by FINSEQ_3:54;
    then
A293: dom Sgm domfvs = Seg n by RELAT_1:71;
    set SL = Sgm domfvs;
    now
      let p be set;
      hereby
        assume
A294:   p in pp;
        then consider x, y being set such that
A295:   p=[x,y] by RELAT_1:def 1;
A296:   x in dom pp & y=pp.x by A294,A295,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A297:   1<=k & k<=len pp by A296,FINSEQ_3:27;
        then
A298:   x in dom SL by A250,A268,A293,FINSEQ_1:3;
        then
A299:   k=SL.k by A292,A293,FUNCT_1:35;
A300:   k in domfvs by A250,A268,A297;
        then
A301:   x in dom (fvs*SL) by A259,A298,A299,FUNCT_1:21;
        0+1<= k by A296,FINSEQ_3:27;
        then consider i being Element of NAT such that
A302:   0<=i & i<n & k=i+1 by A250,A268,A297,GRAPH_2:1;
        (fvs*SL).x = fvs.k by A299,A301,FUNCT_1:22
          .= vs.(1+i) by A259,A300
        ,A302,GRFUNC_1:8
          .= y by A248,A250,A268,A296,A302,GRAPH_2:def 1;
        hence p in fvs*(Sgm domfvs) by A295,A301,FUNCT_1:8;
      end;
      assume
A303: p in fvs*(Sgm domfvs);
      then consider x,y being set such that
A304: p=[x,y] by RELAT_1:def 1;
A305: x in dom (fvs*SL) & y = (fvs*SL).x by A303,A304,FUNCT_1:8;
      then
A306: (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A307: x in dom SL & SL.x in dom fvs by A305,FUNCT_1:21;
      then x in { kk where kk is Element of NAT: 1<=kk & kk<=n}
      by A293,FINSEQ_1:def 1;
      then consider k such that
A308: k=x & 1<=k & k<=n;
A309: k in dom fvs by A259,A308;
A310: k=SL.k by A292,A293,A307,A308,FUNCT_1:35;
A311: k in dom pp by A250,A268,A308,FINSEQ_3:27;
      0+1<=k by A308;
      then consider i being Element of NAT such that
A312: 0<=i & i<n & k=i+1 by A308,GRAPH_2:1;
      pp.k = vs.k by A248,A250,A268,A312,GRAPH_2:def 1
        .= y by A305,A306,A308,A309,A310,GRFUNC_1:8;
      hence p in pp by A304,A308,A311,FUNCT_1:8;
    end;
    then pp = fvs*(Sgm domfvs) by TARSKI:2;
    hence Seq fvs = p1 by A259,FINSEQ_1:def 14;
  end;
end;

theorem
  vs is_oriented_vertex_seq_of c implies
  ex fc being Subset of c, fvs being Subset of vs, sc, vs1
  st Seq fc = sc & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of sc &
  vs.1 = vs1.1 & vs.len vs = vs1.len vs1
proof
  assume
A1: vs is_oriented_vertex_seq_of c;
  per cases;
  suppose
A2: c is Simple;
    reconsider fc = c as Subset of c by GRAPH_2:28;
    reconsider fvs = vs as Subset of vs by GRAPH_2:28;
    reconsider sc = c as oriented simple Chain of G by A2,Th21;
    take fc, fvs, sc, vs;
    thus Seq fc = sc & Seq fvs = vs by FINSEQ_3:125;
    thus vs is_oriented_vertex_seq_of sc by A1;
    thus vs.1 = vs.1 & vs.len vs = vs.len vs;
  end;
  suppose
A3: not c is Simple;
    defpred P[Nat] means
    ex fc being Subset of c, fvs being Subset of vs, c1, vs1
    st Seq fc = c1 & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of c1 &
    vs.1 = vs1.1 & vs.len vs = vs1.len vs1 & len c1 = $1 & not c1 is Simple;
    P[len c]
    proof
      reconsider fc = c as Subset of c by GRAPH_2:28;
      reconsider fvs = vs as Subset of vs by GRAPH_2:28;
      take fc, fvs, c,vs;
      thus Seq fc = c & Seq fvs = vs by FINSEQ_3:125;
      thus vs is_oriented_vertex_seq_of c by A1;
      thus vs.1 = vs.1 & vs.len vs = vs.len vs;
      thus len c = len c & not c is Simple by A3;
    end;
    then
A4: ex k be Nat st P[k];
    consider k be Nat such that
A5: P[k] & for n being Nat st P[n] holds k<=n from NAT_1:sch 5(A4);
    consider fc being Subset of c, fvs being Subset of vs, c1, vs1 such that
A6: Seq fc = c1 & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of c1 &
    vs.1 = vs1.1 & vs.len vs = vs1.len vs1 & len c1 = k & not c1 is Simple
    by A5;
    consider fc1 being Subset of c1, fvs1 being Subset of vs1,
    c2, vs2 such that
A7: len c2 < len c1 & vs2 is_oriented_vertex_seq_of c2 & len vs2 < len vs1 &
    vs1.1 = vs2.1 & vs1.len vs1 = vs2.len vs2 &
    Seq fc1 = c2 & Seq fvs1 = vs2 by A6,Th22;
    reconsider fc'=fc|rng((Sgm dom fc)|dom fc1) as Subset of c by GRAPH_2:29;
A8: Seq fc' = c2 by A6,A7,GRAPH_2:30;
    reconsider fvs'=fvs|rng((Sgm dom fvs)|dom fvs1) as Subset of vs
    by GRAPH_2:29;
A9: Seq fvs' = vs2 by A6,A7,GRAPH_2:30;
    now
      assume c2 is Simple (oriented Chain of G);
      then reconsider sc = c2 as oriented simple Chain of G by Th21;
      take fc',sc;
      Seq fc' = sc by A6,A7,GRAPH_2:30;
      hence thesis by A6,A7,A9;
    end;
    hence thesis by A5,A6,A7,A8,A9;
  end;
end;

registration
  let G;
  cluster empty -> one-to-one (oriented Chain of G);
  correctness;
end;

theorem
  p is OrientedPath of G implies p|Seg(n) is OrientedPath of G
proof
  assume
A1: p is OrientedPath of G;
  then reconsider p'=p|(Seg n) as oriented Chain of G by Th18;
  reconsider q=p|(Seg n) as FinSequence by FINSEQ_1:19;
  now
    let n1,m1 be Element of NAT;
    assume
A2: 1<=n1 & n1 < m1 & m1<=len q;
    then 1<m1 by XXREAL_0:2;
    then m1 in dom q by A2,FINSEQ_3:27;
    then
A3: q.m1=p.m1 by FUNCT_1:68;
    n1 < len q by A2,XXREAL_0:2;
    then n1 in dom q by A2,FINSEQ_3:27;
    then
A4: q.n1=p.n1 by FUNCT_1:68;
    dom q c= dom p by RELAT_1:89;
    then dom q c= Seg len p by FINSEQ_1:def 3;
    then Seg len q c= Seg len p by FINSEQ_1:def 3;
    then len q<=len p by FINSEQ_1:7;
    then m1<=len p by A2,XXREAL_0:2;
    hence q.n1 <> q.m1 by A1,A2,A3,A4,GRAPH_1:def 13;
  end;
  then p' is Path of G by GRAPH_1:def 13;
  hence thesis;
end;

theorem Th25:
  sc is OrientedPath of G
proof
  assume not sc is OrientedPath of G;
  then consider m, n such that
A1: 1<=m & m<n & n<=len sc & sc.m = sc.n by GRAPH_1:def 13;
  sc is Simple by Th20;
  then consider vs such that
A2: vs is_oriented_vertex_seq_of sc &
  (for n, m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs)
  by Def7;
A3: len vs = len sc + 1 by A2,Def5;
A4: m<=len sc by A1,XXREAL_0:2;
A5: 1<=n by A1,XXREAL_0:2;
A6: 1<=m & m<n & n<=len vs by A1,A3,NAT_1:12;
A7: 1<=m+1 & m+1<n+1 & n+1<=len vs by A1,A3,NAT_1:12,XREAL_1:8;
  then
A8: 1<=m & m<n+1 & n+1<=len vs by A1,NAT_1:12;
  set v1 = vs/.m;
  set v2 = vs/.(m+1);
A9: sc.m orientedly_joins v1, v2 by A1,A2,A4,Def5;
  set v1' = vs/.n;
  set v2' = vs/.(n+1);
  sc.n orientedly_joins v1', v2' by A1,A2,A5,Def5;
  then
A10: v1=v1' & v2=v2' by A1,A9,Th2;
  m<=len vs & m+1<=len vs & 1<=n+1 by A7,A8,XXREAL_0:2;
  then
A11: v1=vs.m & v2=vs.(m+1) & v1'=vs.n & v2'=vs.(n+1) by A5,A6,A7, FINSEQ_4:24;
  then m=1 & n=len vs by A2,A6,A10;
  hence contradiction by A2,A7,A10,A11;
end;

theorem
  for c1 being FinSequence holds
  (c1 is Simple (oriented Chain of G) iff c1 is oriented simple Chain of G)
  &(c1 is oriented simple Chain of G implies c1 is OrientedPath of G)
  by Th20,Th21,Th25;

