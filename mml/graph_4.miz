:: Oriented Simple Chains Included in Oriented Chains
::  by Yatsuka Nakamura and Piotr Rudnicki
::
:: Received August 19, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies FINSEQ_1, GRAPH_1, ARYTM_1, FUNCT_1, ORDERS_1, BOOLE, GRAPH_2,
      RELAT_1, FINSET_1, CARD_1, FINSEQ_2, GRAPH_4, FINSEQ_4, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, FINSEQ_1, FINSEQ_2,
      FINSEQ_4, CARD_1, FINSET_1, NUMBERS, XCMPLX_0, NAT_1, GRAPH_1, GRAPH_2,
      BINARITH, XXREAL_0;
 constructors PARTFUN1, XXREAL_0, XREAL_0, NAT_1, RECDEF_1, FINSEQ_4, BINARITH,
      GRAPH_2;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, ARYTM_3, XXREAL_0, XREAL_0, NAT_1,
      FINSEQ_1, GRAPH_2;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FINSEQ_2;
 theorems TARSKI, NAT_1, REAL_1, FINSEQ_1, FINSEQ_2, GRAPH_1, RELAT_1, FUNCT_1,
      GRFUNC_1, FUNCT_2, BINARITH, REAL_2, FINSET_1, FINSEQ_3, FINSEQ_4,
      GRAPH_2, XBOOLE_0, XBOOLE_1, XREAL_1, CARD_1, XXREAL_0;
 schemes NAT_1, FINSEQ_1, FUNCT_1;

begin   ::Oriented vertex sequences and V-sets

reserve
        p, q for FinSequence,
        y1,y2,e,X for set,
        i, j, k, m, n for Element of NAT,
        G for Graph;
reserve x,y,v,v1,v2,v3,v4 for Element of the Vertices of G;

definition let G; let x, y; let e;
   pred e orientedly_joins x, y means
:Def1:  (the Source of G).e = x & (the Target of G).e = y;
end;

theorem Th1:  e orientedly_joins v1, v2 implies e joins v1, v2
 proof assume e orientedly_joins v1, v2;
    then ((the Source of G).e = v1 & (the Target of G).e = v2) by Def1;
  hence e joins v1, v2 by GRAPH_1:def 9;
 end;

definition let G; let x,y be Element of (the Vertices of G);
   pred x,y are_orientedly_incident means
        ex v being set st v in the Edges of G & v orientedly_joins x, y;
end;

theorem Th2:
e orientedly_joins v1,v2 & e orientedly_joins v3,v4 implies v1=v3 & v2=v4
 proof
  assume e orientedly_joins v1,v2 & e orientedly_joins v3,v4;
       then ((the Source of G).e = v1 & (the Target of G).e = v2)
       &
       ((the Source of G).e = v3 & (the Target of G).e = v4)
        by Def1;
  hence v1=v3 & v2=v4;
end;

reserve vs, vs1, vs2 for FinSequence of the Vertices of G,
        c, c1, c2 for oriented Chain of G;

registration let G;
 cluster empty oriented Chain of G;
 existence proof {} is oriented Chain of G by GRAPH_1:14; hence thesis; end;
end;

definition let G, X;
 func G-SVSet X -> set equals
  { v: ex e being Element of the Edges of G st e in X &
                v = (the Source of G).e };
 correctness;
end;

definition let G, X;
 func G-TVSet X -> set equals
  { v: ex e being Element of the Edges of G st e in X &
                v = (the Target of G).e };
 correctness;
end;

canceled;

theorem  G-SVSet {} = {} & G-TVSet {} = {}
proof set X = {};
 A1:now assume A2:G-SVSet X<>{};
   consider x being Element of G-SVSet X;
      x in G-SVSet X by A2;
   then x in { v: ex e being Element of the Edges of G st e in X &
                (v = (the Source of G).e) };
   then consider v such that A3:v=x &
   (ex e being Element of the Edges of G st e in X &
    (v = (the Source of G).e));
   consider e being Element of the Edges of G such that
   A4: e in X & (v = (the Source of G).e) by A3;
  thus contradiction by A4;
 end;
    now assume A5:G-TVSet X<>{};
   consider x being Element of G-TVSet X;
      x in G-TVSet X by A5;
   then x in { v: ex e being Element of the Edges of G st e in X &
                (v = (the Target of G).e) };
   then consider v such that A6:v=x &
   (ex e being Element of the Edges of G st e in X &
    (v = (the Target of G).e));
   consider e being Element of the Edges of G such that
   A7: e in X & (v = (the Target of G).e) by A6;
  thus contradiction by A7;
 end;
 hence thesis by A1;
end;

definition let G, vs; let c be FinSequence;
 pred vs is_oriented_vertex_seq_of c means
:Def5: len vs = len c + 1 &
   for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1);
end;

theorem Th5: vs is_oriented_vertex_seq_of c implies vs is_vertex_seq_of c
proof assume A1: vs is_oriented_vertex_seq_of c;
      then A2:len vs = len c + 1 &
      for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
                                       by Def5;
         for n st 1<=n & n<=len c holds c.n joins vs/.n, vs/.(n+1)
        proof let n;assume 1<=n & n<=len c;
          then c.n orientedly_joins vs/.n, vs/.(n+1) by A1,Def5;
         hence c.n joins vs/.n, vs/.(n+1) by Th1;
        end;
 hence vs is_vertex_seq_of c by A2,GRAPH_2:def 7;
end;

theorem  vs is_oriented_vertex_seq_of c implies G-SVSet rng c c= rng vs
proof
assume
A1: vs is_oriented_vertex_seq_of c;
 then A2: len vs = len c + 1 by Def5;
    G-SVSet rng c c= rng vs
 proof let y be set;
   assume
    y in G-SVSet rng c;
   then consider v being Element of the Vertices of G such that
A3: v=y & ex e being Element of the Edges of G st e in rng c &
        v = (the Source of G).e;
   consider e being Element of the Edges of G such that
A4: e in rng c & (v = (the Source of G).e) by A3;
   consider x being set such that
A5: x in dom c & e = c.x by A4,FUNCT_1:def 5;
   reconsider x as Element of NAT by A5;
A6: 1<=x & x<=len c by A5,FINSEQ_3:27;
then A7: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
   set v1 = vs/.x; set v2 = vs/.(x+1);
A8: v1 = vs.x & v2 = vs.(x+1) by A6,A7,FINSEQ_4:24;
      c.x orientedly_joins v1, v2 by A1,A6,Def5;
then A9: v = v1 by A4,A5,Def1;
      x in dom vs & x+1 in dom vs by A6,A7,FINSEQ_3:27;
   hence y in rng vs by A3,A8,A9,FUNCT_1:def 5;
  end;
hence thesis;
end;

theorem  vs is_oriented_vertex_seq_of c implies G-TVSet rng c c= rng vs
proof
assume
A1: vs is_oriented_vertex_seq_of c;
 then A2: len vs = len c + 1 by Def5;
let y be set;
   assume
    y in G-TVSet rng c;
   then consider v being Element of the Vertices of G such that
A3: v=y & ex e being Element of the Edges of G st e in rng c &
        v = (the Target of G).e;
   consider e being Element of the Edges of G such that
A4: e in rng c & (v = (the Target of G).e) by A3;
   consider x being set such that
A5: x in dom c & e = c.x by A4,FUNCT_1:def 5;
   reconsider x as Element of NAT by A5;
A6: 1<=x & x<=len c by A5,FINSEQ_3:27;
then A7: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
   set v1 = vs/.x; set v2 = vs/.(x+1);
A8: v1 = vs.x & v2 = vs.(x+1) by A6,A7,FINSEQ_4:24;
      c.x orientedly_joins v1, v2 by A1,A6,Def5;
then A9: v = v2 by A4,A5,Def1;
      x in dom vs & x+1 in dom vs by A6,A7,FINSEQ_3:27;
   hence y in rng vs by A3,A8,A9,FUNCT_1:def 5;
end;

theorem  c <>{} & vs is_oriented_vertex_seq_of c implies
   rng vs c= G-SVSet rng c \/ G-TVSet rng c
 proof assume
A1: c <>{} & vs is_oriented_vertex_seq_of c;
then A2: len vs = len c + 1 by Def5;
  let y be set;
  assume y in rng vs; then consider x being set such that
A3:  x in dom vs & y = vs.x by FUNCT_1:def 5;
   reconsider x as Element of NAT by A3;
A4: 1<=x & x<=len vs by A3,FINSEQ_3:27;
   per cases by A2,A4,NAT_1:8;
   suppose
A5:  x<=len c;
then A6: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
       x in dom c by A4,A5,FINSEQ_3:27;
    then c.x in rng c & rng c c= the Edges of G by FINSEQ_1:def 4,FUNCT_1:def 5
;
    then reconsider e = c.x as Element of the Edges of G;
   set v1 = vs/.x; set v2 = vs/.(x+1);
A7: v1 = vs.x & v2 = vs.(x+1) by A4,A6,FINSEQ_4:24;
      c.x orientedly_joins v1, v2 by A1,A4,A5,Def5;
then A8: v1 = (the Source of G).e & v2 = (the Target of G).e by Def1;
      x in dom c by A4,A5,FINSEQ_3:27;
then e in rng c by FUNCT_1:def 5;
   then y in G-SVSet rng c by A3,A7,A8;
   hence y in G-SVSet rng c \/ G-TVSet rng c by XBOOLE_0:def 2;
   end;
   suppose
A9:  x=len c+1; set l = len c;
       l <> 0 by A1,FINSEQ_1:25; then 0<l & 0+1=1;
then A10:  1<=l & l<=l+1 by NAT_1:13;
    then l in dom c by FINSEQ_3:27;
    then c.l in rng c & rng c c= the Edges of G by FINSEQ_1:def 4,FUNCT_1:def 5
;
    then reconsider e = c.l as Element of the Edges of G;
   set v1 = vs/.l; set v2 = vs/.(l+1);
A11: v1 = vs.l & v2 = vs.(l+1) by A2,A4,A9,A10,FINSEQ_4:24;
      c.l orientedly_joins v1, v2 by A1,A10,Def5;
then A12: v1 = (the Source of G).e & v2 = (the Target of G).e by Def1;
      l in dom c by A10,FINSEQ_3:27;
     then e in rng c by FUNCT_1:def 5;
   then y in G-TVSet rng c by A3,A9,A11,A12;
   hence y in G-SVSet rng c \/ G-TVSet rng c by XBOOLE_0:def 2;
end;
end;

begin :: Cutting and glueing of oriented chains

theorem  <*v*> is_oriented_vertex_seq_of {}
proof
 set c ={};
A1: len c = 0 by FINSEQ_1:25;
  hence len <*v*> = len c +1 by FINSEQ_1:57;
  let n; assume 1<=n & n<=len c; hence thesis by A1;
end;

theorem Th10:  ex vs st vs is_oriented_vertex_seq_of c
proof
    now per cases;
 case c <>{}; then A1:len c <>0 by FINSEQ_1:25;
  defpred P[Element of NAT,set] means
  (($1=len c+1 implies $2=(the Target of G).(c.len c)) &
  ($1<>len c+1 implies $2=(the Source of G).(c.$1)));
  A2:  for k,y1,y2 st k in Seg (len c +1) & P[k,y1] & P[k,y2]
   holds y1=y2;
  A3:for k st k in Seg (len c+1) ex x being set st P[k,x]
  proof let k;assume k in Seg (len c+1);
       now per cases;
    case k=len c+1;
     hence ex x being set st
      ((k=len c+1 implies x=(the Target of G).(c.len c)) &
      (k<>len c+1 implies x=(the Source of G).(c.k)));
    end;
    case k<>len c+1;
     hence ex x being set st
      ((k=len c+1 implies x=(the Target of G).(c.len c)) &
      (k<>len c+1 implies x=(the Source of G).(c.k)));
    end;
    end;
   hence ex x being set st
  ((k=len c+1 implies x=(the Target of G).(c.len c)) &
  (k<>len c+1 implies x=(the Source of G).(c.k)));
  end;
   ex p st dom p = Seg (len c+1) & for k st k in Seg (len c+1) holds
  P[k,p.k] from FINSEQ_1:sch 1(A2,A3);
  then consider p such that
  A4: dom p = Seg (len c+1) & for k st k in Seg (len c+1) holds
  ((k=len c+1 implies p.k=(the Target of G).(c.len c)) &
  (k<>len c+1 implies p.k=(the Source of G).(c.k)));
  A5: len p=len c+1 by A4,FINSEQ_1:def 3;
     rng p c= (the Vertices of G)
  proof let y be set;assume y in rng p;
   then consider x being set such that A6:x in dom p & y=p.x by FUNCT_1:def 5;
   reconsider k=x as Element of NAT by A6;
   A7:1<=k & k<=len c+1 by A4,A6,FINSEQ_1:3;
      now per cases;
   case k=len c+1; then A8:y=(the Target of G).(c.len c) by A4,A6;
        len c >0 by A1; then len c>=0+1 by NAT_1:13;
     then len c in Seg len c by FINSEQ_1:3;
     then len c in dom c by FINSEQ_1:def 3;
     then A9:c.len c in rng c by FUNCT_1:def 5;
        rng c c= the Edges of G by FINSEQ_1:def 4;
    hence y in (the Vertices of G) by A8,A9,FUNCT_2:7;
   end;
   case A10:k<>len c+1; then A11:y=(the Source of G).(c.k) by A4,A6;
        k<len c+1 by A7,A10,REAL_1:def 5;
     then k<=len c by NAT_1:13;
     then k in Seg len c by A7,FINSEQ_1:3;
     then k in dom c by FINSEQ_1:def 3;
     then A12:c.k in rng c by FUNCT_1:def 5;
        rng c c= the Edges of G by FINSEQ_1:def 4;
    hence y in (the Vertices of G) by A11,A12,FUNCT_2:7;
   end;
   end;
   hence y in (the Vertices of G);
  end;
  then reconsider vs=p as FinSequence of (the Vertices of G) by FINSEQ_1:def 4;
     for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
  proof let n;assume A13:1<=n & n<=len c;
    per cases;
    suppose A14:n=len c;
      then A15:n<len c+1 by NAT_1:13;
      then n in Seg (len c +1) by A13,FINSEQ_1:3;
      then A16:p.n=(the Source of G).(c.n) by A4,A15;
         1<n+1 by A13,NAT_1:13;
      then n+1 in Seg (len c +1) by A14,FINSEQ_1:3;
      then A17:p.(n+1)=(the Target of G).(c.len c) by A4,A14;
      A18:(the Source of G).(c.n) = vs/.n by A5,A13,A15,A16,FINSEQ_4:24;
         1<n+1 by A13,NAT_1:13;
      then (the Target of G).(c.n) = vs/.(n+1) by A5,A14,A17,FINSEQ_4:24;
     hence thesis by A18,Def1;
    end;
    suppose n<>len c;
      then A19:n<len c by A13,REAL_1:def 5;
      then n+1<=len c by NAT_1:13;
      then A20:n+1<len c+1 by NAT_1:13;
      A21:n<len c+1 by A13,NAT_1:13;
      then n in Seg (len c +1) by A13,FINSEQ_1:3;
      then A22:p.n=(the Source of G).(c.n) by A4,A21;
         1<n+1 by A13,NAT_1:13;
      then n+1 in Seg (len c +1) by A20,FINSEQ_1:3;
      then A23:p.(n+1)=(the Source of G).(c.(n+1)) by A4,A20;
      A24:(the Source of G).(c.n) = vs/.n by A5,A13,A21,A22,FINSEQ_4:24;
         1<n+1 by A13,NAT_1:13;
      then vs/.(n+1)=p.(n+1) by A5,A20,FINSEQ_4:24;
      then (the Target of G).(c.n) = vs/.(n+1) by A13,A19,A23,GRAPH_1:def 12;
     hence thesis by A24,Def1;
  end;
  end;
  then vs is_oriented_vertex_seq_of c by A5,Def5;
 hence thesis;
 end;
 case A25: c ={};
  consider x being Element of the Vertices of G;
  set vs=<*x*>;
  A26: len c =0 by A25,FINSEQ_1:25;
  then A27: len vs = len c + 1 by FINSEQ_1:57;
     for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
                       by A26;
  then vs is_oriented_vertex_seq_of c by A27,Def5;
 hence thesis;
 end;
 end;
 hence thesis;
end;

theorem Th11:
c <> {} & vs1 is_oriented_vertex_seq_of c & vs2 is_oriented_vertex_seq_of c
implies vs1 = vs2
proof assume
A1: c <>{} & vs1 is_oriented_vertex_seq_of c &
    vs2 is_oriented_vertex_seq_of c;
   then A2:   len vs1 = len c + 1 &
      for n st 1<=n & n<=len c holds c.n orientedly_joins vs1/.n, vs1/.(n+1)
                                 by Def5;
   A3:   len vs2 = len c + 1 &
      for n st 1<=n & n<=len c holds c.n orientedly_joins vs2/.n, vs2/.(n+1)
                                 by A1,Def5;
         for n st 1<=n & n<=len vs1 holds vs1.n=vs2.n
      proof let n;assume A4:1<=n & n<=len vs1;
        then A5:n<=len c +1 by A1,Def5;
        per cases;
        suppose n<len c +1;
          then A6:n<=len c by NAT_1:13;
          then c.n orientedly_joins vs1/.n, vs1/.(n+1) by A1,A4,Def5;
          then A7:(the Source of G).(c.n) = vs1/.n
          & (the Target of G).(c.n) = vs1/.(n+1) by Def1;
             c.n orientedly_joins vs2/.n, vs2/.(n+1) by A1,A4,A6,Def5;
          then A8:(the Source of G).(c.n) = vs2/.n
          & (the Target of G).(c.n) = vs2/.(n+1) by Def1;
             vs1.n=vs1/.n by A4,FINSEQ_4:24;
         hence thesis by A2,A3,A4,A7,A8,FINSEQ_4:24;
        end;
        suppose n>=len c+1;
          then A9: n=len c+1 by A5,XXREAL_0:1;
          then A10:n-1=len c;
             0<>len c by A1,FINSEQ_1:25;
          then 0<len c;
then A11:       0+1<=len c by NAT_1:13;
          then A12:n-1=n-'1 by A9,BINARITH:57;
          A13:1<=n-'1 & n-'1=len c by A10,A11,BINARITH:57;
          then c.(n-'1) orientedly_joins vs1/.(n-'1), vs1/.((n-'1)+1)
                                    by A1,Def5;
          then A14:(the Source of G).(c.(n-'1)) = vs1/.(n-'1)
          & (the Target of G).(c.(n-'1)) = vs1/.((n-'1)+1) by Def1;
             c.(n-'1) orientedly_joins vs2/.(n-'1), vs2/.((n-'1)+1)
                                    by A1,A13,Def5;
          then A15:(the Source of G).(c.(n-'1)) = vs2/.(n-'1)
          & (the Target of G).(c.(n-'1)) = vs2/.((n-'1)+1) by Def1;
          A16:n-'1+1=n by A12;
             vs1.n=vs1/.n by A4,FINSEQ_4:24;
         hence thesis by A2,A3,A4,A14,A15,A16,FINSEQ_4:24;
      end;
      end;
  hence thesis by A2,A3,FINSEQ_1:18;
end;

definition let G, c;
assume A1: c <>{};
 func oriented-vertex-seq c -> FinSequence of the Vertices of G means
    it is_oriented_vertex_seq_of c;
existence by Th10;
uniqueness by A1,Th11;
end;

theorem  vs is_oriented_vertex_seq_of c & c1 = c|Seg n & vs1= vs|Seg (n+1)
    implies vs1 is_oriented_vertex_seq_of c1
proof assume A1: vs is_oriented_vertex_seq_of c;
then A2: len vs = len c + 1 &
    for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n,vs/.(n+1)
                                                              by Def5;
 assume
A3: c1 = c|Seg n & vs1= vs|Seg (n+1);
    now
  per cases;
  suppose
A4: n<=len c; then n+1<=len vs by A2,XREAL_1:8;
then A5: len c1 = n & len vs1 = n+1 by A3,A4,FINSEQ_1:21;
   hence len vs1 = len c1 + 1;
   let k be Element of NAT; assume
A6: 1<=k & k<=len c1;
then A7: k<=len c by A4,A5,XXREAL_0:2;
    then k in dom c & k in Seg n by A5,A6,FINSEQ_1:3,FINSEQ_3:27;
then A8: c1.k = c.k by A3,FUNCT_1:72; A9: k<=len vs by A2,A7,NAT_1:12;
A10: k<=n+1 by A5,A6,NAT_1:12;
then A11: k in dom vs & k in Seg (n+1) by A6,A9,FINSEQ_1:3,FINSEQ_3:27;
A12: k+1<=len vs by A2,A7,XREAL_1:9;
A13: 1<=k+1 & k+1<=len c1 +1 by A6,NAT_1:12,XREAL_1:9;
    then k+1 in dom vs & k+1 in Seg (n+1) by A5,A12,FINSEQ_1:3,FINSEQ_3:27;
then A14: vs1.k=vs.k & vs1.(k+1)=vs.(k+1) by A3,A11,FUNCT_1:72;
A15: vs/.k=vs.k & vs/.(k+1)=vs.(k+1) by A6,A9,A12,A13,FINSEQ_4:24;
       vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) by A5,A6,A10,A13,FINSEQ_4:24;
   hence c1.k orientedly_joins vs1/.k,vs1/.(k+1)
     by A1,A6,A7,A8,A14,A15,Def5;
  end;
  suppose
A16: len c <=n; then len vs<=n+1 by A2,XREAL_1:8;
    then c1 = c & vs1 = vs by A3,A16,FINSEQ_2:23;
   hence len vs1 = len c1 + 1 &
         for k being Element of NAT st 1<=k & k<=len c1
            holds c1.k orientedly_joins vs1/.k,vs1/.(k+1) by A1,Def5;
  end;
  end; hence thesis by Def5;
end;

theorem Th13:
1<=m & m<=n & n<=len c & q = (m,n)-cut c implies q is oriented Chain of G
proof assume
A1: 1<=m & m<=n & n<=len c & q = (m,n)-cut c;
consider vs such that
A2: vs is_oriented_vertex_seq_of c by Th10;
     len q +m-m=n+1-m by A1,GRAPH_2:def 1;
then A3: len q = n-m+1;
 reconsider qq=q as Chain of G by A1,GRAPH_2:44;
      for n st 1 <= n & n < len q holds
       (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
     proof
      let k be Element of NAT;
      assume A4:1<=k & k<len q;
          1-1<=m-1 by A1,XREAL_1:11; then m-1 = m-'1 by BINARITH:def 3;
       then reconsider m1= m-1 as Element of NAT;
        reconsider i = m1+k as Element of NAT;
     set v1 = vs/.i; set v2 = vs/.(i+1);
          0+1<=k by A4; then consider j such that
A5:  0<=j & j<len q & k=j+1 by A4,GRAPH_2:1;
A6:  i= m+j by A5;
    i+1= m+(j+1) by A5;
    then A7:c.(i+1)=q.(k+1) & c.i=q.k by A1,A4,A5,A6,GRAPH_2:def 1;
       1-1<=m-1 by A1,XREAL_1:11;
then A8:  0+1<=m-1+k by A4,XREAL_1:9;  i<=(m-1)+(n-(m-1)) by A3,A4,XREAL_1:8;
     then 1<=i & i<=len c by A1,A8,XXREAL_0:2;
 then c.i orientedly_joins v1, v2 by A2,Def5;
     then A9:(the Source of G).(c.i) = v1 & (the Target of G).(c.i) = v2 by
Def1;
A10:1<i+1 by A8,NAT_1:13;
   k+m<n-m+1+m by A3,A4,XREAL_1:8;
then k+m<n+1;
then k+m<=n by NAT_1:13;
then m+k<=len c by A1,XXREAL_0:2;
 then c.(i+1) orientedly_joins v2, vs/.(i+1+1) by A2,A10,Def5;
      hence (the Source of G).(q.(k+1)) = (the Target of G).(q.k)
                                      by A7,A9,Def1;
     end;
    then qq is oriented by GRAPH_1:def 12;
 hence thesis;
end;

theorem Th14:
  1<=m & m<=n & n<=len c & c1 = (m,n)-cut c &
vs is_oriented_vertex_seq_of c & vs1 = (m,n+1)-cut vs
    implies vs1 is_oriented_vertex_seq_of c1 proof assume
A1: 1<=m & m<=n & n<=len c; assume
A2: c1 = (m,n)-cut c; assume
A3: vs is_oriented_vertex_seq_of c & vs1 = (m,n+1)-cut vs;
then A4: len vs = len c + 1 by Def5;
then A5: m<=n+1 & n+1<=len vs by A1,XREAL_1:8,NAT_1:12;
A6: len c1 +m= n+1 by A1,A2,GRAPH_2:def 1;
    len vs1 +m = n+1+1 by A1,A3,A5,GRAPH_2:def 1;
hence A7: len vs1 = len c1 + 1 by A6;
    let k be Element of NAT; assume
A8:  1<=k & k<=len c1; then 0+1<=k; then consider j such that
A9:  0<=j & j<len c1 & k=j+1 by A8,GRAPH_2:1;
     set i = m+j; set v1 = vs/.i; set v2 = vs/.(i+1);
        m+k<=len c1 +m by A8,XREAL_1:9;
     then m+j+1-1<=len c1 +m-1 by A9,XREAL_1:11;
then A10:  1<=i & i<=len c by A1,A6,XXREAL_0:2,NAT_1:12;
then A11:  c.i orientedly_joins v1, v2 by A3,Def5;
         j<len vs1 by A7,A9,NAT_1:13;
then A12: vs1.k = vs.i by A1,A3,A5,A9,GRAPH_2:def 1;
A13:  j+1<len vs1 by A7,A9,XREAL_1:8;
         m+j+1 = m+(j+1);
then A14:  vs1.(k+1) = vs.(i+1) by A1,A3,A5,A9,A13,GRAPH_2:def 1;
      i<=len vs & 1<=i+1 & i+1<=len vs by A4,A10,NAT_1:12,XREAL_1:9;
then A15:  vs/.i=vs.i & vs/.(i+1)=vs.(i+1) by A10,FINSEQ_4:24;
         0+1=1;
then A16:   1<=k & k<=len c1 by A9,NAT_1:13;
   then k<=len vs1 & 1<=k+1 & k+1<=len vs1 by A7,NAT_1:12,XREAL_1:9;
then vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) by A16,FINSEQ_4:24;
    hence c1.k orientedly_joins vs1/.k, vs1/.(k+1)
      by A1,A2,A9,A11,A12,A14,A15,GRAPH_2:def 1;
   end;

theorem Th15:
vs1 is_oriented_vertex_seq_of c1
& vs2 is_oriented_vertex_seq_of c2 & vs1.len vs1 = vs2.1
    implies c1^c2 is oriented Chain of G
proof assume A1: vs1 is_oriented_vertex_seq_of c1
& vs2 is_oriented_vertex_seq_of c2 & vs1.len vs1 = vs2.1;
then A2: vs1 is_vertex_seq_of c1
& vs2 is_vertex_seq_of c2 & vs1.len vs1 = vs2.1 by Th5;
A3: len vs1 = len c1 + 1 & len vs2 = len c2 + 1 by A1,Def5;
  set q=c1^c2; set p=vs1^'vs2;
 thus c1^c2 is oriented Chain of G proof
  reconsider cc=c1^c2 as Chain of G by A2,GRAPH_2:46;
        for n st 1 <= n & n < len q holds
       (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
      proof let n;assume A4: 1 <= n & n < len q;
then A5:  n in dom q by FINSEQ_3:27;
    A6:n+1<=len q by A4,NAT_1:13;
    A7:1<n+1 by A4,NAT_1:13;
       now per cases by A5,FINSEQ_1:38;
    case
A8:   n in dom c1;
then A9:   q.n = c1.n by FINSEQ_1:def 7;
     set v1=vs1/.n; set v2=vs1/.(n+1);
A10:   1<=n & n<=len c1 by A8,FINSEQ_3:27;
then A11:   c1.n orientedly_joins v1, v2 by A1,Def5;
     A12:   n<=len vs1 by A3,A10,NAT_1:12;
        1<=n+1 & n+1<=len c1+1 by A10,XREAL_1:8,NAT_1:12;
then A13:  1<=n+1 & n+1<=len vs1 by A1,Def5;
then A14:  vs1/.n=vs1.n & vs1/.(n+1)=vs1.(n+1) by A10,A12,FINSEQ_4:24;
        p.n = vs1.n & p.(n+1) = vs1.(n+1) by A10,A12,A13,GRAPH_2:14;
    hence ex v1, v2 being Element of the Vertices of G st
      v1 = p.n & v2 = p.(n+1) & q.n orientedly_joins v1, v2 by A9,A11,A14;
    end;
    case ex k being Element of NAT st k in dom c2 & n=len c1 + k;
     then consider k being Element of NAT such that
A15:   k in dom c2 & n = len c1 + k;
A16:   q.n = c2.k by A15,FINSEQ_1:def 7;
A17:   1<=k & k<=len c2 by A15,FINSEQ_3:27;
then A18:  1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12,XREAL_1:9;
     set v1 = vs2/.k; set v2 = vs2/.(k+1);
A19: vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A17,A18,FINSEQ_4:24;
A20:   c2.k orientedly_joins v1, v2 by A1,A17,Def5;
A21:  k<=len vs2 by A3,A17,NAT_1:12;
        0+1<=k by A15,FINSEQ_3:27; then consider j such that
A22:  0<=j & j<len vs2 & k=j+1 by A21,GRAPH_2:1;
A23:   p.n = vs2.k proof per cases by A17,REAL_1:def 5;
      suppose A24: 1=k;  0<len vs1 by A3;
then A25: 0+1<=len vs1 by NAT_1:13;
       thus p.n = p.(len vs1) by A1,A15,A24,Def5
        .= vs2.k by A1,A24,A25,GRAPH_2:14;
      end;
      suppose 1<k; then A26:1<=j by A22,NAT_1:13;
       thus p.n = p.(len c1 +1+j) by A15,A22
       .= p.(len vs1 +j) by A1,Def5
        .= vs2.k by A22,A26,GRAPH_2:15;
     end;
     end;
A27:   k<len vs2 by A3,A17,NAT_1:13;
        p.(n+1) = p.(len c1 +1+k) by A15
            .= p.(len vs1+k) by A1,Def5 .= vs2.(k+1) by A17,A27,GRAPH_2:15;
    hence ex v1, v2 being Element of the Vertices of G st
           v1 = p.n & v2 = p.(n+1)
           & q.n orientedly_joins v1, v2 by A16,A19,A20,A23;
   end;
   end;
       then consider v1,v2 being Element of the Vertices of G such that
       A28: v1=p.n & v2=p.(n+1)
        & q.n orientedly_joins v1,v2;
    A29:  n+1 in dom q by A6,A7,FINSEQ_3:27;
       now per cases by A29,FINSEQ_1:38;
    case
A30:   n+1 in dom c1;
then A31:   q.(n+1) = c1.(n+1) by FINSEQ_1:def 7;
     set v2'=vs1/.(n+1),v3=vs1/.(n+1+1);
A32:   1<=n+1 & n+1<=len c1 by A30,FINSEQ_3:27;
then A33:   c1.(n+1) orientedly_joins v2', v3 by A1,Def5;
     A34:   n+1<=len vs1 by A3,A32,NAT_1:12;
        1<=n+1+1 & n+1+1<=len c1+1 by A32,XREAL_1:8,NAT_1:12;
then A35:  1<=n+1+1 & n+1+1<=len vs1 by A1,Def5;
then A36:  vs1/.(n+1)=vs1.(n+1) &
      vs1/.(n+1+1)=vs1.(n+1+1) by A32,A34,FINSEQ_4:24;
        p.(n+1) = vs1.(n+1) & p.(n+1+1) = vs1.(n+1+1) by A32,A34,A35,GRAPH_2:14
;
    hence ex v2'',v3' being Element of the Vertices of G st
           v2'' = p.(n+1) & v3' = p.(n+1+1)
           & q.(n+1) orientedly_joins v2'', v3' by A31,A33,A36;
    end;
    case ex k being Element of NAT st k in dom c2 & (n+1)=len c1 + k;
     then consider k being Element of NAT such that
A37:   k in dom c2 & (n+1) = len c1 + k;
A38:   q.(n+1) = c2.k by A37,FINSEQ_1:def 7;
A39:   1<=k & k<=len c2 by A37,FINSEQ_3:27;
then A40:  1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12,XREAL_1:9;
     set v2'=vs2/.k, v3 = vs2/.(k+1);
A41: vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A39,A40,FINSEQ_4:24;
A42:   c2.k orientedly_joins v2', v3 by A1,A39,Def5;
A43:  k<=len vs2 by A3,A39,NAT_1:12;
        0+1<=k by A37,FINSEQ_3:27; then consider j such that
A44:  0<=j & j<len vs2 & k=j+1 by A43,GRAPH_2:1;
A45:   p.(n+1) = vs2.k proof per cases by A39,REAL_1:def 5;
      suppose A46: 1=k;  0<len vs1 by A3;
then A47: 0+1<=len vs1 by NAT_1:13;
       thus p.(n+1) = p.(len vs1) by A1,A37,A46,Def5
        .= vs2.k by A1,A46,A47,GRAPH_2:14;
      end;
      suppose 1<k; then A48:1<=j by A44,NAT_1:13;
       thus p.(n+1) = p.(len c1 +1+j) by A37,A44
       .= p.(len vs1 +j) by A1,Def5
        .= vs2.k by A44,A48,GRAPH_2:15;
     end;
     end;
A49:   k<len vs2 by A3,A39,NAT_1:13;
        p.((n+1)+1) = p.(len c1 +1+k) by A37
            .= p.(len vs1+k) by A1,Def5 .= vs2.(k+1) by A39,A49,GRAPH_2:15;
    hence ex v2'',v3' being Element of the Vertices of G st
           v2'' = p.(n+1) & v3' = p.((n+1)+1)
           & q.(n+1) orientedly_joins v2'', v3' by A38,A41,A42,A45;
   end;
   end;
   then consider v2',v3 being Element of the Vertices of G such that
   A50: v2'=p.(n+1) & v3=p.(n+1+1) &
   q.(n+1) orientedly_joins v2',v3;
        (the Source of G).(q.n) = v1 & (the Target of G).(q.n) = v2
                              by A28,Def1;
   hence (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
                                   by A28,A50,Def1;
   end;
  then cc is oriented by GRAPH_1:def 12;
  hence thesis;
 end;
end;

theorem Th16:
vs1 is_oriented_vertex_seq_of c1 & vs2 is_oriented_vertex_seq_of c2
& vs1.len vs1 = vs2.1 &
c = c1^c2 & vs = vs1^'vs2
   implies vs is_oriented_vertex_seq_of c proof assume
A1: vs1 is_oriented_vertex_seq_of c1 & vs2 is_oriented_vertex_seq_of c2
& vs1.len vs1 = vs2.1;
   assume
A2: c = c1^c2 & vs = vs1^'vs2;
A3: len vs1 = len c1 + 1 & len vs2 = len c2 + 1 by A1,Def5;
then A4: vs1 <> {} & vs2 <> {} by FINSEQ_1:25;
  set q=c1^c2; set p=vs1^'vs2;
     len p +1 = len vs1 +len vs2 by A4,GRAPH_2:13;
then A5: len p = len c1 + len c2 + 1 by A3
         .= len q + 1 by FINSEQ_1:35;
   reconsider p as FinSequence of the Vertices of G;
   now let n be Element of NAT; assume
A6:  1<=n & n<=len q;
then n<=len p & 1<=n+1 & n+1<=len p by A5,NAT_1:12,XREAL_1:9;
then A7:  p/.n=p.n & p/.(n+1)=p.(n+1) by A6,FINSEQ_4:24;
A8:  n in dom q by A6,FINSEQ_3:27;
    per cases by A8,FINSEQ_1:38;
    suppose
A9:   n in dom c1;
     set v1=vs1/.n; set v2=vs1/.(n+1);
A10:   1<=n & n<=len c1 by A9,FINSEQ_3:27;
then A11:   c1.n orientedly_joins v1, v2 by A1,Def5;
     A12:   n<=len vs1 by A3,A10,NAT_1:12;
        1<=n+1 & n+1<=len c1+1 by A10,XREAL_1:8,NAT_1:12;
then A13:  1<=n+1 & n+1<=len vs1 by A1,Def5;
then A14:  vs1/.n=vs1.n & vs1/.(n+1)=vs1.(n+1) by A10,A12,FINSEQ_4:24;
        p.n = vs1.n & p.(n+1) = vs1.(n+1) by A10,A12,A13,GRAPH_2:14;
    hence q.n orientedly_joins p/.n, p/.(n+1)
      by A7,A9,A11,A14,FINSEQ_1:def 7;
    end;
    suppose ex k being Element of NAT st k in dom c2 & n=len c1 + k;
     then consider k being Element of NAT such that
A15:   k in dom c2 & n = len c1 + k;
A16:   q.n = c2.k by A15,FINSEQ_1:def 7;
A17:   1<=k & k<=len c2 by A15,FINSEQ_3:27;
then 1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12,XREAL_1:9;
then A18:  vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A17,FINSEQ_4:24;
A19:  k<=len vs2 by A3,A17,NAT_1:12;
        0+1<=k by A15,FINSEQ_3:27; then consider j such that
A20:  0<=j & j<len vs2 & k=j+1 by A19,GRAPH_2:1;
A21:   p.n = vs2.k proof per cases by A17,REAL_1:def 5;
      suppose A22: 1=k;  0<len vs1 by A3;
then A23: 0+1<=len vs1 by NAT_1:13;
       thus p.n = p.(len vs1) by A1,A15,A22,Def5
        .= vs2.k by A1,A22,A23,GRAPH_2:14;
      end;
      suppose 1<k; then A24:1<=j by A20,NAT_1:13;
       thus p.n = p.(len c1 +1+j) by A15,A20 .= p.(len vs1 +j)
                                         by A1,Def5
        .= vs2.k by A20,A24,GRAPH_2:15;
     end;
     end;
A25:   k<len vs2 by A3,A17,NAT_1:13;
        p.(n+1) = p.(len c1 +1+k) by A15
            .= p.(len vs1+k) by A1,Def5 .= vs2.(k+1) by A17,A25,GRAPH_2:15;
    hence q.n orientedly_joins p/.n,p/.(n+1) by A1,A7,A16,A17,A18,A21,Def5;
   end;
   end;
hence thesis by A2,A5,Def5;
end;

begin :: Oriented simple chains in oriented chains

 Lm1: <*v*> is_oriented_vertex_seq_of {} proof
    set p = <*v*>, ec = {};
A1:  len p = 0 + 1 by FINSEQ_1:56 .= len ec + 1 by FINSEQ_1:25;
        now let n; assume 1<=n & n<=len ec;
        then 1<=len ec by XXREAL_0:2; then 1<=0 by FINSEQ_1:25;
      hence ec.n orientedly_joins p/.n,p/.(n+1);
     end;
   hence thesis by A1,Def5;
end;

definition let G;
  let IT be oriented Chain of G;
   attr IT is Simple means
:Def7: ex vs st vs is_oriented_vertex_seq_of IT &
     (for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs);
end;

registration let G;
   cluster Simple (oriented Chain of G);
existence
     proof consider q being empty oriented Chain of G;
     consider x being Element of the Vertices of G;
     reconsider p =<*x*> as FinSequence of the Vertices of G;
     A1:p is_oriented_vertex_seq_of q by Lm1;
        for n,m st 1<=n & n<m & m<=len p & p.n=p.m holds n=1 & m=len p
     proof
      let n,m; assume 1<=n & n<m & m<=len p & p.n = p.m;
       then 1<m & m<=1 by XXREAL_0:2,FINSEQ_1:56;
      hence n =1 & m = len p;
     end;
     then q is Simple by A1,Def7;
  hence thesis;
 end;
end;

registration let G;
   cluster oriented simple Chain of G;
existence
     proof consider q being empty oriented Chain of G;
     consider x being Element of the Vertices of G;
     reconsider p =<*x*> as FinSequence of the Vertices of G;
        p is_oriented_vertex_seq_of q by Lm1;
     then A1: p is_vertex_seq_of q by Th5;
     reconsider q'=q as Chain of G;
        for n,m st 1<=n & n<m & m<=len p & p.n=p.m holds n=1 & m=len p
     proof
      let n,m; assume 1<=n & n<m & m<=len p & p.n = p.m;
       then 1<m & m<=1 by XXREAL_0:2,FINSEQ_1:56;
      hence n =1 & m = len p;
     end;
     then q' is simple by A1,GRAPH_2:def 10;
  hence thesis;
 end;
end;

canceled;

theorem Th18:for q being oriented Chain of G holds
      q|(Seg n) is oriented Chain of G
proof let q be oriented Chain of G;
     reconsider q'= q|(Seg n) as FinSequence by FINSEQ_1:19;
        for i st 1 <= i & i < len q' holds
       (the Source of G).((q|(Seg n)).(i+1))
       = (the Target of G).((q|(Seg n)).i)
      proof let i;assume A1:1 <= i & i < len q';
        per cases;
        suppose n>=len q; then q|(Seg n)=q by FINSEQ_3:55;
         hence thesis by A1,GRAPH_1:def 12;
        end;
        suppose A2:n<len q;
          then A3:len q'=n by FINSEQ_1:21;
          then A4:i<len q by A1,A2,XXREAL_0:2;
             i in Seg n by A1,A3,FINSEQ_1:3;
          then A5:(q|(Seg n)).i=q.i by FUNCT_1:72;
          A6:i+1<=n by A1,A3,NAT_1:13;
             1<i+1 by A1,NAT_1:13;
          then i+1 in Seg n by A6,FINSEQ_1:3;
          then (q|(Seg n)).(i+1)=q.(i+1) by FUNCT_1:72;
         hence thesis by A1,A4,A5,GRAPH_1:def 12;
      end;
      end;
 hence thesis by GRAPH_1:4,def 12;
end;

reserve sc for oriented simple Chain of G;

theorem  sc|(Seg n) is oriented simple Chain of G by Th18,GRAPH_2:49;

theorem Th20: for sc' being oriented Chain of G st sc'=sc holds sc' is Simple
proof let sc' be oriented Chain of G;
  assume A1:sc'=sc;
   consider vs such that A2:vs is_oriented_vertex_seq_of sc' by Th10;
      vs is_vertex_seq_of sc by A1,A2,Th5;
   then for n, m st 1<=n & n<m & m<=len vs & vs.n = vs.m holds n=1 & m=len vs
                                     by GRAPH_2:51;
  hence thesis by A2,Def7;
end;

theorem Th21: for sc' being Simple (oriented Chain of G)
  holds sc' is oriented simple Chain of G
proof let sc' be Simple (oriented Chain of G);
   consider vs such that A1:vs is_oriented_vertex_seq_of sc' &
     (for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs)
                                by Def7;
      vs is_vertex_seq_of sc' by A1,Th5;
  hence thesis by A1,GRAPH_2:def 10;
end;

reserve x,y for set;

Lm2:
 for p be FinSequence, m, n be Element of NAT
  st 1<=m & m<=n+1 & n<=len p
 holds len (m, n)-cut p +m = n+1 &
 for i being Element of NAT st i<len (m, n)-cut p
  holds ((m, n)-cut p).(i+1)=p.(m+i)
 proof let p be FinSequence, m, n be Element of NAT such that
A1: 1<=m & m<=n+1 & n<=len p;
  m <= n+1 by A1;
   then m = n+1 or m < n+1 by XXREAL_0:1;
   then
A2:  m = n+1 or m <= n by NAT_1:13;
  per cases by A2;
  suppose
A3: m = n+1;
   then n < m by XREAL_1:31;
   then not(1<=m & m<=n & n<=len p);
   then (m, n)-cut p = {} by GRAPH_2:def 1;
   then
A4:  len (m, n)-cut p = 0 by CARD_1:47;
  hence len (m, n)-cut p +m = n+1 by A3;
  thus thesis by A4;
  end;
  suppose m<=n;
   hence thesis by A1,GRAPH_2:def 1;
  end;
 end;

theorem Th22:
not c is Simple & vs is_oriented_vertex_seq_of c
implies
  ex fc being Subset of c, fvs being Subset of vs, c1, vs1
   st len c1 < len c & vs1 is_oriented_vertex_seq_of c1 & len vs1 < len vs &
      vs.1 = vs1.1 & vs.len vs = vs1.len vs1 & Seq fc = c1 & Seq fvs = vs1
proof assume
A1: not c is Simple & vs is_oriented_vertex_seq_of c;
   then consider n, m being Element of NAT such that
A2: 1<=n & n<m & m<=len vs & vs.n=vs.m & (n<>1 or m<>len vs) by Def7;
A3: len vs = len c + 1 by A1,Def5;
      A4: m-n>n-n by A2,XREAL_1:11;
   reconsider n1 = n-'1 as Element of NAT; A5: 1-1<=n-1 by A2,XREAL_1:11;
then A6: n-1 = n-'1 by BINARITH:def 3;
A7: n1+1 = n-1+1 by A5,BINARITH:def 3 .= n;
   per cases by A2;
    suppose
A8:  n<>1 & m <> len vs;
    then 1 < n by A2,REAL_1:def 5;
    then 1 + 1<=n by NAT_1:13;
then A9: 1+1-1<=n-1 by XREAL_1:11;
       n < len vs by A2,XXREAL_0:2
; then A10: n-1 < len c +1-1 by A3,XREAL_1:11;
A11: 1<=n1+1 by NAT_1:12;
A12:   m < len vs by A2,A8,REAL_1:def 5;
then A13:  m<=len c by A3,NAT_1:13;
A14: 1<=m & m<=len c & len c <=len c by A2,A3,A12,XXREAL_0:2,NAT_1:13;
   reconsider c1 = (1,n1)-cut c as oriented Chain of G by A6,A9,A10,Th13;
   reconsider c2 = (m,len c)-cut c as oriented Chain of G by A14,Th13;
   set pp = (1,n)-cut vs; set p2 = (m,len c+1)-cut vs;
   set p2' = (m+1, len c +1)-cut vs;
   reconsider pp as FinSequence of the Vertices of G;
   reconsider p2 as FinSequence of the Vertices of G;
A15: 1<=n & n<=len vs by A2,XXREAL_0:2;
A16: 1<=m & m<=len c + 1 & len c + 1<=len vs by A1,A2,Def5,XXREAL_0:2;
A17: pp is_oriented_vertex_seq_of c1 by A1,A7,A9,A10,Th14;
A18: p2 is_oriented_vertex_seq_of c2 by A1,A14,Th14;
A19: len pp = len c1 + 1 by A17,Def5;
A20: len p2 = len c2 + 1 by A18,Def5;
        1-1<=m-1 by A14,XREAL_1:11;
     then m-'1 = m-1 by BINARITH:def 3;
     then reconsider m1 = m-1 as Element of NAT;
A21:  m = m1 +1;  len c2 +m = len c +1
         by A14,GRAPH_2:def 1;
then A22: len c2 =len c - m + 1;
       A23: m-m<=len c -m by A13,XREAL_1:11;
     then len c2 -'1 = len c2 -1 by A22,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
A24:  m+lc21 = m1+len c2;
     A25: 0+1=1 & 0<len p2 by A20;
then A26: 0+1=1 & 0<=1 & 1<=len p2 by NAT_1:13;
A27: p2 = (0+1, len p2)-cut p2 by GRAPH_2:7
       .= (0+1, 1)-cut p2 ^ (1+1, len p2)-cut p2 by A26,GRAPH_2:8;
        m1<=m by A21,NAT_1:12;
then A28: p2 = (m1+1, m)-cut vs ^ (m+1, len c +1)-cut vs by A2,A3,GRAPH_2:8
;
        (1,1)-cut p2 = <*p2.(0+1)*> by A26,GRAPH_2:6
                 .= <*vs.(m+0)*> by A16,A25,GRAPH_2:def 1
                 .= (m,m)-cut vs by A2,A14,GRAPH_2:6;
then A29: p2' = (2, len p2)-cut p2 by A27,A28,FINSEQ_1:46;
  set domfc = {k: 1<=k & k<=n1 or m<=k & k<= len c};
  deffunc F(set) = c.$1;
  consider fc being Function such that
A30: dom fc = domfc and
A31: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A32:    x = kk & (1<=kk & kk<=n1 or m<=kk & kk<= len c);
          1<=kk & kk<=len c by A6,A10,A14,A32,XXREAL_0:2;
      hence x in Seg len c by A32,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A30,FINSEQ_1:def 12;
 fc c= c proof let p be set; assume
A33:  p in fc; then consider x, y being set such that
A34:  [x,y] = p by RELAT_1:def 1;
A35:  x in dom fc & y = fc.x by A33,A34,FUNCT_1:8;
     then consider kk being Element of NAT such that
A36:  x = kk & (1<=kk & kk<=n1 or m<=kk & kk<= len c) by A30;
        1<=kk & kk<=len c by A6,A10,A14,A36,XXREAL_0:2;
then A37:  x in dom c by A36,FINSEQ_3:27;  y = c.kk by A30,A31,A35,A36;
hence p in c by A34,A36,A37,FUNCT_1:8;
  end;
  then reconsider fc as Subset of c;
  take fc;
  set domfvs = {k: 1<=k & k<=n or m+1<=k & k<= len vs};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A38: dom fvs = domfvs and
A39: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A40:    x = kk & (1<=kk & kk<=n or m+1<=kk & kk<= len vs);
          1<=m+1 by NAT_1:12; then 1<=kk & kk<=len vs by A15,A40,XXREAL_0:2;
      hence x in Seg len vs by A40,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A38,FINSEQ_1:def 12;
 fvs c= vs proof let p be set; assume
A41:  p in fvs; then consider x, y being set such that
A42:  [x,y] = p by RELAT_1:def 1;
A43:  x in dom fvs & y = fvs.x by A41,A42,FUNCT_1:8;
     then consider kk being Element of NAT such that
A44:  x = kk & (1<=kk & kk<=n or m+1<=kk & kk<= len vs) by A38;
        1<=m+1 by NAT_1:12;
     then 1<=kk & kk<=len vs by A15,A44,XXREAL_0:2;
then A45:  x in dom vs by A44,FINSEQ_3:27;  y = vs.kk by A38,A39,A43,A44;
     hence p in vs by A42,A44,A45,FUNCT_1:8;
  end;
  then reconsider fvs as Subset of vs;
  take fvs;
A46: p2.1 = vs.m & pp.len pp = vs.n by A15,A16,GRAPH_2:12;
  then reconsider c' = c1^c2 as oriented Chain of G by A2,A17,A18,Th15;
  take c';
  take p1=pp^'p2;
A47: p1 = pp^p2' by A29,GRAPH_2:def 2;
A48:  len c1 +1 = n1+1 by A6,A10,A11,Lm2;
then A49: len c1 = n - 1 by A6;
        -(-(m-n)) = m-n; then A50: -(m-n) < 0 by A4,XREAL_1:60;
    len c' = (n-1)+(len c -m+1)
     by  FINSEQ_1:35,A6,A22,A48
.= len c + (n+ -m );
  hence
A51: len c' < len c by A50,REAL_2:173;
  thus p1 is_oriented_vertex_seq_of c' by A2,A17,A18,A46,Th16;
  then len p1 = len c' + 1 by Def5;
  hence len p1 < len vs by A3,A51,XREAL_1:8;
      1<=1 + len c1 by NAT_1:12; then 1<=len pp by A17,Def5;
 then p1.1 = pp.1 by GRAPH_2:14;
   hence vs.1 = p1.1 by A15,GRAPH_2:12;
A52: p2.len p2 = vs.(len c + 1) by A16,GRAPH_2:12;
     m-m<=len c -m by A14,XREAL_1:11;
  then 0+1<=len c -m+1 by XREAL_1:8; then 1<len p2 by A20,A22,NAT_1:13;
hence vs.len vs = p1.len p1 by A3,A52,GRAPH_2:16;
     set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n1};
     set DR = {kk where kk is Element of NAT: m<=kk & kk<= len c};
        now let x be set;
      hereby assume x in domfc; then consider k being Element of NAT such that
A53:     x=k & (1<=k & k<=n1 or m<=k & k<= len c);
           x in DL or x in DR by A53;
       hence x in DL \/ DR by XBOOLE_0:def 2;
      end; assume A54: x in DL \/ DR;
      per cases by A54,XBOOLE_0:def 2;
      suppose x in DL; then consider k being Element of NAT such that
A55:    x=k & 1<=k & k<=n1;
      thus x in domfc by A55;
      end;
      suppose x in DR; then consider k being Element of NAT such that
A56:    x=k & m<=k & k<=len c;
      thus x in domfc by A56;
     end;
     end;
then A57:  domfc = DL \/ DR by TARSKI:2;
A58:  DL c= Seg len c & DR c= Seg len c proof
      hereby let x be set; assume x in DL;
       then consider k being Element of NAT such that
A59:    x=k & 1<=k & k<=n1;  k<=len c by A6,A10,A59,XXREAL_0:2;
       hence x in Seg len c by A59,FINSEQ_1:3;
      end; let x be set; assume x in DR;
       then consider k being Element of NAT such that
A60:    x=k & m<=k & k<=len c;
          1<=k by A16,A60,XXREAL_0:2;
       hence x in Seg len c by A60,FINSEQ_1:3;
     end;
     then reconsider DL as finite set by FINSET_1:13;
     reconsider DR as finite set by A58,FINSET_1:13;
    now let i,j; assume i in DL; then consider k being Element of NAT such that
A61:    k=i & 1<=k & k<=n1;
      assume j in DR; then consider l being Element of NAT such that
A62:    l=j & m<=l & l<=len c;
          n1<m by A2,A7,NAT_1:13; then k<m by A61,XXREAL_0:2;
      hence i < j by A61,A62,XXREAL_0:2;
     end;
then A63: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A58,FINSEQ_3:48;
    m+lc21 = len c by A22;
     then A64:  card DR = len c2 + -1 +1 by GRAPH_2:4
        .= len c2;
A65:  len Sgm DL = card DL & len Sgm DR = card DR by A58,FINSEQ_3:44;  DL
=
Seg n1 by FINSEQ_1:def 1;
then A66:  Sgm DL = idseq n1 by FINSEQ_3:54;
then A67:  dom Sgm DL = Seg n1 by RELAT_1:71;
        rng Sgm DL = DL by A58,FINSEQ_1:def 13;
then A68: rng Sgm DL c= dom fc by A30,A57,XBOOLE_1:7;
       rng Sgm DR = DR by A58,FINSEQ_1:def 13;
then A69: rng Sgm DR c= dom fc by A30,A57,XBOOLE_1:7;
    set SL = Sgm DL; set SR = Sgm DR;
       now let p be set;
     hereby assume
A70:   p in c1; then consider x, y being set such that
A71:  p=[x,y] by RELAT_1:def 1;
A72:   x in dom c1 & y=c1.x by A70,A71,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A73:   1<=k & k<=len c1 by A72,FINSEQ_3:27;
then A74:   x in dom SL by A6,A49,A67,FINSEQ_1:3;
then A75:   k=SL.k by A66,A67,FUNCT_1:35;
A76:   k in domfc by A6,A49,A73;
then A77:   x in dom (fc*SL) by A30,A74,A75,FUNCT_1:21;
         0+1<=k by A72,FINSEQ_3:27;
      then consider i being Element of NAT such that
A78:   0<=i & i<n1 & k=i+1 by A6,A49,A73,GRAPH_2:1;
         (fc*SL).x = fc.k by A75,A77,FUNCT_1:22 .= c.(1+i) by A30,A76,A78,
GRFUNC_1:8
        .= y by A6,A10,A11,A49,A72,A78,Lm2;
      hence p in fc*(Sgm DL) by A71,A77,FUNCT_1:8;
     end; assume
A79:  p in fc*(Sgm DL); then consider x,y being set such that
A80:  p=[x,y] by RELAT_1:def 1;
A81:   x in dom (fc*SL) & y = (fc*SL).x by A79,A80,FUNCT_1:8;
then A82:  (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A83:   x in dom SL & SL.x in dom fc by A81,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n1}
       by A67,FINSEQ_1:def 1;
      then consider k such that
A84:   k=x & 1<=k & k<=n1;
A85:   k in dom fc by A30,A84;
A86:   k=SL.k by A66,A67,A83,A84,FUNCT_1:35;
A87: k in dom c1 by A6,A49,A84,FINSEQ_3:27;  0+1<=k by A84;
     then consider i being Element of NAT such that
A88: 0<=i & i<n1 & k=i+1 by A84,GRAPH_2:1;
       c1.k = c.k by A6,A10,A11,A49,A88,Lm2
         .= y by A81,A82,A84,A85,A86,GRFUNC_1:8;
     hence p in c1 by A80,A84,A87,FUNCT_1:8;
    end;
then A89: c1 = fc*(Sgm DL) by TARSKI:2;
       now let p be set;
     hereby assume
A90:   p in c2; then consider x, y being set such that
A91:  p=[x,y] by RELAT_1:def 1;
A92:   x in dom c2 & y=c2.x by A90,A91,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A93:   1<=k & k<=len c2 by A92,FINSEQ_3:27;
A94:   x in dom SR by A64,A65,A92,FINSEQ_3:31;
A95:   m1+k=SR.k by A21,A22,A24,A93,GRAPH_2:5;
A96:   SR.k in rng SR by A94,FUNCT_1:def 5;
then A97:   x in dom (fc*SR) by A69,A94,FUNCT_1:21;
0+1<=k by A92,FINSEQ_3:27;
      then consider i being Element of NAT such that
A98:   0<=i & i<len c2 & k=i+1 by A93,GRAPH_2:1;
         (fc*SR).x = fc.(m1+k) by A95,A97,FUNCT_1:22 .= c.(m+i) by  A69,A95
,A96,GRFUNC_1:8,A98
        .= y by A14,A92,A98,GRAPH_2:def 1;
      hence p in fc*(Sgm DR) by A91,A97,FUNCT_1:8;
     end; assume
A99:  p in fc*(Sgm DR); then consider x,y being set such that
A100:  p=[x,y] by RELAT_1:def 1;
A101:   x in dom (fc*SR) & y = (fc*SR).x by A99,A100,FUNCT_1:8;
then A102:   x in dom SR & SR.x in dom fc by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A103:   k in dom c2 by A64,A65,A102,FINSEQ_3:31;
A104:   1<=k & k<=len c2 by A64,A65,A102,FINSEQ_3:27;
then A105:   m1+k=SR.k by A21,A22,A24,GRAPH_2:5;  0+1<=k by A102,FINSEQ_3:27;
     then consider i being Element of NAT such that
A106:   0<=i & i<len c2 & k=i+1 by A104,GRAPH_2:1;
        c2.k = c.(m1+1+i) by A14,A106,GRAPH_2:def 1
         .= fc.(SR.k) by  A106,A102,A105,GRFUNC_1:8 .= y by
A101,A102,FUNCT_1:23;
     hence p in c2 by A100,A103,FUNCT_1:8;
    end;
then A107: c2 = fc*(Sgm DR) by TARSKI:2;
    Seq fc= fc*((Sgm DL)^(Sgm DR))by A30,A57,A63,FINSEQ_1:def 14;
  hence Seq fc = c' by A68,A69,A89,A107,GRAPH_2:26;
     set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n};
     set DR = {kk where kk is Element of NAT: m+1<=kk & kk<= len vs};
        now let x be set;
      hereby assume x in domfvs; then consider k being Element of NAT such that
A108:     x=k & (1<=k & k<=n or m+1<=k & k<= len vs);
           x in DL or x in DR by A108;
       hence x in DL \/ DR by XBOOLE_0:def 2;
      end; assume A109: x in DL \/ DR;
      per cases by A109,XBOOLE_0:def 2;
      suppose x in DL; then consider k being Element of NAT such that
A110:    x=k & 1<=k & k<=n;
      thus x in domfvs by A110;
      end;
      suppose x in DR; then consider k being Element of NAT such that
A111:    x=k & m+1<=k & k<=len vs;
      thus x in domfvs by A111;
     end;
     end;
then A112:  domfvs = DL \/ DR by TARSKI:2;
A113:  DL c= Seg len vs & DR c= Seg len vs proof
      hereby let x be set; assume x in DL;
       then consider k being Element of NAT such that
A114:    x=k & 1<=k & k<=n;  k<=len vs by A15,A114,XXREAL_0:2;
       hence x in Seg len vs by A114,FINSEQ_1:3;
      end; let x be set; assume x in DR;
       then consider k being Element of NAT such that
A115:    x=k & m+1<=k & k<=len vs;
          1<=m+1 by NAT_1:12; then 1<=k by A115,XXREAL_0:2;
       hence x in Seg len vs by A115,FINSEQ_1:3;
     end;
     then reconsider DL as finite set by FINSET_1:13;
     reconsider DR as finite set by A113,FINSET_1:13;
    now let i,j; assume i in DL; then consider k being Element of NAT such that
A116:    k=i & 1<=k & k<=n;
      assume j in DR; then consider l being Element of NAT such that
A117:    l=j & m+1<=l & l<=len vs;  m<=m+1 by NAT_1:12;
then A118:   m<=l by A117,XXREAL_0:2;  k<m by A2,A116,XXREAL_0:2;
      hence i < j by A116,A117,A118,XXREAL_0:2;
     end;
then A119: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A113,FINSEQ_3:48;
        1<=len p2 by A20,NAT_1:12;
     then 1-1<=len p2 -1 by XREAL_1:11;
     then len p2 -'1 = len p2 -1 by BINARITH:def 3;
     then reconsider lp21 = len p2 -1 as Element of NAT;
      lp21 -'1 = lp21 -1 by A20,A22,A23,BINARITH:def 3;
     then reconsider lp22 = lp21 -1 as Element of NAT;
A120:  m+1+lp22 = m+((lp21 -1)+1)
            .= m+(len c -m +1 +1 -1) by A18,A22,Def5
            .= len c +1;
then A121:  card DR = lp22 + 1 by A3,GRAPH_2:4
        .= lp21;
A122:  1<=m+1 & m+1<=len c +1+1 by A16,XREAL_1:8,NAT_1:12;
         len p2 +m = len c +1+1 by A16,GRAPH_2:def 1
     .= len p2' +(m+1) by A16,A122,Lm2
     .= (len p2'+1)+m;
     then A123: len p2 -1 = len p2'+1-1;
A124:  len Sgm DL = card DL & len Sgm DR = card DR by A113,FINSEQ_3:44;
A125:  len pp = n by A6,A19,A48;  DL = Seg n by FINSEQ_1:def 1;
then A126:  Sgm DL = idseq n by FINSEQ_3:54;
then A127:  dom Sgm DL = Seg n by RELAT_1:71;
       rng Sgm DL = DL by A113,FINSEQ_1:def 13;
then A128: rng Sgm DL c= dom fvs by A38,A112,XBOOLE_1:7;
       rng Sgm DR = DR by A113,FINSEQ_1:def 13;
then A129: rng Sgm DR c= dom fvs by A38,A112,XBOOLE_1:7;
    set SL = Sgm DL; set SR = Sgm DR;
       now let p be set;
     hereby assume
A130:   p in pp; then consider x, y being set such that
A131:  p=[x,y] by RELAT_1:def 1;
A132:   x in dom pp & y=pp.x by A130,A131,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A133:   1<=k & k<=len pp by A132,FINSEQ_3:27;
then A134:   x in dom SL by A125,A127,FINSEQ_1:3;
then A135:   k=SL.k by A126,A127,FUNCT_1:35;
A136:   k in domfvs by A125,A133;
then A137:   x in dom (fvs*SL) by A38,A134,A135,FUNCT_1:21;
         0+1<=k by A132,FINSEQ_3:27;
      then consider i being Element of NAT such that
A138:   0<=i & i<n & k=i+1 by A125,A133,GRAPH_2:1;
         (fvs*SL).x = fvs.k by A135,A137,FUNCT_1:22 .= vs.(1+i) by A38,A136
,A138,GRFUNC_1:8 .= y by A15,A125,A132,A138,GRAPH_2:def 1;
      hence p in fvs*(Sgm DL) by A131,A137,FUNCT_1:8;
     end; assume
A139:  p in fvs*(Sgm DL); then consider x,y being set such that
A140:  p=[x,y] by RELAT_1:def 1;
A141:   x in dom (fvs*SL) & y = (fvs*SL).x by A139,A140,FUNCT_1:8;
then A142:  (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A143:   x in dom SL & SL.x in dom fvs by A141,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n}
       by A127,FINSEQ_1:def 1;
      then consider k such that
A144:   k=x & 1<=k & k<=n;
A145:   k in dom fvs by A38,A144;
A146:   k=SL.k by A126,A127,A143,A144,FUNCT_1:35;
A147: k in dom pp by A125,A144,FINSEQ_3:27;  0+1<=k by A144;
     then consider i being Element of NAT such that
A148:  0<=i & i<n & k=i+1 by A144,GRAPH_2:1;
        pp.k = vs.k by A15,A125,A148,GRAPH_2:def 1
         .= y by A141,A142,A144,A145,A146,GRFUNC_1:8;
     hence p in pp by A140,A144,A147,FUNCT_1:8;
    end;
then A149: pp = fvs*(Sgm DL) by TARSKI:2;
A150:  m+1+lp22 = m+lp21;
A151: 1<=m+1 & m+1<=len c +1+1 by A2,A3,NAT_1:12,XREAL_1:9;
       now let p be set;
     hereby assume
A152:   p in p2'; then consider x, y being set such that
A153:  p=[x,y] by RELAT_1:def 1;
A154:   x in dom p2' & y=p2'.x by A152,A153,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A155:   1<=k & k<=len p2' by A154,FINSEQ_3:27;
A156:   x in dom SR by A121,A123,A124,A154,FINSEQ_3:31;
A157:   m+k=SR.k by A3,A120,A123,A150,A155,GRAPH_2:5;
      A158: SR.k in rng SR by A156,FUNCT_1:def 5;
then A159:   x in dom (fvs*SR) by A129,A156,FUNCT_1:21;  0+1<=k by A154,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A160:   0<=i & i<len p2' & k=i+1 by A155,GRAPH_2:1;
         (fvs*SR).x = fvs.(m+k) by A157,A159,FUNCT_1:22 .= vs.(m+1+i) by  A129,
A157,A158,GRFUNC_1:8,A160 .= y by A3,A151,A154,A160,Lm2;
      hence p in fvs*(Sgm DR) by A153,A159,FUNCT_1:8;
     end; assume
A161:  p in fvs*(Sgm DR); then consider x,y being set such that
A162:  p=[x,y] by RELAT_1:def 1;
A163:   x in dom (fvs*SR) & y = (fvs*SR).x by A161,A162,FUNCT_1:8;
then A164:   x in dom SR & SR.x in dom fvs by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A165:   k in dom p2' by A121,A123,A124,A164,FINSEQ_3:31;
A166:   1<=k & k<=len p2' by A121,A123,A124,A164,FINSEQ_3:27;
then A167:   m+k=SR.k by A3,A120,A123,A150,GRAPH_2:5;  0+1<=k by A164,
FINSEQ_3:27;
     then consider i being Element of NAT such that
A168:   0<=i & i<len p2' & k=i+1 by A166,GRAPH_2:1;
        p2'.k = vs.(m+1+i) by A3,A151,A168,Lm2
         .= fvs.(SR.k) by  A168,A164,A167,GRFUNC_1:8 .= y by
A163,A164,FUNCT_1:23;
     hence p in p2' by A162,A165,FUNCT_1:8;
    end;
then A169: p2' = fvs*(Sgm DR) by TARSKI:2;
    Seq fvs = fvs*((Sgm DL)^(Sgm DR)) by A38,A112,A119,FINSEQ_1:def 14;
  hence Seq fvs = p1 by A47,A128,A129,A149,A169,GRAPH_2:26;
 end;
 suppose
A170: n=1 & m <> len vs;
     then A171: m < len vs by A2,REAL_1:def 5;
then A172:  m<=len c by A3,NAT_1:13;
A173:  1 < m by A2,XXREAL_0:2;
A174: 1<=m & m<=len c & len c <=len c by A2,A3,A171,XXREAL_0:2,NAT_1:13;
   reconsider c2 = (m,len c)-cut c as oriented Chain of G by A172,A173,Th13;
   set p2 = (m,len c+1)-cut vs;
A175: p2 is_oriented_vertex_seq_of c2 by A1,A172,A173,Th14;
  set domfc = {k: m<=k & k<= len c};
  deffunc F(set) = c.$1;
  consider fc being Function such that
A176: dom fc = domfc and
A177: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A178:    x = kk & m<=kk & kk<= len c;
          1<=kk & kk<=len c by A173,A178,XXREAL_0:2;
      hence x in Seg len c by A178,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A176,FINSEQ_1:def 12;
 fc c= c proof let p be set; assume
A179:  p in fc; then consider x, y being set such that
A180:  [x,y] = p by RELAT_1:def 1;
A181:  x in dom fc & y = fc.x by A179,A180,FUNCT_1:8;
     then consider kk being Element of NAT such that
A182:  x = kk & m<=kk & kk<= len c by A176;
        1<=kk & kk<=len c by A173,A182,XXREAL_0:2;
then A183:  x in dom c by A182,FINSEQ_3:27;
        y = c.kk by A176,A177,A181,A182;
     hence p in c by A180,A182,A183,FUNCT_1:8;
  end;
  then reconsider fc as Subset of c;
  take fc;
  set domfvs = {k: m<=k & k<= len vs};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A184: dom fvs = domfvs and
A185: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A186:    x = kk & m<=kk & kk<= len vs;
          1<=kk & kk<=len vs by A173,A186,XXREAL_0:2;
      hence x in Seg len vs by A186,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A184,FINSEQ_1:def 12;
 fvs c= vs proof let p be set; assume
A187:  p in fvs; then consider x, y such that
A188:  [x,y] = p by RELAT_1:def 1;
A189:  x in dom fvs & y = fvs.x by A187,A188,FUNCT_1:8;
     then consider kk being Element of NAT such that
A190:  x = kk & m<=kk & kk<= len vs by A184;
        1<=kk & kk<=len vs by A173,A190,XXREAL_0:2;
then A191:  x in dom vs by A190,FINSEQ_3:27;
        y = vs.kk by A184,A185,A189,A190;
     hence p in vs by A188,A190,A191,FUNCT_1:8;
  end;
  then reconsider fvs as Subset of vs;
  take fvs; take c1 = c2; take p1 = p2;
        len c2 +m = len c +1 by A2,A3,A170,Lm2;
then A192: len c2 =len c - m + 1;
       A193: m-m<=len c -m by A172,XREAL_1:11;
        1-1<=m-1 by A173,XREAL_1:11;
     then m-'1 = m-1 by BINARITH:def 3;
      then reconsider m1 = m-1 as Element of NAT;
A194:  m = m1 +1;
      len c2 -'1 = len c2 -1 by A192,A193,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
A195:  m+lc21 = m1+len c2;
A196: 1<=m & m<=len c +1+1 & len c +1<=len vs by A2,A3,A170,NAT_1:12;
     then len p2 +m = len c +1 +1 by Lm2;
then A197: len p2 = len c -m +1+1;
then A198: 1<=len p2 by A192,NAT_1:12;
A199: len c2 = len c +(-m+1) by A192;
        1-1 < m-1 by A173,XREAL_1:11
;
     then 0 < -(-(m-1)); then -(m-1) < 0 by XREAL_1:60;
  hence
A200: len c1 < len c by A199,REAL_2:173;
  thus p1 is_oriented_vertex_seq_of c1 by A1,A174,Th14;
     len p1 = len c1 + 1 by A175,Def5;
  hence len p1 < len vs by A3,A200,XREAL_1:8;
  thus vs.1 = p1.1 by A2,A3,A170,GRAPH_2:12;
   thus vs.len vs = p1.len p1 by A2,A3,A170,GRAPH_2:12;
A201:  domfc c= Seg len c proof
       let x be set; assume x in domfc;
        then consider k being Element of NAT such that
A202:    x=k & m<=k & k<=len c;  1<=k by A173,A202,XXREAL_0:2;
       hence x in Seg len c by A202,FINSEQ_1:3;
     end;
     then reconsider domfc as finite set by FINSET_1:13;
      len c2 -'1 = len c2 -1 by A192,A193,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
  m+lc21 = len c by A192;
then A203:  card domfc = len c2 + -1 +1 by GRAPH_2:4
        .= len c2;
A204: len Sgm domfc = card domfc by A201,FINSEQ_3:44;
    A205: rng Sgm domfc c= dom fc by A176,A201,FINSEQ_1:def 13;
    set SR = Sgm domfc;
       now let p be set;
     hereby assume
A206:   p in c2; then consider x, y being set such that
A207:  p=[x,y] by RELAT_1:def 1;
A208:   x in dom c2 & y=c2.x by A206,A207,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A209:   1<=k & k<=len c2 by A208,FINSEQ_3:27;
A210:   x in dom SR by A203,A204,A208,FINSEQ_3:31;
A211:   m1+k=SR.k by A192,A194,A195,A209,GRAPH_2:5;
      A212: SR.k in rng SR by A210,FUNCT_1:def 5;
then A213:   x in dom (fc*SR) by A205,A210,FUNCT_1:21;  0+1<=k by A208,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A214:   0<=i & i<len c2 & k=i+1 by A209,GRAPH_2:1;
         (fc*SR).x = fc.(m1+k) by A211,A213,FUNCT_1:22 .= c.(m+i) by  A205
,A211,A212,GRFUNC_1:8,A214
        .= y by A2,A3,A170,A208,A214,Lm2;
      hence p in fc*(Sgm domfc) by A207,A213,FUNCT_1:8;
     end; assume
A215:  p in fc*(Sgm domfc); then consider x,y being set such that
A216:  p=[x,y] by RELAT_1:def 1;
A217:   x in dom (fc*SR) & y = (fc*SR).x by A215,A216,FUNCT_1:8;
then A218:   x in dom SR & SR.x in dom fc by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A219:   k in dom c2 by A203,A204,A218,FINSEQ_3:31;
A220:   1<=k & k<=len c2 by A203,A204,A218,FINSEQ_3:27;
then A221:   m1+k=SR.k by A192,A194,A195,GRAPH_2:5;  0+1<=k by A218,FINSEQ_3:27
;
     then consider i being Element of NAT such that
A222:   0<=i & i<len c2 & k=i+1 by A220,GRAPH_2:1;
        c2.k = c.(m1+1+i) by A2,A3,A170,A222,Lm2
         .= fc.(SR.k) by  A222,A218,A221,GRFUNC_1:8 .= y by A217,A218,FUNCT_1:
23
;
     hence p in c2 by A216,A219,FUNCT_1:8;
    end;
 then c2 = fc*(Sgm domfc) by TARSKI:2;
 hence Seq fc = c1 by A176,FINSEQ_1:def 14;
A223:  domfvs c= Seg len vs proof
       let x be set; assume x in domfvs;
        then consider k being Element of NAT such that
A224:    x=k & m<=k & k<=len vs;  1<=k by A173,A224,XXREAL_0:2;
       hence x in Seg len vs by A224,FINSEQ_1:3;
     end;
     then reconsider domfvs as finite set by FINSET_1:13;
        1-1<=len p2 -1 by A198,XREAL_1:11;
     then len p2 -'1 = len p2 -1 by BINARITH:def 3;
     then reconsider lp21 = len p2 -1 as Element of NAT;
A225: m+lp21 = len c+1 by A197;
A226:  m+lp21 = m1+len p2;
A227:  card domfvs = len p2 + -1 +1 by A3,A225,GRAPH_2:4
        .= len p2;
A228: len Sgm domfvs = card domfvs by A223,FINSEQ_3:44;
    A229: rng Sgm domfvs c= dom fvs by A184,A223,FINSEQ_1:def 13;
    set SR = Sgm domfvs;
       now let p be set;
     hereby assume
A230:   p in p2; then consider x, y being set such that
A231:  p=[x,y] by RELAT_1:def 1;
A232:   x in dom p2 & y=p2.x by A230,A231,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A233:   1<=k & k<=len p2 by A232,FINSEQ_3:27;
A234:   x in dom SR by A227,A228,A232,FINSEQ_3:31;
A235:   m1+k=SR.k by A3,A194,A197,A226,A233,GRAPH_2:5; A236: SR.k in rng SR by
A234,FUNCT_1:def 5;
then A237:   x in dom (fvs*SR) by A229,A234,FUNCT_1:21;  0+1<=k by A232,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A238:   0<=i & i<len p2 & k=i+1 by A233,GRAPH_2:1;
         (fvs*SR).x = fvs.(m1+k) by A235,A237,FUNCT_1:22 .= vs.(m+i) by  A229,
A235,A236,GRFUNC_1:8,A238
        .= y by A196,A232,A238,Lm2;
      hence p in fvs*(Sgm domfvs) by A231,A237,FUNCT_1:8;
     end; assume
A239:  p in fvs*(Sgm domfvs); then consider x,y being set such that
A240:  p=[x,y] by RELAT_1:def 1;
A241:   x in dom (fvs*SR) & y = (fvs*SR).x by A239,A240,FUNCT_1:8;
then A242:   x in dom SR & SR.x in dom fvs by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A243:   k in dom p2 by A227,A228,A242,FINSEQ_3:31;
A244:   1<=k & k<=len p2 by A227,A228,A242,FINSEQ_3:27;
then A245:   m1+k=SR.k by A3,A194,A197,A226,GRAPH_2:5;  0+1<=k by A242,
FINSEQ_3:27;
     then consider i being Element of NAT such that
A246:   0<=i & i<len p2 & k=i+1 by A244,GRAPH_2:1;
        p2.k = vs.(m1+1+i) by A196,A246,Lm2
        .= fvs.(SR.k) by  A246,A242,A245,GRFUNC_1:8 .= y
by A241,A242,FUNCT_1:23
;
     hence p in p2 by A240,A243,FUNCT_1:8;
    end;
 then p2 = fvs*(Sgm domfvs) by TARSKI:2;
 hence Seq fvs = p1 by A184,FINSEQ_1:def 14;
 end;
 suppose
A247: n<>1 & m = len vs;
    then 1 < n by A2,REAL_1:def 5;
    then 1 + 1<=n by NAT_1:13; then A248: 1+1-1<=n-1 by XREAL_1:11;
        n < len vs by A2,XXREAL_0:2; then A249: n-1 < len c +1-1 by A3,
XREAL_1:11
;
A250: 1<=n1+1 by NAT_1:12;
   reconsider c1 = (1,n1)-cut c as oriented Chain of G by A6,A248,A249,Th13;
   set pp = (1,n)-cut vs; A251: 1<=n & n<=len vs by A2,XXREAL_0:2;
A252: pp is_oriented_vertex_seq_of c1 by A1,A7,A248,A249,Th14;
then A253: len pp = len c1 + 1 by Def5;
  set domfc = {k: 1<=k & k<=n1};
  deffunc F(set) = c.$1;
  consider fc being Function such that
A254: dom fc = domfc and
A255: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A256:    x = kk & 1<=kk & kk<=n1;  1<=kk & kk<=len c by A6,A249,A256,XXREAL_0:2
;
      hence x in Seg len c by A256,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A254,FINSEQ_1:def 12;
 fc c= c proof let p be set; assume
A257:  p in fc; then consider x, y being set such that
A258:  [x,y] = p by RELAT_1:def 1;
A259:  x in dom fc & y = fc.x by A257,A258,FUNCT_1:8;
     then consider kk being Element of NAT such that
A260:  x = kk & 1<=kk & kk<=n1 by A254;
        1<=kk & kk<=len c by A6,A249,A260,XXREAL_0:2;
then A261:  x in dom c by A260,FINSEQ_3:27;  y = c.kk by A254,A255,A259,A260;
hence p in c by A258,A260,A261,FUNCT_1:8;
  end;
  then reconsider fc as Subset of c;
  take fc;
  set domfvs = { k : 1 <= k & k <= n};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A262: dom fvs = domfvs and
A263: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A264:    x = kk & 1<=kk & kk<=n;  1<=kk & kk<=len vs by A251,A264,XXREAL_0:2;
      hence x in Seg len vs by A264,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A262,FINSEQ_1:def 12;
 fvs c= vs proof let p be set; assume
A265:  p in fvs; then consider x, y being set such that
A266:  [x,y] = p by RELAT_1:def 1;
A267:  x in dom fvs & y = fvs.x by A265,A266,FUNCT_1:8;
     then consider kk being Element of NAT such that
A268:  x = kk & 1<=kk & kk<=n by A262;
        1<=kk & kk<=len vs by A251,A268,XXREAL_0:2;
then A269:  x in dom vs by A268,FINSEQ_3:27;  y = vs.kk by A262,A263,A267,A268
;
hence p in vs by A266,A268,A269,FUNCT_1:8;
  end;
  then reconsider fvs as Subset of vs;
  take fvs; take c' = c1; take p1 = pp;
        len c1+1=n1+1 by A6,A249,A250,Lm2;
then A270: len c1 = n - 1 by A5,BINARITH:def 3;
  hence
   len c' < len c by A249;
  thus p1 is_oriented_vertex_seq_of c' by A1,A7,A248,A249,Th14;  len p1 = len
c1 +
1 by A252,Def5;
  hence len p1 < len vs by A2,A270,XXREAL_0:2;
  thus vs.1 = p1.1 by A251,GRAPH_2:12;
  thus vs.len vs = p1.len p1 by A2,A247,GRAPH_2:12;
    domfc c= Seg len c proof
      let x be set; assume x in domfc;
       then consider k being Element of NAT such that
A271:    x=k & 1<=k & k<=n1;  k<=len c by A6,A249,A271,XXREAL_0:2;
       hence x in Seg len c by A271,FINSEQ_1:3;
     end;
     then reconsider domfc as finite set by FINSET_1:13;  domfc = Seg n1
by FINSEQ_1:def 1;
then A272:  Sgm domfc = idseq n1 by FINSEQ_3:54;
then A273:  dom Sgm domfc = Seg n1 by RELAT_1:71;
    set SL = Sgm domfc;
       now let p be set;
     hereby assume
A274:   p in c1; then consider x, y being set such that
A275:  p=[x,y] by RELAT_1:def 1;
A276:   x in dom c1 & y=c1.x by A274,A275,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A277:   1<=k & k<=len c1 by A276,FINSEQ_3:27;
then A278:   x in dom SL by A6,A270,A273,FINSEQ_1:3;
then A279:   k=SL.k by A272,A273,FUNCT_1:35;
A280:   k in domfc by A6,A270,A277;
then A281:   x in dom (fc*SL)
by A254,A278,A279,FUNCT_1:21;  0+1<=k by A276,FINSEQ_3:27;
      then consider i being Element of NAT such that
A282:   0<=i & i<n1 & k=i+1 by A6,A270,A277,GRAPH_2:1;
         (fc*SL).x = fc.k by A279,A281,FUNCT_1:22 .= c.(1+i) by A254,A280
,A282,GRFUNC_1:8
        .= y by A6,A249,A250,A270,A276,A282,Lm2;
      hence p in fc*(Sgm domfc) by A275,A281,FUNCT_1:8;
     end; assume
A283:  p in fc*(Sgm domfc); then consider x,y being set such that
A284:  p=[x,y] by RELAT_1:def 1;
A285:   x in dom (fc*SL) & y = (fc*SL).x by A283,A284,FUNCT_1:8;
then A286:  (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A287:   x in dom SL & SL.x in dom fc by A285,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n1}
       by A273,FINSEQ_1:def 1;
      then consider k such that
A288:   k=x & 1<=k & k<=n1;
A289:   k in dom fc by A254,A288;
A290:   k=SL.k by A272,A273,A287,A288,FUNCT_1:35;
A291: k in dom c1 by A6,A270,A288,FINSEQ_3:27;  0+1<=k by A288;
     then consider i being Element of NAT such that
A292: 0<=i & i<n1 & k=i+1 by A288,GRAPH_2:1;
       c1.k = c.k by A6,A249,A250,A270,A292,Lm2
         .= y by A285,A286,A288,A289,A290,GRFUNC_1:8;
     hence p in c1 by A284,A288,A291,FUNCT_1:8;
    end; then c1 = fc*(Sgm domfc) by TARSKI:2;
  hence Seq fc = c' by A254,FINSEQ_1:def 14;
    domfvs c= Seg len vs proof
      let x be set; assume x in domfvs;
       then consider k being Element of NAT such that
A293:    x=k & 1<=k & k<=n;  k<=len vs by A251,A293,XXREAL_0:2;
       hence x in Seg len vs by A293,FINSEQ_1:3;
     end; then reconsider domfvs as finite set by FINSET_1:13;
        domfvs = Seg n by FINSEQ_1:def 1;
then A294:  Sgm domfvs = idseq n by FINSEQ_3:54;
then A295:  dom Sgm domfvs = Seg n by RELAT_1:71;
    set SL = Sgm domfvs;
       now let p be set;
     hereby assume
A296:   p in pp; then consider x, y being set such that
A297:  p=[x,y] by RELAT_1:def 1;
A298:   x in dom pp & y=pp.x by A296,A297,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A299:   1<=k & k<=len pp by A298,FINSEQ_3:27;
then A300:   x in dom SL by A253,A270,A295,FINSEQ_1:3;
then A301:   k=SL.k by A294,A295,FUNCT_1:35;
A302:   k in domfvs by A253,A270,A299;
then A303:   x in dom (fvs*SL) by A262,A300,A301,FUNCT_1:21;  0+1<=
k by A298,FINSEQ_3:27;
      then consider i being Element of NAT such that
A304:   0<=i & i<n & k=i+1 by A253,A270,A299,GRAPH_2:1;
         (fvs*SL).x = fvs.k by A301,A303,FUNCT_1:22 .= vs.(1+i) by A262,A302,
A304,GRFUNC_1:8
        .= y by A251,A253,A270,A298,A304,GRAPH_2:def 1;
      hence p in fvs*(Sgm domfvs) by A297,A303,FUNCT_1:8;
     end; assume
A305:  p in fvs*(Sgm domfvs); then consider x,y being set such that
A306:  p=[x,y] by RELAT_1:def 1;
A307:   x in dom (fvs*SL) & y = (fvs*SL).x by A305,A306,FUNCT_1:8;
then A308:  (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A309:   x in dom SL & SL.x in dom fvs by A307,FUNCT_1:21;
      then x in { kk where kk is Element of NAT: 1<=kk & kk<=n}
       by A295,FINSEQ_1:def 1;
      then consider k such that
A310:   k=x & 1<=k & k<=n;
A311:   k in dom fvs by A262,A310;
A312:   k=SL.k by A294,A295,A309,A310,FUNCT_1:35;
A313: k in dom pp by A253,A270,A310,FINSEQ_3:27;  0+1<=k by A310;
     then consider i being Element of NAT such that
A314:  0<=i & i<n & k=i+1 by A310,GRAPH_2:1;
        pp.k = vs.k by A251,A253,A270,A314,GRAPH_2:def 1
         .= y by A307,A308,A310,A311,A312,GRFUNC_1:8;
     hence p in pp by A306,A310,A313,FUNCT_1:8;
    end;
   then pp = fvs*(Sgm domfvs) by TARSKI:2;
  hence Seq fvs = p1 by A262,FINSEQ_1:def 14;
end;
end;

theorem  vs is_oriented_vertex_seq_of c implies
 ex fc being Subset of c, fvs being Subset of vs, sc, vs1
  st Seq fc = sc & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of sc &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 proof assume
A1: vs is_oriented_vertex_seq_of c;
 per cases;
 suppose A2: c is Simple;
  reconsider fc = c as Subset of c by GRAPH_2:28;
  reconsider fvs = vs as Subset of vs by GRAPH_2:28;
  reconsider sc = c as oriented simple Chain of G by A2,Th21;
  take fc, fvs, sc, vs;
  thus Seq fc = sc & Seq fvs = vs by FINSEQ_3:125;
  thus vs is_oriented_vertex_seq_of sc by A1;
  thus vs.1 = vs.1 & vs.len vs = vs.len vs;
 end;
 suppose A3: not c is Simple;
defpred P[Element of NAT] means
 ex fc being Subset of c, fvs being Subset of vs, c1, vs1
  st Seq fc = c1 & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of c1 &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 &
     len c1 = $1 & not c1 is Simple;
     P[len c] proof
   reconsider fc = c as Subset of c by GRAPH_2:28;
   reconsider fvs = vs as Subset of vs by GRAPH_2:28;
   take fc, fvs, c,vs;
   thus Seq fc = c & Seq fvs = vs by FINSEQ_3:125;
   thus vs is_oriented_vertex_seq_of c by A1;
   thus vs.1 = vs.1 & vs.len vs = vs.len vs;
   thus len c = len c & not c is Simple
                  by A3;
  end;
then A4: ex k st P[k]; consider k such that
A5:  P[k] & for n being Element of NAT st P[n] holds k<=n from NAT_1:sch 5(A4);
  consider fc being Subset of c, fvs being Subset of vs,
           c1, vs1 such that
A6:   Seq fc = c1 & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of c1 &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 &
     len c1 = k & not c1 is Simple
     by A5;
  consider fc1 being Subset of c1, fvs1 being Subset of vs1,
           c2, vs2 such that
A7: len c2 < len c1 & vs2 is_oriented_vertex_seq_of c2 & len vs2 < len vs1 &
     vs1.1 = vs2.1 & vs1.len vs1 = vs2.len vs2 &
     Seq fc1 = c2 & Seq fvs1 = vs2 by A6,Th22;
   reconsider fc'=fc|rng((Sgm dom fc)|dom fc1) as Subset of c
                           by GRAPH_2:29;
A8:    Seq fc' = c2 by A6,A7,GRAPH_2:30;
   reconsider fvs'=fvs|rng((Sgm dom fvs)|dom fvs1) as Subset of vs
                                                                by GRAPH_2:29;
A9:    Seq fvs' = vs2 by A6,A7,GRAPH_2:30;
       now assume c2 is Simple (oriented Chain of G);
      then reconsider sc = c2 as oriented simple Chain of G by Th21;
     take fc',sc;  Seq fc' = sc by A6,A7,GRAPH_2:30;
     hence thesis by A6,A7,A9;
    end;
 hence thesis by A5,A6,A7,A8,A9;
end;
end;

registration let G;
 cluster empty -> one-to-one (oriented Chain of G);
 correctness;
end;

theorem  p is OrientedPath of G implies p|Seg(n) is OrientedPath of G
proof assume
A1: p is OrientedPath of G;
then reconsider p'=p|(Seg n) as oriented Chain of G by Th18;
 reconsider q=p|(Seg n) as FinSequence by FINSEQ_1:19;
    now let n1,m1 be Element of NAT; assume
A2: 1<=n1 & n1 < m1 & m1<=len q; then 1<m1 by XXREAL_0:2;
    then m1 in dom q by A2,FINSEQ_3:27;
then A3: q.m1=p.m1 by FUNCT_1:68;  n1 < len q by A2,XXREAL_0:2;
    then n1 in dom q by A2,FINSEQ_3:27;
then A4: q.n1=p.n1 by FUNCT_1:68;
       dom q c= dom p by RELAT_1:89;
       then dom q c= Seg len p by FINSEQ_1:def 3
;
    then Seg len q c= Seg len p by FINSEQ_1:def 3;
    then len q<=len p by FINSEQ_1:7;
    then m1<=len p by A2,XXREAL_0:2;
   hence q.n1 <> q.m1 by A1,A2,A3,A4,GRAPH_1:def 13;
  end;
  then p' is Path of G by GRAPH_1:def 13;
  hence thesis;
end;

theorem Th25: sc is OrientedPath of G
proof
assume not sc is OrientedPath of G;
then consider m, n such that
A1: 1<=m & m<n & n<=len sc & sc.m = sc.n by GRAPH_1:def 13;
    sc is Simple by Th20;
 then consider vs such that
A2: vs is_oriented_vertex_seq_of sc &
   (for n, m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs)
                                                           by Def7;
A3: len vs = len sc + 1 by A2,Def5; A4: m<=len sc by A1,XXREAL_0:2;
A5: 1<=n by A1,XXREAL_0:2;
A6: 1<=m & m<n & n<=len vs by A1,A3,NAT_1:12;
A7: 1<=m+1 & m+1<n+1 & n+1<=len vs by A1,A3,XREAL_1:8,NAT_1:12;
     then A8: 1<=m & m<n+1 & n+1<=len vs by A1,NAT_1:12;
 set v1 = vs/.m; set v2 = vs/.(m+1);
A9: sc.m orientedly_joins v1, v2 by A1,A2,A4,Def5;
 set v1' = vs/.n; set v2' = vs/.(n+1);
    sc.n orientedly_joins v1', v2' by A1,A2,A5,Def5;
then A10: v1=v1' & v2=v2' by A1,A9,Th2;
    m<=len vs & m+1<=len vs & 1<=n+1 by A7,A8,XXREAL_0:2;
then A11: v1=vs.m & v2=vs.(m+1) & v1'=vs.n & v2'=vs.(n+1)
                                by A5,A6,A7,FINSEQ_4:24;
   then m=1 & n=len vs by A2,A6,A10;
   hence contradiction by A2,A7,A10,A11;
end;

theorem  for c1 being FinSequence holds
  (c1 is Simple (oriented Chain of G) iff c1 is oriented simple Chain of G)
  &(c1 is oriented simple Chain of G implies c1 is OrientedPath of G)
   by Th20,Th21,Th25;
