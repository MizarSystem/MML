:: Oriented Simple Chains Included in Oriented Chains
::  by Yatsuka Nakamura and Piotr Rudnicki
::
:: Received August 19, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies FINSEQ_1, GRAPH_1, ARYTM_1, FUNCT_1, ORDERS_1, BOOLE, GRAPH_2,
      RELAT_1, FINSET_1, CARD_1, FINSEQ_2, GRAPH_4, FINSEQ_4, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, FINSEQ_1, FINSEQ_2,
      FINSEQ_4, CARD_1, FINSET_1, NUMBERS, XCMPLX_0, NAT_1, NAT_D, GRAPH_1,
      GRAPH_2, BINARITH, XXREAL_0;
 constructors PARTFUN1, XXREAL_0, XREAL_0, REAL_1, NAT_1, RECDEF_1, FINSEQ_4,
      BINARITH, GRAPH_2, NAT_D;
 registrations FUNCT_1, FINSEQ_1, GRAPH_1, GRAPH_2, RELSET_1, XREAL_0, CARD_1,
      ORDINAL1, XXREAL_0;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FINSEQ_2;
 theorems TARSKI, NAT_1, REAL_1, FINSEQ_1, FINSEQ_2, GRAPH_1, RELAT_1, FUNCT_1,
      GRFUNC_1, FUNCT_2, BINARITH, REAL_2, FINSET_1, FINSEQ_3, FINSEQ_4,
      GRAPH_2, XBOOLE_0, XBOOLE_1, XREAL_1, CARD_1, XXREAL_0;
 schemes NAT_1, FINSEQ_1, FUNCT_1;

begin   ::Oriented vertex sequences and V-sets

reserve
        p, q for FinSequence,
        y1,y2,e,X for set,
        i, j, k, m, n for Element of NAT,
        G for Graph;
reserve x,y,v,v1,v2,v3,v4 for Element of the Vertices of G;

definition let G; let x, y; let e;
   pred e orientedly_joins x, y means
:Def1:  (the Source of G).e = x & (the Target of G).e = y;
end;

theorem Th1:  e orientedly_joins v1, v2 implies e joins v1, v2
 proof assume e orientedly_joins v1, v2;
    then ((the Source of G).e = v1 & (the Target of G).e = v2) by Def1;
  hence e joins v1, v2 by GRAPH_1:def 9;
 end;

definition let G; let x,y be Element of (the Vertices of G);
   pred x,y are_orientedly_incident means
        ex v being set st v in the Edges of G & v orientedly_joins x, y;
end;

theorem Th2:
e orientedly_joins v1,v2 & e orientedly_joins v3,v4 implies v1=v3 & v2=v4
 proof
  assume e orientedly_joins v1,v2 & e orientedly_joins v3,v4;
       then ((the Source of G).e = v1 & (the Target of G).e = v2)
       &
       ((the Source of G).e = v3 & (the Target of G).e = v4)
        by Def1;
  hence v1=v3 & v2=v4;
end;

reserve vs, vs1, vs2 for FinSequence of the Vertices of G,
        c, c1, c2 for oriented Chain of G;

registration let G;
 cluster empty oriented Chain of G;
 existence proof {} is oriented Chain of G by GRAPH_1:14; hence thesis; end;
end;

definition let G, X;
 func G-SVSet X -> set equals
  { v: ex e being Element of the Edges of G st e in X &
                v = (the Source of G).e };
 correctness;
end;

definition let G, X;
 func G-TVSet X -> set equals
  { v: ex e being Element of the Edges of G st e in X &
                v = (the Target of G).e };
 correctness;
end;

canceled;

theorem  G-SVSet {} = {} & G-TVSet {} = {}
proof set X = {};
 A1:now assume A2:G-SVSet X<>{};
   consider x being Element of G-SVSet X;
      x in G-SVSet X by A2;
   then x in { v: ex e being Element of the Edges of G st e in X &
                (v = (the Source of G).e) };
   then consider v such that A3:v=x &
   (ex e being Element of the Edges of G st e in X &
    (v = (the Source of G).e));
   consider e being Element of the Edges of G such that
   A4: e in X & (v = (the Source of G).e) by A3;
  thus contradiction by A4;
 end;
    now assume A5:G-TVSet X<>{};
   consider x being Element of G-TVSet X;
      x in G-TVSet X by A5;
   then x in { v: ex e being Element of the Edges of G st e in X &
                (v = (the Target of G).e) };
   then consider v such that A6:v=x &
   (ex e being Element of the Edges of G st e in X &
    (v = (the Target of G).e));
   consider e being Element of the Edges of G such that
   A7: e in X & (v = (the Target of G).e) by A6;
  thus contradiction by A7;
 end;
 hence thesis by A1;
end;

definition let G, vs; let c be FinSequence;
 pred vs is_oriented_vertex_seq_of c means
:Def5: len vs = len c + 1 &
   for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1);
end;

theorem Th5: vs is_oriented_vertex_seq_of c implies vs is_vertex_seq_of c
proof assume A1: vs is_oriented_vertex_seq_of c;
      then A2:len vs = len c + 1 &
      for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
                                       by Def5;
         for n st 1<=n & n<=len c holds c.n joins vs/.n, vs/.(n+1)
        proof let n;assume 1<=n & n<=len c;
          then c.n orientedly_joins vs/.n, vs/.(n+1) by A1,Def5;
         hence c.n joins vs/.n, vs/.(n+1) by Th1;
        end;
 hence vs is_vertex_seq_of c by A2,GRAPH_2:def 7;
end;

theorem  vs is_oriented_vertex_seq_of c implies G-SVSet rng c c= rng vs
proof
assume
A1: vs is_oriented_vertex_seq_of c;
 then A2: len vs = len c + 1 by Def5;
    G-SVSet rng c c= rng vs
 proof let y be set;
   assume
    y in G-SVSet rng c;
   then consider v being Element of the Vertices of G such that
A3: v=y & ex e being Element of the Edges of G st e in rng c &
        v = (the Source of G).e;
   consider e being Element of the Edges of G such that
A4: e in rng c & (v = (the Source of G).e) by A3;
   consider x being set such that
A5: x in dom c & e = c.x by A4,FUNCT_1:def 5;
   reconsider x as Element of NAT by A5;
A6: 1<=x & x<=len c by A5,FINSEQ_3:27;
then A7: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
   set v1 = vs/.x; set v2 = vs/.(x+1);
A8: v1 = vs.x & v2 = vs.(x+1) by A6,A7,FINSEQ_4:24;
      c.x orientedly_joins v1, v2 by A1,A6,Def5;
then A9: v = v1 by A4,A5,Def1;
      x in dom vs & x+1 in dom vs by A6,A7,FINSEQ_3:27;
   hence y in rng vs by A3,A8,A9,FUNCT_1:def 5;
  end;
hence thesis;
end;

theorem  vs is_oriented_vertex_seq_of c implies G-TVSet rng c c= rng vs
proof
assume
A1: vs is_oriented_vertex_seq_of c;
 then A2: len vs = len c + 1 by Def5;
let y be set;
   assume
    y in G-TVSet rng c;
   then consider v being Element of the Vertices of G such that
A3: v=y & ex e being Element of the Edges of G st e in rng c &
        v = (the Target of G).e;
   consider e being Element of the Edges of G such that
A4: e in rng c & (v = (the Target of G).e) by A3;
   consider x being set such that
A5: x in dom c & e = c.x by A4,FUNCT_1:def 5;
   reconsider x as Element of NAT by A5;
A6: 1<=x & x<=len c by A5,FINSEQ_3:27;
then A7: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
   set v1 = vs/.x; set v2 = vs/.(x+1);
A8: v1 = vs.x & v2 = vs.(x+1) by A6,A7,FINSEQ_4:24;
      c.x orientedly_joins v1, v2 by A1,A6,Def5;
then A9: v = v2 by A4,A5,Def1;
      x in dom vs & x+1 in dom vs by A6,A7,FINSEQ_3:27;
   hence y in rng vs by A3,A8,A9,FUNCT_1:def 5;
end;

theorem  c <>{} & vs is_oriented_vertex_seq_of c implies
   rng vs c= G-SVSet rng c \/ G-TVSet rng c
 proof assume
A1: c <>{} & vs is_oriented_vertex_seq_of c;
then A2: len vs = len c + 1 by Def5;
  let y be set;
  assume y in rng vs; then consider x being set such that
A3:  x in dom vs & y = vs.x by FUNCT_1:def 5;
   reconsider x as Element of NAT by A3;
A4: 1<=x & x<=len vs by A3,FINSEQ_3:27;
   per cases by A2,A4,NAT_1:8;
   suppose
A5:  x<=len c;
then A6: 1<=x+1 & x+1<=len vs & x<=len vs by A2,NAT_1:12,XREAL_1:9;
       x in dom c by A4,A5,FINSEQ_3:27;
    then c.x in rng c & rng c c= the Edges of G by FINSEQ_1:def 4,FUNCT_1:def 5
;
    then reconsider e = c.x as Element of the Edges of G;
   set v1 = vs/.x; set v2 = vs/.(x+1);
A7: v1 = vs.x & v2 = vs.(x+1) by A4,A6,FINSEQ_4:24;
      c.x orientedly_joins v1, v2 by A1,A4,A5,Def5;
then A8: v1 = (the Source of G).e & v2 = (the Target of G).e by Def1;
      x in dom c by A4,A5,FINSEQ_3:27;
then e in rng c by FUNCT_1:def 5;
   then y in G-SVSet rng c by A3,A7,A8;
   hence y in G-SVSet rng c \/ G-TVSet rng c by XBOOLE_0:def 2;
   end;
   suppose
A9:  x=len c+1; set l = len c;
       l <> 0 by A1,FINSEQ_1:25; then 0<l & 0+1=1;
then A10:  1<=l & l<=l+1 by NAT_1:13;
    then l in dom c by FINSEQ_3:27;
    then c.l in rng c & rng c c= the Edges of G by FINSEQ_1:def 4,FUNCT_1:def 5
;
    then reconsider e = c.l as Element of the Edges of G;
   set v1 = vs/.l; set v2 = vs/.(l+1);
A11: v1 = vs.l & v2 = vs.(l+1) by A2,A4,A9,A10,FINSEQ_4:24;
      c.l orientedly_joins v1, v2 by A1,A10,Def5;
then A12: v1 = (the Source of G).e & v2 = (the Target of G).e by Def1;
      l in dom c by A10,FINSEQ_3:27;
     then e in rng c by FUNCT_1:def 5;
   then y in G-TVSet rng c by A3,A9,A11,A12;
   hence y in G-SVSet rng c \/ G-TVSet rng c by XBOOLE_0:def 2;
end;
end;

begin :: Cutting and glueing of oriented chains

theorem  <*v*> is_oriented_vertex_seq_of {}
proof
 set c ={};
A1: len c = 0 by FINSEQ_1:25;
  hence len <*v*> = len c +1 by FINSEQ_1:57;
  let n; assume 1<=n & n<=len c; hence thesis by A1;
end;

theorem Th10:  ex vs st vs is_oriented_vertex_seq_of c
proof
    now per cases;
 case c <>{}; then A1:len c <>0 by FINSEQ_1:25;
  defpred P[Element of NAT,set] means
  (($1=len c+1 implies $2=(the Target of G).(c.len c)) &
  ($1<>len c+1 implies $2=(the Source of G).(c.$1)));
  A2:  for k,y1,y2 st k in Seg (len c +1) & P[k,y1] & P[k,y2]
   holds y1=y2;
  A3:for k st k in Seg (len c+1) ex x being set st P[k,x]
  proof let k;assume k in Seg (len c+1);
       now per cases;
    case k=len c+1;
     hence ex x being set st
      ((k=len c+1 implies x=(the Target of G).(c.len c)) &
      (k<>len c+1 implies x=(the Source of G).(c.k)));
    end;
    case k<>len c+1;
     hence ex x being set st
      ((k=len c+1 implies x=(the Target of G).(c.len c)) &
      (k<>len c+1 implies x=(the Source of G).(c.k)));
    end;
    end;
   hence ex x being set st
  ((k=len c+1 implies x=(the Target of G).(c.len c)) &
  (k<>len c+1 implies x=(the Source of G).(c.k)));
  end;
   ex p st dom p = Seg (len c+1) & for k st k in Seg (len c+1) holds
  P[k,p.k] from FINSEQ_1:sch 1(A2,A3);
  then consider p such that
  A4: dom p = Seg (len c+1) & for k st k in Seg (len c+1) holds
  ((k=len c+1 implies p.k=(the Target of G).(c.len c)) &
  (k<>len c+1 implies p.k=(the Source of G).(c.k)));
  A5: len p=len c+1 by A4,FINSEQ_1:def 3;
     rng p c= (the Vertices of G)
  proof let y be set;assume y in rng p;
   then consider x being set such that A6:x in dom p & y=p.x by FUNCT_1:def 5;
   reconsider k=x as Element of NAT by A6;
   A7:1<=k & k<=len c+1 by A4,A6,FINSEQ_1:3;
      now per cases;
   case k=len c+1; then A8:y=(the Target of G).(c.len c) by A4,A6;
        len c >0 by A1; then len c>=0+1 by NAT_1:13;
     then len c in Seg len c by FINSEQ_1:3;
     then len c in dom c by FINSEQ_1:def 3;
     then A9:c.len c in rng c by FUNCT_1:def 5;
        rng c c= the Edges of G by FINSEQ_1:def 4;
    hence y in (the Vertices of G) by A8,A9,FUNCT_2:7;
   end;
   case A10:k<>len c+1; then A11:y=(the Source of G).(c.k) by A4,A6;
        k<len c+1 by A7,A10,REAL_1:def 5;
     then k<=len c by NAT_1:13;
     then k in Seg len c by A7,FINSEQ_1:3;
     then k in dom c by FINSEQ_1:def 3;
     then A12:c.k in rng c by FUNCT_1:def 5;
        rng c c= the Edges of G by FINSEQ_1:def 4;
    hence y in (the Vertices of G) by A11,A12,FUNCT_2:7;
   end;
   end;
   hence y in (the Vertices of G);
  end;
  then reconsider vs=p as FinSequence of (the Vertices of G) by FINSEQ_1:def 4;
     for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
  proof let n;assume A13:1<=n & n<=len c;
    per cases;
    suppose A14:n=len c;
      then A15:n<len c+1 by NAT_1:13;
      then n in Seg (len c +1) by A13,FINSEQ_1:3;
      then A16:p.n=(the Source of G).(c.n) by A4,A15;
         1<n+1 by A13,NAT_1:13;
      then n+1 in Seg (len c +1) by A14,FINSEQ_1:3;
      then A17:p.(n+1)=(the Target of G).(c.len c) by A4,A14;
      A18:(the Source of G).(c.n) = vs/.n by A5,A13,A15,A16,FINSEQ_4:24;
         1<n+1 by A13,NAT_1:13;
      then (the Target of G).(c.n) = vs/.(n+1) by A5,A14,A17,FINSEQ_4:24;
     hence thesis by A18,Def1;
    end;
    suppose n<>len c;
      then A19:n<len c by A13,REAL_1:def 5;
      then n+1<=len c by NAT_1:13;
      then A20:n+1<len c+1 by NAT_1:13;
      A21:n<len c+1 by A13,NAT_1:13;
      then n in Seg (len c +1) by A13,FINSEQ_1:3;
      then A22:p.n=(the Source of G).(c.n) by A4,A21;
         1<n+1 by A13,NAT_1:13;
      then n+1 in Seg (len c +1) by A20,FINSEQ_1:3;
      then A23:p.(n+1)=(the Source of G).(c.(n+1)) by A4,A20;
      A24:(the Source of G).(c.n) = vs/.n by A5,A13,A21,A22,FINSEQ_4:24;
         1<n+1 by A13,NAT_1:13;
      then vs/.(n+1)=p.(n+1) by A5,A20,FINSEQ_4:24;
      then (the Target of G).(c.n) = vs/.(n+1) by A13,A19,A23,GRAPH_1:def 12;
     hence thesis by A24,Def1;
  end;
  end;
  then vs is_oriented_vertex_seq_of c by A5,Def5;
 hence thesis;
 end;
 case A25: c ={};
  consider x being Element of the Vertices of G;
  set vs=<*x*>;
  A26: len c =0 by A25,FINSEQ_1:25;
  then A27: len vs = len c + 1 by FINSEQ_1:57;
     for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n, vs/.(n+1)
                       by A26;
  then vs is_oriented_vertex_seq_of c by A27,Def5;
 hence thesis;
 end;
 end;
 hence thesis;
end;

theorem Th11:
c <> {} & vs1 is_oriented_vertex_seq_of c & vs2 is_oriented_vertex_seq_of c
implies vs1 = vs2
proof assume
A1: c <>{} & vs1 is_oriented_vertex_seq_of c &
    vs2 is_oriented_vertex_seq_of c;
   then A2:   len vs1 = len c + 1 &
      for n st 1<=n & n<=len c holds c.n orientedly_joins vs1/.n, vs1/.(n+1)
                                 by Def5;
   A3:   len vs2 = len c + 1 &
      for n st 1<=n & n<=len c holds c.n orientedly_joins vs2/.n, vs2/.(n+1)
                                 by A1,Def5;
         for n st 1<=n & n<=len vs1 holds vs1.n=vs2.n
      proof let n;assume A4:1<=n & n<=len vs1;
        then A5:n<=len c +1 by A1,Def5;
        per cases;
        suppose n<len c +1;
          then A6:n<=len c by NAT_1:13;
          then c.n orientedly_joins vs1/.n, vs1/.(n+1) by A1,A4,Def5;
          then A7:(the Source of G).(c.n) = vs1/.n
          & (the Target of G).(c.n) = vs1/.(n+1) by Def1;
             c.n orientedly_joins vs2/.n, vs2/.(n+1) by A1,A4,A6,Def5;
          then A8:(the Source of G).(c.n) = vs2/.n
          & (the Target of G).(c.n) = vs2/.(n+1) by Def1;
             vs1.n=vs1/.n by A4,FINSEQ_4:24;
         hence thesis by A2,A3,A4,A7,A8,FINSEQ_4:24;
        end;
        suppose n>=len c+1;
          then A9: n=len c+1 by A5,XXREAL_0:1;
          then A10:n-1=len c;
             0<>len c by A1,FINSEQ_1:25;
          then 0<len c;
then A11:       0+1<=len c by NAT_1:13;
          then A12:n-1=n-'1 by A9,BINARITH:57;
          A13:1<=n-'1 & n-'1=len c by A10,A11,BINARITH:57;
          then c.(n-'1) orientedly_joins vs1/.(n-'1), vs1/.((n-'1)+1)
                                    by A1,Def5;
          then A14:(the Source of G).(c.(n-'1)) = vs1/.(n-'1)
          & (the Target of G).(c.(n-'1)) = vs1/.((n-'1)+1) by Def1;
             c.(n-'1) orientedly_joins vs2/.(n-'1), vs2/.((n-'1)+1)
                                    by A1,A13,Def5;
          then A15:(the Source of G).(c.(n-'1)) = vs2/.(n-'1)
          & (the Target of G).(c.(n-'1)) = vs2/.((n-'1)+1) by Def1;
          A16:n-'1+1=n by A12;
             vs1.n=vs1/.n by A4,FINSEQ_4:24;
         hence thesis by A2,A3,A4,A14,A15,A16,FINSEQ_4:24;
      end;
      end;
  hence thesis by A2,A3,FINSEQ_1:18;
end;

definition let G, c;
assume A1: c <>{};
 func oriented-vertex-seq c -> FinSequence of the Vertices of G means
    it is_oriented_vertex_seq_of c;
existence by Th10;
uniqueness by A1,Th11;
end;

theorem  vs is_oriented_vertex_seq_of c & c1 = c|Seg n & vs1= vs|Seg (n+1)
    implies vs1 is_oriented_vertex_seq_of c1
proof assume A1: vs is_oriented_vertex_seq_of c;
then A2: len vs = len c + 1 &
    for n st 1<=n & n<=len c holds c.n orientedly_joins vs/.n,vs/.(n+1)
                                                              by Def5;
 assume
A3: c1 = c|Seg n & vs1= vs|Seg (n+1);
    now
  per cases;
  suppose
A4: n<=len c; then n+1<=len vs by A2,XREAL_1:8;
then A5: len c1 = n & len vs1 = n+1 by A3,A4,FINSEQ_1:21;
   hence len vs1 = len c1 + 1;
   let k be Element of NAT; assume
A6: 1<=k & k<=len c1;
then A7: k<=len c by A4,A5,XXREAL_0:2;
    then k in dom c & k in Seg n by A5,A6,FINSEQ_1:3,FINSEQ_3:27;
then A8: c1.k = c.k by A3,FUNCT_1:72; A9: k<=len vs by A2,A7,NAT_1:12;
A10: k<=n+1 by A5,A6,NAT_1:12;
then A11: k in dom vs & k in Seg (n+1) by A6,A9,FINSEQ_1:3,FINSEQ_3:27;
A12: k+1<=len vs by A2,A7,XREAL_1:9;
A13: 1<=k+1 & k+1<=len c1 +1 by A6,NAT_1:12,XREAL_1:9;
    then k+1 in dom vs & k+1 in Seg (n+1) by A5,A12,FINSEQ_1:3,FINSEQ_3:27;
then A14: vs1.k=vs.k & vs1.(k+1)=vs.(k+1) by A3,A11,FUNCT_1:72;
A15: vs/.k=vs.k & vs/.(k+1)=vs.(k+1) by A6,A9,A12,A13,FINSEQ_4:24;
       vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) by A5,A6,A10,A13,FINSEQ_4:24;
   hence c1.k orientedly_joins vs1/.k,vs1/.(k+1)
     by A1,A6,A7,A8,A14,A15,Def5;
  end;
  suppose
A16: len c <=n; then len vs<=n+1 by A2,XREAL_1:8;
    then c1 = c & vs1 = vs by A3,A16,FINSEQ_2:23;
   hence len vs1 = len c1 + 1 &
         for k being Element of NAT st 1<=k & k<=len c1
            holds c1.k orientedly_joins vs1/.k,vs1/.(k+1) by A1,Def5;
  end;
  end; hence thesis by Def5;
end;

theorem Th13:
1<=m & m<=n & n<=len c & q = (m,n)-cut c implies q is oriented Chain of G
proof assume
A1: 1<=m & m<=n & n<=len c & q = (m,n)-cut c;
consider vs such that
A2: vs is_oriented_vertex_seq_of c by Th10;
     len q +m-m=n+1-m by A1,GRAPH_2:def 1;
then A3: len q = n-m+1;
 reconsider qq=q as Chain of G by A1,GRAPH_2:44;
      for n st 1 <= n & n < len q holds
       (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
     proof
      let k be Element of NAT;
      assume A4:1<=k & k<len q;
          1-1<=m-1 by A1,XREAL_1:11; then m-1 = m-'1 by BINARITH:def 3;
       then reconsider m1= m-1 as Element of NAT;
        reconsider i = m1+k as Element of NAT;
     set v1 = vs/.i; set v2 = vs/.(i+1);
          0+1<=k by A4; then consider j such that
A5:  0<=j & j<len q & k=j+1 by A4,GRAPH_2:1;
A6:  i= m+j by A5;
    i+1= m+(j+1) by A5;
    then A7:c.(i+1)=q.(k+1) & c.i=q.k by A1,A4,A5,A6,GRAPH_2:def 1;
       1-1<=m-1 by A1,XREAL_1:11;
then A8:  0+1<=m-1+k by A4,XREAL_1:9;  i<=(m-1)+(n-(m-1)) by A3,A4,XREAL_1:8;
     then 1<=i & i<=len c by A1,A8,XXREAL_0:2;
 then c.i orientedly_joins v1, v2 by A2,Def5;
     then A9:(the Source of G).(c.i) = v1 & (the Target of G).(c.i) = v2 by
Def1;
A10:1<i+1 by A8,NAT_1:13;
   k+m<n-m+1+m by A3,A4,XREAL_1:8;
then k+m<n+1;
then k+m<=n by NAT_1:13;
then m+k<=len c by A1,XXREAL_0:2;
 then c.(i+1) orientedly_joins v2, vs/.(i+1+1) by A2,A10,Def5;
      hence (the Source of G).(q.(k+1)) = (the Target of G).(q.k)
                                      by A7,A9,Def1;
     end;
    then qq is oriented by GRAPH_1:def 12;
 hence thesis;
end;

theorem Th14:
  1<=m & m<=n & n<=len c & c1 = (m,n)-cut c &
vs is_oriented_vertex_seq_of c & vs1 = (m,n+1)-cut vs
    implies vs1 is_oriented_vertex_seq_of c1 proof assume
A1: 1<=m & m<=n & n<=len c; assume
A2: c1 = (m,n)-cut c; assume
A3: vs is_oriented_vertex_seq_of c & vs1 = (m,n+1)-cut vs;
then A4: len vs = len c + 1 by Def5;
then A5: m<=n+1 & n+1<=len vs by A1,XREAL_1:8,NAT_1:12;
A6: len c1 +m= n+1 by A1,A2,GRAPH_2:def 1;
    len vs1 +m = n+1+1 by A1,A3,A5,GRAPH_2:def 1;
hence A7: len vs1 = len c1 + 1 by A6;
    let k be Element of NAT; assume
A8:  1<=k & k<=len c1; then 0+1<=k; then consider j such that
A9:  0<=j & j<len c1 & k=j+1 by A8,GRAPH_2:1;
     set i = m+j; set v1 = vs/.i; set v2 = vs/.(i+1);
        m+k<=len c1 +m by A8,XREAL_1:9;
     then m+j+1-1<=len c1 +m-1 by A9,XREAL_1:11;
then A10:  1<=i & i<=len c by A1,A6,XXREAL_0:2,NAT_1:12;
then A11:  c.i orientedly_joins v1, v2 by A3,Def5;
         j<len vs1 by A7,A9,NAT_1:13;
then A12: vs1.k = vs.i by A1,A3,A5,A9,GRAPH_2:def 1;
A13:  j+1<len vs1 by A7,A9,XREAL_1:8;
         m+j+1 = m+(j+1);
then A14:  vs1.(k+1) = vs.(i+1) by A1,A3,A5,A9,A13,GRAPH_2:def 1;
      i<=len vs & 1<=i+1 & i+1<=len vs by A4,A10,NAT_1:12,XREAL_1:9;
then A15:  vs/.i=vs.i & vs/.(i+1)=vs.(i+1) by A10,FINSEQ_4:24;
         0+1=1;
then A16:   1<=k & k<=len c1 by A9,NAT_1:13;
   then k<=len vs1 & 1<=k+1 & k+1<=len vs1 by A7,NAT_1:12,XREAL_1:9;
then vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) by A16,FINSEQ_4:24;
    hence c1.k orientedly_joins vs1/.k, vs1/.(k+1)
      by A1,A2,A9,A11,A12,A14,A15,GRAPH_2:def 1;
   end;

theorem Th15:
vs1 is_oriented_vertex_seq_of c1
& vs2 is_oriented_vertex_seq_of c2 & vs1.len vs1 = vs2.1
    implies c1^c2 is oriented Chain of G
proof assume A1: vs1 is_oriented_vertex_seq_of c1
& vs2 is_oriented_vertex_seq_of c2 & vs1.len vs1 = vs2.1;
then A2: vs1 is_vertex_seq_of c1
& vs2 is_vertex_seq_of c2 & vs1.len vs1 = vs2.1 by Th5;
A3: len vs1 = len c1 + 1 & len vs2 = len c2 + 1 by A1,Def5;
  set q=c1^c2; set p=vs1^'vs2;
 thus c1^c2 is oriented Chain of G proof
  reconsider cc=c1^c2 as Chain of G by A2,GRAPH_2:46;
        for n st 1 <= n & n < len q holds
       (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
      proof let n;assume A4: 1 <= n & n < len q;
then A5:  n in dom q by FINSEQ_3:27;
    A6:n+1<=len q by A4,NAT_1:13;
    A7:1<n+1 by A4,NAT_1:13;
       now per cases by A5,FINSEQ_1:38;
    case
A8:   n in dom c1;
then A9:   q.n = c1.n by FINSEQ_1:def 7;
     set v1=vs1/.n; set v2=vs1/.(n+1);
A10:   1<=n & n<=len c1 by A8,FINSEQ_3:27;
then A11:   c1.n orientedly_joins v1, v2 by A1,Def5;
     A12:   n<=len vs1 by A3,A10,NAT_1:12;
        1<=n+1 & n+1<=len c1+1 by A10,XREAL_1:8,NAT_1:12;
then A13:  1<=n+1 & n+1<=len vs1 by A1,Def5;
then A14:  vs1/.n=vs1.n & vs1/.(n+1)=vs1.(n+1) by A10,A12,FINSEQ_4:24;
        p.n = vs1.n & p.(n+1) = vs1.(n+1) by A10,A12,A13,GRAPH_2:14;
    hence ex v1, v2 being Element of the Vertices of G st
      v1 = p.n & v2 = p.(n+1) & q.n orientedly_joins v1, v2 by A9,A11,A14;
    end;
    case ex k being Element of NAT st k in dom c2 & n=len c1 + k;
     then consider k being Element of NAT such that
A15:   k in dom c2 & n = len c1 + k;
A16:   q.n = c2.k by A15,FINSEQ_1:def 7;
A17:   1<=k & k<=len c2 by A15,FINSEQ_3:27;
then A18:  1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12,XREAL_1:9;
     set v1 = vs2/.k; set v2 = vs2/.(k+1);
A19: vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A17,A18,FINSEQ_4:24;
A20:   c2.k orientedly_joins v1, v2 by A1,A17,Def5;
A21:  k<=len vs2 by A3,A17,NAT_1:12;
        0+1<=k by A15,FINSEQ_3:27; then consider j such that
A22:  0<=j & j<len vs2 & k=j+1 by A21,GRAPH_2:1;
A23:   p.n = vs2.k proof per cases by A17,REAL_1:def 5;
      suppose A24: 1=k;  0<len vs1 by A3;
then A25: 0+1<=len vs1 by NAT_1:13;
       thus p.n = p.(len vs1) by A1,A15,A24,Def5
        .= vs2.k by A1,A24,A25,GRAPH_2:14;
      end;
      suppose 1<k; then A26:1<=j by A22,NAT_1:13;
       thus p.n = p.(len c1 +1+j) by A15,A22
       .= p.(len vs1 +j) by A1,Def5
        .= vs2.k by A22,A26,GRAPH_2:15;
     end;
     end;
A27:   k<len vs2 by A3,A17,NAT_1:13;
        p.(n+1) = p.(len c1 +1+k) by A15
            .= p.(len vs1+k) by A1,Def5 .= vs2.(k+1) by A17,A27,GRAPH_2:15;
    hence ex v1, v2 being Element of the Vertices of G st
           v1 = p.n & v2 = p.(n+1)
           & q.n orientedly_joins v1, v2 by A16,A19,A20,A23;
   end;
   end;
       then consider v1,v2 being Element of the Vertices of G such that
       A28: v1=p.n & v2=p.(n+1)
        & q.n orientedly_joins v1,v2;
    A29:  n+1 in dom q by A6,A7,FINSEQ_3:27;
       now per cases by A29,FINSEQ_1:38;
    case
A30:   n+1 in dom c1;
then A31:   q.(n+1) = c1.(n+1) by FINSEQ_1:def 7;
     set v2'=vs1/.(n+1),v3=vs1/.(n+1+1);
A32:   1<=n+1 & n+1<=len c1 by A30,FINSEQ_3:27;
then A33:   c1.(n+1) orientedly_joins v2', v3 by A1,Def5;
     A34:   n+1<=len vs1 by A3,A32,NAT_1:12;
        1<=n+1+1 & n+1+1<=len c1+1 by A32,XREAL_1:8,NAT_1:12;
then A35:  1<=n+1+1 & n+1+1<=len vs1 by A1,Def5;
then A36:  vs1/.(n+1)=vs1.(n+1) &
      vs1/.(n+1+1)=vs1.(n+1+1) by A32,A34,FINSEQ_4:24;
        p.(n+1) = vs1.(n+1) & p.(n+1+1) = vs1.(n+1+1) by A32,A34,A35,GRAPH_2:14
;
    hence ex v2'',v3' being Element of the Vertices of G st
           v2'' = p.(n+1) & v3' = p.(n+1+1)
           & q.(n+1) orientedly_joins v2'', v3' by A31,A33,A36;
    end;
    case ex k being Element of NAT st k in dom c2 & (n+1)=len c1 + k;
     then consider k being Element of NAT such that
A37:   k in dom c2 & (n+1) = len c1 + k;
A38:   q.(n+1) = c2.k by A37,FINSEQ_1:def 7;
A39:   1<=k & k<=len c2 by A37,FINSEQ_3:27;
then A40:  1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12,XREAL_1:9;
     set v2'=vs2/.k, v3 = vs2/.(k+1);
A41: vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A39,A40,FINSEQ_4:24;
A42:   c2.k orientedly_joins v2', v3 by A1,A39,Def5;
A43:  k<=len vs2 by A3,A39,NAT_1:12;
        0+1<=k by A37,FINSEQ_3:27; then consider j such that
A44:  0<=j & j<len vs2 & k=j+1 by A43,GRAPH_2:1;
A45:   p.(n+1) = vs2.k proof per cases by A39,REAL_1:def 5;
      suppose A46: 1=k;  0<len vs1 by A3;
then A47: 0+1<=len vs1 by NAT_1:13;
       thus p.(n+1) = p.(len vs1) by A1,A37,A46,Def5
        .= vs2.k by A1,A46,A47,GRAPH_2:14;
      end;
      suppose 1<k; then A48:1<=j by A44,NAT_1:13;
       thus p.(n+1) = p.(len c1 +1+j) by A37,A44
       .= p.(len vs1 +j) by A1,Def5
        .= vs2.k by A44,A48,GRAPH_2:15;
     end;
     end;
A49:   k<len vs2 by A3,A39,NAT_1:13;
        p.((n+1)+1) = p.(len c1 +1+k) by A37
            .= p.(len vs1+k) by A1,Def5 .= vs2.(k+1) by A39,A49,GRAPH_2:15;
    hence ex v2'',v3' being Element of the Vertices of G st
           v2'' = p.(n+1) & v3' = p.((n+1)+1)
           & q.(n+1) orientedly_joins v2'', v3' by A38,A41,A42,A45;
   end;
   end;
   then consider v2',v3 being Element of the Vertices of G such that
   A50: v2'=p.(n+1) & v3=p.(n+1+1) &
   q.(n+1) orientedly_joins v2',v3;
        (the Source of G).(q.n) = v1 & (the Target of G).(q.n) = v2
                              by A28,Def1;
   hence (the Source of G).(q.(n+1)) = (the Target of G).(q.n)
                                   by A28,A50,Def1;
   end;
  then cc is oriented by GRAPH_1:def 12;
  hence thesis;
 end;
end;

theorem Th16:
vs1 is_oriented_vertex_seq_of c1 & vs2 is_oriented_vertex_seq_of c2
& vs1.len vs1 = vs2.1 &
c = c1^c2 & vs = vs1^'vs2
   implies vs is_oriented_vertex_seq_of c proof assume
A1: vs1 is_oriented_vertex_seq_of c1 & vs2 is_oriented_vertex_seq_of c2
& vs1.len vs1 = vs2.1;
   assume
A2: c = c1^c2 & vs = vs1^'vs2;
A3: len vs1 = len c1 + 1 & len vs2 = len c2 + 1 by A1,Def5;
then A4: vs1 <> {} & vs2 <> {} by FINSEQ_1:25;
  set q=c1^c2; set p=vs1^'vs2;
     len p +1 = len vs1 +len vs2 by A4,GRAPH_2:13;
then A5: len p = len c1 + len c2 + 1 by A3
         .= len q + 1 by FINSEQ_1:35;
   reconsider p as FinSequence of the Vertices of G;
   now let n be Element of NAT; assume
A6:  1<=n & n<=len q;
then n<=len p & 1<=n+1 & n+1<=len p by A5,NAT_1:12,XREAL_1:9;
then A7:  p/.n=p.n & p/.(n+1)=p.(n+1) by A6,FINSEQ_4:24;
A8:  n in dom q by A6,FINSEQ_3:27;
    per cases by A8,FINSEQ_1:38;
    suppose
A9:   n in dom c1;
     set v1=vs1/.n; set v2=vs1/.(n+1);
A10:   1<=n & n<=len c1 by A9,FINSEQ_3:27;
then A11:   c1.n orientedly_joins v1, v2 by A1,Def5;
     A12:   n<=len vs1 by A3,A10,NAT_1:12;
        1<=n+1 & n+1<=len c1+1 by A10,XREAL_1:8,NAT_1:12;
then A13:  1<=n+1 & n+1<=len vs1 by A1,Def5;
then A14:  vs1/.n=vs1.n & vs1/.(n+1)=vs1.(n+1) by A10,A12,FINSEQ_4:24;
        p.n = vs1.n & p.(n+1) = vs1.(n+1) by A10,A12,A13,GRAPH_2:14;
    hence q.n orientedly_joins p/.n, p/.(n+1)
      by A7,A9,A11,A14,FINSEQ_1:def 7;
    end;
    suppose ex k being Element of NAT st k in dom c2 & n=len c1 + k;
     then consider k being Element of NAT such that
A15:   k in dom c2 & n = len c1 + k;
A16:   q.n = c2.k by A15,FINSEQ_1:def 7;
A17:   1<=k & k<=len c2 by A15,FINSEQ_3:27;
then 1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12,XREAL_1:9;
then A18:  vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A17,FINSEQ_4:24;
A19:  k<=len vs2 by A3,A17,NAT_1:12;
        0+1<=k by A15,FINSEQ_3:27; then consider j such that
A20:  0<=j & j<len vs2 & k=j+1 by A19,GRAPH_2:1;
A21:   p.n = vs2.k proof per cases by A17,REAL_1:def 5;
      suppose A22: 1=k;  0<len vs1 by A3;
then A23: 0+1<=len vs1 by NAT_1:13;
       thus p.n = p.(len vs1) by A1,A15,A22,Def5
        .= vs2.k by A1,A22,A23,GRAPH_2:14;
      end;
      suppose 1<k; then A24:1<=j by A20,NAT_1:13;
       thus p.n = p.(len c1 +1+j) by A15,A20 .= p.(len vs1 +j)
                                         by A1,Def5
        .= vs2.k by A20,A24,GRAPH_2:15;
     end;
     end;
A25:   k<len vs2 by A3,A17,NAT_1:13;
        p.(n+1) = p.(len c1 +1+k) by A15
            .= p.(len vs1+k) by A1,Def5 .= vs2.(k+1) by A17,A25,GRAPH_2:15;
    hence q.n orientedly_joins p/.n,p/.(n+1) by A1,A7,A16,A17,A18,A21,Def5;
   end;
   end;
hence thesis by A2,A5,Def5;
end;

begin :: Oriented simple chains in oriented chains

 Lm1: <*v*> is_oriented_vertex_seq_of {} proof
    set p = <*v*>, ec = {};
A1:  len p = 0 + 1 by FINSEQ_1:56 .= len ec + 1 by FINSEQ_1:25;
        now let n; assume 1<=n & n<=len ec;
        then 1<=len ec by XXREAL_0:2; then 1<=0 by FINSEQ_1:25;
      hence ec.n orientedly_joins p/.n,p/.(n+1);
     end;
   hence thesis by A1,Def5;
end;

definition let G;
  let IT be oriented Chain of G;
   attr IT is Simple means
:Def7: ex vs st vs is_oriented_vertex_seq_of IT &
     (for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs);
end;

registration let G;
   cluster Simple (oriented Chain of G);
existence
     proof consider q being empty oriented Chain of G;
     consider x being Element of the Vertices of G;
     reconsider p =<*x*> as FinSequence of the Vertices of G;
     A1:p is_oriented_vertex_seq_of q by Lm1;
        for n,m st 1<=n & n<m & m<=len p & p.n=p.m holds n=1 & m=len p
     proof
      let n,m; assume 1<=n & n<m & m<=len p & p.n = p.m;
       then 1<m & m<=1 by XXREAL_0:2,FINSEQ_1:56;
      hence n =1 & m = len p;
     end;
     then q is Simple by A1,Def7;
  hence thesis;
 end;
end;

registration let G;
   cluster oriented simple Chain of G;
existence
     proof consider q being empty oriented Chain of G;
     consider x being Element of the Vertices of G;
     reconsider p =<*x*> as FinSequence of the Vertices of G;
        p is_oriented_vertex_seq_of q by Lm1;
     then A1: p is_vertex_seq_of q by Th5;
     reconsider q'=q as Chain of G;
        for n,m st 1<=n & n<m & m<=len p & p.n=p.m holds n=1 & m=len p
     proof
      let n,m; assume 1<=n & n<m & m<=len p & p.n = p.m;
       then 1<m & m<=1 by XXREAL_0:2,FINSEQ_1:56;
      hence n =1 & m = len p;
     end;
     then q' is simple by A1,GRAPH_2:def 10;
  hence thesis;
 end;
end;

canceled;

theorem Th18:for q being oriented Chain of G holds
      q|(Seg n) is oriented Chain of G
proof let q be oriented Chain of G;
     reconsider q'= q|(Seg n) as FinSequence by FINSEQ_1:19;
        for i st 1 <= i & i < len q' holds
       (the Source of G).((q|(Seg n)).(i+1))
       = (the Target of G).((q|(Seg n)).i)
      proof let i;assume A1:1 <= i & i < len q';
        per cases;
        suppose n>=len q; then q|(Seg n)=q by FINSEQ_3:55;
         hence thesis by A1,GRAPH_1:def 12;
        end;
        suppose A2:n<len q;
          then A3:len q'=n by FINSEQ_1:21;
          then A4:i<len q by A1,A2,XXREAL_0:2;
             i in Seg n by A1,A3,FINSEQ_1:3;
          then A5:(q|(Seg n)).i=q.i by FUNCT_1:72;
          A6:i+1<=n by A1,A3,NAT_1:13;
             1<i+1 by A1,NAT_1:13;
          then i+1 in Seg n by A6,FINSEQ_1:3;
          then (q|(Seg n)).(i+1)=q.(i+1) by FUNCT_1:72;
         hence thesis by A1,A4,A5,GRAPH_1:def 12;
      end;
      end;
 hence thesis by GRAPH_1:4,def 12;
end;

reserve sc for oriented simple Chain of G;

theorem  sc|(Seg n) is oriented simple Chain of G by Th18,GRAPH_2:49;

theorem Th20: for sc' being oriented Chain of G st sc'=sc holds sc' is Simple
proof let sc' be oriented Chain of G;
  assume A1:sc'=sc;
   consider vs such that A2:vs is_oriented_vertex_seq_of sc' by Th10;
      vs is_vertex_seq_of sc by A1,A2,Th5;
   then for n, m st 1<=n & n<m & m<=len vs & vs.n = vs.m holds n=1 & m=len vs
                                     by GRAPH_2:51;
  hence thesis by A2,Def7;
end;

theorem Th21: for sc' being Simple (oriented Chain of G)
  holds sc' is oriented simple Chain of G
proof let sc' be Simple (oriented Chain of G);
   consider vs such that A1:vs is_oriented_vertex_seq_of sc' &
     (for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs)
                                by Def7;
      vs is_vertex_seq_of sc' by A1,Th5;
  hence thesis by A1,GRAPH_2:def 10;
end;

reserve x,y for set;

Lm2:
 for p be FinSequence, m, n be Element of NAT
  st 1<=m & m<=n+1 & n<=len p
 holds len (m, n)-cut p +m = n+1 &
 for i being Element of NAT st i<len (m, n)-cut p
  holds ((m, n)-cut p).(i+1)=p.(m+i)
 proof let p be FinSequence, m, n be Element of NAT such that
A1: 1<=m & m<=n+1 & n<=len p;
  m <= n+1 by A1;
   then m = n+1 or m < n+1 by XXREAL_0:1;
   then
A2:  m = n+1 or m <= n by NAT_1:13;
  per cases by A2;
  suppose
A3: m = n+1;
   then n < m by XREAL_1:31;
   then not(1<=m & m<=n & n<=len p);
   then (m, n)-cut p = {} by GRAPH_2:def 1;
   then
A4:  len (m, n)-cut p = 0 by CARD_1:47;
  hence len (m, n)-cut p +m = n+1 by A3;
  thus thesis by A4;
  end;
  suppose m<=n;
   hence thesis by A1,GRAPH_2:def 1;
  end;
 end;

theorem Th22:
not c is Simple & vs is_oriented_vertex_seq_of c
implies
  ex fc being FinSubsequence of c, fvs being FinSubsequence of vs, c1, vs1
   st len c1 < len c & vs1 is_oriented_vertex_seq_of c1 & len vs1 < len vs &
      vs.1 = vs1.1 & vs.len vs = vs1.len vs1 & Seq fc = c1 & Seq fvs = vs1
proof assume
A1: not c is Simple & vs is_oriented_vertex_seq_of c;
   then consider n, m being Element of NAT such that
A2: 1<=n & n<m & m<=len vs & vs.n=vs.m & (n<>1 or m<>len vs) by Def7;
A3: len vs = len c + 1 by A1,Def5;
      A4: m-n>n-n by A2,XREAL_1:11;
   reconsider n1 = n-'1 as Element of NAT; A5: 1-1<=n-1 by A2,XREAL_1:11;
then A6: n-1 = n-'1 by BINARITH:def 3;
A7: n1+1 = n-1+1 by A5,BINARITH:def 3 .= n;
   per cases by A2;
    suppose
A8:  n<>1 & m <> len vs;
    then 1 < n by A2,REAL_1:def 5;
    then 1 + 1<=n by NAT_1:13;
then A9: 1+1-1<=n-1 by XREAL_1:11;
       n < len vs by A2,XXREAL_0:2
; then A10: n-1 < len c +1-1 by A3,XREAL_1:11;
A11: 1<=n1+1 by NAT_1:12;
A12:   m < len vs by A2,A8,REAL_1:def 5;
then A13:  m<=len c by A3,NAT_1:13;
A14: 1<=m & m<=len c & len c <=len c by A2,A3,A12,XXREAL_0:2,NAT_1:13;
   reconsider c1 = (1,n1)-cut c as oriented Chain of G by A6,A9,A10,Th13;
   reconsider c2 = (m,len c)-cut c as oriented Chain of G by A14,Th13;
   set pp = (1,n)-cut vs; set p2 = (m,len c+1)-cut vs;
   set p2' = (m+1, len c +1)-cut vs;
   reconsider pp as FinSequence of the Vertices of G;
   reconsider p2 as FinSequence of the Vertices of G;
A15: 1<=n & n<=len vs by A2,XXREAL_0:2;
A16: 1<=m & m<=len c + 1 & len c + 1<=len vs by A1,A2,Def5,XXREAL_0:2;
A17: pp is_oriented_vertex_seq_of c1 by A1,A7,A9,A10,Th14;
A18: p2 is_oriented_vertex_seq_of c2 by A1,A14,Th14;
A19: len pp = len c1 + 1 by A17,Def5;
A20: len p2 = len c2 + 1 by A18,Def5;
        1-1<=m-1 by A14,XREAL_1:11;
     then m-'1 = m-1 by BINARITH:def 3;
     then reconsider m1 = m-1 as Element of NAT;
A21:  m = m1 +1;  len c2 +m = len c +1
         by A14,GRAPH_2:def 1;
then A22: len c2 =len c - m + 1;
       A23: m-m<=len c -m by A13,XREAL_1:11;
     then len c2 -'1 = len c2 -1 by A22,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
A24:  m+lc21 = m1+len c2;
     A25: 0+1=1 & 0<len p2 by A20;
then A26: 0+1=1 & 0<=1 & 1<=len p2 by NAT_1:13;
A27: p2 = (0+1, len p2)-cut p2 by GRAPH_2:7
       .= (0+1, 1)-cut p2 ^ (1+1, len p2)-cut p2 by A26,GRAPH_2:8;
        m1<=m by A21,NAT_1:12;
then A28: p2 = (m1+1, m)-cut vs ^ (m+1, len c +1)-cut vs by A2,A3,GRAPH_2:8
;
        (1,1)-cut p2 = <*p2.(0+1)*> by A26,GRAPH_2:6
                 .= <*vs.(m+0)*> by A16,A25,GRAPH_2:def 1
                 .= (m,m)-cut vs by A2,A14,GRAPH_2:6;
then A29: p2' = (2, len p2)-cut p2 by A27,A28,FINSEQ_1:46;
  set domfc = {k: 1<=k & k<=n1 or m<=k & k<= len c};
  deffunc F(set) = c.$1;
  consider fc being Function such that
A30: dom fc = domfc and
A31: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A32:    x = kk & (1<=kk & kk<=n1 or m<=kk & kk<= len c);
          1<=kk & kk<=len c by A6,A10,A14,A32,XXREAL_0:2;
      hence x in Seg len c by A32,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A30,FINSEQ_1:def 12;
A33: fc c= c proof let p be set; assume
A34:  p in fc; then consider x, y being set such that
A35:  [x,y] = p by RELAT_1:def 1;
A36:  x in dom fc & y = fc.x by A34,A35,FUNCT_1:8;
     then consider kk being Element of NAT such that
A37:  x = kk & (1<=kk & kk<=n1 or m<=kk & kk<= len c) by A30;
        1<=kk & kk<=len c by A6,A10,A14,A37,XXREAL_0:2;
then A38:  x in dom c by A37,FINSEQ_3:27;  y = c.kk by A30,A31,A36,A37;
hence p in c by A35,A37,A38,FUNCT_1:8;
  end;
  then reconsider fc as FinSubsequence of c by GRAPH_2:def 5;
  take fc;
  set domfvs = {k: 1<=k & k<=n or m+1<=k & k<= len vs};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A39: dom fvs = domfvs and
A40: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A41:    x = kk & (1<=kk & kk<=n or m+1<=kk & kk<= len vs);
          1<=m+1 by NAT_1:12; then 1<=kk & kk<=len vs by A15,A41,XXREAL_0:2;
      hence x in Seg len vs by A41,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A39,FINSEQ_1:def 12;
A42: fvs c= vs proof let p be set; assume
A43:  p in fvs; then consider x, y being set such that
A44:  [x,y] = p by RELAT_1:def 1;
A45:  x in dom fvs & y = fvs.x by A43,A44,FUNCT_1:8;
     then consider kk being Element of NAT such that
A46:  x = kk & (1<=kk & kk<=n or m+1<=kk & kk<= len vs) by A39;
        1<=m+1 by NAT_1:12;
     then 1<=kk & kk<=len vs by A15,A46,XXREAL_0:2;
then A47:  x in dom vs by A46,FINSEQ_3:27;  y = vs.kk by A39,A40,A45,A46;
     hence p in vs by A44,A46,A47,FUNCT_1:8;
  end;
  then reconsider fvs as FinSubsequence of vs by GRAPH_2:def 5;
  take fvs;
A48: p2.1 = vs.m & pp.len pp = vs.n by A15,A16,GRAPH_2:12;
  then reconsider c' = c1^c2 as oriented Chain of G by A2,A17,A18,Th15;
  take c';
  take p1=pp^'p2;
A49: p1 = pp^p2' by A29,GRAPH_2:def 2;
A50:  len c1 +1 = n1+1 by A6,A10,A11,Lm2;
then A51: len c1 = n - 1 by A6;
        -(-(m-n)) = m-n; then A52: -(m-n) < 0 by A4,XREAL_1:60;
    len c' = (n-1)+(len c -m+1)
     by  FINSEQ_1:35,A6,A22,A50
.= len c + (n+ -m );
  hence
A53: len c' < len c by A52,REAL_2:173;
  thus p1 is_oriented_vertex_seq_of c' by A2,A17,A18,A48,Th16;
  then len p1 = len c' + 1 by Def5;
  hence len p1 < len vs by A3,A53,XREAL_1:8;
      1<=1 + len c1 by NAT_1:12; then 1<=len pp by A17,Def5;
 then p1.1 = pp.1 by GRAPH_2:14;
   hence vs.1 = p1.1 by A15,GRAPH_2:12;
A54: p2.len p2 = vs.(len c + 1) by A16,GRAPH_2:12;
     m-m<=len c -m by A14,XREAL_1:11;
  then 0+1<=len c -m+1 by XREAL_1:8; then 1<len p2 by A20,A22,NAT_1:13;
hence vs.len vs = p1.len p1 by A3,A54,GRAPH_2:16;
     set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n1};
     set DR = {kk where kk is Element of NAT: m<=kk & kk<= len c};
        now let x be set;
      hereby assume x in domfc; then consider k being Element of NAT such that
A55:     x=k & (1<=k & k<=n1 or m<=k & k<= len c);
           x in DL or x in DR by A55;
       hence x in DL \/ DR by XBOOLE_0:def 2;
      end; assume A56: x in DL \/ DR;
      per cases by A56,XBOOLE_0:def 2;
      suppose x in DL; then consider k being Element of NAT such that
A57:    x=k & 1<=k & k<=n1;
      thus x in domfc by A57;
      end;
      suppose x in DR; then consider k being Element of NAT such that
A58:    x=k & m<=k & k<=len c;
      thus x in domfc by A58;
     end;
     end;
then A59:  domfc = DL \/ DR by TARSKI:2;
A60:  DL c= Seg len c & DR c= Seg len c proof
      hereby let x be set; assume x in DL;
       then consider k being Element of NAT such that
A61:    x=k & 1<=k & k<=n1;  k<=len c by A6,A10,A61,XXREAL_0:2;
       hence x in Seg len c by A61,FINSEQ_1:3;
      end; let x be set; assume x in DR;
       then consider k being Element of NAT such that
A62:    x=k & m<=k & k<=len c;
          1<=k by A16,A62,XXREAL_0:2;
       hence x in Seg len c by A62,FINSEQ_1:3;
     end;
     then reconsider DL as finite set by FINSET_1:13;
     reconsider DR as finite set by A60,FINSET_1:13;
    now let i,j; assume i in DL; then consider k being Element of NAT such that
A63:    k=i & 1<=k & k<=n1;
      assume j in DR; then consider l being Element of NAT such that
A64:    l=j & m<=l & l<=len c;
          n1<m by A2,A7,NAT_1:13; then k<m by A63,XXREAL_0:2;
      hence i < j by A63,A64,XXREAL_0:2;
     end;
then A65: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A60,FINSEQ_3:48;
    m+lc21 = len c by A22;
     then A66:  card DR = len c2 + -1 +1 by GRAPH_2:4
        .= len c2;
A67:  len Sgm DL = card DL & len Sgm DR = card DR by A60,FINSEQ_3:44;  DL
=
Seg n1 by FINSEQ_1:def 1;
then A68:  Sgm DL = idseq n1 by FINSEQ_3:54;
then A69:  dom Sgm DL = Seg n1 by RELAT_1:71;
        rng Sgm DL = DL by A60,FINSEQ_1:def 13;
then A70: rng Sgm DL c= dom fc by A30,A59,XBOOLE_1:7;
       rng Sgm DR = DR by A60,FINSEQ_1:def 13;
then A71: rng Sgm DR c= dom fc by A30,A59,XBOOLE_1:7;
    set SL = Sgm DL; set SR = Sgm DR;
       now let p be set;
     hereby assume
A72:   p in c1; then consider x, y being set such that
A73:  p=[x,y] by RELAT_1:def 1;
A74:   x in dom c1 & y=c1.x by A72,A73,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A75:   1<=k & k<=len c1 by A74,FINSEQ_3:27;
then A76:   x in dom SL by A6,A51,A69,FINSEQ_1:3;
then A77:   k=SL.k by A68,A69,FUNCT_1:35;
A78:   k in domfc by A6,A51,A75;
then A79:   x in dom (fc*SL) by A30,A76,A77,FUNCT_1:21;
         0+1<=k by A74,FINSEQ_3:27;
      then consider i being Element of NAT such that
A80:   0<=i & i<n1 & k=i+1 by A6,A51,A75,GRAPH_2:1;
         (fc*SL).x = fc.k by A77,A79,FUNCT_1:22 .= c.(1+i) by A30,A33,A78,A80,
GRFUNC_1:8
        .= y by A6,A10,A11,A51,A74,A80,Lm2;
      hence p in fc*(Sgm DL) by A73,A79,FUNCT_1:8;
     end; assume
A81:  p in fc*(Sgm DL); then consider x,y being set such that
A82:  p=[x,y] by RELAT_1:def 1;
A83:   x in dom (fc*SL) & y = (fc*SL).x by A81,A82,FUNCT_1:8;
then A84:  (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A85:   x in dom SL & SL.x in dom fc by A83,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n1}
       by A69,FINSEQ_1:def 1;
      then consider k such that
A86:   k=x & 1<=k & k<=n1;
A87:   k in dom fc by A30,A86;
A88:   k=SL.k by A68,A69,A85,A86,FUNCT_1:35;
A89: k in dom c1 by A6,A51,A86,FINSEQ_3:27;  0+1<=k by A86;
     then consider i being Element of NAT such that
A90: 0<=i & i<n1 & k=i+1 by A86,GRAPH_2:1;
       c1.k = c.k by A6,A10,A11,A51,A90,Lm2
         .= y by A33,A83,A84,A86,A87,A88,GRFUNC_1:8;
     hence p in c1 by A82,A86,A89,FUNCT_1:8;
    end;
then A91: c1 = fc*(Sgm DL) by TARSKI:2;
       now let p be set;
     hereby assume
A92:   p in c2; then consider x, y being set such that
A93:  p=[x,y] by RELAT_1:def 1;
A94:   x in dom c2 & y=c2.x by A92,A93,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A95:   1<=k & k<=len c2 by A94,FINSEQ_3:27;
A96:   x in dom SR by A66,A67,A94,FINSEQ_3:31;
A97:   m1+k=SR.k by A21,A22,A24,A95,GRAPH_2:5;
A98:   SR.k in rng SR by A96,FUNCT_1:def 5;
then A99:   x in dom (fc*SR) by A71,A96,FUNCT_1:21;
0+1<=k by A94,FINSEQ_3:27;
      then consider i being Element of NAT such that
A100:   0<=i & i<len c2 & k=i+1 by A95,GRAPH_2:1;
         (fc*SR).x = fc.(m1+k) by A97,A99,FUNCT_1:22 .= c.(m+i) by  A33,A71,A97
,A98,GRFUNC_1:8,A100
        .= y by A14,A94,A100,GRAPH_2:def 1;
      hence p in fc*(Sgm DR) by A93,A99,FUNCT_1:8;
     end; assume
A101:  p in fc*(Sgm DR); then consider x,y being set such that
A102:  p=[x,y] by RELAT_1:def 1;
A103:   x in dom (fc*SR) & y = (fc*SR).x by A101,A102,FUNCT_1:8;
then A104:   x in dom SR & SR.x in dom fc by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A105:   k in dom c2 by A66,A67,A104,FINSEQ_3:31;
A106:   1<=k & k<=len c2 by A66,A67,A104,FINSEQ_3:27;
then A107:   m1+k=SR.k by A21,A22,A24,GRAPH_2:5;  0+1<=k by A104,FINSEQ_3:27;
     then consider i being Element of NAT such that
A108:   0<=i & i<len c2 & k=i+1 by A106,GRAPH_2:1;
        c2.k = c.(m1+1+i) by A14,A108,GRAPH_2:def 1
         .= fc.(SR.k) by  A108,A33,A104,A107,GRFUNC_1:8 .= y by
A103,A104,FUNCT_1:23;
     hence p in c2 by A102,A105,FUNCT_1:8;
    end;
then A109: c2 = fc*(Sgm DR) by TARSKI:2;
    Seq fc= fc*((Sgm DL)^(Sgm DR))by A30,A59,A65,FINSEQ_1:def 14;
  hence Seq fc = c' by A70,A71,A91,A109,GRAPH_2:26;
     set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n};
     set DR = {kk where kk is Element of NAT: m+1<=kk & kk<= len vs};
        now let x be set;
      hereby assume x in domfvs; then consider k being Element of NAT such that
A110:     x=k & (1<=k & k<=n or m+1<=k & k<= len vs);
           x in DL or x in DR by A110;
       hence x in DL \/ DR by XBOOLE_0:def 2;
      end; assume A111: x in DL \/ DR;
      per cases by A111,XBOOLE_0:def 2;
      suppose x in DL; then consider k being Element of NAT such that
A112:    x=k & 1<=k & k<=n;
      thus x in domfvs by A112;
      end;
      suppose x in DR; then consider k being Element of NAT such that
A113:    x=k & m+1<=k & k<=len vs;
      thus x in domfvs by A113;
     end;
     end;
then A114:  domfvs = DL \/ DR by TARSKI:2;
A115:  DL c= Seg len vs & DR c= Seg len vs proof
      hereby let x be set; assume x in DL;
       then consider k being Element of NAT such that
A116:    x=k & 1<=k & k<=n;  k<=len vs by A15,A116,XXREAL_0:2;
       hence x in Seg len vs by A116,FINSEQ_1:3;
      end; let x be set; assume x in DR;
       then consider k being Element of NAT such that
A117:    x=k & m+1<=k & k<=len vs;
          1<=m+1 by NAT_1:12; then 1<=k by A117,XXREAL_0:2;
       hence x in Seg len vs by A117,FINSEQ_1:3;
     end;
     then reconsider DL as finite set by FINSET_1:13;
     reconsider DR as finite set by A115,FINSET_1:13;
    now let i,j; assume i in DL; then consider k being Element of NAT such that
A118:    k=i & 1<=k & k<=n;
      assume j in DR; then consider l being Element of NAT such that
A119:    l=j & m+1<=l & l<=len vs;  m<=m+1 by NAT_1:12;
then A120:   m<=l by A119,XXREAL_0:2;  k<m by A2,A118,XXREAL_0:2;
      hence i < j by A118,A119,A120,XXREAL_0:2;
     end;
then A121: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A115,FINSEQ_3:48;
        1<=len p2 by A20,NAT_1:12;
     then 1-1<=len p2 -1 by XREAL_1:11;
     then len p2 -'1 = len p2 -1 by BINARITH:def 3;
     then reconsider lp21 = len p2 -1 as Element of NAT;
      lp21 -'1 = lp21 -1 by A20,A22,A23,BINARITH:def 3;
     then reconsider lp22 = lp21 -1 as Element of NAT;
A122:  m+1+lp22 = m+((lp21 -1)+1)
            .= m+(len c -m +1 +1 -1) by A18,A22,Def5
            .= len c +1;
then A123:  card DR = lp22 + 1 by A3,GRAPH_2:4
        .= lp21;
A124:  1<=m+1 & m+1<=len c +1+1 by A16,XREAL_1:8,NAT_1:12;
         len p2 +m = len c +1+1 by A16,GRAPH_2:def 1
     .= len p2' +(m+1) by A16,A124,Lm2
     .= (len p2'+1)+m;
     then A125: len p2 -1 = len p2'+1-1;
A126:  len Sgm DL = card DL & len Sgm DR = card DR by A115,FINSEQ_3:44;
A127:  len pp = n by A6,A19,A50;  DL = Seg n by FINSEQ_1:def 1;
then A128:  Sgm DL = idseq n by FINSEQ_3:54;
then A129:  dom Sgm DL = Seg n by RELAT_1:71;
       rng Sgm DL = DL by A115,FINSEQ_1:def 13;
then A130: rng Sgm DL c= dom fvs by A39,A114,XBOOLE_1:7;
       rng Sgm DR = DR by A115,FINSEQ_1:def 13;
then A131: rng Sgm DR c= dom fvs by A39,A114,XBOOLE_1:7;
    set SL = Sgm DL; set SR = Sgm DR;
       now let p be set;
     hereby assume
A132:   p in pp; then consider x, y being set such that
A133:  p=[x,y] by RELAT_1:def 1;
A134:   x in dom pp & y=pp.x by A132,A133,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A135:   1<=k & k<=len pp by A134,FINSEQ_3:27;
then A136:   x in dom SL by A127,A129,FINSEQ_1:3;
then A137:   k=SL.k by A128,A129,FUNCT_1:35;
A138:   k in domfvs by A127,A135;
then A139:   x in dom (fvs*SL) by A39,A136,A137,FUNCT_1:21;
         0+1<=k by A134,FINSEQ_3:27;
      then consider i being Element of NAT such that
A140:   0<=i & i<n & k=i+1 by A127,A135,GRAPH_2:1;
         (fvs*SL).x = fvs.k by A137,A139,FUNCT_1:22 .= vs.(1+i) by A39,A42,A138
,A140,GRFUNC_1:8 .= y by A15,A127,A134,A140,GRAPH_2:def 1;
      hence p in fvs*(Sgm DL) by A133,A139,FUNCT_1:8;
     end; assume
A141:  p in fvs*(Sgm DL); then consider x,y being set such that
A142:  p=[x,y] by RELAT_1:def 1;
A143:   x in dom (fvs*SL) & y = (fvs*SL).x by A141,A142,FUNCT_1:8;
then A144:  (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A145:   x in dom SL & SL.x in dom fvs by A143,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n}
       by A129,FINSEQ_1:def 1;
      then consider k such that
A146:   k=x & 1<=k & k<=n;
A147:   k in dom fvs by A39,A146;
A148:   k=SL.k by A128,A129,A145,A146,FUNCT_1:35;
A149: k in dom pp by A127,A146,FINSEQ_3:27;  0+1<=k by A146;
     then consider i being Element of NAT such that
A150:  0<=i & i<n & k=i+1 by A146,GRAPH_2:1;
        pp.k = vs.k by A15,A127,A150,GRAPH_2:def 1
         .= y by A42,A143,A144,A146,A147,A148,GRFUNC_1:8;
     hence p in pp by A142,A146,A149,FUNCT_1:8;
    end;
then A151: pp = fvs*(Sgm DL) by TARSKI:2;
A152:  m+1+lp22 = m+lp21;
A153: 1<=m+1 & m+1<=len c +1+1 by A2,A3,NAT_1:12,XREAL_1:9;
       now let p be set;
     hereby assume
A154:   p in p2'; then consider x, y being set such that
A155:  p=[x,y] by RELAT_1:def 1;
A156:   x in dom p2' & y=p2'.x by A154,A155,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A157:   1<=k & k<=len p2' by A156,FINSEQ_3:27;
A158:   x in dom SR by A123,A125,A126,A156,FINSEQ_3:31;
A159:   m+k=SR.k by A3,A122,A125,A152,A157,GRAPH_2:5;
      A160: SR.k in rng SR by A158,FUNCT_1:def 5;
then A161:   x in dom (fvs*SR) by A131,A158,FUNCT_1:21;  0+1<=k by A156,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A162:   0<=i & i<len p2' & k=i+1 by A157,GRAPH_2:1;
         (fvs*SR).x = fvs.(m+k) by A159,A161,FUNCT_1:22 .= vs.(m+1+i) by  A42
,A131,A159,A160,GRFUNC_1:8,A162 .= y by A3,A153,A156,A162,Lm2;
      hence p in fvs*(Sgm DR) by A155,A161,FUNCT_1:8;
     end; assume
A163:  p in fvs*(Sgm DR); then consider x,y being set such that
A164:  p=[x,y] by RELAT_1:def 1;
A165:   x in dom (fvs*SR) & y = (fvs*SR).x by A163,A164,FUNCT_1:8;
then A166:   x in dom SR & SR.x in dom fvs by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A167:   k in dom p2' by A123,A125,A126,A166,FINSEQ_3:31;
A168:   1<=k & k<=len p2' by A123,A125,A126,A166,FINSEQ_3:27;
then A169:   m+k=SR.k by A3,A122,A125,A152,GRAPH_2:5;  0+1<=k by A166,
FINSEQ_3:27;
     then consider i being Element of NAT such that
A170:   0<=i & i<len p2' & k=i+1 by A168,GRAPH_2:1;
        p2'.k = vs.(m+1+i) by A3,A153,A170,Lm2
         .= fvs.(SR.k) by  A170,A42,A166,A169,GRFUNC_1:8 .= y by
A165,A166,FUNCT_1:23;
     hence p in p2' by A164,A167,FUNCT_1:8;
    end;
then A171: p2' = fvs*(Sgm DR) by TARSKI:2;
    Seq fvs = fvs*((Sgm DL)^(Sgm DR)) by A39,A114,A121,FINSEQ_1:def 14;
  hence Seq fvs = p1 by A49,A130,A131,A151,A171,GRAPH_2:26;
 end;
 suppose
A172: n=1 & m <> len vs;
     then A173: m < len vs by A2,REAL_1:def 5;
then A174:  m<=len c by A3,NAT_1:13;
A175:  1 < m by A2,XXREAL_0:2;
A176: 1<=m & m<=len c & len c <=len c by A2,A3,A173,XXREAL_0:2,NAT_1:13;
   reconsider c2 = (m,len c)-cut c as oriented Chain of G by A174,A175,Th13;
   set p2 = (m,len c+1)-cut vs;
A177: p2 is_oriented_vertex_seq_of c2 by A1,A174,A175,Th14;
  set domfc = {k: m<=k & k<= len c};
  deffunc F(set) = c.$1;
  consider fc being Function such that
A178: dom fc = domfc and
A179: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A180:    x = kk & m<=kk & kk<= len c;
          1<=kk & kk<=len c by A175,A180,XXREAL_0:2;
      hence x in Seg len c by A180,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A178,FINSEQ_1:def 12;
A181: fc c= c proof let p be set; assume
A182:  p in fc; then consider x, y being set such that
A183:  [x,y] = p by RELAT_1:def 1;
A184:  x in dom fc & y = fc.x by A182,A183,FUNCT_1:8;
     then consider kk being Element of NAT such that
A185:  x = kk & m<=kk & kk<= len c by A178;
        1<=kk & kk<=len c by A175,A185,XXREAL_0:2;
then A186:  x in dom c by A185,FINSEQ_3:27;
        y = c.kk by A178,A179,A184,A185;
     hence p in c by A183,A185,A186,FUNCT_1:8;
  end;
  then reconsider fc as FinSubsequence of c by GRAPH_2:def 5;
  take fc;
  set domfvs = {k: m<=k & k<= len vs};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A187: dom fvs = domfvs and
A188: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A189:    x = kk & m<=kk & kk<= len vs;
          1<=kk & kk<=len vs by A175,A189,XXREAL_0:2;
      hence x in Seg len vs by A189,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A187,FINSEQ_1:def 12;
A190: fvs c= vs proof let p be set; assume
A191:  p in fvs; then consider x, y such that
A192:  [x,y] = p by RELAT_1:def 1;
A193:  x in dom fvs & y = fvs.x by A191,A192,FUNCT_1:8;
     then consider kk being Element of NAT such that
A194:  x = kk & m<=kk & kk<= len vs by A187;
        1<=kk & kk<=len vs by A175,A194,XXREAL_0:2;
then A195:  x in dom vs by A194,FINSEQ_3:27;
        y = vs.kk by A187,A188,A193,A194;
     hence p in vs by A192,A194,A195,FUNCT_1:8;
  end;
  then reconsider fvs as FinSubsequence of vs by GRAPH_2:def 5;
  take fvs; take c1 = c2; take p1 = p2;
        len c2 +m = len c +1 by A2,A3,A172,Lm2;
then A196: len c2 =len c - m + 1;
       A197: m-m<=len c -m by A174,XREAL_1:11;
        1-1<=m-1 by A175,XREAL_1:11;
     then m-'1 = m-1 by BINARITH:def 3;
      then reconsider m1 = m-1 as Element of NAT;
A198:  m = m1 +1;
      len c2 -'1 = len c2 -1 by A196,A197,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
A199:  m+lc21 = m1+len c2;
A200: 1<=m & m<=len c +1+1 & len c +1<=len vs by A2,A3,A172,NAT_1:12;
     then len p2 +m = len c +1 +1 by Lm2;
then A201: len p2 = len c -m +1+1;
then A202: 1<=len p2 by A196,NAT_1:12;
A203: len c2 = len c +(-m+1) by A196;
        1-1 < m-1 by A175,XREAL_1:11
;
     then 0 < -(-(m-1)); then -(m-1) < 0 by XREAL_1:60;
  hence
A204: len c1 < len c by A203,REAL_2:173;
  thus p1 is_oriented_vertex_seq_of c1 by A1,A176,Th14;
     len p1 = len c1 + 1 by A177,Def5;
  hence len p1 < len vs by A3,A204,XREAL_1:8;
  thus vs.1 = p1.1 by A2,A3,A172,GRAPH_2:12;
   thus vs.len vs = p1.len p1 by A2,A3,A172,GRAPH_2:12;
A205:  domfc c= Seg len c proof
       let x be set; assume x in domfc;
        then consider k being Element of NAT such that
A206:    x=k & m<=k & k<=len c;  1<=k by A175,A206,XXREAL_0:2;
       hence x in Seg len c by A206,FINSEQ_1:3;
     end;
     then reconsider domfc as finite set by FINSET_1:13;
      len c2 -'1 = len c2 -1 by A196,A197,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
  m+lc21 = len c by A196;
then A207:  card domfc = len c2 + -1 +1 by GRAPH_2:4
        .= len c2;
A208: len Sgm domfc = card domfc by A205,FINSEQ_3:44;
    A209: rng Sgm domfc c= dom fc by A178,A205,FINSEQ_1:def 13;
    set SR = Sgm domfc;
       now let p be set;
     hereby assume
A210:   p in c2; then consider x, y being set such that
A211:  p=[x,y] by RELAT_1:def 1;
A212:   x in dom c2 & y=c2.x by A210,A211,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A213:   1<=k & k<=len c2 by A212,FINSEQ_3:27;
A214:   x in dom SR by A207,A208,A212,FINSEQ_3:31;
A215:   m1+k=SR.k by A196,A198,A199,A213,GRAPH_2:5;
      A216: SR.k in rng SR by A214,FUNCT_1:def 5;
then A217:   x in dom (fc*SR) by A209,A214,FUNCT_1:21;  0+1<=k by A212,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A218:   0<=i & i<len c2 & k=i+1 by A213,GRAPH_2:1;
         (fc*SR).x = fc.(m1+k) by A215,A217,FUNCT_1:22 .= c.(m+i) by  A181,A209
,A215,A216,GRFUNC_1:8,A218
        .= y by A2,A3,A172,A212,A218,Lm2;
      hence p in fc*(Sgm domfc) by A211,A217,FUNCT_1:8;
     end; assume
A219:  p in fc*(Sgm domfc); then consider x,y being set such that
A220:  p=[x,y] by RELAT_1:def 1;
A221:   x in dom (fc*SR) & y = (fc*SR).x by A219,A220,FUNCT_1:8;
then A222:   x in dom SR & SR.x in dom fc by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A223:   k in dom c2 by A207,A208,A222,FINSEQ_3:31;
A224:   1<=k & k<=len c2 by A207,A208,A222,FINSEQ_3:27;
then A225:   m1+k=SR.k by A196,A198,A199,GRAPH_2:5;  0+1<=k by A222,FINSEQ_3:27
;
     then consider i being Element of NAT such that
A226:   0<=i & i<len c2 & k=i+1 by A224,GRAPH_2:1;
        c2.k = c.(m1+1+i) by A2,A3,A172,A226,Lm2
         .= fc.(SR.k) by  A226,
A181,A222,A225,GRFUNC_1:8 .= y by A221,A222,FUNCT_1:23
;
     hence p in c2 by A220,A223,FUNCT_1:8;
    end;
 then c2 = fc*(Sgm domfc) by TARSKI:2;
 hence Seq fc = c1 by A178,FINSEQ_1:def 14;
A227:  domfvs c= Seg len vs proof
       let x be set; assume x in domfvs;
        then consider k being Element of NAT such that
A228:    x=k & m<=k & k<=len vs;  1<=k by A175,A228,XXREAL_0:2;
       hence x in Seg len vs by A228,FINSEQ_1:3;
     end;
     then reconsider domfvs as finite set by FINSET_1:13;
        1-1<=len p2 -1 by A202,XREAL_1:11;
     then len p2 -'1 = len p2 -1 by BINARITH:def 3;
     then reconsider lp21 = len p2 -1 as Element of NAT;
A229: m+lp21 = len c+1 by A201;
A230:  m+lp21 = m1+len p2;
A231:  card domfvs = len p2 + -1 +1 by A3,A229,GRAPH_2:4
        .= len p2;
A232: len Sgm domfvs = card domfvs by A227,FINSEQ_3:44;
    A233: rng Sgm domfvs c= dom fvs by A187,A227,FINSEQ_1:def 13;
    set SR = Sgm domfvs;
       now let p be set;
     hereby assume
A234:   p in p2; then consider x, y being set such that
A235:  p=[x,y] by RELAT_1:def 1;
A236:   x in dom p2 & y=p2.x by A234,A235,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A237:   1<=k & k<=len p2 by A236,FINSEQ_3:27;
A238:   x in dom SR by A231,A232,A236,FINSEQ_3:31;
A239:   m1+k=SR.k by A3,A198,A201,A230,A237,GRAPH_2:5; A240: SR.k in rng SR by
A238,FUNCT_1:def 5;
then A241:   x in dom (fvs*SR) by A233,A238,FUNCT_1:21;  0+1<=k by A236,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A242:   0<=i & i<len p2 & k=i+1 by A237,GRAPH_2:1;
         (fvs*SR).x = fvs.(m1+k) by A239,A241,FUNCT_1:22 .= vs.(m+i) by  A190
,A233,A239,A240,GRFUNC_1:8,A242
        .= y by A200,A236,A242,Lm2;
      hence p in fvs*(Sgm domfvs) by A235,A241,FUNCT_1:8;
     end; assume
A243:  p in fvs*(Sgm domfvs); then consider x,y being set such that
A244:  p=[x,y] by RELAT_1:def 1;
A245:   x in dom (fvs*SR) & y = (fvs*SR).x by A243,A244,FUNCT_1:8;
then A246:   x in dom SR & SR.x in dom fvs by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A247:   k in dom p2 by A231,A232,A246,FINSEQ_3:31;
A248:   1<=k & k<=len p2 by A231,A232,A246,FINSEQ_3:27;
then A249:   m1+k=SR.k by A3,A198,A201,A230,GRAPH_2:5;  0+1<=k by A246,
FINSEQ_3:27;
     then consider i being Element of NAT such that
A250:   0<=i & i<len p2 & k=i+1 by A248,GRAPH_2:1;
        p2.k = vs.(m1+1+i) by A200,A250,Lm2
        .= fvs.(SR.k) by  A250,A190,A246,A249,GRFUNC_1:8 .= y
by A245,A246,FUNCT_1:23
;
     hence p in p2 by A244,A247,FUNCT_1:8;
    end;
 then p2 = fvs*(Sgm domfvs) by TARSKI:2;
 hence Seq fvs = p1 by A187,FINSEQ_1:def 14;
 end;
 suppose
A251: n<>1 & m = len vs;
    then 1 < n by A2,REAL_1:def 5;
    then 1 + 1<=n by NAT_1:13; then A252: 1+1-1<=n-1 by XREAL_1:11;
        n < len vs by A2,XXREAL_0:2; then A253: n-1 < len c +1-1 by A3,
XREAL_1:11
;
A254: 1<=n1+1 by NAT_1:12;
   reconsider c1 = (1,n1)-cut c as oriented Chain of G by A6,A252,A253,Th13;
   set pp = (1,n)-cut vs; A255: 1<=n & n<=len vs by A2,XXREAL_0:2;
A256: pp is_oriented_vertex_seq_of c1 by A1,A7,A252,A253,Th14;
then A257: len pp = len c1 + 1 by Def5;
  set domfc = {k: 1<=k & k<=n1};
  deffunc F(set) = c.$1;
  consider fc being Function such that
A258: dom fc = domfc and
A259: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A260:    x = kk & 1<=kk & kk<=n1;  1<=kk & kk<=len c by A6,A253,A260,XXREAL_0:2
;
      hence x in Seg len c by A260,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A258,FINSEQ_1:def 12;
A261: fc c= c proof let p be set; assume
A262:  p in fc; then consider x, y being set such that
A263:  [x,y] = p by RELAT_1:def 1;
A264:  x in dom fc & y = fc.x by A262,A263,FUNCT_1:8;
     then consider kk being Element of NAT such that
A265:  x = kk & 1<=kk & kk<=n1 by A258;
        1<=kk & kk<=len c by A6,A253,A265,XXREAL_0:2;
then A266:  x in dom c by A265,FINSEQ_3:27;  y = c.kk by A258,A259,A264,A265;
hence p in c by A263,A265,A266,FUNCT_1:8;
  end;
  then reconsider fc as FinSubsequence of c by GRAPH_2:def 5;
  take fc;
  set domfvs = { k : 1 <= k & k <= n};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A267: dom fvs = domfvs and
A268: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A269:    x = kk & 1<=kk & kk<=n;  1<=kk & kk<=len vs by A255,A269,XXREAL_0:2;
      hence x in Seg len vs by A269,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A267,FINSEQ_1:def 12;
A270: fvs c= vs proof let p be set; assume
A271:  p in fvs; then consider x, y being set such that
A272:  [x,y] = p by RELAT_1:def 1;
A273:  x in dom fvs & y = fvs.x by A271,A272,FUNCT_1:8;
     then consider kk being Element of NAT such that
A274:  x = kk & 1<=kk & kk<=n by A267;
        1<=kk & kk<=len vs by A255,A274,XXREAL_0:2;
then A275:  x in dom vs by A274,FINSEQ_3:27;  y = vs.kk by A267,A268,A273,A274
;
hence p in vs by A272,A274,A275,FUNCT_1:8;
  end;
  then reconsider fvs as FinSubsequence of vs by GRAPH_2:def 5;
  take fvs; take c' = c1; take p1 = pp;
        len c1+1=n1+1 by A6,A253,A254,Lm2;
then A276: len c1 = n - 1 by A5,BINARITH:def 3;
  hence
   len c' < len c by A253;
  thus p1 is_oriented_vertex_seq_of c' by A1,A7,A252,A253,Th14;  len p1 = len
c1 +
1 by A256,Def5;
  hence len p1 < len vs by A2,A276,XXREAL_0:2;
  thus vs.1 = p1.1 by A255,GRAPH_2:12;
  thus vs.len vs = p1.len p1 by A2,A251,GRAPH_2:12;
    domfc c= Seg len c proof
      let x be set; assume x in domfc;
       then consider k being Element of NAT such that
A277:    x=k & 1<=k & k<=n1;  k<=len c by A6,A253,A277,XXREAL_0:2;
       hence x in Seg len c by A277,FINSEQ_1:3;
     end;
     then reconsider domfc as finite set by FINSET_1:13;  domfc = Seg n1
by FINSEQ_1:def 1;
then A278:  Sgm domfc = idseq n1 by FINSEQ_3:54;
then A279:  dom Sgm domfc = Seg n1 by RELAT_1:71;
    set SL = Sgm domfc;
       now let p be set;
     hereby assume
A280:   p in c1; then consider x, y being set such that
A281:  p=[x,y] by RELAT_1:def 1;
A282:   x in dom c1 & y=c1.x by A280,A281,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A283:   1<=k & k<=len c1 by A282,FINSEQ_3:27;
then A284:   x in dom SL by A6,A276,A279,FINSEQ_1:3;
then A285:   k=SL.k by A278,A279,FUNCT_1:35;
A286:   k in domfc by A6,A276,A283;
then A287:   x in dom (fc*SL)
by A258,A284,A285,FUNCT_1:21;  0+1<=k by A282,FINSEQ_3:27;
      then consider i being Element of NAT such that
A288:   0<=i & i<n1 & k=i+1 by A6,A276,A283,GRAPH_2:1;
         (fc*SL).x = fc.k by A285,A287,FUNCT_1:22 .= c.(1+i) by A258,A261,A286
,A288,GRFUNC_1:8
        .= y by A6,A253,A254,A276,A282,A288,Lm2;
      hence p in fc*(Sgm domfc) by A281,A287,FUNCT_1:8;
     end; assume
A289:  p in fc*(Sgm domfc); then consider x,y being set such that
A290:  p=[x,y] by RELAT_1:def 1;
A291:   x in dom (fc*SL) & y = (fc*SL).x by A289,A290,FUNCT_1:8;
then A292:  (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A293:   x in dom SL & SL.x in dom fc by A291,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n1}
       by A279,FINSEQ_1:def 1;
      then consider k such that
A294:   k=x & 1<=k & k<=n1;
A295:   k in dom fc by A258,A294;
A296:   k=SL.k by A278,A279,A293,A294,FUNCT_1:35;
A297: k in dom c1 by A6,A276,A294,FINSEQ_3:27;  0+1<=k by A294;
     then consider i being Element of NAT such that
A298: 0<=i & i<n1 & k=i+1 by A294,GRAPH_2:1;
       c1.k = c.k by A6,A253,A254,A276,A298,Lm2
         .= y by A261,A291,A292,A294,A295,A296,GRFUNC_1:8;
     hence p in c1 by A290,A294,A297,FUNCT_1:8;
    end; then c1 = fc*(Sgm domfc) by TARSKI:2;
  hence Seq fc = c' by A258,FINSEQ_1:def 14;
    domfvs c= Seg len vs proof
      let x be set; assume x in domfvs;
       then consider k being Element of NAT such that
A299:    x=k & 1<=k & k<=n;  k<=len vs by A255,A299,XXREAL_0:2;
       hence x in Seg len vs by A299,FINSEQ_1:3;
     end; then reconsider domfvs as finite set by FINSET_1:13;
        domfvs = Seg n by FINSEQ_1:def 1;
then A300:  Sgm domfvs = idseq n by FINSEQ_3:54;
then A301:  dom Sgm domfvs = Seg n by RELAT_1:71;
    set SL = Sgm domfvs;
       now let p be set;
     hereby assume
A302:   p in pp; then consider x, y being set such that
A303:  p=[x,y] by RELAT_1:def 1;
A304:   x in dom pp & y=pp.x by A302,A303,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A305:   1<=k & k<=len pp by A304,FINSEQ_3:27;
then A306:   x in dom SL by A257,A276,A301,FINSEQ_1:3;
then A307:   k=SL.k by A300,A301,FUNCT_1:35;
A308:   k in domfvs by A257,A276,A305;
then A309:   x in dom (fvs*SL) by A267,A306,A307,FUNCT_1:21;  0+1<=
k by A304,FINSEQ_3:27;
      then consider i being Element of NAT such that
A310:   0<=i & i<n & k=i+1 by A257,A276,A305,GRAPH_2:1;
         (fvs*SL).x = fvs.k by A307,A309,FUNCT_1:22 .= vs.(1+i) by A267,A270
,A308,A310,GRFUNC_1:8
        .= y by A255,A257,A276,A304,A310,GRAPH_2:def 1;
      hence p in fvs*(Sgm domfvs) by A303,A309,FUNCT_1:8;
     end; assume
A311:  p in fvs*(Sgm domfvs); then consider x,y being set such that
A312:  p=[x,y] by RELAT_1:def 1;
A313:   x in dom (fvs*SL) & y = (fvs*SL).x by A311,A312,FUNCT_1:8;
then A314:  (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A315:   x in dom SL & SL.x in dom fvs by A313,FUNCT_1:21;
      then x in { kk where kk is Element of NAT: 1<=kk & kk<=n}
       by A301,FINSEQ_1:def 1;
      then consider k such that
A316:   k=x & 1<=k & k<=n;
A317:   k in dom fvs by A267,A316;
A318:   k=SL.k by A300,A301,A315,A316,FUNCT_1:35;
A319: k in dom pp by A257,A276,A316,FINSEQ_3:27;  0+1<=k by A316;
     then consider i being Element of NAT such that
A320:  0<=i & i<n & k=i+1 by A316,GRAPH_2:1;
        pp.k = vs.k by A255,A257,A276,A320,GRAPH_2:def 1
         .= y by A270,A313,A314,A316,A317,A318,GRFUNC_1:8;
     hence p in pp by A312,A316,A319,FUNCT_1:8;
    end;
   then pp = fvs*(Sgm domfvs) by TARSKI:2;
  hence Seq fvs = p1 by A267,FINSEQ_1:def 14;
end;
end;

theorem  vs is_oriented_vertex_seq_of c implies
 ex fc being FinSubsequence of c, fvs being FinSubsequence of vs, sc, vs1
  st Seq fc = sc & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of sc &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 proof assume
A1: vs is_oriented_vertex_seq_of c;
 per cases;
 suppose A2: c is Simple;
  reconsider fc = c as FinSubsequence of c by GRAPH_2:28;
  reconsider fvs = vs as FinSubsequence of vs by GRAPH_2:28;
  reconsider sc = c as oriented simple Chain of G by A2,Th21;
  take fc, fvs, sc, vs;
  thus Seq fc = sc & Seq fvs = vs by FINSEQ_3:125;
  thus vs is_oriented_vertex_seq_of sc by A1;
  thus vs.1 = vs.1 & vs.len vs = vs.len vs;
 end;
 suppose A3: not c is Simple;
defpred P[Element of NAT] means
 ex fc being FinSubsequence of c, fvs being FinSubsequence of vs, c1, vs1
  st Seq fc = c1 & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of c1 &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 &
     len c1 = $1 & not c1 is Simple;
     P[len c] proof
   reconsider fc = c as FinSubsequence of c by GRAPH_2:28;
   reconsider fvs = vs as FinSubsequence of vs by GRAPH_2:28;
   take fc, fvs, c,vs;
   thus Seq fc = c & Seq fvs = vs by FINSEQ_3:125;
   thus vs is_oriented_vertex_seq_of c by A1;
   thus vs.1 = vs.1 & vs.len vs = vs.len vs;
   thus len c = len c & not c is Simple
                  by A3;
  end;
then A4: ex k st P[k]; consider k such that
A5:  P[k] & for n being Element of NAT st P[n] holds k<=n from NAT_1:sch 5(A4);
  consider fc being FinSubsequence of c, fvs being FinSubsequence of vs,
           c1, vs1 such that
A6:   Seq fc = c1 & Seq fvs = vs1 & vs1 is_oriented_vertex_seq_of c1 &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 &
     len c1 = k & not c1 is Simple
     by A5;
  consider fc1 being FinSubsequence of c1, fvs1 being FinSubsequence of vs1,
           c2, vs2 such that
A7: len c2 < len c1 & vs2 is_oriented_vertex_seq_of c2 & len vs2 < len vs1 &
     vs1.1 = vs2.1 & vs1.len vs1 = vs2.len vs2 &
     Seq fc1 = c2 & Seq fvs1 = vs2 by A6,Th22;
   reconsider fc'=fc|rng((Sgm dom fc)|dom fc1) as FinSubsequence of c
                           by GRAPH_2:29;
A8:    Seq fc' = c2 by A6,A7,GRAPH_2:30;
   reconsider fvs'=fvs|rng((Sgm dom fvs)|dom fvs1) as FinSubsequence of vs
                                                                by GRAPH_2:29;
A9:    Seq fvs' = vs2 by A6,A7,GRAPH_2:30;
       now assume c2 is Simple (oriented Chain of G);
      then reconsider sc = c2 as oriented simple Chain of G by Th21;
     take fc',sc;  Seq fc' = sc by A6,A7,GRAPH_2:30;
     hence thesis by A6,A7,A9;
    end;
 hence thesis by A5,A6,A7,A8,A9;
end;
end;

registration let G;
 cluster empty -> one-to-one (oriented Chain of G);
 correctness;
end;

theorem  p is OrientedPath of G implies p|Seg(n) is OrientedPath of G
proof assume
A1: p is OrientedPath of G;
then reconsider p'=p|(Seg n) as oriented Chain of G by Th18;
 reconsider q=p|(Seg n) as FinSequence by FINSEQ_1:19;
    now let n1,m1 be Element of NAT; assume
A2: 1<=n1 & n1 < m1 & m1<=len q; then 1<m1 by XXREAL_0:2;
    then m1 in dom q by A2,FINSEQ_3:27;
then A3: q.m1=p.m1 by FUNCT_1:68;  n1 < len q by A2,XXREAL_0:2;
    then n1 in dom q by A2,FINSEQ_3:27;
then A4: q.n1=p.n1 by FUNCT_1:68;
       dom q c= dom p by RELAT_1:89;
       then dom q c= Seg len p by FINSEQ_1:def 3
;
    then Seg len q c= Seg len p by FINSEQ_1:def 3;
    then len q<=len p by FINSEQ_1:7;
    then m1<=len p by A2,XXREAL_0:2;
   hence q.n1 <> q.m1 by A1,A2,A3,A4,GRAPH_1:def 13;
  end;
  then p' is Path of G by GRAPH_1:def 13;
  hence thesis;
end;

theorem Th25: sc is OrientedPath of G
proof
assume not sc is OrientedPath of G;
then consider m, n such that
A1: 1<=m & m<n & n<=len sc & sc.m = sc.n by GRAPH_1:def 13;
    sc is Simple by Th20;
 then consider vs such that
A2: vs is_oriented_vertex_seq_of sc &
   (for n, m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs)
                                                           by Def7;
A3: len vs = len sc + 1 by A2,Def5; A4: m<=len sc by A1,XXREAL_0:2;
A5: 1<=n by A1,XXREAL_0:2;
A6: 1<=m & m<n & n<=len vs by A1,A3,NAT_1:12;
A7: 1<=m+1 & m+1<n+1 & n+1<=len vs by A1,A3,XREAL_1:8,NAT_1:12;
     then A8: 1<=m & m<n+1 & n+1<=len vs by A1,NAT_1:12;
 set v1 = vs/.m; set v2 = vs/.(m+1);
A9: sc.m orientedly_joins v1, v2 by A1,A2,A4,Def5;
 set v1' = vs/.n; set v2' = vs/.(n+1);
    sc.n orientedly_joins v1', v2' by A1,A2,A5,Def5;
then A10: v1=v1' & v2=v2' by A1,A9,Th2;
    m<=len vs & m+1<=len vs & 1<=n+1 by A7,A8,XXREAL_0:2;
then A11: v1=vs.m & v2=vs.(m+1) & v1'=vs.n & v2'=vs.(n+1)
                                by A5,A6,A7,FINSEQ_4:24;
   then m=1 & n=len vs by A2,A6,A10;
   hence contradiction by A2,A7,A10,A11;
end;

theorem  for c1 being FinSequence holds
  (c1 is Simple (oriented Chain of G) iff c1 is oriented simple Chain of G)
  &(c1 is oriented simple Chain of G implies c1 is OrientedPath of G)
   by Th20,Th21,Th25;
