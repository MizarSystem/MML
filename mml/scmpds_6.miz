:: The Construction and Computation of Conditional Statements for SCMPDS
::  by JingChao Chen
::
:: Received June 15, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, SCMPDS_2, AMI_1, FSM_1, INT_1, AMI_2, RELAT_1,
      XBOOLE_0, GRAPHSP, XXREAL_0, CIRCUIT2, ARYTM_3, CARD_1, FUNCT_7, FUNCT_1,
      SCMFSA6A, FUNCT_4, TARSKI, TURING_1, SCMFSA_7, SCMPDS_4, AMISTD_2,
      COMPLEX1, VALUED_1, SCMFSA6B, MSUALG_1, SCMPDS_5, SCMFSA8A, AMI_3,
      ARYTM_1, UNIALG_2, SCMFSA7B, GLIB_000, SCMFSA8B, NAT_1, STRUCT_0,
      SCMPDS_6, ORDINAL1, PARTFUN1, SCMNORM, AFINSQ_1, SCMFSA6C;
 notations TARSKI, XBOOLE_0, SUBSET_1, XCMPLX_0, RELAT_1, FUNCT_1, CARD_1,
      PARTFUN1, AFINSQ_1, VALUED_1, NUMBERS, COMPLEX1, FUNCT_4, INT_1, NAT_1,
      STRUCT_0, COMPOS_1, EXTPRO_1,
      AMI_1, AMI_2, FUNCT_7, SCMPDS_2, SCMPDS_4,
      SCMPDS_5, XXREAL_0;
 constructors XXREAL_0, REAL_1, INT_2, SCMPDS_4, SCMPDS_5, VALUED_1, AMI_1,
      AMI_2;
 registrations SETFAM_1, FUNCT_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, INT_1,
      AMI_1, SCMPDS_2, SCMPDS_4, SCMPDS_5, ORDINAL1, XBOOLE_0,
      VALUED_1, AFINSQ_1, COMPOS_1, EXTPRO_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions FUNCOP_1, COMPOS_1, EXTPRO_1, AMI_1, SCMPDS_4, SCMPDS_5;
 theorems AMI_1, NAT_1, TARSKI, FUNCT_4, FUNCT_1, INT_1, SCMPDS_2, SCMPDS_3,
      ABSVALUE, GRFUNC_1, SCMPDS_4, SCMPDS_5, XBOOLE_0, XBOOLE_1, XREAL_1,
      ORDINAL1, FUNCOP_1, XXREAL_0, FUNCT_7, AMI_2, VALUED_1, PARTFUN1,
      AFINSQ_1, COMPOS_1, EXTPRO_1;
 schemes NAT_1;

begin :: Preliminaries

reserve m,n for Element of NAT,
  a for Int_position,
  i,j for Instruction of SCMPDS,
  s,s1,s2 for State of SCMPDS,
  k1 for Integer,
  loc for Element of NAT,
  I,J,K for Program of SCMPDS;
set A = NAT;
set D = SCM-Data-Loc;

Lm1: (Stop SCMPDS). 0 = halt SCMPDS by AFINSQ_1:38;
Lm2:  0 in dom Stop SCMPDS by COMPOS_1:45;

canceled;

theorem Th2:
  for s being State of SCMPDS st ProgramPart s halts_on s for k being Element
  of NAT st LifeSpan(ProgramPart(s),s) <= k
   holds CurInstr(ProgramPart (s), Comput(ProgramPart(s),s,k))
        = halt SCMPDS
proof
  let s be State of SCMPDS;
  assume
  ProgramPart s halts_on s;
  then
A1: CurInstr(ProgramPart s,
  Comput(ProgramPart (s),s,LifeSpan(ProgramPart(s),s)))
 = halt SCMPDS by EXTPRO_1:def 14;
  let k be Element of NAT;
  assume
   LifeSpan(ProgramPart(s),s) <= k;
   then
 Comput(ProgramPart(s),s,LifeSpan(ProgramPart(s),s)) = Comput(ProgramPart(s),s,
k) by A1,EXTPRO_1:6;

  hence thesis by A1;
end;

theorem Th3:
  for s being State of SCMPDS st ProgramPart s halts_on s for k being Element
of NAT st LifeSpan(ProgramPart(s),s) <= k holds IC Comput(ProgramPart(s),s,k) =
IC Comput(ProgramPart(s),s,LifeSpan(ProgramPart s,s))
proof
  let s be State of SCMPDS;
  defpred P[Element of NAT] means LifeSpan(ProgramPart(s),s) <= $1 implies IC
Comput(ProgramPart(s),s,$1)
 = IC Comput(ProgramPart(s),s,LifeSpan(ProgramPart(s),s));
  assume
A1: ProgramPart s halts_on s;
A2: now
    let k be Element of NAT;
    assume
A3: P[k];
    now
      assume
A4:   LifeSpan(ProgramPart(s),s) <= k + 1;
      per cases by A4,XXREAL_0:1;
      suppose
        k + 1 = LifeSpan(ProgramPart(s),s);
        hence IC Comput(ProgramPart(s),s,k+1) = IC Comput(ProgramPart(s),s,
LifeSpan(ProgramPart(s),s));
      end;
      suppose
A5:     k + 1 > LifeSpan(ProgramPart(s),s);
        then
A6:     LifeSpan(ProgramPart(s),s) <= k by NAT_1:13;
        thus IC Comput(ProgramPart(s),s,k+1) =
        IC Following(ProgramPart s,Comput(ProgramPart(
s),s,k))
         by EXTPRO_1:4
          .= IC Exec(halt SCMPDS,Comput(ProgramPart(s),s,k)) by A1,A6,Th2
          .= IC Comput(ProgramPart(s),s,LifeSpan(ProgramPart(s),s)) by A3,A5,
EXTPRO_1:def 3,NAT_1:13;
      end;
    end;
    hence P[k + 1];
  end;
  let k be Element of NAT;
  assume
A7: LifeSpan(ProgramPart(s),s) <= k;
A8: P[0];
  for k being Element of NAT holds P[k] from NAT_1:sch 1(A8,A2);
  hence thesis by A7;
end;

theorem Th4:
  for s1,s2 being State of SCMPDS holds s1,s2 equal_outside NAT iff
  IC s1 = IC s2 & DataPart s1 = DataPart s2
proof
  let s1,s2 be State of SCMPDS;
  thus s1,s2 equal_outside NAT implies IC s1 = IC s2 & DataPart s1 = DataPart
  s2 by COMPOS_1:24,138;
  assume that
A1: IC s1 = IC s2 and
A2: DataPart s1 = DataPart s2;
  for a being Int_position holds s1.a = s2.a by A2,SCMPDS_4:23;
  hence thesis by A1,SCMPDS_4:11;
end;

canceled 2;

theorem Th7:
  for s being State of SCMPDS, l being Element of NAT
  holds DataPart s = DataPart(s +* Start-At(l,SCMPDS))
proof
  let s be State of SCMPDS;
  let l be Element of NAT;
  now
    let x be set;
    assume
    x in dom Start-At(l,SCMPDS);
    then x in {IC SCMPDS} by FUNCOP_1:19;
    hence not x in D by SCMPDS_3:6,TARSKI:def 1;
  end;
  then dom Start-At(l,SCMPDS) misses D by XBOOLE_0:3;
  hence thesis by FUNCT_4:76,SCMPDS_2:100;
end;

theorem Th8:
  for s being State of SCMPDS,I being Program of SCMPDS, l being
Element of NAT holds DataPart s = DataPart(s +* (I +* Start-At(l,SCMPDS)))
proof
  let s be State of SCMPDS,I be Program of SCMPDS;
  let l be Element of NAT;
  now
    let x be set;
    assume
    x in dom (I +* Start-At(l,SCMPDS));
    then x in dom I \/ dom Start-At(l,SCMPDS) by FUNCT_4:def 1;
    then x in dom I or x in dom Start-At(l,SCMPDS) by XBOOLE_0:def 3;
    then
A1: x in dom I or x in {IC SCMPDS} by FUNCOP_1:19;
    per cases by A1,TARSKI:def 1;
    suppose
A2:   x in dom I;
      thus not x in D by A2,AMI_2:29,XBOOLE_0:3;
    end;
    suppose
      x = IC SCMPDS;
      hence not x in D by SCMPDS_3:6;
    end;
  end;
  then dom (I +* Start-At(l,SCMPDS)) misses D by XBOOLE_0:3;
  hence thesis by FUNCT_4:76,SCMPDS_2:100;
end;

theorem Th9:
  for s being State of SCMPDS,I being Program of SCMPDS holds
  DataPart s = DataPart(Initialize s +* I)
proof
  let s be State of SCMPDS,I be Program of SCMPDS;
A1: Initialize s +* I = s +* Initialize I by COMPOS_1:125;
  thus thesis by Th8,A1;
end;

theorem Th10:
  for s being State of SCMPDS, l being Element of NAT
  holds dom ProgramPart s misses dom Start-At(l,SCMPDS)
proof
  let s be State of SCMPDS,l be Element of NAT;
  now
    let x be set;
    assume
    x in dom ProgramPart s;
    then x is Element of NAT by COMPOS_1:34;
    then x is Element of NAT;
    then x <> IC SCMPDS by COMPOS_1:3;
    then not x in {IC SCMPDS} by TARSKI:def 1;
    hence not x in dom Start-At(l,SCMPDS) by FUNCOP_1:19;
  end;
  hence thesis by XBOOLE_0:3;
end;

theorem
  for s being State of SCMPDS, I,J being Program of SCMPDS, l being
  Element of NAT holds s +* (I +* Start-At(l,SCMPDS)),
  s +* (J +* Start-At(l,SCMPDS)) equal_outside NAT
proof
  let s be State of SCMPDS,I,J be Program of SCMPDS;
  let l be Element of NAT;
A1: now
    let a;
A2: not a in dom (I +* Start-At(l,SCMPDS)) by SCMPDS_4:61;
    not a in dom (J +* Start-At(l,SCMPDS)) by SCMPDS_4:61;
    hence (s +* (J +* Start-At(l,SCMPDS))).a = s.a by FUNCT_4:12
      .= (s +* (I +* Start-At(l,SCMPDS))).a by A2,FUNCT_4:12;
  end;
  IC (s +* (J +* Start-At(l,SCMPDS))) = IC (s +* J +* Start-At(l,SCMPDS))
  by FUNCT_4:15
    .= l by FUNCT_4:121
    .= IC (s +* I +* Start-At(l,SCMPDS)) by FUNCT_4:121
    .= IC (s +* (I +* Start-At(l,SCMPDS))) by FUNCT_4:15;
  hence thesis by A1,SCMPDS_4:11;
end;

theorem Th12:
  for s1,s2 be State of SCMPDS,I,J be Program of SCMPDS holds
  DataPart s1 = DataPart s2 implies Initialize s1 +* I, Initialize s2 +* J
  equal_outside NAT
proof
  let s1,s2 be State of SCMPDS,I,J be Program of SCMPDS;
  assume
A1: DataPart s1 = DataPart s2;
  set S1 = Initialize s1 +* I, S2 = Initialize s2 +* J;
A2: Initialize s1 +* I = s1 +* Initialize I by COMPOS_1:125;
A3: Initialize s2 +* J = s2 +* Initialize J by COMPOS_1:125;
A4: IC S1 =  0 & IC S2 =  0 by A2,A3,FUNCT_4:26,SCMPDS_5:18;
  DataPart S1 = DataPart s1 by Th9
    .= DataPart S2 by A1,Th9;
  hence thesis by A4,Th4;
end;

canceled;

theorem Th14:
  for s being State of SCMPDS, l1,l2 being Element of NAT
  holds s +* Start-At(l1,SCMPDS) +* Start-At(l2,SCMPDS) =
  s +* Start-At(l2,SCMPDS)
proof
  let s be State of SCMPDS;
  let l1,l2 be Element of NAT;
A1: dom Start-At(l1,SCMPDS) = {IC SCMPDS} by FUNCOP_1:19
    .= dom Start-At(l2,SCMPDS) by FUNCOP_1:19;
  thus s +* Start-At(l1,SCMPDS) +* Start-At(l2,SCMPDS) =
  s +* (Start-At(l1,SCMPDS) +* Start-At(l2,SCMPDS)) by FUNCT_4:15
    .= s +* Start-At(l2,SCMPDS) by A1,FUNCT_4:20;
end;

theorem Th15:
  card (i ';' I)= card I + 1
proof
  thus card (i ';' I) = card (Load i ';' I)
    .=card (Load i) + card I by AFINSQ_1:20
    .=card I+1 by SCMPDS_5:6;
end;

theorem Th16:
  (i ';' I). 0=i
proof
  i ';' I=Load i ';' I &  0 in dom Load i by SCMPDS_5:2;
  hence (i ';' I). 0 =(Load i). 0 by AFINSQ_1:def 4
    .=i by SCMPDS_5:4;
end;

theorem Th17:
  I c= Initialize stop I
proof
  set pI=stop I;
A1: I c= pI by AFINSQ_1:78;
  pI c= Initialize pI by COMPOS_1:126;
  hence thesis by A1,XBOOLE_1:1;
end;

theorem Th18:
  loc in dom I implies loc in dom (stop I)
proof
  dom I c= dom (I ';' Stop SCMPDS) by AFINSQ_1:24;
  then
A1: dom I c= dom stop I;
  assume
  loc in dom I;
  hence thesis by A1;
end;

theorem Th19:
  loc in dom I implies (stop I).loc=I.loc
proof
  assume
A1: loc in dom I;
  thus (stop I).loc =(I ';' Stop SCMPDS).loc
    .=I.loc by A1,AFINSQ_1:def 4;
end;

theorem Th20:
  loc in dom I implies (Initialize stop I).loc=I.loc
proof
  assume
A1: loc in dom I;
  then loc in dom stop (I) by Th18;
  hence (Initialize stop I).loc=(stop I).loc by COMPOS_1:129
    .=I.loc by A1,Th19;
end;

theorem
  IC (Initialize s+* I)= 0
 proof
A1: s+* Initialize I = Initialize s+* I by COMPOS_1:125;
  thus thesis by A1,FUNCT_4:26,SCMPDS_5:18;
 end;

theorem Th22:
  CurInstr(ProgramPart(s+* Initialize stop(i ';' I)),
  Initialize s+* stop(i ';' I)) = i
proof
  set iI=i ';' I, s3=s+*Initialize stop iI;
A1: s+*Initialize stop iI = Initialize s+*stop iI by COMPOS_1:125;
A2:  0 in dom Load i by SCMPDS_5:2;
  card iI=card I +1 by Th15;
  then
A3:  0 in dom iI by AFINSQ_1:70;
A4:  (ProgramPart (Initialize s+* stop(i ';' I)))/.
IC (Initialize s+* stop(i ';' I))
 = (Initialize s+* stop(i ';' I)).IC (Initialize s+* stop(i ';' I))
 by COMPOS_1:38;
  iI c= Initialize stop iI by Th17;
  then dom iI c= dom Initialize stop iI by GRFUNC_1:8;
  then s3. 0 = (Initialize stop iI). 0 by A3,FUNCT_4:14
    .= iI. 0 by A3,Th20
    .=(Load i ';' I). 0
    .=(Load i). 0 by A2,AFINSQ_1:def 4
    .=i by SCMPDS_5:4;
  hence thesis by A4,A1,FUNCT_4:26,SCMPDS_5:18;
end;

theorem Th23:
  for s being State of SCMPDS,m1,m2 being Element of NAT st IC s=
   m1 holds ICplusConst(s,m2)= (m1+m2)
proof
  let s be State of SCMPDS,m1,m2 be Element of NAT;
A1: ex m st m = IC s & ICplusConst(s,m2) = abs(m+m2) by SCMPDS_2:def 20;
  assume
  IC s= m1;
  hence thesis by A1,ABSVALUE:def 1;
end;

theorem Th24:
  for I,J being Program of SCMPDS holds Shift(stop J,card I) c=
  stop(I ';' J)
proof
  let I,J be Program of SCMPDS;
  stop(I ';' J) =I ';' J ';' Stop SCMPDS
    .=I ';' (J ';' Stop SCMPDS) by AFINSQ_1:30
    .=I ';' (stop J);
  then stop(I ';' J) = I +* Shift(stop J, card I);
  hence thesis by FUNCT_4:26;
end;

theorem Th25:
  (card I) in dom (stop I) & (stop I).(card I) = halt
  SCMPDS
proof
  set SS=Stop SCMPDS;
  set pI=stop I;
  card pI=card I+1 by SCMPDS_5:7;
  then card I <card pI by XREAL_1:31;
  hence (card I) in dom pI by AFINSQ_1:70;
  pI.(0+card I) =halt SCMPDS by Lm1,Lm2,AFINSQ_1:def 4;
  hence thesis;
end;

theorem Th26:
  for x,l being Element of NAT holds IExec(J,s).x
  = (IExec(I,s) +* Start-At(l,SCMPDS)).x
proof
  let x,l be Element of NAT;
  dom Start-At(l,SCMPDS) = {IC SCMPDS} & x <> IC SCMPDS
  by COMPOS_1:3,FUNCOP_1:19;
  then
A1: not x in dom Start-At(l,SCMPDS) by TARSKI:def 1;
A2: dom ProgramPart s = A & x in NAT by COMPOS_1:34;
  thus IExec(J,s).x = (Result(ProgramPart(Initialize s+*stop J),
    Initialize s+*stop J) +* s | A).x
    .=(s | A).x by A2,FUNCT_4:14
    .= (Result(ProgramPart(Initialize s+*stop I),Initialize s+*stop I) +* s |
A).x by A2,FUNCT_4:14
    .= IExec(I,s).x
    .= (IExec(I,s) +* Start-At(l,SCMPDS)).x by A1,FUNCT_4:12;
end;

theorem Th27:
  for x,l being Element of NAT holds IExec(I,s).x
  = (s +* Start-At(l,SCMPDS)).x
proof
  let x,l be Element of NAT;
  dom Start-At(l,SCMPDS) = {IC SCMPDS} & x <> IC SCMPDS
  by COMPOS_1:3,FUNCOP_1:19;
  then
A1: not x in dom Start-At(l,SCMPDS) by TARSKI:def 1;
A2: dom ProgramPart s = A by COMPOS_1:34;
  thus IExec(I,s).x = (Result(ProgramPart(Initialize s+*stop I),
     Initialize s+*stop I) +* s | A).x
    .=(s | A).x by A2,FUNCT_4:14
    .= s.x by FUNCT_1:72
    .= (s+* Start-At(l,SCMPDS)).x by A1,FUNCT_4:12;
end;

theorem
  for s being 0-started State of SCMPDS, i being No-StopCode parahalting
Instruction of SCMPDS,J being parahalting shiftable Program of SCMPDS, a being
  Int_position holds IExec(i ';' J,s).a = IExec(J,Exec(i,Initialize s)).a
proof
  let s be 0-started State of SCMPDS,
      i be No-StopCode parahalting Instruction of SCMPDS,
  J be parahalting shiftable Program of SCMPDS, a be Int_position;
  thus IExec(i ';' J,s).a = IExec(Load i ';' J,s).a
    .= IExec(J,IExec(Load i,s)).a by SCMPDS_5:39
    .= IExec(J,Exec(i,Initialize s)).a by SCMPDS_5:45;
end;

theorem Th29:
  for a being Int_position,k1,k2 being Integer holds (a,k1)
  <>0_goto k2 <> halt SCMPDS
proof
  let a be Int_position,k1,k2 be Integer;
  InsCode ((a,k1)<>0_goto k2) = 4 by SCMPDS_2:25;
  hence thesis by SCMPDS_2:21,93;
end;

theorem Th30:
  for a being Int_position,k1,k2 being Integer holds (a,k1)
  <=0_goto k2 <> halt SCMPDS
proof
  let a be Int_position,k1,k2 be Integer;
  InsCode ((a,k1)<=0_goto k2) = 5 by SCMPDS_2:26;
  hence thesis by SCMPDS_2:21,93;
end;

theorem Th31:
  for a being Int_position,k1,k2 being Integer holds (a,k1)
  >=0_goto k2 <> halt SCMPDS
proof
  let a be Int_position,k1,k2 be Integer;
  InsCode ((a,k1)>=0_goto k2) = 6 by SCMPDS_2:27;
  hence thesis by SCMPDS_2:21,93;
end;

definition
  let k1;
  func Goto k1 -> Program of SCMPDS equals
  Load goto k1;
  coherence;
end;

registration
  let n be Element of NAT;
  cluster goto (n+1) -> No-StopCode;
  correctness by SCMPDS_5:25;
  cluster goto -(n+1) -> No-StopCode;
  correctness
  proof
    -(n+1) <> 0;
    hence thesis by SCMPDS_5:25;
  end;
end;

registration
  let n be Element of NAT;
  cluster Goto (n+1) -> halt-free;
  correctness;
  cluster Goto -(n+1) -> halt-free;
  correctness;
end;

canceled;

theorem Th33:
   0 in dom Goto k1 & (Goto k1). 0 = goto k1 by AFINSQ_1:38,69;

begin :: The predicates of is_closed_on and is_halting_on

definition
  let I be Program of SCMPDS;
  let s be State of SCMPDS;
  pred I is_closed_on s means
  :Def2:
  for k being Element of NAT holds
   IC Comput(ProgramPart(Initialize s+*stop I),Initialize s+*stop I,k)
    in dom stop I;
  pred I is_halting_on s means
  :Def3:
  ProgramPart(Initialize s+*stop I) halts_on Initialize s+*stop I;
end;

theorem Th34:
  for I being Program of SCMPDS holds I is paraclosed iff
  for s being State of SCMPDS holds I is_closed_on s
proof
  let I be Program of SCMPDS;
  hereby
    assume
A1: I is paraclosed;
    let s be State of SCMPDS;
:::    Initialize
    stop I c= Initialize s +* stop I by FUNCT_4:26;
    then for n holds
     IC Comput(ProgramPart(Initialize s+*stop I),Initialize s+*stop I,n)
      in dom stop I by A1,SCMPDS_4:def 9;
    hence I is_closed_on s by Def2;
  end;
  assume
A2: for s being State of SCMPDS holds I is_closed_on s;
    let s be 0-started State of SCMPDS;
    let k be Element of NAT;
A3:  Initialize s = s by COMPOS_1:78;
    assume
:::    Initialize
    stop I c= s;
    then
A4: s = Initialize s +* stop I by A3,FUNCT_4:79;
    I is_closed_on s by A2;
    hence IC Comput(ProgramPart(s),s,k) in dom stop I by A4,Def2;
end;

theorem Th35:
  for I being Program of SCMPDS holds I is parahalting iff for s
  being State of SCMPDS holds I is_halting_on s
proof
  let I be Program of SCMPDS;
  hereby
    assume
A1: I is parahalting;
    let s be State of SCMPDS;
    ProgramPart(Initialize s+*stop I) halts_on Initialize s+*stop I
     by A1,FUNCT_4:26,SCMPDS_4:def 10;
    hence I is_halting_on s by Def3;
  end;
  assume
A2: for s being State of SCMPDS holds I is_halting_on s;
    let s be 0-started State of SCMPDS;
A3:  Initialize s = s by COMPOS_1:78;
    assume
:::    Initialize
    stop I c= s;
    then
A4: s = Initialize s +* stop I by A3,FUNCT_4:79;
    I is_halting_on s by A2;
    hence ProgramPart s halts_on s by A4,Def3;
end;

theorem Th36:
  for s1,s2 being State of SCMPDS, I being Program of SCMPDS st
  DataPart s1 = DataPart s2 holds I is_closed_on s1 implies I is_closed_on s2
proof
  let s1,s2 be State of SCMPDS,I be Program of SCMPDS;
  set pI=stop I, S1 = Initialize s1 +* pI, S2 = Initialize s2 +* pI;
A1: s1 +* Initialize pI = Initialize s1 +* pI by COMPOS_1:125;
A2: s2 +* Initialize pI = Initialize s2 +* pI by COMPOS_1:125;
  assume
A3: DataPart s1 = DataPart s2;
A4: Comput(ProgramPart(S2),S2,0) = Initialize s2 +* pI by EXTPRO_1:3;
A5: Comput(ProgramPart(S1),S1,0) = Initialize s1 +* pI by EXTPRO_1:3;
  then
A6: DataPart Comput(ProgramPart(S1),S1,0) = DataPart s1 by Th9
    .= DataPart Comput(ProgramPart(S2),S2,0) by A3,A4,Th9;
  defpred P[Element of NAT] means IC Comput(ProgramPart(S1),S1,$1) = IC Comput(
ProgramPart(S2),S2,$1
  ) & CurInstr(ProgramPart Comput(ProgramPart(S1),S1,$1),Comput(ProgramPart(S1)
,S1,$1))
   = CurInstr(ProgramPart Comput(ProgramPart(S2),S2,$1),Comput(ProgramPart(S2),
S2,$1)) & DataPart
  Comput(ProgramPart(S1),S1,$1) = DataPart Comput(ProgramPart(S2),S2,$1);
A7:  0 in dom pI by COMPOS_1:135;
A8: pI c= Initialize pI by COMPOS_1:126;
  then
A9: dom pI c= dom Initialize pI by GRFUNC_1:8;
  assume
A10: I is_closed_on s1;
A11: now
    let k be Element of NAT;
A12: ProgramPart S2 = ProgramPart Comput(ProgramPart(
S2),S2,k)
by AMI_1:123;
A13: Comput(ProgramPart(S2),S2,k+1) = Following(ProgramPart S2,
Comput(ProgramPart(S2),S2,k)) by EXTPRO_1:4
      .= Exec(CurInstr(ProgramPart Comput(ProgramPart(S2),S2,k),Comput(
ProgramPart(S2),S2,k)),
      Comput(ProgramPart(S2),S2,k)) by A12;
    assume
A14: P[k];
    then
A15: for a holds Comput(ProgramPart(S1),S1,k).a = Comput(ProgramPart(S2),S2,k).
a by SCMPDS_4:23;
    Initialize pI c= Initialize s2 +* pI by A2,FUNCT_4:26;
    then pI c= Initialize s2 +* pI by A8,XBOOLE_1:1;
    then
A16: pI c= Comput(ProgramPart(S2),S2,k+1) by AMI_1:81;
A17: IC Comput(ProgramPart(S1),S1,k+1) in dom pI by A10,Def2;
A18: ProgramPart S1 = ProgramPart Comput(ProgramPart(
S1),S1,k)
by AMI_1:123;
A19: Comput(ProgramPart(S1),S1,k+1) = Following(ProgramPart S1,
Comput(ProgramPart(S1),S1,k)) by EXTPRO_1:4
      .= Exec(CurInstr(ProgramPart Comput(ProgramPart(S1),S1,k),Comput(
ProgramPart(S1),S1,k)),
      Comput(ProgramPart(S1),S1,k)) by A18;
    then
A20: Comput(ProgramPart(S1),S1,k+1),Comput(ProgramPart(S2),S2,k+1)
equal_outside NAT by A14,A15,A13,SCMPDS_4:11,15;
    Comput(ProgramPart(S1),S1,k),Comput(ProgramPart(S2),S2,k) equal_outside A
by A14,A15,SCMPDS_4:11;
    then
A21: IC Comput(ProgramPart(S1),S1,k+1) = IC Comput(ProgramPart(S2),S2,k+1) by
A14,A19,A13,COMPOS_1:24,SCMPDS_4:15;
A22
:  (ProgramPart Comput(ProgramPart(S1),S1,k+1))/.IC Comput(ProgramPart(S1),S1,
k+1)
 = Comput(ProgramPart(S1),S1,k+1).IC Comput(ProgramPart(S1),S1,k+1) by
COMPOS_1:38;
A23
:  (ProgramPart Comput(ProgramPart(S2),S2,k+1))/.IC Comput(ProgramPart(S2),S2,
k+1)
 = Comput(ProgramPart(S2),S2,k+1).IC Comput(ProgramPart(S2),S2,k+1) by
COMPOS_1:38;
    Initialize pI c= Initialize s1 +* pI by A1,FUNCT_4:26;
    then pI c= Initialize s1 +* pI by A8,XBOOLE_1:1;
    then pI c= Comput(ProgramPart(S1),S1,k+1) by AMI_1:81;
    then CurInstr(ProgramPart Comput(ProgramPart(S1),S1,k+1),Comput(ProgramPart
(S1),S1,k+1))
     = pI.IC Comput(ProgramPart(S1),S1,k+1) by A17,A22,GRFUNC_1:8
      .= CurInstr(ProgramPart Comput(ProgramPart(S2),S2,k+1),Comput(ProgramPart
(S2),S2,k+1))
       by A16,A21,A17,A23,GRFUNC_1:8;
    hence P[k+1] by A20,COMPOS_1:24,138;
  end;
A24: IC SCMPDS in dom Initialize pI by COMPOS_1:def 16;
  then
A25: IC Comput(ProgramPart(S2),S2,0) = IC Initialize pI
 by A4,A2,FUNCT_4:14
    .=  0 by COMPOS_1:def 16;
A26
:  (ProgramPart Comput(ProgramPart(S1),S1,0))/.IC Comput(ProgramPart(S1),S1,0)
 = Comput(ProgramPart(S1),S1,0).IC Comput(ProgramPart(S1),S1,0) by COMPOS_1:38;
A27
:  (ProgramPart Comput(ProgramPart(S2),S2,0))/.IC Comput(ProgramPart(S2),S2,0)
 = Comput(ProgramPart(S2),S2,0).IC Comput(ProgramPart(S2),S2,0) by COMPOS_1:38;
A28: IC Comput(ProgramPart(S1),S1,0) = IC Initialize pI
by A24,A5,A1,FUNCT_4:14
    .=  0 by COMPOS_1:def 16;
  then CurInstr(ProgramPart Comput(ProgramPart(S1),S1,0),Comput(ProgramPart(S1)
,S1,0)) = (Initialize pI). 0
   by A5,A9,A7,A26,A1,FUNCT_4:14
    .= CurInstr(ProgramPart Comput(ProgramPart(S2),S2,0),Comput(ProgramPart(S2)
,S2,0))
     by A4,A25,A9,A7,A27,A2,FUNCT_4:14;
  then
A29: P[0] by A28,A25,A6;
  now
    let k be Element of NAT;
A30: for k being Element of NAT holds P[k] from NAT_1:sch 1(A29,A11);
    IC Comput(ProgramPart(S1),S1,k) in dom pI by A10,Def2;
    hence IC Comput(ProgramPart(S2),S2,k) in dom pI by A30;
  end;
  hence thesis by Def2;
end;

theorem
  for s1,s2 being State of SCMPDS,I being Program of SCMPDS st DataPart
  s1 = DataPart s2 holds I is_closed_on s1 & I is_halting_on s1 implies I
  is_closed_on s2 & I is_halting_on s2
proof
  let s1,s2 be State of SCMPDS,I be Program of SCMPDS;
  set pI=stop I, S1 = Initialize s1 +* pI, S2 = Initialize s2 +* pI;
A1: s1 +* Initialize pI = Initialize s1 +* pI by COMPOS_1:125;
A2: s2 +* Initialize pI = Initialize s2 +* pI by COMPOS_1:125;
  defpred P[Element of NAT] means IC Comput(ProgramPart(S1),S1,$1) = IC Comput(
ProgramPart(S2),S2,$1
  ) & CurInstr(ProgramPart (S1),Comput(ProgramPart(S1),S1,$1))
   = CurInstr(ProgramPart (S2),Comput(ProgramPart(S2),S2,$1))
    & DataPart
  Comput(ProgramPart(S1),S1,$1) = DataPart Comput(ProgramPart(S2),S2,$1);
A3: Comput(ProgramPart(S1),S1,0) = Initialize s1 +* pI by EXTPRO_1:3;
A4: Comput(ProgramPart(S2),S2,0) = Initialize s2 +* pI by EXTPRO_1:3;
  assume
  DataPart s1 = DataPart s2;
  then
A5: Comput(ProgramPart(S1),S1,0),Comput(ProgramPart(S2),S2,0) equal_outside A
by A3,A4,Th12;
A6:  0 in dom pI by COMPOS_1:135;
A7: pI c= Initialize pI by COMPOS_1:126;
  then
A8: dom pI c= dom Initialize pI by GRFUNC_1:8;
  assume
A9: I is_closed_on s1;
A10: now
    let k be Element of NAT;
A11: ProgramPart S2 = ProgramPart Comput(ProgramPart(S2),S2,k)
by AMI_1:123;
A12: Comput(ProgramPart(S2),S2,k+1) = Following(ProgramPart S2,
Comput(ProgramPart(S2),S2,k)) by EXTPRO_1:4
      .= Exec(CurInstr(ProgramPart Comput(ProgramPart(S2),S2,k),Comput(
ProgramPart(S2),S2,k)),
      Comput(ProgramPart(S2),S2,k)) by A11;
    assume
A13: P[k];
    then for a holds Comput(ProgramPart(S1),S1,k).a = Comput(ProgramPart(S2),S2
,k).a by SCMPDS_4:23;
    then
A14: Comput(ProgramPart(S1),S1,k),Comput(ProgramPart(S2),S2,k) equal_outside A
by A13,SCMPDS_4:11;
    Initialize pI c= Initialize s2 +* pI by A2,FUNCT_4:26;
    then pI c= Initialize s2 +* pI by A7,XBOOLE_1:1;
    then
A15: pI c= Comput(ProgramPart(S2),S2,k+1) by AMI_1:81;
A16: IC Comput(ProgramPart(S1),S1,k+1) in dom pI by A9,Def2;
A17: Comput(ProgramPart(S1),S1,k+1) = Following(ProgramPart S1,
Comput(ProgramPart(S1),S1,k))
 by EXTPRO_1:4
      .= Exec(CurInstr(ProgramPart (S1),Comput(ProgramPart(S1),S1,k)),
      Comput(ProgramPart(S1),S1,k));
    then
A18: IC Comput(ProgramPart(S1),S1,k+1) = IC Comput(ProgramPart(S2),S2,k+1) by
A13,A14,A12,A11,COMPOS_1:24,SCMPDS_4:15;
A19
:  (ProgramPart Comput(ProgramPart(S1),S1,k+1))/.IC Comput(ProgramPart(S1),S1,
k+1)
 = Comput(ProgramPart(S1),S1,k+1).IC Comput(ProgramPart(S1),S1,k+1) by
COMPOS_1:38;
A20
:  (ProgramPart Comput(ProgramPart(S2),S2,k+1))/.IC Comput(ProgramPart(S2),S2,
k+1)
 = Comput(ProgramPart(S2),S2,k+1).IC Comput(ProgramPart(S2),S2,k+1) by
COMPOS_1:38;
A21: ProgramPart S1 = ProgramPart Comput(ProgramPart(S1),S1,k+1)
by AMI_1:123;
A22: ProgramPart S2 = ProgramPart Comput(ProgramPart(S2),S2,k+1)
by AMI_1:123;
    Initialize pI c= s1 +* Initialize pI by FUNCT_4:26;
    then pI c= Initialize s1 +* pI by A7,A1,XBOOLE_1:1;
    then pI c= Comput(ProgramPart(S1),S1,k+1) by AMI_1:81;
    then CurInstr(ProgramPart S1,Comput(ProgramPart(S1),S1,k+1))
     = pI.IC Comput(ProgramPart(S1),S1,k+1) by A16,A19,A21,GRFUNC_1:8
      .= CurInstr(ProgramPart S2,Comput(ProgramPart(S2),S2,k+1))
       by A15,A18,A16,A20,A22,GRFUNC_1:8;
    hence P[k+1] by A13,A14,A17,A12,A18,A11,COMPOS_1:138,SCMPDS_4:15;
  end;
A23: IC SCMPDS in dom Initialize pI by COMPOS_1:def 16;
  then
A24: IC Comput(ProgramPart(S2),S2,0) = IC Initialize pI
by A4,A2,FUNCT_4:14
    .=  0 by COMPOS_1:def 16;
  assume
  I is_halting_on s1;
  then ProgramPart(Initialize s1+*pI) halts_on Initialize s1+*pI by Def3;
  then consider m such that
A25: CurInstr(ProgramPart S1,Comput(ProgramPart(S1),S1,m))
 = halt SCMPDS by EXTPRO_1:30;
A26
:  (ProgramPart Comput(ProgramPart(S1),S1,0))/.IC Comput(ProgramPart(S1),S1,0)
 = Comput(ProgramPart(S1),S1,0).IC Comput(ProgramPart(S1),S1,0) by COMPOS_1:38;
A27
:  (ProgramPart Comput(ProgramPart(S2),S2,0))/.IC Comput(ProgramPart(S2),S2,0)
 = Comput(ProgramPart(S2),S2,0).IC Comput(ProgramPart(S2),S2,0) by COMPOS_1:38;
A28: IC Comput(ProgramPart(S1),S1,0) = IC Initialize pI
by A23,A3,A1,FUNCT_4:14
    .=  0 by COMPOS_1:def 16;
  then CurInstr(ProgramPart S1,Comput(ProgramPart(S1),S1,0))
   = (Initialize pI). 0 by A3,A8,A6,A26,A1,FUNCT_4:14
    .= CurInstr(ProgramPart S2,Comput(ProgramPart(S2),S2,0))
     by A4,A24,A8,A6,A27,A2,FUNCT_4:14;
  then
A29: P[0] by A28,A24,A5,COMPOS_1:138;
  now
    let k be Element of NAT;
A30: for k being Element of NAT holds P[k] from NAT_1:sch 1(A29,A10);
    IC Comput(ProgramPart(S1),S1,k) in dom pI by A9,Def2;
    hence IC Comput(ProgramPart(S2),S2,k) in dom pI by A30;
  end;
  hence I is_closed_on s2 by Def2;
  for k being Element of NAT holds P[k] from NAT_1:sch 1(A29,A10);
  then CurInstr(ProgramPart S2,Comput(ProgramPart(S2),S2,m))
   = halt SCMPDS by A25;
  then ProgramPart(Initialize s2+*pI) halts_on Initialize s2+*pI by EXTPRO_1:30
;
  hence thesis by Def3;
end;

theorem Th38:
  for s being State of SCMPDS, I,J being Program of SCMPDS holds I
  is_closed_on s iff I is_closed_on Initialize s +* J
proof
  let s be State of SCMPDS,I,J be Program of SCMPDS;
  DataPart s = DataPart(Initialize s +* J) by Th9;
  hence thesis by Th36;
end;

theorem Th39:
  for I,J being Program of SCMPDS,s being State of SCMPDS st I
  is_closed_on s & I is_halting_on s
   holds (for k being Element of NAT st k <=
LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I) holds IC
Comput(ProgramPart((Initialize s +*
stop
  I)), (Initialize s +* stop
  I),k) = IC Comput(ProgramPart((Initialize s +* stop (I ';' J))),
   Initialize s +* stop (I ';' J),k)) &
DataPart
Comput(ProgramPart((Initialize s +* stop I)), (Initialize s +* stop I), (
LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I))) =
  DataPart Comput(ProgramPart((Initialize s +* stop (I ';' J))),
   Initialize s +* stop (I ';' J), (LifeSpan(ProgramPart(Initialize s +*
  stop I),Initialize s +*
  stop I)))
proof
  let I,J be Program of SCMPDS,s be State of SCMPDS;
  assume
A1: I is_closed_on s;
  set pI=stop I, pIJ=stop (I ';' J),
  s1=Initialize s +* pI, IL=NAT;
A2: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A3: Initialize s1 +* pIJ = s1 +* stop (I ';' J)
     by COMPOS_1:78;
  defpred X[Element of NAT] means $1 <= LifeSpan(ProgramPart(s1),s1) implies
Comput(ProgramPart(
s1), s1,$1
  ), Comput(ProgramPart(Initialize s1+*pIJ), Initialize s1+*pIJ,$1)
   equal_outside IL;
  assume
  I is_halting_on s;
  then
A4: ProgramPart s1 halts_on s1 by Def3;
A5: Initialize pI=Initialize pI & Initialize pI c= s1
by A2,FUNCT_4:26;
A6: for m st X[m] holds X[m+1]
  proof
    set JS=J ';' Stop SCMPDS;
    set S1 = s1, S2 = Initialize s1 +* pIJ;
    let m;
    assume
A7: m <= LifeSpan(ProgramPart(s1),s1) implies Comput(ProgramPart(s1), s1,m),
Comput(ProgramPart
((Initialize s1+*pIJ
    )), (Initialize s1+*pIJ
    ),m) equal_outside IL;
A8: pIJ c= Comput(ProgramPart(S2),S2,m) by AMI_1:81,FUNCT_4:26;
A9: ProgramPart S1 = ProgramPart Comput(ProgramPart(S1),S1,m)
by AMI_1:123;
A10: Comput(ProgramPart(S1),S1,m+1) =
Following(ProgramPart S1,Comput(ProgramPart(S1),S1,m)
)
 by EXTPRO_1:4
      .= Exec(CurInstr(ProgramPart Comput(ProgramPart(S1),S1,m),Comput(
ProgramPart(S1),S1,m)),
      Comput(ProgramPart(S1),S1,m)) by A9;
A11: pIJ =I ';' J ';' Stop SCMPDS
      .=I ';' JS by AFINSQ_1:30;
    dom(I ';' JS) = dom (I +* Shift(JS, card I))
      .= dom I \/ dom Shift(JS, card I) by FUNCT_4:def 1;
    then
A12: dom I c= dom(I ';' JS) by XBOOLE_1:7;
A13: ProgramPart S2 = ProgramPart Comput(ProgramPart(S2),S2,m)
by AMI_1:123;
A14: Comput(ProgramPart(S2),S2,m+1) =
Following(ProgramPart S2,Comput(ProgramPart(S2),S2,m)
)
 by EXTPRO_1:4
      .= Exec(CurInstr(ProgramPart Comput(ProgramPart(S2),S2,m),Comput(
ProgramPart(S2),S2,m)),
      Comput(ProgramPart(S2),S2,m)) by A13;
A15: IC Comput(ProgramPart(S1),S1,m) in dom pI by A1,Def2;
A16
:  (ProgramPart Comput(ProgramPart(S1),S1,m))/.IC Comput(ProgramPart(S1),S1,m)
 = Comput(ProgramPart(S1),S1,m).IC Comput(ProgramPart(S1),S1,m) by COMPOS_1:38;
A17: ProgramPart S1 = ProgramPart Comput(ProgramPart(S1),S1,m)
by AMI_1:123;
    dom pI misses dom Start-At(0,SCMPDS) by COMPOS_1:130;
    then pI c= Initialize pI by FUNCT_4:33;
    then pI c= s1 by A5,XBOOLE_1:1;
    then pI c= Comput(ProgramPart(S1),S1,m) by AMI_1:81;
    then
A18: CurInstr(ProgramPart S1,Comput(ProgramPart(S1),
S1,m))
 = pI.IC (Comput(ProgramPart(S1),S1,m)) by A15,A16,A17,GRFUNC_1:8;
    assume
A19: m+1 <= LifeSpan(ProgramPart(s1),s1);
    then m < LifeSpan(ProgramPart(s1),s1) by NAT_1:13;
    then pI.IC(Comput(ProgramPart(S1),S1,m)) <> halt SCMPDS by A4,A18,
EXTPRO_1:def 14;
    then
A20: IC Comput(ProgramPart(S1),S1,m) in dom I by A15,SCMPDS_5:3;
A21
:  (ProgramPart Comput(ProgramPart(S2),S2,m))/.IC Comput(ProgramPart(S2),S2,m)
 = Comput(ProgramPart(S2),S2,m).IC Comput(ProgramPart(S2),S2,m) by COMPOS_1:38;
    CurInstr(ProgramPart Comput(ProgramPart(S1),S1,m),Comput(ProgramPart(S1),S1
,m))
    = (I ';' Stop SCMPDS).IC (Comput(ProgramPart(S1),S1,m
    )) by A18,A17
      .=I.IC (Comput(ProgramPart(S1),S1,m)) by A20,AFINSQ_1:def 4
      .=pIJ.IC(Comput(ProgramPart(S1),S1,m)) by A20,A11,AFINSQ_1:def 4
      .=(Comput(ProgramPart(S2),S2,m)).IC(Comput(ProgramPart(S1),S1,m)) by A8
,A20,A11,A12,GRFUNC_1:8
      .= CurInstr(ProgramPart Comput(ProgramPart(S2),S2,m),Comput(ProgramPart(
S2),S2,m))
       by A7,A19,A21,COMPOS_1:24,NAT_1:13;
    hence thesis by A7,A19,A10,A14,NAT_1:13,SCMPDS_4:15;
  end;
A22: s1 +* Initialize pIJ = Initialize s1 +* pIJ by COMPOS_1:125;
  Comput(ProgramPart(s1), s1,0) = s1 &
  Comput(ProgramPart((Initialize s1+*pIJ)), (s1+*
Initialize pIJ),0) = Initialize s1+*pIJ by A22,EXTPRO_1:3;
  then
A23: X[0] by A3,A22,FUNCT_7:132;
A24: for m holds X[m] from NAT_1:sch 1(A23,A6);

A25: Initialize s1+*pIJ = Initialize Initialize s +* pI+*pIJ
            by COMPOS_1:83
    .=Initialize s +* (pI +* pIJ) by A2,A22,FUNCT_4:15
    .= Initialize s +* pIJ by SCMPDS_5:17
    .=Initialize s+*pIJ by COMPOS_1:125;
  hence
  for k be Element of NAT st k <= LifeSpan(ProgramPart(s1),s1) holds IC Comput(
ProgramPart(s1),
s1,k)
  =IC Comput(ProgramPart(Initialize s +* pIJ), Initialize s +* pIJ,k)
  by A24,COMPOS_1:24;
  thus thesis by A25,A24,COMPOS_1:138;
  set m=LifeSpan(ProgramPart(s1),s1);
end;

theorem Th40:
  for I being Program of SCMPDS,k be Element of NAT st I
  is_closed_on s & I is_halting_on s &
  k < LifeSpan(ProgramPart(Initialize s +* stop(I)),Initialize s +* stop(I))
  holds IC Comput(ProgramPart(Initialize s +* stop I),
  (Initialize s +* stop(I)),k) in dom I
proof
  let I be Program of SCMPDS,k be Element of NAT;
  set ss= Initialize s +* stop I, m=LifeSpan(ProgramPart(ss),ss), Sp
=Stop SCMPDS;
A1: s +* Initialize stop I = Initialize s +* stop I by COMPOS_1:125;
  assume that
A2: I is_closed_on s and
A3: I is_halting_on s and
A4: k < m;
  set Sk= Comput(ProgramPart(ss), ss,k), Ik=IC Sk;
A5: Ik in dom stop(I) by A2,Def2;
  reconsider n = Ik as Element of NAT;
  Initialize stop I c= ss & stop I c= Initialize stop I
   by A1,COMPOS_1:126,FUNCT_4:26;
  then
A6: stop I c= ss by XBOOLE_1:1;
A7: ProgramPart ss halts_on ss by A3,Def3;
A8: now
A9:  (ProgramPart Sk)/.IC Sk
 = Sk.IC Sk by COMPOS_1:38;
A10: ProgramPart ss = ProgramPart Sk
by AMI_1:123;
    assume
A11: n = card I;
    CurInstr(ProgramPart Sk,Sk) =ss.Ik by A9,AMI_1:54
      .=(stop I).(0+n) by A5,A6,GRFUNC_1:8
      .=halt SCMPDS by A11,Lm1,Lm2,AFINSQ_1:def 4;
    hence contradiction by A4,A7,A10,EXTPRO_1:def 14;
  end;
  card stop I=card I + 1 by SCMPDS_5:7;
  then n < card I + 1 by A5,AFINSQ_1:70;
  then n <= card I by INT_1:20;
  then n < card I by A8,XXREAL_0:1;
  hence thesis by AFINSQ_1:70;
end;

theorem Th41:
  for I,J being Program of SCMPDS,s being State of SCMPDS, k being
  Element of NAT st I is_closed_on s & I is_halting_on s &
   k < LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I)
 holds CurInstr(ProgramPart (Initialize s +* stop I),
      Comput(ProgramPart(Initialize s +* stop I), (Initialize s +* stop I)
,k)) =
  CurInstr(ProgramPart (Initialize s +* stop (I ';' J)),
Comput(ProgramPart(Initialize s +* stop (I ';' J)),
(Initialize s +* stop (I ';' J)),k))
proof
  let I,J be Program of SCMPDS,s be State of SCMPDS,k be Element of NAT;
  set s1=Initialize s+*stop I, s2=Initialize s +*stop (I ';' J);
  set s3= Comput(ProgramPart(s1), s1,k), s4= Comput(ProgramPart(s2), s2,k),
SS=Stop SCMPDS;
A1: s+*Initialize stop I = Initialize s+*stop I by COMPOS_1:125;
A2: s+*Initialize stop(I ';' J) = Initialize s+*stop(I ';' J) by COMPOS_1:125;
  assume that
A3: I is_closed_on s and
A4: I is_halting_on s & k < LifeSpan(ProgramPart(s1),s1);
A5: IC s3 in dom I by A3,A4,Th40;
A6: IC s3= IC s4 by A3,A4,Th39;
A7: IC s3 in dom stop(I) by A3,Def2;
  Initialize stop (I ';' J) c= s2 &
  stop (I ';' J) c= Initialize stop (I ';' J) by A2,COMPOS_1:126,FUNCT_4:26;
  then
A8: dom stop I c= dom stop (I ';' J) & stop (I ';' J) c= s2 by SCMPDS_5:16
,XBOOLE_1:1;
  Initialize stop I c= s1 & stop I c= Initialize stop I
   by A1,COMPOS_1:126,FUNCT_4:26;
  then
A9: stop I c= s1 by XBOOLE_1:1;
A10: stop (I ';' J) = I ';' J ';' SS
    .=I ';' (J ';' SS) by AFINSQ_1:30;
A11:  (ProgramPart s3)/.IC s3
 = s3.IC s3 by COMPOS_1:38;
A12:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A13: ProgramPart s1 = ProgramPart s3
by AMI_1:123;
A14: ProgramPart s2 = ProgramPart s4
by AMI_1:123;
  thus CurInstr(ProgramPart s1,s3) =s1.IC s3 by A11,A13,AMI_1:54
    .=(stop I).IC s3 by A7,A9,GRFUNC_1:8
    .=I.IC s3 by A5,AFINSQ_1:def 4
    .=(stop (I ';' J)).IC s3 by A5,A10,AFINSQ_1:def 4
    .=s2.IC s4 by A6,A7,A8,GRFUNC_1:8
    .=CurInstr(ProgramPart s2,s4) by A12,A14,AMI_1:54;
end;

theorem Th42: ::SCMPDS_5:32
  for I being halt-free Program of SCMPDS,s being State of
  SCMPDS, k being Element of NAT st I is_closed_on s & I is_halting_on s & k <
  LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I)
   holds CurInstr(ProgramPart
   Comput(ProgramPart(Initialize s +* stop I),
   Initialize s +* stop I,k),
   Comput(ProgramPart(Initialize s +* stop I),
   Initialize s +* stop I,k)) <> halt SCMPDS
proof
  let I be halt-free Program of SCMPDS,s be State of SCMPDS, k be Element of
  NAT;
  set ss=Initialize s +* stop I, s2= Comput(ProgramPart(ss), ss,k);
A1: s +* Initialize stop I = Initialize s +* stop I by COMPOS_1:125;
  assume
  I is_closed_on s & I is_halting_on s & k < LifeSpan(ProgramPart(ss),ss);
  then
A2: IC s2 in dom I by Th40;
A3:  (ProgramPart s2)/.IC s2
 = s2.IC s2 by COMPOS_1:38;
  Initialize stop I c= ss & I c= Initialize stop I by Th17,A1,FUNCT_4:26;
  then I c= ss by XBOOLE_1:1;
  then I c= s2 by AMI_1:81;
  then CurInstr(ProgramPart s2,s2)=I.IC s2 by A2,A3,GRFUNC_1:8;
  hence thesis by A2,SCMPDS_5:def 3;
end;

theorem Th43:
  for I being halt-free Program of SCMPDS,s being State of
  SCMPDS st I is_closed_on s & I is_halting_on s holds IC Comput(ProgramPart((
Initialize s +* stop I)),
(Initialize s +*
  stop I), LifeSpan(ProgramPart(Initialize s +* stop I),s +*
Initialize stop I)) =  card I
proof
  let I be halt-free Program of SCMPDS,s be State of SCMPDS;
  set s1=Initialize s +* stop I;
A1: s +* Initialize stop I = Initialize s +* stop I by COMPOS_1:125;
  assume that
A2: I is_closed_on s and
A3: I is_halting_on s;
  set Css= Comput(ProgramPart(s1), s1,LifeSpan(ProgramPart(s1),s1));
  reconsider n = IC Css as Element of NAT;
  Initialize stop I c= s1 & I c= Initialize stop I by Th17,A1,FUNCT_4:26;
  then
A4: I c= s1 by XBOOLE_1:1;
A5: ProgramPart s1 halts_on s1 by A3,Def3;
A6: ProgramPart Css = ProgramPart s1
by AMI_1:123;
  now
A7:  (ProgramPart Css)/.IC Css
 = Css.IC Css by COMPOS_1:38;
    assume
A8: IC Css in dom I;
    then I.IC Css=s1.IC Css by A4,GRFUNC_1:8
      .=CurInstr(ProgramPart Css,Css) by A7,AMI_1:54
      .=halt SCMPDS by A5,A6,EXTPRO_1:def 14;
    hence contradiction by A8,SCMPDS_5:def 3;
  end;
  then
A9: n >= card I by AFINSQ_1:70;
A10: card stop I =card I + 1 by SCMPDS_5:7;
  IC Css in dom stop(I) by A2,Def2;
  then n < card I + 1 by A10,AFINSQ_1:70;
  then n <= card I by NAT_1:13;
  hence thesis by A9,A1,XXREAL_0:1;
end;

Lm3:
 for I being halt-free Program of SCMPDS, J being Program of SCMPDS,
     s being State of SCMPDS st I is_closed_on s & I is_halting_on s
  holds IC
Comput(ProgramPart(Initialize s +* stop (I ';' Goto (card J + 1) ';' J )),
(Initialize s +* stop (I ';' Goto (card J + 1) ';' J )), (
LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I) + 1)) =
(card I + card J + 1) &
DataPart Comput(ProgramPart(Initialize s +* stop I), (Initialize s +* stop
I), (LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I)))
 = DataPart Comput(ProgramPart(Initialize s +* stop (I ';' Goto(card J +
1) ';' J)), (Initialize s +* stop (I ';' Goto (card J +
1) ';' J)),
(LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I) + 1)) &
(for k being Element of
NAT st k <= LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop
I)
 holds CurInstr(ProgramPart
  (Initialize s +*stop (I ';' Goto (card J + 1) ';' J )),
   Comput(ProgramPart(Initialize s +*stop (I ';' Goto (card J + 1) ';' J )),
   (Initialize s +*stop (I ';' Goto (card J + 1) ';' J )),k)) <> halt SCMPDS) &
 IC Comput(ProgramPart(Initialize s +* stop (I ';' Goto (card J + 1) ';' J)), (
Initialize s +* stop (I ';' Goto (card J + 1) ';' J)),
 (LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I)))
  =  card I &
 ProgramPart(Initialize s +* stop (I ';' Goto (card J + 1) ';' J))
  halts_on Initialize s +* stop (I ';' Goto (card J + 1) ';' J) &
 LifeSpan(ProgramPart(Initialize s +* stop (I ';' Goto (
card J + 1) ';' J)),Initialize s +* stop (I ';' Goto (
card J + 1) ';' J)) = LifeSpan(ProgramPart(Initialize s +* stop I),
   Initialize s +* stop I) + 1

proof
  let I be halt-free Program of SCMPDS, J be Program of SCMPDS, s be State
  of SCMPDS;
  assume
A1: I is_closed_on s;
  set G1=Goto (card J + 1), SS = Stop SCMPDS, J2 = G1 ';' J ';' SS, IJ=I ';'
G1 ';' J, pJ=stop IJ, s1 = Initialize s +* stop I,
  s2 = Initialize s +* pJ;
A2: s2 = s +* Initialize pJ by COMPOS_1:125;
A3: s1 = s +* Initialize stop I by COMPOS_1:125;
  assume
A4: I is_halting_on s;
  set sm= Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s1),s1));
A5: IJ =I ';' (G1 ';' J) by AFINSQ_1:30;
  then
A6: IC Comput(ProgramPart(s1), s1,LifeSpan(ProgramPart(s1),s1)) =IC sm by A1
,A4,Th39;
  then
A7: IC sm =  card I by A1,A4,Th43,A3;
A8:  0 in dom G1 by Th33;
A9: J2. 0 = (G1 ';' (J ';' SS)). 0 by AFINSQ_1:30
    .=G1. 0 by A8,AFINSQ_1:def 4
    .=goto (card J + 1) by Th33;
  pJ c= Initialize pJ by COMPOS_1:126;
  then
A10: dom pJ c= dom Initialize pJ by GRFUNC_1:8;
  card (G1 ';' J) = card G1 + card J by AFINSQ_1:20
    .=1 + card J by SCMPDS_5:6;
  then
A11: J2. (card J + 1)=J2.(0+card (G1 ';' J))
    .=halt SCMPDS by Lm1,Lm2,AFINSQ_1:def 4;
A12: card J2 = card (G1 ';' (J ';' SS)) by AFINSQ_1:30
    .=card G1 + card (J ';' SS) by AFINSQ_1:20
    .= 1 + card (J ';' SS) by SCMPDS_5:6;
  then
A13:  0 in dom J2 by AFINSQ_1:70;
A14: pJ =I ';' G1 ';' J ';' SS
    .=I ';' (G1 ';' J) ';' SS by AFINSQ_1:30
    .=I ';' J2 by AFINSQ_1:30;
  then
A15: card pJ = card I + card J2 by AFINSQ_1:20;
  then card I + 0 < card pJ by A12,XREAL_1:8;
  then
A16:  card I in dom pJ by AFINSQ_1:70;
A17: card SS = 1 by COMPOS_1:46;
A18: card J2 = 1 + (card J + card SS) by A12,AFINSQ_1:20
    .= card J + (1 + card SS);
  then card J + 1 < card J2 by A17,XREAL_1:8;
  then
A19:  (card J + 1) in dom J2 by AFINSQ_1:70;
  card pJ = card I + card J + 1 + 1 by A15,A18,A17;
  then card I + card J + 1 < card pJ by NAT_1:13;
  then
A20:  (card I + card J + 1) in dom pJ by AFINSQ_1:70;

A21:  (ProgramPart Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s1),s1)))/.IC
Comput(ProgramPart
(s2),s2,LifeSpan(ProgramPart(s1),s1))
 = Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s1),s1)).IC Comput(
ProgramPart(s2),s2,LifeSpan(ProgramPart s1,
s1)) by COMPOS_1:38;
A22: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s1),s1
))
by AMI_1:123;
A23: CurInstr(ProgramPart s2,
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s1),s1)))
 = Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s1),s1)).
   card I by A1,A4,A6,Th43,A21,A22,A3

    .= s2. card I by AMI_1:54
    .= (Initialize pJ). card I by A10,A16,A2,FUNCT_4:14
    .= (I ';' J2).(0+card I) by A14,A16,COMPOS_1:129
    .= goto (card J + 1) by A13,A9,AFINSQ_1:def 4;
A24: now
    let a;
    thus Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s1),s1) + 1).a = (
Following(
ProgramPart s2,sm)).a
    by EXTPRO_1:4
      .= sm.a by A23,SCMPDS_2:66;
  end;
A25
:  (ProgramPart Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s1),s1) + 1))
/.
IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s1),s1) + 1)
 = Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s1),s1) + 1).IC Comput(
ProgramPart(s2), s2,
LifeSpan(ProgramPart(s1),s1) + 1)
 by COMPOS_1:38;
A26: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s1),s1) + 1)
by AMI_1:123;
  thus IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s1),s1) + 1) = IC
Following(
ProgramPart s2,sm)
   by EXTPRO_1:4
    .= ICplusConst(sm,card J +1) by A23,SCMPDS_2:66
    .= (card I + (card J + 1)) by A7,Th23
    .= (card I + card J + 1);

  then
A27: CurInstr(ProgramPart s2,
       Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s1),s1) + 1))
 = s2. (card I + card J
  + 1) by A25,A26,AMI_1:54

    .= (Initialize pJ). (card I + card J + 1) by A10,A20,A2,FUNCT_4:14
    .= (I ';' J2).(card I+(card J+1)) by A14,A20,COMPOS_1:129
    .= halt SCMPDS by A19,A11,AFINSQ_1:def 4;
  DataPart Comput(ProgramPart(s1), s1,LifeSpan(ProgramPart(s1),s1)) = DataPart
sm by A1,A4,A5,Th39;

  hence DataPart Comput(ProgramPart(s1), s1,LifeSpan(ProgramPart(s1),s1)) =
DataPart Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s1),s1) + 1)
by A24,SCMPDS_4:23;

  thus
A28:
   now
    let k be Element of NAT;
A29: ProgramPart s1 = ProgramPart Comput(ProgramPart(s1),s1,k)
by AMI_1:123;
    assume
A30: k <= LifeSpan(ProgramPart(s1),s1);
    per cases;
    suppose
A31:  k < LifeSpan(ProgramPart(s1),s1);
      then CurInstr(ProgramPart s1,Comput(ProgramPart(s1),s1,k))
       <> halt SCMPDS by A1,A4,Th42,A29;

      hence CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,k)) <>
       halt SCMPDS by A1,A4,A5,A31,Th41;

    end;
    suppose
      LifeSpan(ProgramPart(s1),s1) <= k;
      then k = LifeSpan(ProgramPart(s1),s1) by A30,XXREAL_0:1;
      hence CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,k)) <>
       halt SCMPDS by A23,SCMPDS_2:85;
    end;
  end;
  thus IC Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s1),s1))
   =  card I by A1,A4,A6,Th43,A3;
  thus
A32: ProgramPart s2 halts_on s2 by A27,EXTPRO_1:30;
  now
    let k be Element of NAT;
    assume
    CurInstr(ProgramPart (s2),Comput(ProgramPart(s2),s2,k))
     = halt SCMPDS;
    then LifeSpan(ProgramPart(s1),s1) < k by A28;
    hence LifeSpan(ProgramPart(s1),s1)+1 <= k by INT_1:20;
  end;
  hence thesis by A27,A32,EXTPRO_1:def 14;
end;

theorem Th44:
  for I,J being Program of SCMPDS,s being State of SCMPDS st I
  is_closed_on s & I is_halting_on s holds I ';' Goto (card J + 1) ';' J
  is_halting_on s & I ';' Goto (card J + 1) ';' J is_closed_on s
proof
  let I,J be Program of SCMPDS,s be State of SCMPDS;
  set G = Goto (card J + 1), IJ = I ';' G ';' J, J2 = I ';' (G ';' J), pJ =
stop J2, pI =stop I, s1 = Initialize s +* pI, s2 = Initialize s +* pJ,
  m=LifeSpan(ProgramPart(s1),s1), SS=Stop SCMPDS, s3= Comput(
ProgramPart(s1), s1,m)
, s4=
  Comput(ProgramPart(s2), s2,m);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pJ = Initialize s +* pJ by COMPOS_1:125;
A3: IJ=I ';' (G ';' J) by AFINSQ_1:30;
  I c= I ';' SS by AFINSQ_1:78;
  then
A4: I c= pI;
  Initialize pI c= s1 & pI c= Initialize pI by A1,COMPOS_1:126,FUNCT_4:26;
  then pI c= s1 by XBOOLE_1:1;
  then I c= s1 by A4,XBOOLE_1:1;
  then
A5: I c= s3 by AMI_1:81;
A6: dom pI c= dom pJ by SCMPDS_5:16;
  set JS=G ';' J ';' SS;
  reconsider n = IC s3 as Element of NAT;
A7: card pI=card I + 1 by SCMPDS_5:7;
  assume
A8: I is_closed_on s;
  then IC s3 in dom pI by Def2;
  then n < card I + 1 by A7,AFINSQ_1:70;
  then
A9: n <= card I by INT_1:20;
  Initialize pJ c= s2 & pJ c= Initialize pJ by A2,COMPOS_1:126,FUNCT_4:26;
  then
A10: pJ c= s2 by XBOOLE_1:1;
A11: pJ =I ';' (G ';' J) ';' SS
    .=I ';' JS by AFINSQ_1:30;
  then I c= pJ by AFINSQ_1:78;
  then I c= s2 by A10,XBOOLE_1:1;
  then
A12: I c= s4 by AMI_1:81;
  assume
A13: I is_halting_on s;
  then
A14: ProgramPart s1 halts_on s1 by Def3;
A15:  (ProgramPart s3)/.IC s3
 = s3.IC s3 by COMPOS_1:38;
A16:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
  per cases;
  suppose
    IC s3 <>  card I;
    then n < card I by A9,XXREAL_0:1;
    then
A17: IC s3 in dom I by AFINSQ_1:70;
A18: ProgramPart s1 = ProgramPart s3
by AMI_1:123;
A19: ProgramPart s2 = ProgramPart s4
by AMI_1:123;
A20: halt SCMPDS=CurInstr(ProgramPart s1,s3) by A14,EXTPRO_1:def 14
      .=I.IC s3 by A5,A17,A15,A18,GRFUNC_1:8
      .=s4.IC s3 by A12,A17,GRFUNC_1:8
      .=CurInstr(ProgramPart s4,s4) by A8,A13,Th39,A16;
    then
A21: ProgramPart s2 halts_on s2 by A19,EXTPRO_1:30;
    hence IJ is_halting_on s by A3,Def3;
    now
      let k be Element of NAT;
      set C1k=IC Comput(ProgramPart(s1), s1,k), C2k=IC Comput(ProgramPart(s2)
, s2,k);
      per cases;
      suppose
A22:    k <= m;
        C1k in dom pI by A8,Def2;
        then C1k in dom pJ by A6;
        hence C2k in dom pJ by A8,A13,A22,Th39;
      end;
      suppose
A23:    k > m;
        set m2=LifeSpan(ProgramPart(s2),s2);
A24:    m2 <= m by A20,A21,A19,EXTPRO_1:def 14;
        then C2k=IC Comput(ProgramPart(s2), s2,m2) by A21,A23,Th3,XXREAL_0:2
          .=IC Comput(ProgramPart(s1), s1,m2) by A8,A13,A24,Th39;
        then C2k in dom pI by A8,Def2;
        hence C2k in dom pJ by A6;
      end;
    end;
    hence thesis by A3,Def2;
  end;
  suppose
A25: IC s3 = card I;
    then
A26: IC s4= card I by A8,A13,Th39;
A27:  0 in dom G by Th33;
A28: card Stop SCMPDS = 1 by COMPOS_1:46;
A29: JS =G ';' (J ';' SS) by AFINSQ_1:30;
    then
A30: card JS =card G + card (J ';' SS) by AFINSQ_1:20
      .= 1 + card (J ';' SS) by SCMPDS_5:6
      .= card J + 1 + 1 by A28,AFINSQ_1:20;
    then
A31:  0 in dom JS by AFINSQ_1:70;
    card J + 1 < card JS by A30,NAT_1:13;
    then
A32:  (card J + 1) in dom JS by AFINSQ_1:70;
    card pJ = card I + (card J + (1 + 1)) by A11,A30,AFINSQ_1:20
      .= card I + card J + 1 + 1;
    then
A33: card I + card J + 1 < card pJ by NAT_1:13;
    then
A34:  (card I + card J + 1) in dom pJ by AFINSQ_1:70;
A35:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
    card pJ = card I + card JS by A11,AFINSQ_1:20;
    then card I + 0 < card pJ by A30,XREAL_1:8;
    then
A36:  card I in dom pJ by AFINSQ_1:70;
A37: CurInstr(ProgramPart s4,s4)= s4. card I by A8,A13,A25,Th39,A35
      .= s2. card I by AMI_1:54
      .= (I ';' JS).(0+card I) by A11,A10,A36,GRFUNC_1:8
      .= JS. 0 by A31,AFINSQ_1:def 4
      .=G. 0 by A29,A27,AFINSQ_1:def 4
      .=goto (card J + 1) by Th33;
    card (G ';' J) = card G + card J by AFINSQ_1:20
      .=1 + card J by SCMPDS_5:6;
    then
A38: JS. (card J + 1)=JS.(0+card (G ';' J))
      .=halt SCMPDS by Lm1,Lm2,AFINSQ_1:def 4;
A39
:  (ProgramPart Comput(ProgramPart(s2),s2,m+1))/.IC Comput(ProgramPart(s2),s2,
m+1)
 = Comput(ProgramPart(s2),s2,m+1).IC Comput(ProgramPart(s2),s2,m+1) by
COMPOS_1:38;
A40: ProgramPart s2 = ProgramPart s4
by AMI_1:123;
A41: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,m+1)
by AMI_1:123;
A42: IC Comput(ProgramPart(s2), s2,m + 1) = IC Following(ProgramPart s2,s4)
by EXTPRO_1:4
      .= ICplusConst(s4,card J +1) by A37,A40,SCMPDS_2:66
      .= (card I + (card J + 1)) by A26,Th23
      .= (card I + card J + 1);
    then
A43: CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,m+1))
 = s2. (card I + card J + 1) by A39,A41,AMI_1:54
      .= (I ';' JS).(card I+(card J+1)) by A11,A10,A34,GRFUNC_1:8
      .= halt SCMPDS by A32,A38,AFINSQ_1:def 4;
    then
A44: ProgramPart s2 halts_on s2 by EXTPRO_1:30;
    hence IJ is_halting_on s by A3,Def3;
    now
      let k be Element of NAT;
      set C1k=IC Comput(ProgramPart(s1), s1,k), C2k=IC Comput(ProgramPart(s2)
, s2,k);
      per cases;
      suppose
A45:    k <= m;
        C1k in dom pI by A8,Def2;
        then C1k in dom pJ by A6;
        hence C2k in dom pJ by A8,A13,A45,Th39;
      end;
      suppose
A46:    k > m;
        set m2=LifeSpan(ProgramPart(s2),s2);
A47:    m2 <= m+1 by A43,A44,EXTPRO_1:def 14;
        k >= m+1 by A46,INT_1:20;
        then C2k=IC Comput(ProgramPart(s2), s2,m2) by A44,A47,Th3,XXREAL_0:2
          .= (card I + card J + 1) by A42,A44,A47,Th3;
        hence C2k in dom pJ by A33,AFINSQ_1:70;
      end;
    end;
    hence thesis by A3,Def2;
  end;
end;

theorem Th45:
  for I being shiftable Program of SCMPDS st Initialize stop I c=
s1 & I is_closed_on s1 for n being Element of NAT st Shift(stop I,n) c= s2 & IC
  s2 =  n & DataPart s1 = DataPart s2 for i being Element of NAT holds IC
Comput(ProgramPart(s1), s1,i) + n = IC Comput(ProgramPart(s2), s2,i) &
 CurInstr(ProgramPart Comput(ProgramPart(s1),s1,i),Comput(ProgramPart(s1),s1,i)
)
  = CurInstr(ProgramPart Comput(ProgramPart(s2),s2,i),Comput(ProgramPart(s2),s2
,i)) &
  DataPart Comput(ProgramPart(s1), s1,i) = DataPart
  Comput(ProgramPart(s2), s2,i)
proof
  let I be shiftable Program of SCMPDS;
  set SI=stop I;
  assume that
A1: Initialize SI c= s1 and
A2: I is_closed_on s1;
  set S1 = s1, S2 = s2;
  let n be Element of NAT;
  dom SI misses dom Start-At(0,SCMPDS) by COMPOS_1:130;
  then
A3: SI c= Initialize SI by FUNCT_4:33;
  then
A4: dom SI c= dom Initialize SI by GRFUNC_1:8;
  defpred P[Element of NAT] means IC Comput(ProgramPart(S1),S1,$1) + n = IC
Comput(ProgramPart(
  S2),
  S2,$1) & CurInstr(ProgramPart Comput(ProgramPart(S1),S1,$1),Comput(
ProgramPart(S1),S1,$1))
   = CurInstr(ProgramPart Comput(ProgramPart(S2),S2,$1),Comput(ProgramPart(S2),
S2,$1)) &
  DataPart Comput(ProgramPart(S1),S1,$1) = DataPart Comput(ProgramPart(S2),S2,
$1);
  assume that
A5: Shift(SI,n) c= s2 and
A6: IC s2 =  n and
A7: DataPart s1 = DataPart s2;
  let i be Element of NAT;
A8: DataPart Comput(ProgramPart(S1),S1,0) = DataPart s2 by A7,EXTPRO_1:3
    .= DataPart Comput(ProgramPart(S2),S2,0) by EXTPRO_1:3;
A9:  0 in dom SI by COMPOS_1:135;
  then
A10:  (0 + n) in dom Shift(SI,n) by VALUED_1:25;
A11: IC SCMPDS in dom Initialize SI by COMPOS_1:def 16;
  then
A12: s1.IC s1 = s1.IC Initialize SI by A1,GRFUNC_1:8
    .= s1. 0 by COMPOS_1:def 16
    .= (Initialize SI). 0 by A1,A4,A9,GRFUNC_1:8
    .= SI. 0 by A3,A9,GRFUNC_1:8;
A13: s1 +* Initialize SI = Initialize s1 +* SI by COMPOS_1:125;
A14: s1=Initialize s1 +* SI by A1,A13,FUNCT_4:79;
A15: for k being Element of NAT st P[k] holds P[k + 1]
  proof
    let k be Element of NAT;
    assume
A16: P[k];
    reconsider m = IC Comput(ProgramPart(S1),S1,k) as Element of NAT;
    set i = CurInstr(ProgramPart Comput(ProgramPart(S1),S1,k),Comput(
ProgramPart(S1),S1,k));
A17: ProgramPart S1 = ProgramPart Comput(ProgramPart(
S1),S1,k)
by AMI_1:123;
A18: Comput(ProgramPart(S1),S1,k+1) = Following(ProgramPart S1,
Comput(ProgramPart(S1),S1,k))
 by EXTPRO_1:4
      .= Exec(CurInstr(ProgramPart Comput(ProgramPart(S1),S1,k),Comput(
ProgramPart(S1),S1,k)),
      Comput(ProgramPart(S1),S1,k)) by A17;
    reconsider l = IC Comput(ProgramPart(S1),S1,k+1) as Element of NAT;
A19: IC Comput(ProgramPart(S1),S1,k+1) in dom SI by A2,A14,Def2;
    then
A20: l+n in dom Shift(SI,n) by VALUED_1:25;
A21: ProgramPart S2 = ProgramPart Comput(ProgramPart(S2),S2,k)
by AMI_1:123;
A22: Comput(ProgramPart(S2),S2,k+1) =
Following(ProgramPart S2,Comput(ProgramPart(S2),S2,k)
)
 by EXTPRO_1:4
      .= Exec(CurInstr(ProgramPart Comput(ProgramPart(S2),S2,k),Comput(
ProgramPart(S2),S2,k)),
      Comput(ProgramPart(S2),S2,k)) by A21;
A23: IC Comput(ProgramPart(S1),S1,k) in dom SI by A2,A14,Def2;
A24
:  (ProgramPart Comput(ProgramPart(S1),S1,k))/.IC Comput(ProgramPart(S1),S1,k)
 = Comput(ProgramPart(S1),S1,k).IC Comput(ProgramPart(S1),S1,k) by COMPOS_1:38;
A25: i = s1.IC Comput(ProgramPart(S1),S1,k) by A24,AMI_1:54
      .= (Initialize SI).IC Comput(ProgramPart(S1),S1,k)
       by A1,A4,A23,GRFUNC_1:8
      .= SI.IC Comput(ProgramPart(S1),S1,k) by A3,A23,GRFUNC_1:8;
    then
A26: InsCode i <> 1 & InsCode i <> 3 by A23,SCMPDS_4:def 12;
A27: i valid_at m by A23,A25,SCMPDS_4:def 12;
    hence
A28: IC Comput(ProgramPart(S1),S1,k+1) + n = IC Comput(ProgramPart(S2),S2,k+1)
by A16,A18,A22,A26,SCMPDS_4:83;
A29
:  (ProgramPart Comput(ProgramPart(S1),S1,k+1))/.IC Comput(ProgramPart(S1),S1,
k+1)
 = Comput(ProgramPart(S1),S1,k+1).IC Comput(ProgramPart(S1),S1,k+1) by
COMPOS_1:38;
A30
:  (ProgramPart Comput(ProgramPart(S2),S2,k+1))/.IC Comput(ProgramPart(S2),S2,
k+1)
 = Comput(ProgramPart(S2),S2,k+1).IC Comput(ProgramPart(S2),S2,k+1) by
COMPOS_1:38;
    CurInstr(ProgramPart Comput(ProgramPart(S1),S1,k+1),Comput(ProgramPart(S1),
S1,k+1)) = s1.l
     by A29,AMI_1:54
      .= (Initialize SI).l by A1,A4,A19,GRFUNC_1:8
      .= SI.l by A3,A19,GRFUNC_1:8;
    hence CurInstr(ProgramPart Comput(ProgramPart(S1),S1,k+1),Comput(
ProgramPart(S1),S1,k+1))
     = Shift(SI,n).(IC Comput(ProgramPart(S2),S2,k+1))
    by A28,A19,VALUED_1:def 12
      .= s2.IC Comput(ProgramPart(S2),S2,k+1) by A5,A28,A20,GRFUNC_1:8
      .= CurInstr(ProgramPart Comput(ProgramPart(S2),S2,k+1),Comput(ProgramPart
(S2),S2,k+1))
       by A30,AMI_1:54;
    thus thesis by A16,A18,A22,A26,A27,SCMPDS_4:83;
  end;
A31: IC Comput(ProgramPart(S1),S1,0) = s1.IC SCMPDS by EXTPRO_1:3
    .= IC Initialize SI by A1,A11,GRFUNC_1:8
    .=  0 by COMPOS_1:def 16;
A32: Comput(ProgramPart(S1),S1,0) = S1 by EXTPRO_1:3;
A33: Comput(ProgramPart(S2),S2,0) = S2 by EXTPRO_1:3;
A34:  (ProgramPart s2)/.IC s2
 = s2.IC s2 by COMPOS_1:38;
A35:  (ProgramPart s1)/.IC s1
 = s1.IC s1 by COMPOS_1:38;
  CurInstr(ProgramPart Comput(ProgramPart(S1),S1,0),Comput(ProgramPart(S1),S1,0
))
   = CurInstr(ProgramPart s1,s1) by A32
    .= Shift(SI,n).(0 + n) by A9,A12,A35,VALUED_1:def 12
    .= CurInstr(ProgramPart s2,s2) by A5,A6,A10,A34,GRFUNC_1:8
    .= CurInstr(ProgramPart Comput(ProgramPart(S2),S2,0),Comput(ProgramPart(S2)
,S2,0))
     by A33;
  then
A36: P[0] by A6,A31,A8,EXTPRO_1:3;
  for k being Element of NAT holds P[k] from NAT_1:sch 1(A36,A15);
  hence thesis;
end;

theorem Th46: ::SCMFSA8A:61
  for s being State of SCMPDS,I being halt-free Program of SCMPDS,
      J being Program of SCMPDS st I is_closed_on s & I is_halting_on s
  holds IC IExec(I ';' Goto (card J + 1) ';' J,s) = (card I + card J + 1)
proof
  let s be State of SCMPDS,I be halt-free Program of SCMPDS, J be Program of
  SCMPDS;
  set
   m= LifeSpan(ProgramPart(Initialize s +* stop I),Initialize s +* stop I)+1,
   G=Goto (card J + 1),
   s2 = Initialize s +* stop (I ';' G ';' J);
  assume
A1: I is_closed_on s & I is_halting_on s;
  then ProgramPart s2 halts_on s2 & LifeSpan(ProgramPart(s2),s2) = m by Lm3;
  then IC Result(ProgramPart(s2),s2) = IC Comput(ProgramPart(s2), s2,m) by
EXTPRO_1:23
    .=  (card I + card J + 1) by A1,Lm3;
  hence thesis by SCMPDS_5:22;
end;

theorem Th47: ::SCMFSA8A:62
  for s being State of SCMPDS,I being halt-free Program of
SCMPDS, J being Program of SCMPDS st I is_closed_on s & I is_halting_on s holds
IExec(I ';' Goto (card J + 1) ';' J,s) = IExec(I,s) +* Start-At((card I
  + card J + 1),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free Program of SCMPDS, J be Program of
  SCMPDS;
  set s1= Initialize s +* stop I, m= LifeSpan(ProgramPart(s1),s1)+1, G=Goto (
card J + 1), s2 =
  Initialize s +* stop (I ';' G ';' J), l=  (card I + card J + 1);
  assume that
A1: I is_closed_on s and
A2: I is_halting_on s;
A3: ProgramPart s1 halts_on s1 by A2,Def3;
  ProgramPart s2 halts_on s2 & LifeSpan(ProgramPart(s2),s2) = m
   by A1,A2,Lm3;
  then
A4: Result(ProgramPart(s2),s2) = Comput(ProgramPart(s2), s2,m) by EXTPRO_1:23;
  then DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s1),
s1,LifeSpan(ProgramPart(s1),s1))
by A1,A2,Lm3;
  then
A5: DataPart Result(ProgramPart(s2),s2) = DataPart Result(ProgramPart(s1),s1)
by A3,EXTPRO_1:23
    .= DataPart(Result(ProgramPart(s1),s1) +* Start-At(l,SCMPDS)) by Th7;
  IC Result(ProgramPart(s2),s2) = l by A1,A2,A4,Lm3
    .= IC (Result(ProgramPart(s1),s1) +* Start-At(l,SCMPDS)) by FUNCT_4:121;
  then
A6: Result(ProgramPart(s2),s2),Result(ProgramPart(s1),s1) +* Start-At(l,SCMPDS)
equal_outside A by A5,Th4;
  dom ProgramPart s = A by COMPOS_1:34;
  then
A7: Result(ProgramPart(s2),s2) +* s | A = Result(ProgramPart(s1),s1) +*
Start-At(l,SCMPDS) +* s | A
by A6,FUNCT_7:108;
A8: dom ProgramPart s misses dom Start-At(l,SCMPDS) by Th10;
  thus IExec(I ';' G ';' J,s) = Result(ProgramPart(s2),s2) +* s | A
    .= Result(ProgramPart(s1),s1) +* (Start-At(l,SCMPDS) +* s | A) by A7,
FUNCT_4:15
    .= Result(ProgramPart(s1),s1) +* (s | A +* Start-At(l,SCMPDS)) by A8,
FUNCT_4:36
    .= Result(ProgramPart(s1),s1) +* s | A +* Start-At(l,SCMPDS) by FUNCT_4:15
    .= IExec(I,s) +* Start-At(l,SCMPDS);
end;

theorem Th48:
  for s being State of SCMPDS,I being halt-free Program of
  SCMPDS st I is_closed_on s & I is_halting_on s holds IC IExec(I,s) =
  card I
proof
  let s be State of SCMPDS,I be halt-free Program of SCMPDS;
  set s1=Initialize s+*stop(I);
A1: Initialize s+*stop(I) = s+*Initialize stop(I) by COMPOS_1:125;
  assume that
A2: I is_closed_on s and
A3: I is_halting_on s;
A4: ProgramPart s1 halts_on s1 by A3,Def3;
  thus IC IExec(I,s) = IC Result(ProgramPart(s1),s1) by SCMPDS_5:22
    .= IC Comput(ProgramPart(s1), s1,LifeSpan(ProgramPart(s1),s1)) by A4,
EXTPRO_1:23
    .= card I by A2,A3,Th43,A1;
end;

begin :: The construction of conditional statements

definition
  let a be Int_position,k be Integer;
  let I,J be Program of SCMPDS;
  func if=0(a,k,I,J) -> Program of SCMPDS equals
  (a,k)<>0_goto (card I +2) ';'
  I ';' Goto (card J+1) ';' J;
  coherence;
  func if>0(a,k,I,J) -> Program of SCMPDS equals
  (a,k)<=0_goto (card I +2) ';'
  I ';' Goto (card J+1) ';' J;
  coherence;
  func if<0(a,k,I,J) -> Program of SCMPDS equals
  (a,k)>=0_goto (card I +2) ';'
  I ';' Goto (card J+1) ';' J;
  coherence;
end;

definition
  let a be Int_position,k be Integer;
  let I be Program of SCMPDS;
  func if=0(a,k,I) -> Program of SCMPDS equals
  (a,k)<>0_goto (card I +1) ';' I;
  coherence;
  func if<>0(a,k,I) -> Program of SCMPDS equals
  (a,k)<>0_goto 2 ';' goto (card
  I+1) ';' I;
  coherence;
  func if>0(a,k,I) -> Program of SCMPDS equals
  (a,k)<=0_goto (card I +1) ';' I;
  coherence;
  func if<=0(a,k,I) -> Program of SCMPDS equals
  (a,k)<=0_goto 2 ';' goto (card
  I+1) ';' I;
  coherence;
  func if<0(a,k,I) -> Program of SCMPDS equals
  (a,k)>=0_goto (card I +1) ';' I;
  coherence;
  func if>=0(a,k,I) -> Program of SCMPDS equals
  (a,k)>=0_goto 2 ';' goto (card
  I+1) ';' I;
  coherence;
end;

Lm4: card (i ';' I ';' Goto n ';' J) = card I + card J +2
proof
  set G=Goto n;

  thus card (i ';' I ';' G ';' J) =card (i ';' I ';' G) + card J by AFINSQ_1:20

    .=card (i ';' I) + card G + card J by AFINSQ_1:20
    .=card (i ';' I) + 1 + card J by SCMPDS_5:6
    .=card I +1 +1 +card J by Th15
    .=card I + card J +2;
end;

begin :: The computation of "if var=0 then block1 else block2"

theorem
  card if=0(a,k1,I,J) = card I + card J + 2 by Lm4;

theorem
   0 in dom if=0(a,k1,I,J) &  1 in dom if=0(a,k1,I,J)
proof
  set ci=card if=0(a,k1,I,J);
  ci=card I + card J +2 by Lm4;
  then 2 <= ci by NAT_1:12;
  then 1 < ci by XXREAL_0:2;
  hence thesis by AFINSQ_1:70;
end;

Lm5: (i ';' I ';' J ';' K). 0=i
proof
A1:  0 in dom Load i by SCMPDS_5:2;
  i ';' I ';' J ';' K =i ';' (I ';' J) ';' K by SCMPDS_4:50
    .=i ';' (I ';' J ';' K) by SCMPDS_4:50
    .=Load i ';' (I ';' J ';' K);
  hence (i ';' I ';' J ';' K). 0 =(Load i). 0 by A1,AFINSQ_1:def 4
    .=i by SCMPDS_5:4;
end;

theorem
  if=0(a,k1,I,J). 0 = (a,k1)<>0_goto (card I + 2) by Lm5;

Lm6: Shift(stop I,1) c= Comput(ProgramPart(Initialize s+* stop(i ';' I)),
Initialize s+* stop(i ';' I),n)
proof
  set pI=stop I, iI=i ';' I, piI=stop iI, s3=Initialize s+*piI;
  card Load i=1 & iI=(Load i) ';' I by SCMPDS_5:6;
  then
A1: Shift(pI,1) c= piI by Th24;
  piI c= s3 by FUNCT_4:26;
  then Shift(pI,1) c= s3 by A1,XBOOLE_1:1;
  hence thesis by AMI_1:81;
end;

Lm7: Shift(stop I,2) c= Comput(ProgramPart(Initialize s+* stop(i ';' j ';' I
)), Initialize s+* stop(i ';' j ';' I),n)
proof
  set pI=stop I, pjI=stop (i ';' j ';' I), s3=s+*Initialize pjI;
A1: s+*Initialize pjI = Initialize s+*pjI by COMPOS_1:125;
  card (i ';' j)=card (Load i ';' Load j)
    .=card Load i + card Load j by AFINSQ_1:20
    .=1+ card Load j by SCMPDS_5:6
    .=1+1 by SCMPDS_5:6;
  then
A2: Shift(pI,2) c= pjI by Th24;
  pjI c= s3 by COMPOS_1:132,FUNCT_4:26;
  then Shift(pI,2) c= s3 by A2,XBOOLE_1:1;
  hence thesis by A1,AMI_1:81;
end;

theorem Th52:
  for s being State of SCMPDS, I,J being shiftable Program of
  SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)= 0 & I
is_closed_on s & I is_halting_on s holds if=0(a,k1,I,J) is_closed_on s & if=0(a
  ,k1,I,J) is_halting_on s
proof
  let s be State of SCMPDS, I,J be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b = 0;
  set i = (a,k1)<>0_goto (card I + 2);
  set G=Goto (card J+1);
  set I2 = I ';' G ';' J, IF=if=0(a,k1,I,J), pIF=stop IF,
   pI2=stop I2, s2 = Initialize s +* pI2,
  s3 = Initialize s +* pIF, s4 =
  Comput(ProgramPart(s3),s3,1);
A2: s +* Initialize pI2 = Initialize s +* pI2 by COMPOS_1:125;
A3: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A4: IC s3 = 0 by A3,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
A6: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' I2 by SCMPDS_4:50;
  then
A7: Shift(pI2,1) c= s4 by Lm6;
A8: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A6,Th22,A3;
A9: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A9,SCMPDS_4:23
      .= s4.a by A8,SCMPDS_2:67;
  end;
  then
A10: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then s3.DataLoc(s3.a,k1)=s3.b by A3,FUNCT_4:12
    .=0 by A1,A5,A3,FUNCT_4:12;
  then
A11: IC s4 = succ IC s3 by A8,SCMPDS_2:67
    .= (0+1) by A4;
A12:  0 in dom pIF by COMPOS_1:135;
  assume
A13: I is_closed_on s;
  assume
A14: I is_halting_on s;
  then I2 is_halting_on s by A13,Th44;
  then
A15: ProgramPart s2 halts_on s2 by Def3;
A16: I2 is_closed_on s by A13,A14,Th44;
  then
A17: Initialize pI2 c= s2 & I2 is_closed_on s2 by Th38,A2,FUNCT_4:26;
A18: card pIF = card IF +1 by SCMPDS_5:7
    .= card I2 +1+1 by A6,Th15;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A19:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A20:  card pIF = card pI2+1 by A18,SCMPDS_5:7;
       m in dom pI2 by A16,Def2;
      then m < card pI2 by AFINSQ_1:70;
      then
A21:  m+1 < card pIF by A20,XREAL_1:8;
A22: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A19,EXTPRO_1:5
        .=  (m + 1) by A17,A7,A11,A10,Th45,A22;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A21,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A12,A4,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A23: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A24: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
by EXTPRO_1:5;
A25: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A26: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)))
    by A24,A23
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A17,A7,A11,A10
,Th45,A26,A25
    .= halt SCMPDS by A15,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th53:
  for s being State of SCMPDS,I being Program of SCMPDS,J being
shiftable Program of SCMPDS,a being Int_position,k1 being Integer st s.DataLoc(
  s.a,k1)<> 0 & J is_closed_on s & J is_halting_on s holds if=0(a,k1,I,J)
  is_closed_on s & if=0(a,k1,I,J) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, J be shiftable Program of
  SCMPDS, a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set pJ=stop J, s1 = Initialize s +* pJ, IF=if=0(a,k1,I,J), pIF=
  stop IF, s3 = Initialize s +* pIF, s4 = Comput(ProgramPart(s3),s3
,1);
A1: s +* Initialize pJ = Initialize s +* pJ by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
  set i = (a,k1)<>0_goto (card I + 2);
  set G =Goto (card J+1), iG=i ';' I ';' G;
A3: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' (I ';' G ';' J) by SCMPDS_4:50;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  assume
  s.b <> 0;
  then
A8: IC s4 = ICplusConst(s3,card I + 2) by A4,A6,SCMPDS_2:67
    .= (0+(card I + 2)) by A7,Th23;
  assume
A9: J is_closed_on s;
  then
A10: Initialize pJ c= s1 & J is_closed_on s1 by Th38,A1,FUNCT_4:26;
A11: pIF c= s3 by FUNCT_4:26;
A12: card iG = card (i ';' I) + card G by AFINSQ_1:20
    .=card (i ';' I) + 1 by SCMPDS_5:6
    .=card I +1 +1 by Th15
    .=card I +(1 +1);
  then Shift(pJ,card I+2) c= pIF by Th24;
  then Shift(pJ,card I+2) c= s3 by A11,XBOOLE_1:1;
  then
A13: Shift(pJ,card I+2) c= s4 by AMI_1:81;
  assume
  J is_halting_on s;
  then
A14: ProgramPart s1 halts_on s1 by Def3;
A15: DataPart s1 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s1.a = s3.a by A15,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:67;
  end;
  then
A16: DataPart s1 = DataPart s4 by SCMPDS_4:23;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A17:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s1), s1,k1) as Element of NAT;
       m in dom pJ by A9,Def2;
      then m < card pJ by AFINSQ_1:70;
      then
A18:  m + (card I + 2) < card pJ + (card I + 2) by XREAL_1:8;
A19:  card pJ = card J + 1 by SCMPDS_5:7;
A20:  card pIF = card IF+1 by SCMPDS_5:7
        .=card I +2 +card J +1 by A12,AFINSQ_1:20
        .=card I +2 + card pJ by A19;
A21: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A17,EXTPRO_1:5
        .=  (m + (card I + 2)) by A10,A16,A13,A8,Th45,A21;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A20,A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A7,COMPOS_1:135;
    end;
  end;
  hence IF is_closed_on s by Def2;
A22: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A23: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1)
= Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s1),
s1))
 by EXTPRO_1:5;
A24: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1)
)
by AMI_1:123;
A25: ProgramPart s1 = ProgramPart
Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),
s1))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1))) by A23,A22
    .=CurInstr(ProgramPart s1,
    Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),s1))) by A10,A16,A13,A8
,Th45,A25,A24
    .= halt SCMPDS by A14,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th54:
  for s being State of SCMPDS,I being halt-free shiftable
Program of SCMPDS, J being shiftable Program of SCMPDS,a being Int_position,k1
  being Integer st s.DataLoc(s.a,k1)= 0 & I is_closed_on s & I is_halting_on s
holds IExec(if=0(a,k1,I,J),s) = IExec(I,s) +* Start-At((card I + card J
  + 2),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, J be
  shiftable Program of SCMPDS,a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b = 0;
  set i = (a,k1)<>0_goto (card I + 2);
  set G=Goto (card J+1);
  set I2 = I ';' G ';' J, IF=if=0(a,k1,I,J), pI2=
stop I2, s2 = Initialize s +* pI2, s3 = Initialize s +* stop IF,
  s4 = Comput(ProgramPart(s3),s3,1);
A2: s +* Initialize pI2 = Initialize s +* pI2 by COMPOS_1:125;
A3: s +* Initialize stop IF = Initialize s +* stop IF by COMPOS_1:125;
A4: IC s3 = 0 by A3,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize stop IF by SCMPDS_4:31;
A6: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' I2 by SCMPDS_4:50;
  then
A7: Shift(pI2,1) c= s4 by Lm6;
A8: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A6,Th22,A3;
  not a in dom Initialize stop IF by SCMPDS_4:31;
  then s3.DataLoc(s3.a,k1)=s3.b by A3,FUNCT_4:12
    .=0 by A1,A5,A3,FUNCT_4:12;
  then
A9: IC s4 = succ IC s3 by A8,SCMPDS_2:67
    .= (0+1) by A4;
A10: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A10,SCMPDS_4:23
      .= s4.a by A8,SCMPDS_2:67;
  end;
  then
A11: DataPart s2 = DataPart s4 by SCMPDS_4:23;
A12: dom ProgramPart s = A by COMPOS_1:34;
  set SAl= Start-At((card I + card J + 2),SCMPDS);
  assume
A13: I is_closed_on s;
  assume
A14: I is_halting_on s;
  then I2 is_halting_on s by A13,Th44;
  then
A15: ProgramPart s2 halts_on s2 by Def3;
  I2 is_closed_on s by A13,A14,Th44;
  then
A16: Initialize pI2 c= s2 & I2 is_closed_on s2 by Th38,A2,FUNCT_4:26;
A17: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A18: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
 by EXTPRO_1:5;
A19: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A20: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A21: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A18,A17
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A16,A7,A9,A11
,Th45,A20,A19
    .= halt SCMPDS by A15,EXTPRO_1:def 14;
  then
A22: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A23: CurInstr(ProgramPart s3,s3) = i by A6,Th22,A3;
  now
    let l be Element of NAT;
    assume
A24: l < LifeSpan(ProgramPart(s2),s2) + 1;
A25: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l)) =
      CurInstr(ProgramPart s3,s3)
       by A25;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) <>
      halt SCMPDS by A23,Th29;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A26:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A27:  n < LifeSpan(ProgramPart(s2),s2) by A24,A26,XREAL_1:8;
      assume
A28:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A29: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),
s3,1)
by AMI_1:123;
A30: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A31: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
A32: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,n))
       = CurInstr(ProgramPart s4,Comput(ProgramPart(s4),s4,n))
       by A16,A7,A9,A11,A30,A31,Th45
        .= halt SCMPDS by A26,A28,A32,A29;
      hence contradiction by A15,A27,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A33: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A21,A22
,EXTPRO_1:def 14;
A34: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A35: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2), s2
,LifeSpan(ProgramPart(s2),s2)) by A15,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by
A16,A7,A9,A11,Th45,A34
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A22,A33,EXTPRO_1:23;
A36: now
    let x be set;
A37: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A38: x in dom IExec(IF,s);
    per cases by A38,SCMPDS_4:20;
    suppose
A39:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A40:  not x in dom SAl by A37,TARSKI:def 1;
A41: not x in dom (s | A) by A12,A39,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A35,A39,SCMPDS_4:23
        .= IExec(I2,s).x by A41,FUNCT_4:12
        .= (IExec(I2,s) +* SAl).x by A40,FUNCT_4:12;
    end;
    suppose
A42:  x = IC SCMPDS;
A43: not x in dom (s | A) by A12,A42,COMPOS_1:3;
      then
A44:  IC Result(ProgramPart(s2),s2) = IC IExec(I2,s) by A42,FUNCT_4:12
        .=  (card I + card J + 1) by A13,A14,Th46;
A45:  x in dom SAl by A37,A42,TARSKI:def 1;
A46: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A43,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A22,A33,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A42,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 1
        by A16,A7,A9,A11,Th45,A46
        .= IC Result(ProgramPart(s2),s2) + 1 by A15,EXTPRO_1:23
        .= (Start-At ((card I + card J + 1) + 1,SCMPDS)).IC SCMPDS
        by A44,FUNCOP_1:87
        .= (IExec(I2,s) +* SAl).x by A42,A45,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I2,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I2,s) +* SAl) by PARTFUN1:def 4;
  hence IExec(IF,s) = IExec(I2,s) +* SAl by A36,FUNCT_1:9
    .= IExec(I,s) +* Start-At((card I+card J+1),SCMPDS) +* Start-At(
   (card I + card J + 2),SCMPDS) by A13,A14,Th47
    .= IExec(I,s) +* SAl by Th14;
end;

theorem Th55:
  for s being State of SCMPDS,I being Program of SCMPDS,J being
  halt-free shiftable Program of SCMPDS,a being Int_position,k1 being Integer
  st s.DataLoc(s.a,k1)<> 0 & J is_closed_on s & J is_halting_on s holds IExec(
  if=0(a,k1,I,J),s) = IExec(J,s) +*
  Start-At((card I + card J + 2),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,J be halt-free shiftable
  Program of SCMPDS,a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set pJ=stop J, s1 = Initialize s +* pJ, IF=if=0(a,k1,I,J), pIF=
  stop IF, s3 = Initialize s +* pIF, s4 = Comput(ProgramPart(s3),s3,1);
  set i = (a,k1)<>0_goto (card I + 2);
A1: s +* Initialize pJ = Initialize s +* pJ by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
  set G =Goto (card J+1), iG=i ';' I ';' G;
  set SAl=Start-At((card I+card J+2),SCMPDS);
A3: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' (I ';' G ';' J) by SCMPDS_4:50;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A8: dom ProgramPart s = A by COMPOS_1:34;
  assume
  s.b <> 0;
  then
A9: IC s4 = ICplusConst(s3,card I + 2) by A4,A6,SCMPDS_2:67
    .= (0+(card I + 2)) by A7,Th23;
A10: DataPart s1 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s1.a = s3.a by A10,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:67;
  end;
  then
A11: DataPart s1 = DataPart s4 by SCMPDS_4:23;
  card iG = card (i ';' I) + card G by AFINSQ_1:20
    .=card (i ';' I) + 1 by SCMPDS_5:6
    .=card I +1 +1 by Th15
    .=card I +(1 +1);
  then
A12: Shift(pJ,card I+2) c= pIF by Th24;
  pIF c= s3 by FUNCT_4:26;
  then Shift(pJ,card I+2) c= s3 by A12,XBOOLE_1:1;
  then
A13: Shift(pJ,card I+2) c= s4 by AMI_1:81;
  assume
A14: J is_closed_on s;
  then
A15: Initialize pJ c= s1 & J is_closed_on s1 by Th38,A1,FUNCT_4:26;
  assume
A16: J is_halting_on s;
  then
A17: ProgramPart s1 halts_on s1 by Def3;
A18: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A19: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1) =
Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s1),s1
))
 by EXTPRO_1:5;
A20: ProgramPart s4 =
ProgramPart Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1))
by AMI_1:123;
A21: ProgramPart s1 = ProgramPart
Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),
s1))
by AMI_1:123;
A22: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1))) by A19,A18
    .=CurInstr(ProgramPart s1,
    Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),s1))) by A15,A13,A9,A11
,Th45,A21,A20
    .= halt SCMPDS by A17,EXTPRO_1:def 14;
  then
A23: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A24: CurInstr(ProgramPart s3,s3) = i by A3,Th22,A2;
  now
    let l be Element of NAT;
    assume
A25: l < LifeSpan(ProgramPart(s1),s1) + 1;
A26: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A26;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) <>
       halt SCMPDS by A24,Th29;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A27:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A28:  n < LifeSpan(ProgramPart(s1),s1) by A25,A27,XREAL_1:8;
      assume
A29:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A30: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),s3,1)
by AMI_1:123;
A31: Comput(ProgramPart(s3),s3,n+1) =
Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
A32: ProgramPart s1 = ProgramPart Comput(ProgramPart(s1),s1,n)
by AMI_1:123;
A33: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
      CurInstr(ProgramPart s1,Comput(ProgramPart(s1),
s1,n))
       = CurInstr(ProgramPart s4,
      Comput(ProgramPart(s4),s4,n)) by A15,A13,A9,A11,Th45,A32,A33
        .= halt SCMPDS by A27,A29,A31,A30;
      hence contradiction by A17,A28,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s1),s1) + 1 <= l;
  then
A34: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s1),s1) + 1 by A22,A23
,EXTPRO_1:def 14;
A35: DataPart Result(ProgramPart(s1),s1) = DataPart Comput(ProgramPart(s1), s1
,LifeSpan(ProgramPart(s1),s1)) by A17,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s1),s1))
    by A15,A13,A9,A11,Th45,A18
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s1),s1) + 1)
    by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A23,A34,EXTPRO_1:23;
A36: now
    let x be set;
A37: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A38: x in dom IExec(IF,s);
    per cases by A38,SCMPDS_4:20;
    suppose
A39:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A40:  not x in dom SAl by A37,TARSKI:def 1;
A41: not x in dom (s | A) by A8,A39,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s1),s1)).x by A35,A39,SCMPDS_4:23
        .= IExec(J,s).x by A41,FUNCT_4:12
        .= (IExec(J,s) +* SAl).x by A40,FUNCT_4:12;
    end;
    suppose
A42:  x = IC SCMPDS;
A43: not x in dom (s | A) by A8,A42,COMPOS_1:3;
      then
A44:  IC Result(ProgramPart(s1),s1) = IC IExec(J,s) by A42,FUNCT_4:12
        .=  (card J) by A14,A16,Th48;
A45: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A46:  x in dom SAl by A37,A42,TARSKI:def 1;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A43,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s1),s1) + 1).x by
A23,A34,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s1),s1)) by A42,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s1), s1,LifeSpan(ProgramPart(s1),s1)) + (card
I + 2) by A15,A13,A9,A11,Th45,A45
        .= IC Result(ProgramPart(s1),s1) + (card I + 2) by A17,EXTPRO_1:23
        .= (Start-At (card J + (card I + 2),SCMPDS)).IC SCMPDS by A44,
FUNCOP_1:87
        .= (IExec(J,s) +* SAl).x by A42,A46,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(J,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(J,s) +* SAl) by PARTFUN1:def 4;
  hence thesis by A36,FUNCT_1:9;
end;

registration
  let I,J be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if=0(a,k1,I,J) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)<>0_goto (card I + 2), G =Goto (card J+1);
    set IF=if=0(a,k1,I,J), IsIF=Initialize stop IF;
    reconsider IJ=I ';' G ';' J as shiftable Program of SCMPDS;
    thus IF is shiftable;
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
      stop IF c= s;
      then
A2:   s = Initialize s +* stop IF by A1,FUNCT_4:79;
A3:   J is_closed_on s & J is_halting_on s by Th34,Th35;
A4:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1)= 0;
        then IF is_halting_on s by A4,Th52;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) <> 0;
        then IF is_halting_on s by A3,Th53;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I,J be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if=0(a,k1,I,J) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I,J being halt-free shiftable
  parahalting Program of SCMPDS,a being Int_position,k1 being Integer holds IC
  IExec(if=0(a,k1,I,J),s) =  (card I + card J + 2)
proof
  let s be State of SCMPDS,I,J be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if=0(a,k1,I,J);
A1: J is_closed_on s & J is_halting_on s by Th34,Th35;
A2: I is_closed_on s & I is_halting_on s by Th34,Th35;
  hereby
    per cases;
    suppose
      s.DataLoc(s.a,k1) = 0;
      then
      IExec(IF,s) = IExec(I,s) +* Start-At((card I+card J+2),SCMPDS)
      by A2,Th54;
      hence thesis by FUNCT_4:121;
    end;
    suppose
      s.DataLoc(s.a,k1) <> 0;
      then
      IExec(IF,s) = IExec(J,s) +* Start-At((card I+card J+2),SCMPDS)
      by A1,Th55;
      hence thesis by FUNCT_4:121;
    end;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
  Program of SCMPDS,J being shiftable Program of SCMPDS,a,b being Int_position,
  k1 being Integer st s.DataLoc(s.a,k1)= 0 holds IExec(if=0(a,k1,I,J),s).b =
  IExec(I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,J be shiftable Program of SCMPDS,a,b be Int_position, k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1)=0;
A2: not b in dom Start-At((card I+card J+2),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then
  IExec(if=0(a,k1,I,J),s) = IExec(I,s) +* Start-At((card I + card J
  + 2),SCMPDS) by A1,Th54;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,J being
  halt-free parahalting shiftable Program of SCMPDS,a,b being Int_position,k1
being Integer st s.DataLoc(s.a,k1)<> 0 holds IExec(if=0(a,k1,I,J),s).b = IExec(
  J,s).b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,J be halt-free parahalting
  shiftable Program of SCMPDS,a,b be Int_position, k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1)<>0;
A2: not b in dom Start-At((card I+card J+2),SCMPDS) by SCMPDS_4:59;
  J is_closed_on s & J is_halting_on s by Th34,Th35;
  then
  IExec(if=0(a,k1,I,J),s) = IExec(J,s) +* Start-At((card I + card J
  + 2),SCMPDS) by A1,Th55;
  hence thesis by A2,FUNCT_4:12;
end;

begin :: The computation of "if var=0 then block"

theorem
  card if=0(a,k1,I) = card I + 1 by Th15;

theorem
   0 in dom if=0(a,k1,I)
proof
  set ci=card if=0(a,k1,I);
  ci=card I + 1 by Th15;
  hence thesis by AFINSQ_1:70;
end;

theorem
  if=0(a,k1,I). 0 = (a,k1)<>0_goto (card I + 1) by Th16;

theorem Th62:
  for s being State of SCMPDS, I being shiftable Program of SCMPDS
  , a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)= 0 & I
is_closed_on s & I is_halting_on s holds if=0(a,k1,I) is_closed_on s & if=0(a,
  k1,I) is_halting_on s
proof
  let s be State of SCMPDS, I be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b = 0;
  set i = (a,k1)<>0_goto (card I + 1);
  set IF=if=0(a,k1,I), pIF=stop IF, pI=stop I,
  s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
  s4 = Comput(ProgramPart(s3),s3,1);
A2: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A3: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A4: IC s3 = 0 by A3,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
A6: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A3;
A7: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A7,SCMPDS_4:23
      .= s4.a by A6,SCMPDS_2:67;
  end;
  then
A8: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then s3.DataLoc(s3.a,k1)=s3.b by A3,FUNCT_4:12
    .=0 by A1,A5,A3,FUNCT_4:12;
  then
A9: IC s4 = succ IC s3 by A6,SCMPDS_2:67
    .= (0+1) by A4;
  assume
A10: I is_closed_on s;
  then
A11: I is_closed_on s2 by Th38;
  assume
  I is_halting_on s;
  then
A12: ProgramPart s2 halts_on s2 by Def3;
A13:  0 in dom pIF by COMPOS_1:135;
A14: Initialize pI c= s2 & Shift(pI,1) c= s4 by Lm6,A2,FUNCT_4:26;
A15: card pIF = card IF +1 by SCMPDS_5:7
    .= card I +1+1 by Th15;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A16:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A17:  card pIF = card pI+1 by A15,SCMPDS_5:7;
       m in dom pI by A10,Def2;
      then m < card pI by AFINSQ_1:70;
      then
A18:  m+1 < card pIF by A17,XREAL_1:8;
A19: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A16,EXTPRO_1:5
        .=  (m + 1) by A11,A14,A9,A8,Th45,A19;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A13,A4,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A20: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A21: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
 by EXTPRO_1:5;
A22: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A23: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A21,A20
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A11,A14,A9,A8
,Th45,A22,A23
    .= halt SCMPDS by A12,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th63:
  for s being State of SCMPDS,I being Program of SCMPDS, a being
  Int_position, k1 being Integer st s.DataLoc(s.a,k1)<> 0 holds if=0(a,k1,I)
  is_closed_on s & if=0(a,k1,I) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, a be Int_position,k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b <> 0;
  set i = (a,k1)<>0_goto (card I + 1);
  set IF=if=0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4 =
  Comput(ProgramPart(s3),s3,1);
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: ProgramPart s4 = ProgramPart s3
by AMI_1:123;
A4: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
  Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
  Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A2;
  then
A7: IC s4 = ICplusConst(s3,card I + 1) by A1,A6,SCMPDS_2:67
    .= (0+(card I + 1)) by A4,Th23;
A8: card IF=card I+1 by Th15;
  then
A9: (card I+1) in dom pIF by Th25;
A10:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
  Initialize pIF c= s3 by A2,FUNCT_4:26;
  then pIF c= s4 by AMI_1:81,COMPOS_1:132;
  then s4.(card I+1) = pIF.(card I+1) by A9,GRFUNC_1:8
    .=halt SCMPDS by A8,Th25;
  then
A11: CurInstr(ProgramPart s4,s4) = halt SCMPDS by A7,A10;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then 1+0 <= k by INT_1:20;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A9,A7,A11,A3,
EXTPRO_1:6;
    end;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A4,COMPOS_1:135;
    end;
  end;
  hence IF is_closed_on s by Def2;
  ProgramPart s3 halts_on s3 by A11,A3,EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th64:
  for s being State of SCMPDS,I being halt-free shiftable
Program of SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)=
0 & I is_closed_on s & I is_halting_on s holds IExec(if=0(a,k1,I),s) = IExec(I,
  s) +* Start-At((card I+1),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b = 0;
  set i = (a,k1)<>0_goto (card I + 1);
  set IF=if=0(a,k1,I), pI=stop I, pIF = stop IF,
  s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
  s4 = Comput(ProgramPart(s3), s3,1);
A2: Initialize s +* pIF = s +* Initialize pIF by COMPOS_1:125;
A3: Initialize s +* pI = s +* Initialize pI by COMPOS_1:125;
A4: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
A6: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A2;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .=0 by A1,A5,A2,FUNCT_4:12;
  then
A7: IC s4 = succ IC s3 by A6,SCMPDS_2:67
    .= (0+1) by A4;
A8: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A8,SCMPDS_4:23
      .= s4.a by A6,SCMPDS_2:67;
  end;
  then
A9: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
A10: I is_closed_on s;
  then
A11: I is_closed_on s2 by Th38;
  set SAl=Start-At((card I+1),SCMPDS);
A12: dom ProgramPart s = A by COMPOS_1:34;
A13: Initialize pI c= s2 & Shift(pI,1) c= s4 by Lm6,A3,FUNCT_4:26;
  assume
A14: I is_halting_on s;
  then
A15: ProgramPart s2 halts_on s2 by Def3;
A16: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A17: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
by EXTPRO_1:5;
A18: ProgramPart s4 =
ProgramPart Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A19: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A20: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A17,A16
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A11,A13,A7,A9
,Th45,A18,A19
    .= halt SCMPDS by A15,EXTPRO_1:def 14;
  then
A21: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A22: CurInstr(ProgramPart s3,s3) = i by Th22,A2;
  now
    let l be Element of NAT;
    assume
A23: l < LifeSpan(ProgramPart(s2),s2) + 1;
A24: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A24;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) <>
       halt SCMPDS by A22,Th29;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A25:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A26:  n < LifeSpan(ProgramPart(s2),s2) by A23,A25,XREAL_1:8;
      assume
A27:  CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A28: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),s3,1)
by AMI_1:123;
A29: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A30: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
A31: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,n)) =
      CurInstr(ProgramPart s4,Comput(ProgramPart(s4),s4,n)) by A11,A13,A7,A9
,Th45,A30,A29
        .= halt SCMPDS by A25,A27,A31,A28;
      hence contradiction by A15,A26,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A32: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A20,A21
,EXTPRO_1:def 14;
A33: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A34: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2), s2
,LifeSpan(ProgramPart(s2),s2)) by A15,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A11
,A13,A7,A9,Th45,A33
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A21,A32,EXTPRO_1:23;
A35: now
    let x be set;
A36: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A37: x in dom IExec(IF,s);
    per cases by A37,SCMPDS_4:20;
    suppose
A38:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A39:  not x in dom SAl by A36,TARSKI:def 1;
A40: not x in dom (s | A) by A12,A38,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A34,A38,SCMPDS_4:23
        .= IExec(I,s).x by A40,FUNCT_4:12
        .= (IExec(I,s) +* SAl).x by A39,FUNCT_4:12;
    end;
    suppose
A41:  x = IC SCMPDS;
A42: not x in dom (s | A) by A12,A41,COMPOS_1:3;
      then
A43:  IC Result(ProgramPart(s2),s2) = IC IExec(I,s) by A41,FUNCT_4:12
        .=  card I by A10,A14,Th48;
A44:  x in dom SAl by A36,A41,TARSKI:def 1;
A45: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A42,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A21,A32,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A41,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 1
        by A11,A13,A7,A9,Th45,A45
        .= IC Result(ProgramPart(s2),s2) + 1 by A15,EXTPRO_1:23
        .= (Start-At((card I+1),SCMPDS)).IC SCMPDS by A43,FUNCOP_1:87
        .= (IExec(I,s) +* SAl).x by A41,A44,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I,s) +* SAl) by PARTFUN1:def 4;
  hence thesis by A35,FUNCT_1:9;
end;

theorem Th65:
  for s being State of SCMPDS,I being Program of SCMPDS,a being
Int_position, k1 being Integer st s.DataLoc(s.a,k1)<> 0 holds IExec(if=0(a,k1,I
  ),s) = s +* Start-At((card I+1),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a be Int_position, k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  set IF=if=0(a,k1,I), pIF=stop IF,
  s3 = Initialize s +* pIF, s4 =
  Comput(ProgramPart(s3), s3,1);
  set i = (a,k1)<>0_goto (card I + 1);
  set SAl=Start-At((card I+1),SCMPDS);
A1: Initialize s +* pIF = s +* Initialize pIF by COMPOS_1:125;
A2: IC s3 = 0 by A1,FUNCT_4:26,SCMPDS_5:18;
A3: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A4: s3.DataLoc(s3.a,k1)=s3.b by A1,FUNCT_4:12
    .= s.b by A3,A1,FUNCT_4:12;
A5: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A1;
  assume
  s.b <> 0;
  then
A6: IC s4 = ICplusConst(s3,card I + 1) by A5,A4,SCMPDS_2:67
    .= (0+(card I + 1)) by A2,Th23;
  Initialize pIF c= s3 by A1,FUNCT_4:26;
  then
A7: pIF c= s4 by AMI_1:81,COMPOS_1:132;
A8:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A9: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A10: card IF=card I+1 by Th15;
  then (card I+1) in dom pIF by Th25;
  then s4.(card I+1) = pIF.(card I+1) by A7,GRFUNC_1:8
    .=halt SCMPDS by A10,Th25;
  then
A11: CurInstr(ProgramPart s3,s4) = halt SCMPDS by A6,A8,A9;
  then
A12: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A13: CurInstr(ProgramPart s3,s3) = i by Th22,A1;
  now
    let l be Element of NAT;
A14: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    assume
    l < 1;
    then l <1+0;
    then l=0 by NAT_1:13;
    then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
     = CurInstr(ProgramPart s3,s3) by A14;
    hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
     <> halt SCMPDS by A13,Th29;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds 1 <= l;
  then LifeSpan(ProgramPart(s3),s3) = 1 by A11,A12,EXTPRO_1:def 14;
  then
A15: s4 = Result(ProgramPart(s3),s3) by A12,EXTPRO_1:23;
A16: dom ProgramPart s = A by COMPOS_1:34;
A17: now
A18: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    let x be set;
    assume
A19: x in dom IExec(IF,s);
    per cases by A19,SCMPDS_4:20;
    suppose
A20:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A21:  not x in dom SAl by A18,TARSKI:def 1;
      not x in dom (s | A) by A16,A20,SCMPDS_2:53;
      hence IExec(IF,s).x = s4.x by A15,FUNCT_4:12
        .= s3.x by A5,A20,SCMPDS_2:67
        .= s.x by A20,SCMPDS_5:19
        .= (s +* SAl).x by A21,FUNCT_4:12;
    end;
    suppose
A22:  x = IC SCMPDS;
      not x in dom (s | A) by A16,A22,COMPOS_1:3;
      hence IExec(IF,s).x = (card I + 1) by A6,A15,A22,FUNCT_4:12
        .= (s +* SAl).x by A22,FUNCT_4:121;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (s +* SAl).x by Th27;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (s +* SAl) by PARTFUN1:def 4;
  hence thesis by A17,FUNCT_1:9;
end;

registration
  let I be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if=0(a,k1,I) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)<>0_goto (card I +1);
    set IF=if=0(a,k1,I), IsIF=Initialize stop IF;
    thus IF is shiftable;
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
      stop IF c= s;
      then
A2:   s = Initialize s +* stop IF by A1,FUNCT_4:79;
A3:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1)= 0;
        then IF is_halting_on s by A3,Th62;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) <> 0;
        then IF is_halting_on s by Th63;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if=0(a,k1,I) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a being Int_position,k1 being Integer holds IC IExec(if=0(a,
  k1,I),s) =  (card I + 1)
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if=0(a,k1,I);
A1: I is_closed_on s & I is_halting_on s by Th34,Th35;
  per cases;
  suppose
    s.DataLoc(s.a,k1) = 0;
    then IExec(IF,s) =IExec(I,s) +* Start-At((card I+1),SCMPDS)
    by A1,Th64;
    hence thesis by FUNCT_4:121;
  end;
  suppose
    s.DataLoc(s.a,k1) <> 0;
    then IExec(IF,s) =s +* Start-At((card I+1),SCMPDS) by Th65;
    hence thesis by FUNCT_4:121;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a,b being Int_position,k1 being Integer st s.DataLoc(s.a,k1)=
  0 holds IExec(if=0(a,k1,I),s).b = IExec(I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a,b be Int_position,k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1)=0;
A2: not b in dom Start-At((card I+1),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then IExec(if=0(a,k1,I),s) = IExec(I,s) +* Start-At((card I+1),SCMPDS)
  by A1,Th64;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,a,b being
Int_position, k1 being Integer st s.DataLoc(s.a,k1)<> 0 holds IExec(if=0(a,k1,I
  ),s).b = s.b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a,b be Int_position, k1 be
  Integer;
  assume
  s.DataLoc(s.a,k1)<>0;
  then
A1: IExec(if=0(a,k1,I),s) = s +* Start-At((card I+1),SCMPDS) by Th65;
  not b in dom Start-At((card I+1),SCMPDS) by SCMPDS_4:59;
  hence thesis by A1,FUNCT_4:12;
end;

Lm8: card (i ';' j ';' I)=card I+2
proof
  thus card (i ';' j ';' I) =card (i ';' (j ';' I)) by SCMPDS_4:52
    .=card (j ';' I)+1 by Th15
    .=card I+1+1 by Th15
    .=card I+2;
end;

begin :: The computation of "if var<>0 then block"

theorem
  card if<>0(a,k1,I) = card I + 2 by Lm8;

Lm9:  0 in dom (i ';' j ';' I) &  1 in dom (i ';' j ';' I)
proof
  set ci=card (i ';' j ';' I);
  ci=card I + 2 by Lm8;
  then 2 <= ci by NAT_1:11;
  then 1 < ci by XXREAL_0:2;
  hence thesis by AFINSQ_1:70;
end;

theorem
   0 in dom if<>0(a,k1,I) &  1 in dom if<>0(a,k1,I) by Lm9;

Lm10: (i ';' j ';' I). 0=i & (i ';' j ';' I). 1=j
proof
  set jI=j ';' I;
A1: i ';' j ';' I =i ';' jI by SCMPDS_4:52
    .=Load i ';' jI;
   0 in dom Load i by SCMPDS_5:2;
  hence (i ';' j ';' I). 0 =(Load i). 0 by A1,AFINSQ_1:def 4
    .=i by SCMPDS_5:4;
A2:  0 in dom Load j by SCMPDS_5:2;
  card jI=card I+1 by Th15;
  then
A3: card Load i=1 &  0 in dom jI by AFINSQ_1:70,SCMPDS_5:6;
  thus (i ';' j ';' I). 1 =(Load i ';' jI). (0+1) by A1
    .=jI. 0 by A3,AFINSQ_1:def 4
    .=(Load j ';' I). 0
    .=(Load j). 0 by A2,AFINSQ_1:def 4
    .=j by SCMPDS_5:4;
end;

theorem
  if<>0(a,k1,I). 0 = (a,k1)<>0_goto 2 & if<>0(a,k1,I). 1 =
  goto (card I + 1) by Lm10;

theorem Th72:
  for s being State of SCMPDS, I being shiftable Program of SCMPDS
  , a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)<>0 & I
is_closed_on s & I is_halting_on s holds if<>0(a,k1,I) is_closed_on s & if<>0(a
  ,k1,I) is_halting_on s
proof
  let s be State of SCMPDS, I be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if<>0(a,k1,I), pIF=stop IF, pI=stop I,
  s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
  s4 = Comput(ProgramPart(s3),s3,1);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
  set i = (a,k1)<>0_goto 2, j = goto (card I + 1);
A3: IF = i ';' (j ';' I) by SCMPDS_4:52;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A5,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:67;
  end;
  then
A6: DataPart s2 = DataPart s4 by SCMPDS_4:23;
A7: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A8: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A7,A2,FUNCT_4:12;
A9: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  assume
  s.b <> 0;
  then
A10: IC s4 = ICplusConst(s3,2) by A4,A8,SCMPDS_2:67
    .= (0+2) by A9,Th23;
  assume
A11: I is_closed_on s;
  then
A12: I is_closed_on s2 by Th38;
  assume
  I is_halting_on s;
  then
A13: ProgramPart s2 halts_on s2 by Def3;
A14: Initialize pI c= s2 & Shift(pI,2) c= s4 by Lm7,A1,FUNCT_4:26;
A15:  0 in dom pIF by COMPOS_1:135;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A16:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A17:  card pIF = 1+ card IF by SCMPDS_5:7
        .= 1+(card I +2) by Lm8
        .= 1+card I +2
        .=card pI+2 by SCMPDS_5:7;
       m in dom pI by A11,Def2;
      then m < card pI by AFINSQ_1:70;
      then
A18:  m+2 < card pIF by A17,XREAL_1:8;
A19: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A16,EXTPRO_1:5
        .=  (m + 2) by A12,A14,A10,A6,Th45,A19;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A15,A9,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A20: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A21: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
= Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2),
s2))
by EXTPRO_1:5;
A22: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A23: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A21,A20
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A12,A14,A10,A6
,Th45,A23,A22
    .= halt SCMPDS by A13,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th73:
  for s being State of SCMPDS,I being Program of SCMPDS, a being
  Int_position, k1 being Integer st s.DataLoc(s.a,k1)= 0 holds if<>0(a,k1,I)
  is_closed_on s & if<>0(a,k1,I) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, a be Int_position,k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b = 0;
  set IF=if<>0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4
  = Comput(ProgramPart(s3),s3,1), s5 = Comput(ProgramPart(s3),s3,2);
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A4: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .=0 by A1,A3,A2,FUNCT_4:12;
A5: Initialize pIF c= s3 by A2,FUNCT_4:26;
  then
A6: pIF c= s4 by AMI_1:81,COMPOS_1:132;
  set i = (a,k1)<>0_goto 2, j = goto (card I + 1);
A7: IF =i ';' (j ';' I) by SCMPDS_4:52;
A8: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
  Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A7,Th22,A2;
  then
A9: IC s4 = succ IC s3 by A4,SCMPDS_2:67
    .= (0+1) by A8;
A10:  1 in dom IF by Lm9;
  then  1 in dom pIF by Th18;
  then
A11: s4. 1 = pIF. 1 by A6,GRFUNC_1:8
    .=IF. 1 by A10,Th19
    .=j by Lm10;
A12: card IF=card I+2 by Lm8;
  then
A13: (card I+2) in dom pIF by Th25;
A14:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A15: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
  Comput(ProgramPart(s3), s3,1 + 1) = Following(ProgramPart s3,s4) by
EXTPRO_1:4
    .= Exec(j,s4) by A9,A11,A14,A15;
  then
A16: IC s5 = ICplusConst(s4,card I+1) by SCMPDS_2:66
    .= (card I+1+1) by A9,Th23
    .= (card I+(1+1));
A17:  (ProgramPart s5)/.IC s5
 = s5.IC s5 by COMPOS_1:38;
A18: ProgramPart s3 = ProgramPart s5
by AMI_1:123;
  pIF c= s5 by A5,AMI_1:81,COMPOS_1:132;
  then s5.(card I+2) = pIF.(card I+2) by A13,GRFUNC_1:8
    .=halt SCMPDS by A12,Th25;
  then
A19: CurInstr(ProgramPart s3,s5) = halt SCMPDS by A16,A17,A18;
  now
    let k be Element of NAT;
A20: k = 0 or 0 + 1 <= k by INT_1:20;
    per cases by A20,XXREAL_0:1;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A8,COMPOS_1:135;
    end;
    suppose
      k = 1;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A10,A9,Th18;
    end;
    suppose
      1 < k;
      then 1+1 <= k by INT_1:20;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF
      by A13,A16,A19,EXTPRO_1:6;
    end;
  end;
  hence IF is_closed_on s by Def2;
  ProgramPart s3 halts_on s3 by A19,EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th74:
  for s being State of SCMPDS,I being halt-free shiftable
  Program of SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)
  <> 0 & I is_closed_on s & I is_halting_on s holds IExec(if<>0(a,k1,I),s) =
  IExec(I,s) +* Start-At((card I+2),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if<>0(a,k1,I), pI=stop I, pIF = stop IF,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3), s3,1);
  set i = (a,k1)<>0_goto 2, j = goto (card I + 1);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
  set SAl=Start-At((card I+2),SCMPDS);
A3: IF=i ';' (j ';' I) by SCMPDS_4:52;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A8: Initialize pI c= s2 & Shift(pI,2) c= s4 by Lm7,A1,FUNCT_4:26;
A9: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A9,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:67;
  end;
  then
A10: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
  s.b <> 0;
  then
A11: IC s4 = ICplusConst(s3,2) by A4,A6,SCMPDS_2:67
    .= (0+2) by A7,Th23;
A12: dom ProgramPart s = A by COMPOS_1:34;
  assume
A13: I is_closed_on s;
  then
A14: I is_closed_on s2 by Th38;
  assume
A15: I is_halting_on s;
  then
A16: ProgramPart s2 halts_on s2 by Def3;
A17: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A18: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
by EXTPRO_1:5;
A19: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A20: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A21: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A18,A17
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A14,A8,A11,A10
,Th45,A20,A19
    .= halt SCMPDS by A16,EXTPRO_1:def 14;
  then
A22: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A23: CurInstr(ProgramPart s3,s3) = i by A3,Th22,A2;
  now
    let l be Element of NAT;
    assume
A24: l < LifeSpan(ProgramPart(s2),s2) + 1;
A25: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A25;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A23,Th29;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A26:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A27:  n < LifeSpan(ProgramPart(s2),s2) by A24,A26,XREAL_1:8;
      assume
A28:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A29: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),
s3,1)
by AMI_1:123;
A30: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A31: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
A32: Comput(ProgramPart(s3),s3,n+1)
= Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,n)) =
      CurInstr(ProgramPart s4,Comput(ProgramPart(s4),s4,n)) by A14,A8,A11,A10
,Th45,A30,A31
        .= halt SCMPDS by A26,A28,A32,A29;
      hence contradiction by A16,A27,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A33: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A21,A22
,EXTPRO_1:def 14;
A34: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A35: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2), s2
,LifeSpan(ProgramPart(s2),s2)) by A16,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by
A14,A8,A11,A10,Th45,A34
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A22,A33,EXTPRO_1:23;
A36: now
    let x be set;
A37: dom Start-At((card I+2),SCMPDS) = {IC SCMPDS} by FUNCOP_1:19;
    assume
A38: x in dom IExec(IF,s);
    per cases by A38,SCMPDS_4:20;
    suppose
A39:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A40:  not x in dom SAl by A37,TARSKI:def 1;
A41: not x in dom (s | A) by A12,A39,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A35,A39,SCMPDS_4:23
        .= IExec(I,s).x by A41,FUNCT_4:12
        .= (IExec(I,s) +* SAl).x by A40,FUNCT_4:12;
    end;
    suppose
A42:  x = IC SCMPDS;
A43: not x in dom (s | A) by A12,A42,COMPOS_1:3;
      then
A44:  IC Result(ProgramPart(s2),s2) = IC IExec(I,s) by A42,FUNCT_4:12
        .=  card I by A13,A15,Th48;
A45:  x in dom SAl by A37,A42,TARSKI:def 1;
A46: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A43,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A22,A33,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A42,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 2
        by A14,A8,A11,A10,Th45,A46
        .= IC Result(ProgramPart(s2),s2) + 2 by A16,EXTPRO_1:23
        .= SAl.IC SCMPDS by A44,FUNCOP_1:87
        .= (IExec(I,s) +* SAl).x by A42,A45,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I,s) +* Start-At((card I+2),SCMPDS)) by PARTFUN1:def 4;
  hence thesis by A36,FUNCT_1:9;
end;

theorem Th75:
  for s being State of SCMPDS,I being Program of SCMPDS,a being
Int_position, k1 being Integer st s.DataLoc(s.a,k1)= 0 holds IExec(if<>0(a,k1,I
  ),s) = s +* Start-At((card I+2),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a be Int_position, k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b = 0;
  set SAl=Start-At((card I+2),SCMPDS);
  set i = (a,k1)<>0_goto 2, j = goto (card I + 1);
  set IF=if<>0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4
  = Comput(ProgramPart(s3),s3,1), s5 = Comput(ProgramPart(s3),s3,2);
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF =i ';' (j ';' I) by SCMPDS_4:52;
A4: Initialize pIF c= s3 by A2,FUNCT_4:26;
  then
A5: pIF c= s4 by AMI_1:81,COMPOS_1:132;
A6: pIF c= s5 by A4,AMI_1:81,COMPOS_1:132;
A7: not b in dom Initialize pIF by SCMPDS_4:31;
A8: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A9: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .=0 by A1,A7,A2,FUNCT_4:12;
  then
A10: IC s4 = succ IC s3 by A9,SCMPDS_2:67
    .= (0+1) by A8;
A11:  1 in dom IF by Lm9;
  then  1 in dom pIF by Th18;
  then
A12: s4. 1 = pIF. 1 by A5,GRFUNC_1:8
    .=IF. 1 by A11,Th19
    .=j by Lm10;
A13:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A14: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A15: Comput(ProgramPart(s3), s3,1 + 1) = Following(ProgramPart s3,s4) by
EXTPRO_1:4
    .= Exec(j,s4) by A10,A12,A13,A14;
  then
A16: IC s5 = ICplusConst(s4,card I+1) by SCMPDS_2:66
    .= (card I+1+1) by A10,Th23
    .= (card I+(1+1));
A17:  (ProgramPart s5)/.IC s5
 = s5.IC s5 by COMPOS_1:38;
A18: ProgramPart s3 = ProgramPart s5
by AMI_1:123;
A19: card IF=card I+2 by Lm8;
  then (card I+2) in dom pIF by Th25;
  then s5.(card I+2) = pIF.(card I+2) by A6,GRFUNC_1:8
    .=halt SCMPDS by A19,Th25;
  then
A20: CurInstr(ProgramPart s3,s5) = halt SCMPDS by A16,A17,A18;
  then
A21: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A22: CurInstr(ProgramPart s3,s3) = i by A3,Th22,A2;
  now
    let l be Element of NAT;
    assume
    l < 1+1;
    then
A23: l <= 1 by NAT_1:13;
A24: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
A25
:  (ProgramPart Comput(ProgramPart(s3),s3,l))/.IC Comput(ProgramPart(s3),s3,l)
 = Comput(ProgramPart(s3),s3,l).IC Comput(ProgramPart(s3),s3,l) by COMPOS_1:38;
A26: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),s3,l)
by AMI_1:123;
    per cases by A23,NAT_1:26;
    suppose
      l=0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A24;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A22,Th29;
    end;
    suppose
      l=1;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A10,A12,A25,A26,SCMPDS_2:85;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds 2 <= l;
  then LifeSpan(ProgramPart(s3),s3) = 2 by A20,A21,EXTPRO_1:def 14;
  then
A27: s5 = Result(ProgramPart(s3),s3) by A21,EXTPRO_1:23;
A28: dom ProgramPart s = A by COMPOS_1:34;
A29: now
A30: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    let x be set;
    assume
A31: x in dom IExec(IF,s);
    per cases by A31,SCMPDS_4:20;
    suppose
A32:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A33:  not x in dom SAl by A30,TARSKI:def 1;
      not x in dom (s | A) by A28,A32,SCMPDS_2:53;
      hence IExec(IF,s).x = s5.x by A27,FUNCT_4:12
        .= s4.x by A15,A32,SCMPDS_2:66
        .= s3.x by A9,A32,SCMPDS_2:67
        .= s.x by A32,SCMPDS_5:19
        .= (s +* SAl).x by A33,FUNCT_4:12;
    end;
    suppose
A34:  x = IC SCMPDS;
      not x in dom (s | A) by A28,A34,COMPOS_1:3;
      hence IExec(IF,s).x = (card I + 2) by A16,A27,A34,FUNCT_4:12
        .= (s +* Start-At((card I+2),SCMPDS)).x by A34,FUNCT_4:121;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (s +* SAl).x by Th27;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (s +* SAl) by PARTFUN1:def 4;
  hence thesis by A29,FUNCT_1:9;
end;

registration
  let I be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if<>0(a,k1,I) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)<>0_goto 2, j = goto (card I + 1);
    set IF=if<>0(a,k1,I), pIF = stop IF;
    thus IF is shiftable;
:::    now
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
:::      Initialize
      pIF c= s;
      then
A2:   s = Initialize s +* pIF by A1,FUNCT_4:79;
A3:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1)<>0;
        then IF is_halting_on s by A3,Th72;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) = 0;
        then IF is_halting_on s by Th73;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if<>0(a,k1,I) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a being Int_position,k1 being Integer holds IC IExec(if<>0(a,
  k1,I),s) =  (card I + 2)
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if<>0(a,k1,I);
A1: I is_closed_on s & I is_halting_on s by Th34,Th35;
  per cases;
  suppose
    s.DataLoc(s.a,k1) <> 0;
    then IExec(IF,s) =IExec(I,s) +* Start-At((card I+2),SCMPDS)
    by A1,Th74;
    hence thesis by FUNCT_4:121;
  end;
  suppose
    s.DataLoc(s.a,k1) = 0;
    then IExec(IF,s) =s +* Start-At((card I+2),SCMPDS) by Th75;
    hence thesis by FUNCT_4:121;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a,b being Int_position,k1 being Integer st s.DataLoc(s.a,k1)
  <> 0 holds IExec(if<>0(a,k1,I),s).b = IExec(I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a,b be Int_position,k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1)<>0;
A2: not b in dom Start-At((card I+2),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then IExec(if<>0(a,k1,I),s) = IExec(I,s) +* Start-At((card I+2),SCMPDS)
   by A1,Th74;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,a,b being
Int_position, k1 being Integer st s.DataLoc(s.a,k1)= 0 holds IExec(if<>0(a,k1,I
  ),s).b = s.b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a,b be Int_position, k1 be
  Integer;
  assume
  s.DataLoc(s.a,k1)=0;
  then
A1: IExec(if<>0(a,k1,I),s) = s +* Start-At((card I+2),SCMPDS) by Th75;
  not b in dom Start-At((card I+2),SCMPDS) by SCMPDS_4:59;
  hence thesis by A1,FUNCT_4:12;
end;

begin :: The computation of "if var>0 then block1 else block2"

theorem
  card if>0(a,k1,I,J) = card I + card J + 2 by Lm4;

theorem
   0 in dom if>0(a,k1,I,J) &  1 in dom if>0(a,k1,I,J)
proof
  set ci=card if>0(a,k1,I,J);
  ci=card I + card J +2 by Lm4;
  then 2 <= ci by NAT_1:12;
  then 1 < ci by XXREAL_0:2;
  hence thesis by AFINSQ_1:70;
end;

theorem
  if>0(a,k1,I,J). 0 = (a,k1)<=0_goto (card I + 2) by Lm5;

theorem Th82:
  for s being State of SCMPDS, I,J being shiftable Program of
  SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)>0 & I
is_closed_on s & I is_halting_on s holds if>0(a,k1,I,J) is_closed_on s & if>0(a
  ,k1,I,J) is_halting_on s
proof
  let s be State of SCMPDS, I,J be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set G=Goto (card J+1);
  set I2 = I ';' G ';' J, IF=if>0(a,k1,I,J), pIF=stop IF,
   pI2=stop I2, s2 = Initialize s +* pI2, s3 = Initialize s +* pIF,
   s4 =
  Comput(ProgramPart(s3),s3,1);
A1: s +* Initialize pI2 = Initialize s +* pI2 by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
  set i = (a,k1)<=0_goto (card I + 2);
A3:  0 in dom pIF by COMPOS_1:135;
A4: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' I2 by SCMPDS_4:50;
  then
A8: Shift(pI2,1) c= s4 by Lm6;
A9: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A7,Th22,A2;
A10: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A10,SCMPDS_4:23
      .= s4.a by A9,SCMPDS_2:68;
  end;
  then
A11: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
  s.b > 0;
  then
A12: IC s4 = succ IC s3 by A9,A6,SCMPDS_2:68
    .= (0+1) by A4;
  assume
A13: I is_closed_on s;
  assume
A14: I is_halting_on s;
  then
A15: I2 is_closed_on s by A13,Th44;
  then
A16: Initialize pI2 c= s2 & I2 is_closed_on s2 by Th38,A1,FUNCT_4:26;
  I2 is_halting_on s by A13,A14,Th44;
  then
A17: ProgramPart s2 halts_on s2 by Def3;
A18: card pIF = card IF +1 by SCMPDS_5:7
    .= card I2 +1+1 by A7,Th15;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A19:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A20:  card pIF = card pI2+1 by A18,SCMPDS_5:7;
       m in dom pI2 by A15,Def2;
      then m < card pI2 by AFINSQ_1:70;
      then
A21:  m+1 < card pIF by A20,XREAL_1:8;
A22: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A19,EXTPRO_1:5
        .=  (m + 1) by A16,A8,A12,A11,Th45,A22;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A21,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A3,A4,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A23: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A24: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
 by EXTPRO_1:5;
A25: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A26: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A24,A23
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A16,A8,A12,A11
,Th45,A26,A25
    .= halt SCMPDS by A17,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th83:
  for s being State of SCMPDS,I being Program of SCMPDS,J being
shiftable Program of SCMPDS,a being Int_position,k1 being Integer st s.DataLoc(
  s.a,k1) <= 0 & J is_closed_on s & J is_halting_on s holds if>0(a,k1,I,J)
  is_closed_on s & if>0(a,k1,I,J) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, J be shiftable Program of
  SCMPDS, a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set pJ=stop J, s1 = Initialize s +* pJ, IF=if>0(a,k1,I,J), pIF=
  stop IF, s3 = Initialize s +* pIF, s4 = Comput(ProgramPart(s3),s3
,1);
A1: s +* Initialize pJ = Initialize s +* pJ by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
  set i = (a,k1)<=0_goto (card I + 2);
  set G =Goto (card J+1), iG=i ';' I ';' G;
A3: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' (I ';' G ';' J) by SCMPDS_4:50;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  assume
  s.b <= 0;
  then
A8: IC s4 = ICplusConst(s3,card I + 2) by A4,A6,SCMPDS_2:68
    .= (0+(card I + 2)) by A7,Th23;
  assume
A9: J is_closed_on s;
  then
A10: Initialize pJ c= s1 & J is_closed_on s1 by Th38,A1,FUNCT_4:26;
A11: pIF c= s3 by FUNCT_4:26;
A12: card iG = card (i ';' I) + card G by AFINSQ_1:20
    .=card (i ';' I) + 1 by SCMPDS_5:6
    .=card I +1 +1 by Th15
    .=card I +(1 +1);
  then Shift(pJ,card I+2) c= pIF by Th24;
  then Shift(pJ,card I+2) c= s3 by A11,XBOOLE_1:1;
  then
A13: Shift(pJ,card I+2) c= s4 by AMI_1:81;
  assume
  J is_halting_on s;
  then
A14: ProgramPart s1 halts_on s1 by Def3;
A15: DataPart s1 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s1.a = s3.a by A15,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:68;
  end;
  then
A16: DataPart s1 = DataPart s4 by SCMPDS_4:23;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A17:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s1), s1,k1) as Element of NAT;
       m in dom pJ by A9,Def2;
      then m < card pJ by AFINSQ_1:70;
      then
A18:  m + (card I + 2) < card pJ + (card I + 2) by XREAL_1:8;
A19:  card pJ = card J + 1 by SCMPDS_5:7;
A20:  card pIF = card IF+1 by SCMPDS_5:7
        .=card I +2 +card J +1 by A12,AFINSQ_1:20
        .=card I +2 + card pJ by A19;
A21: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A17,EXTPRO_1:5
        .=  (m + (card I + 2)) by A10,A16,A13,A8,Th45,A21;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A20,A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A7,COMPOS_1:135;
    end;
  end;
  hence IF is_closed_on s by Def2;
A22: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A23: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s1)
,s1))
 by EXTPRO_1:5;
A24: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1)
)
by AMI_1:123;
A25: ProgramPart s1 = ProgramPart
Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),
s1))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1))) by A23,A22
    .=CurInstr(ProgramPart s1,
    Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),s1))) by A10,A16,A13,A8
,Th45,A25,A24
    .= halt SCMPDS by A14,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th84:
  for s being State of SCMPDS,I being halt-free shiftable
Program of SCMPDS, J being shiftable Program of SCMPDS,a being Int_position,k1
  being Integer st s.DataLoc(s.a,k1) > 0 & I is_closed_on s & I is_halting_on s
holds IExec(if>0(a,k1,I,J),s) = IExec(I,s) +* Start-At((card I + card J
  + 2),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, J be
  shiftable Program of SCMPDS,a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set G=Goto (card J+1);
  set I2 = I ';' G ';' J, IF=if>0(a,k1,I,J), pIF = stop IF, pI2=
stop I2, s2 = Initialize s +* pI2, s3 = Initialize s +* pIF,
s4 = Comput(ProgramPart(s3),s3,1);
A1: s +* Initialize pI2 = Initialize s +* pI2 by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
  set i = (a,k1)<=0_goto (card I + 2);
  set SAl= Start-At((card I+card J+2),SCMPDS);
A3: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A4: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A5: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A4,A2,FUNCT_4:12;
A6: dom ProgramPart s = A by COMPOS_1:34;
A7: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' I2 by SCMPDS_4:50;
  then
A8: Shift(pI2,1) c= s4 by Lm6;
A9: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A7,Th22,A2;
  assume
  s.b > 0;
  then
A10: IC s4 = succ IC s3 by A9,A5,SCMPDS_2:68
    .= (0+1) by A3;
A11: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A11,SCMPDS_4:23
      .= s4.a by A9,SCMPDS_2:68;
  end;
  then
A12: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
A13: I is_closed_on s;
  assume
A14: I is_halting_on s;
  then I2 is_halting_on s by A13,Th44;
  then
A15: ProgramPart s2 halts_on s2 by Def3;
  I2 is_closed_on s by A13,A14,Th44;
  then
A16: Initialize pI2 c= s2 & I2 is_closed_on s2 by Th38,A1,FUNCT_4:26;
A17: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A18: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
= Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2),
s2))
 by EXTPRO_1:5;
A19: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A20: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A21: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A18,A17
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A16,A8,A10,A12
,Th45,A20,A19
    .= halt SCMPDS by A15,EXTPRO_1:def 14;
  then
A22: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A23: CurInstr(ProgramPart s3,s3) = i by A7,Th22,A2;
  now
    let l be Element of NAT;
    assume
A24: l < LifeSpan(ProgramPart(s2),s2) + 1;
A25: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A25;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A23,Th30;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A26:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A27:  n < LifeSpan(ProgramPart(s2),s2) by A24,A26,XREAL_1:8;
      assume
A28:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A29: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),s3,1)
by AMI_1:123;
A30: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A31: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
A32: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,n)) =
      CurInstr(ProgramPart s4,Comput(ProgramPart(s4),s4,n)) by A16,A8,A10,A12
,Th45,A30,A31
        .= halt SCMPDS by A26,A28,A32,A29;
      hence contradiction by A15,A27,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A33: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A21,A22
,EXTPRO_1:def 14;
A34: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A35: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2), s2
,LifeSpan(ProgramPart(s2),s2)) by A15,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2))
    by A16,A8,A10,A12,Th45,A34
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A22,A33,EXTPRO_1:23;
A36: now
    let x be set;
A37: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A38: x in dom IExec(IF,s);
    per cases by A38,SCMPDS_4:20;
    suppose
A39:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A40:  not x in dom SAl by A37,TARSKI:def 1;
A41: not x in dom (s | A) by A6,A39,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A35,A39,SCMPDS_4:23
        .= IExec(I2,s).x by A41,FUNCT_4:12
        .= (IExec(I2,s) +* SAl).x by A40,FUNCT_4:12;
    end;
    suppose
A42:  x = IC SCMPDS;
A43: not x in dom (s | A) by A6,A42,COMPOS_1:3;
      then
A44:  IC Result(ProgramPart(s2),s2) = IC IExec(I2,s) by A42,FUNCT_4:12
        .=  (card I + card J + 1) by A13,A14,Th46;
A45:  x in dom SAl by A37,A42,TARSKI:def 1;
A46: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A43,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A22,A33,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A42,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 1 by
A16,A8,A10,A12,Th45,A46
        .= IC Result(ProgramPart(s2),s2) + 1 by A15,EXTPRO_1:23
        .= (Start-At(((card I + card J + 1) + 1),SCMPDS)).IC SCMPDS
        by A44,FUNCOP_1:87
        .= (IExec(I2,s) +* SAl).x by A42,A45,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I2,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I2,s) +* SAl) by PARTFUN1:def 4;
  hence IExec(IF,s) = IExec(I2,s) +* SAl by A36,FUNCT_1:9
    .= IExec(I,s) +* Start-At((card I + card J + 1),SCMPDS) +*
     Start-At(
   (card I + card J + 2),SCMPDS) by A13,A14,Th47
    .= IExec(I,s) +* SAl by Th14;
end;

theorem Th85:
  for s being State of SCMPDS,I being Program of SCMPDS,J being
  halt-free shiftable Program of SCMPDS,a being Int_position,k1 being Integer
  st s.DataLoc(s.a,k1) <= 0 & J is_closed_on s & J is_halting_on s holds IExec(
  if>0(a,k1,I,J),s) = IExec(J,s) +* Start-At((card I+card J+2),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,J be halt-free shiftable
  Program of SCMPDS,a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set pJ=stop J, s1 = Initialize s +* pJ, IF=if>0(a,k1,I,J), pIF=
  stop IF, s3 = Initialize s +* pIF, s4 = Comput(ProgramPart(s3),
s3,1);
  set i = (a,k1)<=0_goto (card I + 2);
  set G =Goto (card J+1), iG=i ';' I ';' G;
  set SAl=Start-At((card I+card J+2),SCMPDS);
A1: s +* Initialize pJ = Initialize s +* pJ by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' (I ';' G ';' J) by SCMPDS_4:50;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A8: dom ProgramPart s = A by COMPOS_1:34;
  assume
  s.b <= 0;
  then
A9: IC s4 = ICplusConst(s3,card I + 2) by A4,A6,SCMPDS_2:68
    .= (0+(card I + 2)) by A7,Th23;
A10: DataPart s1 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s1.a = s3.a by A10,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:68;
  end;
  then
A11: DataPart s1 = DataPart s4 by SCMPDS_4:23;
  card iG = card (i ';' I) + card G by AFINSQ_1:20
    .=card (i ';' I) + 1 by SCMPDS_5:6
    .=card I +1 +1 by Th15
    .=card I +(1 +1);
  then
A12: Shift(pJ,card I+2) c= pIF by Th24;
  pIF c= s3 by FUNCT_4:26;
  then Shift(pJ,card I+2) c= s3 by A12,XBOOLE_1:1;
  then
A13: Shift(pJ,card I+2) c= s4 by AMI_1:81;
  assume
A14: J is_closed_on s;
  then
A15: Initialize pJ c= s1 & J is_closed_on s1 by Th38,A1,FUNCT_4:26;
  assume
A16: J is_halting_on s;
  then
A17: ProgramPart s1 halts_on s1 by Def3;
A18: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A19: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s1)
,s1))
 by EXTPRO_1:5;
A20: ProgramPart s1 = ProgramPart
Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),
s1))
by AMI_1:123;
A21: ProgramPart s3 = ProgramPart
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1
)+1)
by AMI_1:123;
A22: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1))
 =CurInstr(ProgramPart Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1)),
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1))) by A19,A18,A21
    .=CurInstr(ProgramPart s1,
    Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),s1))) by A15,A13,A9,A11
,Th45,A20
    .= halt SCMPDS by A17,EXTPRO_1:def 14;
  then
A23: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A24: CurInstr(ProgramPart s3,s3) = i by A3,Th22,A2;
  now
    let l be Element of NAT;
    assume
A25: l < LifeSpan(ProgramPart(s1),s1) + 1;
A26: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A26;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A24,Th30;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A27:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A28:  n < LifeSpan(ProgramPart(s1),s1) by A25,A27,XREAL_1:8;
      assume
A29:  CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A30: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),
s3,1)
by AMI_1:123;
A31: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
A32: ProgramPart s1 = ProgramPart Comput(ProgramPart(s1),s1,n)
by AMI_1:123;
A33: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
      CurInstr(ProgramPart s1,Comput(ProgramPart(s1),s1,n))
       = CurInstr(ProgramPart s4,
      Comput(ProgramPart(s4),s4,n)) by A15,A13,A9,A32,A33,A11,Th45
        .= halt SCMPDS by A27,A29,A31,A30;
      hence contradiction by A17,A28,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s1),s1) + 1 <= l;
  then
A34: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s1),s1) + 1 by A22,A23
,EXTPRO_1:def 14;
A35: DataPart Result(ProgramPart(s1),s1) = DataPart Comput(ProgramPart(s1), s1
,LifeSpan(ProgramPart(s1),s1)) by A17,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s4), s4,LifeSpan(ProgramPart(s1),s1))
    by A15,A13,A9,A11,Th45
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s1),s1) + 1)
by A18,EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A23,A34,EXTPRO_1:23;
A36: now
    let x be set;
A37: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A38: x in dom IExec(IF,s);
    per cases by A38,SCMPDS_4:20;
    suppose
A39:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A40:  not x in dom SAl by A37,TARSKI:def 1;
A41: not x in dom (s | A) by A8,A39,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s1),s1)).x by A35,A39,SCMPDS_4:23
        .= IExec(J,s).x by A41,FUNCT_4:12
        .= (IExec(J,s) +* SAl).x by A40,FUNCT_4:12;
    end;
    suppose
A42:  x = IC SCMPDS;
A43: not x in dom (s | A) by A8,A42,COMPOS_1:3;
      then
A44:  IC Result(ProgramPart(s1),s1) = IC IExec(J,s) by A42,FUNCT_4:12
        .=  (card J) by A14,A16,Th48;
A45:  x in dom SAl by A37,A42,TARSKI:def 1;
A46: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A43,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s1),s1) + 1).x by
A23,A34,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s1),s1)) by A42,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s1), s1,LifeSpan(ProgramPart(s1),s1)) + (card
I + 2) by A15,A13,A9,A11,Th45,A46
        .= IC Result(ProgramPart(s1),s1) + (card I + 2) by A17,EXTPRO_1:23
        .= (Start-At (card J + (card I + 2),SCMPDS)).IC SCMPDS by A44,
FUNCOP_1:87
        .= (IExec(J,s) +* SAl).x by A42,A45,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(J,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(J,s) +* SAl) by PARTFUN1:def 4;
  hence thesis by A36,FUNCT_1:9;
end;

registration
  let I,J be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if>0(a,k1,I,J) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)<=0_goto (card I + 2), G =Goto (card J+1);
    set IF=if>0(a,k1,I,J), pIF = stop IF;
    reconsider IJ=I ';' G ';' J as shiftable Program of SCMPDS;
    thus IF is shiftable;
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
:::      Initialize
      pIF c= s;
      then
A2:   s = Initialize s +* pIF by A1,FUNCT_4:79;
A3:   J is_closed_on s & J is_halting_on s by Th34,Th35;
A4:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1) > 0;
        then IF is_halting_on s by A4,Th82;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) <= 0;
        then IF is_halting_on s by A3,Th83;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I,J be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if>0(a,k1,I,J) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I,J being halt-free shiftable
  parahalting Program of SCMPDS,a being Int_position,k1 being Integer holds IC
  IExec(if>0(a,k1,I,J),s) =  (card I + card J + 2)
proof
  let s be State of SCMPDS,I,J be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if>0(a,k1,I,J);
A1: I is_closed_on s & I is_halting_on s by Th34,Th35;
A2: J is_closed_on s & J is_halting_on s by Th34,Th35;
  per cases;
  suppose
    s.DataLoc(s.a,k1) > 0;
    then IExec(IF,s) = IExec(I,s) +* Start-At((card I+card J+2),SCMPDS)
    by A1,Th84;
    hence thesis by FUNCT_4:121;
  end;
  suppose
    s.DataLoc(s.a,k1) <= 0;
    then IExec(IF,s) = IExec(J,s) +* Start-At((card I+card J+2),SCMPDS)
    by A2,Th85;
    hence thesis by FUNCT_4:121;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
  Program of SCMPDS,J being shiftable Program of SCMPDS,a,b being Int_position,
k1 being Integer st s.DataLoc(s.a,k1)>0 holds IExec(if>0(a,k1,I,J),s).b = IExec
  (I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,J be shiftable Program of SCMPDS,a,b be Int_position, k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1)>0;
A2: not b in dom Start-At((card I+card J+2),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then
  IExec(if>0(a,k1,I,J),s) = IExec(I,s) +* Start-At((card I + card J
  + 2),SCMPDS) by A1,Th84;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,J being
  halt-free parahalting shiftable Program of SCMPDS,a,b being Int_position,k1
being Integer st s.DataLoc(s.a,k1) <= 0 holds IExec(if>0(a,k1,I,J),s).b = IExec
  (J,s).b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,J be halt-free parahalting
  shiftable Program of SCMPDS,a,b be Int_position, k1 be Integer;
  set IF=if>0(a,k1,I,J);
  assume
A1: s.DataLoc(s.a,k1) <= 0;
A2: not b in dom Start-At((card I+card J+2),SCMPDS) by SCMPDS_4:59;
  J is_closed_on s & J is_halting_on s by Th34,Th35;
  then IExec(IF,s) =IExec(J,s) +* Start-At((card I+card J+2),SCMPDS)
  by A1,Th85;
  hence thesis by A2,FUNCT_4:12;
end;

begin :: The computation of "if var>0 then block"

theorem
  card if>0(a,k1,I) = card I + 1 by Th15;

theorem
   0 in dom if>0(a,k1,I)
proof
  set ci=card if>0(a,k1,I);
  ci=card I + 1 by Th15;
  hence thesis by AFINSQ_1:70;
end;

theorem
  if>0(a,k1,I). 0 = (a,k1)<=0_goto (card I + 1) by Th16;

theorem Th92:
  for s being State of SCMPDS, I being shiftable Program of SCMPDS
  , a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)> 0 & I
is_closed_on s & I is_halting_on s holds if>0(a,k1,I) is_closed_on s & if>0(a,
  k1,I) is_halting_on s
proof
  let s be State of SCMPDS, I be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if>0(a,k1,I), pIF=stop IF, pI=stop I,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3),s3,1);
  set i = (a,k1)<=0_goto (card I + 1);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3:  0 in dom pIF by COMPOS_1:135;
A4: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A2;
A8: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A8,SCMPDS_4:23
      .= s4.a by A7,SCMPDS_2:68;
  end;
  then
A9: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
  s.b > 0;
  then
A10: IC s4 = succ IC s3 by A7,A6,SCMPDS_2:68
    .= (0+1) by A4;
  assume
A11: I is_closed_on s;
  then
A12: I is_closed_on s2 by Th38;
  assume
  I is_halting_on s;
  then
A13: ProgramPart s2 halts_on s2 by Def3;
A14: Initialize pI c= s2 & Shift(pI,1) c= s4 by Lm6,A1,FUNCT_4:26;
A15: card pIF = card IF +1 by SCMPDS_5:7
    .= card I +1+1 by Th15;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A16:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A17:  card pIF = card pI+1 by A15,SCMPDS_5:7;
       m in dom pI by A11,Def2;
      then m < card pI by AFINSQ_1:70;
      then
A18:  m+1 < card pIF by A17,XREAL_1:8;
A19: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A16,EXTPRO_1:5
        .=  (m + 1) by A12,A14,A10,A9,Th45,A19;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A3,A4,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A20: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A21: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
 by EXTPRO_1:5;
A22: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A23: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A21,A20
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A12,A14,A10,A9
,Th45,A23,A22
    .= halt SCMPDS by A13,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th93:
  for s being State of SCMPDS,I being Program of SCMPDS, a being
  Int_position, k1 being Integer st s.DataLoc(s.a,k1) <= 0 holds if>0(a,k1,I)
  is_closed_on s & if>0(a,k1,I) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, a be Int_position,k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b <= 0;
  set i = (a,k1)<=0_goto (card I + 1);
  set IF=if>0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4 =
  Comput(ProgramPart(s3),s3,1);
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A4: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A5: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A4,A2,FUNCT_4:12;
  Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
  Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A2;
  then
A6: IC s4 = ICplusConst(s3,card I + 1) by A1,A5,SCMPDS_2:68
    .= (0+(card I + 1)) by A3,Th23;
A7: card IF=card I+1 by Th15;
  then
A8: (card I+1) in dom pIF by Th25;
A9:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
  Initialize pIF c= s3 by A2,FUNCT_4:26;
  then pIF c= s4 by AMI_1:81,COMPOS_1:132;
  then s4.(card I+1) = pIF.(card I+1) by A8,GRFUNC_1:8
    .=halt SCMPDS by A7,Th25;
  then
A10: CurInstr(ProgramPart s4,s4) = halt SCMPDS by A6,A9;
A11: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then 1+0 <= k by INT_1:20;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A8,A6,A10,A11,
EXTPRO_1:6;
    end;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A3,COMPOS_1:135;
    end;
  end;
  hence IF is_closed_on s by Def2;
  ProgramPart s3 halts_on s3 by A10,A11,EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th94:
  for s being State of SCMPDS,I being halt-free shiftable
Program of SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)>
0 & I is_closed_on s & I is_halting_on s holds IExec(if>0(a,k1,I),s) = IExec(I,
  s) +* Start-At((card I+1),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if>0(a,k1,I), pI=stop I, pIF = stop IF,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3), s3,1);
  set i = (a,k1)<=0_goto (card I + 1);
  set SAl=Start-At((card I+1),SCMPDS);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A4: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A5: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A4,A2,FUNCT_4:12;
A6: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A2;
  assume
  s.b > 0;
  then
A7: IC s4 = succ IC s3 by A6,A5,SCMPDS_2:68
    .= (0+1) by A3;
A8: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A8,SCMPDS_4:23
      .= s4.a by A6,SCMPDS_2:68;
  end;
  then
A9: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
A10: I is_closed_on s;
  then
A11: I is_closed_on s2 by Th38;
A12: dom ProgramPart s = A by COMPOS_1:34;
A13: Initialize pI c= s2 & Shift(pI,1) c= s4 by Lm6,A1,FUNCT_4:26;
  assume
A14: I is_halting_on s;
  then
A15: ProgramPart s2 halts_on s2 by Def3;
A16: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A17: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
by EXTPRO_1:5;
A18: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A19: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A20: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A17,A16
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A11,A13,A7,A9
,Th45,A19,A18
    .= halt SCMPDS by A15,EXTPRO_1:def 14;
  then
A21: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A22: CurInstr(ProgramPart s3,s3) = i by Th22,A2;
  now
    let l be Element of NAT;
    assume
A23: l < LifeSpan(ProgramPart(s2),s2) + 1;
A24: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A24;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A22,Th30;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A25:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A26:  n < LifeSpan(ProgramPart(s2),s2) by A23,A25,XREAL_1:8;
      assume
A27:  CurInstr(ProgramPart s3,Comput(ProgramPart(s3),
s3,l)) = halt SCMPDS;
A28: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),
s3,1)
by AMI_1:123;
A29: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
A30: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A31: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),
s2,n)) =
      CurInstr(ProgramPart s4,Comput(ProgramPart(s4),
s4,n)) by A11,A13,A7,A9,A30,A31,Th45
        .= halt SCMPDS by A25,A27,A29,A28;
      hence contradiction by A15,A26,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A32: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A20,A21
,EXTPRO_1:def 14;
A33: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A34: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2),s2,
LifeSpan(ProgramPart(s2),s2)) by A15,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A11
,A13,A7,A9,Th45,A33
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A21,A32,EXTPRO_1:23;
A35: now
    let x be set;
A36: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A37: x in dom IExec(IF,s);
    per cases by A37,SCMPDS_4:20;
    suppose
A38:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A39:  not x in dom SAl by A36,TARSKI:def 1;
A40: not x in dom (s | A) by A12,A38,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A34,A38,SCMPDS_4:23
        .= IExec(I,s).x by A40,FUNCT_4:12
        .= (IExec(I,s) +* SAl).x by A39,FUNCT_4:12;
    end;
    suppose
A41:  x = IC SCMPDS;
A42: not x in dom (s | A) by A12,A41,COMPOS_1:3;
      then
A43:  IC Result(ProgramPart(s2),s2) = IC IExec(I,s) by A41,FUNCT_4:12
        .=  card I by A10,A14,Th48;
A44:  x in dom SAl by A36,A41,TARSKI:def 1;
A45: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A42,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A21,A32,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A41,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 1
        by A11,A13,A7,A9,Th45,A45
        .= IC Result(ProgramPart(s2),s2) + 1 by A15,EXTPRO_1:23
        .= SAl.IC SCMPDS by A43,FUNCOP_1:87
        .= (IExec(I,s) +* SAl).x by A41,A44,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I,s) +* SAl) by PARTFUN1:def 4;
  hence thesis by A35,FUNCT_1:9;
end;

theorem Th95:
  for s being State of SCMPDS,I being Program of SCMPDS,a being
Int_position, k1 being Integer st s.DataLoc(s.a,k1) <= 0 holds IExec(if>0(a,k1,
  I),s) = s +* Start-At((card I+1),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a be Int_position, k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  set IF=if>0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4 =
  Comput(ProgramPart(s3), s3,1);
  set i = (a,k1)<=0_goto (card I + 1);
  set SAl=Start-At((card I+1),SCMPDS);
A1: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A2: IC s3 = 0 by A1,FUNCT_4:26,SCMPDS_5:18;
A3: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A4: s3.DataLoc(s3.a,k1)=s3.b by A1,FUNCT_4:12
    .= s.b by A3,A1,FUNCT_4:12;
A5: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A1;
  assume
  s.b <= 0;
  then
A6: IC s4 = ICplusConst(s3,card I + 1) by A5,A4,SCMPDS_2:68
    .= (0+(card I + 1)) by A2,Th23;
  Initialize pIF c= s3 by A1,FUNCT_4:26;
  then
A7: pIF c= s4 by AMI_1:81,COMPOS_1:132;
A8:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A9: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A10: card IF=card I+1 by Th15;
  then (card I+1) in dom pIF by Th25;
  then s4.(card I+1) = pIF.(card I+1) by A7,GRFUNC_1:8
    .=halt SCMPDS by A10,Th25;
  then
A11: CurInstr(ProgramPart s3,s4) = halt SCMPDS by A6,A8,A9;
  then
A12: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A13: CurInstr(ProgramPart s3,s3) = i by Th22,A1;
  now
    let l be Element of NAT;
A14: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    assume
    l < 1;
    then l <1+0;
    then l=0 by NAT_1:13;
    then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
     = CurInstr(ProgramPart s3,s3) by A14;
    hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
     <> halt SCMPDS by A13,Th30;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds 1 <= l;
  then LifeSpan(ProgramPart(s3),s3) = 1 by A11,A12,EXTPRO_1:def 14;
  then
A15: s4 = Result(ProgramPart(s3),s3) by A12,EXTPRO_1:23;
A16: dom ProgramPart s = A by COMPOS_1:34;
A17: now
A18: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    let x be set;
    assume
A19: x in dom IExec(IF,s);
    per cases by A19,SCMPDS_4:20;
    suppose
A20:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A21:  not x in dom SAl by A18,TARSKI:def 1;
      not x in dom (s | A) by A16,A20,SCMPDS_2:53;
      hence IExec(IF,s).x = s4.x by A15,FUNCT_4:12
        .= s3.x by A5,A20,SCMPDS_2:68
        .= s.x by A20,SCMPDS_5:19
        .= (s +* SAl).x by A21,FUNCT_4:12;
    end;
    suppose
A22:  x = IC SCMPDS;
      not x in dom (s | A) by A16,A22,COMPOS_1:3;
      hence IExec(IF,s).x = (card I + 1) by A6,A15,A22,FUNCT_4:12
        .= (s +* SAl).x by A22,FUNCT_4:121;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (s +* SAl).x by Th27;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (s +* SAl) by PARTFUN1:def 4;
  hence thesis by A17,FUNCT_1:9;
end;

registration
  let I be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if>0(a,k1,I) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)<=0_goto (card I +1);
    set IF=if>0(a,k1,I), pIF = stop IF;
    thus IF is shiftable;
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
:::      Initialize
      pIF c= s;
      then
A2:   s = Initialize s +* pIF by A1,FUNCT_4:79;
A3:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1) > 0;
        then IF is_halting_on s by A3,Th92;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) <= 0;
        then IF is_halting_on s by Th93;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if>0(a,k1,I) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a being Int_position,k1 being Integer holds IC IExec(if>0(a,
  k1,I),s) =  (card I + 1)
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if>0(a,k1,I);
A1: I is_closed_on s & I is_halting_on s by Th34,Th35;
  per cases;
  suppose
    s.DataLoc(s.a,k1) > 0;
    then IExec(IF,s) =IExec(I,s) +* Start-At((card I+1),SCMPDS)
    by A1,Th94;
    hence thesis by FUNCT_4:121;
  end;
  suppose
    s.DataLoc(s.a,k1) <= 0;
    then IExec(IF,s) =s +* Start-At((card I+1),SCMPDS) by Th95;
    hence thesis by FUNCT_4:121;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a,b being Int_position,k1 being Integer st s.DataLoc(s.a,k1)>
  0 holds IExec(if>0(a,k1,I),s).b = IExec(I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a,b be Int_position,k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1) > 0;
A2: not b in dom Start-At((card I+1),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then IExec(if>0(a,k1,I),s) = IExec(I,s) +* Start-At((card I+1),SCMPDS)
  by A1,Th94;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,a,b being
Int_position, k1 being Integer st s.DataLoc(s.a,k1) <= 0 holds IExec(if>0(a,k1,
  I),s).b = s.b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a,b be Int_position, k1 be
  Integer;
  assume
  s.DataLoc(s.a,k1) <= 0;
  then
A1: IExec(if>0(a,k1,I),s) = s +* Start-At((card I+1),SCMPDS) by Th95;
  not b in dom Start-At((card I+1),SCMPDS) by SCMPDS_4:59;
  hence thesis by A1,FUNCT_4:12;
end;

begin :: The computation of "if var<=0 then block"

theorem
  card if<=0(a,k1,I) = card I + 2 by Lm8;

theorem
   0 in dom if<=0(a,k1,I) &  1 in dom if<=0(a,k1,I) by Lm9;

theorem
  if<=0(a,k1,I). 0 = (a,k1)<=0_goto 2 & if<=0(a,k1,I). 1 =
  goto (card I + 1) by Lm10;

theorem Th102:
  for s being State of SCMPDS, I being shiftable Program of
  SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1) <= 0 & I
is_closed_on s & I is_halting_on s holds if<=0(a,k1,I) is_closed_on s & if<=0(a
  ,k1,I) is_halting_on s
proof
  let s be State of SCMPDS, I be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if<=0(a,k1,I), pIF=stop IF, pI=stop I,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3),s3,1);
  set i = (a,k1)<=0_goto 2, j = goto (card I + 1);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF = i ';' (j ';' I) by SCMPDS_4:52;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A5,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:68;
  end;
  then
A6: DataPart s2 = DataPart s4 by SCMPDS_4:23;
A7: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A8: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A7,A2,FUNCT_4:12;
A9: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  assume
  s.b <= 0;
  then
A10: IC s4 = ICplusConst(s3,2) by A4,A8,SCMPDS_2:68
    .= (0+2) by A9,Th23;
  assume
A11: I is_closed_on s;
  then
A12: I is_closed_on s2 by Th38;
  assume
  I is_halting_on s;
  then
A13: ProgramPart s2 halts_on s2 by Def3;
A14: Initialize pI c= s2 & Shift(pI,2) c= s4 by Lm7,A1,FUNCT_4:26;
A15:  0 in dom pIF by COMPOS_1:135;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A16:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A17:  card pIF = 1+ card IF by SCMPDS_5:7
        .= 1+(card I +2) by Lm8
        .= 1+card I +2
        .=card pI+2 by SCMPDS_5:7;
       m in dom pI by A11,Def2;
      then m < card pI by AFINSQ_1:70;
      then
A18:  m+2 < card pIF by A17,XREAL_1:8;
A19: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A16,EXTPRO_1:5
        .=  (m + 2) by A12,A14,A10,A6,Th45,A19;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A15,A9,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A20: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A21: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
by EXTPRO_1:5;
A22: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A23: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A21,A20
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A12,A14,A10,A6
,Th45,A23,A22
    .= halt SCMPDS by A13,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th103:
  for s being State of SCMPDS,I being Program of SCMPDS, a being
  Int_position, k1 being Integer st s.DataLoc(s.a,k1) > 0 holds if<=0(a,k1,I)
  is_closed_on s & if<=0(a,k1,I) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, a be Int_position,k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b > 0;
  set i = (a,k1)<=0_goto 2, j = goto (card I + 1);
  set IF=if<=0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4
  = Comput(ProgramPart(s3),s3,1), s5 = Comput(ProgramPart(s3),s3,2);
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF =i ';' (j ';' I) by SCMPDS_4:52;
A4: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A5: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A4,A2,FUNCT_4:12;
A6: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
  Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
  then
A7: IC s4 = succ IC s3 by A1,A5,SCMPDS_2:68
    .= (0+1) by A6;
A8: Initialize pIF c= s3 by A2,FUNCT_4:26;
  then
A9: pIF c= s4 by AMI_1:81,COMPOS_1:132;
A10:  1 in dom IF by Lm9;
  then  1 in dom pIF by Th18;
  then
A11: s4. 1 = pIF. 1 by A9,GRFUNC_1:8
    .=IF. 1 by A10,Th19
    .=j by Lm10;
A12: card IF=card I+2 by Lm8;
  then
A13: (card I+2) in dom pIF by Th25;
A14:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A15: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
  Comput(ProgramPart(s3), s3,1 + 1) = Following(ProgramPart s3,s4) by
EXTPRO_1:4
    .= Exec(j,s4) by A7,A11,A14,A15;
  then
A16: IC s5 = ICplusConst(s4,card I+1) by SCMPDS_2:66
    .= (card I+1+1) by A7,Th23
    .= (card I+(1+1));
A17:  (ProgramPart s5)/.IC s5
 = s5.IC s5 by COMPOS_1:38;
A18: ProgramPart s3 = ProgramPart s5
by AMI_1:123;
  pIF c= s5 by A8,AMI_1:81,COMPOS_1:132;
  then s5.(card I+2) = pIF.(card I+2) by A13,GRFUNC_1:8
    .=halt SCMPDS by A12,Th25;
  then
A19: CurInstr(ProgramPart s3,s5) = halt SCMPDS by A16,A17,A18;
  now
    let k be Element of NAT;
A20: k = 0 or 0 + 1 <= k by INT_1:20;
    per cases by A20,XXREAL_0:1;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A6,COMPOS_1:135;
    end;
    suppose
      k = 1;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A10,A7,Th18;
    end;
    suppose
      1 < k;
      then 1+1 <= k by INT_1:20;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF
      by A13,A16,A19,EXTPRO_1:6;
    end;
  end;
  hence IF is_closed_on s by Def2;
  ProgramPart s3 halts_on s3 by A19,EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th104:
  for s being State of SCMPDS,I being halt-free shiftable
  Program of SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)
  <= 0 & I is_closed_on s & I is_halting_on s holds IExec(if<=0(a,k1,I),s) =
  IExec(I,s) +* Start-At((card I+2),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if<=0(a,k1,I), pIF = stop IF, pI = stop I,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3), s3,1);
  set i = (a,k1)<=0_goto 2, j = goto (card I + 1);
  set SAl=Start-At((card I+2),SCMPDS);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF=i ';' (j ';' I) by SCMPDS_4:52;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A8: Initialize pI c= s2 & Shift(pI,2) c= s4 by Lm7,A1,FUNCT_4:26;
A9: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A9,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:68;
  end;
  then
A10: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
  s.b <= 0;
  then
A11: IC s4 = ICplusConst(s3,2) by A4,A6,SCMPDS_2:68
    .= (0+2) by A7,Th23;
A12: dom ProgramPart s = A by COMPOS_1:34;
  assume
A13: I is_closed_on s;
  then
A14: I is_closed_on s2 by Th38;
  assume
A15: I is_halting_on s;
  then
A16: ProgramPart s2 halts_on s2 by Def3;
A17: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A18: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
by EXTPRO_1:5;
A19: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A20: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A21: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A18,A17
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A14,A8,A11,A10
,Th45,A20,A19
    .= halt SCMPDS by A16,EXTPRO_1:def 14;
  then
A22: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A23: CurInstr(ProgramPart s3,s3) = i by A3,Th22,A2;
  now
    let l be Element of NAT;
    assume
A24: l < LifeSpan(ProgramPart(s2),s2) + 1;
A25: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A25;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A23,Th30;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A26:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A27:  n < LifeSpan(ProgramPart(s2),s2) by A24,A26,XREAL_1:8;
      assume
A28:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A29: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),
s3,1)
by AMI_1:123;
A30: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
A31: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A32: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,n)) =
      CurInstr(ProgramPart s4,Comput(ProgramPart(s4),s4,n))
       by A14,A8,A11,A10,Th45,A31,A32
        .= halt SCMPDS by A26,A28,A30,A29;
      hence contradiction by A16,A27,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A33: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A21,A22
,EXTPRO_1:def 14;
A34: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A35: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2),s2,
LifeSpan(ProgramPart(s2),s2)) by A16,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by
A14,A8,A11,A10,Th45,A34
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A22,A33,EXTPRO_1:23;
A36: now
    let x be set;
A37: dom Start-At((card I+2),SCMPDS) = {IC SCMPDS} by FUNCOP_1:19;
    assume
A38: x in dom IExec(IF,s);
    per cases by A38,SCMPDS_4:20;
    suppose
A39:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A40:  not x in dom SAl by A37,TARSKI:def 1;
A41: not x in dom (s | A) by A12,A39,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A35,A39,SCMPDS_4:23
        .= IExec(I,s).x by A41,FUNCT_4:12
        .= (IExec(I,s) +* SAl).x by A40,FUNCT_4:12;
    end;
    suppose
A42:  x = IC SCMPDS;
A43: not x in dom (s | A) by A12,A42,COMPOS_1:3;
      then
A44:  IC Result(ProgramPart(s2),s2) = IC IExec(I,s) by A42,FUNCT_4:12
        .=  card I by A13,A15,Th48;
A45:  x in dom SAl by A37,A42,TARSKI:def 1;
A46: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A43,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A22,A33,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A42,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 2
        by A14,A8,A11,A10,Th45,A46
        .= IC Result(ProgramPart(s2),s2) + 2 by A16,EXTPRO_1:23
        .= SAl.IC SCMPDS by A44,FUNCOP_1:87
        .= (IExec(I,s) +* SAl).x by A42,A45,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I,s) +* Start-At((card I+2),SCMPDS)) by PARTFUN1:def 4;
  hence thesis by A36,FUNCT_1:9;
end;

theorem Th105:
  for s being State of SCMPDS,I being Program of SCMPDS,a being
Int_position, k1 being Integer st s.DataLoc(s.a,k1) > 0 holds IExec(if<=0(a,k1,
  I),s) = s +* Start-At((card I+2),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a be Int_position, k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  set IF=if<=0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4
  = Comput(ProgramPart(s3),s3,1), s5 = Comput(ProgramPart(s3),s3,2);
  set i = (a,k1)<=0_goto 2, j = goto (card I + 1);
  set SAl=Start-At((card I+2),SCMPDS);
A1: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A2: IF =i ';' (j ';' I) by SCMPDS_4:52;
A3: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A4: s3.DataLoc(s3.a,k1)=s3.b by A1,FUNCT_4:12
    .= s.b by A3,A1,FUNCT_4:12;
A5: IC s3 = 0 by A1,FUNCT_4:26,SCMPDS_5:18;
A6: Initialize pIF c= s3 by A1,FUNCT_4:26;
  then
A7: pIF c= s4 by AMI_1:81,COMPOS_1:132;
A8: pIF c= s5 by A6,AMI_1:81,COMPOS_1:132;
A9: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A2,Th22,A1;
  assume
  s.b > 0;
  then
A10: IC s4 = succ IC s3 by A9,A4,SCMPDS_2:68
    .= (0+1) by A5;
A11:  1 in dom IF by Lm9;
  then  1 in dom pIF by Th18;
  then
A12: s4. 1 = pIF. 1 by A7,GRFUNC_1:8
    .=IF. 1 by A11,Th19
    .=j by Lm10;
A13:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A14: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A15: Comput(ProgramPart(s3), s3,1 + 1) = Following(ProgramPart s3,s4) by
EXTPRO_1:4
    .= Exec(j,s4) by A10,A12,A13,A14;
  then
A16: IC s5 = ICplusConst(s4,card I+1) by SCMPDS_2:66
    .= (card I+1+1) by A10,Th23
    .= (card I+(1+1));
A17:  (ProgramPart s5)/.IC s5
 = s5.IC s5 by COMPOS_1:38;
A18: ProgramPart s3 = ProgramPart s5
by AMI_1:123;
A19: card IF=card I+2 by Lm8;
  then (card I+2) in dom pIF by Th25;
  then s5.(card I+2) = pIF.(card I+2) by A8,GRFUNC_1:8
    .=halt SCMPDS by A19,Th25;
  then
A20: CurInstr(ProgramPart s3,s5) = halt SCMPDS by A16,A17,A18;
  then
A21: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A22: CurInstr(ProgramPart s3,s3) = i by A2,Th22,A1;
  now
    let l be Element of NAT;
    assume
    l < 1+1;
    then
A23: l <= 1 by NAT_1:13;
A24: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
A25
:  (ProgramPart Comput(ProgramPart(s3),s3,l))/.IC Comput(ProgramPart(s3),s3,l)
 = Comput(ProgramPart(s3),s3,l).IC Comput(ProgramPart(s3),s3,l) by COMPOS_1:38;
A26: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),s3,l)
by AMI_1:123;
    per cases by A23,NAT_1:26;
    suppose
      l=0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart
(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A24;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A22,Th30;
    end;
    suppose
      l=1;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A10,A12,A25,A26,SCMPDS_2:85;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds 2 <= l;
  then LifeSpan(ProgramPart(s3),s3) = 2 by A20,A21,EXTPRO_1:def 14;
  then
A27: s5 = Result(ProgramPart(s3),s3) by A21,EXTPRO_1:23;
A28: dom ProgramPart s = A by COMPOS_1:34;
A29: now
A30: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    let x be set;
    assume
A31: x in dom IExec(IF,s);
    per cases by A31,SCMPDS_4:20;
    suppose
A32:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A33:  not x in dom SAl by A30,TARSKI:def 1;
      not x in dom (s | A) by A28,A32,SCMPDS_2:53;
      hence IExec(IF,s).x = s5.x by A27,FUNCT_4:12
        .= s4.x by A15,A32,SCMPDS_2:66
        .= s3.x by A9,A32,SCMPDS_2:68
        .= s.x by A32,SCMPDS_5:19
        .= (s +* SAl).x by A33,FUNCT_4:12;
    end;
    suppose
A34:  x = IC SCMPDS;
      not x in dom (s | A) by A28,A34,COMPOS_1:3;
      hence IExec(IF,s).x = (card I + 2) by A16,A27,A34,FUNCT_4:12
        .= (s +* Start-At((card I+2),SCMPDS)).x by A34,FUNCT_4:121;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (s +* SAl).x by Th27;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (s +* SAl) by PARTFUN1:def 4;
  hence thesis by A29,FUNCT_1:9;
end;

registration
  let I be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if<=0(a,k1,I) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)<=0_goto 2, j = goto (card I + 1);
    set IF=if<=0(a,k1,I), pIF = stop IF;
    thus IF is shiftable;
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
:::      Initialize
      pIF c= s;
      then
A2:   s = Initialize s +* pIF by A1,FUNCT_4:79;
A3:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1) <= 0;
        then IF is_halting_on s by A3,Th102;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) > 0;
        then IF is_halting_on s by Th103;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if<=0(a,k1,I) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a being Int_position,k1 being Integer holds IC IExec(if<=0(a,
  k1,I),s) =  (card I + 2)
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if<=0(a,k1,I);
A1: I is_closed_on s & I is_halting_on s by Th34,Th35;
  per cases;
  suppose
    s.DataLoc(s.a,k1) <= 0;
    then IExec(IF,s) =IExec(I,s) +* Start-At((card I+2),SCMPDS)
    by A1,Th104;
    hence thesis by FUNCT_4:121;
  end;
  suppose
    s.DataLoc(s.a,k1) > 0;
    then IExec(IF,s) =s +* Start-At((card I+2),SCMPDS) by Th105;
    hence thesis by FUNCT_4:121;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a,b being Int_position,k1 being Integer st s.DataLoc(s.a,k1)
  <= 0 holds IExec(if<=0(a,k1,I),s).b = IExec(I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a,b be Int_position,k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1) <= 0;
A2: not b in dom Start-At((card I+2),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then IExec(if<=0(a,k1,I),s) = IExec(I,s) +* Start-At((card I+2),SCMPDS)
   by A1,Th104;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,a,b being
Int_position, k1 being Integer st s.DataLoc(s.a,k1) > 0 holds IExec(if<=0(a,k1,
  I),s).b = s.b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a,b be Int_position, k1 be
  Integer;
  assume
  s.DataLoc(s.a,k1) > 0;
  then
A1: IExec(if<=0(a,k1,I),s) = s +* Start-At((card I+2),SCMPDS) by Th105;
  not b in dom Start-At((card I+2),SCMPDS) by SCMPDS_4:59;
  hence thesis by A1,FUNCT_4:12;
end;

begin :: The computation of "if var<0 then block1 else block2"

theorem
  card if<0(a,k1,I,J) = card I + card J + 2 by Lm4;

theorem
   0 in dom if<0(a,k1,I,J) &  1 in dom if<0(a,k1,I,J)
proof
  set ci=card if<0(a,k1,I,J);
  ci=card I + card J +2 by Lm4;
  then 2 <= ci by NAT_1:12;
  then 1 < ci by XXREAL_0:2;
  hence thesis by AFINSQ_1:70;
end;

theorem
  if<0(a,k1,I,J). 0 = (a,k1)>=0_goto (card I + 2) by Lm5;

theorem Th112:
  for s being State of SCMPDS, I,J being shiftable Program of
  SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)<0 & I
is_closed_on s & I is_halting_on s holds if<0(a,k1,I,J) is_closed_on s & if<0(a
  ,k1,I,J) is_halting_on s
proof
  let s be State of SCMPDS, I,J be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set G=Goto (card J+1);
  set I2 = I ';' G ';' J, IF=if<0(a,k1,I,J), pIF=stop IF,
   pI2=stop I2, s2 = Initialize s +* pI2, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3),s3,1);
  set i = (a,k1)>=0_goto (card I + 2);
A1: s +* Initialize pI2 = Initialize s +* pI2 by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3:  0 in dom pIF by COMPOS_1:135;
A4: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' I2 by SCMPDS_4:50;
  then
A8: Shift(pI2,1) c= s4 by Lm6;
A9: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A7,Th22,A2;
A10: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A10,SCMPDS_4:23
      .= s4.a by A9,SCMPDS_2:69;
  end;
  then
A11: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
  s.b < 0;
  then
A12: IC s4 = succ IC s3 by A9,A6,SCMPDS_2:69
    .= (0+1) by A4;
  assume
A13: I is_closed_on s;
  assume
A14: I is_halting_on s;
  then
A15: I2 is_closed_on s by A13,Th44;
  then
A16: Initialize pI2 c= s2 & I2 is_closed_on s2 by Th38,A1,FUNCT_4:26;
  I2 is_halting_on s by A13,A14,Th44;
  then
A17: ProgramPart s2 halts_on s2 by Def3;
A18: card pIF = card IF +1 by SCMPDS_5:7
    .= card I2 +1+1 by A7,Th15;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A19:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A20:  card pIF = card pI2+1 by A18,SCMPDS_5:7;
       m in dom pI2 by A15,Def2;
      then m < card pI2 by AFINSQ_1:70;
      then
A21:  m+1 < card pIF by A20,XREAL_1:8;
A22: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A19,EXTPRO_1:5
        .=  (m + 1) by A16,A8,A12,A11,Th45,A22;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A21,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A3,A4,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A23: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A24: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
 by EXTPRO_1:5;
A25: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A26: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A24,A23
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A16,A8,A12,A11
,Th45,A26,A25
    .= halt SCMPDS by A17,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th113:
  for s being State of SCMPDS,I being Program of SCMPDS,J being
shiftable Program of SCMPDS,a being Int_position,k1 being Integer st s.DataLoc(
  s.a,k1) >= 0 & J is_closed_on s & J is_halting_on s holds if<0(a,k1,I,J)
  is_closed_on s & if<0(a,k1,I,J) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, J be shiftable Program of
  SCMPDS, a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set pJ=stop J, s1 = Initialize s +* pJ, IF=if<0(a,k1,I,J), pIF=
  stop IF, s3 = Initialize s +* pIF, s4 = Comput(ProgramPart(s3),s3,1);
  set i = (a,k1)>=0_goto (card I + 2);
  set G =Goto (card J+1), iG=i ';' I ';' G;
A1: s +* Initialize pJ = Initialize s +* pJ by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' (I ';' G ';' J) by SCMPDS_4:50;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  assume
  s.b >= 0;
  then
A8: IC s4 = ICplusConst(s3,card I + 2) by A4,A6,SCMPDS_2:69
    .= (0+(card I + 2)) by A7,Th23;
  assume
A9: J is_closed_on s;
  then
A10: Initialize pJ c= s1 & J is_closed_on s1 by Th38,A1,FUNCT_4:26;
A11: pIF c= s3 by FUNCT_4:26;
A12: card iG = card (i ';' I) + card G by AFINSQ_1:20
    .=card (i ';' I) + 1 by SCMPDS_5:6
    .=card I +1 +1 by Th15
    .=card I +(1 +1);
  then Shift(pJ,card I+2) c= pIF by Th24;
  then Shift(pJ,card I+2) c= s3 by A11,XBOOLE_1:1;
  then
A13: Shift(pJ,card I+2) c= s4 by AMI_1:81;
  assume
  J is_halting_on s;
  then
A14: ProgramPart s1 halts_on s1 by Def3;
A15: DataPart s1 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s1.a = s3.a by A15,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:69;
  end;
  then
A16: DataPart s1 = DataPart s4 by SCMPDS_4:23;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A17:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s1), s1,k1) as Element of NAT;
       m in dom pJ by A9,Def2;
      then m < card pJ by AFINSQ_1:70;
      then
A18:  m + (card I + 2) < card pJ + (card I + 2) by XREAL_1:8;
A19:  card pJ = card J + 1 by SCMPDS_5:7;
A20:  card pIF = card IF+1 by SCMPDS_5:7
        .=card I +2 +card J +1 by A12,AFINSQ_1:20
        .=card I +2 + card pJ by A19;
A21: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A17,EXTPRO_1:5
        .=  (m + (card I + 2)) by A10,A16,A13,A8,Th45,A21;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A20,A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A7,COMPOS_1:135;
    end;
  end;
  hence IF is_closed_on s by Def2;
A22: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A23: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s1)
,s1))
by EXTPRO_1:5;
A24: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1)
)
by AMI_1:123;
A25: ProgramPart s1 = ProgramPart
Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),
s1))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1))) by A23,A22
    .=CurInstr(ProgramPart s1,
    Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),s1))) by A10,A16,A13,A8
,Th45,A25,A24
    .= halt SCMPDS by A14,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th114:
  for s being State of SCMPDS,I being halt-free shiftable
Program of SCMPDS, J being shiftable Program of SCMPDS,a being Int_position,k1
  being Integer st s.DataLoc(s.a,k1) < 0 & I is_closed_on s & I is_halting_on s
holds IExec(if<0(a,k1,I,J),s) = IExec(I,s) +* Start-At((card I + card J
  + 2),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, J be
  shiftable Program of SCMPDS,a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set G=Goto (card J+1);
  set I2 = I ';' G ';' J, IF=if<0(a,k1,I,J), pIF = stop IF, pI2=
stop I2, s2 = Initialize s +* pI2,
s3 = Initialize s +* pIF, s4 = Comput(ProgramPart(s3),s3,1);
A1: s +* Initialize pI2 = Initialize s +* pI2 by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
  set i = (a,k1)>=0_goto (card I + 2);
  set SAl= Start-At((card I+card J+2),SCMPDS);
A3: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A4: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A5: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A4,A2,FUNCT_4:12;
A6: dom ProgramPart s = A by COMPOS_1:34;
A7: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' I2 by SCMPDS_4:50;
  then
A8: Shift(pI2,1) c= s4 by Lm6;
A9: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A7,Th22,A2;
  assume
  s.b < 0;
  then
A10: IC s4 = succ IC s3 by A9,A5,SCMPDS_2:69
    .= (0+1) by A3;
A11: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A11,SCMPDS_4:23
      .= s4.a by A9,SCMPDS_2:69;
  end;
  then
A12: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
A13: I is_closed_on s;
  assume
A14: I is_halting_on s;
  then I2 is_halting_on s by A13,Th44;
  then
A15: ProgramPart s2 halts_on s2 by Def3;
  I2 is_closed_on s by A13,A14,Th44;
  then
A16: Initialize pI2 c= s2 & I2 is_closed_on s2 by Th38,A1,FUNCT_4:26;
A17: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A18: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
 by EXTPRO_1:5;
A19: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A20: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A21: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A18,A17
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A16,A8,A10,A12
,Th45,A20,A19
    .= halt SCMPDS by A15,EXTPRO_1:def 14;
  then
A22: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A23: CurInstr(ProgramPart s3,s3) = i by A7,Th22,A2;
  now
    let l be Element of NAT;
    assume
A24: l < LifeSpan(ProgramPart(s2),s2) + 1;
A25: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A25;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A23,Th31;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A26:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A27:  n < LifeSpan(ProgramPart(s2),s2) by A24,A26,XREAL_1:8;
      assume
A28:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A29: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),s3,1)
by AMI_1:123;
A30: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
A31: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A32: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,n)) =
      CurInstr(ProgramPart s4,Comput(ProgramPart(s4),s4,n)) by A16,A8,A10,A12
,Th45,A31,A32
        .= halt SCMPDS by A26,A28,A30,A29;
      hence contradiction by A15,A27,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A33: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A21,A22
,EXTPRO_1:def 14;
A34: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A35: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2),s2,
LifeSpan(ProgramPart(s2),s2)) by A15,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2))
    by A16,A8,A10,A12,Th45,A34
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A22,A33,EXTPRO_1:23;
A36: now
    let x be set;
A37: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A38: x in dom IExec(IF,s);
    per cases by A38,SCMPDS_4:20;
    suppose
A39:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A40:  not x in dom SAl by A37,TARSKI:def 1;
A41: not x in dom (s | A) by A6,A39,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A35,A39,SCMPDS_4:23
        .= IExec(I2,s).x by A41,FUNCT_4:12
        .= (IExec(I2,s) +* SAl).x by A40,FUNCT_4:12;
    end;
    suppose
A42:  x = IC SCMPDS;
A43: not x in dom (s | A) by A6,A42,COMPOS_1:3;
      then
A44:  IC Result(ProgramPart(s2),s2) = IC IExec(I2,s) by A42,FUNCT_4:12
        .=  (card I + card J + 1) by A13,A14,Th46;
A45:  x in dom SAl by A37,A42,TARSKI:def 1;
A46: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A43,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A22,A33,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A42,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 1 by
A16,A8,A10,A12,Th45,A46
        .= IC Result(ProgramPart(s2),s2) + 1 by A15,EXTPRO_1:23
        .= (Start-At ((card I + card J + 1) + 1,SCMPDS)).IC SCMPDS
        by A44,FUNCOP_1:87
        .= (IExec(I2,s) +* SAl).x by A42,A45,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I2,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I2,s) +* SAl) by PARTFUN1:def 4;
  hence IExec(IF,s) = IExec(I2,s) +* SAl by A36,FUNCT_1:9
    .= IExec(I,s) +* Start-At((card I + card J + 1),SCMPDS) +*
    Start-At(
   (card I + card J + 2),SCMPDS) by A13,A14,Th47
    .= IExec(I,s) +* SAl by Th14;
end;

theorem Th115:
  for s being State of SCMPDS,I being Program of SCMPDS,J being
  halt-free shiftable Program of SCMPDS,a being Int_position,k1 being Integer
  st s.DataLoc(s.a,k1) >= 0 & J is_closed_on s & J is_halting_on s holds IExec(
  if<0(a,k1,I,J),s) = IExec(J,s) +* Start-At((card I+card J+2),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,J be halt-free shiftable
  Program of SCMPDS,a be Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set pJ=stop J, s1 = Initialize s +* pJ, IF=if<0(a,k1,I,J), pIF=
  stop IF, s3 = Initialize s +* pIF, s4 = Comput(ProgramPart(s3),s3,1);
  set i = (a,k1)>=0_goto (card I + 2);
  set G =Goto (card J+1), iG=i ';' I ';' G;
  set SAl=Start-At((card I+card J+2),SCMPDS);
A1: s +* Initialize pJ = Initialize s +* pJ by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF = i ';' (I ';' G) ';' J by SCMPDS_4:50
    .= i ';' (I ';' G ';' J) by SCMPDS_4:50;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A8: dom ProgramPart s = A by COMPOS_1:34;
  assume
  s.b >= 0;
  then
A9: IC s4 = ICplusConst(s3,card I + 2) by A4,A6,SCMPDS_2:69
    .= (0+(card I + 2)) by A7,Th23;
A10: DataPart s1 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s1.a = s3.a by A10,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:69;
  end;
  then
A11: DataPart s1 = DataPart s4 by SCMPDS_4:23;
  card iG = card (i ';' I) + card G by AFINSQ_1:20
    .=card (i ';' I) + 1 by SCMPDS_5:6
    .=card I +1 +1 by Th15
    .=card I +(1 +1);
  then
A12: Shift(pJ,card I+2) c= pIF by Th24;
  pIF c= s3 by FUNCT_4:26;
  then Shift(pJ,card I+2) c= s3 by A12,XBOOLE_1:1;
  then
A13: Shift(pJ,card I+2) c= s4 by AMI_1:81;
  assume
A14: J is_closed_on s;
  then
A15: Initialize pJ c= s1 & J is_closed_on s1 by Th38,A1,FUNCT_4:26;
  assume
A16: J is_halting_on s;
  then
A17: ProgramPart s1 halts_on s1 by Def3;
A18: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A19: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s1)
,s1))
by EXTPRO_1:5;
A20: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1))
by AMI_1:123;
A21: ProgramPart s1 = ProgramPart
Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),
s1))
by AMI_1:123;
A22: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s1),s1)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s1),s1))) by A19,A18
    .=CurInstr(ProgramPart s1,
    Comput(ProgramPart(s1),s1,LifeSpan(ProgramPart(s1),s1))) by A15,A13,A9,A11
,Th45,A21,A20
    .= halt SCMPDS by A17,EXTPRO_1:def 14;
  then
A23: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A24: CurInstr(ProgramPart s3,s3) = i by A3,Th22,A2;
  now
    let l be Element of NAT;
    assume
A25: l < LifeSpan(ProgramPart(s1),s1) + 1;
A26: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A26;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A24,Th31;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A27:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A28:  n < LifeSpan(ProgramPart(s1),s1) by A25,A27,XREAL_1:8;
      assume
A29:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A30: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),
s3,1)
by AMI_1:123;
A31: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
A32: ProgramPart s1 = ProgramPart Comput(ProgramPart(s1),s1,n)
by AMI_1:123;
A33: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
      CurInstr(ProgramPart s1,Comput(ProgramPart(s1),
s1,n))
       = CurInstr(ProgramPart s4,
      Comput(ProgramPart(s4),s4,n)) by A15,A13,A9,A11,Th45,A32,A33
        .= halt SCMPDS by A27,A29,A31,A30;
      hence contradiction by A17,A28,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s1),s1) + 1 <= l;
  then
A34: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s1),s1) + 1 by A22,A23
,EXTPRO_1:def 14;
A35: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A36: DataPart Result(ProgramPart(s1),s1) = DataPart Comput(ProgramPart(s1), s1
,LifeSpan(ProgramPart(s1),s1)) by A17,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s1),s1))
    by A15,A13,A9,A11,Th45,A35
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s1),s1) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A23,A34,EXTPRO_1:23;
A37: now
    let x be set;
A38: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A39: x in dom IExec(IF,s);
    per cases by A39,SCMPDS_4:20;
    suppose
A40:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A41:  not x in dom SAl by A38,TARSKI:def 1;
A42: not x in dom (s | A) by A8,A40,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s1),s1)).x by A36,A40,SCMPDS_4:23
        .= IExec(J,s).x by A42,FUNCT_4:12
        .= (IExec(J,s) +* SAl).x by A41,FUNCT_4:12;
    end;
    suppose
A43:  x = IC SCMPDS;
A44: not x in dom (s | A) by A8,A43,COMPOS_1:3;
      then
A45:  IC Result(ProgramPart(s1),s1) = IC IExec(J,s) by A43,FUNCT_4:12
        .=  (card J) by A14,A16,Th48;
A46:  x in dom SAl by A38,A43,TARSKI:def 1;
A47: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A44,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s1),s1) + 1).x by
A23,A34,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s1),s1)) by A43,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s1), s1,LifeSpan(ProgramPart(s1),s1)) + (card
I + 2) by A15,A13,A9,A11,Th45,A47
        .= IC Result(ProgramPart(s1),s1) + (card I + 2) by A17,EXTPRO_1:23
        .= (Start-At (card J + (card I + 2),SCMPDS)).IC SCMPDS by A45,
FUNCOP_1:87
        .= (IExec(J,s) +* SAl).x by A43,A46,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(J,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(J,s) +* SAl) by PARTFUN1:def 4;
  hence thesis by A37,FUNCT_1:9;
end;

registration
  let I,J be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if<0(a,k1,I,J) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)>=0_goto (card I + 2), G =Goto (card J+1);
    set IF=if<0(a,k1,I,J), pIF = stop IF;
    reconsider IJ=I ';' G ';' J as shiftable Program of SCMPDS;
    thus IF is shiftable;
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
:::      Initialize
      pIF c= s;
      then
A2:   s = Initialize s +* pIF by A1,FUNCT_4:79;
A3:   J is_closed_on s & J is_halting_on s by Th34,Th35;
A4:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1) < 0;
        then IF is_halting_on s by A4,Th112;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) >= 0;
        then IF is_halting_on s by A3,Th113;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I,J be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if<0(a,k1,I,J) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I,J being halt-free shiftable
  parahalting Program of SCMPDS,a being Int_position,k1 being Integer holds IC
  IExec(if<0(a,k1,I,J),s) =  (card I + card J + 2)
proof
  let s be State of SCMPDS,I,J be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if<0(a,k1,I,J);
A1: I is_closed_on s & I is_halting_on s by Th34,Th35;
A2: J is_closed_on s & J is_halting_on s by Th34,Th35;
  per cases;
  suppose
    s.DataLoc(s.a,k1) < 0;
    then IExec(IF,s) = IExec(I,s) +* Start-At((card I+card J+2),SCMPDS)
    by A1,Th114;
    hence thesis by FUNCT_4:121;
  end;
  suppose
    s.DataLoc(s.a,k1) >= 0;
    then IExec(IF,s) = IExec(J,s) +* Start-At((card I+card J+2),SCMPDS)
    by A2,Th115;
    hence thesis by FUNCT_4:121;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
  Program of SCMPDS,J being shiftable Program of SCMPDS,a,b being Int_position,
k1 being Integer st s.DataLoc(s.a,k1)<0 holds IExec(if<0(a,k1,I,J),s).b = IExec
  (I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,J be shiftable Program of SCMPDS,a,b be Int_position, k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1)<0;
A2: not b in dom Start-At((card I+card J+2),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then
  IExec(if<0(a,k1,I,J),s) = IExec(I,s) +* Start-At((card I + card J
  + 2),SCMPDS) by A1,Th114;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,J being
  halt-free parahalting shiftable Program of SCMPDS,a,b being Int_position,k1
being Integer st s.DataLoc(s.a,k1) >= 0 holds IExec(if<0(a,k1,I,J),s).b = IExec
  (J,s).b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,J be halt-free parahalting
  shiftable Program of SCMPDS,a,b be Int_position, k1 be Integer;
  set IF=if<0(a,k1,I,J);
  assume
A1: s.DataLoc(s.a,k1) >= 0;
A2: not b in dom Start-At((card I+card J+2),SCMPDS) by SCMPDS_4:59;
  J is_closed_on s & J is_halting_on s by Th34,Th35;
  then IExec(IF,s) =IExec(J,s) +* Start-At((card I+card J+2),SCMPDS)
  by A1,Th115;
  hence thesis by A2,FUNCT_4:12;
end;

begin :: The computation of "if var<0 then block"

theorem
  card if<0(a,k1,I) = card I + 1 by Th15;

theorem
   0 in dom if<0(a,k1,I)
proof
  set ci=card if<0(a,k1,I);
  ci=card I + 1 by Th15;
  hence thesis by AFINSQ_1:70;
end;

theorem
  if<0(a,k1,I). 0 = (a,k1)>=0_goto (card I + 1) by Th16;

theorem Th122:
  for s being State of SCMPDS, I being shiftable Program of
  SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1) < 0 & I
is_closed_on s & I is_halting_on s holds if<0(a,k1,I) is_closed_on s & if<0(a,
  k1,I) is_halting_on s
proof
  let s be State of SCMPDS, I be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if<0(a,k1,I), pIF=stop IF, pI=stop I,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3),s3,1);
  set i = (a,k1)>=0_goto (card I + 1);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3:  0 in dom pIF by COMPOS_1:135;
A4: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A2;
A8: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A8,SCMPDS_4:23
      .= s4.a by A7,SCMPDS_2:69;
  end;
  then
A9: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
  s.b < 0;
  then
A10: IC s4 = succ IC s3 by A7,A6,SCMPDS_2:69
    .= (0+1) by A4;
  assume
A11: I is_closed_on s;
  then
A12: I is_closed_on s2 by Th38;
  assume
  I is_halting_on s;
  then
A13: ProgramPart s2 halts_on s2 by Def3;
A14: Initialize pI c= s2 & Shift(pI,1) c= s4 by Lm6,A1,FUNCT_4:26;
A15: card pIF = card IF +1 by SCMPDS_5:7
    .= card I +1+1 by Th15;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A16:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A17:  card pIF = card pI+1 by A15,SCMPDS_5:7;
       m in dom pI by A11,Def2;
      then m < card pI by AFINSQ_1:70;
      then
A18:  m+1 < card pIF by A17,XREAL_1:8;
A19: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A16,EXTPRO_1:5
        .=  (m + 1) by A12,A14,A10,A9,Th45,A19;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A3,A4,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A20: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A21: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
by EXTPRO_1:5;
A22: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A23: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A21,A20
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A12,A14,A10,A9
,Th45,A23,A22
    .= halt SCMPDS by A13,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th123:
  for s being State of SCMPDS,I being Program of SCMPDS, a being
  Int_position, k1 being Integer st s.DataLoc(s.a,k1) >= 0 holds if<0(a,k1,I)
  is_closed_on s & if<0(a,k1,I) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, a be Int_position,k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b >= 0;
  set i = (a,k1)>=0_goto (card I + 1);
  set IF=if<0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4 =
  Comput(ProgramPart(s3),s3,1);
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A4: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A5: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A4,A2,FUNCT_4:12;
  Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
  Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A2;
  then
A6: IC s4 = ICplusConst(s3,card I + 1) by A1,A5,SCMPDS_2:69
    .= (0+(card I + 1)) by A3,Th23;
A7: card IF=card I+1 by Th15;
  then
A8: (card I+1) in dom pIF by Th25;
A9:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A10: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
  Initialize pIF c= s3 by A2,FUNCT_4:26;
  then pIF c= s4 by AMI_1:81,COMPOS_1:132;
  then s4.(card I+1) = pIF.(card I+1) by A8,GRFUNC_1:8
    .=halt SCMPDS by A7,Th25;
  then
A11: CurInstr(ProgramPart s3,s4) = halt SCMPDS by A6,A9,A10;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then 1+0 <= k by INT_1:20;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A8,A6,A11,EXTPRO_1:6;
    end;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A3,COMPOS_1:135;
    end;
  end;
  hence IF is_closed_on s by Def2;
  ProgramPart s3 halts_on s3 by A11,EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th124:
  for s being State of SCMPDS,I being halt-free shiftable
Program of SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1) <
0 & I is_closed_on s & I is_halting_on s holds IExec(if<0(a,k1,I),s) = IExec(I,
  s) +* Start-At((card I+1),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if<0(a,k1,I), pIF = stop IF, pI=stop I,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3), s3,1);
  set i = (a,k1)>=0_goto (card I + 1);
  set SAl=Start-At((card I+1),SCMPDS);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A4: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A5: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A4,A2,FUNCT_4:12;
A6: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A2;
  assume
  s.b < 0;
  then
A7: IC s4 = succ IC s3 by A6,A5,SCMPDS_2:69
    .= (0+1) by A3;
A8: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A8,SCMPDS_4:23
      .= s4.a by A6,SCMPDS_2:69;
  end;
  then
A9: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
A10: I is_closed_on s;
  then
A11: I is_closed_on s2 by Th38;
A12: dom ProgramPart s = A by COMPOS_1:34;
A13: Initialize pI c= s2 & Shift(pI,1) c= s4 by Lm6,A1,FUNCT_4:26;
  assume
A14: I is_halting_on s;
  then
A15: ProgramPart s2 halts_on s2 by Def3;
A16: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A17: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
= Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2),
s2))
 by EXTPRO_1:5;
A18: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A19: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A20: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A17,A16
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A11,A13,A7,A9
,Th45,A19,A18
    .= halt SCMPDS by A15,EXTPRO_1:def 14;
  then
A21: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A22: CurInstr(ProgramPart s3,s3) = i by Th22,A2;
  now
    let l be Element of NAT;
    assume
A23: l < LifeSpan(ProgramPart(s2),s2) + 1;
A24: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A24;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A22,Th31;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A25:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A26:  n < LifeSpan(ProgramPart(s2),s2) by A23,A25,XREAL_1:8;
      assume
A27:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l)) = halt SCMPDS;
A28: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),
s3,1)
by AMI_1:123;
A29: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
A30: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A31: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,n)) =
      CurInstr(ProgramPart s4,Comput(ProgramPart(s4),s4,n)) by A11,A13,A7,A9
,Th45,A30,A31
        .= halt SCMPDS by A25,A27,A29,A28;
      hence contradiction by A15,A26,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A32: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A20,A21
,EXTPRO_1:def 14;
A33: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A34: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2),s2,
LifeSpan(ProgramPart(s2),s2)) by A15,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A11
,A13,A7,A9,Th45,A33
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A21,A32,EXTPRO_1:23;
A35: now
    let x be set;
A36: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    assume
A37: x in dom IExec(IF,s);
    per cases by A37,SCMPDS_4:20;
    suppose
A38:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A39:  not x in dom SAl by A36,TARSKI:def 1;
A40: not x in dom (s | A) by A12,A38,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A34,A38,SCMPDS_4:23
        .= IExec(I,s).x by A40,FUNCT_4:12
        .= (IExec(I,s) +* SAl).x by A39,FUNCT_4:12;
    end;
    suppose
A41:  x = IC SCMPDS;
A42: not x in dom (s | A) by A12,A41,COMPOS_1:3;
      then
A43:  IC Result(ProgramPart(s2),s2) = IC IExec(I,s) by A41,FUNCT_4:12
        .=  card I by A10,A14,Th48;
A44:  x in dom SAl by A36,A41,TARSKI:def 1;
A45: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A42,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A21,A32,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A41,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 1
        by A11,A13,A7,A9,Th45,A45
        .= IC Result(ProgramPart(s2),s2) + 1 by A15,EXTPRO_1:23
        .= SAl.IC SCMPDS by A43,FUNCOP_1:87
        .= (IExec(I,s) +* SAl).x by A41,A44,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I,s) +* SAl) by PARTFUN1:def 4;
  hence thesis by A35,FUNCT_1:9;
end;

theorem Th125:
  for s being State of SCMPDS,I being Program of SCMPDS,a being
Int_position, k1 being Integer st s.DataLoc(s.a,k1) >= 0 holds IExec(if<0(a,k1,
  I),s) = s +* Start-At((card I+1),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a be Int_position, k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  set IF=if<0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4 =
  Comput(ProgramPart(s3), s3,1);
  set i = (a,k1)>=0_goto (card I + 1);
  set SAl=Start-At((card I+1),SCMPDS);
A1: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A2: IC s3 = 0 by A1,FUNCT_4:26,SCMPDS_5:18;
A3: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A4: s3.DataLoc(s3.a,k1)=s3.b by A1,FUNCT_4:12
    .= s.b by A3,A1,FUNCT_4:12;
A5: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by Th22,A1;
  assume
  s.b >= 0;
  then
A6: IC s4 = ICplusConst(s3,card I + 1) by A5,A4,SCMPDS_2:69
    .= (0+(card I + 1)) by A2,Th23;
  Initialize pIF c= s3 by A1,FUNCT_4:26;
  then
A7: pIF c= s4 by AMI_1:81,COMPOS_1:132;
A8:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A9: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A10: card IF=card I+1 by Th15;
  then (card I+1) in dom pIF by Th25;
  then s4.(card I+1) = pIF.(card I+1) by A7,GRFUNC_1:8
    .=halt SCMPDS by A10,Th25;
  then
A11: CurInstr(ProgramPart s3,s4) = halt SCMPDS by A6,A8,A9;
  then
A12: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A13: CurInstr(ProgramPart s3,s3) = i by Th22,A1;
  now
    let l be Element of NAT;
A14: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    assume
    l < 1;
    then l <1+0;
    then l=0 by NAT_1:13;
    then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
     = CurInstr(ProgramPart s3,s3) by A14;
    hence CurInstr(ProgramPart (s3),Comput(ProgramPart(
s3),s3,l))
     <> halt SCMPDS by A13,Th31;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds 1 <= l;
  then LifeSpan(ProgramPart(s3),s3) = 1 by A11,A12,EXTPRO_1:def 14;
  then
A15: s4 = Result(ProgramPart(s3),s3) by A12,EXTPRO_1:23;
A16: dom ProgramPart s = A by COMPOS_1:34;
A17: now
A18: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    let x be set;
    assume
A19: x in dom IExec(IF,s);
    per cases by A19,SCMPDS_4:20;
    suppose
A20:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A21:  not x in dom SAl by A18,TARSKI:def 1;
      not x in dom (s | A) by A16,A20,SCMPDS_2:53;
      hence IExec(IF,s).x = s4.x by A15,FUNCT_4:12
        .= s3.x by A5,A20,SCMPDS_2:69
        .= s.x by A20,SCMPDS_5:19
        .= (s +* SAl).x by A21,FUNCT_4:12;
    end;
    suppose
A22:  x = IC SCMPDS;
      not x in dom (s | A) by A16,A22,COMPOS_1:3;
      hence IExec(IF,s).x = (card I + 1) by A6,A15,A22,FUNCT_4:12
        .= (s +* SAl).x by A22,FUNCT_4:121;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (s +* SAl).x by Th27;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (s +* SAl) by PARTFUN1:def 4;
  hence thesis by A17,FUNCT_1:9;
end;

registration
  let I be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if<0(a,k1,I) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)>=0_goto (card I +1);
    set IF=if<0(a,k1,I), pIF = stop IF;
    thus IF is shiftable;
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
:::      Initialize
      pIF c= s;
      then
A2:   s = Initialize s +* pIF by A1,FUNCT_4:79;
A3:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1) < 0;
        then IF is_halting_on s by A3,Th122;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) >= 0;
        then IF is_halting_on s by Th123;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if<0(a,k1,I) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a being Int_position,k1 being Integer holds IC IExec(if<0(a,
  k1,I),s) =  (card I + 1)
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if<0(a,k1,I);
A1: I is_closed_on s & I is_halting_on s by Th34,Th35;
  per cases;
  suppose
    s.DataLoc(s.a,k1) < 0;
    then IExec(IF,s) =IExec(I,s) +* Start-At((card I+1),SCMPDS)
    by A1,Th124;
    hence thesis by FUNCT_4:121;
  end;
  suppose
    s.DataLoc(s.a,k1) >= 0;
    then IExec(IF,s) =s +* Start-At((card I+1),SCMPDS) by Th125;
    hence thesis by FUNCT_4:121;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a,b being Int_position,k1 being Integer st s.DataLoc(s.a,k1)
  < 0 holds IExec(if<0(a,k1,I),s).b = IExec(I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a,b be Int_position,k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1) < 0;
A2: not b in dom Start-At((card I+1),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then IExec(if<0(a,k1,I),s) = IExec(I,s) +* Start-At((card I+1),SCMPDS)
  by A1,Th124;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,a,b being
Int_position, k1 being Integer st s.DataLoc(s.a,k1) >= 0 holds IExec(if<0(a,k1,
  I),s).b = s.b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a,b be Int_position, k1 be
  Integer;
  assume
  s.DataLoc(s.a,k1) >= 0;
  then
A1: IExec(if<0(a,k1,I),s) = s +* Start-At((card I+1),SCMPDS) by Th125;
  not b in dom Start-At((card I+1),SCMPDS) by SCMPDS_4:59;
  hence thesis by A1,FUNCT_4:12;
end;

begin :: The computation of "if var>=0 then block"

theorem
  card if>=0(a,k1,I) = card I + 2 by Lm8;

theorem
   0 in dom if>=0(a,k1,I) &  1 in dom if>=0(a,k1,I) by Lm9;

theorem
  if>=0(a,k1,I). 0 = (a,k1)>=0_goto 2 & if>=0(a,k1,I). 1 =
  goto (card I + 1) by Lm10;

theorem Th132:
  for s being State of SCMPDS, I being shiftable Program of
  SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1) >= 0 & I
is_closed_on s & I is_halting_on s holds if>=0(a,k1,I) is_closed_on s & if>=0(a
  ,k1,I) is_halting_on s
proof
  let s be State of SCMPDS, I be shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if>=0(a,k1,I), pIF=stop IF, pI=stop I,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3),s3,1);
  set i = (a,k1)>=0_goto 2, j = goto (card I + 1);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF = i ';' (j ';' I) by SCMPDS_4:52;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A5,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:69;
  end;
  then
A6: DataPart s2 = DataPart s4 by SCMPDS_4:23;
A7: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A8: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A7,A2,FUNCT_4:12;
A9: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  assume
  s.b >= 0;
  then
A10: IC s4 = ICplusConst(s3,2) by A4,A8,SCMPDS_2:69
    .= (0+2) by A9,Th23;
  assume
A11: I is_closed_on s;
  then
A12: I is_closed_on s2 by Th38;
  assume
  I is_halting_on s;
  then
A13: ProgramPart s2 halts_on s2 by Def3;
A14: Initialize pI c= s2 & Shift(pI,2) c= s4 by Lm7,A1,FUNCT_4:26;
A15:  0 in dom pIF by COMPOS_1:135;
  now
    let k be Element of NAT;
    per cases;
    suppose
      0 < k;
      then consider k1 being Nat such that
A16:  k1 + 1 = k by NAT_1:6;
      reconsider k1 as Element of NAT by ORDINAL1:def 13;
      reconsider m = IC Comput(ProgramPart(s2), s2,k1) as Element of NAT;
A17:  card pIF = 1+ card IF by SCMPDS_5:7
        .= 1+(card I +2) by Lm8
        .= 1+card I +2
        .=card pI+2 by SCMPDS_5:7;
       m in dom pI by A11,Def2;
      then m < card pI by AFINSQ_1:70;
      then
A18:  m+2 < card pIF by A17,XREAL_1:8;
A19: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      IC Comput(ProgramPart(s3),s3,k) = IC Comput(ProgramPart(s3), s4,k1) by
A16,EXTPRO_1:5
        .=  (m + 2) by A12,A14,A10,A6,Th45,A19;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A18,AFINSQ_1:70;
    end;
    suppose
      k = 0;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A15,A9,EXTPRO_1:3;
    end;
  end;
  hence IF is_closed_on s by Def2;
A20: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A21: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
by EXTPRO_1:5;
A22: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2)
)
by AMI_1:123;
A23: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
  CurInstr(ProgramPart s3,
  Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
   =CurInstr(ProgramPart s4,
   Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A21,A20
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2))) by A12,A14,A10,A6
,Th45,A23,A22
    .= halt SCMPDS by A13,EXTPRO_1:def 14;
  then ProgramPart s3 halts_on s3 by EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th133:
  for s being State of SCMPDS,I being Program of SCMPDS, a being
  Int_position, k1 being Integer st s.DataLoc(s.a,k1) < 0 holds if>=0(a,k1,I)
  is_closed_on s & if>=0(a,k1,I) is_halting_on s
proof
  let s be State of SCMPDS,I be Program of SCMPDS, a be Int_position,k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  assume
A1: s.b < 0;
  set i = (a,k1)>=0_goto 2, j = goto (card I + 1);
  set IF=if>=0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4
  = Comput(ProgramPart(s3),s3,1), s5 = Comput(ProgramPart(s3),s3,2);
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF =i ';' (j ';' I) by SCMPDS_4:52;
A4: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A5: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A4,A2,FUNCT_4:12;
A6: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
  Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
  Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
  then
A7: IC s4 = succ IC s3 by A1,A5,SCMPDS_2:69
    .= (0+1) by A6;
A8: Initialize pIF c= s3 by A2,FUNCT_4:26;
  then
A9: pIF c= s4 by AMI_1:81,COMPOS_1:132;
A10:  1 in dom IF by Lm9;
  then  1 in dom pIF by Th18;
  then
A11: s4. 1 = pIF. 1 by A9,GRFUNC_1:8
    .=IF. 1 by A10,Th19
    .=j by Lm10;
A12: card IF=card I+2 by Lm8;
  then
A13: (card I+2) in dom pIF by Th25;
A14:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A15: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
  Comput(ProgramPart(s3), s3,1 + 1) = Following(ProgramPart s3,s4) by
EXTPRO_1:4
    .= Exec(j,s4) by A7,A11,A14,A15;
  then
A16: IC s5 = ICplusConst(s4,card I+1) by SCMPDS_2:66
    .= (card I+1+1) by A7,Th23
    .= (card I+(1+1));
A17:  (ProgramPart s5)/.IC s5
 = s5.IC s5 by COMPOS_1:38;
A18: ProgramPart s3 = ProgramPart s5
by AMI_1:123;
  pIF c= s5 by A8,AMI_1:81,COMPOS_1:132;
  then s5.(card I+2) = pIF.(card I+2) by A13,GRFUNC_1:8
    .=halt SCMPDS by A12,Th25;
  then
A19: CurInstr(ProgramPart s3,s5) = halt SCMPDS by A16,A17,A18;
  now
    let k be Element of NAT;
A20: k = 0 or 0 + 1 <= k by INT_1:20;
    per cases by A20,XXREAL_0:1;
    suppose
      k = 0;
      then Comput(ProgramPart(s3),s3,k) = s3 by EXTPRO_1:3;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A6,COMPOS_1:135;
    end;
    suppose
      k = 1;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF by A10,A7,Th18;
    end;
    suppose
      1 < k;
      then 1+1 <= k by INT_1:20;
      hence IC Comput(ProgramPart(s3),s3,k) in dom pIF
      by A13,A16,A19,EXTPRO_1:6;
    end;
  end;
  hence IF is_closed_on s by Def2;
  ProgramPart s3 halts_on s3 by A19,EXTPRO_1:30;
  hence thesis by Def3;
end;

theorem Th134:
  for s being State of SCMPDS,I being halt-free shiftable
  Program of SCMPDS, a being Int_position,k1 being Integer st s.DataLoc(s.a,k1)
  >= 0 & I is_closed_on s & I is_halting_on s holds IExec(if>=0(a,k1,I),s) =
  IExec(I,s) +* Start-At((card I+2),SCMPDS)
proof
  let s be State of SCMPDS,I be halt-free shiftable Program of SCMPDS, a be
  Int_position,k1 be Integer;
  set b=DataLoc(s.a,k1);
  set IF=if>=0(a,k1,I), pIF = stop IF, pI=stop I,
   s2 = Initialize s +* pI, s3 = Initialize s +* pIF,
   s4 = Comput(ProgramPart(s3), s3,1);
  set i = (a,k1)>=0_goto 2, j = goto (card I + 1);
  set SAl=Start-At((card I+2),SCMPDS);
A1: s +* Initialize pI = Initialize s +* pI by COMPOS_1:125;
A2: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A3: IF=i ';' (j ';' I) by SCMPDS_4:52;
A4: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A3,Th22,A2;
A5: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A6: s3.DataLoc(s3.a,k1)=s3.b by A2,FUNCT_4:12
    .= s.b by A5,A2,FUNCT_4:12;
A7: IC s3 = 0 by A2,FUNCT_4:26,SCMPDS_5:18;
A8: Initialize pI c= s2 & Shift(pI,2) c= s4 by Lm7,A1,FUNCT_4:26;
A9: DataPart s2 = DataPart s3 by COMPOS_1:138,FUNCT_7:134;
  now
    let a;
    thus s2.a = s3.a by A9,SCMPDS_4:23
      .= s4.a by A4,SCMPDS_2:69;
  end;
  then
A10: DataPart s2 = DataPart s4 by SCMPDS_4:23;
  assume
  s.b >= 0;
  then
A11: IC s4 = ICplusConst(s3,2) by A4,A6,SCMPDS_2:69
    .= (0+2) by A7,Th23;
A12: dom ProgramPart s = A by COMPOS_1:34;
  assume
A13: I is_closed_on s;
  then
A14: I is_closed_on s2 by Th38;
  assume
A15: I is_halting_on s;
  then
A16: ProgramPart s2 halts_on s2 by Def3;
A17: ProgramPart s3 = ProgramPart Comput(
ProgramPart(s3),s3,1)
by AMI_1:123;
A18: Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),LifeSpan(ProgramPart(s2)
,s2))
 by EXTPRO_1:5;
A19: ProgramPart s4 = ProgramPart
Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))
by AMI_1:123;
A20: ProgramPart s2 = ProgramPart
Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),
s2))
by AMI_1:123;
A21: CurInstr(ProgramPart s3,
Comput(ProgramPart(s3),s3,LifeSpan(ProgramPart(s2),s2)+1))
 =CurInstr(ProgramPart s4,
 Comput(ProgramPart(s4),s4,LifeSpan(ProgramPart(s2),s2))) by A18,A17
    .=CurInstr(ProgramPart s2,
    Comput(ProgramPart(s2),s2,LifeSpan(ProgramPart(s2),s2)))
     by A14,A8,A11,A10,Th45,A20,A19
    .= halt SCMPDS by A16,EXTPRO_1:def 14;
  then
A22: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A23: CurInstr(ProgramPart s3,s3) = i by A3,Th22,A2;
  now
    let l be Element of NAT;
    assume
A24: l < LifeSpan(ProgramPart(s2),s2) + 1;
A25: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
    per cases;
    suppose
      l = 0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
      by A25;
      hence CurInstr(ProgramPart (s3),Comput(
ProgramPart(s3),s3,l))
       <> halt SCMPDS by A23,Th31;
    end;
    suppose
      l <> 0;
      then consider n be Nat such that
A26:  l = n + 1 by NAT_1:6;
      reconsider n as Element of NAT by ORDINAL1:def 13;
A27:  n < LifeSpan(ProgramPart(s2),s2) by A24,A26,XREAL_1:8;
      assume
A28:  CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),
s3,l)) = halt SCMPDS;
A29: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),
s3,1)
by AMI_1:123;
A30: Comput(ProgramPart(s3),s3,n+1)
 = Comput(ProgramPart(s3),Comput(ProgramPart(s3),s3,1),n) by EXTPRO_1:5;
A31: ProgramPart s2 = ProgramPart Comput(ProgramPart(s2),s2,n)
by AMI_1:123;
A32: ProgramPart s4 = ProgramPart Comput(ProgramPart(s4),s4,n)
by AMI_1:123;
      CurInstr(ProgramPart s2,Comput(ProgramPart(s2),s2,n))
       = CurInstr(ProgramPart s4,Comput(ProgramPart(s4),s4,n))
       by A14,A8,A11,A10,Th45,A31,A32
        .= halt SCMPDS by A26,A28,A30,A29;
      hence contradiction by A16,A27,EXTPRO_1:def 14;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds LifeSpan(ProgramPart(s2),s2) + 1 <= l;
  then
A33: LifeSpan(ProgramPart(s3),s3) = LifeSpan(ProgramPart(s2),s2) + 1 by A21,A22
,EXTPRO_1:def 14;
A34: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A35: DataPart Result(ProgramPart(s2),s2) = DataPart Comput(ProgramPart(s2),s2,
LifeSpan(ProgramPart(s2),s2)) by A16,EXTPRO_1:23
    .= DataPart Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by
A14,A8,A11,A10,Th45,A34
    .= DataPart Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1)
by EXTPRO_1:5
    .= DataPart Result(ProgramPart(s3),s3) by A22,A33,EXTPRO_1:23;
A36: now
    let x be set;
A37: dom Start-At((card I+2),SCMPDS) = {IC SCMPDS} by FUNCOP_1:19;
    assume
A38: x in dom IExec(IF,s);
    per cases by A38,SCMPDS_4:20;
    suppose
A39:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A40:  not x in dom SAl by A37,TARSKI:def 1;
A41: not x in dom (s | A) by A12,A39,SCMPDS_2:53;
      hence IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by FUNCT_4:12
        .= (Result(ProgramPart(s2),s2)).x by A35,A39,SCMPDS_4:23
        .= IExec(I,s).x by A41,FUNCT_4:12
        .= (IExec(I,s) +* SAl).x by A40,FUNCT_4:12;
    end;
    suppose
A42:  x = IC SCMPDS;
A43: not x in dom (s | A) by A12,A42,COMPOS_1:3;
      then
A44:  IC Result(ProgramPart(s2),s2) = IC IExec(I,s) by A42,FUNCT_4:12
        .=  card I by A13,A15,Th48;
A45:  x in dom SAl by A37,A42,TARSKI:def 1;
A46: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
      thus IExec(IF,s).x = (Result(ProgramPart(s3),s3)).x by A43,FUNCT_4:12
        .= Comput(ProgramPart(s3), s3,LifeSpan(ProgramPart(s2),s2) + 1).x by
A22,A33,EXTPRO_1:23
        .= IC Comput(ProgramPart(s3), s4,LifeSpan(ProgramPart(s2),s2)) by A42,
EXTPRO_1:5
        .= IC Comput(ProgramPart(s2), s2,LifeSpan(ProgramPart(s2),s2)) + 2
        by A14,A8,A11,A10,Th45,A46
        .= IC Result(ProgramPart(s2),s2) + 2 by A16,EXTPRO_1:23
        .= SAl.IC SCMPDS by A44,FUNCOP_1:87
        .= (IExec(I,s) +* SAl).x by A42,A45,FUNCT_4:14;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (IExec(I,s) +* SAl).x by Th26;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (IExec(I,s) +* Start-At((card I+2),SCMPDS)) by PARTFUN1:def 4;
  hence thesis by A36,FUNCT_1:9;
end;

theorem Th135:
  for s being State of SCMPDS,I being Program of SCMPDS,a being
Int_position, k1 being Integer st s.DataLoc(s.a,k1) < 0 holds IExec(if>=0(a,k1,
  I),s) = s +* Start-At((card I+2),SCMPDS)
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a be Int_position, k1 be
  Integer;
  set b=DataLoc(s.a,k1);
  set IF=if>=0(a,k1,I), pIF=stop IF, s3 = Initialize s +* pIF, s4
  = Comput(ProgramPart(s3),s3,1), s5 = Comput(ProgramPart(s3),s3,2);
  set i = (a,k1)>=0_goto 2, j = goto (card I + 1);
  set SAl=Start-At((card I+2),SCMPDS);
A1: s +* Initialize pIF = Initialize s +* pIF by COMPOS_1:125;
A2: IF =i ';' (j ';' I) by SCMPDS_4:52;
A3: not b in dom Initialize pIF by SCMPDS_4:31;
  not a in dom Initialize pIF by SCMPDS_4:31;
  then
A4: s3.DataLoc(s3.a,k1)=s3.b by A1,FUNCT_4:12
    .= s.b by A3,A1,FUNCT_4:12;
A5: IC s3 = 0 by A1,FUNCT_4:26,SCMPDS_5:18;
A6: Initialize pIF c= s3 by A1,FUNCT_4:26;
  then
A7: pIF c= s4 by AMI_1:81,COMPOS_1:132;
A8: pIF c= s5 by A6,AMI_1:81,COMPOS_1:132;
A9: Comput(ProgramPart(s3), s3,0 + 1) = Following(ProgramPart s3,
Comput(ProgramPart(s3),s3,0)) by EXTPRO_1:4
    .= Following(ProgramPart s3,s3) by EXTPRO_1:3
    .= Exec(i,s3) by A2,Th22,A1;
  assume
  s.b < 0;
  then
A10: IC s4 = succ IC s3 by A9,A4,SCMPDS_2:69
    .= (0+1) by A5;
A11:  1 in dom IF by Lm9;
  then  1 in dom pIF by Th18;
  then
A12: s4. 1 = pIF. 1 by A7,GRFUNC_1:8
    .=IF. 1 by A11,Th19
    .=j by Lm10;
A13:  (ProgramPart s4)/.IC s4
 = s4.IC s4 by COMPOS_1:38;
A14: ProgramPart s3 = ProgramPart s4
by AMI_1:123;
A15: Comput(ProgramPart(s3), s3,1 + 1) = Following(ProgramPart s3,s4) by
EXTPRO_1:4
    .= Exec(j,s4) by A10,A12,A13,A14;
  then
A16: IC s5 = ICplusConst(s4,card I+1) by SCMPDS_2:66
    .= (card I+1+1) by A10,Th23
    .= (card I+(1+1));
A17:  (ProgramPart s5)/.IC s5
 = s5.IC s5 by COMPOS_1:38;
A18: ProgramPart s3 = ProgramPart s5
by AMI_1:123;
A19: card IF=card I+2 by Lm8;
  then (card I+2) in dom pIF by Th25;
  then s5.(card I+2) = pIF.(card I+2) by A8,GRFUNC_1:8
    .=halt SCMPDS by A19,Th25;
  then
A20: CurInstr(ProgramPart s3,s5) = halt SCMPDS by A16,A17,A18;
  then
A21: ProgramPart s3 halts_on s3 by EXTPRO_1:30;
A22: CurInstr(ProgramPart s3,s3) = i by A2,Th22,A1;
  now
    let l be Element of NAT;
    assume
    l < 1+1;
    then
A23: l <= 1 by NAT_1:13;
A24: Comput(ProgramPart(s3),s3,0) = s3 by EXTPRO_1:3;
A25
:  (ProgramPart Comput(ProgramPart(s3),s3,l))/.IC Comput(ProgramPart(s3),s3,l)
 = Comput(ProgramPart(s3),s3,l).IC Comput(ProgramPart(s3),s3,l) by COMPOS_1:38;
A26: ProgramPart s3 = ProgramPart Comput(ProgramPart(s3),s3,l)
by AMI_1:123;
    per cases by A23,NAT_1:26;
    suppose
      l=0;
      then CurInstr(ProgramPart s3,Comput(ProgramPart(s3),s3,l))
       = CurInstr(ProgramPart s3,s3)
       by A24;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A22,Th31;
    end;
    suppose
      l=1;
      hence CurInstr(ProgramPart (s3),Comput(ProgramPart(s3),s3,l))
       <> halt SCMPDS by A10,A12,A25,A26,SCMPDS_2:85;
    end;
  end;
  then for l be Element of NAT st CurInstr(ProgramPart (s3),
  Comput(ProgramPart(s3),s3,l)) = halt SCMPDS
  holds 2 <= l;
  then LifeSpan(ProgramPart(s3),s3) = 2 by A20,A21,EXTPRO_1:def 14;
  then
A27: s5 = Result(ProgramPart(s3),s3) by A21,EXTPRO_1:23;
A28: dom ProgramPart s = A by COMPOS_1:34;
A29: now
A30: dom SAl = {IC SCMPDS} by FUNCOP_1:19;
    let x be set;
    assume
A31: x in dom IExec(IF,s);
    per cases by A31,SCMPDS_4:20;
    suppose
A32:  x is Int_position;
      then x <> IC SCMPDS by SCMPDS_2:52;
      then
A33:  not x in dom SAl by A30,TARSKI:def 1;
      not x in dom (s | A) by A28,A32,SCMPDS_2:53;
      hence IExec(IF,s).x = s5.x by A27,FUNCT_4:12
        .= s4.x by A15,A32,SCMPDS_2:66
        .= s3.x by A9,A32,SCMPDS_2:69
        .= s.x by A32,SCMPDS_5:19
        .= (s +* SAl).x by A33,FUNCT_4:12;
    end;
    suppose
A34:  x = IC SCMPDS;
      not x in dom (s | A) by A28,A34,COMPOS_1:3;
      hence IExec(IF,s).x = (card I + 2) by A16,A27,A34,FUNCT_4:12
        .= (s +* Start-At((card I+2),SCMPDS)).x by A34,FUNCT_4:121;
    end;
    suppose
      x is Element of NAT;
      hence IExec(IF,s).x = (s +* SAl).x by Th27;
    end;
  end;
  dom IExec(IF,s) = the carrier of SCMPDS by PARTFUN1:def 4
    .= dom (s +* SAl) by PARTFUN1:def 4;
  hence thesis by A29,FUNCT_1:9;
end;

registration
  let I be shiftable parahalting Program of SCMPDS, a be Int_position,k1 be
  Integer;
  cluster if>=0(a,k1,I) -> shiftable parahalting;
  correctness
  proof
    set i = (a,k1)>=0_goto 2, j = goto (card I + 1);
    set IF=if>=0(a,k1,I), pIF = stop IF;
    thus IF is shiftable;
      let s be 0-started State of SCMPDS;
A1:  Initialize s = s by COMPOS_1:78;
      assume
:::      Initialize
      pIF c= s;
      then
A2:   s = Initialize s +* pIF by A1,FUNCT_4:79;
A3:   I is_closed_on s & I is_halting_on s by Th34,Th35;
      per cases;
      suppose
        s.DataLoc(s.a,k1) >= 0;
        then IF is_halting_on s by A3,Th132;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
      suppose
        s.DataLoc(s.a,k1) < 0;
        then IF is_halting_on s by Th133;
        hence ProgramPart s halts_on s by A2,Def3;
      end;
  end;
end;

registration
  let I be halt-free Program of SCMPDS, a be Int_position,k1 be Integer;
  cluster if>=0(a,k1,I) -> halt-free;
  coherence;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a being Int_position,k1 being Integer holds IC IExec(if>=0(a,
  k1,I),s) =  (card I + 2)
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a be Int_position,k1 be Integer;
  set IF=if>=0(a,k1,I);
A1: I is_closed_on s & I is_halting_on s by Th34,Th35;
  per cases;
  suppose
    s.DataLoc(s.a,k1) >= 0;
    then IExec(IF,s) =IExec(I,s) +* Start-At((card I+2),SCMPDS)
    by A1,Th134;
    hence thesis by FUNCT_4:121;
  end;
  suppose
    s.DataLoc(s.a,k1) < 0;
    then IExec(IF,s) =s +* Start-At((card I+2),SCMPDS) by Th135;
    hence thesis by FUNCT_4:121;
  end;
end;

theorem
  for s being State of SCMPDS,I being halt-free shiftable parahalting
Program of SCMPDS,a,b being Int_position,k1 being Integer st s.DataLoc(s.a,k1)
  >= 0 holds IExec(if>=0(a,k1,I),s).b = IExec(I,s).b
proof
  let s be State of SCMPDS,I be halt-free shiftable parahalting Program of
  SCMPDS,a,b be Int_position,k1 be Integer;
  assume
A1: s.DataLoc(s.a,k1) >= 0;
A2: not b in dom Start-At((card I+2),SCMPDS) by SCMPDS_4:59;
  I is_closed_on s & I is_halting_on s by Th34,Th35;
  then IExec(if>=0(a,k1,I),s) = IExec(I,s) +* Start-At((card I+2),SCMPDS)
   by A1,Th134;
  hence thesis by A2,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS,a,b being
Int_position, k1 being Integer st s.DataLoc(s.a,k1) < 0
 holds IExec(if>=0(a,k1,I),s).b = s.b
proof
  let s be State of SCMPDS,I be Program of SCMPDS,a,b be Int_position, k1 be
  Integer;
  assume
  s.DataLoc(s.a,k1) < 0;
  then
A1: IExec(if>=0(a,k1,I),s) = s +* Start-At((card I+2),SCMPDS) by Th135;
  not b in dom Start-At((card I+2),SCMPDS) by SCMPDS_4:59;
  hence thesis by A1,FUNCT_4:12;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS
   holds I is_closed_on s iff I is_closed_on Initialize s
proof let s be State of SCMPDS,I being Program of SCMPDS;
 thus I is_closed_on s implies I is_closed_on Initialize s
  proof assume for k being Element of NAT holds
   IC Comput(ProgramPart(Initialize s+*stop I),Initialize s+*stop I,k)
    in dom stop I;
   hence for k being Element of NAT holds
   IC Comput(ProgramPart(Initialize Initialize s+*stop I),
   Initialize Initialize s+*stop I,k)
    in dom stop I;
  end;
  assume for k being Element of NAT holds
   IC Comput(ProgramPart(Initialize Initialize s+*stop I),
   Initialize Initialize s+*stop I,k)
    in dom stop I;
  hence for k being Element of NAT holds
   IC Comput(ProgramPart(Initialize s+*stop I),Initialize s+*stop I,k)
    in dom stop I;
end;

theorem
  for s being State of SCMPDS,I being Program of SCMPDS
   holds I is_halting_on s iff I is_halting_on Initialize s
proof let s be State of SCMPDS,I be Program of SCMPDS;
 thus I is_halting_on s implies I is_halting_on Initialize s
  proof
   assume ProgramPart(Initialize s+*stop I) halts_on Initialize s+*stop I;
   hence ProgramPart(Initialize Initialize s+*stop I)
    halts_on Initialize Initialize s+*stop I;
  end;
 assume ProgramPart(Initialize Initialize s+*stop I)
    halts_on Initialize Initialize s+*stop I;
 hence ProgramPart(Initialize s+*stop I) halts_on Initialize s+*stop I;
end;

