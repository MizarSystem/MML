:: On the Partial Product of Series and Related Basic Inequalities
::  by Fuguo Ge and Xiquan Liang
::
:: Received July 6, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM, ARYTM_1, ARYTM_3, ASYMPT_0, SQUARE_1, GROUP_1, ABSVALUE,
      FUNCT_1, POWER, PREPOWER, SEQ_1, SERIES_1, SERIES_3, ZF_LANG;
 notations ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, NAT_1, SQUARE_1,
      NEWTON, REAL_1, PREPOWER, COMPLEX1, SEQ_1, BINOP_2, POWER, SERIES_1,
      FUNCT_2;
 constructors NAT_1, SQUARE_1, REAL_1, PREPOWER, BINOP_2, SERIES_1, SEQ_1,
      XXREAL_0;
 registrations XREAL_0, SQUARE_1, NEWTON, MEMBERED, XCMPLX_0, INT_1, COMPLEX1,
      XXREAL_0;
 requirements REAL, SUBSET, BOOLE, NUMERALS, ARITHM;
 definitions SQUARE_1;
 theorems XCMPLX_1, SQUARE_1, SIN_COS2, REAL_1, REAL_2, POLYEQ_2, ABSVALUE,
      NEWTON, POWER, ASYMPT_1, XREAL_1, PREPOWER, FUNCT_2, SERIES_1, SEQ_1,
      COMPLEX1, POLYEQ_1, XXREAL_0;
 schemes NAT_1, RECDEF_1;

begin

 reserve a,b,c for positive (real number),
         m,x,y,z for real number,
         n for Nat,
         s,s1,s2,s3,s4,s5 for Real_Sequence;

registration let x; :: move to another article
  cluster abs x -> non negative;
  coherence by COMPLEX1:132;
end;

Lm1:x^2=x|^2
proof
  x^2=x*x
 .=x|^1*x by NEWTON:10
 .=x|^(1+1) by NEWTON:11
 .=x|^2;
 hence thesis;
end;

Lm2:1|^3=1 by NEWTON:15;

Lm3:2|^3=8
proof
  2|^3=2|^(2+1)
  .=2|^2*2 by NEWTON:11
  .=2^2*2 by Lm1
  .=2*2*2
  .=8;
  hence thesis;
end;

Lm4:3|^3=27
proof
  3|^3=3|^(2+1)
  .=3|^2*3 by NEWTON:11
  .=3^2*3 by Lm1
  .=3*3*3
  .=27;
  hence thesis;
end;

Lm5:(-x)|^3=-x|^3
proof
  3 = 2*1 + 1;
  hence thesis by POWER:2;
end;

Lm6:(x+y)|^3=x|^3+3*x^2*y+3*x*y^2+y|^3 :: revise POLYEQ_1:14
proof
  (x+y)|^3=x|^3+(3*y*x^2+3*y^2*x)+y|^3 by POLYEQ_1:14
         .=x|^3+3*x^2*y+3*x*y^2+y|^3;
  hence thesis;
end;

Lm7:x|^3+y|^3=(x+y)*(x^2-x*y+y^2)
proof
  (x^2-x*y+y^2)*(x+y)
    =x^2*x+y*x^2-(x*(x*y)+y*(x*y))+(x*y^2+y*y^2+0*y^2)
   .=x|^ 3+y*x^2-(x*(x*y)+y*(x*y))+(x*y^2+y*y^2) by POLYEQ_2:4
   .=x|^ 3+y*x^2-(x*x*y+y*(y*x))+(x*y^2+y|^ 3) by POLYEQ_2:4
   .=x|^ 3+y*x^2-(x^2*y+y*y*x)+(x*y^2+y|^ 3)
   .=x|^ 3+x^2*y-(x^2*y+x*y^2)+(x*y^2+y|^ 3)
   .=x|^3+y|^3;
  hence thesis;
end;

Lm8:
  x^2<=y*z implies abs(x) <= sqrt(y*z)
proof
  assume A1:x^2<=y*z;
  x^2>=0 by XREAL_1:65;then
A2:sqrt(x^2)<=sqrt(y*z) by A1,SQUARE_1:94;
  per cases;
  suppose A3:x>=0;then
    x<=sqrt(y*z) by A2,SQUARE_1:89;
    hence thesis by A3,ABSVALUE:def 1;
  end;
  suppose A4:x<0;
    -x<=sqrt(y*z) by A2,A4,SQUARE_1:90;
    hence thesis by A4,ABSVALUE:def 1;
  end;
end;

theorem
  y>x & x>=0 & m>=0 implies x/y <= (x+m)/(y+m)
proof
   assume A1:y>x & x>=0 & m>=0;then
   y-x>0 by XREAL_1:52;then
A2: m*(y-x)>=0 by A1,XREAL_1:129;
A3: y+m > 0 by A1,XREAL_1:36; then
   y*(y+m) > 0 by A1,XREAL_1:131; then
   (y*(x+m)-x*(m+y))/(y*(y+m))>=0 by A2,XREAL_1:138;then
   (x+m)/(y+m)-x/y>=0 by A1,A3,XCMPLX_1:131;then
   (x+m)/(y+m)-x/y+x/y>=0+x/y by XREAL_1:8;
   hence thesis;
end;

theorem Th2:
  (a+b)/2>=sqrt(a*b)
proof
  a+b>=2*sqrt(a*b) by SIN_COS2:1;then
  (a+b)/2>=(2*sqrt(a*b))/2 by XREAL_1:74;
  hence thesis;
end;

theorem
  b/a+a/b>=2
proof
   (a-b)^2>=0 by XREAL_1:65;then
   a^2-2*a*b+b^2>=0;then
   a^2-2*a*b+b^2+2*a*b>=0+2*a*b by XREAL_1:9;then
   (a^2+b^2)/(a*b)>=(2*(a*b))/(1*(a*b)) by REAL_1:73;then
A1:(a^2+b^2)/(a*b)>=2/1 by XCMPLX_1:92;
   b/a+a/b = (b*b)/(a*b)+a/b by XCMPLX_1:92
    .= (b*b)/(a*b)+(a*a)/(a*b) by XCMPLX_1:92
    .= (b*b+a*a)/(a*b) by XCMPLX_1:63
    .= (b^2+a*a)/(a*b)
    .= (b^2+a^2)/(a*b);
   hence b/a + a/b>=2 by A1;
end;

theorem Th4:
  ((x+y)/2)^2>=x*y
proof
  (x-y)^2>=0 by XREAL_1:65;then
  x^2-2*x*y+y^2>=0;then
  x^2-2*x*y+y^2+2*x*y>=0+2*x*y by XREAL_1:9;then
  x^2+ y^2+2*x*y>=2*x*y+2*x*y by XREAL_1:9;then
  x^2+2*x*y+y^2>=2*x*y+2*x*y;then
  (x+y)^2>=4*x*y;then
  (x+y)^2/(2*2)>=4*(x*y)/4 by REAL_1:73;then
  (x+y)^2/(2^2)>=4*(x*y)/4;
  hence thesis by XCMPLX_1:77;
end;

theorem
  (x^2+y^2)/2>=((x+y)/2)^2
proof
  (x-y)^2>=0 by XREAL_1:65;then
  x^2-2*x*y+y^2>=0;then
  x^2-2*x*y+y^2+2*x*y>=0+2*x*y by XREAL_1:9;then
  x^2+y^2+(x^2+y^2)>=2*x*y+(x^2+y^2) by XREAL_1:9;then
  2*x^2+2*y^2>=x^2+2*x*y+y^2;then
  2*x^2+2*y^2>=(x+y)^2;then
  2*(x^2+y^2)/4>=(x+y)^2/4 by REAL_1:73;then
  (x^2+y^2)/2>=(x+y)^2/(2*2);then
  (x^2+y^2)/2>=(x+y)^2/(2^2);
  hence thesis by XCMPLX_1:77;
end;

theorem Th6:
  x^2+y^2>=2*x*y
proof
  (x-y)^2>=0 by XREAL_1:65;then
  x^2-2*x*y+y^2>=0;then
  x^2-2*x*y+y^2+2*x*y>=0+2*x*y by XREAL_1:8;
  hence thesis;
end;

theorem Th7:
  (x^2+y^2)/2>=x*y
proof
  x^2+y^2>=2*x*y by Th6;then
  (x^2+y^2)/2>=(2*x*y)/2 by XREAL_1:74;
  hence thesis;
end;

theorem Th8:  :: CSSPACE2:5
  x^2+y^2>=2*abs(x)*abs(y)
proof
A1:x^2>=0 & y^2>=0 by XREAL_1:65;then
  x^2+y^2>=2*sqrt(x^2*y^2) by SIN_COS2:1;then
  x^2+y^2>=2*(sqrt(x^2)*sqrt(y^2)) by A1,SQUARE_1:97;then
  x^2+y^2>=2*(sqrt((abs x)^2)*sqrt(y^2)) by COMPLEX1:161;then
  x^2+y^2>=2*(sqrt((abs x)^2)*sqrt((abs y)^2)) by COMPLEX1:161;then
  x^2+y^2>=2*(abs(x)*sqrt((abs y)^2)) by SQUARE_1:89;then
  x^2+y^2>=2*(abs(x)*abs(y)) by SQUARE_1:89;
  hence thesis;
end;

theorem Th9:
  (x+y)^2>=4*x*y
proof
  (x^2+y^2)/2>=x*y by Th7;then
  2*((x^2+y^2)/2)>=2*(x*y) by XREAL_1:66;then
  x^2+y^2+2*x*y>=2*x*y+2*x*y by XREAL_1:8;then
  x^2+2*x*y+y^2>=2*x*y+2*x*y;
  hence thesis;
end;

theorem Th10:
  x^2+y^2+z^2>=x*y+y*z+x*z
proof
A1:x^2+y^2>=2*x*y by Th6;
A2:y^2+z^2>=2*y*z by Th6;
A3:z^2+x^2>=2*z*x by Th6;
   x^2+y^2+(y^2+z^2)>=2*x*y+2*y*z by A1,A2,XREAL_1:9;then
   (x^2+y^2+(y^2+z^2))+(z^2+x^2)>=(2*x*y+2*y*z)+(2*z*x)
     by A3,XREAL_1:9;then
   2*(x^2+y^2+z^2)/2>=2*(x*y+y*z+z*x)/2 by XREAL_1:74;
   hence thesis;
end;

theorem
  (x+y+z)^2>=3*(x*y+y*z+x*z)
proof
  x^2+y^2+z^2>=x*y+y*z+x*z by Th10;then
  x^2+y^2+z^2+(2*x*y+2*y*z+2*z*x)>=x*y+y*z+x*z+(2*x*y+2*y*z+2*z*x)
    by XREAL_1:8;
  hence thesis;
end;

theorem Th12:
  a|^3+b|^3+c|^3>=3*a*b*c
proof
  (a+b)|^3-(3*a^2*b+3*a*b^2)=a|^3+3*a^2*b+3*a*b^2+b|^3-(3*a^2*b+3*a*b^2)
   by Lm6;then
A1:(a+b)|^3-3*a^2*b-3*a*b^2=a|^3+b|^3;
   (b+c)|^3-(3*b^2*c+3*b*c^2)=b|^3+3*b^2*c+3*b*c^2+c|^3-(3*b^2*c+3*b*c^2)
     by Lm6;then
A2:(b+c)|^3-3*b^2*c-3*b*c^2=b|^3+c|^3;
   (a+c)|^3-(3*a^2*c+3*a*c^2)=a|^3+3*a^2*c+3*a*c^2+c|^3-(3*a^2*c+3*a*c^2)
    by Lm6;then
  a|^3+b|^3+(b|^3+c|^3)+((a|^3)+(c|^3))=(a+b)|^3-3*a^2*b-3*a*b^2+(((b+c)|^3)
  -3*b^2*c-3*b*c^2)+(((a+c)|^3)-3*a^2*c-3*a*c^2) by A1,A2;then
  a|^3+b|^3+b|^3+c|^3+(a|^3)+(c|^3)=(a+b)|^3-3*a^2*b-3*a*b^2+((b+c)|^3)
  -(3*b^2*c)-(3*b*c^2)+((a+c)|^3)-(3*a^2*c)-(3*a*c^2);then
  2*a|^3+2*b|^3+c|^3+(c|^3)=(a+b)|^3-3*a^2*b-3*a*b^2+((b+c)|^3)
  -(3*b^2*c)-(3*b*c^2)+((a+c)|^3)-(3*a^2*c)-(3*a*c^2);then
  2*a|^3+2*b|^3+(c|^3+(c|^3))=(a+b)|^3-3*a^2*b-3*a*b^2+((b+c)|^3)
  -(3*b^2*c)-(3*b*c^2)+((a+c)|^3)-(3*a^2*c)-(3*a*c^2);then
  2*a|^3+2*b|^3+2*(c|^3)=(a+b)|^3-3*a^2*b-3*a*b^2+((b+c)|^3)
  -(3*b^2*c)-(3*b*c^2)+((a+c)|^3)-(3*a^2*c)-(3*a*c^2);then
A3:2*(a|^3+b|^3+(c|^3))=(a+b)|^3+((b+c)|^3)+((a+c)|^3)-3*a^2*b-3*a*b^2
   -(3*b^2*c)-(3*b*c^2)-(3*a^2*c)-(3*a*c^2);
   (a+b)^2>=4*a*b by Th9;then
   (a+b)^2*(a+b)>=4*a*b*(a+b) by XREAL_1:66;then
   (a+b)|^2*(a+b)>=4*a*b*(a+b) by Lm1;then
   (a+b)|^(2+1)>=4*a*b*(a+b) by NEWTON:11;then
   (a+b)|^3>=4*(a*a)*b+4*a*(b*b);then
   (a+b)|^3>=4*a^2*b+4*a*(b*b);then
A4:(a+b)|^3>=4*a^2*b+4*a*b^2;
   (b+c)^2>=4*b*c by Th9;then
   (b+c)^2*(b+c)>=4*b*c*(b+c) by XREAL_1:66;then
   (b+c)|^2*(b+c)>=4*b*c*(b+c) by Lm1;then
   (b+c)|^(2+1)>=4*b*c*(b+c) by NEWTON:11;then
   (b+c)|^3>=4*(b*b)*c+4*b*(c*c);then
   (b+c)|^3>=4*b^2*c+4*b*(c*c);then
   (b+c)|^3>=4*b^2*c+4*b*c^2;then
A5:(a+b)|^3+(b+c)|^3>=(4*a^2*b+4*a*b^2)+(4*b^2*c+4*b*c^2) by A4,XREAL_1:9;
   (a+c)^2>=4*a*c by Th9;then
   (a+c)^2*(a+c)>=4*a*c*(a+c) by XREAL_1:66;then
   (a+c)|^2*(a+c)>=4*a*c*(a+c) by Lm1;then
   (a+c)|^(2+1)>=4*a*c*(a+c) by NEWTON:11;then
   (a+c)|^3>=4*(a*a)*c+4*a*(c*c);then
   (a+c)|^3>=4*a^2*c+4*a*(c*c);then
   (a+c)|^3>=4*a^2*c+4*a*c^2;then
   ((a+b)|^3+(b+c)|^3)+(a+c)|^3>=(4*a^2*b+4*a*b^2+4*b^2*c+4*b*c^2)
   +(4*a^2*c+4*a*c^2) by A5,XREAL_1:9;then
A6:(((a+b)|^3)+((b+c)|^3)+((a+c)|^3))+(-3*a^2*b-3*a*b^2-(3*b^2*c)-(3*b*c^2)
   -(3*a^2*c)-(3*a*c^2))>=((4*a^2*b)+4*a*b^2+4*b^2*c+4*b*c^2+4*a^2*c+4*a*c^2)
   +(-3*a^2*b-3*a*b^2-(3*b^2*c)-(3*b*c^2)-(3*a^2*c)-(3*a*c^2)) by XREAL_1:8;
  b^2+c^2>=2*b*c by Th6;then
A7:a*(b^2+c^2)>=a*(2*b*c) by XREAL_1:66;
  a^2+c^2>=2*a*c by Th6;then
  b*(a^2+c^2)>=b*(2*a*c) by XREAL_1:66;then
A8:b*(a^2+c^2)+a*(b^2+c^2)>=2*a*b*c+2*a*b*c by A7,XREAL_1:9;
  a^2+b^2>=2*a*b by Th6;then
  c*(a^2+b^2)>=2*a*b*c by XREAL_1:66;then
  (b*(a^2+c^2)+a*(b^2+c^2))+(c*(a^2+b^2))>=4*a*b*c+2*a*b*c
   by A8,XREAL_1:9;then
  2*(a|^3+b|^3+(c|^3))>=6*a*b*c by A3,A6,XXREAL_0:2;then
  (2*(a|^3+b|^3+(c|^3)))/2>=(6*a*b*c)/2 by XREAL_1:74;
  hence thesis;
end;

theorem Th13:
  (a|^3+b|^3+c|^3)/3>=a*b*c
proof
  a|^3+b|^3+c|^3>=3*a*b*c by Th12;then
  (a|^3+b|^3+c|^3)/3>=(3*a*b*c)/3 by XREAL_1:74;
  hence thesis;
end;

theorem
  (a/b)|^3+(b/c)|^3+(c/a)|^3>=b/a+c/b+a/c
proof
A1:(b/a)=(b/a)*1
     .=(b/a)*(c/c) by XCMPLX_1:60
     .=(b/c)*(c/a)*1 by XCMPLX_1:86;
A2:(b/a)<=((b/c)|^3+(c/a)|^3+1|^3)/3 by A1,Th13;
A3:(c/b)=(c/b)*1
     .=(c/b)*(a/a) by XCMPLX_1:60
     .=(a/b)*(c/a)*1 by XCMPLX_1:86;
   (c/b)<=((a/b)|^3+(c/a)|^3+1|^3)/3 by A3,Th13;then
A4:(b/a)+(c/b)<=(((b/c)|^3)/3+((c/a)|^3)/3+1/3)+(((a/b)|^3)/3+
   ((c/a)|^3)/3+1/3) by Lm2,A2,XREAL_1:9;
A5:(a/c)=(a/c)*1
     .=(a/c)*(b/b) by XCMPLX_1:60
     .=(a/b)*(b/c)*1 by XCMPLX_1:86;
   (a/c)<=((a/b)|^3+(b/c)|^3+1|^3)/3 by A5,Th13;then
A6:(a/c)<=((a/b)|^3)/3+((b/c)|^3)/3+1/3 by Lm2;
A7:1=(a/a)*1*1 by XCMPLX_1:60
     .=(a/a)*(b/b)*1 by XCMPLX_1:60
     .=(a/a)*(b/b)*(c/c) by XCMPLX_1:60
     .=(a/b)*(b/a)*(c/c) by XCMPLX_1:86
     .=(a/b)*((b/a)*(c/c))
     .=(a/b)*((b/c)*(c/a)) by XCMPLX_1:86
     .=(a/b)*(b/c)*(c/a);
  (a/b)*(b/c)*(c/a)<=((a/b)|^3+(b/c)|^3+(c/a)|^3)/3 by Th13;then
A8:1<=((a/b)|^3)/3+((b/c)|^3)/3+((c/a)|^3)/3 by A7;
  (b/a)+(c/b)+(a/c)<=(b/c)|^3/3+(a/b)|^3/3+2*(c/a)|^3/3+2/3+
  (((a/b)|^3)/3+((b/c)|^3)/3+1/3) by A4,A6,XREAL_1:9;then
  (b/a)+(c/b)+(a/c)+1<=2*(b/c)|^3/3+2*(a/b)|^3/3+2*(c/a)|^3/3+1+
  (((a/b)|^3)/3+((b/c)|^3)/3+((c/a)|^3)/3) by A8,XREAL_1:9;then
  (b/a)+(c/b)+(a/c)+1-1<=(b/c)|^3+(a/b)|^3+(c/a)|^3+1-1 by XREAL_1:11;
  hence thesis;
end;

theorem Th15:
  (a+b+c)>=3*(3-root(a*b*c))
proof
  (3 -Root(a))>0 & (3 -Root(b))>0 & (3 -Root(c))>0 by PREPOWER:def 3;then
  (3 -Root(a))|^3+(3 -Root(b))|^3+(3 -Root(c))|^3>=3*(3 -Root(a))
  *(3 -Root(b))*(3 -Root(c)) by Th12;then
  a+(3 -Root(b))|^3+(3 -Root(c))|^3>=3*(3 -Root(a))
  *(3 -Root(b))*(3 -Root(c)) by PREPOWER:28;then
  a+b+(3 -Root(c))|^3>=3*(3 -Root(a))*(3 -Root(b))*(3 -Root(c)) by PREPOWER:28;
  then a+b+c>=3*((3 -Root(a))*(3 -Root(b)))*(3 -Root(c)) by PREPOWER:28;then
  a+b+c>=3*(3 -Root(a*b))*(3 -Root(c)) by PREPOWER:31;then
  a+b+c>=3*((3 -Root(a*b))*(3 -Root(c)));then
  a+b+c>=3*(3 -Root(a*b*c)) by PREPOWER:31;
  hence thesis by POWER:def 1;
end;

theorem
  (a+b+c)/3>=3-root(a*b*c)
proof
  a+b+c>=3*(3-root(a*b*c)) by Th15;then
  (a+b+c)/3>=(3*(3-root(a*b*c)))/3 by XREAL_1:74;
  hence thesis;
end;

theorem
  x+y+z=1 implies x*y+y*z+x*z<=1/3
proof
   assume x+y+z =1;then
A1:1=(x+y)^2+2*(x+y)*z+z^2 by SQUARE_1:59,63
    .=x^2+2*x*y+y^2+2*(x+y)*z+z^2
    .=x^2+y^2+z^2+2*(x*z+y*z+x*y);
A2:x^2+y^2>=2*x*y & y^2+z^2>=2*y*z & x^2+z^2>=2*x*z by Th6;then
   (x^2+y^2)+(x^2+z^2)>=2*x*y+2*x*z by XREAL_1:9;then
   (x^2+y^2+x^2+z^2)+(y^2+z^2)>=2*x*y+2*x*z+2*y*z by XREAL_1:9,A2;then
   2*(x^2+y^2+z^2)/2>=2*(x*y+x*z+y*z)/2 by REAL_1:73;then
   x^2+y^2+z^2+2*(x*z+y*z+x*y)>=x*y+x*z+y*z+2*(x*z+y*z+x*y)by XREAL_1:9;
   then 1/3>=3*(x*z+y*z+x*y)/3 by REAL_1:73,A1;
   hence thesis;
end;

theorem Th18:
  x+y=1 implies x*y<=1/4
proof
    assume x+y=1;then
A1: 1=x^2+2*x*y+y^2 by SQUARE_1:59,63;
    (x-y)^2 >= 0 by XREAL_1:65;then
A2: x^2-2*x*y+y^2>=0;
    x^2-2*x*y+y^2-(x^2+2*x*y+y^2)>=0-(x^2+2*x*y+y^2)by A2,XREAL_1:11;then
    -4*x*y>=-1 by A1;then
    4*(x*y)<=1 by XREAL_1:26;then
    4*(x*y)/4<=1/4 by REAL_1:73;
    hence thesis;
end;

theorem
  x+y=1 implies x^2+y^2>=1/2
proof
   assume x+y=1;then
A1:1=x^2+2*x*y+y^2 by SQUARE_1:59,63;
   (x-y)^2 >= 0 by XREAL_1:65;then
A2:x^2-2*x*y+y^2 >= 0;
   x^2-2*x*y+y^2+(x^2+2*x*y+y^2) >= 0+(x^2+2*x*y+y^2)by XREAL_1:9,A2;then
   2*(x^2+y^2)/2 >= 1/2 by REAL_1:73,A1;
   hence thesis;
end;

theorem
  a+b=1 implies (1+1/a)*(1+1/b)>=9
proof
   assume
A1:a+b=1;
A2:a*b <=1/4 by Th18,A1;
A3:(1+1/a)*(1+1/b)=1*1+1*(1/b)+(1/a)*(1/b)+(1/a)*1
      .=1+1/b+1/(a*b)+1/a by XCMPLX_1:103
      .=1+(1/b+1/a) +1/(a*b)
      .=1+(1*a+b*1)/(a*b)+1/(a*b) by XCMPLX_1:117
      .=1+(a+b)/(a*b)+1/(a*b)
      .=1+1/(a*b)+1/(a*b) by A1
      .=1+2*(1/(a*b));
   1/(a*b) >= 1/(1/4) by REAL_2:152,A2;then
   (1/(a*b))*(-2) <= 4*(-2) by XREAL_1:67;then
   -(1/(a*b))*2 <= -8;then
   (1/(a*b))*2 >= 8 by XREAL_1:26;then
   1+2*(1/(a*b)) >= 1+8 by XREAL_1:9;
   hence thesis by A3;
end;

theorem
  x+y=1 implies x|^3+y|^3>=1/4
proof
   assume A1:x+y=1;then
A2: x*y <=1/4 by Th18;
A3: x|^3 + y|^3=(x+y)*(x^2-x*y+y^2) by Lm7;
A4: x|^3 + y|^3 = x^2+2*x*y+y^2-3*x*y by A1,A3
  .=1-3*(x*y) by A1,SQUARE_1:63,SQUARE_1:59;
  (x*y)*(-3)>=(1/4)*(-3) by XREAL_1:67,A2;then
  1+(x*y)*(-3)>=(1/4)*(-3)+1 by XREAL_1:9;
  hence thesis by A4;
end;

theorem
  a+b=1 implies a|^3+b|^3<1
proof
A1:a|^3 + b|^3=(a+b)*(a^2-a*b+b^2) by Lm7;
   assume
A2:a+b=1; then
A3:a|^3 + b|^3 = a^2+2*a*b+b^2-3*a*b by A1
      .=1-3*(a*b) by A2,SQUARE_1:59,63;
   1+(a*b)*(-3)<0+1 by XREAL_1:10;
   hence thesis by A3;
end;

theorem
  a+b=1 implies (a+1/a)*(b+1/b)>=25/4
proof
  assume A1:a+b=1;
  a^2+b^2=(a^2+2*a*b+b^2)-2*a*b;then
A2:a^2+b^2=1-2*a*b by A1,SQUARE_1:59,63;
A3:(a+(1/a))*(b+1/b)=((1+a*a)/a)*(b+1/b) by XCMPLX_1:114
    .=((1+a^2)/a)*(b+1/b)
    .=((1+a^2)/a)*((1+b*b)/b) by XCMPLX_1:114
    .=((1+a^2)/a)*((1+b^2)/b)
    .=((1+a^2)*(1+b^2))/(a*b) by XCMPLX_1:77
    .=((1+(b^2+a^2)+a^2*b^2))/(a*b)
    .=((1+(1^2-2*(a*b)+a^2*b^2)))/(a*b) by A2
    .=((1+(1^2-2*1*(a*b)+(a*b)^2)))/(a*b)
    .=((1+((1-(a*b))^2)))/(a*b);
   ((1-(a*b))^2)>=0 by XREAL_1:65;then
A4:1+((1-(a*b))^2)>=1+0 by XREAL_1:8;
   a*b<=(1/2)^2 by A1,Th4;then
A5:a*b<=(1/2)*(1/2);then
   ((1+((1-(a*b))^2)))/(a*b)>=((1+((1-(a*b))^2)))/(1/4)
     by A4,XREAL_1:120;then
A6:((1+((1-(a*b))^2)))/(a*b)>=(4*((1+((1-(a*b))^2))))/1 by XCMPLX_1:78;
   1-(a*b)>=1-(1/4) by A5,XREAL_1:12;then
   (1-(a*b))^2>=(3/4)^2 by SQUARE_1:77;then
   (1-(a*b))^2>=(3/4)*(3/4);then
   1+(1-(a*b))^2>=1+(9/16) by XREAL_1:8;then
   4*((1+((1-(a*b))^2)))>=4*(25/16) by XREAL_1:66;
   hence thesis by A3,A6,XXREAL_0:2;
end;

theorem
  abs(x)<=a implies x^2<=a^2
proof
  assume A1:abs(x)<=a;
  per cases;
  suppose A2:x>=0;
   x<=a by A1,ABSVALUE:def 1;
   hence thesis by A2,SQUARE_1:77;
  end;
  suppose A3:x<0;then
A4:-x>=0 by XREAL_1:60;
   -x<=a by A1,ABSVALUE:def 1,A3;then
   (-x)^2<=a^2 by A4,SQUARE_1:77;
   hence thesis;
  end;
end;

theorem
  abs(x)>=a implies x^2>=a^2
proof
  assume A1:abs(x)>=a;
  per cases;
   suppose x>=0;then
   x>=a by A1,ABSVALUE:def 1;
   hence thesis by SQUARE_1:77;
  end;
  suppose x<0;then
   -x>=a by A1,ABSVALUE:def 1;then
   (-x)^2>=a^2 by SQUARE_1:77;
  hence thesis;
  end;
end;

theorem
  abs(abs(x)-abs(y))<=abs(x)+abs(y)
proof
A1:abs(abs(x)-abs(y))<=abs(x-y) by COMPLEX1:150;
  abs(x-y)<=abs(x)+abs(y) by COMPLEX1:143;
  hence thesis by A1,XXREAL_0:2;
end;

theorem
  a*b*c = 1 implies (1/a)+(1/b)+(1/c)>=sqrt(a)+sqrt(b)+sqrt(c)
proof
  assume A1:a*b*c =1;
   (1/b+1/c)/2>=sqrt((1/b)*(1/c)) by Th2;then
   (1/b+1/c)/2>=sqrt(1/(b*c)) by XCMPLX_1:103;then
A2:(1/b+1/c)/2>=sqrt((1*a)/((b*c)*a)) by XCMPLX_1:92;
   (1/c+1/a)/2>=sqrt((1/c)*(1/a)) by Th2;then
   (1/c+1/a)/2>=sqrt(1/(c*a)) by XCMPLX_1:103;then
   (1/c+1/a)/2>=sqrt((1*b)/((c*a)*b)) by XCMPLX_1:92;then
A3:(1/b+1/c)/2+(1/c+1/a)/2>=sqrt(a)+sqrt(b) by A1,A2,XREAL_1:9;
   (1/a+1/b)/2>=sqrt((1/a)*(1/b)) by Th2;then
   (1/a+1/b)/2>=sqrt(1/(a*b)) by XCMPLX_1:103;then
   (1/a+1/b)/2>=sqrt((1*c)/((a*b)*c)) by XCMPLX_1:92;then
   (1/b+1/c)/2+(1/c+1/a)/2+(1/a+1/b)/2>=sqrt(a)+sqrt(b)+sqrt(c)
    by A1,A3,XREAL_1:9;
   hence thesis;
end;

theorem
  x>0 & y>0 & z<0 & x+y+z=0 implies
  (x|^2+y|^2+z|^2)|^3>=6*(x|^3+y|^3+z|^3)^2
proof
  assume A1:x>0 & y>0 & z<0 & x+y+z=0;then
A2:x+y+(z-z)=0-z;
  (x+y)-(x+y)+z=0-(x+y) by A1;then
A3:z=-(x+y);then
A4:z|^3 = -((x+y)|^3) by Lm5
  .=-(x|^3+3*x^2*y+3*x*y^2+y|^3) by Lm6;
A5:x|^3+y|^3+z|^3
 =-3*x^2*y-3*x*y^2 by A4
 .=-3*(x*x)*y-3*x*y^2
 .=-3*x*x*y-3*x*(y*y)
 .=3*x*y*z by A3;
A6:6*(x|^3+y|^3+z|^3)^2
 =6*((3*x)*(y*z))^2 by A5
 .=6*((3*x)^2*(y*z)^2)
 .=6*((3^2*x^2)*(y*z)^2)
 .=6*((3^2*x^2)*(y^2*z^2))
 .=6*3^2*x^2*y^2*z^2
 .=6*(3*3)*x^2*y^2*z^2
 .=54*x^2*y^2*z^2;
 x+y>0 by A1,XREAL_1:36;then
A7:(x+y)/2>0/2 by XREAL_1:76;then
A8:x*((x+y)/2)>0*x by A1,XREAL_1:70;
A9:y*((x+y)/2)>0*y by A1,A7,XREAL_1:70;
A10:x*y>0 by A1,XREAL_1:131;
    (x*(x+y)/2)+(y*(x+y)/2)>0 by A8,A9,XREAL_1:36;then
A11:(x*(x+y)/2)+(y*(x+y)/2)+x*y>0 by A10,XREAL_1:36;
 (x^2+y^2)/2>=x*y by Th7;then
 (x|^2+y^2)/2>=x*y by Lm1;then
 (x|^2+y|^2)/2>=x*y by Lm1;then
 (x|^2+x*y)/2+(x*y+y|^2)/2+(x|^2+y|^2)/2>=(x|^2+x*y)/2+(x*y+y|^2)/2+x*y
  by XREAL_1:8;then
 (x|^2+x*y)/2+(x*y+y|^2)/2+(x|^2+y|^2)/2>=(x^2+x*y)/2+(x*y+y|^2)/2+x*y by Lm1;
  then
 (x|^2+x*y)/2+(x*y+y|^2)/2+(x|^2+y|^2)/2>=(x*x+x*y)/2+(x*y+y|^2)/2+x*y;then
 (x|^2+x*y)/2+(x*y+y|^2)/2+(x|^2+y|^2)/2>=(x*x+x*y)/2+(x*y+y^2)/2+x*y by Lm1;
  then
 (x|^2+x*y)/2+(x*y+y|^2)/2+(x|^2+y|^2)/2>=(x*x+x*y)/2+(x*y+y*y)/2+x*y;then
 ((x|^2+x*y)/2+(x*y+y|^2)/2+(x|^2+y|^2)/2)|^3>=(x*(x+y)/2+y*(x+y)/2+x*y)|^3
  by A11,PREPOWER:17;then
A12:8*((x|^2+x*y)/2+(x*y+y|^2)/2+(x|^2+y|^2)/2)|^3>=8*(x*(x+y)/2
   +y*(x+y)/2+x*y)|^3 by XREAL_1:66;
 x*(x+y)/2+y*(x+y)/2+x*y>=3*(3-root((x*(x+y)/2)*(y*(x+y)/2)*(x*y)))
 by A8,A9,A10,Th15;then
 x*(x+y)/2+y*(x+y)/2+x*y>=3*(3-root((((x*x)*(x+y)*(y*y)*(x+y)))/4));then
 x*(x+y)/2+y*(x+y)/2+x*y>=3*(3-root((((x^2)*(x+y)*(y*y)*(x+y)))/4));then
 x*(x+y)/2+y*(x+y)/2+x*y>=3*(3-root((((x^2)*(x+y)*(y^2)*(x+y)))/4));then
 x*(x+y)/2+y*(x+y)/2+x*y>=3*(3-root(((x^2*y^2*((x+y)*(x+y))))/4));then
 x*(x+y)/2+y*(x+y)/2+x*y>=3*(3-root(((x^2*y^2*(-z)^2))/4))
 by A2;then
A13:x*(x+y)/2+y*(x+y)/2+x*y>=3*(3-root(((x^2*y^2*z^2))/4));
A14:x^2>0 & y^2>0 & z^2>0 by A1,SQUARE_1:74;then
 x^2*y^2>0 by XREAL_1:131;then
 x^2*y^2*z^2>0 by A14,XREAL_1:131;then
A15:(x^2*y^2*z^2)/4>0 by XREAL_1:141;
A16:(3*(3-root(((x^2*y^2*z^2))/4)))|^3
 =3|^3*(3-root(((x^2*y^2*z^2))/4))|^3 by NEWTON:12
.=27*(3 -Root(((x^2*y^2*z^2))/4))|^3 by A15,POWER:def 1,Lm4
.=27*(((x^2*y^2*z^2))/4) by A15,PREPOWER:28;
  3 -Root(((x^2*y^2*z^2))/4)>0 by A15,PREPOWER:def 3;then
  3-root(((x^2*y^2*z^2))/4)>0 by A15,POWER:def 1;then
  3*(3-root(((x^2*y^2*z^2))/4))>0 by XREAL_1:131;then
  (x*(x+y)/2+y*(x+y)/2+x*y)|^3>=27*(((x^2*y^2*z^2))/4)
    by A16,A13,PREPOWER:17;then
  8*(x*(x+y)/2+y*(x+y)/2+x*y)|^3>=8*(27*(((x^2*y^2*z^2))/4))
    by XREAL_1:66;then
  8*(x|^2+x*y+y|^2)|^3>=54*(x^2*y^2*z^2) by A12,XXREAL_0:2;then
  (2*(x|^2+x*y+y|^2))|^3>=54*(x^2*y^2*z^2) by Lm3,NEWTON:12;then
  ((x|^2+2*x*y+y|^2)+x|^2+y|^2)|^3>=54*(x^2*y^2*z^2);then
  ((x^2+2*x*y+y|^2)+x|^2+y|^2)|^3>=54*(x^2*y^2*z^2) by Lm1;then
  ((x^2+2*x*y+y|^2)+x^2+y|^2)|^3>=54*(x^2*y^2*z^2) by Lm1;then
  ((x^2+2*x*y+y^2)+x^2+y|^2)|^3>=54*(x^2*y^2*z^2) by Lm1;then
  ((x^2+2*x*y+y^2)+x^2+y^2)|^3>=54*(x^2*y^2*z^2) by Lm1;then
  (((-z)^2)+x^2+y^2)|^3>=54*(x^2*y^2*z^2) by A2,SQUARE_1:63;then
  (z^2+x^2+y^2)|^3>=6*(x|^3+y|^3+z|^3)^2 by A6;then
  (z|^2+x^2+y^2)|^3>=6*(x|^3+y|^3+z|^3)^2 by Lm1;then
  (z|^2+x|^2+y^2)|^3>=6*(x|^3+y|^3+z|^3)^2 by Lm1;then
  (z|^2+x|^2+y|^2)|^3>=6*(x|^3+y|^3+z|^3)^2 by Lm1;
  hence thesis;
end;

theorem
  a>=1 implies (a to_power b)+(a to_power c)>=2*(a to_power sqrt(b*c))
proof
  assume
A1:a>=1;
   b+c>=2*sqrt(b*c) by SIN_COS2:1;then
A2:(b+c)/2>=(2*sqrt(b*c))/2 by XREAL_1:74;
  set o=a to_power b;
  set p=a to_power c;
A3:(a to_power (b+c))>0 by POWER:39;
  o>0 & p>0 by POWER:39;then
  (a to_power b)+(a to_power c)>=2*sqrt((a to_power b)*(a to_power c))
    by SIN_COS2:1;then
  (a to_power b)+(a to_power c)>=2*(sqrt(a to_power (b+c))) by POWER:32;then
  (a to_power b)+(a to_power c)>=2*((a to_power (b+c)) to_power (1/2))
   by A3,ASYMPT_1:89;then
A4:(a to_power b)+(a to_power c)>=2*((a to_power ((1/2)*(b+c))))
   by POWER:38;
  a #R ((b+c)/2) >= a #R (sqrt(b*c)) by A1,A2,PREPOWER:96;then
  a to_power ((b+c)/2) >= a #R (sqrt(b*c)) by POWER:def 2;then
  a to_power ((b+c)/2) >= a to_power (sqrt(b*c)) by POWER:def 2;then
  2*(a to_power ((b+c)/2)) >= 2*(a to_power (sqrt(b*c))) by XREAL_1:66;
  hence thesis by A4,XXREAL_0:2;
end;

theorem
  a>=b & b>=c implies
  (a to_power a)*(b to_power b)*(c to_power c)>=(a*b*c) to_power ((a+b+c)/3)
proof
  assume A1:a>=b & b>=c;then
A2:a>=c by XXREAL_0:2;then
A3:a/b>=1 & b/c>=1 & a/c>=1 by A1,XREAL_1:183;
  set u=a to_power ((a-b)/3);
  set v=b to_power ((a-b)/3);
  set w=b to_power ((b-c)/3);
  set h=c to_power ((b-c)/3);
  set i=a to_power ((a-c)/3);
  set j=c to_power ((a-c)/3);
  set o=a to_power a;
  set p=a to_power ((a+b+c)/3);
  set q=c to_power c;
  set r=c to_power ((a+b+c)/3);
  set s=b to_power b;
  set t=b to_power ((a+b+c)/3);
  a-b>=b-b & b-c>=c-c & a-c>=c-c by XREAL_1:11,A1,A2;then
A4:(a-b)/3>=0 & (b-c)/3>=0 & (a-c)/3>=0 by XREAL_1:138;
A5:((a/b) to_power ((a-b)/3))=((a/b) #R ((a-b)/3)) & ((b/c) to_power ((b-c)/3))
   =((b/c) #R ((b-c)/3)) & ((a/c) to_power ((a-c)/3))=((a/c) #R ((a-c)/3))
    by POWER:def 2;
A6:((a to_power ((a-b)/3))/(b to_power ((a-b)/3)))*((b to_power ((b-c)/3))/
   (c to_power ((b-c)/3)))*((a to_power ((a-c)/3))/(c to_power ((a-c)/3)))
  =(u*w)/(v*h)*(i/j) by XCMPLX_1:77
 .=(u*w*i)/(v*h*j) by XCMPLX_1:77
 .=((a to_power ((a-b)/3))*(a to_power ((a-c)/3))*(b to_power ((b-c)/3)))/
   ((b to_power ((a-b)/3))*(c to_power ((a-c)/3))*(c to_power ((b-c)/3)))
 .=((a to_power (((a-b)/3)+((a-c)/3)))*(b to_power ((b-c)/3)))/
   ((b to_power ((a-b)/3))*(c to_power ((a-c)/3))*(c to_power ((b-c)/3)))
   by POWER:32
 .=((a to_power ((2*a-b-c)/3))*(b to_power ((b-c)/3)))/
   ((b to_power ((a-b)/3))*((c to_power ((a-c)/3))*(c to_power ((b-c)/3))))
 .=((a to_power ((2*a-b-c)/3))*(b to_power ((b-c)/3)))/
   ((b to_power ((a-b)/3))*((c to_power (((a-c)/3)+((b-c)/3)))))
   by POWER:32
 .=((a to_power ((2*a-b-c)/3))/(c to_power ((a+b-2*c)/3)))*
   ((b to_power ((b-c)/3))/(b to_power ((a-b)/3))) by XCMPLX_1:77
 .=((a to_power ((2*a-b-c)/3))/(c to_power ((a+b-2*c)/3)))*
   (b to_power ((b-c)/3-(a-b)/3)) by POWER:34
 .=(1/(c to_power ((a+b-2*c)/3)))*
   (b to_power ((2*b-a-c)/3))*(a to_power ((2*a-b-c)/3)) by XCMPLX_1:102
 .=(c to_power (-((a+b-2*c)/3)))*
   (b to_power ((2*b-a-c)/3))*(a to_power ((2*a-b-c)/3)) by POWER:33
 .=(c to_power (((2*c-a-b)/3)))*
   (b to_power ((2*b-a-c)/3))*(a to_power ((2*a-b-c)/3))
 .=(a to_power ((2*a-b-c)/3))*(c to_power (((2*c-a-b)/3)))*
   (b to_power ((2*b-a-c)/3))
 .=(a to_power ((3*a)/3-(a+b+c)/3))*(c to_power ((3*c)/3-(a+b+c)/3))*
   (b to_power ((3*b)/3-(a+b+c)/3))
 .=((a to_power ((3*a)/3))/(a to_power ((a+b+c)/3)))
   *(c to_power ((3*c)/3-(a+b+c)/3))*
   (b to_power ((3*b)/3-(a+b+c)/3)) by POWER:34
 .=((a to_power ((3*a)/3))/(a to_power ((a+b+c)/3)))
  *((c to_power ((3*c)/3))/(c to_power ((a+b+c)/3)))*
   (b to_power ((3*b)/3-(a+b+c)/3)) by POWER:34
 .=((a to_power a)/(a to_power ((a+b+c)/3)))*
   ((c to_power c)/(c to_power ((a+b+c)/3)))*
   ((b to_power b)/(b to_power ((a+b+c)/3))) by POWER:34
 .=(o*q)/(p*r)*(s/t) by XCMPLX_1:77
 .=((a to_power a)*(c to_power c)*(b to_power b))/((a to_power ((a+b+c)/3))
   *(c to_power ((a+b+c)/3))*(b to_power ((a+b+c)/3))) by XCMPLX_1:77;
A7:((a/b) to_power ((a-b)/3))>=1 & ((b/c) to_power ((b-c)/3))>=1 &
   ((a/c) to_power ((a-c)/3))>=1 by A5,A3,A4,PREPOWER:99;then
   ((a/b) to_power ((a-b)/3))*((b/c) to_power ((b-c)/3))>=1*1
    by XREAL_1:68;then
   ((a/b) to_power ((a-b)/3))*((b/c) to_power ((b-c)/3))*
   ((a/c) to_power ((a-c)/3))>=1 by A7,XREAL_1:68;then
   ((a to_power ((a-b)/3))/(b to_power ((a-b)/3)))*((b/c) to_power ((b-c)/3))*
   ((a/c) to_power ((a-c)/3))>=1 by POWER:36;then
   ((a to_power ((a-b)/3))/(b to_power ((a-b)/3)))*((b to_power ((b-c)/3))/
   (c to_power ((b-c)/3)))*((a/c) to_power ((a-c)/3))>=1 by POWER:36;then
A8:(o*q*s)/(p*r*t)>=1 by A6,POWER:36;
A9:p>0 & r>0 & t>0 by POWER:39;then
   p*r>0 by XREAL_1:131;then
A10:p*r*t>0 by A9,XREAL_1:131;then
   (o*q*s)/(p*r*t)*(p*r*t)>=1*(p*r*t) by A8,XREAL_1:66;then
   ((a to_power a)*(c to_power c)*(b to_power b))>=((a to_power ((a+b+c)/3))
   *(c to_power ((a+b+c)/3))*(b to_power ((a+b+c)/3)))
     by A10,XCMPLX_1:88;then
   (a to_power a)*(c to_power c)*(b to_power b)>=
   ((a*c) to_power ((a+b+c)/3))*(b to_power ((a+b+c)/3))
    by POWER:35;then
   (a to_power a)*((b to_power b)*(c to_power c))>=(a*c*b)
   to_power ((a+b+c)/3) by POWER:35;
   hence thesis;
end;

theorem Th31:
  (a+b)|^(n+2)>=a|^(n+2)+(n+2)*(a|^(n+1))*b
proof
   defpred X[Nat] means (a+b)|^($1+2)>=a|^($1+2)+($1+2)*(a|^($1+1))*b;
A1:(a+b)|^(0+2) = (a+b)^2 by Lm1
    .=a^2+2*a*b+b^2;
A2:a|^(0+2)+(0+2)*a|^(0+1)*b = a^2+2*a|^1*b by Lm1
    .=a^2+2*a*b by NEWTON:10;
A3:X[0] by A1,A2,XREAL_1:33;
A4:for n st X[n] holds X[n+1]
   proof let n;
     assume
A5:  (a+b)|^(n+2)>=a|^(n+2)+(n+2)*(a|^(n+1))*b;
     (a+b)|^(n+2)*(a+b)>=(a|^(n+2)+(n+2)*(a|^(n+1))*b)*(a+b)
       by A5,XREAL_1:66;then
     (a+b)|^(n+2+1)>=(a|^(n+2)+(n+2)*(a|^(n+1))*b)*(a+b)
       by NEWTON:11;then
     (a+b)|^(n+3)>=a|^(n+2)*a+b*a|^(n+2)+(n+2)*(a+b)*(a|^(n+1))*b;then
     (a+b)|^(n+3)>=a|^(n+2+1)+b*a|^(n+2)+(n+2)*(a+b)*(a|^(n+1))*b
       by NEWTON:11;then
     (a+b)|^(n+3)>=a|^(n+3)+a|^(n+2)*b+(n+2)*(a+b)*(a|^(n+1))*b;then
     (a+b)|^(n+3)>=a|^(n+3)+a|^(n+2)*b+(n+2)*(a*(a|^(n+1)))*b+
       (n+2)*(a|^(n+1))*(b*b);then
     (a+b)|^(n+3)>=a|^(n+3)+a|^(n+2)*b+(n+2)*((a|^(n+1+1)))*b+
       (n+2)*(a|^(n+1))*(b*b) by NEWTON:11;then
A6:  (a+b)|^(n+3)>=a|^(n+3)+a|^(n+2)*b+(n+2)*(a|^(n+2))*b+
     (n+2)*(a|^(n+1))*(b^2);
A7:  a|^(n+1)>0 by PREPOWER:13;
     (n+2)*(a|^(n+1))>0 by A7,XREAL_1:131;then
     (n+2)*(a|^(n+1))*(b^2)>=0 by XREAL_1:129;then
     a|^(n+3)+(n+3)*(a|^(n+2))*b<=(n+2)*(a|^(n+1))*(b^2)+
       (a|^(n+3)+(n+3)*(a|^(n+2))*b) by XREAL_1:33;
     hence thesis by A6,XXREAL_0:2;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem
  (a|^n+b|^n)/2 >= ((a+b)/2)|^n
proof
  defpred X[Nat] means (a|^$1+b|^$1)/2 >= ((a+b)/2)|^$1;
  (a|^0+b|^0)/2 = (1+b|^0)/2 by NEWTON:9
       .= (1+1)/2 by NEWTON:9
       .= ((a+b)/2)|^0 by NEWTON:9;then
A1:X[0];
A2:for n st X[n] holds X[n+1]
   proof let n; assume X[n];then
A3:((a|^n+b|^n)/2)*(a+b)>= (((a+b)/2)|^n)*(a+b) by XREAL_1:66;
  per cases;
    suppose A4:a-b>=0;then
    a-b+b>=0+b by XREAL_1:8;then
    a|^n >= b|^n by PREPOWER:17;then
    a|^n-b|^n>=0 by XREAL_1:50;then
    (a-b)*(a|^n-b|^n)>=0 by A4,XREAL_1:129;then
    a|^n*a-a|^n*b-(b|^n*a-b|^n*b)>=0;then
    a|^(n+1)-a|^n*b-(b|^n*a-b|^n*b)>=0 by NEWTON:11;then
    a|^(n+1)-a|^n*b-b|^n*a+b|^n*b>=0;then
    a|^(n+1)-a|^n*b-b|^n*a+b|^(n+1)>=0 by NEWTON:11;then
    a|^(n+1)-a|^n*b-b|^n*a+b|^(n+1)+(a|^n*b+b|^n*a)>=0+(a|^n*b+b|^n*a)
      by XREAL_1:8;then
    a|^(n+1)+b|^(n+1)+(a|^(n+1)+b|^(n+1))>=a|^n*b+b|^n*a+(a|^(n+1)+b|^(n+1))
      by XREAL_1:8;then
    2*a|^(n+1)+2*b|^(n+1)>=a|^(n+1)+a|^n*b+b|^n*a+b|^(n+1);then
    2*a|^(n+1)+2*b|^(n+1)>=a|^n*a+a|^n*b+b|^n*a+b|^(n+1) by NEWTON:11;then
    2*a|^(n+1)+2*b|^(n+1)>=a|^n*a+a|^n*b+b|^n*a+b|^n*b by NEWTON:11;then
    (2*(a|^(n+1)+b|^(n+1)))/2>=((a|^n+b|^n)*(a+b))/2 by XREAL_1:74;then
    (a|^(n+1)+b|^(n+1))>=(((a+b)/2)|^n)*(a+b) by A3,XXREAL_0:2;then
    (a|^(n+1)+b|^(n+1))>=((a+b)|^n/2|^n)*(a+b) by PREPOWER:15;then
    (a|^(n+1)+b|^(n+1))>=((a+b)|^n*(a+b))/2|^n by XCMPLX_1:75;then
    (a|^(n+1)+b|^(n+1))>=((a+b)|^(n+1))/2|^n by NEWTON:11;then
    ((a|^(n+1)+b|^(n+1)))/2>=(((a+b)|^(n+1))/2|^n)/2 by XREAL_1:74;then
    ((a|^(n+1)+b|^(n+1)))/2>=((a+b)|^(n+1))/(2|^n*2) by XCMPLX_1:79;then
    ((a|^(n+1)+b|^(n+1)))/2>=((a+b)|^(n+1))/(2|^(n+1)) by NEWTON:11;
    hence thesis by PREPOWER:15;
    end;
    suppose A5:a-b<0;then
    a-b+b<0+b by XREAL_1:8;then
    a|^n <= b|^n by PREPOWER:17;then
    a|^n-b|^n<=0 by XREAL_1:49;then
    (a-b)*(a|^n-b|^n)>=0 by A5,XREAL_1:130;then
    a|^n*a-a|^n*b-(b|^n*a-b|^n*b)>=0;then
    a|^(n+1)-a|^n*b-(b|^n*a-b|^n*b)>=0 by NEWTON:11;then
    a|^(n+1)-a|^n*b-b|^n*a+b|^n*b>=0;then
    a|^(n+1)-a|^n*b-b|^n*a+b|^(n+1)>=0 by NEWTON:11;then
    a|^(n+1)-a|^n*b-b|^n*a+b|^(n+1)+(a|^n*b+b|^n*a)>=0+(a|^n*b+b|^n*a)
      by XREAL_1:8;then
    a|^(n+1)+b|^(n+1)+(a|^(n+1)+b|^(n+1))>=a|^n*b+b|^n*a+(a|^(n+1)+b|^(n+1))
      by XREAL_1:8;then
    2*a|^(n+1)+2*b|^(n+1)>=a|^(n+1)+a|^n*b+b|^n*a+b|^(n+1);then
    2*a|^(n+1)+2*b|^(n+1)>=a|^n*a+a|^n*b+b|^n*a+b|^(n+1) by NEWTON:11;then
    2*a|^(n+1)+2*b|^(n+1)>=a|^n*a+a|^n*b+b|^n*a+b|^n*b by NEWTON:11;then
    (2*(a|^(n+1)+b|^(n+1)))/2>=((a|^n+b|^n)*(a+b))/2 by XREAL_1:74;then
    (a|^(n+1)+b|^(n+1))>=(((a+b)/2)|^n)*(a+b) by A3,XXREAL_0:2;then
    (a|^(n+1)+b|^(n+1))>=((a+b)|^n/2|^n)*(a+b) by PREPOWER:15;then
    (a|^(n+1)+b|^(n+1))>=((a+b)|^n*(a+b))/2|^n by XCMPLX_1:75;then
    (a|^(n+1)+b|^(n+1))>=((a+b)|^(n+1))/2|^n by NEWTON:11;then
    ((a|^(n+1)+b|^(n+1)))/2>=(((a+b)|^(n+1))/2|^n)/2 by XREAL_1:74;then
    ((a|^(n+1)+b|^(n+1)))/2>=((a+b)|^(n+1))/(2|^n*2) by XCMPLX_1:79;then
    ((a|^(n+1)+b|^(n+1)))/2>=((a+b)|^(n+1))/(2|^(n+1)) by NEWTON:11;
    hence thesis by PREPOWER:15;
    end;
  end;
  for n holds X[n] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

theorem Th33:
  (for n holds s.n>0) implies for n holds (Partial_Sums s).n>0
proof
  assume A1:for n holds s.n>0;
  defpred X[Nat] means (Partial_Sums s).$1>0;
A2:(Partial_Sums s).0=s.0 by SERIES_1:def 1;
A3:X[0] by A1,A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Sums s).n>0;
A6:(Partial_Sums s).(n+1)=(Partial_Sums s).n+s.(n+1) by SERIES_1:def 1;
   s.(n+1)>0 by A1;
   hence thesis by A6,A5,XREAL_1:36;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem Th34:
  (for n holds s.n>=0) implies for n holds (Partial_Sums s).n>=0
proof
  assume A1:for n holds s.n>=0;
  defpred X[Nat] means (Partial_Sums(s)).$1>=0;
A2:(Partial_Sums(s)).0=s.0 by SERIES_1:def 1;
A3:X[0] by A1,A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Sums(s)).n>=0;
A6:(Partial_Sums(s)).(n+1)=(Partial_Sums(s)).n+s.(n+1) by SERIES_1:def 1;
   s.(n+1)>=0 by A1;
   hence thesis by A5,A6,XREAL_1:35;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem Th35:
  (for n holds s.n<0) implies (Partial_Sums s).n<0
proof
  assume A1:for n holds s.n<0;
  defpred X[Nat] means (Partial_Sums(s)).$1<0;
A2:(Partial_Sums(s)).0=s.0 by SERIES_1:def 1;
A3:X[0] by A1,A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
   (Partial_Sums(s)).n<0;then
A5:0*(-1)<(-1)*((Partial_Sums(s)).n) by XREAL_1:71;
A6:(Partial_Sums(s)).(n+1)=(Partial_Sums(s)).n+s.(n+1) by SERIES_1:def 1;
   s.(n+1)<0 by A1;then
   0*(-1)<(s.(n+1))*(-1) by XREAL_1:71;then
   -(Partial_Sums(s)).n+(-s.(n+1))>0 by A5,XREAL_1:36;then
   -((Partial_Sums(s)).n+s.(n+1))>0;
   hence thesis by A6,XREAL_1:60;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem Th36:
  s=s1(#)s1 implies for n holds (Partial_Sums s).n>=0
proof
   assume A1:s=s1(#)s1;
   defpred X[Nat] means (Partial_Sums s).$1>=0;
A2:(Partial_Sums s).0=s.0 by SERIES_1:def 1
  .=s1.0*s1.0 by A1,SEQ_1:12
  .=(s1.0)^2;
A3:X[0] by A2,XREAL_1:65;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Sums(s)).n>=0;
A6:(Partial_Sums s).(n+1)=(Partial_Sums(s)).n+s.(n+1)
    by SERIES_1:def 1
  .=(Partial_Sums s).n+s1.(n+1)*s1.(n+1) by A1,SEQ_1:12
  .=(Partial_Sums s).n+(s1.(n+1))^2;
    (s1.(n+1))^2>=0 by XREAL_1:65;
  hence thesis by A6,A5,XREAL_1:35;
  end;
  for n holds X[n] from NAT_1:sch 1(A3,A4);
  hence thesis;
end;

theorem Th37:
  (for n holds s.n>0 & s.n>s.(n-1)) implies
    (n+1)*s.(n+1) > (Partial_Sums s).n
proof
  assume A1:for n holds s.n>0 & s.n>s.(n-1);
  defpred X[Nat] means ($1+1)*s.($1+1)>(Partial_Sums s).$1;
   s.1>s.(1-1) by A1;then
A2:X[0] by SERIES_1:def 1;
A3:for n st X[n] holds X[n+1]
   proof let n;
   assume
A4:(n+1)*s.(n+1)>(Partial_Sums s).n;
   s.(n+2)>s.((n+2)-1) by A1;then
A5:(n+2)*s.(n+2)>(n+2)*s.(n+1) by XREAL_1:70;
   (Partial_Sums(s)).n+s.(n+1)<(n+1)*s.(n+1)+s.(n+1) by A4,XREAL_1:8;then
   (Partial_Sums s).n+s.(n+1)<(n+2)*s.(n+2) by A5,XXREAL_0:2;
   hence thesis by SERIES_1:def 1;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
   hence thesis;
end;

theorem
  (s=s1(#)s2 & for n holds s1.n>=0 & s2.n>=0) implies
  for n holds (Partial_Sums s).n<=
   (Partial_Sums(s1).n)*(Partial_Sums(s2).n)
proof
  assume
A1:s=s1(#)s2 & for n holds s1.n>=0 & s2.n>=0;
  defpred X[Nat] means (Partial_Sums(s)).$1<=
   (Partial_Sums(s1).$1)*(Partial_Sums(s2).$1);
A2:(Partial_Sums(s)).0 = s.0 by SERIES_1:def 1
 .=s1.0*s2.0 by A1,SEQ_1:12;
  (Partial_Sums(s1).0)*(Partial_Sums(s2).0)
  =s1.0*(Partial_Sums(s2).0) by SERIES_1:def 1
 .=s1.0*s2.0 by SERIES_1:def 1;then
A3:X[0] by A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Sums(s)).n<=
   (Partial_Sums(s1).n)*(Partial_Sums(s2).n);
A6:(Partial_Sums(s)).(n+1)
  =(Partial_Sums(s)).n+s.(n+1) by SERIES_1:def 1
 .=(Partial_Sums(s)).n+s1.(n+1)*s2.(n+1) by A1,SEQ_1:12;
    set j=(Partial_Sums(s)).n;
    set u=Partial_Sums(s1).n;
    set v=Partial_Sums(s2).n;
    set w=s1.(n+1);
    set h=s2.(n+1);
A7:(Partial_Sums(s1).(n+1))*(Partial_Sums(s2).(n+1))
  =(Partial_Sums(s1).n+s1.(n+1))*(Partial_Sums(s2).(n+1)) by SERIES_1:def 1
 .=(u+w)*(v+h) by SERIES_1:def 1
 .=u*v+u*h+w*v+w*h;
A8:j+w*h<=u*v+w*h by A5,XREAL_1:8;
A9:u>=0 & v>=0 by A1,Th34;
   w>=0 & h>=0 by A1;then
   u*h>=0 & w*v>=0 by A9,XREAL_1:129;then
   u*h+w*v>=0 by XREAL_1:33;then
   (u*v+w*h)<=(u*v+w*h)+(u*h+w*v) by XREAL_1:33;
   hence thesis by A7,A6,A8,XXREAL_0:2;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem
  (s=s1(#)s2 & for n holds s1.n<0 & s2.n<0)
   implies (Partial_Sums s).n<=(Partial_Sums(s1).n)*(Partial_Sums(s2).n)
proof
  assume
A1:s=s1(#)s2 & for n holds s1.n<0 & s2.n<0;
  defpred X[Nat] means (Partial_Sums s).$1<=
   (Partial_Sums(s1).$1)*(Partial_Sums(s2).$1);
A2:(Partial_Sums s).0 = s.0 by SERIES_1:def 1
 .=s1.0*s2.0 by A1,SEQ_1:12;
   (Partial_Sums(s1).0)*(Partial_Sums(s2).0)
  =s1.0*(Partial_Sums(s2).0) by SERIES_1:def 1
 .=s1.0*s2.0 by SERIES_1:def 1;then
A3:X[0] by A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Sums(s)).n<=
   (Partial_Sums(s1).n)*(Partial_Sums(s2).n);
A6:(Partial_Sums(s)).(n+1)
  =(Partial_Sums(s)).n+s.(n+1) by SERIES_1:def 1
 .=(Partial_Sums(s)).n+s1.(n+1)*s2.(n+1) by A1,SEQ_1:12;
    set j=(Partial_Sums(s)).n;
    set u=Partial_Sums(s1).n;
    set v=Partial_Sums(s2).n;
    set w=s1.(n+1);
    set h=s2.(n+1);
A7:(Partial_Sums(s1).(n+1))*(Partial_Sums(s2).(n+1))
  =(Partial_Sums(s1).n+s1.(n+1))*(Partial_Sums(s2).(n+1)) by SERIES_1:def 1
 .=(u+w)*(v+h) by SERIES_1:def 1
 .=u*v+u*h+w*v+w*h;
A8:j+w*h<=u*v+w*h by A5,XREAL_1:8;
A9:u<0 & v<0 by A1,Th35;
   w<0 & h<0 by A1;then
   u*h>0 & w*v>0 by A9,XREAL_1:132;then
   u*h+w*v>=0 by XREAL_1:33;then
   (u*v+w*h)<=(u*v+w*h)+(u*h+w*v) by XREAL_1:33;
   hence thesis by A7,A6,A8,XXREAL_0:2;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem Th40:
  for n holds abs((Partial_Sums s).n)<=(Partial_Sums (abs s)).n
proof
  set s1=abs s;
  defpred X[Nat] means abs((Partial_Sums s).$1)<=(Partial_Sums s1).$1;
  let n;
A1:s1.0=abs(s.0) by SEQ_1:16;
A2:(Partial_Sums s1).0=abs(s.0) by A1,SERIES_1:def 1;
A3:X[0] by A2,SERIES_1:def 1;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:abs((Partial_Sums(s)).n)<=(Partial_Sums(s1)).n;
A6:abs((Partial_Sums(s)).(n+1))=abs((Partial_Sums(s)).n + s.(n+1))
      by SERIES_1:def 1;
A7:abs((Partial_Sums(s)).n + s.(n+1))<=abs((Partial_Sums(s)).n) + abs(s.(n+1))
    by COMPLEX1:142;
   (Partial_Sums(s1)).(n+1)=(Partial_Sums(s1)).n + s1.(n+1)
     by SERIES_1:def 1;then
A8:(Partial_Sums(s1)).(n+1)=(Partial_Sums(s1)).n + abs(s.(n+1)) by SEQ_1:16;
   abs((Partial_Sums(s)).n)+abs(s.(n+1))<=(Partial_Sums(s1)).n + abs(s.(n+1))
     by A5,XREAL_1:8;
   hence thesis by A8,A6,A7,XXREAL_0:2;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem
  (Partial_Sums s).n<=(Partial_Sums abs s).n
proof
   set s1=abs s;
A1:(Partial_Sums s).n<=abs((Partial_Sums s).n) by ABSVALUE:11;
   abs((Partial_Sums s).n)<=(Partial_Sums s1).n by Th40;
   hence thesis by A1,XXREAL_0:2;
end;

definition let s;
  func Partial_Product(s) -> Real_Sequence means :Def1:
  it.0=s.0 & for n holds it.(n+1) = it.n * s.(n+1);
existence
proof deffunc U(Nat,Real) = $2 * s.($1+1);
 consider f being Function of NAT,REAL such that
 A1: f.0 = s.0 & for n being Nat holds f.(n+1) = U(n,f.n) from RECDEF_1:sch 4;
 reconsider f as Real_Sequence;
 take f;
 thus thesis by A1;
end;
uniqueness
proof
 let s1,s2;
 assume that A2: s1.0=s.0 & for n holds s1.(n+1)=s1.n * s.(n+1) and
             A3: s2.0=s.0 & for n holds s2.(n+1)=s2.n * s.(n+1);
  defpred X[Nat] means s1.$1=s2.$1;
 A4: X[0] by A2,A3;
 A5: for n st X[n] holds X[n+1]
     proof let n;
      assume s1.n =s2.n;
      hence s1.(n+1)=s2.n * s.(n+1) by A2
                    .=s2.(n+1) by A3;
     end;
    for n holds X[n] from NAT_1:sch 1(A4,A5);
 hence s1=s2 by FUNCT_2:113;
end;
end;

theorem Th42:
  (for n holds s.n>0) implies (Partial_Product s).n>0
proof
  assume A1:for n holds s.n>0;
  defpred X[Nat] means (Partial_Product s).$1>0;
A2:s.0>0 by A1;
A3:X[0] by A2,Def1;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Product s).n>0;
A6:(Partial_Product s).(n+1) = (Partial_Product s).n*s.(n+1) by Def1;
   s.(n+1)>0 by A1;
   hence thesis by A6,A5,XREAL_1:131;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem Th43:
  (for n holds s.n>=0) implies (Partial_Product s).n>=0
proof
  assume
A1:for n holds s.n>=0;
   defpred X[Nat] means (Partial_Product s).$1>=0;
A2:s.0>=0 by A1;
A3:X[0] by A2,Def1;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Product s).n>=0;
A6:(Partial_Product s).(n+1) = (Partial_Product s).n*s.(n+1) by Def1;
   s.(n+1)>=0 by A1;
   hence thesis by A6,A5,XREAL_1:129;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem
  (for n holds s.n>0 & s.n<1) implies
  for n holds (Partial_Product s).n>0 & (Partial_Product s).n<1
proof
  assume A1:for n holds s.n>0 & s.n<1;
  defpred X[Nat] means (Partial_Product s).$1>0 & (Partial_Product s).$1<1;
A2:(Partial_Product s).0=s.0 by Def1;
A3:X[0] by A1,A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Product s).n>0 & (Partial_Product s).n<1;
A6:(Partial_Product s).(n+1)=((Partial_Product s).n)*(s.(n+1)) by Def1;
   s.(n+1)>0 & s.(n+1)<1 by A1;
   hence thesis by A6,A5,XREAL_1:131,164;
   end;
   for n holds X[n] from NAT_1:sch 1(A3,A4);
   hence thesis;
end;

theorem
  (for n holds s.n>=1) implies for n holds (Partial_Product s).n>=1
proof
  assume A1:for n holds s.n>=1;
  defpred X[Nat] means (Partial_Product s).$1>=1;
A2:(Partial_Product s).0=s.0 by Def1;
A3:X[0] by A1,A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Product s).n>=1;
A6:(Partial_Product s).(n+1)=((Partial_Product s).n)*(s.(n+1)) by Def1;
A7:s.(n+1)>=1 by A1;
   ((Partial_Product s).n)<=((Partial_Product s).n)*(s.(n+1))
    by A5,A7,XREAL_1:153;
  hence thesis by A6,A5,XXREAL_0:2;
  end;
  for n holds X[n] from NAT_1:sch 1(A3,A4);
  hence thesis;
end;

theorem
  (for n holds s1.n>=0 & s2.n>=0) implies
  (for n holds (Partial_Product s1).n+(Partial_Product s2).n<=
  (Partial_Product(s1+s2)).n)
proof
   assume
A1:for n holds s1.n>=0 & s2.n>=0;
   set s=s1+s2;
   defpred X[Nat] means (Partial_Product s1).$1+
   (Partial_Product s2).$1<=(Partial_Product s).$1;
A2:(Partial_Product s1).0+(Partial_Product s2).0
  =s1.0+(Partial_Product s2).0 by Def1
 .=s1.0+s2.0 by Def1;
   (Partial_Product s).0 = s.0 by Def1
 .=s1.0+s2.0 by SEQ_1:11;then
A3:X[0] by A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Product s1).n+(Partial_Product s2).n<=
  (Partial_Product s).n;
   set u=(Partial_Product s1).n;
   set v=(Partial_Product s2).n;
   set w=(Partial_Product s).n;
   set h=s1.(n+1);
   set j=s2.(n+1);
A6:u>=0 & v>=0 by A1,Th43;
A7:h>=0 & j>=0 by A1;then
A8:h+j>=0 by XREAL_1:35;
    u*j>=0 & v*h>=0 by A6,A7,XREAL_1:129;then
A9:u*j+v*h>=0 by XREAL_1:35;
A10:(u+v)*(h+j)<=w*(h+j) by A5,A8,XREAL_1:66;
A11:(Partial_Product s1).(n+1)+(Partial_Product s2).(n+1)
  =(Partial_Product s1).n*s1.(n+1)+(Partial_Product s2).(n+1) by Def1
 .=u*h+v*j by Def1;
A12:(Partial_Product s).(n+1)
  =(Partial_Product s).n*s.(n+1) by Def1
 .=w*(h+j) by SEQ_1:11;
   u*h+v*j<=(u*h+v*j)+(u*j+v*h) by A9,XREAL_1:33;then
   u*h+v*j<=w*(h+j) by A10,XXREAL_0:2;
  hence thesis by A11,A12;
  end;
  for n holds X[n] from NAT_1:sch 1(A3,A4);
  hence thesis;
end;

theorem
  (for n holds s.n=(2*n+1)/(2*n+2)) implies
  (Partial_Product s).n<=1/sqrt(3*n+4)
proof
  assume A1:for n holds s.n=(2*n+1)/(2*n+2);
  defpred X[Nat] means (Partial_Product s).$1<=1/sqrt(3*$1+4);
   (Partial_Product s).0 =s.0 by Def1
 .=(2*0+1)/(2*0+2) by A1
 .=1/sqrt(3*0+4) by SQUARE_1:85;then
A2:X[0];
A3:for n st X[n] holds X[n+1]
   proof let n;
   assume
A4:(Partial_Product s).n<=1/sqrt(3*n+4);
   (Partial_Product s).n*((2*n+3)/(2*n+4))<=1/sqrt(3*n+4)*((2*n+3)/(2*n+4))
     by A4,XREAL_1:66;then
A5:(Partial_Product s).n*((2*n+3)/(2*n+4))<=(1*(2*n+3))/(sqrt(3*n+4)*(2*n+4))
     by XCMPLX_1:77;
A6:((2*n+3)^2)*((3*n)+7)>=0 by XREAL_1:129;
A7:sqrt(3*n+4)>0 & sqrt(3*n+7)>0 by SQUARE_1:93;
A8:(2*n+4)*sqrt(3*n+4)>0 by A7,XREAL_1:131;
   111*n<=112*n by XREAL_1:66;then
   111*n+63<=112*n+64 by XREAL_1:9;then
   (12*n|^3+64*n^2)+(111*n+63)<=(12*n|^3+64*n^2)+(112*n+64) by XREAL_1:8;then
   12*n|^3+36*n^2+27*n+28*n^2+84*n+63<=12*n|^3+48*n^2+48*n+16*n^2
     +64*n+64;then
   12*n|^(2+1)+36*(n*n)+27*n+28*n^2+84*n+63<=12*n|^(2+1)+48*n^2+48*n+16*n^2
     +64*n+64;then
   12*n|^(2+1)+36*(n*n)+27*n+28*n^2+84*n+63<=12*n|^(2+1)+48*(n*n)+48*n+16*n^2
     +64*n+64;then
   12*((n|^2)*(n|^1))+36*(n*n)+27*n+28*n^2+84*n+63<=12*n|^(2+1)+48*(n*n)+
     48*n+16*n^2+64*n+64 by NEWTON:13;then
   4*(n|^2)*3*(n|^1)+36*n*n+27*n+28*n^2+84*n+63<=12*n|^(2+1)+48*n*n+
     48*n+16*n^2+64*n+64;then
   4*(n|^2)*3*n+36*n*n+27*n+28*n^2+84*n+63<=12*n|^(2+1)+48*n*n+
     48*n+16*n^2+64*n+64 by NEWTON:10;then
   4*(n^2)*3*n+36*n*n+27*n+28*n^2+84*n+63<=12*n|^(2+1)+48*n*n+
     48*n+16*n^2+64*n+64 by Lm1;then
   4*(n^2)*3*n+36*n*n+27*n+28*n^2+84*n+63<=12*(n|^2*n|^1)+48*n*n+
     48*n+16*n^2+64*n+64 by NEWTON:13;then
   4*(n^2)*3*n+36*n*n+27*n+28*n^2+84*n+63<=12*(n^2*n|^1)+48*n*n+
     48*n+16*n^2+64*n+64 by Lm1;then
   4*(n^2)*3*n+36*n*n+27*n+28*n^2+84*n+63<=12*(n^2*n)+48*n*n+
     48*n+16*n^2+64*n+64 by NEWTON:10;then
   ((4*n^2)+(12*n)+9)*((3*n)+7)<=((4*n^2)+
     (16*n)+16)*((3*n)+4);then
   ((4*(n*n))+(12*n)+9)*((3*n)+7)<=((4*n^2)+
     (16*n)+16)*((3*n)+4);then
   (((2*n)*(2*n))+(2*(2*n)*3)+3*3)*((3*n)+7)<=(((2*n)*(2*n))+
     (2*(2*n)*4)+4*4)*((3*n)+4);then
   (((2*n)^2)+(2*(2*n)*3)+3*3)*((3*n)+7)<=(((2*n)*(2*n))+
     (2*(2*n)*4)+4*4)*((3*n)+4);then
   (((2*n)^2)+(2*(2*n)*3)+3^2)*((3*n)+7)<=(((2*n)*(2*n))+
     (2*(2*n)*4)+4*4)*((3*n)+4);then
   (((2*n)^2)+(2*(2*n)*3)+3^2)*((3*n)+7)<=(((2*n)^2)+
     (2*(2*n)*4)+4*4)*((3*n)+4);then
   (((2*n)^2)+(2*(2*n)*3)+3^2)*((3*n)+7)<=(((2*n)^2)+
     (2*(2*n)*4)+4^2)*((3*n)+4);then
   ((2*n+3)^2)*((3*n)+7)<=(((2*n)^2)+
     (2*(2*n)*4)+4^2)*((3*n)+4);then
   ((2*n+3)^2)*((3*n)+7)<=((2*n)+4)^2*((3*n)+4);then
   sqrt(((2*n+3)^2)*((3*n)+7))<=sqrt(((2*n)+4)^2
     *((3*n)+4)) by A6,SQUARE_1:94;then
   sqrt((2*n+3)^2)*sqrt((3*n)+7)<=sqrt(((2*n)+4)^2
     *((3*n)+4)) by SQUARE_1:97;then
   sqrt((2*n+3)^2)*sqrt((3*n)+7)<=sqrt((2*n+4)^2)*sqrt((3*n+4))
     by SQUARE_1:97;then
   (2*n+3)*sqrt((3*n)+7)<=sqrt((2*n+4)^2)*sqrt((3*n+4)) by SQUARE_1:89;then
   (2*n+3)*sqrt((3*n)+7)<=((2*n+4)*sqrt((3*n+4)))*1 by SQUARE_1:89;then
   1*(2*n+3)/((2*n+4)*sqrt((3*n+4)))<=1/sqrt((3*n)+7)
     by A7,A8,XREAL_1:104;then
   (Partial_Product s).n*((2*(n+1)+1)/(2*(n+1)+2))<=1/sqrt(3*(n+1)+4)
     by A5,XXREAL_0:2;then
   (Partial_Product s).n*s.(n+1)<=1/sqrt(3*(n+1)+4) by A1;
   hence thesis by Def1;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
   hence thesis;
end;

Lm9:
  (for n holds s.n>-1 & s.n<0) implies
  for n holds (Partial_Sums(s).n)*s.(n+1)>=0
proof
  assume A1:for n holds s.n>-1 & s.n<0;
  let n;
    defpred X[Nat] means (Partial_Sums(s).$1)*(s.($1+1))>=0;
A2:  (Partial_Sums(s).0)*(s.1) = (s.0)*(s.1) by SERIES_1:def 1;
     s.0<0 & s.1<0 by A1;then
A3:X[0] by A2,XREAL_1:132;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Sums(s).n)*(s.(n+1))>=0;
A6:s.(n+1)<0 by A1;then
   1/(s.(n+1))<0/(s.(n+1)) by XREAL_1:77;then
   (Partial_Sums(s).n)*(s.(n+1))*(1/(s.(n+1)))<=0*(1/(s.(n+1)))
     by A5,XREAL_1:67;then
   (Partial_Sums(s).n)*((s.(n+1))*(1/(s.(n+1))))<=0;then
A7:(Partial_Sums(s).n)*1<=0 by A6,XCMPLX_1:107;
A8:(Partial_Sums(s).(n+1))*(s.(n+2))
  =(Partial_Sums(s).n + s.(n+1))*(s.(n+2)) by SERIES_1:def 1
 .=(Partial_Sums(s).n)*(s.(n+2))+(s.(n+1))*(s.(n+2));
A9:s.(n+2)<=0 & s.(n+1)<=0 by A1;then
A10:(Partial_Sums(s).n)*(s.(n+2))>=0 by A7,XREAL_1:130;
  (s.(n+1))*(s.(n+2))>=0 by A9,XREAL_1:130;then
  (Partial_Sums(s).n)*(s.(n+2))+((s.(n+1))*(s.(n+2)))>=0+0
   by A10,XREAL_1:9;
  hence thesis by A8;
  end;
  for n holds X[n] from NAT_1:sch 1(A3,A4);
  hence thesis;
  end;

theorem :: Bernoulli Inequality(1)
  (for n holds s1.n=1+s.n & s.n>-1 & s.n<0)
  implies for n holds 1+Partial_Sums(s).n<=(Partial_Product s1).n
proof
   assume
A1:for n holds s1.n=1+s.n & s.n>-1 & s.n<0;
   let n;
   defpred X[Nat] means 1+Partial_Sums(s).$1<=(Partial_Product s1).$1;
A2:Partial_Sums(s).0=s.0 by SERIES_1:def 1;
A3:(Partial_Product s1).0=s1.0 by Def1;
A4:X[0] by A2,A1,A3;
A5:for n st X[n] holds X[n+1]
   proof let n;
   assume
A6:1+Partial_Sums(s).n<=(Partial_Product s1).n;
   s.(n+1)>-1 by A1;then
   s.(n+1)+1>-1+1 by XREAL_1:10;then
A7:(1+Partial_Sums(s).n)*(1+s.(n+1))<=((Partial_Product s1).n)*(1+s.(n+1))
    by A6,XREAL_1:66;
   ((Partial_Product(s1)).n)*(1+s.(n+1))=((Partial_Product s1).n)*s1.(n+1)
      by A1;then
A8:(1+Partial_Sums(s).n)*(1+s.(n+1))<=(Partial_Product s1).(n+1)
     by A7,Def1;
    (Partial_Sums(s).n)*(s.(n+1))>=0 by A1,Lm9;then
A9:(Partial_Sums(s).n)*(s.(n+1))+(1+s.(n+1)+Partial_Sums(s).n)>=
    0+(1+s.(n+1)+Partial_Sums(s).n) by XREAL_1:8;
    1+Partial_Sums(s).(n+1)=1+(Partial_Sums(s).n+s.(n+1)) by SERIES_1:def 1;
    hence thesis by A8,A9,XXREAL_0:2;
   end;
   for n holds X[n] from NAT_1:sch 1(A4,A5);
   hence thesis;
end;

Lm10:(for n holds s.n>=0) implies for n holds (Partial_Sums(s).n)*(s.(n+1))>=0
proof
   assume A1:for n holds s.n>=0;
   let n;
A2:Partial_Sums(s).n>=0 by A1,Th34;
   s.(n+1)>=0 by A1;
   hence thesis by A2,XREAL_1:129;
end;

theorem :: Bernoulli Inequality(2)
  (for n holds s1.n=1+s.n & s.n>=0)
  implies for n holds 1+Partial_Sums(s).n<=(Partial_Product s1).n
proof
   assume
A1:for n holds s1.n=1+s.n & s.n>=0;
   defpred X[Nat] means 1+Partial_Sums(s).$1<=(Partial_Product s1).$1;
A2:Partial_Sums(s).0=s.0 by SERIES_1:def 1;
A3:(Partial_Product s1).0=s1.0 by Def1;
A4:X[0] by A2,A1,A3;
A5:for n st X[n] holds X[n+1]
   proof let n;
   assume
A6:1+Partial_Sums(s).n<=(Partial_Product(s1)).n;
   s.(n+1)>-1 by A1;then
   s.(n+1)+1>-1+1 by XREAL_1:10;then
A7:(1+Partial_Sums(s).n)*(1+s.(n+1))<=((Partial_Product s1).n)*(1+s.(n+1))
    by A6,XREAL_1:66;
   ((Partial_Product s1).n)*(1+s.(n+1))=((Partial_Product s1).n)*s1.(n+1)
     by A1;then
A8:(1+Partial_Sums(s).n)*(1+s.(n+1))<=(Partial_Product s1).(n+1) by A7,Def1;
    (Partial_Sums(s).n)*(s.(n+1))>=0 by A1,Lm10;then
A9:(Partial_Sums(s).n)*(s.(n+1))+(1+s.(n+1)+Partial_Sums(s).n)>=
    0+(1+s.(n+1)+Partial_Sums(s).n) by XREAL_1:8;
    1+Partial_Sums(s).(n+1)=1+(Partial_Sums(s).n+s.(n+1))
     by SERIES_1:def 1;
    hence thesis by A8,A9,XXREAL_0:2;
   end;
   for n holds X[n] from NAT_1:sch 1(A4,A5);
   hence thesis;
end;

theorem :: Cauchy Inequality
  s3=s1(#)s2 & s4=s1(#)s1 & s5=s2(#)s2 implies for n holds
  ((Partial_Sums s3).n)^2<=(Partial_Sums s4).n*(Partial_Sums s5).n
proof
  assume
A1:s3=s1(#)s2 & s4=s1(#)s1 & s5=s2(#)s2;
  defpred X[Nat] means ((Partial_Sums s3).$1)^2<=(Partial_Sums s4).$1*
  (Partial_Sums s5).$1;
   let n;
A2:(Partial_Sums s3).0 =s3.0 by SERIES_1:def 1
 .=s1.0 * s2.0 by A1,SEQ_1:12;
   (Partial_Sums s4).0*(Partial_Sums(s5)).0
  =(s4.0)*(Partial_Sums(s5)).0 by SERIES_1:def 1
 .=(s4.0)*(s5.0) by SERIES_1:def 1
 .=(s1.0)*(s1.0)*(s5.0) by A1,SEQ_1:12
 .=(s1.0)*(s1.0)*((s2.0)*(s2.0)) by A1,SEQ_1:12
 .=(s1.0)^2*((s2.0)*(s2.0))
 .=(s1.0)^2*(s2.0)^2;then
A3:X[0] by A2;
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:((Partial_Sums s3).n)^2<=(Partial_Sums s4).n*(Partial_Sums s5).n;
   set u=(Partial_Sums s3).n;
   set v=(Partial_Sums s4).n;
   set w=(Partial_Sums s5).n;
   set h=s1.(n+1);
   set j=s2.(n+1);
   ((Partial_Sums(s3)).(n+1))=u+s3.(n+1) by SERIES_1:def 1;then
A6:((Partial_Sums(s3)).(n+1))=u+h*s2.(n+1) by A1,SEQ_1:12;
   (Partial_Sums(s4)).(n+1)*(Partial_Sums(s5)).(n+1)=
   ((Partial_Sums(s4)).n+s4.(n+1))*(Partial_Sums(s5)).(n+1)
    by SERIES_1:def 1;then
A7:(Partial_Sums(s4)).(n+1)*(Partial_Sums(s5)).(n+1)=
   (v+s4.(n+1))*(w+s5.(n+1)) by SERIES_1:def 1
  .=(v+h*h)*(w+s5.(n+1)) by A1,SEQ_1:12
  .=(v+h*h)*(w+s2.(n+1)*s2.(n+1)) by A1,SEQ_1:12
  .=v*w+v*(s2.(n+1)*s2.(n+1))+w*h*h+h*h*s2.(n+1)*s2.(n+1)
  .=v*w+v*j^2+w*(h*h)+h*h*s2.(n+1)*s2.(n+1)
  .=v*w+v*j^2+w*h^2+(h*h)*s2.(n+1)*s2.(n+1)
  .=v*w+v*j^2+w*h^2+h^2*s2.(n+1)*s2.(n+1)
  .=v*w+v*j^2+w*h^2+h^2*(s2.(n+1)*s2.(n+1))
  .=v*w+v*j^2+w*h^2+h^2*j^2;
A8:v>=0 & w>=0 by A1,Th36;then
A9:v=(sqrt(v))^2 & w=(sqrt w)^2 by SQUARE_1:def 4;
A10:((Partial_Sums(s3)).(n+1))^2
 =u^2+2*u*(h*j)+(h*j)^2 by A6;
A11:(Partial_Sums s4).(n+1)*(Partial_Sums(s5)).(n+1)-
  ((Partial_Sums s3).(n+1))^2
 =v*w-u^2+v*j^2+w*h^2+h^2*j^2-2*u*(h*j)-(h*j)^2 by A7,A10
.=v*w-u^2+v*j^2+w*h^2+(h*j)^2-2*u*(h*j)-(h*j)^2
.=v*w-u^2+v*j^2+w*h^2-2*u*h*j;
A12:v*j^2+w*h^2
 =((sqrt v)*j)^2+(sqrt w)^2*h^2 by A9
.=((sqrt v)*j)^2+((sqrt w)*h)^2;
A13:sqrt v>=0 & sqrt w>=0 by A8,SQUARE_1:def 4;
  ((sqrt v)*j)^2+((sqrt w)*h)^2>=2*abs((sqrt v)*j)*abs((sqrt w)*h)
    by Th8;then
  ((sqrt v)*j)^2+((sqrt w)*h)^2>=2*(abs(sqrt v)*abs j)*abs((sqrt w)*h)
    by COMPLEX1:151;then
  ((sqrt v)*j)^2+((sqrt w)*h)^2>=2*(abs(sqrt v)*abs j)
   *(abs((sqrt w))*abs h) by COMPLEX1:151;then
  ((sqrt v)*j)^2+((sqrt w)*h)^2>=2*(sqrt v*abs j)*(abs((sqrt w))*abs h)
    by A13,ABSVALUE:def 1;then
  ((sqrt v)*j)^2+((sqrt w)*h)^2>=2*(sqrt v*abs j)*((sqrt w)*abs h)
    by A13,ABSVALUE:def 1;then
  ((sqrt v)*j)^2+((sqrt(w))*h)^2>=2*(sqrt v*(sqrt(w)))*abs(j)*abs h;then
A14:v*j^2+w*h^2>=2*(sqrt(v*w))*abs(j)*abs h by A12,A8,SQUARE_1:97;
A15:abs(u)<=sqrt(v*w) by Lm8,A5;
  (2*(abs(j)*abs(h)))*sqrt(v*w)>=(2*(abs(j)*abs h))*abs(u)
   by A15,XREAL_1:66;then
  v*j^2+w*h^2>=2*(abs(u)*abs(j))*abs(h) by A14,XXREAL_0:2;then
  v*j^2+w*h^2>=2*(abs(u*j))*abs(h) by COMPLEX1:151;then
  v*j^2+w*h^2>=2*((abs(u*j))*abs(h));then
A16:v*j^2+w*h^2>=2*abs(u*j*h) by COMPLEX1:151;
    abs(u*j*h)>=(u*j*h) by ABSVALUE:11;then
    2*abs(u*j*h)>=2*(u*j*h) by XREAL_1:66;then
    v*j^2+w*h^2>=2*u*j*h by A16,XXREAL_0:2;then
A17:v*j^2+w*h^2-2*u*j*h>=0 by XREAL_1:50;
    v*w-u^2>=0 by A5,XREAL_1:50;then
    v*w-u^2+(v*j^2+w*h^2-2*u*j*h)>=0 by A17,XREAL_1:35;then
  (Partial_Sums s4).(n+1)*(Partial_Sums s5).(n+1)-
  ((Partial_Sums s3).(n+1))^2+((Partial_Sums s3).(n+1))^2>=
  0+((Partial_Sums s3).(n+1))^2 by A11,XREAL_1:8;
  hence thesis;
  end;
  for n holds X[n] from NAT_1:sch 1(A3,A4);
  hence thesis;
end;

Lm11:(for n holds s.n=(s1.n+s2.n)^2) implies (Partial_Sums s).n>=0
proof
  assume A1:for n holds s.n=(s1.n+s2.n)^2;
  now let n; s.n=(s1.n+s2.n)^2 by A1; hence s.n>=0 by XREAL_1:65; end;
  hence thesis by Th34;
end;

theorem :: Minkowski Inequality
  (s4=s1(#)s1 & s5=s2(#)s2 & for n holds s1.n>=0 & s2.n>=0 &
  s3.n=(s1.n+s2.n)^2) implies for n holds
  sqrt((Partial_Sums s3).n) <=
    sqrt((Partial_Sums s4).n) + sqrt((Partial_Sums s5).n)
proof
   assume
A1:s4=s1(#)s1 & s5=s2(#)s2 & for n holds s1.n>=0 & s2.n>=0 &
   s3.n=(s1.n+s2.n)^2;
   defpred X[Nat] means sqrt((Partial_Sums(s3)).$1)<=sqrt((Partial_Sums(s4))
   .$1)+sqrt((Partial_Sums(s5)).$1);
A2:s1.0>=0 & s2.0>=0 by A1;then
A3:s1.0+s2.0>=0 by XREAL_1:35;
   sqrt((Partial_Sums(s4)).0)+sqrt((Partial_Sums(s5)).0)
  =sqrt(s4.0)+sqrt((Partial_Sums(s5)).0) by SERIES_1:def 1
 .=sqrt(s4.0)+sqrt(s5.0) by SERIES_1:def 1
 .=sqrt(s1.0*s1.0)+sqrt(s5.0) by A1,SEQ_1:12
 .=sqrt(s1.0*s1.0)+sqrt(s2.0*s2.0) by A1,SEQ_1:12
 .=sqrt((s1.0)^2)+sqrt(s2.0*s2.0)
 .=s1.0+sqrt(s2.0*s2.0) by A2,SQUARE_1:89
 .=s1.0+sqrt((s2.0)^2)
 .=s1.0+s2.0 by A2,SQUARE_1:89
 .=sqrt((s1.0+s2.0)^2) by A3,SQUARE_1:89
 .=sqrt(s3.0) by A1
 .=sqrt((Partial_Sums s3).0) by SERIES_1:def 1;then
A4:X[0];
A5:for n st X[n] holds X[n+1]
   proof let n;
   assume
A6:sqrt((Partial_Sums s3).n)<=sqrt((Partial_Sums s4).n)+
   sqrt((Partial_Sums s5).n);
   set u=(Partial_Sums s3).n;
   set v=(Partial_Sums s4).n;
   set w=(Partial_Sums s5).n;
   set h=s1.(n+1);
   set j=s2.(n+1);
A7:u>=0 by A1,Lm11;then
A8:sqrt(u)>=0 by SQUARE_1:def 4;
A9:v>=0 by A1,Th36;
A10:w>=0 by A1,Th36;
    (sqrt u)^2<=(sqrt(v)+sqrt(w))^2 by A6,A8,SQUARE_1:77;then
    u<=(sqrt(v)+sqrt(w))^2 by A7,SQUARE_1:def 4;then
    u<=(sqrt(v))^2+2*sqrt(v)*sqrt(w)+(sqrt(w))^2;then
    u<=v+2*sqrt(v)*sqrt(w)+(sqrt(w))^2 by A9,SQUARE_1:def 4;then
A11:u<=v+2*sqrt(v)*sqrt(w)+w by A10,SQUARE_1:def 4;
A12:sqrt((Partial_Sums s3).(n+1))
  =sqrt((Partial_Sums s3).n+s3.(n+1)) by SERIES_1:def 1
  .=sqrt(u+(h+j)^2) by A1;
A13:sqrt((Partial_Sums s4).(n+1))+sqrt((Partial_Sums(s5)).(n+1))
   =sqrt((Partial_Sums s4).n+s4.(n+1))+sqrt((Partial_Sums(s5)).(n+1))
     by SERIES_1:def 1
  .=sqrt(v+s4.(n+1))+sqrt(w+s5.(n+1)) by SERIES_1:def 1
  .=sqrt(v+s1.(n+1)*s1.(n+1))+sqrt(w+s5.(n+1)) by A1,SEQ_1:12
  .=sqrt(v+s1.(n+1)*s1.(n+1))+sqrt(w+s2.(n+1)*s2.(n+1)) by A1,SEQ_1:12
  .=sqrt(v+h^2)+sqrt(w+j*j)
  .=sqrt(v+h^2)+sqrt(w+j^2);
A14:h>=0 & j>=0 by A1;
A15:h^2>=0 & j^2>=0 by XREAL_1:65;then
A16:h^2*j^2>=0 by XREAL_1:129;
A17:w*v>=0 by A9,A10,XREAL_1:129;
A18:((sqrt(v*w))+h*j)^2>=0 by XREAL_1:65;
A19:sqrt(v*w)>=0 by A17,SQUARE_1:def 4;
A20:h*j>=0 by A14,XREAL_1:129;
A21:(sqrt(v*w))+h*j>=0 by A19,A20,XREAL_1:35;
A22:v+h^2>=0 by A9,A15,XREAL_1:35;
A23:w+j^2>=0 by A10,A15,XREAL_1:35;
    (h+j)^2>=0 by XREAL_1:65;then
A24:u+(h+j)^2>=0 by A7,XREAL_1:35;
    sqrt(v+h^2)>=0 & sqrt(w+j^2)>=0 by A22,A23,SQUARE_1:def 4;then
A25:sqrt(v+h^2)+sqrt(w+j^2)>=0 by XREAL_1:35;
    h^2*w>=0 & j^2*v>=0 by A9,A10,A15,XREAL_1:129;then
    h^2*w+j^2*v>=2*sqrt((h^2*w)*(j^2*v)) by SIN_COS2:1;then
    h^2*w+j^2*v>=2*sqrt((h^2*j^2)*(w*v));then
    h^2*w+j^2*v>=2*(sqrt(h^2*j^2)*sqrt(w*v))
      by A16,A17,SQUARE_1:97;then
    h^2*w+j^2*v>=2*sqrt(h^2*j^2)*sqrt(w*v);then
    h^2*w+j^2*v>=2*(sqrt(h^2)*sqrt(j^2))*sqrt(w*v)
      by A15,SQUARE_1:97;then
    h^2*w+j^2*v>=2*sqrt(h^2)*sqrt(j^2)*sqrt(w*v);then
    h^2*w+j^2*v>=2*h*sqrt(j^2)*sqrt(w*v) by A14,SQUARE_1:89;then
    h^2*w+j^2*v>=2*h*j*sqrt(w*v) by A14,SQUARE_1:89;then
    (h^2*w+j^2*v)+(v*w+h^2*j^2)>=2*h*j*sqrt(w*v)+(v*w+h^2*j^2)
      by XREAL_1:8;then
    v*w+j^2*v+h^2*w+h^2*j^2>=v*w+2*(sqrt(w*v))*(h*j)+h^2*j^2;then
    (v+h^2)*(w+j^2)>=(sqrt(v*w))^2+2*(sqrt(w*v))*(h*j)+h^2*j^2
      by A17,SQUARE_1:def 4;then
    (v+h^2)*(w+j^2)>=(sqrt(v*w))^2+2*(sqrt(w*v))*(h*j)+(h*j)^2;then
    ((sqrt(v*w))+h*j)^2<=(v+h^2)*(w+j^2) by SQUARE_1:63;then
    sqrt(((sqrt(v*w))+h*j)^2)<=sqrt((v+h^2)*(w+j^2)) by A18,SQUARE_1:94;then
    (sqrt(v*w))+h*j<=sqrt((v+h^2)*(w+j^2)) by A21,SQUARE_1:89;then
    2*((sqrt(v*w))+h*j)<=2*(sqrt((v+h^2)*(w+j^2))) by XREAL_1:66;then
A26:(v+w)+(2*(sqrt(v*w))+2*h*j)<=(v+w)+(2*(sqrt((v+h^2)*(w+j^2))))
      by XREAL_1:8;
    u+2*h*j<=v+w+2*(sqrt(v)*sqrt(w))+2*h*j by XREAL_1:8,A11;then
    u+2*h*j<=v+w+2*sqrt(v*w)+2*h*j by A9,A10,SQUARE_1:97;then
    u+2*h*j<=v+w+2*(sqrt((v+h^2)*(w+j^2))) by A26,XXREAL_0:2;then
    (u+2*h*j)+(h^2+j^2)<=(v+w+2*(sqrt((v+h^2)*(w+j^2))))+(h^2+j^2)
      by XREAL_1:8;then
    u+(h^2+2*h*j+j^2)<=(v+h^2)+2*sqrt((v+h^2)*(w+j^2))+(w+j^2);then
    u+(h+j)^2<=(v+h^2)+2*sqrt((v+h^2)*(w+j^2))+(w+j^2);then
    u+(h+j)^2<=(sqrt(v+h^2))^2+2*sqrt((v+h^2)*(w+j^2))+(w+j^2)
     by A22,SQUARE_1:def 4;then
    u+(h+j)^2<=(sqrt(v+h^2))^2+2*sqrt((v+h^2)*(w+j^2))+(sqrt(w+j^2))^2
     by A23,SQUARE_1:def 4;then
    u+(h+j)^2<=(sqrt(v+h^2))^2+2*(sqrt(v+h^2)*sqrt(w+j^2))+(sqrt(w+j^2))^2
     by A22,A23,SQUARE_1:97;then
    u+(h+j)^2<=(sqrt(v+h^2))^2+2*sqrt(v+h^2)*sqrt(w+j^2)+(sqrt(w+j^2))^2;
     then
    u+(h+j)^2<=(sqrt(v+h^2)+sqrt(w+j^2))^2;then
    sqrt(u+(h+j)^2)<=sqrt((sqrt(v+h^2)+sqrt(w+j^2))^2)
     by A24,SQUARE_1:94;
    hence thesis by A12,A13,A25,SQUARE_1:89;
  end;
  for n holds X[n] from NAT_1:sch 1(A4,A5);
  hence thesis;
end;

Lm12:(for n holds s.n>0) implies (n+1)-root((Partial_Product s).n)>0
proof
  assume for n holds s.n>0;then
A1:(Partial_Product s).n>0 by Th42;
A2:n+1>=0+1 by XREAL_1:8;then
   (n+1)-root((Partial_Product s).n)
 =(n+1) -Root((Partial_Product s).n) by A1,POWER:def 1;
  hence thesis by A1,A2,PREPOWER:def 3;
end;

Lm13:(for n holds s.n>0 & s.n>s.(n-1)) implies
     (s.(n+1)-(((Partial_Sums s).n)/(n+1)))/(n+2)>0
proof
  assume for n holds s.n>0 & s.n>s.(n-1);then
  (n+1)*s.(n+1)>(Partial_Sums s).n by Th37;then
  ((n+1)*s.(n+1))/(n+1)>((Partial_Sums s).n)/(n+1)
    by XREAL_1:76;then
   s.(n+1)>((Partial_Sums s).n)/(n+1) by XCMPLX_1:90;then
   s.(n+1)-(((Partial_Sums s).n)/(n+1))>0 by XREAL_1:52;
   hence thesis by XREAL_1:141;
end;

theorem
  (for n holds s.n>0 & s.n>s.(n-1)) implies
  (Partial_Sums s).n>=(n+1)*((n+1)-root((Partial_Product s).n))
proof
  assume A1:for n holds s.n>0 & s.n>s.(n-1);
  defpred X[Nat] means (Partial_Sums s).$1>=
  ($1+1)*(($1+1)-root((Partial_Product s).$1));
A2:(Partial_Sums s).0=s.0 by SERIES_1:def 1;
   (0+1)*((0+1)-root((Partial_Product s).0))
  =((0+1)-root(s.0)) by Def1
 .=(Partial_Sums s).0 by A2,POWER:10;then
A3:X[0];
A4:for n st X[n] holds X[n+1]
   proof let n;
   assume
A5:(Partial_Sums s).n>=(n+1)*((n+1)-root((Partial_Product s).n));
A6:((Partial_Sums s).(n+1))/(n+2)
  =(1*(Partial_Sums s).n+s.(n+1))/(n+2) by SERIES_1:def 1
 .=((n+1)/(n+1)*(Partial_Sums s).n+s.(n+1))/(n+2) by XCMPLX_1:60
 .=((((n+2)-1)*(Partial_Sums s).n)/(n+1)+s.(n+1))/(n+2) by XCMPLX_1:75;
    set u=(Partial_Sums s).n;
    set v=s.(n+1);
A7:((Partial_Sums s).(n+1))/(n+2)
  =(((n+2)*u-u)/(n+1)+v)/(n+2) by A6
 .=((((n+2)*u)/(n+1)-u/(n+1))+v)/(n+2) by XCMPLX_1:121
 .=(((n+2)*u)/(n+1)-u/(n+1))/(n+2)+v/(n+2) by XCMPLX_1:63
 .=((n+2)*u)/(n+1)/(n+2)-(u/(n+1))/(n+2)+v/(n+2) by XCMPLX_1:121
 .=((n+2)*u)/((n+1)*(n+2))-(u/(n+1))/(n+2)+v/(n+2) by XCMPLX_1:79
 .=u/(n+1)-(u/(n+1))/(n+2)+v/(n+2) by XCMPLX_1:92
 .=u/(n+1)+(v/(n+2)-(u/(n+1))/(n+2))
 .=u/(n+1)+(v-(u/(n+1)))/(n+2) by XCMPLX_1:121;
   u>0 by A1,Th33;then
A8:u/(n+1)>0 by XREAL_1:141;
A9:(v-(u/(n+1)))/(n+2)>0 by A1,Lm13;
  set w=((u/(n+1))+(v-(u/(n+1)))/(n+2))|^(n+2);
  w>=(u/(n+1))|^((n+1)+1)+(n+2)*((u/(n+1))|^(n+1))*
  ((v-(u/(n+1)))/(n+2)) by A8,A9,Th31;then
  w>=(u/(n+1))|^((n+1))*(u/(n+1))+((u/(n+1))|^(n+1))*
     ((v-(u/(n+1)))/(n+2)*(n+2)) by NEWTON:11;then
A10:w>=(u/(n+1))|^((n+1))*(u/(n+1))+((u/(n+1))|^(n+1))*
     (v-(u/(n+1))) by XCMPLX_1:88;
   u/(n+1)>=((n+1)*
   ((n+1)-root((Partial_Product s).n)))/(n+1) by A5,XREAL_1:74;then
A11:((n+1)-root((Partial_Product s).n))<=u/(n+1) by XCMPLX_1:90;
  set h=((n+1)-root((Partial_Product s).n));
  set j=u/(n+1);
A12:h>0 by A1,Lm12;
A13:h|^(n+1)<=j|^(n+1) by A11,A12,PREPOWER:17;
A14:(Partial_Product(s)).n>0 by A1,Th42;
A15:n+1>=0+1 by XREAL_1:8;then
A16:h=((n+1) -Root((Partial_Product s).n)) by A14,POWER:def 1;
A17:(Partial_Product s).n<=j|^(n+1) by A13,A16,A14,A15,PREPOWER:28;
A18:v>0 by A1;
    ((Partial_Product s).n)*v<=(j|^(n+1))*v by A17,A18,XREAL_1:66;then
    ((Partial_Product s).(n+1))<=(j|^(n+1))*v by Def1;then
A19:(((Partial_Sums s).(n+1))/(n+2))|^(n+2)>=
    (Partial_Product s).(n+1) by A7,A10,XXREAL_0:2;
    (n+1)+1>=1+1 by A15,XREAL_1:8;then
A20:n+2>=1 by XXREAL_0:2;
A21:(Partial_Product s).(n+1)>=0 by A1,Th42;
    (Partial_Sums s).(n+1)>0 by A1,Th33;then
A22:((Partial_Sums s).(n+1))/(n+2)>0 by XREAL_1:141;
    (n+2) -Root((((Partial_Sums s).(n+1))/(n+2))|^(n+2))>=
    (n+2) -Root((Partial_Product s).(n+1))
      by A19,A20,A21,PREPOWER:36;then
    (((Partial_Sums s).(n+1))/(n+2))>=
    (n+2) -Root((Partial_Product s).(n+1)) by A20,A22,PREPOWER:28;then
    (((Partial_Sums s).(n+1))/(n+2))>=
    (n+2)-root((Partial_Product s).(n+1)) by A20,A21,POWER:def 1;then
    ((Partial_Sums s).(n+1))/(n+2)*(n+2)>=
    (n+2)*((n+2)-root((Partial_Product s).(n+1))) by XREAL_1:66;
    hence thesis by XCMPLX_1:88;
    end;
    for n holds X[n] from NAT_1:sch 1(A3,A4);
    hence thesis;
end;
