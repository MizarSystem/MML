:: The Ordering of Points on a Curve, Part {IV}
::  by Artur Korni{\l}owicz
::
:: Received September 16, 2002
:: Copyright (c) 2002 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, EUCLID, PRE_TOPC, TOPREAL2, RELAT_1, STRUCT_0,
      XREAL_0, ORDINAL1, ARYTM_1, SQUARE_1, XBOOLE_0, FUNCT_1, TOPS_2, RCOMP_1,
      ORDINAL2, TOPREAL1, XXREAL_2, MCART_1, XXREAL_0, CARD_1, ARYTM_3,
      PCOMPS_1, METRIC_1, TARSKI, COMPLEX1, JORDAN2C, RLTOPSP1, SPPOL_1,
      ZFMISC_1, JORDAN6, JORDAN3, JORDAN18, SEQ_4;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, FUNCT_1, RELSET_1, ORDINAL1,
      NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, SQUARE_1, XXREAL_2, SEQ_4,
      STRUCT_0, PCOMPS_1, COMPLEX1, PRE_TOPC, TOPS_2, RCOMP_1, COMPTS_1,
      METRIC_1, SPPOL_1, RLTOPSP1, EUCLID, TOPREAL1, PSCOMP_1, TOPREAL2,
      JORDAN5C, JORDAN2C, TOPREAL6, JORDAN6;
 constructors REAL_1, SQUARE_1, COMPLEX1, RCOMP_1, CONNSP_1, TOPS_2, COMPTS_1,
      TOPREAL1, SPPOL_1, PSCOMP_1, JORDAN5C, JORDAN6, JORDAN2C, TOPREAL6,
      SEQ_1, XXREAL_2, SEQ_4, RELSET_1, FUNCSDOM, BINOP_2, PCOMPS_1, RVSUM_1;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, SQUARE_1,
      MEMBERED, COMPTS_1, EUCLID, TOPREAL1, STRUCT_0, TOPS_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, ZFMISC_1, SEQ_4, SQUARE_1, EUCLID, PCOMPS_1,
      SUBSET_1, XXREAL_2;
 theorems JORDAN5B, TARSKI, TOPREAL1, XBOOLE_0, EUCLID, JORDAN6, TOPS_2,
      XBOOLE_1, SEQ_4, RELAT_1, PSCOMP_1, FUNCT_2, TOPREAL3, JORDAN1A,
      COMPTS_1, ZFMISC_1, JORDAN5C, TOPREAL2, JORDAN2C, SQUARE_1, RCOMP_1,
      TOPMETR, METRIC_1, JORDAN16, SPPOL_1, COMPLEX1, XREAL_1, TOPREAL6,
      XXREAL_0, JCT_MISC, XXREAL_2, PRE_TOPC, RLTOPSP1;

begin  :: Preliminaries

reserve n for Element of NAT,
  V for Subset of TOP-REAL n,
  s,s1,s2,t,t1,t2 for Point of TOP-REAL n,
  C for Simple_closed_curve,
  P for Subset of TOP-REAL 2,
  a,p ,p1,p2,q,q1,q2 for Point of TOP-REAL 2;

Lm1: dom proj1 = the carrier of TOP-REAL 2 by FUNCT_2:def 1;

Lm2: dom proj2 = the carrier of TOP-REAL 2 by FUNCT_2:def 1;

theorem
  for a,b being real number holds (a-b)^2 = (b-a)^2;

canceled;

theorem
  for S,T being non empty TopStruct, f being Function of S,T, A being
  Subset of T st f is being_homeomorphism & A is compact holds f"A is compact
proof
  let S,T be non empty TopStruct, f be Function of S,T, A be Subset of T such
  that
A1: f is being_homeomorphism and
A2: A is compact;
A3: rng f = [#]T & f is one-to-one by A1,TOPS_2:def 5;
  f" is being_homeomorphism by A1,TOPS_2:70;
  then
A4: rng (f") = [#]S by TOPS_2:def 5;
  f" is continuous by A1,TOPS_2:def 5;
  then f".:A is compact by A2,A4,COMPTS_1:24;
  hence thesis by A3,TOPS_2:68;
end;

theorem Th4:
  proj2.:(north_halfline a) is bounded_below
proof
  take a`2;
  let r be ext-real number;
  assume r in proj2.:(north_halfline a);
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in north_halfline a and
A3: r = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  r = x`2 by A3,PSCOMP_1:def 29;
  hence thesis by A2,TOPREAL1:def 12;
end;

theorem Th5:
  proj2.:(south_halfline a) is bounded_above
proof
  take a`2;
  let r be ext-real number;
  assume r in proj2.:(south_halfline a);
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in south_halfline a and
A3: r = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  r = x`2 by A3,PSCOMP_1:def 29;
  hence thesis by A2,TOPREAL1:def 14;
end;

theorem Th6:
  proj1.:(west_halfline a) is bounded_above
proof
  take a`1;
  let r be ext-real number;
  assume r in proj1.:(west_halfline a);
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in west_halfline a and
A3: r = proj1.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  r = x`1 by A3,PSCOMP_1:def 28;
  hence thesis by A2,TOPREAL1:def 15;
end;

theorem Th7:
  proj1.:(east_halfline a) is bounded_below
proof
  take a`1;
  let r be ext-real number;
  assume r in proj1.:(east_halfline a);
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in east_halfline a and
A3: r = proj1.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  r = x`1 by A3,PSCOMP_1:def 28;
  hence thesis by A2,TOPREAL1:def 13;
end;

registration
  let a;
  cluster proj2.:(north_halfline a) -> non empty;
  coherence by Lm2,RELAT_1:152;
  cluster proj2.:(south_halfline a) -> non empty;
  coherence by Lm2,RELAT_1:152;
  cluster proj1.:(west_halfline a) -> non empty;
  coherence by Lm1,RELAT_1:152;
  cluster proj1.:(east_halfline a) -> non empty;
  coherence by Lm1,RELAT_1:152;
end;

theorem Th8:
  lower_bound(proj2.:(north_halfline a)) = a`2
proof
  set X = proj2.:(north_halfline a);
A1: now
    let r be real number;
    assume r in X;
    then consider x being set such that
A2: x in the carrier of TOP-REAL 2 and
A3: x in north_halfline a and
A4: r = proj2.x by FUNCT_2:115;
    reconsider x as Point of TOP-REAL 2 by A2;
    r = x`2 by A4,PSCOMP_1:def 29;
    hence a`2 <= r by A3,TOPREAL1:def 12;
  end;
A5: now
    reconsider r = a`2 as real number;
    let s be real number;
    assume 0 < s;
    then
A6: r + 0 < a`2 + s by XREAL_1:10;
    take r;
    a in north_halfline a & r = proj2.a by PSCOMP_1:def 29,TOPREAL1:45;
    hence r in X by FUNCT_2:43;
    thus r < a`2 + s by A6;
  end;
  X is bounded_below by Th4;
  hence thesis by A1,A5,SEQ_4:def 5;
end;

theorem Th9:
  upper_bound(proj2.:(south_halfline a)) = a`2
proof
  set X = proj2.:(south_halfline a);
A1: now
    let r be real number;
    assume r in X;
    then consider x being set such that
A2: x in the carrier of TOP-REAL 2 and
A3: x in south_halfline a and
A4: r = proj2.x by FUNCT_2:115;
    reconsider x as Point of TOP-REAL 2 by A2;
    r = x`2 by A4,PSCOMP_1:def 29;
    hence r <= a`2 by A3,TOPREAL1:def 14;
  end;
A5: now
    reconsider r = a`2 as real number;
    let s be real number;
    assume 0 < s;
    then
A6: a`2 - s < r - 0 by XREAL_1:17;
    take r;
    a in south_halfline a & r = proj2.a by PSCOMP_1:def 29,TOPREAL1:45;
    hence r in X by FUNCT_2:43;
    thus a`2 - s < r by A6;
  end;
  X is bounded_above by Th5;
  hence thesis by A1,A5,SEQ_4:def 4;
end;

theorem
  upper_bound(proj1.:(west_halfline a)) = a`1
proof
  set X = proj1.:(west_halfline a);
A1: now
    let r be real number;
    assume r in X;
    then consider x being set such that
A2: x in the carrier of TOP-REAL 2 and
A3: x in west_halfline a and
A4: r = proj1.x by FUNCT_2:115;
    reconsider x as Point of TOP-REAL 2 by A2;
    r = x`1 by A4,PSCOMP_1:def 28;
    hence r <= a`1 by A3,TOPREAL1:def 15;
  end;
A5: now
    reconsider r = a`1 as real number;
    let s be real number;
    assume 0 < s;
    then
A6: a`1 - s < r - 0 by XREAL_1:17;
    take r;
    a in west_halfline a & r = proj1.a by PSCOMP_1:def 28,TOPREAL1:45;
    hence r in X by FUNCT_2:43;
    thus a`1 - s < r by A6;
  end;
  X is bounded_above by Th6;
  hence thesis by A1,A5,SEQ_4:def 4;
end;

theorem
  lower_bound(proj1.:(east_halfline a)) = a`1
proof
  set X = proj1.:(east_halfline a);
A1: now
    let r be real number;
    assume r in X;
    then consider x being set such that
A2: x in the carrier of TOP-REAL 2 and
A3: x in east_halfline a and
A4: r = proj1.x by FUNCT_2:115;
    reconsider x as Point of TOP-REAL 2 by A2;
    r = x`1 by A4,PSCOMP_1:def 28;
    hence a`1 <= r by A3,TOPREAL1:def 13;
  end;
A5: now
    reconsider r = a`1 as real number;
    let s be real number;
    assume 0 < s;
    then
A6: r + 0 < a`1 + s by XREAL_1:10;
    take r;
    a in east_halfline a & r = proj1.a by PSCOMP_1:def 28,TOPREAL1:45;
    hence r in X by FUNCT_2:43;
    thus r < a`1 + s by A6;
  end;
  X is bounded_below by Th7;
  hence thesis by A1,A5,SEQ_4:def 5;
end;

Lm3: the TopStruct of TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8;

registration
  let a;
  cluster north_halfline a -> closed;
  coherence
  proof
    set X = north_halfline a;
    reconsider XX = X` as Subset of TOP-REAL 2;
    reconsider OO = XX as Subset of TopSpaceMetr Euclid 2 by Lm3;
    for p being Point of Euclid 2 st p in X` ex r being real number st r>0
    & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      reconsider x = p as Point of TOP-REAL 2 by EUCLID:71;
      assume p in X`;
      then
A1:   not p in X by XBOOLE_0:def 5;
      per cases by A1,TOPREAL1:def 12;
      suppose
A2:     x`1 <> a`1;
        take r = abs(x`1-a`1);
        x`1 - a`1 <> 0 by A2;
        hence r > 0 by COMPLEX1:133;
        let b be set;
        assume
A3:     b in Ball(p,r);
        then reconsider bb=b as Point of Euclid 2;
        reconsider c = bb as Point of TOP-REAL 2 by EUCLID:71;
        dist(p,bb) < r by A3,METRIC_1:12;
        then
A4:     dist(x,c) < r by TOPREAL6:def 1;
        now
          assume c`1 = a`1;
          then
A5:       sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`1-c`1) by A4,TOPREAL6:101;
A6:       0 <= (x`1-c`1)^2 by XREAL_1:65;
A7:       0 <= (x`2-c`2)^2 by XREAL_1:65;
          then 0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by A6,SQUARE_1:def 4;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`1-c`1))^2 by A5,
SQUARE_1:78;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-c`1)^2 by
COMPLEX1:161;
          then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-c`1)^2 + 0 by A6,SQUARE_1:def 4
;
          hence contradiction by A7,XREAL_1:9;
        end;
        then not c in X by TOPREAL1:def 12;
        hence b in X` by XBOOLE_0:def 5;
      end;
      suppose
A8:     x`2 < a`2;
        take r = a`2-x`2;
        thus r > 0 by A8,XREAL_1:52;
        let b be set;
        assume
A9:     b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2 by EUCLID:71;
        dist(p,b) < r by A9,METRIC_1:12;
        then
A10:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume c`2 >= a`2;
          then
A11:      a`2-x`2 <= c`2-x`2 by XREAL_1:15;
          0 <= a`2-x`2 by A8,XREAL_1:52;
          then
A12:      (a`2-x`2)^2 <= (c`2-x`2)^2 by A11,SQUARE_1:77;
A13:      0 <= (x`1-c`1)^2 by XREAL_1:65;
A14:      sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < a`2-x`2 by A10,TOPREAL6:101;
A15:      0 <= (x`2-c`2)^2 by XREAL_1:65;
          then 0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by A13,SQUARE_1:def 4;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (a`2-x`2)^2 by A14,
SQUARE_1:78;
          then
A16:      (x`1-c`1)^2 + (x`2-c`2)^2 < (a`2-x`2)^2 by A13,A15,SQUARE_1:def 4;
          0 + (x`2-c`2)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A13,XREAL_1:9;
          hence contradiction by A16,A12,XXREAL_0:2;
        end;
        then not c in X by TOPREAL1:def 12;
        hence thesis by XBOOLE_0:def 5;
      end;
    end;
    then OO is open by TOPMETR:22;
    then XX is open by Lm3,PRE_TOPC:60;
    then XX` is closed;
    hence thesis;
  end;
  cluster south_halfline a -> closed;
  coherence
  proof
    set X = south_halfline a;
    reconsider XX = X` as Subset of TOP-REAL 2;
    reconsider OO = XX as Subset of TopSpaceMetr Euclid 2 by Lm3;
    for p being Point of Euclid 2 st p in X` ex r being real number st r>
    0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      reconsider x = p as Point of TOP-REAL 2 by EUCLID:71;
      assume p in X`;
      then
A17:  not p in X by XBOOLE_0:def 5;
      per cases by A17,TOPREAL1:def 14;
      suppose
A18:    x`1 <> a`1;
        take r = abs(x`1-a`1);
        x`1 - a`1 <> 0 by A18;
        hence r > 0 by COMPLEX1:133;
        let b be set;
        assume
A19:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2 by EUCLID:71;
        dist(p,b) < r by A19,METRIC_1:12;
        then
A20:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume c`1 = a`1;
          then
A21:      sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`1-c`1) by A20,TOPREAL6:101;
A22:      0 <= (x`1-c`1)^2 by XREAL_1:65;
A23:      0 <= (x`2-c`2)^2 by XREAL_1:65;
          then 0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by A22,SQUARE_1:def 4;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`1-c`1))^2 by A21,
SQUARE_1:78;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-c`1)^2 by
COMPLEX1:161;
          then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-c`1)^2 + 0 by A22,
SQUARE_1:def 4;
          hence contradiction by A23,XREAL_1:9;
        end;
        then not c in X by TOPREAL1:def 14;
        hence thesis by XBOOLE_0:def 5;
      end;
      suppose
A24:    x`2 > a`2;
        take r = x`2-a`2;
        thus r > 0 by A24,XREAL_1:52;
        let b be set;
        assume
A25:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2 by EUCLID:71;
        dist(p,b) < r by A25,METRIC_1:12;
        then
A26:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume c`2 <= a`2;
          then
A27:      x`2-c`2 >= x`2-a`2 by XREAL_1:15;
          0 <= x`2-a`2 by A24,XREAL_1:52;
          then
A28:      (x`2-a`2)^2 <= (x`2-c`2)^2 by A27,SQUARE_1:77;
A29:      0 <= (x`1-c`1)^2 by XREAL_1:65;
A30:      sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < x`2-a`2 by A26,TOPREAL6:101;
A31:      0 <= (x`2-c`2)^2 by XREAL_1:65;
          then 0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by A29,SQUARE_1:def 4;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-a`2)^2 by A30,
SQUARE_1:78;
          then
A32:      (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-a`2)^2 by A29,A31,SQUARE_1:def 4;
          0 + (x`2-c`2)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A29,XREAL_1:9;
          hence contradiction by A32,A28,XXREAL_0:2;
        end;
        then not c in X by TOPREAL1:def 14;
        hence thesis by XBOOLE_0:def 5;
      end;
    end;
    then OO is open by TOPMETR:22;
    then XX is open by Lm3,PRE_TOPC:60;
    then XX` is closed;
    hence thesis;
  end;
  cluster east_halfline a -> closed;
  coherence
  proof
    set X = east_halfline a;
    reconsider XX = X` as Subset of TOP-REAL 2;
    reconsider OO = XX as Subset of TopSpaceMetr Euclid 2 by Lm3;
    for p being Point of Euclid 2 st p in X` ex r being real number st r>
    0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      reconsider x = p as Point of TOP-REAL 2 by EUCLID:71;
      assume p in X`;
      then
A33:  not p in X by XBOOLE_0:def 5;
      per cases by A33,TOPREAL1:def 13;
      suppose
A34:    x`2 <> a`2;
        take r = abs(x`2-a`2);
        x`2 - a`2 <> 0 by A34;
        hence r > 0 by COMPLEX1:133;
        let b be set;
        assume
A35:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2 by EUCLID:71;
        dist(p,b) < r by A35,METRIC_1:12;
        then
A36:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume c`2 = a`2;
          then
A37:      sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`2-c`2) by A36,TOPREAL6:101;
A38:      0 <= (x`2-c`2)^2 by XREAL_1:65;
A39:      0 <= (x`1-c`1)^2 by XREAL_1:65;
          then 0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by A38,SQUARE_1:def 4;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`2-c`2))^2 by A37,
SQUARE_1:78;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-c`2)^2 by
COMPLEX1:161;
          then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-c`2)^2 + 0 by A38,
SQUARE_1:def 4;
          hence contradiction by A39,XREAL_1:9;
        end;
        then not c in X by TOPREAL1:def 13;
        hence thesis by XBOOLE_0:def 5;
      end;
      suppose
A40:    x`1 < a`1;
        take r = a`1-x`1;
        thus r > 0 by A40,XREAL_1:52;
        let b be set;
        assume
A41:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2 by EUCLID:71;
        dist(p,b) < r by A41,METRIC_1:12;
        then
A42:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume c`1 >= a`1;
          then
A43:      a`1-x`1 <= c`1-x`1 by XREAL_1:15;
          0 <= a`1-x`1 by A40,XREAL_1:52;
          then
A44:      (a`1-x`1)^2 <= (c`1-x`1)^2 by A43,SQUARE_1:77;
A45:      0 <= (x`2-c`2)^2 by XREAL_1:65;
A46:      sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < a`1-x`1 by A42,TOPREAL6:101;
A47:      0 <= (x`1-c`1)^2 by XREAL_1:65;
          then 0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by A45,SQUARE_1:def 4;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (a`1-x`1)^2 by A46,
SQUARE_1:78;
          then
A48:      (x`1-c`1)^2 + (x`2-c`2)^2 < (a`1-x`1)^2 by A45,A47,SQUARE_1:def 4;
          (x`1-c`1)^2 + 0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A45,XREAL_1:9;
          hence contradiction by A48,A44,XXREAL_0:2;
        end;
        then not c in X by TOPREAL1:def 13;
        hence thesis by XBOOLE_0:def 5;
      end;
    end;
    then OO is open by TOPMETR:22;
    then XX is open by Lm3,PRE_TOPC:60;
    then XX` is closed;
    hence thesis;
  end;
  cluster west_halfline a -> closed;
  coherence
  proof
    set X = west_halfline a;
    reconsider XX = X` as Subset of TOP-REAL 2;
    reconsider OO = XX as Subset of TopSpaceMetr Euclid 2 by Lm3;
    for p being Point of Euclid 2 st p in X` ex r being real number st r
    >0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      reconsider x = p as Point of TOP-REAL 2 by EUCLID:71;
      assume p in X`;
      then
A49:  not p in X by XBOOLE_0:def 5;
      per cases by A49,TOPREAL1:def 15;
      suppose
A50:    x`2 <> a`2;
        take r = abs(x`2-a`2);
        x`2 - a`2 <> 0 by A50;
        hence r > 0 by COMPLEX1:133;
        let b be set;
        assume
A51:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2 by EUCLID:71;
        dist(p,b) < r by A51,METRIC_1:12;
        then
A52:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume c`2 = a`2;
          then
A53:      sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`2-c`2) by A52,TOPREAL6:101;
A54:      0 <= (x`2-c`2)^2 by XREAL_1:65;
A55:      0 <= (x`1-c`1)^2 by XREAL_1:65;
          then 0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by A54,SQUARE_1:def 4;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`2-c`2))^2 by A53,
SQUARE_1:78;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-c`2)^2 by
COMPLEX1:161;
          then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-c`2)^2 + 0 by A54,
SQUARE_1:def 4;
          hence contradiction by A55,XREAL_1:9;
        end;
        then not c in X by TOPREAL1:def 15;
        hence thesis by XBOOLE_0:def 5;
      end;
      suppose
A56:    x`1 > a`1;
        take r = x`1-a`1;
        thus r > 0 by A56,XREAL_1:52;
        let b be set;
        assume
A57:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2 by EUCLID:71;
        dist(p,b) < r by A57,METRIC_1:12;
        then
A58:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume c`1 <= a`1;
          then
A59:      x`1-c`1 >= x`1-a`1 by XREAL_1:15;
          0 <= x`1-a`1 by A56,XREAL_1:52;
          then
A60:      (x`1-a`1)^2 <= (x`1-c`1)^2 by A59,SQUARE_1:77;
A61:      0 <= (x`2-c`2)^2 by XREAL_1:65;
A62:      sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < x`1-a`1 by A58,TOPREAL6:101;
A63:      0 <= (x`1-c`1)^2 by XREAL_1:65;
          then 0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by A61,SQUARE_1:def 4;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-a`1)^2 by A62,
SQUARE_1:78;
          then
A64:      (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-a`1)^2 by A61,A63,SQUARE_1:def 4;
          0 + (x`1-c`1)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A61,XREAL_1:9;
          hence contradiction by A64,A60,XXREAL_0:2;
        end;
        then not c in X by TOPREAL1:def 15;
        hence thesis by XBOOLE_0:def 5;
      end;
    end;
    then OO is open by TOPMETR:22;
    then XX is open by Lm3,PRE_TOPC:60;
    then XX` is closed;
    hence thesis;
  end;
end;

theorem Th12:
  a in BDD P implies not north_halfline a c= UBD P
proof
A1: BDD P misses UBD P & a in north_halfline a by JORDAN2C:28,TOPREAL1:45;
  assume a in BDD P;
  hence thesis by A1,XBOOLE_0:3;
end;

theorem Th13:
  a in BDD P implies not south_halfline a c= UBD P
proof
A1: BDD P misses UBD P & a in south_halfline a by JORDAN2C:28,TOPREAL1:45;
  assume a in BDD P;
  hence thesis by A1,XBOOLE_0:3;
end;

theorem
  a in BDD P implies not east_halfline a c= UBD P
proof
A1: BDD P misses UBD P & a in east_halfline a by JORDAN2C:28,TOPREAL1:45;
  assume a in BDD P;
  hence thesis by A1,XBOOLE_0:3;
end;

theorem
  a in BDD P implies not west_halfline a c= UBD P
proof
A1: BDD P misses UBD P & a in west_halfline a by JORDAN2C:28,TOPREAL1:45;
  assume a in BDD P;
  hence thesis by A1,XBOOLE_0:3;
end;

canceled 2;

theorem Th18:
  for C being Simple_closed_curve, P being Subset of TOP-REAL 2,
p1,p2 being Point of TOP-REAL 2 st P is_an_arc_of p1,p2 & P c= C ex R being non
empty Subset of TOP-REAL 2 st R is_an_arc_of p1,p2 & P \/ R = C & P /\ R = {p1,
  p2}
proof
  let C be Simple_closed_curve, P be Subset of TOP-REAL 2, p1,p2 be Point of
  TOP-REAL 2 such that
A1: P is_an_arc_of p1,p2 and
A2: P c= C;
A3: p1 <> p2 by A1,JORDAN6:49;
  p1 in P & p2 in P by A1,TOPREAL1:4;
  then consider P1,P2 being non empty Subset of TOP-REAL 2 such that
A4: P1 is_an_arc_of p1,p2 and
A5: P2 is_an_arc_of p1,p2 and
A6: C = P1 \/ P2 and
A7: P1 /\ P2 = {p1,p2} by A2,A3,TOPREAL2:5;
  reconsider P1,P2 as non empty Subset of TOP-REAL 2;
A8: P1 c= C & P2 c= C by A6,XBOOLE_1:7;
A9: now
    assume
A10: P1 = P2;
    ex p3 being Point of TOP-REAL 2 st p3 in P1 & p3 <> p1 & p3 <> p2 by A4,
JORDAN6:55;
    hence contradiction by A7,A10,TARSKI:def 2;
  end;
  per cases by A1,A2,A4,A5,A8,A9,JORDAN16:18;
  suppose
A11: P = P1;
    take P2;
    thus thesis by A5,A6,A7,A11;
  end;
  suppose
A12: P = P2;
    take P1;
    thus thesis by A4,A6,A7,A12;
  end;
end;

begin  :: Two Special Points on a Simple Closed Curve

definition
  let p,P;
  func North-Bound(p,P) -> Point of TOP-REAL 2 equals
  |[p`1,lower_bound(proj2.:(P /\
  north_halfline p))]|;
  correctness;
  func South-Bound(p,P) -> Point of TOP-REAL 2 equals
  |[p`1,upper_bound(proj2.:(P /\
  south_halfline p))]|;
  correctness;
end;

canceled;

theorem
  North-Bound(p,P)`1 = p`1 & South-Bound(p,P)`1 = p`1 by EUCLID:56;

canceled;

theorem Th22:
  for C being compact Subset of TOP-REAL 2 holds p in BDD C
  implies North-Bound(p,C) in C & North-Bound(p,C) in north_halfline p &
  South-Bound(p,C) in C & South-Bound(p,C) in south_halfline p
proof
  let C be compact Subset of TOP-REAL 2;
A1: C is Bounded by JORDAN2C:73;
  set X = C /\ north_halfline p;
  X c= C by XBOOLE_1:17;
  then proj2.:X is bounded by A1,JCT_MISC:23,JORDAN2C:16;
  then
A2: proj2.:X is bounded_below by XXREAL_2:def 11;
  assume
A3: p in BDD C;
  then not north_halfline p c= UBD C by Th12;
  then north_halfline p meets C by JORDAN2C:137;
  then
A4: X is non empty by XBOOLE_0:def 7;
  X is Bounded by A1,JORDAN2C:16,XBOOLE_1:17;
  then proj2.:X is closed by JCT_MISC:22;
  then lower_bound (proj2.:X) in proj2.:X by A4,A2,Lm2,RCOMP_1:31,RELAT_1:152;
  then consider x being set such that
A5: x in the carrier of TOP-REAL 2 and
A6: x in X and
A7: lower_bound (proj2.:X) = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A5;
A8: x`2 = lower_bound (proj2.:X) by A7,PSCOMP_1:def 29
    .= North-Bound(p,C)`2 by EUCLID:56;
  x in north_halfline p by A6,XBOOLE_0:def 4;
  then x`1 = p`1 by TOPREAL1:def 12
    .= North-Bound(p,C)`1 by EUCLID:56;
  then x = North-Bound(p,C) by A8,TOPREAL3:11;
  hence North-Bound(p,C) in C & North-Bound(p,C) in north_halfline p by A6,
XBOOLE_0:def 4;
  set X = C /\ south_halfline p;
  X c= C by XBOOLE_1:17;
  then proj2.:X is bounded by A1,JCT_MISC:23,JORDAN2C:16;
  then
A9: proj2.:X is bounded_above by XXREAL_2:def 11;
  not south_halfline p c= UBD C by A3,Th13;
  then south_halfline p meets C by JORDAN2C:136;
  then
A10: X is non empty by XBOOLE_0:def 7;
  X is Bounded by A1,JORDAN2C:16,XBOOLE_1:17;
  then proj2.:X is closed by JCT_MISC:22;
  then upper_bound (proj2.:X) in proj2.:X by A10,A9,Lm2,RCOMP_1:30,RELAT_1:152;
  then consider x being set such that
A11: x in the carrier of TOP-REAL 2 and
A12: x in X and
A13: upper_bound (proj2.:X) = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A11;
  x in south_halfline p by A12,XBOOLE_0:def 4;
  then
A14: x`1 = p`1 by TOPREAL1:def 14
    .= South-Bound(p,C)`1 by EUCLID:56;
  x`2 = upper_bound (proj2.:X) by A13,PSCOMP_1:def 29
    .= South-Bound(p,C)`2 by EUCLID:56;
  then x = South-Bound(p,C) by A14,TOPREAL3:11;
  hence thesis by A12,XBOOLE_0:def 4;
end;

theorem Th23:
  for C being compact Subset of TOP-REAL 2 holds p in BDD C
  implies South-Bound(p,C)`2 < p`2 & p`2 < North-Bound(p,C)`2
proof
  let C be compact Subset of TOP-REAL 2;
  assume
A1: p in BDD C;
  then South-Bound(p,C) in C & South-Bound(p,C) in south_halfline p by Th22;
  then South-Bound(p,C) in C /\ south_halfline p by XBOOLE_0:def 4;
  then
A2: proj2.:(C /\ south_halfline p) is non empty by Lm2,RELAT_1:152;
A3: BDD C misses C by JORDAN1A:15;
A4: now
A5: South-Bound(p,C)`1 = p`1 by EUCLID:56;
    assume South-Bound(p,C)`2 = p`2;
    then South-Bound(p,C) = p by A5,TOPREAL3:11;
    then p in C by A1,Th22;
    hence contradiction by A1,A3,XBOOLE_0:3;
  end;
  North-Bound(p,C) in C & North-Bound(p,C) in north_halfline p by A1,Th22;
  then C /\ north_halfline p is non empty by XBOOLE_0:def 4;
  then
A6: proj2.:(C /\ north_halfline p) is non empty by Lm2,RELAT_1:152;
  proj2.:(south_halfline p) is bounded_above & C /\ south_halfline p c=
  south_halfline p by Th5,XBOOLE_1:17;
  then
A7: upper_bound(proj2.:(C /\ south_halfline p)) <=
 upper_bound(proj2.:(south_halfline p) )
  by A2,RELAT_1:156,SEQ_4:65;
A8: now
A9: North-Bound(p,C)`1 = p`1 by EUCLID:56;
    assume North-Bound(p,C)`2 = p`2;
    then North-Bound(p,C) = p by A9,TOPREAL3:11;
    then p in C by A1,Th22;
    hence contradiction by A1,A3,XBOOLE_0:3;
  end;
  South-Bound(p,C)`2 = upper_bound(proj2.:(C /\ south_halfline p))
   & upper_bound(proj2.:(
  south_halfline p)) = p`2 by Th9,EUCLID:56;
  hence South-Bound(p,C)`2 < p`2 by A7,A4,XXREAL_0:1;
  proj2.:(north_halfline p) is bounded_below & C /\ north_halfline p c=
  north_halfline p by Th4,XBOOLE_1:17;
  then
A10: lower_bound(proj2.:(C /\ north_halfline p)) >=
 lower_bound(proj2.:(north_halfline p ))
  by A6,RELAT_1:156,SEQ_4:64;
  lower_bound(proj2.:(north_halfline p)) = p`2 &
   North-Bound(p,C)`2 = lower_bound(proj2.:
  (C /\ north_halfline p)) by Th8,EUCLID:56;
  hence thesis by A10,A8,XXREAL_0:1;
end;

theorem Th24:
  for C being compact Subset of TOP-REAL 2 holds p in BDD C
implies lower_bound(proj2.:(C /\ north_halfline p)) >
 upper_bound(proj2.:(C /\ south_halfline p
  ))
proof
  let C be compact Subset of TOP-REAL 2;
  assume p in BDD C;
  then
A1: South-Bound(p,C)`2 < p`2 & p`2 < North-Bound(p,C)`2 by Th23;
  North-Bound(p,C)`2 = lower_bound(proj2.:(C /\ north_halfline p))
   & South-Bound(p
  ,C) `2 = upper_bound(proj2.:(C /\ south_halfline p)) by EUCLID:56;
  hence thesis by A1,XXREAL_0:2;
end;

theorem
  for C being compact Subset of TOP-REAL 2 holds p in BDD C implies
  South-Bound(p,C) <> North-Bound(p,C)
proof
  let C be compact Subset of TOP-REAL 2;
  assume
A1: p in BDD C;
A2: North-Bound(p,C)`2 = lower_bound(proj2.:(C /\ north_halfline p))
 & South-Bound(p
  ,C) `2 = upper_bound(proj2.:(C /\ south_halfline p)) by EUCLID:56;
  assume not thesis;
  hence thesis by A1,A2,Th24;
end;

theorem Th26:
  for C being Subset of TOP-REAL 2 holds LSeg(North-Bound(p,C),
  South-Bound(p,C)) is vertical
proof
  let C be Subset of TOP-REAL 2;
  North-Bound(p,C)`1 = p`1 & South-Bound(p,C)`1 = p`1 by EUCLID:56;
  hence thesis by SPPOL_1:37;
end;

theorem
  for C being compact Subset of TOP-REAL 2 holds p in BDD C implies LSeg
(North-Bound(p,C),South-Bound(p,C)) /\ C = { North-Bound(p,C), South-Bound(p,C)
  }
proof
  let C be compact Subset of TOP-REAL 2;
  set L = LSeg(North-Bound(p,C),South-Bound(p,C));
  assume
A1: p in BDD C;
  then
A2: North-Bound(p,C) in C & South-Bound(p,C) in C by Th22;
  hereby
A3: North-Bound(p,C)`2 = lower_bound(proj2.:(C /\ north_halfline p))
by EUCLID:56;
A4: South-Bound(p,C)`2 = upper_bound(proj2.:(C /\ south_halfline p))
by EUCLID:56;
    let x be set;
A5: South-Bound(p,C)`1 = p`1 by EUCLID:56;
    assume
A6: x in L /\ C;
    then reconsider y = x as Point of TOP-REAL 2;
A7: x in L by A6,XBOOLE_0:def 4;
    L is vertical & South-Bound(p,C) in L by Th26,RLTOPSP1:69;
    then
A8: y`1 = p`1 by A5,A7,SPPOL_1:def 3;
A9: x in C by A6,XBOOLE_0:def 4;
A10: North-Bound(p,C)`1 = p`1 by EUCLID:56;
    now
A11:  C is Bounded by JORDAN2C:73;
      then C /\ north_halfline p is Bounded by TOPREAL6:98;
      then proj2.:(C /\ north_halfline p) is bounded by JCT_MISC:23;
      then
A12:  proj2.:(C /\ north_halfline p) is bounded_below by XXREAL_2:def 11;
      South-Bound(p,C)`2 < p`2 & p`2 < North-Bound(p,C)`2 by A1,Th23;
      then
A13:  South-Bound(p,C)`2 < North-Bound(p,C)`2 by XXREAL_0:2;
      then
A14:  South-Bound(p,C)`2 <= y`2 by A7,TOPREAL1:10;
      assume y <> North-Bound(p,C);
      then
A15:  y`2 <> North-Bound(p,C)`2 by A10,A8,TOPREAL3:11;
A16:  y`2 = proj2.y by PSCOMP_1:def 29;
      y`2 <= North-Bound(p,C)`2 by A7,A13,TOPREAL1:10;
      then
A17:  y`2 < North-Bound(p,C)`2 by A15,XXREAL_0:1;
      now
        assume y`2 > p`2;
        then y in north_halfline p by A8,TOPREAL1:def 12;
        then y in C /\ north_halfline p by A9,XBOOLE_0:def 4;
        then y`2 in proj2.:(C /\ north_halfline p) by A16,FUNCT_2:43;
        hence contradiction by A3,A17,A12,SEQ_4:def 5;
      end;
      then y in south_halfline p by A8,TOPREAL1:def 14;
      then y in C /\ south_halfline p by A9,XBOOLE_0:def 4;
      then
A18:  y`2 in proj2.:(C /\ south_halfline p) by A16,FUNCT_2:43;
      C /\ south_halfline p is Bounded by A11,TOPREAL6:98;
      then proj2.:(C /\ south_halfline p) is bounded by JCT_MISC:23;
      then proj2.:(C /\ south_halfline p) is bounded_above by XXREAL_2:def 11;
      then y`2 <= South-Bound(p,C)`2 by A4,A18,SEQ_4:def 4;
      then y`2 = South-Bound(p,C)`2 by A14,XXREAL_0:1;
      hence y = South-Bound(p,C) by A5,A8,TOPREAL3:11;
    end;
    hence x in {North-Bound(p,C),South-Bound(p,C)} by TARSKI:def 2;
  end;
  let x be set;
  assume x in {North-Bound(p,C),South-Bound(p,C)};
  then
A19: x = North-Bound(p,C) or x = South-Bound(p,C) by TARSKI:def 2;
  then x in L by RLTOPSP1:69;
  hence thesis by A19,A2,XBOOLE_0:def 4;
end;

theorem
  for C being compact Subset of TOP-REAL 2 holds p in BDD C & q in BDD C
  & p`1 <> q`1 implies North-Bound(p,C), South-Bound(q,C), North-Bound(q,C),
  South-Bound(p,C) are_mutually_different
proof
  let C be compact Subset of TOP-REAL 2;
  set np = North-Bound(p,C), sq = South-Bound(q,C), nq = North-Bound(q,C), sp
  = South-Bound(p,C);
A1: np`1 = p`1 & sp`1 = p`1 by EUCLID:56;
A2: North-Bound(q,C)`2 = lower_bound(proj2.:(C /\ north_halfline q)) &
South-Bound(q
  ,C) `2 = upper_bound(proj2.:(C /\ south_halfline q)) by EUCLID:56;
A3: North-Bound(p,C)`2 = lower_bound(proj2.:(C /\ north_halfline p)) &
South-Bound(p
  ,C) `2 = upper_bound(proj2.:(C /\ south_halfline p)) by EUCLID:56;
  assume p in BDD C & q in BDD C & p`1 <> q`1;
  hence np <> sq & np <> nq & np <> sp & sq <> nq & sq <> sp & nq <> sp by A1
,A2,A3,Th24,EUCLID:56;
end;

begin  :: An Order of Points on a Simple Closed Curve

definition
  let n,V,s1,s2,t1,t2;
  pred s1,s2, V-separate t1,t2 means
  :Def3:
  for A being Subset of TOP-REAL n
  st A is_an_arc_of s1,s2 & A c= V holds A meets {t1,t2};
end;

notation
  let n,V,s1,s2,t1,t2;
  antonym s1,s2 are_neighbours_wrt t1,t2, V for s1,s2, V-separate t1,t2;
end;

theorem
  t,t, V-separate s1,s2
proof
  assume not thesis;
  then ex A being Subset of TOP-REAL n st A is_an_arc_of t,t & A c= V & A
  misses {s1,s2} by Def3;
  hence thesis by JORDAN6:49;
end;

theorem
  s1,s2, V-separate t1,t2 implies s2,s1, V-separate t1,t2
proof
  assume
A1: for A being Subset of TOP-REAL n st A is_an_arc_of s1,s2 & A c= V
  holds A meets {t1,t2};
  let A be Subset of TOP-REAL n;
  assume A is_an_arc_of s2,s1 & A c= V;
  hence thesis by A1,JORDAN5B:14;
end;

theorem
  s1,s2, V-separate t1,t2 implies s1,s2, V-separate t2,t1
proof
  assume
A1: for A being Subset of TOP-REAL n st A is_an_arc_of s1,s2 & A c= V
  holds A meets {t1,t2};
  let A be Subset of TOP-REAL n;
  thus thesis by A1;
end;

theorem Th32:
  s,t1, V-separate s,t2
proof
  let A be Subset of TOP-REAL n such that
A1: A is_an_arc_of s,t1 and
  A c= V;
A2: s in {s,t2} by TARSKI:def 2;
  s in A by A1,TOPREAL1:4;
  hence thesis by A2,XBOOLE_0:3;
end;

theorem Th33:
  t1,s, V-separate t2,s
proof
  let A be Subset of TOP-REAL n such that
A1: A is_an_arc_of t1,s and
  A c= V;
A2: s in {s,t2} by TARSKI:def 2;
  s in A by A1,TOPREAL1:4;
  hence thesis by A2,XBOOLE_0:3;
end;

theorem Th34:
  t1,s, V-separate s,t2
proof
  let A be Subset of TOP-REAL n such that
A1: A is_an_arc_of t1,s and
  A c= V;
A2: s in {s,t2} by TARSKI:def 2;
  s in A by A1,TOPREAL1:4;
  hence thesis by A2,XBOOLE_0:3;
end;

theorem Th35:
  s,t1, V-separate t2,s
proof
  let A be Subset of TOP-REAL n such that
A1: A is_an_arc_of s,t1 and
  A c= V;
A2: s in {s,t2} by TARSKI:def 2;
  s in A by A1,TOPREAL1:4;
  hence thesis by A2,XBOOLE_0:3;
end;

theorem
  for p1,p2,q being Point of TOP-REAL 2 st q in C & p1 in C & p2 in C &
  p1 <> p2 & p1 <> q & p2 <> q holds p1,p2 are_neighbours_wrt q,q, C
proof
  let p1,p2,q be Point of TOP-REAL 2 such that
A1: q in C and
A2: p1 in C & p2 in C & p1 <> p2 and
A3: p1 <> q & p2 <> q;
  consider P1,P2 being non empty Subset of TOP-REAL 2 such that
A4: P1 is_an_arc_of p1,p2 and
A5: P2 is_an_arc_of p1,p2 and
A6: C = P1 \/ P2 and
A7: P1 /\ P2 = {p1,p2} by A2,TOPREAL2:5;
  per cases by A1,A6,XBOOLE_0:def 3;
  suppose
A8: q in P1 & not q in P2;
    take P2;
    thus P2 is_an_arc_of p1,p2 by A5;
    thus P2 c= C by A6,XBOOLE_1:7;
    thus P2 misses {q,q} by A8,ZFMISC_1:57;
  end;
  suppose
A9: q in P2 & not q in P1;
    take P1;
    thus P1 is_an_arc_of p1,p2 by A4;
    thus P1 c= C by A6,XBOOLE_1:7;
    thus P1 misses {q,q} by A9,ZFMISC_1:57;
  end;
  suppose
    q in P1 & q in P2;
    then q in P1 /\ P2 by XBOOLE_0:def 4;
    hence thesis by A3,A7,TARSKI:def 2;
  end;
end;

theorem
  p1 <> p2 & p1 in C & p2 in C implies (p1,p2, C-separate q1,q2 implies
  q1,q2, C-separate p1,p2)
proof
  assume that
A1: p1 <> p2 and
A2: p1 in C and
A3: p2 in C and
A4: p1,p2, C-separate q1,q2;
  per cases;
  suppose
    q1 = p1;
    hence thesis by Th32;
  end;
  suppose
    q2 = p2;
    hence thesis by Th33;
  end;
  suppose
    q1 = p2;
    hence thesis by Th35;
  end;
  suppose
    p1 = q2;
    hence thesis by Th34;
  end;
  suppose that
A5: q1 <> p1 & q2 <> p2 & q1 <> p2 & q2 <> p1;
    let A be Subset of TOP-REAL 2;
    assume A is_an_arc_of q1,q2 & A c= C;
    then consider B being non empty Subset of TOP-REAL 2 such that
A6: B is_an_arc_of q1,q2 and
A7: A \/ B = C and
    A /\ B = {q1,q2} by Th18;
    assume
A8: A misses {p1,p2};
    then not p2 in A by ZFMISC_1:55;
    then
A9: p2 in B by A3,A7,XBOOLE_0:def 3;
    not p1 in A by A8,ZFMISC_1:55;
    then p1 in B by A2,A7,XBOOLE_0:def 3;
    then consider P being non empty Subset of TOP-REAL 2 such that
A10: P is_an_arc_of p1,p2 and
A11: P c= B and
A12: P misses {q1,q2} by A1,A5,A6,A9,JORDAN16:38;
    B c= C by A7,XBOOLE_1:7;
    then P c= C by A11,XBOOLE_1:1;
    hence thesis by A4,A10,A12,Def3;
  end;
end;

theorem
  p1 in C & p2 in C & q1 in C & p1 <> p2 & q1 <> p1 & q1 <> p2 & q2 <>
  p1 & q2 <> p2 implies p1,p2 are_neighbours_wrt q1,q2, C or p1,q1
  are_neighbours_wrt p2,q2, C
proof
  assume that
A1: p1 in C & p2 in C and
A2: q1 in C and
A3: p1 <> p2 and
A4: q1 <> p1 and
A5: q1 <> p2 and
A6: q2 <> p1 & q2 <> p2;
  consider P,P1 being non empty Subset of TOP-REAL 2 such that
A7: P is_an_arc_of p1,p2 and
A8: P1 is_an_arc_of p1,p2 and
A9: C = P \/ P1 and
A10: P /\ P1 = {p1,p2} by A1,A3,TOPREAL2:5;
A11: P c= C by A9,XBOOLE_1:7;
  assume
A12: for A being Subset of TOP-REAL 2 st A is_an_arc_of p1,p2 & A c= C
  holds A meets {q1,q2};
  then
A13: P meets {q1,q2} by A7,A9,XBOOLE_1:7;
A14: P1 c= C by A9,XBOOLE_1:7;
  per cases by A13,ZFMISC_1:57;
  suppose that
A15: q1 in P and
A16: not q2 in P;
    now
      take A = Segment(P,p1,p2,p1,q1);
A17:  now
A18:    A = {q where q is Point of TOP-REAL 2: LE p1,q,P,p1,p2 & LE q,q1,
        P,p1,p2} by JORDAN6:29;
        assume p2 in A;
        then
        ex q being Point of TOP-REAL 2 st p2 = q & LE p1,q,P,p1,p2 & LE q
        ,q1,P,p1,p2 by A18;
        hence contradiction by A5,A7,JORDAN6:70;
      end;
      LE p1, q1, P, p1, p2 by A7,A15,JORDAN5C:10;
      hence A is_an_arc_of p1,q1 by A4,A7,JORDAN16:36;
A19:  A c= P by JORDAN16:5;
      hence A c= C by A11,XBOOLE_1:1;
      not q2 in A by A16,A19;
      hence A misses {p2,q2} by A17,ZFMISC_1:57;
    end;
    hence thesis by Def3;
  end;
  suppose that
A20: q2 in P and
A21: not q1 in P;
    now
      take A = Segment(P1,p1,p2,p1,q1);
A22:  now
A23:    A = {q where q is Point of TOP-REAL 2: LE p1,q,P1,p1,p2 & LE q,q1
        ,P1,p1,p2} by JORDAN6:29;
        assume p2 in A;
        then ex q being Point of TOP-REAL 2 st p2 = q & LE p1,q,P1,p1,p2 & LE
        q,q1,P1,p1,p2 by A23;
        hence contradiction by A5,A8,JORDAN6:70;
      end;
      q1 in P1 by A2,A9,A21,XBOOLE_0:def 3;
      then LE p1, q1, P1, p1, p2 by A8,JORDAN5C:10;
      hence A is_an_arc_of p1,q1 by A4,A8,JORDAN16:36;
A24:  A c= P1 by JORDAN16:5;
      hence A c= C by A14,XBOOLE_1:1;
      now
        assume q2 in A;
        then q2 in {p1,p2} by A10,A20,A24,XBOOLE_0:def 4;
        hence contradiction by A6,TARSKI:def 2;
      end;
      hence A misses {p2,q2} by A22,ZFMISC_1:57;
    end;
    hence thesis by Def3;
  end;
  suppose that
A25: q1 in P & q2 in P;
    P1 meets {q1,q2} by A12,A8,A9,XBOOLE_1:7;
    then q1 in P1 or q2 in P1 by ZFMISC_1:57;
    then q1 in {p1,p2} or q2 in {p1,p2} by A10,A25,XBOOLE_0:def 4;
    hence thesis by A4,A5,A6,TARSKI:def 2;
  end;
end;
