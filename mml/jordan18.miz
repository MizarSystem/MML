:: The Ordering of Points on a Curve, Part {IV}
::  by Artur Korni{\l}owicz
::
:: Received September 16, 2002
:: Copyright (c) 2002 Association of Mizar Users

environ

 vocabularies ORDINAL2, ARYTM, PRE_TOPC, EUCLID, TOPREAL1, BOOLE, TOPREAL2,
      INCPROJ, ARYTM_1, MCART_1, FUNCT_5, RELAT_1, JORDAN1A, JORDAN2C, JORDAN3,
      TOPS_2, SUBSET_1, SEQ_2, FUNCT_1, JORDAN6, COMPTS_1, LATTICES, METRIC_1,
      ABSVALUE, SQUARE_1, JORDAN18, SPPOL_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, FUNCT_1, RELSET_1, ORDINAL1,
      NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, SQUARE_1, SEQ_4, STRUCT_0,
      COMPLEX1, PRE_TOPC, TOPS_2, RCOMP_1, COMPTS_1, METRIC_1, SPPOL_1, EUCLID,
      TOPREAL1, PSCOMP_1, TOPREAL2, JORDAN5C, JORDAN2C, TOPREAL6, JORDAN6;
 constructors REAL_1, SQUARE_1, COMPLEX1, RCOMP_1, CONNSP_1, TOPS_2, COMPTS_1,
      TOPREAL1, SPPOL_1, PSCOMP_1, JORDAN5C, JORDAN6, JORDAN2C, TOPREAL6,
      VALUED_1, PARTFUN1, SEQ_1;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, SQUARE_1,
      MEMBERED, COMPTS_1, EUCLID, TOPREAL1, BORSUK_2, VALUED_1, FUNCT_2;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, ZFMISC_1, SEQ_4, SQUARE_1, EUCLID, PCOMPS_1,
      SUBSET_1, VALUED_1;
 theorems JORDAN5B, TARSKI, TOPREAL1, XBOOLE_0, EUCLID, JORDAN6, TOPS_2,
      XBOOLE_1, SEQ_4, RELAT_1, PSCOMP_1, FUNCT_2, REAL_1, TOPREAL3, JORDAN1A,
      COMPTS_1, ZFMISC_1, JORDAN5C, TOPREAL2, JORDAN2C, TOPS_1, SQUARE_1,
      RCOMP_1, TOPMETR, METRIC_1, JORDAN16, SPPOL_1, COMPLEX1, XREAL_1,
      TOPREAL6, XXREAL_0, JCT_MISC;

begin  :: Preliminaries

reserve n for Element of NAT,
  V for Subset of TOP-REAL n,
  s,s1,s2,t,t1,t2 for Point of TOP-REAL n,
  C for Simple_closed_curve,
  P for Subset of TOP-REAL 2,
  a,p,p1,p2,q,q1,q2 for Point of TOP-REAL 2;

Lm1: dom proj1 = the carrier of TOP-REAL 2 by FUNCT_2:def 1;

Lm2: dom proj2 = the carrier of TOP-REAL 2 by FUNCT_2:def 1;

theorem
  for a,b being real number holds (a-b)^2 = (b-a)^2;

canceled;

theorem
  for S,T being non empty TopStruct, f being Function of S,T,
  A being Subset of T st f is_homeomorphism & A is compact
  holds f"A is compact
proof
  let S,T be non empty TopStruct, f be Function of S,T,
  A be Subset of T such that
A1: f is_homeomorphism and
A2: A is compact;
  f" is_homeomorphism by A1,TOPS_2:70;
  then f" is continuous & rng (f") = [#]S by TOPS_2:def 5;
  then
A3: f".:A is compact by A2,COMPTS_1:24;
  rng f = [#]T & f is one-to-one by A1,TOPS_2:def 5;
  hence f"A is compact by A3,TOPS_2:68;
end;

theorem Th4:
  proj2.:(north_halfline a) is bounded_below
proof
  take a`2;
  let r be real number;
  assume r in proj2.:(north_halfline a);
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in north_halfline a and
A3: r = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  r = x`2 by A3,PSCOMP_1:def 29;
  hence a`2 <= r by A2,TOPREAL1:def 12;
end;

theorem Th5:
  proj2.:(south_halfline a) is bounded_above
proof
  take a`2;
  let r be real number;
  assume r in proj2.:(south_halfline a);
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in south_halfline a and
A3: r = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  r = x`2 by A3,PSCOMP_1:def 29;
  hence r <= a`2 by A2,TOPREAL1:def 14;
end;

theorem Th6:
  proj1.:(west_halfline a) is bounded_above
proof
  take a`1;
  let r be real number;
  assume r in proj1.:(west_halfline a);
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in west_halfline a and
A3: r = proj1.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  r = x`1 by A3,PSCOMP_1:def 28;
  hence r <= a`1 by A2,TOPREAL1:def 15;
end;

theorem Th7:
  proj1.:(east_halfline a) is bounded_below
proof
  take a`1;
  let r be real number;
  assume r in proj1.:(east_halfline a);
  then consider x being set such that
A1: x in the carrier of TOP-REAL 2 and
A2: x in east_halfline a and
A3: r = proj1.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A1;
  r = x`1 by A3,PSCOMP_1:def 28;
  hence a`1 <= r by A2,TOPREAL1:def 13;
end;

registration
  let a;
  cluster proj2.:(north_halfline a) -> non empty;
  coherence by Lm2,RELAT_1:152;
  cluster proj2.:(south_halfline a) -> non empty;
  coherence by Lm2,RELAT_1:152;
  cluster proj1.:(west_halfline a) -> non empty;
  coherence by Lm1,RELAT_1:152;
  cluster proj1.:(east_halfline a) -> non empty;
  coherence by Lm1,RELAT_1:152;
end;

theorem Th8:
  inf(proj2.:(north_halfline a)) = a`2
proof
  set X = proj2.:(north_halfline a);
A1: X is bounded_below by Th4;
A2: now
    let r be real number;
    assume r in X;
    then consider x being set such that
A3: x in the carrier of TOP-REAL 2 and
A4: x in north_halfline a and
A5: r = proj2.x by FUNCT_2:115;
    reconsider x as Point of TOP-REAL 2 by A3;
    r = x`2 by A5,PSCOMP_1:def 29;
    hence a`2 <= r by A4,TOPREAL1:def 12;
  end;
  now
    let s be real number such that
A6: 0 < s;
    reconsider r = a`2 as real number;
    take r;
    a in north_halfline a & r = proj2.a by PSCOMP_1:def 29,TOPREAL1:45;
    hence r in X by FUNCT_2:43;
    r + 0 < a`2 + s by A6,XREAL_1:10;
    hence r < a`2 + s;
  end;
  hence thesis by A1,A2,SEQ_4:def 5;
end;

theorem Th9:
  sup(proj2.:(south_halfline a)) = a`2
proof
  set X = proj2.:(south_halfline a);
A1: X is bounded_above by Th5;
A2: now
    let r be real number;
    assume r in X;
    then consider x being set such that
A3: x in the carrier of TOP-REAL 2 and
A4: x in south_halfline a and
A5: r = proj2.x by FUNCT_2:115;
    reconsider x as Point of TOP-REAL 2 by A3;
    r = x`2 by A5,PSCOMP_1:def 29;
    hence r <= a`2 by A4,TOPREAL1:def 14;
  end;
  now
    let s be real number such that
A6: 0 < s;
    reconsider r = a`2 as real number;
    take r;
    a in south_halfline a & r = proj2.a by PSCOMP_1:def 29,TOPREAL1:45;
    hence r in X by FUNCT_2:43;
    a`2 - s < r - 0 by A6,REAL_1:92;
    hence a`2 - s < r;
  end;
  hence thesis by A1,A2,SEQ_4:def 4;
end;

theorem
  sup(proj1.:(west_halfline a)) = a`1
proof
  set X = proj1.:(west_halfline a);
A1: X is bounded_above by Th6;
A2: now
    let r be real number;
    assume r in X;
    then consider x being set such that
A3: x in the carrier of TOP-REAL 2 and
A4: x in west_halfline a and
A5: r = proj1.x by FUNCT_2:115;
    reconsider x as Point of TOP-REAL 2 by A3;
    r = x`1 by A5,PSCOMP_1:def 28;
    hence r <= a`1 by A4,TOPREAL1:def 15;
  end;
  now
    let s be real number such that
A6: 0 < s;
    reconsider r = a`1 as real number;
    take r;
    a in west_halfline a & r = proj1.a by PSCOMP_1:def 28,TOPREAL1:45;
    hence r in X by FUNCT_2:43;
    a`1 - s < r - 0 by A6,REAL_1:92;
    hence a`1 - s < r;
  end;
  hence thesis by A1,A2,SEQ_4:def 4;
end;

theorem
  inf(proj1.:(east_halfline a)) = a`1
proof
  set X = proj1.:(east_halfline a);
A1: X is bounded_below by Th7;
A2: now
    let r be real number;
    assume r in X;
    then consider x being set such that
A3: x in the carrier of TOP-REAL 2 and
A4: x in east_halfline a and
A5: r = proj1.x by FUNCT_2:115;
    reconsider x as Point of TOP-REAL 2 by A3;
    r = x`1 by A5,PSCOMP_1:def 28;
    hence a`1 <= r by A4,TOPREAL1:def 13;
  end;
  now
    let s be real number such that
A6: 0 < s;
    reconsider r = a`1 as real number;
    take r;
    a in east_halfline a & r = proj1.a by PSCOMP_1:def 28,TOPREAL1:45;
    hence r in X by FUNCT_2:43;
    r + 0 < a`1 + s by A6,XREAL_1:10;
    hence r < a`1 + s;
  end;
  hence thesis by A1,A2,SEQ_4:def 5;
end;

registration
  let a;
  cluster north_halfline a -> closed;
  coherence
  proof
    set X = north_halfline a;
    for p being Point of Euclid 2 st p in X`
    ex r being real number st r>0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      assume
A1:   p in X`;
      reconsider x = p as Point of TOP-REAL 2;
A2:   not p in X by A1,XBOOLE_0:def 4;
      per cases by A2,TOPREAL1:def 12;
      suppose
A3:     x`1 <> a`1;
        take r = abs(x`1-a`1);
        x`1 - a`1 <> 0 by A3;
        hence r > 0 by COMPLEX1:133;
        let b be set;
        assume
A4:     b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2;
        dist(p,b) < r by A4,METRIC_1:12;
        then
A5:     dist(x,c) < r by TOPREAL6:def 1;
        now
          assume
A6:       c`1 = a`1;
A7:       0 <= (x`2-c`2)^2 by XREAL_1:65;
A8:       0 <= (x`1-c`1)^2 by XREAL_1:65;
          then 0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A7,XREAL_1:9;
          then
A9:       0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`1-c`1) by A5,A6,
          TOPREAL6:101;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`1-c`1))^2
          by A9,SQUARE_1:78;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-c`1)^2
          by COMPLEX1:161;

then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-c`1)^2 + 0 by A8,SQUARE_1:def 4;
          hence contradiction by A7,XREAL_1:9;
        end;
        then not c in X by TOPREAL1:def 12;
        hence thesis by XBOOLE_0:def 4;
      end;
      suppose
A10:    x`2 < a`2;
        take r = a`2-x`2;
        thus r > 0 by A10,XREAL_1:52;
        let b be set;
        assume
A11:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2;
        dist(p,b) < r by A11,METRIC_1:12;
        then
A12:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume
A13:      c`2 >= a`2;
A14:      0 <= (x`1-c`1)^2 by XREAL_1:65;
          0 <= (x`2-c`2)^2 by XREAL_1:65;
          then
A15:      0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A14,XREAL_1:9;
          then
A16:      0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < a`2-x`2 by A12,TOPREAL6:101;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (a`2-x`2)^2
          by A16,SQUARE_1:78;
          then
A17:      (x`1-c`1)^2 + (x`2-c`2)^2 < (a`2-x`2)^2 by A15, SQUARE_1:def 4;
A18:      0 <= a`2-x`2 by A10,XREAL_1:52;
          a`2-x`2 <= c`2-x`2 by A13,REAL_1:92;
          then
A19:      (a`2-x`2)^2 <= (c`2-x`2)^2 by A18,SQUARE_1:77;
          0 + (x`2-c`2)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A14,XREAL_1:9;
          hence contradiction by A17,A19,XXREAL_0:2;
        end;
        then not c in X by TOPREAL1:def 12;
        hence thesis by XBOOLE_0:def 4;
      end;
    end;
    then X` is open by TOPMETR:22;
    hence thesis by TOPS_1:29;
  end;
  cluster south_halfline a -> closed;
  coherence
  proof
    set X = south_halfline a;
    for p being Point of Euclid 2 st p in X`
    ex r being real number st r>0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      assume
A20:  p in X`;
      reconsider x = p as Point of TOP-REAL 2;
A21:  not p in X by A20,XBOOLE_0:def 4;
      per cases by A21,TOPREAL1:def 14;
      suppose
A22:    x`1 <> a`1;
        take r = abs(x`1-a`1);
        x`1 - a`1 <> 0 by A22;
        hence r > 0 by COMPLEX1:133;
        let b be set;
        assume
A23:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2;
        dist(p,b) < r by A23,METRIC_1:12;
        then
A24:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume
A25:      c`1 = a`1;
A26:      0 <= (x`2-c`2)^2 by XREAL_1:65;
A27:      0 <= (x`1-c`1)^2 by XREAL_1:65;
          then 0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A26,XREAL_1:9;
          then
A28:      0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`1-c`1) by A24,A25,
          TOPREAL6:101;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`1-c`1))^2
          by A28,SQUARE_1:78;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-c`1)^2
          by COMPLEX1:161;

then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-c`1)^2 + 0 by A27,SQUARE_1:def 4;
          hence contradiction by A26,XREAL_1:9;
        end;
        then not c in X by TOPREAL1:def 14;
        hence thesis by XBOOLE_0:def 4;
      end;
      suppose
A29:    x`2 > a`2;
        take r = x`2-a`2;
        thus r > 0 by A29,XREAL_1:52;
        let b be set;
        assume
A30:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2;
        dist(p,b) < r by A30,METRIC_1:12;
        then
A31:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume
A32:      c`2 <= a`2;
A33:      0 <= (x`1-c`1)^2 by XREAL_1:65;
          0 <= (x`2-c`2)^2 by XREAL_1:65;
          then
A34:      0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A33,XREAL_1:9;
          then
A35:      0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < x`2-a`2 by A31,TOPREAL6:101;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-a`2)^2
          by A35,SQUARE_1:78;
          then
A36:      (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-a`2)^2 by A34, SQUARE_1:def 4;
A37:      0 <= x`2-a`2 by A29,XREAL_1:52;
          x`2-c`2 >= x`2-a`2 by A32,REAL_1:92;
          then
A38:      (x`2-a`2)^2 <= (x`2-c`2)^2 by A37,SQUARE_1:77;
          0 + (x`2-c`2)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A33,XREAL_1:9;
          hence contradiction by A36,A38,XXREAL_0:2;
        end;
        then not c in X by TOPREAL1:def 14;
        hence thesis by XBOOLE_0:def 4;
      end;
    end;
    then X` is open by TOPMETR:22;
    hence thesis by TOPS_1:29;
  end;
  cluster east_halfline a -> closed;
  coherence
  proof
    set X = east_halfline a;
    for p being Point of Euclid 2 st p in X`
    ex r being real number st r>0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      assume
A39:  p in X`;
      reconsider x = p as Point of TOP-REAL 2;
A40:  not p in X by A39,XBOOLE_0:def 4;
      per cases by A40,TOPREAL1:def 13;
      suppose
A41:    x`2 <> a`2;
        take r = abs(x`2-a`2);
        x`2 - a`2 <> 0 by A41;
        hence r > 0 by COMPLEX1:133;
        let b be set;
        assume
A42:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2;
        dist(p,b) < r by A42,METRIC_1:12;
        then
A43:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume
A44:      c`2 = a`2;
A45:      0 <= (x`1-c`1)^2 by XREAL_1:65;
A46:      0 <= (x`2-c`2)^2 by XREAL_1:65;
          then 0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A45,XREAL_1:9;
          then
A47:      0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`2-c`2) by A43,A44,
          TOPREAL6:101;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`2-c`2))^2
          by A47,SQUARE_1:78;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-c`2)^2
          by COMPLEX1:161;

then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-c`2)^2 + 0 by A46,SQUARE_1:def 4;
          hence contradiction by A45,XREAL_1:9;
        end;
        then not c in X by TOPREAL1:def 13;
        hence thesis by XBOOLE_0:def 4;
      end;
      suppose
A48:    x`1 < a`1;
        take r = a`1-x`1;
        thus r > 0 by A48,XREAL_1:52;
        let b be set;
        assume
A49:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2;
        dist(p,b) < r by A49,METRIC_1:12;
        then
A50:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume
A51:      c`1 >= a`1;
A52:      0 <= (x`2-c`2)^2 by XREAL_1:65;
          0 <= (x`1-c`1)^2 by XREAL_1:65;
          then
A53:      0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A52,XREAL_1:9;
          then
A54:      0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < a`1-x`1 by A50,TOPREAL6:101;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (a`1-x`1)^2
          by A54,SQUARE_1:78;
          then
A55:      (x`1-c`1)^2 + (x`2-c`2)^2 < (a`1-x`1)^2 by A53, SQUARE_1:def 4;
A56:      0 <= a`1-x`1 by A48,XREAL_1:52;
          a`1-x`1 <= c`1-x`1 by A51,REAL_1:92;
          then
A57:      (a`1-x`1)^2 <= (c`1-x`1)^2 by A56,SQUARE_1:77;
          (x`1-c`1)^2 + 0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A52,XREAL_1:9;
          hence contradiction by A55,A57,XXREAL_0:2;
        end;
        then not c in X by TOPREAL1:def 13;
        hence thesis by XBOOLE_0:def 4;
      end;
    end;
    then X` is open by TOPMETR:22;
    hence thesis by TOPS_1:29;
  end;
  cluster west_halfline a -> closed;
  coherence
  proof
    set X = west_halfline a;
    for p being Point of Euclid 2 st p in X`
    ex r being real number st r>0 & Ball(p,r) c= X`
    proof
      let p be Point of Euclid 2;
      assume
A58:  p in X`;
      reconsider x = p as Point of TOP-REAL 2;
A59:  not p in X by A58,XBOOLE_0:def 4;
      per cases by A59,TOPREAL1:def 15;
      suppose
A60:    x`2 <> a`2;
        take r = abs(x`2-a`2);
        x`2 - a`2 <> 0 by A60;
        hence r > 0 by COMPLEX1:133;
        let b be set;
        assume
A61:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2;
        dist(p,b) < r by A61,METRIC_1:12;
        then
A62:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume
A63:      c`2 = a`2;
A64:      0 <= (x`1-c`1)^2 by XREAL_1:65;
A65:      0 <= (x`2-c`2)^2 by XREAL_1:65;
          then 0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A64,XREAL_1:9;
          then
A66:      0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < abs(x`2-c`2) by A62,A63,
          TOPREAL6:101;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (abs(x`2-c`2))^2
          by A66,SQUARE_1:78;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`2-c`2)^2
          by COMPLEX1:161;

then (x`1-c`1)^2 + (x`2-c`2)^2 < (x`2-c`2)^2 + 0 by A65,SQUARE_1:def 4;
          hence contradiction by A64,XREAL_1:9;
        end;
        then not c in X by TOPREAL1:def 15;
        hence thesis by XBOOLE_0:def 4;
      end;
      suppose
A67:    x`1 > a`1;
        take r = x`1-a`1;
        thus r > 0 by A67,XREAL_1:52;
        let b be set;
        assume
A68:    b in Ball(p,r);
        then reconsider b as Point of Euclid 2;
        reconsider c = b as Point of TOP-REAL 2;
        dist(p,b) < r by A68,METRIC_1:12;
        then
A69:    dist(x,c) < r by TOPREAL6:def 1;
        now
          assume
A70:      c`1 <= a`1;
A71:      0 <= (x`2-c`2)^2 by XREAL_1:65;
          0 <= (x`1-c`1)^2 by XREAL_1:65;
          then
A72:      0+0 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A71,XREAL_1:9;
          then
A73:      0 <= sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) by SQUARE_1:def 4;
          sqrt ((x`1-c`1)^2 + (x`2-c`2)^2) < x`1-a`1 by A69,TOPREAL6:101;
          then (sqrt ((x`1-c`1)^2 + (x`2-c`2)^2))^2 < (x`1-a`1)^2
          by A73,SQUARE_1:78;
          then
A74:      (x`1-c`1)^2 + (x`2-c`2)^2 < (x`1-a`1)^2 by A72, SQUARE_1:def 4;
A75:      0 <= x`1-a`1 by A67,XREAL_1:52;
          x`1-c`1 >= x`1-a`1 by A70,REAL_1:92;
          then
A76:      (x`1-a`1)^2 <= (x`1-c`1)^2 by A75,SQUARE_1:77;
          0 + (x`1-c`1)^2 <= (x`1-c`1)^2 + (x`2-c`2)^2 by A71,XREAL_1:9;
          hence contradiction by A74,A76,XXREAL_0:2;
        end;
        then not c in X by TOPREAL1:def 15;
        hence thesis by XBOOLE_0:def 4;
      end;
    end;
    then X` is open by TOPMETR:22;
    hence thesis by TOPS_1:29;
  end;
end;

theorem Th12:
  a in BDD P implies not north_halfline a c= UBD P
proof
  assume
A1: a in BDD P;
A2: BDD P misses UBD P by JORDAN2C:28;
  a in north_halfline a by TOPREAL1:45;
  hence not north_halfline a c= UBD P by A1,A2,XBOOLE_0:3;
end;

theorem Th13:
  a in BDD P implies not south_halfline a c= UBD P
proof
  assume
A1: a in BDD P;
A2: BDD P misses UBD P by JORDAN2C:28;
  a in south_halfline a by TOPREAL1:45;
  hence not south_halfline a c= UBD P by A1,A2,XBOOLE_0:3;
end;

theorem
  a in BDD P implies not east_halfline a c= UBD P
proof
  assume
A1: a in BDD P;
A2: BDD P misses UBD P by JORDAN2C:28;
  a in east_halfline a by TOPREAL1:45;
  hence not east_halfline a c= UBD P by A1,A2,XBOOLE_0:3;
end;

theorem
  a in BDD P implies not west_halfline a c= UBD P
proof
  assume
A1: a in BDD P;
A2: BDD P misses UBD P by JORDAN2C:28;
  a in west_halfline a by TOPREAL1:45;
  hence not west_halfline a c= UBD P by A1,A2,XBOOLE_0:3;
end;

canceled 2;

theorem Th18:
  for C being Simple_closed_curve, P being Subset of TOP-REAL 2,
  p1,p2 being Point of TOP-REAL 2 st P is_an_arc_of p1,p2 & P c= C
  ex R being non empty Subset of TOP-REAL 2 st
  R is_an_arc_of p1,p2 & P \/ R = C & P /\ R = {p1,p2}
proof
  let C be Simple_closed_curve, P be Subset of TOP-REAL 2,
  p1,p2 be Point of TOP-REAL 2 such that
A1: P is_an_arc_of p1,p2 and
A2: P c= C;
  p1 in P & p2 in P by A1,TOPREAL1:4;
  then p1 <> p2 & p1 in C & p2 in C by A1,A2,JORDAN6:49;
  then consider P1,P2 being non empty Subset of TOP-REAL 2 such that
A3: P1 is_an_arc_of p1,p2 and
A4: P2 is_an_arc_of p1,p2 and
A5: C = P1 \/ P2 and
A6: P1 /\ P2 = {p1,p2} by TOPREAL2:5;
  reconsider P1,P2 as non empty Subset of TOP-REAL 2;
A7: P1 c= C & P2 c= C by A5,XBOOLE_1:7;
A8: now
    assume
A9: P1 = P2;
    ex p3 being Point of TOP-REAL 2 st p3 in P1 & p3 <> p1 & p3 <> p2
    by A3,JORDAN6:55;
    hence contradiction by A6,A9,TARSKI:def 2;
  end;
  per cases by A1,A2,A3,A4,A7,A8,JORDAN16:18;
  suppose
A10: P = P1;
    take P2;
    thus P2 is_an_arc_of p1,p2 & P \/ P2 = C & P /\ P2 = {p1,p2}
    by A4,A5,A6,A10;
  end;
  suppose
A11: P = P2;
    take P1;
    thus P1 is_an_arc_of p1,p2 & P \/ P1 = C & P /\ P1 = {p1,p2}
    by A3,A5,A6,A11;
  end;
end;

begin  :: Two Special Points on a Simple Closed Curve

definition
  let p,P;
  func North-Bound(p,P) -> Point of TOP-REAL 2 equals
  |[p`1,inf(proj2.:(P /\ north_halfline p))]|;
  correctness;
  func South-Bound(p,P) -> Point of TOP-REAL 2 equals
  |[p`1,sup(proj2.:(P /\ south_halfline p))]|;
  correctness;
end;

canceled;

theorem
  North-Bound(p,P)`1 = p`1 & South-Bound(p,P)`1 = p`1 by EUCLID:56;

canceled;

theorem Th22:
  for C being compact Subset of TOP-REAL 2 holds p in BDD C implies
  North-Bound(p,C) in C & North-Bound(p,C) in north_halfline p &
  South-Bound(p,C) in C & South-Bound(p,C) in south_halfline p
proof
  let C be compact Subset of TOP-REAL 2;
  assume
A1: p in BDD C;
A2: C is Bounded by JORDAN2C:73;
  set X = C /\ north_halfline p;
  not north_halfline p c= UBD C by A1,Th12;
  then north_halfline p meets C by JORDAN2C:137;
  then
A3: X is non empty by XBOOLE_0:def 7;
A4: X c= C by XBOOLE_1:17;
A5: X is Bounded by A2,JORDAN2C:16,XBOOLE_1:17;
  X is closed by TOPS_1:35;
  then
A6: proj2.:X is closed by A5,JCT_MISC:22;
  proj2.:X is bounded by A2,A4,JCT_MISC:23,JORDAN2C:16;
  then proj2.:X is bounded_below by SEQ_4:def 3;
  then inf (proj2.:X) in proj2.:X by A3,A6,Lm2,RCOMP_1:31,RELAT_1:152;
  then consider x being set such that
A7: x in the carrier of TOP-REAL 2 and
A8: x in X and
A9: inf (proj2.:X) = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A7;
  x in north_halfline p by A8,XBOOLE_0:def 3;
  then
A10: x`1 = p`1 by TOPREAL1:def 12
    .= North-Bound(p,C)`1 by EUCLID:56;
  x`2 = inf (proj2.:X) by A9,PSCOMP_1:def 29
    .= North-Bound(p,C)`2 by EUCLID:56;
  then x = North-Bound(p,C) by A10,TOPREAL3:11;
  hence North-Bound(p,C) in C &
  North-Bound(p,C) in north_halfline p by A8,XBOOLE_0:def 3;
  set X = C /\ south_halfline p;
  not south_halfline p c= UBD C by A1,Th13;
  then south_halfline p meets C by JORDAN2C:136;
  then
A11: X is non empty by XBOOLE_0:def 7;
A12: X c= C by XBOOLE_1:17;
A13: X is Bounded by A2,JORDAN2C:16,XBOOLE_1:17;
  X is closed by TOPS_1:35;
  then
A14: proj2.:X is closed by A13,JCT_MISC:22;
  proj2.:X is bounded by A2,A12,JCT_MISC:23,JORDAN2C:16;
  then proj2.:X is bounded_above by SEQ_4:def 3;
  then sup (proj2.:X) in proj2.:X by A11,A14,Lm2,RCOMP_1:30,RELAT_1:152;
  then consider x being set such that
A15: x in the carrier of TOP-REAL 2 and
A16: x in X and
A17: sup (proj2.:X) = proj2.x by FUNCT_2:115;
  reconsider x as Point of TOP-REAL 2 by A15;
  x in south_halfline p by A16,XBOOLE_0:def 3;
  then
A18: x`1 = p`1 by TOPREAL1:def 14
    .= South-Bound(p,C)`1 by EUCLID:56;
  x`2 = sup (proj2.:X) by A17,PSCOMP_1:def 29
    .= South-Bound(p,C)`2 by EUCLID:56;
  then x = South-Bound(p,C) by A18,TOPREAL3:11;
  hence South-Bound(p,C) in C &
  South-Bound(p,C) in south_halfline p by A16,XBOOLE_0:def 3;
end;

theorem Th23:
  for C being compact Subset of TOP-REAL 2 holds
  p in BDD C implies South-Bound(p,C)`2 < p`2 & p`2 < North-Bound(p,C)`2
proof
  let C be compact Subset of TOP-REAL 2;
  assume
A1: p in BDD C;
A2: South-Bound(p,C)`2 = sup(proj2.:(C /\ south_halfline p)) by EUCLID:56;
A3: South-Bound(p,C) in C by A1,Th22;
  South-Bound(p,C) in south_halfline p by A1,Th22;
  then South-Bound(p,C) in C /\ south_halfline p by A3,XBOOLE_0:def 3;
  then
A4: proj2.:(C /\ south_halfline p) is non empty by Lm2,RELAT_1:152;
A5: proj2.:(south_halfline p) is bounded_above by Th5;
  C /\ south_halfline p c= south_halfline p by XBOOLE_1:17;
  then
A6: sup(proj2.:(C /\ south_halfline p)) <= sup(proj2.:(south_halfline p) )
  by A4,A5,RELAT_1:156,SEQ_4:65;
A7: sup(proj2.:(south_halfline p)) = p`2 by Th9;
A8: BDD C misses C by JORDAN1A:15;
  now
    assume
A9: South-Bound(p,C)`2 = p`2;
    South-Bound(p,C)`1 = p`1 by EUCLID:56;
    then South-Bound(p,C) = p by A9,TOPREAL3:11;
    then p in C by A1,Th22;
    hence contradiction by A1,A8,XBOOLE_0:3;
  end;
  hence South-Bound(p,C)`2 < p`2 by A2,A6,A7,REAL_1:def 5;
  North-Bound(p,C) in C & North-Bound(p,C) in north_halfline p by A1, Th22;
  then C /\ north_halfline p is non empty by XBOOLE_0:def 3;
  then
A10: proj2.:(C /\ north_halfline p) is non empty by Lm2,RELAT_1:152;
A11: proj2.:(north_halfline p) is bounded_below by Th4;
  C /\ north_halfline p c= north_halfline p by XBOOLE_1:17;
  then
A12: inf(proj2.:(C /\ north_halfline p)) >= inf(proj2.:(north_halfline p ))
  by A10,A11,RELAT_1:156,SEQ_4:64;
A13: inf(proj2.:(north_halfline p)) = p`2 by Th8;
A14: North-Bound(p,C)`2 = inf(proj2.:(C /\ north_halfline p)) by EUCLID:56;
  now
    assume
A15: North-Bound(p,C)`2 = p`2;
    North-Bound(p,C)`1 = p`1 by EUCLID:56;
    then North-Bound(p,C) = p by A15,TOPREAL3:11;
    then p in C by A1,Th22;
    hence contradiction by A1,A8,XBOOLE_0:3;
  end;
  hence p`2 < North-Bound(p,C)`2 by A12,A13,A14,REAL_1:def 5;
end;

theorem Th24:
  for C being compact Subset of TOP-REAL 2 holds p in BDD C implies
  inf(proj2.:(C /\ north_halfline p)) > sup(proj2.:(C /\ south_halfline p))
proof
  let C be compact Subset of TOP-REAL 2;
  assume p in BDD C;
  then
A1: South-Bound(p,C)`2 < p`2 & p`2 < North-Bound(p,C)`2 by Th23;
  North-Bound(p,C)`2 = inf(proj2.:(C /\ north_halfline p)) &
  South-Bound(p,C)`2 = sup(proj2.:(C /\ south_halfline p)) by EUCLID:56;
  hence thesis by A1,XXREAL_0:2;
end;

theorem
  for C being compact Subset of TOP-REAL 2 holds
  p in BDD C implies South-Bound(p,C) <> North-Bound(p,C)
proof
  let C be compact Subset of TOP-REAL 2;
  assume that
A1: p in BDD C;
  assume
A2: not thesis;
  North-Bound(p,C)`2 = inf(proj2.:(C /\ north_halfline p)) &
  South-Bound(p,C)`2 = sup(proj2.:(C /\ south_halfline p)) by EUCLID:56;
  hence thesis by A1,A2,Th24;
end;

theorem Th26:
  for C being Subset of TOP-REAL 2 holds
  LSeg(North-Bound(p,C),South-Bound(p,C)) is vertical
proof
  let C be Subset of TOP-REAL 2;
  North-Bound(p,C)`1 = p`1 & South-Bound(p,C)`1 = p`1 by EUCLID:56;
  hence thesis by SPPOL_1:37;
end;

theorem
  for C being compact Subset of TOP-REAL 2 holds p in BDD C implies
  LSeg(North-Bound(p,C),South-Bound(p,C)) /\ C
  = { North-Bound(p,C), South-Bound(p,C) }
proof
  let C be compact Subset of TOP-REAL 2;
  assume
A1: p in BDD C;
  set L = LSeg(North-Bound(p,C),South-Bound(p,C));
  hereby
    let x be set;
    assume
A2: x in L /\ C;
    then reconsider y = x as Point of TOP-REAL 2;
A3: South-Bound(p,C)`1 = p`1 by EUCLID:56;
A4: North-Bound(p,C)`1 = p`1 by EUCLID:56;
A5: South-Bound(p,C)`2 = sup(proj2.:(C /\ south_halfline p)) by EUCLID:56;
A6: North-Bound(p,C)`2 = inf(proj2.:(C /\ north_halfline p)) by EUCLID:56;
A7: L is vertical by Th26;
A8: South-Bound(p,C) in L by TOPREAL1:6;
A9: x in C by A2,XBOOLE_0:def 3;
A10: x in L by A2,XBOOLE_0:def 3;
    then
A11: y`1 = p`1 by A3,A7,A8,SPPOL_1:def 3;
    now
      assume y <> North-Bound(p,C);
      then
A12:  y`2 <> North-Bound(p,C)`2 by A4,A11,TOPREAL3:11;
      South-Bound(p,C)`2 < p`2 & p`2 < North-Bound(p,C)`2 by A1,Th23;
      then South-Bound(p,C)`2 < North-Bound(p,C)`2 by XXREAL_0:2;
      then
A13:  South-Bound(p,C)`2 <= y`2 & y`2 <= North-Bound(p,C)`2
      by A10,TOPREAL1:10;
      then
A14:  y`2 < North-Bound(p,C)`2 by A12,REAL_1:def 5;
A15:  C is Bounded by JORDAN2C:73;
      then C /\ south_halfline p is Bounded by TOPREAL6:98;
      then proj2.:(C /\ south_halfline p) is bounded by JCT_MISC:23;
      then
A16:  proj2.:(C /\ south_halfline p) is bounded_above by SEQ_4:def 3;
      C /\ north_halfline p is Bounded by A15,TOPREAL6:98;
      then proj2.:(C /\ north_halfline p) is bounded by JCT_MISC:23;
      then
A17:  proj2.:(C /\ north_halfline p) is bounded_below by SEQ_4:def 3;
A18:  y`2 = proj2.y by PSCOMP_1:def 29;
      now
        assume y`2 > p`2;
        then y in north_halfline p by A11,TOPREAL1:def 12;
        then y in C /\ north_halfline p by A9,XBOOLE_0:def 3;
        then y`2 in proj2.:(C /\ north_halfline p) by A18,FUNCT_2:43;
        hence contradiction by A6,A14,A17,SEQ_4:def 5;
      end;
      then y in south_halfline p by A11,TOPREAL1:def 14;
      then y in C /\ south_halfline p by A9,XBOOLE_0:def 3;
      then y`2 in proj2.:(C /\ south_halfline p) by A18,FUNCT_2:43;
      then y`2 <= South-Bound(p,C)`2 by A5,A16,SEQ_4:def 4;
      then y`2 = South-Bound(p,C)`2 by A13,XXREAL_0:1;
      hence y = South-Bound(p,C) by A3,A11,TOPREAL3:11;
    end;
    hence x in {North-Bound(p,C),South-Bound(p,C)} by TARSKI:def 2;
  end;
  let x be set;
  assume x in {North-Bound(p,C),South-Bound(p,C)};
  then
A19: x = North-Bound(p,C) or x = South-Bound(p,C) by TARSKI:def 2;
A20: North-Bound(p,C) in C & South-Bound(p,C) in C by A1,Th22;
  x in L by A19,TOPREAL1:6;
  hence thesis by A19,A20,XBOOLE_0:def 3;
end;

theorem
  for C being compact Subset of TOP-REAL 2 holds
  p in BDD C & q in BDD C & p`1 <> q`1 implies
  North-Bound(p,C), South-Bound(q,C), North-Bound(q,C), South-Bound(p,C)
  are_mutually_different
proof
  let C be compact Subset of TOP-REAL 2;
  assume that
A1: p in BDD C and
A2: q in BDD C and
A3: p`1 <> q`1;
  set np = North-Bound(p,C), sq = South-Bound(q,C), nq = North-Bound(q,C),
  sp = South-Bound(p,C);
A4: np`1 = p`1 & nq`1 = q`1 & sp`1 = p`1 & sq`1 = q`1 by EUCLID:56;
  North-Bound(q,C)`2 = inf(proj2.:(C /\ north_halfline q)) &
  South-Bound(q,C)`2 = sup(proj2.:(C /\ south_halfline q)) &
  North-Bound(p,C)`2 = inf(proj2.:(C /\ north_halfline p)) &
  South-Bound(p,C)`2 = sup(proj2.:(C /\ south_halfline p)) by EUCLID:56;
  hence np <> sq & np <> nq & np <> sp & sq <> nq & sq <> sp & nq <> sp
  by A1,A2,A3,A4,Th24;
end;

begin  :: An Order of Points on a Simple Closed Curve

definition
  let n,V,s1,s2,t1,t2;
  pred s1,s2, V-separate t1,t2 means
  :Def3:
  for A being Subset of TOP-REAL n st A is_an_arc_of s1,s2 & A c= V
  holds A meets {t1,t2};
end;

notation
  let n,V,s1,s2,t1,t2;
  antonym s1,s2 are_neighbours_wrt t1,t2, V for s1,s2, V-separate t1,t2;
end;

theorem
  t,t, V-separate s1,s2
proof
  assume not thesis;
  then ex A being Subset of TOP-REAL n st
  A is_an_arc_of t,t & A c= V & A misses {s1,s2} by Def3;
  hence thesis by JORDAN6:49;
end;

theorem
  s1,s2, V-separate t1,t2 implies s2,s1, V-separate t1,t2
proof
  assume
A1: for A being Subset of TOP-REAL n st
  A is_an_arc_of s1,s2 & A c= V holds A meets {t1,t2};
  let A be Subset of TOP-REAL n such that
A2: A is_an_arc_of s2,s1 and
A3: A c= V;
  thus A meets {t1,t2} by A1,A2,A3,JORDAN5B:14;
end;

theorem
  s1,s2, V-separate t1,t2 implies s1,s2, V-separate t2,t1
proof
  assume
A1: for A being Subset of TOP-REAL n st
  A is_an_arc_of s1,s2 & A c= V holds A meets {t1,t2};
  let A be Subset of TOP-REAL n;
  thus thesis by A1;
end;

theorem Th32:
  s,t1, V-separate s,t2
proof
  let A be Subset of TOP-REAL n such that
A1: A is_an_arc_of s,t1 and A c= V;
  s in A & s in {s,t2} by A1,TARSKI:def 2,TOPREAL1:4;
  hence A meets {s,t2} by XBOOLE_0:3;
end;

theorem Th33:
  t1,s, V-separate t2,s
proof
  let A be Subset of TOP-REAL n such that
A1: A is_an_arc_of t1,s and A c= V;
  s in A & s in {s,t2} by A1,TARSKI:def 2,TOPREAL1:4;
  hence A meets {t2,s} by XBOOLE_0:3;
end;

theorem Th34:
  t1,s, V-separate s,t2
proof
  let A be Subset of TOP-REAL n such that
A1: A is_an_arc_of t1,s and A c= V;
  s in A & s in {s,t2} by A1,TARSKI:def 2,TOPREAL1:4;
  hence A meets {s,t2} by XBOOLE_0:3;
end;

theorem Th35:
  s,t1, V-separate t2,s
proof
  let A be Subset of TOP-REAL n such that
A1: A is_an_arc_of s,t1 and A c= V;
  s in A & s in {s,t2} by A1,TARSKI:def 2,TOPREAL1:4;
  hence A meets {t2,s} by XBOOLE_0:3;
end;

theorem
  for p1,p2,q being Point of TOP-REAL 2 st
  q in C & p1 in C & p2 in C & p1 <> p2 & p1 <> q & p2 <> q holds
  p1,p2 are_neighbours_wrt q,q, C
proof
  let p1,p2,q be Point of TOP-REAL 2 such that
A1: q in C and
A2: p1 in C & p2 in C & p1 <> p2 and
A3: p1 <> q & p2 <> q;
  consider P1,P2 being non empty Subset of TOP-REAL 2 such that
A4: P1 is_an_arc_of p1,p2 and
A5: P2 is_an_arc_of p1,p2 and
A6: C = P1 \/ P2 and
A7: P1 /\ P2 = {p1,p2} by A2,TOPREAL2:5;
  per cases by A1,A6,XBOOLE_0:def 2;
  suppose
A8: q in P1 & not q in P2;
    take P2;
    thus P2 is_an_arc_of p1,p2 by A5;
    thus P2 c= C by A6,XBOOLE_1:7;
    thus P2 misses {q,q} by A8,ZFMISC_1:57;
  end;
  suppose
A9: q in P2 & not q in P1;
    take P1;
    thus P1 is_an_arc_of p1,p2 by A4;
    thus P1 c= C by A6,XBOOLE_1:7;
    thus P1 misses {q,q} by A9,ZFMISC_1:57;
  end;
  suppose q in P1 & q in P2;
    then q in P1 /\ P2 by XBOOLE_0:def 3;
    hence thesis by A3,A7,TARSKI:def 2;
  end;
end;

theorem
  p1 <> p2 & p1 in C & p2 in C implies
  (p1,p2, C-separate q1,q2 implies q1,q2, C-separate p1,p2)
proof
  assume that
A1: p1 <> p2 and
A2: p1 in C and
A3: p2 in C and
A4: p1,p2, C-separate q1,q2;
  per cases;
  suppose q1 = p1;
    hence q1,q2, C-separate p1,p2 by Th32;
  end;
  suppose q2 = p2;
    hence q1,q2, C-separate p1,p2 by Th33;
  end;
  suppose q1 = p2;
    hence q1,q2, C-separate p1,p2 by Th35;
  end;
  suppose p1 = q2;
    hence q1,q2, C-separate p1,p2 by Th34;
  end;
  suppose that
A5: q1 <> p1 and
A6: q2 <> p2 and
A7: q1 <> p2 and
A8: q2 <> p1;
    let A be Subset of TOP-REAL 2 such that
A9: A is_an_arc_of q1,q2 and
A10: A c= C;
    consider B being non empty Subset of TOP-REAL 2 such that
A11: B is_an_arc_of q1,q2 and
A12: A \/ B = C and A /\ B = {q1,q2} by A9,A10,Th18;
    assume A misses {p1,p2};
    then not p1 in A & not p2 in A by ZFMISC_1:55;
    then p1 in B & p2 in B by A2,A3,A12,XBOOLE_0:def 2;
    then consider P being non empty Subset of TOP-REAL 2 such that
A13: P is_an_arc_of p1,p2 and
A14: P c= B and
A15: P misses {q1,q2} by A1,A5,A6,A7,A8,A11,JORDAN16:38;
    B c= C by A12,XBOOLE_1:7;
    then P c= C by A14,XBOOLE_1:1;
    hence thesis by A4,A13,A15,Def3;
  end;
end;

theorem
  p1 in C & p2 in C & q1 in C &
  p1 <> p2 & q1 <> p1 & q1 <> p2 & q2 <> p1 & q2 <> p2
  implies p1,p2 are_neighbours_wrt q1,q2, C
  or p1,q1 are_neighbours_wrt p2,q2, C
proof
  assume that
A1: p1 in C and
A2: p2 in C and
A3: q1 in C and
A4: p1 <> p2 and
A5: q1 <> p1 and
A6: q1 <> p2 and
A7: q2 <> p1 and
A8: q2 <> p2;
  assume
A9: for A being Subset of TOP-REAL 2 st
  A is_an_arc_of p1,p2 & A c= C holds A meets {q1,q2};
  consider P,P1 being non empty Subset of TOP-REAL 2 such that
A10: P is_an_arc_of p1,p2 and
A11: P1 is_an_arc_of p1,p2 and
A12: C = P \/ P1 and
A13: P /\ P1 = {p1,p2} by A1,A2,A4,TOPREAL2:5;
A14: P c= C by A12,XBOOLE_1:7;
A15: P1 c= C by A12,XBOOLE_1:7;
A16: P meets {q1,q2} by A9,A10,A12,XBOOLE_1:7;
  per cases by A16,ZFMISC_1:57;
  suppose that
A17: q1 in P and
A18: not q2 in P;
    now
      take A = Segment(P,p1,p2,p1,q1);
      LE p1, q1, P, p1, p2 by A10,A17,JORDAN5C:10;
      hence A is_an_arc_of p1,q1 by A5,A10,JORDAN16:36;
A19:  A c= P by JORDAN16:5;
      hence A c= C by A14,XBOOLE_1:1;
A20:  now
        assume
A21:    p2 in A;
        A = {q where q is Point of TOP-REAL 2:
        LE p1,q,P,p1,p2 & LE q,q1,P,p1,p2} by JORDAN6:29;
        then ex q being Point of TOP-REAL 2 st p2 = q &
        LE p1,q,P,p1,p2 & LE q,q1,P,p1,p2 by A21;
        hence contradiction by A6,A10,JORDAN6:70;
      end;
      not q2 in A by A18,A19;
      hence A misses {p2,q2} by A20,ZFMISC_1:57;
    end;
    hence p1,q1 are_neighbours_wrt p2,q2, C by Def3;
  end;
  suppose that
A22: q2 in P and
A23: not q1 in P;
    now
      take A = Segment(P1,p1,p2,p1,q1);
      q1 in P1 by A3,A12,A23,XBOOLE_0:def 2;
      then LE p1, q1, P1, p1, p2 by A11,JORDAN5C:10;
      hence A is_an_arc_of p1,q1 by A5,A11,JORDAN16:36;
A24:  A c= P1 by JORDAN16:5;
      hence A c= C by A15,XBOOLE_1:1;
A25:  now
        assume
A26:    p2 in A;
        A = {q where q is Point of TOP-REAL 2:
        LE p1,q,P1,p1,p2 & LE q,q1,P1,p1,p2} by JORDAN6:29;
        then ex q being Point of TOP-REAL 2 st p2 = q &
        LE p1,q,P1,p1,p2 & LE q,q1,P1,p1,p2 by A26;
        hence contradiction by A6,A11,JORDAN6:70;
      end;
      now
        assume q2 in A;
        then q2 in {p1,p2} by A13,A22,A24,XBOOLE_0:def 3;
        hence contradiction by A7,A8,TARSKI:def 2;
      end;
      hence A misses {p2,q2} by A25,ZFMISC_1:57;
    end;
    hence p1,q1 are_neighbours_wrt p2,q2, C by Def3;
  end;
  suppose that
A27: q1 in P and
A28: q2 in P;
    P1 meets {q1,q2} by A9,A11,A12,XBOOLE_1:7;
    then q1 in P1 or q2 in P1 by ZFMISC_1:57;
    then q1 in {p1,p2} or q2 in {p1,p2} by A13,A27,A28,XBOOLE_0:def 3;
    hence thesis by A5,A6,A7,A8,TARSKI:def 2;
  end;
end;

