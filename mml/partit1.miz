:: A theory of partitions, { I }
::  by Shunichi Kobayashi and Kui Jia
::
:: Received October 5, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies NUMBERS, XBOOLE_0, EQREL_1, SUBSET_1, TARSKI, SETFAM_1, FUNCT_1,
      RELAT_1, ZFMISC_1, FINSEQ_1, XXREAL_0, ARYTM_3, CARD_1, ORDINAL4,
      PARTIT1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, NAT_1, RELAT_1,
      FUNCT_1, FUNCT_2, SETFAM_1, FINSEQ_1, FINSEQ_4, EQREL_1, XXREAL_0;
 constructors SETFAM_1, FUNCT_2, XXREAL_0, XREAL_0, NAT_1, MEMBERED, EQREL_1,
      FINSEQ_4, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, PARTFUN1, MEMBERED, EQREL_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, RELAT_1, EQREL_1;
 theorems TARSKI, FUNCT_1, SETFAM_1, EQREL_1, ZFMISC_1, RELAT_1, NAT_1,
      FINSEQ_1, FINSEQ_2, FUNCT_2, SUBSET_1, RELSET_1, XBOOLE_0, XBOOLE_1,
      XREAL_1, XXREAL_0;
 schemes EQREL_1, NAT_1, CLASSES1, DOMAIN_1, XBOOLE_0;

begin

::Chap. 1  Preliminaries

reserve Y,Z for non empty set;
reserve PA,PB for a_partition of Y;
reserve A,B for Subset of Y;
reserve i,j,k for Element of NAT;
reserve x,y,z,x1,x2,y1,z0,X,V,a,b,d,t,SFX,SFY for set;

theorem Th1:
  X in PA & V in PA & X c= V implies X=V
proof
  assume that
A1: X in PA and
A2: V in PA and
A3: X c= V;
A4: X=V or X misses V by A1,A2,EQREL_1:def 6;
  consider x being Element of X;
A5: X<>{} by A1,EQREL_1:def 6;
A6: x in X by A5;
  thus thesis by A3,A4,A6,XBOOLE_0:3;
end;

notation
  let SFX,SFY;
  synonym SFX '<' SFY for SFX is_finer_than SFY;
  synonym SFY '>' SFX for SFX is_finer_than SFY;
end;

canceled;

theorem Th3:
  union (SFX \ {{}}) = union SFX
proof
A1: union (SFX \ {{}}) c= (union SFX)
  proof
    let y be set;
    assume
A2: y in union (SFX \ {{}});
A3: ex z being set st y in z & z in (SFX \{{} }) by A2,TARSKI:def 4;
    thus thesis by A3,TARSKI:def 4;
  end;
A4: union SFX c= union (SFX \ {{}})
  proof
    let y be set;
    assume
A5: y in union SFX;
    consider X being set such that
A6: y in X and
A7: X in SFX by A5,TARSKI:def 4;
A8: not X in {{}} by A6,TARSKI:def 1;
A9: X in SFX \ {{}} by A7,A8,XBOOLE_0:def 5;
    thus thesis by A6,A9,TARSKI:def 4;
  end;
  thus thesis by A1,A4,XBOOLE_0:def 10;
end;

theorem Th4:
  for PA,PB being a_partition of Y st PA '>' PB & PB '>' PA holds PB c= PA
proof
  let PA,PB be a_partition of Y;
  assume that
A1: PA '>' PB and
A2: PB '>' PA;
  let x be set;
  assume
A3: x in PB;
  consider V such that
A4: V in PA and
A5: x c= V by A1,A3,SETFAM_1:def 2;
  consider W being set such that
A6: W in PB and
A7: V c= W by A2,A4,SETFAM_1:def 2;
A8: x=W by A3,A5,A6,A7,Th1,XBOOLE_1:1;
  thus thesis by A4,A5,A7,A8,XBOOLE_0:def 10;
end;

theorem Th5:
  for PA,PB being a_partition of Y st PA '>' PB & PB '>' PA holds PA = PB
proof
  let PA,PB be a_partition of Y;
  assume
A1: PA '>' PB & PB '>' PA;
A2: PB c= PA & PA c= PB by A1,Th4;
  thus thesis by A2,XBOOLE_0:def 10;
end;

canceled;

theorem Th7:
  for PA,PB being a_partition of Y st PA '>' PB holds PA is_coarser_than PB
proof
  let PA,PB be a_partition of Y;
  assume
A1: PA '>' PB;
A2: for x being set st x in PA ex y being set st y in PB & y c= x
  proof
    let x be set;
    assume
A3: x in PA;
A4: x<>{} by A3,EQREL_1:def 6;
    consider u being Element of x;
A5: u in x by A4;
A6: union PB = Y by EQREL_1:def 6;
    consider y being set such that
A7: u in y and
A8: y in PB by A3,A5,A6,TARSKI:def 4;
    consider z being set such that
A9: z in PA and
A10: y c= z by A1,A8,SETFAM_1:def 2;
A11: x=z or x misses z by A3,A9,EQREL_1:def 6;
    thus thesis by A4,A7,A8,A10,A11,XBOOLE_0:3;
  end;
  thus thesis by A2,SETFAM_1:def 3;
end;

definition
  let Y;
  let PA be a_partition of Y,b be set;
  pred b is_a_dependent_set_of PA means
  :Def1:
  ex B being set st B c= PA & B<>{} & b = union B;
end;

definition
  let Y;
  let PA,PB be a_partition of Y,b be set;
  pred b is_min_depend PA,PB means
  :Def2:
  b is_a_dependent_set_of PA &
  b is_a_dependent_set_of PB & for d being set st d c= b
  & d is_a_dependent_set_of PA & d is_a_dependent_set_of PB holds d=b;
end;

theorem Th8:
  for PA,PB being a_partition of Y
  st PA '>' PB holds for b being set st b in PA holds
  b is_a_dependent_set_of PB
proof
  let PA,PB be a_partition of Y;
  assume
A1: PA '>' PB;
A2: union PB = Y by EQREL_1:def 6;
A3: PA is_coarser_than PB by A1,Th7;
A4: for b being set st b in PA holds b is_a_dependent_set_of PB
  proof
    let b be set;
    assume
A5: b in PA;
    set B0={x8 where x8 is Subset of Y: x8 in PB & x8 c= b};
    consider xb be set such that
A6: xb in PB & xb c= b by A3,A5,SETFAM_1:def 3;
A7: xb in B0 by A6;
A8: z in B0 implies z in PB
    proof
      assume
A9:   z in B0;
A10:  ex x8 being Subset of Y st x8=z & x8 in PB & x8 c= b by A9;
      thus thesis by A10;
    end;
A11: B0 c= PB by A8,TARSKI:def 3;
A12: z in b implies z in union B0
    proof
      assume
A13:  z in b;
      consider x1 such that
A14:  z in x1 and
A15:  x1 in PB by A2,A5,A13,TARSKI:def 4;
      consider y1 such that
A16:  y1 in PA and
A17:  x1 c= y1 by A1,A15,SETFAM_1:def 2;
A18:  b = y1 or b misses y1 by A5,A16,EQREL_1:def 6;
A19:  x1 in B0 by A13,A14,A15,A17,A18,XBOOLE_0:3;
      thus thesis by A14,A19,TARSKI:def 4;
    end;
A20: b c= union B0 by A12,TARSKI:def 3;
A21: z in union B0 implies z in b
    proof
      assume
A22:  z in union B0;
      consider y such that
A23:  z in y and
A24:  y in B0 by A22,TARSKI:def 4;
A25:  ex x8 being Subset of Y st x8=y & x8 in PB & x8 c= b by A24;
      thus thesis by A23,A25;
    end;
A26: union B0 c= b by A21,TARSKI:def 3;
A27: b = union B0 by A20,A26,XBOOLE_0:def 10;
    thus thesis by A7,A11,A27,Def1;
  end;
  thus thesis by A4;
end;

theorem Th9:
  for PA being a_partition of Y holds Y is_a_dependent_set_of PA
proof
  let PA be a_partition of Y;
A1: {Y} is Subset-Family of Y by ZFMISC_1:80;
A2: union {Y} = Y by ZFMISC_1:31;
  A3: for
 A st A in {Y} holds A<>{} & for B st B in {Y} holds A = B or A misses B
  proof
    let A;
    assume
A4: A in {Y};
A5: A=Y by A4,TARSKI:def 1;
    thus A<>{} by A4,TARSKI:def 1;
    let B;
    assume
A6: B in {Y};
    thus thesis by A5,A6,TARSKI:def 1;
  end;
A7: {Y} is a_partition of Y by A1,A2,A3,EQREL_1:def 6;
A8: for a being set st a in PA ex b being set st b in {Y} & a c= b
  proof
    let a be set;
    assume
A9: a in PA;
A10: a<>{} by A9,EQREL_1:def 6;
    consider x being Element of a;
A11: x in Y by A9,A10,TARSKI:def 3;
    consider b being set such that
    x in b and
A12: b in {Y} by A2,A11,TARSKI:def 4;
A13: b = Y by A12,TARSKI:def 1;
    thus thesis by A9,A12,A13;
  end;
A14: {Y} '>' PA by A8,SETFAM_1:def 2;
A15: Y in {Y} by TARSKI:def 1;
  thus thesis by A7,A14,A15,Th8;
end;

theorem Th10:
  for F being Subset-Family of Y st (Intersect(F)<>{} &
  for X st X in F holds X is_a_dependent_set_of PA)
  holds Intersect(F) is_a_dependent_set_of PA
proof
  let F be Subset-Family of Y;
  per cases;
  suppose
A1: F={};
A2: Intersect(F)=Y by A1,SETFAM_1:def 10;
    thus thesis by A2,Th9;
  end;
  suppose
A3: F<>{};
    reconsider F' = F as non empty Subset-Family of Y by A3;
    assume that
A4: Intersect(F)<>{} and
A5: for X st X in F holds X is_a_dependent_set_of PA;
    defpred P[set,set] means $2 c= PA & $2<>{} & $1=union $2;
A6: for X st X in F holds ex B being set st P[X,B]
    proof
      let X;
      assume
A7:   X in F;
A8:   X is_a_dependent_set_of PA by A5,A7;
      thus thesis by A8,Def1;
    end;
    consider f being Function such that
A9: dom f = F & for X st X in F holds P[X,f.X] from CLASSES1:sch 1(A6);
A10: rng f c= bool bool Y
    proof
      let y be set;
      assume
A11:  y in rng f;
      consider x being set such that
A12:  x in dom f and
A13:  y = f.x by A11,FUNCT_1:def 5;
A14:  f.x c= PA by A9,A12;
A15:  y c= bool Y by A13,A14,XBOOLE_1:1;
      thus thesis by A15;
    end;
    reconsider f as Function of F', bool bool Y
    by A9,A10,FUNCT_2:def 1,RELSET_1:11;
    defpred P[set] means $1 in F;
    deffunc S(Element of F') = f.$1;
    reconsider T={S(X) where X is Element of F':P[X]}
    as Subset-Family of bool Y from DOMAIN_1:sch 8;
    reconsider T as Subset-Family of bool Y;
    take B=Intersect(T);
    thus B c= PA
    proof
      let x;
      assume
A16:  x in B;
      consider X being set such that
A17:  X in F by A3,XBOOLE_0:def 1;
A18:  f.X in T by A17;
A19:  x in f.X by A16,A18,SETFAM_1:58;
A20:  f.X c= PA by A9,A17;
      thus thesis by A19,A20;
    end;
    thus B<>{}
    proof
      consider X being set such that
A21:  X in F by A3,XBOOLE_0:def 1;
A22:  f.X in T by A21;
      consider A being set such that
A23:  A in Intersect(F) by A4,XBOOLE_0:def 1;
      reconsider A as Element of Y by A23;
      set AA = EqClass(A,PA);
A24:  A in meet F by A3,A23,SETFAM_1:def 10;
A25:  for X st X in T holds AA in X
      proof
        let X;
        assume
A26:    X in T;
        consider z being Element of F' such that
A27:    X=f.z
        and z in F by A26;
A28:    f.z c= PA by A9;
A29:    z = union (f.z) & A in z by A9,A24,SETFAM_1:def 1;
A30:    ex A0 being set st A in A0 & A0 in f.z by A29,TARSKI:def 4;
        thus thesis by A27,A28,A30,EQREL_1:def 8;
      end;
A31:  EqClass(A,PA) in meet T by A22,A25,SETFAM_1:def 1;
      thus thesis by A31,SETFAM_1:def 10;
    end;
A32: Intersect(F) c= union B
    proof
      let x be set;
      assume
A33:  x in Intersect(F);
A34:  x in meet F by A3,A33,SETFAM_1:def 10;
      reconsider x as Element of Y by A33;
      reconsider PA as a_partition of Y;
      set A = EqClass(x,PA);
      consider X being set such that
A35:  X in F by A3,XBOOLE_0:def 1;
A36:  f.X in T by A35;
A37:  for X st X in T holds A in X
      proof
        let X;
        assume
A38:    X in T;
        consider z being Element of F' such that
A39:    X=f.z
        and z in F by A38;
A40:    f.z c= PA by A9;
A41:    z = union (f.z) by A9;
A42:    x in union (f.z) by A34,A41,SETFAM_1:def 1;
A43:    ex A0 being set st x in A0 & A0 in f.z by A42,TARSKI:def 4;
        thus thesis by A39,A40,A43,EQREL_1:def 8;
      end;
A44:  A in meet T by A36,A37,SETFAM_1:def 1;
      A45:  x
 in A & A in Intersect(T) by A36,A44,EQREL_1:def 8,SETFAM_1:def 10;
      thus thesis by A45,TARSKI:def 4;
    end;
A46: union B c= Intersect(F)
    proof
      let x be set;
      assume
A47:  x in union B;
      consider X being set such that
A48:  X in F by A3,XBOOLE_0:def 1;
A49:  f.X in T by A48;
      consider y being set such that
A50:  x in y and
A51:  y in B by A47,TARSKI:def 4;
A52:  y in meet T by A49,A51,SETFAM_1:def 10;
A53:  for X st X in F holds x in X
      proof
        let X;
        assume
A54:    X in F;
A55:    f.X in T by A54;
A56:    y in f.X by A52,A55,SETFAM_1:def 1;
A57:    X = union (f.X) by A9,A54;
        thus thesis by A50,A56,A57,TARSKI:def 4;
      end;
A58:  x in meet F by A3,A53,SETFAM_1:def 1;
      thus thesis by A3,A58,SETFAM_1:def 10;
    end;
    thus Intersect(F) = union B by A32,A46,XBOOLE_0:def 10;
  end;
end;

theorem Th11:
  for X0,X1 being Subset of Y st X0 is_a_dependent_set_of PA &
  X1 is_a_dependent_set_of PA & X0 meets X1
  holds X0 /\ X1 is_a_dependent_set_of PA
proof
  let X0,X1 be Subset of Y;
  assume that
A1: X0 is_a_dependent_set_of PA and
A2: X1 is_a_dependent_set_of PA and
A3: X0 meets X1;
  consider A being set such that
A4: A c= PA
  and A<>{} and
A5: X0 = union A by A1,Def1;
  consider B being set such that
A6: B c= PA
  and B<>{} and
A7: X1 = union B by A2,Def1;
A8: X0 /\ X1 c= union (A /\ B)
  proof
    let x be set;
    assume
A9: x in (X0 /\ X1);
A10: x in X0 by A9,XBOOLE_0:def 4;
A11: x in X1 by A9,XBOOLE_0:def 4;
    consider y being set such that
A12: x in y and
A13: y in A by A5,A10,TARSKI:def 4;
    consider z being set such that
A14: x in z and
A15: z in B by A7,A11,TARSKI:def 4;
A16: y in PA by A4,A13;
A17: z in PA by A6,A15;
A18: y meets z by A12,A14,XBOOLE_0:3;
A19: y = z by A16,A17,A18,EQREL_1:def 6;
A20: y in A /\ B by A13,A15,A19,XBOOLE_0:def 4;
    thus thesis by A12,A20,TARSKI:def 4;
  end;
A21: union (A /\ B) c= X0 /\ X1
  proof
    let x be set;
    assume
A22: x in union (A /\ B);
    consider y being set such that
A23: x in y and
A24: y in (A /\ B) by A22,TARSKI:def 4;
A25: y in A by A24,XBOOLE_0:def 4;
A26: y in B by A24,XBOOLE_0:def 4;
A27: x in X0 by A5,A23,A25,TARSKI:def 4;
A28: x in X1 by A7,A23,A26,TARSKI:def 4;
    thus thesis by A27,A28,XBOOLE_0:def 4;
  end;
A29: X0 /\ X1 = union (A /\ B) by A8,A21,XBOOLE_0:def 10;
A30: A \/ B c= PA by A4,A6,XBOOLE_1:8;
A31: A /\ B c= A & A c= A \/ B by XBOOLE_1:7,17;
A32: A /\ B c= A \/ B by A31,XBOOLE_1:1;
A33: A /\ B c= PA by A30,A32,XBOOLE_1:1;
A34: now
    assume
A35: A /\ B={};
A36: ex y being set st y in X0 & y in X1 by A3,XBOOLE_0:3;
    thus contradiction by A8,A35,A36,XBOOLE_0:def 4,ZFMISC_1:2;
  end;
  thus thesis by A29,A33,A34,Def1;
end;

theorem Th12:
  for X being Subset of Y holds X is_a_dependent_set_of PA & X<>Y
  implies X` is_a_dependent_set_of PA
proof
  let X be Subset of Y;
  assume that
A1: X is_a_dependent_set_of PA and
A2: X<>Y;
  consider B being set such that
A3: B c= PA
  and B<>{} and
A4: X=union B by A1,Def1;
  take PA \ B;
A5: union PA = Y by EQREL_1:def 6;
A6: X` = union PA \ union B by A4,A5,SUBSET_1:def 5;
  reconsider B as Subset of PA by A3;
A7: now
    assume
A8: PA \ B={};
A9: PA c= B by A8,XBOOLE_1:37;
    thus contradiction by A2,A4,A5,A9,XBOOLE_0:def 10;
  end;
  thus thesis by A6,A7,EQREL_1:52,XBOOLE_1:36;
end;

theorem Th13:
  for y being Element of Y
  ex X being Subset of Y st y in X & X is_min_depend PA,PB
proof
  let y be Element of Y;
A1: union PA = Y by EQREL_1:def 6;
A2: for A being set st A in PA holds A<>{} & for B being set st B in PA
  holds A=B or A misses B by EQREL_1:def 6;
A3: Y is_a_dependent_set_of PA & Y is_a_dependent_set_of PB by Th9;
  defpred P[set] means
  y in $1 & $1 is_a_dependent_set_of PA & $1 is_a_dependent_set_of PB;
  reconsider XX={X where X is Subset of Y:P[X]}
  as Subset-Family of Y from DOMAIN_1:sch 7;
  reconsider XX as Subset-Family of Y;
A4: Y c= Y;
A5: Y in XX by A3,A4;
A6: for X1 be set st X1 in XX holds y in X1
  proof
    let X1 be set;
    assume
A7: X1 in XX;
A8: ex X be Subset of Y st X=X1 & y in X & X
    is_a_dependent_set_of PA & X is_a_dependent_set_of PB by A7;
    thus thesis by A8;
  end;
A9: y in meet XX by A5,A6,SETFAM_1:def 1;
A10: Intersect(XX)<>{} by A9,SETFAM_1:def 10;
  take Intersect(XX);
A11: for X1 be set st X1 in XX holds X1 is_a_dependent_set_of PA
  proof
    let X1 be set;
    assume
A12: X1 in XX;
A13: ex X be Subset of Y st X=X1 & y in X & X is_a_dependent_set_of PA &
    X is_a_dependent_set_of PB by A12;
    thus thesis by A13;
  end;
A14: Intersect(XX) is_a_dependent_set_of PA by A10,A11,Th10;
A15: for X1 be set st X1 in XX holds X1 is_a_dependent_set_of PB
  proof
    let X1 be set;
    assume
A16: X1 in XX;
A17: ex X be Subset of Y st X=X1 & y in X & X is_a_dependent_set_of PA &
    X is_a_dependent_set_of PB by A16;
    thus thesis by A17;
  end;
A18: Intersect(XX) is_a_dependent_set_of PB by A10,A15,Th10;
A19: for d being set st d c= Intersect(XX) & d is_a_dependent_set_of PA &
  d is_a_dependent_set_of PB holds d=Intersect(XX)
  proof
    let d be set;
    assume that
A20: d c= Intersect(XX) and
A21: d is_a_dependent_set_of PA and
A22: d is_a_dependent_set_of PB;
    consider Ad being set such that
A23: Ad c= PA and
A24: Ad<>{} and
A25: d = union Ad by A21,Def1;
A26: d c= Y by A1,A23,A25,ZFMISC_1:95;
    per cases;
    suppose
A27:  y in d;
A28:  d in XX by A21,A22,A26,A27;
A29:  Intersect(XX) c= d
      proof
        let X1 be set;
        assume
A30:    X1 in Intersect(XX);
A31:    X1 in meet XX by A5,A30,SETFAM_1:def 10;
        thus thesis by A28,A31,SETFAM_1:def 1;
      end;
      thus thesis by A20,A29,XBOOLE_0:def 10;
    end;
    suppose
A32:  not y in d;
      reconsider d as Subset of Y by A1,A23,A25,ZFMISC_1:95;
A33:  d` = Y \ d by SUBSET_1:def 5;
A34:  y in d` by A32,A33,XBOOLE_0:def 5;
A35:  d misses d` by SUBSET_1:44;
A36:  d /\ d` = {} by A35,XBOOLE_0:def 7;
A37:  d <> Y by A32;
      A38:  d
` is_a_dependent_set_of PA & d` is_a_dependent_set_of PB by A21,A22,A37,
      Th12;
A39:  d` in XX by A34,A38;
A40:  Intersect(XX) c= d`
      proof
        let X1 be set;
        assume
A41:    X1 in Intersect(XX);
A42:    X1 in meet XX by A5,A41,SETFAM_1:def 10;
        thus thesis by A39,A42,SETFAM_1:def 1;
      end;
A43:  d /\ Intersect(XX) = {} by A36,A40,XBOOLE_1:3,26;
A44:  d /\ d c= d /\ Intersect(XX) by A20,XBOOLE_1:26;
A45:  union Ad = {} by A25,A43,A44;
      consider ad being set such that
A46:  ad in Ad by A24,XBOOLE_0:def 1;
A47:  ad<>{} by A2,A23,A46;
A48:  ad c= {} by A45,A46,ZFMISC_1:92;
      thus thesis by A47,A48;
    end;
  end;
  thus thesis by A5,A9,A14,A18,A19,Def2,SETFAM_1:def 10;
end;

theorem
  for P being a_partition of Y for y being Element of Y
  ex A being Subset of Y st y in A & A in P
proof
  let P be a_partition of Y;
  let y be Element of Y;
  consider R being Equivalence_Relation of Y such that
A1: P = Class R by EQREL_1:43;
  take Class(R,y);
  thus y in Class(R,y) by EQREL_1:28;
  thus thesis by A1,EQREL_1:def 5;
end;

definition
  let Y be set;
  func PARTITIONS(Y) means
  :Def3:
  for x being set holds x in it iff x is a_partition of Y;
  existence
  proof
    defpred P[set] means $1 is a_partition of Y;
    consider X being set such that
A1: for x being set holds x in X iff x in bool bool Y & P[x] from
    XBOOLE_0:sch 1;
    take X;
    let x be set;
    thus x in X implies x is a_partition of Y by A1;
    assume
A2: x is a_partition of Y;
    thus thesis by A1,A2;
  end;
  uniqueness
  proof
    let A1,A2 be set such that
A3: for x being set holds x in A1 iff x is a_partition of Y and
A4: for x being set holds x in A2 iff x is a_partition of Y;
A5: now
      let y be set;
A6:   y in A1 iff y is a_partition of Y by A3;
      thus y in A1 iff y in A2 by A4,A6;
    end;
    thus thesis by A5,TARSKI:2;
  end;
end;

registration
  let Y be set;
  cluster PARTITIONS(Y) -> non empty;
  coherence
  proof
    per cases;
    suppose
A1:   Y={};
      thus thesis by A1,Def3,EQREL_1:54;
    end;
    suppose
A2:   Y<>{};
      reconsider Y as non empty set by A2;
      A3:   {Y} is Subset-Family of Y & union {Y} = Y by ZFMISC_1:31,80;
A4:   for A being Subset of Y st A in {Y} holds A<>{} &
      for B being Subset of Y st B in {Y} holds A = B or A misses B
      proof
        let A be Subset of Y;
        assume
A5:     A in {Y};
A6:     A=Y by A5,TARSKI:def 1;
        thus A<>{} by A5,TARSKI:def 1;
        let B be Subset of Y;
        assume
A7:     B in {Y};
        thus thesis by A6,A7,TARSKI:def 1;
      end;
A8:   {Y} is a_partition of Y by A3,A4,EQREL_1:def 6;
      thus thesis by A8,Def3;
    end;
  end;
end;

begin

::Chap. 2  Join and Meet Operation between Partitions

definition
  let Y;
  let PA,PB be a_partition of Y;
  func PA '/\' PB -> a_partition of Y equals
  INTERSECTION(PA,PB) \ {{}};
  correctness
  proof
A1: {} c= Y by XBOOLE_1:2;
    reconsider e = {{}} as Subset-Family of Y by A1,ZFMISC_1:37;
    set a = INTERSECTION(PA,PB);
A2: for z being set st z in a holds z in bool Y
    proof
      let z be set;
      assume
A3:   z in a;
A4:   ex M,N being set st M in PA & N in PB & z = M /\ N by A3,
      SETFAM_1:def 5;
A5:   z c= Y by A4,XBOOLE_1:108;
      thus thesis by A5;
    end;
    reconsider a = INTERSECTION(PA,PB) as Subset-Family of Y by A2,TARSKI:def 3
    ;
    reconsider ia = a \ e as Subset-Family of Y;
A6: union PA = Y & union PB = Y by EQREL_1:def 6;
A7: union ia = union INTERSECTION(PA,PB) by Th3
      .= union PA /\ union PB by SETFAM_1:39
      .= Y by A6;
A8: for A being Subset of Y st A in ia holds A<>{} &
    for B being Subset of Y st B in ia holds A = B or A misses B
    proof
      let A be Subset of Y;
      assume
A9:   A in ia;
A10:  not A in {{}} by A9,XBOOLE_0:def 5;
A11:  A in INTERSECTION(PA,PB) by A9,XBOOLE_0:def 5;
A12:  for B being Subset of Y st B in ia holds A = B or A misses B
      proof
        A13:    for
 m,n,o,p being set holds (m /\ n) /\ (o /\ p) = m /\ ( n /\ o /\ p)
        proof
          let m,n,o,p be set;
A14:      (m /\ n) /\ (o /\ p) = m /\ ( n /\ (o /\ p) ) by XBOOLE_1:16
            .= m /\ ( n /\ o /\ p) by XBOOLE_1:16;
          thus thesis by A14;
        end;
        let B be Subset of Y;
        assume
A15:    B in ia;
A16:    B in INTERSECTION(PA,PB) by A15,XBOOLE_0:def 5;
        consider M,N being set such that
A17:    M in PA & N in PB and
A18:    B = M /\ N by A16,SETFAM_1:def 5;
        consider S,T being set such that
A19:    S in PA & T in PB and
A20:    A = S /\ T by A11,SETFAM_1:def 5;
A21:    M /\ N = S /\ T or (not M = S or not N = T );
A22:    M /\ N = S /\ T or (M misses S or N misses T)
        by A17,A19,A21,EQREL_1:def 6;
A23:    M /\ N = S /\ T or ( M /\ S = {} or N /\ T = {}) by A22,
        XBOOLE_0:def 7;
A24:    (M /\ S) /\ (N /\ T) = M /\ ( S /\ N /\ T ) by A13
          .=( M /\ N ) /\ ( S /\ T ) by A13;
        thus thesis by A18,A20,A23,A24,XBOOLE_0:def 7;
      end;
      thus thesis by A10,A12,TARSKI:def 1;
    end;
    thus thesis by A7,A8,EQREL_1:def 6;
  end;
  commutativity;
end;

theorem
  for PA being a_partition of Y holds PA '/\' PA = PA
proof
  let PA be a_partition of Y;
A1: for u being set st u in INTERSECTION(PA,PA) \ {{}} ex v being set
  st v in PA & u c= v
  proof
    let u be set;
    assume
A2: u in INTERSECTION(PA,PA) \ {{}};
    consider v,u2 being set such that
A3: v in PA and u2 in PA and
A4: u = v /\ u2 by A2,SETFAM_1:def 5;
    take v;
    thus thesis by A3,A4,XBOOLE_1:17;
  end;
A5: INTERSECTION(PA,PA) \ {{}} '<' PA by A1,SETFAM_1:def 2;
A6: for u being set st u in PA
  ex v being set st v in INTERSECTION(PA,PA) \ {{}} & u c= v
  proof
    let u be set;
    assume
A7: u in PA;
A8: u <> {} by A7,EQREL_1:def 6;
    set v = u /\ u;
A9: not v in {{}} by A8,TARSKI:def 1;
A10: v in INTERSECTION(PA,PA) by A7,SETFAM_1:def 5;
A11: v in INTERSECTION(PA,PA) \ {{}} by A9,A10,XBOOLE_0:def 5;
    thus thesis by A11;
  end;
A12: PA '<' INTERSECTION(PA,PA) \ {{}} by A6,SETFAM_1:def 2;
  thus thesis by A5,A12,Th5;
end;

theorem
  for PA,PB,PC being a_partition of Y holds
  (PA '/\' PB) '/\' PC = PA '/\' (PB '/\' PC)
proof
  let PA,PB,PC be a_partition of Y;
  consider PD,PE being a_partition of Y such that
A1: PD = PA '/\' PB and
A2: PE = PB '/\' PC;
A3: for u being set st u in PD '/\' PC
  ex v being set st v in PA '/\' PE & u c= v
  proof
    let u be set;
    assume
A4: u in PD '/\' PC;
    consider u1, u2 being set such that
A5: u1 in PD and
A6: u2 in PC and
A7: u = u1 /\ u2 by A4,SETFAM_1:def 5;
    consider u3, u4 being set such that
A8: u3 in PA and
A9: u4 in PB and
A10: u1 = u3 /\ u4 by A1,A5,SETFAM_1:def 5;
    consider v, v1,v2 being set such that
A11: v1 = u4 /\ u2 and
A12: v2 = u3 and
A13: v = u3 /\ u4 /\ u2;
A14: v = v2 /\ v1 by A11,A12,A13,XBOOLE_1:16;
A15: v1 in INTERSECTION(PB,PC) by A6,A9,A11,SETFAM_1:def 5;
A16: not u in {{}} by A4,XBOOLE_0:def 5;
A17: u = u3 /\ v1 by A7,A10,A11,XBOOLE_1:16;
A18: v1 <> {} by A16,A17,TARSKI:def 1;
A19: not v1 in {{}} by A18,TARSKI:def 1;
A20: v1 in INTERSECTION(PB,PC) \ {{}} by A15,A19,XBOOLE_0:def 5;
A21: v in INTERSECTION(PA,PE) by A2,A8,A12,A14,A20,SETFAM_1:def 5;
    take v;
    thus thesis by A7,A10,A13,A16,A21,XBOOLE_0:def 5;
  end;
A22: PD '/\' PC '<' PA '/\' PE by A3,SETFAM_1:def 2;
A23: for u being set st u in PA '/\' PE
  ex v being set st v in PD '/\' PC & u c= v
  proof
    let u be set;
    assume
A24: u in PA '/\' PE;
    consider u1,u2 being set such that
A25: u1 in PA and
A26: u2 in PE and
A27: u = u1 /\ u2 by A24,SETFAM_1:def 5;
    consider u3,u4 being set such that
A28: u3 in PB and
A29: u4 in PC and
A30: u2 = u3 /\ u4 by A2,A26,SETFAM_1:def 5;
A31: u = u1 /\ u3 /\ u4 by A27,A30,XBOOLE_1:16;
    consider v, v1,v2 being set such that
A32: v1 = u1 /\ u3 and v2 = u4 and
A33: v = u1 /\ u3 /\ u4;
A34: v1 in INTERSECTION(PA,PB) by A25,A28,A32,SETFAM_1:def 5;
A35: not u in {{}} by A24,XBOOLE_0:def 5;
A36: v1 <> {} by A31,A32,A35,TARSKI:def 1;
A37: not v1 in {{}} by A36,TARSKI:def 1;
A38: v1 in INTERSECTION(PA,PB) \ {{}} by A34,A37,XBOOLE_0:def 5;
A39: v in INTERSECTION(PD,PC) by A1,A29,A32,A33,A38,SETFAM_1:def 5;
    take v;
    thus thesis by A31,A33,A35,A39,XBOOLE_0:def 5;
  end;
A40: PA '/\' PE '<' PD '/\' PC by A23,SETFAM_1:def 2;
  thus thesis by A1,A2,A22,A40,Th5;
end;

theorem Th17:
  for PA,PB being a_partition of Y holds PA '>' PA '/\' PB
proof
  let PA,PB be a_partition of Y;
A1: for u being set st u in PA '/\' PB ex v being set st v in PA & u c= v
  proof
    let u be set;
    assume
A2: u in PA '/\' PB;
    consider u1,u2 being set such that
A3: u1 in PA and u2 in PB and
A4: u = u1 /\ u2 by A2,SETFAM_1:def 5;
    take u1;
    thus thesis by A3,A4,XBOOLE_1:17;
  end;
  thus PA '>' PA '/\' PB by A1,SETFAM_1:def 2;
end;

definition
  let Y;
  let PA,PB be a_partition of Y;
  func PA '\/' PB -> a_partition of Y means
  :Def5:
  for d holds d in it iff d is_min_depend PA,PB;
  existence
  proof
A1: union PA = Y by EQREL_1:def 6;
A2: for A being set st A in PA holds A<>{} & for B being set st B in PA
    holds A=B or A misses B by EQREL_1:def 6;
    defpred P[set] means $1 is_min_depend PA,PB;
    consider X being set such that
A3: for d being set holds d in X iff d in bool Y & P[d] from XBOOLE_0:sch 1;
A4: for d being set holds d in X iff d is_min_depend PA,PB
    proof
      let d be set;
A5:   for d being set holds d is_min_depend PA,PB implies d in bool Y
      proof
        let d be set;
        assume
A6:     d is_min_depend PA,PB;
A7:     d is_a_dependent_set_of PA by A6,Def2;
A8:     ex A being set st A c= PA & A<>{} & d = union A by A7,Def1;
A9:     d c= union PA by A8,ZFMISC_1:95;
        thus thesis by A1,A9;
      end;
A10:  d is_min_depend PA,PB implies
      d is_min_depend PA,PB & d in bool Y by A5;
      thus thesis by A3,A10;
    end;
    take X;
A11: Y c= union X
    proof
      let y be set;
      assume
A12:  y in Y;
      consider a being Subset of Y such that
A13:  y in a and
A14:  a is_min_depend PA,PB by A12,Th13;
A15:  a in X by A4,A14;
      thus thesis by A13,A15,TARSKI:def 4;
    end;
A16: union X c= Y
    proof
      let y be set;
      assume
A17:  y in union X;
      consider a being set such that
A18:  y in a and
A19:  a in X by A17,TARSKI:def 4;
A20:  a is_min_depend PA,PB by A4,A19;
A21:  a is_a_dependent_set_of PA by A20,Def2;
A22:  ex A being set st A c= PA & A<>{} & a = union A by A21,Def1;
A23:  a c= Y by A1,A22,ZFMISC_1:95;
      thus thesis by A18,A23;
    end;
A24: union X = Y by A11,A16,XBOOLE_0:def 10;
A25: for A being Subset of Y st A in X holds A<>{} &
    for B being Subset of Y st B in X holds A=B or A misses B
    proof
      let A be Subset of Y;
      assume
A26:  A in X;
A27:  A is_min_depend PA,PB by A4,A26;
A28:  A is_a_dependent_set_of PA by A27,Def2;
A29:  A is_a_dependent_set_of PB by A27,Def2;
      consider Aa being set such that
A30:  Aa c= PA and
A31:  Aa<>{} and
A32:  A = union Aa by A28,Def1;
      consider aa being set such that
A33:  aa in Aa by A31,XBOOLE_0:def 1;
A34:  aa<>{} by A2,A30,A33;
A35:  aa c= union Aa by A33,ZFMISC_1:92;
      thus A<>{} by A32,A34,A35;
      let B be Subset of Y;
      assume
A36:  B in X;
A37:  B is_min_depend PA,PB by A4,A36;
      A38:  B is_a_dependent_set_of PA & B is_a_dependent_set_of PB by A37,Def2
      ;
A39:  now
        assume
A40:    A meets B;
        A41:    A
 /\ B is_a_dependent_set_of PA & A /\ B is_a_dependent_set_of PB by A28
        ,A29,A38,A40,Th11;
A42:    A /\ B c= A by XBOOLE_1:17;
A43:    A /\ B = A by A27,A41,A42,Def2;
A44:    A c= B
        proof
          let x be set;
          assume
A45:      x in A;
          thus thesis by A43,A45,XBOOLE_0:def 4;
        end;
A46:    A /\ B c= B by XBOOLE_1:17;
A47:    A /\ B = B by A37,A41,A46,Def2;
A48:    B c= A
        proof
          let x be set;
          assume
A49:      x in B;
          thus thesis by A47,A49,XBOOLE_0:def 4;
        end;
        thus A=B by A44,A48,XBOOLE_0:def 10;
      end;
      thus thesis by A39;
    end;
A50: X c= bool Y
    proof
      let x be set;
      assume
A51:  x in X;
A52:  x is_min_depend PA,PB by A4,A51;
A53:  x is_a_dependent_set_of PA by A52,Def2;
A54:  ex a being set st a c= PA & a<>{} & x = union a by A53,Def1;
A55:  x c= Y by A1,A54,ZFMISC_1:95;
      thus thesis by A55;
    end;
    reconsider X as Subset-Family of Y by A50;
A56: X is a_partition of Y by A24,A25,EQREL_1:def 6;
    thus thesis by A4,A56;
  end;
  uniqueness
  proof
    let A1,A2 be a_partition of Y such that
A57: for x being set holds x in A1 iff x is_min_depend PA,PB and
A58: for x being set holds x in A2 iff x is_min_depend PA,PB;
A59: now
      let y be set;
A60:  y in A1 iff y is_min_depend PA,PB by A57;
      thus y in A1 iff y in A2 by A58,A60;
    end;
    thus thesis by A59,TARSKI:2;
  end;
  commutativity
  proof
    let IT,PA,PB be a_partition of Y;
A61: now
      let a be set;
      assume
A62:  a is_min_depend PA,PB;
      A63:  a is_a_dependent_set_of PB & a is_a_dependent_set_of PA by A62,Def2
      ;
A64:  for d being set st d c= a & d is_a_dependent_set_of PB & d
      is_a_dependent_set_of PA holds d=a by A62,Def2;
      thus a is_min_depend PB,PA by A63,A64,Def2;
    end;
A65: now
      let a be set;
      assume
A66:  a is_min_depend PB,PA;
      A67:  a is_a_dependent_set_of PA & a is_a_dependent_set_of PB by A66,Def2
      ;
A68:  for d being set st d c= a & d is_a_dependent_set_of PA & d
      is_a_dependent_set_of PB holds d=a by A66,Def2;
      thus a is_min_depend PA,PB by A67,A68,Def2;
    end;
A69: (for d being set holds (d in a) iff (d is_min_depend PA,PB)) implies
    for d being set holds d in a iff d is_min_depend PB,PA
    proof
      assume
A70:  for d being set holds d in a iff d is_min_depend PA,PB;
      let d be set;
A71:  d in a iff d is_min_depend PA,PB by A70;
      thus d in a implies d is_min_depend PB,PA by A61,A71;
      assume
A72:  d is_min_depend PB,PA;
      thus thesis by A65,A71,A72;
    end;
    thus thesis by A69;
  end;
end;

canceled;

theorem Th19:
  for PA,PB being a_partition of Y holds PA '<' PA '\/' PB
proof
  thus PA '<' PA '\/' PB
  proof
A1: for a being set st a in PA ex b being set st b in PA '\/' PB & a c= b
    proof
      let a be set;
      assume
A2:   a in PA;
A3:   a<>{} by A2,EQREL_1:def 6;
      consider x being Element of a;
A4:   x in Y by A2,A3,TARSKI:def 3;
A5:   union (PA '\/' PB) = Y by EQREL_1:def 6;
      consider b being set such that
A6:   x in b and
A7:   b in PA '\/' PB by A4,A5,TARSKI:def 4;
A8:   b is_min_depend PA,PB by A7,Def5;
A9:   b is_a_dependent_set_of PA by A8,Def2;
      consider B being set such that
A10:  B c= PA
      and B<>{} and
A11:  b = union B by A9,Def1;
A12:  a in B
      proof
        consider u being set such that
A13:    x in u and
A14:    u in B by A6,A11,TARSKI:def 4;
A15:    a /\ u <> {} by A3,A13,XBOOLE_0:def 4;
A16:    not a misses u by A15,XBOOLE_0:def 7;
A17:    u in PA by A10,A14;
        thus thesis by A2,A14,A16,A17,EQREL_1:def 6;
      end;
      thus thesis by A7,A11,A12,ZFMISC_1:92;
    end;
    thus thesis by A1,SETFAM_1:def 2;
  end;
end;

theorem
  for PA being a_partition of Y holds PA '\/' PA = PA
proof
  let PA be a_partition of Y;
A1: PA '<' PA '\/' PA by Th19;
A2: for a being set st a in PA '\/' PA ex b being set st b in PA & a c= b
  proof
    let a be set;
    assume
A3: a in PA '\/' PA;
A4: a is_min_depend PA,PA by A3,Def5;
A5: a is_a_dependent_set_of PA by A4,Def2;
    consider B being set such that
A6: B c= PA
    and B<>{} and
A7: a = union B by A5,Def1;
A8: a <> {} by A3,EQREL_1:def 6;
    consider x being Element of a;
A9: x in a by A8;
A10: x in Y by A3,A9;
A11: x in union PA by A10,EQREL_1:def 6;
    consider b being set such that
A12: x in b and
A13: b in PA by A11,TARSKI:def 4;
A14: b in B
    proof
      consider u being set such that
A15:  x in u and
A16:  u in B by A7,A8,TARSKI:def 4;
A17:  b /\ u <> {} by A12,A15,XBOOLE_0:def 4;
A18:  not b misses u by A17,XBOOLE_0:def 7;
A19:  u in PA by A6,A16;
      thus thesis by A13,A16,A18,A19,EQREL_1:def 6;
    end;
A20: b c= a by A7,A14,ZFMISC_1:92;
A21: b is_a_dependent_set_of PA by A13,Th8;
A22: a = b by A4,A20,A21,Def2;
    thus thesis by A13,A22;
  end;
A23: PA '\/' PA '<' PA by A2,SETFAM_1:def 2;
  thus thesis by A1,A23,Th5;
end;

theorem Th21:
  for PA,PC being a_partition of Y st
  PA '<' PC & x in PC & z0 in PA & t in x & t in z0 holds z0 c= x
proof
  let PA,PC be a_partition of Y;
  assume that
A1: PA '<' PC and
A2: x in PC and
A3: z0 in PA and
A4: t in x & t in z0;
  consider b such that
A5: b in PC and
A6: z0 c= b by A1,A3,SETFAM_1:def 2;
A7: x = b or x misses b by A2,A5,EQREL_1:def 6;
  thus thesis by A4,A6,A7,XBOOLE_0:3;
end;

theorem
  for PA,PB being a_partition of Y st
  x in (PA '\/' PB) & z0 in PA & t in x & t in z0 holds z0 c= x by Th19,Th21;

begin

::Chap. 3  Partitions and Equivalence Relations

theorem Th23:
  for PA being a_partition of Y ex RA being Equivalence_Relation of Y st
  for x,y holds [x,y] in RA iff ex A st A in PA & x in A & y in A
proof
  let PA be a_partition of Y;
A1: union PA = Y by EQREL_1:def 6;
A2: ex RA being Equivalence_Relation of Y st
  for x,y holds [x,y] in RA iff ex A st A in PA & x in A & y in A
  proof
    defpred P[set,set] means ex A st A in PA & $1 in A & $2 in A;
A3: for x st x in Y holds P[x,x]
    proof
      let x;
      assume
A4:   x in Y;
A5:   ex Z being set st x in Z & Z in PA by A1,A4,TARSKI:def 4;
      consider Z such that
A6:   x in Z and
A7:   Z in PA by A5;
      reconsider A = Z as Subset of Y by A7;
      take A;
      thus thesis by A6,A7;
    end;
A8: for x,y st P[x,y] holds P[y,x];
A9: for x,y,z st P[x,y] & P[y,z] holds P[x,z]
    proof
      let x,y,z;
      given A such that
A10:  A in PA and
A11:  x in A & y in A;
      given B such that
A12:  B in PA and
A13:  y in B & z in B;
A14:  A = B or A misses B by A10,A12,EQREL_1:def 6;
      thus thesis by A10,A11,A13,A14,XBOOLE_0:3;
    end;
    consider RA being Equivalence_Relation of Y such that
A15: for x,y holds [x,y] in RA iff
    x in Y & y in Y & P[x,y] from EQREL_1:sch 1(A3,A8,A9);
    take RA;
    thus thesis by A15;
  end;
  thus thesis by A2;
end;

definition
  let Y;
  let PA be a_partition of Y;
  func ERl PA -> Equivalence_Relation of Y means
  :Def6:
  for x1,x2 being set holds
  [x1,x2] in it iff ex A st A in PA & x1 in A & x2 in A;
  existence by Th23;
  uniqueness
  proof
    let f1,f2 be Equivalence_Relation of Y such that
A1: for x1,x2 being set holds [x1,x2] in f1 iff
    ex A st A in PA & x1 in A & x2 in A and
A2: for x1,x2 being set holds [x1,x2] in f2 iff
    ex A st A in PA & x1 in A & x2 in A;
    let x1,x2 be set;
A3: [x1,x2] in f1 iff ex A st A in PA & x1 in A & x2 in A by A1;
    thus thesis by A2,A3;
  end;
end;

definition
  let Y;
  defpred P[set,set] means ex PA st PA = $1 & $2 = ERl PA;
A1: for x st x in PARTITIONS(Y) ex z st P[x,z]
  proof
    let x;
    assume
A2: x in PARTITIONS(Y);
    reconsider x as a_partition of Y by A2,Def3;
    take ERl x;
    thus thesis;
  end;
  func Rel(Y) -> Function means
  dom it = PARTITIONS(Y) &
  for x st x in PARTITIONS(Y) ex PA st PA = x & it.x = ERl PA;
  existence
  proof
    thus ex f being Function st dom f = PARTITIONS(Y)
    & for x st x in PARTITIONS(Y) holds P[x,f.x] from CLASSES1:sch 1(A1);
  end;
  uniqueness
  proof
    let f1,f2 be Function such that
A3: dom f1 = PARTITIONS(Y) and
A4: for x st x in PARTITIONS(Y) ex PA st PA = x & f1.x = ERl PA and
A5: dom f2 = PARTITIONS(Y) and
A6: for x st x in PARTITIONS(Y) ex PA st PA = x & f2.x = ERl PA;
A7: for z st z in PARTITIONS(Y) holds f1.z = f2.z
    proof
      let x;
      assume
A8:   x in PARTITIONS(Y);
      A9:   (
 ex PA st PA = x & f1.x = ERl PA)& ex PA st PA = x & f2.x = ERl PA by A4,A6
      ,A8;
      thus thesis by A9;
    end;
    thus thesis by A3,A5,A7,FUNCT_1:9;
  end;
end;

theorem Th24:
  for PA,PB being a_partition of Y holds PA '<' PB iff ERl(PA) c= ERl(PB)
proof
  let PA,PB be a_partition of Y;
  set RA = ERl PA, RB = ERl PB;
  hereby
    assume
A1: PA '<' PB;
A2: for x1,x2 being set holds [x1,x2] in RA implies [x1,x2] in RB
    proof
      let x1,x2 be set;
      assume
A3:   [x1,x2] in RA;
      consider A being Subset of Y such that
A4:   A in PA and
A5:   x1 in A & x2 in A by A3,Def6;
A6:   ex y st y in PB & A c= y by A1,A4,SETFAM_1:def 2;
      thus thesis by A5,A6,Def6;
    end;
    thus ERl(PA) c= ERl(PB) by A2,RELAT_1:def 3;
  end;
  assume
A7: ERl(PA) c= ERl(PB);
A8: for x st x in PA ex y st y in PB & x c= y
  proof
    let x;
    assume
A9: x in PA;
A10: x<>{} by A9,EQREL_1:def 6;
    consider x0 being Element of x;
    set y={z where z is Element of Y:[x0,z] in ERl(PB)};
A11: x c= y
    proof
      let x1;
      assume
A12:  x1 in x;
A13:  [x0,x1] in RA by A9,A12,Def6;
      thus thesis by A7,A9,A12,A13;
    end;
    consider x1 being Element of x;
A14: [x0,x1] in RA by A9,A10,Def6;
    consider B being Subset of Y such that
A15: B in PB and
A16: x0 in B
    and x1 in B by A7,A14,Def6;
A17: y c= B
    proof
      let x2;
      assume
A18:  x2 in y;
A19:  ex z being Element of Y st z=x2 & [x0,z] in ERl(PB) by A18;
      consider B2 being Subset of Y such that
A20:  B2 in PB and
A21:  x0 in B2 and
A22:  x2 in B2 by A19,Def6;
A23:  B2 meets B by A16,A21,XBOOLE_0:3;
      thus thesis by A15,A20,A22,A23,EQREL_1:def 6;
    end;
A24: B c= y
    proof
      let x2;
      assume
A25:  x2 in B;
A26:  [x0,x2] in RB by A15,A16,A25,Def6;
      thus thesis by A25,A26;
    end;
A27: y=B by A17,A24,XBOOLE_0:def 10;
    thus thesis by A11,A15,A27;
  end;
  thus thesis by A8,SETFAM_1:def 2;
end;

theorem Th25:
  for PA,PB being a_partition of Y,p0,x,y being set,
  f being FinSequence of Y st p0 c= Y &
  x in p0 & f.1=x & f.len f=y & 1 <= len f & (for i st 1<=i & i<len f
  ex p2,p3,u being set st p2 in PA & p3 in PB &
  f.i in p2 & u in p2 & u in p3 & f.(i+1) in p3) &
  p0 is_a_dependent_set_of PA & p0 is_a_dependent_set_of PB holds y in p0
proof
  let PA,PB be a_partition of Y, p0,x,y be set, f be FinSequence of Y;
  assume that
A1: p0 c= Y and
A2: x in p0 & f.1=x and
A3: f.len f=y & 1 <= len f and
A4: for i st 1<=i & i<len f ex p2,p3,u being set st p2 in PA & p3 in PB
  & f.i in p2 & u in p2 & u in p3 & f.(i+1) in p3 and
A5: p0 is_a_dependent_set_of PA and
A6: p0 is_a_dependent_set_of PB;
  consider A1 being set such that
A7: A1 c= PA
  and A1<>{} and
A8: p0 = union A1 by A5,Def1;
  consider B1 being set such that
A9: B1 c= PB
  and B1<>{} and
A10: p0 = union B1 by A6,Def1;
A11: union PA = Y by EQREL_1:def 6;
A12: for A being set st A in PA holds A<>{} & for B being set st B in PA
  holds A=B or A misses B by EQREL_1:def 6;
A13: for A being set st A in PB holds A<>{} & for B being set st B in PB
  holds A=B or A misses B by EQREL_1:def 6;
A14: for k st 1 <= k & k <= len f holds f.k in p0
  proof
    defpred P[Element of NAT] means 1 <= $1 & $1 <= len f implies f.$1 in p0;
A15: P[0];
A16: for k st P[k] holds P[k+1]
    proof
      let k;
      assume
A17:  P[k];
      assume that
A18:  1<=k+1 and
A19:  k+1<=len f;
A20:  k < len f by A19,NAT_1:13;
A21:  1 <= k or 1 = k + 1 by A18,NAT_1:8;
A22:  now per cases by A21;
        suppose
A23:      1 <= k;
          consider p2,p3,u being set such that
A24:      p2 in PA and
A25:      p3 in PB and
A26:      f.k in p2 and
A27:      u in p2 and
A28:      u in p3 and
A29:      f.(k+1) in p3 by A4,A20,A23;
          consider A being set such that
A30:      f.k in A and
A31:      A in PA by A1,A11,A17,A19,A23,NAT_1:13,TARSKI:def 4;
A32:      p2=A or p2 misses A by A24,A31,EQREL_1:def 6;
          consider a being set such that
A33:      f.k in a and
A34:      a in A1 by A8,A17,A19,A23,NAT_1:13,TARSKI:def 4;
A35:      a=p2 or a misses p2 by A7,A12,A24,A34;
A36:      A c= p0 by A8,A26,A30,A32,A33,A34,A35,XBOOLE_0:3
          ,ZFMISC_1:92;
          consider B being set such that
A37:      u in B and
A38:      B in PB by A25,A28;
A39:      p3=B or p3 misses B by A25,A38,EQREL_1:def 6;
          consider b being set such that
A40:      u in b and
A41:      b in B1 by A10,A26,A27,A30,A32,A36,TARSKI:def 4,XBOOLE_0:3;
A42:      p3=b or p3 misses b by A9,A13,A25,A41;
A43:      B c= p0 by A10,A28,A37,A39,A40,A41,A42,XBOOLE_0:3,ZFMISC_1:92;
          thus thesis by A28,A29,A37,A39,A43,XBOOLE_0:3;
        end;
        suppose
A44:      0 = k;
          thus thesis by A2,A44;
        end;
      end;
      thus thesis by A22;
    end;
    thus P[k] from NAT_1:sch 1(A15,A16);
  end;
  thus thesis by A3,A14;
end;

theorem Th26:
  for R1,R2 being Equivalence_Relation of Y,f being FinSequence of Y,
  x,y being set st x in Y &
  f.1=x & f.len f=y & 1 <= len f &
  (for i st 1<=i & i<len f
  ex u being set st u in Y & [f.i,u] in (R1 \/ R2) & [u,f.(i+1)] in (R1 \/
  R2)) holds [x,y] in (R1 "\/" R2)
proof
  let R1,R2 be Equivalence_Relation of Y,f be FinSequence of Y, x,y be set;
  assume that
A1: x in Y and
A2: f.1=x and
A3: f.len f=y & 1 <= len f and
A4: for i st 1<=i & i<len f ex u being set st u in Y & [f.i,u] in R1 \/
  R2 & [u,f.(i+1)] in R1 \/ R2;
A5: for i st 1 <= i & i <= len f holds [f.1,f.i] in (R1 "\/" R2)
  proof
    defpred P[Element of NAT] means
    1<=$1 & $1<=len f implies [f.1,f.$1] in R1 "\/" R2;
A6: P[0];
A7: for i st P[i] holds P[i+1]
    proof
      let i;
      assume
A8:   P[i];
      assume that
A9:   1 <= i + 1 and
A10:  i + 1 <= len f;
A11:  i < len f by A10,NAT_1:13;
A12:  1 <= i or 1 = i + 1 by A9,NAT_1:8;
A13:  R1 \/ R2 c= R1 "\/" R2 by EQREL_1:def 3;
A14:  now per cases by A12;
        suppose
A15:      1 <= i;
          consider u being set such that
A16:      u in Y and
A17:      [f.i,u] in R1 \/ R2 & [u,f.(i+1)] in R1 \/ R2 by A4,A11,A15;
          reconsider u as Element of Y by A16;
A18:      dom f = Seg len f by FINSEQ_1:def 3;
A19:      i in dom f by A11,A15,A18,FINSEQ_1:3;
          reconsider f1=f.i as Element of Y by A19,FINSEQ_2:13;
A20:      (i+1) in dom f by A9,A10,A18,FINSEQ_1:3;
          reconsider f2=f.(i+1) as Element of Y by A20,FINSEQ_2:13;
          reconsider p = <*f1,u,f2*> as FinSequence of Y;
A21:      len p = 3 by FINSEQ_1:62;
A22:      p.1 = f.i & p.3 = f.(i+1) by FINSEQ_1:62;
A23:      for j st 1 <= j & j < len p holds [p.j,p.(j+1)] in (R1 \/ R2)
          proof
            let j;
            assume that
A24:        1 <= j and
A25:        j < len p;
A26:        j < 2+1 by A25,FINSEQ_1:62;
A27:        j <= 2 by A26,NAT_1:13;
A28:        j = 0 or j = 1 or j = 2 by A27,NAT_1:27;
            thus thesis by A17,A22,A24,A28,FINSEQ_1:62;
          end;
A29:      [f.i,f.(i+1)] in R1 "\/" R2 by A21,A22,A23,EQREL_1:36;
          thus thesis by A8,A10,A15,A29,EQREL_1:13,NAT_1:13;
        end;
        suppose
A30:      0 = i;
A31:      [f.1,f.1] in R1 by A1,A2,EQREL_1:11;
A32:      [f.1,f.1] in R1 \/ R2 by A31,XBOOLE_0:def 3;
          thus thesis by A13,A30,A32;
        end;
      end;
      thus thesis by A14;
    end;
    thus P[i] from NAT_1:sch 1(A6,A7);
  end;
  thus thesis by A2,A3,A5;
end;

theorem Th27:
  for PA,PB being a_partition of Y holds
  ERl(PA '\/' PB) = ERl(PA) "\/" ERl(PB)
proof
  let PA,PB be a_partition of Y;
A1: PA is_finer_than (PA '\/' PB) by Th19;
A2: PB is_finer_than (PA '\/' PB) by Th19;
A3: union PA = Y by EQREL_1:def 6;
A4: union PB = Y by EQREL_1:def 6;
A5: ERl(PA '\/' PB) c= (ERl(PA) "\/" ERl(PB))
  proof
    let x,y be set;
    assume
A6: [x,y] in ERl(PA '\/' PB);
    consider p0 being Subset of Y such that
A7: p0 in (PA '\/' PB) and
A8: x in p0 and
A9: y in p0 by A6,Def6;
A10: p0 is_min_depend PA,PB by A7,Def5;
A11: p0 is_a_dependent_set_of PA by A10,Def2;
A12: p0 is_a_dependent_set_of PB by A10,Def2;
    consider A1 being set such that
A13: A1 c= PA
    and A1<>{} and
A14: p0 = union A1 by A11,Def1;
    consider a being set such that
A15: x in a and
A16: a in A1 by A8,A14,TARSKI:def 4;
    reconsider Ca={ p where p is Element of PA: ex f being FinSequence of Y st
    1<=len f & f.1=x & f.len f in p & for i st 1<=i & i<len f holds
    (ex p1,p2,x0 being set st p1 in PA & p2 in PB
    & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2) } as set;
    reconsider pb=union Ca as set;
    reconsider Cb={ p where p is Element of PB:
    ex q being set st q in Ca & p /\ q <> {} } as set;
    reconsider x' = x as Element of Y by A8;
    reconsider fx=<*x'*> as FinSequence of Y;
A17: fx.1=x by FINSEQ_1:def 8;
A18: fx.len fx in a by A15,A17,FINSEQ_1:57;
    A19: 1
<=len fx & for i st 1<=i & i<len fx holds ex p1,p2,x0 being set st p1 in PA
    & p2 in PB & fx.i in p1 & x0 in (p1 /\ p2) & fx.(i+1) in p2 by FINSEQ_1:57;
A20: a in Ca by A13,A16,A17,A18,A19;
    consider y5 being set such that
A21: x in y5 and
A22: y5 in Ca by A15,A20;
    consider z5 being set such that
A23: x' in z5 and
A24: z5 in PB by A4,TARSKI:def 4;
A25: y5 /\ z5 <> {} by A21,A23,XBOOLE_0:def 4;
A26: z5 in Cb by A22,A24,A25;
A27: Ca c= PA
    proof
      let z be set;
      assume
A28:  z in Ca;
A29:  ex p being Element of PA st z=p & ex f being FinSequence of Y st
      1<=len f & f.1=x & f.len f in p & for i st 1<=i & i<len f holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2 by A28;
      thus thesis by A29;
    end;
A30: pb is_a_dependent_set_of PA by A20,A27,Def1;
A31: pb c= union Cb
    proof
      let x1 be set;
      assume
A32:  x1 in pb;
      consider y being set such that
A33:  x1 in y and
A34:  y in Ca by A32,TARSKI:def 4;
A35:  ex p being Element of PA st y=p & ex f being FinSequence
of Y st 1<=len f & f.1=x & f.len f in p & for i st 1<=i & i<len f holds ex p1,
p2,x0 being set st p1 in PA & p2 in PB & f. i in p1 & x0 in (p1 /\ p2) & f.(i+1
      ) in p2 by A34;
      consider z being set such that
A36:  x1 in z and
A37:  z in PB by A4,A33,A35,TARSKI:def 4;
A38:  y /\ z <> {} by A33,A36,XBOOLE_0:def 4;
A39:  z in Cb by A34,A37,A38;
      thus thesis by A36,A39,TARSKI:def 4;
    end;
A40: union Cb c= pb
    proof
      let x1 be set;
      assume
A41:  x1 in union Cb;
      consider y1 being set such that
A42:  x1 in y1 and
A43:  y1 in Cb by A41,TARSKI:def 4;
A44:  ex p being Element of PB st y1=p & ex q being set st q
      in Ca & p /\ q <> {} by A43;
      consider q being set such that
A45:  q in Ca and
A46:  y1 /\ q <> {} by A44;
      consider pd being set such that
A47:  x1 in pd and
A48:  pd in PA by A3,A42,A44,TARSKI:def 4;
A49:  ex pp being Element of PA st q=pp & ex f being
FinSequence of Y st 1<=len f & f.1=x & f.len f in pp & for i st 1<=i & i<len f
holds ex p1,p2,x0 being set st p1 in PA & p2 in PB & f. i in p1 & x0 in (p1 /\
      p2) & f.(i+1) in p2 by A45;
      consider fd being FinSequence of Y such that
A50:  1<=len fd and
A51:  fd.1=x and
A52:  fd.len fd in q and
A53:  for i st 1<=i & i<len fd holds ex p1,p2,x0 being set st p1 in PA &
      p2 in PB & fd.i in p1 & x0 in (p1 /\ p2) & fd.(i+1) in p2
      by A49;
      reconsider x1 as Element of Y by A42,A44;
      reconsider f=fd^<*x1*> as FinSequence of Y;
A54:  len f = len fd + len <*x1*> by FINSEQ_1:35;
A55:  len f = len fd + 1 by A54,FINSEQ_1:57;
A56:  1+1<=len fd + 1 by A50,XREAL_1:8;
A57:  1<=len f by A55,A56,XXREAL_0:2;
A58:  f.(len fd + 1) in y1 by A42,FINSEQ_1:59;
A59:  y1 meets q by A46,XBOOLE_0:def 7;
      consider z0 being set such that
A60:  z0 in y1 & z0 in q by A59,XBOOLE_0:3;
A61:  z0 in (y1 /\ q) by A60,XBOOLE_0:def 4;
A62:  dom fd = Seg len fd by FINSEQ_1:def 3;
A63:  for k being Element of NAT st 1 <= k & k <= len fd holds f.k=fd.k
      proof
        let k be Element of NAT;
        assume
A64:    1 <= k & k <= len fd;
A65:    k in dom fd by A62,A64,FINSEQ_1:3;
        thus thesis by A65,FINSEQ_1:def 7;
      end;
      A66:  (fd^<*x1*>).(len fd + 1)=x1 & f.1=x by A50,A51,A63,FINSEQ_1:59;
A67:  f.len fd in q by A50,A52,A63;
A68:  for i st 1<=i & i<len fd holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
      proof
        let i;
        assume
A69:    1<=i & i<len fd;
A70:    f.i = fd.i by A63,A69;
A71:    1<=(i+1) & (i+1)<=len fd by A69,NAT_1:13;
A72:    f.(i+1) = fd.(i+1) by A63,A71;
        thus thesis by A53,A69,A70,A72;
      end;
A73:  for i st 1<=i & i<len f holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
      proof
        let i;
        assume that
A74:    1<=i and
A75:    i<len f;
A76:    i<= len fd by A55,A75,NAT_1:13;
A77:    now per cases by A74,A76,XXREAL_0:1;
          case
A78:        1<=i & i<len fd;
            thus thesis by A68,A78;
          end;
          case
A79:        1<=i & i=len fd;
            thus thesis by A44,A49,A58,A61,A67,A79;
          end;
        end;
        thus thesis by A77;
      end;
A80:  pd in Ca by A47,A48,A55,A57,A66,A73;
      thus thesis by A47,A80,TARSKI:def 4;
    end;
A81: pb = union Cb by A31,A40,XBOOLE_0:def 10;
A82: Cb c= PB
    proof
      let z be set;
      assume
A83:  z in Cb;
A84:  ex p being Element of PB st z=p &
      ex q being set st q in Ca & p /\ q <> {} by A83;
      thus thesis by A84;
    end;
A85: pb is_a_dependent_set_of PB by A26,A81,A82,Def1;
A86: now
      assume
A87:  not pb c= p0;
A88:  pb \ p0 <> {} by A87,XBOOLE_1:37;
      consider x1 being set such that
A89:  x1 in (pb \ p0) by A88,XBOOLE_0:def 1;
A90:  not x1 in p0 by A89,XBOOLE_0:def 5;
      consider y1 being set such that
A91:  x1 in y1 and
A92:  y1 in Cb by A81,A89,TARSKI:def 4;
A93:  ex p being Element of PB st y1=p & ex q being set st q
      in Ca & p /\ q <> {} by A92;
      consider q being set such that
A94:  q in Ca and
A95:  y1 /\ q <> {} by A93;
A96:  ex pp being Element of PA st q=pp & ex f being
FinSequence of Y st 1<=len f & f.1=x & f.len f in pp & for i st 1<=i & i<len f
holds ex p1,p2,x0 being set st p1 in PA & p2 in PB & f. i in p1 & x0 in (p1 /\
      p2) & f.(i+1) in p2 by A94;
      consider fd being FinSequence of Y such that
A97:  1<=len fd and
A98:  fd.1=x and
A99:  fd.len fd in q and
A100: for i st 1<=i & i<len fd holds ex p1,p2,x0 being set st p1 in PA &
      p2 in PB & fd.i in p1 & x0 in (p1 /\ p2) & fd.(i+1) in p2
      by A96;
      reconsider x1 as Element of Y by A91,A93;
      reconsider f=fd^<*x1*> as FinSequence of Y;
A101: len f = len fd + len <*x1*> by FINSEQ_1:35;
A102: len f = len fd + 1 by A101,FINSEQ_1:57;
A103: 1+1<=len fd + 1 by A97,XREAL_1:8;
A104: 1<=len f by A102,A103,XXREAL_0:2;
A105: f.(len fd + 1) in y1 by A91,FINSEQ_1:59;
A106: y1 meets q by A95,XBOOLE_0:def 7;
      consider z0 being set such that
A107: z0 in y1 & z0 in q by A106,XBOOLE_0:3;
A108: z0 in (y1 /\ q) by A107,XBOOLE_0:def 4;
A109: dom fd = Seg len fd by FINSEQ_1:def 3;
A110: for k being Element of NAT st 1 <= k & k <= len fd holds f.k=fd.k
      proof
        let k be Element of NAT;
        assume
A111:   1 <= k & k <= len fd;
A112:   k in dom fd by A109,A111,FINSEQ_1:3;
        thus thesis by A112,FINSEQ_1:def 7;
      end;
      A113: (fd^<*x1*>).(len fd + 1)=x1 & f.1=x by A97,A98,A110,FINSEQ_1:59;
A114: f.len fd in q by A97,A99,A110;
A115: for i st 1<=i & i<len fd holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
      proof
        let i;
        assume
A116:   1<=i & i<len fd;
A117:   f.i = fd.i by A110,A116;
A118:   1<=(i+1) & (i+1)<=len fd by A116,NAT_1:13;
A119:   f.(i+1) = fd.(i+1) by A110,A118;
        thus thesis by A100,A116,A117,A119;
      end;
A120: for i st 1<=i & i<len f holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
      proof
        let i;
        assume that
A121:   1<=i and
A122:   i<len f;
A123:   i<= len fd by A102,A122,NAT_1:13;
A124:   now per cases by A121,A123,XXREAL_0:1;
          case
A125:       1<=i & i<len fd;
            thus thesis by A115,A125;
          end;
          case
A126:       1<=i & i=len fd;
            thus thesis by A93,A96,A105,A108,A114,A126;
          end;
        end;
        thus thesis by A124;
      end;
A127: for i st 1<=i & i<len f holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in p1 & x0 in p2 & f.(i+1) in p2
      proof
        let i;
        assume
A128:   1<=i & i<len f;
        consider p1,p2,x0 being set such that
A129:   p1 in PA & p2 in PB & f.i in p1 and
A130:   x0 in (p1 /\ p2) and
A131:   f.(i+1) in p2 by A120,A128;
A132:   x0 in p1 & x0 in p2 by A130,XBOOLE_0:def 4;
        thus thesis by A129,A131,A132;
      end;
      thus contradiction by A8,A11,A12,A90,A102,A104,A113,A127,Th25;
    end;
A133: y in pb by A9,A10,A30,A85,A86,Def2;
    consider y1 being set such that
A134: y in y1 and
A135: y1 in Cb by A31,A133,TARSKI:def 4;
A136: ex p being Element of PB st y1=p & ex q being set st q
    in Ca & p /\ q <> {} by A135;
    consider q being set such that
A137: q in Ca and
A138: y1 /\ q <> {} by A136;
A139: ex pp being Element of PA st q=pp & ex f being
FinSequence of Y st 1<=len f & f.1=x & f.len f in pp & for i st 1<=i & i<len f
holds ex p1,p2,x0 being set st p1 in PA & p2 in PB & f. i in p1 & x0 in (p1 /\
    p2) & f.(i+1) in p2 by A137;
    consider fd being FinSequence of Y such that
A140: 1<=len fd and
A141: fd.1=x and
A142: fd.len fd in q and
A143: for i st 1<=i & i<len fd holds ex p1,p2,x0 being set st p1 in PA &
    p2 in PB & fd.i in p1 & x0 in (p1 /\ p2) & fd.(i+1) in p2
    by A139;
    reconsider y' = y as Element of Y by A9;
    reconsider f=fd^<*y'*> as FinSequence of Y;
A144: len f = len fd + len <*y'*> by FINSEQ_1:35;
A145: len f = len fd + 1 by A144,FINSEQ_1:57;
A146: 1+1<=len f by A140,A145,XREAL_1:8;
A147: f.(len fd + 1) in y1 by A134,FINSEQ_1:59;
A148: y1 meets q by A138,XBOOLE_0:def 7;
    consider z0 being set such that
A149: z0 in y1 & z0 in q by A148,XBOOLE_0:3;
A150: z0 in (y1 /\ q) by A149,XBOOLE_0:def 4;
A151: dom fd = Seg len fd by FINSEQ_1:def 3;
A152: for k being Element of NAT st 1 <= k & k <= len fd holds f.k=fd.k
    proof
      let k be Element of NAT;
      assume
A153: 1 <= k & k <= len fd;
A154: k in dom fd by A151,A153,FINSEQ_1:3;
      thus thesis by A154,FINSEQ_1:def 7;
    end;
A155: f.len fd in q by A140,A142,A152;
A156: for i st 1<=i & i<len fd holds
    ex p1,p2,x0 being set st p1 in PA & p2 in PB
    & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
    proof
      let i;
      assume
A157: 1<=i & i<len fd;
A158: f.i = fd.i by A152,A157;
A159: 1<=(i+1) & (i+1)<=len fd by A157,NAT_1:13;
A160: f.(i+1) = fd.(i+1) by A152,A159;
      thus thesis by A143,A157,A158,A160;
    end;
A161: for i st 1<=i & i<len f holds
    ex p1,p2,x0 being set st p1 in PA & p2 in PB
    & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
    proof
      let i;
      assume that
A162: 1<=i and
A163: i<len f;
A164: i<= len fd by A145,A163,NAT_1:13;
A165: now per cases by A162,A164,XXREAL_0:1;
        case
A166:     1<=i & i<len fd;
          thus thesis by A156,A166;
        end;
        case
A167:     1<=i & i=len fd;
          thus thesis by A136,A139,A147,A150,A155,A167;
        end;
      end;
      thus thesis by A165;
    end;
    A168: (fd^<*y'*>).(len fd + 1)=y & 1<=len f by A146,FINSEQ_1:59,XXREAL_0:2;
A169: f.1=x by A140,A141,A152;
A170: for i st 1<=i & i<len f holds
    ex u being set st u in Y & [f.i,u] in (ERl(PA) \/ ERl(PB))
    & [u,f.(i+1)] in (ERl(PA) \/ ERl(PB))
    proof
      let i;
      assume
A171: 1<=i & i<len f;
      consider p1,p2,u being set such that
A172: p1 in PA and
A173: p2 in PB and
A174: f.i in p1 and
A175: u in (p1 /\ p2) and
A176: f.(i+1) in p2 by A161,A171;
A177: u in p1 by A175,XBOOLE_0:def 4;
A178: u in p2 by A175,XBOOLE_0:def 4;
      reconsider x2=f.i as set;
      reconsider y2=f.(i+1) as set;
A179: [x2,u] in ERl(PA) by A172,A174,A177,Def6;
A180: [u,y2] in ERl(PB) by A173,A176,A178,Def6;
      A181: ERl
(PA) c= ERl(PA) \/ ERl(PB) & ERl(PB) c= ERl(PA) \/ ERl(PB) by XBOOLE_1:7;
      thus thesis by A172,A177,A179,A180,A181;
    end;
A182: [x',y'] in (ERl(PA) "\/" ERl(PB)) by A145,A168,A169,A170,Th26;
    thus thesis by A182;
  end;
A183: for x1,x2 being set st [x1,x2] in (ERl(PA) \/ ERl(PB)) holds
  [x1,x2] in ERl(PA '\/' PB)
  proof
    let x1,x2 be set;
    assume
A184: [x1,x2] in (ERl(PA) \/ ERl(PB));
A185: [x1,x2] in ERl(PA) or [x1,x2] in ERl(PB) by A184,XBOOLE_0:def 3;
A186: (ex A being Subset of Y st A in PA & x1 in A & x2 in A) or
    ex B being Subset of Y st B in PB & x1 in B & x2 in B by A185,Def6;
A187: now per cases by A186;
      case
A188:   x1 in Y & x2 in Y & ex A being Subset of Y
        st A in PA & x1 in A & x2 in A;
        consider A being Subset of Y such that
A189:   A in PA and
A190:   x1 in A & x2 in A by A188;
A191:   ex y st y in (PA '\/' PB) & A c= y by A1,A189,SETFAM_1:def 2;
        thus thesis by A190,A191,Def6;
      end;
      case
A192:   x1 in Y & x2 in Y & ex B being Subset of Y
        st B in PB & x1 in B & x2 in B;
        consider B being Subset of Y such that
A193:   B in PB and
A194:   x1 in B & x2 in B by A192;
A195:   ex y st y in (PA '\/' PB) & B c= y by A2,A193,SETFAM_1:def 2;
        thus thesis by A194,A195,Def6;
      end;
    end;
    thus thesis by A187;
  end;
A196: (ERl(PA) \/ ERl(PB)) c= ERl(PA '\/' PB) by A183,RELAT_1:def 3;
A197: (ERl(PA) "\/" ERl(PB)) c= ERl(PA '\/' PB) by A196,EQREL_1:def 3;
  thus thesis by A5,A197,XBOOLE_0:def 10;
end;

theorem Th28:
  for PA,PB being a_partition of Y holds ERl(PA '/\' PB) = ERl(PA) /\ ERl(PB)
proof
  let PA,PB be a_partition of Y;
A1: PA '>' PA '/\' PB by Th17;
A2: PB '>' PA '/\' PB by Th17;
A3: for x1,x2 being set holds
  [x1,x2] in ERl(PA '/\' PB) iff [x1,x2] in (ERl(PA) /\ ERl(PB))
  proof
    let x1,x2 be set;
    hereby
      assume
A4:   [x1,x2] in ERl(PA '/\' PB);
      consider C being Subset of Y such that
A5:   C in (PA '/\' PB) and
A6:   x1 in C & x2 in C by A4,Def6;
A7:   ex A being set st A in PA & C c= A by A1,A5,SETFAM_1:def 2;
A8:   ex B being set st B in PB & C c= B by A2,A5,SETFAM_1:def 2;
A9:   [x1,x2] in ERl(PA) by A6,A7,Def6;
A10:  [x1,x2] in ERl(PB) by A6,A8,Def6;
      thus [x1,x2] in (ERl(PA) /\ ERl(PB)) by A9,A10,XBOOLE_0:def 4;
    end;
    assume
A11: [x1,x2] in (ERl(PA) /\ ERl(PB));
A12: [x1,x2] in ERl(PA) by A11,XBOOLE_0:def 4;
A13: [x1,x2] in ERl(PB) by A11,XBOOLE_0:def 4;
    consider A being Subset of Y such that
A14: A in PA and
A15: x1 in A and
A16: x2 in A by A12,Def6;
    consider B being Subset of Y such that
A17: B in PB and
A18: x1 in B and
A19: x2 in B by A13,Def6;
A20: A /\ B <> {} by A15,A18,XBOOLE_0:def 4;
    consider C being Subset of Y such that
A21: C = A /\ B;
A22: C in INTERSECTION(PA,PB) by A14,A17,A21,SETFAM_1:def 5;
A23: not C in {{}} by A20,A21,TARSKI:def 1;
A24: C in INTERSECTION(PA,PB) \ {{}} by A22,A23,XBOOLE_0:def 5;
    A25: x1 in C & x2 in C by A15,A16,A18,A19,A21,XBOOLE_0:def 4;
    thus thesis by A24,A25,Def6;
  end;
  thus thesis by A3,RELAT_1:def 2;
end;

theorem Th29:
  for PA,PB being a_partition of Y st ERl(PA) = ERl(PB) holds PA = PB
proof
  let PA,PB be a_partition of Y;
  assume
A1: ERl(PA)=ERl(PB);
A2: PA '<' PB & PB '<' PA by A1,Th24;
  thus thesis by A2,Th5;
end;

theorem
  for PA,PB,PC being a_partition of Y holds
  (PA '\/' PB) '\/' PC = PA '\/' (PB '\/' PC)
proof
  let PA,PB,PC be a_partition of Y;
A1: ERl((PA '\/' PB) '\/' PC) = ERl(PA '\/' PB) "\/" ERl(PC) by Th27
    .= (ERl(PA) "\/" ERl(PB)) "\/" ERl(PC) by Th27
    .= ERl(PA) "\/" (ERl(PB) "\/" ERl(PC)) by EQREL_1:21
    .= ERl(PA) "\/" ERl(PB '\/' PC) by Th27
    .= ERl(PA '\/' (PB '\/' PC)) by Th27;
  thus thesis by A1,Th29;
end;

theorem
  for PA,PB being a_partition of Y holds PA '/\' (PA '\/' PB) = PA
proof
  let PA,PB be a_partition of Y;
  A1: ERl
(PA '/\' (PA '\/' PB)) = ERl(PA) /\ ERl(PA '\/' PB) & ERl(PA) /\ ERl(PA
  '\/' PB) = ERl(PA) /\ (ERl(PA) "\/" ERl(PB)) by Th27,Th28;
  thus thesis by A1,Th29,EQREL_1:24;
end;

theorem
  for PA,PB being a_partition of Y holds PA '\/' (PA '/\' PB) = PA
proof
  let PA,PB be a_partition of Y;
  A1: ERl
(PA '\/' (PA '/\' PB)) = ERl(PA) "\/" ERl(PA '/\' PB) & ERl(PA) "\/" ERl(
  PA '/\' PB) = ERl(PA) "\/" (ERl(PA) /\ ERl(PB)) by Th27,Th28;
  thus thesis by A1,Th29,EQREL_1:25;
end;

theorem Th33:
  for PA,PB,PC being a_partition of Y st
  PA '<' PC & PB '<' PC holds PA '\/' PB '<' PC
proof
  let PA,PB,PC be a_partition of Y;
  assume
A1: PA '<' PC & PB '<' PC;
A2: ERl(PA) c= ERl(PC) & ERl(PB) c= ERl(PC) by A1,Th24;
A3: ERl(PA '\/' PB) = ERl(PA) "\/" ERl(PB) by Th27;
A4: ERl(PA) \/ ERl(PB) c= ERl(PC) by A2,XBOOLE_1:8;
A5: ERl(PA) "\/" ERl(PB) c= ERl(PC) by A4,EQREL_1:def 3;
  thus thesis by A3,A5,Th24;
end;

theorem
  for PA,PB,PC being a_partition of Y st
  PA '>' PC & PB '>' PC holds PA '/\' PB '>' PC
proof
  let PA,PB,PC be a_partition of Y;
  assume
A1: PA '>' PC & PB '>' PC;
A2: ERl(PC) c= ERl(PA) & ERl(PC) c= ERl(PB) by A1,Th24;
A3: ERl(PA '/\' PB) = ERl(PA) /\ ERl(PB) by Th28;
A4: ERl(PC) c= ERl(PA '/\' PB) by A2,A3,XBOOLE_1:19;
  thus thesis by A4,Th24;
end;

notation
  let Y;
  synonym %I(Y) for SmallestPartition Y;
end;

definition
  let Y;
  canceled;
  func %O(Y) -> a_partition of Y equals
  {Y};
  correctness
  proof
A1: {Y} is Subset-Family of Y & union {Y} = Y by ZFMISC_1:31,80;
A2: for A st A in {Y} holds A<>{} &
    for B st B in {Y} holds A = B or A misses B
    proof
      let A;
      assume
A3:   A in {Y};
A4:   A=Y by A3,TARSKI:def 1;
      thus A<>{} by A3,TARSKI:def 1;
      let B;
      assume
A5:   B in {Y};
      thus thesis by A4,A5,TARSKI:def 1;
    end;
    thus thesis by A1,A2,EQREL_1:def 6;
  end;
end;

theorem
  %I(Y)={B:ex x being set st B={x} & x in Y}
proof
  set B0={B:ex x being set st B={x} & x in Y};
A1: %I(Y) c= B0
  proof
    let a be set;
    assume
A2: a in %I(Y);
A3: a in {{x} where x is Element of Y: not contradiction} by A2,EQREL_1:46;
    consider x be Element of Y such that
A4: a={x} by A3;
    reconsider y=x as Element of Y;
    reconsider B={y} as Subset of Y by ZFMISC_1:37;
A5: a=B by A4;
    thus thesis by A5;
  end;
A6: B0 c= %I(Y)
  proof
    let x1;
    assume
A7: x1 in B0;
A8: ex B st x1=B & ex x being set st B={x} & x in Y by A7;
A9: x1 in {{z} where z is Element of Y: not contradiction} by A8;
    thus thesis by A9,EQREL_1:46;
  end;
  thus thesis by A1,A6,XBOOLE_0:def 10;
end;

theorem Th36:
  for PA being a_partition of Y holds %O(Y) '>' PA & PA '>' %I(Y)
proof
  let PA be a_partition of Y;
A1: for a being set st a in PA ex b being set st b in %O(Y) & a c= b
  proof
    let a be set;
    assume
A2: a in PA;
A3: a c= Y by A2;
A4: a<>{} by A2,EQREL_1:def 6;
    consider x being Element of a;
A5: x in Y by A2,A4,TARSKI:def 3;
A6: union %O(Y) = Y by EQREL_1:def 6;
    consider b being set such that
    x in b and
A7: b in %O(Y) by A5,A6,TARSKI:def 4;
A8: a c= b by A3,A7,TARSKI:def 1;
    thus thesis by A7,A8;
  end;
A9: for a being set st a in %I(Y) ex b being set st b in PA & a c= b
  proof
    let a be set;
    assume
A10: a in %I(Y);
A11: a in {{x} where x is Element of Y: not contradiction} by A10,
    EQREL_1:46;
    consider x be Element of Y such that
A12: a={x} by A11;
A13: a<>{} by A10,EQREL_1:def 6;
    consider u being Element of a;
A14: u=x by A12,TARSKI:def 1;
A15: u in Y by A10,A13,TARSKI:def 3;
A16: union PA = Y by EQREL_1:def 6;
    consider b being set such that
A17: u in b and
A18: b in PA by A15,A16,TARSKI:def 4;
A19: a c= b
    proof
      let x1;
      assume
A20:  x1 in a;
      thus thesis by A12,A14,A17,A20,TARSKI:def 1;
    end;
    thus thesis by A18,A19;
  end;
  thus thesis by A1,A9,SETFAM_1:def 2;
end;

theorem Th37:
  ERl(%O(Y)) = nabla Y
proof
A1: nabla Y c= ERl(%O(Y))
  proof
    let x1,x2 be set;
    assume
A2: [x1,x2] in nabla Y;
A3: Y in %O(Y) by TARSKI:def 1;
A4: x1 in Y & x2 in Y by A2,ZFMISC_1:106;
    thus thesis by A3,A4,Def6;
  end;
  thus thesis by A1,XBOOLE_0:def 10;
end;

theorem Th38:
  ERl(%I(Y)) = id Y
proof
A1: union %I(Y)=Y by EQREL_1:def 6;
A2: ERl(%I(Y)) c= id Y
  proof
    let x1,x2 be set;
    assume
A3: [x1,x2] in ERl(%I(Y));
    consider a being Subset of Y such that
A4: a in %I(Y) and
A5: x1 in a & x2 in a by A3,Def6;
    A6: %I(Y) = {{x} where x is Element of Y: not contradiction} by EQREL_1:46;
    consider x be Element of Y such that
A7: a={x} by A4,A6;
A8: x1=x & x2=x by A5,A7,TARSKI:def 1;
    thus thesis by A8,RELAT_1:def 10;
  end;
A9: id Y c= ERl(%I(Y))
  proof
    let x1,x2 be set;
    assume
A10: [x1,x2] in id Y;
A11: x1 in Y by A10,RELAT_1:def 10;
A12: x1=x2 by A10,RELAT_1:def 10;
A13: ex y being set st x1 in y & y in %I(Y) by A1,A11,TARSKI:def 4;
    thus thesis by A12,A13,Def6;
  end;
  thus thesis by A2,A9,XBOOLE_0:def 10;
end;

theorem
  %I(Y) '<' %O(Y)
proof
A1: ERl(%O(Y)) = nabla Y by Th37
    .= [:Y,Y:];
A2: ERl(%I(Y)) c= ERl(%O(Y)) by A1;
  thus thesis by A2,Th24;
end;

theorem
  for PA being a_partition of Y holds
  %O(Y) '\/' PA = %O(Y) & %O(Y) '/\' PA = PA
proof
  let PA be a_partition of Y;
A1: ERl(%O(Y) '\/' PA) = ERl(%O(Y)) "\/" ERl(PA) by Th27;
A2: ERl(%O(Y)) = nabla Y by Th37;
A3: ERl(%O(Y)) \/ ERl(PA) = ERl(%O(Y)) by A2,EQREL_1:1;
A4: ERl(%O(Y)) c= ERl(%O(Y)) "\/" ERl(PA) by A3,EQREL_1:def 3;
A5: %O(Y) '<' %O(Y) '\/' PA by A1,A4,Th24;
A6: %O(Y) '>' PA '\/' %O(Y) by Th36;
  thus %O(Y) '\/' PA = %O(Y) by A5,A6,Th5;
  A7: ERl(%O(Y) '/\' PA) = ERl(%O(Y)) /\ ERl(PA) & ERl(%O(Y)) = nabla Y by Th28
  ,Th37;
  thus %O(Y) '/\' PA = PA by A7,Th29,XBOOLE_1:28;
end;

theorem
  for PA being a_partition of Y holds
  %I(Y) '\/' PA = PA & %I(Y) '/\' PA = %I(Y)
proof
  let PA be a_partition of Y;
A1: ERl(%I(Y)) = id Y by Th38;
  A2: ERl
(%I(Y) '\/' PA) = ERl(%I(Y)) "\/" ERl(PA) & ERl(%I(Y)) \/ ERl(PA) c= ERl(
  %I(Y)) "\/" ERl(PA) by Th27,EQREL_1:def 3;
A3: ERl(%I(Y)) \/ ERl(PA) = id Y \/ ERl(PA) by Th38;
A4: %I(Y) '<' PA by Th36;
A5: ERl(%I(Y)) c= ERl(PA) by A4,Th24;
A6: for z being set st z in id Y \/ ERl(PA) holds z in ERl PA
  proof
    let z be set;
    assume
A7: z in id Y \/ ERl(PA);
A8: now per cases by A7,XBOOLE_0:def 3;
      case
A9:     z in id Y;
        thus thesis by A1,A5,A9;
      end;
      case
A10:    z in ERl(PA);
        thus thesis by A10;
      end;
    end;
    thus thesis by A8;
  end;
A11: id Y \/ ERl(PA) c= ERl(PA) by A6,TARSKI:def 3;
A12: ERl(PA) c= id Y \/ ERl(PA) by XBOOLE_1:7;
A13: id Y \/ ERl(PA) = ERl(PA) by A11,A12,XBOOLE_0:def 10;
A14: PA '<' %I(Y) '\/' PA by A2,A3,A13,Th24;
A15: %I(Y) '<' PA by Th36;
A16: %I(Y) '\/' PA '<' PA by A15,Th33;
  thus %I(Y) '\/' PA = PA by A14,A16,Th5;
A17: ERl(%I(Y) '/\' PA) = ERl(%I(Y)) /\ ERl(PA) by Th28
    .= id Y /\ ERl(PA) by Th38
    .= id Y by EQREL_1:17
    .= ERl(%I(Y)) by Th38;
  thus %I(Y) '/\' PA = %I(Y) by A17,Th29;
end;

