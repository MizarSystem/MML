:: A theory of partitions, { I }
::  by Shunichi Kobayashi and Kui Jia
::
:: Received October 5, 1998
:: Copyright (c) 1998 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XBOOLE_0, EQREL_1, SUBSET_1, TARSKI, SETFAM_1, FUNCT_1,
      RELAT_1, ZFMISC_1, FINSEQ_1, XXREAL_0, ARYTM_3, CARD_1, ORDINAL4,
      PARTIT1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, NAT_1, RELAT_1,
      FUNCT_1, FUNCT_2, SETFAM_1, FINSEQ_1, FINSEQ_4, EQREL_1, XXREAL_0;
 constructors SETFAM_1, FUNCT_2, XXREAL_0, XREAL_0, NAT_1, MEMBERED, EQREL_1,
      FINSEQ_4, RELSET_1, DOMAIN_1;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, PARTFUN1, MEMBERED, EQREL_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, RELAT_1, EQREL_1;
 theorems TARSKI, FUNCT_1, SETFAM_1, EQREL_1, ZFMISC_1, RELAT_1, NAT_1,
      FINSEQ_1, FINSEQ_2, FUNCT_2, SUBSET_1, RELSET_1, XBOOLE_0, XBOOLE_1,
      XREAL_1, XXREAL_0;
 schemes EQREL_1, NAT_1, CLASSES1, DOMAIN_1, XBOOLE_0;

begin
 
::Chap. 1  Preliminaries

reserve Y,Z for non empty set;
reserve PA,PB for a_partition of Y;
reserve A,B for Subset of Y;
reserve i,j,k for Element of NAT;
reserve x,y,z,x1,x2,y1,z0,X,V,a,b,d,t,SFX,SFY for set;

theorem Th1:
  X in PA & V in PA & X c= V implies X=V
proof
  assume that
A1: X in PA and
A2: V in PA and
A3: X c= V;
A4: X=V or X misses V by A1,A2,EQREL_1:def 6;
  consider x being Element of X;
 X<>{} by A1,EQREL_1:def 6;
then  x in X;
  hence thesis by A3,A4,XBOOLE_0:3;
end;

notation
  let SFX,SFY;
  synonym SFX '<' SFY for SFX is_finer_than SFY;
  synonym SFY '>' SFX for SFX is_finer_than SFY;
end;

canceled;

theorem Th3:
  union (SFX \ {{}}) = union SFX
proof
A1: union (SFX \ {{}}) c= (union SFX)
  proof
    let y be set;
    assume y in union (SFX \ {{}});
then  ex z being set st y in z & z in (SFX \{{} }) by TARSKI:def 4;
    hence thesis by TARSKI:def 4;
  end;
 union SFX c= union (SFX \ {{}})
  proof
    let y be set;
    assume y in union SFX;
    then consider X being set such that
A6: y in X and
A7: X in SFX by TARSKI:def 4;
 not X in {{}} by A6,TARSKI:def 1;
then  X in SFX \ {{}} by A7,XBOOLE_0:def 5;
    hence thesis by A6,TARSKI:def 4;
  end;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th4:
  for PA,PB being a_partition of Y st PA '>' PB & PB '>' PA holds PB c= PA
proof
  let PA,PB be a_partition of Y;
  assume that
A1: PA '>' PB and
A2: PB '>' PA;
  let x be set;
  assume
A3: x in PB;
  then consider V such that
A4: V in PA and
A5: x c= V by A1,SETFAM_1:def 2;
  consider W being set such that
A6: W in PB and
A7: V c= W by A2,A4,SETFAM_1:def 2;
 x=W by A3,A5,A6,A7,Th1,XBOOLE_1:1;
  hence thesis by A4,A5,A7,XBOOLE_0:def 10;
end;

theorem Th5:
  for PA,PB being a_partition of Y st PA '>' PB & PB '>' PA holds PA = PB
proof
  let PA,PB be a_partition of Y;
  assume PA '>' PB & PB '>' PA;
then  PB c= PA & PA c= PB by Th4;
  hence thesis by XBOOLE_0:def 10;
end;

canceled;

theorem Th7:
  for PA,PB being a_partition of Y st PA '>' PB holds PA is_coarser_than PB
proof
  let PA,PB be a_partition of Y;
  assume
A1: PA '>' PB;
 for x being set st x in PA ex y being set st y in PB & y c= x
  proof
    let x be set;
    assume
A3: x in PA;
then A4: x<>{} by EQREL_1:def 6;
    consider u being Element of x;
A5: u in x by A4;
 union PB = Y by EQREL_1:def 6;
    then consider y being set such that
A7: u in y and
A8: y in PB by A3,A5,TARSKI:def 4;
    consider z being set such that
A9: z in PA and
A10: y c= z by A1,A8,SETFAM_1:def 2;
 x=z or x misses z by A3,A9,EQREL_1:def 6;
    hence thesis by A4,A7,A8,A10,XBOOLE_0:3;
  end;
  hence thesis by SETFAM_1:def 3;
end;

definition
  let Y;
  let PA be a_partition of Y,b be set;
  pred b is_a_dependent_set_of PA means
  :Def1:
  ex B being set st B c= PA & B<>{} & b = union B;
end;

definition
  let Y;
  let PA,PB be a_partition of Y,b be set;
  pred b is_min_depend PA,PB means
  :Def2:
  b is_a_dependent_set_of PA &
  b is_a_dependent_set_of PB & for d being set st d c= b
  & d is_a_dependent_set_of PA & d is_a_dependent_set_of PB holds d=b;
end;

theorem Th8:
  for PA,PB being a_partition of Y
  st PA '>' PB holds for b being set st b in PA holds
  b is_a_dependent_set_of PB
proof
  let PA,PB be a_partition of Y;
  assume
A1: PA '>' PB;
A2: union PB = Y by EQREL_1:def 6;
A3: PA is_coarser_than PB by A1,Th7;
 for b being set st b in PA holds b is_a_dependent_set_of PB
  proof
    let b be set;
    assume
A5: b in PA;
    set B0={x8 where x8 is Subset of Y: x8 in PB & x8 c= b};
    consider xb be set such that
A6: xb in PB & xb c= b by A3,A5,SETFAM_1:def 3;
A7: xb in B0 by A6;
 z in B0 implies z in PB
    proof
      assume z in B0;
then   ex x8 being Subset of Y st x8=z & x8 in PB & x8 c= b;
      hence thesis;
    end;
then A11: B0 c= PB by TARSKI:def 3;
 z in b implies z in union B0
    proof
      assume
A13:  z in b;
      then consider x1 such that
A14:  z in x1 and
A15:  x1 in PB by A2,A5,TARSKI:def 4;
      consider y1 such that
A16:  y1 in PA and
A17:  x1 c= y1 by A1,A15,SETFAM_1:def 2;
  b = y1 or b misses y1 by A5,A16,EQREL_1:def 6;
then   x1 in B0 by A13,A14,A15,A17,XBOOLE_0:3;
      hence thesis by A14,TARSKI:def 4;
    end;
then A20: b c= union B0 by TARSKI:def 3;
 z in union B0 implies z in b
    proof
      assume z in union B0;
      then consider y such that
A23:  z in y and
A24:  y in B0 by TARSKI:def 4;
  ex x8 being Subset of Y st x8=y & x8 in PB & x8 c= b by A24;
      hence thesis by A23;
    end;
then  union B0 c= b by TARSKI:def 3;
then  b = union B0 by A20,XBOOLE_0:def 10;
    hence thesis by A7,A11,Def1;
  end;
  hence thesis;
end;

theorem Th9:
  for PA being a_partition of Y holds Y is_a_dependent_set_of PA
proof
  let PA be a_partition of Y;
A1: {Y} is Subset-Family of Y by ZFMISC_1:80;
A2: union {Y} = Y by ZFMISC_1:31;
   for
 A st A in {Y} holds A<>{} & for B st B in {Y} holds A = B or A misses B
  proof
    let A;
    assume
A4: A in {Y};
then A5: A=Y by TARSKI:def 1;
    thus A<>{} by A4,TARSKI:def 1;
    let B;
    assume B in {Y};
    hence thesis by A5,TARSKI:def 1;
  end;
then A7: {Y} is a_partition of Y by A1,A2,EQREL_1:def 6;
 for a being set st a in PA ex b being set st b in {Y} & a c= b
  proof
    let a be set;
    assume
A9: a in PA;
then A10: a<>{} by EQREL_1:def 6;
    consider x being Element of a;
 x in Y by A9,A10,TARSKI:def 3;
    then consider b being set such that
    x in b and
A12: b in {Y} by A2,TARSKI:def 4;
 b = Y by A12,TARSKI:def 1;
    hence thesis by A9,A12;
  end;
then A14: {Y} '>' PA by SETFAM_1:def 2;
 Y in {Y} by TARSKI:def 1;
  hence thesis by A7,A14,Th8;
end;

theorem Th10:
  for F being Subset-Family of Y st (Intersect(F)<>{} &
  for X st X in F holds X is_a_dependent_set_of PA)
  holds Intersect(F) is_a_dependent_set_of PA
proof
  let F be Subset-Family of Y;
  per cases;
  suppose
 F={};
then  Intersect(F)=Y by SETFAM_1:def 10;
    hence thesis by Th9;
  end;
  suppose
A3: F<>{};
    then reconsider F9 = F as non empty Subset-Family of Y;
    assume that
A4: Intersect(F)<>{} and
A5: for X st X in F holds X is_a_dependent_set_of PA;
    defpred P[set,set] means $2 c= PA & $2<>{} & $1=union $2;
A6: for X st X in F holds ex B being set st P[X,B]
    proof
      let X;
      assume X in F;
then    X is_a_dependent_set_of PA by A5;
      hence thesis by Def1;
    end;
    consider f being Function such that
A9: dom f = F & for X st X in F holds P[X,f.X] from CLASSES1:sch 1(A6);
 rng f c= bool bool Y
    proof
      let y be set;
      assume y in rng f;
      then consider x being set such that
A12:  x in dom f and
A13:  y = f.x by FUNCT_1:def 5;
  f.x c= PA by A9,A12;
then   y c= bool Y by A13,XBOOLE_1:1;
      hence thesis;
    end;
    then reconsider f as Function of F9, bool bool Y
    by A9,FUNCT_2:def 1,RELSET_1:11;
    defpred P[set] means $1 in F;
    deffunc S(Element of F9) = f.$1;
    reconsider T={S(X) where X is Element of F9:P[X]}
    as Subset-Family of bool Y from DOMAIN_1:sch 8;
    reconsider T as Subset-Family of bool Y;
    take B=Intersect(T);
    thus B c= PA
    proof
      let x;
      assume
A16:  x in B;
      consider X being set such that
A17:  X in F by A3,XBOOLE_0:def 1;
  f.X in T by A17;
then A19:  x in f.X by A16,SETFAM_1:58;
  f.X c= PA by A9,A17;
      hence thesis by A19;
    end;
    thus B<>{}
    proof
      consider X being set such that
A21:  X in F by A3,XBOOLE_0:def 1;
A22:  f.X in T by A21;
      consider A being set such that
A23:  A in Intersect(F) by A4,XBOOLE_0:def 1;
      reconsider A as Element of Y by A23;
      set AA = EqClass(A,PA);
A24:  A in meet F by A3,A23,SETFAM_1:def 10;
  for X st X in T holds AA in X
      proof
        let X;
        assume X in T;
        then consider z being Element of F9 such that
A27:    X=f.z and z in F;
A28:    f.z c= PA by A9;
    z = union (f.z) & A in z by A9,A24,SETFAM_1:def 1;
then     ex A0 being set st A in A0 & A0 in f.z by TARSKI:def 4;
        hence thesis by A27,A28,EQREL_1:def 8;
      end;
then   EqClass(A,PA) in meet T by A22,SETFAM_1:def 1;
      hence thesis by SETFAM_1:def 10;
    end;
A32: Intersect(F) c= union B
    proof
      let x be set;
      assume
A33:  x in Intersect(F);
then A34:  x in meet F by A3,SETFAM_1:def 10;
      reconsider x as Element of Y by A33;
      reconsider PA as a_partition of Y;
      set A = EqClass(x,PA);
      consider X being set such that
A35:  X in F by A3,XBOOLE_0:def 1;
A36:  f.X in T by A35;
  for X st X in T holds A in X
      proof
        let X;
        assume X in T;
        then consider z being Element of F9 such that
A39:    X=f.z and z in F;
A40:    f.z c= PA by A9;
    z = union (f.z) by A9;
then     x in union (f.z) by A34,SETFAM_1:def 1;
then     ex A0 being set st x in A0 & A0 in f.z by TARSKI:def 4;
        hence thesis by A39,A40,EQREL_1:def 8;
      end;
then   A in meet T by A36,SETFAM_1:def 1;
      then   x
 in A & A in Intersect(T) by A36,EQREL_1:def 8,SETFAM_1:def 10;
      hence thesis by TARSKI:def 4;
    end;
 union B c= Intersect(F)
    proof
      let x be set;
      assume
A47:  x in union B;
      consider X being set such that
A48:  X in F by A3,XBOOLE_0:def 1;
A49:  f.X in T by A48;
      consider y being set such that
A50:  x in y and
A51:  y in B by A47,TARSKI:def 4;
A52:  y in meet T by A49,A51,SETFAM_1:def 10;
  for X st X in F holds x in X
      proof
        let X;
        assume
A54:    X in F;
then     f.X in T;
then A56:    y in f.X by A52,SETFAM_1:def 1;
    X = union (f.X) by A9,A54;
        hence thesis by A50,A56,TARSKI:def 4;
      end;
then   x in meet F by A3,SETFAM_1:def 1;
      hence thesis by A3,SETFAM_1:def 10;
    end;
    hence Intersect(F) = union B by A32,XBOOLE_0:def 10;
  end;
end;

theorem Th11:
  for X0,X1 being Subset of Y st X0 is_a_dependent_set_of PA &
  X1 is_a_dependent_set_of PA & X0 meets X1
  holds X0 /\ X1 is_a_dependent_set_of PA
proof
  let X0,X1 be Subset of Y;
  assume that
A1: X0 is_a_dependent_set_of PA and
A2: X1 is_a_dependent_set_of PA and
A3: X0 meets X1;
  consider A being set such that
A4: A c= PA and A<>{} and
A5: X0 = union A by A1,Def1;
  consider B being set such that
A6: B c= PA and B<>{} and
A7: X1 = union B by A2,Def1;
A8: X0 /\ X1 c= union (A /\ B)
  proof
    let x be set;
    assume
A9: x in (X0 /\ X1);
then A10: x in X0 by XBOOLE_0:def 4;
A11: x in X1 by A9,XBOOLE_0:def 4;
    consider y being set such that
A12: x in y and
A13: y in A by A5,A10,TARSKI:def 4;
    consider z being set such that
A14: x in z and
A15: z in B by A7,A11,TARSKI:def 4;
A16: y in PA by A4,A13;
A17: z in PA by A6,A15;
 y meets z by A12,A14,XBOOLE_0:3;
then  y = z by A16,A17,EQREL_1:def 6;
then  y in A /\ B by A13,A15,XBOOLE_0:def 4;
    hence thesis by A12,TARSKI:def 4;
  end;
 union (A /\ B) c= X0 /\ X1
  proof
    let x be set;
    assume x in union (A /\ B);
    then consider y being set such that
A23: x in y and
A24: y in (A /\ B) by TARSKI:def 4;
A25: y in A by A24,XBOOLE_0:def 4;
A26: y in B by A24,XBOOLE_0:def 4;
A27: x in X0 by A5,A23,A25,TARSKI:def 4;
 x in X1 by A7,A23,A26,TARSKI:def 4;
    hence thesis by A27,XBOOLE_0:def 4;
  end;
then A29: X0 /\ X1 = union (A /\ B) by A8,XBOOLE_0:def 10;
A30: A \/ B c= PA by A4,A6,XBOOLE_1:8;
 A /\ B c= A & A c= A \/ B by XBOOLE_1:7,17;
then  A /\ B c= A \/ B by XBOOLE_1:1;
then A33: A /\ B c= PA by A30,XBOOLE_1:1;
 now
    assume
A35: A /\ B={};
 ex y being set st y in X0 & y in X1 by A3,XBOOLE_0:3;
    hence contradiction by A8,A35,XBOOLE_0:def 4,ZFMISC_1:2;
  end;
  hence thesis by A29,A33,Def1;
end;

theorem Th12:
  for X being Subset of Y holds X is_a_dependent_set_of PA & X<>Y
  implies X` is_a_dependent_set_of PA
proof
  let X be Subset of Y;
  assume that
A1: X is_a_dependent_set_of PA and
A2: X<>Y;
  consider B being set such that
A3: B c= PA and B<>{} and
A4: X=union B by A1,Def1;
  take PA \ B;
A5: union PA = Y by EQREL_1:def 6;
then A6: X` = union PA \ union B by A4,SUBSET_1:def 5;
  reconsider B as Subset of PA by A3;
 now
    assume PA \ B={};
then  PA c= B by XBOOLE_1:37;
    hence contradiction by A2,A4,A5,XBOOLE_0:def 10;
  end;
  hence thesis by A6,EQREL_1:52,XBOOLE_1:36;
end;

theorem Th13:
  for y being Element of Y
  ex X being Subset of Y st y in X & X is_min_depend PA,PB
proof
  let y be Element of Y;
A1: union PA = Y by EQREL_1:def 6;
A2: for A being set st A in PA holds A<>{} & for B being set st B in PA
  holds A=B or A misses B by EQREL_1:def 6;
A3: Y is_a_dependent_set_of PA & Y is_a_dependent_set_of PB by Th9;
  defpred P[set] means
  y in $1 & $1 is_a_dependent_set_of PA & $1 is_a_dependent_set_of PB;
  reconsider XX={X where X is Subset of Y:P[X]}
  as Subset-Family of Y from DOMAIN_1:sch 7;
  reconsider XX as Subset-Family of Y;
 Y c= Y;
then A5: Y in XX by A3;
 for X1 be set st X1 in XX holds y in X1
  proof
    let X1 be set;
    assume X1 in XX;
then  ex X be Subset of Y st X=X1 & y in X & X
    is_a_dependent_set_of PA & X is_a_dependent_set_of PB;
    hence thesis;
  end;
then A9: y in meet XX by A5,SETFAM_1:def 1;
then A10: Intersect(XX)<>{} by SETFAM_1:def 10;
  take Intersect(XX);
 for X1 be set st X1 in XX holds X1 is_a_dependent_set_of PA
  proof
    let X1 be set;
    assume X1 in XX;
then  ex X be Subset of Y st X=X1 & y in X & X is_a_dependent_set_of PA &
    X is_a_dependent_set_of PB;
    hence thesis;
  end;
then A14: Intersect(XX) is_a_dependent_set_of PA by A10,Th10;
 for X1 be set st X1 in XX holds X1 is_a_dependent_set_of PB
  proof
    let X1 be set;
    assume X1 in XX;
then  ex X be Subset of Y st X=X1 & y in X & X is_a_dependent_set_of PA &
    X is_a_dependent_set_of PB;
    hence thesis;
  end;
then A18: Intersect(XX) is_a_dependent_set_of PB by A10,Th10;
 for d being set st d c= Intersect(XX) & d is_a_dependent_set_of PA &
  d is_a_dependent_set_of PB holds d=Intersect(XX)
  proof
    let d be set;
    assume that
A20: d c= Intersect(XX) and
A21: d is_a_dependent_set_of PA and
A22: d is_a_dependent_set_of PB;
    consider Ad being set such that
A23: Ad c= PA and
A24: Ad<>{} and
A25: d = union Ad by A21,Def1;
A26: d c= Y by A1,A23,A25,ZFMISC_1:95;
    per cases;
    suppose
  y in d;
then A28:  d in XX by A21,A22,A26;
  Intersect(XX) c= d
      proof
        let X1 be set;
        assume X1 in Intersect(XX);
then     X1 in meet XX by A5,SETFAM_1:def 10;
        hence thesis by A28,SETFAM_1:def 1;
      end;
      hence thesis by A20,XBOOLE_0:def 10;
    end;
    suppose
A32:  not y in d;
      reconsider d as Subset of Y by A1,A23,A25,ZFMISC_1:95;
  d` = Y \ d by SUBSET_1:def 5;
then A34:  y in d` by A32,XBOOLE_0:def 5;
  d misses d` by SUBSET_1:44;
then A36:  d /\ d` = {} by XBOOLE_0:def 7;
  d <> Y by A32;
      then   d
` is_a_dependent_set_of PA & d` is_a_dependent_set_of PB by A21,A22,
      Th12;
then A39:  d` in XX by A34;
  Intersect(XX) c= d`
      proof
        let X1 be set;
        assume X1 in Intersect(XX);
then     X1 in meet XX by A5,SETFAM_1:def 10;
        hence thesis by A39,SETFAM_1:def 1;
      end;
then A43:  d /\ Intersect(XX) = {} by A36,XBOOLE_1:3,26;
  d /\ d c= d /\ Intersect(XX) by A20,XBOOLE_1:26;
then A45:  union Ad = {} by A25,A43;
      consider ad being set such that
A46:  ad in Ad by A24,XBOOLE_0:def 1;
A47:  ad<>{} by A2,A23,A46;
  ad c= {} by A45,A46,ZFMISC_1:92;
      hence thesis by A47;
    end;
  end;
  hence thesis by A5,A9,A14,A18,Def2,SETFAM_1:def 10;
end;

theorem
  for P being a_partition of Y for y being Element of Y
  ex A being Subset of Y st y in A & A in P
proof
  let P be a_partition of Y;
  let y be Element of Y;
  consider R being Equivalence_Relation of Y such that
A1: P = Class R by EQREL_1:43;
  take Class(R,y);
  thus y in Class(R,y) by EQREL_1:28;
  thus thesis by A1,EQREL_1:def 5;
end;

definition
  let Y be set;
  func PARTITIONS(Y) means
  :Def3:
  for x being set holds x in it iff x is a_partition of Y;
  existence
  proof
    defpred P[set] means $1 is a_partition of Y;
    consider X being set such that
A1: for x being set holds x in X iff x in bool bool Y & P[x] from
    XBOOLE_0:sch 1;
    take X;
    let x be set;
    thus x in X implies x is a_partition of Y by A1;
    assume x is a_partition of Y;
    hence thesis by A1;
  end;
  uniqueness
  proof
    let A1,A2 be set such that
A3: for x being set holds x in A1 iff x is a_partition of Y and
A4: for x being set holds x in A2 iff x is a_partition of Y;
 now
      let y be set;
   y in A1 iff y is a_partition of Y by A3;
      hence y in A1 iff y in A2 by A4;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let Y be set;
  cluster PARTITIONS(Y) -> non empty;
  coherence
  proof
    per cases;
    suppose
   Y={};
      hence thesis by Def3,EQREL_1:54;
    end;
    suppose
   Y<>{};
      then reconsider Y as non empty set;
      A3:   {Y} is Subset-Family of Y & union {Y} = Y by ZFMISC_1:31,80;
   for A being Subset of Y st A in {Y} holds A<>{} &
      for B being Subset of Y st B in {Y} holds A = B or A misses B
      proof
        let A be Subset of Y;
        assume
A5:     A in {Y};
then A6:     A=Y by TARSKI:def 1;
        thus A<>{} by A5,TARSKI:def 1;
        let B be Subset of Y;
        assume B in {Y};
        hence thesis by A6,TARSKI:def 1;
      end;
then    {Y} is a_partition of Y by A3,EQREL_1:def 6;
      hence thesis by Def3;
    end;
  end;
end;

begin
 
::Chap. 2  Join and Meet Operation between Partitions

definition
  let Y;
  let PA,PB be a_partition of Y;
  func PA '/\' PB -> a_partition of Y equals
  INTERSECTION(PA,PB) \ {{}};
  correctness
  proof
 {} c= Y by XBOOLE_1:2;
    then reconsider e = {{}} as Subset-Family of Y by ZFMISC_1:37;
    set a = INTERSECTION(PA,PB);
 for z being set st z in a holds z in bool Y
    proof
      let z be set;
      assume z in a;
then    ex M,N being set st M in PA & N in PB & z = M /\ N by
      SETFAM_1:def 5;
then    z c= Y by XBOOLE_1:108;
      hence thesis;
    end;
    then
reconsider a = INTERSECTION(PA,PB) as Subset-Family of Y by TARSKI:def 3
    ;
    reconsider ia = a \ e as Subset-Family of Y;
A6: union PA = Y & union PB = Y by EQREL_1:def 6;
A7: union ia = union INTERSECTION(PA,PB) by Th3
      .= union PA /\ union PB by SETFAM_1:39
      .= Y by A6;
 for A being Subset of Y st A in ia holds A<>{} &
    for B being Subset of Y st B in ia holds A = B or A misses B
    proof
      let A be Subset of Y;
      assume
A9:   A in ia;
then A10:  not A in {{}} by XBOOLE_0:def 5;
A11:  A in INTERSECTION(PA,PB) by A9,XBOOLE_0:def 5;
  for B being Subset of Y st B in ia holds A = B or A misses B
      proof
        A13:    for
 m,n,o,p being set holds (m /\ n) /\ (o /\ p) = m /\ ( n /\ o /\ p)
        proof
          let m,n,o,p be set;
      (m /\ n) /\ (o /\ p) = m /\ ( n /\ (o /\ p) ) by XBOOLE_1:16
            .= m /\ ( n /\ o /\ p) by XBOOLE_1:16;
          hence thesis;
        end;
        let B be Subset of Y;
        assume B in ia;
then     B in INTERSECTION(PA,PB) by XBOOLE_0:def 5;
        then consider M,N being set such that
A17:    M in PA & N in PB and
A18:    B = M /\ N by SETFAM_1:def 5;
        consider S,T being set such that
A19:    S in PA & T in PB and
A20:    A = S /\ T by A11,SETFAM_1:def 5;
    M /\ N = S /\ T or (not M = S or not N = T );
then     M /\ N = S /\ T or (M misses S or N misses T)
        by A17,A19,EQREL_1:def 6;
then A23:    M /\ N = S /\ T or ( M /\ S = {} or N /\ T = {}) by
        XBOOLE_0:def 7;
    (M /\ S) /\ (N /\ T) = M /\ ( S /\ N /\ T ) by A13
          .=( M /\ N ) /\ ( S /\ T ) by A13;
        hence thesis by A18,A20,A23,XBOOLE_0:def 7;
      end;
      hence thesis by A10,TARSKI:def 1;
    end;
    hence thesis by A7,EQREL_1:def 6;
  end;
  commutativity;
end;

theorem
  for PA being a_partition of Y holds PA '/\' PA = PA
proof
  let PA be a_partition of Y;
 for u being set st u in INTERSECTION(PA,PA) \ {{}} ex v being set
  st v in PA & u c= v
  proof
    let u be set;
    assume u in INTERSECTION(PA,PA) \ {{}};
    then consider v,u2 being set such that
A3: v in PA and u2 in PA and
A4: u = v /\ u2 by SETFAM_1:def 5;
    take v;
    thus thesis by A3,A4,XBOOLE_1:17;
  end;
then A5: INTERSECTION(PA,PA) \ {{}} '<' PA by SETFAM_1:def 2;
 for u being set st u in PA
  ex v being set st v in INTERSECTION(PA,PA) \ {{}} & u c= v
  proof
    let u be set;
    assume
A7: u in PA;
then A8: u <> {} by EQREL_1:def 6;
    set v = u /\ u;
A9: not v in {{}} by A8,TARSKI:def 1;
 v in INTERSECTION(PA,PA) by A7,SETFAM_1:def 5;
then  v in INTERSECTION(PA,PA) \ {{}} by A9,XBOOLE_0:def 5;
    hence thesis;
  end;
then  PA '<' INTERSECTION(PA,PA) \ {{}} by SETFAM_1:def 2;
  hence thesis by A5,Th5;
end;

theorem
  for PA,PB,PC being a_partition of Y holds
  (PA '/\' PB) '/\' PC = PA '/\' (PB '/\' PC)
proof
  let PA,PB,PC be a_partition of Y;
  consider PD,PE being a_partition of Y such that
A1: PD = PA '/\' PB and
A2: PE = PB '/\' PC;
 for u being set st u in PD '/\' PC
  ex v being set st v in PA '/\' PE & u c= v
  proof
    let u be set;
    assume
A4: u in PD '/\' PC;
    then consider u1, u2 being set such that
A5: u1 in PD and
A6: u2 in PC and
A7: u = u1 /\ u2 by SETFAM_1:def 5;
    consider u3, u4 being set such that
A8: u3 in PA and
A9: u4 in PB and
A10: u1 = u3 /\ u4 by A1,A5,SETFAM_1:def 5;
    consider v, v1,v2 being set such that
A11: v1 = u4 /\ u2 and
A12: v2 = u3 and
A13: v = u3 /\ u4 /\ u2;
A14: v = v2 /\ v1 by A11,A12,A13,XBOOLE_1:16;
A15: v1 in INTERSECTION(PB,PC) by A6,A9,A11,SETFAM_1:def 5;
A16: not u in {{}} by A4,XBOOLE_0:def 5;
 u = u3 /\ v1 by A7,A10,A11,XBOOLE_1:16;
then  v1 <> {} by A16,TARSKI:def 1;
then  not v1 in {{}} by TARSKI:def 1;
then  v1 in INTERSECTION(PB,PC) \ {{}} by A15,XBOOLE_0:def 5;
then A21: v in INTERSECTION(PA,PE) by A2,A8,A12,A14,SETFAM_1:def 5;
    take v;
    thus thesis by A7,A10,A13,A16,A21,XBOOLE_0:def 5;
  end;
then A22: PD '/\' PC '<' PA '/\' PE by SETFAM_1:def 2;
 for u being set st u in PA '/\' PE
  ex v being set st v in PD '/\' PC & u c= v
  proof
    let u be set;
    assume
A24: u in PA '/\' PE;
    then consider u1,u2 being set such that
A25: u1 in PA and
A26: u2 in PE and
A27: u = u1 /\ u2 by SETFAM_1:def 5;
    consider u3,u4 being set such that
A28: u3 in PB and
A29: u4 in PC and
A30: u2 = u3 /\ u4 by A2,A26,SETFAM_1:def 5;
A31: u = u1 /\ u3 /\ u4 by A27,A30,XBOOLE_1:16;
    consider v, v1,v2 being set such that
A32: v1 = u1 /\ u3 and v2 = u4 and
A33: v = u1 /\ u3 /\ u4;
A34: v1 in INTERSECTION(PA,PB) by A25,A28,A32,SETFAM_1:def 5;
A35: not u in {{}} by A24,XBOOLE_0:def 5;
then  v1 <> {} by A31,A32,TARSKI:def 1;
then  not v1 in {{}} by TARSKI:def 1;
then  v1 in INTERSECTION(PA,PB) \ {{}} by A34,XBOOLE_0:def 5;
then A39: v in INTERSECTION(PD,PC) by A1,A29,A32,A33,SETFAM_1:def 5;
    take v;
    thus thesis by A31,A33,A35,A39,XBOOLE_0:def 5;
  end;
then  PA '/\' PE '<' PD '/\' PC by SETFAM_1:def 2;
  hence thesis by A1,A2,A22,Th5;
end;

theorem Th17:
  for PA,PB being a_partition of Y holds PA '>' PA '/\' PB
proof
  let PA,PB be a_partition of Y;
 for u being set st u in PA '/\' PB ex v being set st v in PA & u c= v
  proof
    let u be set;
    assume u in PA '/\' PB;
    then consider u1,u2 being set such that
A3: u1 in PA and u2 in PB and
A4: u = u1 /\ u2 by SETFAM_1:def 5;
    take u1;
    thus thesis by A3,A4,XBOOLE_1:17;
  end;
  hence PA '>' PA '/\' PB by SETFAM_1:def 2;
end;

definition
  let Y;
  let PA,PB be a_partition of Y;
  func PA '\/' PB -> a_partition of Y means
  :Def5:
  for d holds d in it iff d is_min_depend PA,PB;
  existence
  proof
A1: union PA = Y by EQREL_1:def 6;
A2: for A being set st A in PA holds A<>{} & for B being set st B in PA
    holds A=B or A misses B by EQREL_1:def 6;
    defpred P[set] means $1 is_min_depend PA,PB;
    consider X being set such that
A3: for d being set holds d in X iff d in bool Y & P[d] from XBOOLE_0:sch 1;
A4: for d being set holds d in X iff d is_min_depend PA,PB
    proof
      let d be set;
   for d being set holds d is_min_depend PA,PB implies d in bool Y
      proof
        let d be set;
        assume d is_min_depend PA,PB;
then      d is_a_dependent_set_of PA by Def2;
then      ex A being set st A c= PA & A<>{} & d = union A by Def1;
then      d c= union PA by ZFMISC_1:95;
        hence thesis by A1;
      end;
then
  d is_min_depend PA,PB implies d is_min_depend PA,PB & d in bool Y;
      hence thesis by A3;
    end;
    take X;
A11: Y c= union X
    proof
      let y be set;
      assume y in Y;
      then consider a being Subset of Y such that
A13:  y in a and
A14:  a is_min_depend PA,PB by Th13;
  a in X by A4,A14;
      hence thesis by A13,TARSKI:def 4;
    end;
 union X c= Y
    proof
      let y be set;
      assume y in union X;
      then consider a being set such that
A18:  y in a and
A19:  a in X by TARSKI:def 4;
  a is_min_depend PA,PB by A4,A19;
then   a is_a_dependent_set_of PA by Def2;
then   ex A being set st A c= PA & A<>{} & a = union A by Def1;
then   a c= Y by A1,ZFMISC_1:95;
      hence thesis by A18;
    end;
then A24: union X = Y by A11,XBOOLE_0:def 10;
A25: for A being Subset of Y st A in X holds A<>{} &
    for B being Subset of Y st B in X holds A=B or A misses B
    proof
      let A be Subset of Y;
      assume A in X;
then A27:  A is_min_depend PA,PB by A4;
then A28:  A is_a_dependent_set_of PA by Def2;
A29:  A is_a_dependent_set_of PB by A27,Def2;
      consider Aa being set such that
A30:  Aa c= PA and
A31:  Aa<>{} and
A32:  A = union Aa by A28,Def1;
      consider aa being set such that
A33:  aa in Aa by A31,XBOOLE_0:def 1;
A34:  aa<>{} by A2,A30,A33;
  aa c= union Aa by A33,ZFMISC_1:92;
      hence A<>{} by A32,A34;
      let B be Subset of Y;
      assume B in X;
then A37:  B is_min_depend PA,PB by A4;
      then
A38:  B is_a_dependent_set_of PA & B is_a_dependent_set_of PB by Def2
      ;
  now
        assume A meets B;
        then A41:    A
 /\ B is_a_dependent_set_of PA & A /\ B is_a_dependent_set_of PB by A28
        ,A29,A38,Th11;
    A /\ B c= A by XBOOLE_1:17;
then A43:    A /\ B = A by A27,A41,Def2;
A44:    A c= B
        proof
          let x be set;
          assume x in A;
          hence thesis by A43,XBOOLE_0:def 4;
        end;
    A /\ B c= B by XBOOLE_1:17;
then A47:    A /\ B = B by A37,A41,Def2;
    B c= A
        proof
          let x be set;
          assume x in B;
          hence thesis by A47,XBOOLE_0:def 4;
        end;
        hence A=B by A44,XBOOLE_0:def 10;
      end;
      hence thesis;
    end;
 X c= bool Y
    proof
      let x be set;
      assume x in X;
then   x is_min_depend PA,PB by A4;
then   x is_a_dependent_set_of PA by Def2;
then   ex a being set st a c= PA & a<>{} & x = union a by Def1;
then   x c= Y by A1,ZFMISC_1:95;
      hence thesis;
    end;
    then reconsider X as Subset-Family of Y;
 X is a_partition of Y by A24,A25,EQREL_1:def 6;
    hence thesis by A4;
  end;
  uniqueness
  proof
    let A1,A2 be a_partition of Y such that
A57: for x being set holds x in A1 iff x is_min_depend PA,PB and
A58: for x being set holds x in A2 iff x is_min_depend PA,PB;
 now
      let y be set;
  y in A1 iff y is_min_depend PA,PB by A57;
      hence y in A1 iff y in A2 by A58;
    end;
    hence thesis by TARSKI:2;
  end;
  commutativity
  proof
    let IT,PA,PB be a_partition of Y;
A61: now
      let a be set;
      assume
A62:  a is_min_depend PA,PB;
      then
A63:  a is_a_dependent_set_of PB & a is_a_dependent_set_of PA by Def2
      ;
  for d being set st d c= a & d is_a_dependent_set_of PB & d
      is_a_dependent_set_of PA holds d=a by A62,Def2;
      hence a is_min_depend PB,PA by A63,Def2;
    end;
A65: now
      let a be set;
      assume
A66:  a is_min_depend PB,PA;
      then
A67:  a is_a_dependent_set_of PA & a is_a_dependent_set_of PB by Def2
      ;
  for d being set st d c= a & d is_a_dependent_set_of PA & d
      is_a_dependent_set_of PB holds d=a by A66,Def2;
      hence a is_min_depend PA,PB by A67,Def2;
    end;
 (for d being set holds (d in a) iff (d is_min_depend PA,PB)) implies
    for d being set holds d in a iff d is_min_depend PB,PA
    proof
      assume
A70:  for d being set holds d in a iff d is_min_depend PA,PB;
      let d be set;
A71:  d in a iff d is_min_depend PA,PB by A70;
      hence d in a implies d is_min_depend PB,PA by A61;
      assume d is_min_depend PB,PA;
      hence thesis by A65,A71;
    end;
    hence thesis;
  end;
end;

canceled;

theorem Th19:
  for PA,PB being a_partition of Y holds PA '<' PA '\/' PB
proof
  thus PA '<' PA '\/' PB
  proof
 for a being set st a in PA ex b being set st b in PA '\/' PB & a c= b
    proof
      let a be set;
      assume
A2:   a in PA;
then A3:   a<>{} by EQREL_1:def 6;
      consider x being Element of a;
A4:   x in Y by A2,A3,TARSKI:def 3;
   union (PA '\/' PB) = Y by EQREL_1:def 6;
      then consider b being set such that
A6:   x in b and
A7:   b in PA '\/' PB by A4,TARSKI:def 4;
   b is_min_depend PA,PB by A7,Def5;
then    b is_a_dependent_set_of PA by Def2;
      then consider B being set such that
A10:  B c= PA and B<>{} and
A11:  b = union B by Def1;
  a in B
      proof
        consider u being set such that
A13:    x in u and
A14:    u in B by A6,A11,TARSKI:def 4;
    a /\ u <> {} by A3,A13,XBOOLE_0:def 4;
then A16:    not a misses u by XBOOLE_0:def 7;
    u in PA by A10,A14;
        hence thesis by A2,A14,A16,EQREL_1:def 6;
      end;
      hence thesis by A7,A11,ZFMISC_1:92;
    end;
    hence thesis by SETFAM_1:def 2;
  end;
end;

theorem
  for PA being a_partition of Y holds PA '\/' PA = PA
proof
  let PA be a_partition of Y;
A1: PA '<' PA '\/' PA by Th19;
 for a being set st a in PA '\/' PA ex b being set st b in PA & a c= b
  proof
    let a be set;
    assume
A3: a in PA '\/' PA;
then A4: a is_min_depend PA,PA by Def5;
then  a is_a_dependent_set_of PA by Def2;
    then consider B being set such that
A6: B c= PA and B<>{} and
A7: a = union B by Def1;
A8: a <> {} by A3,EQREL_1:def 6;
    consider x being Element of a;
 x in a by A8;
then  x in Y by A3;
then  x in union PA by EQREL_1:def 6;
    then consider b being set such that
A12: x in b and
A13: b in PA by TARSKI:def 4;
 b in B
    proof
      consider u being set such that
A15:  x in u and
A16:  u in B by A7,A8,TARSKI:def 4;
  b /\ u <> {} by A12,A15,XBOOLE_0:def 4;
then A18:  not b misses u by XBOOLE_0:def 7;
  u in PA by A6,A16;
      hence thesis by A13,A16,A18,EQREL_1:def 6;
    end;
then A20: b c= a by A7,ZFMISC_1:92;
 b is_a_dependent_set_of PA by A13,Th8;
then  a = b by A4,A20,Def2;
    hence thesis by A13;
  end;
then  PA '\/' PA '<' PA by SETFAM_1:def 2;
  hence thesis by A1,Th5;
end;

theorem Th21:
  for PA,PC being a_partition of Y st
  PA '<' PC & x in PC & z0 in PA & t in x & t in z0 holds z0 c= x
proof
  let PA,PC be a_partition of Y;
  assume that
A1: PA '<' PC and
A2: x in PC and
A3: z0 in PA and
A4: t in x & t in z0;
  consider b such that
A5: b in PC and
A6: z0 c= b by A1,A3,SETFAM_1:def 2;
 x = b or x misses b by A2,A5,EQREL_1:def 6;
  hence thesis by A4,A6,XBOOLE_0:3;
end;

theorem
  for PA,PB being a_partition of Y st
  x in (PA '\/' PB) & z0 in PA & t in x & t in z0 holds z0 c= x by Th19,Th21;

begin
 
::Chap. 3  Partitions and Equivalence Relations

theorem Th23:
  for PA being a_partition of Y ex RA being Equivalence_Relation of Y st
  for x,y holds [x,y] in RA iff ex A st A in PA & x in A & y in A
proof
  let PA be a_partition of Y;
A1: union PA = Y by EQREL_1:def 6;
 ex RA being Equivalence_Relation of Y st
  for x,y holds [x,y] in RA iff ex A st A in PA & x in A & y in A
  proof
    defpred P[set,set] means ex A st A in PA & $1 in A & $2 in A;
A3: for x st x in Y holds P[x,x]
    proof
      let x;
      assume x in Y;
then    ex Z being set st x in Z & Z in PA by A1,TARSKI:def 4;
      then consider Z such that
A6:   x in Z and
A7:   Z in PA;
      reconsider A = Z as Subset of Y by A7;
      take A;
      thus thesis by A6,A7;
    end;
A8: for x,y st P[x,y] holds P[y,x];
A9: for x,y,z st P[x,y] & P[y,z] holds P[x,z]
    proof
      let x,y,z;
      given A such that
A10:  A in PA and
A11:  x in A & y in A;
      given B such that
A12:  B in PA and
A13:  y in B & z in B;
  A = B or A misses B by A10,A12,EQREL_1:def 6;
      hence thesis by A10,A11,A13,XBOOLE_0:3;
    end;
    consider RA being Equivalence_Relation of Y such that
A15: for x,y holds [x,y] in RA iff
    x in Y & y in Y & P[x,y] from EQREL_1:sch 1(A3,A8,A9);
    take RA;
    thus thesis by A15;
  end;
  hence thesis;
end;

definition
  let Y;
  let PA be a_partition of Y;
  func ERl PA -> Equivalence_Relation of Y means
  :Def6:
  for x1,x2 being set holds
  [x1,x2] in it iff ex A st A in PA & x1 in A & x2 in A;
  existence by Th23;
  uniqueness
  proof
    let f1,f2 be Equivalence_Relation of Y such that
A1: for x1,x2 being set holds [x1,x2] in f1 iff
    ex A st A in PA & x1 in A & x2 in A and
A2: for x1,x2 being set holds [x1,x2] in f2 iff
    ex A st A in PA & x1 in A & x2 in A;
    let x1,x2 be set;
 [x1,x2] in f1 iff ex A st A in PA & x1 in A & x2 in A by A1;
    hence thesis by A2;
  end;
end;

definition
  let Y;
  defpred P[set,set] means ex PA st PA = $1 & $2 = ERl PA;
A1: for x st x in PARTITIONS(Y) ex z st P[x,z]
  proof
    let x;
    assume x in PARTITIONS(Y);
    then reconsider x as a_partition of Y by Def3;
    take ERl x;
    thus thesis;
  end;
  func Rel(Y) -> Function means
  dom it = PARTITIONS(Y) &
  for x st x in PARTITIONS(Y) ex PA st PA = x & it.x = ERl PA;
  existence
  proof
    thus ex f being Function st dom f = PARTITIONS(Y)
    & for x st x in PARTITIONS(Y) holds P[x,f.x] from CLASSES1:sch 1(A1);
  end;
  uniqueness
  proof
    let f1,f2 be Function such that
A3: dom f1 = PARTITIONS(Y) and
A4: for x st x in PARTITIONS(Y) ex PA st PA = x & f1.x = ERl PA and
A5: dom f2 = PARTITIONS(Y) and
A6: for x st x in PARTITIONS(Y) ex PA st PA = x & f2.x = ERl PA;
 for z st z in PARTITIONS(Y) holds f1.z = f2.z
    proof
      let x;
      assume x in PARTITIONS(Y);
      then    (
 ex PA st PA = x & f1.x = ERl PA)& ex PA st PA = x & f2.x = ERl PA by A4,A6
      ;
      hence thesis;
    end;
    hence thesis by A3,A5,FUNCT_1:9;
  end;
end;

theorem Th24:
  for PA,PB being a_partition of Y holds PA '<' PB iff ERl(PA) c= ERl(PB)
proof
  let PA,PB be a_partition of Y;
  set RA = ERl PA, RB = ERl PB;
  hereby
    assume
A1: PA '<' PB;
 for x1,x2 being set holds [x1,x2] in RA implies [x1,x2] in RB
    proof
      let x1,x2 be set;
      assume [x1,x2] in RA;
      then consider A being Subset of Y such that
A4:   A in PA and
A5:   x1 in A & x2 in A by Def6;
   ex y st y in PB & A c= y by A1,A4,SETFAM_1:def 2;
      hence thesis by A5,Def6;
    end;
    hence ERl(PA) c= ERl(PB) by RELAT_1:def 3;
  end;
  assume
A7: ERl(PA) c= ERl(PB);
 for x st x in PA ex y st y in PB & x c= y
  proof
    let x;
    assume
A9: x in PA;
then A10: x<>{} by EQREL_1:def 6;
    consider x0 being Element of x;
    set y={z where z is Element of Y:[x0,z] in ERl(PB)};
A11: x c= y
    proof
      let x1;
      assume
A12:  x1 in x;
then   [x0,x1] in RA by A9,Def6;
      hence thesis by A7,A9,A12;
    end;
    consider x1 being Element of x;
 [x0,x1] in RA by A9,A10,Def6;
    then consider B being Subset of Y such that
A15: B in PB and
A16: x0 in B and x1 in B by A7,Def6;
A17: y c= B
    proof
      let x2;
      assume x2 in y;
then   ex z being Element of Y st z=x2 & [x0,z] in ERl(PB);
      then consider B2 being Subset of Y such that
A20:  B2 in PB and
A21:  x0 in B2 and
A22:  x2 in B2 by Def6;
  B2 meets B by A16,A21,XBOOLE_0:3;
      hence thesis by A15,A20,A22,EQREL_1:def 6;
    end;
 B c= y
    proof
      let x2;
      assume
A25:  x2 in B;
then   [x0,x2] in RB by A15,A16,Def6;
      hence thesis by A25;
    end;
then  y=B by A17,XBOOLE_0:def 10;
    hence thesis by A11,A15;
  end;
  hence thesis by SETFAM_1:def 2;
end;

theorem Th25:
  for PA,PB being a_partition of Y,p0,x,y being set,
  f being FinSequence of Y st p0 c= Y &
  x in p0 & f.1=x & f.len f=y & 1 <= len f & (for i st 1<=i & i<len f
  ex p2,p3,u being set st p2 in PA & p3 in PB &
  f.i in p2 & u in p2 & u in p3 & f.(i+1) in p3) &
  p0 is_a_dependent_set_of PA & p0 is_a_dependent_set_of PB holds y in p0
proof
  let PA,PB be a_partition of Y, p0,x,y be set, f be FinSequence of Y;
  assume that
A1: p0 c= Y and
A2: x in p0 & f.1=x and
A3: f.len f=y & 1 <= len f and
A4: for i st 1<=i & i<len f ex p2,p3,u being set st p2 in PA & p3 in PB
  & f.i in p2 & u in p2 & u in p3 & f.(i+1) in p3 and
A5: p0 is_a_dependent_set_of PA and
A6: p0 is_a_dependent_set_of PB;
  consider A1 being set such that
A7: A1 c= PA and A1<>{} and
A8: p0 = union A1 by A5,Def1;
  consider B1 being set such that
A9: B1 c= PB and B1<>{} and
A10: p0 = union B1 by A6,Def1;
A11: union PA = Y by EQREL_1:def 6;
A12: for A being set st A in PA holds A<>{} & for B being set st B in PA
  holds A=B or A misses B by EQREL_1:def 6;
A13: for A being set st A in PB holds A<>{} & for B being set st B in PB
  holds A=B or A misses B by EQREL_1:def 6;
 for k st 1 <= k & k <= len f holds f.k in p0
  proof
    defpred P[Element of NAT] means 1 <= $1 & $1 <= len f implies f.$1 in p0;
A15: P[0];
A16: for k st P[k] holds P[k+1]
    proof
      let k;
      assume
A17:  P[k];
      assume that
A18:  1<=k+1 and
A19:  k+1<=len f;
A20:  k < len f by A19,NAT_1:13;
A21:  1 <= k or 1 = k + 1 by A18,NAT_1:8;
  now per cases by A21;
        suppose
A23:      1 <= k;
          then consider p2,p3,u being set such that
A24:      p2 in PA and
A25:      p3 in PB and
A26:      f.k in p2 and
A27:      u in p2 and
A28:      u in p3 and
A29:      f.(k+1) in p3 by A4,A20;
          consider A being set such that
A30:      f.k in A and
A31:      A in PA by A1,A11,A17,A19,A23,NAT_1:13,TARSKI:def 4;
A32:      p2=A or p2 misses A by A24,A31,EQREL_1:def 6;
          consider a being set such that
A33:      f.k in a and
A34:      a in A1 by A8,A17,A19,A23,NAT_1:13,TARSKI:def 4;
      a=p2 or a misses p2 by A7,A12,A24,A34;
then A36:      A c= p0 by A8,A26,A30,A32,A33,A34,XBOOLE_0:3
          ,ZFMISC_1:92;
          consider B being set such that
A37:      u in B and
A38:      B in PB by A25,A28;
A39:      p3=B or p3 misses B by A25,A38,EQREL_1:def 6;
          consider b being set such that
A40:      u in b and
A41:      b in B1 by A10,A26,A27,A30,A32,A36,TARSKI:def 4,XBOOLE_0:3;
      p3=b or p3 misses b by A9,A13,A25,A41;
then       B c= p0 by A10,A28,A37,A39,A40,A41,XBOOLE_0:3,ZFMISC_1:92;
          hence thesis by A28,A29,A37,A39,XBOOLE_0:3;
        end;
        suppose
      0 = k;
          hence thesis by A2;
        end;
      end;
      hence thesis;
    end;
    thus P[k] from NAT_1:sch 1(A15,A16);
  end;
  hence thesis by A3;
end;

theorem Th26:
  for R1,R2 being Equivalence_Relation of Y,f being FinSequence of Y,
  x,y being set st x in Y &
  f.1=x & f.len f=y & 1 <= len f &
  (for i st 1<=i & i<len f
  ex u being set st u in Y & [f.i,u] in (R1 \/ R2) & [u,f.(i+1)] in (R1 \/
  R2)) holds [x,y] in (R1 "\/" R2)
proof
  let R1,R2 be Equivalence_Relation of Y,f be FinSequence of Y, x,y be set;
  assume that
A1: x in Y and
A2: f.1=x and
A3: f.len f=y & 1 <= len f and
A4: for i st 1<=i & i<len f ex u being set st u in Y & [f.i,u] in R1 \/
  R2 & [u,f.(i+1)] in R1 \/ R2;
 for i st 1 <= i & i <= len f holds [f.1,f.i] in (R1 "\/" R2)
  proof
    defpred P[Element of NAT] means
    1<=$1 & $1<=len f implies [f.1,f.$1] in R1 "\/" R2;
A6: P[0];
A7: for i st P[i] holds P[i+1]
    proof
      let i;
      assume
A8:   P[i];
      assume that
A9:   1 <= i + 1 and
A10:  i + 1 <= len f;
A11:  i < len f by A10,NAT_1:13;
A12:  1 <= i or 1 = i + 1 by A9,NAT_1:8;
A13:  R1 \/ R2 c= R1 "\/" R2 by EQREL_1:def 3;
  now per cases by A12;
        suppose
A15:      1 <= i;
          then consider u being set such that
A16:      u in Y and
A17:      [f.i,u] in R1 \/ R2 & [u,f.(i+1)] in R1 \/ R2 by A4,A11;
          reconsider u as Element of Y by A16;
A18:      dom f = Seg len f by FINSEQ_1:def 3;
then       i in dom f by A11,A15,FINSEQ_1:3;
          then reconsider f1=f.i as Element of Y by FINSEQ_2:13;
      (i+1) in dom f by A9,A10,A18,FINSEQ_1:3;
          then reconsider f2=f.(i+1) as Element of Y by FINSEQ_2:13;
          reconsider p = <*f1,u,f2*> as FinSequence of Y;
A21:      len p = 3 by FINSEQ_1:62;
A22:      p.1 = f.i & p.3 = f.(i+1) by FINSEQ_1:62;
      for j st 1 <= j & j < len p holds [p.j,p.(j+1)] in (R1 \/ R2)
          proof
            let j;
            assume that
A24:        1 <= j and
A25:        j < len p;
        j < 2+1 by A25,FINSEQ_1:62;
then         j <= 2 by NAT_1:13;
then         j = 0 or j = 1 or j = 2 by NAT_1:27;
            hence thesis by A17,A22,A24,FINSEQ_1:62;
          end;
then       [f.i,f.(i+1)] in R1 "\/" R2 by A21,A22,EQREL_1:36;
          hence thesis by A8,A10,A15,EQREL_1:13,NAT_1:13;
        end;
        suppose
A30:      0 = i;
      [f.1,f.1] in R1 by A1,A2,EQREL_1:11;
then       [f.1,f.1] in R1 \/ R2 by XBOOLE_0:def 3;
          hence thesis by A13,A30;
        end;
      end;
      hence thesis;
    end;
    thus P[i] from NAT_1:sch 1(A6,A7);
  end;
  hence thesis by A2,A3;
end;

theorem Th27:
  for PA,PB being a_partition of Y holds ERl(PA '\/' PB) = ERl(PA) "\/" ERl(PB)
proof
  let PA,PB be a_partition of Y;
A1: PA is_finer_than (PA '\/' PB) by Th19;
A2: PB is_finer_than (PA '\/' PB) by Th19;
A3: union PA = Y by EQREL_1:def 6;
A4: union PB = Y by EQREL_1:def 6;
A5: ERl(PA '\/' PB) c= (ERl(PA) "\/" ERl(PB))
  proof
    let x,y be set;
    assume [x,y] in ERl(PA '\/' PB);
    then consider p0 being Subset of Y such that
A7: p0 in (PA '\/' PB) and
A8: x in p0 and
A9: y in p0 by Def6;
A10: p0 is_min_depend PA,PB by A7,Def5;
then A11: p0 is_a_dependent_set_of PA by Def2;
A12: p0 is_a_dependent_set_of PB by A10,Def2;
    consider A1 being set such that
A13: A1 c= PA and A1<>{} and
A14: p0 = union A1 by A11,Def1;
    consider a being set such that
A15: x in a and
A16: a in A1 by A8,A14,TARSKI:def 4;
    reconsider Ca={ p where p is Element of PA: ex f being FinSequence of Y st
    1<=len f & f.1=x & f.len f in p & for i st 1<=i & i<len f holds
    (ex p1,p2,x0 being set st p1 in PA & p2 in PB
    & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2) } as set;
    reconsider pb=union Ca as set;
    reconsider Cb={ p where p is Element of PB:
    ex q being set st q in Ca & p /\ q <> {} } as set;
    reconsider x9 = x as Element of Y by A8;
    reconsider fx=<*x9*> as FinSequence of Y;
A17: fx.1=x by FINSEQ_1:def 8;
then A18: fx.len fx in a by A15,FINSEQ_1:57;
     1
<=len fx & for i st 1<=i & i<len fx holds ex p1,p2,x0 being set st p1 in PA
    & p2 in PB & fx.i in p1 & x0 in (p1 /\ p2) & fx.(i+1) in p2 by FINSEQ_1:57;
then A20: a in Ca by A13,A16,A17,A18;
    then consider y5 being set such that
A21: x in y5 and
A22: y5 in Ca by A15;
    consider z5 being set such that
A23: x9 in z5 and
A24: z5 in PB by A4,TARSKI:def 4;
 y5 /\ z5 <> {} by A21,A23,XBOOLE_0:def 4;
then A26: z5 in Cb by A22,A24;
 Ca c= PA
    proof
      let z be set;
      assume z in Ca;
then   ex p being Element of PA st z=p & ex f being FinSequence of Y st
      1<=len f & f.1=x & f.len f in p & for i st 1<=i & i<len f holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2;
      hence thesis;
    end;
then A30: pb is_a_dependent_set_of PA by A20,Def1;
A31: pb c= union Cb
    proof
      let x1 be set;
      assume x1 in pb;
      then consider y being set such that
A33:  x1 in y and
A34:  y in Ca by TARSKI:def 4;
  ex p being Element of PA st y=p & ex f being FinSequence
of Y st 1<=len f & f.1=x & f.len f in p & for i st 1<=i & i<len f holds ex p1,
p2,x0 being set st p1 in PA & p2 in PB & f. i in p1 & x0 in (p1 /\ p2) & f.(i+1
      ) in p2 by A34;
      then consider z being set such that
A36:  x1 in z and
A37:  z in PB by A4,A33,TARSKI:def 4;
  y /\ z <> {} by A33,A36,XBOOLE_0:def 4;
then   z in Cb by A34,A37;
      hence thesis by A36,TARSKI:def 4;
    end;
 union Cb c= pb
    proof
      let x1 be set;
      assume x1 in union Cb;
      then consider y1 being set such that
A42:  x1 in y1 and
A43:  y1 in Cb by TARSKI:def 4;
A44:  ex p being Element of PB st y1=p & ex q being set st q
      in Ca & p /\ q <> {} by A43;
      then consider q being set such that
A45:  q in Ca and
A46:  y1 /\ q <> {};
      consider pd being set such that
A47:  x1 in pd and
A48:  pd in PA by A3,A42,A44,TARSKI:def 4;
A49:  ex pp being Element of PA st q=pp & ex f being
FinSequence of Y st 1<=len f & f.1=x & f.len f in pp & for i st 1<=i & i<len f
holds ex p1,p2,x0 being set st p1 in PA & p2 in PB & f. i in p1 & x0 in (p1 /\
      p2) & f.(i+1) in p2 by A45;
      then consider fd being FinSequence of Y such that
A50:  1<=len fd and
A51:  fd.1=x and
A52:  fd.len fd in q and
A53:  for i st 1<=i & i<len fd holds ex p1,p2,x0 being set st p1 in PA &
      p2 in PB & fd.i in p1 & x0 in (p1 /\ p2) & fd.(i+1) in p2;
      reconsider x1 as Element of Y by A42,A44;
      reconsider f=fd^<*x1*> as FinSequence of Y;
  len f = len fd + len <*x1*> by FINSEQ_1:35;
then A55:  len f = len fd + 1 by FINSEQ_1:57;
  1+1<=len fd + 1 by A50,XREAL_1:8;
then A57:  1<=len f by A55,XXREAL_0:2;
A58:  f.(len fd + 1) in y1 by A42,FINSEQ_1:59;
  y1 meets q by A46,XBOOLE_0:def 7;
      then consider z0 being set such that
A60:  z0 in y1 & z0 in q by XBOOLE_0:3;
A61:  z0 in (y1 /\ q) by A60,XBOOLE_0:def 4;
A62:  dom fd = Seg len fd by FINSEQ_1:def 3;
A63:  for k being Element of NAT st 1 <= k & k <= len fd holds f.k=fd.k
      proof
        let k be Element of NAT;
        assume 1 <= k & k <= len fd;
then     k in dom fd by A62,FINSEQ_1:3;
        hence thesis by FINSEQ_1:def 7;
      end;
      then
A66:  (fd^<*x1*>).(len fd + 1)=x1 & f.1=x by A50,A51,FINSEQ_1:59;
A67:  f.len fd in q by A50,A52,A63;
A68:  for i st 1<=i & i<len fd holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
      proof
        let i;
        assume
A69:    1<=i & i<len fd;
then A70:    f.i = fd.i by A63;
    1<=(i+1) & (i+1)<=len fd by A69,NAT_1:13;
then     f.(i+1) = fd.(i+1) by A63;
        hence thesis by A53,A69,A70;
      end;
  for i st 1<=i & i<len f holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
      proof
        let i;
        assume that
A74:    1<=i and
A75:    i<len f;
A76:    i<= len fd by A55,A75,NAT_1:13;
    now per cases by A74,A76,XXREAL_0:1;
          case
        1<=i & i<len fd;
            hence thesis by A68;
          end;
          case
        1<=i & i=len fd;
            hence thesis by A44,A49,A58,A61,A67;
          end;
        end;
        hence thesis;
      end;
then   pd in Ca by A47,A48,A55,A57,A66;
      hence thesis by A47,TARSKI:def 4;
    end;
then A81: pb = union Cb by A31,XBOOLE_0:def 10;
 Cb c= PB
    proof
      let z be set;
      assume z in Cb;
then   ex p being Element of PB st z=p &
      ex q being set st q in Ca & p /\ q <> {};
      hence thesis;
    end;
then A85: pb is_a_dependent_set_of PB by A26,A81,Def1;
 now
      assume not pb c= p0;
then   pb \ p0 <> {} by XBOOLE_1:37;
      then consider x1 being set such that
A89:  x1 in (pb \ p0) by XBOOLE_0:def 1;
A90:  not x1 in p0 by A89,XBOOLE_0:def 5;
      consider y1 being set such that
A91:  x1 in y1 and
A92:  y1 in Cb by A81,A89,TARSKI:def 4;
A93:  ex p being Element of PB st y1=p & ex q being set st q
      in Ca & p /\ q <> {} by A92;
      then consider q being set such that
A94:  q in Ca and
A95:  y1 /\ q <> {};
A96:  ex pp being Element of PA st q=pp & ex f being
FinSequence of Y st 1<=len f & f.1=x & f.len f in pp & for i st 1<=i & i<len f
holds ex p1,p2,x0 being set st p1 in PA & p2 in PB & f. i in p1 & x0 in (p1 /\
      p2) & f.(i+1) in p2 by A94;
      then consider fd being FinSequence of Y such that
A97:  1<=len fd and
A98:  fd.1=x and
A99:  fd.len fd in q and
A100: for i st 1<=i & i<len fd holds ex p1,p2,x0 being set st p1 in PA &
      p2 in PB & fd.i in p1 & x0 in (p1 /\ p2) & fd.(i+1) in p2;
      reconsider x1 as Element of Y by A91,A93;
      reconsider f=fd^<*x1*> as FinSequence of Y;
 len f = len fd + len <*x1*> by FINSEQ_1:35;
then A102: len f = len fd + 1 by FINSEQ_1:57;
 1+1<=len fd + 1 by A97,XREAL_1:8;
then A104: 1<=len f by A102,XXREAL_0:2;
A105: f.(len fd + 1) in y1 by A91,FINSEQ_1:59;
 y1 meets q by A95,XBOOLE_0:def 7;
      then consider z0 being set such that
A107: z0 in y1 & z0 in q by XBOOLE_0:3;
A108: z0 in (y1 /\ q) by A107,XBOOLE_0:def 4;
A109: dom fd = Seg len fd by FINSEQ_1:def 3;
A110: for k being Element of NAT st 1 <= k & k <= len fd holds f.k=fd.k
      proof
        let k be Element of NAT;
        assume 1 <= k & k <= len fd;
then    k in dom fd by A109,FINSEQ_1:3;
        hence thesis by FINSEQ_1:def 7;
      end;
      then
A113: (fd^<*x1*>).(len fd + 1)=x1 & f.1=x by A97,A98,FINSEQ_1:59;
A114: f.len fd in q by A97,A99,A110;
A115: for i st 1<=i & i<len fd holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
      proof
        let i;
        assume
A116:   1<=i & i<len fd;
then A117:   f.i = fd.i by A110;
   1<=(i+1) & (i+1)<=len fd by A116,NAT_1:13;
then    f.(i+1) = fd.(i+1) by A110;
        hence thesis by A100,A116,A117;
      end;
A120: for i st 1<=i & i<len f holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
      proof
        let i;
        assume that
A121:   1<=i and
A122:   i<len f;
A123:   i<= len fd by A102,A122,NAT_1:13;
   now per cases by A121,A123,XXREAL_0:1;
          case
       1<=i & i<len fd;
            hence thesis by A115;
          end;
          case
       1<=i & i=len fd;
            hence thesis by A93,A96,A105,A108,A114;
          end;
        end;
        hence thesis;
      end;
 for i st 1<=i & i<len f holds
      ex p1,p2,x0 being set st p1 in PA & p2 in PB
      & f.i in p1 & x0 in p1 & x0 in p2 & f.(i+1) in p2
      proof
        let i;
        assume 1<=i & i<len f;
        then consider p1,p2,x0 being set such that
A129:   p1 in PA & p2 in PB & f.i in p1 and
A130:   x0 in (p1 /\ p2) and
A131:   f.(i+1) in p2 by A120;
   x0 in p1 & x0 in p2 by A130,XBOOLE_0:def 4;
        hence thesis by A129,A131;
      end;
      hence contradiction by A8,A11,A12,A90,A102,A104,A113,Th25;
    end;
then  y in pb by A9,A10,A30,A85,Def2;
    then consider y1 being set such that
A134: y in y1 and
A135: y1 in Cb by A31,TARSKI:def 4;
A136: ex p being Element of PB st y1=p & ex q being set st q
    in Ca & p /\ q <> {} by A135;
    then consider q being set such that
A137: q in Ca and
A138: y1 /\ q <> {};
A139: ex pp being Element of PA st q=pp & ex f being
FinSequence of Y st 1<=len f & f.1=x & f.len f in pp & for i st 1<=i & i<len f
holds ex p1,p2,x0 being set st p1 in PA & p2 in PB & f. i in p1 & x0 in (p1 /\
    p2) & f.(i+1) in p2 by A137;
    then consider fd being FinSequence of Y such that
A140: 1<=len fd and
A141: fd.1=x and
A142: fd.len fd in q and
A143: for i st 1<=i & i<len fd holds ex p1,p2,x0 being set st p1 in PA &
    p2 in PB & fd.i in p1 & x0 in (p1 /\ p2) & fd.(i+1) in p2;
    reconsider y9 = y as Element of Y by A9;
    reconsider f=fd^<*y9*> as FinSequence of Y;
 len f = len fd + len <*y9*> by FINSEQ_1:35;
then A145: len f = len fd + 1 by FINSEQ_1:57;
then A146: 1+1<=len f by A140,XREAL_1:8;
A147: f.(len fd + 1) in y1 by A134,FINSEQ_1:59;
 y1 meets q by A138,XBOOLE_0:def 7;
    then consider z0 being set such that
A149: z0 in y1 & z0 in q by XBOOLE_0:3;
A150: z0 in (y1 /\ q) by A149,XBOOLE_0:def 4;
A151: dom fd = Seg len fd by FINSEQ_1:def 3;
A152: for k being Element of NAT st 1 <= k & k <= len fd holds f.k=fd.k
    proof
      let k be Element of NAT;
      assume 1 <= k & k <= len fd;
then  k in dom fd by A151,FINSEQ_1:3;
      hence thesis by FINSEQ_1:def 7;
    end;
then A155: f.len fd in q by A140,A142;
A156: for i st 1<=i & i<len fd holds
    ex p1,p2,x0 being set st p1 in PA & p2 in PB
    & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
    proof
      let i;
      assume
A157: 1<=i & i<len fd;
then A158: f.i = fd.i by A152;
 1<=(i+1) & (i+1)<=len fd by A157,NAT_1:13;
then  f.(i+1) = fd.(i+1) by A152;
      hence thesis by A143,A157,A158;
    end;
A161: for i st 1<=i & i<len f holds
    ex p1,p2,x0 being set st p1 in PA & p2 in PB
    & f.i in p1 & x0 in (p1 /\ p2) & f.(i+1) in p2
    proof
      let i;
      assume that
A162: 1<=i and
A163: i<len f;
A164: i<= len fd by A145,A163,NAT_1:13;
 now per cases by A162,A164,XXREAL_0:1;
        case
     1<=i & i<len fd;
          hence thesis by A156;
        end;
        case
     1<=i & i=len fd;
          hence thesis by A136,A139,A147,A150,A155;
        end;
      end;
      hence thesis;
    end;
    A168: (fd^<*y9*>).(len fd + 1)=y & 1<=len f by A146,FINSEQ_1:59,XXREAL_0:2;
A169: f.1=x by A140,A141,A152;
 for i st 1<=i & i<len f holds
    ex u being set st u in Y & [f.i,u] in (ERl(PA) \/ ERl(PB))
    & [u,f.(i+1)] in (ERl(PA) \/ ERl(PB))
    proof
      let i;
      assume 1<=i & i<len f;
      then consider p1,p2,u being set such that
A172: p1 in PA and
A173: p2 in PB and
A174: f.i in p1 and
A175: u in (p1 /\ p2) and
A176: f.(i+1) in p2 by A161;
A177: u in p1 by A175,XBOOLE_0:def 4;
A178: u in p2 by A175,XBOOLE_0:def 4;
      reconsider x2=f.i as set;
      reconsider y2=f.(i+1) as set;
A179: [x2,u] in ERl(PA) by A172,A174,A177,Def6;
A180: [u,y2] in ERl(PB) by A173,A176,A178,Def6;
       ERl
(PA) c= ERl(PA) \/ ERl(PB) & ERl(PB) c= ERl(PA) \/ ERl(PB) by XBOOLE_1:7;
      hence thesis by A172,A177,A179,A180;
    end;
then  [x9,y9] in (ERl(PA) "\/" ERl(PB)) by A145,A168,A169,Th26;
    hence thesis;
  end;
 for x1,x2 being set st [x1,x2] in (ERl(PA) \/ ERl(PB)) holds
  [x1,x2] in ERl(PA '\/' PB)
  proof
    let x1,x2 be set;
    assume [x1,x2] in (ERl(PA) \/ ERl(PB));
then  [x1,x2] in ERl(PA) or [x1,x2] in ERl(PB) by XBOOLE_0:def 3;
then A186: (ex A being Subset of Y st A in PA & x1 in A & x2 in A) or
    ex B being Subset of Y st B in PB & x1 in B & x2 in B by Def6;
 now per cases by A186;
      case
   x1 in Y & x2 in Y & ex A being Subset of Y
        st A in PA & x1 in A & x2 in A;
        then consider A being Subset of Y such that
A189:   A in PA and
A190:   x1 in A & x2 in A;
   ex y st y in (PA '\/' PB) & A c= y by A1,A189,SETFAM_1:def 2;
        hence thesis by A190,Def6;
      end;
      case
   x1 in Y & x2 in Y & ex B being Subset of Y
        st B in PB & x1 in B & x2 in B;
        then consider B being Subset of Y such that
A193:   B in PB and
A194:   x1 in B & x2 in B;
   ex y st y in (PA '\/' PB) & B c= y by A2,A193,SETFAM_1:def 2;
        hence thesis by A194,Def6;
      end;
    end;
    hence thesis;
  end;
then  (ERl(PA) \/ ERl(PB)) c= ERl(PA '\/' PB) by RELAT_1:def 3;
then  (ERl(PA) "\/" ERl(PB)) c= ERl(PA '\/' PB) by EQREL_1:def 3;
  hence thesis by A5,XBOOLE_0:def 10;
end;

theorem Th28:
  for PA,PB being a_partition of Y holds ERl(PA '/\' PB) = ERl(PA) /\ ERl(PB)
proof
  let PA,PB be a_partition of Y;
A1: PA '>' PA '/\' PB by Th17;
A2: PB '>' PA '/\' PB by Th17;
 for x1,x2 being set holds
  [x1,x2] in ERl(PA '/\' PB) iff [x1,x2] in (ERl(PA) /\ ERl(PB))
  proof
    let x1,x2 be set;
    hereby
      assume [x1,x2] in ERl(PA '/\' PB);
      then consider C being Subset of Y such that
A5:   C in (PA '/\' PB) and
A6:   x1 in C & x2 in C by Def6;
A7:   ex A being set st A in PA & C c= A by A1,A5,SETFAM_1:def 2;
A8:   ex B being set st B in PB & C c= B by A2,A5,SETFAM_1:def 2;
A9:   [x1,x2] in ERl(PA) by A6,A7,Def6;
  [x1,x2] in ERl(PB) by A6,A8,Def6;
      hence [x1,x2] in (ERl(PA) /\ ERl(PB)) by A9,XBOOLE_0:def 4;
    end;
    assume
A11: [x1,x2] in (ERl(PA) /\ ERl(PB));
then A12: [x1,x2] in ERl(PA) by XBOOLE_0:def 4;
A13: [x1,x2] in ERl(PB) by A11,XBOOLE_0:def 4;
    consider A being Subset of Y such that
A14: A in PA and
A15: x1 in A and
A16: x2 in A by A12,Def6;
    consider B being Subset of Y such that
A17: B in PB and
A18: x1 in B and
A19: x2 in B by A13,Def6;
A20: A /\ B <> {} by A15,A18,XBOOLE_0:def 4;
    consider C being Subset of Y such that
A21: C = A /\ B;
A22: C in INTERSECTION(PA,PB) by A14,A17,A21,SETFAM_1:def 5;
 not C in {{}} by A20,A21,TARSKI:def 1;
then A24: C in INTERSECTION(PA,PB) \ {{}} by A22,XBOOLE_0:def 5;
     x1 in C & x2 in C by A15,A16,A18,A19,A21,XBOOLE_0:def 4;
    hence thesis by A24,Def6;
  end;
  hence thesis by RELAT_1:def 2;
end;

theorem Th29:
  for PA,PB being a_partition of Y st ERl(PA) = ERl(PB) holds PA = PB
proof
  let PA,PB be a_partition of Y;
  assume ERl(PA)=ERl(PB);
then  PA '<' PB & PB '<' PA by Th24;
  hence thesis by Th5;
end;

theorem
  for PA,PB,PC being a_partition of Y holds
  (PA '\/' PB) '\/' PC = PA '\/' (PB '\/' PC)
proof
  let PA,PB,PC be a_partition of Y;
 ERl((PA '\/' PB) '\/' PC) = ERl(PA '\/' PB) "\/" ERl(PC) by Th27
    .= (ERl(PA) "\/" ERl(PB)) "\/" ERl(PC) by Th27
    .= ERl(PA) "\/" (ERl(PB) "\/" ERl(PC)) by EQREL_1:21
    .= ERl(PA) "\/" ERl(PB '\/' PC) by Th27
    .= ERl(PA '\/' (PB '\/' PC)) by Th27;
  hence thesis by Th29;
end;

theorem
  for PA,PB being a_partition of Y holds PA '/\' (PA '\/' PB) = PA
proof
  let PA,PB be a_partition of Y;
   ERl
(PA '/\' (PA '\/' PB)) = ERl(PA) /\ ERl(PA '\/' PB) & ERl(PA) /\ ERl(PA
  '\/' PB) = ERl(PA) /\ (ERl(PA) "\/" ERl(PB)) by Th27,Th28;
  hence thesis by Th29,EQREL_1:24;
end;

theorem
  for PA,PB being a_partition of Y holds PA '\/' (PA '/\' PB) = PA
proof
  let PA,PB be a_partition of Y;
   ERl
(PA '\/' (PA '/\' PB)) = ERl(PA) "\/" ERl(PA '/\' PB) & ERl(PA) "\/" ERl(
  PA '/\' PB) = ERl(PA) "\/" (ERl(PA) /\ ERl(PB)) by Th27,Th28;
  hence thesis by Th29,EQREL_1:25;
end;

theorem Th33:
  for PA,PB,PC being a_partition of Y st
  PA '<' PC & PB '<' PC holds PA '\/' PB '<' PC
proof
  let PA,PB,PC be a_partition of Y;
  assume PA '<' PC & PB '<' PC;
then A2: ERl(PA) c= ERl(PC) & ERl(PB) c= ERl(PC) by Th24;
A3: ERl(PA '\/' PB) = ERl(PA) "\/" ERl(PB) by Th27;
 ERl(PA) \/ ERl(PB) c= ERl(PC) by A2,XBOOLE_1:8;
then  ERl(PA) "\/" ERl(PB) c= ERl(PC) by EQREL_1:def 3;
  hence thesis by A3,Th24;
end;

theorem
  for PA,PB,PC being a_partition of Y st
  PA '>' PC & PB '>' PC holds PA '/\' PB '>' PC
proof
  let PA,PB,PC be a_partition of Y;
  assume PA '>' PC & PB '>' PC;
then A2: ERl(PC) c= ERl(PA) & ERl(PC) c= ERl(PB) by Th24;
 ERl(PA '/\' PB) = ERl(PA) /\ ERl(PB) by Th28;
then  ERl(PC) c= ERl(PA '/\' PB) by A2,XBOOLE_1:19;
  hence thesis by Th24;
end;

notation
  let Y;
  synonym %I(Y) for SmallestPartition Y;
end;

definition
  let Y;
  canceled;
  func %O(Y) -> a_partition of Y equals
  {Y};
  correctness
  proof
A1: {Y} is Subset-Family of Y & union {Y} = Y by ZFMISC_1:31,80;
 for A st A in {Y} holds A<>{} & for B st B in {Y} holds A = B or A misses B
    proof
      let A;
      assume
A3:   A in {Y};
then A4:   A=Y by TARSKI:def 1;
      thus A<>{} by A3,TARSKI:def 1;
      let B;
      assume B in {Y};
      hence thesis by A4,TARSKI:def 1;
    end;
    hence thesis by A1,EQREL_1:def 6;
  end;
end;

theorem
  %I(Y)={B:ex x being set st B={x} & x in Y}
proof
  set B0={B:ex x being set st B={x} & x in Y};
A1: %I(Y) c= B0
  proof
    let a be set;
    assume a in %I(Y);
then
 a in {{x} where x is Element of Y: not contradiction} by EQREL_1:46;
    then consider x be Element of Y such that
A4: a={x};
    reconsider y=x as Element of Y;
    reconsider B={y} as Subset of Y by ZFMISC_1:37;
 a=B by A4;
    hence thesis;
  end;
 B0 c= %I(Y)
  proof
    let x1;
    assume x1 in B0;
then  ex B st x1=B & ex x being set st B={x} & x in Y;
then  x1 in {{z} where z is Element of Y: not contradiction};
    hence thesis by EQREL_1:46;
  end;
  hence thesis by A1,XBOOLE_0:def 10;
end;

theorem Th36:
  for PA being a_partition of Y holds %O(Y) '>' PA & PA '>' %I(Y)
proof
  let PA be a_partition of Y;
A1: for a being set st a in PA ex b being set st b in %O(Y) & a c= b
  proof
    let a be set;
    assume
A2: a in PA;
then A3: a c= Y;
A4: a<>{} by A2,EQREL_1:def 6;
    consider x being Element of a;
A5: x in Y by A2,A4,TARSKI:def 3;
 union %O(Y) = Y by EQREL_1:def 6;
    then consider b being set such that
    x in b and
A7: b in %O(Y) by A5,TARSKI:def 4;
 a c= b by A3,A7,TARSKI:def 1;
    hence thesis by A7;
  end;
 for a being set st a in %I(Y) ex b being set st b in PA & a c= b
  proof
    let a be set;
    assume
A10: a in %I(Y);
then  a in {{x} where x is Element of Y: not contradiction} by
    EQREL_1:46;
    then consider x be Element of Y such that
A12: a={x};
A13: a<>{} by A10,EQREL_1:def 6;
    consider u being Element of a;
A14: u=x by A12,TARSKI:def 1;
A15: u in Y by A10,A13,TARSKI:def 3;
 union PA = Y by EQREL_1:def 6;
    then consider b being set such that
A17: u in b and
A18: b in PA by A15,TARSKI:def 4;
 a c= b
    proof
      let x1;
      assume x1 in a;
      hence thesis by A12,A14,A17,TARSKI:def 1;
    end;
    hence thesis by A18;
  end;
  hence thesis by A1,SETFAM_1:def 2;
end;

theorem Th37:
  ERl(%O(Y)) = nabla Y
proof
 nabla Y c= ERl(%O(Y))
  proof
    let x1,x2 be set;
    assume
A2: [x1,x2] in nabla Y;
A3: Y in %O(Y) by TARSKI:def 1;
 x1 in Y & x2 in Y by A2,ZFMISC_1:106;
    hence thesis by A3,Def6;
  end;
  hence thesis by XBOOLE_0:def 10;
end;

theorem Th38:
  ERl(%I(Y)) = id Y
proof
A1: union %I(Y)=Y by EQREL_1:def 6;
A2: ERl(%I(Y)) c= id Y
  proof
    let x1,x2 be set;
    assume [x1,x2] in ERl(%I(Y));
    then consider a being Subset of Y such that
A4: a in %I(Y) and
A5: x1 in a & x2 in a by Def6;
     %I(Y) = {{x} where x is Element of Y: not contradiction} by EQREL_1:46;
    then consider x be Element of Y such that
A7: a={x} by A4;
 x1=x & x2=x by A5,A7,TARSKI:def 1;
    hence thesis by RELAT_1:def 10;
  end;
 id Y c= ERl(%I(Y))
  proof
    let x1,x2 be set;
    assume
A10: [x1,x2] in id Y;
then A11: x1 in Y by RELAT_1:def 10;
A12: x1=x2 by A10,RELAT_1:def 10;
 ex y being set st x1 in y & y in %I(Y) by A1,A11,TARSKI:def 4;
    hence thesis by A12,Def6;
  end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem
  %I(Y) '<' %O(Y)
proof
 ERl(%O(Y)) = nabla Y by Th37
    .= [:Y,Y:];
then  ERl(%I(Y)) c= ERl(%O(Y));
  hence thesis by Th24;
end;

theorem
  for PA being a_partition of Y holds
  %O(Y) '\/' PA = %O(Y) & %O(Y) '/\' PA = PA
proof
  let PA be a_partition of Y;
A1: ERl(%O(Y) '\/' PA) = ERl(%O(Y)) "\/" ERl(PA) by Th27;
 ERl(%O(Y)) = nabla Y by Th37;
then  ERl(%O(Y)) \/ ERl(PA) = ERl(%O(Y)) by EQREL_1:1;
then  ERl(%O(Y)) c= ERl(%O(Y)) "\/" ERl(PA) by EQREL_1:def 3;
then A5: %O(Y) '<' %O(Y) '\/' PA by A1,Th24;
 %O(Y) '>' PA '\/' %O(Y) by Th36;
  hence %O(Y) '\/' PA = %O(Y) by A5,Th5;
   ERl(%O(Y) '/\' PA) = ERl(%O(Y)) /\ ERl(PA) & ERl(%O(Y)) = nabla Y by Th28
  ,Th37;
  hence %O(Y) '/\' PA = PA by Th29,XBOOLE_1:28;
end;

theorem
  for PA being a_partition of Y holds
  %I(Y) '\/' PA = PA & %I(Y) '/\' PA = %I(Y)
proof
  let PA be a_partition of Y;
A1: ERl(%I(Y)) = id Y by Th38;
  A2: ERl
(%I(Y) '\/' PA) = ERl(%I(Y)) "\/" ERl(PA) & ERl(%I(Y)) \/ ERl(PA) c= ERl(
  %I(Y)) "\/" ERl(PA) by Th27,EQREL_1:def 3;
A3: ERl(%I(Y)) \/ ERl(PA) = id Y \/ ERl(PA) by Th38;
 %I(Y) '<' PA by Th36;
then A5: ERl(%I(Y)) c= ERl(PA) by Th24;
 for z being set st z in id Y \/ ERl(PA) holds z in ERl PA
  proof
    let z be set;
    assume
A7: z in id Y \/ ERl(PA);
 now per cases by A7,XBOOLE_0:def 3;
      case
     z in id Y;
        hence thesis by A1,A5;
      end;
      case
    z in ERl(PA);
        hence thesis;
      end;
    end;
    hence thesis;
  end;
then A11: id Y \/ ERl(PA) c= ERl(PA) by TARSKI:def 3;
 ERl(PA) c= id Y \/ ERl(PA) by XBOOLE_1:7;
then  id Y \/ ERl(PA) = ERl(PA) by A11,XBOOLE_0:def 10;
then A14: PA '<' %I(Y) '\/' PA by A2,A3,Th24;
 %I(Y) '<' PA by Th36;
then  %I(Y) '\/' PA '<' PA by Th33;
  hence %I(Y) '\/' PA = PA by A14,Th5;
 ERl(%I(Y) '/\' PA) = ERl(%I(Y)) /\ ERl(PA) by Th28
    .= id Y /\ ERl(PA) by Th38
    .= id Y by EQREL_1:17
    .= ERl(%I(Y)) by Th38;
  hence %I(Y) '/\' PA = %I(Y) by Th29;
end;
