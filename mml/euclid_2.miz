:: The Inner Product of Finite Sequences and of Points of $n$-dimensional
:: Topological Space
::  by Kanchun and Yatsuka Nakamura
::
:: Received February 3, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies NUMBERS, NAT_1, XREAL_0, ORDINAL1, SUBSET_1, FINSEQ_2, PRE_TOPC,
      EUCLID, RVSUM_1, FUNCT_1, CARD_1, FINSEQ_1, RELAT_1, VALUED_0, ARYTM_3,
      COMPLEX1, SQUARE_1, ARYTM_1, CARD_3, XXREAL_0, TARSKI, FUNCT_3, SUPINF_2;
 notations TARSKI, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, NUMBERS, XCMPLX_0,
      XREAL_0, COMPLEX1, NAT_1, FUNCOP_1, FINSEQ_1, FINSEQ_2, VALUED_0, QUIN_1,
      STRUCT_0, PRE_TOPC, RVSUM_1, BINOP_2, RLVECT_1, EUCLID, SQUARE_1,
      XXREAL_0;
 constructors REAL_1, SQUARE_1, BINOP_2, COMPLEX1, QUIN_1, FINSEQOP, MONOID_0,
      TOPRNS_1, SEQ_1, RELSET_1;
 registrations FUNCT_1, RELSET_1, FUNCOP_1, NUMBERS, XREAL_0, SQUARE_1,
      BINOP_2, MEMBERED, FINSEQ_2, RVSUM_1, MONOID_0, EUCLID, VALUED_0;
 requirements REAL, SUBSET, NUMERALS, ARITHM, BOOLE;
 definitions SQUARE_1, FINSEQ_2, RVSUM_1, VALUED_1, ALGSTR_0;
 theorems EUCLID, RVSUM_1, FINSEQ_2, FINSEQ_1, FINSEQ_3, SQUARE_1, QUIN_1,
      ABSVALUE, TOPRNS_1, JORDAN2C, COMPLEX1, XREAL_1, XXREAL_0, FUNCOP_1,
      XCMPLX_1, FINSEQOP, ORDINAL1, FUNCT_1;

begin :: Preliminaries

reserve i, n for Nat,
  x, y, a for real number,
  v for Element of n-tuples_on REAL,
  p, p1, p2, p3, q, q1, q2 for Point of TOP-REAL n;

theorem Th1:
  mlt(v, 0*n).i = 0
proof
A1: 0*n = n |-> (0 qua set) by EUCLID:def 4;
  set v0 = (0*n).i;
A3: dom(0*n) = Seg n by FINSEQ_2:144;
  i in Seg n or not i in Seg n;
  then
A2: v0=0 by A1,A3,FUNCOP_1:13,FUNCT_1:def 4;
  mlt(v,0*n).i = v.i * v0 by RVSUM_1:87
    .= 0 by A2;
  hence thesis;
end;

theorem Th2:
  mlt(v, 0*n) = 0*n
proof
  len 0*n = n by FINSEQ_1:def 18;
  then reconsider z= 0*n as Element of n-tuples_on REAL by FINSEQ_2:110;
A1: len 0*n = n by FINSEQ_1:def 18;
  then
A2: dom 0*n = Seg n by FINSEQ_1:def 3;
A3: for j be Nat st j in dom 0*n holds mlt(v,0*n).j=(0*n).j
  proof
    let j be Nat;
    assume
A4: j in dom 0*n;
    thus mlt(v,0*n).j = 0 by Th1
      .= (n |-> 0).j by A2,A4,FUNCOP_1:13
      .= (0*n).j by EUCLID:def 4;
  end;
  len mlt(v, z) = n by FINSEQ_1:def 18;
  then dom mlt(v, 0*n) = dom 0*n by A1,FINSEQ_3:31;
  hence thesis by A3,FINSEQ_1:17;
end;

begin :: Inner Product of Finite Sequences

canceled 7;

theorem Th10:
  for y1,y2 being real-valued FinSequence, x1,x2 being Element of REAL
  n st x1=y1 & x2=y2 holds |(y1,y2)|=1/4*((|.(x1+x2).|)^2-(|.(x1-x2).|)^2)
proof
  let y1,y2 be real-valued FinSequence,x1,x2 be Element of REAL n;
  assume
A1: x1=y1 & x2=y2;
  reconsider w1=x1, w2=x2 as Element of n-tuples_on REAL by EUCLID:def 1;
  set v1=sqr (x1+x2), v2=sqr (x1-x2);
  set z1=x1+x2, z2=x1-x2;
A2: 1/4*((|.(x1+x2).|)^2-(|.(x1-x2).|)^2) =1/4*((sqrt Sum sqr (z1))^2-(|.(z2
  ).|)^2) by EUCLID:def 5
    .=1/4*((sqrt Sum sqr (z1))^2-(sqrt Sum sqr (z2))^2) by EUCLID:def 5;
  Sum sqr (x1+x2)>=0 by RVSUM_1:116;
  then
A3: (sqrt Sum sqr (x1+x2))^2 =Sum sqr (x1+x2) by SQUARE_1:def 4;
A4: Sum sqr (x1+x2)-Sum sqr (x1-x2)=Sum (v1 - v2) by RVSUM_1:120;
  Sum sqr (x1-x2)>=0 by RVSUM_1:116;
  then
A5: (sqrt Sum sqr (x1-x2))^2=Sum sqr (x1-x2) by SQUARE_1:def 4;
A6: 2*mlt(w1,w2) +sqr w2+ 2*mlt(w1,w2) = 2*mlt(w1,w2)+(2*mlt(w1,w2) + sqr w2 )
    .= 2*mlt(w1,w2)+2*mlt(w1,w2) + sqr w2 by FINSEQOP:29;
A7: sqr w1 + (2*mlt(w1,w2) + sqr w2) = (2*mlt(w1,w2) + sqr w2) + sqr w1;
  v1-v2=sqr w1 + 2*mlt(w1,w2) + sqr w2 -sqr (w1-w2) by RVSUM_1:98
    .= sqr w1 + 2*mlt(w1,w2) + sqr w2 -(sqr w1 - 2*mlt(w1,w2) + sqr w2) by
  RVSUM_1:99
    .= 2*mlt(w1,w2) +sqr w2+ sqr w1 -(sqr w1 - 2*mlt(w1,w2) + sqr w2) by A7,
  FINSEQOP:29
    .= 2*mlt(w1,w2) +sqr w2+ sqr w1 -(sqr w1 - 2*mlt(w1,w2))- sqr w2 by
  RVSUM_1:60
    .= 2*mlt(w1,w2) +sqr w2+ sqr w1 - sqr w1 + 2*mlt(w1,w2)- sqr w2 by
  RVSUM_1:62
    .= (2*mlt(w1,w2) +sqr w2+ 2*mlt(w1,w2))- sqr w2 by RVSUM_1:63
    .= 2*mlt(w1,w2)+2*mlt(w1,w2) by A6,RVSUM_1:63
    .= (2+2)*mlt(w1,w2) by RVSUM_1:72
    .= 4*mlt(w1,w2);
  then 1/4*((|.(x1+x2).|)^2-(|.(x1-x2).|)^2) = 1/4*(4*Sum(mlt(w1,w2))) by A2,
  A3,A5,A4,RVSUM_1:117
    .= Sum(mlt(w1,w2));
  hence thesis by A1;
end;

canceled;

Lm1:
now
  let x be real-valued FinSequence;
  rng x c= REAL;
  hence x is FinSequence of REAL by FINSEQ_1:def 4;
end;

theorem Th12:
  for x being real-valued FinSequence holds |.x.|^2 = |(x,x)|
proof
  let x be real-valued FinSequence;
A1: 0 <= |(x,x)| by RVSUM_1:149;
  |.x.|^2 = (sqrt Sum sqr x)^2 by EUCLID:def 5
    .= |(x,x)| by A1,SQUARE_1:def 4;
  hence thesis;
end;

theorem Th13:
  for x being real-valued FinSequence holds |.x.| = sqrt |(x,x)|
proof
  let x be real-valued FinSequence;
  |.x.| = sqrt |.x.|^2 by EUCLID:12,SQUARE_1:89
    .= sqrt |(x,x)| by Th12;
  hence thesis;
end;

theorem Th14:
  for x being real-valued FinSequence holds 0 <= |.x.|
proof
  let x be real-valued FinSequence;
  |.x.| = sqrt |(x,x)| & 0 <= |(x,x)| by RVSUM_1:149,Th13;
  hence thesis by SQUARE_1:def 4;
end;

theorem Th15:
  for x being real-valued FinSequence holds |(x,x)|=0 iff x=0*(len x)
proof
  let x be real-valued FinSequence;
  thus |(x,x)|=0 implies x=0*(len x)
  proof
    x is FinSequence of REAL by Lm1;
    then reconsider y=x as Element of REAL len x by JORDAN2C:52;
    assume |(x,x)|=0;
    then |.x.|^2=0 by Th12;
    then |.x.|=0 by XCMPLX_1:6;
    then y=0*(len x) by EUCLID:11;
    hence thesis;
  end;
  assume x=0*(len x);
  then |.x.|=0 by EUCLID:10;
  then |(x,x)|=0^2 by Th12;
  hence thesis;
end;

theorem
  for x being real-valued FinSequence holds |(x,x)| = 0 iff |.x.| = 0
proof
  let x be real-valued FinSequence;
A1: |(x,x)| = 0^2 implies |.x.| = 0
  proof
    assume |(x,x)| = 0^2;
    then x=0*(len x) by Th15;
    hence thesis by EUCLID:10;
  end;
  |.x.| = 0 implies |(x,x)| = 0^2 by Th12;
  hence thesis by A1;
end;

theorem Th17:
  for x being real-valued FinSequence holds |(x, 0*(len x))| = 0
proof
  let x be real-valued FinSequence;
  set n=len x;
  x is FinSequence of REAL by Lm1;
  then reconsider p1=x as Element of n-tuples_on REAL by FINSEQ_2:110;
A1: 0*n = n |-> 0 by EUCLID:def 4;
  |(x, 0*n)| = Sum mlt(p1,0*n) .= Sum 0*n by Th2
    .= 0 by A1,RVSUM_1:111;
  hence thesis;
end;

theorem
  for x being real-valued FinSequence holds |(0*(len x),x)| = 0 by Th17;

canceled 14;

theorem Th33:
  for x,y being real-valued FinSequence st len x=len y holds |.x+y.|^2
  = |.x.|^2 + 2*|(y, x)| + |.y.|^2
proof
  let x,y be real-valued FinSequence;
  assume
A1: len x=len y;
  |.x+y.|^2 = |(x+y, x+y)| by Th12
    .= |(x, x)| + 2*|(x, y)| + |(y, y)| by A1,RVSUM_1:158
    .= |.x.|^2 + 2*|(y, x)| + |(y, y)| by Th12
    .= |.x.|^2 + 2*|(y, x)| + |.y.|^2 by Th12;
  hence thesis;
end;

theorem Th34:
  for x,y being real-valued FinSequence st len x=len y holds |.x-y.|^2
  = |.x.|^2 - 2*|(y, x)| + |.y.|^2
proof
  let x,y be real-valued FinSequence;
  assume
A1: len x=len y;
  |.x-y.|^2 = |(x-y, x-y)| by Th12
    .= |(x, x)| - 2*|(x, y)| + |(y, y)| by A1,RVSUM_1:159
    .= |.x.|^2 - 2*|(y, x)| + |(y, y)| by Th12
    .= |.x.|^2 - 2*|(y, x)| + |.y.|^2 by Th12;
  hence thesis;
end;

theorem
  for x,y being real-valued FinSequence st len x=len y holds |.x+y.|^2 + |.x
  -y.|^2 = 2*(|.x.|^2 + |.y.|^2)
proof
  let x,y be real-valued FinSequence;
  assume
A1: len x=len y;
  then
  |.x+y.|^2 + |.x-y.|^2 = (|.x.|^2 + 2*|(y, x)| + |.y.|^2) + |.x-y.|^2 by
  Th33
    .= (|.x.|^2 + 2*|(x, y)| + |.y.|^2) + (|.x.|^2 - 2*|(y, x)| + |.y.|^2)
  by A1,Th34
    .= 2*(|.x.|^2 + |.y.|^2);
  hence thesis;
end;

theorem
  for x,y being real-valued FinSequence st len x=len y holds |.x+y.|^2 - |.x
  -y.|^2 = 4* |(x,y)|
proof
  let x,y be real-valued FinSequence;
  assume
A1: len x=len y;
  then |.x+y.|^2 - |.x-y.|^2 = (|.x.|^2 + 2*|(y,x)| + |.y.|^2) - |.x-y.|^2 by
  Th33
    .= (|.x.|^2 + 2*|(x, y)| + |.y.|^2) - (|.x.|^2 - 2*|(y, x)| + |.y.|^2)
  by A1,Th34
    .= 4*|(x, y)|;
  hence thesis;
end;

theorem Th37: :: Schwartz
  for x,y being real-valued FinSequence st len x=len y holds
  abs |(x,y)| <= |.x.|*|.y.|
proof
  let x,y be real-valued FinSequence;
  assume
A1: len x=len y;
A2: x = 0*(len x) implies abs(|(x,y)|) <= sqrt (|(x,x)|) * sqrt (|(y,y)|)
  proof
    assume x = 0*(len x);
    then |(x,y)| = 0 & |(x,x)|=0 by A1,Th17;
    hence thesis by ABSVALUE:7,SQUARE_1:82;
  end;
A3: x <> 0*(len x) implies abs(|(x,y)|) <= sqrt (|(x,x)|) * sqrt (|(y,y)|)
  proof
A4: for t be real number holds |(x,x)| * t^2 + (2 * (|(x,y)|)) * t + |(y,
    y)| >= 0
    proof
      let t be real number;
      set s=t^2;
A5:   len (t * x)=len x by RVSUM_1:147;
      |((t * x + y),(t * x + y))| >= 0 by RVSUM_1:149;
      then
      |((t * x) , (t * x))| + 2 * (|((t*x),y)|) + |(y,y)| >= 0 by A1,A5,
      RVSUM_1:158;
      then t * ( |(t*x,x)|) + 2 * |((t*x),y)| + |(y,y)| >= 0 by A5,
      RVSUM_1:151;
      then t * ( t * |(x,x)|) + 2 * |((t*x),y)| + |(y,y)| >= 0 by A1,
      RVSUM_1:151;
      then |(x,x)| * s + 2 * (|(x,y)| * t) + |(y,y)| >= 0 by A1,RVSUM_1:151;
      hence thesis;
    end;
    set w=abs( |(x,y)| ), u=|(x,y)|;
A6: |(x,x)| >= 0 by RVSUM_1:149;
    assume x <> 0*(len x);
    then |(x,x)| <> 0 by Th15;
    then |(x,x)| > 0 by A6,XXREAL_0:1;
    then delta(|(x,x)|,(2 * (|(x,y)|)),|(y,y)|) <= 0 by A4,QUIN_1:10;
    then (2 * u)^2 - 4 * (|(x,x)|) * (|(y,y)|) <= 0 by QUIN_1:def 1;
    then 4 * ((( u )^2) - (|(x,x)|) * (|(y,y)|)) <= 0;
    then ((|(x,y)|)^2) - (|(x,x)|) * (|(y,y)|) <= 0/4 by XREAL_1:79;
    then (|(x,y)|)^2 <= (|(x,x)|) * (|(y,y)|) by XREAL_1:52;
    then 0 <= w^2 & (abs(|(x,y)|))^2 <= (|(x,x)|) * (|(y,y)|) by
    COMPLEX1:
    161 ,XREAL_1:65;
    then (sqrt (abs(|(x,y)|))^2) <= sqrt ((|(x,x)|) * (|(y,y)|)) by
    SQUARE_1:94;
    then
A7: abs(|(x,y)|) <= sqrt (|(x,x)| * |(y,y)|) by COMPLEX1:132 ,
    SQUARE_1:89;
    |(y,y)| >= 0 by RVSUM_1:149;
    hence thesis by A6,A7,SQUARE_1:97;
  end;
  sqrt (|(x,x)|)=|.x.| by Th13;
  hence thesis by A2,A3,Th13;
end;

theorem Th38: :: Triangle
  for x,y being real-valued FinSequence st len x=len y holds
  |.x+y.| <= |.x.| + |.y.|
proof
  let x,y be real-valued FinSequence;
  assume
A1: len x=len y;
  then |(x,y)| <= abs(|(x,y)|) & abs(|(x,y)|) <= |.x.|*|.y.| by Th37,
  ABSVALUE:11;
  then |(x,y)| <= |.x.|*|.y.| by XXREAL_0:2;
  then 2*|(x,y)| <= 2*(|.x.|*|.y.|) by XREAL_1:66;
  then
A2: (|.x.|)^2+ 2*|(x,y)|<=(|.x.|)^2 + 2*(|.x.|*|.y.|) by XREAL_1:9;
  (|.x+y.|)^2 = (|.x.|)^2 + 2*|(y, x)| + (|.y.|)^2 by A1,Th33;
  then
A3: (|.x+y.|)^2 <= (|.x.|)^2 + 2*|.x.|*|.y.|+(|.y.|)^2 by A2,XREAL_1:9;
  0<= (|.x+y.|)^2 by XREAL_1:65;
  then sqrt((|.x+y.|)^2) <= sqrt((|.x.| + |.y.|)^2) by A3,SQUARE_1:94;
  then
A4: |.x+y.| <= sqrt((|.x.| + |.y.|)^2) by Th14,SQUARE_1:89;
  0<= |.x.| & 0 <= |.y.| by Th14;
  then 0+0 <= |.x.| + |.y.| by XREAL_1:9;
  hence thesis by A4,SQUARE_1:89;
end;

begin  :: Inner Product of Points of TOP-REAL n

theorem
  |(p1,p2)| = 1/4*((|. p1+p2 .|)^2-(|. p1-p2 .|)^2)
proof
  reconsider q1= p1, q2= p2 as Element of REAL n by EUCLID:25;
A1: p1-p2 = q1-q2 by EUCLID:73;
  thus |(p1,p2)| = 1/4*((|. q1+q2 .|)^2-(|. q1-q2 .|)^2) by Th10
    .= 1/4*((|. p1+p2 .|)^2-(|. p1-p2 .|)^2) by A1,EUCLID:68;
end;

theorem Th40:
  |(p1 + p2, p3)| = |(p1, p3)| + |(p2, p3)|
proof
  reconsider f1=p1, f2=p2, f3=p3 as FinSequence of REAL by EUCLID:27;
  reconsider q1 = p1, q2=p2 as Element of REAL n by EUCLID:25;
  len f2=n by FINSEQ_1:def 18;
  then
A1: len f1=len f2 & len f2=len f3 by FINSEQ_1:def 18;
  p1 + p2 = q1 + q2 by EUCLID:68;
  hence thesis by A1,RVSUM_1:150;
end;

theorem Th41:
  for x being real number holds |(x*p1, p2)| = x*|(p1, p2)|
proof
  let x be real number;
  reconsider f1=p1,f2=p2 as FinSequence of REAL by EUCLID:27;
  reconsider q1 = p1 as Element of REAL n by EUCLID:25;
A1: x*p1 = x*q1 by EUCLID:69;
  len f1=n & len f2=n by FINSEQ_1:def 18;
  hence thesis by A1,RVSUM_1:151;
end;

theorem
  for x being real number holds |(p1, x*p2)| = x*|(p1, p2)| by Th41;

theorem Th43:
  |(-p1, p2)| = -|(p1, p2)|
proof
  |(-p1, p2)| = |((-1)*p1, p2)| by EUCLID:43
    .= (-1)*|(p1, p2)| by Th41;
  hence thesis;
end;

theorem
  |(p1, -p2)| = -|(p1, p2)| by Th43;

theorem
  |(-p1, -p2)| = |(p1, p2)|
proof
  |(-p1, -p2)| = -|(p1, -p2)| by Th43
    .= --|(p1, p2)| by Th43;
  hence thesis;
end;

theorem Th46:
  |(p1-p2, p3)| = |(p1, p3)| - |(p2, p3)|
proof
  |(p1 - p2, p3)| = |(p1, p3)| + |(-p2, p3)| by Th40
    .= |(p1, p3)| + - |(p2, p3)| by Th43;
  hence thesis;
end;

theorem
  |((x*p1+y*p2), p3)| = x*|(p1,p3)| + y*|(p2,p3)|
proof
  |(x*p1 + y*p2, p3)| = |(x*p1, p3)| + |(y*p2, p3)| by Th40
    .= x*|(p1,p3)| + |(y*p2,p3)| by Th41
    .= x*|(p1,p3)| + y*|(p2,p3)| by Th41;
  hence thesis;
end;

theorem
  |(p, q1+q2)| = |(p, q1)| + |(p, q2)| by Th40;

theorem
  |(p, q1-q2)| = |(p, q1)| - |(p, q2)| by Th46;

theorem Th50:
  |(p1+p2, q1+q2)| = |(p1, q1)| + |(p1, q2)| + |(p2, q1)| + |(p2, q2)|
proof
A1: |(p1+p2, q1)| = |(p1, q1)| + |(p2, q1)| & |(p1+p2, q2)| = |(p1, q2)| +
  |(p2, q2)| by Th40;
  |(p1+p2, q1+q2)| = |(p1+p2, q1)| + |(p1+p2, q2)| by Th40
    .= |(p1, q1)|+|(p1, q2)|+|(p2, q1)|+|(p2, q2)| by A1;
  hence thesis;
end;

theorem Th51:
  |(p1-p2, q1-q2)| = |(p1, q1)| - |(p1, q2)| - |(p2, q1)| + |(p2, q2)|
proof
A1: |(p1,q1-q2)| = |(p1,q1)| - |(p1,q2)| & |(p2,q1-q2)| = |(p2,q1)| - |(p2,
  q2)| by Th46;
  |(p1-p2, q1-q2)| = |(p1, q1-q2)| - |(p2, q1-q2)| by Th46
    .= |(p1,q1)|-|(p1,q2)|-|(p2,q1)|+|(p2,q2)| by A1;
  hence thesis;
end;

theorem Th52:
  |(p+q, p+q)| = |(p, p)| + 2*|(p, q)| + |(q, q)|
proof
  |(p, p)| + |(p, q)| + |(p, q)| = |(p, p)| + 2*|(p, q)|;
  hence thesis by Th50;
end;

theorem Th53:
  |(p-q, p-q)| = |(p, p)| - 2*|(p, q)| + |(q, q)|
proof
  |(p-q, p-q)| = |(p,p)| - |(p,q)| - |(p,q)| + |(q, q)| by Th51
    .= |(p,p)| - 2*|(p,q)| + |(q, q)|;
  hence thesis;
end;

theorem Th54:
  |(p, 0.TOP-REAL n)| = 0
proof
  reconsider f1=p as FinSequence of REAL by EUCLID:27;
  len f1=n by FINSEQ_1:def 18;
  then 0*(len f1) = 0.TOP-REAL n by EUCLID:74;
  hence thesis by Th17;
end;

theorem
  |(0.TOP-REAL n, p)| = 0 by Th54;

theorem
  |(0.TOP-REAL n, 0.TOP-REAL n)| = 0 by Th54;

theorem Th57:
  |(p,p)| >= 0 by RVSUM_1:149;

theorem Th58:
  |(p,p)| = |.p.|^2 by Th12;

theorem Th59:
  |.p.| = sqrt |(p,p)|
proof
  n in NAT by ORDINAL1:def 13;
  then |.p.| = sqrt |.p.|^2 by SQUARE_1:89,TOPRNS_1:26
    .= sqrt |(p,p)| by Th58;
  hence thesis;
end;

theorem Th60:
  0 <= |.p.|
proof
  |.p.| = sqrt |(p,p)| & 0 <= |(p,p)| by Th57,Th59;
  hence thesis by SQUARE_1:def 4;
end;

theorem Th61:
  |. 0.TOP-REAL n .| = 0
proof
  n in NAT by ORDINAL1:def 13;
  hence thesis by TOPRNS_1:24;
end;

theorem Th62:
  |(p,p)| = 0 iff |.p.| = 0
proof
A1: |(p,p)| = 0^2 implies |.p.| = 0
  proof
    assume |(p,p)| = 0^2;
    then |.p.|^2 = 0 by Th58;
    hence thesis by XCMPLX_1:6;
  end;
  |.p.| = 0 implies |(p,p)| = 0^2 by Th58;
  hence thesis by A1;
end;

theorem Th63:
  |(p,p)| = 0 iff p = 0.TOP-REAL n
proof
  |(p,p)| = 0 implies p = 0.TOP-REAL n
  proof
    assume |(p,p)| = 0;
    then n in NAT & |.p.| = 0 by Th62,ORDINAL1:def 13;
    hence thesis by TOPRNS_1:25;
  end;
  hence thesis by Th54;
end;

theorem
  |.p.|=0 iff p = 0.TOP-REAL n
proof
  n in NAT by ORDINAL1:def 13;
  hence thesis by Th61,TOPRNS_1:25;
end;

theorem
  p <> 0.TOP-REAL n iff |(p, p)| > 0
proof
  p <> 0.TOP-REAL n implies |(p, p)| > 0
  proof
    assume p <> 0.TOP-REAL n;
    then
A1: |(p,p)| <> 0 by Th63;
    0 <= |(p,p)| by Th57;
    hence thesis by A1,XXREAL_0:1;
  end;
  hence thesis by Th63;
end;

theorem
  p <> 0.TOP-REAL n iff |.p.| > 0
proof
  p <> 0.TOP-REAL n implies |.p.| > 0
  proof
    assume
A1: p <> 0.TOP-REAL n;
    n in NAT & 0 <= |.p.| by Th60,ORDINAL1:def 13;
    hence thesis by A1,TOPRNS_1:25,XXREAL_0:1;
  end;
  hence thesis by Th61;
end;

theorem Th67:
  |.p+q.|^2 = |.p.|^2 + 2*|(q, p)| + |.q.|^2
proof
  |.p+q.|^2 = |(p+q, p+q)| by Th58
    .= |(p, p)| + 2*|(q, p)| + |(q, q)| by Th52
    .= |.p.|^2 + 2*|(q, p)| + |(q, q)| by Th58
    .= |.p.|^2 + 2*|(q, p)| + |.q.|^2 by Th58;
  hence thesis;
end;

theorem Th68:
  |.p-q.|^2 = |.p.|^2 - 2*|(q, p)| + |.q.|^2
proof
  |.p-q.|^2 = |(p-q, p-q)| by Th58
    .= |(p, p)| - 2*|(q, p)| + |(q, q)| by Th53
    .= |.p.|^2 - 2*|(q, p)| + |(q, q)| by Th58
    .= |.p.|^2 - 2*|(q, p)| + |.q.|^2 by Th58;
  hence thesis;
end;

theorem
  |.p+q.|^2 + |.p-q.|^2 = 2*(|.p.|^2 + |.q.|^2)
proof
A1: (|.p.|^2 - 2*|(p, q)| + |.q.|^2) = (|.p.|^2 + |.q.|^2 -2*|(p, q)| );
  |.p+q.|^2 + |.p-q.|^2 = (|.p.|^2 + 2*|(p, q)| + |.q.|^2) + |.p-q.|^2 by
  Th67
    .= (|.p.|^2 + |.q.|^2 + 2*|(p, q)|) + (|.p.|^2 + |.q.|^2 - 2*|(p, q)|)
  by A1,Th68
    .= 2*(|.p.|^2 + |.q.|^2);
  hence thesis;
end;

theorem
  |.p+q.|^2 - |.p-q.|^2 = 4* |(p,q)|
proof
  |.p+q.|^2 - |.p-q.|^2 = (|.p.|^2 + 2*|(p,q)| + |.q.|^2) - |.p-q.|^2 by
  Th67
    .= (|.p.|^2 + 2*|(p, q)| + |.q.|^2) - (|.p.|^2 - 2*|(p, q)| + |.q.|^2)
  by Th68
    .= 4*|(p, q)|;
  hence thesis;
end;

theorem
  |(p,q)| = (1/4)*(|.p+q.|^2 - |.p-q.|^2)
proof
  |.p+q.|^2 - |.p-q.|^2 = (|.p.|^2 + 2*|(p,q)| + |.q.|^2) - |.p-q.|^2 by
  Th67
    .= (|.p.|^2 + 2*|(p, q)| + |.q.|^2) - (|.p.|^2 - 2*|(p, q)| + |.q.|^2)
  by Th68
    .= 4*|(p, q)|;
  hence thesis;
end;

theorem
  |(p,q)| <= |(p,p)| + |(q,q)|
proof
  0 <= |(p,p)| & 0 <= |(q,q)| by Th57;
  then 0 + 0 <= |(p,p)| + |(q,q)| by XREAL_1:9;
  then
A1: 0/2 <= (|(p,p)| + |(q,q)|)/2 by XREAL_1:74;
  |(p-q, p-q)| = |(p,p)| - 2*|(p,q)| + |(q,q)| by Th53
    .= |(p,p)| + |(q,q)| - 2*|(p,q)|;
  then 2*|(p,q)| <= |(p,p)| + |(q,q)| - 0 by Th57,XREAL_1:13;
  then (2*|(p,q)|)/2 <= (|(p,p)| + |(q,q)|)/2 by XREAL_1:74;
  then 0 + |(p,q)| <= (|(p,p)| + |(q,q)|)/2 + (|(p,p)| + |(q,q)|)/2 by A1,
  XREAL_1:9;
  hence thesis;
end;

theorem :: Schwartz
  abs |(p,q)| <= |.p.|*|.q.|
proof
  len p = n & len q = n by FINSEQ_1:def 18;
  hence thesis by Th37;
end;

theorem :: Triangle
  |.p+q.| <= |.p.| + |.q.|
proof
A1: len p = n & len q = n by FINSEQ_1:def 18;
  p+q = p qua real-valued FinSequence+q by EUCLID:68;
  hence thesis by A1,Th38;
end;

theorem Th75:
  p,0.TOP-REAL n are_orthogonal
proof
  |(p,0.TOP-REAL n)|=0 by Th54;
  hence thesis by RVSUM_1:def 18;
end;

theorem
  0.TOP-REAL n,p are_orthogonal by Th75;

theorem Th77:
  p,p are_orthogonal iff p=0.TOP-REAL n
proof
  p,p are_orthogonal iff |(p,p)|=0 by RVSUM_1:def 18;
  hence thesis by Th63;
end;

theorem Th78:
  p, q are_orthogonal implies a*p,q are_orthogonal
proof
  assume p, q are_orthogonal;
  then |(p,q)|=0 by RVSUM_1:def 18;
  then a*(|(p,q)|)=0;
  then |(a*p,q)|=0 by Th41;
  hence thesis by RVSUM_1:def 18;
end;

theorem
  p, q are_orthogonal implies p,a*q are_orthogonal by Th78;

theorem
  (for q holds p,q are_orthogonal) implies p=0.TOP-REAL n
proof
  assume for q holds p,q are_orthogonal;
  then p,p are_orthogonal;
  hence thesis by Th77;
end;
