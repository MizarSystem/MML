:: Infimum and Supremum of the Set of Real Numbers. Measure Theory
::  by J\'ozef Bia{\l}as
::
:: Received September 27, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies BOOLE, SEQ_2, ARYTM, LATTICES, ORDINAL2, TARSKI, SUPINF_1,
      SETFAM_1, NAT_1, MEMBERED;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, ORDINAL1, NUMBERS,
      DOMAIN_1, MEMBERED, XXREAL_0, XREAL_0, REAL_1;
 constructors DOMAIN_1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, MEMBERED, NAT_D;
 registrations XREAL_0, SUBSET_1, XBOOLE_0, MEMBERED, NUMBERS, XXREAL_0;
 requirements SUBSET, BOOLE;
 definitions TARSKI, XBOOLE_0, XXREAL_0, MEMBERED;
 theorems AXIOMS, TARSKI, ZFMISC_1, XREAL_0, XBOOLE_0, XBOOLE_1, XREAL_1,
      XXREAL_0, MEMBERED;
 schemes XBOOLE_0, MEMBERED;

begin

theorem
  not +infty in REAL;

definition let IT be set;
 canceled;

  attr IT is +Inf-like means :Def2:
    IT = +infty;
end;

registration
  cluster +Inf-like set;
  existence
  proof
    take +infty;
    thus thesis by Def2;
  end;
end;

definition
  mode +Inf is +Inf-like set;
end;

canceled 2;

theorem
  +infty is +Inf by Def2;

canceled;

theorem
  not -infty in REAL by XREAL_0:def 1;

definition let IT be set;
 canceled;

  attr IT is -Inf-like means :Def4:
    IT = -infty;
end;

registration
  cluster -Inf-like set;
  existence
  proof
    take -infty;
    thus thesis by Def4;
  end;
end;

definition
  mode -Inf is -Inf-like set;
end;

canceled;

theorem
  -infty is -Inf by Def4;

definition
  mode R_eal is Element of ExtREAL;
end;

canceled 2;

theorem Th11:
  -infty is ext-real number & +infty is ext-real number;

definition
  redefine func +infty -> R_eal;
  coherence by XXREAL_0:def 1;
  redefine func -infty -> R_eal;
  coherence by XXREAL_0:def 1;
end;

definition let x,y be ext-real number;
 redefine
 canceled 2;

  pred x <= y means
    ex p,q being Real st p = x & q = y & p <= q if x in REAL & y in REAL
    otherwise x = -infty or y = +infty;
  consistency;
  compatibility
   proof
F:   x in ExtREAL by XXREAL_0:def 1;
G:   y in ExtREAL by XXREAL_0:def 1;
    thus x in REAL & y in REAL implies
     (x <= y iff ex p,q being Real st p = x & q = y & p <= q);
    thus not(x in REAL & y in REAL) implies
     (x <= y iff x = -infty or y = +infty)
     proof assume
A1:     not(x in REAL & y in REAL);
      per cases by A1;
      suppose not x in REAL;
        then
A2:       x in {-infty,+infty} by XBOOLE_0:def 2,F;
       thus x <= y implies x = -infty or y = +infty
        proof assume
A3:        x <= y;
         assume x <> -infty;
          then x = +infty by A2,TARSKI:def 2;
         hence thesis by A3,XXREAL_0:4;
        end;
       thus thesis by XXREAL_0:3,5;
      end;
      suppose not y in REAL;
        then
A4:       y in {-infty,+infty} by XBOOLE_0:def 2,G;
       thus x <= y implies x = -infty or y = +infty
        proof assume
A5:        x <= y;
         assume
A6:        x <> -infty;
         assume y <> +infty;
          then y = -infty by A4,TARSKI:def 2;
         hence thesis by A5,XXREAL_0:6,A6;
        end;
       thus thesis by XXREAL_0:3,5;
      end;
     end;
   end;
  end;

scheme SepReal {P[R_eal]}:
   ex X being Subset of ExtREAL st
   for x being R_eal holds x in X iff P[x]
proof defpred Q[set] means ex x being R_eal st x=$1 & P[x];
   consider X being set such that
A1:for r being set holds r in X iff r in REAL \/ {-infty,+infty} & Q[r]
     from XBOOLE_0:sch 1;
   X c= REAL \/ {-infty,+infty}
   proof
     let r be set;
     assume r in X;
     hence thesis by A1;
   end;
   then reconsider X as Subset of ExtREAL by MEMBERED:20;
   take X;
   let x be R_eal;
   hereby assume x in X;
     then ex y being R_eal st y = x & P[y] by A1;
     hence P[x];
   end;
   assume A2: P[x];
   thus thesis by A1,A2;
end;

canceled 20;

::
::       Majorant and minorant element of X being subset of ExtREAL
::

definition let X be ext-real-membered set;
 canceled;

  mode majorant of X -> ext-real number means :Def9:
    for x be ext-real number holds x in X implies x <= it;
  existence
  proof
    take +infty;
    thus thesis by XXREAL_0:4;
  end;
end;

canceled;

theorem Th33:
  for X being ext-real-membered set holds
    +infty is majorant of X
proof
   let X be ext-real-membered set;
   for x being ext-real number holds x in X implies x <= +infty by XXREAL_0:4;
   hence thesis by Def9;
end;

theorem Th34:
  for X,Y being ext-real-membered set st X c= Y holds
   for UB being ext-real number
    holds UB is majorant of Y implies UB is majorant of X
proof
   let X,Y be ext-real-membered set;
   assume
A1:X c= Y;
   let UB be ext-real number;
   assume UB is majorant of Y;
   then for x be ext-real number st x in X holds x <= UB by A1,Def9;
   hence thesis by Def9;
end;

definition let X be  ext-real-membered set;
  mode minorant of X -> ext-real number means :Def10:
    for x be ext-real number holds x in X implies it <= x;
existence
proof
   take -infty;
   thus thesis by XXREAL_0:5;
end;
end;

canceled;

theorem Th36:
  for X being ext-real-membered set holds
    -infty is minorant of X
proof
  let X be ext-real-membered set;
  for x being ext-real number st x in X holds -infty <= x by XXREAL_0:5;
  hence thesis by Def10;
end;

canceled 2;

theorem Th39:
  for X,Y being ext-real-membered set st X c= Y holds
    for LB being ext-real number
     holds LB is minorant of Y implies LB is minorant of X
proof
   let X,Y be ext-real-membered set;
   assume
A1:X c= Y;
   let LB be ext-real number;
   assume LB is minorant of Y;
   then for x be ext-real number st x in X holds LB <= x by A1,Def10;
   hence thesis by Def10;
end;

definition
  redefine func REAL -> non empty ext-real-membered set;
  coherence by XBOOLE_1:7;
end;

canceled;

theorem
  +infty is majorant of REAL by Th33;

theorem
  -infty is minorant of REAL by Th36;

::
::       Bounded above, bounded below and bounded sets of r_eal numbers
::

definition let X be ext-real-membered set;
  attr X is bounded_above means :Def11:
    ex UB being majorant of X st UB in REAL;
end;

canceled;

theorem Th44:
  for X,Y being ext-real-membered set st X c= Y & Y is bounded_above holds
    X is bounded_above
proof
   let X,Y be ext-real-membered set;
   assume
A1:X c= Y;
   assume Y is bounded_above;
   then ex UB being majorant of Y st UB in REAL by Def11;
   then consider UB being ext-real number such that
A2:UB is majorant of Y & UB in REAL;
   UB is majorant of X & UB in REAL by A1,A2,Th34;
   hence thesis by Def11;
end;

theorem
  not REAL is bounded_above
proof
   for X being non empty ext-real-membered set holds
   X = REAL implies not X is bounded_above
   proof
      let X be non empty ext-real-membered set;
      assume
   A1:X = REAL;
      assume X is bounded_above;
      then consider UB being majorant of X such that
   A2:UB in REAL by Def11;
      reconsider UB as Real by A2;
      consider x being real number such that
   A3:UB < x by XREAL_1:3;
      reconsider x as Real by XREAL_0:def 1;
      reconsider x as ext-real number by XXREAL_0:def 1;
      reconsider UB as ext-real number;
   A4:x <= UB by A1,Def9;
      consider a,b being ext-real number;
      thus thesis by A3,A4;
   end;
   hence thesis;
end;

definition let X be ext-real-membered set;
  attr X is bounded_below means :Def12:
    ex LB being minorant of X st LB in REAL;
end;

canceled;

theorem Th47:
  for X,Y being ext-real-membered set st
   X c= Y & Y is bounded_below holds X is bounded_below
proof
   let X,Y be ext-real-membered set;
   assume
A1:X c= Y;
   assume Y is bounded_below;
then ex LB being minorant of Y st LB in REAL by Def12;
   then consider LB being ext-real number such that
A2:LB is minorant of Y & LB in REAL;
   LB is minorant of X & LB in REAL by A1,A2,Th39;
   hence thesis by Def12;
end;

theorem
  not REAL is bounded_below
proof
   for X being non empty ext-real-membered set holds
   X = REAL implies not X is bounded_below
   proof
      let X be non empty ext-real-membered set;
      assume
   A1:X = REAL;
      assume X is bounded_below;
      then consider LB being minorant of X such that
   A2:LB in REAL by Def12;
      reconsider LB as Real by A2;
      consider x being real number such that
   A3:x < LB by XREAL_1:4;
      reconsider x as Real by XREAL_0:def 1;
      reconsider x as ext-real number by XXREAL_0:def 1;
      reconsider LB as ext-real number;
   A4:LB <= x by A1,Def10;
      consider a,b being ext-real number;
      thus thesis by A3,A4;
   end;
   hence thesis;
end;

definition let X be ext-real-membered set;
  attr X is bounded means :Def13:
    X is bounded_above bounded_below;
end;

canceled;

theorem
  for X,Y being ext-real-membered set st
   X c= Y & Y is bounded holds X is bounded
proof
   let X,Y be ext-real-membered set;
   assume
A1:X c= Y;
   assume Y is bounded;
   then Y is bounded_above bounded_below by Def13;
   then X is bounded_above bounded_below by A1,Th44,Th47;
   hence thesis by Def13;
end;

theorem Th51:
   for X being ext-real-membered set holds
   ex Y being non empty ext-real-membered set st
   for x being ext-real number holds x in Y iff x is majorant of X
proof
   let X be ext-real-membered set;
   defpred P[ext-real number] means $1 is majorant of X;
   consider Y being ext-real-membered set such that
A1:for x being ext-real number holds x in Y iff P[x] from MEMBERED:sch 2;
    +infty is majorant of X by Th33;
   then reconsider Y as non empty set by A1;
   reconsider Y as non empty ext-real-membered set;
   take Y;
   thus thesis by A1;
end;

::
::       Set of majorant and set of minorant of X being a subset of ExtREAL
::

definition let X be ext-real-membered set;
  func SetMajorant(X) -> ext-real-membered set means :Def14:
    for x being ext-real number holds x in it iff x is majorant of X;
existence
  proof
   consider Y being non empty ext-real-membered set such that
A1:   for x being ext-real number holds x in Y iff x is majorant of X by Th51;
  take Y;
  thus thesis by A1;
  end;
uniqueness
proof
   let Y1,Y2 be ext-real-membered set such that
A2:for x being ext-real number holds
   x in Y1 iff x is majorant of X and
A3:for x being ext-real number holds
   x in Y2 iff x is majorant of X;
      let x be ext-real number;
      x in Y1 iff x is majorant of X by A2;
      hence thesis by A3;
end;
end;

registration let X be ext-real-membered set;
  cluster SetMajorant(X) -> non empty;
  coherence
  proof
    consider x being majorant of X;
    x in SetMajorant(X) by Def14;
    hence thesis;
  end;
end;

canceled 2;

theorem
  for X,Y being ext-real-membered set st X c= Y holds
    for x being ext-real number
     holds x in SetMajorant Y implies x in SetMajorant(X)
proof
   let X,Y be ext-real-membered set;
   assume
A1:X c= Y;
   let x be ext-real number;
   assume x in SetMajorant(Y);
   then x is majorant of Y by Def14;
   then x is majorant of X by A1,Th34;
   hence thesis by Def14;
end;

theorem Th55:
  for X being ext-real-membered set holds
   ex Y being non empty ext-real-membered set st
   for x being ext-real number holds x in Y iff x is minorant of X
proof
   let X be ext-real-membered set;
   defpred P[ext-real number] means $1 is minorant of X;
   consider Y being ext-real-membered set such that
A1:for x being ext-real number holds x in Y iff P[x] from MEMBERED:sch 2;
   -infty is minorant of X by Th36;
   then reconsider Y as non empty set by A1;
   reconsider Y as non empty ext-real-membered set;
   take Y;
   thus thesis by A1;
end;

definition let X be ext-real-membered set;
  func SetMinorant(X) -> ext-real-membered set means :Def15:
    for x being ext-real number holds x in it iff x is minorant of X;
existence
 proof
  consider Y being non empty ext-real-membered set such that
A1:  for x being ext-real number holds x in Y iff x is minorant of X by Th55;
  take Y;
  thus thesis by A1;
 end;
uniqueness
proof
   let Y1,Y2 be ext-real-membered set such that
A2:for x being ext-real number holds
   x in Y1 iff x is minorant of X and
A3:for x being ext-real number holds
   x in Y2 iff x is minorant of X;
     let x be ext-real number;
     x in Y1 iff x is minorant of X by A2;
     hence thesis by A3;
end;
end;

registration let X be ext-real-membered set;
  cluster SetMinorant(X) -> non empty;
  coherence
  proof
    consider x being minorant of X;
    x in SetMinorant(X) by Def15;
    hence thesis;
  end;
end;

canceled 2;

theorem
  for X,Y being ext-real-membered set st X c= Y holds
   for x being ext-real number
    holds x in SetMinorant(Y) implies x in SetMinorant(X)
proof
   let X,Y be ext-real-membered set;
   assume
A1:X c= Y;
   let x be ext-real number;
   assume x in SetMinorant(Y);
   then x is minorant of Y by Def15;
   then x is minorant of X by A1,Th39;
   hence thesis by Def15;
end;

theorem Th59:
   for X being non empty ext-real-membered set st
   X is bounded_above & X <> {-infty} holds
   ex x being Real st x in X & x <> -infty
proof
   let X be non empty ext-real-membered set;
   assume
A1:X is bounded_above & X <> {-infty};
   ex x being set st x in X & x <> -infty
   proof
      assume
A2:   not ex x being set st x in X & x <> -infty;
      for x being set holds x in X implies x in {-infty}
      proof
        let x be set;
        assume x in X;
        then x = -infty by A2;
        hence thesis by TARSKI:def 1;
      end;
      then X c= {-infty} by TARSKI:def 3;
      hence thesis by A1,ZFMISC_1:39;
   end;
   then consider x being set such that
A3:x in X & x <> -infty;
   x in REAL
   proof
A4:  x <> +infty
     proof
       assume
    A5:x = +infty;
       then reconsider x as ext-real number;
       consider UB0 being majorant of X such that
    A6:UB0 in REAL by A1,Def11;
       reconsider UB0 as Real by A6;
       reconsider UB0 as ext-real number;
       x <= UB0 by A3,Def9;
       hence thesis by A5,XXREAL_0:9;
     end;
     X c= ExtREAL by MEMBERED:2;
     then x in REAL or x in {-infty,+infty} by A3,XBOOLE_0:def 2;
     hence thesis by A3,A4,TARSKI:def 2;
   end;
   then reconsider x as Real;
   take x;
   thus thesis by A3;
end;

theorem Th60:
   for X being non empty ext-real-membered set st
   X is bounded_below & not X = {+infty} holds
   ex x being Real st x in X & x <> +infty
proof
   let X be non empty ext-real-membered set;
   assume
A1:X is bounded_below & X <> {+infty};
   ex x being set st x in X & x <> +infty
   proof
      assume
A2:   not ex x being set st x in X & x <> +infty;
      for x being set holds x in X implies x in {+infty}
      proof
        let x be set;
        assume x in X;
        then x = +infty by A2;
        hence thesis by TARSKI:def 1;
      end;
      then X c= {+infty} by TARSKI:def 3;
      hence thesis by A1,ZFMISC_1:39;
   end;
   then consider x being set such that
A3:x in X & x <> +infty;
   x in REAL
   proof
   A4:x <> -infty
      proof
        assume
     A5:x = -infty;
        then reconsider x as ext-real number;
        consider LB0 being minorant of X such that
     A6:LB0 in REAL by A1,Def12;
        reconsider LB0 as Real by A6;
        reconsider LB0 as ext-real number;
        LB0 <= x by A3,Def10;
        hence thesis by A5,XXREAL_0:12;
      end;
       X c= ExtREAL by MEMBERED:2;
       then x in REAL or x in {-infty,+infty} by A3,XBOOLE_0:def 2;
      hence thesis by A3,A4,TARSKI:def 2;
   end;
   then reconsider x as Real;
   take x;
   thus thesis by A3;
end;

canceled;

theorem Th62:
   for X being non empty ext-real-membered set st
   X is bounded_above & X <> {-infty} holds
   ex UB being ext-real number st UB is majorant of X &
   for y being ext-real number st y is majorant of X holds UB <= y
proof
   let X be non empty ext-real-membered set;
   assume
A1:X is bounded_above & X <> {-infty};
   then consider UB0 being majorant of X such that
A2:UB0 in REAL by Def11;
A3:UB0 in SetMajorant(X) by Def14;
   consider x being Real such that
A4:x in X & x <> -infty by A1,Th59;
   reconsider S = X /\ REAL as Subset of REAL by XBOOLE_1:17;
   reconsider Y = REAL /\ SetMajorant(X) as Subset of REAL by XBOOLE_1:17;
   reconsider UB0 as Real by A2;
A5:x in S by A4,XBOOLE_0:def 3;
      now let a,b be real number;
       assume
   A6:a in S & b in Y;
   then A7:a in X by XBOOLE_0:def 3;
   A8:b in SetMajorant(X) by A6,XBOOLE_0:def 3;
A9:  a is Real & b is Real by XREAL_0:def 1;
    reconsider d = b as ext-real number by XXREAL_0:def 1;
A10:d is majorant of X by A8,Def14;
    reconsider c = a as ext-real number by XXREAL_0:def 1;
    c <= d by A7,A10,Def9;
    then ex p,q being Real st p = c & q = d & p <= q by A9;
    hence a <= b;
   end;
   then consider UB being real number such that
A11:for a,b being real number st a in S & b in Y holds a <= UB & UB <= b
       by AXIOMS:26;
    set d = UB;
    reconsider UB as Real by XREAL_0:def 1;
    reconsider UB as ext-real number by XXREAL_0:def 1;
A12:now let a,b be ext-real number;
       assume
   A13:a in X & b in SetMajorant(X);
       thus a <= UB & UB <= b
       proof
       A14:(not a = -infty & b = +infty) implies a <= UB & UB <= b
           proof
             assume
         A15:a <> -infty & b = +infty;
             a in REAL
             proof
           A16:a <> +infty
               proof
                 assume
             A17:a = +infty;
                 reconsider UB0 as ext-real number;
                 a <= UB0 by A13,Def9;
                 hence thesis by A17,XXREAL_0:9;
               end;
               a in ExtREAL by XXREAL_0:def 1;
               then a in REAL or a in {-infty,+infty} by XBOOLE_0:def 2;
               hence thesis by A15,A16,TARSKI:def 2;
             end;
             then reconsider a as Real;
         A18:a in S by A13,XBOOLE_0:def 3;
             a <= d & d <= UB0
             proof
               UB0 in Y by A3,XBOOLE_0:def 3;
               hence thesis by A11,A18;
             end;
             hence thesis by A15,XXREAL_0:4;
           end;
      A19:(a = -infty & not b = +infty) implies a <= UB & UB <= b
           proof
             assume
         A20:a = -infty & b <> +infty;
A21:         b is majorant of X by A13,Def14;
             reconsider x as ext-real number by XXREAL_0:def 1;
             x <= b by A4,A21,Def9;then
         A22:b <> -infty by XXREAL_0:12;
             b in ExtREAL by XXREAL_0:def 1;
             then b in REAL or b in {-infty,+infty} by XBOOLE_0:def 2;
             then reconsider b as Real by A20,A22,TARSKI:def 2;
         A23:b in Y by A13,XBOOLE_0:def 3;
             reconsider x as Real;
             x <= d & d <= b by A5,A11,A23;
             hence thesis by A20,XXREAL_0:5;
           end;
           (a <> -infty & b <> +infty) implies a <= UB & UB <= b
           proof
             assume
         A24:a <> -infty & b <> +infty;
             a in REAL
             proof
           A25:a <> +infty
               proof
                 assume
             A26:a = +infty;
                 reconsider UB0 as ext-real number;
                 a <= UB0 by A13,Def9;
                 hence thesis by A26,XXREAL_0:9;
               end;
             a in ExtREAL by XXREAL_0:def 1;
               then a in REAL or a in {-infty,+infty} by XBOOLE_0:def 2;
               hence thesis by A24,A25,TARSKI:def 2;
             end;
             then reconsider a as Real;
A27:         b is majorant of X by A13,Def14;
             reconsider x as ext-real number by XXREAL_0:def 1;
             x <= b by A4,A27,Def9;
             then A28:not b = -infty by XXREAL_0:12;
             b in ExtREAL by XXREAL_0:def 1;
             then b in REAL or b in {-infty,+infty} by XBOOLE_0:def 2;
             then reconsider b as Real by A24,A28,TARSKI:def 2;
         A29:b in Y by A13,XBOOLE_0:def 3;
             a in S by A13,XBOOLE_0:def 3;
             then a <= d & d <= b by A11,A29;
             hence thesis;
           end;
           hence thesis by A14,A19,XXREAL_0:3,5;
       end;
   end;
A30:UB is majorant of X
   proof
      for a being ext-real number st a in X holds a <= UB by A3,A12;
      hence thesis by Def9;
   end;
A31:for y being ext-real number st y is majorant of X holds UB <= y
   proof
     let y be ext-real number;
     assume y is majorant of X;
     then y in SetMajorant(X) by Def14;
     hence thesis by A4,A12;
   end;
   take UB;
   thus thesis by A30,A31;
end;

theorem Th63:
   for X being non empty ext-real-membered set st
   X is bounded_below & X <> {+infty} holds
   ex LB being ext-real number st LB is minorant of X &
   (for y being ext-real number st y is minorant of X holds y <= LB)
proof
   let X be non empty ext-real-membered set;
   assume
A1:X is bounded_below & X <> {+infty};
   then consider LB0 being minorant of X such that
A2:LB0 in REAL by Def12;
A3:LB0 in SetMinorant(X) by Def15;
   consider x being Real such that
A4:x in X & not x = +infty by A1,Th60;
   reconsider S = X /\ REAL as Subset of REAL by XBOOLE_1:17;
   reconsider Y = REAL /\ SetMinorant(X) as Subset of REAL by XBOOLE_1:17;
   reconsider LB0 as Real by A2;
A5:x in S by A4,XBOOLE_0:def 3;
      now let b,a be real number;
      assume
   A6:b in Y & a in S;
   then A7:a in X by XBOOLE_0:def 3;
   A8:b in SetMinorant(X) by A6,XBOOLE_0:def 3;
   A9: a is Real & b is Real by XREAL_0:def 1;
       reconsider d = b as ext-real number by XXREAL_0:def 1;
   A10:d is minorant of X by A8,Def15;
       reconsider c = a as ext-real number by XXREAL_0:def 1;
       d <= c by A7,A10,Def10;
       then ex p,q being Real st p = d & q = c & p <= q by A9;
       hence b <= a;
   end;
   then consider LB being real number such that
A11:for b,a being real number st b in Y & a in S holds b <= LB & LB <= a by
AXIOMS:26;
   reconsider LB as Real by XREAL_0:def 1;
   set d = LB;
   reconsider LB as ext-real number by XXREAL_0:def 1;
A12:for b,a being ext-real number st b in SetMinorant(X) & a in X holds
      b <= LB & LB <= a
   proof
       let b,a be ext-real number;
       assume
   A13:b in SetMinorant(X) & a in X;
   A14:(b = -infty & a <> +infty) implies b <= LB & LB <= a
       proof
         assume
     A15:b = -infty & a <> +infty;
         a in REAL
         proof
       A16:a <> -infty
           proof
             assume
         A17:a = -infty;
             reconsider LB0 as ext-real number;
             LB0 <= a by A13,Def10;
             hence thesis by A17,XXREAL_0:12;
           end;
           a in ExtREAL by XXREAL_0:def 1;
           then a in REAL or a in {-infty,+infty} by XBOOLE_0:def 2;
           hence thesis by A15,A16,TARSKI:def 2;
         end;
         then reconsider a as Real;
       A18:a in S by A13,XBOOLE_0:def 3;
           LB0 <= d & d <= a
           proof
             LB0 in Y by A3,XBOOLE_0:def 3;
             hence thesis by A11,A18;
           end;
           hence thesis by A15,XXREAL_0:5;
       end;
   A19:(b <> -infty & a = +infty) implies b <= LB & LB <= a
       proof
           assume
       A20:not b = -infty & a = +infty;
A21:       b is minorant of X by A13,Def15;
           reconsider x as ext-real number by XXREAL_0:def 1;
          b <= x by A4,A21,Def10;
       then A22:not b = +infty by XXREAL_0:9;
             b in ExtREAL by XXREAL_0:def 1;
           then b in REAL or b in {-infty,+infty} by XBOOLE_0:def 2;
           then reconsider b as Real by A20,A22,TARSKI:def 2;
       A23:b in Y by A13,XBOOLE_0:def 3;
           reconsider x as Real;
          b <= d & d <= x by A5,A11,A23;
           hence thesis by A20,XXREAL_0:4;
       end;
      (not b = -infty & not a = +infty) implies b <= LB & LB <= a
       proof
         assume
     A24:b <> -infty & a <> +infty;
         a in REAL
         proof
       A25:a <> -infty
           proof
             assume
         A26:a = -infty;
             reconsider LB0 as ext-real number;
             LB0 <= a by A13,Def10;
             hence thesis by A26,XXREAL_0:12;
           end;
           a in ExtREAL by XXREAL_0:def 1;
           then a in REAL or a in {-infty,+infty} by XBOOLE_0:def 2;
           hence thesis by A24,A25,TARSKI:def 2;
         end;
         then reconsider a as Real;
A27:     b is minorant of X by A13,Def15;
         reconsider x as ext-real number by XXREAL_0:def 1;
         b <= x by A4,A27,Def10;
         then A28:not b = +infty by XXREAL_0:9;
         b in ExtREAL by XXREAL_0:def 1;
         then b in REAL or b in {-infty,+infty} by XBOOLE_0:def 2;
         then reconsider b as Real by A24,A28,TARSKI:def 2;
     A29:b in Y by A13,XBOOLE_0:def 3;
         a in S by A13,XBOOLE_0:def 3;
         then b <= d & d <= a by A11,A29;
         hence thesis;
       end;
       hence thesis by A14,A19,XXREAL_0:3,5;
   end;
   take LB;
   for a being ext-real number st a in X holds LB <= a by A3,A12;
   hence LB is minorant of X by Def10;
   let y be ext-real number;
   assume
   y is minorant of X;
   then y in SetMinorant(X) by Def15;
   hence thesis by A4,A12;
end;

theorem Th64:
  for X being ext-real-membered set st
    X = {-infty} holds X is bounded_above
proof
   let X be ext-real-membered set;
   assume
A1:X = {-infty};
   consider x being Element of REAL;
   reconsider x as ext-real number;
   now let a be ext-real number;
     assume a in X;
     then a = -infty by A1,TARSKI:def 1;
     hence a <= x by XXREAL_0:5;
   end;
   then x is majorant of X by Def9;
   hence thesis by Def11;
end;

theorem Th65:
  for X being ext-real-membered set st
    X = {+infty} holds X is bounded_below
proof
   let X be ext-real-membered set;
   assume
A1:X = {+infty};
   consider x being Element of REAL;
   reconsider x as ext-real number;
   now let a be ext-real number;
     assume a in X;
     then a = +infty by A1,TARSKI:def 1;
     hence x <= a by XXREAL_0:4;
   end;
   then x is minorant of X by Def10;
   hence thesis by Def12;
end;

theorem Th66:
   for X being ext-real-membered set st X = {-infty} holds
   ex UB being ext-real number st UB is majorant of X &
   for y being ext-real number st y is majorant of X holds UB <= y
proof
   let X be ext-real-membered set;
   assume
A1:X = {-infty};
   set UB = -infty;
A2:for a being ext-real number holds a in X implies a <= UB by A1,TARSKI:def 1;
   take UB;
   thus thesis by A2,Def9,XXREAL_0:5;
end;

theorem Th67:
   for X being ext-real-membered set st
   X = {+infty} holds
   ex LB being ext-real number st LB is minorant of X &
   for y being ext-real number st y is minorant of X holds y <= LB
proof
   let X be ext-real-membered set;
   assume
A1:X = {+infty};
   set LB = +infty;
A2:   for a being ext-real number holds a in X implies LB <= a
 by A1,TARSKI:def 1;
   take LB;
   thus thesis by A2,Def10,XXREAL_0:4;
end;

theorem Th68:
   for X being non empty ext-real-membered set st
   X is bounded_above holds
   ex UB being ext-real number st UB is majorant of X &
   for y being ext-real number st y is majorant of X holds UB <= y
proof
   let X be non empty ext-real-membered set;
   assume X is bounded_above;
then (X is bounded_above & not X = {-infty}) or (X is bounded_above & X = {
-infty});
   hence thesis by Th62,Th66;
end;

theorem Th69:
   for X being non empty ext-real-membered set st
   X is bounded_below holds
   ex LB being ext-real number st LB is minorant of X &
   for y being ext-real number st
   y is minorant of X holds y <= LB
proof
   let X be non empty ext-real-membered set;
   assume X is bounded_below;
then (X is bounded_below & X <> {+infty}) or (X is bounded_below & X = {
+infty});
   hence thesis by Th63,Th67;
end;

theorem Th70:
   for X being non empty ext-real-membered set st
   not X is bounded_above holds
   for y being ext-real number st y is majorant of X holds y = +infty
proof
   let X be non empty ext-real-membered set;
   assume
A1:not X is bounded_above;
   for y being ext-real number st
   y is majorant of X holds y = +infty
   proof
     let y be ext-real number;
F:   y in ExtREAL by XXREAL_0:def 1;
     assume
  A2:y is majorant of X;
     then not y in REAL by A1,Def11;
     then A3: y in {-infty,+infty} by XBOOLE_0:def 2,F;
     y <> -infty
     proof
       assume
    A4:y = -infty;
       for a being set holds a in X implies a in {-infty}
       proof
         let a be set;
         assume
      A5:a in X;
         reconsider a as ext-real number by A5,MEMBERED:def 2;
         a <= -infty by A2,A4,A5,Def9;
          then a = -infty by XXREAL_0:6;
         hence thesis by TARSKI:def 1;
       end;
       then X c= {-infty} by TARSKI:def 3;
       then X = {} or X = {-infty} by ZFMISC_1:39;
       hence thesis by A1,Th64;
     end;
     hence thesis by A3,TARSKI:def 2;
   end;
   hence thesis;
end;

theorem Th71:
   for X being non empty ext-real-membered set st
   not X is bounded_below holds
   for y being ext-real number st
   y is minorant of X holds y = -infty
proof
   let X be non empty ext-real-membered set;
   assume
A1:not X is bounded_below;
   for y being ext-real number st
   y is minorant of X holds y = -infty
   proof
     let y be ext-real number;
F:   y in ExtREAL by XXREAL_0:def 1;
     assume
  A2:y is minorant of X;
     then not y in REAL by A1,Def12;
     then A3: y in {-infty,+infty} by XBOOLE_0:def 2,F;
     y <> +infty
     proof
       assume
    A4:y = +infty;
       for a being set st a in X holds a in {+infty}
       proof
         let a be set;
         assume
      A5:a in X;
         reconsider a as ext-real number by A5,MEMBERED:def 2;
         +infty <= a by A2,A4,A5,Def10;
         then a = +infty by XXREAL_0:4;
         hence thesis by TARSKI:def 1;
       end;
       then X c= {+infty} by TARSKI:def 3;
       then X = {} or X = {+infty} by ZFMISC_1:39;
       hence thesis by A1,Th65;
     end;
      hence thesis by A3,TARSKI:def 2;
   end;
   hence thesis;
end;

theorem Th72:
   for X being non empty ext-real-membered set holds
   ex UB being ext-real number st UB is majorant of X &
   for y being ext-real number st y is majorant of X holds UB <= y
proof
   let X be non empty ext-real-membered set;
   not X is bounded_above implies
   ex UB being ext-real number st UB is majorant of X &
   for y being ext-real number st y is majorant of X holds UB <= y
   proof
      assume
A1:   not X is bounded_above;
      take +infty;
      thus thesis by A1,Th33,Th70;
   end;
   hence thesis by Th68;
end;

theorem Th73:
   for X being non empty ext-real-membered set holds
   ex LB being ext-real number st LB is minorant of X &
   for y being ext-real number st y is minorant of X holds y <= LB
proof
   let X be non empty ext-real-membered set;
   not X is bounded_below implies
   ex LB being ext-real number st LB is minorant of X &
   for y being ext-real number st y is minorant of X holds y <= LB
   proof
      assume
A1:   not X is bounded_below;
      take -infty;
      thus thesis by A1,Th36,Th71;
   end;
   hence thesis by Th69;
end;

::
::       sup X, inf X least upper bound and greatest lower bound of set X
::

definition let X be non empty ext-real-membered set;
  func sup X -> ext-real number means :Def16:
    it is majorant of X &
    for y being ext-real number st y is majorant of X holds it <= y;
existence by Th72;
uniqueness
proof
   let S1,S2 be ext-real number such that
A1:S1 is majorant of X &
   for y being ext-real number st
   y is majorant of X holds S1 <= y and
A2:S2 is majorant of X &
   for y being ext-real number st y is majorant of X holds S2 <= y;
   S1 <= S2 & S2 <= S1 by A1,A2;
   hence thesis by XXREAL_0:1;
end;
end;

canceled 2;

theorem Th76:
   for X being non empty ext-real-membered set,
       x being ext-real number st x in X holds x <= sup X
proof
   let X be non empty ext-real-membered set,
       x be ext-real number;
   assume
A1:x in X;
   sup X is majorant of X by Def16;
   hence thesis by A1,Def9;
end;

definition let X be non empty  ext-real-membered set;
   func inf X -> ext-real number means :Def17:
     it is minorant of X &
     for y being ext-real number st y is minorant of X holds y <= it;
existence by Th73;
uniqueness
proof
   let S1,S2 be ext-real number such that
A1:S1 is minorant of X &
   for y being ext-real number st y is minorant of X holds y <= S1 and
A2:S2 is minorant of X &
   for y being ext-real number st y is minorant of X holds y <= S2;
   S1 <= S2 & S2 <= S1 by A1,A2;
   hence thesis by XXREAL_0:1;
end;
end;

canceled 2;

theorem Th79:
   for X being non empty ext-real-membered set,
       x being ext-real number st x in X holds inf X <= x
proof
   let X be non empty ext-real-membered set, x be ext-real number;
   assume
A1:x in X;
   inf X is minorant of X by Def17;
   hence thesis by A1,Def10;
end;

theorem Th80:
  for X being non empty ext-real-membered set,
      x being majorant of X st
   x in X holds x = sup X
proof
   let X be non empty ext-real-membered set, x be majorant of X;
   assume x in X;
   then for y being ext-real number st y is majorant of X holds x <= y by Def9;
   hence thesis by Def16;
end;

theorem Th81:
   for X being non empty ext-real-membered set,
       x being minorant of X st
   x in X holds x = inf X
proof
   let X be non empty ext-real-membered set, x be minorant of X;
   assume x in X;
   then for y being ext-real number st
   y is minorant of X holds y <= x by Def10;
   hence thesis by Def17;
end;

theorem
  for X being non empty ext-real-membered set holds
    sup X = inf SetMajorant(X) & inf X = sup SetMinorant(X)
proof
   let X be non empty ext-real-membered set;
   sup X is majorant of X by Def16; then
A1:sup X in SetMajorant X by Def14;
   for y being ext-real number st y in SetMajorant X holds sup X <= y
   proof
     let y be ext-real number;
     assume y in SetMajorant X;
     then y is majorant of X by Def14;
     hence thesis by Def16;
   end;
   then A2: sup X is minorant of SetMajorant X by Def10;
   inf X is minorant of X by Def17;
then A3:inf X in SetMinorant X by Def15;
   for y being ext-real number st y in SetMinorant X holds y <= inf X
   proof
     let y be ext-real number;
     assume y in SetMinorant X;
     then y is minorant of X by Def15;
     hence thesis by Def17;
   end;
   then inf X is majorant of SetMinorant X by Def9;
   hence thesis by A1,A2,A3,Th80,Th81;
end;

theorem
  for X being non empty ext-real-membered set st
   X is bounded_above & X <> {-infty} holds sup X in REAL
proof
   let X be non empty ext-real-membered set;
   assume
A1:X is bounded_above & X <> {-infty};
   then consider UB0 being majorant of X such that
A2:UB0 in REAL by Def11;
A3:sup X <= UB0 by Def16;
   consider x being Real such that
A4:x in X & x <> -infty by A1,Th59;
   reconsider x as ext-real number by XXREAL_0:def 1;
   sup X is majorant of X by Def16;
   then x <= sup X by A4,Def9;
   then not sup X = -infty by XXREAL_0:12;
   hence thesis by A2,A3,XXREAL_0:13;
end;

theorem
  for X being non empty ext-real-membered set st
   X is bounded_below & X <> {+infty} holds inf X in REAL
proof
   let X be non empty ext-real-membered set;
   assume
A1:X is bounded_below & X <> {+infty};
   then consider LB0 being minorant of X such that
A2:LB0 in REAL by Def12;
A3:LB0 <= inf X by Def17;
   consider x being Real such that
A4:x in X & x <> +infty by A1,Th60;
   reconsider x as ext-real number by XXREAL_0:def 1;
   inf X is minorant of X by Def17;
   then inf X <= x by A4,Def10;
   then inf X <> +infty by XXREAL_0:9;
   hence thesis by A2,A3,XXREAL_0:10;
end;

theorem
  for x being ext-real number holds sup{x} = x
proof
   let x be ext-real number;
A1:sup{x} is majorant of {x} by Def16;
   x is majorant of {x}
   proof
     for a being ext-real number st a in {x} holds a <= x by TARSKI:def 1;
     hence thesis by Def9;
   end;
then A2:sup{x} <= x by Def16;
   x in {x} by TARSKI:def 1;
   then x <= sup{x} by A1,Def9;
   hence thesis by A2,XXREAL_0:1;
end;

theorem
  for x being ext-real number holds inf{x} = x
proof
   let x be ext-real number;
A1:inf{x} is minorant of {x} by Def17;
   x is minorant of {x}
   proof
     for a being ext-real number st a in {x} holds x <= a by TARSKI:def 1;
     hence thesis by Def10;
   end;
   then A2:x <= inf{x} by Def17;
   x in {x} by TARSKI:def 1;
   then inf{x} <= x by A1,Def10;
   hence thesis by A2,XXREAL_0:1;
end;

canceled 4;

theorem Th91:
  for X,Y being non empty ext-real-membered set st
    X c= Y holds sup X <= sup Y
proof
   let X,Y be non empty ext-real-membered set;
   assume
A1:X c= Y;
   set S2 = sup Y;
   S2 is majorant of Y &
   for y being ext-real number st y is majorant of Y holds S2 <= y by Def16;
   then S2 is majorant of X by A1,Th34;
   hence thesis by Def16;
end;

theorem Th92:
  for x,y being ext-real number,
      a being ext-real number st
   x <= a & y <= a holds sup{x,y} <= a
proof
   let x,y be ext-real number, a be ext-real number;
   assume x <= a & y <= a;
   then for c being ext-real number st c in {x,y} holds c <= a by TARSKI:def 2;
   then a is majorant of {x,y} by Def9;
   hence thesis by Def16;
end;

theorem
  for x,y being ext-real number holds
   (x <= y implies sup{x,y} = y) & (y <= x implies sup{x,y} = x)
proof
   let x,y be ext-real number;
   thus x <= y implies sup{x,y} = y
   proof
     assume x <= y;
     then A1:sup{x,y} <= y by Th92;
  A2:y in {x,y} by TARSKI:def 2;
     sup{x,y} is majorant of {x,y} by Def16;
     then y <= sup{x,y} by A2,Def9;
     hence thesis by A1,XXREAL_0:1;
   end;
   assume y <= x;
   then A3:sup{x,y} <= x by Th92;
A4:x in {x,y} by TARSKI:def 2;
   sup{x,y} is majorant of {x,y} by Def16;
   then x <= sup{x,y} by A4,Def9;
   hence thesis by A3,XXREAL_0:1;
end;

theorem Th94:
  for X,Y being non empty ext-real-membered set st
    X c= Y holds inf Y <= inf X
proof
   let X,Y be non empty ext-real-membered set;
   assume
A1:X c= Y;
   inf Y <= inf X
   proof
     set S2 = inf Y;
     S2 is minorant of Y &
     for y being ext-real number st y is minorant of Y holds y <= S2 by Def17;
     then S2 is minorant of X by A1,Th39;
     hence thesis by Def17;
   end;
   hence thesis;
end;

theorem Th95:
  for x,y being ext-real number,
      a being ext-real number st
    a <= x & a <= y holds a <= inf{x,y}
proof
   let x,y be ext-real number, a be ext-real number;
   assume a <= x & a <= y;
   then for c being ext-real number holds c in {x,y} implies a <= c
    by TARSKI:def 2;
   then a is minorant of {x,y} by Def10;
   hence thesis by Def17;
end;

theorem
  for x,y being ext-real number holds
    (x <= y implies inf{x,y} = x) & (y <= x implies inf{x,y} = y)
proof
   let x,y be ext-real number;
   thus x <= y implies inf{x,y} = x
   proof
     assume x <=y;
     then A1:x <= inf{x,y} by Th95;
  A2:x in {x,y} by TARSKI:def 2;
     inf{x,y} is minorant of {x,y} by Def17;
     then inf{x,y} <= x by A2,Def10;
     hence thesis by A1,XXREAL_0:1;
   end;
   assume y <=x;
   then A3:y <= inf{x,y} by Th95;
A4:y in {x,y} by TARSKI:def 2;
   inf{x,y} is minorant of {x,y} by Def17;
   then inf{x,y} <= y by A4,Def10;
   hence thesis by A3,XXREAL_0:1;
end;

theorem Th97:
  for X being non empty ext-real-membered set,
      x being ext-real number st
   (ex y being ext-real number st y in X & x <= y) holds x <= sup X
proof
   let X be non empty ext-real-membered set;
   let x be ext-real number;
   given y being ext-real number such that
A1:y in X & x <= y;
   sup X is majorant of X by Def16;
   then y <= sup X by A1,Def9;
   hence thesis by A1,XXREAL_0:2;
end;

theorem Th98:
  for X being non empty ext-real-membered set,
      x being ext-real number st
   (ex y being ext-real number st y in X & y <= x) holds inf X <= x
proof
   let X be non empty ext-real-membered set;
   let x be ext-real number;
   given y being ext-real number such that
A1:y in X & y <= x;
   inf X is minorant of X by Def17;
   then inf X <= y by A1,Def10;
   hence thesis by A1,XXREAL_0:2;
end;

theorem
  for X,Y being non empty ext-real-membered set st
   (for x being ext-real number st x in X holds
   (ex y being ext-real number st y in Y & x <= y)) holds
   sup X <= sup Y
proof
   let X,Y be non empty ext-real-membered set;
   assume
A1:for x being ext-real number st x in X
    ex y being ext-real number st y in Y & x <= y;
   for x being ext-real number st x in X holds x <= sup Y
   proof
     let x be ext-real number;
     assume x in X;
     then consider y being ext-real number such that
  A2:y in Y & x <= y by A1;
     y <= sup Y by A2,Th76;
     hence thesis by A2,XXREAL_0:2;
   end;
   then sup Y is majorant of X by Def9;
   hence thesis by Def16;
end;

theorem
  for X,Y being non empty ext-real-membered set st
   (for y being ext-real number st y in Y holds
   (ex x being ext-real number st x in X & x <= y)) holds
   inf X <= inf Y
proof
   let X,Y be non empty ext-real-membered set;
   assume
A1:for y being ext-real number st y in Y
    ex x being ext-real number st x in X & x <= y;
   for y being ext-real number st y in Y holds inf X <= y
   proof
      let y be ext-real number;
      assume y in Y;
      then consider x being ext-real number such that
   A2:x in X & x <= y by A1;
      inf X <= x by A2,Th79;
      hence thesis by A2,XXREAL_0:2;
   end;
   then inf X is minorant of Y by Def10;
   hence thesis by Def17;
end;

theorem Th101:
   for X,Y being ext-real-membered set,
       UB1 being majorant of X,
       UB2 being majorant of Y holds
   sup{UB1,UB2} is majorant of X \/ Y
proof
   let X,Y be ext-real-membered set,
       UB1 be majorant of X,
       UB2 be majorant of Y;
   for a being ext-real number st a in X \/ Y holds a <= sup{UB1,UB2}
   proof
     let a be ext-real number;
     assume
A1:  a in X \/ Y;
  A2:a in X implies a <= sup{UB1,UB2}
     proof
       assume a in X;
       then A3:a <= UB1 by Def9;
    A4:UB1 in {UB1,UB2} by TARSKI:def 2;
       sup{UB1,UB2} is majorant of {UB1,UB2} by Def16;
       then UB1 <= sup{UB1,UB2}by A4,Def9;
       hence thesis by A3,XXREAL_0:2;
     end;
     a in Y implies a <= sup{UB1,UB2}
     proof
        assume a in Y;
        then A5:a <= UB2 by Def9;
     A6:UB2 in {UB1,UB2} by TARSKI:def 2;
        sup{UB1,UB2} is majorant of {UB1,UB2} by Def16;
        then UB2 <= sup{UB1,UB2}by A6,Def9;
        hence thesis by A5,XXREAL_0:2;
      end;
      hence thesis by A1,A2,XBOOLE_0:def 2;
   end;
   hence thesis by Def9;
end;

theorem Th102:
   for X,Y being ext-real-membered set,
       LB1 being minorant of X,
       LB2 being minorant of Y holds
   inf{LB1,LB2} is minorant of X \/ Y
proof
   let X,Y be ext-real-membered set,
       LB1 be minorant of X,
       LB2 be minorant of Y;
   for a being ext-real number st a in X \/ Y holds inf{LB1,LB2} <= a
   proof
     let a be ext-real number;
     assume
A1:  a in X \/ Y;
  A2:a in X implies inf{LB1,LB2} <= a
     proof
       assume a in X;
       then A3:LB1 <= a by Def10;
    A4:LB1 in {LB1,LB2} by TARSKI:def 2;
       inf{LB1,LB2} is minorant of {LB1,LB2} by Def17;
       then inf{LB1,LB2} <= LB1 by A4,Def10;
       hence thesis by A3,XXREAL_0:2;
     end;
     a in Y implies inf{LB1,LB2} <= a
     proof
       assume a in Y;
       then A5:LB2 <= a by Def10;
    A6:LB2 in {LB1,LB2} by TARSKI:def 2;
       inf{LB1,LB2} is minorant of {LB1,LB2} by Def17;
       then inf{LB1,LB2} <= LB2 by A6,Def10;
       hence thesis by A5,XXREAL_0:2;
     end;
     hence thesis by A1,A2,XBOOLE_0:def 2;
   end;
   hence thesis by Def10;
end;

theorem Th103:
   for X,Y,S being ext-real-membered set,
       UB1 being majorant of X,
       UB2 being majorant of Y st
   S = X /\ Y holds inf{UB1,UB2} is majorant of S
proof
   let X,Y,S be ext-real-membered set,
       UB1 be majorant of X,
       UB2 be majorant of Y;
   assume
A1:S = X /\ Y;
   now let a be ext-real number;
     assume a in S;
     then a in X & a in Y by A1,XBOOLE_0:def 3;
     then a <= UB1 & a <= UB2 by Def9;
     hence a <= inf{UB1,UB2} by Th95;
   end;
   hence thesis by Def9;
end;

theorem Th104:
   for X,Y,S being ext-real-membered set,
       LB1 being minorant of X,
       LB2 being minorant of Y st
   S = X /\ Y holds sup{LB1,LB2} is minorant of S
proof
   let X,Y,S be ext-real-membered set,
       LB1 be minorant of X,
       LB2 be minorant of Y;
   assume
A1:S = X /\ Y;
   now let a be ext-real number;
     assume a in S;
     then a in X & a in Y by A1,XBOOLE_0:def 3;
     then LB1 <= a & LB2 <= a by Def10;
     hence sup{LB1,LB2} <= a by Th92;
   end;
   hence thesis by Def10;
end;

theorem
  for X,Y being non empty ext-real-membered set holds
    sup(X \/ Y) = sup{sup X,sup Y}
proof
   let X,Y be non empty ext-real-membered set;
   set a = sup(X \/ Y);
A1:sup X is majorant of X by Def16;
   sup Y is majorant of Y by Def16;
   then sup{sup X,sup Y} is majorant of X \/ Y by A1,Th101;
   then A2:a <= sup{sup X,sup Y} by Def16;
   X c= X \/ Y by XBOOLE_1:7;
   then A3:sup X <= a by Th91;
   Y c= X \/ Y by XBOOLE_1:7;
   then sup Y <= a by Th91;
   then sup{sup X,sup Y} <= a by A3,Th92;
   hence thesis by A2,XXREAL_0:1;
end;

theorem
  for X,Y being non empty ext-real-membered set holds
   inf(X \/ Y) = inf{inf X,inf Y}
proof
   let X,Y be non empty ext-real-membered set;
   set a = inf(X \/ Y);
A1:inf X is minorant of X by Def17;
   inf Y is minorant of Y by Def17;
   then inf{inf X,inf Y} is minorant of X \/ Y by A1,Th102;
   then A2:inf{inf X,inf Y} <= a by Def17;
   X c= X \/ Y by XBOOLE_1:7;
   then A3:a <= inf X by Th94;
   Y c= X \/ Y by XBOOLE_1:7;
   then a <= inf Y by Th94;
   then a <= inf{inf X,inf Y} by A3,Th95;
   hence thesis by A2,XXREAL_0:1;
end;

theorem
  for X,Y,S being non empty ext-real-membered set st
    S = X /\ Y holds sup(S) <= inf{sup X,sup Y}
proof
   let X,Y,S be non empty ext-real-membered set;
   assume
A1:S = X /\ Y;
   sup(S) <= inf{sup X,sup Y}
   proof
  A2:sup X is majorant of X by Def16;
     sup Y is majorant of Y by Def16;
  then inf{sup X,sup Y} is majorant of S by A1,A2,Th103;
     hence thesis by Def16;
   end;
   hence thesis;
end;

theorem
  for X,Y,S being non empty ext-real-membered set st
    S = X /\ Y holds sup{inf X,inf Y} <= inf(S)
proof
   let X,Y,S be non empty ext-real-membered set;
   assume
A1:S = X /\ Y;
   sup{inf X,inf Y} <= inf(S)
   proof
   A2:inf X is minorant of X by Def17;
      inf Y is minorant of Y by Def17;
  then sup{inf X,inf Y} is minorant of S by A1,A2,Th104;
      hence thesis by Def17;
   end;
   hence thesis;
end;

definition let X be non empty set;
   mode bool_DOMAIN of X -> set means :Def18:
     it is non empty Subset-Family of X &
     for A being set st A in it holds A is non empty;
existence
proof
   set F = {X};
A1: for x being set holds x in F implies x in bool X
   proof
     let x be set;
     assume
  A2:x in F;
     X in bool X by ZFMISC_1:def 1;
     hence thesis by A2,TARSKI:def 1;
   end;
   take F;
   thus thesis by A1,TARSKI:def 1,def 3;
end;
end;

definition let F be bool_DOMAIN of ExtREAL;
  func SUP(F) -> ext-real-membered set means :Def19:
    for a being ext-real number holds
      a in it iff ex A being non empty ext-real-membered set st
        A in F & a = sup A;
existence
proof
  defpred P[ext-real number] means
   ex A being non empty ext-real-membered set st A in F & $1 = sup A;
   consider S being ext-real-membered set such that
A1:for a being ext-real number holds a in S iff P[a] from MEMBERED:sch 2;
   reconsider S as ext-real-membered set;
   take S;
   thus thesis by A1;
end;
uniqueness
proof
   let S1,S2 be ext-real-membered set such that
A2:for a being ext-real number holds a in S1 iff
   ex A being non empty ext-real-membered set st A in F & a = sup A and
A3:for a being ext-real number holds a in S2 iff
   ex A being non empty ext-real-membered set st A in F & a = sup A;
      let a be ext-real number;
      hereby assume
        a in S1;
         then ex A being non empty ext-real-membered set
          st A in F & a = sup A by A2;
         hence a in S2 by A3;
      end;
      assume
   A4:a in S2;
         ex A being non empty ext-real-membered set
          st A in F & a = sup A by A3,A4;
      hence thesis by A2;
end;
end;

registration let F be bool_DOMAIN of ExtREAL;
   cluster SUP(F) -> non empty;
   coherence
   proof
A1:F is non empty Subset-Family of ExtREAL &
   for A being set st A in F holds A is non empty by Def18;
   consider A being Element of F;
   F <> {} by Def18;
   then A in F;
   then A is Subset of ExtREAL by A1;
   then reconsider A as non empty ext-real-membered set by A1;
   sup A = sup A;
   hence thesis by A1,Def19;
   end;
end;

canceled 3;

theorem Th112:
   for F being bool_DOMAIN of ExtREAL,
       S being non empty ext-real-membered number st
   S = union F holds sup S is majorant of SUP(F)
proof
   let F be bool_DOMAIN of ExtREAL,
       S be non empty ext-real-membered set;
   assume
A1:S = union F;
   for x being ext-real number st x in SUP(F) holds x <= sup S
   proof
     let x be ext-real number;
     assume x in SUP(F);
     then consider A being non empty ext-real-membered set such that
  A2:A in F & x = sup A by Def19;
     A c= S
     proof
       let a be ext-real number; assume a in A;
       hence thesis by A1,A2,TARSKI:def 4;
     end;
     hence thesis by A2,Th91;
   end;
   hence thesis by Def9;
end;

theorem Th113:
   for F being bool_DOMAIN of ExtREAL,
       S being ext-real-membered set st
   S = union F holds sup SUP(F) is majorant of S
proof
   let F be bool_DOMAIN of ExtREAL,
       S be ext-real-membered set;
   assume
A1:S = union F;
   for x being ext-real number st x in S holds x <= sup SUP(F)
   proof
     let x be ext-real number;
     assume x in S;
     then consider Z being set such that
  A2:x in Z & Z in F by A1,TARSKI:def 4;
     F is non empty Subset-Family of ExtREAL by Def18;
     then Z is Subset of ExtREAL by A2;
     then reconsider Z as non empty ext-real-membered set by A2;
     consider a being set such that
  A3:a = sup Z;
     reconsider a as ext-real number by A3;
     sup Z is majorant of Z by Def16;
     then A4:x <= sup Z by A2,Def9;
     a in SUP(F) by A2,A3,Def19;
     hence thesis by A3,A4,Th97;
   end;
   hence thesis by Def9;
end;

theorem
  for F being bool_DOMAIN of ExtREAL,
      S being non empty ext-real-membered set st
   S = union F holds sup S = sup SUP(F)
proof
   let F be bool_DOMAIN of ExtREAL,
       S be non empty ext-real-membered set;
   assume
A1:S = union F;
   set a = sup S;
   set b = sup SUP(F);
A2:sup SUP(F) is majorant of S by A1,Th113;
   sup S is majorant of SUP(F) by A1,Th112;
   then a <= b & b <= a by A2,Def16;
   hence thesis by XXREAL_0:1;
end;

definition let F be bool_DOMAIN of ExtREAL;
   func INF F -> ext-real-membered set means :Def20:
     for a being ext-real number holds
        a in it iff ex A being non empty ext-real-membered set st
        A in F & a = inf A;
existence
proof defpred P[ext-real number] means
   ex A being non empty ext-real-membered set st A in F & $1 = inf A;
A1:F is non empty Subset-Family of ExtREAL &
   for A being set holds A in F implies A is non empty set by Def18;
   consider A being Element of F;
   A is Subset of ExtREAL by A1,TARSKI:def 3;
   then reconsider A as non empty ext-real-membered set by A1,TARSKI:def 3;
A2:inf A = inf A;
   consider S being ext-real-membered set such that
A3: for a being ext-real number holds a in S iff P[a] from MEMBERED:sch 2;
   S is non empty set by A1,A2,A3;
   then reconsider S as non empty ext-real-membered set;
   take S;
   thus thesis by A3;
end;
uniqueness
proof
   let S1,S2 be ext-real-membered set such that
A4:for a being ext-real number holds a in S1 iff
   ex A being non empty ext-real-membered set st A in F & a = inf A and
A5:for a being ext-real number holds a in S2 iff
   ex A being non empty ext-real-membered set st A in F & a = inf A;
   for a being set holds a in S1 iff a in S2
   proof
      let a be set;
      hereby assume
      A6:a in S1;
         then reconsider a' = a as ext-real number by MEMBERED:def 2;
         ex A being non empty ext-real-membered set st A in F &
         a' = inf A by A4,A6;
         hence a in S2 by A5;
      end;
      assume
   A7:a in S2;
      then reconsider a as ext-real number by MEMBERED:def 2;
      ex A being non empty ext-real-membered set st A in F & a = inf A
       by A5,A7;
      hence thesis by A4;
   end;
   hence thesis by TARSKI:2;
end;
end;

registration let F be bool_DOMAIN of ExtREAL;
  cluster INF(F) -> non empty;
coherence
proof
A1:F is non empty Subset-Family of ExtREAL &
   for A being set holds A in F implies A is non empty set by Def18;
   consider A being Element of F;
   F <> {} by Def18;
   then A in F;
   then A is Subset of ExtREAL by A1;
   then reconsider A as non empty ext-real-membered set by A1;
   inf A = inf A;
   hence thesis by A1,Def20;
end;
end;

canceled 2;

theorem Th117:
   for F being bool_DOMAIN of ExtREAL,
       S being non empty ext-real-membered set st
   S = union F holds inf S is minorant of INF(F)
proof
   let F be bool_DOMAIN of ExtREAL,
       S be non empty ext-real-membered set;
   assume
A1:S = union F;
   for x being ext-real number st x in INF(F) holds inf S <= x
   proof
      let x be ext-real number;
      assume x in INF(F);
      then consider A being non empty ext-real-membered set such that
   A2:A in F & x = inf A by Def20;
      A c= S
      proof
        let a be ext-real number; assume a in A;
        hence thesis by A1,A2,TARSKI:def 4;
      end;
      hence thesis by A2,Th94;
   end;
   hence thesis by Def10;
end;

theorem Th118:
   for F being bool_DOMAIN of ExtREAL,
       S being ext-real-membered set st
   S = union F holds inf INF(F) is minorant of S
proof
   let F be bool_DOMAIN of ExtREAL,
       S be ext-real-membered set;
   assume
A1:S = union F;
   for x being ext-real number st x in S holds inf INF(F) <= x
   proof
     let x be ext-real number;
     assume x in S;
     then consider Z being set such that
  A2:x in Z & Z in F by A1,TARSKI:def 4;
X:   F is non empty Subset-Family of ExtREAL by Def18;
     F <> {} by Def18;
     then Z is Subset of ExtREAL by A1,A2,X;
     then reconsider Z as non empty ext-real-membered set by A1,A2;
     consider a being set such that
  A3:a = inf Z;
     reconsider a as ext-real number by A3;
     inf Z is minorant of Z by Def17;
     then A4:inf Z <= x by A2,Def10;
     a in INF(F) by A2,A3,Def20;
     hence thesis by A3,A4,Th98;
   end;
   hence thesis by Def10;
end;

theorem
  for F being bool_DOMAIN of ExtREAL,
      S being non empty ext-real-membered set st
   S = union F holds inf S = inf INF(F)
proof
   let F be bool_DOMAIN of ExtREAL,
       S be non empty ext-real-membered set;
   assume
A1:S = union F;
   set a = inf S;
   set b = inf INF(F);
A2:   inf INF(F) is minorant of S by A1,Th118;
   inf S is minorant of INF(F) by A1,Th117;
   then a <= b & b <= a by A2,Def17;
   hence thesis by XXREAL_0:1;
end;
