:: Infimum and Supremum of the Set of Real Numbers. Measure Theory
::  by J\'ozef Bia{\l}as
::
:: Received September 27, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies SUBSET_1, NUMBERS, XXREAL_0, MEMBERED, XXREAL_2, XREAL_0,
      ORDINAL1, XBOOLE_0, TARSKI, ORDINAL2, SETFAM_1, SUPINF_1;
 notations XBOOLE_0, SUBSET_1, SETFAM_1, DOMAIN_1, ORDINAL1, NUMBERS, MEMBERED,
      XXREAL_0, XREAL_0, XXREAL_2;
 constructors NUMBERS, XXREAL_0, XREAL_0, MEMBERED, SETFAM_1, DOMAIN_1,
      XXREAL_2;
 registrations XBOOLE_0, SUBSET_1, NUMBERS, XXREAL_0, XREAL_0, MEMBERED;
 requirements SUBSET, BOOLE;
 definitions TARSKI, XXREAL_0, MEMBERED, SUBSET_1, SETFAM_1, XXREAL_2;
 theorems TARSKI, XREAL_0, XXREAL_0, SETFAM_1, XXREAL_2;
 schemes MEMBERED;

begin

definition
  mode R_eal is Element of ExtREAL;
end;

definition
  redefine func +infty -> R_eal;
  coherence by XXREAL_0:def 1;
  redefine func -infty -> R_eal;
  coherence by XXREAL_0:def 1;
end;

::
::       Bounded above, bounded below and bounded sets of r_eal numbers
::

definition
  canceled 10;
  let X be ext-real-membered set;
  redefine attr X is bounded_above means
  ex UB being UpperBound of X st UB in
  REAL;
  compatibility
  proof
    thus X is bounded_above implies ex UB being UpperBound of X st UB in REAL
    proof
      given r being real number such that
A1:   r is UpperBound of X;
      r in REAL by XREAL_0:def 1;
      hence thesis by A1;
    end;
    given UB being UpperBound of X such that
A2: UB in REAL;
    reconsider UB as real number by A2;
    take UB;
    thus thesis;
  end;
end;

definition
  let X be ext-real-membered set;
  redefine attr X is bounded_below means
  ex LB being LowerBound of X st LB in
  REAL;
  compatibility
  proof
    thus X is bounded_below implies ex UB being LowerBound of X st UB in REAL
    proof
      given r being real number such that
A1:   r is LowerBound of X;
      r in REAL by XREAL_0:def 1;
      hence thesis by A1;
    end;
    given UB being LowerBound of X such that
A2: UB in REAL;
    reconsider UB as real number by A2;
    take UB;
    thus thesis;
  end;
end;

::
::       Set of UpperBound and set of LowerBound of X being a subset of ExtREAL
::

definition
  canceled;
  let X be ext-real-membered set;
  func SetMajorant(X) -> ext-real-membered set means
  :Def14:
  for x being
  ext-real number holds x in it iff x is UpperBound of X;
  existence
  proof
    defpred P[ext-real number] means $1 is UpperBound of X;
    consider Y being ext-real-membered set such that
A1: for x being ext-real number holds x in Y iff P[x] from MEMBERED:
    sch 2;
    take Y;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let Y1,Y2 be ext-real-membered set such that
A2: for x being ext-real number holds x in Y1 iff x is UpperBound of X and
A3: for x being ext-real number holds x in Y2 iff x is UpperBound of X;
    let x be ext-real number;
    x in Y1 iff x is UpperBound of X by A2;
    hence thesis by A3;
  end;
end;

registration
  let X be ext-real-membered set;
  cluster SetMajorant(X) -> non empty;
  coherence
  proof
    consider x being UpperBound of X;
    x in SetMajorant(X) by Def14;
    hence thesis;
  end;
end;

canceled 52;

theorem
  for X,Y being ext-real-membered set st X c= Y holds for x being
  ext-real number holds x in SetMajorant Y implies x in SetMajorant(X)
proof
  let X,Y be ext-real-membered set;
  assume
A1: X c= Y;
  let x be ext-real number;
  assume
  x in SetMajorant(Y);
  then x is UpperBound of Y by Def14;
  then x is UpperBound of X by A1,XXREAL_2:6;
  hence thesis by Def14;
end;

definition
  let X be ext-real-membered set;
  func SetMinorant(X) -> ext-real-membered set means
  :Def15:
  for x being
  ext-real number holds x in it iff x is LowerBound of X;
  existence
  proof
    defpred P[ext-real number] means $1 is LowerBound of X;
    consider Y being ext-real-membered set such that
A1: for x being ext-real number holds x in Y iff P[x] from MEMBERED:
    sch 2;
    take Y;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let Y1,Y2 be ext-real-membered set such that
A2: for x being ext-real number holds x in Y1 iff x is LowerBound of X and
A3: for x being ext-real number holds x in Y2 iff x is LowerBound of X;
    let x be ext-real number;
    x in Y1 iff x is LowerBound of X by A2;
    hence thesis by A3;
  end;
end;

registration
  let X be ext-real-membered set;
  cluster SetMinorant(X) -> non empty;
  coherence
  proof
    consider x being LowerBound of X;
    x in SetMinorant(X) by Def15;
    hence thesis;
  end;
end;

canceled 3;

theorem
  for X,Y being ext-real-membered set st X c= Y holds for x being
  ext-real number holds x in SetMinorant(Y) implies x in SetMinorant(X)
proof
  let X,Y be ext-real-membered set;
  assume
A1: X c= Y;
  let x be ext-real number;
  assume
  x in SetMinorant(Y);
  then x is LowerBound of Y by Def15;
  then x is LowerBound of X by A1,XXREAL_2:5;
  hence thesis by Def15;
end;

::
::       sup X, inf X least upper bound and greatest lower bound of set X
::

canceled 24;

theorem
  for X being non empty ext-real-membered set holds sup X = inf
  SetMajorant(X) & inf X = sup SetMinorant(X)
proof
  let X be non empty ext-real-membered set;
  for y being ext-real number st y in SetMajorant X holds sup X <= y
  proof
    let y be ext-real number;
    assume
    y in SetMajorant X;
    then y is UpperBound of X by Def14;
    hence thesis by XXREAL_2:def 3;
  end;
  then
A1: sup X is LowerBound of SetMajorant X by XXREAL_2:def 2;
  inf X is LowerBound of X by XXREAL_2:def 4;
  then
A2: inf X in SetMinorant X by Def15;
  for y being ext-real number st y in SetMinorant X holds y <= inf X
  proof
    let y be ext-real number;
    assume
    y in SetMinorant X;
    then y is LowerBound of X by Def15;
    hence thesis by XXREAL_2:def 4;
  end;
  then
A3: inf X is UpperBound of SetMinorant X by XXREAL_2:def 1;
  sup X is UpperBound of X by XXREAL_2:def 3;
  then sup X in SetMajorant X by Def14;
  hence thesis by A1,A2,A3,XXREAL_2:55,56;
end;

canceled 26;

registration
  let X be non empty set;
  cluster non empty with_non-empty_elements Subset-Family of X;
  existence
  proof
    take {[#]X};
    thus {[#]X} is non empty;
    assume
    {} in {[#]X};
    hence contradiction by TARSKI:def 1;
  end;
end;

definition
  let X be non empty set;
  mode bool_DOMAIN of X is non empty with_non-empty_elements Subset-Family of
    X;
end;

definition
  let F be bool_DOMAIN of ExtREAL;
  canceled 3;
  func SUP(F) -> ext-real-membered set means
  :Def19:
  for a being ext-real
number holds a in it iff ex A being non empty ext-real-membered set st A in F &
  a = sup A;
  existence
  proof
    defpred P[ext-real number] means ex A being non empty ext-real-membered
    set st A in F & $1 = sup A;
    consider S being ext-real-membered set such that
A1: for a being ext-real number holds a in S iff P[a] from MEMBERED:
    sch 2;
    reconsider S as ext-real-membered set;
    take S;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let S1,S2 be ext-real-membered set such that
A2: for a being ext-real number holds a in S1 iff ex A being non empty
    ext-real-membered set st A in F & a = sup A and
A3: for a being ext-real number holds a in S2 iff ex A being non empty
    ext-real-membered set st A in F & a = sup A;
    let a be ext-real number;
    hereby
      assume
      a in S1;
      then
      ex A being non empty ext-real-membered set st A in F & a = sup A by
      A2;
      hence a in S2 by A3;
    end;
    assume
    a in S2;
    then
    ex A being non empty ext-real-membered set st A in F & a = sup A by A3;
    hence thesis by A2;
  end;
end;

registration
  let F be bool_DOMAIN of ExtREAL;
  cluster SUP(F) -> non empty;
  coherence
  proof
    consider A being Element of F;
    reconsider A as non empty ext-real-membered set by SETFAM_1:def 9;
    sup A = sup A;
    hence thesis by Def19;
  end;
end;

canceled 3;

theorem Th112:
  for F being bool_DOMAIN of ExtREAL, S being non empty
  ext-real-membered number st S = union F holds sup S is UpperBound of SUP(F)
proof
  let F be bool_DOMAIN of ExtREAL, S be non empty ext-real-membered set;
  assume
A1: S = union F;
  for x being ext-real number st x in SUP(F) holds x <= sup S
  proof
    let x be ext-real number;
    assume
    x in SUP(F);
    then consider A being non empty ext-real-membered set such that
A2: A in F and
A3: x = sup A by Def19;
    A c= S
    proof
      let a be ext-real number;
      assume
      a in A;
      hence thesis by A1,A2,TARSKI:def 4;
    end;
    hence thesis by A3,XXREAL_2:59;
  end;
  hence thesis by XXREAL_2:def 1;
end;

theorem Th113:
  for F being bool_DOMAIN of ExtREAL, S being ext-real-membered
  set st S = union F holds sup SUP(F) is UpperBound of S
proof
  let F be bool_DOMAIN of ExtREAL, S be ext-real-membered set;
  assume
A1: S = union F;
  for x being ext-real number st x in S holds x <= sup SUP(F)
  proof
    let x be ext-real number;
    assume
    x in S;
    then consider Z being set such that
A2: x in Z and
A3: Z in F by A1,TARSKI:def 4;
    reconsider Z as non empty ext-real-membered set by A2,A3;
    consider a being set such that
A4: a = sup Z;
    reconsider a as ext-real number by A4;
    sup Z is UpperBound of Z & a in SUP(F) by A3,A4,Def19,XXREAL_2:def 3;
    hence thesis by A2,A4,XXREAL_2:61,def 1;
  end;
  hence thesis by XXREAL_2:def 1;
end;

theorem
  for F being bool_DOMAIN of ExtREAL, S being non empty
  ext-real-membered set st S = union F holds sup S = sup SUP(F)
proof
  let F be bool_DOMAIN of ExtREAL, S be non empty ext-real-membered set;
  set a = sup S;
  set b = sup SUP(F);
  assume
A1: S = union F;
  then sup S is UpperBound of SUP(F) by Th112;
  then
A2: b <= a by XXREAL_2:def 3;
  sup SUP(F) is UpperBound of S by A1,Th113;
  then a <= b by XXREAL_2:def 3;
  hence thesis by A2,XXREAL_0:1;
end;

definition
  let F be bool_DOMAIN of ExtREAL;
  func INF F -> ext-real-membered set means
  :Def20:
  for a being ext-real
number holds a in it iff ex A being non empty ext-real-membered set st A in F &
  a = inf A;
  existence
  proof
    consider A being Element of F;
    defpred P[ext-real number] means ex A being non empty ext-real-membered
    set st A in F & $1 = inf A;
    reconsider A as non empty ext-real-membered set by SETFAM_1:def 9;
    consider S being ext-real-membered set such that
A1: for a being ext-real number holds a in S iff P[a] from MEMBERED:
    sch 2;
    inf A = inf A;
    then reconsider S as non empty ext-real-membered set by A1;
    take S;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let S1,S2 be ext-real-membered set such that
A2: for a being ext-real number holds a in S1 iff ex A being non empty
    ext-real-membered set st A in F & a = inf A and
A3: for a being ext-real number holds a in S2 iff ex A being non empty
    ext-real-membered set st A in F & a = inf A;
    for a being set holds a in S1 iff a in S2
    proof
      let a be set;
      hereby
        assume
A4:     a in S1;
        then reconsider a9 = a as ext-real number;
        ex A being non empty ext-real-membered set st A in F & a9 = inf A
        by A2,A4;
        hence a in S2 by A3;
      end;
      assume
A5:   a in S2;
      then reconsider a as ext-real number;
      ex A being non empty ext-real-membered set st A in F & a = inf A by
      A3,A5;
      hence thesis by A2;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let F be bool_DOMAIN of ExtREAL;
  cluster INF(F) -> non empty;
  coherence
  proof
    consider A being Element of F;
    reconsider A as non empty ext-real-membered set by SETFAM_1:def 9;
    inf A = inf A;
    hence thesis by Def20;
  end;
end;

canceled 2;

theorem Th117:
  for F being bool_DOMAIN of ExtREAL, S being non empty
  ext-real-membered set st S = union F holds inf S is LowerBound of INF(F)
proof
  let F be bool_DOMAIN of ExtREAL, S be non empty ext-real-membered set;
  assume
A1: S = union F;
  for x being ext-real number st x in INF(F) holds inf S <= x
  proof
    let x be ext-real number;
    assume
    x in INF(F);
    then consider A being non empty ext-real-membered set such that
A2: A in F and
A3: x = inf A by Def20;
    A c= S
    proof
      let a be ext-real number;
      assume
      a in A;
      hence thesis by A1,A2,TARSKI:def 4;
    end;
    hence thesis by A3,XXREAL_2:60;
  end;
  hence thesis by XXREAL_2:def 2;
end;

theorem Th118:
  for F being bool_DOMAIN of ExtREAL, S being ext-real-membered
  set st S = union F holds inf INF(F) is LowerBound of S
proof
  let F be bool_DOMAIN of ExtREAL, S be ext-real-membered set;
  assume
A1: S = union F;
  for x being ext-real number st x in S holds inf INF(F) <= x
  proof
    let x be ext-real number;
    assume
    x in S;
    then consider Z being set such that
A2: x in Z and
A3: Z in F by A1,TARSKI:def 4;
    reconsider Z as non empty ext-real-membered set by A2,A3;
    consider a being set such that
A4: a = inf Z;
    reconsider a as ext-real number by A4;
    inf Z is LowerBound of Z & a in INF(F) by A3,A4,Def20,XXREAL_2:def 4;
    hence thesis by A2,A4,XXREAL_2:62,def 2;
  end;
  hence thesis by XXREAL_2:def 2;
end;

theorem
  for F being bool_DOMAIN of ExtREAL, S being non empty
  ext-real-membered set st S = union F holds inf S = inf INF(F)
proof
  let F be bool_DOMAIN of ExtREAL, S be non empty ext-real-membered set;
  set a = inf S;
  set b = inf INF(F);
  assume
A1: S = union F;
  then inf S is LowerBound of INF(F) by Th117;
  then
A2: a <= b by XXREAL_2:def 4;
  inf INF(F) is LowerBound of S by A1,Th118;
  then b <= a by XXREAL_2:def 4;
  hence thesis by A2,XXREAL_0:1;
end;

