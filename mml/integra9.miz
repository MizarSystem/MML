:: Several Integrability Formulas of Some Functions, Orthogonal Polynomials
:: and Norm Functions
::  by Bo Li , Yanping Zhuang , Bing Xie and Pan Wang
::
:: Received October 14, 2008
:: Copyright (c) 2008 Association of Mizar Users

environ

 vocabularies INTEGRA9, ARYTM, PRE_TOPC, RELAT_1, FUNCT_1, ARYTM_1, JGRAPH_2,
      PARTFUN1, SEQ_1, SIN_COS, INTEGRA1, FDIFF_1, REALSET1, BOOLE, SQUARE_1,
      ARYTM_3, ORDINAL2, RCOMP_1, PREPOWER, LATTICES, FINSEQ_1, TAYLOR_1,
      SIN_COS4, SUBSET_1, GROUP_1, SEQ_4;
 notations FUNCT_1,
      SEQ_1, SIN_COS, TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, VALUED_0,
      VALUED_1, MEMBERED, XREAL_0, SEQ_4,
      REAL_1, RELSET_1, PARTFUN1, RFUNCT_1, RCOMP_1, PSCOMP_1, INTEGRA1,
      XXREAL_0, XXREAL_2,
      FCONT_1, SINCOS10, FDIFF_9, SQUARE_1, INTEGRA5, PARTFUN2,
      NEWTON, PREPOWER, TAYLOR_1, FDIFF_1, JORDAN16, SEQ_2;
 constructors SIN_COS, TAYLOR_1, SEQ_1, REAL_1, FDIFF_1, FCONT_1, PSCOMP_1,
      NAT_1, BINOP_2, SQUARE_1, PREPOWER, INTEGRA5, PARTFUN2, LIMFUNC1,
      JORDAN16, SEQ_4, RFUNCT_1, FDIFF_9, SINCOS10, POWER, RELSET_1;
 registrations VALUED_1, NAT_1, XBOOLE_0, NUMBERS, MEMBERED, VALUED_0,
      INTEGRA1, RCOMP_1, INT_1, RELAT_1, ORDINAL1, FUNCT_2, FDIFF_1, COMPLEX1,
      RFUNCT_1, XREAL_0, RELSET_1, FINSEQ_2, ORDINAL2, NEWTON, SEQM_3,
      XXREAL_0, FINSEQ_1, SIN_COS, SQUARE_1, XREAL_1, FUNCOP_1, PARTFUN1,
      TAYLOR_1, JORDAN16, FCONT_3, XXREAL_2;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions SIN_COS, INTEGRA5, VALUED_1, SUBSET_1, TARSKI, FDIFF_9, SQUARE_1;
 theorems PARTFUN1, INTEGRA1, RFUNCT_1, FUNCT_1, FDIFF_1, INTEGRA2, FUNCT_2,
      XBOOLE_0, XBOOLE_1, SQUARE_1, XCMPLX_1, INTEGRA5, INTEGRA8, INTEGRA6,
      SIN_COS, XREAL_1, FDIFF_4, TAYLOR_1, FDIFF_6, VALUED_1, FCONT_1,
      JORDAN16, RELAT_1, FDIFF_2, FDIFF_5, FDIFF_8, FDIFF_9,
      PREPOWER;

begin

 reserve r,p,x for Real;
 reserve C for non empty set;
 reserve n for Element of NAT;
 reserve A for closed-interval Subset of REAL;
 reserve f,g,h,f1,f2 for PartFunc of REAL,REAL;
 reserve Z for open Subset of REAL;

Lm1: dom (-(exp_R*(AffineMap(-1,0)))) = [#]REAL by FUNCT_2:def 1;

:: f.x = -exp_R.(-x)
theorem
  -(exp_R*(AffineMap(-1,0))) is_differentiable_on REAL
  & for x holds ((-(exp_R*(AffineMap(-1,0))))`|REAL).x = exp_R(-x)
proof
A1: [#]REAL = dom (exp_R*(AffineMap(-1,0))) by FUNCT_2:def 1;
A2: [#]REAL = dom (AffineMap(-1,0)) by FUNCT_2:def 1;
A3: for x st x in REAL holds
   AffineMap(-1,0).x=(-1)*x + 0 by JORDAN16:def 3; then
A4: (AffineMap(-1,0)) is_differentiable_on REAL & for x st x in [#]REAL
 holds ((AffineMap(-1,0))`|REAL).x = -1 by A2,FDIFF_1:31;
   for x st x in REAL holds
  exp_R*(AffineMap(-1,0)) is_differentiable_in x
    proof
      let x;
   assume x in REAL;
    (AffineMap(-1,0)) is_differentiable_in x by A4,FDIFF_1:16;
    hence thesis by TAYLOR_1:19;
    end; then
A5: exp_R*(AffineMap(-1,0)) is_differentiable_on REAL
         by A1,FDIFF_1:16;
   for x st x in REAL holds
    ((-(exp_R*(AffineMap(-1,0))))`|REAL).x = exp_R(-x)
    proof
      let x;
     assume x in REAL;
A6:  (AffineMap(-1,0)) is_differentiable_in x by A4,FDIFF_1:16;
       ((-(exp_R*(AffineMap(-1,0))))`|REAL).x
     = (-1)*diff(exp_R*(AffineMap(-1,0)),x) by Lm1,A5,FDIFF_1:28
    .= (-1)*(exp_R.((AffineMap(-1,0)).x)
           *diff((AffineMap(-1,0)),x)) by A6,TAYLOR_1:19
    .= (-1)*(exp_R.((AffineMap(-1,0)).x)
           *((AffineMap(-1,0))`|REAL).x) by A4,FDIFF_1:def 8
    .= (-1)*(exp_R.((AffineMap(-1,0)).x)*(-1))
            by A2,A3,FDIFF_1:31
    .= (-1)*(exp_R.((-1)*x + 0)*(-1)) by JORDAN16:def 3
    .= exp_R(-x);
      hence thesis;
    end;
    hence thesis by A5,Lm1,FDIFF_1:28;
end;

canceled 3;

:: f.x = (1/r)*exp_R.(r*x)
theorem Th5:
  r<>0 implies
  (1/r)(#)(exp_R*(AffineMap(r,0))) is_differentiable_on REAL &
  for x holds
  (((1/r)(#)(exp_R*(AffineMap(r,0))))`|REAL).x=(exp_R*(AffineMap(r,0))).x
proof
 assume B: r<>0;
A:  [#]REAL= dom ((1/r)(#)(exp_R*(AffineMap(r,0)))) by FUNCT_2:def 1;
A1: [#]REAL = dom (exp_R*(AffineMap(r,0))) by FUNCT_2:def 1;
A2: [#]REAL = dom (AffineMap(r,0)) by FUNCT_2:def 1;
A3: for x st x in REAL holds
      AffineMap(r,0).x=r*x + 0 by JORDAN16:def 3;then
A4: (AffineMap(r,0)) is_differentiable_on REAL
    & for x st x in [#]REAL holds
    ((AffineMap(r,0))`|REAL).x = r by A2,FDIFF_1:31;
    for x st x in REAL holds exp_R*(AffineMap(r,0)) is_differentiable_in x
    proof
      let x;
      assume x in REAL;
      (AffineMap(r,0)) is_differentiable_in x
         by A4,FDIFF_1:16;
      hence thesis by TAYLOR_1:19;
    end; then
A5: exp_R*(AffineMap(r,0)) is_differentiable_on REAL
         by A1,FDIFF_1:16;
    for x st x in REAL holds
    (((1/r)(#)(exp_R*(AffineMap(r,0))))`|REAL).x = (exp_R*(AffineMap(r,0))).x
    proof
      let x;
      assume x in REAL;
A6:   (AffineMap(r,0)) is_differentiable_in x by A4,FDIFF_1:16;
       (((1/r)(#)(exp_R*(AffineMap(r,0))))`|REAL).x
     = (1/r)*diff(exp_R*(AffineMap(r,0)),x) by A,A5,FDIFF_1:28
    .= (1/r)*(exp_R.((AffineMap(r,0)).x)
        *diff((AffineMap(r,0)),x)) by A6,TAYLOR_1:19
    .= (1/r)*(exp_R.((AffineMap(r,0)).x)
        *(AffineMap(r,0)`|REAL).x) by A4,FDIFF_1:def 8
    .= (1/r)*(exp_R.((AffineMap(r,0)).x)*r) by A2,A3,FDIFF_1:31
    .= r*(1/r)*(exp_R.((AffineMap(r,0)).x))
    .= r/r*(exp_R.((AffineMap(r,0)).x)) by XCMPLX_1:100
    .= 1*(exp_R.((AffineMap(r,0)).x)) by B,XCMPLX_1:60
    .= (exp_R*(AffineMap(r,0))).x by A1,FUNCT_1:22;
      hence thesis;
    end;
    hence thesis by A5,A,FDIFF_1:28;
end;

::  f.x = exp_R.(r*x)
theorem
  r<>0 implies integral(exp_R*(AffineMap(r,0)),A)
   = ((1/r)(#)(exp_R*(AffineMap(r,0)))).(upper_bound A)
    -((1/r)(#)(exp_R*(AffineMap(r,0)))).(lower_bound A)
proof
   assume B:r<>0;
A1: dom (exp_R*(AffineMap(r,0))) = REAL by FUNCT_2:def 1;
A2: [#]REAL = dom (AffineMap(r,0)) by FUNCT_2:def 1;
A3: for x st x in REAL holds
    AffineMap(r,0).x=r*x + 0 by JORDAN16:def 3;
    AffineMap(r,0)|REAL is continuous
      by A2,A3,FDIFF_1:31,33;then
A4: AffineMap(r,0)|A is continuous by FCONT_1:17;
B1: exp_R|REAL is continuous by FDIFF_1:33,SIN_COS:71;
    exp_R|((AffineMap(r,0)).:A) is continuous by B1,FCONT_1:17;then
    (exp_R*AffineMap(r,0))|A is continuous by A4,FCONT_1:26; then
A5: (exp_R*AffineMap(r,0)) is_integrable_on A
      & (exp_R*AffineMap(r,0))|A is bounded by A1,INTEGRA5:10,11;
A6:for x st x in dom (((1/r)(#)(exp_R*(AffineMap(r,0))))`|REAL) holds
     (((1/r)(#)(exp_R*(AffineMap(r,0))))`|REAL).x
   = (exp_R*(AffineMap(r,0))).x by B,Th5;
    (1/r)(#)(exp_R*(AffineMap(r,0)))
    is_differentiable_on REAL by B,Th5;then
     dom (((1/r)(#)(exp_R*(AffineMap(r,0))))`|REAL)
  = dom (exp_R*(AffineMap(r,0))) by A1,FDIFF_1:def 8;then
     (((1/r)(#)(exp_R*(AffineMap(r,0))))`|REAL)
  = exp_R*(AffineMap(r,0)) by A6,PARTFUN1:34;
   hence thesis by B,A5,Th5,INTEGRA5:13;
end;

canceled;

::  f.x=(-1/n)*cos(n*x)
theorem Th8:
  n<>0 implies (-1/n)(#)(cos*(AffineMap(n,0))) is_differentiable_on REAL &
  for x holds (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL).x =sin(n*x)
proof
  assume A1:n<>0;
A: [#]REAL = dom ((-1/n)(#)(cos*(AffineMap(n,0)))) by FUNCT_2:def 1;
A2: [#]REAL = dom (cos*(AffineMap(n,0))) by FUNCT_2:def 1;
A3: for x st x in REAL holds
    AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3;
then A4:cos*(AffineMap(n,0)) is_differentiable_on REAL &
   for x st x in REAL holds ((cos*(AffineMap(n,0)))`|REAL).x
  = -n* sin.(n*x+0) by A2,FDIFF_4:38;
   for x st x in REAL holds
   (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL).x =sin(n*x)
    proof
      let x;
      assume x in REAL;
      (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL).x
   = (-1/n)*diff((cos*(AffineMap(n,0))),x) by A,A4,FDIFF_1:28
  .= (-1/n)*((cos*(AffineMap(n,0)))`|REAL).x by A4,FDIFF_1:def 8
  .= (-1/n)*(-n * sin.(n*x+0)) by A2,A3,FDIFF_4:38
  .= (1/n)*n * (sin.(n*x+0))
  .= n/n*(sin.(n*x+0)) by XCMPLX_1:100
  .= 1*(sin.(n*x+0)) by A1,XCMPLX_1:60
  .= sin(n*x);
      hence thesis;
    end;
    hence thesis by A,A4,FDIFF_1:28;
  end;

::  f.x=sin(n*x)
theorem
  n<>0 implies integral(sin*(AffineMap(n,0)),A)
 = ((-1/n)(#)(cos*(AffineMap(n,0)))).(upper_bound A)
  -((-1/n)(#)(cos*(AffineMap(n,0)))).(lower_bound A)
proof
  assume B: n<>0;
A1: [#]REAL = dom (sin*(AffineMap(n,0))) by FUNCT_2:def 1;
A2: [#]REAL = dom (AffineMap(n,0)) by FUNCT_2:def 1;
  for x st x in REAL holds
    AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3; then
  AffineMap(n,0)|REAL is continuous by A2,FDIFF_1:31,33;then
A3: AffineMap(n,0)|A is continuous by FCONT_1:17;
B1: sin|REAL is continuous by FDIFF_1:33,SIN_COS:73;
 sin|((AffineMap(n,0)).:A) is continuous by B1,FCONT_1:17;then
 (sin*AffineMap(n,0))|A is continuous by A3,FCONT_1:26; then
A4:(sin*AffineMap(n,0)) is_integrable_on A
 & (sin*AffineMap(n,0))|A is bounded by A1,INTEGRA5:10,11;
A5: for x st x in REAL holds AffineMap(n,0).x=n*x
  proof
    let x;
   assume x in REAL;
    (AffineMap(n,0)).x = n*x + 0 by JORDAN16:def 3
      .=n*x;
    hence thesis;
  end;
A6: (-1/n)(#)(cos*(AffineMap(n,0)))
  is_differentiable_on REAL by B,Th8;
A7:for x st x in dom (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL) holds
   (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL).x = (sin*(AffineMap(n,0))).x
   proof
     let x;
     assume x in dom (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL);
      (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL).x
    = sin(n*x) by B,Th8
   .= sin.((AffineMap(n,0)).x) by A5
   .= (sin*(AffineMap(n,0))).x by A1,FUNCT_1:22;
     hence thesis;
   end;
   dom (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL)
  = dom (sin*(AffineMap(n,0))) by A1,A6,FDIFF_1:def 8;then
     (((-1/n)(#)(cos*(AffineMap(n,0))))`|REAL)
    = sin*(AffineMap(n,0)) by A7,PARTFUN1:34;
   hence thesis by B,A4,Th8,INTEGRA5:13;
end;

canceled 2;

::  f.x=(1/n)*sin(n*x)
theorem Th12:
  n<>0 implies (1/n)(#)(sin*(AffineMap(n,0)))
  is_differentiable_on REAL & for x holds
  (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL).x = cos(n*x)
proof
  assume A1:n<>0;
A: [#]REAL = dom ((1/n)(#)(sin*(AffineMap(n,0)))) by FUNCT_2:def 1;
A2: [#]REAL = dom (sin*(AffineMap(n,0))) by FUNCT_2:def 1;
A3: for x st x in REAL holds
    AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3;
  then A4:sin*(AffineMap(n,0)) is_differentiable_on REAL &
    for x st x in REAL holds ((sin*(AffineMap(n,0)))`|REAL).x
   = n* cos.(n*x+0) by A2,FDIFF_4:37;
  for x st x in REAL holds
 (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL).x =cos(n*x)
    proof
      let x;
      assume x in REAL;
       (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL).x
     =(1/n)*diff((sin*(AffineMap(n,0))),x) by A,A4,FDIFF_1:28
    .= (1/n)*((sin*(AffineMap(n,0)))`|REAL).x by A4,FDIFF_1:def 8
    .= (1/n)*(n * cos.(n*x+0)) by A2,A3,FDIFF_4:37
    .= n*(1/n)*(cos.(n*x+0))
    .= n/n*(cos.(n*x+0)) by XCMPLX_1:100
    .= 1*(cos.(n*x+0)) by A1,XCMPLX_1:60
    .= cos(n*x);
      hence thesis;
    end;
    hence thesis by A,A4,FDIFF_1:28;
  end;

::  f.x=cos(n*x)
theorem
  n<>0 implies integral(cos*(AffineMap(n,0)),A)
  = ((1/n)(#)(sin*(AffineMap(n,0)))).(upper_bound A)
    -((1/n)(#)(sin*(AffineMap(n,0)))).(lower_bound A)
proof
  assume B:n<>0;
A1: [#]REAL = dom (cos*(AffineMap(n,0))) by FUNCT_2:def 1;
A2: [#]REAL = dom (AffineMap(n,0)) by FUNCT_2:def 1;
  for x st x in REAL holds AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3; then
  AffineMap(n,0)|REAL is continuous by A2,FDIFF_1:31,33;then
A3: AffineMap(n,0)|A is continuous by FCONT_1:17;
B1: cos|REAL is continuous by FDIFF_1:33,SIN_COS:72;
 cos|((AffineMap(n,0)).:A) is continuous by B1,FCONT_1:17;then
 (cos*AffineMap(n,0))|A is continuous by A3,FCONT_1:26;
   then
A4:(cos*AffineMap(n,0)) is_integrable_on A
 & (cos*AffineMap(n,0))|A is bounded by A1,INTEGRA5:10,11;
A5: for x st x in REAL holds AffineMap(n,0).x=n*x
  proof
    let x;
   assume x in REAL;
    (AffineMap(n,0)).x = n*x + 0 by JORDAN16:def 3
      .=n*x;
    hence thesis;
  end;
A6: (1/n)(#)(sin*(AffineMap(n,0)))
  is_differentiable_on REAL by B,Th12;
A7:for x st x in dom (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL) holds
   (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL).x = (cos*(AffineMap(n,0))).x
   proof
     let x;
     assume x in dom (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL);
      (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL).x
    = cos(n*x) by B,Th12
   .= cos.((AffineMap(n,0)).x) by A5
   .= (cos*(AffineMap(n,0))).x by A1,FUNCT_1:22;
     hence thesis;
   end;
   dom (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL)
  = dom (cos*(AffineMap(n,0))) by A1,A6,FDIFF_1:def 8;then
     (((1/n)(#)(sin*(AffineMap(n,0))))`|REAL)
    = cos*(AffineMap(n,0)) by A7,PARTFUN1:34;
   hence thesis by B,A4,Th12,INTEGRA5:13;
end;

::  f.x=x*sin.x
theorem
  A c= Z implies integral((id Z)(#)sin,A)
    = ((-id Z)(#)cos+sin).(upper_bound A)-((-id Z)(#)cos+sin).(lower_bound A)
proof
 assume A1: A c= Z;
D1: dom ((-id Z)(#)cos+sin) = Z
  proof
  A2: dom (-id Z) = dom id Z by VALUED_1:8;
    dom ((-id Z)(#)cos+sin)
   = dom ((-id Z)(#)cos) /\ REAL by SIN_COS:27,VALUED_1:def 1
  .= dom ((-id Z)(#)cos) by XBOOLE_1:28
  .= dom (-id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
  .= dom (-id Z) by XBOOLE_1:28
  .= Z by A2,RELAT_1:71;
    hence thesis;
  end;
D2: Z = dom ((id Z)(#)sin)
  proof
     dom ((id Z)(#)sin)
   = dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
  .= dom (id Z) by XBOOLE_1:28
  .= Z by RELAT_1:71;
  hence thesis;
  end;
 ((id Z)(#)sin)|Z is continuous by D2,FDIFF_1:33,FDIFF_4:45;then
 ((id Z)(#)sin)|A is continuous by A1,FCONT_1:17; then
A3: (id Z)(#)sin is_integrable_on A
    & ((id Z)(#)sin)|A is bounded by A1,D2,INTEGRA5:10,11;
A4: (-id Z)(#)cos+sin is_differentiable_on Z by D1,FDIFF_4:46;
B: for x st x in Z holds ((id Z)(#)sin).x = x*sin.x
  proof
    let x;
    assume C:x in Z;
    ((id Z)(#)sin).x = ((id Z).x)*(sin.x) by VALUED_1:5
       .= x*sin.x by C,FUNCT_1:35;
    hence thesis;
  end;
A5: for x st x in dom (((-id Z)(#)cos+sin)`|Z) holds
   (((-id Z)(#)cos+sin)`|Z).x = ((id Z)(#)sin).x
   proof
     let x;
     assume x in dom (((-id Z)(#)cos+sin)`|Z); then
     A6: x in Z by A4,FDIFF_1:def 8; then
     (((-id Z)(#)cos+sin)`|Z).x =x*sin.x by D1,FDIFF_4:46
      .= ((id Z)(#)sin).x by B,A6;
     hence thesis;
   end;
     dom (((-id Z)(#)cos+sin)`|Z)
  = dom ((id Z)(#)sin) by D2,A4,FDIFF_1:def 8;then
   (((-id Z)(#)cos+sin)`|Z) = (id Z)(#)sin by A5,PARTFUN1:34;
   hence thesis by A1,D1,A3,FDIFF_4:46,INTEGRA5:13;
end;

::  f.x=x*cos.x
theorem
  A c= Z implies integral((id Z)(#)cos,A)
  = ((id Z)(#)sin+cos).(upper_bound A)-((id Z)(#)sin+cos).(lower_bound A)
proof
  assume A1: A c= Z;
D1: dom ((id Z)(#)sin+cos) = Z
  proof
     dom ((id Z)(#)sin+cos)
   = dom ((id Z)(#)sin) /\ REAL by SIN_COS:27,VALUED_1:def 1
  .= dom ((id Z)(#)sin) by XBOOLE_1:28
  .= dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
  .= dom (id Z) by XBOOLE_1:28;
    hence thesis by RELAT_1:71;
  end;
D2: Z = dom ((id Z)(#)cos)
  proof
     dom ((id Z)(#)cos)
   = dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
  .= dom (id Z) by XBOOLE_1:28
  .= Z by RELAT_1:71;
  hence thesis;
  end; then
  Z = dom (id Z) /\ dom cos by VALUED_1:def 4; then
AA: Z c= dom (id Z) & Z c= dom cos by XBOOLE_1:18;
A3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
B: for x st x in Z holds ((id Z)(#)cos).x = x*cos.x
  proof
    let x;
    assume C:x in Z;
    ((id Z)(#)cos).x = ((id Z).x)*(cos.x) by VALUED_1:5
      .= x*cos.x by C,FUNCT_1:35;
    hence thesis;
  end;
C:  id Z is_differentiable_on Z by AA,A3,FDIFF_1:31;
 cos is_differentiable_on Z by FDIFF_1:34,SIN_COS:72; then
 ((id Z)(#)cos)|Z is continuous by D2,C,FDIFF_1:29,33; then
  ((id Z)(#)cos)|A is continuous by A1,FCONT_1:17; then
A4: (id Z)(#)cos is_integrable_on A
    & ((id Z)(#)cos)|A is bounded by A1,D2,INTEGRA5:10,11;
A5: (id Z)(#)sin+cos is_differentiable_on Z by D1,FDIFF_4:47;
A6: for x st x in dom (((id Z)(#)sin+cos)`|Z) holds
   (((id Z)(#)sin+cos)`|Z).x = ((id Z)(#)cos).x
   proof
     let x;
     assume x in dom (((id Z)(#)sin+cos)`|Z); then
     A7: x in Z by A5,FDIFF_1:def 8; then
     (((id Z)(#)sin+cos)`|Z).x =x*cos.x by D1,FDIFF_4:47
      .= ((id Z)(#)cos).x by B,A7;
     hence thesis;
   end;
     dom (((id Z)(#)sin+cos)`|Z)
  = dom ((id Z)(#)cos) by D2,A5,FDIFF_1:def 8;then
   (((id Z)(#)sin+cos)`|Z) = (id Z)(#)cos by A6,PARTFUN1:34;
   hence thesis by A1,D1,A4,FDIFF_4:47,INTEGRA5:13;
end;

:: f.x=x*cos.x
theorem Th16:
  (id Z)(#)cos is_differentiable_on Z &
  for x st x in Z holds (((id Z)(#)cos)`|Z).x =cos.x-x*sin.x
proof
A1: Z = dom ((id Z)(#)cos)
 proof
   dom ((id Z)(#)cos) = dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
     .= dom (id Z) by XBOOLE_1:28
     .= Z by RELAT_1:71;
   hence thesis;
  end;
then Z c= dom (id Z) /\ dom cos by VALUED_1:def 4; then
A2: Z c= dom (id Z) & Z c= dom cos by XBOOLE_1:18;
A3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
 for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35; then
A4: id Z is_differentiable_on Z & for x st x in Z
  holds ((id Z)`|Z).x = 1 by A2,FDIFF_1:31;
A5: cos is_differentiable_on Z by FDIFF_1:34,SIN_COS:72;
  now
    let x;
    assume
A6: x in Z;
    hence (((id Z)(#)cos)`|Z).x = (cos.x)*diff((id Z),x)
            + ((id Z).x)*diff(cos,x) by A1,A4,A5,FDIFF_1:29
      .=(cos.x)*((id Z)`|Z).x + ((id Z).x)*diff(cos,x) by A4,A6,FDIFF_1:def 8
      .=(cos.x)*1+ ((id Z).x)*diff(cos,x) by A2,A3,A6,FDIFF_1:31
      .=(cos.x)*1+((id Z).x)*(-sin.x) by SIN_COS:68
      .=cos.x+x*(-sin.x) by A6,FUNCT_1:35
      .=cos.x-x*sin.x;
  end;
  hence thesis by A1,A4,A5,FDIFF_1:29;
end;

Lm5: -sin is_differentiable_in x & diff(-sin,x) = -cos.x
proof
A1: sin is_differentiable_in x by SIN_COS:69;
  then diff(-sin,x) = -diff(sin,x) by INTEGRA8:22
    .= - cos.x by SIN_COS:69;
  hence thesis by A1,INTEGRA8:22;
end;

:: f.x = -sin.x+x*cos.x
theorem Th17:
  -sin+(id Z)(#)cos is_differentiable_on Z
  & for x st x in Z holds ((-sin+(id Z)(#)cos)`|Z).x =-x*sin.x
proof
A1: Z = dom (-sin+(id Z)(#)cos)
 proof
   dom (-sin+(id Z)(#)cos)
 = dom (-sin) /\ dom((id Z)(#)cos) by VALUED_1:def 1
.= REAL /\ dom((id Z)(#)cos) by SIN_COS:27,VALUED_1:8
.= dom((id Z)(#)cos) by XBOOLE_1:28
.= dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
.= dom (id Z) by XBOOLE_1:28;
  hence thesis by RELAT_1:71;
  end;
A2: (id Z)(#)cos is_differentiable_on Z &
     for x st x in Z holds (((id Z)(#)cos)`|Z).x =cos.x - x*sin.x by Th16;
A3: -sin is_differentiable_on Z by FDIFF_1:34,INTEGRA8:24;
  now
    let x;
    assume
A4: x in Z;
      hence ((-sin+(id Z)(#)cos)`|Z).x
       = diff(-sin,x)+diff((id Z)(#)cos,x) by A1,A2,A3,FDIFF_1:26
      .=(((id Z)(#)cos)`|Z).x + diff(-sin,x) by A2,A4,FDIFF_1:def 8
      .=cos.x- x*sin.x+ diff(-sin,x) by A4,Th16
      .=cos.x- x*sin.x +(-cos.x) by Lm5
      .=-x*sin.x;
  end;
  hence thesis by A1,A2,A3,FDIFF_1:26;
end;

::  f.x=-x*sin.x
theorem
  A c= Z implies integral((-id Z)(#)sin,A)
   = (-sin+(id Z)(#)cos).(upper_bound A)-(-sin+(id Z)(#)cos).(lower_bound A)
proof
  assume A1: A c= Z;
A2: Z = dom ((-id Z)(#)sin)
 proof
   dom ((-id Z)(#)sin) = dom (-id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
     .= dom (-id Z) by XBOOLE_1:28
     .= dom (id Z) by VALUED_1:8;
   hence thesis by RELAT_1:71;
  end;
   Z c= dom (-id Z) /\ dom sin by A2,VALUED_1:def 4; then
B1: Z c= dom (-id Z) & Z c= dom sin by XBOOLE_1:18;
B2: for x st x in Z holds (-id Z).x =(-1)*x +0
  proof
    let x;
    assume
B3: x in Z;
    (-id Z).x = -(id Z.x) by VALUED_1:8
      .=-x by B3,FUNCT_1:35
      .=(-1)*x +0;
    hence thesis;
  end; then
B4: -id Z is_differentiable_on Z by B1,FDIFF_1:31;
A3: sin is_differentiable_on Z by FDIFF_1:34,SIN_COS:73;
  ((-id Z)(#)sin)|Z is continuous
  by A2,B4,A3,FDIFF_1:29,33;then
 ((-id Z)(#)sin)|A is continuous by A1,FCONT_1:17; then
A4: (-id Z)(#)sin is_integrable_on A
    & ((-id Z)(#)sin)|A is bounded by A1,A2,INTEGRA5:10,11;
A5: -sin+(id Z)(#)cos is_differentiable_on Z by Th17;
B: for x st x in Z holds ((-id Z)(#)sin).x = -x*sin.x
  proof
   let x;
   assume C:x in Z;
   ((-id Z)(#)sin).x = ((-id Z).x)*(sin.x) by VALUED_1:5
       .= ((-1)*x+0)*(sin.x) by B2,C
       .= -x*sin.x;
   hence thesis;
  end;
A6: for x st x in dom ((-sin+(id Z)(#)cos)`|Z) holds
   ((-sin+(id Z)(#)cos)`|Z).x = ((-id Z)(#)sin).x
   proof
     let x;
     assume x in dom ((-sin+(id Z)(#)cos)`|Z); then
     A7: x in Z by A5,FDIFF_1:def 8; then
     ((-sin+(id Z)(#)cos)`|Z).x =-x*sin.x by Th17
      .= ((-id Z)(#)sin).x by B,A7;
     hence thesis;
   end;
     dom ((-sin+(id Z)(#)cos)`|Z)
  = dom ((-id Z)(#)sin) by A2,A5,FDIFF_1:def 8;then
   ((-sin+(id Z)(#)cos)`|Z) = (-id Z)(#)sin by A6,PARTFUN1:34;
   hence thesis by A1,A4,Th17,INTEGRA5:13;
end;

:: f.x = -cos.x-x*sin.x
theorem Th19:
  -cos-(id Z)(#)sin is_differentiable_on Z
  & for x st x in Z holds ((-cos-(id Z)(#)sin)`|Z).x =-x*cos.x
proof
A1: Z = dom (-cos-(id Z)(#)sin)
 proof
   dom (-cos-(id Z)(#)sin) = dom (-cos) /\ dom ((id Z)(#)sin) by VALUED_1:12
.= REAL /\ dom ((id Z)(#)sin) by SIN_COS:27,VALUED_1:8
.= dom((id Z)(#)sin) by XBOOLE_1:28
.= dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
.= dom (id Z) by XBOOLE_1:28;
  hence thesis by RELAT_1:71;
  end;
  then Z c= dom (-cos) /\ dom ((id Z)(#)sin) by VALUED_1:12; then
A2: Z c= dom ((id Z)(#)sin) & Z c= dom (-cos) by XBOOLE_1:18; then
A3: (id Z)(#)sin is_differentiable_on Z &
     for x st x in Z holds
 (((id Z)(#)sin)`|Z).x =sin.x+x*cos.x by FDIFF_4:45;
A4: -cos is_differentiable_on Z by FDIFF_1:34,INTEGRA8:26;
  now
    let x;
    assume
A5: x in Z;
      hence ((-cos-(id Z)(#)sin)`|Z).x
       = diff(-cos,x)-diff((id Z)(#)sin,x) by A1,A3,A4,FDIFF_1:27
      .= sin.x - diff((id Z)(#)sin,x) by INTEGRA8:26
      .= sin.x - (((id Z)(#)sin)`|Z).x by A3,A5,FDIFF_1:def 8
      .= sin.x - (sin.x+x*cos.x) by A5,A2,FDIFF_4:45
      .= -x*cos.x;
  end;
  hence thesis by A1,A3,A4,FDIFF_1:27;
end;

:: f.x = -x*cos.x
theorem
  A c= Z implies integral((-id Z)(#)cos,A)
  = (-cos-(id Z)(#)sin).(upper_bound A)-(-cos-(id Z)(#)sin).(lower_bound A)
proof
  assume A1: A c= Z;
A2: Z = dom ((-id Z)(#)cos)
 proof
   dom ((-id Z)(#)cos) = dom (-id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
    .= dom (-id Z) by XBOOLE_1:28
    .= dom (id Z) by VALUED_1:8;
   hence thesis by RELAT_1:71;
  end;
  Z = dom (-id Z) /\ dom cos by A2,VALUED_1:def 4; then
AA: Z c= dom (-id Z) & Z c= dom cos by XBOOLE_1:18;
A3: for x st x in Z holds (-id Z).x =(-1)*x +0
  proof
    let x;
    assume
C1: x in Z;
    (-id Z).x = -(id Z.x) by VALUED_1:8
      .=-x by C1,FUNCT_1:35
      .=(-1)*x +0;
    hence thesis;
  end;
B: for x st x in Z holds ((-id Z)(#)cos).x = -x*cos.x
  proof
    let x;
  assume C:x in Z;
   ((-id Z)(#)cos).x = ((-id Z).x)*(cos.x) by VALUED_1:5
       .= ((-1)*x+0)*(cos.x) by A3,C
       .= -x*cos.x;
    hence thesis;
  end;
C:  -id Z is_differentiable_on Z by AA,A3,FDIFF_1:31;
 cos is_differentiable_on Z by FDIFF_1:34,SIN_COS:72; then
  ((-id Z)(#)cos)|Z is continuous by A2,C,FDIFF_1:29,33;then
 ((-id Z)(#)cos)|A is continuous by A1,FCONT_1:17; then
A4: (-id Z)(#)cos is_integrable_on A
    & ((-id Z)(#)cos)|A is bounded by A1,A2,INTEGRA5:10,11;
A5: -cos-(id Z)(#)sin is_differentiable_on Z by Th19;
A6: for x st x in dom ((-cos-(id Z)(#)sin)`|Z) holds
   ((-cos-(id Z)(#)sin)`|Z).x = ((-id Z)(#)cos).x
   proof
     let x;
     assume x in dom ((-cos-(id Z)(#)sin)`|Z); then
A7:  x in Z by A5,FDIFF_1:def 8; then
     ((-cos-(id Z)(#)sin)`|Z).x =-x*cos.x by Th19
      .= ((-id Z)(#)cos).x by B,A7;
     hence thesis;
   end;
     dom ((-cos-(id Z)(#)sin)`|Z)
  = dom ((-id Z)(#)cos) by A2,A5,FDIFF_1:def 8;then
   ((-cos-(id Z)(#)sin)`|Z) = (-id Z)(#)cos by A6,PARTFUN1:34;
   hence thesis by A1,A4,Th19,INTEGRA5:13;
end;

:: f.x=sin.x+x*cos.x
theorem
  A c= Z implies integral(sin+(id Z)(#)cos,A)
  = ((id Z)(#)sin).(upper_bound A)-((id Z)(#)sin).(lower_bound A)
proof
  assume A: A c= Z;
A1: dom ((id Z)(#)sin) = Z
 proof
   dom ((id Z)(#)sin) = dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
   .= dom (id Z) by XBOOLE_1:28
   .= Z by RELAT_1:71;
   hence thesis;
  end;
A2: Z = dom (sin+(id Z)(#)cos)
 proof
     dom (sin+(id Z)(#)cos)
   = REAL /\ dom ((id Z)(#)cos) by SIN_COS:27,VALUED_1:def 1
  .= dom ((id Z)(#)cos) by XBOOLE_1:28
  .= dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
  .= dom (id Z) by XBOOLE_1:28;
    hence thesis by RELAT_1:71;
  end;
 Z = dom sin /\ dom ((id Z)(#)cos) by A2,VALUED_1:def 1;
   then
D: Z c= dom sin & Z c= dom ((id Z)(#)cos) by XBOOLE_1:18;then
 Z c= dom sin & (Z c= dom (id Z) /\ dom cos) by VALUED_1:def 4;
   then
AA: Z c= dom sin & Z c= dom (id Z) & Z c= dom cos by XBOOLE_1:18;
AB: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
B: for x st x in Z holds (sin+(id Z)(#)cos).x = sin.x+x*cos.x
  proof
    let x;
    assume C:x in Z;then
    (sin+(id Z)(#)cos).x = sin.x+((id Z)(#)cos).x by A2,VALUED_1:def 1
   .= sin.x+((id Z).x)*(cos.x) by VALUED_1:5
   .= sin.x+x*cos.x by C,FUNCT_1:35;
    hence thesis;
  end;
C1: id Z is_differentiable_on Z by AA,AB,FDIFF_1:31;
C2: sin is_differentiable_on Z by FDIFF_1:34,SIN_COS:73;
 cos is_differentiable_on Z by FDIFF_1:34,SIN_COS:72; then
 (id Z)(#)cos is_differentiable_on Z by D,C1,FDIFF_1:29; then
  (sin+(id Z)(#)cos)|Z is continuous by A2,C2,FDIFF_1:26,33;then
 (sin+(id Z)(#)cos)|A is continuous by A,FCONT_1:17;then
A3: sin+(id Z)(#)cos is_integrable_on A
    & (sin+(id Z)(#)cos)|A is bounded by A,A2,INTEGRA5:10,11;
A4: (id Z)(#)sin is_differentiable_on Z by A1,FDIFF_4:45;
A5: for x st x in dom (((id Z)(#)sin)`|Z) holds
   (((id Z)(#)sin)`|Z).x = (sin+(id Z)(#)cos).x
   proof
     let x;
     assume x in dom (((id Z)(#)sin)`|Z); then
A6:  x in Z by A4,FDIFF_1:def 8; then
     (((id Z)(#)sin)`|Z).x =sin.x+x*cos.x by A1,FDIFF_4:45
      .= (sin+(id Z)(#)cos).x by B,A6;
     hence thesis;
   end;
   dom (((id Z)(#)sin)`|Z)
  = dom (sin+(id Z)(#)cos) by A2,A4,FDIFF_1:def 8;then
   (((id Z)(#)sin)`|Z) = sin+(id Z)(#)cos by A5,PARTFUN1:34;
   hence thesis by A,A1,A3,FDIFF_4:45,INTEGRA5:13;
end;

:: f.x=-cos.x+x*sin.x
theorem
  A c= Z implies integral(-cos+(id Z)(#)sin,A)
  = ((-id Z)(#)cos).(upper_bound A)-((-id Z)(#)cos).(lower_bound A)
proof
  assume A: A c= Z;
A1: dom ((-id Z)(#)cos) = Z
 proof
   dom ((-id Z)(#)cos)
 = dom (-id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
.= dom (-id Z) by XBOOLE_1:28
.= dom (id Z) by VALUED_1:8;
  hence thesis by RELAT_1:71;
  end;
A2: Z = dom (-cos+(id Z)(#)sin)
 proof
   dom (-cos+(id Z)(#)sin)
 = dom (-cos) /\ dom((id Z)(#)sin) by VALUED_1:def 1
.= REAL /\ dom((id Z)(#)sin) by SIN_COS:27,VALUED_1:8
.= dom((id Z)(#)sin) by XBOOLE_1:28
.= dom (id Z) /\ REAL by SIN_COS:27,VALUED_1:def 4
.= dom (id Z) by XBOOLE_1:28;
  hence thesis by RELAT_1:71;
  end;
 Z = dom (-cos) /\ dom ((id Z)(#)sin) by A2,VALUED_1:def 1; then
D: Z c= dom (-cos) & Z c= dom ((id Z)(#)sin) by XBOOLE_1:18;then
 Z c= dom (-cos) & (Z c= dom (id Z) /\ dom sin) by VALUED_1:def 4; then
AA: Z c= dom (-cos) & Z c= dom (id Z) & Z c= dom sin by XBOOLE_1:18;
AB: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
B: for x st x in Z holds (-cos+(id Z)(#)sin).x = -cos.x+x*sin.x
  proof
    let x;
  assume C:x in Z;then
      (-cos+(id Z)(#)sin).x
    = (-cos).x+((id Z)(#)sin).x by A2,VALUED_1:def 1
   .= (-cos).x+((id Z).x)*(sin.x) by VALUED_1:5
   .= (-cos).x+x*sin.x by C,FUNCT_1:35
   .= -cos.x+x*sin.x by VALUED_1:8;
    hence thesis;
  end;
C1: id Z is_differentiable_on Z by AA,AB,FDIFF_1:31;
 sin is_differentiable_on Z by FDIFF_1:34,SIN_COS:73; then
C2: (id Z)(#)sin is_differentiable_on Z by D,C1,FDIFF_1:29;
 -cos is_differentiable_on Z by FDIFF_1:34,INTEGRA8:26; then
  (-cos+(id Z)(#)sin)|Z is continuous
   by A2,C2,FDIFF_1:26,33;then
 (-cos+(id Z)(#)sin)|A is continuous by A,FCONT_1:17; then
A3: -cos+(id Z)(#)sin is_integrable_on A
    & (-cos+(id Z)(#)sin)|A is bounded by A,A2,INTEGRA5:10,11;
A4: (-id Z)(#)cos is_differentiable_on Z by A1,FDIFF_4:44;
A5: for x st x in dom (((-id Z)(#)cos)`|Z) holds
   (((-id Z)(#)cos)`|Z).x = (-cos+(id Z)(#)sin).x
   proof
     let x;
     assume x in dom (((-id Z)(#)cos)`|Z); then
     A6: x in Z by A4,FDIFF_1:def 8; then
     (((-id Z)(#)cos)`|Z).x =-cos.x+x*sin.x by A1,FDIFF_4:44
      .= (-cos+(id Z)(#)sin).x by B,A6;
     hence thesis;
   end;
     dom (((-id Z)(#)cos)`|Z)
  = dom (-cos+(id Z)(#)sin) by A2,A4,FDIFF_1:def 8;then
   (((-id Z)(#)cos)`|Z) = -cos+(id Z)(#)sin by A5,PARTFUN1:34;
   hence thesis by A,A1,A3,FDIFF_4:44,INTEGRA5:13;
end;

:: f.x = x*(exp_R.x)
theorem
  integral((AffineMap(1,0))(#)exp_R,A)
   = (exp_R(#)(AffineMap(1,-1))).(upper_bound A)
    -(exp_R(#)(AffineMap(1,-1))).(lower_bound A)
proof
A1: dom (exp_R(#)(AffineMap(1,-1))) = [#]REAL by FUNCT_2:def 1;
A2: dom (AffineMap(1,0)) = [#]REAL by FUNCT_2:def 1;
B1: [#]REAL = dom ((AffineMap(1,0))(#)exp_R) by FUNCT_2:def 1;
A3: for x st x in REAL holds (AffineMap(1,-1)).x = x-1
 proof
  let x;
   assume x in REAL;
    (AffineMap(1,-1)).x = 1*x + (-1) by JORDAN16:def 3
      .= x-1;
    hence thesis;
  end;
A4: for x st x in REAL holds
   (AffineMap(1,0)).x = 1*x+0 by JORDAN16:def 3;
C1: AffineMap(1,0) is_differentiable_on REAL by A2,A4,FDIFF_1:31;
    (AffineMap(1,0))(#)exp_R is_differentiable_on REAL
         by B1,C1,FDIFF_1:29,TAYLOR_1:16;then
 ((AffineMap(1,0))(#)exp_R)|REAL is continuous by FDIFF_1:33;then
 ((AffineMap(1,0))(#)exp_R)|A is continuous by FCONT_1:17;
then A5: (AffineMap(1,0))(#)exp_R is_integrable_on A
   & ((AffineMap(1,0))(#)exp_R)|A is bounded by B1,INTEGRA5:10,11;
A6: (exp_R(#)(AffineMap(1,-1)))
  is_differentiable_on REAL by A1,A3,FDIFF_4:55;
B: for x st x in REAL holds ((AffineMap(1,0))(#)exp_R).x = x*(exp_R.x)
  proof
    let x;
  assume x in REAL;
      ((AffineMap(1,0))(#)exp_R).x
    = ((AffineMap(1,0)).x)*(exp_R.x) by VALUED_1:5
   .= (1*x+0)*(exp_R.x) by JORDAN16:def 3
   .= x*(exp_R.x);
    hence thesis;
  end;
A7:for x st x in dom ((exp_R(#)AffineMap(1,-1))`|REAL) holds
   ((exp_R(#)AffineMap(1,-1))`|REAL).x = ((AffineMap(1,0))(#)exp_R).x
   proof
     let x;
     assume x in dom ((exp_R(#)AffineMap(1,-1))`|REAL);
     ((exp_R(#)AffineMap(1,-1))`|REAL).x
       = x*(exp_R.x) by A1,A3,FDIFF_4:55
      .= ((AffineMap(1,0))(#)exp_R).x by B;
     hence thesis;
   end;
   dom ((exp_R(#)AffineMap(1,-1))`|REAL)
 = dom ((AffineMap(1,0))(#)exp_R) by B1,A6,FDIFF_1:def 8;then
   ((exp_R(#)AffineMap(1,-1))`|REAL)
 = (AffineMap(1,0))(#)exp_R by A7,PARTFUN1:34;
   hence thesis by A1,A3,A5,FDIFF_4:55,INTEGRA5:13;
end;

:: f.x = (1/(n+1))*x^(n+1)
theorem Th24:
  (1/(n+1))(#)( #Z (n+1)) is_differentiable_on REAL
  & for x holds (((1/(n+1))(#)( #Z (n+1)))`|REAL).x = x #Z n
proof
A1: [#]REAL = dom ( #Z (n+1)) &
  [#]REAL = dom ((1/(n+1))(#)( #Z (n+1))) by FUNCT_2:def 1;
A2: ( #Z (n+1)) is_differentiable_on REAL
 proof
  for x st x in REAL holds ( #Z (n+1)) is_differentiable_in x by TAYLOR_1:2;
   hence thesis by A1,FDIFF_1:16;
  end;
A3: for x st x in REAL holds (( #Z (n+1))`|REAL).x=(n+1) * x #Z n
  proof
    let x;
    assume x in REAL;
    set m=n+1;
    diff(( #Z m),x) = m * x #Z (m-1) by TAYLOR_1:2;
    hence thesis by A2,FDIFF_1:def 8;
  end;
 for x st x in REAL holds (((1/(n+1))(#)( #Z (n+1)))`|REAL).x=x #Z n
  proof
    let x;
    assume x in REAL;
     (((1/(n+1))(#)( #Z (n+1)))`|REAL).x=(1/(n+1))*diff(( #Z (n+1)),x)
    by A1,A2,FDIFF_1:28
      .=(1/(n+1))*(( #Z (n+1))`|REAL).x by A2,FDIFF_1:def 8
      .=(1/(n+1))*((n+1) * x #Z n) by A3
      .=(1/(n+1))*(n+1) * x #Z n
      .=(n+1)/(n+1) * x #Z n by XCMPLX_1:100
      .=1*x #Z n by XCMPLX_1:60;
  hence thesis;
  end;
  hence thesis by A1,A2,FDIFF_1:28;
end;

::  f.x = x^n
theorem
  integral( #Z n,A) =
    ((1/(n+1))*((upper_bound A) |^ (n+1))) -
      ((1/(n+1))*((lower_bound A) |^ (n+1)))
proof
A1: dom ( #Z n) = [#]REAL & dom ( #Z (n+1)) = [#]REAL
 & [#]REAL = dom ((1/(n+1))(#)( #Z (n+1))) by FUNCT_2:def 1;
A2: ( #Z n) is_differentiable_on REAL
 proof
  for x st x in REAL holds ( #Z n) is_differentiable_in x by TAYLOR_1:2;
   hence thesis by A1,FDIFF_1:16;
  end;
B1: ( #Z (n+1)) is_differentiable_on REAL
   proof
     for x st x in REAL holds ( #Z (n+1))
       is_differentiable_in x by TAYLOR_1:2;
     hence thesis by A1,FDIFF_1:16;
  end;
B2: (1/(n+1))(#)( #Z (n+1)) is_differentiable_on REAL
   by A1,B1,FDIFF_1:28;
 ( #Z n)|REAL is continuous by A2,FDIFF_1:33;then
 ( #Z n)|A is continuous by FCONT_1:17;
then A3:( #Z n) is_integrable_on A
    & ( #Z n)|A is bounded by A1,INTEGRA5:10,11;
A4:for x st x in dom (((1/(n+1))(#)( #Z (n+1)))`|REAL) holds
   (((1/(n+1))(#)( #Z (n+1)))`|REAL).x = ( #Z n).x
   proof
     let x;
     assume x in dom (((1/(n+1))(#)( #Z (n+1)))`|REAL);
     (((1/(n+1))(#)( #Z (n+1)))`|REAL).x =x #Z n by Th24
       .= ( #Z n).x by TAYLOR_1:def 1;
     hence thesis;
   end;
X2:( #Z (n+1)).(upper_bound A) = (upper_bound A) #Z (n+1) by TAYLOR_1:def 1
     .= (upper_bound A) |^ (n+1) by PREPOWER:46;
X1:( #Z (n+1)).(lower_bound A) = (lower_bound A) #Z (n+1) by TAYLOR_1:def 1
     .= (lower_bound A) |^ (n+1) by PREPOWER:46;
   dom (((1/(n+1))(#)( #Z (n+1)))`|REAL)
     = dom ( #Z n) by A1,B2,FDIFF_1:def 8; then
   (((1/(n+1))(#)( #Z (n+1)))`|REAL) = ( #Z n) by A4,PARTFUN1:34;
   then integral( #Z n,A) = ((1/(n+1))(#)( #Z (n+1))).(upper_bound A)
   -((1/(n+1))(#)( #Z (n+1))).(lower_bound A) by A3,Th24,INTEGRA5:13
     .= ((1/(n+1))*( #Z (n+1)).(upper_bound A))
       -((1/(n+1))(#)( #Z (n+1))).(lower_bound A) by VALUED_1:6
     .= (1/(n+1))*( #Z (n+1)).(upper_bound A)
       -(1/(n+1))*( #Z (n+1)).(lower_bound A) by VALUED_1:6
     .= (1/(n+1))*((upper_bound A) |^ (n+1))
       -(1/(n+1))*((lower_bound A) |^ (n+1)) by X1,X2;
   hence thesis;
end;

begin

theorem Th26:
for f,g being PartFunc of REAL,REAL,
C being non empty Subset of REAL
holds (f-g)||C = f||C - g||C
proof
   let f,g be PartFunc of REAL,REAL;
   let C be non empty Subset of REAL;
A1: dom ((f-g)||C) = dom (f-g) /\ C by RELAT_1:90
   .= dom f /\ dom g /\ C by VALUED_1:12;
a1:dom (f||C-g||C) = dom (f|C) /\ dom (g|C) by VALUED_1:12
   .= (dom f /\ C) /\ dom (g|C) by RELAT_1:90
   .= (dom f /\ C) /\ (dom g /\ C) by RELAT_1:90
   .= dom f /\ (C /\ (dom g /\ C)) by XBOOLE_1:16
   .= dom f /\ (dom g /\ (C /\ C)) by XBOOLE_1:16
   .= dom f /\ (dom g /\ C);
then A2:dom ((f-g)||C) = dom (f||C - g||C) by A1,XBOOLE_1:16;
   for c being Element of C st c in dom ((f-g)||C) holds
   (f-g)||C.c = (f||C - g||C).c
   proof
    let c be Element of C;
    assume A3:c in dom((f-g)||C);
    then c in dom(f-g) /\ C by RELAT_1:90;
then A4: c in dom(f-g) by XBOOLE_0:def 4;
A5:    (f-g)||C.c = (f-g).c by A3,FUNCT_1:70
    .= f.c - g.c by A4,VALUED_1:13;
        c in dom(f||C) /\ dom(g||C) by A2,A3,VALUED_1:12;
  then A6: c in dom(f|C) & c in dom(g|C) by XBOOLE_0:def 4;
       (f||C-g||C).c = f||C.c - g||C.c by A2,A3,VALUED_1:13
    .= f.c - g|C.c by A6,FUNCT_1:70;
    hence thesis by A5,A6,FUNCT_1:70;
   end;
   hence thesis by A1,a1,PARTFUN1:34,XBOOLE_1:16;
end;

theorem Th27:
for f1,f2,g being PartFunc of REAL,REAL,
C being non empty Subset of REAL
holds ((f1+f2)||C)(#)(g||C)=(f1(#)g+f2(#)g)||C
proof
 let f1,f2,g be PartFunc of REAL,REAL;
 let C be non empty Subset of REAL;
A1:  dom (((f1+f2)||C)(#)(g||C))
   = dom ((f1+f2)||C) /\ dom (g||C) by VALUED_1:def 4
  .= (dom (f1+f2) /\ C) /\ dom (g|C) by RELAT_1:90
  .= (dom (f1+f2) /\ C) /\ (dom g /\ C) by RELAT_1:90
  .= (dom f1 /\ dom f2 /\ C) /\ (dom g /\ C) by VALUED_1:def 1
  .= (dom f1 /\ dom f2) /\ (C /\ dom g /\ C) by XBOOLE_1:16
  .= (dom f1 /\ dom f2) /\ (dom g /\ (C /\ C)) by XBOOLE_1:16
  .= (dom f1 /\ dom f2 /\ dom g) /\ C by XBOOLE_1:16;
A2:    dom ((f1(#)g+f2(#)g)||C)
     = dom (f1(#)g+f2(#)g) /\ C by RELAT_1:90
    .= (dom (f1(#)g) /\ dom (f2(#)g))/\ C by VALUED_1:def 1
    .= (dom f1 /\ dom g) /\ dom (f2(#)g) /\ C by VALUED_1:def 4
    .= dom f1 /\ dom g /\ (dom f2 /\ dom g) /\ C by VALUED_1:def 4
    .= dom f1 /\ (dom g /\ dom f2 /\ dom g) /\ C  by XBOOLE_1:16
    .= dom f1 /\ (dom f2 /\ (dom g /\ dom g)) /\ C  by XBOOLE_1:16
    .= (dom f1 /\ dom f2 /\ dom g) /\ C  by XBOOLE_1:16;
 for c being Element of C st c in dom(((f1+f2)||C)(#)(g||C))
   holds (((f1+f2)||C)(#)(g||C)).c = ((f1(#)g+f2(#)g)||C).c
proof
  let c be Element of C;
  assume A3:c in dom(((f1+f2)||C)(#)(g||C));
  then c in dom((f1+f2)||C) /\ dom (g||C) by VALUED_1:def 4;
then A4: c in dom ((f1+f2)|C) & c in dom (g|C) by XBOOLE_0:def 4;
  then c in dom(f1+f2) /\ C & c in dom (g|C) by RELAT_1:90;
then A5: c in dom(f1+f2) & c in dom (g|C) by XBOOLE_0:def 4;
A6:  (((f1+f2)||C)(#)(g||C)).c
   = ((f1+f2)||C).c * (g||C).c by A3,VALUED_1:def 4
  .= (f1+f2).c * (g|C).c by A4,FUNCT_1:70
  .= (f1.c + f2.c) * (g|C).c by A5,VALUED_1:def 1;
    c in dom (f1(#)g+f2(#)g) /\ C by A1,A2,A3,RELAT_1:90;
then A7: c in dom (f1(#)g+f2(#)g) by XBOOLE_0:def 4;
then c in dom (f1(#)g) /\ dom(f2(#)g) by VALUED_1:def 1;
then A8: c in dom (f1(#)g) & c in dom(f2(#)g) by XBOOLE_0:def 4;
        ((f1(#)g)+(f2(#)g))||C.c
      = (f1(#)g+f2(#)g).c by A1,A2,A3,FUNCT_1:70
     .= (f1(#)g).c+(f2(#)g).c by A7,VALUED_1:def 1
     .= f1.c * g.c+(f2(#)g).c by A8,VALUED_1:def 4
     .= f1.c * g.c+f2.c * g.c by A8,VALUED_1:def 4
     .= (f1.c+f2.c) * g.c;
hence thesis by A4,A6,FUNCT_1:70;
  end;
  hence thesis by A1,A2,PARTFUN1:34;
end;

theorem Th28:
for f1,f2,g being PartFunc of REAL,REAL,
C being non empty Subset of REAL
holds ((f1-f2)||C)(#)(g||C)=(f1(#)g-f2(#)g)||C
proof
 let f1,f2,g be PartFunc of REAL,REAL;
 let C be non empty Subset of REAL;
A1:  dom (((f1-f2)||C)(#)(g||C))
   = dom ((f1-f2)|C) /\ dom (g|C) by VALUED_1:def 4
  .= (dom (f1-f2) /\ C) /\ dom (g|C) by RELAT_1:90
  .= (dom (f1-f2) /\ C) /\ (dom g /\ C) by RELAT_1:90
  .= (dom f1 /\ dom f2 /\ C) /\ (dom g /\ C) by VALUED_1:12
  .= (dom f1 /\ dom f2) /\ (C /\ dom g /\ C) by XBOOLE_1:16
  .= (dom f1 /\ dom f2) /\ (dom g /\ (C /\ C)) by XBOOLE_1:16
  .= (dom f1 /\ dom f2 /\ dom g) /\ C by XBOOLE_1:16;
A2:   dom ((f1(#)g-f2(#)g)||C)
     = dom (f1(#)g-f2(#)g) /\ C by RELAT_1:90
    .= (dom (f1(#)g) /\ dom (f2(#)g))/\ C by VALUED_1:12
    .= (dom f1 /\ dom g) /\ dom (f2(#)g) /\ C by VALUED_1:def 4
    .= dom f1 /\ dom g /\ (dom f2 /\ dom g) /\ C by VALUED_1:def 4
    .= dom f1 /\ (dom g /\ dom f2 /\ dom g) /\ C  by XBOOLE_1:16
    .= dom f1 /\ (dom f2 /\ (dom g /\ dom g)) /\ C  by XBOOLE_1:16
    .= (dom f1 /\ dom f2 /\ dom g) /\ C by XBOOLE_1:16;
 for c being Element of C st c in dom(((f1-f2)||C)(#)(g||C))
   holds (((f1-f2)||C)(#)(g||C)).c = ((f1(#)g-f2(#)g)||C).c
proof
  let c be Element of C;
  assume A3:c in dom(((f1-f2)||C)(#)(g||C));
  then c in dom((f1-f2)||C) /\ dom (g||C) by VALUED_1:def 4;
then A4: c in dom ((f1-f2)|C) & c in dom (g|C) by XBOOLE_0:def 4;
  then c in dom(f1-f2) /\ C & c in dom (g|C) by RELAT_1:90;
then A5: c in dom(f1-f2) & c in dom (g|C) by XBOOLE_0:def 4;
A6:  (((f1-f2)||C)(#)(g||C)).c
   =((f1-f2)|C).c * (g|C).c by A3,VALUED_1:def 4
  .= (f1-f2).c * (g|C).c by A4,FUNCT_1:70
  .= (f1.c - f2.c) * (g|C).c by A5,VALUED_1:13;
     c in dom (f1(#)g-f2(#)g) /\ C by A1,A2,A3,RELAT_1:90;
then A7: c in dom (f1(#)g-f2(#)g) by XBOOLE_0:def 4;
then c in dom (f1(#)g) /\ dom(f2(#)g) by VALUED_1:12;
then A8: c in dom (f1(#)g) & c in dom(f2(#)g) by XBOOLE_0:def 4;
        ((f1(#)g)-(f2(#)g))||C.c = (f1(#)g-f2(#)g).c by A1,A2,A3,FUNCT_1:70

     .= (f1(#)g).c-(f2(#)g).c by A7,VALUED_1:13
     .= f1.c * g.c-(f2(#)g).c by A8,VALUED_1:def 4
     .= f1.c * g.c-f2.c * g.c by A8,VALUED_1:def 4
     .= (f1.c-f2.c) * g.c;
hence thesis by A4,A6,FUNCT_1:70;
  end;
  hence thesis by A1,A2,PARTFUN1:34;
end;

theorem
for f1,f2,g being PartFunc of REAL,REAL,
C being non empty Subset of REAL
holds ((f1(#)f2)||C)(#)(g||C)=(f1||C)(#)((f2(#)g)||C)
proof
 let f1,f2,g be PartFunc of REAL,REAL;
 let C be non empty Subset of REAL;
A1: ((f1(#)f2)||C)(#)(g||C) = (f1||C)(#)(f2||C)(#)(g||C) by INTEGRA5:4;
   (f1||C)(#)((f2(#)g)||C) = (f1||C)(#)((f2||C)(#)(g||C)) by INTEGRA5:4;
 hence thesis by A1,RFUNCT_1:21;
end;

definition
  let A be closed-interval Subset of REAL;
  let f,g be PartFunc of REAL,REAL;
  func |||(f,g,A)||| -> Real equals
  integral((f(#)g),A);
  correctness;
end;

theorem
  for f,g being PartFunc of REAL,REAL,
      A being closed-interval Subset of REAL holds
    |||(f,g,A)||| = |||(g,f,A)|||;

theorem
for f1,f2,g being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL st (f1(#)g)||A is total &
(f2(#)g)||A is total & (f1(#)g)||A|A is bounded &
(f1(#)g)||A is integrable & (f2(#)g)||A|A is bounded &
(f2(#)g)||A is integrable
holds |||(f1+f2,g,A)||| = |||(f1,g,A)||| + |||(f2,g,A)|||
proof
let f1,f2,g be PartFunc of REAL,REAL;
let A be closed-interval Subset of REAL;
assume A1:(f1(#)g)||A is total & (f2(#)g)||A is total;
assume A2: (f1(#)g)||A|A is bounded & (f1(#)g)||A is integrable
  & (f2(#)g)||A|A is bounded & (f2(#)g)||A is integrable;
 |||(f1+f2,g,A)||| = integral(((f1+f2)||A)(#)(g||A)) by INTEGRA5:4
  .= integral(((f1(#)g)+(f2(#)g))||A) by Th27
  .= integral(((f1(#)g)||A)+((f2(#)g)||A)) by INTEGRA5:5
  .= integral((f1(#)g)||A)
      + integral((f2(#)g)||A) by A1,A2,INTEGRA1:59;
 hence thesis;
end;

theorem
for f1,f2,g being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL st (f1(#)g)||A is total &
(f2(#)g)||A is total & (f1(#)g)||A|A is bounded &
(f1(#)g)||A is integrable & (f2(#)g)||A|A is bounded &
(f2(#)g)||A is integrable
holds |||(f1-f2,g,A)||| = |||(f1,g,A)||| - |||(f2,g,A)|||
proof
let f1,f2,g be PartFunc of REAL,REAL;
let A be closed-interval Subset of REAL;
assume A1:(f1(#)g)||A is total & (f2(#)g)||A is total;
assume A2:(f1(#)g)||A|A is bounded & (f1(#)g)||A is integrable &
(f2(#)g)||A|A is bounded & (f2(#)g)||A is integrable;
|||(f1-f2,g,A)||| = integral(((f1-f2)||A)(#)(g||A)) by INTEGRA5:4
 .= integral(((f1(#)g)-(f2(#)g))||A) by Th28
 .= integral(((f1(#)g)||A)-((f2(#)g)||A)) by Th26
 .= integral((f1(#)g)||A)
     - integral((f2(#)g)||A) by A1,A2,INTEGRA2:33;
hence thesis;
end;

theorem
for f,g being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL
st (f(#)g)|A is bounded & f(#)g is_integrable_on A
& A c= dom(f(#)g)
holds |||(-f,g,A)||| = - |||(f,g,A)|||
proof
let f,g being PartFunc of REAL,REAL;
let A being closed-interval Subset of REAL;
  assume A1: (f(#)g)|A is bounded
     & f(#)g is_integrable_on A & A c= dom(f(#)g);
|||(-f,g,A)||| = integral(((-1)(#)(f(#)g)),A) by RFUNCT_1:24
 .= (-1)*integral((f(#)g),A) by A1,INTEGRA6:9
 .= - |||(f,g,A)|||;
hence thesis;
end;

theorem
for f,g being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL
st (f(#)g)|A is bounded & f(#)g is_integrable_on A
& A c= dom(f(#)g)
 holds |||((r(#)f),g,A)||| = r*|||(f,g,A)|||
proof
let f,g be PartFunc of REAL,REAL;
let A be closed-interval Subset of REAL;
  assume A1: (f(#)g)|A is bounded
     & f(#)g is_integrable_on A & A c= dom(f(#)g);
|||((r(#)f),g,A)||| = integral((r(#)(f(#)g)),A) by RFUNCT_1:24
.= r*integral((f(#)g),A) by A1,INTEGRA6:9;
hence thesis;
end;

theorem
for f,g being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL
st (f(#)g)|A is bounded & f(#)g is_integrable_on A & A c= dom(f(#)g)
holds |||((r(#)f),(p(#)g),A)||| = r*p*|||(f,g,A)|||
proof
let f,g be PartFunc of REAL,REAL;
let A be closed-interval Subset of REAL;
  assume A1: (f(#)g)|A is bounded
     & f(#)g is_integrable_on A & A c= dom(f(#)g);
|||((r(#)f),(p(#)g),A)||| = integral((r(#)(f(#)(p(#)g))),A) by RFUNCT_1:24
.= integral((r(#)(p(#)(f(#)g))),A) by RFUNCT_1:25
.= integral(((r*p)(#)(f(#)g)),A) by RFUNCT_1:29
.= (r*p)*integral((f(#)g),A) by A1,INTEGRA6:9;
hence thesis;
end;

theorem
for f,g,h being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL
holds |||((f(#)g),h,A)||| = |||(f,(g(#)h),A)||| by RFUNCT_1:21;

theorem Th37:
for f,g being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL st (f(#)f)||A is total &
(f(#)g)||A is total & (g(#)g)||A is total & (f(#)f)||A|A is bounded &
(f(#)g)||A|A is bounded & (g(#)g)||A|A is bounded
& (f(#)f) is_integrable_on A & (f(#)g) is_integrable_on A
& (g(#)g) is_integrable_on A holds
  |||(f+g,f+g,A)||| = |||(f,f,A)||| + 2*|||(f,g,A)||| + |||(g,g,A)|||
proof
let f,g be PartFunc of REAL,REAL;
let A be closed-interval Subset of REAL;
assume A1: (f(#)f)||A is total
      & (f(#)g)||A is total & (g(#)g)||A is total;
assume A2: (f(#)f)||A|A is bounded &
  (f(#)g)||A|A is bounded & (g(#)g)||A|A is bounded;
 then A3:((f(#)f)||A+(f(#)g)||A)|(A /\ A) is bounded
    & ((f(#)g)||A+(g(#)g)||A)|(A /\ A) is bounded by RFUNCT_1:100;
assume (f(#)f) is_integrable_on A
    & (f(#)g) is_integrable_on A & (g(#)g) is_integrable_on A;
then A4: (f(#)f)||A is integrable & (f(#)g)||A is integrable
  & (g(#)g)||A is integrable by INTEGRA5:def 2;
then A5: ((f(#)f)||A+(f(#)g)||A) is integrable &
 ((f(#)g)||A+(g(#)g)||A) is integrable by A1,A2,INTEGRA1:59;
|||(f+g,f+g,A)|||
  = integral(((f(#)(f+g))+(g(#)(f+g)))||A) by RFUNCT_1:22
 .= integral(((f(#)(f+g))||A)+((g(#)(f+g))||A)) by INTEGRA5:5
 .= integral((((f(#)f)+(f(#)g))||A)+((g(#)(f+g))||A)) by RFUNCT_1:23
 .= integral((((f(#)f)+(f(#)g))||A)
          +(((g(#)f)+(g(#)g))||A)) by RFUNCT_1:23
 .= integral(((f(#)f)||A+(f(#)g)||A)
          +(((g(#)f)+(g(#)g))||A)) by INTEGRA5:5
 .= integral(((f(#)f)||A+(f(#)g)||A)
          +((g(#)f)||A+(g(#)g)||A)) by INTEGRA5:5
 .= integral((f(#)f)||A+(f(#)g)||A)
          +integral((f(#)g)||A+(g(#)g)||A) by A1,A3,A5,INTEGRA1:59
 .= integral((f(#)f)||A)+integral((f(#)g)||A)
          +integral((f(#)g)||A+(g(#)g)||A) by A1,A2,A4,INTEGRA1:59
 .= integral((f(#)f)||A)+integral((f(#)g)||A)
       +(integral((f(#)g)||A)+integral((g(#)g)||A)) by A1,A2,A4,INTEGRA1:59;
hence thesis;
end;

begin :: Orthogonal polynomials

definition
  let A be closed-interval Subset of REAL;
  let f,g be PartFunc of REAL,REAL;
  pred f is_orthogonal_with g,A means :Def2:
    |||(f,g,A)||| = 0;
end;

theorem Th38:
for f,g being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL st (f(#)f)||A is total &
(f(#)g)||A is total & (g(#)g)||A is total & (f(#)f)||A|A is bounded &
(f(#)g)||A|A is bounded & (g(#)g)||A|A is bounded
& (f(#)f) is_integrable_on A & (f(#)g) is_integrable_on A
& (g(#)g) is_integrable_on A & f is_orthogonal_with g,A
  holds |||(f+g,f+g,A)||| = |||(f,f,A)||| + |||(g,g,A)|||
proof
let f,g be PartFunc of REAL,REAL;
let A be closed-interval Subset of REAL;
assume (f(#)f)||A is total & (f(#)g)||A is total
 & (g(#)g)||A is total &(f(#)f)||A|A is bounded &
(f(#)g)||A|A is bounded & (g(#)g)||A|A is bounded
& (f(#)f) is_integrable_on A & (f(#)g) is_integrable_on A
& (g(#)g) is_integrable_on A;
then A1: |||(f+g,f+g,A)||| = |||(f,f,A)|||
    + 2*|||(f,g,A)||| + |||(g,g,A)||| by Th37;
assume f is_orthogonal_with g,A;
then |||(f,g,A)||| = 0 by Def2;
   hence thesis by A1;
end;

theorem
for f being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL st (f(#)f)||A is total
& (f(#)f)||A|A is bounded & (f(#)f)||A is integrable
& (for x st x in A holds (f(#)f)||A.x >= 0)
holds |||(f,f,A)||| >= 0 by INTEGRA2:32;

theorem
A = [.0,PI.] implies sin is_orthogonal_with cos,A
proof
 assume A1: A = [.0,PI.];
 |||(sin,cos,A)||| = 0 by A1,INTEGRA8:92;
 hence thesis by Def2;
end;

theorem
A = [.0,PI*2.] implies sin is_orthogonal_with cos,A
proof
 assume A1: A = [.0,PI*2.];
 |||(sin,cos,A)||| = 0 by A1,INTEGRA8:94;
 hence thesis by Def2;
end;

theorem
A = [.2*n*PI,(2*n+1)*PI.] implies sin is_orthogonal_with cos,A
proof
 assume A1: A = [.2*n*PI,(2*n+1)*PI.];
 |||(sin,cos,A)||| = 0 by A1,INTEGRA8:95;
 hence thesis by Def2;
end;

theorem
A = [.x+2*n*PI,x+(2*n+1)*PI.] implies sin is_orthogonal_with cos,A
proof
  assume A1: A = [.x+2*n*PI,x+(2*n+1)*PI.];
  |||(sin,cos,A)||| = 0 by A1,INTEGRA8:96;
  hence thesis by Def2;
end;

theorem
A = [.-PI,PI.] implies sin is_orthogonal_with cos,A
proof
  assume A = [.-PI,PI.];then
   A1: upper_bound A = PI & lower_bound A = -PI by INTEGRA8:37;
  |||(sin,cos,A)||| = 1/2*(cos.(lower_bound A)*cos.(lower_bound A)
        -cos.(upper_bound A)*cos.(upper_bound A)) by INTEGRA8:90
   .= 1/2*(cos.(PI)*cos.(-PI)-cos.PI*cos.PI) by A1,SIN_COS:33
   .= 1/2*(cos.(PI)*cos.(PI)-cos.PI*cos.PI) by SIN_COS:33;
   hence thesis by Def2;
end;

theorem
A = [.-PI/2,PI/2.] implies sin is_orthogonal_with cos,A
proof
  assume A = [.-PI/2,PI/2.];then
   A1: upper_bound A = PI/2 & lower_bound A = -PI/2 by INTEGRA8:37;
      |||(sin,cos,A)||| = 1/2*(cos.(lower_bound A)*cos.(lower_bound A)
        -cos.(upper_bound A)*cos.(upper_bound A)) by INTEGRA8:90
   .= 1/2*(cos.(PI/2)*cos.(-PI/2)
            -cos.(PI/2)*cos.(PI/2)) by A1,SIN_COS:33
   .= 1/2*(cos.(PI/2)*cos.(PI/2)
            -cos.(PI/2)*cos.(PI/2)) by SIN_COS:33;
   hence thesis by Def2;
end;

theorem
A = [.-2*PI,2*PI.] implies sin is_orthogonal_with cos,A
proof
  assume A = [.-2*PI,2*PI.];then
   A1: upper_bound A = 2*PI & lower_bound A = -2*PI by INTEGRA8:37;
      |||(sin,cos,A)||| = 1/2*(cos.(lower_bound A)*cos.(lower_bound A)
        -cos.(upper_bound A)*cos.(upper_bound A)) by INTEGRA8:90
   .= 1/2*(cos.(2*PI)*cos.(-2*PI)
          -cos.(2*PI)*cos.(2*PI)) by A1,SIN_COS:33
   .= 1/2*(cos.(2*PI)*cos.(2*PI)
           -cos.(2*PI)*cos.(2*PI)) by SIN_COS:33;
   hence thesis by Def2;
end;

theorem
A = [.-2*n*PI,2*n*PI.] implies sin is_orthogonal_with cos,A
proof
  assume A = [.-2*n*PI,2*n*PI.];then
   A1: upper_bound A = 2*n*PI & lower_bound A = -2*n*PI by INTEGRA8:37;
      |||(sin,cos,A)||| = 1/2*(cos.(lower_bound A)*cos.(lower_bound A)
        -cos.(upper_bound A)*cos.(upper_bound A)) by INTEGRA8:90
   .= 1/2*(cos.(2*n*PI)*cos.(-2*n*PI)
          -cos.(2*n*PI)*cos.(2*n*PI)) by A1,SIN_COS:33
   .= 1/2*(cos.(2*n*PI)*cos.(2*n*PI)
           -cos.(2*n*PI)*cos.(2*n*PI)) by SIN_COS:33;
   hence thesis by Def2;
end;

theorem
A = [.x-2*n*PI,x+2*n*PI.] implies sin is_orthogonal_with cos,A
proof
  assume A = [.x-2*n*PI,x+2*n*PI.];then
   A1: upper_bound A = x+2*n*PI & lower_bound A = x-2*n*PI by INTEGRA8:37;
      |||(sin,cos,A)||| = 1/2*(cos.(lower_bound A)*cos.(lower_bound A)
        -cos.(upper_bound A)*cos.(upper_bound A)) by INTEGRA8:90
   .= 1/2*(cos.(2*n*PI-x)*cos.(-(2*n*PI-x))
       -cos.(x+2*n*PI)*cos.(x+2*n*PI)) by A1,SIN_COS:33
   .= 1/2*(cos.(-x+2*n*PI)*cos.(-x+2*n*PI)
       -cos.(x+2*n*PI)*cos.(x+2*n*PI)) by SIN_COS:33
   .= 1/2*(cos(-x)*cos(-x+2*n*PI)
       -cos.(x+2*n*PI)*cos.(x+2*n*PI)) by INTEGRA8:3
   .= 1/2*(cos(-x)*cos(-x)
       -cos.(x+2*n*PI)*cos.(x+2*n*PI)) by INTEGRA8:3
   .= 1/2*(cos(x)*cos(-x)
       -cos.(x+2*n*PI)*cos.(x+2*n*PI)) by SIN_COS:34
   .= 1/2*(cos(x)*cos(x)
        -cos(x+2*n*PI)*cos.(x+2*n*PI)) by SIN_COS:34
   .= 1/2*(cos(x)*cos(x) -cos(x)*cos(x+2*n*PI)) by INTEGRA8:3
   .= 1/2*(cos(x)*cos(x) - cos(x)*cos(x)) by INTEGRA8:3;
   hence thesis by Def2;
end;

begin :: Definition and properties of norm function

definition
  let A be closed-interval Subset of REAL;
  let f be PartFunc of REAL,REAL;
  func ||..f,A..|| -> Real equals
    sqrt (|||(f,f,A)|||);
  correctness;
end;

theorem
for f being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL st
(f(#)f)||A is total & (f(#)f)||A|A is bounded &
(f(#)f)||A is integrable &
(for x st x in A holds (f(#)f)||A.x >= 0) holds 0 <= ||..f,A..||
proof
  let f be PartFunc of REAL,REAL;
  let A be closed-interval Subset of REAL;
  assume A1: (f(#)f)||A is total;
  assume A2:(f(#)f)||A|A is bounded & (f(#)f)||A is integrable
     & (for x st x in A holds (f(#)f)||A.x >= 0);
  |||(f,f,A)||| >= 0 by A1,A2,INTEGRA2:32;
  hence thesis by SQUARE_1:def 4;
end;

theorem
for f being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL holds
||..(1(#)f),A..|| = ||..f,A..|| by RFUNCT_1:33;

theorem
for f,g being PartFunc of REAL,REAL,
A being closed-interval Subset of REAL st (f(#)f)||A is total &
(f(#)g)||A is total & (g(#)g)||A is total & (f(#)f)||A|A is bounded
& (f(#)g)||A|A is bounded & (g(#)g)||A|A is bounded
& (f(#)f) is_integrable_on A & (f(#)g) is_integrable_on A
& (g(#)g) is_integrable_on A & f is_orthogonal_with g,A
& (for x st x in A holds (f(#)f)||A.x >= 0)
& (for x st x in A holds (g(#)g)||A.x >= 0)
holds ||..(f+g),A..||^2 = ||..f,A..||^2 + ||..g,A..||^2
proof
let f,g be PartFunc of REAL,REAL;
let A be closed-interval Subset of REAL;
assume A1: (f(#)f)||A is total & (f(#)g)||A is total
 & (g(#)g)||A is total & (f(#)f)||A|A is bounded &
(f(#)g)||A|A is bounded & (g(#)g)||A|A is bounded
& (f(#)f) is_integrable_on A & (f(#)g) is_integrable_on A
& (g(#)g) is_integrable_on A;
then B1: (f(#)f)||A is integrable & (f(#)g)||A is integrable
 & (g(#)g)||A is integrable by INTEGRA5:def 2;
assume A2: f is_orthogonal_with g,A;
A3: |||(f+g,f+g,A)||| = |||(f,f,A)||| + |||(g,g,A)||| by A1,A2,Th38;
assume (for x st x in A holds (f(#)f)||A.x >= 0);then
A4: |||(f,f,A)||| >= 0 by A1,B1,INTEGRA2:32;
assume (for x st x in A holds (g(#)g)||A.x >= 0);then
A5: |||(g,g,A)||| >= 0 by A1,B1,INTEGRA2:32;
|||((f+g),(f+g),A)||| >=0 by A3,A4,A5,XREAL_1:35; then
A6: ||..(f+g),A..||^2 = |||((f+g),(f+g),A)||| by SQUARE_1:def 4;
A7: ||..f,A..||^2 = |||(f,f,A)||| by A4,SQUARE_1:def 4;
 ||..g,A..||^2 = |||(g,g,A)||| by A5,SQUARE_1:def 4;
 hence thesis by A1,A2,Th38,A6,A7;
end;

begin :: To be revised

 reserve r,a,b,p,x,r,s for Real;
 reserve y for set;
 reserve C for non empty set;
 reserve n for Element of NAT;
 reserve A for closed-interval Subset of REAL;
 reserve f,g,h,f1,f2 for PartFunc of REAL,REAL;
 reserve Z for open Subset of REAL;

theorem :: f.x = 1/(a+x)
  not -a in A implies (AffineMap (1,a))^|A is continuous
  proof
    assume
AA: not -a in A;
    set g2 = AffineMap (1,a);
    set i2 = g2^;
A3: dom g2 = [#]REAL by FUNCT_2:def 1;
    g2|dom g2 is continuous by RELAT_1:98; then
A2: g2|A is continuous by A3,FCONT_1:17;
    not 0 in rng (g2|A)
    proof
      set h2 = g2|A;
      assume 0 in rng (g2|A); then
      consider x being set such that
C1:   x in dom h2 & h2.x = 0 by FUNCT_1:def 5;
      reconsider d = x as Real by C1;
C4:   dom h2 c= A by RELAT_1:87;
Af:   d in A by C1,RELAT_1:86;
      g2.d = a+1*d by JORDAN16:def 3; then
      a+d = 0 by C1,Af,FUNCT_1:72;
      hence thesis by AA,C4,C1;
    end; then
    (g2|A)"{0} = {} by FUNCT_1:142;
    hence thesis by A2,FCONT_1:24;
  end;

::  f.x=-1/(a+x)^2
theorem
  A c= Z & (for x st x in Z holds f.x = a+x & f.x<>0) & Z = dom f
  & dom f = dom f2 & (for x st x in Z holds f2.x = -1/(a+x)^2 ) &
  f2|A is continuous implies
  integral(f2,A) = f^.(upper_bound A)-f^.(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = a+x & f.x<>0) & Z = dom f
  & dom f = dom f2 & (for x st x in Z holds f2.x = -1/(a+x)^2 ) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:f^ is_differentiable_on Z by A1,FDIFF_4:14;
A4:for x st x in dom ((f^)`|Z) holds
   ((f^)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((f^)`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     ((f^)`|Z).x = -1/(a+x)^2 by A1,FDIFF_4:14
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((f^)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((f^)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x=1/(a+x)^2
theorem
  A c= Z & (for x st x in Z holds f.x = a+x & f.x<>0) & dom((-1)(#)(f^))=Z
  & dom((-1)(#)(f^))=dom f2 & (for x st x in Z holds f2.x = 1/(a+x)^2 ) &
  f2|A is continuous implies
  integral(f2,A) = ((-1)(#)(f^)).(upper_bound A)-((-1)(#)(f^)).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = a+x & f.x<>0) & dom((-1)(#)(f^))=Z
  & dom((-1)(#)(f^))=dom f2 & (for x st x in Z holds f2.x = 1/(a+x)^2 ) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:((-1)(#)(f^)) is_differentiable_on Z by A1,FDIFF_4:15;
A4:for x st x in dom (((-1)(#)(f^))`|Z) holds
   (((-1)(#)(f^))`|Z).x = f2.x
   proof
     let x;
     assume x in dom (((-1)(#)(f^))`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     (((-1)(#)(f^))`|Z).x = 1/(a+x)^2 by A1,FDIFF_4:15
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom (((-1)(#)(f^))`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   (((-1)(#)(f^))`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x=1/(a-x)^2
theorem
  A c= Z & (for x st x in Z holds f.x = a-x & f.x<>0) & dom f = Z
  & dom f = dom f2 & (for x st x in Z holds f2.x = 1/(a-x)^2 ) &
  f2|A is continuous implies
  integral(f2,A) = f^.(upper_bound A)-f^.(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = a-x & f.x<>0) & dom f = Z
  & dom f = dom f2 & (for x st x in Z holds f2.x = 1/(a-x)^2 ) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:f^ is_differentiable_on Z by A1,FDIFF_4:16;
A4:for x st x in dom ((f^)`|Z) holds ((f^)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((f^)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((f^)`|Z).x = 1/(a-x)^2 by A1,FDIFF_4:16
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((f^)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((f^)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x=1/(a+x)
theorem
  A c= Z & (for x st x in Z holds f.x = a+x & f.x > 0) & dom (ln*f) = Z
  & dom (ln*f) = dom f2 & (for x st x in Z holds f2.x = 1/(a+x) ) &
  f2|A is continuous implies
  integral(f2,A) = (ln*f).(upper_bound A)-(ln*f).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = a+x & f.x > 0) & dom (ln*f) = Z
  & dom (ln*f) = dom f2 & (for x st x in Z holds f2.x = 1/(a+x)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:ln*f is_differentiable_on Z by A1,FDIFF_4:1;
A4:for x st x in dom ((ln*f)`|Z) holds
   ((ln*f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((ln*f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((ln*f)`|Z).x = 1/(a+x) by A1,FDIFF_4:1
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((ln*f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((ln*f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x=1/(x-a)
theorem
  A c= Z & (for x st x in Z holds f.x = x-a & f.x > 0) & dom (ln*f) = Z
  & dom (ln*f) = dom f2 & (for x st x in Z holds f2.x = 1/(x-a) ) &
  f2|A is continuous implies
  integral(f2,A) = (ln*f).(upper_bound A)-(ln*f).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = x-a & f.x > 0) & dom (ln*f) = Z
  & dom (ln*f) = dom f2 & (for x st x in Z holds f2.x = 1/(x-a)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:ln*f is_differentiable_on Z by A1,FDIFF_4:2;
A4:for x st x in dom ((ln*f)`|Z) holds
   ((ln*f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((ln*f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((ln*f)`|Z).x = 1/(x-a) by A1,FDIFF_4:2
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((ln*f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((ln*f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x=1/(a-x)
theorem
  A c= Z & (for x st x in Z holds f.x = a-x & f.x > 0) & dom (-(ln*f)) = Z
  & dom (-(ln*f)) = dom f2 & (for x st x in Z holds f2.x = 1/(a-x) ) &
  f2|A is continuous implies
  integral(f2,A) = (-(ln*f)).(upper_bound A)-(-(ln*f)).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = a-x & f.x > 0) & dom (-(ln*f)) = Z
  & dom (-(ln*f)) = dom f2 & (for x st x in Z holds f2.x = 1/(a-x)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(-(ln*f)) is_differentiable_on Z by A1,FDIFF_4:3;
A4:for x st x in dom ((-(ln*f))`|Z) holds
   ((-(ln*f))`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((-(ln*f))`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((-(ln*f))`|Z).x = 1/(a-x) by A1,FDIFF_4:3
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((-(ln*f))`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((-(ln*f))`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x= x/(a+x)
theorem
  A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=a+x & f1.x>0) & dom (id Z - a(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = x/(a+x) ) &
  f2|A is continuous implies
  integral(f2,A) =
    (id Z - a(#)f).(upper_bound A)-(id Z - a(#)f).(lower_bound A)
proof
   assume
A1:A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=a+x & f1.x>0) & dom (id Z - a(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = x/(a+x)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(id Z - a(#)f) is_differentiable_on Z by A1,FDIFF_4:4;
A4:for x st x in dom ((id Z - a(#)f)`|Z) holds
   ((id Z - a(#)f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((id Z - a(#)f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z - a(#)f)`|Z).x = x/(a+x) by A1,FDIFF_4:4
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z - a(#)f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((id Z - a(#)f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x= (a-x)/(a+x)
theorem
  A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=a+x & f1.x>0) & dom ((2*a)(#)f - id Z) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (a-x)/(a+x)) &
  f2|A is continuous implies
  integral(f2,A) =
    ((2*a)(#)f - id Z).(upper_bound A)-((2*a)(#)f - id Z).(lower_bound A)
proof
   assume
A1:A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=a+x & f1.x>0) & dom ((2*a)(#)f - id Z) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (a-x)/(a+x)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:((2*a)(#)f - id Z) is_differentiable_on Z by A1,FDIFF_4:5;
A4:for x st x in dom (((2*a)(#)f - id Z)`|Z) holds
   (((2*a)(#)f - id Z)`|Z).x = f2.x
   proof
     let x;
     assume x in dom (((2*a)(#)f - id Z)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     (((2*a)(#)f - id Z)`|Z).x = (a-x)/(a+x) by A1,FDIFF_4:5
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom (((2*a)(#)f - id Z)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   (((2*a)(#)f - id Z)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x= (x-a)/(x+a)
theorem
  A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x+a & f1.x>0) & dom (id Z -(2*a)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x-a)/(x+a)) &
  f2|A is continuous implies
  integral(f2,A) =
    (id Z -(2*a)(#)f).(upper_bound A)-(id Z -(2*a)(#)f).(lower_bound A)
proof
   assume
A1:A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x+a & f1.x>0) & dom (id Z -(2*a)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x-a)/(x+a)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(id Z -(2*a)(#)f) is_differentiable_on Z by A1,FDIFF_4:6;
A4:for x st x in dom ((id Z -(2*a)(#)f)`|Z) holds
   ((id Z -(2*a)(#)f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((id Z -(2*a)(#)f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z -(2*a)(#)f)`|Z).x = (x-a)/(x+a) by A1,FDIFF_4:6
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z -(2*a)(#)f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((id Z -(2*a)(#)f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x= (x+a)/(x-a)
theorem
  A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x-a & f1.x>0) & dom (id Z + (2*a)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x+a)/(x-a)) &
  f2|A is continuous implies
  integral(f2,A) =
    (id Z + (2*a)(#)f).(upper_bound A)-(id Z + (2*a)(#)f).(lower_bound A)
proof
   assume
A1:A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x-a & f1.x>0) & dom (id Z + (2*a)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x+a)/(x-a)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(id Z + (2*a)(#)f) is_differentiable_on Z by A1,FDIFF_4:7;
A4:for x st x in dom ((id Z + (2*a)(#)f)`|Z) holds
   ((id Z + (2*a)(#)f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((id Z + (2*a)(#)f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z + (2*a)(#)f)`|Z).x = (x+a)/(x-a) by A1,FDIFF_4:7
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z + (2*a)(#)f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((id Z + (2*a)(#)f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x= (x+a)/(x+b)
theorem
  A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x+b & f1.x>0) & dom (id Z + (a-b)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x+a)/(x+b)) &
  f2|A is continuous implies
  integral(f2,A) =
    (id Z + (a-b)(#)f).(upper_bound A)-(id Z + (a-b)(#)f).(lower_bound A)
proof
   assume
   A1:A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x+b & f1.x>0) & dom (id Z + (a-b)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x+a)/(x+b)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(id Z + (a-b)(#)f) is_differentiable_on Z by A1,FDIFF_4:8;
A4:for x st x in dom ((id Z + (a-b)(#)f)`|Z) holds
   ((id Z + (a-b)(#)f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((id Z + (a-b)(#)f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z + (a-b)(#)f)`|Z).x = (x+a)/(x+b) by A1,FDIFF_4:8
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z + (a-b)(#)f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((id Z + (a-b)(#)f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x= (x+a)/(x-b)
theorem
  A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x-b & f1.x>0) & dom (id Z + (a+b)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x+a)/(x-b)) &
  f2|A is continuous implies
  integral(f2,A) =
    (id Z + (a+b)(#)f).(upper_bound A)-(id Z + (a+b)(#)f).(lower_bound A)
proof
   assume
A1:A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x-b & f1.x>0) & dom (id Z + (a+b)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x+a)/(x-b)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(id Z + (a+b)(#)f) is_differentiable_on Z by A1,FDIFF_4:9;
A4:for x st x in dom ((id Z + (a+b)(#)f)`|Z) holds
   ((id Z + (a+b)(#)f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((id Z + (a+b)(#)f)`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     ((id Z + (a+b)(#)f)`|Z).x = (x+a)/(x-b) by A1,FDIFF_4:9
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z + (a+b)(#)f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((id Z + (a+b)(#)f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x= (x-a)/(x+b)
theorem
  A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x+b & f1.x>0) & dom (id Z - (a+b)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x-a)/(x+b)) &
  f2|A is continuous implies
  integral(f2,A) =
    (id Z - (a+b)(#)f).(upper_bound A)-(id Z - (a+b)(#)f).(lower_bound A)
proof
   assume
A1:A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x+b & f1.x>0) & dom (id Z - (a+b)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x-a)/(x+b)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(id Z - (a+b)(#)f) is_differentiable_on Z by A1,FDIFF_4:10;
A4:for x st x in dom ((id Z - (a+b)(#)f)`|Z) holds
   ((id Z - (a+b)(#)f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((id Z - (a+b)(#)f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z - (a+b)(#)f)`|Z).x = (x-a)/(x+b) by A1,FDIFF_4:10
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z - (a+b)(#)f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((id Z - (a+b)(#)f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x= (x-a)/(x-b)
theorem
  A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x-b & f1.x>0) & dom (id Z + (b-a)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x-a)/(x-b)) &
  f2|A is continuous implies
  integral(f2,A) =
    (id Z + (b-a)(#)f).(upper_bound A)-(id Z + (b-a)(#)f).(lower_bound A)
proof
   assume
A1:A c= Z & f = ln*f1 &
  (for x st x in Z holds f1.x=x-b & f1.x>0) & dom (id Z + (b-a)(#)f) = Z
  & Z = dom f2 & (for x st x in Z holds f2.x = (x-a)/(x-b)) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(id Z + (b-a)(#)f) is_differentiable_on Z by A1,FDIFF_4:11;
A4:for x st x in dom ((id Z + (b-a)(#)f)`|Z) holds
   ((id Z + (b-a)(#)f)`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((id Z + (b-a)(#)f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z + (b-a)(#)f)`|Z).x = (x-a)/(x-b) by A1,FDIFF_4:11
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z + (b-a)(#)f)`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((id Z + (b-a)(#)f)`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x=1/x
theorem
  A c= Z & (for x st x in Z holds x > 0) & dom ln = Z &
  Z = dom ((id Z)^) & (id Z)^|A is continuous implies
  integral((id Z)^,A) = ln.(upper_bound A)-ln.(lower_bound A)
proof
   set f2 = (id Z)^;
   assume
A1:A c= Z & (for x st x in Z holds x > 0) & dom ln = Z
   & Z = dom ((id Z)^) & ((id Z)^)|A is continuous; then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:ln is_differentiable_on Z by A1,FDIFF_5:19;
A4:for x st x in dom (ln`|Z) holds (ln`|Z).x = f2.x
   proof
     let x;
     assume x in dom (ln`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     (ln`|Z).x = 1/x by A1,FDIFF_5:19
      .= x" by XCMPLX_1:217
      .= ((id Z).x)" by A5,FUNCT_1:35
      .= f2.x by A1,A5,RFUNCT_1:def 8;
     hence thesis;
   end;
   dom (ln`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   (ln`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x=n/x
theorem
  A c= Z & (for x st x in Z holds x > 0) & dom (ln*( #Z n)) = Z
  & dom (ln*( #Z n)) = dom f2 & (for x st x in Z holds f2.x = n/x ) &
  f2|A is continuous implies
  integral(f2,A) = (ln*( #Z n)).(upper_bound A)-(ln*( #Z n)).(lower_bound A)
proof
   assume
A1:A c= Z & (for x st x in Z holds x > 0) & dom (ln*( #Z n)) = Z
  & dom (ln*( #Z n)) = dom f2 & (for x st x in Z holds f2.x = n/x) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:ln*( #Z n) is_differentiable_on Z by A1,FDIFF_6:9;
A4:for x st x in dom ((ln*( #Z n))`|Z) holds
   ((ln*( #Z n))`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((ln*( #Z n))`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     ((ln*( #Z n))`|Z).x = n/x by A1,FDIFF_6:9
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((ln*( #Z n))`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((ln*( #Z n))`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x=-1/x
theorem
  not 0 in Z & A c= Z & dom (ln*((id Z)^)) = Z
  & dom (ln*((id Z)^)) = dom f2 & (for x st x in Z holds f2.x = -1/x) &
  f2|A is continuous implies
  integral(f2,A) =
    (ln*((id Z)^)).(upper_bound A)-(ln*((id Z)^)).(lower_bound A)
proof
   set f = id Z;
   assume
A1:not 0 in Z & A c= Z & dom (ln*(f^)) = Z
  & dom (ln*(f^)) = dom f2 & (for x st x in Z holds f2.x = -1/x) &
  f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:ln*(f^) is_differentiable_on Z by A1,FDIFF_8:5;
A4:for x st x in dom ((ln*(f^))`|Z) holds
   ((ln*(f^))`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((ln*(f^))`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     ((ln*(f^))`|Z).x = -1/x by A1,FDIFF_8:5
      .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((ln*(f^))`|Z) = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((ln*(f^))`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: irrational function
:: f.x = (a+x) #R (1/2)
theorem
  A c= Z & (for x st x in Z holds f.x=a+x & f.x>0)
  & dom ((2/3)(#)(( #R (3/2))*f)) = Z
  & dom ((2/3)(#)(( #R (3/2))*f)) = dom f2
  & (for x st x in Z holds f2.x = (a+x) #R (1/2)) &
  f2|A is continuous implies
  integral(f2,A) = ((2/3)(#)(( #R (3/2))*f)).(upper_bound A)
            -((2/3)(#)(( #R (3/2))*f)).(lower_bound A)
proof
   assume
 A1:A c= Z & (for x st x in Z holds f.x=a+x & f.x>0)
   & dom ((2/3)(#)(( #R (3/2))*f)) = Z
   & dom ((2/3)(#)(( #R (3/2))*f)) = dom f2
   & (for x st x in Z holds f2.x = (a+x) #R (1/2)) &
   f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(2/3)(#)(( #R (3/2))*f) is_differentiable_on Z by A1,FDIFF_4:28;
A4:for x st x in dom (((2/3)(#)(( #R (3/2))*f))`|Z)
   holds (((2/3)(#)(( #R (3/2))*f))`|Z).x = f2.x
   proof
     let x;
     assume x in dom (((2/3)(#)(( #R (3/2))*f))`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     (((2/3)(#)(( #R (3/2))*f))`|Z).x = (a+x) #R (1/2) by A1,FDIFF_4:28
       .= f2.x by A1,A5;
     hence thesis;
   end;
   dom (((2/3)(#)(( #R (3/2))*f))`|Z)
    = dom f2 by A1,A3,FDIFF_1:def 8;then
   (((2/3)(#)(( #R (3/2))*f))`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = (a-x) #R (1/2)
theorem
  A c= Z & (for x st x in Z holds f.x=a-x & f.x>0)
  & dom ((-2/3)(#)(( #R (3/2))*f)) = Z
  & dom ((-2/3)(#)(( #R (3/2))*f)) = dom f2
  & (for x st x in Z holds f2.x = (a-x) #R (1/2)) &
  f2|A is continuous implies
  integral(f2,A) = ((-2/3)(#)(( #R (3/2))*f)).(upper_bound A)
            -((-2/3)(#)(( #R (3/2))*f)).(lower_bound A)
proof
   assume
 A1:A c= Z & (for x st x in Z holds f.x=a-x & f.x>0)
   & dom ((-2/3)(#)(( #R (3/2))*f)) = Z
   & dom ((-2/3)(#)(( #R (3/2))*f)) = dom f2
   & (for x st x in Z holds f2.x = (a-x) #R (1/2)) &
   f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(-2/3)(#)(( #R (3/2))*f) is_differentiable_on Z by A1,FDIFF_4:29;
A4:for x st x in dom (((-2/3)(#)(( #R (3/2))*f))`|Z)
   holds (((-2/3)(#)(( #R (3/2))*f))`|Z).x = f2.x
   proof
     let x;
     assume x in dom (((-2/3)(#)(( #R (3/2))*f))`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     (((-2/3)(#)(( #R (3/2))*f))`|Z).x = (a-x) #R (1/2) by A1,FDIFF_4:29
  .= f2.x by A1,A5;
     hence thesis;
   end;
   dom (((-2/3)(#)(( #R (3/2))*f))`|Z)
    = dom f2 by A1,A3,FDIFF_1:def 8;then
   (((-2/3)(#)(( #R (3/2))*f))`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = (a+x) #R (-1/2)
theorem
  A c= Z & (for x st x in Z holds f.x=a+x & f.x>0)
  & dom (2(#)(( #R (1/2))*f)) = Z
  & dom (2(#)(( #R (1/2))*f)) = dom f2
  & (for x st x in Z holds f2.x = (a+x) #R (-1/2)) &
  f2|A is continuous implies
  integral(f2,A) = (2(#)(( #R (1/2))*f)).(upper_bound A)
            -(2(#)(( #R (1/2))*f)).(lower_bound A)
proof
   assume
 A1:A c= Z & (for x st x in Z holds f.x=a+x & f.x>0)
   & dom (2(#)(( #R (1/2))*f)) = Z
   & dom (2(#)(( #R (1/2))*f)) = dom f2
   & (for x st x in Z holds f2.x = (a+x) #R (-1/2)) &
   f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:2(#)(( #R (1/2))*f) is_differentiable_on Z by A1,FDIFF_4:30;
A4:for x st x in dom ((2(#)(( #R (1/2))*f))`|Z)
   holds ((2(#)(( #R (1/2))*f))`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((2(#)(( #R (1/2))*f))`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     ((2(#)(( #R (1/2))*f))`|Z).x = (a+x) #R (-1/2) by A1,FDIFF_4:30
       .= f2.x by A1,A5;
     hence thesis;
   end;
   dom ((2(#)(( #R (1/2))*f))`|Z)
    = dom f2 by A1,A3,FDIFF_1:def 8;then
   ((2(#)(( #R (1/2))*f))`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = (a-x) #R (-1/2)
theorem
  A c= Z & (for x st x in Z holds f.x=a-x & f.x>0)
  & dom ((-2)(#)(( #R (1/2))*f)) = Z
  & dom ((-2)(#)(( #R (1/2))*f)) = dom f2
  & (for x st x in Z holds f2.x = (a-x) #R (-1/2)) &
  f2|A is continuous implies
  integral(f2,A) = ((-2)(#)(( #R (1/2))*f)).(upper_bound A)
            -((-2)(#)(( #R (1/2))*f)).(lower_bound A)
proof
   assume
 A1:A c= Z & (for x st x in Z holds f.x=a-x & f.x>0)
   & dom ((-2)(#)(( #R (1/2))*f)) = Z
   & dom ((-2)(#)(( #R (1/2))*f)) = dom f2
   & (for x st x in Z holds f2.x = (a-x) #R (-1/2)) &
   f2|A is continuous;then
A2:f2 is_integrable_on A & f2|A is bounded by INTEGRA5:10,11;
A3:(-2)(#)(( #R (1/2))*f) is_differentiable_on Z by A1,FDIFF_4:31;
A4:for x st x in dom (((-2)(#)(( #R (1/2))*f))`|Z)
   holds (((-2)(#)(( #R (1/2))*f))`|Z).x = f2.x
   proof
     let x;
     assume x in dom (((-2)(#)(( #R (1/2))*f))`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     (((-2)(#)(( #R (1/2))*f))`|Z).x = (a-x) #R (-1/2) by A1,FDIFF_4:31
  .= f2.x by A1,A5;
     hence thesis;
   end;
   dom (((-2)(#)(( #R (1/2))*f))`|Z)
    = dom f2 by A1,A3,FDIFF_1:def 8;then
   (((-2)(#)(( #R (1/2))*f))`|Z) = f2 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

::  f.x=-x*cos.x+sin.x
theorem
  A c= Z & dom ((-id Z)(#)cos+sin) = Z
  & (for x st x in Z holds f.x =x*sin.x) & Z = dom f &
  f|A is continuous implies
  integral(f,A) =
    ((-id Z)(#)cos+sin).(upper_bound A)-((-id Z)(#)cos+sin).(lower_bound A)
proof
assume A1: A c= Z & dom ((-id Z)(#)cos+sin) = Z
  & (for x st x in Z holds f.x =x*sin.x) & Z = dom f &
 f|A is continuous;then
A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:(-id Z)(#)cos+sin is_differentiable_on Z by A1,FDIFF_4:46;
A4:for x st x in dom (((-id Z)(#)cos+sin)`|Z) holds
   (((-id Z)(#)cos+sin)`|Z).x = f.x
   proof
     let x;
     assume x in dom (((-id Z)(#)cos+sin)`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     (((-id Z)(#)cos+sin)`|Z).x =x*sin.x by A1,FDIFF_4:46
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom (((-id Z)(#)cos+sin)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   (((-id Z)(#)cos+sin)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,FDIFF_4:46,INTEGRA5:13;
end;

::  f.x=sin.x/(cos.x)^2
theorem
  A c= Z & dom sec = Z
  & (for x st x in Z holds f.x =sin.x/(cos.x)^2) & Z = dom f &
  f|A is continuous implies
  integral(f,A) = sec.(upper_bound A)-sec.(lower_bound A)
proof
  assume A1: A c= Z & dom sec = Z
  & (for x st x in Z holds f.x =sin.x/(cos.x)^2)
  & Z = dom f & f|A is continuous;then
A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3: sec is_differentiable_on Z by A1,FDIFF_9:4;
A4:for x st x in dom ((sec)`|Z) holds
   ((sec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((sec)`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     ((sec)`|Z).x = sin.x/(cos.x)^2 by A1,FDIFF_9:4
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((sec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((sec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,FDIFF_9:4,INTEGRA5:13;
end;

:: f.x = (-cosec).x
theorem Th31:
  Z c= dom (-cosec) implies -cosec is_differentiable_on Z &
  for x st x in Z holds ( (-cosec)`|Z).x = cos.x/(sin.x)^2
  proof
    assume
A1: Z c= dom (-cosec);
AA: Z c= dom cosec by A1,VALUED_1:8; then
A2: for x st x in Z holds sin.x<>0 by RFUNCT_1:13;
A3: sin is_differentiable_on Z by FDIFF_1:34,SIN_COS:73;then
A4: cosec is_differentiable_on Z by A2,FDIFF_2:22; then
A6: (-1)(#)cosec is_differentiable_on Z by A1,FDIFF_1:28;
    for x st x in Z holds ( (-cosec)`|Z).x= cos.x/(sin.x)^2
    proof
      let x;
      assume A7: x in Z; then
A8:   sin.x<>0 by AA,RFUNCT_1:13;
A9:   sin is_differentiable_in x by A3,A7,FDIFF_1:16;
      ((-cosec)`|Z).x = (-1)*diff(sin^,x) by A1,A4,A7,FDIFF_1:28
        .= (-1)*(- diff(sin,x)/(sin.x)^2) by A8,A9,FDIFF_2:15
        .= cos.x/(sin.x)^2 by SIN_COS:69;
      hence thesis;
    end;
    hence thesis by A6;
  end;

:: f.x=cos.x/(sin.x)^2
theorem
  A c= Z & dom (-cosec) = Z
  & (for x st x in Z holds f.x =cos.x/(sin.x)^2) & Z = dom f &
  f|A is continuous implies
  integral(f,A) = (-cosec).(upper_bound A)-(-cosec).(lower_bound A)
proof
assume A1: A c= Z & dom (-cosec) = Z
  & (for x st x in Z holds f.x =cos.x/(sin.x)^2)
  & Z = dom f & f|A is continuous;then
A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3: -cosec is_differentiable_on Z by A1,Th31;
A4:for x st x in dom ((-cosec)`|Z) holds ((-cosec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((-cosec)`|Z); then
     A5: x in Z by A3,FDIFF_1:def 8; then
     ((-cosec)`|Z).x = cos.x/(sin.x)^2 by A1,Th31
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((-cosec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((-cosec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,Th31,INTEGRA5:13;
end;

