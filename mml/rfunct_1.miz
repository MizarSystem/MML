:: Partial Functions from a Domain to the Set of Real Numbers
::  by Jaros{\l}aw Kotowicz
::
:: Received May 27, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, VALUED_0, FUNCT_1, XCMPLX_0,
      ORDINAL1, ARYTM_1, RELAT_1, ARYTM_3, CARD_1, MEMBERED, PARTFUN1, TARSKI,
      ORDINAL4, VALUED_1, COMPLEX1, XREAL_0, FUNCT_3, XXREAL_2, XXREAL_0,
      REAL_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      XREAL_0, COMPLEX1, MEMBERED, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1,
      FUNCT_3, VALUED_0, VALUED_1, SEQ_2, REAL_1;
 constructors PARTFUN1, FUNCT_3, XXREAL_0, REAL_1, COMPLEX1, VALUED_1,
      PARTFUN2, SEQ_2, SEQ_1, RELSET_1;
 registrations XBOOLE_0, FUNCT_1, RELSET_1, NUMBERS, XREAL_0, MEMBERED,
      VALUED_0, VALUED_1, XCMPLX_0, SEQ_2, XXREAL_0, RELAT_1;
 requirements NUMERALS, SUBSET, BOOLE, ARITHM, REAL;
 definitions TARSKI, PARTFUN1, XBOOLE_0, VALUED_0, VALUED_1, SEQ_2, FUNCT_1;
 theorems TARSKI, FUNCT_1, FUNCT_3, ABSVALUE, PARTFUN1, PARTFUN2, RELSET_1,
      RELAT_1, XBOOLE_0, XBOOLE_1, XCMPLX_0, XCMPLX_1, XREAL_1, COMPLEX1,
      XXREAL_0, VALUED_0, VALUED_1, SEQ_2;
 schemes FUNCT_1;

begin

reserve x,X,Y for set;
reserve C for non empty set;
reserve c for Element of C;
reserve f,f1,f2,f3,g,g1 for complex-valued Function;
reserve r,p for complex number;

Lm1: (-1)"=-1;
 
::
::OPERATIONS ON PARTIAL FUNCTIONS FROM A DOMAIN, TO THE SET OF REAL NUMBERS
::

definition
  let f1,f2 be complex-valued Function;
  canceled 3;
  func f1/f2 -> Function means
  :Def4:
  dom it = dom f1 /\ (dom f2 \ f2"{0}) &
  for c being set st c in dom it holds it.c = f1.c * (f2.c)";
  existence
  proof
    deffunc F(set) = f1.$1 * (f2.$1)";
    ex f being Function st dom f = dom f1 /\ (dom f2 \ f2"{0}) & for x st
    x in dom f1 /\ (dom f2 \ f2"{0}) holds f.x = F(x) from FUNCT_1:sch 3;
    hence thesis;
  end;
  uniqueness
  proof
    let f, g be Function such that
A1: dom f = dom f1 /\ (dom f2 \ f2"{0}) and
A2: for c being set st c in dom f holds f.c = f1.c * (f2.c)" and
A3: dom g = dom f1 /\ (dom f2 \ f2"{0}) and
A4: for c being set st c in dom g holds g.c = f1.c * (f2.c)";
    now
      let x;
      assume
A5:   x in dom f;
      hence f.x = f1.x * (f2.x)" by A2
        .= g.x by A1,A3,A4,A5;
    end;
    hence thesis by A1,A3,FUNCT_1:9;
  end;
end;

registration
  let f1, f2 be complex-valued Function;
  cluster f1/f2 -> complex-valued;
  coherence
  proof
    let x be set;
    set F = f1/f2;
    assume x in dom F;
    then F.x = f1.x * (f2.x)" by Def4;
    hence thesis;
  end;
end;

registration
  let f1, f2 be real-valued Function;
  cluster f1/f2 -> real-valued;
  coherence
  proof
    let x be set;
    set F = f1/f2;
    assume x in dom F;
    then F.x = f1.x * (f2.x)" by Def4;
    hence thesis;
  end;
end;

definition
  let C be set, D be complex-membered set;
  let f1, f2 be PartFunc of C,D;
  redefine func f1/f2 -> PartFunc of C,COMPLEX;
  coherence
  proof
    set F = f1/f2;
A1: rng F c= COMPLEX by VALUED_0:def 1;
    dom F = dom f1 /\ (dom f2 \ f2"{0}) by Def4;
    hence thesis by A1,RELSET_1:11;
  end;
end;

definition
  let C be set, D be real-membered set;
  let f1, f2 be PartFunc of C,D;
  redefine func f1/f2 -> PartFunc of C,REAL;
  coherence
  proof
    set F = f1/f2;
    rng F c= REAL by VALUED_0:def 3;
    then F is PartFunc of dom F, REAL by RELSET_1:11;
    hence thesis by RELSET_1:13;
  end;
end;

definition
  let f be complex-valued Function;
  canceled 3;
  func f^ -> Function means
  :Def8:
  dom it = dom f \ f"{0} & for c being set st c in dom it holds it.c = (f.c)";
  existence
  proof
    deffunc F(set) = (f.$1)";
    ex h being Function st dom h = dom f \ f"{0} & for x st x in dom f \ f
    "{0} holds h.x = F(x) from FUNCT_1:sch 3;
    hence thesis;
  end;
  uniqueness
  proof
    let h, g be Function such that
A1: dom h = dom f \ f"{0} and
A2: for c being set st c in dom h holds h.c = (f.c)" and
A3: dom g = dom f \ f"{0} and
A4: for c being set st c in dom g holds g.c = (f.c)";
    now
      let x;
      assume
A5:   x in dom h;
      hence h.x = (f.x)" by A2
        .= g.x by A1,A3,A4,A5;
    end;
    hence thesis by A1,A3,FUNCT_1:9;
  end;
end;

registration
  let f be complex-valued Function;
  cluster f^ -> complex-valued;
  coherence
  proof
    let x be set;
    set F = f^;
    assume x in dom F;
    then F.x = (f.x)" by Def8;
    hence thesis;
  end;
end;

registration
  let f be real-valued Function;
  cluster f^ -> real-valued;
  coherence
  proof
    let x be set;
    set F = f^;
    assume x in dom F;
    then F.x = (f.x)" by Def8;
    hence thesis;
  end;
end;

definition
  let C be set, D be complex-membered set, f be PartFunc of C,D;
  redefine func f^ -> PartFunc of C,COMPLEX;
  coherence
  proof
    set F = f^;
A1: rng F c= COMPLEX by VALUED_0:def 1;
    dom F = dom f \ f"{0} by Def8;
    hence thesis by A1,RELSET_1:11;
  end;
end;

definition
  let C be set, D be real-membered set, f be PartFunc of C,D;
  redefine func f^ -> PartFunc of C,REAL;
  coherence
  proof
    set F = f^;
    rng F c= REAL by VALUED_0:def 3;
    then F is PartFunc of dom F, REAL by RELSET_1:11;
    hence thesis by RELSET_1:13;
  end;
end;

canceled 10;

theorem Th11:
  dom (g^) c= dom g & dom g /\ (dom g \ g"{0}) = dom g \ g"{0}
proof
  dom (g^) = dom g \ g"{0} by Def8;
  hence dom (g^) c= dom g;
  thus thesis by XBOOLE_1:28;
end;

theorem Th12:
  dom (f1(#)f2) \ (f1(#)f2)"{0} = (dom f1 \ f1"{0}) /\ (dom f2 \ f2"{0})
proof
  thus dom (f1(#)f2) \ (f1(#)f2)"{0} c= (dom f1 \ (f1)"{0}) /\ (dom f2 \ (f2)"
  {0})
  proof
    let x;
    assume
A1: x in dom (f1(#)f2) \ (f1(#)f2)"{0};
    then not x in (f1(#)f2)"{0} by XBOOLE_0:def 5;
    then not (f1(#)f2).x in {0} by A1,FUNCT_1:def 13;
    then (f1(#)f2).x <> 0 by TARSKI:def 1;
    then
A2: f1.x * f2.x <> 0 by VALUED_1:5;
    then f2.x <> 0;
    then not f2.x in {0} by TARSKI:def 1;
    then
A3: not x in (f2)"{0} by FUNCT_1:def 13;
    x in dom (f1(#)f2) by A1;
    then
A4: x in dom f1 /\ dom f2 by VALUED_1:def 4;
    then x in dom f2 by XBOOLE_0:def 4;
    then
A5: x in dom f2 \ (f2)"{0} by A3,XBOOLE_0:def 5;
    f1.x <> 0 by A2;
    then not f1.x in {0} by TARSKI:def 1;
    then
A6: not x in (f1)"{0} by FUNCT_1:def 13;
    x in dom f1 by A4,XBOOLE_0:def 4;
    then x in dom f1 \ (f1)"{0} by A6,XBOOLE_0:def 5;
    hence thesis by A5,XBOOLE_0:def 4;
  end;
  thus (dom f1 \ (f1)"{0}) /\ (dom f2 \ (f2)"{0}) c= dom (f1(#)f2) \ (f1(#)f2)
  "{0}
  proof
    let x;
    assume
A7: x in (dom f1 \ (f1)"{0}) /\ (dom f2 \ (f2)"{0});
    then x in dom f2 \ (f2)"{0} by XBOOLE_0:def 4;
    then not x in (f2)"{0} by XBOOLE_0:def 5;
    then not f2.x in {0} by A7,FUNCT_1:def 13;
    then
A8: f2.x <> 0 by TARSKI:def 1;
A9: x in dom f1 \ (f1)"{0} by A7,XBOOLE_0:def 4;
    then not x in (f1)"{0} by XBOOLE_0:def 5;
    then not f1.x in {0} by A9,FUNCT_1:def 13;
    then f1.x <> 0 by TARSKI:def 1;
    then f1.x * f2.x <>0 by A8;
    then (f1(#)f2).x <> 0 by VALUED_1:5;
    then not (f1(#)f2).x in {0} by TARSKI:def 1;
    then
A10: not x in (f1(#)f2)"{0} by FUNCT_1:def 13;
    x in dom f1 /\ dom f2 by A7,A9,XBOOLE_0:def 4;
    then x in dom (f1(#)f2) by VALUED_1:def 4;
    hence thesis by A10,XBOOLE_0:def 5;
  end;
end;

theorem Th13:
  c in dom (f^) implies f.c <> 0
proof
  assume that
A1: c in dom (f^) and
A2: f.c = 0;
A3: c in dom f \ f"{0} by A1,Def8;
  then
A4: not c in f"{0} by XBOOLE_0:def 5;
  now
    per cases by A4,FUNCT_1:def 13;
    suppose
      not c in dom f;
      hence contradiction by A3;
    end;
    suppose
      not f.c in {0};
      hence contradiction by A2,TARSKI:def 1;
    end;
  end;
  hence contradiction;
end;

theorem Th14:
  (f^)"{0} = {}
proof
  consider x being Element of (f^)"{0};
  assume
A1: (f^)"{0} <> {};
  then
A2: x in dom (f^) by FUNCT_1:def 13;
  then
A3: x in dom f \ f"{0} by Def8;
  then not x in f"{0} by XBOOLE_0:def 5;
  then
A4: not f.x in {0} by A3,FUNCT_1:def 13;
  ((f^) qua Function).x in {0} by A1,FUNCT_1:def 13;
  then (f^).x = 0 by TARSKI:def 1;
  then (f.x)" = 0 by A2,Def8;
  hence contradiction by A4,TARSKI:def 1,XCMPLX_1:203;
end;

theorem Th15:
  (abs(f))"{0} = f"{0} & (-f)"{0} = f"{0}
proof
  now
    let c be set;
    thus c in (abs(f))"{0} implies c in f"{0}
    proof
      assume
A1:   c in (abs(f))"{0};
      then (abs(f)).c in {0} by FUNCT_1:def 13;
      then (abs(f)).c = 0 by TARSKI:def 1;
      then abs(f.c) = 0 by VALUED_1:18;
      then f.c = 0 by COMPLEX1:131;
      then
A2:   f.c in {0} by TARSKI:def 1;
      c in dom (abs(f)) by A1,FUNCT_1:def 13;
      then c in dom f by VALUED_1:def 11;
      hence thesis by A2,FUNCT_1:def 13;
    end;
    assume
A3: c in (f)"{0};
    then f.c in {0} by FUNCT_1:def 13;
    then f.c = 0 by TARSKI:def 1;
    then abs(f.c) = 0 by ABSVALUE:7;
    then (abs(f)).c = 0 by VALUED_1:18;
    then
A4: (abs(f)).c in {0} by TARSKI:def 1;
    c in dom f by A3,FUNCT_1:def 13;
    then c in dom (abs(f)) by VALUED_1:def 11;
    hence c in (abs(f))"{0} by A4,FUNCT_1:def 13;
  end;
  hence (abs(f))"{0} = f"{0} by TARSKI:2;
  now
    let c be set;
    thus c in (-f)"{0} implies c in f"{0}
    proof
      assume
A5:   c in (-f)"{0};
      then (-f).c in {0} by FUNCT_1:def 13;
      then (-f).c = 0 by TARSKI:def 1;
      then --(f.c) = -0 by VALUED_1:8;
      then
A6:   f.c in {0} by TARSKI:def 1;
      c in dom (-f) by A5,FUNCT_1:def 13;
      then c in dom f by VALUED_1:8;
      hence thesis by A6,FUNCT_1:def 13;
    end;
    assume
A7: c in (f)"{0};
    then f.c in {0} by FUNCT_1:def 13;
    then f.c = 0 by TARSKI:def 1;
    then (-f).c = -0 by VALUED_1:8;
    then
A8: (-f).c in {0} by TARSKI:def 1;
    c in dom f by A7,FUNCT_1:def 13;
    then c in dom (-f) by VALUED_1:8;
    hence c in (-f)"{0} by A8,FUNCT_1:def 13;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th16:
  dom (f^^) = dom (f|(dom (f^)))
proof
A1: dom (f^) = dom f \ f"{0} by Def8;
  thus dom (f^^) = dom (f^) \(f^)"{0} by Def8
    .= dom (f^) \ {} by Th14
    .= dom f /\ dom (f^) by A1,XBOOLE_1:28
    .= dom (f|(dom (f^))) by RELAT_1:90;
end;

theorem Th17:
  r<>0 implies (r(#)f)"{0} = f"{0}
proof
  assume
A1: r<>0;
  now
    let c be set;
    thus c in (r(#)f)"{0} implies c in f"{0}
    proof
      assume
A2:   c in (r(#)f)"{0};
      then
A3:   c in dom (r(#)f) by FUNCT_1:def 13;
      (r(#)f).c in {0} by A2,FUNCT_1:def 13;
      then (r(#)f).c = 0 by TARSKI:def 1;
      then r*f.c = 0 by A3,VALUED_1:def 5;
      then f.c = 0 by A1;
      then
A4:   f.c in {0} by TARSKI:def 1;
      c in dom f by A3,VALUED_1:def 5;
      hence thesis by A4,FUNCT_1:def 13;
    end;
    assume
A5: c in (f)"{0};
    then f.c in {0} by FUNCT_1:def 13;
    then f.c = 0 by TARSKI:def 1;
    then
A6: r*f.c = 0;
A7: c in dom f by A5,FUNCT_1:def 13;
    then c in dom (r(#)f) by VALUED_1:def 5;
    then (r(#)f).c = 0 by A6,VALUED_1:def 5;
    then
A8: (r(#)f).c in {0} by TARSKI:def 1;
    c in dom (r(#)f) by A7,VALUED_1:def 5;
    hence c in (r(#)f)"{0} by A8,FUNCT_1:def 13;
  end;
  hence thesis by TARSKI:2;
end;
 
::
:: BASIC PROPERTIES OF OPERATIONS
::

canceled;

theorem
  (f1 + f2) + f3 = f1 + (f2 + f3)
proof
A1: dom (f1 + f2 + f3) = dom (f1 + f2) /\ dom f3 by VALUED_1:def 1
    .= dom f1 /\ dom f2 /\ dom f3 by VALUED_1:def 1
    .= dom f1 /\ (dom f2 /\ dom f3) by XBOOLE_1:16
    .= dom f1 /\ dom (f2 + f3) by VALUED_1:def 1
    .= dom (f1 + (f2 + f3)) by VALUED_1:def 1;
  now
    let c be set;
    assume
A2: c in dom (f1 + f2 + f3);
    then c in dom (f1 + f2) /\ dom f3 by VALUED_1:def 1;
    then
A3: c in dom (f1 + f2) by XBOOLE_0:def 4;
    c in dom f1 /\ dom (f2 + f3) by A1,A2,VALUED_1:def 1;
    then
A4: c in dom (f2 + f3) by XBOOLE_0:def 4;
    thus (f1 + f2 + f3).c = (f1 + f2).c + f3.c by A2,VALUED_1:def 1
      .= f1.c + f2.c + f3.c by A3,VALUED_1:def 1
      .= f1.c + (f2.c + f3.c)
      .= f1.c + (f2 + f3).c by A4,VALUED_1:def 1
      .= (f1 + (f2 + f3)).c by A1,A2,VALUED_1:def 1;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

canceled;

theorem Th21:
  (f1 (#) f2) (#) f3 = f1 (#) (f2 (#) f3)
proof
A1: now
    let c be set;
    assume c in dom (f1(#)f2(#)f3);
    thus (f1 (#) f2 (#) f3).c = (f1 (#) f2).c * f3.c by VALUED_1:5
      .= f1.c * f2.c * f3.c by VALUED_1:5
      .= f1.c * (f2.c * f3.c)
      .= f1.c * (f2 (#) f3).c by VALUED_1:5
      .= (f1 (#) (f2 (#) f3)).c by VALUED_1:5;
  end;
  dom (f1 (#) f2 (#) f3) = dom (f1 (#) f2) /\ dom f3 by VALUED_1:def 4
    .= dom f1 /\ dom f2 /\ dom f3 by VALUED_1:def 4
    .= dom f1 /\ (dom f2 /\ dom f3) by XBOOLE_1:16
    .= dom f1 /\ dom (f2 (#) f3) by VALUED_1:def 4
    .= dom (f1 (#) (f2 (#) f3)) by VALUED_1:def 4;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th22:
  (f1 + f2) (#) f3=f1 (#) f3 + f2 (#) f3
proof
A1: dom ((f1 + f2) (#) f3) = dom (f1 + f2) /\ dom f3 by VALUED_1:def 4
    .= dom f1 /\ dom f2 /\ (dom f3 /\ dom f3) by VALUED_1:def 1
    .= dom f1 /\ dom f2 /\ dom f3 /\ dom f3 by XBOOLE_1:16
    .= dom f1 /\ dom f3 /\ dom f2 /\ dom f3 by XBOOLE_1:16
    .= dom f1 /\ dom f3 /\ (dom f2 /\ dom f3) by XBOOLE_1:16
    .= dom (f1 (#) f3) /\ (dom f2 /\ dom f3) by VALUED_1:def 4
    .= dom (f1 (#) f3) /\ dom (f2 (#) f3) by VALUED_1:def 4
    .= dom (f1 (#) f3 + f2 (#) f3) by VALUED_1:def 1;
  now
    let c be set;
    assume
A2: c in dom ((f1 + f2)(#)f3);
    then c in dom (f1 + f2) /\ dom f3 by VALUED_1:def 4;
    then
A3: c in dom (f1 + f2) by XBOOLE_0:def 4;
    thus ((f1 + f2) (#) f3).c = (f1 + f2).c * f3.c by VALUED_1:5
      .= (f1.c + f2.c) * f3.c by A3,VALUED_1:def 1
      .= f1.c * f3.c + f2.c * f3.c
      .= (f1 (#) f3).c + f2.c* f3.c by VALUED_1:5
      .= (f1 (#) f3).c + (f2 (#) f3).c by VALUED_1:5
      .=((f1 (#) f3) + (f2 (#) f3)).c by A1,A2,VALUED_1:def 1;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  f3 (#) (f1 + f2)=f3(#)f1 + f3(#)f2 by Th22;

theorem Th24:
  r(#)(f1(#)f2)=r(#)f1(#)f2
proof
A1: dom (r(#)(f1 (#) f2)) = dom (f1 (#) f2) by VALUED_1:def 5
    .= dom f1 /\ dom f2 by VALUED_1:def 4
    .= dom (r(#)f1) /\ dom f2 by VALUED_1:def 5
    .= dom (r(#)f1(#)f2) by VALUED_1:def 4;
  now
    let c be set;
    assume
A2: c in dom (r(#)(f1(#)f2));
    then c in dom (r(#)f1) /\ dom f2 by A1,VALUED_1:def 4;
    then
A3: c in dom (r(#)f1) by XBOOLE_0:def 4;
    thus (r(#)(f1(#)f2)).c = r * (f1(#)f2).c by A2,VALUED_1:def 5
      .= r*(f1.c * f2.c) by VALUED_1:5
      .= (r*f1.c) * f2.c
      .= (r(#)f1).c * f2.c by A3,VALUED_1:def 5
      .= (r(#)f1 (#) f2).c by VALUED_1:5;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th25:
  r(#)(f1(#)f2)=f1(#)(r(#)f2)
proof
A1: dom (r(#)(f1 (#) f2)) = dom (f1 (#) f2) by VALUED_1:def 5
    .= dom f1 /\ dom f2 by VALUED_1:def 4
    .= dom f1 /\ dom (r(#)f2) by VALUED_1:def 5
    .= dom (f1(#)(r(#)f2)) by VALUED_1:def 4;
  now
    let c be set;
    assume
A2: c in dom (r(#)(f1(#)f2));
    then c in dom f1 /\ dom (r(#)f2) by A1,VALUED_1:def 4;
    then
A3: c in dom (r(#)f2) by XBOOLE_0:def 4;
    thus (r(#)(f1(#)f2)).c = r * (f1(#)f2).c by A2,VALUED_1:def 5
      .= r * (f1.c * f2.c) by VALUED_1:5
      .= f1.c * (r * f2.c)
      .= f1.c * (r(#)f2).c by A3,VALUED_1:def 5
      .= (f1(#)(r(#)f2)).c by VALUED_1:5;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th26:
  (f1 - f2)(#)f3=f1(#)f3 - f2(#)f3
proof
A1: dom ((f1 - f2) (#) f3) = dom (f1 - f2) /\ dom f3 by VALUED_1:def 4
    .= dom f1 /\ dom f2 /\ (dom f3 /\ dom f3) by VALUED_1:12
    .= dom f1 /\ dom f2 /\ dom f3 /\ dom f3 by XBOOLE_1:16
    .= dom f1 /\ dom f3 /\ dom f2 /\ dom f3 by XBOOLE_1:16
    .= dom f1 /\ dom f3 /\ (dom f2 /\ dom f3) by XBOOLE_1:16
    .= dom (f1 (#) f3) /\ (dom f2 /\ dom f3) by VALUED_1:def 4
    .= dom (f1 (#) f3) /\ dom (f2 (#) f3) by VALUED_1:def 4
    .= dom (f1 (#) f3 - f2 (#) f3) by VALUED_1:12;
  now
    let c be set;
    assume
A2: c in dom ((f1 - f2)(#)f3);
    then c in dom (f1 - f2) /\ dom f3 by VALUED_1:def 4;
    then
A3: c in dom (f1 - f2) by XBOOLE_0:def 4;
    thus ((f1 - f2) (#) f3).c = (f1 - f2).c * f3.c by VALUED_1:5
      .= (f1.c - f2.c) * f3.c by A3,VALUED_1:13
      .= f1.c * f3.c - f2.c * f3.c
      .= (f1 (#) f3).c - f2.c * f3.c by VALUED_1:5
      .= (f1 (#) f3).c - (f2 (#) f3).c by VALUED_1:5
      .=((f1 (#) f3) - (f2 (#) f3)).c by A1,A2,VALUED_1:13;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  f3(#)f1 - f3(#)f2 = f3(#)(f1 - f2) by Th26;

theorem
  r(#)(f1 + f2) = r(#)f1 + r(#)f2
proof
A1: dom (r(#)(f1 + f2)) = dom (f1 + f2) by VALUED_1:def 5
    .= dom f1 /\ dom f2 by VALUED_1:def 1
    .= dom f1 /\ dom (r(#)f2) by VALUED_1:def 5
    .= dom (r(#)f1) /\ dom (r(#)f2) by VALUED_1:def 5
    .= dom (r(#)f1 + r(#)f2) by VALUED_1:def 1;
  now
    let c be set;
    assume
A2: c in dom (r(#)(f1 + f2));
    then
A3: c in dom (f1 + f2) by VALUED_1:def 5;
A4: c in dom (r(#)f1) /\ dom (r(#)f2) by A1,A2,VALUED_1:def 1;
    then
A5: c in dom (r(#)f1) by XBOOLE_0:def 4;
A6: c in dom (r(#)f2) by A4,XBOOLE_0:def 4;
    thus (r(#)(f1 + f2)).c = r * (f1 + f2).c by A2,VALUED_1:def 5
      .= r * (f1.c + f2.c) by A3,VALUED_1:def 1
      .= r * f1.c + r * f2.c
      .= (r(#)f1).c + r * f2.c by A5,VALUED_1:def 5
      .= (r(#)f1).c + (r(#)f2).c by A6,VALUED_1:def 5
      .= (r(#)f1 + r(#)f2).c by A1,A2,VALUED_1:def 1;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  (r*p)(#)f = r(#)(p(#)f)
proof
A1: dom ((r*p) (#) f) = dom f by VALUED_1:def 5
    .= dom (p(#)f) by VALUED_1:def 5
    .= dom (r(#)(p(#)f)) by VALUED_1:def 5;
  now
    let c be set;
    assume
A2: c in dom ((r*p)(#)f);
    then
A3: c in dom (p(#)f) by A1,VALUED_1:def 5;
    thus ((r*p)(#)f).c = r*p * f.c by A2,VALUED_1:def 5
      .= r*(p * f.c)
      .= r * (p(#)f).c by A3,VALUED_1:def 5
      .= (r(#)(p(#)f)).c by A1,A2,VALUED_1:def 5;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  r(#)(f1 - f2) = r(#)f1 - r(#)f2
proof
A1: dom (r(#)(f1 - f2)) = dom (f1 - f2) by VALUED_1:def 5
    .= dom f1 /\ dom f2 by VALUED_1:12
    .= dom f1 /\ dom (r(#)f2) by VALUED_1:def 5
    .= dom (r(#)f1) /\ dom (r(#)f2) by VALUED_1:def 5
    .= dom (r(#)f1 - r(#)f2) by VALUED_1:12;
  now
    let c be set;
    assume
A2: c in dom (r(#)(f1 - f2));
    then
A3: c in dom (f1 - f2) by VALUED_1:def 5;
A4: c in dom (r(#)f1) /\ dom (r(#)f2) by A1,A2,VALUED_1:12;
    then
A5: c in dom (r(#)f1) by XBOOLE_0:def 4;
A6: c in dom (r(#)f2) by A4,XBOOLE_0:def 4;
    thus (r(#)(f1 - f2)).c = r * (f1 - f2).c by A2,VALUED_1:def 5
      .= r * (f1.c - f2.c) by A3,VALUED_1:13
      .= r * f1.c - r * f2.c
      .= (r(#)f1).c - r * f2.c by A5,VALUED_1:def 5
      .= (r(#)f1).c - (r(#)f2).c by A6,VALUED_1:def 5
      .= (r(#)f1 - r(#)f2).c by A1,A2,VALUED_1:13;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  f1-f2 = (-1)(#)(f2-f1)
proof
A1: dom (f1 - f2) = dom f2 /\ dom f1 by VALUED_1:12
    .= dom (f2 - f1) by VALUED_1:12
    .= dom ((-1)(#)(f2 - f1)) by VALUED_1:def 5;
  now
A2: dom (f1 - f2) = dom f2 /\ dom f1 by VALUED_1:12
      .= dom (f2 - f1) by VALUED_1:12;
    let c be set such that
A3: c in dom (f1-f2);
    thus (f1 - f2).c = f1.c - f2.c by A3,VALUED_1:13
      .= (-1)*(f2.c - f1.c)
      .= (-1)*((f2 - f1).c) by A3,A2,VALUED_1:13
      .= ((-1)(#)(f2 - f1)).c by A1,A3,VALUED_1:def 5;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  f1 - (f2 + f3) = f1 - f2 - f3
proof
A1: dom (f1 - (f2 + f3)) = dom f1 /\ dom (f2 + f3) by VALUED_1:12
    .= dom f1 /\ (dom f2 /\ dom f3) by VALUED_1:def 1
    .= dom f1 /\ dom f2 /\ dom f3 by XBOOLE_1:16
    .= dom (f1 - f2) /\ dom f3 by VALUED_1:12
    .= dom (f1 - f2 - f3) by VALUED_1:12;
  now
    let c be set;
    assume
A2: c in dom (f1 - (f2 + f3));
    then c in dom f1 /\ dom (f2 + f3) by VALUED_1:12;
    then
A3: c in dom (f2 + f3) by XBOOLE_0:def 4;
    c in dom (f1 - f2) /\ dom f3 by A1,A2,VALUED_1:12;
    then
A4: c in dom (f1 - f2) by XBOOLE_0:def 4;
    thus (f1 - (f2 + f3)).c = f1.c - (f2 + f3).c by A2,VALUED_1:13
      .= f1.c - (f2.c + f3.c) by A3,VALUED_1:def 1
      .= f1.c - f2.c - f3.c
      .= (f1 - f2).c - f3.c by A4,VALUED_1:13
      .= (f1 - f2 - f3).c by A1,A2,VALUED_1:13;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  1(#)f = f
proof
A1: now
    let c be set;
    assume c in dom (1(#)f);
    hence (1(#)f).c = 1 * f.c by VALUED_1:def 5
      .= f.c;
  end;
  dom (1(#)f) = dom f by VALUED_1:def 5;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  f1 - (f2 - f3) = f1 - f2 + f3
proof
A1: dom (f1 - (f2 - f3)) = dom f1 /\ dom (f2 - f3) by VALUED_1:12
    .= dom f1 /\ (dom f2 /\ dom f3) by VALUED_1:12
    .= dom f1 /\ dom f2 /\ dom f3 by XBOOLE_1:16
    .= dom (f1 - f2) /\ dom f3 by VALUED_1:12
    .= dom (f1 - f2 + f3) by VALUED_1:def 1;
  now
    let c be set;
    assume
A2: c in dom (f1 - (f2 - f3));
    then c in dom f1 /\ dom (f2 - f3) by VALUED_1:12;
    then
A3: c in dom (f2 - f3) by XBOOLE_0:def 4;
    c in dom (f1 - f2) /\ dom f3 by A1,A2,VALUED_1:def 1;
    then
A4: c in dom (f1 - f2) by XBOOLE_0:def 4;
    thus (f1 - (f2 - f3)).c = f1.c - (f2 - f3).c by A2,VALUED_1:13
      .= f1.c - (f2.c - f3.c) by A3,VALUED_1:13
      .= f1.c - f2.c + f3.c
      .= (f1 - f2).c + f3.c by A4,VALUED_1:13
      .= (f1 - f2 + f3).c by A1,A2,VALUED_1:def 1;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  f1 + (f2 - f3) = f1 + f2 - f3
proof
A1: dom (f1 + (f2 - f3)) = dom f1 /\ dom (f2 - f3) by VALUED_1:def 1
    .= dom f1 /\ (dom f2 /\ dom f3) by VALUED_1:12
    .= dom f1 /\ dom f2 /\ dom f3 by XBOOLE_1:16
    .= dom (f1 + f2) /\ dom f3 by VALUED_1:def 1
    .= dom (f1 + f2 - f3) by VALUED_1:12;
  now
    let c be set;
    assume
A2: c in dom (f1 + (f2 - f3));
    then c in dom f1 /\ dom (f2 - f3) by VALUED_1:def 1;
    then
A3: c in dom (f2 - f3) by XBOOLE_0:def 4;
    c in dom (f1 + f2) /\ dom f3 by A1,A2,VALUED_1:12;
    then
A4: c in dom (f1 + f2) by XBOOLE_0:def 4;
    thus (f1 + (f2 - f3)).c = f1.c + (f2 - f3).c by A2,VALUED_1:def 1
      .= f1.c + (f2.c - f3.c) by A3,VALUED_1:13
      .= f1.c + f2.c - f3.c
      .= (f1 + f2).c - f3.c by A4,VALUED_1:def 1
      .= (f1 + f2 - f3).c by A1,A2,VALUED_1:13;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th36:
  abs(f1(#)f2) = abs(f1)(#)abs(f2)
proof
A1: now
    let c be set;
    assume c in dom (abs(f1 (#) f2));
    thus (abs(f1(#)f2)).c = abs((f1(#)f2).c) by VALUED_1:18
      .= abs(f1.c * f2.c) by VALUED_1:5
      .= abs(f1.c) * abs(f2.c) by COMPLEX1:151
      .= ((abs(f1)).c) * abs(f2.c) by VALUED_1:18
      .= ((abs(f1)).c) * (abs(f2)).c by VALUED_1:18
      .= (abs(f1)(#)abs(f2)).c by VALUED_1:5;
  end;
  dom (abs(f1 (#) f2)) = dom (f1 (#) f2) by VALUED_1:def 11
    .= dom f1 /\ dom f2 by VALUED_1:def 4
    .= dom f1 /\ dom (abs(f2)) by VALUED_1:def 11
    .= dom (abs(f1)) /\ dom (abs(f2)) by VALUED_1:def 11
    .= dom (abs(f1)(#)abs(f2)) by VALUED_1:def 4;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  abs(r(#)f) = abs(r)(#)abs(f)
proof
A1: dom (abs(r(#)f)) = dom (r(#)f) by VALUED_1:def 11
    .= dom f by VALUED_1:def 5
    .= dom (abs(f)) by VALUED_1:def 11
    .= dom (abs(r)(#)abs(f)) by VALUED_1:def 5;
  now
    let c be set;
    assume
A2: c in dom (abs(r(#)f));
    then
A3: c in dom (r(#)f) by VALUED_1:def 11;
    thus (abs(r(#)f)).c = abs((r(#)f).c) by VALUED_1:18
      .=abs(r*(f.c)) by A3,VALUED_1:def 5
      .=abs(r)*abs(f.c) by COMPLEX1:151
      .=abs(r)*(abs(f)).c by VALUED_1:18
      .=(abs(r)(#)abs(f)).c by A1,A2,VALUED_1:def 5;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

canceled 4;

theorem Th42:
  f^^ = f|(dom (f^))
proof
A1: dom (f^^) = dom (f|(dom (f^))) by Th16;
  now
    let c be set;
    assume
A2: c in dom (f^^);
    then c in dom f /\ dom (f^) by A1,RELAT_1:90;
    then
A3: c in dom (f^) by XBOOLE_0:def 4;
    thus (f^^).c = ((f^).c)" by A2,Def8
      .= ((f.c)")" by A3,Def8
      .= (f|(dom (f^))).c by A1,A2,FUNCT_1:70;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th43:
  (f1(#)f2)^ = (f1^)(#)(f2^)
proof
A1: dom ((f1(#)f2)^) = dom (f1(#)f2) \ (f1(#)f2)"{0} by Def8
    .= (dom f1 \ f1"{0}) /\ (dom f2 \ (f2)"{0}) by Th12
    .= dom (f1^) /\ (dom f2 \ (f2)"{0}) by Def8
    .= dom (f1^) /\ dom (f2^) by Def8
    .= dom ((f1^) (#) (f2^)) by VALUED_1:def 4;
  now
    let c be set;
    assume
A2: c in dom ((f1(#)f2)^);
    then
A3: c in dom (f1^) /\ dom (f2^) by A1,VALUED_1:def 4;
    then
A4: c in dom (f1^) by XBOOLE_0:def 4;
A5: c in dom (f2^) by A3,XBOOLE_0:def 4;
    thus ((f1(#)f2)^).c = ((f1(#)f2).c)" by A2,Def8
      .= (f1.c * f2.c)" by VALUED_1:5
      .= (f1.c)"* (f2.c)" by XCMPLX_1:205
      .= ((f1^).c)*(f2.c)" by A4,Def8
      .= ((f1^).c) *((f2^).c) by A5,Def8
      .= ((f1^) (#) (f2^)).c by VALUED_1:5;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th44:
  r<>0 implies (r(#)f)^ = r" (#) (f^)
proof
  assume
A1: r<>0;
A2: dom ((r(#)f)^) = dom (r(#)f) \ (r(#)f)"{0} by Def8
    .= dom (r(#)f) \ f"{0} by A1,Th17
    .= dom f \ f"{0} by VALUED_1:def 5
    .= dom (f^) by Def8
    .= dom (r"(#)(f^)) by VALUED_1:def 5;
  now
    let c be set;
    assume
A3: c in dom ((r(#)f)^);
    then
A4: c in dom (r(#)f) \ (r(#)f)"{0} by Def8;
A5: c in dom (f^) by A2,A3,VALUED_1:def 5;
    thus ((r(#)f)^).c = ((r(#)f).c)" by A3,Def8
      .= (r*(f.c))" by A4,VALUED_1:def 5
      .= r"* (f.c)" by XCMPLX_1:205
      .= r"* ((f^).c) by A5,Def8
      .= (r" (#) (f^)).c by A2,A3,VALUED_1:def 5;
  end;
  hence thesis by A2,FUNCT_1:9;
end;

theorem
  (-f)^ = (-1)(#)(f^) by Lm1,Th44;

theorem Th46:
  (abs(f))^ = abs(f^)
proof
A1: dom ((abs(f))^) = dom (abs(f)) \ (abs(f))"{0} by Def8
    .= dom f \ (abs(f))"{0} by VALUED_1:def 11
    .= dom f \ f"{0} by Th15
    .= dom (f^) by Def8
    .= dom (abs((f^))) by VALUED_1:def 11;
  now
    let c be set;
    assume
A2: c in dom ((abs(f))^);
    then
A3: c in dom (f^) by A1,VALUED_1:def 11;
    thus ((abs(f))^).c = ((abs(f)).c)" by A2,Def8
      .= (abs(f.c))" by VALUED_1:18
      .= 1/abs(f.c) by XCMPLX_1:217
      .= abs(1/f.c) by COMPLEX1:166
      .= abs((f.c)") by XCMPLX_1:217
      .= abs(f^.c) by A3,Def8
      .= (abs(f^)).c by VALUED_1:18;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th47:
  f/g = f(#) (g^)
proof
A1: now
    let c be set;
    assume
A2: c in dom (f/g);
    then c in dom f /\ (dom g \ g"{0}) by Def4;
    then c in dom f /\ dom (g^) by Def8;
    then
A3: c in dom (g^) by XBOOLE_0:def 4;
    thus (f/g).c = f.c * (g.c)" by A2,Def4
      .= f.c * (g^).c by A3,Def8
      .= (f (#) (g^)).c by VALUED_1:5;
  end;
  dom (f/g) = dom f /\ (dom g \ g"{0}) by Def4
    .= dom f /\ dom (g^) by Def8
    .= dom (f(#)(g^)) by VALUED_1:def 4;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th48:
  r(#)(g/f) = (r(#)g)/f
proof
  thus r(#)(g/f) = r(#)(g(#)(f^)) by Th47
    .= (r(#)g)(#)(f^) by Th24
    .= (r(#)g)/f by Th47;
end;

theorem
  (f/g)(#)g = (f|dom(g^))
proof
A1: dom ((f/g)(#)g) = dom (f/g) /\ dom g by VALUED_1:def 4
    .= dom f /\ (dom g \ g"{0}) /\ dom g by Def4
    .= dom f /\ ((dom g \ g"{0}) /\ dom g) by XBOOLE_1:16
    .= dom f /\ (dom (g^) /\ dom g) by Def8
    .= dom f /\ dom (g^) by Th11,XBOOLE_1:28
    .= dom (f|(dom (g^))) by RELAT_1:90;
  now
    let c be set;
    assume
A2: c in dom ((f/g)(#)g);
    then c in dom f /\ dom (g^) by A1,RELAT_1:90;
    then
A3: c in dom (g^) by XBOOLE_0:def 4;
    then
A4: g.c <> 0 by Th13;
    thus ((f/g)(#)g).c = ((f/g).c) * g.c by VALUED_1:5
      .= (f(#)(g^)).c * g.c by Th47
      .= (f.c) *((g^).c) * g.c by VALUED_1:5
      .= (f.c)*(g.c)"*g.c by A3,Def8
      .= (f.c)*((g.c)" * (g.c))
      .= (f.c)*1 by A4,XCMPLX_0:def 7
      .= (f|(dom (g^))).c by A1,A2,FUNCT_1:70;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th50:
  (f/g)(#)(f1/g1) = (f(#)f1)/(g(#)g1)
proof
A1: now
    let c be set;
    assume c in dom ((f/g)(#)(f1/g1));
    thus ((f/g)(#)(f1/g1)).c = ((f/g).c)* (f1/g1).c by VALUED_1:5
      .= ((f(#)(g^)).c) * (f1/g1).c by Th47
      .= ((f(#)(g^)).c) * (f1(#)(g1^)).c by Th47
      .= (f.c) * ((g^).c) * (f1(#)(g1^)).c by VALUED_1:5
      .= (f.c) * ((g^).c) * ((f1.c)* (g1^).c) by VALUED_1:5
      .= (f.c) * ((f1.c) * (((g^).c) * (g1^).c))
      .= (f.c) * ((f1.c) * ((g^)(#)(g1^)).c) by VALUED_1:5
      .= (f.c) * (f1.c) * ((g^)(#)(g1^)).c
      .= (f.c) * (f1.c) * ((g(#)g1)^).c by Th43
      .= ((f(#)f1).c) * ((g(#)g1)^).c by VALUED_1:5
      .= ((f(#)f1)(#)((g(#)g1)^)).c by VALUED_1:5
      .= ((f(#)f1)/(g(#)g1)).c by Th47;
  end;
  dom ((f/g)(#)(f1/g1)) = dom (f/g) /\ dom (f1/g1) by VALUED_1:def 4
    .= dom f /\ (dom g \ g"{0}) /\ dom (f1/g1) by Def4
    .= dom f /\ (dom g \ g"{0}) /\ (dom f1 /\ (dom g1 \ g1"{0})) by Def4
    .= dom f /\ ((dom g \ g"{0}) /\ (dom f1 /\ (dom g1 \ g1"{0}))) by
  XBOOLE_1:16
    .= dom f /\ (dom f1 /\ ((dom g \ g"{0}) /\ (dom g1 \ g1"{0}))) by
  XBOOLE_1:16
    .= dom f /\ dom f1 /\ ((dom g \ g"{0}) /\ (dom g1 \ g1"{0})) by XBOOLE_1
  :16
    .= dom (f(#)f1) /\ ((dom g \ g"{0}) /\ (dom g1 \ g1"{0})) by VALUED_1:
  def 4
    .= dom (f(#)f1) /\ (dom (g(#)g1) \ (g(#)g1)"{0}) by Th12
    .= dom ((f(#)f1)/(g(#)g1)) by Def4;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th51:
  (f1/f2)^ = (f2|dom(f2^))/f1
proof
  thus (f1/f2)^ = (f1(#)(f2^))^ by Th47
    .= (f1^)(#)(f2^^) by Th43
    .= (f2|dom(f2^))(#)(f1^) by Th42
    .= (f2|dom(f2^))/f1 by Th47;
end;

theorem Th52:
  g (#) (f1/f2) = (g (#) f1)/f2
proof
  thus g (#) (f1/f2) = g (#) (f1 (#) (f2^)) by Th47
    .= g (#) f1 (#) (f2^) by Th21
    .= (g (#) f1)/f2 by Th47;
end;

theorem
  g/(f1/f2) = (g(#)(f2|dom(f2^)))/f1
proof
  thus g/(f1/f2) = g (#) ((f1/f2)^) by Th47
    .= g (#) ((f2|dom(f2^))/f1) by Th51
    .= (g (#) (f2|dom(f2^)))/f1 by Th52;
end;

theorem
  -f/g = (-f)/g & f/(-g) = -f/g
proof
  thus -f/g = (-f)/g by Th48;
  thus f/(-g) = f (#) ((-g)^) by Th47
    .= f (#) ((-1) (#) (g^)) by Lm1,Th44
    .= -(f (#) (g^)) by Th25
    .= -(f/g) by Th47;
end;

theorem
  f1/f + f2/f = (f1 + f2)/f & f1/f - f2/f = (f1 - f2)/f
proof
  thus f1/f + f2/f = f1(#)(f^) +f2/f by Th47
    .= f1(#)(f^) + f2(#)(f^) by Th47
    .= (f1+f2) (#) (f^) by Th22
    .= (f1+f2)/f by Th47;
  thus f1/f - f2/f = f1(#)(f^) - f2/f by Th47
    .= f1(#)(f^) -f2(#)(f^) by Th47
    .= (f1-f2)(#)(f^) by Th26
    .= (f1-f2)/f by Th47;
end;

theorem Th56:
  f1/f + g1/g = (f1(#)g + g1(#)f)/(f(#)g)
proof
A1: now
    let c be set;
A2: dom (g^) c= dom g by Th11;
    assume
A3: c in dom ((f1/f) + (g1/g));
    then
A4: c in dom (f1/f) /\ dom (g1/g) by VALUED_1:def 1;
    then
A5: c in dom (f1/f) by XBOOLE_0:def 4;
A6: c in dom (g1/g) by A4,XBOOLE_0:def 4;
A7: dom (f^) c= dom f by Th11;
A8: c in dom (f1 (#)(f^)) /\ dom (g1/g) by A4,Th47;
    then c in dom (f1 (#)(f^)) by XBOOLE_0:def 4;
    then
A9: c in dom f1 /\ dom(f^) by VALUED_1:def 4;
    then
A10: c in dom(f^) by XBOOLE_0:def 4;
    then
A11: f.c <> 0 by Th13;
    c in dom (f1 (#)(f^)) /\ dom (g1(#)(g^)) by A8,Th47;
    then c in dom (g1(#)(g^)) by XBOOLE_0:def 4;
    then
A12: c in dom g1 /\ dom(g^) by VALUED_1:def 4;
    then
A13: c in dom(g^) by XBOOLE_0:def 4;
    then
A14: g.c <> 0 by Th13;
    c in dom g1 by A12,XBOOLE_0:def 4;
    then c in dom g1 /\ dom f by A10,A7,XBOOLE_0:def 4;
    then
A15: c in dom (g1(#)f) by VALUED_1:def 4;
    c in dom f1 by A9,XBOOLE_0:def 4;
    then c in dom f1 /\ dom g by A13,A2,XBOOLE_0:def 4;
    then c in dom (f1(#)g) by VALUED_1:def 4;
    then c in dom (f1(#)g) /\ dom (g1(#)f) by A15,XBOOLE_0:def 4;
    then
A16: c in dom (f1(#)g + g1(#)f) by VALUED_1:def 1;
    c in dom (f^) /\ dom (g^) by A10,A13,XBOOLE_0:def 4;
    then c in dom ((f^)(#)(g^)) by VALUED_1:def 4;
    then c in dom ((f(#)g)^) by Th43;
    then c in dom (f1(#)g + g1(#)f) /\ dom ((f(#)g)^) by A16,XBOOLE_0:def
    4;
    then c in dom ((f1(#)g + g1(#)f)(#)((f(#)g)^)) by VALUED_1:def 4;
    then
A17: c in dom ((f1(#)g + g1(#)f)/(f(#)g)) by Th47;
    thus (f1/f + g1/g).c = (f1/f).c + (g1/g).c by A3,VALUED_1:def 1
      .= (f1.c) * (f.c)" + (g1/g).c by A5,Def4
      .= (f1.c) *(1*(f.c)") + (g1.c) * 1 * (g.c)" by A6,Def4
      .= (f1.c) *((g.c) *(g.c)"* (f.c)") + (g1.c) * (1 * (g.c)") by A14,
    XCMPLX_0:def 7
      .= (f1.c) *(g.c *((g.c)"* (f.c)")) + (g1.c) * ((f.c) *(f.c)" * (g.c)")
    by A11,XCMPLX_0:def 7
      .= (f1.c) *((g.c) *((g.c * f.c)")) + (g1.c) * ((f.c) *((f.c)" * (g.c)"
    )) by XCMPLX_1:205
      .= (f1.c) *((g.c) *((f.c* g.c)")) + (g1.c) * ((f.c) *((f.c * g.c)"))
    by XCMPLX_1:205
      .= (f1.c) *((g.c) * ((f(#)g).c)") + (g1.c) * ((f.c) *((f.c * g.c)"))
    by VALUED_1:5
      .= (f1.c) *(g.c) * ((f(#)g).c)" + (g1.c) * ((f.c) * ((f(#) g).c)") by
    VALUED_1:5
      .= (f1(#)g).c * ((f(#)g).c)" + (g1.c) *f.c *((f(#)g).c)" by VALUED_1:5
      .= (f1(#)g).c * ((f(#)g).c)" + (g1(#)f).c *((f(#)g).c)" by VALUED_1:5
      .= ((f1(#)g).c + (g1(#)f).c) *((f(#)g).c)"
      .= (f1(#)g + g1(#)f).c *((f(#)g).c)" by A16,VALUED_1:def 1
      .= ((f1(#)g + g1(#)f)/(f(#)g)).c by A17,Def4;
  end;
  dom ((f1/f) + (g1/g)) = dom (f1/f) /\ dom (g1/g) by VALUED_1:def 1
    .= dom f1 /\ (dom f \ f"{0}) /\ dom (g1/g) by Def4
    .= dom f1 /\ (dom f \ f"{0}) /\ (dom g1 /\ (dom g \ g"{0})) by Def4
    .= dom f1 /\ (dom f /\ (dom f \ f"{0})) /\ (dom g1 /\ (dom g \ g"{0}))
  by Th11
    .= dom f /\ (dom f \ f"{0}) /\ dom f1 /\ (dom g /\ (dom g \ g"{0}) /\
  dom g1) by Th11
    .= dom f /\ (dom f \ f"{0}) /\ (dom f1 /\ (dom g /\ (dom g \ g"{0}) /\
  dom g1)) by XBOOLE_1:16
    .= dom f /\ (dom f \ f"{0}) /\ (dom f1 /\ (dom g /\ (dom g \ g"{0})) /\
  dom g1) by XBOOLE_1:16
    .= dom f /\ (dom f \ f"{0}) /\ (dom f1 /\ dom g /\ (dom g \ g"{0}) /\
  dom g1) by XBOOLE_1:16
    .= dom f /\ (dom f \ f"{0}) /\ (dom (f1(#)g) /\ (dom g \ g"{0}) /\ dom
  g1) by VALUED_1:def 4
    .= dom f /\ (dom f \ f"{0}) /\ (dom (f1(#)g) /\ (dom g1 /\ (dom g \ g"{0
  }))) by XBOOLE_1:16
    .= dom (f1(#)g) /\ ((dom f \ f"{0}) /\ dom f /\ (dom g1 /\ (dom g \ g"{0
  }))) by XBOOLE_1:16
    .= dom (f1(#)g) /\ ((dom f \ f"{0}) /\ (dom f /\ (dom g1 /\ (dom g \ g"{
  0})))) by XBOOLE_1:16
    .= dom (f1(#)g) /\ ((dom f \ f"{0}) /\ (dom g1 /\ dom f /\ (dom g \ g"{0
  }))) by XBOOLE_1:16
    .= dom (f1(#)g) /\ ((dom f \ f"{0}) /\ (dom (g1(#)f) /\ (dom g \ g"{0}))
  ) by VALUED_1:def 4
    .= dom (f1(#)g) /\ (dom (g1(#)f) /\ ((dom f \ f"{0}) /\ (dom g \ g"{0}))
  ) by XBOOLE_1:16
    .= dom (f1(#)g) /\ dom (g1(#) f) /\ ((dom f \ f"{0}) /\ (dom g \ g"{0}))
  by XBOOLE_1:16
    .= dom (f1(#)g + g1(#)f) /\ ((dom f \ f"{0}) /\ (dom g \ g"{0})) by
  VALUED_1:def 1
    .= dom (f1(#)g + g1(#)f) /\ (dom (f(#)g) \ (f(#)g)"{0}) by Th12
    .= dom ((f1(#)g + g1(#)f)/(f(#)g)) by Def4;
  hence thesis by A1,FUNCT_1:9;
end;

theorem
  (f/g)/(f1/g1) = (f(#)(g1|dom(g1^)))/(g(#)f1)
proof
  thus (f/g)/(f1/g1) = (f/g)(#)((f1/g1)^) by Th47
    .= (f/g)(#)(((g1|dom(g1^)))/f1) by Th51
    .= (f(#)(g1|dom(g1^)))/(g(#)f1) by Th50;
end;

theorem
  f1/f - g1/g = (f1(#)g - g1(#)f)/(f(#)g)
proof
  thus f1/f - g1/g = f1/f + ((-1)(#) g1)/g by Th48
    .= (f1(#)g + (-1)(#) g1(#)f)/(f(#)g) by Th56
    .= (f1(#)g - (g1(#)f))/(f(#)g) by Th24;
end;

theorem
  abs(f1/f2) = abs(f1)/abs(f2)
proof
  thus abs(f1/f2) = abs(f1(#)(f2^)) by Th47
    .= abs(f1)(#)abs((f2^)) by Th36
    .= abs(f1)(#)((abs(f2))^) by Th46
    .= abs(f1)/abs(f2) by Th47;
end;

theorem Th60:
  (f1+f2)|X = f1|X + f2|X & (f1+f2)|X = f1|X + f2 & (f1+f2)|X = f1 + f2|X
proof
A1: now
    let c be set;
    assume
A2: c in dom ((f1+f2)|X);
    then
A3: c in dom (f1+f2) /\ X by RELAT_1:90;
    then
A4: c in X by XBOOLE_0:def 4;
A5: c in dom (f1+f2) by A3,XBOOLE_0:def 4;
    then
A6: c in dom f1 /\ dom f2 by VALUED_1:def 1;
    then c in dom f2 by XBOOLE_0:def 4;
    then c in dom f2 /\ X by A4,XBOOLE_0:def 4;
    then
A7: c in dom (f2|X) by RELAT_1:90;
    c in dom f1 by A6,XBOOLE_0:def 4;
    then c in dom f1 /\ X by A4,XBOOLE_0:def 4;
    then
A8: c in dom (f1|X) by RELAT_1:90;
    then c in dom (f1|X) /\ dom (f2|X) by A7,XBOOLE_0:def 4;
    then
A9: c in dom ((f1|X) + (f2|X)) by VALUED_1:def 1;
    thus ((f1+f2)|X).c = (f1+f2).c by A2,FUNCT_1:70
      .= (f1.c) + (f2.c) by A5,VALUED_1:def 1
      .= ((f1|X).c) + (f2.c) by A8,FUNCT_1:70
      .= ((f1|X).c) + ((f2|X).c) by A7,FUNCT_1:70
      .= ((f1|X)+(f2|X)).c by A9,VALUED_1:def 1;
  end;
  dom ((f1+f2)|X) = dom (f1+f2) /\ X by RELAT_1:90
    .= dom f1 /\ dom f2 /\ (X /\ X) by VALUED_1:def 1
    .= dom f1 /\ (dom f2 /\ (X /\ X)) by XBOOLE_1:16
    .= dom f1 /\ (dom f2 /\ X /\ X) by XBOOLE_1:16
    .= dom f1 /\ (X /\ dom (f2|X)) by RELAT_1:90
    .= dom f1 /\ X /\ dom (f2|X) by XBOOLE_1:16
    .= dom (f1|X) /\ dom (f2|X) by RELAT_1:90
    .= dom ((f1|X)+(f2|X)) by VALUED_1:def 1;
  hence (f1+f2)|X = f1|X + f2|X by A1,FUNCT_1:9;
A10: now
    let c be set;
    assume
A11: c in dom ((f1+f2)|X);
    then
A12: c in dom (f1+f2) /\ X by RELAT_1:90;
    then
A13: c in X by XBOOLE_0:def 4;
A14: c in dom (f1+f2) by A12,XBOOLE_0:def 4;
    then
A15: c in dom f1 /\ dom f2 by VALUED_1:def 1;
    then c in dom f1 by XBOOLE_0:def 4;
    then c in dom f1 /\ X by A13,XBOOLE_0:def 4;
    then
A16: c in dom (f1|X) by RELAT_1:90;
    c in dom f2 by A15,XBOOLE_0:def 4;
    then c in dom (f1|X) /\ dom f2 by A16,XBOOLE_0:def 4;
    then
A17: c in dom ((f1|X) + f2) by VALUED_1:def 1;
    thus ((f1+f2)|X).c = (f1+f2).c by A11,FUNCT_1:70
      .= (f1.c) +(f2.c) by A14,VALUED_1:def 1
      .= ((f1|X).c) +(f2.c) by A16,FUNCT_1:70
      .= ((f1|X)+f2).c by A17,VALUED_1:def 1;
  end;
  dom ((f1+f2)|X) = dom (f1+f2) /\ X by RELAT_1:90
    .= dom f1 /\ dom f2 /\ X by VALUED_1:def 1
    .= dom f1 /\ X /\ dom f2 by XBOOLE_1:16
    .= dom (f1|X) /\ dom f2 by RELAT_1:90
    .= dom ((f1|X)+ f2) by VALUED_1:def 1;
  hence (f1+f2)|X = f1|X + f2 by A10,FUNCT_1:9;
A18: now
    let c be set;
    assume
A19: c in dom ((f1+f2)|X);
    then
A20: c in dom (f1+f2) /\ X by RELAT_1:90;
    then
A21: c in X by XBOOLE_0:def 4;
A22: c in dom (f1+f2) by A20,XBOOLE_0:def 4;
    then
A23: c in dom f1 /\ dom f2 by VALUED_1:def 1;
    then c in dom f2 by XBOOLE_0:def 4;
    then c in dom f2 /\ X by A21,XBOOLE_0:def 4;
    then
A24: c in dom (f2|X) by RELAT_1:90;
    c in dom f1 by A23,XBOOLE_0:def 4;
    then c in dom f1 /\ dom (f2|X) by A24,XBOOLE_0:def 4;
    then
A25: c in dom (f1 + (f2|X)) by VALUED_1:def 1;
    thus ((f1+f2)|X).c = (f1+f2).c by A19,FUNCT_1:70
      .= (f1.c) +(f2.c) by A22,VALUED_1:def 1
      .= (f1.c) +((f2|X).c) by A24,FUNCT_1:70
      .= (f1+(f2|X)).c by A25,VALUED_1:def 1;
  end;
  dom ((f1+f2)|X) = dom (f1+f2) /\ X by RELAT_1:90
    .= dom f1 /\ dom f2 /\ X by VALUED_1:def 1
    .= dom f1 /\ (dom f2 /\ X) by XBOOLE_1:16
    .= dom f1 /\ dom (f2|X) by RELAT_1:90
    .= dom (f1 + (f2|X)) by VALUED_1:def 1;
  hence thesis by A18,FUNCT_1:9;
end;

theorem Th61:
  (f1(#)f2)|X = f1|X (#) f2|X & (f1(#)f2)|X = f1|X (#) f2 & (f1(#)
  f2)|X = f1 (#) f2|X
proof
A1: now
    let c be set;
    assume
A2: c in dom ((f1(#)f2)|X);
    then
A3: c in dom (f1(#)f2) /\ X by RELAT_1:90;
    then
A4: c in X by XBOOLE_0:def 4;
    c in dom (f1(#)f2) by A3,XBOOLE_0:def 4;
    then
A5: c in dom f1 /\ dom f2 by VALUED_1:def 4;
    then c in dom f1 by XBOOLE_0:def 4;
    then c in dom f1 /\ X by A4,XBOOLE_0:def 4;
    then
A6: c in dom (f1|X) by RELAT_1:90;
    c in dom f2 by A5,XBOOLE_0:def 4;
    then c in dom f2 /\ X by A4,XBOOLE_0:def 4;
    then
A7: c in dom (f2|X) by RELAT_1:90;
    thus ((f1(#)f2)|X).c = (f1(#)f2).c by A2,FUNCT_1:70
      .= (f1.c) *(f2.c) by VALUED_1:5
      .= ((f1|X).c) *(f2.c) by A6,FUNCT_1:70
      .= ((f1|X).c) *((f2|X).c) by A7,FUNCT_1:70
      .= ((f1|X)(#)(f2|X)).c by VALUED_1:5;
  end;
  dom ((f1(#)f2)|X) = dom (f1(#)f2) /\ X by RELAT_1:90
    .= dom f1 /\ dom f2 /\ (X /\ X) by VALUED_1:def 4
    .= dom f1 /\ (dom f2 /\ (X /\ X)) by XBOOLE_1:16
    .= dom f1 /\ (dom f2 /\ X /\ X) by XBOOLE_1:16
    .= dom f1 /\ (X /\ dom (f2|X)) by RELAT_1:90
    .= dom f1 /\ X /\ dom (f2|X) by XBOOLE_1:16
    .= dom (f1|X) /\ dom (f2|X) by RELAT_1:90
    .= dom ((f1|X)(#)(f2|X)) by VALUED_1:def 4;
  hence (f1(#)f2)|X = f1|X (#) f2|X by A1,FUNCT_1:9;
A8: now
    let c be set;
    assume
A9: c in dom ((f1(#)f2)|X);
    then
A10: c in dom (f1(#)f2) /\ X by RELAT_1:90;
    then c in dom (f1(#)f2) by XBOOLE_0:def 4;
    then c in dom f1 /\ dom f2 by VALUED_1:def 4;
    then
A11: c in dom f1 by XBOOLE_0:def 4;
    c in X by A10,XBOOLE_0:def 4;
    then c in dom f1 /\ X by A11,XBOOLE_0:def 4;
    then
A12: c in dom (f1|X) by RELAT_1:90;
    thus ((f1(#)f2)|X).c = (f1(#)f2).c by A9,FUNCT_1:70
      .= (f1.c) *(f2.c) by VALUED_1:5
      .= ((f1|X).c) *(f2.c) by A12,FUNCT_1:70
      .= ((f1|X)(#)f2).c by VALUED_1:5;
  end;
  dom ((f1(#)f2)|X) = dom (f1(#)f2) /\ X by RELAT_1:90
    .= dom f1 /\ dom f2 /\ X by VALUED_1:def 4
    .= dom f1 /\ X /\ dom f2 by XBOOLE_1:16
    .= dom (f1|X) /\ dom f2 by RELAT_1:90
    .= dom ((f1|X)(#) f2) by VALUED_1:def 4;
  hence (f1(#)f2)|X = f1|X (#) f2 by A8,FUNCT_1:9;
A13: now
    let c be set;
    assume
A14: c in dom ((f1(#)f2)|X);
    then
A15: c in dom (f1(#)f2) /\ X by RELAT_1:90;
    then c in dom (f1(#)f2) by XBOOLE_0:def 4;
    then c in dom f1 /\ dom f2 by VALUED_1:def 4;
    then
A16: c in dom f2 by XBOOLE_0:def 4;
    c in X by A15,XBOOLE_0:def 4;
    then c in dom f2 /\ X by A16,XBOOLE_0:def 4;
    then
A17: c in dom (f2|X) by RELAT_1:90;
    thus ((f1(#)f2)|X).c = (f1(#)f2).c by A14,FUNCT_1:70
      .= (f1.c) *(f2.c) by VALUED_1:5
      .= (f1.c) *((f2|X).c) by A17,FUNCT_1:70
      .= (f1(#)(f2|X)).c by VALUED_1:5;
  end;
  dom ((f1(#)f2)|X) = dom (f1(#)f2) /\ X by RELAT_1:90
    .= dom f1 /\ dom f2 /\ X by VALUED_1:def 4
    .= dom f1 /\ (dom f2 /\ X) by XBOOLE_1:16
    .= dom f1 /\ dom (f2|X) by RELAT_1:90
    .= dom (f1 (#) (f2|X)) by VALUED_1:def 4;
  hence thesis by A13,FUNCT_1:9;
end;

theorem Th62:
  (-f)|X = -(f|X) & (f^)|X = (f|X)^ & (abs(f))|X = abs((f|X))
proof
A1: now
    let c be set;
    assume
A2: c in dom ((-f)|X);
    then
A3: c in dom (-f) /\ X by RELAT_1:90;
    then c in dom (-f) by XBOOLE_0:def 4;
    then
A4: c in dom f by VALUED_1:8;
    c in X by A3,XBOOLE_0:def 4;
    then c in dom f /\ X by A4,XBOOLE_0:def 4;
    then
A5: c in dom (f|X) by RELAT_1:90;
    thus ((-f)|X).c = (-f).c by A2,FUNCT_1:70
      .= -(f.c) by VALUED_1:8
      .= -((f|X).c) by A5,FUNCT_1:70
      .= (-(f|X)).c by VALUED_1:8;
  end;
  dom ((-f)|X) = dom (-f) /\ X by RELAT_1:90
    .= dom f /\ X by VALUED_1:8
    .= dom (f|X) by RELAT_1:90
    .= dom (-(f|X)) by VALUED_1:8;
  hence (-f)|X = -(f|X) by A1,FUNCT_1:9;
A6: dom ((f^)|X) = dom (f^) /\ X by RELAT_1:90
    .= (dom f \ f"{0}) /\ X by Def8
    .= dom f /\ X \ f"{0} /\ X by XBOOLE_1:50
    .= dom (f|X) \ X /\ f"{0} by RELAT_1:90
    .= dom (f|X) \ (f|X)"{0} by FUNCT_1:139
    .= dom ((f|X)^) by Def8;
A7: dom ((f|X)^) c= dom (f|X) by Th11;
  now
    let c be set;
    assume
A8: c in dom ((f^)|X);
    then c in dom (f^) /\ X by RELAT_1:90;
    then
A9: c in dom (f^) by XBOOLE_0:def 4;
    thus ((f^)|X).c = (f^).c by A8,FUNCT_1:70
      .= (f.c)" by A9,Def8
      .= ((f|X).c)" by A7,A6,A8,FUNCT_1:70
      .= ((f|X)^).c by A6,A8,Def8;
  end;
  hence (f^)|X = (f|X)^ by A6,FUNCT_1:9;
A10: dom ((abs(f))|X) = dom (abs(f)) /\ X by RELAT_1:90
    .= dom f /\ X by VALUED_1:def 11
    .= dom (f|X) by RELAT_1:90
    .= dom (abs((f|X))) by VALUED_1:def 11;
  now
    let c be set;
    assume
A11: c in dom ((abs(f))|X);
    then
A12: c in dom (f|X) by A10,VALUED_1:def 11;
    thus ((abs(f))|X).c = (abs(f)).c by A11,FUNCT_1:70
      .= abs(f.c) by VALUED_1:18
      .= abs((f|X).c) by A12,FUNCT_1:70
      .= (abs((f|X))).c by VALUED_1:18;
  end;
  hence thesis by A10,FUNCT_1:9;
end;

theorem
  (f1-f2)|X = f1|X - f2|X & (f1-f2)|X = f1|X - f2 & (f1-f2)|X = f1 - f2| X
proof
  thus (f1-f2)|X = (f1|X)+ (-f2)|X by Th60
    .= (f1|X) - (f2|X) by Th62;
  thus (f1-f2)|X = (f1|X) - f2 by Th60;
  thus (f1-f2)|X = f1+ (-f2)|X by Th60
    .= f1 - (f2|X) by Th62;
end;

theorem
  (f1/f2)|X = f1|X / f2|X & (f1/f2)|X = f1|X / f2 &(f1/f2)|X = f1 / f2|X
proof
  thus (f1/f2)|X = (f1(#)(f2^))|X by Th47
    .= (f1|X) (#) (f2^|X) by Th61
    .= (f1|X) (#) ((f2|X)^) by Th62
    .= (f1|X)/(f2|X) by Th47;
  thus (f1/f2)|X = (f1(#)(f2^))|X by Th47
    .= (f1|X) (#) (f2^) by Th61
    .= (f1|X)/f2 by Th47;
  thus (f1/f2)|X = (f1(#)(f2^))|X by Th47
    .= f1 (#) (f2^)|X by Th61
    .= f1 (#) ((f2|X)^) by Th62
    .= f1/(f2|X) by Th47;
end;

theorem Th65:
  (r(#)f)|X = r(#)(f|X)
proof
A1: now
    let c be set;
    assume
A2: c in dom ((r(#)f)|X);
    then
A3: c in dom (r(#)f) /\ X by RELAT_1:90;
    then
A4: c in X by XBOOLE_0:def 4;
A5: c in dom (r(#)f) by A3,XBOOLE_0:def 4;
    then c in dom f by VALUED_1:def 5;
    then c in dom f /\ X by A4,XBOOLE_0:def 4;
    then
A6: c in dom (f|X) by RELAT_1:90;
    then
A7: c in dom (r(#)(f|X)) by VALUED_1:def 5;
    thus ((r(#)f)|X).c = (r(#)f).c by A2,FUNCT_1:70
      .= r*(f.c) by A5,VALUED_1:def 5
      .= r*(f|X).c by A6,FUNCT_1:70
      .= (r(#)(f|X)).c by A7,VALUED_1:def 5;
  end;
  dom ((r(#)f)|X) = dom (r(#)f) /\ X by RELAT_1:90
    .= dom f /\ X by VALUED_1:def 5
    .= dom (f|X) by RELAT_1:90
    .= dom (r(#)(f|X)) by VALUED_1:def 5;
  hence thesis by A1,FUNCT_1:9;
end;
 
::
:: TOTAL PARTIAL FUNCTIONS FROM A DOMAIN, TO REAL
::

reserve r,r1,r2,p for real number;
reserve f,f1,f2 for PartFunc of C,REAL;

theorem Th66:
  (f1 is total & f2 is total iff f1+f2 is total) & (f1 is total &
  f2 is total iff f1-f2 is total) & (f1 is total & f2 is total iff f1(#)f2 is
  total)
proof
  thus f1 is total & f2 is total iff f1+f2 is total
  proof
    thus f1 is total & f2 is total implies f1+f2 is total
    proof
      assume that
A1:   f1 is total and
A2:   f2 is total;
A3:   dom f2 = C by A2,PARTFUN1:def 4;
      dom f1 = C by A1,PARTFUN1:def 4;
      hence dom (f1+f2) = C /\ C by A3,VALUED_1:def 1
        .= C;
    end;
    assume f1+f2 is total;
    then dom (f1+f2) = C by PARTFUN1:def 4;
    then
A4: dom f1 /\ dom f2 = C by VALUED_1:def 1;
    then
A5: C c= dom f2 by XBOOLE_1:17;
    C c= dom f1 by A4,XBOOLE_1:17;
    hence dom f1 = C & dom f2 = C by A5,XBOOLE_0:def 10;
  end;
  thus f1 is total & f2 is total iff f1-f2 is total
  proof
    thus f1 is total & f2 is total implies f1-f2 is total
    proof
      assume that
A6:   f1 is total and
A7:   f2 is total;
A8:   dom f2 = C by A7,PARTFUN1:def 4;
      dom f1 = C by A6,PARTFUN1:def 4;
      hence dom (f1-f2) = C /\ C by A8,VALUED_1:12
        .= C;
    end;
    assume f1-f2 is total;
    then dom (f1-f2) = C by PARTFUN1:def 4;
    then
A9: dom f1 /\ dom f2 = C by VALUED_1:12;
    then
A10: C c= dom f2 by XBOOLE_1:17;
    C c= dom f1 by A9,XBOOLE_1:17;
    hence dom f1 = C & dom f2 = C by A10,XBOOLE_0:def 10;
  end;
  thus f1 is total & f2 is total implies f1(#)f2 is total
  proof
    assume that
A11: f1 is total and
A12: f2 is total;
A13: dom f2 = C by A12,PARTFUN1:def 4;
    dom f1 = C by A11,PARTFUN1:def 4;
    hence dom (f1(#)f2) = C /\ C by A13,VALUED_1:def 4
      .= C;
  end;
  assume f1(#)f2 is total;
  then dom (f1(#)f2) = C by PARTFUN1:def 4;
  then
A14: dom f1 /\ dom f2 = C by VALUED_1:def 4;
  then
A15: C c= dom f2 by XBOOLE_1:17;
  C c= dom f1 by A14,XBOOLE_1:17;
  hence dom f1 = C & dom f2 = C by A15,XBOOLE_0:def 10;
end;

theorem Th67:
  f is total iff r(#)f is total
proof
  thus f is total implies r(#)f is total
  proof
    assume f is total;
    then dom f = C by PARTFUN1:def 4;
    hence dom (r(#)f) = C by VALUED_1:def 5;
  end;
  assume r(#)f is total;
  then dom (r(#)f) = C by PARTFUN1:def 4;
  hence dom f = C by VALUED_1:def 5;
end;

theorem
  f is total iff -f is total by Th67;

theorem
  f is total iff abs(f) is total
proof
  thus f is total implies abs(f) is total
  proof
    assume f is total;
    then dom f = C by PARTFUN1:def 4;
    hence dom (abs(f)) = C by VALUED_1:def 11;
  end;
  assume abs(f) is total;
  then dom (abs(f)) = C by PARTFUN1:def 4;
  hence dom f = C by VALUED_1:def 11;
end;

theorem Th70:
  f^ is total iff f"{0} = {} & f is total
proof
  thus f^ is total implies f"{0} = {} & f is total
  proof
    assume f^ is total;
    then
A1: dom (f^) = C by PARTFUN1:def 4;
    f"{0} c= C;
    then f"{0} c= dom f \ f"{0} by A1,Def8;
    hence f"{0} = {} by XBOOLE_1:38;
    then C = dom f \ {} by A1,Def8;
    hence dom f = C;
  end;
  assume that
A2: f"{0} = {} and
A3: f is total;
  thus dom (f^) = dom f \ f"{0} by Def8
    .= C by A2,A3,PARTFUN1:def 4;
end;

theorem Th71:
  f1 is total & f2"{0} = {} & f2 is total iff f1/f2 is total
proof
  thus f1 is total & f2"{0} = {} & f2 is total implies f1/f2 is total
  proof
    assume that
A1: f1 is total and
A2: f2"{0} = {} and
A3: f2 is total;
    f2^ is total by A2,A3,Th70;
    then f1(#)(f2^) is total by A1,Th66;
    hence thesis by Th47;
  end;
  assume f1/f2 is total;
  then
A4: f1(#)(f2^) is total by Th47;
  hence f1 is total by Th66;
  f2^ is total by A4,Th66;
  hence thesis by Th70;
end;

theorem
  f1 is total & f2 is total implies (f1+f2).c = f1.c + f2.c & (f1-f2).c
  = f1.c - f2.c & (f1(#)f2).c = f1.c * f2.c
proof
  assume that
A1: f1 is total and
A2: f2 is total;
  f1+f2 is total by A1,A2,Th66;
  then dom (f1+f2) = C by PARTFUN1:def 4;
  hence (f1+f2).c = f1.c + f2.c by VALUED_1:def 1;
  f1-f2 is total by A1,A2,Th66;
  then dom (f1-f2) = C by PARTFUN1:def 4;
  hence (f1-f2).c = f1.c - f2.c by VALUED_1:13;
  thus thesis by VALUED_1:5;
end;

theorem
  f is total implies (r(#)f).c = r * (f.c)
proof
  assume f is total;
  then r(#)f is total by Th67;
  then dom (r(#)f) = C by PARTFUN1:def 4;
  hence thesis by VALUED_1:def 5;
end;

theorem
  f is total implies (-f).c = - f.c & (abs(f)).c = abs( f.c ) by
  VALUED_1:8,18;

theorem
  f^ is total implies (f^).c = (f.c)"
proof
  assume f^ is total;
  then dom (f^) = C by PARTFUN1:def 4;
  hence thesis by Def8;
end;

theorem
  f1 is total & f2^ is total implies (f1/f2).c = f1.c *(f2.c)"
proof
  assume that
A1: f1 is total and
A2: f2^ is total;
A3: f2 is total by A2,Th70;
  f2"{0} = {} by A2,Th70;
  then f1/f2 is total by A1,A3,Th71;
  then dom (f1/f2) = C by PARTFUN1:def 4;
  hence thesis by Def4;
end;
 
::
:: CHARCTERISTIC FUNCTION OF A SUBSET OF A DOMAIN
::

definition
  let X,C be set;
  redefine func chi(X,C) -> PartFunc of C,REAL;
  coherence
  proof
    now
      let x;
      assume x in rng chi(X,C);
      then x = 0 or x = 1 by TARSKI:def 2;
      hence x in REAL;
    end;
    then
A1: rng chi(X,C) c= REAL by TARSKI:def 3;
    dom chi(X,C) =C by FUNCT_3:def 3;
    hence thesis by A1,RELSET_1:11;
  end;
end;

registration
  let X,C be set;
  cluster chi(X,C) -> total PartFunc of C,REAL;
  coherence
  proof
    dom chi(X,C) =C by FUNCT_3:def 3;
    hence thesis by PARTFUN1:def 4;
  end;
end;

theorem Th77:
  f= chi(X,C) iff ( dom f = C & for c holds (c in X implies f.c =
  1) & (not c in X implies f.c = 0))
proof
  thus f= chi(X,C) implies (dom f = C & for c holds (c in X implies f.c = 1) &
  (not c in X implies f.c = 0)) by FUNCT_3:def 3;
  assume that
A1: dom f = C and
A2: for c holds (c in X implies f.c = 1) & (not c in X implies f.c = 0);
  for x st x in C holds (x in X implies (f qua Function).x = 1) & (not x
  in X implies (f qua Function).x = 0) by A2;
  hence thesis by A1,FUNCT_3:def 3;
end;

theorem
  chi(X,C) is total;

theorem
  c in X iff chi(X,C).c = 1 by Th77;

theorem
  not c in X iff chi(X,C).c = 0 by Th77;

theorem
  c in C \ X iff chi(X,C).c = 0
proof
  thus c in C \ X implies chi(X,C).c = 0
  proof
    assume c in C \ X;
    then not c in X by XBOOLE_0:def 5;
    hence thesis by Th77;
  end;
  assume chi(X,C).c = 0;
  then not c in X by Th77;
  hence thesis by XBOOLE_0:def 5;
end;

canceled;

theorem
  chi(C,C).c = 1 by Th77;

theorem Th84:
  chi(X,C).c <> 1 iff chi(X,C).c = 0
proof
  thus chi(X,C).c <> 1 implies chi(X,C).c = 0
  proof
    c in C;
    then c in dom chi(X,C) by Th77;
    then
A1: chi(X,C).c in rng chi(X,C) by FUNCT_1:def 5;
A2: rng chi(X,C) c= {0,1} by FUNCT_3:48;
    assume chi(X,C).c <> 1;
    hence thesis by A1,A2,TARSKI:def 2;
  end;
  assume that
A3: chi(X,C).c = 0 and
A4: chi(X,C).c = 1;
  thus contradiction by A3,A4;
end;

theorem
  X misses Y implies chi(X,C) + chi(Y,C) = chi(X \/ Y,C)
proof
  assume
A1: X /\ Y = {};
A2: now
    let c such that
A3: c in dom (chi(X,C) + chi(Y,C));
    now
      per cases;
      suppose
A4:     c in X;
        then not c in Y by A1,XBOOLE_0:def 4;
        then
A5:     chi(Y,C).c = 0 by Th77;
A6:     c in X \/ Y by A4,XBOOLE_0:def 3;
        chi(X,C).c = 1 by A4,Th77;
        hence chi(X,C).c + chi(Y,C).c = chi(X \/ Y,C).c by A5,A6,Th77;
      end;
      suppose
A7:     not c in X;
        then
A8:     chi(X,C).c = 0 by Th77;
        now
          per cases;
          suppose
A9:         c in Y;
            then
A10:        c in X \/ Y by XBOOLE_0:def 3;
            chi(Y,C).c = 1 by A9,Th77;
            hence chi(X,C).c + chi(Y,C).c = chi(X \/ Y,C).c by A8,A10,
            Th77;
          end;
          suppose
A11:        not c in Y;
            then
A12:        not c in X \/ Y by A7,XBOOLE_0:def 3;
            chi(Y,C).c = 0 by A11,Th77;
            hence chi(X,C).c + chi(Y,C).c = chi(X \/ Y,C).c by A8,A12,
            Th77;
          end;
        end;
        hence chi(X,C).c + chi(Y,C).c = chi(X \/ Y,C).c;
      end;
    end;
    hence (chi(X,C) + chi(Y,C)).c = chi(X \/ Y,C).c by A3,VALUED_1:def 1;
  end;
  dom (chi(X,C) + chi(Y,C)) = dom chi(X,C) /\ dom chi(Y,C) by VALUED_1:def
  1
    .= C /\ dom chi(Y,C) by Th77
    .= C /\ C by Th77
    .= dom chi(X \/ Y,C) by Th77;
  hence thesis by A2,PARTFUN1:34;
end;

theorem
  chi(X,C) (#) chi(Y,C) = chi(X /\ Y,C)
proof
A1: now
    let c such that
    c in dom (chi(X,C) (#) chi(Y,C));
    now
      per cases;
      suppose
A2:     chi(X,C).c * chi(Y,C).c = 0;
        now
          per cases by A2;
          suppose
            chi(X,C).c = 0;
            then not c in X by Th77;
            then not c in X /\ Y by XBOOLE_0:def 4;
            hence chi(X,C).c * chi(Y,C).c = chi(X /\ Y,C).c by A2,Th77;
          end;
          suppose
            chi(Y,C).c = 0;
            then not c in Y by Th77;
            then not c in X /\ Y by XBOOLE_0:def 4;
            hence chi(X,C).c * chi(Y,C).c = chi(X /\ Y,C).c by A2,Th77;
          end;
        end;
        hence chi(X,C).c * chi(Y,C).c = chi(X /\ Y,C).c;
      end;
      suppose
A3:     chi(X,C).c * chi(Y,C).c <> 0;
        then
A4:     chi(Y,C).c <> 0;
        then
A5:     chi(Y,C).c = 1 by Th84;
A6:     c in Y by A4,Th77;
A7:     chi(X,C).c <> 0 by A3;
        then c in X by Th77;
        then
A8:     c in X /\ Y by A6,XBOOLE_0:def 4;
        chi(X,C).c = 1 by A7,Th84;
        hence chi(X,C).c * chi(Y,C).c = chi(X /\ Y,C).c by A5,A8,Th77;
      end;
    end;
    hence (chi(X,C)(#)chi(Y,C)).c = chi(X /\ Y,C).c by VALUED_1:5;
  end;
  dom (chi(X,C) (#) chi(Y,C)) = dom chi(X,C) /\ dom chi(Y,C) by VALUED_1:
  def 4
    .= C /\ dom chi(Y,C) by Th77
    .= C /\ C by Th77
    .= dom chi(X /\ Y,C) by Th77;
  hence thesis by A1,PARTFUN1:34;
end;
 
::
:: BOUNDED AND CONSTANT PARTIAL FUNCTIONS FROM A DOMAIN, TO REAL
::

reserve f for real-valued Function;

theorem Th87:
  f|Y is bounded_above iff ex r st for c being set st c in Y /\
  dom f holds f.c <= r
proof
  thus f|Y is bounded_above implies ex r st for c being set st c in Y /\ dom f
  holds f.c <= r
  proof
    given r being real number such that
A1: for p being set st p in dom(f|Y) holds (f|Y).p<r;
    take r;
    let c be set;
    assume c in Y /\ dom f;
    then
A2: c in dom(f|Y) by RELAT_1:90;
    then (f|Y).c < r by A1;
    hence thesis by A2,FUNCT_1:70;
  end;
  given r such that
A3: for c being set st c in Y /\ dom f holds f.c <= r;
  reconsider r1 = r+1 as real number;
  take r1;
  let p be set;
  assume
A4: p in dom(f|Y);
  then p in Y /\ dom f by RELAT_1:90;
  then f.p <= r by A3;
  then
A5: (f|Y).p <= r by A4,FUNCT_1:70;
  r < r1 by XREAL_1:31;
  hence thesis by A5,XXREAL_0:2;
end;

theorem Th88:
  f|Y is bounded_below iff ex r st for c being set st c in Y /\
  dom f holds r <= f.c
proof
  thus f|Y is bounded_below implies ex r st for c being set st c in Y /\ dom f
  holds r <= f.c
  proof
    given r being real number such that
A1: for p being set st p in dom(f|Y) holds r < (f|Y).p;
    take r;
    let c be set;
    assume c in Y /\ dom f;
    then
A2: c in dom(f|Y) by RELAT_1:90;
    then r < (f|Y).c by A1;
    hence thesis by A2,FUNCT_1:70;
  end;
  given r such that
A3: for c being set st c in Y /\ dom f holds r <= f.c;
  reconsider r1 = r-1 as real number;
  take r1;
  let p be set;
  assume
A4: p in dom(f|Y);
  then p in Y /\ dom f by RELAT_1:90;
  then r <= f.p by A3;
  then
A5: r <= (f|Y).p by A4,FUNCT_1:70;
  r1 < r by XREAL_1:46;
  hence thesis by A5,XXREAL_0:2;
end;

theorem Th89:
  f is bounded iff ex r st for c being set st c in dom f holds abs (f.c)<=r
proof
  thus f is bounded implies ex r st for c being set st c in dom f holds abs(f.
  c)<=r
  proof
    assume
A1: f is bounded;
    then consider r1 such that
A2: for c being set st c in dom f holds f.c < r1 by SEQ_2:def 1;
    consider r2 such that
A3: for c being set st c in dom f holds r2 < f.c by A1,SEQ_2:def 2;
    take g=abs(r1)+abs(r2);
    let c be set such that
A4: c in dom f;
A5: 0 <= abs(r2) by COMPLEX1:132;
    r1 <= abs(r1) by ABSVALUE:11;
    then f.c <= abs(r1) by A2,A4,XXREAL_0:2;
    then
A6: f.c + 0 <= abs(r1)+abs(r2) by A5,XREAL_1:9;
A7: 0 <= abs(r1) by COMPLEX1:132;
    -abs(r2) <= r2 by ABSVALUE:11;
    then -abs(r2) <= f.c by A3,A4,XXREAL_0:2;
    then
A8: -abs(r1)+-abs(r2) <= 0 + f.c by A7,XREAL_1:9;
    -abs(r1)+-abs(r2) = -g;
    hence thesis by A6,A8,ABSVALUE:12;
  end;
  given r such that
A9: for c being set st c in dom f holds abs(f.c) <= r;
  thus f is bounded_above
  proof
    take r+1;
    let c be set;
    assume c in dom f;
    then
A10: abs(f.c) < r+1 by A9,XREAL_1:41;
    f.c <= abs(f.c) by ABSVALUE:11;
    hence thesis by A10,XXREAL_0:2;
  end;
  take -(r+1);
  let c be set;
  assume c in dom f;
  then abs(f.c) < r+1 by A9,XREAL_1:41;
  then
A11: -(r+1) < -abs(f.c) by XREAL_1:26;
  -abs(f.c) <= f.c by ABSVALUE:11;
  hence thesis by A11,XXREAL_0:2;
end;

theorem
  f|Y is bounded iff ex r st for c being set st c in Y /\ dom f holds
  abs(f.c)<=r
proof
  thus f|Y is bounded implies ex r st for c being set st c in Y /\ dom f holds
  abs(f.c)<=r
  proof
    assume
A1: f|Y is bounded;
    then consider r1 such that
A2: for c being set st c in Y /\ dom f holds f.c <= r1 by Th87;
    consider r2 such that
A3: for c being set st c in Y /\ dom f holds r2 <= f.c by A1,Th88;
    take g=abs(r1)+abs(r2);
A4: r1 <= abs(r1) by ABSVALUE:11;
    let c be set such that
A5: c in Y /\ dom f;
    f.c <= r1 by A2,A5;
    then
A6: f.c <= abs(r1) by A4,XXREAL_0:2;
A7: -abs(r2) <= r2 by ABSVALUE:11;
    r2 <= f.c by A3,A5;
    then
A8: -abs(r2) <= f.c by A7,XXREAL_0:2;
    0 <= abs(r1) by COMPLEX1:132;
    then
A9: -abs(r1)+-abs(r2) <= 0 + f.c by A8,XREAL_1:9;
    0 <= abs(r2) by COMPLEX1:132;
    then
A10: f.c + 0 <= abs(r1)+abs(r2) by A6,XREAL_1:9;
    -abs(r1)+-abs(r2) = -g;
    hence thesis by A10,A9,ABSVALUE:12;
  end;
  given r such that
A11: for c being set st c in Y /\ dom f holds abs(f.c) <= r;
  now
    let c be set;
    assume c in Y /\ dom f;
    then abs(f.c) <= r by A11;
    then
A12: -r <= -abs(f.c) by XREAL_1:26;
    -abs(f.c) <= f.c by ABSVALUE:11;
    hence -r <= f.c by A12,XXREAL_0:2;
  end;
  then
A13: f|Y is bounded_below by Th88;
  now
    let c be set;
    assume c in Y /\ dom f;
    then
A14: abs(f.c) <= r by A11;
    f.c <= abs(f.c) by ABSVALUE:11;
    hence f.c <= r by A14,XXREAL_0:2;
  end;
  then f|Y is bounded_above by Th87;
  hence thesis by A13;
end;

theorem
  (Y c= X & f|X is bounded_above implies f|Y is bounded_above) & (Y c= X
& f|X is bounded_below implies f|Y is bounded_below) & (Y c= X & f|X is bounded
  implies f|Y is bounded)
proof
  thus
A1: Y c= X & f|X is bounded_above implies f|Y is bounded_above
  proof
    assume that
A2: Y c= X and
A3: f|X is bounded_above;
    consider r such that
A4: for c being set st c in X /\ dom f holds f.c <= r by A3,Th87;
    now
      take r;
      let c be set;
      assume
A5:   c in Y /\ dom f;
      then
A6:   c in dom f by XBOOLE_0:def 4;
      c in Y by A5,XBOOLE_0:def 4;
      then c in X /\ dom f by A2,A6,XBOOLE_0:def 4;
      hence f.c <= r by A4;
    end;
    hence thesis by Th87;
  end;
  thus
A7: Y c= X & f|X is bounded_below implies f|Y is bounded_below
  proof
    assume that
A8: Y c= X and
A9: f|X is bounded_below;
    consider r such that
A10: for c being set st c in X /\ dom f holds r <= f.c by A9,Th88;
    now
      take r;
      let c be set;
      assume
A11:  c in Y /\ dom f;
      then
A12:  c in dom f by XBOOLE_0:def 4;
      c in Y by A11,XBOOLE_0:def 4;
      then c in X /\ dom f by A8,A12,XBOOLE_0:def 4;
      hence r <= f.c by A10;
    end;
    hence thesis by Th88;
  end;
  assume that
A13: Y c= X and
A14: f|X is bounded;
  thus thesis by A1,A7,A13,A14;
end;

theorem
  f|X is bounded_above & f|Y is bounded_below implies f|(X /\ Y) is bounded
proof
  assume that
A1: f|X is bounded_above and
A2: f|Y is bounded_below;
  consider r1 such that
A3: for c being set st c in X /\ dom f holds f.c <= r1 by A1,Th87;
  now
    let c be set;
    assume
A4: c in X /\ Y /\ dom f;
    then c in X /\ Y by XBOOLE_0:def 4;
    then
A5: c in X by XBOOLE_0:def 4;
    c in dom f by A4,XBOOLE_0:def 4;
    then c in X /\ dom f by A5,XBOOLE_0:def 4;
    hence f.c <= r1 by A3;
  end;
  hence f|(X /\ Y) is bounded_above by Th87;
  consider r2 such that
A6: for c being set st c in Y /\ dom f holds r2 <= f.c by A2,Th88;
  now
    let c be set;
    assume
A7: c in X /\ Y /\ dom f;
    then c in X /\ Y by XBOOLE_0:def 4;
    then
A8: c in Y by XBOOLE_0:def 4;
    c in dom f by A7,XBOOLE_0:def 4;
    then c in Y /\ dom f by A8,XBOOLE_0:def 4;
    hence r2 <= f.c by A6;
  end;
  hence thesis by Th88;
end;

registration
  cluster constant -> bounded (real-valued Function);
  coherence
  proof
    let f be real-valued Function;
    per cases;
    suppose
A1:   f is empty;
      reconsider z=0 as real number;
      assume f is constant;
      thus f is bounded_above
      proof
        take z;
        let x be set;
        thus thesis by A1;
      end;
      take z;
      let x be set;
      thus thesis by A1;
    end;
    suppose
A2:   f is not empty;
      assume f is constant;
      then consider r such that
A3:   for c being set st c in dom f holds f.c = r by A2,VALUED_0:15;
      thus f is bounded_above
      proof
        take r+1;
        let y be set;
        assume y in dom f;
        then f.y = r by A3;
        hence thesis by XREAL_1:41;
      end;
      take r-1;
      let y be set;
      assume y in dom f;
      then f.y = r by A3;
      hence thesis by XREAL_1:233;
    end;
  end;
end;

theorem
  X misses dom f implies f|X is bounded
proof
  assume X /\ dom f = {};
  then dom(f|X) = {} by RELAT_1:90;
  then f|X = {};
  hence thesis;
end;

theorem
  (0(#)f)|Y is bounded
proof
  reconsider p1 = 0 as Real;
  set r = 0;
  now
    take p=p1;
    let c be set;
    assume c in Y /\ dom (r(#)f);
    then
A1: c in dom (r(#)f) by XBOOLE_0:def 4;
    r*f.c <= 0;
    hence (r(#)f).c <= p by A1,VALUED_1:def 5;
  end;
  hence (r(#)f)|Y is bounded_above by Th87;
  now
    take p=p1;
    let c be set;
    assume c in Y /\ dom (r(#)f);
    then
A2: c in dom (r(#)f) by XBOOLE_0:def 4;
    0 <= r*f.c;
    hence p <= (r(#)f).c by A2,VALUED_1:def 5;
  end;
  hence thesis by Th88;
end;

registration
  let f be bounded_above (real-valued Function), X be set;
  cluster f|X -> bounded_above (real-valued Function);
  coherence
  proof
    consider r such that
A1: for y being set st y in dom f holds f.y<r by SEQ_2:def 1;
    f|X is bounded_above
    proof
      take r;
      let y be set;
      assume
A2:   y in dom(f|X);
      then y in dom f by RELAT_1:86;
      then f.y < r by A1;
      hence thesis by A2,FUNCT_1:70;
    end;
    hence thesis;
  end;
end;

registration
  let f be bounded_below (real-valued Function), X be set;
  cluster f|X -> bounded_below (real-valued Function);
  coherence
  proof
    consider r such that
A1: for y being set st y in dom f holds r < f.y by SEQ_2:def 2;
    f|X is bounded_below
    proof
      take r;
      let y be set;
      assume
A2:   y in dom(f|X);
      then y in dom f by RELAT_1:86;
      then r < f.y by A1;
      hence thesis by A2,FUNCT_1:70;
    end;
    hence thesis;
  end;
end;

registration
  let f be bounded_above (real-valued Function);
  let r be non negative (real number);
  cluster r(#)f -> bounded_above (real-valued Function);
  coherence
  proof
    consider r1 such that
A1: for c being set st c in dom f holds f.c < r1 by SEQ_2:def 1;
    now
      take p = r*abs(r1)+1;
      let c be set;
A2:   r1 <= abs(r1) by ABSVALUE:11;
      assume
A3:   c in dom (r(#)f);
      then c in dom f by VALUED_1:def 5;
      then f.c < abs(r1) by A1,A2,XXREAL_0:2;
      then r * (f.c) <= abs(r1)*r by XREAL_1:66;
      then (r(#)f).c <= abs(r1)*r by A3,VALUED_1:def 5;
      hence (r(#)f).c < p by XREAL_1:41;
    end;
    hence thesis by SEQ_2:def 1;
  end;
end;

registration
  let f be bounded_below (real-valued Function);
  let r be non negative (real number);
  cluster r(#)f -> bounded_below (real-valued Function);
  coherence
  proof
    consider r1 such that
A1: for c being set st c in dom f holds r1 < f.c by SEQ_2:def 2;
    now
      take p = r*(-(abs r1))-1;
      let c be set;
A2:   -abs(r1) <= r1 by ABSVALUE:11;
      assume
A3:   c in dom (r(#)f);
      then c in dom f by VALUED_1:def 5;
      then f.c >= -abs(r1) by A1,A2,XXREAL_0:2;
      then r * (f.c) >= (-abs(r1))*r by XREAL_1:66;
      then (r(#)f).c >= (-abs(r1))*r by A3,VALUED_1:def 5;
      hence (r(#)f).c > p by XREAL_1:233;
    end;
    hence thesis by SEQ_2:def 2;
  end;
end;

registration
  let f be bounded_above (real-valued Function);
  let r be non positive (real number);
  cluster r(#)f -> bounded_below (real-valued Function);
  coherence
  proof
    consider r1 such that
A1: for c being set st c in dom f holds f.c < r1 by SEQ_2:def 1;
    now
      take p=r*abs(r1)-1;
      let c be set;
A2:   r1<=abs(r1) by ABSVALUE:11;
      assume
A3:   c in dom (r(#)f);
      then c in dom f by VALUED_1:def 5;
      then f.c <= abs(r1) by A1,A2,XXREAL_0:2;
      then r*abs(r1) <= r * f.c by XREAL_1:67;
      then r*abs(r1) <= (r(#)f).c by A3,VALUED_1:def 5;
      hence p < (r(#)f).c by XREAL_1:233;
    end;
    hence thesis by SEQ_2:def 2;
  end;
end;

registration
  let f be bounded_below (real-valued Function);
  let r be non positive (real number);
  cluster r(#)f -> bounded_above (real-valued Function);
  coherence
  proof
    consider r1 such that
A1: for c being set st c in dom f holds r1 < f.c by SEQ_2:def 2;
    now
      take p=r*(-abs(r1))+1;
      let c be set;
A2:   -abs(r1) <= r1 by ABSVALUE:11;
      assume
A3:   c in dom (r(#)f);
      then c in dom f by VALUED_1:def 5;
      then -abs(r1) <= f.c by A1,A2,XXREAL_0:2;
      then r * f.c <= r*(-abs(r1)) by XREAL_1:67;
      then (r(#)f).c <= r*(-abs(r1)) by A3,VALUED_1:def 5;
      hence (r(#)f).c < p by XREAL_1:41;
    end;
    hence thesis by SEQ_2:def 1;
  end;
end;

theorem Th95:
  (f|Y is bounded_above & 0<=r implies (r(#)f)|Y is bounded_above)
  & (f|Y is bounded_above & r<=0 implies (r(#)f)|Y is bounded_below)
proof
  (r(#)f)|Y = r(#)(f|Y) by Th65;
  hence thesis;
end;

theorem Th96:
  (f|Y is bounded_below & 0<=r implies (r(#)f)|Y is bounded_below)
  & (f|Y is bounded_below & r<=0 implies (r(#)f)|Y is bounded_above)
proof
  (r(#)f)|Y = r(#)(f|Y) by Th65;
  hence thesis;
end;

theorem Th97:
  f|Y is bounded implies (r(#)f)|Y is bounded
proof
  assume
A1: f|Y is bounded;
  per cases;
  suppose
A2: 0 <= r;
    hence (r(#)f)|Y is bounded_above by A1,Th95;
    thus thesis by A1,A2,Th96;
  end;
  suppose
A3: r <= 0;
    hence (r(#)f)|Y is bounded_above by A1,Th96;
    thus thesis by A1,A3,Th95;
  end;
end;

registration
  let f;
  cluster abs f -> bounded_below;
  coherence
  proof
    now
      reconsider p = -1 as real number;
      take p;
      let c be set;
      assume c in dom (abs(f));
      0 <= abs(f.c) by COMPLEX1:132;
      hence p < (abs(f)).c by VALUED_1:18;
    end;
    hence thesis by SEQ_2:def 2;
  end;
end;

theorem
  (abs f)|X is bounded_below;

registration
  let f be bounded (real-valued Function);
  cluster abs f -> bounded (real-valued Function);
  coherence
  proof
    consider r1 such that
A1: for c being set st c in dom f holds abs(f.c) <= r1 by Th89;
    now
      take r1;
      let c be set;
      assume c in dom abs f;
      then c in dom f by VALUED_1:def 11;
      then abs abs(f.c) <= r1 by A1;
      hence abs((abs f).c) <= r1 by VALUED_1:18;
    end;
    hence thesis by Th89;
  end;
end;

registration
  let f be bounded_above (real-valued Function);
  cluster -f -> bounded_below (real-valued Function);
  coherence;
end;

theorem Th99:
  f|Y is bounded implies (abs f)|Y is bounded & (-f)|Y is bounded
proof
  assume
A1: f|Y is bounded;
  (abs f)|Y = abs(f|Y) by Th62;
  hence (abs f)|Y is bounded by A1;
  thus thesis by A1,Th97;
end;

reserve f1,f2 for real-valued Function;

registration
  let f1,f2 be bounded_above (real-valued Function);
  cluster f1+f2 -> bounded_above (real-valued Function);
  coherence
  proof
    consider r2 such that
A1: for c being set st c in dom f2 holds f2.c < r2 by SEQ_2:def 1;
    consider r1 such that
A2: for c being set st c in dom f1 holds f1.c < r1 by SEQ_2:def 1;
    now
      take r=r1+r2;
      let c be set;
      assume
A3:   c in dom (f1+f2);
      then
A4:   c in dom f1 /\ dom f2 by VALUED_1:def 1;
      then c in dom f1 by XBOOLE_0:def 4;
      then
A5:   f1.c < r1 by A2;
      c in dom f2 by A4,XBOOLE_0:def 4;
      then f1.c + f2.c < r by A1,A5,XREAL_1:10;
      hence (f1+f2).c < r by A3,VALUED_1:def 1;
    end;
    hence thesis by SEQ_2:def 1;
  end;
end;

registration
  let f1,f2 be bounded_below (real-valued Function);
  cluster f1+f2 -> bounded_below (real-valued Function);
  coherence
  proof
    consider r2 such that
A1: for c being set st c in dom f2 holds f2.c > r2 by SEQ_2:def 2;
    consider r1 such that
A2: for c being set st c in dom f1 holds f1.c > r1 by SEQ_2:def 2;
    now
      take r=r1+r2;
      let c be set;
      assume
A3:   c in dom (f1+f2);
      then
A4:   c in dom f1 /\ dom f2 by VALUED_1:def 1;
      then c in dom f1 by XBOOLE_0:def 4;
      then
A5:   f1.c > r1 by A2;
      c in dom f2 by A4,XBOOLE_0:def 4;
      then f1.c + f2.c > r by A1,A5,XREAL_1:10;
      hence (f1+f2).c > r by A3,VALUED_1:def 1;
    end;
    hence thesis by SEQ_2:def 2;
  end;
end;

theorem Th100:
  (f1|X is bounded_above & f2|Y is bounded_above implies (f1+f2)|
  (X /\ Y) is bounded_above) & (f1|X is bounded_below & f2|Y is bounded_below
implies (f1+f2)|(X /\ Y) is bounded_below) & (f1|X is bounded & f2|Y is bounded
  implies (f1+f2)|(X /\ Y) is bounded)
proof
  (f1+f2)|(X /\ Y) = f1|(X /\ Y)+f2|(X /\ Y) by Th60
    .= f1|(X /\ Y)+f2|Y|X by RELAT_1:100
    .= f1|X|Y+f2|Y|X by RELAT_1:100;
  hence thesis;
end;

registration
  let f1,f2 be bounded (real-valued Function);
  cluster f1(#)f2 -> bounded (real-valued Function);
  coherence
  proof
    consider r2 such that
A1: for c being set st c in dom f2 holds abs(f2.c) <= r2 by Th89;
    consider r1 such that
A2: for c being set st c in dom f1 holds abs(f1.c) <= r1 by Th89;
    now
      take r=r1*r2;
      let c be set;
A3:   0<=abs(f2.c) by COMPLEX1:132;
      assume c in dom (f1(#)f2);
      then
A4:   c in dom f1 /\ dom f2 by VALUED_1:def 4;
      then c in dom f1 by XBOOLE_0:def 4;
      then
A5:   abs(f1.c) <= r1 by A2;
      c in dom f2 by A4,XBOOLE_0:def 4;
      then
A6:   abs(f2.c) <= r2 by A1;
      0<=abs(f1.c) by COMPLEX1:132;
      then abs(f1.c)*abs(f2.c) <= r by A5,A6,A3,XREAL_1:68;
      then abs(f1.c * f2.c) <= r by COMPLEX1:151;
      hence abs((f1(#)f2).c) <= r by VALUED_1:5;
    end;
    hence thesis by Th89;
  end;
end;

theorem Th101:
  f1|X is bounded & f2|Y is bounded implies (f1(#)f2)|(X /\ Y) is
  bounded & (f1-f2)|(X /\ Y) is bounded
proof
  assume that
A1: f1|X is bounded and
A2: f2|Y is bounded;
  (f1(#)f2)|(X /\ Y) = f1|(X /\ Y)(#)f2|(X /\ Y) by Th61
    .= f1|(X /\ Y)(#)f2|Y|X by RELAT_1:100
    .= f1|X|Y(#)f2|Y|X by RELAT_1:100;
  hence (f1(#)f2)|(X /\ Y) is bounded by A1,A2;
  (-f2)|Y is bounded by A2,Th99;
  hence thesis by A1,Th100;
end;

theorem Th102:
  f|X is bounded_above & f|Y is bounded_above implies f|(X \/ Y)
  is bounded_above
proof
  assume that
A1: f|X is bounded_above and
A2: f|Y is bounded_above;
  consider r1 such that
A3: for c being set st c in X /\ dom f holds f.c <= r1 by A1,Th87;
  consider r2 such that
A4: for c being set st c in Y /\ dom f holds f.c <= r2 by A2,Th87;
  now
    take r = abs(r1) + abs(r2);
    let c be set;
    assume
A5: c in (X \/ Y) /\ dom f;
    then
A6: c in dom f by XBOOLE_0:def 4;
A7: c in X \/ Y by A5,XBOOLE_0:def 4;
    now
      per cases by A7,XBOOLE_0:def 3;
      suppose
        c in X;
        then c in X /\ dom f by A6,XBOOLE_0:def 4;
        then
A8:     f.c <= r1 by A3;
A9:     0 <= abs(r2) by COMPLEX1:132;
        r1 <= abs(r1) by ABSVALUE:11;
        then f.c <= abs(r1) by A8,XXREAL_0:2;
        then f.c + 0 <= r by A9,XREAL_1:9;
        hence f.c <= r;
      end;
      suppose
        c in Y;
        then c in Y /\ dom f by A6,XBOOLE_0:def 4;
        then
A10:    f.c <= r2 by A4;
A11:    0 <= abs(r1) by COMPLEX1:132;
        r2 <= abs(r2) by ABSVALUE:11;
        then f.c <= abs(r2) by A10,XXREAL_0:2;
        then 0 + f.c <= r by A11,XREAL_1:9;
        hence f.c <= r;
      end;
    end;
    hence f.c <= r;
  end;
  hence thesis by Th87;
end;

theorem Th103:
  f|X is bounded_below & f|Y is bounded_below implies f|(X \/ Y)
  is bounded_below
proof
  assume that
A1: f|X is bounded_below and
A2: f|Y is bounded_below;
  consider r1 such that
A3: for c being set st c in X /\ dom f holds r1 <= f.c by A1,Th88;
  consider r2 such that
A4: for c being set st c in Y /\ dom f holds r2 <= f.c by A2,Th88;
  now
    take r = -abs(r1) - abs(r2);
    let c be set;
    assume
A5: c in (X \/ Y) /\ dom f;
    then
A6: c in dom f by XBOOLE_0:def 4;
A7: c in X \/ Y by A5,XBOOLE_0:def 4;
    now
      per cases by A7,XBOOLE_0:def 3;
      suppose
        c in X;
        then c in X /\ dom f by A6,XBOOLE_0:def 4;
        then
A8:     r1 <= f.c by A3;
A9:     0 <= abs(r2) by COMPLEX1:132;
        -abs(r1) <= r1 by ABSVALUE:11;
        then -abs(r1) <= f.c by A8,XXREAL_0:2;
        then r <= f.c - 0 by A9,XREAL_1:15;
        hence r <= f.c;
      end;
      suppose
        c in Y;
        then c in Y /\ dom f by A6,XBOOLE_0:def 4;
        then
A10:    r2 <= f.c by A4;
A11:    0 <= abs(r1) by COMPLEX1:132;
        -abs(r2) <= r2 by ABSVALUE:11;
        then -abs(r2) <= f.c by A10,XXREAL_0:2;
        then -abs(r2) - abs(r1) <= f.c - 0 by A11,XREAL_1:15;
        hence r <= f.c;
      end;
    end;
    hence r <= f.c;
  end;
  hence thesis by Th88;
end;

theorem
  f|X is bounded & f|Y is bounded implies f|(X \/ Y) is bounded
proof
  assume that
A1: f|X is bounded and
A2: f|Y is bounded;
  thus f|(X \/ Y) is bounded_above by A1,A2,Th102;
  thus thesis by A1,A2,Th103;
end;

reserve f,f1,f2 for PartFunc of C,REAL;

registration
  let C;
  let f1,f2 be constant PartFunc of C,REAL;
  cluster f1+f2 -> constant;
  coherence
  proof
    consider r2 being Real such that
A1: for c st c in dom f2 holds f2.c = r2 by PARTFUN2:def 1;
    consider r1 being Real such that
A2: for c st c in dom f1 holds f1.c = r1 by PARTFUN2:def 1;
    now
      let c;
      assume
A3:   c in dom(f1+f2);
      then
A4:   c in dom f1 /\ dom f2 by VALUED_1:def 1;
      then
A5:   c in dom f1 by XBOOLE_0:def 4;
A6:   c in dom f2 by A4,XBOOLE_0:def 4;
      thus (f1+f2).c = f1.c + f2.c by A3,VALUED_1:def 1
        .= f1.c + r2 by A1,A6
        .= r1+r2 by A2,A5;
    end;
    hence thesis by PARTFUN2:def 1;
  end;
  cluster f1-f2 -> constant;
  coherence
  proof
    consider r2 being Real such that
A7: for c st c in dom f2 holds f2.c = r2 by PARTFUN2:def 1;
    consider r1 being Real such that
A8: for c st c in dom f1 holds f1.c = r1 by PARTFUN2:def 1;
    now
      let c;
      assume
A9:   c in dom(f1-f2);
      then
A10:  c in dom f1 /\ dom f2 by VALUED_1:12;
      then
A11:  c in dom f1 by XBOOLE_0:def 4;
A12:  c in dom f2 by A10,XBOOLE_0:def 4;
      thus (f1-f2).c = f1.c - f2.c by A9,VALUED_1:13
        .= f1.c - r2 by A7,A12
        .= r1-r2 by A8,A11;
    end;
    hence thesis by PARTFUN2:def 1;
  end;
  cluster f1(#)f2 -> constant;
  coherence
  proof
    consider r2 being Real such that
A13: for c st c in dom f2 holds f2.c = r2 by PARTFUN2:def 1;
    consider r1 being Real such that
A14: for c st c in dom f1 holds f1.c = r1 by PARTFUN2:def 1;
    now
      let c;
      assume
A15:  c in dom(f1(#)f2);
      then
A16:  c in dom f1 /\ dom f2 by VALUED_1:def 4;
      then
A17:  c in dom f1 by XBOOLE_0:def 4;
A18:  c in dom f2 by A16,XBOOLE_0:def 4;
      thus (f1(#)f2).c = f1.c * f2.c by A15,VALUED_1:def 4
        .= f1.c * r2 by A13,A18
        .= r1*r2 by A14,A17;
    end;
    hence thesis by PARTFUN2:def 1;
  end;
end;

theorem
  f1|X is constant & f2|Y is constant implies (f1+f2)|(X /\ Y) is
  constant & (f1-f2)|(X /\ Y) is constant & (f1(#)f2)|(X /\ Y) is constant
proof
  assume that
A1: f1|X is constant and
A2: f2|Y is constant;
  consider r1 be Real such that
A3: for c st c in X /\ dom f1 holds f1.c = r1 by A1,PARTFUN2:76;
  consider r2 be Real such that
A4: for c st c in Y /\ dom f2 holds f2.c = r2 by A2,PARTFUN2:76;
  now
    let c;
    assume
A5: c in X /\ Y /\ dom (f1+f2);
    then
A6: c in X /\ Y by XBOOLE_0:def 4;
    then
A7: c in X by XBOOLE_0:def 4;
A8: c in dom (f1+f2) by A5,XBOOLE_0:def 4;
    then
A9: c in (dom f1 /\ dom f2) by VALUED_1:def 1;
    then c in dom f1 by XBOOLE_0:def 4;
    then
A10: c in X /\ dom f1 by A7,XBOOLE_0:def 4;
A11: c in Y by A6,XBOOLE_0:def 4;
    c in dom f2 by A9,XBOOLE_0:def 4;
    then
A12: c in Y /\ dom f2 by A11,XBOOLE_0:def 4;
    thus (f1+f2).c =f1.c + f2.c by A8,VALUED_1:def 1
      .= r1 + f2.c by A3,A10
      .= r1 + r2 by A4,A12;
  end;
  hence (f1+f2)|(X /\ Y) is constant by PARTFUN2:76;
  now
    let c;
    assume
A13: c in X /\ Y /\ dom (f1-f2);
    then
A14: c in X /\ Y by XBOOLE_0:def 4;
    then
A15: c in X by XBOOLE_0:def 4;
A16: c in dom (f1-f2) by A13,XBOOLE_0:def 4;
    then
A17: c in (dom f1 /\ dom f2) by VALUED_1:12;
    then c in dom f1 by XBOOLE_0:def 4;
    then
A18: c in X /\ dom f1 by A15,XBOOLE_0:def 4;
A19: c in Y by A14,XBOOLE_0:def 4;
    c in dom f2 by A17,XBOOLE_0:def 4;
    then
A20: c in Y /\ dom f2 by A19,XBOOLE_0:def 4;
    thus (f1-f2).c = f1.c - f2.c by A16,VALUED_1:13
      .= r1 - f2.c by A3,A18
      .= r1 - r2 by A4,A20;
  end;
  hence (f1-f2)|(X /\ Y) is constant by PARTFUN2:76;
  now
    let c;
    assume
A21: c in X /\ Y /\ dom (f1(#)f2);
    then
A22: c in X /\ Y by XBOOLE_0:def 4;
    c in dom (f1(#)f2) by A21,XBOOLE_0:def 4;
    then
A23: c in (dom f1 /\ dom f2) by VALUED_1:def 4;
    then
A24: c in dom f1 by XBOOLE_0:def 4;
A25: c in dom f2 by A23,XBOOLE_0:def 4;
    c in Y by A22,XBOOLE_0:def 4;
    then
A26: c in Y /\ dom f2 by A25,XBOOLE_0:def 4;
    c in X by A22,XBOOLE_0:def 4;
    then
A27: c in X /\ dom f1 by A24,XBOOLE_0:def 4;
    thus (f1(#)f2).c =f1.c * f2.c by VALUED_1:5
      .= r1 * f2.c by A3,A27
      .= r1 * r2 by A4,A26;
  end;
  hence thesis by PARTFUN2:76;
end;

registration
  let C;
  let f be constant PartFunc of C,REAL;
  cluster -f -> constant;
  coherence
  proof
    consider r be Real such that
A1: for c be Element of C st c in dom f holds f.c = r by PARTFUN2:def
    1;
    now
      take p=-r;
      let c be Element of C;
      assume c in dom (-f);
      then c in dom f by VALUED_1:8;
      then -f.c = p by A1;
      hence (-f).c = p by VALUED_1:8;
    end;
    hence thesis by PARTFUN2:def 1;
  end;
  cluster abs f -> constant;
  coherence
  proof
    consider r be Real such that
A2: for c be Element of C st c in dom f holds f.c = r by PARTFUN2:def
    1;
    now
      take p= abs r;
      let c be Element of C;
      assume c in dom (abs f);
      then c in dom f by VALUED_1:def 11;
      then abs(f.c) = p by A2;
      hence (abs f).c = p by VALUED_1:18;
    end;
    hence thesis by PARTFUN2:def 1;
  end;
  let p;
  cluster p(#)f -> constant;
  coherence
  proof
    consider r be Real such that
A3: for c be Element of C st c in dom f holds f.c = r by PARTFUN2:def
    1;
    now
      take r1= p*r;
      let c be Element of C;
      assume c in dom (p(#)f);
      then c in dom f by VALUED_1:def 5;
      then p*(f.c) = r1 by A3;
      hence (p(#)f).c = r1 by VALUED_1:6;
    end;
    hence thesis by PARTFUN2:def 1;
  end;
end;

theorem Th106:
  f|Y is constant implies (p(#)f)|Y is constant
proof
  (p(#)f)|Y = p(#)(f|Y) by Th65;
  hence thesis;
end;

theorem Th107:
  f|Y is constant implies (-f)|Y is constant
proof
  (-f)|Y = -(f|Y) by Th62;
  hence thesis;
end;

theorem Th108:
  f|Y is constant implies (abs f)|Y is constant
proof
  (abs f)|Y = abs(f|Y) by Th62;
  hence thesis;
end;

theorem
  f|Y is constant implies (for r holds (r(#)f)|Y is bounded) & (-f)|Y is
  bounded & (abs f)|Y is bounded
proof
  assume
A1: f|Y is constant;
  hereby
    let r;
    (r(#)f)|Y is constant by A1,Th106;
    hence (r(#)f)|Y is bounded;
  end;
  (-f)|Y is constant by A1,Th107;
  hence (-f)|Y is bounded;
  (abs f)|Y is constant by A1,Th108;
  hence thesis;
end;

theorem
  (f1|X is bounded_above & f2|Y is constant implies (f1+f2)|(X /\ Y) is
  bounded_above) & (f1|X is bounded_below & f2|Y is constant implies (f1+f2)|(X
/\ Y) is bounded_below) & (f1|X is bounded & f2|Y is constant implies (f1+f2)|(
  X /\ Y) is bounded) by Th100;

theorem
  (f1|X is bounded_above & f2|Y is constant implies (f1-f2)|(X /\ Y) is
  bounded_above) & (f1|X is bounded_below & f2|Y is constant implies (f1-f2)|(X
/\ Y) is bounded_below) & (f1|X is bounded & f2|Y is constant implies (f1-f2)|(
X /\ Y) is bounded & (f2-f1)|(X /\ Y) is bounded& (f1(#)f2)|(X /\ Y) is bounded
  )
proof
  thus f1|X is bounded_above & f2|Y is constant implies (f1-f2)|(X /\ Y) is
  bounded_above
  proof
    assume that
A1: f1|X is bounded_above and
A2: f2|Y is constant;
    (-f2)|Y is constant by A2,Th107;
    hence thesis by A1,Th100;
  end;
  thus f1|X is bounded_below & f2|Y is constant implies (f1-f2)|(X /\ Y) is
  bounded_below
  proof
    assume that
A3: f1|X is bounded_below and
A4: f2|Y is constant;
    (-f2)|Y is constant by A4,Th107;
    hence thesis by A3,Th100;
  end;
  assume that
A5: f1|X is bounded and
A6: f2|Y is constant;
  (-f2)|Y is constant by A6,Th107;
  hence (f1-f2)|(X /\ Y) is bounded by A5,Th100;
  thus (f2-f1)|(X /\ Y) is bounded by A5,A6,Th101;
  thus thesis by A5,A6,Th101;
end;
