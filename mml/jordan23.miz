:: Subsequences of Almost, Weakly and Poorly One-to-one Finite Sequences
::  by Robert Milewski
::
:: Received February 1, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies JORDAN23, JORDAN8, BOOLE, JORDAN9, GOBOARD1, GOBOARD5, JORDAN3,
      FINSEQ_5, JORDAN2C, SPPOL_2, GROUP_2, RFINSEQ, REALSET1, FINSEQ_4,
      COMPTS_1, TOPREAL1, SPPOL_1, FINSEQ_1, EUCLID, RELAT_1, MCART_1,
      MATRIX_1, PRE_TOPC, SEQM_3, FUNCT_1, ARYTM_1, FINSEQ_6, GOBOARD2,
      GRAPH_2, ARYTM_3, JORDAN18, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0, REAL_1,
      NAT_1, REALSET1, FUNCT_1, FINSEQ_1, FINSEQ_4, FINSEQ_5, RFINSEQ,
      MATRIX_1, FINSEQ_6, GRAPH_2, STRUCT_0, PRE_TOPC, BINARITH, COMPTS_1,
      EUCLID, GOBOARD1, TOPREAL1, GOBOARD2, GOBOARD5, SPPOL_1, SPPOL_2,
      JORDAN3, JORDAN5C, JORDAN8, JORDAN9, JORDAN2C, JORDAN18;
 constructors XXREAL_0, REAL_1, NAT_1, FINSEQ_4, REALSET1, RFINSEQ, BINARITH,
      FINSEQ_5, GOBOARD2, GRAPH_2, JORDAN3, JORDAN5C, SPRECT_1, JORDAN2C,
      JORDAN8, JORDAN9, JORDAN18;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ARYTM_3, XXREAL_0,
      XREAL_0, REAL_1, FINSEQ_1, REALSET1, FINSEQ_5, FINSEQ_6, STRUCT_0,
      GOBOARD2, SPPOL_2, SPRECT_1, REVROT_1, TOPREAL6, JORDAN1J;
 requirements ARITHM, NUMERALS, BOOLE, SUBSET, REAL;
 definitions TARSKI, XBOOLE_0, FUNCT_1;
 theorems NAT_1, FINSEQ_1, FINSEQ_4, EUCLID, FINSEQ_3, REAL_1, SPPOL_2, TARSKI,
      JORDAN3, FINSEQ_5, FINSEQ_6, GOBOARD7, TOPREAL1, BINARITH, JORDAN5B,
      SPRECT_2, SPPOL_1, FUNCT_1, FINSEQ_2, JORDAN4, GOBOARD2, SPRECT_3,
      TOPREAL3, JORDAN8, PARTFUN2, SPRECT_1, XBOOLE_0, XBOOLE_1, ZFMISC_1,
      JORDAN1E, REVROT_1, JORDAN1B, JORDAN1J, GOBOARD3, TOPREAL8, GRAPH_2,
      SPRECT_5, JORDAN18, JORDAN5C, MSUALG_8, XREAL_1, XXREAL_0, ORDINAL1;

begin

  reserve n for Element of NAT;

  theorem Th1:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st p in L~f holds
    len L_Cut(f,p) >= 1
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume A1: p in L~f;
    L_Cut(f,p) <> {} by A1,JORDAN1E:7;
    then len L_Cut(f,p) <> 0 by FINSEQ_1:25;
    then len L_Cut(f,p) > 0 & 1=0+1 by NAT_1:3;
    hence len L_Cut(f,p) >= 1 by NAT_1:13;
   end;

  theorem
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 holds
    len R_Cut(f,p) >= 1
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    R_Cut(f,p) <> {} by JORDAN1J:44;
    then len R_Cut(f,p) <> 0 by FINSEQ_1:25;
    then len R_Cut(f,p) > 0 & 1 = 0+1 by NAT_1:3;
    hence len R_Cut(f,p) >= 1 by NAT_1:13;
   end;

  theorem Th3:
   for f be FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 holds
    B_Cut (f,p,q) <> {}
   proof
    let f be FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    A1: R_Cut(L_Cut(f,q),p) <> {} by JORDAN1J:44;
    per cases;
     suppose p in L~f & q in L~f & Index(p,f)<Index(q,f) or
       Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      then B_Cut(f,p,q) = R_Cut(L_Cut(f,p),q) by JORDAN3:def 8;
      hence B_Cut (f,p,q) <> {} by JORDAN1J:44;
     end;
     suppose not (p in L~f & q in L~f & Index(p,f)<Index(q,f) or
       Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
      then B_Cut(f,p,q) = Rev R_Cut(L_Cut(f,q),p) by JORDAN3:def 8;
      hence B_Cut (f,p,q) <> {} by A1,JORDAN1B:2;
     end;
   end;

  registration
   let x be set;
   cluster <*x*> -> one-to-one;
   coherence by FINSEQ_3:102;
  end;

  definition
   let f be FinSequence;
   attr f is almost-one-to-one means :Def1:
    for i,j be Element of NAT
    st i in dom f & j in dom f & (i <> 1 or j <> len f) &
    (i <> len f or j <> 1) & f.i = f.j holds i = j;
  end;

  definition
   let f be FinSequence;
   attr f is weakly-one-to-one means :Def2:
    for i be Element of NAT st 1 <= i & i < len f holds f.i <> f.(i+1);
  end;

  definition
   let f be FinSequence;
   attr f is poorly-one-to-one means :Def3:
    for i be Element of NAT
    st 1 <= i & i < len f holds f.i <> f.(i+1) if len f <> 2
    otherwise not contradiction;
   consistency;
  end;

  theorem
   for D be set
   for f be FinSequence of D holds f is almost-one-to-one iff
    for i,j be Element of NAT
    st i in dom f & j in dom f & (i <> 1 or j <> len f) &
     (i <> len f or j <> 1) & f/.i = f/.j holds i = j
   proof
    let D be set;
    let f be FinSequence of D;
    thus f is almost-one-to-one implies
     for i,j be Element of NAT
     st i in dom f & j in dom f & (i <> 1 or j <> len f) &
      (i <> len f or j <> 1) & f/.i = f/.j holds i = j
    proof
     assume A1: f is almost-one-to-one;
     let i,j be Element of NAT such that
      A2: i in dom f & j in dom f and
      A3: i <> 1 or j <> len f and
      A4: i <> len f or j <> 1 and
      A5: f/.i = f/.j;
     f.i = f/.j by A2,A5,FINSEQ_4:def 4
        .= f.j by A2,FINSEQ_4:def 4;
     hence i = j by A1,A2,A3,A4,Def1;
    end;
    assume A6: for i,j be Element of NAT st i in dom f & j in dom f &
     (i <> 1 or j <> len f) & (i <> len f or j <> 1) & f/.i = f/.j
      holds i = j;
    now let i,j be Element of NAT such that
      A7: i in dom f & j in dom f and
      A8: i <> 1 or j <> len f and
      A9: i <> len f or j <> 1 and
      A10: f.i = f.j;
     f/.i = f.j by A7,A10,FINSEQ_4:def 4
        .= f/.j by A7,FINSEQ_4:def 4;
     hence i = j by A6,A7,A8,A9;
    end;
    hence f is almost-one-to-one by Def1;
   end;

  theorem Th5:
   for D be set
   for f be FinSequence of D holds f is weakly-one-to-one iff
    for i be Element of NAT st 1 <= i & i < len f holds f/.i <> f/.(i+1)
   proof
    let D be set;
    let f be FinSequence of D;
    thus f is weakly-one-to-one implies
     for i be Element of NAT st 1 <= i & i < len f holds f/.i <> f/.(i+1)
    proof
     assume A1: f is weakly-one-to-one;
     let i be Element of NAT such that
      A2: 1 <= i & i < len f;
     A3: f.i = f/.i by A2,FINSEQ_4:24;
     1 < i+1 & i+1 <= len f by A2,NAT_1:13;
     then f.(i+1) = f/.(i+1) by FINSEQ_4:24;
     hence f/.i <> f/.(i+1) by A1,A2,A3,Def2;
    end;
    assume A4: for i be Element of NAT st 1 <= i & i < len f
    holds f/.i <> f/.(i+1);
    now let i be Element of NAT such that
      A5: 1 <= i & i < len f;
     A6: f.i = f/.i by A5,FINSEQ_4:24;
     1 < i+1 & i+1 <= len f by A5,NAT_1:13;
     then f.(i+1) = f/.(i+1) by FINSEQ_4:24;
     hence f.i <> f.(i+1) by A4,A5,A6;
    end;
    hence f is weakly-one-to-one by Def2;
   end;

  theorem
   for D be set
   for f be FinSequence of D holds f is poorly-one-to-one iff
    (len f <> 2 implies
     for i be Element of NAT st 1 <= i & i < len f holds f/.i <> f/.(i+1))
   proof
    let D be set;
    let f be FinSequence of D;
    thus f is poorly-one-to-one & len f <> 2 implies
     for i be Element of NAT st 1 <= i & i < len f holds f/.i <> f/.(i+1)
    proof
     assume A1: f is poorly-one-to-one & len f <> 2;
     let i be Element of NAT such that
      A2: 1 <= i & i < len f;
     A3: f.i = f/.i by A2,FINSEQ_4:24;
     1 < i+1 & i+1 <= len f by A2,NAT_1:13;
     then f.(i+1) = f/.(i+1) by FINSEQ_4:24;
     hence f/.i <> f/.(i+1) by A1,A2,A3,Def3;
    end;
    assume A4: len f <> 2 implies
     for i be Element of NAT st 1 <= i & i < len f holds f/.i <> f/.(i+1);
    per cases;
     suppose A5: len f <> 2;
      now let i be Element of NAT such that
        A6: 1 <= i & i < len f;
       A7: f.i = f/.i by A6,FINSEQ_4:24;
       1 < i+1 & i+1 <= len f by A6,NAT_1:13;
       then f.(i+1) = f/.(i+1) by FINSEQ_4:24;
       hence f.i <> f.(i+1) by A4,A5,A6,A7;
      end;
      hence f is poorly-one-to-one by A5,Def3;
     end;
     suppose len f = 2;
      hence f is poorly-one-to-one by Def3;
     end;
   end;

  registration
   cluster one-to-one -> almost-one-to-one FinSequence;
   coherence
   proof
    let f be FinSequence;
    assume A1: f is one-to-one;
    let i,j be Element of NAT;
    assume that
     A2: i in dom f and
     A3: j in dom f and
     i <> 1 or j <> len f and
     i <> len f or j <> 1 and
     A4: f.i = f.j;
    thus i = j by A1,A2,A3,A4,FUNCT_1:def 8;
   end;
  end;

  registration
   cluster almost-one-to-one -> poorly-one-to-one FinSequence;
   coherence
   proof
    let f be FinSequence;
    assume A1: f is almost-one-to-one;
    per cases;
     case A2: len f <> 2;
      now per cases;
       suppose A3: len f <> 0;
        let i be Element of NAT;
        assume A4: 1 <= i & i < len f;
        then A5: i in dom f by FINSEQ_3:27;
        A6: i+1 >= 1 by NAT_1:11;
        i+1 <= len f by A4,NAT_1:13;
        then A7: i+1 in dom f by A6,FINSEQ_3:27;
        A8: i <> i+1;
        A9: not (i = 1 & i+1 = len f) by A2;
        not (i = len f & i+1 = 1) by A3;
        hence f.i <> f.(i+1) by A1,A5,A7,A8,A9,Def1;
       end;
       suppose A10: len f = 0;
        let i be Element of NAT;
        assume 1 <= i & i < len f;
        hence f.i <> f.(i+1) by A10;
       end;
      end;
      hence thesis;
     end;
     case len f = 2;
      thus thesis;
     end;
   end;
  end;

  theorem Th7:
   for f be FinSequence st len f <> 2 holds
    f is weakly-one-to-one iff f is poorly-one-to-one
   proof
    let f be FinSequence;
    assume A1: len f <> 2;
    thus f is weakly-one-to-one implies f is poorly-one-to-one
    proof
     assume f is weakly-one-to-one;
     then for i be Element of NAT st 1 <= i & i < len f
     holds f.i <> f.(i+1) by Def2;
     hence f is poorly-one-to-one by A1,Def3;
    end;
    assume f is poorly-one-to-one;
    then for i be Element of NAT st 1 <= i & i < len f
    holds f.i <> f.(i+1) by A1,Def3;
    hence f is weakly-one-to-one by Def2;
   end;

  registration
   cluster {} -> weakly-one-to-one;
   coherence
   proof
    let i be Element of NAT;
    assume 1 <= i & i < len {};
    hence {}.i <> {}.(i+1) by FINSEQ_1:25;
   end;
  end;

  registration
   let x be set;
   cluster <*x*> -> weakly-one-to-one;
   coherence
   proof
    let i be Element of NAT;
    assume 1 <= i & i < len <*x*>;
    hence <*x*>.i <> <*x*>.(i+1) by FINSEQ_1:56;
   end;
  end;

  registration
   let x,y be set;
   cluster <*x,y*> -> poorly-one-to-one;
   coherence
   proof
    len <*x,y*> = 2 by FINSEQ_1:61;
    hence thesis by Def3;
   end;
  end;

  registration
   cluster weakly-one-to-one non empty FinSequence;
   existence
   proof
    consider x be set;
    len <*x*> <> 2 by FINSEQ_1:56;
    hence thesis;
   end;
  end;

  registration
   let D be non empty set;
   cluster weakly-one-to-one circular non empty FinSequence of D;
   existence
   proof
    consider x be Element of D;
    <*x*>/.1 = <*x*>/.len <*x*> by FINSEQ_1:56;
    then <*x*> is circular by FINSEQ_6:def 1;
    hence thesis;
   end;
  end;

  theorem Th8:
   for f be FinSequence st f is almost-one-to-one holds
    Rev f is almost-one-to-one
   proof
    let f be FinSequence;
    assume A1: f is almost-one-to-one;
    let i,j be Element of NAT;
    assume that
     A2: i in dom Rev f and
     A3: j in dom Rev f and
     A4: i <> 1 or j <> len Rev f and
     A5: i <> len Rev f or j <> 1 and
     A6: Rev f.i = Rev f.j;
    A7: dom f = dom Rev f by FINSEQ_5:60;
    i in Seg len f by A2,A7,FINSEQ_1:def 3;
    then len f - i + 1 in Seg len f by FINSEQ_5:2;
    then A8: len f - i + 1 in dom f by FINSEQ_1:def 3;
    j in Seg len f by A3,A7,FINSEQ_1:def 3;
    then len f - j + 1 in Seg len f by FINSEQ_5:2;
    then A9: len f - j + 1 in dom f by FINSEQ_1:def 3;
    A10: not(len f - i + 1 = 1 & len f - j + 1 = len f) by A5,FINSEQ_5:def 3;
    A11: not(len f - i + 1 = len f & len f - j + 1 = 1) by A4,FINSEQ_5:def 3;
    Rev f.i = f.(len f - i + 1) & Rev f.j = f.(len f - j + 1)
                                                     by A2,A3,FINSEQ_5:def 3;
    then len f - i + 1 = len f - j + 1 by A1,A6,A8,A9,A10,A11,Def1;
    then len f - i = len f - j;
    hence i = j;
   end;

  theorem Th9:
   for f be FinSequence st f is weakly-one-to-one holds
    Rev f is weakly-one-to-one
   proof
    let f be FinSequence;
    assume A1: f is weakly-one-to-one;
    let i be Element of NAT;
    assume that
     A2: 1 <= i and
     A3: i < len Rev f;
    A4: len f = len Rev f by FINSEQ_5:def 3;
    A5: i in Seg len Rev f by A2,A3,FINSEQ_1:3;
    then A6: i in dom Rev f by FINSEQ_1:def 3;
    A7: i+1 >= 1 by NAT_1:11;
    A8: i+1 <= len Rev f by A3,NAT_1:13;
    then A9: i+1 in Seg len Rev f by A7,FINSEQ_1:3;
    then i+1 in dom Rev f by FINSEQ_1:def 3;
    then A10: Rev f.i = f.(len f - i + 1) & Rev f.(i+1) = f.(len f - (i+1) + 1)
                                                        by A6,FINSEQ_5:def 3;
    len f - (i+1) + 1 = len f - i;
    then reconsider j = len f - i as Element of NAT by A4,A8,FINSEQ_5:1;
    len f - (i+1) + 1 in Seg len f by A4,A9,FINSEQ_5:2;
    then A11: j >= 1 by FINSEQ_1:3;
    len f - i + 1 in Seg len f by A4,A5,FINSEQ_5:2;
    then j+1 <= len f by FINSEQ_1:3;
    then j < len f by NAT_1:13;
    hence Rev f.i <> Rev f.(i+1) by A1,A10,A11,Def2;
   end;

  theorem Th10:
   for f be FinSequence st f is poorly-one-to-one holds
    Rev f is poorly-one-to-one
   proof
    let f be FinSequence;
    assume A1: f is poorly-one-to-one;
    A2: len f = len Rev f by FINSEQ_5:def 3;
    per cases;
     suppose A3: len f <> 2;
      then f is weakly-one-to-one by A1,Th7;
      then Rev f is weakly-one-to-one by Th9;
      hence Rev f is poorly-one-to-one by A3,A2,Th7;
     end;
     suppose len f = 2;
      hence Rev f is poorly-one-to-one by A2,Def3;
     end;
   end;

  registration :: FINSEQ_5
   cluster one-to-one non empty FinSequence;
   existence
   proof
    consider x be set;
    <*x*> is non empty one-to-one;
    hence thesis;
   end;
  end;

  registration
   let f be almost-one-to-one FinSequence;
   cluster Rev f -> almost-one-to-one;
   coherence by Th8;
  end;

  registration
   let f be weakly-one-to-one FinSequence;
   cluster Rev f -> weakly-one-to-one;
   coherence by Th9;
  end;

  registration
   let f be poorly-one-to-one FinSequence;
   cluster Rev f -> poorly-one-to-one;
   coherence by Th10;
  end;

  theorem Th11:
   for D be non empty set
   for f be FinSequence of D st f is almost-one-to-one
   for p be Element of D holds
    Rotate(f,p) is almost-one-to-one
   proof
    let D be non empty set;
    let f be FinSequence of D;
    assume A1: f is almost-one-to-one;
    let p be Element of D;
    per cases;
     suppose A2: p in rng f;
      A3: 0+1 <= p..f & p..f <= len f by A2,FINSEQ_4:31;
      A4: len (f:-p) = len f - p..f + 1 by A2,FINSEQ_5:53;
      let i,j be Element of NAT;
      assume that
       A5: i in dom Rotate(f,p) and
       A6: j in dom Rotate(f,p) and
       A7: i <> 1 or j <> len Rotate(f,p) and
       A8: i <> len Rotate(f,p) or j <> 1 and
       A9: Rotate(f,p).i = Rotate(f,p).j;
      A10: dom Rotate(f,p) = dom f by REVROT_1:15;
      A11: len Rotate(f,p) = len f by REVROT_1:14;
      A12: 0+1 <= i & i <= len f by A5,A10,FINSEQ_3:27;
      A13: 0+1 <= j & j <= len f by A6,A10,FINSEQ_3:27;
      A14: i - 1 >= 0 & j - 1 >= 0 by A12,A13,XREAL_1:21;
      then A15: i -' 1 >= 0 & j -' 1 >= 0 by BINARITH:def 3;
      A16: j - len f <= len f-len f & i - len f <= len f-len f
         by A12,A13,XREAL_1:11;
      A17: p..f - 1 >= 0 by A3,XREAL_1:21;
      A18: now assume A19: i -' 1 + p..f = 1 & j -' 1 + p..f = len f;
       then i -' 1 + (p..f - 1) = 0;
       then A20: i -' 1 = 0 & p..f - 1 = 0 by A15,A17,XREAL_1:29;
       then i - 1 = 0 by A14,BINARITH:def 3;
       hence contradiction by A7,A13,A19,A11,A20,BINARITH:53;
      end;
      A21: now assume A22: i -' 1 + p..f = len f & j -' 1 + p..f = 1;
       then j -' 1 + (p..f - 1) = 0;
       then A23: j -' 1 = 0 & p..f - 1 = 0 by A15,A17,XREAL_1:29;
       then j - 1 = 0 by A14,BINARITH:def 3;
       hence contradiction by A8,A12,A22,A11,A23,BINARITH:53;
      end;
      now per cases;
       suppose A24: i <= len(f:-p);
        then Rotate(f,p)/.i = f/.(i -' 1 + p..f) by A2,A12,REVROT_1:9;
        then A25: Rotate(f,p).i = f/.(i -' 1 + p..f) by A5,FINSEQ_4:def 4;
        i in dom(f:-p) by A12,A24,FINSEQ_3:27;
        then i -' 1 + 1 in dom(f:-p) by A12,BINARITH:53;
        then A26: i -' 1 + p..f in dom f by A2,FINSEQ_5:54;
        now per cases;
         suppose A27: j <= len(f:-p);
          then Rotate(f,p)/.j = f/.(j -' 1 + p..f) by A2,A13,REVROT_1:9;
          then A28: f/.(i -' 1 + p..f) = f/.(j -' 1 + p..f)
                                                  by A6,A9,A25,FINSEQ_4:def 4;
          j in dom(f:-p) by A13,A27,FINSEQ_3:27;
          then j -' 1 + 1 in dom(f:-p) by A13,BINARITH:53;
          then A29: j -' 1 + p..f in dom f by A2,FINSEQ_5:54;
          f.(i -' 1 + p..f) = f/.(i -' 1 + p..f) by A26,FINSEQ_4:def 4
             .= f.(j -' 1 + p..f) by A29,A28,FINSEQ_4:def 4;
          then i -' 1 + p..f = j -' 1 + p..f by A1,A26,A29,A18,A21,Def1;
          then i -' 1 = j -' 1;
          hence i = j by A12,A13,BINARITH:49;
         end;
         suppose A30: j > len(f:-p);
          then Rotate(f,p)/.j = f/.(j + p..f -' len f) by A2,A13,REVROT_1:12;
          then A31: f/.(i -' 1 + p..f) = f/.(j + p..f -' len f)
                                                  by A6,A9,A25,FINSEQ_4:def 4;
          A32: j - (len f - p..f) > 1 by A30,A4,XREAL_1:22;
          then A33: j + p..f - len f > 1;
          then A34: j + p..f -' len f >= 1 by BINARITH:def 3;
          A35: now assume A36: i -' 1 + p..f = 1 & j + p..f -' len f = len f;
           then i -' 1 + (p..f - 1) = 0;
           then i -' 1 = 0 & p..f - 1 = 0 by A15,A17,XREAL_1:29;
           then A37: i - 1 = 0 by A14,BINARITH:def 3;
           j + p..f - len f = len f by A36,A32,BINARITH:def 3;
           then j + p..f = len f + len f;
           then j >= len f by A3,XREAL_1:10;
           hence contradiction by A7,A13,A37,A11,XXREAL_0:1;
          end;
          A38: i -' 1 + p..f <> len f or j + p..f -' len f <> 1
                                                    by A33,BINARITH:def 3;
          j + p..f <= len f + len f by A13,A3,XREAL_1:9;
          then j + p..f - len f <= len f by XREAL_1:22;
          then j + p..f -' len f <= len f by A32,BINARITH:def 3;
          then A39: j + p..f -' len f in dom f by A34,FINSEQ_3:27;
          f.(i -' 1 + p..f) = f/.(i -' 1 + p..f) by A26,FINSEQ_4:def 4
             .= f.(j + p..f -' len f) by A39,A31,FINSEQ_4:def 4;
          then i -' 1 + p..f = j + p..f -' len f by A1,A26,A39,A35,A38,Def1;
          then i -' 1 + p..f = j + p..f - len f by A32,BINARITH:def 3;
          then i -' 1 + p..f = j - len f + p..f;
          then i -' 1 = j - len f;
          then i - 1 = 0 & j - len f = 0 by A14,A16,BINARITH:def 3;
          then i = 0 + 1 & j = 0 + len f;
          hence i = j by A7,REVROT_1:14;
         end;
        end;
        hence i = j;
       end;
       suppose A40: i > len(f:-p);
        then A41: Rotate(f,p)/.i = f/.(i+p..f -' len f) by A2,A12,REVROT_1:12;
        A42: i - (len f - p..f) > 1 by A40,A4,XREAL_1:22;
        then A43: i + p..f - len f > 1;
        A44: i + p..f -' len f >= 1 by A43,BINARITH:def 3;
        i + p..f <= len f + len f by A12,A3,XREAL_1:9;
        then i + p..f - len f <= len f by XREAL_1:22;
        then i + p..f -' len f <= len f by A42,BINARITH:def 3;
        then A45: i + p..f -' len f in dom f by A44,FINSEQ_3:27;
        now per cases;
         suppose A46: j <= len(f:-p);
          then Rotate(f,p)/.j = f/.(j -' 1 + p..f) by A2,A13,REVROT_1:9;
          then Rotate(f,p).j = f/.(j -' 1 + p..f) by A6,FINSEQ_4:def 4;
          then A47: f/.(j -' 1 + p..f) = f/.(i + p..f -' len f)
                                                  by A5,A9,A41,FINSEQ_4:def 4;
          j in dom(f:-p) by A13,A46,FINSEQ_3:27;
          then j -' 1 + 1 in dom(f:-p) by A13,BINARITH:53;
          then A48: j -' 1 + p..f in dom f by A2,FINSEQ_5:54;
          A49: now assume A50: j -' 1 + p..f = 1 & i + p..f -' len f = len f;
           then j -' 1 + (p..f - 1) = 0;
           then j -' 1 = 0 & p..f - 1 = 0 by A15,A17,XREAL_1:29;
           then A51: j - 1 = 0 by A14,BINARITH:def 3;
           i + p..f - len f = len f by A50,A42,BINARITH:def 3;
           then i + p..f = len f + len f;
           then i >= len f by A3,XREAL_1:10;
           hence contradiction by A8,A12,A51,A11,XXREAL_0:1;
          end;
          A52: j -' 1 + p..f <> len f or i + p..f -' len f <> 1
                                                    by A43,BINARITH:def 3;
          f.(j -' 1 + p..f) = f/.(j -' 1 + p..f) by A48,FINSEQ_4:def 4
             .= f.(i + p..f -' len f) by A45,A47,FINSEQ_4:def 4;
          then j -' 1 + p..f = i + p..f -' len f by A1,A48,A45,A49,A52,Def1;
          then j -' 1 + p..f = i + p..f - len f by A42,BINARITH:def 3;
          then j -' 1 + p..f = i - len f + p..f;
          then j -' 1 = i - len f;
          then j - 1 = 0 & i - len f = 0 by A14,A16,BINARITH:def 3;
          then j = 0 + 1 & i = 0 + len f;
          hence i = j by A8,REVROT_1:14;
         end;
         suppose A53: j > len(f:-p);
          then Rotate(f,p)/.j = f/.(j + p..f -' len f) by A2,A13,REVROT_1:12;
          then Rotate(f,p).j = f/.(j + p..f -' len f) by A6,FINSEQ_4:def 4;
          then A54: f/.(j + p..f -' len f) = f/.(i + p..f -' len f)
                                                  by A5,A9,A41,FINSEQ_4:def 4;
          A55: j - (len f - p..f) > 1 by A53,A4,XREAL_1:22;
          then A56: j + p..f - len f > 1;
          A57: j + p..f -' len f >= 1 by A56,BINARITH:def 3;
          j + p..f <= len f + len f by A13,A3,XREAL_1:9;
          then j + p..f - len f <= len f by XREAL_1:22;
          then j + p..f -' len f <= len f by A55,BINARITH:def 3;
          then A58: j + p..f -' len f in dom f by A57,FINSEQ_3:27;
          A59: i+p..f -' len f <> 1 or j + p..f -' len f <> len f
                                                   by A43,BINARITH:def 3;
          A60: i+p..f -' len f <> len f or j + p..f -' len f <> 1
                                                 by A56,BINARITH:def 3;
          f.(i + p..f -' len f) = f/.(i + p..f -' len f) by A45,FINSEQ_4:def 4
             .= f.(j + p..f -' len f) by A58,A54,FINSEQ_4:def 4;
          then i + p..f -' len f = j + p..f -' len f
                                                 by A1,A45,A58,A59,A60,Def1;
          then i + p..f - len f = j + p..f -' len f by A42,BINARITH:def 3;
          then i + p..f - len f = j + p..f - len f by A55,BINARITH:def 3;
          then i + p..f = j + p..f;
          hence i = j;
         end;
        end;
        hence i = j;
       end;
      end;
      hence i = j;
     end;
     suppose not p in rng f;
      hence Rotate(f,p) is almost-one-to-one by A1,FINSEQ_6:def 2;
     end;
   end;

  theorem Th12:
   for D be non empty set
   for f be FinSequence of D st f is weakly-one-to-one circular
   for p be Element of D holds
    Rotate(f,p) is weakly-one-to-one
   proof
    let D be non empty set;
    let f be FinSequence of D;
    assume A1: f is weakly-one-to-one circular;
    let p be Element of D;
    per cases;
     suppose A2: p in rng f;
      let i be Element of NAT;
      A3: 1 <= p..f & p..f <= len f by A2,FINSEQ_4:31;
      A4: len (f:-p) = len f - p..f + 1 by A2,FINSEQ_5:53;
      A5: len Rotate(f,p) = len f by REVROT_1:14;
      assume A6: 1 <= i & i < len Rotate(f,p);
      then A7: i+1 > 1 & i+1 <= len Rotate(f,p) by NAT_1:13;
      A8: len f > 1 by A6,A5,XXREAL_0:2;
      then A9: len f >= 1+1 by NAT_1:13;
      i-1+1 >= 1 by A6;
      then A10: i-1 >= 1-1 by XREAL_1:22;
      i -' 1 >= 0 by A10,BINARITH:def 3;
      then A11: i -' 1 + p..f >= 0+1 by A3,XREAL_1:9;
      then A12: i -' 1 + p..f + 1 > 1 by NAT_1:13;
      A13: i + 1 -' 1 + p..f = i + 1 - 1 + p..f by A6,BINARITH:def 3
         .= i - 1 + p..f + 1
         .= i -' 1 + p..f + 1 by A10,BINARITH:def 3;
      now per cases by REAL_1:def 5;
       suppose A14: i < len(f:-p);
        then i - 1 < len f - p..f by A4,XREAL_1:21;
        then i - 1 + p..f < len f by XREAL_1:22;
        then A15: i -' 1 + p..f < len f by A10,BINARITH:def 3;
        then A16: i -' 1 + p..f + 1 <= len f by NAT_1:13;
        i+1 <= len(f:-p) by A14,NAT_1:13;
        then Rotate(f,p)/.(i+1) = f/.(i+1 -' 1 + p..f) by A2,A7,REVROT_1:9
           .= f.(i+1 -' 1 + p..f) by A13,A12,A16,FINSEQ_4:24;
        then A17: Rotate(f,p).(i+1) = f.(i+1 -' 1 + p..f) by A7,FINSEQ_4:24;
        Rotate(f,p)/.i = f/.(i -' 1 + p..f) by A2,A6,A14,REVROT_1:9
           .= f.(i -' 1 + p..f) by A11,A15,FINSEQ_4:24;
        then Rotate(f,p).i = f.(i -' 1 + p..f) by A6,FINSEQ_4:24;
        hence Rotate(f,p).i <> Rotate(f,p).(i+1) by A1,A13,A11,A15,A17,Def2;
       end;
       suppose A18: i = len(f:-p);
        then Rotate(f,p)/.i = f/.len f by A2,REVROT_1:11
           .= f/.1 by A1,FINSEQ_6:def 1
           .= f.1 by A8,FINSEQ_4:24;
        then A19: Rotate(f,p).i = f.1 by A6,FINSEQ_4:24;
        A20: i + 1 + p..f -' len f = len f + (1+1) - len f
           by A4,A18,BINARITH:def 3
           .= 2;
        i + 1 > len(f:-p) by A18,NAT_1:13;
        then Rotate(f,p)/.(i+1) = f/.(i + 1 + p..f -' len f)
                                                      by A2,A7,A5,REVROT_1:12
           .= f.(i + 1 + p..f -' len f) by A20,A9,FINSEQ_4:24;
        then Rotate(f,p).(i+1) = f.(1+1) by A7,A20,FINSEQ_4:24;
        hence Rotate(f,p).i <> Rotate(f,p).(i+1) by A1,A8,A19,Def2;
       end;
       suppose A21: i > len(f:-p);
        A22: i - (len f - p..f) > 1 by A4,A21,XREAL_1:22;
        then A23: i + p..f - len f > 1;
        A24: i + p..f - len f > 1-1 by A22,XXREAL_0:2;
        A25: i + p..f -' len f > 1 by A23,BINARITH:def 3;
        A26: i + p..f - len f + 1 > 1 by A24,XREAL_1:21;
        then i+1 + p..f - len f > 1;
        then A27: i+1 + p..f -' len f > 1 by BINARITH:def 3;
        i + p..f < len f + len f by A6,A5,A3,XREAL_1:10;
        then i + p..f - len f < len f by XREAL_1:21;
        then A28: i + p..f -' len f < len f by A22,BINARITH:def 3;
        i+1 + p..f <= len f + len f by A7,A5,A3,XREAL_1:9;
        then i+1 + p..f - len f <= len f by XREAL_1:22;
        then A29: i+1 + p..f -' len f <= len f by A26,BINARITH:def 3;
        i+1 > len(f:-p) by A21,NAT_1:13;
        then Rotate(f,p)/.(i+1) = f/.(i+1 + p..f -' len f)
                                                       by A2,A7,A5,REVROT_1:12
           .= f.(i+1 + p..f -' len f) by A27,A29,FINSEQ_4:24;
        then A30: Rotate(f,p).(i+1) = f.(i+1+p..f -' len f) by A7,FINSEQ_4:24;
        Rotate(f,p)/.i = f/.(i + p..f -' len f) by A2,A6,A5,A21,REVROT_1:12
           .= f.(i + p..f -' len f) by A25,A28,FINSEQ_4:24;
        then A31: Rotate(f,p).i = f.(i + p..f -' len f) by A6,FINSEQ_4:24;
        i + 1 + p..f -' len f = i + 1 + p..f - len f by A26,BINARITH:def 3
           .= i + p..f - len f + 1
           .= i + p..f -' len f + 1 by A22,BINARITH:def 3;
        hence Rotate(f,p).i <> Rotate(f,p).(i+1) by A1,A25,A28,A30,A31,Def2;
       end;
      end;
      hence Rotate(f,p).i <> Rotate(f,p).(i+1);
     end;
     suppose not p in rng f;
      hence Rotate(f,p) is weakly-one-to-one by A1,FINSEQ_6:def 2;
     end;
   end;

  theorem Th13:
   for D be non empty set
   for f be FinSequence of D st f is poorly-one-to-one circular
   for p be Element of D holds
    Rotate(f,p) is poorly-one-to-one
   proof
    let D be non empty set;
    let f be FinSequence of D;
    assume A1: f is poorly-one-to-one circular;
    let p be Element of D;
    A2: len Rotate(f,p) = len f by REVROT_1:14;
    per cases;
     suppose A3: len f <> 2;
      then f is weakly-one-to-one by A1,Th7;
      then Rotate(f,p) is weakly-one-to-one by A1,Th12;
      hence Rotate(f,p) is poorly-one-to-one by A3,A2,Th7;
     end;
     suppose len f = 2;
      hence Rotate(f,p) is poorly-one-to-one by A2,Def3;
     end;
   end;

  registration :: FINSEQ_6
   let D be non empty set;
   cluster one-to-one circular non empty FinSequence of D;
   existence
   proof
    consider x be Element of D;
    <*x*>/.1 = <*x*>/.len <*x*> by FINSEQ_1:56;
    then <*x*> is circular by FINSEQ_6:def 1;
    hence thesis;
   end;
  end;

  registration
   let D be non empty set;
   let f be almost-one-to-one FinSequence of D;
   let p be Element of D;
   cluster Rotate(f,p) -> almost-one-to-one;
   coherence by Th11;
  end;

  registration
   let D be non empty set;
   let f be circular weakly-one-to-one FinSequence of D;
   let p be Element of D;
   cluster Rotate(f,p) -> weakly-one-to-one;
   coherence by Th12;
  end;

  registration
   let D be non empty set;
   let f be circular poorly-one-to-one FinSequence of D;
   let p be Element of D;
   cluster Rotate(f,p) -> poorly-one-to-one;
   coherence by Th13;
  end;

  theorem Th14:
   for D be non empty set
   for f be FinSequence of D holds
    f is almost-one-to-one iff f/^1 is one-to-one & f|(len f-'1) is one-to-one
   proof
    let D be non empty set;
    let f be FinSequence of D;
    A1: len (f/^1) = len f-'1 by JORDAN3:19;
    thus f is almost-one-to-one implies
     f/^1 is one-to-one & f|(len f-'1) is one-to-one
    proof
     assume A2: f is almost-one-to-one;
     thus f/^1 is one-to-one
     proof
      let x,y be set;
      assume that
       A3: x in dom (f/^1) and
       A4: y in dom (f/^1) and
       A5: (f/^1).x = (f/^1).y;
      reconsider i=x, j=y as Element of NAT by A3,A4;
      i in Seg len (f/^1) & j in Seg len (f/^1) by A3,A4,FINSEQ_1:def 3;
      then A6: 1 <= i & i <= len (f/^1) & 1 <= j & j <= len (f/^1)
                                                                by FINSEQ_1:3;
      then len f-'1 >= 1 by A1,XXREAL_0:2;
      then len f-'1 = len f-1 by BINARITH:57;
      then A7: i+1 <= len f & j+1 <= len f by A6,A1,XREAL_1:21;
      then A8: (f/^1).i = f.(i+1) & (f/^1).j = f.(j+1) by A6,JORDAN3:23;
      A9: i+1 > 1 & j+1 > 1 by A6,NAT_1:13;
      then i+1 in dom f & j+1 in dom f by A7,FINSEQ_3:27;
      then i+1 = j+1 by A2,A5,A8,A9,Def1;
      hence x = y;
     end;
     len f-'1 <= len f by BINARITH:52;
     then A10: len (f|(len f-'1)) = len f-'1 by FINSEQ_1:80;
     thus f|(len f-'1) is one-to-one
     proof
      let x,y be set;
      assume that
       A11: x in dom (f|(len f-'1)) and
       A12: y in dom (f|(len f-'1)) and
       A13: (f|(len f-'1)).x = (f|(len f-'1)).y;
      reconsider i=x, j=y as Element of NAT by A11,A12;
      i in Seg len (f|(len f-'1)) & j in Seg len (f|(len f-'1))
                                                     by A11,A12,FINSEQ_1:def 3;
      then A14: 1 <= i & i <= len (f|(len f-'1)) &
       1 <= j & j <= len (f|(len f-'1)) by FINSEQ_1:3;
      then len f-'1 >= 1 by A10,XXREAL_0:2;
      then A15: len f-'1 = len f-1 by BINARITH:57;
      A16: (f|(len f-'1)).i = f.i & (f|(len f-'1)).j = f.j
                                                      by A14,A10,FINSEQ_3:121;
      i+1 <= len f & j+1 <= len f by A14,A15,A10,XREAL_1:21;
      then A17: i < len f & j < len f by NAT_1:13;
      then i in dom f & j in dom f by A14,FINSEQ_3:27;
      hence x = y by A2,A13,A16,A17,Def1;
     end;
    end;
    assume that
     A18: f/^1 is one-to-one and
     A19: f|(len f-'1) is one-to-one;
    let i,j be Element of NAT;
    assume that
     A20: i in dom f and
     A21: j in dom f and
     A22: i <> 1 or j <> len f and
     A23: i <> len f or j <> 1 and
     A24: f.i = f.j;
    A25: 0+1 <= i & i <= len f & 0+1 <= j & j <= len f
      by A20,A21,FINSEQ_3:27;
    A26: i-1 >= 0 & j-1 >= 0 by A25,XREAL_1:21;
    A27: len f >= 0+1 by A25,XXREAL_0:2;
    then A28: i = i-'1+1 & j = j-'1+1 & len f = len f-'1+1 by A25,BINARITH:53;
    A29: len f-1 >= 0 by A27,XREAL_1:21;
    i-1 <= len f-1 & j-1 <= len f-1 by A25,XREAL_1:11;
    then i-'1 <= len f-1 & j-'1 <= len f-1 by A26,BINARITH:def 3;
    then A30: i-'1 <= len f-'1 & j-'1 <= len f-'1 by A29,BINARITH:def 3;
    f is non empty by A20,FINSEQ_1:26;
    then A31: f = <* f/.1 *> ^ (f/^1) by FINSEQ_5:32;
    len f-'1 <= len f by A28,NAT_1:13;
    then A32: len (f|(len f-'1)) = len f-'1 by FINSEQ_1:80;
    per cases;
     suppose i <> 1 & j <> 1;
      then i > 0+1 & j > 0+1 by A25,REAL_1:def 5;
      then i-1 > 0 & j-1 > 0 by XREAL_1:22;
      then i-'1 > 0 & j-'1 > 0 by BINARITH:def 3;
      then i-'1 >= 0+1 & j-'1 >= 0+1 by NAT_1:13;
      then A33: i-'1 in dom (f/^1) & j-'1 in dom (f/^1) by A1,A30,FINSEQ_3:27;
      then f.i = (f/^1).(i-'1) & f.j = (f/^1).(j-'1) by A31,A28,FINSEQ_3:112;
      then i-'1 = j-'1 by A24,A18,A33,FUNCT_1:def 8;
      then i-1 = j-'1 by A26,BINARITH:def 3;
      then i-1 = j-1 by A26,BINARITH:def 3;
      hence i = j;
     end;
     suppose A34: i = 1;
      then j < len f by A22,A25,REAL_1:def 5;
      then j+1 <= len f by NAT_1:13;
      then j <= len f-1 by XREAL_1:21;
      then A35: j <= len f-'1 by A29,BINARITH:def 3;
      then A36: (f|(len f-'1)).j = f.j by FINSEQ_3:121;
      A37: i <= len f-'1 by A25,A35,A34,XXREAL_0:2;
      then A38: (f|(len f-'1)).i = f.i by FINSEQ_3:121;
      i in dom(f|(len f-'1)) & j in dom(f|(len f-'1))
        by A25,A35,A32,A37,FINSEQ_3:27;
      hence i = j by A24,A19,A36,A38,FUNCT_1:def 8;
     end;
     suppose A39: j = 1;
      then i < len f by A23,A25,REAL_1:def 5;
      then i+1 <= len f by NAT_1:13;
      then i <= len f-1 by XREAL_1:21;
      then A40: i <= len f-'1 by A29,BINARITH:def 3;
      then A41: (f|(len f-'1)).i = f.i by FINSEQ_3:121;
      A42: j <= len f-'1 by A25,A40,A39,XXREAL_0:2;
      then A43: (f|(len f-'1)).j = f.j by FINSEQ_3:121;
      i in dom(f|(len f-'1)) & j in dom(f|(len f-'1))
        by A25,A40,A32,A42,FINSEQ_3:27;
      hence i = j by A24,A19,A41,A43,FUNCT_1:def 8;
     end;
   end;

  registration
   let C be compact non vertical non horizontal Subset of TOP-REAL 2;
   let n be Element of NAT;
   cluster Cage(C,n) -> almost-one-to-one;
   coherence
   proof
    A1: Cage(C,n) /^ 1 is one-to-one by SPRECT_5:8;
    A2: len Cage(C,n) > 4 by GOBOARD7:36;
    then len Cage(C,n) > 0+1 by XXREAL_0:2;
    then len Cage(C,n)-1 > 0 by XREAL_1:22;
    then A3: len Cage(C,n)-'1 = len Cage(C,n)-1 by BINARITH:def 3;
    len Cage(C,n) < len Cage(C,n)+1 by NAT_1:13;
    then len Cage(C,n)-'1 < len Cage(C,n) by A3,XREAL_1:21;
    then Cage(C,n)|(len Cage(C,n)-'1) is one-to-one by A2,TOPREAL8:22;
    hence Cage(C,n) is almost-one-to-one by A1,Th14;
   end;
  end;

  registration
   let C be compact non vertical non horizontal Subset of TOP-REAL 2;
   let n be Element of NAT;
   cluster Cage(C,n) -> weakly-one-to-one;
   coherence
   proof
    len Cage(C,n) > 4 by GOBOARD7:36;
    then len Cage(C,n) > 2 by XXREAL_0:2;
    hence Cage(C,n) is weakly-one-to-one by Th7;
   end;
  end;

  theorem Th15: :: JORDAN5B:24
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st p in L~f & f is weakly-one-to-one holds
    B_Cut(f,p,p) = <*p*>
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: p in L~f and
     A2: f is weakly-one-to-one;
    A3: Index(p,f) < len f by A1,JORDAN3:41;
    then A4: 1 <= Index(p,f) & Index(p,f)+1 <= len f by A1,JORDAN3:41,NAT_1:13;
    then A5: Index(p,f) in dom f & Index(p,f) + 1 in dom f by GOBOARD2:3;
    f.Index(p,f) <> f.(Index(p,f)+1) by A3,A2,A4,Def2;
    then f.Index(p,f) <> f/.(Index(p,f)+1) by A5,FINSEQ_4:def 4;
    then A6: f/.Index(p,f) <> f/.(Index(p,f)+1) by A5,FINSEQ_4:def 4;
    p in LSeg(f,Index(p,f)) by A1,JORDAN3:42;
    then p in LSeg(f/.Index(p,f), f/.(Index(p,f)+1)) by A4,TOPREAL1:def 5;
    then A7: LE p,p,f/.Index(p,f),f/.(Index(p,f)+1) by A6,JORDAN5B:9;
    L_Cut(f,p).1 = p by A1,JORDAN3:58;
    then R_Cut(L_Cut(f,p),p) = <*p*> by JORDAN3:def 5;
    hence thesis by A7,JORDAN3:def 8;
   end;

theorem Th16:
  for f being FinSequence st f is one-to-one holds
    f is weakly-one-to-one
  proof
    let f be FinSequence;
    assume
A1: f is one-to-one;
    for i be Element of NAT st 1 <= i & i < len f holds f.i <> f.(i+1)
    proof
      let i be Element of NAT;
      assume 1 <= i & i < len f; then
      i in dom f & i+1 in dom f & i <> i+1 by MSUALG_8:1;
      hence thesis by A1,FUNCT_1:def 8;
    end;
    hence thesis by Def2;
  end;

registration
  cluster one-to-one -> weakly-one-to-one FinSequence;
  coherence by Th16;
end;

  theorem Th17:
   for f be FinSequence of TOP-REAL 2 st f is weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q) = Rev B_Cut(f,q,p)
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is weakly-one-to-one;
    let p,q be Point of TOP-REAL 2 such that
     A2: p in L~f and
     A3: q in L~f;
    per cases;
     suppose A4: p = q;
      then B_Cut(f,p,q) = <*p*> by A1,A2,Th15;
      hence B_Cut(f,p,q) = Rev B_Cut(f,q,p) by A4,FINSEQ_5:63;
     end;
     suppose that
       A5: p <> q and
       A6: Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
        LE p,q,f/.Index(p,f),f/.(Index(p,f)+1);
      A7: B_Cut(f,p,q) = R_Cut(L_Cut(f,p),q) by A2,A3,A6,JORDAN3:def 8;
      not (Index(q,f)=Index(p,f) & LE q,p,f/.(Index(q,f)),f/.(Index(q,f)+1))
       by A5,A6,JORDAN3:62;
      then Rev B_Cut(f,q,p) = Rev Rev (R_Cut(L_Cut(f,p),q))
                                                   by A6,JORDAN3:def 8;
      hence B_Cut(f,p,q) = Rev B_Cut(f,q,p) by A7,FINSEQ_6:29;
     end;
     suppose that
       p <> q and
       A8: not(Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
        LE p,q,f/.Index(p,f),f/.(Index(p,f)+1));
      A9: B_Cut(f,p,q) = Rev R_Cut(L_Cut(f,q),p) by A8,JORDAN3:def 8;
      A10: Index(q,f) < Index(p,f) or Index(p,f)=Index(q,f) &
       not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1) by A8,XXREAL_0:1;
      now assume that
        A11: Index(p,f)=Index(q,f) and
        A12: not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1);
       A13: 1 <= Index(p,f) by A2,JORDAN3:41;
       A14: Index(p,f) < len f by A2,JORDAN3:41;
       then A15: Index(p,f)+1 <= len f by NAT_1:13;
       then A16: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                                                       by A13,TOPREAL1:def 5;
       then A17: p in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A2,JORDAN3:42;
       A18: q in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A3,A11,A16,JORDAN3:42
;
       A19: Index(p,f) in dom f by A13,A14,FINSEQ_3:27;
       1 <= Index(p,f)+1 by NAT_1:11;
       then A20: Index(p,f)+1 in dom f by A15,FINSEQ_3:27;
       f.Index(p,f) <> f.(Index(p,f)+1) by A1,A13,A14,Def2;
       then f.Index(p,f) <> f/.(Index(p,f)+1) by A20,FINSEQ_4:def 4;
       then f/.Index(p,f) <> f/.(Index(p,f)+1) by A19,FINSEQ_4:def 4;
       then LT q,p,f/.Index(p,f),f/.(Index(p,f)+1) by A12,A17,A18,JORDAN3:63;
       hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A11,JORDAN3:def 7;
      end;
      hence B_Cut(f,p,q) = Rev B_Cut(f,q,p) by A2,A3,A9,A10,JORDAN3:def 8;
     end;
   end;

  theorem Th18:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2
   for i1 be Element of NAT st f is poorly-one-to-one unfolded s.n.c. &
     1<i1 & i1<=len f & p=f.i1 holds
    Index(p,f) + 1 = i1
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2,i1 be Element of NAT;
    assume A1: f is poorly-one-to-one unfolded s.n.c.;
    assume A2: 1<i1 & i1<=len f;
    then A3: i1 in dom f by FINSEQ_3:27;
    consider j being Nat such that
     A4: i1 = j+1 by A2,NAT_1:6;
     reconsider j as Element of NAT by ORDINAL1:def 13;
    A5: 1 + 0 <= j by A2,A4,NAT_1:13;
    assume A6: p=f.i1;
    then p = f/.i1 by A3,FINSEQ_4:def 4;
    then A7: p in LSeg(f,j) by A2,A4,A5,TOPREAL1:27;
    then A8: Index(p,f) <= j by JORDAN3:40;
    assume Index(p,f) + 1 <> i1;
    then Index(p,f) < j by A4,A8,XXREAL_0:1;
    then A9: Index(p,f) + 1 <= j by NAT_1:13;
    A10:  LSeg(f,j) c= L~f by TOPREAL3:26;
    then A11: p in LSeg(f,Index(p,f)) by A7,JORDAN3:42;
    per cases by A9,XXREAL_0:1;
     suppose A12: Index(p,f) + 1 = j;
      A13: 1 <= Index(p,f) & Index(p,f) + (1+1) <= len f
       by A2,A4,A7,A10,A12,JORDAN3:41;
      then LSeg(f,Index(p,f)) /\ LSeg(f,j) = {f/.j} by A1,A12,TOPREAL1:def 8;
      then A14: p in {f/.j} by A7,A11,XBOOLE_0:def 3;
      A15: j < len f by A2,A4,NAT_1:13;
      then j in dom f by A5,FINSEQ_3:27;
      then A16: f.j = f/.j by FINSEQ_4:def 4
         .= f.i1 by A6,A14,TARSKI:def 1;
      Index(p,f) + 2 >= 1+2 by A13,XREAL_1:9;
      then len f >= 2+1 by A2,A4,A12,XXREAL_0:2;
      then len f > 2 by NAT_1:13;
      hence contradiction by A1,A4,A5,A15,A16,Def3;
     end;
     suppose A17: Index(p,f) + 1 < j;
      p in LSeg(f,Index(p,f)) /\ LSeg(f,j) by A7,A11,XBOOLE_0:def 3;
      then LSeg(f,Index(p,f)) meets LSeg(f,j) by XBOOLE_0:4;
      hence contradiction by A1,A17,TOPREAL1:def 9;
     end;
   end;

  theorem Th19:
   for f be FinSequence of TOP-REAL 2 st f is weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q)/.1 = p
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is weakly-one-to-one;
    let p,q be Point of TOP-REAL 2;
    assume A2: p in L~f & q in L~f;
    A3: 1 <= len L_Cut(f,p) by A2,Th1;
    A4: 1 <= Index(p,f) & Index(p,f) < len f by A2,JORDAN3:41;
    then A5: Index(p,f) in dom f by FINSEQ_3:27;
    per cases;
     suppose A6: p <> q;
      now per cases;
       suppose A7: p in L~f & q in L~f & Index(p,f)<Index(q,f);
        then q in L~L_Cut(f,p) by JORDAN3:64;
        then (R_Cut(L_Cut(f,p),q))/.1 = L_Cut(f,p)/.1 by SPRECT_3:39
           .= L_Cut(f,p).1 by A3,FINSEQ_4:24
           .= p by A7,JORDAN3:58;
        hence B_Cut(f,p,q)/.1 = p by A7,JORDAN3:def 8;
       end;
       suppose A8: Index(p,f)=Index(q,f) &
         LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
        A9: q in L~L_Cut(f,p) by A2,A8,A6,JORDAN3:66;
        (R_Cut(L_Cut(f,p),q))/.1 = L_Cut(f,p)/.1 by A9,SPRECT_3:39
           .= L_Cut(f,p).1 by A3,FINSEQ_4:24
           .= p by A2,JORDAN3:58;
        hence B_Cut(f,p,q)/.1 = p by A8,JORDAN3:def 8;
       end;
       suppose A10: not (p in L~f & q in L~f & Index(p,f)<Index(q,f)) &
         not(Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
        then A11: B_Cut(f,p,q) = Rev (R_Cut(L_Cut(f,q),p)) by JORDAN3:def 8;
        now per cases by A2,A10,REAL_1:def 5;
         suppose Index(p,f) > Index(q,f);
          then A12: p in L~L_Cut(f,q) by A2,JORDAN3:64;
          R_Cut(L_Cut(f,q),p) <> {} by JORDAN1J:44;
          hence B_Cut(f,p,q)/.1 = R_Cut(L_Cut(f,q),p)/.len R_Cut(L_Cut(f,q),p)
                                                            by A11,FINSEQ_5:68
             .= p by A12,JORDAN1J:45;
         end;
         suppose A13: Index(p,f) = Index(q,f);
          set Ls = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1));
          A14: Index(p,f)+1 <= len f by A4,NAT_1:13;
          Index(p,f)+1 >= 1 by NAT_1:11;
          then A15: Index(p,f)+1 in dom f by A14,FINSEQ_3:27;
          f.Index(p,f) <> f.(Index(p,f)+1) by A4,A1,Def2;
          then f.Index(p,f) <> f/.(Index(p,f)+1) by A15,FINSEQ_4:def 4;
          then A16: f/.Index(p,f) <> f/.(Index(p,f)+1) by A5,FINSEQ_4:def 4;
          then A17: Ls is_an_arc_of f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                               by TOPREAL1:15;
          A18: not LE p,q,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                   by A10,A13,A16,JORDAN5C:17;
          A19: p in Ls by A2,JORDAN5B:32;
          A20: R_Cut(L_Cut(f,q),p) <> {} by JORDAN1J:44;
          q in Ls by A2,A13,JORDAN5B:32;
          then LE q,p,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                 by A6,A17,A19,A18,JORDAN5C:14;
          then LE q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A16,JORDAN5C:17;
          then A21: p in L~(L_Cut(f,q)) by A2,A6,A13,JORDAN3:66;
          thus B_Cut(f,p,q)/.1 = R_Cut(L_Cut(f,q),p)/.len R_Cut(L_Cut(f,q),p)
                                                        by A11,A20,FINSEQ_5:68
             .= p by A21,JORDAN1J:45;
         end;
        end;
        hence B_Cut(f,p,q)/.1 = p;
       end;
      end;
      hence thesis;
     end;
     suppose p = q;
      then B_Cut(f,p,q) = <*p*> by A2,A1,Th15;
      hence B_Cut(f,p,q)/.1 = p by FINSEQ_4:25;
     end;
   end;

  theorem Th20:
   for f be FinSequence of TOP-REAL 2 st f is weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q)/.len B_Cut(f,p,q) = q
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is weakly-one-to-one;
    let p,q be Point of TOP-REAL 2 such that
     A2: p in L~f and
     A3: q in L~f;
    A4: B_Cut(f,q,p) <> {} by Th3;
    B_Cut(f,p,q) = Rev B_Cut(f,q,p) by A1,A2,A3,Th17;
    hence B_Cut(f,p,q)/.len B_Cut(f,p,q) = Rev B_Cut(f,q,p)/.len B_Cut(f,q,p)
                                                            by FINSEQ_5:def 3
       .= B_Cut(f,q,p)/.1 by A4,FINSEQ_5:68
       .= q by A1,A2,A3,Th19;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st p in L~f holds
    L~L_Cut(f,p) c= L~f
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2 such that
     A1: p in L~f;
    len f <> 0 by A1,TOPREAL1:28;
    then len f > 0 by NAT_1:3;
    then A2: len f >= 0+1 by NAT_1:13;
    then A3: len f in dom f by FINSEQ_3:27;
    A4: 1<=Index(p,f) & Index(p,f)<len f by A1,JORDAN3:41;
    then A5: 1<Index(p,f)+1 & Index(p,f)+1<=len f by NAT_1:13;
    then A6: Index(p,f)+1 in dom f by FINSEQ_3:27;
    per cases;
     suppose p = f.(Index(p,f)+1);
      then L_Cut(f,p) = mid(f,Index(p,f)+1,len f) by JORDAN3:def 4;
      hence thesis by A2,A5,JORDAN4:47;
     end;
     suppose p <> f.(Index(p,f)+1);
      then A7: L_Cut(f,p) = <*p*>^mid(f,Index(p,f)+1,len f) by JORDAN3:def 4;
      len mid(f,Index(p,f)+1,len f) >= 0+1 by A6,A3,SPRECT_2:9;
      then mid(f,Index(p,f)+1,len f) <> {} by FINSEQ_1:25;
      then A8: L~(<*p*>^mid(f,Index(p,f)+1,len f))
         = LSeg(p,mid(f,Index(p,f)+1,len f)/.1) \/ L~mid(f,Index(p,f)+1,len f)
                                                             by SPPOL_2:20;
      A9: mid(f,Index(p,f)+1,len f)/.1 = f/.(Index(p,f)+1)
                                                       by A6,A3,SPRECT_2:12;
      p in LSeg(f,Index(p,f)) by A1,JORDAN3:42;
      then A10: p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
                                                     by A4,A5,TOPREAL1:def 5;
      f/.(Index(p,f)+1) in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
                                                                by TOPREAL1:6;
      then A11: LSeg(p,mid(f,Index(p,f)+1,len f)/.1) c=
       LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A9,A10,TOPREAL1:12;
      LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) c= L~f by A4,A5,SPPOL_2:16;
      then A12: LSeg(p,mid(f,Index(p,f)+1,len f)/.1) c= L~f by A11,XBOOLE_1:1;
      L~mid(f,Index(p,f)+1,len f) c= L~f by A2,A5,JORDAN4:47;
      hence L~(L_Cut(f,p)) c= L~f by A7,A8,A12,XBOOLE_1:8;
     end;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f &
     f is weakly-one-to-one holds
    L~B_Cut(f,p,q) c= L~f
   proof
    let f be FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2 such that
     A1: p in L~f and
     A2: q in L~f and
     A3: f is weakly-one-to-one;
    per cases;
     suppose p = q;
      then B_Cut(f,p,q) = <*p*> by A1,A3,Th15;
      then len B_Cut(f,p,q) = 1 by FINSEQ_1:56;
      then L~B_Cut(f,p,q) = {} by TOPREAL1:28;
      hence thesis by XBOOLE_1:2;
     end;
     suppose p <> q & (Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
       LE p,q,f/.Index(p,f),f/.(Index(p,f)+1));
      hence thesis by A1,A2,JORDAN5B:36;
     end;
     suppose that
       A4: p <> q and
       A5: not(Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
        LE p,q,f/.Index(p,f),f/.(Index(p,f)+1));
      A6: B_Cut(f,p,q)=Rev R_Cut(L_Cut(f,q),p) by A5,JORDAN3:def 8;
      A7: Index(q,f) < Index(p,f) or
       Index(p,f)=Index(q,f) & not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1)
                                                              by A5,XXREAL_0:1;
      A8: now assume that
        A9: Index(p,f)=Index(q,f) and
        A10: not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1);
       A11: 1 <= Index(p,f) by A1,JORDAN3:41;
       A12: Index(p,f) < len f by A1,JORDAN3:41;
       then A13: Index(p,f)+1 <= len f by NAT_1:13;
       then A14: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                                                       by A11,TOPREAL1:def 5;
       then A15: p in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A1,JORDAN3:42;
       A16: q in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A2,A9,A14,JORDAN3:42;
       A17: Index(p,f) in dom f by A11,A12,FINSEQ_3:27;
       1 <= Index(p,f)+1 by NAT_1:11;
       then A18: Index(p,f)+1 in dom f by A13,FINSEQ_3:27;
       f.Index(p,f) <> f.(Index(p,f)+1) by A3,A11,A12,Def2;
       then f/.Index(p,f) <> f.(Index(p,f)+1) by A17,FINSEQ_4:def 4;
       then f/.Index(p,f) <> f/.(Index(p,f)+1) by A18,FINSEQ_4:def 4;
       then LT q,p,f/.Index(p,f),f/.(Index(p,f)+1) by A10,A15,A16,JORDAN3:63;
       hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A9,JORDAN3:def 7;
      end;
      then A19:  Rev B_Cut(f,q,p) = B_Cut(f,p,q) by A1,A2,A6,A7,JORDAN3:def 8;
      L~B_Cut(f,q,p) c= L~f by A1,A2,A4,A7,A8,JORDAN5B:36;
      hence thesis by A19,SPPOL_2:22;
     end;
   end;

  theorem Th23:
   for f,g be FinSequence holds dom f c= dom (f^'g)
   proof
    let f,g be FinSequence;
    len f <= len(f^'g) by TOPREAL8:7;
    hence dom f c= dom (f^'g) by FINSEQ_3:32;
   end;

  theorem
   for f be non empty FinSequence
   for g be FinSequence holds dom g c= dom (f^'g)
   proof
    let f be non empty FinSequence;
    let g be FinSequence;
    len g <= len(f^'g) by TOPREAL8:9;
    hence dom g c= dom (f^'g) by FINSEQ_3:32;
   end;

  theorem Th25:
   for f,g be FinSequence st f^'g is constant holds
    f is constant
   proof
    let f,g be FinSequence;
    assume f^'g is constant;
    then reconsider h = f^'g as constant FinSequence;
    rng f c= rng h by TOPREAL8:10;
    then rng f is trivial by SPRECT_1:1;
    hence f is constant by SPRECT_1:2;
   end;

  theorem
   for f,g be FinSequence st f^'g is constant & f.len f = g.1 & f <> {} holds
    g is constant
   proof
    let f,g be FinSequence;
    assume that
     A1: f^'g is constant and
     A2: f.len f = g.1 and
     A3: f <> {};
    reconsider h = f^'g as constant FinSequence by A1;
    per cases;
     suppose g <> {};
      then rng h = rng f \/ rng g by A2,A3,GRAPH_2:18;
      then rng g c= rng h by XBOOLE_1:7;
      then rng g is trivial by SPRECT_1:1;
      hence g is constant by SPRECT_1:2;
     end;
     suppose g = {};
      hence g is constant;
     end;
   end;

  theorem Th27:
   for f be special FinSequence of TOP-REAL 2
   for i,j be Element of NAT holds
    mid(f,i,j) is special
   proof
    let f be special FinSequence of TOP-REAL 2;
    let i,j be Element of NAT;
    per cases;
     suppose A1: i <= j;
      mid(f,i,j) = (f/^(i-'1))|(j-'i+1) by A1,JORDAN3:def 1;
      hence mid(f,i,j) is special;
     end;
     suppose A2: j < i;
      mid(f,j,i) = (f/^(j-'1))|(i-'j+1) by A2,JORDAN3:def 1;
      then Rev mid(f,i,j) is special by JORDAN4:30;
      then Rev Rev mid(f,i,j) is special by SPPOL_2:42;
      hence mid(f,i,j) is special by FINSEQ_6:29;
     end;
   end;

  theorem Th28:
   for f be unfolded FinSequence of TOP-REAL 2
   for i,j be Element of NAT holds
    mid(f,i,j) is unfolded
   proof
    let f be unfolded FinSequence of TOP-REAL 2;
    let i,j be Element of NAT;
    per cases;
     suppose A1: i <= j;
      mid(f,i,j) = (f/^(i-'1))|(j-'i+1) by A1,JORDAN3:def 1;
      hence mid(f,i,j) is unfolded;
     end;
     suppose A2: j < i;
      mid(f,j,i) = (f/^(j-'1))|(i-'j+1) by A2,JORDAN3:def 1;
      then Rev mid(f,i,j) is unfolded by JORDAN4:30;
      then Rev Rev mid(f,i,j) is unfolded by SPPOL_2:29;
      hence mid(f,i,j) is unfolded by FINSEQ_6:29;
     end;
   end;

  theorem Th29:
   for f be FinSequence of TOP-REAL 2 st f is special
   for p be Point of TOP-REAL 2 st p in L~f holds
    L_Cut(f,p) is special
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is special;
    let p be Point of TOP-REAL 2;
    assume A2: p in L~f;
    A3: <*p*> is special by SPPOL_2:39;
    len f <> 0 by A2,TOPREAL1:28;
    then len f > 0 by NAT_1:3;
    then A4: len f >= 0+1 by NAT_1:13;
    A5: Index(p,f)+1 >= 1 by NAT_1:11;
    A6: mid(f,Index(p,f)+1,len f) is special by A1,Th27;
    A7: len <*p*> = 1 by FINSEQ_1:56;
    A8: <*p*>/.1 = p by FINSEQ_4:25;
    A9: 1 <= Index(p,f) & Index(p,f) < len f by A2,JORDAN3:41;
    then A10: Index(p,f)+1 <= len f by NAT_1:13;
    then Index(p,f)+1 in dom f & len f in dom f by A4,A5,FINSEQ_3:27;
    then A11: mid(f,Index(p,f)+1,len f)/.1 = f/.(Index(p,f)+1) by SPRECT_2:12;
    A12: p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) ) by A2,JORDAN5B:32;
    A13: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f), f/.(Index(p,f)+1))
                                                    by A10,A9,TOPREAL1:def 5;
    A14: now per cases by A1,SPPOL_1:41;
     case LSeg(f,Index(p,f)) is vertical;
      hence p`1=(f/.(Index(p,f)+1))`1 by A12,A13,SPPOL_1:64;
     end;
     case LSeg(f,Index(p,f)) is horizontal;
      hence p`2=(f/.(Index(p,f)+1))`2 by A12,A13,SPPOL_1:63;
     end;
    end;
    per cases;
     suppose p <> f.(Index(p,f)+1);
      then L_Cut(f,p) = <*p*>^mid(f,Index(p,f)+1,len f) by JORDAN3:def 4;
      hence L_Cut(f,p) is special by A3,A6,A14,A7,A8,A11,GOBOARD2:13;
     end;
     suppose p = f.(Index(p,f)+1);
      hence L_Cut(f,p) is special by A6,JORDAN3:def 4;
     end;
   end;

  theorem Th30:
   for f be FinSequence of TOP-REAL 2 st f is special
   for p be Point of TOP-REAL 2 st p in L~f holds
    R_Cut(f,p) is special
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is special;
    let p be Point of TOP-REAL 2;
    assume A2: p in L~f;
    A3: <*p*> is special by SPPOL_2:39;
    len f <> 0 by A2,TOPREAL1:28;
    then len f > 0 by NAT_1:3;
    then A4: len f >= 0+1 by NAT_1:13;
    A5: 1 <= Index(p,f) & Index(p,f) < len f by A2,JORDAN3:41;
    A6: mid(f,1,Index(p,f)) is special by A1,Th27;
    A7: <*p*>/.1 = p by FINSEQ_4:25;
    A8: Index(p,f)+1 <= len f by A5,NAT_1:13;
    Index(p,f) in dom f & 1 in dom f by A4,A5,FINSEQ_3:27;
    then A9: mid(f,1,Index(p,f))/.len mid(f,1,Index(p,f)) = f/.Index(p,f)
                                                               by SPRECT_2:13;
    A10: p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) ) by A2,JORDAN5B:32;
    A11: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f), f/.(Index(p,f)+1))
                                                    by A8,A5,TOPREAL1:def 5;
    A12: now per cases by A1,SPPOL_1:41;
     case LSeg(f,Index(p,f)) is vertical;
      hence p`1=(f/.Index(p,f))`1 by A10,A11,SPPOL_1:64;
     end;
     case LSeg(f,Index(p,f)) is horizontal;
      hence p`2=(f/.Index(p,f))`2 by A10,A11,SPPOL_1:63;
     end;
    end;
    per cases;
     suppose p <> f.1;
      then R_Cut(f,p) = mid(f,1,Index(p,f))^<*p*> by JORDAN3:def 5;
      hence R_Cut(f,p) is special by A3,A6,A12,A7,A9,GOBOARD2:13;
     end;
     suppose p = f.1;
      hence R_Cut(f,p) is special by A3,JORDAN3:def 5;
     end;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2 st f is special weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q) is special
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is special weakly-one-to-one;
    let p,q be Point of TOP-REAL 2;
    assume A2: p in L~f & q in L~f;
    A3: 1 <= Index(p,f) & Index(p,f) < len f by A2,JORDAN3:41;
    then A4: Index(p,f) in dom f by FINSEQ_3:27;
    per cases;
     suppose A5: p <> q;
      now per cases;
       suppose A6: p in L~f & q in L~f & Index(p,f) < Index(q,f);
        then A7: q in L~L_Cut(f,p) by JORDAN3:64;
        L_Cut(f,p) is special by A1,A2,Th29;
        then R_Cut(L_Cut(f,p),q) is special by A7,Th30;
        hence B_Cut(f,p,q) is special by A6,JORDAN3:def 8;
       end;
       suppose A8: Index(p,f)=Index(q,f) &
         LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
        then A9: q in L~L_Cut(f,p) by A2,A5,JORDAN3:66;
        L_Cut(f,p) is special by A1,A2,Th29;
        then R_Cut(L_Cut(f,p),q) is special by A9,Th30;
        hence B_Cut(f,p,q) is special by A8,JORDAN3:def 8;
       end;
       suppose A10: not (p in L~f & q in L~f & Index(p,f) < Index(q,f) or
         Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
        then A11: B_Cut(f,p,q) = Rev (R_Cut(L_Cut(f,q),p)) by JORDAN3:def 8;
        A12: now per cases by A2,A10,REAL_1:def 5;
         suppose Index(q,f) < Index(p,f);
          hence p in L~L_Cut(f,q) by A2,JORDAN3:64;
         end;
         suppose A13: Index(q,f) = Index(p,f);
          set Ls = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1));
          A14: Index(p,f)+1 <= len f by A3,NAT_1:13;
          Index(p,f)+1 >= 1 by NAT_1:11;
          then A15: Index(p,f)+1 in dom f by A14,FINSEQ_3:27;
          f.Index(p,f) <> f.(Index(p,f)+1) by A3,A1,Def2;
          then f.Index(p,f) <> f/.(Index(p,f)+1) by A15,FINSEQ_4:def 4;
          then A16: f/.Index(p,f) <> f/.(Index(p,f)+1) by A4,FINSEQ_4:def 4;
          then A17: Ls is_an_arc_of f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                               by TOPREAL1:15;
          A18: not LE p,q,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                    by A10,A13,A16,JORDAN5C:17;
          A19: p in Ls by A2,JORDAN5B:32;
          q in Ls by A2,A13,JORDAN5B:32;
          then LE q,p,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                 by A5,A17,A19,A18,JORDAN5C:14;
          then LE q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A16,JORDAN5C:17;
          hence p in L~L_Cut(f,q) by A2,A5,A13,JORDAN3:66;
         end;
        end;
        L_Cut(f,q) is special by A1,A2,Th29;
        then R_Cut(L_Cut(f,q),p) is special by A12,Th30;
        hence B_Cut(f,p,q) is special by A11,SPPOL_2:42;
       end;
      end;
      hence B_Cut(f,p,q) is special;
     end;
     suppose p = q;
      then B_Cut(f,p,q) = <*p*> by A1,A2,Th15;
      hence B_Cut(f,p,q) is special by SPPOL_2:39;
     end;
   end;

  theorem Th32:
   for f be FinSequence of TOP-REAL 2 st f is unfolded
   for p be Point of TOP-REAL 2 st p in L~f holds
    L_Cut(f,p) is unfolded
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is unfolded;
    let p be Point of TOP-REAL 2;
    assume A2: p in L~f;
    len f <> 0 by A2,TOPREAL1:28;
    then len f > 0 by NAT_1:3;
    then A3: len f >= 0+1 by NAT_1:13;
    A4: mid(f,Index(p,f)+1,len f) is unfolded by A1,Th28;
    A5: 1 <= Index(p,f) & Index(p,f) < len f by A2,JORDAN3:41;
    then A6: 1<Index(p,f)+1 & Index(p,f)+1+0 <= len f by NAT_1:13;
    then A7: Index(p,f)+1 in dom f & len f in dom f by A3,FINSEQ_3:27;
    then A8: mid(f,Index(p,f)+1,len f)/.1 = f/.(Index(p,f)+1) by SPRECT_2:12;
    A9: p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) ) by A2,JORDAN5B:32;
    A10: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f), f/.(Index(p,f)+1))
                                                    by A6,A5,TOPREAL1:def 5;
    per cases;
     suppose A11: Index(p,f)+1+1 <= len f;
      then A12: LSeg(f,Index(p,f)+1) =
        LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) by A6,TOPREAL1:def 5;
      A13: len f -(Index(p,f)+1) >= 0 by A6,XREAL_1:21;
      2+(Index(p,f)+1)-1 = Index(p,f)+1+1;
      then A14: 2+(Index(p,f)+1)-1 >= 0 by NAT_1:2;
      A15: len mid(f,Index(p,f)+1,len f) = len f-'(Index(p,f)+1)+1
                                                           by A3,A6,JORDAN3:27
           .= len f -(Index(p,f)+1)+1 by A13,BINARITH:def 3
           .= len f - Index(p,f);
      Index(p,f)+(1+1) <= len f by A11;
      then A16: 2 <= len f - Index(p,f) by XREAL_1:21;
      then 2 in dom mid(f,Index(p,f)+1,len f) by A15,FINSEQ_3:27;
      then (mid(f,Index(p,f)+1,len f))/.2 = f/.(2+(Index(p,f)+1)-'1)
                                                          by A6,A7,SPRECT_2:7
         .= f/.(Index(p,f)+(1+1)) by A14,BINARITH:def 3;
      then A17: LSeg(mid(f,Index(p,f)+1,len f),1) =
                LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1)))
                                                by A8,A16,A15,TOPREAL1:def 5;
      A18: LSeg(f/.Index(p,f), f/.(Index(p,f)+1)) /\
           LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) = {f/.(Index(p,f)+1)}
                                        by A1,A5,A10,A12,A11,TOPREAL1:def 8;
      f/.(Index(p,f)+1) in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by TOPREAL1:6;
      then LSeg(p,f/.(Index(p,f)+1)) c= LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                                                           by A9,TOPREAL1:12;
      then A19: LSeg(p,f/.(Index(p,f)+1)) /\
         LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) c= {f/.(Index(p,f)+1)}
                                                           by A18,XBOOLE_1:26;
      f/.(Index(p,f)+1) in LSeg(p,f/.(Index(p,f)+1)) &
       f/.(Index(p,f)+1) in LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1)))
                                                                by TOPREAL1:6;
      then f/.(Index(p,f)+1) in LSeg(p,f/.(Index(p,f)+1)) /\
           LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) by XBOOLE_0:def 3;
      then {f/.(Index(p,f)+1)} c= LSeg(p,f/.(Index(p,f)+1)) /\
           LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) by ZFMISC_1:37;
      then A20: LSeg(p,f/.(Index(p,f)+1)) /\
           LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) = {f/.(Index(p,f)+1)}
                                                      by A19,XBOOLE_0:def 10;
      now per cases;
       suppose p <> f.(Index(p,f)+1);
        then L_Cut(f,p) = <*p*>^mid(f,Index(p,f)+1,len f) by JORDAN3:def 4;
        hence L_Cut(f,p) is unfolded by A4,A8,A20,A17,SPPOL_2:30;
       end;
       suppose p = f.(Index(p,f)+1);
        hence L_Cut(f,p) is unfolded by A4,JORDAN3:def 4;
       end;
      end;
      hence L_Cut(f,p) is unfolded;
     end;
     suppose A21: Index(p,f)+1+1 > len f;
      A22: Index(p,f)+1 < len f or Index(p,f)+1 = len f by A6,REAL_1:def 5;
      now per cases;
       suppose p <> f.(Index(p,f)+1);
        then L_Cut(f,p) = <*p*>^mid(f,len f,len f)
               by A21,A22,JORDAN3:def 4,NAT_1:13
           .= <*p*>^<*f/.len f*> by A3,JORDAN4:27
           .= <*p,f/.len f*> by FINSEQ_1:def 9;
        then len L_Cut(f,p) = 2 by FINSEQ_1:61;
        hence L_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
       suppose p = f.(Index(p,f)+1);
        then L_Cut(f,p) = mid(f,len f,len f)
           by A21,A22,JORDAN3:def 4,NAT_1:13
           .= <*f/.len f*> by A3,JORDAN4:27;
        then len L_Cut(f,p) = 1 by FINSEQ_1:56;
        hence L_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
      end;
      hence L_Cut(f,p) is unfolded;
     end;
   end;

  theorem Th33:
   for f be FinSequence of TOP-REAL 2 st f is unfolded
   for p be Point of TOP-REAL 2 st p in L~f holds
    R_Cut(f,p) is unfolded
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is unfolded;
    let p be Point of TOP-REAL 2;
    assume A2: p in L~f;
    len f <> 0 by A2,TOPREAL1:28;
    then len f > 0 by NAT_1:3;
    then A3: len f >= 0+1 by NAT_1:13;
    A4: 1 <= Index(p,f) & Index(p,f) < len f by A2,JORDAN3:41;
    then A5: 1<Index(p,f)+1 & Index(p,f)+1+0 <= len f by NAT_1:13;
    A6: Index(p,f)-'1+1 = Index(p,f) by A4,BINARITH:53;
    Index(p,f) in Seg len f & 1 in Seg len f by A3,A4,FINSEQ_1:3;
    then A7: Index(p,f) in dom f & 1 in dom f by FINSEQ_1:def 3;
    then A8: mid(f,1,Index(p,f))/.len mid(f,1,Index(p,f)) = f/.Index(p,f)
                                                               by SPRECT_2:13;
    A9: mid(f,1,Index(p,f)) is unfolded by A1,Th28;
    A10: p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) ) by A2,JORDAN5B:32;
    A11: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f), f/.(Index(p,f)+1))
                                                    by A5,A4,TOPREAL1:def 5;
    per cases by A4,REAL_1:def 5;
     suppose Index(p,f) > 0+1;
      then Index(p,f)-1 > 0 by XREAL_1:22;
      then Index(p,f)-'1 > 0 by BINARITH:def 3;
      then A12: Index(p,f)-'1 >= 0+1 by NAT_1:13;
      A13: LSeg(f,Index(p,f)-'1) = LSeg(f/.(Index(p,f)-'1),f/.(Index(p,f)))
         by A4,A12,A6,TOPREAL1:def 5;
      A14: len mid(f,1,Index(p,f)) = Index(p,f)-'1+1 by A3,A4,JORDAN3:27
           .= Index(p,f) by A4,BINARITH:53;
      len mid(f,1,Index(p,f))-'1 <= len mid(f,1,Index(p,f)) by BINARITH:52;
      then len mid(f,1,Index(p,f))-'1 in Seg len mid(f,1,Index(p,f))
                                                        by A12,A14,FINSEQ_1:3;
      then len mid(f,1,Index(p,f))-'1 in dom mid(f,1,Index(p,f))
                                                           by FINSEQ_1:def 3;
      then A15: mid(f,1,Index(p,f))/.(len mid(f,1,Index(p,f))-'1) =
            f/.(len mid(f,1,Index(p,f))-'1+1-'1) by A4,A7,SPRECT_2:7
         .= f/.(len mid(f,1,Index(p,f))-'1) by BINARITH:39
         .= f/.(Index(p,f)-'1+1-'1) by A4,JORDAN4:20
         .= f/.(Index(p,f)-'1) by A4,BINARITH:53;
      len mid(f,1,Index(p,f))-'1+1=len mid(f,1,Index(p,f))
       by A4,A14,BINARITH:53;
      then A16: LSeg(mid(f,1,Index(p,f)),len mid(f,1,Index(p,f))-'1) =
       LSeg(f/.(Index(p,f)-'1),f/.Index(p,f))
                                            by A8,A12,A15,A14,TOPREAL1:def 5;
      Index(p,f)-'1+(1+1) <= len f by A5,A6;
      then A17: LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
           LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) = {f/.Index(p,f)}
                                         by A1,A11,A12,A13,A6,TOPREAL1:def 8;
      f/.Index(p,f) in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by TOPREAL1:6;
      then LSeg(f/.Index(p,f),p) c= LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                                                           by A10,TOPREAL1:12;
      then A18: LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
          LSeg(f/.Index(p,f),p) c= {f/.Index(p,f)} by A17,XBOOLE_1:26;
      f/.Index(p,f) in LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) &
       f/.Index(p,f) in LSeg(f/.Index(p,f),p) by TOPREAL1:6;
      then f/.Index(p,f) in LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
           LSeg(f/.Index(p,f),p) by XBOOLE_0:def 3;
      then A19: {f/.Index(p,f)} c= LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
           LSeg(f/.Index(p,f),p) by ZFMISC_1:37;
      A20: len mid(f,1,Index(p,f))-'1+1 = len mid(f,1,Index(p,f))
                                               by A4,A14,BINARITH:53;
      A21: LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
          LSeg(f/.Index(p,f),p) = {f/.Index(p,f)} by A18,A19,XBOOLE_0:def 10;
      now per cases;
       suppose p <> f.1;
        then R_Cut(f,p) = mid(f,1,Index(p,f))^<*p*> by JORDAN3:def 5;
        hence R_Cut(f,p) is unfolded by A9,A8,A16,A20,A21,SPPOL_2:31;
       end;
       suppose p = f.1;
        then R_Cut(f,p) = <*p*> by JORDAN3:def 5;
        then len R_Cut(f,p) = 1 by FINSEQ_1:56;
        hence R_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
      end;
      hence R_Cut(f,p) is unfolded;
     end;
     suppose A22: Index(p,f) = 0+1;
      now per cases;
       suppose p <> f.1;
        then R_Cut(f,p) = mid(f,1,1)^<*p*> by A22,JORDAN3:def 5
           .= <*f/.1 *>^<*p*> by A3,JORDAN4:27
           .= <*f/.1,p*> by FINSEQ_1:def 9;
        then len R_Cut(f,p) = 2 by FINSEQ_1:61;
        hence R_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
       suppose p = f.1;
        then R_Cut(f,p) = <*p*> by JORDAN3:def 5;
        then len R_Cut(f,p) = 1 by FINSEQ_1:56;
        hence R_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
      end;
      hence R_Cut(f,p) is unfolded;
     end;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2 st f is unfolded weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q) is unfolded
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A1: f is unfolded weakly-one-to-one;
    let p,q be Point of TOP-REAL 2;
    assume A2: p in L~f & q in L~f;
    A3: 1 <= Index(p,f) & Index(p,f) < len f by A2,JORDAN3:41;
    then Index(p,f) in Seg len f by FINSEQ_1:3;
    then A4: Index(p,f) in dom f by FINSEQ_1:def 3;
    per cases;
     suppose A5: p <> q;
      now per cases;
       suppose A6: p in L~f & q in L~f & Index(p,f) < Index(q,f);
        then A7: q in L~L_Cut(f,p) by JORDAN3:64;
        L_Cut(f,p) is unfolded by A1,A2,Th32;
        then R_Cut(L_Cut(f,p),q) is unfolded by A7,Th33;
        hence B_Cut(f,p,q) is unfolded by A6,JORDAN3:def 8;
       end;
       suppose A8: Index(p,f)=Index(q,f) &
         LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
        then A9: q in L~L_Cut(f,p) by A2,A5,JORDAN3:66;
        L_Cut(f,p) is unfolded by A1,A2,Th32;
        then R_Cut(L_Cut(f,p),q) is unfolded by A9,Th33;
        hence B_Cut(f,p,q) is unfolded by A8,JORDAN3:def 8;
       end;
       suppose A10: not (p in L~f & q in L~f & Index(p,f) < Index(q,f) or
         Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
        then A11: B_Cut(f,p,q) = Rev (R_Cut(L_Cut(f,q),p)) by JORDAN3:def 8;
        A12: now per cases by A2,A10,REAL_1:def 5;
         suppose Index(q,f) < Index(p,f);
          hence p in L~L_Cut(f,q) by A2,JORDAN3:64;
         end;
         suppose A13: Index(q,f) = Index(p,f);
          set Ls = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1));
          A14: Index(p,f)+1 <= len f by A3,NAT_1:13;
          Index(p,f)+1 >= 1 by NAT_1:11;
          then Index(p,f)+1 in Seg len f by A14,FINSEQ_1:3;
          then A15: Index(p,f)+1 in dom f by FINSEQ_1:def 3;
          f.Index(p,f) <> f.(Index(p,f)+1) by A3,A1,Def2;
          then f.Index(p,f) <> f/.(Index(p,f)+1) by A15,FINSEQ_4:def 4;
          then A16: f/.Index(p,f) <> f/.(Index(p,f)+1) by A4,FINSEQ_4:def 4;
          then A17: Ls is_an_arc_of f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                               by TOPREAL1:15;
          A18: not LE p,q,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                    by A10,A13,A16,JORDAN5C:17;
          A19: p in Ls by A2,JORDAN5B:32;
          q in Ls by A2,A13,JORDAN5B:32;
          then LE q,p,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                 by A5,A17,A19,A18,JORDAN5C:14;
          then LE q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A16,JORDAN5C:17;
          hence p in L~L_Cut(f,q) by A2,A5,A13,JORDAN3:66;
         end;
        end;
        L_Cut(f,q) is unfolded by A1,A2,Th32;
        then R_Cut(L_Cut(f,q),p) is unfolded by A12,Th33;
        hence B_Cut(f,p,q) is unfolded by A11,SPPOL_2:29;
       end;
      end;
      hence B_Cut(f,p,q) is unfolded;
     end;
     suppose p = q;
      then B_Cut(f,p,q) = <*p*> by A1,A2,Th15;
      then len B_Cut(f,p,q) = 1 by FINSEQ_1:56;
      hence B_Cut(f,p,q) is unfolded by SPPOL_2:27;
     end;
   end;

  theorem Th35:
   for f,g be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p<>f.1 & g=mid(f,1,Index(p,f))^<*p*> holds
     g is_S-Seq_joining f/.1,p
   proof
    let f,g be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: f is almost-one-to-one special unfolded s.n.c. and
     A2: p in L~f and
     A3: p<>f.1 and
     A4: g=mid(f,1,Index(p,f))^<*p*>;
    consider i be Element of NAT such that
    A5:1<=i & i+1<=len f & p in LSeg(f,i)
     by A2,SPPOL_2:13;
    A6: 1<=Index(p,f) by A2,JORDAN3:41;
    A7: Index(p,f)<len f by A2,JORDAN3:41;
    1<=1+i by NAT_1:12;
    then A8:1<=len f by A5,XXREAL_0:2;
    then A9: len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A6,A7,JORDAN3:27;
    then A10: len mid(f,1,Index(p,f))=Index(p,f) by A6,BINARITH:53;
    1<=len <*p*> by FINSEQ_1:56;
    then A11: 1 in dom <*p*> by FINSEQ_3:27;
    g.1 = mid(f,1,Index(p,f)).1 by A4,A6,A10,JORDAN3:17;
    then g.1=f.1 by A6,A7,A8,JORDAN3:27;
    then A12: g.1=f/.1 by A8,FINSEQ_4:24;
    A13: len g = len mid(f,1,Index(p,f))+ len<*p*> by A4,FINSEQ_1:35
       .= len mid(f,1,Index(p,f))+ 1 by FINSEQ_1:56;
    then A14:  g.len g = p by A4,FINSEQ_1:59;
    A15: 1+1<=len g by A6,A10,A13,XREAL_1:8;
    A16: f is almost-one-to-one FinSequence by A1;
    A17: for x1,x2 being set st x1 in dom g & x2 in dom g & g.x1=g.x2 holds
     x1=x2
    proof
     let x1,x2 be set;
     assume A18: x1 in dom g & x2 in dom g & g.x1=g.x2;
     then reconsider n1=x1,n2=x2 as Element of NAT;
     A19: 1<=n1 & n1<=len g & 1<=n2 & n2<=len g by A18,FINSEQ_3:27;
     now
      A20:g.len g = <*p*>.1 by A4,A11,A13,FINSEQ_1:def 7
         .= p by FINSEQ_1:def 8;
      now per cases;
       case A21:n1=len g;
        now assume A22: n2<>len g; then n2<len g by A19,REAL_1:def 5;
         then A23:n2<=len mid(f,1,Index(p,f)) by A13,NAT_1:13;
         then g.n2=mid(f,1,Index(p,f)).n2 by A4,A19,FINSEQ_1:85;
         then g.n2=f.(n2+1-'1) by A6,A7,A8,A19,A23,JORDAN3:27;
         then A24:p=f.n2 by A18,A20,A21,BINARITH:39;
         A25: n2 < len f by A7,A10,A23,XXREAL_0:2;
         1 < n2 by A3,A19,A24,XXREAL_0:1;
         then Index(p,f)+1 = n2 by A16,A1,A24,A25,Th18;
         hence contradiction by A6,A9,A13,A22,BINARITH:53;
        end;
        hence x1=x2 by A21;
       end;
       case A26:n2=len g;
        now assume A27: n1<>len g; then n1<len g by A19,REAL_1:def 5;
         then A28:n1<=len mid(f,1,Index(p,f)) by A13,NAT_1:13;
         then g.n1=mid(f,1,Index(p,f)).n1 by A4,A19,FINSEQ_1:85;
         then g.n1=f.(n1+1-'1) by A6,A7,A8,A19,A28,JORDAN3:27;
         then A29:p=f.n1 by A18,A20,A26,BINARITH:39;
         A30: n1 < len f by A7,A10,A28,XXREAL_0:2;
         1 < n1 by A3,A19,A29,XXREAL_0:1;
         then Index(p,f)+1 = n1 by A16,A1,A29,A30,Th18;
         hence contradiction by A6,A9,A13,A27,BINARITH:53;
        end;
        hence x1=x2 by A26;
       end;
       case that
         A31: n1 <> len g and
         A32: n2 <> len g;
        n1 < len g by A19,A31,REAL_1:def 5;
        then A33:n1<=len mid(f,1,Index(p,f)) by A13,NAT_1:13;
        then A34:n1<len f by A7,A10,XXREAL_0:2;
        A35: g.n1=mid(f,1,Index(p,f)).n1 by A4,A19,A33,FINSEQ_1:85
           .=f.n1 by A7,A10,A19,A33,JORDAN3:32;
        n2 < len g by A19,A32,XXREAL_0:1;
        then A36:n2<=len mid(f,1,Index(p,f)) by A13,NAT_1:13;
        then A37:n2<len f by A7,A10,XXREAL_0:2;
        A38: g.n2 = mid(f,1,Index(p,f)).n2 by A4,A19,A36,FINSEQ_1:85
           .= f.n2 by A7,A10,A19,A36,JORDAN3:32;
        n1 in Seg len f & n2 in Seg len f by A19,A34,A37,FINSEQ_1:3;
        then n1 in dom f & n2 in dom f by FINSEQ_1:def 3;
        hence x1=x2 by A1,A7,A10,A18,A33,A35,A36,A38,Def1;
       end;
      end;
      hence x1=x2;
     end;
     hence x1=x2;
    end;
    A39: for j be Element of NAT st 1 <= j & j + 2 <= len g holds
     LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)}
    proof
     let j be Element of NAT;
     assume A40: 1 <= j & j + 2 <= len g;
     then j+1<=len g by BINARITH:65;
     then A41: LSeg(g,j) c= LSeg(f,j) by A2,A4,A40,JORDAN3:51;
     A42: j+1+1<=len g by A40;
     A43: 1<=j+1 by A40,BINARITH:66;
     then LSeg(g,j+1) c= LSeg(f,j+1) by A2,A4,A42,JORDAN3:51;
     then A44:LSeg(g,j)/\ LSeg(g,j+1) c= LSeg(f,j)/\ LSeg(f,j+1)
      by A41,XBOOLE_1:27;
     A45: j+1<=len g by A40,BINARITH:65;
     then LSeg(g,j)=LSeg(g/.j,g/.(j+1)) by A40,TOPREAL1:def 5;
     then A46:g/.(j+1) in LSeg(g,j) by TOPREAL1:6;
     A47: g/.(j+1)=g.(j+1) by A43,A45,FINSEQ_4:24;
     A48: Index(p,f)<=len f by A2,JORDAN3:41;
     LSeg(g,j+1)=LSeg(g/.(j+1),g/.(j+1+1)) by A40,A43,TOPREAL1:def 5;
     then g/.(j+1) in LSeg(g,j+1) by TOPREAL1:6;
     then g/.(j+1) in LSeg(g,j) /\ LSeg(g,j+1) by A46,XBOOLE_0:def 3;
     then A49:{g/.(j+1)} c= LSeg(g,j) /\ LSeg(g,j+1) by ZFMISC_1:37;
     now
      A50: len g=len mid(f,1,Index(p,f))+1 by A4,FINSEQ_2:19;
      Index(p,f)<=len f by A2,JORDAN3:41;
      then A51: len g<=len f +1 by A10,A50,XREAL_1:8;
      now per cases by A51,REAL_1:def 5;
       case len g = len f +1;
        then len f = Index(p,f) by A10,A50;
        hence contradiction by A2,JORDAN3:41;
       end;
       case len g<len f+1;
        then len g<=len f by NAT_1:13;
        then A52: j+2<=len f by A40,XXREAL_0:2;
        A53: j+1<=Index(p,f) by A10,A42,A50,XREAL_1:8;
        then A54: j+1<=len f by A48,XXREAL_0:2;
        A55: LSeg(g,j)/\ LSeg(g,j+1) c= {f/.(j+1)}
         by A1,A40,A44,A52,TOPREAL1:def 8;
        A56: f.(j+1)=f/.(j+1) by A43,A54,FINSEQ_4:24;
        g.(j+1) = mid(f,1,Index(p,f)).(j+1) by A4,A10,A43,A53,FINSEQ_1:85
           .= f.(j+1) by A7,A43,A53,JORDAN3:32;
        hence LSeg(g,j)/\ LSeg(g,j+1) = {g/.(j+1)}
         by A47,A49,A55,A56,XBOOLE_0:def 10;
       end;
      end;
      hence LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)};
     end;
     hence LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)};
    end;
    A57: for j1,j2 be Element of NAT st j1+1 < j2
    holds LSeg(g,j1) misses LSeg(g,j2)
    proof
     let j1,j2 be Element of NAT;
     assume A58:j1+1 < j2;
     j1>=0 by NAT_1:2;
     then A59: j1=0 or j1>=0+1 by NAT_1:13;
     now per cases by A59,REAL_1:def 5;
      case j1=0;
       then LSeg(g,j1)={} by TOPREAL1:def 5;
       then LSeg(g,j1) /\ LSeg(g,j2) = {};
       hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
      end;
      case that
        A60: j1=1 or j1>1 and
        A61: j2+1<=len g;
       j2<len g by A61,NAT_1:13;
       then j1+1<len g by A58,XXREAL_0:2;
       then A62: LSeg(g,j1) c= LSeg(f,j1) by A2,A4,A60,JORDAN3:51;
       1+1<=j1+1 by A60,XREAL_1:8;
       then 2<=j2 by A58,XXREAL_0:2;
       then 1<=j2 by XXREAL_0:2;
       then LSeg(g,j2) c= LSeg(f,j2) by A2,A4,A61,JORDAN3:51;
       then A63: LSeg(g,j1)/\ LSeg(g,j2) c= LSeg(f,j1)/\ LSeg(f,j2)
        by A62,XBOOLE_1:27;
       LSeg(f,j1) misses LSeg(f,j2) by A1,A58,TOPREAL1:def 9;
       then LSeg(f,j1) /\ LSeg(f,j2) = {} by XBOOLE_0:def 7;
       then LSeg(g,j1) /\ LSeg(g,j2) = {} by A63,XBOOLE_1:3;
       hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
      end;
      case j2+1>len g;
       then LSeg(g,j2)={} by TOPREAL1:def 5;
       then LSeg(g,j1)/\ LSeg(g,j2)={};
       hence thesis by XBOOLE_0:def 7;
      end;
     end;
     hence LSeg(g,j1) misses LSeg(g,j2);
    end;
    for j be Element of NAT st 1 <= j & j+1 <= len g holds
     (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
    proof
     let j be Element of NAT;
     assume A64: 1 <= j & j+1 <= len g;
     A65: now
      j+1 <= Index(p,f)+1 by A4,A10,A64,FINSEQ_2:19;
      then A66: j<=Index(p,f) by XREAL_1:8;
      Index(p,f)<len f by A2,JORDAN3:41;
      then j<len f by A66,XXREAL_0:2;
      hence j+1<=len f by NAT_1:13;
     end;
     A67: LSeg(g,j) c= LSeg(f,j) by A2,A4,A64,JORDAN3:51;
     A68: LSeg(g,j) = LSeg(g/.j,g/.(j+1)) by A64,TOPREAL1:def 5;
     A69: LSeg(f,j) = LSeg(f/.j,f/.(j+1)) by A64,A65,TOPREAL1:def 5;
     (f/.j)`1 = (f/.(j+1))`1 or (f/.j)`2 = (f/.(j+1))`2
      by A1,A64,A65,TOPREAL1:def 7;
     hence (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
      by A67,A68,A69,JORDAN3:36;
    end;
    then g is one-to-one & len g >= 2 & g is unfolded s.n.c. special
     by A15,A17,A39,A57,FUNCT_1:def 8,TOPREAL1:def 7,def 8,
def 9;
    then g is_S-Seq by TOPREAL1:def 10;
    hence g is_S-Seq_joining f/.1,p by A12,A14,JORDAN3:def 3;
   end;

  theorem Th36:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st f is poorly-one-to-one unfolded s.n.c. &
    p in L~f & p = f.(Index(p,f)+1) & p <> f.len f holds
     Index(p,Rev f) + Index(p,f) + 1 = len f
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2 such that
     A1: f is poorly-one-to-one unfolded s.n.c. and
     A2: p in L~f and
     A3: p = f.(Index(p,f)+1) and
     A4: p <> f.len f;
    A5: Rev f is poorly-one-to-one by A1,Th10;
    A6: Rev f is unfolded by A1,SPPOL_2:29;
    A7: Rev f is s.n.c. by A1,SPPOL_2:36;
    A8: len f = len Rev f by FINSEQ_5:def 3;
    len f <= len f + Index(p,f) by NAT_1:11;
    then A9: len f - Index(p,f) <= len Rev f by A8,XREAL_1:22;
    Index(p,f) < len f by A2,JORDAN3:41;
    then A10: Index(p,f)+1 <= len f by NAT_1:13;
    1 <= Index(p,f)+1 by NAT_1:11;
    then Index(p,f)+1 in dom f by A10,FINSEQ_3:27;
    then A11: Index(p,f)+1 in dom Rev f by FINSEQ_5:60;
    Index(p,f)+1 < len f by A3,A4,A10,XXREAL_0:1;
    then A12: 1 < len f - Index(p,f) by XREAL_1:22;
    Index(p,f) <= len f by A2,JORDAN3:41;
    then A13: len f - Index(p,f) = len f -' Index(p,f) by BINARITH:50;
    p = (Rev Rev f).(Index(p,f)+1) by A3,FINSEQ_6:29
       .= (Rev f).(len Rev f - (Index(p,f)+1) + 1) by A11,FINSEQ_5:61
       .= (Rev f).(len Rev f - Index(p,f))
       .= (Rev f).(len f - Index(p,f)) by FINSEQ_5:def 3;
    then A14:  Index(p,Rev f) + 1 = len f -' Index(p,f)
       by A5,A6,A7,A9,A12,A13,Th18;
    thus Index(p,Rev f) + Index(p,f) + 1 = len f by A13,A14;
   end;

  theorem
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st f is weakly-one-to-one & len f >= 2 holds
    L_Cut (f,f/.1) = f
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: f is weakly-one-to-one and
     A2: len f >= 2;
    A3: 1 <= len f by A2,XXREAL_0:2;
    then A4: 1 in dom f by FINSEQ_3:27;
    2 = 1+1;
    then A5: 1 < len f by A2,NAT_1:13;
    A6: f/.1 = f.1 by A4,FINSEQ_4:def 4;
    A7: Index(f/.1,f) = 1 by A2,JORDAN3:44;
    f.1 <> f.(1+1) by A1,A5,Def2;
    then f/.1 <> f.(1+1) by A4,FINSEQ_4:def 4;
    hence L_Cut (f,f/.1) = <*f/.1 *>^mid(f,Index(f/.1,f)+1,len f)
          by A7,JORDAN3:def 4
       .= mid(f,1,len f) by A4,A5,A6,A7,JORDAN3:56
       .= f by A3,JORDAN3:29;
   end;

  theorem Th38:
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st f is poorly-one-to-one unfolded s.n.c. &
    p in L~f & p <> f.len f holds L_Cut(Rev f,p) = Rev R_Cut(f,p)
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2 such that
     A1: f is poorly-one-to-one unfolded s.n.c. and
     A2: p in L~f and
     A3: p <> f.len f;
    A4: Rev f is poorly-one-to-one by A1,Th10;
    A5: Rev f is unfolded by A1,SPPOL_2:29;
    A6: Rev f is s.n.c. by A1,SPPOL_2:36;
    A7: p in L~Rev f by A2,SPPOL_2:22;
    A8: Rev Rev f = f by FINSEQ_6:29;
    A9: len f = len Rev f by FINSEQ_5:def 3;
    A10: L~f = L~Rev f by SPPOL_2:22;
    A11: dom Rev f = dom f by FINSEQ_5:60;
    A12: 1 <= Index(p, Rev f)+1 by NAT_1:11;
    Index(p, Rev f) < len Rev f by A2,A10,JORDAN3:41;
    then A13: Index(p, Rev f) + 1 <= len f by A9,NAT_1:13;
    then A14: Index(p, Rev f)+1 in dom f by A12,FINSEQ_3:27;
    A15: 1 <= Index(p,f) by A2,JORDAN3:41;
    A16: Index(p,f) < len f by A2,JORDAN3:41;
    then A17: 1 < len f by A15,XXREAL_0:2;
    per cases by A3;
     suppose A18: p = f.1;
      then A19: p = (Rev f).len f by FINSEQ_5:65;
      A20:len (Rev f/^(len Rev f-'1))=len Rev f-'(len Rev f-'1) by JORDAN3:19;
      A21: len Rev f-'1+1=len Rev f by A9,A17,BINARITH:53;
      then A22:(Rev f/^(len Rev f-'1)).1=Rev f.len Rev f by JORDAN3:23;
      1<=len Rev f-(len Rev f-'1) by A21;
      then A23: 1<=len (Rev f/^(len Rev f-'1)) by A20,BINARITH:57;
      then A24:Rev f/^(len Rev f-'1) is non empty by FINSEQ_1:25;
      len Rev f-'len Rev f+1=len Rev f-len Rev f+1 by BINARITH:50
         .=1;
      then A25: mid(Rev f,len Rev f,len Rev f)=(Rev f/^(len Rev f-'1))|1
            by JORDAN3:def 1
         .=<*(Rev f/^(len Rev f-'1))/.1 *> by A24,FINSEQ_5:23
         .=<*Rev f.len Rev f*> by A22,A23,FINSEQ_4:24;
      Index(p,Rev f) + 1 = len f by A4,A5,A6,A9,A17,A19,Th18;
      hence L_Cut(Rev f,p) = <*p*> by A9,A19,A25,JORDAN3:def 4
         .= Rev <*p*> by FINSEQ_5:63
         .= Rev R_Cut(f,p) by A18,JORDAN3:def 5;
     end;
     suppose that
       A26: p <> f.1 and
       A27: p <> f.len f and
       A28: p = f.(Index(p,f)+1);
       A29: p <> (Rev f).len f by A26,FINSEQ_5:65;
      len f = Index(p,Rev f) + Index(p,f) + 1 by A1,A2,A27,A28,Th36
         .= Index(p,Rev f) + (Index(p,f) + 1);
      then A30: p = f.(len f - (Index(p, Rev f)+1) + 1) by A28
         .= (Rev f).(Index(p, Rev f)+1) by A14,FINSEQ_5:61;
      A31: len f = Index(p,Rev f) + Index(p,f) + 1 by A1,A2,A27,A28,Th36
         .= Index(p,f) + (Index(p,Rev f) + 1);
      A32: len f -' Index(p,f) = len f - Index(p,f) by A16,BINARITH:50
         .= Index(p,Rev f)+1 by A31;
      A33: Index(p, Rev f)+1 < len f by A13,A29,A30,XXREAL_0:1;
      thus L_Cut(Rev f,p)
          = mid(Rev f,Index(p,Rev f)+1,len f) by A9,A30,JORDAN3:def 4
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f)
            by A11,A14,A30,A32,A33,JORDAN3:56
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f-'1+1)
                   by A17,BINARITH:53
         .= <*p*>^Rev mid(f,1,Index(p,f)) by A17,A15,A16,JORDAN3:22
         .= Rev (mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
         .= Rev R_Cut(f,p) by A26,JORDAN3:def 5;
     end;
     suppose that
       A34: p <> f.1 and
       A35: p <> f.(Index(p,f)+1);
       A36: p <> (Rev f).len f by A34,FINSEQ_5:65;
      A37: now assume A38: p = (Rev f).(Index(p, Rev f)+1);
       then A39: len Rev f = Index(p,Rev Rev f) + Index(p,Rev f) + 1
             by A4,A5,A6,A7,A9,A36,Th36
          .= Index(p,f) + 1 + Index(p,Rev f) by A8;
       p = f.(len f - (Index(p, Rev f)+1) + 1) by A14,A38,FINSEQ_5:61
        .= f.(Index(p,f)+1) by A9,A39;
       hence contradiction by A35;
      end;
      A40: len f = Index(p,Rev f) + Index(p,f) by A1,A2,A35,JORDAN3:54;
      A41: Index(p, f) < len f by A2,JORDAN3:41;
      Index(p,Rev f) = len f - Index(p,f) by A40
         .= len f -' Index(p,f) by A41,BINARITH:50;
      hence L_Cut(Rev f,p)
          = <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f)
             by A9,A37,JORDAN3:def 4
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f-'1+1)
          by A17,BINARITH:53
         .= <*p*>^Rev mid(f,1,Index(p,f)) by A17,A15,A16,JORDAN3:22
         .= Rev (mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
         .= Rev R_Cut(f,p) by A34,JORDAN3:def 5;
     end;
   end;

  theorem Th39:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p<>f.1 holds
     R_Cut(f,p) is_S-Seq_joining f/.1,p
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: f is almost-one-to-one special unfolded s.n.c. and
     A2: p in L~f and
     A3: p<>f.1;
    R_Cut(f,p)=mid(f,1,Index(p,f))^<*p*> by A3,JORDAN3:def 5;
    hence thesis by A1,A2,A3,Th35;
   end;

  theorem Th40:
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p<>f.len f & p <> f.1 holds
     L_Cut(f,p) is_S-Seq_joining p,f/.len f
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: f is almost-one-to-one special unfolded s.n.c. and
     A2: p in L~f & p<>f.len f and
     A3: p <> f.1;
    p <> Rev f.len f by A3,FINSEQ_5:65;
    then A4: p <> Rev f.len Rev f by FINSEQ_5:def 3;
    A5: Rev f is almost-one-to-one FinSequence by A1,Th8;
    A6: Rev f is special by A1,SPPOL_2:42;
    A7: Rev f is unfolded by A1,SPPOL_2:29;
    A8: Rev f is s.n.c. by A1,SPPOL_2:36;
    A9: p in L~Rev f by A2,SPPOL_2:22;
    A10: L_Cut(f,p) = L_Cut(Rev Rev f,p) by FINSEQ_6:29
       .= Rev R_Cut(Rev f,p) by A4,A5,A7,A8,A9,Th38;
    p <> (Rev f).1 by A2,FINSEQ_5:65;
    then R_Cut(Rev f,p) is_S-Seq_joining (Rev f)/.1,p
       by A5,A6,A7,A8,A9,Th39;
    then L_Cut(f,p) is_S-Seq_joining p,(Rev f)/.1 by A10,JORDAN3:48;
    hence thesis by FINSEQ_5:68;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p<>f.1 holds
     R_Cut(f,p) is_S-Seq
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume f is almost-one-to-one special unfolded s.n.c. &
     p in L~f & p<>f.1;
    then R_Cut(f,p) is_S-Seq_joining f/.1,p by Th39;
    hence R_Cut(f,p) is_S-Seq by JORDAN3:def 3;
   end;

  theorem Th42:
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p <> f.len f & p <> f.1 holds
     L_Cut(f,p) is_S-Seq
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume f is almost-one-to-one special unfolded s.n.c. &
     p in L~f & p <> f.len f & p <> f.1;
    then L_Cut(f,p) is_S-Seq_joining p,f/.len f by Th40;
    hence L_Cut(f,p) is_S-Seq by JORDAN3:def 3;
   end;

  Lm1:
   for f be non empty FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & q in L~f & p<>q & p <> f.1 & (Index(p,f)<Index(q,f) or
    Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1)) holds
     B_Cut(f,p,q) is_S-Seq_joining p,q
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    assume A1: f is almost-one-to-one special unfolded s.n.c. &
     p in L~f & q in L~f & p <> q;
    assume A2: p <> f.1;
    assume A3: Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
     & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
    then A4:B_Cut(f,p,q)=R_Cut(L_Cut(f,p),q) by A1,JORDAN3:def 8;
    A5:1<=Index(q,f) & Index(q,f)<len f by A1,JORDAN3:41;
    Index(p,f) < len f by A1,JORDAN3:41;
    then A6: Index(p,f)+1 <= len f by NAT_1:13;
    A7: 1<=Index(p,f)+1 by NAT_1:11;
    A8: 1<len f by A5,XXREAL_0:2;
    A9: f is almost-one-to-one FinSequence by A1;
    A10: now per cases by A3;
     case Index(p,f)<Index(q,f);
      then A11:Index(p,f)+1<=Index(q,f) by NAT_1:13;
      assume p=f.len f;
      then len f <= Index(q,f) by A9,A1,A8,A11,Th18;
      hence contradiction by A1,JORDAN3:41;
     end;
     case A12: Index(p,f)=Index(q,f) &
       LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      A13: now
       assume A14: p = f.(Index(p,f)+1);
       then A15: p = f/.(Index(p,f)+1) by A6,A7,FINSEQ_4:24;
       q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A12,JORDAN3:def 6;
       then consider r being Real such that
        A16: 0 <= r & r <= 1 and
        A17: q = (1-r)*f/.(Index(p,f))+r*f/.(Index(p,f)+1) by SPPOL_1:21;
       A18: p = 1 * p by EUCLID:33
          .= 0.REAL 2 + 1 * p by EUCLID:31
          .= (1-1)*f/.(Index(p,f))+1 * p by EUCLID:33;
       then 1<=r by A12,A15,A16,A17,JORDAN3:def 6;
       then r = 1 by A16,XXREAL_0:1;
       hence contradiction by A1,A6,A7,A14,A17,A18,FINSEQ_4:24;
      end;
      assume p=f.len f;
      hence contradiction by A9,A1,A8,A13,Th18;
     end;
    end;
    then A19:L_Cut(f,p) is_S-Seq_joining p,f/.len f by A1,A2,Th40;
    A20: L_Cut(f,p) is_S-Seq by A1,A10,A2,Th42;
    now per cases by A3;
     case Index(p,f)<Index(q,f);
      then q in L~(L_Cut(f,p)) by A1,JORDAN3:64;
      hence ex i1 being Element of NAT st 1<=i1 & i1+1<=len L_Cut(f,p)
        & q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
     end;
     case Index(p,f)=Index(q,f)
         & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      then q in L~(L_Cut(f,p)) by A1,JORDAN3:66;
      hence ex i1 being Element of NAT st 1<=i1 & i1+1<=len L_Cut(f,p)
        & q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
     end;
    end;
    then A21: q in L~L_Cut(f,p) by SPPOL_2:17;
    then 1<=Index(q,L_Cut(f,p)) & Index(q,L_Cut(f,p))<len L_Cut(f,p)
      by JORDAN3:41;
    then A22: 1<=len L_Cut(f,p) by XXREAL_0:2;
    A23:(L_Cut(f,p)).1=p by A19,JORDAN3:def 3;
    then p=(L_Cut(f,p))/. 1 by A22,FINSEQ_4:24;
    hence B_Cut(f,p,q) is_S-Seq_joining p,q by A1,A4,A20,A21,A23,JORDAN3:67;
  end;

  theorem Th43:
   for f be non empty FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. & len f <> 2 &
    p in L~f & q in L~f & p<>q & p <> f.1 & q <> f.1 holds
     B_Cut(f,p,q) is_S-Seq_joining p,q
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    assume A1: f is almost-one-to-one special unfolded s.n.c. &
     len f <> 2 & p in L~f & q in L~f & p<>q & p <> f.1 & q <> f.1;
    per cases;
     suppose Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
       LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      hence thesis by A1,Lm1;
     end;
     suppose A2: not(Index(p,f)<Index(q,f) or
       Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
      then A3: B_Cut(f,p,q)=Rev R_Cut(L_Cut(f,q),p) by JORDAN3:def 8;
      A4: Index(q,f) < Index(p,f) or Index(p,f)=Index(q,f) &
       not LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1) by A2,XXREAL_0:1;
      A5: now assume that
        A6: Index(p,f)=Index(q,f) and
        A7: not LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
       A8: 1 <= Index(p,f) by A1,JORDAN3:41;
       A9: Index(p,f) < len f by A1,JORDAN3:41;
       then A10: Index(p,f)+1 <= len f by NAT_1:13;
       then A11: LSeg(f,Index(p,f)) = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
          by A8,TOPREAL1:def 5;
       then A12: p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A1,JORDAN3:42;
       A13: q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
          by A1,A6,A11,JORDAN3:42;
       A14: Index(p,f) in dom f by A8,A9,FINSEQ_3:27;
       1 <= Index(p,f)+1 by NAT_1:11;
       then A15: Index(p,f)+1 in dom f by A10,FINSEQ_3:27;
       f is almost-one-to-one FinSequence by A1;
       then f is weakly-one-to-one by A1,Th7;
       then f.Index(p,f) <> f.(Index(p,f)+1) by A8,A9,Def2;
       then f/.(Index(p,f))<>f.(Index(p,f)+1) by A14,FINSEQ_4:def 4;
       then f/.(Index(p,f))<>f/.(Index(p,f)+1) by A15,FINSEQ_4:def 4;
       then LT q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A7,A12,A13,JORDAN3:63;
       hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A6,JORDAN3:def 7;
      end;
      then A16: B_Cut(f,q,p) is_S-Seq_joining q,p by A1,A4,Lm1;
      Rev B_Cut(f,q,p) = B_Cut(f,p,q) by A1,A3,A4,A5,JORDAN3:def 8;
      hence thesis by A16,JORDAN3:48;
     end;
   end;

  theorem
   for f be non empty FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. & len f <> 2 &
    p in L~f & q in L~f & p<>q & p <> f.1 & q <> f.1 holds
     B_Cut(f,p,q) is_S-Seq
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    assume f is almost-one-to-one special unfolded s.n.c. &
     len f <> 2 & p in L~f & q in L~f & p<>q & p <> f.1 & q <> f.1;
    then B_Cut(f,p,q) is_S-Seq_joining p,q by Th43;
    hence B_Cut(f,p,q) is_S-Seq by JORDAN3:def 3;
   end;

  theorem :: Lemat do "bardzo wazne dla Cage"
   for C be compact non vertical non horizontal Subset of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st p in BDD L~Cage(C,n)
   ex B be S-Sequence_in_R2 st
    B = B_Cut(Rotate(Cage(C,n),Cage(C,n)/.Index(South-Bound(p,L~Cage(C,n)),
     Cage(C,n)))|(len Rotate(Cage(C,n),Cage(C,n)/.
      Index(South-Bound(p,L~Cage(C,n)),
     Cage(C,n)))-'1),South-Bound(p,L~Cage(C,n)),North-Bound(p,L~Cage(C,n))) &
   ex P be S-Sequence_in_R2 st P is_sequence_on
     GoB(B^'<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*>) &
    L~<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*> = L~P &
    P/.1 = North-Bound(p,L~Cage(C,n)) &
    P/.len P = South-Bound(p,L~Cage(C,n)) &
    len P >= 2 &
   ex B1 be S-Sequence_in_R2 st B1 is_sequence_on
     GoB(B^'<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*>) &
    L~B = L~B1 &
    B/.1 = B1/.1 &
    B/.len B = B1/.len B1 &
    len B <= len B1 &
   ex g be non constant standard special_circular_sequence st
    g = B1^'P
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    set G = Gauge(C,n);
    set f = Cage(C,n);
    set Lf = L~f;
    set a = North-Bound(p,Lf);
    set b = South-Bound(p,Lf);
    set c = North-Bound(q,Lf);
    set d = South-Bound(q,Lf);
    assume that
     A1: p in BDD Lf;
    set Rotf = Rotate(f,f/.Index(b,f));
    set rf = Rotf|(len Rotf-'1);
    A2: L~Rotf = L~f by REVROT_1:33;
    A3: len Rotf = len f by REVROT_1:14;
    A4: len f > 4 by GOBOARD7:36;
    then A5: len f > 1+1 by XXREAL_0:2;
    then A6: len f-1 > 1 by XREAL_1:22;
    A7: 1 < len f-'1 by A6,BINARITH:def 3;
    A8: len f > 1 by A4,XXREAL_0:2;
    then A9: len f-'1+1 = len f by BINARITH:53;
    then reconsider rf as S-Sequence_in_R2 by A3,A7,JORDAN4:49;
    A10: a <> b by A1,JORDAN18:25;
    A11: a`1 = p`1 & b`1 = p`1 by JORDAN18:20;
    A12: b`2 < p`2 & p`2 < a`2 by A1,JORDAN18:23;
    then A13: p in LSeg(a,b) by A11,GOBOARD7:8;
    A14: L~rf c= L~f by A2,TOPREAL3:27;
    A15: a in Lf & b in Lf by A1,JORDAN18:22;
    A16: 1<=Index(b,f) & Index(b,f) < len f by A15,JORDAN3:41;
    then A17: Index(b,f) in dom f by FINSEQ_3:27;
    then A18: f/.Index(b,f) in rng f by PARTFUN2:4;
    A19: for i be Element of NAT st 1 <= i & i < Index(b,f)
    holds f/.i <> f/.Index(b,f)
     by A16,GOBOARD7:38;
    A20: len(f:-(f/.Index(b,f)))=len f-(f/.Index(b,f))..f+1 by A18,FINSEQ_5:53;
    f/.Index(b,f)..f <= Index(b,f) by A17,FINSEQ_5:42;
    then 0+(f/.Index(b,f))..f < len f by A16,XXREAL_0:2;
    then len f - (f/.Index(b,f))..f > 1-1 by XREAL_1:22;
    then 1 < len(f:-(f/.Index(b,f))) by A20,XREAL_1:21;
    then LSeg(Rotate(f,f/.Index(b,f)),1) = LSeg(f,1 -' 1 + (f/.Index(b,f))..f)
                                                            by A18,REVROT_1:24
       .= LSeg(f,0+(f/.Index(b,f))..f) by BINARITH:51
       .= LSeg(f,Index(b,f)) by A17,A19,FINSEQ_6:52;
    then A21: b in LSeg(Rotate(f,f/.Index(b,f)),1) by A15,JORDAN3:42;
    len Rotf >=2+2 by GOBOARD7:36;
    then len Rotf >=1+1 by XXREAL_0:2;
    then A22: LSeg(Rotf,1) = LSeg(Rotf/.1,Rotf/.(1+1)) by TOPREAL1:def 5;
    A23: LSeg(a,b) is vertical by A11,SPPOL_1:37;
    A24: Rotf/.1 = Rotf/.len Rotf by FINSEQ_6:def 1;
    A25: Rotf/.2 in LSeg(Rotf/.2,Rotf/.1) &
     Rotf/.(len Rotf-'1) in LSeg(Rotf/.(len Rotf-'1),Rotf/.(len Rotf))
      by TOPREAL1:6;
    A26: len f-'1 < len f by A9,NAT_1:13;
    A27: LSeg(Rotf,len Rotf -' 1) /\ LSeg(Rotf,1) = {Rotf/.1}
     by A3,A4,REVROT_1:30;
    then A28: LSeg(Rotf,len Rotf -' 1) meets LSeg(Rotf,1) by XBOOLE_0:def 7;
    A29: LSeg(Rotf,1) = LSeg(Rotf/.1,Rotf/.(1+1)) by A3,A5,TOPREAL1:def 5;
    A30: LSeg(Rotf,len Rotf-'1) = LSeg(Rotf/.(len Rotf-'1),Rotf/.(len Rotf))
       by A3,A9,A6,TOPREAL1:def 5;
    A31: b`2 < a`2 by A12,XXREAL_0:2;
    A32: now assume A33: a in LSeg(Rotf,len Rotf-'1);
     per cases by SPPOL_1:41;
      suppose A34: LSeg(Rotf,1) is vertical &
        LSeg(Rotf,len Rotf-'1) is vertical;
       A35: (Rotf/.1)`1 = (Rotf/.(1+1))`1 &
        (Rotf/.(len Rotf-'1))`1 = (Rotf/.(len Rotf))`1
         by A29,A30,A34,SPPOL_1:37;
       A36: a`1 = (Rotf/.(len Rotf-'1))`1 & b`1 = (Rotf/.1)`1
        by A21,A29,A30,A33,A34,SPPOL_1:64;
       per cases by A31,XXREAL_0:2;
        suppose A37: b`2 < (Rotf/.1)`2;
         set p1 = |[(Rotf/.1)`1,(b`2+(Rotf/.1)`2)/2]|;
         A38: p1`1 = (Rotf/.1)`1 & p1`2 = (b`2+(Rotf/.1)`2)/2 by EUCLID:56;
         A39: b`2 < (b`2+(Rotf/.1)`2)/2 & (b`2+(Rotf/.1)`2)/2 < (Rotf/.1)`2
          by A37,XREAL_1:228;
         A40: (Rotf/.1)`2 > (Rotf/.(1+1))`2 by A21,A29,A37,TOPREAL1:10;
         then (Rotf/.(1+1))`2 <= b`2 by A21,A29,TOPREAL1:10;
         then (Rotf/.(1+1))`2 < p1`2 by A38,A39,XXREAL_0:2;
         then p1 in LSeg(Rotf,1) by A22,A38,A35,A39,GOBOARD7:8;
         then A41: p1 in Lf by A2,SPPOL_2:17;
         (Rotf/.len Rotf)`2 <= (Rotf/.(len Rotf-'1))`2
         proof
          assume A42: (Rotf/.len Rotf)`2 > (Rotf/.(len Rotf-'1))`2;
          (Rotf/.(len Rotf-'1))`2 < (Rotf/.(1+1))`2 or
           (Rotf/.(len Rotf-'1))`2 >= (Rotf/.(1+1))`2;
          then Rotf/.2 in LSeg(Rotf/.(len Rotf-'1),Rotf/.(len Rotf)) or
           Rotf/.(len Rotf-'1) in LSeg(Rotf/.2,Rotf/.1)
            by A35,A24,A42,A40,GOBOARD7:8;
          then Rotf/.2 in {Rotf/.1} or Rotf/.(len Rotf-'1) in {Rotf/.1}
           by A29,A30,A25,A27,XBOOLE_0:def 3;
          then Rotf/.(1+1) = Rotf/.1 or Rotf/.(len Rotf-'1) = Rotf/.(len Rotf)
           by A24,TARSKI:def 1;
          hence contradiction by A3,A6,A9,A8,A26,Th5;
         end;
         then A43: (Rotf/.len Rotf)`2 <= a`2 by A30,A33,TOPREAL1:10;
         then p1`2 < a`2 by A38,A39,A24,XXREAL_0:2;
         then p1 in LSeg(b,a) by A11,A38,A36,A39,GOBOARD7:8;
         then p1 in LSeg(a,b) /\ Lf by A41,XBOOLE_0:def 3;
         then p1 in {a,b} by A1,JORDAN18:27;
         hence contradiction by A38,A39,A24,A43,TARSKI:def 2;
        end;
        suppose A44: (Rotf/.1)`2 < a`2;
         set p1 = |[(Rotf/.1)`1,(a`2+(Rotf/.1)`2)/2]|;
         A45: p1`1 = (Rotf/.1)`1 & p1`2 = (a`2+(Rotf/.1)`2)/2 by EUCLID:56;
         A46: (Rotf/.1)`2 < (a`2+(Rotf/.1)`2)/2 & (a`2+(Rotf/.1)`2)/2 < a`2
          by A44,XREAL_1:228;
         A47: (Rotf/.(len Rotf-'1))`2 > (Rotf/.len Rotf)`2
          by A30,A33,A44,A24,TOPREAL1:10;
         then a`2 <= (Rotf/.(len Rotf-'1))`2 by A30,A33,TOPREAL1:10;
         then p1`2 < (Rotf/.(len Rotf-'1))`2 by A45,A46,XXREAL_0:2;
         then p1 in LSeg(Rotf,len Rotf-'1) by A30,A45,A35,A46,A24,GOBOARD7:8;
         then A48: p1 in Lf by A2,SPPOL_2:17;
         (Rotf/.(1+1))`2 <= (Rotf/.1)`2
         proof
          assume A49: (Rotf/.(1+1))`2 > (Rotf/.1)`2;
          (Rotf/.(len Rotf-'1))`2 < (Rotf/.(1+1))`2 or
           (Rotf/.(len Rotf-'1))`2 >= (Rotf/.(1+1))`2;
          then Rotf/.2 in LSeg(Rotf/.(len Rotf-'1),Rotf/.(len Rotf)) or
           Rotf/.(len Rotf-'1) in LSeg(Rotf/.2,Rotf/.1)
            by A35,A24,A49,A47,GOBOARD7:8;
          then Rotf/.2 in {Rotf/.1} or Rotf/.(len Rotf-'1) in {Rotf/.1}
           by A29,A30,A25,A27,XBOOLE_0:def 3;
          then Rotf/.(1+1) = Rotf/.1 or Rotf/.(len Rotf-'1) = Rotf/.(len Rotf)
           by A24,TARSKI:def 1;
          hence contradiction by A3,A6,A9,A8,A26,Th5;
         end;
         then A50: b`2 <= (Rotf/.1)`2 by A21,A29,TOPREAL1:10;
         then b`2 < p1`2 by A45,A46,XXREAL_0:2;
         then p1 in LSeg(b,a) by A11,A45,A36,A46,GOBOARD7:8;
         then p1 in LSeg(a,b) /\ Lf by A48,XBOOLE_0:def 3;
         then p1 in {a,b} by A1,JORDAN18:27;
         hence contradiction by A45,A46,A50,TARSKI:def 2;
        end;
      end;
      suppose A51: LSeg(Rotf,1) is vertical &
        LSeg(Rotf,len Rotf-'1) is horizontal;
       A52: a`1 = (Rotf/.len Rotf)`1 by A11,A24,A21,A29,A51,SPPOL_1:64;
       a`2 = (Rotf/.len Rotf)`2 by A30,A33,A51,SPPOL_1:63;
       then a = Rotf/.len Rotf by A52,TOPREAL3:11;
       then a in LSeg(Rotf/.1,Rotf/.2) by A24,TOPREAL1:6;
       then LSeg(a,b) c= LSeg(Rotf/.1,Rotf/.2) by A21,A29,TOPREAL1:12;
       then p in Lf by A13,A2,A29,SPPOL_2:17;
       then p in LSeg(a,b) /\ Lf by A13,XBOOLE_0:def 3;
       then p in {a,b} by A1,JORDAN18:27;
       hence contradiction by A12,TARSKI:def 2;
      end;
      suppose A53: LSeg(Rotf,1) is horizontal &
        LSeg(Rotf,len Rotf-'1) is vertical;
       A54: b`1 = (Rotf/.1)`1 by A11,A30,A33,A24,A53,SPPOL_1:64;
       b`2 = (Rotf/.1)`2 by A21,A29,A53,SPPOL_1:63;
       then b = Rotf/.1 by A54,TOPREAL3:11;
       then b in LSeg(Rotf/.(len Rotf-'1),Rotf/.len Rotf) by A24,TOPREAL1:6;
       then LSeg(a,b) c= LSeg(Rotf/.(len Rotf-'1),Rotf/.len Rotf)
        by A30,A33,TOPREAL1:12;
       then p in Lf by A13,A2,A30,SPPOL_2:17;
       then p in LSeg(a,b) /\ Lf by A13,XBOOLE_0:def 3;
       then p in {a,b} by A1,JORDAN18:27;
       hence contradiction by A12,TARSKI:def 2;
      end;
      suppose A55: LSeg(Rotf,1) is horizontal &
        LSeg(Rotf,len Rotf-'1)is horizontal;
       b`2 = (Rotf/.1)`2 & a`2 = (Rotf/.(len Rotf-'1))`2
        by A21,A29,A30,A55,A33,SPPOL_1:63;
       hence contradiction by A29,A30,A28,A12,A55,SPRECT_3:21;
      end;
    end;
    A56: L~Rotf = L~rf \/ LSeg(Rotf,len Rotf-'1)
       by A3,A6,A9,GOBOARD2:8;
    A57: LSeg(rf,1) c= L~rf by TOPREAL3:26;
    len Rotf-'1 <= len Rotf by BINARITH:52;
    then len rf = len Rotf-'1 by FINSEQ_1:80;
    then 1+1 <= len rf by A3,A7,NAT_1:13;
    then b in LSeg(rf,1) by A21,SPPOL_2:3;
    then A58: a in L~rf & b in L~rf by A15,A2,A32,A56,A57,XBOOLE_0:def 2;
    then reconsider BCut = B_Cut(rf,b,a) as S-Sequence_in_R2 by A10,JORDAN3:72;
    set pion = <*a,b*>;
    set Ga = GoB(BCut^'pion);
    A59: len pion = 1+1 by FINSEQ_1:61;
    len (BCut^'pion) + 1 = len BCut + (1+1) by A59,GRAPH_2:13
       .= len BCut + 1 + 1;
    then A60: len (BCut^'pion) = len BCut + 1;
    then A61: len BCut < len (BCut^'pion) by NAT_1:13;
    A62: len BCut + 1 >= 1 by NAT_1:11;
    len BCut <> 0 by FINSEQ_1:25;
    then len BCut > 0 by NAT_1:3;
    then A63: len BCut >= 0+1 by NAT_1:13;
    then A64: (BCut^'pion)/.len BCut = BCut/.len BCut by GRAPH_2:61
       .= a by A58,Th20
       .= pion/.1 by FINSEQ_4:26;
    A65: now let n be Element of NAT;
     assume n in dom pion;
     then A66: n in Seg 2 by FINSEQ_3:29;
     per cases by A66,TARSKI:def 2,FINSEQ_1:4;
      suppose A67: n = 1;
       len BCut in Seg len (BCut^'pion) by A63,A61,FINSEQ_1:3;
       then len BCut in dom (BCut^'pion) by FINSEQ_1:def 3;
       then consider i,j be Element of NAT such that
        A68: [i,j] in Indices Ga and
        A69: (BCut^'pion)/.len BCut = Ga*(i,j) by GOBOARD2:25;
       take i,j;
       thus [i,j] in Indices Ga by A68;
       thus pion/.n = Ga*(i,j) by A69,A67,A64;
      end;
      suppose A70: n = 1+1;
       len BCut + 1 in dom (BCut^'pion) by A60,A62,FINSEQ_3:27;
       then consider i,j be Element of NAT such that
        A71: [i,j] in Indices Ga and
        A72: (BCut^'pion)/.(len BCut + 1) = Ga*(i,j) by GOBOARD2:25;
       take i,j;
       thus [i,j] in Indices Ga by A71;
       thus pion/.n = Ga*(i,j) by A59,A72,A70,GRAPH_2:62;
      end;
    end;
    pion is_S-Seq by A10,A23,JORDAN1B:8;
    then consider pion1 be FinSequence of TOP-REAL 2 such that
     A73: pion1 is_sequence_on Ga and
     A74: pion1 is_S-Seq and
     A75: L~pion = L~pion1 and
     A76: pion/.1 = pion1/.1 and
     A77: pion/.len pion = pion1/.len pion1 and
     A78: len pion <= len pion1 by A65,GOBOARD3:2;
    reconsider pion1 as S-Sequence_in_R2 by A74;
    A79: now let n be Element of NAT;
     assume A80: n in dom BCut;
     dom BCut c= dom (BCut^'pion) by Th23;
     then consider i,j be Element of NAT such that
      A81: [i,j] in Indices GoB(BCut^'pion) and
      A82: (BCut^'pion)/.n = GoB(BCut^'pion)*(i,j) by A80,GOBOARD2:25;
     take i,j;
     thus [i,j] in Indices Ga by A81;
     1 <= n & n <= len BCut by A80,FINSEQ_3:27;
     hence BCut/.n = Ga*(i,j) by A82,GRAPH_2:61;
    end;
    consider BCut1 be FinSequence of TOP-REAL 2 such that
     A83: BCut1 is_sequence_on Ga and
     A84: BCut1 is_S-Seq and
     A85: L~BCut = L~BCut1 and
     A86: BCut/.1 = BCut1/.1 and
     A87: BCut/.len BCut = BCut1/.len BCut1 and
     A88: len BCut <= len BCut1 by A79,GOBOARD3:2;
    reconsider BCut1 as S-Sequence_in_R2 by A84;
    set g = BCut1^'pion1;
    A89: BCut1/.len BCut1 = a by A58,A87,Th20
       .= pion1/.1 by A76,FINSEQ_4:26;
    A90: 1 in dom BCut1 & len BCut1 in dom BCut1 by FINSEQ_5:6;
    A91: now assume BCut1 is constant;
     then BCut1/.1 = BCut1/.len BCut1 by A90,REVROT_1:def 1;
     then BCut1/.len BCut1 = b by A58,A86,Th19;
     then a = b by A58,A87,Th20;
     hence contradiction by A1,JORDAN18:25;
    end;
    A92: BCut1^'pion1 is_sequence_on Ga by A73,A89,A83,TOPREAL8:12;
    A93: L~BCut1 c= L~rf by A58,A85,JORDAN5B:27;
    LSeg(BCut1,len BCut1 -' 1) c= L~BCut1 by TOPREAL3:26;
    then LSeg(BCut1,len BCut1 -' 1) c= L~rf by A93,XBOOLE_1:1;
    then A94: LSeg(BCut1,len BCut1 -' 1) c= Lf by A14,XBOOLE_1:1;
    LSeg(pion1,1) c= L~pion by A75,TOPREAL3:26;
    then LSeg(pion1,1) c= LSeg(a,b) by SPPOL_2:21;
    then LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1) c= LSeg(a,b) /\ Lf
     by A94,XBOOLE_1:27;
    then A95: LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1) c= {a,b}
     by A1,JORDAN18:27;
    A96: len BCut1 >= 1+1 by TOPREAL1:def 10;
    then A97: len BCut1-1 >= 1 by XREAL_1:21;
    A98: len BCut1-'1 >= 1 by A97,BINARITH:def 3;
    len BCut1 > 1 by A96,NAT_1:13;
    then A99: len BCut1-'1+1 = len BCut1 by BINARITH:53;
    BCut/.len BCut = a by A58,Th20;
    then A100: a in LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1)
     by A87,TOPREAL1:6;
    A101: L~BCut1 c= Lf by A14,A93,XBOOLE_1:1;
    A102: now assume b in LSeg(BCut1,len BCut1 -' 1);
     then b in LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1)
      by A98,A99,TOPREAL1:def 5;
     then LSeg(a,b) c= LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1)
      by A100,TOPREAL1:12;
     then p in LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1) by A13;
     then p in LSeg(BCut1,len BCut1-'1) by A98,A99,TOPREAL1:def 5;
     then p in L~BCut1 by SPPOL_2:17;
     then p in LSeg(a,b) /\ Lf by A13,A101,XBOOLE_0:def 3;
     then p in {a,b} by A1,JORDAN18:27;
     hence contradiction by A12,TARSKI:def 2;
    end;
    A103: LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1) = { BCut1/.len BCut1 }
    proof
     thus LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1) c= { BCut1/.len BCut1 }
     proof
      let x be set;
      assume A104: x in LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1);
      x <> b by A102,A104,XBOOLE_0:def 3;
      then x = a by A95,A104,TARSKI:def 2;
      then x = BCut1/.len BCut1 by A58,A87,Th20;
      hence x in { BCut1/.len BCut1 } by TARSKI:def 1;
     end;
     let x be set;
     assume x in { BCut1/.len BCut1 };
     then A105: x = BCut1/.len BCut1 by TARSKI:def 1;
     x in LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1) by A105,TOPREAL1:6;
     then A106: x in LSeg(BCut1,len BCut1-'1) by A98,A99,TOPREAL1:def 5;
     x in LSeg(pion1/.1,pion1/.(1+1)) by A105,A89,TOPREAL1:6;
     then x in LSeg(pion1,1) by A59,A78,TOPREAL1:def 5;
     hence x in LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1)
      by A106,XBOOLE_0:def 3;
    end;
    A107: pion1/.len pion1 = b by A59,A77,FINSEQ_4:26
       .= BCut1/.1 by A58,A86,Th19;
    now assume len BCut = 1;
     then BCut/.1 = a by A58,Th20;
     hence contradiction by A10,A58,Th19;
    end;
    then len BCut > 1 by A63,REAL_1:def 5;
    then len BCut1 > 1 by A88,XXREAL_0:2;
    then A108: len BCut1 >= 1+1 by NAT_1:13;
    A109: LSeg(a,b) /\ Lf = {a,b} by A1,JORDAN18:27;
    L~BCut1 c= Lf by A14,A93,XBOOLE_1:1;
    then L~BCut1 /\ LSeg(a,b) c= {a,b} by A109,XBOOLE_1:26;
    then L~BCut1 /\ L~pion1 c= {a,b} by A75,SPPOL_2:21;
    then L~BCut1 /\ L~pion1 c= {a,BCut1/.1} by A58,A86,Th19;
    then A110: L~BCut1 /\ L~pion1 c= {BCut1/.1,pion1/.1} by A76,FINSEQ_4:26;
    {BCut1/.1,pion1/.1} c= L~BCut1 /\ L~pion1
    proof
     let x be set;
     assume x in {BCut1/.1,pion1/.1};
     then x = BCut1/.1 or x = pion1/.1 by TARSKI:def 2;
     then x in L~BCut1 & x in L~pion1 by A107,A108,A59,A78,A89,JORDAN3:34;
     hence x in L~BCut1 /\ L~pion1 by XBOOLE_0:def 3;
    end;
    then L~BCut1 /\ L~pion1 = {BCut1/.1,pion1/.1} by A110,XBOOLE_0:def 10;
    then reconsider g as non constant standard special_circular_sequence
           by A91,A92,A103,A107,A89,Th25,TOPREAL8:11,33,
34,JORDAN8:7;
    A111: len <*a,b*> = 2 by FINSEQ_1:61;
    take BCut;
    thus BCut = B_Cut(Rotate(Cage(C,n),Cage(C,n)/.
     Index(South-Bound(p,L~Cage(C,n)),
     Cage(C,n)))|(len Rotate(Cage(C,n),Cage(C,n)/.
     Index(South-Bound(p,L~Cage(C,n)),
     Cage(C,n)))-'1),South-Bound(p,L~Cage(C,n)),North-Bound(p,L~Cage(C,n)));
    take pion1;
    thus pion1 is_sequence_on
     GoB(BCut^'<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*>) &
     L~<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*> = L~pion1 &
     pion1/.1 = North-Bound(p,L~Cage(C,n)) &
     pion1/.len pion1 = South-Bound(p,L~Cage(C,n)) &
     len pion1 >= 2 by A73,A75,A76,A77,A78,A111,FINSEQ_4:26;
    take BCut1;
    thus BCut1 is_sequence_on
     GoB(BCut^'<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*>) &
     L~BCut = L~BCut1 &
     BCut/.1 = BCut1/.1 &
     BCut/.len BCut = BCut1/.len BCut1 &
     len BCut <= len BCut1 by A83,A85,A86,A87,A88;
    take g;
    thus g = BCut1^'pion1;
   end;

