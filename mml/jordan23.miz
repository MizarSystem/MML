:: Subsequences of Almost, Weakly and Poorly One-to-one Finite Sequences
::  by Robert Milewski
:: 
:: Received February 1, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies JORDAN23, JORDAN8, BOOLE, JORDAN9, GOBOARD1, GOBOARD5, JORDAN3,
      FINSEQ_5, JORDAN2C, SPPOL_2, GROUP_2, RFINSEQ, REALSET1, FINSEQ_4,
      COMPTS_1, TOPREAL1, SPPOL_1, FINSEQ_1, EUCLID, RELAT_1, MCART_1,
      MATRIX_1, PRE_TOPC, SEQM_3, FUNCT_1, ARYTM_1, FINSEQ_6, GOBOARD2,
      GRAPH_2, ARYTM_3, JORDAN18;
 notations TARSKI, XBOOLE_0, SUBSET_1, XREAL_0, REAL_1, NAT_1, REALSET1,
      FUNCT_1, FINSEQ_1, FINSEQ_4, FINSEQ_5, RFINSEQ, MATRIX_1, FINSEQ_6,
      GRAPH_2, STRUCT_0, PRE_TOPC, BINARITH, COMPTS_1, EUCLID, GOBOARD1,
      TOPREAL1, GOBOARD2, GOBOARD5, SPPOL_1, SPPOL_2, JORDAN3, JORDAN5C,
      JORDAN8, JORDAN9, JORDAN2C, JORDAN18;
 constructors ARYTM_0, NAT_1, JORDAN8, REALSET1, GOBOARD9, JORDAN6, REAL_1,
      CARD_4, PSCOMP_1, BINARITH, JORDAN2C, CONNSP_1, JORDAN9, JORDAN1A,
      SQUARE_1, FINSEQ_4, GOBRD13, JORDAN1, JORDAN3, RFINSEQ, MATRIX_2,
      TOPREAL2, TBSP_1, JORDAN5C, PARTFUN1, GRAPH_2, SPRECT_1, GOBOARD2, INT_1,
      TOPS_1, FINSOP_1, JORDAN1E, RCOMP_1, CQC_SIM1, SEQ_1, JORDAN10, WSIERP_1,
      JCT_MISC, JORDAN1H, ABSVALUE, ENUMSET1, CONNSP_2, PROB_1, WAYBEL_3,
      LIMFUNC1, TOPRNS_1, COMPTS_1, POWER, SERIES_1, GOBRD14, JORDAN18,
      REALSET2;
 registrations SPRECT_1, TOPREAL6, REVROT_1, RELSET_1, REAL_1, SPRECT_3,
      GOBOARD2, FINSEQ_5, SPPOL_2, FUNCT_7, JORDAN1J, FINSET_1, FINSEQ_6,
      XREAL_0, ARYTM_3;
 requirements ARITHM, NUMERALS, BOOLE, SUBSET, REAL;
 definitions TARSKI, XBOOLE_0, FUNCT_1;
 theorems NAT_1, FINSEQ_1, FINSEQ_4, EUCLID, FINSEQ_3, AXIOMS, REAL_1, SPPOL_2,
      TARSKI, JORDAN3, FINSEQ_5, FINSEQ_6, GOBOARD7, TOPREAL1, BINARITH, AMI_5,
      JORDAN5B, SPRECT_2, SPPOL_1, FUNCT_1, GOBOARD9, FINSEQ_2, JORDAN4,
      GOBOARD2, SPRECT_3, TOPREAL3, JORDAN8, PARTFUN2, SCMFSA_7, SPRECT_1,
      XBOOLE_0, XBOOLE_1, ZFMISC_1, JORDAN1E, REVROT_1, JORDAN1B, JORDAN1J,
      GOBOARD3, TOPREAL8, AMISTD_1, GRAPH_2, SPRECT_5, JORDAN18, XCMPLX_1,
      JORDAN5C, MSUALG_8;

begin

  reserve n for Nat;

  theorem ThRob3:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st p in L~f holds
    len L_Cut(f,p) >= 1
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume A1: p in L~f;
    L_Cut(f,p) <> {} by A1,JORDAN1E:7;
    then len L_Cut(f,p) <> 0 by FINSEQ_1:25;
    then len L_Cut(f,p) > 0 & 1=0+1 by NAT_1:19;
    hence len L_Cut(f,p) >= 1 by NAT_1:38;
   end;

  theorem
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 holds
    len R_Cut(f,p) >= 1
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    R_Cut(f,p) <> {} by JORDAN1J:44;
    then len R_Cut(f,p) <> 0 by FINSEQ_1:25;
    then len R_Cut(f,p) > 0 & 1 = 0+1 by NAT_1:19;
    hence len R_Cut(f,p) >= 1 by NAT_1:38;
   end;

  theorem ThRob8:
   for f be FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 holds
    B_Cut (f,p,q) <> {}
   proof
    let f be FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    A2: R_Cut(L_Cut(f,q),p) <> {} by JORDAN1J:44;
    per cases;
     suppose p in L~f & q in L~f & Index(p,f)<Index(q,f) or
       Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      then B_Cut(f,p,q) = R_Cut(L_Cut(f,p),q) by JORDAN3:def 8;
      hence B_Cut (f,p,q) <> {} by JORDAN1J:44;
     end;
     suppose not (p in L~f & q in L~f & Index(p,f)<Index(q,f) or
       Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
      then B_Cut(f,p,q) = Rev R_Cut(L_Cut(f,q),p) by JORDAN3:def 8;
      hence B_Cut (f,p,q) <> {} by A2,JORDAN1B:2;
     end;
   end;

  registration
   let x be set;
   cluster <*x*> -> one-to-one;
   coherence by FINSEQ_3:102;
  end;

  definition
   let f be FinSequence;
   attr f is almost-one-to-one means :Def1:
    for i,j be Nat st i in dom f & j in dom f & (i <> 1 or j <> len f) &
    (i <> len f or j <> 1) & f.i = f.j holds i = j;
  end;

  definition
   let f be FinSequence;
   attr f is weakly-one-to-one means :Def2:
    for i be Nat st 1 <= i & i < len f holds f.i <> f.(i+1);
  end;

  definition
   let f be FinSequence;
   attr f is poorly-one-to-one means :Def3:
    for i be Nat st 1 <= i & i < len f holds f.i <> f.(i+1) if len f <> 2
    otherwise not contradiction;
   consistency;
  end;

  theorem
   for D be set
   for f be FinSequence of D holds f is almost-one-to-one iff
    for i,j be Nat st i in dom f & j in dom f & (i <> 1 or j <> len f) &
     (i <> len f or j <> 1) & f/.i = f/.j holds i = j
   proof
    let D be set;
    let f be FinSequence of D;
    thus f is almost-one-to-one implies
     for i,j be Nat st i in dom f & j in dom f & (i <> 1 or j <> len f) &
      (i <> len f or j <> 1) & f/.i = f/.j holds i = j
    proof
     assume A1: f is almost-one-to-one;
     let i,j be Nat such that
      A2: i in dom f & j in dom f and
      A3: i <> 1 or j <> len f and
      A4: i <> len f or j <> 1 and
      A5: f/.i = f/.j;
     f.i = f/.j by A2,A5,FINSEQ_4:def 4
        .= f.j by A2,FINSEQ_4:def 4;
     hence i = j by A1,A2,A3,A4,Def1;
    end;
    assume A1: for i,j be Nat st i in dom f & j in dom f &
     (i <> 1 or j <> len f) & (i <> len f or j <> 1) & f/.i = f/.j
      holds i = j;
    now let i,j be Nat such that
      A2: i in dom f & j in dom f and
      A3: i <> 1 or j <> len f and
      A4: i <> len f or j <> 1 and
      A5: f.i = f.j;
     f/.i = f.j by A2,A5,FINSEQ_4:def 4
        .= f/.j by A2,FINSEQ_4:def 4;
     hence i = j by A1,A2,A3,A4;
    end;
    hence f is almost-one-to-one by Def1;
   end;

  theorem ThRob45:
   for D be set
   for f be FinSequence of D holds f is weakly-one-to-one iff
    for i be Nat st 1 <= i & i < len f holds f/.i <> f/.(i+1)
   proof
    let D be set;
    let f be FinSequence of D;
    thus f is weakly-one-to-one implies
     for i be Nat st 1 <= i & i < len f holds f/.i <> f/.(i+1)
    proof
     assume A1: f is weakly-one-to-one;
     let i be Nat such that
      A2: 1 <= i & i < len f;
     A3: f.i = f/.i by A2,FINSEQ_4:24;
     1 < i+1 & i+1 <= len f by A2,NAT_1:38;
     then f.(i+1) = f/.(i+1) by FINSEQ_4:24;
     hence f/.i <> f/.(i+1) by A1,A2,A3,Def2;
    end;
    assume A1: for i be Nat st 1 <= i & i < len f holds f/.i <> f/.(i+1);
    now let i be Nat such that
      A2: 1 <= i & i < len f;
     A3: f.i = f/.i by A2,FINSEQ_4:24;
     1 < i+1 & i+1 <= len f by A2,NAT_1:38;
     then f.(i+1) = f/.(i+1) by FINSEQ_4:24;
     hence f.i <> f.(i+1) by A1,A2,A3;
    end;
    hence f is weakly-one-to-one by Def2;
   end;

  theorem
   for D be set
   for f be FinSequence of D holds f is poorly-one-to-one iff
    (len f <> 2 implies
     for i be Nat st 1 <= i & i < len f holds f/.i <> f/.(i+1))
   proof
    let D be set;
    let f be FinSequence of D;
    thus f is poorly-one-to-one & len f <> 2 implies
     for i be Nat st 1 <= i & i < len f holds f/.i <> f/.(i+1)
    proof
     assume A1: f is poorly-one-to-one & len f <> 2;
     let i be Nat such that
      A2: 1 <= i & i < len f;
     A3: f.i = f/.i by A2,FINSEQ_4:24;
     1 < i+1 & i+1 <= len f by A2,NAT_1:38;
     then f.(i+1) = f/.(i+1) by FINSEQ_4:24;
     hence f/.i <> f/.(i+1) by A1,A2,A3,Def3;
    end;
    assume A1: len f <> 2 implies
     for i be Nat st 1 <= i & i < len f holds f/.i <> f/.(i+1);
    per cases;
     suppose A2: len f <> 2;
      now let i be Nat such that
        A3: 1 <= i & i < len f;
       A4: f.i = f/.i by A3,FINSEQ_4:24;
       1 < i+1 & i+1 <= len f by A3,NAT_1:38;
       then f.(i+1) = f/.(i+1) by FINSEQ_4:24;
       hence f.i <> f.(i+1) by A1,A2,A3,A4;
      end;
      hence f is poorly-one-to-one by A2,Def3;
     end;
     suppose len f = 2;
      hence f is poorly-one-to-one by Def3;
     end;
   end;

  registration
   cluster one-to-one -> almost-one-to-one FinSequence;
   coherence
   proof
    let f be FinSequence;
    assume A0: f is one-to-one;
    let i,j be Nat;
    assume that
     A1: i in dom f and
     A2: j in dom f and
     i <> 1 or j <> len f and
     i <> len f or j <> 1 and
     A3: f.i = f.j;
    thus i = j by A0,A1,A2,A3,FUNCT_1:def 8;
   end;
  end;

  registration
   cluster almost-one-to-one -> poorly-one-to-one FinSequence;
   coherence
   proof
    let f be FinSequence;
    assume A0: f is almost-one-to-one;
    per cases;
     case A7: len f <> 2;
      now per cases;
       suppose A8: len f <> 0;
        let i be Nat;
        assume A2: 1 <= i & i < len f;
        then A1: i in dom f by FINSEQ_3:27;
        A3: i+1 >= 1 by NAT_1:29;
        i+1 <= len f by A2,NAT_1:38;
        then A4: i+1 in dom f by A3,FINSEQ_3:27;
        A5: i <> i+1 by NAT_1:38;
        A6: not (i = 1 & i+1 = len f) by A7;
        not (i = len f & i+1 = 1) by A8,XCMPLX_1:3;
        hence f.i <> f.(i+1) by A0,A1,A4,A5,A6,Def1;
       end;
       suppose A8: len f = 0;
        let i be Nat;
        assume 1 <= i & i < len f;
        hence f.i <> f.(i+1) by A8;
       end;
      end;
      hence thesis;
     end;
     case len f = 2;
      thus thesis;
     end;
   end;
  end;

  theorem ThRob9:
   for f be FinSequence st len f <> 2 holds
    f is weakly-one-to-one iff f is poorly-one-to-one
   proof
    let f be FinSequence;
    assume A1: len f <> 2;
    thus f is weakly-one-to-one implies f is poorly-one-to-one
    proof
     assume f is weakly-one-to-one;
     then for i be Nat st 1 <= i & i < len f holds f.i <> f.(i+1) by Def2;
     hence f is poorly-one-to-one by A1,Def3;
    end;
    assume f is poorly-one-to-one;
    then for i be Nat st 1 <= i & i < len f holds f.i <> f.(i+1) by A1,Def3;
    hence f is weakly-one-to-one by Def2;
   end;

  registration
   cluster {} -> weakly-one-to-one;
   coherence
   proof
    let i be Nat;
    assume 1 <= i & i < len {};
    hence {}.i <> {}.(i+1) by FINSEQ_1:25;
   end;
  end;

  registration
   let x be set;
   cluster <*x*> -> weakly-one-to-one;
   coherence
   proof
    let i be Nat;
    assume 1 <= i & i < len <*x*>;
    hence <*x*>.i <> <*x*>.(i+1) by FINSEQ_1:56;
   end;
  end;

  registration
   let x,y be set;
   cluster <*x,y*> -> poorly-one-to-one;
   coherence
   proof
    len <*x,y*> = 2 by FINSEQ_1:61;
    hence thesis by Def3;
   end;
  end;

  registration
   cluster weakly-one-to-one non empty FinSequence;
   existence
   proof
    consider x be set;
    len <*x*> <> 2 by FINSEQ_1:56;
    hence thesis;
   end;
  end;

  registration
   let D be non empty set;
   cluster weakly-one-to-one circular non empty FinSequence of D;
   existence
   proof
    consider x be Element of D;
    <*x*>/.1 = <*x*>/.len <*x*> by FINSEQ_1:56;
    then <*x*> is circular by FINSEQ_6:def 1;
    hence thesis;
   end;
  end;

  theorem ThRob10:
   for f be FinSequence st f is almost-one-to-one holds
    Rev f is almost-one-to-one
   proof
    let f be FinSequence;
    assume A00: f is almost-one-to-one;
    let i,j be Nat;
    assume that
     A1: i in dom Rev f and
     A2: j in dom Rev f and
     A3: i <> 1 or j <> len Rev f and
     A4: i <> len Rev f or j <> 1 and
     A5: Rev f.i = Rev f.j;
    A6: dom f = dom Rev f by FINSEQ_5:60;
    i in Seg len f by A1,A6,FINSEQ_1:def 3;
    then len f - i + 1 in Seg len f by FINSEQ_5:2;
    then A10: len f - i + 1 in dom f by FINSEQ_1:def 3;
    j in Seg len f by A2,A6,FINSEQ_1:def 3;
    then len f - j + 1 in Seg len f by FINSEQ_5:2;
    then A11: len f - j + 1 in dom f by FINSEQ_1:def 3;
    A12: now assume A8: len f - i + 1 = 1 & len f - j + 1 = len f;
     then len f - i = 0 by XCMPLX_1:3;
     then A9: len f = 0 + i;
     len f - (j - 1) = len f by A8,XCMPLX_1:37;
     then j - 1 = 0 by XCMPLX_1:16;
     then j = 0 + 1;
     hence contradiction by A4,A9,FINSEQ_5:def 3;
    end;
    A13: now assume A8: len f - i + 1 = len f & len f - j + 1 = 1;
     then len f - j = 0 by XCMPLX_1:3;
     then A9: len f = 0 + j;
     len f - (i - 1) = len f by A8,XCMPLX_1:37;
     then i - 1 = 0 by XCMPLX_1:16;
     then i = 0 + 1;
     hence contradiction by A3,A9,FINSEQ_5:def 3;
    end;
    Rev f.i = f.(len f - i + 1) & Rev f.j = f.(len f - j + 1)
                                                     by A1,A2,FINSEQ_5:def 3;
    then len f - i + 1 = len f - j + 1 by A00,A5,A10,A11,A12,A13,Def1;
    then len f - i = len f - j by XCMPLX_1:2;
    hence i = j by XCMPLX_1:20;
   end;

  theorem ThRob11:
   for f be FinSequence st f is weakly-one-to-one holds
    Rev f is weakly-one-to-one
   proof
    let f be FinSequence;
    assume A00: f is weakly-one-to-one;
    let i be Nat;
    assume that
     A1: 1 <= i and
     A2: i < len Rev f;
    A8: len f = len Rev f by FINSEQ_5:def 3;
    A9: i in Seg len Rev f by A1,A2,FINSEQ_1:3;
    then A3: i in dom Rev f by FINSEQ_1:def 3;
    A4: i+1 >= 1 by NAT_1:29;
    A11: i+1 <= len Rev f by A2,NAT_1:38;
    then A10: i+1 in Seg len Rev f by A4,FINSEQ_1:3;
    then i+1 in dom Rev f by FINSEQ_1:def 3;
    then A5: Rev f.i = f.(len f - i + 1) & Rev f.(i+1) = f.(len f - (i+1) + 1)
                                                        by A3,FINSEQ_5:def 3;
    len f - (i+1) + 1 = len f - i;
    then reconsider j = len f - i as Nat by A8,A11,FINSEQ_5:1;
    len f - (i+1) + 1 in Seg len f by A8,A10,FINSEQ_5:2;
    then A6: j >= 1 by FINSEQ_1:3;
    len f - i + 1 in Seg len f by A8,A9,FINSEQ_5:2;
    then j+1 <= len f by FINSEQ_1:3;
    then j < len f by NAT_1:38;
    hence Rev f.i <> Rev f.(i+1) by A00,A5,A6,Def2;
   end;

  theorem ThRob12:
   for f be FinSequence st f is poorly-one-to-one holds
    Rev f is poorly-one-to-one
   proof
    let f be FinSequence;
    assume A00: f is poorly-one-to-one;
    A2: len f = len Rev f by FINSEQ_5:def 3;
    per cases;
     suppose A1: len f <> 2;
      then f is weakly-one-to-one by A00,ThRob9;
      then Rev f is weakly-one-to-one by ThRob11;
      hence Rev f is poorly-one-to-one by A1,A2,ThRob9;
     end;
     suppose len f = 2;
      hence Rev f is poorly-one-to-one by A2,Def3;
     end;
   end;

  registration :: FINSEQ_5
   cluster one-to-one non empty FinSequence;
   existence
   proof
    consider x be set;
    <*x*> is non empty one-to-one;
    hence thesis;
   end;
  end;

  registration
   let f be almost-one-to-one FinSequence;
   cluster Rev f -> almost-one-to-one;
   coherence by ThRob10;
  end;

  registration
   let f be weakly-one-to-one FinSequence;
   cluster Rev f -> weakly-one-to-one;
   coherence by ThRob11;
  end;

  registration
   let f be poorly-one-to-one FinSequence;
   cluster Rev f -> poorly-one-to-one;
   coherence by ThRob12;
  end;

  theorem ThRob13:
   for D be non empty set
   for f be FinSequence of D st f is almost-one-to-one
   for p be Element of D holds
    Rotate(f,p) is almost-one-to-one
   proof
    let D be non empty set;
    let f be FinSequence of D;
    assume A00: f is almost-one-to-one;
    let p be Element of D;
    per cases;
     suppose A0: p in rng f;
      A17: 0+1 <= p..f & p..f <= len f by A0,FINSEQ_4:31;
      A30: len (f:-p) = len f - p..f + 1 by A0,FINSEQ_5:53;
      let i,j be Nat;
      assume that
       A1: i in dom Rotate(f,p) and
       A2: j in dom Rotate(f,p) and
       A3: i <> 1 or j <> len Rotate(f,p) and
       A4: i <> len Rotate(f,p) or j <> 1 and
       A5: Rotate(f,p).i = Rotate(f,p).j;
      A6: dom Rotate(f,p) = dom f by REVROT_1:15;
      A23: len Rotate(f,p) = len f by REVROT_1:14;
      A7: 0+1 <= i & i <= len f by A1,A6,FINSEQ_3:27;
      A8: 0+1 <= j & j <= len f by A2,A6,FINSEQ_3:27;
      A20: i - 1 >= 0 & j - 1 >= 0 by A7,A8,REAL_1:84;
      then A18: i -' 1 >= 0 & j -' 1 >= 0 by BINARITH:def 3;
      A32_: j - len f <= len f-len f & i - len f <= len f-len f 
         by A7,A8,REAL_1:49;
      A19: p..f - 1 >= 0 by A17,REAL_1:84;
      A12: now assume A22: i -' 1 + p..f = 1 & j -' 1 + p..f = len f;
       then i -' 1 + (p..f - 1) = 0;
       then A24: i -' 1 = 0 & p..f - 1 = 0 by A18,A19,SPPOL_1:8;
       then i - 1 = 0 by A20,BINARITH:def 3;
       hence contradiction by A3,A8,A22,A23,A24,AMI_5:4,XCMPLX_1:15;
      end;
      A13: now assume A22: i -' 1 + p..f = len f & j -' 1 + p..f = 1;
       then j -' 1 + (p..f - 1) = 0;
       then A24: j -' 1 = 0 & p..f - 1 = 0 by A18,A19,SPPOL_1:8;
       then j - 1 = 0 by A20,BINARITH:def 3;
       hence contradiction by A4,A7,A22,A23,A24,AMI_5:4,XCMPLX_1:15;
      end;
      now per cases;
       suppose A15: i <= len(f:-p);
        then Rotate(f,p)/.i = f/.(i -' 1 + p..f) by A0,A7,REVROT_1:9;
        then A9: Rotate(f,p).i = f/.(i -' 1 + p..f) by A1,FINSEQ_4:def 4;
        i in dom(f:-p) by A7,A15,FINSEQ_3:27;
        then i -' 1 + 1 in dom(f:-p) by A7,AMI_5:4;
        then A10: i -' 1 + p..f in dom f by A0,FINSEQ_5:54;
        now per cases;
         suppose A16: j <= len(f:-p);
          then Rotate(f,p)/.j = f/.(j -' 1 + p..f) by A0,A8,REVROT_1:9;
          then A14: f/.(i -' 1 + p..f) = f/.(j -' 1 + p..f)
                                                  by A2,A5,A9,FINSEQ_4:def 4;
          j in dom(f:-p) by A8,A16,FINSEQ_3:27;
          then j -' 1 + 1 in dom(f:-p) by A8,AMI_5:4;
          then A11: j -' 1 + p..f in dom f by A0,FINSEQ_5:54;
          f.(i -' 1 + p..f) = f/.(i -' 1 + p..f) by A10,FINSEQ_4:def 4
             .= f.(j -' 1 + p..f) by A11,A14,FINSEQ_4:def 4;
          then i -' 1 + p..f = j -' 1 + p..f by A00,A10,A11,A12,A13,Def1;
          then i -' 1 = j -' 1 by XCMPLX_1:2;
          hence i = j by A7,A8,SCMFSA_7:2;
         end;
         suppose A16: j > len(f:-p);
          then Rotate(f,p)/.j = f/.(j + p..f -' len f) by A0,A8,REVROT_1:12;
          then A14: f/.(i -' 1 + p..f) = f/.(j + p..f -' len f)
                                                  by A2,A5,A9,FINSEQ_4:def 4;
          A29_: j - (len f - p..f) > 1 by A16,A30,REAL_1:86;
          then A29: j + p..f - len f > 1;
          then A27: j + p..f -' len f >= 1 by BINARITH:def 3;
          A25: now assume A22: i -' 1 + p..f = 1 & j + p..f -' len f = len f;
           then i -' 1 + (p..f - 1) = 0;
           then i -' 1 = 0 & p..f - 1 = 0 by A18,A19,SPPOL_1:8;
           then A21: i - 1 = 0 by A20,BINARITH:def 3;
           j + p..f - len f = len f by A22,A29_,BINARITH:def 3;
           then j + p..f = len f + len f by XCMPLX_1:27;
           then j >= len f by A17,REAL_1:67;
           hence contradiction by A3,A8,A21,A23,AXIOMS:21;
          end;
          A26: i -' 1 + p..f <> len f or j + p..f -' len f <> 1
                                                    by A29,BINARITH:def 3;
          j + p..f <= len f + len f by A8,A17,REAL_1:55;
          then j + p..f - len f <= len f by REAL_1:86;
          then j + p..f -' len f <= len f by A29_,BINARITH:def 3;
          then A11: j + p..f -' len f in dom f by A27,FINSEQ_3:27;
          f.(i -' 1 + p..f) = f/.(i -' 1 + p..f) by A10,FINSEQ_4:def 4
             .= f.(j + p..f -' len f) by A11,A14,FINSEQ_4:def 4;
          then i -' 1 + p..f = j + p..f -' len f by A00,A10,A11,A25,A26,Def1;
          then i -' 1 + p..f = j + p..f - len f by A29_,BINARITH:def 3;
          then i -' 1 + p..f = j - len f + p..f;
          then i -' 1 = j - len f by XCMPLX_1:2;
          then i - 1 = 0 & j - len f = 0 by A20,A32_,BINARITH:def 3;
          then i = 0 + 1 & j = 0 + len f by XCMPLX_1:27;
          hence i = j by A3,REVROT_1:14;
         end;
        end;
        hence i = j;
       end;
       suppose A16: i > len(f:-p);
        then A9: Rotate(f,p)/.i = f/.(i+p..f -' len f) by A0,A7,REVROT_1:12;
        A29_: i - (len f - p..f) > 1 by A16,A30,REAL_1:86;
        then A29: i + p..f - len f > 1;
        A27: i + p..f -' len f >= 1 by A29,BINARITH:def 3;
        i + p..f <= len f + len f by A7,A17,REAL_1:55;
        then i + p..f - len f <= len f by REAL_1:86;
        then i + p..f -' len f <= len f by A29_,BINARITH:def 3;
        then A11: i + p..f -' len f in dom f by A27,FINSEQ_3:27;
        now per cases;
         suppose A15: j <= len(f:-p);
          then Rotate(f,p)/.j = f/.(j -' 1 + p..f) by A0,A8,REVROT_1:9;
          then Rotate(f,p).j = f/.(j -' 1 + p..f) by A2,FINSEQ_4:def 4;
          then A14: f/.(j -' 1 + p..f) = f/.(i + p..f -' len f)
                                                  by A1,A5,A9,FINSEQ_4:def 4;
          j in dom(f:-p) by A8,A15,FINSEQ_3:27;
          then j -' 1 + 1 in dom(f:-p) by A8,AMI_5:4;
          then A10: j -' 1 + p..f in dom f by A0,FINSEQ_5:54;
          A25: now assume A22: j -' 1 + p..f = 1 & i + p..f -' len f = len f;
           then j -' 1 + (p..f - 1) = 0;
           then j -' 1 = 0 & p..f - 1 = 0 by A18,A19,SPPOL_1:8;
           then A21: j - 1 = 0 by A20,BINARITH:def 3;
           i + p..f - len f = len f by A22,A29_,BINARITH:def 3;
           then i + p..f = len f + len f by XCMPLX_1:27;
           then i >= len f by A17,REAL_1:67;
           hence contradiction by A4,A7,A21,A23,AXIOMS:21;
          end;
          A26: j -' 1 + p..f <> len f or i + p..f -' len f <> 1
                                                    by A29,BINARITH:def 3;
          f.(j -' 1 + p..f) = f/.(j -' 1 + p..f) by A10,FINSEQ_4:def 4
             .= f.(i + p..f -' len f) by A11,A14,FINSEQ_4:def 4;
          then j -' 1 + p..f = i + p..f -' len f by A00,A10,A11,A25,A26,Def1;
          then j -' 1 + p..f = i + p..f - len f by A29_,BINARITH:def 3;
          then j -' 1 + p..f = i - len f + p..f;
          then j -' 1 = i - len f by XCMPLX_1:2;
          then j - 1 = 0 & i - len f = 0 by A20,A32_,BINARITH:def 3;
          then j = 0 + 1 & i = 0 + len f by XCMPLX_1:27;
          hence i = j by A4,REVROT_1:14;
         end;
         suppose A16': j > len(f:-p);
          then Rotate(f,p)/.j = f/.(j + p..f -' len f) by A0,A8,REVROT_1:12;
          then Rotate(f,p).j = f/.(j + p..f -' len f) by A2,FINSEQ_4:def 4;
          then A14: f/.(j + p..f -' len f) = f/.(i + p..f -' len f)
                                                  by A1,A5,A9,FINSEQ_4:def 4;
          A29'_: j - (len f - p..f) > 1 by A16',A30,REAL_1:86;
          then A29': j + p..f - len f > 1;
          A27: j + p..f -' len f >= 1 by A29',BINARITH:def 3;
          j + p..f <= len f + len f by A8,A17,REAL_1:55;
          then j + p..f - len f <= len f by REAL_1:86;
          then j + p..f -' len f <= len f by A29'_,BINARITH:def 3;
          then A11': j + p..f -' len f in dom f by A27,FINSEQ_3:27;
          A12: i+p..f -' len f <> 1 or j + p..f -' len f <> len f
                                                   by A29,BINARITH:def 3;
          A13: i+p..f -' len f <> len f or j + p..f -' len f <> 1
                                                 by A29',BINARITH:def 3;
          f.(i + p..f -' len f) = f/.(i + p..f -' len f) by A11,FINSEQ_4:def 4
             .= f.(j + p..f -' len f) by A11',A14,FINSEQ_4:def 4;
          then i + p..f -' len f = j + p..f -' len f
                                                 by A00,A11,A11',A12,A13,Def1;
          then i + p..f - len f = j + p..f -' len f by A29_,BINARITH:def 3;
          then i + p..f - len f = j + p..f - len f by A29'_,BINARITH:def 3;
          then i + p..f = j + p..f by XCMPLX_1:19;
          hence i = j by XCMPLX_1:2;
         end;
        end;
        hence i = j;
       end;
      end;
      hence i = j;
     end;
     suppose not p in rng f;
      hence Rotate(f,p) is almost-one-to-one by A00,FINSEQ_6:def 2;
     end;
   end;

  theorem ThRob14:
   for D be non empty set
   for f be FinSequence of D st f is weakly-one-to-one circular
   for p be Element of D holds
    Rotate(f,p) is weakly-one-to-one
   proof
    let D be non empty set;
    let f be FinSequence of D;
    assume A00: f is weakly-one-to-one circular;
    let p be Element of D;
    per cases;
     suppose A0: p in rng f;
      let i be Nat;
      A13: 1 <= p..f & p..f <= len f by A0,FINSEQ_4:31;
      A14: len (f:-p) = len f - p..f + 1 by A0,FINSEQ_5:53;
      A5: len Rotate(f,p) = len f by REVROT_1:14;
      assume A1: 1 <= i & i < len Rotate(f,p);
      then A2: i+1 > 1 & i+1 <= len Rotate(f,p) by NAT_1:38;
      A15: len f > 1 by A1,A5,AXIOMS:22;
      then A19: len f >= 1+1 by NAT_1:38;
      i-1+1 >= 1 by A1;
      then A11: i-1 >= 1-1 by REAL_1:86;
      i -' 1 >= 0 by A11,BINARITH:def 3;
      then A6: i -' 1 + p..f >= 0+1 by A13,REAL_1:55;
      then A9: i -' 1 + p..f + 1 > 1 by NAT_1:38;
      A4: i + 1 -' 1 + p..f = i + 1 - 1 + p..f by A1,BINARITH:def 3
         .= i - 1 + p..f + 1
         .= i -' 1 + p..f + 1 by A11,BINARITH:def 3;
      now per cases by REAL_1:def 5;
       suppose A3: i < len(f:-p);
        then i - 1 < len f - p..f by A14,REAL_1:84;
        then i - 1 + p..f < len f by REAL_1:86;
        then A7: i -' 1 + p..f < len f by A11,BINARITH:def 3;
        then A10: i -' 1 + p..f + 1 <= len f by NAT_1:38;
        i+1 <= len(f:-p) by A3,NAT_1:38;
        then Rotate(f,p)/.(i+1) = f/.(i+1 -' 1 + p..f) by A0,A2,REVROT_1:9
           .= f.(i+1 -' 1 + p..f) by A4,A9,A10,FINSEQ_4:24;
        then A8: Rotate(f,p).(i+1) = f.(i+1 -' 1 + p..f) by A2,FINSEQ_4:24;
        Rotate(f,p)/.i = f/.(i -' 1 + p..f) by A0,A1,A3,REVROT_1:9
           .= f.(i -' 1 + p..f) by A6,A7,FINSEQ_4:24;
        then Rotate(f,p).i = f.(i -' 1 + p..f) by A1,FINSEQ_4:24;
        hence Rotate(f,p).i <> Rotate(f,p).(i+1) by A00,A4,A6,A7,A8,Def2;
       end;
       suppose A17: i = len(f:-p);
        then Rotate(f,p)/.i = f/.len f by A0,REVROT_1:11
           .= f/.1 by A00,FINSEQ_6:def 1
           .= f.1 by A15,FINSEQ_4:24;
        then A16: Rotate(f,p).i = f.1 by A1,FINSEQ_4:24;
        A18: i + 1 + p..f -' len f = len f + (1+1) - len f 
           by A14,A17,BINARITH:def 3
           .= 2;
        i + 1 > len(f:-p) by A17,NAT_1:38;
        then Rotate(f,p)/.(i+1) = f/.(i + 1 + p..f -' len f)
                                                      by A0,A2,A5,REVROT_1:12
           .= f.(i + 1 + p..f -' len f) by A18,A19,FINSEQ_4:24;
        then Rotate(f,p).(i+1) = f.(1+1) by A2,A18,FINSEQ_4:24;
        hence Rotate(f,p).i <> Rotate(f,p).(i+1) by A00,A15,A16,Def2;
       end;
       suppose A17: i > len(f:-p);
        A21_: i - (len f - p..f) > 1 by A14,A17,REAL_1:86;
        then A21: i + p..f - len f > 1;
        A27: i + p..f - len f > 1-1 by A21_,AXIOMS:22;
        A22: i + p..f -' len f > 1 by A21,BINARITH:def 3;
        A26_: i + p..f - len f + 1 > 1 by A27,REAL_1:84;
        then i+1 + p..f - len f > 1;
        then A24: i+1 + p..f -' len f > 1 by BINARITH:def 3;
        i + p..f < len f + len f by A1,A5,A13,REAL_1:67;
        then i + p..f - len f < len f by REAL_1:84;
        then A23: i + p..f -' len f < len f by A21_,BINARITH:def 3;
        i+1 + p..f <= len f + len f by A2,A5,A13,REAL_1:55;
        then i+1 + p..f - len f <= len f by REAL_1:86;
        then A25: i+1 + p..f -' len f <= len f by A26_,BINARITH:def 3;
        i+1 > len(f:-p) by A17,NAT_1:38;
        then Rotate(f,p)/.(i+1) = f/.(i+1 + p..f -' len f)
                                                       by A0,A2,A5,REVROT_1:12
           .= f.(i+1 + p..f -' len f) by A24,A25,FINSEQ_4:24;
        then A29: Rotate(f,p).(i+1) = f.(i+1+p..f -' len f) by A2,FINSEQ_4:24;
        Rotate(f,p)/.i = f/.(i + p..f -' len f) by A0,A1,A5,A17,REVROT_1:12
           .= f.(i + p..f -' len f) by A22,A23,FINSEQ_4:24;
        then A30: Rotate(f,p).i = f.(i + p..f -' len f) by A1,FINSEQ_4:24;
        i + 1 + p..f -' len f = i + 1 + p..f - len f by A26_,BINARITH:def 3
           .= i + p..f - len f + 1
           .= i + p..f -' len f + 1 by A21_,BINARITH:def 3;
        hence Rotate(f,p).i <> Rotate(f,p).(i+1) by A00,A22,A23,A29,A30,Def2;
       end;
      end;
      hence Rotate(f,p).i <> Rotate(f,p).(i+1);
     end;
     suppose not p in rng f;
      hence Rotate(f,p) is weakly-one-to-one by A00,FINSEQ_6:def 2;
     end;
   end;

  theorem ThRob15:
   for D be non empty set
   for f be FinSequence of D st f is poorly-one-to-one circular
   for p be Element of D holds
    Rotate(f,p) is poorly-one-to-one
   proof
    let D be non empty set;
    let f be FinSequence of D;
    assume A00: f is poorly-one-to-one circular;
    let p be Element of D;
    A2: len Rotate(f,p) = len f by REVROT_1:14;
    per cases;
     suppose A1: len f <> 2;
      then f is weakly-one-to-one by A00,ThRob9;
      then Rotate(f,p) is weakly-one-to-one by A00,ThRob14;
      hence Rotate(f,p) is poorly-one-to-one by A1,A2,ThRob9;
     end;
     suppose len f = 2;
      hence Rotate(f,p) is poorly-one-to-one by A2,Def3;
     end;
   end;

  registration :: FINSEQ_6
   let D be non empty set;
   cluster one-to-one circular non empty FinSequence of D;
   existence
   proof
    consider x be Element of D;
    <*x*>/.1 = <*x*>/.len <*x*> by FINSEQ_1:56;
    then <*x*> is circular by FINSEQ_6:def 1;
    hence thesis;
   end;
  end;

  registration
   let D be non empty set;
   let f be almost-one-to-one FinSequence of D;
   let p be Element of D;
   cluster Rotate(f,p) -> almost-one-to-one;
   coherence by ThRob13;
  end;

  registration
   let D be non empty set;
   let f be circular weakly-one-to-one FinSequence of D;
   let p be Element of D;
   cluster Rotate(f,p) -> weakly-one-to-one;
   coherence by ThRob14;
  end;

  registration
   let D be non empty set;
   let f be circular poorly-one-to-one FinSequence of D;
   let p be Element of D;
   cluster Rotate(f,p) -> poorly-one-to-one;
   coherence by ThRob15;
  end;

  theorem ThRob28:
   for D be non empty set
   for f be FinSequence of D holds
    f is almost-one-to-one iff f/^1 is one-to-one & f|(len f-'1) is one-to-one
   proof
    let D be non empty set;
    let f be FinSequence of D;
    A9: len (f/^1) = len f-'1 by JORDAN3:19;
    thus f is almost-one-to-one implies
     f/^1 is one-to-one & f|(len f-'1) is one-to-one
    proof
     assume A0: f is almost-one-to-one;
     thus f/^1 is one-to-one
     proof
      let x,y be set;
      assume that
       A1: x in dom (f/^1) and
       A2: y in dom (f/^1) and
       A3: (f/^1).x = (f/^1).y;
      reconsider i=x, j=y as Nat by A1,A2;
      i in Seg len (f/^1) & j in Seg len (f/^1) by A1,A2,FINSEQ_1:def 3;
      then A4: 1 <= i & i <= len (f/^1) & 1 <= j & j <= len (f/^1)
                                                                by FINSEQ_1:3;
      then len f-'1 >= 1 by A9,AXIOMS:22;
      then len f-'1 = len f-1 by JORDAN3:1;
      then A7: i+1 <= len f & j+1 <= len f by A4,A9,REAL_1:84;
      then A5: (f/^1).i = f.(i+1) & (f/^1).j = f.(j+1) by A4,JORDAN3:23;
      A6: i+1 > 1 & j+1 > 1 by A4,NAT_1:38;
      then i+1 in dom f & j+1 in dom f by A7,FINSEQ_3:27;
      then i+1 = j+1 by A0,A3,A5,A6,Def1;
      hence x = y by XCMPLX_1:2;
     end;
     len f-'1 <= len f by GOBOARD9:2;
     then A10: len (f|(len f-'1)) = len f-'1 by FINSEQ_1:80;
     thus f|(len f-'1) is one-to-one
     proof
      let x,y be set;
      assume that
       A1: x in dom (f|(len f-'1)) and
       A2: y in dom (f|(len f-'1)) and
       A3: (f|(len f-'1)).x = (f|(len f-'1)).y;
      reconsider i=x, j=y as Nat by A1,A2;
      i in Seg len (f|(len f-'1)) & j in Seg len (f|(len f-'1))
                                                     by A1,A2,FINSEQ_1:def 3;
      then A4: 1 <= i & i <= len (f|(len f-'1)) &
       1 <= j & j <= len (f|(len f-'1)) by FINSEQ_1:3;
      then len f-'1 >= 1 by A10,AXIOMS:22;
      then A8: len f-'1 = len f-1 by JORDAN3:1;
      A5: (f|(len f-'1)).i = f.i & (f|(len f-'1)).j = f.j
                                                      by A4,A10,JORDAN3:20;
      i+1 <= len f & j+1 <= len f by A4,A8,A10,REAL_1:84;
      then A6: i < len f & j < len f by NAT_1:38;
      then i in dom f & j in dom f by A4,FINSEQ_3:27;
      hence x = y by A0,A3,A5,A6,Def1;
     end;
    end;
    assume that
     A13: f/^1 is one-to-one and
     A14: f|(len f-'1) is one-to-one;
    let i,j be Nat;
    assume that
     A1: i in dom f and
     A2: j in dom f and
     A3: i <> 1 or j <> len f and
     A4: i <> len f or j <> 1 and
     A5: f.i = f.j;
    A6: 0+1 <= i & i <= len f & 0+1 <= j & j <= len f
      by A1,A2,FINSEQ_3:27;
    A7: i-1 >= 0 & j-1 >= 0 by A6,REAL_1:84;
    A16: len f >= 0+1 by A6,AXIOMS:22;
    then A12: i = i-'1+1 & j = j-'1+1 & len f = len f-'1+1 by A6,AMI_5:4;
    A8: len f-1 >= 0 by A16,REAL_1:84;
    i-1 <= len f-1 & j-1 <= len f-1 by A6,REAL_1:49;
    then i-'1 <= len f-1 & j-'1 <= len f-1 by A7,BINARITH:def 3;
    then A10: i-'1 <= len f-'1 & j-'1 <= len f-'1 by A8,BINARITH:def 3;
    f is non empty by A1,FINSEQ_1:26;
    then A11: f = <* f/.1 *> ^ (f/^1) by FINSEQ_5:32;
    len f-'1 <= len f by A12,NAT_1:38;
    then A21: len (f|(len f-'1)) = len f-'1 by FINSEQ_1:80;
    per cases;
     suppose i <> 1 & j <> 1;
      then i > 0+1 & j > 0+1 by A6,REAL_1:def 5;
      then i-1 > 0 & j-1 > 0 by REAL_1:86;
      then i-'1 > 0 & j-'1 > 0 by BINARITH:def 3;
      then i-'1 >= 0+1 & j-'1 >= 0+1 by NAT_1:38;
      then A15: i-'1 in dom (f/^1) & j-'1 in dom (f/^1) by A9,A10,FINSEQ_3:27;
      then f.i = (f/^1).(i-'1) & f.j = (f/^1).(j-'1) by A11,A12,FINSEQ_3:112;
      then i-'1 = j-'1 by A5,A13,A15,FUNCT_1:def 8;
      then i-1 = j-'1 by A7,BINARITH:def 3;
      then i-1 = j-1 by A7,BINARITH:def 3;
      hence i = j by XCMPLX_1:19;
     end;
     suppose A19: i = 1;
      then j < len f by A3,A6,REAL_1:def 5;
      then j+1 <= len f by NAT_1:38;
      then j <= len f-1 by REAL_1:84;
      then A17: j <= len f-'1 by A8,BINARITH:def 3;
      then A18: (f|(len f-'1)).j = f.j by JORDAN3:20;
      A22: i <= len f-'1 by A6,A17,A19,AXIOMS:22;
      then A20: (f|(len f-'1)).i = f.i by JORDAN3:20;
      i in dom(f|(len f-'1)) & j in dom(f|(len f-'1))
        by A6,A17,A21,A22,FINSEQ_3:27;
      hence i = j by A5,A14,A18,A20,FUNCT_1:def 8;
     end;
     suppose A19: j = 1;
      then i < len f by A4,A6,REAL_1:def 5;
      then i+1 <= len f by NAT_1:38;
      then i <= len f-1 by REAL_1:84;
      then A17: i <= len f-'1 by A8,BINARITH:def 3;
      then A18: (f|(len f-'1)).i = f.i by JORDAN3:20;
      A22: j <= len f-'1 by A6,A17,A19,AXIOMS:22;
      then A20: (f|(len f-'1)).j = f.j by JORDAN3:20;
      i in dom(f|(len f-'1)) & j in dom(f|(len f-'1))
        by A6,A17,A21,A22,FINSEQ_3:27;
      hence i = j by A5,A14,A18,A20,FUNCT_1:def 8;
     end;
   end;

  registration
   let C be compact non vertical non horizontal Subset of TOP-REAL 2;
   let n be Nat;
   cluster Cage(C,n) -> almost-one-to-one;
   coherence
   proof
    A1: Cage(C,n) /^ 1 is one-to-one by SPRECT_5:8;
    A2: len Cage(C,n) > 4 by GOBOARD7:36;
    then len Cage(C,n) > 0+1 by AXIOMS:22;
    then len Cage(C,n)-1 > 0 by REAL_1:86;
    then A3: len Cage(C,n)-'1 = len Cage(C,n)-1 by BINARITH:def 3;
    len Cage(C,n) < len Cage(C,n)+1 by NAT_1:38;
    then len Cage(C,n)-'1 < len Cage(C,n) by A3,REAL_1:84;
    then Cage(C,n)|(len Cage(C,n)-'1) is one-to-one by A2,TOPREAL8:22;
    hence Cage(C,n) is almost-one-to-one by A1,ThRob28;
   end;
  end;

  registration
   let C be compact non vertical non horizontal Subset of TOP-REAL 2;
   let n be Nat;
   cluster Cage(C,n) -> weakly-one-to-one;
   coherence
   proof
    len Cage(C,n) > 4 by GOBOARD7:36;
    then len Cage(C,n) > 2 by AXIOMS:22;
    hence Cage(C,n) is weakly-one-to-one by ThRob9;
   end;
  end;

  theorem ThRob16: :: JORDAN5B:24
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st p in L~f & f is weakly-one-to-one holds
    B_Cut(f,p,p) = <*p*>
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: p in L~f and
     A4: f is weakly-one-to-one;
    A3: Index(p,f) < len f by A1,JORDAN3:41;
    then A5: 1 <= Index(p,f) & Index(p,f)+1 <= len f by A1,JORDAN3:41,NAT_1:38;
    then A8: Index(p,f) in dom f & Index(p,f) + 1 in dom f by GOBOARD2:3;
    f.Index(p,f) <> f.(Index(p,f)+1) by A3,A4,A5,Def2;
    then f.Index(p,f) <> f/.(Index(p,f)+1) by A8,FINSEQ_4:def 4;
    then A6: f/.Index(p,f) <> f/.(Index(p,f)+1) by A8,FINSEQ_4:def 4;
    p in LSeg(f,Index(p,f)) by A1,JORDAN3:42;
    then p in LSeg(f/.Index(p,f), f/.(Index(p,f)+1)) by A5,TOPREAL1:def 5;
    then A7: LE p,p,f/.Index(p,f),f/.(Index(p,f)+1) by A6,JORDAN5B:9;
    L_Cut(f,p).1 = p by A1,JORDAN3:58;
    then R_Cut(L_Cut(f,p),p) = <*p*> by JORDAN3:def 5;
    hence thesis by A7,JORDAN3:def 8;
   end;

theorem ThAd1:
  for f being FinSequence st f is one-to-one holds
    f is weakly-one-to-one
  proof
    let f be FinSequence;
    assume
A0: f is one-to-one;
    for i be Nat st 1 <= i & i < len f holds f.i <> f.(i+1)
    proof
      let i be Nat;
      assume 1 <= i & i < len f; then
      i in dom f & i+1 in dom f & i <> i+1 by MSUALG_8:1,NAT_1:38;
      hence thesis by A0,FUNCT_1:def 8;
    end;
    hence thesis by Def2;
  end;

registration
  cluster one-to-one -> weakly-one-to-one FinSequence;
  coherence by ThAd1;
end;

  theorem ThRob6:
   for f be FinSequence of TOP-REAL 2 st f is weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q) = Rev B_Cut(f,q,p)
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A00: f is weakly-one-to-one;
    let p,q be Point of TOP-REAL 2 such that
     A1: p in L~f and
     A2: q in L~f;
    per cases;
     suppose A2: p = q;
      then B_Cut(f,p,q) = <*p*> by A00,A1,ThRob16;
      hence B_Cut(f,p,q) = Rev B_Cut(f,q,p) by A2,FINSEQ_5:63;
     end;
     suppose that
       A4: p <> q and
       A5: Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
        LE p,q,f/.Index(p,f),f/.(Index(p,f)+1);
      A6: B_Cut(f,p,q) = R_Cut(L_Cut(f,p),q) by A1,A2,A5,JORDAN3:def 8;
      not (Index(q,f)=Index(p,f) & LE q,p,f/.(Index(q,f)),f/.(Index(q,f)+1))
       by A4,A5,JORDAN3:62;
      then Rev B_Cut(f,q,p) = Rev Rev (R_Cut(L_Cut(f,p),q))
                                                   by A5,JORDAN3:def 8;
      hence B_Cut(f,p,q) = Rev B_Cut(f,q,p) by A6,FINSEQ_6:29;
     end;
     suppose that
       p <> q and
       A5: not(Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
        LE p,q,f/.Index(p,f),f/.(Index(p,f)+1));
      A6: B_Cut(f,p,q) = Rev R_Cut(L_Cut(f,q),p) by A5,JORDAN3:def 8;
      A7: Index(q,f) < Index(p,f) or Index(p,f)=Index(q,f) &
       not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1) by A5,AXIOMS:21;
      now assume that
        A9: Index(p,f)=Index(q,f) and
        A10: not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1);
       A11: 1 <= Index(p,f) by A1,JORDAN3:41;
       A12: Index(p,f) < len f by A1,JORDAN3:41;
       then A13: Index(p,f)+1 <= len f by NAT_1:38;
       then A14: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                                                       by A11,TOPREAL1:def 5;
       then A15: p in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A1,JORDAN3:42;
       A16: q in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A2,A9,A14,JORDAN3:42;
       A17: Index(p,f) in dom f by A11,A12,FINSEQ_3:27;
       1 <= Index(p,f)+1 by NAT_1:29;
       then A18: Index(p,f)+1 in dom f by A13,FINSEQ_3:27;
       f.Index(p,f) <> f.(Index(p,f)+1) by A00,A11,A12,Def2;
       then f.Index(p,f) <> f/.(Index(p,f)+1) by A18,FINSEQ_4:def 4;
       then f/.Index(p,f) <> f/.(Index(p,f)+1) by A17,FINSEQ_4:def 4;
       then LT q,p,f/.Index(p,f),f/.(Index(p,f)+1) by A10,A15,A16,JORDAN3:63;
       hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A9,JORDAN3:def 7;
      end;
      hence B_Cut(f,p,q) = Rev B_Cut(f,q,p) by A1,A2,A6,A7,JORDAN3:def 8;
     end;
   end;

  theorem ThRob41:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2
   for i1 be Nat st f is poorly-one-to-one unfolded s.n.c. &
     1<i1 & i1<=len f & p=f.i1 holds
    Index(p,f) + 1 = i1
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2,i1 be Nat;
    assume A1: f is poorly-one-to-one unfolded s.n.c.;
    assume A2: 1<i1 & i1<=len f;
    then A3: i1 in dom f by FINSEQ_3:27;
    consider j being Nat such that
     A4: i1 = j+1 by A2,NAT_1:22;
    A5: 1 + 0 <= j by A2,A4,NAT_1:38;
    assume A6_: p=f.i1;
    then p = f/.i1 by A3,FINSEQ_4:def 4;
    then A7: p in LSeg(f,j) by A2,A4,A5,TOPREAL1:27;
    then A8: Index(p,f) <= j by JORDAN3:40;
    assume Index(p,f) + 1 <> i1;
    then Index(p,f) < j by A4,A8,AXIOMS:21;
    then A9: Index(p,f) + 1 <= j by NAT_1:38;
    A10:  LSeg(f,j) c= L~f by TOPREAL3:26;
    then A11: p in LSeg(f,Index(p,f)) by A7,JORDAN3:42;
    per cases by A9,AXIOMS:21;
     suppose A12: Index(p,f) + 1 = j;
      A13: 1 <= Index(p,f) & Index(p,f) + (1+1) <= len f
       by A2,A4,A7,A10,A12,JORDAN3:41;
      then LSeg(f,Index(p,f)) /\ LSeg(f,j) = {f/.j} by A1,A12,TOPREAL1:def 8;
      then A14: p in {f/.j} by A7,A11,XBOOLE_0:def 3;
      A15: j < len f by A2,A4,NAT_1:38;
      then j in dom f by A5,FINSEQ_3:27;
      then A16: f.j = f/.j by FINSEQ_4:def 4
         .= f.i1 by A6_,A14,TARSKI:def 1;
      Index(p,f) + 2 >= 1+2 by A13,REAL_1:55;
      then len f >= 2+1 by A2,A4,A12,AXIOMS:22;
      then len f > 2 by NAT_1:38;
      hence contradiction by A1,A4,A5,A15,A16,Def3;
     end;
     suppose A17: Index(p,f) + 1 < j;
      p in LSeg(f,Index(p,f)) /\ LSeg(f,j) by A7,A11,XBOOLE_0:def 3;
      then LSeg(f,Index(p,f)) meets LSeg(f,j) by XBOOLE_0:4;
      hence contradiction by A1,A17,TOPREAL1:def 9;
     end;
   end;

  theorem ThRob19:
   for f be FinSequence of TOP-REAL 2 st f is weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q)/.1 = p
   proof
    let f be FinSequence of TOP-REAL 2;
    assume B1: f is weakly-one-to-one;
    let p,q be Point of TOP-REAL 2;
    assume A0: p in L~f & q in L~f;
    A2: 1 <= len L_Cut(f,p) by A0,ThRob3;
    A28: 1 <= Index(p,f) & Index(p,f) < len f by A0,JORDAN3:41;
    then A13: Index(p,f) in dom f by FINSEQ_3:27;
    per cases;
     suppose A7: p <> q;
      now per cases;
       suppose A4: p in L~f & q in L~f & Index(p,f)<Index(q,f);
        then q in L~L_Cut(f,p) by JORDAN3:64;
        then (R_Cut(L_Cut(f,p),q))/.1 = L_Cut(f,p)/.1 by SPRECT_3:39
           .= L_Cut(f,p).1 by A2,FINSEQ_4:24
           .= p by A4,JORDAN3:58;
        hence B_Cut(f,p,q)/.1 = p by A4,JORDAN3:def 8;
       end;
       suppose A4: Index(p,f)=Index(q,f) &
         LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
        A3: q in L~L_Cut(f,p) by A0,A4,A7,JORDAN3:66;
        (R_Cut(L_Cut(f,p),q))/.1 = L_Cut(f,p)/.1 by A3,SPRECT_3:39
           .= L_Cut(f,p).1 by A2,FINSEQ_4:24
           .= p by A0,JORDAN3:58;
        hence B_Cut(f,p,q)/.1 = p by A4,JORDAN3:def 8;
       end;
       suppose A16: not (p in L~f & q in L~f & Index(p,f)<Index(q,f)) &
         not(Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
        then A17: B_Cut(f,p,q) = Rev (R_Cut(L_Cut(f,q),p)) by JORDAN3:def 8;
        now per cases by A0,A16,REAL_1:def 5;
         suppose Index(p,f) > Index(q,f);
          then A18: p in L~L_Cut(f,q) by A0,JORDAN3:64;
          R_Cut(L_Cut(f,q),p) <> {} by JORDAN1J:44;
          hence B_Cut(f,p,q)/.1 = R_Cut(L_Cut(f,q),p)/.len R_Cut(L_Cut(f,q),p)
                                                            by A17,FINSEQ_5:68
             .= p by A18,JORDAN1J:45;
         end;
         suppose A27: Index(p,f) = Index(q,f);
          set Ls = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1));
          A29: Index(p,f)+1 <= len f by A28,NAT_1:38;
          Index(p,f)+1 >= 1 by NAT_1:29;
          then A14: Index(p,f)+1 in dom f by A29,FINSEQ_3:27;
          f.Index(p,f) <> f.(Index(p,f)+1) by A28,B1,Def2;
          then f.Index(p,f) <> f/.(Index(p,f)+1) by A14,FINSEQ_4:def 4;
          then A26: f/.Index(p,f) <> f/.(Index(p,f)+1) by A13,FINSEQ_4:def 4;
          then A23: Ls is_an_arc_of f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                               by TOPREAL1:15;
          A25: not LE p,q,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                   by A16,A27,A26,JORDAN5C:17;
          A24: p in Ls by A0,JORDAN5B:32;
          A30: R_Cut(L_Cut(f,q),p) <> {} by JORDAN1J:44;
          q in Ls by A0,A27,JORDAN5B:32;
          then LE q,p,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                 by A7,A23,A24,A25,JORDAN5C:14;
          then LE q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A26,JORDAN5C:17;
          then A18: p in L~(L_Cut(f,q)) by A0,A7,A27,JORDAN3:66;
          thus B_Cut(f,p,q)/.1 = R_Cut(L_Cut(f,q),p)/.len R_Cut(L_Cut(f,q),p)
                                                        by A17,A30,FINSEQ_5:68
             .= p by A18,JORDAN1J:45;
         end;
        end;
        hence B_Cut(f,p,q)/.1 = p;
       end;
      end;
      hence thesis;
     end;
     suppose p = q;
      then B_Cut(f,p,q) = <*p*> by A0,B1,ThRob16;
      hence B_Cut(f,p,q)/.1 = p by FINSEQ_4:25;
     end;
   end;

  theorem ThA:
   for f be FinSequence of TOP-REAL 2 st f is weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q)/.len B_Cut(f,p,q) = q
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A0: f is weakly-one-to-one;
    let p,q be Point of TOP-REAL 2 such that
     A1: p in L~f and
     A2: q in L~f;
    A3: B_Cut(f,q,p) <> {} by ThRob8;
    B_Cut(f,p,q) = Rev B_Cut(f,q,p) by A0,A1,A2,ThRob6;
    hence B_Cut(f,p,q)/.len B_Cut(f,p,q) = Rev B_Cut(f,q,p)/.len B_Cut(f,q,p)
                                                            by FINSEQ_5:def 3
       .= B_Cut(f,q,p)/.1 by A3,FINSEQ_5:68
       .= q by A0,A1,A2,ThRob19;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st p in L~f holds
    L~L_Cut(f,p) c= L~f
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2 such that
     A1: p in L~f;
    len f <> 0 by A1,TOPREAL1:28;
    then len f > 0 by NAT_1:19;
    then A3: len f >= 0+1 by NAT_1:38;
    then A14: len f in dom f by FINSEQ_3:27;
    A4: 1<=Index(p,f) & Index(p,f)<len f by A1,JORDAN3:41;
    then A6: 1<Index(p,f)+1 & Index(p,f)+1<=len f by NAT_1:38;
    then A13: Index(p,f)+1 in dom f by FINSEQ_3:27;
    per cases;
     suppose p = f.(Index(p,f)+1);
      then L_Cut(f,p) = mid(f,Index(p,f)+1,len f) by JORDAN3:def 4;
      hence thesis by A3,A6,JORDAN4:47;
     end;
     suppose p <> f.(Index(p,f)+1);
      then A5: L_Cut(f,p) = <*p*>^mid(f,Index(p,f)+1,len f) by JORDAN3:def 4;
      len mid(f,Index(p,f)+1,len f) >= 0+1 by A13,A14,SPRECT_2:9;
      then mid(f,Index(p,f)+1,len f) <> {} by FINSEQ_1:25;
      then A8: L~(<*p*>^mid(f,Index(p,f)+1,len f))
         = LSeg(p,mid(f,Index(p,f)+1,len f)/.1) \/ L~mid(f,Index(p,f)+1,len f)
                                                             by SPPOL_2:20;
      A9: mid(f,Index(p,f)+1,len f)/.1 = f/.(Index(p,f)+1)
                                                       by A13,A14,SPRECT_2:12;
      p in LSeg(f,Index(p,f)) by A1,JORDAN3:42;
      then A10: p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
                                                     by A4,A6,TOPREAL1:def 5;
      f/.(Index(p,f)+1) in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
                                                                by TOPREAL1:6;
      then A11: LSeg(p,mid(f,Index(p,f)+1,len f)/.1) c=
       LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A9,A10,TOPREAL1:12;
      LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) c= L~f by A4,A6,SPPOL_2:16;
      then A12: LSeg(p,mid(f,Index(p,f)+1,len f)/.1) c= L~f by A11,XBOOLE_1:1;
      L~mid(f,Index(p,f)+1,len f) c= L~f by A3,A6,JORDAN4:47;
      hence L~(L_Cut(f,p)) c= L~f by A5,A8,A12,XBOOLE_1:8;
     end;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f &
     f is weakly-one-to-one holds
    L~B_Cut(f,p,q) c= L~f
   proof
    let f be FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2 such that
     A1: p in L~f and
     A2: q in L~f and
     A3: f is weakly-one-to-one;
    per cases;
     suppose p = q;
      then B_Cut(f,p,q) = <*p*> by A1,A3,ThRob16;
      then len B_Cut(f,p,q) = 1 by FINSEQ_1:56;
      then L~B_Cut(f,p,q) = {} by TOPREAL1:28;
      hence thesis by XBOOLE_1:2;
     end;
     suppose p <> q & (Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
       LE p,q,f/.Index(p,f),f/.(Index(p,f)+1));
      hence thesis by A1,A2,JORDAN5B:36;
     end;
     suppose that
       A4: p <> q and
       A5: not(Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
        LE p,q,f/.Index(p,f),f/.(Index(p,f)+1));
      A6: B_Cut(f,p,q)=Rev R_Cut(L_Cut(f,q),p) by A5,JORDAN3:def 8;
      A7: Index(q,f) < Index(p,f) or
       Index(p,f)=Index(q,f) & not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1)
                                                              by A5,AXIOMS:21;
      A8: now assume that
        A9: Index(p,f)=Index(q,f) and
        A10: not LE p,q,f/.Index(p,f),f/.(Index(p,f)+1);
       A11: 1 <= Index(p,f) by A1,JORDAN3:41;
       A12: Index(p,f) < len f by A1,JORDAN3:41;
       then A13: Index(p,f)+1 <= len f by NAT_1:38;
       then A14: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                                                       by A11,TOPREAL1:def 5;
       then A15: p in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A1,JORDAN3:42;
       A16: q in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by A2,A9,A14,JORDAN3:42;
       A17: Index(p,f) in dom f by A11,A12,FINSEQ_3:27;
       1 <= Index(p,f)+1 by NAT_1:29;
       then A18: Index(p,f)+1 in dom f by A13,FINSEQ_3:27;
       f.Index(p,f) <> f.(Index(p,f)+1) by A3,A11,A12,Def2;
       then f/.Index(p,f) <> f.(Index(p,f)+1) by A17,FINSEQ_4:def 4;
       then f/.Index(p,f) <> f/.(Index(p,f)+1) by A18,FINSEQ_4:def 4;
       then LT q,p,f/.Index(p,f),f/.(Index(p,f)+1) by A10,A15,A16,JORDAN3:63;
       hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A9,JORDAN3:def 7;
      end;
      then A20:  Rev B_Cut(f,q,p) = B_Cut(f,p,q) by A1,A2,A6,A7,JORDAN3:def 8;
      L~B_Cut(f,q,p) c= L~f by A1,A2,A4,A7,A8,JORDAN5B:36;
      hence thesis by A20,SPPOL_2:22;
     end;
   end;

  theorem ThRob46:
   for f,g be FinSequence holds dom f c= dom (f^'g)
   proof
    let f,g be FinSequence;
    len f <= len(f^'g) by TOPREAL8:7;
    hence dom f c= dom (f^'g) by FINSEQ_3:32;
   end;

  theorem
   for f be non empty FinSequence
   for g be FinSequence holds dom g c= dom (f^'g)
   proof
    let f be non empty FinSequence;
    let g be FinSequence;
    len g <= len(f^'g) by TOPREAL8:9;
    hence dom g c= dom (f^'g) by FINSEQ_3:32;
   end;

  theorem ThRob32:
   for f,g be FinSequence st f^'g is constant holds
    f is constant
   proof
    let f,g be FinSequence;
    assume f^'g is constant;
    then reconsider h = f^'g as constant FinSequence;
    rng f c= rng h by TOPREAL8:10;
    then rng f is trivial by SPRECT_1:1;
    hence f is constant by SPRECT_1:2;
   end;

  theorem
   for f,g be FinSequence st f^'g is constant & f.len f = g.1 & f <> {} holds
    g is constant
   proof
    let f,g be FinSequence;
    assume that
     A1: f^'g is constant and
     A2: f.len f = g.1 and
     A3: f <> {};
    reconsider h = f^'g as constant FinSequence by A1;
    per cases;
     suppose g <> {};
      then rng h = rng f \/ rng g by A2,A3,GRAPH_2:18;
      then rng g c= rng h by XBOOLE_1:7;
      then rng g is trivial by SPRECT_1:1;
      hence g is constant by SPRECT_1:2;
     end;
     suppose g = {};
      hence g is constant;
     end;
   end;

  theorem ThRob33:
   for f be special FinSequence of TOP-REAL 2
   for i,j be Nat holds
    mid(f,i,j) is special
   proof
    let f be special FinSequence of TOP-REAL 2;
    let i,j be Nat;
    per cases;
     suppose A1: i <= j;
      mid(f,i,j) = (f/^(i-'1))|(j-'i+1) by A1,JORDAN3:def 1;
      hence mid(f,i,j) is special;
     end;
     suppose A1: j < i;
      mid(f,j,i) = (f/^(j-'1))|(i-'j+1) by A1,JORDAN3:def 1;
      then Rev mid(f,i,j) is special by JORDAN4:30;
      then Rev Rev mid(f,i,j) is special by SPPOL_2:42;
      hence mid(f,i,j) is special by FINSEQ_6:29;
     end;
   end;

  theorem ThRob37:
   for f be unfolded FinSequence of TOP-REAL 2
   for i,j be Nat holds
    mid(f,i,j) is unfolded
   proof
    let f be unfolded FinSequence of TOP-REAL 2;
    let i,j be Nat;
    per cases;
     suppose A1: i <= j;
      mid(f,i,j) = (f/^(i-'1))|(j-'i+1) by A1,JORDAN3:def 1;
      hence mid(f,i,j) is unfolded;
     end;
     suppose A1: j < i;
      mid(f,j,i) = (f/^(j-'1))|(i-'j+1) by A1,JORDAN3:def 1;
      then Rev mid(f,i,j) is unfolded by JORDAN4:30;
      then Rev Rev mid(f,i,j) is unfolded by SPPOL_2:29;
      hence mid(f,i,j) is unfolded by FINSEQ_6:29;
     end;
   end;

  theorem ThRob34:
   for f be FinSequence of TOP-REAL 2 st f is special
   for p be Point of TOP-REAL 2 st p in L~f holds
    L_Cut(f,p) is special
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A0: f is special;
    let p be Point of TOP-REAL 2;
    assume A00: p in L~f;
    A1: <*p*> is special by SPPOL_2:39;
    len f <> 0 by A00,TOPREAL1:28;
    then len f > 0 by NAT_1:19;
    then A6: len f >= 0+1 by NAT_1:38;
    A7: Index(p,f)+1 >= 1 by NAT_1:29;
    A2: mid(f,Index(p,f)+1,len f) is special by A0,ThRob33;
    A4: len <*p*> = 1 by FINSEQ_1:56;
    A5: <*p*>/.1 = p by FINSEQ_4:25;
    A10: 1 <= Index(p,f) & Index(p,f) < len f by A00,JORDAN3:41;
    then A9: Index(p,f)+1 <= len f by NAT_1:38;
    then Index(p,f)+1 in dom f & len f in dom f by A6,A7,FINSEQ_3:27;
    then A8: mid(f,Index(p,f)+1,len f)/.1 = f/.(Index(p,f)+1) by SPRECT_2:12;
    A11: p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) ) by A00,JORDAN5B:32;
    A12: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f), f/.(Index(p,f)+1))
                                                    by A9,A10,TOPREAL1:def 5;
    A3: now per cases by A0,SPPOL_1:41;
     case LSeg(f,Index(p,f)) is vertical;
      hence p`1=(f/.(Index(p,f)+1))`1 by A11,A12,SPRECT_3:20;
     end;
     case LSeg(f,Index(p,f)) is horizontal;
      hence p`2=(f/.(Index(p,f)+1))`2 by A11,A12,SPRECT_3:19;
     end;
    end;
    per cases;
     suppose p <> f.(Index(p,f)+1);
      then L_Cut(f,p) = <*p*>^mid(f,Index(p,f)+1,len f) by JORDAN3:def 4;
      hence L_Cut(f,p) is special by A1,A2,A3,A4,A5,A8,GOBOARD2:13;
     end;
     suppose p = f.(Index(p,f)+1);
      hence L_Cut(f,p) is special by A2,JORDAN3:def 4;
     end;
   end;

  theorem ThRob35:
   for f be FinSequence of TOP-REAL 2 st f is special
   for p be Point of TOP-REAL 2 st p in L~f holds
    R_Cut(f,p) is special
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A0: f is special;
    let p be Point of TOP-REAL 2;
    assume A00: p in L~f;
    A1: <*p*> is special by SPPOL_2:39;
    len f <> 0 by A00,TOPREAL1:28;
    then len f > 0 by NAT_1:19;
    then A6: len f >= 0+1 by NAT_1:38;
    A10: 1 <= Index(p,f) & Index(p,f) < len f by A00,JORDAN3:41;
    A2: mid(f,1,Index(p,f)) is special by A0,ThRob33;
    A5: <*p*>/.1 = p by FINSEQ_4:25;
    A9: Index(p,f)+1 <= len f by A10,NAT_1:38;
    Index(p,f) in dom f & 1 in dom f by A6,A10,FINSEQ_3:27;
    then A8: mid(f,1,Index(p,f))/.len mid(f,1,Index(p,f)) = f/.Index(p,f)
                                                               by SPRECT_2:13;
    A11: p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) ) by A00,JORDAN5B:32;
    A12: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f), f/.(Index(p,f)+1))
                                                    by A9,A10,TOPREAL1:def 5;
    A3: now per cases by A0,SPPOL_1:41;
     case LSeg(f,Index(p,f)) is vertical;
      hence p`1=(f/.Index(p,f))`1 by A11,A12,SPRECT_3:20;
     end;
     case LSeg(f,Index(p,f)) is horizontal;
      hence p`2=(f/.Index(p,f))`2 by A11,A12,SPRECT_3:19;
     end;
    end;
    per cases;
     suppose p <> f.1;
      then R_Cut(f,p) = mid(f,1,Index(p,f))^<*p*> by JORDAN3:def 5;
      hence R_Cut(f,p) is special by A1,A2,A3,A5,A8,GOBOARD2:13;
     end;
     suppose p = f.1;
      hence R_Cut(f,p) is special by A1,JORDAN3:def 5;
     end;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2 st f is special weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q) is special
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A0: f is special weakly-one-to-one;
    let p,q be Point of TOP-REAL 2;
    assume A1: p in L~f & q in L~f;
    A28: 1 <= Index(p,f) & Index(p,f) < len f by A1,JORDAN3:41;
    then A13: Index(p,f) in dom f by FINSEQ_3:27;
    per cases;
     suppose A4: p <> q;
      now per cases;
       suppose A3: p in L~f & q in L~f & Index(p,f) < Index(q,f);
        then A2: q in L~L_Cut(f,p) by JORDAN3:64;
        L_Cut(f,p) is special by A0,A1,ThRob34;
        then R_Cut(L_Cut(f,p),q) is special by A2,ThRob35;
        hence B_Cut(f,p,q) is special by A3,JORDAN3:def 8;
       end;
       suppose A3: Index(p,f)=Index(q,f) &
         LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
        then A2: q in L~L_Cut(f,p) by A1,A4,JORDAN3:66;
        L_Cut(f,p) is special by A0,A1,ThRob34;
        then R_Cut(L_Cut(f,p),q) is special by A2,ThRob35;
        hence B_Cut(f,p,q) is special by A3,JORDAN3:def 8;
       end;
       suppose A3: not (p in L~f & q in L~f & Index(p,f) < Index(q,f) or
         Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
        then A4_: B_Cut(f,p,q) = Rev (R_Cut(L_Cut(f,q),p)) by JORDAN3:def 8;
        A5: now per cases by A1,A3,REAL_1:def 5;
         suppose Index(q,f) < Index(p,f);
          hence p in L~L_Cut(f,q) by A1,JORDAN3:64;
         end;
         suppose A27: Index(q,f) = Index(p,f);
          set Ls = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1));
          A29: Index(p,f)+1 <= len f by A28,NAT_1:38;
          Index(p,f)+1 >= 1 by NAT_1:29;
          then A14: Index(p,f)+1 in dom f by A29,FINSEQ_3:27;
          f.Index(p,f) <> f.(Index(p,f)+1) by A28,A0,Def2;
          then f.Index(p,f) <> f/.(Index(p,f)+1) by A14,FINSEQ_4:def 4;
          then A26: f/.Index(p,f) <> f/.(Index(p,f)+1) by A13,FINSEQ_4:def 4;
          then A23: Ls is_an_arc_of f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                               by TOPREAL1:15;
          A25: not LE p,q,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                    by A3,A27,A26,JORDAN5C:17;
          A24: p in Ls by A1,JORDAN5B:32;
          q in Ls by A1,A27,JORDAN5B:32;
          then LE q,p,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                 by A4,A23,A24,A25,JORDAN5C:14;
          then LE q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A26,JORDAN5C:17;
          hence p in L~L_Cut(f,q) by A1,A4,A27,JORDAN3:66;
         end;
        end;
        L_Cut(f,q) is special by A0,A1,ThRob34;
        then R_Cut(L_Cut(f,q),p) is special by A5,ThRob35;
        hence B_Cut(f,p,q) is special by A4_,SPPOL_2:42;
       end;
      end;
      hence B_Cut(f,p,q) is special;
     end;
     suppose p = q;
      then B_Cut(f,p,q) = <*p*> by A0,A1,ThRob16;
      hence B_Cut(f,p,q) is special by SPPOL_2:39;
     end;
   end;

  theorem ThRob38:
   for f be FinSequence of TOP-REAL 2 st f is unfolded
   for p be Point of TOP-REAL 2 st p in L~f holds
    L_Cut(f,p) is unfolded
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A0: f is unfolded;
    let p be Point of TOP-REAL 2;
    assume A00: p in L~f;
    len f <> 0 by A00,TOPREAL1:28;
    then len f > 0 by NAT_1:19;
    then A6: len f >= 0+1 by NAT_1:38;
    A2: mid(f,Index(p,f)+1,len f) is unfolded by A0,ThRob37;
    A10: 1 <= Index(p,f) & Index(p,f) < len f by A00,JORDAN3:41;
    then A9: 1<Index(p,f)+1 & Index(p,f)+1+0 <= len f by NAT_1:38;
    then A15: Index(p,f)+1 in dom f & len f in dom f by A6,FINSEQ_3:27;
    then A8: mid(f,Index(p,f)+1,len f)/.1 = f/.(Index(p,f)+1) by SPRECT_2:12;
    A11: p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) ) by A00,JORDAN5B:32;
    A12: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f), f/.(Index(p,f)+1))
                                                    by A9,A10,TOPREAL1:def 5;
    per cases;
     suppose A21: Index(p,f)+1+1 <= len f;
      then A23: LSeg(f,Index(p,f)+1) =
        LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) by A9,TOPREAL1:def 5;
      A14: len f -(Index(p,f)+1) >= 0 by A9,REAL_1:84;
      2+(Index(p,f)+1)-1 = Index(p,f)+1+1;
      then A17: 2+(Index(p,f)+1)-1 >= 0 by NAT_1:18;
      A22: len mid(f,Index(p,f)+1,len f) = len f-'(Index(p,f)+1)+1
                                                           by A6,A9,JORDAN3:27
           .= len f -(Index(p,f)+1)+1 by A14,BINARITH:def 3
           .= len f - Index(p,f);
      Index(p,f)+(1+1) <= len f by A21;
      then A16: 2 <= len f - Index(p,f) by REAL_1:84;
      then 2 in dom mid(f,Index(p,f)+1,len f) by A22,FINSEQ_3:27;
      then (mid(f,Index(p,f)+1,len f))/.2 = f/.(2+(Index(p,f)+1)-'1)
                                                          by A9,A15,SPRECT_2:7
         .= f/.(Index(p,f)+(1+1)) by A17,BINARITH:def 3;
      then A20: LSeg(mid(f,Index(p,f)+1,len f),1) =
                LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1)))
                                                by A8,A16,A22,TOPREAL1:def 5;
      A25: LSeg(f/.Index(p,f), f/.(Index(p,f)+1)) /\
           LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) = {f/.(Index(p,f)+1)}
                                        by A0,A10,A12,A23,A21,TOPREAL1:def 8;
      f/.(Index(p,f)+1) in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by TOPREAL1:6;
      then LSeg(p,f/.(Index(p,f)+1)) c= LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                                                           by A11,TOPREAL1:12;
      then A26: LSeg(p,f/.(Index(p,f)+1)) /\
         LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) c= {f/.(Index(p,f)+1)}
                                                           by A25,XBOOLE_1:26;
      f/.(Index(p,f)+1) in LSeg(p,f/.(Index(p,f)+1)) &
       f/.(Index(p,f)+1) in LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1)))
                                                                by TOPREAL1:6;
      then f/.(Index(p,f)+1) in LSeg(p,f/.(Index(p,f)+1)) /\
           LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) by XBOOLE_0:def 3;
      then {f/.(Index(p,f)+1)} c= LSeg(p,f/.(Index(p,f)+1)) /\
           LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) by ZFMISC_1:37;
      then A13: LSeg(p,f/.(Index(p,f)+1)) /\
           LSeg(f/.(Index(p,f)+1),f/.(Index(p,f)+(1+1))) = {f/.(Index(p,f)+1)}
                                                      by A26,XBOOLE_0:def 10;
      now per cases;
       suppose p <> f.(Index(p,f)+1);
        then L_Cut(f,p) = <*p*>^mid(f,Index(p,f)+1,len f) by JORDAN3:def 4;
        hence L_Cut(f,p) is unfolded by A2,A8,A13,A20,SPPOL_2:30;
       end;
       suppose p = f.(Index(p,f)+1);
        hence L_Cut(f,p) is unfolded by A2,JORDAN3:def 4;
       end;
      end;
      hence L_Cut(f,p) is unfolded;
     end;
     suppose A21: Index(p,f)+1+1 > len f;
      A22_: Index(p,f)+1 < len f or Index(p,f)+1 = len f by A9,REAL_1:def 5;
      now per cases;
       suppose p <> f.(Index(p,f)+1);
        then L_Cut(f,p) = <*p*>^mid(f,len f,len f) 
               by A21,A22_,JORDAN3:def 4,NAT_1:38
           .= <*p*>^<*f/.len f*> by A6,JORDAN4:27
           .= <*p,f/.len f*> by FINSEQ_1:def 9;
        then len L_Cut(f,p) = 2 by FINSEQ_1:61;
        hence L_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
       suppose p = f.(Index(p,f)+1);
        then L_Cut(f,p) = mid(f,len f,len f) 
           by A21,A22_,JORDAN3:def 4,NAT_1:38
           .= <*f/.len f*> by A6,JORDAN4:27;
        then len L_Cut(f,p) = 1 by FINSEQ_1:56;
        hence L_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
      end;
      hence L_Cut(f,p) is unfolded;
     end;
   end;

  theorem ThRob39:
   for f be FinSequence of TOP-REAL 2 st f is unfolded
   for p be Point of TOP-REAL 2 st p in L~f holds
    R_Cut(f,p) is unfolded
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A0: f is unfolded;
    let p be Point of TOP-REAL 2;
    assume A00: p in L~f;
    len f <> 0 by A00,TOPREAL1:28;
    then len f > 0 by NAT_1:19;
    then A6: len f >= 0+1 by NAT_1:38;
    A10: 1 <= Index(p,f) & Index(p,f) < len f by A00,JORDAN3:41;
    then A9: 1<Index(p,f)+1 & Index(p,f)+1+0 <= len f by NAT_1:38;
    B4: Index(p,f)-'1+1 = Index(p,f) by A10,AMI_5:4;
    Index(p,f) in Seg len f & 1 in Seg len f by A6,A10,FINSEQ_1:3;
    then A15: Index(p,f) in dom f & 1 in dom f by FINSEQ_1:def 3;
    then A8: mid(f,1,Index(p,f))/.len mid(f,1,Index(p,f)) = f/.Index(p,f)
                                                               by SPRECT_2:13;
    A2: mid(f,1,Index(p,f)) is unfolded by A0,ThRob37;
    A11: p in LSeg ( f/.Index(p,f), f/.(Index(p,f)+1) ) by A00,JORDAN5B:32;
    A12: LSeg(f,Index(p,f)) = LSeg(f/.Index(p,f), f/.(Index(p,f)+1))
                                                    by A9,A10,TOPREAL1:def 5;
    per cases by A10,REAL_1:def 5;
     suppose Index(p,f) > 0+1;
      then Index(p,f)-1 > 0 by REAL_1:86;
      then Index(p,f)-'1 > 0 by BINARITH:def 3;
      then A17: Index(p,f)-'1 >= 0+1 by NAT_1:38;
      A23: LSeg(f,Index(p,f)-'1) = LSeg(f/.(Index(p,f)-'1),f/.(Index(p,f)))
         by A10,A17,B4,TOPREAL1:def 5;
      A22: len mid(f,1,Index(p,f)) = Index(p,f)-'1+1 by A6,A10,JORDAN3:27
           .= Index(p,f) by A10,AMI_5:4;
      len mid(f,1,Index(p,f))-'1 <= len mid(f,1,Index(p,f)) by GOBOARD9:2;
      then len mid(f,1,Index(p,f))-'1 in Seg len mid(f,1,Index(p,f))
                                                        by A17,A22,FINSEQ_1:3;
      then len mid(f,1,Index(p,f))-'1 in dom mid(f,1,Index(p,f))
                                                           by FINSEQ_1:def 3;
      then A19: mid(f,1,Index(p,f))/.(len mid(f,1,Index(p,f))-'1) =
            f/.(len mid(f,1,Index(p,f))-'1+1-'1) by A10,A15,SPRECT_2:7
         .= f/.(len mid(f,1,Index(p,f))-'1) by BINARITH:39
         .= f/.(Index(p,f)-'1+1-'1) by A10,JORDAN4:20
         .= f/.(Index(p,f)-'1) by A10,AMI_5:4;
      len mid(f,1,Index(p,f))-'1+1=len mid(f,1,Index(p,f)) by A10,A22,AMI_5:4;
      then A20: LSeg(mid(f,1,Index(p,f)),len mid(f,1,Index(p,f))-'1) =
       LSeg(f/.(Index(p,f)-'1),f/.Index(p,f))
                                            by A8,A17,A19,A22,TOPREAL1:def 5;
      Index(p,f)-'1+(1+1) <= len f by A9,B4,XCMPLX_1:1;
      then A25: LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
           LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) = {f/.Index(p,f)}
                                         by A0,A12,A17,A23,B4,TOPREAL1:def 8;
      f/.Index(p,f) in LSeg(f/.Index(p,f),f/.(Index(p,f)+1)) by TOPREAL1:6;
      then LSeg(f/.Index(p,f),p) c= LSeg(f/.Index(p,f),f/.(Index(p,f)+1))
                                                           by A11,TOPREAL1:12;
      then A26: LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
          LSeg(f/.Index(p,f),p) c= {f/.Index(p,f)} by A25,XBOOLE_1:26;
      f/.Index(p,f) in LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) &
       f/.Index(p,f) in LSeg(f/.Index(p,f),p) by TOPREAL1:6;
      then f/.Index(p,f) in LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
           LSeg(f/.Index(p,f),p) by XBOOLE_0:def 3;
      then B3: {f/.Index(p,f)} c= LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
           LSeg(f/.Index(p,f),p) by ZFMISC_1:37;
      B1: len mid(f,1,Index(p,f))-'1+1 = len mid(f,1,Index(p,f))
                                                           by A10,A22,AMI_5:4;
      B2: LSeg(f/.(Index(p,f)-'1),f/.Index(p,f)) /\
          LSeg(f/.Index(p,f),p) = {f/.Index(p,f)} by A26,B3,XBOOLE_0:def 10;
      now per cases;
       suppose p <> f.1;
        then R_Cut(f,p) = mid(f,1,Index(p,f))^<*p*> by JORDAN3:def 5;
        hence R_Cut(f,p) is unfolded by A2,A8,A20,B1,B2,SPPOL_2:31;
       end;
       suppose p = f.1;
        then R_Cut(f,p) = <*p*> by JORDAN3:def 5;
        then len R_Cut(f,p) = 1 by FINSEQ_1:56;
        hence R_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
      end;
      hence R_Cut(f,p) is unfolded;
     end;
     suppose A21: Index(p,f) = 0+1;
      now per cases;
       suppose p <> f.1;
        then R_Cut(f,p) = mid(f,1,1)^<*p*> by A21,JORDAN3:def 5
           .= <*f/.1 *>^<*p*> by A6,JORDAN4:27
           .= <*f/.1,p*> by FINSEQ_1:def 9;
        then len R_Cut(f,p) = 2 by FINSEQ_1:61;
        hence R_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
       suppose p = f.1;
        then R_Cut(f,p) = <*p*> by JORDAN3:def 5;
        then len R_Cut(f,p) = 1 by FINSEQ_1:56;
        hence R_Cut(f,p) is unfolded by SPPOL_2:27;
       end;
      end;
      hence R_Cut(f,p) is unfolded;
     end;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2 st f is unfolded weakly-one-to-one
   for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f holds
    B_Cut(f,p,q) is unfolded
   proof
    let f be FinSequence of TOP-REAL 2;
    assume A0: f is unfolded weakly-one-to-one;
    let p,q be Point of TOP-REAL 2;
    assume A1: p in L~f & q in L~f;
    A28: 1 <= Index(p,f) & Index(p,f) < len f by A1,JORDAN3:41;
    then Index(p,f) in Seg len f by FINSEQ_1:3;
    then A13: Index(p,f) in dom f by FINSEQ_1:def 3;
    per cases;
     suppose A4: p <> q;
      now per cases;
       suppose A3: p in L~f & q in L~f & Index(p,f) < Index(q,f);
        then A2: q in L~L_Cut(f,p) by JORDAN3:64;
        L_Cut(f,p) is unfolded by A0,A1,ThRob38;
        then R_Cut(L_Cut(f,p),q) is unfolded by A2,ThRob39;
        hence B_Cut(f,p,q) is unfolded by A3,JORDAN3:def 8;
       end;
       suppose A3: Index(p,f)=Index(q,f) &
         LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
        then A2: q in L~L_Cut(f,p) by A1,A4,JORDAN3:66;
        L_Cut(f,p) is unfolded by A0,A1,ThRob38;
        then R_Cut(L_Cut(f,p),q) is unfolded by A2,ThRob39;
        hence B_Cut(f,p,q) is unfolded by A3,JORDAN3:def 8;
       end;
       suppose A3: not (p in L~f & q in L~f & Index(p,f) < Index(q,f) or
         Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
        then A4_: B_Cut(f,p,q) = Rev (R_Cut(L_Cut(f,q),p)) by JORDAN3:def 8;
        A5: now per cases by A1,A3,REAL_1:def 5;
         suppose Index(q,f) < Index(p,f);
          hence p in L~L_Cut(f,q) by A1,JORDAN3:64;
         end;
         suppose A27: Index(q,f) = Index(p,f);
          set Ls = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1));
          A29: Index(p,f)+1 <= len f by A28,NAT_1:38;
          Index(p,f)+1 >= 1 by NAT_1:29;
          then Index(p,f)+1 in Seg len f by A29,FINSEQ_1:3;
          then A14: Index(p,f)+1 in dom f by FINSEQ_1:def 3;
          f.Index(p,f) <> f.(Index(p,f)+1) by A28,A0,Def2;
          then f.Index(p,f) <> f/.(Index(p,f)+1) by A14,FINSEQ_4:def 4;
          then A26: f/.Index(p,f) <> f/.(Index(p,f)+1) by A13,FINSEQ_4:def 4;
          then A23: Ls is_an_arc_of f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                               by TOPREAL1:15;
          A25: not LE p,q,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                    by A3,A27,A26,JORDAN5C:17;
          A24: p in Ls by A1,JORDAN5B:32;
          q in Ls by A1,A27,JORDAN5B:32;
          then LE q,p,Ls,f/.(Index(p,f)),f/.(Index(p,f)+1)
                                                 by A4,A23,A24,A25,JORDAN5C:14;
          then LE q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A26,JORDAN5C:17;
          hence p in L~L_Cut(f,q) by A1,A4,A27,JORDAN3:66;
         end;
        end;
        L_Cut(f,q) is unfolded by A0,A1,ThRob38;
        then R_Cut(L_Cut(f,q),p) is unfolded by A5,ThRob39;
        hence B_Cut(f,p,q) is unfolded by A4_,SPPOL_2:29;
       end;
      end;
      hence B_Cut(f,p,q) is unfolded;
     end;
     suppose p = q;
      then B_Cut(f,p,q) = <*p*> by A0,A1,ThRob16;
      then len B_Cut(f,p,q) = 1 by FINSEQ_1:56;
      hence B_Cut(f,p,q) is unfolded by SPPOL_2:27;
     end;
   end;

  theorem Th52:
   for f,g be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p<>f.1 & g=mid(f,1,Index(p,f))^<*p*> holds
     g is_S-Seq_joining f/.1,p
   proof
    let f,g be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: f is almost-one-to-one special unfolded s.n.c. and
     A2: p in L~f and
     A3: p<>f.1 and
     A4: g=mid(f,1,Index(p,f))^<*p*>;
    consider i be Nat such that A5:1<=i & i+1<=len f & p in LSeg(f,i)
     by A2,SPPOL_2:13;
    A6: 1<=Index(p,f) by A2,JORDAN3:41;
    A7: Index(p,f)<len f by A2,JORDAN3:41;
    1<=1+i by NAT_1:37;
    then A8:1<=len f by A5,AXIOMS:22;
    then A9: len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A6,A7,JORDAN3:27;
    then A10: len mid(f,1,Index(p,f))=Index(p,f) by A6,AMI_5:4;
    1<=len <*p*> by FINSEQ_1:56;
    then A11: 1 in dom <*p*> by FINSEQ_3:27;
    g.1 = mid(f,1,Index(p,f)).1 by A4,A6,A10,JORDAN3:17;
    then g.1=f.1 by A6,A7,A8,JORDAN3:27;
    then A12: g.1=f/.1 by A8,FINSEQ_4:24;
    A13: len g = len mid(f,1,Index(p,f))+ len<*p*> by A4,FINSEQ_1:35
       .= len mid(f,1,Index(p,f))+ 1 by FINSEQ_1:56;
    then A14:  g.len g = p by A4,JORDAN3:16;
    A15: 1+1<=len g by A6,A10,A13,AXIOMS:24;
    A0: f is almost-one-to-one FinSequence by A1;
    A19: for x1,x2 being set st x1 in dom g & x2 in dom g & g.x1=g.x2 holds
     x1=x2
    proof
     let x1,x2 be set;
     assume A20: x1 in dom g & x2 in dom g & g.x1=g.x2;
     then reconsider n1=x1,n2=x2 as Nat;
     A21: 1<=n1 & n1<=len g & 1<=n2 & n2<=len g by A20,FINSEQ_3:27;
     now
      A22:g.len g = <*p*>.1 by A4,A11,A13,FINSEQ_1:def 7
         .= p by FINSEQ_1:def 8;
      now per cases;
       case A23:n1=len g;
        now assume A24: n2<>len g; then n2<len g by A21,REAL_1:def 5;
         then A25:n2<=len mid(f,1,Index(p,f)) by A13,NAT_1:38;
         then g.n2=mid(f,1,Index(p,f)).n2 by A4,A21,SCMFSA_7:9;
         then g.n2=f.(n2+1-'1) by A6,A7,A8,A21,A25,JORDAN3:27;
         then A26:p=f.n2 by A20,A22,A23,BINARITH:39;
         A27: n2 < len f by A7,A10,A25,AXIOMS:22;
         1 < n2 by A3,A21,A26,AXIOMS:21;
         then Index(p,f)+1 = n2 by A0,A1,A26,A27,ThRob41;
         hence contradiction by A6,A9,A13,A24,AMI_5:4;
        end;
        hence x1=x2 by A23;
       end;
       case A28:n2=len g;
        now assume A29: n1<>len g; then n1<len g by A21,REAL_1:def 5;
         then A30:n1<=len mid(f,1,Index(p,f)) by A13,NAT_1:38;
         then g.n1=mid(f,1,Index(p,f)).n1 by A4,A21,SCMFSA_7:9;
         then g.n1=f.(n1+1-'1) by A6,A7,A8,A21,A30,JORDAN3:27;
         then A31:p=f.n1 by A20,A22,A28,BINARITH:39;
         A32: n1 < len f by A7,A10,A30,AXIOMS:22;
         1 < n1 by A3,A21,A31,AXIOMS:21;
         then Index(p,f)+1 = n1 by A0,A1,A31,A32,ThRob41;
         hence contradiction by A6,A9,A13,A29,AMI_5:4;
        end;
        hence x1=x2 by A28;
       end;
       case that
         A33: n1 <> len g and
         A34: n2 <> len g;
        n1 < len g by A21,A33,REAL_1:def 5;
        then A35:n1<=len mid(f,1,Index(p,f)) by A13,NAT_1:38;
        then A36:n1<len f by A7,A10,AXIOMS:22;
        A37: g.n1=mid(f,1,Index(p,f)).n1 by A4,A21,A35,SCMFSA_7:9
           .=f.n1 by A7,A10,A21,A35,JORDAN3:32;
        n2 < len g by A21,A34,AXIOMS:21;
        then A38:n2<=len mid(f,1,Index(p,f)) by A13,NAT_1:38;
        then A39:n2<len f by A7,A10,AXIOMS:22;
        A40: g.n2 = mid(f,1,Index(p,f)).n2 by A4,A21,A38,SCMFSA_7:9
           .= f.n2 by A7,A10,A21,A38,JORDAN3:32;
        n1 in Seg len f & n2 in Seg len f by A21,A36,A39,FINSEQ_1:3;
        then n1 in dom f & n2 in dom f by FINSEQ_1:def 3;
        hence x1=x2 by A1,A7,A10,A20,A35,A37,A38,A40,Def1;
       end;
      end;
      hence x1=x2;
     end;
     hence x1=x2;
    end;
    A41: for j be Nat st 1 <= j & j + 2 <= len g holds
     LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)}
    proof
     let j be Nat;
     assume A42: 1 <= j & j + 2 <= len g;
     then j+1<=len g by JORDAN3:10;
     then A43: LSeg(g,j) c= LSeg(f,j) by A2,A4,A42,JORDAN3:51;
     A44: j+1+1<=len g by A42;
     A45: 1<=j+1 by A42,JORDAN3:11;
     then LSeg(g,j+1) c= LSeg(f,j+1) by A2,A4,A44,JORDAN3:51;
     then A46:LSeg(g,j)/\ LSeg(g,j+1) c= LSeg(f,j)/\ LSeg(f,j+1)
      by A43,XBOOLE_1:27;
     A47: j+1<=len g by A42,JORDAN3:10;
     then LSeg(g,j)=LSeg(g/.j,g/.(j+1)) by A42,TOPREAL1:def 5;
     then A48:g/.(j+1) in LSeg(g,j) by TOPREAL1:6;
     A49: g/.(j+1)=g.(j+1) by A45,A47,FINSEQ_4:24;
     A50: Index(p,f)<=len f by A2,JORDAN3:41;
     LSeg(g,j+1)=LSeg(g/.(j+1),g/.(j+1+1)) by A42,A45,TOPREAL1:def 5;
     then g/.(j+1) in LSeg(g,j+1) by TOPREAL1:6;
     then g/.(j+1) in LSeg(g,j) /\ LSeg(g,j+1) by A48,XBOOLE_0:def 3;
     then A51:{g/.(j+1)} c= LSeg(g,j) /\ LSeg(g,j+1) by ZFMISC_1:37;
     now
      A52: len g=len mid(f,1,Index(p,f))+1 by A4,FINSEQ_2:19;
      Index(p,f)<=len f by A2,JORDAN3:41;
      then A53: len g<=len f +1 by A10,A52,AXIOMS:24;
      now per cases by A53,REAL_1:def 5;
       case len g = len f +1;
        then len f = Index(p,f) by A10,A52,XCMPLX_1:2;
        hence contradiction by A2,JORDAN3:41;
       end;
       case len g<len f+1;
        then len g<=len f by NAT_1:38;
        then A54: j+2<=len f by A42,AXIOMS:22;
        A55: j+1<=Index(p,f) by A10,A44,A52,REAL_1:53;
        then A56: j+1<=len f by A50,AXIOMS:22;
        A57: LSeg(g,j)/\ LSeg(g,j+1) c= {f/.(j+1)}
         by A1,A42,A46,A54,TOPREAL1:def 8;
        A58: f.(j+1)=f/.(j+1) by A45,A56,FINSEQ_4:24;
        g.(j+1) = mid(f,1,Index(p,f)).(j+1) by A4,A10,A45,A55,SCMFSA_7:9
           .= f.(j+1) by A7,A45,A55,JORDAN3:32;
        hence LSeg(g,j)/\ LSeg(g,j+1) = {g/.(j+1)}
         by A49,A51,A57,A58,XBOOLE_0:def 10;
       end;
      end;
      hence LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)};
     end;
     hence LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)};
    end;
    A60: for j1,j2 be Nat st j1+1 < j2 holds LSeg(g,j1) misses LSeg(g,j2)
    proof
     let j1,j2 be Nat;
     assume A61:j1+1 < j2;
     j1>=0 by NAT_1:18;
     then A62: j1=0 or j1>=0+1 by NAT_1:38;
     now per cases by A62,REAL_1:def 5;
      case j1=0;
       then LSeg(g,j1)={} by TOPREAL1:def 5;
       then LSeg(g,j1) /\ LSeg(g,j2) = {};
       hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
      end;
      case that
        A63: j1=1 or j1>1 and
        A64: j2+1<=len g;
       j2<len g by A64,NAT_1:38;
       then j1+1<len g by A61,AXIOMS:22;
       then A65: LSeg(g,j1) c= LSeg(f,j1) by A2,A4,A63,JORDAN3:51;
       1+1<=j1+1 by A63,AXIOMS:24;
       then 2<=j2 by A61,AXIOMS:22;
       then 1<=j2 by AXIOMS:22;
       then LSeg(g,j2) c= LSeg(f,j2) by A2,A4,A64,JORDAN3:51;
       then A66: LSeg(g,j1)/\ LSeg(g,j2) c= LSeg(f,j1)/\ LSeg(f,j2)
        by A65,XBOOLE_1:27;
       LSeg(f,j1) misses LSeg(f,j2) by A1,A61,TOPREAL1:def 9;
       then LSeg(f,j1) /\ LSeg(f,j2) = {} by XBOOLE_0:def 7;
       then LSeg(g,j1) /\ LSeg(g,j2) = {} by A66,XBOOLE_1:3;
       hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
      end;
      case j2+1>len g;
       then LSeg(g,j2)={} by TOPREAL1:def 5;
       then LSeg(g,j1)/\ LSeg(g,j2)={};
       hence thesis by XBOOLE_0:def 7;
      end;
     end;
     hence LSeg(g,j1) misses LSeg(g,j2);
    end;
    for j be Nat st 1 <= j & j+1 <= len g holds
     (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
    proof
     let j be Nat;
     assume A68: 1 <= j & j+1 <= len g;
     A69: now
      j+1 <= Index(p,f)+1 by A4,A10,A68,FINSEQ_2:19;
      then A70: j<=Index(p,f) by REAL_1:53;
      Index(p,f)<len f by A2,JORDAN3:41;
      then j<len f by A70,AXIOMS:22;
      hence j+1<=len f by NAT_1:38;
     end;
     A71: LSeg(g,j) c= LSeg(f,j) by A2,A4,A68,JORDAN3:51;
     A72: LSeg(g,j) = LSeg(g/.j,g/.(j+1)) by A68,TOPREAL1:def 5;
     A73: LSeg(f,j) = LSeg(f/.j,f/.(j+1)) by A68,A69,TOPREAL1:def 5;
     (f/.j)`1 = (f/.(j+1))`1 or (f/.j)`2 = (f/.(j+1))`2
      by A1,A68,A69,TOPREAL1:def 7;
     hence (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
      by A71,A72,A73,JORDAN3:36;
    end;
    then g is one-to-one & len g >= 2 & g is unfolded s.n.c. special
     by A15,A19,A41,A60,FUNCT_1:def 8,TOPREAL1:def 7,def 8,def 9;
    then g is_S-Seq by TOPREAL1:def 10;
    hence g is_S-Seq_joining f/.1,p by A12,A14,JORDAN3:def 3;
   end;

  theorem Th53:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st f is poorly-one-to-one unfolded s.n.c. &
    p in L~f & p = f.(Index(p,f)+1) & p <> f.len f holds
     Index(p,Rev f) + Index(p,f) + 1 = len f
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2 such that
     A1: f is poorly-one-to-one unfolded s.n.c. and
     A2: p in L~f and
     A3: p = f.(Index(p,f)+1) and
     A4: p <> f.len f;
    A5a: Rev f is poorly-one-to-one by A1,ThRob12;
    A5b: Rev f is unfolded by A1,SPPOL_2:29;
    A5c: Rev f is s.n.c. by A1,SPPOL_2:36;
    A6: len f = len Rev f by FINSEQ_5:def 3;
    len f <= len f + Index(p,f) by NAT_1:29;
    then A7: len f - Index(p,f) <= len Rev f by A6,REAL_1:86;
    Index(p,f) < len f by A2,JORDAN3:41;
    then A8: Index(p,f)+1 <= len f by NAT_1:38;
    1 <= Index(p,f)+1 by NAT_1:29;
    then Index(p,f)+1 in dom f by A8,FINSEQ_3:27;
    then A9: Index(p,f)+1 in dom Rev f by FINSEQ_5:60;
    Index(p,f)+1 < len f by A3,A4,A8,AXIOMS:21;
    then A10: 1 < len f - Index(p,f) by REAL_1:86;
    Index(p,f) <= len f by A2,JORDAN3:41;
    then A11: len f - Index(p,f) = len f -' Index(p,f) by SCMFSA_7:3;
    p = (Rev Rev f).(Index(p,f)+1) by A3,FINSEQ_6:29
       .= (Rev f).(len Rev f - (Index(p,f)+1) + 1) by A9,FINSEQ_5:61
       .= (Rev f).(len Rev f - Index(p,f))
       .= (Rev f).(len f - Index(p,f)) by FINSEQ_5:def 3;
    then A12:  Index(p,Rev f) + 1 = len f -' Index(p,f)
       by A5a,A5b,A5c,A7,A10,A11,ThRob41;
    thus Index(p,Rev f) + Index(p,f) + 1 = Index(p,Rev f) + 1 + Index(p,f)
       .= len f by A11,A12,XCMPLX_1:27;
   end;

  theorem
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st f is weakly-one-to-one & len f >= 2 holds
    L_Cut (f,f/.1) = f
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: f is weakly-one-to-one and
     A2: len f >= 2;
    A3: 1 <= len f by A2,AXIOMS:22;
    then A4: 1 in dom f by FINSEQ_3:27;
    2 = 1+1;
    then A6: 1 < len f by A2,NAT_1:38;
    A8: f/.1 = f.1 by A4,FINSEQ_4:def 4;
    A9: Index(f/.1,f) = 1 by A2,JORDAN3:44;
    f.1 <> f.(1+1) by A1,A6,Def2;
    then f/.1 <> f.(1+1) by A4,FINSEQ_4:def 4;
    hence L_Cut (f,f/.1) = <*f/.1 *>^mid(f,Index(f/.1,f)+1,len f)
          by A9,JORDAN3:def 4
       .= mid(f,1,len f) by A4,A6,A8,A9,JORDAN3:56
       .= f by A3,JORDAN3:29;
   end;

  theorem Th57:
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st f is poorly-one-to-one unfolded s.n.c. &
    p in L~f & p <> f.len f holds L_Cut(Rev f,p) = Rev R_Cut(f,p)
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2 such that
     A1: f is poorly-one-to-one unfolded s.n.c. and
     A2: p in L~f and
     A3: p <> f.len f;
    A3a: Rev f is poorly-one-to-one by A1,ThRob12;
    A3b: Rev f is unfolded by A1,SPPOL_2:29;
    A3c: Rev f is s.n.c. by A1,SPPOL_2:36;
    A4: p in L~Rev f by A2,SPPOL_2:22;
    A5: Rev Rev f = f by FINSEQ_6:29;
    A6: len f = len Rev f by FINSEQ_5:def 3;
    A7: L~f = L~Rev f by SPPOL_2:22;
    A8: dom Rev f = dom f by FINSEQ_5:60;
    A9: 1 <= Index(p, Rev f)+1 by NAT_1:29;
    Index(p, Rev f) < len Rev f by A2,A7,JORDAN3:41;
    then A10: Index(p, Rev f) + 1 <= len f by A6,NAT_1:38;
    then A11: Index(p, Rev f)+1 in dom f by A9,FINSEQ_3:27;
    A14: 1 <= Index(p,f) by A2,JORDAN3:41;
    A15: Index(p,f) < len f by A2,JORDAN3:41;
    then A13: 1 < len f by A14,AXIOMS:22;
    per cases by A3;
     suppose A25: p = f.1;
      then A26: p = (Rev f).len f by FINSEQ_5:65;
      A27:len (Rev f/^(len Rev f-'1))=len Rev f-'(len Rev f-'1) by JORDAN3:19;
      A28: len Rev f-'1+1=len Rev f by A6,A13,AMI_5:4;
      then A29:(Rev f/^(len Rev f-'1)).1=Rev f.len Rev f by JORDAN3:23;
      1<=len Rev f-(len Rev f-'1) by A28,XCMPLX_1:26;
      then A30: 1<=len (Rev f/^(len Rev f-'1)) by A27,JORDAN3:1;
      then A31:Rev f/^(len Rev f-'1) is non empty by FINSEQ_1:25;
      len Rev f-'len Rev f+1=len Rev f-len Rev f+1 by SCMFSA_7:3
         .=1;
      then A32: mid(Rev f,len Rev f,len Rev f)=(Rev f/^(len Rev f-'1))|1
            by JORDAN3:def 1
         .=<*(Rev f/^(len Rev f-'1))/.1 *> by A31,FINSEQ_5:23
         .=<*Rev f.len Rev f*> by A29,A30,FINSEQ_4:24;
      Index(p,Rev f) + 1 = len f by A3a,A3b,A3c,A6,A13,A26,ThRob41;
      hence L_Cut(Rev f,p) = <*p*> by A6,A26,A32,JORDAN3:def 4
         .= Rev <*p*> by FINSEQ_5:63
         .= Rev R_Cut(f,p) by A25,JORDAN3:def 5;
     end;
     suppose that
       A33: p <> f.1 and
       A34: p <> f.len f and
       A35: p = f.(Index(p,f)+1);
       A36: p <> (Rev f).len f by A33,FINSEQ_5:65;
      len f = Index(p,Rev f) + Index(p,f) + 1 by A1,A2,A34,A35,Th53
         .= Index(p,Rev f) + (Index(p,f) + 1);
      then A37: p = f.(len f - (Index(p, Rev f)+1) + 1) by A35
         .= (Rev f).(Index(p, Rev f)+1) by A11,FINSEQ_5:61;
      A38: len f = Index(p,Rev f) + Index(p,f) + 1 by A1,A2,A34,A35,Th53
         .= Index(p,f) + (Index(p,Rev f) + 1);
      A39: len f -' Index(p,f) = len f - Index(p,f) by A15,SCMFSA_7:3
         .= Index(p,Rev f)+1 by A38;
      A40: Index(p, Rev f)+1 < len f by A10,A36,A37,AXIOMS:21;
      thus L_Cut(Rev f,p)
          = mid(Rev f,Index(p,Rev f)+1,len f) by A6,A37,JORDAN3:def 4
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f)
            by A8,A11,A37,A39,A40,JORDAN3:56
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f-'1+1) by A13,AMI_5:4
         .= <*p*>^Rev mid(f,1,Index(p,f)) by A13,A14,A15,JORDAN3:22
         .= Rev (mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
         .= Rev R_Cut(f,p) by A33,JORDAN3:def 5;
     end;
     suppose that
       A41: p <> f.1 and
       A42: p <> f.(Index(p,f)+1);
       A43: p <> (Rev f).len f by A41,FINSEQ_5:65;
      A44: now assume A45: p = (Rev f).(Index(p, Rev f)+1);
       then A46: len Rev f = Index(p,Rev Rev f) + Index(p,Rev f) + 1
             by A3a,A3b,A3c,A4,A6,A43,Th53
          .= Index(p,f) + 1 + Index(p,Rev f) by A5;
       p = f.(len f - (Index(p, Rev f)+1) + 1) by A11,A45,FINSEQ_5:61
        .= f.(Index(p,f)+1) by A6,A46;
       hence contradiction by A42;
      end;
      A47: len f = Index(p,Rev f) + Index(p,f) by A1,A2,A42,JORDAN3:54;
      A48: Index(p, f) < len f by A2,JORDAN3:41;
      Index(p,Rev f) = len f - Index(p,f) by A47
         .= len f -' Index(p,f) by A48,SCMFSA_7:3;
      hence L_Cut(Rev f,p)
          = <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f)
             by A6,A44,JORDAN3:def 4
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f-'1+1) by A13,AMI_5:4
         .= <*p*>^Rev mid(f,1,Index(p,f)) by A13,A14,A15,JORDAN3:22
         .= Rev (mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
         .= Rev R_Cut(f,p) by A41,JORDAN3:def 5;
     end;
   end;

  theorem Th67:
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p<>f.1 holds
     R_Cut(f,p) is_S-Seq_joining f/.1,p
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: f is almost-one-to-one special unfolded s.n.c. and
     A2: p in L~f and
     A3: p<>f.1;
    R_Cut(f,p)=mid(f,1,Index(p,f))^<*p*> by A3,JORDAN3:def 5;
    hence thesis by A1,A2,A3,Th52;
   end;

  theorem Th68:
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p<>f.len f & p <> f.1 holds
     L_Cut(f,p) is_S-Seq_joining p,f/.len f
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume that
     A1: f is almost-one-to-one special unfolded s.n.c. and
     A2: p in L~f & p<>f.len f and
     A0: p <> f.1;
    p <> Rev f.len f by A0,FINSEQ_5:65;
    then A0: p <> Rev f.len Rev f by FINSEQ_5:def 3;
    A0a: Rev f is almost-one-to-one FinSequence by A1,ThRob10;
    A0b: Rev f is special by A1,SPPOL_2:42;
    A0c: Rev f is unfolded by A1,SPPOL_2:29;
    A0d: Rev f is s.n.c. by A1,SPPOL_2:36;
    A3: p in L~Rev f by A2,SPPOL_2:22;
    A4: L_Cut(f,p) = L_Cut(Rev Rev f,p) by FINSEQ_6:29
       .= Rev R_Cut(Rev f,p) by A0,A0a,A0c,A0d,A3,Th57;
    p <> (Rev f).1 by A2,FINSEQ_5:65;
    then R_Cut(Rev f,p) is_S-Seq_joining (Rev f)/.1,p
       by A0a,A0b,A0c,A0d,A3,Th67;
    then L_Cut(f,p) is_S-Seq_joining p,(Rev f)/.1 by A4,JORDAN3:48;
    hence thesis by FINSEQ_5:68;
   end;

  theorem
   for f be FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p<>f.1 holds
     R_Cut(f,p) is_S-Seq
   proof
    let f be FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume f is almost-one-to-one special unfolded s.n.c. &
     p in L~f & p<>f.1;
    then R_Cut(f,p) is_S-Seq_joining f/.1,p by Th67;
    hence R_Cut(f,p) is_S-Seq by JORDAN3:def 3;
   end;

  theorem Th69:
   for f be non empty FinSequence of TOP-REAL 2
   for p be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & p <> f.len f & p <> f.1 holds
     L_Cut(f,p) is_S-Seq
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p be Point of TOP-REAL 2;
    assume f is almost-one-to-one special unfolded s.n.c. &
     p in L~f & p <> f.len f & p <> f.1;
    then L_Cut(f,p) is_S-Seq_joining p,f/.len f by Th68;
    hence L_Cut(f,p) is_S-Seq by JORDAN3:def 3;
   end;

  Lm2:
   for f be non empty FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. &
    p in L~f & q in L~f & p<>q & p <> f.1 & (Index(p,f)<Index(q,f) or
    Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1)) holds
     B_Cut(f,p,q) is_S-Seq_joining p,q
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    assume A1: f is almost-one-to-one special unfolded s.n.c. &
     p in L~f & q in L~f & p <> q;
    assume B1: p <> f.1;
    assume A2: Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
     & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
    then A3:B_Cut(f,p,q)=R_Cut(L_Cut(f,p),q) by A1,JORDAN3:def 8;
    A4:1<=Index(q,f) & Index(q,f)<len f by A1,JORDAN3:41;
    Index(p,f) < len f by A1,JORDAN3:41;
    then A5: Index(p,f)+1 <= len f by NAT_1:38;
    A6: 1<=Index(p,f)+1 by NAT_1:29;
    A7: 1<len f by A4,AXIOMS:22;
    A0: f is almost-one-to-one FinSequence by A1;
    A8: now per cases by A2;
     case Index(p,f)<Index(q,f);
      then A9:Index(p,f)+1<=Index(q,f) by NAT_1:38;
      assume p=f.len f;
      then len f <= Index(q,f) by A0,A1,A7,A9,ThRob41;
      hence contradiction by A1,JORDAN3:41;
     end;
     case A10: Index(p,f)=Index(q,f) &
       LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      A11: now
       assume A12: p = f.(Index(p,f)+1);
       then A13: p = f/.(Index(p,f)+1) by A5,A6,FINSEQ_4:24;
       q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A10,JORDAN3:def 6;
       then consider r being Real such that
        A14: 0 <= r & r <= 1 and
        A15: q = (1-r)*f/.(Index(p,f))+r*f/.(Index(p,f)+1) by SPPOL_1:21;
       A16: p = 1 * p by EUCLID:33
          .= 0.REAL 2 + 1 * p by EUCLID:31
          .= (1-1)*f/.(Index(p,f))+1 * p by EUCLID:33;
       then 1<=r by A10,A13,A14,A15,JORDAN3:def 6;
       then r = 1 by A14,AXIOMS:21;
       hence contradiction by A1,A5,A6,A12,A15,A16,FINSEQ_4:24;
      end;
      assume p=f.len f;
      hence contradiction by A0,A1,A7,A11,ThRob41;
     end;
    end;
    then A17:L_Cut(f,p) is_S-Seq_joining p,f/.len f by A1,B1,Th68;
    A18: L_Cut(f,p) is_S-Seq by A1,A8,B1,Th69;
    now per cases by A2;
     case Index(p,f)<Index(q,f);
      then q in L~(L_Cut(f,p)) by A1,JORDAN3:64;
      hence ex i1 being Nat st 1<=i1 & i1+1<=len L_Cut(f,p)
        & q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
     end;
     case Index(p,f)=Index(q,f)
         & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      then q in L~(L_Cut(f,p)) by A1,JORDAN3:66;
      hence ex i1 being Nat st 1<=i1 & i1+1<=len L_Cut(f,p)
        & q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
     end;
    end;
    then A19: q in L~L_Cut(f,p) by SPPOL_2:17;
    then 1<=Index(q,L_Cut(f,p)) & Index(q,L_Cut(f,p))<len L_Cut(f,p)
      by JORDAN3:41;
    then A20: 1<=len L_Cut(f,p) by AXIOMS:22;
    A21:(L_Cut(f,p)).1=p by A17,JORDAN3:def 3;
    then p=(L_Cut(f,p))/. 1 by A20,FINSEQ_4:24;
    hence B_Cut(f,p,q) is_S-Seq_joining p,q by A1,A3,A18,A19,A21,JORDAN3:67;
  end;

  theorem Th71:
   for f be non empty FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. & len f <> 2 &
    p in L~f & q in L~f & p<>q & p <> f.1 & q <> f.1 holds
     B_Cut(f,p,q) is_S-Seq_joining p,q
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    assume A1: f is almost-one-to-one special unfolded s.n.c. &
     len f <> 2 & p in L~f & q in L~f & p<>q & p <> f.1 & q <> f.1;
    per cases;
     suppose Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f) &
       LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      hence thesis by A1,Lm2;
     end;
     suppose A2: not(Index(p,f)<Index(q,f) or
       Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
      then A3: B_Cut(f,p,q)=Rev R_Cut(L_Cut(f,q),p) by JORDAN3:def 8;
      A4: Index(q,f) < Index(p,f) or Index(p,f)=Index(q,f) &
       not LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1) by A2,AXIOMS:21;
      A5: now assume that
        A6: Index(p,f)=Index(q,f) and
        A7: not LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
       A8: 1 <= Index(p,f) by A1,JORDAN3:41;
       A9: Index(p,f) < len f by A1,JORDAN3:41;
       then A10: Index(p,f)+1 <= len f by NAT_1:38;
       then A11: LSeg(f,Index(p,f)) = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
          by A8,TOPREAL1:def 5;
       then A12: p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A1,JORDAN3:42;
       A13: q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
          by A1,A6,A11,JORDAN3:42;
       A14: Index(p,f) in dom f by A8,A9,FINSEQ_3:27;
       1 <= Index(p,f)+1 by NAT_1:29;
       then A15: Index(p,f)+1 in dom f by A10,FINSEQ_3:27;
       f is almost-one-to-one FinSequence by A1;
       then f is weakly-one-to-one by A1,ThRob9;
       then f.Index(p,f) <> f.(Index(p,f)+1) by A8,A9,Def2;
       then f/.(Index(p,f))<>f.(Index(p,f)+1) by A14,FINSEQ_4:def 4;
       then f/.(Index(p,f))<>f/.(Index(p,f)+1) by A15,FINSEQ_4:def 4;
       then LT q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A7,A12,A13,JORDAN3:63;
       hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A6,JORDAN3:def 7;
      end;
      then A17: B_Cut(f,q,p) is_S-Seq_joining q,p by A1,A4,Lm2;
      Rev B_Cut(f,q,p) = B_Cut(f,p,q) by A1,A3,A4,A5,JORDAN3:def 8;
      hence thesis by A17,JORDAN3:48;
     end;
   end;

  theorem
   for f be non empty FinSequence of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st
    f is almost-one-to-one special unfolded s.n.c. & len f <> 2 &
    p in L~f & q in L~f & p<>q & p <> f.1 & q <> f.1 holds
     B_Cut(f,p,q) is_S-Seq
   proof
    let f be non empty FinSequence of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    assume f is almost-one-to-one special unfolded s.n.c. &
     len f <> 2 & p in L~f & q in L~f & p<>q & p <> f.1 & q <> f.1;
    then B_Cut(f,p,q) is_S-Seq_joining p,q by Th71;
    hence B_Cut(f,p,q) is_S-Seq by JORDAN3:def 3;
   end;

  theorem :: Lemat do "bardzo wazne dla Cage"
   for C be compact non vertical non horizontal Subset of TOP-REAL 2
   for p,q be Point of TOP-REAL 2 st p in BDD L~Cage(C,n)
   ex B be S-Sequence_in_R2 st
    B = B_Cut(Rotate(Cage(C,n),Cage(C,n)/.Index(South-Bound(p,L~Cage(C,n)),
     Cage(C,n)))|(len Rotate(Cage(C,n),Cage(C,n)/.
      Index(South-Bound(p,L~Cage(C,n)),
     Cage(C,n)))-'1),South-Bound(p,L~Cage(C,n)),North-Bound(p,L~Cage(C,n))) &
   ex P be S-Sequence_in_R2 st P is_sequence_on
     GoB(B^'<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*>) &
    L~<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*> = L~P &
    P/.1 = North-Bound(p,L~Cage(C,n)) &
    P/.len P = South-Bound(p,L~Cage(C,n)) &
    len P >= 2 &
   ex B1 be S-Sequence_in_R2 st B1 is_sequence_on
     GoB(B^'<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*>) &
    L~B = L~B1 &
    B/.1 = B1/.1 &
    B/.len B = B1/.len B1 &
    len B <= len B1 &
   ex g be non constant standard special_circular_sequence st
    g = B1^'P
   proof
    let C be compact non vertical non horizontal Subset of TOP-REAL 2;
    let p,q be Point of TOP-REAL 2;
    set G = Gauge(C,n);
    set f = Cage(C,n);
    set Lf = L~f;
    set a = North-Bound(p,Lf);
    set b = South-Bound(p,Lf);
    set c = North-Bound(q,Lf);
    set d = South-Bound(q,Lf);
    assume that
     A1: p in BDD Lf;
    set Rotf = Rotate(f,f/.Index(b,f));
    set rf = Rotf|(len Rotf-'1);
    B20: L~Rotf = L~f by REVROT_1:33;
    B21: len Rotf = len f by REVROT_1:14;
    B24: len f > 4 by GOBOARD7:36;
    then B41: len f > 1+1 by AXIOMS:22;
    then B23: len f-1 > 1 by REAL_1:86;
    B22: 1 < len f-'1 by B23,BINARITH:def 3;
    D5: len f > 1 by B24,AXIOMS:22;
    then B25: len f-'1+1 = len f by AMI_5:4;
    then reconsider rf as S-Sequence_in_R2 by B21,B22,JORDAN4:49;
    B2: a <> b by A1,JORDAN18:25;
    A34: a`1 = p`1 & b`1 = p`1 by JORDAN18:20;
    B47: b`2 < p`2 & p`2 < a`2 by A1,JORDAN18:23;
    then A35: p in LSeg(a,b) by A34,GOBOARD7:8;
    A18: L~rf c= L~f by B20,TOPREAL3:27;
    A17: a in Lf & b in Lf by A1,JORDAN18:22;
    A37: 1<=Index(b,f) & Index(b,f) < len f by A17,JORDAN3:41;
    then A19: Index(b,f) in dom f by FINSEQ_3:27;
    then A22: f/.Index(b,f) in rng f by PARTFUN2:4;
    A20: for i be Nat st 1 <= i & i < Index(b,f) holds f/.i <> f/.Index(b,f)
     by A37,GOBOARD7:38;
    A38: len(f:-(f/.Index(b,f)))=len f-(f/.Index(b,f))..f+1 by A22,FINSEQ_5:53;
    f/.Index(b,f)..f <= Index(b,f) by A19,FINSEQ_5:42;
    then 0+(f/.Index(b,f))..f < len f by A37,AXIOMS:22;
    then len f - (f/.Index(b,f))..f > 1-1 by REAL_1:86;
    then 1 < len(f:-(f/.Index(b,f))) by A38,REAL_1:84;
    then LSeg(Rotate(f,f/.Index(b,f)),1) = LSeg(f,1 -' 1 + (f/.Index(b,f))..f)
                                                            by A22,REVROT_1:24
       .= LSeg(f,0+(f/.Index(b,f))..f) by GOBOARD9:1
       .= LSeg(f,Index(b,f)) by A19,A20,FINSEQ_6:52;
    then B28: b in LSeg(Rotate(f,f/.Index(b,f)),1) by A17,JORDAN3:42;
    len Rotf >=2+2 by GOBOARD7:36;
    then len Rotf >=1+1 by AXIOMS:22;
    then A32: LSeg(Rotf,1) = LSeg(Rotf/.1,Rotf/.(1+1)) by TOPREAL1:def 5;
    B51: LSeg(a,b) is vertical by A34,SPPOL_1:37;
    B57: Rotf/.1 = Rotf/.len Rotf by FINSEQ_6:def 1;
    D3: Rotf/.2 in LSeg(Rotf/.2,Rotf/.1) &
     Rotf/.(len Rotf-'1) in LSeg(Rotf/.(len Rotf-'1),Rotf/.(len Rotf))
      by TOPREAL1:6;
    D6: len f-'1 < len f by B25,NAT_1:38;
    D4: LSeg(Rotf,len Rotf -' 1) /\ LSeg(Rotf,1) = {Rotf/.1}
     by B21,B24,REVROT_1:30;
    then B44: LSeg(Rotf,len Rotf -' 1) meets LSeg(Rotf,1) by XBOOLE_0:def 7;
    B42: LSeg(Rotf,1) = LSeg(Rotf/.1,Rotf/.(1+1)) by B21,B41,TOPREAL1:def 5;
    B43: LSeg(Rotf,len Rotf-'1) = LSeg(Rotf/.(len Rotf-'1),Rotf/.(len Rotf))
       by B21,B25,B23,TOPREAL1:def 5;
    B50: b`2 < a`2 by B47,AXIOMS:22;
    B26: now assume B46: a in LSeg(Rotf,len Rotf-'1);
     per cases by SPPOL_1:41;
      suppose B49: LSeg(Rotf,1) is vertical &
        LSeg(Rotf,len Rotf-'1) is vertical;
       B53: (Rotf/.1)`1 = (Rotf/.(1+1))`1 &
        (Rotf/.(len Rotf-'1))`1 = (Rotf/.(len Rotf))`1
         by B42,B43,B49,SPPOL_1:37;
       B54: a`1 = (Rotf/.(len Rotf-'1))`1 & b`1 = (Rotf/.1)`1
        by B28,B42,B43,B46,B49,SPRECT_3:20;
       per cases by B50,AXIOMS:22;
        suppose B55: b`2 < (Rotf/.1)`2;
         set p1 = |[(Rotf/.1)`1,(b`2+(Rotf/.1)`2)/2]|;
         B52: p1`1 = (Rotf/.1)`1 & p1`2 = (b`2+(Rotf/.1)`2)/2 by EUCLID:56;
         B56: b`2 < (b`2+(Rotf/.1)`2)/2 & (b`2+(Rotf/.1)`2)/2 < (Rotf/.1)`2
          by B55,TOPREAL3:3;
         D2: (Rotf/.1)`2 > (Rotf/.(1+1))`2 by B28,B42,B55,TOPREAL1:10;
         then (Rotf/.(1+1))`2 <= b`2 by B28,B42,TOPREAL1:10;
         then (Rotf/.(1+1))`2 < p1`2 by B52,B56,AXIOMS:22;
         then p1 in LSeg(Rotf,1) by A32,B52,B53,B56,GOBOARD7:8;
         then B58: p1 in Lf by B20,SPPOL_2:17;
         (Rotf/.len Rotf)`2 <= (Rotf/.(len Rotf-'1))`2
         proof
          assume D1: (Rotf/.len Rotf)`2 > (Rotf/.(len Rotf-'1))`2;
          (Rotf/.(len Rotf-'1))`2 < (Rotf/.(1+1))`2 or
           (Rotf/.(len Rotf-'1))`2 >= (Rotf/.(1+1))`2;
          then Rotf/.2 in LSeg(Rotf/.(len Rotf-'1),Rotf/.(len Rotf)) or
           Rotf/.(len Rotf-'1) in LSeg(Rotf/.2,Rotf/.1)
            by B53,B57,D1,D2,GOBOARD7:8;
          then Rotf/.2 in {Rotf/.1} or Rotf/.(len Rotf-'1) in {Rotf/.1}
           by B42,B43,D3,D4,XBOOLE_0:def 3;
          then Rotf/.(1+1) = Rotf/.1 or Rotf/.(len Rotf-'1) = Rotf/.(len Rotf)
           by B57,TARSKI:def 1;
          hence contradiction by B21,B23,B25,D5,D6,ThRob45;
         end;
         then B59: (Rotf/.len Rotf)`2 <= a`2 by B43,B46,TOPREAL1:10;
         then p1`2 < a`2 by B52,B56,B57,AXIOMS:22;
         then p1 in LSeg(b,a) by A34,B52,B54,B56,GOBOARD7:8;
         then p1 in LSeg(a,b) /\ Lf by B58,XBOOLE_0:def 3;
         then p1 in {a,b} by A1,JORDAN18:27;
         hence contradiction by B52,B56,B57,B59,TARSKI:def 2;
        end;
        suppose B55: (Rotf/.1)`2 < a`2;
         set p1 = |[(Rotf/.1)`1,(a`2+(Rotf/.1)`2)/2]|;
         B52: p1`1 = (Rotf/.1)`1 & p1`2 = (a`2+(Rotf/.1)`2)/2 by EUCLID:56;
         B56: (Rotf/.1)`2 < (a`2+(Rotf/.1)`2)/2 & (a`2+(Rotf/.1)`2)/2 < a`2
          by B55,TOPREAL3:3;
         D2: (Rotf/.(len Rotf-'1))`2 > (Rotf/.len Rotf)`2
          by B43,B46,B55,B57,TOPREAL1:10;
         then a`2 <= (Rotf/.(len Rotf-'1))`2 by B43,B46,TOPREAL1:10;
         then p1`2 < (Rotf/.(len Rotf-'1))`2 by B52,B56,AXIOMS:22;
         then p1 in LSeg(Rotf,len Rotf-'1) by B43,B52,B53,B56,B57,GOBOARD7:8;
         then B58: p1 in Lf by B20,SPPOL_2:17;
         (Rotf/.(1+1))`2 <= (Rotf/.1)`2
         proof
          assume D1: (Rotf/.(1+1))`2 > (Rotf/.1)`2;
          (Rotf/.(len Rotf-'1))`2 < (Rotf/.(1+1))`2 or
           (Rotf/.(len Rotf-'1))`2 >= (Rotf/.(1+1))`2;
          then Rotf/.2 in LSeg(Rotf/.(len Rotf-'1),Rotf/.(len Rotf)) or
           Rotf/.(len Rotf-'1) in LSeg(Rotf/.2,Rotf/.1)
            by B53,B57,D1,D2,GOBOARD7:8;
          then Rotf/.2 in {Rotf/.1} or Rotf/.(len Rotf-'1) in {Rotf/.1}
           by B42,B43,D3,D4,XBOOLE_0:def 3;
          then Rotf/.(1+1) = Rotf/.1 or Rotf/.(len Rotf-'1) = Rotf/.(len Rotf)
           by B57,TARSKI:def 1;
          hence contradiction by B21,B23,B25,D5,D6,ThRob45;
         end;
         then B59: b`2 <= (Rotf/.1)`2 by B28,B42,TOPREAL1:10;
         then b`2 < p1`2 by B52,B56,AXIOMS:22;
         then p1 in LSeg(b,a) by A34,B52,B54,B56,GOBOARD7:8;
         then p1 in LSeg(a,b) /\ Lf by B58,XBOOLE_0:def 3;
         then p1 in {a,b} by A1,JORDAN18:27;
         hence contradiction by B52,B56,B59,TARSKI:def 2;
        end;
      end;
      suppose D7: LSeg(Rotf,1) is vertical &
        LSeg(Rotf,len Rotf-'1) is horizontal;
       D8: a`1 = (Rotf/.len Rotf)`1 by A34,B57,B28,B42,D7,SPRECT_3:20;
       a`2 = (Rotf/.len Rotf)`2 by B43,B46,D7,SPRECT_3:19;
       then a = Rotf/.len Rotf by D8,TOPREAL3:11;
       then a in LSeg(Rotf/.1,Rotf/.2) by B57,TOPREAL1:6;
       then LSeg(a,b) c= LSeg(Rotf/.1,Rotf/.2) by B28,B42,TOPREAL1:12;
       then p in Lf by A35,B20,B42,SPPOL_2:17;
       then p in LSeg(a,b) /\ Lf by A35,XBOOLE_0:def 3;
       then p in {a,b} by A1,JORDAN18:27;
       hence contradiction by B47,TARSKI:def 2;
      end;
      suppose D7: LSeg(Rotf,1) is horizontal &
        LSeg(Rotf,len Rotf-'1) is vertical;
       D8: b`1 = (Rotf/.1)`1 by A34,B43,B46,B57,D7,SPRECT_3:20;
       b`2 = (Rotf/.1)`2 by B28,B42,D7,SPRECT_3:19;
       then b = Rotf/.1 by D8,TOPREAL3:11;
       then b in LSeg(Rotf/.(len Rotf-'1),Rotf/.len Rotf) by B57,TOPREAL1:6;
       then LSeg(a,b) c= LSeg(Rotf/.(len Rotf-'1),Rotf/.len Rotf)
        by B43,B46,TOPREAL1:12;
       then p in Lf by A35,B20,B43,SPPOL_2:17;
       then p in LSeg(a,b) /\ Lf by A35,XBOOLE_0:def 3;
       then p in {a,b} by A1,JORDAN18:27;
       hence contradiction by B47,TARSKI:def 2;
      end;
      suppose B45: LSeg(Rotf,1) is horizontal &
        LSeg(Rotf,len Rotf-'1)is horizontal;
       b`2 = (Rotf/.1)`2 & a`2 = (Rotf/.(len Rotf-'1))`2
        by B28,B42,B43,B45,B46,SPRECT_3:19;
       hence contradiction by B42,B43,B44,B47,B45,SPRECT_3:21;
      end;
    end;
    B29: L~Rotf = L~rf \/ LSeg(Rotf,len Rotf-'1) 
       by B21,B23,B25,GOBOARD2:8;
    B40: LSeg(rf,1) c= L~rf by TOPREAL3:26;
    len Rotf-'1 <= len Rotf by GOBOARD9:2;
    then len rf = len Rotf-'1 by FINSEQ_1:80;
    then 1+1 <= len rf by B21,B22,NAT_1:38;
    then b in LSeg(rf,1) by B28,SPPOL_2:3;
    then A17': a in L~rf & b in L~rf by A17,B20,B26,B29,B40,XBOOLE_0:def 2;
    then reconsider BCut = B_Cut(rf,b,a) as S-Sequence_in_R2 by B2,JORDAN3:72;
    set pion = <*a,b*>;
    set Ga = GoB(BCut^'pion);
    B8: len pion = 1+1 by FINSEQ_1:61;
    len (BCut^'pion) + 1 = len BCut + (1+1) by B8,GRAPH_2:13
       .= len BCut + 1 + 1;
    then B9: len (BCut^'pion) = len BCut + 1 by XCMPLX_1:2;
    then B13: len BCut < len (BCut^'pion) by NAT_1:38;
    B10: len BCut + 1 >= 1 by NAT_1:29;
    len BCut <> 0 by FINSEQ_1:25;
    then len BCut > 0 by NAT_1:19;
    then B12: len BCut >= 0+1 by NAT_1:38;
    then B11: (BCut^'pion)/.len BCut = BCut/.len BCut by AMISTD_1:9
       .= a by A17',ThA
       .= pion/.1 by FINSEQ_4:26;
    B1: now let n be Nat;
     assume n in dom pion;
     then B3: n in Seg 2 by FINSEQ_3:29;
     per cases by B3,TARSKI:def 2,FINSEQ_1:4;
      suppose B7: n = 1;
       len BCut in Seg len (BCut^'pion) by B12,B13,FINSEQ_1:3;
       then len BCut in dom (BCut^'pion) by FINSEQ_1:def 3;
       then consider i,j be Nat such that
        B5: [i,j] in Indices Ga and
        B6: (BCut^'pion)/.len BCut = Ga*(i,j) by GOBOARD2:25;
       take i,j;
       thus [i,j] in Indices Ga by B5;
       thus pion/.n = Ga*(i,j) by B6,B7,B11;
      end;
      suppose B7: n = 1+1;
       len BCut + 1 in dom (BCut^'pion) by B9,B10,FINSEQ_3:27;
       then consider i,j be Nat such that
        B5: [i,j] in Indices Ga and
        B6: (BCut^'pion)/.(len BCut + 1) = Ga*(i,j) by GOBOARD2:25;
       take i,j;
       thus [i,j] in Indices Ga by B5;
       thus pion/.n = Ga*(i,j) by B8,B6,B7,AMISTD_1:10;
      end;
    end;
    pion is_S-Seq by B2,B51,JORDAN1B:8;
    then consider pion1 be FinSequence of TOP-REAL 2 such that
     B14: pion1 is_sequence_on Ga and
     B15: pion1 is_S-Seq and
     B16: L~pion = L~pion1 and
     B17: pion/.1 = pion1/.1 and
     B18: pion/.len pion = pion1/.len pion1 and
     B19: len pion <= len pion1 by B1,GOBOARD3:2;
    reconsider pion1 as S-Sequence_in_R2 by B15;
    C0: now let n be Nat;
     assume D9: n in dom BCut;
     dom BCut c= dom (BCut^'pion) by ThRob46;
     then consider i,j be Nat such that
      D10: [i,j] in Indices GoB(BCut^'pion) and
      D11: (BCut^'pion)/.n = GoB(BCut^'pion)*(i,j) by D9,GOBOARD2:25;
     take i,j;
     thus [i,j] in Indices Ga by D10;
     1 <= n & n <= len BCut by D9,FINSEQ_3:27;
     hence BCut/.n = Ga*(i,j) by D11,AMISTD_1:9;
    end;
    consider BCut1 be FinSequence of TOP-REAL 2 such that
     C1: BCut1 is_sequence_on Ga and
     C2: BCut1 is_S-Seq and
     C3: L~BCut = L~BCut1 and
     C4: BCut/.1 = BCut1/.1 and
     C5: BCut/.len BCut = BCut1/.len BCut1 and
     C6: len BCut <= len BCut1 by C0,GOBOARD3:2;
    reconsider BCut1 as S-Sequence_in_R2 by C2;
    set g = BCut1^'pion1;
    B30: BCut1/.len BCut1 = a by A17',C5,ThA
       .= pion1/.1 by B17,FINSEQ_4:26;
    A62: 1 in dom BCut1 & len BCut1 in dom BCut1 by FINSEQ_5:6;
    A57: now assume BCut1 is constant;
     then BCut1/.1 = BCut1/.len BCut1 by A62,REVROT_1:def 1;
     then BCut1/.len BCut1 = b by A17',C4,ThRob19;
     then a = b by A17',C5,ThA;
     hence contradiction by A1,JORDAN18:25;
    end;
    A58: BCut1^'pion1 is_sequence_on Ga by B14,B30,C1,TOPREAL8:12;
    D12: L~BCut1 c= L~rf by A17',C3,JORDAN5B:27;
    LSeg(BCut1,len BCut1 -' 1) c= L~BCut1 by TOPREAL3:26;
    then LSeg(BCut1,len BCut1 -' 1) c= L~rf by D12,XBOOLE_1:1;
    then D13: LSeg(BCut1,len BCut1 -' 1) c= Lf by A18,XBOOLE_1:1;
    LSeg(pion1,1) c= L~pion by B16,TOPREAL3:26;
    then LSeg(pion1,1) c= LSeg(a,b) by SPPOL_2:21;
    then LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1) c= LSeg(a,b) /\ Lf
     by D13,XBOOLE_1:27;
    then D14: LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1) c= {a,b}
     by A1,JORDAN18:27;
    D21: len BCut1 >= 1+1 by TOPREAL1:def 10;
    then D20: len BCut1-1 >= 1 by REAL_1:84;
    D19: len BCut1-'1 >= 1 by D20,BINARITH:def 3;
    len BCut1 > 1 by D21,NAT_1:38;
    then D20: len BCut1-'1+1 = len BCut1 by AMI_5:4;
    BCut/.len BCut = a by A17',ThA;
    then D22: a in LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1)
     by C5,TOPREAL1:6;
    D23: L~BCut1 c= Lf by A18,D12,XBOOLE_1:1;
    D15: now assume b in LSeg(BCut1,len BCut1 -' 1);
     then b in LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1)
      by D19,D20,TOPREAL1:def 5;
     then LSeg(a,b) c= LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1)
      by D22,TOPREAL1:12;
     then p in LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1) by A35;
     then p in LSeg(BCut1,len BCut1-'1) by D19,D20,TOPREAL1:def 5;
     then p in L~BCut1 by SPPOL_2:17;
     then p in LSeg(a,b) /\ Lf by A35,D23,XBOOLE_0:def 3;
     then p in {a,b} by A1,JORDAN18:27;
     hence contradiction by B47,TARSKI:def 2;
    end;
    A60: LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1) = { BCut1/.len BCut1 }
    proof
     thus LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1) c= { BCut1/.len BCut1 }
     proof
      let x be set;
      assume D16: x in LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1);
      x <> b by D15,D16,XBOOLE_0:def 3;
      then x = a by D14,D16,TARSKI:def 2;
      then x = BCut1/.len BCut1 by A17',C5,ThA;
      hence x in { BCut1/.len BCut1 } by TARSKI:def 1;
     end;
     let x be set;
     assume x in { BCut1/.len BCut1 };
     then B22: x = BCut1/.len BCut1 by TARSKI:def 1;
     x in LSeg(BCut1/.(len BCut1-'1),BCut1/.len BCut1) by B22,TOPREAL1:6;
     then D18: x in LSeg(BCut1,len BCut1-'1) by D19,D20,TOPREAL1:def 5;
     x in LSeg(pion1/.1,pion1/.(1+1)) by B22,B30,TOPREAL1:6;
     then x in LSeg(pion1,1) by B8,B19,TOPREAL1:def 5;
     hence x in LSeg(BCut1,len BCut1 -' 1) /\ LSeg(pion1,1)
      by D18,XBOOLE_0:def 3;
    end;
    A64: pion1/.len pion1 = b by B8,B18,FINSEQ_4:26
       .= BCut1/.1 by A17',C4,ThRob19;
    now assume len BCut = 1;
     then BCut/.1 = a by A17',ThA;
     hence contradiction by B2,A17',ThRob19;
    end;
    then len BCut > 1 by B12,REAL_1:def 5;
    then len BCut1 > 1 by C6,AXIOMS:22;
    then A69: len BCut1 >= 1+1 by NAT_1:38;
    A70: LSeg(a,b) /\ Lf = {a,b} by A1,JORDAN18:27;
    L~BCut1 c= Lf by A18,D12,XBOOLE_1:1;
    then L~BCut1 /\ LSeg(a,b) c= {a,b} by A70,XBOOLE_1:26;
    then L~BCut1 /\ L~pion1 c= {a,b} by B16,SPPOL_2:21;
    then L~BCut1 /\ L~pion1 c= {a,BCut1/.1} by A17',C4,ThRob19;
    then A68: L~BCut1 /\ L~pion1 c= {BCut1/.1,pion1/.1} by B17,FINSEQ_4:26;
    {BCut1/.1,pion1/.1} c= L~BCut1 /\ L~pion1
    proof
     let x be set;
     assume x in {BCut1/.1,pion1/.1};
     then x = BCut1/.1 or x = pion1/.1 by TARSKI:def 2;
     then x in L~BCut1 & x in L~pion1 by A64,A69,B8,B19,B30,JORDAN3:34;
     hence x in L~BCut1 /\ L~pion1 by XBOOLE_0:def 3;
    end;
    then L~BCut1 /\ L~pion1 = {BCut1/.1,pion1/.1} by A68,XBOOLE_0:def 10;
    then reconsider g as non constant standard special_circular_sequence
           by A57,A58,A60,A64,B30,ThRob32,TOPREAL8:11,33,34,JORDAN8:7;
    R1: len <*a,b*> = 2 by FINSEQ_1:61;
    take BCut;
    thus BCut = B_Cut(Rotate(Cage(C,n),Cage(C,n)/.
     Index(South-Bound(p,L~Cage(C,n)),
     Cage(C,n)))|(len Rotate(Cage(C,n),Cage(C,n)/.
     Index(South-Bound(p,L~Cage(C,n)),
     Cage(C,n)))-'1),South-Bound(p,L~Cage(C,n)),North-Bound(p,L~Cage(C,n)));
    take pion1;
    thus pion1 is_sequence_on
     GoB(BCut^'<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*>) &
     L~<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*> = L~pion1 &
     pion1/.1 = North-Bound(p,L~Cage(C,n)) &
     pion1/.len pion1 = South-Bound(p,L~Cage(C,n)) &
     len pion1 >= 2 by B14,B16,B17,B18,B19,R1,FINSEQ_4:26;
    take BCut1;
    thus BCut1 is_sequence_on
     GoB(BCut^'<*North-Bound(p,L~Cage(C,n)),South-Bound(p,L~Cage(C,n))*>) &
     L~BCut = L~BCut1 &
     BCut/.1 = BCut1/.1 &
     BCut/.len BCut = BCut1/.len BCut1 &
     len BCut <= len BCut1 by C1,C3,C4,C5,C6;
    take g;
    thus g = BCut1^'pion1;
   end;

