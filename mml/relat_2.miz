:: Properties of Binary Relations
::  by Edmund Woronowicz and Anna Zalewska
::
:: Received March 15, 1989
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies RELAT_1, TARSKI, XBOOLE_0, ZFMISC_1, RELAT_2;
 notations TARSKI, XBOOLE_0, ZFMISC_1, RELAT_1;
 constructors TARSKI, SUBSET_1, RELAT_1;
 registrations RELAT_1;
 requirements SUBSET;
 theorems TARSKI, XBOOLE_0, ZFMISC_1, RELAT_1;

begin

reserve X,a,b,c,x,y,z for set;
reserve P,R for Relation;

definition
  let R,X;
  pred R is_reflexive_in X means
  :Def1:
  x in X implies [x,x] in R;
  pred R is_irreflexive_in X means
  :Def2:
  x in X implies not [x,x] in R;
  pred R is_symmetric_in X means
  :Def3:
  x in X & y in X & [x,y] in R implies [ y,x] in R;
  pred R is_antisymmetric_in X means
  :Def4:
  x in X & y in X & [x,y] in R & [y, x] in R implies x = y;
  pred R is_asymmetric_in X means
  :Def5:
  x in X & y in X & [x,y] in R implies not [y,x] in R;
  pred R is_connected_in X means
  :Def6:
  x in X & y in X & x <>y implies [x,y] in R or [y,x] in R;
  pred R is_strongly_connected_in X means
  :Def7:
  x in X & y in X implies [x,y] in R or [y,x] in R;
  pred R is_transitive_in X means
  :Def8:
  x in X & y in X & z in X & [x,y] in R & [y,z] in R implies [x,z] in R;
end;

definition
  let R;
  attr R is reflexive means
  :Def9:
  R is_reflexive_in field R;
  attr R is irreflexive means
  :Def10:
  R is_irreflexive_in field R;
  attr R is symmetric means
  :Def11:
  R is_symmetric_in field R;
  attr R is antisymmetric means
  :Def12:
  R is_antisymmetric_in field R;
  attr R is asymmetric means
  :Def13:
  R is_asymmetric_in field R;
  attr R is connected means
  :Def14:
  R is_connected_in field R;
  attr R is strongly_connected means
  :Def15:
  R is_strongly_connected_in field R;
  attr R is transitive means
  :Def16:
  R is_transitive_in field R;
end;

canceled 16;

theorem
  R is reflexive iff id field R c= R
proof
A1: now
    assume
A2: id field R c= R;
    now
      let a;
      assume a in field R;
      then [a,a] in id field R by RELAT_1:def 10;
      hence [a,a] in R by A2;
    end;
    then R is_reflexive_in field R by Def1;
    hence R is reflexive by Def9;
  end;
  now
    assume R is reflexive;
    then
A3: R is_reflexive_in field R by Def9;
    now
      let a,b;
      assume [a,b] in id field R;
      then a in field R & a = b by RELAT_1:def 10;
      hence [a,b] in R by A3,Def1;
    end;
    hence id field R c= R by RELAT_1:def 3;
  end;
  hence thesis by A1;
end;

theorem
  R is irreflexive iff id field R misses R
proof
A1: now
    assume R is irreflexive;
    then
A2: R is_irreflexive_in field R by Def10;
    now
      let a,b;
      assume
A3:   [a,b] in id (field R) /\ R;
      then [a,b] in id (field R) by XBOOLE_0:def 4;
      then
A4:   a in field R & a = b by RELAT_1:def 10;
      [a,b] in R by A3,XBOOLE_0:def 4;
      hence contradiction by A2,A4,Def2;
    end;
    then id (field R) /\ R = {} by RELAT_1:56;
    hence id (field R) misses R by XBOOLE_0:def 7;
  end;
  now
    assume
A5: id (field R) misses R;
    now
      let a;
      assume a in field R;
      then [a,a] in id field R by RELAT_1:def 10;
      hence not [a,a] in R by A5,XBOOLE_0:3;
    end;
    then R is_irreflexive_in field R by Def2;
    hence R is irreflexive by Def10;
  end;
  hence thesis by A1;
end;

theorem
  R is_antisymmetric_in X iff R \ id X is_asymmetric_in X
proof
  thus R is_antisymmetric_in X implies R \ id X is_asymmetric_in X
  proof
    assume
A1: R is_antisymmetric_in X;
    now
      let x,y;
      assume that
A2:   x in X and
A3:   y in X and
A4:   [x,y] in R \ id X;
      not [x,y] in id X by A4,XBOOLE_0:def 5;
      then
A5:   x <> y by A2,RELAT_1:def 10;
      [x,y] in R by A4,XBOOLE_0:def 5;
      then not [y,x] in R by A1,A2,A3,A5,Def4;
      hence not [y,x] in R \ id X by XBOOLE_0:def 5;
    end;
    hence thesis by Def5;
  end;
  thus R \ id X is_asymmetric_in X implies R is_antisymmetric_in X
  proof
    assume
A6: R \ id X is_asymmetric_in X;
    now
      let x,y;
      assume that
A7:   x in X & y in X and
A8:   [x,y] in R and
A9:   [y,x] in R;
      now
        assume
A10:    x <> y;
        then not [y,x] in id X by RELAT_1:def 10;
        then
A11:    [y,x] in R \ id X by A9,XBOOLE_0:def 5;
        not [x,y] in id X by A10,RELAT_1:def 10;
        then [x,y] in R \ id X by A8,XBOOLE_0:def 5;
        hence contradiction by A6,A7,A11,Def5;
      end;
      hence x = y;
    end;
    hence thesis by Def4;
  end;
end;

theorem
  R is_asymmetric_in X implies R \/ id X is_antisymmetric_in X
proof
  assume
A1: R is_asymmetric_in X;
  now
    let x,y;
    assume that
A2: x in X & y in X and
A3: [x,y] in R \/ id X and
A4: [y,x] in R \/ id X;
    now
      assume
A5:   x <> y;
      then not [y,x] in id X by RELAT_1:def 10;
      then
A6:   [y,x] in R by A4,XBOOLE_0:def 3;
      not [x,y] in id X by A5,RELAT_1:def 10;
      then [x,y] in R by A3,XBOOLE_0:def 3;
      hence contradiction by A1,A2,A6,Def5;
    end;
    hence x = y;
  end;
  hence thesis by Def4;
end;

canceled;

theorem Th22:
  R is symmetric & R is transitive implies R is reflexive
proof
  assume that
A1: R is symmetric and
A2: R is transitive;
A3: R is_transitive_in field R by A2,Def16;
A4: R is_symmetric_in field R by A1,Def11;
  now
    let a;
A5: now
      assume a in dom R;
      then consider b such that
A6:   [a,b] in R by RELAT_1:def 4;
A7:   a in field R & b in field R by A6,RELAT_1:30;
      then [b,a] in R by A4,A6,Def3;
      hence [a,a] in R by A3,A6,A7,Def8;
    end;
A8: now
      assume a in rng R;
      then consider b such that
A9:   [b,a] in R by RELAT_1:def 5;
A10:  a in field R & b in field R by A9,RELAT_1:30;
      then [a,b] in R by A4,A9,Def3;
      hence [a,a] in R by A3,A9,A10,Def8;
    end;
    assume a in field R;
    then a in dom R \/ rng R by RELAT_1:def 6;
    hence [a,a] in R by A5,A8,XBOOLE_0:def 3;
  end;
  then R is_reflexive_in field R by Def1;
  hence thesis by Def9;
end;

theorem Th23:
  id X is symmetric transitive
proof
  thus id X is symmetric
  proof
    let a,b;
    assume that
    a in field(id X) and
    b in field(id X) and
A1: [a,b] in id X;
    a = b by A1,RELAT_1:def 10;
    hence thesis by A1;
  end;
  thus id X is transitive
  proof
    let a,b,c;
    assume that
    a in field id X and
    b in field id X and
    c in field id X and
A2: [a,b] in id X & [b,c] in id X;
    thus thesis by A2,RELAT_1:def 10;
  end;
end;

theorem
  id X is antisymmetric reflexive
proof
  thus id X is antisymmetric
  proof
    let a,b;
    assume that
    a in field(id X) and
    b in field(id X) and
A1: [a,b] in id X and
    [b,a] in id X;
    thus thesis by A1,RELAT_1:def 10;
  end;
  id X is symmetric & id X is transitive by Th23;
  hence id X is reflexive by Th22;
end;

theorem
  R is irreflexive transitive implies R is asymmetric
proof
  assume that
A1: R is irreflexive and
A2: R is transitive;
A3: R is_transitive_in field R by A2,Def16;
A4: R is_irreflexive_in field R by A1,Def10;
  now
    let a,b;
    assume that
A5: a in field R and
A6: b in field R;
    not [a,a] in R by A4,A5,Def2;
    hence [a,b] in R implies not [b,a] in R by A3,A5,A6,Def8;
  end;
  then R is_asymmetric_in field R by Def5;
  hence thesis by Def13;
end;

theorem
  R is asymmetric implies R is irreflexive antisymmetric
proof
  assume R is asymmetric;
  then
A1: R is_asymmetric_in field R by Def13;
  then for x holds x in field R implies not [x,x] in R by Def5;
  then R is_irreflexive_in field R by Def2;
  hence R is irreflexive by Def10;
  for x,y holds x in field R & y in field R & [x,y] in R & [y,x] in R
  implies x = y by A1,Def5;
  then R is_antisymmetric_in field R by Def4;
  hence thesis by Def12;
end;

theorem Th27:
  R is reflexive implies R~ is reflexive
proof
  assume R is reflexive;
  then
A1: R is_reflexive_in field R by Def9;
  now
    let x;
    assume x in field(R~);
    then x in field R by RELAT_1:38;
    then [x,x] in R by A1,Def1;
    hence [x,x] in R~ by RELAT_1:def 7;
  end;
  then R~ is_reflexive_in field(R~) by Def1;
  hence thesis by Def9;
end;

theorem
  R is irreflexive implies R~ is irreflexive
proof
  assume R is irreflexive;
  then
A1: R is_irreflexive_in field R by Def10;
  now
    let x;
    assume x in field(R~);
    then x in field R by RELAT_1:38;
    then not [x,x] in R by A1,Def2;
    hence not [x,x] in R~ by RELAT_1:def 7;
  end;
  then R~ is_irreflexive_in field(R~) by Def2;
  hence thesis by Def10;
end;

theorem
  R is reflexive implies dom R = dom(R~) & rng R = rng(R~)
proof
  assume
A1: R is reflexive;
  then
A2: R is_reflexive_in field R by Def9;
  R~ is reflexive by A1,Th27;
  then
A3: R~ is_reflexive_in field(R~) by Def9;
  now
    let x;
A4: now
      assume x in dom(R~);
      then x in dom(R~) \/ rng(R~) by XBOOLE_0:def 3;
      then x in field(R~) by RELAT_1:def 6;
      then [x,x] in R~ by A3,Def1;
      then [x,x] in R by RELAT_1:def 7;
      hence x in dom R by RELAT_1:def 4;
    end;
    now
      assume x in dom R;
      then x in dom R \/ rng R by XBOOLE_0:def 3;
      then x in field R by RELAT_1:def 6;
      then [x,x] in R by A2,Def1;
      then [x,x] in R~ by RELAT_1:def 7;
      hence x in dom(R~) by RELAT_1:def 4;
    end;
    hence x in dom R iff x in dom(R~) by A4;
  end;
  hence dom R = dom(R~) by TARSKI:2;
  now
    let x;
A5: now
      assume x in rng(R~);
      then x in dom(R~) \/ rng(R~) by XBOOLE_0:def 3;
      then x in field(R~) by RELAT_1:def 6;
      then [x,x] in R~ by A3,Def1;
      then [x,x] in R by RELAT_1:def 7;
      hence x in rng R by RELAT_1:def 5;
    end;
    now
      assume x in rng R;
      then x in dom R \/ rng R by XBOOLE_0:def 3;
      then x in field R by RELAT_1:def 6;
      then [x,x] in R by A2,Def1;
      then [x,x] in R~ by RELAT_1:def 7;
      hence x in rng(R~) by RELAT_1:def 5;
    end;
    hence x in rng R iff x in rng(R~) by A5;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th30:
  R is symmetric iff R = R~
proof
A1: now
    assume R is symmetric;
    then
A2: R is_symmetric_in field R by Def11;
    now
      let a,b;
A3:   now
        assume [a,b] in R~;
        then
A4:     [b,a] in R by RELAT_1:def 7;
        then a in field R & b in field R by RELAT_1:30;
        hence [a,b] in R by A2,A4,Def3;
      end;
      now
        assume
A5:     [a,b] in R;
        then a in field R & b in field R by RELAT_1:30;
        then [b,a] in R by A2,A5,Def3;
        hence [a,b] in R~ by RELAT_1:def 7;
      end;
      hence [a,b] in R iff [a,b] in R~ by A3;
    end;
    hence R = R~ by RELAT_1:def 2;
  end;
  now
    assume R = R~;
    then for a,b holds a in field R & b in field R & [a,b] in R implies [b,a]
    in R by RELAT_1:def 7;
    then R is_symmetric_in field R by Def3;
    hence R is symmetric by Def11;
  end;
  hence thesis by A1;
end;

theorem
  P is reflexive & R is reflexive implies P \/ R is reflexive & P /\ R
  is reflexive
proof
  assume that
A1: P is reflexive and
A2: R is reflexive;
A3: R is_reflexive_in field R by A2,Def9;
A4: P is_reflexive_in field P by A1,Def9;
  now
    let a;
A5: now
      assume a in field P;
      then [a,a] in P by A4,Def1;
      hence [a,a] in P \/ R by XBOOLE_0:def 3;
    end;
A6: now
      assume a in field R;
      then [a,a] in R by A3,Def1;
      hence [a,a] in P \/ R by XBOOLE_0:def 3;
    end;
    assume a in field(P \/ R);
    then a in field P \/ field R by RELAT_1:33;
    hence [a,a] in P \/ R by A5,A6,XBOOLE_0:def 3;
  end;
  then P \/ R is_reflexive_in field(P \/ R) by Def1;
  hence P \/ R is reflexive by Def9;
  now
    let a;
    assume
A7: a in field(P /\ R);
A8: field(P /\ R) c= field P /\ field R by RELAT_1:34;
    then a in field R by A7,XBOOLE_0:def 4;
    then
A9: [a,a] in R by A3,Def1;
    a in field P by A8,A7,XBOOLE_0:def 4;
    then [a,a] in P by A4,Def1;
    hence [a,a] in P /\ R by A9,XBOOLE_0:def 4;
  end;
  then P /\ R is_reflexive_in field(P /\ R) by Def1;
  hence thesis by Def9;
end;

theorem
  P is irreflexive & R is irreflexive implies P \/ R is irreflexive & P
  /\ R is irreflexive
proof
  assume that
A1: P is irreflexive and
A2: R is irreflexive;
A3: P is_irreflexive_in field P by A1,Def10;
A4: R is_irreflexive_in field R by A2,Def10;
  now
    let a;
A5: now
      assume a in field P;
      then
A6:   not [a,a] in P by A3,Def2;
A7:   not a in field R implies not [a,a] in R by RELAT_1:30;
      a in field R implies not [a,a] in R by A4,Def2;
      hence not [a,a] in P \/ R by A6,A7,XBOOLE_0:def 3;
    end;
A8: now
      assume a in field R;
      then
A9:   not [a,a] in R by A4,Def2;
A10:  not a in field P implies not [a,a] in P by RELAT_1:30;
      a in field P implies not [a,a] in P by A3,Def2;
      hence not [a,a] in P \/ R by A9,A10,XBOOLE_0:def 3;
    end;
    assume a in field(P \/ R);
    then a in field P \/ field R by RELAT_1:33;
    hence not [a,a] in P \/ R by A5,A8,XBOOLE_0:def 3;
  end;
  then P \/ R is_irreflexive_in field(P \/ R) by Def2;
  hence P \/ R is irreflexive by Def10;
  now
    let a;
    assume
A11: a in field(P /\ R);
    field(P /\ R) c= (field P) /\ (field R) by RELAT_1:34;
    then a in field P by A11,XBOOLE_0:def 4;
    then not [a,a] in P by A3,Def2;
    hence not [a,a] in P /\ R by XBOOLE_0:def 4;
  end;
  then P /\ R is_irreflexive_in field(P /\ R) by Def2;
  hence thesis by Def10;
end;

theorem
  P is irreflexive implies P \ R is irreflexive
proof
  assume P is irreflexive;
  then
A1: P is_irreflexive_in field P by Def10;
  now
    let a;
A2: now
      assume a in dom(P \ R);
      then consider b such that
A3:   [a,b] in P \ R by RELAT_1:def 4;
      [a,b] in P by A3,XBOOLE_0:def 5;
      then a in field P by RELAT_1:30;
      hence not [a,a] in P by A1,Def2;
    end;
A4: now
      assume a in rng(P \ R);
      then consider b such that
A5:   [b,a] in P \ R by RELAT_1:def 5;
      [b,a] in P by A5,XBOOLE_0:def 5;
      then a in field P by RELAT_1:30;
      hence not [a,a] in P by A1,Def2;
    end;
    assume a in field(P \ R);
    then a in dom(P \ R) \/ rng(P \ R) by RELAT_1:def 6;
    hence not [a,a] in P \ R by A2,A4,XBOOLE_0:def 3,def 5;
  end;
  then P \ R is_irreflexive_in field(P \ R) by Def2;
  hence thesis by Def10;
end;

theorem
  R is symmetric implies R~ is symmetric by Th30;

theorem
  P is symmetric & R is symmetric implies P \/ R is symmetric & P /\ R
  is symmetric & P \ R is symmetric
proof
  assume that
A1: P is symmetric and
A2: R is symmetric;
A3: R is_symmetric_in field R by A2,Def11;
A4: P is_symmetric_in field P by A1,Def11;
  now
    let a,b;
    assume that
    a in field(P \/ R) and
    b in field(P \/ R) and
A5: [a,b] in P \/ R;
A6: now
      assume
A7:   [a,b] in R;
      then a in field R & b in field R by RELAT_1:30;
      then [b,a] in R by A3,A7,Def3;
      hence [b,a] in P \/ R by XBOOLE_0:def 3;
    end;
    now
      assume
A8:   [a,b] in P;
      then a in field P & b in field P by RELAT_1:30;
      then [b,a] in P by A4,A8,Def3;
      hence [b,a] in P \/ R by XBOOLE_0:def 3;
    end;
    hence [b,a] in P \/ R by A5,A6,XBOOLE_0:def 3;
  end;
  then P \/ R is_symmetric_in field(P \/ R) by Def3;
  hence P \/ R is symmetric by Def11;
  now
    let a,b;
    assume that
A9: a in field(P /\ R) & b in field(P /\ R) and
A10: [a,b] in P /\ R;
A11: [a,b] in R by A10,XBOOLE_0:def 4;
A12: field(P /\ R) c= field P /\ field R by RELAT_1:34;
    then a in field R & b in field R by A9,XBOOLE_0:def 4;
    then
A13: [b,a] in R by A3,A11,Def3;
A14: [a,b] in P by A10,XBOOLE_0:def 4;
    a in field P & b in field P by A12,A9,XBOOLE_0:def 4;
    then [b,a] in P by A4,A14,Def3;
    hence [b,a] in P /\ R by A13,XBOOLE_0:def 4;
  end;
  then P /\ R is_symmetric_in field(P /\ R) by Def3;
  hence P /\ R is symmetric by Def11;
  now
    let a,b;
    assume that
    a in field(P \ R) and
    b in field(P \ R) and
A15: [a,b] in P \ R;
    not [a,b] in R by A15,XBOOLE_0:def 5;
    then
A16: not b in field R or not a in field R or not [b,a] in R by A3,
    Def3;
A17: [a,b] in P by A15,XBOOLE_0:def 5;
    then a in field P & b in field P by RELAT_1:30;
    then
A18: [b,a] in P by A4,A17,Def3;
    ( not b in field R or not a in field R ) implies not [b,a] in R by
    RELAT_1:30;
    hence [b,a] in P \ R by A18,A16,XBOOLE_0:def 5;
  end;
  then P \ R is_symmetric_in field(P \ R) by Def3;
  hence thesis by Def11;
end;

theorem
  R is asymmetric implies R~ is asymmetric
proof
  assume R is asymmetric;
  then
A1: R is_asymmetric_in field R by Def13;
  now
    let x,y;
    assume that
A2: x in field(R~) & y in field(R~) and
A3: [x,y] in R~;
A4: [y,x] in R by A3,RELAT_1:def 7;
    x in field R & y in field R by A2,RELAT_1:38;
    then not [x,y] in R by A1,A4,Def5;
    hence not [y,x] in R~ by RELAT_1:def 7;
  end;
  then R~ is_asymmetric_in field(R~) by Def5;
  hence thesis by Def13;
end;

theorem
  R is asymmetric implies P /\ R is asymmetric
proof
  assume R is asymmetric;
  then
A1: R is_asymmetric_in field R by Def13;
A2: field(P /\ R) c= (field P) /\ (field R) by RELAT_1:34;
  now
    let a,b;
    assume that
A3: a in field(P /\ R) & b in field(P /\ R) and
A4: [a,b] in P /\ R;
A5: [a,b] in R by A4,XBOOLE_0:def 4;
    a in field R & b in field R by A2,A3,XBOOLE_0:def 4;
    then not [b,a] in R by A1,A5,Def5;
    hence not [b,a] in P /\ R by XBOOLE_0:def 4;
  end;
  then P /\ R is_asymmetric_in field(P /\ R) by Def5;
  hence thesis by Def13;
end;

theorem
  P is asymmetric implies P \ R is asymmetric
proof
  assume P is asymmetric;
  then
A1: P is_asymmetric_in field P by Def13;
  now
    let a,b;
    assume that
    a in field(P \ R) and
    b in field(P \ R) and
A2: [a,b] in P \ R;
A3: [a,b] in P by A2,XBOOLE_0:def 5;
    then a in field P & b in field P by RELAT_1:30;
    then not [b,a] in P by A1,A3,Def5;
    hence not [b,a] in P \ R by XBOOLE_0:def 5;
  end;
  then P \ R is_asymmetric_in field(P \ R) by Def5;
  hence thesis by Def13;
end;

theorem
  R is antisymmetric iff R /\ (R~) c= id (dom R)
proof
A1: now
    assume R is antisymmetric;
    then
A2: R is_antisymmetric_in field R by Def12;
    now
      let a,b;
      assume
A3:   [a,b] in R /\ (R~);
      then [a,b] in R~ by XBOOLE_0:def 4;
      then
A4:   [b,a] in R by RELAT_1:def 7;
      then
A5:   b in dom R by RELAT_1:def 4;
A6:   [a,b] in R by A3,XBOOLE_0:def 4;
      then a in field R & b in field R by RELAT_1:30;
      then a = b by A2,A6,A4,Def4;
      hence [a,b] in id (dom R) by A5,RELAT_1:def 10;
    end;
    hence R /\ (R~) c= id (dom R) by RELAT_1:def 3;
  end;
  now
    assume
A7: R /\ (R~) c= id (dom R);
    now
      let a,b;
      assume that
      a in field R and
      b in field R and
A8:   [a,b] in R and
A9:   [b,a] in R;
      [a,b] in R~ by A9,RELAT_1:def 7;
      then [a,b] in R /\ (R~) by A8,XBOOLE_0:def 4;
      hence a = b by A7,RELAT_1:def 10;
    end;
    then R is_antisymmetric_in field R by Def4;
    hence R is antisymmetric by Def12;
  end;
  hence thesis by A1;
end;

theorem
  R is antisymmetric implies R~ is antisymmetric
proof
  assume R is antisymmetric;
  then
A1: R is_antisymmetric_in field R by Def12;
  now
    let x,y;
    assume that
A2: x in field(R~) & y in field(R~) and
A3: [x,y] in R~ & [y,x] in R~;
A4: [y,x] in R & [x,y] in R by A3,RELAT_1:def 7;
    x in field R & y in field R by A2,RELAT_1:38;
    hence x = y by A1,A4,Def4;
  end;
  then R~ is_antisymmetric_in field(R~) by Def4;
  hence thesis by Def12;
end;

theorem
  P is antisymmetric implies P /\ R is antisymmetric & P \ R is antisymmetric
proof
  assume P is antisymmetric;
  then
A1: P is_antisymmetric_in field P by Def12;
  now
    let a,b;
    assume that
    a in field(P /\ R) and
    b in field(P /\ R) and
A2: [a,b] in (P /\ R) and
A3: [b,a] in (P /\ R);
A4: [b,a] in P by A3,XBOOLE_0:def 4;
A5: [a,b] in P by A2,XBOOLE_0:def 4;
    then a in field P & b in field P by RELAT_1:30;
    hence a = b by A1,A5,A4,Def4;
  end;
  then P /\ R is_antisymmetric_in field(P /\ R) by Def4;
  hence P /\ R is antisymmetric by Def12;
  now
    let a,b;
    assume that
    a in field(P \ R) and
    b in field(P \ R) and
A6: [a,b] in P \ R and
A7: [b,a] in P \ R;
A8: [b,a] in P by A7,XBOOLE_0:def 5;
A9: [a,b] in P by A6,XBOOLE_0:def 5;
    then a in field P & b in field P by RELAT_1:30;
    hence a = b by A1,A9,A8,Def4;
  end;
  then P \ R is_antisymmetric_in field(P \ R) by Def4;
  hence thesis by Def12;
end;

theorem
  R is transitive implies R~ is transitive
proof
  assume R is transitive;
  then
A1: R is_transitive_in field R by Def16;
  now
    let x,y,z;
    assume that
A2: x in field(R~) & y in field(R~) and
A3: z in field(R~) and
A4: [x,y] in R~ and
A5: [y,z] in R~;
A6: x in field R & y in field R by A2,RELAT_1:38;
A7: [y,x] in R by A4,RELAT_1:def 7;
    z in field R & [z,y] in R by A3,A5,RELAT_1:38,def 7;
    then [z,x] in R by A1,A6,A7,Def8;
    hence [x,z] in R~ by RELAT_1:def 7;
  end;
  then R~ is_transitive_in field(R~) by Def8;
  hence thesis by Def16;
end;

theorem
  P is transitive & R is transitive implies P /\ R is transitive
proof
  assume that
A1: P is transitive and
A2: R is transitive;
A3: R is_transitive_in field R by A2,Def16;
A4: P is_transitive_in field P by A1,Def16;
  now
    let a,b,c;
    assume that
    a in field(P /\ R) and
    b in field(P /\ R) and
    c in field(P /\ R) and
A5: [a,b] in P /\ R and
A6: [b,c] in P /\ R;
A7: [b,c] in R by A6,XBOOLE_0:def 4;
    then
A8: c in field R by RELAT_1:30;
A9: [a,b] in R by A5,XBOOLE_0:def 4;
    then a in field R & b in field R by RELAT_1:30;
    then
A10: [a,c] in R by A3,A9,A7,A8,Def8;
A11: [b,c] in P by A6,XBOOLE_0:def 4;
    then
A12: c in field P by RELAT_1:30;
A13: [a,b] in P by A5,XBOOLE_0:def 4;
    then a in field P & b in field P by RELAT_1:30;
    then [a,c] in P by A4,A13,A11,A12,Def8;
    hence [a,c] in P /\ R by A10,XBOOLE_0:def 4;
  end;
  then P /\ R is_transitive_in field (P /\ R) by Def8;
  hence thesis by Def16;
end;

theorem
  R is transitive iff R*R c= R
proof
A1: now
    assume R is transitive;
    then
A2: R is_transitive_in field R by Def16;
    now
      let a,b;
      assume [a,b] in R*R;
      then consider c such that
A3:   [a,c] in R and
A4:   [c,b] in R by RELAT_1:def 8;
A5:   c in field R by A3,RELAT_1:30;
      a in field R & b in field R by A3,A4,RELAT_1:30;
      hence [a,b] in R by A2,A3,A4,A5,Def8;
    end;
    hence R*R c= R by RELAT_1:def 3;
  end;
  now
    assume
A6: R*R c= R;
    now
      let a,b,c;
      assume that
      a in field R and
      b in field R and
      c in field R and
A7:   [a,b] in R & [b,c] in R;
      [a,c] in R*R by A7,RELAT_1:def 8;
      hence [a,c] in R by A6;
    end;
    then R is_transitive_in field R by Def8;
    hence R is transitive by Def16;
  end;
  hence thesis by A1;
end;

theorem
  R is connected iff [:field R,field R:] \ id (field R) c= R \/ R~
proof
A1: now
    assume R is connected;
    then
A2: R is_connected_in field R by Def14;
    now
      let x;
      now
        assume
A3:     x in [:field R, field R:] \ id (field R);
        then x in [:field R, field R:] by XBOOLE_0:def 5;
        then consider y,z such that
A4:     y in field R and
A5:     z in field R and
A6:     x = [y,z] by ZFMISC_1:def 2;
        not x in id (field R) by A3,XBOOLE_0:def 5;
        then y <> z by A4,A6,RELAT_1:def 10;
        then [y,z] in R or [z,y] in R by A2,A4,A5,Def6;
        then [y,z] in R or [y,z] in R~ by RELAT_1:def 7;
        hence x in R \/ R~ by A6,XBOOLE_0:def 3;
      end;
      hence x in [:field R, field R:] \ id (field R) implies x in R \/ R~;
    end;
    hence [:field R, field R:] \ id (field R) c= R \/ R~ by TARSKI:def 3;
  end;
  now
    assume
A7: [:field R, field R:] \ id (field R) c= R \/ R~;
    now
      let a,b;
      [a,b] in [:field R, field R:] \ id (field R) implies [a,b] in R \/
      R~ by A7;
      then
      [a,b] in [:field R, field R:] & not [a,b] in id (field R) implies [
      a,b] in R \/ R~ by XBOOLE_0:def 5;
      then
A8:   a in field R & b in field R & a <> b implies [a,b] in R or [a,b] in
      R~ by RELAT_1:def 10,XBOOLE_0:def 3 ,ZFMISC_1:106;
      assume a in field R & b in field R & a <> b;
      hence [a,b] in R or [b,a] in R by A8,RELAT_1:def 7;
    end;
    then R is_connected_in field R by Def6;
    hence R is connected by Def14;
  end;
  hence thesis by A1;
end;

theorem
  R is strongly_connected implies R is connected reflexive
proof
  assume R is strongly_connected;
  then
A1: R is_strongly_connected_in field R by Def15;
  then
  for x,y holds x in field R & y in field R & x <> y implies ( [x,y] in R
  or [y,x] in R) by Def7;
  then R is_connected_in field R by Def6;
  hence R is connected by Def14;
  thus R is reflexive
  proof
    let x;
    assume x in field R;
    hence thesis by A1,Def7;
  end;
end;

theorem
  R is strongly_connected iff [:field R, field R:] = R \/ R~
proof
A1: now
    assume R is strongly_connected;
    then
A2: R is_strongly_connected_in field R by Def15;
    now
      let x;
A3:   now
        assume
A4:     x in R \/ R~;
        then consider y,z such that
A5:     x = [y,z] by RELAT_1:def 1;
        [y,z] in R or [y,z] in R~ by A4,A5,XBOOLE_0:def 3;
        then [y,z] in R or [z,y] in R by RELAT_1:def 7;
        then y in field R & z in field R by RELAT_1:30;
        hence x in [:field R, field R:] by A5,ZFMISC_1:106;
      end;
      now
        assume x in [:field R, field R:];
        then consider y,z such that
A6:     y in field R & z in field R and
A7:     x = [y,z] by ZFMISC_1:def 2;
        [y,z] in R or [z,y] in R by A2,A6,Def7;
        then [y,z] in R or [y,z] in R~ by RELAT_1:def 7;
        hence x in R \/ R~ by A7,XBOOLE_0:def 3;
      end;
      hence x in [:field R, field R:] iff x in R \/ R~ by A3;
    end;
    hence [:field R, field R:] = R \/ R~ by TARSKI:2;
  end;
  now
    assume
A8: [:field R, field R:] = R \/ R~;
    now
      let a,b;
      a in field R & b in field R implies [a,b] in R \/ R~ by A8,
      ZFMISC_1:106;
      then a in field R & b in field R implies [a,b] in R or [a,b] in R~ by
      XBOOLE_0:def 3;
      hence a in field R & b in field R implies [a,b] in R or [b,a] in R by
      RELAT_1:def 7;
    end;
    then R is_strongly_connected_in field R by Def7;
    hence R is strongly_connected by Def15;
  end;
  hence thesis by A1;
end;

theorem
  R is transitive iff for x,y,z st [x,y] in R & [y,z] in R holds [x,z] in R
proof
  hereby
    assume R is transitive;
    then
A1: R is_transitive_in field R by Def16;
    let x,y,z;
    assume that
A2: [x,y] in R and
A3: [y,z] in R;
A4: z in field R by A3,RELAT_1:30;
    x in field R & y in field R by A2,RELAT_1:30;
    hence [x,z] in R by A1,A2,A3,A4,Def8;
  end;
  assume for x,y,z st [x,y] in R & [y,z] in R holds [x,z] in R;
  then x in field R & y in field R & z in field R & [x,y] in R & [y,z] in R
  implies [x,z] in R;
  then R is_transitive_in field R by Def8;
  hence thesis by Def16;
end;
