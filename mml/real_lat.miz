:: The Lattice of Real Numbers. The Lattice of Real Functions
::  by Marek Chmur
::
:: Received May 22, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies REAL_1, BINOP_1, NUMBERS, FUNCT_1, XXREAL_0, SUBSET_1, LATTICES,
      XREAL_0, XBOOLE_0, EQREL_1, FUNCT_2, RELAT_1, QC_LANG1, REAL_LAT;
 notations XBOOLE_0, SUBSET_1, NUMBERS, XXREAL_0, XREAL_0, REAL_1, SQUARE_1,
      STRUCT_0, LATTICES, BINOP_1, FUNCSDOM, RELAT_1, FUNCT_2;
 constructors SQUARE_1, LATTICES, FUNCSDOM, RELSET_1;
 registrations XBOOLE_0, RELSET_1, NUMBERS, XREAL_0, LATTICES;
 requirements SUBSET, BOOLE;
 definitions LATTICES;
 theorems LATTICES, BINOP_1, FUNCT_2, FUNCOP_1, XREAL_0, XXREAL_0;
 schemes BINOP_1;

begin :: LATTICE of REAL NUMBERS

reserve x,y for Real;

definition
  func minreal-> BinOp of REAL means
  :Def1:
  it.(x,y)=min(x,y);
  existence
  proof
    ex o being BinOp of REAL st for a,b being Element of REAL holds o.(a,b
    ) = min(a,b) from BINOP_1:sch 4;
    hence thesis;
  end;
  uniqueness
  proof
    let f1,f2 be BinOp of REAL;
    assume that
A1: f1.(x,y)=min(x,y) and
A2: f2.(x,y)=min(x,y);
    for x,y being Element of REAL holds f1.(x,y)=f2.(x,y)
    proof
      let x,y be Element of REAL;
      f1.(x,y)=min(x,y) by A1;
      hence thesis by A2;
    end;
    hence thesis by BINOP_1:2;
  end;
  func maxreal-> BinOp of REAL means
  :Def2:
  it.(x,y)=max(x,y);
  existence
  proof
    ex o being BinOp of REAL st for a,b being Element of REAL holds o.(a,
    b) = max(a,b) from BINOP_1:sch 4;
    hence thesis;
  end;
  uniqueness
  proof
    let f1,f2 be BinOp of REAL;
    assume that
A3: f1.(x,y)=max(x,y) and
A4: f2.(x,y)=max(x,y);
    for x,y being Element of REAL holds f1.(x,y)=f2.(x,y)
    proof
      let x,y be Element of REAL;
      f1.(x,y)=max(x,y) by A3;
      hence thesis by A4;
    end;
    hence thesis by BINOP_1:2;
  end;
end;

definition
  canceled;
  func Real_Lattice -> strict LattStr equals
  LattStr (# REAL, maxreal, minreal
  #);
  coherence;
end;

registration
  cluster -> real Element of Real_Lattice;
  coherence by XREAL_0:def 1;
end;

registration
  cluster Real_Lattice -> non empty;
  coherence;
end;

reserve a,b,c for Element of Real_Lattice;

registration
  let a,b be Element of Real_Lattice;
  identify a "\/" b with max(a,b);
  compatibility by Def2;
  identify a "/\" b with min(a,b);
  compatibility by Def1;
end;

Lm1: a"\/"b = b"\/"a;

Lm2: a"\/"(b"\/"c) = (a"\/"b)"\/"c by XXREAL_0:34;

Lm3: (a"/\"b)"\/"b = b by XXREAL_0:36;

Lm4: a"/\"b = b"/\"a;

Lm5: a"/\"(b"/\"c) = (a"/\"b)"/\"c by XXREAL_0:33;

Lm6: a"/\"(a"\/"b) = a by XXREAL_0:35;

registration
  cluster Real_Lattice -> Lattice-like;
  coherence
  proof
    Real_Lattice is join-commutative join-associative meet-absorbing
meet-commutative meet-associative join-absorbing by Lm1,Lm2,Lm3,Lm4,Lm5,Lm6,
    LATTICES:def 4,def 5,def 6,def 7,def 8,def 9;
    hence thesis;
  end;
end;

Lm7: a"/\"(b"\/"c) = (a"/\"b)"\/"(a"/\"c) by XXREAL_0:38;

reserve p,q,r for Element of Real_Lattice;

canceled 7;

theorem Th8:
  maxreal.(p,q) = maxreal.(q,p)
proof
  thus maxreal.(p,q) = q"\/"p by LATTICES:def 1
    .= maxreal.(q,p);
end;

theorem Th9:
  minreal.(p,q) = minreal.(q,p)
proof
  thus minreal.(p,q) = q"/\"p by LATTICES:def 2
    .= minreal.(q,p);
end;

theorem Th10:
  maxreal.(p,(maxreal.(q,r)))=maxreal.((maxreal.(q,r)),p) &
maxreal.(p,(maxreal.(q,r)))=maxreal.((maxreal.(p,q)),r) & maxreal.(p,(maxreal.(
  q,r)))=maxreal.((maxreal.(q,p)),r) & maxreal.(p,(maxreal.(q,r)))=maxreal.((
  maxreal.(r,p)),q) & maxreal.(p,(maxreal.(q,r)))=maxreal.((maxreal.(r,q)),p) &
  maxreal.(p,(maxreal.(q,r)))=maxreal.((maxreal.(p,r)),q)
proof
  set s=q"\/"r;
  thus
A1: maxreal.(p,(maxreal.(q,r))) = s"\/"p by LATTICES:def 1
    .= maxreal.((maxreal.(q,r)),p);
  thus maxreal.(p,(maxreal.(q,r))) = p"\/"s .= (p"\/"q)"\/"r by XXREAL_0:34
    .= maxreal.(maxreal.(p,q),r);
  thus maxreal.(p,(maxreal.(q,r))) = p"\/"s .= (q"\/"p)"\/"r by XXREAL_0:34
    .= maxreal.(maxreal.(q,p),r);
  thus
A2: maxreal.(p,(maxreal.(q,r))) = p"\/"(q"\/"r) .= (r"\/"p)"\/"q by XXREAL_0:34
    .= maxreal.(maxreal.(r,p),q);
  thus maxreal.(p,(maxreal.(q,r)))=maxreal.((maxreal.(r,q)),p) by A1,Th8;
  thus thesis by A2,Th8;
end;

theorem Th11:
  minreal.(p,(minreal.(q,r)))=minreal.((minreal.(q,r)),p) &
minreal.(p,(minreal.(q,r)))=minreal.((minreal.(p,q)),r) & minreal.(p,(minreal.(
  q,r)))=minreal.((minreal.(q,p)),r) & minreal.(p,(minreal.(q,r)))=minreal.((
  minreal.(r,p)),q) & minreal.(p,(minreal.(q,r)))=minreal.((minreal.(r,q)),p) &
  minreal.(p,(minreal.(q,r)))=minreal.((minreal.(p,r)),q)
proof
  set s=q"/\"r;
  thus
A1: minreal.(p,(minreal.(q,r))) = s"/\"p by LATTICES:def 2
    .= minreal.((minreal.(q,r)),p);
  thus minreal.(p,(minreal.(q,r))) = p"/\"s .= (p"/\"q)"/\"r by XXREAL_0:33
    .= minreal.(minreal.(p,q),r);
  thus minreal.(p,(minreal.(q,r))) = p"/\"s .= (q"/\"p)"/\"r by XXREAL_0:33
    .= minreal.(minreal.(q,p),r);
  thus
A2: minreal.(p,(minreal.(q,r))) = p"/\"(q"/\"r) .= (r"/\"p)"/\"q by XXREAL_0:33
    .= minreal.(minreal.(r,p),q);
  thus minreal.(p,(minreal.(q,r)))=minreal.((minreal.(r,q)),p) by A1,Th9;
  thus thesis by A2,Th9;
end;

theorem Th12:
  maxreal.(minreal.(p,q),q)=q & maxreal.(q,minreal.(p,q))=q &
  maxreal.(q,minreal.(q,p))=q & maxreal.(minreal.(q,p),q)=q
proof
  set s=p"/\"q;
  thus
A1: maxreal.(minreal.(p,q),q) =s"\/"q .=q by XXREAL_0:36;
  thus maxreal.(q,minreal.(p,q)) =(p"/\"q)"\/"q by LATTICES:def 1
    .=q by XXREAL_0:36;
  thus maxreal.(q,minreal.(q,p))=maxreal.(q,q"/\"p)
    .=(p"/\"q)"\/"q by LATTICES:def 1
    .=q by XXREAL_0:36;
  thus thesis by A1,Th9;
end;

theorem Th13:
  minreal.(q,maxreal.(q,p))=q & minreal.(maxreal.(p,q),q)=q &
  minreal.(q,maxreal.(p,q))=q & minreal.(maxreal.(q,p),q)=q
proof
  set s=q"\/"p;
  thus
A1: minreal.(q,maxreal.(q,p)) =q"/\"s .=q by XXREAL_0:35;
  thus
A2: minreal.(maxreal.(p,q),q)=minreal.(p"\/"q,q)
    .=q"/\"(q"\/"p) by LATTICES:def 2
    .=q by XXREAL_0:35;
  thus minreal.(q,maxreal.(p,q))=q by A1,Th8;
  thus thesis by A2,Th8;
end;

theorem Th14:
  minreal.(q,maxreal.(p,r))=maxreal.(minreal.(q,p),minreal.(q,r))
proof
  set s=p"\/"r;
  thus minreal.(q,maxreal.(p,r)) =q"/\"s .=(q"/\"p)"\/"(q"/\"r) by XXREAL_0:38
    .=maxreal.(minreal.(q,p),minreal.(q,r));
end;

registration
  cluster Real_Lattice -> distributive;
  coherence by Lm7,LATTICES:def 11;
end;

reserve A,B for non empty set;
reserve f,g,h for Element of Funcs(A,REAL);

definition
  let A;
  func maxfuncreal(A) -> BinOp of Funcs(A,REAL) means
  :Def5:
  it.(f,g) = maxreal.:(f,g);
  existence
  proof
    deffunc O(Element of Funcs(A,REAL),Element of Funcs(A,REAL)) = maxreal.:(
    $1,$2);
    ex o being BinOp of Funcs(A,REAL) st for a,b being Element of Funcs(A,
    REAL) holds o.(a,b) = O(a,b) from BINOP_1:sch 4;
    hence thesis;
  end;
  uniqueness
  proof
    let F1,F2 be BinOp of Funcs(A,REAL) such that
A1: for f,g being Element of Funcs(A,REAL) holds F1.(f,g) = maxreal.:( f,g) and
A2: for f,g being Element of Funcs(A,REAL) holds F2.(f,g) = maxreal.:( f,g);
    now
      let f,g be Element of Funcs(A,REAL);
      thus F1.(f,g) = maxreal.:(f,g) by A1
        .= F2.(f,g) by A2;
    end;
    hence thesis by BINOP_1:2;
  end;
  func minfuncreal(A) -> BinOp of Funcs(A,REAL) means
  :Def6:
  it.(f,g) = minreal.:(f,g);
  existence
  proof
    deffunc O(Element of Funcs(A,REAL),Element of Funcs(A,REAL)) = minreal.:(
    $1,$2);
    ex o being BinOp of Funcs(A,REAL) st for a,b being Element of Funcs(A,
    REAL) holds o.(a,b) = O(a,b) from BINOP_1:sch 4;
    hence thesis;
  end;
  uniqueness
  proof
    let F1,F2 be BinOp of Funcs(A,REAL) such that
A3: for f,g being Element of Funcs(A,REAL) holds F1.(f,g) = minreal.: (f,g) and
A4: for f,g being Element of Funcs(A,REAL) holds F2.(f,g) = minreal.: (f,g);
    now
      let f,g be Element of Funcs(A,REAL);
      thus F1.(f,g) = minreal.:(f,g) by A3
        .= F2.(f,g) by A4;
    end;
    hence thesis by BINOP_1:2;
  end;
end;

Lm8: for x being Element of A, f being Function of A,B holds x in dom f
proof
  let x be Element of A,f be Function of A,B;
  x in A;
  hence thesis by FUNCT_2:def 1;
end;

canceled 5;

theorem Th20:
  (maxfuncreal(A)).(f,g) = (maxfuncreal(A)).(g,f)
proof
  now
    let x be Element of A;
A1: x in dom (maxreal.:(f,g)) by Lm8;
A2: x in dom (maxreal.:(g,f)) by Lm8;
    thus ((maxfuncreal(A)).(f,g)).x = (maxreal.:(f,g)).x by Def5
      .= maxreal.(f.x,g.x) by A1,FUNCOP_1:28
      .= maxreal.(g.x,f.x) by Th8
      .= (maxreal.:(g,f)).x by A2,FUNCOP_1:28
      .= ((maxfuncreal(A)).(g,f)).x by Def5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th21:
  (minfuncreal(A)).(f,g) = (minfuncreal(A)).(g,f)
proof
  now
    let x be Element of A;
A1: x in dom (minreal.:(f,g)) by Lm8;
A2: x in dom (minreal.:(g,f)) by Lm8;
    thus ((minfuncreal(A)).(f,g)).x = (minreal.:(f,g)).x by Def6
      .= minreal.(f.x,g.x) by A1,FUNCOP_1:28
      .= minreal.(g.x,f.x) by Th9
      .= (minreal.:(g,f)).x by A2,FUNCOP_1:28
      .= ((minfuncreal(A)).(g,f)).x by Def6;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th22:
  (maxfuncreal(A)).((maxfuncreal(A)).(f,g),h) =(maxfuncreal(A)).(f
  ,(maxfuncreal(A)).(g,h))
proof
  now
    let x be Element of A;
A1: x in dom (maxreal.:(f,g)) by Lm8;
A2: x in dom (maxreal.:(g,h)) by Lm8;
A3: x in dom (maxreal.:((maxreal.:(f,g)),h)) by Lm8;
A4: x in dom (maxreal.:(f,(maxreal.:(g,h)))) by Lm8;
    thus ((maxfuncreal(A)).((maxfuncreal(A)).(f,g),h)).x =((maxfuncreal(A)).(
    maxreal.:(f,g),h)).x by Def5
      .=(maxreal.:(maxreal.:(f,g),h)).x by Def5
      .=maxreal.((maxreal.:(f,g)).x,h.x) by A3,FUNCOP_1:28
      .=maxreal.(maxreal.(f.x,g.x),h.x) by A1,FUNCOP_1:28
      .=maxreal.(f.x,maxreal.(g.x,h.x)) by Th10
      .=maxreal.(f.x,((maxreal.:(g,h)).x)) by A2,FUNCOP_1:28
      .=(maxreal.:(f,(maxreal.:(g,h)))).x by A4,FUNCOP_1:28
      .=((maxfuncreal(A)).(f,(maxreal.:(g,h)))).x by Def5
      .=((maxfuncreal(A)).(f,((maxfuncreal(A)).(g,h)))).x by Def5;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th23:
  (minfuncreal(A)).((minfuncreal(A)).(f,g),h) =(minfuncreal(A)).(f
  ,(minfuncreal(A)).(g,h))
proof
  now
    let x be Element of A;
A1: x in dom (minreal.:(f,g)) by Lm8;
A2: x in dom (minreal.:(g,h)) by Lm8;
A3: x in dom (minreal.:((minreal.:(f,g)),h)) by Lm8;
A4: x in dom (minreal.:(f,(minreal.:(g,h)))) by Lm8;
    thus ((minfuncreal(A)).((minfuncreal(A)).(f,g),h)).x =((minfuncreal(A)).(
    minreal.:(f,g),h)).x by Def6
      .=(minreal.:(minreal.:(f,g),h)).x by Def6
      .=minreal.((minreal.:(f,g)).x,h.x) by A3,FUNCOP_1:28
      .=minreal.(minreal.(f.x,g.x),h.x) by A1,FUNCOP_1:28
      .=minreal.(f.x,minreal.(g.x,h.x)) by Th11
      .=minreal.(f.x,((minreal.:(g,h)).x)) by A2,FUNCOP_1:28
      .=(minreal.:(f,(minreal.:(g,h)))).x by A4,FUNCOP_1:28
      .=((minfuncreal(A)).(f,(minreal.:(g,h)))).x by Def6
      .=((minfuncreal(A)).(f,((minfuncreal(A)).(g,h)))).x by Def6;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th24:
  (maxfuncreal(A)).(f,(minfuncreal(A)).(f,g))=f
proof
  now
    let x be Element of A;
A1: x in dom (minreal.:(f,g)) by Lm8;
A2: x in dom (maxreal.:(f,(minreal.:(f,g)))) by Lm8;
    thus (maxfuncreal(A)).(f,(minfuncreal(A)).(f,g)).x =((maxfuncreal(A)).(f,
    minreal.:(f,g))).x by Def6
      .=(maxreal.:(f,minreal.:(f,g))).x by Def5
      .=maxreal.(f.x,(minreal.:(f,g)).x) by A2,FUNCOP_1:28
      .=maxreal.(f.x,(minreal.(f.x,g.x))) by A1,FUNCOP_1:28
      .=f.x by Th12;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th25:
  (maxfuncreal(A)).((minfuncreal(A)).(f,g),f)=f
proof
  thus (maxfuncreal(A)).((minfuncreal(A)).(f,g),f) =(maxfuncreal(A)).(f,(
  minfuncreal(A)).(f,g)) by Th20
    .=f by Th24;
end;

theorem Th26:
  (maxfuncreal(A)).((minfuncreal(A)).(g,f),f)=f
proof
  thus (maxfuncreal(A)).((minfuncreal(A)).(g,f),f) =(maxfuncreal(A)).((
  minfuncreal(A)).(f,g),f) by Th21
    .=f by Th25;
end;

theorem
  (maxfuncreal(A)).(f,(minfuncreal(A)).(g,f))=f
proof
  thus (maxfuncreal(A)).(f,(minfuncreal(A)).(g,f)) =(maxfuncreal(A)).(f,(
  minfuncreal(A)).(f,g)) by Th21
    .=f by Th24;
end;

theorem Th28:
  (minfuncreal(A)).(f,(maxfuncreal(A)).(f,g))=f
proof
  now
    let x be Element of A;
A1: x in dom (maxreal.:(f,g)) by Lm8;
A2: x in dom (minreal.:(f,(maxreal.:(f,g)))) by Lm8;
    thus (minfuncreal(A)).(f,(maxfuncreal(A)).(f,g)).x =((minfuncreal(A)).(f,
    maxreal.:(f,g))).x by Def5
      .=(minreal.:(f,maxreal.:(f,g))).x by Def6
      .=minreal.(f.x,(maxreal.:(f,g)).x) by A2,FUNCOP_1:28
      .=minreal.(f.x,(maxreal.(f.x,g.x))) by A1,FUNCOP_1:28
      .=f.x by Th13;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem Th29:
  (minfuncreal(A)).(f,(maxfuncreal(A)).(g,f))=f
proof
  thus (minfuncreal(A)).(f,(maxfuncreal(A)).(g,f)) =(minfuncreal(A)).(f,(
  maxfuncreal(A)).(f,g)) by Th20
    .=f by Th28;
end;

theorem Th30:
  (minfuncreal(A)).((maxfuncreal(A)).(g,f),f)=f
proof
  thus (minfuncreal(A)).((maxfuncreal(A)).(g,f),f) =(minfuncreal(A)).(f,(
  maxfuncreal(A)).(g,f)) by Th21
    .=f by Th29;
end;

theorem
  (minfuncreal(A)).((maxfuncreal(A)).(f,g),f)=f
proof
  thus (minfuncreal(A)).((maxfuncreal(A)).(f,g),f) =(minfuncreal(A)).((
  maxfuncreal(A)).(g,f),f) by Th20
    .=f by Th30;
end;

theorem Th32:
  (minfuncreal(A)).(f,(maxfuncreal(A)).(g,h)) = (maxfuncreal(A)).(
  (minfuncreal(A)).(f,g),(minfuncreal(A)).(f,h))
proof
  now
    let x be Element of A;
A1: x in dom (minreal.:(f,g)) by Lm8;
A2: x in dom (minreal.:(f,h)) by Lm8;
A3: x in dom (minreal.:(f,(maxreal.:(g,h)))) by Lm8;
A4: x in dom (maxreal.:(minreal.:(f,g),minreal.:(f,h))) by Lm8;
A5: x in dom (maxreal.:(g,h)) by Lm8;
    thus (minfuncreal(A)).(f,(maxfuncreal(A)).(g,h)).x =((minfuncreal(A)).(f,
    maxreal.:(g,h))).x by Def5
      .=(minreal.:(f,maxreal.:(g,h))).x by Def6
      .=minreal.(f.x,(maxreal.:(g,h)).x) by A3,FUNCOP_1:28
      .=minreal.(f.x,(maxreal.(g.x,h.x))) by A5,FUNCOP_1:28
      .=maxreal.(minreal.(f.x,g.x),minreal.(f.x,h.x)) by Th14
      .=maxreal.(minreal.:(f,g).x,minreal.(f.x,h.x)) by A1,FUNCOP_1:28
      .=maxreal.(minreal.:(f,g).x,minreal.:(f,h).x) by A2,FUNCOP_1:28
      .=maxreal.:(minreal.:(f,g),minreal.:(f,h)).x by A4,FUNCOP_1:28
      .=(maxfuncreal(A)).(minreal.:(f,g),minreal.:(f,h)).x by Def5
      .=(maxfuncreal(A)).((minfuncreal(A)).(f,g),minreal.:(f,h)).x by Def6
      .=(maxfuncreal(A)).((minfuncreal(A)).(f,g),(minfuncreal(A)).(f,h)).x
    by Def6;
  end;
  hence thesis by FUNCT_2:113;
end;

reserve p,q,r for Element of LattStr(# Funcs(A,REAL), maxfuncreal(A),
    minfuncreal(A) #);

definition
  let A;
  let x be Element of LattStr(# Funcs(A,REAL), maxfuncreal(A), minfuncreal(A)
  #);
  canceled 2;
  func @x->Element of Funcs(A,REAL) equals
  x;
  coherence;
end;

Lm9: for a,b,c being Element of LattStr(# Funcs(A,REAL), maxfuncreal(A),
  minfuncreal(A) #) holds a"\/"(b"\/"c) = (a"\/"b)"\/"c by Th22;

Lm10: for a,b,c being Element of LattStr(# Funcs(A,REAL), maxfuncreal(A),
  minfuncreal(A) #) holds a"/\"(b"/\"c) = (a"/\"b)"/\"c by Th23;

definition
  let A;
  func RealFunc_Lattice(A) -> strict Lattice equals
  LattStr (# Funcs(A,REAL),
    maxfuncreal(A), minfuncreal(A) #);
  coherence
  proof
    set L =LattStr (# Funcs(A,REAL), maxfuncreal(A), minfuncreal(A) #);
    for p,q,r holds p"\/"(q"\/"r) = (p"\/"q)"\/"r by Th22;
    then
A1: L is join-associative by LATTICES:def 5;
    for p,q holds (p"/\"q)"\/"q = q by Th26;
    then
A2: L is meet-absorbing by LATTICES:def 8;
    for p,q holds p"/\"(p"\/"q) = p by Th28;
    then
A3: L is join-absorbing by LATTICES:def 9;
    for p,q,r holds p"/\"(q"/\"r) = (p"/\"q)"/\"r by Th23;
    then
A4: L is meet-associative by LATTICES:def 7;
    for p,q holds p"/\"q = q"/\"p by Th21;
    then
A5: L is meet-commutative by LATTICES:def 6;
    for p,q holds p"\/"q = q"\/"p by Th20;
    then L is join-commutative by LATTICES:def 4;
    hence thesis by A1,A2,A5,A4,A3;
  end;
end;

reserve p,q,r for Element of RealFunc_Lattice(A);

canceled 7;

theorem Th40:
  (maxfuncreal(A)).(p,q) = (maxfuncreal(A)).(q,p)
proof
  thus (maxfuncreal(A)).(p,q) = q"\/"p by LATTICES:def 1
    .= (maxfuncreal(A)).(q,p);
end;

theorem Th41:
  (minfuncreal(A)).(p,q) = (minfuncreal(A)).(q,p)
proof
  thus (minfuncreal(A)).(p,q) = q"/\"p by LATTICES:def 2
    .= (minfuncreal(A)).(q,p);
end;

theorem
  (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) =(maxfuncreal(A)).(((
  maxfuncreal(A)).(q,r)),p) & (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) =(
  maxfuncreal(A)).(((maxfuncreal(A)).(p,q)),r) & (maxfuncreal(A)).(p,((
  maxfuncreal(A)).(q,r))) =(maxfuncreal(A)).(((maxfuncreal(A)).(q,p)),r) & (
maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) =(maxfuncreal(A)).(((maxfuncreal(A
)).(r,p)),q) & (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) =(maxfuncreal(A)).
(((maxfuncreal(A)).(r,q)),p) & (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) =(
  maxfuncreal(A)).(((maxfuncreal(A)).(p,r)),q)
proof
  set s=q"\/"r;
  thus
A1: (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) = s"\/"p by LATTICES:
  def 1
    .= (maxfuncreal(A)).(((maxfuncreal(A)).(q,r)),p);
  thus (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) = (maxfuncreal(A)).((
  maxfuncreal(A)).(p,q),r) by Th22;
  thus (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) = p"\/"s
    .= (q"\/"p)"\/"r by Lm9
    .= (maxfuncreal(A)).((maxfuncreal(A)).(q,p),r);
  thus
A2: (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r))) = p"\/"(q"\/"r)
    .= (r"\/"p)"\/"q by Lm9
    .= (maxfuncreal(A)).((maxfuncreal(A)).(r,p),q);
  thus (maxfuncreal(A)).(p,((maxfuncreal(A)).(q,r)))= (maxfuncreal(A)).(((
  maxfuncreal(A)).(r,q)),p) by A1,Th40;
  thus thesis by A2,Th40;
end;

theorem
  (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) =(minfuncreal(A)).(((
  minfuncreal(A)).(q,r)),p) & (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) =(
  minfuncreal(A)).(((minfuncreal(A)).(p,q)),r) & (minfuncreal(A)).(p,((
  minfuncreal(A)).(q,r))) =(minfuncreal(A)).(((minfuncreal(A)).(q,p)),r) & (
minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) =(minfuncreal(A)).(((minfuncreal(A
)).(r,p)),q) & (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) =(minfuncreal(A)).
(((minfuncreal(A)).(r,q)),p) & (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) =(
  minfuncreal(A)).(((minfuncreal(A)).(p,r)),q)
proof
  set s=q"/\"r;
  thus
A1: (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) = s"/\"p by LATTICES:
  def 2
    .= (minfuncreal(A)).(((minfuncreal(A)).(q,r)),p);
  thus (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) = (minfuncreal(A)).((
  minfuncreal(A)).(p,q),r) by Th23;
  thus (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) = p"/\"s
    .= (q"/\"p)"/\"r by Lm10
    .= (minfuncreal(A)).((minfuncreal(A)).(q,p),r);
  thus
A2: (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) = p"/\"(q"/\"r)
    .= (r"/\"p)"/\"q by Lm10
    .= (minfuncreal(A)).((minfuncreal(A)).(r,p),q);
  thus (minfuncreal(A)).(p,((minfuncreal(A)).(q,r))) =(minfuncreal(A)).(((
  minfuncreal(A)).(r,q)),p) by A1,Th41;
  thus thesis by A2,Th41;
end;

theorem
  (maxfuncreal(A)).((minfuncreal(A)).(p,q),q)=q & (maxfuncreal(A)).(q,(
  minfuncreal(A)).(p,q))=q & (maxfuncreal(A)).(q,(minfuncreal(A)).(q,p))=q & (
  maxfuncreal(A)).((minfuncreal(A)).(q,p),q)=q
proof
  thus
A1: (maxfuncreal(A)).((minfuncreal(A)).(p,q),q) =q by Th26;
  thus (maxfuncreal(A)).(q,(minfuncreal(A)).(p,q)) =(p"/\"q)"\/"q by LATTICES:
  def 1
    .=q by Th26;
  thus (maxfuncreal(A)).(q,(minfuncreal(A)).(q,p)) =(maxfuncreal(A)).(q,q"/\"p
  )
    .=(p"/\"q)"\/"q by LATTICES:def 1
    .=q by Th26;
  thus thesis by A1,Th41;
end;

theorem
  (minfuncreal(A)).(q,(maxfuncreal(A)).(q,p))=q & (minfuncreal(A)).((
maxfuncreal(A)).(p,q),q)=q & (minfuncreal(A)).(q,(maxfuncreal(A)).(p,q))=q & (
  minfuncreal(A)).((maxfuncreal(A)).(q,p),q)=q
proof
  thus
A1: (minfuncreal(A)).(q,(maxfuncreal(A)).(q,p)) =q by Th28;
  thus
A2: (minfuncreal(A)).((maxfuncreal(A)).(p,q),q) =(minfuncreal(A)).(p "\/"q,q)
    .=q"/\"(q"\/"p) by LATTICES:def 2
    .=q by Th28;
  thus (minfuncreal(A)).(q,(maxfuncreal(A)).(p,q))=q by A1,Th40;
  thus thesis by A2,Th40;
end;

theorem
  (minfuncreal(A)).(q,(maxfuncreal(A)).(p,r)) =(maxfuncreal(A)).((
  minfuncreal(A)).(q,p),(minfuncreal(A)).(q,r)) by Th32;

theorem
  RealFunc_Lattice(A) is D_Lattice
proof
  p"/\"(q"\/"r) = (p"/\"q)"\/"(p"/\"r) by Th32;
  hence thesis by LATTICES:def 11;
end;
