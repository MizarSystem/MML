:: Combinatorial {G}rassmannians
::  by Andrzej Owsiejczuk
::
:: Received April 16, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies TARSKI, BOOLE, RELAT_1, INCSP_1, COMBGRAS, WELLORD2, MOD_4,
      FUNCT_1, FUNCT_2, ORDINAL1, ANPROJ_2, FINSET_1, ARYTM_1, CARD_1, SGRAPH1,
      MEASURE6, SETFAM_1, COMPLEX1, AFF_2, INCPROJ;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, ENUMSET1, ZFMISC_1, SETFAM_1,
      DOMAIN_1, FINSET_1, RELAT_1, RELSET_1, WELLORD2, NUMBERS, INCSP_1,
      XXREAL_0, REAL_1, FUNCT_1, FUNCT_2, INCPROJ, NAT_1, CARD_1;
 constructors SETFAM_1, WELLORD2, DOMAIN_1, XXREAL_0, XREAL_0, REAL_1, NAT_1,
      BINOP_2, CARD_1, INCPROJ;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCT_2,
      ARYTM_3, FINSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, CARD_1, INCSP_1,
      NECKLACE;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, INCSP_1, INCPROJ, FUNCT_1, FUNCT_2, RELAT_1;
 theorems ENUMSET1, TARSKI, ZFMISC_1, RELSET_1, XBOOLE_0, XBOOLE_1, XREAL_1,
      SETFAM_1, PENCIL_1, INCSP_1, WELLORD2, CARD_1, CARD_2, NAT_1, CARD_4,
      ORDINAL1, CARD_FIL, STIRL2_1, CARD_FIN, FINSET_1, XXREAL_0, INCPROJ,
      REAL_1, FUNCT_1, FUNCT_2, RELAT_1;
 schemes FUNCT_1, NAT_1, CLASSES1;

begin :: Preliminaries

 reserve S for IncProjStr,
         A,B,C,D for POINT of S,
         l,L for LINE of S,
         k,n,i,j for Element of NAT,
         a,b,c,x,y,z,X,Y,Z for set;

theorem Th1:
  for a,b being set st a <> b & Card a = n & Card b = n
  holds Card (a /\ b) <` n & n + 1 <=` Card (a \/ b)
  proof
    let a,b be set;
    assume
A1:   a <> b & Card a = n & Card b = n &
      (not Card (a /\ b) <` n or not n + 1 <=` Card (a \/ b)); then
    n <=` Card (a /\ b) or Card (a \/ b) <` n + 1 by ORDINAL1:26;then
    n <=` Card (a /\ b) or Card (a \/ b) in succ n by CARD_1:52;then
A2: n <=` Card (a /\ b) or Card (a \/ b) c= n by ORDINAL1:34;
A3: a c= a \/ b & b c= a \/ b & a /\ b c= a & a /\ b c= b
          by XBOOLE_1:7,XBOOLE_1:17;then
    n <=` Card(a \/ b) & Card(a /\ b) <=` n by A1,CARD_1:27;then
A4: (Card a = Card (a /\ b) & Card (a /\ b) = Card b) or
    (Card (a \/ b) = Card a & Card (a \/ b) = Card b)
          by A1,A2,XBOOLE_0:def 10;
    n = Card n by CARD_1:66;then
A5: a is finite set & b is finite set by A1,CARD_4:4;then
    (a \/ b) is finite set & (a /\ b) is finite set
           by A3,FINSET_1:13,FINSET_1:14;then
    (a = a /\ b & b = a /\ b) or (a = a \/ b & b = a \/ b)
            by A3,A4,A5,CARD_FIN:1;
    hence contradiction by A1;
  end;

theorem Th2:
  for a,b being set st Card a = n + k & Card b = n + k
  holds Card(a /\ b) = n iff Card(a \/ b) = n + 2*k
  proof
    let a,b be set;
    assume
A1:   Card a = n + k & Card b = n + k;
    Card(n + k) = n + k by CARD_1:66;then
A2: a is finite & b is finite by A1,CARD_4:4;
    thus Card(a /\ b) = n implies Card(a \/ b) = n + 2*k
    proof
      assume Card(a /\ b) = n;then
      Card(a \/ b) = n + k + (n + k) - n by A1,A2,CARD_2:64;
      hence thesis;
    end;
    thus Card (a \/ b) = n + 2*k implies Card (a /\ b) = n
    proof
      assume
A3:      Card(a \/ b) = n + 2*k;
      a /\ b c= b by XBOOLE_1:17;then
      a /\ b is finite by A2,FINSET_1:13;then
      consider m being Element of NAT such that
A4:     Card m = Card (a /\ b) by CARD_4:4;
      m = Card (a /\ b) by A4,CARD_1:66;then
      n + 2*k = n + k + (n + k) - m by A1,A2,A3,CARD_2:64;
      hence thesis by A4,CARD_1:66;
    end;
    thus thesis;
  end;

theorem Th3:
  Card X <=` Card Y iff
     ex f being Function st f is one-to-one & X c= dom f & f.:X c= Y
  proof
    thus Card X <=` Card Y implies
      ex f being Function st f is one-to-one & X c= dom f & f.:X c= Y
    proof assume Card X <=` Card Y;
      then consider f being Function such that
A1:     f is one-to-one & dom f = X & rng f c= Y by CARD_1:26;
      take f;
      thus thesis by A1,RELAT_1:146;
    end;
    given f being Function such that
A2:  f is one-to-one & X c= dom f & f.:X c= Y;
A3:f|X is one-to-one by A2,FUNCT_1:84;
A4:dom(f|X) = X by A2,RELAT_1:91;
    rng(f|X) c= Y
    proof let y; assume y in rng(f|X);
      then consider x such that
A5:     x in dom(f|X) & y = (f|X).x by FUNCT_1:def 5;
A6:  x in X by A2,A5,RELAT_1:91;then
      x in dom f & y = f.x by A2,A5,FUNCT_1:70;then
      y in f.:X by A6,FUNCT_1:def 12;
      hence thesis by A2;
    end;
    hence thesis by A3,A4,CARD_1:26;
  end;

theorem Th4:
  for f being Function st f is one-to-one & X c= dom f holds
    Card(f.:X) = Card X
  proof
    let f be Function such that
A1: f is one-to-one & X c= dom f;
A2: Card(f.:X) <=` Card X by CARD_2:3;
    Card X <=` Card(f.:X) by A1,Th3;
    hence thesis by A2,XBOOLE_0:def 10;
  end;

theorem Th5:
  X \ Y = X \ Z & Y c= X & Z c= X implies Y = Z
  proof
    assume
A1: X \ Y = X \ Z & Y c= X & Z c= X;then
A2: X \ Y misses Z & Y \/ X = X & X \ Z misses Y & Z \/ X = X
      by XBOOLE_1:12,XBOOLE_1:106;then
A3: X misses Z \ Y & X misses Y \ Z & Z c= Y \/ X & Y c= X \/ Z
      by A1,XBOOLE_1:81;
    Z \ Y c= X & Y \ Z c= X by A2,A1,XBOOLE_1:43;then
    Z \ Y = {} & Y \ Z = {} by A3,XBOOLE_1:67;then
    Z c= Y & Y c= Z by XBOOLE_1:37;
    hence thesis by XBOOLE_0:def 10;
  end;

theorem Th6:
  for Y being non empty set
  for p being Function of X,Y st p is one-to-one
  for x1,x2 being Subset of X holds
  x1 <> x2 implies p.:x1 <> p.:x2
  proof
    let Y be non empty set;
    let p be Function of X,Y such that
A1: p is one-to-one;
    let x1 be Subset of X;
    let x2 be Subset of X;
    assume
A2: x1 <> x2;
A3: X = dom p by FUNCT_2:def 1;
A4:not x1 c= x2 implies p.:x1 <> p.:x2
    proof
      assume not x1 c= x2;then
      consider a being set such that
A5:  a in x1 & not a in x2 by TARSKI:def 3;
      not p.a in p.:x2
      proof
        assume p.a in p.:x2;then
        consider b being set such that
A6:    b in dom p & b in x2 & p.a = p.b by FUNCT_1:def 12;
        thus contradiction by A1,A3,A5,A6,FUNCT_1:def 8;
      end;
      hence thesis by A3,A5,FUNCT_1:def 12;
    end;
    not x2 c= x1 implies p.:x1 <> p.:x2
    proof
      assume not x2 c= x1;then
      consider a being set such that
A7:    a in x2 & not a in x1 by TARSKI:def 3;
      not p.a in p.:x1
      proof
        assume p.a in p.:x1;then
        consider b being set such that
A8:    b in dom p & b in x1 & p.a = p.b by FUNCT_1:def 12;
        thus contradiction by A1,A3,A7,A8,FUNCT_1:def 8;
      end;
      hence thesis by A3,A7,FUNCT_1:def 12;
    end;
    hence thesis by A2,A4,XBOOLE_0:def 10;
  end;

theorem Th7:
  for a,b,c being set st Card a = n - 1 & Card b = n - 1 & Card c = n - 1 &
  Card(a /\ b) = n - 2 & Card(a /\ c) = n - 2 & Card(b /\ c) = n - 2 & 2 <= n
  holds
    (3 <= n implies (Card(a /\ b /\ c) = n - 2 & Card(a \/ b \/ c) = n + 1) or
    (Card(a /\ b /\ c) = n - 3 & Card(a \/ b \/ c) = n)) &
      (n = 2 implies (Card(a /\ b /\ c) = n - 2 & Card(a \/ b \/ c) = n + 1))
  proof
    let a,b,c be set;
    assume
A1:  Card a = n - 1 & Card b = n - 1 & Card c = n - 1 & Card(a /\ b) = n - 2 &
          Card(a /\ c) = n - 2 & Card(b /\ c) = n - 2 & 2 <= n;then
    2 <= n + 1 & 1 <= 1 by NAT_1:13;then
    2 - 1 <= (n + 1) - 1 by REAL_1:92;then
A2: n - 2 is Element of NAT & n - 1 is Element of NAT by A1,NAT_1:21;then
    Card(n - 2) = n - 2 & Card(n - 1) = n - 1 by CARD_1:66;then
A3:a /\ b c= a & a /\ b c= b & a /\ c c= c & a /\ c c= a & a is finite &
     b is finite & c is finite & a /\ b is finite & a /\ c is finite
         & b /\ c is finite by A1,A2,XBOOLE_1:17,CARD_4:4;then
A4: Card(a \ (a /\ b)) = (n - 1) - (n - 2) &
       Card(c \ (a /\ c)) = (n - 1) - (n - 2)&
      Card(b \ (a /\ b)) = (n - 1) - (n - 2) by A1,CARD_2:63;then
    consider x1 being set such that
A5:    {x1} = a \ (a /\ b) by CARD_2:60;
    consider x2 being set such that
A6:    {x2} = b \ (a /\ b) by A4,CARD_2:60;
    consider x3 being set such that
A7:   {x3} = c \ (a /\ c) by A4,CARD_2:60;
A8: a = (a /\ b) \/ {x1} & b = (a /\ b) \/ {x2} &
      c = (a /\ c) \/ {x3} by A5,A6,A7,A3,XBOOLE_1:45;
A9:x1 in {x1} & x2 in {x2} & x3 in {x3} by TARSKI:def 1;then
A10: x1 in a & not x1 in a /\ b & x2 in b & not x2 in a /\ b &
      x3 in c & not x3 in a /\ c by A5,A6,A7,XBOOLE_0:def 4;then
A11: not x1 in b & not x2 in a & x1 <> x2 & not x3 in a & x1 <> x3
          by XBOOLE_0:def 3;
A12: a /\ b /\ c = a /\ c /\ b & a /\ b /\ c = b /\ c /\ a &
      a /\ c /\ b c= a /\ c & b /\ c /\ a c= b /\ c
             by XBOOLE_1:16,XBOOLE_1:17;
A13: n = 2 implies (Card(a /\ b /\ c) = n - 2 & Card(a \/ b \/ c) = n + 1)
    proof
      assume
A14:   n = 2;then
A15:   a /\ b = {} & a /\ b /\ c c= a /\ b & a /\ b c= {}
        by A1,CARD_2:59,XBOOLE_1:17;
      Card(a \/ b \/ c) = n + 1
      proof
A16:     a /\ b /\ c = a /\ c & a /\ b /\ c = b /\ c & a /\ b /\ c = a /\ b
           & x3 in {x3} by A1,A3,A15,A12,CARD_FIN:1,TARSKI:def 1;then
A17:     not x3 in a & not x3 in b & x1 <> x3 & x2 <> x3 by A10,XBOOLE_0:def 3;
        a \/ b \/ c = (a /\ b /\ c) \/ ({x1,x2} \/ {x3})
                by A5,A6,A15,ENUMSET1:41,A7,A16;then
A18:    a \/ b \/ c = (a /\ b /\ c) \/ {x1,x2,x3} by ENUMSET1:43;
        thus thesis by A17,A11,CARD_2:77,A15,A14,A18;
      end;
      hence thesis by A15,CARD_1:78,A14;
    end;
    3 <= n implies (Card(a /\ b /\ c) = n - 2 & Card(a \/ b \/ c) = n + 1) or
       (Card(a /\ b /\ c) = n - 3 & Card(a \/ b \/ c) = n)
    proof
      assume 3 <= n;then
A19:   n - 3 is Element of NAT by NAT_1:21;
A20:  x1 in c implies (Card(a /\ b /\ c) = n - 3 & Card(a \/ b \/ c) = n)
      proof
        assume
          x1 in c;then
        x1 in a /\ c by A9,A5,XBOOLE_0:def 3;then
        {x1} c= a /\ c & Card {x1} = 1 by CARD_1:79,ZFMISC_1:37;then
A21:    Card((a /\ c) \ {x1}) = (n - 2) - 1 &
           Card(a \ (a /\ c)) = (n - 1) - (n - 2) by A1,A3,CARD_2:63;
A22:     (a /\ c) \ {x1} c= b
        proof
          let z be set;
          assume z in (a /\ c) \ {x1};then
          z in a /\ c & not z in {x1} by XBOOLE_0:def 4;then
          z in a & not z in a \ (a /\ b) & not z in a or z in (a /\ b)
                by A5,XBOOLE_0:def 3,XBOOLE_0:def 4;
          hence thesis by XBOOLE_0:def 3;
        end;
        a /\ b /\ c misses {x1}
        proof
          assume a /\ b /\ c meets {x1};then
A23:      not a /\ b /\ c /\ {x1} = {} by XBOOLE_0:def 7;
          consider x being set such that
A24:          x in a /\ b /\ c /\ {x1} by A23,XBOOLE_0:def 1;
A25:       x in a /\ b /\ c & x in {x1} by A24,XBOOLE_0:def 3;then
          x in a /\ c /\ b & x = x1 by TARSKI:def 1,XBOOLE_1:16;
          hence contradiction by A25,XBOOLE_0:def 3,A10;
        end;then
A26:    a /\ b /\ c c= (a /\ c) \ {x1} by A12,XBOOLE_1:86;
        (a /\ c) \ {x1} c= a /\ c /\ b by A22,XBOOLE_1:19;then
A27:     (a /\ c) \ {x1} c= a /\ b /\ c by XBOOLE_1:16;then
A28:    Card(a /\ b /\ c) = n - 3 by A21,A26,XBOOLE_0:def 10;
A29:     x3 = x2
        proof
          assume
A30:      x2 <> x3;
          b /\ c c= a /\ b /\ c
          proof
            let z1 be set;
            assume z1 in b /\ c;then
            z1 in b & z1 in c by XBOOLE_0:def 3;then
            (z1 in a /\ b or z1 in {x2}) & (z1 in a /\ c or z1 in {x3})
               by A8,XBOOLE_0:def 2;then
            (z1 in a /\ b or z1 = x2) & (z1 in a /\ c or z1 = x3)
              by TARSKI:def 1;then
            (z1 in a /\ b or z1 in {x2}) & z1 in a /\ c or
              z1 in a /\ b & (z1 in a /\ c or z1 in {x3}) by A30,TARSKI:def 1;
            hence thesis by A12,XBOOLE_0:def 3,A6,A7,A3;
          end;then
          Card(b /\ c) c= Card(a /\ b /\ c) by CARD_1:27;then
          - 2 + n <= - 3 + n by A1,A2,A28,A19,CARD_1:56;
          hence contradiction by XREAL_1:8;
        end;
A31:    c c= a \/ b & a \/ b = (a /\ b) \/ ({x1} \/ {x2})
               by A6,A29,A3,A8,XBOOLE_1:5,XBOOLE_1:13;then
A32:    a \/ b \/ c = a \/ b & a \/ b = (a /\ b) \/ {x1,x2} & Card{x1,x2} = 2
                  by A11,ENUMSET1:41,XBOOLE_1:12,CARD_2:76;
        a /\ b misses {x1,x2}
        proof
          assume a /\ b meets {x1,x2};then
          (a /\ b) /\ {x1,x2} <> {} by XBOOLE_0:def 7;then
          consider z1 being set such that
A33:         z1 in (a /\ b) /\ {x1,x2} by XBOOLE_0:def 1;
          z1 in a /\ b & z1 in {x1,x2} by A33,XBOOLE_0:def 3;
          hence contradiction by A10,TARSKI:def 2;
        end;then
        Card(a \/ b) = (n - 2) + 2 by A1,A3,A32,CARD_2:53;
        hence thesis by A27,A21,A26,XBOOLE_0:def 10,A31,XBOOLE_1:12;
      end;
      not x1 in c implies
          (Card(a /\ b /\ c) = n - 2 & Card(a \/ b \/ c) = n + 1)
      proof
        assume
A34:    not x1 in c;
        a /\ c misses {x1} & a /\ b misses {x1}
        proof
          assume not a /\ c misses {x1} or not a /\ b misses {x1};then
          (a /\ c) /\ {x1} <> {} or (a /\ b) /\ {x1} <> {}
             by XBOOLE_0:def 7;then
          consider z2 being set such that
A35:      z2 in (a /\ c) /\ {x1} or z2 in (a /\ b) /\ {x1} by XBOOLE_0:def 1;
          z2 in a /\ c & z2 in {x1} or z2 in a /\ b & z2 in {x1}
             by A35,XBOOLE_0:def 3;then
          z2 in a & z2 in c & z2 = x1 or z2 in a /\ b & z2 = x1
             by TARSKI:def 1,XBOOLE_0:def 3;
          hence contradiction by A5,XBOOLE_0:def 4,A9,A34;
        end;then
        a /\ c c= a \ {x1} & a /\ b c= a \ {x1} & a \ {x1} c= a & {x1} c= a &
           Card(a \ {x1}) = (n - 1) - 1 & a \ {x1} is finite
          by A1,A4,A5,A3,CARD_2:63,FINSET_1:13,XBOOLE_1:86;then
A36:    a /\ c = a \ {x1} & a /\ b = a \ {x1} by A1,A3,CARD_FIN:1;then
        (a /\ b) /\ (a /\ c) = a /\ b;then
        b /\ a /\ a /\ c = a /\ b by XBOOLE_1:16;then
A37:    b /\ (a /\ a) /\ c = a /\ b by XBOOLE_1:16;then
A38:    a /\ b /\ c = b /\ c by A1,A3,A12,CARD_FIN:1;
        a \/ b = (a /\ b) \/ ({x1} \/ {x2}) by A8,XBOOLE_1:5;then
        a \/ b = (a /\ b) \/ {x1,x2} by ENUMSET1:41;then
        a \/ b \/ c = (a /\ b) \/ ({x1,x2} \/ {x3}) by A8,A36,XBOOLE_1:5;then
A39:    a \/ b \/ c = (a /\ b) \/ {x1,x2,x3} by ENUMSET1:43;
        x1 <> x3 & x2 <> x3 by A10,A37,A38,XBOOLE_0:def 3;then
A40:    Card{x1,x2,x3} = 3 by A11,CARD_2:77;
        a /\ b misses {x1,x2,x3}
        proof
          assume not a /\ b misses {x1,x2,x3};then
          (a /\ b) /\ {x1,x2,x3} <> {} by XBOOLE_0:def 7;then
          consider z3 being set such that
A41:         z3 in (a /\ b) /\ {x1,x2,x3} by XBOOLE_0:def 1;
          z3 in a /\ b & z3 in {x1,x2,x3} by A41,XBOOLE_0:def 3;
          hence contradiction by A10,A36,ENUMSET1:def 1;
        end;then
        Card(a \/ b \/ c) = (n - 2) + 3 by A1,A40,A3,A39,CARD_2:53;
        hence thesis by A1,A37;
      end;
      hence thesis by A20;
    end;
    hence thesis by A13;
  end;

theorem Th8:
  for P1,P2 being IncProjStr st the IncProjStr of P1 = the IncProjStr of P2
  for A1 being POINT of P1, A2 being POINT of P2 st A1 = A2
  for L1 being LINE of P1, L2 being LINE of P2 st L1 = L2
  holds A1 on L1 implies A2 on L2
  proof
    let P1,P2 be IncProjStr;
    assume
A1:   the IncProjStr of P1 = the IncProjStr of P2;
    let A1 be POINT of P1, A2 be POINT of P2;
    assume
A2:   A1 = A2;
    let L1 be LINE of P1, L2 be LINE of P2;
    assume L1 = L2 & A1 on L1;then
    [A2,L2] in the Inc of P2 by A1,A2,INCSP_1:def 1;
    hence thesis by INCSP_1:def 1;
  end;

theorem Th9:
  for P1,P2 being IncProjStr st the IncProjStr of P1 = the IncProjStr of P2
  for A1 being Subset of the Points of P1
  for A2 being Subset of the Points of P2 st A1 = A2
  for L1 being LINE of P1, L2 being LINE of P2 st L1 = L2
  holds A1 on L1 implies A2 on L2
  proof
    let P1,P2 be IncProjStr;
    assume
A1:   the IncProjStr of P1 = the IncProjStr of P2;
    let A1 be Subset of the Points of P1, A2 be Subset of the Points of P2;
    assume
A2:   A1 = A2;
    let L1 be LINE of P1, L2 be LINE of P2;
    assume
A3:   L1 = L2 & A1 on L1;
    thus A2 on L2
    proof
      let A be POINT of P2;
      assume
A4:   A in A2;
      consider B being POINT of P1 such that
A5:   A = B by A1;
      B on L1 by A2,A4,A5,A3,INCSP_1:def 4;then
      [A,L2] in the Inc of P2 by A1,A3,A5,INCSP_1:def 1;
      hence thesis by INCSP_1:def 1;
    end;
  end;

registration
  cluster with_non-trivial_lines linear up-2-rank strict IncProjStr;
  existence
  proof consider P being IncSpace-like strict IncStruct;
    take IT = IncProjStr(#the Points of P, the Lines of P, the Inc of P#);
    thus for L being LINE of IT
      ex A,B being POINT of IT st A <> B & {A,B} on L
    proof
      let L be LINE of IT;
      reconsider L' = L as LINE of P;
      consider A',B' being POINT of P such that
A1:   A' <> B' & {A',B'} on L' by INCSP_1:def 8;
      reconsider A = A', B = B' as POINT of IT;
      take A,B; thus thesis by A1,Th9;
    end;
    thus IT is linear
    proof
      let A,B be POINT of IT;
      reconsider A' = A, B' = B as POINT of P;
      consider L' being LINE of P such that
A2:   {A',B'} on L' by INCSP_1:def 9;
      reconsider L = L' as LINE of IT;
      take L;
      A' on L' & B' on L' by A2,INCSP_1:11;
      hence thesis by Th8;
    end;
    thus for A,B being POINT of IT, K,L being LINE of IT
        st A <> B & {A,B} on K & {A,B} on L holds K = L
    proof
      let A,B be POINT of IT, K,L be LINE of IT;
      assume
A3:     A <> B & {A,B} on K & {A,B} on L;
      reconsider A' = A, B' = B as POINT of P;
      reconsider K' = K, L' = L as LINE of P;
      A' <> B' & {A',B'} on K' & {A',B'} on L' by A3,Th9;
      hence thesis by INCSP_1:def 10;
    end;
    thus thesis;
  end;
end;

begin :: Configuration G

definition
  mode PartialLinearSpace is with_non-trivial_lines up-2-rank IncProjStr;
end;

 reserve P for PartialLinearSpace;

definition
  let k be Element of NAT;
  let X be non empty set such that
A1:  0 < k & k + 1 <=` Card X;
  func G_(k,X) -> strict PartialLinearSpace means:Def1:
    the Points of it = {A where A is Subset of X: Card A = k} &
    the Lines of it = {L where L is Subset of X: Card L = k + 1} &
    the Inc of it = (RelIncl bool X) /\ [:the Points of it, the Lines of it:];
  existence
  proof
    set P = {A where A is Subset of X: Card A = k};
    set L = {B where B is Subset of X: Card B = k + 1};
    set I = (RelIncl bool X) /\ [:P,L:];
    k <= k + 1 by NAT_1:11;then
    k c= k + 1 & k + 1 c= Card X by A1,CARD_1:56;then
    k c= Card X by XBOOLE_1:1;then
    consider A being set such that
A2: A c= X & Card A = k by CARD_FIL:36;
    A in P by A2; then
    reconsider P as non empty set;
    consider B being set such that
A3: B c= X & Card B = k + 1 by A1,CARD_FIL:36;
    B in L by A3; then
    reconsider L as non empty set;
    I c= [:P,L:] by XBOOLE_1:17; then
    reconsider I as Relation of P,L by RELSET_1:def 1;
    set G = IncProjStr(#P,L,I#);
A4: G is with_non-trivial_lines
    proof
      let l being LINE of G;
      l in L; then
      consider C being Subset of X such that
A5:   l = C & Card C = k + 1;
      1 < k + 1 by A1,XREAL_1:31;then
      1 + 1 <= k + 1 by NAT_1:13;then
      2 c= k + 1 by CARD_1:56;then
      consider a,b being set such that
A6:   a in C & b in C & a <> b by A5,PENCIL_1:2;
A7:   C c= {a} \/ C & C c= {b} \/ C by XBOOLE_1:7;
      reconsider x = C \ {a},y = C \ {b} as Subset of X;
      Card x = k & Card y = k by A5,A6,STIRL2_1:65;then
      x in P & y in P; then
      reconsider x,y as POINT of G;
      take x,y; thus
      x <> y
      proof
A8:     b in {b} by TARSKI:def 1;
        not b in {a} by A6,TARSKI:def 1;then
        b in x & not b in y by A6,A8,XBOOLE_0:def 4;
        hence thesis;
      end;
      {x,y} on l
      proof
        let z be POINT of G;
        assume z in {x,y};then
        z = x or z = y by TARSKI:def 2;then
        z in bool X & z c= l by A5,A7,XBOOLE_1:43;then
        [z,l] in RelIncl bool X by A5,WELLORD2:def 1;then
        [z,l] in I by XBOOLE_0:def 3;
        hence z on l by INCSP_1:def 1;
      end;
      hence thesis;
    end;
    G is up-2-rank
    proof
      let a,b being POINT of G;
      a in P; then
      consider A being Subset of X such that
A9:  a = A & Card A = k;
      b in P; then
      consider B being Subset of X such that
A10:  b = B & Card B = k;
      let l1,l2 being LINE of G;
      l1 in L; then
      consider C being Subset of X such that
A11:  l1 = C & Card C = k + 1;
      l2 in L; then
      consider D being Subset of X such that
A12:  l2 = D & Card D = k + 1;
A13:  a in {a,b} & b in {a,b} by TARSKI:def 2;
      assume
A14:  a <> b & {a,b} on l1 & {a,b} on l2;then
      a on l1 & b on l1 & a on l2 & b on l2 by A13,INCSP_1:def 4;then
      [a,l1] in I & [b,l1] in I & [a,l2] in I & [b,l2] in I
           by INCSP_1:def 1;then
      [a,l1] in RelIncl bool X & [b,l1] in RelIncl bool X &
          [a,l2] in RelIncl bool X & [b,l2] in RelIncl bool X
               by XBOOLE_0:def 3;then
      a c= l1 & b c= l1 & a c= l2 & b c= l2
           by A9,A10,A11,A12,WELLORD2:def 1;then
A15:  a \/ b c= l1 & a \/ b c= l2 by XBOOLE_1:8; then
A16:  Card (a \/ b) <=` k + 1 by A12,CARD_1:27;
      k + 1 c= Card (a \/ b) by A9,A10,A14,Th1;then
A17:  Card (a \/ b) = k + 1 & Card (k + 1) = k + 1
            by A16,CARD_1:66,XBOOLE_0:def 10;then
      a \/ b is finite & l2 is finite & l1 is finite by A11,A12,CARD_4:4;then
      a \/ b = l1 & a \/ b = l2 by A11,A12,A15,A17,CARD_FIN:1;
      hence thesis;
    end;
    hence thesis by A4;
  end;
  uniqueness;
end;

theorem Th10:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X
  for A being POINT of G_(k,X)
  for L being LINE of G_(k,X) holds
  A on L iff A c= L
  proof
    let k be Element of NAT;
    let X be non empty set;
    assume
A1:   0 < k & k + 1 <=` Card X;then
A2: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by Def1;
A3: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
          by A1,Def1;
A4: the Inc of G_(k,X) = (RelIncl bool X) /\ [:the Points of G_(k,X),
            the Lines of G_(k,X):]
      by A1,Def1;
    let A be POINT of G_(k,X);
    let L be LINE of G_(k,X);
    A in the Points of G_(k,X);then
    consider A1 being Subset of X such that
A5:    A1 = A & Card A1 = k by A2;
    L in the Lines of G_(k,X);then
    consider L1 being Subset of X such that
A6:    L1 = L & Card L1 = k + 1 by A3;
    thus A on L implies A c= L
    proof
      assume A on L;then
      [A,L] in the Inc of G_(k,X) by INCSP_1:def 1;then
      [A,L] in RelIncl bool X by A4,XBOOLE_0:def 3;
      hence thesis by A5,A6,WELLORD2:def 1;
    end;
    thus A c= L implies A on L
    proof
      assume A c= L;then
      [A,L] in RelIncl bool X by A5,A6,WELLORD2:def 1;then
      [A,L] in the Inc of G_(k,X) by A4,XBOOLE_0:def 3;
      hence thesis by INCSP_1:def 1;
    end;
    thus thesis;
  end;

theorem Th11:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X holds
  G_(k,X) is Vebleian
  proof
    let k be Element of NAT;
    let X be non empty set;
    assume
A1:   0 < k & k + 1 <=` Card X;
    let A1,A2,A3,A4,A5,A6 be POINT of G_(k,X),L1,L2,L3,L4 be LINE of G_(k,X);
A2: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A1,Def1;
A3: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
          by A1,Def1;
    A1 in the Points of G_(k,X);then
    consider B1 being Subset of X such that
A4:    B1 = A1 & Card B1 = k by A2;
    A2 in the Points of G_(k,X);then
    consider B2 being Subset of X such that
A5:    B2 = A2 & Card B2 = k by A2;
    A3 in the Points of G_(k,X);then
    consider B3 being Subset of X such that
A6:    B3 = A3 & Card B3 = k by A2;
    A4 in the Points of G_(k,X);then
    consider B4 being Subset of X such that
A7:    B4 = A4 & Card B4 = k by A2;
    A5 in the Points of G_(k,X);then
    consider B5 being Subset of X such that
A8:   B5 = A5 & Card B5 = k by A2;
    L1 in the Lines of G_(k,X);then
    consider K1 being Subset of X such that
A9:   K1 = L1 & Card K1 = k + 1 by A3;
    L2 in the Lines of G_(k,X);then
    consider K2 being Subset of X such that
A10:   K2 = L2 & Card K2 = k + 1 by A3;
    L3 in the Lines of G_(k,X);then
    consider K3 being Subset of X such that
A11:   K3 = L3 & Card K3 = k + 1 by A3;
    L4 in the Lines of G_(k,X);then
    consider K4 being Subset of X such that
A12:   K4 = L4 & Card K4 = k + 1 by A3;
    assume
A13:  A1 on L1 & A2 on L1 & A3 on L2 & A4 on L2 & A5 on L1 & A5 on L2 &
        A1 on L3 & A3 on L3 & A2 on L4 & A4 on L4 & not A5 on L3 &
            not A5 on L4 & L1 <> L2;then
A14:A1 c= L1 & A2 c= L1 & A3 c= L2 & A4 c= L2 & A5 c= L1 & A5 c= L2 &
      A1 c= L3 & A3 c= L3 & A2 c= L4 & A4 c= L4 & not A5 c= L3 & not A5 c= L4
            by A1,Th10;then
    A5 c= L1 /\ L2 by XBOOLE_1:19;then
A15:k <=` Card (L1 /\ L2) by A8,CARD_1:27;
    Card (L1 /\ L2) <` k + 1 by A9,A10,A13,Th1;then
    Card (L1 /\ L2) in succ k by CARD_1:52;then
    Card (L1 /\ L2) c= k by ORDINAL1:34;then
    Card (L1 /\ L2) = k by A15,XBOOLE_0:def 10;then
A16:Card (L1 \/ L2) = k + 2*1 by A9,A10,Th2;
A17:A1 \/ A3 c= L3 & A2 \/ A4 c= L4 by A14,XBOOLE_1:8;then
A18:Card (A1 \/ A3) <=` k + 1 & Card (A2 \/ A4) <=` k + 1
         by A11,A12,CARD_1:27;
    A1 <> A3 & A2 <> A4
    proof
      assume A1 = A3 or A2 = A4;then
      ({A1,A5} on L1 & {A1,A5} on L2) or
        ({A2,A5} on L1 & {A2,A5} on L2) by A13,INCSP_1:11;
      hence contradiction by A13,INCSP_1:def 10;
    end;then
    k + 1 <=` Card (A1 \/ A3) & k + 1 <=` Card (A2 \/ A4)
           by A4,A5,A6,A7,Th1;then
A19:Card (A1 \/ A3) = k + 1 & Card (A2 \/ A4) = k + 1 &
       Card (k + 1) = k + 1 by A18,CARD_1:66,XBOOLE_0:def 10;then
    A1 \/ A3 is finite & L3 is finite & A2 \/ A4 is finite
       & L4 is finite by A11,A12,CARD_4:4;then
A20:A1 \/ A3 = L3 & A2 \/ A4 = L4 by A11,A12,A17,A19,CARD_FIN:1;
    L3 c= L1 \/ L2 & L4 c= L1 \/ L2 by A14,A20,XBOOLE_1:13;then
    L3 c= L3 \/ L4 & L3 \/ L4 c= L1 \/ L2 by XBOOLE_1:7,XBOOLE_1:8;then
A21:Card (L3 \/ L4) <=` k + 2 & k + 1 <=` Card (L3 \/ L4)
         by A11,A16,CARD_1:27;
    k <=` Card (L3 /\ L4)
    proof
      Card (L3 \/ L4) in succ (k + 2) by A21,ORDINAL1:34;then
      Card (L3 \/ L4) in (k + 1 + 1) or Card (L3 \/ L4) = k + 2
              by ORDINAL1:13;then
      Card (L3 \/ L4) in succ (k + 1) or Card (L3 \/ L4) = k + 2
             by CARD_1:52;then
      Card (L3 \/ L4) c= k + 1 or Card (L3 \/ L4) = k + 2 by ORDINAL1:34;then
      Card (L3 \/ L4) = k + 1 + 2*0 or Card (L3 \/ L4) = k + 2*1
              by A21,XBOOLE_0:def 10;then
      (k + 1 c= Card (L3 /\ L4) or k c= Card (L3 /\ L4)) & k <= k + 1
             by A11,A12,Th2,NAT_1:11;then
      (k + 1 c= Card (L3 /\ L4) or k c= Card (L3 /\ L4)) & k c= k + 1
               by CARD_1:56;
      hence thesis by XBOOLE_1:1;
    end;then
    consider B6 being set such that
A22:   B6 c= L3 /\ L4 & Card B6 = k by CARD_FIL:36;
    take B6;
A23:L3 /\ L4 c= L3 & L3 /\ L4 c= L4 & L3 c= X by A11,XBOOLE_1:17;then
    L3 /\ L4 c= X by XBOOLE_1:1;then
    reconsider A6 = B6 as Subset of X by A22,XBOOLE_1:1;
    A6 in the Points of G_(k,X) by A2,A22;then
    reconsider A6 as POINT of G_(k,X);
    A6 c= B6 & B6 c= L3 & B6 c= L4 by A22,A23,XBOOLE_1:1;
    hence thesis by A1,Th10;
  end;

theorem Th12:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X holds
  for A1,A2,A3,A4,A5,A6 being POINT of G_(k,X)
  for L1,L2,L3,L4 being LINE of G_(k,X) st
  A1 on L1 & A2 on L1 & A3 on L2 & A4 on L2 & A5 on L1 & A5 on L2 & A1 on L3 &
     A3 on L3 & A2 on L4 & A4 on L4 & not A5 on L3 & not A5 on L4 & L1 <> L2 &
        L3 <> L4 holds
  ex A6 being POINT of G_(k,X) st A6 on L3 & A6 on L4 &
        A6 = (A1 /\ A2) \/ (A3 /\ A4)
  proof
    let k be Element of NAT;
    let X be non empty set;
    assume
A1:   0 < k & k + 1 <=` Card X;
    let A1,A2,A3,A4,A5,A6 be POINT of G_(k,X),L1,L2,L3,L4 be LINE of G_(k,X);
A2: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A1,Def1;
A3: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
           by A1,Def1;
    A1 in the Points of G_(k,X);then
    consider B1 being Subset of X such that
A4:    B1 = A1 & Card B1 = k by A2;
    A2 in the Points of G_(k,X);then
    consider B2 being Subset of X such that
A5:    B2 = A2 & Card B2 = k by A2;
    A3 in the Points of G_(k,X);then
    consider B3 being Subset of X such that
A6:    B3 = A3 & Card B3 = k by A2;
    A4 in the Points of G_(k,X);then
    consider B4 being Subset of X such that
A7:    B4 = A4 & Card B4 = k by A2;
    A5 in the Points of G_(k,X);then
    consider B5 being Subset of X such that
A8:   B5 = A5 & Card B5 = k by A2;
    L1 in the Lines of G_(k,X);then
    consider K1 being Subset of X such that
A9:   K1 = L1 & Card K1 = k + 1 by A3;
    L2 in the Lines of G_(k,X);then
    consider K2 being Subset of X such that
A10:   K2 = L2 & Card K2 = k + 1 by A3;
    L3 in the Lines of G_(k,X);then
    consider K3 being Subset of X such that
A11:   K3 = L3 & Card K3 = k + 1 by A3;
    L4 in the Lines of G_(k,X);then
    consider K4 being Subset of X such that
A12:   K4 = L4 & Card K4 = k + 1 by A3;
    assume
A13:   A1 on L1 & A2 on L1 & A3 on L2 & A4 on L2 & A5 on L1 & A5 on L2 &
       A1 on L3 & A3 on L3 & A2 on L4 & A4 on L4 & not A5 on L3 &
           not A5 on L4 & L1 <> L2 & L3 <> L4;then
A14: A1 c= L1 & A2 c= L1 & A3 c= L2 & A4 c= L2 & A5 c= L1 & A5 c= L2 &
      A1 c= L3 & A3 c= L3 & A2 c= L4 & A4 c= L4 & not A5 c= L3 & not A5 c= L4
             by A1,Th10;
    G_(k,X) is Vebleian by A1,Th11;then
    consider A6 being POINT of G_(k,X) such that
A15:  A6 on L3 & A6 on L4 by A13,INCPROJ:def 13;
    A6 in the Points of G_(k,X);then
    consider a6 being Subset of X such that
A16:   a6 = A6 & Card a6 = k by A2;
A17: A6 c= L3 & A6 c= L4 & k - 1 is Element of NAT by A1,A15,Th10,NAT_1:20;then
A18: k + 1 = Card (k + 1) & k = Card k & k - 1 = Card (k - 1) by CARD_1:66;then
A19: L1 is finite & L2 is finite & L3 is finite & L4 is finite & A6 is finite &
      A1 is finite & A2 is finite & A3 is finite & A4 is finite
          by A11,A12,A16,A4,A5,A6,A7,A9,A10,CARD_4:4;
A20: L3 /\ L4 = A6
    proof
      L3 /\ L4 c= L3 by XBOOLE_1:17;then
A21:   A6 c= L3 /\ L4 & L3 /\ L4 is finite
             by A17,A19,FINSET_1:13,XBOOLE_1:19;then
A22:   k c= Card (L3 /\ L4) by A16,CARD_1:27;
      Card (L3 /\ L4) <` k + 1 by A13,A11,A12,Th1;then
      Card (L3 /\ L4) in succ k by CARD_1:52;then
      Card (L3 /\ L4) c= k by ORDINAL1:34;then
      Card (L3 /\ L4) = k by A22,XBOOLE_0:def 10;
      hence thesis by A16,A21,A19,CARD_FIN:1;
    end;
A23: A1 /\ A2 c= A6 & A3 /\ A4 c= A6 by A20,A14,XBOOLE_1:27;then
A24: (A1 /\ A2) \/ (A3 /\ A4) c= A6 by XBOOLE_1:8;then
A25: (A1 /\ A2) \/ (A3 /\ A4) is finite & A1 /\ A2 c= (A1 /\ A2) \/ (A3 /\ A4)
       & A3 /\ A4 c= (A1 /\ A2) \/ (A3 /\ A4) by A19,FINSET_1:13,XBOOLE_1:7;
A26: A6 on L1 implies A6 = (A1 /\ A2) \/ (A3 /\ A4)
    proof
      assume
A27:     A6 on L1;
A28:   A2 = A6
      proof
        assume
A29:        A2 <> A6;
        {A2,A6} on L1 & {A2,A6} on L4 by A13,A15,A27,INCSP_1:11;
        hence contradiction by A13,A29,INCSP_1:def 10;
      end;
      A1 = A6
      proof
        assume
A30:        A1 <> A6;
        {A1,A6} on L1 & {A1,A6} on L3 by A13,A15,A27,INCSP_1:11;
        hence contradiction by A13,A30,INCSP_1:def 10;
      end;
      hence thesis by A23,A28,XBOOLE_1:12;
    end;
    not A6 on L1 implies A6 = (A1 /\ A2) \/ (A3 /\ A4)
    proof
      assume
A31:     not A6 on L1;
A32:   A6 on L2 implies A6 = (A1 /\ A2) \/ (A3 /\ A4)
      proof
        assume
A33:       A6 on L2;
A34:     A3 = A6
        proof
          assume
A35:         A3 <> A6;
          {A3,A6} on L2 & {A3,A6} on L3 by A13,A15,A33,INCSP_1:11;
          hence contradiction by A13,A35,INCSP_1:def 10;
        end;
        A4 = A6
        proof
          assume
A36:          A4 <> A6;
          {A4,A6} on L2 & {A4,A6} on L4 by A13,A15,A33,INCSP_1:11;
          hence contradiction by A13,A36,INCSP_1:def 10;
        end;
        hence thesis by A23,A34,XBOOLE_1:12;
      end;
      not A6 on L2 implies A6 = (A1 /\ A2) \/ (A3 /\ A4)
      proof
        assume
A37:       not A6 on L2;
        Card ((A1 /\ A2) \/ (A3 /\ A4)) = k
        proof
A38:      A1 <> A2 & A3 <> A4
          proof
            assume A1 = A2 or A3 = A4;then
            ({A1,A6} on L3 & {A1,A6} on L4) or
              ({A3,A6} on L3 & {A3,A6} on L4) by A13,A15,INCSP_1:11;
            hence contradiction by A37,A13,A31,INCSP_1:def 10;
          end;
A39:      A1 \/ A2 c= L1 & A3 \/ A4 c= L2 by A14,XBOOLE_1:8;then
          k + 1 c= Card(A1 \/ A2) & Card(A1 \/ A2) c= k + 1 &
             k + 1 c= Card(A3 \/ A4) & Card(A3 \/ A4) c= k + 1
              by A4,A5,A6,A7,A9,A10,A38,Th1,CARD_1:27;then
A40:       Card (A3 \/ A4) = (k - 1) + 2*1 & Card A3 = (k - 1) + 1 &
          Card A4 = (k - 1) + 1 &
            Card (A1 \/ A2) = (k - 1) + 2*1 & Card A1 = (k - 1) + 1 &
             Card A2 = (k - 1) + 1 by A4,A5,A6,A7,XBOOLE_0:def 10;then
A41:       Card (A1 /\ A2) = k - 1 & Card (A3 /\ A4) = k - 1 by A17,Th2;
          A1 /\ A2 c= (A1 /\ A2) \/ (A3 /\ A4) by XBOOLE_1:7;then
A42:       Card ((A1 /\ A2) \/ (A3 /\ A4)) c= k &
           k - 1 c= Card ((A1 /\ A2) \/ (A3 /\ A4)) by A24,A16,A41,CARD_1:27;
then
          Card ((A1 /\ A2) \/ (A3 /\ A4)) in succ k by ORDINAL1:34;then
          Card ((A1 /\ A2) \/ (A3 /\ A4)) in k or
            Card ((A1 /\ A2) \/ (A3 /\ A4)) = k by ORDINAL1:13;then
          Card ((A1 /\ A2) \/ (A3 /\ A4)) in succ (k - 1) or
            Card ((A1 /\ A2) \/ (A3 /\ A4)) = k by A17,A40,CARD_1:52;then
A43:       Card ((A1 /\ A2) \/ (A3 /\ A4)) c= k - 1 or
            Card ((A1 /\ A2) \/ (A3 /\ A4)) = k by A17,ORDINAL1:34;
          not Card ((A1 /\ A2) \/ (A3 /\ A4)) = k - 1
          proof
            assume
A44:           Card ((A1 /\ A2) \/ (A3 /\ A4)) = k - 1;then
            Card ((A1 /\ A2) \/ (A3 /\ A4)) = k - 1 + 2*0;then
A45:        Card ((A1 /\ A2) /\ (A3 /\ A4)) = k - 1 by A17,A41,Th2;then
A46:        (A1 /\ A2) /\ (A3 /\ A4) c= (A1 /\ A2) \/ (A3 /\ A4) &
              (A1 /\ A2) /\ (A3 /\ A4) is finite
                   by A17,A18,CARD_4:4,XBOOLE_1:29;then
            (A1 /\ A2) /\ (A3 /\ A4) c= (A1 /\ A2) \/ (A3 /\ A4) &
              A1 /\ A2 /\ A3 /\ A4 is finite &
               (A1 /\ A2) /\ (A3 /\ A4) is finite &
                A1 /\ A2 c= A1 & A1 /\ A2 c= A2 &
                 A3 /\ A4 c= A3 & A3 /\ A4 c= A4 &
                  (A1 /\ A2) /\ (A3 /\ A4) c= A1 /\ A2 &
                   (A1 /\ A2) /\ (A3 /\ A4) c= A3 /\ A4
                         by XBOOLE_1:16,XBOOLE_1:17;then
A47:        (A1 /\ A2) /\ (A3 /\ A4) = (A1 /\ A2) \/ (A3 /\ A4) &
             (A1 /\ A2) /\ (A3 /\ A4) c= A1 &
             (A1 /\ A2) /\ (A3 /\ A4) c= A2 & (A1 /\ A2) /\ (A3 /\ A4) c= A3 &
               (A1 /\ A2) /\ (A3 /\ A4) c= A4 &
                A1 /\ A2 /\ A3 /\ A4 = (A1 /\ A2) /\ (A3 /\ A4)
                  by A25,A44,A45,CARD_FIN:1,XBOOLE_1:1,XBOOLE_1:16;then
A48:         Card (A1 \ (A1 /\ A2 /\ A3 /\ A4)) = k - (k - 1) &
              Card (A2 \ (A1 /\ A2 /\ A3 /\ A4)) = k - (k - 1) &
              Card (A3 \ (A1 /\ A2 /\ A3 /\ A4)) = k - (k - 1) &
               Card (A4 \ (A1 /\ A2 /\ A3 /\ A4)) = k - (k - 1)
                  by A4,A5,A6,A7,A19,A46,CARD_2:63,A44;then
            consider x1 being set such that
A49:           A1 \ (A1 /\ A2 /\ A3 /\ A4) = {x1} by CARD_2:60;
            consider x2 being set such that
A50:           A2 \ (A1 /\ A2 /\ A3 /\ A4) = {x2} by A48,CARD_2:60;
            consider x3 being set such that
A51:          A3 \ (A1 /\ A2 /\ A3 /\ A4) = {x3} by A48,CARD_2:60;
            consider x4 being set such that
A52:          A4 \ (A1 /\ A2 /\ A3 /\ A4) = {x4} by A48,CARD_2:60;
A53:        A1 = (A1 /\ A2 /\ A3 /\ A4) \/ (A1 \ (A1 /\ A2 /\ A3 /\ A4)) &
             A2 = (A1 /\ A2 /\ A3 /\ A4) \/ (A2 \ (A1 /\ A2 /\ A3 /\ A4)) &
             A3 = (A1 /\ A2 /\ A3 /\ A4) \/ (A3 \ (A1 /\ A2 /\ A3 /\ A4)) &
              A4 = (A1 /\ A2 /\ A3 /\ A4) \/ (A4 \ (A1 /\ A2 /\ A3 /\ A4)) &
              x1 in A1 \ (A1 /\ A2 /\ A3 /\ A4) &
               x2 in A2 \ (A1 /\ A2 /\ A3 /\ A4) &
               x3 in A3 \ (A1 /\ A2 /\ A3 /\ A4) &
                x4 in A4 \ (A1 /\ A2 /\ A3 /\ A4)
                    by A49,A50,A51,A52,A47,XBOOLE_1:45,TARSKI:def 1;
A54:        x1 <> x3 & x1 <> x4 & x2 <> x3 & x2 <> x4
            proof
              assume x1 = x3 or x1 = x4 or x2 = x3 or x2 = x4;then
              ({A1,A5} on L1 & {A1,A5} on L2) or ({A1,A5} on L1 &
                {A1,A5} on L2) or ({A2,A5} on L1 & {A2,A5} on L2) or
                 ({A2,A5} on L1 & {A2,A5} on L2)
                    by A13,INCSP_1:11,A49,A50,A51,A52,A53;
              hence contradiction by A13,INCSP_1:def 10;
            end;
            k + 1 c= Card(A1 \/ A2) & k + 1 c= Card(A3 \/ A4) &
              Card(A1 \/ A2) c= k + 1 & Card(A3 \/ A4) c= k + 1
                 by A39,A4,A5,A6,A7,A9,A10,A38,CARD_1:27,Th1;then
            A1 \/ A2 is finite & A3 \/ A4 is finite &
             Card(A1 \/ A2) = k + 1 & Card(A3 \/ A4) = k + 1
                      by A19,FINSET_1:14,XBOOLE_0:def 10;then
            A1 \/ A2 = L1 & A3 \/ A4 = L2 by A39,A19,A9,A10,CARD_FIN:1;then
A55:         L1 = (A1 /\ A2 /\ A3 /\ A4) \/ ({x1} \/ {x2}) &
               L2 = (A1 /\ A2 /\ A3 /\ A4) \/ ({x3} \/ {x4})
                  by A53,XBOOLE_1:5,A49,A50,A51,A52;then
A56:        L1 = (A1 /\ A2 /\ A3 /\ A4) \/ {x1,x2} &
                L2 = (A1 /\ A2 /\ A3 /\ A4) \/ {x3,x4} by ENUMSET1:41;
            x1 in {x1} & x2 in {x2} & x3 in {x3} & x4 in {x4}
                by TARSKI:def 1;then
A57:        not x1 in A1 /\ A2 /\ A3 /\ A4 & not x2 in A1 /\ A2 /\ A3 /\ A4 &
              not x3 in A1 /\ A2 /\ A3 /\ A4 & not x4 in A1 /\ A2 /\ A3 /\ A4
                    by A49,A50,A51,A52,XBOOLE_0:def 4;
A58:         A5 c= L1 /\ L2 by A14,XBOOLE_1:19;
            L1 /\ L2 = A1 /\ A2 /\ A3 /\ A4
            proof
              thus L1 /\ L2 c= A1 /\ A2 /\ A3 /\ A4
              proof
                assume
A59:                not L1 /\ L2 c= A1 /\ A2 /\ A3 /\ A4;
                consider x being set such that
A60:               x in L1 /\ L2 & not x in A1 /\ A2 /\ A3 /\ A4
                    by A59,TARSKI:def 3;
A61:            x in L1 & x in L2 & not x in A1 /\ A2 /\ A3 /\ A4
                    by A60,XBOOLE_0:def 3;then
                x in {x1,x2} by A56,XBOOLE_0:def 2;then
                x1 in L2 or x2 in L2 by A61,TARSKI:def 2;then
                x1 in {x3,x4} or x2 in {x3,x4} by A57,A56,XBOOLE_0:def 2;
                hence contradiction by A54,TARSKI:def 2;
              end;
              thus A1 /\ A2 /\ A3 /\ A4 c= L1 /\ L2
              proof
                A1 /\ A2 /\ A3 /\ A4 c= L1 & A1 /\ A2 /\ A3 /\ A4 c= L2
                     by A55,XBOOLE_1:10;
                hence thesis by XBOOLE_1:19;
              end;
              thus thesis;
            end;then
            Card k <=` Card (k - 1) & k - 1 <= (k - 1) + 1
               by A58,A8,CARD_1:27,A44,A47,A17,A18,NAT_1:11;then
            k <= k - 1 & k - 1 <= k by A17,CARD_1:72;then
            k = k - 1 by XXREAL_0:1;
            hence contradiction;
          end;
          hence thesis by A43,A42,XBOOLE_0:def 10;
        end;
        hence thesis by A24,A25,A16,A19,CARD_FIN:1;
      end;
      hence thesis by A32;
    end;
    hence thesis by A15,A26;
  end;

theorem
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X holds
  G_(k,X) is Desarguesian
  proof
    let k be Element of NAT;
    let X be non empty set;
    assume
A1:    0 < k & k + 1 <=` Card X;then
A2: k - 1 is Element of NAT by NAT_1:20;
    let o,b1,a1,b2,a2,b3,a3,r,s,t be POINT of G_(k,X);
    let C1,C2,C3,A1,A2,A3,B1,B2,B3 be LINE of G_(k,X);
A3: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A1,Def1;
A4: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
           by A1,Def1;
    o in the Points of G_(k,X);then
    consider o1 being Subset of X such that
A5:    o1 = o & Card o1 = k by A3;
    a1 in the Points of G_(k,X);then
    consider a11 being Subset of X such that
A6:    a11 = a1 & Card a11 = k by A3;
    a2 in the Points of G_(k,X);then
    consider a12 being Subset of X such that
A7:    a12 = a2 & Card a12 = k by A3;
    a3 in the Points of G_(k,X);then
    consider a13 being Subset of X such that
A8:    a13 = a3 & Card a13 = k by A3;
    b1 in the Points of G_(k,X);then
    consider b11 being Subset of X such that
A9:   b11 = b1 & Card b11 = k by A3;
    b2 in the Points of G_(k,X);then
    consider b12 being Subset of X such that
A10:   b12 = b2 & Card b12 = k by A3;
    b3 in the Points of G_(k,X);then
    consider b13 being Subset of X such that
A11:   b13 = b3 & Card b13 = k by A3;
    C1 in the Lines of G_(k,X);then
    consider C11 being Subset of X such that
A12:   C11 = C1 & Card C11 = k + 1 by A4;
    C2 in the Lines of G_(k,X);then
    consider C12 being Subset of X such that
A13:   C12 = C2 & Card C12 = k + 1 by A4;
    C3 in the Lines of G_(k,X);then
    consider C13 being Subset of X such that
A14:   C13 = C3 & Card C13 = k + 1 by A4;
    assume
A15:{o,b1,a1} on C1 & {o,a2,b2} on C2 & {o,a3,b3} on C3 &
      {a3,a2,t} on A1 & {a3,r,a1} on A2 & {a2,s,a1} on A3 &
       {t,b2,b3} on B1 & {b1,r,b3} on B2 & {b1,s,b2} on B3 &
       C1,C2,C3 are_mutually_different &
         o<>a1 & o<>a2 & o<>a3 & o<>b1 & o<>b2 & o<>b3 & a1<>b1 & a2<>b2 &
            a3<>b3;then
A16:o on C1 & b1 on C1 & a1 on C1 & o on C2 & a2 on C2 & b2 on C2 &
      o on C3 & a3 on C3 & b3 on C3 & a3 on A1 & a2 on A1 & t on A1 &
       a3 on A2 & r on A2 & a1 on A2 & a2 on A3 & s on A3 & a1 on A3 &
       t on B1 & b2 on B1 & b3 on B1 & b1 on B2 & r on B2 & b3 on B2 &
        b1 on B3 & s on B3 & b2 on B3 by INCSP_1:12;then
A17:{o,a1} on C1 & {o,a2} on C2 & {o,a3} on C3 & {a2,a3} on A1 &
       {a1,a3} on A2 & {a1,a2} on A3 &
        {a1,b1} on C1 & {a2,b2} on C2 & {a3,b3} on C3 &
         {o,b1} on C1 & {o,b2} on C2 & {o,b3} on C3 & {b2,b3} on B1 &
             {b1,b3} on B2 & {b1,b2} on B3 by INCSP_1:11;
A18: C1 <> C2 & C2 <> C3 & C1 <> C3 by A15,ZFMISC_1:def 5;
A19: o c= C1 & b1 c= C1 & a1 c= C1 & o c= C2 & a2 c= C2 & b2 c= C2 &
      o c= C3 & a3 c= C3 & b3 c= C3 & a3 c= A1 & a2 c= A1 & t c= A1 &
       a3 c= A2 & r c= A2 & a1 c= A2 & a2 c= A3 & s c= A3 & a1 c= A3 &
       t c= B1 & b2 c= B1 & b3 c= B1 & b1 c= B2 & r c= B2 & b3 c= B2 &
          b1 c= B3 & s c= B3 & b2 c= B3 by A1,A16,Th10;
    not a1 on B2 & not a2 on B3 & not a3 on B1
    proof
      assume a1 on B2 or a2 on B3 or a3 on B1;then
      {a1,b1} on B2 or {a2,b2} on B3 or {a3,b3} on B1 by A16,INCSP_1:11;then
      b3 on C1 or b1 on C2 or b2 on C3 by A16,A15,A17,INCSP_1:def 10;then
      {o,b3} on C1 or {o,b1} on C2 or {o,b2} on C3 by A16,INCSP_1:11;
      hence contradiction by A15,A17,INCSP_1:def 10,A18;
    end;then
A20: not a1 c= B2 & not a2 c= B3 & not a3 c= B1 by A1,Th10;then
    consider z1 being set such that
A21:    z1 in a1 & not z1 in B2 by TARSKI:def 3;
    consider z2 being set such that
A22:    z2 in a2 & not z2 in B3 by A20,TARSKI:def 3;
    consider z3 being set such that
A23:    z3 in a3 & not z3 in B1 by A20,TARSKI:def 3;
A24: A1 <> B1 & A2 <> B2 & A3 <> B3 by A19,A21,A22,A23;
A25: not o on A1 & not o on B1 & not o on A2 & not o on B2 & not o on A3 &
          not o on B3
    proof
      assume
        o on A1 or o on B1 or o on A2 or o on B2 or o on A3 or o on B3;then
      ({o,a2} on A1 & {o,a3} on A1) or ({o,b2} on B1 & {o,b3} on B1) or
       ({o,a1} on A2 & {o,a3} on A2) or ({o,b1} on B2 & {o,b3} on B2) or
         ({o,a2} on A3 & {o,a1} on A3) or ({o,b2} on B3 & {o,b1} on B3)
                by A16,INCSP_1:11;then
      (A1 = C2 & A1 = C3) or (B1 = C2 & B1 = C3) or (A2 = C1 & A2 = C3) or
         (B2 = C1 & B2 = C3) or (A3 = C2 & A3 = C1) or (B3 = C2 & B3 = C1)
                  by A15,A17,INCSP_1:def 10;
      hence contradiction by A18;
    end;
    consider salpha being POINT of G_(k,X) such that
A26:    salpha on A3 & salpha on B3 & salpha = (a1 /\ b1) \/ (a2 /\ b2)
         by A1,A16,A24,A25,Th12,A18;
    consider ralpha being POINT of G_(k,X) such that
A27:    ralpha on B2 & ralpha on A2 & ralpha = (a1 /\ b1) \/ (a3 /\ b3)
         by A1,A16,A24,A25,Th12,A18;
    consider talpha being POINT of G_(k,X) such that
A28:    talpha on A1 & talpha on B1 & talpha = (a2 /\ b2) \/ (a3 /\ b3)
         by A1,A16,A24,A25,Th12,A18;
A29: r = ralpha & s = salpha & t = talpha
    proof
      assume
A30:      r <> ralpha or s <> salpha or t <> talpha;
      {r,ralpha} on A2 & {r,ralpha} on B2 & {s,salpha} on A3 &
       {s,salpha} on B3 & {t,talpha} on A1 & {t,talpha} on B1
              by A16,A26,A27,A28,INCSP_1:11;
      hence contradiction by A24,A30,INCSP_1:def 10;
    end;
    r in the Points of G_(k,X);then
    consider r1 being Subset of X such that
A31:   r1 = r & Card r1 = k by A3;
    s in the Points of G_(k,X);then
    consider s1 being Subset of X such that
A32:   s1 = s & Card s1 = k by A3;
    t in the Points of G_(k,X);then
    consider t1 being Subset of X such that
A33:   t1 = t & Card t1 = k by A3;
    a1 \/ b1 c= C1 & a2 \/ b2 c= C2 & a3 \/ b3 c= C3 by A19,XBOOLE_1:8;then
    k + 1 c= Card(a1 \/ b1) & Card(a1 \/ b1) c= k + 1 &
      k + 1 c= Card(a2 \/ b2) & Card(a2 \/ b2) c= k + 1 &
       k + 1 c= Card(a3 \/ b3) & Card(a3 \/ b3) c= k + 1
          by A6,A7,A8,A9,A10,A11,A12,A13,A14,A15,Th1,CARD_1:27;then
A34:Card(a1 \/ b1) = (k - 1) + 2*1 & Card(a2 \/ b2) = (k - 1) + 2*1 &
      Card(a3 \/ b3) = (k - 1) + 2*1 & Card a1 = (k - 1) + 1 &
       Card a2 = (k - 1) + 1 & Card a3 = (k - 1) + 1 & Card b1 = (k - 1) + 1 &
          Card b2 = (k - 1) + 1 & Card b3 = (k - 1) + 1
                by A6,A7,A8,A9,A10,A11,XBOOLE_0:def 10;then
A35:Card(a1 /\ b1) = k - 1 & Card(a2 /\ b2) = k - 1 & Card(a3 /\ b3) = k - 1
              by A2,Th2;
    r \/ s = (a3 /\ b3) \/ (a1 /\ b1) \/ (a1 /\ b1) \/ (a2 /\ b2)
             by A29,A26,A27,XBOOLE_1:4;then
    r \/ s = (a3 /\ b3) \/ ((a1 /\ b1) \/ (a1 /\ b1)) \/ (a2 /\ b2)
         by XBOOLE_1:4;then
    r \/ s = (a1 /\ b1) \/ (a3 /\ b3) \/ (a2 /\ b2) &
     (a3 /\ b3) \/ (a2 /\ b2) c= (a1 /\ b1) \/ ((a3 /\ b3) \/ (a2 /\ b2)) &
       (a1 /\ b1) \/ (a3 /\ b3) \/ (a2 /\ b2) =
            (a1 /\ b1) \/ ((a3 /\ b3) \/ (a2 /\ b2))
                by XBOOLE_1:4,XBOOLE_1:7;then
A36:r \/ s \/ t = (a1 /\ b1) \/ (a3 /\ b3) \/ (a2 /\ b2)
         by A29,A28,XBOOLE_1:12;
A37: not k = 1
    proof
      assume
A38:     k = 1;then
      consider z1 being set such that
A39:    o = {z1} by A5,CARD_2:60;
      consider z2 being set such that
A40:    a1 = {z2} by A6,A38,CARD_2:60;
      consider z3 being set such that
A41:    b1 = {z3} by A9,A38,CARD_2:60;
A42:  Card{z1,z2,z3} = 3 by A15,A39,A40,A41,CARD_2:77;
      o \/ a1 c= C1 by A19,XBOOLE_1:8;then
      (o \/ a1) \/ b1 c= C1 by A19,XBOOLE_1:8;then
      {z1,z2} \/ b1 c= C1 by A39,A40,ENUMSET1:41;then
      {z1,z2,z3} c= C1 by A41,ENUMSET1:43;then
      3 c= Card C1 & Card C1 = 2 by A12,A38,A42,CARD_1:27;
      hence contradiction by CARD_1:56;
    end;
    0 + 1 < k + 1 by A1,XREAL_1:10;then
    1 <= (k - 1) + 1 by NAT_1:13;then
    1 <= k - 1 or k = 1 by A2,NAT_1:8;then
A43: 1 + 1 <= (k - 1) + 1 or k = 1 by XREAL_1:8;then
    k = 2 or 2 <= k - 1 by A37,A2,NAT_1:8;then
A44: k = 2 or 2 + 1 <= (k - 1) + 1 by XREAL_1:8;
A45:k - 2 is Element of NAT by A43,A37,NAT_1:21;
    Card((a1 /\ b1) \/ (a3 /\ b3)) = (k - 2) + 2*1 &
     Card(a1 /\ b1) = (k - 2) + 1 &
      Card((a1 /\ b1) \/ (a2 /\ b2)) = (k - 2) + 2*1 &
      Card(a2 /\ b2) = (k - 2) + 1 &
       Card((a2 /\ b2) \/ (a3 /\ b3)) = (k - 2) + 2*1 &
       Card(a3 /\ b3) = (k - 2) + 1 by A29,A26,A27,A28,A31,A32,A33,A34,A2,Th2;
then
A46: Card((a1 /\ b1) /\ (a3 /\ b3)) = k - 2 &
      Card((a2 /\ b2) /\ (a3 /\ b3)) = k - 2 &
       Card((a1 /\ b1) /\ (a2 /\ b2)) = k - 2 by A45,Th2;
    r \/ s c= X & k = Card k by A31,A32,CARD_1:66,XBOOLE_1:8;then
A47:    r \/ s \/ t c= X & r is finite & s is finite & t is finite
       by A31,A32,A33,CARD_4:4,XBOOLE_1:8;then
    r \/ s \/ t is Subset of X & s \/ t is finite
          by FINSET_1:14;then
    r c= r \/ (s \/ t) & s c= s \/ (r \/ t) & t c= t \/ (r \/ s) &
       r \/ (s \/ t) is finite by A47,XBOOLE_1:7,FINSET_1:14;then
A48: r c= r \/ s \/ t & s c= r \/ s \/ t & t c= r \/ s \/ t &
        r \/ s \/ t is finite by XBOOLE_1:4;
A49:k = 2 implies ex O being LINE of G_(k,X) st {r,s,t} on O
    proof
      assume k = 2;then
A50:  Card(r \/ s \/ t) = k + 1 by A35,A36,A46,Th7;
      r \/ s \/ t in the Lines of G_(k,X) by A4,A50,A47;then
      consider O being LINE of G_(k,X) such that
A51:    O = r \/ s \/ t;
      r on O & s on O & t on O by A1,A51,A48,Th10;then
      {r,s,t} on O by INCSP_1:12;
      hence thesis;
    end;
    3 <= k implies ex O being LINE of G_(k,X) st {r,s,t} on O
    proof
      assume
A52:       3 <= k;
A53:  Card(r \/ s \/ t) = k + 1 implies ex O being LINE of G_(k,X) st
            {r,s,t} on O
      proof
        assume Card(r \/ s \/ t) = k + 1;then
        r \/ s \/ t in the Lines of G_(k,X) by A4,A47;then
        consider O being LINE of G_(k,X) such that
A54:      O = r \/ s \/ t;
        r on O & s on O & t on O by A1,A54,A48,Th10;then
        {r,s,t} on O by INCSP_1:12;
        hence thesis;
      end;
      Card(r \/ s \/ t) = k implies ex O being LINE of G_(k,X) st {r,s,t} on O
      proof
        assume Card(r \/ s \/ t) = k;then
        r = r \/ s \/ t & s = r \/ s \/ t & t = r \/ s \/ t
                by A31,A32,A33,A48,A47,CARD_FIN:1;then
        {r,s,t} on A1 by INCSP_1:12,A16;
        hence thesis;
      end;
      hence thesis by A35,A36,A46,A43,Th7,A52,A53;
    end;
  hence thesis by A44,A49;
end;

definition
   let S be IncProjStr;
   let K be Subset of the Points of S;
   attr K is clique means :Def2:
     for A,B being POINT of S st A in K & B in K
          ex L being LINE of S st {A,B} on L;
end;

definition
   let S be IncProjStr;
   let K be Subset of the Points of S;
   attr K is maximal_clique means :Def3:
     K is clique &
      (for U being Subset of the Points of S st U is clique & K c= U holds
               U = K);
end;

definition
   let k be Element of NAT;
   let X be non empty set;
   let T be Subset of the Points of G_(k,X);
   attr T is STAR means :Def4:
     ex S being Subset of X st
     Card S = k - 1 & T = {A where A is Subset of X: Card A = k & S c= A};

   attr T is TOP means :Def5:
     ex S being Subset of X st
       Card S = k + 1 &
        T = {A where A is Subset of X: Card A = k & A c= S};
end;

theorem Th14:
  for k being Element of NAT
  for X being non empty set st 2 <= k & k + 2 <=` Card X
  for K being Subset of the Points of G_(k,X) holds
  K is STAR or K is TOP implies K is maximal_clique
  proof
    let k be Element of NAT;
    let X be non empty set;
    assume
A1:      2 <= k & k + 2 <=` Card X;
A2: k - 2 is Element of NAT & k + 1 <= k + 2 & 1 <= k
         by A1,NAT_1:21,XXREAL_0:2,XREAL_1:9;then
    - 2 < 0 & k + 1 c= k + 2 by CARD_1:56;then
A3: 2 - 2 < k & k + 1 <=` Card X by A1,XBOOLE_1:1;
A4: k - 1 is Element of NAT by A2,NAT_1:21;
A5: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A3,Def1;
A6: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
         by A3,Def1;
    let K be Subset of the Points of G_(k,X);
A7: succ(k - 1) = (k - 1) + 1 & succ k = k + 1 & succ(k + 1) = (k + 1) + 1 &
      Card k = k & Card(k - 1) = k - 1 & Card 2 = 2 & Card 1 = 1 & Card 0 = 0
       & succ 1 = 1 + 1 & succ 0 = 0 + 1 & Card(k + 1) = k + 1 &
        Card(k - 2) = k - 2 & succ(k - 2) = (k - 2)+ 1 & succ 2 = 2 + 1
            by A2,A4,CARD_1:52,CARD_1:66;
A8:K is STAR implies K is maximal_clique
    proof
      assume K is STAR;then
      consider S being Subset of X such that
A9:    Card S = k - 1 & K = {A where A is Subset of X: Card A = k & S c= A}
              by Def4;
A10:  S is finite by A4,A7,A9,CARD_4:4;
A11:  K is clique
      proof
        let A,B be POINT of G_(k,X);
        assume
A12:    A in K & B in K;then
        consider A1 being Subset of X such that
A13:    A1 = A & Card A1 = k & S c= A1 by A9;
        consider B1 being Subset of X such that
A14:    B1 = B & Card B1 = k & S c= B1 by A9,A12;
A15:    A is finite & B is finite & A /\ B c= A & A /\ B c= B
          by A7,A13,A14,CARD_4:4,XBOOLE_1:17;then
A16:    A /\ B is finite & A c= X & B c= X by A13,A14,FINSET_1:13;
        S c= A /\ B by A13,A14,XBOOLE_1:19;then
A17:    k - 1 c= Card(A /\ B) & Card(A /\ B) c= k
          by A9,A13,A15,CARD_1:27;then
        k - 1 in succ Card(A /\ B) by A4,ORDINAL1:34;then
        Card(A /\ B) = k - 1 or k - 1 in Card(A /\ B) by ORDINAL1:13;then
A18:    Card(A /\ B) = k - 1 or k c= Card(A /\ B) by A7,ORDINAL1:33;
A19:    Card(A /\ B) = k - 1 implies ex L being LINE of G_(k,X) st {A,B} on L
        proof
          assume
A20:        Card(A /\ B) = k - 1;
          Card A = (k - 1) + 1 & Card B = (k - 1) + 1 by A13,A14;then
          Card(A \/ B) = (k - 1) + 2*1 & A \/ B c= X
                by A13,A14,A4,A20,Th2,XBOOLE_1:8;then
          A \/ B in the Lines of G_(k,X) by A6;then
          consider L being LINE of G_(k,X) such that
A21:      L = A \/ B;
          A c= A \/ B & B c= A \/ B by XBOOLE_1:7;then
          A on L & B on L by A3,A21,Th10;then
          {A,B} on L by INCSP_1:11;
          hence thesis;
        end;
        Card(A /\ B) = k implies ex L being LINE of G_(k,X) st {A,B} on L
        proof
          assume Card(A /\ B) = k;then
A22:      A /\ B = A & A /\ B = B by A13,A14,A15,A16,CARD_FIN:1;
A23:      Card A <=` Card X by A13,CARD_1:27;
          Card A <> Card X
          proof
            assume Card A = Card X;then
            k in k by A3,A13,A7,ORDINAL1:33;
            hence contradiction;
          end;then
          Card A <` Card X by A23,CARD_1:13;then
          X \ A <> {} by CARD_2:4;then
          consider x being set such that
A24:          x in X \ A by XBOOLE_0:def 1;
          x in X & not x in A by A24,XBOOLE_0:def 4;then
A25:      Card(A \/ {x}) = k + 1 & {x} c= X
             by A13,A15,CARD_2:54,ZFMISC_1:37;then
          A \/ {x} c= X by A13,XBOOLE_1:8;then
          A \/ {x} in the Lines of G_(k,X) by A6,A25;then
          consider L being LINE of G_(k,X) such that
A26:          L = A \/ {x};
          A c= A \/ {x} & B c= A \/ {x} by A22,XBOOLE_1:7;then
          A on L & B on L by A3,A26,Th10;then
          {A,B} on L by INCSP_1:11;
          hence thesis;
        end;
        hence thesis by A18,A17,XBOOLE_0:def 10,A19;
      end;
      for U being Subset of the Points of G_(k,X) st U is clique & K c= U
          holds U = K
      proof
        let U be Subset of the Points of G_(k,X);
        assume
A27:      U is clique & K c= U & U <> K;then
        not U c= K by XBOOLE_0:def 10;then
        consider A being set such that
A28:        A in U & not A in K by TARSKI:def 3;
A29:    A in the Points of G_(k,X) by A28;
        consider A2 being POINT of G_(k,X) such that
A30:            A2 = A by A28;
        consider A1 being Subset of X such that
A31:       A1 = A & Card A1 = k by A5,A29;
A32:    not S c= A & A is finite & A c= X
             by A7,A9,A28,A31,CARD_4:4;
A33:    S /\ A c= S & S /\ A c= A by XBOOLE_1:17;then
A34:    S /\ A is finite by A32,FINSET_1:13;
        Card(S /\ A) c= k - 1 by A9,A33,CARD_1:27;then
        Card(S /\ A) in succ(k - 1) & succ(k - 2) = (k - 2) + 1
                  by A2,A4,ORDINAL1:34,CARD_1:52;then
A35:    Card(S /\ A) in succ(k - 2) or Card (S /\ A) = k - 1 by ORDINAL1:13;
A36:    Card(S /\ A) c= k - 2
           by A2,A35,ORDINAL1:34,A9,A10,A32,A33,A34,CARD_FIN:1;
        k - 1 < (k - 1) + 1 by A4,NAT_1:13;then
        Card S <` Card A by A7,A9,A31,A4,CARD_2:58;then
        A \ S <> {} by CARD_2:4;then
        consider x being set such that
A37:        x in A \ S by XBOOLE_0:def 1;
        x in A & not x in S by A37,XBOOLE_0:def 4;then
A38:    Card(S \/ {x}) = (k - 1) + 1 & x in X & {x} c= A & {x} c= X
           by A9,A31,A10,CARD_2:54,ZFMISC_1:37;then
A39:    S \/ {x} c= X by XBOOLE_1:8;
A40:    not X \ (A \/ S) = {}
        proof
          assume X \ (A \/ S) = {};then
          X c= A \/ S & A \/ S c= X by A31,XBOOLE_1:8,XBOOLE_1:37;then
A41:      X \ A c= S & A \/ S = X by XBOOLE_0:def 10,XBOOLE_1:43;
          S \/ {x} in the Points of G_(k,X) by A5,A38,A39;then
          consider B being POINT of G_(k,X) such that
A42:            B = S \/ {x};
          A \/ B = (A \/ S) \/ {x} & S c= B & {x} c= A \/ S & B c= X
            by A38,A42,XBOOLE_1:4,XBOOLE_1:8,XBOOLE_1:10;then
A43:      B in K & A \/ B = A \/ S by A9,A38,A42,XBOOLE_1:12;
          not ex L being LINE of G_(k,X) st {A2,B} on L
          proof
            assume ex L being LINE of G_(k,X) st {A2,B} on L;then
            consider L being LINE of G_(k,X) such that
A44:              {A2,B} on L;
            L in the Lines of G_(k,X);then
            consider L1 being Subset of X such that
A45:              L = L1 & Card L1 = k + 1 by A6;
            A2 on L & B on L by A44,INCSP_1:11;then
            A c= L & B c= L by A3,A30,Th10;then
            A \/ B c= L by XBOOLE_1:8;then
            Card(A \/ B) <=` Card L by CARD_1:27;then
            k + 2 c= Card L by A43,A1,A41,XBOOLE_1:1;then
            k + 1 in k + 1 by A7,A45,ORDINAL1:33;
            hence contradiction;
          end;
          hence contradiction by A27,A28,A30,A43,Def2;
        end;
        not X \ (A \/ S) <> {}
        proof
          assume X \ (A \/ S) <> {};then
          consider y being set such that
A46:      y in X \ (A \/ S) by XBOOLE_0:def 1;
          y in X & not y in A \/ S by A46,XBOOLE_0:def 4;then
A47:      not y in A & not y in S & {y} c= X
             by XBOOLE_0:def 2,ZFMISC_1:37;then
A48:      Card(S \/ {y}) = (k - 1) + 1 & S \/ {y} c= X
             by A9,A10,CARD_2:54,XBOOLE_1:8;then
          S \/ {y} in the Points of G_(k,X) by A5;then
          consider B being POINT of G_(k,X) such that
A49:      B = S \/ {y};
          S c= B & B c= X by A47,A49,XBOOLE_1:7,XBOOLE_1:8;then
A50:      B in K by A9,A48,A49;
          A /\ B c= A /\ S
          proof
            let a be set;
            assume a in A /\ B;then
A51:        a in A & a in S \/ {y} by A49,XBOOLE_0:def 3;then
            a in S or a in {y} by XBOOLE_0:def 2;
            hence thesis by A47,A51,TARSKI:def 1,XBOOLE_0:def 3;
          end;then
          Card(A /\ B) c= Card(A /\ S) by CARD_1:27;then
          Card(A /\ B) c= k - 2 & succ(k - 2) = (k - 2) + 1
                   by A2,A36,CARD_1:52,XBOOLE_1:1;then
A52:      Card(A /\ B) in k - 1 by A2,ORDINAL1:34;
          not ex L being LINE of G_(k,X) st {A2,B} on L
          proof
            assume ex L being LINE of G_(k,X) st {A2,B} on L;then
            consider L being LINE of G_(k,X) such that
A53:        {A2,B} on L;
            L in the Lines of G_(k,X);then
            consider L1 being Subset of X such that
A54:        L = L1 & Card L1 = k + 1 by A6;
            A2 on L & B on L by A53,INCSP_1:11;then
            A c= L & B c= L by A3,A30,Th10;then
A55:        A \/ B c= L by XBOOLE_1:8;
            A <> B
            proof
              assume A = B;then
              {y} c= A by A49,XBOOLE_1:7;
              hence contradiction by A47,ZFMISC_1:37;
            end;then
            k + 1 <=` Card(A \/ B) & Card(A \/ B) <=` k + 1
                by A31,A48,A49,A54,A55,Th1,CARD_1:27;then
            Card(A \/ B) = (k - 1) + 2*1 & Card A = (k - 1) + 1 &
              Card B = (k - 1) + 1
                 by A31,A47,A9,A10,CARD_2:54,A49,XBOOLE_0:def 10;then
            Card(A /\ B) = k - 1 by A4,Th2;
            hence contradiction by A52;
          end;
          hence contradiction by A27,A28,A30,A50,Def2;
        end;
        hence thesis by A40;
      end;
      hence thesis by A11,Def3;
    end;
    K is TOP implies K is maximal_clique
    proof
      assume
      K is TOP;then
      consider S being Subset of X such that
A56:      Card S = k + 1 & K = {A where A is Subset of X: Card A = k & A c= S}
             by Def5;
A57:  S is finite by A7,A56,CARD_4:4;
A58:  K is clique
      proof
        let A,B be POINT of G_(k,X);
        assume
A59:    A in K & B in K;then
        consider A1 being Subset of X such that
A60:    A1 = A & Card A1 = k & A1 c= S by A56;
        consider B1 being Subset of X such that
A61:    B1 = B & Card B1 = k & B1 c= S by A56,A59;
        S in the Lines of G_(k,X) by A6,A56;then
        consider L being LINE of G_(k,X) such that
A62:    L = S;
        A on L & B on L by A60,A61,A62,A3,Th10;then
        {A,B} on L by INCSP_1:11;
        hence thesis;
      end;
      for U being Subset of the Points of G_(k,X) st U is clique & K c= U
              holds U = K
      proof
        let U be Subset of the Points of G_(k,X);
        assume
A63:      U is clique & K c= U & U <> K;then
        not U c= K by XBOOLE_0:def 10;then
        consider A being set such that
A64:         A in U & not A in K by TARSKI:def 3;
A65:    A in the Points of G_(k,X) by A64;
        consider A2 being POINT of G_(k,X) such that
A66:            A2 = A by A64;
        consider A1 being Subset of X such that
A67:       A1 = A & Card A1 = k by A5,A65;
A68:    not A c= S & A is finite & A c= X
             by A7,A56,A64,A67,CARD_4:4;then
        consider x being set such that
A69:           x in A & not x in S by TARSKI:def 3;
        k <= k + 1 by NAT_1:11;then
A70:    Card A <=` Card S by A67,A56,CARD_1:56;
        Card A <> Card S by A56,A67;then
        Card A <` Card S by A70,CARD_1:13;then
        S \ A <> {} by CARD_2:4;then
A71:    Card(S \ A) <> 0 by CARD_4:17;
        2 c= Card(S \ A)
        proof
          assume not 2 c= Card(S \ A);then
A72:      Card(S \ A) in succ 1 & 0 <=` Card(S \ A) by ORDINAL1:26;then
          Card(S \ A) in 1 or Card(S \ A) = 1 by ORDINAL1:13;then
A73:      Card(S \ A) c= 0 or Card(S \ A) = 1 by A7,ORDINAL1:34;
          not Card(S \ A) = 1
          proof
            assume
A74:            Card(S \ A) = 1;
A75:        S \ A c= S by XBOOLE_1:36;then
            S \ A is finite by A57,FINSET_1:13;then
A76:        Card(S \ (S \ A)) = (k + 1) - 1 & S \ (S \ A) = S /\ A &
              S /\ A c= S & S /\ A c= A
                 by A56,A57,A74,A75,CARD_2:63,XBOOLE_1:17,XBOOLE_1:48;then
            Card(S /\ A) = k & S /\ A is finite
                  by A7,CARD_4:4;
            hence contradiction by A76,A67,A68,CARD_FIN:1;
          end;
          hence contradiction by A71,A73,A72,XBOOLE_0:def 10;
        end;then
        consider B1 being set such that
A77:         B1 c= S \ A & Card B1 = 2 by CARD_FIL:36;
A78:    B1 c= S & B1 misses A by A77,XBOOLE_1:106;then
A79:    B1 is finite by A57,FINSET_1:13;then
A80:    Card(S \ B1) = (k + 1) - 2 by A56,A57,A77,A78,CARD_2:63;
        k - 2 <= (k - 2) + 1 by A2,NAT_1:11;then
        k - 2 c= Card(S \ B1) by A80,A2,A4,CARD_1:56;then
        consider B2 being set such that
A81:       B2 c= S \ B1 & Card B2 = k - 2 by A2,CARD_FIL:36;
A82:   B2 c= S & B2 misses B1 by A81,XBOOLE_1:106;then
        B2 is finite & B1 c= X & B2 c= X
            by A57,A77,FINSET_1:13,XBOOLE_1:1;then
A83:   Card(B1 \/ B2) = 2 + (k - 2) & B1 \/ B2 c= X & B1 \/ B2 c= S
                by A77,A78,A79,A81,A82,CARD_2:53,XBOOLE_1:8;then
        B1 \/ B2 in the Points of G_(k,X) by A5;then
        consider B being POINT of G_(k,X) such that
A84:        B = B1 \/ B2;
A85:   A /\ B c= A \/ B & not x in B1 & not x in B2 &
            A \/ B = A \/ (B1 \/ B2) & B c= S
            by A69,A78,A82,XBOOLE_1:8,A84,XBOOLE_1:29;
A86:   not x in B1 \/ B2 & B1 /\ A = {} & (B1 /\ A) /\ B c= B1 /\ A & B in K
             by A69,A56,A78,A83,A84,XBOOLE_0:def 7,XBOOLE_1:17;
A87:   k + 2 c= Card(A \/ B)
        proof
          assume not k + 2 c= Card(A \/ B);then
A88:         Card(A \/ B) in succ(k + 1) by A7,ORDINAL1:26;
A89:     ({x} \/ B1) misses (A /\ B)
          proof
            assume not ({x} \/ B1) misses (A /\ B);then
            ({x} \/ B1) /\ (A /\ B) <> {} by XBOOLE_0:def 7;then
            consider y being set such that
A90:            y in ({x} \/ B1) /\ (A /\ B) by XBOOLE_0:def 1;
            y in ({x} \/ B1) & y in A /\ B by A90,XBOOLE_0:def 3;then
            (y in {x} or y in B1) & y in A & y in B
                by XBOOLE_0:def 2,XBOOLE_0:def 3;
            hence contradiction by A84,A86,XBOOLE_0:def 3,TARSKI:def 1;
          end;
          (B1 /\ A) /\ B c= {} & {x} c= A
                by A69,A86,ZFMISC_1:37;then
          (B1 /\ A) /\ B = {} & B1 c= B & {x} c= A \/ B
            by A84,XBOOLE_1:3,XBOOLE_1:10;then
          B1 /\ (A /\ B) = {} & B1 c= A \/ B & (A /\ B) \/ {x} c= A \/ B
              by A85,XBOOLE_1:10,XBOOLE_1:16,XBOOLE_1:8;then
          (A /\ B) \/ {x} \/ B1 c= A \/ B by XBOOLE_1:8;then
A91:     (A /\ B) \/ ({x} \/ B1) c= A \/ B & Card(A \/ B) c= k + 1
              by A88,XBOOLE_1:4,ORDINAL1:34;
          Card(A \/ B) = k + 1 or Card(A \/ B) in succ k & A c= A \/ B
               by A7,A88,ORDINAL1:13,XBOOLE_1:7;then
          Card(A \/ B) = k + 1 or Card(A \/ B) c= k & k c= Card(A \/ B)
                 by A67,ORDINAL1:34,CARD_1:27;then
          (Card(A \/ B) = (k - 1) + 2*1 or Card(A \/ B) = k + 2*0) &
             Card A = k + 0 & Card B = k + 0 & Card A = (k - 1) + 1 &
              Card B = (k - 1) + 1 by A67,A83,A84,XBOOLE_0:def 10;then
A92:     Card(A /\ B) = k - 1 or Card(A /\ B) = k
                  by A4,Th2;
          Card({x} \/ B1) = 2 + 1 & {x} \/ B1 is finite & A /\ B is finite
               by A68,A79,A77,A85,CARD_2:54,FINSET_1:14,FINSET_1:15;then
          (Card((A /\ B) \/ ({x} \/ B1)) = (k - 1) + 3 or
             Card((A /\ B) \/ ({x} \/ B1)) = k + 3) &
               Card((A /\ B) \/ ({x} \/ B1)) c= Card(A \/ B)
                   by A89,A91,A92,CARD_1:27,CARD_2:53;then
          k + 2 c= k + 1 or k + 3 c= k + 1 by A91,XBOOLE_1:1;then
          k + 1 in k + 1 or k + 3 <= k + 1 by A7,ORDINAL1:33,CARD_1:56;
          hence contradiction by XREAL_1:8;
        end;
        not ex L being LINE of G_(k,X) st {A2,B} on L
        proof
          assume ex L being LINE of G_(k,X) st {A2,B} on L;then
          consider L being LINE of G_(k,X) such that
A93:            {A2,B} on L;
          L in the Lines of G_(k,X);then
          consider L1 being Subset of X such that
A94:           L = L1 & Card L1 = k + 1 by A6;
          A2 on L & B on L by A93,INCSP_1:11;then
          A c= L & B c= L by A3,A66,Th10;then
          A \/ B c= L by XBOOLE_1:8;then
          k + 1 <=` Card(A \/ B) & Card(A \/ B) <=` k + 1
              by A69,A86,A67,A83,A84,A94,Th1,CARD_1:27;then
          k + 2 c= k + 1 by A87,XBOOLE_0:def 10;then
          k + 1 in k + 1 by A7,ORDINAL1:33;
          hence contradiction;
        end;
        hence thesis by A63,A86,A64,A66,Def2;
      end;
      hence thesis by A58,Def3;
    end;
    hence thesis by A8;
  end;

theorem Th15:
  for k being Element of NAT
  for X being non empty set st 2 <= k & k + 2 <=` Card X
  for K being Subset of the Points of G_(k,X) holds
  K is maximal_clique implies K is STAR or K is TOP
  proof
    let k be Element of NAT;
    let X be non empty set;
    assume
A1:      2 <= k & k + 2 <=` Card X;then
A2: k - 2 is Element of NAT & k + 1 <= k + 2 & 1 <= k
         by NAT_1:21,XXREAL_0:2,XREAL_1:9;then
A3:- 2 < 0 & k + 1 c= k + 2 by CARD_1:56;then
A4: 2 - 2 < k & k + 1 <=` Card X by A1,XBOOLE_1:1;
A5: k - 1 is Element of NAT by A2,NAT_1:21;
A6: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A4,Def1;
A7: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
         by A4,Def1;
    let K be Subset of the Points of G_(k,X);
A8: succ(k - 1) = (k - 1) + 1 & succ k = k + 1 & succ(k + 1) = (k + 1) + 1 &
      Card k = k & Card(k - 1) = k - 1 & Card 2 = 2 & Card 1 = 1 & Card 0 = 0
        & succ 1 = 1 + 1 & succ 0 = 0 + 1 & Card(k + 1) = k + 1 &
         Card(k - 2) = k - 2 & succ(k - 2) = (k - 2)+ 1 & succ 2 = 2 + 1
                by A2,A5,CARD_1:52,CARD_1:66;
    assume
A9:    K is maximal_clique;then
A10:K is clique & k <= k + 1 by NAT_1:11,Def3;
A11:k c= k + 1 by A10,CARD_1:56;then
A12:k c= Card X & K is Subset of the Points of G_(k,X) by A4,XBOOLE_1:1;
A13:K <> {}
    proof
      assume
A14:      K = {};
      consider A1 being set such that
A15:   A1 c= X & Card A1 = k by A12,CARD_FIL:36;
      A1 in the Points of G_(k,X) by A6,A15;then
      consider A being POINT of G_(k,X) such that
A16:    A = A1;
      Card A <> Card X
      proof
        assume Card A = Card X;then
        k + 1 c= k by A1,XBOOLE_1:1,A3,A15,A16;then
        k in k by A8,ORDINAL1:33;
        hence contradiction;
      end;then
      Card A <` Card X by A12,A15,A16,CARD_1:13;then
      X \ A <> {} by CARD_2:4;then
      consider x being set such that
A17:    x in X \ A by XBOOLE_0:def 1;
A18:   x in X & not x in A & A is finite
            by A8,A15,A16,A17,CARD_4:4,XBOOLE_0:def 4;
      {x} c= X by A17,ZFMISC_1:37;then
      Card(A \/ {x}) = k + 1 & A \/ {x} c= X
           by A15,A16,A18,CARD_2:54,XBOOLE_1:8;then
      A \/ {x} in the Lines of G_(k,X) by A7;then
      consider L being LINE of G_(k,X) such that
A19:        L = A \/ {x};
      A c= L by A19,XBOOLE_1:7;then
A20:   A on L by A4,Th10;
      consider U being Subset of the Points of G_(k,X) such that
A21:        U = {A};
A22: K c= U by A14,XBOOLE_1:2;
      U is clique
      proof
        let B,C be POINT of G_(k,X);
        assume B in U & C in U;then
        B on L & C on L by A20,A21,TARSKI:def 1;then
        {B,C} on L by INCSP_1:11;
        hence thesis;
      end;
      hence contradiction by A9,A14,A21,A22,Def3;
    end;
A23:Card K <> 1
    proof
      assume Card K = 1;then
      consider A3 being set such that
A24:     K = {A3} by CARD_2:60;
A25:  A3 in K by A24,TARSKI:def 1;then
A26: A3 in the Points of G_(k,X);
      consider A being POINT of G_(k,X) such that
A27:      A = A3 by A25;
      consider A4 being Subset of X such that
A28:       A = A4 & Card A4 = k by A26,A27,A6;
A29: Card A <=` Card X by A11,A4,XBOOLE_1:1,A28;
      Card A <> Card X
      proof
        assume Card A = Card X;then
        k + 1 c= k by A3,A1,XBOOLE_1:1,A28;then
        k in k by A8,ORDINAL1:33;
        hence contradiction;
      end;then
      Card A <` Card X by A29,CARD_1:13;then
      X \ A <> {} by CARD_2:4;then
      consider x being set such that
A30:    x in X \ A by XBOOLE_0:def 1;
A31: x in X & not x in A & A is finite
          by A8,A28,A30,CARD_4:4,XBOOLE_0:def 4;
A32: {x} c= X by A30,ZFMISC_1:37;then
      Card(A \/ {x}) = k + 1 & A \/ {x} c= X
           by A28,A31,CARD_2:54,XBOOLE_1:8;then
      A \/ {x} in the Lines of G_(k,X) by A7;then
      consider L being LINE of G_(k,X) such that
A33:        L = A \/ {x};
A34: A c= L by A33,XBOOLE_1:7;then
A35: A on L by A4,Th10;
      k - 1 <= (k - 1) + 1 by A5,NAT_1:11;then
      k - 1 c= Card A by A28,A5,CARD_1:56;then
      consider B2 being set such that
A36:     B2 c= A & Card B2 = k - 1 by A5,CARD_FIL:36;
      B2 c= X & not x in B2 & B2 is finite
         by A5,A8,A28,A30,CARD_4:4,XBOOLE_0:def 4,A36,XBOOLE_1:1;then
      Card(B2 \/ {x}) = (k - 1) + 1 & B2 \/ {x} c= X
            by A36,A32,CARD_2:54,XBOOLE_1:8;then
      B2 \/ {x} in the Points of G_(k,X) by A6;then
      consider A1 being POINT of G_(k,X) such that
A37:     A1 = B2 \/ {x};
      B2 c= L & {x} c= L by A33,A34,A36,XBOOLE_1:1,XBOOLE_1:7;then
      A1 c= L by A37,XBOOLE_1:8;then
A38: A1 on L by A4,Th10;
A39: A <> A1
      proof
        {x} c= A1 by A37,XBOOLE_1:7;then
        x in A1 by ZFMISC_1:37;
        hence thesis by A30,XBOOLE_0:def 4;
      end;
      consider U being Subset of the Points of G_(k,X) such that
A40:      U = {A,A1};
      A in U by A40,TARSKI:def 2;then
A41: K c= U by A24,A27,ZFMISC_1:37;
A42: U is clique
      proof
        let B1,B2 be POINT of G_(k,X);
        assume B1 in U & B2 in U;then
        B1 on L & B2 on L by A35,A38,A40,TARSKI:def 2;then
        {B1,B2} on L by INCSP_1:11;
        hence thesis;
      end;
      U <> K
      proof
        A1 in U & not A1 in K
             by A24,A27,A39,A40,TARSKI:def 1,TARSKI:def 2;
        hence thesis;
      end;
      hence contradiction by A9,A41,A42,Def3;
    end;
    0 c= Card K by XBOOLE_1:2;then
    0 in succ(Card K) by ORDINAL1:34;then
    Card K = 0 or 0 in Card K by ORDINAL1:13;then
    1 c= Card K by A8,A13,CARD_4:17,ORDINAL1:33;then
    1 in succ Card K by ORDINAL1:34;then
    Card K = 1 or 1 in Card K by ORDINAL1:13;then
A43: 2 <=` Card K by A8,A23,ORDINAL1:33;then
    consider R being set such that
A44:        R c= K & Card R = 2 by CARD_FIL:36;
    R is finite by A8,A44,CARD_4:4;then
    consider A1,B1 being set such that
A45:    A1 <> B1 & R = {A1,B1} by A44,CARD_2:79;
A46:A1 in R & B1 in R by A45,TARSKI:def 2;then
A47:A1 in the Points of G_(k,X) & B1 in the Points of G_(k,X)
             by A44,TARSKI:def 3;then
    consider A being POINT of G_(k,X) such that
A48:       A = A1;
    consider B being POINT of G_(k,X) such that
A49:      B = B1 by A47;
    consider A2 being Subset of X such that
A50:     A2 = A & Card A2 = k by A47,A48,A6;
    consider B2 being Subset of X such that
A51:    B2 = B & Card B2 = k by A47,A49,A6;
    consider L being LINE of G_(k,X) such that
A52:     {A,B} on L by A44,A48,A49,A46,A10,Def2;
    A on L & B on L by A52,INCSP_1:11;then
A53: A c= L & B c= L by A4,Th10;
A54: A is finite & B is finite by A8,A50,A51,CARD_4:4;
    L in the Lines of G_(k,X);then
    consider L1 being Subset of X such that
A55:     L1 = L & Card L1 = k + 1 by A7;
A56: A \/ B c= L & A /\ B c= L & L is finite
      by A55,A8,CARD_4:4,A53,XBOOLE_1:8,XBOOLE_1:108;then
A57:Card(A \/ B) <=` k + 1 & k + 1 <=` Card(A \/ B)
         by A45,A48,A49,A50,A51,A55,CARD_1:27,Th1;then
A58: Card(A \/ B) = k + 1 & A /\ B c= X & A \/ B is finite
          by A55,A56,XBOOLE_0:def 10,XBOOLE_1:1,FINSET_1:13;then
A59: A \/ B = L by A56,A55,CARD_FIN:1;
A60:(ex C being POINT of G_(k,X) st C in K & C on L & A <> C & B <> C)
       implies K is TOP
    proof
      assume ex C being POINT of G_(k,X) st C in K & C on L & A <> C &
            B <> C;then
      consider C being POINT of G_(k,X) such that
A61:      C in K & C on L & A <> C & B <> C;
A62:   C c= L by A4,A61,Th10;
      C in the Points of G_(k,X);then
      consider C2 being Subset of X such that
A63:   C2 = C & Card C2 = k by A6;
A64:   A \/ C c= L & B \/ C c= L & C is finite & L is finite
            by A8,A63,A55,A53,A62,CARD_4:4,XBOOLE_1:8;then
A65:   A \/ B \/ C c= L & Card(A \/ C) <=` k + 1 & k + 1 <=` Card(A \/ C) &
         A \/ B is finite & Card(B \/ C) <=` k + 1 & k + 1 <=` Card(B \/ C)
     by A54,A56,A50,A51,A61,A63,A55,A62,Th1,CARD_1:27,XBOOLE_1:8,FINSET_1:14;
        then
A66:   Card(A \/ B) = (k - 1) + 2*1 & Card(B \/ C) = (k - 1) + 2*1 &
         A \/ B c= (A \/ B) \/ C & Card(A \/ C) = (k - 1) + 2*1 &
          Card A = (k - 1) + 1 & Card B = (k - 1) + 1 & Card C = (k - 1) + 1
           by A57,A50,A51,A63,XBOOLE_0:def 10,XBOOLE_1:7;then
      Card(A /\ B) = k - 1 & Card(B /\ C) = k - 1
       & A \/ B = L & Card(A /\ C) = k - 1 & A \/ B c= A \/ B \/ C
           by A65,A56,A5,A55,Th2,CARD_FIN:1;then
A67:   Card(A \/ B \/ C) = k + 1 & Card A = (k + 1) - 1 & Card B = (k + 1) - 1
        & Card C = (k + 1) - 1 & Card(A /\ C) = (k + 1) - 2 & 2 + 1 <= k + 1 &
         Card(B /\ C) = (k + 1) - 2 & Card(A /\ B) = (k + 1) - 2 &
            1 + 1 <= k + 1 by A65,XBOOLE_0:def 10,A66,A1,A2,XREAL_1:9;then
A68:   (Card(A /\ B /\ C) = (k + 1) - 3 & Card(A \/ B \/ C) = k + 1) or
        (Card(A /\ B /\ C) = (k + 1) - 2 & Card(A \/ B \/ C) = (k + 1) + 1)
                  by Th7;
      consider T being set such that
A69:      T = {D where D is Subset of X: Card D = k & D c= L};
A70:  K c= T
      proof
        let D2 be set;
        assume
A71:         D2 in K & not D2 in T;then
        D2 in the Points of G_(k,X);then
        consider D1 being Subset of X such that
A72:           D1 = D2 & Card D1 = k by A6;
        consider D being POINT of G_(k,X) such that
A73:           D = D1 by A71,A72;
        not D c= L by A69,A72,A73,A71;then
        consider x being set such that
A74:          x in D & not x in L by TARSKI:def 3;
        consider L11 being LINE of G_(k,X) such that
A75:       {A,D} on L11 by A10,A72,A73,A71,A44,A48,A46,Def2;
        consider L12 being LINE of G_(k,X) such that
A76:        {B,D} on L12 by A10,A72,A73,A71,A44,A49,A46,Def2;
        consider L13 being LINE of G_(k,X) such that
A77:        {C,D} on L13 by A10,A72,A73,A71,A61,Def2;
        L11 in the Lines of G_(k,X);then
        consider L21 being Subset of X such that
A78:        L21 = L11 & Card L21 = k + 1 by A7;
        L12 in the Lines of G_(k,X);then
        consider L22 being Subset of X such that
A79:        L22 = L12 & Card L22 = k + 1 by A7;
        L13 in the Lines of G_(k,X);then
        consider L23 being Subset of X such that
A80:        L23 = L13 & Card L23 = k + 1 by A7;
A81:   A <> D & B <> D & C <> D by A72,A73,A69,A53,A62,A71;
        A on L11 & D on L11 & B on L12 & D on L12 & C on L13 & D on L13
           by A75,A76,A77,INCSP_1:11;then
        A c= L11 & D c= L11 & B c= L12 & D c= L12 & C c= L13 & D c= L13
            by A4,Th10;then
        A \/ D c= L11 & B \/ D c= L12 & C \/ D c= L13 by XBOOLE_1:8;then
        Card(A \/ D) <=` k + 1 & Card(B \/ D) <=` k + 1 &
           Card(C \/ D) <=` k + 1 & k + 1 <=` Card(A \/ D) &
             k + 1 <=` Card(B \/ D) & k + 1 <=` Card(C \/ D)
           by A50,A51,A63,A72,A73,A78,A79,A80,A81,CARD_1:27,Th1;then
A82:    Card A = (k - 1) + 1 & Card B = (k - 1) + 1 & Card C = (k - 1) + 1 &
           Card D = (k - 1) + 1 & Card(A \/ D) = (k - 1) + 2*1 &
            Card(B \/ D) = (k - 1) + 2*1 & Card(C \/ D) = (k - 1) + 2*1
               by A50,A51,A63,A72,A73,XBOOLE_0:def 10;then
A83:   Card(A /\ D) = k - 1 & Card(B /\ D) = k - 1 & Card(C /\ D) = k - 1 &
         {x} c= D & D is finite & Card {x} = 1 & D \ {x} c= D &
           A /\ D is finite & B /\ D is finite & C /\ D is finite
      by A5,A8,A74,A54,A64,Th2,CARD_1:79,CARD_4:4,ZFMISC_1:37,FINSET_1:15;then
A84:   Card(D \ {x}) = k - 1 & D \ {x} is finite & not x in A & not x in B &
            not x in C by A72,A73,A74,A53,A62,CARD_2:63,FINSET_1:13;
A85:   A /\ D c= D \ {x}
        proof
          let z be set;
          assume z in A /\ D;then
A86:     z <> x & z in A & z in D by A84,XBOOLE_0:def 3;then
          not z in {x} by TARSKI:def 1;
          hence thesis by A86,XBOOLE_0:def 4;
        end;
A87:   B /\ D c= D \ {x}
        proof
          let z be set;
          assume z in B /\ D;then
A88:     z <> x & z in B & z in D by A84,XBOOLE_0:def 3;then
          not z in {x} by TARSKI:def 1;
          hence thesis by A88,XBOOLE_0:def 4;
        end;
        C /\ D c= D \ {x}
        proof
          let z be set;
          assume z in C /\ D;then
A89:     z <> x & z in C & z in D by A84,XBOOLE_0:def 3;then
          not z in {x} by TARSKI:def 1;
          hence thesis by A89,XBOOLE_0:def 4;
        end;then
A90:   A /\ D = D \ {x} & B /\ D = D \ {x} & C /\ D = D \ {x}
             by A83,A84,A85,A87,CARD_FIN:1;then
        A /\ D = (A /\ D) /\ (B /\ D);then
        A /\ D = A /\ (D /\ B) /\ D by XBOOLE_1:16;then
        A /\ D = A /\ B /\ D /\ D by XBOOLE_1:16;then
        A /\ D = A /\ B /\ (D /\ D) by XBOOLE_1:16;then
        A /\ D = (A /\ B /\ D) /\ (C /\ D) by A90;then
        A /\ D = A /\ B /\ (D /\ C) /\ D by XBOOLE_1:16;then
        A /\ D = A /\ B /\ C /\ D /\ D by XBOOLE_1:16;then
        A /\ D = A /\ B /\ C /\ (D /\ D) by XBOOLE_1:16;then
        Card(A /\ B /\ C /\ D) = k - 1 & (A /\ B /\ C) /\ D c= A /\ B /\ C
          by A82,A5,Th2,XBOOLE_1:17;then
        k - 1 c= k - 2 by A67,A68,CARD_1:27;then
        k - 1 in k - 1 by A8,ORDINAL1:34;
        hence contradiction;
      end;
      T c= the Points of G_(k,X)
      proof
        let e be set;
        assume e in T;then
        consider E being Subset of X such that
A91:       e = E & Card E = k & E c= L by A69;
        thus thesis by A6,A91;
      end;then
      consider T1 being Subset of the Points of G_(k,X) such that
A92:        T1 = T;
A93:  T1 is TOP by A55,A92,A69,Def5;then
      T1 is maximal_clique by A1,Th14;then
      T1 is clique by Def3;
      hence thesis by A93,A9,A92,A70,Def3;
    end;
    not (ex C being POINT of G_(k,X) st C in K & C on L & A <> C & B <> C)
         implies K is STAR
    proof
      assume
A94: not (ex C being POINT of G_(k,X) st C in K & C on L & A <> C & B <> C);
A95:  Card A = (k - 1) + 1 & Card B = (k - 1) + 1 &
         Card(A \/ B) = (k - 1) + 2*1 by A57,XBOOLE_0:def 10,A50,A51;then
A96: Card(A /\ B) = k - 1 by A5,Th2;
      consider S being set such that
A97:       S = {D where D is Subset of X: Card D = k & A /\ B c= D};
A98: Card L <=` Card X by A3,A1,XBOOLE_1:1,A55;
      Card L <> Card X
      proof
        assume Card L = Card X;then
        k + 1 in k + 1 by A1,A55,A8,ORDINAL1:33;
        hence contradiction;
      end;then
      Card L <` Card X by A98,CARD_1:13;then
      X \ L <> {} by CARD_2:4;then
      consider x being set such that
A99:      x in X \ L by XBOOLE_0:def 1;
A100: {x} c= X & not x in A /\ B & not x in A & not x in B & A /\ B is finite
           by A96,A5,A8,A53,A56,CARD_4:4,A99,XBOOLE_0:def 4,ZFMISC_1:37;then
A101: Card((A /\ B) \/ {x}) = (k - 1) + 1 & (A /\ B) \/ {x} c= X
           by A96,A58,CARD_2:54,XBOOLE_1:8;then
      (A /\ B) \/ {x} in the Points of G_(k,X) by A6;then
      consider C being POINT of G_(k,X) such that
A102:        C = (A /\ B) \/ {x};
A103:  A /\ {x} = {} & B /\ {x} = {}
      proof
        assume
        A /\ {x} <> {} or B /\ {x} <> {};then
        consider z1 being set such that
A104:        z1 in A /\ {x} or z1 in B /\ {x} by XBOOLE_0:def 1;
        (z1 in A & z1 in {x}) or (z1 in B & z1 in {x}) by A104,XBOOLE_0:def 3;
        hence contradiction by A100,TARSKI:def 1;
      end;
A105:  A /\ B c= C & A /\ B c= A & A /\ B c= B by A102,XBOOLE_1:7,XBOOLE_1:17;
      A /\ C = (A /\ {x}) \/ (A /\ (A /\ B)) &
         B /\ C = (B /\ {x}) \/ (B /\ (B /\ A)) by A102,XBOOLE_1:23;then
A106:  A /\ C = (A /\ {x}) \/ (A /\ A /\ B) &
         B /\ C = (B /\ {x}) \/ (B /\ B /\ A) by XBOOLE_1:16;
A107:  Card A = (k - 1) + 1 & Card B = (k - 1) + 1 &
         Card(A \/ B) = (k - 1) + 2*1 by A50,A51,A57,XBOOLE_0:def 10;then
A108:  Card(A /\ B) = k - 1 & A c= A \/ B & B c= B \/ C & C c= A \/ C
               by A5,Th2,XBOOLE_1:7;
      Card(A /\ C) = k - 1 & Card(B /\ C) = k - 1 by A107,A106,A103,A5,Th2;then
A109:  Card(A \/ C) = (k - 1) + 2*1 & Card(B \/ C) = (k - 1) + 2*1 &
        A \/ C c= X & B \/ C c= X & C is finite
           by A5,A8,A50,A51,A101,A102,Th2,XBOOLE_1:8,CARD_4:4;then
      A \/ C in the Lines of G_(k,X) by A7;then
      consider L1 being LINE of G_(k,X) such that
A110:      L1 = A \/ C;
      B \/ C in the Lines of G_(k,X) by A7,A109;then
      consider L2 being LINE of G_(k,X) such that
A111:      L2 = B \/ C;
A112:  A /\ B is finite & A /\ C is finite & B /\ C is finite & C is finite
            by A8,A101,A102,A54,FINSET_1:15,CARD_4:4;then
A113:  Card(A \ (A /\ B)) = k - (k - 1) & Card(B \ (A /\ B)) = k - (k - 1)
         by A105,A108,A54,A50,A51,CARD_2:63;then
      consider z1 being set such that
A114:       A \ (A /\ B) = {z1} by CARD_2:60;
      consider z2 being set such that
A115:          B \ (A /\ B) = {z2} by A113,CARD_2:60;
A116:  A = (A /\ B) \/ {z1} & B = (A /\ B) \/ {z2}
          by A105,A114,A115,XBOOLE_1:45;
A117:  z1 in {z1} & z2 in {z2} by TARSKI:def 1;then
A118:  z1 in A & not z1 in A /\ B & z2 in B & not z2 in A /\ B
            by A114,A115,XBOOLE_0:def 4;
A119:  C <> A & C <> B
      proof
        assume
A120:        A = C or B = C;
        {x} c= C by A102,XBOOLE_1:11;
        hence contradiction by A120,A100,ZFMISC_1:37;
      end;
A121: {A,B,C} is clique
      proof
        let Z1,Z2 be POINT of G_(k,X);
        assume Z1 in {A,B,C} & Z2 in {A,B,C};then
        (Z1 = A or Z1 = B or Z1 = C) & (Z2 = A or Z2 = B or Z2 = C)
             by ENUMSET1:def 1;then
        (Z1 c= A \/ B & Z2 c= A \/ B) or (Z1 c= A \/ C & Z2 c= A \/ C) or
            (Z1 c= B \/ C & Z2 c= B \/ C) by XBOOLE_1:11;then
        (Z1 on L & Z2 on L) or (Z1 on L1 & Z2 on L1) or (Z1 on L2 & Z2 on L2)
            by A4,A59,A110,A111,Th10;then
        {Z1,Z2} on L or {Z1,Z2} on L1 or {Z1,Z2} on L2 by INCSP_1:11;
        hence thesis;
      end;
A122:  3 <=` Card K
      proof
        assume not 3 <=` Card K;then
        Card K in 3 by ORDINAL1:26;then
        Card K c= 2 by A8,ORDINAL1:34;then
A123:   Card K = 2 by A43,XBOOLE_0:def 10;then
        K is finite & {A,B} is finite & A in {A,B,C} & B in {A,B,C} &
           not C in {A,B} & C in {A,B,C}
               by A8,A119,CARD_4:4,ENUMSET1:def 1,TARSKI:def 2;then
        K = {A,B} & {A,B} c= {A,B,C} & not {A,B,C} c= {A,B}
           by A44,A45,A48,A49,A123,CARD_FIN:1,ZFMISC_1:38;
        hence contradiction by A9,A121,Def3;
      end;
      Card {A,B} <> Card K
      proof
        assume Card{A,B} = Card K;then
        3 in 3 by A122,A8,A44,A45,A48,A49,ORDINAL1:34;
        hence contradiction;
      end;then
      Card{A,B} <` Card K by A44,A45,A48,A49,A43,CARD_1:13;then
      K \ {A,B} <> {} by CARD_2:4;then
      consider E2 being set such that
A124:      E2 in K \ {A,B} by XBOOLE_0:def 1;
A125:  E2 in K & not E2 in {A,B} by A124,XBOOLE_0:def 4;then
A126:  E2 in the Points of G_(k,X) & E2 <> A & E2 <> B by TARSKI:def 2;then
      consider E1 being Subset of X such that
A127:      E1 = E2 & Card E1 = k by A6;
      consider E being POINT of G_(k,X) such that
A128:       E = E1 by A127,A124;
      consider K1 being LINE of G_(k,X) such that
A129:       {A,E} on K1 by A10,A127,A128,A44,A48,A46,A125,Def2;
      K1 in the Lines of G_(k,X);then
      consider M1 being Subset of X such that
A130:       K1 = M1 & Card M1 = k + 1 by A7;
      consider K2 being LINE of G_(k,X) such that
A131:       {B,E} on K2 by A10,A127,A128,A44,A49,A46,A125,Def2;
      K2 in the Lines of G_(k,X);then
      consider M2 being Subset of X such that
A132:       K2 = M2 & Card M2 = k + 1 by A7;
      A on K1 & E on K1 & B on K2 & E on K2 by A129,A131,INCSP_1:11;then
      A c= K1 & E c= K1 & B c= K2 & E c= K2 by A4,Th10;then
      A \/ E c= K1 & B \/ E c= K2 by XBOOLE_1:8;then
A133:   Card(A \/ E) <=` k + 1 & Card(B \/ E) <=` k + 1 & not E on L &
          k + 1 <=` Card(A \/ E) & k + 1 <=` Card(B \/ E)
        by A125,A126,A128,A94,A50,A51,A127,A130,A132,CARD_1:27,Th1;then
      Card(A \/ E) = (k - 1) + 2*1 & Card(B \/ E) = (k - 1) + 2*1 &
         Card A = (k - 1) + 1 & Card B = (k - 1) + 1 & Card E = (k - 1) + 1
            by A127,A128,A50,A51,XBOOLE_0:def 10;then
      Card(A /\ E) = (k + 1) - 2 & Card(B /\ E) = (k + 1) - 2 &
         Card A = (k + 1) - 1 & Card B = (k + 1) - 1 & Card E = (k + 1) - 1 &
           2 + 1 <= k + 1 & 1 + 1 <= k + 1
             & Card(A /\ B) = (k + 1) - 2 by A1,A2,A95,A5,Th2,XREAL_1:9;then
A134:  (Card(A /\ B /\ E) = (k + 1) - 2 & Card(A \/ B \/ E) = (k + 1) + 1) or
        (Card(A /\ B /\ E) = (k + 1) - 3 & Card(A \/ B \/ E) = k + 1) by Th7;
A135:  not Card(A \/ B \/ E) = k + 1
      proof
        assume
A136:        Card(A \/ B \/ E) = k + 1;then
        A \/ B c= A \/ B \/ E & A \/ B \/ E is finite
              by A8,CARD_4:4,XBOOLE_1:7;then
A137:    A \/ B = A \/ B \/ E by A58,A136,CARD_FIN:1;
        E c= A \/ B \/ E by XBOOLE_1:7;then
        E c= L by A56,A137,XBOOLE_1:1;
        hence contradiction by A133,A4,Th10;
      end;then
A138:  Card(A /\ B /\ E) = k - 1 & Card(A \/ B \/ E) = k + 2 &
          A /\ B /\ E c= A /\ B by A134,XBOOLE_1:17;
      A /\ B /\ E is finite by A5,A8,CARD_4:4,A134,A135;then
      A /\ B = A /\ B /\ E by A100,A108,A138,CARD_FIN:1;then
A139:  A /\ B c= E & E is finite by A127,A128,CARD_4:4,A8,XBOOLE_1:17;then
      Card(E \ (A /\ B)) = k - (k - 1) by A127,A128,A108,A112,CARD_2:63;then
      consider z4 being set such that
A140:       E \ (A /\ B) = {z4} by CARD_2:60;
A141:  E = (A /\ B) \/ {z4} by A140,A139,XBOOLE_1:45;
A142: K c= S
      proof
        assume not K c= S;then
        consider D2 being set such that
A143:         D2 in K & not D2 in S by TARSKI:def 3;
        D2 in the Points of G_(k,X) by A143;then
        consider D1 being Subset of X such that
A144:          D1 = D2 & Card D1 = k by A6;
        consider D being POINT of G_(k,X) such that
A145:          D = D1 by A143,A144;
        not A /\ B c= D by A97,A143,A144,A145;then
        consider y being set such that
A146:        y in A /\ B & not y in D by TARSKI:def 3;
        consider K11 being LINE of G_(k,X) such that
A147:       {A,D} on K11 by A10,A143,A144,A145,A44,A48,A46,Def2;
        consider K12 being LINE of G_(k,X) such that
A148:       {B,D} on K12 by A10,A143,A144,A145,A44,A49,A46,Def2;
        consider K13 being LINE of G_(k,X) such that
A149:      {E,D} on K13 by A10,A125,A127,A128,A143,A144,A145,Def2;
        K11 in the Lines of G_(k,X);then
        consider R11 being Subset of X such that
A150:         R11 = K11 & Card R11 = k + 1 by A7;
        K12 in the Lines of G_(k,X);then
        consider R12 being Subset of X such that
A151:         R12 = K12 & Card R12 = k + 1 by A7;
        K13 in the Lines of G_(k,X);then
        consider R13 being Subset of X such that
A152:         R13 = K13 & Card R13 = k + 1 by A7;
A153:    A <> D & B <> D & E <> D by A143,A144,A145,A97,A105,A139;
        A on K11 & D on K11 & B on K12 & D on K12 & E on K13 & D on K13
             by A147,A148,A149,INCSP_1:11;then
        A c= K11 & D c= K11 & B c= K12 & D c= K12 & E c= K13 & D c= K13
               by A4,Th10;then
        A \/ D c= K11 & B \/ D c= K12 & E \/ D c= K13 by XBOOLE_1:8;then
        Card(A \/ D) <=` k + 1 & Card(B \/ D) <=` k + 1 &
          Card(E \/ D) <=` k + 1 & k + 1 <=` Card(A \/ D) &
            k + 1 <=` Card(B \/ D) & k + 1 <=` Card(E \/ D)
           by A50,A51,A127,A128,A144,A145,A150,A151,A152,A153,CARD_1:27,Th1;
then
        Card(A \/ D) = (k - 1) + 2*1 & Card(B \/ D) = (k - 1) + 2*1 &
          Card(E \/ D) = (k - 1) + 2*1 & Card D = (k - 1) + 1
                by A144,A145,XBOOLE_0:def 10;then
A154:    Card(A /\ D) = k - 1 & Card(B /\ D) = k - 1 & Card(E /\ D) = k - 1
          by A5,A127,A128,A50,A51,Th2;
        not A /\ B c= (A /\ B) /\ D & z4 in E \ (A /\ B)
             by A140,A146,XBOOLE_0:def 3,TARSKI:def 1;then
A155:   A /\ B <> (A /\ B) /\ D & (A /\ B) /\ D c= A /\ B & not z4 in A /\ B
            by XBOOLE_0:def 4,XBOOLE_1:17;
A156:    Card((A /\ B) /\ D) <> Card(A /\ B)
        proof
          assume
A157:         Card((A /\ B) /\ D) = Card(A /\ B);then
          (A /\ B) /\ D is finite by A5,A8,A108,CARD_4:4;
          hence contradiction by A155,A157,A112,CARD_FIN:1;
        end;
A158:   z1 in D & z2 in D & z4 in D
        proof
          assume
          not z1 in D or not z2 in D or not z4 in D;then
          (A /\ D = ((A /\ B) \/ {z1}) /\ D & {z1} misses D) or
               (B /\ D = ((A /\ B) \/ {z2}) /\ D & {z2} misses D) or
                 (E /\ D = ((A /\ B) \/ {z4}) /\ D & {z4} misses D)
                      by ZFMISC_1:56,A105,A114,A115,A140,A139,XBOOLE_1:45;then
          (A /\ D = ((A /\ B) /\ D) \/ ({z1} /\ D) & {z1} /\ D = {}) or
              (B /\ D = ((A /\ B) /\ D) \/ ({z2} /\ D) & {z2} /\ D = {}) or
                (E /\ D = ((A /\ B) /\ D) \/ ({z4} /\ D) & {z4} /\ D = {})
                  by XBOOLE_1:23,XBOOLE_0:def 7;
          hence contradiction by A156,A154,A107,A5,Th2;
        end;
A159:    (A /\ B) /\ D is finite by A155,A112,FINSET_1:13;then
        consider r being Element of NAT such that
A160:      Card((A /\ B) /\ D) = Card r by CARD_4:4;
A161:   Card((A /\ B) /\ D) = r by A160,CARD_1:66;
        A /\ D = ((A /\ B) \/ {z1}) /\ D by A105,A114,XBOOLE_1:45;then
        A /\ D = ((A /\ B) /\ D) \/ ({z1} /\ D) & {z1} /\ D = {z1}
              by A158,XBOOLE_1:23,ZFMISC_1:52;then
A162:   A /\ D = ((A /\ B) /\ D) \/ {z1} & not z1 in (A /\ B) /\ D &
             (A /\ B) /\ D c= D by A118,XBOOLE_1:17,XBOOLE_0:def 3;then
A163:    Card(A /\ D) = r + 1 by A161,A159,CARD_2:54;
A164:   Card((A /\ B) /\ D) = k - 2 by CARD_1:66,A160,A154,A163;
        {z1,z2} c= D & {z4} c= D by A158,ZFMISC_1:37,ZFMISC_1:38;then
        {z1,z2} \/ {z4} c= D by XBOOLE_1:8;then
A165:   {z1,z2,z4} c= D by ENUMSET1:43;
A166:   Card{z1,z2,z4} = 3 by A45,A48,A49,A116,CARD_2:77,A127,A128,A141,A126;
        {z1,z2,z4} misses (A /\ B) /\ D
        proof
          assume not {z1,z2,z4} misses (A /\ B) /\ D;then
          {z1,z2,z4} /\ ((A /\ B) /\ D) <> {} by XBOOLE_0:def 7;then
          consider m being set such that
A167:         m in {z1,z2,z4} /\ ((A /\ B) /\ D) by XBOOLE_0:def 1;
A168:     m in {z1,z2,z4} & m in (A /\ B) /\ D by A167,XBOOLE_0:def 3;then
          m = z1 or m = z2 or m = z4 by ENUMSET1:def 1;
          hence contradiction by A117,A114,A115,XBOOLE_0:def 4,A155,A168;
        end;then
A169:   Card(((A /\ B) /\ D) \/ {z1,z2,z4}) = (k - 2) + 3
               by A164,A166,A159,CARD_2:53;
        ((A /\ B) /\ D) \/ {z1,z2,z4} c= D by A162,A165,XBOOLE_1:8;then
        k + 1 c= k by A144,A145,A169,CARD_1:27;then
        k in k by A8,ORDINAL1:33;
        hence contradiction;
      end;
      S c= the Points of G_(k,X)
      proof
        let Z be set;
        assume Z in S;then
        consider Z1 being Subset of X such that
A170:       Z = Z1 & Card Z1 = k & A /\ B c= Z1 by A97;
        thus thesis by A170,A6;
      end;then
      consider S1 being Subset of the Points of G_(k,X) such that
A171:     S1 = S;
A172: S1 is STAR by A108,A97,A171,A58,Def4;then
      S1 is maximal_clique by A1,Th14; then
      S1 is clique by Def3;
      hence thesis by A172,A171,A142,A9,Def3;
    end;
    hence thesis by A60;
  end;

begin :: Automorphisms

definition
  let S1,S2 be IncProjStr;
  struct IncProjMap over S1,S2
     (#point-map -> Function of the Points of S1, the Points of S2,
       line-map -> Function of the Lines of S1, the Lines of S2#);
end;

definition
  let S1,S2 be IncProjStr;
  let F be IncProjMap over S1,S2;
  let a be POINT of S1;
  func F.a -> POINT of S2 equals (the point-map of F).a;
  coherence;
end;

definition
  let S1,S2 be IncProjStr;
  let F be IncProjMap over S1,S2;
  let L be LINE of S1;
  func F.L -> LINE of S2 equals (the line-map of F).L;
  coherence;
end;

theorem Th16:
  for S1,S2 being IncProjStr
  for F1,F2 being IncProjMap over S1,S2
  st (for A being POINT of S1 holds F1.A = F2.A) &
        (for L being LINE of S1 holds F1.L = F2.L)
  holds the IncProjMap of F1 = the IncProjMap of F2
  proof
    let S1,S2 be IncProjStr;
    let F1,F2 be IncProjMap over S1,S2;
    assume
A1:  (for A being POINT of S1 holds F1.A = F2.A) &
       (for L being LINE of S1 holds F1.L = F2.L);
A2: for a being set holds (a in the Points of S1 implies
       (the point-map of F1).a = (the point-map of F2).a)
    proof
      let a be set;
      assume a in the Points of S1;then
      consider A being POINT of S1 such that
A3:      A = a;
      F1.A = F2.A by A1;
      hence thesis by A3;
    end;
    for l being set holds (l in the Lines of S1 implies
       (the line-map of F1).l = (the line-map of F2).l)
    proof
      let l be set;
      assume l in the Lines of S1;then
      consider L being LINE of S1 such that
A4:      L = l;
      F1.L = F2.L by A1;
      hence thesis by A4;
    end;then
    the point-map of F1 = the point-map of F2 &
       the line-map of F1 = the line-map of F2 by A2,FUNCT_2:18;
    hence thesis;
  end;

definition
  let S1,S2 be IncProjStr;
  let F be IncProjMap over S1,S2;
  attr F is incidence_preserving means :Def8:
     for A1 being POINT of S1 for L1 being LINE of S1 holds
       (A1 on L1 iff F.A1 on F.L1);
end;

theorem
  for S1,S2 being IncProjStr
  for F1,F2 being IncProjMap over S1,S2
  st the IncProjMap of F1 = the IncProjMap of F2
  holds F1 is incidence_preserving implies F2 is incidence_preserving
  proof
    let S1,S2 be IncProjStr;
    let F1,F2 be IncProjMap over S1,S2;
    assume
A1:  the IncProjMap of F1 = the IncProjMap of F2 & F1 is incidence_preserving;
    let A1 be POINT of S1;
    let L1 be LINE of S1;
    F2.A1 = F1.A1 & F2.L1 = F1.L1 by A1;
    hence thesis by A1,Def8;
  end;

definition
  let S be IncProjStr;
  let F be IncProjMap over S,S;
  attr F is automorphism means :Def9:
    (the line-map of F is bijective &
     the point-map of F is bijective &
      F is incidence_preserving);
end;

definition
  let S1,S2 be IncProjStr;
  let F be IncProjMap over S1,S2;
  let K be Subset of the Points of S1;
  func F.:K -> Subset of the Points of S2 equals (the point-map of F).:K;
  coherence
  proof
    (the point-map of F).:K c= the Points of S2
    proof
      let b be set;
      assume b in (the point-map of F).:K;then
      consider a being set such that
A1:    a in dom (the point-map of F) & a in K & b = (the point-map of F).a
              by FUNCT_1:def 12;
      thus thesis by A1,FUNCT_2:7;
    end;
    hence thesis;
  end;
end;

definition
  let S1,S2 be IncProjStr;
  let F be IncProjMap over S1,S2;
  let K be Subset of the Points of S2;
  func F"K -> Subset of the Points of S1 equals (the point-map of F)"K;
  coherence
  proof
    (the point-map of F)"K c= the Points of S1
    proof
      let b be set;
      assume b in (the point-map of F)"K;then
      b in dom (the point-map of F) & (the point-map of F).b in K
            by FUNCT_1:def 13;
      hence thesis;
    end;
    hence thesis;
  end;
end;

definition
  let X be set;
  let A be finite set;
  func ^^(A,X) -> Subset of bool X equals
        {B where B is Subset of X: Card B = (card A)+1 & A c= B};
  coherence
  proof
    set Y = {B where B is Subset of X: Card B = (card A) + 1 & A c= B};
    Y c= bool X
    proof
      let x be set such that
A1:     x in Y;
      consider B1 being Subset of X such that
A2:    x = B1 & Card B1 = (card A) + 1 & A c= B1 by A1;
      thus thesis by A2;
    end;
    hence thesis;
  end;
end;

definition
  let k be Element of NAT;
  let X be non empty set such that
A1:  0 < k & k + 1 <=` Card X;
  let A be finite set such that
A2: Card A = k - 1 & A c= X;
  func ^^(A,X,k) -> Subset of the Points of G_(k,X) equals
:Def13: ^^(A,X);
  coherence
  proof
A3: the Points of G_(k,X) = {B where B is Subset of X: Card B = k} by A1,Def1;
    ^^(A,X) c= the Points of G_(k,X)
    proof
      let x be set such that
A4:    x in ^^(A,X);
      consider B1 being Subset of X such that
A5:    x = B1 & Card B1 = (card A) + 1 & A c= B1 by A4;
      thus thesis by A2,A3,A5;
    end;
    hence thesis;
  end;
end;

theorem Th18:
  for S1,S2 being IncProjStr
  for F being IncProjMap over S1,S2
  for K being Subset of the Points of S1 holds
  F.:K =
     {B where B is POINT of S2:ex A being POINT of S1 st (A in K & F.A = B)}
  proof
    let S1,S2 be IncProjStr;
    let F be IncProjMap over S1,S2;
    let K be Subset of the Points of S1;
    set Image =
     {B where B is POINT of S2:ex A being POINT of S1 st (A in K & F.A = B)};
A1: F.:K c= Image
    proof
      let b be set;
      assume b in F.:K;then
      consider a being set such that
A2:     a in dom (the point-map of F) & a in K & b = (the point-map of F).a
           by FUNCT_1:def 12;
      consider A being POINT of S1 such that
A3:      a = A by A2;
      b in the Points of S2 by A2,FUNCT_2:7;then
      consider B1 being POINT of S2 such that
A4:      b = B1;
      F.A = B1 by A2,A3,A4;
      hence thesis by A2,A3;
    end;
    Image c= F.:K
    proof
      let b be set;
      assume b in Image;then
      consider B being POINT of S2 such that
A5:     B = b & (ex A being POINT of S1 st A in K & F.A = B);
      the Points of S1 = dom (the point-map of F) by FUNCT_2:def 1;
      hence thesis by A5,FUNCT_1:def 12;
    end;
    hence thesis by A1,XBOOLE_0:def 10;
  end;

theorem
  for S1,S2 being IncProjStr
  for F being IncProjMap over S1,S2
  for K being Subset of the Points of S2 holds
  F"K =
    {A where A is POINT of S1:ex B being POINT of S2 st (B in K & F.A = B)}
  proof
    let S1,S2 be IncProjStr;
    let F be IncProjMap over S1,S2;
    let K be Subset of the Points of S2;
    set Image =
      {A where A is POINT of S1:ex B being POINT of S2 st (B in K & F.A = B)};
A1: F"K c= Image
    proof
      let a be set;
      assume
A2:      a in F"K;then
A3:   a in dom (the point-map of F) & (the point-map of F).a in K
          by FUNCT_1:def 13;
      consider A being POINT of S1 such that
A4:      a = A by A2;
      consider B1 being POINT of S2 such that
A5:      (the point-map of F).a = B1 by A3;
      F.A = B1 by A4,A5;
      hence thesis by A3,A4;
    end;
    Image c= F"K
    proof
      let a be set;
      assume a in Image;then
      consider A being POINT of S1 such that
A6:     A = a & (ex B being POINT of S2 st B in K & F.A = B);
      the Points of S1 = dom (the point-map of F) by FUNCT_2:def 1;
      hence thesis by A6,FUNCT_1:def 13;
    end;
    hence thesis by A1,XBOOLE_0:def 10;
  end;

theorem Th20:
  for S being IncProjStr
  for F being IncProjMap over S,S
  for K being Subset of the Points of S holds
   (F is incidence_preserving & K is clique) implies
    (F.:K is clique)
  proof
    let S be IncProjStr;
    let F be IncProjMap over S,S;
    let K be Subset of the Points of S;
    assume
A1:   F is incidence_preserving & K is clique;
A2: F.:K =
 {B where B is POINT of S:ex A being POINT of S st (A in K & F.A = B)} by Th18;
    let B1,B2 be POINT of S;
    assume
A3:    B1 in F.:K & B2 in F.:K;then
    consider B11 being POINT of S such that
A4:    B1 = B11 & (ex A being POINT of S st A in K & F.A = B11) by A2;
    consider B12 being POINT of S such that
A5:   B2 = B12 & (ex A being POINT of S st A in K & F.A = B12) by A2,A3;
    consider A11 being POINT of S such that
A6:    A11 in K & F.A11 = B11 by A4;
    consider A12 being POINT of S such that
A7:    A12 in K & F.A12 = B12 by A5;
    consider L1 being LINE of S such that
A8:    {A11,A12} on L1 by A1,A6,A7,Def2;
    A11 on L1 & A12 on L1 by A8,INCSP_1:11;then
    F.A11 on F.L1 & F.A12 on F.L1 by A1,Def8;then
    {B1,B2} on F.L1 by A4,A5,A6,A7,INCSP_1:11;
    hence thesis;
  end;

theorem Th21:
  for S being IncProjStr
  for F being IncProjMap over S,S
  for K being Subset of the Points of S holds
   (F is incidence_preserving & the line-map of F is onto & K is clique)
       implies (F"K is clique)
  proof
    let S be IncProjStr;
    let F be IncProjMap over S,S;
    let K be Subset of the Points of S;
    assume
A1:   F is incidence_preserving & the line-map of F is onto & K is clique;
A2: the Points of S = dom (the point-map of F) &
     the Lines of S = rng(the line-map of F) &
      the Lines of S = dom (the line-map of F) by A1,FUNCT_2:67,FUNCT_2:def 3;
    let A1,A2 be POINT of S such that
A3:   A1 in F"K & A2 in F"K;
    A1 in dom(the point-map of F) & F.A1 in K & A2 in dom(the point-map of F)
      & F.A2 in K by A3,FUNCT_1:def 13;then
    consider L2 being LINE of S such that
A4:   {F.A1,F.A2} on L2 by A1,Def2;
A5:F.A1 on L2 & F.A2 on L2 & L2 in rng(the line-map of F)
       by A2,A4,INCSP_1:11;
    consider l1 being set such that
A6:  l1 in dom(the line-map of F) & L2 = (the line-map of F).l1
           by A2,FUNCT_1:def 5;
    consider L1 being LINE of S such that
A7:    L1 = l1 by A6;
    L2 = F.L1 by A6,A7;then
    A1 on L1 & A2 on L1 by A1,A5,Def8;then
    {A1,A2} on L1 by INCSP_1:11;
    hence thesis;
  end;

theorem Th22:
  for S being IncProjStr
  for F being IncProjMap over S,S
  for K being Subset of the Points of S holds
   (F is automorphism & K is maximal_clique) implies
        (F.:K is maximal_clique & F"K is maximal_clique)
  proof
    let S be IncProjStr;
    let F be IncProjMap over S,S;
    let K be Subset of the Points of S;
    assume
A1:   F is automorphism & K is maximal_clique;
    F.:K =
      {B where B is POINT of S:ex A being POINT of S st (A in K & F.A = B)}
       & the line-map of F is bijective & the point-map of F is bijective
             by Th18,A1,Def9;then
A2: K is clique & F is incidence_preserving & the line-map of F is onto &
       the point-map of F is onto by A1,Def3,Def9,FUNCT_2:def 4;then
A3: F.:K is clique & F"K is clique &
      the Points of S = dom (the point-map of F) &
       the Lines of S = rng(the line-map of F) &
         the Lines of S = dom (the line-map of F)
         & the Points of S = rng(the point-map of F)
               by Th20,Th21,FUNCT_2:67,FUNCT_2:def 3;
A4: for U being Subset of the Points of S st U is clique & F.:K c= U holds
            U = F.:K
    proof
      let U be Subset of the Points of S such that
A5:      U is clique & F.:K c= U;
A6:  F"U is clique by A2,A5,Th21;
A7:  F"(F.:K) c= F"U & K c= dom(the point-map of F) by A3,A5,RELAT_1:178;
      K c= F"(F.:K) by A3,FUNCT_1:146;then
      K c= F"U by A7,XBOOLE_1:1;then
      F.:(F"U) c= F.:K & U c= rng(the point-map of F) by A1,A6,Def3,A3;then
      U c= F.:K by FUNCT_1:147;
      hence thesis by A5,XBOOLE_0:def 10;
    end;
    for U being Subset of the Points of S st U is clique & F"K c= U holds
           U = F"K
    proof
      let U be Subset of the Points of S such that
A8:      U is clique & F"K c= U;
A9:  F.:U is clique by A2,A8,Th20;
      F.:(F"K) c= F.:U & K c= rng(the point-map of F)
               by A3,A8,RELAT_1:156;then
      K c= F.:U by FUNCT_1:147;then
      F"(F.:U) c= F"K & U c= F"(F.:U) by A3,A1,A9,Def3,FUNCT_1:146;then
      U c= F"K by XBOOLE_1:1;
      hence thesis by A8,XBOOLE_0:def 10;
    end;
    hence thesis by A3,A4,Def3;
  end;

theorem Th23:
  for k being Element of NAT
  for X being non empty set st 2 <= k & k + 2 <=` Card X
  for F being IncProjMap over G_(k,X), G_(k,X) st F is automorphism
  for K being Subset of the Points of G_(k,X) holds
     K is STAR implies F.:K is STAR & F"K is STAR
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:    2 <= k & k + 2 <=` Card X;
    let F be IncProjMap over G_(k,X), G_(k,X) such that
A2:       F is automorphism;
    let K be Subset of the Points of G_(k,X);
A3: k + 1 <= k + 2 & 1 <= k & k + 0 <= k + 1 & 0 <= 1
         by A1,XXREAL_0:2,XREAL_1:9;then
    - 2 < 0 & k + 1 c= k + 2 & k + 0 <= k + 1 by CARD_1:56;then
A4: 2 - 2 < k & k + 1 <=` Card X & 2 - 1 <= k - 1 & 1 <= k + 1 &
      k - 1 is Element of NAT & k - 2 is Element of NAT
        by A1,A3,XREAL_1:11,XBOOLE_1:1,XXREAL_0:2,NAT_1:21;then
A5: 1 c= k + 1 & 1 c= k & 1 c= k - 1 & Card(k + 1) = k + 1 & Card 1 = 1 &
        Card k = k & succ(k - 1) = (k - 1) + 1 & succ 2 = 2 + 1
             by A3,CARD_1:52,CARD_1:56,CARD_1:66;
A6: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A4,Def1;
A7: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
            by A4,Def1;
    assume
A8:    K is STAR;then
    K is maximal_clique by A1,Th14;then
A9: F.:K is maximal_clique & F"K is maximal_clique & K is clique
            by A2,Th22,Def3;
A10: not F.:K is TOP
    proof
      assume F.:K is TOP;then
      consider B being Subset of X such that
A11:   Card B = k + 1 & F.:K = {A where A is Subset of X: Card A = k & A c= B}
                 by Def5;
      consider X1 being set such that
A12:       X1 c= B & Card X1 = 1 by A5,A11,CARD_FIL:36;
A13:  X1 is finite & B is finite by A5,A11,A12,CARD_4:4;then
A14:  Card(B \ X1) = (k + 1) - 1 by A11,A12,CARD_2:63;then
      consider X2 being set such that
A15:       X2 c= B \ X1 & Card X2 = 1 by A5,CARD_FIL:36;
A16:  X2 is finite & B \ X1 is finite by A5,A14,A15,CARD_4:4;then
      Card((B \ X1) \ X2) = k - 1 by A14,A15,CARD_2:63;then
      consider X3 being set such that
A17:       X3 c= (B \ X1) \ X2 & Card X3 = 1 by A5,CARD_FIL:36;
      X3 c= B \ (X2 \/ X1) & the point-map of F is bijective &
          the line-map of F is bijective by A2,A17,XBOOLE_1:41,Def9;then
A18:  X3 c= B & X2 c= B & X3 is finite & X3 c= (B \ X2) \ X1 &
         the point-map of F is onto & the line-map of F is onto &
         F is incidence_preserving & the point-map of F is one-to-one
    by Def9,A2,A5,A15,A17,CARD_4:4,XBOOLE_1:41,XBOOLE_1:106,FUNCT_2:def 4;then
A19:  Card(B \ X2) = (k + 1) - 1 & Card(B \ X3) = (k + 1) - 1 & B \ X1 c= X &
       B \ X2 c= X & B \ X3 c= X & X3 c= B \ X2 & X3 c= B \ X1 &
        X1 misses X2 & X2 misses X3 & X1 misses X3 & B \ X1 c= B &
        B \ X2 c= B & B \ X3 c= B &
         the Points of G_(k,X) = rng(the point-map of F) &
          the Lines of G_(k,X) = rng(the line-map of F) &
            the Points of G_(k,X) = dom(the point-map of F) &
             the Lines of G_(k,X) = dom(the line-map of F)
    by A11,A13,A15,A16,A17,CARD_2:63,FUNCT_2:def 3,FUNCT_2:67,XBOOLE_1:106;then
A20:  B \ X1 in the Points of G_(k,X) & B \ X2 in the Points of G_(k,X) &
         B \ X3 in the Points of G_(k,X) & B in the Lines of G_(k,X)
            by A11,A14,A6,A7;
A21:  B \ X1 <> B \ X2 & B \ X2 <> B \ X3 & B \ X1 <> B \ X3
      proof
        assume B \ X1 = B \ X2 or B \ X2 = B \ X3 or B \ X1 = B \ X3;then
        X2 c= B \ X2 or X3 c= B \ X3 or X3 c= B \ X3
          by A18,A15,A17,XBOOLE_1:106;then
        X2 = {} or X3 = {} or X3 = {} by XBOOLE_1:38;
        hence contradiction by A15,A17,CARD_4:17;
      end;
      consider B1 being POINT of G_(k,X) such that
A22:      B \ X1 = B1 by A20;
      consider B2 being POINT of G_(k,X) such that
A23:      B \ X2 = B2 by A20;
      consider B3 being POINT of G_(k,X) such that
A24:      B \ X3 = B3 by A20;
      consider L2 being LINE of G_(k,X) such that
A25:       B = L2 by A20;
A26:  B1 on L2 & B2 on L2 & B3 on L2 & B1 in rng(the point-map of F) &
        B2 in rng(the point-map of F) & B3 in rng(the point-map of F) &
            L2 in rng(the line-map of F) by A4,A19,A22,A23,A24,A25,Th10;
      consider a1 being set such that
A27:        a1 in dom(the point-map of F) & B1 = (the point-map of F).a1
           by FUNCT_1:def 5,A19;
      consider a2 being set such that
A28:    a2 in dom(the point-map of F) & B2 = (the point-map of F).a2
          by A19,FUNCT_1:def 5;
      consider a3 being set such that
A29:     a3 in dom(the point-map of F) & B3 = (the point-map of F).a3
              by A19,FUNCT_1:def 5;
      consider l1 being set such that
A30:        l1 in dom(the line-map of F) & L2 = (the line-map of F).l1
            by A19,FUNCT_1:def 5;
      consider A1 being POINT of G_(k,X) such that
A31:      a1 = A1 by A27;
      consider A2 being POINT of G_(k,X) such that
A32:      a2 = A2 by A28;
      consider A3 being POINT of G_(k,X) such that
A33:      a3 = A3 by A29;
      consider L1 being LINE of G_(k,X) such that
A34:       l1 = L1 by A30;
A35:  B1 = F.A1 & B2 = F.A2 & B3 = F.A3 & L2 = F.L1
          by A27,A28,A29,A30,A31,A32,A33,A34;then
      A1 on L1 & A2 on L1 & A3 on L1 by A18,A26,Def8;then
A36:  A1 c= L1 & A2 c= L1 & A3 c= L1 & A1 <> A2 & A2 <> A3 & A1 <> A3 &
        B1 in F.:K & B2 in F.:K & B3 in F.:K
            by A4,A11,A14,A19,A21,A22,A23,A24,A27,A28,A29,A31,A32,A33,Th10;then
      consider C1 being set such that
A37:  C1 in dom(the point-map of F) & C1 in K & B1 = (the point-map of F).C1
          by FUNCT_1:def 12;
      consider C2 being set such that
A38:    C2 in dom(the point-map of F) & C2 in K & B2 = (the point-map of F).C2
          by A36,FUNCT_1:def 12;
      consider C3 being set such that
A39:    C3 in dom(the point-map of F) & C3 in K & B3 = (the point-map of F).C3
          by A36,FUNCT_1:def 12;
A40:  A1 in K & A2 in K & A3 in K
         by A18,A27,A28,A29,A31,A32,A33,A37,A38,A39,FUNCT_1:def 8;
      consider L3a being LINE of G_(k,X) such that
A41:        {A1,A2} on L3a by A40,A9,Def2;
      consider L3b being LINE of G_(k,X) such that
A42:        {A2,A3} on L3b by A40,A9,Def2;
      consider S being Subset of X such that
A43:     Card S = k - 1 & K = {A where A is Subset of X: Card A = k & S c= A}
                by A8,Def4;
      consider A11 being Subset of X such that
A44:     A1 = A11 & Card A11 = k & S c= A11 by A40,A43;
      consider A12 being Subset of X such that
A45:     A2 = A12 & Card A12 = k & S c= A12 by A40,A43;
      consider A13 being Subset of X such that
A46:     A3 = A13 & Card A13 = k & S c= A13 by A40,A43;
      consider m being Nat such that
A47:          m = k - 1 by A4;
      L1 in the Lines of G_(k,X);then
      consider l12 being Subset of X such that
A48:     L1 = l12 & Card l12 = k + 1 by A7;
      S c= A1 /\ A2 & Card(A1 /\ A2) <` k & A1 \/ A2 c= L1 & A1 \/ A3 c= L1 &
          A2 \/ A3 c= L1 & 2 c= k
            by A1,A36,A44,A45,XBOOLE_1:8,XBOOLE_1:19,Th1,CARD_1:56;then
      S c= (A1 /\ A2) /\ A3 & Card(A1 /\ A2) in succ(k - 1) &
        (A1 /\ A2) /\ A3 c= A1 /\ A2 & Card(A1 \/ A2) <=` k + 1 &
           Card(A1 \/ A3) <=` k + 1 & Card(A2 \/ A3) <=` k + 1 &
            k + 1 <=` Card(A1 \/ A2) & k + 1 <=` Card(A1 \/ A3) &
            k + 1 <=` Card(A2 \/ A3) & 2 in succ k
             by A5,A36,A44,A45,A46,A48,XBOOLE_1:17,XBOOLE_1:19,CARD_1:27,
                 Th1,ORDINAL1:34;then
A49:  m <=` Card(A1 /\ A2 /\ A3) & Card(A1 /\ A2) <=` m &
        Card(A1 \/ A2) = k + 1 & Card(A2 \/ A3) = k + 1 &
         Card(A1 \/ A3) = k + 1 & Card(A1 /\ A2 /\ A3) <=` Card(A1 /\ A2) &
           (2 in k or k = 2)
             by A43,A47,CARD_1:27,ORDINAL1:34,XBOOLE_0:def 10,ORDINAL1:13;then
      Card(A1 /\ A2 /\ A3) <=` m & (3 c= k or k = 2)
                by A5,XBOOLE_1:1,ORDINAL1:33;then
A50:  k - 1 = Card(A1 /\ A2 /\ A3) & Card(A1 \/ A2) = (k - 1) + 2*1 &
        Card A1 = (k - 1) + 1 & Card(A2 \/ A3) = (k - 1) + 2*1 &
         Card(A1 \/ A3) = (k - 1) + 2*1 & Card A2 = (k - 1) + 1 &
           Card A3 = (k - 1) + 1 by A44,A45,A46,A47,A49,XBOOLE_0:def 10;then
      Card(A1 /\ A2) = (k + 1) - 2 & Card A1 = (k + 1) - 1 &
        Card(A2 /\ A3) = (k + 1) - 2 & Card(A1 /\ A3) = (k + 1)  - 2 &
          Card A2 = (k + 1) - 1 & Card A3 = (k + 1) - 1 &
            2 + 1 <= k + 1 & 2 <= k + 1 & k - 1 <> (k + 1) - 3 & A1 on L3a &
             A2 on L3b & A3 on L3b
                 by A1,A3,A4,A41,A42,Th2,INCSP_1:11,XXREAL_0:2,XREAL_1:8;then
A51:  Card(A1 \/ A2 \/ A3) = (k + 1) + 1 & Card(A1 /\ A2 /\ A3) = (k + 1) - 2
          & A1 c= L3a & A2 c= L3b & A3 c= L3b by A4,A50,Th10,Th7;
      L3b in the Lines of G_(k,X);then
      consider l13b being Subset of X such that
A52:     L3b = l13b & Card l13b = k + 1 by A7;
A53:  L3a <> L3b
      proof
        assume L3a = L3b;then
        A1 \/ A2 c= L3b by A51,XBOOLE_1:8;then
        (A1 \/ A2) \/ A3 c= L3b by A51,XBOOLE_1:8;then
        k + 2 <=` k + 1 by A51,A52,CARD_1:27;then
        k + 2 <= k + 1 by CARD_1:56;
        hence contradiction by XREAL_1:8;
      end;
      A1 on L1 & A2 on L1 & A3 on L1 by A18,A26,A35,Def8;then
      {A1,A2} on L1 & {A2,A3} on L1 by INCSP_1:11;then
      L1 = L3a & L1 = L3b
         by A21,A22,A23,A24,A27,A28,A29,A31,A32,A33,A41,A42,INCSP_1:def 10;
      hence contradiction by A53;
    end;
    not F"K is TOP
    proof
      assume F"K is TOP;then
      consider B being Subset of X such that
A54:   Card B = k + 1 & F"K = {A where A is Subset of X: Card A = k & A c= B}
              by Def5;
      consider X1 being set such that
A55:       X1 c= B & Card X1 = 1 by A5,A54,CARD_FIL:36;
A56:  X1 is finite & B is finite by A5,A54,A55,CARD_4:4;then
A57:  Card(B \ X1) = (k + 1) - 1 by A54,A55,CARD_2:63;then
      consider X2 being set such that
A58:       X2 c= B \ X1 & Card X2 = 1 by A5,CARD_FIL:36;
A59:  X2 is finite & B \ X1 is finite by A5,A57,A58,CARD_4:4;then
      Card((B \ X1) \ X2) = k - 1 by A57,A58,CARD_2:63;then
      consider X3 being set such that
A60:       X3 c= (B \ X1) \ X2 & Card X3 = 1 by A5,CARD_FIL:36;
      X3 c= B \ (X2 \/ X1) & the point-map of F is bijective &
          the line-map of F is bijective by A2,A60,XBOOLE_1:41,Def9;then
A61:  X3 c= B & X2 c= B & X3 is finite & X3 c= (B \ X2) \ X1 &
        the point-map of F is onto & the line-map of F is onto &
         F is incidence_preserving & the point-map of F is one-to-one &
            the point-map of F is Function-like
     by Def9,A2,A5,A58,A60,CARD_4:4,XBOOLE_1:41,XBOOLE_1:106,FUNCT_2:def 4;then
A62:  Card(B \ X2) = (k + 1) - 1 & Card(B \ X3) = (k + 1) - 1 & B \ X1 c= X &
        B \ X2 c= X & B \ X3 c= X & X3 c= B \ X2 & X3 c= B \ X1 &
        X1 misses X2 & X2 misses X3 & X1 misses X3 & B \ X1 c= B &
         B \ X2 c= B & B \ X3 c= B &
          the Points of G_(k,X) = rng(the point-map of F) &
          the Lines of G_(k,X) = rng(the line-map of F) &
            the Points of G_(k,X) = dom(the point-map of F) &
             the Lines of G_(k,X) = dom(the line-map of F)
   by A54,A56,A58,A59,A60,CARD_2:63,XBOOLE_1:106,FUNCT_2:def 3,FUNCT_2:67;then
A63:  B \ X1 in the Points of G_(k,X) & B \ X2 in the Points of G_(k,X) &
         B \ X3 in the Points of G_(k,X) & B in the Lines of G_(k,X)
              by A54,A57,A6,A7;
A64:  B \ X1 <> B \ X2 & B \ X2 <> B \ X3 & B \ X1 <> B \ X3
      proof
        assume B \ X1 = B \ X2 or B \ X2 = B \ X3 or B \ X1 = B \ X3;then
        X2 = {} or X3 = {} or X3 = {} by A58,A62,XBOOLE_1:38;
        hence contradiction by A58,A60,CARD_4:17;
      end;
      consider B1 being POINT of G_(k,X) such that
A65:      B \ X1 = B1 by A63;
      consider B2 being POINT of G_(k,X) such that
A66:      B \ X2 = B2 by A63;
      consider B3 being POINT of G_(k,X) such that
A67:      B \ X3 = B3 by A63;
      consider L2 being LINE of G_(k,X) such that
A68:       B = L2 by A63;
A69:  B1 on L2 & B2 on L2 & B3 on L2 & B1 in dom(the point-map of F) &
        B2 in dom(the point-map of F) & B3 in dom(the point-map of F) &
          F.B2 in the Points of G_(k,X) & L2 in dom(the line-map of F) &
           F.B1 in the Points of G_(k,X) & F.B3 in the Points of G_(k,X)
            & F.L2 in the Lines of G_(k,X) by A4,A62,A65,A66,A67,A68,Th10;
      consider A1 being POINT of G_(k,X) such that
A70:        A1 = F.B1;
      consider A2 being POINT of G_(k,X) such that
A71:       A2 = F.B2;
      consider A3 being POINT of G_(k,X) such that
A72:      A3 = F.B3;
      consider L1 being LINE of G_(k,X) such that
A73:        L1 = F.L2;
A74:  A1 on L1 & A2 on L1 & A3 on L1 by A61,A69,A70,A71,A72,A73,Def8;then
A75:  A1 c= L1 & A2 c= L1 & A3 c= L1 & B1 in F"K & A1 <> A2 & A2 <> A3 &
        A1 <> A3 & B2 in F"K & B3 in F"K
     by A4,A54,A57,A61,A62,A64,A65,A66,A67,A70,A71,A72,Th10,FUNCT_1:def 8;then
A76:  A1 in K & A2 in K & A3 in K by A70,A71,A72,FUNCT_1:def 13;
      consider L3a being LINE of G_(k,X) such that
A77:        {A1,A2} on L3a by A76,A9,Def2;
      consider L3b being LINE of G_(k,X) such that
A78:        {A2,A3} on L3b by A76,A9,Def2;
      consider S being Subset of X such that
A79:     Card S = k - 1 & K = {A where A is Subset of X: Card A = k & S c= A}
                by A8,Def4;
      consider A11 being Subset of X such that
A80:     A1 = A11 & Card A11 = k & S c= A11 by A76,A79;
      consider A12 being Subset of X such that
A81:     A2 = A12 & Card A12 = k & S c= A12 by A76,A79;
      consider A13 being Subset of X such that
A82:     A3 = A13 & Card A13 = k & S c= A13 by A76,A79;
      consider m being Nat such that
A83:          m = k - 1 by A4;
      L1 in the Lines of G_(k,X);then
      consider l12 being Subset of X such that
A84:     L1 = l12 & Card l12 = k + 1 by A7;
      S c= A1 /\ A2 & Card(A1 /\ A2) <` k & A1 \/ A2 c= L1 & A1 \/ A3 c= L1 &
        A2 \/ A3 c= L1 & 2 c= k
            by A1,A75,A80,A81,XBOOLE_1:8,XBOOLE_1:19,Th1,CARD_1:56;then
      S c= (A1 /\ A2) /\ A3 & Card(A1 /\ A2) in succ(k - 1) &
       (A1 /\ A2) /\ A3 c= A1 /\ A2 & Card(A1 \/ A2) <=` k + 1 &
         Card(A1 \/ A3) <=` k + 1 & Card(A2 \/ A3) <=` k + 1 &
         k + 1 <=` Card(A1 \/ A2) & k + 1 <=` Card(A1 \/ A3) &
          k + 1 <=` Card(A2 \/ A3) & 2 in succ k
              by A5,A75,A80,A81,A82,A84,XBOOLE_1:17,XBOOLE_1:19,
                  CARD_1:27,Th1,ORDINAL1:34;then
A85:  m <=` Card(A1 /\ A2 /\ A3) & Card(A1 /\ A2) <=` m &
        Card(A1 \/ A2) = k + 1 & Card(A2 \/ A3) = k + 1 &
          Card(A1 \/ A3) = k + 1 & Card(A1 /\ A2 /\ A3) <=` Card(A1 /\ A2) &
          (2 in k or k = 2)
             by A79,A83,CARD_1:27,ORDINAL1:34,XBOOLE_0:def 10,ORDINAL1:13;then
      Card(A1 /\ A2 /\ A3) <=` m & (3 c= k or k = 2)
            by A5,XBOOLE_1:1,ORDINAL1:33;then
A86:  k - 1 = Card(A1 /\ A2 /\ A3) & Card(A1 \/ A2) = (k - 1) + 2*1 &
        Card A1 = (k - 1) + 1 & Card(A2 \/ A3) = (k - 1) + 2*1 &
          Card(A1 \/ A3) = (k - 1) + 2*1 & Card A2 = (k - 1) + 1 &
          Card A3 = (k - 1) + 1 by A80,A81,A82,A83,A85,XBOOLE_0:def 10;then
      Card(A1 /\ A2) = (k + 1) - 2 & Card A1 = (k + 1) - 1 &
       Card(A2 /\ A3) = (k + 1) - 2 & Card(A1 /\ A3) = (k + 1)  - 2 &
        Card A2 = (k + 1) - 1 & Card A3 = (k + 1) - 1 & 2 + 1 <= k + 1 &
        2 <= k + 1 & k - 1 <> (k + 1) - 3 & A1 on L3a & A2 on L3b & A3 on L3b
                 by A1,A3,A4,A77,A78,Th2,INCSP_1:11,XXREAL_0:2,XREAL_1:8;then
A87:  Card(A1 \/ A2 \/ A3) = (k + 1) + 1 & Card(A1 /\ A2 /\ A3) = (k + 1) - 2
         & A1 c= L3a & A2 c= L3b & A3 c= L3b by A4,A86,Th10,Th7;
      L3b in the Lines of G_(k,X);then
      consider l13b being Subset of X such that
A88:     L3b = l13b & Card l13b = k + 1 by A7;
A89:  L3a <> L3b
      proof
        assume L3a = L3b;then
        A1 \/ A2 c= L3b by A87,XBOOLE_1:8;then
        (A1 \/ A2) \/ A3 c= L3b by A87,XBOOLE_1:8;then
        k + 2 <=` k + 1 by A87,A88,CARD_1:27;then
        k + 2 <= k + 1 by CARD_1:56;
        hence contradiction by XREAL_1:8;
      end;
      {A1,A2} on L1 & {A2,A3} on L1 by A74,INCSP_1:11;then
      L1 = L3a & L1 = L3b by A75,A77,A78,INCSP_1:def 10;
      hence contradiction by A89;
    end;
    hence thesis by A9,A1,Th15,A10;
  end;

definition
  let k be Element of NAT;
  let X be non empty set such that
A1:  0 < k & k + 1 <=` Card X;
  let s be Permutation of X;
 func incprojmap(k,s) -> strict IncProjMap over G_(k,X), G_(k,X) means :Def14:
     (for A being POINT of G_(k,X) holds it.A = s.:A) &
       (for L being LINE of G_(k,X) holds it.L = s.:L);
  existence
  proof
    deffunc F(set) = s.:$1;
A2: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A1,Def1;
A3: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
         by A1,Def1;
    consider P being Function such that
A4: dom P = the Points of G_(k,X) & for x st x in the Points of G_(k,X) holds
       P.x = F(x) from FUNCT_1:sch 3;
    consider L being Function such that
A5: dom L = the Lines of G_(k,X) & for x st x in the Lines of G_(k,X) holds
         L.x = F(x) from FUNCT_1:sch 3;
    rng P c= the Points of G_(k,X)
    proof
      let b be set;
      assume b in rng P;then
      consider a being set such that
A6:       a in the Points of G_(k,X) & b = P.a by A4,FUNCT_1:def 5;
      consider A being Subset of X such that
A7:      A = a & Card A = k by A6,A2;
      dom s = X by FUNCT_2:def 1;then
A8:   b = s.:A & A c= dom s by A4,A6,A7;then
A9:   Card b = k by A7,Th4;
      b c= X
      proof
        let y such that
A10:       y in b;
        consider x such that
A11:       x in dom s & x in A & y = s.x by A8,A10,FUNCT_1:def 12;
        y in rng s & rng s = X by A11,FUNCT_1:12,FUNCT_2:def 3;
        hence thesis;
      end;
      hence thesis by A9,A2;
    end;then
    reconsider P as Function of the Points of G_(k,X), the Points of G_(k,X)
            by A4,FUNCT_2:4;
    rng L c= the Lines of G_(k,X)
    proof
      let b be set;
      assume b in rng L;then
      consider a being set such that
A12:       a in the Lines of G_(k,X) & b = L.a by A5,FUNCT_1:def 5;
      consider A being Subset of X such that
A13:      A = a & Card A = k + 1 by A12,A3;
      dom s = X by FUNCT_2:def 1;then
A14:  b = s.:A & A c= dom s by A5,A12,A13;then
A15:  Card b = k + 1 by A13,Th4;
      b c= X
      proof
        let y such that
A16:       y in b;
        consider x such that
A17:       x in dom s & x in A & y = s.x by A14,A16,FUNCT_1:def 12;
        y in rng s & rng s = X by A17,FUNCT_1:12,FUNCT_2:def 3;
        hence thesis;
      end;
      hence thesis by A15,A3;
    end;then
    reconsider L as Function of the Lines of G_(k,X), the Lines of G_(k,X)
            by A5,FUNCT_2:4;
    take F = IncProjMap(#P,L#);
    thus thesis by A4,A5;
  end;
  uniqueness
  proof
    let f1,f2 be strict IncProjMap over G_(k,X), G_(k,X);
    assume
A18:  (for A being POINT of G_(k,X) holds f1.A = s.:A) &
       (for L being LINE of G_(k,X) holds f1.L = s.:L) &
        (for A being POINT of G_(k,X) holds f2.A = s.:A) &
        (for L being LINE of G_(k,X) holds f2.L = s.:L);
A19:for A being POINT of G_(k,X) holds f1.A = f2.A
    proof
      let A be POINT of G_(k,X);
      f1.A = s.:A & f2.A = s.:A by A18;
      hence thesis;
    end;
    for L being LINE of G_(k,X) holds f1.L = f2.L
    proof
      let L be LINE of G_(k,X);
      f1.L = s.:L & f2.L = s.:L by A18;
      hence thesis;
    end;
    hence thesis by A19,Th16;
  end;
end;

theorem Th24:
  for k being Element of NAT
  for X being non empty set st k = 1 & k + 1 <=` Card X
  for F being IncProjMap over G_(k,X), G_(k,X) st F is automorphism
  holds (ex s being Permutation of X st the IncProjMap of F = incprojmap(k,s))
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1: k = 1 & k + 1 <=` Card X;
A2: the Points of G_(k,X) = {A where A is Subset of X: Card A = 1} by A1,Def1;
A3: the Lines of G_(k,X) = {L where L is Subset of X: Card L = 1 + 1}
          by A1,Def1;
    let F be IncProjMap over G_(k,X), G_(k,X) such that
A4: F is automorphism;
    deffunc CH(set) = {$1};
    consider c being Function such that
A5: dom c = X and
A6: for x being set st x in X holds c.x = CH(x) from FUNCT_1:sch 3;
    rng c c= the Points of G_(k,X)
    proof
      let y be set;
      assume y in rng c;then
      consider x being set such that
A7:    x in dom c & y = c.x by FUNCT_1:def 5;
      {x} c= X & Card {x} = 1 & y = {x} by A5,A6,A7,CARD_1:79,ZFMISC_1:37;
      hence thesis by A2;
    end;then
    reconsider c as Function of X, the Points of G_(k,X) by A5,FUNCT_2:4;
    deffunc W(Element of X) = union (F.(c.$1));
    consider f being Function such that
A8:dom f = X and
A9:for x being Element of X holds f.x = W(x) from FUNCT_1:sch 4;
    rng f c= X
    proof
      let b be set;
      assume b in rng f;then
      consider a being set such that
A10:     a in X & b = f.a by A8,FUNCT_1:def 5;
      reconsider a as Element of X by A10;
A11:  b = union (F.(c.a)) by A9,A10;
      consider A being POINT of G_(k,X) such that
A12:     A = F.(c.a);
      A in the Points of G_(k,X);then
      consider A1 being Subset of X such that
A13:     A1 = A & Card A1 = 1 by A2;
      consider x being set such that
A14:      A = {x} by A13,CARD_2:60;
      x in X by A13,A14,ZFMISC_1:37;
      hence thesis by A11,A12,A14,ZFMISC_1:31;
    end;then
    reconsider f as Function of X,X by A8,FUNCT_2:4;
A15: the point-map of F is bijective &
      dom(the point-map of F) = the Points of G_(k,X) &
       dom(the line-map of F) = the Lines of G_(k,X) & 0 < k &
         F is incidence_preserving by A1,A4,Def9,FUNCT_2:67;then
A16: the point-map of F is one-to-one & the point-map of F is onto
            by FUNCT_2:def 4;then
A17: rng(the point-map of F) = the Points of G_(k,X) & Card 2 = 2 & Card 1 = 1
           by FUNCT_2:def 3,CARD_1:66;
    f is bijective
    proof
      thus f is one-to-one
      proof
        let x1,x2 be set such that
A18:          x1 in dom f & x2 in dom f & f.x1 = f.x2;
        reconsider x1,x2 as Element of X by A18;
A19:      f.x1 = union(F.(c.x1)) & f.x2 = union(F.(c.x2)) by A9;
        consider A1 being POINT of G_(k,X) such that
A20:     A1 = F.(c.x1);
        consider A2 being POINT of G_(k,X) such that
A21:     A2 = F.(c.x2);
        A1 in the Points of G_(k,X);then
        consider A11 being Subset of X such that
A22:        A11 = A1 & Card A11 = 1 by A2;
        consider y1 being set such that
A23:         A1 = {y1} by A22,CARD_2:60;
        A2 in the Points of G_(k,X);then
        consider A12 being Subset of X such that
A24:        A12 = A2 & Card A12 = 1 by A2;
        consider y2 being set such that
A25:         A2 = {y2} by A24,CARD_2:60;
        f.x1 = y1 & f.x2 = y2 by A19,A20,A21,A23,A25,ZFMISC_1:31;then
        c.x1 = c.x2 & c.x1 = {x1} & c.x2 = {x2}
             by A15,A18,A23,A25,A16,A6,A20,A21,FUNCT_1:def 8;
        hence thesis by ZFMISC_1:6;
      end;
      thus f is onto
      proof
        for y st y in X ex x st x in X & y = f.x
        proof
          let y be set;
          assume y in X;then
          {y} c= X & Card{y} = 1 by ZFMISC_1:37,CARD_1:79;then
          {y} in rng(the point-map of F) by A2,A17;then
          consider a being set such that
A26:         a in dom(the point-map of F) & {y} = (the point-map of F).a
                  by FUNCT_1:def 5;
A27:      a in the Points of G_(k,X) by A26;
          consider A1 being Subset of X such that
A28:         A1 = a & Card A1 = 1 by A2,A27;
          consider x being set such that
A29:          a = {x} by A28,CARD_2:60;
          reconsider x as Element of X by A28,A29,ZFMISC_1:37;
          {y} = F.(c.x) by A6,A26,A29;then
          y = union(F.(c.x)) by ZFMISC_1:31;then
          y = f.x by A9;
          hence thesis;
        end;then
        rng f = X by FUNCT_2:16;
        hence thesis by FUNCT_2:def 3;
      end;
    end;then
    reconsider f as Permutation of X;
    the IncProjMap of F = incprojmap(k,f)
    proof
A30:  the point-map of F = the point-map of incprojmap(k,f)
      proof
A31:    dom(the point-map of incprojmap(k,f)) = the Points of G_(k,X)
                by FUNCT_2:67;
        for x st x in dom(the point-map of F) holds
              (the point-map of F).x = (the point-map of incprojmap(k,f)).x
        proof
          let x be set;
          assume
A32:         x in dom(the point-map of F);then
          consider A being POINT of G_(k,X) such that
A33:      x = A;
          consider A1 being Subset of X such that
A34:      x = A1 & Card A1 = 1 by A2,A15,A32;
          consider x1 being set such that
A35:      x = {x1} by A34,CARD_2:60;
A36:      incprojmap(k,f).A = f.:A & x1 in X by A1,A34,A35,Def14,ZFMISC_1:37;
          reconsider x1 as Element of X by A34,A35,ZFMISC_1:37;
A37:        F.(c.x1) = F.A by A6,A33,A35;
          Im(f,x1) = {f.x1} by A8,FUNCT_1:117; then
A38:      f.:{x1} = {f.x1} & f.x1 = union(F.A) by A37,A9;
          F.A in the Points of G_(k,X);then
          consider B being Subset of X such that
A39:      B = F.A & Card B = 1 by A2;
          consider x2 being set such that
A40:      B = {x2} by A39,CARD_2:60;
          thus thesis by A33,A35,A36,A38,A39,A40,ZFMISC_1:31;
        end;
        hence thesis by A15,A31,FUNCT_1:9;
      end;
      the line-map of F = the line-map of incprojmap(k,f)
      proof
A41:    dom(the line-map of incprojmap(k,f)) = the Lines of G_(k,X)
            by FUNCT_2:67;
        for x st x in dom(the line-map of F) holds
              (the line-map of F).x = (the line-map of incprojmap(k,f)).x
        proof
          let x be set;
          assume
A42:         x in dom(the line-map of F);then
          consider A being LINE of G_(k,X) such that
A43:         x = A;
          consider A11 being Subset of X such that
A44:          x = A11 & Card A11 = 2 by A15,A3,A42;
          A11 is finite by A44,A17,CARD_4:4;then
          consider x1,x2 being set such that
A45:            x1 <> x2 & x = {x1,x2} by A44,CARD_2:79;
A46:      incprojmap(k,f).A = f.:A & x1 in X & x2 in X
                 by A1,A44,A45,Def14,ZFMISC_1:38;
          reconsider x1,x2 as Element of X by A44,A45,ZFMISC_1:38;
A47:      c.x1 = {x1} & c.x2 = {x2} & Card{x1} = 1 & Card{x2} = 1 &
             {x1} c= X & {x2} c= X by A6,CARD_1:79;then
          consider A1 being POINT of G_(k,X) such that
A48:           A1 = {x1};
          consider A2 being POINT of G_(k,X) such that
A49:           A2 = {x2} by A47;
          A1 c= A & A2 c= A & F.(c.x1) = F.A1 & F.(c.x2) = F.A2
                by A43,A45,A6,A48,A49,ZFMISC_1:42;then
A50:      f.:(A1 \/ A2) = f.:A1 \/ f.:A2 & A1 on A & A2 on A & A1 <> A2 &
             A1 \/ A2 = A & f.x1 = union(F.A1) & f.x2 = union(F.A2)
        by A1,A9,A43,A45,A48,A49,Th10,RELAT_1:153,ZFMISC_1:24,ENUMSET1:41;
A51:        Im(f,x1) = {f.x1} & Im(f,x2) = {f.x2} by FUNCT_1:117,A8; then
A52:      f.:A1 = {f.x1} & f.:A2 = {f.x2} & F.A1 on F.A & F.A2 on F.A &
            incprojmap(k,f).A1 = f.:A1 & incprojmap(k,f).A2 = f.:A2 &
               F.A1 <> F.A2 by A1,A15,A8,A48,A49,A50,
                FUNCT_1:def 8,Def8,Def14;then
          F.A1 c= F.A & F.A2 c= F.A by A1,Th10;then
A53:      F.A1 \/ F.A2 c= F.A by XBOOLE_1:8;
A54:       F.A1 in the Points of G_(k,X) & F.A2 in the Points of G_(k,X);then
          consider B1 being Subset of X such that
A55:           B1 = F.A1 & Card B1 = 1 by A2;
          consider B2 being Subset of X such that
A56:           B2 = F.A2 & Card B2 = 1 by A2,A54;
          F.A in the Lines of G_(k,X);then
          consider B3 being Subset of X such that
A57:           B3 = F.A & Card B3 = 2 by A3;
A58:      F.A1 is finite & F.A2 is finite & F.A is finite
               by A55,A56,A57,A17,CARD_4:4;
          consider y1 being set such that
A59:           F.A1 = {y1} by A55,CARD_2:60;
          consider y2 being set such that
A60:      F.A2 = {y2} by A56,CARD_2:60;
          not y2 in F.A1
          proof
            assume y2 in F.A1;then
            F.A2 c= F.A1 by A60,ZFMISC_1:37;
            hence contradiction by A52,A58,A55,A56,CARD_FIN:1;
          end;then
          Card(F.A1 \/ F.A2) = 1 + 1 & F.A1 \/ F.A2 is finite & f.:A1 = F.A1 &
             f.:A2 = F.A2 by A50,A51,A55,A59,A60,CARD_2:54,FINSET_1:14,
               ZFMISC_1:31,A48,A49;
          hence thesis by A43,A46,A50,A53,A57,A58,CARD_FIN:1;
        end;
        hence thesis by A15,A41,FUNCT_1:9;
      end;
      hence thesis by A30;
    end;
    hence thesis;
  end;

theorem Th25:
  for k being Element of NAT
  for X being non empty set st 1 < k & Card X = k + 1
  for F being IncProjMap over G_(k,X), G_(k,X) st F is automorphism
  holds (ex s being Permutation of X st the IncProjMap of F = incprojmap(k,s))
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1: 1 < k & k + 1 = Card X;
A2: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A1,Def1;
A3: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
          by A1,Def1;
    let F be IncProjMap over G_(k,X), G_(k,X) such that
A4: F is automorphism;
    deffunc CH(set) = X \ {$1};
    consider c being Function such that
A5: dom c = X and
A6: for x being set st x in X holds c.x = CH(x) from FUNCT_1:sch 3;
A7: Card(k + 1) = k + 1 & Card 1 = 1 & Card k = k by CARD_1:66;then
A8: X is finite by A1,CARD_4:4;
    rng c c= the Points of G_(k,X)
    proof
      let y be set;
      assume y in rng c;then
      consider x being set such that
A9:    x in dom c & y = c.x by FUNCT_1:def 5;
A10:  y = X \ {x} & {x} c= X & Card{x} = 1
           by A5,A6,A9,CARD_1:79,ZFMISC_1:37;then
      Card(X \ {x}) = (k + 1) - 1 & X \ {x} c= X by A1,A8,CARD_2:63;
      hence thesis by A2,A10;
    end;then
    reconsider c as Function of X, the Points of G_(k,X) by A5,FUNCT_2:4;
    deffunc W(Element of X) = union (X \ F.(c.$1));
    consider f being Function such that
A11:dom f = X and
A12:for x being Element of X holds f.x = W(x) from FUNCT_1:sch 4;
    rng f c= X
    proof
      let b be set;
      assume b in rng f;then
      consider a being set such that
A13:     a in X & b = f.a by A11,FUNCT_1:def 5;
      reconsider a as Element of X by A13;
A14:  b = union (X \ F.(c.a)) by A12,A13;
      consider A being POINT of G_(k,X) such that
A15:     A = F.(c.a);
      A in the Points of G_(k,X);then
      consider A1 being Subset of X such that
A16:     A1 = A & Card A1 = k by A2;
      A is finite by A7,A16,CARD_4:4;then
      Card(X \ A) = (k + 1) - k by A1,A8,A16,CARD_2:63;then
      consider x being set such that
A17:      X \ A = {x} by CARD_2:60;
      x in X by A17,ZFMISC_1:37;
      hence thesis by A14,A15,A17,ZFMISC_1:31;
    end;then
    reconsider f as Function of X,X by A11,FUNCT_2:4;
A18: the point-map of F is bijective &
      dom(the point-map of F) = the Points of G_(k,X) &
       dom(the line-map of F) = the Lines of G_(k,X) & 0 < k &
        F is incidence_preserving & k + 1 <=` Card X &
           the line-map of F is bijective by A1,A4,Def9,FUNCT_2:67;then
A19: the point-map of F is one-to-one & the point-map of F is onto &
       the line-map of F is onto by FUNCT_2:def 4;then
A20: rng(the point-map of F) = the Points of G_(k,X) & Card 2 = 2 &
      Card 1 = 1 & rng(the line-map of F) = the Lines of G_(k,X)
            by FUNCT_2:def 3,CARD_1:66;
A21: f is one-to-one
    proof
      let x1,x2 be set such that
A22:        x1 in dom f & x2 in dom f & f.x1 = f.x2;
      reconsider x1,x2 as Element of X by A22;
A23:    f.x1 = union(X \ F.(c.x1)) & f.x2 = union(X \ F.(c.x2)) by A12;
      consider A1 being POINT of G_(k,X) such that
A24:    A1 = F.(c.x1);
      consider A2 being POINT of G_(k,X) such that
A25:     A2 = F.(c.x2);
      A1 in the Points of G_(k,X);then
      consider A11 being Subset of X such that
A26:      A11 = A1 & Card A11 = k by A2;
      A2 in the Points of G_(k,X);then
      consider A12 being Subset of X such that
A27:      A12 = A2 & Card A12 = k by A2;
      X \ A1 c= X & X \ A2 c= X & A1 is finite & A2 is finite
            by A7,A26,A27,CARD_4:4;then
A28:  Card(X \ A1) = (k + 1) - k & Card(X \ A2) = (k + 1) - k
               by A1,A8,A26,A27,CARD_2:63;then
      consider y1 being set such that
A29:     X \ A1 = {y1} by CARD_2:60;
      consider y2 being set such that
A30:      X \ A2 = {y2} by A28,CARD_2:60;
      f.x1 = y1 & f.x2 = y2 by A23,A24,A25,A29,A30,ZFMISC_1:31;then
      A1 = A2 & c.x1 in dom(the point-map of F)
        & c.x2 in dom(the point-map of F) by A18,A26,A27,A22,A29,A30,Th5;then
      c.x1 = c.x2 & c.x1 = X \ {x1} & c.x2 = X \ {x2} & {x1} c= X & {x2} c= X
             by A19,A6,A24,A25,FUNCT_1:def 8;then
      {x1} = {x2} by Th5;
      hence thesis by ZFMISC_1:6;
    end;
    for y st y in X ex x st x in X & y = f.x
    proof
      let y be set;
      assume y in X;then
A31:   {y} c= X & Card{y} = 1 by ZFMISC_1:37,CARD_1:79;then
      Card(X \ {y}) = (k + 1) - 1 & X \ {y} c= X by A1,A8,CARD_2:63;then
      X \ {y} in rng(the point-map of F) by A2,A20;then
      consider a being set such that
A32:     a in dom(the point-map of F) & X \ {y} = (the point-map of F).a
            by FUNCT_1:def 5;
A33:  a in the Points of G_(k,X) by A32;
      consider A1 being Subset of X such that
A34:     A1 = a & Card A1 = k by A2,A33;
      a is finite by A7,A34,CARD_4:4;then
      Card(X \ a) = (k + 1) - k by A1,A8,A34,CARD_2:63;then
      consider x being set such that
A35:      X \ a = {x} by CARD_2:60;
      reconsider x as Element of X by A35,ZFMISC_1:37;
      a /\ X = X \ {x} by A35,XBOOLE_1:48;then
      c.x = X \ {x} & X \ {x} = a by A6,A34,XBOOLE_1:28;then
      X /\ {y} = X \ F.(c.x) by A32,XBOOLE_1:48;then
      {y} = X \ F.(c.x) by A31,XBOOLE_1:28;then
      y = union(X \ F.(c.x)) by ZFMISC_1:31;then
      y = f.x by A12;
      hence thesis;
    end;then
A36: rng f = X by FUNCT_2:16;then
    f is onto by FUNCT_2:def 3;then
    reconsider f as Permutation of X by A21,FUNCT_2:def 4;
    the IncProjMap of F = incprojmap(k,f)
    proof
A37:  the point-map of F = the point-map of incprojmap(k,f)
      proof
A38:    dom(the point-map of incprojmap(k,f)) = the Points of G_(k,X)
           by FUNCT_2:67;
        for x st x in dom(the point-map of F) holds
              (the point-map of F).x = (the point-map of incprojmap(k,f)).x
        proof
          let x be set;
          assume
A39:         x in dom(the point-map of F);then
          consider A being POINT of G_(k,X) such that
A40:         x = A;
          consider A1 being Subset of X such that
A41:          x = A1 & Card A1 = k by A2,A18,A39;
          F.A in the Points of G_(k,X);then
          consider B being Subset of X such that
A42:          B = F.A & Card B = k by A2;
A43:       X \ (X \ F.A) = F.A /\ X & F.A /\ X = F.A & F.A is finite &
           A is finite by A7,A40,A41,A42,XBOOLE_1:28,XBOOLE_1:48,CARD_4:4;then
A44:      X \ (X \ F.A) = F.A & Card(X \ F.A) = (k + 1) - k &
              Card(X \ A) = (k + 1) - k by A1,A8,A40,A41,A42,CARD_2:63;then
          consider x1 being set such that
A45:          X \ A = {x1} by CARD_2:60;
          consider x2 being set such that
A46:          X \ F.A = {x2} by A44,CARD_2:60;
A47:      F.A = X \ {union(X \ F.A)} & x1 in X
               by A43,A45,A46,ZFMISC_1:31,ZFMISC_1:37;
          reconsider x1 as Element of X by A45,ZFMISC_1:37;
A48:       X \ (X \ A) = X \ {x1} & X \ (X \ A) = A /\ X & A /\ X = A
                       by A40,A41,A45,XBOOLE_1:28,XBOOLE_1:48;then
A49:      c.x1 = X \ {x1} & A = X \ {x1} by A6;
          incprojmap(k,f).A = f.:A by A1,Def14;then
A50:      incprojmap(k,f).A = f.:X \ f.:{x1} by A48,FUNCT_1:123;
          Im(f,x1) = {f.x1} by A11,FUNCT_1:117; then
          f.:X = X & f.:{x1} = {f.x1} by A36,FUNCT_2:79;
          hence thesis by A40,A49,A12,A47,A50;
        end;
        hence thesis by A18,A38,FUNCT_1:9;
      end;
      the line-map of F = the line-map of incprojmap(k,f)
      proof
A51:    dom(the line-map of incprojmap(k,f)) = the Lines of G_(k,X)
             by FUNCT_2:67;
        for x st x in dom(the line-map of F) holds
              (the line-map of F).x = (the line-map of incprojmap(k,f)).x
        proof
          let x be set;
          assume
A52:         x in dom(the line-map of F);then
          consider A being LINE of G_(k,X) such that
A53:         x = A;
          consider A11 being Subset of X such that
A54:          x = A11 & Card A11 = k + 1 by A18,A3,A52;
          A11 is finite by A7,A54,CARD_4:4;then
A55:      x = X by A1,A8,A54,CARD_FIN:1;
          F.A in the Lines of G_(k,X);then
          consider y being Subset of X such that
A56:           y = F.A & Card y = k + 1 by A3;
          F.A is finite & f.:x = X by A7,A55,A56,A36,CARD_4:4,FUNCT_2:79;then
          F.A = X & incprojmap(k,f).A = f.:x
                by A1,A8,A53,A56,CARD_FIN:1,Def14;
          hence thesis by A53,A55,A36,FUNCT_2:79;
        end;
        hence thesis by A18,A51,FUNCT_1:9;
      end;
      hence thesis by A37;
    end;
    hence thesis;
  end;

theorem Th26:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X
  for T being Subset of the Points of G_(k,X)
  for S being Subset of X holds
  Card S = k - 1 & T = {A where A is Subset of X: Card A = k & S c= A} implies
        S = meet T
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:   0 < k & k + 1 <=` Card X;
    let T be Subset of the Points of G_(k,X);
    let S be Subset of X;
A2: k - 1 is Element of NAT by A1,NAT_1:20;then
A3: Card(k - 1) = k - 1 & Card k = k by CARD_1:66;
    assume
A4:     Card S = k - 1 & T = {A where A is Subset of X: Card A = k & S c= A};
A5: S is finite by A2,A3,A4,CARD_4:4;
    S = meet T
    proof
A6:   T <> {}
      proof
        X \ S <> {}
        proof
          assume X \ S = {};then
          X c= S by XBOOLE_1:37;then
          Card X = k - 1 by A4,XBOOLE_0:def 10;then
          1 + k <= - 1 + k by A1,A2,CARD_1:56;
          hence contradiction by XREAL_1:8;
        end;then
        consider x being set such that
A7:         x in X \ S by XBOOLE_0:def 1;
        not x in S & {x} c= X by A7,XBOOLE_0:def 4,ZFMISC_1:37;then
        Card(S \/ {x}) = (k - 1) + 1 & S c= S \/ {x} & S \/ {x} c= X
               by A4,A5,CARD_2:54,XBOOLE_1:7,XBOOLE_1:8;then
        S \/ {x} in T by A4;
        hence thesis;
      end;
A8:  S c= meet T
      proof
        for a1 being set st a1 in T holds S c= a1
        proof
          let a1 be set such that
A9:      a1 in T;
          consider A1 being Subset of X such that
A10:      a1 = A1 & Card A1 = k & S c= A1 by A4,A9;
          thus thesis by A10;
        end;
        hence thesis by A6,SETFAM_1:6;
      end;
      meet T c= S
      proof
        let y be set such that
A11:         y in meet T;
        y in S
        proof
          assume
A12:         not y in S;
          consider a1 being set such that
A13:          a1 in T by A6,XBOOLE_0:def 1;
          consider A1 being Subset of X such that
A14:           a1 = A1 & Card A1 = k & S c= A1 by A4,A13;
A15:      y in a1 & Card{y} = 1 by A11,A13,SETFAM_1:def 1,CARD_1:50;then
          {y} c= a1 & a1 is finite by A3,A14,CARD_4:4,ZFMISC_1:37;then
A16:      Card(a1 \ {y}) = k - 1 by A14,A15,CARD_2:63;
          X \ a1 <> {}
          proof
            assume X \ a1 = {};then
            X c= a1 by XBOOLE_1:37;then
            Card X = k by A14,XBOOLE_0:def 10;then
            1 + k <= 0 + k by A1,CARD_1:56;
            hence contradiction by XREAL_1:8;
          end;then
          consider y2 being set such that
A17:             y2 in X \ a1 by XBOOLE_0:def 1;
A18:      not y2 in a1 \ {y} & a1 \ {y} is finite & (a1 \ {y}) c= X &
             {y2} c= X
      by A2,A3,A14,A16,CARD_4:4,XBOOLE_1:1,ZFMISC_1:37,A17,XBOOLE_0:def 4;then
A19:      Card((a1 \ {y}) \/ {y2}) = (k - 1) + 1 & (a1 \ {y}) \/ {y2} c= X
                by A16,CARD_2:54,XBOOLE_1:8;
A20:      y2 <> y by A15,A17,XBOOLE_0:def 4;
          S misses {y}
          proof
            assume not S misses {y};then
            S /\ {y} <> {} by XBOOLE_0:def 7;then
            consider z being set such that
A21:             z in S /\ {y} by XBOOLE_0:def 1;
            z in {y} by A21,XBOOLE_0:def 3;then
            z in S & z = y by A21,TARSKI:def 1,XBOOLE_0:def 3;
            hence contradiction by A12;
          end;then
          S c= a1 \ {y} by A14,XBOOLE_1:86;then
          S c= (a1 \ {y}) \/ {y2} & S = a1 \ {y}
               by A4,A5,A16,A18,CARD_FIN:1,XBOOLE_1:10;then
A22:      (a1 \ {y}) \/ {y2} in T & not y in (a1 \ {y}) & not y in {y2}
                     by A4,A12,A19,A20,TARSKI:def 1;then
          not y in (a1 \ {y}) \/ {y2} by XBOOLE_0:def 2;
          hence contradiction by A11,A22,SETFAM_1:def 1;
        end;
        hence thesis;
      end;
      hence thesis by A8,XBOOLE_0:def 10;
    end;
    hence thesis;
  end;

theorem ::LemStarb:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X
  for T being Subset of the Points of G_(k,X) st T is STAR
  for S being Subset of X holds
  S = meet T implies Card S = k - 1 &
         T = {A where A is Subset of X: Card A = k & S c= A}
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:   0 < k & k + 1 <=` Card X;
    let T be Subset of the Points of G_(k,X) such that
A2:    T is STAR;
    let S be Subset of X;
    assume
A3:     S = meet T;
    consider S1 being Subset of X such that
A4:    Card S1 = k - 1 & T = {A where A is Subset of X: Card A = k & S1 c= A}
              by A2,Def4;
    S1 = meet T by A1,A4,Th26;
    hence thesis by A3,A4;
  end;

theorem Th28:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X
  for T1,T2 being Subset of the Points of G_(k,X)
  st T1 is STAR & T2 is STAR & meet T1 = meet T2
  holds T1 = T2
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:    0 < k & k + 1 <=` Card X;
    let T1,T2 be Subset of the Points of G_(k,X) such that
A2:   T1 is STAR & T2 is STAR & meet T1 = meet T2;
    consider S1 being Subset of X such that
A3:   Card S1 = k - 1 & T1 = {A where A is Subset of X: Card A = k & S1 c= A}
            by A2,Def4;
    consider S2 being Subset of X such that
A4:   Card S2 = k - 1 & T2 = {A where A is Subset of X: Card A = k & S2 c= A}
           by A2,Def4;
    S1 = meet T1 & S2 = meet T2 by A1,A3,A4,Th26;
    hence thesis by A2,A3,A4;
  end;

theorem Th29:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X
  for A being finite Subset of X st Card A = k-1
  holds ^^(A,X,k) is STAR
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:    0 < k & k + 1 <=` Card X;
    let A be finite Subset of X such that
A2:     Card A = k - 1;
    ^^(A,X,k) = ^^(A,X) by Def13,A1,A2;
    hence thesis by A2,Def4;
  end;

theorem Th30:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X
  for A being finite Subset of X st Card A = k-1
  holds meet ^^(A,X,k) = A
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:    0 < k & k + 1 <=` Card X;
    let A be finite Subset of X such that
A2:     Card A = k - 1;
    ^^(A,X,k) = ^^(A,X) by Def13,A1,A2;
    hence thesis by A1,A2,Th26;
  end;

theorem Th31:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 3 <=` Card X
  for F being IncProjMap over G_(k+1,X), G_(k+1,X) st F is automorphism
  ex H being IncProjMap over G_(k,X), G_(k,X) st H is automorphism &
  the line-map of H = the point-map of F &
  for A being POINT of G_(k,X), B being finite set st B = A holds
          H.A = meet(F.:(^^(B,X,k+1)))
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:     0 < k & k + 3 <=` Card X;
    let F be IncProjMap over G_(k+1,X), G_(k+1,X) such that
A2:      F is automorphism;
A3: (k + 1) + 2 <=` Card X & (k + 1) + 0 <= (k + 1) + 2 &
       0 + 2 < k + (1 + 1) & Card k = k & (k + 1) + 1 <= (k + 1) + 2
            by A1,CARD_1:66,XREAL_1:8;then
    k + 1 c= k + 3 & k + 2 c= k + 3 & 0 + 2 < (k + 1) + 1 & k + 0 < k + 1
          by CARD_1:56,XREAL_1:10;then
A4: k + 1 <=` Card X & 2 <= k + 1 & (k + 1) + 2 <=` Card X &
       (k + 1) + 1 <=` Card X & 0 < k + 1 by A1,XBOOLE_1:1,NAT_1:13;
A5: the Points of G_(k,X) = {A where A is Subset of X: Card A = k}
          by A1,A4,Def1;
A6: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
        by A1,A4,Def1;
A7: the Points of G_(k+1,X) = {A where A is Subset of X: Card A = k+1}
         by A4,Def1;
    defpred P[set,set] means ex B being finite set st
      B = $1 & $2 = meet(F.:(^^(B,X,k+1)));
A8:  for e being set st e in the Points of G_(k,X)
    ex u being set st P[e,u]
    proof
      let e be set; assume e in the Points of G_(k,X);then
      consider B being Subset of X such that
A9:      B = e & Card B = k by A5;
      reconsider B = e as finite Subset of X by A3,A9,CARD_4:4;
      take u = meet(F.:(^^(B,X,k+1)));
      thus P[e,u];
    end;
    consider Hp being Function such that
A10:  dom Hp = the Points of G_(k,X) and
A11:  for e being set st e in the Points of G_(k,X) holds P[e,Hp.e]
         from CLASSES1:sch 1(A8);
    rng Hp c= the Points of G_(k,X)
    proof
      let y be set such that
A12:     y in rng Hp;
      consider x being set such that
A13:    x in dom Hp & y = Hp.x by A12,FUNCT_1:def 5;
      consider B being finite set such that
A14:      B = x & y = meet(F.:(^^(B,X,k+1))) by A13,A10,A11;
      consider x1 being Subset of X such that
A15:      x = x1 & Card x1 = k by A13,A10,A5;
      Card B = (k + 1) - 1 & B c= X by A14,A15;then
      ^^(B,X,k+1) is STAR by A4,Th29;then
      F.:(^^(B,X,k+1)) is STAR by A2,A4,Th23;then
      consider S being Subset of X such that
A16:   Card S = (k+1) - 1 &
         F.:(^^(B,X,k+1)) = {C where C is Subset of X: Card C = k+1 & S c= C}
                by Def4;
      S = meet(F.:(^^(B,X,k+1))) & S in the Points of G_(k,X)
           by A4,A16,A5,Th26;
      hence thesis by A14;
    end;then
    reconsider Hp as Function of the Points of G_(k,X), the Points of G_(k,X)
         by A10,FUNCT_2:4;
    the point-map of F is bijective by A2,Def9;then
A17:the point-map of F is onto & the point-map of F is one-to-one
           by FUNCT_2:def 4;then
A18:dom the point-map of F = the Points of G_(k+1,X) &
      rng the point-map of F = the Points of G_(k+1,X)
       by FUNCT_2:67,FUNCT_2:def 3;
    reconsider Hl =
  the point-map of F as Function of the Lines of G_(k,X), the Lines of G_(k,X)
             by A6,A7;
    take H = IncProjMap(#Hp,Hl#);
A19:for A being POINT of G_(k,X), B being finite set st B = A holds
          Hp.A = meet(F.:(^^(B,X,k+1)))
    proof
      let A be POINT of G_(k,X);
      let B be finite set such that
A20:       A = B;
      P[A,Hp.A] by A11;
      hence thesis by A20;
    end;
    H is automorphism
    proof
A21:  the line-map of H is bijective by A2,Def9,A6,A7;
A22:  the point-map of H is bijective
      proof
A23:    Hp is one-to-one
        proof
          let x1,x2 be set such that
A24:        x1 in dom Hp & x2 in dom Hp & Hp.x1 = Hp.x2;
          consider X1 being finite set such that
A25:        X1 = x1 & Hp.x1 = meet(F.:(^^(X1,X,k+1))) by A24,A11;
          consider X2 being finite set such that
A26:        X2 = x2 & Hp.x2 = meet(F.:(^^(X2,X,k+1))) by A24,A11;
          consider x11 being Subset of X such that
A27:        x1 = x11 & Card x11 = k by A24,A10,A5;
          Card X1 = (k + 1) - 1 & X1 c= X by A25,A27;then
          ^^(X1,X,k+1) is STAR by A4,Th29;then
A28:      F.:(^^(X1,X,k+1)) is STAR by A2,A4,Th23;
          consider x12 being Subset of X such that
A29:        x2 = x12 & Card x12 = k by A24,A10,A5;
A30:       Card X2 = (k + 1) - 1 & X2 c= X by A26,A29;then
          ^^(X2,X,k+1) is STAR by A4,Th29;then
          F.:(^^(X2,X,k+1)) is STAR by A2,A4,Th23;then
A31:       F.:(^^(X1,X,k+1)) = F.:(^^(X2,X,k+1))
                by A4,A24,A25,A26,A28,Th28;
          meet(^^(X1,X,k+1)) = X1 & meet(^^(X2,X,k+1)) = X2
                 by A4,A25,A27,A30,Th30;
          hence thesis by A31,A25,A26,A17,Th6;
        end;
        for y st y in the Points of G_(k,X)
              ex x st x in the Points of G_(k,X) & y = Hp.x
        proof
          let y be set such that
A32:         y in the Points of G_(k,X);
          consider Y1 being Subset of X such that
A33:        y = Y1 & Card Y1 = k by A32,A5;
          reconsider y as finite Subset of X by A3,A33,CARD_4:4;
A34:      Card y = (k + 1) - 1 by A33;then
          ^^(y,X,k+1) is STAR by A4,Th29;then
          F"(^^(y,X,k+1)) is STAR by A2,A4,Th23;then
          consider S being Subset of X such that
A35:        Card S = (k+1) - 1 &
           F"(^^(y,X,k+1)) = {A where A is Subset of X: Card A = k+1 & S c= A}
                 by Def4;
A36:      S = meet(F"(^^(y,X,k+1))) & S in the Points of G_(k,X)
               by A4,A35,A5,Th26;
          reconsider S as finite Subset of X by A3,A35,CARD_4:4;
          ^^(S,X,k+1) = ^^(S,X) by A4,A35,Def13;then
          F"(^^(y,X,k+1)) = ^^(S,X,k+1) & P[S,Hp.S] by A35,A36,A11;then
          Hp.S = meet(^^(y,X,k+1)) by A18,FUNCT_1:147;then
          y = Hp.S by A4,A34,Th30;
          hence thesis by A36;
        end;then
        rng Hp = the Points of G_(k,X) by FUNCT_2:16;then
        Hp is onto by FUNCT_2:def 3;
        hence thesis by A23,FUNCT_2:def 4;
      end;
      H is incidence_preserving
      proof
        let A1 be POINT of G_(k,X);
        let L1 be LINE of G_(k,X);
        A1 in the Points of G_(k,X);then
        consider a1 being Subset of X such that
A37:        a1 = A1 & Card a1 = k by A5;
        L1 in the Lines of G_(k,X);then
        consider l1 being Subset of X such that
A38:        l1 = L1 & Card l1 = k+1 by A6;
        L1 in the Points of G_(k+1,X) by A38,A7;then
        consider L11 being POINT of G_(k+1,X) such that
A39:        L11 = L1;
        reconsider a1 as finite Subset of X by A3,A37,CARD_4:4;
A40:     P[A1,Hp.A1] by A11;
A41:    Card a1 = (k + 1) - 1 by A37;then
A42:     ^^(a1,X,k+1) = ^^(a1,X) by A4,Def13;
A43:    A1 on L1 implies H.A1 on H.L1
        proof
          assume A1 on L1;then
A44:      A1 c= L1 by A1,A4,Th10;
          L1 in ^^(a1,X,k+1) by A44,A37,A38,A42;then
          F.L11 in F.:(^^(a1,X,k+1)) by A18,A39,FUNCT_1:def 12;then
          meet(F.:(^^(a1,X,k+1))) c= F.L11 & H.L1 = F.L11 by A39,SETFAM_1:4;
          hence thesis by A1,A4,A40,A37,Th10;
        end;
        H.A1 on H.L1 implies A1 on L1
        proof
          assume H.A1 on H.L1;then
A45:      H.A1 c= H.L1 by A1,A4,Th10;
          H.A1 in the Points of G_(k,X);then
          consider ha1 being Subset of X such that
A46:          ha1 = H.A1 & Card ha1 = k by A5;
          H.L1 in the Lines of G_(k,X);then
          consider hl1 being Subset of X such that
A47:          hl1 = H.L1 & Card hl1 = k+1 by A6;
          ^^(a1,X,k+1) is STAR by A4,A41,Th29;then
          F.:(^^(a1,X,k+1)) is STAR by A2,A4,Th23;then
          consider S being Subset of X such that
A48:        Card S = (k+1) - 1 &
         F.:(^^(a1,X,k+1)) = {A where A is Subset of X: Card A = k+1 & S c= A}
                 by Def4;
          reconsider ha1,S as finite Subset of X by A3,A46,A48,CARD_4:4;
A49:       ^^(ha1,X,k+1) = ^^(ha1,X) & ^^(S,X,k+1) = ^^(S,X)
                 by A4,A46,A48,Def13;then
          F.L11 in (^^(ha1,X,k+1)) & F.:(^^(a1,X,k+1)) = ^^(S,X,k+1)
               by A45,A46,A39,A47,A48;then
          L1 in F"(^^(ha1,X,k+1)) & F"(F.:(^^(a1,X,k+1))) c= ^^(a1,X,k+1) &
           ^^(a1,X,k+1) c= F"(F.:(^^(a1,X,k+1))) & S = meet(F.:(^^(a1,X,k+1)))
    by A4,A17,A18,A39,A48,FUNCT_1:146,FUNCT_1:152,FUNCT_1:def 13,Th30;then
          meet(F"(^^(ha1,X,k+1))) c= L1 & F"(F.:(^^(a1,X,k+1))) = ^^(a1,X,k+1)
            & meet(^^(a1,X,k+1)) = a1 & ^^(ha1,X,k+1) = F.:(^^(a1,X,k+1))
                by A4,A40,A49,A37,A46,A48,XBOOLE_0:def 10,SETFAM_1:4,Th30;
          hence thesis by A1,A4,A37,Th10;
        end;
        hence thesis by A43;
      end;
      hence thesis by A21,A22,Def9;
    end;
    hence thesis by A19;
  end;

theorem Th32:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 3 <=` Card X
  for F being IncProjMap over G_(k+1,X), G_(k+1,X) st F is automorphism
  for H being IncProjMap over G_(k,X), G_(k,X) st H is automorphism &
      the line-map of H = the point-map of F &
    (for A being POINT of G_(k,X), B being finite set st B = A holds
         H.A = meet(F.:(^^(B,X,k+1))))
  for f being Permutation of X st the IncProjMap of H = incprojmap(k,f)
  holds
    the IncProjMap of F = incprojmap(k+1,f)
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:     0 < k & k + 3 <=` Card X;
    let F be IncProjMap over G_(k+1,X), G_(k+1,X) such that
A2:       F is automorphism;
    let H be IncProjMap over G_(k,X), G_(k,X) such that
A3:    H is automorphism & the line-map of H = the point-map of F &
        (for A being POINT of G_(k,X), B being finite set st B = A holds
          H.A = meet(F.:(^^(B,X,k+1))));
    let f be Permutation of X such that
A4:       the IncProjMap of H = incprojmap(k,f);
    k + 2 <= k + 3 & k + 1 <= k + 2 & k + 1 <= k + 3 & k + 0 <= k + 1
         by XREAL_1:9;then
A5: k + 2 c= k + 3 & k + 1 <=` k + 2 & k + 1 c= k + 3 & k <=` k + 1
      & Card k = k & Card(k + 1) = k + 1 & Card(k + 2) = k + 2
           by CARD_1:56,CARD_1:66;then
A6: k + 1 <=` Card X & (k + 1) + 1 <=` Card X & 0 < k + 1 by A1,XBOOLE_1:1;
A7: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
        by A1,A6,Def1;
A8: the Points of G_(k+1,X) = {A where A is Subset of X: Card A = k + 1}
          by A6,Def1;
A9: the Lines of G_(k+1,X) = {L where L is Subset of X: Card L = (k + 1) + 1}
            by A6,Def1;
A10: the point-map of F is bijective & F is incidence_preserving &
    dom(the point-map of F) = the Points of G_(k+1,X) &
      dom(the line-map of F) = the Lines of G_(k+1,X) &
        dom(the point-map of incprojmap(k+1,f)) = the Points of G_(k+1,X) &
         dom(the line-map of incprojmap(k+1,f)) = the Lines of G_(k+1,X)
            by A2,Def9,FUNCT_2:67;then
A11: the point-map of F is one-to-one & the point-map of F is onto
         by FUNCT_2:def 4;
A12:the point-map of F = the point-map of incprojmap(k+1,f)
    proof
      for x st x in dom(the point-map of F) holds
            (the point-map of F).x = (the point-map of incprojmap(k+1,f)).x
      proof
        let x be set;
        assume x in dom(the point-map of F);then
        consider A being POINT of G_(k+1,X) such that
A13:       x = A;
        consider A1 being LINE of G_(k,X) such that
A14:        x = A1 by A7,A8,A13;
        incprojmap(k,f).A1 = f.:A1 by A1,A6,Def14;then
        F.A = incprojmap(k+1,f).A by A3,A4,A6,A13,A14,Def14;
        hence thesis by A13;
      end;
      hence thesis by A10,FUNCT_1:9;
    end;
    the line-map of F = the line-map of incprojmap(k+1,f)
    proof
      for x st x in dom(the line-map of F) holds
         (the line-map of F).x = (the line-map of incprojmap(k+1,f)).x
      proof
        let x be set;
        assume x in dom(the line-map of F);then
        consider A being LINE of G_(k+1,X) such that
A15:       x = A;
        x in the Lines of G_(k+1,X) by A15;then
        consider A11 being Subset of X such that
A16:        x = A11 & Card A11 = (k + 1) + 1 by A9;
        consider B1 being set such that
A17:      B1 c= x & Card B1 = k + 1 by A16,A5,CARD_FIL:36;
        consider C1 being set such that
A18:      C1 c= B1 & Card C1 = k by A17,A5,CARD_FIL:36;
A19:    B1 is finite & C1 is finite & x is finite & x \ B1 c= x & C1 c= x
             by A16,A17,A18,A5,CARD_4:4,XBOOLE_1:1;then
A20:     Card(x \ B1) = (k + 2) - (k + 1) & C1 misses(x \ B1) &
          x \ B1 is finite & C1 \/ (x \ B1) c= x
            by A16,A17,A18,CARD_2:63,XBOOLE_1:85,FINSET_1:13,XBOOLE_1:8;then
A21:    Card(C1 \/ (x \ B1)) = k + 1 & C1 \/ (x \ B1) c= X & B1 c= X &
           B1 \/ (C1 \/ (x \ B1)) c= x
           by A16,A17,A18,A19,CARD_2:53,XBOOLE_1:1,XBOOLE_1:8;then
A22:    B1 in the Points of G_(k+1,X) &
           C1 \/ (x \ B1) in the Points of G_(k+1,X) by A8,A17;then
        consider b1 being POINT of G_(k+1,X) such that
A23:         b1 = B1;
        consider b2 being POINT of G_(k+1,X) such that
A24:          b2 = C1 \/ (x \ B1) by A22;
        B1 misses (x \ B1) by XBOOLE_1:79;then
A25:    Card((x \ B1) /\ B1) = 0 by CARD_1:78,XBOOLE_0:def 7;
        not x \ B1 c= B1 by A25,A20,XBOOLE_1:28;then
A26:     b1 <> b2 by A23,A24,XBOOLE_1:11;then
        Card(b1 \/ b2) <=` k + 2 & (k + 1) + 1 <=` Card(b1 \/ b2) &
          b2 is finite & b1 on A & b2 on A
           by A6,A15,A16,A17,A21,A23,A24,A20,A5,CARD_4:4,CARD_1:27,Th1,Th10;
then
        Card(b1 \/ b2) = k + 2 & b1 \/ b2 is finite & b1 \/ b2 c= x &
           F.b1 on F.A & F.b2 on F.A
     by A20,A17,A19,XBOOLE_1:8,A23,A24,A10,FINSET_1:14,XBOOLE_0:def 10,Def8;
then
A27:    b1 \/ b2 = x & f.:b1 \/ f.:b2 = f.:(b1 \/ b2) &
           F.b1 in the Points of G_(k+1,X) & F.b2 in the Points of G_(k+1,X) &
             F.A in the Lines of G_(k+1,X) & F.b1 c= F.A & F.b2 c= F.A
                 by A6,A16,A19,CARD_FIN:1,RELAT_1:153,Th10;then
        consider B11 being Subset of X such that
A28:        F.b1 = B11 & Card B11 = k + 1 by A8;
        consider B12 being Subset of X such that
A29:         F.b2 = B12 & Card B12 = k + 1 by A8,A27;
        consider L1 being Subset of X such that
A30:         F.A = L1 & Card L1 = (k + 1) + 1 by A9,A27;
A31:    F.b1 <> F.b2 & F.b1 \/ F.b2 c= F.A & F.A is finite
            by A27,A30,A11,A10,A5,A26,FUNCT_1:def 8,XBOOLE_1:8,CARD_4:4;then
        (k + 1) + 1 <=` Card(F.b1 \/ F.b2) & Card(F.b1 \/ F.b2) <=` k + 2
              by A28,A29,A30,CARD_1:27,Th1;then
        Card(F.b1 \/ F.b2) = k + 2 & F.b1 \/ F.b2 is finite
             by A31,FINSET_1:13,XBOOLE_0:def 10;then
        F.b1 \/ F.b2 = F.A & incprojmap(k+1,f).A = f.:x &
             F.b2 = incprojmap(k+1,f).b2 & F.b1 = incprojmap(k+1,f).b1 &
              incprojmap(k+1,f).b1 = f.:b1 & incprojmap(k+1,f).b2 = f.:b2
                     by A6,A12,A15,A30,A31,CARD_FIN:1,Def14;
        hence thesis by A15,A27;
      end;
      hence thesis by A10,FUNCT_1:9;
    end;
    hence thesis by A12;
  end;

theorem Th33:
  for k being Element of NAT
  for X being non empty set st 2 <= k & k + 2 <=` Card X
  for F being IncProjMap over G_(k,X), G_(k,X) st F is automorphism
  holds (ex s being Permutation of X st the IncProjMap of F = incprojmap(k,s))
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1: 2 <= k & k + 2 <=` Card X;
    let F be IncProjMap over G_(k,X), G_(k,X) such that
A2: F is automorphism;
    defpred P[Element of NAT] means
    1 <= $1 & $1 <= k implies
    for F being IncProjMap over G_($1,X), G_($1,X) st F is automorphism
    ex f being Permutation of X st the IncProjMap of F = incprojmap($1,f);
A3: P[0];
A4: for i being Element of NAT st P[i] holds P[i+1]
    proof
      let i be Element of NAT such that
A5:     P[i];
      1 <= i + 1 & i + 1 <= k implies
      for F being IncProjMap over G_(i+1,X), G_(i+1,X) st F is automorphism
        ex f being Permutation of X st the IncProjMap of F = incprojmap(i+1,f)
      proof
        assume
A6:      1 <= i + 1 & i + 1 <= k;
        let F2 be IncProjMap over G_(i+1,X), G_(i+1,X) such that
A7:       F2 is automorphism;
        0 c= i by CARD_1:56;then
        0 in succ i & (i + 1) + 2 <= k + 2 by A6,XREAL_1:9,ORDINAL1:34;then
        (0 = i or 0 in i) & Card i = i & i + 3 c= k + 2
            by ORDINAL1:13,CARD_1:56,CARD_1:66;then
A8:    (0 = i or 0 < i) & i + 3 <=` Card X by A1,XBOOLE_1:1;
A9:    i = 0 implies ex
         f being Permutation of X st the IncProjMap of F2 = incprojmap(i+1,f)
        proof
          assume
A10:         i = 0;
          i + 2 <= i + 3 by XREAL_1:9;then
          i + 2 c= i + 3 by CARD_1:56;then
          i + 1 = 0 + 1 & (i + 1) + 1 <=` Card X by A8,A10,XBOOLE_1:1;
          hence thesis by A7,Th24;
        end;
        0 < i implies ex f being Permutation of X st
               the IncProjMap of F2 = incprojmap(i+1,f)
        proof
          assume
A11:        0 < i;
          consider F1 being IncProjMap over G_(i,X), G_(i,X) such that
A12:          F1 is automorphism & the line-map of F1 = the point-map of F2 &
               for A being POINT of G_(i,X), B being finite set st B = A holds
                  F1.A = meet(F2.:(^^(B,X,i+1))) by A7,A8,A11,Th31;
          0 + 1 < i + 1 & i + 0 <= i + 1 by A11,XREAL_1:10;then
          consider f being Permutation of X such that
A13:        the IncProjMap of F1 = incprojmap(i,f) by A6,NAT_1:13,A5,A12;
          the IncProjMap of F2 = incprojmap(i+1,f)
                  by A7,A8,A11,A12,A13,Th32;
          hence thesis;
        end;
        hence thesis by A9;
      end;
      hence thesis;
    end;
    for i being Element of NAT holds P[i] from NAT_1:sch 1(A3,A4);then
    P[k] & 1 <= k by A1,XXREAL_0:2;
    hence thesis by A2;
  end;

theorem Th34:
  for k being Element of NAT
  for X being non empty set st 0 < k & k + 1 <=` Card X
  for s being Permutation of X holds
  incprojmap(k,s) is automorphism
  proof
    let k be Element of NAT;
    let X be non empty set such that
A1:    0 < k & k + 1 <=` Card X;
A2: the Points of G_(k,X) = {A where A is Subset of X: Card A = k} by A1,Def1;
A3: the Lines of G_(k,X) = {L where L is Subset of X: Card L = k + 1}
            by A1,Def1;
    let s be Permutation of X;
A4: dom s = X by FUNCT_2:67;
A5:the line-map of incprojmap(k,s) is bijective
    proof
A6:  the line-map of incprojmap(k,s) is one-to-one
      proof
        let x1,x2 be set;
        assume
A7:       x1 in dom (the line-map of incprojmap(k,s)) &
            x2 in dom (the line-map of incprojmap(k,s)) &
  (the line-map of incprojmap(k,s)).x1 = (the line-map of incprojmap(k,s)).x2;
A8:    x1 in the Lines of G_(k,X) & x2 in the Lines of G_(k,X) by A7;
        consider X1 being LINE of G_(k,X) such that
A9:           X1 = x1 by A7;
        consider X2 being LINE of G_(k,X) such that
A10:           X2 = x2 by A7;
        consider X11 being Subset of X such that
A11:           X11 = x1 & Card X11 = k + 1 by A3,A8;
        consider X12 being Subset of X such that
A12:           X12 = x2 & Card X12 = k + 1 by A3,A8;
        incprojmap(k,s).X1 = s.:x1 & incprojmap(k,s).X2 = s.:x2
                by A1,A9,A10,Def14;
        hence thesis by A7,A9,A10,A11,A12,Th6;
      end;
      the line-map of incprojmap(k,s) is onto
      proof
        for y st y in the Lines of G_(k,X) ex x st x in the Lines of G_(k,X) &
             y = (the line-map of incprojmap(k,s)).x
        proof
          let y be set such that
A13:        y in the Lines of G_(k,X);
          consider B being Subset of X such that
A14:         B = y & Card B = k + 1 by A3,A13;
          rng s = X by FUNCT_2:def 3;then
A15:      s"y c= dom s & s.:(s"y) = y by A14,FUNCT_1:147,RELAT_1:167;then
          Card(s"y) = k + 1 & s"y c= X by A14,FUNCT_2:67,Th4;then
          s"y in the Lines of G_(k,X) by A3;then
          consider A being LINE of G_(k,X) such that
A16:         A = s"y;
          y = incprojmap(k,s).A by A1,A15,A16,Def14;
          hence thesis;
        end;then
        rng (the line-map of incprojmap(k,s)) = the Lines of G_(k,X)
                by FUNCT_2:16;
        hence thesis by FUNCT_2:def 3;
      end;
      hence thesis by A6,FUNCT_2:def 4;
    end;
A17:the point-map of incprojmap(k,s) is bijective
    proof
A18:  the point-map of incprojmap(k,s) is one-to-one
      proof
        let x1,x2 be set;
        assume
A19:       x1 in dom (the point-map of incprojmap(k,s)) &
            x2 in dom (the point-map of incprojmap(k,s)) &
(the point-map of incprojmap(k,s)).x1 = (the point-map of incprojmap(k,s)).x2;
A20:    x1 in the Points of G_(k,X) & x2 in the Points of G_(k,X) by A19;
        consider X1 being POINT of G_(k,X) such that
A21:           X1 = x1 by A19;
        consider X2 being POINT of G_(k,X) such that
A22:           X2 = x2 by A19;
        consider X11 being Subset of X such that
A23:           X11 = x1 & Card X11 = k by A2,A20;
        consider X12 being Subset of X such that
A24:           X12 = x2 & Card X12 = k by A2,A20;
        incprojmap(k,s).X1 = s.:x1 & incprojmap(k,s).X2 = s.:x2
                  by A1,A21,A22,Def14;
        hence thesis by A19,A21,A22,A23,A24,Th6;
      end;
      the point-map of incprojmap(k,s) is onto
      proof
        for y st y in the Points of G_(k,X) ex x st x in the Points of G_(k,X)
            & y = (the point-map of incprojmap(k,s)).x
        proof
          let y be set such that
A25:        y in the Points of G_(k,X);
          consider B being Subset of X such that
A26:         B = y & Card B = k by A2,A25;
          rng s = X by FUNCT_2:def 3;then
A27:      s"y c= dom s & s.:(s"y) = y by A26,FUNCT_1:147,RELAT_1:167;then
          Card(s"y) = k & s"y c= X by A26,Th4,FUNCT_2:67;then
          s"y in the Points of G_(k,X) by A2;then
          consider A being POINT of G_(k,X) such that
A28:         A = s"y;
          y = incprojmap(k,s).A by A1,A27,A28,Def14;
          hence thesis;
        end;then
        rng (the point-map of incprojmap(k,s)) = the Points of G_(k,X)
              by FUNCT_2:16;
        hence thesis by FUNCT_2:def 3;
      end;
      hence thesis by A18,FUNCT_2:def 4;
    end;
    incprojmap(k,s) is incidence_preserving
    proof
      let A1 be POINT of G_(k,X);
      let L1 be LINE of G_(k,X);
      A1 in the Points of G_(k,X);then
      consider a1 being Subset of X such that
A29:     A1 = a1 & Card a1 = k by A2;
A30:  s.:A1 = incprojmap(k,s).A1 & s.:L1 = incprojmap(k,s).L1 by A1,Def14;
A31:  A1 on L1 implies incprojmap(k,s).A1 on incprojmap(k,s).L1
      proof
        assume A1 on L1;then
        A1 c= L1 by A1,Th10;then
        s.:A1 c= s.:L1 & A1 c= X by A29,RELAT_1:156;
        hence thesis by A1,A30,Th10;
      end;
      incprojmap(k,s).A1 on incprojmap(k,s).L1 implies A1 on L1
      proof
        assume incprojmap(k,s).A1 on incprojmap(k,s).L1;then
        s.:A1 c= s.:L1 & A1 c= X & A1 c= dom s by A1,Th10,A4,A29,A30;then
        A1 c= L1 by FUNCT_1:157;
        hence thesis by A1,Th10;
      end;
      hence thesis by A31;
    end;
    hence thesis by A5,A17,Def9;
  end;

theorem
  for X being non empty set st 0 < k & k + 1 <=` Card X
  for F being IncProjMap over G_(k,X), G_(k,X) holds
  F is automorphism iff
      (ex s being Permutation of X st the IncProjMap of F = incprojmap(k,s))
  proof
    let X be non empty set such that
A1:    0 < k & k + 1 <=` Card X;
    let F be IncProjMap over G_(k,X), G_(k,X);
A2: F is automorphism implies
      (ex s being Permutation of X st the IncProjMap of F = incprojmap(k,s))
    proof
      assume
A3:    F is automorphism;
A4:   0 + 1 < k + 1 & succ k = k + 1 & Card 1 = 1 & Card(k + 1) = k + 1 &
          Card k = k by A1,XREAL_1:10,CARD_1:52,CARD_1:66;then
      1 in succ k & k + 1 in succ Card X by A1,CARD_1:73,ORDINAL1:34;then
      (1 = k or 1 in k) & (k + 1 = Card X or k + 1 in Card X) &
          succ 1 = 1 + 1 & succ(k + 1) = (k + 1) + 1
                 by ORDINAL1:13,CARD_1:52;then
      (1 = k or (1 < k & 2 c= k)) & (k + 1 = Card X or k + 2 <=` Card X)
             by A4,CARD_1:73,ORDINAL1:33;then
      (1 = k or (1 < k & Card X = k + 1) or (2 <= k & k + 2 <=` Card X))
              by CARD_1:56;
      hence thesis by A1,A3,Th24,Th25,Th33;
    end;
    (ex s being Permutation of X st the IncProjMap of F = incprojmap(k,s))
          implies F is automorphism
    proof
      assume ex s being Permutation of X st
            the IncProjMap of F = incprojmap(k,s);then
      consider s being Permutation of X such that
A5:      the IncProjMap of F = incprojmap(k,s);
      incprojmap(k,s) is automorphism by A1,Th34;then
A6:  the line-map of F is bijective & the point-map of F is bijective &
           incprojmap(k,s) is incidence_preserving by A5,Def9;
      F is incidence_preserving
      proof
        let A be POINT of G_(k,X);
        let L be LINE of G_(k,X);
        F.A = incprojmap(k,s).A & F.L = incprojmap(k,s).L by A5;
        hence thesis by A6,Def8;
      end;
      hence thesis by A6,Def9;
    end;
    hence thesis by A2;
  end;
