:: The Fundamental Group
::  by Artur Korni{\l}owicz , Yasunari Shidama and Adam Grabowski
:: 
:: Received March 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary PRE_TOPC, ORDINAL2, FUNCT_1, EUCLID, RELAT_1, ARYTM_1, BORSUK_2,
      GRAPH_1, BORSUK_1, ALGSTR_1, WAYBEL20, PARTFUN1, RELAT_2, EQREL_1,
      VECTSP_1, BINOP_1, GROUP_1, SEQM_3, FUNCT_2, BOOLE, REALSET1, TOPS_1,
      ARYTM, RCOMP_1, ARYTM_3, METRIC_1, COMPLEX1, FINSEQ_2, ABSVALUE,
      SUBSET_1, WELLORD1, GROUP_6, SGRAPH1, BORSUK_6, TOPALG_1;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELSET_1, EQREL_1, FUNCT_1,
      PARTFUN1, FUNCT_2, RELAT_2, BINOP_1, SEQM_3, NUMBERS, ORDINAL1, XCMPLX_0,
      XREAL_0, REAL_1, ABSVALUE, NAT_1, RCOMP_1, RCOMP_2, RVSUM_1, FINSEQ_2,
      COMPLEX1,
      STRUCT_0, GRCAT_1, METRIC_1, PRE_TOPC, YELLOW_2, TOPS_1, TOPS_2,
      VECTSP_1, GROUP_1, BORSUK_1, EUCLID, BORSUK_2, GROUP_6, BORSUK_6;
 constructors TOPS_2, EUCLID, XCMPLX_0, NAT_1, BORSUK_2, VECTSP_1, FILTER_1,
      GROUP_1, SEQM_3, REALSET2, COMPTS_1, TREAL_1, GRCAT_1, TOPS_1, YELLOW_2,
      REAL_1, RCOMP_1, RCOMP_2, T_0TOPSP, GROUP_6, BORSUK_6, BINOP_2,
      COMPLEX1,INT_2;
 clusters PRE_TOPC, RELSET_1, NAT_1, BORSUK_1, BORSUK_2, STRUCT_0, VECTSP_1,
      EQREL_1, PARTFUN1, FILTER_1, XBOOLE_0, JORDAN5A, TEX_2, SEQM_3, BORSUK_4,
      XCMPLX_0, XREAL_0, REAL_1, METRIC_1, EUCLID, TOPS_1,
      COMPLEX1,NUMBERS;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, RELAT_1, FUNCT_1, FUNCT_2, STRUCT_0, VECTSP_1,
      GROUP_1, BORSUK_2, GROUP_6, BORSUK_6;
 theorems BORSUK_2, ZFMISC_1, EQREL_1, TOLER_1, BINOP_1, VECTSP_1, FUNCT_2,
      REALSET2, TARSKI, BORSUK_1, GRCAT_1, TOPS_2, EUCLID, JORDAN6, XBOOLE_0,
      TOPS_1, BORSUK_6, YELLOW16, JORDAN5B, JGRAPH_2, XBOOLE_1, XCMPLX_1,
      FUNCT_1, SEQ_2, REAL_1, TOPREAL3, GOBOARD6, XREAL_0, METRIC_1, AXIOMS,
      SPPOL_1, RVSUM_1, ABSVALUE, PRE_TOPC, SEQ_4, RCOMP_1, FUNCT_3, UNIFORM1,
      RCOMP_2, SQUARE_1, REAL_2, BORSUK_4, JORDAN5A, TOPREAL5, SUBSET_1,
      JORDAN2C, GROUP_6, WAYBEL_1, JGRAPH_6;
 schemes EQREL_1, BINOP_1, XBOOLE_0, FUNCT_2;

begin :: Preliminaries

reserve
  p, q, x, y for real number,
  n for Nat;

theorem
  for G, H being Group, h being Homomorphism of G,H st
  h * h" = id H & h" * h = id G holds h is_isomorphism
proof
  let G, H be Group,
      h be Homomorphism of G,H;
  assume h*h" = id H;
  then
a4: rng h = the carrier of H by YELLOW16:8;
  assume h"*h = id G;
  then h is one-to-one by WAYBEL_1:25;
  hence h is_isomorphism by a4,GROUP_6:70;
end;
  
theorem Top1:
  for X being Subset of I[01], a being Point of I[01] st X = ]. a, 1 .] holds
  X` = [. 0, a .]
proof
  let X be Subset of I[01],
      a be Point of I[01] such that
a1: X = ]. a, 1 .];
  set I = the carrier of I[01];
  set Y = [. 0,a .];
a0: X` = I \ X by SUBSET_1:def 5;
  hereby
    let x be set;
    assume
a4:   x in X`;
    then reconsider y = x as Point of I[01];
a2: 0 <= y by JORDAN5A:2;
    not x in X by a0,a4,XBOOLE_0:def 4;
    then y <= a or y > 1 by a1,RCOMP_2:4;
    hence x in Y by a2,TOPREAL5:1,JORDAN5A:2;
  end;
  let x be set;
  assume
b2: x in Y;
  then reconsider y = x as Real;
b3: 0 <= y by b2,TOPREAL5:1;
b4:y <= a by b2,TOPREAL5:1;
   a <= 1 by JORDAN5A:2;
   then y <= 1 by b4,AXIOMS:22;
   then
b1:y in I by b3,JORDAN5A:2;
  not y in X by a1,b4,RCOMP_2:4;
  hence x in X` by a0,b1,XBOOLE_0:def 4;
end;
  
theorem Top0:
  for X being Subset of I[01], a being Point of I[01] st X = [. 0, a .[ holds
  X` = [. a, 1 .]
proof
  let X be Subset of I[01],
      a be Point of I[01] such that
a1: X = [. 0, a .[;
  set I = the carrier of I[01];
  set Y = [. a,1 .];
a0: X` = I \ X by SUBSET_1:def 5;
  hereby
    let x be set;
    assume
a4:   x in X`;
    then reconsider y = x as Point of I[01];
a2: y <= 1 by JORDAN5A:2;
    not x in X by a0,a4,XBOOLE_0:def 4;
    then y >= a or y < 0 by a1,RCOMP_2:3;
    hence x in Y by a2,TOPREAL5:1,JORDAN5A:2;
  end;
  let x be set;
  assume
b2: x in Y;
  then reconsider y = x as Real;
b3: y <= 1 by b2,TOPREAL5:1;
b4:a <= y by b2,TOPREAL5:1;
   0 <= a by JORDAN5A:2;
   then
b1:y in I by b3,b4,JORDAN5A:2;
  not y in X by a1,b4,RCOMP_2:3;
  hence x in X` by a0,b1,XBOOLE_0:def 4;
end;
  
theorem Top:
  for X being Subset of I[01], a being Point of I[01] st X = ]. a, 1 .] holds
  X is open
proof
  let X be Subset of I[01],
      a be Point of I[01] such that
a1: X = ]. a, 1 .];
  set Y = [. 0,a .];
  Y c= the carrier of I[01]
  proof
    let x be set;
    assume
a3:   x in Y;
    then reconsider x as Real;
a4: 0 <= x by a3,TOPREAL5:1;
a5: x <= a by a3,TOPREAL5:1;
    a <= 1 by JORDAN5A:2;
    then x <= 1 by a5,AXIOMS:22;
    hence thesis by a4,JORDAN5A:2;
  end;
  then reconsider Y as Subset of I[01];
a2: Y is closed by BORSUK_4:48;
  X` = Y by a1,Top1;
  hence X is open by a2,TOPS_1:30;
end;

theorem Top2:
  for X being Subset of I[01], a being Point of I[01] st X = [. 0, a .[ holds
  X is open
proof
  let X be Subset of I[01],
      a be Point of I[01] such that
a1: X = [. 0, a .[;
  set Y = [. a,1 .];
  Y c= the carrier of I[01]
  proof
    let x be set;
    assume
a3:   x in Y;
    then reconsider x as Real;
a4: x <= 1 by a3,TOPREAL5:1;
a5: a <= x by a3,TOPREAL5:1;
    0 <= a by JORDAN5A:2;
    hence thesis by a4,a5,JORDAN5A:2;
  end;
  then reconsider Y as Subset of I[01];
a2: Y is closed by BORSUK_4:48;
  X` = Y by a1,Top0;
  hence X is open by a2,TOPS_1:30;
end;

theorem Tgg:
  for f being Element of n-tuples_on REAL holds x * (-f) = - (x*f)
proof
  let f be Element of n-tuples_on REAL;
a2: x is Real by XREAL_0:def 1;
  thus x * (-f) = x*((-1)*f) by RVSUM_1:76
    .= (-1) * x*f by a2,RVSUM_1:71
    .= (-1) * (x*f) by a2,RVSUM_1:71
    .= - x*f by RVSUM_1:76;
end;

theorem Tff:
  for f, g being Element of n-tuples_on REAL holds x * (f-g) = x*f - x*g
proof
  let f, g be Element of n-tuples_on REAL;
a2: x is Real by XREAL_0:def 1;
  thus x * (f-g) = x * (f+-g) by RVSUM_1:52
    .= (x*f) + (x*(-g)) by a2,RVSUM_1:73
    .= (x*f) + -(x*g) by Tgg
    .= x*f - x*g by RVSUM_1:52;
end;

theorem Tff1:
  for f being Element of n-tuples_on REAL holds (x-y) * f = x*f - y*f
proof
  let f be Element of n-tuples_on REAL;
c1: REAL n = n-tuples_on REAL by EUCLID:def 1;
  then reconsider g = f as Point of TOP-REAL n by EUCLID:25;
a1:(x-y) * g = x*g - y*g by EUCLID:54;
  (x-y) * g = (x-y) * f & x*g = x*f & y*g = y*f by c1,EUCLID:def 11;
  hence (x-y) * f = x*f - y*f by a1,c1,EUCLID:def 13;
end;

theorem Ta1:
  for f, g, h, k being Element of n-tuples_on REAL holds
  (f+g)-(h+k) = (f-h)+(g-k)
proof
  let f, g, h, k be Element of n-tuples_on REAL;
  thus f+g-(h+k) = f+g+-(h+k) by RVSUM_1:52
    .= f+(g+-(h+k)) by RVSUM_1:32
    .= f+(g+(-h+-k)) by RVSUM_1:45
    .= f+(-h+(g+-k)) by RVSUM_1:32
    .= f+-h+(g+-k) by RVSUM_1:32
    .= (f-h)+(g+-k) by RVSUM_1:52
    .= (f-h) + (g-k) by RVSUM_1:52;
end;
  
theorem Tla:
  for f being Element of REAL n st 0 <= x & x <= 1 holds |.x*f.| <= |.f.|
proof
  let f be Element of REAL n such that
a1: 0 <= x and
a2: x <= 1;
a3:|.x*f.| = abs(x)*|.f.| by EUCLID:14;
a4: 0 <= |.f.| by EUCLID:12;
  x = abs(x) by a1,ABSVALUE:def 1;
  then |.x*f.| <= 1*|.f.| by a1,a2,a3,a4,SEQ_4:7;
  hence |.x*f.| <= |.f.|;
end;

theorem
  for f being Element of REAL n, p being Point of I[01] holds |.p*f.| <= |.f.|
proof
  let f be Element of REAL n,
      p be Point of I[01];
OO: [. 0,1 .] = {r where r is Real: 0 <= r & r <= 1 } by RCOMP_1:def 1;
  p in the carrier of I[01];
  then ex r being Real st r = p & 0 <= r & r <= 1 by OO,BORSUK_1:83;
  hence |.p*f.| <= |.f.| by Tla;
end;

theorem
  for e1, e2, e3, e4, e5, e6 being Point of Euclid n,
      p1, p2, p3, p4 being Point of TOP-REAL n st
   e1 = p1 & e2 = p2 & e3 = p3 & e4 = p4 & e5 = p1+p3 & e6 = p2+p4 &
   dist(e1,e2) < x & dist(e3,e4) < y
   holds dist(e5,e6) < x+y
proof
  let e1, e2, e3, e4, e5, e6 be Point of Euclid n,
      p1, p2, p3, p4 be Point of TOP-REAL n such that
a1: e1 = p1 & e2 = p2 & e3 = p3 & e4 = p4 and
a2: e5 = p1+p3 and
m1: e6 = p2+p4 and
a3: dist(e1,e2) < x and
a4: dist(e3,e4) < y;
  reconsider f1 = e1, f2 = e2, f3 = e3, f4 = e4, f5 = e5, f6 = e6 as
    Element of REAL n by a1,a2,m1,EUCLID:25;
c1: REAL n = n-tuples_on REAL by EUCLID:def 1;
b1: dist(e5,e6) = |. f5-f6 .| by SPPOL_1:20
    .= |. f1+f3-f6 .| by a1,a2,EUCLID:def 10
    .= |. f1+f3-(f2+f4) .| by a1,m1,EUCLID:def 10
    .= |. (f1-f2) + (f3-f4) .| by c1,Ta1;
b2: dist(e1,e2) = |. f1-f2 .| & dist(e3,e4) = |. f3-f4 .| by SPPOL_1:20;
b4: |. f1-f2 + (f3-f4) .| <= |. f1-f2 .| + |. f3-f4 .| by EUCLID:15;
  dist(e1,e2) + dist(e3,e4) < x + y by a3,a4,REAL_1:67;
  hence dist(e5,e6) < x+y by b1,b2,b4,AXIOMS:22;
end;
   
theorem Til:
  for e1, e2, e5, e6 being Point of Euclid n,
      p1, p2 being Point of TOP-REAL n st
   e1 = p1 & e2 = p2 & e5 = y*p1 & e6 = y*p2 & dist(e1,e2) < x & y <> 0
   holds dist(e5,e6) < abs(y)*x
proof
  let e1, e2, e5, e6 be Point of Euclid n,
      p1, p2 be Point of TOP-REAL n such that
a1: e1 = p1 & e2 = p2 and
a2: e5 = y*p1 and
j1: e6 = y*p2 and
a3: dist(e1,e2) < x and
ee: y <> 0;
  reconsider f1 = e1, f2 = e2, f5 = e5, f6 = e6 as Element of REAL n
    by a1,a2,j1,EUCLID:25;
c1: REAL n = n-tuples_on REAL by EUCLID:def 1;
b1: dist(e5,e6) = |. f5-f6 .| by SPPOL_1:20
    .= |. y*f1-f6 .| by a1,a2,EUCLID:def 11
    .= |. y*f1-y*f2 .| by a1,j1,EUCLID:def 11
    .= |. y*(f1-f2) .| by c1,Tff
    .= abs(y) * |. f1-f2 .| by EUCLID:14;
b2: dist(e1,e2) = |. f1-f2 .| by SPPOL_1:20;
  0 < abs(y) by ee,ABSVALUE:6;
  hence dist(e5,e6) < abs(y)*x by b1,b2,a3,REAL_1:70;
end;

theorem Tsum2:
  for e1, e2, e3, e4, e5, e6 being Point of Euclid n,
      p1, p2, p3, p4 being Point of TOP-REAL n st
   e1 = p1 & e2 = p2 & e3 = p3 & e4 = p4 & e5 = x*p1+y*p3 & e6 = x*p2+y*p4 &
   dist(e1,e2) < p & dist(e3,e4) < q & x <> 0 & y <> 0
   holds dist(e5,e6) < abs(x)*p + abs(y)*q
proof
  let e1, e2, e3, e4, e5, e6 be Point of Euclid n,
      p1, p2, p3, p4 be Point of TOP-REAL n such that
a1: e1 = p1 & e2 = p2 & e3 = p3 & e4 = p4 and
a2: e5 = x*p1+y*p3 and
m1: e6 = x*p2+y*p4 and
a3: dist(e1,e2) < p and
a4: dist(e3,e4) < q and
f1: x <> 0 and
f2: y <> 0;
  reconsider f1 = e1, f2 = e2, f3 = e3, f4 = e4, f5 = e5, f6 = e6 as
    Element of REAL n by a1,a2,m1,EUCLID:25;
c1: REAL n = n-tuples_on REAL by EUCLID:def 1;
    x*p1 = x*f1 & y*p3 = y*f3 by a1,EUCLID:def 11;
    then
f3: f5 = x*f1+y*f3 by a2,EUCLID:def 10;
    x*p2 = x*f2 & y*p4 = y*f4 by a1,EUCLID:def 11;
    then f6 = x*f2+y*f4 by m1,EUCLID:def 10;
    then
b1: dist(e5,e6) = |. x*f1+y*f3 - (x*f2+y*f4) .| by f3,SPPOL_1:20
    .= |. (x*f1-x*f2) + (y*f3-y*f4) .| by c1,Ta1
    .= |. x*(f1-f2) + (y*f3-y*f4) .| by c1,Tff
    .= |. x*(f1-f2) + y*(f3-f4) .| by c1,Tff;
b2: dist(e1,e2) = |. f1-f2 .| & dist(e3,e4) = |. f3-f4 .| by SPPOL_1:20;
    |. x*(f1-f2) + y*(f3-f4) .| <= |. x*(f1-f2) .| + |. y*(f3-f4) .|
      by EUCLID:15;
    then |. x*(f1-f2) + y*(f3-f4) .| <= |.x*(f1-f2).| + abs(y)*|.(f3-f4).|
      by EUCLID:14;
    then
b5: |. x*(f1-f2) + y*(f3-f4) .| <= abs(x)*|.f1-f2.| + abs(y)*|.f3-f4.|
      by EUCLID:14;
  0 < abs(x) & 0 < abs(y) by f1,f2,ABSVALUE:6;
  then abs(x)*|.f1-f2.| < abs(x)*p & abs(y)*|.f3-f4.| < abs(y)*q
    by a3,a4,b2,REAL_1:70;
  then abs(x)*|.f1-f2.| + abs(y)*|.f3-f4.| < abs(x)*p + abs(y)*q by REAL_1:67;
  hence dist(e5,e6) < abs(x)*p+abs(y)*q by b1,b5,AXIOMS:22;
end;

canceled;

TT102:
  for X being non empty TopSpace,
      f1, f2, g being map of X,TOP-REAL n st
    f1 is continuous & f2 is continuous &
    for p being Point of X holds g.p = f1.p + f2.p
  holds g is continuous
proof
  let X being non empty TopSpace,
      f1, f2, g be map of X,TOP-REAL n such that
A1: f1 is continuous and
A1a: f2 is continuous and
A3: for p being Point of X holds g.p = f1.p + f2.p;
  consider h being map of X,TOP-REAL n such that
a1: for r being Point of X holds h.r = f1.r + f2.r and
a2: h is continuous by A1,A1a,JGRAPH_6:20;
  for x being Point of X holds g.x = h.x
  proof
    let x be Point of X;
    thus g.x = f1.x + f2.x by A3
      .= h.x by a1;
  end;
  then g = h by FUNCT_2:113;
  hence g is continuous by a2;
end;

theorem Tcon1:
  for X being non empty TopSpace,
      f, g being map of X,TOP-REAL n st
    f is continuous & for p being Point of X holds g.p = y * f.p
  holds g is continuous
proof
  let X being non empty TopSpace,
      f, g be map of X,TOP-REAL n such that
A1: f is continuous and
A3: for p being Point of X holds g.p = y * f.p;
  for p being Point of X, V being Subset of TOP-REAL n st g.p in V & V is open
   ex W being Subset of X st p in W & W is open & g.:W c= V
  proof
    let p be Point of X,
        V be Subset of TOP-REAL n;
    assume g.p in V & V is open;
    then
A5: g.p in Int V by TOPS_1:55;
    reconsider r = g.p as Point of Euclid n by TOPREAL3:13;
    consider r0 being real number such that
A6:   r0 > 0 and
A6a:  Ball(r,r0) c= V by A5,GOBOARD6:8;
hh: r0/abs(y) is Real by XREAL_0:def 1;
    reconsider r1 = f.p as Point of Euclid n by TOPREAL3:13;
    reconsider G1 = Ball(r1,r0/abs(y)) as Subset of TOP-REAL n by TOPREAL3:13;
    per cases;
    suppose
ee:   y <> 0;
    then
W1: 0 < abs(y) by ABSVALUE:6;
    then
B1: r0/abs(y) > 0 by A6,SEQ_2:6;
A8: r1 in G1 by B1,GOBOARD6:4;
    G1 is open by hh,GOBOARD6:6;
    then consider W1 being Subset of X such that
A9:   p in W1 and
A9a:   W1 is open and
A9b:   f.:W1 c= G1 by A1,A8,JGRAPH_2:20;
    take W1;
    thus p in W1 by A9;
    thus W1 is open by A9a;
    g.:W1 c= Ball(r,r0)
    proof
      let x be set;
      assume x in g.:W1;
      then consider z being set such that
A15:    z in dom g and
A15a:    z in W1 and
A15b:    g.z = x by FUNCT_1:def 12;
      reconsider z as Point of X by A15;
      z in the carrier of X;
      then z in dom f by FUNCT_2:def 1;
      then
A18:  f.z in f.:W1 by A15a,FUNCT_1:def 12;
      reconsider ea1 = f.z as Point of Euclid n by TOPREAL3:13;
A21:  x = y * f.z by A3,A15b;
      then reconsider e1x = x as Point of Euclid n by TOPREAL3:13;
C1:   dist(r1,ea1) < r0/abs(y) by A9b,A18,METRIC_1:12;
      r = y * f.p by A3;
      then dist(r,e1x) < abs(y)*(r0/abs(y)) by ee,A21,C1,Til;
      then dist(r,e1x) < r0 by W1,XCMPLX_1:88;
      hence x in Ball(r,r0) by METRIC_1:12;
    end;
    hence g.:W1 c= V by A6a,XBOOLE_1:1;
  end;
  suppose
K4: y = 0;
  take W = [#]X;
  thus p in W by PRE_TOPC:13;
  thus W is open;
  let x be set;
  assume x in g.:W;
  then consider z being set such that
     z in dom g and
K2:  z in W and
K3:  g.z = x by FUNCT_1:def 12;
  reconsider z as Point of X by K2;
K5:x = y * f.z by A3,K3
     .= 0.REAL n by K4,EUCLID:33;
  r = y * f.p by A3
   .= 0.REAL n by K4,EUCLID:33;
  then x in Ball(r,r0) by A6,K5,GOBOARD6:4;
  hence x in V by A6a;
  end;
  end;
  hence g is continuous by JGRAPH_2:20;
end;

theorem
  for X being non empty TopSpace,
      f1, f2, g being map of X,TOP-REAL n st
    f1 is continuous & f2 is continuous &
    for p being Point of X holds g.p = x * f1.p + y * f2.p
  holds g is continuous
proof
  let X being non empty TopSpace,
      f1, f2, g be map of X,TOP-REAL n such that
A1: f1 is continuous and
A1a: f2 is continuous and
A3: for p being Point of X holds g.p = x*f1.p + y*f2.p;
  per cases;
  suppose that
x0: x <> 0 and
y0: y <> 0;
  for p being Point of X, V being Subset of TOP-REAL n st g.p in V & V is open
   ex W being Subset of X st p in W & W is open & g.:W c= V
  proof
    let p be Point of X,
        V be Subset of TOP-REAL n;
    assume g.p in V & V is open;
    then
A5: g.p in Int V by TOPS_1:55;
    reconsider r = g.p as Point of Euclid n by TOPREAL3:13;
    consider r0 being real number such that
A6:   r0 > 0 and
A6a:  Ball(r,r0) c= V by A5,GOBOARD6:8;
hh: r0/2/abs(x) is Real by XREAL_0:def 1;
HH: r0/2/abs(y) is Real by XREAL_0:def 1;
u1: r0/2 > 0 by A6,SEQ_2:3;
    reconsider r1 = f1.p as Point of Euclid n by TOPREAL3:13;
    reconsider G1 = Ball(r1,r0/2/abs(x)) as Subset of TOP-REAL n
      by TOPREAL3:13;
Xa: abs(x) > 0 by x0,ABSVALUE:6;      
Xb: abs(y) > 0 by y0,ABSVALUE:6;
    r0/2/abs(x) > 0 by u1,Xa,SEQ_2:6;
    then
A8: r1 in G1 by GOBOARD6:4;
    G1 is open by hh,GOBOARD6:6;
    then consider W1 being Subset of X such that
A9:   p in W1 and
A9a:   W1 is open and
A9b:   f1.:W1 c= G1 by A1,A8,JGRAPH_2:20;
    reconsider r2 = f2.p as Point of Euclid n by TOPREAL3:13;
    reconsider G2 = Ball(r2,r0/2/abs(y)) as Subset of TOP-REAL n
      by TOPREAL3:13;
    r0/2/abs(y) > 0 by u1,Xb,SEQ_2:6;
    then
A11: r2 in G2 by GOBOARD6:4;
    G2 is open by HH,GOBOARD6:6;
    then consider W2 being Subset of X such that
A12:  p in W2 and
A12a:  W2 is open and
A12b:  f2.:W2 c= G2 by A1a,A11,JGRAPH_2:20;
    take W = W1 /\ W2;
    thus p in W by A9,A12,XBOOLE_0:def 3;
    thus W is open by A9a,A12a,TOPS_1:38;
    g.:W c= Ball(r,r0)
    proof
      let a be set;
      assume a in g.:W;
      then consider z being set such that
A15:    z in dom g and
A15a:    z in W and
A15b:    g.z = a by FUNCT_1:def 12;
A16:  z in W1 by A15a,XBOOLE_0:def 3;
      reconsider z as Point of X by A15;
A17:  z in the carrier of X;
      then z in dom f1 by FUNCT_2:def 1;
      then
A18:  f1.z in f1.:W1 by A16,FUNCT_1:def 12;
      reconsider ea1 = f1.z as Point of Euclid n by TOPREAL3:13;
A19:  z in W2 by A15a,XBOOLE_0:def 3;
      z in dom f2 by A17,FUNCT_2:def 1;
      then
A20:  f2.z in f2.:W2 by A19,FUNCT_1:def 12;
      reconsider ea2 = f2.z as Point of Euclid n by TOPREAL3:13;
A21:  a = x*f1.z + y*f2.z by A3,A15b;
      then reconsider e1x = a as Point of Euclid n by TOPREAL3:13;
C1:   dist(r1,ea1) < r0/2/abs(x) by A9b,A18,METRIC_1:12;
C4:   dist(r2,ea2) < r0/2/abs(y) by A12b,A20,METRIC_1:12;
      r = x*f1.p + y*f2.p by A3;
      then dist(r,e1x) < abs(x)*(r0/2/abs(x)) + abs(y)*(r0/2/abs(y))
        by x0,y0,A21,C1,C4,Tsum2;
      then dist(r,e1x) < abs(x)*(r0/abs(x)/2) + abs(y)*(r0/2/abs(y))
        by XCMPLX_1:48;
      then dist(r,e1x) < abs(x)*(r0/abs(x)/2) + abs(y)*(r0/abs(y)/2)
        by XCMPLX_1:48;
      then dist(r,e1x) < r0/2 + abs(y)*(r0/abs(y)/2) by Xa,XCMPLX_1:98;
      then dist(r,e1x) < r0/2 + r0/2 by Xb,XCMPLX_1:98;
      hence a in Ball(r,r0) by METRIC_1:12;
    end;
    hence g.:W c= V by A6a,XBOOLE_1:1;
  end;
  hence g is continuous by JGRAPH_2:20;
  end;
  suppose
d1: x = 0;
  for p being Point of X holds g.p = y * f2.p
  proof
    let p be Point of X;
    thus g.p = x*f1.p + y*f2.p by A3
      .= 0.REAL n + y*f2.p by d1,EUCLID:33
      .= y*f2.p by EUCLID:31;
  end;
  hence thesis by A1a,Tcon1;
  end;
  suppose
d1: y = 0;
  for p being Point of X holds g.p = x * f1.p
  proof
    let p be Point of X;
    thus g.p = x*f1.p + y*f2.p by A3
      .= x*f1.p + 0.REAL n by d1,EUCLID:33
      .= x*f1.p by EUCLID:31;
  end;
  hence thesis by A1,Tcon1;
  end;
end;

theorem TT100:
  for F being map of [:TOP-REAL n,I[01]:], TOP-REAL n st
   for x being Point of TOP-REAL n, i being Point of I[01] holds
    F.(x,i) = (1-i) * x holds
  F is continuous
proof
  let F be map of [:TOP-REAL n,I[01]:], TOP-REAL n such that
A3: for x being Point of TOP-REAL n, i being Point of I[01] holds
    F.(x,i) = (1-i) * x;
   set I = the carrier of I[01];
AA: [#]I[01] = I by PRE_TOPC:12;
c1: REAL n = n-tuples_on REAL by EUCLID:def 1;
  for p being Point of [:TOP-REAL n,I[01]:], V being Subset of TOP-REAL n st
   F.p in V & V is open
   ex W being Subset of [:TOP-REAL n,I[01]:] st p in W & W is open & F.:W c= V
  proof
    let p be Point of [:TOP-REAL n,I[01]:],
        V be Subset of TOP-REAL n;
    assume F.p in V & V is open;
    then
A5: F.p in Int V by TOPS_1:55;
    reconsider ep = F.p as Point of Euclid n by TOPREAL3:13;
    consider x being Point of TOP-REAL n, i being Point of I[01] such that
L0:   p = [x,i] by BORSUK_1:50;
    consider r0 being real number such that
A6:   r0 > 0 and
A6a:  Ball(ep,r0) c= V by A5,GOBOARD6:8;
    reconsider lx = x as Point of Euclid n by TOPREAL3:13;
    reconsider fx = x as Element of REAL n by EUCLID:25;
hh: r0/2/(1-i) is Real by XREAL_0:def 1;
d1: r0/2 > 0 by A6,SEQ_2:6;
FF: 0 <= |.fx.| by EUCLID:12;
a2: ep = F.(x,i) by L0,BINOP_1:def 1
      .= (1-i)*x by A3;
    per cases;
    suppose
E1:   1-i > 0;
    then
B1: r0/2/(1-i) > 0 by d1,SEQ_2:6;
    reconsider B = Ball(lx,r0/2/(1-i)) as Subset of TOP-REAL n by TOPREAL3:13;
    set t = 2*(1-i)*|.fx.|+r0;
    set c = (1-i)*r0 / t;
    set X1 = ]. i-c, i+c .[;
    set X2 = X1 /\ I;
Bc: 0 < 2*(1-i) by E1,SQUARE_1:21;
Bz: 0 <= 2*(1-i)*|.fx.| by Bc,FF,SQUARE_1:19;
    then
Bb: 0 + 0 < t by A6,REAL_1:67;
    0 < (1-i)*r0 by E1,A6,SQUARE_1:21;
    then
EE: 0 < c by Bb,REAL_2:127;
    reconsider X2 as Subset of I[01] by XBOOLE_1:17;
    take W = [:B,X2:];
M2: x in B by B1,GOBOARD6:4;
    abs(i-i) < c by EE,ABSVALUE:def 1;
    then i in X1 by RCOMP_1:8;
    then i in X2 by XBOOLE_0:def 3;
    hence p in W by L0,M2,ZFMISC_1:106;
    0 <= i by JORDAN5A:2;
    then
kk: 0 <= i*(2*(1-i)*|.fx.|) by Bz,SQUARE_1:19;
Bj: i*2*(1-i)*|.fx.|+0 <= i*2*(1-i)*|.fx.|+r0 by A6,AXIOMS:24;

Cd: i+c = i*t/t + ((1-i)*r0 / t) by Bb,XCMPLX_1:90
       .= (i*t + (1-i)*r0) / t by XCMPLX_1:63
       .= (i*2*(1-i)*|.fx.| + r0) / t;
    then
Y1: 0 <= i+c by Bb,Bj,kk,REAL_2:125;
bh: i+c-1 = (i*2*(1-i)*|.fx.|+r0) / t - t / t by Cd,Bb,XCMPLX_1:60
         .= (i*2*(1-i)*|.fx.|+r0 - t) / t by XCMPLX_1:121;
    --(1-i) > -0 by E1;
    then -(1-i) < 0 by REAL_1:50;
    then (i-1)*(2*(1-i)*|.fx.|) <= 0 by Bz,SQUARE_1:23;
    then i+c-1 <= 0 by Bb,bh,REAL_2:126;
    then
Ju: i+c-1+1 <= 0+1 by REAL_1:55;
    then
Y3: i+c is Point of I[01] by Y1,JORDAN5A:2;
M1: now
      per cases;
      suppose
Y4:     0 <= i-c;
      X1 c= the carrier of I[01]
      proof
        let a be set;
        assume
Y8:       a in X1;
        then reconsider a as Real;
        i-c < a & a < i+c by Y8,JORDAN6:45;
        then 0 < a & a < 1 by Y4,Ju,AXIOMS:22;
        hence thesis by JORDAN5A:2;
      end;
      then reconsider X1 as Subset of I[01];
      now per cases;
        suppose i-c <= i+c;
        then i-c <= 1 by Ju,AXIOMS:22;
        then i-c is Point of I[01] by Y4,JORDAN5A:2;
        hence X1 is open by Y3,BORSUK_4:70;
        end;
        suppose i-c > i+c;
        then X1 = {} by RCOMP_1:12;
        then X1 = {}I[01] by PRE_TOPC:def 2;
        hence X1 is open by TOPS_1:52;
        end;
      end;  
      hence X2 is open by AA,TOPS_1:38;
      end;
      suppose
H5:     i-c < 0;
      X2 = [. 0, i+c .[
      proof
        hereby
          let a be set;
          assume
H0:         a in X2;
          then
H2:       a in X1 by XBOOLE_0:def 3;
          then reconsider b = a as Real;
H1:       b < i+c by H2,JORDAN6:45;
          0 <= b by H0,JORDAN5A:2;
          hence a in [. 0,i+c.[ by H1,RCOMP_2:3;
        end;
        let a be set;
        assume
H4:       a in [. 0,i+c .[;
        then reconsider b = a as Real;
H6:     b < i+c by H4,RCOMP_2:3;
L2:     0 <= b by H4,RCOMP_2:3;
        then
H3:     a in X1 by H5,H6,JORDAN6:45;
        b <= 1 by H6,Ju,AXIOMS:22;
        then a in I by L2,TOPREAL5:1,BORSUK_1:83;
        hence a in X2 by H3,XBOOLE_0:def 3;
      end;
      hence X2 is open by Y3,Top2;
      end;
    end;
    B is open by hh,GOBOARD6:6;
    hence W is open by M1,BORSUK_1:46;
    F.:W c= Ball(ep,r0)
    proof
      let m be set;
      assume m in F.:W;
      then consider z being set such that
A15:    z in dom F and
A15a:    z in W and
A15b:    F.z = m by FUNCT_1:def 12;
      reconsider z as Point of [:TOP-REAL n,I[01]:] by A15;
      consider y being Point of TOP-REAL n, j being Point of I[01] such that
L1:     z = [y,j] by BORSUK_1:50;
      reconsider ez = F.z, ey = y as Point of Euclid n by TOPREAL3:13;
d2:   ez = F.(y,j) by L1,BINOP_1:def 1
        .= (1-j)*y by A3;
      reconsider fp = ep, fz = ez, fe = (1-i)*y, fy = y as Element of REAL n
        by EUCLID:25;
d4:   dist(ep,ez) = |.fp-fz.| by SPPOL_1:20;
d5:   |.fp-fz.| <= |.fp-fe.| + |.fe-fz.| by EUCLID:22;
      fy in B by A15a,L1,ZFMISC_1:106;
      then
e1:   dist(lx,ey) < r0/2/(1-i) by METRIC_1:12;
d6:   dist(lx,ey) = |.fx-fy.| by SPPOL_1:20;
      then
e3:   (1-i) * |.fx-fy.| < (1-i) * (r0/2/(1-i)) by e1,E1,REAL_1:70;
e4:   (1-i) * (r0/(1-i)/2) = r0/2 by E1,XCMPLX_1:98;
G8:   i is Real & j is Real by XREAL_0:def 1;        
      fe = (1-i)*fy & fz = (1-j)*fy by d2,EUCLID:def 11;
      then
G5:   |.fe-fz.| = |.(1-i-(1-j))*fy.| by c1,Tff1
        .= abs(j-i)*|.fy.| by EUCLID:14
        .= abs(i-j)*|.fy.| by G8,UNIFORM1:13;
GG:   0 <= |.fy.| by EUCLID:12;
      j in X2 by L1,A15a,ZFMISC_1:106;
      then j in X1 by XBOOLE_0:def 3;
      then abs(j-i) < c by RCOMP_1:8;
      then abs(i-j) < c by G8,UNIFORM1:13;
      then
WW:   abs(i-j)*|.fy.| <= c*|.fy.| by GG,AXIOMS:25;
W2:   |.fx-fy.| = |.fy-fx.| by EUCLID:21;
      |.fy.| - |.fx.| <= |.fy-fx.| by EUCLID:18;
      then |.fy.| - |.fx.| < r0/2/(1-i) by e1,d6,W2,AXIOMS:22;
      then |.fy.| < |.fx.| + r0/2/(1-i) by REAL_1:84;
      then
Gy:   c*|.fy.| < c * (|.fx.| + r0/2/(1-i)) by EE,REAL_1:70;
      c * (|.fx.| + r0/2/(1-i)) = c * (|.fx.| + r0/(2*(1-i))) by XCMPLX_1:79
       .= c * ((|.fx.|*(2*(1-i)))/(2*(1-i))+r0/(2*(1-i))) by Bc,XCMPLX_1:90
       .= c * ((|.fx.|*(2*(1-i))+r0)/(2*(1-i))) by XCMPLX_1:63
       .= (1-i)*r0 / (2*(1-i)) by Bb,XCMPLX_1:99
       .= r0/2 by E1,XCMPLX_1:92;
      then
G6:   abs(i-j)*|.fy.| <= r0/2 by Gy,WW,AXIOMS:22;
      r0/2/(1-i) = r0/(1-i)/2 by XCMPLX_1:48;
      then abs(1-i) * |.fx-fy.| < r0/2 by e3,e4,E1,ABSVALUE:def 1;
      then |.(1-i)*(fx-fy).| < r0/2 by EUCLID:14;
      then |.(1-i)*fx-(1-i)*fy.| < r0/2 by c1,Tff;
      then |.(1-i)*fx-fe.| < r0/2 by EUCLID:def 11;
      then |.fp-fe.| < r0/2 by a2,EUCLID:def 11;
      then |.fp-fe.| + |.fe-fz.| < r0/2 + r0/2 by G5,G6,REAL_1:67;
      then dist(ep,ez) < r0 by d4,d5,AXIOMS:22;
      hence thesis by A15b,METRIC_1:12;
  end;
  hence F.:W c= V by A6a,XBOOLE_1:1;
  end;
  suppose 1-i <= 0;
  then
s1: 1-i+i <= 0+i by AXIOMS:24;
  i <= 1 by JORDAN5A:2;
  then
E1: i = 1 by s1,AXIOMS:21;
hh: r0 is Real by XREAL_0:def 1;
  reconsider B = Ball(lx,r0) as Subset of TOP-REAL n by TOPREAL3:13;
  set t = |.fx.|+r0;
  set c = r0 / t;
  set X1 = ]. 1-c, 1 .];
Bb: 0 + 0 < t by A6,FF,REAL_1:67;
EE: 0 < c by A6,Bb,REAL_2:127;
    0+r0 <= t by FF,REAL_1:55;
    then
TT: c <= 1 by Bb,REAL_2:117;
    then
ee: c-c <= 1-c by REAL_1:49;
    c is Point of I[01] by EE,TT,JORDAN5A:2;
    then
PP: 1-c is Point of I[01] by JORDAN5B:4;
    X1 c= I
    proof
      let s be set;
      assume
s1:     s in X1;
      then reconsider s as Real;
s2:   s <= 1 by s1,RCOMP_2:4;
      1-c < s by s1,RCOMP_2:4;
      then 0 <= s by ee,AXIOMS:22;
      hence thesis by s2,JORDAN5A:2;
    end;
    then reconsider X1 as Subset of I[01];
    take W = [:B,X1:];
M2: x in B by A6,GOBOARD6:4;
    1-c < 1-0 by EE,REAL_1:92;
    then i in X1 by E1,RCOMP_2:4;
    hence p in W by L0,M2,ZFMISC_1:106;
M1: X1 is open by PP,Top;
    B is open by hh,GOBOARD6:6;
    hence W is open by M1,BORSUK_1:46;
    F.:W c= Ball(ep,r0)
    proof
      let m be set;
      assume m in F.:W;
      then consider z being set such that
A15:    z in dom F and
A15a:    z in W and
A15b:    F.z = m by FUNCT_1:def 12;
      reconsider z as Point of [:TOP-REAL n,I[01]:] by A15;
      consider y being Point of TOP-REAL n, j being Point of I[01] such that
L1:     z = [y,j] by BORSUK_1:50;
      reconsider ez = F.z, ey = y as Point of Euclid n by TOPREAL3:13;
d2:   ez = F.(y,j) by L1,BINOP_1:def 1
        .= (1-j)*y by A3;
      reconsider fp = ep, fz = ez, fe = (1-i)*y, fy = y, f0 = 0.REAL n
        as Element of REAL n by EUCLID:25;
kk:   F.z - 0.REAL n = F.z by JORDAN2C:13;
h1:   fz = (1-j)*fy by d2,EUCLID:def 11;
      1-j is Point of I[01] by JORDAN5B:4;
      then
h2:   0 <= 1-j by JORDAN5A:2;
uy:   fz-fp = fz-f0 by a2,E1,EUCLID:33
        .= fz by kk,EUCLID:def 13;
d4:   dist(ep,ez) = |.fz-fp.| by SPPOL_1:20
        .= abs(1-j)*|.fy.| by h1,uy,EUCLID:14
        .= (1-j)*|.fy.| by h2,ABSVALUE:def 1;
      fy in B by A15a,L1,ZFMISC_1:106;
      then
e1:   dist(lx,ey) < r0 by METRIC_1:12;
d6:   dist(lx,ey) = |.fx-fy.| by SPPOL_1:20;
W2:   |.fx-fy.| = |.fy-fx.| by EUCLID:21;
      |.fy.| - |.fx.| <= |.fy-fx.| by EUCLID:18;
      then |.fy.| - |.fx.| < r0 by e1,d6,W2,AXIOMS:22;
      then
h3:   |.fy.| < t by REAL_1:84;
      now
      per cases;
      suppose j < 1;
      then
k1:   j-j < 1-j by REAL_1:92;
      j in X1 by L1,A15a,ZFMISC_1:106;
      then 1-c < j by RCOMP_2:4;
      then 1-c+c < j+c by REAL_1:67;
      then 1-j < j+c-j by REAL_1:92;
      then 1-j < c by XCMPLX_1:26;
      then r0/(1-j) > r0/c by A6,k1,REAL_2:200;
      then t < r0/(1-j) by EE,XCMPLX_1:54;
      then |.fy.| < r0/(1-j) by h3,AXIOMS:22;
      then (1-j)*|.fy.| < (1-j)*(r0/(1-j)) by k1,REAL_1:70;
      hence (1-j)*|.fy.| < r0 by k1,XCMPLX_1:88;
      end;
      suppose
oo:     j >= 1;
      j <= 1 by JORDAN5A:2;
      then j = 1 by oo,AXIOMS:21;
      hence (1-j)*|.fy.| < r0 by A6;
      end;
      end;
      hence thesis by A15b,d4,METRIC_1:12;
  end;
  hence F.:W c= V by A6a,XBOOLE_1:1;
  end;
  end;
  hence F is continuous by JGRAPH_2:20;
end;
  
theorem TT101:
  for F being map of [:TOP-REAL n,I[01]:], TOP-REAL n st
   for x being Point of TOP-REAL n, i being Point of I[01] holds
    F.(x,i) = i * x holds
  F is continuous
proof
  let F be map of [:TOP-REAL n,I[01]:], TOP-REAL n such that
A3: for x being Point of TOP-REAL n, i being Point of I[01] holds
    F.(x,i) = i * x;
   set I = the carrier of I[01];
AA: [#]I[01] = I by PRE_TOPC:12;
c1: REAL n = n-tuples_on REAL by EUCLID:def 1;
  for p being Point of [:TOP-REAL n,I[01]:], V being Subset of TOP-REAL n st
   F.p in V & V is open
   ex W being Subset of [:TOP-REAL n,I[01]:] st p in W & W is open & F.:W c= V
  proof
    let p be Point of [:TOP-REAL n,I[01]:],
        V be Subset of TOP-REAL n;
    assume F.p in V & V is open;
    then
A5: F.p in Int V by TOPS_1:55;
    reconsider ep = F.p as Point of Euclid n by TOPREAL3:13;
    consider x being Point of TOP-REAL n, i being Point of I[01] such that
L0:   p = [x,i] by BORSUK_1:50;
    consider r0 being real number such that
A6:   r0 > 0 and
A6a:  Ball(ep,r0) c= V by A5,GOBOARD6:8;
    reconsider lx = x as Point of Euclid n by TOPREAL3:13;
    reconsider fx = x as Element of REAL n by EUCLID:25;
hh: r0/2/i is Real by XREAL_0:def 1;
d1: r0/2 > 0 by A6,SEQ_2:6;
FF: 0 <= |.fx.| by EUCLID:12;
a2: ep = F.(x,i) by L0,BINOP_1:def 1
      .= i*x by A3;
    per cases;
    suppose
E1:   i > 0;
B1: r0/2/i > 0 by d1,E1,SEQ_2:6;
    reconsider B = Ball(lx,r0/2/i) as Subset of TOP-REAL n by TOPREAL3:13;
    set t = 2*i*|.fx.|+r0;
    set c = i*r0 / t;
    set X1 = ]. i-c, i+c .[;
    set X2 = X1 /\ I;
Bc: 0 < 2*i by E1,SQUARE_1:21;
Bz: 0 <= 2*i*|.fx.| by Bc,FF,SQUARE_1:19;
    then
Bb: 0 + 0 < t by A6,REAL_1:67;
    0 < i*r0 by E1,A6,SQUARE_1:21;
    then
EE: 0 < c by Bb,REAL_2:127;
    reconsider X2 as Subset of I[01] by XBOOLE_1:17;
    take W = [:B,X2:];
M2: x in B by B1,GOBOARD6:4;
    abs(i-i) < c by EE,ABSVALUE:def 1;
    then i in X1 by RCOMP_1:8;
    then i in X2 by XBOOLE_0:def 3;
    hence p in W by L0,M2,ZFMISC_1:106;
Bj: 0 <= i*(2*i*|.fx.|) by E1,Bz,SQUARE_1:19;
Cd: i-c = i*t/t - (i*r0 / t) by Bb,XCMPLX_1:90
       .= (i*t - i*r0) / t by XCMPLX_1:121
       .= i*(2*i*|.fx.|) / t;
    then
Y1: 0 <= i-c by Bb,Bj,REAL_2:125;
bh: i-c-1 = i*(2*i*|.fx.|) / t - t / t by Cd,Bb,XCMPLX_1:60
         .= (i*(2*i*|.fx.|) - t) / t by XCMPLX_1:121;
    i <= 1 by JORDAN5A:2;
    then 1-1 >= i-1 by REAL_1:49;
    then 2*i*|.fx.| * (i-1) <= 0 by Bz,SQUARE_1:23;
    then i*(2*i*|.fx.|) - 2*i*|.fx.| - r0 < r0 - r0 by A6,REAL_1:92;
    then i-c-1 < 0 by Bb,bh,REAL_2:128;
    then i-c-1+1 < 0+1 by REAL_1:67;
    then
Y3: i-c is Point of I[01] by Y1,JORDAN5A:2;
M1: now
      per cases;
      suppose
Y4:     i+c <= 1;
      X1 c= the carrier of I[01]
      proof
        let a be set;
        assume
Y8:       a in X1;
        then reconsider a as Real;
        i-c < a & a < i+c by Y8,JORDAN6:45;
        then 0 < a & a < 1 by Cd,Y4,AXIOMS:22,Bb,Bj,REAL_2:125;
        hence thesis by JORDAN5A:2;
      end;
      then reconsider X1 as Subset of I[01];
      0+0 <= i+c by E1,EE,REAL_1:55;
      then i+c is Point of I[01] by Y4,JORDAN5A:2;
      then X1 is open by Y3,BORSUK_4:70;
      hence X2 is open by AA,TOPS_1:38;
      end;
      suppose
H5:     i+c > 1;
      X2 = ]. i-c, 1 .]
      proof
        hereby
          let a be set;
          assume
H0:         a in X2;
          then
H2:       a in X1 by XBOOLE_0:def 3;
          then reconsider b = a as Real;
H1:       i-c < b by H2,JORDAN6:45;
          b <= 1 by H0,JORDAN5A:2;
          hence a in ]. i-c, 1 .] by H1,RCOMP_2:4;
        end;
        let a be set;
        assume
H4:       a in ]. i-c, 1 .];
        then reconsider b = a as Real;
H6:     i-c < b by H4,RCOMP_2:4;
L2:     b <= 1 by H4,RCOMP_2:4;
        then b < i+c by H5,AXIOMS:22;
        then
H3:     a in X1 by H6,JORDAN6:45;
        0 <= b by Y1,H6,AXIOMS:22;
        then a in I by L2,TOPREAL5:1,BORSUK_1:83;
        hence a in X2 by H3,XBOOLE_0:def 3;
      end;
      hence X2 is open by Y3,Top;
      end;
    end;
    B is open by hh,GOBOARD6:6;
    hence W is open by M1,BORSUK_1:46;
    F.:W c= Ball(ep,r0)
    proof
      let m be set;
      assume m in F.:W;
      then consider z being set such that
A15:    z in dom F and
A15a:    z in W and
A15b:    F.z = m by FUNCT_1:def 12;
      reconsider z as Point of [:TOP-REAL n,I[01]:] by A15;
      consider y being Point of TOP-REAL n, j being Point of I[01] such that
L1:     z = [y,j] by BORSUK_1:50;
      reconsider ez = F.z, ey = y as Point of Euclid n by TOPREAL3:13;
d2:   ez = F.(y,j) by L1,BINOP_1:def 1
        .= j*y by A3;
      reconsider fp = ep, fz = ez, fe = i*y, fy = y as Element of REAL n
        by EUCLID:25;
d4:   dist(ep,ez) = |.fp-fz.| by SPPOL_1:20;
d5:   |.fp-fz.| <= |.fp-fe.| + |.fe-fz.| by EUCLID:22;
      fy in B by A15a,L1,ZFMISC_1:106;
      then
e1:   dist(lx,ey) < r0/2/i by METRIC_1:12;
d6:   dist(lx,ey) = |.fx-fy.| by SPPOL_1:20;
      then
e3:   i * |.fx-fy.| < i * (r0/2/i) by e1,E1,REAL_1:70;
e4:   i * (r0/i/2) = r0/2 by E1,XCMPLX_1:98;
      fe = i*fy & fz = j*fy by d2,EUCLID:def 11;
      then
G5:   |.fe-fz.| = |.(i-j)*fy.| by c1,Tff1
        .= abs(i-j)*|.fy.| by EUCLID:14;
G8:   i is Real & j is Real by XREAL_0:def 1;        
GG:   0 <= |.fy.| by EUCLID:12;
      j in X2 by L1,A15a,ZFMISC_1:106;
      then j in X1 by XBOOLE_0:def 3;
      then abs(j-i) < c by RCOMP_1:8;
      then abs(i-j) < c by G8,UNIFORM1:13;
      then
WW:   abs(i-j)*|.fy.| <= c*|.fy.| by GG,AXIOMS:25;
W2:   |.fx-fy.| = |.fy-fx.| by EUCLID:21;
      |.fy.| - |.fx.| <= |.fy-fx.| by EUCLID:18;
      then |.fy.| - |.fx.| < r0/2/i by e1,d6,W2,AXIOMS:22;
      then |.fy.| < |.fx.| + r0/2/i by REAL_1:84;
      then
Gy:   c*|.fy.| < c * (|.fx.| + r0/2/i) by EE,REAL_1:70;
      c * (|.fx.| + r0/2/i) = c * (|.fx.| + r0/(2*i)) by XCMPLX_1:79
       .= c * ((|.fx.|*(2*i))/(2*i)+r0/(2*i)) by Bc,XCMPLX_1:90
       .= c * ((|.fx.|*(2*i)+r0)/(2*i)) by XCMPLX_1:63
       .= i*r0 / (2*i) by Bb,XCMPLX_1:99
       .= r0/2 by E1,XCMPLX_1:92;
      then
G6:   abs(i-j)*|.fy.| <= r0/2 by Gy,WW,AXIOMS:22;
      r0/2/i = r0/i/2 by XCMPLX_1:48;
      then abs(i) * |.fx-fy.| < r0/2 by e3,e4,E1,ABSVALUE:def 1;
      then |.i*(fx-fy).| < r0/2 by EUCLID:14;
      then |.i*fx-i*fy.| < r0/2 by c1,Tff;
      then |.i*fx-fe.| < r0/2 by EUCLID:def 11;
      then |.fp-fe.| < r0/2 by a2,EUCLID:def 11;
      then |.fp-fe.| + |.fe-fz.| < r0/2 + r0/2 by G5,G6,REAL_1:67;
      then dist(ep,ez) < r0 by d4,d5,AXIOMS:22;
      hence thesis by A15b,METRIC_1:12;
  end;
  hence F.:W c= V by A6a,XBOOLE_1:1;
  end;
  suppose i <= 0;
  then
E1: i = 0 by JORDAN5A:2;
hh: r0 is Real by XREAL_0:def 1;
  reconsider B = Ball(lx,r0) as Subset of TOP-REAL n by TOPREAL3:13;
  set t = |.fx.|+r0;
  set c = r0 / t;
  set X1 = [. 0, c .[;
Bb: 0 + 0 < t by A6,FF,REAL_1:67;
EE: 0 < c by A6,Bb,REAL_2:127;
    0+r0 <= t by FF,REAL_1:55;
    then
TT: c <= 1 by Bb,REAL_2:117;
    then
PP: c is Point of I[01] by EE,JORDAN5A:2;
    X1 c= I
    proof
      let s be set;
      assume
s1:     s in X1;
      then reconsider s as Real;
s2:   0 <= s  by s1,RCOMP_2:3;
      s < c by s1,RCOMP_2:3;
      then s <= 1 by TT,AXIOMS:22;
      hence thesis by s2,JORDAN5A:2;
    end;
    then reconsider X1 as Subset of I[01];
    take W = [:B,X1:];
M2: x in B by A6,GOBOARD6:4;
    i in X1 by E1,EE,RCOMP_2:3;
    hence p in W by L0,M2,ZFMISC_1:106;
M1: X1 is open by PP,Top2;
    B is open by hh,GOBOARD6:6;
    hence W is open by M1,BORSUK_1:46;
    F.:W c= Ball(ep,r0)
    proof
      let m be set;
      assume m in F.:W;
      then consider z being set such that
A15:    z in dom F and
A15a:    z in W and
A15b:    F.z = m by FUNCT_1:def 12;
      reconsider z as Point of [:TOP-REAL n,I[01]:] by A15;
      consider y being Point of TOP-REAL n, j being Point of I[01] such that
L1:     z = [y,j] by BORSUK_1:50;
      reconsider ez = F.z, ey = y as Point of Euclid n by TOPREAL3:13;
d2:   ez = F.(y,j) by L1,BINOP_1:def 1
        .= j*y by A3;
      reconsider fp = ep, fz = ez, fe = i*y, fy = y, f0 = 0.REAL n
        as Element of REAL n by EUCLID:25;
kk:   F.z - 0.REAL n = F.z by JORDAN2C:13;
h1:   fz = j*fy by d2,EUCLID:def 11;
h2:   0 <= j by JORDAN5A:2;
uy:   fz-fp = fz-f0 by a2,E1,EUCLID:33
        .= fz by kk,EUCLID:def 13;
d4:   dist(ep,ez) = |.fz-fp.| by SPPOL_1:20
        .= abs(j)*|.fy.| by h1,uy,EUCLID:14
        .= j*|.fy.| by h2,ABSVALUE:def 1;
      fy in B by A15a,L1,ZFMISC_1:106;
      then
e1:   dist(lx,ey) < r0 by METRIC_1:12;
d6:   dist(lx,ey) = |.fx-fy.| by SPPOL_1:20;
W2:   |.fx-fy.| = |.fy-fx.| by EUCLID:21;
      |.fy.| - |.fx.| <= |.fy-fx.| by EUCLID:18;
      then |.fy.| - |.fx.| < r0 by e1,d6,W2,AXIOMS:22;
      then
h3:   |.fy.| < t by REAL_1:84;
      now
      per cases;
      suppose  
ky:   0 < j;
      j in X1 by L1,A15a,ZFMISC_1:106;
      then j < c by RCOMP_2:3;
      then r0/j > r0/c by A6,ky,REAL_2:200;
      then t < r0/j by EE,XCMPLX_1:54;
      then |.fy.| < r0/j by h3,AXIOMS:22;
      then j*|.fy.| < j*(r0/j) by ky,REAL_1:70;
      hence j*|.fy.| < r0 by ky,XCMPLX_1:88;
      end;
      suppose j <= 0;
      then j = 0 by JORDAN5A:2;
      hence j*|.fy.| < r0 by A6;
      end;
      end;
      hence thesis by A15b,d4,METRIC_1:12;
  end;
  hence F.:W c= V by A6a,XBOOLE_1:1;
  end;
  end;
  hence F is continuous by JGRAPH_2:20;
end;

begin :: Paths

reserve
  X for non empty TopSpace,
  a, b, c, d, e, f for Point of X,
  T for non empty arcwise_connected TopSpace,
  a1, b1, c1, d1, e1, f1 for Point of T;

theorem Tk6:
  a,b are_connected & b,c are_connected implies
  for A being Path of a,b, B being Path of b,c holds
  A, A+B+-B are_homotopic 
proof
  assume that
a1: a,b are_connected and
f1: b,c are_connected;
  let A be Path of a,b,
      B be Path of b,c;
  consider X being constant Path of b,b;
a2: A, A are_homotopic by a1,BORSUK_6:49;
  B+-B, X are_homotopic by f1,BORSUK_6:92;
  then
a3: A+(B+-B), A+X are_homotopic by a1,a2,BORSUK_6:83;
  A, A+X are_homotopic by a1,BORSUK_6:88;
  then
a4: A+(B+-B), A are_homotopic by a3,BORSUK_6:87;
  A+(B+-B), A+B+-B are_homotopic by a1,f1,BORSUK_6:81;
  hence A, A+B+-B are_homotopic by a4,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of b1,c1 holds
  A, A+B+-B are_homotopic 
proof
  a1,b1 are_connected & b1,c1 are_connected by BORSUK_6:47;
  hence thesis by Tk6;
end;

theorem Tk6x:
  a,b are_connected & c,b are_connected implies
  for A being Path of a,b, B being Path of c,b holds
  A, A+-B+B are_homotopic 
proof
  assume that
a1: a,b are_connected and
f1: c,b are_connected;
  let A be Path of a,b,
      B be Path of c,b;
  consider X being constant Path of b,b;
a2: A, A are_homotopic by a1,BORSUK_6:49;
  -B+B, X are_homotopic by f1,BORSUK_6:94;
  then
a3: A+(-B+B), A+X are_homotopic by a1,a2,BORSUK_6:83;
  A, A+X are_homotopic by a1,BORSUK_6:88;
  then
a4: A+(-B+B), A are_homotopic by a3,BORSUK_6:87;
  A+(-B+B), A+-B+B are_homotopic by a1,f1,BORSUK_6:81;
  hence A, A+-B+B are_homotopic by a4,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of c1,b1 holds
  A, A+-B+B are_homotopic 
proof
  a1,b1 are_connected & c1,b1 are_connected by BORSUK_6:47;
  hence thesis by Tk6x;
end;

theorem Tk8:
  a,b are_connected & c,a are_connected implies
  for A being Path of a,b, B being Path of c,a holds
  A, -B+B+A are_homotopic 
proof
  assume that
a1: a,b are_connected and
f1: c,a are_connected;
  let A be Path of a,b,
      B be Path of c,a;
  consider X being constant Path of a,a;
a2: A, A are_homotopic by a1,BORSUK_6:49;
  -B+B, X are_homotopic by f1,BORSUK_6:94;
  then
a3: -B+B+A, X+A are_homotopic by a1,a2,BORSUK_6:83;
  A, X+A are_homotopic by a1,BORSUK_6:90;
  hence A, -B+B+A are_homotopic by a3,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of c1,a1 holds
  A, -B+B+A are_homotopic 
proof
  a1,b1 are_connected & c1,a1 are_connected by BORSUK_6:47;
  hence thesis by Tk8;
end;

theorem Tk8a:
  a,b are_connected & a,c are_connected implies
  for A being Path of a,b, B being Path of a,c holds
  A, B+-B+A are_homotopic 
proof
  assume that
a1: a,b are_connected and
f1: a,c are_connected;
  let A be Path of a,b,
      B be Path of a,c;
  consider X being constant Path of a,a;
a2: A, A are_homotopic by a1,BORSUK_6:49;
  B+-B, X are_homotopic by f1,BORSUK_6:92;
  then
a3: B+-B+A, X+A are_homotopic by a1,a2,BORSUK_6:83;
  A, X+A are_homotopic by a1,BORSUK_6:90;
  hence A, B+-B+A are_homotopic by a3,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of a1,c1 holds
  A, B+-B+A are_homotopic 
proof
  a1,b1 are_connected & a1,c1 are_connected by BORSUK_6:47;
  hence thesis by Tk8a;
end;

theorem Tk5:
  a,b are_connected & c,b are_connected implies
  for A, B being Path of a,b, C being Path of b,c st
  A+C, B+C are_homotopic holds A, B are_homotopic
proof
  assume that
a1: a,b are_connected and
a0: c,b are_connected;
  let A, B be Path of a,b,
      C be Path of b,c;
  assume
a2: A+C, B+C are_homotopic;
  consider X being constant Path of b,b;
f1: a,c are_connected by a1,a0,BORSUK_6:46;
  -C, -C are_homotopic by a0,BORSUK_6:49;
  then
b1: A+C+-C, B+C+-C are_homotopic by f1,a0,a2,BORSUK_6:83;
  A+C+-C, A are_homotopic by a0,a1,Tk6;
  then
b2: A, B+C+-C are_homotopic by b1,BORSUK_6:87;
  B+C+-C, B are_homotopic by a0,a1,Tk6;
  hence A, B are_homotopic by b2,BORSUK_6:87;
end;

theorem
  for A, B being Path of a1,b1, C being Path of b1,c1 st
  A+C, B+C are_homotopic holds A, B are_homotopic
proof
  a1,b1 are_connected & c1,b1 are_connected by BORSUK_6:47;
  hence thesis by Tk5;
end;

theorem Tk7:
  a,b are_connected & a,c are_connected implies
  for A, B being Path of a,b, C being Path of c,a st
  C+A, C+B are_homotopic holds A, B are_homotopic
proof
  assume that
a1: a,b are_connected and
a0: a,c are_connected;
  let A, B be Path of a,b,
      C be Path of c,a;
  assume
a2: C+A, C+B are_homotopic;
  consider X being constant Path of b,b;
f1: b,c are_connected by a1,a0,BORSUK_6:46;
  -C, -C are_homotopic by a0,BORSUK_6:49;
  then
b3: -C+(C+A), -C+(C+B) are_homotopic by f1,a0,a2,BORSUK_6:83;
b4: -C+C+A, -C+(C+A) are_homotopic by a1,a0,BORSUK_6:81;
b5: -C+C+B, -C+(C+B) are_homotopic by a1,a0,BORSUK_6:81;
  -C+C+A, -C+(C+B) are_homotopic by b3,b4,BORSUK_6:87;
  then
b1: -C+C+A, -C+C+B are_homotopic by b5,BORSUK_6:87;
  -C+C+A, A are_homotopic by a0,a1,Tk8;
  then
b2: A, -C+C+B are_homotopic by b1,BORSUK_6:87;
  -C+C+B, B are_homotopic by a0,a1,Tk8;
  hence A, B are_homotopic by b2,BORSUK_6:87;
end;

theorem
  for A, B being Path of a1,b1, C being Path of c1,a1 st
  C+A, C+B are_homotopic holds A, B are_homotopic
proof
  a1,b1 are_connected & a1,c1 are_connected by BORSUK_6:47;
  hence thesis by Tk7;
end;

theorem Tk1:
  a,b are_connected & b,c are_connected & c,d are_connected &
  d,e are_connected implies
  for A being Path of a,b, B being Path of b,c, C being Path of c,d,
      D being Path of d,e holds
  A+B+C+D, A+(B+C)+D are_homotopic
proof
  assume that
f1: a,b are_connected and
f2: b,c are_connected and
f3: c,d are_connected and
x1: d,e are_connected;
  let A be Path of a,b, B be Path of b,c, C be Path of c,d,
      D be Path of d,e;
a1: A+B+C, A+(B+C) are_homotopic by f1,f2,f3,BORSUK_6:81;
   a,c are_connected by f1,f2,BORSUK_6:46;
   then
g1: a,d are_connected by f3,BORSUK_6:46;
  D, D are_homotopic by x1,BORSUK_6:49;
  hence A+B+C+D, A+(B+C)+D are_homotopic by x1,a1,g1,BORSUK_6:83;
end;

theorem
  for A being Path of a1,b1, B being Path of b1,c1, C being Path of c1,d1,
      D being Path of d1,e1 holds
  A+B+C+D, A+(B+C)+D are_homotopic
proof
  a1,b1 are_connected & b1,c1 are_connected & c1,d1 are_connected &
  d1,e1 are_connected by BORSUK_6:47;
  hence thesis by Tk1;
end;

theorem Tk3:
  a,b are_connected & b,c are_connected & c,d are_connected &
  d,e are_connected implies
  for A being Path of a,b, B being Path of b,c, C being Path of c,d,
      D being Path of d,e holds
  A+B+C+D, A+(B+C+D) are_homotopic
proof
  assume that
f1: a,b are_connected and
f2: b,c are_connected and
f3: c,d are_connected and
x1: d,e are_connected;
  let A be Path of a,b, B be Path of b,c, C be Path of c,d,
      D be Path of d,e;
g2: b,d are_connected by f2,f3,BORSUK_6:46;
  then
a1: A+(B+C)+D, A+(B+C+D) are_homotopic by f1,x1,BORSUK_6:81;
a2: A+(B+C), A+B+C are_homotopic by f1,f2,f3,BORSUK_6:81;
g1: a,d are_connected by f1,g2,BORSUK_6:46;
  D, D are_homotopic by x1,BORSUK_6:49;
  then A+(B+C)+D, A+B+C+D are_homotopic by x1,a2,g1,BORSUK_6:83;
  hence A+B+C+D, A+(B+C+D) are_homotopic by a1,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of b1,c1, C being Path of c1,d1,
      D being Path of d1,e1 holds
  A+B+C+D, A+(B+C+D) are_homotopic
proof
  a1,b1 are_connected & b1,c1 are_connected & c1,d1 are_connected &
  d1,e1 are_connected by BORSUK_6:47;
  hence thesis by Tk3;
end;

theorem Tk4:
  a,b are_connected & b,c are_connected & c,d are_connected &
  d,e are_connected implies
  for A being Path of a,b, B being Path of b,c, C being Path of c,d,
      D being Path of d,e holds
  A+(B+C)+D, A+B+(C+D) are_homotopic
proof
  assume that
f1: a,b are_connected and
f2: b,c are_connected and
f3: c,d are_connected and
x1: d,e are_connected;
  let A be Path of a,b, B be Path of b,c, C be Path of c,d,
      D be Path of d,e;
a1: A+B+C+D, A+(B+C)+D are_homotopic by f1,f2,f3,x1,Tk1;
  a,c are_connected by f1,f2,BORSUK_6:46;
  then A+B+C+D, A+B+(C+D) are_homotopic by f3,x1,BORSUK_6:81;
  hence A+(B+C)+D, A+B+(C+D) are_homotopic by a1,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of b1,c1, C being Path of c1,d1,
      D being Path of d1,e1 holds
  A+(B+C)+D, A+B+(C+D) are_homotopic
proof
  a1,b1 are_connected & b1,c1 are_connected & c1,d1 are_connected &
  d1,e1 are_connected by BORSUK_6:47;
  hence thesis by Tk4;
end;

theorem Tk0:
  a,b are_connected & b,c are_connected & b,d are_connected implies
  for A being Path of a,b, B being Path of d,b, C being Path of b,c holds
  A+-B+B+C, A+C are_homotopic
proof
  assume that
x1: a,b are_connected and
x2: b,c are_connected and
f1: b,d are_connected;
  let A be Path of a,b, B be Path of d,b, C be Path of b,c;
  consider X being constant Path of b,b;
b0: A,A are_homotopic by x1,BORSUK_6:49;
b2: C,C are_homotopic by x2,BORSUK_6:49;
  -B+B, X are_homotopic by f1,BORSUK_6:94;
  then
a1: -B+B+C, X+C are_homotopic by x2,b2,BORSUK_6:83;
  X+C, C are_homotopic by x2,BORSUK_6:90;
  then -B+B+C, C are_homotopic by a1,BORSUK_6:87;
  then
a2: A+(-B+B+C), A+C are_homotopic by x1,x2,b0,BORSUK_6:83;
  A+-B+B+C, A+(-B+B+C) are_homotopic by x1,x2,f1,Tk3;
  hence A+-B+B+C, A+C are_homotopic by a2,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of d1,b1, C being Path of b1,c1 holds
  A+-B+B+C, A+C are_homotopic
proof
  a1,b1 are_connected & b1,c1 are_connected & b1,d1 are_connected
  by BORSUK_6:47;
  hence thesis by Tk0;
end;

theorem Tk10:
  a,b are_connected & a,c are_connected & c,d are_connected implies
  for A being Path of a,b, B being Path of c,d, C being Path of a,c holds
  A+-A+C+B+-B, C are_homotopic
proof
  assume that
g1: a,b are_connected and
b1: a,c are_connected and
f1: c,d are_connected;
  let A be Path of a,b, B be Path of c,d, C be Path of a,c;
a3: B+-B, B+-B are_homotopic by BORSUK_6:49;
  A+-A+C, C are_homotopic by g1,b1,Tk8a;
  then
a4: A+-A+C+(B+-B), C+(B+-B) are_homotopic by b1,a3,BORSUK_6:83;
  A+-A+C+B+-B, A+-A+C+(B+-B) are_homotopic by b1,f1,BORSUK_6:81;
  then
a5: A+-A+C+B+-B, C+(B+-B) are_homotopic by a4,BORSUK_6:87;
b2: C, C+B+-B are_homotopic by b1,f1,Tk6;
  C+B+-B, C+(B+-B) are_homotopic by b1,f1,BORSUK_6:81;
  then C, C+(B+-B) are_homotopic by b2,BORSUK_6:87;
  hence A+-A+C+B+-B, C are_homotopic by a5,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of c1,d1, C being Path of a1,c1 holds
  A+-A+C+B+-B, C are_homotopic
proof
  a1,b1 are_connected & a1,c1 are_connected & c1,d1 are_connected
  by BORSUK_6:47;
  hence thesis by Tk10;
end;

theorem Tk9:
  a,b are_connected & a,c are_connected & d,c are_connected implies
  for A being Path of a,b, B being Path of c,d, C being Path of a,c holds
  A+(-A+C+B)+-B, C are_homotopic
proof
  assume that
f1: a,b are_connected and
b1: a,c are_connected and
a0: d,c are_connected;
  let A be Path of a,b, B be Path of c,d, C be Path of a,c;
a1: A+(-A+C+B), A+-A+C+B are_homotopic by a0,b1,f1,Tk3;
g1: a,d are_connected by b1,a0,BORSUK_6:46;
  -B, -B are_homotopic by a0,BORSUK_6:49;
  then
a2: A+(-A+C+B)+-B, A+-A+C+B+-B are_homotopic by a0,a1,g1,BORSUK_6:83;
  A+-A+C+B+-B, C are_homotopic by f1,a0,b1,Tk10;
  hence A+(-A+C+B)+-B, C are_homotopic by a2,BORSUK_6:87;
end;

theorem
  for A being Path of a1,b1, B being Path of c1,d1, C being Path of a1,c1 holds
  A+(-A+C+B)+-B, C are_homotopic
proof
  a1,b1 are_connected & d1,c1 are_connected & a1,c1 are_connected
  by BORSUK_6:47;
  hence thesis by Tk9;
end;

theorem Tkolos:
  a,b are_connected & b,c are_connected & c,d are_connected &
  d,e are_connected & a,f are_connected implies
  for A being Path of a,b, B being Path of b,c, C being Path of c,d,
      D being Path of d,e, E being Path of f,c holds
  A+(B+C)+D, A+B+-E+(E+C+D) are_homotopic
proof
  assume that
g1: a,b are_connected and
g2: b,c are_connected and
g3: c,d are_connected and
x4: d,e are_connected and
x3: a,f are_connected;
x1: a,c are_connected by g1,g2,BORSUK_6:46;
x2: c,e are_connected by g3,x4,BORSUK_6:46;
  let A be Path of a,b, B be Path of b,c, C be Path of c,d,
      D be Path of d,e, E be Path of f,c;
  consider X being constant Path of c,c;
f1: f,c are_connected by x1,x3,BORSUK_6:46;
  then
m1: A+B+-E+E+(C+D), A+B+(C+D) are_homotopic by x1,x2,Tk0;
  A+B+(C+D), A+(B+C)+D are_homotopic by g1,g2,g3,x4,Tk4;
  then
m3: A+(B+C)+D, A+B+-E+E+(C+D) are_homotopic by m1,BORSUK_6:87;
m4: E+(C+D), E+C+D are_homotopic by f1,g3,x4,BORSUK_6:81;
y1: f,e are_connected by f1,x2,BORSUK_6:46;
  A+B+-E, A+B+-E are_homotopic by x3,BORSUK_6:49;
  then
m5: A+B+-E+(E+(C+D)), A+B+-E+(E+C+D) are_homotopic by x3,m4,y1,BORSUK_6:83;
  A+B+-E+(E+(C+D)), A+B+-E+E+(C+D) are_homotopic by f1,x2,x3,BORSUK_6:81;
  then A+B+-E+E+(C+D), A+B+-E+(E+C+D) are_homotopic by m5,BORSUK_6:87;
  hence A+(B+C)+D, A+B+-E+(E+C+D) are_homotopic by m3,BORSUK_6:87;
end;

theorem 
  for A being Path of a1,b1, B being Path of b1,c1, C being Path of c1,d1,
      D being Path of d1,e1, E being Path of f1,c1 holds
  A+(B+C)+D, A+B+-E+(E+C+D) are_homotopic
proof
  a1,b1 are_connected & b1,c1 are_connected & c1,d1 are_connected &
  d1,e1 are_connected & a1,f1 are_connected by BORSUK_6:47;
  hence thesis by Tkolos;
end;

begin :: Fundamental group

definition
  let T be TopStruct, t be Point of T;
  mode Loop of t is Path of t,t;
end;

definition
  let T be non empty TopStruct, t be Point of T;
  func Loops(t) means :dloopsa:
  for x being set holds x in it iff x is Loop of t;
existence
proof
  defpred P[set] means $1 is Loop of t;
  consider X being set such that
a1: for x being set holds x in X iff
     x in Funcs(the carrier of I[01],the carrier of T) & P[x]
       from XBOOLE_0:sch 1;
  take X;
  let x be set;
  thus x in X implies x is Loop of t by a1;
  assume
a2: x is Loop of t;
  then x in Funcs(the carrier of I[01],the carrier of T) by FUNCT_2:11;
  hence thesis by a1,a2;
end;
uniqueness
proof
  defpred P[set] means $1 is Loop of t;
  thus for X1,X2 being set st
   (for x being set holds x in X1 iff P[x]) &
   (for x being set holds x in X2 iff P[x]) holds X1 = X2 from XBOOLE_0:sch 3;
end;
end;

registration
  let T be non empty TopStruct, t be Point of T;
  cluster Loops(t) -> non empty;
coherence
proof
  consider x being Loop of t;
  x in Loops(t) by dloopsa;
  hence thesis;
end;
end;

Th1:
  for X being non empty TopSpace, a being Point of X
   ex E being Equivalence_Relation of Loops(a) st
   for x,y being set holds [x,y] in E iff x in Loops(a) & y in Loops(a) &
    ex P,Q being Loop of a st P = x & Q = y & P,Q are_homotopic
proof
  let X be non empty TopSpace,
      a be Point of X;
  defpred P[set,set] means
  ex P,Q being Loop of a st P = $1 & Q = $2 & P,Q are_homotopic;
a1: for x being set st x in Loops(a) holds P[x,x]
  proof
    let x be set;
    assume x in Loops(a);
    then reconsider x as Loop of a by dloopsa;
    x,x are_homotopic by BORSUK_6:48;
    hence thesis;
  end;
a2: for x,y being set st P[x,y] holds P[y,x];
a3: for x,y,z being set st P[x,y] & P[y,z] holds P[x,z]
  proof
    let x,y,z be set;
    assume P[x,y];
    then consider P,Q being Loop of a such that
a4:   P = x & Q = y & P,Q are_homotopic;
    assume P[y,z];
    then consider R,S being Loop of a such that
a5:   R = y & S = z & R,S are_homotopic;
    take P,S;
    thus thesis by a4,a5,BORSUK_6:87;
  end;
  thus ex EqR being Equivalence_Relation of Loops(a) st
    for x,y being set holds [x,y] in EqR iff x in Loops(a) & y in Loops(a) &
     P[x,y] from EQREL_1:sch 1(a1,a2,a3);
end;
   
definition
  let X be non empty TopSpace, a be Point of X;
  func EqRel(X,a) -> Relation of Loops(a) means :deq:
  for P, Q being Loop of a holds [P,Q] in it iff P,Q are_homotopic;
existence
proof
  consider E being Equivalence_Relation of Loops(a) such that
a1: for x,y being set holds [x,y] in E iff x in Loops(a) & y in Loops(a) &
     ex P,Q being Loop of a st P = x & Q = y & P,Q are_homotopic by Th1;
  take E;
  let P, Q be Loop of a;
  thus [P,Q] in E implies P,Q are_homotopic
  proof
    assume [P,Q] in E;
    then ex P1,Q1 being Loop of a st P1 = P & Q1 = Q & P1,Q1 are_homotopic
      by a1;
    hence thesis;
  end;
  P in Loops(a) & Q in Loops(a) by dloopsa;
  hence thesis by a1;
end;
uniqueness
proof
  let E,F be Relation of Loops(a) such that
a1: for P, Q being Loop of a holds [P,Q] in E iff P,Q are_homotopic and
a2: for P, Q being Loop of a holds [P,Q] in F iff P,Q are_homotopic;
  let x,y be set;
  thus [x,y] in E implies [x,y] in F
  proof
    assume
a3:   [x,y] in E;
    then x in Loops(a) & y in Loops(a) by ZFMISC_1:106;
    then reconsider x,y as Loop of a by dloopsa;
    x,y are_homotopic by a1,a3;
    hence thesis by a2;
  end;
  assume
a3: [x,y] in F;
  then x in Loops(a) & y in Loops(a) by ZFMISC_1:106;
  then reconsider x,y as Loop of a by dloopsa;
  x,y are_homotopic by a2,a3;
  hence thesis by a1;
end;
end;

registration
  let X be non empty TopSpace, a be Point of X;
  cluster EqRel(X,a) -> non empty total symmetric transitive;
coherence
proof
  set E = EqRel(X,a);
  consider EqR being Equivalence_Relation of Loops(a) such that
a1: for x,y being set holds [x,y] in EqR iff x in Loops(a) & y in Loops(a) &
    ex P,Q being Loop of a st P = x & Q = y & P,Q are_homotopic by Th1;
  E = EqR
  proof
    let x,y be set;
    thus [x,y] in E implies [x,y] in EqR
    proof
      assume
a3:     [x,y] in E;
      then
a4:   x in Loops(a) & y in Loops(a) by ZFMISC_1:106;
      then reconsider x,y as Loop of a by dloopsa;
      x,y are_homotopic by a3,deq;
      hence thesis by a1,a4;
    end;
    assume
a3:   [x,y] in EqR;
    then x in Loops(a) & y in Loops(a) by ZFMISC_1:106;
    then reconsider x,y as Loop of a by dloopsa;
    ex P,Q being Loop of a st P = x & Q = y & P,Q are_homotopic by a1,a3;
    hence thesis by deq;
  end;
  hence thesis by EQREL_1:16,TOLER_1:1;
end;
end;

theorem Th4:
  for P, Q being Loop of a holds
  Q in Class(EqRel(X,a),P) iff P,Q are_homotopic
proof
  let P, Q be Loop of a;
  set E = EqRel(X,a);
  hereby
    assume Q in Class(E,P);
    then [Q,P] in E by EQREL_1:27;
    hence P,Q are_homotopic by deq;
  end;
  assume P,Q are_homotopic;
  then [Q,P] in E by deq;
  hence thesis by EQREL_1:27;
end;
  
theorem Thiff:
  for P, Q being Loop of a holds
  Class(EqRel(X,a),P) = Class(EqRel(X,a),Q) iff P,Q are_homotopic
proof
  let P, Q be Loop of a;
  set E = EqRel(X,a);
a2: Q in Loops(a) by dloopsa;
  hereby
    assume Class(E,P) = Class(E,Q);
    then P in Class(E,Q) by a2,EQREL_1:31;
    hence P,Q are_homotopic by Th4;
  end;
  assume P,Q are_homotopic;
  then P in Class(E,Q) by Th4;
  hence thesis by a2,EQREL_1:31;
end;

definition
  let X be non empty TopSpace, a be Point of X;
  func FundamentalGroup(X,a) -> strict HGrStr means :dgr:
  the carrier of it = Class EqRel (X,a) &
  for x,y being Element of it
   ex P,Q being Loop of a st x = Class(EqRel(X,a),P) & y = Class(EqRel(X,a),Q)
    & (the mult of it).(x,y) = Class(EqRel(X,a),P+Q);
existence
proof
  set A = Class EqRel(X,a);
  defpred P[set,set,set] means
   ex P,Q being Loop of a st $1 = Class(EqRel(X,a),P) &
    $2 = Class(EqRel(X,a),Q) & $3 = Class(EqRel(X,a),P+Q);
a1: for x,y being Element of A ex z being Element of A st P[x,y,z]
  proof
    let x, y be Element of A;
    x in A;
    then consider P being set such that
a2:   P in Loops(a) and
a3:   x = Class(EqRel(X,a),P) by EQREL_1:def 5;
    y in A;
    then consider Q being set such that
a4:   Q in Loops(a) and
a5:   y = Class(EqRel(X,a),Q) by EQREL_1:def 5;
    reconsider P,Q as Loop of a by a2,a4,dloopsa;
    P+Q in Loops(a) by dloopsa;
    then reconsider z = Class(EqRel(X,a),P+Q) as Element of A by EQREL_1:def 5;
    take z;
    thus thesis by a3,a5;
  end;
  consider g being BinOp of A such that
a2: for a,b being Element of A holds P[a,b,g.(a,b)] from BINOP_1:sch 1(a1);
  take G = HGrStr(# A,g #);
  thus thesis by a2;
end;
uniqueness
proof
  let F,G be strict HGrStr such that
a1:  the carrier of F = Class EqRel (X,a) and
a2:  for x,y being Element of F
   ex P,Q being Loop of a st x = Class(EqRel(X,a),P) & y = Class(EqRel(X,a),Q)
    & (the mult of F).(x,y) = Class(EqRel(X,a),P+Q) and
a3:  the carrier of G = Class EqRel (X,a) and
a4:  for x,y being Element of G
   ex P,Q being Loop of a st x = Class(EqRel(X,a),P) & y = Class(EqRel(X,a),Q)
    & (the mult of G).(x,y) = Class(EqRel(X,a),P+Q);
  set f = the mult of F;
  set g = the mult of G;
  for c,d being Element of the carrier of F holds f.(c,d) = g.(c,d)
  proof
    let c,d be Element of the carrier of F;
    consider P1,Q1 being Loop of a such that
a5:   c = Class(EqRel(X,a),P1) and
a9:   d = Class(EqRel(X,a),Q1) and
a6:   f.(c,d) = Class(EqRel(X,a),P1+Q1) by a2;
    consider P2,Q2 being Loop of a such that
a7:   c = Class(EqRel(X,a),P2) and
b1:   d = Class(EqRel(X,a),Q2) and
a8:   g.(c,d) = Class(EqRel(X,a),P2+Q2) by a1,a3,a4;
    P1,P2 are_homotopic & Q1,Q2 are_homotopic by a5,a7,a9,b1,Thiff;
    then P1+Q1,P2+Q2 are_homotopic by BORSUK_6:83;
    hence f.(c,d) = g.(c,d) by a6,a8,Thiff;
  end;
  hence thesis by a1,a3,BINOP_1:2;
end;
end;

notation
  let X be non empty TopSpace, a be Point of X;
  synonym pi_1(X,a) for FundamentalGroup(X,a);
end;

registration
  let X be non empty TopSpace;
  let a be Point of X;
  cluster pi_1(X,a) -> non empty;
coherence
proof
  the carrier of pi_1(X,a) = Class EqRel (X,a) by dgr;
  hence the carrier of pi_1(X,a) is non empty;
end;
end;

theorem Tciff:
  for x being set holds x in the carrier of pi_1(X,a) iff
  ex P being Loop of a st x = Class(EqRel(X,a),P)
proof
  let x be set;
a1: the carrier of pi_1(X,a) = Class EqRel (X,a) by dgr;
  hereby
    assume x in the carrier of pi_1(X,a);
    then consider P being Element of Loops(a) such that
a2:   x = Class(EqRel(X,a),P) by a1,EQREL_1:45;
    reconsider P as Loop of a by dloopsa;
    take P;
    thus x = Class(EqRel(X,a),P) by a2;
  end;
  given P being Loop of a such that
a3: x = Class(EqRel(X,a),P);
  P in Loops(a) by dloopsa;
  hence thesis by a1,a3,EQREL_1:def 5;
end;

registration
  let X be non empty TopSpace;
  let a be Point of X;
  cluster pi_1(X,a) -> associative Group-like;
coherence
proof
  set G = pi_1(X,a);
  set g = the mult of G;
  set E = EqRel(X,a);
  thus G is associative
  proof
    let x,y,z be Element of G;
    consider P1,Q1 being Loop of a such that
a1:   x = Class(E,P1) and
a6:   y = Class(E,Q1) and
a2:   g.(x,y) = Class(E,P1+Q1) by dgr;
    consider P2,Q2 being Loop of a such that
a3:   y = Class(E,P2) and
a7:   z = Class(E,Q2) and
a4:   g.(y,z) = Class(E,P2+Q2) by dgr;
    consider P3,Q3 being Loop of a such that
b1:   Class(E,P1+Q1) = Class(E,P3) and
b2:   Class(E,Q2) = Class(E,Q3) and
b3:   g.(Class(E,P1+Q1),Class(E,Q2)) = Class(E,P3+Q3) by a2,a7,dgr;
    consider P4,Q4 being Loop of a such that
b4:   Class(E,P1) = Class(E,P4) and
b5:   Class(E,P2+Q2) = Class(E,Q4) and
b6:   g.(Class(E,P1),Class(E,P2+Q2)) = Class(E,P4+Q4) by a1,a4,dgr;
a5: Q1,P2 are_homotopic by a6,a3,Thiff;
c3: P1+Q1,P3 are_homotopic by b1,Thiff;
c4: Q2,Q3 are_homotopic by b2,Thiff;
c2: P1,P4 are_homotopic by b4,Thiff;
c5: P2+Q2,Q4 are_homotopic by b5,Thiff;
    Q3,Q3 are_homotopic by BORSUK_6:48;
    then
d1: P1+Q1+Q3,P3+Q3 are_homotopic by c3,BORSUK_6:83;
    P4,P4 are_homotopic by BORSUK_6:48;
    then
d2: P4+(P2+Q2),P4+Q4 are_homotopic by c5,BORSUK_6:83;
    P1+Q1,P4+P2 are_homotopic by c2,a5,BORSUK_6:83;
    then
d5: P1+Q1+Q3,P4+P2+Q2 are_homotopic by c4,BORSUK_6:83;
    P4+P2+Q2,P4+(P2+Q2) are_homotopic by BORSUK_6:81;
    then P1+Q1+Q3,P4+(P2+Q2) are_homotopic by d5,BORSUK_6:87;
    then P3+Q3,P4+(P2+Q2) are_homotopic by d1,BORSUK_6:87;
    then
c1: P3+Q3,P4+Q4 are_homotopic by d2,BORSUK_6:87;
    thus (x*y)*z = g.(x*y,z) by VECTSP_1:def 10
      .= g.(g.(x,y),z) by VECTSP_1:def 10
      .= g.(x,g.(y,z)) by a1,a4,b6,c1,a2,a7,b3,Thiff
      .= g.(x,y*z) by VECTSP_1:def 10
      .= x*(y*z) by VECTSP_1:def 10;
  end;
  consider C being constant Loop of a;
  set e = Class(E,C);
  C in Loops a by dloopsa;
  then e in Class EqRel (X,a) by EQREL_1:def 5;
  then reconsider e as Element of G by dgr;
  take e;
  let h be Element of G;
  consider P1,Q1 being Loop of a such that
a1: h = Class(E,P1) and
a2: e = Class(E,Q1) and
a3: g.(h,e) = Class(E,P1+Q1) by dgr;
ff:P1,P1 are_homotopic by BORSUK_6:48;
  C,Q1 are_homotopic by a2,Thiff; 
  then
a6: P1+C,P1+Q1 are_homotopic by ff,BORSUK_6:83;
  P1+C,P1 are_homotopic by BORSUK_6:88;
  then
a4: P1,P1+Q1 are_homotopic by a6,BORSUK_6:87;
  thus h * e = g.(h,e) by VECTSP_1:def 10
    .= h by a1,a3,a4,Thiff;
  consider P1,Q1 being Loop of a such that
a1: e = Class(E,P1) and
a2: h = Class(E,Q1) and
a3: g.(e,h) = Class(E,P1+Q1) by dgr;
ff:Q1,Q1 are_homotopic by BORSUK_6:48;
  C,P1 are_homotopic by a1,Thiff; 
  then
a6: C+Q1,P1+Q1 are_homotopic by ff,BORSUK_6:83;
  C+Q1,Q1 are_homotopic by BORSUK_6:90;
  then
a4: Q1,P1+Q1 are_homotopic by a6,BORSUK_6:87;
  thus e * h = g.(e,h) by VECTSP_1:def 10
    .= h by a2,a3,a4,Thiff;
  h in the carrier of G;
  then h in Class EqRel (X,a) by dgr;
  then consider H being Element of Loops a such that
f5:  h = Class(E,H) by EQREL_1:45;
  reconsider H as Loop of a by dloopsa;
  set x = Class(E,-H);
  -H in Loops a by dloopsa;
  then x in Class EqRel (X,a) by EQREL_1:def 5;
  then reconsider x as Element of G by dgr;
  take x;
  consider P,Q being Loop of a such that
f1: h = Class(E,P) and
f2: x = Class(E,Q) and
f3: g.(h,x) = Class(E,P+Q) by dgr;
f6:P,H are_homotopic by f5,f1,Thiff;
   Q,-H are_homotopic by f2,Thiff;
   then
f7:P+Q,H+-H are_homotopic by f6,BORSUK_6:83;
   C,H+-H are_homotopic by BORSUK_6:92;
   then
f4: C,P+Q are_homotopic by f7,BORSUK_6:87;
  thus h * x = g.(h,x) by VECTSP_1:def 10
    .= e by f3,f4,Thiff;    
  consider P,Q being Loop of a such that
f1: x = Class(E,P) and
f2: h = Class(E,Q) and
f3: g.(x,h) = Class(E,P+Q) by dgr;
f6:Q,H are_homotopic by f5,f2,Thiff;
   P,-H are_homotopic by f1,Thiff;
   then
f7:P+Q,-H+H are_homotopic by f6,BORSUK_6:83;
   C,-H+H are_homotopic by BORSUK_6:94;
   then
f4: C,P+Q are_homotopic by f7,BORSUK_6:87;
  thus x * h = g.(x,h) by VECTSP_1:def 10
    .= e by f3,f4,Thiff;
end;
end;

definition
  let T be non empty TopSpace,
      x0, x1 be Point of T,
      P being Path of x0,x1;
  assume
b0:    x0,x1 are_connected;
  func pi_1-iso(P) -> map of pi_1(T,x1), pi_1(T,x0) means :diso:
  for Q being Loop of x1 holds
   it.Class(EqRel(T,x1),Q) = Class(EqRel(T,x0),P+Q+-P);
existence
proof
  defpred P[set,set] means
   ex L being Loop of x1 st $1 = Class(EqRel(T,x1),L) &
     $2 = Class(EqRel(T,x0),P+L+-P);
a3: the carrier of pi_1(T,x1) = Class EqRel (T,x1) by dgr;
a0: for x being Element of the carrier of pi_1(T,x1)
     ex y being Element of the carrier of pi_1(T,x0) st P[x,y]
  proof
    let x be Element of the carrier of pi_1(T,x1);
    consider Q being Loop of x1 such that
a4:   x = Class(EqRel(T,x1),Q) by Tciff;
    reconsider y = Class(EqRel(T,x0),P+Q+-P) as
     Element of the carrier of pi_1(T,x0) by Tciff;
    take y;
    thus thesis by a4;
  end;
  consider f being Function of the carrier of pi_1(T,x1),
     the carrier of pi_1(T,x0) such that
a1: for x being Element of the carrier of pi_1(T,x1) holds P[x,f.x]
     from FUNCT_2:sch 3(a0);
  reconsider f as map of pi_1(T,x1), pi_1(T,x0);
  take f;
  let Q be Loop of x1;
  Q in Loops(x1) by dloopsa;
  then Class(EqRel(T,x1),Q) is Element of the carrier of pi_1(T,x1)
    by a3,EQREL_1:def 5;
  then consider L being Loop of x1 such that
b1:  Class(EqRel(T,x1),Q) = Class(EqRel(T,x1),L) and
b2:  f.Class(EqRel(T,x1),Q) = Class(EqRel(T,x0),P+L+-P) by a1;
b4: P,P are_homotopic by b0,BORSUK_6:49;
b3: -P,-P are_homotopic by b0,BORSUK_6:49;
  L,Q are_homotopic by b1,Thiff;
  then P+L,P+Q are_homotopic by b0,b4,BORSUK_6:83;
  then P+L+-P,P+Q+-P are_homotopic by b0,b3,BORSUK_6:83;
  hence f.Class(EqRel(T,x1),Q) = Class(EqRel(T,x0),P+Q+-P) by b2,Thiff;
end;
uniqueness
proof
  let f1, f2 be map of pi_1(T,x1), pi_1(T,x0) such that
a1: for Q being Loop of x1 holds
     f1.Class(EqRel(T,x1),Q) = Class(EqRel(T,x0),P+Q+-P) and
a2: for Q being Loop of x1 holds
     f2.Class(EqRel(T,x1),Q) = Class(EqRel(T,x0),P+Q+-P);
  for x being Element of the carrier of pi_1(T,x1) holds f1.x = f2.x
  proof
    let x be Element of the carrier of pi_1(T,x1);
    consider L being Loop of x1 such that
a3:   x = Class(EqRel(T,x1),L) by Tciff;
    thus f1.x = Class(EqRel(T,x0),P+L+-P) by a1,a3
      .= f2.x by a2,a3;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

reserve
  x0, x1 for Point of X,
  P, Q for Path of x0,x1,
  y0, y1 for Point of T,
  R, V for Path of y0,y1;

theorem Ty:
  x0,x1 are_connected & P,Q are_homotopic implies pi_1-iso(P) = pi_1-iso(Q)
proof
  assume that
a1: x0,x1 are_connected and
a2: P,Q are_homotopic;
  for x being Element of the carrier of pi_1(X,x1) holds
    (pi_1-iso(P)).x = (pi_1-iso(Q)).x
  proof
    let x be Element of the carrier of pi_1(X,x1);
    consider L being Loop of x1 such that
a3:   x = Class(EqRel(X,x1),L) by Tciff;
    L, L are_homotopic by BORSUK_6:48;
    then
a5: P+L, Q+L are_homotopic by a1,a2,BORSUK_6:83;
    -P, -Q are_homotopic by a1,a2,BORSUK_6:85;
    then
a4: P+L+-P, Q+L+-Q are_homotopic by a1,a5,BORSUK_6:83;
    thus (pi_1-iso(P)).x = Class(EqRel(X,x0),P+L+-P) by a1,a3,diso
      .= Class(EqRel(X,x0),Q+L+-Q) by a4,Thiff
      .= (pi_1-iso(Q)).x by a1,a3,diso;
  end;
  hence thesis by FUNCT_2:113;
end;
  
theorem
  R,V are_homotopic implies pi_1-iso(R) = pi_1-iso(V)
proof
  y0,y1 are_connected by BORSUK_6:47;
  hence thesis by Ty;
end;

theorem Tho:
  x0,x1 are_connected implies
  pi_1-iso(P) is Homomorphism of pi_1(X,x1), pi_1(X,x0)
proof  
  assume
a5: x0,x1 are_connected;
  set f = pi_1-iso(P);
  set m0 = the mult of pi_1(X,x0);
  set m1 = the mult of pi_1(X,x1);
  let x, y be Element of pi_1(X,x1);
  consider Z, Y being Loop of x1 such that
a1:  x = Class(EqRel(X,x1),Z) and
a2:  y = Class(EqRel(X,x1),Y) and
a3:  m1.(x,y) = Class(EqRel(X,x1),Z+Y) by dgr;
  consider fX, fY being Loop of x0 such that
b1:  f.x = Class(EqRel(X,x0),fX) and
b2:  f.y = Class(EqRel(X,x0),fY) and
b3:  m0.(f.x,f.y) = Class(EqRel(X,x0),fX+fY) by dgr;
   f.x = Class(EqRel(X,x0),P+Z+-P) by a1,a5,diso;
   then
a7:fX,P+Z+-P are_homotopic by b1,Thiff;
   f.y = Class(EqRel(X,x0),P+Y+-P) by a2,a5,diso;
   then fY,P+Y+-P are_homotopic by b2,Thiff;
   then
a8: fX+fY,P+Z+-P+(P+Y+-P) are_homotopic by a7,BORSUK_6:83;
   P+(Z+Y)+-P,P+Z+-P+(P+Y+-P) are_homotopic by a5,Tkolos;
   then
a6:P+(Z+Y)+-P,fX+fY are_homotopic by a8,BORSUK_6:87;
  thus f.(x*y) = f.(m1.(x,y)) by VECTSP_1:def 10
    .= Class(EqRel(X,x0),P+(Z+Y)+-P) by a3,a5,diso
    .= m0.(f.x,f.y) by b3,a6,Thiff
    .= f.x * f.y by VECTSP_1:def 10;
end;

definition
  let T be non empty arcwise_connected TopSpace,
      x0, x1 be Point of T,
      P be Path of x0,x1;
  redefine func pi_1-iso(P) -> Homomorphism of pi_1(T,x1), pi_1(T,x0);
coherence
proof
  x0,x1 are_connected by BORSUK_6:47;
  hence thesis by Tho;
end;
end;

theorem Thoone:
  x0,x1 are_connected implies pi_1-iso(P) is one-to-one
proof
  assume
a5: x0,x1 are_connected;
  set f = pi_1-iso(P);
  let a, b be set such that
a1: a in dom f and
a2: b in dom f and
a3: f.a = f.b;
  consider A being Loop of x1 such that
a4: a = Class(EqRel(X,x1),A) by a1,Tciff;  
  consider B being Loop of x1 such that
a6: b = Class(EqRel(X,x1),B) by a2,Tciff;
b1: f.a = Class(EqRel(X,x0),P+A+-P) by a4,a5,diso; 
  f.b = Class(EqRel(X,x0),P+B+-P) by a6,a5,diso;
  then P+A+-P, P+B+-P are_homotopic by a3,b1,Thiff;
  then P+A, P+B are_homotopic by a5,Tk5;
  then A,B are_homotopic by a5,Tk7;
  hence a = b by a4,a6,Thiff;
end;

theorem Thorng:
  x0,x1 are_connected implies pi_1-iso(P) is onto
proof  
  assume
a5: x0,x1 are_connected;
  set f = pi_1-iso(P);
  thus rng f c= the carrier of pi_1(X,x0);
  let y be set;
  assume y in the carrier of pi_1(X,x0);
  then consider Y being Loop of x0 such that
a1:  y = Class(EqRel(X,x0),Y) by Tciff;
  set Z = Class(EqRel(X,x1),-P+Y+P);
  dom f = the carrier of pi_1(X,x1) by FUNCT_2:def 1;
  then
a2: Z in dom f by Tciff;
a3: P+(-P+Y+P)+-P, Y are_homotopic by a5,Tk9;
  f.Z = Class(EqRel(X,x0),P+(-P+Y+P)+-P) by a5,diso
     .= y by a1,a3,Thiff;
  hence y in rng f by a2,FUNCT_1:def 5;
end;

registration
  let T be non empty arcwise_connected TopSpace,
      x0, x1 be Point of T,
      P be Path of x0,x1;
  cluster pi_1-iso(P) -> one-to-one onto;
coherence
proof
  x0,x1 are_connected by BORSUK_6:47;
  hence thesis by Thoone,Thorng;
end;
end;

theorem Todw:
  x0,x1 are_connected implies (pi_1-iso(P))" = pi_1-iso(-P)
proof
  assume
d1: x0,x1 are_connected;
  set f = pi_1-iso(P);
  set g = pi_1-iso(-P);
aa: f is one-to-one by d1,Thoone;
  f is onto by d1,Thorng;
  then
  rng f = the carrier of pi_1(X,x0) by FUNCT_2:def 3
     .= [#]pi_1(X,x0) by PRE_TOPC:12;
   then
c1: f" = (f qua Function)" by aa,TOPS_2:def 4;
  for x being Element of the carrier of pi_1(X,x0) holds f".x = g.x
  proof
    let x be Element of the carrier of pi_1(X,x0);
    consider Q being Loop of x0 such that
a1:   x = Class(EqRel(X,x0),Q) by Tciff;
    dom f = the carrier of pi_1(X,x1) by FUNCT_2:def 1;
    then
a3: Class(EqRel(X,x1),-P+Q+--P) in dom f by Tciff;
    --P = P by d1,BORSUK_6:50;
    then
a4: P+(-P+Q+--P)+-P, Q are_homotopic by d1,Tk9;
    f.Class(EqRel(X,x1),-P+Q+--P) = Class(EqRel(X,x0),P+(-P+Q+--P)+-P)
       by d1,diso
      .= x by a1,a4,Thiff;
    hence f".x = Class(EqRel(X,x1),-P+Q+--P) by aa,c1,a3,FUNCT_1:54
       .= g.x by a1,d1,diso;
  end;
  hence thesis by FUNCT_2:113;
end;

theorem
  (pi_1-iso(R))" = pi_1-iso(-R)
proof
  y0,y1 are_connected by BORSUK_6:47;
  hence thesis by Todw;
end;

theorem Tiso:
  x0,x1 are_connected implies
  for h being Homomorphism of pi_1(X,x1), pi_1(X,x0) st h = pi_1-iso(P) holds
  h is_isomorphism
proof
  assume
b1: x0,x1 are_connected;
  let h be Homomorphism of pi_1(X,x1), pi_1(X,x0) such that
a1: h = pi_1-iso(P);
a3: h is one-to-one by a1,b1,Thoone;
  h is onto by a1,b1,Thorng;
  then rng h = the carrier of pi_1(X,x0) by FUNCT_2:def 3;
  hence thesis by a3,GROUP_6:70;
end;

theorem Tiso2:
  pi_1-iso(R) is_isomorphism
proof
  y0,y1 are_connected by BORSUK_6:47;
  hence thesis by Tiso;
end;

theorem
  x0,x1 are_connected implies
  pi_1(X,x0), pi_1(X,x1) are_isomorphic
proof
  assume
a1: x0,x1 are_connected;
  consider P being Path of x1,x0;
  reconsider h = pi_1-iso(P) as Homomorphism of pi_1(X,x0), pi_1(X,x1)
    by a1,Tho;
  take h;
  thus h is_isomorphism by a1,Tiso;
end;
   
theorem
  pi_1(T,y0), pi_1(T,y1) are_isomorphic
proof
  consider R being Path of y1,y0;
  take pi_1-iso(R);
  thus pi_1-iso(R) is_isomorphism by Tiso2;
end;
   
begin :: TOP-REAL n

definition
  let n be Nat,
      a, b be Point of TOP-REAL n,
      P, Q be Path of a,b;
  func RealHomotopy(P,Q) -> map of [:I[01],I[01]:], TOP-REAL n means :def12:
  for s, t being Element of I[01] holds it.(s,t) = (1-t) * P.s + t * Q.s;
existence
proof
  set I = the carrier of I[01];
  deffunc F(Element of I, Element of I) = (1-$2) * P.$1 + $2 * Q.$1;
  consider F being Function of [:I,I:],the carrier of TOP-REAL n such that
A1: for x, y being Element of I holds F. [x,y] = F(x,y) from FUNCT_2:sch 8;
  the carrier of [:I[01],I[01]:] = [:I,I:] by BORSUK_1:def 5;
  then reconsider F as map of [:I[01],I[01]:], TOP-REAL n;
  take F;
  let x, y be Element of I;
  thus F.(x,y) = F. [x,y] by BINOP_1:def 1
    .= F(x,y) by A1;
end;
uniqueness
proof
  let f, g be map of [:I[01],I[01]:], TOP-REAL n such that
a1: for s, t being Element of I[01] holds f.(s,t) = (1-t) * P.s + t * Q.s and
a2: for s, t being Element of I[01] holds g.(s,t) = (1-t) * P.s + t * Q.s;
  set I = the carrier of I[01];
a3:the carrier of [:I[01],I[01]:] = [:I,I:] by BORSUK_1:def 5;
  for s, t being Element of I holds f.(s,t) = g.(s,t)
  proof
    let s, t be Element of I;
    thus f.(s,t) = (1-t) * P.s + t * Q.s by a1
      .= g.(s,t) by a2;
  end;
  hence f = g by a3,BINOP_1:2;
end;
end;

T90c:
  for a, b being Point of TOP-REAL n,
      P, Q being Path of a,b holds RealHomotopy(P,Q) is continuous
proof
  let a, b be Point of TOP-REAL n,
      P, Q being Path of a,b;
  set F = RealHomotopy(P,Q);
  set I = the carrier of I[01];
  set T = the carrier of TOP-REAL n;
  set PI = [:P,id I[01]:];
  set QI = [:Q,id I[01]:];
xA5: PI is continuous & QI is continuous by BORSUK_2:12;
  deffunc Fa(Element of the carrier of TOP-REAL n,Element of I) = (1-$2)*$1;
  deffunc Fb(Element of the carrier of TOP-REAL n,Element of I) = $2*$1;
  consider Pa being Function of [:T,I:],T such that
xA1: for x being Element of T, i being Element of I holds
    Pa. [x,i] = Fa(x,i) from FUNCT_2:sch 8;
  consider Pb being Function of [:T,I:],T such that
xA2: for x being Element of T, i being Element of I holds
    Pb. [x,i] = Fb(x,i) from FUNCT_2:sch 8;
  the carrier of [:TOP-REAL n,I[01]:] = [:T,I:] by BORSUK_1:def 5;
  then reconsider Pa, Pb as map of [:TOP-REAL n,I[01]:],TOP-REAL n;
yA1: for x being Element of T, i being Element of I holds
    Pa.(x,i) = Fa(x,i)
  proof
    let x be Element of T, i be Element of I;
    thus Pa.(x,i) = Pa. [x,i] by BINOP_1:def 1
      .= Fa(x,i) by xA1;
  end;
  for x being Element of T, i being Element of I holds
    Pb.(x,i) = Fb(x,i)
  proof
    let x be Element of T, i be Element of I;
    thus Pb.(x,i) = Pb. [x,i] by BINOP_1:def 1
      .= Fb(x,i) by xA2;
  end;
  then Pa is continuous & Pb is continuous by yA1,TT100,TT101;
  then
xA3: Pa*PI is continuous & Pb*QI is continuous by xA5,TOPS_2:58;
  for p being Point of [:I[01],I[01]:] holds F.p = (Pa*PI).p + (Pb*QI).p
  proof
    let p be Point of [:I[01],I[01]:];
    consider s, t being Point of I[01] such that
a1:   p = [s,t] by BORSUK_1:50;
a4: dom P = I by FUNCT_2:def 1;
a7: dom Q = I by FUNCT_2:def 1;
a5: dom id I[01] = I by FUNCT_2:def 1;
a6: (id I[01]).t = t by GRCAT_1:11;
a2: (Pa*PI).p = Pa.(PI.p) by FUNCT_2:21
      .= Pa. [P.s,t] by a1,a4,a5,a6,FUNCT_3:def 9
      .= (1-t) * P.s by xA1;
k1: (Pb*QI).p = Pb.(QI.p) by FUNCT_2:21
      .= Pb. [Q.s,t] by a1,a7,a5,a6,FUNCT_3:def 9
      .= t * Q.s by xA2;
    thus F.p = F.(s,t) by a1,BINOP_1:def 1
      .= (Pa*PI).p + (Pb*QI).p by a2,k1,def12;
  end;
  hence F is continuous by xA3,TT102;
end;
  
T90k:
  for a, b being Point of TOP-REAL n,
      P, Q being Path of a,b holds
  for s being Point of I[01] holds RealHomotopy(P,Q).(s,0) = P.s &
                                   RealHomotopy(P,Q).(s,1) = Q.s &
  for t being Point of I[01] holds RealHomotopy(P,Q).(0,t) = a &
                                   RealHomotopy(P,Q).(1,t) = b
proof
  let a, b be Point of TOP-REAL n,
      P, Q be Path of a,b;
  set F = RealHomotopy(P,Q);
  let s be Point of I[01];
  thus F.(s,0) = (1-0) * P.s + 0 * Q.s by def12,BORSUK_1:def 17
    .= P.s + 0 * Q.s by EUCLID:33
    .= P.s + 0.REAL n by EUCLID:33
    .= P.s by EUCLID:31;
  thus F.(s,1) = (1-1) * P.s + 1 * Q.s by def12,BORSUK_1:def 18
    .= 0.REAL n + 1 * Q.s by EUCLID:33
    .= 0.REAL n + Q.s by EUCLID:33
    .= Q.s by EUCLID:31;
  let t be Point of I[01];
A2: P.0[01] = a by BORSUK_1:def 17,BORSUK_2:def 3;
A3: Q.0[01] = a by BORSUK_1:def 17,BORSUK_2:def 3;
A4: P.1[01] = b by BORSUK_1:def 18,BORSUK_2:def 3;
A5: Q.1[01] = b by BORSUK_1:def 18,BORSUK_2:def 3;
  thus F.(0,t) = (1-t) * P.0[01] + t * Q.0[01] by def12,BORSUK_1:def 17
    .= 1*a - t*a + t*a by A2,A3,EUCLID:54
    .= 1*a by EUCLID:52
    .= a by EUCLID:33;
  thus F.(1,t) = (1-t) * P.1[01] + t * Q.1[01] by def12,BORSUK_1:def 18
    .= 1*b - t*b + t*b by A4,A5,EUCLID:54
    .= 1*b by EUCLID:52
    .= b by EUCLID:33;
end;

theorem T90:
  for a, b being Point of TOP-REAL n,
      P, Q being Path of a,b holds
  P, Q are_homotopic
proof
  let a, b be Point of TOP-REAL n,
      P, Q be Path of a,b;
  take F = RealHomotopy(P,Q);
  thus F is continuous by T90c;
  thus thesis by T90k;
end;

definition
  let n be Nat,
      a, b be Point of TOP-REAL n,
      P, Q be Path of a,b;
  redefine func RealHomotopy(P,Q) -> Homotopy of P,Q;
coherence
proof
  thus P, Q are_homotopic by T90;
  thus RealHomotopy(P,Q) is continuous by T90c;
  thus thesis by T90k;
end;
end;

registration
  let n be Nat,
      a, b be Point of TOP-REAL n,
      P, Q be Path of a,b;
  cluster -> continuous Homotopy of P,Q;
coherence
proof
  let F be Homotopy of P,Q;
  P, Q are_homotopic by T90;
  hence F is continuous by BORSUK_6:def 13;
end;
end;

theorem T91:
  for a being Point of TOP-REAL n,
      C being Loop of a holds
  the carrier of pi_1(TOP-REAL n,a) = { Class(EqRel(TOP-REAL n,a),C) }
proof
  let a be Point of TOP-REAL n,
      C be Loop of a;
  set X = TOP-REAL n;
  set E = EqRel(X,a);
  hereby
    let x be set;
    assume x in the carrier of pi_1(X,a);
    then consider P being Loop of a such that
a3:   x = Class(E,P) by Tciff;
    P,C are_homotopic by T90;
    then x = Class(E,C) by a3,Thiff;
    hence x in { Class(E,C) } by TARSKI:def 1;
  end;
  let x be set;
  assume x in { Class(E,C) };
  then
a2: x = Class(E,C) by TARSKI:def 1;
  C in Loops a by dloopsa;
  then x in Class E by a2,EQREL_1:def 5;
  hence thesis by dgr;
end;
  
registration
  let n be Nat;
  let a be Point of TOP-REAL n;
  cluster pi_1(TOP-REAL n,a) -> trivial;
coherence
proof
  set X = TOP-REAL n;
  set E = EqRel(X,a);
  consider C being constant Loop of a;
a1: the carrier of pi_1(X,a) = Class E by dgr;
  then { Class(E,C) } = Class E by T91;
  hence thesis by a1,REALSET2:def 5;
end;
end;
