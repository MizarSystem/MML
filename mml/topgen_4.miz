:: On the {B}orel Families of Subsets of Topological Spaces
::  by Adam Grabowski
::
:: Received August 30, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, SETFAM_1, SUBSET_1, BOOLE, TARSKI, FINSET_1, FUNCT_1,
      RELAT_1, ORDINAL2, TOPS_1, CONNSP_1, NATTRA_1, RLVECT_3, CARD_1,
      WAYBEL23, URYSOHN1, TOPMETR, RAT_1, CARD_4, PCOMPS_1, TOPGEN_1, TOPGEN_4,
      PROB_1, TSP_1, MCART_1, OPENLATT, MEASURE1, CONNSP_2, WAYBEL30, REALSET1,
      NEWTON;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, CARD_1, ORDINAL2,
      NUMBERS, CARD_4, MCART_1, FUNCT_2, NAT_1, FINSET_1, SETFAM_1, STRUCT_0,
      GROUP_1, CONNSP_2, REALSET1, OPENLATT, MEASURE1, PRE_TOPC, TOPS_1,
      TOPS_2, CONNSP_1, TDLAT_3, WAYBEL23, PROB_1, URYSOHN1, TOPMETR, CANTOR_1,
      ORDERS_4, TSP_1, TOPGEN_1, TOPGEN_3, PCOMPS_1;
 constructors MEMBERED, TOPS_1, TOPS_2, CONNSP_1, TDLAT_3, WAYBEL23, URYSOHN1,
      COMPTS_1, TSP_1, CARD_4, ORDERS_4, SETFAM_1, KURATO_2, MCART_1, TOPGEN_1,
      TOPGEN_2, TOPGEN_3, MEASURE1, OPENLATT;
 registrations STRUCT_0, RELSET_1, TOPS_1, YELLOW13, CARD_1, ORDINAL1,
      TOPREAL6, TOPGEN_1, FINSET_1, WAYBEL12, SUBSET_1, BORSUK_3, TOPGEN_2,
      TOPGEN_3, MEASURE1, ORDINAL2, PROB_1;
 requirements NUMERALS, BOOLE, SUBSET;
 definitions TARSKI, XBOOLE_0, CARD_4, MEASURE1, TOPS_2;
 theorems TARSKI, SETFAM_1, FUNCT_1, FUNCT_2, ZFMISC_1, PRE_TOPC, TOPS_1,
      XBOOLE_1, SUBSET_1, YELLOW_8, WAYBEL23, TOPMETR, NUMBERS, CONNSP_1,
      CONNSP_2, CARD_4, CARD_1, KURATO_2, PCOMPS_1, XBOOLE_0, TOPGEN_1, TOPS_2,
      MEASURE1, WAYBEL12, MCART_1, REALSET1, BORSUK_4, TSP_1, TOPGEN_2,
      TOPGEN_3, PROB_1, SUPINF_2, SETLIM_1, CARD_3, CARD_2, OPENLATT;
 schemes FUNCT_1, TREES_2, BORSUK_2, SUBSET_1, XBOOLE_0;

begin :: Preliminaries

definition let T be 1-sorted;
  func TotFam T -> Subset-Family of T equals
    bool the carrier of T;
  coherence;
end;

theorem ComCon:
  for T being set, F being Subset-Family of T holds
    F is countable iff COMPLEMENT F is countable
  proof
    let T be set, F be Subset-Family of T;
    F, COMPLEMENT F are_equipotent by YELLOW_8:4;
    hence thesis by YELLOW_8:5;
  end;

registration
  cluster RAT -> countable;
  coherence by CARD_4:def 1,TOPGEN_3:17;
end;

Th45: for X being set holds
    X is countable iff ex f being Function st dom f = RAT & X c= rng f
  proof
    let X be set;
    thus X is countable implies ex f be Function st dom f = RAT & X c= rng f
    proof assume Card X <=` alef 0;
      hence thesis by TOPGEN_3:17,CARD_1:28;
    end;
    assume ex f be Function st dom f = RAT & X c= rng f;
    hence Card X <=` alef 0 by TOPGEN_3:17,CARD_1:28;
  end;

scheme FraenCoun11 { P[set] } :
  { {n} where n is Element of RAT : P[n] } is countable
  proof
    deffunc g(set) = { $1 };
    consider f being Function such that
A1: dom f = RAT & for x being set st x in RAT holds
      f.x = g(x) from FUNCT_1:sch 3;
    { {n} where n is Element of RAT : P[n] } c= rng f
    proof let x be set;
      assume x in { {n} where n is Element of RAT : P[n] };
      then consider n be Element of RAT such that
A2:   x = {n} & P[n];
      f.n = x by A1,A2;
      hence thesis by A1,FUNCT_1:def 5;
    end;
    hence thesis by A1,Th45;
  end;

theorem
  for T being non empty TopSpace,
      A being Subset of T holds
    Der A = { x where x is Point of T : x in Cl (A \ {x}) }
  proof
    let T be non empty TopSpace,
        A be Subset of T;
    thus Der A c= { x where x is Point of T : x in Cl (A \ {x}) }
    proof
      let x be set;
      assume
A1:   x in Der A;
      x is_an_accumulation_point_of A by A1,TOPGEN_1:def 3; then
      x in Cl (A \ {x}) by TOPGEN_1:def 2;
      hence thesis;
    end;
    let y be set;
    assume y in { x where x is Point of T : x in Cl (A \ {x}) }; then
    consider z being Point of T such that
A2: z = y & z in Cl (A \ {z});
    z is_an_accumulation_point_of A by A2,TOPGEN_1:def 2;
    hence thesis by A2,TOPGEN_1:def 3;
  end;

registration
  cluster finite -> second-countable TopStruct;
  coherence
  proof
    let T be TopStruct;
    assume T is finite; then
    reconsider T' = T as finite TopStruct;
    weight T' is finite by TOPGEN_2:def 4; then
    weight T' in alef 0 by CARD_4:9; then
    weight T' <=` alef 0 by CARD_1:13;
    hence thesis by WAYBEL23:def 6,CARD_1:84,CARD_1:38;
  end;
end;

registration
  cluster REAL -> non countable;
  coherence
  proof
    assume REAL is countable; then
    Card REAL <=` alef 0 by CARD_4:def 1;
    hence thesis by CARD_1:14,TOPGEN_3:30,TOPGEN_3:def 4;
  end;
end;

registration
  cluster non countable -> non finite set;
  coherence by CARD_4:43;
  cluster non finite -> non trivial set;
  coherence
  proof
    let S be set;
    assume
A0: S is non finite;
    assume
A1: S is trivial;
    per cases by A1,REALSET1:def 4;
    suppose S is empty;
      then reconsider T = S as empty set;
      T is finite;
      hence thesis by A0;
    end;
    suppose ex x being set st S = {x};
    hence thesis by A0;
  end;
  end;
  cluster non countable non empty set;
  existence
  proof
    take REAL;
    thus thesis;
  end;
end;

 reserve T for non empty TopSpace,
         A, B for Subset of T,
         F, G for Subset-Family of T;

theorem
  A is closed iff Der A c= A
  proof
    hereby
      assume A is closed; then
A1:   A = Cl A by PRE_TOPC:52;
      Cl A = A \/ Der A by TOPGEN_1:31;
      hence Der A c= A by A1,XBOOLE_1:7;
    end;
    assume
A2: Der A c= A;
    Cl A = A \/ Der A by TOPGEN_1:31 .= A by A2,XBOOLE_1:12;
    hence thesis;
  end;

theorem Wiel:
  for T being non empty TopStruct,
      B being Basis of T,
      V being Subset of T st V is open & V <> {}
   ex W being Subset of T st W in B & W c= V & W <> {}
  proof
    let T be non empty TopStruct,
        B be Basis of T,
        V be Subset of T;
    assume
A1: V is open & V <> {}; then
A3: V = union { G where G is Subset of T : G in B & G c= V } by YELLOW_8:18;
    consider x being set such that
A2: x in V by A1,XBOOLE_0:def 1;
    consider Y being set such that
A4: x in Y & Y in { G where G is Subset of T : G in B & G c= V }
      by A2,A3,TARSKI:def 4;
    consider Z being Subset of T such that
A5: Z = Y & Z in B & Z c= V by A4;
    thus thesis by A4,A5;
  end;

begin :: Regular Formalization: Separable Spaces

:: 1.3.7. Theorem

theorem Th137:
  density T <=` weight T
  proof
    consider B being Basis of T;
    consider B1 being Basis of T such that
X1: B1 c= B & Card B1 = weight T by TOPGEN_2:20;
    deffunc F(set) = choose $1;
    defpred P[set] means $1 <> {};
    set A = { F(BB) where BB is Element of bool the carrier of T :
      BB in B1 & P[BB] };
    A c= the carrier of T
    proof
      let x be set;
      assume x in A; then
      consider B2 being Subset of T such that
Z1:   x = choose B2 & B2 in B1 & B2 <> {};
      x in B2 by Z1;
      hence thesis;
    end; then
    reconsider A as Subset of T;
    for Q being Subset of T st Q <> {} & Q is open holds A meets Q
    proof
      let Q be Subset of T;
      assume Q <> {} & Q is open; then
      consider W being Subset of T such that
H1:   W in B1 & W c= Q & W <> {} by Wiel;
H2:   choose W in A by H1;
      choose W in W by H1;
      hence thesis by H1,H2,XBOOLE_0:3;
    end; then
    A is dense by TOPS_1:80; then
B1: density T <=` Card A by TOPGEN_1:def 12;
    Card { F(w) where w is Element of bool the carrier of T : w in B1 & P[w] }
      <=` Card B1 from BORSUK_2:sch 1;
    hence thesis by X1,B1,XBOOLE_1:1;
  end;

theorem
  T is separable iff
    ex A being Subset of T st A is dense countable
  proof
    hereby assume T is separable; then
A1:   density T <=` alef 0 by TOPGEN_1:def 13;
      consider A being Subset of T such that
A2:   A is dense & density T = Card A by TOPGEN_1:def 12;
      A is countable by A1,A2,CARD_4:def 1;
      hence ex A being Subset of T st A is dense countable by A2;
    end;
    given A being Subset of T such that
A3: A is dense countable;
A4: density T <=` Card A by A3,TOPGEN_1:def 12;
    Card A <=` alef 0 by A3,CARD_4:def 1; then
    density T <=` alef 0 by A4,XBOOLE_1:1;
    hence thesis by TOPGEN_1:def 13;
  end;

:: 1.3.8. Corollary

theorem Pap1:
  T is second-countable implies T is separable
  proof
    assume T is second-countable; then
A1: weight T c= omega by WAYBEL23:def 6;
    density T <=` weight T by Th137; then
    density T c= omega by A1,XBOOLE_1:1;
    hence thesis by TOPGEN_1:def 13,CARD_1:83;
  end;

registration
  cluster second-countable -> separable (non empty TopSpace);
  coherence by Pap1;
end;

:: Exercises

theorem :: Exercise 1.3.A.
  for T being non empty TopSpace,
      A, B being Subset of T st A, B are_separated holds
    Fr (A \/ B) = Fr A \/ Fr B
  proof
    let T be non empty TopSpace,
        A, B be Subset of T;
    assume
A0: A, B are_separated; then
A1: A misses B by CONNSP_1:2;
V1: Cl A misses B & A misses Cl B by A0,CONNSP_1:def 1;
A3: Fr A \/ Fr B = Fr (A \/ B) \/ Fr (A /\ B) \/ (Fr A /\ Fr B) by TOPS_1:70;
A2: A /\ B = {} by A1,XBOOLE_0:def 7;
a4: Fr {}T = {};
B1: Fr (A \/ B) c= Fr A \/ Fr B by TOPS_1:67;
    Fr A \/ Fr B c= Fr (A \/ B)
    proof
      let x be set;
      assume
PP:   x in Fr A \/ Fr B;
      per cases by a4,PP,XBOOLE_0:def 2,A2,A3;
      suppose x in Fr (A \/ B);
        hence thesis;
      end;
      suppose x in Fr A /\ Fr B; then
        x in Fr A & x in Fr B by XBOOLE_0:def 3; then
        x in Cl A /\ Cl A` & x in Cl B /\ Cl B` by TOPS_1:def 2; then
V2:     x in Cl A & x in Cl A` & x in Cl B & x in Cl B`
          by XBOOLE_0:def 3;
        not x in B by V1,V2,XBOOLE_0:3; then
X1:     x in B` by PP,KURATO_2:1;
        not x in A by V1,V2,XBOOLE_0:3; then
X2:     x in A` by PP,KURATO_2:1;
        x in Cl A \/ Cl B by V2,XBOOLE_0:def 2; then
W1:     x in Cl (A \/ B) by PRE_TOPC:50;
        x in A` /\ B` by X1,X2,XBOOLE_0:def 3; then
X3:     x in (A \/ B)` by SUBSET_1:29;
        (A \/ B)` c= Cl (A \/ B)` by PRE_TOPC:48; then
        x in Cl (A \/ B) /\ Cl (A \/ B)` by X3,W1,XBOOLE_0:def 3;
        hence thesis by TOPS_1:def 2;
      end;
    end;
    hence thesis by B1,XBOOLE_0:def 10;
  end;

:: Exercise 1.3.B.

theorem :: Ex. 1.3.B. a)
  F is locally_finite implies
    Fr union F c= union Fr F
  proof
    assume F is locally_finite; then
A2: Cl union F = union clf F by PCOMPS_1:23;
    let x be set;
    assume x in Fr union F; then
A5: x in Cl union F /\ Cl (union F)` by TOPS_1:def 2; then
    x in Cl union F by XBOOLE_0:def 3; then
    consider A being set such that
A3: x in A & A in clf F by A2,TARSKI:def 4;
    consider B being Subset of T such that
AA: A = Cl B & B in F by A3,PCOMPS_1:def 3;
G1: x in Cl (union F)` by A5,XBOOLE_0:def 3;
    B c= union F by AA,ZFMISC_1:92; then
    (union F)` c= B` by SUBSET_1:31; then
    Cl (union F)` c= Cl B` by PRE_TOPC:49; then
A8: x in Cl B /\ Cl B` by G1,A3,AA,XBOOLE_0:def 3;
    reconsider A as Subset of T by A3;
H1: x in Fr B by A8,TOPS_1:def 2;
    Fr B in Fr F by AA,TOPGEN_1:def 1;
    hence thesis by H1,TARSKI:def 4;
  end;

theorem Th1: :: also for empty TopSpaces
  for T being discrete non empty TopSpace holds
    T is separable iff Card [#]T <=` alef 0
  proof
    let T be discrete non empty TopSpace;
    hereby
      assume T is separable; then
A3:   density T <=` alef 0 by TOPGEN_1:def 13;
      consider A being Subset of T such that
A1:   A is dense & density T = Card A by TOPGEN_1:def 12;
      thus Card [#]T <=` alef 0 by A1,A3,TOPGEN_1:48;
    end;
    assume Card [#]T <=` alef 0; then
    T is countable by TOPGEN_1:3;
    hence thesis by TOPGEN_1:49;
  end;

theorem
  for T being discrete non empty TopSpace holds
    T is separable iff T is countable
  proof
    let T be discrete non empty TopSpace;
    T is separable iff Card [#]T <=` alef 0 by Th1;
    hence thesis by TOPGEN_1:3;
  end;

begin :: Families of Subsets Closed for Countable Unions and Complement

definition let T, F;
  attr F is all-open-containing means :AllOpen:
    for A being Subset of T st A is open holds
      A in F;
end;

definition let T, F;
  attr F is all-closed-containing means :AllClosed:
    for A being Subset of T st A is closed holds
      A in F;
end;

definition let T be set, F be Subset-Family of T;
  attr F is closed_for_countable_unions means :CounUn:
    for G being countable Subset-Family of T st
      G c= F holds union G in F;
end;

Lemma1:
  for T being set, F being Subset-Family of T st
    F is sigma_Field_Subset of T holds
      F is closed_for_countable_unions
  proof
    let T be set,
        F be Subset-Family of T;
    assume
A0: F is sigma_Field_Subset of T;
    let G be countable Subset-Family of T;
    assume
A1: G c= F;
    per cases;
    suppose G is empty;
      hence thesis by A0,MEASURE1:45,ZFMISC_1:2;
    end;
    suppose G is non empty;
      hence thesis by A0,A1,MEASURE1:def 9;
    end;
  end;

registration let T be set;
  cluster -> closed_for_countable_unions sigma_Field_Subset of T;
  coherence by Lemma1;
end;

theorem Lemma2:
  for T being set, F being Subset-Family of T st
    F is closed_for_countable_unions holds
      {} in F
  proof
    let T be set, F be Subset-Family of T;
    assume
A1: F is closed_for_countable_unions;
    reconsider E = {} as countable Subset-Family of T by XBOOLE_1:2;
    E c= F by XBOOLE_1:2;
    hence thesis by A1,CounUn,ZFMISC_1:2;
  end;

registration let T be set;
  cluster closed_for_countable_unions -> non empty Subset-Family of T;
  coherence by Lemma2;
end;

theorem Kachu:
  for T being set, F being Subset-Family of T holds
    F is sigma_Field_Subset of T iff
      F is compl-closed closed_for_countable_unions
  proof
    let T be set, F be Subset-Family of T;
    thus F is sigma_Field_Subset of T implies
      F is compl-closed closed_for_countable_unions;
    assume
A1: F is compl-closed closed_for_countable_unions;
    F is sigma_Field_Subset-like
    proof
      let M be N_Sub_set_fam of T;
      assume M c= F;
      hence thesis by A1,CounUn;
    end;
    hence thesis by A1,Lemma2;
  end;

definition let T be set, F be Subset-Family of T;
  attr F is closed_for_countable_meets means :CounMet:
    for G being countable Subset-Family of T st
      G c= F holds meet G in F;
end;

theorem JP2:
  for F being Subset-Family of T holds
    F is all-closed-containing compl-closed iff
      F is all-open-containing compl-closed
  proof
    let F be Subset-Family of T;
    hereby assume
A1:   F is all-closed-containing compl-closed;
      for A being Subset of T st A is open holds A in F
      proof
        let A be Subset of T;
        assume A is open; then
        A` is closed by TOPS_1:30; then
        A` in F by A1,AllClosed; then
        A`` in F by A1,PROB_1:def 1;
        hence thesis;
      end;
      hence F is all-open-containing compl-closed by A1,AllOpen;
    end;
    assume
A1: F is all-open-containing compl-closed;
    for A being Subset of T st A is closed holds A in F
    proof
      let A be Subset of T;
      assume A is closed; then
      A`` is closed; then
      A` is open by TOPS_1:30; then
      A` in F by A1,AllOpen; then
      A`` in F by A1,PROB_1:def 1;
      hence thesis;
    end;
    hence F is all-closed-containing compl-closed by A1,AllClosed;
  end;

theorem
  for T being set, F being Subset-Family of T st F is compl-closed holds
    F = COMPLEMENT F
  proof
    let T be set, F be Subset-Family of T;
    assume
A0: F is compl-closed;
    thus F c= COMPLEMENT F
    proof
      let x be set;
      assume
A1:   x in F; then
      reconsider x' = x as Subset of T;
      x'` in F by A0,A1,PROB_1:def 1;
      hence thesis by SETFAM_1:def 8;
    end;
    let x be set;
    assume
B1: x in COMPLEMENT F; then
    reconsider x' = x as Subset of T;
    x'` in F by B1,SETFAM_1:def 8; then
    x'`` in F by A0,PROB_1:def 1;
    hence thesis;
  end;

theorem Police2:
  for T being set, F, G being Subset-Family of T st
    F c= G & G is compl-closed holds
      COMPLEMENT F c= G
  proof
    let T be set, F, G be Subset-Family of T;
    assume
A1: F c= G & G is compl-closed;
    let x be set;
    assume
A2: x in COMPLEMENT F; then
    reconsider x' = x as Subset of T;
    x'` in F by A2,SETFAM_1:def 8; then
    x'`` in G by A1,PROB_1:def 1;
    hence thesis;
  end;

theorem JP1:
  for T being set, F being Subset-Family of T holds
    F is closed_for_countable_meets compl-closed iff
      F is closed_for_countable_unions compl-closed
  proof
    let T be set, F be Subset-Family of T;
    hereby assume
A1:   F is closed_for_countable_meets compl-closed;
      for G being countable Subset-Family of T st
        G c= F holds union G in F
      proof
        let G be countable Subset-Family of T;
        assume
V1:     G c= F;
        per cases;
        suppose G = {};
          hence thesis by ZFMISC_1:2,V1,SETFAM_1:2,A1,CounMet;
        end;
        suppose G <> {}; then
          reconsider G' = G as non empty Subset-Family of T;
V2:       COMPLEMENT G' c= F by V1,A1,Police2;
          COMPLEMENT G' is countable by ComCon; then
z1:       meet COMPLEMENT G' in F by A1,V2,CounMet;
          (meet COMPLEMENT G')` = (union G')`` by TOPS_2:11 .= union G';
          hence union G in F by z1,A1,PROB_1:def 1;
        end;
      end;
      hence F is closed_for_countable_unions compl-closed by A1,CounUn;
    end;
    assume
A1: F is closed_for_countable_unions compl-closed;
    for G being countable Subset-Family of T st
      G c= F holds meet G in F
    proof
      let G be countable Subset-Family of T;
      assume
V1:   G c= F;
      per cases;
      suppose G = {};
        hence thesis by V1,ZFMISC_1:2,SETFAM_1:2,A1,CounUn;
      end;
      suppose G <> {}; then
        reconsider G' = G as non empty Subset-Family of T;
V2:     COMPLEMENT G' c= F by V1,A1,Police2;
        COMPLEMENT G' is countable by ComCon; then
z1:     union COMPLEMENT G' in F by A1,V2,CounUn;
        (union COMPLEMENT G')` = (meet G')`` by TOPS_2:12 .= meet G';
        hence meet G in F by z1,A1,PROB_1:def 1;
      end;
    end;
    hence thesis by A1,CounMet;
  end;

registration let T;
  cluster all-open-containing compl-closed closed_for_countable_unions ->
    all-closed-containing closed_for_countable_meets Subset-Family of T;
  coherence by JP1,JP2;
  cluster all-closed-containing compl-closed closed_for_countable_meets ->
      all-open-containing closed_for_countable_unions Subset-Family of T;
  coherence by JP1,JP2;
end;

begin :: On the Families of Subsets

registration let T be set;
             let F be countable Subset-Family of T;
  cluster COMPLEMENT F -> countable;
  coherence by ComCon;
end;

registration let T;
  cluster empty -> open closed Subset-Family of T;
  coherence
  proof
    let F be Subset-Family of T;
    assume
A1: F is empty; then
a2: for P being Subset of T holds P in F implies P is open;
    for P being Subset of T holds P in F implies P is closed by A1;
    hence thesis by a2,TOPS_2:def 2,TOPS_2:def 1;
  end;
end;

registration let T;
  cluster countable open closed Subset-Family of T;
  existence
  proof
    consider F being empty Subset-Family of T;
    F is open;
    hence thesis;
  end;
end;

theorem Th01:
  for T being set holds {} is empty Subset-Family of T
  proof
    let T be set;
    consider F being empty Subset-Family of T;
    F = {};
    hence thesis;
  end;

registration
  cluster empty -> countable set;
  coherence
  proof
    let A be set;
    assume A is empty; then
    reconsider A' = A as empty set;
    A' is countable;
    hence thesis;
  end;
end;

begin :: Collective Properties of Families

theorem ThOp:
  F = { A } implies (A is open iff F is open)
  proof
    assume
A1: F = { A };
    hereby
      assume A is open; then
      for A being Subset of T st A in F holds
        A is open by A1,TARSKI:def 1;
      hence F is open by TOPS_2:def 1;
    end;
    assume
A2: F is open;
    A in F by A1,TARSKI:def 1;
    hence thesis by A2,TOPS_2:def 1;
  end;

theorem ThCl:
  F = { A } implies (A is closed iff F is closed)
  proof
    assume
A1: F = { A };
    hereby
      assume A is closed; then
      for A being Subset of T st A in F holds
        A is closed by A1,TARSKI:def 1;
      hence F is closed by TOPS_2:def 2;
    end;
    assume
A2: F is closed;
    A in F by A1,TARSKI:def 1;
    hence thesis by A2,TOPS_2:def 2;
  end;

definition let T be set, F, G be Subset-Family of T;
  redefine func INTERSECTION (F,G) -> Subset-Family of T;
  coherence
  proof
    INTERSECTION (F,G) c= bool T
    proof
      let x be set;
      assume x in INTERSECTION (F,G); then
      consider X, Y being set such that
A1:   X in F & Y in G & x = X /\ Y by SETFAM_1:def 5;
      X /\ Y c= X by XBOOLE_1:17; then
      x is Subset of T by A1,XBOOLE_1:1;
      hence thesis;
    end;
    hence thesis;
  end;
  redefine func UNION (F,G) -> Subset-Family of T;
  coherence
  proof
    UNION (F,G) c= bool T
    proof
      let x be set;
      assume x in UNION (F,G); then
      consider X, Y being set such that
A1:   X in F & Y in G & x = X \/ Y by SETFAM_1:def 4;
      X \/ Y c= T by A1,XBOOLE_1:8;
      hence thesis by A1;
    end;
    hence thesis;
  end;
end;

theorem Kul1:
  F is closed & G is closed implies INTERSECTION (F,G) is closed
  proof
    assume that
A1: F is closed and
A2: G is closed;
    for A being Subset of T st A in INTERSECTION (F,G) holds
      A is closed
    proof
      let A be Subset of T;
      assume A in INTERSECTION (F,G); then
      consider X, Y being set such that
B1:   X in F & Y in G & A = X /\ Y by SETFAM_1:def 5;
      reconsider X, Y as Subset of T by B1;
      X is closed & Y is closed by B1,A1,A2,TOPS_2:def 2;
      hence thesis by B1,TOPS_1:35;
    end;
    hence thesis by TOPS_2:def 2;
  end;

theorem Kul1':
  F is closed & G is closed implies UNION (F,G) is closed
  proof
    assume that
A1: F is closed and
A2: G is closed;
    for A being Subset of T st A in UNION (F,G) holds
      A is closed
    proof
      let A be Subset of T;
      assume A in UNION (F,G); then
      consider X, Y being set such that
B1:   X in F & Y in G & A = X \/ Y by SETFAM_1:def 4;
      reconsider X, Y as Subset of T by B1;
      X is closed & Y is closed by B1,A1,A2,TOPS_2:def 2;
      hence thesis by B1,TOPS_1:36;
    end;
    hence thesis by TOPS_2:def 2;
  end;

theorem Kul21:
  F is open & G is open implies INTERSECTION (F,G) is open
  proof
    assume that
A1: F is open and
A2: G is open;
    for A being Subset of T st A in INTERSECTION (F,G) holds
      A is open
    proof
      let A be Subset of T;
      assume A in INTERSECTION (F,G); then
      consider X, Y being set such that
B1:   X in F & Y in G & A = X /\ Y by SETFAM_1:def 5;
      reconsider X, Y as Subset of T by B1;
      X is open & Y is open by B1,A1,A2,TOPS_2:def 1;
      hence thesis by B1,TOPS_1:38;
    end;
    hence thesis by TOPS_2:def 1;
  end;

theorem Kul2:
  F is open & G is open implies UNION (F,G) is open
  proof
    assume that
A1: F is open and
A2: G is open;
    for A being Subset of T st A in UNION (F,G) holds
      A is open
    proof
      let A be Subset of T;
      assume A in UNION (F,G); then
      consider X, Y being set such that
B1:   X in F & Y in G & A = X \/ Y by SETFAM_1:def 4;
      reconsider X, Y as Subset of T by B1;
      X is open & Y is open by B1,A1,A2,TOPS_2:def 1;
      hence thesis by B1,TOPS_1:37;
    end;
    hence thesis by TOPS_2:def 1;
  end;

theorem KolHam:
  for T being set,
      F, G being Subset-Family of T holds
    Card INTERSECTION (F,G) <=` Card [:F,G:]
  proof
    let T be set,
        F, G be Subset-Family of T;
    set XX = [:F,G:];
    set YY = INTERSECTION (F,G);
    deffunc F(set) = $1`1 /\ $1`2;
    set IN = { F(X) where X is Element of [:bool T, bool T:] : X in XX };
    set A = [:bool T, bool T:];
    set AL = F, BL = G;
P1: Card { F(w) where w is Element of A : w in XX } <=` Card XX
      from TREES_2:sch 2;
    set C = INTERSECTION(AL,BL);
    IN = C
    proof
      thus IN c= C
      proof
        let a be set;
        assume a in IN; then
        consider X being Element of [:bool T, bool T:] such that
B1:     a = F(X) & X in XX;
B4:     X`1 in F & X`2 in G by B1,MCART_1:10;
        reconsider X1 = X`1, X2 = X`2 as Subset of T by MCART_1:10;
        thus a in C by B4,B1,SETFAM_1:def 5;
      end;
      thus C c= IN
      proof
        let a be set; assume a in C;
        then consider X,Y be set such that
A6:     X in AL & Y in BL & a = X /\ Y by SETFAM_1:def 5;
        reconsider X,Y as Subset of T by A6;
        set XY = [X,Y];
a6:     XY`1 = X & XY`2 = Y by MCART_1:7;
        XY in XX by A6,ZFMISC_1:106;
        hence a in IN by a6,A6;
      end;
    end;
    hence thesis by P1;
  end;

theorem KolHam2:
  for T being set,
      F, G being Subset-Family of T holds
    Card UNION (F,G) <=` Card [:F,G:]
  proof
    let T be set,
        F, G be Subset-Family of T;
    set XX = [:F,G:];
    deffunc F(set) = $1`1 \/ $1`2;
    set IN = { F(X) where X is Element of [:bool T, bool T:] : X in XX };
    set A = [:bool T, bool T:];
    set AL = F, BL = G;
P1: Card { F(w) where w is Element of A : w in XX } <=` Card XX
      from TREES_2:sch 2;
    set C = UNION(AL,BL);
    IN = C
    proof
      thus IN c= C
      proof
        let a be set;
        assume a in IN; then
        consider X being Element of [:bool T, bool T:] such that
B1:     a = F(X) & X in XX;
B4:     X`1 in F & X`2 in G by B1,MCART_1:10;
        reconsider X1 = X`1, X2 = X`2 as Subset of T by MCART_1:10;
        thus a in C by SETFAM_1:def 4,B4,B1;
      end;
      let a be set; assume a in C;
      then consider X,Y be set such that
A6:   X in AL & Y in BL & a = X \/ Y by SETFAM_1:def 4;
      reconsider X,Y as Subset of T by A6;
      set XY = [X,Y];
c2:   XY`1 = X & XY`2 = Y by MCART_1:7;
      XY in XX by A6,ZFMISC_1:106;
      hence a in IN by c2,A6;
    end;
    hence thesis by P1;
  end;

theorem HHH0:
  for F, G being set holds
    union UNION (F,G) c= union F \/ union G
  proof
    let F, G be set;
    let x be set;
    assume x in union UNION (F,G); then
    consider Y being set such that
A1: x in Y & Y in UNION (F,G) by TARSKI:def 4;
    consider f,g being set such that
A2: f in F & g in G & Y = f \/ g by A1,SETFAM_1:def 4;
    per cases by A2,A1,XBOOLE_0:def 2;
    suppose x in f; then
      x in union F by A2,TARSKI:def 4;
      hence thesis by XBOOLE_0:def 2;
    end;
    suppose x in g; then
      x in union G by A2,TARSKI:def 4;
      hence thesis by XBOOLE_0:def 2;
    end;
  end;

theorem HHH1:
  for F, G being set st F <> {} & G <> {} holds
    union F \/ union G = union UNION (F,G)
  proof
    let F, G be set;
    assume
X0: F <> {} & G <> {};
    thus union F \/ union G c= union UNION (F,G)
    proof
      let x be set;
      assume
a1:   x in union F \/ union G;
      per cases by a1,XBOOLE_0:def 2;
      suppose x in union F; then
        consider Y being set such that
B1:     x in Y & Y in F by TARSKI:def 4;
        consider W being set such that
B2:     W in G by X0,XBOOLE_0:def 1;
        set YW = Y \/ W;
b3:     Y c= YW by XBOOLE_1:7;
        YW in UNION (F,G) by B1,B2,SETFAM_1:def 4;
        hence thesis by b3,B1,TARSKI:def 4;
      end;
      suppose x in union G; then
        consider Y being set such that
B1:     x in Y & Y in G by TARSKI:def 4;
        consider W being set such that
B2:     W in F by X0,XBOOLE_0:def 1;
        set YW = W \/ Y;
b3:     Y c= YW by XBOOLE_1:7;
        YW in UNION (F,G) by B1,B2,SETFAM_1:def 4;
        hence thesis by b3,B1,TARSKI:def 4;
      end;
    end;
    thus thesis by HHH0;
  end;

theorem BBB:
  for F being set holds UNION ({},F) = {}
  proof
    let F be set;
    UNION ({},F) c= {}
    proof
      let x be set;
      assume x in UNION ({},F); then
      consider x1, x2 being set such that
A1:   x1 in {} & x2 in F & x = x1 \/ x2 by SETFAM_1:def 4;
      thus thesis by A1;
    end;
    hence thesis by XBOOLE_1:3;
  end;

theorem
  for F, G being set holds
    UNION (F, G) = {} implies F = {} or G = {}
  proof
    let F, G be set;
    assume
B1: UNION (F,G) = {};
    assume
A0: F <> {} & G <> {}; then
    consider X being set such that
A1: X in F by XBOOLE_0:def 1;
    consider Y being set such that
A2: Y in G by A0,XBOOLE_0:def 1;
    X \/ Y in UNION (F,G) by A1,A2,SETFAM_1:def 4;
    hence thesis by B1;
  end;

theorem
  for F, G being set st
    INTERSECTION (F, G) = {} holds F = {} or G = {}
  proof
    let F, G be set;
    assume that
B1: INTERSECTION (F,G) = {} and
A0: F <> {} & G <> {};
    consider X being set such that
A1: X in F by A0,XBOOLE_0:def 1;
    consider Y being set such that
A2: Y in G by A0,XBOOLE_0:def 1;
    X /\ Y in INTERSECTION (F,G) by A1,A2,SETFAM_1:def 5;
    hence thesis by B1;
  end;

theorem AAA:
  for F, G being set holds
    meet UNION (F,G) c= meet F \/ meet G
  proof
    let F, G be set;
    per cases;
    suppose
A0:   F <> {} & G <> {};
      let x be set;
      assume
C1:   x in meet UNION (F,G);
      assume not x in meet F \/ meet G; then
A1:   not x in meet F & not x in meet G by XBOOLE_0:def 2; then
      consider Y being set such that
B1:   Y in F & not x in Y by A0,SETFAM_1:def 1;
      consider Z being set such that
B2:   Z in G & not x in Z by A0,A1,SETFAM_1:def 1;
B3:   not x in Y \/ Z by B1,B2,XBOOLE_0:def 2;
      Y \/ Z in UNION (F,G) by B1,B2,SETFAM_1:def 4;
      hence thesis by C1,SETFAM_1:def 1,B3;
    end;
    suppose F = {} or G = {}; then
      UNION (F,G) = {} by BBB; then
      meet UNION (F,G) = {} by SETFAM_1:def 1;
      hence thesis by XBOOLE_1:2;
    end;
  end;

theorem
  for F, G being set st F <> {} & G <> {} holds
    meet UNION(F,G) = meet F \/ meet G
  proof
    let F, G be set;
    assume
A1: F <> {} & G <> {};
    thus meet UNION(F,G) c= meet F \/ meet G by AAA;
    thus meet F \/ meet G c= meet UNION(F,G) by A1,SETFAM_1:40;
  end;

theorem SETF:
  for F, G being set st
    F <> {} & G <> {} holds
      meet F /\ meet G = meet INTERSECTION(F,G)
  proof
    let F, G be set;
    assume
A2: F <> {} & G <> {}; then
    consider y being set such that
A3: y in F by XBOOLE_0:def 1;
    consider z being set such that
a3: z in G by A2,XBOOLE_0:def 1;
A4: y /\ z in INTERSECTION(F,G) by A3,a3,SETFAM_1:def 5;
A5: meet F /\ meet G c= meet INTERSECTION (F,G)
    proof
      let x be set;
      assume x in meet F /\ meet G; then
A6:   x in meet F & x in meet G by XBOOLE_0:def 3;
      for Z be set st Z in INTERSECTION(F,G) holds x in Z
      proof
        let Z be set; assume
         Z in INTERSECTION(F,G); then
         consider Z1,Z2 be set such that
A7:      Z1 in F & Z2 in G & Z = Z1 /\ Z2 by SETFAM_1:def 5;
         x in Z1 & x in Z2 by A6,A7,SETFAM_1:def 1;
         hence x in Z by A7,XBOOLE_0:def 3;
       end;
       hence x in meet INTERSECTION(F,G) by A4,SETFAM_1:def 1;
     end;
     meet INTERSECTION(F,G) c= meet F /\ meet G
     proof
       let x be set such that
A8:    x in meet INTERSECTION(F,G);
       for Z be set st Z in F holds x in Z
       proof
         let Z be set; assume Z in F;
         then Z /\ z in INTERSECTION(F,G) by a3,SETFAM_1:def 5;
         then x in Z /\ z by A8,SETFAM_1:def 1;
         hence x in Z by XBOOLE_0:def 3;
       end; then
A9:    x in meet F by A2,SETFAM_1:def 1;
       for Z be set st Z in G holds x in Z
       proof
         let Z be set; assume Z in G;
         then y /\ Z in INTERSECTION(F,G) by A3,SETFAM_1:def 5;
         then x in y /\ Z by A8,SETFAM_1:def 1;
         hence x in Z by XBOOLE_0:def 3;
       end;
       then x in meet G by A2,SETFAM_1:def 1;
       hence x in meet F /\ meet G by A9,XBOOLE_0:def 3;
     end;
     hence thesis by A5,XBOOLE_0:def 10;
   end;

begin :: F_sigma and G_delta Types of Subsets

definition let T, A;
  attr A is F_sigma means :FSigDef:
    ex F being closed countable Subset-Family of T st
       A = union F;
end;

definition let T, A;
  attr A is G_delta means :GDelDef:
    ex F being open countable Subset-Family of T st
       A = meet F;
end;

theorem CykC:
  {}T is F_sigma
  proof
    reconsider E = {} as empty Subset-Family of T by Th01;
    {}T = union E by ZFMISC_1:2;
    hence thesis by FSigDef;
  end;

theorem Cyk1:
  {}T is G_delta
  proof
    reconsider F = { {}T } as Subset-Family of T by ZFMISC_1:37;
A2: F is open by ThOp;
    {}T = meet F by SETFAM_1:11;
    hence thesis by A2,GDelDef;
  end;

registration let T;
  cluster {}T -> F_sigma G_delta;
  coherence by Cyk1,CykC;
end;

theorem CarSig:
  [#]T is F_sigma
  proof
    reconsider F = { [#]T } as Subset-Family of T by ZFMISC_1:37;
A2: F is closed by ThCl;
    [#]T = union F by ZFMISC_1:31;
    hence thesis by A2,FSigDef;
  end;

theorem CarDel:
  [#]T is G_delta
  proof
    reconsider F = { [#]T } as Subset-Family of T by ZFMISC_1:37;
A2: F is open by ThOp;
    [#]T = meet F by SETFAM_1:11;
    hence thesis by A2,GDelDef;
  end;

registration let T;
  cluster [#]T -> F_sigma G_delta;
  coherence by CarSig,CarDel;
end;

theorem
  A is F_sigma implies A` is G_delta
  proof
    assume A is F_sigma; then
    consider F being closed countable Subset-Family of T such that
A1: A = union F by FSigDef;
    per cases;
      suppose F <> {}; then
A2:   (union F)` = meet COMPLEMENT F by TOPS_2:11;
      set G = COMPLEMENT F;
      G is open by TOPS_2:16;
      hence thesis by A2,A1,GDelDef;
    end;
    suppose F = {}; then
      A = {}T by A1,ZFMISC_1:2; then
      A` = [#]T by PRE_TOPC:27;
      hence thesis;
    end;
  end;

theorem
  A is G_delta implies A` is F_sigma
  proof
    assume A is G_delta; then
    consider F being open countable Subset-Family of T such that
A1: A = meet F by GDelDef;
    per cases;
      suppose F <> {}; then
A2:   (meet F)` = union COMPLEMENT F by TOPS_2:12;
      set G = COMPLEMENT F;
      G is closed by TOPS_2:17;
      hence thesis by A2,A1,FSigDef;
    end;
    suppose F = {}; then
      A = {}T by A1,SETFAM_1:2; then
      A` = [#]T by PRE_TOPC:27;
      hence thesis;
    end;
  end;

theorem
  A is F_sigma & B is F_sigma implies A /\ B is F_sigma
  proof
    assume that
A1: A is F_sigma and
A2: B is F_sigma;
    consider F being closed countable Subset-Family of T such that
A3: A = union F by A1,FSigDef;
    consider G being closed countable Subset-Family of T such that
A4: B = union G by A2,FSigDef;
    reconsider H = INTERSECTION (F,G) as Subset-Family of T;
Z1: A /\ B = union H by A3,A4,SETFAM_1:39;
Z2: H is closed by Kul1;
J1: Card H <=` Card [:F,G:] by KolHam;
    [:F,G:] is countable by CARD_4:55; then
    H is countable by J1,WAYBEL12:2;
    hence thesis by Z1,Z2,FSigDef;
  end;

theorem
  A is F_sigma & B is F_sigma implies A \/ B is F_sigma
  proof
    assume that
A1: A is F_sigma and
A2: B is F_sigma;
    consider F being closed countable Subset-Family of T such that
A3: A = union F by A1,FSigDef;
    consider G being closed countable Subset-Family of T such that
A4: B = union G by A2,FSigDef;
    reconsider H = UNION (F,G) as Subset-Family of T;
    per cases;
    suppose A <> {} & B <> {}; then
Z1:   A \/ B = union H by A3,HHH1,A4,ZFMISC_1:2;
Z2:   H is closed by Kul1';
J1:   Card H <=` Card [:F,G:] by KolHam2;
      [:F,G:] is countable by CARD_4:55; then
      H is countable by J1,WAYBEL12:2;
      hence thesis by Z1,Z2,FSigDef;
    end;
    suppose A = {};
      hence thesis by A2;
    end;
    suppose B = {};
      hence thesis by A1;
    end;
  end;

theorem
  A is G_delta & B is G_delta implies A \/ B is G_delta
  proof
    assume that
A1: A is G_delta and
A2: B is G_delta;
    consider F being open countable Subset-Family of T such that
A3: A = meet F by A1,GDelDef;
    consider G being open countable Subset-Family of T such that
A4: B = meet G by A2,GDelDef;
    reconsider H = UNION (F,G) as Subset-Family of T;
    per cases;
    suppose F <> {} & G <> {}; then
Y1:   meet F \/ meet G c= meet UNION(F,G) by SETFAM_1:40;
      meet UNION(F,G) c= meet F \/ meet G by AAA; then
Z1:   A \/ B = meet H by A3,A4,Y1,XBOOLE_0:def 10;
Z2:   H is open by Kul2;
J1:   Card H <=` Card [:F,G:] by KolHam2;
      [:F,G:] is countable by CARD_4:55; then
      H is countable by J1,WAYBEL12:2;
      hence thesis by GDelDef,Z2,Z1;
    end;
    suppose F = {} or G = {}; then
      A = {} or B = {} by A3,A4,SETFAM_1:def 1;
      hence thesis by A1,A2;
    end;
  end;

theorem
  A is G_delta & B is G_delta implies A /\ B is G_delta
  proof
    assume that
A1: A is G_delta and
A2: B is G_delta;
    consider F being open countable Subset-Family of T such that
A3: A = meet F by A1,GDelDef;
    consider G being open countable Subset-Family of T such that
A4: B = meet G by A2,GDelDef;
    reconsider H = INTERSECTION (F,G) as Subset-Family of T;
    per cases;
    suppose F <> {} & G <> {}; then
Z1:   A /\ B = meet H by A3,A4,SETF;
Z2:   H is open by Kul21;
J1:   Card H <=` Card [:F,G:] by KolHam;
      [:F,G:] is countable by CARD_4:55; then
      H is countable by J1,WAYBEL12:2;
      hence thesis by GDelDef,Z2,Z1;
    end;
    suppose F = {} or G = {}; then
      A = {} or B = {} by A3,A4,SETFAM_1:def 1; then
      A /\ B = {}T;
      hence thesis;
    end;
  end;

theorem
  for A being Subset of T st A is closed holds
    A is F_sigma
  proof
    let A be Subset of T;
    assume A is closed; then
    reconsider F = {A} as closed countable Subset-Family of T
      by ThCl,ZFMISC_1:37;
    take F;
    thus thesis by ZFMISC_1:31;
  end;

theorem
  for A being Subset of T st A is open holds
    A is G_delta
  proof
    let A be Subset of T;
    assume A is open; then
    reconsider F = {A} as open countable Subset-Family of T
      by ThOp,ZFMISC_1:37;
    take F;
    thus thesis by SETFAM_1:11;
  end;

theorem
  for A being Subset of R^1 st A = RAT holds
    A is F_sigma
  proof
    let A be Subset of R^1;
    assume
A0: A = RAT;
    defpred P[set] means ex a being Element of RAT st {a} = $1;
    defpred R[set] means ex a being Element of RAT st a = $1;
    ex F being set st
      for x being set holds x in F iff x in bool RAT & P[x]
        from XBOOLE_0:sch 1; then
    consider F being set such that
A1: for x being set holds x in F iff x in bool RAT & P[x];
AA: bool RAT c= bool REAL by NUMBERS:12,ZFMISC_1:79;
    F c= bool the carrier of R^1
    proof
      let x be set;
      assume x in F; then
      x in bool RAT by A1;
      hence thesis by AA,TOPMETR:24;
    end; then
    reconsider F as Subset-Family of R^1;
    ex F being Subset-Family of R^1 st
      F is closed countable & A = union F
    proof
      take F;
      for B being Subset of R^1 st B in F holds
        B is closed
      proof
        let B be Subset of R^1;
        assume B in F; then
        consider a being Element of RAT such that
B1:     {a} = B by A1;
        a in RAT; then
        reconsider a as Point of R^1 by TOPMETR:24,NUMBERS:12;
        B = {a} by B1;
        hence thesis;
      end;
      hence F is closed by TOPS_2:def 2;
T1:   F = { {x} where x is Element of RAT : R[x] }
      proof
        thus F c= { {x} where x is Element of RAT : R[x] }
        proof
          let y be set;
          assume y in F; then
          y in bool RAT & P[y] by A1;
          hence thesis;
        end;
        let y be set;
        assume y in { {x} where x is Element of RAT : R[x] }; then
        consider z being Element of RAT such that
Z1:     y = {z} & R[z];
        {z} c= RAT by ZFMISC_1:37;
        hence thesis by A1,Z1;
      end;
      { {x} where x is Element of RAT : R[x] } is countable
        from FraenCoun11;
      hence F is countable by T1;
      thus A c= union F
      proof
        let x be set;
        assume x in A; then
        reconsider x as Element of RAT by A0;
        {x} c= RAT by ZFMISC_1:37; then
C1:     {x} in F by A1;
        x in {x} by TARSKI:def 1;
        hence thesis by C1,TARSKI:def 4;
      end;
      let x be set;
      assume x in union F; then
      consider Y being set such that
C1:   x in Y & Y in F by TARSKI:def 4;
      consider a being Element of RAT such that
C2:   {a} = Y by C1,A1;
      x = a by C2,C1,TARSKI:def 1;
      hence thesis by A0;
    end;
    hence thesis by FSigDef;
  end;

begin :: T_1/2 Topological Spaces

definition let T be TopSpace;
  attr T is T_1/2 means :Pol:
    for A being Subset of T holds Der A is closed;
end;

theorem Glen2:
  for T being TopSpace st T is being_T1 holds T is T_1/2
  proof
    let T be TopSpace;
    assume
A1: T is being_T1;
    for A being Subset of T holds Der A is closed
    proof
      let A be Subset of T;
      Der A = Cl Der A by A1,TOPGEN_1:35;
      hence thesis;
    end;
    hence thesis by Pol;
  end;

theorem Glen1:
  for T being non empty TopSpace st T is T_1/2 holds T is T_0
  proof
    let T be non empty TopSpace;
    assume
a1: T is T_1/2;
    now let x, y be Point of T;
      assume
ZX:   x <> y;
      assume
B1:   x in Cl {y} & y in Cl {x};
      not for G being Subset of T st G is open holds
        y in G implies {x} meets G
      proof
        assume
T1:     for G being Subset of T st G is open holds
        y in G implies {x} meets G;
        for U being open Subset of T st y in U
        ex r being Point of T st r in {x} /\ U & y <> r
        proof
          let U be open Subset of T;
          assume y in U; then
          {x} meets U by T1; then
T2:       x in U by ZFMISC_1:56;
          x in {x} by TARSKI:def 1; then
          x in {x} /\ U by T2,XBOOLE_0:def 3;
          hence thesis by ZX;
        end; then
J1:     y in Der {x} by TOPGEN_1:19;
        not x in Der {x}
        proof
          assume
h1:       x in Der {x};
          consider U being a_neighborhood of x;
          consider V being Subset of T such that
H3:       V is open & V c= U & x in V by CONNSP_2:8;
          consider z being Point of T such that
H2:       z in {x} /\ V & x <> z by H3,h1,TOPGEN_1:19;
          z in {x} by H2,XBOOLE_0:def 3;
          hence thesis by H2,TARSKI:def 1;
        end; then
K4:     x in (Der {x})` by SUBSET_1:50;
        Der {x} is closed by a1,Pol; then
K1:     (Der {x})` is open by TOPS_1:29;
        set X = (Der {x})`;
K3:     not y in X by J1,SUBSET_1:54;
        {y} meets X by K1,B1,K4,PRE_TOPC:54;
        hence thesis by K3,ZFMISC_1:56;
      end;
      hence contradiction by B1,PRE_TOPC:54;
    end;
    hence thesis by TSP_1:def 6;
  end;

theorem
  for T being non empty TopSpace,
      p being Point of T holds
    p is_isolated_in [#]T or p is_an_accumulation_point_of [#]T
  proof
    let T be non empty TopSpace,
        p be Point of T;
    p in the carrier of T; then
    p in [#]T by PRE_TOPC:def 3;
    hence thesis by TOPGEN_1:def 4;
  end;

registration
  cluster T_1/2 -> T_0 TopSpace;
  coherence
  proof
    let T be TopSpace;
    per cases;
    suppose T is non empty; then
      reconsider T' = T as non empty TopSpace;
      assume T is T_1/2; then
      T' is T_1/2;
      hence thesis by Glen1;
    end;
    suppose T is empty; then
      reconsider T' = T as empty TopSpace;
      T' is T_0 by TSP_1:def 4;
      hence thesis;
    end;
  end;
  cluster being_T1 -> T_1/2 TopSpace;
  coherence by Glen2;
end;

begin :: Condensation Points
      :: Page 77 - 1.7.11

definition let T, A; let x be Point of T;
  pred x is_a_condensation_point_of A means :ConDef:
    for N being a_neighborhood of x holds
      N /\ A is not countable;
end;

 reserve x for Point of T;

theorem BleBle:
  x is_a_condensation_point_of A & A c= B implies
    x is_a_condensation_point_of B
  proof
    assume that
A1: x is_a_condensation_point_of A and
A2: A c= B;
    for N being a_neighborhood of x holds
      N /\ B is not countable
    proof
      let N be a_neighborhood of x;
A3:   N /\ A is not countable by A1,ConDef;
      N /\ A c= N /\ B by A2,XBOOLE_1:26;
      hence thesis by A3,CARD_4:46;
    end;
    hence thesis by ConDef;
  end;

definition let T, A;
  func A^0 -> Subset of T means :ZeroDef:
    for x being Point of T holds x in it iff
      x is_a_condensation_point_of A;
  existence
  proof
    defpred P[Point of T] means $1 is_a_condensation_point_of A;
    consider X being Subset of T such that
A1: for x being Element of T holds
      x in X iff P[x] from SUBSET_1:sch 3;
    thus thesis by A1;
  end;
  uniqueness
  proof
    defpred P[Point of T] means $1 is_a_condensation_point_of A;
    for A1, A2 being Subset of T st
    (for x being Element of T holds x in A1 iff P[x]) &
    (for x being Element of T holds x in A2 iff P[x]) holds
      A1 = A2 from SUBSET_1:sch 2;
    hence thesis;
  end;
end;

theorem Ble1:
  for p being Point of T st
    p is_a_condensation_point_of A holds
      p is_an_accumulation_point_of A
  proof
    let p be Point of T;
    assume
a2: p is_a_condensation_point_of A;
    per cases;
    suppose
V1:   p in A;
      for U being open Subset of T st p in U
       ex q being Point of T st q <> p & q in A & q in U
      proof
        let U be open Subset of T;
        assume
Z0:     p in U; then
        reconsider N = U as a_neighborhood of p by CONNSP_2:5;
        reconsider NU = N /\ A as non empty non countable set
          by a2,V1,Z0,XBOOLE_0:def 3,ConDef;
        consider q being Element of NU such that
Z1:     p <> q by BORSUK_4:3;
        q in NU; then
        reconsider q as Point of T;
        q <> p & q in A & q in U by Z1,XBOOLE_0:def 3;
        hence thesis;
      end;
      hence thesis by TOPGEN_1:23;
    end;
    suppose
X1:   not p in A;
      for G being Subset of T st G is open holds
        p in G implies A \ {p} meets G
      proof
        let G be Subset of T;
        assume
X2:     G is open;
        assume p in G; then
        G is a_neighborhood of p by X2,CONNSP_2:5; then
x3:     G /\ A is not countable by a2,ConDef;
        A \ {p} = A by X1,ZFMISC_1:65;
        hence thesis by x3,XBOOLE_0:def 7;
      end; then
      p in Cl (A \ {p}) by PRE_TOPC:54;
      hence thesis by TOPGEN_1:def 2;
    end;
  end;

theorem
  A^0 c= Der A
  proof
    let x be set;
    assume
A1: x in A^0; then
    reconsider p = x as Point of T;
    p is_a_condensation_point_of A by A1,ZeroDef; then
    p is_an_accumulation_point_of A by Ble1;
    hence thesis by TOPGEN_1:18;
  end;

theorem
  A^0 = Cl (A^0)
  proof
    thus A^0 c= Cl (A^0) by PRE_TOPC:48;
    let x be set;
    assume
A1: x in Cl (A^0); then
    reconsider p = x as Point of T;
    for N being a_neighborhood of p holds
      N /\ A is not countable
    proof
      let N be a_neighborhood of p;
      consider N1 being Subset of T such that
A2:   N1 is open & N1 c= N & p in N1 by CONNSP_2:8;
      A^0 meets N1 by A1,A2,PRE_TOPC:54; then
      consider y being set such that
A3:   y in A^0 & y in N1 by XBOOLE_0:3;
      reconsider y' = y as Point of T by A3;
AA:   y' is_a_condensation_point_of A by A3,ZeroDef;
      reconsider N1 as a_neighborhood of y' by A3,A2,CONNSP_2:8;
A6:   N1 /\ A is not countable by ConDef,AA;
      N1 /\ A c= N /\ A by A2,XBOOLE_1:26;
      hence thesis by A6,CARD_4:46;
    end; then
    p is_a_condensation_point_of A by ConDef;
    hence thesis by ZeroDef;
  end;

theorem Mono1:
  A c= B implies A^0 c= B^0
  proof
    assume
A1: A c= B;
    let x be set;
    assume
A2: x in A^0; then
    reconsider x' = x as Point of T;
    x' is_a_condensation_point_of A by A2,ZeroDef; then
    x' is_a_condensation_point_of B by A1,BleBle;
    hence thesis by ZeroDef;
  end;

theorem Bla3:
  x is_a_condensation_point_of A \/ B implies
    x is_a_condensation_point_of A or
      x is_a_condensation_point_of B
  proof
    assume
A0: x is_a_condensation_point_of A \/ B;
    assume that
A1: not x is_a_condensation_point_of A and
A2: not x is_a_condensation_point_of B;
    consider N1 being a_neighborhood of x such that
A3: N1 /\ A is countable by A1,ConDef;
    consider N2 being a_neighborhood of x such that
A4: N2 /\ B is countable by A2,ConDef;
    reconsider N3 = N1 /\ N2 as a_neighborhood of x by CONNSP_2:4;
A5: N3 c= N1 by XBOOLE_1:17;
A6: N3 c= N2 by XBOOLE_1:17;
    N3 /\ A c= N1 /\ A by A5,XBOOLE_1:26; then
A7: N3 /\ A is countable by CARD_4:46,A3;
    N3 /\ B c= N2 /\ B by A6,XBOOLE_1:26; then
    N3 /\ B is countable by CARD_4:46,A4; then
    (N3 /\ A) \/ (N3 /\ B) is countable by A7,CARD_4:47; then
    N3 /\ (A \/ B) is countable by XBOOLE_1:23;
    hence thesis by A0,ConDef;
  end;

theorem
  (A \/ B)^0 = (A^0) \/ (B^0)
  proof
    thus (A \/ B)^0 c= (A^0) \/ (B^0)
    proof
      let x be set;
      assume
A1:   x in (A \/ B)^0; then
      reconsider x' = x as Point of T;
      x' is_a_condensation_point_of A \/ B by A1,ZeroDef; then
      x' is_a_condensation_point_of A or
        x' is_a_condensation_point_of B by Bla3; then
      x' in A^0 or x' in B^0 by ZeroDef;
      hence thesis by XBOOLE_0:def 2;
    end;
    A c= A \/ B by XBOOLE_1:7; then
T1: A^0 c= (A \/ B)^0 by Mono1;
    B c= A \/ B by XBOOLE_1:7; then
    B^0 c= (A \/ B)^0 by Mono1;
    hence (A^0) \/ (B^0) c= (A \/ B)^0 by T1,XBOOLE_1:8;
  end;

theorem ThR1:
  A is countable implies
    not ex x being Point of T st x is_a_condensation_point_of A
  proof
    assume
A0: A is countable;
    given x being Point of T such that
A1: x is_a_condensation_point_of A;
    consider N being a_neighborhood of x;
    N /\ A is not countable by A1,ConDef;
    hence thesis by A0,CARD_4:48;
  end;

theorem ThR2:
  A is countable implies
    A^0 = {}
  proof
    assume
A1: A is countable;
    assume A^0 <> {}; then
    consider x being set such that
A2: x in A^0 by XBOOLE_0:def 1;
    reconsider x' = x as Point of T by A2;
    x' is_a_condensation_point_of A by A2,ZeroDef;
    hence thesis by A1,ThR1;
  end;

registration let T;
             let A be countable Subset of T;
  cluster A^0 -> empty;
  coherence by ThR2;
end;

theorem
  T is second-countable implies
    ex B being Basis of T st B is countable
  proof
    assume
A1: T is second-countable;
    consider B being Basis of T;
    consider B1 being Basis of T such that
T1: B1 c= B & Card B1 = weight T by TOPGEN_2:20;
T4: Card B1, B1 are_equipotent by CARD_1:def 5;
    Card B1 c= omega by T1,A1,WAYBEL23:def 6; then
    Card Card B1 <=` Card omega by CARD_1:27; then
    Card B1 is countable by WAYBEL12:2,CARD_4:44; then
    B1 is countable by T4,YELLOW_8:5;
    hence thesis;
  end;

registration
  cluster second-countable non empty TopSpace;
  existence
  proof
    consider T being finite non empty TopSpace;
    take T;
    thus thesis;
  end;
end;

begin :: Borel Families of Subsets

registration let T;
  cluster TotFam T -> non empty all-open-containing compl-closed
            closed_for_countable_unions;
  coherence
  proof
    thus TotFam T is non empty;
    thus TotFam T is all-open-containing
    proof
      let A be Subset of T;
      assume A is open;
      A in bool the carrier of T;
      hence thesis;
    end;
     for A be Subset of T st A in TotFam T holds A` in TotFam T;
    hence TotFam T is compl-closed by PROB_1:def 1;
     for G being countable Subset-Family of T st G c= TotFam T
      holds union G in TotFam T;
    hence TotFam T is closed_for_countable_unions by CounUn;
  end;
end;

theorem LemXS:
  for T being set, A being SetSequence of T holds
    rng A is countable non empty Subset-Family of T
  proof
    let T be set, A be SetSequence of T;
    A.1 in rng A by FUNCT_2:6; then
    reconsider AA = rng A as non empty Subset-Family of T;
A2: Card rng A c= Card dom A by CARD_2:80;
A3: dom A is countable by CARD_4:44,FUNCT_2:def 1;
    AA is countable by A2,A3,WAYBEL12:2;
    hence thesis;
  end;

theorem Th1:
  for T, F being set holds
    F is SigmaField of T iff
      F is sigma_Field_Subset of T
  proof
    let T, F be set;
    hereby assume
A0:   F is SigmaField of T; then
      reconsider F' = F as non empty compl-closed Subset-Family of T;
      for M being N_Sub_set_fam of T st M c= F' holds union M in F'
      proof
        let M be N_Sub_set_fam of T;
        assume
B0:     M c= F';
        consider f being Function of NAT, bool T such that
B1:     rng f = M by SUPINF_2:def 14;
        Union f = (Intersection Complement f)` by SETLIM_1:35; then
B3:     union rng f = (Intersection Complement f)` by CARD_3:def 4;
        for n being Nat holds (Complement f).n in F'
        proof
          let n be Nat;
          set B = (Complement f).n;
d2:       f.n in rng f by FUNCT_2:6;
          B = (f.n)` by PROB_1:def 4;
          hence thesis by d2,B0,B1,PROB_1:def 1;
        end; then
        Intersection Complement f in F' by A0,PROB_1:def 8;
        hence thesis by B1,B3,PROB_1:def 1;
      end;
      hence F is sigma_Field_Subset of T by MEASURE1:def 9;
    end;
    assume
D1: F is sigma_Field_Subset of T;
D2: for A1 being SetSequence of T st
      (for n being Nat holds A1.n in F) holds Intersection A1 in F
    proof
      let A1 be SetSequence of T;
      assume
E1:   for n being Nat holds A1.n in F;
      reconsider M = rng Complement A1 as N_Sub_set_fam of T by LemXS;
      set B = Complement A1;
Z1:   Complement B = A1;
      M c= F
      proof
        let x be set;
        assume x in M; then
        consider a being set such that
H1:     a in dom B & x = B.a by FUNCT_1:def 5;
        reconsider a as Nat by H1,FUNCT_2:def 1;
H3:     A1.a in F by E1;
        x = (A1.a)` by H1,PROB_1:def 4;
        hence thesis by H3,D1,PROB_1:def 1;
      end; then
      union rng Complement A1 in F by D1,MEASURE1:def 9; then
      Union Complement A1 in F by CARD_3:def 4; then
Z2:   (Union Complement A1)` in F by D1,PROB_1:def 1;
      Union Complement A1 = (Intersection A1)` by Z1,SETLIM_1:35;
      hence thesis by Z2;
    end;
    for A being Subset of T st A in F holds A` in F
      by D1,PROB_1:def 1;
    hence thesis by D1,D2,PROB_1:def 8;
  end;

theorem Pipi1:
  for F, G being Subset-Family of T st F is all-open-containing & F c= G
    holds G is all-open-containing
   proof
     let F, G be Subset-Family of T;
     assume that
A1:  F is all-open-containing and
A2:  F c= G;
     for A being Subset of T st A is open holds A in G
     proof
       let A be Subset of T;
       assume A is open; then
       A in F by A1,AllOpen;
       hence thesis by A2;
     end;
     hence thesis by AllOpen;
   end;

theorem
  for F, G being Subset-Family of T st F is all-closed-containing & F c= G
    holds G is all-closed-containing
   proof
     let F, G be Subset-Family of T;
     assume that
A1:  F is all-closed-containing and
A2:  F c= G;
     for A being Subset of T st A is closed holds A in G
     proof
       let A be Subset of T;
       assume A is closed; then
       A in F by A1,AllClosed;
       hence thesis by A2;
     end;
     hence thesis by AllClosed;
   end;

definition let T be 1-sorted;
  mode SigmaField of T is SigmaField of the carrier of T;
  mode sigma_Field_Subset of T is sigma_Field_Subset of the carrier of T;
end;

registration let T be non empty TopSpace;
  cluster compl-closed closed_for_countable_unions closed_for_countable_meets
    all-closed-containing all-open-containing Subset-Family of T;
  existence
  proof
    take TotFam T;
    thus thesis;
  end;
end;

theorem ExCl:
  sigma TotFam T is all-open-containing compl-closed
    closed_for_countable_unions
  proof
    set E = sigma TotFam T;
    TotFam T c= sigma TotFam T by PROB_1:def 14;
    hence E is all-open-containing by Pipi1;
    thus E is compl-closed;
    thus E is closed_for_countable_unions by Th1;
  end;

registration let T;
  cluster sigma TotFam T -> all-open-containing compl-closed
     closed_for_countable_unions;
  coherence by ExCl;
end;

registration let T be non empty 1-sorted;
  cluster sigma_Field_Subset-like compl-closed closed_for_countable_unions
    non empty Subset-Family of T;
  existence
  proof
    take E = sigma TotFam T;
    thus thesis by Th1;
  end;
end;

registration let T be non empty TopSpace;
  cluster -> closed_for_countable_unions SigmaField of T;
  coherence by Th1;
end;

theorem Paulin:
  for T being non empty TopSpace,
      F being Subset-Family of T st
    F is compl-closed closed_for_countable_unions holds
      F is SigmaField of T
  proof
    let T being non empty TopSpace,
        F be Subset-Family of T;
    assume F is compl-closed closed_for_countable_unions; then
    F is sigma_Field_Subset of T by Kachu;
    hence thesis by Th1;
  end;

registration let T be non empty TopSpace;
  cluster all-open-containing SigmaField of T;
  existence
  proof
    take sigma TotFam T;
    thus thesis;
  end;
end;

registration let T be non empty TopSpace;
  cluster Topology_of T -> open all-open-containing;
  coherence
  proof
    set E = Topology_of T;
    thus E is open
    proof
      let A be Subset of T;
      assume A in E;
      hence A is open by OPENLATT:3;
    end;
    E is all-open-containing
    proof
      let A be Subset of T;
      assume A is open;
      hence thesis by OPENLATT:3;
    end;
    hence thesis;
  end;
end;

theorem Lm2:
  for X being Subset-Family of T
   ex Y being all-open-containing compl-closed
          closed_for_countable_unions Subset-Family of T st X c= Y &
     for Z be all-open-containing compl-closed
          closed_for_countable_unions Subset-Family of T st X c= Z holds
        Y c= Z
  proof let X be Subset-Family of T;
    set V = { S where S is Subset-Family of T :
      X c= S & S is all-open-containing compl-closed
          closed_for_countable_unions Subset-Family of T };
    set Y = meet V;
A1: bool the carrier of T in V
    proof
      reconsider X1 = bool the carrier of T as Subset-Family of T;
      set X1 = TotFam T;
      X c= bool the carrier of T; then
      X c= X1; then
      X1 in V;
      hence thesis;
    end;
    then A2: Y c= bool the carrier of T by SETFAM_1:4;
    now let Z be set; assume Z in V;
      then consider S being Subset-Family of T such that
AA:   Z = S & X c= S & S is all-open-containing compl-closed
          closed_for_countable_unions Subset-Family of T;
      Z is Field_Subset of the carrier of T by AA,Paulin;
      hence {} in Z by PROB_1:10;
    end; then
A3: {} in Y by A1,SETFAM_1:def 1;
A4: for BSeq be SetSequence of the carrier of T st
    (for n being Nat holds BSeq.n in Y) holds Intersection BSeq in Y
    proof let BSeq be SetSequence of the carrier of T such that
A5:   for n being Nat holds BSeq.n in Y;
      now let Z be set; assume A6: Z in V;
        then consider S be Subset-Family of T such that
a7:     Z = S & X c= S & S is all-open-containing compl-closed
          closed_for_countable_unions  Subset-Family of T;
A7:     Z = S & X c= S & S is SigmaField of T by a7,Paulin;
        now let n be Nat;
          BSeq.n in Y by A5;
          hence BSeq.n in Z by A6,SETFAM_1:def 1;
        end;
        hence Intersection BSeq in Z by A7,PROB_1:def 8;
      end;
      hence thesis by A1,SETFAM_1:def 1;
    end;
    for E being Subset of T st E in Y holds E` in Y
    proof let E be Subset of T such that A8: E in Y;
      now let Z be set; assume A9: Z in V;
        then A10: E in Z by A8,SETFAM_1:def 1;
        consider S being Subset-Family of T such that
JJ:     Z = S & X c= S & S is all-open-containing compl-closed
          closed_for_countable_unions Subset-Family of T by A9;
        Z = S & X c= S & S is SigmaField of T by JJ,Paulin;
        hence E` in Z by A10,PROB_1:def 8;
      end;
      hence thesis by A1,SETFAM_1:def 1;
    end;
    then reconsider Y as SigmaField of T by A2,A3,A4,PROB_1:32;
    for A being Subset of T st A is open holds A in Y
    proof
      let A be Subset of T;
      assume
K4:   A is open;
      for Y being set holds Y in V implies A in Y
      proof
        let Y be set;
        assume Y in V; then
        consider S being Subset-Family of the carrier of T such that
K3:     Y = S & X c= S & S is all-open-containing compl-closed
          closed_for_countable_unions Subset-Family of T;
        thus thesis by K4,K3,AllOpen;
      end;
      hence thesis by A1,SETFAM_1:def 1;
    end; then
    reconsider Y as all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T by AllOpen;
    take Y;
A11:now let Z be set; assume Z in V;
      then ex S be Subset-Family of T st
        Z = S & X c= S & S is all-open-containing compl-closed
        closed_for_countable_unions Subset-Family of T;
      hence X c= Z;
    end;
    for Z be set st X c= Z & Z is all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T holds Y c= Z
    proof let Z be set;
      assume A12: X c= Z & Z is all-open-containing compl-closed
        closed_for_countable_unions Subset-Family of T;
      then reconsider Z as Subset-Family of T;
      Z in V by A12;
      hence thesis by SETFAM_1:4;
    end;
    hence thesis by A1,A11,SETFAM_1:6;
  end;

definition let T;
  func BorelSets T -> all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T means :BorSets:
    for G being all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T holds
      it c= G;
  existence
  proof
    reconsider E = {} as Subset-Family of T by Th01;
    consider Y being all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T such that
A0: E c= Y &
    for Z be all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T st E c= Z holds
      Y c= Z by Lm2;
    take Y;
    let G be all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T;
    E c= G by XBOOLE_1:2;
    hence thesis by A0;
  end;
  uniqueness
  proof
    let R1,R2 be all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T such that
A1: for G being all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T holds R1 c= G and
A2: for G being all-open-containing compl-closed
      closed_for_countable_unions Subset-Family of T holds R2 c= G;
A3: R1 c= R2 by A1;
    R2 c= R1 by A2;
    hence thesis by A3,XBOOLE_0:def 10;
  end;
end;

theorem XX1:
  for F being closed Subset-Family of T holds
    F c= BorelSets T
  proof
    let F be closed Subset-Family of T;
    F c= BorelSets T
    proof
      let x be set;
      assume
A1:   x in F; then
      reconsider xx = x as Subset of T;
      xx is closed by A1,TOPS_2:def 2;
      hence thesis by AllClosed;
    end;
    hence thesis;
  end;

theorem XX2:
  for F being open Subset-Family of T holds
    F c= BorelSets T
  proof
    let F be open Subset-Family of T;
    F c= BorelSets T
    proof
      let x be set;
      assume
A1:   x in F; then
      reconsider xx = x as Subset of T;
      xx is open by A1,TOPS_2:def 1;
      hence thesis by AllOpen;
    end;
    hence thesis;
  end;

theorem
  BorelSets T = sigma Topology_of T
  proof
    set BT = BorelSets T;
    reconsider BT = BorelSets T as SigmaField of T by Paulin;
    set X = Topology_of T;
A1: X c= BT by XX2;
    for Z being set st X c= Z & Z is SigmaField of T holds BT c= Z
    proof
      let Z be set;
      assume
A2:   X c= Z & Z is SigmaField of T; then
      reconsider Z' = Z as sigma_Field_Subset of T by Th1;
      Z' is all-open-containing by A2,Pipi1;
      hence thesis by BorSets;
    end;
    hence thesis by A1,PROB_1:def 14;
  end;

definition let T, A;
  attr A is Borel means :SetDef:
    A in BorelSets T;
end;

registration let T;
  cluster F_sigma -> Borel Subset of T;
  coherence
  proof
    let A be Subset of T;
    assume A is F_sigma; then
    consider F being closed countable Subset-Family of T such that
A1: A = union F by FSigDef;
    F c= BorelSets T by XX1; then
    A in BorelSets T by A1,CounUn;
    hence thesis by SetDef;
  end;
end;

registration let T;
  cluster G_delta -> Borel Subset of T;
  coherence
  proof
    let A be Subset of T;
    assume A is G_delta; then
    consider F being open countable Subset-Family of T such that
A1: A = meet F by GDelDef;
    F c= BorelSets T by XX2; then
    A in BorelSets T by A1,CounMet;
    hence thesis by SetDef;
  end;
end;
