:: Semigroup operations on finite subsets
::  by Czes{\l}aw Byli\'nski
::
:: Received May 4, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, FINSUB_1, BINOP_1, FUNCT_1, NAT_1,
      FINSEQ_1, FINSEQ_2, ARYTM_3, XXREAL_0, SETWISEO, TARSKI, RELAT_1,
      FINSEQOP, FUNCOP_1, FUNCT_4, ORDINAL4, FINSOP_1, CARD_1, SETWOP_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, NAT_1, RELAT_1,
      RELSET_1, FUNCT_1, FINSEQ_1, FINSUB_1, PARTFUN1, FUNCT_2, BINOP_1,
      FUNCOP_1, SETWISEO, FINSEQ_2, FINSEQOP, FINSOP_1, XXREAL_0;
 constructors PARTFUN1, WELLORD2, BINOP_1, SETWISEO, XXREAL_0, XREAL_0, NAT_1,
      FINSEQOP, FINSOP_1, RELSET_1;
 registrations XBOOLE_0, FUNCT_1, ORDINAL1, RELSET_1, FUNCT_2, FINSUB_1,
      XREAL_0, FINSEQ_1, FINSEQ_2, NAT_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions BINOP_1, XBOOLE_0, RELAT_1;
 theorems TARSKI, ENUMSET1, ZFMISC_1, NAT_1, FUNCT_1, FUNCT_2, FUNCOP_1,
      FINSEQ_1, FINSUB_1, BINOP_1, SETWISEO, FINSEQ_2, WELLORD2, CARD_1,
      FINSEQOP, RELAT_1, FUNCT_4, FINSOP_1, XBOOLE_0, XBOOLE_1, XREAL_1,
      ORDINAL1;
 schemes NAT_1, SETWISEO, FINSEQ_2;

begin

reserve x,y for set;
reserve C,C',D,E for non empty set;
reserve c,c',c1,c2,c3 for Element of C;
reserve B,B',B1,B2 for Element of Fin C;
reserve A for Element of Fin C';
reserve d,d1,d2,d3,d4,e for Element of D;
reserve F,G for BinOp of D;
reserve u for UnOp of D;
reserve f,f' for Function of C,D;
reserve g for Function of C',D;
reserve H for BinOp of E;
reserve h for Function of D,E;
reserve i,j for Nat;
reserve s for Function;
reserve p,q for FinSequence of D;
reserve T1,T2 for Element of i-tuples_on D;

Lm1: for i be Nat holds Seg i is Element of Fin NAT by FINSUB_1:def 5;

Lm2: now
  let i;
  assume
  i+1 in Seg i;
  then i+1 <= i by FINSEQ_1:3;
  hence contradiction by XREAL_1:31;
end;

canceled 2;

theorem Th3:
  F is commutative associative & c1 <> c2 implies F $$ ({.c1,c2.},f
  ) = F.(f.c1, f.c2)
proof
  assume that
A1: F is commutative associative and
A2: c1 <> c2;
  consider g being Function of Fin C, D such that
A3: F $$ ({.c1,c2.},f) = g.{c1,c2} and
  for e st e is_a_unity_wrt F holds g.{} = e and
A4: for c holds g.{c} = f.c and
A5: for B st B c= { c1,c2 } & B <> {} for c st c in { c1,c2 } \ B holds
  g.(B \/ {c}) = F.(g.B,f.c) by A1,SETWISEO:def 3;
  c1 in {c1} & not c2 in {c1} by A2,TARSKI:def 1;
  then {c1,c2} \ {c1} = {c2} by ZFMISC_1:70;
  then
A6: c2 in {c1,c2} \ {c1} by TARSKI:def 1;
  thus F $$ ({.c1,c2.},f) = g.({.c1.} \/ {.c2.}) by A3,ENUMSET1:41
    .= F.(g.{c1}, f.c2) by A5,A6,ZFMISC_1:12
    .= F.(f.c1, f.c2) by A4;
end;

theorem Th4:
  F is commutative associative & (B <> {} or F is having_a_unity) &
  not c in B implies F $$(B \/ {.c.}, f) = F.(F $$(B,f),f.c)
proof
  assume that
A1: F is commutative associative and
A2: B <> {} or F is having_a_unity and
A3: not c in B;
  per cases;
  suppose
A4: B = {};
    then B = {}.C;
    then F $$(B,f) = the_unity_wrt F by A1,A2,SETWISEO:40;
    hence F.(F $$(B,f),f.c) = f.c by A2,A4,SETWISEO:23
      .= F $$(B \/ {.c.}, f) by A1,A4,SETWISEO:26;
  end;
  suppose
A5: B <> {};
    consider g' being Function of Fin C, D such that
A6: F $$ (B,f) = g'.B and
    for e st e is_a_unity_wrt F holds g'.{} = e and
A7: for c' holds g'.{c'} = f.c' and
A8: for B' st B' c= B & B' <> {} for c' st c' in B \ B' holds g'.(B'
    \/ {c'}) = F.(g'.B',f.c') by A1,A2,SETWISEO:def 3;
    consider g being Function of Fin C, D such that
A9: F $$ (B \/ {.c.},f) = g.(B \/ {c}) and
    for e st e is_a_unity_wrt F holds g.{} = e and
A10: for c' holds g.{c'} = f.c' and
A11: for B' st B' c= B \/ {c} & B' <> {} for c' st c' in (B \/ {c}) \
    B' holds g.(B' \/ {c'}) = F.(g.B',f.c') by A1,SETWISEO:def 3;
    defpred X[set] means $1 <> {} & $1 c= B implies g.($1) = g'.($1);
A12: for B' being Element of Fin C, b being Element of C holds X[B'] & not
    b in B' implies X[B' \/ {b}]
    proof
A13:  B c= B \/ {c} by XBOOLE_1:7;
      let B', c' such that
A14:  B' <> {} & B' c= B implies g.B' = g'.B' and
A15:  not c' in B' and
      B' \/ {c'} <> {} and
A16:  B' \/ {c'} c= B;
A17:  c' in B by A16,SETWISEO:8;
      then
A18:  c' in B \ B' by A15,XBOOLE_0:def 5;
      c' in (B \/ {c}) by A17,XBOOLE_0:def 3;
      then
A19:  c' in (B \/ {c}) \ B' by A15,XBOOLE_0:def 5;
      B' c= B by A16,XBOOLE_1:11;
      then
A20:  B' c= B \/ {c} by A13,XBOOLE_1:1;
      per cases;
      suppose
A21:    B' = {};
        then g.(B' \/ {c'}) = f.c' by A10;
        hence thesis by A7,A21;
      end;
      suppose
A22:    B' <> {};
        hence g.(B' \/ {c'}) = F.(g'.B', f.c') by A11,A14,A16,A20,A19,
        XBOOLE_1:11
          .= g'.(B' \/ {c'}) by A8,A16,A18,A22,XBOOLE_1:11;
      end;
    end;
A23: X[{}.C];
A24: for B' holds X[B'] from SETWISEO:sch 2(A23,A12);
    c in B \/ {c} by SETWISEO:6;
    then c in (B \/ {c}) \ B by A3,XBOOLE_0:def 5;
    hence F $$(B \/ {.c.}, f) = F.(g.B, f.c) by A5,A9,A11,XBOOLE_1:7
      .= F.(F $$(B,f),f.c) by A5,A6,A24;
  end;
end;

theorem
  F is commutative associative & c1 <> c2 & c1 <> c3 & c2 <> c3 implies
  F $$ ({.c1,c2,c3.},f) = F.(F.(f.c1, f.c2),f.c3)
proof
  assume that
A1: F is commutative associative and
A2: c1 <> c2;
  assume
  c1 <> c3 & c2 <> c3;
  then
A3: not c3 in {c1,c2} by TARSKI:def 2;
  thus F $$ ({.c1,c2,c3.},f) = F $$ ({.c1,c2.} \/ {.c3.},f) by ENUMSET1:43
    .= F.(F $$ ({.c1,c2.},f),f.c3) by A1,A3,Th4
    .= F.(F.(f.c1, f.c2),f.c3) by A1,A2,Th3;
end;

theorem
  F is commutative associative & (B1 <> {} & B2 <> {} or F is
having_a_unity) & B1 misses B2 implies F $$(B1 \/ B2, f) = F.(F $$(B1,f),F $$(
  B2,f))
proof
  assume that
A1: F is commutative associative and
A2: B1 <> {} & B2 <> {} or F is having_a_unity and
A3: B1 /\ B2 = {};
  now
    per cases;
    suppose
A4:   B1 = {} or B2 = {};
      now
        per cases by A4;
        suppose
A5:       B1 = {};
          hence F $$(B1 \/ B2, f) = F.(the_unity_wrt F,F $$(B2,f)) by A2,
          SETWISEO:23
            .= F.(F $$({}.C,f),F $$(B2,f)) by A1,A2,A4,SETWISEO:40
            .= F.(F $$(B1,f),F $$(B2,f)) by A5;
        end;
        suppose
A6:       B2 = {};
          hence F $$(B1 \/ B2, f) = F.(F $$(B1,f),the_unity_wrt F) by A2,
          SETWISEO:23
            .= F.(F $$(B1,f),F $$({}.C,f)) by A1,A2,A4,SETWISEO:40
            .= F.(F $$(B1,f),F $$(B2,f)) by A6;
        end;
      end;
      hence thesis;
    end;
    suppose
A7:   B1 <> {} & B2 <> {};
      defpred X[Element of Fin C] means $1 <> {} & B1 /\ $1 = {} implies F $$(
      B1 \/ $1,f) = F.(F $$(B1,f),F $$($1,f));
A8:   for B' being Element of Fin C, b being Element of C holds X[B'] &
      not b in B' implies X[B' \/ {.b.}]
      proof
        let B,c such that
A9:     B <> {} & B1 /\ B = {} implies F $$(B1 \/ B,f) = F.(F $$ (B1,
        f),F $$(B,f)) and
A10:    not c in B and
        B \/ {c} <> {};
        assume
A11:    B1 /\ (B \/ {c}) = {};
        then
A12:    B1 misses (B \/ {c}) by XBOOLE_0:def 7;
        c in B \/ {c} by SETWISEO:6;
        then
A13:    not c in B1 by A11,XBOOLE_0:def 4;
A14:    B <> {} & B1 misses B implies F $$(B1 \/ B,f) = F.(F $$ (B1,f),F
        $$(B,f)) by A9,XBOOLE_0:def 7;
        now
          per cases;
          suppose
A15:        B = {};
            hence
            F $$ (B1 \/ (B \/ {.c.}),f) = F.(F $$ (B1,f), f.c) by A1,A7,
            A13,Th4
              .= F.(F $$ (B1,f),F $$(B \/ {.c.},f)) by A1,A15,SETWISEO:26;
          end;
          suppose
A16:        B <> {};
A17:        not c in B1 \/ B by A10,A13,XBOOLE_0:def 3;
            thus F $$ (B1 \/ (B \/ {.c.}),f) = F $$ (B1 \/ B \/ {.c.},f) by
            XBOOLE_1:4
              .= F.(F.(F $$ (B1,f),F $$(B,f)), f.c) by A1,A14,A12,A16,A17,
            Th4,XBOOLE_1:7,63
              .= F.(F $$ (B1,f),F.(F $$(B,f), f.c)) by A1,BINOP_1:def 3
              .= F.(F $$(B1,f),F $$(B \/ {.c.},f)) by A1,A10,A16,Th4;
          end;
        end;
        hence thesis;
      end;
A18:  X[{}.C];
      for B2 holds X[B2] from SETWISEO:sch 2(A18,A8);
      hence thesis by A3,A7;
    end;
  end;
  hence thesis;
end;

theorem Th7:
  F is commutative associative & (A <> {} or F is having_a_unity) &
(ex s st dom s = A & rng s = B & s is one-to-one & g|A = f*s) implies F $$(A,g)
  = F $$(B,f)
proof
  defpred X[Element of Fin C'] means $1 <> {} or F is having_a_unity implies
for B st ex s st dom s = $1 & rng s = B & s is one-to-one & g|$1 = f*s holds F
  $$($1,g) = F $$(B,f);
  assume
A1: F is commutative associative;
A2: for B' being Element of Fin C', b being Element of C' holds X[B'] & not
  b in B' implies X[B' \/ {.b.}]
  proof
    let A' be Element of Fin C',a be Element of C' such that
A3: A' <> {} or F is having_a_unity implies for B st ex s st dom s =
    A' & rng s = B & s is one-to-one & g|A' = f*s holds F $$(A',g) = F $$(B,f)
    and
A4: not a in A';
    assume
    A' \/ {a} <> {} or F is having_a_unity;
    let B;
    set A = A' \/ {.a.};
    given s such that
A5: dom s = A and
A6: rng s = B and
A7: s is one-to-one and
A8: g|A = f*s;
A9: a in A by SETWISEO:6;
    then
A10: s.a in B by A5,A6,FUNCT_1:def 5;
    B c= C by FINSUB_1:def 5;
    then reconsider c = s.a as Element of C by A10;
    set B' = B \ {.c.};
    set s' = s|A';
A11: s' is one-to-one by A7,FUNCT_1:84;
    now
      let y;
      thus y in rng s' implies y in B'
      proof
        assume
        y in rng s';
        then consider x such that
A12:    x in dom s' and
A13:    y = s'.x by FUNCT_1:def 5;
A14:    s'.x = s.x by A12,FUNCT_1:70;
A15:    x in dom s /\ A' by A12,RELAT_1:90;
        then x in dom s & x <> a by A4,XBOOLE_0:def 4;
        then s.x <> c by A5,A7,A9,FUNCT_1:def 8;
        then
A16:    not y in {c} by A13,A14,TARSKI:def 1;
        x in dom s by A15,XBOOLE_0:def 4;
        then y in B by A6,A13,A14,FUNCT_1:def 5;
        hence thesis by A16,XBOOLE_0:def 5;
      end;
      assume
A17:  y in B';
      then y in B by XBOOLE_0:def 5;
      then consider x such that
A18:  x in dom s and
A19:  y = s.x by A6,FUNCT_1:def 5;
A20:  x in A' or x in {a} by A5,A18,XBOOLE_0:def 3;
      not y in {c} by A17,XBOOLE_0:def 5;
      then x <> a by A19,TARSKI:def 1;
      then x in dom s /\ A' by A18,A20,TARSKI:def 1,XBOOLE_0:def 4;
      then x in dom s' & s'.x = s.x by FUNCT_1:71,RELAT_1:90;
      hence y in rng s' by A19,FUNCT_1:def 5;
    end;
    then
A21: rng s' = B' by TARSKI:2;
    now
      let x;
      thus x in dom(g|A') implies x in dom(f*s')
      proof
        assume
        x in dom(g|A');
        then
A22:    x in dom g /\ A' by RELAT_1:90;
        then
A23:    x in A' by XBOOLE_0:def 4;
A24:    x in dom g by A22,XBOOLE_0:def 4;
        x in A by A23,SETWISEO:6;
        then x in dom g /\ A by A24,XBOOLE_0:def 4;
        then
A25:    x in dom(f*s) by A8,RELAT_1:90;
        then
A26:    s.x in dom f by FUNCT_1:21;
        x in dom s by A25,FUNCT_1:21;
        then x in dom s /\ A' by A23,XBOOLE_0:def 4;
        then
A27:    x in dom s' by RELAT_1:90;
        then s'.x = s.x by FUNCT_1:70;
        hence thesis by A26,A27,FUNCT_1:21;
      end;
      assume
A28:  x in dom(f*s');
      then
A29:  x in dom s' by FUNCT_1:21;
      then
A30:  x in dom s /\ A' by RELAT_1:90;
      then
A31:  x in dom s by XBOOLE_0:def 4;
      s'.x in dom f by A28,FUNCT_1:21;
      then s.x in dom f by A29,FUNCT_1:70;
      then x in dom(g|A) by A8,A31,FUNCT_1:21;
      then x in dom g /\ A by RELAT_1:90;
      then
A32:  x in dom g by XBOOLE_0:def 4;
      x in A' by A30,XBOOLE_0:def 4;
      then x in dom g /\ A' by A32,XBOOLE_0:def 4;
      hence x in dom(g|A') by RELAT_1:90;
    end;
    then
A33: dom(g|A') = dom(f*s') by TARSKI:2;
    a in C';
    then a in dom g by FUNCT_2:def 1;
    then a in dom g /\ A by A9,XBOOLE_0:def 4;
    then a in dom(f*s) & g.a = (f*s).a by A8,FUNCT_1:71,RELAT_1:90;
    then
A34: g.a = f.c by FUNCT_1:22;
    B' \/ {c} = B \/ {c} by XBOOLE_1:39;
    then
A35: B = B' \/ {c} by A10,ZFMISC_1:46;
A36: dom s' = A' by A5,RELAT_1:91,XBOOLE_1:7;
A37: for x st x in dom(g|A') holds (g|A').x = (f*s').x
    proof
      let x;
      assume
      x in dom(g|A');
      then
A38:  x in dom g /\ A' by RELAT_1:90;
      then
A39:  x in A' by XBOOLE_0:def 4;
      then
A40:  x in A by SETWISEO:6;
      x in dom g by A38,XBOOLE_0:def 4;
      then x in dom g /\ A by A40,XBOOLE_0:def 4;
      then x in dom(f*s) by A8,RELAT_1:90;
      then
A41:  x in dom s by FUNCT_1:21;
      then x in dom s /\ A' by A39,XBOOLE_0:def 4;
      then
A42:  x in dom s' by RELAT_1:90;
      then
A43:  s'.x = s.x by FUNCT_1:70;
      thus (g|A').x = g.x by A39,FUNCT_1:72
        .= (f*s).x by A8,A40,FUNCT_1:72
        .= f.(s.x) by A41,FUNCT_1:23
        .= (f*s').x by A42,A43,FUNCT_1:23;
    end;
    then
A44: g|A' = f*s' by A33,FUNCT_1:9;
    now
      let y;
      thus y in g.:A' implies y in f.:B'
      proof
        assume
        y in g.:A';
        then consider x such that
A45:    x in dom g and
A46:    x in A' and
A47:    y = g.x by FUNCT_1:def 12;
        x in dom g /\ A' by A45,A46,XBOOLE_0:def 4;
        then
A48:    x in dom(g|A') by RELAT_1:90;
        then x in dom s' by A33,FUNCT_1:21;
        then
A49:    s'.x in B' by A21,FUNCT_1:def 5;
        y = (f*s').x by A44,A46,A47,FUNCT_1:72;
        then
A50:    y = f.(s'.x) by A33,A48,FUNCT_1:22;
        s'.x in dom f by A33,A48,FUNCT_1:21;
        hence thesis by A50,A49,FUNCT_1:def 12;
      end;
      assume
      y in f.:B';
      then consider x such that
      x in dom f and
A51:  x in B' and
A52:  y = f.x by FUNCT_1:def 12;
      set x' = s'".x;
A53:  x' in A' by A11,A36,A21,A51,FUNCT_1:54;
      A' c= C' by FINSUB_1:def 5;
      then x' in C' by A53;
      then
A54:  x' in dom g by FUNCT_2:def 1;
      s'.x' = x by A11,A21,A51,FUNCT_1:57;
      then y = (f*s').x' by A36,A52,A53,FUNCT_1:23
        .= g.x' by A44,A53,FUNCT_1:72;
      hence y in g.:A' by A53,A54,FUNCT_1:def 12;
    end;
    then
A55: f.:B' = g.:A' by TARSKI:2;
A56: not c in B' by ZFMISC_1:64;
    now
      per cases;
      suppose
A57:    A' = {};
        B' c= C by FINSUB_1:def 5;
        then B' c= dom f by FUNCT_2:def 1;
        then
A58:    B' = {} by A55,A57,RELAT_1:149;
        thus F $$(A,g) = f.c by A1,A34,A57,SETWISEO:26
          .= F $$(B,f) by A1,A35,A58,SETWISEO:26;
      end;
      suppose
A59:    A' <> {};
        A' c= C' by FINSUB_1:def 5;
        then A' c= dom g by FUNCT_2:def 1;
        then
A60:    B' <> {} by A55,A59,RELAT_1:149;
        thus F $$(A,g) = F.(F $$(A',g),g.a) by A1,A4,A59,Th4
          .= F.(F $$(B',f),f.c) by A3,A34,A11,A36,A21,A33,A37,A59,FUNCT_1:
        9
          .= F $$(B,f) by A1,A35,A56,A60,Th4;
      end;
    end;
    hence thesis;
  end;
A61: X[{}.C']
  proof
    assume
A62: {}.C' <> {} or F is having_a_unity;
    let B;
    given s such that
A63: dom s = {}.C' & rng s = B & s is one-to-one and
    g|({}.C') = f*s;
    B,{} are_equipotent by A63,WELLORD2:def 4;
    then
A64: B = {}.C by CARD_1:46;
    F $$({}.C',g) = the_unity_wrt F by A1,A62,SETWISEO:40;
    hence thesis by A1,A62,A64,SETWISEO:40;
  end;
  for A holds X[A] from SETWISEO:sch 2(A61,A2);
  hence thesis;
end;

theorem
  H is commutative associative & (B <> {} or H is having_a_unity) & f is
  one-to-one implies H $$(f.:B,h) = H $$(B,h*f)
proof
  assume that
A1: H is commutative associative &( B <> {} or H is having_a_unity) and
A2: f is one-to-one;
  set s = f|B;
A3: rng s = f.:B & (h*f)|B = h*s by RELAT_1:112,148;
  B c= C by FINSUB_1:def 5;
  then B c= dom f by FUNCT_2:def 1;
  then
A4: dom s = B by RELAT_1:91;
  s is one-to-one by A2,FUNCT_1:84;
  hence thesis by A1,A4,A3,Th7;
end;

theorem
  F is commutative associative & (B <> {} or F is having_a_unity) & f|B
  = f'|B implies F $$(B,f) = F $$(B,f')
proof
  assume
A1: F is commutative associative &( B <> {} or F is having_a_unity);
  set s = id B;
A2: dom s = B & rng s = B by RELAT_1:71;
  assume
  f|B = f'|B;
  then f|B = f'*s by RELAT_1:94;
  hence thesis by A1,A2,Th7;
end;

theorem
  F is commutative associative & F is having_a_unity & e = the_unity_wrt
  F & f.:B = {e} implies F$$(B,f) = e
proof
  assume that
A1: F is commutative associative and
A2: F is having_a_unity and
A3: e = the_unity_wrt F;
  defpred X[Element of Fin C] means f.:($1) = {e} implies F$$($1,f) = e;
A4: for B' being Element of Fin C, b being Element of C holds X[B'] & not b
  in B' implies X[B' \/ {.b.}]
  proof
    let B',c such that
A5: f.:(B') = {e} implies F$$(B',f) = e and
A6: not c in B' and
A7: f.:(B' \/ {c}) = {e};
A8: now
      per cases;
      suppose
        B' = {};
        then
A9:     B' = {}.C;
        thus F$$(B' \/ {.c.},f) = F.(F $$(B',f),f.c) by A1,A2,A6,Th4
          .= F.(e,f.c) by A1,A2,A3,A9,SETWISEO:40;
      end;
      suppose
A10:    B' <> {};
        B' c= C by FINSUB_1:def 5;
        then
A11:    B' c= dom f by FUNCT_2:def 1;
        f.:B' c= {e} by A7,RELAT_1:156,XBOOLE_1:7;
        hence F$$(B' \/ {.c.},f) = F.(e,f.c) by A1,A5,A6,A10,A11,Th4,
        ZFMISC_1:39;
      end;
    end;
    {.c.} c= C by FINSUB_1:def 5;
    then
A12: {c} c= dom f by FUNCT_2:def 1;
    then
A13: c in dom f by ZFMISC_1:37;
    Im(f,c) c= {e} by A7,RELAT_1:156,XBOOLE_1:7;
    then Im(f,c) = {e} by A12,ZFMISC_1:39;
    then {e} = {f.c} by A13,FUNCT_1:117;
    then f.c = e by ZFMISC_1:6;
    hence thesis by A2,A3,A8,SETWISEO:23;
  end;
A14: X[{}.C] by RELAT_1:149;
  for B holds X[B] from SETWISEO:sch 2(A14,A4);
  hence thesis;
end;

theorem Th11:
  F is commutative associative & F is having_a_unity & e =
the_unity_wrt F & G.(e,e) = e & (for d1,d2,d3,d4 holds F.(G.(d1,d2),G.(d3,d4))=
  G.(F.(d1,d3),F.(d2,d4))) implies G.(F$$(B,f),F$$(B,f')) = F $$(B,G.:(f,f'))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: e = the_unity_wrt F and
A3: G.(e,e) = e and
A4: for d1,d2,d3,d4 holds F.(G.(d1,d2),G.(d3,d4))= G.(F.(d1,d3),F.(d2,d4
  ));
  defpred X[Element of Fin C] means G.(F$$($1,f),F$$($1,f')) = F $$($1,G.:(f,
  f'));
A5: for B' being Element of Fin C, b being Element of C holds X[B'] & not b
  in B' implies X[B' \/ {.b.}]
  proof
    let B,c such that
A6: G.(F$$(B,f),F$$(B,f')) = F $$(B,G.:(f,f')) and
A7: not c in B;
    set s' = F$$(B,f');
    set s = F$$(B,f);
    F$$(B \/ {.c.},f) = F.(s,f.c) & F$$(B \/ {.c.},f') = F.(s',f'.c) by
    A1,A7, Th4;
    hence G.(F$$(B \/ {.c.},f),F$$(B \/ {.c.},f')) = F.(G.(s,s'),G.(f.c,f'.c))
    by A4
      .= F.(G.(s,s'),G.:(f,f').c) by FUNCOP_1:48
      .= F $$(B \/ {.c.},G.:(f,f')) by A1,A6,A7,Th4;
  end;
  F$$({}.C,f) = e & F$$({}.C,f') = e by A1,A2,SETWISEO:40;
  then
A8: X[{}.C] by A1,A2,A3,SETWISEO:40;
  for B holds X[B] from SETWISEO:sch 2(A8,A5);
  hence thesis;
end;

Lm3: F is commutative associative implies for d1,d2,d3,d4 holds F.(F.(d1,d2),F
.(d3,d4))= F.(F.(d1,d3),F.(d2,d4))

proof
  assume that
A1: F is commutative and
A2: F is associative;
  let d1,d2,d3,d4;
  thus F.(F.(d1,d2),F.(d3,d4)) = F.(d1,F.(d2,F.(d3,d4))) by A2,BINOP_1:def 3
    .= F.(d1,F.(F.(d2,d3),d4)) by A2,BINOP_1:def 3
    .= F.(d1,F.(F.(d3,d2),d4)) by A1,BINOP_1:def 2
    .= F.(d1,F.(d3,F.(d2,d4))) by A2,BINOP_1:def 3
    .= F.(F.(d1,d3),F.(d2,d4)) by A2,BINOP_1:def 3;
end;

theorem
  F is commutative associative & F is having_a_unity implies F.(F$$(B,f)
  ,F$$(B,f')) = F $$(B,F.:(f,f'))
proof
  set e = the_unity_wrt F;
  assume
A1: F is commutative & F is associative & F is having_a_unity;
  then
  F.(e,e) = e & for d1,d2,d3,d4 holds F.(F.(d1,d2),F.(d3,d4))= F.(F.(d1,d3
  ),F. (d2,d4)) by Lm3,SETWISEO:23;
  hence thesis by A1,Th11;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
having_an_inverseOp & G = F*(id D,the_inverseOp_wrt F) implies G.(F$$(B,f),F$$(
  B,f')) = F $$(B,G.:(f,f'))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: F is having_an_inverseOp & G = F*(id D,the_inverseOp_wrt F);
  set e = the_unity_wrt F;
  G.(e,e) = e & for d1,d2,d3,d4 holds F.(G.(d1,d2),G.(d3,d4))= G.(F.(d1,d3
  ),F. (d2,d4)) by A1,A2,FINSEQOP:91,94;
  hence thesis by A1,Th11;
end;

theorem Th14:
  F is commutative associative & F is having_a_unity & e =
the_unity_wrt F & G is_distributive_wrt F & G.(d,e) = e implies G.(d,F$$(B,f))
  = F $$(B,G[;](d,f))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: e = the_unity_wrt F and
A3: G is_distributive_wrt F;
  defpred X[Element of Fin C] means G.(d,F$$($1,f)) = F $$($1,G[;](d,f));
A4: for B' being Element of Fin C, b being Element of C holds X[B'] & not b
  in B' implies X[B' \/ {.b.}]
  proof
    let B',c such that
A5: G.(d,F$$(B',f)) = F $$(B',G[;](d,f)) and
A6: not c in B';
    thus G.(d,F$$(B' \/ {.c.},f)) = G.(d,F.(F$$(B',f),f.c)) by A1,A6,Th4
      .= F.(G.(d,F$$(B',f)),G.(d,f.c)) by A3,BINOP_1:23
      .= F.(F $$(B',G[;](d,f)),(G[;](d,f)).c) by A5,FUNCOP_1:66
      .= F $$(B' \/ {.c.},G[;](d,f)) by A1,A6,Th4;
  end;
  assume
  G.(d,e) = e;
  then G.(d,F$$({}.C,f)) = e by A1,A2,SETWISEO:40
    .= F $$({}.C,G[;](d,f)) by A1,A2,SETWISEO:40;
  then
A7: X[{}.C];
  for B holds X[B] from SETWISEO:sch 2(A7,A4);
  hence thesis;
end;

theorem Th15:
  F is commutative associative & F is having_a_unity & e =
the_unity_wrt F & G is_distributive_wrt F & G.(e,d) = e implies G.(F$$(B,f),d)
  = F $$(B,G[:](f,d))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: e = the_unity_wrt F and
A3: G is_distributive_wrt F;
  defpred X[Element of Fin C] means G.(F$$($1,f),d) = F $$($1,G[:](f,d));
A4: for B' being (Element of Fin C), b being Element of C holds X[B'] & not
  b in B' implies X[B' \/ {.b.}]
  proof
    let B',c such that
A5: G.(F$$(B',f),d) = F $$(B',G[:](f,d)) and
A6: not c in B';
    thus G.(F$$(B' \/ {.c.},f),d) = G.(F.(F$$(B',f),f.c),d) by A1,A6,Th4
      .= F.(G.(F$$(B',f),d),G.(f.c,d)) by A3,BINOP_1:23
      .= F.(F $$(B',G[:](f,d)),(G[:](f,d)).c) by A5,FUNCOP_1:60
      .= F $$(B' \/ {.c.},G[:](f,d)) by A1,A6,Th4;
  end;
  assume
  G.(e,d) = e;
  then G.(F$$({}.C,f),d) = e by A1,A2,SETWISEO:40
    .= F $$({}.C,G[:](f,d)) by A1,A2,SETWISEO:40;
  then
A7: X[{}.C];
  for B holds X[B] from SETWISEO:sch 2(A7,A4);
  hence thesis;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
having_an_inverseOp & G is_distributive_wrt F implies G.(d,F$$(B,f)) = F $$(B,G
  [;](d,f))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: F is having_an_inverseOp and
A3: G is_distributive_wrt F;
  set e = the_unity_wrt F;
  G.(d,e) = e by A1,A2,A3,FINSEQOP:70;
  hence thesis by A1,A3,Th14;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
having_an_inverseOp & G is_distributive_wrt F implies G.(F$$(B,f),d) = F $$(B,G
  [:](f,d))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: F is having_an_inverseOp and
A3: G is_distributive_wrt F;
  set e = the_unity_wrt F;
  G.(e,d) = e by A1,A2,A3,FINSEQOP:70;
  hence thesis by A1,A3,Th15;
end;

theorem Th18:
  F is commutative associative & F is having_a_unity & H is
  commutative associative & H is having_a_unity & h.the_unity_wrt F =
the_unity_wrt H & (for d1,d2 holds h.(F.(d1,d2)) = H.(h.d1,h.d2)) implies h.(F
  $$(B,f)) = H $$(B,h*f)
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: H is commutative associative & H is having_a_unity and
A3: h.the_unity_wrt F = the_unity_wrt H and
A4: for d1,d2 holds h.(F.(d1,d2)) = H.(h.d1,h.d2);
  defpred X[Element of Fin C] means h.(F$$($1,f)) = H $$($1,h*f);
A5: for B' being (Element of Fin C), b being Element of C holds X[B'] & not
  b in B' implies X[B' \/ {.b.}]
  proof
    let B,c such that
A6: h.(F$$(B,f)) = H $$(B,h*f) and
A7: not c in B;
    thus h.(F$$(B \/ {.c.},f)) = h.(F.(F$$(B,f),f.c)) by A1,A7,Th4
      .= H.(H $$(B,h*f),h.(f.c)) by A4,A6
      .= H.(H $$(B,h*f),(h*f).c) by FUNCT_2:21
      .= H $$(B \/ {.c.},h*f) by A2,A7,Th4;
  end;
  h.(F$$({}.C,f)) = the_unity_wrt H by A1,A3,SETWISEO:40
    .= H $$({}.C,h*f) by A2,SETWISEO:40;
  then
A8: X[{}.C];
  for B holds X[B] from SETWISEO:sch 2(A8,A5);
  hence thesis;
end;

theorem
  F is commutative associative & F is having_a_unity & u.the_unity_wrt F
= the_unity_wrt F & u is_distributive_wrt F implies u.(F$$(B,f)) = F $$(B,u*f)
proof
  assume
A1: F is commutative associative & F is having_a_unity & u.the_unity_wrt
  F = the_unity_wrt F;
  assume
  for d1,d2 holds u.(F.(d1,d2)) = F.(u.d1,u.d2);
  hence thesis by A1,Th18;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
having_an_inverseOp & G is_distributive_wrt F implies G[;](d,id D).(F$$(B,f)) =
  F $$(B,G[;](d,id D)*f)
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: F is having_an_inverseOp and
A3: G is_distributive_wrt F;
  set e = the_unity_wrt F;
  set u = G[;](d,id D);
  u is_distributive_wrt F by A3,FINSEQOP:55;
  then
A4: for d1,d2 holds u.(F.(d1,d2)) = F.(u.d1,u.d2) by BINOP_1:def 12;
  G[;](d,id D).e = e by A1,A2,A3,FINSEQOP:73;
  hence thesis by A1,A4,Th18;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
  having_an_inverseOp implies (the_inverseOp_wrt F).(F$$(B,f)) = F $$(B,(
  the_inverseOp_wrt F)*f)
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: F is having_an_inverseOp;
  set e = the_unity_wrt F, u = the_inverseOp_wrt F;
  u is_distributive_wrt F by A1,A2,FINSEQOP:67;
  then
A3: for d1,d2 holds u.(F.(d1,d2)) = F.(u.d1,u.d2) by BINOP_1:def 12;
  u.e = e by A1,A2,FINSEQOP:65;
  hence thesis by A1,A3,Th18;
end;

definition
  let D,p,d;
  func [#](p,d) -> Function of NAT,D equals
  (NAT --> d) +* p;
  coherence;
end;

theorem Th22:
  (i in dom p implies [#](p,d).i = p.i) & (not i in dom p implies
  [#](p,d).i = d)
proof
  thus i in dom p implies [#](p,d).i = p.i by FUNCT_4:14;
A1: i in NAT by ORDINAL1:def 13;
  assume
  not i in dom p;
  hence [#](p,d).i = (NAT --> d).i by FUNCT_4:12
    .= d by A1,FUNCOP_1:13;
end;

theorem
  [#](p,d)|(dom p) = p
proof
  set k = len p, f = [#](p,d);
  Seg k c= NAT;
  then Seg k c= dom f by FUNCT_2:def 1;
  then
A1: dom (f|Seg k) = Seg k by RELAT_1:91;
A2: dom p = Seg k by FINSEQ_1:def 3;
  now
    let x;
    assume
A3: x in Seg k;
    then (f|Seg k).x = f.x by A1,FUNCT_1:70;
    hence (f|Seg k).x = p.x by A2,A3,Th22;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

theorem
  [#](p^q,d)|(dom p) = p
proof
  set k = len p, f = [#](p^q,d);
  Seg k c= NAT;
  then Seg k c= dom f by FUNCT_2:def 1;
  then
A1: dom (f|Seg k) = Seg k by RELAT_1:91;
A2: dom p = Seg k by FINSEQ_1:def 3;
  now
    let x;
    k <= k + len q by NAT_1:12;
    then k <= len (p^q) by FINSEQ_1:35;
    then
A3: Seg(len(p^q)) = dom (p^q) & Seg k c= Seg len(p^q) by FINSEQ_1:7,
    def 3;
    assume
A4: x in Seg k;
    then (f|Seg k).x = f.x by A1,FUNCT_1:70;
    hence (f|Seg k).x = (p^q).x by A4,A3,Th22
      .= p.x by A2,A4,FINSEQ_1:def 7;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

theorem
  rng [#](p,d) = rng p \/ {d}
proof
  now
    let y;
    thus y in rng [#](p,d) implies y in rng p \/ {d}
    proof
      assume
      y in rng [#](p,d);
      then consider x such that
A1:   x in dom [#](p,d) and
A2:   y = [#](p,d).x by FUNCT_1:def 5;
      reconsider i = x as Element of NAT by A1;
      now
        per cases;
        case
A3:       i in dom p;
          then y = p.i by A2,Th22;
          hence y in rng p by A3,FUNCT_1:def 5;
        end;
        case
          not i in dom p;
          then y = d by A2,Th22;
          hence y in {d} by TARSKI:def 1;
        end;
      end;
      hence thesis by XBOOLE_0:def 3;
    end;
    assume
A4: y in rng p \/ {d};
    now
      per cases by A4,XBOOLE_0:def 3;
      suppose
        y in rng p;
        then consider i being Nat such that
A5:     i in dom p and
A6:     y = p.i by FINSEQ_2:11;
        y = [#](p,d).i by A5,A6,Th22;
        hence y in rng [#](p,d) by A5,FUNCT_2:6;
      end;
      suppose
A7:     y in {d};
        dom p = Seg len p by FINSEQ_1:def 3;
        then
A8:     not len p + 1 in dom p by Lm2;
        y = d by A7,TARSKI:def 1;
        then y = [#](p,d).(len p + 1) by A8,Th22;
        hence y in rng [#](p,d) by FUNCT_2:6;
      end;
    end;
    hence y in rng [#](p,d);
  end;
  hence thesis by TARSKI:2;
end;

theorem
  h*[#](p,d) = [#](h*p,h.d)
proof
  now
    let i be Element of NAT;
A1: dom(h*p)=Seg len(h*p) by FINSEQ_1:def 3;
A2: len(h*p) = len p & Seg len p = dom p by FINSEQ_1:def 3,FINSEQ_2:37;
    now
      per cases;
      suppose
A3:     i in dom p;
        hence h.([#](p,d).i) = h.(p.i) by Th22
          .= (h*p).i by A3,FUNCT_1:23
          .= [#](h*p,h.d).i by A2,A1,A3,Th22;
      end;
      suppose
A4:     not i in dom p;
        hence h.([#](p,d).i) = h.d by Th22
          .= [#](h*p,h.d).i by A2,A1,A4,Th22;
      end;
    end;
    hence (h*[#](p,d)).i = [#](h*p,h.d).i by FUNCT_2:21;
  end;
  hence thesis by FUNCT_2:113;
end;

Lm4: len p = len q & F.(e,e) = e implies F.:([#](p,e),[#](q,e)) = [#](F.:(p,q)
,e)

proof
  assume that
A1: len p = len q and
A2: F.(e,e) = e;
  set r = F.:(p,q);
A3: len r = len p by A1,FINSEQ_2:86;
A4: dom r = Seg len r by FINSEQ_1:def 3;
A5: dom p = Seg len p by FINSEQ_1:def 3;
A6: dom q = Seg len q by FINSEQ_1:def 3;
  now
    let i be Element of NAT;
    now
      per cases;
      suppose
A7:     i in dom p;
        hence F.([#](p,e).i,[#](q,e).i) = F.(p.i,[#](q,e).i) by Th22
          .= F.(p.i,q.i) by A1,A5,A6,A7,Th22
          .= r.i by A3,A5,A4,A7,FUNCOP_1:28
          .= [#](r,e).i by A3,A5,A4,A7,Th22;
      end;
      suppose
A8:     not i in dom p;
        hence F.([#](p,e).i,[#](q,e).i) = F.(e,[#](q,e).i) by Th22
          .= e by A1,A2,A5,A6,A8,Th22
          .= [#](r,e).i by A3,A5,A4,A8,Th22;
      end;
    end;
    hence F.:([#](p,e),[#](q,e)).i = [#](r,e).i by FUNCOP_1:48;
  end;
  hence thesis by FUNCT_2:113;
end;

Lm5: F.(e,d) = e implies F[:]([#](p,e),d) = [#](F[:](p,d),e)
proof
  assume
A1: F.(e,d) = e;
  now
    let i be Element of NAT;
A2: dom p = Seg len p by FINSEQ_1:def 3;

A3: len(F[:](p,d)) = len p & dom(F[:](p,d)) = Seg len(F[:](p,d)) by
    FINSEQ_1:def 3,FINSEQ_2:98;

    now
      per cases;
      suppose
A4:     i in dom p;
        hence F.([#](p,e).i,d) = F.(p.i,d) by Th22
          .= (F[:](p,d)).i by A3,A2,A4,FUNCOP_1:35
          .= ([#](F[:](p,d),e)).i by A3,A2,A4,Th22;
      end;
      suppose
A5:     not i in dom p;
        hence F.([#](p,e).i,d) = F.(e,d) by Th22
          .= ([#](F[:](p,d),e)).i by A1,A3,A2,A5,Th22;
      end;
    end;
    hence (F[:]([#](p,e),d)).i = ([#](F[:](p,d),e)).i by FUNCOP_1:60;
  end;
  hence thesis by FUNCT_2:113;
end;

Lm6: F.(d,e) = e implies F[;](d,[#](p,e)) = [#](F[;](d,p),e)
proof
  assume
A1: F.(d,e) = e;
  now
    let i be Element of NAT;
A2: dom p = Seg len p by FINSEQ_1:def 3;

A3: len(F[;](d,p)) = len p & dom(F[;](d,p)) = Seg len(F[;](d,p)) by
    FINSEQ_1:def 3,FINSEQ_2:92;

    now
      per cases;
      suppose
A4:     i in dom p;
        hence F.(d,[#](p,e).i) = F.(d,p.i) by Th22
          .= (F[;](d,p)).i by A3,A2,A4,FUNCOP_1:42
          .= ([#](F[;](d,p),e)).i by A3,A2,A4,Th22;
      end;
      suppose
A5:     not i in dom p;
        hence F.(d,[#](p,e).i) = F.(d,e) by Th22
          .= ([#](F[;](d,p),e)).i by A1,A3,A2,A5,Th22;
      end;
    end;
    hence (F[;](d,[#](p,e))).i = ([#](F[;](d,p),e)).i by FUNCOP_1:66;
  end;
  hence thesis by FUNCT_2:113;
end;

notation
  let i be Nat;
  synonym finSeg i for Seg i;
end;

definition
  let i be Nat;
  redefine func finSeg i -> Element of Fin NAT;
  coherence by Lm1;
end;

notation
  let D,p,F;
  synonym F $$ p for F "**" p;
end;

definition
  let D,p,F;
  assume
A1: ( F is having_a_unity or len p >= 1)& F is associative commutative;
  redefine func F $$ p equals
  :Def2:
  F $$(findom p,[#](p,the_unity_wrt F));
  compatibility by A1,FINSOP_1:4;
end;

canceled 8;

theorem Th35:
  F is having_a_unity implies F"**"(i|->the_unity_wrt F) =
  the_unity_wrt F
proof
  set e = the_unity_wrt F;
  defpred X[Nat] means F"**"($1|->e) = e;
  assume
A1: F is having_a_unity;
A2: for i st X[i] holds X[i+1]
  proof
    let i;
    assume
A3: F"**"(i|->e) = e;
    thus F"**"((i+1)|->e) = F"**"((i|->e)^<*e*>) by FINSEQ_2:74
      .= F.(F"**"(i|->e),e) by A1,FINSOP_1:5
      .= e by A1,A3,SETWISEO:23;
  end;
  F"**"(0|->e) = F"**" <*>D
    .= e by A1,FINSOP_1:11;
  then
A4: X[0];
  for i holds X[i] from NAT_1:sch 2(A4,A2);
  hence thesis;
end;

canceled;

theorem Th37:
  F is associative & (i>=1 & j>=1 or F is having_a_unity) implies
  F"**"((i+j)|->d) = F.(F"**"(i|->d),F"**"(j|->d))
proof
  assume
A1: F is associative;
  set p1 = (i|->d),p2 = (j|->d);
  assume
  i>=1 & j>=1 or F is having_a_unity;
  then len p1 >= 1 & len p2 >= 1 or F is having_a_unity by
  FINSEQ_1:def 18;
  then F "**"(p1^p2) = F.(F"**"p1,F"**"p2) by A1,FINSOP_1:6;
  hence thesis by FINSEQ_2:143;
end;

theorem
  F is commutative associative & (i>=1 & j>=1 or F is having_a_unity)
  implies F"**"((i*j)|->d) = F"**"(j|->(F"**"(i|->d)))
proof
  assume that
A1: F is commutative associative and
A2: i>=1 & j>=1 or F is having_a_unity;
  per cases by NAT_1:3;
  suppose
A3: i = 0 or j = 0;
    set e = the_unity_wrt F;
A4: now
      per cases by A3;
      suppose
        i = 0;
        then i|->d = <*>D;
        then F"**"(i|->d) = e by A2,A3,FINSOP_1:11;
        hence F"**"(j|->(F"**"(i|->d))) = e by A2,A3,Th35;
      end;
      suppose
        j = 0;
        then j|->(F"**"(i|->d)) = <*>D;
        hence F"**"(j|->(F"**"(i|->d))) = e by A2,A3,FINSOP_1:11;
      end;
    end;
    (i*j)|->d = <*>D by A3;
    hence thesis by A2,A3,A4,FINSOP_1:11;
  end;
  suppose
A5: i > 0 & j > 0;
    defpred X[Nat] means $1 <> 0 implies F"**"((i*$1)|->d) = F"**"($1|->(F"**"
    (i|->d)));
A6: for j st X[j] holds X[j+1]
    proof
      let j such that
A7:   j <> 0 implies F"**"((i*j)|->d) = F"**"(j|->(F"**"(i|->d)));
      now
        per cases by NAT_1:14;
        suppose
A8:       j = 0;
          1|->(F"**"(i|->d)) = <*F"**"(i|->d)*> by FINSEQ_2:73;
          hence thesis by A8,FINSOP_1:12;
        end;
        suppose
A9:       j >= 1+0;
          then j > 0 by NAT_1:13;
          then i*j > i*0 by A5,XREAL_1:70;
          then
A10:      i*j >= 1+0 by NAT_1:13;
          F"**"((i*(j+1))|->d) = F"**"((i*j+i*1)|->d)
            .= F.(F"**"((i*j)|->d),F"**"(i|->d)) by A1,A2,A10,Th37
            .= F.(F"**"((i*j)|->d),F"**"(1|->(F"**"(i|->d)))) by FINSOP_1:17
            .= F"**"((j+1)|->(F"**"(i|->d))) by A1,A7,A9,Th37;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
A11: X[0];
    for j holds X[j] from NAT_1:sch 2(A11,A6);
    hence thesis by A5;
  end;
end;

theorem Th39:
  F is having_a_unity & H is having_a_unity & h.the_unity_wrt F =
the_unity_wrt H & (for d1,d2 holds h.(F.(d1,d2)) = H.(h.d1,h.d2)) implies h.(F
  "**"p) = H "**"(h*p)
proof
  assume that
A1: F is having_a_unity and
A2: H is having_a_unity and
A3: h.the_unity_wrt F = the_unity_wrt H and
A4: for d1,d2 holds h.(F.(d1,d2)) = H.(h.d1,h.d2);
  defpred X[FinSequence of D] means h.(F"**"$1) = H "**"(h*$1);
A5: for q,d st X[q] holds X[q^<*d*>]
  proof
    let q,d such that
A6: h.(F"**"q) = H "**"(h*q);
    thus h.(F"**"(q^<*d*>)) = h.(F.(F"**"q,d)) by A1,FINSOP_1:5
      .= H.(H "**"(h*q),h.d) by A4,A6
      .= H "**" ((h*q)^<*h.d*>) by A2,FINSOP_1:5
      .= H "**"(h*(q^<*d*>)) by FINSEQOP:9;
  end;
  h.(F"**"<*>D) = h.(the_unity_wrt F) by A1,FINSOP_1:11
    .= H "**"(<*>E) by A2,A3,FINSOP_1:11
    .= H "**"(h*<*>D) by RELAT_1:62;
  then
A7: X[<*>D];
  X[q] from FINSEQ_2:sch 2(A7,A5);
  hence thesis;
end;

theorem
  F is having_a_unity & u.the_unity_wrt F = the_unity_wrt F & u
  is_distributive_wrt F implies u.(F"**"p) = F "**"(u*p)
proof
  assume
A1: F is having_a_unity & u.the_unity_wrt F = the_unity_wrt F;
  assume
  for d1,d2 holds u.(F.(d1,d2)) = F.(u.d1,u.d2);
  hence thesis by A1,Th39;
end;

theorem
  F is associative & F is having_a_unity & F is having_an_inverseOp & G
  is_distributive_wrt F implies G[;](d,id D).(F"**"p) = F "**"(G[;](d,id D)*p)
proof
  assume that
A1: F is associative and
A2: F is having_a_unity and
A3: F is having_an_inverseOp and
A4: G is_distributive_wrt F;
  set e = the_unity_wrt F;
  set u = G[;](d,id D);
  u is_distributive_wrt F by A4,FINSEQOP:55;
  then
A5: for d1,d2 holds u.(F.(d1,d2)) = F.(u.d1,u.d2) by BINOP_1:def 12;
  G[;](d,id D).e = e by A1,A2,A3,A4,FINSEQOP:73;
  hence thesis by A2,A5,Th39;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
  having_an_inverseOp implies (the_inverseOp_wrt F).(F"**"p) = F "**"((
  the_inverseOp_wrt F)*p)
proof
  assume that
A1: F is commutative associative and
A2: F is having_a_unity and
A3: F is having_an_inverseOp;
  set e = the_unity_wrt F, u = the_inverseOp_wrt F;
  u is_distributive_wrt F by A1,A2,A3,FINSEQOP:67;
  then
A4: for d1,d2 holds u.(F.(d1,d2)) = F.(u.d1,u.d2) by BINOP_1:def 12;
  u.e = e by A1,A2,A3,FINSEQOP:65;
  hence thesis by A2,A4,Th39;
end;

theorem Th43:
  F is commutative associative & F is having_a_unity & e =
the_unity_wrt F & G.(e,e) = e & (for d1,d2,d3,d4 holds F.(G.(d1,d2),G.(d3,d4))=
G.(F.(d1,d3),F.(d2,d4))) & len p = len q implies G.(F"**"p,F"**"q) = F "**"(G.:
  (p,q))
proof
  assume that
A1: F is commutative & F is associative & F is having_a_unity & e =
  the_unity_wrt F and
A2: G.(e,e) = e and
A3: F.(G.(d1,d2),G.(d3,d4))= G.(F.(d1,d3),F.(d2,d4)) and
A4: len p = len q;
A5: len p = len(G.:(p,q)) by A4,FINSEQ_2:86;
A6: dom(G.:(p,q)) = Seg len(G.:(p,q)) by FINSEQ_1:def 3;
A7: dom q = Seg len q by FINSEQ_1:def 3;
A8: dom p = Seg len p by FINSEQ_1:def 3;
  thus G.(F "**"p,F "**"q) = G.(F $$(findom p,[#](p,e)),F "**"q) by A1,Def2
    .= G.(F $$(findom p,[#](p,e)),F $$(findom q,[#](q,e))) by A1,Def2
    .= F $$(findom p,G.:([#](p,e),[#](q,e))) by A1,A2,A3,A4,A8,A7,Th11
    .= F $$(findom(G.:(p,q)),[#](G.:(p,q),e)) by A2,A4,A5,A8,A6,Lm4
    .= F "**"(G.:(p,q)) by A1,Def2;
end;

theorem Th44:
  F is commutative associative & F is having_a_unity & e =
the_unity_wrt F & G.(e,e) = e & (for d1,d2,d3,d4 holds F.(G.(d1,d2),G.(d3,d4))=
  G.(F.(d1,d3),F.(d2,d4))) implies G.(F"**"T1,F"**"T2) = F "**"(G.:(T1,T2))
proof
  len T1 = i & len T2 = i by FINSEQ_1:def 18;
  hence thesis by Th43;
end;

theorem Th45:
  F is commutative associative & F is having_a_unity & len p = len
  q implies F.(F"**"p,F"**"q) = F "**"(F.:(p,q))
proof
  set e = the_unity_wrt F;
  assume
A1: F is commutative & F is associative & F is having_a_unity;
  then
  F.(e,e) = e & for d1,d2,d3,d4 holds F.(F.(d1,d2),F.(d3,d4))= F.(F.(d1,d3
  ),F. (d2,d4)) by Lm3,SETWISEO:23;
  hence thesis by A1,Th43;
end;

theorem Th46:
  F is commutative associative & F is having_a_unity implies F.(F
  "**"T1,F"**"T2) = F "**"(F.:(T1,T2))
proof
  len T1 = i & len T2 = i by FINSEQ_1:def 18;
  hence thesis by Th45;
end;

theorem
  F is commutative associative & F is having_a_unity implies F"**"(i|->(
  F.(d1,d2))) = F.(F"**"(i|->d1),F"**"(i|->d2))
proof
  reconsider T1 = i|->d1, T2 = i|->d2 as Element of i-tuples_on D;
  i|->(F.(d1,d2)) = F.:(T1,T2) by FINSEQOP:18;
  hence thesis by Th46;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
having_an_inverseOp & G = F*(id D,the_inverseOp_wrt F) implies G.(F"**"T1,F"**"
  T2) = F "**"(G.:(T1,T2))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: F is having_an_inverseOp & G = F*(id D,the_inverseOp_wrt F);
  set e = the_unity_wrt F;
  G.(e,e) = e & for d1,d2,d3,d4 holds F.(G.(d1,d2),G.(d3,d4))= G.(F.(d1,d3
  ),F. (d2,d4)) by A1,A2,FINSEQOP:91,94;
  hence thesis by A1,Th44;
end;

theorem Th49:
  F is commutative associative & F is having_a_unity & e =
the_unity_wrt F & G is_distributive_wrt F & G.(d,e) = e implies G.(d,F"**"p) =
  F "**"(G[;](d,p))
proof
  assume that
A1: F is commutative & F is associative & F is having_a_unity & e =
  the_unity_wrt F and
A2: G is_distributive_wrt F and
A3: G.(d,e) = e;
A4: len p = len(G[;](d,p)) & Seg len p = dom p by FINSEQ_1:def 3,FINSEQ_2:92;
A5: Seg len(G[;](d,p)) = dom(G[;](d,p)) by FINSEQ_1:def 3;
  thus G.(d,F"**"p) = G.(d,F$$(findom p,[#](p,e))) by A1,Def2
    .= F $$(findom p,G[;](d,[#](p,e))) by A1,A2,A3,Th14
    .= F $$(findom p,[#](G[;](d,p),e)) by A3,Lm6
    .= F "**"(G[;](d,p)) by A1,A4,A5,Def2;
end;

theorem Th50:
  F is commutative associative & F is having_a_unity & e =
the_unity_wrt F & G is_distributive_wrt F & G.(e,d) = e implies G.(F"**"p,d) =
  F "**"(G[:](p,d))
proof
  assume that
A1: F is commutative associative & F is having_a_unity & e =
  the_unity_wrt F and
A2: G is_distributive_wrt F and
A3: G.(e,d) = e;
A4: len p = len(G[:](p,d)) & Seg len p = dom p by FINSEQ_1:def 3,FINSEQ_2:98;
A5: Seg len(G[:](p,d)) = dom(G[:](p,d)) by FINSEQ_1:def 3;
  thus G.(F"**"p,d) = G.(F$$(findom p,[#](p,e)),d) by A1,Def2
    .= F $$(findom p,G[:]([#](p,e),d)) by A1,A2,A3,Th15
    .= F $$(findom p,[#](G[:](p,d),e)) by A3,Lm5
    .= F "**"(G[:](p,d)) by A1,A4,A5,Def2;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
  having_an_inverseOp & G is_distributive_wrt F implies G.(d,F"**"p) = F "**"(G
  [;](d,p))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: F is having_an_inverseOp and
A3: G is_distributive_wrt F;
  set e = the_unity_wrt F;
  G.(d,e) = e by A1,A2,A3,FINSEQOP:70;
  hence thesis by A1,A3,Th49;
end;

theorem
  F is commutative associative & F is having_a_unity & F is
  having_an_inverseOp & G is_distributive_wrt F implies G.(F"**"p,d) = F "**"(G
  [:](p,d))
proof
  assume that
A1: F is commutative associative & F is having_a_unity and
A2: F is having_an_inverseOp and
A3: G is_distributive_wrt F;
  set e = the_unity_wrt F;
  G.(e,d) = e by A1,A2,A3,FINSEQOP:70;
  hence thesis by A1,A3,Th50;
end;

