:: Basic Properties of the Rank of Matrices over a Field
::  by Karol P\c{a}k
::
:: Received August 28, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies TARSKI, BOOLE, ARYTM_1, ARYTM_3, FINSEQ_1, FINSEQ_2, FINSEQ_4,
      FINSEQ_5, FINSEQ_7, TREES_1, RELAT_1, FUNCT_1, FUNCT_2, FUNCT_4,
      FINSOP_1, FUNCOP_1, INCSP_1, CAT_1, CAT_3, RVSUM_1, RLSUB_1, GROUP_1,
      BINOP_1, SETWISEO, FINSUB_1, FINSET_1, SGRAPH1, CARD_1, CARD_3, NAT_1,
      INT_1, ABSVALUE, QC_LANG1, ROUGHS_1, NEWTON, CARD_FIN, GR_CY_1, FREEALG,
      FSM_1, XREAL_0, SEQ_1, MATRLIN, MATRIX_1, MATRIX_2, MATRIX_3, MATRIX11,
      LAPLACE, MATRIX13, VECTSP_1, VECTSP_9, PRVECT_1, RLVECT_1, RLVECT_2,
      RLVECT_3, LMOD_7, COMPLEX1, CLASSES1, SEQM_3;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, CARD_1, XCMPLX_0,
      XXREAL_0, NAT_1, NAT_D, FINSET_1, RELAT_1, RELSET_1, FUNCT_1, PARTFUN1,
      FUNCT_2, SETWOP_2, FINSEQ_1, BINOP_1, STRUCT_0, FUNCOP_1, RLVECT_1,
      GROUP_1, VECTSP_1, SETWISEO, FINSEQ_2,
      SEQM_3, MATRIX_1, FINSUB_1, MATRIX_2,
      FVSUM_1, FINSOP_1, BINARITH, GROUP_4, MATRIX_3, FINSEQ_7, FUNCT_4,
      SGRAPH1, NEWTON, FINSEQ_5, DOMAIN_1, MATRIX_6, MATRIX_7, MATRIX11,
      CARD_FIN, FINSEQ_3, LAPLACE, PSCOMP_1, PRVECT_1, VECTSP_4, VECTSP_6,
      VECTSP_7, VECTSP_9, FINSEQOP;
 constructors PARTFUN1, SETWISEO, FINSOP_1, ALGSTR_1, FVSUM_1, DOMAIN_1,
      SEQM_3,
      XXREAL_0, FINSEQ_5, GROUP_4, FINSEQ_7, WELLORD2, NEWTON, STIRL2_1,
      CARD_FIN, MATRIX_6, MATRIX_7, MATRIX11, LAPLACE, PRVECT_1, VECTSP_6,
      VECTSP_7, VECTSP_9, NAT_D, NAT_1;
 registrations SUBSET_1, FUNCT_1, FINSET_1, STRUCT_0, FVSUM_1, MATRIX_2,
      VECTSP_1, FUNCT_2, ORDINAL1, XXREAL_0, NAT_1, FINSEQ_1, RELAT_1, CARD_1,
      SGRAPH1, MATRIX_1, FUNCOP_1, MATRIX11, SETFAM_1, FINSEQ_2, SEQM_3,
      PRVECT_1, MATRLIN, XREAL_0, XBOOLE_0, FRAENKEL, MEMBERED;
 requirements ARITHM, NUMERALS, REAL, BOOLE, SUBSET;
 definitions RLVECT_1, FINSEQ_2, FINSEQ_1, TARSKI, MATRIX_1, FVSUM_1, GROUP_4,
      MATRIX_2, MATRIX_3, MATRIX_7, MATRIX11, LAPLACE, PSCOMP_1, VECTSP_1,
      VECTSP_7, FUNCT_1, RELAT_1, STRUCT_0;
 theorems BINARITH, CARD_1, CARD_2, CARD_FIN, COMPUT_1, CQC_LANG, ENUMSET1,
      FINSEQ_1, FINSEQ_2, FINSEQ_3, FINSEQ_4, FINSEQ_5, FINSET_1, FINSOP_1,
      FINSUB_1, FRECHET, FUNCT_1, FUNCT_2, FUNCT_4, FUNCT_7, FUNCOP_1, FVSUM_1,
      GRAPH_2, GRAPH_5, GROUP_1, JORDAN16, LAPLACE, MATRIX_1, MATRIX_2,
      MATRIX_3, MATRIX_7, MATRIX_9, MATRIX11, MATRIXR1, NAT_1, ORDINAL1,
      PENCIL_4, PRVECT_1, PSCOMP_1, REAL_1, RELAT_1, RELSET_1, RLVECT_1,
      SETWISEO, SETWOP_2, SGRAPH1, STIRL2_1, TARSKI, VECTSP_1, VECTSP_2,
      VECTSP_4, VECTSP_6, VECTSP_7, VECTSP_9, VECTSP10, WELLORD2, XBOOLE_0,
      XBOOLE_1, XREAL_1, ZFMISC_1, NAT_D, XXREAL_0, FINSEQOP, STRUCT_0,
      MATRIXR2, PARTFUN1;
 schemes FINSEQ_1, FINSEQ_2, FUNCT_1, FUNCT_2, NAT_1, TRIANG_1, MATRIX_1;

begin :: Triangular Matrices

 reserve x,y,X,Y for set,
      D for non empty set,
      i,j,k,l,m,n,m',n' for Element of NAT,
      i0,j0,n0,m0 for non zero Element of NAT,
      K for Field,
      a,b for Element of K,
      p for FinSequence of K,
      M for (Matrix of n,K);

theorem Th1:
  for A be Matrix of n,m,D holds
    (n=0 implies m=0) iff len A = n & width A = m
proof
  let A be Matrix of n,m,D;
  thus (n=0 implies m=0) implies len A = n & width A = m
  proof
    assume A1:n=0 implies m=0;
    per cases;
      suppose A2:n=0;
        then len A=0 by MATRIX_1:def 3;
        hence thesis by A1,A2,MATRIX_1:def 4;end;
      suppose A3:n>0;
        len A=n by MATRIX_1:def 3;
        hence thesis by A3,MATRIX_1:20;end;
  end;
  thus thesis by MATRIX_1:def 4;
end;

theorem Th2:
  M is Lower_Triangular_Matrix of n,K iff M@ is Upper_Triangular_Matrix of n,K
proof
  thus (M is Lower_Triangular_Matrix of n,K) implies
                                         (M@ is Upper_Triangular_Matrix of n,K)
  proof
    assume A1:M is Lower_Triangular_Matrix of n,K;
    let i,j be Nat such that A2:[i,j] in Indices M@ & i>j;
    [j,i] in Indices M by A2,MATRIX_1:def 7;
    then M*(j,i)=0.K & M@*(i,j)=M*(j,i) by A1,A2,MATRIX_1:def 7,MATRIX_2:def 4;
    hence thesis;
  end;
  assume A3:M@ is Upper_Triangular_Matrix of n,K;
  let i,j be Nat such that A4:[i,j] in Indices M & i<j;
  [j,i] in Indices M@ by A4,MATRIX_1:def 7;
  then M@*(j,i)=0.K & M@*(j,i)=M*(i,j) by A3,A4,MATRIX_1:def 7,MATRIX_2:def 3;
  hence thesis;
end;

theorem Th3:
  diagonal_of_Matrix M = diagonal_of_Matrix M@
proof
  set DM=diagonal_of_Matrix M;set DM'=diagonal_of_Matrix M@;
  A1:len DM=n & len DM'=n by MATRIX_3:def 10;
  now let i be Nat such that A2:1<= i & i <= len DM;
    i in NAT by ORDINAL1:def 13;
    then i in Seg n & Indices M=[:Seg n,Seg n:] by A1,A2,MATRIX_1:25;
    then [i,i] in Indices M & DM.i=M*(i,i) & DM'.i=M@*(i,i)
      by MATRIX_3:def 10,ZFMISC_1:106;
    hence DM.i=DM'.i by MATRIX_1:def 7;
  end;
  hence thesis by A1,FINSEQ_1:18;
end;

theorem Th4:
  for perm being Element of Permutations n st perm <> idseq n holds
    (ex i st i in Seg n & perm.i > i) & (ex j st j in Seg n & perm.j < j)
proof
  let p be Element of Permutations n such that A1:p <> idseq n;
  reconsider p'=p as Permutation of Seg n by MATRIX_2:def 11;
  dom p'=Seg n by FUNCT_2:67;
  then consider x be set such that
    A2:x in Seg n & p.x<>x by A1,FUNCT_1:34;
  consider i such that A3:i=x & 1<=i & i<=n by A2;
  now
  per cases by A2,A3,REAL_1:def 5;
    suppose p.i>i;
      hence ex j st j in Seg n & p.j > j by A2,A3;end;
    suppose A4:p.i<i;
      then reconsider i1=i-1 as Nat by NAT_1:20;
      thus ex j st j in Seg n & p.j > j
      proof
        assume A5:for j st j in Seg n holds p.j<=j;
        A6:p.:Seg i c= Seg i1
        proof
          let x; assume x in p.:Seg i;
          then consider y be set such that
            A7:y in dom p' & y in Seg i & p.y=x by FUNCT_1:def 12;
          consider j such that A8:j=y & 1<=j & j<=i by A7;
          per cases by A8,REAL_1:def 5;
            suppose j=i;
              then p.j<i1+1 & p.j in rng p & rng p'=Seg n
                by A4,A7,A8,FUNCT_1:def 5,FUNCT_2:def 3;
              then p.j <= i1 & p.j>=1 by FINSEQ_1:3,NAT_1:13;
              hence x in Seg i1 by A7,A8,FINSEQ_1:3;end;
            suppose A9:j<i;
              p.j <=j & p.j in rng p & rng p'=Seg n
                by A5,A7,A8,FUNCT_1:def 5,FUNCT_2:def 3;
              then p.j < i1+1 & p.j>=1 by A9,FINSEQ_1:3,XXREAL_0:2;
              then p.j<= i1 & p.j>=1 by NAT_1:13;
              hence x in Seg i1 by A7,A8,FINSEQ_1:3;end;
          end;
          reconsider pS=p'.:Seg i as finite set;
          Seg i c= Seg n & dom p'=Seg n by A3,FINSEQ_1:7,FUNCT_2:67;
          then (Seg i),pS are_equipotent by CARD_1:60;
          then card Seg i=card pS & card pS <= card Seg i1 & card Seg i=i
            by A6,CARD_1:80,81,FINSEQ_1:78;
          then i1+1 <= i1 by FINSEQ_1:78;
          hence thesis by NAT_1:13;
        end;end;
  end;
  hence ex j st j in Seg n & p.j > j;
  per cases by A2,A3,REAL_1:def 5;
    suppose p.i<i;
      hence ex j st j in Seg n & p.j < j by A2,A3;end;
    suppose A10:p.i>i;
      thus ex j st j in Seg n & p.j < j
      proof
        assume A11:for j st j in Seg n holds p.j>=j;
        set NI=nat_interval(i,n);
        reconsider pN=p'.:NI as finite set;
        A12:pN c= NI
        proof
          let x; assume x in pN;
          then consider j be set such that
            A13:j in dom p' & j in NI & p.j=x by FUNCT_1:def 12;
          reconsider j as Nat by A13,ORDINAL1:def 13;
          reconsider pj=p.j,j'=j as Element of NAT by ORDINAL1:def 13;
          i<=j & j<=pj & pj in rng p' & rng p'=Seg n
            by A11,A13,FUNCT_1:def 5,FUNCT_2:def 3,SGRAPH1:3;
          then i <=pj & pj<=n by FINSEQ_1:3,XXREAL_0:2;
          hence thesis by A13,SGRAPH1:2;
        end;
        NI c= Seg n & dom p'=Seg n by A3,FUNCT_2:67,SGRAPH1:5;
        then NI,pN are_equipotent by CARD_1:60;
        then card NI=card pN & i<=n by A3,CARD_1:81;
        then NI=pN & i in NI by A12,CARD_FIN:1,SGRAPH1:3;
        then consider j be set such that
          A14:j in dom p' & j in NI & p.j=i by FUNCT_1:def 12;
        reconsider j as Nat by A14,ORDINAL1:def 13;
        j <= i & i<=j by A11,A14,SGRAPH1:3;
        hence thesis by A10,A14,XXREAL_0:1;
      end;end;
end;

theorem Th5:
  for M be (Matrix of n,K),perm being Element of Permutations n st
    perm <> idseq n &
         ( M is Lower_Triangular_Matrix of n,K or
           M is Upper_Triangular_Matrix of n,K )
    holds (Path_product(M)).perm = 0.K
proof
  let M be (Matrix of n,K),p be Element of Permutations n such that
    A1:p <> idseq n and
    A2:M is Lower_Triangular_Matrix of n,K or
        M is Upper_Triangular_Matrix of n,K;
  set PATH=Path_matrix(p,M);set PP=Path_product(M);
  reconsider p'=p as Permutation of Seg n by MATRIX_2:def 11;
  now per cases by A2;
    suppose A3:M is Lower_Triangular_Matrix of n,K;
      consider i such that
        A4:i in Seg n & p.i>i by A1,Th4;
      dom p'=Seg n & rng p'=Seg n by FUNCT_2:67,def 3;
      then A5:p'.i in Seg n by A4,FUNCT_1:def 5;
      reconsider Pi=p.i as Nat;
      len PATH=n by MATRIX_3:def 7;
      then dom PATH=Seg n & [i,Pi] in [:Seg n,Seg n:]&Indices M=[:Seg n,Seg n:]
         by A4,A5,FINSEQ_1:def 3,MATRIX_1:25,ZFMISC_1:106;
      then PATH.i=M*(i,Pi) & i in dom PATH & M*(i,Pi)=0.K
        by A3,A4,MATRIX_2:def 4,MATRIX_3:def 7;
      hence ex i st i in dom PATH & PATH.i=0.K;
    end;
    suppose A6:M is Upper_Triangular_Matrix of n,K;
    consider i such that
        A7:i in Seg n & p.i<i by A1,Th4;
      dom p'=Seg n & rng p'=Seg n by FUNCT_2:67,def 3;
      then A8:p'.i in Seg n by A7,FUNCT_1:def 5;
      reconsider Pi=p.i as Nat;
      len PATH=n by MATRIX_3:def 7;
      then dom PATH=Seg n & [i,Pi] in [:Seg n,Seg n:]&Indices M=[:Seg n,Seg n:]
         by A7,A8,FINSEQ_1:def 3,MATRIX_1:25,ZFMISC_1:106;
      then PATH.i=M*(i,Pi) & i in dom PATH & M*(i,Pi)=0.K
        by A6,A7,MATRIX_2:def 3,MATRIX_3:def 7;
      hence ex i st i in dom PATH & PATH.i=0.K;
    end;
  end;
  then consider i such that
  A9:i in dom PATH & PATH.i=0.K;
  set mm=the mult of K;
  Product PATH=0.K by A9,FVSUM_1:107;
  then mm "**" PATH=0.K &(p is even or p is odd);
  then PP.p=-(0.K,p) & (-(0.K,p)=0.K or -(0.K,p)=-0.K ) & -0.K=0.K
    by MATRIX_2:def 16,MATRIX_3:def 8,RLVECT_1:25;
  hence thesis;
end;

theorem Th6:
  for M be (Matrix of n,K), I be Element of Permutations n st I = idseq n
    holds diagonal_of_Matrix M = Path_matrix(I,M)
proof
   let M be (Matrix of n,K), I be Element of Permutations n such that
     A1:I=idseq n;
   set D=diagonal_of_Matrix M;
   set P=Path_matrix(I,M);
   A2:len P=n & len D=n by MATRIX_3:def 7,def 10;
   now let i be Nat such that A3:1<=i & i<= n;
    i in NAT by ORDINAL1:def 13;
     then A4:i in Seg n & dom P=Seg n & n is Element of NAT
       by A2,A3,FINSEQ_1:def 3;
     then i in dom P & I.i=i by A1,FINSEQ_1:4,FINSEQ_2:57;
     then P.i=M*(i,i) & D.i=M*(i,i) by A4,MATRIX_3:def 7,def 10;
     hence P.i=D.i;
   end;
   hence thesis by A2,FINSEQ_1:18;
end;

theorem Th7:
  for M be Upper_Triangular_Matrix of n,K
    holds Det M = (the mult of K) $$ diagonal_of_Matrix M
proof
  let M be Upper_Triangular_Matrix of n,K;
  set aa=the add of K;set mm=the mult of K;
  set KK=the carrier of K;
  set P=Permutations n;set F=FinOmega P;
  set PP=Path_product M;
  idseq n is Element of Group_of_Perm(n) by MATRIX_2:23;
  then reconsider I=idseq n as Element of P by MATRIX_2:def 13;
  I=id Seg n & len P=n by MATRIX_2:20;
  then A1:I is even by MATRIX_2:29;
  {I} c= P & P is finite by MATRIX_2:30;
  then A2:P\{I} c= P & {I} c= P & P\{I} is finite & F=P
    by FINSET_1:16,MATRIX_2:def 17;
  then reconsider II={I},PI=P\{I} as Element of Fin P by FINSUB_1:def 5;
  now per cases;
    suppose PI={};
      then P c= II by XBOOLE_1:37;
      hence aa $$ (F,PP)=aa $$ (II,PP) by A2,XBOOLE_0:def 10;end;
    suppose A3:PI<>{};
      A4:PP.:PI c= {0.K}
      proof
        let y;assume y in PP.:PI;
        then consider x such that
          A5:x in dom PP & x in PI & y = PP.x by FUNCT_1:def 12;
        reconsider f=x as Element of P by A5;
        not f in {I} by A5,XBOOLE_0:def 4;
        then f <> I by TARSKI:def 1;
        then PP.f=0.K by Th5;
        hence thesis by A5,TARSKI:def 1;
      end;
      dom PP=P by FUNCT_2:def 1;
      then PP.:PI<>{} by A3,RELAT_1:152;
      then PP.:PI={0.K} & aa has_a_unity & 0.K = the_unity_wrt aa
        by A4,FVSUM_1:9,10,ZFMISC_1:39;
      then aa $$ (PI,PP)=0.K & PI misses II & PI\/II=II\/P & II\/P=P
        by SETWOP_2:10,XBOOLE_1:12,39,79;
      hence aa $$ (F,PP) = aa $$ (II,PP)+0.K by A2,A3,SETWOP_2:6
                        .= aa $$ (II,PP) by RLVECT_1:def 7;end;
  end;
  hence Det M = PP.I by SETWISEO:26
              .= -(mm"**"Path_matrix(I,M),I) by MATRIX_3:def 8
              .= mm"**"Path_matrix(I,M) by A1,MATRIX_2:def 16
              .= mm"**"diagonal_of_Matrix M by Th6;
end;

theorem Th8:
  for M be Lower_Triangular_Matrix of n,K
    holds Det M=(the mult of K) $$ diagonal_of_Matrix M
proof
  let M be Lower_Triangular_Matrix of n,K;
  Det M = Det M@ & M@ is Upper_Triangular_Matrix of n,K
    by Th2,MATRIXR2:43;
  hence Det M= (the mult of K) $$ diagonal_of_Matrix M@ by Th7
       .=(the mult of K) $$ diagonal_of_Matrix M by Th3;
end;

theorem Th9:
  for X be finite set, n holds
    Card {Y where Y is Subset of X: card Y = n} = card X choose n
proof
  let X be finite set, n;
  set YY={Y where Y is Subset of X: card Y=n};
  set CH=Choose(X,n,1,0);
  deffunc F(set)=(X-->0)+*($1-->1);
  consider f be Function such that
    A1:dom f = YY & for x st x in YY holds f.x = F(x) from FUNCT_1:sch 3;
  A2:rng f c= CH
  proof
    let y; assume y in rng f;
    then consider x such that
      A3:x in dom f & f.x=y by FUNCT_1:def 5;
    consider Y be Subset of X such that
      A4:x=Y & card Y=n by A1,A3;
    Y\/X=X by XBOOLE_1:12;
    then F(Y) in CH by A4,CARD_FIN:19;
    hence thesis by A1,A3,A4;
  end;
  A5:CH c= rng f
  proof
    let y; assume y in CH;
    then consider g be Function of X,{0,1} such that
      A6:g = y & Card (g"{1})=n by CARD_FIN:def 1;
    X=dom g by FUNCT_2:def 1;
    then reconsider Y=g"{1} as Subset of X by RELAT_1:167;
    dom (Y-->1)= Y & dom (X-->0)= X by FUNCOP_1:19;
    then A7:dom F(Y) = X\/Y by FUNCT_4:def 1
                    .= X by XBOOLE_1:12;
    A8:dom g=X by FUNCT_2:def 1;
    now let x such that A9:x in dom g;
      now per cases;
        suppose x in Y;
          then g.x in {1} & x in dom (Y-->1) & (Y-->1).x=1
            by FUNCOP_1:13,19,FUNCT_1:def 13;
          then g.x=1 & F(Y).x=1 by FUNCT_4:14,TARSKI:def 1;
          hence g.x=F(Y).x;end;
        suppose A10:not x in Y;
          then not g.x in {1} & g.x in rng g & rng g c= {0,1}
            by A9,FUNCT_1:def 5,def 13,RELSET_1:12;
          then (X-->0).x=0 & dom (Y-->1)=Y & g.x<> 1 & g.x in {0,1}
            by A9,FUNCOP_1:13,19,TARSKI:def 1;
          then F(Y).x=0 & g.x=0 by A10,FUNCT_4:12,TARSKI:def 2;
          hence g.x=F(Y).x;end;
      end;
      hence g.x=F(Y).x;
    end;
    then F(Y)=g & Y in YY by A6,A7,A8,FUNCT_1:9;
    then f.Y=g & f.Y in rng f by A1,FUNCT_1:def 5;
    hence thesis by A6;
  end;
  for x1,x2 be set st x1 in dom f & x2 in dom f & f.x1 = f.x2 holds x1 = x2
  proof
    let x1,x2 be set such that A11:x1 in dom f & x2 in dom f & f.x1 = f.x2;
    consider Y1 be Subset of X such that
      A12:x1=Y1 & card Y1=n by A1,A11;
    consider Y2 be Subset of X such that
      A13:x2=Y2 & card Y2=n by A1,A11;
    Y1 c= Y2
    proof
      let y such that A14:y in Y1;
      assume A15:not y in Y2;
      dom (Y1-->1)=Y1 & (Y1-->1).y=1 & dom (Y2-->1)=Y2 & (X-->0).y=0
        by A14,FUNCOP_1:13,19;
      then F(Y1).y=1 & F(Y2).y=0 & F(Y1)=f.x1 & F(Y2)=f.x2
        by A1,A11,A12,A13,A14,A15,FUNCT_4:12,14;
      hence thesis by A11;
    end;
    hence thesis by A12,A13,CARD_FIN:1;
  end;
  then f is one-to-one & rng f=CH by A2,A5,FUNCT_1:def 8,XBOOLE_0:def 10;
  then YY,CH are_equipotent by A1,WELLORD2:def 4;
  then Card YY=Card CH by CARD_1:21;
  hence thesis by CARD_FIN:18;
end;

theorem Th10:
  Card 2Set Seg n = n choose 2
proof
  {Y where Y is Subset of Seg n: card Y = 2} = 2Set Seg n
    by SGRAPH1:def 4;
  hence Card 2Set Seg n = card (Seg n) choose 2 by Th9
                       .= n choose 2 by FINSEQ_1:78;
end;

theorem
  for R be Element of Permutations(n) st R=Rev idseq n holds
    R is even iff (n choose 2) mod 2 = 0
proof
  let r be Element of Permutations(n) such that A1:r=Rev idseq n;
  per cases;
  suppose A2:n<2;
    then n=0 or n=1 by NAT_1:23;
    then n*(n-1)/2=0;
    then n choose 2=0 by STIRL2_1:61;
    hence thesis by A2,LAPLACE:11,NAT_D:26;
  end;
  suppose n>=2;
    then reconsider n2=n-2 as Nat by NAT_1:21;
    reconsider R=r as Element of Permutations(n2+2);
    idseq (n2+2) is Element of Group_of_Perm(n2+2) by MATRIX_2:23;
    then reconsider I=idseq (n2+2) as Element of Permutations(n2+2)
      by MATRIX_2:def 13;
    consider K be Fanoian Field;
    set S=2Set Seg (n2+2);
    set D={s where s is Element of S:s in S &
                                     Part_sgn(I,K).s <> Part_sgn(R,K).s};
    A3:D c= S
    proof
      let x;assume x in D;
      then ex s be Element of S st x=s & s in S &
                                   Part_sgn(I,K).s <> Part_sgn(R,K).s;
      hence thesis;
    end;
    then reconsider D as finite set by FINSET_1:13;
    S c= D
    proof
      let x;assume x in S;
      then reconsider s=x as Element of S;
      consider i,j be Nat such that
        A4:i in Seg (n2+2) & j in Seg (n2+2) & i < j & s={i,j} by MATRIX11:1;
      reconsider i'=i,j'=j,n2'=n2 as Element of NAT by ORDINAL1:def 13;
      I.i=i & I.j=j by A4,FUNCT_1:34;
      then A5:Part_sgn(I,K).s=1_K by A4,MATRIX11:def 1;
      A6:len idseq (n2+2)=n2'+2 by FINSEQ_2:55;
      i'<=n2'+2 & j'<=n2+2 by A4,FINSEQ_1:3;
      then reconsider ni=(n2'+2)-i'+1,nj=(n2'+2)-j'+1 as Element of NAT
        by FINSEQ_5:1;
      A7:(n2'+2)+1-i' > (n2'+2)+1-j' by A4,XREAL_1:17;
      i in dom I & j in dom I & ni in Seg (n2+2) & nj in Seg (n2+2)
        by A4,A6,FINSEQ_1:def 3,FINSEQ_5:2;
      then R.i'=I.ni & R.j'=I.nj & I.ni=ni & I.nj=nj
        by A1,A6,FINSEQ_5:61,FUNCT_1:34;
      then Part_sgn(R,K).s=-1_K by A4,A7,MATRIX11:def 1;
      then Part_sgn(I,K).s <> Part_sgn(R,K).s by A5,MATRIX11:22;
      hence thesis;
    end;
    then A8:S=D & FinOmega(S)=S & card S=n choose 2
      by A3,Th10,MATRIX_2:def 17,XBOOLE_0:def 10;
    set CH=n choose 2;
    per cases by NAT_D:12;
      suppose A9:CH mod 2=0;
        then sgn(I,K)= sgn(R,K) & sgn (I,K)=1_K by A8,MATRIX11:7,12;
        hence thesis by A9,MATRIX11:23;end;
      suppose A10:CH mod 2=1;
        then sgn(R,K)= -sgn(I,K) & sgn (I,K)=1_K by A8,MATRIX11:7,12;
        hence thesis by A10,MATRIX11:23;end;end;
end;

theorem Th12:
  for M be (Matrix of n,K),R be Permutation of Seg n st
    R=Rev idseq n &
    for i,j st i in Seg n & j in Seg n & i+j <= n holds M*(i,j) = 0.K
  holds M*R is Upper_Triangular_Matrix of n,K
proof
  let M be (Matrix of n,K),R be Permutation of Seg n such that
    A1:R=Rev idseq n and
    A2:for i,j st i in Seg n & j in Seg n & i+j <= n holds M*(i,j)=0.K;
  set MR=M*R;
  set I=idseq n;
  let i,j be Nat such that A3:[i,j] in Indices MR & i>j;
  reconsider i'=i as Element of NAT by ORDINAL1:def 13;
  A4:Indices MR=[:Seg n,Seg n:] & Indices M=[:Seg n,Seg n:] by MATRIX_1:25;
  then A5:i in Seg n & j in Seg n & [i,j] in Indices M by A3,ZFMISC_1:106;
  then i<= n by FINSEQ_1:3;
  then reconsider ni=n-i'+1 as Element of NAT by FINSEQ_5:1;
  n+1-i<n+1-j by A3,XREAL_1:17;
  then ni+j<(n+1-j)+j by XREAL_1:10;
  then ni in Seg n & dom I=Seg len I & len I=n & ni+j<=n
    by A5,FINSEQ_1:def 3,FINSEQ_2:55,FINSEQ_5:2,NAT_1:13;
  then R.i=I.ni & I.ni=ni & M*(ni,j)=0.K
    by A1,A2,A5,FINSEQ_5:61,FUNCT_1:34;
  hence thesis by A3,A4,MATRIX11:def 4;
end;

theorem Th13:
  for M be (Matrix of n,K),R be Permutation of Seg n st
    R=Rev idseq n &
    for i,j st i in Seg n & j in Seg n & i+j > n+1 holds M*(i,j) = 0.K
  holds M*R is Lower_Triangular_Matrix of n,K
proof
  let M be (Matrix of n,K),R be Permutation of Seg n such that
    A1:R=Rev idseq n and
    A2:for i,j st i in Seg n & j in Seg n & i+j > n+1 holds M*(i,j)=0.K;
  set MR=M*R;
  set I=idseq n;
  let i,j be Nat such that A3:[i,j] in Indices MR & i<j;
  reconsider i'=i as Element of NAT by ORDINAL1:def 13;
  A4:Indices MR=[:Seg n,Seg n:] & Indices M=[:Seg n,Seg n:] by MATRIX_1:25;
  then A5:i in Seg n & j in Seg n & [i,j] in Indices M by A3,ZFMISC_1:106;
  then i<= n by FINSEQ_1:3;
  then reconsider ni=n-i'+1 as Element of NAT by FINSEQ_5:1;
  n+1-i>n+1-j by A3,XREAL_1:17;
  then ni+j>(n+1-j)+j by XREAL_1:10;
  then ni in Seg n & dom I=Seg len I & len I=n & ni+j>n+1
    by A5,FINSEQ_1:def 3,FINSEQ_2:55,FINSEQ_5:2;
  then R.i=I.ni & I.ni=ni & M*(ni,j)=0.K
    by A1,A2,A5,FINSEQ_5:61,FUNCT_1:34;
  hence thesis by A3,A4,MATRIX11:def 4;
end;

theorem
  for M be (Matrix of n,K),R be Element of Permutations(n) st
     R = Rev idseq n &
     ((for i,j st i in Seg n & j in Seg n & i+j <= n holds M*(i,j) = 0.K)or
      (for i,j st i in Seg n & j in Seg n & i+j > n+1 holds M*(i,j) = 0.K))
  holds Det M = -((the mult of K) "**" Path_matrix(R,M), R)
proof
  let M be (Matrix of n,K),R be Element of Permutations(n) such that
    A1:R=Rev idseq n and
    A2:(for i,j st i in Seg n & j in Seg n & i+j <= n holds M*(i,j)=0.K)or
     (for i,j st i in Seg n & j in Seg n & i+j > n+1 holds M*(i,j)=0.K);
  set X=Seg n;set mm=the mult of K;
  idseq n is Element of Group_of_Perm(n) by MATRIX_2:23;
  then reconsider I=idseq n as Element of Permutations(n) by MATRIX_2:def 13;
  reconsider r=Rev idseq n as Permutation of X by A1,MATRIX_2:def 11;
  set Mr=M*r;
  set PR=Path_matrix(R,M);
  set PI=Path_matrix(I,Mr);
  A3:len PI=n & len PR=n by MATRIX_3:def 7;
  A4:now per cases;
    suppose n<1;
      then n=0 & dom PI=Seg n & dom PR=Seg n by A3,FINSEQ_1:def 3,NAT_1:14;
      then PI={} & PR={} by FINSEQ_1:4,RELAT_1:64;
      hence mm "**"PI=mm "**"PR;
    end;
    suppose A5:n>=1;
      then reconsider PRR=PR*R as FinSequence of K by A3,MATRIX_7:34;
      rng I=X by RELAT_1:71;
      then rng (Rev idseq n)=X & dom PR=X by A3,FINSEQ_1:def 3,FINSEQ_5:60;
      then A6:dom PRR=dom R & dom r=X & n is Element of NAT
        by A1,FUNCT_2:67,RELAT_1:46;
      then A7:len PRR=n by A1,FINSEQ_1:def 3;
      now let k be Nat such that A8:1 <=k & k <= len PI;
        k in NAT by ORDINAL1:def 13;
        then
        A9:k in Seg n & Indices M=[:Seg n,Seg n:] by A3,A8,MATRIX_1:25;
        then [k,k] in Indices M by ZFMISC_1:106;
        then consider m be Nat such that
          A10:r.k = m & [m,k] in Indices M & Mr*(k,k) = M*(m,k) by MATRIX11:37;
        len idseq n = n & m in X & k in X by A9,A10,FINSEQ_2:55,ZFMISC_1:106;
        then r.k=I.(n-k+1) & r.m=I.(n-m+1) & n-k+1 in X & n-m+1 in X
          by A6,FINSEQ_5:2,def 3;
        then r.k=n-k+1 & r.m=n-m+1 by FUNCT_1:34;
        then dom PI=Seg n & I.k=k & m in Seg n & dom PR=Seg n & R.m=k
          by A1,A3,A9,A10,FINSEQ_1:def 3,FUNCT_1:34,ZFMISC_1:106;
        then PI.k=Mr*(k,k) & PR.m=M*(m,k) & PR.(R.k)=PRR.k
          by A1,A6,A9,FUNCT_1:22,MATRIX_3:def 7;
        hence PI.k=PRR.k by A1,A10;
      end;
      then PI=PRR by A3,A7,FINSEQ_1:18;
      hence mm "**"PI=mm "**"PR by A3,A5,MATRIX_7:33;end;
  end;
  Mr is Upper_Triangular_Matrix of n,K or Mr is Lower_Triangular_Matrix of n,K
    by A2,Th12,Th13;
  then A11:mm $$ diagonal_of_Matrix Mr = Det Mr by Th7,Th8
                                  .= -(Det M,R) by A1,MATRIX11:46;
  then A12: -(Det M,R)=mm $$ PR by A4,Th6;
  per cases;
    suppose R is even;
      then -(Det M,R)=Det M & -(mm"**"PR,R)=mm"**"PR by MATRIX_2:def 16;
      hence thesis by A4,A11,Th6;end;
    suppose R is odd;
     then -(Det M,R)= -Det M & -(mm"**"PR,R)= -mm"**"PR by MATRIX_2:def 16;
     then -(mm"**"PR,R)+(-Det M) =0.K by A12,VECTSP_1:66;
     hence thesis by VECTSP_1:66;end;
end;

theorem Th15:
  for M be Matrix of n,K
    for M1,M2 be Upper_Triangular_Matrix of n,K st M = M1*M2 holds
      M is Upper_Triangular_Matrix of n,K &
      diagonal_of_Matrix M = mlt(diagonal_of_Matrix M1,diagonal_of_Matrix M2)
proof
  let M be Matrix of n,K;
  let M1,M2 be Upper_Triangular_Matrix of n,K such that A1:M = M1*M2;
  set KK=the carrier of K;  set SS=[:Seg n,Seg n:];
  M1 is Matrix of n,K;
  then A2:width M1=n & len M1=n & width M2=n & len M2=n by MATRIX_1:25;
  thus M is Upper_Triangular_Matrix of n,K
  proof
    let i,j be Nat such that A3:[i,j] in Indices M & i>j;
    set L=Line(M1,i); set C=Col(M2,j);
    reconsider L'=L,C'=C as Element of n-tuples_on KK by MATRIX_1:25;
    set m=mlt(L',C'); set n0=n|->0.K;
    A4:len m=n & len n0= n by FINSEQ_2:109;
    now let k be Nat such that A5:1<= k & k<= n;
A6:    k in NAT by ORDINAL1:def 13;
      Indices M = SS by MATRIX_1:25;
      then i in Seg n & j in Seg n & k in Seg n & dom M2=Seg n & Indices M1=SS&
        Indices M2=SS by A2,A3,A5,A6,FINSEQ_1:def 3,ZFMISC_1:106;
      then A7:L.k=M1*(i,k)&C.k=M2*(k,j)&[i,k] in Indices M1&[k,j] in Indices M2
        & n0.k=0.K
        by A2,FINSEQ_1:4,FINSEQ_2:71,MATRIX_1:def 8,def 9,ZFMISC_1:106;
      A8:dom m = Seg n & k in Seg n by A4,A5,A6,FINSEQ_1:def 3;
      per cases;
        suppose k<=j;
          then k<i by A3,XXREAL_0:2;
          then M1*(i,k)=0.K & m.k=(M1*(i,k))*(M2*(k,j))
            by A7,A8,FVSUM_1:73,MATRIX_2:def 3;
          hence m.k=n0.k by A7,VECTSP_1:44;end;
        suppose k>j;
          then M2*(k,j)=0.K & m.k=(M1*(i,k))*(M2*(k,j))
            by A7,A8,FVSUM_1:73,MATRIX_2:def 3;
          hence m.k=n0.k by A7,VECTSP_1:44;end;
    end;
    then m=n0 by A4,FINSEQ_1:18;
    hence 0.K = L "*" C by MATRIX_3:13
             .= M*(i,j) by A1,A2,A3,MATRIX_3:def 4;
  end;
  set DM=diagonal_of_Matrix M;
  set D1=diagonal_of_Matrix M1;
  set D2=diagonal_of_Matrix M2;
  len D1=n & len D2=n by MATRIX_3:def 10;
  then reconsider D1'=D1,D2'=D2 as Element of n-tuples_on KK by FINSEQ_2:110;
  set m=mlt(D1',D2');
  A9:len m = n & len DM=n by FINSEQ_2:109,MATRIX_3:def 10;
  now let i be Nat such that A10:1<=i & i<=n;
    set L=Line(M1,i); set C=Col(M2,i);
    reconsider L'=L,C'=C as Element of n-tuples_on KK by MATRIX_1:25;
    set mLC=mlt(L',C');
    i in NAT by ORDINAL1:def 13;
    then i in Seg n & dom m=Seg n & Indices M=SS
      by A9,A10,FINSEQ_1:def 3,MATRIX_1:25;
    then D1.i=M1*(i,i) & D2.i=M2*(i,i) & DM.i=M*(i,i) & i in dom m
      & [i,i] in Indices M by MATRIX_3:def 10,ZFMISC_1:106;
    then A11:m.i=(M1*(i,i))*(M2*(i,i)) & DM.i=L"*"C & L"*"C=Sum(mLC)
      by A1,A2,FVSUM_1:73,MATRIX_3:def 4;
    A12:for j st j in Seg n holds
      (j<>i implies mLC.j=0.K) & (j=i implies mLC.j=m.i)
    proof
      let j such that A13:j in Seg n;
      i in NAT by ORDINAL1:def 13;
      then dom M2=Seg n & Indices M1=SS & Indices M2=SS & i in Seg n
        by A2,A10,FINSEQ_1:def 3;
      then A14:L.j=M1*(i,j) & C.j=M2*(j,i) & [i,j] in Indices M1 &
        [j,i] in Indices M2
        by A2,A13,MATRIX_1:def 8,def 9,ZFMISC_1:106;
      per cases;
        suppose A15:i<>j;
          then i<j or j<i by REAL_1:def 5;
          then (M1*(i,j)=0.K or M2*(j,i)=0.K) & mLC.j=(M1*(i,j))*(M2*(j,i))
            by A13,A14,FVSUM_1:74,MATRIX_2:def 3;
          hence thesis by A15,VECTSP_1:44;end;
        suppose i=j;
          hence thesis by A11,A13,A14,FVSUM_1:74;end;
    end;
    set aa=the add of K;
    A16:aa has_a_unity & len mLC=n & n<>0
      by A10,FINSEQ_2:109,FVSUM_1:10;
    consider f be Function of NAT,KK such that
      A17:f.1 = mLC.1 and
      A18:for k be Element of NAT st 0 <> k & k < n holds
           f.(k+1) = aa.(f.k,mLC.(k+1)) and
      A19:DM.i = f.n by A11,A16,FINSOP_1:def 1;
    defpred P[Nat] means 1<=$1 & $1 <= n implies
      ($1 < i implies f.$1=0.K) & ($1 >= i implies f.$1=m.i);
    A20:P[0];
    A21:for k be Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT such that A22:P[k];set k1=k+1;
      assume A23:1<=k1 & k1 <= n;
      then A24:k1 in Seg n;
      per cases;
        suppose k=0;
          then (f.k1=0.K & k1<i)or(f.k1=mLC.k1 & k1=i)
            by A10,A12,A17,A24,XXREAL_0:1;
          hence thesis by A12,A24;end;
        suppose A25:k>0;
          1<=k & k<n by A23,A25,NAT_1:13,14;
          then A26:f.k1 = aa.(f.k,mLC.k1) by A18;
          per cases by REAL_1:def 5;
            suppose A27:k1<i;
              then f.k1=0.K+0.K by A12,A22,A23,A24,A25,A26,NAT_1:13,14;
              hence thesis by A27,RLVECT_1:def 7;end;
            suppose A28:k1=i;
              then f.k1=0.K+(M1*(i,i))*(M2*(i,i))
                by A11,A12,A22,A23,A24,A25,A26,NAT_1:13,14;
              hence thesis by A11,A28,RLVECT_1:def 7;end;
            suppose A29:k1>i;
              then f.k1=(M1*(i,i))*(M2*(i,i))+0.K
                by A11,A12,A22,A23,A24,A25,A26,NAT_1:13,14;
              hence thesis by A11,A29,RLVECT_1:def 7;end;
          end;
    end;
    for k be Element of NAT holds P[k] from NAT_1:sch 1(A20,A21);
    then P[n'] & 1<=n by A10,NAT_1:14;
    hence m.i=DM.i by A10,A19;
  end;
  hence thesis by A9,FINSEQ_1:18;
end;

theorem
  for M be Matrix of n,K
    for M1,M2 be Lower_Triangular_Matrix of n,K st M = M1*M2 holds
      M is Lower_Triangular_Matrix of n,K &
      diagonal_of_Matrix M = mlt(diagonal_of_Matrix M1,diagonal_of_Matrix M2)
proof
  let M be Matrix of n,K;
  let M1,M2 be Lower_Triangular_Matrix of n,K such that A1:M = M1*M2;
  M1 is Matrix of n,K;
  then A2:width M1=n & width M2=n & len M2=n & len (M1@)=n & width (M2@)=n &
    len (M2@)=n by MATRIX_1:25;
  set D1=diagonal_of_Matrix M1;set D1'=diagonal_of_Matrix M1@;
  set D2=diagonal_of_Matrix M2;set D2'=diagonal_of_Matrix M2@;
  len D1=n & len D2=n by MATRIX_3:def 10;
  then reconsider d1=D1,d2=D2 as Element of n-tuples_on the carrier of K by
FINSEQ_2:110;
  now per cases;
    suppose n=0;
      then len (M2@*M1@) =0 & len (M@)=0 by MATRIX_1:25;
      then M2@*M1@={} & M@={} by FINSEQ_1:25;
      hence M@ = M2@*M1@;end;
    suppose n>0;
      hence M@ = (M2@)*(M1@) by A1,A2,MATRIX_3:24;end;
  end;
  then M@ = (M2@)*(M1@) &
  M1@ is Upper_Triangular_Matrix of n,K &
  M2@ is Upper_Triangular_Matrix of n,K by Th2;
  then A3:M@ is Upper_Triangular_Matrix of n,K &
    diagonal_of_Matrix M@ = mlt(D2',D1') by Th15;
  then diagonal_of_Matrix M = mlt(D2',D1') by Th3
                           .= mlt(D2,D1') by Th3
                           .= mlt(d2,d1) by Th3
                           .= mlt(D1,D2) by FVSUM_1:77;
  hence thesis by A3,Th2;
end;

begin :: The Rank of Matrices

definition
  let D be non empty set; let M be Matrix of D; let n,m be Nat;
  let nt be Element of n-tuples_on NAT;
  let mt be Element of m-tuples_on NAT;
  func Segm(M,nt,mt) -> Matrix of n,m,D means :Def1:
    for i,j being Nat st [i,j] in Indices it holds it*(i,j)=M*(nt.i,mt.j);
  existence
  proof
    deffunc s(set,set)=M*(nt.$1,mt.$2);
    reconsider m'=m,n'=n as Element of NAT by ORDINAL1:def 13;
    ex S be Matrix of n',m',D st
      for i,j being Nat st [i,j] in Indices S holds S*(i,j) = s(i,j)
        from MATRIX_1:sch 1; then
    consider S be Matrix of n',m',D such that
A1: for i,j being Nat st [i,j] in Indices S holds S*(i,j) = s(i,j);
    reconsider S as Matrix of n,m,D;
    take S;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let S1,S2 be Matrix of n,m,D such that
A2: for i,j be Nat st [i,j] in Indices S1 holds S1*(i,j)=M*(nt.i,mt.j) and
A3: for i,j be Nat st [i,j] in Indices S2 holds S2*(i,j)=M*(nt.i,mt.j);
    now let i,j be Nat such that A4:[i,j] in Indices S1;
      reconsider i'=i,j'=j as Element of NAT by ORDINAL1:def 13;
      [i,j] in Indices S2 & S1*(i,j)=M*(nt.i',mt.j') by A2,A4,MATRIX_1:27;
      hence S1*(i,j)=S2*(i,j) by A3;
    end;
    hence thesis by MATRIX_1:28;
  end;
end;

 reserve A,B for (Matrix of D),
      A' for Matrix of n',m',D,
      M' for Matrix of n',m',K,
      nt,nt1,nt2 for Element of n-tuples_on NAT,
      mt,mt1 for Element of m-tuples_on NAT,
      M for Matrix of K;

theorem Th17:
  [:rng nt,rng mt:] c= Indices A implies
   ( [i,j] in Indices Segm(A,nt,mt) iff [nt.i,mt.j] in Indices A )
proof
  assume A1:[:rng nt,rng mt:] c= Indices A;
  set S=Segm(A,nt,mt);
  thus [i,j] in Indices Segm(A,nt,mt) implies [nt.i,mt.j] in Indices A
  proof
    assume [i,j] in Indices S;
    then [i,j] in [:Seg n,Seg width S:] by MATRIX_1:26;
    then A2:i in Seg n & j in Seg width S by ZFMISC_1:106;
    then width S=m & dom nt=Seg n & dom mt=Seg m
      by Th1,FINSEQ_1:4,FINSEQ_2:144;
    then nt.i in rng nt & mt.j in rng mt by A2,FUNCT_1:def 5;
    then [nt.i,mt.j] in [:rng nt,rng mt:] by ZFMISC_1:106;
    hence thesis by A1;
  end;
  assume A3:[nt.i,mt.j] in Indices A;
  A4:i in dom nt
  proof
    assume not i in dom nt;
    then nt.i = {} & dom A=Seg len A
      by FINSEQ_1:def 3,FUNCT_1:def 4;
    then 0 in Seg len A by A3,ZFMISC_1:106;
    hence thesis by FINSEQ_1:3;
  end;
  A5:j in dom mt
  proof
    assume not j in dom mt;
    then mt.j = {} by FUNCT_1:def 4;
    then 0 in Seg width A by A3,ZFMISC_1:106;
    hence thesis by FINSEQ_1:3;
  end;
  n<>0 by A4,FINSEQ_1:4,FINSEQ_2:144;
  then width S=m & dom nt=Seg n & dom mt=Seg m by Th1,FINSEQ_2:144;
  then [i,j] in [:Seg n,Seg width S:] by A4,A5,ZFMISC_1:106;
  hence thesis by MATRIX_1:26;
end;

theorem Th18:
  [:rng nt,rng mt:] c= Indices A & (n = 0 iff m = 0) implies
    Segm(A,nt,mt)@ = Segm(A@,mt,nt)
proof
  assume that A1:[:rng nt,rng mt:] c= Indices A and A2:n = 0 iff m = 0;
  set SA=Segm(A,nt,mt);set A'=A@;set SA'=Segm(A',mt,nt);
   per cases;
     suppose n=0;
       then width SA=0 by A2,Th1;
       then len (SA@)=0 & len SA'=0 by A2,Th1,MATRIX_1:def 7;
       then SA@ ={} & SA' ={} by FINSEQ_1:25;
       hence thesis;end;
     suppose n>0;
      then A3:m>0 & width SA=m & len SA=n & width SA'=n & len SA'=m
        by A2,Th1;
      then A4:len (SA@) = len SA' & width (SA@) = width SA' by MATRIX_2:12;
      now let i,j be Nat such that A5:[i,j] in Indices (SA@);
        reconsider i'=i,j'=j as Element of NAT by ORDINAL1:def 13;
        [j',i'] in Indices SA & Indices SA=[:Seg n,Seg m:]
           by A3,A5,MATRIX_1:26,def 7;
        then [nt.j,mt.i] in Indices A & SA*(j',i')=A*(nt.j,mt.i) & j' in Seg n&
          i' in Seg m & (SA@)*(i',j')=SA*(j',i') & Indices SA'=[:Seg m,Seg n:]
          by A1,A3,Def1,Th17,MATRIX_1:26,def 7,ZFMISC_1:106;
        then (SA@)*(i',j')=A'*(mt.i,nt.j) & [i',j'] in Indices SA'
          by MATRIX_1:def 7,ZFMISC_1:106;
        hence (SA@)*(i,j)=SA'* (i,j) by Def1;
      end;
      hence thesis by A4,MATRIX_1:21;end;
end;

theorem Th19:
  [:rng nt,rng mt:] c= Indices A & (m=0 implies n=0) implies
    Segm(A,nt,mt) = Segm(A@,mt,nt)@
proof
  assume that A1:[:rng nt,rng mt:] c= Indices A and A2:m=0 implies n=0;
  set S=Segm(A,nt,mt);set S'=Segm(A@,mt,nt);
   per cases;
    suppose A3:n=0;
      len S'=0 or len S'>0 & len S'=m by MATRIX_1:def 3;
      then width S'=0 & len S=0 by A3,MATRIX_1:24,def 3,def 4;
      then len (S'@)=0 & S={} by FINSEQ_1:25,MATRIX_1:def 7;
      hence thesis by FINSEQ_1:25;
    end;
    suppose A4:n>0;
  then len S=n & width S=m & m>0 by A2,Th1;
  then (S@)@ = S & width (S@)=n & S@ = S'
    by A1,A4,Th18,MATRIX_2:12,15;
  hence thesis;end;
end;

theorem Th20:
  for A be Matrix of 1,D holds A = <*<* A*(1,1) *>*>
proof
  let A be Matrix of 1,D;
  reconsider AA=<*<* A*(1,1) *>*> as Matrix of 1,D by MATRIX_1:15;
  now let i,j be Nat such that A1:[i,j] in Indices A;
    Indices A=[:Seg 1,Seg 1:] by MATRIX_1:25;
    then i in {1} & j in {1} by A1,FINSEQ_1:4,ZFMISC_1:106;
    then i=1 & j=1 by TARSKI:def 1;
    hence AA*(i,j)=A*(i,j) by MATRIX_2:5;
  end;
  hence thesis by MATRIX_1:28;
end;

theorem Th21:
  n = 1 & m = 1 implies Segm(A,nt,mt) = <*<* A*(nt.1,mt.1) *>*>
proof
  assume A1:n = 1 & m = 1;
  then 1 in Seg 1 & Indices Segm(A,nt,mt)=[:Seg 1,Seg 1:]
    by MATRIX_1:25;
  then [1,1] in Indices Segm(A,nt,mt) by ZFMISC_1:106;
  then Segm(A,nt,mt)*(1,1)=A*(nt.1,mt.1) by Def1;
  hence thesis by A1,Th20;
end;

theorem Th22:
  for A be Matrix of 2,D holds A = ( A*(1,1),A*(1,2) )][( A*(2,1),A*(2,2) )
proof
  let A be Matrix of 2,D;
  reconsider AA=(A*(1,1),A*(1,2))][(A*(2,1),A*(2,2)) as Matrix of 2,D;
  now let i,j be Nat such that A1:[i,j] in Indices A;
    Indices A=[:Seg 2,Seg 2:] by MATRIX_1:25;
    then i in {1,2} & j in {1,2} by A1,FINSEQ_1:4,ZFMISC_1:106;
    then (i=1 or i=2)&(j=1 or j=2) by TARSKI:def 2;
    hence AA*(i,j)=A*(i,j) by MATRIX_2:6;
  end;
  hence thesis by MATRIX_1:28;
end;

theorem Th23:
  n = 2 & m = 2 implies Segm(A,nt,mt) =
     ( A*(nt.1,mt.1),A*(nt.1,mt.2) )][( A*(nt.2,mt.1),A*(nt.2,mt.2) )
proof
  set S=Segm(A,nt,mt);set I=Indices S;
  assume A1:n = 2 & m = 2;
  then 1 in Seg 2 & 2 in Seg 2 & I=[:Seg 2,Seg 2:]
    by MATRIX_1:25;
  then [1,1] in I & [1,2] in I & [2,1] in I & [2,2] in I by ZFMISC_1:106;
  then S*(1,1)=A*(nt.1,mt.1) & S*(1,2)=A*(nt.1,mt.2) &
       S*(2,1)=A*(nt.2,mt.1) & S*(2,2)=A*(nt.2,mt.2) by Def1;
  hence thesis by A1,Th22;
end;

theorem Th24:
  i in Seg n & rng mt c= Seg width A implies
    Line(Segm(A,nt,mt),i) = Line(A,nt.i) * mt
proof
  set S=Segm(A,nt,mt);set Li=Line(S,i);set LA=Line(A,nt.i);
  assume that A1:i in Seg n and A2:rng mt c= Seg width A;
  A3:len LA=width A & width S=m by A1,Th1,FINSEQ_1:4,MATRIX_1:def 8;
  then dom LA=Seg width A & len Li=m by FINSEQ_1:def 3,MATRIX_1:def 8;
  then A4:dom (LA*mt)=dom mt & dom mt=Seg m & dom Li=Seg m
    by A2,FINSEQ_1:def 3,FINSEQ_2:144,RELAT_1:46;
  now let x such that A5:x in dom Li;
    consider k such that A6:k=x & 1<=k & k<=m by A4,A5;
    mt.k in rng mt & [i,k] in [:Seg n,Seg width S:]
      by A1,A3,A4,A5,A6,FUNCT_1:def 5,ZFMISC_1:106;
    then (LA*mt).k=LA.(mt.k) & LA.(mt.k)=A*(nt.i,mt.k) & [i,k] in Indices S &
      Li.k=S*(i,k) by A2,A3,A4,A5,A6,FUNCT_1:22,MATRIX_1:26,def 8;
    hence Li.x=(LA*mt).x by A6,Def1;
  end;
  hence thesis by A4,FUNCT_1:9;
end;

theorem Th25:
  i in Seg n & j in Seg n & nt.i = nt.j implies
    Line(Segm(A,nt,mt),i) = Line(Segm(A,nt,mt),j)
proof
  set S=Segm(A,nt,mt);set Li=Line(S,i);set Lj=Line(S,j);
  assume that A1:i in Seg n & j in Seg n and A2:nt.i=nt.j;
  A3:len Li=width S & len Lj=width S by MATRIX_1:def 8;
  now let k be Nat such that A4:1<=k & k<=width S;
    k in NAT by ORDINAL1:def 13;
    then
    A5:k in Seg width S by A4;
    then [i,k] in [:Seg n,Seg width S:] & [j,k] in [:Seg n,Seg width S:]
      by A1,ZFMISC_1:106;
    then [i,k] in Indices S & [j,k] in Indices S by MATRIX_1:26;
    then S*(i,k)=A*(nt.i,mt.k) & S*(j,k)=A*(nt.j,mt.k) & S*(i,k) =Li.k &
      S*(j,k)=Lj.k by A5,Def1,MATRIX_1:def 8;
    hence Li.k=Lj.k by A2;
  end;
  hence thesis by A3,FINSEQ_1:18;
end;

theorem Th26:
   i in Seg n & j in Seg n & nt.i = nt.j & i<>j implies
     Det Segm(M,nt,nt1) = 0.K
proof
  assume that A1:i in Seg n & j in Seg n & nt.i = nt.j and A2:i<>j;
  Line(Segm(M,nt,nt1),i) = Line(Segm(M,nt,nt1),j) & (i<j or j<i)
    by A1,A2,Th25,REAL_1:def 5;
  hence thesis by A1,MATRIX11:50;
end;

theorem Th27:
  not nt is one-to-one implies Det Segm(M,nt,nt1) = 0.K
proof
  assume not nt is one-to-one;
  then consider x,y such that
    A1:x in dom nt & y in dom nt & nt.x=nt.y & x<>y by FUNCT_1:def 8;
  A2:dom nt=Seg n by FINSEQ_2:144;
  then consider i such that A3:x=i & 1<=i & i<=n by A1;
  consider j such that A4:y=j & 1<=j & j<=n by A1,A2;
  i in Seg n & j in Seg n by A3,A4;
  hence thesis by A1,A3,A4,Th26;
end;

theorem Th28:
  j in Seg m & rng nt c= Seg len A implies
    Col(Segm(A,nt,mt),j) = Col(A,mt.j) * nt
proof
  set S=Segm(A,nt,mt);set Cj=Col(S,j);set CA=Col(A,mt.j);
  assume that A1:j in Seg m and A2:rng nt c= Seg len A;
  len CA=len A & len S=n by MATRIX_1:def 3,def 9;
  then A3:dom CA=Seg len A & len Cj=n & dom S=Seg n
    by FINSEQ_1:def 3,MATRIX_1:def 9;
  then A4:dom (CA*nt)=dom nt & dom nt=Seg n & dom Cj=Seg n & dom A=Seg len A
    by A2,FINSEQ_1:def 3,FINSEQ_2:144,RELAT_1:46;
  now let x such that A5:x in dom Cj;
    consider k such that A6:k=x & 1<=k & k<=n by A4,A5;
    nt.k in rng nt & [k,j] in [:Seg n,Seg m:] & m=width S
      by A1,A4,A5,A6,Th1,FUNCT_1:def 5,ZFMISC_1:106;
    then (CA*nt).k=CA.(nt.k) & CA.(nt.k)=A*(nt.k,mt.j) & [k,j] in Indices S &
      Cj.k=S*(k,j) by A2,A3,A4,A5,A6,FUNCT_1:22,MATRIX_1:def 9;
    hence Cj.x=(CA*nt).x by A6,Def1;
  end;
  hence thesis by A4,FUNCT_1:9;
end;

theorem Th29:
  i in Seg m & j in Seg m & mt.i = mt.j implies
    Col(Segm(A,nt,mt),i) = Col(Segm(A,nt,mt),j)
proof
  set S=Segm(A,nt,mt);set Ci=Col(S,i);set Cj=Col(S,j);
  assume that A1:i in Seg m & j in Seg m and A2:mt.i=mt.j;
  A3:len Ci=len S & len Cj=len S by MATRIX_1:def 9;
  now let k be Nat such that A4:1<=k & k<=len S;
    k in NAT by ORDINAL1:def 13;
    then k in Seg len S by A4;
    then A5:k in Seg n & k in dom S by FINSEQ_1:def 3,MATRIX_1:def 3;
    then [k,i] in [:Seg n,Seg m:] & [k,j] in [:Seg n,Seg m:] & width S = m
      by A1,Th1,FINSEQ_1:4,ZFMISC_1:106;
    then [k,i] in Indices S & [k,j] in Indices S by MATRIX_1:26;
    then S*(k,i)=A*(nt.k,mt.i) & S*(k,j)=A*(nt.k,mt.j) & S*(k,i) =Ci.k &
      S*(k,j)=Cj.k by A5,Def1,MATRIX_1:def 9;
    hence Ci.k=Cj.k by A2;
  end;
  hence thesis by A3,FINSEQ_1:18;
end;

theorem Th30:
   i in Seg m & j in Seg m & mt.i = mt.j & i<>j implies
     Det Segm(M,mt1,mt) = 0.K
proof
  assume that A1:i in Seg m & j in Seg m & mt.i = mt.j and A2:i<>j;
  set S = Segm(M,mt1,mt);
  width S=m by MATRIX_1:25;
  then Col(S,i)=Line(S@,i) & Col(S,j)=Line(S@,j) by A1,MATRIX_2:17;
  then Line(S@,i) = Line(S@,j) & (i<j or j<i)
    by A1,A2,Th29,REAL_1:def 5;
  hence 0.K = Det (S@) by A1,MATRIX11:50
          .= Det S by MATRIXR2:43;
end;

theorem Th31:
  not mt is one-to-one implies Det Segm(M,mt1,mt) = 0.K
proof
  assume not mt is one-to-one;
  then consider x,y such that
    A1:x in dom mt & y in dom mt & mt.x=mt.y & x<>y by FUNCT_1:def 8;
  A2:dom mt=Seg m by FINSEQ_2:144;
  then consider i such that
    A3:x=i & 1<=i & i<=m by A1;
  consider j such that
    A4:y=j & 1<=j & j<=m by A1,A2;
  i in Seg m & j in Seg m by A3,A4;
  hence thesis by A1,A3,A4,Th30;
end;

theorem Th32:
  for nt,nt1 be Element of n-tuples_on NAT st
      nt is one-to-one & nt1 is one-to-one & rng nt = rng nt1
    ex perm be Permutation of Seg n st nt1 = nt * perm
proof
  let nt,nt1 such that A1:nt is one-to-one & nt1 is one-to-one and
                       A2:rng nt = rng nt1;
  reconsider nt'=nt" as Function;
  dom nt=Seg n & dom nt1 = Seg n & dom nt' = rng nt1 & rng nt' = dom nt
    by A1,A2,FINSEQ_2:144,FUNCT_1:55;
  then A3:dom (nt'*nt1)=Seg n & rng (nt'*nt1)=Seg n by RELAT_1:46,47;
  then reconsider nn=nt'*nt1 as Function of Seg n,Seg n by FUNCT_2:3;
  nt' is one-to-one by A1,FUNCT_1:62;
  then nn is one-to-one onto by A1,A3,FUNCT_1:46,FUNCT_2:def 3;
  then reconsider nn as Permutation of Seg n by FUNCT_2:def 4;
  take nn;
  thus nt * nn = (nt*nt')*nt1 by RELAT_1:55
              .= (id rng nt1)*nt1 by A1,A2,FUNCT_1:61
              .= nt1 by RELAT_1:80;
end;

theorem Th33:
  for f be Function of Seg n,Seg n st nt1 = nt * f
    holds Segm(A,nt1,mt) = Segm(A,nt,mt) * f
proof
  let f be Function of Seg n,Seg n such that A1:nt1 = nt * f;
  set S1=Segm(A,nt1,mt);set S=Segm(A,nt,mt);set Sf=S*f;
  now let i,j be Nat such that A2:[i,j] in Indices S1;
   Indices S1=Indices S by MATRIX_1:27;
   then consider k be Nat such that
     A3:f.i = k & [k,j] in Indices S & Sf*(i,j) = S*(k,j) by A2,MATRIX11:37;
   reconsider i'=i,j'=j,k'=k as Element of NAT by ORDINAL1:def 13;
   Indices S1=[:Seg n,Seg width S1:] by MATRIX_1:26;
   then i in Seg n & Seg n=dom nt1 by A2,FINSEQ_2:144,ZFMISC_1:106;
   then nt1.i'=nt.(f.i) by A1,FUNCT_1:22;
   hence S1*(i,j) = A*(nt.k',mt.j') by A2,A3,Def1
                 .= Sf*(i,j) by A3,Def1;
  end;
  hence thesis by MATRIX_1:28;
end;

theorem Th34:
  for f be Function of Seg m,Seg m st mt1 = mt * f holds
    Segm(A,nt,mt1)@ = Segm(A,nt,mt)@ * f
proof
  let f be Function of Seg m,Seg m such that A1:mt1 = mt * f;
  set S1=Segm(A,nt,mt1);set S=Segm(A,nt,mt);
  per cases;
    suppose A2:m=0 or n=0;
      len S1=n & len S=n by MATRIX_1:def 3;
      then width S1=0 & width S=0 by A2,Th1,MATRIX_1:def 4;
      then len (S1@)=0 & len (S@)=0 by MATRIX_1:def 7;
      then S1@={} & S@={} by FINSEQ_1:25;
      hence thesis by RELAT_1:62;end;
    suppose A3:m>0 & n>0;
      then len S=n & width S=m & len S1=n & width S1=m
        by Th1;
      then A4:width (S@)=n & len (S@)=m & width (S1@)=n & len (S1@)=m
        by A3,MATRIX_2:12;
      then reconsider S'=S@,S1'=S1@ as Matrix of m,n,D by A3,MATRIX_1:20;
      set Sf=S'*f;
      now let i,j be Nat such that A5:[i,j] in Indices S1';
        Indices S1'=Indices S' by MATRIX_1:27;
        then consider k be Nat such that
          A6:f.i=k & [k,j] in Indices S' & Sf*(i,j)=S'*(k,j)by A5,MATRIX11:37;
        reconsider i'=i,j'=j,k'=k as Element of NAT by ORDINAL1:def 13;
        Indices S1'=[:Seg m,Seg n:] by A4,MATRIX_1:26;
        then i in Seg m & Seg m=dom mt1 by A5,FINSEQ_2:144,ZFMISC_1:106;
        then mt1.i'=mt.(f.i) & [j,i] in Indices S1 & [j,k] in Indices S
          by A1,A5,A6,FUNCT_1:22,MATRIX_1:def 7;
        then S1'*(i,j) = S1*(j,i) & S1*(j',i') = A*(nt.j',mt.k') &
              S'*(k,j) = S*(j,k)  & S*(j',k')  = A*(nt.j',mt.k')
          by A6,Def1,MATRIX_1:def 7;
        hence S1'*(i,j)=Sf*(i,j) by A6;
      end;
      hence thesis by MATRIX_1:28;end;
end;

theorem Th35:
  for perm be Element of Permutations n st nt1 = nt2 * perm holds
    Det Segm(M,nt1,nt) = -(Det Segm(M,nt2,nt),perm) &
    Det Segm(M,nt,nt1) = -(Det Segm(M,nt,nt2),perm)
proof
  let perm be Element of Permutations n such that A1:nt1=nt2*perm;
  reconsider Perm=perm as Permutation of Seg n by MATRIX_2:def 11;
  Segm(M,nt1,nt) = Segm(M,nt2,nt) * Perm by A1,Th33;
  hence Det Segm(M,nt1,nt) = -(Det Segm(M,nt2,nt),perm) by MATRIX11:46;
  thus Det Segm(M,nt,nt1) = Det Segm(M,nt,nt1)@ by MATRIXR2:43
                         .= Det (Segm(M,nt,nt2)@ * Perm) by A1,Th34
                         .= -(Det (Segm(M,nt,nt2)@),perm) by MATRIX11:46
                         .= -(Det Segm(M,nt,nt2),perm) by MATRIXR2:43;
end;

Lm1:rng nt = rng nt1 & nt is one-to-one implies nt1 is one-to-one
proof
  assume A1:rng nt = rng nt1 & nt is one-to-one;
  len nt=n & len nt1=n by FINSEQ_2:109;
  hence thesis by A1,FINSEQ_4:76;
end;

theorem Th36:
  for nt,nt1,nt',nt1' be Element of n-tuples_on NAT st
      rng nt = rng nt' & rng nt1 = rng nt1' holds
    Det Segm(M,nt,nt1) =  Det Segm(M,nt',nt1') or
    Det Segm(M,nt,nt1) = -Det Segm(M,nt',nt1')
proof
  let nt,nt1,nt',nt1' be Element of n-tuples_on NAT such that
    A1:rng nt=rng nt' &  rng nt1=rng nt1';
  set S=Segm(M,nt,nt1);set S'=Segm(M,nt',nt1');set S1'=Segm(M,nt,nt1');
   per cases;
    suppose A2:not nt is one-to-one or not nt1 is one-to-one;
      then not nt' is one-to-one or not nt1' is one-to-one by A1,Lm1;
      then Det S=0.K & Det S'=0.K
        by A2,Th27,Th31;
      hence thesis;end;
    suppose A3:nt is one-to-one & nt1 is one-to-one;
      then nt' is one-to-one by A1,Lm1;
      then consider perm be Permutation of Seg n such that
        A4:nt = nt' * perm by A1,A3,Th32;
      nt1' is one-to-one by A1,A3,Lm1;
      then consider perm1 be Permutation of Seg n such that
        A5:nt1 = nt1' * perm1 by A1,A3,Th32;
      reconsider perm,perm1 as Element of Permutations n by MATRIX_2:def 11;
      per cases;
        suppose A6:perm1 is even;
          Det S = -(Det S1',perm1) & (perm is even or perm is odd)
            & Det S1' = -(Det S',perm) by A4,A5,Th35;
          then Det S=Det S1' & (Det S1'=Det S' or Det S1'=-Det S')
            by A6,MATRIX_2:def 16;
          hence thesis;end;
        suppose A7:perm1 is odd;
          Det S = -(Det S1',perm1) & (perm is even or perm is odd)
            & Det S1' = -(Det S',perm) by A4,A5,Th35;
          then Det S=-Det S1' & (Det S1'=Det S' or Det S1'=-Det S')
            by A7,MATRIX_2:def 16;
          then Det S=-Det S' or Det S = 0.K + --Det S' by RLVECT_1:def 7;
          then Det S=-Det S' or Det S = 0.K -(-Det S');
          then Det S=-Det S' or Det S +(- Det S') =0.K by VECTSP_2:22;
          hence thesis by VECTSP_1:66;end;end;
end;

theorem Th37:
  for F,Fmt be FinSequence of D,nt,mt st
       len F = width A' & Fmt = F * mt & [:rng nt,rng mt:] c= Indices A'
    for i,j st nt"{j} = {i} holds
      RLine(Segm(A',nt,mt),i,Fmt) = Segm(RLine(A',j,F),nt,mt)
proof
  let F,Fmt be FinSequence of D,nt,mt such that
      A1:len F = width A' and A2:Fmt = F * mt & [:rng nt,rng mt:]c=Indices A';
  let i,j such that A3:nt"{j} = {i};
  set S=Segm(A',nt,mt);set R=RLine(A',j,F);
  set RS=RLine(S,i,Fmt);set SR=Segm(R,nt,mt);
  i in {i} by TARSKI:def 1;
  then i in dom nt & nt.i in {j} by A3,FUNCT_1:def 13;
  then A4:n<>0 & nt.i=j & nt.i in rng nt
    by FINSEQ_1:4,FINSEQ_2:144,FUNCT_1:def 5,TARSKI:def 1;
  then A5:n>0;
  then A6:width S=m by MATRIX_1:24;
  rng mt={} or [:rng nt,rng mt:]<>{} by A4,ZFMISC_1:113;
  then rng mt c= Seg width A' & dom F=Seg width A'
    by A1,A2,FINSEQ_1:def 3,XBOOLE_1:2,ZFMISC_1:138;
  then A7:dom Fmt=dom mt & dom mt=Seg m by A2,FINSEQ_2:144,RELAT_1:46;
  then A8:len Fmt=width S by A6,FINSEQ_1:def 3;
  A9:Indices A'=Indices R & Indices SR=Indices S by MATRIX_1:27;
  now let k,l be Nat such that A10:[k,l] in Indices SR;
    reconsider k'=k,l'=l as Element of NAT by ORDINAL1:def 13;
    Indices S=[:Seg n,Seg m:] by A5,MATRIX_1:24;
    then A11:l in dom mt & k in Seg n & dom nt=Seg n
      by A7,A9,A10,FINSEQ_2:144,ZFMISC_1:106;
    A12:SR*(k,l) = R*(nt.k',mt.l') & [nt.k',mt.l'] in Indices A'
      by A2,A9,A10,Def1,Th17;
    per cases;
      suppose k=i;
        then RS*(k,l)= Fmt.l & SR*(k,l)=F.(mt.l)
          by A1,A4,A8,A9,A10,A12,MATRIX11:def 3;
        hence RS*(k,l)=SR*(k,l) by A2,A11,FUNCT_1:23;end;
      suppose k<>i;
        then not k in nt"{j} by A3,TARSKI:def 1;
        then not nt.k in {j}  by A11,FUNCT_1:def 13;
        then nt.k<>j by TARSKI:def 1;
        then SR*(k,l)=A'*(nt.k',mt.l') & RS*(k,l)=S*(k,l)
          by A1,A4,A8,A9,A10,A12,MATRIX11:def 3;
        hence RS*(k,l)=SR*(k,l) by A9,A10,Def1;end;
  end;
  hence thesis by MATRIX_1:28;
end;

theorem Th38:
  for F be FinSequence of D,i,nt st not i in rng nt &
                                   [:rng nt,rng mt:] c= Indices A'
    holds Segm(A',nt,mt) = Segm(RLine(A',i,F),nt,mt)
proof
  let F be FinSequence of D,i,nt such that A1:not i in rng nt and
                                           A2:[:rng nt,rng mt:] c= Indices A';
  set R=RLine(A',i,F);set S=Segm(A',nt,mt);set SR=Segm(R,nt,mt);
  per cases;
    suppose len F<>width A';
      hence thesis by MATRIX11:def 3;end;
    suppose A3:len F=width A';
      A4:Indices SR = Indices S by MATRIX_1:27;
      now let k,m be Nat such that A5:[k,m] in Indices SR;
        reconsider K=k,M=m as Element of NAT by ORDINAL1:def 13;
        Indices SR=[:Seg n,Seg width SR:] by MATRIX_1:26;
        then k in Seg n & dom nt=Seg n by A5,FINSEQ_2:144,ZFMISC_1:106;
        then i <> nt.k & [nt.K,mt.M] in Indices A'
          by A1,A2,A4,A5,Th17,FUNCT_1:def 5;
        then S*(K,M)=A'*(nt.K,mt.M) & A'*(nt.K,mt.M)=R*(nt.K,mt.M)
          by A3,A4,A5,Def1,MATRIX11:def 3;
        hence SR*(k,m)=S*(k,m) by A5,Def1;
     end;
     hence thesis by MATRIX_1:28;end;
end;

theorem Th39:
  i in Seg n' & i in rng nt & [:rng nt,rng mt:] c= Indices A' implies
    ex nt1 st rng nt1 = rng nt\{i} \/ {j} &
              Segm(RLine(A',i,Line(A',j)),nt,mt) = Segm(A',nt1,mt)
proof
   assume that A1:i in Seg n' & i in rng nt and
               A2:[:rng nt,rng mt:] c= Indices A';
   defpred P[set,set] means for k st k=$1 holds
     (nt.k=i implies $2=j) & (nt.k<>i implies $2=nt.k);
   A3:for k be Nat st k in Seg n ex x be Element of NAT st P[k,x]
   proof
     let k be Nat such that k in Seg n;
     P[k,j] or P[k,nt.k] & nt.k is Element of NAT by ORDINAL1:def 13;
     hence thesis;
   end;
   consider p be FinSequence of NAT such that
     A4:dom p = Seg n and
     A5:for k being Nat st k in Seg n holds P[k,p.k] from FINSEQ_1:sch 5(A3);
   len p = n by A4,FINSEQ_1:def 3;
   then reconsider p'=p as Element of n-tuples_on NAT by FINSEQ_2:110;
   take p';
   A6:rng p' c= (rng nt\{i}) \/ {j}
   proof
     let y;assume y in rng p';
     then consider x be set such that
       A7:x in dom p & p.x=y by FUNCT_1:def 5;
     consider k such that A8:x=k& 1<=k & k<=n by A4,A7;
     dom nt=Seg n & (nt.k=i or nt.k<>i) by FINSEQ_2:144;
     then p.k=j & nt.k=i or p.k=nt.k& nt.k<>i & nt.k in rng nt
       by A4,A5,A7,A8,FUNCT_1:def 5;
     then p.k in {j} or p.k in rng nt & not p.k in {i}
       by TARSKI:def 1;
     then p.k in {j} or p.k in rng nt\{i} by XBOOLE_0:def 4;
     hence thesis by A7,A8,XBOOLE_0:def 2;
   end;
   (rng nt\{i}) \/ {j} c= rng p'
   proof
     let y such that A9:y in (rng nt\{i}) \/ {j};
      per cases by A9,XBOOLE_0:def 2;
        suppose A10:y in rng nt\{i};
          then consider x be set such that
            A11:x in dom nt & nt.x=y by FUNCT_1:def 5;
          A12:dom nt=Seg n by FINSEQ_2:144;
          then consider k such that A13:x=k& 1<=k & k<=n by A11;
          not y in {i} by A10,XBOOLE_0:def 4;
          then y<>i by TARSKI:def 1;
          then p.k=y by A5,A11,A12,A13;
          hence thesis by A4,A11,A12,A13,FUNCT_1:def 5;end;
        suppose A14:y in {j};
          consider x be set such that
            A15:x in dom nt & nt.x=i by A1,FUNCT_1:def 5;
          A16:dom nt=Seg n by FINSEQ_2:144;
          then consider k such that A17:x=k& 1<=k & k<=n by A15;
          p.k=j & y=j by A5,A14,A15,A16,A17,TARSKI:def 1;
          hence thesis by A4,A15,A16,A17,FUNCT_1:def 5;end;
   end;
   hence rng p' =(rng nt\{i}) \/ {j} by A6,XBOOLE_0:def 10;
   set LA=Line(A',j); set R=RLine(A',i,LA);
   set SR=Segm(R,nt,mt);set S=Segm(A',p',mt);
   A18:Indices A'=Indices R & Indices S=Indices SR & len LA=width A'
     by MATRIX_1:27,def 8;
   now let k,l be Nat such that A19:[k,l] in Indices SR;
     reconsider K=k,L=l as Element of NAT by ORDINAL1:def 13;
     A20:[nt.K,mt.L] in Indices A' & Indices A'=[:dom A',Seg width A':] &
       SR*(K,L)=R*(nt.K,mt.L) & Indices SR=[:Seg n,Seg width SR:]
       by A2,A18,A19,Def1,Th17,MATRIX_1:26;
     then (nt.K=i or nt.K<>i) & K in Seg n & mt.L in Seg width A'
       by A19,ZFMISC_1:106;
     then SR*(K,L)=LA.(mt.L) & p'.K=j & LA.(mt.L)=A'*(j,mt.L) or
       SR*(K,L)=A'*(nt.K,mt.L) & p'.K=nt.K
       by A5,A18,A20,MATRIX11:def 3,MATRIX_1:def 8;
     hence SR*(k,l)=S*(k,l) by A18,A19,Def1;
   end;
   hence thesis by MATRIX_1:28;
end;

theorem Th40:
  for F be FinSequence of D holds
    not i in Seg len A' implies RLine(A',i,F) = A'
proof
  let F be FinSequence of D;
  assume A1:not i in Seg len A';set R=RLine(A',i,F);
  per cases;
    suppose len F=width A';
      then A2:len A'=len R by MATRIX11:def 3;
      now let k be Nat such that A3:1 <= k & k <= len A';
        k in NAT by ORDINAL1:def 13;
        then k in Seg len A' & len A'=n' by A3,MATRIX_1:def 3;
        then Line(R,k)=Line(A',k) & R.k=Line(R,k) & A'.k=Line(A',k)
          by A1,MATRIX11:28,MATRIX_2:10;
         hence R.k=A'.k;
      end;
      hence thesis by A2,FINSEQ_1:18;end;
    suppose len F<>width A';
      hence thesis by MATRIX11:def 3;end;
end;

definition let n,m be Nat,K be Field;
  let M be Matrix of n,m,K, a be Element of K;
  redefine func a*M -> Matrix of n,m,K;
  coherence
  proof
    A1:len M=n & len (a*M)=len M  by MATRIX_1:def 3,MATRIX_3:def 5;
   per cases;
      suppose A2:n=0;
        then a*M={} by A1,FINSEQ_1:25;
        hence thesis by A2,MATRIX_1:13;end;
      suppose n>0;
        then width M=m & width (a*M)=width M  by MATRIX_1:24,MATRIX_3:def 5;
        hence thesis by A1,MATRIX_2:7;end;
    end;
end;

theorem Th41:
  [:rng nt,rng mt:] c= Indices M implies a * Segm(M,nt,mt) = Segm(a * M,nt,mt)
proof
  assume A1:[:rng nt,rng mt:] c= Indices M;
  set Sa=Segm(a * M,nt,mt);set S=Segm(M,nt,mt);set aS=a*S;
  A2:Indices (a*M)=Indices M & Indices Sa=Indices S by MATRIXR1:18,MATRIX_1:27;
  now let i,j be Nat such that A3:[i,j] in Indices Sa;
    reconsider i'=i,j'=j as Element of NAT by ORDINAL1:def 13;
    Sa*(i',j')=(a*M)*(nt.i,mt.j) & [nt.i',mt.j'] in Indices M &
      aS*(i,j)=a * (S*(i,j)) & S*(i',j')=M*(nt.i,mt.j)
      by A1,A2,A3,Def1,Th17,MATRIX_3:def 5;
    hence Sa*(i,j)=aS*(i,j) by MATRIX_3:def 5;
  end;
  hence thesis by MATRIX_1:28;
end;

theorem Th42:
  nt = idseq len A & mt = idseq width A implies Segm(A,nt,mt) = A
proof
  set S=Segm(A,nt,mt);
  assume A1:nt = idseq len A & mt = idseq width A;
  A2:len nt=n & len mt=m & len idseq len A =len A & len idseq width A=width A
    by FINSEQ_2:55,109;
  per cases;
    suppose n=0;
      then A={} & len S=0 by A1,A2,FINSEQ_1:25,MATRIX_1:def 3;
      hence thesis by FINSEQ_1:25;end;
    suppose n>0;
      then A3:len S=n & width S=m by Th1;
      then A4:Indices S=[:Seg n,Seg m:] by MATRIX_1:26;
      now let i,j be Nat such that A5:[i,j] in Indices S;
        reconsider i'=i,j'=j as Element of NAT by ORDINAL1:def 13;
        i in Seg n & j in Seg m by A4,A5,ZFMISC_1:106;
        then nt.i'=i & mt.j'=j by A1,A2,FINSEQ_1:4,FINSEQ_2:57;
        hence S*(i,j)=A*(i,j) by A5,Def1;
      end;
      hence thesis by A1,A2,A3,MATRIX_1:21;end;
end;

registration
  cluster empty without_zero finite Subset of NAT;
  existence
  proof
    {}NAT is without_zero by PSCOMP_1:def 1;
    hence thesis;
  end;
  cluster non empty without_zero finite Subset of NAT;
  existence
  proof
    {1} is Subset of NAT & not 0 in {1} by TARSKI:def 1;
    hence thesis;
  end;
end;

registration let n;
  cluster Seg n -> without_zero;
  coherence
  proof
    not 0 in Seg n by FINSEQ_1:3;
    hence thesis by PSCOMP_1:def 1;
  end;
end;

registration
  let X be without_zero set, Y be set;
  cluster X \ Y -> without_zero;
coherence
  proof assume 0 in X \ Y;
    hence thesis by PSCOMP_1:def 1;
  end;
  cluster X /\ Y -> without_zero;
coherence
  proof
    assume 0 in X /\ Y;
    then 0 in X by XBOOLE_0:def 3;
    hence thesis by PSCOMP_1:def 1;
  end;
end;

theorem Th43:
  for N be finite without_zero Subset of NAT
    ex k st N c= Seg k
proof
  let N be finite without_zero Subset of NAT;
  consider k such that
  A1:for n st n in N holds n<=k by STIRL2_1:66;
  take k;
    thus N c= Seg k
    proof
      let x;assume A2:x in N;
      then consider n such that A3:n=x;
      x<>0 by A2,PSCOMP_1:def 1;
      then n>=1 & n<=k by A1,A2,A3,NAT_1:14;
      hence thesis by A3;
    end;
end;

definition let N be finite without_zero Subset of NAT;
  redefine func Sgm N -> Element of (card N)-tuples_on NAT;
  coherence
  proof
    consider k such that A1:N c= Seg k by Th43;
    len(Sgm N)=card N by A1,FINSEQ_3:44;
    hence thesis by FINSEQ_2:110;
  end;
end;

definition
  let D be non empty set,A be Matrix of D;
  let P,Q be without_zero finite Subset of NAT;
  func Segm(A,P,Q) -> Matrix of card P,card Q,D equals
    Segm(A,Sgm P,Sgm Q);
  coherence;
end;

theorem Th44:
  Segm(A,{i0},{j0}) = <*<* A*(i0,j0) *>*>
proof
  card {i0}= 1 & card {j0}=1 & Sgm {i0}=<*i0*> & Sgm {j0}=<*j0*> & <*i0*>.1=i0
    & <*j0*>.1=j0 by CARD_1:50,FINSEQ_1:57,FINSEQ_3:50;
  hence thesis by Th21;
end;

theorem Th45:
  i0 < j0 & n0 < m0 implies
    Segm(A,{i0,j0},{n0,m0}) = ( A*(i0,n0),A*(i0,m0) )][( A*(j0,n0),A*(j0,m0) )
proof
  assume i0 < j0 & n0 < m0;
  then A1:card {i0,j0}= 2 & card {n0,m0}=2 & Sgm {i0,j0}=<*i0,j0*> &
    Sgm {n0,m0}=<*n0,m0*> by CARD_2:76,FINSEQ_3:51;
  then Sgm {n0,m0}.1=n0 & Sgm {n0,m0}.2=m0 &
       Sgm {i0,j0}.1=i0 & Sgm {i0,j0}.2=j0 by FINSEQ_1:61;
  hence thesis by A1,Th23;
end;

 reserve P,P1,P2,Q,Q1,Q2 for without_zero finite Subset of NAT;

theorem Th46:
  Segm(A,Seg len A,Seg width A) = A
proof
  Sgm Seg len A=idseq len A & Sgm Seg width A=idseq width A by FINSEQ_3:54;
  hence thesis by Th42;
end;

theorem Th47:
  i in Seg card P & Q c= Seg width A implies
     Line(Segm(A,P,Q),i) = Line(A,Sgm P.i) * Sgm Q
proof
  assume A1:i in Seg card P & Q c= Seg width A;
  rng (Sgm Q)=Q by A1,FINSEQ_1:def 13;
  hence thesis by A1,Th24;
end;

theorem Th48:
  i in Seg card P implies Line(Segm(A,P,Seg width A),i) = Line(A,Sgm P.i)
proof
  assume A1:i in Seg card P;
  set sP=Sgm P;set S=Seg width A;
  len Line(A,sP.i)=width A by MATRIX_1:def 8;
  then dom Line(A,sP.i)=S & Sgm S = idseq width A
    by FINSEQ_1:def 3,FINSEQ_3:54;
  then Line(A,sP.i) * Sgm S = Line(A,sP.i) by RELAT_1:78;
  hence thesis by A1,Th47;
end;

theorem Th49:
  j in Seg card Q & P c= Seg len A implies
     Col(Segm(A,P,Q),j) = Col(A,Sgm Q.j) * Sgm P
proof
  assume A1:j in Seg card Q & P c= Seg len A;
  rng (Sgm P)=P by A1,FINSEQ_1:def 13;
  hence thesis by A1,Th28;
end;

theorem Th50:
  j in Seg card Q implies Col(Segm(A,Seg len A,Q),j) = Col(A,Sgm Q.j)
proof
  assume A1:j in Seg card Q;
  set sQ=Sgm Q;set S=Seg len A;
  len Col(A,sQ.j)=len A by MATRIX_1:def 9;
  then dom Col(A,sQ.j)=S & Sgm S = idseq len A
    by FINSEQ_1:def 3,FINSEQ_3:54;
  then Col(A,sQ.j) * Sgm S = Col(A,sQ.j) by RELAT_1:78;
  hence thesis by A1,Th49;
end;

theorem Th51:
  Segm(A,Seg len A \ {i},Seg width A) = Del(A,i)
proof
  set SLA=Seg len A;set Si=SLA\{i};
  set S= Segm(A,Si,Seg width A);
  A1:dom A=SLA by FINSEQ_1:def 3;
  per cases;
    suppose not i in dom A;
      then Si=SLA & Del(A,i)=A by A1,FINSEQ_3:113,ZFMISC_1:65;
      hence thesis by Th46;end;
    suppose A2:i in dom A;
      then consider m be Nat such that
        A3:len A = m + 1 & len Del(A,i) = m by FINSEQ_3:113;
      reconsider m as Element of NAT by ORDINAL1:def 13;
      card SLA = m+1 by A3,FINSEQ_1:78;
      then A4:card Si=m by A1,A2,STIRL2_1:65;
      then A5:len S=m by MATRIX_1:def 3;
      now let j be Nat such that A6:1<=j & j<=m;
        reconsider A'=A as Matrix of m+1,width A,D by A3,MATRIX_1:20;
        j in NAT by ORDINAL1:def 13;
        then A7:dom Del(A,i)=Seg m & j in Seg m & Del(A,i)=A*Sgm Si
          by A1,A3,A6,FINSEQ_1:def 3,FINSEQ_3:def 2;
        then Del(A,i).j=A'.(Sgm Si.j) & Sgm Si.j in dom A & dom A=Seg len A
           by FINSEQ_1:def 3,FUNCT_1:21,22;
        hence Del(A,i).j = Line(A',Sgm Si.j) by A3,MATRIX_2:10
                        .= Line(S,j) by A4,A7,Th48
                        .= S.j by A4,A7,MATRIX_2:10;
      end;
      hence thesis by A3,A5,FINSEQ_1:18;end;
end;

theorem Th52:
  Segm(M,Seg len M,Seg width M\{i}) = DelCol(M,i)
proof
  set SW=Seg width M;set Si=SW\{i};set SL=Seg len M;
  set SEGM=Segm(M,SL,Si);set D=DelCol(M,i);
  card SL=len M by FINSEQ_1:78;
  then A1:len SEGM=len M & len D=len M  by MATRIX_1:def 3,MATRIX_2:def 6;
  now let j be Nat such that A2:1<=j & j<=len M;
    j in NAT by ORDINAL1:def 13;
    then card SL=len M & j in Seg len M & Si c= SW & Sgm SL=idseq len M &
      len Line(M,j)=width M
      by A2,FINSEQ_1:78,FINSEQ_3:54,MATRIX_1:def 8,XBOOLE_1:36;
    then Line(SEGM,j) = Line(M,Sgm SL.j) * Sgm Si & Sgm SL.j=j &
      Line(SEGM,j)=SEGM.j & dom Line(M,j)=SW & j in dom M
      by Th47,FINSEQ_1:4,def 3,FINSEQ_2:57,MATRIX_2:10;
    then SEGM.j = Del(Line(M,j),i) & D.j=Del(Line(M,j),i)
      by FINSEQ_3:def 2,MATRIX_2:def 6;
    hence SEGM.j=D.j;
  end;
  hence thesis by A1,FINSEQ_1:18;
end;

theorem Th53:
  Sgm P " X is without_zero finite Subset of NAT
proof
  consider k such that A1:P c= Seg k by Th43;
  Sgm P " X c= dom (Sgm P) & dom (Sgm P) = Seg card P
    by A1,FINSEQ_3:45,RELAT_1:167;
  then Sgm P"X is finite Subset of NAT & not 0 in Sgm P"X
    by PSCOMP_1:def 1,XBOOLE_1:1;
  hence thesis by PSCOMP_1:def 1;
end;

theorem Th54:
  X c= P implies Sgm X = Sgm P * Sgm (Sgm P " X)
proof
  assume A1:X c= P;
  consider n such that A2:P c= Seg n by Th43;
  A3:rng Sgm P=P by A2,FINSEQ_1:def 13;
  A4:Sgm P " X c= dom Sgm P by RELAT_1:167;
  rng ((Sgm P)|(Sgm P " X)) = (Sgm P).:(Sgm P " X) by RELAT_1:148
                           .= X by A1,A3,FUNCT_1:147;
  hence Sgm P * Sgm (Sgm P"X)=Sgm X by A2,A4,GRAPH_2:3;
end;

Lm2:X c= P implies Card X = Card (Sgm P " X)
proof
  assume A1:X c= P;
  consider n such that A2:P c= Seg n by Th43;
  rng Sgm P=P by A2,FINSEQ_1:def 13;
  then Sgm P.:(Sgm P " X) = X & Sgm P " X c= dom Sgm P & Sgm P is one-to-one
    by A1,A2,FINSEQ_3:99,FUNCT_1:147,RELAT_1:167;
  then X,Sgm P " X are_equipotent by CARD_1:60;
  hence thesis by CARD_1:21;
end;

theorem Th55:
  [:Sgm P " X,Sgm Q " Y:] c= Indices Segm(A,P,Q)
proof
  set SP=Sgm P;set SQ=Sgm Q;set I=Indices Segm(A,P,Q);
  consider n such that A1:P c= Seg n by Th43;
  consider m such that A2:Q c= Seg m by Th43;
  dom SP=Seg card P & dom SQ=Seg card Q by A1,A2,FINSEQ_3:45;
  then A3:SP " X c= Seg card P & SQ " Y c= Seg card Q by RELAT_1:167;
  now per cases;
    suppose A4:card P=0;
      then [:Seg card P,Seg card Q:]={} by FINSEQ_1:4,ZFMISC_1:113;
      hence [:Seg card P,Seg card Q:]=I by A4,MATRIX_1:23;end;
    suppose card P>0;
      hence [:Seg card P,Seg card Q:]=I by MATRIX_1:24;end;
  end;
  hence thesis by A3,ZFMISC_1:119;
end;

theorem Th56:
  P c= P1 & Q c= Q1 & P2 = Sgm P1 " P & Q2 = Sgm Q1 " Q implies
     [:rng Sgm P2,rng Sgm Q2:] c= Indices Segm(A,P1,Q1) &
     Segm(Segm(A,P1,Q1),P2,Q2) = Segm(A,P,Q)
proof
  assume that A1:P c= P1 & Q c= Q1 and A2:P2 = Sgm P1 " P & Q2 = Sgm Q1 " Q;
  set SA=Segm(A,P1,Q1);set S=Segm(A,P,Q);
  set Sp1=Sgm P1;set Sq1=Sgm Q1;set Sp2=Sgm P2;set Sq2=Sgm Q2;
  consider p2 be Element of NAT such that A3:P2 c= Seg p2 by Th43;
  consider q2 be Element of NAT such that A4:Q2 c= Seg q2 by Th43;
  rng Sp2=P2 & rng Sq2=Q2 by A3,A4,FINSEQ_1:def 13;
  hence A5:[:rng Sp2,rng Sq2:] c= Indices SA by A2,Th55;
  Card P=Card P2 & Card Q=Card Q2 by A1,A2,Lm2;
  then reconsider SAA=Segm(SA,P2,Q2) as Matrix of card P,card Q,D;
  now let i,j be Nat such that A6:[i,j] in Indices S;
    reconsider i'=i,j'=j as Element of NAT by ORDINAL1:def 13;
    reconsider Sp2i=Sp2.i,Sq2j=Sq2.j as Element of NAT by ORDINAL1:def 13;
    [i,j] in Indices SAA & Indices SAA=[:Seg card P2,Seg width SAA:]
      by A6,MATRIX_1:26,27;
    then A7:i in Seg card P2 & j in Seg width SAA by ZFMISC_1:106;
    then width SAA = card Q2 by Th1,FINSEQ_1:4;
    then i in dom Sp2 & j in dom Sq2 & Sp1*Sp2=Sgm P & Sq1*Sq2=Sgm Q
      by A1,A2,A3,A4,A7,Th54,FINSEQ_3:45;
    then A8:Sp1.Sp2i=Sgm P.i & Sq1.Sq2j=Sgm Q.j by FUNCT_1:23;
    [i',j'] in Indices SAA by A6,MATRIX_1:27;
    then SAA*(i,j)=SA*(Sp2i,Sq2j) & [Sp2i,Sq2j] in Indices SA
      by A5,Def1,Th17;
    then SAA*(i,j)=A*(Sp1.Sp2i,Sq1.Sq2j)& S*(i',j')=A*(Sgm P.i,Sgm Q.j)
      by A6,Def1;
    hence S*(i,j) = SAA*(i,j) by A8;
  end;
  hence thesis by MATRIX_1:28;
end;

theorem Th57:
  (P = {} iff Q={}) & [:P,Q:] c= Indices Segm(A,P1,Q1) implies
    ex P2,Q2 st P2 c= P1 & Q2 c= Q1 & P2 = Sgm P1.:P & Q2=Sgm Q1.:Q &
       card P2=card P & card Q2=card Q &
       Segm(Segm(A,P1,Q1),P,Q) = Segm(A,P2,Q2)
proof
  assume that A1:P = {} iff Q={} and A2:[:P,Q:] c= Indices Segm(A,P1,Q1);
  set S=Segm(A,P1,Q1);
A3:now per cases;
    suppose P={};
      hence P c= Seg card P1 & Q c= Seg card Q1 by A1,XBOOLE_1:2;
    end;
    suppose A4:P<>{};
      then [:P,Q:]<>{} & Indices S=[:Seg len S,Seg width S:]
        by A1,FINSEQ_1:def 3,ZFMISC_1:113;
      then A5:P c= Seg len S & Q c= Seg width S by A2,ZFMISC_1:138;
      then len S<>0 & len S=card P1
        by A4,FINSEQ_1:4,MATRIX_1:def 3,XBOOLE_1:3;
      hence P c= Seg card P1 & Q c= Seg card Q1 by A5,Th1;
    end;
   end;
   consider k such that A6:P1 c= Seg k by Th43;
   consider k such that A7:Q1 c= Seg k by Th43;
   set SP=Sgm P1;set SQ=Sgm Q1;
   A8:dom SP=Seg card P1 & rng SP=P1 & SP is one-to-one & dom SQ=Seg card Q1 &
     rng SQ=Q1 & SQ is one-to-one by A6,A7,FINSEQ_1:def 13,FINSEQ_3:45,99;
   then A9:SP.:P c= P1 & not 0 in P1 & SQ.:Q c= Q1 & not 0 in Q1
     by PSCOMP_1:def 1,RELAT_1:144;
   then SP.:P is finite & SP.:P c= NAT & not 0 in SP.:P & SQ.:Q is finite&
     SQ.:Q c= NAT & not 0 in SQ.:Q by XBOOLE_1:1;
   then reconsider P2=SP.:P,Q2=SQ.:Q as without_zero finite Subset of NAT
     by PSCOMP_1:def 1;
   SP " P2 = P & SQ " Q2 = Q & P,P2 are_equipotent & Q,Q2 are_equipotent
     by A3,A8,CARD_1:60,FUNCT_1:164;
   then Segm(Segm(A,P1,Q1),P,Q)=Segm(A,P2,Q2) & card P=card P2 & card Q=card Q2
     by A9,Th56,CARD_1:21;
   hence thesis by A9;
end;

theorem Th58:
  for M be Matrix of n,K holds
    Segm(M,Seg n\{i},Seg n\{j}) = Deleting(M,i,j)
proof
  let M be Matrix of n,K;
  A1:len M = n & width M=n by MATRIX_1:25;
  then A2:dom M=Seg n by FINSEQ_1:def 3;
  per cases;
    suppose not i in Seg n;
      then Del(M,i)=M & Seg n=Seg n\{i} by A2,FINSEQ_3:113,ZFMISC_1:65;
      hence thesis by A1,Th52;end;
    suppose A3:i in Seg n;
      then consider m be Nat such that
        A4:len M = m + 1 & len Del(M,i) = m by A2,FINSEQ_3:113;
      set P=Seg n\{i};set Q=Seg n\{j};set Q1=Seg n;set SS=Segm(M,P,Q1);
      per cases;
        suppose m=0;
          then i = 1 & Q1\{1} = {} & len Deleting(M,i,j) = 0
            by A1,A3,A4,FINSEQ_1:4,MATRIX_2:def 6,TARSKI:def 1,XBOOLE_1:37;
          then Deleting(M,i,j) = {} & len Segm(M,P,Q) = 0
            by CARD_1:78,FINSEQ_1:25,MATRIX_1:def 3;
          hence thesis by FINSEQ_1:25;end;
       suppose A5:m>0;
         A6:Q c= Seg n & P c= Seg n by XBOOLE_1:36;
         then rng Sgm P = P & dom Sgm P = Seg card P
           by FINSEQ_1:def 13,FINSEQ_3:45;
         then A7:Sgm P"P = Seg card P by RELAT_1:169
                        .= Seg len SS by MATRIX_1:def 3;
         n > 1+0 by A1,A4,A5,XREAL_1:10;
         then n = width DelLine(M,i) & SS = Del(M,i) & Sgm Q1 =idseq n &
           id Q1"Q =Q by A1,A6,Th51,FINSEQ_3:54,FUNCT_2:171,LAPLACE:4;
         then Deleting(M,i,j)=Segm(SS,Seg len SS,Seg width SS\{j}) &
           Sgm Q1"Q =Seg width SS\{j} by Th52;
         hence thesis by A6,A7,Th56;end;end;
end;

theorem Th59:
  for F,FQ be FinSequence of D st
       len F = width A' & FQ = F * Sgm Q & [:P,Q:] c= Indices A' holds
    RLine(Segm(A',P,Q),i,FQ) = Segm(RLine(A',Sgm P.i,F),P,Q)
proof
  let F,FQ be FinSequence of D such that
    A1:len F = width A' and A2:FQ = F * Sgm Q and A3:[:P,Q:] c= Indices A';
  set SP=Sgm P;set SQ=Sgm Q;
  consider n such that A4:P c= Seg n by Th43;
  consider m such that A5:Q c= Seg m by Th43;
  A6:rng SP=P & rng SQ=Q & dom SP=Seg card P & card P=len Segm(A',P,Q)
    & SP is one-to-one by A4,A5,FINSEQ_1:def 13,FINSEQ_3:45,99,MATRIX_1:def 3;
  per cases;
    suppose i in dom SP;
      then SP"{SP.i} = {i} & SP.i is Element of NAT
        by A6,FINSEQ_5:4,ORDINAL1:def 13;
      hence thesis  by A1,A2,A3,A6,Th37;end;
    suppose A7:not i in dom SP;
      then SP.i=0 & not 0 in Seg len A' by FINSEQ_1:3,FUNCT_1:def 4;
      hence Segm(RLine(A',SP.i,F),P,Q) = Segm(A',P,Q) by Th40
                                .= RLine(Segm(A',P,Q),i,FQ) by A6,A7,Th40;end;
end;

theorem Th60:
  for F be FinSequence of D,i,P st not i in P & [:P,Q:] c= Indices A' holds
    Segm(A',P,Q) = Segm(RLine(A',i,F),P,Q)
proof
  let F be FinSequence of D,i,P such that A1:not i in P and
                                          A2:[:P,Q:] c= Indices A';
  set R=RLine(A',i,F);set S=Segm(A',P,Q);set SR=Segm(R,P,Q);
  consider n such that A3:P c= Seg n by Th43;
  consider m such that A4:Q c= Seg m by Th43;
  rng Sgm P=P & rng Sgm Q=Q by A3,A4,FINSEQ_1:def 13;
  hence thesis by A1,A2,Th38;
end;

theorem
  [:P,Q:] c= Indices A & (card P=0 iff card Q=0) implies
    Segm(A,P,Q)@ = Segm(A@,Q,P)
proof
  assume that A1:[:P,Q:] c= Indices A and A2:card P=0 iff card Q = 0;
  consider n such that A3:P c= Seg n by Th43;
  consider m such that A4:Q c= Seg m by Th43;
  rng Sgm P=P & rng Sgm Q=Q by A3,A4,FINSEQ_1:def 13;
  hence thesis by A1,A2,Th18;
end;

theorem Th62:
  [:P,Q:] c= Indices A & (card Q=0 implies card P = 0) implies
    Segm(A,P,Q) = Segm(A@,Q,P)@
proof
  assume that A1:[:P,Q:] c= Indices A and A2:card Q=0 implies card P = 0;
  consider n such that A3:P c= Seg n by Th43;
  consider m such that A4:Q c= Seg m by Th43;
  rng Sgm P=P & rng Sgm Q=Q by A3,A4,FINSEQ_1:def 13;
  hence thesis by A1,A2,Th19;
end;

theorem Th63:
  [:P,Q:] c= Indices M implies a * Segm(M,P,Q) = Segm(a * M,P,Q)
proof
  assume A1:[:P,Q:] c= Indices M;
  consider n such that A2:P c= Seg n by Th43;
  consider k such that A3:Q c= Seg k by Th43;
  rng Sgm P=P & rng Sgm Q=Q by A2,A3,FINSEQ_1:def 13;
  hence thesis by A1,Th41;
end;

definition
  let D be non empty set,A be Matrix of D;
  let P,Q be without_zero finite Subset of NAT;
  assume A1:card P = card Q;
  func EqSegm(A,P,Q) -> Matrix of card P,D equals
  :Def3:  Segm(A,P,Q);
  coherence by A1;
end;

theorem Th64:
  for P,Q,i,j st i in Seg card P & j in Seg card P & card P = card Q holds
    Delete(EqSegm(M,P,Q),i,j) = EqSegm(M,P\{Sgm P.i},Q\{Sgm Q.j}) &
      card (P\{Sgm P.i}) = card (Q\{Sgm Q.j})
proof
  let P1,Q1,i,j such that A1:i in Seg card P1 & j in Seg card P1 and
                          A2:card P1 = card Q1;
  set SP1=Sgm P1;set SQ1=Sgm Q1;set E=EqSegm(M,P1,Q1);
  set P=P1\{SP1.i};set Q=Q1\{SQ1.j};
  set P2=Seg card P1\{i};set Q2=Seg card Q1\{j};
  A3:P c= P1 & Q c= Q1 by XBOOLE_1:36;
  consider n such that A4:P1 c= Seg n by Th43;
  consider m such that A5:Q1 c= Seg m by Th43;
  card P1<>0 by A1,FINSEQ_1:4;
  then card P1>0;
  then reconsider C=card P1-1 as Element of NAT by NAT_1:20;
  A6:dom SP1=Seg card P1 & rng SP1=P1 & dom SQ1=Seg card Q1 & rng SQ1=Q1 &
    SP1 is one-to-one & SQ1 is one-to-one
    by A4,A5,FINSEQ_1:def 13,FINSEQ_3:45,99;
  then SP1"{SP1.i}={i} & SQ1"{SQ1.j}={j} & SP1"P1 = Seg card P1 &
    SQ1"Q1 = Seg card P1 by A1,A2,FINSEQ_5:4,RELAT_1:169;
  then A7:P2 = SP1 " P & Q2 = SQ1 " Q by A2,FUNCT_1:138;
  SP1.i in P1 & SQ1.j in Q1 & card P1=C+1 by A1,A2,A6,FUNCT_1:def 5;
  then A8:card P = C & card Q = C by A2,STIRL2_1:65;
  Delete(E,i,j)=Deleting(E,i,j) by A1,LAPLACE:def 1
    .=Segm(E,P2,Q2) by A2,Th58
    .=Segm(Segm(M,P1,Q1),P2,Q2) by A2,Def3
    .=Segm(M,P,Q) by A3,A7,Th56
    .=EqSegm(M,P,Q) by A8,Def3;
  hence thesis by A8;
end;

Lm3:for M,P,Q,i st i in Seg card P & card P=card Q & Det EqSegm(M,P,Q)<>0.K
    ex j st j in Seg card P & Det Delete(EqSegm(M,P,Q),i,j) <> 0.K
proof
  let M,P,Q,i such that A1:i in Seg card P & card P = card Q and
                        A2:Det EqSegm(M,P,Q) <> 0.K;
  set E=EqSegm(M,P,Q);set C=card P;set LL=LaplaceExpL(E,i);set CC=C |-> 0.K;
  Sum CC=0.K & len LL=C by LAPLACE:def 7,MATRIX_3:13;
  then A3:LL<>CC & dom LL=Seg C & dom CC = Seg C
    by A1,A2,FINSEQ_1:def 3,FUNCOP_1:19,LAPLACE:25;
  then consider j be Nat such that
    A4:j in dom LL & LL.j <> CC.j by FINSEQ_1:17;
  reconsider ij=i+j as Element of NAT by ORDINAL1:def 13;
  CC.j=0.K & LL.j=E*(i,j)*Cofactor(E,i,j)
    by A3,A4,FINSEQ_1:4,FINSEQ_2:71,LAPLACE:def 7;
  then Cofactor(E,i,j)<>0.K by A4,VECTSP_1:44;
  then Minor(E,i,j)<>0.K by VECTSP_1:44;
  hence thesis by A3,A4;
end;

theorem Th65:
  for M,P,P1,Q1 st card P1 = card Q1 & P c= P1 & Det EqSegm(M,P1,Q1) <> 0.K
    ex Q st Q c= Q1 & card P = card Q & Det EqSegm(M,P,Q) <> 0.K
proof
  let M,P,P1,Q1 such that A1:card P1 = card Q1 & P c= P1 and
                          A2:Det EqSegm(M,P1,Q1) <> 0.K;
  defpred Q[Nat] means for P st P c= P1 & card P=$1
    ex Q st Q c= Q1 & card P=card Q & Det EqSegm(M,P,Q) <> 0.K;
  A3:Q[card P1]
  proof let P;assume P c= P1 & card P=card P1;
    then P=P1 by CARD_FIN:1;
    hence thesis by A1,A2;
  end;
  A4:for k st k < card P1 & Q[k+1] holds Q[k]
  proof
    let k such that A5:k < card P1 & Q[k+1];
    let P such that A6:P c= P1 & card P=k;
    P c< P1 by A5,A6,XBOOLE_0:def 8;
    then P1\P<>{} by XBOOLE_1:105;
    then consider x such that
      A7:x in P1\P by XBOOLE_0:def 1;
    reconsider x as non zero Element of NAT by A7,PSCOMP_1:def 1;
    reconsider Px=P\/{x} as without_zero finite Subset of NAT;
    x in P1 & not 0 in P1 by A7,PSCOMP_1:def 1,XBOOLE_0:def 4;
    then A8:not x in P & {x} c= P1 by A7,XBOOLE_0:def 4,ZFMISC_1:37;
    then A9:card Px=k+1 & Px c= P1 by A6,CARD_2:54,XBOOLE_1:8;
    then consider Q2 such that
      A10:Q2 c= Q1 & card Px=card Q2 & Det EqSegm(M,Px,Q2) <> 0.K by A5;
    set E=EqSegm(M,Px,Q2);
    consider n such that A11:Px c= Seg n by Th43;
    x in {x} by TARSKI:def 1;
    then rng Sgm Px = Px & dom Sgm Px = Seg card Px & x in Px
      by A11,FINSEQ_1:def 13,FINSEQ_3:45,XBOOLE_0:def 2;
    then consider i be set such that
      A12:i in Seg card Px & Sgm Px.i=x by FUNCT_1:def 5;
    reconsider i as Element of NAT by A12;
    consider j such that
      A13:j in Seg card Px & Det Delete(E,i,j) <> 0.K by A10,A12,Lm3;
    take Q=Q2\{Sgm Q2.j};
    Px\{x}=P & Q c= Q2 & k+1-'1=k+1-1
      by A8,BINARITH:def 3,XBOOLE_1:36,ZFMISC_1:141;
    hence thesis by A6,A9,A10,A12,A13,Th64,XBOOLE_1:1;
  end;
  A14:for k st k <= card P1 holds Q[k] from TRIANG_1:sch 1(A3,A4);
  card P <= card P1 by A1,CARD_1:80;
  hence thesis by A1,A14;
end;

Lm4:for M,P,Q,i st j in Seg card P & card P=card Q & Det EqSegm(M,P,Q)<>0.K
    ex i st i in Seg card P & Det Delete(EqSegm(M,P,Q),i,j) <> 0.K
proof
  let M,P,Q,i such that A1:j in Seg card P & card P = card Q and
                        A2:Det EqSegm(M,P,Q) <> 0.K;
  set E=EqSegm(M,P,Q);set C=card P;set LC=LaplaceExpC(E,j);set CC=C |-> 0.K;
  Sum CC=0.K & len LC=C by LAPLACE:def 8,MATRIX_3:13;
  then A3:LC<>CC & dom LC=Seg C & dom CC = Seg C
    by A1,A2,FINSEQ_1:def 3,FUNCOP_1:19,LAPLACE:27;
  then consider i be Nat such that
    A4:i in dom LC & LC.i <> CC.i by FINSEQ_1:17;
  reconsider ij=i+j as Element of NAT by ORDINAL1:def 13;
  CC.i=0.K & LC.i=E*(i,j)*Cofactor(E,i,j) by A3,A4,FINSEQ_1:4,FINSEQ_2:71
,LAPLACE:def 8;
  then Cofactor(E,i,j)<>0.K by A4,VECTSP_1:44;
  then Minor(E,i,j)<>0.K by VECTSP_1:44;
  hence thesis by A3,A4;
end;

theorem
  for M,P1,Q,Q1 st card P1 = card Q1 & Q c= Q1 & Det EqSegm(M,P1,Q1) <> 0.K
    ex P st P c= P1 & card P = card Q & Det EqSegm(M,P,Q) <> 0.K
proof
  let M,P1,Q,Q1 such that A1:card P1 = card Q1 & Q c= Q1 and
                          A2:Det EqSegm(M,P1,Q1) <> 0.K;
  defpred R[Nat] means for Q st Q c= Q1 & card Q=$1
    ex P st P c= P1 & card P=card Q & Det EqSegm(M,P,Q) <> 0.K;
  A3:R[card Q1]
  proof let Q;assume Q c= Q1 & card Q=card Q1;
    then Q=Q1 by CARD_FIN:1;
    hence thesis by A1,A2;
  end;
  A4:for k st k < card Q1 & R[k+1] holds R[k]
  proof
    let k such that A5:k < card Q1 & R[k+1];
    let Q such that A6:Q c= Q1 & card Q=k;
    Q c< Q1 by A5,A6,XBOOLE_0:def 8;
    then Q1\Q<>{} by XBOOLE_1:105;
    then consider x such that
      A7:x in Q1\Q by XBOOLE_0:def 1;
    reconsider x as non zero Element of NAT by A7,PSCOMP_1:def 1;
    reconsider Qx=Q\/{x} as without_zero finite Subset of NAT;
    x in Q1 & not 0 in Q1 by A7,PSCOMP_1:def 1,XBOOLE_0:def 4;
    then A8:not x in Q & {x} c= Q1 by A7,XBOOLE_0:def 4,ZFMISC_1:37;
    then A9:card Qx=k+1 & Qx c= Q1 by A6,CARD_2:54,XBOOLE_1:8;
    then consider P2 such that
      A10:P2 c= P1 & card Qx=card P2 & Det EqSegm(M,P2,Qx) <> 0.K by A5;
    set E=EqSegm(M,P2,Qx);
    consider n such that A11:Qx c= Seg n by Th43;
    x in {x} by TARSKI:def 1;
    then rng Sgm Qx = Qx & dom Sgm Qx = Seg card Qx & x in Qx
      by A11,FINSEQ_1:def 13,FINSEQ_3:45,XBOOLE_0:def 2;
    then consider j be set such that
      A12:j in Seg card Qx & Sgm Qx.j=x by FUNCT_1:def 5;
    reconsider j as Element of NAT by A12;
    consider i such that
      A13:i in Seg card Qx & Det Delete(E,i,j) <> 0.K by A10,A12,Lm4;
    take P=P2\{Sgm P2.i};
    A14:Qx\{x}=Q & P c= P2 & k+1-'1=k+1-1
      by A8,BINARITH:def 3,XBOOLE_1:36,ZFMISC_1:141;
    then card P=card Q by A10,A12,A13,Th64;
    hence thesis by A6,A9,A10,A12,A13,A14,Th64,XBOOLE_1:1;
  end;
  A15:for k st k <= card Q1 holds R[k] from TRIANG_1:sch 1(A3,A4);
  card Q <= card Q1 by A1,CARD_1:80;
  hence thesis by A1,A15;
end;

theorem Th67:
  card P=card Q implies
    ([:P,Q:] c= Indices A iff P c= Seg len A & Q c= Seg width A)
proof
  assume A1:card P = card Q;
  A2:Indices A=[:Seg len A,Seg width A:] by FINSEQ_1:def 3;
  thus [:P,Q:] c= Indices A implies (P c= Seg len A & Q c= Seg width A)
  proof
    assume A3:[:P,Q:] c= Indices A;
    per cases;
      suppose [:P,Q:]<>{};
        hence thesis by A2,A3,ZFMISC_1:138;end;
      suppose [:P,Q:]={};
        then card P=0 or card Q=0 by CARD_1:78,ZFMISC_1:113;
        then P={} & Q={} by A1,CARD_2:59;
        hence thesis by XBOOLE_1:2;end;
  end;
  thus thesis by A2,ZFMISC_1:119;
end;

theorem Th68:
  for P,Q,i,j0 st i in Seg n' & j0 in Seg n' & i in P & not j0 in P &
                  card P = card Q & [:P,Q:] c= Indices M' holds
    card P = card (P\{i}\/{j0}) & [:P\{i}\/{j0},Q:] c= Indices M' &
    (Det EqSegm(RLine(M',i,Line(M',j0)),P,Q) =  Det EqSegm(M',P\{i}\/{j0},Q) or
     Det EqSegm(RLine(M',i,Line(M',j0)),P,Q) = -Det EqSegm(M',P\{i}\/{j0},Q))
proof
  let P,Q,i,j0 such that A1:i in Seg n' & j0 in Seg n' and
                         A2:i in P & not j0 in P and
                         A3:card P = card Q & [:P,Q:] c= Indices M';
  set Pi=P\{i};set Pij=Pi\/{j0};
  card P<>0 by A2,CARD_2:59;
  then card P>0;
  then reconsider C=card P - 1 as Element of NAT by NAT_1:20;
  card P=C+1;
  then card Pi=C & not j0 in Pi by A2,STIRL2_1:65,XBOOLE_0:def 4;
  hence A4:card Pij = C+1 by CARD_2:54
                   .= card P;
  P c= Seg len M' & Pi c= P & n'=len M'
    by A3,Th67,MATRIX_1:def 3,XBOOLE_1:36;
  then Pi c= Seg len M' & {j0} c= Seg len M' by A1,XBOOLE_1:1,ZFMISC_1:37;
  then Pij c= Seg len M' & Q c= Seg width M' by A3,Th67,XBOOLE_1:8;
  hence [:Pij,Q:] c= Indices M' by A3,A4,Th67;
  consider n such that A5:P c= Seg n by Th43;
  consider k such that A6:Pij c= Seg k by Th43;
  consider m such that A7:Q c= Seg m by Th43;
  set SP=Sgm P;set SPij=Sgm Pij;set SQ=Sgm Q;
  A8:rng SP=P & rng SPij=Pij & rng SQ=Q by A5,A6,A7,FINSEQ_1:def 13;
  then consider PT be Element of (card P)-tuples_on NAT such that
    A9:rng PT=Pij and
    A10:Segm(RLine(M',i,Line(M',j0)),SP,SQ) = Segm(M',PT,SQ) by A1,A2,A3,Th39;
  reconsider SPij,SQ'=SQ as Element of (card P)-tuples_on NAT by A3,A4;
  A11:EqSegm(RLine(M',i,Line(M',j0)),P,Q) = Segm(RLine(M',i,Line(M',j0)),P,Q)
                                                           by A3,Def3
                                        .= Segm(M',PT,SQ') by A3,A10;
  Segm(M',SPij,SQ') = Segm(M',Pij,Q) by A3,A4
                   .= EqSegm(M',Pij,Q) by A3,A4,Def3;
  hence thesis by A4,A8,A9,A11,Th36;
end;

theorem Th69:
  card P = card Q implies ([:P,Q:] c= Indices A iff [:Q,P:] c= Indices A@)
proof
  assume A1:card P = card Q;
  per cases;
    suppose Q={};
      then [:P,Q:] = {} & [:Q,P:] = {} by ZFMISC_1:113;
      hence thesis by XBOOLE_1:2;end;
    suppose A2:Q<>{};
      thus [:P,Q:] c= Indices A implies [:Q,P:] c= Indices A@
      proof
        assume A3:[:P,Q:] c= Indices A;
        then A4:Q c= Seg width A by A1,Th67;
        then width A<>0 by A2,FINSEQ_1:4,XBOOLE_1:3;
        then width A>0;
        then len A=width (A@) & len (A@)=width A & P c= Seg len A
          by A1,A3,Th67,MATRIX_2:12;
        hence thesis by A1,A4,Th67;
      end;
      thus [:Q,P:] c= Indices A@ implies [:P,Q:] c= Indices A
      proof
        assume A5:[:Q,P:] c= Indices A@;
        then A6:Q c= Seg len (A@) by A1,Th67;
        then len (A@)<>0 & len (A@)=width A
          by A2,FINSEQ_1:4,MATRIX_1:def 7,XBOOLE_1:3;
        then width A>0;
        then len A=width (A@) & len (A@)=width A & P c= Seg width (A@)
          by A1,A5,Th67,MATRIX_2:12;
        hence thesis by A1,A6,Th67;
     end;end;
end;

theorem Th70:
  [:P,Q:] c= Indices M & card P = card Q implies
    Det EqSegm(M,P,Q) = Det EqSegm(M@,Q,P)
proof
  assume that A1:[:P,Q:] c= Indices M and A2:card P = card Q;
  A3:card Q=0 implies card P = 0 by A2;
  EqSegm(M,P,Q)= Segm(M,P,Q) by A2,Def3
  .=Segm(M@,Q,P)@ by A1,A3,Th62
  .=EqSegm(M@,Q,P)@ by A2,Def3;
  hence Det EqSegm(M,P,Q)=Det EqSegm(M@,Q,P) by A2,MATRIXR2:43;
end;

theorem Th71:
  for M be Matrix of n,K holds Det (a*M) = power(K).(a,n) * Det M
proof
  let M be Matrix of n,K;
  defpred P[Nat] means
    for k st k=$1 & k <=n
    ex aM be Matrix of n,K st
      Det aM = power(K).(a,k) * Det M &
      for i st i in Seg n holds
        (i<=k implies Line(aM,i)=a * Line(M,i)) &
        (i > k implies Line(aM,i)=Line(M,i));
  A1:P[0]
  proof
    let k such that A2:k=0 & k <=n;take aM=M;
    power(K).(a,0)=1_K by GROUP_1:def 8;
    hence Det aM = power(K).(a,k) * Det M by A2,VECTSP_1:def 19;
    let i such that A3:i in Seg n;
    thus thesis by A2,A3,FINSEQ_1:3;
  end;
  A4:for m st P[m] holds P[m+1]
  proof
    let m such that A5:P[m];
    reconsider m'=m as Element of NAT;
    let k such that A6:k=m+1 & k <=n;
    m'<=n by A6,NAT_1:13;
    then consider aM be Matrix of n,K such that
      A7:Det aM = power(K).(a,m') * Det M and
      A8:for i st i in Seg n holds
            (i<=m' implies Line(aM,i)=a*Line(M,i))&
            (i>m' implies Line(aM,i)=Line(M,i)) by A5;
    take R=RLine(aM,k,a*Line(aM,k));
    0+1<=k by A6,XREAL_1:9;
    then k in Seg n by A6;
    hence Det R = a * (power(K).(a,m') * Det M) by A7,MATRIX11:35
               .= power(K).(a,m') *a * Det M by GROUP_1:def 4
               .= power(K).(a,k) *Det M by A6,GROUP_1:def 8;
    let i such that A9:i in Seg n;
    per cases by REAL_1:def 5;
      suppose i< k or i>k;
        then Line(R,i)=Line(aM,i) & (i<=m & i<k or i>m' & i>k)
          by A6,A9,MATRIX11:28,NAT_1:13;
        hence thesis by A8,A9;end;
      suppose A10:i=k;
        len (a*Line(aM,k)) = len Line(aM,k) by MATRIXR1:16
                          .= width aM by MATRIX_1:def 8;
        then Line(R,i)=a*Line(aM,k) & i > m' by A6,A9,A10,MATRIX11:28,NAT_1:13;
        hence thesis by A8,A9,A10;end;
  end;
  for m holds P[m] from NAT_1:sch 1(A1,A4);
  then consider aM be Matrix of n,K such that
    A11:Det aM = power(K).(a,n) * Det M and
    A12:for i st i in Seg n holds
      (i<=n implies Line(aM,i)=a*Line(M,i))&(i>n implies Line(aM,i)=Line(M,i));
  set AM=a*M;
  A13:len aM=n & len AM = n & len M=n by MATRIX_1:def 3;
  now let i be Nat such that A14:1<=i & i<=n;
    i in NAT by ORDINAL1:def 13;
    then
    A15:i in Seg n by A14;
    hence aM.i = Line(aM,i) by MATRIX_2:10
              .= a*Line(M,i) by A12,A14,A15
              .= Line(AM,i) by A13,A14,MATRIXR1:20
              .= AM.i by A15,MATRIX_2:10;
  end;
  hence thesis by A11,A13,FINSEQ_1:18;
end;

theorem Th72:
  [:P,Q:] c= Indices M & card P = card Q implies
    Det EqSegm(a*M,P,Q) = power(K).(a,card P) * Det EqSegm(M,P,Q)
proof
  assume that A1:[:P,Q:] c= Indices M and A2:card P = card Q;
  EqSegm(a*M,P,Q) = Segm(a*M,P,Q) by A2,Def3
                 .= a * Segm(M,P,Q) by A1,Th63
                 .= a * EqSegm(M,P,Q) by A2,Def3;
  hence thesis by Th71;
end;

definition
  let K be Field;let M be Matrix of K;
  func the_rank_of M -> Element of NAT means
    :Def4:(ex P,Q st [:P,Q:] c= Indices M & card P = card Q & card P = it &
         Det EqSegm(M,P,Q)<>0.K)
    & for P1,Q1 st [:P1,Q1:] c= Indices M & card P1 = card Q1 &
         Det EqSegm(M,P1,Q1) <> 0.K holds card P1 <= it;
  existence
  proof
    defpred P[Nat] means
      ex P,Q st [:P,Q:] c= Indices M & card P=card Q & card Q=$1 &
        Det EqSegm(M,P,Q)<>0.K;
    A1:for k st P[k] holds k <= len M
      proof
        let k;assume P[k];
        then consider P,Q such that
          A2:[:P,Q:] c= Indices M & card P=card Q & card Q=k and
          Det EqSegm(M,P,Q)<>0.K;
        P c= Seg len M & card Seg len M =len M by A2,Th67,FINSEQ_1:78;
        hence thesis by A2,CARD_1:80;
      end;
    A3:ex k st P[k]
    proof
      consider E be empty finite without_zero Subset of NAT;
      reconsider E as finite without_zero Subset of NAT;
      take C=card E;take P=E,Q=E;
      Det EqSegm(M,E,E) = 1_K & E c= Seg len M & E c= Seg width M
        by CARD_1:78,MATRIXR2:41,XBOOLE_1:2;
      hence thesis by Th67,VECTSP_1:def 21;
    end;
    consider k such that
      A4:P[k] and A5:for n st P[n] holds n <= k from NAT_1:sch 6(A1,A3);
    take k;thus thesis by A4,A5;
  end;
  uniqueness
  proof
    let n1,n2 be Element of NAT such that
      A6:ex P,Q st [:P,Q:]c=Indices M & card P=card Q & card P=n1&
          Det EqSegm(M,P,Q)<>0.K and
      A7:for P1,Q1 st [:P1,Q1:] c= Indices M & card P1=card Q1 &
         Det EqSegm(M,P1,Q1) <> 0.K holds card P1 <= n1 and
      A8:ex P,Q st [:P,Q:]c=Indices M & card P=card Q & card P=n2&
          Det EqSegm(M,P,Q)<>0.K and
      A9:for P1,Q1 st [:P1,Q1:] c= Indices M & card P1=card Q1 &
         Det EqSegm(M,P1,Q1) <> 0.K holds card P1 <= n2;
      n1<=n2 & n2<= n1 by A6,A7,A8,A9;
      hence thesis by XXREAL_0:1;
  end;
end;

theorem Th73:
  for P,Q st [:P,Q:] c= Indices M & card P = card Q holds
    card P <= len M & card Q <= width M
proof
  let P,Q such that A1:[:P,Q:] c= Indices M & card P = card Q;
  P c= Seg len M & Q c= Seg width M by A1,Th67;
  then card P<=card Seg len M & card Q <=card Seg width M &
    card Seg len M=len M by CARD_1:80,FINSEQ_1:78;
  hence thesis by FINSEQ_1:78;
end;

theorem Th74:
  the_rank_of M <= len M & the_rank_of M <= width M
proof
  ex P,Q st [:P,Q:] c= Indices M & card P = card Q & card P = the_rank_of M &
     Det EqSegm(M,P,Q)<>0.K by Def4;
  hence thesis by Th73;
end;

Lm5:[:rng nt,rng mt:] c= Indices M & (n=0 iff m=0) implies
  ex P1,P2 st P1 = rng nt & P2 = rng mt
proof
  assume that A1:[:rng nt,rng mt:] c= Indices M and
              A2:n=0 iff m=0;
  A3:Seg n={} iff Seg m={} by A2,FINSEQ_1:5;
  A4:rng nt is without_zero
  proof
    assume A5:0 in rng nt;
    then consider x such that
      A6:x in dom nt & nt.x=0 by FUNCT_1:def 5;
    dom nt=Seg n & dom mt=Seg m by FINSEQ_2:144;
    then rng mt<>{} & dom M=Seg len M by A3,A6,FINSEQ_1:def 3,RELAT_1:65;
    then [:rng nt,rng mt:]<>{} & Indices M=[:Seg len M,Seg width M:]
      by A5,ZFMISC_1:113;
    then rng nt c= Seg len M by A1,ZFMISC_1:138;
    hence thesis by A5,FINSEQ_1:3;
  end;
  rng mt is without_zero
  proof
    assume A7:0 in rng mt;
    then consider x such that
      A8:x in dom mt & mt.x=0 by FUNCT_1:def 5;
    dom nt=Seg n & dom mt=Seg m by FINSEQ_2:144;
    then rng nt<>{} & dom M=Seg len M by A3,A8,FINSEQ_1:def 3,RELAT_1:65;
    then [:rng nt,rng mt:]<>{} & Indices M=[:Seg len M,Seg width M:]
      by A7,ZFMISC_1:113;
    then rng mt c= Seg width M by A1,ZFMISC_1:138;
    hence thesis by A7,FINSEQ_1:3;
  end;
  then rng nt is finite without_zero Subset of NAT &
    rng mt is finite without_zero Subset of NAT by A4,FINSEQ_1:def 4;
    hence thesis;
end;

theorem Th75:
  [:rng nt1,rng nt2:] c= Indices M & Det Segm(M,nt1,nt2)<>0.K implies
    ex P1,P2 st P1 = rng nt1 & P2 = rng nt2 & card P1 = card P2 &
                card P1 = n & Det EqSegm(M,P1,P2) <>0.K
proof
  assume that A1:[:rng nt1,rng nt2:] c= Indices M and
              A2:Det Segm(M,nt1,nt2)<>0.K;
  n=0 iff n=0;
  then consider P1,P2 such that
    A3:P1 = rng nt1 & P2 = rng nt2 by A1,Lm5;
  nt1 is one-to-one & nt2 is one-to-one by A2,Th27,Th31;
  then A4:card P1 =len nt1 & card P2 =len nt2 & len nt1=n & len nt2=n
    by A3,FINSEQ_2:109,FINSEQ_4:77;
  then reconsider SP1=Sgm P1,SP2=Sgm P2 as Element of n-tuples_on NAT;
  consider k such that A5:P1 c= Seg k by Th43;
  consider m such that A6:P2 c= Seg m by Th43;
  A7:Segm(M,Sgm P1,Sgm P2) = Segm(M,P1,P2)
                          .= EqSegm(M,P1,P2) by A4,Def3;
  rng SP1=P1 & rng SP2=P2 by A5,A6,FINSEQ_1:def 13;
  then Det Segm(M,nt1,nt2) = Det Segm(M,SP1,SP2) or
      -Det Segm(M,nt1,nt2) = Det Segm(M,SP1,SP2) by A3,Th36;
  then Det EqSegm(M,P1,P2) <>0.K by A2,A4,A7,VECTSP_1:86;
  hence thesis by A3,A4;
end;

theorem Th76:
  for RANK be Element of NAT holds the_rank_of M = RANK iff
          (ex rt1,rt2 be Element of RANK-tuples_on NAT st
              [:rng rt1,rng rt2:] c= Indices M & Det Segm(M,rt1,rt2)<>0.K) &
          for n,nt1,nt2 st [:rng nt1,rng nt2:] c= Indices M &
                         Det Segm(M,nt1,nt2)<>0.K holds n <= RANK
proof
  let RANK be Element of NAT;
  thus the_rank_of M = RANK implies
          (ex rt1,rt2 be Element of RANK-tuples_on NAT st
              [:rng rt1,rng rt2:] c= Indices M & Det Segm(M,rt1,rt2)<>0.K) &
          for n,nt1,nt2 st [:rng nt1,rng nt2:] c= Indices M &
                         Det Segm(M,nt1,nt2)<>0.K holds n <= RANK
  proof
    assume A1:the_rank_of M = RANK;
    then consider P,Q such that
      A2:[:P,Q:] c= Indices M & card P = card Q & card P = RANK and
      A3:Det EqSegm(M,P,Q)<>0.K by Def4;
    reconsider Sp=Sgm P,Sq=Sgm Q as Element of RANK-tuples_on NAT by A2;
    consider k such that A4:P c= Seg k by Th43;
    consider m such that A5:Q c= Seg m by Th43;
    A6:rng Sp=P & rng Sq=Q by A4,A5,FINSEQ_1:def 13;
    EqSegm(M,P,Q) = Segm(M,P,Q) by A2,Def3
                 .= Segm(M,Sgm P,Sgm Q);
    hence ex rt1,rt2 be Element of RANK-tuples_on NAT st
      [:rng rt1,rng rt2:] c= Indices M & Det Segm(M,rt1,rt2)<>0.K by A2,A3,A6;
    let n,nt1,nt2 such that A7:[:rng nt1,rng nt2:] c= Indices M and
                          A8:Det Segm(M,nt1,nt2)<>0.K;
    consider P1,P2 such that
      A9:P1 = rng nt1 & P2 = rng nt2 and
      A10:card P1=card P2 & card P1 = n and
      A11:Det EqSegm(M,P1,P2) <>0.K by A7,A8,Th75;
    thus thesis by A1,A7,A9,A10,A11,Def4;
  end;
  assume that A12:ex rt1,rt2 be Element of RANK-tuples_on NAT st
       [:rng rt1,rng rt2:] c= Indices M & Det Segm(M,rt1,rt2)<>0.K and
              A13:for n for nt1,nt2 st [:rng nt1,rng nt2:] c= Indices M &
       Det Segm(M,nt1,nt2)<>0.K holds n <= RANK;
  consider rt1,rt2 be Element of RANK-tuples_on NAT such that
    A14:[:rng rt1,rng rt2:] c= Indices M & Det Segm(M,rt1,rt2)<>0.K by A12;
  consider P1,P2 such that
    A15:P1 = rng rt1 & P2 = rng rt2 and
    A16:card P1=card P2 & card P1 = RANK and
    A17:Det EqSegm(M,P1,P2) <>0.K by A14,Th75;
  consider P,Q such that
    A18:[:P,Q:] c= Indices M & card P = card Q & card P = the_rank_of M and
    A19:Det EqSegm(M,P,Q)<>0.K by Def4;
  reconsider SP=Sgm P,SQ=Sgm Q as Element of (card P)-tuples_on NAT by A18;
  consider k such that A20:P c= Seg k by Th43;
  consider m such that A21:Q c= Seg m by Th43;
  EqSegm(M,P,Q) = Segm(M,P,Q) by A18,Def3
               .= Segm(M,Sgm P,Sgm Q);
  then Det EqSegm(M,P,Q)=Det Segm(M,SP,SQ) & rng SP=P & rng SQ=Q
    by A18,A20,A21,FINSEQ_1:def 13;
  then card P <= RANK &  RANK <= card P by A13,A14,A15,A16,A17,A18,A19,Def4;
  hence thesis by A18,XXREAL_0:1;
end;

theorem Th77:
  n = 0 or m = 0 implies the_rank_of Segm(M,nt,mt) = 0
proof
  set S=Segm(M,nt,mt);
  assume n=0 or m = 0;
  then len S=0 or width S=0 by Th1,MATRIX_1:def 3;
  then the_rank_of S <= 0 by Th74;
  hence thesis;
end;

theorem Th78:
  [:rng nt,rng mt:] c= Indices M implies
    the_rank_of M >= the_rank_of Segm(M,nt,mt)
proof
  assume A1:[:rng nt,rng mt:] c= Indices M;
  per cases;
    suppose n=0 or m=0;
      hence thesis by Th77;end;
    suppose A2:n>0 & m>0;
      set S=Segm(M,nt,mt);set RS=the_rank_of S;
      consider rt1,rt2 be Element of RS-tuples_on NAT such that
        A3:[:rng rt1,rng rt2:] c= Indices S & Det Segm(S,rt1,rt2)<>0.K by Th76;
      consider R1,R2 be finite without_zero Subset of NAT such that
        A4:R1 = rng rt1 & R2 = rng rt2 and
        A5:card R1=card R2 & card R1 = RS and
        Det EqSegm(S,R1,R2) <>0.K by A3,Th75;
      set nr=nt*rt1;set mr=mt*rt2;
      len S=n & width S=m & dom nt=Seg n & dom mt=Seg m
        by A2,Th1,FINSEQ_2:144;
      then A6:rng rt1 c= dom nt & rng rt2 c= dom mt by A3,A4,A5,Th67;
      then A7:dom nr=dom rt1 & rng nr c= rng nt & rng nt c= NAT &
               dom mr=dom rt2 & rng mr c= rng mt & rng mt c= NAT
        by FINSEQ_1:def 4,RELAT_1:45,46;
      then A8:dom nr=Seg RS & rng nr c= NAT & dom mr=Seg RS & rng mr c= NAT
        by FINSEQ_2:144,XBOOLE_1:1;
      reconsider nr,mr as FinSequence by A6,FINSEQ_1:20;
      reconsider nr,mr as FinSequence of NAT by A8,FINSEQ_1:def 4;
      len nr=RS & len mr=RS by A8,FINSEQ_1:def 3;
      then reconsider nr,mr as Element of RS-tuples_on NAT by FINSEQ_2:110;
      set SS=Segm(S,rt1,rt2);set MR=Segm(M,nr,mr);
      now let i,j be Nat such that A9:[i,j] in Indices SS;
        reconsider I=i,J=j,rtI=rt1.i,rtJ=rt2.j as Element of NAT
          by ORDINAL1:def 13;
        A10:[rt1.I,rt2.J] in Indices S by A3,A9,Th17;
        Indices SS=[:dom nr,dom mr:] by A8,MATRIX_1:25;
        then A11:i in dom nr & j in dom mr & [i,j] in Indices MR
          by A9,MATRIX_1:27,ZFMISC_1:106;
        hence MR*(i,j) = M*(nr.I,mr.J) by Def1
                      .= M*(nt.rtI,mr.J) by A11,FUNCT_1:22
                      .= M*(nt.rtI,mt.rtJ) by A11,FUNCT_1:22
                      .= S*(rt1.I,rt2.J) by A10,Def1
                      .= SS*(i,j) by A9,Def1;
      end;
      then A12:SS=MR by MATRIX_1:28;
      [:rng nr,rng mr:] c= [:rng nt,rng mt:] by A7,ZFMISC_1:119;
      then [:rng nr,rng mr:] c= Indices M by A1,XBOOLE_1:1;
      hence thesis by A3,A12,Th76;end;
end;

theorem Th79:
  [:P,Q:] c= Indices M implies the_rank_of M >= the_rank_of Segm(M,P,Q)
proof
  assume A1:[:P,Q:] c= Indices M;
  consider k such that A2:P c= Seg k by Th43;
  consider n such that A3:Q c= Seg n by Th43;
   rng Sgm P=P & rng Sgm Q=Q by A2,A3,FINSEQ_1:def 13;
  hence thesis by A1,Th78;
end;

theorem Th80:
   P c= P1 & Q c= Q1 implies
    the_rank_of Segm(M,P,Q) <= the_rank_of Segm(M,P1,Q1)
proof
  assume that A1:P c= P1 & Q c= Q1;
  set S=Segm(M,P,Q);set S1=Segm(M,P1,Q1);
  consider P2,Q2 such that
    A2:[:P2,Q2:] c= Indices S & card P2 = card Q2 and
    A3:card P2 = the_rank_of S & Det EqSegm(S,P2,Q2)<>0.K by Def4;
  P2={} iff Q2={} by A2,CARD_1:47,CARD_2:59;
  then consider P3,Q3 be without_zero finite Subset of NAT such that
    A4:P3 c= P & Q3 c= Q and
    P3 = Sgm P.:P2 & Q3=Sgm Q.:Q2 and
    A5:card P3=card P2 & card Q3=card Q2 and
    A6:Segm(S,P2,Q2) = Segm(M,P3,Q3) by A2,Th57;
  reconsider P4=Sgm P1"P3, Q4=Sgm Q1"Q3 as without_zero finite Subset of NAT
    by Th53;
  consider k such that A7:P4 c= Seg k by Th43;
  consider k such that A8:Q4 c= Seg k by Th43;
  P3 c= P1 & Q3 c= Q1 & rng Sgm P4=P4 & rng Sgm Q4=Q4
    by A1,A4,A7,A8,FINSEQ_1:def 13,XBOOLE_1:1;
  then A9:Segm(S1,P4,Q4) = Segm(M,P3,Q3) & card P4=card P2 &
    card Q4=card P2 & [:P4,Q4:] c= Indices S1 by A2,A5,Lm2,Th56;
  EqSegm(S,P2,Q2) = Segm(S1,P4,Q4) by A2,A6,A9,Def3
                 .= EqSegm(S1,P4,Q4) by A9,Def3;
  hence thesis by A3,A9,Def4;
end;

theorem Th81:
  for f,g be Function st rng f c= rng g
    ex h be Function st dom h = dom f & rng h c= dom g & f = g*h
proof
  let f,g be Function such that A1:rng f c= rng g;
  defpred P[set,set] means f.$1=g.$2;
  A2:for x st x in dom f ex y st y in dom g & P[x,y] by A1,GRAPH_5:2;
  consider h be Function of dom f,dom g such that
    A3:for x st x in dom f holds P[x,h.x] from FUNCT_2:sch 1(A2);
  per cases;
    suppose dom g={};
      then rng g={} by RELAT_1:65;
      then rng f={} by A1,XBOOLE_1:3;
      then f=g*{} & dom {}=dom f & rng {} c= dom g by RELAT_1:64,XBOOLE_1:2;
      hence thesis;end;
    suppose dom g<>{};
      then A4:dom h=dom f & rng h c= dom g by FUNCT_2:def 1,RELSET_1:12;
      then A5:dom (g*h)=dom f by RELAT_1:46;
      now let x such that A6:x in dom f;
        thus f.x = g.(h.x) by A3,A6
                .= (g*h).x by A5,A6,FUNCT_1:22;
      end;
      then f=g*h by A5,FUNCT_1:9;
      hence thesis by A4;end;
end;

theorem Th82:
  [:rng nt,rng mt:] = Indices M implies
     the_rank_of M = the_rank_of Segm(M,nt,mt)
proof
  set RM=the_rank_of M;set S=Segm(M,nt,mt);
  assume A1:[:rng nt,rng mt:] = Indices M;
  consider rt1,rt2 be Element of RM-tuples_on NAT such that
A2:[:rng rt1,rng rt2:] c= Indices M & Det Segm(M,rt1,rt2)<>0.K by Th76;
  now per cases;
    suppose RM=0;
      hence RM <= the_rank_of S;end;
    suppose RM>0;
      then len rt1>0 & len rt2>0 by FINSEQ_2:109;
      then rt1<>{} & rt2<>{} by FINSEQ_1:25;
      then A3:rng rt1<>{} & rng rt2<>{} by FINSEQ_1:27;
      then [:rng rt1,rng rt2:]<>{} by ZFMISC_1:113;
      then A4:rng rt1 c= rng nt & rng rt2 c= rng mt by A1,A2,ZFMISC_1:138;
      then consider R1 be Function such that
        A5:dom R1 = dom rt1 & rng R1 c= dom nt & rt1 = nt*R1 by Th81;
      consider R2 be Function such that
        A6:dom R2 = dom rt2 & rng R2 c= dom mt & rt2 = mt*R2 by A4,Th81;
      A7:dom rt1=Seg RM & dom rt2=Seg RM by FINSEQ_2:144;
      then reconsider R1,R2 as FinSequence by A5,A6,FINSEQ_1:def 2;
      A8:dom nt=Seg n & dom mt =Seg m by FINSEQ_2:144;
      then rng R1 c= NAT & rng R2 c= NAT by A5,A6,XBOOLE_1:1;
      then reconsider R1,R2 as FinSequence of NAT by FINSEQ_1:def 4;
      len R1=RM & len R2=RM by A5,A6,A7,FINSEQ_1:def 3;
      then reconsider R1,R2 as Element of RM-tuples_on NAT by FINSEQ_2:110;
      rng nt<>{} by A3,A4,XBOOLE_1:3;
      then dom nt<>{} & dom nt=Seg n by FINSEQ_2:144,RELAT_1:65;
      then len S=n & width S=m by Th1,FINSEQ_1:4;
      then A9:Indices S=[:Seg n,Seg m:] by FINSEQ_1:def 3;
      set SR=Segm(S,R1,R2);set MR=Segm(M,rt1,rt2);
      now let i,j be Nat such that A10:[i,j] in Indices SR;
        reconsider I=i,J=j,RI=R1.i,RJ=R2.j as Element of NAT
          by ORDINAL1:def 13;
        Indices SR=[:dom R1,dom R2:] by A5,A6,A7,MATRIX_1:25;
        then A11:i in dom R1 & j in dom R2 & [i,j] in Indices MR
          by A10,MATRIX_1:27,ZFMISC_1:106;
        then R1.i in rng R1 & R2.j in rng R2 by FUNCT_1:def 5;
        then R1.i in dom nt & R2.j in dom mt & dom nt=Seg n & dom mt=Seg m
          by A5,A6,FINSEQ_2:144;
        then A12:[R1.I,R2.J] in Indices S by A9,ZFMISC_1:106;
        thus MR*(i,j) = M*(rt1.I,rt2.J) by A11,Def1
                     .= M*(nt.RI,rt2.J) by A5,A11,FUNCT_1:22
                     .= M*(nt.RI,mt.RJ) by A6,A11,FUNCT_1:22
                     .= S*(R1.I,R2.J) by A12,Def1
                     .= SR*(i,j) by A10,Def1;
      end;
      then Det SR<>0.K & [:rng R1,rng R2:] c= Indices S
        by A2,A5,A6,A8,A9,MATRIX_1:28,ZFMISC_1:119;
      hence RM <= the_rank_of S by Th76; end;
  end;
  then RM <= the_rank_of S & RM >= the_rank_of S by A1,Th78;
  hence thesis by XXREAL_0:1;
end;

theorem Th83:
  for M be Matrix of n,K holds
    the_rank_of M = n iff Det M <> 0.K
proof
  let M be Matrix of n,K;
  A1:len M=n & width M=n & card Seg n=n by FINSEQ_1:78,MATRIX_1:25;
  thus the_rank_of M = n implies Det M <> 0.K
  proof
    assume the_rank_of M = n;
    then consider P,Q such that
      A2:[:P,Q:] c= Indices M & card P = card Q & card P = n and
      A3:Det EqSegm(M,P,Q)<>0.K by Def4;
    P c= Seg n & Q c= Seg n by A1,A2,Th67;
    then P=Seg n & Q=Seg n by A1,A2,CARD_FIN:1;
    then M = Segm(M,P,Q) by A1,Th46
          .= EqSegm(M,P,Q) by A2,Def3;
    hence thesis by A2,A3;
  end;
  assume A4:Det M <> 0.K;
  M = Segm(M,Seg n,Seg n) by A1,Th46
   .= EqSegm(M,Seg n,Seg n) by Def3;
  then [:Seg n,Seg n:] c= Indices M & Det M = Det EqSegm(M,Seg n,Seg n)
    by FINSEQ_1:78,MATRIX_1:25;
  then the_rank_of M >= n & the_rank_of M <= n by A1,A4,Def4,Th74;
  hence thesis by XXREAL_0:1;
end;

theorem
  the_rank_of M = the_rank_of (M@)
proof
  consider P,Q such that
    A1:[:P,Q:] c= Indices M & card P = card Q and
    A2:card P = the_rank_of M & Det EqSegm(M,P,Q)<>0.K by Def4;
  [:Q,P:] c= Indices (M@) & Det EqSegm(M@,Q,P)<>0.K by A1,A2,Th69,Th70;
  then A3:the_rank_of (M@) >= the_rank_of M by A1,A2,Def4;
  consider P1,Q1 such that
    A4:[:P1,Q1:] c= Indices (M@) & card P1 = card Q1 and
    A5:card P1 = the_rank_of (M@) & Det EqSegm(M@,P1,Q1)<>0.K by Def4;
  A6:[:Q1,P1:] c= Indices M by A4,Th69;
  then Det EqSegm(M,Q1,P1)<>0.K by A4,A5,Th70;
  then the_rank_of M >= the_rank_of (M@) by A4,A5,A6,Def4;
  hence thesis by A3,XXREAL_0:1;
end;

Lm6:a <> 0.K implies power(K).(a,n)<>0.K
proof
  assume A1:a<>0.K;
  defpred P[Nat] means for n st n=$1 holds power(K).(a,n)<>0.K;
  A2:P[0]
  proof
    let n such that A3:n=0;
    1_K<>0.K by VECTSP_1:def 21;
    hence thesis by A3,GROUP_1:def 8;
    end;
  A4:for k st P[k] holds P[k+1]
  proof
    let k such that A5:P[k];
    let n such that A6:n=k+1;
    power(K).(a,n)=power(K).(a,k) * a & power(K).(a,k) <>0.K
      by A5,A6,GROUP_1:def 8;
    hence thesis by A1,VECTSP_1:44;
  end;
  for k holds P[k] from NAT_1:sch 1(A2,A4);
  hence thesis;
end;

theorem
  for M be Matrix of n,m,K,F be Permutation of Seg n holds
    the_rank_of M = the_rank_of (M*F)
proof
  let M be Matrix of n,m,K,F be Permutation of Seg n;
  set P=Seg len M;set Q=Seg width M;set SP=Sgm P;set SQ=Sgm Q;
  A1:len M=n & card P=len M by FINSEQ_1:78,MATRIX_1:def 3;
  then reconsider F'=F as Permutation of Seg card P;
  dom SP=Seg card P & rng F=Seg n
    by FINSEQ_3:45,FUNCT_2:def 3;
  then A2:dom (SP*F)=dom F & rng (SP*F)=rng SP & dom F=Seg n
     by A1,FUNCT_2:67,RELAT_1:46,47;
  then reconsider SPF=SP*F as FinSequence by FINSEQ_1:def 2;
  rng SP c= NAT by FINSEQ_1:def 4;
  then reconsider SPF as FinSequence of NAT by A2,FINSEQ_1:def 4;
  len SPF=card P by A1,A2,FINSEQ_1:def 3;
  then reconsider SPF as Element of (card P)-tuples_on NAT by FINSEQ_2:110;
  A3:Segm(M,SPF,SQ) = Segm(M,P,Q) * F' by Th33
                   .= M*F by Th46;
  Indices M=[:Seg len M,Seg width M:] & rng SP=P & rng SQ=Q
    by FINSEQ_1:def 3,def 13;
  hence the_rank_of M =the_rank_of (M*F) by A2,A3,Th82;
end;

theorem
  a <> 0.K implies the_rank_of M = the_rank_of (a*M)
proof
  assume A1:a<>0.K;
  A2:Indices M = Indices (a*M) by MATRIXR1:18;
  consider P,Q such that
    A3:[:P,Q:] c= Indices M & card P = card Q and
    A4:card P = the_rank_of M & Det EqSegm(M,P,Q)<>0.K by Def4;
  Det EqSegm(a*M,P,Q)=power(K).(a,card P)*Det EqSegm(M,P,Q) &
    power(K).(a,card P)<>0.K by A1,A3,Lm6,Th72;
  then Det EqSegm(a*M,P,Q)<>0.K by A4,VECTSP_1:44;
  then A5:the_rank_of (a*M) >= the_rank_of M by A2,A3,A4,Def4;
  consider P1,Q1 such that
    A6:[:P1,Q1:] c= Indices (a*M) & card P1 = card Q1 and
    A7:card P1 = the_rank_of (a*M) & Det EqSegm(a*M,P1,Q1)<>0.K by Def4;
  Det EqSegm(a*M,P1,Q1)=power(K).(a,card P1)*Det EqSegm(M,P1,Q1) by A2,A6,Th72;
  then Det EqSegm(M,P1,Q1)<>0.K by A7,VECTSP_1:44;
  then the_rank_of M >= the_rank_of (a*M) by A2,A6,A7,Def4;
  hence thesis by A5,XXREAL_0:1;
end;

theorem Th87:
  for p,pf be FinSequence of K, f be Function st pf = p * f & rng f c= dom p
    holds (a*p) * f = a * pf
proof
  let p,pf be FinSequence of K, f be Function such that
    A1:pf = p * f and A2:rng f c= dom p;
  len (a*p)=len p & len (a*pf)=len pf by MATRIXR1:16;
  then A3:dom (a*p)=Seg len p & Seg len p=dom p & dom (a*pf)=Seg len pf
    & Seg len pf =dom pf by FINSEQ_1:def 3;
  then A4:dom (a*p*f)=dom f & dom (a*pf)=dom f by A1,A2,RELAT_1:46;
  now let x such that A5:x in dom (a*pf);set KK=the carrier of K;
    f.x in rng f by A4,A5,FUNCT_1:def 5;
    then A6:f.x in dom p & p.(f.x) in rng p & rng p c= KK & pf.x in rng pf &
      rng pf c= KK by A2,A3,A5,FINSEQ_1:def 4,FUNCT_1:def 5;
    then reconsider pf'x=pf.x,p'fx=p.(f.x) as Element of K;
    thus (a*pf).x = a*pf'x by A3,A5,FVSUM_1:62
                 .= a*p'fx by A1,A3,A5,FUNCT_1:22
                 .= (a*p).(f.x) by A3,A6,FVSUM_1:62
                 .= ((a*p)*f).x by A4,A5,FUNCT_1:22;
    end;
  hence thesis by A4,FUNCT_1:9;
end;

theorem Th88:
  for p,pf,q,qf be FinSequence of K, f be Function st
       pf = p * f & rng f c= dom p & qf = q * f & rng f c= dom q
    holds (p+q) * f = pf + qf
proof
  let p,pf,q,qf be FinSequence of K, f be Function such that
    A1:pf = p * f & rng f c= dom p and A2:qf = q * f & rng f c= dom q;
  set KK=the carrier of K; set pq=p+q;
  rng p c= KK & rng q c= KK by FINSEQ_1:def 4;
  then [:rng p,rng q:]c=[:KK,KK:] by ZFMISC_1:119;
  then [:rng p, rng q:] c= dom the add of K by FUNCT_2:def 1;
  then dom pq=dom p /\ dom q by FUNCOP_1:84;
  then A3:rng f c= dom pq by A1,A2,XBOOLE_1:19;
  then A4:dom (pq*f)=dom f by RELAT_1:46;
  A5:dom pf=dom f & dom qf=dom f & dom pf=Seg len pf
    by A1,A2,FINSEQ_1:def 3,RELAT_1:46;
  then len qf=len pf by FINSEQ_1:def 3;
  then reconsider pf'=pf,qf'=qf as Element of (len pf)-tuples_on KK
    by FINSEQ_2:110;
  A6:dom (pf'+qf')=dom f by A5,FINSEQ_2:144;
  now let x such that A7:x in dom f;
    A8:f.x in rng f & dom p=Seg len p by A7,FINSEQ_1:def 3,FUNCT_1:def 5;
    then f.x in Seg len p by A1;
    then reconsider n=x,fx=f.x as Element of NAT by A5,A7;
    pf.x in rng pf & rng pf c= KK & qf.x in rng qf & rng qf c= KK
      by A5,A7,FINSEQ_1:def 4,FUNCT_1:def 5;
    then reconsider pfn=pf.n,qfn=qf.n as Element of K;
    A9:pfn=p.fx & qfn=q.fx by A1,A2,A7,FUNCT_1:23;
    thus (pq*f).x = pq.fx by A7,FUNCT_1:23
                 .= pfn+qfn by A3,A8,A9,FVSUM_1:21
                 .= (pf'+qf').x by A5,A7,FVSUM_1:22;
  end;
  hence thesis by A4,A6,FUNCT_1:9;
end;

theorem Th89:
  a<>0.K implies the_rank_of M' = the_rank_of RLine(M',i,a*Line(M',i))
proof
  assume A1:a<>0.K;set L=Line(M',i);set aL=a*L;set R=RLine(M',i,aL);
  consider P,Q such that
    A2:[:P,Q:] c= Indices M' & card P = card Q and
    A3:card P = the_rank_of M' & Det EqSegm(M',P,Q)<>0.K by Def4;
  A4:Indices M'=Indices R by MATRIX_1:27;
  A5:now per cases;
    suppose A6:i in P;
      consider k such that A7:P c= Seg k by Th43;
      rng Sgm P=P by A7,FINSEQ_1:def 13;
      then consider x such that
        A8:x in dom Sgm P & Sgm P.x=i by A6,FUNCT_1:def 5;
      A9:dom Sgm P=Seg card P by A7,FINSEQ_3:45;
      then reconsider x as Element of NAT by A8;
      consider n such that A10:Q c= Seg n by Th43;
      A11:rng Sgm Q=Q by A10,FINSEQ_1:def 13;
      A12:len L=width M' by MATRIX_1:def 8;
      then A13:Q c= Seg width M' & dom L=Seg width M'
        by A2,Th67,FINSEQ_1:def 3;
      then Line(Segm(M',P,Q),x) = L * Sgm Q by A8,A9,Th47;
      then A14:(a*Line(Segm(M',P,Q),x)) = aL * Sgm Q & len aL=len L
        by A11,A13,Th87,MATRIXR1:16;
      RLine(EqSegm(M',P,Q),x,a*Line(EqSegm(M',P,Q),x))
          = RLine(Segm(M',P,Q),x,a*Line(EqSegm(M',P,Q),x)) by A2,Def3
         .= RLine(Segm(M',P,Q),x,a*Line(Segm(M',P,Q),x)) by A2,Def3
         .= Segm(R,P,Q)  by A2,A8,A12,A14,Th59
         .= EqSegm(R,P,Q) by A2,Def3;
      then Det EqSegm(R,P,Q)= a * Det EqSegm(M',P,Q) by A8,A9,MATRIX11:35;
      then Det EqSegm(R,P,Q)<>0.K by A1,A3,VECTSP_1:44;
      hence the_rank_of R >= the_rank_of M' by A2,A3,A4,Def4;end;
    suppose A15:not i in P;
      EqSegm(M',P,Q) = Segm(M',P,Q) by A2,Def3
                    .= Segm(R,P,Q) by A2,A15,Th60
                    .= EqSegm(R,P,Q) by A2,Def3;
      hence the_rank_of R >= the_rank_of M' by A2,A3,A4,Def4;end;
  end;
  consider P1,Q1 such that
    A16:[:P1,Q1:] c= Indices R & card P1 = card Q1 and
    A17:card P1 = the_rank_of R & Det EqSegm(R,P1,Q1)<>0.K by Def4;
  now per cases;
    suppose A18:i in P1;
      consider k such that A19:P1 c= Seg k by Th43;
      rng Sgm P1=P1 by A19,FINSEQ_1:def 13;
      then consider x such that
        A20:x in dom Sgm P1 & Sgm P1.x=i by A18,FUNCT_1:def 5;
      A21:dom Sgm P1=Seg card P1 by A19,FINSEQ_3:45;
      then reconsider x as Element of NAT by A20;
      consider n such that A22:Q1 c= Seg n by Th43;
      A23:rng Sgm Q1=Q1 by A22,FINSEQ_1:def 13;
      A24:len L=width M' by MATRIX_1:def 8;
      then A25:Q1 c= Seg width M' & dom L=Seg width M'
        by A4,A16,Th67,FINSEQ_1:def 3;
      then Line(Segm(M',P1,Q1),x) = L * Sgm Q1 by A20,A21,Th47;
      then A26:(a*Line(Segm(M',P1,Q1),x)) = aL * Sgm Q1 &
        len aL=len L by A23,A25,Th87,MATRIXR1:16;
      RLine(EqSegm(M',P1,Q1),x,a*Line(EqSegm(M',P1,Q1),x))
        =RLine(Segm(M',P1,Q1),x,a*Line(EqSegm(M',P1,Q1),x)) by A16,Def3
       .=RLine(Segm(M',P1,Q1),x,a*Line(Segm(M',P1,Q1),x)) by A16,Def3
       .=Segm(R,P1,Q1) by A4,A16,A20,A24,A26,Th59
       .=EqSegm(R,P1,Q1) by A16,Def3;
      then Det EqSegm(R,P1,Q1)=a*Det EqSegm(M',P1,Q1) by A20,A21,MATRIX11:35;
      then Det EqSegm(M',P1,Q1)<>0.K by A17,VECTSP_1:44;
      hence the_rank_of M' >= the_rank_of R by A4,A16,A17,Def4;end;
    suppose A27:not i in P1;
      EqSegm(M',P1,Q1) = Segm(M',P1,Q1) by A16,Def3
                      .= Segm(R,P1,Q1) by A4,A16,A27,Th60
                      .= EqSegm(R,P1,Q1) by A16,Def3;
      hence the_rank_of M' >= the_rank_of R by A4,A16,A17,Def4;end;
  end;
  hence thesis by A5,XXREAL_0:1;
end;

theorem Th90:
  Line(M,i)=(width M) |-> 0.K implies
    the_rank_of DelLine(M,i) = the_rank_of M
proof
  assume A1:Line(M,i)=(width M) |-> 0.K;
  set D=DelLine(M,i);
  per cases;
    suppose not i in Seg len M;
      then Seg len M\{i}=Seg len M by ZFMISC_1:65;
      then DelLine(M,i) = Segm(M,Seg len M,Seg width M) by Th51
                       .= M by Th46;
      hence thesis;end;
    suppose i in Seg len M;
      consider P,Q such that
        A2:[:P,Q:] c= Indices D & card P = card Q & card P = the_rank_of D &
           Det EqSegm(D,P,Q)<>0.K by Def4;
      A3:Segm(M,Seg len M \ {i},Seg width M) = D by Th51;
      P={} iff Q={} by A2,CARD_1:47,CARD_2:59;
      then consider P2,Q2 such that
        A4:P2 c= Seg len M \ {i} & Q2 c= Seg width M and
        P2 = Sgm (Seg len M \ {i}).:P & Q2=Sgm (Seg width M).:Q and
        card P2=card P & card Q2=card Q and
        A5:Segm(D,P,Q) = Segm(M,P2,Q2) by A2,A3,Th57;
      EqSegm(D,P,Q)=Segm(D,P,Q) by A2,Def3;
      then A6:the_rank_of Segm(D,P,Q)=card P by A2,Th83;
      A7:Seg len M \ {i} c= Seg len M by XBOOLE_1:36;
      then A8:P2 c= Seg len M & Indices M=[:Seg len M,Seg width M:]
        by A4,FINSEQ_1:def 3,XBOOLE_1:1;
      then [:P2,Q2:] c= Indices M by A4,ZFMISC_1:119;
      then A9:the_rank_of D <= the_rank_of M by A2,A5,A6,Th79;
      consider p,q be without_zero finite Subset of NAT such that
        A10:[:p,q:] c= Indices M & card p = card q & card p = the_rank_of M &
           Det EqSegm(M,p,q)<>0.K by Def4;
      not i in p
      proof
        assume A11:i in p;
        then reconsider i0=i as non zero Element of NAT by PSCOMP_1:def 1;
        {i} c= p by A11,ZFMISC_1:37;
        then consider q1 be without_zero finite Subset of NAT such that
        A12:q1 c= q & card {i} = card q1 & Det EqSegm(M,{i0},q1) <> 0.K
          by A10,Th65;
        A13:card { i } = 1 by CARD_1:79;
        then consider y such that A14:{y}=q1 by A12,CARD_2:60;
        y in {y} & not 0 in q1 by PSCOMP_1:def 1,TARSKI:def 1;
        then reconsider y as non zero Element of NAT by A14;
        y in q1 by A14,TARSKI:def 1;
        then y in q & q c= Seg width M by A10,A12,Th67;
        then A15:M*(i0,y)=Line(M,i).y & Line(M,i).y =0.K
          by A1,FINSEQ_1:4,FINSEQ_2:71,MATRIX_1:def 8;
        EqSegm(M,{i0},q1) = Segm(M,{i0},{y}) by A12,A14,Def3
                         .= <*<* 0.K *>*> by A15,Th44;
        hence thesis by A12,A13,MATRIX_3:36;
      end;
      then p\{i}=p & p c= Seg len M by A10,Th67,ZFMISC_1:65;
      then p c= Seg len M\{i} & q c= Seg width M &
        [:Seg len M\{i},Seg width M:]c= Indices M
        by A7,A8,A10,Th67,XBOOLE_1:33,ZFMISC_1:119;
      then A16:the_rank_of Segm(M,p,q)<=the_rank_of D by A3,Th80;
      EqSegm(M,p,q)=Segm(M,p,q) by A10,Def3;
      then the_rank_of Segm(M,p,q)=card p by A10,Th83;
      hence thesis by A9,A10,A16,XXREAL_0:1;end;
end;

theorem Th91:
  for p st len p = width M' holds
    the_rank_of DelLine(M',i) = the_rank_of RLine(M',i,0.K*p)
proof
  let p such that A1:len p = width M';
  set R=RLine(M',i,0.K*p);set D=DelLine(M',i);
  A2:Seg len M'=dom M' & len M'=n' by FINSEQ_1:def 3,MATRIX_1:def 3;
  per cases;
    suppose not i in dom M';
      then R=M' & DelLine(M',i)=M' by A2,Th40,FINSEQ_3:113;
      hence thesis;end;
    suppose A3:i in dom M';
      then A4:width M'=m' & width R=m' by A2,Th1,FINSEQ_1:4;
      set KK=the carrier of K;
      A5:len (0.K *p)=len p by MATRIXR1:16;
      then Line(R,i)=0.K * p & p is Element of (len p)-tuples_on KK
        by A1,A2,A3,FINSEQ_2:110,MATRIX11:28;
      then Line(R,i)=(len p) |-> 0.K by FVSUM_1:71;
      then A6:the_rank_of R=the_rank_of DelLine(R,i) by A1,A4,Th90;
      reconsider 0p=0.K*p as Element of KK* by FINSEQ_1:def 11;
      R=Replace(M',i,0p) by A1,A5,MATRIX11:29;
      then Replace(R,i,0p)=Replace(M',i,0p) by FUNCT_7:36;
      hence thesis by A6,COMPUT_1:7;end;
end;

theorem Th92:
  j in Seg len M' & (i=j implies a <> - 1_K) implies
     the_rank_of M' = the_rank_of RLine(M',i,Line(M',i)+a*Line(M',j))
proof
  assume that A1:j in Seg len M' and A2:i=j implies a <> - 1_K;
  per cases;
    suppose not i in Seg len M';
      hence thesis by Th40;end;
    suppose A3:i in Seg len M';
      set Li=Line(M',i);set Lj=Line(M',j);set R=RLine(M',i,Li+a*Lj);
      set W=width M';set KK=the carrier of K;
      A4:len (Li+a*Lj)=W & len M'=n' & len Li=W & len Lj=W & len (a*Lj)=W
        by FINSEQ_2:109,MATRIX_1:def 3;
      then A5:Line(R,i)=Li + a*Lj & dom Li=Seg W & dom Lj=Seg W &
        dom (a*Lj)=Seg W & W=width R by A3,FINSEQ_1:def 3,MATRIX11:28,def 3;
      reconsider Li'=Li,Lj'=Lj,LL=Li+a*Lj as Element of KK* by FINSEQ_1:def 11;
      A6:RLine(R,i,Li) = Replace(R,i,Li') by A4,A5,MATRIX11:29
                      .= Replace(Replace(M',i,LL),i,Li') by A4,MATRIX11:29
                      .= Replace(M',i,Li') by FUNCT_7:36
                      .= RLine(M',i,Li) by A4,MATRIX11:29
                      .= M' by MATRIX11:30;
      A7:RLine(R,i,Lj) = Replace(R,i,Lj') by A4,A5,MATRIX11:29
                      .= Replace(Replace(M',i,LL),i,Lj') by A4,MATRIX11:29
                      .= Replace(M',i,Lj') by FUNCT_7:36
                      .= RLine(M',i,Lj) by A4,MATRIX11:29;
      consider P,Q such that
        A8:[:P,Q:] c= Indices R & card P = card Q and
        A9:card P = the_rank_of R & Det EqSegm(R,P,Q)<>0.K by Def4;
      A10:EqSegm(R,P,Q)=Segm(R,P,Q) & EqSegm(M',P,Q)=Segm(M',P,Q)
        by A8,Def3;
      A11:Indices R=Indices M' by MATRIX_1:27;
A12:   now per cases;
        suppose not i in P;
          then Segm(M',P,Q) = Segm(R,P,Q) by A8,A11,Th60;
          hence the_rank_of R<=the_rank_of M' by A8,A9,A10,A11,Def4;end;
        suppose A13:i in P;
          consider k such that A14:P c= Seg k by Th43;
          rng Sgm P=P by A14,FINSEQ_1:def 13;
          then consider m be set such that
            A15:m in dom Sgm P & Sgm P.m=i by A13,FUNCT_1:def 5;
          A16:dom Sgm P=Seg card P by A14,FINSEQ_3:45;
          then reconsider m as Element of NAT by A15;
          A17:Q c= Seg W & Q c= Seg width R by A8,A11,Th67;
          then A18:rng Sgm Q c= Seg W by FINSEQ_1:def 13;
          then A19:dom (Lj*Sgm Q)=dom Sgm Q & dom Sgm Q=Seg card Q
            by A5,A17,FINSEQ_3:45,RELAT_1:46;
          then reconsider LjQ=Lj*Sgm Q as FinSequence by FINSEQ_1:def 2;
          rng LjQ c= rng Lj & rng Lj c= KK by FINSEQ_1:def 4,RELAT_1:45;
          then rng LjQ c= the carrier of K by XBOOLE_1:1;
          then reconsider LjQ as FinSequence of K by FINSEQ_1:def 4;
          set SR=EqSegm(R,P,Q);set SM=EqSegm(M',P,Q);
          A20:len LjQ=card P & len EqSegm(R,P,Q) = card P & len LjQ=len(a*LjQ)
            by A8,A19,FINSEQ_1:def 3,MATRIXR1:16,MATRIX_1:def 3;
          A21:len Line(SM,m)=width SM & width SM=card P & card P=len SM
            by MATRIX_1:25,def 8;
          A22:Line(SR,m) = Line(R,i) * Sgm Q & Line(SM,m) = Line(M',i) * Sgm Q
            by A10,A15,A16,A17,Th47;
          A23:a *LjQ=(a*Lj)*Sgm Q by A5,A18,Th87;
          A24:RLine(SR,m,Line(SM,m))=Segm(M',P,Q) &
              RLine(SR,m,LjQ)=Segm(RLine(M',i,Lj),P,Q) &
              Segm(RLine(M',i,Lj),P,Q)=EqSegm(RLine(M',i,Lj),P,Q)
            by A4,A5,A6,A7,A8,A10,A15,A22,Def3,Th59;
          SR = RLine(SR,m,Line(SR,m)) by MATRIX11:30
            .= RLine(SR,m,Line(SM,m) + a *LjQ) by A5,A18,A22,A23,Th88;
          then A25:Det SR = Det RLine(SR,m,Line(SM,m)) + Det RLine(SR,m,a *LjQ)
                                by A15,A16,A20,A21,MATRIX11:36
                         .= Det RLine(SR,m,Line(SM,m)) + a*Det RLine(SR,m,LjQ)
                                by A15,A16,A20,MATRIX11:34
                         .= Det SM+a*Det EqSegm(RLine(M',i,Lj),P,Q)
                                by A8,A24,Def3;
          per cases;
            suppose Det EqSegm(M',P,Q) <>0.K;
              hence the_rank_of R<=the_rank_of M' by A8,A9,A11,Def4;end;
           suppose A26:Det EqSegm(M',P,Q) =0.K;
              reconsider j0=j as non zero Element of NAT by A1,PSCOMP_1:def 1;
              per cases;
                suppose i=j;
                  then Det SR = Det SM + a * Det SM by A25,MATRIX11:30
                             .= 1_K * Det SM + a * Det SM by VECTSP_1:def 13
                             .= (1_K+a)*Det SM by VECTSP_1:def 18;
                  hence the_rank_of R<=the_rank_of M' by A9,A26,VECTSP_1:44;
                  end;
                suppose A27:i<>j & j in P;
                  rng Sgm P=P by A14,FINSEQ_1:def 13;
                  then consider l be set such that
                    A28:l in dom Sgm P & Sgm P.l=j0 by A27,FUNCT_1:def 5;
                  reconsider l as Element of NAT by A16,A28;
                  Line(SM,l) = Line(M',j0) *Sgm Q by A10,A16,A17,A28,Th47;
                  then A29:RLine(SM,m,Line(SM,l)) = EqSegm(RLine(M',i,Lj),P,Q)
                    by A4,A8,A10,A11,A15,A24,Th59;
                  0.K=Det RLine(SM,m,Line(SM,l))by A15,A16,A27,A28,MATRIX11:51;
                  then 0.K = a * Det RLine(SM,m,Line(SM,l)) by VECTSP_1:44
                          .= Det SM + a* Det EqSegm(RLine(M',i,Lj),P,Q)
                                by A26,A29,RLVECT_1:def 7;
                  hence the_rank_of R <= the_rank_of M' by A9,A25;end;
                suppose A30:i<>j & not j in P;set Pij=P\{i}\/{j0};
                  a*Det EqSegm(RLine(M',i,Lj),P,Q)<>0.K
                    by A9,A25,A26,RLVECT_1:def 7;
                  then A31:Det EqSegm(RLine(M',i,Lj),P,Q)<>0.K by VECTSP_1:44;
                  Det EqSegm(RLine(M',i,Lj),P,Q)=Det EqSegm(M',Pij,Q) or
                    Det EqSegm(RLine(M',i,Lj),P,Q)=-Det EqSegm(M',Pij,Q)
                    by A1,A3,A4,A8,A11,A13,A30,Th68;
                  then Det EqSegm(M',Pij,Q)<>0.K & card P = card Pij &
                    [:Pij,Q:] c= Indices M'
                    by A1,A3,A4,A8,A11,A13,A30,A31,Th68,VECTSP_2:34;
                  hence the_rank_of R <= the_rank_of M' by A8,A9,Def4;
      end;end;end;end;
      consider P1,Q1 such that
        A32:[:P1,Q1:] c= Indices M' & card P1 = card Q1 and
        A33:card P1=the_rank_of M' &Det EqSegm(M',P1,Q1)<>0.K by Def4;
      A34:EqSegm(R,P1,Q1)=Segm(R,P1,Q1) & EqSegm(M',P1,Q1)=Segm(M',P1,Q1)
        by A32,Def3;
      now per cases;
        suppose not i in P1;
          then Segm(M',P1,Q1) = Segm(R,P1,Q1) by A32,Th60;
          hence the_rank_of R>=the_rank_of M' by A11,A32,A33,A34,Def4;end;
        suppose A35:i in P1;
          consider k such that A36:P1 c= Seg k by Th43;
          rng Sgm P1=P1 by A36,FINSEQ_1:def 13;
          then consider m be set such that
            A37:m in dom Sgm P1 & Sgm P1.m=i by A35,FUNCT_1:def 5;
          A38:dom Sgm P1=Seg card P1 by A36,FINSEQ_3:45;
          then reconsider m as Element of NAT by A37;
          A39:Q1 c= Seg W & Q1 c= Seg width R by A11,A32,Th67;
          then A40:rng Sgm Q1 c= Seg W by FINSEQ_1:def 13;
          then A41:dom (Lj*Sgm Q1)=dom Sgm Q1 & dom Sgm Q1=Seg card Q1
            by A5,A39,FINSEQ_3:45,RELAT_1:46;
          then reconsider LjQ=Lj*Sgm Q1 as FinSequence by FINSEQ_1:def 2;
          rng LjQ c= rng Lj & rng Lj c= KK by FINSEQ_1:def 4,RELAT_1:45;
          then rng LjQ c= the carrier of K by XBOOLE_1:1;
          then reconsider LjQ as FinSequence of K by FINSEQ_1:def 4;
          set SR=EqSegm(R,P1,Q1);set SM=EqSegm(M',P1,Q1);
          A42:len LjQ=card P1 & len SR = card P1 & len LjQ=len(a*LjQ)
            by A32,A41,FINSEQ_1:def 3,MATRIXR1:16,MATRIX_1:def 3;
          A43:len Line(SM,m)=width SM & width SM=card P1 & card P1=len SM
            by MATRIX_1:25,def 8;
          A44:Line(SR,m) = Line(R,i)*Sgm Q1 & Line(SM,m)=Line(M',i)*Sgm Q1
            by A34,A37,A38,A39,Th47;
          A45:a *LjQ=(a*Lj)*Sgm Q1 by A5,A40,Th87;
          A46:RLine(SR,m,Line(SM,m))=Segm(M',P1,Q1) &
              RLine(SR,m,LjQ)=Segm(RLine(M',i,Lj),P1,Q1) &
              Segm(RLine(M',i,Lj),P1,Q1)=EqSegm(RLine(M',i,Lj),P1,Q1)
            by A4,A5,A6,A7,A11,A32,A34,A37,A44,Def3,Th59;
          SR = RLine(SR,m,Line(SR,m)) by MATRIX11:30
            .= RLine(SR,m,Line(SM,m) + a *LjQ) by A5,A40,A44,A45,Th88;
          then A47:Det SR = Det RLine(SR,m,Line(SM,m)) + Det RLine(SR,m,a *LjQ)
                                by A37,A38,A42,A43,MATRIX11:36
                         .= Det RLine(SR,m,Line(SM,m)) + a*Det RLine(SR,m,LjQ)
                                by A37,A38,A42,MATRIX11:34
                         .= Det SM+a*Det EqSegm(RLine(M',i,Lj),P1,Q1)
                                by A32,A46,Def3;
          per cases;
            suppose Det SR <>0.K;
              hence the_rank_of M'<=the_rank_of R by A11,A32,A33,Def4;end;
            suppose A48:Det SR = 0.K;
              reconsider j0=j as non zero Element of NAT by A1,PSCOMP_1:def 1;
              per cases;
                suppose A49:i=j;
                  then Det SR = Det SM + a * Det SM by A47,MATRIX11:30
                             .= 1_K * Det SM + a * Det SM by VECTSP_1:def 13
                             .= (1_K+a)*Det SM by VECTSP_1:def 18;
                  then 1_K+a=0.K by A33,A48,VECTSP_1:44;
                  then a = 0.K - 1_K by VECTSP_2:22
                        .= 0.K+(-1_K) .= -1_K by RLVECT_1:def 7;
                  hence the_rank_of R>=the_rank_of M' by A2,A49;end;
                suppose A50:i<>j & j in P1;
                  rng Sgm P1=P1 by A36,FINSEQ_1:def 13;
                  then consider l be set such that
                    A51:l in dom Sgm P1 & Sgm P1.l=j0 by A50,FUNCT_1:def 5;
                  reconsider l as Element of NAT by A38,A51;
                  Line(SM,l) = Line(M',j0)*Sgm Q1 by A34,A38,A39,A51,Th47;
                  then A52:RLine(SM,m,Line(SM,l))=EqSegm(RLine(M',i,Lj),P1,Q1)
                    by A4,A32,A34,A37,A46,Th59;
                  0.K=Det RLine(SM,m,Line(SM,l))by A37,A38,A50,A51,MATRIX11:51;
                  then 0.K = a * Det RLine(SM,m,Line(SM,l)) by VECTSP_1:44;
                  hence the_rank_of R >= the_rank_of M'
                    by A33,A47,A48,A52,RLVECT_1:def 7;end;
                suppose A53:i<>j & not j in P1;set Pij=P1\{i}\/{j0};
                  a*Det EqSegm(RLine(M',i,Lj),P1,Q1)<>0.K
                    by A33,A47,A48,RLVECT_1:def 7;then
                  A54:Det EqSegm(RLine(M',i,Lj),P1,Q1)<>0.K by VECTSP_1:44;
                  Det EqSegm(RLine(M',i,Lj),P1,Q1)=Det EqSegm(M',Pij,Q1) or
                    Det EqSegm(RLine(M',i,Lj),P1,Q1)=-Det EqSegm(M',Pij,Q1)
                    by A1,A3,A4,A32,A35,A53,Th68;
                  then A55:Det EqSegm(M',Pij,Q1)<>0.K & card P1 = card Pij &
                    [:Pij,Q1:] c= Indices R
                    by A1,A3,A4,A11,A32,A35,A53,A54,Th68,VECTSP_2:34;
                  not i in P1\{i}&not i in {j} by A53,TARSKI:def 1,ZFMISC_1:64;
                  then A56:not i in Pij by XBOOLE_0:def 2;
                  EqSegm(M',Pij,Q1) = Segm(M',Pij,Q1) by A32,A55,Def3
                                   .= Segm(R,Pij,Q1) by A11,A55,A56,Th60
                                   .= EqSegm(R,Pij,Q1) by A32,A55,Def3;
                  hence the_rank_of R >= the_rank_of M' by A32,A33,A55,Def4;
      end;end;end;end;
      hence thesis by A12,XXREAL_0:1;end;
end;

theorem Th93:
  j in Seg len M' & j<>i implies
    the_rank_of DelLine(M',i) = the_rank_of RLine(M',i,a*Line(M',j))
proof
  assume A1:j in Seg len M' & i<>j;
  per cases;
    suppose A2:i in Seg len M';set W=width M';
      set Li=Line(M',i);set Lj=Line(M',j);set R=RLine(M',i,0.K*Li);
      A3:W=len Li & W=len (0.K*Li) & W=len (a*Lj) by FINSEQ_2:109;
      then A4:len R=len M' & width R=W & len M'=n'
        by MATRIX11:def 3,MATRIX_1:def 3;
      reconsider 0Li=0.K*Li,aLj=a*Lj as Element of (the carrier of K)*
        by FINSEQ_1:def 11;
      Line(R,i)=0.K*Li & Line(R,j) =Line(M',j)
        by A1,A2,A3,A4,MATRIX11:28;
      then A5:Line(R,i)+a*Line(R,j) = (W|->0.K)+a*Line(M',j)by FVSUM_1:71
                                   .= a*Line(M',j) by FVSUM_1:28;
      A6:RLine(R,i,aLj) = Replace(R,i,aLj) by A3,A4,MATRIX11:29
                       .= Replace(Replace(M',i,0Li),i,aLj) by A3,MATRIX11:29
                       .= Replace(M',i,aLj) by FUNCT_7:36
                       .= RLine(M',i,aLj) by A3,MATRIX11:29;
      thus the_rank_of DelLine(M',i) = the_rank_of R by A3,Th91
                       .= the_rank_of RLine(M',i,a*Lj)by A1,A4,A5,A6,Th92;end;
    suppose A7:not i in Seg len M';
      then not i in dom M' by FINSEQ_1:def 3;
      then DelLine(M',i)=M' & RLine(M',i,a*Line(M',j))=M'
        by A7,Th40,FINSEQ_3:113;
      hence thesis;end;
end;

theorem Th94:
  the_rank_of M > 0 iff ex i,j st [i,j] in Indices M & M*(i,j) <> 0.K
proof
  set r=the_rank_of M;
  thus r > 0 implies ex i,j st [i,j] in Indices M & M*(i,j) <> 0.K
  proof
    assume A1:r>0;
    consider P,Q such that
      A2:[:P,Q:] c= Indices M & card P = card Q and
      A3:card P = r & Det EqSegm(M,P,Q)<>0.K by Def4;
    consider x such that
      A4:x in P by A1,A3,CARD_1:78,XBOOLE_0:def 1;
    reconsider x as non zero Element of NAT by A4,PSCOMP_1:def 1;
    {x} c= P by A4,ZFMISC_1:37;
    then consider Q1 such that
      A5:Q1 c= Q & card {x} = card Q1 & Det EqSegm(M,{x},Q1) <> 0.K
      by A2,A3,Th65;
    A6:card { x } = 1 by CARD_1:79;
    then consider y such that
      A7:{y}=Q1 by A5,CARD_2:60;
    y in {y} & not 0 in Q1 by PSCOMP_1:def 1,TARSKI:def 1;
    then reconsider y as non zero Element of NAT by A7;
    take x,y;
    y in Q1 by A7,TARSKI:def 1;
    then [x,y] in [:P,Q:] by A4,A5,ZFMISC_1:106;
    hence [x,y] in Indices M by A2;
    EqSegm(M,{x},Q1) = Segm(M,{x},{y}) by A5,A7,Def3
                    .= <*<* M*(x,y) *>*> by Th44;
    hence thesis by A5,A6,MATRIX_3:36;
  end;
  given i,j such that
    A8:[i,j] in Indices M & M*(i,j) <> 0.K;
  Indices M = [:Seg len M,Seg width M:] by FINSEQ_1:def 3;
  then A9:i in Seg len M & j in Seg width M by A8,ZFMISC_1:106;
  then reconsider i,j as non zero Element of NAT by PSCOMP_1:def 1;
  A10:card { i } = 1 & card {j}=1 by CARD_1:79;
  then EqSegm(M,{i},{j}) = Segm(M,{i},{j}) by Def3
                        .= <*<* M*(i,j) *>*> by Th44;
  then A11:Det EqSegm(M,{i},{j})<>0.K by A8,A10,MATRIX_3:36;
  {i} c= Seg len M & {j} c= Seg width M by A9,ZFMISC_1:37;
  then [:{i},{j}:] c= Indices M by A10,Th67;
  hence thesis by A10,A11,Def4;
end;

theorem
  the_rank_of M = 0 iff M = 0.(K,len M,width M)
proof
  set NULL=0.(K,len M,width M);
  reconsider M'=M as Matrix of len M,width M,K by MATRIX_2:7;
  thus the_rank_of M = 0 implies M = 0.(K,len M,width M)
  proof
    assume A1:the_rank_of M = 0;
    now let i,j be Nat such that A2:[i,j] in Indices M';
      reconsider i'=i,j'=j as Element of NAT by ORDINAL1:def 13;
      Indices M'=Indices NULL by MATRIX_1:27;
      then M*(i',j')=0.K & NULL*(i,j)=0.K by A1,A2,Th94,MATRIX_3:3;
      hence M'*(i,j)=NULL*(i,j);
    end;
    hence thesis by MATRIX_1:28;
  end;
  assume A3:M = NULL;
  assume the_rank_of M<>0;
  then the_rank_of M>0;
  then ex i,j st [i,j] in Indices M & M*(i,j) <> 0.K by Th94;
  hence thesis by A3,MATRIX_3:3;
end;

theorem Th96:
  the_rank_of M = 1 iff (ex i,j st [i,j] in Indices M & M*(i,j) <> 0.K) &
                         for i0,j0,n0,m0 st i0<>j0 & n0<>m0 &
                                            [:{i0,j0},{n0,m0}:] c= Indices M
                           holds Det EqSegm(M,{i0,j0},{n0,m0}) = 0.K
proof
  thus the_rank_of M = 1 implies (ex i,j st [i,j] in Indices M & M*(i,j)<>0.K)&
    for i0,j0,n0,m0 st i0<>j0 & n0<>m0 & [:{i0,j0},{n0,m0}:] c= Indices M
       holds Det EqSegm(M,{i0,j0},{n0,m0}) = 0.K
  proof
    assume A1:the_rank_of M = 1;
    hence ex i,j st [i,j] in Indices M & M*(i,j) <> 0.K by Th94;
    let i0,j0,n0,m0 such that
      A2:i0<>j0 & n0<>m0 and A3:[:{i0,j0},{n0,m0}:] c= Indices M;
    assume A4:Det EqSegm(M,{i0,j0},{n0,m0}) <> 0.K;
    card {i0,j0} =2 & card {n0,m0}=2 by A2,CARD_2:76;
    hence thesis by A1,A3,A4,Def4;
  end;
  assume ex i,j st [i,j] in Indices M & M*(i,j) <> 0.K;
  then A5:the_rank_of M>0 by Th94;
  assume A6:for i0,j0,n0,m0 st i0<>j0 & n0<>m0 & [:{i0,j0},{n0,m0}:]c=Indices M
           holds Det EqSegm(M,{i0,j0},{n0,m0}) = 0.K;
  assume A7:the_rank_of M <> 1;
  consider P,Q such that
    A8:[:P,Q:] c= Indices M & card P = card Q and
    A9:card P = the_rank_of M & Det EqSegm(M,P,Q)<>0.K by Def4;
  card P>1 by A5,A7,A9,NAT_1:26;
  then card P>=1+1 by NAT_1:13;
  then consider P1 be Subset of P such that
    A10:card P1=2 by PENCIL_4:2;
  not 0 in P1 by PSCOMP_1:def 1;
  then reconsider P1 as without_zero finite Subset of NAT
    by PSCOMP_1:def 1,XBOOLE_1:1;
  consider i,j be set such that
    A11:i <> j & P1 = {i,j} by A10,CARD_2:79;
  i in P1 & j in P1 & not 0 in P1 by A11,PSCOMP_1:def 1,TARSKI:def 2;
  then reconsider i,j as non zero Element of NAT;
  consider Q1 such that
    A12:Q1 c=Q & card P1=card Q1 & Det EqSegm(M,P1,Q1)<>0.K by A8,A9,Th65;
  consider n,m be set such that
    A13:n <> m & Q1 = {n,m} by A10,A12,CARD_2:79;
  n in Q1 & m in Q1 & not 0 in Q1 by A13,PSCOMP_1:def 1,TARSKI:def 2;
  then reconsider n,m as non zero Element of NAT;
  [:P1,Q1:] c= [:P,Q:] by A12,ZFMISC_1:119;
  then [:{i,j},{n,m}:] c= Indices M by A8,A11,A13,XBOOLE_1:1;
  hence thesis by A6,A11,A12,A13;
end;

theorem Th97:
  the_rank_of M = 1 iff (ex i,j st [i,j] in Indices M & M*(i,j) <> 0.K) &
                         for i,j,n,m st [:{i,j},{n,m}:] c= Indices M holds
                           M*(i,n) * (M*(j,m)) = M*(i,m) * (M*(j,n))
proof
  thus the_rank_of M=1 implies (ex i,j st [i,j] in Indices M & M*(i,j)<>0.K)&
    for i,j,n,m st [:{i,j},{n,m}:]c=Indices M holds
      M*(i,n)*(M*(j,m))=M*(i,m)*(M*(j,n))
  proof
    assume A1:the_rank_of M=1;
    hence ex i,j st [i,j] in Indices M & M*(i,j)<>0.K by Th96;
    let i,j,n,p be Element of NAT such that A2:[:{i,j},{n,p}:] c= Indices M;
       per cases;
         suppose i=j or n=p;
           hence thesis;end;
         suppose A3:i<>j & n<>p;
           Indices M=[:Seg len M,Seg width M:] by FINSEQ_1:def 3;
           then {i,j}c=Seg len M & {n,p}c=Seg width M & i in {i,j} &j in {i,j}&
             p in {n,p} & n in {n,p} & not 0 in Seg len M &not 0 in Seg width M
             by A2,PSCOMP_1:def 1,TARSKI:def 2,ZFMISC_1:138;
           then reconsider I=i,J=j,P=p,N=n as non zero Element of NAT;
           A4:card {I,J}=2 & card {N,P}=2 by A3,CARD_2:76;
           then A5:EqSegm(M,{I,J},{N,P})=Segm(M,{I,J},{N,P}) by Def3;
           A6:Det EqSegm(M,{I,J},{N,P}) = 0.K by A1,A2,A3,Th96;
           set IN=M*(I,N);set IP=M*(I,P);set JN=M*(J,N);set JP=M*(J,P);
           per cases by A3,REAL_1:def 5;
             suppose I<J & N<P;
               then 0.K = Det(IN,IP)][(JN,JP) by A4,A5,A6,Th45
                       .= IN*JP-IP*JN by MATRIX_9:13;
               hence thesis by VECTSP_1:66;end;
             suppose I<J & N>P;
               then 0.K = Det(IP,IN)][(JP,JN) by A4,A5,A6,Th45
                       .= IP*JN-IN*JP by MATRIX_9:13;
               hence thesis by VECTSP_1:66;end;
             suppose I>J & N<P;
               then 0.K = Det(JN,JP)][(IN,IP) by A4,A5,A6,Th45
                       .= JN*IP-JP*IN by MATRIX_9:13;
               hence thesis by VECTSP_1:66;end;
             suppose I>J & N>P;
               then 0.K = Det(JP,JN)][(IP,IN) by A4,A5,A6,Th45
                       .= JP*IN-JN*IP by MATRIX_9:13;
               hence thesis by VECTSP_1:66;end;end;
  end;
  assume that A7:ex i,j st [i,j] in Indices M & M*(i,j) <> 0.K and
    A8:for i,j,n,m st [:{i,j},{n,m}:] c= Indices M holds
      M*(i,n)*(M*(j,m))=M*(i,m)*(M*(j,n));
  now let i0,j0,n0,m0 such that A9:i0<>j0 & n0<>m0 and
                                A10:[:{i0,j0},{n0,m0}:] c= Indices M;
    A11:card {i0,j0}=2 & card {n0,m0}=2 by A9,CARD_2:76;
    then A12:EqSegm(M,{i0,j0},{n0,m0})=Segm(M,{i0,j0},{n0,m0}) by Def3;
    set IN=M*(i0,n0);set IM=M*(i0,m0);set JN=M*(j0,n0);set JM=M*(j0,m0);
    A13:IN*JM=IM*JN by A8,A10;
    per cases by A9,REAL_1:def 5;
      suppose i0<j0 & n0<m0;
        then EqSegm(M,{i0,j0},{n0,m0}) = (IN,IM)][(JN,JM) by A12,Th45;
        hence Det EqSegm(M,{i0,j0},{n0,m0}) = IN*JM-IM*JN by A11,MATRIX_9:13
                                           .= 0.K by A13,VECTSP_1:66;end;
      suppose i0<j0 & n0>m0;
        then EqSegm(M,{i0,j0},{n0,m0} )= (IM,IN)][(JM,JN) by A12,Th45;
        hence Det EqSegm(M,{i0,j0},{n0,m0}) = IM*JN-IN*JM by A11,MATRIX_9:13
                                           .= 0.K by A13,VECTSP_1:66;end;
      suppose i0>j0 & n0<m0;
        then EqSegm(M,{i0,j0},{n0,m0}) = (JN,JM)][(IN,IM) by A12,Th45;
        hence Det EqSegm(M,{i0,j0},{n0,m0}) = JN*IM-JM*IN by A11,MATRIX_9:13
                                           .= 0.K by A13,VECTSP_1:66;end;
      suppose i0>j0 & n0>m0;
        then EqSegm(M,{i0,j0},{n0,m0})=(JM,JN)][(IM,IN) by A12,Th45;
        hence Det EqSegm(M,{i0,j0},{n0,m0}) = JM*IN-JN*IM by A11,MATRIX_9:13
                                           .= 0.K by A13,VECTSP_1:66;end;
  end;
  hence thesis by A7,Th96;
end;

theorem
  the_rank_of M = 1 iff ex i st i in Seg len M &
                        (ex j st j in Seg width M & M*(i,j) <> 0.K) &
                        for k st k in Seg len M ex a st Line(M,k) =a*Line(M,i)
proof
  A1:Indices M=[:Seg len M,Seg width M:] by FINSEQ_1:def 3;
  thus the_rank_of M = 1 implies ex i st i in Seg len M &
      (ex j st j in Seg width M & M*(i,j) <> 0.K) &
      for k st k in Seg len M ex a st Line(M,k) = a*Line(M,i)
  proof
    assume A2:the_rank_of M = 1;
    then consider i,j such that
      A3:[i,j] in Indices M & M*(i,j) <> 0.K by Th97;
    take i;
    A4:i in Seg len M & j in Seg width M by A1,A3,ZFMISC_1:106;
    hence i in Seg len M & ex j st j in Seg width M & M*(i,j) <> 0.K by A3;
    let k such that A5:k in Seg len M;
    set ij=M*(i,j); set kj=M*(k,j);set Lk=Line(M,k);set Li=Line(M,i);
    take a=kj * ij";
    A6:len Lk=width M & len (a*Li)=width M by FINSEQ_2:109;
    now let n be Nat such that A7:1<=n & n<=width M;
      A8:kj = kj * 1_K by VECTSP_1:def 13
           .= kj * (ij * ij") by A3,VECTSP_1:def 22
           .= a * ij by GROUP_1:def 4;
A9:        n in NAT by ORDINAL1:def 13;
           then
      A10:n in Seg width M by A7;
      then {i,k} c= Seg len M & {j,n} c= Seg width M by A4,A5,ZFMISC_1:38;
      then [:{i,k},{j,n}:] c= Indices M by A1,ZFMISC_1:119;
      then A11:ij*(M*(k,n)) = a*ij *(M*(i,n)) by A2,A8,A9,Th97
                          .= ij * (a * (M*(i,n))) by GROUP_1:def 4;
      Li.n = M*(i,n) by A10,MATRIX_1:def 8;
      then (a*Li).n=a*(M*(i,n)) & Lk.n=M*(k,n) by A10,FVSUM_1:63,MATRIX_1:def 8
;
      hence Lk.n=(a*Li).n by A3,A11,VECTSP_2:42;
    end;
    hence thesis by A6,FINSEQ_1:18;
  end;
  given i such that
    A12:i in Seg len M and
    A13:ex j st j in Seg width M & M*(i,j) <> 0.K and
    A14:for k st k in Seg len M ex a st Line(M,k) = a*Line(M,i);
  consider j such that
    A15:j in Seg width M & M*(i,j) <> 0.K by A13;
  A16:[i,j] in Indices M by A1,A12,A15,ZFMISC_1:106;
  now let I,J,n,m be Element of NAT such that A17:[:{I,J},{n,m}:]c=Indices M;
  set Li=Line(M,i);set LI=Line(M,I);set LJ=Line(M,J);
    A18:{I,J} c= Seg len M & {n,m} c= Seg width M by A1,A17,ZFMISC_1:138;
    then I in Seg len M by ZFMISC_1:38;
    then consider a such that
    A19:LI = a*Li by A14;
    J in Seg len M by A18,ZFMISC_1:38;
    then consider b such that
    A20:LJ = b*Li by A14;
    A21:n in Seg width M & m in Seg width M by A18,ZFMISC_1:38;
    then LI.n=M*(I,n) & LI.m=M*(I,m) & LJ.n=M*(J,n) & LJ.m=M*(J,m) &
      Li.n=M*(i,n) & Li.m=M*(i,m) by MATRIX_1:def 8;
    then A22:M*(I,n)= a * (M*(i,n)) & M*(J,n)= b * (M*(i,n)) &
      M*(I,m)= a * (M*(i,m)) & M*(J,m)= b * (M*(i,m)) by A19,A20,A21,FVSUM_1:63
;
    hence M*(I,n) * (M*(J,m)) = (a * (M*(i,n)) * b) * (M*(i,m))
    by GROUP_1:def 4
             .= (b *(M*(i,n)) * a) * (M*(i,m)) by GROUP_1:def 4
             .= M*(I,m) * (M*(J,n)) by A22,GROUP_1:def 4;
  end;
  hence thesis by A15,A16,Th97;
end;

registration let K;
  cluster diagonal Matrix of K;
  existence
  proof
    consider E being diagonal Matrix of 1,K;
    take E;
    thus thesis;
  end;
end;

theorem Th99:
  for M be diagonal Matrix of K
    for NonZero be set st NonZero ={i:[i,i] in Indices M & M*(i,i)<>0.K}
      for P,Q st [:P,Q:] c= Indices M & card P = card Q &
                 Det EqSegm(M,P,Q)<>0.K
       holds P c= NonZero & Q c= NonZero
proof
  let M be diagonal Matrix of K;
  let NonZero be set such that
     A1:NonZero={i:[i,i] in Indices M & M*(i,i)<>0.K};
  let P,Q such that A2:[:P,Q:] c= Indices M & card P = card Q and
                    A3:Det EqSegm(M,P,Q)<>0.K;
  set ES=EqSegm(M,P,Q);set S=Segm(M,P,Q);set SP=Sgm P;set SQ=Sgm Q;
  A4:ES=S by A2,Def3;
  A5:Indices S=[:Seg len S,Seg width S:] & len S=card P & dom S=Seg len S
    & width S=card P by A2,FINSEQ_1:def 3,MATRIX_1:25;
  thus P c= NonZero
  proof
    assume not P c= NonZero;
    then consider x such that
      A6:x in P & not x in NonZero by TARSKI:def 3;
    P c= Seg len M & Q c= Seg width  M  by A2,Th67;
    then A7:rng SP=P & dom SP=Seg card P & rng SQ=Q
      by FINSEQ_1:def 13,FINSEQ_3:45;
    then consider y such that
      A8:y in dom SP & SP.y=x by A6,FUNCT_1:def 5;
    reconsider x,y as Element of NAT by A6,A7,A8;set L=Line(S,y);
    A9:len L=len (0.K* L) & len L=width S by MATRIXR1:16,MATRIX_1:def 8;
    now let i be Nat such that A10:1<=i & i<=width S;
      i in NAT by ORDINAL1:def 13;
      then
      A11:i in Seg width S & y in Seg len S
      by A2,A7,A8,A10,MATRIX_1:25;
      then [y,i] in Indices S by A5,ZFMISC_1:106;
      then A12:S*(y,i)=M*(x,SQ.i) & [x,SQ.i] in Indices M & L.i=S*(y,i)
        by A2,A7,A8,A11,Def1,Th17,MATRIX_1:def 8;
      SQ.i<>x or SQ.i=x;
      then L.i=0.K & 0.K*0.K=0.K by A1,A6,A12,MATRIX_1:def 15,VECTSP_1:39;
      hence L.i=(0.K*L).i by A11,FVSUM_1:63;
    end;
    then Line(S,y) = 0.K*Line(S,y) by A9,FINSEQ_1:18;
    then Det(RLine(ES,y,Line(ES,y))) = 0.K*Det(ES) & RLine(ES,y,Line(ES,y))=ES
      by A4,A7,A8,MATRIX11:30,35;
    hence thesis by A3,VECTSP_1:39;
  end;
  thus Q c= NonZero
  proof
    assume not Q c= NonZero;
    then consider x such that
      A13:x in Q & not x in NonZero by TARSKI:def 3;
    P c= Seg len M & Q c= Seg width  M by A2,Th67;
    then A14:rng SP=P & dom SQ=Seg card Q & rng SQ=Q
      by FINSEQ_1:def 13,FINSEQ_3:45;
    then consider y such that
      A15:y in dom SQ & SQ.y=x by A13,FUNCT_1:def 5;
    reconsider x,y as Element of NAT by A13,A14,A15;set C=Col(ES,y);
    now let k such that A16:k in Seg card P;
      [k,y] in Indices S by A2,A5,A14,A15,A16,ZFMISC_1:106;
      then A17:S*(k,y)=M*(SP.k,x) & [SP.k,x] in Indices M & S*(k,y)=C.k
        by A2,A4,A5,A14,A15,A16,Def1,Th17,MATRIX_1:def 9;
      SP.k<>x or SP.k=x;
      hence C.k=0.K by A1,A13,A17,MATRIX_1:def 15;
    end;
    hence thesis by A2,A3,A14,A15,MATRIX_9:53;
  end;
end;

theorem Th100:
  for M be diagonal Matrix of K
    for P st [:P,P:] c= Indices M holds Segm(M,P,P) is diagonal
proof
  let M be diagonal Matrix of K;let P such that A1:[:P,P:] c= Indices M;
  set S=Segm(M,P,P);set SP=Sgm P;
  let i,j such that A2:i in Seg card P & j in Seg card P & i <> j;
  [i,j] in [:Seg card P,Seg card P:] & P c= Seg len M
    by A1,A2,Th67,ZFMISC_1:106;
  then [i,j] in Indices S & rng SP=P & dom SP=Seg card P & SP is one-to-one
    by FINSEQ_1:def 13,FINSEQ_3:45,99,MATRIX_1:25;
  then S*(i,j)=M*(SP.i,SP.j) & [SP.i,SP.j] in Indices M & SP.i<>SP.j
    by A1,A2,Def1,Th17,FUNCT_1:def 8;
  hence thesis by MATRIX_1:def 15;
end;

theorem
  for M be diagonal Matrix of K
    for NonZero be set st NonZero={i:[i,i] in Indices M & M*(i,i)<>0.K}
      holds the_rank_of M = Card NonZero
proof
  let M be diagonal Matrix of K;
  let NZ be set such that A1:NZ={i:[i,i] in Indices M & M*(i,i)<>0.K};
  A2:NZ c= Seg width M
  proof
    let x;assume x in NZ;
    then ex i st x=i & [i,i] in Indices M & M*(i,i)<>0.K by A1;
    hence thesis by ZFMISC_1:106;
  end;
  A3:NZ c= dom M
  proof
    let x;assume x in NZ;
    then ex i st x=i & [i,i] in Indices M & M*(i,i)<>0.K by A1;
    hence thesis by ZFMISC_1:106;
  end;
  not 0 in NZ & NZ is finite by A2,FINSET_1:13,PSCOMP_1:def 1;
  then reconsider nz=NZ as without_zero finite Subset of NAT
    by A2,PSCOMP_1:def 1,XBOOLE_1:1;
  set S=Segm(M,nz,nz);
  A4:[:nz,nz:] c= Indices M by A2,A3,ZFMISC_1:119;
  then reconsider S as diagonal Matrix of card nz,K by Th100;
  set d=diagonal_of_Matrix S;
  now per cases by NAT_1:14;
    suppose card nz=0;
      then Det S=1_K by MATRIXR2:41;
      hence Det S<>0.K by VECTSP_1:def 21;
      end;
     suppose card nz>=1;
       then A5:Det S = Product d by MATRIX_7:17;set Sn=Sgm nz;
       now let k such that A6:k in dom d;
         dom d=Seg len d & len d=card nz&Indices S=[:Seg card nz,Seg card nz:]
           by FINSEQ_1:def 3,MATRIX_1:25,MATRIX_3:def 10;
         then A7:d.k=S*(k,k) & [k,k] in Indices S & rng Sn=nz & dom Sn=dom d
           by A2,A6,FINSEQ_1:def 13,FINSEQ_3:45,MATRIX_3:def 10,ZFMISC_1:106;
         then Sn.k in nz by A6,FUNCT_1:def 5;
         then ex i st i=Sn.k & [i,i] in Indices M & M*(i,i)<>0.K by A1;
         hence d.k <> 0.K by A7,Def1;
       end;
       hence Det S<>0.K by A5,FVSUM_1:107;
     end;
  end;
  then Det EqSegm(M,nz,nz)<>0.K by Def3;
  then A8:the_rank_of M >= card nz by A4,Def4;
  consider P,Q such that
    A9:[:P,Q:] c= Indices M & card P = card Q and
    A10:card P = the_rank_of M & Det EqSegm(M,P,Q)<>0.K by Def4;
  P c= nz by A1,A9,A10,Th99;
  then card P <= card nz by CARD_1:80;
  hence thesis by A8,A10,XXREAL_0:1;
end;

 reserve v,v1,v2,u,w for Vector of n-VectSp_over K,
        t,t1,t2 for Element of n-tuples_on the carrier of K,
        F for FinSequence of n-VectSp_over K,
        f for Function of the carrier of n-VectSp_over K, the carrier of K,
        L for Linear_Combination of n-VectSp_over K,
        M,M1 for Matrix of m,n,K;

theorem Th102:
  the carrier of n -VectSp_over K = n-tuples_on the carrier of K &
           0.(n -VectSp_over K) = n |-> 0.K &
        ( t1 = v1 & t2 = v2 implies t1 + t2 = v1 + v2 ) &
                ( t = v implies a * t = a * v )
proof
  A1:n-Group_over K=LoopStr(# n-tuples_on the carrier of K,
        product(the add of K,n),
        (n .--> 0.K) qua Element of n-tuples_on the carrier of K#)
        by PRVECT_1:def 3;
  A2:the LoopStr of n -VectSp_over K=n-Group_over K by PRVECT_1:def 5;
  thus the carrier of n -VectSp_over K = n-tuples_on the carrier of K &
           0.(n -VectSp_over K) =(n |-> 0.K) by A1,A2;
  thus t1 = v1 & t2 = v2 implies t1 + t2 = v1 + v2 by A1,A2,PRVECT_1:def 1;
  assume A3:t=v;
  rng t c= the carrier of K by RELSET_1:12;
  then A4:(id (the carrier of K))*t=t by RELAT_1:79;
  thus a*v = (n-Mult_over K).(a,v) by PRVECT_1:def 5
          .= (the mult of K)[;](a,t) by A3,PRVECT_1:def 4
          .= a*t by A4,FUNCOP_1:44;
end;

definition let K,n;
  redefine func n -VectSp_over K -> strict VectSp of K;
  coherence
  proof
    set nV = n -VectSp_over K;
A1: now let u,v,w;
      reconsider V=v,U=u,W=w,UV=u+v,VW=v+w as
                            Element of n-tuples_on the carrier of K by Th102;
      thus (u + v) + w = UV + W by Th102
                      .= (U + V) + W by Th102
                      .= U + (V + W) by FINSEQOP:29
                      .= U + VW by Th102
                      .= u + (v + w) by Th102;end;
A2: now let v;
     reconsider V=v,n0=0.nV as
                            Element of n-tuples_on the carrier of K by Th102;
     thus v + 0.nV = V + n0 by Th102
                  .= V + (n|->0.K) by Th102
                  .= v by FVSUM_1:28;end;
A3: now let v;
      reconsider V=v as Element of n-tuples_on the carrier of K by Th102;
      reconsider u=-V as Element of nV by Th102;
      v + u = V+ -V by Th102
           .= n|->0.K by FVSUM_1:35
           .= 0.nV by Th102;
      hence ex u st v+u=0.nV;end;
A4: now let v,u;
      reconsider V=v,U=u as Element of n-tuples_on the carrier of K by Th102;
      thus v + u = V + U by Th102
                .= U + V by FINSEQOP:34
                .= u + v by Th102;end;
    thus thesis by A1,A2,A3,A4,RLVECT_1:def 5,def 6,def 7,def 8;
  end;
end;

registration let K,n;
  cluster -> Function-like Relation-like Vector of n-VectSp_over K;
  correctness
  proof
    let v be Element of n-VectSp_over K;
    v is Element of n-tuples_on the carrier of K by Th102;
    hence thesis;
  end;
end;

Lm7:rng M is Subset of n-VectSp_over K
proof
  rng M c= the carrier of n-VectSp_over K
  proof
    let x such that A1:x in rng M;
    consider m be Nat such that
      A2:for x st x in rng M ex p st x = p & len p = m by MATRIX_1:9;
    consider p such that
      A3:x=p & len p =m by A1,A2;
    len p=n by A1,A3,MATRIX_1:def 3;
    then p is Element of n-tuples_on the carrier of K
       & n-tuples_on the carrier of K is non empty by FINSEQ_2:110;
    then p in n-tuples_on the carrier of K;
    hence thesis by A3,Th102;
  end;
  hence thesis;
end;

notation let K,m,n;let M be Matrix of m,n,K;
  synonym lines M for rng M;
  synonym M is without_repeated_line for M is one-to-one;
end;

definition
  let K be Field,m,n;
  let M be Matrix of m,n,K;
  redefine func lines M -> Subset of n-VectSp_over K;
  coherence by Lm7;
end;

theorem Th103:
  x in lines M iff ex i st i in Seg m & x = Line(M,i)
proof
  thus x in lines M implies ex i st i in Seg m & x = Line(M,i)
  proof
    assume x in lines M;
    then consider i be set such that
      A1:i in dom M & M.i=x by FUNCT_1:def 5;
    A2:dom M=Seg len M & len M=m by FINSEQ_1:def 3,MATRIX_1:def 3;
    reconsider i as Element of NAT by A1,A2;
    M.i=Line(M,i) by A1,A2,MATRIX_2:10;
    hence thesis by A1,A2;
  end;
  given i such that
    A3:i in Seg m & x = Line(M,i);
  dom M=Seg len M & len M=m by FINSEQ_1:def 3,MATRIX_1:def 3;
  then M.i in rng M & M.i=Line(M,i) by A3,FUNCT_1:def 5,MATRIX_2:10;
  hence thesis by A3;
end;

theorem
  for V be finite Subset of n-VectSp_over K
    ex M be Matrix of card V,n,K st M is without_repeated_line &
                                    lines M = V
proof
  let V be finite Subset of n-VectSp_over K;set cV=card V;
  card Seg cV=cV & card card cV =card V by FINSEQ_1:78;
  then Seg cV,V are_equipotent by CARD_1:21;
  then consider m be Function such that
    A1:m is one-to-one & dom m = Seg cV & rng m=V by WELLORD2:def 4;
  reconsider M=m as FinSequence by A1,FINSEQ_1:def 2;
  A2:the carrier of n -VectSp_over K = n-tuples_on the carrier of K by Th102;
  now let x such that A3:x in rng M;
    reconsider p=x as Element of n-tuples_on the carrier of K by A1,A3,Th102;
    len p=n by FINSEQ_2:109;
    hence ex p st x = p & len p = n;
  end;
  then reconsider M as Matrix of K by MATRIX_1:9;
  A4:len M=cV by A1,FINSEQ_1:def 3;
  for p st p in rng M holds len p=n by A1,A2,FINSEQ_2:109;
  then M is Matrix of cV,n,K by A4,MATRIX_1:def 3;
  hence thesis by A1;
end;

definition let K,n;
  let F be FinSequence of n-VectSp_over K;
  func FinS2MX F -> Matrix of len F,n,K equals
     F;
  coherence
  proof
    A1:F is FinSequence of n-tuples_on the carrier of K by Th102;
    now let x such that A2:x in rng F;
     rng F c= n-tuples_on the carrier of K by A1,FINSEQ_1:def 4;
     then reconsider p=x as Element of n-tuples_on the carrier of K by A2;
     len p=n by FINSEQ_2:109;
     hence ex p st x = p & len p = n;
    end;
    then reconsider F'=F as Matrix of K by MATRIX_1:9;
    now let p such that A3:p in rng F';
      rng F' c= n-tuples_on the carrier of K by A1,FINSEQ_1:def 4;
      hence len p=n by A3,FINSEQ_2:109;
    end;
    hence thesis by MATRIX_1:def 3;
  end;
end;

definition let K,m,n;
  let M be Matrix of m,n,K;
  func MX2FinS M -> FinSequence of n-VectSp_over K equals
    M;
  coherence
  proof
    lines M is Subset of n-VectSp_over K;
    hence thesis by FINSEQ_1:def 4;
  end;
end;

theorem Th105:
  the_rank_of M = m implies M is without_repeated_line
proof
  assume A1:the_rank_of M = m;
  assume not M is without_repeated_line;
  then consider x1,x2 be set such that
    A2:x1 in dom M & x2 in dom M & M.x1=M.x2 & x1<>x2 by FUNCT_1:def 8;
  A3:dom M=Seg len M & len M = m by FINSEQ_1:def 3,MATRIX_1:def 3;
  then reconsider x1,x2 as Element of NAT by A2;
  consider k be Nat such that
    A4:len M = k + 1 & len Del(M,x1) = k by A2,FINSEQ_3:113;
  M.x1=Line(M,x1) & M.x2=Line(M,x2) by A2,A3,MATRIX_2:10;
  then M = RLine(M,x1,Line(M,x2)) by A2,MATRIX11:30
        .= RLine(M,x1,1_K*Line(M,x2)) by FVSUM_1:70;
  then m = the_rank_of DelLine(M,x1) by A1,A2,A3,Th93;
  then m <= k by A4,Th74;
  hence thesis by A3,A4,NAT_1:13;
end;

theorem Th106:
  i in Seg len M & a = L.(M.i) implies
    Line(FinS2MX(L (#) MX2FinS M),i) = a * Line(M,i)
proof
  assume that A1:i in Seg len M and A2:a = L.(M.i);
  set MX=MX2FinS M;set LM=L (#) MX;set FLM = FinS2MX LM;
  len LM=len M & i in dom M & len M=m
    by A1,FINSEQ_1:def 3,MATRIX_1:def 3,VECTSP_6:def 8;
  then A3:Line(FLM,i)=FLM.i & M.i=MX/.i & i in dom FLM & Line(M,i)=M.i
    & Line(M,i) in lines M
  by A1,Th103,FINSEQ_1:def 3,MATRIX_2:10,PARTFUN1:def 8;
  then reconsider L=Line(M,i) as Element of n-tuples_on the carrier of K
    by Th102;
  thus Line(FLM,i) = a *MX/.i by A2,A3,VECTSP_6:def 8
                  .= a*L by A3,Th102
                  .= a*Line(M,i);
end;

theorem Th107:
  M is without_repeated_line & Carrier L c= lines M & i in Seg n implies
    (Sum L).i = Sum Col(FinS2MX(L (#) MX2FinS M),i)
proof
  assume that A1:M is without_repeated_line & Carrier L c= lines M and
              A2:i in Seg n;
  set V=n-VectSp_over K;set MX=MX2FinS M;set LM=L (#) MX;
  set FLM = FinS2MX LM;set C=Col(FLM,i);
  A3:Sum L = Sum LM & len LM=len M
    by A1,VECTSP_6:def 8,VECTSP_9:7;
  then consider f be Function of NAT, the carrier of V such that
    A4:Sum L = f.(len M) and
    A5:f.0 = 0.V and
    A6:for j,v st j < len M & v = LM.(j+1) holds f.(j+1) = f.j+v
      by RLVECT_1:def 12;
  A7:len LM=len C & len M=m by MATRIX_1:def 3,def 9;
  then consider g be Function of NAT, the carrier of K such that
    A8:Sum C = g.(len M) and
    A9:g.0 = 0.K and
    A10:for j,a st j < len M & a = C.(j+1) holds g.(j+1) = g.j+a
      by A3,RLVECT_1:def 12;
  defpred P[Nat] means $1 <= len M implies for v be Element of V st
     v=f.$1 holds v.i=g.$1;
  A11:P[0]
  proof assume 0 <= len M;
    let v such that A12:v=f.0;
    0.V=n|->0.K by Th102;
    hence v.i=g.0 by A2,A5,A9,A12,FINSEQ_1:4,FINSEQ_2:71;
  end;
  A13:for k st P[k] holds P[k+1]
  proof
    let k such that A14:P[k];set k1=k+1;
    reconsider K1=k1 as Element of NAT;
    assume A15:k1 <= len M;
    let v such that A16:v=f.k1;
    1<=k1 by NAT_1:14;
    then A17:k1 in Seg len M by A15;
    then A18:k1 in dom MX & k1 in dom FLM by A3,FINSEQ_1:def 3;
    A19:MX.K1=Line(M,K1) & width FLM=n
      by A3,A7,A17,Th1,FINSEQ_1:4,MATRIX_2:10;
    then MX.K1 in lines M by A7,A17,Th103;
    then reconsider MXK1=MX.K1 as Element of V;
    MX/.K1=MX.K1 by A18,PARTFUN1:def 8;
    then A20:LM.K1 = L.MXK1 * MXK1 by A18,VECTSP_6:def 8;
    then reconsider LMK1=LM.K1 as Element of V;
    reconsider lmk1=LMK1,mxk1=MXK1,fk=f.k
      as Element of n-tuples_on the carrier of K by Th102;
    dom lmk1=Seg n by FINSEQ_2:144;
    then lmk1.i in rng lmk1 & rng lmk1 c= the carrier of K
      by A2,FINSEQ_1:def 4,FUNCT_1:def 5;
    then reconsider lmk1i=lmk1.i as Element of K;
    LMK1 = L.MXK1 * mxk1 by A20,Th102
              .= Line(FLM,K1) by A17,A19,Th106;
    then LMK1.i = FLM*(K1,i) & C.K1 = FLM*(K1,i) & k<len M
      by A2,A15,A18,A19,MATRIX_1:def 8,def 9,NAT_1:13;
    then g.K1=lmk1i + g.k & v=LMK1 +f.k & fk.i = g.k &
      LMK1 +f.k=lmk1 +fk by A6,A10,A14,A16,Th102;
    hence v.i=g.k1 by A2,FVSUM_1:22;
  end;
  for k holds P[k] from NAT_1:sch 1(A11,A13);
  hence thesis by A4,A8;
end;

theorem Th108:
  for M,M1 st M is without_repeated_line &
              for i st i in Seg m holds ex a st Line(M1,i) = a * Line(M,i)
    holds
      ex L be Linear_Combination of lines M st L (#) MX2FinS M = M1
proof
  let M,M1 such that A1:M is without_repeated_line and
                     A2:for i st i in Seg m ex a st Line(M1,i) = a * Line(M,i);
  set V=n-VectSp_over K;
  defpred P[set,set] means
    for i st $1=i ex a st a=$2 & Line(M1,i) = a * Line(M,i);
A3: for k be Nat st k in Seg len M ex x be Element of K st P[k,x]
  proof
    let k be Nat such that A4:k in Seg len M;
    len M=m by MATRIX_1:def 3;
    then consider a such that
      A5:Line(M1,k) = a * Line(M,k) by A2,A4;
    take a;
    thus thesis by A5;
  end;
  consider p such that
    A6:dom p = Seg len M and
    A7:for k be Nat st k in Seg len M holds P[k,p.k] from FINSEQ_1:sch 5(A3);
  defpred Q[set,set] means
    for v st $1=v holds
      (not v in lines M implies $2=0.K) &
      (v in lines M implies for k st k in Seg m & v=Line(M,k) holds $2=p.k);
  A8:for x st x in the carrier of V ex y st y in the carrier of K & Q[x,y]
  proof
    let x such that A9:x in the carrier of V;
    reconsider v=x as Element of V by A9;
    per cases;
      suppose A10:v in lines M;
        then consider j such that
          A11:j in Seg m & v=Line(M,j) by Th103;
        len M=m by MATRIX_1:def 3;
        then p.j in rng p & rng p c= the carrier of K
          by A6,A11,FINSEQ_1:def 4,FUNCT_1:def 5;
        then reconsider pj=p.j as Element of the carrier of K;
        take pj;thus pj in the carrier of K;
        let w such that A12:w=x;
        thus not w in lines M implies pj=0.K by A10,A12;
        thus w in lines M implies
          for k st k in Seg m & w=Line(M,k) holds pj = p.k
        proof
          assume w in lines M;
          let k such that A13:k in Seg m & w=Line(M,k);
          len M=m by MATRIX_1:def 3;
          then dom M=Seg m & M.k=Line(M,k) & M.j=Line(M,j)
            by A11,A13,FINSEQ_1:def 3,MATRIX_2:10;
          hence thesis by A1,A11,A12,A13,FUNCT_1:def 8;
        end;end;
      suppose A14:not v in lines M;
        take 0K=0.K;thus 0K in the carrier of K;
        let w such that A15:w=x;
        thus not w in lines M implies 0K=0.K;
        thus w in lines M implies
          for k st k in Seg m & w=Line(M,k) holds 0K=p.k by A14,A15;end;
  end;
  consider l be Function of the carrier of V,the carrier of K such that
    A16:for x st x in the carrier of V holds Q[x,l.x] from FUNCT_2:sch 1(A8);
  reconsider L=l as Element of Funcs(the carrier of V, the carrier of K)
    by FUNCT_2:11;
  for v st not v in lines M holds L.v = 0.K by A16;
  then reconsider L as Linear_Combination of V by VECTSP_6:def 4;
  Carrier L c= lines M
  proof
    let x;assume x in Carrier L;
    then consider v such that
      A17:x = v & L.v <> 0.K by VECTSP_6:19;
    thus thesis by A16,A17;
  end;
  then reconsider L as Linear_Combination of lines M by VECTSP_6:def 7;
  set MX=MX2FinS M;set LM=L(#)MX;
  A18:len M=m & len LM=len M & len M1=m by MATRIX_1:def 3,VECTSP_6:def 8;
  now let k be Nat such that A19:1<=k & k<=m;
    k in NAT by ORDINAL1:def 13; then
    A20:k in Seg m by A19;
    then consider a such that
      A21:p.k=a & Line(M1,k) = a * Line(M,k) by A7,A18;
    A22:Line(M,k) in lines M by A20,Th103;
    then reconsider LMk=Line(M,k) as Element of n-tuples_on the carrier of K
      by Th102;
    dom LM=Seg m & dom MX = Seg m by A18,FINSEQ_1:def 3;
    then LM.k=L.(MX/.k) * MX/.k & MX/.k=M.k & L.LMk =p.k & LMk=M.k
      by A16,A20,A22,MATRIX_2:10,PARTFUN1:def 8,VECTSP_6:def 8;
    then LM.k=a * LMk & M1.k=Line(M1,k) by A20,A21,Th102,MATRIX_2:10;
    hence M1.k=LM.k by A21;
  end;
  then M1=LM by A18,FINSEQ_1:18;
  hence thesis;
end;

theorem Th109:
  for M st M is without_repeated_line holds
    ( for i st i in Seg m holds Line(M,i) <> n |-> 0.K ) &
    ( for M1 st (for i st i in Seg m ex a st Line(M1,i) = a * Line(M,i))&
                 for j st j in Seg n holds Sum Col(M1,j) = 0.K holds
       M1 = 0.(K,m,n) )
     iff lines M is linearly-independent
proof
  let M such that A1:M is without_repeated_line;
  thus (for i st i in Seg m holds Line(M,i) <> n |-> 0.K ) &
       (for M1 st (for i st i in Seg m ex a st Line(M1,i) = a * Line(M,i)) &
                  (for j st j in Seg n holds Sum Col(M1,j)=0.K) holds
       M1 = 0.(K,m,n) ) implies lines M is linearly-independent
  proof
    assume that A2:for i st i in Seg m holds Line(M,i) <> n |-> 0.K and
          A3:for M1 st (for i st i in Seg m ex a st Line(M1,i)=a*Line(M,i))&
                        for j st j in Seg n holds Sum Col(M1,j)=0.K holds
                M1 = 0.(K,m,n);
    set V=n-VectSp_over K;
    let L be Linear_Combination of lines M such that A4:Sum L = 0.V;
    set MX=MX2FinS M;set LM=L (#) MX;set FLM = FinS2MX LM;
    A5:len M=m & len M=len LM by MATRIX_1:def 3,VECTSP_6:def 8;
    then reconsider flm=FLM as Matrix of m,n,K;
    A6:for i st i in Seg m ex a st Line(flm,i)=a*Line(M,i)
    proof
      let i such that A7:i in Seg m;
      Line(M,i) in lines M by A7,Th103;
      then reconsider LM=Line(M,i) as Element of V;
      reconsider LLM=L.LM as Element of K;
      Line(M,i)=M.i by A7,MATRIX_2:10;
      then Line(flm,i)=LLM*Line(M,i) by A5,A7,Th106;
      hence thesis;
    end;
    now let j such that A8:j in Seg n;
      Carrier L c= lines M by VECTSP_6:def 7;
      then (Sum L).j = Sum Col(flm,j) & (n|->0.K).j=0.K
        by A1,A8,Th107,FINSEQ_1:4,FINSEQ_2:71;
      hence Sum Col(flm,j) = 0.K by A4,Th102;
    end;
    then A9:flm=0.(K,m,n) by A3,A6;
    assume Carrier L<>{};
    then consider x such that
      A10:x in Carrier L by XBOOLE_0:def 1;
    Carrier L c= lines M by VECTSP_6:def 7;
    then consider i such that
      A11:i in Seg m & x=Line(M,i) by A10,Th103;
    consider v such that
      A12:x=v & L.v<>0.K by A10,VECTSP_6:19;
    reconsider LM=Line(M,i) as Element of n-tuples_on the carrier of K
      by A11,A12,Th102;
    LM<> n|->0.K & len LM=n & len (n|->0.K)=n by A2,A11,FINSEQ_2:109;
    then consider j be Nat such that
      A13:1<=j & j<= n & LM.j <> (n|->0.K).j by FINSEQ_1:18;
A14:   j in NAT by ORDINAL1:def 13;
      then j in Seg n & dom LM=Seg n by A13,FINSEQ_2:144;
    then LM.j in rng LM & rng LM c= the carrier of K
      by FUNCT_1:def 5,RELSET_1:12;
    then reconsider LMj=LM.j as Element of K;
    A15:flm.i=n|->0.K & flm.i = Line(flm,i)
      by A9,A11,FUNCOP_1:13,MATRIX_2:10;
    j in Seg n & Line(M,i)=M.i by A11,A13,A14,MATRIX_2:10;
    then LM.j<>0.K & ((L.v) * LM).j=(L.v)*LMj & Line(flm,i) = (L.v) * LM
      & Line(flm,i).j=0.K by A5,A11,A12,A13,A15,Th106,FINSEQ_2:71,FVSUM_1:63;
    hence thesis by A12,VECTSP_1:44;
  end;
  assume A16:lines M is linearly-independent; set V=n-VectSp_over K;
  thus now let i such that A17:i in Seg m;
    Line(M,i) in lines M by A17,Th103;
    then Line(M,i)<>0.V by A16,VECTSP_7:3;
    hence Line(M,i) <> n |-> 0.K by Th102;
  end;
  let M1 such that
    A18:for i st i in Seg m ex a st Line(M1,i) = a * Line(M,i) and
    A19:for j st j in Seg n holds Sum Col(M1,j)=0.K;
  consider L be Linear_Combination of lines M such that
    A20:L (#) MX2FinS M = M1 by A1,A18,Th108;
  reconsider SumL=Sum L as Element of n-tuples_on the carrier of K by Th102;
  A21:Carrier L c= lines M by VECTSP_6:def 7;set n0=n|->0.K;
  A22:len SumL=n & len n0=n by FINSEQ_2:109;
  now let j be Nat such that A23:1<=j & j<=n;
    j in NAT by ORDINAL1:def 13;
    then
    A24:j in Seg n by A23;
    hence (Sum L).j = Sum Col(FinS2MX(L (#) MX2FinS M),j) by A1,A21,Th107
                   .= 0.K by A19,A20,A24
                   .= n0.j by A24,FINSEQ_1:4,FINSEQ_2:71;
  end;
  then SumL = n0 by A22,FINSEQ_1:18
           .= 0.V by Th102;
  then A25:Carrier L={} by A16,VECTSP_7:def 1;
  assume M1 <> 0.(K,m,n);
  then consider I,J be Nat such that
    A26:[I,J] in Indices M1 & M1*(I,J)<>(0.(K,m,n))*(I,J) by MATRIX_1:28;
  reconsider ii=I,jj=J as Element of NAT by ORDINAL1:def 13;
  [I,J] in Indices 0.(K,m,n) by A26,MATRIX_1:27;
  then A27:M1*(I,J)<>0.K by A26,MATRIX_3:3;
  Indices M1=Indices M by MATRIX_1:27;
  then Indices M1 = [:Seg len M, Seg width M :] &
       Indices M1 = [:Seg len M1,Seg width M1:] by FINSEQ_1:def 3;
  then A28:ii in Seg len M & jj in Seg width M & jj in Seg width M1 & len M=m
    by A26,MATRIX_1:def 3,ZFMISC_1:106;
  then M.ii=Line(M,ii) & Line(M,ii) in lines M by Th103,MATRIX_2:10;
  then reconsider Mii=M.ii as Element of V;
  A29:Line(M,ii).jj=M*(ii,jj) by A28,MATRIX_1:def 8;
  M1*(I,J) = Line(FinS2MX(L (#) MX2FinS M),ii).jj by A20,A28,MATRIX_1:def 8
          .= ((L.Mii)* Line(M,ii)).jj by A28,Th106
          .= (L.Mii) *(M*(ii,jj)) by A28,A29,FVSUM_1:63;
  then L.Mii<>0.K by A27,VECTSP_1:44;
  hence thesis by A25,VECTSP_6:19;
end;

theorem Th110:
  the_rank_of M = m implies lines M is linearly-independent
proof
  assume A1:the_rank_of M = m;
  set V=n-VectSp_over K;
  per cases;
    suppose m=0;
      then len M=0 by MATRIX_1:def 3;
      then lines M = {}(the carrier of V) by FINSEQ_1:25,RELAT_1:60;
      hence thesis by VECTSP_7:4;end;
    suppose A2:m<>0;
      then A3:width M=n by Th1;
      A4:M is without_repeated_line by A1,Th105;
   A5:now let i such that A6:i in Seg m;
        assume A7:Line(M,i) = n |-> 0.K;
        dom M=Seg len M & len M = m by FINSEQ_1:def 3,MATRIX_1:def 3;
        then consider k be Nat such that
          A8:m = k + 1 & len Del(M,i) = k by A6,FINSEQ_3:113;
        the_rank_of DelLine(M,i) = m by A1,A3,A7,Th90;
        then m<= k by A8,Th74;
        hence contradiction by A8,NAT_1:13;
      end;
      now let M1 such that
          A9:for i st i in Seg m ex a st Line(M1,i) = a * Line(M,i) and
          A10:for j st j in Seg n holds Sum Col(M1,j) = 0.K;set NULL=0.(K,m,n);
        A11:Indices M1=Indices NULL & Indices M1=Indices M & width M1=n &
          Indices M=[:Seg m,Seg n:] by A2,A3,Th1,MATRIX_1:26,27;
        assume M1 <> 0.(K,m,n);
        then consider i,j be Nat such that
          A12:[i,j] in Indices M1 & M1*(i,j)<>NULL*(i,j) by MATRIX_1:28;
        reconsider i,j as Element of NAT by ORDINAL1:def 13;
        A13:M1*(i,j)<>0.K & i in Seg m & j in Seg n
          by A11,A12,MATRIX_3:3,ZFMISC_1:106;
        consider a such that
          A14:Line(M1,i) = a * Line(M,i) by A9,A13;
        A15:Line(M,i).j=M*(i,j) by A3,A13,MATRIX_1:def 8;
        set R=RLine(M,i,a*Line(M,i));
        M1*(i,j) = Line(M1,i).j by A11,A13,MATRIX_1:def 8
                .= a * (M*(i,j)) by A3,A13,A14,A15,FVSUM_1:63;
        then a<>0.K by A13,VECTSP_1:44;
        then A16:m=the_rank_of R by A1,Th89;
        consider L be Linear_Combination of lines M such that
          A17:L (#) MX2FinS M = M1 by A4,A9,Th108;
        set LM=L (#) MX2FinS M;
        len M1=len M & len M=m by A17,MATRIX_1:def 3,VECTSP_6:def 8;
        then consider f be Function of NAT, the carrier of V such that
          A18:Sum LM= f.m and
          A19:f.0 = 0.V and
          A20:for j,v st j < m & v = LM.(j+1) holds f.(j+1) = f.j+v
            by A17,RLVECT_1:def 12;
        defpred P[Nat] means $1 < i implies for t st t=f.$1 holds
                       the_rank_of R=the_rank_of RLine(R,i,Line(R,i)+t);
        width M = len Line(M,i) by MATRIX_1:def 8
               .= len (a*Line(M,i)) by MATRIXR1:16;
        then A21:len Line(R,i)=width R & width R=width M
          by MATRIX11:def 3,MATRIX_1:def 8;
        A22:P[0]
        proof
          assume 0<i;
          let t such that A23:t=f.0;
          t = n|-> 0.K by A19,A23,Th102;
          then Line(R,i)+t=Line(R,i) & R=RLine(R,i,Line(R,i))
            by A3,A21,FVSUM_1:28,MATRIX11:30;
          hence the_rank_of R=the_rank_of RLine(R,i,Line(R,i)+t);
        end;
        A24:for k st P[k] holds P[k+1]
        proof
          let k such that A25:P[k];set k1=k+1;
          assume A26:k1 < i;
          let t such that A27:t = f.k1;
          reconsider t1=f.k as Element of n-tuples_on the carrier of K
            by Th102;
          set RR=RLine(R,i,Line(R,i)+t1);
          k < i & i<=m by A13,A26,FINSEQ_1:3,NAT_1:13;
          then A28:the_rank_of R=the_rank_of RR & k<m by A25,XXREAL_0:2;
          i<=m by A13,FINSEQ_1:3;
          then 1<=k1 & k1 <m by A26,NAT_1:14,XXREAL_0:2;
          then A29:k1 in Seg m;
          then consider a such that
            A30:Line(M1,k1) = a * Line(M,k1) by A9;
          A31:Line(M1,k1)=M1.k1 & Line(M1,k1) in lines M1
            by A29,Th103,MATRIX_2:10;
          then reconsider LMk1=LM.k1 as Element of V by A17;
          reconsider LR=Line(R,i),LM1=Line(M1,k1) as Element of n-tuples_on
            the carrier of K by A2,Th1;
          reconsider LRt=LR+t,LRt1=LR+t1 as Element of (the carrier of K)*
            by FINSEQ_1:def 11;
          A32:len (LR+t1)=width R by A3,A21,FINSEQ_2:109;
          then A33:Line(RR,i)=LR+t1 & width RR=width R & len RR=m
            by A13,MATRIX11:28,def 3,MATRIX_1:def 3;
          A34:Line(M,k1) = Line(R,k1) by A26,A29,MATRIX11:28
                        .= Line(RR,k1) by A26,A29,MATRIX11:28;
          A35:len (LR+t)=n by FINSEQ_2:109;
          then A36:RLine(RR,i,LR+t) = Replace(RR,i,LRt)
                                                     by A3,A21,A33,MATRIX11:29
                                   .= Replace(Replace(R,i,LRt1),i,LRt)
                                                     by A32,MATRIX11:29
                                   .= Replace(R,i,LRt) by FUNCT_7:36
                                   .= RLine(R,i,LR+t)by A3,A21,A35,MATRIX11:29;
          t = f.k+LMk1 by A20,A27,A28
           .= t1+Line(M1,k1) by A11,A17,A31,Th102;
          then LR+t = LR+t1 + LM1 by FINSEQOP:29
            .= Line(RR,i) + a * Line(RR,k1) by A13,A30,A32,A34,MATRIX11:28;
          hence thesis by A26,A28,A29,A33,A36,Th92;
        end;
        A37:for k holds P[k] from NAT_1:sch 1(A22,A24);
        defpred Q[Nat] means i<=$1 & $1<=m implies
          for t st t=f.$1 holds the_rank_of R=the_rank_of RLine(R,i,t);
        A38:Q[0] by A13,FINSEQ_1:3;
        A39:for k st Q[k] holds Q[k+1]
        proof
          let k such that A40:Q[k];set k1=k+1;
          assume A41:i<=k1 & k1<=m;
          let t such that A42:t=f.k1;
          reconsider t1=f.k as Element of n-tuples_on the carrier of K
            by Th102;
          1<=k1 by NAT_1:14;
          then A43:k1 in Seg m by A41;
          then consider b such that
            A44:Line(M1,k1) = b * Line(M,k1) by A9;
          A45:Line(M1,k1)=M1.k1 & Line(M1,k1) in lines M1
            by A43,Th103,MATRIX_2:10;
          then reconsider LMk1=LM.k1 as Element of V by A17;
          reconsider LR=Line(R,i),LM1=Line(M1,k1) as Element of n-tuples_on
            the carrier of K by A2,Th1;
          reconsider T=t,T1=t1 as Element of (the carrier of K)*
            by FINSEQ_1:def 11;
          k<m by A41,NAT_1:13;
          then A46:t = f.k+LMk1 by A20,A42
                    .= t1+LM1 by A17,A45,Th102;
          per cases by A41,REAL_1:def 5;
            suppose A47:i=k1;len LM1=n by FINSEQ_2:109;
              then LR=LM1 by A3,A14,A43,A47,MATRIX11:28;
              then A48:LR+t1=t by A46,FINSEQOP:34;
              k<i by A47,NAT_1:13;
              hence thesis by A37,A48;end;
            suppose A49:i<k1;
              then A50:the_rank_of R=the_rank_of RLine(R,i,t1)
                by A40,A41,NAT_1:13;set RR=RLine(R,i,t1);
              A51:len t1=width R by A3,A21,FINSEQ_2:109;
              then A52:Line(RR,i)=t1 & width RR=width R & len RR=m
                by A13,MATRIX11:28,def 3,MATRIX_1:def 3;
              then A53:t = Line(RR,i)+b* Line(R,k1)
                                              by A43,A44,A46,A49,MATRIX11:28
                        .= Line(RR,i)+ b * Line(RR,k1) by A43,A49,MATRIX11:28;
              A54:len t=n by FINSEQ_2:109;
              then A55:RLine(RR,i,t) = Replace(RR,i,T)by A3,A21,A52,MATRIX11:29
                                    .= Replace(Replace(R,i,T1),i,T)
                                                             by A51,MATRIX11:29
                                    .= Replace(R,i,T) by FUNCT_7:36
                                    .= RLine(R,i,t) by A3,A21,A54,MATRIX11:29;
              len RR=m by MATRIX_1:def 3;
              hence thesis by A43,A49,A50,A53,A55,Th92;end;
        end;
       A56:for k holds Q[k] from NAT_1:sch 1(A38,A39);
       reconsider SumLM=Sum LM as Element of n-tuples_on
         the carrier of K by Th102;set n0=n|->0.K;
       A57:len SumLM=n & len n0=n by FINSEQ_2:109;
       now let j be Nat such that A58:1<=j & j<=n;
         j in NAT by ORDINAL1:def 13;
         then
A59:     j in Seg n & Carrier L c= lines M by A58,VECTSP_6:def 7;
         M1 = FinS2MX(L (#) MX2FinS M) by A17;
         then Sum Col(M1,j) = (Sum L).j by A4,A59,Th107
                           .= SumLM.j by A4,A59,VECTSP_9:7;
         hence SumLM.j = 0.K by A10,A59
                      .= n0.j by A59,FINSEQ_1:4,FINSEQ_2:71;
       end;
       then SumLM=n0 & i<=m by A13,A57,FINSEQ_1:3,18;
       then A60:m=the_rank_of RLine(R,i,n0) by A16,A18,A56;
       set RR=RLine(R,i,n0);
       dom RR=Seg len RR & len RR=m by FINSEQ_1:def 3,MATRIX_1:def 3;
       then consider k be Nat such that
         A61:m = k + 1 & len Del(RR,i) = k by A13,FINSEQ_3:113;
       Line(RR,i)=n0 & width RR=width R
         by A3,A13,A21,A57,MATRIX11:28,def 3;
       then m=the_rank_of DelLine(RR,i) by A3,A21,A60,Th90;
       then m<=k by A61,Th74;
       hence contradiction by A61,NAT_1:13;
     end;
     hence thesis by A4,A5,Th109;end;
end;

theorem Th111:
  for M be Diagonal of n,K st the_rank_of M = n holds
    lines M is Basis of n-VectSp_over K
proof
  let M be Diagonal of n,K such that A1:the_rank_of M = n;
  set V=n-VectSp_over K;set lM=lines M;
  reconsider V'=V as Subspace of V by VECTSP_4:32;
  now let v;
    thus v in Lin lM implies v in V' by STRUCT_0:def 5;
    thus v in V' implies v in Lin lM
    proof assume v in V';
      reconsider t=v as Element of n-tuples_on the carrier of K by Th102;
      deffunc F(Nat)=(t/.$1)*(M*($1,$1)")*Line(M,$1);
      consider f be FinSequence of (width M)-tuples_on the carrier of K
        such that
        A2:len f = n and
        A3:for j be Nat st j in Seg n holds f.j = F(j) from FINSEQ_2:sch 1;
      width M=n by MATRIX_1:25;
      then reconsider f as FinSequence of the carrier of V by Th102;
      reconsider M1=FinS2MX f as Matrix of n,K by A2;
      A4:M is without_repeated_line by A1,Th105;
      now let i such that A5:i in Seg n;
        Line(M1,i) = M1.i by A5,MATRIX_2:10
                  .= F(i) by A3,A5;
        hence ex a st Line(M1,i) = a * Line(M,i);
      end;
      then consider L be Linear_Combination of lM such that
        A6:L (#) MX2FinS M = M1 by A4,Th108; set MX=MX2FinS M;
      A7:Carrier L c= lM by VECTSP_6:def 7;
      reconsider SumL=Sum L as Element of n-tuples_on the carrier of K
        by Th102;
      A8:len SumL=n & len t=n by FINSEQ_2:109;
      now let i be Nat such that A9:1<=i & i<=n;
        set C=Col(M1,i);
A10:     i in NAT by ORDINAL1:def 13;
        A11:len C=len M1 & len M1=n & len M=n & width M1=n & width M=n
          by MATRIX_1:25,def 9;
        then A12:i in Seg n & dom C=Seg len M1 & dom M1=Seg n & dom M=Seg n
          by A9,A10,FINSEQ_1:def 3;
        now let k be Nat such that A13:k in dom C & k<>i;
        A14:[k,i] in Indices M by A11,A12,A13,ZFMISC_1:106;
        A15:(MX/.k) = (M.k) by A11,A12,A13,PARTFUN1:def 8
                   .= Line(M,k) by A11,A12,A13,MATRIX_2:10;
        A16:Line(M,k).i = M*(k,i) by A11,A12,MATRIX_1:def 8
                       .= 0.K by A13,A14,MATRIX_1:def 15;
        Line(M1,k) = M1.k by A12,A13,MATRIX_2:10
                  .= L.(MX/.k)* MX/.k by A6,A11,A12,A13,VECTSP_6:def 8
                  .= L.(MX/.k)* Line(M,k) by A11,A15,Th102;
        then Line(M1,k).i = L.(MX/.k) * 0.K by A11,A12,A16,FVSUM_1:63
                         .= 0.K by VECTSP_1:39;
        hence 0.K = M1*(k,i) by A11,A12,MATRIX_1:def 8
                 .= C.k by A11,A12,A13,MATRIX_1:def 9;
      end;
      then A17:C.i = Sum C by A11,A12,MATRIX_3:14
                  .= SumL.i by A4,A6,A7,A12,Th107;
      set diag=diagonal_of_Matrix M;
      n>=1 & len diag=n by A9,MATRIX_3:def 10,NAT_1:14;
      then Det M=Product diag & Det M<>0.K & dom diag=Seg n
        by A1,Th83,FINSEQ_1:def 3,MATRIX_7:17;
      then A18:diag.i<>0.K & diag.i=M*(i,i) by A12,FVSUM_1:107,MATRIX_3:def 10;
      A19:Line(M1,i) = M1.i by A12,MATRIX_2:10
                    .= (t/.i)*((M*(i,i))")*Line(M,i) by A3,A12;
      A20:Line(M,i).i = M*(i,i) by A11,A12,MATRIX_1:def 8;
      A21:dom t=Seg n by FINSEQ_2:144;
      thus SumL.i = M1*(i,i) by A12,A17,MATRIX_1:def 9
                 .= Line(M1,i).i by A11,A12,MATRIX_1:def 8
                 .= (t/.i)*((M*(i,i))")*(M*(i,i)) by A11,A12,A19,A20,FVSUM_1:63
                 .= (t/.i)*((M*(i,i)")*(M*(i,i))) by GROUP_1:def 4
                 .= (t/.i)*1_K by A18,VECTSP_1:def 22
                 .= t/.i by VECTSP_1:def 13
                 .= t.i by A12,A21,PARTFUN1:def 8;
    end;
    then SumL=t by A8,FINSEQ_1:18;
    hence thesis by VECTSP_7:12;end;
  end;
  then Lin lM = the VectSpStr of V & lines M is linearly-independent
    by A1,Th110,VECTSP_4:38;
  hence thesis by VECTSP_7:def 3;
end;

Lm8:lines 1.(K,n) is Basis of n-VectSp_over K & the_rank_of 1.(K,n)=n
proof
  set ONE=1.(K,n);
  for i,j be Nat st [i,j] in Indices ONE & ONE*(i,j) <> 0.K holds i=j
    by MATRIX_1:def 12;
  then A1:ONE is diagonal by MATRIX_1:def 15;
  n=0 or n>=1 by NAT_1:14;
  then Det ONE=1_K & 1_K<>0.K by MATRIXR2:41,MATRIX_7:16,VECTSP_1:def 21;
  then the_rank_of ONE=n by Th83;
  hence thesis by A1,Th111;
end;

definition let K,n;
  redefine func n -VectSp_over K -> strict finite-dimensional VectSp of K;
  coherence
  proof
    lines 1.(K,n) is Basis of n-VectSp_over K by Lm8;
    hence thesis by VECTSP_9:def 1;
  end;
end;

theorem
  dim (n-VectSp_over K) = n
proof
  set ONE=1.(K,n);
  A1:lines ONE is Basis of n-VectSp_over K & the_rank_of ONE=n & len ONE=n
    by Lm8,MATRIX_1:25;
  then ONE is without_repeated_line & dom ONE=Seg n by Th105,FINSEQ_1:def 3;
  then Seg n,ONE.:Seg n are_equipotent & ONE.:Seg n=lines ONE
    by CARD_1:60,RELAT_1:146;
  then card Seg n=card lines ONE by CARD_1:21;
  then card lines ONE=n by FINSEQ_1:78;
  hence thesis by A1,VECTSP_9:def 2;
end;

theorem Th113:
  for M,i,a st for j st j in Seg m holds M*(j,i) = a
    holds M is without_repeated_line iff
          Segm(M,Seg len M,Seg width M\{i}) is without_repeated_line
proof
   let M, i, a such that A1:for j st j in Seg m holds M*(j,i) = a;
   set SMi=Seg width M\{i};set S=Segm(M,Seg len M,SMi);
   set Si=Sgm SMi;set Sl=Sgm Seg len M;
   A2:len M=m & len S=card Seg len M & card Seg len M=len M
     by FINSEQ_1:78,MATRIX_1:def 3;
   then A3:dom M=Seg m & dom S=Seg m by FINSEQ_1:def 3;
   thus M is without_repeated_line implies S is without_repeated_line
   proof
     assume A4:M is without_repeated_line;
     let x1,x2 be set such that A5:x1 in dom S & x2 in dom S & S.x1=S.x2;
     reconsider i1=x1,i2=x2 as Element of NAT by A3,A5;
     A6:SMi c= Seg width M & Sl=idseq m by A2,FINSEQ_3:54,XBOOLE_1:36;
     A7:Line(M,i1) * Si = Line(M,Sl.i1) *Si by A3,A5,A6,FINSEQ_1:4,FINSEQ_2:57
                       .= Line(S,i1) by A2,A3,A5,A6,Th47
                       .= S.i1 by A2,A3,A5,MATRIX_2:10
                       .= Line(S,i2) by A2,A3,A5,MATRIX_2:10
                       .= Line(M,Sl.i2) * Si by A2,A3,A5,A6,Th47
                       .= Line(M,i2) * Si by A3,A5,A6,FINSEQ_1:4,FINSEQ_2:57;
     A8:len Line(M,i1)=width M & len Line(M,i2)=width M by FINSEQ_2:109;
     now let k be Nat such that A9:1<=k & k<=width M;
       k in NAT by ORDINAL1:def 13;
       then
       A10:k in Seg width M by A9;
       per cases;
         suppose k = i;
           then M*(i1,k)=a & M*(i2,k)=a & M*(i1,k) =Line(M,i1).k
             by A1,A3,A5,A10,MATRIX_1:def 8;
           hence Line(M,i1).k=Line(M,i2).k by A10,MATRIX_1:def 8;end;
         suppose k <> i;
           then k in SMi & rng Si=SMi
             by A6,A10,FINSEQ_1:def 13,ZFMISC_1:64;
           then consider x such that
             A11:x in dom Si & Si.x=k by FUNCT_1:def 5;
           thus Line(M,i1).k = (Line(M,i1)*Si).x by A11,FUNCT_1:23
                            .= Line(M,i2).k by A7,A11,FUNCT_1:23;end;
     end;
     then Line(M,i1) = Line(M,i2) by A8,FINSEQ_1:18
                    .= M.i2 by A3,A5,MATRIX_2:10;
     then M.i1 = M.i2 by A3,A5,MATRIX_2:10;
     hence x1 = x2 by A3,A4,A5,FUNCT_1:def 8;
   end;
   thus S is without_repeated_line implies M is without_repeated_line
   proof
     assume A12:S is without_repeated_line;
     let x1,x2 be set such that A13:x1 in dom M & x2 in dom M & M.x1=M.x2;
     reconsider i1=x1,i2=x2 as Element of NAT by A3,A13;
     A14:SMi c= Seg width M & Sl=idseq m by A2,FINSEQ_3:54,XBOOLE_1:36;
     A15:Line(M,i1)=M.i1 & Line(M,i2)=M.i2 by A3,A13,MATRIX_2:10;
     S.x1 = Line(S,i1) by A2,A3,A13,MATRIX_2:10
         .= Line(M,Sl.i1) * Si by A2,A3,A13,A14,Th47
         .= Line(M,i2) * Si by A3,A13,A14,A15,FINSEQ_1:4,FINSEQ_2:57
         .= Line(M,Sl.i2) * Si by A3,A13,A14,FINSEQ_1:4,FINSEQ_2:57
         .= Line(S,i2) by A2,A3,A13,A14,Th47
         .= S.x2 by A2,A3,A13,MATRIX_2:10;
     hence x1=x2 by A3,A12,A13,FUNCT_1:def 8;
   end;
end;

theorem Th114:
  for M,i st M is without_repeated_line &
    lines M is linearly-independent &
    for j st j in Seg m holds M*(j,i) = 0.K holds
     lines Segm(M,Seg len M,Seg width M\{i}) is linearly-independent
proof
  let M,i; assume that A1:M is without_repeated_line and
              A2:lines M is linearly-independent and
              A3:for j st j in Seg m holds M*(j,i) = 0.K;
  set Sl=Seg len M;set SMi=Seg width M\{i};
  set S=Segm(M,Sl,SMi);
  A4:SMi c= Seg width M by XBOOLE_1:36;
  A5:card Sl=len M & len M=m by FINSEQ_1:78,MATRIX_1:def 3;
  per cases;
    suppose m=0;
      then len S=0 by A5,MATRIX_1:def 3;
      then lines S = {}(the carrier of card SMi-VectSp_over K)
        by FINSEQ_1:25,RELAT_1:60;
      hence thesis by VECTSP_7:4;end;
    suppose m<>0;
      then A6:width M=n by Th1;
      A7:S is without_repeated_line by A1,A3,Th113;
    A8:now let k such that A9:k in Seg card Sl;
        assume A10:Line(S,k) = card SMi |-> 0.K;
        Line(M,k) in lines M by A5,A9,Th103;
        then reconsider LM=Line(M,k) as Element of n-tuples_on the carrier of K
          by Th102;set n0=n|->0.K;
        LM <> n0 & len LM=n & len n0=n by A1,A2,A5,A9,Th109,FINSEQ_2:109;
        then consider n' be Nat such that
          A11:1<=n' & n'<=n & LM.n' <> n0.n' by FINSEQ_1:18;
        n' in NAT by ORDINAL1:def 13;
        then
        A12:n' in Seg n by A11;
        then A13:LM.n'=M*(k,n') & n0.n'=0.K
          by A6,FINSEQ_1:4,FINSEQ_2:71,MATRIX_1:def 8;
        then n'<>i by A3,A5,A9,A11;
        then n' in SMi & rng Sgm SMi=SMi
          by A4,A6,A12,FINSEQ_1:def 13,ZFMISC_1:64;
        then consider x such that
          A14:x in dom Sgm SMi & Sgm SMi.x = n' by FUNCT_1:def 5;
        A15:dom Sgm SMi=Seg card SMi by A4,FINSEQ_3:45;
        then reconsider x as Element of NAT by A14;
        Line(S,k) = Line(M,Sgm Sl.k) * Sgm SMi & Sgm Sl =idseq m
          by A4,A5,A9,Th47,FINSEQ_3:54;
        then Line(S,k).x=Line(M,Sgm Sl.k).n' & Sgm Sl.k = k
          by A5,A9,A14,FINSEQ_1:4,FINSEQ_2:57,FUNCT_1:23;
        hence contradiction by A10,A11,A13,A14,A15,FINSEQ_1:4,FINSEQ_2:71;
      end;
      now let M1 be Matrix of card Sl,card SMi,K such that
        A16:for i st i in Seg card Sl ex a st Line(M1,i) = a * Line(S,i) and
        A17:for j st j in Seg card SMi holds Sum Col(M1,j) = 0.K;
        defpred P[set,set] means
          for i st $1=i ex a st a=$2 & Line(M1,i) = a * Line(S,i);
        A18:for k be Nat st k in Seg m ex x be Element of K st P[k,x]
        proof
          let k be Nat such that A19:k in Seg m;
          consider a such that
          A20:Line(M1,k) = a * Line(S,k) by A5,A16,A19;
          take a;
          thus thesis by A20;
        end;
        consider p such that
          A21:dom p = Seg m and
          A22:for k be Nat st k in Seg m holds P[k,p.k]
          from FINSEQ_1:sch 5(A18);
        deffunc F(Nat)=p/.$1 * Line(M,$1);
        consider f be FinSequence of (width M)-tuples_on the carrier of K
                                                                 such that
          A23:len f = m and
          A24:for j be Nat st j in Seg m holds f.j = F(j) from FINSEQ_2:sch 1;
        reconsider f'=f as FinSequence of the carrier of n-VectSp_over K
          by A6,Th102;
        FinS2MX f' is Matrix of m,n,K by A23;
        then reconsider Mf=f as Matrix of m,n,K;
        len Mf = m by MATRIX_1:def 3; then
A25:     dom Mf = Seg m by FINSEQ_1:def 3;
        A26:now let j such that A27:j in Seg m;
            take pj=p/.j;
            thus Line(Mf,j) = Mf.j by A27,MATRIX_2:10
                           .= pj * Line(M,j) by A24,A27;
        end;
        now let j such that A28:j in Seg n;set C=Col(Mf,j);
          A29:len C = len Mf by MATRIX_1:def 9
                   .= m by MATRIX_1:def 3;
          A30:len Mf=m & dom Mf=Seg len Mf & len M1=m & dom M1=Seg len M1
            by A5,FINSEQ_1:def 3,MATRIX_1:def 3;
          per cases;
            suppose A31:j=i;set m0=m|->0.K;
              A32:len m0=m by FINSEQ_2:69;
              now let n' be Nat such that A33:1<=n' & n'<=m;
                n' in NAT by ORDINAL1:def 13;
                then
                A34:n' in Seg m & width M = n & width Mf=n by A33,Th1; then
A35:             Mf.n' = Mf/.n' by A25,PARTFUN1:def 8;
                0.K = M*(n',i) by A3,A34
                        .= Line(M,n').i by A28,A31,A34,MATRIX_1:def 8;
                then p/.n'*0.K = (p/.n' * Line(M,n')).i
                                                     by A28,A31,A34,FVSUM_1:63
                              .= (Mf/.n').i by A24,A34,A35
                              .= Line(Mf,n').i by A34,A35,MATRIX_2:10
                              .= Mf*(n',i) by A28,A31,A34,MATRIX_1:def 8
                              .= Col(Mf,i).n' by A30,A34,MATRIX_1:def 9;
                hence Col(Mf,j).n' = 0.K by A31,VECTSP_1:39
                                  .= m0.n' by A34,FINSEQ_1:4,FINSEQ_2:71;
              end;
              then C=m0 by A29,A32,FINSEQ_1:18;
              hence Sum C=0.K by MATRIX_3:13;end;
            suppose j<>i;
              then j in SMi & rng Sgm SMi=SMi
                by A4,A6,A28,FINSEQ_1:def 13,ZFMISC_1:64;
              then consider x such that
                A36:x in dom Sgm SMi & Sgm SMi.x=j by FUNCT_1:def 5;
              A37:dom Sgm SMi =Seg card SMi by A4,FINSEQ_3:45;
              then reconsider x as Element of NAT by A36;
              set C1=Col(M1,x);
              A38:len C=len Mf & len C1=len M1 & len M1=m
                by A5,MATRIX_1:def 3,def 9;
              now let n' be Nat such that A39:1<=n' & n'<=m;
                n' in NAT by ORDINAL1:def 13;
                then
                A40:n' in Seg m & width Mf=n & width M1=card SMi
                  & width S=card SMi by A5,A39,Th1;
                consider a such that
                  A41:a=p.n' & Line(M1,n') = a * Line(S,n') by A22,A40;
                Sgm Sl=idseq m & (idseq m).n'=n'
                  by A5,A40,FINSEQ_1:4,FINSEQ_2:57,FINSEQ_3:54;
                then Line(M,n') * Sgm SMi=Line(S,n') by A4,A5,A40,Th47;
                then A42:Line(S,n').x = Line(M,n').j & Line(M,n').j=M*(n',j)
                  by A6,A28,A36,FUNCT_1:23,MATRIX_1:def 8;
A43:             Mf.n' = Mf/.n' by A25,A40,PARTFUN1:def 8;
                thus C.n' = Mf*(n',j) by A30,A40,MATRIX_1:def 9
                         .= Line(Mf,n').j by A28,A40,MATRIX_1:def 8
                         .= (Mf/.n').j by A40,A43,MATRIX_2:10
                         .= (p/.n' * Line(M,n')).j by A24,A40,A43
                         .= (a * Line(M,n')).j by A21,A40,A41,PARTFUN1:def 8
                         .= a* (M*(n',j)) by A6,A28,A42,FVSUM_1:63
                         .= (a*Line(S,n')).x by A36,A37,A40,A42,FVSUM_1:63
                         .= M1*(n',x) by A36,A37,A40,A41,MATRIX_1:def 8
                         .= C1.n' by A30,A40,MATRIX_1:def 9;
              end;
              then C=C1 by A30,A38,FINSEQ_1:18;
              hence Sum C=0.K by A17,A36,A37;end;
        end;
        then A44:Mf = 0.(K,m,n) by A1,A2,A26,Th109;
        set NULL=0.(K,card Sl,card SMi);
        A45:len M1=m & len NULL=m by A5,MATRIX_1:def 3;
        now let j be Nat such that A46:1<=j & j<=m;
          j in NAT by ORDINAL1:def 13;
          then
          A47:j in Seg m by A46;
          then consider a such that
            A48:a=p.j & Line(M1,j) = a * Line(S,j) by A22;
          p.j=p/.j by A21,A47,PARTFUN1:def 8;
          then A49:a * Line(M,j) = Mf.j by A24,A47,A48
                                .= Line(Mf,j) by A47,MATRIX_2:10;
          dom Line(M,j) = Seg len Line(M,j) by FINSEQ_1:def 3
                       .= Seg width M by FINSEQ_2:109;
          then A50:rng Sgm SMi =SMi & SMi c= dom Line(M,j) & Seg n /\ SMi =SMi
            by A4,A6,FINSEQ_1:def 13,XBOOLE_1:28;
          then A51:Sgm SMi " (Seg n) = Sgm SMi "(rng Sgm SMi) by RELAT_1:168
                                    .= dom Sgm SMi by RELAT_1:169
                                    .= Seg card SMi by A4,FINSEQ_3:45;
          A52:Line(0.(K,m,n),j) = 0.(K,m,n).j by A47,MATRIX_2:10
                               .= n |-> 0.K by A47,FUNCOP_1:13;
          Sgm Sl =idseq m by A5,FINSEQ_3:54;
          then Sgm Sl.j = j by A47,FINSEQ_1:4,FINSEQ_2:57;
          then Line(S,j) = Line(M,j) * Sgm SMi by A4,A5,A47,Th47;
          then Line(M1,j) = Line(0.(K,m,n),j)*Sgm SMi
                                                  by A44,A48,A49,A50,Th87
                         .= card SMi |-> 0.K by A51,A52,FUNCOP_1:25
                         .= NULL.j by A5,A47,FUNCOP_1:13;
          hence M1.j = NULL.j by A5,A47,MATRIX_2:10;
        end;
        hence M1 = NULL by A45,FINSEQ_1:18;
      end;
      hence lines S is linearly-independent by A7,A8,Th109;end;
end;

theorem Th115:
  for V be VectSp of K
    for U be finite Subset of V st U is linearly-independent
      for u, v be Vector of V st u in U & v in U & u <> v
        holds U\{u} \/ {u+a*v} is linearly-independent
proof
  let V be VectSp of K;
  let U be finite Subset of V such that A1: U is linearly-independent;
  let u, v be Vector of V such that A2:u in U & v in U & u<>v;
  set Uu=U\{u};set ua=u+a*v;set Uua=Uu\/{ua};
  per cases;
   suppose u=ua;
     hence thesis by A1,A2,ZFMISC_1:140;end;
   suppose A3:u<>ua;
     now let L be Linear_Combination of Uua such that A4:Sum(L)=0.V;
       per cases;
         suppose A5:L.ua=0.K;
           Carrier L c= Uu
           proof let x; assume A6:x in Carrier L;
             then consider v be Vector of V such that
               A7:x = v & L.v <> 0.K by VECTSP_6:19;
             Carrier L c= Uua by VECTSP_6:def 7;
             then v in Uu or v in {ua} & not v in {ua}
               by A5,A6,A7,TARSKI:def 1,XBOOLE_0:def 2;
             hence thesis by A7;
           end;
           then reconsider L'=L as Linear_Combination of Uu by VECTSP_6:def 7;
           Uu c= U by XBOOLE_1:36;
           then Uu is linearly-independent & Sum L'=0.V by A1,A4,VECTSP_7:2;
           hence Carrier L={} by VECTSP_7:def 1;end;
         suppose A8:L.ua<>0.K;
           ua in {ua} & u in {u} & v in {v} by TARSKI:def 1;
           then A9:ua in Lin{ua} & u in Lin{u} & v in Lin{v} by VECTSP_7:13;
           then consider Lua be Linear_Combination of {ua} such that
             A10:ua=Sum Lua by VECTSP_7:12;
           consider Lu be Linear_Combination of {u} such that
             A11:u=Sum Lu by A9,VECTSP_7:12;
           consider Lv be Linear_Combination of {v} such that
             A12:v=Sum Lv by A9,VECTSP_7:12;
           reconsider LLua=L.ua * Lua as Linear_Combination of {ua}
             by VECTSP_6:61;
           reconsider LLv=(L.ua*a) * Lv as Linear_Combination of {v}
             by VECTSP_6:61;
           reconsider LLu=L.ua * Lu as Linear_Combination of {u}
             by VECTSP_6:61;
           A13:Sum (L+ LLv+LLu-LLua)=Sum (L+LLv+LLu) - Sum LLua by VECTSP_6:80
                    .= Sum (L+LLv)+ Sum LLu - Sum LLua by VECTSP_6:77
                    .= Sum L+ Sum LLv+ Sum LLu - Sum LLua by VECTSP_6:77
                    .= Sum L+ Sum LLv+ Sum LLu - L.ua * ua by A10,VECTSP_6:78
                    .= Sum L+ Sum LLv+ (L.ua)*u - L.ua * ua by A11,VECTSP_6:78
                    .= Sum L+ (a*L.ua)*v+ (L.ua)*u-L.ua*ua by A12,VECTSP_6:78
                    .= Sum L+L.ua*(a*v)+L.ua*u-L.ua*ua by VECTSP_1:def 26
                    .= Sum L+(L.ua*(a*v) + L.ua*u)-L.ua*ua by RLVECT_1:def 6
                    .= Sum L+ L.ua*(a*v+ u)- L.ua*ua by VECTSP_1:def 26
                    .= Sum L+ (L.ua*ua- L.ua*ua) by RLVECT_1:def 6
                    .= 0.V+ 0.V by A4,VECTSP_1:63
                    .= 0.V by RLVECT_1:def 7;
           A14:Carrier LLv c= {v} & {v} c= U & Carrier LLu c= {u} & {u} c= U &
             Carrier LLua c= {ua} & Carrier L c= Uua
             by A2,VECTSP_6:def 7,ZFMISC_1:37;
           then Carrier LLv c= U & Carrier LLu c= U by XBOOLE_1:1;
           then Carrier (LLv+ LLu) c= Carrier LLv \/ Carrier LLu &
                Carrier LLv \/ Carrier LLu c= U by VECTSP_6:51,XBOOLE_1:8;
           then Carrier (LLv+ LLu) c= U & Uu c= U by XBOOLE_1:1,36;
           then Carrier(LLv+LLu)\/Carrier LLua c= U\/{ua} & Uua c=U\/{ua} &
             Carrier (LLv+LLu-LLua) c= Carrier (LLv+LLu) \/Carrier LLua
             by A14,VECTSP_6:74,XBOOLE_1:13;
           then A15:Carrier (LLv+LLu-LLua) c= U\/{ua} & Carrier L c= U\/{ua}
             by A14,XBOOLE_1:1;
           L+ LLv+LLu-LLua = L+(LLv+LLu)-LLua by VECTSP_6:54
                          .= L+(LLv+LLu)+(-LLua) by VECTSP_6:def 14
                          .= L+(LLv+LLu+(-LLua)) by VECTSP_6:54
                          .= L+ (LLv+LLu-LLua) by VECTSP_6:def 14;
           then Carrier (L+ LLv+LLu-LLua)c=Carrier L\/Carrier (LLv+LLu-LLua)&
             Carrier L\/Carrier (LLv+LLu-LLua)c=U\/{ua}
             by A15,VECTSP_6:51,XBOOLE_1:8;
           then A16:Carrier (L+ LLv+LLu-LLua) c= U\/{ua} by XBOOLE_1:1;
           v <> ua
           proof
             assume v=u+a*v;
             then v-a*v = u+(a*v-a*v) by RLVECT_1:def 6
                       .= u+0.V by VECTSP_1:63
                       .= u by RLVECT_1:def 7;
             then u = 1_K*v+-a*v by VECTSP_1:def 26
                   .= 1_K*v+(-a)*v by VECTSP_1:68
                   .= (1_K-a)*v by VECTSP_1:def 26;
             then A17:{v,u} is linearly-dependent by A2,VECTSP_7:8;
             {v,u} c= U by A2,ZFMISC_1:38;
             hence thesis by A1,A17,VECTSP_7:2;
           end;
           then not ua in Carrier LLv & not ua in Carrier LLu
             by A3,A14,TARSKI:def 1;
           then A18:LLv.ua=0.K & LLu.ua=0.K by VECTSP_6:20;
           A19:u + a * v <> 0.V
           proof
             assume A20:0.V=u + a * v;
             {v,u} c= U by A2,ZFMISC_1:38;
             then {v,u} is linearly-independent & 1_K*u = u
               by A1,VECTSP_1:def 26,VECTSP_7:2;
             then 1_K=0.K by A2,A20,VECTSP_7:9;
             hence thesis by VECTSP_1:def 21;
           end;
           Lua.ua * ua = ua by A10,VECTSP_6:43
                      .= 1_K*ua by VECTSP_1:def 26;
           then A21:Lua.ua=1_K by A19,VECTSP10:5;
           A22:(L+ LLv+LLu-LLua).ua=(L+ LLv+LLu+(-LLua)).ua by VECTSP_6:def 14
                    .= (L+ LLv+LLu).ua + (-LLua).ua by VECTSP_6:def 11
                    .= (L+ LLv).ua+ LLu.ua + (-LLua).ua by VECTSP_6:def 11
                    .= L.ua+ 0.K+ 0.K + (-LLua).ua by A18,VECTSP_6:def 11
                    .= L.ua+ 0.K + (-LLua).ua by RLVECT_1:def 7
                    .= L.ua+(-LLua).ua by RLVECT_1:def 7
                    .= L.ua-LLua.ua by VECTSP_6:67
                    .= L.ua - L.ua*1_K by A21,VECTSP_6:def 12
                    .= L.ua - L.ua by VECTSP_1:def 13
                    .= 0.K by VECTSP_1:66;
           Carrier (L+ LLv+LLu-LLua) c= U
           proof
             let x;
             assume A23:x in Carrier (L+ LLv+LLu-LLua);
             then consider v be Element of V such that
               A24:x=v & (L+ LLv+LLu-LLua).v<>0.K by VECTSP_6:19;
             assume A25:not x in U;
             x in {ua} by A16,A23,A25,XBOOLE_0:def 2;
             hence contradiction by A22,A24,TARSKI:def 1;
           end;
           then reconsider LLL=L+LLv+LLu-LLua as Linear_Combination of U
             by VECTSP_6:def 7;
           not u in {ua} & not u in Uu by A3,TARSKI:def 1,ZFMISC_1:64;
           then not u in Carrier LLv&not u in Carrier LLua&not u in Carrier L
             by A2,A14,TARSKI:def 1,XBOOLE_0:def 2;
           then A26:LLv.u=0.K & LLua.u=0.K & L.u=0.K by VECTSP_6:20;
           A27:u<>0.V by A1,A2,VECTSP_7:3;
           Lu.u * u = u by A11,VECTSP_6:43
                   .= 1_K*u by VECTSP_1:def 26;
           then A28:Lu.u=1_K by A27,VECTSP10:5;
           LLL.u = (L+ LLv+LLu+(-LLua)).u by VECTSP_6:def 14
                .= (L+ LLv+LLu).u + (-LLua).u by VECTSP_6:def 11
                .= (L+ LLv).u+ LLu.u + (-LLua).u by VECTSP_6:def 11
                .= L.u+ LLv.u+ LLu.u + (-LLua).u by VECTSP_6:def 11
                .= 0.K+ 0.K + LLu.u -0.K by A26,VECTSP_6:67
                .= 0.K+LLu.u -0.K by RLVECT_1:def 7
                .= LLu.u-0.K by RLVECT_1:def 7
                .= LLu.u by VECTSP_1:65
                .= L.ua*1_K by A28,VECTSP_6:def 12
                .= L.ua by VECTSP_1:def 13;
           then u in Carrier LLL by A8,VECTSP_6:19;
           hence Carrier L={} by A1,A13,VECTSP_7:def 1;end;
     end;
     hence thesis by VECTSP_7:def 1;end;
end;

theorem Th116:
  for V be VectSp of K
    for u, v be Vector of V holds
      x in Lin {u,v} iff ex a,b st x = a * u + b * v
proof
  let V be VectSp of K;
  let u, v be Vector of V;
  per cases;
    suppose A1:u=v;
      then A2:{u,v}={u} by ENUMSET1:69;
      thus x in Lin {u,v} implies ex a,b st x = a * u + b * v
      proof
        assume x in Lin{u,v};
        then consider a such that
        A3:x=a*u by A2,VECTSP10:4;
        x = a*u+0.V by A3,RLVECT_1:def 7
         .= a*u+0.K*v by VECTSP10:2;
        hence thesis;
      end;
      given a,b such that
      A4:x = a * u + b * v;
      x=(a+b)*u by A1,A4,VECTSP_1:def 26;
      hence x in Lin{u,v} by A2,VECTSP10:4;end;
    suppose A5:u<>v;
     thus x in Lin {u,v} implies ex a,b st x = a * u + b * v
     proof
       assume x in Lin {u,v};
       then consider L be Linear_Combination of {u,v} such that
         A6:x=Sum L by VECTSP_7:12;
       x=L.u*u+L.v*v by A5,A6,VECTSP_6:44;
       hence thesis;
     end;
     given a,b such that
        A7:x = a * u + b * v;
     deffunc F(set) = 0.K;
     consider L be Function of the carrier of V, the carrier of K such that
       A8: L.u = a & L.v=b and
       A9: for z be Element of V st z <> u & z<>v holds L.z = F(z)
                                                    from FUNCT_2:sch 7(A5);
     reconsider L as Element of Funcs(the carrier of V,the carrier of K)
       by FUNCT_2:11;
     now let z be Vector of V such that A10:not z in {u,v};
       z <> v & z<>u by A10,TARSKI:def 2;
       hence L.z = 0.K by A9;
     end;
     then reconsider L as Linear_Combination of V by VECTSP_6:def 4;
     Carrier L c= {u,v}
     proof
       let x such that A11: x in Carrier L;
       L.x <> 0.K by A11,VECTSP_6:20;
       then x = v or x=u by A9,A11;
       hence x in {u,v} by TARSKI:def 2;
     end;
     then reconsider L as Linear_Combination of {u,v} by VECTSP_6:def 7;
     Sum(L) = x by A5,A7,A8,VECTSP_6:44;
     hence x in Lin{u,v} by VECTSP_7:12;end;
end;

theorem Th117:
  for M st lines M is linearly-independent & M is without_repeated_line
    for i,j st j in Seg len M & i <> j holds
      RLine(M,i,Line(M,i) + a*Line(M,j)) is without_repeated_line &
      lines RLine(M,i,Line(M,i) + a*Line(M,j)) is linearly-independent
proof
  let M such that A1:lines M is linearly-independent and
                  A2:M is without_repeated_line;
  let i,j such that A3:j in Seg len M & i<>j;
  set V=n-VectSp_over K;
  set Li=Line(M,i);set Lj=Line(M,j);set R=RLine(M,i,Li+a*Lj);
  per cases;
    suppose not i in Seg len M;
      hence thesis by A1,A2,Th40;
    end;
    suppose A4:i in Seg len M;
      A5:len M=m & dom M=Seg len M by FINSEQ_1:def 3,MATRIX_1:def 3;
      then A6:M.i<>M.j & M.i=Li & M.j=Lj & Li in lines M & Lj in lines M
        by A2,A3,A4,Th103,FUNCT_1:def 8,MATRIX_2:10;
      then reconsider LI=Li,LJ=Lj as Vector of V;
      reconsider li=LI,lj=LJ as Element of n-tuples_on the carrier of K
        by Th102;
    A7:lines M\{LI}\/{LI+a*LJ} is linearly-independent by A1,A6,Th115;
    A8:for k st k in Seg m & k<>i holds Line(R,k)<>Li + a*Lj
      proof
        let k such that A9:k in Seg m & k<>i;
        A10:Line(R,k) = Line(M,k) by A9,MATRIX11:28;
        assume A11:Line(R,k)=Li+a*Lj;set Lk=Line(M,k);
        A12:Lj<>Lk
        proof
          assume A13:Lj=Lk;
          {LI,LJ} c= lines M by A6,ZFMISC_1:38;
          then A14:{LI,LJ} is linearly-independent & (-1_K)*LI=(-1_K)*li
            & (1_K+(-1_K)*a)*LJ=(1_K+(-1_K)*a)*lj by A1,Th102,VECTSP_7:2;
          0.V = n|->0.K by Th102
             .= lj+-(li+a*lj) by A10,A11,A13,FVSUM_1:35
             .= lj+(-li+-a*lj) by FVSUM_1:40
             .= lj+((-1_K)*li+-a*lj) by FVSUM_1:72
             .= lj+((-1_K)*li+(-1_K)*(a*lj)) by FVSUM_1:72
             .= lj+((-1_K)*li+((-1_K)*a)*lj) by FVSUM_1:67
             .= lj+(((-1_K)*a)*lj+(-1_K)*li) by FINSEQOP:34
             .= lj+((-1_K)*a)*lj+(-1_K)*li by FINSEQOP:29
             .= 1_K*lj+((-1_K)*a)*lj+(-1_K)*li by FVSUM_1:70
             .= (1_K+(-1_K)*a)*lj+(-1_K)*li by FVSUM_1:68
             .= (1_K+(-1_K)*a)*LJ+(-1_K)*LI by A14,Th102;
          then -1_K=0.K by A6,A14,VECTSP_7:9;
          then 1_K=0.K by VECTSP_1:86;
          hence thesis by VECTSP_1:def 21;
        end;
        A15:M.i<>M.k & M.k=Lk & Lk in lines M
          by A2,A4,A5,A9,Th103,FUNCT_1:def 8,MATRIX_2:10;
        then reconsider LK=Lk as Vector of V;
        reconsider KIJ={LK,LI,LJ} as Subset of V;
        a*lj = a*LJ by Th102;
        then li+a*lj = LI+a*LJ by Th102
                    .= (1_K)*LI+a*LJ by VECTSP_1:def 26;
        then A16:li+a*lj in Lin {LI,LJ} by Th116;
        KIJ c= lines M by A6,A15,JORDAN16:2;
        then Lk in KIJ & KIJ is linearly-independent & KIJ \ {LK} = {LI, LJ}
          by A1,A6,A12,A15,ENUMSET1:136,def 1,VECTSP_7:2;
        hence thesis by A10,A11,A16,VECTSP_9:18;
      end;
      A17:len (li+a*lj)=n & n=width M
        by A4,A5,Th1,FINSEQ_1:4,FINSEQ_2:109;
      then A18:Line(R,i)=Li+a*Lj by A4,A5,MATRIX11:28;
      now let x1,x2 be set such that A19:x1 in dom R & x2 in dom R & R.x1=R.x2;
        A20:dom R=Seg len R & len R=m by FINSEQ_1:def 3,MATRIX_1:def 3;
        then reconsider i1=x1,i2=x2 as Element of NAT by A19;
        A21:R.i1=Line(R,i1) & R.i2=Line(R,i2) by A19,A20,MATRIX_2:10;
        per cases;
          suppose i1=i & i2=i;
            hence x1=x2;end;
          suppose i1=i & i2<>i or i1<>i & i2=i;
            hence x1=x2 by A8,A18,A19,A20,A21;end;
          suppose i1<>i & i2<>i;
            then R.i1=Line(M,i1) & Line(M,i1) = M.i1 & R.i2=Line(M,i2) &
              Line(M,i2) = M.i2 by A19,A20,A21,MATRIX11:28,MATRIX_2:10;
            hence x1=x2 by A2,A5,A19,A20,FUNCT_1:def 8;end;
      end;
      hence R is without_repeated_line by FUNCT_1:def 8;
      reconsider LiaLj=li+a*lj as Element of (the carrier of K)*
        by FINSEQ_1:def 11;
      reconsider LL=LiaLj as set;set iLL=i.-->LL;
      A22:a*lj = a*LJ by Th102;
      RLine(M,i,li+a*lj) = Replace(M, i, LiaLj) by A17,MATRIX11:29
                        .= M+*iLL by A4,A5,FUNCT_7:def 3;
      then lines RLine(M,i,Li+a*Lj) = M.:(dom M\dom iLL)\/rng iLL by FRECHET:12
                                   .= M.:(dom M\{i})\/rng iLL by CQC_LANG:5
                                   .= M.:(dom M\{i})\/{LL} by CQC_LANG:5
                                   .= (M.:dom M\M.:{i})\/{LL} by A2,FUNCT_1:123
                                   .= (lines M\Im(M,i))\/{LL} by RELAT_1:146
                                   .= (lines M\{LI})\/{li+a*lj}
                                                       by A4,A5,A6,FUNCT_1:117;
      hence thesis by A7,A22,Th102;end;
end;

theorem Th118:
  P c= Seg m implies lines Segm(M,P,Seg n) c= lines M
  proof
    assume A1:P c= Seg m;
    set S=Segm(M,P,Seg n);
    let x;assume x in lines S;
    then consider i such that
      A2:i in Seg card P & x = Line(S,i) by Th103;
    dom Sgm P=Seg card P by A1,FINSEQ_3:45;
    then A3:Sgm P.i in rng Sgm P & rng Sgm P = P
      by A1,A2,FINSEQ_1:def 13,FUNCT_1:def 5;
    Seg m <>{} by A1,A2,CARD_1:47,FINSEQ_1:4,XBOOLE_1:3;
    then width M=n by Th1,FINSEQ_1:4;
    then Line(S,i)=Line(M,Sgm P.i) by A2,Th48;
    hence thesis by A1,A2,A3,Th103;
  end;

theorem Th119:
  P c= Seg m & lines M is linearly-independent implies
    lines Segm(M,P,Seg n) is linearly-independent
proof
  assume that A1:P c= Seg m and
              A2:lines M is linearly-independent;
  card Seg n=n & lines Segm(M,P,Seg n) c= lines M
    by A1,Th118,FINSEQ_1:78;
  hence thesis by A2,VECTSP_7:2;
end;

theorem Th120:
  P c= Seg m & M is without_repeated_line implies
    Segm(M,P,Seg n) is without_repeated_line
proof
  assume that A1:P c= Seg m and
              A2:M is without_repeated_line;
  set S=Segm(M,P,Seg n);
  let x1,x2 be set such that A3:x1 in dom S & x2 in dom S & S.x1=S.x2;
  len S=card P by MATRIX_1:def 3;
  then A4:dom S=Seg card P by FINSEQ_1:def 3;
  then reconsider i1=x1,i2=x2 as Element of NAT by A3;
  Seg m<>{} by A1,A3,A4,CARD_1:47,FINSEQ_1:4,XBOOLE_1:3;
  then width M=n by Th1,FINSEQ_1:4;
  then A5:Line(S,i1) = Line(M,Sgm P.i1) & Line(S,i2) = Line(M,Sgm P.i2) &
    Line(S,i1)=S.i1 & Line(S,i2)=S.i2 by A3,A4,Th48,MATRIX_2:10;
  A6:dom Sgm P=dom S & rng Sgm P=P by A1,A4,FINSEQ_1:def 13,FINSEQ_3:45;
  then Sgm P.i1 in P & Sgm P.i2 in P by A3,FUNCT_1:def 5;
  then Sgm P.i1 in Seg m & Sgm P.i2 in Seg m & len M=m by A1,MATRIX_1:def 3;
  then Line(M,Sgm P.i1)=M.(Sgm P.i1) & Line(M,Sgm P.i2)=M.(Sgm P.i2) &
    Sgm P.i1 in dom M & Sgm P.i2 in dom M by FINSEQ_1:def 3,MATRIX_2:10;
  then Sgm P.i1=Sgm P.i2 & Sgm P is one-to-one
    by A1,A2,A3,A5,FINSEQ_3:99,FUNCT_1:def 8;
  hence thesis by A3,A6,FUNCT_1:def 8;
end;

theorem Th121:
  for M be Matrix of m,n,K holds
    lines M is linearly-independent &
          M is without_repeated_line iff the_rank_of M = m
proof
  defpred P[Nat] means
    for m,n st m=$1
      for M be Matrix of m,n,K holds
        lines M is linearly-independent &
        M is without_repeated_line iff the_rank_of M = m;
  A1:P[0]
  proof
    let m,n such that A2:m=0;
    let M be Matrix of m,n,K;
    len M=0 by A2,MATRIX_1:def 3;
    then M={} & the_rank_of M <= 0 by Th74,FINSEQ_1:25;
    then lines M={}(the carrier of n-VectSp_over K) & the_rank_of M=0 & M={}
      by FINSEQ_1:27;
    hence thesis by A2,VECTSP_7:4;
  end;
  A3:for k st P[k] holds P[k+1]
  proof
    let k such that A4:P[k];
    let m,n such that A5:m=k+1;
    let M be Matrix of m,n,K;
    thus (lines M is linearly-independent & M is without_repeated_line)
      implies the_rank_of M = m
   proof
     assume that A6:lines M is linearly-independent and
                 A7:M is without_repeated_line;
     A8:m in Seg m & len M=m by A5,FINSEQ_1:6,MATRIX_1:def 3;
     then Line(M,m) in lines M by Th103;
     then reconsider LM=Line(M,m) as Element of n-tuples_on the carrier of K
       by Th102;
     A9:LM <> n |-> 0.K & len LM=n & len (n|->0.K)=n
       by A6,A7,A8,Th109,FINSEQ_2:109;
     then consider i be Nat such that
       A10:1<=i & i<=n & LM.i<>(n|->0.K).i by FINSEQ_1:18;
       i in NAT by ORDINAL1:def 13;
       then
     A11:i in Seg n & len Line(M,m)=width M by A10,MATRIX_1:def 8;
     then A12:LM.i<>0.K & LM.i=M*(m,i)
       by A9,A10,FINSEQ_2:71,MATRIX_1:def 8;
     defpred Q[Nat] means $1<m implies
       ex M1 be Matrix of m,n,K st
         Line(M1,m)=Line(M,m) & lines M1 is linearly-independent &
         M1 is without_repeated_line & the_rank_of M1=the_rank_of M &
         for j st j <= $1 & j in Seg m holds M1*(j,i)=0.K;
     A13:Q[0]
     proof
       assume 0<m;take M;
       thus thesis by A6,A7,FINSEQ_1:3;
     end;
     A14:for l st Q[l] holds Q[l+1]
     proof
       let L be Element of NAT such that A15:Q[L];set L1=L+1;
       assume A16:L1<m;
       then consider M1 be Matrix of m,n,K such that
         A17:Line(M1,m)=Line(M,m) & lines M1 is linearly-independent and
         A18:M1 is without_repeated_line & the_rank_of M1=the_rank_of M and
         A19:for j st j <= L & j in Seg m holds M1*(j,i) = 0.K by A15,NAT_1:13;
       set Mmi=M*(m,i);set MLi=M1*(L1,i);
       0+1<=L1 by NAT_1:13;
       then A20:L1 in Seg m by A16;
       take R=RLine(M1,L1,Line(M1,L1)+((-(Mmi"*MLi))*Line(M1,m)));
       len M1=m by MATRIX_1:def 3;
       hence Line(R,m)=Line(M,m) & lines R is linearly-independent &
             R is without_repeated_line & the_rank_of R=the_rank_of M
         by A8,A16,A17,A18,Th92,Th117,MATRIX11:28;
       let j such that A21:j <= L1 & j in Seg m;
       set LML=Line(M1,L1);set LMm=Line(M1,m);
       A22:width R=n & width M1=n & width M=n by A21,Th1,FINSEQ_1:4;
       then A23:LML.i=MLi & LMm.i=M*(m,i) by A11,A17,MATRIX_1:def 8;
       then A24:((-(Mmi"*MLi))*LMm).i=(-(Mmi" * MLi)) *Mmi
         by A11,A22,FVSUM_1:63;
       len (LML+(-(Mmi"*MLi))*LMm) = width M1 by FINSEQ_2:109;
       then Line(R,L1) = LML+(-(Mmi"*MLi))*LMm by A20,MATRIX11:28;
       then A25:Line(R,L1).i = MLi+(-(Mmi" * MLi)) *Mmi
                                 by A11,A22,A23,A24,FVSUM_1:22
           .= MLi+(-(1_K*(Mmi" * MLi))) *Mmi by VECTSP_1:def 13
           .= MLi+(-1_K)*(Mmi" * MLi) *Mmi by VECTSP_1:41
           .= MLi+(-1_K)*(Mmi" * MLi *Mmi) by GROUP_1:def 4
           .= MLi+(-1_K)*(Mmi" * Mmi* MLi ) by GROUP_1:def 4
           .= MLi+(-1_K)*(1_K* MLi) by A12,VECTSP_1:def 22
           .= MLi+(-1_K)*MLi by VECTSP_1:def 13
           .= MLi+(-(1_K)*MLi) by VECTSP_1:41
           .= MLi+(-MLi) by VECTSP_1:def 13
           .= 0.K by RLVECT_1:16;
       per cases by A21,NAT_1:9;
         suppose j=L1;
           hence 0.K=R*(j,i) by A11,A22,A25,MATRIX_1:def 8;end;
         suppose A26:j<=L;
           then A27:j<L1 by NAT_1:13;
           thus 0.K = M1*(j,i) by A19,A21,A26
                   .= Line(M1,j).i by A11,A22,MATRIX_1:def 8
                   .= Line(R,j).i by A21,A27,MATRIX11:28
                   .= R*(j,i) by A11,A22,MATRIX_1:def 8;end;
     end;
     A28:for l holds Q[l] from NAT_1:sch 1(A13,A14);
     k<m by A5,NAT_1:13;
     then consider M1 be Matrix of m,n,K such that
       A29:Line(M1,m)=Line(M,m) & lines M1 is linearly-independent and
       A30:M1 is without_repeated_line & the_rank_of M1=the_rank_of M and
       A31:for j st j <= k & j in Seg m holds M1*(j,i)=0.K by A28;
     set S=Segm(M1,Seg k,Seg n);
     k<m by A5,NAT_1:13;
     then A32:Seg k c= Seg m by FINSEQ_1:7;
     then A33:lines S is linearly-independent &
       S is without_repeated_line by A29,A30,Th119,Th120;
     A34:card Seg k=k & card Seg n=n  by FINSEQ_1:78;
     then A35:len S=k by MATRIX_1:def 3;
     set SwS=Seg width S;set SSS=Segm(S,Seg k,SwS\{i});
A36:  i in NAT by ORDINAL1:def 13;
     now let j be Element of NAT such that A37:j in Seg k;
       A38:(Sgm Seg k).j = (idseq k).j by FINSEQ_3:54
                        .= j by A37,FINSEQ_1:4,FINSEQ_2:57;
       A39:(Sgm Seg n).i = (idseq n).i by FINSEQ_3:54
                        .= i by A11,FINSEQ_1:4,FINSEQ_2:57;
       A40:j<=k & j in Seg m by A32,A37,FINSEQ_1:3;
       width S=n by A34,A37,Th1,FINSEQ_1:4;
       then [j,i] in [:Seg k,Seg width S:] by A11,A37,ZFMISC_1:106;
       then [j,i] in Indices S by A35,FINSEQ_1:def 3;
       hence S*(j,i) = M1*((Sgm Seg k).j,(Sgm Seg n).i) by Def1
                    .= 0.K by A31,A38,A39,A40;
     end;
     then lines SSS is linearly-independent & SSS is without_repeated_line
       by A33,A34,A35,A36,Th113,Th114;
     then the_rank_of SSS=k by A4,A34;
     then consider P,Q such that
       A41:[:P,Q:] c= Indices SSS & card P = card Q and
       A42:card P = k & Det EqSegm(SSS,P,Q)<>0.K by Def4;
     P={} iff Q={} by A41,CARD_1:47,CARD_2:59;
     then consider P1,Q1 such that
       A43:P1 c= Seg k & Q1 c= Seg width S\{i} and
           P1 = (Sgm Seg k).:P & Q1=Sgm (Seg width S\{i}).:Q and
       A44:card P1=card P & card Q1=card Q and
       A45:Segm(SSS,P,Q) = Segm(S,P1,Q1) by A41,Th57;
     Seg width S\{i} c= Seg width S by XBOOLE_1:36;
     then A46:Q1 c= Seg width S by A43,XBOOLE_1:1;
     then [:P1,Q1:] c= [:Seg k,Seg width S:] by A43,ZFMISC_1:119;
     then A47:[:P1,Q1:] c= Indices S by A35,FINSEQ_1:def 3;
     P1={} iff Q1={} by A41,A44,CARD_1:47,CARD_2:59;
     then consider P2,Q2 such that
       A48:P2 c= Seg k & Q2 c= Seg n and
       A49:P2= Sgm (Seg k).:P1 & Q2=Sgm (Seg n).:Q1 and
       A50:card P2=card P1 & card Q2=card Q1 and
       A51:Segm(S,P1,Q1) = Segm(M1,P2,Q2) by A47,Th57;
     A52:P2 = (idseq k).:P1 by A49,FINSEQ_3:54
           .= P1 by A43,FRECHET:13;
     A53:now per cases;
       suppose k=0;
         then width S=0 by A35,MATRIX_1:def 4;
         then Seg width S c= Seg n by FINSEQ_1:7;
         hence Q1 c= Seg n by A46,XBOOLE_1:1;end;
       suppose k>0;
         hence Q1 c= Seg n by A34,A46,Th1;end;
     end;
     A54:Q2 = (idseq n).:Q1 by A49,FINSEQ_3:54
           .= Q1 by A53,FRECHET:13;
     A55:EqSegm(SSS,P,Q) = Segm(S,P1,Q1) by A41,A45,Def3
                        .= EqSegm(M1,P1,Q1) by A41,A44,A51,A52,A54,Def3;
     reconsider i,m as non zero Element of NAT by A5,A10,ORDINAL1:def 13;
     set P2m=P2\/{m};set Q2i=Q2\/{i};
     {m} c= Seg m & P2 c= Seg m & {i} c= Seg n & m<>0
        by A8,A11,A32,A48,XBOOLE_1:1,ZFMISC_1:37;
     then A56:P2m c= Seg m & Q2i c= Seg n by A48,XBOOLE_1:8;
     then A57:[:P2m,Q2i:] c= [:Seg m,Seg n:] & len M1=m & width M1=n
       by Th1,ZFMISC_1:119;
     then A58:[:P2m,Q2i:] c= Indices M1 by FINSEQ_1:def 3;
     k<m & i in {i} by A5,NAT_1:13,TARSKI:def 1;
     then A59:not m in P2 & not i in Q2
       by A43,A52,A54,FINSEQ_1:3,XBOOLE_0:def 4;
     then A60:card P2m=m & card Q2i=m by A5,A41,A42,A44,A50,CARD_2:54;
     set SP2m=Sgm P2m;set SQ2i=Sgm Q2i;
     i in {i} by TARSKI:def 1;
     then i in Q2i & rng SQ2i=Q2i & dom SQ2i=Seg m
       by A56,A60,FINSEQ_1:def 13,FINSEQ_3:45,XBOOLE_0:def 2;
     then consider Si be set such that
     A61:Si in Seg m & SQ2i.Si=i by FUNCT_1:def 5;
     reconsider Si as Element of NAT by A61;
     set ES=EqSegm(M1,P2m,Q2i);set LC=LaplaceExpC(ES,Si);
     card Seg m=m by FINSEQ_1:78;
     then A62:P2m=Seg m by A56,A60,CARD_FIN:1;
     A63:now let j such that A64:j in Seg m;
        A65:Col(M1,i) = Col(Segm(M1,P2m,Q2i),Si) by A57,A60,A61,A62,Th50
                     .= Col(ES,Si) by A60,Def3;
        A66:dom M1=Seg len M1 & m=len ES & dom ES=Seg len ES
          by A60,FINSEQ_1:def 3,MATRIX_1:def 3;
        hence ES*(j,Si) = Col(M1,i).j by A64,A65,MATRIX_1:def 9
                       .= M1*(j,i) by A57,A64,A66,MATRIX_1:def 9;
     end;
     len LC = m by A60,LAPLACE:def 8;
     then A67:dom LC=Seg m by FINSEQ_1:def 3;
     now let j be Nat such that A68:j in Seg m & j<>m;
       reconsider J=j as Element of NAT by ORDINAL1:def 13;
       j<=m by A68,FINSEQ_1:3;
       then j<=k by A5,A68,NAT_1:9;
       then 0.K = M1*(j,i) by A31,A68
               .= ES*(j,Si) by A63,A68;
       hence 0.K = ES*(j,Si)*Cofactor(ES,J,Si) by VECTSP_1:39
                .= LC.j by A67,A68,LAPLACE:def 8;
     end;
     then A69:LC.m = Sum LC by A8,A67,MATRIX_3:14
                  .= Det ES by A60,A61,LAPLACE:27;
     Sgm P2m.m = (idseq m).m by A62,FINSEQ_3:54
              .= m by A8,FINSEQ_2:57;
     then A70:Delete(ES,m,Si) = EqSegm(M1,P2m\{m},Q2i\{i}) by A8,A60,A61,Th64
                             .= EqSegm(M1,P2,Q2i\{i}) by A59,ZFMISC_1:141
                             .= EqSegm(M1,P2,Q2) by A59,ZFMISC_1:141;
     A71:len Delete(ES,m,Si)=m-'1 & len EqSegm(M1,P2,Q2)=k
       by A42,A44,A50,A60,MATRIX_1:def 3;
     reconsider mSi=m+Si as Element of NAT;
     A72:ES*(m,Si) = M1*(m,i) by A8,A63
                  .= Line(M,m).i by A11,A29,A57,MATRIX_1:def 8;
     1_K<>0.K by VECTSP_1:def 21;
     then -1_K<>0.K by VECTSP_1:86;
     then power(K).(-1_K,mSi)<>0.K by Lm6;
     then power(K).(-1_K,mSi)*Minor(ES,m,Si)<>0.K
       by A42,A52,A54,A55,A60,A70,A71,VECTSP_1:44;
     then ES*(m,Si)* Cofactor(ES,m,Si)<>0.K by A12,A72,VECTSP_1:44;
     then Det ES<>0.K by A8,A67,A69,LAPLACE:def 8;
     then the_rank_of M1 >= m & the_rank_of M1 <= m
       by A57,A58,A60,Def4,Th74;
     hence thesis by A30,XXREAL_0:1;
   end;
    thus thesis by Th105,Th110;
  end;
  for k holds P[k] from NAT_1:sch 1(A1,A3);
  hence thesis;
end;

theorem Th122:
  for U be Subset of n-VectSp_over K st U c= lines M
    ex P st P c= Seg m & lines Segm(M,P,Seg n) = U &
            Segm(M,P,Seg n) is without_repeated_line
proof
  let U be Subset of n-VectSp_over K such that A1:U c= lines M;
  defpred P[set,set] means
    ex i st i in Seg m & Line(M,i)=$1 & $2=i;
  A2:for x st x in U ex y st y in Seg m & P[x,y]
  proof
    let x such that A3:x in U;
    consider i such that
      A4:i in Seg m & x=Line(M,i) by A1,A3,Th103;
    take i;
    thus thesis by A4;
  end;
  consider f be Function of U,Seg m such that
    A5:for x st x in U holds P[x,f.x] from FUNCT_2:sch 1(A2);
  A6:rng f c= Seg m & not 0 in Seg m by PSCOMP_1:def 1,RELSET_1:12;
  then rng f is finite Subset of NAT & not 0 in rng f
    by FINSET_1:13,XBOOLE_1:1;
  then reconsider P=rng f as without_zero finite Subset of NAT
    by PSCOMP_1:def 1;
  take P; thus P c= Seg m by RELSET_1:12;set S=Segm(M,P,Seg n);
  A7:lines S c= U
  proof
    let x;assume A8:x in lines S;
    then consider y such that
      A9:y in dom S & S.y=x by FUNCT_1:def 5;
    A10:dom S = Seg len S by FINSEQ_1:def 3
            .= Seg card P by MATRIX_1:def 3;
    then reconsider y as Element of NAT by A9;
    dom Sgm P = Seg card P by A6,FINSEQ_3:45;
    then Sgm P.y in rng Sgm P & rng Sgm P=P & P c= Seg m
      by A6,A9,A10,FINSEQ_1:def 13,FUNCT_1:def 5;
    then consider z be set such that
      A11:z in dom f & f.z=Sgm P.y by FUNCT_1:def 5;
    consider i such that
      A12:i in Seg m & Line(M,i)=z & f.z=i by A5,A11;
    lines S c= lines M by A6,Th118;
    then M<>{} & len M=m by A8,FINSEQ_1:27,MATRIX_1:def 3;
    then m<>0 by FINSEQ_1:25;
    then width M=n by Th1;
    then z = Line(S,y) by A9,A10,A11,A12,Th48
          .= x by A9,A10,MATRIX_2:10;
    hence thesis by A11;
  end;
  U c= lines S
  proof
    let x; assume A13:x in U;
    then consider i such that
    A14:i in Seg m & Line(M,i)=x & f.x=i by A5;
    dom f=U by A14,FUNCT_2:def 1;
    then i in P by A13,A14,FUNCT_1:def 5;
    then i in rng Sgm P by A6,FINSEQ_1:def 13;
    then consider y such that
      A15:y in dom Sgm P & Sgm P.y=i by FUNCT_1:def 5;
    A16:dom Sgm P=Seg card P by A6,FINSEQ_3:45;
    then reconsider y as Element of NAT by A15;
    width M=n by A14,Th1,FINSEQ_1:4;
    then Line(S,y)=x by A14,A15,A16,Th48;
    hence thesis by A15,A16,Th103;
  end;
  hence U=lines S by A7,XBOOLE_0:def 10;
  let x1,x2 be set such that A17:x1 in dom S & x2 in dom S & S.x1=S.x2;
  A18:dom S = Seg len S by FINSEQ_1:def 3
          .= Seg card P by MATRIX_1:def 3;
  then reconsider i1=x1,i2=x2 as Element of NAT by A17;
  dom Sgm P = Seg card P by A6,FINSEQ_3:45;
  then A19:Sgm P.i1 in rng Sgm P & Sgm P.i2 in rng Sgm P &
    rng Sgm P=P & P c= Seg m by A6,A17,A18,FINSEQ_1:def 13,FUNCT_1:def 5;
  then consider y1 be set such that
    A20:y1 in dom f & f.y1=Sgm P.i1 by FUNCT_1:def 5;
  consider y2 be set such that
    A21:y2 in dom f & f.y2=Sgm P.i2 by A19,FUNCT_1:def 5;
  consider j1 be Element of NAT such that
    A22:j1 in Seg m & Line(M,j1)=y1 & f.y1=j1 by A5,A20;
  consider j2 be Element of NAT such that
    A23:j2 in Seg m & Line(M,j2)=y2 & f.y2=j2 by A5,A21;
  width M=n by A22,Th1,FINSEQ_1:4;
  then Line(S,i1)=y1 & Line(S,i2)=y2 & Line(S,i1)=S.i1 & Line(S,i2)=S.i2
    by A17,A18,A20,A21,A22,A23,Th48,MATRIX_2:10;
  then Sgm P.i1=Sgm P.i2 & dom Sgm P=dom S & Sgm P is one-to-one
    by A6,A17,A18,A20,A21,FINSEQ_3:45,99;
  hence thesis by A17,FUNCT_1:def 8;
end;

theorem
  for RANK be Element of NAT holds the_rank_of M = RANK iff
         (ex U be finite Subset of n-VectSp_over K st
              U is linearly-independent & U c= lines M & card U = RANK) &
          for W be finite Subset of n-VectSp_over K st
              W is linearly-independent & W c= lines M holds card W <= RANK
proof
  let R be Element of NAT; set V=n-VectSp_over K;
  consider P,Q such that
    A1:[:P,Q:] c= Indices M & card P = card Q and
    A2:card P = the_rank_of M & Det EqSegm(M,P,Q)<>0.K by Def4;
  A3:now per cases;
      suppose len M=0;
        then width M=0 by MATRIX_1:def 4;
        hence Seg width M c= Seg n by FINSEQ_1:7;end;
      suppose len M>0;
        then m>0 by MATRIX_1:def 3;
        hence Seg width M c= Seg n by Th1;end;
  end;
  set S=Segm(M,P,Seg n);
  Q c= Seg width M by A1,Th67;
  then Q c= Seg n & Segm(M,P,Q)=EqSegm(M,P,Q)
    by A1,A3,Def3,XBOOLE_1:1;
  then the_rank_of EqSegm(M,P,Q) <= the_rank_of S &
       the_rank_of EqSegm(M,P,Q) = card P & len S=card P &
       the_rank_of S<=len S by A2,Th74,Th80,Th83,MATRIX_1:def 3;
  then the_rank_of S = card P by XXREAL_0:1;
  then A4:lines S is linearly-independent & S is without_repeated_line
    by Th121;
  then dom S,S.:dom S are_equipotent & S.:dom S=lines S
    by CARD_1:60,RELAT_1:146;
  then A5:card lines S = card dom S by CARD_1:21
                      .= card Seg len S by FINSEQ_1:def 3
                      .= card Seg card P by MATRIX_1:def 3
                      .= card P by FINSEQ_1:78;
  P c= Seg len M & len M=m by A1,Th67,MATRIX_1:def 3;
  then A6:lines S c= lines M by Th118;
  A7:card Seg n=n by FINSEQ_1:78;
  A8:for W be finite Subset of n-VectSp_over K st
      W is linearly-independent & W c= lines M holds
        card W <= the_rank_of M
  proof
    let W be finite Subset of n-VectSp_over K such that
      A9:W is linearly-independent & W c= lines M;
    consider P1 such that
      A10:P1 c= Seg m & lines Segm(M,P1,Seg n) = W and
      A11:Segm(M,P1,Seg n) is without_repeated_line by A9,Th122;
    set S1=Segm(M,P1,Seg n);
    dom S1,S1.:dom S1 are_equipotent & S1.:dom S1=lines S1
      by A11,CARD_1:60,RELAT_1:146;
    then A12:card W = card dom S1 by A10,CARD_1:21
                   .= card Seg len S1 by FINSEQ_1:def 3
                   .= card Seg card P1 by MATRIX_1:def 3
                   .= card P1 by FINSEQ_1:78;
    per cases;
      suppose card P1=0;
        hence thesis by A12;end;
      suppose card P1>0;
        then Seg m <>{} by A10,CARD_1:47,XBOOLE_1:3;
        then width M=n & len M=m by Th1,FINSEQ_1:4;
        then [:P1,Seg n:] c= [:Seg len M,Seg width M:] by A10,ZFMISC_1:119;
        then [:P1,Seg n:] c= Indices M by FINSEQ_1:def 3;
        then the_rank_of S1 <= the_rank_of M by Th79;
        hence thesis by A7,A9,A10,A11,A12,Th121;end;
  end;
  thus the_rank_of M = R implies
      (ex U be finite Subset of n-VectSp_over K st
              U is linearly-independent & U c= lines M & card U = R) &
          for W be finite Subset of n-VectSp_over K st
              W is linearly-independent & W c= lines M holds card W <= R
    by A2,A4,A5,A6,A7,A8;
  assume that A13:ex U be finite Subset of n-VectSp_over K st
                   U is linearly-independent & U c= lines M & card U = R and
              A14:for W be finite Subset of n-VectSp_over K st
                  W is linearly-independent & W c= lines M holds card W <= R;
  the_rank_of M <= R & R <= the_rank_of M by A2,A4,A5,A6,A7,A8,A13,A14;
  hence thesis by XXREAL_0:1;
end;
