:: Recursive Definitions
::  by Krzysztof Hryniewiecki
::
:: Received September 4, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FINSEQ_1, FUNCT_1, RELAT_1, TARSKI, ARYTM_1, FUNCOP_1, ARYTM,
      VALUED_0, ORDINAL1, CLASSES1, ORDINAL2, MCART_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1,
      MCART_1, SUBSET_1, NUMBERS, XXREAL_0, REAL_1,
      NAT_1, RELAT_1, FUNCT_1, FUNCOP_1, ORDINAL1, ORDINAL2, CLASSES1,
      FINSEQ_1, FUNCT_2, BINOP_1, DOMAIN_1, VALUED_0;
 constructors BINOP_1, DOMAIN_1, FUNCOP_1, XXREAL_0, REAL_1, NAT_1, FINSEQ_1,
      VALUED_0, CLASSES1, ORDINAL2;
 registrations SUBSET_1, FUNCT_1, ORDINAL1, RELSET_1, NUMBERS, XREAL_0,
      FINSEQ_1, VALUED_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, BINOP_1;
 theorems TARSKI, NAT_1, FUNCT_1, FINSEQ_1, FUNCT_2, RELAT_1, XBOOLE_1,
      FUNCOP_1, XREAL_1, XXREAL_0, ORDINAL1, CLASSES1, ORDINAL2, XBOOLE_0,
      ZFMISC_1, MCART_1;
 schemes NAT_1, FUNCT_1, FINSEQ_1, BINOP_1, CLASSES1, ORDINAL1;

begin

reserve n,m,k for Element of NAT,
  D for non empty set,
  Z,x,y,z,y1,y2 for set,
  p,q for FinSequence;

Lm1: for p being FinSequence of D st 1 <= n & n <= len p holds
p.n is Element of D
proof
  let p be FinSequence of D;
  assume 1 <= n & n <= len p;
  then n in Seg len p by FINSEQ_1:3;
  then n in dom p by FINSEQ_1:def 3;
  then
A1: p.n in rng p by FUNCT_1:def 5;
  rng p c= D by FINSEQ_1:def 4;
  hence thesis by A1;
end;

definition
  let p be natural-valued Function;
  let n be set;
  redefine func p.n -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

::       Schemes of existence

scheme RecEx{A() -> set,P[set,set,set]}:
  ex f being Function st dom f = NAT & f.0 = A() &
  for n being Element of NAT holds P[n,f.n,f.(n+1)]
provided
A1: for n being Element of NAT for x being set ex y being set st P[n,x,y]
proof
  defpred Q[set,set,set] means ex O2 being Ordinal st O2 = $3 &
  (for X being set, n being Element of NAT st X c= Rank the_rank_of $2
  ex Y being set st Y c= Rank O2 & P[n,X,Y]) & (for O being Ordinal st
  for X being set, n being Element of NAT st X c= Rank the_rank_of $2
  ex Y being set st Y c= Rank O & P[n,X,Y] holds O2 c= O);
A2: for n being Element of NAT for x being set ex y being set st Q[n,x,y]
  proof
    let n be Element of NAT;
    let x be set;
    defpred Y[set,set] means for m being Element of NAT ex y being set st
    $2 is Ordinal & y c= Rank the_rank_of $2 & P[m,$1,y];
A3: for x' being set st x' in bool Rank the_rank_of x
    ex o being set st Y[x',o]
    proof
      let x' be set;
      assume x' in bool Rank the_rank_of x;
      defpred X[set,set] means
      ex y being set st $2 is Ordinal & y c= Rank the_rank_of $2 & P[$1,x',y];
A4:   for e being set st e in NAT ex u being set st X[e,u]
      proof
        let i be set;
        assume i in NAT;
        then reconsider i' = i as Element of NAT;
        thus ex o being set, y being set st
        o is Ordinal & y c= Rank the_rank_of o & P[i,x',y]
        proof
          consider y being set such that
A5:       P[i',x',y] by A1;
          take o = the_rank_of y, y;
          thus o is Ordinal;
          the_rank_of the_rank_of y = the_rank_of y by CLASSES1:81;
          hence y c= Rank the_rank_of o by CLASSES1:73;
          thus thesis by A5;
        end;
      end;
      consider h being Function such that
A6:   dom h = NAT and
A7:   for i being set st i in NAT holds X[i,h.i] from CLASSES1:sch 1(A4);
      take o = sup rng h;
      thus for m being Element of NAT ex y being set st
      o is Ordinal & y c= Rank the_rank_of o & P[m,x',y]
      proof
        let m be Element of NAT;
        consider y being set such that
A8:     h.m is Ordinal and
A9:     y c= Rank the_rank_of (h.m) and
A10:    P[m,x',y] by A7;
        take y;
        thus o is Ordinal;
        h.m in rng h by A6,FUNCT_1:def 5;
        then h.m in sup rng h by A8,ORDINAL2:27;
        then
A11:    h.m c= o by ORDINAL1:def 2;
        reconsider O = h.m as Ordinal by A8;
A12:    Rank O c= Rank o by A11,CLASSES1:43;
        y c= Rank O by A9,CLASSES1:81;
        then y c= Rank o by A12,XBOOLE_1:1;
        hence y c= Rank the_rank_of o by CLASSES1:81;
        thus thesis by A10;
      end;
    end;
    consider f being Function such that
A13: dom f = bool Rank the_rank_of x and
A14: for x' being set st x' in bool Rank the_rank_of x holds Y[x',f.x']
    from CLASSES1:sch 1(A3);
    defpred X[Ordinal] means
    for X being set, n being Element of NAT st X c= Rank the_rank_of x
    ex Y being set st Y c= Rank $1 & P[n,X,Y];
A15: ex O being Ordinal st X[O]
    proof
      take O2 = sup rng f;
      thus (for X being set, m being Element of NAT st X c= Rank the_rank_of x
      ex Y being set st Y c= Rank O2 & P[m,X,Y])
      proof
        let X be set;
        let m be Element of NAT;
        assume
A16:    X c= Rank the_rank_of x;
        then consider Y being set such that
A17:    f.X is Ordinal and
A18:    Y c= Rank the_rank_of (f.X) and
A19:    P[m,X,Y] by A14;
        take Y;
        f.X in rng f by A13,A16,FUNCT_1:def 5;
        then f.X in sup rng f by A17,ORDINAL2:27;
        then
A20:    f.X c= O2 by ORDINAL1:def 2;
        reconsider O = f.X as Ordinal by A17;
A21:    Rank O c= Rank O2 by A20,CLASSES1:43;
        the_rank_of O = O by CLASSES1:81;
        hence Y c= Rank O2 by A18,A21,XBOOLE_1:1;
        thus thesis by A19;
      end;
    end;
    consider O2 being Ordinal such that
A22: X[O2] &
    (for O being Ordinal st X[O] holds O2 c= O) from ORDINAL1:sch 1(A15);
    take O2;
    thus thesis by A22;
  end;
A23: for n being Element of NAT for x,y1,y2 being set st Q[n,x,y1] & Q[n,x,y2]
  holds y1 = y2
  proof
    let n be Element of NAT;
    let x,y1,y2 be set;
    assume that
A24: Q[n,x,y1] and
A25: Q[n,x,y2];
    consider O1 being Ordinal such that
A26: O1 = y1 and
A27: for X being set, n being Element of NAT st X c= Rank the_rank_of x
    ex Y being set st Y c= Rank O1 & P[n,X,Y] and
A28: for O being Ordinal st
    for X being set, n being Element of NAT st X c= Rank the_rank_of x
    ex Y being set st Y c= Rank O & P[n,X,Y] holds O1 c= O by A24;
    consider O2 being Ordinal such that
A29: O2 = y2 and
A30: for X being set, n being Element of NAT st X c= Rank the_rank_of x
    ex Y being set st Y c= Rank O2 & P[n,X,Y] and
A31: for O being Ordinal st
    for X being set, n being Element of NAT st X c= Rank the_rank_of x
    ex Y being set st Y c= Rank O & P[n,X,Y] holds O2 c= O by A25;
A32: O1 c= O2 by A28,A30;
    O2 c= O1 by A27,A31;
    hence thesis by A26,A29,A32,XBOOLE_0:def 10;
  end;
  ex f being Function st dom f = NAT & f.0 = the_rank_of A() &
  for n being Element of NAT holds Q[n,f.n,f.(n+1)]
proof
  deffunc S(Element of NAT) = {k : k <= $1};
A9: for n ex p being Function st dom p = S(n) & p.0 = the_rank_of A() &
  for k st k < n holds Q[k,p.k,p.(k+1)]
  proof
    defpred Z[Element of NAT] means
    ex p being Function st dom p = S($1) & p.0 = the_rank_of A() &
    for k st k < $1 holds Q[k,p.k,p.(k+1)];
A10: Z[0]
    proof
      set s = S(0) --> the_rank_of A();
      take s;
      thus dom s = S(0) by FUNCOP_1:19;
      0 in S(0);
      hence s.0 = the_rank_of A() by FUNCOP_1:13;
      thus thesis by NAT_1:2;
    end;
A11: for n st Z[n] holds Z[n+1]
    proof
      let n;
      given p being Function such that
A12:  dom p = S(n) & p.0 = the_rank_of A() &
 for k st k < n holds Q[k,p.k,p.(k+1)];
      consider z such that
A13:  Q[n,p.n,z] by A2;
      defpred ST[set,set] means (for k st k=$1 holds
      (k <= n implies $2 = p.k) & (k=n+1 implies $2 = z ));
A20:  for x st x in S(n+1) ex y st ST[x,y]
      proof
        let x;
        assume x in S(n+1);
        then
A21:    ex m st m=x & m <= n+1;
        then reconsider t=x as Element of NAT;
A22:    t = n+1 implies thesis
        proof
          assume
B23:      t=n+1;
          take z;
          let k such that
A24:      k=x;
          thus k <= n implies z=p.k
          proof
            assume k <= n;
            then n+1 <= n+0 by B23,A24;
            hence thesis by XREAL_1:8;
          end;
          thus thesis;
        end;
        t <= n implies thesis
        proof
          assume
A25:      t <= n;
          take p.x;
          let k;
          assume
A26:      k=x;
          hence k <= n implies p.x = p.k;
          assume k=n+1;
          then n+1 <= n+0 by A25,A26;
          hence thesis by XREAL_1:8;
        end;
        hence thesis by A21,A22,NAT_1:8;
      end;
      consider q being Function such that
A27:  dom q = S(n+1) & for x st x in S(n+1) holds ST[x,q.x] from CLASSES1:sch 1
      (A20);
      take q;
      thus dom q = S(n+1) by A27;
      thus q.0 = the_rank_of A()
      proof
        0 <= n+1 by NAT_1:2;
        then
A28:    0 in S(n+1);
        0 <= n by NAT_1:2;
        hence thesis by A12,A27,A28;
      end;
      let k such that
A29:  k < n+1;
A30:  now
        assume
A31:    k = n;
        then k+1 in S(n+1);
        then q.(k+1) = z by A31,A27;
        then
A32:    Q[k,p.k,q.(k+1)] by A13,A31;
        k <= n+1 by A31,NAT_1:11;
        then k in S(n+1);
        then q.k = p.k by A31,A27;
        hence thesis by A32;
      end;
      now
        assume k <> n;
        then
A33:    k+1 <> n+1;
A34:    k+1 <= n+1 by A29,NAT_1:13;
        then
A35:    k+1 <= n by A33,NAT_1:8;
        k+1 in S(n+1) by A34;
        then
B36:     q.(k+1) = p.(k+1) by A35,A27;
A37:    k < n by A35,NAT_1:13;
        then
A38:    Q[k,p.k,q.(k+1)] by B36,A12;
        k in S(n+1) by A29;
        then p.k = q.k by A37,A27;
        hence thesis by A38;
      end;
      hence thesis by A30;
    end;
    thus for n holds Z[n] from NAT_1:sch 1(A10,A11);
  end;
A39: for p,q being Function,k st dom p = S(k) & dom q = S(k+1) & p.0 = q.0 &
  for n st n < k holds Q[n,p.n,p.(n+1)] & Q[n,q.n,q.(n+1)] holds p.k = q.k
  proof
    let p,q be Function;
    let k;
    defpred Z[Element of NAT] means $1 <= k implies p.$1 = q.$1;
    assume
A40: dom p = S(k) & dom q = S(k+1) & p.0 = q.0 &
    for n st n < k holds Q[n,p.n,p.(n+1)] & Q[n,q.n,q.(n+1)];
    then
A41: Z[0];
A42: for n st Z[n] holds Z[n+1]
    proof
      let n;
      assume
A43:  n <= k implies p.n = q.n;
      assume n+1 <= k;
      then n < k by NAT_1:13;
      then Q[n,p.n,p.(n+1)] & Q[n,p.n,q.(n+1)] by A40,A43;
      hence thesis by A23;
    end;
    for n holds Z[n] from NAT_1:sch 1(A41,A42);
    hence thesis;
  end;
  ex f being Function st dom f = NAT &
  for n ex p being Function st dom p = S(n) & p.0 = the_rank_of A() &
  (for k st k < n holds Q[k,p.k,p.(k+1)]) & f.n = p.n
  proof
    defpred Z[set,set] means
    for n st n=$1 ex p being Function
     st dom p = S(n) & p.0 = the_rank_of A() &
    (for k st k < n holds Q[k,p.k,p.(k+1)]) & $2 = p.n;
A48: for x st x in NAT ex y st Z[x,y]
    proof
      let x;
      assume x in NAT;
      then reconsider n=x as Element of NAT;
      consider p being Function such that
A49:  dom p = S(n) & p.0 = the_rank_of A() & for k st k < n
 holds Q[k,p.k,p.(k+1)] by A9;
      take p.n;
      let k such that
A50:  k = x;
      take p;
      thus thesis by A49,A50;
    end;
    consider f being Function such that
A51: dom f = NAT & for x st x in NAT holds Z[x,f.x] from CLASSES1:sch 1
    (A48);
    take f;
    thus dom f = NAT by A51;
    let n;
    consider p being Function such that
A52: dom p = S(n) & p.0 = the_rank_of A() &
 (for k st k < n holds Q[k,p.k,p.(k+1)])
    & f.n = p.n by A51;
    take p;
    thus dom p = S(n) & p.0 = the_rank_of A() by A52;
    thus for k st k < n holds Q[k,p.k,p.(k+1)] by A52;
    thus thesis by A52;
  end;
  then consider f being Function such that
A53: dom f = NAT &
 for n ex p being Function st dom p = S(n) & p.0 = the_rank_of A() &
  (for k st k < n holds Q[k,p.k,p.(k+1)]) & f.n = p.n;
  take f;
  thus dom f = NAT by A53;
  thus f.0 = the_rank_of A()
  proof
    ex p being Function st dom p = S(0) & p.0 = the_rank_of A() &
    (for k st k < 0 holds Q[k,p.k,p.(k+1)]) & f.0 = p.0 by A53;
    hence thesis;
  end;
  let d be Element of NAT;
  consider p being Function such that
A54: dom p = S(d+1) & p.0 = the_rank_of A() & (for k st k < d+1
 holds Q[k,p.k,p.(k+1)]) &
  f.(d+1) = p.(d+1) by A53;
  consider q being Function such that
A55: dom q = S(d) & q.0 = the_rank_of A() &
 (for k st k < d holds Q[k,q.k,q.(k+1)]) &
  f.d = q.d by A53;
A56: p.d = q.d
  proof
    dom p = S(d+1) & dom q = S(d) & p.0 = q.0 & for k st k < d holds
    Q[k,q.k,q.(k+1)] & Q[k,p.k,p.(k+1)]
    proof
      thus dom p = S(d+1) by A54;
      thus dom q = S(d) by A55;
      thus p.0 = q.0 by A54,A55;
      let k;
      assume
A57:  k < d;
      hence Q[k,q.k,q.(k+1)] by A55;
      d <= d+1 by NAT_1:11;
      then k < d+1 by A57,XXREAL_0:2;
      hence thesis by A54;
    end;
    hence thesis by A39;
  end;
  thus thesis by A54,A55,A56,XREAL_1:31;
end;
  then consider g being Function such that
A33: dom g = NAT and
A34: g.0 = the_rank_of A() and
A35: for n being Element of NAT holds Q[n,g.n,g.(n+1)];
  set beta = sup rng g;
  defpred X[set,set] means ex i being Element of NAT st i = $1`2 &
  P[$1`2,$1`1,$2`1] & $2`2 = i+1 &
  not ex y being set st P[$1`2,$1`1,y] & the_rank_of y in the_rank_of $2`1;
A36: for x being set st x in [:Rank beta, NAT:] ex u being set st X[x,u]
  proof
    let x be set;
    assume x in [:Rank beta, NAT:];
    then consider a,b being set such that
A37: a in Rank beta and
A38: b in NAT and
A39: x = [a,b] by ZFMISC_1:def 2;
    reconsider b as Element of NAT by A38;
    the_rank_of a in beta by A37,CLASSES1:74;
    then consider alfa being Ordinal such that
A40: alfa in rng g and
A41: the_rank_of a c= alfa by ORDINAL2:29;
A42: a c= Rank alfa by A41,CLASSES1:73;
    consider k being set such that
A43: k in dom g and
A44: alfa = g.k by A40,FUNCT_1:def 5;
    reconsider k as Element of NAT by A33,A43;
A45: Q[k,alfa,g.(k+1)] by A35,A44;
    then reconsider O2 = g.(k+1) as Ordinal;
A46: x`2 = b & x`1 = a by A39,MCART_1:7;
    defpred X[Ordinal] means ex y being set st y c= Rank $1 & P[x`2,x`1,y];
    a c= Rank the_rank_of alfa by A42,CLASSES1:81;
    then ex y being set st y c= Rank O2 & P[x`2,x`1,y] by A45,A46;
    then
A47: ex O being Ordinal st X[O];
    consider O being Ordinal such that
A48: X[O] and
A49: for O' being Ordinal st X[O'] holds O c= O' from ORDINAL1:sch 1(A47);
    consider Y being set such that
A50: Y c= Rank O and
A51: P[b,a,Y] by A46,A48;
    take u = [Y,b+1], i = b;
    thus i = x`2 by A39,MCART_1:7;
    thus P[x`2,x`1,u`1] by A46,A51,MCART_1:7;
    thus u`2 = i+1 by MCART_1:7;
    given y being set such that
A52: P[x`2,x`1,y] and
A53: the_rank_of y in the_rank_of u`1;
A54: the_rank_of y in the_rank_of Y by A53,MCART_1:7;
A55: the_rank_of Y c= O by A50,CLASSES1:73;
    y c= Rank the_rank_of y by CLASSES1:def 8;
    hence contradiction by A49,A52,A54,A55,ORDINAL1:7;
  end;
  consider F being Function such that dom F = [:Rank beta, NAT:] and
A56: for x being set st x in [:Rank beta, NAT:] holds X[x,F.x]
  from CLASSES1:sch 1(A36);
  deffunc U(Nat,set) = (F.[$2,$1])`1;
  consider f being Function such that
A57: dom f = NAT and
A58: f.0 = A() and
A59: for n being Nat holds f.(n+1) = U(n,f.n) from NAT_1:sch 11;
  take f;
  thus dom f = NAT by A57;
  thus f.0 = A() by A58;
  defpred X[Element of NAT] means the_rank_of (f.$1) in sup rng g;
  g.0 in rng g by A33,FUNCT_1:def 5;
  then
A60: X[0] by A34,A58,ORDINAL2:27;
A61: for n being Element of NAT st X[n] holds X[n+1]
  proof
    let n be Element of NAT;
    assume
A62: the_rank_of (f.n) in sup rng g;
    then f.n in Rank sup rng g by CLASSES1:74;
    then
A63: [f.n,n] in [:Rank beta, NAT:] by ZFMISC_1:106;
    [f.n,n]`1 = f.n & [f.n,n]`2 = n by MCART_1:7;
    then consider i being Element of NAT such that i = n and
    P[n,f.n,(F.[f.n,n])`1] and (F.[f.n,n])`2 = i+1 and
A64: not ex y being set st P[n,f.n,y] &
    the_rank_of y in the_rank_of (F.[f.n,n])`1 by A56,A63;
A65: f.(n+1) = (F.[f.n,n])`1 by A59;
    consider o1 being Ordinal such that
A66: o1 in rng g and
A67: the_rank_of (f.n) c= o1 by A62,ORDINAL2:29;
    consider m being set such that
A68: m in dom g and
A69: g.m = o1 by A66,FUNCT_1:def 5;
    reconsider m as Element of NAT by A33,A68;
    consider o2 being Ordinal such that
A70: o2 = g.(m+1) and
A71: for X being set, n being Element of NAT st X c= Rank the_rank_of (g.m)
    ex Y being set st Y c= Rank o2 & P[n,X,Y] and for o being Ordinal st
    for X being set, n being Element of NAT st X c= Rank the_rank_of (g.m)
    ex Y being set st Y c= Rank o & P[n,X,Y] holds o2 c= o by A35;
    the_rank_of (f.n) c= the_rank_of (g.m) by A67,A69,CLASSES1:81;
    then f.n c= Rank the_rank_of (g.m) by CLASSES1:73;
    then consider YY being set such that
A72: YY c= Rank o2 and
A73: P[n,f.n,YY] by A71;
A74: the_rank_of (F.[f.n,n])`1 c= the_rank_of YY by A64,A73,ORDINAL1:26;
A75: the_rank_of YY c= o2 by A72,CLASSES1:73;
    g.(m+1) in rng g by A33,FUNCT_1:def 5;
    then o2 in sup rng g by A70,ORDINAL2:27;
    hence thesis by A65,A74,A75,ORDINAL1:22
    ,XBOOLE_1:1;
  end;
A76: for n being Element of NAT holds X[n] from NAT_1:sch 1(A60,A61);
A77: [A(),0]`1 = A() & [A(),0]`2 = 0 by MCART_1:7;
  A() in Rank sup rng g by A58,A60,CLASSES1:74;
  then [A(),0] in [:Rank beta, NAT:] by ZFMISC_1:106;
  then consider i being Element of NAT such that i = [A(),0]`2 and
A78: P[0,A(),(F.[A(),0])`1] and
  (F.[A(),0])`2 = i+1 and not ex y being set st P[0,A(),y] &
  the_rank_of y in the_rank_of (F.[A(),0])`1 by A56,A77;
  defpred X[Element of NAT] means P[$1,f.$1,f.($1+1)];
A79: X[0] by A58,A59,A78;
A80: for n being Element of NAT st X[n] holds X[n+1]
  proof
    let n be Element of NAT;
    assume P[n,f.n,f.(n+1)];
A81: [f.(n+1),n+1]`1 = f.(n+1) & [f.(n+1),n+1]`2 = n+1 by MCART_1:7;
    the_rank_of (f.(n+1)) in sup rng g by A76;
    then f.(n+1) in Rank sup rng g by CLASSES1:74;
    then [f.(n+1),n+1] in [:Rank beta, NAT:] by ZFMISC_1:106;
    then consider i being Element of NAT such that i = n+1 and
A82: P[n+1,f.(n+1),(F.[f.(n+1),n+1])`1] and (F.[f.(n+1),n+1])`2 = i+1 and
    not ex y being set st P[(n+1),f.(n+1),y] &
    the_rank_of y in the_rank_of (F.[f.(n+1),n+1])`1 by A56,A81;
    thus thesis by A59,A82;
  end;
  thus for n being Element of NAT holds X[n] from NAT_1:sch 1(A79,A80);
end;

scheme RecExD{D()->non empty set,A() -> Element of D(), P[set,set,set]}:
  ex f being Function of NAT,D() st f.0 = A() &
  for n being Element of NAT holds P[n,f.n,f.(n+1)]
provided
A1: for n being Element of NAT for x being Element of D()
ex y being Element of D() st P[n,x,y]
proof
  defpred Q[Element of NAT qua non empty set,set,set] means P[$1,$2,$3];
A2: for x being Element of NAT qua non empty set for y being Element of D()
  ex z being Element of D() st Q[x,y,z] by A1;
  consider f being Function of [:(NAT qua non empty set),D():],D() such that
A3: for x being Element of NAT qua non empty set for y being Element of D()
  holds Q[x,y,f.(x,y)] from BINOP_1:sch 3(A2);
  defpred P[FinSequence] means
  ($1.1=A() & for n st n+2 <= len $1 holds $1.(n+2) = f.[n,$1.(n+1)]);
  consider X being set such that
A4: for x holds x in X iff ex p st p in D()* & P[p] & x=p from FINSEQ_1:sch 4;
A5: x in X implies x in D()*
  proof
    assume x in X;
    then ex p st p in D()* &
    (p.1=A() & for n st n + 2 <= len p holds p.(n+2) = f.[n,p.(n+1)]) &
    x=p by A4;
    hence thesis;
  end;
A6: for p,q st p in X & q in X & len p <= len q holds p c= q
  proof
    let p,q;
    assume
A7: p in X & q in X & len p <= len q;
A8: (p in D()* & p.1 = A() &
    for n st n + 2 <= len p holds p.(n+2) = f.[n,p.(n+1)]) &
    (q in D()* & q.1 = A() &
    for n st n + 2 <= len q holds q.(n+2) = f.[n,q.(n+1)])
    proof
A9:   ex p' being FinSequence st p' in D()* & (p'.1 = A() &
      for n st n+2<=len p' holds p'.(n+2) = f.[n,p'.(n+1)]) & p=p' by A4,A7;
      ex q' being FinSequence st q' in D()* & (q'.1 = A() &
      for n st n+2<=len q' holds q'.(n+2) = f.[n,q'.(n+1)]) & q=q' by A4,A7;
      hence thesis by A9;
    end;
A10: for n st 1 <= n & n <= len p holds p.n = q.n
    proof
      defpred P[Nat] means 1 <= $1 & $1 <= len p & p.$1 <> q.$1;
      assume ex n st P[n];
      then
A11:  ex n be Nat st P[n];
      consider k be Nat such that
A12:  P[k] & for n be Nat st P[n] holds k <= n from NAT_1:sch 5(A11);
      k = 1
      proof
        assume
A13:    k <> 1;
        0 <> k by A12;
        then consider n being Nat such that
A14:    k = n+1 by NAT_1:6;
        1 < n+1 by A12,A13,A14,XXREAL_0:1;
        then
A15:    1 <= n by NAT_1:13;
A16:    n <> 0 by A13,A14;
        n+0 <= n+1 by XREAL_1:9;
        then
A17:    n <= len p by A12,A14,XXREAL_0:2;
A18:    n+0 < n+1 by XREAL_1:8;
        consider m being Nat such that
A19:    n=m+1 by A16,NAT_1:6;
        reconsider m as Element of NAT by ORDINAL1:def 13;
A20:    m+2 <= len q by A7,A12,A14,A19,XXREAL_0:2;
        p.k = p.(m+(1+1)) by A14,A19
          .=f.[m,p.n] by A8,A12,A14,A19
          .=f.[m,q.(m+1)] by A12,A14,A15,A17,A18,A19
          .=q.k by A8,A14,A19,A20;
        hence thesis by A12;
      end;
      hence contradiction by A8,A12;
    end;
    now
      let x;
      assume x in p;
      then consider n be Nat such that
A21:  n in dom p & x = [n,p.n] by FINSEQ_1:16;
      n in Seg len p by A21,FINSEQ_1:def 3;
      then
A22:  1 <= n & n <= len p by FINSEQ_1:3;
      then
A23:  x = [n,q.n] by A10,A21;
      1 <= n & n <= len q by A7,A22,XXREAL_0:2;
      then n in Seg len q by FINSEQ_1:3;
      then n in dom q by FINSEQ_1:def 3;
      hence x in q by A23,FUNCT_1:8;
    end;
    hence thesis by TARSKI:def 3;
  end;
  set Y = union X;
  ex f being Function st f = Y
  proof
    defpred Z[set,set] means [$1,$2] in Y;
A24: for x,y,z st Z[x,y] & Z[x,z] holds y=z
    proof
      let x,y,z;
      assume
A25:  [x,y] in Y & [x,z] in Y;
      then consider Z1 being set such that
A26:  [x,y] in Z1 & Z1 in X by TARSKI:def 4;
      Z1 in D()* by A5,A26;
      then reconsider p=Z1 as FinSequence of D() by FINSEQ_1:def 11;
      consider Z2 being set such that
A27:  [x,z] in Z2 & Z2 in X by A25,TARSKI:def 4;
      Z2 in D()* by A5,A27;
      then reconsider q=Z2 as FinSequence of D() by FINSEQ_1:def 11;
A28:  now
        assume len p <= len q;
        then p c= Z2 by A6,A26,A27;
        then [x,y] in q & [x,z] in q by A26,A27;
        hence thesis by FUNCT_1:def 1;
      end;
      now
        assume len q <= len p;
        then q c= Z1 by A6,A26,A27;
        then [x,y] in p & [x,z] in p by A26,A27;
        hence thesis by FUNCT_1:def 1;
      end;
      hence thesis by A28;
    end;
    consider h being Function such that
A29: for x,y holds [x,y] in h iff x in NAT & Z[x,y] from FUNCT_1:sch 1(A24);
    take h;
A30: for x,y holds [x,y] in h iff [x,y] in Y
    proof
      let x,y; thus
      [x,y] in h implies [x,y] in Y by A29; thus
      [x,y] in Y implies [x,y] in h
      proof
        assume
A31:    [x,y] in Y;
        then consider Z such that
A32:    [x,y] in Z & Z in X by TARSKI:def 4;
        Z in D()* by A5,A32;
        then reconsider p=Z as FinSequence of D() by FINSEQ_1:def 11;
        x in dom p by A32,RELAT_1:def 4;
        hence thesis by A29,A31;
      end;
    end;
    for x holds x in h iff x in Y
    proof
      let x;
      thus x in h implies x in Y
      proof
        assume
A33:    x in h;
        then ex y,z st [y,z] = x by RELAT_1:def 1;
        hence thesis by A30,A33;
      end;
      assume
A34:  x in Y;
      then consider Z such that
A35:  x in Z & Z in X by TARSKI:def 4;
      Z in D()* by A5,A35;
      then reconsider p=Z as FinSequence of D() by FINSEQ_1:def 11;
      x in p by A35;
      then ex y,z st [y,z] = x by RELAT_1:def 1;
      hence thesis by A30,A34;
    end;
    hence thesis by TARSKI:2;
  end;
  then consider g being Function such that
A36: g = Y;
A37: rng g c= D()
  proof
    for x st x in rng g holds x in D()
    proof
      let x;
      assume x in rng g;
      then consider y such that
A38:  y in dom g & x = g.y by FUNCT_1:def 5;
      [y,x] in Y by A36,A38,FUNCT_1:8;
      then consider Z such that
A39:  [y,x] in Z & Z in X by TARSKI:def 4;
      Z in D()* by A5,A39;
      then reconsider p=Z as FinSequence of D() by FINSEQ_1:def 11;
A40:  y in dom p by A39,FUNCT_1:8;
      x = p.y by A39,FUNCT_1:8;
      then
A41:  x in rng p by A40,FUNCT_1:def 5;
      rng p c= D() by FINSEQ_1:def 4;
      hence thesis by A41;
    end;
    hence thesis by TARSKI:def 3;
  end;
  then reconsider h = g as Function of dom g,D() by FUNCT_2:4;
A42: for n holds (n+1) in dom h
  proof
    defpred P[Element of NAT] means $1+1 in dom h;
A43: P[0]
    proof
A44:  <*A()*>.1 = A() by FINSEQ_1:def 8;
A45:  <*A()*> in D()* by FINSEQ_1:def 11;
      for n st n+2 <= len <*A()*> holds <*A()*>.(n+2) = f.[n,<*A()*>.(n+1)]
      proof
        let n;
        assume n+2 <= len <*A()*>;
        then n+2 <= 1 by FINSEQ_1:56;
        then n+(1+1) <= n+1 by NAT_1:12;
        hence thesis by XREAL_1:8;
      end;
      then <*A()*> in X by A4,A44,A45;
      then
A46:  {[1,A()]} in X by FINSEQ_1:52;
      [1,A()] in {[1,A()]} by TARSKI:def 1;
      then [1,A()] in h by A36,A46,TARSKI:def 4;
      hence thesis by FUNCT_1:8;
    end;
A47: for k st P[k] holds P[k+1]
    proof
      let k;
      assume k+1 in dom h;
      then [k+1,h.(k+1)] in Y by A36,FUNCT_1:8;
      then consider Z such that
A48:  [k+1,h.(k+1)] in Z & Z in X by TARSKI:def 4;
      Z in D()* by A5,A48;
      then reconsider Z as FinSequence of D() by FINSEQ_1:def 11;
A49:  k+1 = len Z implies thesis
      proof
        assume
A50:    k+1 = len Z;
        set p=Z^<*f.[k,Z.(k+1)]*>;
        len p = len Z + len <*f.[k,Z.(k+1)]*> by FINSEQ_1:35
          .=k+1+1 by A50,FINSEQ_1:56;
        then 1 <= k+1+1 & k+1+1 <= len p by NAT_1:12;
        then k+1+1 in Seg len p by FINSEQ_1:3;
        then k+1+1 in dom p by FINSEQ_1:def 3;
        then
A51:    [k+1+1,p.(k+1+1)] in p by FUNCT_1:8;
        p in X
        proof
          1 <= 1 & 1 <= len Z by A50,NAT_1:12;
          then 1 in Seg len Z by FINSEQ_1:3;
          then
A52:      1 in dom Z by FINSEQ_1:def 3;
          Z.1 = A()
          proof
            ex p being FinSequence st p in D()* & p.1 = A() &
            (for n st n+2 <= len p holds
            p.(n+2) = f.[n,p.(n+1)]) & Z=p by A4,A48;
            hence thesis;
          end;
          then
A53:      p.1 = A() by A52,FINSEQ_1:def 7;
A54:      p in D()*
          proof
            reconsider n=k as Element of (NAT qua non empty set);
            Z.(k+1) is Element of D()
            proof
              1 <= k + 1 by NAT_1:12;
              then k+1 in Seg len Z by A50,FINSEQ_1:3;
              then k+1 in dom Z by FINSEQ_1:def 3;
              then
A55:          Z.(k+1) in rng Z by FUNCT_1:def 5;
              rng Z c= D() by FINSEQ_1:def 4;
              hence thesis by A55;
            end;
            then reconsider z=Z.(k+1) as Element of D();
            p= Z^<*f.[n,z]*>;
            hence thesis by FINSEQ_1:def 11;
          end;
          for n st n+2 <= len p holds p.(n+2) = f.[n,p.(n+1)]
          proof
            let n;
            assume n+2 <= len p;
            then
A56:        n+2 <= len Z + len <*f.[k,Z.(k+1)]*> by FINSEQ_1:35;
            then
A57:        n+2 <= len Z + 1 by FINSEQ_1:57;
A58:        n+2 = len Z + 1 implies thesis
            proof
              assume
A59:          n+2 = len Z + 1;
              then
A60:          p.(n+2) =<*f.[k,Z.(k+1)]*>.(n+2-(n+2-1 )) by A56,FINSEQ_1:36
                .=f.[n,Z.(n+1)] by A50,A59,FINSEQ_1:57;
A61:          1 <= n + 1 by NAT_1:12;
              n+1+1 <= len Z + 1 by A56,FINSEQ_1:57;
              then n+1 <= len Z by XREAL_1:8;
              then n+1 in Seg len Z by A61,FINSEQ_1:3;
              then n+1 in dom Z by FINSEQ_1:def 3;
              hence thesis by A60,FINSEQ_1:def 7;
            end;
            n+2 <> len Z + 1 implies thesis
            proof
              assume
A62:          n+2 <> len Z + 1;
              then
A63:          n+2 <= len Z by A57,NAT_1:8;
              1 <= n+(1+1) by NAT_1:12;
              then n+2 in Seg len Z by A63,FINSEQ_1:3;
              then
A64:          n+2 in dom Z by FINSEQ_1:def 3;
              ex q st q in D()* & q.1 = A() & (for n st n+2 <= len q holds
              q.(n+2) = f.[n,q.(n+1)]) & Z = q by A4,A48;
              then Z.(n+2) = f.[n,Z.(n+1)] by A57,A62,NAT_1:8;
              then
A65:          p.(n+2) = f.[n,Z.(n+1)] by A64,FINSEQ_1:def 7;
A66:          1 <= n + 1 by NAT_1:12;
              n+1+1 <= len Z + 1 by A56,FINSEQ_1:57;
              then n+1 <= len Z by XREAL_1:8;
              then n+1 in Seg len Z by A66,FINSEQ_1:3;
              then n+1 in dom Z by FINSEQ_1:def 3;
              hence thesis by A65,FINSEQ_1:def 7;
            end;
            hence thesis by A58;
          end;
          hence thesis by A4,A53,A54;
        end;
        then [k+1+1,p.(k+1+1)] in h by A36,A51,TARSKI:def 4;
        hence thesis by FUNCT_1:8;
      end;
      k+1 <> len Z implies thesis
      proof
        assume
A67:    k+1 <> len Z;
        k+1 in dom Z by A48,FUNCT_1:8;
        then k+1 in Seg len Z by FINSEQ_1:def 3;
        then 1 <= k+1 & k+1 <= len Z by FINSEQ_1:3;
        then k+1 < len Z by A67,XXREAL_0:1;
        then
A68:    k+1+1 <= len Z by NAT_1:13;
        1 <= k+1+1 by NAT_1:12;
        then k+1+1 in Seg len Z by A68,FINSEQ_1:3;
        then k+1+1 in dom Z by FINSEQ_1:def 3;
        then [k+1+1,Z.(k+1+1)] in Z by FUNCT_1:8;
        then [k+1+1,Z.(k+1+1)] in h by A36,A48,TARSKI:def 4;
        hence thesis by FUNCT_1:8;
      end;
      hence thesis by A49;
    end;
    thus for k holds P[k] from NAT_1:sch 1(A43,A47);
  end;
  ex g being Function of NAT,D() st for n holds g.n = h.(n+1)
  proof
    ex g being Function st dom g = NAT & for n holds g.n = h.(n+1)
    proof
      defpred P[set,set] means ex n st n = $1 & $2=h.(n+1);
A70:  for x st x in NAT ex y st P[x,y]
      proof
        let x;
        assume x in NAT;
        then reconsider n=x as Element of NAT;
        take h.(n+1);
        take n;thus thesis;
      end;
      consider g being Function such that
A71:  dom g = NAT & for x st x in NAT holds P[x,g.x] from CLASSES1:sch 1
      (A70);
      take g;
      thus dom g = NAT by A71;
      thus for n holds g.n = h.(n+1)
      proof
        let n;
        ex m st m=n & g.n = h.(m+1) by A71;
        hence thesis;
      end;
    end;
    then consider g being Function such that
A72: dom g = NAT & for n holds g.n = h.(n+1);
    rng g c= D()
    proof
      let x;
      assume x in rng g;
      then consider y such that
A73:  y in dom g & x = g.y by FUNCT_1:def 5;
      reconsider k=y as Element of NAT by A72,A73;
      k+1 in dom h by A42;
      then
A74:  h.(k+1) in rng h by FUNCT_1:def 5;
      x=h.(k+1) by A72,A73;
      hence thesis by A37,A74;
    end;
    then reconsider g as Function of NAT,D() by A72,FUNCT_2:4;
    take g;
    thus thesis by A72;
  end;
  then consider g being Function of NAT,D() such that
A75: for n holds g.n = h.(n+1);
  take g;
A76: for n holds h.(n+2) = f.[n,h.(n+1)]
  proof
    let n;
    (n+1)+1 in dom h by A42;
    then [n+2,h.(n+2)] in h by FUNCT_1:def 4;
    then consider Z being set such that
A77: [n+2,h.(n+2)] in Z & Z in X by A36,TARSKI:def 4;
A78: ex p st p in D()* &
    (p.1=A() & for n st n+2 <= len p holds p.(n+2) = f.[n,p.(n+1)]) &
    Z=p by A4,A77;
    Z in D()* by A5,A77;
    then reconsider Z as FinSequence of D() by FINSEQ_1:def 11;
    n+2 in dom Z & h.(n+2) = Z.(n+2) by A77,FUNCT_1:8;
    then n+2 in Seg len Z by FINSEQ_1:def 3;
    then
A79: n+2 <= len Z by FINSEQ_1:3;
A80: 1 <= n+1 by NAT_1:12;
    n+1 <= n+2 by XREAL_1:9;
    then n+1 <= len Z by A79,XXREAL_0:2;
    then n+1 in Seg len Z by A80,FINSEQ_1:3;
    then n+1 in dom Z by FINSEQ_1:def 3;
    then [n+1,Z.(n+1)] in Z by FUNCT_1:8;
    then
A81: [n+1,Z.(n+1)] in h by A36,A77,TARSKI:def 4;
    thus h.(n+2) = Z.(n+2) by A77,FUNCT_1:8
      .= f.[n,Z.(n+1)] by A78,A79
      .= f.[n,h.(n+1)] by A81,FUNCT_1:8;
  end;
  thus g.0 = A()
  proof
A82: <*A()*> in D()* by FINSEQ_1:def 11;
A83: <*A()*>.1 = A() by FINSEQ_1:def 8;
    for n st n + 2 <= len <*A()*> holds <*A()*>.(n+2) = f.[n,<*A()*>.(n+1)]
    proof
      let n;
      assume n + 2 <= len <*A()*>;
      then n +1+1 <= 0 + 1 by FINSEQ_1:56;
      then n + 1 <= 0 by XREAL_1:8;
      then n + 1 <= 0 + n by NAT_1:12;
      hence thesis by XREAL_1:8;
    end;
    then <*A()*> in X by A4,A82,A83;
    then
A84: {[1,A()]} in X by FINSEQ_1:52;
    [1,A()] in {[1,A()]} by TARSKI:def 1;
    then [1,A()] in h by A36,A84,TARSKI:def 4;
    then A() = h.(0+1) by FUNCT_1:8
      .= g.0 by A75;
    hence thesis;
  end;
  let n be Element of NAT;
A85: P[n,g.n,f.(n,g.n)] by A3;
  h.(n+(1+1)) =f.(n,h.(n+1)) by A76;
  then P[n,g.n,h.(n+1+1)] by A75,A85;
  hence thesis by A75;
end;

scheme FinRecEx{A() -> set,N() -> Nat,P[set,set,set]}:
  ex p being FinSequence st len p = N() & (p.1 = A() or N() = 0) &
  for n st 1 <= n & n < N() holds P[n,p.n,p.(n+1)]
provided
A1: for n being Element of NAT st 1 <= n & n < N()
for x being set ex y being set st P[n,x,y]
proof
  defpred Q[Element of NAT,set,set] means ($1 < N()-1 implies P[$1+1,$2,$3]) &
  (not $1 < N()-1 implies $3=0);
A3: for n being Element of NAT for x being set ex y being set st Q[n,x,y]
  proof
    let n be Element of NAT,x be set;
    n < N()-1 implies thesis
    proof
      assume
A4:   n < N()-1;
      then
A5:   n+1 < N() by XREAL_1:22;
      consider y such that
A6:   P[n+1,x,y] by A1,A5,NAT_1:11;
      take y;
      thus n < N()-1 implies P[n+1,x,y] by A6;
      thus thesis by A4;
    end;
    hence thesis;
  end;
  consider f being Function such that
A9: dom f = NAT & f.0 = A() &
  for n being Element of NAT holds Q[n,f.n,f.(n+1)] from RecEx(A3);
  defpred Q[set,set] means for r being Real st r=$1 holds $2=f.(r-1);
A10: for x st x in REAL ex y st Q[x,y]
  proof
    let x;
    assume x in REAL;
    then reconsider r=x as Real;
    take f.(r-1);
    thus thesis;
  end;
  consider g being Function such that
A13: dom g = REAL &
  for x st x in REAL holds Q[x,g.x] from CLASSES1:sch 1(A10);
A14: dom (g|Seg N()) = Seg N() by A13,RELAT_1:91,XBOOLE_1:1;
  then reconsider p = g|Seg N() as FinSequence by FINSEQ_1:def 2;
  take p;
A15: for n being Element of NAT st n < N() holds p.(n+1) = f.n
  proof
    let n be Element of NAT such that
A16: n < N();
A17: 1 <= n+1 by NAT_1:11;
    n+1 <= N() by A16,NAT_1:13;
    then
A18: n+1 in Seg N() by A17,FINSEQ_1:3;
    g.(n+1) = f.(n+1-1) by A13
      .= f.n;
    hence thesis by A18,FUNCT_1:72;
  end;
   N() in NAT by ORDINAL1:def 13;
  hence len p = N() by A14,FINSEQ_1:def 3;
  thus p.1 = A() or N() = 0
  proof
    N() <> 0 implies thesis
    proof
      assume N() <> 0;
      then consider k being Nat such that
A19:  N() = k+1 by NAT_1:6;
      0 <= k by NAT_1:2;
      then 0 + 1 <= k +1 by XREAL_1:9;
      then 1 in Seg N() by A19,FINSEQ_1:3;
      then p.1 = g.1 by FUNCT_1:72
        .= f.(1-1) by A13
        .= A() by A9;
      hence thesis;
    end;
    hence thesis;
  end;
  let n;
  assume
A20: 1 <= n & n < N();
  then 0 <> n;
  then consider k being Nat such that
A21: n = k+1 by NAT_1:6;
  reconsider k as Element of NAT by ORDINAL1:def 13;
  k < N()-1 by A20,A21,XREAL_1:22;
  then P[k+1,f.k,f.(k+1)] by A9;
  then
A22: P[k+1,f.k,p.(k+1+1)] by A15,A20,A21;
  k <= k+1 by NAT_1:11;
  then k < N() by A20,A21,XXREAL_0:2;
  hence thesis by A15,A21,A22;
end;

scheme FinRecExD{D() -> non empty set,A() -> Element of D(),
  N() -> Nat, P[set,set,set]}:
  ex p being FinSequence of D() st len p = N() & (p.1 = A() or N() = 0) &
  for n st 1 <= n & n < N() holds P[n,p.n,p.(n+1)]
provided
A1: for n being Element of NAT st 1 <= n & n < N() for x being Element of D()
ex y being Element of D() st P[n,x,y]
proof
  consider 00 being Element of D();
  defpred Z[Element of NAT,set,set] means ($1 < N()-1 implies P[$1+1,$2,$3]) &
  (not $1 < N()-1 implies $3=00);
A2: for n being Element of NAT for x being Element of D()
  ex y being Element of D() st Z[n,x,y]
  proof
    let n be Element of NAT,x be Element of D();
    n < N()-1 implies thesis
    proof
      assume
A3:   n < N()-1;
      then 1 <= n+1 & n+1 < N() by NAT_1:11,XREAL_1:22;
      then consider y being Element of D() such that
A4:   P[n+1,x,y] by A1;
      take y;
      thus n < N()-1 implies P[n+1,x,y] by A4;
      thus thesis by A3;
    end;
    hence thesis;
  end;
  consider f being Function of NAT,D() such that
A5: f.0 = A() &
  for n being Element of NAT holds Z[n,f.n,f.(n+1)] from RecExD(A2);
  defpred P[set,set] means for r being Real st r = $1 holds $2 = f.(r-1);
A6: for x st x in REAL ex y st P[x,y]
  proof
    let x;
    assume x in REAL;
    then reconsider r=x as Real;
    take f.(r-1);
    thus thesis;
  end;
  consider g being Function such that
A9: dom g = REAL &
  for x st x in REAL holds P[x,g.x] from CLASSES1:sch 1(A6);
A10: dom (g|Seg N()) = Seg N() by A9,RELAT_1:91,XBOOLE_1:1;
  then reconsider p = g|Seg N() as FinSequence by FINSEQ_1:def 2;
  rng p c= D()
  proof
    let x;
    assume x in rng p;
    then consider y such that
A11: y in dom p & x = p.y by FUNCT_1:def 5;
    reconsider y as Element of NAT by A11;
A12: f.(y-1) in D()
    proof
      y <> 0 by A10,A11,FINSEQ_1:3;
      then consider k being Nat such that
A13:  y = k+1 by NAT_1:6;
      reconsider k as Element of NAT by ORDINAL1:def 13;
      f.k in D();
      hence thesis by A13;
    end;
    p.y = g.y by A11,FUNCT_1:70;
    hence thesis by A9,A11,A12;
  end;
  then reconsider p as FinSequence of D() by FINSEQ_1:def 4;
  take p;
A14: for n being Element of NAT st n < N() holds p.(n+1) = f.n
  proof
    let n be Element of NAT such that
A15: n < N();
A16: 1 <= n+1 by NAT_1:11;
    n+1 <= N() by A15,NAT_1:13;
    then
A17: n+1 in Seg N() by A16,FINSEQ_1:3;
    g.(n+1) = f.(n+1-1) by A9
      .= f.n;
    hence thesis by A17,FUNCT_1:72;
  end;
   N() in NAT by ORDINAL1:def 13;
  hence len p = N() by A10,FINSEQ_1:def 3;
  thus p.1 = A() or N() = 0
  proof
    N() <> 0 implies thesis
    proof
      assume N() <> 0;
      then consider k being Nat such that
A18:  N() = k+1 by NAT_1:6;
      0 <= k by NAT_1:2;
      then 0 + 1 <= k +1 by XREAL_1:9;
      then 1 in Seg N() by A18,FINSEQ_1:3;
      then p.1 = g.1 by FUNCT_1:72
        .= f.(1-1) by A9
        .= A() by A5;
      hence thesis;
    end;
    hence thesis;
  end;
  let n;
  assume
A19: 1 <= n & n < N();
  then 0 <> n;
  then consider k being Nat such that
A20: n = k+1 by NAT_1:6;
  reconsider k as Element of NAT by ORDINAL1:def 13;
  k < N() - 1 by A19,A20,XREAL_1:22;
  then P[k+1,f.k,f.(k+1)] by A5;
  then
A21: P[k+1,f.k,p.(k+1+1)] by A14,A19,A20;
  k <= k+1 by NAT_1:11;
  then k < N() by A19,A20,XXREAL_0:2;
  hence thesis by A14,A20,A21;
end;

scheme SeqBinOpEx{S() -> FinSequence,P[set,set,set]}:
  ex x st ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S() holds P[S().(k+1),p.k,p.(k+1)]
provided
A1: for k,x st 1 <= k & k < len S() ex y st P[S().(k+1),x,y]
proof
  defpred Q[Element of NAT,set,set] means P[S().($1+1),$2,$3];
A3: for k st 1 <= k & k < len S() for x ex y st Q[k,x,y] by A1;
  consider p such that
A5: len p = len S() & (p.1 = S().1 or len S() = 0) &
  for k st 1 <= k & k < len S() holds Q[k,p.k,p.(k+1)] from FinRecEx(A3);
A6: len S() <> 0 implies thesis
  proof
    assume
A7: len S() <> 0;
    take p.(len p),p;
    thus p.(len p) = p.(len p) & len p = len S() & p.1 = S().1 by A5,A7;
    let k;
    assume 1 <= k & k < len S();
    hence thesis by A5;
  end;
  len S() = 0 implies thesis
  proof
    assume
A8: len S() = 0;
    take S().0,S();
    thus S().0 = S().(len S()) & len S() = len S() & S().1 = S(). 1 by A8;
    thus thesis by A8,XXREAL_0:2;
  end;
  hence thesis by A6;
end;

scheme LambdaSeqBinOpEx{S() -> FinSequence,F(set,set) -> set}:
  ex x st ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S() holds p.(k+1) = F(S().(k+1),p.k)
proof
  defpred P[set,set,set] means $3 = F($1,$2);
A1: for k,x st 1 <= k & k < len S() ex y st P[S().(k+1),x,y];
  consider x such that
A3: ex p st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S()
  holds P[S().(k+1),p.k,p.(k+1)] from SeqBinOpEx(A1);
  take x;
  consider p such that
A4: x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S()
  holds p.(k+1) = F(S().(k+1),p.k) by A3;
  take p;
  thus x = p.(len p) & len p = len S() & p.1 = S().1 by A4;
  let k;
  assume 1 <= k & k < len S();
  hence thesis by A4;
end;

::     Schemes of uniqueness

scheme FinRecUn{A() -> set,N() -> Nat,
  F, G() -> FinSequence, P[set,set,set]}: F() = G()
provided
A1: for n st 1 <= n & n < N()
for x,y1,y2 being set st P[n,x,y1] & P[n,x,y2] holds y1 = y2
and
A2: len F() = N() & (F().1 = A() or N() = 0) &
for n st 1 <= n & n < N() holds P[n,F().n,F().(n+1)]
and
A3: len G() = N() & (G().1 = A() or N() = 0) &
for n st 1 <= n & n < N() holds P[n,G().n,G().(n+1)]
proof
A4: dom F() = Seg len G() by A2,A3,FINSEQ_1:def 3
    .= dom G() by FINSEQ_1:def 3;
  assume F() <> G();
  then consider x such that
A5: x in dom F() & F().x <> G().x by A4,FUNCT_1:9;
A6: x in Seg len F() by A5,FINSEQ_1:def 3;
  reconsider x as Element of NAT by A5;
  defpred P[Nat] means 1 <= $1 & $1 <= N() & F().$1 <> G().$1;
  1 <= x & x <= N() by A2,A6,FINSEQ_1:3;
  then
A7: ex n be Nat st P[n] by A5;
  consider n be Nat such that
A8: P[n]& for k be Nat st P[k] holds n <= k from NAT_1:sch 5(A7);
  n <> 1 by A2,A3,A8;
  then
A9: 1 < n by A8,XXREAL_0:1;
  0 <> n by A8;
  then consider k being Nat such that
A10: n = k+1 by NAT_1:6;
  reconsider k as Element of NAT by ORDINAL1:def 13;
A11: 1 <= k by A9,A10,NAT_1:13;
  k < n by A10,XREAL_1:31;
  then
A12: k < N() by A8,XXREAL_0:2;
  n > k by A10,NAT_1:13;
  then F().k = G().k by A8,A11,A12;
  then P[k,F().k,F().(k+1)] & P[k,F().k,G().(k+1)] by A2,A3,A11,A12;
  hence contradiction by A1,A8,A10,A11,A12;
end;

scheme
  FinRecUnD{D() -> non empty set, A() -> Element of D(), N() -> Nat,
  F, G() -> FinSequence of D(), P[set,set,set]}: F() = G()
provided
A1: for n st 1 <= n & n < N() for x,y1,y2 being Element of D()
st P[n,x,y1] & P[n,x,y2] holds y1 = y2
and
A2: len F() = N() & (F().1 = A() or N() = 0) &
for n st 1 <= n & n < N() holds P[n,F().n,F().(n+1)]
and
A3: len G() = N() & (G().1 = A() or N() = 0) &
for n st 1 <= n & n < N() holds P[n,G().n,G().(n+1)]
proof
A4: dom F() = Seg len G() by A2,A3,FINSEQ_1:def 3
    .= dom G() by FINSEQ_1:def 3;
  assume F() <> G();
  then consider x such that
A5: x in dom F() & F().x <> G().x by A4,FUNCT_1:9;
A6: x in Seg len F() by A5,FINSEQ_1:def 3;
  reconsider x as Element of NAT by A5;
  defpred P[Nat] means 1 <= $1 & $1 <= N() & F().$1 <> G().$1;
  1 <= x & x <= N() by A2,A6,FINSEQ_1:3;
  then
A7: ex n be Nat st P[n] by A5;
  consider n be Nat such that
A8: P[n] & for k be Nat st P[k] holds n <= k from NAT_1:sch 5(A7);
  n <> 1 by A2,A3,A8;
  then
A9: 1 < n by A8,XXREAL_0:1;
  0 <> n by A8;
  then consider k being Nat such that
A10: n = k+1 by NAT_1:6;
  reconsider k as Element of NAT by ORDINAL1:def 13;
A11: 1 <= k by A9,A10,NAT_1:13;
  k < n by A10,XREAL_1:31;
  then
A12: k < N() by A8,XXREAL_0:2;
  n > k by A10,NAT_1:13;
  then
A13: F().k = G().k by A8,A11,A12;
  reconsider Fk = F().k as Element of D() by A2,A11,A12,Lm1;
  reconsider Fk1 = F().(k+1) as Element of D() by A2,A8,A10,Lm1;
  reconsider Gk1 = G().(k+1) as Element of D() by A3,A8,A10,Lm1;
  P[k,Fk,Fk1] & P[k,Fk,Gk1] by A2,A3,A11,A12,A13;
  hence contradiction by A1,A8,A10,A11,A12;
end;

scheme SeqBinOpUn{S() -> FinSequence,P[set,set,set],x, y() -> set}: x() = y()
provided
A1: for k,x,y1,y2,z st 1 <= k & k < len S() & z = S().(k+1) &
P[z,x,y1] & P[z,x,y2] holds y1 = y2
and
A2: ex p being FinSequence st x() = p.(len p) & len p = len S() & p.1 = S().1 &
for k st 1 <= k & k < len S() holds P[S().(k+1),p.k,p.(k+1)]
and
A3: ex p being FinSequence st y() = p.(len p) & len p = len S() & p.1 = S().1 &
for k st 1 <= k & k < len S() holds P[S().(k+1),p.k,p.(k+1)]
proof
  defpred Q[Element of NAT,set,set] means P[S().($1+1),$2,$3];
A4: for k st 1 <= k & k < len S() for x,y1,y2 st
  Q[k,x,y1] & Q[k,x,y2] holds y1 = y2 by A1;
  consider p such that
A5: x() = p.(len p) & len p = len S() & p.1 = S().1 &
  for k st 1 <= k & k < len S() holds Q[k,p.k,p.(k+1)] by A2;
  consider q such that
A6: y() = q.(len q) & len q = len S() & q.1 = S().1 &
  for k st 1 <= k & k < len S() holds Q[k,q.k,q.(k+1)] by A3;
A7: len p = len S() & (p.1 = S().1 or len S() = 0) &
  for k st 1 <= k & k < len S() holds Q[k,p.k,p.(k+1)] by A5;
A8: len q = len S() & (q.1 = S().1 or len S() = 0) &
  for k st 1 <= k & k < len S() holds Q[k,q.k,q.(k+1)] by A6;
  p = q from FinRecUn(A4,A7,A8);
  hence thesis by A5,A6;
end;

scheme LambdaSeqBinOpUn{S() -> FinSequence, F(set,set) -> set, x, y() -> set}:
  x() = y()
provided
A1: ex p being FinSequence st x() = p.(len p) & len p = len S() & p.1 = S().1 &
for k st 1 <= k & k < len S() holds p.(k+1) = F(S().(k+1),p.k)
and
A2: ex p being FinSequence st y() = p.(len p) & len p = len S() & p.1 = S().1 &
for k st 1 <= k & k < len S() holds p.(k+1) = F(S().(k+1),p.k)
proof
  defpred P[set,set,set] means $3 = F($1,$2);
A3: for k,x,y1,y2,z st 1 <= k & k < len S() & z = S().(k+1) &
  P[z,x,y1] & P[z,x,y2] holds y1 = y2;
A4: ex p st x() = p.(len p) & len p = len S() & p.1 = S().1 &
  for k st 1 <= k & k < len S() holds P[S().(k+1),p.k,p.(k+1)] by A1;
A5: ex p st y() = p.(len p) & len p = len S() & p.1 = S().1 &
  for k st 1 <= k & k < len S() holds P[S().(k+1),p.k,p.(k+1)] by A2;
  thus x() = y() from SeqBinOpUn(A3,A4,A5);
end;

::   Schemes of definitness

scheme DefRec{A() -> set,n() -> Nat,P[set,set,set]}:
  (ex y being set st ex f being Function st
  y = f.n() & dom f = NAT & f.0 = A() & for n holds P[n,f.n,f.(n+1)]) &
  for y1,y2 being set st (ex f being Function st
  y1 = f.n() & dom f = NAT & f.0 = A() & for n holds P[n,f.n,f.(n+1)]) &
  (ex f being Function st
  y2 = f.n() & dom f = NAT & f.0 = A() & for n holds P[n,f.n,f.(n+1)])
  holds y1 = y2
provided
A1: for n,x ex y st P[n,x,y]
and
A2: for n,x,y1,y2 st P[n,x,y1] & P[n,x,y2] holds y1 = y2
proof
A3: for n,x ex y st P[n,x,y] by A1;
A4: for n being Nat,x,y1,y2 st P[n,x,y1] & P[n,x,y2] holds y1 = y2
 proof let n be Nat;
  n in NAT by ORDINAL1:def 13;
  hence thesis by A2;
 end;
  consider f being Function such that
A5: dom f = NAT & f.0 = A() &
  for n being Element of NAT holds P[n,f.n,f.(n+1)] from RecEx(A3);
  thus
  (ex y being set st ex f being Function st
  y = f.n() & dom f = NAT & f.0 = A() & for n holds P[n,f.n,f.(n+1)])
  proof
    take f.n(),f;
    thus thesis by A5;
  end;
  let y1,y2 be set;
  given f1 being Function such that
A6: y1 = f1.n() and
B6: dom f1 = NAT and
C6: f1.0 = A() and
D6: for n holds P[n,f1.n,f1.(n+1)];
  given f2 being Function such that
A7: y2 = f2.n() and
B7: dom f2 = NAT and
C7: f2.0 = A() and
D7: for n holds P[n,f2.n,f2.(n+1)];
A8: for n being Nat holds P[n,f1.n,f1.(n+1)]
 proof let n be Nat;
  n in NAT by ORDINAL1:def 13;
  hence thesis by D6;
 end;
A9: for n being Nat holds P[n,f2.n,f2.(n+1)]
 proof let n be Nat;
  n in NAT by ORDINAL1:def 13;
  hence thesis by D7;
 end;
  f1 = f2 from NAT_1:sch 13(B6,C6,A8,B7,C7,A9,A4);
  hence thesis by A6,A7;
end;

scheme LambdaDefRec{A() -> set,n() -> Nat,RecFun(set,set) -> set}:
  (ex y being set st ex f being Function st
  y = f.n() & dom f = NAT & f.0 = A() & for n holds f.(n+1) = RecFun(n,f.n)) &
  for y1,y2 being set st (ex f being Function st
  y1 = f.n() & dom f = NAT & f.0 = A() &
  for n holds f.(n+1) = RecFun(n,f.n)) & (ex f being Function st
  y2 = f.n() & dom f = NAT & f.0 = A() & for n holds f.(n+1) = RecFun(n,f.n))
  holds y1 = y2
proof
  defpred P[set,set,set] means for z st z = $2 holds $3 = RecFun($1,z);
A1: for n,x ex y st P[n,x,y]
  proof
    let n,x;
    take RecFun(n,x);
    let z;
    assume z = x;
    hence thesis;
  end;
A2: for n,x,y1,y2 st P[n,x,y1] & P[n,x,y2] holds y1 = y2
  proof
    let n,x,y1,y2;
    assume
A3: (for z st z = x holds y1 = RecFun(n,z)) &
    for z st z = x holds y2 = RecFun(n,z);
    hence y1 = RecFun(n,x)
      .= y2 by A3;
  end;
A4: (ex y being set st ex f being Function st
  y = f.n() & dom f = NAT & f.0 = A() & for n holds P[n,f.n,f.(n+1)] ) &
  for y1,y2 being set st (ex f being Function st
  y1 = f.n() & dom f = NAT & f.0 = A() & for n holds P[n,f.n,f.(n+1)] ) &
  (ex f being Function st y2 = f.n() & dom f = NAT & f.0 = A() &
  for n holds P[n,f.n,f.(n+1)] ) holds y1 = y2 from DefRec(A1,A2);
  then consider y being set, f being Function such that
A5: y = f.n() & dom f = NAT & f.0 = A() and
A6: for n holds P[n,f.n,f.(n+1)];
  thus ex y being set st ex f being Function st
  y = f.n() & dom f = NAT & f.0 = A() & for n holds f.(n+1) = RecFun(n,f.n)
  proof
    take y,f;
    thus thesis by A5,A6;
  end;
  let y1,y2 being set;
  given f1 being Function such that
A7: y1 = f1.n() & dom f1 = NAT & f1.0 = A() &
  for n holds f1.(n+1) = RecFun(n,f1.n);
  given f2 being Function such that
A8: y2 = f2.n() & dom f2 = NAT & f2.0 = A() &
  for n holds f2.(n+1) = RecFun(n,f2.n);
A9: for n holds P[n,f1.n,f1.(n+1)] by A7;
  for n holds P[n,f2.n,f2.(n+1)] by A8;
  hence thesis by A4,A7,A8,A9;
end;

scheme DefRecD{D() -> non empty set,A() -> (Element of D()),
  n() -> Nat,P[set,set,set]}:
  (ex y being Element of D() st ex f being Function of NAT,D() st
  y = f.n() & f.0 = A() & for n holds P[n,f.n,f.(n+1)]) &
  for y1,y2 being Element of D() st (ex f being Function of NAT,D() st
  y1 = f.n() & f.0 = A() & for n holds P[n,f.n,f.(n+1)]) &
  (ex f being Function of NAT,D() st
  y2 = f.n() & f.0 = A() & for n holds P[n,f.n,f.(n+1)]) holds y1 = y2
provided
A1: for n being Element of NAT,x being Element of D()
ex y being Element of D() st P[n,x,y]
and
A2: for n being Element of NAT, x,y1,y2 being Element of D()
st P[n,x,y1] & P[n,x,y2] holds y1 = y2
proof
A3: for n being Element of NAT,x being Element of D()
  ex y being Element of D() st P[n,x,y] by A1;
  consider f being Function of NAT,D() such that
A4: f.0 = A() &
  for n being Element of NAT holds P[n,f.n,f.(n+1)] from RecExD(A3); thus
  (ex y being Element of D() st ex f being Function of NAT,D() st
  y = f.n() & f.0 = A() & for n holds P[n,f.n,f.(n+1)])
  proof reconsider n = n() as Element of NAT by ORDINAL1:def 13;
    take f.n,f;
    thus thesis by A4;
  end;
A5: for n being Nat, x,y1,y2 being Element of D()
  st P[n,x,y1] & P[n,x,y2] holds y1 = y2
  proof let n be Nat;
   n in NAT by ORDINAL1:def 13;
   hence thesis by A2;
  end;
  let y1,y2 be Element of D();
  given f1 being Function of NAT,D() such that
A6: y1 = f1.n() & f1.0 = A() & for n holds P[n,f1.n,f1.(n+1)];
  given f2 being Function of NAT,D() such that
A7: y2 = f2.n() & f2.0 = A() & for n holds P[n,f2.n,f2.(n+1)];
A8: f1.0 = A() by A6;
B8: for n being Nat holds P[n,f1.n,f1.(n+1)]
  proof let n be Nat;
   n in NAT by ORDINAL1:def 13;
   hence thesis by A6;
  end;
A9: f2.0 = A() by A7;
B9: for n being Nat holds P[n,f2.n,f2.(n+1)]
  proof let n be Nat;
   n in NAT by ORDINAL1:def 13;
   hence thesis by A7;
  end;
  f1 = f2 from NAT_1:sch 14(A8,B8,A9,B9,A5);
  hence thesis by A6,A7;
end;

scheme LambdaDefRecD{D() -> non empty set, A() -> Element of D(),
  n() -> Nat, RecFun(set,set) -> Element of D()}:
  (ex y being Element of D() st ex f being Function of NAT,D() st
  y = f.n() & f.0 = A() &
  for n being Nat holds f.(n+1) = RecFun(n,f.n)) &
  for y1,y2 being Element of D() st (ex f being Function of NAT,D() st
  y1 = f.n() & f.0 = A() &
  for n being Nat holds f.(n+1) = RecFun(n,f.n)) &
  (ex f being Function of NAT,D() st y2 = f.n() & f.0 = A() &
  for n being Nat holds f.(n+1) = RecFun(n,f.n)) holds y1 = y2
proof
  defpred Q[set,set,set] means for z being Element of D() st
  z=$2 holds $3 = RecFun($1,z);
A1: for n being Element of NAT,x being Element of D()
  ex y being Element of D() st Q[n,x,y]
  proof
    let n be Element of NAT,x be Element of D();
    take RecFun(n,x);
    let z be Element of D();
    assume z = x;
    hence thesis;
  end;
A2: for n being Element of NAT,x,y1,y2 being Element of D()
  st Q[n,x,y1] & Q[n,x,y2] holds y1 = y2
  proof
    let n be Element of NAT,x,y1,y2 be Element of D();
    assume
A3: (for z being Element of D() st z = x holds y1 = RecFun(n,z)) &
    for z being Element of D() st z = x holds y2 = RecFun(n,z);
    hence y1 = RecFun(n,x)
      .= y2 by A3;
  end;
A4: (ex y being Element of D() st ex f being Function of NAT,D() st
  y = f.n() & f.0 = A() & for n being Element of NAT holds Q[n,f.n,f.(n+1)]) &
  for y1,y2 being Element of D() st (ex f being Function of NAT,D() st
  y1 = f.n() & f.0 = A() &
  for n being Element of NAT holds Q[n,f.n,f.(n+1)]) &
  (ex f being Function of NAT,D() st y2 = f.n() & f.0 = A() &
  for n being Element of NAT holds Q[n,f.n,f.(n+1)])
  holds y1 = y2 from DefRecD(A1,A2);
  then consider y being Element of D(), f being Function of NAT,D() such that
A5: y = f.n() & f.0 = A() and
A6: for n being Element of NAT holds Q[n,f.n,f.(n+1)];
  thus ex y being Element of D() st ex f being Function of NAT,D() st
  y = f.n() & f.0 = A() &
  for n being Nat holds f.(n+1) = RecFun(n,f.n)
  proof
    take y,f;
    thus y = f.n() & f.0 = A() by A5;
    let n be Nat;
     reconsider n as Element of NAT by ORDINAL1:def 13;
     Q[n,f.n,f.(n+1)] by A6;
    hence thesis;
  end;
  let y1,y2 being Element of D();
  given f being Function of NAT,D() such that
A7: y1 = f.n() & f.0 = A() &
  for n being Nat holds f.(n+1) = RecFun(n,f.n);
  given f2 being Function of NAT,D() such that
A8: y2 = f2.n() & f2.0 = A() &
  for n being Nat holds f2.(n+1) = RecFun(n,f2.n);
A9: for n being Element of NAT holds Q[n,f.n,f.(n+1)] by A7;
  for n being Element of NAT holds Q[n,f2.n,f2.(n+1)] by A8;
  hence thesis by A4,A7,A8,A9;
end;

scheme SeqBinOpDef{S() -> FinSequence,P[set,set,set]}:
  (ex x st ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S() holds
  P[S().(k+1),p.k,p.(k+1)]) & for x,y st
  (ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S() holds
  P[S().(k+1),p.k,p.(k+1)]) &
  (ex p being FinSequence st y = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S() holds P[S().(k+1),p.k,p.(k+1)])
  holds x = y
provided
A1: for k,y st 1 <= k & k < len S() ex z st P[S().(k+1),y,z]
and
A2: for k,x,y1,y2,z st 1 <= k & k < len S() & z = S().(k+1) &
P[z,x,y1] & P[z,x,y2] holds y1 = y2
proof
A3: for k,y st 1 <= k & k < len S() ex z st P[S().(k+1),y,z] by A1;
A4: for k,x,y1,y2,z st 1 <= k & k < len S() & z = S().(k+1) &
  P[z,x,y1] & P[z,x,y2] holds y1 = y2 by A2; thus
  (ex x st ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S() holds
  P[S().(k+1),p.k,p.(k+1)]) from SeqBinOpEx(A3);
  let x,y;
  assume
A5: ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S() holds P[S().(k+1),p.k,p.(k+1)];
  assume
A6: ex p being FinSequence st y = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S() holds P[S().(k+1),p.k,p.(k+1)];
  thus x = y from SeqBinOpUn(A4,A5,A6);
end;

scheme LambdaSeqBinOpDef{S() -> FinSequence,F(set,set) -> set}:
  (ex x st ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S()
  holds p.(k+1) = F(S().(k+1),p.k)) & for x,y st
  (ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S()
  holds p.(k+1) = F(S().(k+1),p.k)) &
  (ex p being FinSequence st y = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S()
  holds p.(k+1) = F(S().(k+1),p.k)) holds x = y
proof thus
  (ex x st ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S()
  holds p.(k+1) = F(S().(k+1),p.k)) from LambdaSeqBinOpEx;
  let x,y;
  assume
A1: ex p being FinSequence st x = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S()
  holds p.(k+1) = F(S().(k+1),p.k);
  assume
A2: ex p being FinSequence st y = p.(len p) & len p = len S() &
  p.1 = S().1 & for k st 1 <= k & k < len S()
  holds p.(k+1) = F(S().(k+1),p.k);
  thus x = y from LambdaSeqBinOpUn(A1,A2);
end;

