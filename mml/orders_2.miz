:: Kuratowski - Zorn Lemma
::  by Wojciech A. Trybulec and Grzegorz Bancerek
::
:: Received September 19, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FUNCT_1, BOOLE, TARSKI, RELAT_1, RELAT_2, WELLORD1, SUBSET_1,
      ORDERS_1, PARTFUN1, ORDERS_2;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, RELAT_2, FUNCT_1,
      RELSET_1, PARTFUN1, WELLORD1, STRUCT_0, PRE_TOPC, ORDERS_1;
 constructors RELAT_2, MCART_1, WELLORD1, PRE_TOPC, PARTFUN1, MEMBERED,
      XBOOLE_0, ORDERS_1;
 registrations RELSET_1, STRUCT_0, PRE_TOPC, SUBSET_1, XBOOLE_0, ORDERS_1;
 requirements BOOLE, SUBSET;
 definitions RELAT_2, TARSKI, WELLORD1, STRUCT_0, XBOOLE_0, ORDERS_1;
 theorems RELAT_1, RELAT_2, TARSKI, WELLORD1, ZFMISC_1, STRUCT_0, PRE_TOPC,
      XBOOLE_0, XBOOLE_1, PARTFUN1, ORDERS_1;
 schemes XBOOLE_0;

begin :: Original ORDERS_1

reserve X,Y,x,y for set;

canceled 7;

::
::  Partially ordered sets.
::

definition
  struct(1-sorted) RelStr (#
    carrier -> set,
    InternalRel -> Relation of the carrier
  #);
end;

registration
 let X be non empty set;
 let R be Relation of X;
 cluster RelStr(#X,R#) -> non empty;
 coherence proof thus the carrier of RelStr(#X,R#) is non empty; end;
end;

definition let A be RelStr;
 canceled 3;
 attr A is reflexive means :Def4:
  the InternalRel of A is_reflexive_in the carrier of A;
 attr A is transitive means :Def5:
  the InternalRel of A is_transitive_in the carrier of A;
 attr A is antisymmetric means :Def6:
  the InternalRel of A is_antisymmetric_in the carrier of A;
end;

registration
 cluster non empty reflexive transitive antisymmetric strict RelStr;
 existence
  proof consider A being non empty set;
    consider R being Order of A;
   take L = RelStr(#A,R#);
   thus the carrier of L is non empty;
A1:  field R = the carrier of L by ORDERS_1:97;
   hence the InternalRel of L is_reflexive_in the carrier of L
                     by RELAT_2:def 9;
   thus the InternalRel of L is_transitive_in the carrier of L
                                by A1,RELAT_2:def 16;
   thus the InternalRel of L is_antisymmetric_in the carrier of L
                                                by A1,RELAT_2:def 12;
   thus thesis;
  end;
end;

definition
  mode Poset is reflexive transitive antisymmetric RelStr;
end;

registration let A be Poset;
 cluster the InternalRel of A -> total reflexive antisymmetric transitive;
 coherence
  proof set R = the InternalRel of A;
A1:  R is_reflexive_in the carrier of A by Def4;
then A2:  field R = the carrier of A by ORDERS_1:98;
      dom R = the carrier of A by A1,ORDERS_1:98;
   hence the InternalRel of A is total by PARTFUN1:def 4;
      the InternalRel of A is_reflexive_in the carrier of A by Def4;
   hence the InternalRel of A is reflexive by A2,RELAT_2:def 9;
      the InternalRel of A is_antisymmetric_in the carrier of A by Def6;
   hence the InternalRel of A is antisymmetric by A2,RELAT_2:def 12;
      the InternalRel of A is_transitive_in the carrier of A by Def5;
   hence the InternalRel of A is transitive by A2,RELAT_2:def 16;
  end;
end;

registration
 let X be set;
 let O be Order of X;
 cluster RelStr(#X,O#) -> reflexive transitive antisymmetric;
 coherence
  proof set A = RelStr(#X,O#);
      field O = X by ORDERS_1:97;
   hence the InternalRel of A is_reflexive_in the carrier of A &
    the InternalRel of A is_transitive_in the carrier of A &
    the InternalRel of A is_antisymmetric_in the carrier of A
          by RELAT_2:def 9,def 12,def 16;
  end;
end;

 reserve A for non empty Poset;
 reserve a,a1,a2,a3,b,c for Element of A;
 reserve S,T for Subset of A;

Lm1: x in S implies x is Element of A;

definition let A be RelStr; let a1,a2 be Element of A;
 canceled 2;

 pred a1 <= a2 means
  :Def9: [a1,a2] in the InternalRel of A;
end;

notation let A be RelStr; let a1,a2 be Element of A;
 synonym a2 >= a1 for a1 <= a2;
end;

definition let A be RelStr; let a1,a2 be Element of A;
 pred a1 < a2 means
  :Def10: a1 <= a2 & a1 <> a2;
 irreflexivity;
end;

notation let A be RelStr; let a1,a2 be Element of A;
 synonym a2 > a1 for a1 < a2;
end;

canceled 16;

theorem Th24:
 for A being reflexive non empty RelStr, a being Element of A
  holds a <= a
  proof let A be reflexive non empty RelStr, a be Element of A;
        the InternalRel of A is_reflexive_in the carrier of A by Def4;
    then [a,a] in the InternalRel of A by RELAT_2:def 1;
   hence thesis by Def9;
  end;

definition let A be reflexive non empty RelStr;
           let a1,a2 be Element of A;
 redefine pred a1 <= a2;
 reflexivity by Th24;
end;

theorem Th25:
 for A being antisymmetric RelStr,
 a1,a2 being Element of A
  st a1 <= a2 & a2 <= a1 holds a1 = a2
  proof let A be antisymmetric RelStr,
   a1,a2 be Element of A; assume
A1:  [a1,a2] in the InternalRel of A & [a2,a1] in the InternalRel of A;
    then a1 in the carrier of A & a2 in the carrier of A &
    the InternalRel of A is_antisymmetric_in the carrier of A
     by Def6,ZFMISC_1:106;
   hence thesis by A1,RELAT_2:def 4;
  end;

theorem Th26:
 for A being transitive RelStr,
   a1,a2,a3 being Element of A holds
 a1 <= a2 & a2 <= a3 implies a1 <= a3
  proof let A be transitive RelStr,
  a1,a2,a3 be Element of A; assume
A1:  [a1,a2] in the InternalRel of A & [a2,a3] in the InternalRel of A;
    then a1 in the carrier of A & a2 in the carrier of A & a3 in the carrier
of A &
    the InternalRel of A is_transitive_in the carrier of A
     by Def5,ZFMISC_1:106;
   hence [a1,a3] in the InternalRel of A by A1,RELAT_2:def 8;
  end;

canceled;

theorem Th28:
 for A being antisymmetric RelStr,
  a1,a2 being Element of A holds
 not(a1 < a2 & a2 < a1)
  proof let A be antisymmetric RelStr, a1,a2 be Element of A;
   assume a1 < a2 & a2 < a1;
    then a1 <= a2 & a1 <> a2 & a2 <= a1 by Def10;
   hence thesis by Th25;
  end;

theorem Th29:
 for A being transitive antisymmetric RelStr
 for a1,a2,a3 being Element of A holds
 a1 < a2 & a2 < a3 implies a1 < a3
  proof let A be transitive antisymmetric RelStr;
   let a1,a2,a3 be Element of A; assume
A1:  a1 < a2 & a2 < a3;
    then a1 <= a2 & a2 <= a3 by Def10;
   hence a1 <= a3 by Th26;
   thus thesis by A1,Th28;
  end;

theorem Th30:
 for A being antisymmetric RelStr,
  a1,a2 being Element of A holds
 a1 <= a2 implies not a2 < a1
  proof let A be antisymmetric RelStr, a1,a2 be Element of A;
    assume that A1: a1 <= a2 and A2: a2 < a1;
     a2 <= a1 by A2,Def10;
     hence thesis by A1,A2,Th25;
  end;

canceled;

theorem Th32:
  for A being transitive antisymmetric RelStr
 for a1,a2,a3 being Element of A holds
 (a1 < a2 & a2 <= a3) or (a1 <= a2 & a2 < a3) implies a1 < a3
  proof let A be transitive antisymmetric RelStr;
   let a1,a2,a3 be Element of A; assume
    A1: (a1 < a2 & a2 <= a3) or (a1 <= a2 & a2 < a3);
    A2: now assume A3: a1 < a2 & a2 <= a3;
      then A4: a1 <= a2 & a2 <= a3 by Def10;
      then A5: a1 <= a3 by Th26;
      a1 <> a3 by A3,A4,Th25;
     hence thesis by A5,Def10;
    end;
     now assume A6: a1 <= a2 & a2 < a3;
      then A7: a1 <= a2 & a2 <= a3 by Def10;
      then A8: a1 <= a3 by Th26;
      a1 <> a3 by A6,A7,Th25;
     hence thesis by A8,Def10;
    end;
   hence thesis by A1,A2;
  end;

::
::  Chains.
::

definition let A be RelStr;
 let IT be Subset of A;
 attr IT is strongly_connected means
  :Def11: the InternalRel of A is_strongly_connected_in IT;
end;

registration let A be RelStr;
 cluster {}A -> strongly_connected;
 coherence proof let x1,x2 be set; thus thesis; end;
end;

registration let A be RelStr;
 cluster strongly_connected Subset of A;
 existence proof take {}A; thus thesis; end;
end;

definition let A be RelStr;
 mode Chain of A is strongly_connected Subset of A;
end;

canceled 2;

theorem Th35:
 for A being non empty reflexive RelStr
 for a being Element of A holds {a} is Chain of A
  proof let A be non empty reflexive RelStr, a be Element of A;
   A1: a in the carrier of A &
        the InternalRel of A is_reflexive_in the carrier of A by Def4;
    {a} is strongly_connected
    proof let x1,x2 be set;
     assume x1 in {a} & x2 in {a};
      then x1 = a & x2 = a by TARSKI:def 1;
     hence thesis by A1,RELAT_2:def 1;
    end;
   hence thesis;
  end;

theorem Th36:
 for A being non empty reflexive RelStr, a1,a2 being Element of A holds
 {a1,a2} is Chain of A iff a1 <= a2 or a2 <= a1
  proof let A be non empty reflexive RelStr, a1,a2 be Element of A;
   thus {a1,a2} is Chain of A implies a1 <= a2 or a2 <= a1
    proof A1: a1 in {a1,a2} & a2 in {a1,a2} by TARSKI:def 2;
      assume {a1,a2} is Chain of A;
       then the InternalRel of A is_strongly_connected_in {a1,a2} by Def11;
       then [a1,a2] in the InternalRel of A or
            [a2,a1] in the InternalRel of A by A1,RELAT_2:def 7;
     hence thesis by Def9;
    end;
    assume A2: a1 <= a2 or a2 <= a1;
A3: a1 <= a1 & a2 <= a2;
    {a1,a2} is strongly_connected
    proof let x,y;
    assume A4: x in {a1,a2} & y in {a1,a2};
     now per cases by A4,TARSKI:def 2;
      suppose x = a1 & y = a1;
       hence thesis by A3,Def9;
      end;
      suppose x = a1 & y = a2;
       hence thesis by A2,Def9;
      end;
      suppose x = a2 & y = a1;
       hence thesis by A2,Def9;
      end;
      suppose x = a2 & y = a2;
       hence thesis by A3,Def9;
     end;
     end;
    hence thesis;
    end;
   hence thesis;
  end;

theorem
  for A being RelStr, C being Chain of A, S being Subset of A holds
 S c= C implies S is Chain of A
  proof let A be RelStr, C be Chain of A, S be Subset of A;
  assume A1: S c= C;
        the InternalRel of A is_strongly_connected_in C by Def11;
    then the InternalRel of A is_strongly_connected_in S by A1,ORDERS_1:96;
   hence thesis by Def11;
  end;

theorem Th38:
 for A being reflexive RelStr, a1,a2 being Element of A holds
 (ex C being Chain of A st a1 in C & a2 in C) iff a1 <= a2 or a2 <= a1
  proof let A be reflexive RelStr;
   let a1,a2 be Element of A;
   thus (ex C being Chain of A st a1 in C & a2 in
 C) implies a1 <= a2 or a2 <= a1
    proof given C being Chain of A such that A1: a1 in C & a2 in C;
          the InternalRel of A is_strongly_connected_in C by Def11;
      then [a1,a2] in the InternalRel of A or
           [a2,a1] in the InternalRel of A by A1,RELAT_2:def 7;
     hence thesis by Def9;
    end;
    assume A2: a1 <= a2 or a2 <= a1;
     then [a1,a2] in the InternalRel of A or [a2,a1] in the InternalRel of A
      by Def9;
     then a1 in the carrier of A by ZFMISC_1:106;
     then reconsider B = A as non empty reflexive RelStr by STRUCT_0:def 1;
     reconsider b1 = a1, b2 = a2 as Element of B;
     reconsider Y = {b1,b2} as Chain of A by A2,Th36;
   take Y;
   thus thesis by TARSKI:def 2;
  end;

theorem Th39:
 for A being reflexive antisymmetric RelStr, a1,a2 being Element of A holds
 (ex C being Chain of A st a1 in C & a2 in C) iff (a1 < a2 iff not a2 <= a1)
  proof let A be reflexive antisymmetric RelStr;
   let a1,a2 be Element of A;
   thus (ex C being Chain of A st a1 in C & a2 in C) implies
     (a1 < a2 iff not a2 <= a1)
    proof given C being Chain of A such that A1: a1 in C & a2 in C;
     thus a1 < a2 implies not a2 <= a1 by Th30;
      assume that A2: not a2 <= a1 and A3: not a1 < a2;
            not a1 <= a2 or a1 = a2 by A3,Def10;
     hence thesis by A1,A2,Th38;
    end;
    assume a1 < a2 iff not a2 <= a1;
      then a1 <= a2 or a2 <= a1 by Def10;
    hence thesis by Th38;
  end;

theorem Th40:
 for A being RelStr, T being Subset of A holds
 the InternalRel of A well_orders T implies
  T is Chain of A
   proof let A be RelStr, T be Subset of A;
    assume the InternalRel of A well_orders T;
     then the InternalRel of A is_connected_in T &
          the InternalRel of A is_reflexive_in T by WELLORD1:def 5;
     then the InternalRel of A is_strongly_connected_in T by ORDERS_1:92;
    hence thesis by Def11;
   end;

::
::  Upper and lower cones.
::

definition let A; let S;
 func UpperCone(S) -> Subset of A equals
  :Def12:  {a1 : for a2 st a2 in S holds a2 < a1};
 coherence
  proof set T = {a1 : for a2 st a2 in S holds a2 < a1};
    T c= the carrier of A
     proof let x;
      assume x in T;
      then ex a1 st x = a1 & for a2 st a2 in S holds a2 < a1;
      hence thesis;
     end;
    hence T is Subset of A;
   thus thesis;
  end;
end;

definition let A; let S;
 func LowerCone(S) -> Subset of A equals
  :Def13:  {a1 : for a2 st a2 in S holds a1 < a2};
 coherence
  proof set T = {a1 : for a2 st a2 in S holds a1 < a2};
     T c= the carrier of A
     proof let x;
      assume x in T;
      then ex a1 st x = a1 & for a2 st a2 in S holds a1 < a2;
      hence thesis;
     end;
    hence T is Subset of A;
   thus thesis;
  end;
end;

canceled 2;

theorem Th43:
 UpperCone({}(A)) = the carrier of A
  proof
   thus UpperCone({}(A)) c= the carrier of A;
   let x;
    assume x in the carrier of A;
     then reconsider a = x as Element of A;
     for a2 st a2 in {}(A) holds a2 < a;
     then a in {a1 : for a2 st a2 in {}(A) holds a2 < a1};
   hence thesis by Def12;
  end;

theorem
  UpperCone([#](A)) = {}
  proof
   thus UpperCone([#](A)) c= {}
    proof let x;
      assume x in UpperCone([#](A));
        then x in {a1 : for a2 st a2 in [#](A) holds a2 < a1} by Def12;
       then consider a such that x = a and
                                 A1: for a2 st a2 in [#](A) holds a2 < a;
        a in the carrier of A;
        then a in [#](A) by PRE_TOPC:12;
        hence thesis by A1;
    end;
   thus thesis by XBOOLE_1:2;
  end;

theorem
  LowerCone({}(A)) = the carrier of A
  proof
   thus LowerCone({}(A)) c= the carrier of A;
   let x;
    assume x in the carrier of A;
     then reconsider a = x as Element of A;
     for a2 st a2 in {}(A) holds a < a2;
     then a in {a1 : for a2 st a2 in {}(A) holds a1 < a2};
   hence thesis by Def13;
  end;

theorem
  LowerCone([#](A)) = {}
  proof
   thus LowerCone([#](A)) c= {}
    proof let x;
      assume x in LowerCone([#](A));
        then x in {a1 : for a2 st a2 in [#](A) holds a1 < a2} by Def13;
       then consider a such that x = a and
                                 A1: for a2 st a2 in [#](A) holds a < a2;
            a in the carrier of A;
        then a in [#](A) by PRE_TOPC:12;
        hence thesis by A1;
    end;
   thus thesis by XBOOLE_1:2;
  end;

theorem Th47:
  a in S implies not a in UpperCone(S)
  proof assume that A1: a in S and A2: a in UpperCone(S);
    a in {a1 : for a2 st a2 in S holds a2 < a1} by A2,Def12;
    then ex a1 st a1 = a & for a2 st a2 in S holds a2 < a1;
    hence thesis by A1;
  end;

theorem
  not a in UpperCone{a}
  proof a in {a} by TARSKI:def 1;
   hence thesis by Th47;
  end;

theorem Th49:
  a in S implies not a in LowerCone(S)
  proof assume that A1: a in S and A2: a in LowerCone(S);
    a in {a1 : for a2 st a2 in S holds a1 < a2} by A2,Def13;
    then ex a1 st a1 = a & for a2 st a2 in S holds a1 < a2;
    hence thesis by A1;
  end;

theorem Th50:
  not a in LowerCone{a}
  proof a in {a} by TARSKI:def 1;
   hence thesis by Th49;
  end;

theorem
     c < a iff a in UpperCone{c}
  proof
   thus c < a implies a in UpperCone{c}
    proof assume c < a;
      then for b holds b in {c} implies b < a by TARSKI:def 1;
      then a in {a1 : for a2 st a2 in {c} holds a2 < a1};
     hence thesis by Def12;
    end;
    assume a in UpperCone{c};
      then a in {a1 : for a2 st a2 in {c} holds a2 < a1} by Def12;
     then A1: ex a1 st a1 = a & for a2 st a2 in {c} holds a2 < a1;
          c in {c} by TARSKI:def 1;
   hence thesis by A1;
  end;

theorem Th52:
 a < c iff a in LowerCone{c}
  proof
   thus a < c implies a in LowerCone{c}
    proof assume a < c;
      then for b holds b in {c} implies a < b by TARSKI:def 1;
      then a in {a1 : for a2 st a2 in {c} holds a1 < a2};
     hence thesis by Def13;
    end;
    assume a in LowerCone{c};
      then a in {a1 : for a2 st a2 in {c} holds a1 < a2} by Def13;
     then A1: ex a1 st a1 = a & for a2 st a2 in {c} holds a1 < a2;
          c in {c} by TARSKI:def 1;
   hence thesis by A1;
  end;

::
::  Initial segments.
::

definition let A; let S; let a;
 func InitSegm(S,a) -> Subset of A equals
  :Def14:  LowerCone{a} /\ S;
 correctness;
end;

definition let A; let S;
 mode Initial_Segm of S -> Subset of A means
  :Def15: ex a st a in S & it = InitSegm(S,a) if S <> {}
        otherwise it = {};
 existence
  proof
        now consider x being Element of S;
     assume A1: S <> {};
      then reconsider x as Element of A by Lm1;
     take T = InitSegm(S,x);
     thus S <> {} implies ex a st a in S & T = InitSegm(S,a);
     thus S = {} implies T = {} by A1;
    end;
   hence thesis;
  end;
 consistency;
end;

canceled 3;

theorem Th56:
 x in InitSegm(S,a) iff x in LowerCone{a} & x in S
  proof
        x in InitSegm(S,a) iff x in LowerCone{a} /\ S by Def14;
   hence thesis by XBOOLE_0:def 3;
  end;

theorem Th57:
 a in InitSegm(S,b) iff a < b & a in S
  proof
   a in InitSegm(S,b) iff a in LowerCone{b} & a in S by Th56;
   hence thesis by Th52;
  end;

canceled 2;

theorem
  InitSegm({}(A),a) = {}
  proof
   thus InitSegm({}(A),a) = LowerCone{a} /\ {} by Def14
                        .= {};
  end;

theorem Th61:
 InitSegm(S,a) c= S
  proof InitSegm(S,a) = LowerCone{a} /\ S by Def14;
   hence thesis by XBOOLE_1:17;
  end;

theorem Th62:
  not a in InitSegm(S,a)
  proof assume not thesis;
     then a in LowerCone{a} /\ S by Def14;
     then a in LowerCone{a} by XBOOLE_0:def 3;
     then a in {a1 : for a2 st a2 in {a} holds a1 < a2} by Def13;
    then A1: ex a1 st a = a1 & for a2 st a2 in {a} holds a1 < a2;
         a in {a} by TARSKI:def 1;
     hence thesis by A1;
  end;

canceled;

theorem
  a1 < a2 implies InitSegm(S,a1) c= InitSegm(S,a2)
  proof assume A1: a1 < a2;
   let x;
    assume x in InitSegm(S,a1);
      then A2: x in LowerCone{a1} /\ S by Def14;
      then A3: x in S by XBOOLE_0:def 3;
          x in LowerCone{a1} by A2,XBOOLE_0:def 3;
      then x in {a : for b st b in {a1} holds a < b} by Def13;
     then consider a such that A4: a = x and
                               A5: for b st b in {a1} holds a < b;
      a1 in {a1} by TARSKI:def 1;
      then a < a1 by A5;
      then a < a2 by A1,Th29;
      then for b holds b in {a2} implies a < b by TARSKI:def 1;
      then a in {c : for b st b in {a2} holds c < b};
      then x in LowerCone{a2} by A4,Def13;
      then x in LowerCone{a2} /\ S by A3,XBOOLE_0:def 3;
   hence thesis by Def14;
  end;

theorem Th65:
  S c= T implies InitSegm(S,a) c= InitSegm(T,a)
  proof assume A1: S c= T;
   let x;
    assume x in InitSegm(S,a);
     then A2: x in LowerCone{a} /\ S by Def14;
     then x in S by XBOOLE_0:def 3;
     then x in T & x in LowerCone{a} by A1,A2,XBOOLE_0:def 3;
     then x in LowerCone{a} /\ T by XBOOLE_0:def 3;
   hence thesis by Def14;
  end;

canceled;

theorem Th67:
  for I being Initial_Segm of S holds I c= S
  proof
    let I be Initial_Segm of S;
    per cases;
     suppose S = {};
      hence thesis by Def15;
     end;
     suppose S <> {};
      then consider a such that a in S and A1: I = InitSegm(S,a) by Def15;
      I = LowerCone{a} /\ S by A1,Def14;
      hence thesis by XBOOLE_1:17;
    end;
  end;

theorem Th68:
 S <> {} iff not S is Initial_Segm of S
  proof
   thus S <> {} implies not S is Initial_Segm of S
    proof assume S <> {} & S is Initial_Segm of S;
      then consider a such that A1: a in S and A2: S = InitSegm(S,a) by Def15;
      S = LowerCone{a} /\ S by A2,Def14;
      then a in LowerCone{a} by A1,XBOOLE_0:def 3;
     hence thesis by Th50;
    end;
   thus thesis by Def15;
  end;

theorem Th69:
 (S <> {} or T <> {}) & (S is Initial_Segm of T) implies
   not T is Initial_Segm of S
    proof assume that A1: S <> {} or T <> {} and A2: S is Initial_Segm of T and
                      A3: T is Initial_Segm of S;
          now per cases;
       suppose S = {};
        hence thesis by A1,A3,Def15;
       end;
       suppose T = {};
        hence thesis by A1,A2,Def15;
       end;
       suppose A4: S <> {} & T <> {};
         then consider a1 such that A5: a1 in
 S & T = InitSegm(S,a1) by A3,Def15;
         consider a2 such that A6: a2 in T & S = InitSegm(T,a2) by A2,A4,Def15;
              a1 in LowerCone{a2} /\ T & a2 in LowerCone{a1} /\ S by A5,A6,
Def14;
          then a1 in LowerCone{a2} & a2 in LowerCone{a1} by XBOOLE_0:def 3;
          then A7: a1 in {a : for b st b in {a2} holds a < b} &
                  a2 in {a3 : for b st b in {a1} holds a3 < b} by Def13;
         then A8: ex a st a = a1 & for b st b in {a2} holds a < b;
 A9:         ex a3 st a3 = a2 & for b st b in {a1} holds a3 < b by A7;
              a1 in {a1} & a2 in {a2} by TARSKI:def 1;
          then a1 < a2 & a2 < a1 by A8,A9;
        hence thesis by Th28;
      end;
      end;
     hence thesis;
    end;

theorem Th70:
 a1 < a2 & a1 in S & a2 in T & T is Initial_Segm of S implies
  a1 in T
   proof assume that A1: a1 < a2 and A2: a1 in S and A3: a2 in T and
                     A4: T is Initial_Segm of S;
     consider a such that a in S and A5: T = InitSegm(S,a) by A2,A4,Def15;
          now let b;
        assume b in {a};
          then A6: b = a by TARSKI:def 1;
              a2 in LowerCone{a} /\ S by A3,A5,Def14;
          then a2 in LowerCone{a} by XBOOLE_0:def 3;
          then a2 in {a3 : for c st c in {a} holds a3 < c} by Def13;
         then A7: ex a3 st a3 = a2 & for c st c in {a} holds a3 < c;
              a in {a} by TARSKI:def 1;
          then a2 < a by A7;
       hence a1 < b by A1,A6,Th29;
      end;
      then a1 in {b: for c st c in {a} holds b < c};
      then a1 in LowerCone{a} by Def13;
      then a1 in LowerCone{a} /\ S by A2,XBOOLE_0:def 3;
    hence thesis by A5,Def14;
   end;

theorem Th71:
 a in S & S is Initial_Segm of T implies
  InitSegm(S,a) = InitSegm(T,a)
   proof assume that A1: a in S and A2: S is Initial_Segm of T;
     A3: S c= T by A2,Th67;
    thus InitSegm(S,a) c= InitSegm(T,a)
     proof let x;
       assume x in InitSegm(S,a);
        then x in LowerCone{a} /\ S by Def14;
        then x in LowerCone{a} & x in S by XBOOLE_0:def 3;
        then x in LowerCone{a} /\ T by A3,XBOOLE_0:def 3;
      hence thesis by Def14;
     end;
    let x;
     assume x in InitSegm(T,a);
       then A4: x in LowerCone{a} /\ T by Def14;
       then A5: x in LowerCone{a} by XBOOLE_0:def 3;
       then x in {a1 : for a2 st a2 in {a} holds a1 < a2} by Def13;
      then consider a1 such that A6: x = a1 and
                                 A7: for a2 st a2 in {a} holds a1 < a2;
           a in {a} by TARSKI:def 1;
       then a1 < a & a1 in T by A4,A6,A7,XBOOLE_0:def 3;
       then x in S by A1,A2,A6,Th70;
       then x in LowerCone{a} /\ S by A5,XBOOLE_0:def 3;
    hence thesis by Def14;
   end;

theorem
     S c= T & the InternalRel of A well_orders T &
  (for a1,a2 st a2 in S & a1 < a2 holds a1 in S) implies
    S = T or S is Initial_Segm of T
     proof assume that A1: S c= T and A2: the InternalRel of A well_orders T
and
                       A3: for a1,a2 st a2 in S & a1 < a2 holds a1 in S and
                       A4: S <> T;
      per cases;
       case T <> {};
        set Y = T \ S;
             not T c= S by A1,A4,XBOOLE_0:def 10;
         then Y c= T & Y <> {} by XBOOLE_1:36,37;
        then consider x such that A5: x in Y and
         A6: for y st y in Y holds [x,y] in the InternalRel of A by A2,
WELLORD1:9;
        reconsider x as Element of A by A5;
      take x;
      thus A7: x in T by A5,XBOOLE_0:def 4;
            S = LowerCone{x} /\ T
         proof
          thus S c= LowerCone{x} /\ T
           proof let y;
             assume A8: y in S;
              then reconsider a = y as Element of A;
                   now let a1;
                 assume that A9: a1 in {x} and A10: not a < a1;
                  A11: T is Chain of A & a1 = x by A2,A9,Th40,TARSKI:def 1;
                  then a1 <= a & a1 <> a by A1,A5,A7,A8,A10,Th39,XBOOLE_0:def 4
;
                  then a1 < a by Def10;
                  then a1 in S by A3,A8;
                hence contradiction by A5,A11,XBOOLE_0:def 4;
               end;
               then y in {a1 : for a2 st a2 in {x} holds a1 < a2};
               then y in LowerCone{x} by Def13;
            hence thesis by A1,A8,XBOOLE_0:def 3;
           end;
          let y;
           assume A12: y in LowerCone{x} /\ T;
             then y in LowerCone{x} by XBOOLE_0:def 3;
             then y in {a1 : for a2 st a2 in {x} holds a1 < a2} by Def13;
            then consider a such that A13: a = y and
                                      A14: for a2 st a2 in {x} holds a < a2;
             A15: now assume y in Y;
               then [x,y] in the InternalRel of A by A6;
               then A16: x <= a by A13,Def9;
                   x in {x} by TARSKI:def 1;
               then a < x by A14;
              hence contradiction by A16,Th30;
             end;
                 y in T by A12,XBOOLE_0:def 3;
          hence thesis by A15,XBOOLE_0:def 4;
         end;
      hence thesis by Def14;
       end;
       case T = {};
      hence thesis by A1,XBOOLE_1:3;
     end;
     end;

theorem Th73:
 S c= T & the InternalRel of A well_orders T &
  (for a1,a2 st a2 in S & a1 in T & a1 < a2 holds a1 in S) implies
    S = T or S is Initial_Segm of T
     proof assume that A1: S c= T and A2: the InternalRel of A well_orders T
and
        A3: for a1,a2 st a2 in S & a1 in T & a1 < a2 holds a1 in
 S and A4: S <> T;
      per cases;
       case T <> {};
        set Y = T \ S;
             not T c= S by A1,A4,XBOOLE_0:def 10;
         then Y c= T & Y <> {} by XBOOLE_1:36,37;
        then consider x such that A5: x in Y and
         A6: for y st y in Y holds [x,y] in the InternalRel of A by A2,
WELLORD1:9;
        reconsider x as Element of A by A5;
      take x;
      thus A7: x in T by A5,XBOOLE_0:def 4;
            S = LowerCone{x} /\ T
         proof
          thus S c= LowerCone{x} /\ T
           proof let y;
             assume A8: y in S;
              then reconsider a = y as Element of A;
                   now let a1;
                 assume that A9: a1 in {x} and A10: not a < a1;
                  A11: T is Chain of A & a1 = x by A2,A9,Th40,TARSKI:def 1;
                  then a1 <= a & a1 <> a by A1,A5,A7,A8,A10,Th39,XBOOLE_0:def 4
;
                  then a1 < a by Def10;
                  then a1 in S by A3,A7,A8,A11;
                hence contradiction by A5,A11,XBOOLE_0:def 4;
               end;
               then y in {a1 : for a2 st a2 in {x} holds a1 < a2};
               then y in LowerCone{x} by Def13;
            hence thesis by A1,A8,XBOOLE_0:def 3;
           end;
          let y;
           assume A12: y in LowerCone{x} /\ T;
             then y in LowerCone{x} by XBOOLE_0:def 3;
             then y in {a1 : for a2 st a2 in {x} holds a1 < a2} by Def13;
            then consider a such that A13: a = y and
                                      A14: for a2 st a2 in {x} holds a < a2;
             A15: now assume y in Y;
               then [x,y] in the InternalRel of A by A6;
               then A16: x <= a by A13,Def9;
                   x in {x} by TARSKI:def 1;
               then a < x by A14;
              hence contradiction by A16,Th30;
             end;
                 y in T by A12,XBOOLE_0:def 3;
          hence thesis by A15,XBOOLE_0:def 4;
         end;
      hence thesis by Def14;
       end;
       case T = {};
      hence thesis by A1,XBOOLE_1:3;
     end;
     end;

::
::  Chains of choice function of BOOL of partially ordered sets.
::

reserve f for Choice_Function of BOOL(the carrier of A);

definition let A; let f;
 mode Chain of f -> Chain of A means
  :Def16: it <> {} &
        the InternalRel of A well_orders it &
        for a st a in it holds f.UpperCone(InitSegm(it,a)) = a;
 existence
  proof set AC = the carrier of A;
         AC in BOOL AC & not {} in BOOL AC by ORDERS_1:4,5;
    then reconsider aa = f.AC as Element of A by ORDERS_1:def 1;
    reconsider X = {aa} as Chain of A by Th35;
   take X;
   thus X <> {};
         X c= the carrier of A &
      the InternalRel of A is_reflexive_in the carrier of A &
      the InternalRel of A is_transitive_in the carrier of A &
      the InternalRel of A is_antisymmetric_in the carrier of A
          by Def4,Def5,Def6;
   hence the InternalRel of A is_reflexive_in X &
         the InternalRel of A is_transitive_in X &
         the InternalRel of A is_antisymmetric_in X by ORDERS_1:93,94,95;
       the InternalRel of A is_strongly_connected_in X by Def11;
   hence the InternalRel of A is_connected_in X by ORDERS_1:92;
   thus the InternalRel of A is_well_founded_in X
    proof let Y;
      assume that A1: Y c= X and A2: Y <> {};
       reconsider x = aa as set;
     take x;
            Y = X by A1,A2,ZFMISC_1:39;
     hence x in Y by TARSKI:def 1;
     thus (the InternalRel of A)-Seg(x) /\ Y c= {}
      proof let y;
        assume A3: y in (the InternalRel of A)-Seg(x) /\ Y;
         then y in Y by XBOOLE_0:def 3;
         then A4: y = aa by A1,TARSKI:def 1;
             y in (the InternalRel of A)-Seg(x) by A3,XBOOLE_0:def 3;
       hence thesis by A4,WELLORD1:def 1;
      end;
     thus {} c= (the InternalRel of A)-Seg(x) /\ Y by XBOOLE_1:2;
    end;
   let a;
    assume a in X;
     then A5: a = aa by TARSKI:def 1;
      LowerCone{a} /\ X = {}(A)
      proof
       thus LowerCone{a} /\ X c= {}(A)
        proof let x;
          assume A6: x in LowerCone{a} /\ X;
            then A7: x in X by XBOOLE_0:def 3;
                x in LowerCone{a} by A6,XBOOLE_0:def 3;
            then x in {a1 : for a2 st a2 in {a} holds a1 < a2} by Def13;
           then consider a1 such that A8: x = a1 and
                                      A9: for a2 st a2 in {a} holds a1 < a2;
            thus thesis by A5,A7,A8,A9;
        end;
       thus thesis by XBOOLE_1:2;
      end;
     then UpperCone(LowerCone{a} /\ X) = the carrier of A by Th43;
   hence thesis by A5,Def14;
  end;
end;

reserve fC,fC1,fC2 for Chain of f;

canceled 4;

theorem Th78:
 {f.(the carrier of A)} is Chain of f
  proof set AC = the carrier of A;
         AC in BOOL AC & not {} in BOOL AC by ORDERS_1:4,5;
    then reconsider aa = f.AC as Element of A by ORDERS_1:def 1;
    reconsider X = {aa} as Chain of A by Th35;
         X c= the carrier of A &
      the InternalRel of A is_reflexive_in the carrier of A &
      the InternalRel of A is_transitive_in the carrier of A &
      the InternalRel of A is_antisymmetric_in the carrier of A
          by Def4,Def5,Def6;
     then A1: the InternalRel of A is_reflexive_in X &
              the InternalRel of A is_transitive_in X &
              the InternalRel of A is_antisymmetric_in X
                                              by ORDERS_1:93,94,95;
         the InternalRel of A is_strongly_connected_in X by Def11;
     then A2: the InternalRel of A is_connected_in X by ORDERS_1:92;
         the InternalRel of A is_well_founded_in X
      proof let Y;
        assume that A3: Y c= X and A4: Y <> {};
         reconsider x = aa as set;
       take x;
              Y = X by A3,A4,ZFMISC_1:39;
       hence x in Y by TARSKI:def 1;
       thus (the InternalRel of A)-Seg(x) /\ Y c= {}
        proof let y;
          assume A5: y in (the InternalRel of A)-Seg(x) /\ Y;
           then y in Y by XBOOLE_0:def 3;
           then A6: y = aa by A3,TARSKI:def 1;
               y in (the InternalRel of A)-Seg(x) by A5,XBOOLE_0:def 3;
         hence thesis by A6,WELLORD1:def 1;
        end;
       thus {} c= (the InternalRel of A)-Seg(x) /\ Y by XBOOLE_1:2;
      end;
     then A7: the InternalRel of A well_orders X by A1,A2,WELLORD1:def 5;
         for a st a in X holds f.UpperCone(InitSegm(X,a)) = a
      proof
       let a;
        assume a in X;
         then A8: a = aa by TARSKI:def 1;
             LowerCone{a} /\ X = {}(A)
          proof
           thus LowerCone{a} /\ X c= {}(A)
            proof let x;
              assume A9: x in LowerCone{a} /\ X;
                then A10: x in X by XBOOLE_0:def 3;
                    x in LowerCone{a} by A9,XBOOLE_0:def 3;
                then x in {a1 : for a2 st a2 in {a} holds a1 < a2} by Def13;
               then consider a1 such that A11: x = a1 and
                               A12: for a2 st a2 in {a} holds a1 < a2;
                thus thesis by A8,A10,A11,A12;
            end;
           thus thesis by XBOOLE_1:2;
          end;
         then UpperCone(LowerCone{a} /\ X) = the carrier of A by Th43;
       hence thesis by A8,Def14;
      end;
  hence thesis by A7,Def16;
 end;

theorem Th79:
 f.(the carrier of A) in fC
  proof the InternalRel of A well_orders fC & fC <> {} & fC c= fC
                                                         by Def16;
    then consider x such that A1: x in fC and
     A2: for y st y in fC holds [x,y] in the InternalRel of A by WELLORD1:9;
    reconsider x as Element of A by A1;
A3:   now consider y being Element of LowerCone{x} /\ fC;
      assume
A4:     LowerCone{x} /\ fC <> {}(A);
         then A5: y in fC by XBOOLE_0:def 3;
        reconsider a = y as Element of A by A4,Lm1;
             [x,y] in the InternalRel of A by A2,A5;
         then A6: x <= a by Def9;
             a in LowerCone{x} by A4,XBOOLE_0:def 3;
         then a in {a1 : for a2 st a2 in {x} holds a1 < a2} by Def13;
        then A7: ex a1 st a = a1 & for a2 st a2 in {x} holds a1 < a2;
             x in {x} by TARSKI:def 1;
         then a < x by A7;
      hence contradiction by A6,Th30;
     end;
         LowerCone{x} /\ fC = InitSegm(fC,x) by Def14;
     then f.UpperCone(LowerCone{x} /\ fC) = x by A1,Def16;
   hence thesis by A1,A3,Th43;
  end;

theorem Th80:
 a in fC & b = f.(the carrier of A) implies b <= a
  proof assume that A1: a in fC and A2: b = f.(the carrier of A);
         the InternalRel of A well_orders fC & fC <> {} & fC c= fC
                                                         by Def16;
    then consider x such that A3: x in fC and
     A4: for y st y in fC holds [x,y] in the InternalRel of A by WELLORD1:9;
    reconsider x as Element of A by A3;
A5:   now consider y being Element of LowerCone{x} /\ fC;
      assume
A6:     LowerCone{x} /\ fC <> {}(A);
         then A7: y in fC by XBOOLE_0:def 3;
        reconsider a = y as Element of A by A6,Lm1;
             [x,y] in the InternalRel of A by A4,A7;
         then A8: x <= a by Def9;
             a in LowerCone{x} by A6,XBOOLE_0:def 3;
         then a in {a1 : for a2 st a2 in {x} holds a1 < a2} by Def13;
        then A9: ex a1 st a = a1 & for a2 st a2 in {x} holds a1 < a2;
             x in {x} by TARSKI:def 1;
         then a < x by A9;
      hence contradiction by A8,Th30;
     end;
         LowerCone{x} /\ fC = InitSegm(fC,x) by Def14;
     then f.UpperCone(LowerCone{x} /\ fC) = x by A3,Def16;
     then A10: f.(the carrier of A) = x by A5,Th43;
         [x,a] in the InternalRel of A by A1,A4;
   hence thesis by A2,A10,Def9;
  end;

theorem
     a = f.(the carrier of A) implies InitSegm(fC,a) = {}
  proof assume A1: a = f.(the carrier of A);
    then A2: a in fC by Th79;
    consider x being Element of LowerCone{a} /\ fC;
     assume InitSegm(fC,a) <> {};
then A3:    LowerCone {a} /\ fC <> {} by Def14;
        then A4: x in fC & fC c= the carrier of A by XBOOLE_0:def 3;
       reconsider b = x as Element of A by A3,Lm1;
             x in LowerCone{a} by A3,XBOOLE_0:def 3;
        then b in {a1 : for a2 st a2 in {a} holds a1 < a2} by Def13;
       then A5: ex a1 st a1 = b & for a2 st a2 in {a} holds a1 < a2;
            a in {a} by TARSKI:def 1;
        then a <= b & b < a by A1,A4,A5,Th80;
     hence contradiction by A2,A4,Th39;
  end;

theorem
     fC1 meets fC2
  proof f.(the carrier of A) in fC1 & f.(the carrier of A) in fC2 by Th79;
   hence thesis by XBOOLE_0:3;
  end;

theorem Th83:
 fC1 <> fC2 implies
  (fC1 is Initial_Segm of fC2 iff not fC2 is Initial_Segm of fC1)
   proof assume A1: fC1 <> fC2;
     set N = {a : a in fC1 /\ fC2 & InitSegm(fC1,a) = InitSegm(fC2,a)};
      A2: N c= fC1
       proof let x;
         assume x in N;
          then ex a st a = x & a in fC1 /\ fC2 &
                                    InitSegm(fC1,a) = InitSegm(fC2,a);
        hence thesis by XBOOLE_0:def 3;
       end;
     then reconsider N as Subset of A by XBOOLE_1:1;
      A3: the InternalRel of A well_orders fC1 by Def16;
A4:      now let a1,a2;
        assume that A5: a2 in N and A6: a1 in fC1 and A7: a1 < a2;
 A8:         ex a st a = a2 & a in fC1 /\ fC2 &
 InitSegm(fC1,a) = InitSegm(fC2,a) by A5;
          then a1 in InitSegm(fC2,a2) by A6,A7,Th57;
          then a1 in LowerCone{a2} /\ fC2 by Def14;
          then a1 in fC2 by XBOOLE_0:def 3;
          then A9: a1 in fC1 /\ fC2 by A6,XBOOLE_0:def 3;
              InitSegm(fC1,a1) = InitSegm(fC2,a1)
           proof
            thus InitSegm(fC1,a1) c= InitSegm(fC2,a1)
             proof let x;
               assume A10: x in InitSegm(fC1,a1);
                then reconsider b = x as Element of A;
                 A11: b < a1 by A10,Th57;
                 then b < a2 & b in fC1 by A7,A10,Th29,Th57;
                 then b in InitSegm(fC1,a2) by Th57;
                 then b in fC2 by A8,Th57;
              hence thesis by A11,Th57;
             end;
            let x;
             assume A12: x in InitSegm(fC2,a1);
              then reconsider b = x as Element of A;
               A13: b < a1 by A12,Th57;
               then b < a2 & b in fC2 by A7,A12,Th29,Th57;
               then b in InitSegm(fC2,a2) by Th57;
               then b in fC1 by A8,Th57;
            hence thesis by A13,Th57;
           end;
       hence a1 in N by A9;
      end;
A14:  the InternalRel of A well_orders fC2 by Def16;
A15:  N c= fC2
       proof let x;
         assume x in N;
          then ex a st a = x & a in fC1 /\ fC2 &
                                    InitSegm(fC1,a) = InitSegm(fC2,a);
        hence thesis by XBOOLE_0:def 3;
       end;
A16:      now let a1,a2;
        assume that A17: a2 in N and A18: a1 in fC2 and A19: a1 < a2;
 A20:         ex a st a = a2 & a in fC1 /\ fC2 &
 InitSegm(fC1,a) = InitSegm(fC2,a) by A17;
          then a1 in InitSegm(fC1,a2) by A18,A19,Th57;
          then a1 in LowerCone{a2} /\ fC1 by Def14;
          then a1 in fC1 by XBOOLE_0:def 3;
          then A21: a1 in fC1 /\ fC2 by A18,XBOOLE_0:def 3;
              InitSegm(fC1,a1) = InitSegm(fC2,a1)
           proof
            thus InitSegm(fC1,a1) c= InitSegm(fC2,a1)
             proof let x;
               assume A22: x in InitSegm(fC1,a1);
                then reconsider b = x as Element of A;
                 A23: b < a1 by A22,Th57;
                 then b < a2 & b in fC1 by A19,A22,Th29,Th57;
                 then b in InitSegm(fC1,a2) by Th57;
                 then b in fC2 by A20,Th57;
              hence thesis by A23,Th57;
             end;
            let x;
             assume A24: x in InitSegm(fC2,a1);
              then reconsider b = x as Element of A;
               A25: b < a1 by A24,Th57;
               then b < a2 & b in fC2 by A19,A24,Th29,Th57;
               then b in InitSegm(fC2,a2) by Th57;
               then b in fC1 by A20,Th57;
            hence thesis by A25,Th57;
           end;
       hence a1 in N by A21;
      end;

          now per cases by A2,A3,A4,A14,A15,A16,Th73;
       suppose N is Initial_Segm of fC1 & N = fC2;
         then fC2 is Initial_Segm of fC1 & fC1 <> {} by Def16;
        hence thesis by Th69;
       end;
       suppose N is Initial_Segm of fC2 & N = fC1;
         then fC1 is Initial_Segm of fC2 & fC2 <> {} by Def16;
        hence thesis by Th69;
       end;
       suppose N = fC1 & N = fC2;
        hence thesis by A1;
       end;
       suppose A26: N is Initial_Segm of fC1 & N is Initial_Segm of fC2;
              fC1 <> {} by Def16;
         then consider a such that A27: a in fC1 and A28: N = InitSegm(fC1,a)
                                                   by A26,Def15;
              fC2 <> {} by Def16;
         then consider b such that A29: b in fC2 and A30: N = InitSegm(fC2,b)
                                                   by A26,Def15;
          A31: a = f.UpperCone(InitSegm(fC2,b)) by A27,A28,A30,Def16
               .= b by A29,Def16;
          then a in fC1 /\ fC2 by A27,A29,XBOOLE_0:def 3;
          then a in N by A28,A30,A31;
        hence thesis by A28,Th62;
      end;
      end;
    hence thesis;
   end;

theorem Th84:
 fC1 c< fC2 iff fC1 is Initial_Segm of fC2
  proof
   thus fC1 c< fC2 implies fC1 is Initial_Segm of fC2
    proof assume
A1:    fC1 c< fC2;
     then A2: fC1 c= fC2 by XBOOLE_0:def 8;
          now assume A3: fC2 is Initial_Segm of fC1;
            fC1 <> {} by Def16;
        then ex a st a in fC1 & fC2 = InitSegm(fC1,a) by A3,Def15;
        then fC2 c= fC1 by Th61;
       hence contradiction by A1,A2,XBOOLE_0:def 10;
      end;
     hence thesis by A1,Th83;
    end;
    assume A4: fC1 is Initial_Segm of fC2;
           fC2 <> {} by Def16;
     then ex a st a in fC2 & fC1 = InitSegm(fC2,a) by A4,Def15;
    then fC1 <> fC2 & fC1 c= fC2 by A4,Th61,Th68;
   hence thesis by XBOOLE_0:def 8;
  end;

definition let A; let f;
 func Chains f -> set means
  :Def17: x in it iff x is Chain of f;
 existence
  proof
    defpred P[set] means $1 is Chain of f;
    consider X such that
     A1: for x holds x in X iff x in bool(the carrier of A) & P[x]
                                               from XBOOLE_0:sch 1;
   take X;
   let x;
   thus x in X implies x is Chain of f by A1;
    assume x is Chain of f;
   hence thesis by A1;
  end;
 uniqueness
  proof let D1,D2 be set;
    assume A2: for x holds x in D1 iff x is Chain of f;
    assume A3: for x holds x in D2 iff x is Chain of f;
   thus D1 c= D2
    proof let x;
     assume x in D1;
      then x is Chain of f by A2;
     hence thesis by A3;
    end;
   let x;
    assume x in D2;
     then x is Chain of f by A3;
   hence thesis by A2;
  end;
end;

registration let A; let f;
 cluster Chains f -> non empty;
 coherence
  proof
    consider x being Chain of f;
        x in Chains f by Def17;
    hence thesis;
  end;
end;

Lm2: union(Chains(f)) is Subset of A
 proof
       now let X;
     assume X in Chains(f);
      then X is Chain of f by Def17;
    hence X c= the carrier of A;
   end;
  hence thesis by ZFMISC_1:94;
 end;

Lm3: union(Chains(f)) is Chain of A
 proof
   reconsider C = union(Chains(f)) as Subset of A by Lm2;
        the InternalRel of A is_strongly_connected_in C
     proof let x,y;
       assume that A1: x in C and A2: y in C;
        consider X such that A3: x in X and A4: X in Chains(f) by A1,
TARSKI:def 4;
        consider Y such that A5: y in Y and A6: Y in Chains(f) by A2,
TARSKI:def 4;
         reconsider X,Y as Chain of f by A4,A6,Def17;
         A7: the InternalRel of A is_strongly_connected_in X by Def11;
         A8: the InternalRel of A is_strongly_connected_in Y by Def11;
             now per cases;
          suppose X = Y;
           hence thesis by A3,A5,A7,RELAT_2:def 7;
          end;
          suppose A9: X <> Y;
                now per cases by A9,Th83;
             suppose X is Initial_Segm of Y;
               then X c< Y by Th84;
               then X c= Y by XBOOLE_0:def 8;
               hence thesis by A3,A5,A8,RELAT_2:def 7;
             end;
             suppose Y is Initial_Segm of X;
               then Y c< X by Th84;
               then Y c= X by XBOOLE_0:def 8;
               hence thesis by A3,A5,A7,RELAT_2:def 7;
            end;
            end;
           hence thesis;
         end;
         end;
      hence thesis;
     end;
  hence thesis by Def11;
 end;

canceled 2;

theorem Th87:
 union(Chains(f)) <> {}
  proof
   {f.(the carrier of A)} is Chain of f by Th78;
   then {f.(the carrier of A)} in Chains(f) & {f.(the carrier of A)} <> {}
                                                            by Def17;
   hence thesis by ORDERS_1:91;
  end;

theorem Th88:
 fC <> union(Chains(f)) & S = union(Chains(f)) implies
  fC is Initial_Segm of S
   proof assume that A1: fC <> union(Chains(f)) and A2: S = union(Chains(f));
     consider x being Element of S \ fC;
          fC in Chains(f) by Def17;
      then fC c= union(Chains(f)) by ZFMISC_1:92;
      then not union(Chains(f)) c= fC by A1,XBOOLE_0:def 10;
      then A3:     S \ fC <> {} by A2,XBOOLE_1:37;
      then x in S by XBOOLE_0:def 4;
     then consider X such that A4: x in X and A5: X in Chains(f) by A2,
TARSKI:def 4;
     reconsider X as Chain of f by A5,Def17;
A6:      not x in fC by A3,XBOOLE_0:def 4;
          fC <> X & not X c= fC by A3,A4,XBOOLE_0:def 4;
      then not X c< fC by XBOOLE_0:def 8;
      then not X is Initial_Segm of fC by Th84;
      then fC is Initial_Segm of X & X <> {} by A4,A6,Th83;
     then consider a such that A7: a in X and A8: fC = InitSegm(X,a) by Def15;
      A9: X c= S by A2,A5,ZFMISC_1:92;
          InitSegm(S,a) = InitSegm(X,a)
       proof
        thus InitSegm(S,a) c= InitSegm(X,a)
         proof let x;
           assume x in InitSegm(S,a);
             then A10: x in LowerCone{a} /\ S by Def14;
             then A11: x in LowerCone{a} by XBOOLE_0:def 3;
             then x in {a1 : for a2 st a2 in {a} holds a1 < a2} by Def13;
            then consider b such that A12: b = x and
                                      A13: for a2 st a2 in {a} holds b < a2;
                 a in {a} by TARSKI:def 1;
             then A14: b < a by A13;
                 b in S by A10,A12,XBOOLE_0:def 3;
            then consider Y such that A15: b in Y and A16: Y in Chains(f)
                                                           by A2,TARSKI:def 4;
            reconsider Y as Chain of f by A16,Def17;
                 now per cases;
              suppose X = Y;
                then x in LowerCone{a} /\ X by A11,A12,A15,XBOOLE_0:def 3;
               hence thesis by Def14;
              end;
              suppose A17: X <> Y;
                    now per cases by A17,Th83;
                 suppose X is Initial_Segm of Y;
                   then x in X by A7,A12,A14,A15,Th70;
                   then x in LowerCone{a} /\ X by A11,XBOOLE_0:def 3;
                  hence thesis by Def14;
                 end;
                 suppose Y is Initial_Segm of X;
                   then Y c< X by Th84;
                   then Y c= X by XBOOLE_0:def 8;
                   then x in LowerCone{a} /\ X by A11,A12,A15,XBOOLE_0:def 3;
                  hence thesis by Def14;
                end;
                end;
               hence thesis;
             end;
             end;
          hence thesis;
         end;
        let x;
         assume x in InitSegm(X,a);
          then x in LowerCone{a} /\ X by Def14;
          then x in LowerCone{a} & x in X by XBOOLE_0:def 3;
          then x in LowerCone{a} /\ S by A9,XBOOLE_0:def 3;
        hence thesis by Def14;
       end;
    hence thesis by A7,A8,A9,Def15;
   end;

theorem Th89:
     union(Chains(f)) is Chain of f
  proof
    reconsider C = union(Chains(f)) as Chain of A by Lm3;
     A1: C <> {} by Th87;
     A2: the InternalRel of A well_orders C
      proof
            the InternalRel of A is_reflexive_in the carrier of A &
         the InternalRel of A is_transitive_in the carrier of A &
         the InternalRel of A is_antisymmetric_in the carrier of A
          by Def4,Def5,Def6;
       hence the InternalRel of A is_reflexive_in C &
             the InternalRel of A is_transitive_in C &
             the InternalRel of A is_antisymmetric_in C
                                               by ORDERS_1:93,94,95;
            the InternalRel of A is_strongly_connected_in C by Def11;
       hence the InternalRel of A is_connected_in C by ORDERS_1:92;
       let Y;
         consider x being Element of Y;
        assume that A3: Y c= C and A4: Y <> {};
              x in C by A3,A4,TARSKI:def 3;
         then consider X such that A5: x in X and A6: X in Chains(f) by
TARSKI:def 4;
         reconsider X as Chain of f by A6,Def17;
              X /\ Y <> {} & X /\ Y c= X & the InternalRel of A well_orders X
                                                       by A4,A5,Def16,
XBOOLE_0:def 3,XBOOLE_1:17;
         then consider a being set such that A7: a in X /\ Y and
          A8: for x st x in X /\ Y holds [a,x] in the InternalRel of A
                                                             by WELLORD1:9;
              a in X by A7,XBOOLE_0:def 3;
         then reconsider aa = a as Element of A;
       take a;
       thus a in Y by A7,XBOOLE_0:def 3;
       thus (the InternalRel of A)-Seg(a) /\ Y c= {}
        proof let y;
          assume y in (the InternalRel of A)-Seg(a) /\ Y;
            then A9: y in Y & y in
 (the InternalRel of A)-Seg(a) by XBOOLE_0:def 3;
            then y in C & C c= the carrier of A by A3;
           then reconsider b = y as Element of A;
            A10: y <> a & [y,a] in the InternalRel of A by A9,WELLORD1:def 1;
            then A11: b <= aa by Def9;
                now per cases;
             suppose X <> C;
              then a in X & b < aa & X is Initial_Segm of C & y in C
                                    by A3,A7,A9,A10,A11,Def10,Th88,
XBOOLE_0:def 3;
              hence y in X by Th70;
             end;
             suppose X = C;
              hence y in X by A3,A9;
            end;
            end;
            then y in X /\ Y by A9,XBOOLE_0:def 3;
            then [a,y] in the InternalRel of A by A8;
            then aa <= b by Def9;
         hence thesis by A10,A11,Th25;
        end;
       thus {} c= (the InternalRel of A)-Seg(a) /\ Y by XBOOLE_1:2;
      end;
         now let a;
       assume A12: a in C;
        then consider X such that A13: a in X and A14: X in
 Chains(f) by TARSKI:def 4;
        reconsider X as Chain of f by A14,Def17;
             now per cases;
          suppose X = C;
           hence f.UpperCone(InitSegm(C,a)) = a by A12,Def16;
          end;
          suppose X <> C;
           then X is Initial_Segm of C by Th88;
            then InitSegm(C,a) = InitSegm(X,a) &
                 f.UpperCone(InitSegm(X,a)) = a by A13,Def16,Th71;
          hence f.UpperCone(InitSegm(C,a)) = a;
         end;
         end;
      hence f.UpperCone(InitSegm(C,a)) = a;
     end;
   hence thesis by A1,A2,Def16;
  end;

begin :: From original ORDERS_2

 reserve R,P for Relation,
         D for non empty set,
         A for non empty Poset,
         C for Chain of A,
         S for Subset of A,
         a,a1,a2,b,c1,c2 for Element of A;

::
::  Orders.
::

canceled 17;

theorem Th107:
 field((the InternalRel of A) |_2 S) = S
  proof set P = (the InternalRel of A) |_2 S;
   thus field P c= S by WELLORD1:20;
   thus S c= field P
    proof let x;
      assume A1: x in S;
       S c= the carrier of A &
       the InternalRel of A is_reflexive_in the carrier of A by Def4;
       then [x,x] in the InternalRel of A & [x,x] in [:S,S:]
                                          by A1,RELAT_2:def 1,ZFMISC_1:106;
       then [x,x] in P by WELLORD1:16;
       then x in dom P & x in rng P by RELAT_1:def 4,def 5;
       then x in dom P \/ rng P by XBOOLE_0:def 2;
     hence thesis by RELAT_1:def 6;
    end;
  end;

theorem
  (the InternalRel of A) |_2 S is_linear-order implies
  S is Chain of A
   proof assume (the InternalRel of A) |_2 S is_linear-order;
     then (the InternalRel of A) |_2 S is connected &
          field((the InternalRel of A) |_2 S) = S by Th107,ORDERS_1:def 5;
     then A1: (the InternalRel of A) |_2 S is_connected_in S by RELAT_2:def 14;
          S is strongly_connected
     proof let x,y;
       assume A2: x in S & y in S;
        then reconsider a = x, b = y as Element of A;
              now per cases;
          suppose x = y;
           then a <= b;
          hence thesis by Def9;
          end;
          suppose x <> y;
           then [x,y] in (the InternalRel of A) |_2 S or
                 [y,x] in (the InternalRel of A) |_2 S by A1,A2,RELAT_2:def 6;
           hence thesis by WELLORD1:16;
         end;
         end;
      hence thesis;
     end;
     hence thesis;
   end;

theorem
  (the InternalRel of A) |_2 C is_linear-order
  proof set P = (the InternalRel of A) |_2 C;
   the InternalRel of A is_partial-order by ORDERS_1:def 4;
    then P is_partial-order by ORDERS_1:114;
   hence P is reflexive & P is transitive & P is antisymmetric
       by ORDERS_1:def 4;
    A1: C = field P by Th107;
         P is_connected_in C
     proof let x,y;
       assume that A2: x in C & y in C;
       the InternalRel of A is_strongly_connected_in C by Def11;
        then A3: [x,y] in the InternalRel of A or [y,x] in the InternalRel of A
                                  by A2,RELAT_2:def 7;
      [x,y] in [:C,C:] & [y,x] in [:C,C:] by A2,ZFMISC_1:106;
      hence thesis by A3,WELLORD1:16;
     end;
   hence thesis by A1,RELAT_2:def 14;
  end;

canceled 25;

theorem
  the InternalRel of A linearly_orders S implies
  S is Chain of A
   proof assume the InternalRel of A linearly_orders S;
     then the InternalRel of A is_reflexive_in S &
          the InternalRel of A is_connected_in S by ORDERS_1:def 8;
     then the InternalRel of A is_strongly_connected_in S by ORDERS_1:92;
    hence thesis by Def11;
   end;

theorem
  the InternalRel of A linearly_orders C
  proof the InternalRel of A is_reflexive_in the carrier of A &
        the InternalRel of A is_transitive_in the carrier of A &
        the InternalRel of A is_antisymmetric_in the carrier of A &
        C c= the carrier of A by Def4,Def5,Def6;
   hence the InternalRel of A is_reflexive_in C &
        the InternalRel of A is_transitive_in C &
        the InternalRel of A is_antisymmetric_in C
                                        by ORDERS_1:93,94,95;
         the InternalRel of A is_strongly_connected_in C by Def11;
   hence thesis by ORDERS_1:92;
  end;

canceled 21;

theorem
  a is_minimal_in the InternalRel of A iff
  for b holds not b < a
   proof A1: the carrier of A = field(the InternalRel of A) by ORDERS_1:100;
    thus a is_minimal_in the InternalRel of A implies for b holds not b < a
     proof assume A2: a is_minimal_in the InternalRel of A;
      let b;
       a = b or not [b,a] in the InternalRel of A by A1,A2,ORDERS_1:def 12;
       then a = b or not b <= a by Def9;
      hence thesis by Def10;
     end;
     assume A3: for b holds not b < a;
    thus a in field(the InternalRel of A) by A1;
    let y;
     assume that A4: y in field(the InternalRel of A) and
                 A5: y <> a and A6: [y,a] in the InternalRel of A;
      reconsider b = y as Element of A by A4,ORDERS_1:100;
            b <= a by A6,Def9;
       then b < a by A5,Def10;
    hence thesis by A3;
   end;

theorem
  a is_maximal_in the InternalRel of A iff
  for b holds not a < b
   proof A1: the carrier of A = field(the InternalRel of A) by ORDERS_1:100;
    thus a is_maximal_in the InternalRel of A implies for b holds not a < b
     proof assume A2: a is_maximal_in the InternalRel of A;
      let b;
       a = b or not [a,b] in the InternalRel of A by A1,A2,ORDERS_1:def 11;
       then a = b or not a <= b by Def9;
      hence thesis by Def10;
     end;
     assume A3: for b holds not a < b;
    thus a in field(the InternalRel of A) by A1;
    let y;
     assume that A4: y in field(the InternalRel of A) and
                 A5: y <> a and A6: [a,y] in the InternalRel of A;
      reconsider b = y as Element of A by A4,ORDERS_1:100;
            a <= b by A6,Def9;
       then a < b by A5,Def10;
    hence thesis by A3;
   end;

theorem
  a is_superior_of the InternalRel of A iff
  for b st a <> b holds b < a
   proof A1: the carrier of A = field(the InternalRel of A) by ORDERS_1:100;
    thus a is_superior_of the InternalRel of A implies
          for b st a <> b holds b < a
     proof assume A2: a is_superior_of the InternalRel of A;
      let b;
       assume A3: a <> b;
        then [b,a] in the InternalRel of A by A1,A2,ORDERS_1:def 13;
        then b <= a by Def9;
      hence thesis by A3,Def10;
     end;
     assume A4: for b st a <> b holds b < a;
    thus a in field(the InternalRel of A) by A1;
    let y;
     assume y in field(the InternalRel of A);
      then reconsider b = y as Element of A by ORDERS_1:100;
     assume y <> a;
       then b < a by A4;
       then b <= a by Def10;
    hence thesis by Def9;
   end;

theorem
  a is_inferior_of the InternalRel of A iff
  for b st a <> b holds a < b
   proof A1: the carrier of A = field(the InternalRel of A) by ORDERS_1:100;
    thus a is_inferior_of the InternalRel of A implies
          for b st a <> b holds a < b
     proof assume A2: a is_inferior_of the InternalRel of A;
      let b;
       assume A3: a <> b;
        then [a,b] in the InternalRel of A by A1,A2,ORDERS_1:def 14;
        then a <= b by Def9;
      hence thesis by A3,Def10;
     end;
     assume A4: for b st a <> b holds a < b;
    thus a in field(the InternalRel of A) by A1;
    let y;
     assume y in field(the InternalRel of A);
      then reconsider b = y as Element of A by ORDERS_1:100;
     assume y <> a;
       then a < b by A4;
       then a <= b by Def10;
    hence thesis by Def9;
   end;

Lm4: R is_connected_in X & Y c= X implies R is_connected_in Y
 proof assume that A1: R is_connected_in X and A2: Y c= X;
  let x,y;
   assume x in Y & y in Y;
    hence thesis by A1,A2,RELAT_2:def 6;
 end;

Lm5: R well_orders X & Y c= X implies R well_orders Y
 proof assume that A1: R well_orders X and A2: Y c= X;
         R is_reflexive_in X & R is_transitive_in X & R is_antisymmetric_in X &
    R is_connected_in X by A1,WELLORD1:def 5;
  hence R is_reflexive_in Y & R is_transitive_in Y & R is_antisymmetric_in Y &
        R is_connected_in Y by A2,Lm4,ORDERS_1:93,94,95;
  let Z be set;
   assume that A3: Z c= Y and A4: Z <> {};
         Z c= X & R is_well_founded_in X by A1,A2,A3,WELLORD1:def 5,XBOOLE_1:1;
  hence thesis by A4,WELLORD1:def 3;
 end;

::
::  Kuratowski - Zorn Lemma.
::

theorem Th162:
 (for C ex a st for b st b in C holds b <= a) implies
   ex a st for b holds not a < b
    proof assume A1: for C ex a st for b st b in C holds b <= a;
      consider f being Choice_Function of BOOL(the carrier of A);
      reconsider F = union(Chains(f)) as Chain of f by Th89;
      consider a such that A2: for b st b in F holds b <= a by A1;
     take a;
     let b;
      assume A3: a < b;
        now let a1;
         assume a1 in F;
          then a1 <= a by A2;
        hence a1 < b by A3,Th32;
       end;
       then b in {a1 : for a2 st a2 in F holds a2 < a1};
       then UpperCone(F) <> {} by Def12;
       then not UpperCone(F) in {{}} by TARSKI:def 1;
       then UpperCone(F) in bool(the carrier of A) \ {{}} by XBOOLE_0:def 4;
       then UpperCone(F) in BOOL(the carrier of A) &
            not {} in BOOL(the carrier of A) by ORDERS_1:4,def 2;
       then A4: f.UpperCone(F) in UpperCone(F) by ORDERS_1:def 1;
      then reconsider c = f.UpperCone(F) as Element of A;
            c in {a1 : for a2 st a2 in F holds a2 < a1} by A4,Def12;
      then A5: ex c11 being Element of A st c11 = c &
       for a2 st a2 in F holds a2 < c11;
      reconsider Z = F \/ {c} as Subset of A;
            Z c= the carrier of A &
        the InternalRel of A is_reflexive_in the carrier of A &
        the InternalRel of A is_antisymmetric_in the carrier of A &
        the InternalRel of A is_transitive_in the carrier of A
                         by Def4,Def5,Def6;
       then A6: the InternalRel of A is_reflexive_in Z &
                the InternalRel of A is_antisymmetric_in Z &
                the InternalRel of A is_transitive_in Z
                                   by ORDERS_1:93,94,95;
       A7: the InternalRel of A is_connected_in Z
        proof let x,y;
          assume that A8: x in Z and A9: y in Z;
           reconsider x1 = x, y1 = y as Element of A by A8,A9;
           now per cases by A8,A9,XBOOLE_0:def 2;
             suppose x1 in F & y1 in F;
               then x1 <= y1 or y1 <= x1 by Th38;
              hence thesis by Def9;
             end;
             suppose x1 in F & y1 in {c};
               then x1 in F & y1 = c by TARSKI:def 1;
               then x1 < y1 by A5;
               then x1 <= y1 by Def10;
              hence thesis by Def9;
             end;
             suppose x1 in {c} & y1 in F;
               then y1 in F & x1 = c by TARSKI:def 1;
               then y1 < x1 by A5;
               then y1 <= x1 by Def10;
              hence thesis by Def9;
             end;
             suppose x1 in {c} & y1 in {c};
               then c <= c & x1 = c & y1 = c by TARSKI:def 1;
              hence thesis;
            end;
            end;
         hence thesis;
        end;
        the InternalRel of A is_well_founded_in Z
        proof let Y;
          assume that A10: Y c= Z and A11: Y <> {};
           now per cases;
            case A12: Y = {c};
             take x = c;
             thus x in Y by A12,TARSKI:def 1;
              assume (the InternalRel of A)-Seg(x) meets Y;
              then consider x' being set such that
A13:            x' in (the InternalRel of A)-Seg(x) & x' in Y
                by XBOOLE_0:3;
              x' = c & x' in (the InternalRel of A)-Seg(x)
                                by A12,A13,TARSKI:def 1;
             hence contradiction by WELLORD1:def 1;
            end;
            case A14: Y <> {c};
              set X = Y \ {c};
               A15: X c= F
                proof let x;
                  assume that A16: x in X and A17: not x in F;
                  x in Y by A16,XBOOLE_0:def 4;
                  then x in {c} & not x in {c} by A10,A16,A17,XBOOLE_0:def 2
,def 4;
                 hence thesis;
                end;
                    the InternalRel of A well_orders F by Def16;
               then the InternalRel of A well_orders X by A15,Lm5;
               then A18: the InternalRel of A is_well_founded_in X
                                                           by WELLORD1:def 5;
                    now assume X = {};
                 then Y c= {c} by XBOOLE_1:37;
                hence contradiction by A11,A14,ZFMISC_1:39;
               end;
              then consider x such that A19: x in X and
               A20: (the InternalRel of A)-Seg(x) misses X
                 by A18,WELLORD1:def 3;
A21:          (the InternalRel of A)-Seg(x) /\ X = {} by A20,XBOOLE_0:def 7;
             take x' = x;
             X c= Y by XBOOLE_1:36;
             hence x' in Y by A19;
              now per cases;
                suppose c in Y;
                  then {c} c= Y by ZFMISC_1:37;
                  then A22: Y = X \/ {c} by XBOOLE_1:45;
                  A23: now assume c in (the InternalRel of A)-Seg(x');
                     then A24: c <> x' & [c,x'] in the InternalRel of A
                                                           by WELLORD1:def 1;
                    x' in F by A15,A19;
                    then reconsider x'' = x' as Element of A;
                    c <> x'' & c <= x'' by A24,Def9;
                    then c < x'' & x'' < c by A5,A15,A19,Def10;
                   hence contradiction by Th28;
                  end;
               now assume A25: (the InternalRel of A)-Seg(x') /\ {c} <> {};
                  consider x being
                   Element of (the InternalRel of A)-Seg(x') /\ {c};
                   x in {c} by A25,XBOOLE_0:def 3;
                   then x = c & x in (the InternalRel of A)-Seg(x')
                                          by A25,TARSKI:def 1,XBOOLE_0:def 3;
                   hence contradiction by A23;
                  end;
                 then (the InternalRel of A)-Seg(x') /\ Y = {} \/ {}
                                               by A21,A22,XBOOLE_1:23
                                                          .= {};
                 hence (the InternalRel of A)-Seg(x') misses Y by
XBOOLE_0:def 7;
                end;
                suppose not c in Y;
                  then Y misses {c} by ZFMISC_1:56;
                 hence (the InternalRel of A)-Seg(x') misses Y
                       by A20,XBOOLE_1:83;
               end;
               end;
             hence (the InternalRel of A)-Seg(x') misses Y;
           end;
           end;
         hence thesis;
        end;
       then A26: the InternalRel of A well_orders Z by A6,A7,WELLORD1:def 5;
            the InternalRel of A is_strongly_connected_in Z
            by A6,A7,ORDERS_1:92;
       then A27: Z is Chain of A by Def11;
            now let a1;
         assume A28: a1 in Z;
               now per cases;
           suppose A29: a1 = c;
             InitSegm(Z,c) = F
              proof
               thus InitSegm(Z,c) c= F
                proof let x;
                  assume that A30: x in InitSegm(Z,c) and A31: not x in F;
                   A32: x in LowerCone{c} /\ Z by A30,Def14;
                   then x in Z by XBOOLE_0:def 3;
                   then x in {c} by A31,XBOOLE_0:def 2;
                  then x = c & x in LowerCone{c} by A32,TARSKI:def 1
,XBOOLE_0:def 3;
                 hence contradiction by Th50;
                end;
               let x;
                assume A33: x in F;
                  then A34: x in Z by XBOOLE_0:def 2;
                 reconsider y = x as Element of A by A33;
                  y < c by A5,A33;
                  then x in LowerCone{c} by Th52;
                  then x in LowerCone{c} /\ Z by A34,XBOOLE_0:def 3;
               hence thesis by Def14;
              end;
            hence f.UpperCone(InitSegm(Z,a1)) = a1 by A29;
            end;
            suppose a1 <> c;
              then not a1 in {c} by TARSKI:def 1;
              then A35: a1 in F by A28,XBOOLE_0:def 2;
              InitSegm(Z,a1) = InitSegm(F,a1)
               proof
                thus InitSegm(Z,a1) c= InitSegm(F,a1)
                 proof let x;
                   assume x in InitSegm(Z,a1);
                    then A36: x in LowerCone{a1} /\ Z by Def14;
                    then A37: x in LowerCone{a1} by XBOOLE_0:def 3;
                      now assume A38: not x in F;
                       x in Z by A36,XBOOLE_0:def 3;
                       then x in {c} by A38,XBOOLE_0:def 2;
                       then x = c by TARSKI:def 1;
                       then c in {c1 : for c2 st c2 in {a1} holds c1 < c2}
                                         by A37,Def13;
                      then A39: ex c1 st c1 = c &
                       for c2 st c2 in {a1} holds c1 < c2;
                       a1 in {a1} by TARSKI:def 1;
                       then c < a1 & a1 < c by A5,A35,A39;
                     hence contradiction by Th28;
                    end;
                    then x in LowerCone{a1} /\ F by A37,XBOOLE_0:def 3;
                  hence thesis by Def14;
                 end;
                F c= Z by XBOOLE_1:7;
                hence thesis by Th65;
               end;
             hence f.UpperCone(InitSegm(Z,a1)) = a1 by A35,Def16;
          end;
          end;
        hence f.UpperCone(InitSegm(Z,a1)) = a1;
       end;
      then reconsider Z as Chain of f by A26,A27,Def16;
       c in {c} by TARSKI:def 1;
       then Z in Chains(f) & c in Z by Def17,XBOOLE_0:def 2;
       then c in F by TARSKI:def 4;
       hence thesis by A5;
    end;

theorem
 (for C ex a st for b st b in C holds a <= b) implies
   ex a st for b holds not b < a
  proof assume A1: for C ex a st for b st b in C holds a <= b;
    set X = the carrier of A;
     defpred P[Element of A, Element of A] means
       ex a1,a2 st a1 = $2 & a2 = $1 & a1 <= a2;
     set R = (the InternalRel of A)~;
A2:  for a,b being Element of A holds
     [a,b] in R iff b <= a
     proof let a,b be Element of A;
          [a,b] in R iff [b,a] in the InternalRel of A by RELAT_1:def 7;
      hence [a,b] in R iff b <= a by Def9;
     end;
   dom R = dom the InternalRel of A by RELAT_2:29
         .= X by PARTFUN1:def 4;
     then reconsider R as Order of the carrier of A
       by PARTFUN1:def 4,RELAT_2:27,40,42;
     set B = RelStr (# the carrier of A, R #);
           for E being Chain of B
       ex e being Element of B st
        for f being Element of B st f in E holds f <= e
         proof let E be Chain of B;
            reconsider C = E as Subset of A;
             the InternalRel of A is_strongly_connected_in C
              proof let x,y;
                assume A3: x in C & y in C;
                 then reconsider e = x, f = y as Element of B;
                  A4: (e <= f or f <= e) & R = the InternalRel of B
                                                           by A3,Th38;
                 reconsider e1 = e, f1 = f as Element of A;
                       now per cases by A4,Def9;
                   suppose [e,f] in R;
                     then f1 <= e1 by A2;
                    hence thesis by Def9;
                   end;
                   suppose [f,e] in R;
                     then e1 <= f1 by A2;
                    hence thesis by Def9;
                  end;
                  end;
               hence thesis;
              end;
            then reconsider C as Chain of A by Def11;
            consider a such that A5: for b st b in C holds a <= b by A1;
            reconsider e = a as Element of B;
          take e;
          let f be Element of B;
            reconsider b = f as Element of A;
           assume f in E;
            then a <= b by A5;
            then [f,e] in R & R = the InternalRel of B by A2;
          hence thesis by Def9;
         end;
     then consider e being Element of B such that
A6:  for f being Element of B holds not e < f by Th162;
     reconsider d = e as Element of A;
   take d;
   let b;
     reconsider f = b as Element of B;
    assume A7: b < d;
     then b <= d by Def10;
     then [e,f] in R & R = the InternalRel of B by A2;
     then e <= f & b <> d by A7,Def9;
     then e < f by Def10;
   hence thesis by A6;
  end;

