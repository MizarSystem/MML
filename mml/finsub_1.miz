:: Boolean Domains
::  by Andrzej Trybulec and Agata Darmochwa\l
::
:: Received April 14, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies XBOOLE_0, SUBSET_1, ZFMISC_1, TARSKI, FINSET_1, FINSUB_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, FINSET_1;
 constructors TARSKI, SUBSET_1, FINSET_1;
 registrations XBOOLE_0, SUBSET_1, FINSET_1;
 requirements SUBSET, BOOLE;
 definitions TARSKI, XBOOLE_0;
 theorems TARSKI, ZFMISC_1, XBOOLE_0, XBOOLE_1;
 schemes XBOOLE_0;

begin :: Auxiliary theorems

reserve X,Y,x for set;

definition
  let IT be set;
  attr IT is cup-closed means
  :Def1:
  for X,Y being set st X in IT & Y in IT holds X \/ Y in IT;
  attr IT is cap-closed means
  for X,Y being set st X in IT & Y in IT holds X /\ Y in IT;
  attr IT is diff-closed means
  :Def3:
  for X,Y being set st X in IT & Y in IT holds X \ Y in IT;
end;

definition
  let IT be set;
  attr IT is preBoolean means
  :Def4:
  IT is cup-closed diff-closed;
end;

registration
  cluster preBoolean -> cup-closed diff-closed set;
  coherence by Def4;
  cluster cup-closed diff-closed -> preBoolean set;
  coherence by Def4;
end;

registration
  cluster non empty cup-closed cap-closed diff-closed set;
  existence
  proof
    take {{}};
    thus {{}} is non empty;
    thus {{}} is cup-closed
    proof
      let X,Y be set;
      assume that
A1:   X in {{}} and
A2:   Y in {{}};
      X = {} by A1,TARSKI:def 1;
      hence thesis by A2;
    end;
    thus {{}} is cap-closed
    proof
      let X,Y be set;
      assume that
A3:   X in {{}} and
      Y in {{}};
      X = {} by A3,TARSKI:def 1;
      hence thesis by TARSKI:def 1;
    end;
    thus {{}} is diff-closed
    proof
      let X,Y be set;
      assume that
A4:   X in {{}} and
      Y in {{}};
      X = {} by A4,TARSKI:def 1;
      hence thesis by TARSKI:def 1;
    end;
  end;
end;

canceled 9;

theorem Th10:
  for A being set holds A is preBoolean iff for X,Y being set st X
  in A & Y in A holds X \/ Y in A & X \ Y in A
proof
  let A be set;
  thus A is preBoolean implies for X,Y being set st X in A & Y in A holds X \/
  Y in A & X \ Y in A by Def1,Def3;
  assume
A1: for X,Y being set st X in A & Y in A holds X \/ Y in A & X \ Y in A;
A2: A is diff-closed
  proof
    let X,Y be set;
    assume X in A & Y in A;
    hence thesis by A1;
  end;
  A is cup-closed
  proof
    let X,Y be set;
    assume X in A & Y in A;
    hence thesis by A1;
  end;
  hence thesis by A2;
end;

reserve A for non empty preBoolean set;

definition
  let A;
  let X,Y be Element of A;
  redefine func X \/ Y -> Element of A;
  correctness by Th10;
  redefine func X \ Y -> Element of A;
  correctness by Th10;
end;

canceled 2;

theorem Th13:
  X is Element of A & Y is Element of A implies X /\ Y is Element of A
proof
  assume X is Element of A & Y is Element of A;
  then reconsider X,Y as Element of A;
  X /\ Y = X \ (X \ Y) by XBOOLE_1:48;
  hence thesis;
end;

theorem Th14:
  X is Element of A & Y is Element of A implies X \+\ Y is Element of A
proof
  assume X is Element of A & Y is Element of A;
  then reconsider X,Y as Element of A;
  X \+\ Y = (X \ Y) \/ (Y \ X);
  hence thesis;
end;

theorem
  for A being non empty set st for X,Y being Element of A holds X \+\ Y
  in A & X \ Y in A holds A is preBoolean
proof
  let A be non empty set such that
A1: for X,Y being Element of A holds X \+\ Y in A & X \ Y in A;
  now
    let X,Y be set;
    assume that
A2: X in A and
A3: Y in A;
    reconsider Z = Y \ X as Element of A by A1,A2,A3;
    X \/ Y = X \+\ Z by XBOOLE_1:98;
    hence X \/ Y in A by A1,A2;
    thus X \ Y in A by A1,A2,A3;
  end;
  hence thesis by Th10;
end;

theorem
  for A being non empty set st for X,Y being Element of A holds X \+\ Y
  in A & X /\ Y in A holds A is preBoolean
proof
  let A be non empty set such that
A1: for X,Y being Element of A holds X \+\ Y in A & X /\ Y in A;
  now
    let X,Y be set;
    assume that
A2: X in A and
A3: Y in A;
    reconsider Z1 = X \+\ Y, Z2 = X /\ Y as Element of A by A1,A2,A3;
    X \/ Y = Z1 \+\ Z2 by XBOOLE_1:94;
    hence X \/ Y in A by A1;
    X \ Y = X \+\ Z2 by XBOOLE_1:100;
    hence X \ Y in A by A1,A2;
  end;
  hence thesis by Th10;
end;

theorem
  for A being non empty set st for X,Y being Element of A holds X \+\ Y
  in A & X \/ Y in A holds A is preBoolean
proof
  let A be non empty set such that
A1: for X,Y being Element of A holds X \+\ Y in A & X \/ Y in A;
  now
    let X,Y be set;
    assume that
A2: X in A and
A3: Y in A;
    thus X \/ Y in A by A1,A2,A3;
    reconsider Z1 = X \+\ Y, Z2 = X \/ Y as Element of A by A1,A2,A3;
    X /\ Y = Z1 \+\ Z2 by XBOOLE_1:95;
    then reconsider Z = X /\ Y as Element of A by A1;
    X \ Y = X \+\ Z by XBOOLE_1:100;
    hence X \ Y in A by A1,A2;
  end;
  hence thesis by Th10;
end;

definition
  let A;
  let X,Y be Element of A;
  redefine func X /\ Y -> Element of A;
  coherence by Th13;
  redefine func X \+\ Y -> Element of A;
  coherence by Th14;
end;

theorem Th18:
  {} in A
proof
  consider x being Element of A;
  x \ x = {} by XBOOLE_1:37;
  hence thesis;
end;

canceled;

theorem Th20:
  for A being set holds bool A is preBoolean
proof
  let A be set;
  now
    let X,Y be set;
    assume X in bool A & Y in bool A;
    then reconsider X9=X,Y9=Y as Subset of A;
    X9 \/ Y9 in bool A & X9 \ Y9 in bool A;
    hence X \/ Y in bool A & X \ Y in bool A;
  end;
  hence thesis by Th10;
end;

registration
  let A be set;
  cluster bool A -> preBoolean;
  coherence by Th20;
end;

theorem
  for A,B being non empty preBoolean set holds A /\ B is non empty preBoolean
proof
  let A,B be non empty preBoolean set;
  {} in A & {} in B by Th18;
  then reconsider C = A /\ B as non empty set by XBOOLE_0:def 4;
  now
    let X,Y be set;
    assume
A1: X in C & Y in C;
    then
A2: X in B & Y in B by XBOOLE_0:def 4;
    then
A3: X \/ Y in B by Th10;
A4: X \ Y in B by A2,Th10;
A5: X in A & Y in A by A1,XBOOLE_0:def 4;
    then X \/ Y in A by Th10;
    hence X \/ Y in C by A3,XBOOLE_0:def 4;
    X \ Y in A by A5,Th10;
    hence X \ Y in C by A4,XBOOLE_0:def 4;
  end;
  hence thesis by Th10;
end;
 
:: The set of all finite subsets of a set

definition
  let A be set;
  func Fin A -> preBoolean set means
  :Def5:
  for X being set holds X in it iff X c= A & X is finite;
  existence
  proof
    defpred P[set] means ex y being set st y=$1 & y c= A & y is finite;
    consider P being set such that
A1: for x holds x in P iff x in bool A & P[x] from XBOOLE_0:sch 1;
    {} c= A by XBOOLE_1:2;
    then reconsider Q=P as non empty set by A1;
    for X,Y being set st X in Q & Y in Q holds X \/ Y in Q & X \ Y in Q
    proof
      let X,Y be set;
      assume that
A2:   X in Q and
A3:   Y in Q;
      consider Z1 being set such that
A4:   Z1=X and
A5:   Z1 c=A and
A6:   Z1 is finite by A1,A2;
      consider Z2 being set such that
A7:   Z2=Y and
A8:   Z2 c= A and
A9:   Z2 is finite by A1,A3;
A10:  Z1 \ Z2 c= A by A5,XBOOLE_1:1;
      Z1 \/ Z2 c= A by A5,A8,XBOOLE_1:8;
      hence thesis by A1,A4,A6,A7,A9,A10;
    end;
    then reconsider R=Q as non empty preBoolean set by Th10;
    for X being set holds X in R iff X c= A & X is finite
    proof
      let X be set;
      thus X in R implies X c= A & X is finite
      proof
        assume X in R;
        then ex Y being set st Y=X & Y c= A & Y is finite by A1;
        hence thesis;
      end;
      thus thesis by A1;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let P,Q be preBoolean set;
    assume that
A11: for X being set holds X in P iff X c= A & X is finite and
A12: for X being set holds X in Q iff X c= A & X is finite;
    for x being set holds x in P iff x in Q
    proof
      let x;
      thus x in P implies x in Q
      proof
        assume x in P;
        then x c= A & x is finite by A11;
        hence thesis by A12;
      end;
      thus x in Q implies x in P
      proof
        assume x in Q;
        then x c= A & x is finite by A12;
        hence thesis by A11;
      end;
    end;
    hence thesis by TARSKI:2;
  end;
end;

registration
  let A be set;
  cluster Fin A -> non empty;
  coherence
  proof
    {} c= A by XBOOLE_1:2;
    hence thesis by Def5;
  end;
end;

registration
  let A be set;
  cluster -> finite Element of Fin A;
  coherence by Def5;
end;

canceled;

theorem Th23:
  for A, B being set st A c= B holds Fin A c= Fin B
proof
  let A,B be set;
  assume
A1: A c= B;
  now
    let X;
    assume
A2: X in Fin A;
    then X c= A by Def5;
    then X c= B by A1,XBOOLE_1:1;
    hence X in Fin B by A2,Def5;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem
  for A, B being set holds Fin (A /\ B) = Fin A /\ Fin B
proof
  let A, B be set;
  Fin (A /\ B) c= Fin A & Fin (A /\ B) c= Fin B by Th23,XBOOLE_1:17;
  hence Fin (A /\ B) c= Fin A /\ Fin B by XBOOLE_1:19;
  now
    let X;
    assume
A1: X in Fin A /\ Fin B;
    then X in Fin B by XBOOLE_0:def 4;
    then
A2: X c= B by Def5;
A3: X in Fin A by A1,XBOOLE_0:def 4;
    then X c= A by Def5;
    then X c= A /\ B by A2,XBOOLE_1:19;
    hence X in Fin (A /\ B) by A3,Def5;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem
  for A, B being set holds Fin A \/ Fin B c= Fin (A \/ B)
proof
  let A, B be set;
  Fin A c= Fin (A \/ B) & Fin B c= Fin(A \/ B) by Th23,XBOOLE_1:7;
  hence thesis by XBOOLE_1:8;
end;

theorem Th26:
  for A being set holds Fin A c= bool A
proof
  let A be set;
  let x;
  assume x in Fin A;
  then x c= A by Def5;
  hence thesis;
end;

theorem Th27:
  for A being set st A is finite holds Fin A = bool A
proof
  let A be set;
  assume
A1: A is finite;
A2: bool A c= Fin A
  proof
    let x;
    assume x in bool A;
    hence thesis by A1,Def5;
  end;
  Fin A c= bool A by Th26;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem
  Fin {} = { {} } by Th27,ZFMISC_1:1;
 
:: Finite subsets

definition
  let A be set;
  mode Finite_Subset of A is Element of Fin A;
end;

canceled;

theorem
  for A being set, X being Finite_Subset of A holds X is finite;

canceled;

theorem
  for A being set, X being Finite_Subset of A holds X is Subset of A by
  Def5;

canceled;

theorem
  for A being set, X being Subset of A st A is finite holds X is
  Finite_Subset of A by Def5;
