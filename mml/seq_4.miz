:: Convergent Real Sequences. Upper and Lower Bound of Sets of Real Numbers
::  by Jaros{\l}aw Kotowicz
::
:: Received November 23, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ARYTM, SEQ_1, ORDINAL2, SEQM_3, ARYTM_3, RELAT_1, ARYTM_1, SEQ_2,
      LATTICES, ABSVALUE, FUNCT_1, PROB_1, SEQ_4, MEMBERED, FUNCOP_1, FRECHET2,
      SQUARE_1, XXREAL_2, SUPINF_1, ANPROJ_1, NORMSP_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, RELAT_1, COMPLEX1, PARTFUN1, FUNCT_2,
      NAT_1, VALUED_0, VALUED_1, SEQ_1, SEQ_2,
      FUNCOP_1, SQUARE_1, SEQM_3, MEMBERED, XXREAL_0, XXREAL_2,
      RECDEF_1;
 constructors FUNCOP_1, REAL_1, NAT_1, COMPLEX1, PARTFUN1, SEQ_2, SEQM_3,
      SQUARE_1, VALUED_1, SEQ_1, RECDEF_1, XXREAL_2, FINSET_1;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0,
      XREAL_0, MEMBERED, SEQM_3, VALUED_0, VALUED_1, FUNCT_2, XXREAL_2,
      FUNCOP_1, SEQ_2, FINSET_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, SEQ_2, SQUARE_1, XXREAL_2, VALUED_0;
 theorems NAT_1, SEQ_1, SEQ_2, SEQM_3, AXIOMS, FUNCT_1, FUNCT_2,
      ABSVALUE, TARSKI, RELAT_1, RELSET_1, XREAL_0, XBOOLE_1, SUBSET_1,
      XCMPLX_0, MEMBERED, XREAL_1, FUNCOP_1, COMPLEX1, XXREAL_0, ORDINAL1,
      SQUARE_1, XXREAL_2, VALUED_0;
 schemes NAT_1, RECDEF_1, SEQ_1, SUBSET_1, FUNCT_2;

begin

reserve n,k,k1,m,m1,n1,n2,l for Element of NAT;
reserve r,r1,r2,p,p1,g,g1,g2,s,s1,s2,t for real number;
reserve seq,seq1,seq2 for Real_Sequence;
reserve Nseq for increasing sequence of NAT;
reserve x for set;
reserve X,Y for Subset of REAL;

canceled 7;

theorem Th8:
  for X,Y st for r,p st r in X & p in Y holds r<p
  ex g st for r,p st r in X & p in Y holds r<=g & g<=p
proof
  let X,Y such that
A1: for r,p st r in X & p in Y holds r<p;
  for r,p st r in X & p in Y holds r<=p by A1;
  then consider g such that
A2: for r,p st r in X & p in Y holds r <= g & g <= p by AXIOMS:26;
  reconsider g as Real by XREAL_0:def 1;
  take g;
  thus thesis by A2;
end;

theorem Th9:
  0<p & (ex r st r in X) & (for r st r in X holds r+p in X) implies
  for g ex r st r in X & g<r
proof
  assume that
A1: 0<p and
A2: ex r st r in X and
A3: for r st r in X holds r+p in X and
A4: ex g st for r st r in X holds not g<r;
  consider g1 such that
A5: for r st r in X holds not g1<r by A4;
  defpred X[Real] means for r st r in X holds not $1<r;
  consider Y such that
A6: for p1 being Real holds p1 in Y iff X[p1] from SUBSET_1:sch 3;
  g1 is Real by XREAL_0:def 1;
  then
A7: g1 in Y by A5,A6;
  now
    let r,p1 such that
A8: r in X and
A9: p1 in Y;
A10: r+p in X by A3,A8;
A11: r+0<r+p by A1,XREAL_1:10;
    r+p<=p1 by A6,A9,A10;
    hence r<p1 by A11,XXREAL_0:2;
  end;
  then consider g2 such that
A12: for r,p1 st r in X & p1 in Y holds r<=g2 & g2<=p1 by Th8;
A13: g2-p is Real by XREAL_0:def 1;
A14: now
    assume not g2-p in Y;
    then consider r1 such that
A15: r1 in X and
A16: g2-p<r1 by A6,A13;
A17: g2-p+p<r1+p by A16,XREAL_1:8;
    r1+p in X by A3,A15;
    hence contradiction by A7,A12,A17;
  end;
  -p<-0 by A1,XREAL_1:26;
  then g2+-p<g2+0 by XREAL_1:8;
  hence contradiction by A2,A12,A14;
end;

theorem Th10:
  for r ex n st r<n
proof
  let r;
  for r st r in NAT holds r+1 in NAT by AXIOMS:28;
  then consider p such that
A1: p in NAT and
A2: r<p by Th9;
  consider n1 such that
A3: n1=p by A1;
  take n1;
  thus r<n1 by A2,A3;
end;

definition
  let X be real-membered set;
  redefine attr X is bounded_above means
  :Def1:
  ex p st for r st r in X holds r<=p;
  compatibility
   proof
    thus X is bounded_above implies
     ex p st for r st r in X holds r<=p
    proof
     given p being real number such that
G:    p is UpperBound of X;
     reconsider p as Element of REAL by XREAL_0:def 1;
     take p;
     let r;
     thus thesis by G,XXREAL_2:def 1;
    end;
    given p such that
G:   for r st r in X holds r<=p;
    take p;
    let r be ext-real number;
    assume r in X;
    hence thesis by G;
   end;
  attr X is bounded_below means
  :Def2:
  ex p st for r st r in X holds p<=r;
  compatibility
   proof
    thus X is bounded_below implies
     ex p st for r st r in X holds p<=r
    proof
     given p being real number such that
G:    p is LowerBound of X;
     reconsider p as Element of REAL by XREAL_0:def 1;
     take p;
     let r;
     thus thesis by G,XXREAL_2:def 2;
    end;
    given p such that
G:   for r st r in X holds p<=r;
    take p;
    let r be ext-real number;
    assume r in X;
    hence thesis by G;
   end;
end;

canceled 3;

theorem
  X is bounded iff ex s st 0<s & for r st r in X holds abs(r)<s
proof
  thus X is bounded implies ex s st 0<s & for r st r in X holds abs(r)<s
  proof
    assume
A1: X is bounded;
    then consider s1 such that
A2: for r st r in X holds r<=s1 by Def1;
    consider s2 such that
A3: for r st r in X holds s2<=r by A1,Def2;
    take s=abs(s1)+abs(s2)+1;
A4: 0<=abs(s1) by COMPLEX1:132;
A5: 0<=abs(s2) by COMPLEX1:132;
    hence 0<s by A4;
    let r such that
A6: r in X;
A7: s1<=abs(s1) by ABSVALUE:11;
    r<=s1 by A2,A6;
    then r<=abs(s1) by A7,XXREAL_0:2;
    then r+0<=abs(s1)+abs(s2) by A5,XREAL_1:9;
    then
A8: r<s by XREAL_1:10;
A9: -abs(s2)<=s2 by ABSVALUE:11;
    s2<=r by A3,A6;
    then -abs(s2)<=r by A9,XXREAL_0:2;
    then -abs(s1)+-abs(s2)<=0+r by A4,XREAL_1:9;
    then
A11: -abs(s1)-abs(s2)+-1<r+0 by XREAL_1:10;
    -abs(s1)-abs(s2)-1=-(abs(s1)+abs(s2)+1);
    hence abs(r)<s by A8,A11,SEQ_2:9;
  end;
  given s such that 0<s and
A12: for r st r in X holds abs(r)<s;
  now
    take g=s;
    let r;
    assume
A13: r in X;
    r<=abs(r) by ABSVALUE:11;
    hence r<=g by A12,A13,XXREAL_0:2;
  end;
  then
A14: X is bounded_above by Def1;
  now
    take g=-s;
    let r;
    assume r in X;
    then abs(r)<s by A12;
    then
A15: -s<-abs(r) by XREAL_1:26;
    -abs(r)<=r by ABSVALUE:11;
    hence g<=r by A15,XXREAL_0:2;
  end;
  then X is bounded_below by Def2;
  hence thesis by A14;
end;

definition
  let r;
  redefine func {r} -> Subset of REAL;
  coherence
  proof
    {r} c= REAL
    proof
      let x be set;
      assume x in {r};
      hence thesis by XREAL_0:def 1;
    end;
    hence thesis;
  end;
end;

canceled;

theorem Th16:
  for X being real-membered set holds X is non empty bounded_above implies
  ex g st (for r st r in X holds r<=g) & for s st 0<s ex r st r in X & g-s<r
proof
  let X be real-membered set;
  assume that
A1: X is non empty and
A2: X is bounded_above;
  consider r1 being real number such that
A3: r1 in X by A1,MEMBERED:9;
  consider p1 such that
A4: for r st r in X holds r<=p1 by A2,Def1;
A5: X is Subset of REAL by MEMBERED:3;
  defpred X[Real] means for r st r in X holds r<=$1;
  consider Y such that
A6: for p be Real holds p in Y iff X[p] from SUBSET_1:sch 3;
  p1 is Real by XREAL_0:def 1;
  then
A7: p1 in Y by A4,A6;
  for r,p st r in X & p in Y holds r<=p by A6;
  then consider g1 such that
A8: for r,p st r in X & p in Y holds r<=g1 & g1<=p by A5,AXIOMS:26;
  reconsider g1 as Real by XREAL_0:def 1;
  take g=g1;
  now
    given s1 such that
A9: 0<s1 and
A10: for r st r in X holds not g-s1<r;
    g-s1 in Y by A6,A10;
    then g<=g-s1 by A3,A8;
    then g-(g-s1)<=g-s1-(g-s1) by XREAL_1:11;
    hence contradiction by A9;
  end;
  hence thesis by A7,A8;
end;

theorem Th17:
  for X being real-membered set holds (for r st r in X holds r<=g1) &
  (for s st 0<s ex r st (r in X & g1-s<r)) & (for r st r in X holds r<=g2) &
  (for s st 0<s ex r st (r in X & g2-s<r)) implies g1=g2
proof
  let X be real-membered set;
  assume that
A1: for r st r in X holds r<=g1 and
A2: for s st 0<s ex r st (r in X & g1-s<r) and
A3: for r st r in X holds r<=g2 and
A4: for s st 0<s ex r st (r in X & g2-s<r);
A5: now
    assume g1<g2;
    then consider r1 such that
A6: r1 in X and
A7: g2-(g2-g1)<r1 by A4,XREAL_1:52;
    thus contradiction by A1,A6,A7;
  end;
  now
    assume g2<g1;
    then consider r1 such that
A8: r1 in X and
A9: g1-(g1-g2)<r1 by A2,XREAL_1:52;
    thus contradiction by A3,A8,A9;
  end;
  hence thesis by A5,XXREAL_0:1;
end;

theorem Th18:
  for X being real-membered set holds
  X is non empty bounded_below implies ex g st
  (for r st r in X holds g<=r) & (for s st 0<s ex r st r in X & r<g+s)
proof
  let X be real-membered set;
  assume that
A1: X is non empty and
A2: X is bounded_below;
  consider r1 being real number such that
A3: r1 in X by A1,MEMBERED:9;
  consider p1 such that
A4: for r st r in X holds p1<=r by A2,Def2;
  reconsider X as Subset of REAL by MEMBERED:3;
  defpred X[Real] means for r st r in X holds $1<=r;
  consider Y such that
A5: for p be Real holds p in Y iff X[p] from SUBSET_1:sch 3;
  p1 is Real by XREAL_0:def 1;
  then
A6: p1 in Y by A4,A5;
  for p,r st p in Y & r in X holds p<=r by A5;
  then consider g1 such that
A7: for p,r st p in Y & r in X holds p<=g1 & g1<=r by AXIOMS:26;
  reconsider g1 as Real by XREAL_0:def 1;
  take g=g1;
  now
    given s1 such that
A8: 0<s1 and
A9: for r st r in X holds not r<g+s1;
    g+s1 in Y by A5,A9;
    then g+s1<=g by A3,A7;
    then g+s1-g<=g-g by XREAL_1:11;
    hence contradiction by A8;
  end;
  hence thesis by A6,A7;
end;

theorem Th19:
  for X being real-membered set holds (for r st r in X holds g1<=r) &
  (for s st 0<s ex r st (r in X & r<g1+s)) & (for r st r in X holds g2<=r) &
  (for s st 0<s ex r st (r in X & r<g2+s)) implies g1=g2
proof
  let X be real-membered set;
  assume that
A1: for r st r in X holds g1<=r and
A2: for s st 0<s ex r st (r in X & r<g1+s) and
A3: for r st r in X holds g2<=r and
A4: for s st 0<s ex r st (r in X & r<g2+s);
A5: now
    assume g1<g2;
    then consider r1 such that
A6: r1 in X and
A7: r1<g1+(g2-g1) by A2,XREAL_1:52;
    thus contradiction by A3,A6,A7;
  end;
  now
    assume g2<g1;
    then consider r1 such that
A8: r1 in X and
A9: r1<g2+(g1-g2) by A4,XREAL_1:52;
    thus contradiction by A1,A8,A9;
  end;
  hence thesis by A5,XXREAL_0:1;
end;

definition
 canceled;
  let X be real-membered set;
  assume
A1: X is non empty bounded_above;
  func upper_bound X -> real number means
  :Def4:
  (for r st r in X holds r<=it) & (for s st 0<s ex r st r in X & it-s<r);
  existence by A1,Th16;
  uniqueness by Th17;
end;

definition
  let X be real-membered set;
  assume
A1: X is non empty bounded_below;
  func lower_bound X -> real number means
  :Def5:
  (for r st r in X holds it<=r) & (for s st 0<s ex r st r in X & r<it+s);
  existence by A1,Th18;
  uniqueness by Th19;
end;

Lm61:
  for X being non empty real-membered set st (for s st s in X holds s >= r) &
  for t st for s st s in X holds s >= t holds r >= t holds r = lower_bound X
proof
  let X be non empty real-membered set;
  assume that
A1: for s st s in X holds s >= r and
A2: for t st for s st s in X holds s >= t holds r >= t;
A3: X is bounded_below by A1,Def2;
  now
    let s be real number such that
A4: 0 < s;
    assume for t be real number st t in X holds t >= r+s;
    then r >= r+s by A2;
    hence contradiction by A4,XREAL_1:31;
  end;
  hence r = lower_bound X by A1,A3,Def5;
end;

Lm63:
  for X being non empty real-membered set, r
  st (for s st s in X holds s <= r) &
  for t st for s st s in X holds s <= t holds r <= t holds r = upper_bound X
proof
  let X be non empty real-membered set, r;
  assume that
A1: for s st s in X holds s <= r and
A2: for t st for s st s in X holds s <= t holds r <= t;
A3: X is bounded_above by A1,Def1;
  now
    let s be real number such that
A4: 0 < s;
    assume for t be real number st t in X holds r-s >= t;
    then r <= r-s by A2;
    then r+s <= r by XREAL_1:21;
    hence contradiction by A4,XREAL_1:31;
  end;
  hence r = upper_bound X by A1,A3,Def4;
end;

registration
  let X be non empty bounded_below (real-membered set);
  identify lower_bound X with inf X;
  compatibility
   proof
A:  for s st s in X holds s >= inf X by XXREAL_2:3;
    now let t;
     assume
Z:    for s st s in X holds s >= t;
      for x being ext-real number st x in X holds t <= x by Z;
      then t is LowerBound of X by XXREAL_2:def 2;
     hence inf X >= t by XXREAL_2:def 4;
    end;
    hence lower_bound X = inf X by A,Lm61;
   end;
end;

registration
  let X be non empty bounded_above (real-membered set);
  identify upper_bound X with sup X;
  compatibility
   proof
A:  for s st s in X holds s <= sup X by XXREAL_2:4;
    now let t;
     assume
Z:    for s st s in X holds t >= s;
      for x being ext-real number st x in X holds x <= t by Z;
      then t is UpperBound of X by XXREAL_2:def 1;
     hence t >= sup X by XXREAL_2:def 3;
    end;
    hence upper_bound X = sup X by A,Lm63;
   end;
end;

definition
  let X;
  redefine func upper_bound X -> Real;
  coherence by XREAL_0:def 1;
  redefine func lower_bound X -> Real;
  coherence by XREAL_0:def 1;
end;

canceled 2;

theorem Th22:
  lower_bound {r} = r & upper_bound {r} = r by XXREAL_2:11,13;

theorem Th23:
  lower_bound {r} = upper_bound {r}
proof
  lower_bound {r}=r & upper_bound {r}=r by Th22;
  hence thesis;
end;

theorem
  X is bounded non empty implies lower_bound X <= upper_bound X
proof
  assume X is bounded non empty;
  then reconsider X as bounded non empty (real-membered set);
  lower_bound X <= upper_bound X by XXREAL_2:40;
 hence thesis;
end;

theorem
  X is bounded non empty implies
  ((ex r,p st r in X & p in X & p<>r) iff lower_bound X < upper_bound X)
proof
  assume that
A1: X is bounded and
A2: X is non empty;
  thus (ex r,p st r in X & p in X & p<>r) implies lower_bound X<upper_bound X
  proof
    given r,p such that
A3: r in X and
A4: p in X and
A5: p<>r;
A6: now
      assume
A7:   p<r;
      lower_bound X<=p by A1,A4,Def5;
      then
A8:   lower_bound X<r by A7,XXREAL_0:2;
      r<=upper_bound X by A1,A3,Def4;
      hence lower_bound X<upper_bound X by A8,XXREAL_0:2;
    end;
    now
      assume
A9:   r<p;
      lower_bound X<=r by A3,Def5,A1;
      then
A10:  lower_bound X<p by A9,XXREAL_0:2;
      p<=upper_bound X by A4,Def4,A1;
      hence lower_bound X<upper_bound X by A10,XXREAL_0:2;
    end;
    hence thesis by A5,A6,XXREAL_0:1;
  end;
  assume that
A11: lower_bound X<upper_bound X and
A12: for r,p st r in X & p in X holds p=r;
  consider r being Element of REAL such that
A13: r in X by A2,SUBSET_1:10;
    x in X iff x=r by A12,A13;
  then X={r} by TARSKI:def 1;
  hence contradiction by A11,Th23;
end;

::
:: Theorems about the Convergence and the Limit
::

theorem Th26:
  seq is convergent implies abs seq is convergent
proof
  assume seq is convergent;
  then consider g1 such that
A1: for p st 0<p ex n st for m st n<=m holds abs((seq.m)-g1)<p by SEQ_2:def 6;
  reconsider g1 as Real by XREAL_0:def 1;
  take g=abs(g1);
  let p;
  assume 0<p;
  then consider n1 such that
A2: for m st n1<=m holds abs((seq.m)-g1)<p by A1;
  take n=n1;
  let m;
  assume n<=m;
  then
A3: abs((seq.m)-g1)<p by A2;
A4: abs(seq.m)-g<=abs((seq.m)-g1) by COMPLEX1:145;
  abs(g1-seq.m)=abs(-((seq.m)-g1))
    .=abs((seq.m)-g1) by COMPLEX1:138;
  then g-abs(seq.m)<=abs((seq.m)-g1) by COMPLEX1:145;
  then -abs((seq.m)-g1)<= -(g-abs(seq.m)) by XREAL_1:26;
  then abs(abs(seq.m)-g)<=abs((seq.m)-g1) by A4,ABSVALUE:12;
  then abs(((abs seq).m)-g) <=abs((seq.m)-g1) by SEQ_1:16;
  hence thesis by A3,XXREAL_0:2;
end;

theorem
  seq is convergent implies lim abs seq = abs lim seq
proof
  assume
A1: seq is convergent;
  then
A2: abs seq is convergent by Th26;
  now
    let p;
    assume 0<p;
    then consider n1 such that
A3: for m st n1<=m holds abs((seq.m)-(lim seq))<p by A1,SEQ_2:def 7;
    take n=n1;
    let m;
    assume n<=m;
    then
A4: abs((seq.m)-(lim seq))<p by A3;
A5: abs(seq.m)-abs(lim seq)<=abs((seq.m)-(lim seq)) by COMPLEX1:145;
    abs((lim seq)-seq.m)=abs(-((seq.m)-(lim seq)))
      .=abs((seq.m)-(lim seq)) by COMPLEX1:138;
    then abs(lim seq)-abs(seq.m)<=abs((seq.m)-(lim seq)) by COMPLEX1:145;
    then -abs((seq.m)-(lim seq))<=-(abs(lim seq)-abs(seq.m)) by XREAL_1:26;
    then abs(abs(seq.m)-abs(lim seq))<=abs((seq.m)-(lim seq))
    by A5,ABSVALUE:12;
    then abs(((abs seq).m)-abs(lim seq)) <=abs((seq.m)-(lim seq)) by SEQ_1:16;
    hence abs(((abs seq).m)-abs(lim seq))<p by A4,XXREAL_0:2;
  end;
  hence thesis by A2,SEQ_2:def 7;
end;

theorem
  abs seq is convergent & lim abs seq=0 implies seq is convergent & lim seq=0
proof
  assume that
A1: abs seq is convergent and
A2: lim abs seq=0;
A3: -abs seq is convergent by A1,SEQ_2:23;
A4: lim(-abs seq)=-lim abs seq by A1,SEQ_2:24;
A5: now
    let n;
    -abs(seq.n)<=seq.n by ABSVALUE:11;
    then
A6: -((abs seq).n)<=seq.n by SEQ_1:16;
    seq.n<=abs(seq.n) by ABSVALUE:11;
    hence (-abs seq).n<=seq.n & seq.n<=(abs seq).n by A6,SEQ_1:14,16;
  end;
  hence seq is convergent by A1,A2,A3,A4,SEQ_2:33;
  thus lim seq=0 by A1,A2,A3,A4,A5,SEQ_2:34;
end;

theorem Th29:
  seq1 is subsequence of seq & seq is convergent implies seq1 is convergent
proof
  assume that
A1: seq1 is subsequence of seq and
A2: seq is convergent;
  consider g1 such that
A3: for p st 0<p ex n st for m st n<=m holds
  abs((seq.m)-g1)<p by A2,SEQ_2:def 6;
  consider Nseq such that
A4: seq1=seq*Nseq by A1,VALUED_0:def 17;
  take g=g1;
  let p;
  assume 0<p;
  then consider n1 such that
A5: for m st n1<=m holds abs((seq.m)-g1)<p by A3;
  take n=n1;
  let m such that
A6: n<=m;
  m<=Nseq.m by SEQM_3:33;
  then
A7: n<=Nseq.m by A6,XXREAL_0:2;
  seq1.m=seq.(Nseq.m) by A4,FUNCT_2:21;
  hence abs((seq1.m)-g)<p by A5,A7;
end;

theorem Th30:
  seq1 is subsequence of seq & seq is convergent implies lim seq1=lim seq
proof
  assume that
A1: seq1 is subsequence of seq and
A2: seq is convergent;
A3: seq1 is convergent by A1,A2,Th29;
  consider Nseq such that
A4: seq1=seq*Nseq by A1,VALUED_0:def 17;
  now
    let p;
    assume 0<p;
    then consider n1 such that
A5: for m st n1<=m holds abs((seq.m)-(lim seq))<p by A2,SEQ_2:def 7;
    take n=n1;
    let m such that
A6: n<=m;
    m<=Nseq.m by SEQM_3:33;
    then
A7: n<=Nseq.m by A6,XXREAL_0:2;
    seq1.m=seq.(Nseq.m) by A4,FUNCT_2:21;
    hence abs((seq1.m)-(lim seq))<p by A5,A7;
  end;
  hence thesis by A3,SEQ_2:def 7;
end;

theorem Th31:
  seq is convergent & (ex k st for n st k<=n holds seq1.n=seq.n)
  implies seq1 is convergent
proof
  assume that
A1: seq is convergent and
A2: ex k st for n st k<=n holds seq1.n=seq.n;
  consider g1 such that
A3: for p st 0<p ex n st for m st n<=m holds
  abs((seq.m)-g1)<p by A1,SEQ_2:def 6;
  consider k such that
A4: for n st k<=n holds seq1.n=seq.n by A2;
  take g=g1;
  let p;
  assume 0<p;
  then consider n1 such that
A5: for m st n1<=m holds abs((seq.m)-g1)<p by A3;
A6: now
    assume
A7: k<=n1;
    take n=n1;
    let m;
    assume
A8: n<=m;
    then seq1.m=seq.m by A4,A7,XXREAL_0:2;
    hence abs((seq1.m)-g)<p by A5,A8;
  end;
  now
    assume
A9: n1<=k;
    take n=k;
    let m;
    assume
A10: n<=m;
    then n1<=m by A9,XXREAL_0:2;
    then abs((seq.m)-g1)<p by A5;
    hence abs((seq1.m)-g)<p by A4,A10;
  end;
  hence thesis by A6;
end;

theorem
  seq is convergent & (ex k st for n st k<=n holds seq1.n=seq.n)
  implies lim seq=lim seq1
proof
  assume that
A1: seq is convergent and
A2: ex k st for n st k<=n holds seq1.n=seq.n;
A3: seq1 is convergent by A1,A2,Th31;
  consider k such that
A4: for n st k<=n holds seq1.n=seq.n by A2;
  now
    let p;
    assume 0<p;
    then consider n1 such that
A5: for m st n1<=m holds abs((seq.m)-(lim seq))<p by A1,SEQ_2:def 7;
A6: now
      assume
A7:   k<=n1;
      take n=n1;
      let m;
      assume
A8:   n<=m;
      then seq1.m=seq.m by A4,A7,XXREAL_0:2;
      hence abs((seq1.m)-(lim seq))<p by A5,A8;
    end;
    now
      assume
A9:   n1<=k;
      take n=k;
      let m;
      assume
A10:  n<=m;
      then n1<=m by A9,XXREAL_0:2;
      then abs((seq.m)-(lim seq))<p by A5;
      hence abs((seq1.m)-(lim seq))<p by A4,A10;
    end;
    hence ex n st for m st n<=m holds abs(seq1.m-(lim seq))<p by A6;
  end;
  hence thesis by A3,SEQ_2:def 7;
end;

registration
  cluster constant -> convergent Real_Sequence;
  coherence
proof let seq be Real_Sequence;
  assume seq is constant;
  then consider r being Real such that
A1: for n being Nat holds seq.n=r by VALUED_0:def 18;
  take g=r;
  let p such that
A2: 0<p;
  take n=0;
  let m such that n<=m;
  abs((seq.m)-g)=abs(r-g) by A1
    .=0 by ABSVALUE:7;
  hence abs((seq.m)-g)<p by A2;
end;
end;

registration
  cluster constant Real_Sequence;
  existence
  proof
    reconsider cs = NAT --> 0 as Real_Sequence by FUNCOP_1:57;

    take cs;
    thus cs is constant;
  end;
end;

registration let seq be convergent Real_Sequence; let k;
 cluster seq ^\k -> convergent Real_Sequence;
 coherence by Th29;
end;

theorem Th33:
  seq is convergent implies lim (seq^\k)=lim seq by Th30;

canceled;

theorem Th35:
  seq^\k is convergent implies seq is convergent
proof
  assume that
A1: seq^\k is convergent;
  consider g1 such that
A4: for p st 0<p ex n st for m st n<=m holds abs((seq^\k).m-g1)<p
  by A1,SEQ_2:def 6;
  take g=g1;
  let p;
  assume 0<p;
  then consider n1 such that
A5: for m st n1<=m holds abs((seq^\k).m-g1)<p by A4;
  take n=n1+k;
  let m;
  assume
A6: n<=m;
  then consider l be Nat such that
A7: m=n1+k+l by NAT_1:10;
  reconsider l as Element of NAT by ORDINAL1:def 13;
  m-k=n1+l+k+-k by A7;
  then consider m1 be Element of NAT such that
A8: m1=m-k;
A9: now
    assume not n1<=m1;
    then m1+k<n1+k by XREAL_1:8;
    hence contradiction by A6,A8;
  end;
A10: m1+k=m by A8;
  abs((seq^\k).m1-g1)<p by A5,A9;
  hence abs(seq.m-g)<p by A10,NAT_1:def 3;
end;

theorem
  seq^\k is convergent implies lim seq = lim (seq^\k)
proof
  assume that
A1: seq^\k is convergent;
A3: seq is convergent by A1,Th35;
  now
    let p;
    assume 0<p;
    then consider n1 such that
A5: for m st n1<=m holds abs((seq^\k).m-(lim (seq^\k)))<p by A1,SEQ_2:def 7;
    take n=n1+k;
    let m;
    assume
A6: n<=m;
    then consider l be Nat such that
A7: m=n1+k+l by NAT_1:10;
    reconsider l as Element of NAT by ORDINAL1:def 13;
    m-k=n1+l+k+-k by A7;
    then consider m1 such that
A8: m1=m-k;
A9: now
      assume not n1<=m1;
      then m1+k<n1+k by XREAL_1:8;
      hence contradiction by A6,A8;
    end;
A10: m1+k=m by A8;
    abs((seq^\k).m1-(lim(seq^\k)))<p by A5,A9;
    hence abs(seq.m-(lim(seq^\k)))<p by A10,NAT_1:def 3;
  end;
  hence thesis by A3,SEQ_2:def 7;
end;

theorem Th37:
  seq is convergent & lim seq<>0 implies ex k st (seq ^\k) is non-zero
proof
  assume that
A1: seq is convergent and
A2: lim seq<>0;
  consider n1 such that
A3: for m st n1<=m holds abs(lim seq)/2<abs((seq.m)) by A1,A2,SEQ_2:30;
  take k=n1;
  now
    let n;
    0<=n by NAT_1:2;
    then 0+k<=n+k by XREAL_1:9;
    then abs(lim seq)/2<abs((seq.(n+k))) by A3;
    then
A4: abs(lim seq)/2<abs(((seq ^\k).n)) by NAT_1:def 3;
    0<abs(lim seq) by A2,COMPLEX1:133;
    then 0/2<abs(lim seq)/2 by XREAL_1:76;
    hence (seq ^\k).n<>0 by A4,ABSVALUE:7;
  end;
  hence thesis by SEQ_1:7;
end;

theorem
  seq is convergent & lim seq<>0 implies
  ex seq1 st seq1 is subsequence of seq & seq1 is non-zero
proof
  assume that
A1: seq is convergent and
A2: lim seq <>0;
  consider k such that
A3: seq ^\k is non-zero by A1,A2,Th37;
  take seq ^\k;
  thus thesis by A3;
end;

canceled;

theorem Th40:
  (seq is constant & r in rng seq or
  seq is constant & (ex n st seq.n=r)) implies lim seq=r
proof
  assume
A1: seq is constant & r in rng seq or seq is constant & (ex n st seq.n=r);
A2: now
    assume that
A3: seq is constant and
A4: r in rng seq;
    consider r1 being Real such that
A5: rng seq={r1} by A3,FUNCT_2:188;
    consider r2 being Real such that
A6: for n being Nat holds seq.n=r2 by A3,VALUED_0:def 18;
A7: r=r1 by A4,A5,TARSKI:def 1;
    now
      let p such that
A9:   0<p;
      take n=0;
      let m such that n<=m;
      m in NAT;
      then m in dom seq by FUNCT_2:def 1;
      then seq.m in rng seq by FUNCT_1:def 5;
      then r2 in rng seq by A6;
      then r2=r by A5,A7,TARSKI:def 1;
      then seq.m=r by A6;
      hence abs((seq.m)-r)<p by A9,ABSVALUE:7;
    end;
    hence lim seq =r by A3,SEQ_2:def 7;
  end;
  now
    assume that
A10: seq is constant and
A11: ex n st seq.n=r;
    consider n such that
A12: seq.n=r by A11;
    n in NAT;
    then n in dom seq by FUNCT_2:def 1;
    hence lim seq= r by A2,A10,A12,FUNCT_1:def 5;
  end;
  hence thesis by A1,A2;
end;

theorem
  seq is constant implies for n holds lim seq=seq.n by Th40;

theorem
  seq is convergent & lim seq<>0 implies
  for seq1 st seq1 is subsequence of seq & seq1 is non-zero holds
  lim (seq1")=(lim seq)"
proof
  assume that
A1: seq is convergent and
A2: lim seq<>0;
  let seq1 such that
A3: seq1 is subsequence of seq and
A4: seq1 is non-zero;
  lim seq1=lim seq by A1,A3,Th30;
  hence lim seq1"=(lim seq)" by A1,A2,A3,A4,Th29,SEQ_2:36;
end;

theorem Th43:
  0<=r & (for n holds seq.n=1/(n+r)) implies seq is convergent
proof
  assume that
A1: 0<=r and
A2: for n holds seq.n=1/(n+r);
  take g=0;
  let p;
  assume 0<p;
  then
A3: 0<p";
  consider k1 such that
A4: p"<k1 by Th10;
  take n=k1;
  let m such that
A5: n<=m;
  p"+0<k1+r by A1,A4,XREAL_1:10;
  then 1/(k1+r)<1/p" by A3,XREAL_1:78;
  then
A6: 1/(k1+r)<1*p"" by XCMPLX_0:def 9;
  n+r<=m+r by A5,XREAL_1:8;
  then 1/(m+r)<=1/(n+r) by A1,A3,A4,XREAL_1:120;
  then
A8: 1/(m+r)<p by A6,XXREAL_0:2;
A9: seq.m=1/(m+r) by A2;
  0<=m by NAT_1:2;
  hence abs(seq.m-g)<p by A1,A8,A9,ABSVALUE:def 1;
end;

theorem Th44:
  0<=r & (for n holds seq.n=1/(n+r)) implies lim seq=0
proof
  assume that
A1: 0<=r and
A2: for n holds seq.n=1/(n+r);
A3: seq is convergent by A1,A2,Th43;
  now
    let p;
    assume 0<p;
    then
A4: 0<p";
    consider k1 such that
A5: p"<k1 by Th10;
    take n=k1;
    let m such that
A6: n<=m;
    p"+0<k1+r by A1,A5,XREAL_1:10;
    then 1/(k1+r)<1/p" by A4,XREAL_1:78;
    then
A7: 1/(k1+r)<1*p"" by XCMPLX_0:def 9;
    n+r<=m+r by A6,XREAL_1:8;
    then
    1/(m+r)<=1/(n+r) by A1,A4,A5,XREAL_1:120;
    then
A9: 1/(m+r)<p by A7,XXREAL_0:2;
A10: seq.m=1/(m+r) by A2;
    0<=m by NAT_1:2;
    hence abs(seq.m-0)<p by A9,A10,A1,ABSVALUE:def 1;
  end;
  hence thesis by A3,SEQ_2:def 7;
end;

theorem
  (for n holds seq.n=1/(n+1)) implies seq is convergent & lim seq=0
  by Th43,Th44;

theorem
  0<=r & (for n holds seq.n=g/(n+r)) implies seq is convergent & lim seq=0
proof
  assume that
A1: 0<=r and
A2: for n holds seq.n=g/(n+r);
  reconsider r1 = r as Real by XREAL_0:def 1;
  deffunc U(Element of NAT) = 1/($1+r1);
  consider seq1 such that
A3: for n holds seq1.n=U(n) from SEQ_1:sch 1;
A5: g(#)seq1 is convergent by A1,A3,Th43,SEQ_2:21;
A6: lim (g(#)seq1)=g*(lim seq1) by A1,A3,Th43,SEQ_2:22
    .=g*0 by A1,A3,Th44
    .=0;
  now
    let n;
    thus (g(#)seq1).n=g*(seq1.n) by SEQ_1:13
      .=g*(1/(n+r)) by A3
      .=g*(1*(n+r)") by XCMPLX_0:def 9
      .=g/(n+r) by XCMPLX_0:def 9
      .=seq.n by A2;
  end;
  hence thesis by A5,A6,FUNCT_2:113;
end;

theorem Th47:
  0<=r & (for n holds seq.n=1/(n*n+r)) implies seq is convergent
proof
  assume that
A1: 0<=r and
A2: for n holds seq.n=1/(n*n+r);
  take g=0;
  let p;
  assume
A3: 0<p;
  then
A4: 0<p";
  consider k1 such that
A5: p"<k1 by Th10;
  take n=k1;
  let m such that
A6: n<=m;
  0<=n & 0<=m by NAT_1:2;
  then
A7: n*n<=m*m by A6,XREAL_1:68;
A8: k1>0 by A3,A5;
  then k1>=1+0 by NAT_1:13;
  then k1<=k1*k1 by XREAL_1:153;
  then
A9: k1+r<=k1*k1+r by XREAL_1:8;
  p"+0<k1+r by A1,A5,XREAL_1:10;
  then p"+0<k1*k1+r by A9,XXREAL_0:2;
  then 1/(k1*k1+r)<1/p" by A4,XREAL_1:78;
  then
A10: 1/(k1*k1+r)<1*p"" by XCMPLX_0:def 9;
B11: 0<n^2 by A8,SQUARE_1:74;
  n*n+r<=m*m+r by A7,XREAL_1:8;
  then
  1/(m*m+r)<=1/(n*n+r) by A1,B11,XREAL_1:120;
  then
A12: 1/(m*m+r)<p by A10,XXREAL_0:2;
A13: seq.m=1/(m*m+r) by A2;
  0<=m*m by NAT_1:2;
  hence abs(seq.m-g)<p by A12,A13,A1,ABSVALUE:def 1;
end;

theorem Th48:
  0<=r & (for n holds seq.n=1/(n*n+r)) implies lim seq=0
proof
  assume that
A1: 0<=r and
A2: for n holds seq.n=1/(n*n+r);
A3: seq is convergent by A1,A2,Th47;
  now
    let p;
    assume
A4: 0<p;
    then
A5: 0<p";
    consider k1 such that
A6: p"<k1 by Th10;
    take n=k1;
    let m such that
A7: n<=m;
    0<=n & 0<=m by NAT_1:2;
    then
A8: n*n<=m*m by A7,XREAL_1:68;
A9: k1>0 by A4,A6;
    then k1>=1+0 by NAT_1:13;
    then k1<=k1*k1 by XREAL_1:153;
    then
A10: k1+r<=k1*k1+r by XREAL_1:8;
    p"+0<k1+r by A1,A6,XREAL_1:10;
    then p"+0<k1*k1+r by A10,XXREAL_0:2;
    then 1/(k1*k1+r)<1/p" by A5,XREAL_1:78;
    then
A11: 1/(k1*k1+r)<1*p"" by XCMPLX_0:def 9;
B12: 0<n^2 by A9,SQUARE_1:74;
    n*n+r<=m*m+r by A8,XREAL_1:8;
    then
    1/(m*m+r)<=1/(n*n+r) by A1,B12,XREAL_1:120;
    then
A13: 1/(m*m+r)<p by A11,XXREAL_0:2;
A14: seq.m=1/(m*m+r) by A2;
    0<=m*m by NAT_1:2;
    hence abs(seq.m-0)<p by A13,A14,A1,ABSVALUE:def 1;
  end;
  hence thesis by A3,SEQ_2:def 7;
end;

theorem
  (for n holds seq.n=1/(n*n+1)) implies
  seq is convergent & lim seq=0 by Th47,Th48;

theorem
  0<=r & (for n holds seq.n=g/(n*n+r)) implies seq is convergent & lim seq=0
proof
  assume that
A1: 0<=r and
A2: for n holds seq.n=g/(n*n+r);
  reconsider r1 = r as Real by XREAL_0:def 1;
  deffunc U(Element of NAT) = 1/($1*$1+r1);
  consider seq1 such that
A3: for n holds seq1.n=U(n) from SEQ_1:sch 1;
A5: g(#)seq1 is convergent by A1,A3,Th47,SEQ_2:21;
A6: lim (g(#)seq1)=g*(lim seq1) by A1,A3,Th47,SEQ_2:22
    .=g*0 by A1,A3,Th48
    .=0;
  now
    let n;
    thus (g(#)seq1).n=g*(seq1.n) by SEQ_1:13
      .=g*(1/(n*n+r)) by A3
      .=g*(1*(n*n+r)") by XCMPLX_0:def 9
      .=g/(n*n+r) by XCMPLX_0:def 9
      .=seq.n by A2;
  end;
  hence seq is convergent & lim seq=0 by A5,A6,FUNCT_2:113;
end;

registration
 cluster non-decreasing bounded_above -> convergent Real_Sequence;
 coherence
proof let seq be Real_Sequence;
  assume that
A1: seq is non-decreasing and
A2: seq is bounded_above;
  consider r2 such that
A3: for n holds seq.n<r2 by A2,SEQ_2:def 3;
  defpred X[Real] means ex n st $1=seq.n;
  consider X such that
A4: for p be Real holds p in X iff X[p] from SUBSET_1:sch 3;
A5: seq.0 in X by A4;
A6: X is bounded_above iff ex r st for p st p in X holds p<=r by Def1;
A7: now
    take r=r2;
    let p;
    assume p in X;
    then ex n1 st p=seq.n1 by A4;
    hence p<=r by A3;
  end;
  take g=upper_bound X;
  let s;
  assume
A8: 0<s;
  then consider p1 such that
A9: p1 in X and
A10: upper_bound X-s<p1 by A5,A6,Def4;
  consider n1 such that
A11: p1=seq.n1 by A4,A9;
  take n=n1;
  let m;
  assume n<=m;
  then seq.n<=seq.m by A1,SEQM_3:12;
  then g+ -s<seq.m by A10,A11,XXREAL_0:2;
  then
A12: -s<seq.m -g by XREAL_1:22;
  seq.m in X by A4;
  then seq.m<=g by A6,A7,Def4;
  then seq.m +0<g+s by A8,XREAL_1:10;
  then seq.m -g<s by XREAL_1:21;
  hence abs(seq.m -g)<s by A12,SEQ_2:9;
end;
end;

registration
 cluster non-increasing bounded_below -> convergent Real_Sequence;
 coherence
proof let seq be Real_Sequence;
  assume that
A1: seq is non-increasing and
A2: seq is bounded_below;
  consider r1 such that
A3: for n holds r1< seq.n by A2,SEQ_2:def 4;
  defpred X[Real] means ex n st $1=seq.n;
  consider X such that
A4: for p be Real holds p in X iff X[p] from SUBSET_1:sch 3;
A5: seq.0 in X by A4;
A6: X is bounded_below iff ex r st for p st p in X holds r<=p by Def2;
A7: now
    take r=r1;
    let p;
    assume p in X;
    then ex n1 st p=seq.n1 by A4;
    hence r<=p by A3;
  end;
  take g=lower_bound X;
  let s;
  assume
A8: 0<s;
  then consider p1 such that
A9: p1 in X and
A10: p1<lower_bound X+s by A5,A6,Def5;
  consider n1 such that
A11: p1=seq.n1 by A4,A9;
  take n=n1;
  let m;
  assume n<=m;
  then seq.m<=seq.n by A1,SEQM_3:14;
  then seq.m <g+s by A10,A11,XXREAL_0:2;
  then
A12: seq.m -g<s by XREAL_1:21;
  seq.m in X by A4;
  then 0+g<=seq.m by A6,A7,Def5;
  then
A13: 0<=seq.m-g by XREAL_1:21;
  -s<-0 by A8,XREAL_1:26;
  hence abs(seq.m -g)<s by A12,A13,SEQ_2:9;
end;
end;

registration
 cluster monotone bounded -> convergent Real_Sequence;
 coherence
proof let seq be Real_Sequence;
  assume that
A1: seq is monotone and
A2: seq is bounded;
A4: seq is non-decreasing implies thesis by A2;
  seq is non-increasing implies thesis by A2;
  hence thesis by A1,A4,SEQM_3:def 7;
end;
end;

canceled 2;

theorem Th53:
  seq is monotone & seq is bounded implies seq is convergent;

theorem
  seq is bounded_above & seq is non-decreasing implies
  for n holds seq.n<=lim seq
proof
  assume that
A1: seq is bounded_above and
A2: seq is non-decreasing;
  let m;
  reconsider seq1 = NAT --> seq.m as Real_Sequence;
  seq1.0=seq.m by FUNCOP_1:13;
  then
A5: lim seq1=seq.m by Th40;
  deffunc U(Nat) = seq.($1+m);
  consider seq2 such that
A6: for n holds seq2.n=U(n) from SEQ_1:sch 1;
  now let n be Nat;
    n in NAT by ORDINAL1:def 13;
   hence seq2.n=U(n) by A6;
  end;
  then
A7: seq2=seq^\m by NAT_1:def 3;
  then
A8: lim seq2=lim seq by A1,A2,Th33;
  now
    let n;
A10: seq1.n=seq.m by FUNCOP_1:13;
    seq2.n=seq.(m+n) by A6;
    hence seq1.n<=seq2.n by A2,A10,SEQM_3:11;
  end;
  hence seq.m<=lim seq by A5,A8,A1,A2,A7,SEQ_2:32;
end;

theorem
  seq is bounded_below & seq is non-increasing implies
  for n holds lim seq <= seq.n
proof
  assume that
A1: seq is bounded_below and
A2: seq is non-increasing;
  let m;
  set seq1 = NAT --> seq.m;
  seq1.0=seq.m by FUNCOP_1:13;
  then
A7: lim seq1=seq.m by Th40;
  deffunc U(Nat) = seq.($1+m);
  consider seq2 such that
A8: for n holds seq2.n=U(n) from SEQ_1:sch 1;
  now let n be Nat;
    n in NAT by ORDINAL1:def 13;
   hence seq2.n=U(n) by A8;
  end;
  then
A9: seq2=seq^\m by NAT_1:def 3;
  then
A10: lim seq2=lim seq by A1,A2,Th33;
  now
    let n;
A12: seq1.n=seq.m by FUNCOP_1:13;
    seq2.n=seq.(m+n) by A8;
    hence seq2.n<=seq1.n by A2,A12,SEQM_3:13;
  end;
  hence lim seq<=seq.m by A7,A10,A1,A2,A9,SEQ_2:32;
end;

theorem Th56:
  for seq ex Nseq st seq*Nseq is monotone
proof
  let seq;
  defpred X[Element of NAT] means for m st $1<m holds seq.$1<seq.m;
  consider XN being Subset of NAT such that
A1: for n holds n in XN iff X[n] from SUBSET_1:sch 3;
A2: now
    given k1 such that
A3: for n st n in XN holds n<=k1;
A4: now
      let k;
      assume k1<k;
      then not k in XN by A3;
      then consider m such that
A5:   k<m and
A6:   not seq.k<seq.m by A1;
      take n=m;
      thus k<n by A5;
      thus seq.n<=seq.k by A6;
    end;
    set seq1=seq ^\(1+k1);
    consider Nseq such that
A7: seq1=seq*Nseq by VALUED_0:def 17;
A8: now
      let k;
      0<=k by NAT_1:2;
      then 0+k1<k+1+k1 by XREAL_1:10;
      then consider n such that
A9:   k+1+k1<n and
A10:  seq.n<=seq.(k+1+k1) by A4;
      consider m be Nat such that
A11:  n=k+1+k1+m by A9,NAT_1:10;
      reconsider m as Element of NAT by ORDINAL1:def 13;
      n-(1+k1)=k+0+m by A11;
      then consider n1 be Element of NAT such that
A12:  n1=n-(1+k1);
      take l=n1;
      now
        assume not k<l;
        then n-(1+k1)+(1+k1)<=k+(1+k1) by A12,XREAL_1:8;
        hence contradiction by A9;
      end;
      hence k<l;
A13:  n=l+(1+k1) by A12;
      seq.n<=seq.(k+(1+k1)) by A10;
      then seq.n<=seq1.k by NAT_1:def 3;
      hence seq1.l<=seq1.k by A13,NAT_1:def 3;
    end;
    defpred P[set,set,set] means for k,l st k=$2 & l=$3 holds k<l &
    seq1.l<=seq1.k & for m st k<m & seq1.m<=seq1.k holds l<=m;
A14: for n being Element of NAT for x being Element of NAT
    ex y being Element of NAT st P[n,x,y]
    proof
      let n be Element of NAT,x be Element of NAT;
      defpred X[Nat] means x<$1 & seq1.$1<=seq1.x;
      ex n be Element of NAT st X[n] by A8;
      then
A15:  ex n be Nat st X[n];
      ex l be Nat st X[l] &
      for m be Nat st X[m] holds l <= m from NAT_1:sch 5(A15);
      then consider l be Nat such that
A16:  x<l and
A17:  seq1.l<=seq1.x and
A18:  for m be Nat st x<m & seq1.m<=seq1.x holds l <= m;
      take l;
      l in NAT by ORDINAL1:def 13;
      hence thesis by A16,A17,A18;
    end;
    reconsider 0'=0 as Element of NAT qua non empty set;
    consider f being Function of NAT,NAT such that f.0 = 0' and
A19: for n being Element of NAT holds P[n,f.n,f.(n+1)] from RECDEF_1:sch 2
    (A14);
A20: rng f c= NAT by RELSET_1:12;
    then
A21: rng f c= REAL by XBOOLE_1:1;
A22: dom f =NAT by FUNCT_2:def 1;
    then reconsider f as Real_Sequence by A21,FUNCT_2:def 1,RELSET_1:11;
A23: now
      let n;
      f.n in rng f by A22,FUNCT_1:def 5;
      hence f.n is Element of NAT by A20;
    end;
    now
      let n;
      (f.n is Element of NAT) & f.(n+1) is Element of NAT by A23;
      hence f.n<f.(n+1) by A19;
    end;
    then reconsider f as increasing sequence of NAT by SEQM_3:def 11;
    reconsider Nseq1=Nseq*f as increasing sequence of NAT;
    take Nseq1;
    now
      let n;
      seq1.(f.(n+1))<=seq1.(f.n) by A19;
      then (seq1*f).(n+1)<=seq1.(f.n) by FUNCT_2:21;
      then (seq*Nseq*f).(n+1)<=(seq1*f).n by A7,FUNCT_2:21;
      then (seq*Nseq1).(n+1)<=(seq*Nseq*f).n by A7,RELAT_1:55;
      hence (seq*Nseq1).(n+1)<=(seq*Nseq1).n by RELAT_1:55;
    end;
    then seq*Nseq1 is non-increasing by SEQM_3:def 14;
    hence seq*Nseq1 is monotone by SEQM_3:def 7;
  end;
  now
    assume
A24: for l ex n st n in XN & not n<=l;
    defpred P[set,set,set] means
    for k,l being Element of NAT st k=$2 & l=$3 holds
    l in XN & k<l & for m st m in XN & k<m holds l<=m;
A25: for n being Element of NAT for x being Element of NAT
    ex y being Element of NAT st P[n,x,y]
    proof
      let n be Element of NAT,x be Element of NAT;
      defpred X[Nat] means $1 in XN & x<$1;
      ex n be Element of NAT st X[n] by A24;
      then
A26:  ex n be Nat st X[n];
      ex l be Nat st X[l] &
      for m be Nat st X[m] holds l <= m from NAT_1:sch 5(A26);
      then consider l be Nat such that
A27:  l in XN and
A28:  x<l and
A29:  for m be Nat st m in XN & x<m holds l <= m;
      reconsider l as Element of NAT by ORDINAL1:def 13;
      take l;
      thus P[n,x,l] by A27,A28,A29;
    end;
    consider n1 such that
A30: n1 in XN & not n1<=0 by A24;
    reconsider 0'=n1 as Element of NAT qua non empty set;
    consider f being Function of NAT,NAT such that
A31: f.0 = 0' and
A32: for n being Element of NAT holds P[n,f.n,f.(n+1)] from RECDEF_1:sch 2
    (A25);
A33: rng f c= NAT by RELSET_1:12;
    then
A34: rng f c= REAL by XBOOLE_1:1;
A35: dom f =NAT by FUNCT_2:def 1;
    then reconsider f as Real_Sequence by A34,FUNCT_2:def 1,RELSET_1:11;
A36: now
      let n;
      thus n is Element of NAT;
A37:  f.n in rng f by A35,FUNCT_1:def 5;
      hence f.n is Element of NAT by A33;
      thus f.n is Element of NAT by A33,A37;
    end;
A38: now
      let n;
      defpred X[Element of NAT] means f.$1 in XN;
A39:  X[0] by A30,A31;
A40:  now
        let k such that X[k];
        (f.k is Element of NAT) & f.(k+1) is Element of NAT by A36;
        hence X[k+1] by A32;
      end;
      thus for n holds X[n] from NAT_1:sch 1(A39,A40);
    end;
A41: now
      let n;
      (f.n is Element of NAT) & f.(n+1) is Element of NAT by A36;
      hence f.n<f.(n+1) by A32;
    end;
    then reconsider f as increasing sequence of NAT by SEQM_3:def 11;
    take Nseq=f;
    now
      let n;
A42:  Nseq.n in XN by A38;
      Nseq.n<Nseq.(n+1) by A41;
      then seq.(Nseq.n)<seq.(Nseq.(n+1)) by A1,A42;
      then (seq*Nseq).n<seq.(Nseq.(n+1)) by FUNCT_2:21;
      hence (seq*Nseq).n<(seq*Nseq).(n+1) by FUNCT_2:21;
    end;
    then seq*Nseq is increasing by SEQM_3:def 11;
    hence seq*Nseq is monotone by SEQM_3:def 7;
  end;
  hence thesis by A2;
end;

theorem Th57:
  seq is bounded implies
  ex seq1 st seq1 is subsequence of seq & seq1 is convergent
proof
  assume
A1: seq is bounded;
  consider Nseq such that
A2: seq*Nseq is monotone by Th56;
  take seq1=seq*Nseq;
  thus seq1 is subsequence of seq;
  thus seq1 is convergent by A1,A2,Th53,SEQM_3:58;
end;

theorem :: Cauchy Theorem
  seq is convergent iff
  for s st 0<s ex n st for m st n<=m holds abs(seq.m -seq.n)<s
proof
  thus seq is convergent implies
  for s st 0<s ex n st for m st n<=m holds abs(seq.m -seq.n)<s
  proof
    assume seq is convergent;
    then consider g1 such that
A1: for s st 0<s ex n st for m st n<=m holds abs(seq.m -g1)<s by SEQ_2:def 6;
    let s;
    assume 0<s;
    then consider n1 such that
A2: for m st n1<=m holds abs(seq.m -g1)<s/2 by A1,XREAL_1:217;
    take n=n1;
    let m such that
A3: n<=m;
    abs(seq.n -g1)<s/2 by A2;
    then abs(-(g1-seq.n))<s/2;
    then
A4: abs(g1-seq.n)<s/2 by COMPLEX1:138;
    abs(seq.m -g1)<s/2 by A2,A3;
    then
A5: abs(seq.m -g1)+abs(g1-seq.n)<s/2+s/2 by A4,XREAL_1:10;
    abs(seq.m -g1+(g1-seq.n))<=abs(seq.m -g1)+abs(g1-seq.n) by COMPLEX1:142;
    hence thesis by A5,XXREAL_0:2;
  end;
  assume
A6: for s st 0<s ex n st for m st n<=m holds abs(seq.m -seq.n)<s;
  then consider n1 such that
A7: for m st n1<=m holds abs(seq.m -seq.n1)<1;
  consider r1 such that
A8: 0<r1 and
A9: for m st m<=n1 holds abs(seq.m)<r1 by SEQ_2:16;
  now
    take r=r1+abs(seq.n1)+1;
    0+0<r1+abs(seq.n1) by A8,COMPLEX1:132,XREAL_1:10;
    hence 0<r;
    let m;
A10: now
      assume m<=n1;
      then abs(seq.m)<r1 by A9;
      then abs(seq.m)+0<r1+abs(seq.n1) by COMPLEX1:132,XREAL_1:10;
      hence abs(seq.m)<r by XREAL_1:10;
    end;
    now
      assume n1<=m;
      then
A11:  abs(seq.m -seq.n1)<1 by A7;
      abs(seq.m)-abs(seq.n1)<=abs(seq.m -seq.n1) by COMPLEX1:145;
      then abs(seq.m)-abs(seq.n1)<1 by A11,XXREAL_0:2;
      then abs(seq.m)-abs(seq.n1)+abs(seq.n1)<1+abs(seq.n1) by XREAL_1:8;
      then 0+abs(seq.m)<r1+(abs(seq.n1)+1) by A8,XREAL_1:10;
      hence abs(seq.m)<r;
    end;
    hence abs(seq.m)<r by A10;
  end;
  then seq is bounded by SEQ_2:15;
  then consider seq1 such that
A12: seq1 is subsequence of seq and
A13: seq1 is convergent by Th57;
  consider g1 such that
A14: for s st 0<s ex n st for m st n<=m holds abs(seq1.m -g1)<s
  by A13,SEQ_2:def 6;
  consider Nseq such that
A15: seq1=seq*Nseq by A12,VALUED_0:def 17;
  take g=g1;
  let s;
  assume
Z:  0<s;
  then consider n1 such that
A17: for m st n1<=m holds abs(seq1.m -g1)<s/3 by A14,XREAL_1:224;
  consider n2 such that
A18: for m st n2<=m holds abs(seq.m -seq.n2)<s/3 by A6,Z,XREAL_1:224;
  take n=n1+n2;
  let m such that
A19: n<=m;
  n1<=n by NAT_1:12;
  then n1<=m by A19,XXREAL_0:2;
  then abs((seq*Nseq).m -g1)<s/3 by A15,A17;
  then
A20: abs(seq.(Nseq.m) -g1)<s/3 by FUNCT_2:21;
  n2<=n by NAT_1:12;
  then
A21: n2<=m by A19,XXREAL_0:2;
  then
A22: abs(seq.m -seq.n2)<s/3 by A18;
  m<=Nseq.m by SEQM_3:33;
  then n2<=Nseq.m by A21,XXREAL_0:2;
  then abs(seq.(Nseq.m) -seq.n2)<s/3 by A18;
  then abs(-(seq.n2-seq.(Nseq.m)))<s/3;
  then abs(seq.n2-seq.(Nseq.m))<s/3 by COMPLEX1:138;
  then
A23: abs(seq.m -seq.n2)+abs(seq.n2-seq.(Nseq.m))<s/3+s/3 by A22,XREAL_1:10;
  abs(seq.m -seq.n2+(seq.n2-seq.(Nseq.m)))<=
  abs(seq.m -seq.n2)+abs(seq.n2-seq.(Nseq.m)) by COMPLEX1:142;
  then abs(seq.m -seq.(Nseq.m))<s/3+s/3 by A23,XXREAL_0:2;
  then
A24: abs(seq.m -seq.(Nseq.m))+abs(seq.(Nseq.m) -g1)<s/3+s/3+s/3
  by A20,XREAL_1:10;
  abs(seq.m -seq.(Nseq.m)+(seq.(Nseq.m) -g1))<=
  abs(seq.m -seq.(Nseq.m))+abs(seq.(Nseq.m) -g1) by COMPLEX1:142;
  hence abs(seq.m -g)<s by A24,XXREAL_0:2;
end;

theorem
  seq is constant & seq1 is convergent implies
  lim (seq+seq1) =(seq.0) + lim seq1 & lim (seq-seq1) =(seq.0) - lim seq1 &
  lim (seq1-seq) =(lim seq1) -seq.0 & lim (seq(#)seq1) =(seq.0) * (lim seq1)
proof
  assume that
A1: seq is constant and
A2: seq1 is convergent;
  thus lim (seq+seq1)=(lim seq)+(lim seq1) by A2,A1,SEQ_2:20
    .=(seq.0)+(lim seq1) by A1,Th40;
  thus lim (seq-seq1)=(lim seq)-(lim seq1) by A2,A1,SEQ_2:26
    .=(seq.0)-(lim seq1) by A1,Th40;
  thus lim (seq1-seq)=(lim seq1)-(lim seq) by A2,A1,SEQ_2:26
    .=(lim seq1)-(seq.0) by A1,Th40;
  thus lim (seq(#)seq1)=(lim seq)*(lim seq1) by A2,A1,SEQ_2:29
    .=(seq.0)*(lim seq1) by A1,Th40;
end;

begin :: Addenda

:: from PSCOMP1

theorem Th60:
  for X being non empty real-membered set
  for t st for s st s in X holds s >= t holds lower_bound X >= t
proof
  let X be non empty real-membered set;
  set r = lower_bound X;
  let t;
  assume
A1: for s st s in X holds s >= t;
  then
A2: X is bounded_below by Def2;
  set s = t-r;
  assume r < t;
  then s > 0 by XREAL_1:52;
  then consider t' be real number such that
A3: t' in X & t' < r+s by A2,Def5;
  thus contradiction by A1,A3;
end;

theorem
  for X being non empty real-membered set st (for s st s in X holds s >= r) &
  for t st for s st s in X holds s >= t holds r >= t holds r = lower_bound X
                 by Lm61;

theorem Th62:
  for X being non empty real-membered set, r
  for t st for s st s in X holds s <= t holds upper_bound X <= t
proof
  let X be non empty real-membered set, r;
  set r = upper_bound X;
  let t;
  assume
A1: for s st s in X holds s <= t;
  then
A2: X is bounded_above by Def1;
  set s = r-t;
  assume r > t;
  then s > 0 by XREAL_1:52;
  then consider t' be real number such that
A3: t' in X & r-s < t' by A2,Def4;
  thus contradiction by A1,A3;
end;

theorem
  for X being non empty real-membered set, r
  st (for s st s in X holds s <= r) &
  for t st for s st s in X holds s <= t holds r <= t holds r = upper_bound X
                 by Lm63;

theorem
  for X being non empty real-membered set, Y being real-membered set
  st X c= Y & Y is bounded_below holds lower_bound Y <= lower_bound X
proof
  let X be non empty real-membered set, Y be real-membered set;
  assume that
A1: X c= Y and
A2: Y is bounded_below;
  t in X implies t >= lower_bound Y by A1,A2,Def5;
  hence thesis by Th60;
end;

theorem
  for X being non empty real-membered set, Y being real-membered set
  st X c= Y & Y is bounded_above holds upper_bound X <= upper_bound Y
proof
  let X be non empty real-membered set, Y be real-membered set;
  assume that
A1: X c= Y and
A2: Y is bounded_above;
  t in X implies t <= upper_bound Y by A1,A2,Def4;
  hence upper_bound X <= upper_bound Y by Th62;
end;

:: from CQC_SIM1, 2007.03.15, A.T.

definition
  let A be non empty natural-membered set;
  redefine func min A -> Element of NAT;
  coherence by ORDINAL1:def 13;
end;

:: from SCMFSA9A, 2007.07.26, A.T.

scheme MinPred { F(Element of NAT)->Element of NAT, P[set]} :
  ex k st P[k] & for n st P[n] holds k <= n
provided
A1: for k holds F(k+1) < F(k) or P[k]
proof
  now
    assume
A2: for k holds not P[k];
    consider f being Function of NAT, NAT such that
A3: for k holds f.k = F(k) from FUNCT_2:sch 4;
    f.0 in rng f by FUNCT_2:6;
    then reconsider rf = rng f as non empty Subset of NAT by RELSET_1:12;
    set m = min rf;
    m in rf by XXREAL_2:def 7;
    then consider x being set such that
A4: x in dom f & m = f.x by FUNCT_1:def 5;
    reconsider x as Element of NAT by A4,FUNCT_2:def 1;
    f.x = F(x) & f.(x+1) = F(x+1) by A3;
    then
A5: f.(x+1) < f.x or P[x] by A1;
    f.(x+1) in rf by FUNCT_2:6;
    hence contradiction by A2,A4,A5,XXREAL_2:def 7;
  end;
  then
A6: ex k be Nat st P[k];
  consider k be Nat such that
A7: P[k] and
A8: for n be Nat st P[n] holds k <= n from NAT_1:sch 5(A6);
A9: k in NAT by ORDINAL1:def 13;
  for n be Element of NAT st P[n] holds k <= n by A8;
  hence thesis by A7,A9;
end;

