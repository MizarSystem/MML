:: Again on the Order on a Special Polygon
::  by Andrzej Trybulec and Yatsuka Nakamura
::
:: Received October 16, 2000
:: Copyright (c) 2000 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XBOOLE_0, FINSEQ_1, FINSEQ_6, SUBSET_1, RELAT_1,
      FINSEQ_4, XXREAL_0, FINSEQ_5, ARYTM_1, ARYTM_3, ORDINAL4, RFINSEQ,
      PARTFUN1, FUNCT_1, GOBOARD5, PRE_TOPC, EUCLID, CARD_1, PSCOMP_1,
      TOPREAL1, SPRECT_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0, FUNCT_1,
      PARTFUN1, FINSEQ_1, FINSEQ_4, FINSEQ_5, RFINSEQ, FINSEQ_6, NAT_1, NAT_D,
      PRE_TOPC, EUCLID, TOPREAL1, GOBOARD5, PSCOMP_1, SPRECT_2;
 constructors XXREAL_0, FINSEQ_4, RFINSEQ, FINSEQ_5, GOBOARD5, PSCOMP_1,
      SPRECT_2, NAT_D, RELSET_1;
 registrations XBOOLE_0, RELSET_1, XREAL_0, FINSEQ_6, STRUCT_0, SPRECT_2,
      REVROT_1, ORDINAL1, FUNCT_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions FUNCT_1;
 theorems FINSEQ_6, FINSEQ_4, FINSEQ_5, FINSEQ_3, NAT_1, FINSEQ_1, TARSKI,
      GOBOARD7, RFINSEQ, REVROT_1, SPRECT_2, XBOOLE_0, XREAL_1, XXREAL_0,
      PARTFUN1, CARD_1;

begin

reserve D for non empty set,
  f for FinSequence of D,
  g for circular FinSequence of D,
  p,p1,p2,p3,q for Element of D;

theorem Th1:
  q in rng(f|(p..f)) implies q..f <= p..f
proof
  assume
A1: q in rng(f|(p..f));
  then q..(f|(p..f)) = q..f by FINSEQ_5:43;
  then
A2: q..f <= len(f|(p..f)) by A1,FINSEQ_4:31;
  len(f|(p..f)) <= p..f by FINSEQ_5:19;
  hence thesis by A2,XXREAL_0:2;
end;

theorem Th2:
  p in rng f & q in rng f & p..f <= q..f implies q..(f:-p) = q..f - p..f + 1
proof
  assume that
A1: p in rng f and
A2: q in rng f and
A3: p..f <= q..f;
A4: f = (f|(p..f))^(f/^(p..f)) by RFINSEQ:21;
  per cases by A3,XXREAL_0:1;
  suppose
    p..f = q..f;
    then p = q by A1,A2,FINSEQ_5:10;
    hence thesis by FINSEQ_6:84;
  end;
  suppose
A5: p..f < q..f;
    p..f <= len f by A1,FINSEQ_4:31;
    then
A6: len(f|(p..f)) = p..f by FINSEQ_1:80;
A7: not q in rng(f|(p..f)) by A5,Th1;
    q in rng(f|(p..f)) \/ rng(f/^(p..f)) by A2,A4,FINSEQ_1:44;
    then
A8: q in rng(f/^(p..f)) by A7,XBOOLE_0:def 3;
    then q in rng(f/^(p..f)) \ rng(f|(p..f)) by A7,XBOOLE_0:def 5;
    then
A9: q..f = p..f + q..(f/^(p..f)) by A4,A6,FINSEQ_6:9;
    not q in {p} by A5,TARSKI:def 1;
    then not q in rng<*p*> by FINSEQ_1:55;
    then
A10: q in rng(f/^(p..f)) \ rng<*p*> by A8,XBOOLE_0:def 5;
    f:-p = <*p*>^(f/^p..f) by FINSEQ_5:def 2;
    hence q..(f:-p) = len<*p*> + q..(f/^(p..f)) by A10,FINSEQ_6:9
      .= q..f - p..f + 1 by A9,FINSEQ_1:56;
  end;
end;

theorem Th3:
  p in rng f & q in rng f & p..f <= q..f implies p..(f-:q) = p..f
proof
A1: f-:q = f|(q..f) by FINSEQ_5:def 1;
  assume p in rng f & q in rng f & p..f <= q..f;
  then p in rng(f|(q..f)) by A1,FINSEQ_5:49;
  hence thesis by A1,FINSEQ_5:43;
end;

theorem Th4:
  p in rng f & q in rng f & p..f <= q..f implies q..Rotate(f,p) = q
  ..f - p..f + 1
proof
  assume that
A1: p in rng f and
A2: q in rng f;
A3: Rotate(f,p) = (f:-p)^((f-:p)/^1) by A1,FINSEQ_6:def 2;
  assume
A4: p..f <= q..f;
  then q in rng(f:-p) by A1,A2,FINSEQ_6:67;
  hence q..Rotate(f,p) = q..(f:-p) by A3,FINSEQ_6:8
    .= q..f - p..f + 1 by A1,A2,A4,Th2;
end;

theorem Th5:
  p1 in rng f & p2 in rng f & p3 in rng f & p1..f <= p2..f & p2..f
  < p3..f implies p2..Rotate(f,p1) < p3..Rotate(f,p1)
proof
  assume that
A1: p1 in rng f & p2 in rng f & p3 in rng f & p1..f <= p2..f and
A2: p2..f < p3..f;
A3: p2..f - p1..f < p3..f - p1..f by A2,XREAL_1:11;
  p2..Rotate(f,p1) = p2..f - p1..f + 1 & p3..Rotate(f,p1) = p3..f - p1..f
  + 1 by A1,A2,Th4,XXREAL_0:2;
  hence thesis by A3,XREAL_1:8;
end;

theorem
  p1 in rng f & p2 in rng f & p3 in rng f & p1..f < p2..f & p2..f <= p3
  ..f implies p2..Rotate(f,p1) <= p3..Rotate(f,p1)
proof
  assume that
A1: p1 in rng f and
A2: p2 in rng f & p3 in rng f and
A3: p1..f < p2..f and
A4: p2..f <= p3..f;
  per cases by A4,XXREAL_0:1;
  suppose
    p2..f < p3..f;
    hence thesis by A1,A2,A3,Th5;
  end;
  suppose
    p2..f = p3..f;
    hence thesis by A2,FINSEQ_5:10;
  end;
end;

theorem Th7:
  p in rng g & len g > 1 implies p..g < len g
proof
  assume that
A1: p in rng g and
A2: len g > 1 and
A3: p..g >= len g;
  p..g <= len g by A1,FINSEQ_4:31;
  then p..g = len g by A3,XXREAL_0:1;
  then
A4: p = g/.len g by A1,FINSEQ_5:41
    .= g/.1 by FINSEQ_6:def 1;
  g is non empty by A2,CARD_1:47;
  hence contradiction by A2,A3,A4,FINSEQ_6:47;
end;

begin :: Ordering of special points on a standard special sequence

reserve f for non constant standard special_circular_sequence,
  p,p1,p2,p3,q for Point of TOP-REAL 2;

canceled;

registration
  let f;
  cluster f/^1 -> one-to-one;
coherence
proof
  let x1,x2 be set such that
A1: x1 in dom(f/^1) and
A2: x2 in dom (f/^1) and
A3: (f/^1).x1 = (f/^1).x2;
  reconsider i=x1, j=x2 as Element of NAT by A1,A2;
  1 <= i by A1,FINSEQ_3:27;
  then
A4: 1 < i+1 by NAT_1:13;
  i+1 in dom f by A1,FINSEQ_5:29;
  then
A5: i+1 <= len f by FINSEQ_3:27;
  1 <= j by A2,FINSEQ_3:27;
  then
A6: 1 < j+1 by NAT_1:13;
  j+1 in dom f by A2,FINSEQ_5:29;
  then
A7: j+1 <= len f by FINSEQ_3:27;
  assume
A8: x1 <> x2;
  per cases by A8,XXREAL_0:1;
  suppose
    i < j;
    then
A9: i+1 < j+1 by XREAL_1:8;
    f/.(i+1) = (f/^1)/.i by A1,FINSEQ_5:30
      .= (f/^1).j by A1,A3,PARTFUN1:def 8
      .= (f/^1)/.j by A2,PARTFUN1:def 8
      .= f/.(j+1) by A2,FINSEQ_5:30;
    hence contradiction by A4,A7,A9,GOBOARD7:39;
  end;
  suppose
    j < i;
    then
A10: j+1 < i+1 by XREAL_1:8;
    f/.(j+1) = (f/^1)/.j by A2,FINSEQ_5:30
      .= (f/^1).i by A2,A3,PARTFUN1:def 8
      .= (f/^1)/.i by A1,PARTFUN1:def 8
      .= f/.(i+1) by A1,FINSEQ_5:30;
    hence contradiction by A5,A6,A10,GOBOARD7:39;
  end;
end;
end;

theorem Th9:
  1 < q..f & q in rng f implies (f/.1)..Rotate(f,q) = len f + 1 - q ..f
proof
  assume that
A1: 1 < q..f and
A2: q in rng f;
  set i = 1 + len f -' q..f;
A3: q..f - 1 > 0 by A1,XREAL_1:52;
A4: q..f <= len f by A2,FINSEQ_4:31;
  then
A5: q..f <= len f + 1 by NAT_1:13;
  then
A6: i = 1 + len f - q..f by XREAL_1:235;
  then i +(q..f - 1) = len f;
  then i < len f by A3,XREAL_1:31;
  then
A7: i < len Rotate(f,q) by REVROT_1:14;
  now
    assume q..f + 0 >= len f;
    then
A8: q..f = len f by A4,XXREAL_0:1;
    q = f/.(q..f) by A2,FINSEQ_5:41
      .= f/.1 by A8,FINSEQ_6:def 1;
    hence contradiction by A1,FINSEQ_6:47;
  end;
  then
A9: len f - q..f > 0 by XREAL_1:22;
  i = 1 + (len f - q..f) by A6;
  then
A10: 1 + 0 < i by A9,XREAL_1:8;
  then
A11: i in dom Rotate(f,q) by A7,FINSEQ_3:27;
A12: f/.1 = Rotate(f,q)/.(1 + len f -' q..f) by A1,A2,REVROT_1:18;
  then
A13: f/.1 = Rotate(f,q).i by A11,PARTFUN1:def 8;
  for j being set st j in dom Rotate(f,q) & j <> i holds Rotate(f,q).j <> f/.1
  proof
    let z be set;
    assume that
A14: z in dom Rotate(f,q) and
A15: z <> i;
    reconsider j = z as Element of NAT by A14;
    per cases by A15,XXREAL_0:1;
    suppose
A16:  j < i;
      1 <= j by A14,FINSEQ_3:27;
      then Rotate(f,q)/.j <> f/.1 by A12,A7,A16,GOBOARD7:38;
      hence thesis by A14,PARTFUN1:def 8;
    end;
    suppose
A17:  i < j;
      j <= len Rotate(f,q) by A14,FINSEQ_3:27;
      then Rotate(f,q)/.j <> f/.1 by A12,A10,A17,GOBOARD7:39;
      hence thesis by A14,PARTFUN1:def 8;
    end;
  end;
  then
A18: Rotate(f,q) just_once_values f/.1 by A11,A13,FINSEQ_4:9;
  then 1 + len f -' q..f = Rotate(f,q) <- (f/.1) by A11,A13,
  FINSEQ_4:def 3;
  hence f/.1..Rotate(f,q) = 1 + len f -' q..f by A18,FINSEQ_4:35
    .= len f + 1 - q..f by A5,XREAL_1:235;
end;

theorem Th10:
  p in rng f & q in rng f & p..f < q..f implies p..Rotate(f,q) =
  len f + p..f - q..f
proof
  assume that
A1: p in rng f and
A2: q in rng f;
  assume
A3: p..f < q..f;
  then
A4: p..f = p..(f-:q) by A1,A2,Th3;
A5: p in rng(f-:q) by A1,A2,A3,FINSEQ_5:49;
  then
A6: p..(f-:q) >= 1 by FINSEQ_4:31;
A7: Rotate(f,q) = (f:-q)^((f-:q)/^1) by A2,FINSEQ_6:def 2;
  per cases by A6,A4,XXREAL_0:1;
  suppose
A8: p..f = 1;
    then p = f/.1 by A1,FINSEQ_5:41;
    hence thesis by A2,A3,A8,Th9;
  end;
  suppose
A9: p..f > 1;
    then
A10: p..f = 1 + p..((f-:q)/^1) by A5,A4,FINSEQ_6:61;
    not p in {q} by A3,TARSKI:def 1;
    then
A11: not p in rng<*q*> by FINSEQ_1:55;
A12: q in rng(f/^1) by A2,A3,A9,FINSEQ_6:83;
    then ((f/^1) -| q)^<*q*> = (f/^1)|(q..(f/^1)) by FINSEQ_5:27
      .= (f/^1)-:q by FINSEQ_5:def 1;
    then
A13: rng((f/^1)-:q) = rng((f/^1) -| q) \/ rng<*q*> by FINSEQ_1:44;
A14: rng((f/^1) -| q) misses rng((f/^1) |-- q) by A12,FINSEQ_4:72;
    (f/^1):-q = <*q*>^((f/^1) |-- q) by A12,FINSEQ_6:45;
    then
A15: rng((f/^1):-q) = rng<*q*> \/ rng((f/^1) |-- q) by FINSEQ_1:44;
    p..(f-:q) > 1 by A1,A2,A3,A9,Th3;
    then
A16: p in rng((f-:q)/^1) by A5,FINSEQ_6:62;
    then p in rng((f/^1)-:q) by A2,A3,A9,FINSEQ_6:65;
    then p in rng((f/^1) -| q) by A13,A11,XBOOLE_0:def 3;
    then not p in rng((f/^1) |-- q) by A14,XBOOLE_0:3;
    then not p in rng((f/^1):-q) by A11,A15,XBOOLE_0:def 3;
    then not p in rng(f:-q) by A2,A3,A9,FINSEQ_6:89;
    then p in rng((f-:q)/^1) \ rng(f:-q) by A16,XBOOLE_0:def 5;
    hence p..Rotate(f,q) = len(f:-q) + p..((f-:q)/^1) by A7,FINSEQ_6:9
      .= len f - q..f + 1 + (p..f - 1) by A2,A10,FINSEQ_5:53
      .= len f + p..f - q..f;
  end;
end;

theorem Th11:
  p1 in rng f & p2 in rng f & p3 in rng f & p1..f < p2..f & p2..f
  < p3..f implies p3..Rotate(f,p2) < p1..Rotate(f,p2)
proof
  assume that
A1: p1 in rng f and
A2: p2 in rng f and
A3: p3 in rng f and
A4: p1..f < p2..f and
A5: p2..f < p3..f;
A6: p1 in rng Rotate(f,p2) & p3 in rng Rotate(f,p2) by A1,A2,A3,FINSEQ_6:96;
  1 <= p1..f & p3..f <= len f by A1,A3,FINSEQ_4:31;
  then
A7: p3..f + 1 <= len f + p1..f by XREAL_1:9;
A8: p3..Rotate(f,p2) = p3..f - p2..f + 1 by A2,A3,A5,Th4
    .= p3..f + 1 - p2..f;
  p1..Rotate(f,p2) = len f + p1..f - p2..f by A1,A2,A4,Th10;
  then p3..Rotate(f,p2) <= p1..Rotate(f,p2) by A8,A7,XREAL_1:11;
  hence thesis by A4,A5,A6,FINSEQ_5:10,XXREAL_0:1;
end;

theorem Th12:
  p1 in rng f & p2 in rng f & p3 in rng f & p1..f < p2..f & p2..f
  < p3..f implies p1..Rotate(f,p3) < p2..Rotate(f,p3)
proof
  assume that
A1: p1 in rng f and
A2: p2 in rng f and
A3: p3 in rng f and
A4: p1..f < p2..f & p2..f < p3..f;
  p1..f < p3..f by A4,XXREAL_0:2;
  then
A5: p1..Rotate(f,p3) = len f + p1..f - p3..f by A1,A3,Th10;
  p2..Rotate(f,p3) = len f + p2..f - p3..f & len f + p1..f < len f + p2..f
  by A2,A3,A4,Th10,XREAL_1:8;
  hence thesis by A5,XREAL_1:11;
end;

theorem
  p1 in rng f & p2 in rng f & p3 in rng f & p1..f <= p2..f & p2..f < p3
  ..f implies p1..Rotate(f,p3) <= p2..Rotate(f,p3)
proof
  assume that
A1: p1 in rng f & p2 in rng f and
A2: p3 in rng f and
A3: p1..f <= p2..f and
A4: p2..f < p3..f;
  per cases by A3,XXREAL_0:1;
  suppose
    p1..f < p2..f;
    hence thesis by A1,A2,A4,Th12;
  end;
  suppose
    p1..f = p2..f;
    hence thesis by A1,FINSEQ_5:10;
  end;
end;

theorem
  (S-min L~f)..f < len f
proof
  S-min L~f in rng f & len f > 1 by GOBOARD7:36,SPRECT_2:45,XXREAL_0:2;
  hence thesis by Th7;
end;

theorem
  (S-max L~f)..f < len f
proof
  S-max L~f in rng f & len f > 1 by GOBOARD7:36,SPRECT_2:46,XXREAL_0:2;
  hence thesis by Th7;
end;

theorem
  (E-min L~f)..f < len f
proof
  E-min L~f in rng f & len f > 1 by GOBOARD7:36,SPRECT_2:49,XXREAL_0:2;
  hence thesis by Th7;
end;

theorem
  (E-max L~f)..f < len f
proof
  E-max L~f in rng f & len f > 1 by GOBOARD7:36,SPRECT_2:50,XXREAL_0:2;
  hence thesis by Th7;
end;

theorem
  (N-min L~f)..f < len f
proof
  N-min L~f in rng f & len f > 1 by GOBOARD7:36,SPRECT_2:43,XXREAL_0:2;
  hence thesis by Th7;
end;

theorem
  (N-max L~f)..f < len f
proof
  N-max L~f in rng f & len f > 1 by GOBOARD7:36,SPRECT_2:44,XXREAL_0:2;
  hence thesis by Th7;
end;

theorem
  (W-max L~f)..f < len f
proof
  W-max L~f in rng f & len f > 1 by GOBOARD7:36,SPRECT_2:48,XXREAL_0:2;
  hence thesis by Th7;
end;

theorem
  (W-min L~f)..f < len f
proof
  W-min L~f in rng f & len f > 1 by GOBOARD7:36,SPRECT_2:47,XXREAL_0:2;
  hence thesis by Th7;
end;

begin :: Ordering of special points on a clockwise oriented sequence

reserve z for clockwise_oriented (non constant standard
  special_circular_sequence);

Lm1: z/.1 = N-min L~z implies (E-max L~z)..z < (S-max L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then
 (E-max L~z)..z < (E-min L~z)..z by SPRECT_2:75;
    hence thesis by A1,SPRECT_2:76,XXREAL_0:2;
end;

Lm2: z/.1 = N-min L~z implies (E-max L~z)..z < (S-min L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then (E-max L~z)..z < (S-max L~z)..z by Lm1;
  hence thesis by A1,SPRECT_2:77,XXREAL_0:2;
end;

Lm3: z/.1 = N-min L~z implies (E-max L~z)..z < (W-min L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then
 (E-max L~z)..z < (S-min L~z)..z by Lm2;
    hence thesis by A1,SPRECT_2:78,XXREAL_0:2;
end;

Lm4: z/.1 = N-min L~z implies (E-min L~z)..z < (S-min L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then
 (S-max L~z)..z < (S-min L~z)..z by SPRECT_2:77;
    hence thesis by A1,SPRECT_2:76,XXREAL_0:2;
end;

Lm5: z/.1 = N-min L~z implies (E-min L~z)..z < (W-min L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then
 (E-min L~z)..z < (S-min L~z)..z by Lm4;
    hence thesis by A1,SPRECT_2:78,XXREAL_0:2;
end;

Lm6: z/.1 = N-min L~z implies (S-max L~z)..z < (W-min L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then
 (S-max L~z)..z < (S-min L~z)..z by SPRECT_2:77;
    hence thesis by A1,SPRECT_2:78,XXREAL_0:2;
end;

Lm7: z/.1 = N-min L~z implies (N-max L~z)..z < (W-min L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then
 (E-max L~z)..z < (W-min L~z)..z by Lm3;
    hence thesis by A1,SPRECT_2:74,XXREAL_0:2;
end;

Lm8: z/.1 = N-min L~z implies (N-min L~z)..z < (W-min L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then (N-max L~z)..z < (W-min L~z)..z by Lm7;
  hence thesis by A1,SPRECT_2:72,XXREAL_0:2;
end;

Lm9: z/.1 = N-min L~z implies (N-max L~z)..z < (S-max L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then (N-max L~z)..z <= (E-max L~z)..z by SPRECT_2:74;
  hence thesis by A1,Lm1,XXREAL_0:2;
end;

Lm10: z/.1 = N-min L~z implies (N-max L~z)..z < (S-min L~z)..z
proof
  assume
A1: z/.1 = N-min L~z;
  then (N-max L~z)..z < (S-max L~z)..z by Lm9;
  hence thesis by A1,SPRECT_2:77,XXREAL_0:2;
end;

theorem Th22:
  f/.1 = W-min L~f implies (W-min L~f)..f < (W-max L~f)..f
proof
  assume f/.1 = W-min L~f;
  then
A1: (W-min L~f)..f = 1 by FINSEQ_6:47;
A2: W-max L~f in rng f by SPRECT_2:48;
  then (W-max L~f)..f in dom f by FINSEQ_4:30;
  then
A3: (W-max L~f)..f >= 1 by FINSEQ_3:27;
  W-min L~f in rng f by SPRECT_2:47;
  then (W-min L~f)..f <> (W-max L~f)..f by A2,FINSEQ_5:10,SPRECT_2:62;
  hence thesis by A3,A1,XXREAL_0:1;
end;

theorem
  f/.1 = W-min L~f implies (W-max L~f)..f > 1
proof
  assume
A1: f/.1 = W-min L~f;
  then (W-min L~f)..f = 1 by FINSEQ_6:47;
  hence thesis by A1,Th22;
end;

theorem Th24:
  z/.1 = W-min L~z & W-max L~z <> N-min L~z implies (W-max L~z)..z
  < (N-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A3: (W-min L~g)..g > (N-min L~g)..g by Lm8;
  assume that
A4: z/.1 = W-min L~z and
A5: W-max L~z <> N-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,W-min L~z) = z by A4,REVROT_1:16;
A7: N-min L~g in rng g & W-max L~g in rng g by SPRECT_2:43,48;
  W-min L~g in rng g & (W-min L~g)..g < (W-max L~g)..g by A1,A5,A2,
  SPRECT_2:47,79;
  hence thesis by A1,A6,A7,A3,Th11;
end;

theorem Th25:
  z/.1 = W-min L~z implies (N-min L~z)..z < (N-max L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A2: (N-min L~g)..g < (N-max L~g)..g & (N-max L~g)..g < (W-min L~g)..g by
  Lm7,SPRECT_2:72;
A3: W-min L~g in rng g by SPRECT_2:47;
  assume
A4: z/.1 = W-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,W-min L~z) = z by A4,REVROT_1:16;
  N-min L~g in rng g & N-max L~g in rng g by SPRECT_2:43,44;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem Th26:
  z/.1 = W-min L~z & N-max L~z <> E-max L~z implies (N-max L~z)..z
  < (E-max L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A3: (E-max L~g)..g < (W-min L~g)..g by Lm3;
  assume that
A4: z/.1 = W-min L~z and
A5: N-max L~z <> E-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,W-min L~z) = z by A4,REVROT_1:16;
A7: N-max L~g in rng g & E-max L~g in rng g by SPRECT_2:44,50;
  W-min L~g in rng g & (N-max L~g)..g < (E-max L~g)..g by A1,A5,A2,
  SPRECT_2:47,74;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem Th27:
  z/.1 = W-min L~z implies (E-max L~z)..z < (E-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A2: (E-max L~g)..g < (E-min L~g)..g & (E-min L~g)..g < (W-min L~g)..g by
  Lm5,SPRECT_2:75;
A3: W-min L~g in rng g by SPRECT_2:47;
  assume
A4: z/.1 = W-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,W-min L~z) = z by A4,REVROT_1:16;
  E-min L~g in rng g & E-max L~g in rng g by SPRECT_2:49,50;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem Th28:
  z/.1 = W-min L~z & E-min L~z <> S-max L~z implies (E-min L~z)..z
  < (S-max L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A3: (S-max L~g)..g < (W-min L~g)..g by Lm6;
  assume that
A4: z/.1 = W-min L~z and
A5: E-min L~z <> S-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,W-min L~z) = z by A4,REVROT_1:16;
A7: E-min L~g in rng g & S-max L~g in rng g by SPRECT_2:46,49;
  W-min L~g in rng g & (E-min L~g)..g < (S-max L~g)..g by A1,A5,A2,
  SPRECT_2:47,76;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem
  z/.1 = W-min L~z & S-min L~z <> W-min L~z implies (S-max L~z)..z < (
  S-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  assume
A2: z/.1 = W-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A3: Rotate(g,W-min L~z) = z by A2,REVROT_1:16;
A4: S-min L~g in rng g & S-max L~g in rng g by SPRECT_2:45,46;
  N-min L~z in rng z by SPRECT_2:43;
  then
A5: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A6: W-min L~g in rng g & (S-max L~g)..g < (S-min L~g)..g by
  SPRECT_2:47,
  77;
  assume S-min L~z <> W-min L~z;
  then (S-min L~g)..g < (W-min L~g)..g by A1,A5,SPRECT_2:78;
  hence thesis by A1,A3,A4,A6,Th12;
end;

theorem Th30:
  f/.1 = S-max L~f implies (S-max L~f)..f < (S-min L~f)..f
proof
  assume f/.1 = S-max L~f;
  then
A1: (S-max L~f)..f = 1 by FINSEQ_6:47;
A2: S-min L~f in rng f by SPRECT_2:45;
  then (S-min L~f)..f in dom f by FINSEQ_4:30;
  then
A3: (S-min L~f)..f >= 1 by FINSEQ_3:27;
  S-max L~f in rng f by SPRECT_2:46;
  then (S-max L~f)..f <> (S-min L~f)..f by A2,FINSEQ_5:10,SPRECT_2:60;
  hence thesis by A3,A1,XXREAL_0:1;
end;

theorem
  f/.1 = S-max L~f implies (S-min L~f)..f > 1
proof
  assume
A1: f/.1 = S-max L~f;
  then (S-max L~f)..f = 1 by FINSEQ_6:47;
  hence thesis by A1,Th30;
end;

Lm11: z/.1 = W-min L~z implies (E-max L~z)..z < (S-max L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then
 (E-max L~z)..z < (E-min L~z)..z by Th27;
    hence thesis by A1,Th28,XXREAL_0:2;
end;

Lm12: z/.1 = W-min L~z implies (N-min L~z)..z < (E-max L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then
 (N-min L~z)..z < (N-max L~z)..z by Th25;
    hence thesis by A1,Th26,XXREAL_0:2;
end;

Lm13: z/.1 = W-min L~z implies (N-min L~z)..z < (S-max L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then (N-min L~z)..z < (E-max L~z)..z by Lm12;
  hence thesis by A1,Lm11,XXREAL_0:2;
end;

Lm14: z/.1 = W-min L~z implies (N-max L~z)..z < (S-max L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then
 (E-max L~z)..z < (S-max L~z)..z by Lm11;
    hence thesis by A1,Th26,XXREAL_0:2;
end;

Lm15: z/.1 = W-min L~z implies (W-max L~z)..z < (S-max L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then
 (N-min L~z)..z < (S-max L~z)..z by Lm13;
    hence thesis by A1,Th24,XXREAL_0:2;
end;

Lm16: z/.1 = W-min L~z implies (N-max L~z)..z < (E-min L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then
 (E-max L~z)..z < (E-min L~z)..z by Th27;
    hence thesis by A1,Th26,XXREAL_0:2;
end;

Lm17: z/.1 = W-min L~z implies (N-min L~z)..z < (E-max L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then
 (N-min L~z)..z < (N-max L~z)..z by Th25;
    hence thesis by A1,Th26,XXREAL_0:2;
end;

Lm18: z/.1 = W-min L~z implies (W-max L~z)..z < (E-max L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then (W-max L~z)..z <= (N-min L~z)..z by Th24;
  hence thesis by A1,Lm17,XXREAL_0:2;
end;

Lm19: z/.1 = W-min L~z implies (W-max L~z)..z < (E-min L~z)..z
proof
  assume
A1: z/.1 = W-min L~z;
  then (W-max L~z)..z < (E-max L~z)..z by Lm18;
  hence thesis by A1,Th27,XXREAL_0:2;
end;

theorem Th32:
  z/.1 = S-max L~z & S-min L~z <> W-min L~z implies (S-min L~z)..z
  < (W-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A3: S-min L~g in rng g & (S-max L~g)..g < (S-min L~g)..g by
  SPRECT_2:45,
  77;
  assume that
A4: z/.1 = S-max L~z and
A5: S-min L~z <> W-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,S-max L~z) = z by A4,REVROT_1:16;
A7: W-min L~g in rng g & S-max L~g in rng g by SPRECT_2:46,47;
  (S-min L~g)..g < (W-min L~g)..g by A1,A5,A2,SPRECT_2:78;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem Th33:
  z/.1 = S-max L~z implies (W-min L~z)..z < (W-max L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A2: (S-max L~g)..g > (W-max L~g)..g & (W-min L~g)..g < (W-max L~g)..g by
  Lm15,Th22;
A3: W-min L~g in rng g by SPRECT_2:47;
  assume
A4: z/.1 = S-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,S-max L~z) = z by A4,REVROT_1:16;
  S-max L~g in rng g & W-max L~g in rng g by SPRECT_2:46,48;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem Th34:
  z/.1 = S-max L~z & W-max L~z <> N-min L~z implies (W-max L~z)..z
  < (N-min L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then
A2: g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A3: (N-min L~g)..g < (S-max L~g)..g by Lm13;
  assume that
A4: z/.1 = S-max L~z and
A5: W-max L~z <> N-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,S-max L~z) = z by A4,REVROT_1:16;
A7: W-max L~g in rng g & N-min L~g in rng g by SPRECT_2:43,48;
  S-max L~g in rng g & (W-max L~g)..g < (N-min L~g)..g by A1,A5,A2,Th24,
  SPRECT_2:46;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem Th35:
  z/.1 = S-max L~z implies (N-min L~z)..z < (N-max L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A2: (N-min L~g)..g < (N-max L~g)..g & (N-max L~g)..g < (S-max L~g)..g by
  Lm14,Th25;
A3: S-max L~g in rng g by SPRECT_2:46;
  assume
A4: z/.1 = S-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,S-max L~z) = z by A4,REVROT_1:16;
  N-max L~g in rng g & N-min L~g in rng g by SPRECT_2:43,44;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem Th36:
  z/.1 = S-max L~z & N-max L~z <> E-max L~z implies (N-max L~z)..z
  < (E-max L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then
A2: g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A3: (E-max L~g)..g < (S-max L~g)..g by Lm11;
  assume that
A4: z/.1 = S-max L~z and
A5: N-max L~z <> E-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,S-max L~z) = z by A4,REVROT_1:16;
A7: N-max L~g in rng g & E-max L~g in rng g by SPRECT_2:44,50;
  S-max L~g in rng g & (N-max L~g)..g < (E-max L~g)..g by A1,A5,A2,Th26,
  SPRECT_2:46;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem
  z/.1 = S-max L~z & E-min L~z <> S-max L~z implies (E-max L~z)..z < (
  E-min L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  assume
A2: z/.1 = S-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A3: Rotate(g,S-max L~z) = z by A2,REVROT_1:16;
A4: E-min L~g in rng g & E-max L~g in rng g by SPRECT_2:49,50;
  W-min L~z in rng z by SPRECT_2:47;
  then
A5: g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A6: S-max L~g in rng g & (E-max L~g)..g < (E-min L~g)..g by
  Th27,SPRECT_2
  :46;
  assume E-min L~z <> S-max L~z;
  then (E-min L~g)..g < (S-max L~g)..g by A1,A5,Th28;
  hence thesis by A1,A3,A4,A6,Th12;
end;

theorem Th38:
  f/.1 = E-max L~f implies (E-max L~f)..f < (E-min L~f)..f
proof
  assume f/.1 = E-max L~f;
  then
A1: (E-max L~f)..f = 1 by FINSEQ_6:47;
A2: E-min L~f in rng f by SPRECT_2:49;
  then (E-min L~f)..f in dom f by FINSEQ_4:30;
  then
A3: (E-min L~f)..f >= 1 by FINSEQ_3:27;
  E-max L~f in rng f by SPRECT_2:50;
  then (E-min L~f)..f <> (E-max L~f)..f by A2,FINSEQ_5:10,SPRECT_2:58;
  hence thesis by A3,A1,XXREAL_0:1;
end;

theorem
  f/.1 = E-max L~f implies (E-min L~f)..f > 1
proof
  assume
A1: f/.1 = E-max L~f;
  then (E-max L~f)..f = 1 by FINSEQ_6:47;
  hence thesis by A1,Th38;
end;

theorem Th40:
  z/.1 = E-max L~z & S-max L~z <> E-min L~z implies (E-min L~z)..z
  < (S-max L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A3: E-min L~g in rng g & (E-max L~g)..g < (E-min L~g)..g by
  SPRECT_2:49,
  75;
  assume that
A4: z/.1 = E-max L~z and
A5: S-max L~z <> E-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,E-max L~z) = z by A4,REVROT_1:16;
A7: S-max L~g in rng g & E-max L~g in rng g by SPRECT_2:46,50;
  (E-min L~g)..g < (S-max L~g)..g by A1,A5,A2,SPRECT_2:76;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem Th41:
  z/.1 = E-max L~z implies (S-max L~z)..z < (S-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A2: (E-max L~g)..g < (S-max L~g)..g & (S-max L~g)..g < (S-min L~g)..g by
  Lm1,SPRECT_2:77;
A3: S-min L~g in rng g by SPRECT_2:45;
  assume
A4: z/.1 = E-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,E-max L~z) = z by A4,REVROT_1:16;
  S-max L~g in rng g & E-max L~g in rng g by SPRECT_2:46,50;
  hence thesis by A1,A5,A3,A2,Th5;
end;

Lm20: z/.1 = S-max L~z implies (N-min L~z)..z < (E-max L~z)..z
proof
  assume
A1: z/.1 = S-max L~z;
  then
 (N-min L~z)..z < (N-max L~z)..z by Th35;
    hence thesis by A1,Th36,XXREAL_0:2;
end;

Lm21: z/.1 = S-max L~z implies (W-max L~z)..z < (E-max L~z)..z
proof
  assume
A1: z/.1 = S-max L~z;
  then
 (N-min L~z)..z < (E-max L~z)..z by Lm20;
    hence thesis by A1,Th34,XXREAL_0:2;
end;

Lm22: z/.1 = S-max L~z implies (W-min L~z)..z < (E-max L~z)..z
proof
  assume
A1: z/.1 = S-max L~z;
  then (W-min L~z)..z < (W-max L~z)..z by Th33;
  hence thesis by A1,Lm21,XXREAL_0:2;
end;

Lm23: z/.1 = S-max L~z implies (W-max L~z)..z < (N-max L~z)..z
proof
  assume
A1: z/.1 = S-max L~z;
  then
 (N-min L~z)..z < (N-max L~z)..z by Th35;
    hence thesis by A1,Th34,XXREAL_0:2;
end;

Lm24: z/.1 = S-max L~z implies (W-min L~z)..z < (N-min L~z)..z
proof
  assume
A1: z/.1 = S-max L~z;
  then
 (W-min L~z)..z < (W-max L~z)..z by Th33;
    hence thesis by A1,Th34,XXREAL_0:2;
end;

Lm25: z/.1 = S-max L~z implies (S-min L~z)..z < (N-min L~z)..z
proof
  assume
A1: z/.1 = S-max L~z;
  then (S-min L~z)..z <= (W-min L~z)..z by Th32;
  hence thesis by A1,Lm24,XXREAL_0:2;
end;

Lm26: z/.1 = S-max L~z implies (S-min L~z)..z < (N-max L~z)..z
proof
  assume
A1: z/.1 = S-max L~z;
  then (S-min L~z)..z < (N-min L~z)..z by Lm25;
  hence thesis by A1,Th35,XXREAL_0:2;
end;

theorem Th42:
  z/.1 = E-max L~z & S-min L~z <> W-min L~z implies (S-min L~z)..z
  < (W-min L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then
A2: g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A3: (W-min L~g)..g < (E-max L~g)..g by Lm22;
  assume that
A4: z/.1 = E-max L~z and
A5: S-min L~z <> W-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,E-max L~z) = z by A4,REVROT_1:16;
A7: S-min L~g in rng g & W-min L~g in rng g by SPRECT_2:45,47;
  E-max L~g in rng g & (S-min L~g)..g < (W-min L~g)..g by A1,A5,A2,Th32,
  SPRECT_2:50;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem Th43:
  z/.1 = E-max L~z implies (W-min L~z)..z < (W-max L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A2: (W-min L~g)..g < (W-max L~g)..g & (W-max L~g)..g < (E-max L~g)..g by
  Lm21,Th33;
A3: E-max L~g in rng g by SPRECT_2:50;
  assume
A4: z/.1 = E-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,E-max L~z) = z by A4,REVROT_1:16;
  W-max L~g in rng g & W-min L~g in rng g by SPRECT_2:47,48;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem Th44:
  z/.1 = E-max L~z & W-max L~z <> N-min L~z implies (W-max L~z)..z
  < (N-min L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then
A2: g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A3: (N-min L~g)..g < (E-max L~g)..g by Lm20;
  assume that
A4: z/.1 = E-max L~z and
A5: W-max L~z <> N-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,E-max L~z) = z by A4,REVROT_1:16;
A7: W-max L~g in rng g & N-min L~g in rng g by SPRECT_2:43,48;
  E-max L~g in rng g & (W-max L~g)..g < (N-min L~g)..g by A1,A5,A2,Th34,
  SPRECT_2:50;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem
  z/.1 = E-max L~z & N-max L~z <> E-max L~z implies (N-min L~z)..z < (
  N-max L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  assume
A2: z/.1 = E-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A3: Rotate(g,E-max L~z) = z by A2,REVROT_1:16;
A4: N-max L~g in rng g & N-min L~g in rng g by SPRECT_2:43,44;
  S-max L~z in rng z by SPRECT_2:46;
  then
A5: g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A6: E-max L~g in rng g & (N-min L~g)..g < (N-max L~g)..g by
  Th35,SPRECT_2
  :50;
  assume N-max L~z <> E-max L~z;
  then (N-max L~g)..g < (E-max L~g)..g by A1,A5,Th36;
  hence thesis by A1,A3,A4,A6,Th12;
end;

theorem
  f/.1 = N-max L~f & N-max L~f <> E-max L~f implies (N-max L~f)..f < (
  E-max L~f)..f
proof
  assume that
A1: f/.1 = N-max L~f and
A2: N-max L~f <> E-max L~f;
A3: E-max L~f in rng f by SPRECT_2:50;
  then (E-max L~f)..f in dom f by FINSEQ_4:30;
  then
A4: (E-max L~f)..f >= 1 by FINSEQ_3:27;
  N-max L~f in rng f & (N-max L~f)..f = 1 by A1,FINSEQ_6:47,SPRECT_2:44;
  hence thesis by A3,A2,A4,FINSEQ_5:10,XXREAL_0:1;
end;

theorem
  z/.1 = N-max L~z implies (E-max L~z)..z < (E-min L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A2: (N-max L~g)..g <= (E-max L~g)..g & (E-max L~g)..g < (E-min L~g)..g by
  Th26,Th27;
A3: E-max L~g in rng g by SPRECT_2:50;
  assume
A4: z/.1 = N-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,N-max L~z) = z by A4,REVROT_1:16;
  E-min L~g in rng g & N-max L~g in rng g by SPRECT_2:44,49;
  hence thesis by A1,A5,A3,A2,Th5;
end;

theorem
  z/.1 = N-max L~z & E-min L~z <> S-max L~z implies (E-min L~z)..z < (
  S-max L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then
A2: g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A3: S-max L~g in rng g & (N-max L~g)..g < (E-min L~g)..g by
  Lm16,SPRECT_2
  :46;
  assume that
A4: z/.1 = N-max L~z and
A5: E-min L~z <> S-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,N-max L~z) = z by A4,REVROT_1:16;
A7: E-min L~g in rng g & N-max L~g in rng g by SPRECT_2:44,49;
  (E-min L~g)..g < (S-max L~g)..g by A1,A5,A2,Th28;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem
  z/.1 = N-max L~z implies (S-max L~z)..z < (S-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A2: (S-max L~g)..g < (S-min L~g)..g & (N-max L~g)..g < (S-max L~g)..g by
  Lm9,SPRECT_2:77;
A3: N-max L~g in rng g by SPRECT_2:44;
  assume
A4: z/.1 = N-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,N-max L~z) = z by A4,REVROT_1:16;
  S-max L~g in rng g & S-min L~g in rng g by SPRECT_2:45,46;
  hence thesis by A1,A5,A3,A2,Th5;
end;

theorem
  z/.1 = N-max L~z & S-min L~z <> W-min L~z implies (S-min L~z)..z < (
  W-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A3: (N-max L~g)..g < (S-min L~g)..g by Lm10;
  assume that
A4: z/.1 = N-max L~z and
A5: S-min L~z <> W-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,N-max L~z) = z by A4,REVROT_1:16;
A7: W-min L~g in rng g & S-min L~g in rng g by SPRECT_2:45,47;
  N-max L~g in rng g & (S-min L~g)..g < (W-min L~g)..g by A1,A5,A2,
  SPRECT_2:44,78;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem
  z/.1 = N-max L~z implies (W-min L~z)..z < (W-max L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A2: (W-min L~g)..g < (W-max L~g)..g & (W-max L~g)..g < (N-max L~g)..g by
  Lm23,Th33;
A3: N-max L~g in rng g by SPRECT_2:44;
  assume
A4: z/.1 = N-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,N-max L~z) = z by A4,REVROT_1:16;
  W-min L~g in rng g & W-max L~g in rng g by SPRECT_2:47,48;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem
  z/.1 = N-max L~z & N-min L~z <> W-max L~z implies (W-max L~z)..z < (
  N-min L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then
A2: g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A3: (N-min L~g)..g < (N-max L~g)..g by Th35;
  assume that
A4: z/.1 = N-max L~z and
A5: N-min L~z <> W-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,N-max L~z) = z by A4,REVROT_1:16;
A7: N-min L~g in rng g & W-max L~g in rng g by SPRECT_2:43,48;
  N-max L~g in rng g & (W-max L~g)..g < (N-min L~g)..g by A1,A5,A2,Th34,
  SPRECT_2:44;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem
  f/.1 = E-min L~f & E-min L~f <> S-max L~f implies (E-min L~f)..f < (
  S-max L~f)..f
proof
  assume that
A1: f/.1 = E-min L~f and
A2: E-min L~f <> S-max L~f;
A3: S-max L~f in rng f by SPRECT_2:46;
  then (S-max L~f)..f in dom f by FINSEQ_4:30;
  then
A4: (S-max L~f)..f >= 1 by FINSEQ_3:27;
  E-min L~f in rng f & (E-min L~f)..f = 1 by A1,FINSEQ_6:47,SPRECT_2:49;
  hence thesis by A3,A2,A4,FINSEQ_5:10,XXREAL_0:1;
end;

theorem
  z/.1 = E-min L~z implies (S-max L~z)..z < (S-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A2: (E-min L~g)..g <= (S-max L~g)..g & (S-max L~g)..g < (S-min L~g)..g by
  SPRECT_2:76,77;
A3: S-max L~g in rng g by SPRECT_2:46;
  assume
A4: z/.1 = E-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,E-min L~z) = z by A4,REVROT_1:16;
  S-min L~g in rng g & E-min L~g in rng g by SPRECT_2:45,49;
  hence thesis by A1,A5,A3,A2,Th5;
end;

theorem
  z/.1 = E-min L~z & S-min L~z <> W-min L~z implies (S-min L~z)..z < (
  W-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A3: W-min L~g in rng g & (E-min L~g)..g < (S-min L~g)..g by
  Lm4,SPRECT_2:
  47;
  assume that
A4: z/.1 = E-min L~z and
A5: S-min L~z <> W-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,E-min L~z) = z by A4,REVROT_1:16;
A7: S-min L~g in rng g & E-min L~g in rng g by SPRECT_2:45,49;
  (S-min L~g)..g < (W-min L~g)..g by A1,A5,A2,SPRECT_2:78;
  hence thesis by A1,A6,A7,A3,Th5;
end;

Lm27: z/.1 = E-max L~z implies (S-max L~z)..z < (W-min L~z)..z
proof
  assume
A1: z/.1 = E-max L~z;
  then
 (S-max L~z)..z < (S-min L~z)..z by Th41;
    hence thesis by A1,Th42,XXREAL_0:2;
end;

Lm28: z/.1 = E-max L~z implies (E-min L~z)..z < (W-min L~z)..z
proof
  assume
A1: z/.1 = E-max L~z;
  then (E-min L~z)..z <= (S-max L~z)..z by Th40;
  hence thesis by A1,Lm27,XXREAL_0:2;
end;

Lm29: z/.1 = E-max L~z implies (E-min L~z)..z < (W-max L~z)..z
proof
  assume
A1: z/.1 = E-max L~z;
  then (E-min L~z)..z < (W-min L~z)..z by Lm28;
  hence thesis by A1,Th43,XXREAL_0:2;
end;

Lm30: z/.1 = E-max L~z implies (S-min L~z)..z < (W-max L~z)..z
proof
  assume
A1: z/.1 = E-max L~z;
  then
 (W-min L~z)..z < (W-max L~z)..z by Th43;
    hence thesis by A1,Th42,XXREAL_0:2;
end;

theorem
  z/.1 = E-min L~z implies (W-min L~z)..z < (W-max L~z)..z
proof
  set g = Rotate(z,E-max L~z);
A1: L~z = L~g by REVROT_1:33;
  E-max L~z in rng z by SPRECT_2:50;
  then g/.1 = E-max L~g by A1,FINSEQ_6:98;
  then
A2: (W-min L~g)..g < (W-max L~g)..g & (E-min L~g)..g < (W-min L~g)..g by
  Lm28,Th43;
A3: E-min L~g in rng g by SPRECT_2:49;
  assume
A4: z/.1 = E-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,E-min L~z) = z by A4,REVROT_1:16;
  W-min L~g in rng g & W-max L~g in rng g by SPRECT_2:47,48;
  hence thesis by A1,A5,A3,A2,Th5;
end;

theorem
  z/.1 = E-min L~z & W-max L~z <> N-min L~z implies (W-max L~z)..z < (
  N-min L~z)..z
proof
  set g = Rotate(z,E-max L~z);
A1: L~z = L~g by REVROT_1:33;
  E-max L~z in rng z by SPRECT_2:50;
  then
A2: g/.1 = E-max L~g by A1,FINSEQ_6:98;
  then
A3: (E-min L~g)..g < (W-max L~g)..g by Lm29;
  assume that
A4: z/.1 = E-min L~z and
A5: W-max L~z <> N-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,E-min L~z) = z by A4,REVROT_1:16;
A7: N-min L~g in rng g & W-max L~g in rng g by SPRECT_2:43,48;
  E-min L~g in rng g & (W-max L~g)..g < (N-min L~g)..g by A1,A5,A2,Th44,
  SPRECT_2:49;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem
  z/.1 = E-min L~z implies (N-min L~z)..z < (N-max L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A2: (N-min L~g)..g < (N-max L~g)..g & (N-max L~g)..g < (E-min L~g)..g by
  Lm16,Th25;
A3: E-min L~g in rng g by SPRECT_2:49;
  assume
A4: z/.1 = E-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,E-min L~z) = z by A4,REVROT_1:16;
  N-min L~g in rng g & N-max L~g in rng g by SPRECT_2:43,44;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem
  z/.1 = E-min L~z & E-max L~z <> N-max L~z implies (N-max L~z)..z < (
  E-max L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then
A2: g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A3: (E-max L~g)..g < (E-min L~g)..g by Th27;
  assume that
A4: z/.1 = E-min L~z and
A5: E-max L~z <> N-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,E-min L~z) = z by A4,REVROT_1:16;
A7: E-max L~g in rng g & N-max L~g in rng g by SPRECT_2:44,50;
  E-min L~g in rng g & (N-max L~g)..g < (E-max L~g)..g by A1,A5,A2,Th26,
  SPRECT_2:49;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem
  f/.1 = S-min L~f & S-min L~f <> W-min L~f implies (S-min L~f)..f < (
  W-min L~f)..f
proof
  assume that
A1: f/.1 = S-min L~f and
A2: S-min L~f <> W-min L~f;
A3: W-min L~f in rng f by SPRECT_2:47;
  then (W-min L~f)..f in dom f by FINSEQ_4:30;
  then
A4: (W-min L~f)..f >= 1 by FINSEQ_3:27;
  S-min L~f in rng f & (S-min L~f)..f = 1 by A1,FINSEQ_6:47,SPRECT_2:45;
  hence thesis by A3,A2,A4,FINSEQ_5:10,XXREAL_0:1;
end;

theorem
  z/.1 = S-min L~z implies (W-min L~z)..z < (W-max L~z)..z
proof
  set g = Rotate(z,E-max L~z);
A1: L~z = L~g by REVROT_1:33;
  E-max L~z in rng z by SPRECT_2:50;
  then g/.1 = E-max L~g by A1,FINSEQ_6:98;
  then
A2: (S-min L~g)..g <= (W-min L~g)..g & (W-min L~g)..g < (W-max L~g)..g by
  Th42,Th43;
A3: W-min L~g in rng g by SPRECT_2:47;
  assume
A4: z/.1 = S-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,S-min L~z) = z by A4,REVROT_1:16;
  W-max L~g in rng g & S-min L~g in rng g by SPRECT_2:45,48;
  hence thesis by A1,A5,A3,A2,Th5;
end;

theorem
  z/.1 = S-min L~z & W-max L~z <> N-min L~z implies (W-max L~z)..z < (
  N-min L~z)..z
proof
  set g = Rotate(z,E-max L~z);
A1: L~z = L~g by REVROT_1:33;
  E-max L~z in rng z by SPRECT_2:50;
  then
A2: g/.1 = E-max L~g by A1,FINSEQ_6:98;
  then
A3: N-min L~g in rng g & (S-min L~g)..g < (W-max L~g)..g by
  Lm30,SPRECT_2
  :43;
  assume that
A4: z/.1 = S-min L~z and
A5: W-max L~z <> N-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,S-min L~z) = z by A4,REVROT_1:16;
A7: W-max L~g in rng g & S-min L~g in rng g by SPRECT_2:45,48;
  (W-max L~g)..g < (N-min L~g)..g by A1,A5,A2,Th44;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem
  z/.1 = S-min L~z implies (N-min L~z)..z < (N-max L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A2: (N-min L~g)..g < (N-max L~g)..g & (S-min L~g)..g < (N-min L~g)..g by
  Lm25,Th35;
A3: S-min L~g in rng g by SPRECT_2:45;
  assume
A4: z/.1 = S-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,S-min L~z) = z by A4,REVROT_1:16;
  N-min L~g in rng g & N-max L~g in rng g by SPRECT_2:43,44;
  hence thesis by A1,A5,A3,A2,Th5;
end;

theorem
  z/.1 = S-min L~z & N-max L~z <> E-max L~z implies (N-max L~z)..z < (
  E-max L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then
A2: g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A3: (S-min L~g)..g < (N-max L~g)..g by Lm26;
  assume that
A4: z/.1 = S-min L~z and
A5: N-max L~z <> E-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,S-min L~z) = z by A4,REVROT_1:16;
A7: E-max L~g in rng g & N-max L~g in rng g by SPRECT_2:44,50;
  S-min L~g in rng g & (N-max L~g)..g < (E-max L~g)..g by A1,A5,A2,Th36,
  SPRECT_2:45;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem
  z/.1 = S-min L~z implies (E-max L~z)..z < (E-min L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A2: (E-max L~g)..g < (E-min L~g)..g & (E-min L~g)..g < (S-min L~g)..g by
  Lm4,SPRECT_2:75;
A3: S-min L~g in rng g by SPRECT_2:45;
  assume
A4: z/.1 = S-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,S-min L~z) = z by A4,REVROT_1:16;
  E-max L~g in rng g & E-min L~g in rng g by SPRECT_2:49,50;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem
  z/.1 = S-min L~z & S-max L~z <> E-min L~z implies (E-min L~z)..z < (
  S-max L~z)..z
proof
  set g = Rotate(z,N-min L~z);
A1: L~z = L~g by REVROT_1:33;
  N-min L~z in rng z by SPRECT_2:43;
  then
A2: g/.1 = N-min L~g by A1,FINSEQ_6:98;
  then
A3: (S-max L~g)..g < (S-min L~g)..g by SPRECT_2:77;
  assume that
A4: z/.1 = S-min L~z and
A5: S-max L~z <> E-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,S-min L~z) = z by A4,REVROT_1:16;
A7: S-max L~g in rng g & E-min L~g in rng g by SPRECT_2:46,49;
  S-min L~g in rng g & (E-min L~g)..g < (S-max L~g)..g by A1,A5,A2,
  SPRECT_2:45,76;
  hence thesis by A1,A6,A7,A3,Th12;
end;

theorem
  f/.1 = W-max L~f & W-max L~f <> N-min L~f implies (W-max L~f)..f < (
  N-min L~f)..f
proof
  assume that
A1: f/.1 = W-max L~f and
A2: W-max L~f <> N-min L~f;
A3: N-min L~f in rng f by SPRECT_2:43;
  then (N-min L~f)..f in dom f by FINSEQ_4:30;
  then
A4: (N-min L~f)..f >= 1 by FINSEQ_3:27;
  W-max L~f in rng f & (W-max L~f)..f = 1 by A1,FINSEQ_6:47,SPRECT_2:48;
  hence thesis by A3,A2,A4,FINSEQ_5:10,XXREAL_0:1;
end;

theorem
  z/.1 = W-max L~z implies (N-min L~z)..z < (N-max L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A2: (W-max L~g)..g <= (N-min L~g)..g & (N-min L~g)..g < (N-max L~g)..g by
  Th34,Th35;
A3: N-min L~g in rng g by SPRECT_2:43;
  assume
A4: z/.1 = W-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,W-max L~z) = z by A4,REVROT_1:16;
  N-max L~g in rng g & W-max L~g in rng g by SPRECT_2:44,48;
  hence thesis by A1,A5,A3,A2,Th5;
end;

theorem
  z/.1 = W-max L~z & N-max L~z <> E-max L~z implies (N-max L~z)..z < (
  E-max L~z)..z
proof
  set g = Rotate(z,S-max L~z);
A1: L~z = L~g by REVROT_1:33;
  S-max L~z in rng z by SPRECT_2:46;
  then
A2: g/.1 = S-max L~g by A1,FINSEQ_6:98;
  then
A3: E-max L~g in rng g & (W-max L~g)..g < (N-max L~g)..g by
  Lm23,SPRECT_2
  :50;
  assume that
A4: z/.1 = W-max L~z and
A5: N-max L~z <> E-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,W-max L~z) = z by A4,REVROT_1:16;
A7: N-max L~g in rng g & W-max L~g in rng g by SPRECT_2:44,48;
  (N-max L~g)..g < (E-max L~g)..g by A1,A5,A2,Th36;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem
  z/.1 = W-max L~z implies (E-max L~z)..z < (E-min L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A2: (E-max L~g)..g < (E-min L~g)..g & (W-max L~g)..g < (E-max L~g)..g by
  Lm18,Th27;
A3: W-max L~g in rng g by SPRECT_2:48;
  assume
A4: z/.1 = W-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,W-max L~z) = z by A4,REVROT_1:16;
  E-max L~g in rng g & E-min L~g in rng g by SPRECT_2:49,50;
  hence thesis by A1,A5,A3,A2,Th5;
end;

theorem
  z/.1 = W-max L~z & E-min L~z <> S-max L~z implies (E-min L~z)..z < (
  S-max L~z)..z
proof
  set g = Rotate(z,W-min L~z);
A1: L~z = L~g by REVROT_1:33;
  W-min L~z in rng z by SPRECT_2:47;
  then
A2: g/.1 = W-min L~g by A1,FINSEQ_6:98;
  then
A3: (W-max L~g)..g < (E-min L~g)..g by Lm19;
  assume that
A4: z/.1 = W-max L~z and
A5: E-min L~z <> S-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,W-max L~z) = z by A4,REVROT_1:16;
A7: S-max L~g in rng g & E-min L~g in rng g by SPRECT_2:46,49;
  W-max L~g in rng g & (E-min L~g)..g < (S-max L~g)..g by A1,A5,A2,Th28,
  SPRECT_2:48;
  hence thesis by A1,A6,A7,A3,Th5;
end;

theorem
  z/.1 = W-max L~z implies (S-max L~z)..z < (S-min L~z)..z
proof
  set g = Rotate(z,E-max L~z);
A1: L~z = L~g by REVROT_1:33;
  E-max L~z in rng z by SPRECT_2:50;
  then g/.1 = E-max L~g by A1,FINSEQ_6:98;
  then
A2: (S-max L~g)..g < (S-min L~g)..g & (S-min L~g)..g < (W-max L~g)..g by
  Lm30,Th41;
A3: W-max L~g in rng g by SPRECT_2:48;
  assume
A4: z/.1 = W-max L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A5: Rotate(g,W-max L~z) = z by A4,REVROT_1:16;
  S-max L~g in rng g & S-min L~g in rng g by SPRECT_2:45,46;
  hence thesis by A1,A5,A3,A2,Th12;
end;

theorem
  z/.1 = W-max L~z & W-min L~z <> S-min L~z implies (S-min L~z)..z < (
  W-min L~z)..z
proof
  set g = Rotate(z,E-max L~z);
A1: L~z = L~g by REVROT_1:33;
  E-max L~z in rng z by SPRECT_2:50;
  then
A2: g/.1 = E-max L~g by A1,FINSEQ_6:98;
  then
A3: (W-min L~g)..g < (W-max L~g)..g by Th43;
  assume that
A4: z/.1 = W-max L~z and
A5: W-min L~z <> S-min L~z;
  for i be Element of NAT st 1 < i & i < len z holds z/.i <> z/.1 by
  GOBOARD7:38;
  then
A6: Rotate(g,W-max L~z) = z by A4,REVROT_1:16;
A7: W-min L~g in rng g & S-min L~g in rng g by SPRECT_2:45,47;
  W-max L~g in rng g & (S-min L~g)..g < (W-min L~g)..g by A1,A5,A2,Th42,
  SPRECT_2:48;
  hence thesis by A1,A6,A7,A3,Th12;
end;
