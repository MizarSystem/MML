:: The First Mean Value Theorem for Integrals
::  by Keiko Narita , Noboru Endou and Yasunari Shidama
:: 
:: Received October 30, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies PARTFUN1, MEASURE1, FINSEQ_1, RELAT_1, ABSVALUE, FUNCT_1,
      ORDINAL2, LATTICES, FINSEQ_2, FINSEQ_4, CARD_3, BINOP_1, SETWISEO,
      COMPLEX1, SEQ_1, SEQ_2, MEASURE6, BOOLE, ARYTM, ARYTM_1, POWER, MESFUNC1,
      TARSKI, ARYTM_3, SQUARE_1, RLVECT_1, GROUP_1, PROB_1, INTEGRA1, MESFUNC2,
      MESFUNC5, SUPINF_1, SUPINF_2, MESFUNC7, FUNCT_3, RFUNCT_3, FINSET_1,
      VALUED_0;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, VALUED_0, XREAL_0,
      REAL_1,
      XXREAL_0, MEASURE1, NAT_1, COMPLEX1, SUPINF_1, RELAT_1, RELSET_1,
      SETWISEO, PARTFUN1, FINSEQ_1, FINSEQ_2, SETWOP_2, BINOP_1, FUNCT_2,
      NEWTON, SQUARE_1, PROB_1, SUPINF_2, EXTREAL1, POWER, MESFUNC5, MESFUNC1,
      MESFUNC2, MEASURE6;
 constructors REAL_1, NAT_1, DOMAIN_1, FINSOP_1, EXTREAL1, BINOP_1, NEWTON,
      POWER, MESFUNC1, MEASURE6, MESFUNC2, MEASURE3, SETWISEO, EXTREAL2,
      SQUARE_1, MESFUNC5, VALUED_0;
 registrations SUBSET_1, NAT_1, RELSET_1, XREAL_0, MEMBERED, ORDINAL1,
      FINSEQ_1, MEASURE1, NUMBERS, XXREAL_0, XCMPLX_0, XBOOLE_0, VALUED_0;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, SUPINF_2, MESFUNC1, MEASURE6, MESFUNC5, XCMPLX_0, NUMBERS,
      VALUED_0;
 theorems ABSVALUE, SETWISEO, POWER, HOLDER_1, TARSKI, SUPINF_1, PARTFUN1,
      FUNCT_1, SUPINF_2, EXTREAL1, EXTREAL2, BINOP_1, MESFUNC1, FINSEQ_1,
      XREAL_0, XBOOLE_0, XBOOLE_1, XCMPLX_1, MESFUNC2, NEWTON, XREAL_1,
      SQUARE_1, COMPLEX1, XXREAL_0, REAL_2, FINSEQ_2, MESFUNC5, FINSOP_1,
      NAT_1, RELAT_1, FUNCT_3, MEASURE1, MESFUNC6, MEASURE6, ORDINAL1,
      VALUED_0;
 schemes FUNCT_2, NAT_1, PARTFUN2, BINOP_1, BINOP_2;

begin  ::  Lemmas for extended real valued functions

reserve X for non empty set,
        S for SigmaField of X,
        M for sigma_Measure of S,
        f,g for PartFunc of X,ExtREAL,
        E for Element of S;

theorem LMXZ:
(for x be Element of X st x in dom f holds f.x <= g.x)
 implies g-f is nonnegative
proof
   assume
AS0:for x be Element of X st x in dom f holds f.x <= g.x;
   now let y be R_eal;
    assume y in rng (g-f); then
    consider x being set such that
P1:  x in dom (g-f) & y = (g-f).x by FUNCT_1:def 5;
P10:dom (g-f) =
      (dom g /\ dom f)\((g"{+infty} /\ f"{+infty}) \/ (g"{-infty} /\
       f"{-infty})) by MESFUNC1:def 4; then
    x in dom g /\ dom f by P1,XBOOLE_0:def 4; then
P12:x in dom f & x in dom g by XBOOLE_0:def 3; then
P11:f.x <=g.x by AS0;
X:  now assume f.x <> -infty; then
      -infty < f.x by XXREAL_0:6; then
C12:  -infty <g.x by P11,XXREAL_0:2;
      now assume not (f.x < +infty); then
C131:  +infty = f.x by XXREAL_0:4; then
       +infty = g.x by P11,XXREAL_0:4; then
       f.x in {+infty} & g.x in {+infty} by C131,TARSKI:def 1; then
       x in f"{+infty} & x in g"{+infty} by P12,FUNCT_1:def 13; then
       x in g"{+infty} /\ f"{+infty} by XBOOLE_0:def 3; then
       x in (g"{+infty} /\ f"{+infty}) \/ (g"{-infty} /\ f"{-infty}) 
         by XBOOLE_0:def 2;
       hence contradiction by P10,P1,XBOOLE_0:def 4;
      end; then
      0. <= g.x-f.x by P11,C12,EXTREAL1:40;
      hence 0. <=(g-f).x by P1,MESFUNC1:def 4;
    end;
    now assume C2: f.x = -infty;
      now assume not(-infty < g.x); then
      -infty = g.x by XXREAL_0:6; then
       f.x in {-infty} & g.x in {-infty} by C2,TARSKI:def 1; then
       x in f"{-infty} & x in g"{-infty} by P12,FUNCT_1:def 13; then
       x in g"{-infty} /\ f"{-infty} by XBOOLE_0:def 3; then
       x in (g"{+infty} /\ f"{+infty}) \/ (g"{-infty} /\ f"{-infty}) 
         by XBOOLE_0:def 2;
       hence contradiction by P10,P1,XBOOLE_0:def 4;
      end; then
      0. <= g.x-f.x by C2,EXTREAL1:40;
      hence 0. <=(g-f).x by P1,MESFUNC1:def 4;
     end;
     hence 0 <= y by P1,X;
   end; then
   rng (g-f) is nonnegative by SUPINF_2:def 15;
   hence g-f is nonnegative by SUPINF_2:def 22;
end;

theorem AAA:
for Y be set, f be PartFunc of X,ExtREAL, r be Real holds
   (r(#)f)|Y = r(#)(f|Y)
proof
   let Y be set, f be PartFunc of X,ExtREAL, r be Real;
A1:dom ((r(#)f)|Y) = dom (r(#)f) /\ Y by RELAT_1:90
    .= dom f /\ Y by MESFUNC1:def 6
    .= dom (f|Y) by RELAT_1:90
    .= dom (r(#)(f|Y)) by MESFUNC1:def 6;
   now let x be Element of X;
    assume A2: x in dom ((r(#)f)|Y); then
    x in dom (r(#)f) /\ Y by RELAT_1:90; then
A3: x in dom (r(#)f) & x in Y by XBOOLE_0:def 3; then
    x in dom f by MESFUNC1:def 6; then
    x in dom f /\ Y by A3,XBOOLE_0:def 3; then
A4: x in dom (f|Y) by RELAT_1:90; then
A5: x in dom (r(#)(f|Y)) by MESFUNC1:def 6;
    thus ((r(#)f)|Y).x = (r(#)f).x by A2,FUNCT_1:68
      .= (R_EAL r)*((f.x)) by A3,MESFUNC1:def 6
      .= (R_EAL r)*((f|Y).x) by A4,FUNCT_1:68
      .= (r(#)(f|Y)).x by A5,MESFUNC1:def 6;
    end;
    hence thesis by A1,PARTFUN1:34;
end;

theorem LMXY: 
f is_integrable_on M & g is_integrable_on M & g-f is nonnegative implies
  ex E be Element of S st
    E = dom f /\ dom g & Integral(M,f|E) <= Integral(M,g|E)
proof
   assume that
X2: f is_integrable_on M & g is_integrable_on M and
X3: g-f is nonnegative;
B2:ex E2 be Element of S st
    E2 = dom g & g is_measurable_on E2 by MESFUNC5:def 17,X2;
   set h=(-1)(#)f;
P2:h is_integrable_on M & Integral(M,h) = R_EAL (-1) * Integral(M,f)
       by MESFUNC5:116,X2; then
   consider E be Element of S such that
P3: E = dom h /\ dom g &
    Integral(M,h+g) = Integral(M,h|E)+Integral(M,g|E) by X2,MESFUNC5:115;
   g+(-f) is nonnegative by X3,MESFUNC2:9; then
D1:h+g is nonnegative by MESFUNC2:11;
   ex E3 be Element of S st
    E3 = dom h & h is_measurable_on E3 by P2,MESFUNC5:def 17; then
   ex A be Element of S st A = dom(h+g) & (h+g) is_measurable_on A
     by B2,MESFUNC5:53; then
F1:0 <= Integral(M,h|E)+Integral(M,g|E) by P3,D1,MESFUNC5:96;
F3:f|E is_integrable_on M by MESFUNC5:103,X2;
   take E;
   h|E = (-1)(#)(f|E) by AAA; then
G1:0 <= R_EAL(-1) * Integral(M,f|E) + Integral(M,g|E) by MESFUNC5:116,F3,F1;
   -infty < Integral(M,f|E) & Integral(M,f|E) < +infty by MESFUNC5:102,F3; then
   reconsider c1=Integral(M,f|E) as Real by EXTREAL1:1;
   g|E is_integrable_on M by MESFUNC5:103,X2; then
   -infty < Integral(M,g|E) & Integral(M,g|E) < +infty by MESFUNC5:102; then
   reconsider c2=Integral(M,g|E) as Real by EXTREAL1:1;
   R_EAL (-1) * Integral(M,f|E) = (-1)*c1 by EXTREAL1:13; then
   0 <= -c1 + c2 by SUPINF_2:1,G1; then
   0 +c1 <=-c1+c2+c1 by XREAL_1:8;
   hence thesis by P3,MESFUNC1:def 6;                
end;

begin  ::  Sigma finite set  

registration let X;
  cluster nonnegative PartFunc of X,ExtREAL;
existence
proof
  consider f be PartFunc of X,ExtREAL;
  take |.f.|;
  now let x be set;
    assume x in dom |.f.|; then
    (|.f.|).x = |.f.x.| by MESFUNC1:def 10;
    hence 0. <= (|.f.|).x by EXTREAL2:51; 
  end; 
  hence thesis by SUPINF_2:71;
end;
end;

definition let X,f;
  redefine func |.f.| -> nonnegative PartFunc of X,ExtREAL;
correctness
proof
  now let x be set;
    assume x in dom |.f.|; then
    (|.f.|).x = |.f.x.| by MESFUNC1:def 10;
    hence 0. <= (|.f.|).x by EXTREAL2:51; 
  end; 
  hence thesis by SUPINF_2:71;
end;
end;

theorem 
f is_integrable_on M implies
 ex F be Function of NAT,S st
  ( for n be Element of NAT holds
      F.n = dom f /\ great_eq_dom(|.f.|, R_EAL(1/(n+1))) ) &
  dom f \ eq_dom(f, 0.) = union rng F &
  for n be Element of NAT holds F.n in S & M.(F.n) <+infty
proof
   assume
A1: f is_integrable_on M; then
   consider E be Element of S such that
A2: E = dom f & f is_measurable_on E by MESFUNC5:def 17;
P11:|.f.| is_measurable_on E by A2,MESFUNC2:29;
P12:dom |.f.| = E by A2,MESFUNC1:def 10;
   defpred P[Element of NAT,set] means
    $2 = E /\ great_eq_dom(|.f.|, R_EAL(1/($1+1)));
P10:for n be Element of NAT ex Z be Element of S st P[n,Z]
   proof
    let n be Element of NAT;
    take Z = E /\ great_eq_dom(|.f.|, R_EAL(1/(n+1)));
    E /\ great_eq_dom(|.f.|, R_EAL(1/(n+1))) is_measurable_on S
     by P11,P12,MESFUNC1:31;
    hence thesis by MESFUNC1:def 11;
   end;
   consider F be Function of NAT,S such that
A02:for n be Element of NAT holds P[n,F.n] from FUNCT_2:sch 10(P10);
   take F;
A21:for n be Element of NAT holds
     F.n = E /\ great_eq_dom(|.f.|, R_EAL(0+1/(n+1))) by A02;
B2:E /\ great_dom(|.f.|, R_EAL 0) = union rng F by A21,MESFUNC1:26;
   now let x be set;
    assume P0: x in E /\ great_dom(|.f.|, 0.); then
P1: x in E & x in great_dom(|.f.|, 0.) by XBOOLE_0:def 3;
    reconsider z=x as Element of X by P0;
P2: ex y being R_eal st y=(|.f.|).z & 0. < y by P1,MESFUNC1:def 14;
    x in dom |.f.| by P1,A2,MESFUNC1:def 10; then
P42:0. < |.f.z.| by P2,MESFUNC1:def 10;
    not x in eq_dom(f, 0.)
    proof
     assume x in eq_dom(f, 0.); then
     ex y2 being R_eal st y2=f.z & y2 = 0. by MESFUNC1:def 16;
     hence contradiction by EXTREAL2:53,P42;
    end;
    hence x in E \ eq_dom(f, 0.) by P1,XBOOLE_0:def 4;
   end; then
L: E /\ great_dom(|.f.|, 0.) c= E \ eq_dom(f, 0.) by TARSKI:def 3;
   now let x be set;
    assume Q0: x in E \ eq_dom(f, 0.); then
Q1: x in E & not x in eq_dom(f, 0.) by XBOOLE_0:def 4;
    reconsider z=x as Element of X by Q0;
    reconsider y=f.z as R_eal;
    not y = 0. by Q1,A2,MESFUNC1:def 16; then
Q5: 0. < |.f.z.| by EXTREAL2:52;
Q6: x in dom |.f.| by Q1,A2,MESFUNC1:def 10; then
    0. < (|.f.|).z by Q5,MESFUNC1:def 10; then
    x in great_dom(|.f.|, 0.) by Q6,MESFUNC1:def 14;
    hence x in E /\ great_dom(|.f.|, 0.) by Q1,XBOOLE_0:def 3;
   end; then
B3:E \ eq_dom(f, 0.) c= E /\ great_dom(|.f.|, 0.) by TARSKI:def 3;
   for n be Element of NAT holds F.n in S & M.(F.n) <+infty
   proof
    let n be Element of NAT;
    set d = R_EAL(1/(n+1));
    set En=F.n;
    set g= (|.f.|)|En;
    F.n = E /\ great_eq_dom(|.f.| ,R_EAL(1/(n+1))) by A02; then
B421:F.n c= E & F.n c= great_eq_dom(|.f.|, R_EAL(1/(n+1))) by XBOOLE_1:17; then
B424:dom g = En by P12,RELAT_1:91;
A02:|.f.| is_measurable_on En by B421,P11,MESFUNC1:34;
    dom |.f.| /\ En = E /\ En by A2,MESFUNC1:def 10; then
    dom |.f.| /\ En = En by B421,XBOOLE_1:28; then
A424:g is_measurable_on En by A02,MESFUNC5:48;
A425:g is nonnegative by MESFUNC5:21;
    consider nf be PartFunc of X,ExtREAL such that
A426: nf is_simple_func_in S & dom nf = En & 
      for x be set st x in En holds nf.x=d by MESFUNC5:47;
    for x be set st x in dom nf holds nf.x >= 0 by A426; then
A427:nf is nonnegative by SUPINF_2:71;
A428:nf is_measurable_on En by A426,MESFUNC2:37;
    for x be Element of X st x in dom nf holds nf.x <= g.x
    proof
     let x be Element of X;
     assume A01: x in dom nf; then
A03: g.x = |.f.| .x by A426,B424,FUNCT_1:68;
     ex y be R_eal st y = |.f.| .x & R_EAL(1/(n+1)) <= y
                             by A01,A426,B421,MESFUNC1:def 15;
     hence thesis by A03,A01,A426;
    end; then
A430:integral+(M,nf) <= integral+(M,g)
        by MESFUNC5:91,A424,B424,A425,A426,A427,A428;
C1: integral+(M,g) =Integral(M,g) &
    integral+(M,nf) = Integral(M,nf) &
    Integral(M,nf) = integral'(M,nf)
      by MESFUNC5:94,95,A424,B424,A425,A426,A427; then
A433:integral+(M,nf) = R_EAL(1/(n+1)) * M.(En) by A426,MESFUNC5:110;
    |.f.| is_integrable_on M by A1,A2,MESFUNC5:106; then
A435:Integral(M,|.f.|) < +infty by MESFUNC5:102;
    (|.f.|)|E=|.f.| by P12,RELAT_1:98; then
    Integral(M,g) <= Integral(M,|.f.|) by B421,P11,P12,MESFUNC5:99; then
    integral+(M,nf) <= Integral(M,|.f.|) by XXREAL_0:2,A430,C1; then
A437:R_EAL(1/(n+1)) * M.En < +infty by A433,A435,XXREAL_0:2;
    set z = R_EAL(1/(n+1));
    z < +infty by XXREAL_0:9; then
    z* M.En / z = M.En & +infty / z = +infty by EXTREAL2:17,19;
    hence thesis by A437,EXTREAL1:51;
   end;
   hence thesis by A2,A02,B2,B3,L,XBOOLE_0:def 10;
end;

begin  ::  The first mean value theorem for integrals

notation let F be Relation;
 synonym F is extreal-yielding for F is ext-real-valued;
end;

definition let k be natural number; let x be Element of ExtREAL;
  redefine func k |-> x -> FinSequence of ExtREAL;
  coherence by FINSEQ_2:77;
end;

registration 
  cluster extreal-yielding FinSequence;
existence
proof 
  consider f being FinSequence of ExtREAL;
  rng f c= ExtREAL; then
  f is extreal-yielding by VALUED_0:def 2;
  hence thesis;
end;
end;

definition
 canceled;
  func multextreal -> BinOp of ExtREAL means :Def14:
    for x,y be Element of ExtREAL holds it.(x,y) = x*y;
existence from BINOP_1:sch 4;
uniqueness from BINOP_2:sch 2;
end;

registration
  cluster multextreal -> commutative associative;
coherence
proof
A0:for a,b be Element of ExtREAL holds multextreal.(a,b) = multextreal.(b,a)
  proof
    let a, b be Element of ExtREAL;
    multextreal.(a,b) = a * b by Def14;
    hence thesis by Def14;
  end;
  for a,b,c be Element of ExtREAL holds
    multextreal.(a, multextreal.(b,c)) = multextreal.(multextreal.(a,b),c)
  proof
    let a, b, c be Element of ExtREAL;
    multextreal.(a, multextreal.(b,c)) = multextreal.(a, b * c) by Def14; then
    multextreal.(a, multextreal.(b,c)) = a * (b * c) by Def14; then
    multextreal.(a, multextreal.(b,c)) = a * b * c by EXTREAL1:23; then
    multextreal.(a, multextreal.(b,c)) = multextreal.(a * b, c) by Def14;
    hence thesis by Def14;
  end;
  hence thesis by A0,BINOP_1:def 2,def 3;
end;
end;

Lm27: 1. is_a_unity_wrt multextreal
proof
   for r be Element of ExtREAL holds multextreal.(1.,r) = r
   proof 
     let r be Element of ExtREAL;
     multextreal.(1.,r) = 1. * r by Def14;
     hence thesis by EXTREAL2:4;
   end; then
   A1: 1. is_a_left_unity_wrt multextreal by BINOP_1:def 5;
   for r be Element of ExtREAL holds multextreal.(r,1.) = r
   proof
     let r be Element of ExtREAL;
     multextreal.(r,1.) = r * 1. by Def14;
     hence thesis by EXTREAL2:4;
   end; then
   1. is_a_right_unity_wrt multextreal by BINOP_1:def 6;
   hence thesis by A1,BINOP_1:def 7;
end;

theorem BINOP27:
the_unity_wrt multextreal = 1 by Lm27,BINOP_1:def 8;

registration
  cluster multextreal -> having_a_unity;
  coherence by BINOP27,Lm27,SETWISEO:22;
end;

definition let F be extreal-yielding FinSequence;
  func Product F -> Element of ExtREAL means :Def12:
    ex f being FinSequence of ExtREAL st f = F & it = multextreal $$ f;
existence
proof
   rng F c= ExtREAL by VALUED_0:def 2; then
   reconsider f = F as FinSequence of ExtREAL by FINSEQ_1:def 4;
   take multextreal $$ f;
   thus thesis;
end;
uniqueness;
end;

registration let x be Element of ExtREAL, n be natural number;
  cluster n |-> x -> extreal-yielding;
  coherence by VALUED_0:def 2;
end;

definition let x be Element of ExtREAL;
           let k be natural number;
  func x |^ k equals
    Product (k |-> x);
  coherence;
end;

definition let x be Element of ExtREAL, k be natural number;
  redefine func x |^ k -> R_eal;
  coherence;
end;

registration
  cluster <*>ExtREAL -> extreal-yielding;
coherence by VALUED_0:def 2;
end;

registration let r be Element of ExtREAL;
  cluster <*r*> -> extreal-yielding;
coherence by VALUED_0:def 2;
end;

theorem Th24:
Product (<*>ExtREAL) = 1
proof 
  Product <*>ExtREAL = multextreal $$ (<*>ExtREAL ) by Def12;
  hence thesis by BINOP27,FINSOP_1:11;
end;

theorem Th25:
for r be Element of ExtREAL holds
Product (<*r*>) = r
proof 
  let r be Element of ExtREAL;
  reconsider r' = r as Element of ExtREAL;
  reconsider F = <*r'*> as FinSequence of ExtREAL;
  multextreal $$ F = r by FINSOP_1:12;
  hence thesis by Def12;
end;

registration let f,g be extreal-yielding FinSequence;
  cluster f^g -> extreal-yielding;
coherence
proof
  A1: rng f c= ExtREAL & rng g c= ExtREAL by VALUED_0:def 2;
  rng (f^g) = rng f \/ rng g by FINSEQ_1:44; then
  rng (f^g) c= ExtREAL by A1,XBOOLE_1:8;
  hence thesis by VALUED_0:def 2;
end;
end;

theorem Th26: 
for F being extreal-yielding FinSequence, r be Element of ExtREAL holds
Product (F^<*r*>) = Product F * r
proof
  let F be extreal-yielding FinSequence, r be Element of ExtREAL;
  rng F c= ExtREAL & rng (F^<*r*>) c= ExtREAL by VALUED_0:def 2; then
  reconsider Fr = F^<*r*>, Ff = F as FinSequence of ExtREAL by FINSEQ_1:def 4;
  reconsider Ff1=Ff as extreal-yielding FinSequence;
  Product (F^<*r*>) = multextreal $$ Fr by Def12; then
  Product (F^<*r*>) = multextreal.(multextreal $$ Ff,r) by FINSOP_1:5; then
  Product (F^<*r*>) = multextreal.(Product Ff1,r) by Def12;
  hence thesis by Def14;
end;

theorem Th10:
for x be Element of ExtREAL holds x|^1 = x
proof
  let x be Element of ExtREAL;
  Product(1 |-> x) = Product(<*x*>) by FINSEQ_2:73;
  hence x|^1 = x by Th25;
end;

theorem Th11:
for x be Element of ExtREAL, k be natural number holds
x|^(k+1) = x|^k*x 
proof
  let x be Element of ExtREAL;
  defpred P[Nat] means x|^($1+1) = x|^$1*x;
  x|^(0+1) = Product(<*x*>) by FINSEQ_2:73; then
  x|^(0+1) = x by Th25; then
  x|^(0+1) =1. * x by EXTREAL2:4; then 
  A1: P[0] by FINSEQ_2:72,Th24; 
  A2: for k be Nat st P[k] holds P[k+1] 
  proof 
    let k be Nat;
    assume x|^(k+1) = x|^k*x; 
    reconsider k1=k+1 as Element of NAT;
    Product((k1+1) |-> x) = Product((k1 |-> x) ^ <*x*>) by FINSEQ_2:74;
    hence thesis by Th26; 
  end; 
  for k be Nat holds P[k] from NAT_1:sch 2(A1,A2); 
  hence thesis; 
end; 
 
definition let k be natural number, X,f;
  func f|^k -> PartFunc of X,ExtREAL means :Def16:
    dom it = dom f &
    for x be Element of X st x in dom it holds it.x = (f.x)|^k;
existence 
proof 
  defpred X[set] means $1 in dom f;
  deffunc U(set) = (f.$1) |^ k; 
  consider f3 being PartFunc of X, ExtREAL such that
  A1: for d be Element of X holds d in dom f3 iff X[d] and
  A2: for d be Element of X st d in dom f3 holds f3/.d = U(d)
      from PARTFUN2:sch 2; 
  take f3;
  (for x be set st x in dom f3 holds x in dom f) &
  for x be set st x in dom f holds x in dom f3 by A1; then
  dom f3 c= dom f & dom f c= dom f3 by TARSKI:def 3;
  hence dom f3 = dom f by XBOOLE_0:def 10;
  let d be Element of X;
  assume A0: d in dom f3; then
  f3/.d = (f.d) |^ k by A2;
  hence thesis by A0,PARTFUN1:def 8;
end;
uniqueness 
proof 
  let f1,f2 be PartFunc of X, ExtREAL;
  assume that
  A5: dom f1= dom f &
      for d being Element of X st d in dom f1 holds f1.d=(f.d) |^ k and
  A6: dom f2= dom f &
      for d being Element of X st d in dom f2 holds f2.d=(f.d) |^ k;
  for d being Element of X st d in dom f holds f1.d = f2.d
  proof
    let d be Element of X;
    assume d in dom f; then
    f1.d=(f.d) |^ k & f2.d=(f.d) |^ k by A5,A6;
    hence f1.d = f2.d;
  end;
  hence f1=f2 by A5,A6,PARTFUN1:34;
end;
end;

theorem Th12:
for x be Element of ExtREAL, y be real number, k be natural number st
  x=y holds x|^k = y|^k  
proof
  let x be Element of ExtREAL, y be real number, k be natural number;
  assume AS0: x=y;
  defpred P[natural number] means x|^$1=y|^$1;
  x|^0 = 1. by FINSEQ_2:72,Th24; then
  A1: P[0] by NEWTON:9;
  A2: for k be natural number st P[k] holds P[k+1]
   proof
     let k be natural number;
     reconsider y1=y as Element of REAL by XREAL_0:def 1;
     assume P[k]; then
     (x|^k)*x = (y1|^k)*y1 by AS0,EXTREAL1:13; then
     (x|^k)*x = y|^(k+1) by NEWTON:11;
     hence P[k+1] by Th11;
   end;
   for k be natural number holds P[k] from NAT_1:sch 2(A1,A2); 
   hence thesis; 
end;

theorem Th14:
for x be Element of ExtREAL, k be natural number st 0 <=x holds 0 <= x|^k
proof
  let x be Element of ExtREAL, k be natural number;
  assume AS0: 0 <=x;
  defpred P[natural number] means 0 <= x|^$1;
  A1:P[0] by FINSEQ_2:72,Th24;
  A2: for k be natural number st P[k] holds P[k+1]
  proof
    let k be natural number;
    assume AS1: P[k];
    x|^(k+1)=(x|^k)*x by Th11;
    hence P[k+1] by AS0,AS1,EXTREAL2:45;
  end;
  for k be natural number holds P[k] from NAT_1:sch 2(A1,A2);
  hence thesis;
end;

theorem Th15:
for k be natural number st 1<=k holds +infty|^k =+infty
proof
  defpred P[Nat] means +infty|^$1 = +infty;
  A1:P[1] by Th10;
  A2: for k be non empty Nat st P[k] holds P[k+1]
  proof 
    let k be non empty Nat;
    assume AS2: P[k];
    +infty|^(k+1)=(+infty|^k)*+infty by Th11;
    hence P[k+1] by AS2,EXTREAL1:def 1;
  end;
  for k be non empty Nat holds P[k] from NAT_1:sch 10(A1,A2);
  hence thesis; 
end; 

theorem Th103:
for k be natural number, X,S,f,E st
  E c= dom f & f is_measurable_on E holds (|.f.|) |^ k is_measurable_on E 
proof
   let k be natural number;
   let X,S,f,E;
   reconsider k1=k as Element of NAT by ORDINAL1:def 13;
   assume
A1: E c= dom f & f is_measurable_on E;
P1:dom ((|.f.|)|^k) = dom |.f.| by Def16; then
P2:dom ((|.f.|)|^k) = dom f by MESFUNC1:def 10;
   per cases;
   suppose C1: k = 0;
A42:for r be real number st 1 < r holds
     E /\ less_dom((|.f.|)|^k,R_EAL r) is_measurable_on S
    proof
     let r be real number;
     assume AA22: 1 < r;
     E c= less_dom((|.f.|)|^k,R_EAL r)
     proof
      let x be set;
      assume AS21:x in E; then
      ((|.f.|)|^k).x = ((|.f.|).x)|^k by A1,Def16,P2; then
      ((|.f.|)|^k).x < r by AA22,C1,FINSEQ_2:72,Th24;
      hence x in less_dom((|.f.|)|^k,R_EAL r) by A1,AS21,P2,MESFUNC1:def 12;
     end; then
     E /\ less_dom((|.f.|)|^k,R_EAL r) = E by XBOOLE_1:28;
     hence E /\ less_dom((|.f.|) |^k,R_EAL r) is_measurable_on S
        by MESFUNC1:def 11;
    end;
X0: E c= dom ((|.f.|)|^k) by A1,P1,MESFUNC1:def 10;
    for r be real number holds
     E /\ less_dom((|.f.|)|^k,R_EAL r) is_measurable_on S
    proof
     let r be real number;
     now assume AA21:r <= 1;
      E c= great_eq_dom((|.f.|)|^k,R_EAL r)
      proof 
       let x be set; 
       assume AS21: x in E; then
       ((|.f.|)|^k).x = ((|.f.|).x)|^k by A1,Def16,P2; then
       r <= ((|.f.|)|^k).x by AA21,C1,FINSEQ_2:72,Th24;
       hence x in great_eq_dom((|.f.|)|^k,R_EAL r)
          by A1,AS21,P2,MESFUNC1:def 15;
      end; then
      E /\ great_eq_dom((|.f.|)|^k,R_EAL r) = E by XBOOLE_1:28; then
      E /\ less_dom((|.f.|)|^k,R_EAL r) = E \ E by X0,MESFUNC1:21;
      hence E /\ less_dom((|.f.|)|^k,R_EAL r) is_measurable_on S
                                                 by MESFUNC1:def 11;
     end;
     hence thesis by A42;
    end;
    hence (|.f.|)|^ k is_measurable_on E by MESFUNC1:def 17;
   end;
   suppose XX:k <> 0; then
X1: 1 <= k by NAT_1:14;
X2: 1/k * k = 1 by XX,XCMPLX_1:88;
A22:for r be real number st 0 < r holds
     great_eq_dom((|.f.|)|^k,R_EAL r)
       = great_eq_dom(|.f.|,R_EAL r to_power (1/k))
    proof
     let r be real number;
     assume AS0: 0 < r;
     L:great_eq_dom(|.f.|,R_EAL r to_power (1/k))
         c= great_eq_dom((|.f.|)|^k,R_EAL r) 
     proof 
      let x be set;
      assume AS21: x in great_eq_dom(|.f.|,R_EAL r to_power (1/k)); then
      AS20: x in dom |.f.| by MESFUNC1:def 15; then
      |.f.| .x = |.f.x.| by MESFUNC1:def 10; then
      P223: 0 <= |.f.| .x by EXTREAL2:51;
K2:   ((|.f.|)|^k).x = (|.f.| .x)|^k by P1,AS20,Def16;
K1:   now assume |.f.| .x = +infty; then
       ((|.f.|).x) |^k = +infty by Th15,X1;
       hence r <= ((|.f.|)|^k).x by K2,XXREAL_0:3;
      end;
      now assume |.f.| .x <> +infty; then
       reconsider fx = |.f.| .x as Element of REAL by P223,SUPINF_2:2;
       Q222: ((|.f.|)|^k).x = fx|^k by K2,Th12; 
       R0: ex y being R_eal st
          y = |.f.| .x & R_EAL r to_power (1/k) <= y by AS21,MESFUNC1:def 15;
       R2: 0 < r to_power (1/k) by AS0,POWER:39;
       reconsider R = r to_power (1/k) as Real by XREAL_0:def 1;
       R to_power k1 = r to_power (1/k*k) by AS0,POWER:38; then
       R9: r to_power 1 <= fx to_power k by X2,X1,R0,R2,HOLDER_1:3;
       fx to_power k1 = fx|^k1 by X1,POWER:47;
       hence r <=((|.f.|) |^k).x by Q222,R9,POWER:30;
      end;
      hence
        x in great_eq_dom((|.f.|)|^k,R_EAL r) by K1,P1,AS20,MESFUNC1:def 15;
     end;
     great_eq_dom((|.f.|)|^k,R_EAL r)
       c= great_eq_dom(|.f.|,R_EAL r to_power (1/k))
     proof 
      let x be set; 
      assume BS21: x in great_eq_dom((|.f.|)|^k,R_EAL r); then
      BS20: x in dom ((|.f.|)|^k) by MESFUNC1:def 15; then
      P221: |.f.| .x = |.f.x.| by P1,MESFUNC1:def 10; then
      P223: 0 <= |.f.| .x by EXTREAL2:51;
      per cases;
      suppose |.f.| .x = +infty; then
       R_EAL r to_power (1/k) <= |.f.| .x by XXREAL_0:3;
       hence x in great_eq_dom(|.f.|, R_EAL r to_power (1/k))
                               by P1,BS20,MESFUNC1:def 15;
      end;
      suppose |.f.| .x <> +infty; then
       reconsider fx= |.f.| .x as Element of REAL by P223,SUPINF_2:2;
       R01: 0 < 1/k by X1,REAL_2:127;
       R0: ex y being R_eal st
          y = ((|.f.|)|^k).x & r <= y by BS21,MESFUNC1:def 15;
       ((|.f.|)|^k).x = ((|.f.|).x)|^k by BS20,Def16; then
       ((|.f.|)|^k).x = fx|^k by Th12; then
       R3: r <= fx to_power k1 by R0,X1,POWER:47; 
       r is Real by XREAL_0:def 1; then
       R5: r to_power (1/k) <= (fx to_power k) to_power (1/k)
                                   by R01,R3,AS0,HOLDER_1:3;
XXX:   0 < k by XX;
       0 <= fx by P221,EXTREAL2:51; then
       (fx to_power k) to_power (1/k) = fx to_power (k * 1/k)
         by XXX,R01,HOLDER_1:2; then
       (fx to_power k) to_power (1/k) = fx by X2,POWER:30;
       hence x in great_eq_dom(|.f.|, R_EAL r to_power (1/k))
                           by P1,BS20,R5,MESFUNC1:def 15;
      end;
     end;
     hence thesis by L,XBOOLE_0:def 10;
    end;
A12:|.f.| is_measurable_on E by A1,MESFUNC2:29;
    for r be real number holds
     E /\ great_eq_dom((|.f.|)|^k,R_EAL r) is_measurable_on S
    proof
     let r be real number;
     per cases;
     suppose A021: r <= 0;
      E c= great_eq_dom((|.f.|)|^k,R_EAL r)
      proof
       let x be set;
       assume AS21:x in E; then
        AA12: ((|.f.|)|^k).x = ((|.f.|).x)|^k by A1,Def16,P2;
       (|.f.|).x = |.f.x.| by A1,AS21,MESFUNC1:def 10,P1,P2; then
       0 <= (|.f.|).x by EXTREAL2:51; then
       r <= ((|.f.|)|^k).x by A021,AA12,Th14;
       hence x in great_eq_dom((|.f.|)|^k,R_EAL r)
                       by A1,AS21,P2,MESFUNC1:def 15;
      end; then
      E /\ great_eq_dom((|.f.|)|^k,R_EAL r) = E by XBOOLE_1:28;
      hence E /\ great_eq_dom((|.f.|)|^k,R_EAL r) is_measurable_on S
        by MESFUNC1:def 11;
     end;
     suppose 0 <r; then
      E /\ great_eq_dom((|.f.|)|^k,R_EAL r) = 
       E /\ great_eq_dom(|.f.|,R_EAL r to_power (1/k)) by A22;
      hence E /\ great_eq_dom((|.f.|)|^k,R_EAL r) is_measurable_on S
        by A1,P1,P2,A12,MESFUNC1:31;
     end;
    end;
    hence (|.f.|)|^ k is_measurable_on E by A1,MESFUNC1:31,P2;
   end;
end;

theorem Th105:
dom f /\ dom g = E & f is finite & g is finite & 
f is_measurable_on E & g is_measurable_on E implies 
  f(#)g is_measurable_on E
proof
  assume
A1:dom f /\ dom g = E &
   f is finite & g is finite & 
   f is_measurable_on E & g is_measurable_on E;
P1:dom ((|.f+g.|)|^2) = dom |.f+g.| &
   dom ((|.f-g.|)|^2) = dom |.f-g.| by Def16; then
P2:dom ((|.f+g.|)|^2) = dom (f+g) &
   dom ((|.f-g.|)|^2) = dom (f-g) by MESFUNC1:def 10; then
P3:dom ((|.f+g.|)|^2) = dom f /\ dom g &
   dom ((|.f-g.|)|^2) = dom f /\ dom g by A1,MESFUNC2:2; then
P4:dom ((|.f+g.|)|^2) c= dom f & dom ((|.f+g.|)|^2) c= dom g &
   dom ((|.f-g.|)|^2) c= dom f & dom ((|.f-g.|)|^2) c= dom g
                                           by XBOOLE_1:17;
P5:dom (f(#)g) = dom f /\ dom g by MESFUNC1:def 5;
P8:dom ((1/4)(#)(|.f+g.|)|^2) = dom ((|.f+g.|)|^2) &
   dom ((1/4)(#)(|.f-g.|)|^2) = dom ((|.f-g.|)|^2) by MESFUNC1:def 6;
S1:for x be Element of X st 
    x in dom ((|.f+g.|)|^2) holds |. ((|.f+g.|)|^2).x .| < +infty
   proof
    let x be Element of X;
    assume A00: x in dom ((|. f+g .|)|^2); then
    |.f.x.| < +infty & |.g.x.| < +infty by A1,MESFUNC2:def 1,P4; then
    reconsider c1 = f.x, c2 = g.x as Real by EXTREAL2:102;
    ((|.f+g.|)|^2).x = ((|.f+g.|).x)|^(1+1) by A00,Def16; then
A14:((|.f+g.|)|^2).x = (|.f+g.| .x)|^1 * (|.f+g.| .x) by Th11;
    f.x + g.x = c1 + c2 by SUPINF_2:1; then
    |. f.x + g.x .| = abs(c1+c2) by EXTREAL2:49; then 
A15:|. f.x + g.x .| * |. f.x + g.x .| = abs(c1+c2)*abs(c1+c2) by EXTREAL1:13;
    |.f+g.| .x = |.(f+g).x.| & |.(f+g).x.| = |. f.x+g.x .|
         by A00,P1,P2,MESFUNC1:def 3,def 10; then
    |.((|.f+g.|)|^2).x.| 
      = |. |. f.x + g.x .| * |. f.x + g.x .| .| by A14,Th10
     .= |. |.(f.x + g.x) * (f.x + g.x).| .| by EXTREAL2:56
     .= |.(f.x + g.x) * (f.x + g.x).| by EXTREAL2:70
     .= |. f.x + g.x .| * |. f.x + g.x .| by EXTREAL2:56;
    hence thesis by A15,XXREAL_0:9;
   end;
   for x be Element of X st 
    x in dom ((|.f-g.|)|^2) holds |.((|.f-g.|)|^2).x .| < +infty
   proof
    let x be Element of X;
    assume B00: x in dom ((|.f-g.|)|^2); then
    |.f.x.| < +infty & |.g.x.| < +infty by A1,MESFUNC2:def 1,P4; then
    reconsider c1 = f.x, c2 = g.x as Real by EXTREAL2:102;
    ((|.f-g.|)|^2).x = ((|.f-g.|).x)|^(1+1) by B00,Def16; then
B14:((|.f-g.|)|^2).x = ((|.f-g.|).x)|^1 * (|.f-g.|).x by Th11;
    f.x - g.x = c1 - c2 by SUPINF_2:5; then
    |. f.x-g.x.| = abs(c1-c2) by EXTREAL2:49; then
B15:|. f.x-g.x .| * |. f.x-g.x .| = abs(c1-c2)*abs(c1-c2) by EXTREAL1:13;
    ((|.f-g.|).x) = |.(f-g).x.| by B00,P1,MESFUNC1:def 10; then
    ((|.f-g.|).x) = |. f.x-g.x .| by B00,P2,MESFUNC1:def 4; then
    |.((|.f-g.|)|^2).x.| 
      = |. |. f.x - g.x .| * |. f.x - g.x .| .| by B14,Th10
     .= |. |.(f.x - g.x) * (f.x - g.x).| .| by EXTREAL2:56
     .= |.(f.x - g.x) * (f.x - g.x).| by EXTREAL2:70
     .= |. f.x - g.x .| * |. f.x - g.x .| by EXTREAL2:56;
    hence thesis by B15,XXREAL_0:9;
   end; then
   (|.f+g.|)|^2 is finite & (|.f-g.|)|^2 is finite by S1,MESFUNC2:def 1; then
A4:(1/4)(#)(|.f+g.|)|^2 is finite & (1/4)(#)(|.f-g.|)|^2 is finite
      by MESFUNC2:12;
A2:f(#)g = (1/4)(#)(|.f+g.|)|^2 - (1/4)(#)(|.f-g.|)|^2
   proof
C1: dom ((1/4)(#)(|.f+g.|)|^2 - (1/4)(#)(|.f-g.|)|^2)
     = dom((1/4)(#)(|.f+g.|)|^2) /\ dom((1/4)(#)(|.f-g.|)|^2) by A4,MESFUNC2:2;
    for x be Element of X st x in dom (f(#)g) holds 
          (f(#)g).x = ((1/4)(#)(|.f+g.|) |^ 2 - (1/4)(#)(|.f-g.|) |^ 2).x
    proof
     let x be Element of X;
     assume B01: x in dom (f(#)g); then 
B2:  |.(f+g).x.| = |. f.x+g.x .| & |.(f-g).x.| = |. f.x-g.x .| &
     |.f+g.| .x = |.(f+g).x.| & |.f-g.| .x = |.(f-g).x.| 
       by P5,P1,P2,P3,MESFUNC1:def 3,def 4,def 10;
     ((1/4)(#)(|.f+g.|)|^2).x = R_EAL(1/4) * ((|.f+g.|)|^2).x &
     ((1/4)(#)(|.f-g.|)|^2).x = R_EAL(1/4) * ((|.f-g.|)|^2).x  
                                         by P5,B01,P8,P3,MESFUNC1:def 6; then
     ((1/4)(#)(|.f+g.|)|^2).x = R_EAL(1/4) * (|.f+g.| .x)|^(1+1) &
     ((1/4)(#)(|.f-g.|)|^2).x = R_EAL(1/4) * (|.f-g.| .x)|^(1+1)
                                                     by P5,B01,P3,Def16; then
     ((1/4)(#)(|.f+g.|)|^2).x = R_EAL(1/4) * ((|.f+g.| .x)|^1 * (|.f+g.| .x)) &
     ((1/4)(#)(|.f-g.|)|^2).x = R_EAL(1/4) * ((|.f-g.| .x)|^1 * (|.f-g.| .x)) 
         by Th11; then
B29: ((1/4)(#)(|.f+g.|)|^2).x = R_EAL(1/4) * (|. f.x+g.x .| * |. f.x+g.x .|) &
     ((1/4)(#)(|.f-g.|)|^2).x = R_EAL(1/4) * (|. f.x-g.x .| * |. f.x-g.x .|) 
         by B2,Th10;
     |.f.x.| <+infty & |.g.x.| <+infty by A1,P3,P4,P5,B01,MESFUNC2:def 1; then
     reconsider c1 = f.x, c2 = g.x as Real by EXTREAL2:102; 
     f.x + g.x = c1 + c2 & f.x - g.x = c1 - c2 by SUPINF_2:1,5; then
     |. f.x+g.x .| = abs(c1+c2) & |. f.x-g.x .| = abs(c1-c2)
         by EXTREAL2:49; then
     |. f.x+g.x .| * |. f.x+g.x .| = abs(c1+c2)*abs(c1+c2) &
     |. f.x-g.x .| * |. f.x-g.x .| = abs(c1-c2)*abs(c1-c2) by EXTREAL1:13; then
C0:  ((1/4)(#)(|.f+g.|)|^2).x=(1/4)*(abs(c1+c2)*abs(c1+c2)) &
     ((1/4)(#)(|.f-g.|)|^2).x=(1/4)*(abs(c1-c2)*abs(c1-c2)) 
                                               by B29,EXTREAL1:13;  
     (abs(c1+c2)*abs(c1+c2)) = (abs(c1+c2))^2 & 
     (abs(c1-c2)*abs(c1-c2)) = (abs(c1-c2))^2 by SQUARE_1:def 3; then
     (abs(c1+c2)*abs(c1+c2)) = (c1+c2)^2 &
     (abs(c1-c2)*abs(c1-c2)) = (c1-c2)^2 by COMPLEX1:161; then
     (abs(c1+c2)*abs(c1+c2)) = c1^2+2*c1*c2+c2^2 &
     (abs(c1-c2)*abs(c1-c2)) = c1^2-2*c1*c2+c2^2 by SQUARE_1:63,64; then
     ((1/4)(#)(|.f+g.|)|^2).x -((1/4)(#)(|.f-g.|)|^2).x 
      =(1/4)*(c1^2+2*c1*c2+c2^2) - (1/4)*(c1^2-2*c1*c2+c2^2) by C0,SUPINF_2:5
     .= c1*c2
     .= (f.x)*(g.x) by EXTREAL1:13
     .= (f(#)g).x by B01,MESFUNC1:def 5;
     hence thesis by C1,P8,P3,P5,B01,MESFUNC1:def 4;
    end; 
    hence thesis by P5,C1,P8,P3,PARTFUN1:34; 
   end; 
   f+g is_measurable_on E & f-g is_measurable_on E 
     by A1,P3,P4,MESFUNC2:7,13; then
   (|.f+g.|)|^2 is_measurable_on E &
   (|.f-g.|)|^2 is_measurable_on E by A1,P2,P3,Th103; then
   (1/4)(#)(|.f+g.|)|^2 is_measurable_on E & 
   (1/4)(#)(|.f-g.|)|^2 is_measurable_on E by A1,P3,MESFUNC5:55;
   hence thesis by A2,P8,A1,P3,A4,MESFUNC2:13; 
end;

theorem AAF:
rng f is bounded implies f is finite 
proof
  assume A1: rng f is bounded; then
  rng f is bounded_above by SUPINF_1:def 13; then
  consider UB being majorant of rng f such that
P1: UB in REAL by SUPINF_1:def 11; 
  rng f is bounded_below by A1,SUPINF_1:def 13; then
  consider LB being minorant of rng f such that
P2: LB in REAL by SUPINF_1:def 12; 
P3: UB < +infty & -infty < LB by P1,P2,XXREAL_0:9,12;
  now let x be Element of X;
    assume x in dom f; then
    f.x in rng f by FUNCT_1:12; then
    f.x <= UB & LB <= f.x by SUPINF_1:def 9,def 10; then
Q1: f.x < +infty & -infty < f.x by P3,XXREAL_0:2; then
    -(+infty) < f.x by EXTREAL1:4;
    hence |. f.x .| < +infty by Q1,EXTREAL2:59;
  end;
  hence f is finite by MESFUNC2:def 1;
end;

theorem 
for M be sigma_Measure of S,
    f,g be PartFunc of X, ExtREAL,
    E be Element of S,
    F be non empty Subset of ExtREAL st
dom f /\ dom g = E & rng f = F & g is finite &
f is_measurable_on E & rng f is bounded & g is_integrable_on M
holds
  (f(#)g)|E is_integrable_on M &
  ex c be Element of REAL st 
     c >= inf F & c <= sup F &
    Integral(M, (f(#)|.g.|)|E) = R_EAL c * Integral(M, (|.g.|)|E)
proof
   let M be sigma_Measure of S,
       f,g be PartFunc of X, ExtREAL,
       E be Element of S,
       F be non empty Subset of ExtREAL;
   assume
A1:   dom f /\ dom g = E & rng f = F &
      g is finite &
      f is_measurable_on E &
      rng f is bounded & g is_integrable_on M;
      consider E1 be Element of S such that
PP1:   E1 = dom g & g is_measurable_on E1 by A1,MESFUNC5:def 17;
PP3:  E1 = dom |.g.| & |.g.| is_measurable_on E1
         by PP1,MESFUNC1:def 10,MESFUNC2:29;
      E c= E1 by A1,PP1,XBOOLE_1:17; then
R2:   g is_measurable_on E & |.g.| is_measurable_on E by PP1,PP3,MESFUNC1:34;
B11:  rng f is bounded_below & rng f is bounded_above 
                                               by A1,SUPINF_1:def 13; 
R1:   f is finite by A1,AAF; then
R3:   rng f is Subset of REAL by MESFUNC2:35; then
      not +infty in rng f & not -infty in rng f by SUPINF_1:1,6; then
      rng f <> {+infty} & rng f <> {-infty} by TARSKI:def 1; then
      reconsider k0=inf F, l0=sup F as Real by B11,A1,SUPINF_1:83,84; 
      |.inf F.|=abs k0 & |.sup F.|=abs l0 by EXTREAL2:49; then
      reconsider k1= |.inf F.|, l1= |.sup F.| as Real;
BS3:  dom |.g.| = dom g by MESFUNC1:def 10;
BS1:  E c= dom f & E c= dom g by A1,XBOOLE_1:17; then
BS4:  E c= dom |.g.| by MESFUNC1:def 10;
CS1:  dom (f(#)g) = E by A1,MESFUNC1:def 5; then
CS6:  dom((f(#)g)|E) = E by RELAT_1:91; then
CS5:  dom (|.(f(#)g)|E.|) = E by MESFUNC1:def 10;
      dom ( ((k1+l1)(#)|.g.|)|E ) = dom ( (k1+l1)(#)(|.g.|)|E ) by AAA; then
      dom ( ((k1+l1)(#)|.g.|)|E ) = dom ((|.g.|)|E) by MESFUNC1:def 6; then
DS3:  dom ( ((k1+l1)(#)|.g.|)|E ) = E by BS4,RELAT_1:91;
DS4:  dom ((k1+l1)(#)|.g.|) = dom |.g.| by MESFUNC1:def 6; 
FS1:  dom (f(#)|.g.|) =  dom f /\ dom |.g.| by MESFUNC1:def 5; then
FS2:  dom (f(#)|.g.|) = E by A1,MESFUNC1:def 10;  
FS3:  dom ((f(#)|.g.|)|E) = dom (f(#)|.g.|) /\ E by FUNCT_1:68; 
M1:   inf F is minorant of rng f & sup F is majorant of rng f
                               by A1,SUPINF_1:def 16,def 17;
A21:  for x be Element of X st x in E holds 
                                     |. f.x .| <= |.inf F.| + |.sup F.|
      proof
        let x be Element of X;
        assume x in E; then 
A212:   f.x in rng f by BS1,FUNCT_1:12; then
        reconsider fx=f.x as Real by R3;  
A217:   k0 <=fx & fx <=l0 by A212,M1,SUPINF_1:def 9,def 10;
        l0 <= abs l0 & 0 <= abs k0 by COMPLEX1:162,132; then 
        l0+abs k0 <=abs l0 + abs k0 & l0+0 <=l0+abs k0 by XREAL_1:8; then
P7:     l0 <=abs l0 + abs k0 by XXREAL_0:2;
        0 <= abs l0 by COMPLEX1:132; then 
Q1:     -abs k0 -abs l0 <= -abs k0 -0 by XREAL_1:12;
        -abs k0 <= k0 by COMPLEX1:162; then
        -abs k0 -abs l0 <= k0 by XXREAL_0:2,Q1; then 
        -(abs k0 + abs l0) <= fx & fx <= abs k0 + abs l0 
                           by P7,XXREAL_0:2,A217; then 
P10:    abs fx <= abs k0 + abs l0 by ABSVALUE:12; 
        abs fx = |.f.x.| & abs k0= |.inf F.| & 
                                          abs l0 = |.sup F.| by EXTREAL2:49;
        hence |.f.x.| <= |.inf F.| + |.sup F.| by P10,SUPINF_2:1;  
      end;
A22:  |.g.| is_integrable_on M by A1,PP1,MESFUNC5:106; then
A220: (|.g.|)|E is_integrable_on M by MESFUNC5:103;
      (k1+l1)(#)|.g.| is_integrable_on M by A22,MESFUNC5:116; then
A25:  ((k1+l1)(#)|.g.|)|E is_integrable_on M by MESFUNC5:103;
A20:  f(#)g is_measurable_on E by A1,R1,R2,Th105; 
      dom (f(#)g) /\ E = E by CS1; then 
A202: (f(#)g)|E is_measurable_on E by A20,MESFUNC5:48;
A205: for x be Element of X st x in dom ((f(#)g)|E) holds 
               |.((f(#)g)|E).x.| <= (((k1+l1)(#)|.g.|)|E).x 
      proof
        let x be Element of X;
        assume A2051: x in dom ((f(#)g)|E); then
B03:    ((f(#)g)|E).x = (f(#)g).x by FUNCT_1:70; 
        dom (f|E) = E & dom (g|E) = E by BS1,RELAT_1:91; then
B04:    (f|E).x = f.x & (g|E).x = g.x 
                           by FUNCT_1:70,CS6,A2051; then
A242:   |.(f|E).x.| <= |.inf F.| + |.sup F.| by A2051,A21,CS6;
        0 <= |.(g|E).x.| by EXTREAL2:51; then
A24:    |.(f|E).x.|*|.(g|E).x.| <= (|.inf F.| + |.sup F.|)*|.(g|E).x.| 
          by A242,EXTREAL1:42;        
B06:    (((k1+l1)(#)|.g.|)|E).x = ((k1+l1)(#)|.g.|).x 
                                       by A2051,CS6,DS3,FUNCT_1:70;
        |.(f(#)g).x.| = |. f.x * g.x .| 
                                by A2051,CS1,CS6,MESFUNC1:def 5; then
A2052:  |.((f(#)g)|E).x.| = |.(f|E).x.|*|.(g|E).x.| 
                                              by B03,B04,EXTREAL2:56; 
        ((k1+l1)(#)|.g.|).x = R_EAL(k1+l1) * |.g.| .x 
                            by A2051,CS6,BS4,DS4,MESFUNC1:def 6; then
        (((k1+l1)(#)|.g.|)|E).x = R_EAL(k1+l1)*|.(g|E).x.| 
                             by B04,B06,BS4,A2051,CS6,MESFUNC1:def 10;
        hence thesis by A24,A2052,SUPINF_2:1;
      end; then
      (f(#)g)|E is_integrable_on M 
                       by CS6,A202,DS3,A25,MESFUNC5:108; then
A26:  |.((f(#)g)|E).| is_integrable_on M by A202,CS6,MESFUNC5:106;  
      for x be Element of X st x in dom |.g.| holds |. |.g.| .x.| < +infty
      proof
        let x be Element of X;
        assume B01: x in dom |.g.|; then
        |. |.g.| .x.| = |.|.g.x.|.| by MESFUNC1:def 10; then
        |. |.g.| .x.| = |.g.x.| by EXTREAL2:70;
        hence thesis by B01,A1,BS3,MESFUNC2:def 1;
      end; then
A3124:|.g.| is finite by MESFUNC2:def 1;
      dom f /\ dom |.g.| = E by A1,MESFUNC1:def 10; then
AS312:f(#)|.g.| is_measurable_on E by A1,R1,R2,A3124,Th105; 
      dom (f(#)|.g.|) /\ E = E by FS2; then 
A312: (f(#)|.g.|)|E is_measurable_on E by AS312,MESFUNC5:48;
      for x be Element of X st x in dom ((f(#)|.g.|)|E) holds 
                   |.((f(#)|.g.|)|E).x.| <= |.((f(#)g)|E).| .x 
      proof
        let x be Element of X;
        assume A3141: x in dom ((f(#)|.g.|)|E); then 
        |. (f(#)|.g.|).x.| = |. f.x*|.g.|.x .| by FS3,FS2,MESFUNC1:def 5
         .= |. f.x*|.g.x.| .| by A3141,FS3,FS1,A1,BS3,BS1,MESFUNC1:def 10
         .= |.f.x.| * |. |.g.x.| .| by EXTREAL2:56
         .= |.f.x.|*|.g.x.| by EXTREAL2:70; then
A3144:  |. (f(#)|.g.|).x.| = |. f.x*g.x .| by EXTREAL2:56;
        dom |.f(#)g.| = E by CS1,MESFUNC1:def 10; then
AS4:    |.(f(#)g).|.x = |.(f(#)g).x.| by A3141,FS3,FS2,MESFUNC1:def 10; 
B01:    ((f(#)|.g.|)|E).x = (f(#)|.g.|).x by A3141,FUNCT_1:70;
        |. ((f(#)g)|E).x .| = |. (f(#)g).x .| 
                              by FUNCT_1:70,A3141,CS6,FS3,FS2; then
        |.((f(#)g)|E).| .x = |. f(#)g .| .x 
                          by A3141,CS5,FS3,FS2,AS4,MESFUNC1:def 10;
        hence thesis by AS4,B01,CS1,A3144,A3141,FS3,FS2,MESFUNC1:def 5;
      end; then
A31:  (f(#)|.g.|)|E is_integrable_on M 
                                 by A312,FS3,FS2,CS5,A26,MESFUNC5:108; 
A32:  for x be Element of X st x in E holds 
        inf F*|.g.x.| <= (f.x)*|.g.x.| & (f.x)*|.g.x.| <= sup F*|.g.x.|
      proof
        let x be Element of X;
        assume x in E; then
        f.x in rng f by BS1,FUNCT_1:12; then
A322:   inf F <= f.x & f.x <= sup F by M1,SUPINF_1:def 9,def 10;
        0 <= |.g.x.| by EXTREAL2:51;
        hence thesis by A322,EXTREAL1:42;
      end;
A01:  dom (k0(#)|.g.|) = dom |.g.| & dom (l0(#)|.g.|) = dom |.g.|
                                               by MESFUNC1:def 6; then
A02:  dom ((k0(#)|.g.|)|E) = E & dom ((l0(#)|.g.|)|E) = E by BS4,RELAT_1:91;
A03:  dom (f(#)|.g.|) c= dom (l0(#)|.g.|) by A01,FS1,XBOOLE_1:17;
      Integral(M, (k0(#)|.g.|)|E) = Integral(M, k0(#)((|.g.|)|E)) &
      Integral(M, (l0(#)|.g.|)|E) = Integral(M, l0(#)((|.g.|)|E)) by AAA; then
A34:  Integral(M, (k0(#)|.g.|)|E) = R_EAL k0 * Integral(M, (|.g.|)|E) &
      Integral(M, (l0(#)|.g.|)|E) = R_EAL l0 * Integral(M, (|.g.|)|E) 
                                                     by A220,MESFUNC5:116;
      k0(#)|.g.| is_integrable_on M by A22,MESFUNC5:116; then
A364: (k0(#)|.g.|)|E is_integrable_on M by MESFUNC5:103;
      for x be Element of X st x in dom ((k0(#)|.g.|)|E) holds 
       ((k0(#)|.g.|)|E).x <= ((f(#)|.g.|)|E).x 
      proof
        let x be Element of X;
        assume A3641: x in dom ((k0(#)|.g.|)|E); then
        (k0(#)|.g.|).x = (R_EAL k0)*(|.g.|).x &
        (f(#)|.g.|).x = (f.x)*(|.g.|.x) 
          by A01,BS4,A02,FS2,MESFUNC1:def 5,def 6; then
        (k0(#)|.g.|).x = (R_EAL k0)*|.g.x.| &
        (f(#)|.g.|).x = (f.x)*|.g.x.| by A3641,BS4,A02,MESFUNC1:def 10; then
B02:    (k0(#)|.g.|).x <= (f(#)|.g.|).x by A32,A3641,A02;
        ((k0(#)|.g.|)|E).x = (k0(#)|.g.|).x by A3641,FUNCT_1:70; 
        hence thesis by B02,A3641,A02,FS3,FS2,FUNCT_1:70;
      end; then
     (f(#)|.g.|)|E - (k0(#)|.g.|)|E is nonnegative by LMXZ; then
     consider V1 be Element of S such that
EE1:  V1 = dom((k0(#)|.g.|)|E) /\ dom((f(#)|.g.|)|E) &
      Integral(M,((k0(#)|.g.|)|E)|V1) <= Integral(M,((f(#)|.g.|)|E)|V1) 
       by A364,A31,LMXY;
A36: ((k0(#)|.g.|)|E)|V1 = (k0(#)|.g.|)|E & ((f(#)|.g.|)|E)|V1 = (f(#)|.g.|)|E 
                                        by EE1,A02,FS3,FS2,RELAT_1:97; 
      l0(#)|.g.| is_integrable_on M by A22,MESFUNC5:116; then
A374: (l0(#)|.g.|)|E is_integrable_on M by MESFUNC5:103;
    
      for x be Element of X st x in dom ((f(#)|.g.|)|E) holds 
                             ((f(#)|.g.|)|E).x <= ((l0(#)|.g.|)|E).x 
      proof
        let x be Element of X;
        assume A3751: x in dom ((f(#)|.g.|)|E); then
        (f(#)|.g.|).x = (f.x)*(|.g.|).x & (l0(#)|.g.|).x = (R_EAL l0)*(|.g.|.x)
            by FS3,FS2,A03,MESFUNC1:def 5,def 6; then
        (f(#)|.g.|).x = (f.x)*|.g.x.| & (l0(#)|.g.|).x = (R_EAL l0)*|.g.x.| 
            by A3751,BS4,FS3,FS2,MESFUNC1:def 10; then
B03:    (f(#)|.g.|).x <= (l0(#)|.g.|).x by A3751,FS3,FS2,A32; 
        ((f(#)|.g.|)|E).x = (f(#)|.g.|).x by A3751,FUNCT_1:70;
        hence thesis by B03,A3751,FS3,FS2,A02,FUNCT_1:70; 
      end; then 
      (l0(#)|.g.|)|E - (f(#)|.g.|)|E is nonnegative by LMXZ; then
      consider V2 be Element of S such that
EE2:   V2 = dom((l0(#)|.g.|)|E) /\ dom((f(#)|.g.|)|E) &
       Integral(M,((f(#)|.g.|)|E)|V2) <= Integral(M,((l0(#)|.g.|)|E)|V2)
          by A374,A31,LMXY; 
A37:  ((f(#)|.g.|)|E)|V2 = (f(#)|.g.|)|E & ((l0(#)|.g.|)|E)|V2 = (l0(#)|.g.|)|E 
                                      by EE2,FS3,FS2,A02,RELAT_1:97;
A40:  -infty < Integral(M, (f(#)|.g.|)|E) & 
                  Integral(M, (f(#)|.g.|)|E) < +infty by A31,MESFUNC5:102;
      ex c be Element of REAL st c >= inf F & c <= sup F &
       Integral(M,(f(#)|.g.|)|E) = R_EAL c * Integral(M,(|.g.|)|E)
      proof 
       per cases;
       suppose A01: Integral(M, (|.g.|)|E) <> 0.; then
A001:   Integral(M, (|.g.|)|E) > 0. by PP3,MESFUNC5:98;
A38:    -infty < Integral(M, (|.g.|)|E) & Integral(M, (|.g.|)|E) < +infty 
                                              by A220,MESFUNC5:102; then
        reconsider c1=Integral(M, (|.g.|)|E) as Real by EXTREAL1:1;
A419:   Integral(M, (|.g.|)|E)/Integral(M, (|.g.|)|E) =c1/c1 
                                                    by A01,EXTREAL1:32;
        inf F * Integral(M, (|.g.|)|E) = k0*c1 &
        sup F * Integral(M, (|.g.|)|E) = l0*c1 by EXTREAL1:13; then
C1:     (inf F * Integral(M, (|.g.|)|E)) / Integral(M, (|.g.|)|E) = k0*c1/c1 &
        (sup F * Integral(M, (|.g.|)|E)) / Integral(M, (|.g.|)|E) = l0*c1/c1
                                                   by A01,EXTREAL1:32; 
        k0*c1/c1=k0*(c1/c1) & l0*c1/c1=l0*(c1/c1); then
A413:   inf F * Integral(M, (|.g.|)|E) / Integral(M, (|.g.|)|E)
          = inf F *(Integral(M, (|.g.|)|E) / Integral(M, (|.g.|)|E)) &
        sup F * Integral(M, (|.g.|)|E) / Integral(M, (|.g.|)|E)
          = sup F *(Integral(M, (|.g.|)|E) / Integral(M, (|.g.|)|E)) 
                                        by C1,A419,EXTREAL1:13;
        inf F * (Integral(M,(|.g.|)|E) / Integral(M,(|.g.|)|E)) = inf F * 1. &
        sup F * (Integral(M,(|.g.|)|E) / Integral(M,(|.g.|)|E)) = sup F * 1.
                                         by A01,A38,EXTREAL1:34; then
A414:   inf F * (Integral(M, (|.g.|)|E) / Integral(M, (|.g.|)|E)) = inf F &
        sup F * (Integral(M, (|.g.|)|E) / Integral(M, (|.g.|)|E)) = sup F 
                                                           by EXTREAL2:4;
        set c2 = Integral(M, (f(#)|.g.|)|E) / Integral(M, (|.g.|)|E);
        reconsider c3 = Integral(M, (f(#)|.g.|)|E) as Real by A40,EXTREAL1:1;
        Integral(M, (f(#)|.g.|)|E) / Integral(M, (|.g.|)|E) = c3 /c1  
                                                   by A01,EXTREAL1:32; then
        reconsider c = c2 as Element of REAL;
A45:     c >= inf F & c <= sup F 
                  by A38,A36,EE1,A34,A37,EE2,A001,A413,A414,EXTREAL1:48;
        Integral(M, (f(#)|.g.|)|E) / Integral(M, (|.g.|)|E) = c3 / c1 &
        Integral(M, (|.g.|)|E) / Integral(M, (|.g.|)|E) = c1 / c1 
                                            by A01,EXTREAL1:32; then
        Integral(M,(|.g.|)|E) 
                   * ( Integral(M,(f(#)|.g.|)|E) / Integral(M,(|.g.|)|E) )
          = c1 * (c3 / c1) &
        c3 * (c1 / c1)  
          = Integral(M, (f(#)|.g.|)|E) 
                    * ( Integral(M, (|.g.|)|E) / Integral(M, (|.g.|)|E) ) 
                                            by EXTREAL1:13; then
        R_EAL c * Integral(M, (|.g.|)|E) = Integral(M, (f(#)|.g.|)|E) 
                                              by A01,A38,EXTREAL2:19;
        hence thesis by A45;
       end;
       suppose A02: Integral(M, (|.g.|)|E) = 0.;
        consider y be set such that 
A021:    y in F by XBOOLE_0:def 1; 
        reconsider y as Element of ExtREAL by A021; 
        inf F <= y & y <= sup F by SUPINF_1:76,79,A021; then
A50:    inf F <= k0 & k0 <= sup F by XXREAL_0:2;                     
        0. <= Integral(M, (f(#)|.g.|)|E) by A36,EE1,A34,A02,EXTREAL1:22; then
A55:    Integral(M, (f(#)|.g.|)|E) = 0. by A37,EE2,A34,A02,EXTREAL1:22; 
        R_EAL k0 * Integral(M, (|.g.|)|E) = 0. by A02,EXTREAL1:22;
        hence thesis by A50,A55;
       end;
  end;
  hence thesis by CS6,A202,DS3,A205,A25,MESFUNC5:108;
end;

begin  ::  Selected properties of integrals  

reserve E1,E2 for Element of S;
reserve x,A for set;
reserve a,b for real number;

theorem AAE:
(|.f.|)|A = |.(f|A).|
proof
A0:dom((|.f.|)|A) = dom |.f.| /\ A & dom(f|A) = dom f /\ A by FUNCT_1:68; then
A1:dom((|.f.|)|A) = dom f /\ A & dom|.(f|A).| = dom f /\ A by MESFUNC1:def 10;   
   now let x be Element of X;
    assume A2:x in dom((|.f.|)|A); then
A3: ((|.f.|)|A).x = (|.f.|).x by FUNCT_1:70;    
    x in dom f by A1,A2,XBOOLE_0:def 3; then
A4: x in dom |.f.| by MESFUNC1:def 10;    
    (|.(f|A).|).x = |. (f|A).x .| by A1,A2,MESFUNC1:def 10; then
    (|.(f|A).|).x = |. f.x .| by A0,A1,A2,FUNCT_1:70;
    hence ((|.f.|)|A).x = (|.(f|A).|).x by A3,A4,MESFUNC1:def 10;
   end;
   hence thesis by A1,PARTFUN1:34;
end;

theorem AAB:
dom(|.f.|+|.g.|) = dom f /\ dom g & dom |.f+g.| c= dom |.f.|
proof
   set F = |.f.|;
   set G = |.g.|;
A1:dom(|.f.|+|.g.|) =
    (dom F /\ dom G)\((F"{-infty} /\ G"{+infty}) \/ (F"{+infty} /\ G"{-infty}))
      by MESFUNC1:def 3;
   F is without-infty & G is without-infty by MESFUNC5:18; then
   not -infty in rng F & not -infty in rng G by MESFUNC5:def 3; then
   F"{-infty} = {} & G"{-infty} = {} by FUNCT_1:142; then
   dom(|.f.|+|.g.|) = dom f /\ dom G by A1,MESFUNC1:def 10;
   hence dom(|.f.|+|.g.|) = dom f /\ dom g by MESFUNC1:def 10;
   dom |.f+g.| = dom(f+g) by MESFUNC1:def 10
    .= (dom f /\ dom g)
                 \((f"{-infty} /\ g"{+infty}) \/ (f"{+infty} /\ g"{-infty}))
                   by MESFUNC1:def 3
    .= dom f /\ 
         (dom g \((f"{-infty} /\ g"{+infty}) \/ (f"{+infty} /\ g"{-infty})))
                   by XBOOLE_1:49; then
   dom |.f+g.| c= dom f by XBOOLE_1:17;
   hence dom |.f+g.| c= dom |.f.| by MESFUNC1:def 10;
end;

theorem AAB1:
(|.f.|)|(dom |.f+g.|) + (|.g.|)|(dom |.f+g.|) = (|.f.|+|.g.|)|(dom |.f+g.|)
proof
   set F = |.f.|;
   set G = |.g.|;
A0:dom |.f+g.| c= dom |.f.| & dom |.f+g.| c= dom |.g.| by AAB; then
A2:dom |.f+g.| c= dom f & dom |.f+g.| c= dom g by MESFUNC1:def 10;
   dom(f|(dom |.f+g.|)) = dom f /\ dom |.f+g.| &
   dom(g|(dom |.f+g.|)) = dom g /\ dom |.f+g.| by FUNCT_1:68; then
A3:dom(f|(dom |.f+g.|)) = dom |.f+g.| &
   dom(g|(dom |.f+g.|)) = dom |.f+g.| by A2,XBOOLE_1:28; then
A7:dom |.(f|(dom |.f+g.|)).| = dom |.f+g.| &
   dom |.(g|(dom |.f+g.|)).| = dom |.f+g.| by MESFUNC1:def 10;
A5:(|.f.|)|(dom |.f+g.|) = |.(f|(dom |.f+g.|)).| &
   (|.g.|)|(dom |.f+g.|) = |.(g|(dom |.f+g.|)).| by AAE; then
A4:dom((|.f.|)|(dom |.f+g.|) + (|.g.|)|(dom |.f+g.|))
    = dom (f|(dom |.f+g.|)) /\ dom (g|(dom |.f+g.|)) by AAB
   .= dom |.f+g.| by A3;
   dom |.f+g.| /\ dom |.f+g.| c= dom f /\ dom g by A2,XBOOLE_1:27; then
B0:dom |.f+g.| c= dom(|.f.|+|.g.|) by AAB; then
B1:dom((|.f.|+|.g.|)|(dom |.f+g.|)) = dom |.f+g.| by RELAT_1:91;
   now let x be Element of X;
    assume 
B2:  x in dom((|.f.|+|.g.|)|(dom |.f+g.|)); then
B4: ((|.f.|+|.g.|)|(dom |.f+g.|)).x
     = (|.f.|+|.g.|).x by FUNCT_1:70
    .= (|.f.|).x + (|.g.|).x by B2,B1,B0,MESFUNC1:def 3
    .= |. f.x .| + (|.g.|).x by B2,B1,A0,MESFUNC1:def 10;   
A6: x in dom |.f+g.| by B0,B2,RELAT_1:91; then
    ((|.f.|)|(dom |.f+g.|) + (|.g.|)|(dom |.f+g.|)).x
     = ((|.f.|)|(dom |.f+g.|)).x + ((|.g.|)|(dom |.f+g.|)).x 
         by A4,MESFUNC1:def 3
    .= |.(f|(dom |.f+g.|)).x .| + |.(g|(dom |.f+g.|)).|.x
      by A5,A6,A7,MESFUNC1:def 10
    .= |.(f|(dom |.f+g.|)).x .| + |.(g|(dom |.f+g.|)).x .|
      by A6,A7,MESFUNC1:def 10
    .= |. f.x .| + |.(g|(dom |.f+g.|)).x .| by A6,FUNCT_1:72
    .= |. f.x .| + |. g.x .| by A6,FUNCT_1:72;
    hence ((|.f.|+|.g.|)|(dom |.f+g.|)).x 
      = ((|.f.|)|(dom |.f+g.|) + (|.g.|)|(dom |.f+g.|)).x 
        by B4,B2,B1,A0,MESFUNC1:def 10;
   end;
   hence thesis by A4,B1,PARTFUN1:34;
end;

theorem AAD:
x in dom |.f+g.| implies (|.f+g.|).x <= (|.f.|+|.g.|).x
proof
   assume C1:x in dom |.f+g.|; then
C2:x in dom (f+g) by MESFUNC1:def 10; then
   x in (dom f /\ dom g) 
      \ ((f"{-infty} /\ g"{+infty}) \/ (f"{+infty} /\ g"{-infty})) 
           by MESFUNC1:def 3; then
   x in dom f /\ dom g &
   not x in (f"{-infty} /\ g"{+infty}) \/ (f"{+infty} /\ g"{-infty})
         by XBOOLE_0:def 4; then
C3:x in dom f & x in dom g &
   not x in (f"{-infty} /\ g"{+infty}) & not x in (f"{+infty} /\ g"{-infty})
         by XBOOLE_0:def 2,def 3; then
   (not x in f"{-infty} or not x in g"{+infty}) &
   (not x in f"{+infty} or not x in g"{-infty}) by XBOOLE_0:def 3; then
   (not f.x in {-infty} or not g.x in {+infty}) &
   (not f.x in {+infty} or not g.x in {-infty}) by C3,FUNCT_1:def 13; then
   (f.x <> -infty or g.x <> +infty) &
   (f.x <> +infty or g.x <> -infty) by TARSKI:def 1; then
   |. f.x + g.x .| <= |. f.x .| + |. g.x .| by EXTREAL2:61; then
C4:|. (f+g).x .| <= |. f.x .| + |. g.x .| by C2,MESFUNC1:def 3;
C5:dom |.f+g.| c= dom |.f.| & dom |.f+g.| c= dom |.g.| by AAB; then
C6:|. f.x .| = |.f.| .x & |. g.x .| = |.g.| .x by C1,MESFUNC1:def 10;
   x in dom |.f.| & x in dom |.g.| by C1,C5; then
   x in dom f & x in dom g by MESFUNC1:def 10; then
   x in dom f /\ dom g by XBOOLE_0:def 3; then
   x in dom(|.f.| + |.g.|) by AAB; then
   |. f.x .| + |. g.x .| = (|.f.| + |.g.|).x by C6,MESFUNC1:def 3;
   hence thesis by C1,C4,MESFUNC1:def 10;
end;

theorem
f is_integrable_on M & g is_integrable_on M implies
  ex E be Element of S st
    E = dom(f+g) &
    Integral(M,(|.f+g.|)|E) <= Integral(M,(|.f.|)|E) + Integral(M,(|.g.|)|E)
proof
   assume 
A1: f is_integrable_on M & g is_integrable_on M; then
   (ex A be Element of S st A = dom f & f is_measurable_on A) &
   (ex A be Element of S st A = dom g & g is_measurable_on A)
     by MESFUNC5:def 17; then
A2:|.f.| is_integrable_on M & |.g.| is_integrable_on M 
     by A1,MESFUNC5:106; then
A3:|.f.|+|.g.| is_integrable_on M by MESFUNC5:114;
   set F = |.f.|;
   set G = |.g.|;
A4:f+g is_integrable_on M by A1,MESFUNC5:114; then
   ex A be Element of S st A = dom(f+g) & f+g is_measurable_on A
     by MESFUNC5:def 17; then
A5:|.f+g.| is_integrable_on M by A4,MESFUNC5:106;   
   for x be Element of X st 
     x in dom |.f+g.| holds (|.f+g.|).x <= (|.f.|+|.g.|).x by AAD; then
   (|.f.|+|.g.|) - |.f+g.| is nonnegative by LMXZ; then
   consider E be Element of S such that
A6: E = dom(|.f.|+|.g.|) /\ dom |.f+g.| & 
    Integral(M,(|.f+g.|)|E) <= Integral(M,(|.f.|+|.g.|)|E) by A3,A5,LMXY;
   take E;
   F|E is_integrable_on M & G|E is_integrable_on M by A2,MESFUNC5:103; then
   consider E1 be Element of S such that
A7: E1 = dom (F|E) /\ dom (G|E) &
    Integral(M,F|E+G|E) = Integral(M,(F|E)|E1) + Integral(M,(G|E)|E1)
     by MESFUNC5:115;
B1:dom (|.f.|+|.g.|) = dom f /\ dom g by AAB;
   dom |.f+g.| = dom(f+g) by MESFUNC1:def 10
    .= (dom f /\ dom g)
         \((f"{-infty} /\ g"{+infty}) \/ (f"{+infty} /\ g"{-infty}))
           by MESFUNC1:def 3; then
B2:dom |.f+g.| c= dom f /\ dom g by XBOOLE_1:36; then
A8:E = dom |.f+g.| by A6,B1,XBOOLE_1:28;
   dom(F|E) = dom F /\ E & dom(G|E) = dom G /\ E by FUNCT_1:68; then
   dom(F|E) = dom f /\ E & dom(G|E) = dom g /\ E by MESFUNC1:def 10; then
   E1 = (dom f /\ E /\ E) /\ dom g by A7,XBOOLE_1:16; then
   E1 = (dom f /\ (E /\ E)) /\ dom g by XBOOLE_1:16; then
   E1 = dom f /\ dom g /\ E by XBOOLE_1:16; then
   E1 = E by B2,A6,B1,XBOOLE_1:28; then
   (F|E)|E1 = F|E & (G|E)|E1 = G|E by FUNCT_1:82;
   hence thesis by A6,A7,A8,AAB1,MESFUNC1:def 10;
end;

theorem AF:
max+(chi(A,X)) = chi(A,X)
proof
A1:dom max+(chi(A,X)) = dom chi(A,X) by MESFUNC2:def 2;
   now let x be Element of X;
    assume A2:x in dom max+(chi(A,X)); then
A3: (max+(chi(A,X))).x = max((chi(A,X)).x,0.) by MESFUNC2:def 2;
A4: (chi(A,X)).x in rng chi(A,X) by A1,A2,FUNCT_1:12;
    rng chi(A,X) c= {0,1} by FUNCT_3:48; then
    (chi(A,X)).x = 0 or (chi(A,X)).x = 1 by A4,TARSKI:def 2;
    hence (max+(chi(A,X))).x = (chi(A,X)).x by A3,XXREAL_0:def 9;
   end;
   hence thesis by A1,PARTFUN1:34;
end;

theorem AG:
M.E < +infty implies
 chi(E,X) is_integrable_on M & Integral(M,chi(E,X)) = M.E &
 Integral(M,(chi(E,X))|E) = M.E
proof
   assume A1: M.E < +infty;  
   reconsider XX = X as Element of S by MEASURE1:21;   
A2:XX = dom chi(E,X) & chi(E,X) is_measurable_on XX
      by FUNCT_3:def 3,MESFUNC2:32; then
A3:XX = dom(max+(chi(E,X))) & max+(chi(E,X)) is_measurable_on XX by AF;   
   set F = XX \ E;   
   for x be set st x in dom max+(chi(E,X)) holds 0. <= (max+(chi(E,X))).x
      by MESFUNC2:14; then
P1:max+(chi(E,X)) is nonnegative by SUPINF_2:71; then
P2:(max+(chi(E,X)))|E is nonnegative by MESFUNC5:21;   
   E misses F by XBOOLE_1:79; then
X1:integral+(M,(max+ chi(E,X))|(E\/F))
    = integral+(M,(max+ chi(E,X))|E) + integral+(M,(max+ chi(E,X))|F)
       by A3,P1,MESFUNC5:87;
A6:XX /\ F = F & XX /\ E = E by XBOOLE_1:28; then
A5:dom((max+(chi(E,X)))|F) = F &
   dom((max+(chi(E,X)))|E) = E by A3,FUNCT_1:68;
   max+(chi(E,X)) is_measurable_on E &
   max+(chi(E,X)) is_measurable_on F by A3,MESFUNC1:34; then
A8:(max+(chi(E,X)))|E is_measurable_on E & 
   (max+(chi(E,X)))|F is_measurable_on F by A3,A6,MESFUNC5:48;
   E \/ F = XX by A6,XBOOLE_1:51; then
C1:(max+ chi(E,X))|(E\/F) = max+ chi(E,X) by A3,RELAT_1:98;
   now let x be Element of X;
    assume B1:x in dom((max+(chi(E,X)))|F); then
    chi(E,X).x = 0 by A5,FUNCT_3:43; then
    (max+(chi(E,X))).x = 0 by AF;
    hence ((max+(chi(E,X)))|F).x = 0 by B1,FUNCT_1:70;
   end; then
   integral+(M,(max+ chi(E,X))|F) = 0 by A5,A8,MESFUNC5:93; then   
X2:integral+(M,max+ chi(E,X)) = integral+(M,(max+ chi(E,X))|E)
     by X1,C1,SUPINF_2:18;    
P3:now let x be set;
    assume B1:x in dom((max+(chi(E,X)))|E); then
    chi(E,X).x = 1 by A5,FUNCT_3:def 3; then
    (max+(chi(E,X))).x = 1 by AF;
    hence ((max+(chi(E,X)))|E).x = 1 by B1,FUNCT_1:70;
   end; then
   (max+(chi(E,X)))|E is_simple_func_in S by A5,MESFUNC6:2; then
   integral+(M,max+ chi(E,X)) = integral'(M,(max+ chi(E,X))|E) 
     by X2,P2,MESFUNC5:83; then
P8:integral+(M,max+ chi(E,X)) = R_EAL 1 * M.(dom((max+(chi(E,X)))|E))
     by A5,P3,MESFUNC5:110; then
P4:integral+(M,max+ chi(E,X)) < +infty by A1,A5,EXTREAL2:4;
P5:XX = dom(max- chi(E,X)) & max- chi(E,X) is_measurable_on XX 
     by A2,MESFUNC2:def 3,28;
   now let x be Element of X;
    assume B1:x in dom(max- chi(E,X));
L:  now assume x in E; then
L2:  chi(E,X).x = 1 by FUNCT_3:def 3; then
     ex c be Real st c = chi(E,X).x & -(chi(E,X).x) = -c by SUPINF_2:def 3;
     hence max(-(chi(E,X).x),0.) = 0. by L2,XXREAL_0:def 9;
    end;
    now assume not x in E; then
     chi(E,X).x = 0. by FUNCT_3:def 3;
     hence max(-(chi(E,X).x),0.) = 0. by EXTREAL1:24;
    end;
    hence (max-(chi(E,X))).x = 0 by L,B1,MESFUNC2:def 3;
   end; then
QQ:integral+(M,max- chi(E,X)) = 0 by P5,MESFUNC5:93;
   hence chi(E,X) is_integrable_on M by A2,P4,MESFUNC5:def 17;
   Integral(M,chi(E,X)) = R_EAL 1 * M.E by P8,A5,QQ,MEASURE6:21;
   hence Integral(M,chi(E,X)) = M.E by EXTREAL2:4;
Q1:E = dom((chi(E,X))|E) & (chi(E,X))|E is_measurable_on E by A5,A8,AF;
Q2:(chi(E,X))|E is nonnegative by P2,AF;
Q3:(chi(E,X))|E = (max+ chi(E,X))|E by AF;
   Integral(M,(chi(E,X))|E) =integral+(M,(chi(E,X))|E)
     by Q1,Q2,MESFUNC5:94;
   hence Integral(M,(chi(E,X))|E) = M.E by X2,Q3,P8,A5,EXTREAL2:4;
end;

theorem AH:
M.(E1/\E2) < +infty implies Integral(M,(chi(E1,X))|E2) = M.(E1/\E2)
proof
   assume A1: M.(E1/\E2) < +infty;
   reconsider XX = X as Element of S by MEASURE1:21;
A2:XX = dom chi(E1,X) & chi(E1,X) is_measurable_on XX 
      by FUNCT_3:def 3,MESFUNC2:32;
   now let x be set;
    assume x in dom chi(E1,X); then
A4: (chi(E1,X)).x in rng chi(E1,X) by FUNCT_1:12;
    rng chi(E1,X) c= {0,1} by FUNCT_3:48;
    hence 0. <= (chi(E1,X)).x by A4,TARSKI:def 2;
   end; then
P1:chi(E1,X) is nonnegative by SUPINF_2:71;   
   E1 /\ E2 misses E2 \ E1 & E2 = (E1 /\ E2) \/ (E2 \ E1) 
     by XBOOLE_1:51,89; then
P6:Integral(M,(chi(E1,X))|E2) 
    = Integral(M,(chi(E1,X))|(E1/\E2)) + Integral(M,(chi(E1,X))|(E2\E1))
         by A2,P1,MESFUNC5:97;   
B1:dom((chi(E1,X))|(E1/\E2)) = dom(chi(E1,X)) /\ (E1/\E2) by FUNCT_1:68
    .= X /\ (E1/\E2) by FUNCT_3:def 3;
B2:dom(chi(E1/\E2,X)|(E1/\E2)) = dom(chi(E1/\E2,X)) /\ (E1/\E2) by FUNCT_1:68
    .= X /\ (E1/\E2) by FUNCT_3:def 3;
   now let x be Element of X;
    assume B3:x in dom((chi(E1,X))|(E1/\E2)); then
B4: x in X & x in E1 /\ E2 by B1,XBOOLE_0:def 3; then
B5: x in E1 by XBOOLE_0:def 3;
B6: ((chi(E1,X))|(E1/\E2)).x = (chi(E1,X)).x by B3,FUNCT_1:70
     .= 1 by B5,FUNCT_3:def 3;
    (chi(E1/\E2,X)|(E1/\E2)).x = (chi(E1/\E2,X)).x by B1,B2,B3,FUNCT_1:70;
    hence ((chi(E1,X))|(E1/\E2)).x = (chi(E1/\E2,X)|(E1/\E2)).x
       by B4,B6,FUNCT_3:def 3;
   end; then
   (chi(E1,X))|(E1/\E2) = chi(E1/\E2,X)|(E1/\E2) by B1,B2,PARTFUN1:34; then
P3:Integral(M,(chi(E1,X))|(E1/\E2)) = M.(E1/\E2) by A1,AG;   
   set F = E2\E1;
C3:F = dom((chi(E1,X))|(E2\E1)) by A2,RELAT_1:91; then
C2:F = dom(chi(E1,X)) /\ F by FUNCT_1:68;
   chi(E1,X) is_measurable_on F by MESFUNC2:32; then
C4:(chi(E1,X))|(E2\E1) is_measurable_on F by C2,MESFUNC5:48;
   now let x be Element of X;
    assume C5:x in dom ((chi(E1,X))|(E2\E1));
    E2 \ E1 c= X \ E1 by XBOOLE_1:33; then
    (chi(E1,X)).x = 0 by C5,C3,FUNCT_3:43;
    hence 0= ((chi(E1,X))|(E2\E1)).x by C5,FUNCT_1:70;
   end; then
P5:integral+(M,(chi(E1,X))|(E2\E1)) = 0 by C3,C4,MESFUNC5:93;
   (chi(E1,X))|(E2\E1) is nonnegative by P1,MESFUNC5:21; then
   Integral(M,(chi(E1,X))|(E2\E1)) = 0. by C3,C4,P5,MESFUNC5:94;
   hence thesis by P3,P6,SUPINF_2:18;
end;

theorem
f is_integrable_on M & E c= dom f & M.E < +infty &
(for x be Element of X st x in E holds a <= f.x & f.x <= b)
implies 
 (R_EAL a)*M.E <= Integral(M,f|E) & Integral(M,f|E) <= (R_EAL b)*M.E
proof
   reconsider a1=a,b1=b as Element of REAL by XREAL_0:def 1;
   assume that
A1: f is_integrable_on M and
A2: E c= dom f and
B1: M.E < +infty and
A3: for x be Element of X st x in E holds a <= f.x & f.x <= b;
   set C = chi(E,X);   
P1:f|E is_integrable_on M by A1,MESFUNC5:103;   
   chi(E,X) is_integrable_on M by B1,AG; then
P3:C|E is_integrable_on M by MESFUNC5:103; then   
P2:a1(#)(C|E) is_integrable_on M &
   b1(#)(C|E) is_integrable_on M by MESFUNC5:116;   
   for x be Element of X st x in dom(a1(#)(C|E)) holds
     (a1(#)(C|E)).x <= (f|E).x
   proof
    let x be Element of X;
    assume M1: x in dom(a1(#)(C|E)); then
M2: x in dom(C|E) by MESFUNC1:def 6; then
    x in dom C /\ E by FUNCT_1:68; then
M3: x in dom chi(E,X) & x in E by XBOOLE_0:def 3; then
    a <= f.x by A3; then
M4: a <= (f|E).x by M3,FUNCT_1:72;
    (a1(#)(C|E)).x = R_EAL a * (C|E).x by M1,MESFUNC1:def 6
     .= R_EAL a * C.x by M2,FUNCT_1:70
     .= R_EAL a * 1. by M3,FUNCT_3:def 3;
    hence thesis by M4,EXTREAL2:4;
   end; then
   (f|E) - (a1(#)(C|E)) is nonnegative by LMXZ; then
   consider E1 be Element of S such that
A4: E1 = dom(f|E) /\ dom(a1(#)(C|E)) &
    Integral(M,(a1(#)(C|E))|E1) <= Integral(M,(f|E)|E1) by P1,P2,LMXY;   
   dom(f|E) = dom f /\ E by FUNCT_1:68; then
A6:dom(f|E) = E by A2,XBOOLE_1:28;   
   dom(a1(#)(C|E)) = dom(C|E) & dom(b1(#)(C|E)) = dom(C|E) 
      by MESFUNC1:def 6;then
   dom(a1(#)(C|E)) = dom C /\ E &
   dom(b1(#)(C|E)) = dom C /\ E by FUNCT_1:68; then
   dom(a1(#)(C|E)) = X /\ E & dom(b1(#)(C|E)) = X /\ E by FUNCT_3:def 3; then
A8:dom(a1(#)(C|E)) = E & dom(b1(#)(C|E)) = E by XBOOLE_1:28;
   for x be Element of X st x in dom(f|E) holds (f|E).x <= (b1(#)(C|E)).x
   proof
    let x be Element of X;
    assume M0:x in dom(f|E); then
M2: x in dom(C|E) by A6,A8,MESFUNC1:def 6; then
    x in dom C /\ E by FUNCT_1:68; then
M3: x in dom chi(E,X) & x in E by XBOOLE_0:def 3; then
    f.x <= b by A3; then
M4: (f|E).x <= b by M3,FUNCT_1:72;
    (b1(#)(C|E)).x = R_EAL b * (C|E).x by M0,A6,A8,MESFUNC1:def 6
     .= R_EAL b * C.x by M2,FUNCT_1:70
     .= R_EAL b * 1. by M3,FUNCT_3:def 3;
    hence thesis by M4,EXTREAL2:4;
   end; then
   (b1(#)(C|E)) - (f|E) is nonnegative by LMXZ; then
   consider E2 be Element of S such that
A5: E2 = dom(f|E) /\ dom(b1(#)(C|E)) &
    Integral(M,(f|E)|E2) <= Integral(M,(b1(#)(C|E))|E2) by P1,P2,LMXY;
A7:(a1(#)(C|E))|E1 = a1(#)(C|E) & (b1(#)(C|E))|E2 = b1(#)(C|E) &
   (f|E)|E1 = f|E & (f|E)|E2 = f|E by A4,A5,A6,A8,RELAT_1:98;
   E = E/\E; then
   Integral(M,C|E) = M.E by B1,AH;
   hence thesis by A4,A5,A7,P3,MESFUNC5:116;
end;

