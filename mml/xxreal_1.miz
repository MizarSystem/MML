:: Basic Properties of Extended Real Numbers
::  by Andrzej Trybulec, Yatsuka Nakamura, Artur Korni{\l}owicz and
::    Adam Grabowski
::
:: Received January 22, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies SUPINF_1, ARYTM, BOOLE, RCOMP_1, MEMBERED, SQUARE_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, MEMBERED, XXREAL_0,
      XREAL_0;
 constructors NUMBERS, XXREAL_0, XREAL_0, MEMBERED;
 registrations XBOOLE_0, SUBSET_1, NUMBERS, XXREAL_0, XREAL_0, MEMBERED;
 requirements SUBSET, BOOLE;
 definitions MEMBERED;
 theorems XBOOLE_0, XXREAL_0, XBOOLE_1, TARSKI, AXIOMS, SUBSET_1, XREAL_0,
      XREAL_1, MEMBERED, ZFMISC_1;

begin

 reserve x,y,A,B for set,
         p,q,r,s,t,u for ext-real number,
         a,b for Element of ExtREAL,
         X,Y for ext-real-membered set;

scheme Conti { P,Q[set] }:
 ex s st (for r st P[r] holds r <= s) & for r st Q[r] holds s <= r
provided
A1: for r,s st P[r] & Q[s] holds r <= s
proof
  set A = { a : P[a]},  B = { a : Q[a]};
  reconsider X = A /\ REAL,  Y = B /\ REAL as Subset of REAL by XBOOLE_1:17;
 per cases;
 suppose
A2: X = {};
  per cases;
  suppose
A3: +infty in A;
   take +infty;
   thus for r st P[r] holds r <= +infty by XXREAL_0:3;
     ex a st a = +infty & P[a] by A3;
   hence thesis by A1;
  end;
  suppose
A4:  not +infty in A;
   take -infty;
   thus for r st P[r] holds r <= -infty
    proof let r such that
A5:    P[r];
       r in ExtREAL by XXREAL_0:def 1;
      then A6:     r in A by A5;
     per cases by A6,A4, XXREAL_0:14;
     suppose r = -infty;
     hence thesis;
     end;
     suppose r in REAL;
     hence thesis by A2,A6,XBOOLE_0:def 3;
     end;
    end;
   thus thesis by XXREAL_0:5;
  end;
 end;
 suppose
A7: Y = {};
  per cases;
  suppose
A8: -infty in B;
   take -infty;
   thus for r st P[r] holds r <= -infty
    proof ex a st a = -infty & Q[a] by A8;
     hence thesis by A1;
    end;
   thus thesis by XXREAL_0:5;
  end;
  suppose
A9:  not -infty in B;
   take +infty;
   thus for r st P[r] holds r <= +infty by XXREAL_0:3;
   let r such that
A10:  Q[r];
     r in ExtREAL by XXREAL_0:def 1;
    then A11:  r in B by A10;
   per cases by A11,A9,XXREAL_0:14;
    suppose r = +infty;
     hence thesis;
    end;
    suppose r in REAL;
     hence thesis by A7,A11,XBOOLE_0:def 3;
    end;
  end;
 end;
 suppose that
A12: X <> {} and
A13: Y <> {};
    for x,y being real number st x in X & y in Y holds x <= y
    proof let x,y be real number;
     assume x in X;
      then x in A by XBOOLE_0:def 3;
      then consider a such that
A14:    a = x and
A15:    P[a];
     assume y in Y;
      then y in B by XBOOLE_0:def 3;
      then ex a st a = y & Q[a];
     hence x <= y by A14,A15,A1;
    end;
   then consider s being real number such that
A16:   for x,y being real number st x in X & y in Y holds x <= s & s <= y
                  by AXIOMS:26;
   reconsider s as ext-real number;
  take s;
  thus for r st P[r] holds r <= s
   proof let r;
     consider x being Element of REAL such that
A17:    x in Y by A13,SUBSET_1:10;
      x in B by A17,XBOOLE_0:def 3;
     then A18:    ex a st x = a & Q[a];
    assume
A19:  P[r];
     then A20:   r <= x by A18,A1;
    per cases by A20,XXREAL_0:13;
    suppose
A21:   r in REAL;
     then reconsider r as real number by XREAL_0:def 1;
      r is Element of ExtREAL by XXREAL_0:def 1;
     then r in A by A19;
     then r in X by A21,XBOOLE_0:def 3;
    hence thesis by A17,A16;
    end;
    suppose r = -infty;
    hence r <= s by XXREAL_0:5;
    end;
   end;
  let r;
   consider x being Element of REAL such that
A22:  x in X by A12,SUBSET_1:10;
    x in A by A22,XBOOLE_0:def 3;
   then A23:  ex a st x = a & P[a];
  assume
A24: Q[r];
   then A25: x <= r by A23,A1;
  per cases by A25,XXREAL_0:10;
  suppose
A26: r in REAL;
   then reconsider r as real number by XREAL_0:def 1;
    r is Element of ExtREAL by XXREAL_0:def 1;
   then r in B by A24;
   then r in Y by A26,XBOOLE_0:def 3;
  hence thesis by A22,A16;
  end;
  suppose r = +infty;
  hence s <= r by XXREAL_0:3;
  end;
 end;
end;

begin :: Intervals

definition let r,s;
 func [.r,s.] equals
  { a : r <= a & a <= s };
 correctness;
 func [.r,s.[ equals
  { a : r <= a & a < s };
 correctness;
 func ].r,s.] equals
  { a : r < a & a <= s };
 correctness;
 func ].r,s.[ equals
  { a : r < a & a < s };
 correctness;
end;

theorem Th1: :: RCOMP_1:48
 t in [.r,s.] iff r <= t & t <= s
 proof
  hereby assume t in [.r,s.];
    then ex a st a = t & r <= a & a <= s;
   hence r <= t & t <= s;
  end;
    t is Element of ExtREAL by XXREAL_0:def 1;
  hence thesis;
 end;

theorem Th2: :: RCOMP_2:4
 t in ].r,s.] iff r < t & t <= s
 proof
  hereby assume t in ].r,s.];
    then ex a st a = t & r < a & a <= s;
   hence r < t & t <= s;
  end;
    t is Element of ExtREAL by XXREAL_0:def 1;
  hence thesis;
 end;

theorem Th3: :: RCOMP_2:3
 t in [.r,s.[ iff r <= t & t < s
 proof
  hereby assume t in [.r,s.[;
    then ex a st a = t & r <= a & a < s;
   hence r <= t & t < s;
  end;
    t is Element of ExtREAL by XXREAL_0:def 1;
  hence thesis;
 end;

theorem Th4:  :: RCOMP_1:47
 t in ].r,s.[ iff r < t & t < s
 proof
  hereby assume t in ].r,s.[;
    then ex a st a = t & r < a & a < s;
   hence r < t & t < s;
  end;
    t is Element of ExtREAL by XXREAL_0:def 1;
  hence thesis;
 end;

registration let r,s; :: MEASURE5:11
 cluster [.r,s.] -> ext-real-membered;
 coherence
  proof let x;
   assume x in [.r,s.];
    then ex a st x = a & r <= a & a <= s;
   hence thesis;
  end;
 cluster [.r,s.[ -> ext-real-membered;
 coherence
  proof let x;
   assume x in [.r,s.[;
    then ex a st x = a & r <= a & a < s;
   hence thesis;
  end;
 cluster ].r,s.] -> ext-real-membered;
 coherence
  proof let x;
   assume x in ].r,s.];
    then ex a st x = a & r < a & a <= s;
   hence thesis;
  end;
 cluster ].r,s.[ -> ext-real-membered;
 coherence
  proof let x;
   assume x in ].r,s.[;
    then ex a st x = a & r < a & a < s;
   hence thesis;
  end;
end;

theorem Th5: :: BORSUK_4:10
 x in [.p,q.] implies x in ].p,q.[ or x = p or x = q
  proof
   assume
A1:   x in [. p, q .];
     then reconsider s = x as ext-real number by MEMBERED:def 2;
      p <= s & s <= q by A1,Th1;
     then (p = s or p < s) & (s = q or s < q) by XXREAL_0:1;
    hence x in ].p,q.[ or x = p or x = q by Th4;
  end;

theorem Th6:
 x in [.p,q.] implies x in ].p,q.] or x = p
  proof
   assume
A1:   x in [.p,q.];
     then reconsider s = x as ext-real number by MEMBERED:def 2;
A2:   p <= s & s <= q by A1,Th1;
     then p = s or p < s by XXREAL_0:1;
    hence x in ].p,q.] or x = p by Th2,A2;
  end;

theorem Th7:
 x in [.p,q.] implies x in [.p,q.[ or x = q
  proof
   assume
A1:   x in [.p,q.];
     then reconsider s = x as ext-real number by MEMBERED:def 2;
A2:   p <= s & s <= q by A1,Th1;
     then q = s or s < q by XXREAL_0:1;
    hence x in [.p,q.[ or x = q by Th3,A2;
  end;

theorem Th8:
 x in [.p,q.[ implies x in ].p,q.[ or x = p
  proof
   assume
A1:   x in [.p,q.[;
     then reconsider s = x as ext-real number by MEMBERED:def 2;
A2:   p <= s & s < q by A1,Th3;
     then p = s or p < s by XXREAL_0:1;
    hence x in ].p,q.[ or x = p by Th4,A2;
  end;

theorem Th9:
 x in ].p,q.] implies x in ].p,q.[ or x = q
  proof
   assume
A1:   x in ].p,q.];
     then reconsider s = x as ext-real number by MEMBERED:def 2;
A2:   p < s & s <= q by A1,Th2;
     then q = s or s < q by XXREAL_0:1;
    hence x in ].p,q.[ or x = q by Th4,A2;
  end;

theorem
  x in [.p,q.[ implies x in ].p,q.] & x <> q or x = p
  proof
   assume
A1:   x in [.p,q.[;
     then reconsider s = x as ext-real number by MEMBERED:def 2;
A2:   p <= s & s < q by A1,Th3;
     then p = s or p < s by XXREAL_0:1;
    hence x in ].p,q.] & x <> q or x = p by Th2,A2;
  end;

theorem
  x in ].p,q.] implies x in [.p,q.[ & x <> p or x = q
  proof
   assume
A1:   x in ].p,q.];
     then reconsider s = x as ext-real number by MEMBERED:def 2;
A2:   p < s & s <= q by A1,Th2;
     then q = s or s < q by XXREAL_0:1;
    hence x in [.p,q.[ & x <> p or x = q by Th3,A2;
  end;

theorem Th12:
 x in ].p,q.] implies x in [.p,q.] & x <> p
  proof
   assume
A1:   x in ].p,q.];
     then reconsider s = x as ext-real number by MEMBERED:def 2;
      p < s & s <= q by A1,Th2;
   hence x in [.p,q.] & x <> p by Th1;
  end;

theorem Th13:
 x in [.p,q.[ implies x in [.p,q.] & x <> q
  proof
   assume
A1:   x in [.p,q.[;
     then reconsider s = x as ext-real number by MEMBERED:def 2;
      p <= s & s < q by A1,Th3;
   hence x in [.p,q.] & x <> q by Th1;
  end;

theorem Th14:
 x in ].p,q.[ implies x in [.p,q.[ & x <> p
  proof
   assume
A1:   x in ].p,q.[;
     then reconsider s = x as ext-real number by MEMBERED:def 2;
      p < s & s < q by A1,Th4;
   hence x in [.p,q.[ & x <> p by Th3;
  end;

theorem Th15:
 x in ].p,q.[ implies x in ].p,q.] & x <> q
  proof
   assume
A1:   x in ].p,q.[;
     then reconsider s = x as ext-real number by MEMBERED:def 2;
      p < s & s < q by A1,Th4;
   hence x in ].p,q.] & x <> q by Th2;
  end;

theorem Th16:
 x in ].p,q.[ implies x in [.p,q.] & x <> p & x <> q
 proof
  assume x in ].p,q.[;
   then x in ].p,q.] & x <> q by Th15;
  hence x in [.p,q.] & x <> p & x <> q by Th12;
 end;

theorem Th17: :: RCOMP_1:14 :: MEASURE5:14
 [.r,r.] = {r}
 proof let s;
  thus s in [.r,r.] implies s in {r}
   proof
    assume s in [.r,r.];
     then ex a st s = a & r <= a & a <= r;
     then s = r by XXREAL_0:1;
    hence thesis by TARSKI:def 1;
   end;
  assume s in {r};
   then A1:  s = r by TARSKI:def 1;
   reconsider s as Element of ExtREAL by XXREAL_0:def 1;
    s <= s;
  hence thesis by A1;
 end;

theorem Th18: :: MEASURE5:13  :: RCOMP_2:7
 [.r,r.[ = {}
  proof let p;
     not ex p st p in [.r,r.[
     proof given p such that
A1:    p in [.r,r.[;
       ex a st p = a & r <= a & a < r by A1;
     hence thesis;
     end;
   hence thesis;
  end;

theorem Th19: :: MEASURE5:13 :: RCOMP_2:7
 ].r,r.] = {}
  proof let p;
   thus p in ].r,r.] implies p in {}
    proof
     assume p in ].r,r.];
      then ex a st p = a & r < a & a <= r;
     hence thesis;
    end;
   thus thesis;
  end;

theorem Th20: :: MEASURE5:13
 ].r,r.[ = {}
  proof let p;
   thus p in ].r,r.[ implies p in {}
    proof
     assume p in ].r,r.[;
      then ex a st p = a & r < a & a < r;
     hence thesis;
    end;
   thus thesis;
  end;

registration let r;
 cluster [.r,r.] -> non empty;
 coherence
  proof
     [.r,r.] = {r} by Th17;
   hence thesis;
  end;
 cluster [.r,r.[ -> empty;
 coherence by Th18;
 cluster ].r,r.] -> empty;
 coherence by Th19;
 cluster ].r,r.[ -> empty;
 coherence by Th20;
end;

theorem Th21: :: RCOMP_2:17
 ].p,q.[ c= ].p,q.]
proof
 let s;
 assume s in ].p,q.[;
  then p < s & s < q by Th4;
 hence s in ].p,q.] by Th2;
end;

theorem Th22: :: RCOMP_2:17
 ].p,q.[ c= [.p,q.[
proof
 let s;
 assume s in ].p,q.[;
  then p < s & s < q by Th4;
 hence s in [.p,q.[ by Th3;
end;

theorem Th23: :: RCOMP_2:17
 ].p,q.] c= [.p,q.]
proof
 let s;
 assume s in ].p,q.];
  then p < s & s <= q by Th2;
 hence s in [.p,q.] by Th1;
end;

theorem Th24: :: RCOMP_2:17
 [.p,q.[ c= [.p,q.]
proof
 let s;
 assume s in [.p,q.[;
  then p <= s & s < q by Th3;
 hence s in [.p,q.] by Th1;
end;

theorem Th25: :: RCOMP_1:15 :: RCOMP_2:17
 ].p,q.[ c= [.p,q.]
proof
   ].p,q.[ c= [.p,q.[ & [.p,q.[ c= [.p,q.] by Th22,Th24;
 hence thesis by XBOOLE_1:1;
end;

theorem :: MEASURE5:12,15 :: RCOMP_2:10
  p <= q implies ].q,p.] = {}
proof assume
A1: p <= q;
 assume ].q,p.] <> {};
  then consider r such that
A2: r in ].q,p.] by MEMBERED:8;
   q < r & r <= p by A2,Th2;
 hence contradiction by A1,XXREAL_0:2;
end;

theorem Th27: :: MEASURE5:12,15 :: RCOMP_2:9
 p <= q implies [.q,p.[ = {}
proof assume
A1: p <= q;
 assume [.q,p.[ <> {};
  then consider r such that
A2: r in [.q,p.[ by MEMBERED:8;
   q <= r & r < p by A2,Th3;
 hence contradiction by A1,XXREAL_0:2;
end;

theorem Th28: :: RCOMP_1:12 :: MEASURE5:12,15
 p <= q implies ].q,p.[ = {}
proof assume p <= q;
  then A1: [.q,p.[ = {} by Th27;
    ].q,p.[ c= [.q,p.[ by Th22;
 hence ].q,p.[ = {} by A1,XBOOLE_1:3;
end;

theorem Th29: :: RCOMP_1:13 :: MEASURE5:12
 p < q implies [.q,p.] = {}
proof assume
A1: p < q;
 assume [.q,p.] <> {};
  then consider r such that
A2: r in [.q,p.] by MEMBERED:8;
   q <= r & r <= p by A2,Th1;
 hence contradiction by A1,XXREAL_0:2;
end;

theorem
  r <= s implies [.r,s.] is non empty by Th1;

theorem
  p < q implies [.p,q.[ is non empty by Th3;

theorem
  p < q implies ].p,q.] is non empty by Th2;

theorem
  p < q implies ].p,q.[ is non empty
 proof assume p < q;
   then ex s st p < s & s < q by XREAL_1:229;
  hence thesis by Th4;
 end;

theorem Th34: :: RCOMP_1:49 :: TREAL_1:1
 p <= r & s <= q implies [.r,s.] c= [.p,q.]
 proof assume
A1: p <= r & s <= q;
  let t;
  assume t in [.r,s.];
   then r <= t & t <= s by Th1;
   then p <= t & t <= q by A1,XXREAL_0:2;
  hence t in [.p,q.] by Th1;
 end;

theorem :: TOPREALA:11
  p <= r & s <= q implies [.r,s.[ c= [.p,q.]
 proof
A1: [.r,s.[ c= [.r,s.] by Th24;
  assume p <= r & s <= q;
   then [.r,s.] c= [.p,q.] by Th34;
  hence thesis by A1,XBOOLE_1:1;
 end;

theorem :: TOPREALA:15
  p <= r & s <= q implies ].r,s.] c= [.p,q.]
 proof
A1: ].r,s.] c= [.r,s.] by Th23;
  assume p <= r & s <= q;
   then [.r,s.] c= [.p,q.] by Th34;
  hence thesis by A1,XBOOLE_1:1;
 end;

theorem :: TOPREALA:19
  p <= r & s <= q implies ].r,s.[ c= [.p,q.]
 proof
A1: ].r,s.[ c= [.r,s.] by Th25;
  assume p <= r & s <= q;
   then [.r,s.] c= [.p,q.] by Th34;
  hence thesis by A1,XBOOLE_1:1;
 end;

theorem :: TOPREALA:12
  p <= r & s <= q implies [.r,s.[ c= [.p,q.[
 proof assume
A1: p <= r & s <= q;
  let t;
  assume t in [.r,s.[;
   then r <= t & t < s by Th3;
   then p <= t & t < q by A1,XXREAL_0:2;
  hence t in [.p,q.[ by Th3;
 end;

theorem Th39: :: TOPREALA:9
 p < r & s <= q implies [.r,s.] c= ].p,q.]
 proof assume
A1: p < r & s <= q;
  let t;
  assume t in [.r,s.];
   then r <= t & t <= s by Th1;
   then p <t & t <= q by A1,XXREAL_0:2;
  hence t in ].p,q.] by Th2;
 end;

theorem :: TOPREALA:13
  p < r & s <= q implies [.r,s.[ c= ].p,q.]
 proof
A1: [.r,s.[ c= [.r,s.] by Th24;
  assume p < r & s <= q;
   then [.r,s.] c= ].p,q.] by Th39;
  hence thesis by A1,XBOOLE_1:1;
 end;

theorem :: TOPREALA:21
   p <= r & s <= q implies ].r,s.[ c= ].p,q.]
 proof assume
A1: p <= r & s <= q;
  let t;
  assume t in ].r,s.[;
   then r < t & t < s by Th4;
   then p < t & t < q by A1,XXREAL_0:2;
  hence t in ].p,q.] by Th2;
 end;

theorem :: TOPREALA:17
  p <= r & s <= q implies ].r,s.] c= ].p,q.]
 proof assume
A1: p <= r & s <= q;
  let t;
  assume t in ].r,s.];
   then r < t & t <= s by Th2;
   then p < t & t <= q by A1,XXREAL_0:2;
  hence t in ].p,q.] by Th2;
 end;

theorem Th43: :: TOPREALA:8
  p <= r & s < q implies [.r,s.] c= [.p,q.[
 proof assume
A1: p <= r & s < q;
  let t;
  assume t in [.r,s.];
   then r <= t & t <= s by Th1;
   then p <= t & t < q by A1,XXREAL_0:2;
  hence t in [.p,q.[ by Th3;
 end;

theorem :: TOPREALA:16
   p <= r & s < q implies ].r,s.] c= [.p,q.[
 proof
A1: ].r,s.] c= [.r,s.] by Th23;
  assume p <= r & s < q;
   then [.r,s.] c= [.p,q.[ by Th43;
  hence thesis by A1,XBOOLE_1:1;
 end;

theorem :: TOPREALA:20
   p <= r & s <= q implies ].r,s.[ c= [.p,q.[
 proof assume
A1: p <= r & s <= q;
  let t;
  assume t in ].r,s.[;
   then r < t & t < s by Th4;
   then p <= t & t < q by A1,XXREAL_0:2;
  hence t in [.p,q.[ by Th3;
 end;

theorem :: RCOMP_1:52 :: JGRAPH_3:9
  p <= r & s <= q implies ].r,s.[ c= ].p,q.[
 proof assume
A1: p <= r & s <= q;
  let t;
  assume t in ].r,s.[;
   then r < t & t < s by Th4;
   then p < t & t < q by A1,XXREAL_0:2;
  hence t in ].p,q.[ by Th4;
 end;

theorem :: TOPREALA:10
   p < r & s < q implies [.r,s.] c= ].p,q.[
 proof assume
A1: p < r & s < q;
  let t;
  assume t in [.r,s.];
   then r <= t & t <= s by Th1;
   then p < t & t < q by A1,XXREAL_0:2;
  hence t in ].p,q.[ by Th4;
 end;

theorem :: TOPREALA:14
   p < r & s <= q implies [.r,s.[ c= ].p,q.[
 proof assume
A1: p < r & s <= q;
  let t;
  assume t in [.r,s.[;
   then r <= t & t < s by Th3;
   then p < t & t < q by A1,XXREAL_0:2;
  hence t in ].p,q.[ by Th4;
 end;

theorem :: TOPREALA:18
   p <= r & s < q implies ].r,s.] c= ].p,q.[
 proof assume
A1: p <= r & s < q;
  let t;
  assume t in ].r,s.];
   then r < t & t <= s by Th2;
   then p < t & t < q by A1,XXREAL_0:2;
  hence t in ].p,q.[ by Th4;
 end;

theorem Th50: :: BORSUK_4:14
 r <= s & [.r,s.] c= [.p,q.] implies p <= r & s <= q
 proof assume r <= s;
   then r in [.r,s.] & s in [.r,s.] by Th1;
  hence thesis by Th1;
 end;

theorem Th51: :: BORSUK_4:15
 r < s & ].r,s.[ c= [.p,q.] implies p <= r & s <= q
 proof assume that
A1: r < s and
A2: ].r,s.[ c= [.p,q.];
    now let t;
    assume r < t & t < s;
     then t in ].r,s.[ by Th4;
    hence p <= t by Th1,A2;
   end;
  hence p <= r by A1,XREAL_1:230;
    now let t;
    assume r < t & t < s;
     then t in ].r,s.[ by Th4;
    hence t <= q by Th1,A2;
   end;
  hence s <= q by A1,XREAL_1:231;
 end;

theorem Th52: :: RCOMP_3:12
 r < s & [.r,s.[ c= [.p,q.] implies p <= r & s <= q
 proof assume that
A1: r < s and
A2: [.r,s.[ c= [.p,q.];
    ].r,s.[ c= [.r,s.[ by Th22;
   then ].r,s.[ c= [.p,q.] by A2,XBOOLE_1:1;
  hence thesis by A1,Th51;
 end;

theorem Th53: :: RCOMP_3:14
 r < s & ].r,s.] c= [.p,q.] implies p <= r & s <= q
 proof assume that
A1: r < s and
A2: ].r,s.] c= [.p,q.];
    ].r,s.[ c= ].r,s.] by Th21;
   then ].r,s.[ c= [.p,q.] by A2,XBOOLE_1:1;
  hence thesis by A1,Th51;
 end;

theorem Th54:
 r <= s & [.r,s.] c= [.p,q.[ implies p <= r & s < q
 proof assume that
A1: r <= s and
A2: [.r,s.] c= [.p,q.[;
    [.p,q.[ c= [.p,q.] by Th24;
   then [.r,s.] c= [.p,q.] by A2,XBOOLE_1:1;
  hence p <= r by A1,Th50;
    s in [.r,s.] by A1,Th1;
  hence s < q by A2,Th3;
 end;

theorem Th55: :: RCOMP_3:13
 r < s & [.r,s.[ c= [.p,q.[ implies p <= r & s <= q
 proof assume that
A1: r < s and
A2: [.r,s.[ c= [.p,q.[;
    ].r,s.[ c= [.r,s.[ by Th22;
   then A3:  ].r,s.[ c= [.p,q.[ by A2,XBOOLE_1:1;
    [.p,q.[ c= [.p,q.] by Th24;
   then ].r,s.[ c= [.p,q.] by A3,XBOOLE_1:1;
  hence p <= r & s <= q by A1,Th51;
 end;

theorem Th56:
 r < s & ].r,s.[ c= [.p,q.[ implies p <= r & s <= q
 proof assume that
A1: r < s and
A2: ].r,s.[ c= [.p,q.[;
    [.p,q.[ c= [.p,q.] by Th24;
   then ].r,s.[ c= [.p,q.] by A2,XBOOLE_1:1;
  hence p <= r & s <= q by A1,Th51;
 end;

theorem Th57:
 r < s & ].r,s.] c= [.p,q.[ implies p <= r & s < q
 proof assume that
A1: r < s and
A2: ].r,s.] c= [.p,q.[;
    [.p,q.[ c= [.p,q.] by Th24;
   then ].r,s.] c= [.p,q.] by A2,XBOOLE_1:1;
  hence p <= r by A1,Th53;
    s in ].r,s.] by A1,Th2;
  hence s < q by A2,Th3;
 end;

theorem Th58:
 r <= s & [.r,s.] c= ].p,q.] implies p < r & s <= q
 proof assume that
A1: r <= s and
A2: [.r,s.] c= ].p,q.];
    ].p,q.] c= [.p,q.] by Th23;
   then A3:  [.r,s.] c= [.p,q.] by A2,XBOOLE_1:1;
    r in [.r,s.] by A1,Th1;
  hence p < r by A2,Th2;
  thus s <= q by A3,A1,Th50;
 end;

theorem Th59:
 r < s & ].r,s.[ c= ].p,q.] implies p <= r & s <= q
 proof assume that
A1: r < s and
A2: ].r,s.[ c= ].p,q.];
    ].p,q.] c= [.p,q.] by Th23;
   then ].r,s.[ c= [.p,q.] by A2,XBOOLE_1:1;
  hence p <= r & s <= q by A1,Th51;
 end;

theorem Th60:
 r < s & [.r,s.[ c= ].p,q.] implies p < r & s <= q
 proof assume that
A1: r < s and
A2: [.r,s.[ c= ].p,q.];
    ].p,q.] c= [.p,q.] by Th23;
   then A3:  [.r,s.[ c= [.p,q.] by A2,XBOOLE_1:1;
    r in [.r,s.[ by A1,Th3;
  hence p < r by Th2,A2;
  thus s <= q by A3,A1,Th52;
 end;

theorem Th61: :: RCOMP_3:15
 r < s & ].r,s.] c= ].p,q.] implies p <= r & s <= q
 proof assume that
A1: r < s and
A2: ].r,s.] c= ].p,q.];
    ].r,s.[ c= ].r,s.] by Th21;
   then A3:  ].r,s.[ c= ].p,q.] by A2,XBOOLE_1:1;
    ].p,q.] c= [.p,q.] by Th23;
   then ].r,s.[ c= [.p,q.] by A3,XBOOLE_1:1;
  hence p <= r & s <= q by A1,Th51;
 end;

theorem Th62:
 r <= s & [.r,s.] c= ].p,q.[ implies p < r & s < q
 proof assume that
A1: r <= s and
A2: [.r,s.] c= ].p,q.[;
    r in [.r,s.] by A1,Th1;
  hence p < r by A2,Th4;
    s in [.r,s.] by A1,Th1;
  hence s < q by A2,Th4;
 end;

theorem Th63:
 r < s & ].r,s.[ c= ].p,q.[ implies p <= r & s <= q
 proof assume that
A1: r < s and
A2: ].r,s.[ c= ].p,q.[;
    ].p,q.[ c= [.p,q.] by Th25;
   then ].r,s.[ c= [.p,q.] by A2,XBOOLE_1:1;
  hence thesis by A1,Th51;
 end;

theorem Th64:
 r < s & [.r,s.[ c= ].p,q.[ implies p < r & s <= q
 proof assume that
A1: r < s and
A2: [.r,s.[ c= ].p,q.[;
    ].p,q.[ c= [.p,q.] by Th25;
   then A3:  [.r,s.[ c= [.p,q.] by A2,XBOOLE_1:1;
    r in [.r,s.[ by A1,Th3;
  hence p < r by A2,Th4;
  thus s <= q by A3,A1,Th52;
 end;

theorem Th65:
 r < s & ].r,s.] c= ].p,q.[ implies p <= r & s < q
 proof assume that
A1: r < s and
A2: ].r,s.] c= ].p,q.[;
    ].p,q.[ c= [.p,q.] by Th25;
   then ].r,s.] c= [.p,q.] by A2,XBOOLE_1:1;
  hence p <= r by A1,Th53;
    s in ].r,s.] by A1,Th2;
  hence s < q by A2,Th4;
 end;

theorem :: RCOMP_3:8 :: INTEGRA1:6 ??
   p <= q & [.p,q.] = [.r,s.] implies p = r & q = s
 proof assume that
A1: p <= q and
A2: [.p,q.] = [.r,s.];
A3:  r <= p & q <= s by A1,A2,Th50;
   then r <= q by A1,XXREAL_0:2;
   then r <= s by A3,XXREAL_0:2;
   then p <= r & s <= q by A2,Th50;
  hence p = r & q = s by XXREAL_0:1,A3;
 end;

theorem :: RCOMP_3:9
   p < q & ].p,q.[ = ].r,s.[ implies p = r & q = s
 proof assume that
A1: p < q and
A2: ].p,q.[ = ].r,s.[;
A3:  r <= p & q <= s by A1,A2,Th63;
   then r < q by A1,XXREAL_0:2;
   then r < s by A3,XXREAL_0:2;
   then p <= r & s <= q by A2,Th63;
  hence p = r & q = s by XXREAL_0:1,A3;
 end;

theorem :: RCOMP_3:10
   p < q & ].p,q.] = ].r,s.] implies p = r & q = s
 proof assume that
A1: p < q and
A2: ].p,q.] = ].r,s.];
A3:  r <= p & q <= s by A1,A2,Th61;
   then r < q by A1,XXREAL_0:2;
   then r < s by A3,XXREAL_0:2;
   then p <= r & s <= q by A2,Th61;
  hence p = r & q = s by XXREAL_0:1,A3;
 end;

theorem :: RCOMP_3:11
   p < q & [.p,q.[ = [.r,s.[ implies p = r & q = s
 proof assume that
A1: p < q and
A2: [.p,q.[ = [.r,s.[;
A3:  r <= p & q <= s by A1,A2,Th55;
   then r < q by A1,XXREAL_0:2;
   then r < s by A3,XXREAL_0:2;
   then p <= r & s <= q by A2,Th55;
  hence p = r & q = s by XXREAL_0:1,A3;
 end;

theorem :: BORSUK_4:25 :: MEASURE5:52
   r <= s implies [.r,s.] <> ].p,q.]
 proof assume that
A1: r <= s and
A2: [.r,s.] = ].p,q.];
    now assume r in ].p,q.];
     then A3:    p < r by Th2;
      s <= q by A1,A2,Th58;
     then r <= q by A1,XXREAL_0:2;
     then p < q by A3,XXREAL_0:2;
    hence contradiction by A3,A2,Th53;
   end;
  hence contradiction by A1,Th1,A2;
 end;

theorem  :: MEASURE5:52
   r <= s implies [.r,s.] <> [.p,q.[
 proof assume that
A1: r <= s and
A2: [.r,s.] = [.p,q.[;
    now assume s in [.p,q.[;
     then A3:    s < q by Th3;
      p <= r by A1,A2,Th54;
     then p <= s by A1,XXREAL_0:2;
     then p < q by A3,XXREAL_0:2;
    hence contradiction by A3,A2,Th52;
   end;
  hence contradiction by A1,Th1,A2;
 end;

theorem  :: MEASURE5:52
   r <= s implies [.r,s.] <> ].p,q.[
 proof assume that
A1: r <= s and
A2: [.r,s.] = ].p,q.[;
    now assume s in ].p,q.[;
     then A3:    s < q by Th4;
      p <= r by A1,A2,Th62;
     then p <= s by A1,XXREAL_0:2;
     then p < q by A3,XXREAL_0:2;
    hence contradiction by A3,A2,Th51;
   end;
  hence contradiction by A1,Th1,A2;
 end;

theorem  :: MEASURE5:52
   r < s implies [.r,s.[ <> [.p,q.]
 proof assume that
A1: r < s and
A2: [.r,s.[ = [.p,q.];
A3:  not s in [.r,s.[ by Th3;
    p <= r by A1,A2,Th52;
   then A4: p <= s by A1,XXREAL_0:2;
    s <= q by A2,A1,Th52;
  hence contradiction by A3,A2,A4,Th1;
 end;

theorem :: MEASURE5:52
   r < s implies [.r,s.[ <> ].p,q.]
 proof assume that
A1: r < s and
A2: [.r,s.[ = ].p,q.];
A3:  not s in [.r,s.[ by Th3;
    p <= r by A1,A2,Th60;
   then A4: p < s by A1,XXREAL_0:2;
    s <= q by A2,A1,Th60;
  hence contradiction by A3,A2,A4,Th2;
 end;

theorem  :: MEASURE5:52
   r < s implies [.r,s.[ <> ].p,q.[
 proof assume that
A1: r < s and
A2: [.r,s.[ = ].p,q.[;
    now assume r in ].p,q.[;
     then A3:   p < r by Th4;
      s <= q by A1,A2,Th64;
     then r < q by A1,XXREAL_0:2;
     then p < q by A3,XXREAL_0:2;
    hence contradiction by A3,A2,Th56;
   end;
  hence contradiction by A1,Th3,A2;
 end;

theorem  :: MEASURE5:52
  r < s implies ].r,s.] <> [.p,q.]
 proof assume that
A1: r < s and
A2: ].r,s.] = [.p,q.];
A3:  not r in ].r,s.] by Th2;
A4: p <= r by A1,A2,Th53;
    s <= q by A2,A1,Th53;
   then r <= q by A1,XXREAL_0:2;
  hence contradiction by A3,A2,A4,Th1;
 end;

theorem  :: MEASURE5:52
   r < s implies ].r,s.] <> [.p,q.[
 proof assume that
A1: r < s and
A2: ].r,s.] = [.p,q.[;
A3:  not r in ].r,s.] by Th2;
A4: p <= r by A1,A2,Th57;
    s <= q by A2,A1,Th57;
   then r < q by A1,XXREAL_0:2;
  hence contradiction by A3,A2,A4,Th3;
 end;

theorem :: MEASURE5:52
   r < s implies ].r,s.] <> ].p,q.[
 proof assume that
A1: r < s and
A2: ].r,s.] = ].p,q.[;
    now assume s in ].p,q.[;
     then A3:    s < q by Th4;
      p <= r by A1,A2,Th65;
     then p <= s by A1,XXREAL_0:2;
     then p < q by A3,XXREAL_0:2;
    hence contradiction by A3,A2,Th59;
   end;
  hence contradiction by A1,Th2,A2;
 end;

theorem  :: MEASURE5:52
   r < s implies ].r,s.[ <> [.p,q.]
 proof assume that
A1: r < s and
A2: ].r,s.[ = [.p,q.];
A3:  not r in ].r,s.[ by Th4;
A4: p <= r by A1,A2,Th51;
    s <= q by A2,A1,Th51;
   then r <= q by A1,XXREAL_0:2;
  hence contradiction by A3,A2,A4,Th1;
 end;

theorem :: MEASURE5:52
   r < s implies ].r,s.[ <> ].p,q.]
 proof assume that
A1: r < s and
A2: ].r,s.[ = ].p,q.];
A3:  not s in ].r,s.[ by Th4;
    p <= r by A1,A2,Th59;
   then A4: p < s by A1,XXREAL_0:2;
    s <= q by A2,A1,Th59;
  hence contradiction by A3,A2,A4,Th2;
 end;

theorem  :: MEASURE5:52
   r < s implies ].r,s.[ <> [.p,q.[
 proof assume that
A1: r < s and
A2: ].r,s.[ = [.p,q.[;
A3:  not r in ].r,s.[ by Th4;
A4: p <= r by A1,A2,Th56;
    s <= q by A2,A1,Th56;
   then r < q by A1,XXREAL_0:2;
  hence contradiction by A3,A2,A4,Th3;
 end;

theorem
   r <= s & [.r,s.] c< [.p,q.] implies p < r or s < q
 proof assume
A1: r <= s;
  assume
A2: [.r,s.] c< [.p,q.];
   then [.r,s.] c= [.p,q.] by XBOOLE_0:def 8;
   then A3:  p <= r & s <= q by A1,Th50;
    p <> r or s <> q by A2;
  hence p < r or s < q by A3,XXREAL_0:1;
 end;

theorem :: BORSUK_4:16
   r < s & ].r,s.[ c= [.p,q.] implies [.r,s.] c= [.p,q.]
 proof assume that
A1: r < s and
A2: ].r,s.[ c= [.p,q.];
  let t;
  assume
A3: t in [.r,s.];
  per cases by A3,Th5;
  suppose t in ].r,s.[;
   hence t in [.p,q.] by A2;
  end;
  suppose
A4: t = r;
   then A5:  p <= t by A1,A2,Th51;
     s <= q by A1,A2,Th51;
    then t <= q by A4,A1,XXREAL_0:2;
   hence t in [.p,q.] by Th1,A5;
  end;
  suppose
A6: t = s;
A7:  s <= q by A1,A2,Th51;
     p <= r by A1,A2,Th51;
    then p <= t by A6,A1,XXREAL_0:2;
   hence t in [.p,q.] by A6,Th1,A7;
  end;
 end;

theorem :: BORSUK_4:39
   r < s implies [.s,p.[ c= ].r,p.[
 proof assume
A1: r < s;
  let t;
  assume t in [.s,p.[;
   then s <= t & t < p by Th3;
   then r < t & t < p by A1,XXREAL_0:2;
  hence t in ].r,p.[ by Th4;
 end;

theorem Th85: :: MEASURE5:15
  s <= r implies [.r,s.] c= {r} & [.r,s.] c= {s}
 proof assume
A1: s <= r;
  thus [.r,s.] c= {r}
   proof let t;
    assume t in [.r,s.];
     then r <= t & t <= s by Th1;
     then r <= t & t <= r by A1,XXREAL_0:2;
     then r = t by XXREAL_0:1;
    hence t in {r} by TARSKI:def 1;
   end;
  let t;
  assume t in [.r,s.];
   then r <= t & t <= s by Th1;
   then s <= t & t <= s by A1,XXREAL_0:2;
   then s = t by XXREAL_0:1;
  hence t in {s} by TARSKI:def 1;
 end;

theorem :: RCOMP_1:46
  ].r,s.[ misses {r,s}
 proof let t;
  assume t in ].r,s.[;
   then r < t & t < s by Th4;
  hence not t in {r,s} by TARSKI:def 2;
 end;

theorem :: BORSUK_4:26
  [.r,s.[ misses {s}
 proof let t;
  assume t in [.r,s.[;
   then t < s by Th3;
  hence not t in {s} by TARSKI:def 1;
 end;

theorem :: BORSUK_4:26
  ].r,s.] misses {r}
 proof let t;
  assume t in ].r,s.];
   then r < t by Th2;
  hence not t in {r} by TARSKI:def 1;
 end;

theorem :: BORSUK_4:12
  s <= p implies [.r,s.] misses ].p,q.[
proof assume
A1: s <= p;
 let t;
 assume t in [.r,s.];
  then t <= s by Th1;
  then t <= p by A1,XXREAL_0:2;
 hence not t in ].p,q.[ by Th4;
end;

theorem :: BORSUK_5:19 :: BORSUK_4:30 :: ISOMICHI:38
  s <= p implies [.r,s.] misses ].p,q.]
proof assume
A1: s <= p;
 let t;
 assume t in [.r,s.];
  then t <= s by Th1;
  then t <= p by A1,XXREAL_0:2;
 hence not t in ].p,q.] by Th2;
end;

theorem :: BORSUK_5:26
  s <= p implies ].r,s.] misses ].p,q.[
proof assume
A1: s <= p;
 let t;
 assume t in ].r,s.];
  then t <= s by Th2;
  then t <= p by A1,XXREAL_0:2;
 hence not t in ].p,q.[ by Th4;
end;

theorem :: RCOMP_2:23
   s <= p implies ].r,s.] misses ].p,q.]
proof assume
A1: s <= p;
 let t;
 assume t in ].r,s.];
  then t <= s by Th2;
  then t <= p by A1,XXREAL_0:2;
 hence not t in ].p,q.] by Th2;
end;

theorem :: BORSUK_4:13 :: BORSUK_4:11
   s <= p implies ].r,s.[ misses [.p,q.]
proof assume
A1: s <= p;
 let t;
 assume t in ].r,s.[;
  then t < s by Th4;
  then t < p by A1,XXREAL_0:2;
 hence not t in [.p,q.] by Th1;
end;

theorem :: ISOMICHI:34 :: BORSUK_5:27
   s <= p implies ].r,s.[ misses [.p,q.[
proof assume
A1: s <= p;
 let t;
 assume t in ].r,s.[;
  then t < s by Th4;
  then t < p by A1,XXREAL_0:2;
 hence not t in [.p,q.[ by Th3;
end;

theorem :: BORSUK_5:20 :: BORSUK_4:30
  s <= p implies [.r,s.[ misses [.p,q.]
proof assume
A1: s <= p;
 let t;
 assume t in [.r,s.[;
  then t < s by Th3;
  then t < p by A1,XXREAL_0:2;
 hence not t in [.p,q.] by Th1;
end;

theorem :: RCOMP_2:22
  s <= p implies [.r,s.[ misses [.p,q.[
proof assume
A1: s <= p;
 let t;
 assume t in [.r,s.[;
  then t < s by Th3;
  then t < p by A1,XXREAL_0:2;
 hence not t in [.p,q.[ by Th3;
end;

theorem :: MEASURE5:24
  r < p & r < s implies not ].r,s.[ c= [.p,q.]
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.[ by A5,A4,Th4;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in [.p,q.] by Th1;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t < s by A6,A8, XXREAL_0:2;
 hence t in ].r,s.[ by A7,Th4;
 thus not t in [.p,q.] by A8,Th1;
 end;
end;

theorem :: MEASURE5:38
  r < p & r < s implies not [.r,s.[ c= [.p,q.]
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in [.r,s.[ by A5,A4,Th3;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in [.p,q.] by Th1;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t < s by A6,A8, XXREAL_0:2;
 hence t in [.r,s.[ by A7,Th3;
 thus not t in [.p,q.] by A8,Th1;
 end;
end;

theorem :: MEASURE5:42
  r < p & r < s implies not ].r,s.] c= [.p,q.]
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.] by A5,A4,Th2;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in [.p,q.] by Th1;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t < s by A6,A8, XXREAL_0:2;
 hence t in ].r,s.] by A7,Th2;
 thus not t in [.p,q.] by A8,Th1;
 end;
end;

theorem :: MEASURE5:34
  r < p & r <= s implies not [.r,s.] c= [.p,q.]
proof assume that
A1: r < p and
A2: r <= s;
  take t=r;
  thus t in [.r,s.] by A2,Th1;
  thus not t in [.p,q.] by A1,Th1;
end;

theorem :: MEASURE5:26
  r < p & r < s implies not ].r,s.[ c= [.p,q.[
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.[ by A5,A4,Th4;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in [.p,q.[ by Th3;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t < s by A6,A8, XXREAL_0:2;
 hence t in ].r,s.[ by A7,Th4;
 thus not t in [.p,q.[ by A8,Th3;
 end;
end;

theorem :: MEASURE5:48
  r < p & r < s implies not ].r,s.] c= [.p,q.[
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.] by A5,A4,Th2;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in [.p,q.[ by Th3;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t < s by A6,A8, XXREAL_0:2;
 hence t in ].r,s.] by A7,Th2;
 thus not t in [.p,q.[ by A8,Th3;
 end;
end;

theorem
  r < p & r < s implies not [.r,s.[ c= [.p,q.[
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in [.r,s.[ by A5,A4,Th3;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in [.p,q.[ by Th3;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t < s by A6,A8, XXREAL_0:2;
 hence t in [.r,s.[ by A7,Th3;
 thus not t in [.p,q.[ by A8,Th3;
 end;
end;

theorem :: MEASURE5:44
  r < p & r <= s implies not [.r,s.] c= [.p,q.[
proof assume that
A1: r < p and
A2: r <= s;
  take t=r;
  thus t in [.r,s.] by A2,Th1;
  thus not t in [.p,q.[ by Th3,A1;
end;

theorem :: MEASURE5:30
  r < p & r < s implies not ].r,s.[ c= ].p,q.]
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.[ by A5,A4,Th4;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in ].p,q.] by Th2;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t < s by A6,A8, XXREAL_0:2;
 hence t in ].r,s.[ by A7,Th4;
 thus not t in ].p,q.] by A8,Th2;
 end;
end;

theorem :: MEASURE5:46
  r <= p & r < s implies not [.r,s.[ c= ].p,q.]
proof assume that
A1: r <= p and
A2: r < s;
 take t=r;
 thus t in [.r,s.[ by A2,Th3;
 thus not t in ].p,q.] by A1,Th2;
end;

theorem :: MEASURE5:50
  r < p & r < s implies not ].r,s.] c= ].p,q.]
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.] by A5,A4,Th2;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in ].p,q.] by Th2;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t <= s by A6,A8,XXREAL_0:2;
 hence t in ].r,s.] by A7,Th2;
 thus not t in ].p,q.] by A8,Th2;
 end;
end;

theorem :: MEASURE5:40
  r <= p & r <= s implies not [.r,s.] c= ].p,q.]
proof assume that
A1: r <= p and
A2: r <= s;
 take r;
 thus r in [.r,s.] by A2,Th1;
 thus not r in ].p,q.] by Th2,A1;
end;

theorem :: MEASURE5:22 :: MEASURE5:36
  r <= p & r <= s implies not [.r,s.] c= ].p,q.[
proof assume that
A1: r <= p and
A2: r <= s;
 take r;
 thus r in [.r,s.] by A2,Th1;
 thus not r in ].p,q.[ by Th4,A1;
end;

theorem :: MEASURE5:28
  r <= p & r < s implies not [.r,s.[ c= ].p,q.[
proof assume that
A1: r <= p and
A2: r < s;
 take r;
 thus r in [.r,s.[ by A2,Th3;
 thus not r in ].p,q.[ by Th4,A1;
end;

theorem :: MEASURE5:32
  r < p & r < s implies not ].r,s.] c= ].p,q.[
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.] by A5,A4,Th2;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in ].p,q.[ by Th4;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t <= s by A6,A8,XXREAL_0:2;
 hence t in ].r,s.] by A7,Th2;
 thus not t in ].p,q.[ by A8,Th4;
 end;
end;

theorem :: MEASURE5:20
  r < p & r < s implies not ].r,s.[ c= ].p,q.[
proof assume that
A1: r < p and
A2: r < s;
 per cases;
 suppose
A3: s <= p;
  consider t such that
A4: r < t and
A5: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.[ by A5,A4,Th4;
    t < p by A3,A5,XXREAL_0:2;
  hence not t in ].p,q.[ by Th4;
 end;
 suppose
A6: p <= s;
  consider t such that
A7: r < t and
A8: t < p by A1,XREAL_1:229;
 take t;
   t < s by A6,A8,XXREAL_0:2;
 hence t in ].r,s.[ by A7,Th4;
 thus not t in ].p,q.[ by A8,Th4;
 end;
end;

theorem :: MEASURE5:21
  q < s & r < s implies not ].r,s.[ c= [.p,q.]
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in ].r,s.[ by A5,Th4;
  thus not t in [.p,q.] by A4,Th1;
 end;
 suppose
A6: q <= r;
  consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.[ by A7,A8,Th4;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in [.p,q.] by Th1;
 end;
end;

theorem
  q < s & r < s implies not [.r,s.[ c= [.p,q.]
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in [.r,s.[ by A5,Th3;
  thus not t in [.p,q.] by A4,Th1;
 end;
 suppose
A6: q <= r;
  consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in [.r,s.[ by A7,A8,Th3;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in [.p,q.] by Th1;
 end;
end;

theorem
  q < s & r < s implies not ].r,s.] c= [.p,q.]
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in ].r,s.] by A5,Th2;
  thus not t in [.p,q.] by A4,Th1;
 end;
 suppose
A6: q <= r;
  consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.] by A7,A8,Th2;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in [.p,q.] by Th1;
 end;
end;

theorem
  q < s & r <= s implies not [.r,s.] c= [.p,q.]
proof assume that
A1: q < s and
A2: r <= s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in [.r,s.] by A5,Th1;
  thus not t in [.p,q.] by A4,Th1;
 end;
 suppose
A6: q < r;
 take t=r;
 thus t in [.r,s.] by A2,Th1;
 thus not t in [.p,q.] by A6,Th1;
 end;
end;

theorem
  q < s & r < s implies not ].r,s.[ c= [.p,q.[
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in ].r,s.[ by A5,Th4;
  thus not t in [.p,q.[ by A4,Th3;
 end;
 suppose
A6: q < r;
   consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.[ by A7,A8,Th4;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in [.p,q.[ by Th3;
 end;
end;

theorem
  q <= s & r < s implies not ].r,s.] c= [.p,q.[
proof assume that
A1: q <= s and
A2: r < s;
  take t=s;
  thus t in ].r,s.] by A2,Th2;
  thus not t in [.p,q.[ by A1,Th3;
end;

theorem
  q < s & r < s implies not [.r,s.[ c= [.p,q.[
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in [.r,s.[ by A5,Th3;
  thus not t in [.p,q.[ by A4,Th3;
 end;
 suppose
A6: q < r;
   consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in [.r,s.[ by A7,A8,Th3;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in [.p,q.[ by Th3;
 end;
end;

theorem
  q < s & r < s implies not ].r,s.[ c= ].p,q.]
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in ].r,s.[ by A5,Th4;
  thus not t in ].p,q.] by A4,Th2;
 end;
 suppose
A6: q < r;
   consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.[ by A7,A8,Th4;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in ].p,q.] by Th2;
 end;
end;

theorem
  q < s & r <= s implies not [.r,s.] c= ].p,q.]
proof assume that
A1: q < s and
A2: r <= s;
 take t=s;
 thus t in [.r,s.] by A2,Th1;
  thus not t in ].p,q.] by A1,Th2;
end;

theorem
  q < s & r < s implies not [.r,s.[ c= ].p,q.]
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in [.r,s.[ by A5,Th3;
  thus not t in ].p,q.] by A4,Th2;
 end;
 suppose
A6: q < r;
   consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in [.r,s.[ by A7,A8,Th3;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in ].p,q.] by Th2;
 end;
end;

theorem
  q < s & r < s implies not ].r,s.] c= ].p,q.]
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in ].r,s.] by A5,Th2;
  thus not t in ].p,q.] by A4,Th2;
 end;
 suppose
A6: q < r;
   consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.] by A7,A8,Th2;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in ].p,q.] by Th2;
 end;
end;

theorem
  q <= s & r <= s implies not [.r,s.] c= ].p,q.[
 proof assume that
A1: q <= s and
A2: r <= s;
  take t=s;
  thus t in [.r,s.] by A2,Th1;
  thus not t in ].p,q.[ by A1,Th4;
 end;

theorem
  q < s & r < s implies not [.r,s.[ c= ].p,q.[
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in [.r,s.[ by A5,Th3;
  thus not t in ].p,q.[ by A4,Th4;
 end;
 suppose
A6: q < r;
   consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in [.r,s.[ by A7,A8,Th3;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in ].p,q.[ by Th4;
 end;
end;

theorem
  q <= s & r < s implies not ].r,s.] c= ].p,q.[
proof assume that
A1: q <= s and
A2: r < s;
  take t=s;
  thus t in ].r,s.] by A2,Th2;
  thus not t in ].p,q.[ by A1,Th4;
end;

theorem
  q < s & r < s implies not ].r,s.[ c= ].p,q.[
proof assume that
A1: q < s and
A2: r < s;
 per cases;
 suppose
A3: r <= q;
   consider t such that
A4: q < t and
A5: t < s by A1,XREAL_1:229;
  take t;
    r < t by A3,A4,XXREAL_0:2;
  hence t in ].r,s.[ by A5,Th4;
  thus not t in ].p,q.[ by A4,Th4;
 end;
 suppose
A6: q < r;
   consider t such that
A7: r < t and
A8: t < s by A2,XREAL_1:229;
 take t;
 thus t in ].r,s.[ by A7,A8,Th4;
   q < t by A6,A7,XXREAL_0:2;
 hence not t in ].p,q.[ by Th4;
 end;
end;

begin :: Boolean operations

theorem Th128: :: RCOMP_1:11
 r <= s implies [.r,s.] = ].r,s.[ \/ {r,s}
proof assume
A1: r <= s;
  let t;
 thus t in [.r,s.] implies t in ].r,s.[ \/ {r,s}
  proof
   assume t in [.r,s.];
    then t in ].r,s.[ or t = r or t = s by Th5;
    then t in ].r,s.[ or t in {r,s} by TARSKI:def 2;
   hence t in ].r,s.[ \/ {r,s} by XBOOLE_0:def 2;
  end;
 assume t in ].r,s.[ \/ {r,s};
  then t in ].r,s.[ or t in {r,s} by XBOOLE_0:def 2;
  then t in ].r,s.[ or t = r or t = s by TARSKI:def 2;
 hence t in [.r,s.] by Th16,A1,Th1;
end;

theorem Th129: :: BORSUK_4:34
 r <= s implies [.r,s.] = [.r,s.[ \/ {s}
proof assume
A1: r <= s;
  let t;
 thus t in [.r,s.] implies t in [.r,s.[ \/ {s}
  proof
   assume t in [.r,s.];
    then t in [.r,s.[ or t = s by Th7;
    then t in [.r,s.[ or t in {s} by TARSKI:def 1;
   hence t in [.r,s.[ \/ {s} by XBOOLE_0:def 2;
  end;
 assume t in [.r,s.[ \/ {s};
  then t in [.r,s.[ or t in {s} by XBOOLE_0:def 2;
  then t in [.r,s.[ or t = s by TARSKI:def 1;
 hence t in [.r,s.] by Th13,A1,Th1;
end;

theorem Th130: :: BORSUK_4:35
 r <= s implies [.r,s.] = {r} \/ ].r,s.]
proof assume
A1: r <= s;
  let t;
 thus t in [.r,s.] implies t in {r} \/ ].r,s.]
  proof
   assume t in [.r,s.];
    then t in ].r,s.] or t = r by Th6;
    then t in ].r,s.] or t in {r} by TARSKI:def 1;
   hence t in {r} \/].r,s.] by XBOOLE_0:def 2;
  end;
 assume t in {r} \/].r,s.];
  then t in ].r,s.] or t in {r} by XBOOLE_0:def 2;
  then t in ].r,s.] or t = r by TARSKI:def 1;
 hence t in [.r,s.] by Th12,A1,Th1;
end;

theorem Th131: :: RCOMP_2:5
 r < s implies [.r,s.[ = {r} \/ ].r,s.[
proof assume
A1: r < s;
  let t;
 thus t in [.r,s.[ implies t in {r} \/ ].r,s.[
  proof
   assume t in [.r,s.[;
    then t in ].r,s.[ or t = r by Th8;
    then t in ].r,s.[ or t in {r} by TARSKI:def 1;
   hence t in {r} \/].r,s.[ by XBOOLE_0:def 2;
  end;
 assume t in {r} \/].r,s.[;
  then t in ].r,s.[ or t in {r} by XBOOLE_0:def 2;
  then t in ].r,s.[ or t = r by TARSKI:def 1;
 hence t in [.r,s.[ by Th14,A1,Th3;
end;

theorem Th132: :: RCOMP_2:6
 r < s implies ].r,s.] = ].r,s.[ \/ {s}
proof assume
A1: r < s;
  let t;
 thus t in ].r,s.] implies t in ].r,s.[ \/ {s}
  proof
   assume t in ].r,s.];
    then t in ].r,s.[ or t = s by Th9;
    then t in ].r,s.[ or t in {s} by TARSKI:def 1;
   hence t in ].r,s.[ \/ {s} by XBOOLE_0:def 2;
  end;
 assume t in ].r,s.[ \/ {s};
  then t in ].r,s.[ or t in {s} by XBOOLE_0:def 2;
  then t in ].r,s.[ or t = s by TARSKI:def 1;
 hence t in ].r,s.] by Th15,A1,Th2;
end;

theorem
  r <= s implies [.r,s.] \ {r,s} = ].r,s.[
 proof assume r <= s;
   then A1:  [.r,s.] = ].r,s.[ \/ {r,s} by Th128;
    not r in ].r,s.[ & not s in ].r,s.[ by Th4;
  hence [.r,s.] \ {r,s} = ].r,s.[ by A1,ZFMISC_1:145;
 end;

theorem :: BORSUK_4:27
   r <= s implies [.r,s.] \ {r} = ].r,s.]
 proof assume r <= s;
then A1:  [.r,s.] = {r} \/ ].r,s.] by Th130;
    not r in ].r,s.] by Th2;
  hence [.r,s.] \ {r} = ].r,s.] by A1,ZFMISC_1:141;
 end;

theorem :: BORSUK_4:28
   r <= s implies [.r,s.] \ {s} = [.r,s.[
 proof assume r <= s;
   then A1:  [.r,s.] = [.r,s.[ \/ {s} by Th129;
    not s in [.r,s.[ by Th3;
  hence [.r,s.] \ {s} = [.r,s.[ by A1,ZFMISC_1:141;
 end;

theorem
  r < s implies [.r,s.[ \ {r} = ].r,s.[
 proof assume r < s;
then A1:  [.r,s.[= {r} \/ ].r,s.[ by Th131;
    not r in ].r,s.[ by Th4;
  hence [.r,s.[ \ {r} = ].r,s.[ by A1,ZFMISC_1:141;
 end;

theorem
  r < s implies ].r,s.] \ {s} = ].r,s.[
 proof assume r < s;
then A1:  ].r,s.] = ].r,s.[ \/ {s} by Th132;
    not s in ].r,s.[ by Th4;
  hence ].r,s.] \ {s} = ].r,s.[ by A1,ZFMISC_1:141;
 end;

theorem :: BORSUK_4:29
  r < s & s < t implies ].r,s.] /\ [.s,t.[ = {s}
 proof assume that
A1: r < s and
A2: s < t;
    now let x;
    hereby assume
A3:   x in ].r,s.] /\ [.s,t.[;
     then reconsider p = x as ext-real number by MEMBERED:def 2;
       p in ].r,s.] & p in [.s,t.[ by A3,XBOOLE_0:def 3;
      then s <= p & p <= s by Th2,Th3;
     hence x = s by XXREAL_0:1;
    end;
    assume
A4:   x = s;
      s in ].r,s.] & s in [.s,t.[ by A1,A2,Th2,Th3;
    hence x in ].r,s.] /\ [.s,t.[ by A4,XBOOLE_0:def 3;
   end;
  hence ].r,s.] /\ [.s,t.[ = {s} by TARSKI:def 1;
 end;

theorem :: RCOMP_2:15
 [.r,s.[ /\ [.p,q.[ = [.max(r,p),min(s,q).[
proof let t;
 thus t in [.r,s.[ /\ [.p,q.[ implies t in [.max(r,p),min(s,q).[
  proof assume t in [.r,s.[ /\ [.p,q.[;
    then t in [.r,s.[ & t in [.p,q.[ by XBOOLE_0:def 3;
    then r <= t & t < s & p <= t & t < q by Th3;
    then max(r,p) <= t & t < min(s,q) by XXREAL_0:21,28;
   hence t in [.max(r,p),min(s,q).[ by Th3;
  end;
 assume t in [.max(r,p),min(s,q).[;
  then max(r,p) <= t & t < min(s,q) by Th3;
  then r <= t & p <= t & t < s & t < q by XXREAL_0:23,30;
  then t in [.r,s.[ & t in [.p,q.[ by Th3;
 hence t in [.r,s.[ /\ [.p,q.[ by XBOOLE_0:def 3;
end;

theorem
 [.r,s.] /\ [.p,q.] = [.max(r,p),min(s,q).]
proof let t;
 thus t in [.r,s.] /\ [.p,q.] implies t in [.max(r,p),min(s,q).]
  proof assume t in [.r,s.] /\ [.p,q.];
    then t in [.r,s.] & t in [.p,q.] by XBOOLE_0:def 3;
    then r <= t & t <= s & p <= t & t <= q by Th1;
    then max(r,p) <= t & t <= min(s,q) by XXREAL_0:20,28;
   hence t in [.max(r,p),min(s,q).] by Th1;
  end;
 assume t in [.max(r,p),min(s,q).];
  then max(r,p) <= t & t <= min(s,q) by Th1;
  then r <= t & p <= t & t <= s & t <= q by XXREAL_0:22,30;
  then t in [.r,s.] & t in [.p,q.] by Th1;
 hence t in [.r,s.] /\ [.p,q.] by XBOOLE_0:def 3;
end;

theorem :: RCOMP_2:16
   ].r,s.] /\ ].p,q.] = ].max(r,p),min(s,q).]
proof let t;
 thus t in ].r,s.] /\ ].p,q.] implies t in ].max(r,p),min(s,q).]
  proof assume t in ].r,s.] /\ ].p,q.];
    then t in ].r,s.] & t in ].p,q.] by XBOOLE_0:def 3;
    then r < t & t <= s & p < t & t <= q by Th2;
    then max(r,p) < t & t <= min(s,q) by XXREAL_0:20,29;
   hence t in ].max(r,p),min(s,q).] by Th2;
  end;
 assume t in ].max(r,p),min(s,q).];
  then max(r,p) < t & t <= min(s,q) by Th2;
  then r < t & p < t & t <= s & t <= q by XXREAL_0:22,31;
  then t in ].r,s.] & t in ].p,q.] by Th2;
 hence t in ].r,s.] /\ ].p,q.] by XBOOLE_0:def 3;
end;

theorem
   ].r,s.[ /\ ].p,q.[ = ].max(r,p),min(s,q).[
proof let t;
 thus t in ].r,s.[ /\ ].p,q.[ implies t in ].max(r,p),min(s,q).[
  proof assume t in ].r,s.[ /\ ].p,q.[;
    then t in ].r,s.[ & t in ].p,q.[ by XBOOLE_0:def 3;
    then r < t & t < s & p < t & t < q by Th4;
    then max(r,p) < t & t < min(s,q) by XXREAL_0:21,29;
   hence t in ].max(r,p),min(s,q).[ by Th4;
  end;
 assume t in ].max(r,p),min(s,q).[;
  then max(r,p) < t & t < min(s,q) by Th4;
  then r < t & p < t & t < s & t < q by XXREAL_0:23,31;
  then t in ].r,s.[ & t in ].p,q.[ by Th4;
 hence t in ].r,s.[ /\ ].p,q.[ by XBOOLE_0:def 3;
end;

theorem :: RCOMP_1:51 :: TREAL_1:3
   r <= p & s <= q implies [.r,s.] /\ [.p,q.] = [.p,s.]
 proof assume that
A1: r <= p and
A2: s <= q;
  let t;
  thus t in [.r,s.] /\ [.p,q.] implies t in [.p,s.]
   proof assume t in [.r,s.] /\ [.p,q.];
     then t in [.r,s.] & t in [.p,q.] by XBOOLE_0:def 3;
     then t <= s & p <= t by Th1;
    hence t in [.p,s.] by Th1;
   end;
  assume t in [.p,s.];
   then A3:  p <= t & t <= s by Th1;
   then r <= t & t <= q by A1,A2,XXREAL_0:2;
   then t in [.r,s.] & t in [.p,q.] by A3,Th1;
  hence t in [.r,s.] /\ [.p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r <= p & s <= q implies [.r,s.[ /\ [.p,q.] = [.p,s.[
 proof assume that
A1: r <= p and
A2: s <= q;
  let t;
  thus t in [.r,s.[ /\ [.p,q.] implies t in [.p,s.[
   proof assume t in [.r,s.[ /\ [.p,q.];
     then t in [.r,s.[ & t in [.p,q.] by XBOOLE_0:def 3;
     then t < s & p <= t by Th1,Th3;
    hence t in [.p,s.[ by Th3;
   end;
  assume t in [.p,s.[;
   then A3:  p <= t & t < s by Th3;
   then r <= t & t <= q by A1,A2,XXREAL_0:2;
   then t in [.r,s.[ & t in [.p,q.] by A3,Th1,Th3;
  hence t in [.r,s.[ /\ [.p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r >= p & s > q implies [.r,s.[ /\ [.p,q.] = [.r,q.]
 proof assume that
A1: r >= p and
A2: s > q;
  let t;
  thus t in [.r,s.[ /\ [.p,q.] implies t in [.r,q.]
   proof assume t in [.r,s.[ /\ [.p,q.];
     then t in [.r,s.[ & t in [.p,q.] by XBOOLE_0:def 3;
     then r <= t & t <= q by Th1,Th3;
    hence t in [.r,q.] by Th1;
   end;
  assume t in [.r,q.];
   then A3:  r <= t & t <= q by Th1;
   then t < s & p <= t by A1,A2,XXREAL_0:2;
   then t in [.r,s.[ & t in [.p,q.] by A3,Th1,Th3;
  hence t in [.r,s.[ /\ [.p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r < p & s <= q implies ].r,s.] /\ [.p,q.] = [.p,s.]
 proof assume that
A1: r < p and
A2: s <= q;
  let t;
  thus t in ].r,s.] /\ [.p,q.] implies t in [.p,s.]
   proof assume t in ].r,s.] /\ [.p,q.];
     then t in ].r,s.] & t in [.p,q.] by XBOOLE_0:def 3;
     then t <= s & p <= t by Th1,Th2;
    hence t in [.p,s.] by Th1;
   end;
  assume t in [.p,s.];
   then A3:  p <= t & t <= s by Th1;
   then r < t & t <= q by A1,A2,XXREAL_0:2;
   then t in ].r,s.] & t in [.p,q.] by A3,Th1,Th2;
  hence t in ].r,s.] /\ [.p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r >= p & s >= q implies ].r,s.] /\ [.p,q.] = ].r,q.]
 proof assume that
A1: r >= p and
A2: s >= q;
  let t;
  thus t in ].r,s.] /\ [.p,q.] implies t in ].r,q.]
   proof assume t in ].r,s.] /\ [.p,q.];
     then t in ].r,s.] & t in [.p,q.] by XBOOLE_0:def 3;
     then r < t & t <= q by Th1,Th2;
    hence t in ].r,q.] by Th2;
   end;
  assume t in ].r,q.];
   then A3:  r < t & t <= q by Th2;
   then t <= s & p <= t by A1,A2,XXREAL_0:2;
   then t in ].r,s.] & t in [.p,q.] by A3,Th1,Th2;
  hence t in ].r,s.] /\ [.p,q.] by XBOOLE_0:def 3;
 end;

theorem :: TOPGEN_5:8
   r < p & s <= q implies ].r,s.[ /\ [.p,q.] = [.p,s.[
 proof assume that
A1: r < p and
A2: s <= q;
  let t;
  thus t in ].r,s.[ /\ [.p,q.] implies t in [.p,s.[
   proof assume t in ].r,s.[ /\ [.p,q.];
     then t in ].r,s.[ & t in [.p,q.] by XBOOLE_0:def 3;
     then t < s & p <= t by Th1,Th4;
    hence t in [.p,s.[ by Th3;
   end;
  assume t in [.p,s.[;
   then A3:  p <= t & t < s by Th3;
   then r < t & t <= q by A1,A2,XXREAL_0:2;
   then t in ].r,s.[ & t in [.p,q.] by A3,Th1,Th4;
  hence t in ].r,s.[ /\ [.p,q.] by XBOOLE_0:def 3;
 end;

theorem :: TOPGEN_5:9
   r >= p & s > q implies ].r,s.[ /\ [.p,q.] = ].r,q.]
 proof assume that
A1: r >= p and
A2: s > q;
  let t;
  thus t in ].r,s.[ /\ [.p,q.] implies t in ].r,q.]
   proof assume t in ].r,s.[ /\ [.p,q.];
     then t in ].r,s.[ & t in [.p,q.] by XBOOLE_0:def 3;
     then r < t & t <= q by Th1,Th4;
    hence t in ].r,q.] by Th2;
   end;
  assume t in ].r,q.];
   then A3:  r < t & t <= q by Th2;
   then t < s & p <= t by A1,A2,XXREAL_0:2;
   then t in ].r,s.[ & t in [.p,q.] by A3,Th1,Th4;
  hence t in ].r,s.[ /\ [.p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r <= p & s <= q implies [.r,s.[ /\ [.p,q.[ = [.p,s.[
proof assume that
A1: r <= p and
A2: s <= q;
  let t;
  thus t in [.r,s.[ /\ [.p,q.[ implies t in [.p,s.[
   proof assume t in [.r,s.[ /\ [.p,q.[;
     then t in [.r,s.[ & t in [.p,q.[ by XBOOLE_0:def 3;
     then t < s & p <= t by Th3;
    hence t in [.p,s.[ by Th3;
   end;
  assume t in [.p,s.[;
   then A3:  p <= t & t < s by Th3;
   then r <= t & t < q by A1,A2,XXREAL_0:2;
   then t in [.r,s.[ & t in [.p,q.[ by A3,Th3;
  hence t in [.r,s.[ /\ [.p,q.[ by XBOOLE_0:def 3;
 end;

theorem
   r >= p & s >= q implies [.r,s.[ /\ [.p,q.[ = [.r,q.[
 proof assume that
A1: r >= p and
A2: s >= q;
  let t;
  thus t in [.r,s.[ /\ [.p,q.[ implies t in [.r,q.[
   proof assume t in [.r,s.[ /\ [.p,q.[;
     then t in [.r,s.[ & t in [.p,q.[ by XBOOLE_0:def 3;
     then r <= t & t < q by Th3;
    hence t in [.r,q.[ by Th3;
   end;
  assume t in [.r,q.[;
   then A3:  r <= t & t < q by Th3;
   then t < s & p <= t by A1,A2,XXREAL_0:2;
   then t in [.r,s.[ & t in [.p,q.[ by A3,Th3;
  hence t in [.r,s.[ /\ [.p,q.[ by XBOOLE_0:def 3;
 end;

theorem
   r < p & s < q implies ].r,s.] /\ [.p,q.[ = [.p,s.]
proof assume that
A1: r < p and
A2: s < q;
  let t;
  thus t in ].r,s.] /\ [.p,q.[ implies t in [.p,s.]
   proof assume t in ].r,s.] /\ [.p,q.[;
     then t in ].r,s.] & t in [.p,q.[ by XBOOLE_0:def 3;
     then t <= s & p <= t by Th2,Th3;
    hence t in [.p,s.] by Th1;
   end;
  assume t in [.p,s.];
   then A3:  p <= t & t <= s by Th1;
   then r < t & t < q by A1,A2,XXREAL_0:2;
   then t in ].r,s.] & t in [.p,q.[ by A3,Th3,Th2;
  hence t in ].r,s.] /\ [.p,q.[ by XBOOLE_0:def 3;
 end;

theorem :: TOPGEN_5:11
   r >= p & s >= q implies ].r,s.] /\ [.p,q.[ = ].r,q.[
 proof assume that
A1: r >= p and
A2: s >= q;
  let t;
  thus t in ].r,s.] /\ [.p,q.[ implies t in ].r,q.[
   proof assume t in ].r,s.] /\ [.p,q.[;
     then t in ].r,s.] & t in [.p,q.[ by XBOOLE_0:def 3;
     then r < t & t < q by Th2,Th3;
    hence t in ].r,q.[ by Th4;
   end;
  assume t in ].r,q.[;
   then A3:  r < t & t < q by Th4;
   then t <= s & p <= t by A1,A2,XXREAL_0:2;
   then t in ].r,s.] & t in [.p,q.[ by A3,Th2,Th3;
  hence t in ].r,s.] /\ [.p,q.[ by XBOOLE_0:def 3;
 end;

theorem
   r < p & s <= q implies ].r,s.[ /\ [.p,q.[ = [.p,s.[
proof assume that
A1: r < p and
A2: s <= q;
  let t;
  thus t in ].r,s.[ /\ [.p,q.[ implies t in [.p,s.[
   proof assume t in ].r,s.[ /\ [.p,q.[;
     then t in ].r,s.[ & t in [.p,q.[ by XBOOLE_0:def 3;
     then t < s & p <= t by Th3,Th4;
    hence t in [.p,s.[ by Th3;
   end;
  assume t in [.p,s.[;
   then A3:  p <= t & t < s by Th3;
   then r < t & t < q by A1,A2,XXREAL_0:2;
   then t in ].r,s.[ & t in [.p,q.[ by A3,Th3,Th4;
  hence t in ].r,s.[ /\ [.p,q.[ by XBOOLE_0:def 3;
 end;

theorem
   r >= p & s >= q implies ].r,s.[ /\ [.p,q.[ = ].r,q.[
 proof assume that
A1: r >= p and
A2: s >= q;
  let t;
  thus t in ].r,s.[ /\ [.p,q.[ implies t in ].r,q.[
   proof assume t in ].r,s.[ /\ [.p,q.[;
     then t in ].r,s.[ & t in [.p,q.[ by XBOOLE_0:def 3;
     then r < t & t < q by Th3,Th4;
    hence t in ].r,q.[ by Th4;
   end;
  assume t in ].r,q.[;
   then A3:  r < t & t < q by Th4;
   then t < s & p <= t by A1,A2,XXREAL_0:2;
   then t in ].r,s.[ & t in [.p,q.[ by A3,Th3,Th4;
  hence t in ].r,s.[ /\ [.p,q.[ by XBOOLE_0:def 3;
 end;

theorem
   r <= p & s <= q implies ].r,s.] /\ ].p,q.] = ].p,s.]
proof assume that
A1: r <= p and
A2: s <= q;
  let t;
  thus t in ].r,s.] /\ ].p,q.] implies t in ].p,s.]
   proof assume t in ].r,s.] /\ ].p,q.];
     then t in ].r,s.] & t in ].p,q.] by XBOOLE_0:def 3;
     then t <= s & p < t by Th2;
    hence t in ].p,s.] by Th2;
   end;
  assume t in ].p,s.];
   then A3:  p < t & t <= s by Th2;
   then r < t & t <= q by A1,A2,XXREAL_0:2;
   then t in ].r,s.] & t in ].p,q.] by A3,Th2;
  hence t in ].r,s.] /\ ].p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r >= p & s >= q implies ].r,s.] /\ ].p,q.] = ].r,q.]
 proof assume that
A1: r >= p and
A2: s >= q;
  let t;
  thus t in ].r,s.] /\ ].p,q.] implies t in ].r,q.]
   proof assume t in ].r,s.] /\ ].p,q.];
     then t in ].r,s.] & t in ].p,q.] by XBOOLE_0:def 3;
     then r < t & t <= q by Th2;
    hence t in ].r,q.] by Th2;
   end;
  assume t in ].r,q.];
   then A3:  r < t & t <= q by Th2;
   then t <= s & p < t by A1,A2,XXREAL_0:2;
   then t in ].r,s.] & t in ].p,q.] by A3,Th2;
  hence t in ].r,s.] /\ ].p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r <= p & s <= q implies ].r,s.[ /\ ].p,q.] = ].p,s.[
proof assume that
A1: r <= p and
A2: s <= q;
  let t;
  thus t in ].r,s.[ /\ ].p,q.] implies t in ].p,s.[
   proof assume t in ].r,s.[ /\ ].p,q.];
     then t in ].r,s.[ & t in ].p,q.] by XBOOLE_0:def 3;
     then t < s & p < t by Th2,Th4;
    hence t in ].p,s.[ by Th4;
   end;
  assume t in ].p,s.[;
   then A3:  p < t & t < s by Th4;
   then r < t & t <= q by A1,A2,XXREAL_0:2;
   then t in ].r,s.[ & t in ].p,q.] by A3,Th2,Th4;
  hence t in ].r,s.[ /\ ].p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r >= p & s > q implies ].r,s.[ /\ ].p,q.] = ].r,q.]
 proof assume that
A1: r >= p and
A2: s > q;
  let t;
  thus t in ].r,s.[ /\ ].p,q.] implies t in ].r,q.]
   proof assume t in ].r,s.[ /\ ].p,q.];
     then t in ].r,s.[ & t in ].p,q.] by XBOOLE_0:def 3;
     then r < t & t <= q by Th2,Th4;
    hence t in ].r,q.] by Th2;
   end;
  assume t in ].r,q.];
   then A3:  r < t & t <= q by Th2;
   then t < s & p < t by A1,A2,XXREAL_0:2;
   then t in ].r,s.[ & t in ].p,q.] by A3,Th2,Th4;
  hence t in ].r,s.[ /\ ].p,q.] by XBOOLE_0:def 3;
 end;

theorem
   r <= p & s <= q implies ].r,s.[ /\ ].p,q.[ = ].p,s.[
proof assume that
A1: r <= p and
A2: s <= q;
  let t;
  thus t in ].r,s.[ /\ ].p,q.[ implies t in ].p,s.[
   proof assume t in ].r,s.[ /\ ].p,q.[;
     then t in ].r,s.[ & t in ].p,q.[ by XBOOLE_0:def 3;
     then t < s & p < t by Th4;
    hence t in ].p,s.[ by Th4;
   end;
  assume t in ].p,s.[;
   then A3:  p < t & t < s by Th4;
   then r < t & t < q by A1,A2,XXREAL_0:2;
   then t in ].r,s.[ & t in ].p,q.[ by A3,Th4;
  hence t in ].r,s.[ /\ ].p,q.[ by XBOOLE_0:def 3;
 end;

theorem
   [.r,s.[ \/ [.p,q.[ c= [.min(r,p),max(s,q).[
 proof let t;
  assume t in [.r,s.[ \/ [.p,q.[;
   then t in [.r,s.[ or t in [.p,q.[ by XBOOLE_0:def 2;
   then r <= t & t < s or p <= t & t < q by Th3;
   then min(r,p) <= t & t < max(s,q) by XXREAL_0:23,30;
  hence t in [.min(r,p),max(s,q).[ by Th3;
 end;

theorem :: RCOMP_2:24
   [.r,s.[ meets [.p,q.[ implies
    [.r,s.[ \/ [.p,q.[ = [.min(r,p),max(s,q).[
proof assume [.r,s.[ meets [.p,q.[;
 then consider u such that
A1: u in [.r,s.[ and
A2: u in [.p,q.[ by MEMBERED:def 20;
 let t;
 thus t in [.r,s.[ \/ [.p,q.[ implies t in [.min(r,p),max(s,q).[
  proof assume t in [.r,s.[ \/ [.p,q.[;
    then t in [.r,s.[ or t in [.p,q.[ by XBOOLE_0:def 2;
    then r <= t & t < s or p <= t & t < q by Th3;
    then min(r,p) <= t & t < max(s,q) by XXREAL_0:23,30;
   hence t in [.min(r,p),max(s,q).[ by Th3;
  end;
A3: r <= u & u < s by A1,Th3;
A4: p <= u & u < q by A2,Th3;
 assume t in [.min(r,p),max(s,q).[;
  then A5: min(r,p) <= t & t < max(s,q) by Th3;
  per cases by A5,XXREAL_0:28,21;
  suppose r <= t & t < s or p <= t & t < q;
    then t in [.r,s.[ or t in [.p,q.[ by Th3;
   hence t in [.r,s.[ \/ [.p,q.[ by XBOOLE_0:def 2;
  end;
  suppose that
A6: p <= t and
A7: t < s;
     u <= t or t <= u;
    then r <= t or t < q by A3,A4,XXREAL_0:2;
    then t in [.r,s.[ or t in [.p,q.[ by Th3,A6,A7;
   hence t in [.r,s.[ \/ [.p,q.[ by XBOOLE_0:def 2;
  end;
  suppose that
A8:  r <= t and
A9: t < q;
     u <= t or t <= u;
    then t < s or p <= t by A3,A4,XXREAL_0:2;
    then t in [.r,s.[ or t in [.p,q.[ by Th3,A8,A9;
   hence t in [.r,s.[ \/ [.p,q.[ by XBOOLE_0:def 2;
  end;
end;

theorem
  ].r,s.] \/ ].p,q.] c= ].min(r,p),max(s,q).]
 proof let t;
  assume t in ].r,s.] \/ ].p,q.];
   then t in ].r,s.] or t in ].p,q.] by XBOOLE_0:def 2;
   then r < t & t <= s or p < t & t <= q by Th2;
   then min(r,p) < t & t <= max(s,q) by XXREAL_0:22,31;
  hence t in ].min(r,p),max(s,q).] by Th2;
 end;

theorem :: RCOMP_2:25
 ].r,s.] meets ].p,q.] implies
    ].r,s.] \/ ].p,q.] = ].min(r,p),max(s,q).]
proof assume ].r,s.] meets ].p,q.];
 then consider u such that
A1: u in ].r,s.] and
A2: u in ].p,q.] by MEMBERED:def 20;
 let t;
 thus t in ].r,s.] \/ ].p,q.] implies t in ].min(r,p),max(s,q).]
  proof assume t in ].r,s.] \/ ].p,q.];
    then t in ].r,s.] or t in ].p,q.] by XBOOLE_0:def 2;
    then r < t & t <= s or p < t & t <= q by Th2;
    then min(r,p) < t & t <= max(s,q) by XXREAL_0:22,31;
   hence t in ].min(r,p),max(s,q).] by Th2;
  end;
A3: r < u & u <= s by A1,Th2;
A4: p < u & u <= q by A2,Th2;
 assume t in ].min(r,p),max(s,q).];
  then A5: min(r,p) < t & t <= max(s,q) by Th2;
  per cases by A5,XXREAL_0:29,20;
  suppose r < t & t <= s or p < t & t <= q;
    then t in ].r,s.] or t in ].p,q.] by Th2;
   hence t in ].r,s.] \/ ].p,q.] by XBOOLE_0:def 2;
  end;
  suppose that
A6: p < t and
A7: t <= s;
     u <= t or t <= u;
    then r < t or t <= q by A3,A4,XXREAL_0:2;
    then t in ].r,s.] or t in ].p,q.] by Th2,A6,A7;
   hence t in ].r,s.] \/ ].p,q.] by XBOOLE_0:def 2;
  end;
  suppose that
A8:  r < t and
A9: t <= q;
     u <= t or t <= u;
    then t <= s or p < t by A3,A4,XXREAL_0:2;
    then t in ].r,s.] or t in ].p,q.] by Th2,A8,A9;
   hence t in ].r,s.] \/ ].p,q.] by XBOOLE_0:def 2;
  end;
end;

theorem :: HEINE:2 :: RCOMP_2:21
   r <= s & s <= t implies [.r,s.] \/ [.s,t.] = [.r,t.]
 proof assume that
A1: r <= s and
A2: s <= t;
  let p;
  thus p in [.r,s.] \/ [.s,t.] implies p in [.r,t.]
   proof assume p in [.r,s.] \/ [.s,t.];
     then p in [.r,s.] or p in [.s,t.] by XBOOLE_0:def 2;
     then r <= p & p <= s or s <= p & p <= t by Th1;
     then r <= p & p <= t by A1,A2,XXREAL_0:2;
    hence p in [.r,t.] by Th1;
   end;
  assume p in [.r,t.];
   then r <= p & p <= s or s <= p & p <= t by Th1;
   then p in [.r,s.] or p in [.s,t.] by Th1;
  hence p in [.r,s.] \/ [.s,t.] by XBOOLE_0:def 2;
 end;

theorem Th166:
 r <= s & s <= t implies [.r,s.[ \/ [.s,t.] = [.r,t.]
 proof assume that
A1: r <= s and
A2: s <= t;
  let p;
  thus p in [.r,s.[ \/ [.s,t.] implies p in [.r,t.]
   proof assume p in [.r,s.[ \/ [.s,t.];
     then p in [.r,s.[ or p in [.s,t.] by XBOOLE_0:def 2;
     then r <= p & p < s or s <= p & p <= t by Th1,Th3;
     then r <= p & p <= t by A1,A2,XXREAL_0:2;
    hence p in [.r,t.] by Th1;
   end;
  assume p in [.r,t.];
   then r <= p & p < s or s <= p & p <= t by Th1;
   then p in [.r,s.[ or p in [.s,t.] by Th1,Th3;
  hence p in [.r,s.[ \/ [.s,t.] by XBOOLE_0:def 2;
 end;

theorem Th167: :: RCOMP_2:20
 r <= s & s <= t implies [.r,s.] \/ ].s,t.] = [.r,t.]
 proof assume that
A1: r <= s and
A2: s <= t;
  let p;
  thus p in [.r,s.] \/ ].s,t.] implies p in [.r,t.]
   proof assume p in [.r,s.] \/ ].s,t.];
     then p in [.r,s.] or p in ].s,t.] by XBOOLE_0:def 2;
     then r <= p & p <= s or s < p & p <= t by Th1,Th2;
     then r <= p & p <= t by A1,A2,XXREAL_0:2;
    hence p in [.r,t.] by Th1;
   end;
  assume p in [.r,t.];
   then r <= p & p <= s or s < p & p <= t by Th1;
   then p in [.r,s.] or p in ].s,t.] by Th1,Th2;
  hence p in [.r,s.] \/ ].s,t.] by XBOOLE_0:def 2;
 end;

theorem :: RCOMP_2:11
  r <= s & s <= t implies [.r,s.[ \/ [.s,t.[ = [.r,t.[
 proof assume that
A1: r <= s and
A2: s <= t;
  let p;
  thus p in [.r,s.[ \/ [.s,t.[ implies p in [.r,t.[
   proof assume p in [.r,s.[ \/ [.s,t.[;
     then p in [.r,s.[ or p in [.s,t.[ by XBOOLE_0:def 2;
     then r <= p & p < s or s <= p & p < t by Th3;
     then r <= p & p < t by A1,A2,XXREAL_0:2;
    hence p in [.r,t.[ by Th3;
   end;
  assume p in [.r,t.[;
   then r <= p & p < s or s <= p & p < t by Th3;
   then p in [.r,s.[ or p in [.s,t.[ by Th3;
  hence p in [.r,s.[ \/ [.s,t.[ by XBOOLE_0:def 2;
 end;

theorem Th169: :: RCOMP_2:11
 r <= s & s < t implies [.r,s.] \/ ].s,t.[ = [.r,t.[
 proof assume that
A1: r <= s and
A2: s < t;
  let p;
  thus p in [.r,s.] \/ ].s,t.[ implies p in [.r,t.[
   proof assume p in [.r,s.] \/ ].s,t.[;
     then p in [.r,s.] or p in ].s,t.[ by XBOOLE_0:def 2;
     then r <= p & p <= s or s < p & p < t by Th1,Th4;
     then r <= p & p < t by A1,A2,XXREAL_0:2;
    hence p in [.r,t.[ by Th3;
   end;
  assume p in [.r,t.[;
   then r <= p & p <= s or s < p & p < t by Th3;
   then p in [.r,s.] or p in ].s,t.[ by Th1,Th4;
  hence p in [.r,s.] \/ ].s,t.[ by XBOOLE_0:def 2;
 end;

theorem :: RCOMP_2:12
  r <= s & s <= t implies ].r,s.] \/ ].s,t.] = ].r,t.]
 proof assume that
A1: r <= s and
A2: s <= t;
  let p;
  thus p in ].r,s.] \/ ].s,t.] implies p in ].r,t.]
   proof assume p in ].r,s.] \/ ].s,t.];
     then p in ].r,s.] or p in ].s,t.] by XBOOLE_0:def 2;
     then r < p & p <= s or s < p & p <= t by Th2;
     then r < p & p <= t by A1,A2,XXREAL_0:2;
    hence p in ].r,t.] by Th2;
   end;
  assume p in ].r,t.];
   then r < p & p <= s or s < p & p <= t by Th2;
   then p in ].r,s.] or p in ].s,t.] by Th2;
  hence p in ].r,s.] \/ ].s,t.] by XBOOLE_0:def 2;
 end;

theorem Th171: :: BORSUK_4:38
 r <= s & s < t implies ].r,s.] \/ ].s,t.[ = ].r,t.[
 proof assume that
A1: r <= s and
A2: s < t;
  let p;
  thus p in ].r,s.] \/ ].s,t.[ implies p in ].r,t.[
   proof
    assume p in ].r,s.] \/ ].s,t.[;
     then p in ].r,s.] or p in ].s,t.[ by XBOOLE_0:def 2;
     then r < p & p <= s or s < p & p < t by Th2,Th4;
     then r < p & p < t by A1,A2,XXREAL_0:2;
    hence p in ].r,t.[ by Th4;
   end;
  assume p in ].r,t.[;
   then r < p & p <= s or s < p & p < t by Th4;
   then p in ].r,s.] or p in ].s,t.[ by Th2,Th4;
  hence p in ].r,s.] \/ ].s,t.[ by XBOOLE_0:def 2;
 end;

theorem :: BORSUK_4:40
  r < s & s < t implies ].r,s.] \/ [.s,t.[ = ].r,t.[
 proof assume that
A1: r < s and
A2: s < t;
  let p;
  thus p in ].r,s.] \/ [.s,t.[ implies p in ].r,t.[
   proof
    assume p in ].r,s.] \/ [.s,t.[;
     then p in ].r,s.] or p in [.s,t.[ by XBOOLE_0:def 2;
     then r < p & p <= s or s <= p & p < t by Th2,Th3;
     then r < p & p < t by A1,A2,XXREAL_0:2;
    hence p in ].r,t.[ by Th4;
   end;
  assume p in ].r,t.[;
   then r < p & p <= s or s < p & p < t by Th4;
   then p in ].r,s.] or p in [.s,t.[ by Th2,Th3;
  hence p in ].r,s.] \/ [.s,t.[ by XBOOLE_0:def 2;
 end;

theorem Th173:
 r < s & s < t implies ].r,s.[ \/ [.s,t.[ = ].r,t.[
 proof assume that
A1: r < s and
A2: s < t;
  let p;
  thus p in ].r,s.[ \/ [.s,t.[ implies p in ].r,t.[
   proof
    assume p in ].r,s.[ \/ [.s,t.[;
     then p in ].r,s.[ or p in [.s,t.[ by XBOOLE_0:def 2;
     then r < p & p < s or s <= p & p < t by Th3,Th4;
     then r < p & p < t by A1,A2,XXREAL_0:2;
    hence p in ].r,t.[ by Th4;
   end;
  assume p in ].r,t.[;
   then r < p & p < s or s <= p & p < t by Th4;
   then p in ].r,s.[ or p in [.s,t.[ by Th3,Th4;
  hence p in ].r,s.[ \/ [.s,t.[ by XBOOLE_0:def 2;
 end;

theorem Th174: :: RCOMP_1:50 :: TREAL_1:2
 p <= s & r <= q & s <= r implies [.p,r.] \/ [.s,q.] = [.p,q.]
 proof assume that
A1: p <= s and
A2: r <= q and
A3: s <= r;
  let t;
  thus t in [.p,r.] \/ [.s,q.] implies t in [.p,q.]
   proof assume t in [.p,r.] \/ [.s,q.];
     then t in [.p,r.] or t in [.s,q.] by XBOOLE_0:def 2;
     then p <= t & t <= r or s <= t & t <= q by Th1;
     then p <= t & t <= q by A1,A2,XXREAL_0:2;
    hence t in [.p,q.] by Th1;
   end;
  assume t in [.p,q.];
   then p <= t & t <= r or s <= t & t <= q by A3,XXREAL_0:2,Th1;
   then t in [.p,r.] or t in [.s,q.] by Th1;
  hence t in [.p,r.] \/ [.s,q.] by XBOOLE_0:def 2;
 end;

theorem Th175: :: TOPGEN_5:10
 p <= s & r <= q & s < r implies [.p,r.[ \/ ].s,q.] = [.p,q.]
 proof assume that
A1: p <= s and
A2: r <= q and
A3: s < r;
  let t;
  thus t in [.p,r.[ \/ ].s,q.] implies t in [.p,q.]
   proof assume t in [.p,r.[ \/ ].s,q.];
     then t in [.p,r.[ or t in ].s,q.] by XBOOLE_0:def 2;
     then p <= t & t <= r or s <= t & t <= q by Th2,Th3;
     then p <= t & t <= q by A1,A2,XXREAL_0:2;
    hence t in [.p,q.] by Th1;
   end;
  assume t in [.p,q.];
   then p <= t & t < r or s < t & t <= q by A3,XXREAL_0:2,Th1;
   then t in [.p,r.[ or t in ].s,q.] by Th2,Th3;
  hence t in [.p,r.[ \/ ].s,q.] by XBOOLE_0:def 2;
 end;

theorem :: BORSUK_5:28
  p <= s & s <= r & r < q implies [.p,r.] \/ [.s,q.[ = [.p,q.[
 proof assume that
A1: p <= s and
A2: s <= r and
A3: r < q;
  let t;
  thus t in [.p,r.] \/ [.s,q.[ implies t in [.p,q.[
   proof assume t in [.p,r.] \/ [.s,q.[;
     then t in [.p,r.] or t in [.s,q.[ by XBOOLE_0:def 2;
     then p <= t & t <= r or s <= t & t < q by Th1,Th3;
     then p <= t & t < q by A1,XXREAL_0:2,A3;
    hence t in [.p,q.[ by Th3;
   end;
  assume t in [.p,q.[;
   then p <= t & t <= r or s <= t & t < q by A2,XXREAL_0:2,Th3;
   then t in [.p,r.] or t in [.s,q.[ by Th1,Th3;
  hence t in [.p,r.] \/ [.s,q.[ by XBOOLE_0:def 2;
 end;

theorem :: BORSUK_5:29
  p < s & r <= q & s <= r implies ].p,r.] \/ [.s,q.] = ].p,q.]
 proof assume that
A1: p < s and
A2: r <= q and
A3: s <= r;
  let t;
  thus t in ].p,r.] \/ [.s,q.] implies t in ].p,q.]
   proof assume t in ].p,r.] \/ [.s,q.];
     then t in ].p,r.] or t in [.s,q.] by XBOOLE_0:def 2;
     then p < t & t <= r or s <= t & t <= q by Th1,Th2;
     then p < t & t <= q by A1,XXREAL_0:2,A2;
    hence t in ].p,q.] by Th2;
   end;
  assume t in ].p,q.];
   then p < t & t <= r or s <= t & t <= q by A3,XXREAL_0:2,Th2;
   then t in ].p,r.] or t in [.s,q.] by Th1,Th2;
  hence t in ].p,r.] \/ [.s,q.] by XBOOLE_0:def 2;
 end;

theorem Th178:
 p < s & r < q & s <= r implies ].p,r.] \/ [.s,q.[ = ].p,q.[
 proof assume that
A1: p < s and
A2: r < q and
A3: s <= r;
  let t;
  thus t in ].p,r.] \/ [.s,q.[ implies t in ].p,q.[
   proof assume t in ].p,r.] \/ [.s,q.[;
     then t in ].p,r.] or t in [.s,q.[ by XBOOLE_0:def 2;
     then p < t & t <= r or s <= t & t < q by Th3,Th2;
     then p < t & t < q by A1,XXREAL_0:2,A2;
    hence t in ].p,q.[ by Th4;
   end;
  assume t in ].p,q.[;
   then p < t & t <= r or s <= t & t < q by A3,XXREAL_0:2,Th4;
   then t in ].p,r.] or t in [.s,q.[ by Th3,Th2;
  hence t in ].p,r.] \/ [.s,q.[ by XBOOLE_0:def 2;
 end;

theorem :: RCOMP_2:13
  p<=r & p<=s & r<=q & s<=q implies [.p,r.[ \/ [.r,s.] \/ ].s,q.] = [.p,q.]
 proof assume that
A1: p <= r and
A2: p <= s and
A3: r <= q and
A4: s <= q;
  per cases;
  suppose r <= s;
  hence [.p,r.[ \/ [.r,s.] \/ ].s,q.] = [.p,s.] \/ ].s,q.] by Th166,A1
        .= [.p,q.] by A2,A4,Th167;
  end;
  suppose
A5: s < r;
  hence [.p,r.[ \/ [.r,s.] \/ ].s,q.] = [.p,r.[ \/ {} \/ ].s,q.] by Th29
        .= [.p,q.] by A5,Th175,A2,A3;
  end;
 end;

theorem :: RCOMP_2:14
  p<r & p<s & r<q & s<q implies ].p,r.] \/ ].r,s.[ \/ [.s,q.[ = ].p,q.[
 proof assume that
A1: p < r and
A2: p < s and
A3: r < q and
A4: s < q;
  per cases;
  suppose r < s;
  hence ].p,r.] \/ ].r,s.[ \/ [.s,q.[ = ].p,s.[ \/ [.s,q.[ by Th171,A1
        .= ].p,q.[ by A2,A4,Th173;
  end;
  suppose
A5: s <= r;
  hence ].p,r.] \/ ].r,s.[ \/ [.s,q.[ = ].p,r.] \/ {} \/ [.s,q.[ by Th28
        .= ].p,q.[ by A5,Th178,A2,A3;
  end;
 end;

theorem :: BORSUK_4:36
  p <= r & r <= s & s <= q implies [.p,r.] \/ ].r,s.[ \/ [.s,q.] = [.p,q.]
 proof assume that
A1: p <= r and
A2: r <= s and
A3: s <= q;
A4: p <= s by A2,A1,XXREAL_0:2;
A5: r <= q by A2,A3,XXREAL_0:2;
  per cases;
  suppose r < s;
  hence [.p,r.] \/ ].r,s.[ \/ [.s,q.] = [.p,s.[ \/ [.s,q.] by Th169,A1
        .= [.p,q.] by A3,Th166,A4;
  end;
  suppose
A6: s <= r;
  hence [.p,r.] \/ ].r,s.[ \/ [.s,q.] = [.p,r.] \/ {} \/ [.s,q.] by Th28
        .= [.p,q.] by A6,Th174,A4,A5;
  end;
 end;

theorem Th182:
 r <= s implies [.r,t.] \ [.r,s.] = ].s,t.]
 proof assume that
A1: r <= s;
  let p;
  thus p in [.r,t.] \ [.r,s.] implies p in ].s,t.]
   proof assume p in [.r,t.] \ [.r,s.];
     then p in [.r,t.] & not p in [.r,s.] by XBOOLE_0:def 4;
     then r <= p & p <= t & (p < r or s < p) by Th1;
    hence p in ].s,t.] by Th2;
   end;
  assume p in ].s,t.];
   then s < p & p <= t by Th2;
   then r <= p & p <= t & (p < r or s < p) by A1,XXREAL_0:2;
   then p in [.r,t.] & not p in [.r,s.] by Th1;
  hence p in [.r,t.] \ [.r,s.] by XBOOLE_0:def 4;
 end;

theorem
  r <= s implies [.r,t.[ \ [.r,s.] = ].s,t.[
 proof assume that
A1: r <= s;
  let p;
  thus p in [.r,t.[ \ [.r,s.] implies p in ].s,t.[
   proof assume p in [.r,t.[ \ [.r,s.];
     then p in [.r,t.[ & not p in [.r,s.] by XBOOLE_0:def 4;
     then r <= p & p < t & (p < r or s < p) by Th1,Th3;
    hence p in ].s,t.[ by Th4;
   end;
  assume p in ].s,t.[;
   then s < p & p < t by Th4;
   then r <= p & p < t & (p < r or s < p) by A1,XXREAL_0:2;
   then p in [.r,t.[ & not p in [.r,s.] by Th1,Th3;
  hence p in [.r,t.[ \ [.r,s.] by XBOOLE_0:def 4;
 end;

theorem
  r < s implies [.r,t.] \ [.r,s.[ = [.s,t.]
 proof assume that
A1: r < s;
  let p;
  thus p in [.r,t.] \ [.r,s.[ implies p in [.s,t.]
   proof assume p in [.r,t.] \ [.r,s.[;
     then p in [.r,t.] & not p in [.r,s.[ by XBOOLE_0:def 4;
     then r <= p & p <= t & (p < r or s <= p) by Th1,Th3;
    hence p in [.s,t.] by Th1;
   end;
  assume p in [.s,t.];
   then s <= p & p <= t by Th1;
   then r <= p & p <= t & (p < r or s <= p) by A1,XXREAL_0:2;
   then p in [.r,t.] & not p in [.r,s.[ by Th1,Th3;
  hence p in [.r,t.] \ [.r,s.[ by XBOOLE_0:def 4;
 end;

theorem
  r < s implies [.r,t.[ \ [.r,s.[ = [.s,t.[
 proof assume that
A1: r < s;
  let p;
  thus p in [.r,t.[ \ [.r,s.[ implies p in [.s,t.[
   proof assume p in [.r,t.[ \ [.r,s.[;
     then p in [.r,t.[ & not p in [.r,s.[ by XBOOLE_0:def 4;
     then r <= p & p < t & (p < r or s <= p) by Th3;
    hence p in [.s,t.[ by Th3;
   end;
  assume p in [.s,t.[;
   then s <= p & p < t by Th3;
   then r <= p & p < t & (p < r or s <= p) by A1,XXREAL_0:2;
   then p in [.r,t.[ & not p in [.r,s.[ by Th3;
  hence p in [.r,t.[ \ [.r,s.[ by XBOOLE_0:def 4;
 end;

theorem
  r <= s implies [.r,t.] \ [.r,s.] = ].s,t.]
 proof assume that
A1: r <= s;
  let p;
  thus p in [.r,t.] \ [.r,s.] implies p in ].s,t.]
   proof assume p in [.r,t.] \ [.r,s.];
     then p in [.r,t.] & not p in [.r,s.] by XBOOLE_0:def 4;
     then r <= p & p <= t & (p < r or s < p) by Th1;
    hence p in ].s,t.] by Th2;
   end;
  assume p in ].s,t.];
   then s < p & p <= t by Th2;
   then r <= p & p <= t & (p < r or s < p) by A1,XXREAL_0:2;
   then p in [.r,t.] & not p in [.r,s.] by Th1;
  hence p in [.r,t.] \ [.r,s.] by XBOOLE_0:def 4;
 end;

theorem:: BORSUK_4:41
  r < s implies ].r,t.[ \ ].r,s.] = ].s,t.[
 proof assume that
A1: r < s;
  let p;
  thus p in ].r,t.[ \ ].r,s.] implies p in ].s,t.[
   proof assume p in ].r,t.[ \ ].r,s.];
     then p in ].r,t.[ & not p in ].r,s.] by XBOOLE_0:def 4;
     then r < p & p < t & (p <= r or s < p) by Th4,Th2;
    hence p in ].s,t.[ by Th4;
   end;
  assume p in ].s,t.[;
   then s < p & p < t by Th4;
   then r < p & p < t & (p <= r or s < p) by A1,XXREAL_0:2;
   then p in ].r,t.[ & not p in ].r,s.] by Th4,Th2;
  hence p in ].r,t.[ \ ].r,s.] by XBOOLE_0:def 4;
 end;

theorem
  r < s implies ].r,t.] \ ].r,s.[ = [.s,t.]
 proof assume that
A1: r < s;
  let p;
  thus p in ].r,t.] \ ].r,s.[ implies p in [.s,t.]
   proof assume p in ].r,t.] \ ].r,s.[;
     then p in ].r,t.] & not p in ].r,s.[ by XBOOLE_0:def 4;
     then r < p & p <= t & (p <= r or s <= p) by Th2,Th4;
    hence p in [.s,t.] by Th1;
   end;
  assume p in [.s,t.];
   then s <= p & p <= t by Th1;
   then r < p & p <= t & (p <= r or s <= p) by A1,XXREAL_0:2;
   then p in ].r,t.] & not p in ].r,s.[ by Th4,Th2;
  hence p in ].r,t.] \ ].r,s.[ by XBOOLE_0:def 4;
 end;

theorem
  r < s implies ].r,t.[ \ ].r,s.[ = [.s,t.[
 proof assume that
A1: r < s;
  let p;
  thus p in ].r,t.[ \ ].r,s.[ implies p in [.s,t.[
   proof assume p in ].r,t.[ \ ].r,s.[;
     then p in ].r,t.[ & not p in ].r,s.[ by XBOOLE_0:def 4;
     then r < p & p < t & (p <= r or s <= p) by Th4;
    hence p in [.s,t.[ by Th3;
   end;
  assume p in [.s,t.[;
   then s <= p & p < t by Th3;
   then r < p & p < t & (p <= r or s <= p) by A1,XXREAL_0:2;
   then p in ].r,t.[ & not p in ].r,s.[ by Th4;
  hence p in ].r,t.[ \ ].r,s.[ by XBOOLE_0:def 4;
 end;

theorem
  s <= t implies [.r,t.] \ [.s,t.] = [.r,s.[
 proof assume that
A1: s <= t;
  let p;
  thus p in [.r,t.] \ [.s,t.] implies p in [.r,s.[
   proof assume p in [.r,t.] \ [.s,t.];
     then p in [.r,t.] & not p in [.s,t.] by XBOOLE_0:def 4;
     then r <= p & p <= t & (p < s or t < p) by Th1;
    hence p in [.r,s.[ by Th3;
   end;
  assume p in [.r,s.[;
   then r <= p & p < s by Th3;
   then r <= p & p <= t & (p < s or t < p) by A1,XXREAL_0:2;
   then p in [.r,t.] & not p in [.s,t.] by Th1;
  hence p in [.r,t.] \ [.s,t.] by XBOOLE_0:def 4;
 end;

theorem Th191: :: BORSUK_4:42
 s <= t implies ].r,t.] \ [.s,t.] = ].r,s.[
 proof assume that
A1: s <= t;
  let p;
  thus p in ].r,t.] \ [.s,t.] implies p in ].r,s.[
   proof assume p in ].r,t.] \ [.s,t.];
     then p in ].r,t.] & not p in [.s,t.] by XBOOLE_0:def 4;
     then r < p & p <= t & (p < s or t < p) by Th1,Th2;
    hence p in ].r,s.[ by Th4;
   end;
  assume p in ].r,s.[;
   then r < p & p < s by Th4;
   then r < p & p <= t & (p < s or t < p) by A1,XXREAL_0:2;
   then p in ].r,t.] & not p in [.s,t.] by Th1,Th2;
  hence p in ].r,t.] \ [.s,t.] by XBOOLE_0:def 4;
 end;

theorem
  s < t implies [.r,t.] \ ].s,t.] = [.r,s.]
 proof assume that
A1: s < t;
  let p;
  thus p in [.r,t.] \ ].s,t.] implies p in [.r,s.]
   proof assume p in [.r,t.] \ ].s,t.];
     then p in [.r,t.] & not p in ].s,t.] by XBOOLE_0:def 4;
     then r <= p & p <= t & (p <= s or t < p) by Th1,Th2;
    hence p in [.r,s.] by Th1;
   end;
  assume p in [.r,s.];
   then r <= p & p <= s by Th1;
   then r <= p & p <= t & (p <= s or t < p) by A1,XXREAL_0:2;
   then p in [.r,t.] & not p in ].s,t.] by Th1,Th2;
  hence p in [.r,t.] \ ].s,t.] by XBOOLE_0:def 4;
 end;

theorem
  s < t implies ].r,t.] \ ].s,t.] = ].r,s.]
 proof assume that
A1: s < t;
  let p;
  thus p in ].r,t.] \ ].s,t.] implies p in ].r,s.]
   proof assume p in ].r,t.] \ ].s,t.];
     then p in ].r,t.] & not p in ].s,t.] by XBOOLE_0:def 4;
     then r < p & p <= t & (p <= s or t < p) by Th2;
    hence p in ].r,s.] by Th2;
   end;
  assume p in ].r,s.];
   then r < p & p <= s by Th2;
   then r < p & p <= t & (p <= s or t < p) by A1,XXREAL_0:2;
   then p in ].r,t.] & not p in ].s,t.] by Th2;
  hence p in ].r,t.] \ ].s,t.] by XBOOLE_0:def 4;
 end;

theorem
  s < t implies [.r,t.[ \ [.s,t.[ = [.r,s.[
 proof assume that
A1: s < t;
  let p;
  thus p in [.r,t.[ \ [.s,t.[ implies p in [.r,s.[
   proof assume p in [.r,t.[ \ [.s,t.[;
     then p in [.r,t.[ & not p in [.s,t.[ by XBOOLE_0:def 4;
     then r <= p & p < t & (p < s or t <= p) by Th3;
    hence p in [.r,s.[ by Th3;
   end;
  assume p in [.r,s.[;
   then r <= p & p < s by Th3;
   then r <= p & p < t & (p < s or t <= p) by A1,XXREAL_0:2;
   then p in [.r,t.[ & not p in [.s,t.[ by Th3;
  hence p in [.r,t.[ \ [.s,t.[ by XBOOLE_0:def 4;
 end;

theorem
  s < t implies ].r,t.[ \ [.s,t.[ = ].r,s.[
 proof assume that
A1: s < t;
  let p;
  thus p in ].r,t.[ \ [.s,t.[ implies p in ].r,s.[
   proof assume p in ].r,t.[ \ [.s,t.[;
     then p in ].r,t.[ & not p in [.s,t.[ by XBOOLE_0:def 4;
     then r < p & p < t & (p < s or t <= p) by Th4,Th3;
    hence p in ].r,s.[ by Th4;
   end;
  assume p in ].r,s.[;
   then r < p & p < s by Th4;
   then r < p & p < t & (p < s or t <= p) by A1,XXREAL_0:2;
   then p in ].r,t.[ & not p in [.s,t.[ by Th4,Th3;
  hence p in ].r,t.[ \ [.s,t.[ by XBOOLE_0:def 4;
 end;

theorem
  s < t implies [.r,t.[ \ ].s,t.[ = [.r,s.]
 proof assume that
A1: s < t;
  let p;
  thus p in [.r,t.[ \ ].s,t.[ implies p in [.r,s.]
   proof assume p in [.r,t.[ \ ].s,t.[;
     then p in [.r,t.[ & not p in ].s,t.[ by XBOOLE_0:def 4;
     then r <= p & p < t & (p <= s or t <= p) by Th4,Th3;
    hence p in [.r,s.] by Th1;
   end;
  assume p in [.r,s.];
   then r <= p & p <= s by Th1;
   then r <= p & p < t & (p <= s or t <= p) by A1,XXREAL_0:2;
   then p in [.r,t.[ & not p in ].s,t.[ by Th4,Th3;
  hence p in [.r,t.[ \ ].s,t.[ by XBOOLE_0:def 4;
 end;

theorem
  s < t implies ].r,t.[ \ ].s,t.[ = ].r,s.]
 proof assume that
A1: s < t;
  let p;
  thus p in ].r,t.[ \ ].s,t.[ implies p in ].r,s.]
   proof assume p in ].r,t.[ \ ].s,t.[;
     then p in ].r,t.[ & not p in ].s,t.[ by XBOOLE_0:def 4;
     then r < p & p < t & (p <= s or t <= p) by Th4;
    hence p in ].r,s.] by Th2;
   end;
  assume p in ].r,s.];
   then r < p & p <= s by Th2;
   then r < p & p < t & (p <= s or t <= p) by A1,XXREAL_0:2;
   then p in ].r,t.[ & not p in ].s,t.[ by Th4;
  hence p in ].r,t.[ \ ].s,t.[ by XBOOLE_0:def 4;
 end;

theorem :: RCOMP_2:26
  [.p,q.[ meets [.r,s.[ implies [.p,q.[ \ [.r,s.[ = [.p,r.[ \/ [.s,q.[
proof assume
 [.p,q.[ meets [.r,s.[;
 then consider u such that
A1: u in [.r,s.[ and
A2: u in [.p,q.[ by MEMBERED:def 20;
A3: r <= u & u <= s by A1,Th3;
A4: p <= u & u <= q by A2,Th3;
  then A5: r <= q by A3,XXREAL_0:2;
A6: p <= s by A3,A4,XXREAL_0:2;
 let t;
 thus t in [.p,q.[ \ [.r,s.[ implies t in [.p,r.[ \/ [.s,q.[
  proof assume t in [.p,q.[ \ [.r,s.[;
    then t in [.p,q.[ & not(t in [.r,s.[) by XBOOLE_0:def 4;
    then p <= t & t < q & (t < r or s <= t) by Th3;
    then t in [.p,r.[ or t in [.s,q.[ by Th3;
   hence t in [.p,r.[ \/ [.s,q.[ by XBOOLE_0:def 2;
  end;
 assume t in [.p,r.[ \/ [.s,q.[;
  then t in [.p,r.[ or t in [.s,q.[ by XBOOLE_0:def 2;
  then p <= t & t < r or s <= t & t < q by Th3;
  then p <= t & t < q & (t < r or s <= t) by A5,XXREAL_0:2,A6;
  then t in [.p,q.[ & not(t in [.r,s.[) by Th3;
 hence t in [.p,q.[ \ [.r,s.[ by XBOOLE_0:def 4;
end;

theorem :: RCOMP_2:27
  ].p,q.] meets ].r,s.] implies ].p,q.] \ ].r,s.] = ].p,r.] \/ ].s,q.]
proof assume
  ].p,q.] meets ].r,s.];
 then consider u such that
A1: u in ].r,s.] and
A2: u in ].p,q.] by MEMBERED:def 20;
A3: r < u & u <= s by A1,Th2;
A4: p < u & u <= q by A2,Th2;
  then A5: r <= q by A3,XXREAL_0:2;
A6: p <= s by A3,A4,XXREAL_0:2;
 let t;
 thus t in ].p,q.] \ ].r,s.] implies t in ].p,r.] \/ ].s,q.]
  proof assume t in ].p,q.] \ ].r,s.];
    then t in ].p,q.] & not(t in ].r,s.]) by XBOOLE_0:def 4;
    then p < t & t <= q & (t <= r or s < t) by Th2;
    then t in ].p,r.] or t in ].s,q.] by Th2;
   hence t in ].p,r.] \/ ].s,q.] by XBOOLE_0:def 2;
  end;
 assume t in ].p,r.] \/ ].s,q.];
  then t in ].p,r.] or t in ].s,q.] by XBOOLE_0:def 2;
  then p < t & t <= r or s < t & t <= q by Th2;
  then p < t & t <= q & (t <= r or s < t) by A5,XXREAL_0:2,A6;
  then t in ].p,q.] & not(t in ].r,s.]) by Th2;
 hence t in ].p,q.] \ ].r,s.] by XBOOLE_0:def 4;
end;

theorem :: BORSUK_4:37
  p <= r & s <= q implies [.p,q.] \ ([.p,r.] \/ [.s,q.]) = ].r,s.[
  proof assume that
A1: p <= r and
A2: s <= q;
   thus [.p,q.] \ ([.p,r.] \/ [.s,q.])
          = [.p,q.] \ [.p,r.] \ [.s,q.] by XBOOLE_1:41
         .= ].r,q.] \ [.s,q.] by A1,Th182
         .= ].r,s.[ by A2,Th191;
  end;

theorem :: BORSUK_4:31
  r <= s & s <= t implies [.r,t.] \ {s} = [.r,s.[ \/ ].s,t.]
 proof assume that
A1: r <= s and
A2: s <= t;
  let p;
  thus p in [.r,t.] \ {s} implies p in [.r,s.[ \/ ].s,t.]
   proof assume
A3:  p in [.r,t.] \ {s};
     then not p in {s} by XBOOLE_0:def 4;
     then p <> s by TARSKI:def 1;
     then r <= p & p < s or s < p & p <= t by XXREAL_0:1,Th1,A3;
     then p in [.r,s.[ or p in ].s,t.] by Th2,Th3;
    hence p in [.r,s.[ \/ ].s,t.] by XBOOLE_0:def 2;
   end;
  assume p in [.r,s.[ \/ ].s,t.];
   then p in [.r,s.[ or p in ].s,t.] by XBOOLE_0:def 2;
   then r <= p & p < s or s < p & p <= t by Th2,Th3;
   then r <= p & p <= t & p <> s by A1,A2,XXREAL_0:2;
   then p in [.r,t.] & not p in {s} by Th1,TARSKI:def 1;
  hence p in [.r,t.] \ {s} by XBOOLE_0:def 4;
 end;

theorem
  r <= s & s < t implies [.r,t.[ \ {s} = [.r,s.[ \/ ].s,t.[
 proof assume that
A1: r <= s and
A2: s < t;
  let p;
  thus p in [.r,t.[ \ {s} implies p in [.r,s.[ \/ ].s,t.[
   proof assume
A3:  p in [.r,t.[ \ {s};
     then not p in {s} by XBOOLE_0:def 4;
     then p <> s by TARSKI:def 1;
     then r <= p & p < s or s < p & p < t by XXREAL_0:1,Th3,A3;
     then p in [.r,s.[ or p in ].s,t.[ by Th4,Th3;
    hence p in [.r,s.[ \/ ].s,t.[ by XBOOLE_0:def 2;
   end;
  assume p in [.r,s.[ \/ ].s,t.[;
   then p in [.r,s.[ or p in ].s,t.[ by XBOOLE_0:def 2;
   then r <= p & p < s or s < p & p < t by Th4,Th3;
   then r <= p & p < t & p <> s by A1,A2,XXREAL_0:2;
   then p in [.r,t.[ & not p in {s} by Th3,TARSKI:def 1;
  hence p in [.r,t.[ \ {s} by XBOOLE_0:def 4;
 end;

theorem :: BORSUK_4:31
  r < s & s <= t implies ].r,t.] \ {s} = ].r,s.[ \/ ].s,t.]
 proof assume that
A1: r < s and
A2: s <= t;
  let p;
  thus p in ].r,t.] \ {s} implies p in ].r,s.[ \/ ].s,t.]
   proof assume
A3:  p in ].r,t.] \ {s};
     then not p in {s} by XBOOLE_0:def 4;
     then p <> s by TARSKI:def 1;
     then r < p & p < s or s < p & p <= t by XXREAL_0:1,Th2,A3;
     then p in ].r,s.[ or p in ].s,t.] by Th2,Th4;
    hence p in ].r,s.[ \/ ].s,t.] by XBOOLE_0:def 2;
   end;
  assume p in ].r,s.[ \/ ].s,t.];
   then p in ].r,s.[ or p in ].s,t.] by XBOOLE_0:def 2;
   then r < p & p < s or s < p & p <= t by Th2,Th4;
   then r < p & p <= t & p <> s by A1,A2,XXREAL_0:2;
   then p in ].r,t.] & not p in {s} by Th2,TARSKI:def 1;
  hence p in ].r,t.] \ {s} by XBOOLE_0:def 4;
 end;

theorem
  r < s & s < t implies ].r,t.[ \ {s} = ].r,s.[ \/ ].s,t.[
 proof assume that
A1: r < s and
A2: s < t;
  let p;
  thus p in ].r,t.[ \ {s} implies p in ].r,s.[ \/ ].s,t.[
   proof assume
A3:  p in ].r,t.[ \ {s};
     then not p in {s} by XBOOLE_0:def 4;
     then p <> s by TARSKI:def 1;
     then r < p & p < s or s < p & p < t by XXREAL_0:1,Th4,A3;
     then p in ].r,s.[ or p in ].s,t.[ by Th4;
    hence p in ].r,s.[ \/ ].s,t.[ by XBOOLE_0:def 2;
   end;
  assume p in ].r,s.[ \/ ].s,t.[;
   then p in ].r,s.[ or p in ].s,t.[ by XBOOLE_0:def 2;
   then r < p & p < s or s < p & p < t by Th4;
   then r < p & p < t & p <> s by A1,A2,XXREAL_0:2;
   then p in ].r,t.[ & not p in {s} by Th4,TARSKI:def 1;
  hence p in ].r,t.[ \ {s} by XBOOLE_0:def 4;
 end;

theorem :: BORSUK_5:87
  not s in ].r,s .[ \/ ].s,t.[
 proof assume s in ].r,s .[ \/ ].s,t.[;
   then s in ].r,s .[ or s in ].s,t.[ by XBOOLE_0:def 2;
  hence contradiction by Th4;
 end;

theorem
  not s in [.r,s .[ \/ ].s,t.[
 proof assume s in [.r,s .[ \/ ].s,t.[;
   then s in [.r,s .[ or s in ].s,t.[ by XBOOLE_0:def 2;
  hence contradiction by Th4,Th3;
 end;

theorem
  not s in ].r,s .[ \/ ].s,t.]
 proof assume s in ].r,s .[ \/ ].s,t.];
   then s in ].r,s .[ or s in ].s,t.] by XBOOLE_0:def 2;
  hence contradiction by Th4,Th2;
 end;

theorem
  not s in [.r,s .[ \/ ].s,t.]
 proof assume s in [.r,s .[ \/ ].s,t.];
   then s in [.r,s .[ or s in ].s,t.] by XBOOLE_0:def 2;
  hence contradiction by Th2,Th3;
 end;

begin :: -infty & + infty

theorem
  [.-infty,+infty.] = ExtREAL
 proof let r;
  thus r in [.-infty,+infty.] implies r in ExtREAL by XXREAL_0:def 1;
  assume r in ExtREAL;
    -infty <= r & r <= +infty by XXREAL_0:3,5;
  hence thesis by Th1;
 end;

theorem :: MEASURE6:36
  ].p,-infty.[ = {}
 proof
    not ex x st x in ].p,-infty.[
    proof given x such that
A1:    x in ].p,-infty.[;
      reconsider s = x as ext-real number by A1,MEMBERED:def 2;
       p < s & s < -infty by A1,Th4;
     hence contradiction by XXREAL_0:5;
    end;
  hence thesis by XBOOLE_0:def 1;
 end;

theorem :: MEASURE6:36
  [.p,-infty.[ = {}
 proof
    not ex x st x in [.p,-infty.[
    proof given x such that
A1:    x in [.p,-infty.[;
      reconsider s = x as ext-real number by A1,MEMBERED:def 2;
       p <= s & s < -infty by A1,Th3;
     hence contradiction by XXREAL_0:5;
    end;
  hence thesis by XBOOLE_0:def 1;
 end;

theorem :: MEASURE6:36
  ].p,-infty.] = {}
 proof
    not ex x st x in ].p,-infty.]
    proof given x such that
A1:    x in ].p,-infty.];
      reconsider s = x as ext-real number by A1,MEMBERED:def 2;
       p < s & s <= -infty by A1,Th2;
      then p < -infty by XXREAL_0:2;
     hence contradiction by XXREAL_0:5;
    end;
  hence thesis by XBOOLE_0:def 1;
 end;

theorem :: MEASURE6:36
  p <> -infty implies [.p,-infty.] = {}
 proof assume
A1: p <> -infty;
    not ex x st x in [.p,-infty.]
    proof given x such that
A2:    x in [.p,-infty.];
      reconsider s = x as ext-real number by A2,MEMBERED:def 2;
       p <= s & s <= -infty by A2,Th1;
      then p <= -infty by XXREAL_0:2;
     hence contradiction by XXREAL_0:6,A1;
    end;
  hence thesis by XBOOLE_0:def 1;
 end;

theorem :: MEASURE6:37
  ].+infty,p.[ = {}
 proof
    not ex x st x in ].+infty,p.[
    proof given x such that
A1:    x in ].+infty,p.[;
      reconsider s = x as ext-real number by A1,MEMBERED:def 2;
       +infty < s & s < p by A1,Th4;
     hence contradiction by XXREAL_0:3;
    end;
  hence thesis by XBOOLE_0:def 1;
 end;

theorem :: MEASURE6:37
  [.+infty,p.[ = {}
 proof
    not ex x st x in [.+infty,p.[
    proof given x such that
A1:    x in [.+infty,p.[;
      reconsider s = x as ext-real number by A1,MEMBERED:def 2;
       +infty <= s & s < p by A1,Th3;
      then p > +infty by XXREAL_0:2;
     hence contradiction by XXREAL_0:3;
    end;
  hence thesis by XBOOLE_0:def 1;
 end;

theorem :: MEASURE6:37
  ].+infty,p.] = {}
 proof
    not ex x st x in ].+infty,p.]
    proof given x such that
A1:    x in ].+infty,p.];
      reconsider s = x as ext-real number by A1,MEMBERED:def 2;
       +infty < s & s <= p by A1,Th2;
     hence contradiction by XXREAL_0:3;
    end;
  hence thesis by XBOOLE_0:def 1;
 end;

theorem :: MEASURE6:37
  p <> +infty implies [.+infty,p.] = {}
 proof assume
A1: p <> +infty;
    not ex x st x in [.+infty,p.]
    proof given x such that
A2:    x in [.+infty,p.];
      reconsider s = x as ext-real number by A2,MEMBERED:def 2;
       +infty <= s & s <= p by A2,Th1;
      then p >= +infty by XXREAL_0:2;
     hence contradiction by XXREAL_0:4,A1;
    end;
  hence thesis by XBOOLE_0:def 1;
 end;

theorem
  p > q implies p in ].q, +infty.]
  proof
     p <= +infty by XXREAL_0:3;
   hence thesis by Th2;
  end;

theorem
  q <= p implies p in [.q, +infty.]
  proof
     p <= +infty by XXREAL_0:3;
   hence thesis by Th1;
  end;

theorem
  p <= q implies p in [.-infty,q.]
  proof
     p >= -infty by XXREAL_0:5;
   hence thesis by Th1;
  end;

theorem
  p < q implies p in [.-infty, q.[
  proof
     p >= -infty by XXREAL_0:5;
   hence thesis by Th3;
  end;

begin :: Additional

theorem :: RCOMP_1:18
  p <= q implies [.p,q.] = [.p,q.] \/ [.q,p.]
  proof assume
A1:  p <= q;
    then A2:   [.q,p.] c= {p} by Th85;
     p in [.p,q.] by A1,Th1;
    then {p} c= [.p,q.] by ZFMISC_1:37;
    then [.q,p.] c= [.p,q.] by A2,XBOOLE_1:1;
   hence [.p,q.] = [.p,q.] \/ [.q,p.] by XBOOLE_1:12;
  end;

theorem :: BORSUK_5:86
  r <= s & s <= t implies not r in ].s,t.[ \/ ].t,p.[
 proof assume that
A1: r <= s and
A2: s <= t;
  assume r in ].s,t.[ \/ ].t,p.[;
   then r in ].s,t.[ or r in ].t,p.[ by XBOOLE_0:def 2;
   then s < r & r < t or t < r & r < p by Th4;
  hence contradiction by A1,A2,XXREAL_0:2;
 end;
