:: Vertex sequences induced by chains
::  by Yatsuka Nakamura and Piotr Rudnicki
::
:: Received May 13, 1995
:: Copyright (c) 1995 Association of Mizar Users

environ

 vocabularies NUMBERS, FINSEQ_1, XBOOLE_0, SUBSET_1, NAT_1, ARYTM_3, XXREAL_0,
      INT_1, ARYTM_1, RELAT_1, FUNCT_1, TARSKI, FINSET_1, CARD_1, ORDINAL1,
      ORDINAL4, GRAPH_1, STRUCT_0, TREES_2, PARTFUN1, GLIB_000, FINSEQ_2,
      ZFMISC_1, FINSEQ_4, FUNCT_4, GRAPH_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, CARD_1, NUMBERS, XCMPLX_0,
      XXREAL_0, RELAT_1, RELSET_1, XXREAL_2, FINSEQ_1, FUNCT_1, PARTFUN1,
      FUNCT_2, REALSET1, FINSEQ_2, FINSEQ_4, FUNCT_7, FINSET_1, NAT_1, NAT_D,
      INT_1, RECDEF_1, STRUCT_0, GRAPH_1;
 constructors PARTFUN1, WELLORD2, XXREAL_0, NAT_1, INT_1, RECDEF_1, FINSEQ_4,
      REALSET1, BINARITH, FINSEQ_6, GRAPH_1, FUNCT_7, MEMBERED, XXREAL_2,
      NAT_D, RELSET_1, FINSEQ_2;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, FINSET_1, XXREAL_0, XREAL_0, NAT_1,
      INT_1, CARD_1, FINSEQ_1, REALSET1, FINSEQ_6, GRAPH_1, NUMBERS, MEMBERED,
      VALUED_0, XXREAL_2, STRUCT_0, RELSET_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, GRAPH_1, XBOOLE_0, FINSEQ_2, FINSEQ_1;
 theorems TARSKI, ENUMSET1, NAT_1, FINSEQ_1, FINSEQ_2, GRAPH_1, RELAT_1,
      FUNCT_1, GRFUNC_1, FUNCT_2, INT_1, CARD_1, CARD_2, FINSEQ_3, FINSEQ_4,
      CQC_THE1, ZFMISC_1, XBOOLE_0, XBOOLE_1, XCMPLX_1, ORDINAL1, XREAL_1,
      FINSEQ_5, REALSET1, XXREAL_0, MEMBERED, FUNCT_7, PARTFUN1, XXREAL_2,
      XREAL_0;
 schemes NAT_1, FINSEQ_1, FUNCT_1;

begin :: Preliminaries

reserve p, q for FinSequence,
  X, Y, x, y for set,
  D for non empty set,
  i, j, k , l, m, n, r for Element of NAT;

theorem Th1:
  for m,k,n being Nat holds m+1<=k & k<=n iff ex i being Element of
  NAT st m<=i & i<n & k=i+1
proof
  let m,k,n be Nat;
  hereby
    reconsider a19=1 as Integer;
    reconsider k9=k as Integer;
    assume that
A1: m+1<=k and
A2: k<=n;
    1<=m+1 by NAT_1:11;
    then reconsider i=k9-a19 as Element of NAT by A1,INT_1:18,XXREAL_0:2;
    take i;
    m+1-1<=k-1 by A1,XREAL_1:11;
    hence m<=i;
    k<k+1 by NAT_1:13;
    then k-1<k+1-1 by XREAL_1:11;
    hence i<n by A2,XXREAL_0:2;
    thus k=i+1;
  end;
  given i being Element of NAT such that
A3: m<=i and
A4: i<n and
A5: k=i+1;
  thus thesis by A3,A4,A5,NAT_1:13,XREAL_1:9;
end;

theorem Th2:
  q = p|Seg n implies len q<=len p & for i st 1<=i & i<=len q holds p.i = q.i
proof
  assume
A1: q = p|Seg n;
  per cases;
  suppose
A2: n<=len p;
    hence len q<=len p by A1,FINSEQ_1:21;
    let i be Element of NAT;
    assume that
A3: 1<=i and
A4: i<=len q;
    len q = n by A1,A2,FINSEQ_1:21;
    then i in Seg n by A3,A4;
    hence thesis by A1,FUNCT_1:72;
  end;
  suppose
A5: len p<=n;
    hence len q<=len p by A1,FINSEQ_2:23;
    let i be Element of NAT;
    assume that
    1<=i and
    i<=len q;
    thus thesis by A1,A5,FINSEQ_2:23;
  end;
end;

theorem Th3:
  X c= Seg k & Y c= dom Sgm X implies (Sgm X)*(Sgm Y) = Sgm rng (( Sgm X)|Y)
proof
  assume that
A1: X c= Seg k and
A2: Y c= dom Sgm X;
  reconsider Y9 = Y as finite set by A2;
  set M = (Sgm X)*(Sgm Y);
  set R = rng ((Sgm X)|Y);
A3: Y c= Seg len Sgm X by A2,FINSEQ_1:def 3;
  then
A4: rng Sgm Y = Y by FINSEQ_1:def 13;
  dom Sgm Y9 = Seg card Y9 by A3,FINSEQ_3:45;
  then
A5: dom M = Seg card Y9 by A2,A4,RELAT_1:46;
A6: rng Sgm X c= Seg k by A1,FINSEQ_1:def 13;
  then
A7: rng Sgm X c= NAT by XBOOLE_1:1;
  reconsider M as FinSequence by A5,FINSEQ_1:def 2;
  rng M c= rng Sgm X by RELAT_1:45;
  then rng M c= NAT by A7,XBOOLE_1:1;
  then reconsider L = (Sgm X)*(Sgm Y) as FinSequence of NAT by FINSEQ_1:def 4;
  now
    let y be set;
    hereby
      assume y in rng L;
      then consider x such that
A8:   x in dom L and
A9:   y = L.x by FUNCT_1:def 5;
      x in dom Sgm Y by A8,FUNCT_1:21;
      then
A10:  (Sgm Y).x in Y by A4,FUNCT_1:def 5;
      y = (Sgm X).((Sgm Y).x) by A8,A9,FUNCT_1:22;
      hence y in R by A2,A10,FUNCT_1:73;
    end;
    assume y in R;
    then consider x such that
A11: x in dom ((Sgm X)|Y) and
A12: y = ((Sgm X)|Y).x by FUNCT_1:def 5;
A13: x in (dom Sgm X)/\Y by A11,RELAT_1:90;
    then
A14: x in Y by XBOOLE_0:def 4;
    then consider z being set such that
A15: z in dom Sgm Y and
A16: x = (Sgm Y).z by A4,FUNCT_1:def 5;
    x in dom Sgm X by A13,XBOOLE_0:def 4;
    then
A17: z in dom((Sgm X)*(Sgm Y)) by A15,A16,FUNCT_1:21;
    y = (Sgm X).x by A12,A14,FUNCT_1:72;
    then L.z = y by A16,A17,FUNCT_1:22;
    hence y in rng L by A17,FUNCT_1:def 5;
  end;
  then
A18: rng L = R by TARSKI:2;
A19: now
    let l,m,k1,k2 be natural number;
    assume that
A20: 1 <= l and
A21: l < m and
A22: m <= len L and
A23: k1=L.l and
A24: k2=L.m;
    l <= len L by A21,A22,XXREAL_0:2;
    then
A25: l in dom L by A20,FINSEQ_3:27;
    then
A26: L.l = (Sgm X).((Sgm Y).l) by FUNCT_1:22;
    1<=m by A20,A21,XXREAL_0:2;
    then
A27: m in dom L by A22,FINSEQ_3:27;
    then
A28: L.m = (Sgm X).((Sgm Y).m) by FUNCT_1:22;
A29: (Sgm Y).l in dom Sgm X by A25,FUNCT_1:21;
    m in dom Sgm Y by A27,FUNCT_1:21;
    then
A30: m<=len Sgm Y by FINSEQ_3:27;
A31: (Sgm Y).m in dom Sgm X by A27,FUNCT_1:21;
    reconsider l, m as Element of NAT by ORDINAL1:def 13;
    reconsider K1 = (Sgm Y).l, K2 = (Sgm Y).m as Element of NAT;
A32: 1<=K1 by A29,FINSEQ_3:27;
A33: K2<=len Sgm X by A31,FINSEQ_3:27;
    K1 < K2 by A3,A20,A21,A30,FINSEQ_1:def 13;
    hence k1 < k2 by A1,A23,A24,A26,A28,A32,A33,FINSEQ_1:def 13;
  end;
  R c= rng Sgm X by RELAT_1:99;
  then R c= Seg k by A6,XBOOLE_1:1;
  hence thesis by A18,A19,FINSEQ_1:def 13;
end;

Lm1: for m, n being Element of NAT, F being finite set st F = {k: m<=k & k<=m+
n} holds card F = n+1
proof
  let m be Element of NAT;
  defpred P[Element of NAT] means for F being finite set st F = {k: m<=k & k<=
  m+$1} holds card F = $1+1;
A1: now
    let n be Element of NAT;
    assume
A2: P[n];
    thus P[n+1]
    proof
      reconsider F2 = {k: k<=m+n} as finite set by CQC_THE1:12;
      set F1 = {k: m<=k & k<=m+n};
      F1 c= F2
      proof
        let x be set;
        assume x in F1;
        then ex k being Element of NAT st x=k & m<=k & k<=m+n;
        hence thesis;
      end;
      then reconsider F1 as finite set;
      let F be finite set;
A3:   not m+(n+1) in {k: m<=k & k<=m+n}
      proof
        assume not thesis;
        then ex k st m+(n+1) = k & m<=k & k<=m+n;
        then m+n+1<=m+n+0;
        hence contradiction by XREAL_1:8;
      end;
      assume
A4:   F = {k: m<=k & k<=m+(n+1)};
      now
        let x be set;
        hereby
          assume x in F;
          then consider k such that
A5:       x=k and
A6:       m<=k and
A7:       k<=m+(n+1) by A4;
          k<=m+n+1 by A7;
          then k<=m+n or k=m+(n+1) by NAT_1:8;
          then k in F1 or k in {m+(n+1)} by A6,TARSKI:def 1;
          hence x in F1 \/ {m+(n+1)} by A5,XBOOLE_0:def 3;
        end;
        assume
A8:     x in F1 \/ {m+(n+1)};
        per cases by A8,XBOOLE_0:def 3;
        suppose
          x in F1;
          then consider k such that
A9:       x=k and
A10:      m<=k and
A11:      k<=m+n;
          k<=m+n+1 by A11,NAT_1:12;
          hence x in F by A4,A9,A10;
        end;
        suppose
A12:      x in {m+(n+1)};
          then reconsider k = x as Element of NAT by TARSKI:def 1;
A13:      x=m+(n+1) by A12,TARSKI:def 1;
          then m<=k by NAT_1:11;
          hence x in F by A4,A13;
        end;
      end;
      then
A14:  F = {k: m<=k & k<=m+n} \/ {m+(n+1)} by TARSKI:2;
      card F1 = n+1 by A2;
      hence thesis by A14,A3,CARD_2:54;
    end;
  end;
A15: P[0]
  proof
    let F be finite set;
    assume
A16: F = {k: m<=k & k<=m+0};
    now
      let x be set;
      hereby
        assume x in F;
        then ex k being Element of NAT st x = k & m<=k & k<=m+0 by A16;
        hence x = m by XXREAL_0:1;
      end;
      assume x = m;
      hence x in F by A16;
    end;
    then F = {m} by TARSKI:def 1;
    hence thesis by CARD_1:50;
  end;
  for n be Element of NAT holds P[n] from NAT_1:sch 1 (A15, A1);
  hence thesis;
end;

theorem Th4:
  for m, n being Element of NAT holds card {k: m<=k & k<=m+n} = n+1
proof
  deffunc F(Element of NAT) = $1;
  defpred P[Element of NAT] means not contradiction;
  let m, n be Element of NAT;
  set F = {F(k): m<=k & k<=m+n & P[k]};
  F is finite from FINSEQ_1:sch 6;
  then reconsider F as finite set;
  F = {F(k): m<=k & k<=m+n}
  proof
    thus F c= {F(k): m<=k & k<=m+n}
    proof
      let x be set;
      assume x in F;
      then
      ex k1 being Element of NAT st x = F(k1) & m <= k1 & k1 <= m+n;
      hence thesis;
    end;
    let x be set;
    assume x in {F(k): m<=k & k<=m+n};
    then ex k1 being Element of NAT st x = F(k1) & m <= k1 & k1 <= m+n;
    hence thesis;
  end;
  then reconsider G = {F(k): m<=k & k<=m+n} as finite set;
  card G = n+1 by Lm1;
  hence thesis;
end;

theorem Th5:
  for l st 1<=l & l<=n holds Sgm {kk where kk is Element of NAT: m+
  1<=kk & kk<=m+n}.l = m+l
proof
  set F= Sgm {kk where kk is Element of NAT: m+1<=kk & kk<=m+n};
  defpred P[Nat] means 1<=$1 & $1<=n & F.$1 <> m+$1;
  set rF = {kk where kk is Element of NAT: m+1<=kk & kk<=m+n};
A1: rF c= Seg (m+n)
  proof
    let x be set;
    assume x in rF;
    then consider x9 being Element of NAT such that
A2: x9=x and
A3: m+1<=x9 and
A4: x9<=m+n;
    1<=m+1 by NAT_1:11;
    then 1<=x9 by A3,XXREAL_0:2;
    hence thesis by A2,A4;
  end;
  then reconsider rF as finite set;
  assume ex l st P[l];
  then
A5: ex l be Nat st P[l];
  consider k be Nat such that
A6: P[k] and
A7: for nn being Nat st P[nn] holds k <= nn from NAT_1:sch 5 (A5);
  reconsider k as Element of NAT by ORDINAL1:def 13;
A8: m+k<=m+n by A6,XREAL_1:9;
  reconsider Fk = F.k as Element of NAT;
A9: card rF = n
  proof
    per cases;
    suppose
A10:  n = 0;
      rF = {}
      proof
        assume rF <> {};
        then consider x being set such that
A11:    x in rF by XBOOLE_0:def 1;
        ex x9 being Element of NAT st x9=x & m+1<=x9 & x9<=m+n by A11;
        then m+1<=m+0 by A10,XXREAL_0:2;
        hence contradiction by XREAL_1:8;
      end;
      hence thesis by A10;
    end;
    suppose
      0<n;
      then 0+1<=n by NAT_1:13;
      then 1-1<=n-1 by XREAL_1:11;
      then n-'1 = n-1 by XREAL_0:def 2;
      then reconsider n1 = n-1 as Element of NAT;
      (m+1)+n1 = m+n;
      then card rF = n1+1 by Th4;
      hence thesis;
    end;
  end;
  then
A12: len Sgm rF = n by A1,FINSEQ_3:44;
A13: rng F = rF by A1,FINSEQ_1:def 13;
  now
    defpred P[Nat] means for Fk be Element of NAT st 1<=$1 & $1<=n & Fk = F.$1
    holds m+$1<=Fk;
A14: for k st P[k] holds P[k+1]
    proof
      let k be Element of NAT;
      assume
A15:  P[k];
A16:  k<k+1 by NAT_1:13;
      let Fk1 be Element of NAT;
      assume that
A17:  1<=k+1 and
A18:  k+1<=n and
A19:  Fk1 = F.(k+1);
      per cases by A17,XXREAL_0:1;
      suppose
A20:    1<k+1;
        reconsider Fk = F.k as Element of NAT;
A21:    1<=k by A20,NAT_1:13;
        k<=n by A18,NAT_1:13;
        then
A22:    m+k<=Fk by A15,A21;
        Fk < Fk1 by A1,A12,A18,A19,A16,A21,FINSEQ_1:def 13;
        then m+k<Fk1 by A22,XXREAL_0:2;
        then m+k+1<=Fk1 by NAT_1:13;
        hence thesis;
      end;
      suppose
A23:    1=k+1;
        then 1 in dom F by A12,A18,FINSEQ_3:27;
        then F.1 in rng F by FUNCT_1:def 5;
        then ex F1 being Element of NAT st F1 = F.1 & m+1<=F1 & F1<=m +n
        by A13;
        hence thesis by A19,A23;
      end;
    end;
A24: P[0];
    thus for k holds P[k] from NAT_1:sch 1(A24,A14);
  end;
  then
A25: m+k<=Fk by A6;
  m+1<=m+k by A6,XREAL_1:9;
  then m+k in rF by A8;
  then m+k in rng Sgm rF by A1,FINSEQ_1:def 13;
  then consider x such that
A26: x in dom Sgm rF and
A27: m+k = (Sgm rF).x by FUNCT_1:def 5;
  reconsider x as Element of NAT by A26;
A28: dom Sgm rF = Seg n by A1,A9,FINSEQ_3:45;
  then
A29: 1<=x by A26,FINSEQ_1:3;
A30: x<=n by A28,A26,FINSEQ_1:3;
  per cases by A6,A27,XXREAL_0:1;
  suppose
    k < x;
    hence contradiction by A6,A1,A12,A27,A25,A30,FINSEQ_1:def 13;
  end;
  suppose
A31: k > x;
    F.x <> m+x by A6,A27,XCMPLX_1:2;
    hence contradiction by A7,A29,A30,A31;
  end;
end;

begin :: The cut operation

definition
  let p be FinSequence, m, n be Nat;
  func (m, n)-cut p -> FinSequence means
  :Def1:
  len it +m = n+1 & for i being
Nat st i<len it holds it.(i+1)=p.(m+i) if 1<=m & m<=n & n<=len p otherwise it =
  {};
  consistency;
  existence
  proof
    hereby
      deffunc F(Nat) = p.(m+($1-1));
      assume that
      1<=m and
A1:   m<=n and
      n<=len p;
      consider s being FinSequence such that
A2:   len s = n+1-'m & for k being Nat st k in dom s holds s.k=F(k)
      from FINSEQ_1:sch 2;
      take s;
      n <= n+1 by XREAL_1:31;
      then m <= n+1 by A1,XXREAL_0:2;
      then m-m <= n+1-m by XREAL_1:11;
      then len s +m = n+1-m+m by A2,XREAL_0:def 2;
      hence len s +m = n+1;
      let k be Nat;
      assume k<len s;
      then
A3:   k+1<=len s by NAT_1:13;
      0+1<=k+1 by XREAL_1:9;
      then k+1 in dom s by A3,FINSEQ_3:27;
      hence s.(k+1) = p.(m+(k+1-1)) by A2
        .= p.(m+k);
    end;
    thus thesis;
  end;
  uniqueness
  proof
    let s1, s2 be FinSequence;
    hereby
      assume that
      1<=m and
      m<=n and
      n<=len p;
      assume that
A4:   len s1 +m = n+1 and
A5:   for i being Nat st i<len s1 holds s1.(i+1) = p.(m+i);
      assume that
A6:   len s2 +m = n+1 and
A7:   for i being Nat st i<len s2 holds s2.(i+1) = p.(m+i);
      now
        thus len s1 = len s1;
        thus len s2 = len s1 by A4,A6;
        let i be Nat;
        assume
A8:     i in dom s1;
A9:     dom s1 = Seg len s1 by FINSEQ_1:def 3;
        then
A10:    i<=len s1 by A8,FINSEQ_1:3;
        0+1<=i by A9,A8,FINSEQ_1:3;
        then consider k such that
        0<=k and
A11:    k<len s1 and
A12:    i=k+1 by A10,Th1;
        thus s1.i = p.(m+k) by A5,A11,A12
          .= s2.i by A4,A6,A7,A11,A12;
      end;
      hence s1 = s2 by FINSEQ_2:10;
    end;
    thus thesis;
  end;
end;

Lm2: for p be FinSequence, m, n be Element of NAT st 1<=m & m<=n+1 & n<=len p
holds len (m, n)-cut p +m = n+1 & for i being Element of NAT st i<len (m, n)
-cut p holds ((m, n)-cut p).(i+1)=p.(m+i)
proof
  let p be FinSequence, m, n be Element of NAT such that
A1: 1<=m and
A2: m<=n+1 and
A3: n<=len p;
A4: m = n+1 or m < n+1 by A2,XXREAL_0:1;
  per cases by A4,NAT_1:13;
  suppose
A5: m = n+1;
    then
A6: n < m by XREAL_1:31;
    then (m, n)-cut p = {} by Def1;
    hence len (m, n)-cut p +m = n+1 by A5,CARD_1:47;
    thus thesis by A6,Def1,CARD_1:47;
  end;
  suppose
    m<=n;
    hence thesis by A1,A3,Def1;
  end;
end;

theorem Th6:
  1<=m & m<=len p implies (m,m)-cut p = <*p.m*>
proof
  assume that
A1: 1<=m and
A2: m<=len p;
  set mp = (m,m)-cut p;
A3: len mp + m = m + 1 by A1,A2,Def1;
  then mp.(0+1) = p.(m+0) by A1,A2,Def1
    .= p.m;
  hence thesis by A3,FINSEQ_1:57;
end;

theorem Th7:
  (1, len p)-cut p = p
proof
  set cp = (1, len p)-cut p;
  now
A1: 1<=len p +1 by NAT_1:11;
    then
A2: len cp + 1 = len p + 1 by Lm2;
    hence len cp = len p;
    thus len p = len p;
    let i be Nat;
    assume
A3: i in dom cp;
A4: dom cp = Seg len p by A2,FINSEQ_1:def 3;
    then
A5: i<=len p by A3,FINSEQ_1:3;
    0+1<=i by A4,A3,FINSEQ_1:3;
    then ex k being Element of NAT st 0<=k & k<len cp & i=k+1 by A2,
    A5,
    Th1;
    hence cp.i = p.i by A1,Lm2;
  end;
  hence thesis by FINSEQ_2:10;
end;

theorem Th8:
  m<=n & n<=r & r<=len p implies (m+1, n)-cut p ^ (n+1, r)-cut p =
  (m+1, r)-cut p
proof
  assume that
A1: m<=n and
A2: n<=r and
A3: r<=len p;
  set p3 = (m+1, r)-cut p;
  set p2 = (n+1, r)-cut p;
  set p1 = (m+1, n)-cut p;
  set p12 = p1^p2;
  now
    reconsider n9=n as Integer;
    reconsider m9=m as Integer;
    thus len p12 = len p12;
A4: dom p12 = Seg len p12 by FINSEQ_1:def 3;
    reconsider nm=n9-m9 as Element of NAT by A1,INT_1:18;
A5: 1<=m+1 by NAT_1:11;
    m<=r by A1,A2,XXREAL_0:2;
    then
A6: m+1<=r+1 by XREAL_1:8;
A7: m+1<=n+1 by A1,XREAL_1:8;
A8: n+1<=r+1 by A2,XREAL_1:8;
    then m+1<=r+1 by A7,XXREAL_0:2;
    then
A9: len p3 +(m+1)=r+1 by A3,A5,Lm2;
A10: n<=len p by A2,A3,XXREAL_0:2;
    then
A11: len p1 +(m+1) = n+1 by A5,A7,Lm2;
A12: 1<=n+1 by NAT_1:11;
    then len p2 +(n+1) = r+1 by A3,A8,Lm2;
    then
A13: len p12= n-m+(r+ -n) by A11,FINSEQ_1:35
      .= r-m;
    hence len p3 = len p12 by A9;
    let i be Nat;
    assume
A14: i in dom p12;
    then
A15: 1<=i by A4,FINSEQ_1:3;
A16: i<=len p12 by A4,A14,FINSEQ_1:3;
    per cases by NAT_1:13;
    suppose
A17:  i<=nm;
      0+1=1;
      then consider k being Element of NAT such that
      0<=k and
A18:  k<nm and
A19:  i=k+1 by A15,A17,Th1;
      nm<=r-m by A2,XREAL_1:11;
      then
A20:  k<len p3 by A9,A18,XXREAL_0:2;
      i in dom p1 by A11,A15,A17,FINSEQ_3:27;
      hence p12.i = p1.i by FINSEQ_1:def 7
        .= p.((m+1)+k) by A5,A7,A10,A11,A18,A19,Lm2
        .= p3.i by A3,A5,A6,A19,A20,Lm2;
    end;
    suppose
      nm+1<=i;
      then consider k such that
A21:  nm<=k and
A22:  k<len p12 and
A23:  i=k+1 by A16,Th1;
      reconsider k99=k as Integer;
      reconsider k9=k99-nm as Element of NAT by A21,INT_1:18;
A24:  1<=k9+1 by NAT_1:11;
      len p12 = len p2 + len p1 by FINSEQ_1:35;
      then len p2 = len p12 - len p1;
      then
A25:  k9<len p2 by A11,A22,XREAL_1:11;
      then k9+1<=len p2 by NAT_1:13;
      then
A26:  k9+1 in dom p2 by A24,FINSEQ_3:27;
A27:  n+1+k9 = m+1+k;
      i = nm+(k9+1) by A23;
      hence p12.i = p2.(k9+1) by A11,A26,FINSEQ_1:def 7
        .= p.(n+1+k9) by A3,A12,A8,A25,Lm2
        .= p3.i by A3,A5,A6,A13,A9,A22,A23,A27,Lm2;
    end;
  end;
  hence thesis by FINSEQ_2:10;
end;

theorem
  m<=len p implies (1, m)-cut p ^ (m+1, len p)-cut p = p
proof
  set cp1 = (1, m)-cut p;
  set cpm = (m+1, len p)-cut p;
A1: 0+1=1;
  assume m<=len p;
  hence cp1 ^ cpm = (1, len p)-cut p by A1,Th8
    .= p by Th7;
end;

theorem
  m<=n & n<=len p implies (1, m)-cut p ^ (m+1, n)-cut p ^ (n+1, len p)
  -cut p = p
proof
  assume that
A1: m<=n and
A2: n<=len p;
  set cp3 = (n+1, len p)-cut p;
  set cp2 = (m+1, n)-cut p;
  set cp1 = (1, m)-cut p;
A3: 0+1=1;
  hence cp1^cp2^cp3 = (1, n)-cut p ^cp3 by A1,A2,Th8
    .= (1, len p)-cut p by A2,A3,Th8
    .= p by Th7;
end;

theorem Th11:
  rng ((m,n)-cut p) c= rng p
proof
  set c = ((m,n)-cut p);
A1: now
    assume that
A2: 1<=m and
A3: m<=n and
A4: n<=len p;
    n <= n+1 by NAT_1:11;
    then
A5: m <= n+1 by A3,XXREAL_0:2;
    thus thesis
    proof
      let x be set;
      assume x in rng c;
      then consider z being set such that
A6:   z in dom c and
A7:   x = c.z by FUNCT_1:def 5;
      reconsider z as Element of NAT by A6;
A8:   z<=len c by A6,FINSEQ_3:27;
      0+1<=z by A6,FINSEQ_3:27;
      then consider i such that
      0<=i and
A9:   i<len c and
A10:  z=i+1 by A8,Th1;
      m+i<len c +m by A9,XREAL_1:8;
      then m+i<n+1 by A2,A4,A5,Lm2;
      then m+i<=n by NAT_1:13;
      then
A11:  m+i<=len p by A4,XXREAL_0:2;
      1<=m+i by A2,NAT_1:12;
      then
A12:  m+i in dom p by A11,FINSEQ_3:27;
      c.z = p.(m+i) by A2,A4,A5,A9,A10,Lm2;
      hence thesis by A7,A12,FUNCT_1:def 5;
    end;
  end;
  now
    assume not (1<=m & m<=n & n<=len p);
    then c = {} by Def1;
    then rng c = {};
    hence thesis by XBOOLE_1:2;
  end;
  hence thesis by A1;
end;

definition
  let D be set, p be FinSequence of D, m, n be Nat;
  redefine func (m, n)-cut p -> FinSequence of D;
  coherence
  proof
A1: rng p c= D by FINSEQ_1:def 4;
A2: n is Element of NAT by ORDINAL1:def 13;
    m is Element of NAT by ORDINAL1:def 13;
    then rng (m,n)-cut p c= rng p by A2,Th11;
    then rng (m,n)-cut p c= D by A1,XBOOLE_1:1;
    hence thesis by FINSEQ_1:def 4;
  end;
end;

theorem Th12:
  1<=m & m<=n & n<=len p implies ((m,n)-cut p).1 = p.m & ((m,n)
  -cut p).len ((m,n)-cut p)=p.n
proof
  set c = ((m,n)-cut p);
  assume that
A1: 1<=m and
A2: m<=n and
A3: n<=len p;
A4: m<=n+1 by A2,NAT_1:12;
  then
A5: len c +m = n+1 by A1,A3,Lm2;
A6: now
    assume len c = 0;
    then n+1<=n+0 by A2,A5;
    hence contradiction by XREAL_1:8;
  end;
  then 0+1<=len c by NAT_1:13;
  then consider i being Element of NAT such that
  0<=i and
A7: i<len c and
A8: len c =i+1 by Th1;
  0+1=1;
  hence c.1 = p.(m+0) by A1,A3,A4,A6,Lm2
    .= p.m;
  m+i = n by A5,A8;
  hence thesis by A1,A3,A4,A7,A8,Lm2;
end;

begin  :: The glueing catenation

definition
  let p, q be FinSequence;
  func p ^' q -> FinSequence equals
  p^(2, len q)-cut q;
  correctness;
end;

theorem Th13:
  q<>{} implies len (p^'q) +1 = len p + len q
proof
  set r = (p^'q);
  set qc = (2,len q)-cut q;
  assume q<>{};
  then 0+1<=len q by NAT_1:13;
  then 1+1<=len q +1 by XREAL_1:9;
  then
A1: len qc +(1+1) = len q + 1 by Lm2;
  thus len r +1 = len p + len qc + 1 by FINSEQ_1:35
    .= len p + len q by A1;
end;

theorem Th14:
  1<=k & k<=len p implies (p^'q).k=p.k
proof
  assume that
A1: 1<=k and
A2: k<=len p;
  k in dom p by A1,A2,FINSEQ_3:27;
  hence thesis by FINSEQ_1:def 7;
end;

theorem Th15:
  1<=k & k<len q implies (p^'q).(len p +k) = q.(k+1)
proof
  set qc = (2,len q)-cut q;
  assume that
A1: 1<=k and
A2: k<len q;
  per cases;
  suppose
    q={};
    hence thesis by A2;
  end;
  suppose
    q<>{};
    then 0+1<=len q by NAT_1:13;
    then
A3: 1+1<=len q +1 by XREAL_1:9;
    then len qc +(1+1) = len q + 1 by Lm2;
    then len qc +1+1 = len q + 1;
    then
A4: k<=len qc by A2,NAT_1:13;
    0+1<=k by A1;
    then consider i being Element of NAT such that
    0<=i and
A5: i<len qc and
A6: k=i+1 by A4,Th1;
    k in dom qc by A1,A4,FINSEQ_3:27;
    hence (p^'q).(len p +k) = qc.k by FINSEQ_1:def 7
      .= q.(1+1+i) by A3,A5,A6,Lm2
      .= q.(k+1) by A6;
  end;
end;

theorem Th16:
  1<len q implies (p^'q).len (p^'q) = q.len q
proof
  set r = p^'q;
  set qc = (2,len q)-cut q;
  assume
A1: 1<len q;
  then 1+1<=len q by NAT_1:13;
  then
A2: 1+1-1<=len q -1 by XREAL_1:11;
  q<>{} by A1;
  then len r +1-1=len p +len q -1 by Th13;
  then
A3: len r = len p +(len q -1);
  1+1<=len q +1 by A1,XREAL_1:9;
  then len qc +(1+1) = len q + 1 by Lm2;
  then
A4: len qc +1+1 = len q + 1;
  then len qc < len q by NAT_1:13;
  hence thesis by A3,A4,A2,Th15;
end;

theorem Th17:
  rng (p^'q) c= rng p \/ rng q
proof
  set r = p^'q;
  set qc = (2,len q)-cut q;
  let x be set;
  assume x in rng r;
  then x in rng p \/ rng qc by FINSEQ_1:44;
  then
A1: x in rng p or x in rng qc by XBOOLE_0:def 3;
  rng qc c= rng q by Th11;
  hence thesis by A1,XBOOLE_0:def 3;
end;

definition
  let D be set, p, q be FinSequence of D;
  redefine func p^'q -> FinSequence of D;
  coherence
  proof
A1: rng (p^'q) c= rng p \/ rng q by Th17;
A2: rng q c= D by FINSEQ_1:def 4;
    rng p c= D by FINSEQ_1:def 4;
    then rng p \/ rng q c= D by A2,XBOOLE_1:8;
    then rng (p^'q) c= D by A1,XBOOLE_1:1;
    hence thesis by FINSEQ_1:def 4;
  end;
end;

theorem
  p<>{} & q<>{} & p.len p = q.1 implies rng (p^'q) = rng p \/ rng q
proof
  set r = p^'q;
  set qc = (2,len q)-cut q;
  assume that
A1: p<>{} and
A2: q<>{} and
A3: p.len p = q.1;
  0+1<=len q by A2,NAT_1:13;
  then
A4: 1+1<=len q +1 by XREAL_1:9;
  then len qc +(1+1) = len q + 1 by Lm2;
  then
A5: len qc +1+1 = len q + 1;
  now
    let x be set;
    hereby
      assume x in rng r;
      then x in rng p \/ rng qc by FINSEQ_1:44;
      then
A6:   x in rng p or x in rng qc by XBOOLE_0:def 3;
      rng qc c= rng q by Th11;
      hence x in rng p \/ rng q by A6,XBOOLE_0:def 3;
    end;
    assume x in rng p \/ rng q;
    then
A7: x in rng p or x in rng q by XBOOLE_0:def 3;
    assume not x in rng r;
    then
A8: not x in rng p \/ rng qc by FINSEQ_1:44;
    then consider y being set such that
A9: y in dom q and
A10: x=q.y by A7,FUNCT_1:def 5,XBOOLE_0:def 3;
A11: not x in rng p by A8,XBOOLE_0:def 3;
    reconsider y as Element of NAT by A9;
A12: 1<=y by A9,FINSEQ_3:27;
A13: y<=len q by A9,FINSEQ_3:27;
A14: not x in rng qc by A8,XBOOLE_0:def 3;
    per cases by A12,XXREAL_0:1;
    suppose
A15:  y=1;
      0+1<=len p by A1,NAT_1:13;
      then len p in dom p by FINSEQ_3:27;
      hence contradiction by A3,A11,A10,A15,FUNCT_1:def 5;
    end;
    suppose
      1<y;
      then 1+1<=y by NAT_1:13;
      then consider i such that
A16:  1<=i and
A17:  i<len q and
A18:  y=i+1 by A13,Th1;
A19:  i<=len qc by A5,A17,NAT_1:13;
      then
A20:  i in dom qc by A16,FINSEQ_3:27;
      0+1<=i by A16;
      then consider j such that
      0<=j and
A21:  j<len qc and
A22:  i=j+1 by A19,Th1;
      qc.(j+1) = q.(1+1+j) by A4,A21,Lm2
        .= q.y by A18,A22;
      hence contradiction by A14,A10,A22,A20,FUNCT_1:def 5;
    end;
  end;
  hence thesis by TARSKI:2;
end;

begin :: Two valued alternating finsequences

definition
  let f be FinSequence;
  attr f is TwoValued means
  :Def3:
  card rng f = 2;
end;

Lm3: now
  set p = <*1, 2*>;
  2 > 1;
  hence len p > 1 by FINSEQ_1:61;
  thus 1 <> 2;
  thus rng p = rng<*1*>\/ rng<*2*> by FINSEQ_1:44
    .= {1} \/ rng <*2*> by FINSEQ_1:55
    .= {1} \/ {2} by FINSEQ_1:55
    .= {1, 2} by ENUMSET1:41;
end;

theorem Th19:
  p is TwoValued iff len p >1 & ex x,y being set st x<>y & rng p = {x, y}
proof
  hereby
    assume p is TwoValued;
    then card rng p = 2 by Def3;
    then consider x,y being set such that
A1: x<>y and
A2: rng p={x,y} by CARD_2:79;
    thus len p >1
    proof
      set l=len p;
      assume
A3:   len p<=1;
      per cases by A3,NAT_1:26;
      suppose
        l=0;
        then p={};
        hence contradiction by A2;
      end;
      suppose
A4:     l=1;
        then 1 in dom p by FINSEQ_3:27;
        then consider z being set such that
A5:     [1,z] in p by RELAT_1:def 4;
        z=p.1 by A5,FUNCT_1:8;
        then p=<*z*> by A4,FINSEQ_1:57;
        then
A6:     rng p = {z} by FINSEQ_1:56;
        then z=x by A2,ZFMISC_1:8;
        hence contradiction by A1,A2,A6,ZFMISC_1:8;
      end;
    end;
    thus ex x,y being set st x<>y & rng p ={x, y} by A1,A2;
  end;
  assume len p >1;
  given x, y being set such that
A7: x<>y and
A8: rng p = {x, y};
  card rng p = 2 by A7,A8,CARD_2:76;
  hence thesis by Def3;
end;
then

Lm4: <*1, 2*> is TwoValued by Lm3;

Lm5: now
  let i be Nat;
  set p = <*1, 2*>;
  assume that
A1: 1<=i and
A2: (i+1)<=len p;
  i+1<=1+1 by A2,FINSEQ_1:61;
  then i<=1 by XREAL_1:8;
  then
A3: i = 1 by A1,XXREAL_0:1;
  then p.i = 1 by FINSEQ_1:61;
  hence p.i <> p.(i+1) by A3,FINSEQ_1:61;
end;

definition
  let f be FinSequence;
  attr f is Alternating means
  :Def4:
  for i being Nat st 1<=i & (i+1)<=len f holds f.i <> f.(i+1);
end;

Lm6: <*1, 2*> is Alternating by Def4,Lm5;

registration
  cluster TwoValued Alternating FinSequence;
  existence by Lm4,Lm6;
end;

reserve a, a1, a2 for TwoValued Alternating FinSequence;

theorem Th20:
  len a1 = len a2 & rng a1 = rng a2 & a1.1 = a2.1 implies a1 = a2
proof
  assume that
A1: len a1 = len a2 and
A2: rng a1 = rng a2 and
A3: a1.1 = a2.1;
  defpred P[Nat] means 1<=$1 & $1<=len a1 implies a1.$1=a2.$1;
A4: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat;
    assume that
A5: 1<=k & k<=len a1 implies a1.k = a2.k;
A6: 0=k or 0<k & 0+1=1;
    assume that
A7: 1<=k+1 and
A8: k+1<=len a1;
A9: k+1 in dom a1 by A7,A8,FINSEQ_3:27;
    per cases by A6,NAT_1:13;
    suppose
      0 = k;
      hence thesis by A3;
    end;
    suppose
A10:  1<=k;
A11:  dom a2 = Seg len a2 by FINSEQ_1:def 3;
      consider X, Y being set such that
      X <> Y and
A12:  rng a1 = { X, Y } by Th19;
      a1.(k+1) in rng a1 by A9,FUNCT_1:def 5;
      then
A13:  a1.(k+1)=X or a1.(k+1)= Y by A12,TARSKI:def 2;
      dom a1 = Seg len a1 by FINSEQ_1:def 3;
      then a2.(k+1) in rng a2 by A1,A9,A11,FUNCT_1:def 5;
      then
A14:  a2.(k+1) = X or a2.(k+1) = Y by A2,A12,TARSKI:def 2;
      k<=len a1 by A8,NAT_1:13;
      then k in dom a1 by A10,FINSEQ_3:27;
      then a1.k in rng a1 by FUNCT_1:def 5;
      then a1.k=X or a1.k=Y by A12,TARSKI:def 2;
      hence thesis by A1,A5,A8,A10,A13,A14,Def4,NAT_1:13;
    end;
  end;
A15: P[0];
  for i being Nat holds P[i] from NAT_1:sch 2(A15, A4);
  hence thesis by A1,FINSEQ_1:18;
end;

theorem Th21:
  a1<>a2 & len a1 = len a2 & rng a1 = rng a2 implies for i st 1<=i
  & i<=len a1 holds a1.i<>a2.i
proof
  assume that
A1: a1 <> a2 and
A2: len a1 = len a2 and
A3: rng a1 = rng a2;
  defpred P[Element of NAT] means 1<=$1 & $1<=len a1 implies a1.$1<>a2.$1;
A4: for k being Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT;
    assume that
A5: 1<=k & k<=len a1 implies a1.k <> a2.k;
A6: 0=k or 0<k & 0+1=1;
    assume that
A7: 1<=k+1 and
A8: k+1<=len a1;
A9: k+1 in dom a1 by A7,A8,FINSEQ_3:27;
    per cases by A6,NAT_1:13;
    suppose
      0 = k;
      hence thesis by A1,A2,A3,Th20;
    end;
    suppose
A10:  1<=k;
      k<=len a1 by A8,NAT_1:13;
      then
A11:  k in dom a1 by A10,FINSEQ_3:27;
A12:  dom a2 = Seg len a2 by FINSEQ_1:def 3;
      consider X, Y being set such that
      X <> Y and
A13:  rng a1 = { X, Y } by Th19;
      a1.(k+1) in rng a1 by A9,FUNCT_1:def 5;
      then
A14:  a1.(k+1)=X or a1.(k+1)= Y by A13,TARSKI:def 2;
      dom a1 = Seg len a1 by FINSEQ_1:def 3;
      then a2.k in rng a2 by A2,A11,A12,FUNCT_1:def 5;
      then
A15:  a2.k=X or a2.k=Y by A3,A13,TARSKI:def 2;
      a1.k in rng a1 by A11,FUNCT_1:def 5;
      then a1.k=X or a1.k=Y by A13,TARSKI:def 2;
      hence thesis by A2,A5,A8,A10,A15,A14,Def4,NAT_1:13;
    end;
  end;
A16: P[0];
  thus for i holds P[i] from NAT_1:sch 1 (A16, A4);
end;

theorem Th22:
  a1<>a2 & len a1 = len a2 & rng a1 = rng a2 implies for a st len
  a=len a1 & rng a=rng a1 holds a=a1 or a=a2
proof
  assume that
A1: a1 <> a2 and
A2: len a1 = len a2 and
A3: rng a1 = rng a2;
  let a;
  assume that
A4: len a = len a1 and
A5: rng a = rng a1;
  assume
A6: a <> a1;
  now
    let i be Nat;
    assume that
A7: 1<=i and
A8: i<=len a;
    consider X, Y being set such that
    X <> Y and
A9: rng a = { X, Y } by Th19;
A10: i in dom a by A7,A8,FINSEQ_3:27;
    then a.i in rng a by FUNCT_1:def 5;
    then
A11: a.i=X or a.i=Y by A9,TARSKI:def 2;
A12: dom a=Seg len a by FINSEQ_1:def 3;
    dom a1=Seg len a1 by FINSEQ_1:def 3;
    then a1.i in rng a1 by A4,A10,A12,FUNCT_1:def 5;
    then
A13: a1.i=X or a1.i=Y by A5,A9,TARSKI:def 2;
    dom a2=Seg len a2 by FINSEQ_1:def 3;
    then a2.i in rng a2 by A2,A4,A10,A12,FUNCT_1:def 5;
    then a2.i=X or a2.i=Y by A3,A5,A9,TARSKI:def 2;
    hence a.i = a2.i by A1,A2,A3,A4,A5,A6,A7,A8,A10,A11,A13,Th21;
  end;
  hence thesis by A2,A4,FINSEQ_1:18;
end;

theorem Th23:
  X <> Y & n > 1 implies ex a1 st rng a1 = {X, Y} & len a1 = n & a1.1 = X
proof
  deffunc F(Nat,Nat) = 3-'$2;
  assume that
A1: X <> Y and
A2: n > 1;
  set p = <*X, Y*>;
A3: p.1 = X by FINSEQ_1:61;
A4: 3-'2 =1+2-2 by XREAL_0:def 2
    .=1;
A5: p.2 = Y by FINSEQ_1:61;
  consider f1 being Function of NAT, NAT such that
A6: f1.0=2 & for n being Nat holds f1.(n+1)=F(n,f1.n) from NAT_1:sch 12;
  defpred P[Element of NAT] means (f1.$1=1 or f1.$1=2) & f1.$1<>f1.($1+1);
A7: 3-'1=2+1-1 by XREAL_0:def 2
    .=2;
A8: for i being Element of NAT st P[i] holds P[i+1]
  proof
    let i be Element of NAT such that
A9: f1.i = 1 or f1.i = 2 and
    f1.i <> f1.(i+1);
    thus f1.(i+1)=1 or f1.(i+1) = 2 by A6,A4,A7,A9;
    hence thesis by A6,A4,A7;
  end;
A10: P[0] by A6,A4;
A11: for i holds P[i] from NAT_1:sch 1 (A10, A8);
  deffunc F(Nat) = p.(f1.$1);
  consider p1 being FinSequence such that
A12: len p1 = n & for k be Nat st k in dom p1 holds p1.k= F(k) from
  FINSEQ_1:sch 2;
A13: f1.(0+1) = 1 by A6,A4;
  then
A14: f1.(1+1) = 2 by A6,A7;
A15: now
    let y be set;
    hereby
      assume y in {X, Y};
      then
A16:  y = X or y = Y by TARSKI:def 2;
      1 in dom p1 by A2,A12,FINSEQ_3:27;
      then
A17:  p1.1=X by A3,A13,A12;
A18:  1 in dom p1 by A2,A12,FINSEQ_3:27;
      1+1<=n by A2,NAT_1:13;
      then
A19:  2 in dom p1 by A12,FINSEQ_3:27;
      then p1.2=Y by A5,A14,A12;
      hence ex x being set st x in dom p1 & y = p1.x by A16,A19,A17,A18;
    end;
    given x being set such that
A20: x in dom p1 and
A21: y = p1.x;
    x in Seg len p1 by A20,FINSEQ_1:def 3;
    then consider x9 being Element of NAT such that
A22: x9=x and
A23: 1<=x9 and
A24: x9<=len p1;
    x9 in dom p1 by A23,A24,FINSEQ_3:27;
    then
A25: p1.x9 = p.(f1.x9) by A12;
    f1.x9 = 1 or f1.x9 = 2 by A11;
    hence y in {X, Y} by A3,A5,A21,A22,A25,TARSKI:def 2;
  end;
  then rng p1 = {X, Y} by FUNCT_1:def 5;
  then reconsider p1 as TwoValued FinSequence by A1,A2,A12,Th19;
  now
    let i be Nat;
    assume that
A26: 1<=i and
A27: (i+1)<=len p1;
    1<=(i+1) by A26,NAT_1:13;
    then (i+1) in dom p1 by A27,FINSEQ_3:27;
    then
A28: p1.(i+1)=p.(f1.(i+1)) by A12;
A29: f1.(i+1)=1 or f1.(i+1)=2 by A11;
    i<=n by A12,A27,NAT_1:13;
    then
A30: i in dom p1 by A12,A26,FINSEQ_3:27;
    then p1.i=p.(f1.i) by A12;
    hence p1.i <> p1.(i+1) by A1,A3,A5,A11,A30,A28,A29;
  end;
  then reconsider p1 as TwoValued Alternating FinSequence by Def4;
  take p1;
  thus rng p1 = {X, Y} by A15,FUNCT_1:def 5;
  thus len p1 = n by A12;
  1 in dom p1 by A2,A12,FINSEQ_3:27;
  hence thesis by A3,A13,A12;
end;

begin    :: Finite subsequences of finite sequences

registration
  let X;
  let fs be FinSequence of X;
  cluster -> FinSubsequence-like Subset of fs;
  coherence
  proof
    let IT be Subset of fs;
    take len fs;
    dom IT c= dom fs by RELAT_1:25;
    hence thesis by FINSEQ_1:def 3;
  end;
end;



canceled 2;

theorem Th26:
  for f being FinSubsequence, g, h, fg, fh, fgh being FinSequence
st rng g c= dom f & rng h c= dom f & fg=f*g & fh=f*h & fgh=f*(g^h) holds fgh =
  fg^fh
proof
  let f be FinSubsequence, g, h, fg, fh, fgh be FinSequence;
  assume that
A1: rng g c= dom f and
A2: rng h c= dom f and
A3: fg=f*g and
A4: fh=f*h and
A5: fgh=f*(g^h);
  now
    rng (g^h) = rng g \/ rng h by FINSEQ_1:44;
    hence len fgh = len (g^h) by A1,A2,A5,FINSEQ_2:33,XBOOLE_1:8
      .= len g + len h by FINSEQ_1:35;
    then
A6: dom fgh = Seg (len g +len h) by FINSEQ_1:def 3;
A7: dom fh = dom h by A2,A4,RELAT_1:46;
A8: dom fg = dom g by A1,A3,RELAT_1:46;
A9: len fg = len g by A1,A3,FINSEQ_2:33;
    len fh = len h by A2,A4,FINSEQ_2:33;
    hence len (fg^fh) = len g + len h by A9,FINSEQ_1:35;
    let j be Nat;
    assume
A10: j in dom fgh;
    then
A11: 1<=j by A6,FINSEQ_1:3;
A12: j<=len g + len h by A6,A10,FINSEQ_1:3;
    per cases;
    suppose
      j<=len g;
      then
A13:  j in dom g by A11,FINSEQ_3:27;
      thus fgh.j = f.((g^h).j) by A5,A10,FUNCT_1:22
        .= f.(g.j) by A13,FINSEQ_1:def 7
        .= fg.j by A3,A13,FUNCT_1:23
        .= (fg^fh).j by A8,A13,FINSEQ_1:def 7;
    end;
    suppose
      len g < j;
      then len g +1 <=j by NAT_1:13;
      then
A14:  1<=j-len g by XREAL_1:21;
      then j-'len g = j-len g by XREAL_0:def 2;
      then reconsider j9 = j-len g as Element of NAT;
A15:  j = len g + j9;
      then j9<=len h by A12,XREAL_1:8;
      then
A16:  j9 in dom h by A14,FINSEQ_3:27;
      thus fgh.j = f.((g^h).j) by A5,A10,FUNCT_1:22
        .= f.(h.j9) by A15,A16,FINSEQ_1:def 7
        .= fh.j9 by A4,A16,FUNCT_1:23
        .= (fg^fh).j by A9,A7,A15,A16,FINSEQ_1:def 7;
    end;
  end;
  hence thesis by FINSEQ_2:10;
end;

reserve fs, fs1, fs2 for FinSequence of X,
  fss, fss2 for Subset of fs;

theorem
  dom fss c= dom fs & rng fss c= rng fs by RELAT_1:25;

theorem Th28:
  fs is Subset of fs
proof
  fs c= fs;
  hence thesis;
end;

theorem Th29:
  fss|Y is Subset of fs
proof
  reconsider f = fss|Y as FinSubsequence;
  f c= fss by RELAT_1:88;
  hence thesis by XBOOLE_1:1;
end;

theorem Th30:
  for fss1 being Subset of fs1 st Seq fss = fs1 & Seq fss1 = fs2 &
  fss2 = fss|rng((Sgm dom fss)|dom fss1) holds Seq fss2 = fs2
proof
  let fss1 be Subset of fs1 such that
A1: Seq fss = fs1 and
A2: Seq fss1 = fs2 and
A3: fss2 = fss|rng((Sgm dom fss)|dom fss1);
  consider k be Nat such that
A4: dom fs = Seg k by FINSEQ_1:def 2;
  reconsider d2 = dom fss2 as finite set;
  reconsider d1 = dom fss1 as finite set;
A5: ex l be Nat st dom fs1 = Seg l by FINSEQ_1:def 2;
  then
A6: dom Sgm d1 = Seg card d1 by FINSEQ_3:45,RELAT_1:25;
  reconsider k as Element of NAT by ORDINAL1:def 13;
A7: dom fss c= Seg k by A4,RELAT_1:25;
  then rng Sgm dom fss c= dom fss by FINSEQ_1:def 13;
  then
A8: dom fs1 = dom Sgm dom fss by A1,RELAT_1:46;
  dom fss2 c= Seg k by A4,RELAT_1:25;
  then
A9: rng Sgm dom fss2 = dom fss2 by FINSEQ_1:def 13;
A10: (Sgm dom fss)|dom fss1 c= Sgm dom fss by RELAT_1:88;
  then
A11: dom ((Sgm dom fss)|dom fss1) c= dom (Sgm dom fss) by RELAT_1:25;
  rng ((Sgm dom fss)|dom fss1) c= rng (Sgm dom fss) by A10,RELAT_1:25;
  then
A12: rng((Sgm dom fss)|dom fss1) c= dom fss by A7,FINSEQ_1:def 13;
  then
A13: dom fss2 = rng((Sgm dom fss)|dom fss1) by A3,RELAT_1:91;
A14: dom fss1 c= dom fs1 by RELAT_1:25;
  now
    take Z = (Sgm dom fss)|dom fss1;
A15: dom Z = dom fss1 by A8,RELAT_1:25,91;
    hereby
      let x be set;
      assume
A16:  x in d1;
      reconsider y = Z.x as set;
      take y;
      thus y in d2 by A13,A15,A16,FUNCT_1:def 5;
      thus [x,y] in Z by A15,A16,FUNCT_1:8;
    end;
    hereby
      let y be set;
      assume y in d2;
      then consider x such that
A17:  x in dom Z and
A18:  y = Z.x by A13,FUNCT_1:def 5;
      take x;
      thus x in d1 & [x,y] in Z by A14,A8,A17,A18,FUNCT_1:8,RELAT_1:91;
    end;
    let x,y,z,u be set;
    assume that
A19: [x,y] in Z and
A20: [z,u] in Z;
A21: z in dom Z by A20,FUNCT_1:8;
A22: u = Z.z by A20,FUNCT_1:8;
    then u in rng Z by A21,FUNCT_1:def 5;
    then u in dom fss by A12;
    then
A23: u in Seg k by A7;
A24: u = (Sgm dom fss).z by A21,A22,FUNCT_1:70;
A25: y = Z.x by A19,FUNCT_1:8;
    hence x=z implies y=u by A20,FUNCT_1:8;
A26: x in dom Z by A19,FUNCT_1:8;
    then
A27: y = (Sgm dom fss).x by A25,FUNCT_1:70;
    y in rng Z by A26,A25,FUNCT_1:def 5;
    then y in dom fss by A12;
    then
A28: y in Seg k by A7;
    assume
A29: y=u;
    assume
A30: x<>z;
    reconsider x,z as Element of NAT by A26,A21;
A31: x<=len Sgm dom fss by A11,A26,FINSEQ_3:27;
A32: z<= len Sgm dom fss by A11,A21,FINSEQ_3:27;
A33: 1<=z by A11,A21,FINSEQ_3:27;
A34: 1<=x by A11,A26,FINSEQ_3:27;
    per cases by A30,XXREAL_0:1;
    suppose
      x<z;
      hence contradiction by A7,A27,A24,A29,A34,A32,FINSEQ_1:def 13;
    end;
    suppose
      z<x;
      hence contradiction by A7,A27,A24,A29,A31,A33,FINSEQ_1:def 13;
    end;
  end;
  then d1,d2 are_equipotent by TARSKI:def 6;
  then card d1 = card d2 by CARD_1:21;
  then
A35: dom Sgm d2 = Seg card d1 by A4,FINSEQ_3:45,RELAT_1:25;
A36: rng Sgm dom fss1 = dom fss1 by A14,A5,FINSEQ_1:def 13;
  now
    now
      let x be set;
      hereby
        assume x in dom fs2;
        then
A37:    x in dom Sgm dom fss2 by A2,A6,A35,FUNCT_1:21;
        then (Sgm dom fss2).x in dom fss2 by A9,FUNCT_1:def 5;
        hence x in dom Seq fss2 by A37,FUNCT_1:21;
      end;
      assume x in dom Seq fss2;
      then
A38:  x in dom Sgm dom fss1 by A6,A35,FUNCT_1:21;
      then (Sgm dom fss1).x in dom fss1 by A36,FUNCT_1:def 5;
      hence x in dom fs2 by A2,A38,FUNCT_1:21;
    end;
    hence
A39: dom fs2 = dom Seq fss2 by TARSKI:2;
    thus dom fs2 = dom fs2;
    let k be Nat;
    assume
A40: k in dom fs2;
    then
A41: (Sgm dom fss1).k in dom fss1 by A2,FUNCT_1:21;
A42: (Sgm dom fss2).k in dom fss2 by A39,A40,FUNCT_1:21;
A43: (Sgm dom fss1).k in dom fss1 by A2,A40,FUNCT_1:21;
    then
A44: fss1.((Sgm dom fss1).k) = (Seq fss).((Sgm dom fss1).k) by A1,
    GRFUNC_1:8;
A45: (Sgm dom fss2).k = ((Sgm dom fss)*(Sgm dom fss1)).k by A7,A13,A8,Th3
    , RELAT_1:25;
    k in dom Sgm dom fss1 by A2,A40,FUNCT_1:21;
    then k in dom ((Sgm dom fss)*(Sgm dom fss1)) by A14,A8,
    A41,FUNCT_1:21;
    then
A46: (Sgm dom fss2).k = (Sgm dom fss).((Sgm dom fss1).k) by A45,
    FUNCT_1:22;
A47: fss2 c= fss by A3,RELAT_1:88;
    dom fss1 c= dom Seq fss by A1,RELAT_1:25;
    then
A48: (Sgm dom fss1).k in dom Sgm dom fss by A43,FUNCT_1:21;
    thus (Seq fss2).k = fss2.((Sgm dom fss2).k) by A39,A40,FUNCT_1:22
      .= fss.((Sgm dom fss).((Sgm dom fss1).k)) by A46,A47,A42,GRFUNC_1:8
      .= fss1.((Sgm dom fss1).k) by A48,A44,FUNCT_1:23
      .= fs2.k by A2,A40,FUNCT_1:22;
  end;
  hence thesis by FINSEQ_1:17;
end;

begin   :: Vertex sequences induced by chains

reserve G for Graph;
reserve v, v1, v2, v3, v4 for Element of G,
  e for set;

theorem Th31:
  e joins v1,v2 implies e joins v2,v1
proof
  assume e joins v1,v2;
  then
  (the Source of G).e = v1 & (the Target of G).e = v2 or (the Source of G)
  .e = v2 & (the Target of G).e = v1 by GRAPH_1:def 10;
  hence thesis by GRAPH_1:def 10;
end;

theorem Th32:
  e joins v1,v2 & e joins v3,v4 implies v1=v3 & v2=v4 or v1=v4 & v2=v3
proof
  assume that
A1: e joins v1,v2 and
A2: e joins v3,v4;
  (the Source of G).e = v1 & (the Target of G).e = v2 or (the Source of G)
  .e = v2 & (the Target of G).e = v1 by A1,GRAPH_1:def 10;
  hence thesis by A2,GRAPH_1:def 10;
end;

reserve vs, vs1, vs2 for FinSequence of the carrier of G,
  c, c1, c2 for Chain of G;

definition
  let G, X;
  canceled;
  func G-VSet X -> set equals
  { v: ex e being Element of the carrier' of G st
  e in X & (v = (the Source of G).e or v = (the Target of G).e) };
  correctness;
end;

definition
  let G, vs;
  let c be FinSequence;
  pred vs is_vertex_seq_of c means
  :Def7:
  len vs = len c + 1 & for n st 1<=n &
  n<=len c holds c.n joins vs/.n, vs/.(n+1);
end;

canceled;

theorem Th34:
  c <>{} & vs is_vertex_seq_of c implies G-VSet rng c = rng vs
proof
  assume that
A1: c <>{} and
A2: vs is_vertex_seq_of c;
A3: len vs = len c + 1 by A2,Def7;
  now
    let y be set;
    hereby
      assume y in G-VSet rng c;
      then consider v being Element of G such that
A4:   v=y and
A5:   ex e being Element of the carrier' of G st e in rng c & (v = (
      the Source of G).e or v = (the Target of G).e);
      consider e being Element of the carrier' of G such that
A6:   e in rng c and
A7:   v = (the Source of G).e or v = (the Target of G).e by A5;
      consider x being set such that
A8:   x in dom c and
A9:   e = c.x by A6,FUNCT_1:def 5;
      reconsider x as Element of NAT by A8;
A10:  1<=x+1 by NAT_1:12;
      set v2 = vs/.(x+1);
      set v1 = vs/.x;
A11:  x<=len c by A8,FINSEQ_3:27;
      then x+1<=len vs by A3,XREAL_1:9;
      then
A12:  x+1 in dom vs by A10,FINSEQ_3:27;
A13:  1<=x by A8,FINSEQ_3:27;
      then c.x joins v1, v2 by A2,A11,Def7;
      then
A14:  v = v1 or v = v2 by A7,A9,GRAPH_1:def 10;
A15:  x<=len vs by A3,A11,NAT_1:12;
      then
A16:  v1 = vs.x by A13,FINSEQ_4:24;
A17:  x in dom vs by A13,A15,FINSEQ_3:27;
      v2 = vs.(x+1) by A3,A11,A10,FINSEQ_4:24,XREAL_1:9;
      hence y in rng vs by A4,A16,A14,A17,A12,FUNCT_1:def 5;
    end;
    assume y in rng vs;
    then consider x being set such that
A18: x in dom vs and
A19: y = vs.x by FUNCT_1:def 5;
    reconsider x as Element of NAT by A18;
A20: 1<=x by A18,FINSEQ_3:27;
A21: x<=len vs by A18,FINSEQ_3:27;
    per cases by A3,A21,NAT_1:8;
    suppose
A22:  x<=len c;
      then x in dom c by A20,FINSEQ_3:27;
      then
A23:  c.x in rng c by FUNCT_1:def 5;
      rng c c= the carrier' of G by FINSEQ_1:def 4;
      then reconsider e = c.x as Element of the carrier' of G by A23;
      x in dom c by A20,A22,FINSEQ_3:27;
      then
A24:  e in rng c by FUNCT_1:def 5;
      set v2 = vs/.(x+1);
      set v1 = vs/.x;
      c.x joins v1, v2 by A2,A20,A22,Def7;
      then
A25:  v1 = (the Source of G).e & v2 = (the Target of G).e or v2 = (the
      Source of G).e & v1 = (the Target of G).e by GRAPH_1:def 10;
      v1 = vs.x by A20,A21,FINSEQ_4:24;
      hence y in G-VSet rng c by A19,A25,A24;
    end;
    suppose
A26:  x=len c+1;
      set l = len c;
A27:  rng c c= the carrier' of G by FINSEQ_1:def 4;
      0+1=1;
      then
A28:  1<=l by A1,NAT_1:13;
      then l in dom c by FINSEQ_3:27;
      then c.l in rng c by FUNCT_1:def 5;
      then reconsider e = c.l as Element of the carrier' of G by A27;
      set v2 = vs/.(l+1);
      set v1 = vs/.l;
      l in dom c by A28,FINSEQ_3:27;
      then
A29:  e in rng c by FUNCT_1:def 5;
      c.l joins v1, v2 by A2,A28,Def7;
      then
A30:  v1 = (the Source of G).e & v2 = (the Target of G).e or v2 = (the
      Source of G).e & v1 = (the Target of G).e by GRAPH_1:def 10;
      v2 = vs.(l+1) by A3,A20,A26,FINSEQ_4:24;
      hence y in G-VSet rng c by A19,A26,A30,A29;
    end;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th35:
  <*v*> is_vertex_seq_of {}
proof
  thus len <*v*> = len {} +1 by FINSEQ_1:57;
  thus thesis;
end;

theorem Th36:
  ex vs st vs is_vertex_seq_of c
proof
  consider p such that
A1: len p = len c + 1 and
A2: for n st 1<=n & n<=len p holds p.n in the carrier of G and
A3: for n st 1<=n & n<=len c ex v1,v2 st v1=p.n & v2=p.(n+1) & c.n joins
  v1,v2 by GRAPH_1:def 12;
  rng p c= the carrier of G
  proof
    let y be set;
    assume y in rng p;
    then consider x being set such that
A4: x in dom p and
A5: y = p.x by FUNCT_1:def 5;
    reconsider n=x as Element of NAT by A4;
A6: n<=len p by A4,FINSEQ_3:27;
    1<=n by A4,FINSEQ_3:27;
    hence thesis by A2,A5,A6;
  end;
  then reconsider p as FinSequence of the carrier of G by FINSEQ_1:def 4;
  take p;
  thus len p = len c + 1 by A1;
  let n;
  assume that
A7: 1<=n and
A8: n<=len c;
A9: n<=len p by A1,A8,NAT_1:12;
  1<=n+1 by NAT_1:12;
  then
A10: p/.(n+1)=p.(n+1) by A1,A8,FINSEQ_4:24,XREAL_1:9;
  ex v1,v2 st v1=p.n & v2=p.(n+1) & c.n joins v1,v2 by A3,A7,A8;
  hence thesis by A7,A9,A10,FINSEQ_4:24;
end;

theorem Th37:
  c <>{} & vs1 is_vertex_seq_of c & vs2 is_vertex_seq_of c & vs1
<> vs2 implies vs1.1<>vs2.1 & for vs st vs is_vertex_seq_of c holds vs = vs1 or
  vs = vs2
proof
  assume that
A1: c <>{} and
A2: vs1 is_vertex_seq_of c and
A3: vs2 is_vertex_seq_of c;
A4: len vs1 = len c +1 by A2,Def7;
  defpred P[Nat] means $1 in dom vs1 & vs1.$1<>vs2.$1;
  set TG = the Target of G;
  set SG = the Source of G;
A5: Seg len vs1 = dom vs1 by FINSEQ_1:def 3;
A6: Seg len vs2 = dom vs2 by FINSEQ_1:def 3;
A7: len vs2 = len c +1 by A3,Def7;
  assume vs1 <> vs2;
  then
A8: ex j being Nat st P[j] by A4,A7,FINSEQ_2:10;
  consider k be Nat such that
A9: P[k] and
A10: for n be Nat st P[n] holds k<=n from NAT_1:sch 5 (A8);
A11: 1<=k by A9,FINSEQ_3:27;
  per cases by A11,XXREAL_0:1;
  suppose
A12: k=1;
    hence vs1.1 <> vs2.1 by A9;
    set v21 = vs2/.1;
    set v12 = vs1/.(1+1);
    set v11 = vs1/.1;
    let vs;
    set v1 = vs/.1;
    set v2 = vs/.(1+1);
    assume
A13: vs is_vertex_seq_of c;
    then
A14: len vs = len c +1 by Def7;
    0+1=1;
    then
A15: 1<=len c by A1,NAT_1:13;
    then
A16: c.1 joins vs/.1, vs/.(1+1) by A13,Def7;
    c.1 joins vs1/.1, vs1/.(1+1) by A2,A15,Def7;
    then
A17: v1=v11 & v2=v12 or v1=v12 & v2=v11 by A16,Th32;
A18: 1<=len vs1 by A4,NAT_1:12;
    then
A19: v11=vs1.1 by FINSEQ_4:24;
A20: v21=vs2.1 by A4,A7,A18,FINSEQ_4:24;
A21: c.1 joins vs2/.1, vs2/.(1+1) by A3,A15,Def7;
    thus vs = vs1 or vs = vs2
    proof
      per cases by A9,A12,A19,A20,A16,A21,A17,Th32;
      suppose
A22:    v1 = v11;
        now
          defpred P[Nat] means $1 in dom vs implies vs.$1=vs1.$1;
          thus len vs = len vs;
          thus len vs1 = len vs by A4,A13,Def7;
A23:      for i being Nat st P[i] holds P[i+1]
          proof
A24:        0+1=1;
            let i be Nat;
            assume
A25:        i in dom vs implies vs.i=vs1.i;
            assume
A26:        i+1 in dom vs;
            reconsider i as Element of NAT by ORDINAL1:def 13;
A27:        1<=i+1 by A26,FINSEQ_3:27;
A28:        i+1<=len vs by A26,FINSEQ_3:27;
            per cases by A24,NAT_1:13;
            suppose
              i=0;
              hence thesis by A4,A14,A18,A19,A22,FINSEQ_4:24;
            end;
            suppose
A29:          1<=i;
              set v12 = vs1/.(i+1);
              set v11 = vs1/.i;
              set v2 = vs/.(i+1);
A30:          v2=vs.(i+1) by A27,A28,FINSEQ_4:24;
              set v1 = vs/.i;
A31:          i<=len c by A14,A28,XREAL_1:8;
              then
A32:          c.i joins vs1/.i, vs1/.(i+1) by A2,A29,Def7;
A33:          i<=len vs by A14,A31,NAT_1:12;
              then
A34:          v1 =vs.i by A29,FINSEQ_4:24;
              c.i joins vs/.i, vs/.(i+1) by A13,A29,A31,Def7;
              then
A35:          v1=v11 & v2=v12 or v1=v12 & v2=v11 by A32,Th32;
              v11=vs1.i by A4,A14,A29,A33,FINSEQ_4:24;
              hence thesis by A4,A14,A25,A27,A28,A29,A33,A34,A30,A35,
              FINSEQ_3:27,FINSEQ_4:24;
            end;
          end;
A36:      P[0] by FINSEQ_3:27;
          thus for i being Nat holds P[i] from NAT_1:sch 2(A36, A23);
        end;
        hence thesis by FINSEQ_2:10;
      end;
      suppose
A37:    v1 = v21;
        now
          defpred P[Nat] means $1 in dom vs implies vs.$1=vs2.$1;
          thus len vs = len vs;
          thus len vs2 = len vs by A7,A13,Def7;
A38:      for i being Nat st P[i] holds P[i+1]
          proof
A39:        0+1=1;
            let i be Nat;
            assume
A40:        i in dom vs implies vs.i=vs2.i;
            assume
A41:        i+1 in dom vs;
            then
A42:        1<=i+1 by FINSEQ_3:27;
A43:        i+1<=len vs by A41,FINSEQ_3:27;
            reconsider i as Element of NAT by ORDINAL1:def 13;
            per cases by A39,NAT_1:13;
            suppose
              i=0;
              hence thesis by A4,A14,A18,A20,A37,FINSEQ_4:24;
            end;
            suppose
A44:          1<=i;
              set v12 = vs2/.(i+1);
              set v11 = vs2/.i;
              set v2 = vs/.(i+1);
A45:          v2=vs.(i+1) by A42,A43,FINSEQ_4:24;
              set v1 = vs/.i;
A46:          i<=len c by A14,A43,XREAL_1:8;
              then
A47:          c.i joins vs2/.i, vs2/.(i+1) by A3,A44,Def7;
A48:          i<=len vs by A14,A46,NAT_1:12;
              then
A49:          v1 =vs.i by A44,FINSEQ_4:24;
              c.i joins vs/.i, vs/.(i+1) by A13,A44,A46,Def7;
              then
A50:          v1=v11 & v2=v12 or v1=v12 & v2=v11 by A47,Th32;
              v11=vs2.i by A7,A14,A44,A48,FINSEQ_4:24;
              hence thesis by A7,A14,A40,A42,A43,A44,A48,A49,A45,A50,
              FINSEQ_3:27,FINSEQ_4:24;
            end;
          end;
A51:      P[0] by FINSEQ_3:27;
          thus for i being Nat holds P[i] from NAT_1:sch 2(A51, A38);
        end;
        hence thesis by FINSEQ_2:10;
      end;
    end;
  end;
  suppose
    1<k;
    then 1+1<=k by NAT_1:13;
    then consider k1 being Element of NAT such that
A52: 1<=k1 and
A53: k1<k and
A54: k=k1+1 by Th1;
A55: k<=len vs1 by A9,FINSEQ_3:27;
    then
A56: k1<=len vs1 by A53,XXREAL_0:2;
    then
A57: k1 in dom vs1 by A52,FINSEQ_3:27;
A58: vs1/.k1=vs1.k1 by A52,A56,FINSEQ_4:24;
A59: vs2/.k=vs2.k by A4,A7,A5,A6,A9,PARTFUN1:def 8;
A60: vs1/.k=vs1.k by A9,PARTFUN1:def 8;
A61: k1<=len c by A4,A54,A55,XREAL_1:8;
    then c.k1 joins vs1/.k1, vs1/.(k1+1) by A2,A52,Def7;
    then
A62: SG.(c.k1)=vs1/.k1 & TG.(c.k1)=vs1/.k or SG.(c.k1)=vs1/.k & TG.(c.k1)=
    vs1/.k1 by A54,GRAPH_1:def 10;
    c.k1 joins vs2/.k1, vs2/.(k1+1) by A3,A52,A61,Def7;
    then
A63: SG.(c.k1)=vs2/.k1 & TG.(c.k1)=vs2/.k or SG.(c.k1)=vs2/.k & TG.(c.k1)=
    vs2/.k1 by A54,GRAPH_1:def 10;
    vs2/.k1=vs2.k1 by A4,A7,A52,A56,FINSEQ_4:24;
    hence thesis by A9,A10,A53,A57,A58,A60,A59,A62,A63;
  end;
end;

definition
  let G;
  let c be FinSequence;
  pred c alternates_vertices_in G means
  :Def8:
  len c>=1 & card (G-VSet rng c)
= 2 & for n st n in dom c holds (the Source of G).(c.n) <> (the Target of G).(c
  .n);
end;

theorem Th38:
  c alternates_vertices_in G & vs is_vertex_seq_of c implies for k
  st k in dom c holds vs.k <> vs.(k+1)
proof
  assume that
A1: c alternates_vertices_in G and
A2: vs is_vertex_seq_of c;
  set TG = (the Target of G);
  set SG = (the Source of G);
  let k;
  set px = vs/.k;
  set px1 = vs/.(k+1);
  assume
A3: k in dom c;
  then
A4: k<=len c by FINSEQ_3:27;
A5: 1<=k by A3,FINSEQ_3:27;
  then c.k joins px, px1 by A2,A4,Def7;
  then
A6: TG.(c.k)=px1 & SG.(c.k)=px or TG.(c.k)=px & SG.(c.k)=px1 by
  GRAPH_1:def 10;
A7: len vs = len c +1 by A2,Def7;
  then k<=len vs by A4,NAT_1:12;
  then
A8: px = vs.k by A5,FINSEQ_4:24;
  1<=k+1 by NAT_1:12;
  then px1 = vs.(k+1) by A4,A7,FINSEQ_4:24,XREAL_1:9;
  hence thesis by A1,A3,A8,A6,Def8;
end;

theorem Th39:
  c alternates_vertices_in G & vs is_vertex_seq_of c implies rng
  vs = {(the Source of G).(c.1), (the Target of G).(c.1)}
proof
  assume that
A1: c alternates_vertices_in G and
A2: vs is_vertex_seq_of c;
  set px1 = vs/.(1+1);
  set TG = (the Target of G);
  set SG = (the Source of G);
  set px = vs/.1;
A3: len vs = len c +1 by A2,Def7;
  then
A4: 1<=len vs by NAT_1:12;
  then
A5: px = vs.1 by FINSEQ_4:24;
  c <>{} by A1,Def8,CARD_1:47;
  then G-VSet rng c = rng vs by A2,Th34;
  then card rng vs = 2 by A1,Def8;
  then consider x, y being set such that
  x<>y and
A6: rng vs={x,y} by CARD_2:79;
  1 in dom vs by A4,FINSEQ_3:27;
  then vs.1 in rng vs by FUNCT_1:def 5;
  then
A7: vs.1 = x or vs.1 = y by A6,TARSKI:def 2;
A8: 1<=len c by A1,Def8;
  then
A9: 1 in dom c by FINSEQ_3:27;
A10: px1 = vs.(1+1) by A8,A3,FINSEQ_4:24,XREAL_1:9;
  c.1 joins px, px1 by A2,A8,Def7;
  then
A11: TG.(c.1)=px1 & SG.(c.1)=px or TG.(c.1)=px & SG.(c.1)=px1 by
  GRAPH_1:def 10;
  1+1<=len vs by A8,A3,XREAL_1:9;
  then 1+1 in dom vs by FINSEQ_3:27;
  then vs.(1+1) in rng vs by FUNCT_1:def 5;
  then vs.(1+1) = x or vs.(1+1) = y by A6,TARSKI:def 2;
  hence thesis by A1,A6,A5,A10,A11,A7,A9,Def8;
end;

theorem Th40:
  c alternates_vertices_in G & vs is_vertex_seq_of c implies vs is
  TwoValued Alternating FinSequence
proof
  assume that
A1: c alternates_vertices_in G and
A2: vs is_vertex_seq_of c;
A3: c <>{} by A1,Def8,CARD_1:47;
A4: len vs = len c + 1 by A2,Def7;
A5: now
    let k be Nat;
    assume that
A6: 1<=k and
A7: (k+1)<=len vs;
    k<=len c by A4,A7,XREAL_1:8;
    then k in dom c by A6,FINSEQ_3:27;
    hence vs.k <> vs.(k+1) by A1,A2,Th38;
  end;
  card (G-VSet rng c) = 2 by A1,Def8;
  then card rng vs = 2 by A2,A3,Th34;
  hence thesis by A5,Def3,Def4;
end;

theorem Th41:
  c alternates_vertices_in G implies ex vs1,vs2 st vs1<>vs2 & vs1
  is_vertex_seq_of c & vs2 is_vertex_seq_of c & for vs st vs is_vertex_seq_of c
  holds vs=vs1 or vs=vs2
proof
  set X = (the Source of G).(c.1);
  set Y = (the Target of G).(c.1);
  consider p1 being FinSequence of the carrier of G such that
A1: p1 is_vertex_seq_of c by Th36;
  assume
A2: c alternates_vertices_in G;
  then
A3: 1<=len c by Def8;
  then
A4: 1 in dom c by FINSEQ_3:27;
A5: 1+1=2;
  then
A6: p1.1 <> p1.2 by A2,A1,A4,Th38;
A7: rng p1 = {X, Y} by A2,A1,Th39;
A8: len p1 = len c + 1 by A1,Def7;
  then
A9: len p1 > 1 by A3,NAT_1:13;
  then consider p2 being TwoValued Alternating FinSequence such that
A10: rng p2 = {p1.2, p1.1} and
A11: len p2 = len p1 and
A12: p2.1=p1.2 by A6,Th23;
A13: dom p1 = dom p2 by A11,FINSEQ_3:31;
  1+1<=len p1 by A9,NAT_1:13;
  then 2 in dom p1 by FINSEQ_3:27;
  then p1.2 in rng p1 by FUNCT_1:def 5;
  then
A14: p1.2 = X or p1.2 = Y by A7,TARSKI:def 2;
  1 in dom p1 by A9,FINSEQ_3:27;
  then p1.1 in rng p1 by FUNCT_1:def 5;
  then
A15: p1.1 = X or p1.1 = Y by A7,TARSKI:def 2;
  then reconsider p2 as FinSequence of the carrier of G by A2,A1,A7,A4,A5,A10,
  A14,Th38,FINSEQ_1:def 4;
  take p1, p2;
  thus p1<>p2 by A2,A1,A4,A5,A12,Th38;
  thus p1 is_vertex_seq_of c by A1;
A16: p1 is TwoValued Alternating FinSequence by A2,A1,Th40;
  now
    thus len p2 = len c + 1 by A1,A11,Def7;
    let n be Element of NAT such that
A17: 1<=n and
A18: n<=len c;
A19: n<=len p1 by A8,A18,NAT_1:12;
    then
A20: p2/.n = p2.n by A11,A17,FINSEQ_4:24;
A21: n in dom p1 by A17,A19,FINSEQ_3:27;
    then p2.n in {X, Y} by A2,A1,A4,A5,A10,A13,A15,A14,
    Th38,FUNCT_1:def 5;
    then
A22: p2.n=X or p2.n=Y by TARSKI:def 2;
    set x = p1/.n;
    set y = p1/.(n+1);
A23: c.n joins x, y by A1,A17,A18,Def7;
A24: n+1<=len p1 by A8,A18,XREAL_1:8;
    then
A25: p2/.(n+1) = p2.(n+1) by A11,FINSEQ_4:24,NAT_1:12;
A26: 1<=n+1 by NAT_1:12;
    then
A27: n+1 in dom p1 by A24,FINSEQ_3:27;
    then p2.(n+1) in {X, Y} by A2,A1,A4,A5,A10,A13,A15,A14,Th38,FUNCT_1:
    def 5;
    then
A28: p2.(n+1)=X or p2.(n+1)=Y by TARSKI:def 2;
    p1.n in {X, Y} by A7,A21,FUNCT_1:def 5;
    then p1.n=X or p1.n=Y by TARSKI:def 2;
    then
A29: x = p2.(n+1) by A7,A16,A6,A10,A11,A12,A15,A14,A17,A19,A24,
    A22,A28
    , Def4,Th21,FINSEQ_4:24;
    p1.(n+1) in {X, Y} by A7,A27,FUNCT_1:def 5;
    then p1.(n+1)=X or p1.(n+1)=Y by TARSKI:def 2;
    then y = p2.n by A7,A16,A6,A10,A11,A12,A15,A14,A17,A26,A24,A22,A28,
    Def4, Th21,FINSEQ_4:24;
    hence c.n joins p2/.n,p2/.(n+1) by A23,A29,A20,A25,Th31;
  end;
  hence p2 is_vertex_seq_of c by Def7;
  let p be FinSequence of the carrier of G;
  assume
A30: p is_vertex_seq_of c;
  then reconsider p9 = p as TwoValued Alternating FinSequence by A2,Th40;
A31: len p = len c + 1 by A30,Def7;
  rng p = {X, Y} by A2,A30,Th39;
  then p9=p1 or p9=p2 by A8,A7,A16,A6,A10,A11,A12,A15,A14,A31,Th22;
  hence thesis;
end;

Lm7: (for x, y st x in D & y in D holds x=y) implies card D = 1
proof
  consider x being Element of D;
  assume for x, y st x in D & y in D holds x=y;
  then for y holds y in D iff y=x;
  then D = {x} by TARSKI:def 1;
  hence thesis by CARD_2:60;
end;

theorem Th42:
  vs is_vertex_seq_of c implies (card the carrier of G = 1 or c <>
{} & not c alternates_vertices_in G iff for vs1 st vs1 is_vertex_seq_of c holds
  vs1 = vs)
proof
  assume
A1: vs is_vertex_seq_of c;
  hereby
    assume
A2: card the carrier of G = 1 or c <>{} & not c alternates_vertices_in G;
    per cases by A2;
    suppose
A3:   card the carrier of G = 1;
      then reconsider tVG = the carrier of G as finite set;
      consider X being set such that
A4:   tVG = {X} by A3,CARD_2:60;
A5:   rng vs c= {X} by A4,FINSEQ_1:def 4;
A6:   len vs = len c +1 by A1,Def7;
      let vs1;
A7:   Seg len vs = dom vs by FINSEQ_1:def 3;
      assume vs1 is_vertex_seq_of c;
      then
A8:   len vs1 = len c +1 by Def7;
      assume vs1 <> vs;
      then consider j being Nat such that
A9:   j in dom vs and
A10:  vs1.j <> vs.j by A8,A6,FINSEQ_2:10;
      vs.j in rng vs by A9,FUNCT_1:def 5;
      then
A11:  vs.j = X by A5,TARSKI:def 1;
A12:  rng vs1 c= {X} by A4,FINSEQ_1:def 4;
      Seg len vs1 = dom vs1 by FINSEQ_1:def 3;
      then vs1.j in rng vs1 by A8,A6,A7,A9,FUNCT_1:def 5;
      hence contradiction by A10,A12,A11,TARSKI:def 1;
    end;
    suppose
A13:  c <>{} & not c alternates_vertices_in G;
      thus for vs1 st vs1 is_vertex_seq_of c holds vs1 = vs
      proof
        set TG = the Target of G;
        set SG = the Source of G;
        let vs1;
        defpred P[Nat] means $1 in dom vs1 & vs1.$1<>vs.$1;
        assume
A14:    vs1 is_vertex_seq_of c;
        then
A15:    len vs1 = len c +1 by Def7;
A16:    len vs = len c +1 by A1,Def7;
        then
A17:    dom vs1 = dom vs by A15,FINSEQ_3:31;
        assume vs1 <> vs;
        then
A18:    ex i being Nat st P[i] by A15,A16,FINSEQ_2:10;
        consider k be Nat such that
A19:    P[k] and
A20:    for n be Nat st P[n] holds k<=n from NAT_1:sch 5 (A18);
A21:    0+1=1 & k=0 or 0+1<=k by NAT_1:13;
        per cases by A21,XXREAL_0:1;
        suppose
          k=0;
          hence contradiction by A19,FINSEQ_3:27;
        end;
        suppose
A22:      k=1;
          thus contradiction
          proof
A23:        0+1=1;
            per cases by A23,NAT_1:13;
            suppose
              len c = 0;
              hence contradiction by A13;
            end;
            suppose
A24:          1<=len c;
              defpred P[Element of NAT] means $1 in dom c implies vs1/.$1<>vs
/.$1 & vs1/.($1+1)<>vs/.($1+1) & (TG.(c.$1)=TG.(c.1) & SG.(c.$1)=SG.(c.1) or TG
              .(c.$1)=SG.(c.1) & SG.(c.$1)=TG.(c.1));
A25:          vs/.k=vs.k by A17,A19,PARTFUN1:def 8;
A26:          vs1/.k=vs1.k by A19,PARTFUN1:def 8;
A27:          now
                let n be Element of NAT;
                assume
A28:            P[n];
                thus P[n+1]
                proof
                  assume
A29:              (n+1) in dom c;
                  then
A30:              1<=n+1 by FINSEQ_3:27;
A31:              n+1<=len c by A29,FINSEQ_3:27;
                  thus vs1/.(n+1)<>vs/.(n+1) & vs1/.(n+1+1)<>vs/.(n+1+1) & (TG
.(c.(n+1))=TG.(c.1) & SG.(c.(n+1))=SG.(c.1) or TG.(c.(n+1))=SG.(c.1) & SG.(c.(n
                  +1))=TG.(c.1))
                  proof
                    per cases;
                    suppose
A32:                  n=0;
                      hence vs1/.(n+1)<>vs/.(n+1) by A17,A19,A22,A26,
                      PARTFUN1:def 8;
A33:                  1<=len c by A30,A31,XXREAL_0:2;
                      then c.1 joins vs/.1,vs/.(1+1) by A1,Def7;
                      then
A34:                  SG.(c.1)=vs/.1 & TG.(c.1)=vs/.(1+1) or SG.(c.1)=vs
                      /.(1+1) & TG.(c.1)= vs/.1 by GRAPH_1:def 10;
                      c.1 joins vs1/.1,vs1/.(1+1) by A14,A33,Def7;
                      then
                      SG.(c.1)=vs1/.1 & TG.(c.1)= vs1/.(1+1) or SG.(c.1)=
                      vs1/.(1+1) & TG.(c. 1)=vs1/.1 by GRAPH_1:def 10;
                      hence vs1/.(n+1+1)<>vs/.(n+1+1) by A17,A19,A22,A26,A32,
                      A34,PARTFUN1:def 8;
                      thus thesis by A32;
                    end;
                    suppose
A35:                  0<n;
A36:                  n<=len c by A31,NAT_1:13;
A37:                  0+1<=n by A35,NAT_1:13;
                      hence vs1/.(n+1)<> vs/.(n+1) by A28,A36,FINSEQ_3:27;
                      c.n joins vs1/.n, vs1/.(n+1) by A14,A37,A36,Def7;
                      then
A38:                  SG.(c.n)=vs1/.n & TG.(c.n)= vs1/.(n+1) or SG.(c.n)=
                      vs1/.(n+1) & TG.(c. n)=vs1/.n by GRAPH_1:def 10;
                      c.(n+1) joins vs/.(n+1), vs/.(n+1+1) by A1,A30,A31,
                      Def7;
                      then
A39:                  SG.(c.(n+1))=vs/.(n+1) & TG.(c.(n+1))=vs/.(n+1+1)
or SG.(c.(n+1))=vs/. (n+1+1) & TG.(c.(n+1))=vs/.(n+1) by GRAPH_1:def 10;
A40:                  c.(n+1) joins vs1/.(n+1), vs1/.(n+1+1) by A14,A30,
                      A31, Def7;
                      then
                      SG.(c.(n+1))=vs1/.(n+1) & TG. (c.(n+1))=vs1/.(n+1+1
) or SG.(c.(n+1))= vs1/.(n+1+1) & TG.(c.(n+1))=vs1/.(n+1) by GRAPH_1:def 10;
                      hence vs1/.(n+1+1)<>vs/.(n+1+1) by A28,A37,A36,A39,
                      FINSEQ_3:27;
                      c.n joins vs/.n, vs/.(n+1) by A1,A37,A36,Def7;
                      hence thesis by A28,A37,A36,A38,A40,A39,FINSEQ_3:27 ,
                      GRAPH_1:def 10;
                    end;
                  end;
                end;
              end;
A41:          P[0] by FINSEQ_3:27;
A42:          for n holds P[n] from NAT_1:sch 1(A41,A27);
              now
                let x be set;
                hereby
                  assume x in G-VSet rng c;
                  then consider v such that
A43:              x=v and
A44:              ex e being Element of the carrier' of G st e in rng
                  c & (v = (the Source of G).e or v = (the Target of G).e);
                  consider e such that
A45:              e in rng c and
A46:              v = SG.e or v = TG.e by A44;
                  consider d being set such that
A47:              d in dom c and
A48:              e=c.d by A45,FUNCT_1:def 5;
                  reconsider d as Element of NAT by A47;
                  TG.(c.d)=TG.(c.1) & SG.(c.d)=SG.(c.1) or TG.(c.d)=SG.(c
                  .1) & SG.(c.d)=TG.(c.1) by A42,A47;
                  hence x in {SG.(c.1), TG.(c.1)} by A43,A46,A48, TARSKI:
                  def 2;
                end;
                0+1<=len c by A13,NAT_1:13;
                then
A49:            1 in dom c by FINSEQ_3:27;
                then
A50:            c.1 in rng c by FUNCT_1:def 5;
A51:            rng c c= the carrier' of G by FINSEQ_1:def 4;
                then reconsider e=c.1 as Element of the carrier' of G by A50;
                reconsider t = TG.e as Element of G by A50,A51,
                FUNCT_2:7;
                reconsider s = SG.e as Element of G by A50,A51,
                FUNCT_2:7;
                assume x in {SG.(c.1), TG.(c.1)};
                then
A52:            x=s or x =t by TARSKI:def 2;
                e in rng c by A49,FUNCT_1:def 5;
                hence x in G-VSet rng c by A52;
              end;
              then
A53:          G-VSet rng c = {SG.(c.1), TG.(c.1)} by TARSKI:2;
              c.k joins vs1/.k, vs1/.(k+1) by A14,A22,A24,Def7;
              then
A54:          SG.(c.1)=vs1/.1 & TG.(c.1)=vs1/.(k+1) or SG.(c.1)=vs1/.(k+1
              ) & TG.(c.1 )=vs1/.1 by A22,GRAPH_1:def 10;
A55:          c.k joins vs/.k, vs/.(k+1) by A1,A22,A24,Def7;
A56:          now
                let n;
                n in dom c implies TG.(c.n)=TG.(c.1) & SG.(c.n)=SG.(c.1)
                or TG.(c.n)=SG.(c.1) & SG.(c.n)=TG.(c.1) by A42;
                hence n in dom c implies SG.(c.n) <> TG.(c.n) by A19,A22,A26,
                A25,A55,A54, GRAPH_1:def 10;
              end;
              SG.(c.1)=vs/.1 & TG.(c.1)=vs/.(k+1) or SG.(c.1)=vs/.(k+1) &
              TG.(c.1)= vs/.1 by A22,A55,GRAPH_1:def 10;
              then card (G-VSet rng c) = 2 by A19,A22,A26,A25,A54,A53,
              CARD_2:76;
              hence contradiction by A13,A24,A56,Def8;
            end;
          end;
        end;
        suppose
          1<k;
          then 1+1<=k by NAT_1:13;
          then consider k1 being Element of NAT such that
A57:      1<=k1 and
A58:      k1<k and
A59:      k=k1+1 by Th1;
A60:      k<=len vs1 by A19,FINSEQ_3:27;
          then
A61:      k1<=len vs1 by A58,XXREAL_0:2;
          then
A62:      k1 in dom vs1 by A57,FINSEQ_3:27;
A63:      k1<=len c by A15,A59,A60,XREAL_1:8;
          then c.k1 joins vs1/.k1, vs1/.(k1+1) by A14,A57,Def7;
          then
A64:      SG.(c.k1)=vs1/.k1 & TG.(c.k1)=vs1/.k or SG.(c.k1)=vs1/.k & TG.(
          c.k1)= vs1/.k1 by A59,GRAPH_1:def 10;
A65:      vs1/.k1=vs1.k1 by A57,A61,FINSEQ_4:24;
A66:      vs1/.k=vs1.k by A19,PARTFUN1:def 8;
          c.k1 joins vs/.k1, vs/.(k1+1) by A1,A57,A63,Def7;
          then
A67:      SG.(c.k1)=vs/.k1 & TG.(c.k1)=vs/.k or SG.(c.k1)=vs/.k & TG.(c.
          k1)=vs/. k1 by A59,GRAPH_1:def 10;
A68:      vs/.k=vs.k by A17,A19,PARTFUN1:def 8;
          vs/.k1=vs.k1 by A15,A16,A57,A61,FINSEQ_4:24;
          hence contradiction by A19,A20,A58,A62,A65,A66,A68,A64,A67;
        end;
      end;
    end;
  end;
  assume
A69: for vs1 st vs1 is_vertex_seq_of c holds vs1 = vs;
  assume card the carrier of G <> 1;
  then consider x, y such that
A70: x in the carrier of G and
A71: y in the carrier of G and
A72: x<>y by Lm7;
  reconsider y as Element of G by A71;
  reconsider x as Element of G by A70;
  assume
A73: c ={} or c alternates_vertices_in G;
  thus contradiction
  proof
    per cases by A73;
    suppose
A74:  c ={};
      then <*x*> = vs by A69,Th35;
      then
A75:  vs.1 = x by FINSEQ_1:57;
      <*y*> = vs by A69,A74,Th35;
      hence contradiction by A72,A75,FINSEQ_1:57;
    end;
    suppose
      c alternates_vertices_in G;
      then consider vs1,vs2 such that
A76:  vs1<>vs2 and
A77:  vs1 is_vertex_seq_of c and
A78:  vs2 is_vertex_seq_of c and
      for vs st vs is_vertex_seq_of c holds vs=vs1 or vs=vs2 by Th41;
      vs1 = vs by A69,A77;
      hence contradiction by A69,A76,A78;
    end;
  end;
end;

definition
  let G, c;
  assume
A1: card the carrier of G = 1 or c <>{} & not c alternates_vertices_in G;
  func vertex-seq c -> FinSequence of the carrier of G means
  it is_vertex_seq_of c;
  existence by Th36;
  uniqueness by A1,Th42;
end;

theorem Th43:
  vs is_vertex_seq_of c & c1 = c|Seg n & vs1= vs|Seg (n+1) implies
  vs1 is_vertex_seq_of c1
proof
  assume
A1: vs is_vertex_seq_of c;
  then
A2: len vs = len c + 1 by Def7;
  assume that
A3: c1 = c|Seg n and
A4: vs1= vs|Seg (n+1);
  now
    per cases;
    suppose
A5:   n<=len c;
      then
A6:   n+1<=len vs by A2,XREAL_1:8;
      then
A7:   len vs1 = n+1 by A4,FINSEQ_1:21;
A8:   len c1 = n by A3,A5,FINSEQ_1:21;
      hence len vs1 = len c1 + 1 by A4,A6,FINSEQ_1:21;
      let k be Element of NAT;
      assume that
A9:   1<=k and
A10:  k<=len c1;
A11:  1<=k+1 by NAT_1:12;
      then
A12:  vs1/.(k+1)=vs1.(k+1) by A8,A7,A10,FINSEQ_4:24,XREAL_1:9;
      k+1<=len c1 +1 by A10,XREAL_1:9;
      then k+1 in Seg (n+1) by A8,A11;
      then
A13:  vs1.(k+1)=vs.(k+1) by A4,FUNCT_1:72;
A14:  k<=len c by A5,A8,A10,XXREAL_0:2;
      then k<=len vs by A2,NAT_1:12;
      then
A15:  vs/.k=vs.k by A9,FINSEQ_4:24;
A16:  k<=n+1 by A8,A10,NAT_1:12;
      then
A17:  vs1/.k=vs1.k by A7,A9,FINSEQ_4:24;
      k in Seg n by A8,A9,A10;
      then
A18:  c1.k = c.k by A3,FUNCT_1:72;
      k in Seg (n+1) by A9,A16;
      then
A19:  vs1.k=vs.k by A4,FUNCT_1:72;
      vs/.(k+1)=vs.(k+1) by A2,A14,A11,FINSEQ_4:24,XREAL_1:9;
      hence c1.k joins vs1/.k,vs1/.(k+1) by A1,A9,A14,A18,A19,A13,A15,A17
      ,A12,Def7;
    end;
    suppose
A20:  len c <=n;
      then len vs<=n+1 by A2,XREAL_1:8;
      then
A21:  vs1 = vs by A4,FINSEQ_2:23;
      c1 = c by A3,A20,FINSEQ_2:23;
      hence len vs1 = len c1 + 1 & for k being Element of NAT st 1<=k & k<=len
      c1 holds c1.k joins vs1/.k,vs1/.(k+1) by A1,A21,Def7;
    end;
  end;
  hence thesis by Def7;
end;

theorem Th44:
  1<=m & m<=n & n<=len c & q = (m,n)-cut c implies q is Chain of G
proof
  assume that
A1: 1<=m and
A2: m<=n and
A3: n<=len c;
A4: m<=n+1 by A2,NAT_1:12;
  consider vs such that
A5: vs is_vertex_seq_of c by Th36;
  set p9 = (m,n+1)-cut vs;
A6: now
    let k be Element of NAT;
    assume that
A7: 1<=k and
A8: k<=len p9;
    k in dom p9 by A7,A8,FINSEQ_3:27;
    then
A9: p9.k in rng p9 by FUNCT_1:def 5;
A10: rng vs c= the carrier of G by FINSEQ_1:def 4;
    rng p9 c= rng vs by Th11;
    then p9.k in rng vs by A9;
    hence p9.k in the carrier of G by A10;
  end;
  assume
A11: q = (m,n)-cut c;
  then
A12: len q +m-m=n+1-m by A1,A3,A4,Lm2;
A13: len vs = len c + 1 by A5,Def7;
  then
A14: n+1<=len vs by A3,XREAL_1:8;
  then
A15: len p9+m-m=(n+1)+1-m by A1,A4,Def1;
  then
A16: len p9 = n-m+1+1;
A17: now
    1-1<=m-1 by A1,XREAL_1:11;
    then m-1 = m-'1 by XREAL_0:def 2;
    then reconsider m1= m-1 as Element of NAT;
    let k be Element of NAT;
    reconsider i = m1+k as Element of NAT;
    assume that
A18: 1<=k and
A19: k<=len q;
    0+1<=k by A18;
    then consider j such that
    0<=j and
A20: j<len q and
A21: k=j+1 by A19,Th1;
A22: j+1<len p9 by A12,A16,A20,XREAL_1:8;
    i+1 = m+(j+1) by A21;
    then
A23: p9.(k+1) = vs.(i+1) by A1,A4,A14,A22,Def1;
    set v2 = vs/.(i+1);
    set v1 = vs/.i;
A24: 1<=i+1 by NAT_1:12;
A25: i= m+j by A21;
    j<len p9 by A12,A16,A20,NAT_1:13;
    then
A26: p9.k = vs.i by A1,A4,A14,A25,A22,Def1;
    i<=(m-1)+(n-(m-1)) by A12,A19,XREAL_1:8;
    then
A27: i<=len c by A3,XXREAL_0:2;
    then
A28: i<=len vs by A13,NAT_1:12;
    take v1, v2;
A29: i+1<=len vs by A13,A27,XREAL_1:9;
    1-1<=m-1 by A1,XREAL_1:11;
    then
A30: 0+1<=m-1+k by A18,XREAL_1:9;
    then c.i joins v1, v2 by A5,A27,Def7;
    hence
    v1 = p9.k & v2 = p9.(k+1) & q.k joins v1, v2 by A1,A3,A11,A4,A20,A21,
    A25,A30,A28,A24,A29,A26,A23,Lm2,FINSEQ_4:24;
  end;
  thus q is Chain of G
  proof
    hereby
      let k be Element of NAT;
      assume that
A31:  1<=k and
A32:  k<=len q;
      k in dom q by A31,A32,FINSEQ_3:27;
      then
A33:  q.k in rng q by FUNCT_1:def 5;
      rng q c= rng c by A11,Th11;
      then
A34:  q.k in rng c by A33;
      rng c c= the carrier' of G by FINSEQ_1:def 4;
      hence q.k in the carrier' of G by A34;
    end;
    take p9;
    thus len p9 = len q + 1 by A12,A15;
    thus for n st 1<=n & n<=len p9 holds p9.n in the carrier of G by A6;
    thus thesis by A17;
  end;
end;

theorem Th45:
  1<=m & m<=n & n<=len c & c1 = (m,n)-cut c & vs is_vertex_seq_of
  c & vs1 = (m,n+1)-cut vs implies vs1 is_vertex_seq_of c1
proof
  assume that
A1: 1<=m and
A2: m<=n and
A3: n<=len c;
A4: m<=n+1 by A2,NAT_1:12;
  assume
A5: c1 = (m,n)-cut c;
  then
A6: len c1 +m= n+1 by A1,A3,A4,Lm2;
  assume that
A7: vs is_vertex_seq_of c and
A8: vs1 = (m,n+1)-cut vs;
A9: len vs = len c + 1 by A7,Def7;
  then
A10: n+1<=len vs by A3,XREAL_1:8;
  then len vs1 +m = n+1+1 by A1,A8,A4,Def1;
  hence
A11: len vs1 = len c1 + 1 by A6;
  let k be Element of NAT;
  assume that
A12: 1<=k and
A13: k<=len c1;
  0+1<=k by A12;
  then consider j such that
  0<=j and
A14: j<len c1 and
A15: k=j+1 by A13,Th1;
  set i = m+j;
  j<len vs1 by A11,A14,NAT_1:13;
  then
A16: vs1.k = vs.i by A1,A8,A4,A10,A15,Def1;
  m+k<=len c1 +m by A13,XREAL_1:9;
  then m+j+1-1<=len c1 +m-1 by A15,XREAL_1:11;
  then
A17: i<=len c by A3,A6,XXREAL_0:2;
  then i<=len vs by A9,NAT_1:12;
  then
A18: vs/.i=vs.i by A1,FINSEQ_4:24,NAT_1:12;
A19: k<=len c1 by A14,A15,NAT_1:13;
  then
A20: k<=len vs1 by A11,NAT_1:12;
  1<=k+1 by NAT_1:12;
  then
A21: vs1/.(k+1)=vs1.(k+1) by A11,A19,FINSEQ_4:24,XREAL_1:9;
  0+1=1;
  then 1<=k by A15,NAT_1:13;
  then
A22: vs1/.k=vs1.k by A20,FINSEQ_4:24;
  set v2 = vs/.(i+1);
  set v1 = vs/.i;
A23: m+j+1 = m+(j+1);
  1<=i by A1,NAT_1:12;
  then
A24: c.i joins v1, v2 by A7,A17,Def7;
  1<=i+1 by NAT_1:12;
  then
A25: vs/.(i+1)=vs.(i+1) by A9,A17,FINSEQ_4:24,XREAL_1:9;
  j+1<len vs1 by A11,A14,XREAL_1:8;
  then vs1.(k+1) = vs.(i+1) by A1,A8,A4,A10,A15,A23,Def1;
  hence thesis by A1,A3,A5,A4,A14,A15,A24,A16,A18,A25,A22,A21,Lm2;
end;

theorem Th46:
  vs1 is_vertex_seq_of c1 & vs2 is_vertex_seq_of c2 & vs1.len vs1
  = vs2.1 implies c1^c2 is Chain of G
proof
  assume that
A1: vs1 is_vertex_seq_of c1 and
A2: vs2 is_vertex_seq_of c2 and
A3: vs1.len vs1 = vs2.1;
  set q=c1^c2;
  set p=vs1^'vs2;
A4: len vs2 = len c2 + 1 by A2,Def7;
  then vs2 <> {};
  then
A5: len p +1 = len vs1 +len vs2 by Th13;
A6: now
    let n be Element of NAT such that
A7: 1<=n and
A8: n<=len p;
    per cases;
    suppose
A9:   n<=len vs1;
      then
A10:  n in dom vs1 by A7,FINSEQ_3:27;
      p.n = vs1.n by A7,A9,Th14;
      hence p.n in the carrier of G by A10,FINSEQ_2:13;
    end;
    suppose
A11:  n > len vs1;
      then consider m being Nat such that
A12:  n = len vs1 + m by NAT_1:10;
      reconsider m as Element of NAT by ORDINAL1:def 13;
      m <> 0 by A11,A12;
      then
A13:  0+1<=m by NAT_1:13;
      len vs1 + m<=len vs1 + (len vs2 - 1) by A5,A8,A12;
      then m<=len vs2 - 1 by XREAL_1:8;
      then
A14:  m+1<=len vs2 - 1+1 by XREAL_1:8;
      1<=m+1 by NAT_1:12;
      then
A15:  m+1 in dom vs2 by A14,FINSEQ_3:27;
      m<len vs2 by A14,NAT_1:13;
      then p.(len vs1 + m) = vs2.(m+1) by A13,Th15;
      hence p.n in the carrier of G by A12,A15,FINSEQ_2:13;
    end;
  end;
A16: len vs1 = len c1 + 1 by A1,Def7;
A17: now
    let n be Element of NAT;
    assume that
A18: 1<=n and
A19: n<=len q;
A20: n in dom q by A18,A19,FINSEQ_3:27;
    per cases by A20,FINSEQ_1:38;
    suppose
A21:  n in dom c1;
      set v2=vs1/.(n+1);
      set v1=vs1/.n;
A22:  q.n = c1.n by A21,FINSEQ_1:def 7;
A23:  1<=n by A21,FINSEQ_3:27;
A24:  n<=len c1 by A21,FINSEQ_3:27;
      then n+1<=len c1+1 by XREAL_1:8;
      then
A25:  n+1<=len vs1 by A1,Def7;
      then
A26:  vs1/.(n+1)=vs1.(n+1) by FINSEQ_4:24,NAT_1:12;
A27:  n<=len vs1 by A16,A24,NAT_1:12;
      then
A28:  vs1/.n=vs1.n by A23,FINSEQ_4:24;
A29:  p.(n+1) = vs1.(n+1) by A25,Th14,NAT_1:12;
A30:  p.n = vs1.n by A23,A27,Th14;
      c1.n joins v1, v2 by A1,A23,A24,Def7;
      hence
      ex v1, v2 being Element of G st v1 = p.n & v2 = p.(n
      +1) & q.n joins v1, v2 by A22,A28,A26,A30,A29;
    end;
    suppose
      ex k being Nat st k in dom c2 & n=len c1 + k;
      then consider k being Nat such that
A31:  k in dom c2 and
A32:  n = len c1 + k;
A33:  q.n = c2.k by A31,A32,FINSEQ_1:def 7;
A34:  1<=k+1 by NAT_1:12;
A35:  1<=k by A31,FINSEQ_3:27;
A36:  k<=len c2 by A31,FINSEQ_3:27;
      then
A37:  k<=len vs2 by A4,NAT_1:12;
      reconsider k as Element of NAT by A31;
A38:  0+1<=k by A31,FINSEQ_3:27;
A39:  vs2/.(k+1)=vs2.(k+1) by A4,A36,A34,FINSEQ_4:24,XREAL_1:9;
A40:  k<len vs2 by A4,A36,NAT_1:13;
      k<=len vs2 by A4,A36,NAT_1:12;
      then consider j such that
      0<=j and
A41:  j<len vs2 and
A42:  k=j+1 by A38,Th1;
A43:  p.n = vs2.k
      proof
        per cases by A35,XXREAL_0:1;
        suppose
A44:      1=k;
          0+1<=len vs1 by A16,NAT_1:13;
          hence thesis by A3,A16,A32,A44,Th14;
        end;
        suppose
          1<k;
          then
A45:      1<=j by A42,NAT_1:13;
          thus p.n = p.(len vs1 +j) by A16,A32,A42
            .= vs2.k by A41,A42,A45,Th15;
        end;
      end;
      set v2 = vs2/.(k+1);
      set v1 = vs2/.k;
A46:  c2.k joins v1, v2 by A2,A35,A36,Def7;
A47:  vs2/.k=vs2.k by A35,A37,FINSEQ_4:24;
      p.(n+1) = p.(len c1 +1+k) by A32
        .= vs2.(k+1) by A16,A35,A40,Th15;
      hence
      ex v1, v2 being Element of G st v1 = p.n & v2 = p.(n
      +1) & q.n joins v1, v2 by A33,A47,A39,A46,A43;
    end;
  end;
A48: len p = len c1 + len c2 + 1 by A16,A4,A5
    .= len q + 1 by FINSEQ_1:35;
  thus c1^c2 is Chain of G
  proof
    hereby
      let n be Element of NAT;
      assume that
A49:  1<=n and
A50:  n<=len q;
A51:  n in dom q by A49,A50,FINSEQ_3:27;
      per cases by A51,FINSEQ_1:38;
      suppose
A52:    n in dom c1;
        then
A53:    q.n = c1.n by FINSEQ_1:def 7;
A54:    n<=len c1 by A52,FINSEQ_3:27;
        1<=n by A52,FINSEQ_3:27;
        hence (c1^c2).n in the carrier' of G by A54,A53,GRAPH_1:def 12;
      end;
      suppose
        ex k being Nat st k in dom c2 & n=len c1 + k;
        then consider k being Nat such that
A55:    k in dom c2 and
A56:    n = len c1 + k;
A57:    1<=k by A55,FINSEQ_3:27;
A58:    k<= len c2 by A55,FINSEQ_3:27;
        q.n = c2.k by A55,A56,FINSEQ_1:def 7;
        hence (c1^c2).n in the carrier' of G by A55,A57,A58,GRAPH_1:def 12;
      end;
    end;
    thus thesis by A48,A6,A17;
  end;
end;

theorem Th47:
  vs1 is_vertex_seq_of c1 & vs2 is_vertex_seq_of c2 & vs1.len vs1
  = vs2.1 & c = c1^c2 & vs = vs1^'vs2 implies vs is_vertex_seq_of c
proof
  assume that
A1: vs1 is_vertex_seq_of c1 and
A2: vs2 is_vertex_seq_of c2 and
A3: vs1.len vs1 = vs2.1;
  set p=vs1^'vs2;
  set q=c1^c2;
  assume that
A4: c = c1^c2 and
A5: vs = vs1^'vs2;
A6: len vs1 = len c1 + 1 by A1,Def7;
A7: len vs2 = len c2 + 1 by A2,Def7;
  then vs2 <> {};
  then len p +1 = len vs1 +len vs2 by Th13;
  then
A8: len p = len c1 + len c2 + 1 by A6,A7
    .= len q + 1 by FINSEQ_1:35;
  reconsider p as FinSequence of the carrier of G;
  now
    let n be Element of NAT;
    assume that
A9: 1<=n and
A10: n<=len q;
A11: n in dom q by A9,A10,FINSEQ_3:27;
    n<=len p by A8,A10,NAT_1:12;
    then
A12: p/.n=p.n by A9,FINSEQ_4:24;
    1<=n+1 by NAT_1:12;
    then
A13: p/.(n+1)=p.(n+1) by A8,A10,FINSEQ_4:24,XREAL_1:9;
    per cases by A11,FINSEQ_1:38;
    suppose
A14:  n in dom c1;
      set v2=vs1/.(n+1);
      set v1=vs1/.n;
A15:  1<=n by A14,FINSEQ_3:27;
A16:  n<=len c1 by A14,FINSEQ_3:27;
      then n+1<=len c1+1 by XREAL_1:8;
      then
A17:  n+1<=len vs1 by A1,Def7;
      then
A18:  vs1/.(n+1)=vs1.(n+1) by FINSEQ_4:24,NAT_1:12;
A19:  n<=len vs1 by A6,A16,NAT_1:12;
      then
A20:  vs1/.n=vs1.n by A15,FINSEQ_4:24;
A21:  p.(n+1) = vs1.(n+1) by A17,Th14,NAT_1:12;
A22:  p.n = vs1.n by A15,A19,Th14;
      c1.n joins v1, v2 by A1,A15,A16,Def7;
      hence q.n joins p/.n, p/.(n+1) by A12,A13,A14,A20,A18,A22,A21,
      FINSEQ_1:def 7;
    end;
    suppose
      ex k being Nat st k in dom c2 & n=len c1 + k;
      then consider k being Element of NAT such that
A23:  k in dom c2 and
A24:  n = len c1 + k;
A25:  0+1<=k by A23,FINSEQ_3:27;
      set v2 = vs2/.(k+1);
      set v1 = vs2/.k;
A26:  k<=len c2 by A23,FINSEQ_3:27;
      then
A27:  k<len vs2 by A7,NAT_1:13;
A28:  1<=k by A23,FINSEQ_3:27;
      then
A29:  c2.k joins v1, v2 by A2,A26,Def7;
      k<=len vs2 by A7,A26,NAT_1:12;
      then consider j such that
      0<=j and
A30:  j<len vs2 and
A31:  k=j+1 by A25,Th1;
A32:  p.n = vs2.k
      proof
        per cases by A28,XXREAL_0:1;
        suppose
A33:      1=k;
          0+1<=len vs1 by A6,NAT_1:13;
          hence thesis by A3,A6,A24,A33,Th14;
        end;
        suppose
          1<k;
          then
A34:      1<=j by A31,NAT_1:13;
          thus p.n = p.(len vs1 +j) by A6,A24,A31
            .= vs2.k by A30,A31,A34,Th15;
        end;
      end;
      1<=k+1 by NAT_1:12;
      then
A35:  vs2/.(k+1)=vs2.(k+1) by A7,A26,FINSEQ_4:24,XREAL_1:9;
      k<=len vs2 by A7,A26,NAT_1:12;
      then
A36:  vs2/.k=vs2.k by A28,FINSEQ_4:24;
      p.(n+1) = p.(len c1 +1+k) by A24
        .= vs2.(k+1) by A6,A28,A27,Th15;
      hence q.n joins p/.n,p/.(n+1) by A12,A13,A23,A24,A36,A35,A29,A32,
      FINSEQ_1:def 7;
    end;
  end;
  hence thesis by A4,A5,A8,Def7;
end;

begin :: Simple chains, paths and oriented chains

Lm8: <*v*> is_vertex_seq_of {}
proof
  set p = <*v*>, ec = {};
A1: for n st 1<=n & n<=len ec holds ec.n joins p/.n,p/.(n+1);
  len p = 0 + 1 by FINSEQ_1:56
    .= len ec + 1;
  hence thesis by A1,Def7;
end;

definition
  let G;
  let IT be Chain of G;
  attr IT is simple means
  :Def10:
  ex vs st vs is_vertex_seq_of IT & for n,m st
  1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs;
end;

registration
  let G;
  cluster simple Chain of G;
  existence
  proof
    consider x being Element of G;
    consider q being empty Chain of G;
    take q;
    reconsider p =<*x*> as FinSequence of the carrier of G;
    take p;
    thus p is_vertex_seq_of q by Lm8;
    let n,m;
    assume that
A1: 1<=n and
A2: n<m and
A3: m<=len p and
    p.n = p.m;
    1<m by A1,A2,XXREAL_0:2;
    hence thesis by A3,FINSEQ_1:56;
  end;
end;

reserve sc for simple Chain of G;

canceled;

theorem
  sc|(Seg n) is simple Chain of G
proof
  reconsider q9 = sc|Seg n as Chain of G by GRAPH_1:4;
  consider vs such that
A1: vs is_vertex_seq_of sc and
A2: for n, m st 1<=n & n < m & m<=len vs & vs.n = vs.m holds n=1 & m=
  len vs by Def10;
  reconsider p9 = vs|Seg(n+1) as FinSequence of the carrier of G by FINSEQ_1:
  23;
  now
    take p9;
    thus p9 is_vertex_seq_of q9 by A1,Th43;
    let k, m;
    assume that
A3: 1<=k and
A4: k < m and
A5: m<=len p9 and
A6: p9.k = p9.m;
    k<=len p9 by A4,A5,XXREAL_0:2;
    then
A7: p9.k = vs.k by A3,Th2;
    1<=m by A3,A4,XXREAL_0:2;
    then
A8: p9.m = vs.m by A5,Th2;
A9: len p9<=len vs by Th2;
    then
A10: m<=len vs by A5,XXREAL_0:2;
    hence k=1 by A2,A3,A4,A6,A7,A8;
    len p9 = len vs or len p9 < len vs by A9,XXREAL_0:1;
    hence m = len p9 by A2,A3,A4,A5,A6,A7,A8,A10;
  end;
  hence thesis by Def10;
end;

theorem Th50:
  2<len sc & vs1 is_vertex_seq_of sc & vs2 is_vertex_seq_of sc implies vs1=vs2
proof
  assume that
A1: 2<len sc and
A2: vs1 is_vertex_seq_of sc and
A3: vs2 is_vertex_seq_of sc;
A4: len vs1 = len sc +1 by A2,Def7;
  defpred P[Nat] means $1 in dom vs1 & vs1.$1<>vs2.$1;
  set TG = the Target of G;
  set SG = the Source of G;
A5: Seg len vs1 = dom vs1 by FINSEQ_1:def 3;
A6: Seg len vs2 = dom vs2 by FINSEQ_1:def 3;
A7: len vs2 = len sc +1 by A3,Def7;
  assume
A8: vs1 <> vs2;
  then
A9: ex j being Nat st P[j] by A4,A7,FINSEQ_2:10;
  consider k be Nat such that
A10: P[k] and
A11: for n be Nat st P[n] holds k<=n from NAT_1:sch 5 (A9);
A12: 1<=k by A10,FINSEQ_3:27;
  per cases by A12,XXREAL_0:1;
  suppose
A13: k=1;
    set v23=vs2/.(1+1+1);
    set v22=vs2/.(1+1);
    set v21=vs2/.1;
    set v13=vs1/.(1+1+1);
    set v12=vs1/.(1+1);
    set v11=vs1/.1;
A14: 1+1+1<=len vs1 by A1,A4,XREAL_1:8;
    then
A15: v13=vs1.(1+1+1) by FINSEQ_4:24;
A16: 1<=len vs1 by A14,XXREAL_0:2;
    then
A17: v11=vs1.1 by FINSEQ_4:24;
A18: 1<=len sc by A1,XXREAL_0:2;
    then
A19: sc.1 joins v21,v22 by A3,Def7;
    sc.1 joins v11,v12 by A2,A18,Def7;
    then
A20: v11=v21 & v12=v22 or v11=v22 & v12=v21 by A19,Th32;
A21: v21=vs2.1 by A4,A7,A16,FINSEQ_4:24;
    consider vs such that
A22: vs is_vertex_seq_of sc and
A23: for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len
    vs by Def10;
    sc <>{} by A1;
    then
A24: vs = vs1 or vs = vs2 by A2,A3,A8,A22,Th37;
A25: v23=vs2.(1+1+1) by A4,A7,A14,FINSEQ_4:24;
A26: sc.2 joins v22,v23 by A1,A3,Def7;
A27: sc.2 joins v12,v13 by A1,A2,Def7;
    then
A28: v21=v23 by A10,A13,A17,A21,A26,A20,Th32;
    v11=v13 by A10,A13,A17,A21,A27,A26,A20,Th32;
    then 1+1+1 = len vs by A4,A7,A14,A17,A15,A21,A25,A28,A23,A24;
    hence contradiction by A1,A4,A7,A24;
  end;
  suppose
    1<k;
    then 1+1<=k by NAT_1:13;
    then consider k1 being Element of NAT such that
A29: 1<=k1 and
A30: k1<k and
A31: k=k1+1 by Th1;
A32: k<=len vs1 by A10,FINSEQ_3:27;
    then
A33: k1<=len vs1 by A30,XXREAL_0:2;
    then
A34: k1 in dom vs1 by A29,FINSEQ_3:27;
A35: vs1/.k1=vs1.k1 by A29,A33,FINSEQ_4:24;
A36: vs2/.k=vs2.k by A4,A7,A5,A6,A10,PARTFUN1:def 8;
A37: vs1/.k=vs1.k by A10,PARTFUN1:def 8;
A38: k1<=len sc by A4,A31,A32,XREAL_1:8;
    then sc.k1 joins vs1/.k1, vs1/.(k1+1) by A2,A29,Def7;
    then
A39: SG.(sc.k1)=vs1/.k1 & TG.(sc.k1)=vs1/.k or SG.(sc.k1)=vs1/.k & TG.(sc.
    k1)=vs1/.k1 by A31,GRAPH_1:def 10;
    sc.k1 joins vs2/.k1, vs2/.(k1+1) by A3,A29,A38,Def7;
    then
A40: SG.(sc.k1)=vs2/.k1 & TG.(sc.k1)=vs2/.k or SG.(sc.k1)=vs2/.k & TG.(sc.
    k1)=vs2/.k1 by A31,GRAPH_1:def 10;
    vs2/.k1=vs2.k1 by A4,A7,A29,A33,FINSEQ_4:24;
    hence contradiction by A10,A11,A30,A34,A35,A37,A36,A39,A40;
  end;
end;

theorem
  vs is_vertex_seq_of sc implies for n, m st 1<=n & n<m & m<=len vs & vs
  .n = vs.m holds n=1 & m=len vs
proof
  assume
A1: vs is_vertex_seq_of sc;
  consider vs1 such that
A2: vs1 is_vertex_seq_of sc and
A3: for n,m st 1<=n & n<m & m<=len vs1 & vs1.n=vs1.m holds n=1 & m=len
  vs1 by Def10;
  per cases;
  suppose
A4: len sc <= 2;
    thus thesis
    proof
      per cases by A4,NAT_1:27;
      suppose
A5:     len sc = 0;
        let n,m;
        len vs = 0+1 by A1,A5,Def7;
        hence thesis by XXREAL_0:2;
      end;
      suppose
        len sc = 1;
        then
A6:     len vs = 1+1 by A1,Def7;
        let n,m;
        assume that
A7:     1<=n and
A8:     n<m and
A9:     m<=len vs and
        vs.n=vs.m;
A10:    n+1<=m by A8,NAT_1:13;
        then n+1<=1+1 by A6,A9,XXREAL_0:2;
        then n<=1 by XREAL_1:8;
        then n=1 by A7,XXREAL_0:1;
        hence thesis by A6,A9,A10,XXREAL_0:1;
      end;
      suppose
A11:    len sc = 2;
        set v12=vs1/.(1+1);
        set v2=vs/.(1+1);
        set v11=vs1/.1;
A12:    sc.1 joins v11,v12 by A2,A11,Def7;
        set v1=vs/.1;
        sc.1 joins v1, v2 by A1,A11,Def7;
        then
A13:    v1=v11 & v2=v12 or v1=v12 & v2=v11 by A12,Th32;
A14:    len vs = 1+1+1 by A1,A11,Def7;
        then
A15:    v2=vs.(1+1) by FINSEQ_4:24;
        set v3=vs/.(1+1+1);
        set v13=vs1/.(1+1+1);
A16:    sc.2 joins v12,v13 by A2,A11,Def7;
        sc.2 joins v2, v3 by A1,A11,Def7;
        then
A17:    v2=v12 & v3=v13 or v2=v13 & v3=v12 by A16,Th32;
A18:    len vs1 = 1+1+1 by A2,A11,Def7;
        then
A19:    v11=vs1.1 by FINSEQ_4:24;
A20:    v13=vs1.(1+1+1) by A18,FINSEQ_4:24;
A21:    v12=vs1.(1+1) by A18,FINSEQ_4:24;
        let n,m;
        assume that
A22:    1<=n and
A23:    n<m and
A24:    m<=len vs and
A25:    vs.n=vs.m;
        n+1<=m by A23,NAT_1:13;
        then n+1<=1+1+1 by A14,A24,XXREAL_0:2;
        then
A26:    n<=1+1 by XREAL_1:8;
A27:    v3=vs.(1+1+1) by A14,FINSEQ_4:24;
A28:    v1=vs.1 by A14,FINSEQ_4:24;
        thus thesis
        proof
          per cases by A22,A26,NAT_1:9;
          suppose
A29:        n=1;
            1<m by A22,A23,XXREAL_0:2;
            then
A30:        1+1<=m by NAT_1:13;
            thus thesis
            proof
              per cases by A14,A24,A30,NAT_1:9;
              suppose
                m=1+1;
                hence thesis by A3,A18,A28,A15,A19,A21,A13,A25,A29;
              end;
              suppose
                m=1+1+1;
                hence thesis by A1,A11,A29,Def7;
              end;
            end;
          end;
          suppose
A31:        n=1+1;
            then 1+1+1<=m by A23,NAT_1:13;
            then m=1+1+1 by A14,A24,XXREAL_0:1;
            hence thesis by A3,A18,A15,A27,A21,A20,A17,A25,A31;
          end;
        end;
      end;
    end;
  end;
  suppose
    2<len sc;
    then vs=vs1 by A1,A2,Th50;
    hence thesis by A3;
  end;
end;
 
:: A chain may have more than one simple chain spanning its endpoints.
:: The following chain:
::             .--->.
::             ^    |
::             |    v
::        .--->.<---.--->.
::             |    ^
::             v    |
::             .--->.
:: is a case in point:

theorem Th52:
  not c is simple Chain of G & vs is_vertex_seq_of c implies ex fc
  being Subset of c, fvs being Subset of vs, c1, vs1 st len c1 < len c & vs1
is_vertex_seq_of c1 & len vs1 < len vs & vs.1 = vs1.1 & vs.len vs = vs1.len vs1
  & Seq fc = c1 & Seq fvs = vs1
proof
  assume that
A1: not c is simple Chain of G and
A2: vs is_vertex_seq_of c;
  consider n, m being Element of NAT such that
A3: 1<=n and
A4: n<m and
A5: m<=len vs and
A6: vs.n=vs.m and
A7: n<>1 or m<>len vs by A1,A2,Def10;
A8: m-n>n-n by A4,XREAL_1:11;
A9: len vs = len c + 1 by A2,Def7;
  reconsider n1 = n-'1 as Element of NAT;
A10: 1-1<=n-1 by A3,XREAL_1:11;
  then
A11: n-1 = n-'1 by XREAL_0:def 2;
  then
A12: n1+1 = n;
  per cases by A7;
  suppose
A13: n<>1 & m <> len vs;
    set DR = {kk where kk is Element of NAT: m<=kk & kk<= len c};
    set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n1};
    set domfvs = {k: 1<=k & k<=n or m+1<=k & k<= len vs};
    reconsider p2 = (m,len c+1)-cut vs as FinSequence of the carrier of G;
    reconsider pp = (1,n)-cut vs as FinSequence of the carrier of G;
    set p29 = (m+1, len c +1)-cut vs;
A14: 1<=m+1 by NAT_1:12;
A15: 1<=m by A3,A4,XXREAL_0:2;
    then 1-1<=m-1 by XREAL_1:11;
    then m-'1 = m-1 by XREAL_0:def 2;
    then reconsider m1 = m-1 as Element of NAT;
A16: m < len vs by A5,A13,XXREAL_0:1;
    then
A17: m<=len c by A9,NAT_1:13;
    then reconsider c2 = (m,len c)-cut c as Chain of G by A15,Th44;
A18: len c2 +m = len c +1 by A15,A17,Def1;
    deffunc F(set) = c.$1;
    set domfc = {k: 1<=k & k<=n1 or m<=k & k<= len c};
    consider fc being Function such that
A19: dom fc = domfc and
A20: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
    n < len vs by A4,A5,XXREAL_0:2;
    then
A21: n-1 < len c +1-1 by A9,XREAL_1:11;
    domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider kk being Element of NAT such that
A22:  x = kk and
A23:  1<=kk & kk<=n1 or m<=kk & kk<= len c;
A24:  1<=kk by A15,A23,XXREAL_0:2;
      kk<=len c by A11,A21,A23,XXREAL_0:2;
      hence thesis by A22,A24;
    end;
    then reconsider fc as FinSubsequence by A19,FINSEQ_1:def 12;
A25: fc c= c
    proof
      let p be set;
      assume
A26:  p in fc;
      then consider x, y such that
A27:  [x,y] = p by RELAT_1:def 1;
A28:  x in dom fc by A26,A27,FUNCT_1:8;
      then consider kk being Element of NAT such that
A29:  x = kk and
A30:  1<=kk & kk<=n1 or m<=kk & kk<= len c by A19;
A31:  1<=kk by A15,A30,XXREAL_0:2;
      kk<=len c by A11,A21,A30,XXREAL_0:2;
      then
A32:  x in dom c by A29,A31,FINSEQ_3:27;
      y = fc.x by A26,A27,FUNCT_1:8;
      then y = c.kk by A19,A20,A28,A29;
      hence thesis by A27,A29,A32,FUNCT_1:8;
    end;
    1 < n by A3,A13,XXREAL_0:1;
    then 1 + 1<=n by NAT_1:13;
    then
A33: 1+1-1<=n-1 by XREAL_1:11;
    then reconsider c1 = (1,n1)-cut c as Chain of G by A11,A21,Th44;
    reconsider fc as Subset of c by A25;
    take fc;
A34: pp is_vertex_seq_of c1 by A2,A12,A33,A21,Th45;
    then
A35: len pp = len c1 + 1 by Def7;
    now
      let x be set;
      hereby
        assume x in domfc;
        then ex k being Element of NAT st x=k &( 1<=k & k<=n1 or m<=k & k<=
        len c);
        then x in DL or x in DR;
        hence x in DL \/ DR by XBOOLE_0:def 3;
      end;
      assume
A36:  x in DL \/ DR;
      per cases by A36,XBOOLE_0:def 3;
      suppose
        x in DL;
        then ex k being Element of NAT st x=k & 1<=k & k<=n1;
        hence x in domfc;
      end;
      suppose
        x in DR;
        then ex k being Element of NAT st x=k & m<=k & k<=len c;
        hence x in domfc;
      end;
    end;
    then
A37: domfc = DL \/ DR by TARSKI:2;
A38: DL c= Seg len c & DR c= Seg len c
    proof
      hereby
        let x be set;
        assume x in DL;
        then consider k being Element of NAT such that
A39:    x=k and
A40:    1<=k and
A41:    k<=n1;
        k<=len c by A11,A21,A41,XXREAL_0:2;
        hence x in Seg len c by A39,A40;
      end;
      let x be set;
      assume x in DR;
      then consider k being Element of NAT such that
A42:  x=k and
A43:  m<=k and
A44:  k<=len c;
      1<=k by A15,A43,XXREAL_0:2;
      hence thesis by A42,A44;
    end;
    then reconsider DR as finite set;
    rng Sgm DR = DR by A38,FINSEQ_1:def 13;
    then
A45: rng Sgm DR c= dom fc by A19,A37,XBOOLE_1:7;
    reconsider DL as finite set by A38;
    set SL = Sgm DL;
A46: 1<=m by A3,A4,XXREAL_0:2;
    set SR = Sgm DR;
A47: len Sgm DR = card DR by A38,FINSEQ_3:44;
A48: m<=len c by A9,A16,NAT_1:13;
    then
A49: m-m<=len c -m by XREAL_1:11;
    then len c2 -'1 = len c2 -1 by A18,XREAL_0:def 2;
    then reconsider lc21 = len c2 -1 as Element of NAT;
    -(-(m-n)) = m-n;
    then
A50: -(m-n) < 0 by A8;
A51: m = m1 +1;
    then m1<=m by NAT_1:12;
    then
A52: p2 = (m1+1, m)-cut vs ^ (m+1, len c +1)-cut vs by A5,A9,Th8;
A53: p2 is_vertex_seq_of c2 by A2,A15,A17,Th45;
    then
A54: len p2 = len c2 + 1 by Def7;
    then 1<=len p2 by NAT_1:12;
    then 1-1<=len p2 -1 by XREAL_1:11;
    then len p2 -'1 = len p2 -1 by XREAL_0:def 2;
    then reconsider lp21 = len p2 -1 as Element of NAT;
    0+1=1;
    then
A55: 1<=len p2 by A54,NAT_1:13;
    m-m<=len c -m by A48,XREAL_1:11;
    then 0+1<=len c -m+1 by XREAL_1:8;
    then
A56: 1<len p2 by A54,A18,NAT_1:13;
    lp21 -'1 = lp21 -1 by A54,A18,A49,XREAL_0:def 2;
    then reconsider lp22 = lp21 -1 as Element of NAT;
A57: m+1+lp22 = m+lp21;
    m+lc21 = len c by A18;
    then
A58: card DR = len c2 + -1 +1 by Th4
      .= len c2;
A59: m+lc21 = m1+len c2;
    now
      let p be set;
      hereby
        assume
A60:    p in c2;
        then consider x, y being set such that
A61:    p=[x,y] by RELAT_1:def 1;
A62:    y=c2.x by A60,A61,FUNCT_1:8;
A63:    x in dom c2 by A60,A61,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A64:    k<=len c2 by A63,FINSEQ_3:27;
        1<=k by A63,FINSEQ_3:27;
        then
A65:    m1+k=SR.k by A51,A18,A59,A64,Th5;
        0+1<=k by A63,FINSEQ_3:27;
        then consider i being Element of NAT such that
        0<=i and
A66:    i<len c2 and
A67:    k=i+1 by A64,Th1;
A68:    x in dom SR by A58,A47,A63,FINSEQ_3:31;
        then
A69:    SR.k in rng SR by FUNCT_1:def 5;
        then
A70:    x in dom (fc*SR) by A45,A68,FUNCT_1:21;
        then (fc*SR).x = fc.(m1+k) by A65,FUNCT_1:22
          .= c.(m+i) by A45,A65,A69,A67,GRFUNC_1:8
          .= y by A15,A17,A62,A66,A67,Def1;
        hence p in fc*(Sgm DR) by A61,A70,FUNCT_1:8;
      end;
      assume
A71:  p in fc*(Sgm DR);
      then consider x,y being set such that
A72:  p=[x,y] by RELAT_1:def 1;
A73:  y = (fc*SR).x by A71,A72,FUNCT_1:8;
A74:  x in dom (fc*SR) by A71,A72,FUNCT_1:8;
      then
A75:  x in dom SR by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A76:  k<=len c2 by A58,A47,A75,FINSEQ_3:27;
      1<=k by A75,FINSEQ_3:27;
      then
A77:  m1+k=SR.k by A51,A18,A59,A76,Th5;
A78:  k in dom c2 by A58,A47,A75,FINSEQ_3:31;
A79:  SR.x in dom fc by A74,FUNCT_1:21;
      0+1<=k by A75,FINSEQ_3:27;
      then consider i being Element of NAT such that
      0<=i and
A80:  i<len c2 and
A81:  k=i+1 by A76,Th1;
      c2.k = c.(m1+1+i) by A15,A17,A80,A81,Def1
        .= fc.(SR.k) by A79,A77,A81,GRFUNC_1:8
        .= y by A73,A75,FUNCT_1:23;
      hence p in c2 by A72,A78,FUNCT_1:8;
    end;
    then
A82: c2 = fc*(Sgm DR) by TARSKI:2;
    now
      let i,j;
      assume i in DL;
      then consider k being Element of NAT such that
A83:  k=i and
      1<=k and
A84:  k<=n1;
      assume j in DR;
      then
A85:  ex l being Element of NAT st l=j & m<=l & l<=len c;
      n1<m by A4,A12,NAT_1:13;
      then k<m by A84,XXREAL_0:2;
      hence i < j by A83,A85,XXREAL_0:2;
    end;
    then
A86: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A38,FINSEQ_3:48;
    set DR = {kk where kk is Element of NAT: m+1<=kk & kk<= len vs};
A87: 1<=m+1 by NAT_1:12;
    deffunc F(set) = vs.$1;
    consider fvs being Function such that
A88: dom fvs = domfvs and
A89: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
A90: n<=len vs by A4,A5,XXREAL_0:2;
    domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider kk being Element of NAT such that
A91:  x = kk and
A92:  1<=kk & kk<=n or m+1<=kk & kk<= len vs;
A93:  kk<=len vs by A90,A92,XXREAL_0:2;
      1<=m+1 by NAT_1:12;
      then 1<=kk by A92,XXREAL_0:2;
      hence thesis by A91,A93;
    end;
    then reconsider fvs as FinSubsequence by A88,FINSEQ_1:def 12;
    fvs c= vs
    proof
      let p be set;
      assume
A94:  p in fvs;
      then consider x, y such that
A95:  [x,y] = p by RELAT_1:def 1;
A96:  x in dom fvs by A94,A95,FUNCT_1:8;
      then consider kk being Element of NAT such that
A97:  x = kk and
A98:  1<=kk & kk<=n or m+1<=kk & kk<= len vs by A88;
A99:  kk<=len vs by A90,A98,XXREAL_0:2;
      1<=m+1 by NAT_1:12;
      then 1<=kk by A98,XXREAL_0:2;
      then
A100: x in dom vs by A97,A99,FINSEQ_3:27;
      y = fvs.x by A94,A95,FUNCT_1:8;
      then y = vs.kk by A88,A89,A96,A97;
      hence thesis by A95,A97,A100,FUNCT_1:8;
    end;
    then reconsider fvs as Subset of vs;
A101: len c + 1<=len vs by A2,Def7;
A102: m<=len c + 1 by A2,A5,Def7;
    then
A103: p2.1 = vs.m by A46,A101,Th12;
A104: pp.len pp = vs.n by A3,A90,Th12;
    then reconsider c9 = c1^c2 as Chain of G by A6,A34,A53,A103,Th46;
    take fvs;
    take c9;
    take p1=pp^'p2;
A105: 1<=n1+1 by NAT_1:12;
    then
A106: len c1 +1 = n1+1 by A11,A21,Lm2;
    then len c1 = n - 1 by A10,XREAL_0:def 2;
    then len c9 = (n+-1)+(len c +(-m +1)) by A18,FINSEQ_1:35
      .= len c + (n+ -m );
    hence
A107: len c9 < len c by A50,XREAL_1:32;
A108: Sgm DL = idseq n1 by FINSEQ_3:54;
    then
A109: dom Sgm DL = Seg n1 by RELAT_1:71;
    now
      let p be set;
      hereby
        assume
A110:   p in c1;
        then consider x, y being set such that
A111:   p=[x,y] by RELAT_1:def 1;
A112:   y=c1.x by A110,A111,FUNCT_1:8;
A113:   x in dom c1 by A110,A111,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A114:   k<=len c1 by A113,FINSEQ_3:27;
A115:   1<=k by A113,FINSEQ_3:27;
        then
A116:   k in domfc by A106,A114;
        0+1<=k by A113,FINSEQ_3:27;
        then consider i being Element of NAT such that
        0<=i and
A117:   i<n1 and
A118:   k=i+1 by A106,A114,Th1;
A119:   x in dom SL by A106,A109,A115,A114;
        then
A120:   k=SL.k by A108,A109,FUNCT_1:35;
        then
A121:   x in dom (fc*SL) by A19,A119,A116,FUNCT_1:21;
        then (fc*SL).x = fc.k by A120,FUNCT_1:22
          .= c.(1+i) by A19,A116,A118,GRFUNC_1:8
          .= y by A11,A21,A105,A106,A112,A117,A118,Lm2;
        hence p in fc*(Sgm DL) by A111,A121,FUNCT_1:8;
      end;
      assume
A122: p in fc*(Sgm DL);
      then consider x,y being set such that
A123: p=[x,y] by RELAT_1:def 1;
A124: y = (fc*SL).x by A122,A123,FUNCT_1:8;
A125: x in dom (fc*SL) by A122,A123,FUNCT_1:8;
      then
A126: (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A127: x in dom SL by A125,FUNCT_1:21;
      then consider k such that
A128: k=x and
A129: 1<=k and
A130: k<=n1 by A109;
A131: k in dom fc by A19,A129,A130;
A132: k in dom c1 by A106,A129,A130,FINSEQ_3:27;
A133: k=SL.k by A108,A109,A127,A128,FUNCT_1:35;
      0+1<=k by A129;
      then ex i being Element of NAT st 0<=i & i<n1 & k=i+1 by A130,
      Th1;
      then c1.k = c.k by A11,A21,A105,A106,Lm2
        .= y by A124,A126,A128,A131,A133,GRFUNC_1:8;
      hence p in c1 by A123,A128,A132,FUNCT_1:8;
    end;
    then
A134: c1 = fc*(Sgm DL) by TARSKI:2;
    thus p1 is_vertex_seq_of c9 by A6,A34,A53,A103,A104,Th47;
    then len p1 = len c9 + 1 by Def7;
    hence len p1 < len vs by A9,A107,XREAL_1:8;
    1<=1 + len c1 by NAT_1:12;
    then 1<=len pp by A34,Def7;
    then p1.1 = pp.1 by Th14;
    hence vs.1 = p1.1 by A3,A90,Th12;
    p2.len p2 = vs.(len c + 1) by A46,A102,A101,Th12;
    hence vs.len vs = p1.len p1 by A9,A56,Th16;
A135: p2 = (0+1, len p2)-cut p2 by Th7
      .= (0+1, 1)-cut p2 ^ (1+1, len p2)-cut p2 by A55,Th8;
    rng Sgm DL = DL by A38,FINSEQ_1:def 13;
    then rng Sgm DL c= dom fc by A19,A37,XBOOLE_1:7;
    hence Seq fc = c9 by A19,A37,A86,A45,A134,A82,Th26;
    set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n};
A136: DL c= Seg len vs & DR c= Seg len vs
    proof
      hereby
        let x be set;
        assume x in DL;
        then consider k being Element of NAT such that
A137:   x=k and
A138:   1<=k and
A139:   k<=n;
        k<=len vs by A90,A139,XXREAL_0:2;
        hence x in Seg len vs by A137,A138;
      end;
      let x be set;
      assume x in DR;
      then consider k being Element of NAT such that
A140: x=k and
A141: m+1<=k and
A142: k<=len vs;
      1<=m+1 by NAT_1:12;
      then 1<=k by A141,XXREAL_0:2;
      hence thesis by A140,A142;
    end;
    now
      let x be set;
      hereby
        assume x in domfvs;
        then ex k being Element of NAT st x=k &( 1<=k & k<=n or m+1<=k & k<=
        len vs);
        then x in DL or x in DR;
        hence x in DL \/ DR by XBOOLE_0:def 3;
      end;
      assume
A143: x in DL \/ DR;
      per cases by A143,XBOOLE_0:def 3;
      suppose
        x in DL;
        then ex k being Element of NAT st x=k & 1<=k & k<=n;
        hence x in domfvs;
      end;
      suppose
        x in DR;
        then ex k being Element of NAT st x=k & m+1<=k & k<=len vs;
        hence x in domfvs;
      end;
    end;
    then
A144: domfvs = DL \/ DR by TARSKI:2;
    reconsider DR as finite set by A136;
    rng Sgm DR = DR by A136,FINSEQ_1:def 13;
    then
A145: rng Sgm DR c= dom fvs by A88,A144,XBOOLE_1:7;
    reconsider DL as finite set by A136;
A146: m+1<=len c +1+1 by A5,A9,XREAL_1:8;
A147: m<=len c +1+1 by A5,A9,NAT_1:12;
    then
A148: len p2 +m = len c +1+1 by A9,A15,Lm2
      .= len p29 +(m+1) by A9,A14,A146,Lm2
      .= (len p29+1)+m;
    set SL = Sgm DL;
A149: Sgm DL = idseq n by FINSEQ_3:54;
    then
A150: dom Sgm DL = Seg n by RELAT_1:71;
    now
      let p be set;
      hereby
        assume
A151:   p in pp;
        then consider x, y being set such that
A152:   p=[x,y] by RELAT_1:def 1;
A153:   y=pp.x by A151,A152,FUNCT_1:8;
A154:   x in dom pp by A151,A152,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A155:   k<=len pp by A154,FINSEQ_3:27;
A156:   1<=k by A154,FINSEQ_3:27;
        then
A157:   k in domfvs by A11,A35,A106,A155;
        0+1<=k by A154,FINSEQ_3:27;
        then consider i being Element of NAT such that
        0<=i and
A158:   i<n and
A159:   k=i+1 by A11,A35,A106,A155,Th1;
A160:   x in dom SL by A11,A35,A106,A150,A156,A155;
        then
A161:   k=SL.k by A149,A150,FUNCT_1:35;
        then
A162:   x in dom (fvs*SL) by A88,A160,A157,FUNCT_1:21;
        then (fvs*SL).x = fvs.k by A161,FUNCT_1:22
          .= vs.(1+i) by A88,A157,A159,GRFUNC_1:8
          .= y by A3,A11,A90,A35,A106,A153,A158,A159,Def1;
        hence p in fvs*(Sgm DL) by A152,A162,FUNCT_1:8;
      end;
      assume
A163: p in fvs*(Sgm DL);
      then consider x,y being set such that
A164: p=[x,y] by RELAT_1:def 1;
A165: y = (fvs*SL).x by A163,A164,FUNCT_1:8;
A166: x in dom (fvs*SL) by A163,A164,FUNCT_1:8;
      then
A167: (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A168: x in dom SL by A166,FUNCT_1:21;
      then consider k such that
A169: k=x and
A170: 1<=k and
A171: k<=n by A150;
A172: k in dom fvs by A88,A170,A171;
A173: k=SL.k by A149,A150,A168,A169,FUNCT_1:35;
A174: k in dom pp by A11,A35,A106,A170,A171,FINSEQ_3:27;
      0+1<=k by A170;
      then ex i being Element of NAT st 0<=i & i<n & k=i+1 by A171,
      Th1;
      then pp.k = vs.k by A3,A11,A90,A35,A106,Def1
        .= y by A165,A167,A169,A172,A173,GRFUNC_1:8;
      hence p in pp by A164,A169,A174,FUNCT_1:8;
    end;
    then
A175: pp = fvs*(Sgm DL) by TARSKI:2;
    set SR = Sgm DR;
A176: len Sgm DR = card DR by A136,FINSEQ_3:44;
A177: m+1+lp22 = m+((lp21 -1)+1) .= m+(len c -m +1 +1 -1) by A53,A18,Def7
      .= len c +1;
    then
A178: card DR = lp21 -1+1 by A9,Th4
      .= lp21;
A179: m+1<=len c +1+1 by A5,A9,XREAL_1:9;
    now
      let p be set;
      hereby
        assume
A180:   p in p29;
        then consider x, y being set such that
A181:   p=[x,y] by RELAT_1:def 1;
A182:   y=p29.x by A180,A181,FUNCT_1:8;
A183:   x in dom p29 by A180,A181,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A184:   k<=len p29 by A183,FINSEQ_3:27;
        1<=k by A183,FINSEQ_3:27;
        then
A185:   m+k=SR.k by A9,A177,A148,A57,A184,Th5;
        0+1<=k by A183,FINSEQ_3:27;
        then consider i being Element of NAT such that
        0<=i and
A186:   i<len p29 and
A187:   k=i+1 by A184,Th1;
A188:   x in dom SR by A178,A148,A176,A183,FINSEQ_3:31;
        then
A189:   SR.k in rng SR by FUNCT_1:def 5;
        then
A190:   x in dom (fvs*SR) by A145,A188,FUNCT_1:21;
        then (fvs*SR).x = fvs.(m+k) by A185,FUNCT_1:22
          .= vs.(m+1+i) by A145,A185,A189,A187,GRFUNC_1:8
          .= y by A9,A87,A179,A182,A186,A187,Lm2;
        hence p in fvs*(Sgm DR) by A181,A190,FUNCT_1:8;
      end;
      assume
A191: p in fvs*(Sgm DR);
      then consider x,y being set such that
A192: p=[x,y] by RELAT_1:def 1;
A193: y = (fvs*SR).x by A191,A192,FUNCT_1:8;
A194: x in dom (fvs*SR) by A191,A192,FUNCT_1:8;
      then
A195: x in dom SR by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A196: k<=len p29 by A178,A148,A176,A195,FINSEQ_3:27;
      1<=k by A195,FINSEQ_3:27;
      then
A197: m+k=SR.k by A9,A177,A148,A57,A196,Th5;
A198: k in dom p29 by A178,A148,A176,A195,FINSEQ_3:31;
A199: SR.x in dom fvs by A194,FUNCT_1:21;
      0+1<=k by A195,FINSEQ_3:27;
      then consider i being Element of NAT such that
      0<=i and
A200: i<len p29 and
A201: k=i+1 by A196,Th1;
      p29.k = vs.(m+1+i) by A9,A87,A179,A200,A201,Lm2
        .= fvs.(SR.k) by A199,A197,A201,GRFUNC_1:8
        .= y by A193,A195,FUNCT_1:23;
      hence p in p29 by A192,A198,FUNCT_1:8;
    end;
    then
A202: p29 = fvs*(Sgm DR) by TARSKI:2;
    now
      let i,j;
      assume i in DL;
      then consider k being Element of NAT such that
A203: k=i and
      1<=k and
A204: k<=n;
A205: k<m by A4,A204,XXREAL_0:2;
      assume j in DR;
      then consider l being Element of NAT such that
A206: l=j and
A207: m+1<=l and
      l<=len vs;
      m<=m+1 by NAT_1:12;
      then m<=l by A207,XXREAL_0:2;
      hence i < j by A203,A206,A205,XXREAL_0:2;
    end;
    then
A208: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A136,FINSEQ_3:48;
    (1,1)-cut p2 = <*p2.(0+1)*> by A55,Th6
      .= <*vs.(m+0)*> by A9,A15,A54,A147,Lm2
      .= (m,m)-cut vs by A5,A15,Th6;
    then
A209: p1 = pp^p29 by A135,A52,FINSEQ_1:46;
    rng Sgm DL = DL by A136,FINSEQ_1:def 13;
    then rng Sgm DL c= dom fvs by A88,A144,XBOOLE_1:7;
    hence thesis by A88,A209,A144,A208,A145,A175,A202,Th26;
  end;
  suppose
A210: n=1 & m <> len vs;
    set domfvs = {k: m<=k & k<= len vs};
    deffunc F(set) = c.$1;
    set domfc = {k: m<=k & k<= len c};
    set p2 = (m,len c+1)-cut vs;
    consider fc being Function such that
A211: dom fc = domfc and
A212: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
A213: 1 < m by A3,A4,XXREAL_0:2;
    then 1-1 < m-1 by XREAL_1:11;
    then 0 < -(-(m-1));
    then
A214: -(m-1) < 0;
    1-1<=m-1 by A213,XREAL_1:11;
    then m-'1 = m-1 by XREAL_0:def 2;
    then reconsider m1 = m-1 as Element of NAT;
A215: m = m1 +1;
    domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider kk being Element of NAT such that
A216: x = kk and
A217: m<=kk and
A218: kk<= len c;
      1<=kk by A213,A217,XXREAL_0:2;
      hence thesis by A216,A218;
    end;
    then reconsider fc as FinSubsequence by A211,FINSEQ_1:def 12;
    fc c= c
    proof
      let p be set;
      assume
A219: p in fc;
      then consider x, y such that
A220: [x,y] = p by RELAT_1:def 1;
A221: x in dom fc by A219,A220,FUNCT_1:8;
      then consider kk being Element of NAT such that
A222: x = kk and
A223: m<=kk and
A224: kk<= len c by A211;
      1<=kk by A213,A223,XXREAL_0:2;
      then
A225: x in dom c by A222,A224,FINSEQ_3:27;
      y = fc.x by A219,A220,FUNCT_1:8;
      then y = c.kk by A211,A212,A221,A222;
      hence thesis by A220,A222,A225,FUNCT_1:8;
    end;
    then reconsider fc as Subset of c;
    take fc;
    deffunc F(set) = vs.$1;
    consider fvs being Function such that
A226: dom fvs = domfvs and
A227: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
    domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider kk being Element of NAT such that
A228: x = kk and
A229: m<=kk and
A230: kk<= len vs;
      1<=kk by A213,A229,XXREAL_0:2;
      hence thesis by A228,A230;
    end;
    then reconsider fvs as FinSubsequence by A226,FINSEQ_1:def 12;
A231: fvs c= vs
    proof
      let p be set;
      assume
A232: p in fvs;
      then consider x, y such that
A233: [x,y] = p by RELAT_1:def 1;
A234: x in dom fvs by A232,A233,FUNCT_1:8;
      then consider kk being Element of NAT such that
A235: x = kk and
A236: m<=kk and
A237: kk<= len vs by A226;
      1<=kk by A213,A236,XXREAL_0:2;
      then
A238: x in dom vs by A235,A237,FINSEQ_3:27;
      y = fvs.x by A232,A233,FUNCT_1:8;
      then y = vs.kk by A226,A227,A234,A235;
      hence thesis by A233,A235,A238,FUNCT_1:8;
    end;
A239: m < len vs by A5,A210,XXREAL_0:1;
    then
A240: m<=len c by A9,NAT_1:13;
    then reconsider c2 = (m,len c)-cut c as Chain of G by A213,Th44;
A241: m<=len c by A9,A239,NAT_1:13;
    reconsider fvs as Subset of vs by A231;
    take fvs;
    take c1 = c2;
    take p1 = p2;
A242: len c2 +m = len c +1 by A4,A5,A9,A210,Lm2;
    then len c2 = len c +(-m+1);
    hence
A243: len c1 < len c by A214,XREAL_1:32;
    1<=m by A3,A4,XXREAL_0:2;
    hence p1 is_vertex_seq_of c1 by A2,A241,Th45;
    p2 is_vertex_seq_of c2 by A2,A240,A213,Th45;
    then len p1 = len c1 + 1 by Def7;
    hence len p1 < len vs by A9,A243,XREAL_1:8;
    thus vs.1 = p1.1 by A4,A5,A6,A9,A210,Th12;
    thus vs.len vs = p1.len p1 by A4,A5,A9,A210,Th12;
A244: domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider k being Element of NAT such that
A245: x=k and
A246: m<=k and
A247: k<=len vs;
      1<=k by A213,A246,XXREAL_0:2;
      hence thesis by A245,A247;
    end;
A248: domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider k being Element of NAT such that
A249: x=k and
A250: m<=k and
A251: k<=len c;
      1<=k by A213,A250,XXREAL_0:2;
      hence thesis by A249,A251;
    end;
    then reconsider domfc as finite set;
A252: len Sgm domfc = card domfc by A248,FINSEQ_3:44;
    reconsider domfvs as finite set by A244;
A253: rng Sgm domfvs c= dom fvs by A226,A244,FINSEQ_1:def 13;
    set SR = Sgm domfc;
A254: len c +1<=len vs by A2,Def7;
A255: m-m<=len c -m by A240,XREAL_1:11;
    then len c2 -'1 = len c2 -1 by A242,XREAL_0:def 2;
    then reconsider lc21 = len c2 -1 as Element of NAT;
A256: m+lc21 = m1+len c2;
    len c2 -'1 = len c2 -1 by A242,A255,XREAL_0:def 2;
    then reconsider lc21 = len c2 -1 as Element of NAT;
    m+lc21 = len c by A242;
    then
A257: card domfc = len c2 + -1 +1 by Th4
      .= len c2;
A258: rng Sgm domfc c= dom fc by A211,A248,FINSEQ_1:def 13;
    now
      let p be set;
      hereby
        assume
A259:   p in c2;
        then consider x, y being set such that
A260:   p=[x,y] by RELAT_1:def 1;
A261:   y=c2.x by A259,A260,FUNCT_1:8;
A262:   x in dom c2 by A259,A260,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A263:   k<=len c2 by A262,FINSEQ_3:27;
        1<=k by A262,FINSEQ_3:27;
        then
A264:   m1+k=SR.k by A242,A215,A256,A263,Th5;
        0+1<=k by A262,FINSEQ_3:27;
        then consider i being Element of NAT such that
        0<=i and
A265:   i<len c2 and
A266:   k=i+1 by A263,Th1;
A267:   x in dom SR by A257,A252,A262,FINSEQ_3:31;
        then
A268:   SR.k in rng SR by FUNCT_1:def 5;
        then
A269:   x in dom (fc*SR) by A258,A267,FUNCT_1:21;
        then (fc*SR).x = fc.(m1+k) by A264,FUNCT_1:22
          .= c.(m+i) by A258,A264,A268,A266,GRFUNC_1:8
          .= y by A4,A5,A9,A210,A261,A265,A266,Lm2;
        hence p in fc*(Sgm domfc) by A260,A269,FUNCT_1:8;
      end;
      assume
A270: p in fc*(Sgm domfc);
      then consider x,y being set such that
A271: p=[x,y] by RELAT_1:def 1;
A272: y = (fc*SR).x by A270,A271,FUNCT_1:8;
A273: x in dom (fc*SR) by A270,A271,FUNCT_1:8;
      then
A274: x in dom SR by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A275: k<=len c2 by A257,A252,A274,FINSEQ_3:27;
      1<=k by A274,FINSEQ_3:27;
      then
A276: m1+k=SR.k by A242,A215,A256,A275,Th5;
A277: k in dom c2 by A257,A252,A274,FINSEQ_3:31;
A278: SR.x in dom fc by A273,FUNCT_1:21;
      0+1<=k by A274,FINSEQ_3:27;
      then consider i being Element of NAT such that
      0<=i and
A279: i<len c2 and
A280: k=i+1 by A275,Th1;
      c2.k = c.(m1+1+i) by A4,A5,A9,A210,A279,A280,Lm2
        .= fc.(SR.k) by A278,A276,A280,GRFUNC_1:8
        .= y by A272,A274,FUNCT_1:23;
      hence p in c2 by A271,A277,FUNCT_1:8;
    end;
    hence Seq fc = c1 by A211,TARSKI:2;
    set SR = Sgm domfvs;
A281: len Sgm domfvs = card domfvs by A244,FINSEQ_3:44;
A282: m<=len c +1+1 by A5,A9,NAT_1:12;
    then
A283: len p2 +m = len c +1 +1 by A4,A210,A254,Lm2;
    then len p2 = len c -m +1+1;
    then 1<=len p2 by A242,NAT_1:12;
    then 1-1<=len p2 -1 by XREAL_1:11;
    then len p2 -'1 = len p2 -1 by XREAL_0:def 2;
    then reconsider lp21 = len p2 -1 as Element of NAT;
    m+lp21 = len c+1 by A283;
    then
A284: card domfvs = len p2 + -1 +1 by A9,Th4
      .= len p2;
A285: m+lp21 = m1+len p2;
    now
      let p be set;
      hereby
        assume
A286:   p in p2;
        then consider x, y being set such that
A287:   p=[x,y] by RELAT_1:def 1;
A288:   y=p2.x by A286,A287,FUNCT_1:8;
A289:   x in dom p2 by A286,A287,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A290:   k<=len p2 by A289,FINSEQ_3:27;
        1<=k by A289,FINSEQ_3:27;
        then
A291:   m1+k=SR.k by A9,A215,A283,A285,A290,Th5;
        0+1<=k by A289,FINSEQ_3:27;
        then consider i being Element of NAT such that
        0<=i and
A292:   i<len p2 and
A293:   k=i+1 by A290,Th1;
A294:   x in dom SR by A284,A281,A289,FINSEQ_3:31;
        then
A295:   SR.k in rng SR by FUNCT_1:def 5;
        then
A296:   x in dom (fvs*SR) by A253,A294,FUNCT_1:21;
        then (fvs*SR).x = fvs.(m1+k) by A291,FUNCT_1:22
          .= vs.(m+i) by A253,A291,A295,A293,GRFUNC_1:8
          .= y by A4,A210,A282,A254,A288,A292,A293,Lm2;
        hence p in fvs*(Sgm domfvs) by A287,A296,FUNCT_1:8;
      end;
      assume
A297: p in fvs*(Sgm domfvs);
      then consider x,y being set such that
A298: p=[x,y] by RELAT_1:def 1;
A299: y = (fvs*SR).x by A297,A298,FUNCT_1:8;
A300: x in dom (fvs*SR) by A297,A298,FUNCT_1:8;
      then
A301: x in dom SR by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A302: k<=len p2 by A284,A281,A301,FINSEQ_3:27;
      1<=k by A301,FINSEQ_3:27;
      then
A303: m1+k=SR.k by A9,A215,A283,A285,A302,Th5;
A304: k in dom p2 by A284,A281,A301,FINSEQ_3:31;
A305: SR.x in dom fvs by A300,FUNCT_1:21;
      0+1<=k by A301,FINSEQ_3:27;
      then consider i being Element of NAT such that
      0<=i and
A306: i<len p2 and
A307: k=i+1 by A302,Th1;
      p2.k = vs.(m1+1+i) by A4,A210,A282,A254,A306,A307,Lm2
        .= fvs.(SR.k) by A305,A303,A307,GRFUNC_1:8
        .= y by A299,A301,FUNCT_1:23;
      hence p in p2 by A298,A304,FUNCT_1:8;
    end;
    hence thesis by A226,TARSKI:2;
  end;
  suppose
A308: n<>1 & m = len vs;
    set domfvs = {k: 1<=k & k<=n};
    set pp = (1,n)-cut vs;
    deffunc F(set) = c.$1;
    reconsider domfc = {k: 1<=k & k<=n1} as set;
    consider fc being Function such that
A309: dom fc = domfc and
A310: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
    n < len vs by A4,A5,XXREAL_0:2;
    then
A311: n-1 < len c +1-1 by A9,XREAL_1:11;
    domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider kk being Element of NAT such that
A312: x = kk and
A313: 1<=kk and
A314: kk<=n1;
      kk<=len c by A11,A311,A314,XXREAL_0:2;
      hence thesis by A312,A313;
    end;
    then reconsider fc as FinSubsequence by A309,FINSEQ_1:def 12;
    1 < n by A3,A308,XXREAL_0:1;
    then 1 + 1<=n by NAT_1:13;
    then
A315: 1+1-1<=n-1 by XREAL_1:11;
    then reconsider c1 = (1,n1)-cut c as Chain of G by A11,A311,Th44;
A316: 1<=n1+1 by NAT_1:12;
    then
A317: len c1+1=n1+1 by A11,A311,Lm2;
    then
A318: len c1 = n - 1 by A10,XREAL_0:def 2;
A319: fc c= c
    proof
      let p be set;
      assume
A320: p in fc;
      then consider x, y such that
A321: [x,y] = p by RELAT_1:def 1;
A322: x in dom fc by A320,A321,FUNCT_1:8;
      then consider kk being Element of NAT such that
A323: x = kk and
A324: 1<=kk and
A325: kk<=n1 by A309;
      kk<=len c by A11,A311,A325,XXREAL_0:2;
      then
A326: x in dom c by A323,A324,FINSEQ_3:27;
      y = fc.x by A320,A321,FUNCT_1:8;
      then y = c.kk by A309,A310,A322,A323;
      hence thesis by A321,A323,A326,FUNCT_1:8;
    end;
A327: domfc c= Seg len c
    proof
      let x be set;
      assume x in domfc;
      then consider k being Element of NAT such that
A328: x=k and
A329: 1<=k and
A330: k<=n1;
      k<=len c by A11,A311,A330,XXREAL_0:2;
      hence thesis by A328,A329;
    end;
    reconsider fc as Subset of c by A319;
    take fc;
    reconsider domfc as finite set by A327;
    set SL = Sgm domfc;
    deffunc F(set) = vs.$1;
    consider fvs being Function such that
A331: dom fvs = domfvs and
A332: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
A333: n<=len vs by A4,A5,XXREAL_0:2;
    domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider kk being Element of NAT such that
A334: x = kk and
A335: 1<=kk and
A336: kk<=n;
      kk<=len vs by A333,A336,XXREAL_0:2;
      hence thesis by A334,A335;
    end;
    then reconsider fvs as FinSubsequence by A331,FINSEQ_1:def 12;
    fvs c= vs
    proof
      let p be set;
      assume
A337: p in fvs;
      then consider x, y such that
A338: [x,y] = p by RELAT_1:def 1;
A339: x in dom fvs by A337,A338,FUNCT_1:8;
      then consider kk being Element of NAT such that
A340: x = kk and
A341: 1<=kk and
A342: kk<=n by A331;
      kk<=len vs by A333,A342,XXREAL_0:2;
      then
A343: x in dom vs by A340,A341,FINSEQ_3:27;
      y = fvs.x by A337,A338,FUNCT_1:8;
      then y = vs.kk by A331,A332,A339,A340;
      hence thesis by A338,A340,A343,FUNCT_1:8;
    end;
    then reconsider fvs as Subset of vs;
    domfvs c= Seg len vs
    proof
      let x be set;
      assume x in domfvs;
      then consider k being Element of NAT such that
A344: x=k and
A345: 1<=k and
A346: k<=n;
      k<=len vs by A333,A346,XXREAL_0:2;
      hence thesis by A344,A345;
    end;
    then reconsider domfvs as finite set;
    take fvs;
    take c9 = c1;
    take p1 = pp;
A347: pp is_vertex_seq_of c1 by A2,A12,A315,A311,Th45;
    then
A348: len pp = len c1 + 1 by Def7;
    thus len c9 < len c by A10,A311,A317,XREAL_0:def 2;
    thus p1 is_vertex_seq_of c9 by A2,A12,A315,A311,Th45;
    len p1 = len c1 + 1 by A347,Def7;
    hence len p1 < len vs by A4,A5,A318,XXREAL_0:2;
    thus vs.1 = p1.1 by A3,A333,Th12;
    thus vs.len vs = p1.len p1 by A3,A4,A6,A308,Th12;
A349: Sgm domfc = idseq n1 by FINSEQ_3:54;
    then
A350: dom Sgm domfc = Seg n1 by RELAT_1:71;
    now
      let p be set;
      hereby
        assume
A351:   p in c1;
        then consider x, y being set such that
A352:   p=[x,y] by RELAT_1:def 1;
A353:   y=c1.x by A351,A352,FUNCT_1:8;
A354:   x in dom c1 by A351,A352,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A355:   k<=len c1 by A354,FINSEQ_3:27;
A356:   1<=k by A354,FINSEQ_3:27;
        then x in dom SL by A317,A350,A355;
        then
A357:   k=SL.k by A349,A350,FUNCT_1:35;
        0+1<=k by A354,FINSEQ_3:27;
        then consider i being Element of NAT such that
        0<=i and
A358:   i<n1 and
A359:   k=i+1 by A317,A355,Th1;
A360:   k in domfc by A317,A356,A355;
        then
A361:   x in dom (fc*SL) by A309,A350,A357,FUNCT_1:21;
        then (fc*SL).x = fc.k by A357,FUNCT_1:22
          .= c.(1+i) by A309,A360,A359,GRFUNC_1:8
          .= y by A11,A311,A316,A317,A353,A358,A359,Lm2;
        hence p in fc*(Sgm domfc) by A352,A361,FUNCT_1:8;
      end;
      assume
A362: p in fc*(Sgm domfc);
      then consider x,y being set such that
A363: p=[x,y] by RELAT_1:def 1;
A364: y = (fc*SL).x by A362,A363,FUNCT_1:8;
A365: x in dom (fc*SL) by A362,A363,FUNCT_1:8;
      then
A366: (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A367: x in dom SL by A365,FUNCT_1:21;
      then consider k such that
A368: k=x and
A369: 1<=k and
A370: k<=n1 by A350;
A371: k in dom fc by A309,A369,A370;
A372: k in dom c1 by A317,A369,A370,FINSEQ_3:27;
A373: k=SL.k by A349,A350,A367,A368,FUNCT_1:35;
      0+1<=k by A369;
      then ex i being Element of NAT st 0<=i & i<n1 & k=i+1 by A370,
      Th1;
      then c1.k = c.k by A11,A311,A316,A317,Lm2
        .= y by A364,A366,A368,A371,A373,GRFUNC_1:8;
      hence p in c1 by A363,A368,A372,FUNCT_1:8;
    end;
    hence Seq fc = c9 by A309,TARSKI:2;
    set SL = Sgm domfvs;
A374: Sgm domfvs = idseq n by FINSEQ_3:54;
    then
A375: dom Sgm domfvs = Seg n by RELAT_1:71;
    now
      let p be set;
      hereby
        assume
A376:   p in pp;
        then consider x, y being set such that
A377:   p=[x,y] by RELAT_1:def 1;
A378:   y=pp.x by A376,A377,FUNCT_1:8;
A379:   x in dom pp by A376,A377,FUNCT_1:8;
        then reconsider k = x as Element of NAT;
A380:   k<=len pp by A379,FINSEQ_3:27;
A381:   1<=k by A379,FINSEQ_3:27;
        then x in dom SL by A348,A318,A375,A380;
        then
A382:   k=SL.k by A374,A375,FUNCT_1:35;
        0+1<=k by A379,FINSEQ_3:27;
        then consider i being Element of NAT such that
        0<=i and
A383:   i<n and
A384:   k=i+1 by A348,A318,A380,Th1;
A385:   k in domfvs by A348,A318,A381,A380;
        then
A386:   x in dom (fvs*SL) by A331,A375,A382,FUNCT_1:21;
        then (fvs*SL).x = fvs.k by A382,FUNCT_1:22
          .= vs.(1+i) by A331,A385,A384,GRFUNC_1:8
          .= y by A3,A333,A348,A318,A378,A383,A384,Def1;
        hence p in fvs*(Sgm domfvs) by A377,A386,FUNCT_1:8;
      end;
      assume
A387: p in fvs*(Sgm domfvs);
      then consider x,y being set such that
A388: p=[x,y] by RELAT_1:def 1;
A389: y = (fvs*SL).x by A387,A388,FUNCT_1:8;
A390: x in dom (fvs*SL) by A387,A388,FUNCT_1:8;
      then
A391: (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A392: x in dom SL by A390,FUNCT_1:21;
      then consider k such that
A393: k=x and
A394: 1<=k and
A395: k<=n by A375;
A396: k in dom fvs by A331,A394,A395;
A397: k in dom pp by A348,A318,A394,A395,FINSEQ_3:27;
A398: k=SL.k by A374,A375,A392,A393,FUNCT_1:35;
      0+1<=k by A394;
      then ex i being Element of NAT st 0<=i & i<n & k=i+1 by A395,
      Th1;
      then pp.k = vs.k by A3,A333,A348,A318,Def1
        .= y by A389,A391,A393,A396,A398,GRFUNC_1:8;
      hence p in pp by A388,A393,A397,FUNCT_1:8;
    end;
    hence thesis by A331,TARSKI:2;
  end;
end;

theorem
  vs is_vertex_seq_of c implies ex fc being Subset of c, fvs being
Subset of vs, sc, vs1 st Seq fc = sc & Seq fvs = vs1 & vs1 is_vertex_seq_of sc
  & vs.1 = vs1.1 & vs.len vs = vs1.len vs1
proof
  assume
A1: vs is_vertex_seq_of c;
  per cases;
  suppose
    c is simple Chain of G;
    then reconsider sc = c as simple Chain of G;
    reconsider fvs = vs as Subset of vs by Th28;
    reconsider fc = c as Subset of c by Th28;
    take fc, fvs, sc, vs;
    thus Seq fc = sc & Seq fvs = vs by FINSEQ_3:125;
    thus vs is_vertex_seq_of sc by A1;
    thus thesis;
  end;
  suppose
A2: not c is simple Chain of G;
    defpred P[Nat] means ex fc being Subset of c, fvs being Subset of vs, c1,
vs1 st Seq fc = c1 & Seq fvs = vs1 & vs1 is_vertex_seq_of c1 & vs.1 = vs1.1 &
    vs.len vs = vs1.len vs1 & len c1 = $1 & not c1 is simple Chain of G;
    P[len c qua Element of NAT]
    proof
      reconsider fvs = vs as Subset of vs by Th28;
      reconsider fc = c as Subset of c by Th28;
      take fc, fvs, c,vs;
      thus Seq fc = c & Seq fvs = vs by FINSEQ_3:125;
      thus vs is_vertex_seq_of c by A1;
      thus vs.1 = vs.1 & vs.len vs = vs.len vs;
      thus thesis by A2;
    end;
    then
A3: ex k be Nat st P[k];
    consider k be Nat such that
A4: P[k] & for n being Nat st P[n] holds k<=n from NAT_1:sch 5(A3);
    consider fc being Subset of c, fvs being Subset of vs, c1, vs1 such that
A5: Seq fc = c1 and
A6: Seq fvs = vs1 and
A7: vs1 is_vertex_seq_of c1 and
A8: vs.1 = vs1.1 and
A9: vs.len vs = vs1.len vs1 and
A10: len c1 = k and
A11: not c1 is simple Chain of G by A4;
    consider fc1 being Subset of c1, fvs1 being Subset of vs1, c2, vs2 such
    that
A12: len c2 < len c1 and
A13: vs2 is_vertex_seq_of c2 and
    len vs2 < len vs1 and
A14: vs1.1 = vs2.1 and
A15: vs1.len vs1 = vs2.len vs2 and
A16: Seq fc1 = c2 and
A17: Seq fvs1 = vs2 by A7,A11,Th52;
    reconsider fc9=fc|rng((Sgm dom fc)|dom fc1) as Subset of c by Th29;
    reconsider fvs9=fvs|rng((Sgm dom fvs)|dom fvs1) as Subset of vs by Th29;
A18: Seq fvs9 = vs2 by A6,A17,Th30;
A19: Seq fc9 = c2 by A5,A16,Th30;
    then c2 is simple Chain of G implies thesis by A8,A9,A13,A14,A15,
    A18;
    hence thesis by A4,A8,A9,A10,A12,A13,A14,A15,A19,A18;
  end;
end;

registration
  let G;
  cluster empty -> one-to-one Chain of G;
  correctness;
end;

theorem
  p is Path of G implies p|Seg(n) is Path of G
proof
  reconsider q=p|(Seg n) as FinSequence;
  assume
A1: p is Path of G;
  now
    let n1,m1 be Element of NAT;
    assume that
A2: 1<=n1 and
A3: n1 < m1 and
A4: m1<=len q;
    1<m1 by A2,A3,XXREAL_0:2;
    then m1 in dom q by A4,FINSEQ_3:27;
    then
A5: q.m1=p.m1 by FUNCT_1:70;
    n1 < len q by A3,A4,XXREAL_0:2;
    then n1 in dom q by A2,FINSEQ_3:27;
    then
A6: q.n1=p.n1 by FUNCT_1:70;
    dom q c= dom p by RELAT_1:89;
    then dom q c= Seg len p by FINSEQ_1:def 3;
    then Seg len q c= Seg len p by FINSEQ_1:def 3;
    then len q<=len p by FINSEQ_1:7;
    then m1<=len p by A4,XXREAL_0:2;
    hence q.n1 <> q.m1 by A1,A2,A3,A5,A6,GRAPH_1:def 14;
  end;
  hence thesis by A1,GRAPH_1:4,def 14;
end;

registration
  let G;
  cluster simple Path of G;
  existence
  proof
    consider x being Element of G;
    consider q being empty Chain of G;
    reconsider q as one-to-one Chain of G;
    take q;
    reconsider p = <*x*> as FinSequence of the carrier of G;
    take p;
    thus p is_vertex_seq_of q by Lm8;
    let n,m;
    assume that
A1: 1<=n and
A2: n<m and
A3: m<=len p and
    p.n = p.m;
    1<m by A1,A2,XXREAL_0:2;
    hence thesis by A3,FINSEQ_1:56;
  end;
end;

theorem
  2<len sc implies sc is Path of G
proof
  assume
A1: 2 < len sc;
  assume not sc is Path of G;
  then consider m, n such that
A2: 1<=m and
A3: m<n and
A4: n<=len sc and
A5: sc.m = sc.n by GRAPH_1:def 14;
  consider vs such that
A6: vs is_vertex_seq_of sc and
A7: for n, m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs
  by Def10;
A8: len vs = len sc + 1 by A6,Def7;
  then
A9: n+1<=len vs by A4,XREAL_1:8;
  set v29 = vs/.(n+1);
  set v19 = vs/.n;
A10: 1<=n by A2,A3,XXREAL_0:2;
  then
A11: sc.n joins v19, v29 by A4,A6,Def7;
A12: n<=len vs by A4,A8,NAT_1:12;
  then
A13: v19=vs.n by A10,FINSEQ_4:24;
  set v1 = vs/.m;
  set v2 = vs/.(m+1);
  m<=len sc by A3,A4,XXREAL_0:2;
  then
A14: sc.m joins v1, v2 by A2,A6,Def7;
A15: n+1<=len vs by A4,A8,XREAL_1:8;
  then
A16: v29=vs.(n+1) by FINSEQ_4:24,NAT_1:12;
A17: m+1<n+1 by A3,XREAL_1:8;
  then
A18: m<n+1 by NAT_1:12;
  then m<=len vs by A15,XXREAL_0:2;
  then
A19: v1=vs.m by A2,FINSEQ_4:24;
  m+1<=len vs by A17,A15,XXREAL_0:2;
  then
A20: v2=vs.(m+1) by FINSEQ_4:24,NAT_1:12;
  per cases by A5,A14,A11,Th32;
  suppose
A21: v1=v19 & v2=v29;
    then m=1 by A2,A3,A7,A12,A19,A13;
    hence contradiction by A7,A17,A15,A20,A16,A21;
  end;
  suppose
A22: v1=v29 & v2=v19;
    then
A23: n+1=len vs by A2,A7,A18,A9,A19,A16;
    m=1 by A2,A7,A18,A9,A19,A16,A22;
    hence contradiction by A1,A7,A8,A12,A20,A13,A22,A23;
  end;
end;

theorem
  sc is Path of G iff len sc = 0 or len sc = 1 or sc.1<>sc.2
proof
  hereby
    assume
A1: sc is Path of G;
    assume
A2: not len sc = 0;
    assume not len sc = 1;
    then len sc >1 by A2,NAT_1:26;
    then
A3: 1+1<=len sc by NAT_1:13;
    assume sc.1 = sc.2;
    hence contradiction by A1,A3,GRAPH_1:def 14;
  end;
  assume
A4: len sc = 0 or len sc = 1 or sc.1<>sc.2;
  per cases by A4;
  suppose
    len sc = 0;
    then for n, m st 1<=n & n<m & m<=len sc holds sc.n<>sc.m;
    hence thesis by GRAPH_1:def 14;
  end;
  suppose
    len sc = 1;
    then for n,m st 1<=n & n<m & m<=len sc holds sc.n<>sc.m by
    XXREAL_0:2;
    hence thesis by GRAPH_1:def 14;
  end;
  suppose
A5: sc.1<>sc.2;
    now
      let n, m;
      assume that
A6:   1<=n and
A7:   n<m and
A8:   m<=len sc;
      thus sc.n<>sc.m
      proof
        consider vs such that
A9:     vs is_vertex_seq_of sc and
A10:    for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=
        len vs by Def10;
        set vm1 = vs/.(m+1);
A11:    len vs = len sc +1 by A9,Def7;
        then
A12:    m+1<=len vs by A8,XREAL_1:8;
        then
A13:    vm1=vs.(m+1) by FINSEQ_4:24,NAT_1:12;
        set vn1 = vs/.(n+1);
        set vn = vs/.n;
A14:    n<=len sc by A7,A8,XXREAL_0:2;
        then
A15:    sc.n joins vn, vn1 by A6,A9,Def7;
        n+1<=len vs by A11,A14,XREAL_1:8;
        then
A16:    vn1=vs.(n+1) by FINSEQ_4:24,NAT_1:12;
        n<=len vs by A11,A14,NAT_1:12;
        then
A17:    vn=vs.n by A6,FINSEQ_4:24;
        set vm = vs/.m;
A18:    1<=m by A6,A7,XXREAL_0:2;
        then
A19:    sc.m joins vm, vm1 by A8,A9,Def7;
A20:    m<=len vs by A8,A11,NAT_1:12;
        then
A21:    vm=vs.m by A18,FINSEQ_4:24;
        assume
A22:    not thesis;
        per cases by A22,A15,A19,Th32;
        suppose
A23:      vn=vm & vn1=vm1;
A24:      n+1<m+1 by A7,XREAL_1:8;
          n=1 by A6,A7,A10,A20,A17,A21,A23;
          hence contradiction by A10,A12,A16,A13,A23,A24;
        end;
        suppose
A25:      vn=vm1 & vn1=vm;
          thus contradiction
          proof
A26:        n+1<=m by A7,NAT_1:13;
A27:        n+0<m+1 by A7,XREAL_1:10;
            per cases by A26,XXREAL_0:1;
            suppose
A28:          n+1=m;
              n=1 by A6,A10,A12,A17,A13,A25,A27;
              hence contradiction by A5,A22,A28;
            end;
            suppose
A29:          n+1<m;
              n=1 by A6,A10,A12,A17,A13,A25,A27;
              hence contradiction by A10,A20,A16,A21,A25,A29;
            end;
          end;
        end;
      end;
    end;
    hence thesis by GRAPH_1:def 14;
  end;
end;

registration
  let G;
  cluster empty -> oriented Chain of G;
  correctness;
end;

definition
  let G;
  let oc be oriented Chain of G;
  assume
A1: oc <> {};
  func vertex-seq oc -> FinSequence of the carrier of G means
  it is_vertex_seq_of oc & it.1 = (the Source of G).(oc.1);
  existence
  proof
    set TG = the Target of G;
    set SG = the Source of G;
    deffunc F(Nat) = SG.(oc.$1);
    consider z being FinSequence such that
A2: len z = len oc & for j be Nat st j in dom z holds z.j = F(j) from
    FINSEQ_1:sch 2;
    set vs = z^<*TG.(oc.len oc)*>;
A3: len vs = len oc + len <*TG.(oc.len oc)*> by A2,FINSEQ_1:35
      .= len oc +1 by FINSEQ_1:56;
A4: Seg len z = dom z by FINSEQ_1:def 3;
A5: Seg len oc = dom oc by FINSEQ_1:def 3;
    0+1=1;
    then
A6: 1<=len oc by A1,NAT_1:13;
    rng vs c= the carrier of G
    proof
      let y be set;
      assume y in rng vs;
      then consider x being set such that
A7:   x in dom vs and
A8:   y = vs.x by FUNCT_1:def 5;
      reconsider x as Element of NAT by A7;
A9:   1<=x by A7,FINSEQ_3:27;
A10:  x<=len vs by A7,FINSEQ_3:27;
      per cases by A3,A10,NAT_1:8;
      suppose
A11:    x<=len oc;
A12:    rng oc c= the carrier' of G by FINSEQ_1:def 4;
A13:    x in dom oc by A9,A11,FINSEQ_3:27;
        then
A14:    oc.x in rng oc by FUNCT_1:def 5;
        then reconsider e=oc.x as Element of the carrier' of G by A12;
A15:    SG.e in rng the Source of G by A14,A12,FUNCT_2:6;
A16:    rng the Source of G c= the carrier of G by RELAT_1:def 19;
        vs.x = z.x by A2,A5,A4,A13,FINSEQ_1:def 7
          .= SG.e by A2,A5,A4,A13;
        hence thesis by A8,A15,A16;
      end;
      suppose
A17:    x=len oc +1;
        len oc in dom oc by A6,FINSEQ_3:27;
        then
A18:    oc.len oc in rng oc by FUNCT_1:def 5;
A19:    rng oc c= the carrier' of G by FINSEQ_1:def 4;
        then reconsider e=oc.len oc as Element of the carrier' of G by A18;
A20:    rng the Target of G c= the carrier of G by RELAT_1:def 19;
        TG.e in rng the Target of G by A18,A19,FUNCT_2:6;
        then y is Element of G by A2,A8,A17,A20,FINSEQ_1:
        59;
        hence thesis;
      end;
    end;
    then reconsider vs as FinSequence of the carrier of G by FINSEQ_1:def 4;
    take vs;
    now
      thus len vs = len oc + 1 by A3;
      let n;
      assume that
A21:  1<=n and
A22:  n<=len oc;
A23:  1<=n+1 by NAT_1:12;
      n+1<=len vs by A3,A22,XREAL_1:9;
      then n+1 in dom vs by A23,FINSEQ_3:27;
      then reconsider vsn1 = vs.(n+1) as Element of G by
      FINSEQ_2:13;
A24:  vsn1 = vs/.(n+1) by A3,A22,A23,FINSEQ_4:24,XREAL_1:9;
A25:  vsn1 = TG.(oc.n)
      proof
        per cases by A22,XXREAL_0:1;
        suppose
A26:      n<len oc;
          then n+1<=len oc by NAT_1:13;
          then
A27:      n+1 in dom oc by A23,FINSEQ_3:27;
          hence vsn1 = z.(n+1) by A2,A5,A4,FINSEQ_1:def 7
            .= SG.(oc.(n+1)) by A2,A5,A4,A27
            .= TG.(oc.n) by A21,A26,GRAPH_1:def 13;
        end;
        suppose
          n=len oc;
          hence thesis by A2,FINSEQ_1:59;
        end;
      end;
A28:  n<=len vs by A3,A22,NAT_1:12;
      then n in dom vs by A21,FINSEQ_3:27;
      then reconsider vsn = vs.n as Element of G by
      FINSEQ_2:13;
A29:  vsn = vs/.n by A21,A28,FINSEQ_4:24;
A30:  n in dom oc by A21,A22,FINSEQ_3:27;
      then vsn = z.n by A2,A5,A4,FINSEQ_1:def 7
        .= SG.(oc.n) by A2,A5,A4,A30;
      hence oc.n joins vs/.n, vs/.(n+1) by A25,A29,A24,GRAPH_1:def 10;
    end;
    hence vs is_vertex_seq_of oc by Def7;
A31: 1 in dom z by A2,A6,FINSEQ_3:27;
    then z.1 = SG.(oc.1) by A2;
    hence thesis by A31,FINSEQ_1:def 7;
  end;
  uniqueness by A1,Th37;
end;

begin :: Addenda
:: from AMISTD_1, 2006.01.12, A.T.

theorem
  for f1 being non empty FinSequence of D, f2 being FinSequence of D
  holds (f1^'f2)/.1 = f1/.1
proof
  let f1 be non empty FinSequence of D, f2 be FinSequence of D;
A1: 1 in dom f1 by FINSEQ_5:6;
  1 in dom (f1^(2, len f2)-cut f2) by FINSEQ_5:6;
  hence (f1^'f2)/.1 = (f1^(2, len f2)-cut f2).1 by PARTFUN1:def 8
    .= f1.1 by A1,FINSEQ_1:def 7
    .= f1/.1 by A1,PARTFUN1:def 8;
end;

theorem
  for f1 being FinSequence of D, f2 being non trivial FinSequence of D
  holds (f1^'f2)/.len(f1^'f2) = f2/.len f2
proof
  let f1 be FinSequence of D, f2 be non trivial FinSequence of D;
A1: len (f1^'f2) + 1 = len f1 + len f2 by Th13;
  2 <= len f2 by REALSET1:13;
  then
A2: 1 < len f2 by XXREAL_0:2;
  then 1+0 < len f1 + len f2 by XREAL_1:10;
  then 1 <= len(f1^'f2) by A1,NAT_1:13;
  hence (f1^'f2)/.len(f1^'f2) = (f1^'f2).len(f1^'f2) by FINSEQ_4:24
    .= f2.len f2 by A2,Th16
    .= f2/.len f2 by A2,FINSEQ_4:24;
end;

theorem Th59:
  for f being FinSequence holds f^'{} = f
proof
  let f be FinSequence;
  thus f^'{} = f^{} by Def1
    .= f by FINSEQ_1:47;
end;

theorem
  for f being FinSequence holds f^'<*x*> = f
proof
  let f be FinSequence;
  len <*x*> = 1 by FINSEQ_1:56;
  then (2, len <*x*>)-cut <*x*> = {} by Def1;
  hence thesis by FINSEQ_1:47;
end;

theorem :: GRAPH_2:14
  for f1, f2 being FinSequence of D holds 1<=n & n<=len f1 implies (f1^'
  f2)/.n = f1/.n
proof
  let f1, f2 be FinSequence of D;
  assume that
A1: 1<=n and
A2: n<=len f1;
  per cases;
  suppose
    f2 = {};
    hence thesis by Th59;
  end;
  suppose
A3: f2 <> {};
    then
A4: len f1 + 0 < len f1 + len f2 by XREAL_1:8;
    len (f1^'f2) + 1 = len f1 + len f2 by A3,Th13;
    then n < len (f1^'f2) + 1 by A2,A4,XXREAL_0:2;
    then n <= len (f1^'f2) by NAT_1:13;
    hence (f1^'f2)/.n = (f1^'f2).n by A1,FINSEQ_4:24
      .= f1.n by A1,A2,Th14
      .= f1/.n by A1,A2,FINSEQ_4:24;
  end;
end;

theorem :: GRAPH_2:15
  for f1, f2 being FinSequence of D holds 1<=n & n<len f2 implies (f1^'
  f2)/.(len f1 + n) = f2/.(n+1)
proof
  let f1, f2 be FinSequence of D;
  assume that
A1: 1<=n and
A2: n<len f2;
A3: n+1 <= len f2 by A2,NAT_1:13;
A4: now
    per cases;
    suppose
A5:   f2 <> {};
A6:   len f1 + n < len f1 + len f2 by A2,XREAL_1:8;
      len (f1^'f2) + 1 = len f1 + len f2 by A5,Th13;
      hence len f1+n <= len (f1^'f2) by A6,NAT_1:13;
    end;
    suppose
      f2 = {};
      hence len f1+n <= len (f1^'f2) by A2;
    end;
  end;
A7: 0+1 <= n+1 by XREAL_1:8;
  0+1 <= len f1 + n by A1,XREAL_1:9;
  hence (f1^'f2)/.(len f1 + n) = (f1^'f2).(len f1 + n) by A4,FINSEQ_4:24
    .= f2.(n+1) by A1,A2,Th15
    .= f2/.(n+1) by A7,A3,FINSEQ_4:24;
end;
 
:: from SFMATR3, 2007.07.26, A.T.

definition
  let F be FinSequence of INT, m, n be Element of NAT;
  assume that
A1: 1 <= m and
A2: m <= n and
A3: n <= len F;
  func min_at(F, m, n) -> Element of NAT means
  :Def12:
  ex X being finite non
empty Subset of INT st X = rng ((m,n)-cut F) & it+1 = (min X)..(m,n)-cut F +m;
  existence
  proof
    set Cut = (m,n)-cut F;
    set X = rng Cut;
    m < n+1 by A2,NAT_1:13;
    then
A4: m-m < n+1-m by XREAL_1:11;
    len Cut +m = n+1 by A1,A2,A3,Def1;
    then
A5: Cut is non empty by A4;
    rng (Cut qua Relation of NAT, INT) is Subset of INT;
    then reconsider X as finite non empty Subset of INT by A5;
    set q = (min X)..Cut +m -1;
    1-1 <= m-1 by A1,XREAL_1:11;
    then 0+0 <= (min X)..Cut + (m-1);
    then reconsider q as Element of NAT by INT_1:16;
    take q, X;
    thus X = rng Cut;
    thus thesis;
  end;
  uniqueness;
end;

reserve F, F1 for FinSequence of INT,
  k, m, n, ma for Element of NAT;

theorem Th63:
  1 <= m & m <= n & n <= len F implies (ma = min_at(F, m, n) iff m
<= ma & ma <= n & (for i being Element of NAT st m <= i & i <= n holds F.ma <=
  F.i) & for i being Element of NAT st m <= i & i < ma holds F.ma < F.i)
proof
  assume that
A1: 1 <= m and
A2: m <= n and
A3: n <= len F;
  set Cut = (m,n)-cut F;
A4: len Cut +m = n+1 by A1,A2,A3,Def1;
  hereby
A5: n-m < n-m+1 by XREAL_1:31;
    assume ma = min_at(F, m, n);
    then consider X being finite non empty Subset of INT such that
A6: X = rng Cut and
A7: ma+1 = (min X)..Cut +m by A1,A2,A3,Def12;
A8: ma = (min X)..Cut-1 +m by A7;
A9: ma = (min X)..Cut +m-1 by A7;
A10: min X in X by XXREAL_2:def 7;
    then
A11: 1 <= (min X)..Cut by A6,FINSEQ_4:31;
    then 1-1 <= (min X)..Cut-1 by XREAL_1:11;
    then reconsider i1 = (min X)..Cut-1 as Element of NAT by INT_1:16;
A12: (min X)..Cut <= len Cut by A6,A10,FINSEQ_4:31;
    then i1 < len Cut by XREAL_1:148,XXREAL_0:2;
    then
A13: F.ma = Cut.(i1+1) by A1,A2,A3,A8,Def1
      .= Cut.(ma+1-m) by A7;
    (min X)..Cut+m <= len Cut +m by A12,XREAL_1:8;
    then
A14: ma <= len Cut +m-1 by A9,XREAL_1:11;
    m+1 <= (min X)..Cut +m by A11,XREAL_1:8;
    then m+1-1 <= ma by A9,XREAL_1:11;
    hence m <= ma & ma <= n by A4,A14;
A15: Cut.((min X)..Cut) = min X by A6,A10,FINSEQ_4:29;
    thus
A16: for i being Element of NAT st m <= i & i <= n holds F.ma <= F.i
    proof
      let i be Element of NAT;
      assume that
A17:  m <= i and
A18:  i <= n;
      m-m <= i-m by A17,XREAL_1:11;
      then reconsider i1 = i-m as Element of NAT by INT_1:16;
A19:  0+1 <= i1+1 by XREAL_1:8;
A20:  n-m < n-m+1 by XREAL_1:31;
      i1 <= n-m by A18,XREAL_1:11;
      then
A21:  i1 < len Cut by A4,A20,XXREAL_0:2;
      then i1+1 <= len Cut by NAT_1:13;
      then
A22:  i1+1 in dom Cut by A19,FINSEQ_3:27;
      Cut.(i1+1) = F.(m+i1) by A1,A2,A3,A21,Def1;
      then F.i in rng Cut by A22,FUNCT_1:def 5;
      hence thesis by A6,A7,A13,A15,XXREAL_2:def 7;
    end;
    let i be Element of NAT;
    assume that
A23: m <= i and
A24: i < ma;
A25: i <= n by A4,A14,A24,XXREAL_0:2;
    then
A26: F.ma <= F.i by A16,A23;
    m-m <= i-m by A23,XREAL_1:11;
    then reconsider i1 = i-m as Element of NAT by INT_1:16;
    reconsider k = i1+1 as Element of NAT;
    i <= len Cut -1 +m by A14,A24,XXREAL_0:2;
    then i-m <= len Cut -1 by XREAL_1:22;
    then
A27: k <= len Cut by XREAL_1:21;
    i1 <= n-m by A25,XREAL_1:11;
    then
A28: i1 < len Cut by A4,A5,XXREAL_0:2;
    0+1 <= k by XREAL_1:8;
    then
A29: k in dom Cut by A27,FINSEQ_3:27;
    i-m < ma-m by A24,XREAL_1:11;
    then
A30: k < ma-m+1 by XREAL_1:8;
    F.i = F.(i1+m) .= Cut.k by A1,A2,A3,A28,Def1;
    then F.i <> F.ma by A6,A7,A10,A13,A30,A29,FINSEQ_4:29,34;
    hence F.ma < F.i by A26,XXREAL_0:1;
  end;
  set Cut = (m,n)-cut F;
A31: len Cut +m = n+1 by A1,A2,A3,Def1;
  then
A32: len Cut = n+1-m;
  set X = rng Cut;
A33: rng (Cut qua Relation of NAT, INT) is Subset of INT;
  m < n+1 by A2,NAT_1:13;
  then m-m < n+1-m by XREAL_1:11;
  then Cut is non empty by A31;
  then reconsider X as finite non empty Subset of INT by A33;
  reconsider rX = X as finite non empty Subset of REAL by MEMBERED:3;
  assume that
A34: m <= ma and
A35: ma <= n and
A36: for i being Element of NAT st m <= i & i <= n holds F.ma <= F.i and
A37: for i being Element of NAT st m <= i & i < ma holds F.ma < F.i;
  m-m <= ma-m by A34,XREAL_1:11;
  then reconsider qm = ma-m as Element of NAT by INT_1:16;
A38: qm+1 = ma+1-m;
  then reconsider q1 = ma+1-m as Element of NAT;
  ma+1 <= n+1 by A35,XREAL_1:8;
  then
A39: q1 <= len Cut by A32,XREAL_1:11;
  0+1 <= qm+1 by XREAL_1:8;
  then
A40: q1 in dom Cut by A39,FINSEQ_3:27;
A41: ma = qm+m;
  qm < len Cut by A38,A39,NAT_1:13;
  then
A42: F.ma = Cut.(ma+1-m) by A1,A2,A3,A38,A41,Def1;
  now
    thus F.ma in X by A40,A42,FUNCT_1:def 5;
    let k be ext-real number;
    assume k in X;
    then consider dk being set such that
A43: dk in dom Cut and
A44: Cut.dk = k by FUNCT_1:def 5;
    reconsider dk as Element of NAT by A43;
    1 <= dk by A43,FINSEQ_3:27;
    then 1-1 <= dk-1 by XREAL_1:11;
    then reconsider dk1 = dk-1 as Element of NAT by INT_1:16;
A45: dk <= len Cut by A43,FINSEQ_3:27;
    then dk+m <= (len Cut)+m by XREAL_1:8;
    then
A46: dk+m-1 <= n by A4,XREAL_1:22;
    dk1 < len Cut by A45,XREAL_1:148,XXREAL_0:2;
    then F.(dk1+m) = Cut.(dk1+1) by A1,A2,A3,Def1
      .= Cut.dk;
    hence F.ma <= k by A36,A44,A46,NAT_1:12;
  end;
  then
A47: F.ma = min rX by XXREAL_2:def 7;
  set mX = min X;
  set mXC = mX..Cut;
A48: mX in X by XXREAL_2:def 7;
  then 1 <= mXC by FINSEQ_4:31;
  then 1-1 <= mXC -1 by XREAL_1:11;
  then reconsider mXC1 = mXC-1 as Element of NAT by INT_1:16;
  set mXCm = mXC1+m;
  mXC <= len Cut by A48,FINSEQ_4:31;
  then
A49: mXC1 < len Cut by XREAL_1:148,XXREAL_0:2;
  mXC = mXC1+1;
  then
A50: F.mXCm = Cut.mXC by A1,A2,A3,A49,Def1;
A51: m <= mXCm by NAT_1:12;
A52: Cut.(mX..Cut) = mX by A48,FINSEQ_4:29;
  now
    assume
A53: q1 <> mXC;
    per cases by A53,XXREAL_0:1;
    suppose
      q1 < mXC;
      hence contradiction by A40,A42,A47,FINSEQ_4:34;
    end;
    suppose
      q1 > mXC;
      then mXC+m < ma+1 by XREAL_1:22;
      then mXC+m-1 < ma by XREAL_1:21;
      hence contradiction by A37,A52,A47,A51,A50;
    end;
  end;
  then ma+1 = mX..Cut +m;
  hence thesis by A1,A2,A3,Def12;
end;

theorem
  1 <= m & m <= len F implies min_at(F, m, m) = m
proof
  assume that
A1: 1 <= m and
A2: m <= len F;
A3: for i being Element of NAT st m <= i & i < m holds F.m < F.i;
  for i being Element of NAT st m <= i & i <= m holds F.m <= F.i by
  XXREAL_0:1;
  hence thesis by A1,A2,A3,Th63;
end;

definition
  let F be FinSequence of INT, m, n be Element of NAT;
  pred F is_non_decreasing_on m, n means
  :Def13:
  for i, j being Element of NAT st m <= i & i <= j & j <= n holds F.i <= F.j;
end;

definition
  let F be FinSequence of INT, n be Element of NAT;
  pred F is_split_at n means
  :Def14:
  for i, j being Element of NAT st 1 <= i &
  i <= n & n < j & j <= len F holds F.i <= F.j;
end;

theorem
  k+1 <= len F & ma = min_at(F, k+1, len F) & F is_split_at k & F
  is_non_decreasing_on 1, k & F1 = F+*(k+1, F.ma)+*(ma, F.(k+1)) implies F1
  is_non_decreasing_on 1, k+1
proof
  assume that
A1: k+1 <= len F and
A2: ma = min_at(F, k+1, len F) and
A3: F is_split_at k and
A4: F is_non_decreasing_on 1, k and
A5: F1 = F+*(k+1, F.ma)+*(ma, F.(k+1));
A6: 1 <= k+1 by NAT_1:12;
  set Fk = F+*(k+1, F.ma);
A7: dom F1 = dom Fk by A5,FUNCT_7:32;
A8: dom Fk = dom F by FUNCT_7:32;
  then
A9: k+1 in dom F1 by A1,A7,A6,FINSEQ_3:27;
  let i, j be Element of NAT such that
A10: 1 <= i and
A11: i <= j and
A12: j <= k+1;
A13: j <= len F by A1,A12,XXREAL_0:2;
  1 <= j by A10,A11,XXREAL_0:2;
  then
A14: j in dom F1 by A7,A8,A13,FINSEQ_3:27;
  per cases by A12,XXREAL_0:1;
  suppose
A15: j < k+1;
    then i < k+1 by A11,XXREAL_0:2;
    then i <> ma by A1,A2,A6,Th63;
    then
A16: F1.i = Fk.i by A5,FUNCT_7:34
      .= F.i by A11,A15,FUNCT_7:34;
    j <> ma by A1,A2,A6,A15,Th63;
    then
A17: F1.j = Fk.j by A5,FUNCT_7:34
      .= F.j by A15,FUNCT_7:34;
    j <= k by A15,NAT_1:13;
    hence thesis by A4,A10,A11,A16,A17,Def13;
  end;
  suppose
A18: j = k+1;
    thus F1.i <= F1.j
    proof
      per cases by A11,XXREAL_0:1;
      suppose
A19:    i < j;
        then i <> ma by A1,A2,A6,A18,Th63;
        then
A20:    F1.i = Fk.i by A5,FUNCT_7:34
          .= F.i by A18,A19,FUNCT_7:34;
A21:    i <= k by A18,A19,NAT_1:13;
A22:    k < j by A18,NAT_1:13;
        thus F1.i <= F1.j
        proof
          per cases;
          suppose
            k+1 = ma;
            then F1.j = F.(k+1) by A5,A7,A9,A18,FUNCT_7:33;
            hence thesis by A1,A3,A10,A18,A20,A21,A22,Def14;
          end;
          suppose
A23:        k+1 <> ma;
A24:        ma <= len F by A1,A2,A6,Th63;
            k+1 <= ma by A1,A2,A6,Th63;
            then
A25:        k < ma by NAT_1:13;
            F1.j = Fk.j by A5,A18,A23,FUNCT_7:34
              .= F.ma by A7,A8,A14,A18,FUNCT_7:33;
            hence thesis by A3,A10,A20,A21,A25,A24,Def14;
          end;
        end;
      end;
      suppose
        i = j;
        hence thesis;
      end;
    end;
  end;
end;

theorem
  k+1 <= len F & ma = min_at(F, k+1, len F) & F is_split_at k & F1 = F+*
  (k+1, F.ma)+*(ma, F.(k+1)) implies F1 is_split_at k+1
proof
  assume that
A1: k+1 <= len F and
A2: ma = min_at(F, k+1, len F) and
A3: F is_split_at k and
A4: F1 = F+*(k+1, F.ma)+*(ma, F.(k+1));
A5: dom F1 = dom (F+*(k+1, F.ma)) by A4,FUNCT_7:32;
A6: k < k+1 by NAT_1:13;
A7: 1 <= k+1 by NAT_1:12;
  let i, j be Element of NAT;
  assume that
A8: 1 <= i and
A9: i <= k+1 and
A10: k+1 < j and
A11: j <= len F1;
A12: k < j by A10,NAT_1:13;
A13: dom (F+*(k+1, F.ma)) = dom F by FUNCT_7:32;
  then
A14: len F1 = len F by A5,FINSEQ_3:31;
  then
A15: k+1 <= len F by A10,A11,XXREAL_0:2;
  i <= len F1 by A1,A14,A9,XXREAL_0:2;
  then
A16: i in dom F1 by A8,FINSEQ_3:27;
  1 <= j by A10,NAT_1:12;
  then
A17: j in dom F1 by A11,FINSEQ_3:27;
  per cases by A9,XXREAL_0:1;
  suppose
A18: i < k+1;
    then i <> ma by A1,A2,A7,Th63;
    then
A19: F1.i = (F+*(k+1, F.ma)).i by A4,FUNCT_7:34
      .= F.i by A18,FUNCT_7:34;
A20: i <= k by A18,NAT_1:13;
    thus F1.i <= F1.j
    proof
      per cases;
      suppose
        j <> ma;
        then F1.j = (F+*(k+1, F.ma)).j by A4,FUNCT_7:34
          .= F.j by A10,FUNCT_7:34;
        hence thesis by A3,A14,A8,A11,A12,A20,A19,Def14;
      end;
      suppose
        j = ma;
        then F1.j = F.(k+1) by A4,A5,A17,FUNCT_7:33;
        hence thesis by A3,A6,A8,A15,A20,A19,Def14;
      end;
    end;
  end;
  suppose
A21: i = k+1;
A22: F1.i = F.ma
    proof
      per cases;
      suppose
        k+1 = ma;
        hence thesis by A4,A5,A16,A21,FUNCT_7:33;
      end;
      suppose
        k+1 <> ma;
        hence F1.i = (F+*(k+1, F.ma)).i by A4,A21,FUNCT_7:34
          .= F.ma by A5,A13,A16,A21,FUNCT_7:33;
      end;
    end;
    thus thesis
    proof
      per cases;
      suppose
        j = ma;
        then F1.j = F.(k+1) by A4,A5,A17,FUNCT_7:33;
        hence thesis by A1,A2,A7,A22,Th63;
      end;
      suppose
        j <> ma;
        then F1.j = (F+*(k+1, F.ma)).j by A4,FUNCT_7:34
          .= F.j by A10,FUNCT_7:34;
        hence thesis by A1,A2,A14,A7,A10,A11,A22,Th63;
      end;
    end;
  end;
end;
