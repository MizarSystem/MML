:: Vertex sequences induced by chains
::  by Yatsuka Nakamura and Piotr Rudnicki
::
:: Received May 13, 1995
:: Copyright (c) 1995 Association of Mizar Users

environ

 vocabularies FINSEQ_1, FINSET_1, FUNCT_1, ARYTM_1, RELAT_1, INT_1, CARD_1,
      BOOLE, FINSEQ_2, TARSKI, GRAPH_1, ORDERS_1, GRAPH_2, FINSEQ_4, ORDINAL2,
      ARYTM, REALSET1, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, RELAT_1,
      FUNCT_1, FUNCT_2, FINSEQ_1, REALSET1, FINSEQ_2, FINSEQ_4, CARD_1,
      FINSET_1, NAT_1, NAT_D, INT_1, RECDEF_1, GRAPH_1, BINARITH, XXREAL_0;
 constructors PARTFUN1, WELLORD2, XXREAL_0, NAT_1, INT_1, RECDEF_1, FINSEQ_4,
      REALSET1, BINARITH, FINSEQ_6, GRAPH_1, ORDINAL2, NAT_D;
 registrations INT_1, FUNCT_1, FINSEQ_1, FINSET_1, CARD_1, GRAPH_1, RELSET_1,
      NAT_1, XREAL_0, ORDINAL2, FINSEQ_6, REALSET1, XXREAL_0, ORDINAL1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, GRAPH_1, XBOOLE_0, FINSEQ_2;
 theorems TARSKI, ENUMSET1, NAT_1, REAL_1, FINSEQ_1, FINSEQ_2, GRAPH_1,
      RELAT_1, FUNCT_1, GRFUNC_1, FUNCT_2, BINARITH, REAL_2, INT_1, FINSET_1,
      CARD_1, CARD_2, CARD_4, FINSEQ_3, FINSEQ_4, CQC_THE1, ZFMISC_1, RELSET_1,
      XBOOLE_0, XBOOLE_1, XCMPLX_1, ORDINAL1, XREAL_1, FINSEQ_5, REALSET1,
      XXREAL_0;
 schemes NAT_1, RECDEF_1, FINSEQ_1, FUNCT_1;

begin :: Preliminaries

reserve p, q for FinSequence,
        X, Y, x, y for set,
        D for non empty set,
        i, j, k, l, m, n, r for Element of NAT;

theorem Th1:
 m+1<=k & k<=n iff ex i being Element of NAT st m<=i & i<n & k=i+1 proof
 hereby assume that
A1: m+1<=k and
A2: k<=n;
  reconsider k'=k as Integer; reconsider 1'=1 as Integer;
      1<=m+1 by NAT_1:11; then 1'<=k' by A1,XXREAL_0:2;
   then reconsider i=k'-1' as Element of NAT by INT_1:18;
 take i;  m+1-1<=k-1 by A1,XREAL_1:11;
 hence m<=i;
     k<k+1 by NAT_1:13; then k-1<k+1-1 by XREAL_1:11;
 hence i<n by A2,XXREAL_0:2; thus k=i+1;
 end; given i being Element of NAT such that A3: m<=i & i<n & k=i+1;
 thus m+1<=k & k<=n by A3,NAT_1:13,XREAL_1:9;
end;

theorem Th2: q = p|Seg n implies
 len q<=len p & for i st 1<=i & i<=len q
      holds p.i = q.i
 proof assume
A1: q = p|Seg n;
  per cases;
  suppose A2: n<=len p; then A3: len q = n by A1,FINSEQ_1:21;
   thus len q<=len p by A1,A2,FINSEQ_1:21; let i be Element of NAT;
   assume that
A4:  1<=i and
A5: i<=len q;
       i in Seg n by A3,A4,A5,FINSEQ_1:3;
   hence p.i = q.i by A1,FUNCT_1:72;
  end;
  suppose
A6: len p<=n;
   hence len q<=len p by A1,FINSEQ_2:23; let i be Element of NAT;
   assume 1<=i & i<=len q;
   thus p.i = q.i by A1,A6,FINSEQ_2:23;
end;
end;

theorem Th3:
 X c= Seg k & Y c= dom Sgm X
  implies (Sgm X)*(Sgm Y) = Sgm rng ((Sgm X)|Y) proof
  assume that
A1: X c= Seg k and
A2: Y c= dom Sgm X;
 A3: Y c= Seg len Sgm X by A2,FINSEQ_1:def 3;
  set M = (Sgm X)*(Sgm Y);
  reconsider Y' = Y as finite set by A3,FINSET_1:13;
A4: dom Sgm Y' = Seg card Y' by A3,FINSEQ_3:45;
A5: rng Sgm Y = Y by A3,FINSEQ_1:def 13;
  then dom M = Seg card Y' by A2,A4,RELAT_1:46;
  then reconsider M as FinSequence by FINSEQ_1:def 2;
  A6: rng Sgm X c= Seg k & Seg k c= NAT by A1,FINSEQ_1:def 13;
then A7: rng Sgm X c= NAT by XBOOLE_1:1;
     rng M c= rng Sgm X by RELAT_1:45;
  then rng M c= NAT by A7,XBOOLE_1:1;
  then reconsider L = (Sgm X)*(Sgm Y) as FinSequence of NAT by FINSEQ_1:def 4;
  set R = rng ((Sgm X)|Y);
     R c= rng Sgm X by RELAT_1:99;
then A8: R c= Seg k by A6,XBOOLE_1:1;
     now let y be set;
   hereby assume y in rng L; then consider x such that
A9:  x in dom L & y = L.x by FUNCT_1:def 5;
A10:  y = (Sgm X).((Sgm Y).x) by A9,FUNCT_1:22;
       x in dom Sgm Y by A9,FUNCT_1:21;
 then (Sgm Y).x in Y by A5,FUNCT_1:def 5; hence y in R by A2,A10,FUNCT_1:73;
   end;
   assume y in R; then consider x such that
A11:  x in dom ((Sgm X)|Y) & y = ((Sgm X)|Y).x by FUNCT_1:def 5;
       x in (dom Sgm X)/\Y by A11,RELAT_1:90;
then A12:  x in dom Sgm X & x in Y by XBOOLE_0:def 3;
then A13:  y = (Sgm X).x by A11,FUNCT_1:72;
    consider z being set such that
A14:  z in dom Sgm Y & x = (Sgm Y).z by A5,A12,FUNCT_1:def 5;
A15:  z in dom((Sgm X)*(Sgm Y)) by A12,A14,FUNCT_1:21;
    then L.z = y by A13,A14,FUNCT_1:22;
   hence y in rng L by A15,FUNCT_1:def 5;
  end;
then A16: rng L = R by TARSKI:2;
   now let l,m,k1,k2 be natural number;
      assume that
A17:  1 <= l and
A18:  l < m and
A19:  m <= len L and
A20:  k1=L.l and
A21:  k2=L.m;
        l <= len L & 1<=m by A17,A18,A19,XXREAL_0:2;
then A22:  l in dom L & m in dom L by A17,A19,FINSEQ_3:27;
then A23:  L.l = (Sgm X).((Sgm Y).l) & L.m = (Sgm X).((Sgm Y).m) by FUNCT_1:22;
A24:   l in dom Sgm Y & (Sgm Y).l in dom Sgm X &
     m in dom Sgm Y & (Sgm Y).m in dom Sgm X by A22,FUNCT_1:21;
then A25:   1<=l & l<=len Sgm Y & 1<=m & m<=len Sgm Y by FINSEQ_3:27;
     reconsider l, m as Element of NAT by ORDINAL1:def 13;
     reconsider K1 = (Sgm Y).l, K2 = (Sgm Y).m as Element of NAT;
A26:   K1 < K2 by A3,A18,A25,FINSEQ_1:def 13;
        1<=K1 & K1<=len Sgm X & 1<=K2 & K2<=len Sgm X by A24,FINSEQ_3:27;
     hence k1 < k2 by A1,A20,A21,A23,A26,FINSEQ_1:def 13;
   end;
 hence (Sgm X)*(Sgm Y) = Sgm rng ((Sgm X)|Y) by A8,A16,FINSEQ_1:def 13;
end;

Lm1: for m, n being Element of NAT, F being finite set
  st F = {k: m<=k & k<=m+n} holds card F = n+1 proof
  let m be Element of NAT;
    defpred P[Element of NAT] means for F being finite set
        st F = {k: m<=k & k<=m+$1} holds card F = $1+1;
A1: P[0] proof let F be finite set; assume
A2:    F = {k: m<=k & k<=m+0};
         now let x be set;
       hereby assume x in F; then consider k being Element of NAT such that
A3:       x = k & m<=k & k<=m+0 by A2;
        thus x = m by A3,XXREAL_0:1;
       end; assume x = m; hence x in F by A2;
      end; then F = {m} & 0+1 = 1 by TARSKI:def 1;
     hence card F = 0+1 by CARD_1:79;
    end;
A4: now let n be Element of NAT; assume
A5: P[n];
    thus P[n+1]
    proof
   let F be finite set; assume
A6:    F = {k: m<=k & k<=m+(n+1)};
      set F1 = {k: m<=k & k<=m+n};
      reconsider F2 = {k: k<=m+n} as finite set by CQC_THE1:12;
         F1 c= F2 proof let x be set; assume
           x in F1; then consider k being Element of NAT such that
A7:     x=k & m<=k & k<=m+n;
       thus x in F2 by A7;
      end; then reconsider F1 as finite set by FINSET_1:13;
         now let x be set;
       hereby assume x in F; then consider k such that
A8:      x=k & m<=k & k<=m+(n+1) by A6;
            k<=m+n+1 by A8;
         then k<=m+n or k=m+(n+1) by NAT_1:8;
         then k in F1 or k in {m+(n+1)} by A8,TARSKI:def 1;
        hence x in F1 \/ {m+(n+1)} by A8,XBOOLE_0:def 2;
       end;
        assume
A9:      x in F1 \/ {m+(n+1)};
        per cases by A9,XBOOLE_0:def 2;
        suppose x in F1; then consider k such that
A10:      x=k & m<=k & k<=m+n;
            k<=m+n+1 by A10,NAT_1:12;
        hence x in F by A6,A10;
        end;
        suppose A11: x in {m+(n+1)};
then A12:      x=m+(n+1) by TARSKI:def 1;
         reconsider k = x as Element of NAT by A11,TARSKI:def 1;
            m<=k & k<=m+(n+1) by A12,NAT_1:11;
        hence x in F by A6;
      end;
      end;
then A13:    F = {k: m<=k & k<=m+n} \/ {m+(n+1)} by TARSKI:2;
A14:    not m+(n+1) in {k: m<=k & k<=m+n} proof assume
           not thesis; then consider k such that
A15:     m+(n+1) = k & m<=k & k<=m+n;
           m+n+1<=m+n+0 by A15; hence contradiction by XREAL_1:8;
      end;
         card F1 = n+1 by A5;
     hence card F = n+1+1 by A13,A14,CARD_2:54;
     end;
    end;
     for n be Element of NAT holds P[n] from NAT_1:sch 1 (A1, A4);
  hence thesis;
end;

theorem Th4: for m, n being Element of NAT holds Card {k: m<=k & k<=m+n} = n+1
 proof
 let m, n be Element of NAT;
 defpred P[Element of NAT] means not contradiction;
 deffunc F(Element of NAT) = $1;
 set F = {F(k): m<=k & k<=m+n & P[k]};
  F is finite from FINSEQ_1:sch 6;
 then reconsider F as finite set;
  F = {F(k): m<=k & k<=m+n}
 proof
   thus F c= {F(k): m<=k & k<=m+n}
   proof
    let x be set;
    assume x in F;
    then consider k1 being Element of NAT such that
A1: x = F(k1) & m <= k1 & k1 <= m+n;
    thus thesis by A1;
   end;
   let x be set;
   assume x in {F(k): m<=k & k<=m+n};
   then consider k1 being Element of NAT such that
A2:x = F(k1) & m <= k1 & k1 <= m+n;
   thus thesis by A2;
 end;
 then reconsider G = {F(k): m<=k & k<=m+n} as finite set;
  card G = n+1 by Lm1;
 hence thesis;
end;

theorem Th5: for l st 1<=l & l<=n holds
   Sgm {kk where kk is Element of NAT: m+1<=kk & kk<=m+n}.l = m+l
proof
  set F= Sgm {kk where kk is Element of NAT: m+1<=kk & kk<=m+n};
  defpred P[Element of NAT] means 1<=$1 & $1<=n & F.$1 <> m+$1;
  assume
A1: ex l st P[l];
   consider k such that
A2: P[k] and
A3: for nn being Element of NAT st P[nn] holds k <= nn from NAT_1:sch 5 (A1);
   set rF = {kk where kk is Element of NAT: m+1<=kk & kk<=m+n};
A4: rF c= Seg (m+n) proof let x be set; assume x in rF;
   then consider x' being Element of NAT such that
A5: x'=x & m+1<=x' & x'<=m+n;
       1<=m+1 by NAT_1:11; then 1<=x' by A5,XXREAL_0:2;
    hence x in Seg (m+n) by A5,FINSEQ_1:3;
   end;
   then reconsider rF as finite set by FINSET_1:13;
A6: card rF = n proof
    per cases;
    suppose
A7:  n = 0;
        rF = {} proof assume rF <> {}; then consider x being set such that
A8:   x in rF by XBOOLE_0:def 1; consider x' being Element of NAT such that
A9:   x'=x & m+1<=x' & x'<=m+n by A8;
         m+1<=m+0 by A7,A9,XXREAL_0:2; hence contradiction by XREAL_1:8;
     end;
    hence thesis by A7,CARD_1:78;
    end;
    suppose 0<n; then 0+1<=n by NAT_1:13;
     then 1-1<=n-1 by XREAL_1:11;
     then n-'1 = n-1 by BINARITH:def 3;
     then reconsider n1 = n-1 as Element of NAT;
        (m+1)+n1 = m+n;
     then card rF = n1+1 by Th4;
    hence thesis;
   end;
   end;
    m+1<=m+k & m+k<=m+n by A2,XREAL_1:9;
then A10: m+k in rF;
A11: dom Sgm rF = Seg n by A4,A6,FINSEQ_3:45;
A12: len Sgm rF = n by A4,A6,FINSEQ_3:44;
A13: rng F = rF by A4,FINSEQ_1:def 13;
       m+k in rng Sgm rF by A4,A10,FINSEQ_1:def 13; then consider x such that
A14:  x in dom Sgm rF & m+k = (Sgm rF).x by FUNCT_1:def 5;
    reconsider x as Element of NAT by A14;
    reconsider Fk = F.k as Element of NAT;
      now
    defpred P[Element of NAT] means
     for Fk be Element of NAT st 1<=$1 & $1<=n & Fk = F.$1
           holds m+$1<=Fk;
A15: P[0];
A16: for k st P[k] holds P[k+1]
     proof let k be Element of NAT; assume
A17: P[k];
     let Fk1 be Element of NAT;
     assume that
A18:  1<=k+1 and
A19:  k+1<=n and
A20:  Fk1 = F.(k+1);
A21:  k<k+1 by NAT_1:13;
      per cases by A18,REAL_1:def 5;
      suppose 1<k+1;
then A22:    1<=k & k<=n by A19,NAT_1:13;
       reconsider Fk = F.k as Element of NAT;
A23:    m+k<=Fk by A17,A22;
          Fk < Fk1 by A4,A12,A19,A20,A21,A22,FINSEQ_1:def 13;
       then m+k<Fk1 by A23,XXREAL_0:2; then m+k+1<=Fk1 by NAT_1:13;
       hence m+(k+1)<=Fk1;
      end;
      suppose
A24:    1=k+1;
       then 1 in dom F by A12,A19,FINSEQ_3:27;
       then F.1 in rng F by FUNCT_1:def 5;
        then consider F1 being Element of NAT such
that
A25:    F1 = F.1 & m+1<=F1 & F1<=m+n by A13;
       thus m+(k+1)<=Fk1 by A20,A24,A25;
    end;
    end;
    thus for k holds P[k] from NAT_1:sch 1(A15,A16);
   end;
then A26:  m+k<=Fk by A2;
A27: 1<=x & x<=n by A11,A14,FINSEQ_1:3;
 per cases by A2,A14,REAL_1:def 5;
 suppose k < x;
  hence contradiction by A2,A4,A12,A14,A26,A27,FINSEQ_1:def 13;
 end;
 suppose
A28: k > x;  F.x <> m+x by A2,A14,XCMPLX_1:2;
 hence contradiction by A3,A27,A28;
end;
end;

begin :: The cut operation

definition let p be FinSequence, m, n be Nat;
 func (m, n)-cut p -> FinSequence means :Def1:
  len it +m = n+1 &
   for i being Nat st i<len it holds it.(i+1)=p.(m+i)
    if 1<=m & m<=n & n<=len p
    otherwise it ={};
 consistency;
 existence proof
  hereby
  assume that 1<=m and
A1: m<=n and n<=len p;
    deffunc F(Element of NAT) = p.(m+($1-1));
    consider s being FinSequence such that
A2: len s = n+1-'m &
 for k being Element of NAT st k in Seg (n+1-'m) holds s.k=F(k)
                                                  from FINSEQ_1:sch 2;
  take s;
   n <= n+1 by XREAL_1:31;
   then m <= n+1 by A1, XXREAL_0:2;
   then m-m <= n+1-m by XREAL_1:11;
   then len s +m = n+1-m+m by A2,BINARITH:def 3;
  hence len s +m = n+1; let k be Nat; assume
A3: k<len s;  0+1<=k+1 by XREAL_1:9;
    then 1<=k+1 & k+1<=len s by A3,NAT_1:13;
   then k+1 in Seg (n+1-'m) by A2,FINSEQ_1:3;
  hence s.(k+1) = p.(m+(k+1-1)) by A2 .= p.(m+k);
  end;
  thus thesis;
 end;
 uniqueness proof let s1, s2 be FinSequence;
  hereby
   assume 1<=m & m<=n & n<=len p;
   assume that
A4: len s1 +m = n+1 and
A5: for i being Nat st i<len s1 holds s1.(i+1) = p.(m+i);
   assume that
A6: len s2 +m = n+1 and
A7: for i being Nat st i<len s2 holds s2.(i+1) = p.(m+i);
      now thus len s1 = len s1; thus
A8:   len s2 = len s1 by A4,A6;
   let i be Element of NAT;assume i in Seg len s1;
    then 0+1<=i&i<=len s1 by FINSEQ_1:3;
   then consider k such that
A9:   0<=k & k<len s1 & i=k+1 by Th1;
    thus s1.i = p.(m+k) by A5,A9 .= s2.i by A7,A8,A9;
   end; hence s1 = s2 by FINSEQ_2:10;
  end;
  thus thesis;
 end;
end;

Lm2:
 for p be FinSequence, m, n be Element of NAT
  st 1<=m & m<=n+1 & n<=len p
 holds len (m, n)-cut p +m = n+1 &
 for i being Element of NAT st i<len (m, n)-cut p
  holds ((m, n)-cut p).(i+1)=p.(m+i)
 proof let p be FinSequence, m, n be Element of NAT such that
A1: 1<=m & m<=n+1 & n<=len p;
  m <= n+1 by A1;
   then m = n+1 or m < n+1 by XXREAL_0:1;
   then
A2:  m = n+1 or m <= n by NAT_1:13;
  per cases by A2;
  suppose
A3: m = n+1;
   then n < m by XREAL_1:31;
   then not(1<=m & m<=n & n<=len p);
   then (m, n)-cut p = {} by Def1;
   then
A4:  len (m, n)-cut p = 0 by CARD_1:47;
  hence len (m, n)-cut p +m = n+1 by A3;
  thus thesis by A4;
  end;
  suppose m<=n;
   hence thesis by A1,Def1;
  end;
 end;

theorem Th6:
  1<=m & m<=len p implies (m,m)-cut p = <*p.m*> proof
  assume that
A1: 1<=m and
A2: m<=len p; set mp = (m,m)-cut p; len mp + m = m + 1 by A1,A2,Def1;
then A3: len mp = 1;
   then mp.(0+1) = p.(m+0) by A1,A2,Def1 .= p.m;
 hence (m,m)-cut p = <*p.m*> by A3,FINSEQ_1:57;
end;

theorem Th7: (1, len p)-cut p = p proof set cp = (1, len p)-cut p;
    now
A1: 1<=len p +1 by NAT_1:11; then len cp + 1 = len p + 1 by Lm2; hence
A2: len cp = len p; thus len p = len p;
  let i; assume i in Seg len p; then 0+1<=i & i<=len p by FINSEQ_1:3;
   then consider k being Element of NAT such that
   A3: 0<=k & k<len cp & i=k+1 by A2,Th1;
  thus cp.i = p.i by A1,A3,Lm2;
 end; hence thesis by FINSEQ_2:10;
end;

theorem Th8: m<=n & n<=r & r<=len p implies
        (m+1, n)-cut p ^ (n+1, r)-cut p = (m+1, r)-cut p proof
        assume that
A1: m<=n and
A2: n<=r and
A3: r<=len p;
 set p1 = (m+1, n)-cut p; set p2 = (n+1, r)-cut p; set p3 = (m+1, r)-cut p;
 set p12 = p1^p2;
     now
A4: 1<=m+1 & m+1<=n+1 & n<=len p by A1,A2,A3,XXREAL_0:2,XREAL_1:8
,NAT_1:11;
  then len p1 +(m+1) = n+1 by Lm2; then len p1+m+1 = n+1;
    then A5: len p1 +m-m = n-m;
then A6: len p1 = n-m;
A7: 1<=n+1 & n+1<=r+1 by A2,XREAL_1:8,NAT_1:11;
     then len p2 +(n+1) = r+1 by A3,Lm2;
     then len p2 +n+1 = r+1;
     then len p2 +n-n = r-n;
then A8: len p2 = r-n;  m<=r by A1,A2,XXREAL_0:2;
   then A9: m+1<=r+1 by XREAL_1:8;
   thus len p12 = len p12;
A10:  len p12= n-m+(r+ -n) by  FINSEQ_1:35,A5,A8
          .= r-m;
       m+1<=r+1 by A4,A7,XXREAL_0:2; then len p3 +(m+1)=r+1 by A3,A4,Lm2;
    then len p3 +m+1 = r+1; then A11: len p3 +m-m = r-m;
   hence
A12: len p3 = len p12 by A10;
   let i; assume i in Seg len p12;
then A13: 1<=i & i<=len p12 by FINSEQ_1:3;
   reconsider m'=m as Integer;reconsider n'=n as Integer;
    reconsider nm=n'-m' as Element of NAT by A1,INT_1:18;
    per cases by NAT_1:13;
    suppose
A14:   i<=nm;  0+1=1; then consider k being Element of NAT such that
A15:   0<=k & k<nm & i=k+1 by A13,A14,Th1;  nm<=r-m by A2,XREAL_1:11;
then A16:   k<len p3 by A11,A15,XXREAL_0:2;
        i in dom p1 by A6,A13,A14,FINSEQ_3:27;
    hence p12.i = p1.i by FINSEQ_1:def 7 .= p.((m+1)+k) by A4,A6,A15,Lm2
               .= p3.i by A3,A4,A9,A15,A16,Lm2;
    end;
    suppose nm+1<=i; then consider k such that
A17:   nm<=k & k<len p12 & i=k+1 by A13,Th1;
     reconsider k''=k as Integer;
     reconsider k'=k''-nm as Element of NAT by A17,INT_1:18;
A18:   i = nm+(k'+1) by A17;
        len p12 = len p2 + len p1 by FINSEQ_1:35;
   then len p2 = len p12 - len p1;
     then A19:   k'<len p2 by A6,A17,XREAL_1:11;
     then 1<=k'+1 & k'+1<=len p2 by NAT_1:11,13;
then A20:   k'+1 in dom p2 by FINSEQ_3:27;
A21:   n+1+k' = m+1+k;
    thus p12.i = p2.(k'+1) by A6,A18,A20,FINSEQ_1:def 7
             .= p.(n+1+k') by A3,A7,A19,Lm2
             .= p3.i by A3,A4,A9,A12,A17,A21,Lm2;
  end;
  end;
 hence (m+1, n)-cut p ^ (n+1, r)-cut p = (m+1, r)-cut p by FINSEQ_2:10;
end;

theorem
     m<=len p implies (1, m)-cut p ^ (m+1, len p)-cut p = p
proof
 assume that
A1: m<=len p;
  set cp1 = (1, m)-cut p; set cpm = (m+1, len p)-cut p;
     0+1=1; hence cp1 ^ cpm = (1, len p)-cut p by A1,Th8 .= p by Th7;
end;

theorem
     m<=n & n<=len p implies
        (1, m)-cut p ^ (m+1, n)-cut p ^ (n+1, len p)-cut p = p proof
        assume that
A1: m<=n and
A2: n<=len p;
set cp1 = (1, m)-cut p; set cp2 = (m+1, n)-cut p; set cp3 = (n+1, len p)-cut p;
A3: 0+1=1;
 hence cp1^cp2^cp3 = (1, n)-cut p ^cp3 by A1,A2,Th8
                  .= (1, len p)-cut p by A2,A3,Th8 .= p by Th7;
end;

theorem Th11: rng ((m,n)-cut p) c= rng p
proof set c = ((m,n)-cut p);
A1: now assume that
A2: 1<=m and
A3: m<=n and
A4: n<=len p;
  n <= n+1 by NAT_1:11;
  then
A5: m <= n+1 by A3,XXREAL_0:2;
 thus thesis proof let x be set; assume x in rng c;
   then consider z being set such that
A6: z in dom c & x = c.z by FUNCT_1:def 5;
   reconsider z as Element of NAT by A6;
      0+1<=z & z<=len c by A6,FINSEQ_3:27; then consider i such that
A7: 0<=i & i<len c & z=i+1 by Th1;
A8: c.z = p.(m+i) by A2,A4,A7,Lm2,A5;
      m+i<len c +m by A7,XREAL_1:8; then m+i<n+1 by A2,A4,Lm2,A5;
   then m+i<=n by NAT_1:13; then 1<=m+i & m+i<=len p
    by A2,A4,XXREAL_0:2,NAT_1:12;
   then m+i in dom p by FINSEQ_3:27;
  hence x in rng p by A6,A8,FUNCT_1:def 5;
 end;
 end;
    now assume not (1<=m & m<=n & n<=len p);
  then c = {} by Def1; then rng c = {} by FINSEQ_1:27;
  hence thesis by XBOOLE_1:2;
 end;
 hence thesis by A1;
end;

definition let D be set, p be FinSequence of D, m, n be Nat;
 redefine func (m, n)-cut p -> FinSequence of D;
 coherence proof
    A1: m is Element of NAT & n is Element of NAT by ORDINAL1:def 13;
A2: rng p c= D by FINSEQ_1:def 4;  rng (m,n)-cut p c= rng p by A1, Th11;
   then rng (m,n)-cut p c= D by A2,XBOOLE_1:1; hence thesis by FINSEQ_1:def 4
;
 end;
end;

theorem Th12: 1<=m & m<=n & n<=len p implies
      ((m,n)-cut p).1 = p.m & ((m,n)-cut p).len ((m,n)-cut p)=p.n
proof set c = ((m,n)-cut p);
 assume that
A1: 1<=m and
A2: m<=n and
A3: n<=len p;
A4: m<=n+1 by A2,NAT_1:12;
then A5: len c +m = n+1 by A1,A3,Lm2;
A6: now assume len c = 0;
        then n+1<=n+0 by A2,A5; hence contradiction by XREAL_1:8
;
    end; A7: 0<=len c;
  0<len c & 0+1=1 by A6;
 hence c.1 = p.(m+0) by A1,A3,A4,Lm2 .= p.m;
     0+1<=len c by A6,A7,NAT_1:13;
      then consider i being Element of NAT such that
A8: 0<=i & i<len c & len c =i+1 by Th1;
   m+i = n by A5,A8;
 hence c.(len c) = p.n by A1,A3,A4,A8,Lm2;
end;

begin  :: The glueing catenation

definition let p, q be FinSequence;
 func p ^' q -> FinSequence equals
  p^(2, len q)-cut q;
 correctness;
end;

theorem Th13: q<>{} implies len (p^'q) +1 = len p + len q
proof set r = (p^'q); set qc = (2,len q)-cut q; assume
     q<>{};
     then len q <> 0 by FINSEQ_1:25; then 0<len q;
then 0+1<=len q by NAT_1:13; then 1+1<=len q +1 by XREAL_1:9;
then len qc +(1+1) = len q + 1 by Lm2;
     then len qc +1+1 = len q + 1;
then A1: len qc +1 = len q;
 thus len r +1 = len p + len qc + 1 by FINSEQ_1:35
              .= len p + len q by A1;
end;

theorem Th14:
  1<=k & k<=len p implies (p^'q).k=p.k
proof
   assume that
A1: 1<=k and
A2: k<=len p;  k in dom p by A1,A2,FINSEQ_3:27;
  hence (p^'q).k=p.k by FINSEQ_1:def 7;
end;

theorem Th15:
  1<=k & k<len q implies (p^'q).(len p +k) = q.(k+1)
proof set qc = (2,len q)-cut q;
   assume that
A1: 1<=k and
A2: k<len q;
    per cases;
    suppose q={}; then len q = 0 by FINSEQ_1:25;
     hence thesis by A2;
    end;
    suppose
        q<>{}; then len q <> 0 by FINSEQ_1:25; then 0<len q;
     then 0+1<=len q by NAT_1:13;
then A3:  1+1<=len q +1 by XREAL_1:9;
     then len qc +(1+1) = len q + 1 by Lm2;
     then len qc +1+1 = len q + 1;
 then len qc +1 = len q; then A4:   k<=len qc by A2,NAT_1:13;
 then A5: k in dom qc by A1,FINSEQ_3:27;
      0+1<=k by A1; then consider i being Element of NAT such that
A6: 0<=i & i<len qc & k=i+1 by A4,Th1;
   thus (p^'q).(len p +k) = qc.k by A5,FINSEQ_1:def 7
             .= q.(1+1+i) by A3,A6,Lm2
             .= q.(k+1) by A6;
end;
end;

theorem Th16:
  1<len q implies (p^'q).len (p^'q) = q.len q
proof set r = p^'q; set qc = (2,len q)-cut q; assume
A1: 1<len q; then len q <>0; then q<>{} by FINSEQ_1:25;
    then len r +1-1=len p +len q -1 by Th13;
then A2:  len r = len p +(len q -1);
       1+1<=len q +1 by A1,XREAL_1:9; then len qc +(1+1) = len q + 1 by Lm2
;
     then A3: len qc +1+1 = len q + 1;
then A4:   len qc +1 = len q;
        1+1<=len q by A1,NAT_1:13;
then A5:  1+1-1<=len q -1 by XREAL_1:11;
    len qc < len q by A4,NAT_1:13;
 hence r.len r = q.len q by A2,A3,A5,Th15;
end;

theorem Th17: rng (p^'q) c= rng p \/ rng q
proof set r = p^'q; set qc = (2,len q)-cut q;
 let x be set; assume x in rng r;
then A1: x in rng p \/ rng qc by FINSEQ_1:44;
A2: rng qc c= rng q by Th11;
      x in rng p or x in rng qc by A1,XBOOLE_0:def 2;
hence x in rng p \/ rng q by A2,XBOOLE_0:def 2;
end;

definition let D be set, p, q be FinSequence of D;
 redefine func p^'q -> FinSequence of D;
 coherence proof
      rng p c= D & rng q c= D by FINSEQ_1:def 4;
then A1: rng p \/ rng q c= D by XBOOLE_1:8
;  rng (p^'q) c= rng p \/ rng q by Th17;
   then rng (p^'q) c= D by A1,XBOOLE_1:1;
  hence thesis by FINSEQ_1:def 4;
 end;
end;

theorem
       p<>{} & q<>{} & p.len p = q.1 implies rng (p^'q) = rng p \/ rng q
proof set r = p^'q; set qc = (2,len q)-cut q;
 assume that
A1: p<>{} and
A2: q<>{} and
A3: p.len p = q.1;
        len q <> 0 by A2,FINSEQ_1:25; then 0<len q;
     then 0+1<=len q by NAT_1:13;
then A4:  1+1<=len q +1 by XREAL_1:9;
     then len qc +(1+1) = len q + 1 by Lm2;
     then len qc +1+1 = len q + 1;
then A5:   len qc +1 = len q;
    now let x be set;
  hereby assume x in rng r;
then A6: x in rng p \/ rng qc by FINSEQ_1:44;
A7: rng qc c= rng q by Th11;
      x in rng p or x in rng qc by A6,XBOOLE_0:def 2;
hence x in rng p \/ rng q by A7,XBOOLE_0:def 2;
  end;
  assume x in rng p \/ rng q;
then A8: x in rng p or x in rng q by XBOOLE_0:def 2;
  assume
      not x in rng r; then A9: not x in rng p \/ rng qc by FINSEQ_1:44;
then A10: not x in rng p & not x in rng qc by XBOOLE_0:def 2;
   consider y being set such that
A11: y in dom q & x=q.y by A8,A9,FUNCT_1:def 5,XBOOLE_0:def 2;
   reconsider y as Element of NAT by A11;
A12: 1<=y & y<=len q by A11,FINSEQ_3:27;
   per cases by A12,REAL_1:def 5;
   suppose A13: y=1;  len p <>0 by A1,FINSEQ_1:25;
   then 0<len p;
     then 0+1<=len p by NAT_1:13; then len p in dom p by FINSEQ_3:27;
hence contradiction by A3,A10,A11,A13,FUNCT_1:def 5;
   end;
   suppose 1<y; then 1+1<=y by NAT_1:13; then consider i such that
A14:   1<=i & i<len q & y=i+1 by A12,Th1;
     A15:  0+1<=i & i<=len qc by A5,A14,NAT_1:13
; then consider j such that
A16:   0<=j & j<len qc & i=j+1 by Th1;
A17:   qc.(j+1) = q.(1+1+j) by A4,A16,Lm2
             .= q.y by A14,A16;
        i in dom qc by A15,FINSEQ_3:27; hence contradiction by A10,A11,A16,A17,
FUNCT_1:def 5;
 end;
 end;
 hence rng (p^'q) = rng p \/ rng q by TARSKI:2;
end;

begin :: Two valued alternating finsequences

definition let f be FinSequence;
  attr f is TwoValued means
:Def3: card rng f = 2;
end;

Lm3: now set p = <*1, 2*>;  2 > 1;
  hence len p > 1 by FINSEQ_1:61; thus 1 <> 2;
  thus rng p = rng (<*1*>^<*2*>) by FINSEQ_1:def 9
   .= rng<*1*>\/ rng<*2*> by FINSEQ_1:44 .= {1} \/ rng <*2*> by FINSEQ_1:55
   .= {1} \/ {2} by FINSEQ_1:55 .= {1, 2} by ENUMSET1:41;
end;

theorem Th19:
p is TwoValued iff len p >1 & ex x,y being set st x<>y & rng p ={x, y} proof
 hereby assume p is TwoValued; then card rng p = 2 by Def3;
   then consider x,y being set such that
A1:  x<>y & rng p={x,y} by CARD_2:79;
  thus len p >1 proof set l=len p; assume A2: len p<=1;
   per cases by A2,NAT_1:26;
   suppose l=0; then p={} by FINSEQ_1:25; hence contradiction by A1,FINSEQ_1:27
;
   end;
   suppose A3: l=1; then 1 in dom p by FINSEQ_3:27;
     then consider z being set such that
A4:   [1,z] in p by RELAT_1:def 4;
        z=p.1 by A4,FUNCT_1:8; then p=<*z*> by A3,FINSEQ_1:57;
     then rng p = {z} by FINSEQ_1:56; then z=x & z=y by A1,ZFMISC_1:8;
    hence contradiction by A1;
  end;
  end;
  thus ex x,y being set st x<>y & rng p ={x, y} by A1;
 end;
 assume len p >1;
  given x, y being set such that
A5: x<>y & rng p = {x, y};  card rng p = 2 by A5,CARD_2:76;
  hence p is TwoValued by Def3;
end;

then Lm4: <*1, 2*> is TwoValued by Lm3;

Lm5: now set p = <*1, 2*>;
  let i be Element of NAT;
  assume that
A1: 1<=i and
A2: (i+1)<=len p;
      i+1<=1+1 by A2,FINSEQ_1:61;
   then i<=1 by XREAL_1:8; then i = 1 by A1,XXREAL_0:1;
   then p.i = 1 & p.(i+1) = 2 by FINSEQ_1:61;
 hence p.i <> p.(i+1);
end;

definition let f be FinSequence;
 attr f is Alternating means
:Def4:
  for i being Element of NAT st 1<=i & (i+1)<=len f holds f.i <> f.(i+1);
end;

Lm6: <*1, 2*> is Alternating by Def4,Lm5;

registration
 cluster TwoValued Alternating FinSequence;
 existence by Lm4,Lm6;
end;

reserve a, a1, a2 for TwoValued Alternating FinSequence;

theorem Th20:
len a1 = len a2 & rng a1 = rng a2 & a1.1 = a2.1 implies a1 = a2 proof
 assume that
A1: len a1 = len a2 and
A2: rng a1 = rng a2 and
A3: a1.1 = a2.1;
    defpred P[Element of NAT] means 1<=$1 & $1<=len a1 implies a1.$1=a2.$1;
A4: P[0];
A5: for k being Element of NAT st P[k] holds P[k+1]
    proof let k be Element of NAT;
     assume
A6:    1<=k & k<=len a1 implies a1.k = a2.k;
     assume that
A7:  1<=k+1 and
A8:  k+1<=len a1;
A9:    k+1 in dom a1 by A7,A8,FINSEQ_3:27;
A10:   0=k or 0<k & 0+1=1;
     per cases by A10,NAT_1:13;
     suppose 0 = k; hence a1.(k+1) = a2.(k+1) by A3;
     end;
     suppose
A11:    1<=k;
      k<=len a1 by A8,NAT_1:13;
then A12:    k in dom a1 by A11,FINSEQ_3:27;
A13:    dom a1 = Seg len a1 & dom a2 = Seg len a2 by FINSEQ_1:def 3;
       consider X, Y being set such that
A14:    X <> Y & rng a1 = { X, Y } by Th19;
          a1.k in rng a1 & a2.k in rng a2 &
       a1.(k+1) in rng a1 & a2.(k+1) in rng a2
       by A1,A9,A12,A13,FUNCT_1:def 5;
       then (a1.k=X or a1.k=Y) & (a2.k=X or a2.k=Y) & (a1.(k+1)=X or a1.(k+1)=
Y) &
       (a2.(k+1) = X or a2.(k+1) = Y) by A2,A14,TARSKI:def 2;
     hence a1.(k+1) = a2.(k+1) by A1,A6,A8,A11,Def4,NAT_1:13;
    end;
    end;
     for i being Element of NAT holds P[i] from NAT_1:sch 1 (A4, A5);
  hence a1 = a2 by A1,FINSEQ_1:18;
end;

theorem Th21: a1<>a2 & len a1 = len a2 & rng a1 = rng a2
          implies for i st 1<=i & i<=len a1 holds a1.i<>a2.i proof
          assume that
A1:  a1 <> a2 and
A2:  len a1 = len a2 and
A3: rng a1 = rng a2;
    defpred P[Element of NAT] means 1<=$1 & $1<=len a1 implies a1.$1<>a2.$1;
A4: P[0];
A5: for k being Element of NAT st P[k] holds P[k+1]
    proof let k be Element of NAT; assume
A6:    1<=k & k<=len a1 implies a1.k <> a2.k;
     assume that
A7:  1<=k+1 and
A8:  k+1<=len a1;
A9:    k+1 in dom a1 by A7,A8,FINSEQ_3:27;
A10:   0=k or 0<k & 0+1=1;
     per cases by A10,NAT_1:13;
     suppose 0 = k; hence a1.(k+1) <> a2.(k+1) by A1,A2,A3,Th20;
     end;
     suppose
A11:    1<=k;
      k<=len a1 by A8,NAT_1:13;
then A12:    k in dom a1 by A11,FINSEQ_3:27;
A13:    dom a1 = Seg len a1 & dom a2 = Seg len a2 by FINSEQ_1:def 3;
       consider X, Y being set such that
A14:    X <> Y & rng a1 = { X, Y } by Th19;
          a1.k in rng a1 & a2.k in rng a2 &
       a1.(k+1) in rng a1 & a2.(k+1) in rng a2
       by A2,A9,A12,A13,FUNCT_1:def 5;
       then (a1.k=X or a1.k=Y) & (a2.k=X or a2.k=Y) & (a1.(k+1)=X or a1.(k+1)=
Y) &
       (a2.(k+1) = X or a2.(k+1) = Y) by A3,A14,TARSKI:def 2;
     hence a1.(k+1) <> a2.(k+1) by A2,A6,A8,A11,Def4,NAT_1:13;
    end;
    end;
 thus for i holds P[i] from NAT_1:sch 1 (A4, A5);
end;

theorem Th22: a1<>a2 & len a1 = len a2 & rng a1 = rng a2
           implies for a st len a=len a1 & rng a=rng a1 holds a=a1 or a=a2
proof assume that
A1: a1 <> a2 and
A2: len a1 = len a2 and
A3: rng a1 = rng a2; let a;
 assume that
A4: len a = len a1 and
A5: rng a = rng a1; assume
A6: a <> a1;
     now let i be Element of NAT;
   assume that
A7: 1<=i and
A8: i<=len a;
A9:    i in dom a by A7,A8,FINSEQ_3:27;
A10: dom a=Seg len a & dom a1=Seg len a1 & dom a2=Seg len a2 by FINSEQ_1:def 3;
    consider X, Y being set such that
A11:  X <> Y & rng a = { X, Y } by Th19;
       a.i in rng a & a1.i in rng a1 & a2.i in rng a2
    by A2,A4,A9,A10,FUNCT_1:def 5;
   then (a.i=X or a.i=Y)&(a1.i=X or a1.i=Y)&(a2.i=X or a2.i=Y)
     by A3,A5,A11,TARSKI:def 2;
   hence a.i = a2.i by A1,A2,A3,A4,A5,A6,A7,A8,Th21;
  end; hence a = a2 by A2,A4,FINSEQ_1:18;
end;

theorem Th23:
X <> Y & n > 1 implies ex a1 st rng a1 = {X, Y} & len a1 = n & a1.1 = X
   proof assume that
A1: X <> Y and
A2: n > 1; set p = <*X, Y*>;
A3: p.1 = X & p.2 = Y by FINSEQ_1:61;
  deffunc F(Element of NAT,Element of NAT) = 3-'$2;
  consider f1 being Function of NAT, NAT such that
A4: f1.0=2 & for n holds f1.(n+1)=F(n,f1.n) from RECDEF_1:sch 4;
     defpred P[Element of NAT] means (f1.$1=1 or f1.$1=2) & f1.$1<>f1.($1+1);
A5: now
     thus A6: 3-'2 =1+2-2 by BINARITH:def 3.=1;
     thus A7: 3-'1=2+1-1 by BINARITH:def 3.=2;
     thus f1.(0+1) = 1 by A4,A6;
     hence f1.(1+1) = 2 by A4,A7;
    end;
then A8: P[0] by A4;
A9: for i being Element of NAT st P[i] holds P[i+1]
    proof let i be Element of NAT such that
A10:   (f1.i = 1 or f1.i = 2) & f1.i <> f1.(i+1);
      thus (f1.(i+1)=1 or f1.(i+1) = 2) by A4,A5,A10;
      hence f1.(i+1) <> f1.((i+1)+1) by A4,A5;
     end;
A11: for i holds P[i] from NAT_1:sch 1 (A8, A9);
   deffunc F(Element of NAT) = p.(f1.$1);
   consider p1 being FinSequence such that
A12: len p1 = n & for k st k in Seg n holds p1.k= F(k) from FINSEQ_1:sch 2;
A13:  now let y be set;
   hereby assume y in {X, Y}; then A14: y = X or y = Y by TARSKI:def 2;
       1+1<=n by A2,NAT_1:13;
    then 1<=1&1<=len p1&1<=2&2<=len p1 by A12,XXREAL_0:2;
then A15: 1 in Seg len p1&2 in Seg len p1 by FINSEQ_1:3;
then A16: p1.1=X & p1.2=Y by A3,A5,A12;
       1 in dom p1 & 2 in dom p1 by A15,FINSEQ_1:def 3;
    hence ex x being set st x in dom p1 & y = p1.x by A14,A16;
   end;
   given x being set such that
A17: x in dom p1 & y = p1.x;  x in Seg len p1 by A17,FINSEQ_1:def 3;
    then x in { k : 1<=k & k<=len p1} by FINSEQ_1:def 1;
    then consider x' being Element of NAT such that
A18:  x'=x & 1<=x' & x'<=len p1;  x' in Seg n by A12,A18,FINSEQ_1:3;
     then p1.x' = p.(f1.x') & (f1.x' = 1 or f1.x' = 2) by A11,A12;
hence y in {X, Y} by A3,A17,A18,TARSKI:def 2;
  end;
 then rng p1 = {X, Y} by FUNCT_1:def 5;
  then reconsider p1 as TwoValued FinSequence by A1,A2,A12,Th19;
     now let i be Element of NAT;
   assume that
A19: 1<=i and
A20: (i+1)<=len p1;
       1<=i & i<=n & 1<=(i+1) & (i+1)<=n by A12,A19,A20,NAT_1:13;
    then i in Seg n & (i+1) in Seg n by FINSEQ_1:3;
     then p1.i=p.(f1.i)&p1.(i+1)=p.(f1.(i+1)) & (f1.i=1 or f1.i=2) &
     (f1.(i+1)=1 or f1.(i+1)=2) & f1.i<>f1.(i+1) by A11,A12;
   hence p1.i <> p1.(i+1) by A1,A3;
  end;
 then reconsider p1 as TwoValued Alternating FinSequence by Def4;
 take p1; thus rng p1 = {X, Y} by A13,FUNCT_1:def 5; thus len p1 = n by A12;
     1 in Seg n by A2,FINSEQ_1:3;
  hence p1.1 = X by A3,A5,A12;
end;

begin    :: Finite subsequences of finite sequences

definition let X; let fs be FinSequence of X;
 mode FinSubsequence of fs -> FinSubsequence means
:Def5: it c= fs;
 existence proof reconsider IT = fs as FinSubsequence by FINSEQ_1:68;
  take IT; thus thesis;
 end;
end;

reserve ss for FinSubsequence;

 Lm7: ss is FinSequence implies Seq ss = ss by FINSEQ_3:125;

canceled 2;

theorem Th26:
  for f being FinSubsequence, g, h, fg, fh, fgh being FinSequence
   st rng g c= dom f & rng h c= dom f & fg=f*g & fh=f*h & fgh=f*(g^h)
   holds fgh = fg^fh proof
  let f be FinSubsequence, g, h, fg, fh, fgh be FinSequence;
  assume that
A1: rng g c= dom f and
A2: rng h c= dom f and
A3: fg=f*g and
A4: fh=f*h and
A5: fgh=f*(g^h);
    now rng (g^h) = rng g \/ rng h by FINSEQ_1:44;
 then rng (g^h) c= dom f by A1,A2,XBOOLE_1:8;
  hence
A6: len fgh = len (g^h) by A5,FINSEQ_2:33
     .= len g + len h by FINSEQ_1:35;
A7: len fg = len g by A1,A3,FINSEQ_2:33;
A8: len fh = len h by A2,A4,FINSEQ_2:33;
A9: dom fgh = Seg (len g +len h) by A6,FINSEQ_1:def 3;
A10: dom fg = dom g by A1,A3,RELAT_1:46;
A11: dom fh = dom h by A2,A4,RELAT_1:46;
  thus len (fg^fh) = len g + len h by A7,A8,FINSEQ_1:35;
  let j; assume
A12: j in Seg (len g + len h);
then A13: 1<=j & j<=len g + len h by FINSEQ_1:3;
  per cases;
  suppose j<=len g;
then A14: j in dom g by A13,FINSEQ_3:27;
   thus fgh.j = f.((g^h).j) by A5,A9,A12,FUNCT_1:22
             .= f.(g.j) by A14,FINSEQ_1:def 7
             .= fg.j by A3,A14,FUNCT_1:23
             .= (fg^fh).j by A10,A14,FINSEQ_1:def 7;
  end;
  suppose len g < j; then len g +1 <=j by NAT_1:13;
then A15: 1<=j-len g by XREAL_1:21; then 0<=j-len g;
    then j-'len g = j-len g by BINARITH:def 3;
    then reconsider j' = j-len g as Element of NAT;
A16: j = len g + j';
    then j'<=len h by A13,XREAL_1:8;
then A17: j' in dom h by A15,FINSEQ_3:27;
   thus fgh.j = f.((g^h).j) by A5,A9,A12,FUNCT_1:22
             .= f.(h.j') by A16,A17,FINSEQ_1:def 7
             .= fh.j' by A4,A17,FUNCT_1:23
             .= (fg^fh).j by A7,A11,A16,A17,FINSEQ_1:def 7;
 end;
 end;
 hence fgh = fg^fh by FINSEQ_2:10;
end;

reserve fs, fs1, fs2 for FinSequence of X,
        fss, fss2 for FinSubsequence of fs;

theorem Th27: dom fss c= dom fs & rng fss c= rng fs
 proof fss c= fs by Def5; hence thesis by RELAT_1:25; end;

theorem Th28: fs is FinSubsequence of fs
proof reconsider fs'=fs as FinSubsequence by FINSEQ_1:68;  fs' c= fs;
 hence thesis by Def5;
end;

theorem Th29: fss|Y is FinSubsequence of fs proof
   consider k such that
A1: dom fss c= Seg k by FINSEQ_1:def 12;
      dom (fss|Y) c= dom fss by RELAT_1:89;
   then dom (fss|Y) c= Seg k by A1,XBOOLE_1:1;
   then reconsider f = fss|Y as FinSubsequence by FINSEQ_1:def 12;
      f c= fss & fss c= fs by Def5,RELAT_1:88; then f c= fs by XBOOLE_1:1;
 hence thesis by Def5;
end;

theorem Th30:
for fss1 being FinSubsequence of fs1
 st Seq fss = fs1 & Seq fss1 = fs2 & fss2 = fss|rng((Sgm dom fss)|dom fss1)
  holds Seq fss2 = fs2 proof
let fss1 be FinSubsequence of fs1 such that
A1: Seq fss = fs1 & Seq fss1 = fs2 & fss2 = fss|rng((Sgm dom fss)|dom fss1);
    consider k such that
A2: dom fs = Seg k by FINSEQ_1:def 2;
A3: dom fss c= Seg k by A2,Th27;
A4: dom fss2 c= Seg k by A2,Th27;
A5: dom fss1 c= dom fs1 by Th27; consider l such that
A6: dom fs1 = Seg l by FINSEQ_1:def 2;
       (Sgm dom fss)|dom fss1 c= Sgm dom fss by RELAT_1:88;
then A7: dom ((Sgm dom fss)|dom fss1) c= dom (Sgm dom fss) &
    rng ((Sgm dom fss)|dom fss1) c= rng (Sgm dom fss) by RELAT_1:25;
then A8: rng((Sgm dom fss)|dom fss1) c= dom fss by A3,FINSEQ_1:def 13;
    then A9: dom fss2 = rng((Sgm dom fss)|dom fss1) by A1,RELAT_1:91;
A10: rng Sgm dom fss c= dom fss by A3,FINSEQ_1:def 13;
A11: dom fs1 = dom (fss*Sgm dom fss) by A1,FINSEQ_1:def 14
           .= dom Sgm dom fss by A10,RELAT_1:46;
A12: rng Sgm dom fss2 = dom fss2 by A4,FINSEQ_1:def 13;
A13: rng Sgm dom fss1 = dom fss1 by A5,A6,FINSEQ_1:def 13;
   reconsider d1 = dom fss1 as finite set by A5,FINSET_1:13;
   reconsider d2 = dom fss2 as finite set by A4,FINSET_1:13;
      now take Z = (Sgm dom fss)|dom fss1;
A14:  dom Z = dom fss1 by A5,A11,RELAT_1:91;
    hereby let x be set; assume
A15: x in d1;
     take y = Z.x;
     thus y in d2 by A9,A14,A15,FUNCT_1:def 5;
     thus [x,y] in Z by A14,A15,FUNCT_1:8;
    end;
    hereby let y be set; assume y in d2;
      then consider x such that
A16:   x in dom Z & y = Z.x by A9,FUNCT_1:def 5;
      take x;
     thus x in d1 & [x,y] in Z by A5,A11,A16,FUNCT_1:8,RELAT_1:91;
    end;
    let x,y,z,u be set;
    assume that
A17:  [x,y] in Z and
A18:  [z,u] in Z;
A19:  x in dom Z & y = Z.x & z in dom Z & u = Z.z by A17,A18,FUNCT_1:8;
  then A20: x in Seg l & z in Seg l by A5,A6,A14;
A21:   y = (Sgm dom fss).x & u = (Sgm dom fss).z by A19,FUNCT_1:70;
         y in rng Z & u in rng Z by A19,FUNCT_1:def 5;
      then y in dom fss & u in dom fss by A8;
      then A22: y in Seg k & u in Seg k by A3;
     thus x=z implies y=u by A19;
     assume
A23:   y=u;
     assume
A24:   x<>z;
      reconsider x,z as Element of NAT by A20;
      reconsider y,u as Element of NAT by A22;
A25:  1<=x & x<=len Sgm dom fss & 1<=z & z<=
len Sgm dom fss by A7,A19,FINSEQ_3:27;
     per cases by A24,REAL_1:def 5;
     suppose x<z; then y < u by A3,A21,A25,FINSEQ_1:def 13;
     hence contradiction by A23;
     end;
     suppose z<x; then u < y by A3,A21,A25,FINSEQ_1:def 13;
     hence contradiction by A23;
   end;
   end; then d1,d2 are_equipotent by TARSKI:def 6;
   then card d1 = card d2 by CARD_1:81;
then A26: dom Sgm d1 = Seg card d1 &
     dom Sgm d2 = Seg card d1 by A4,A5,A6,FINSEQ_3:45;
     now
       now let x be set;
     hereby assume x in dom fs2;
       then x in dom (fss1*Sgm dom fss1) by A1,FINSEQ_1:def 14;
then A27:     x in dom Sgm dom fss2 by A26,FUNCT_1:21;
       then (Sgm dom fss2).x in dom fss2 by A12,FUNCT_1:def 5;
       then x in dom (fss2*Sgm dom fss2) by A27,FUNCT_1:21;
      hence x in dom Seq fss2 by FINSEQ_1:def 14;
     end;
     assume x in dom Seq fss2;
       then x in dom (fss2*Sgm dom fss2) by FINSEQ_1:def 14;
then A28:     x in dom Sgm dom fss1 by A26,FUNCT_1:21;
       then (Sgm dom fss1).x in dom fss1 by A13,FUNCT_1:def 5;
       then x in dom (fss1*Sgm dom fss1) by A28,FUNCT_1:21;
       hence x in dom fs2 by A1,FINSEQ_1:def 14;
    end;
   hence
A29:  dom fs2 = dom Seq fss2 by TARSKI:2;
   thus dom fs2 = dom fs2;
   let k; assume
A30:  k in dom fs2;
then A31:  k in dom (fss1*Sgm dom fss1) by A1,FINSEQ_1:def 14;
A32:  k in dom (fss2*Sgm dom fss2) by A29,A30,FINSEQ_1:def 14;
A33: k in dom Sgm dom fss1 by A31,FUNCT_1:21;
A34: (Sgm dom fss1).k in dom fss1 by A31,FUNCT_1:21;
       dom fss1 c= dom Seq fss by A1,Th27;
    then dom fss1 c= dom (fss*Sgm dom fss) by FINSEQ_1:def 14;
then A35: (Sgm dom fss1).k in dom Sgm dom fss by A34,FUNCT_1:21;
       fss1 c= Seq fss by A1,Def5;
then A36: fss1.((Sgm dom fss1).k) = (Seq fss).((Sgm dom fss1).k) by A34,
GRFUNC_1:8;
       (Sgm dom fss1).k in dom fss1 by A31,FUNCT_1:21;
then A37: k in dom ((Sgm dom fss)*(Sgm dom fss1)) by A5,A11,A33,FUNCT_1:21;
       (Sgm dom fss2).k = ((Sgm dom fss)*(Sgm dom fss1)).k by A3,A5,A9,A11,Th3;
    then A38: (Sgm dom fss2).k = (Sgm dom fss).((Sgm dom fss1).k) by A37,
FUNCT_1:22;
A39: fss2 c= fss by A1,RELAT_1:88;
    A40: (Sgm dom fss2).k in dom fss2 by A32,FUNCT_1:21;
   thus (Seq fss2).k = (fss2*Sgm dom fss2).k by FINSEQ_1:def 14
    .= fss2.((Sgm dom fss2).k) by A32,FUNCT_1:22
    .= fss.((Sgm dom fss).((Sgm dom fss1).k)) by A38,A39,A40,GRFUNC_1:8
    .= (fss*Sgm dom fss).((Sgm dom fss1).k) by A35,FUNCT_1:23
    .= fss1.((Sgm dom fss1).k) by A36,FINSEQ_1:def 14.= (fss1*Sgm dom fss1).k
by A31,FUNCT_1:22
   .= fs2.k by A1,FINSEQ_1:def 14;
  end;
 hence Seq fss2 = fs2 by FINSEQ_1:17;
end;

begin   :: Vertex sequences induced by chains

reserve G for Graph;

registration let G;
 cluster the Vertices of G -> non empty;
 coherence by GRAPH_1:def 1;
end;

reserve v, v1, v2, v3, v4 for Element of the Vertices of G,
        e for set;

theorem Th31: e joins v1,v2 implies e joins v2,v1
proof assume e joins v1,v2;
       then ((the Source of G).e = v1 & (the Target of G).e = v2) or
       ((the Source of G).e = v2 & (the Target of G).e = v1) by GRAPH_1:def 9;
 hence e joins v2,v1 by GRAPH_1:def 9;
end;

theorem Th32:
e joins v1,v2 & e joins v3,v4 implies v1=v3 & v2=v4 or v1=v4 & v2=v3 proof
  assume that
A1: e joins v1,v2 and
A2: e joins v3,v4;
          (((the Source of G).e = v1 & (the Target of G).e = v2) or
       ((the Source of G).e = v2 & (the Target of G).e = v1)) &
       (((the Source of G).e = v3 & (the Target of G).e = v4) or
       ((the Source of G).e = v4 & (the Target of G).e = v3))
       by A1,A2,GRAPH_1:def 9;
  hence v1=v3 & v2=v4 or v1=v4 & v2=v3;
end;

reserve vs, vs1, vs2 for FinSequence of the Vertices of G,
        c, c1, c2 for Chain of G;

registration let G;
 cluster empty Chain of G;
 existence proof {} is Chain of G by GRAPH_1:14; hence thesis; end;
end;

definition let G, X;
 func G-VSet X -> set equals
  { v: ex e being Element of the Edges of G st e in X &
                (v = (the Source of G).e or v = (the Target of G).e) };
 correctness;
end;

definition let G, vs; let c be FinSequence;
 pred vs is_vertex_seq_of c means
:Def7:
      len vs = len c + 1 &
      for n st 1<=n & n<=len c holds c.n joins vs/.n, vs/.(n+1);
end;

canceled;

theorem Th34:
 c <>{} & vs is_vertex_seq_of c implies G-VSet rng c = rng vs proof
 assume that
A1: c <>{} and
A2: vs is_vertex_seq_of c;
 A3: len vs = len c + 1 by A2,Def7;
    now let y be set;
  hereby assume
    y in G-VSet rng c;
   then consider v being Element of the Vertices of G such that
A4: v=y & ex e being Element of the Edges of G st e in rng c &
        (v = (the Source of G).e or v = (the Target of G).e);
   consider e being Element of the Edges of G such that
A5: e in rng c & (v = (the Source of G).e or v = (the Target of G).e) by A4;
   consider x being set such that
A6: x in dom c & e = c.x by A5,FUNCT_1:def 5;
   reconsider x as Element of NAT by A6;
A7: 1<=x & x<=len c by A6,FINSEQ_3:27;
then A8: 1<=x+1 & x+1<=len vs & x<=len vs by A3,NAT_1:12,XREAL_1:9;
   set v1 = vs/.x; set v2 = vs/.(x+1);
A9: v1 = vs.x & v2 = vs.(x+1) by A7,A8,FINSEQ_4:24;
      c.x joins v1, v2 by A2,A7,Def7;
then A10: v = v1 or v = v2 by A5,A6,GRAPH_1:def 9;
      x in dom vs & x+1 in dom vs by A7,A8,FINSEQ_3:27;
   hence y in rng vs by A4,A9,A10,FUNCT_1:def 5;
  end; assume y in rng vs; then consider x being set such that
A11:  x in dom vs & y = vs.x by FUNCT_1:def 5;
   reconsider x as Element of NAT by A11;
A12: 1<=x & x<=len vs by A11,FINSEQ_3:27;
   per cases by A3,A12,NAT_1:8;
   suppose
A13:  x<=len c;
then A14: 1<=x+1 & x+1<=len vs & x<=len vs by A3,NAT_1:12,XREAL_1:9;
       x in dom c by A12,A13,FINSEQ_3:27;
    then c.x in rng c & rng c c= the Edges of G by FINSEQ_1:def 4,FUNCT_1:def 5
;
    then reconsider e = c.x as Element of the Edges of G;
   set v1 = vs/.x; set v2 = vs/.(x+1);
A15: v1 = vs.x & v2 = vs.(x+1) by A12,A14,FINSEQ_4:24;
      c.x joins v1, v2 by A2,A12,A13,Def7;
then A16: v1 = (the Source of G).e & v2 = (the Target of G).e or
   v2 = (the Source of G).e & v1 = (the Target of G).e by GRAPH_1:def 9;
      x in dom c by A12,A13,FINSEQ_3:27;
then e in rng c by FUNCT_1:def 5;
   hence y in G-VSet rng c by A11,A15,A16;
   end;
   suppose
A17:  x=len c+1; set l = len c;
       l <> 0 by A1,FINSEQ_1:25; then 0<l & 0+1=1;
then A18:  1<=l & l<=l+1 by NAT_1:13;
    then l in dom c by FINSEQ_3:27;
    then c.l in rng c & rng c c= the Edges of G by FINSEQ_1:def 4,FUNCT_1:def 5
;
    then reconsider e = c.l as Element of the Edges of G;
   set v1 = vs/.l; set v2 = vs/.(l+1);
A19: v1 = vs.l & v2 = vs.(l+1) by A3,A12,A17,A18,FINSEQ_4:24;
      c.l joins v1, v2 by A2,A18,Def7;
then A20: v1 = (the Source of G).e & v2 = (the Target of G).e or
   v2 = (the Source of G).e & v1 = (the Target of G).e by GRAPH_1:def 9;
      l in dom c by A18,FINSEQ_3:27;
     then e in rng c by FUNCT_1:def 5;
   hence y in G-VSet rng c by A11,A17,A19,A20;
 end;
 end;
 hence G-VSet rng c = rng vs by TARSKI:2;
end;

theorem Th35: <*v*> is_vertex_seq_of {} proof
A1: len {} = 0 by FINSEQ_1:25;
  hence len <*v*> = len {} +1 by FINSEQ_1:57;
  thus thesis by A1;
end;

theorem Th36: ex vs st vs is_vertex_seq_of c proof consider p such that
A1: len p = len c + 1 &
    (for n st 1<=n & n<=len p holds p.n in the Vertices of G) &
    for n st 1<=n & n<=len c
       ex v1,v2 st v1=p.n & v2=p.(n+1) & c.n joins v1,v2 by GRAPH_1:def 11;
    rng p c= the Vertices of G proof let y be set; assume y in rng p;
   then consider x being set such that
A2: x in dom p & y = p.x by FUNCT_1:def 5;
   reconsider n=x as Element of NAT by A2;
      1<=n & n<=len p by A2,FINSEQ_3:27; hence y in the Vertices of G by A1,A2
;
 end;
 then reconsider p as FinSequence of the Vertices of G by FINSEQ_1:def 4;
 take p;
 thus len p = len c + 1 by A1;
 let n; assume that
A3: 1<=n and
A4: n<=len c; consider v1,v2 such that
A5: v1=p.n & v2=p.(n+1) & c.n joins v1,v2 by A1,A3,A4;
A6: 1<=n+1 & len c <=len c +1 & n+1<=len c +1 by A4,NAT_1:12,XREAL_1:9;
      n<=len p by A1,A4,NAT_1:12;
   then p/.n=p.n & p/.(n+1)=p.(n+1) by A1,A3,A6,FINSEQ_4:24;
 hence c.n joins p/.n, p/.(n+1) by A5;
end;

theorem Th37:
c <>{} & vs1 is_vertex_seq_of c & vs2 is_vertex_seq_of c & vs1 <> vs2
 implies
vs1.1<>vs2.1 & for vs st vs is_vertex_seq_of c holds vs = vs1 or vs = vs2
proof assume that
A1: c <>{} and
A2: vs1 is_vertex_seq_of c and
A3: vs2 is_vertex_seq_of c; assume
A4: vs1 <> vs2;
  set SG = the Source of G; set TG = the Target of G;
A5: len vs1 = len c +1 & len vs2 = len c +1 by A2,A3,Def7;
   defpred P[Element of NAT] means $1 in dom vs1 & vs1.$1<>vs2.$1;
A6:   Seg len vs1 = dom vs1 & Seg len vs2 = dom vs2 by FINSEQ_1:def 3;
then A7: ex j st P[j] by A4,A5,FINSEQ_2:10;
   consider k such that
A8: P[k] and
A9: for n st P[n] holds k<=n from NAT_1:sch 5 (A7);
A10: 1<=k & k<=len c +1 by A5,A8,FINSEQ_3:27;
   per cases by A10,REAL_1:def 5;
   suppose
A11: k=1;
   hence vs1.1 <> vs2.1 by A8; let vs; assume
A12: vs is_vertex_seq_of c;
then A13: len vs = len c +1 by Def7;
      len c <>0 by A1,FINSEQ_1:25; then 0+1=1 & 0<len c;
then A14: 1<=len c by NAT_1:13;
A15: 1<=len vs1 by A5,NAT_1:12;
A16:  Seg len vs = dom vs by FINSEQ_1:def 3;
 set v1 = vs/.1; set v2 = vs/.(1+1);
 set v11 = vs1/.1; set v12 = vs1/.(1+1);
 set v21 = vs2/.1;
A17: v1=vs.1 & v11=vs1.1 & v21=vs2.1 by A5,A13,A15,FINSEQ_4:24;
A18: c.1 joins vs/.1, vs/.(1+1) by A12,A14,Def7;
A19: c.1 joins vs1/.1, vs1/.(1+1) by A2,A14,Def7;
A20: c.1 joins vs2/.1, vs2/.(1+1) by A3,A14,Def7;
A21: v1=v11 & v2=v12 or v1=v12 & v2=v11 by A18,A19,Th32;
   thus vs = vs1 or vs = vs2 proof
     per cases by A8,A11,A17,A18,A20,A21,Th32;
     suppose
A22:   v1 = v11;
         now thus len vs = len vs; thus len vs1 = len vs by A5,A12,Def7;
      defpred P[Element of NAT] means $1 in dom vs implies vs.$1=vs1.$1;
A23: P[0] by FINSEQ_3:27;
A24: for i st P[i] holds P[i+1]
     proof let i; assume
A25:  i in dom vs implies vs.i=vs1.i; assume i+1 in dom vs;
then A26:  1<=i+1 & i+1<=len vs by FINSEQ_3:27;
A27:  (0=i or 0<i) & 0+1=1;
     per cases by A27,NAT_1:13;
     suppose i=0;
      hence vs.(i+1) = vs1.(i+1) by A17,A22;
     end;
     suppose
A28:  1<=i;
A29:  i<=len c by A13,A26,XREAL_1:8;
then A30:  i<=len vs by A13,NAT_1:12;
 set v1 = vs/.i; set v2 = vs/.(i+1);
 set v11 = vs1/.i; set v12 = vs1/.(i+1);
A31: v1=vs.i & v11=vs1.i & v2=vs.(i+1) & v12=vs1.(i+1) by A5,A13,A26,A28,A30,
FINSEQ_4:24;
A32: c.i joins vs/.i, vs/.(i+1) by A12,A28,A29,Def7;
    c.i joins vs1/.i, vs1/.(i+1) by A2,A28,A29,Def7;
    then v1=v11 & v2=v12 or v1=v12 & v2=v11 by A32,Th32;
      hence vs.(i+1) = vs1.(i+1) by A25,A28,A30,A31,FINSEQ_3:27;
    end;
    end;
      thus for i holds P[i] from NAT_1:sch 1 (A23, A24);
      end;
     hence thesis by A16,FINSEQ_2:10;
     end;
     suppose
A33:   v1 = v21;
         now thus len vs = len vs; thus len vs2 = len vs by A5,A12,Def7;
     defpred P[Element of NAT] means $1 in dom vs implies vs.$1=vs2.$1;
A34: P[0] by FINSEQ_3:27;
A35: for i st P[i] holds P[i+1]
     proof let i; assume
A36:  i in dom vs implies vs.i=vs2.i; assume i+1 in dom vs;
then A37:   1<=i+1 & i+1<=len vs by FINSEQ_3:27;
     A38: (0=i or 0<i) & 0+1=1;
     per cases by A38,NAT_1:13;
     suppose i=0;
      hence vs.(i+1) = vs2.(i+1) by A17,A33;
     end;
     suppose
A39:  1<=i;
A40:  i<=len c by A13,A37,XREAL_1:8;
then A41:  i<=len vs by A13,NAT_1:12;
 set v1 = vs/.i; set v2 = vs/.(i+1);
 set v11 = vs2/.i; set v12 = vs2/.(i+1);
A42: v1=vs.i & v11=vs2.i & v2=vs.(i+1) & v12=vs2.(i+1) by A5,A13,A37,A39,A41,
FINSEQ_4:24;
A43: c.i joins vs/.i, vs/.(i+1) by A12,A39,A40,Def7;
    c.i joins vs2/.i, vs2/.(i+1) by A3,A39,A40,Def7;
    then v1=v11 & v2=v12 or v1=v12 & v2=v11 by A43,Th32;
      hence vs.(i+1) = vs2.(i+1) by A36,A39,A41,A42,FINSEQ_3:27;
    end;
    end;
       thus for i holds P[i] from NAT_1:sch 1 (A34, A35);
      end;
     hence thesis by A16,FINSEQ_2:10;
    end;
    end;
   end;
   suppose 1<k;
    then 1+1<=k by NAT_1:13; then consider k1 being Element of NAT such that
A44: 1<=k1 & k1<k & k=k1+1 by Th1;
A45: k<=len vs1 by A8,FINSEQ_3:27;
then A46: k1<=len c by A5,A44,XREAL_1:8;
A47: k1<=len vs1 by A44,A45,XXREAL_0:2;
then A48: k1 in dom vs1 by A44,FINSEQ_3:27;
A49: vs1/.k1=vs1.k1 & vs1/.k=vs1.k &
    vs2/.k1=vs2.k1 & vs2/.k=vs2.k
                           by A5,A6,A8,A44,A47,FINSEQ_4:24,def 4;
A50: c.k1 joins vs1/.k1, vs1/.(k1+1) by A2,A44,A46,Def7;
   c.k1 joins vs2/.k1, vs2/.(k1+1) by A3,A44,A46,Def7;
    then (SG.(c.k1)=vs1/.k1 & TG.(c.k1)=vs1/.k or
     SG.(c.k1)=vs1/.k & TG.(c.k1)=vs1/.k1) &
    (SG.(c.k1)=vs2/.k1 & TG.(c.k1)=vs2/.k or
     SG.(c.k1)=vs2/.k & TG.(c.k1)=vs2/.k1) by A44,A50,GRAPH_1:def 9;
hence thesis by A8,A9,A44,A48,A49;
end;
end;

definition let G; let c be FinSequence;
  pred c alternates_vertices_in G means
:Def8: len c>=1 & Card (G-VSet rng c) = 2 &
  for n st n in dom c holds (the Source of G).(c.n) <> (the Target of G).(c.n);
end;

theorem Th38: c alternates_vertices_in G & vs is_vertex_seq_of c
        implies for k st k in dom c holds vs.k <> vs.(k+1) proof
 assume that
A1: c alternates_vertices_in G and
A2: vs is_vertex_seq_of c;
 let k; assume
A3: k in dom c;
then A4: 1<=k & k<=len c by FINSEQ_3:27;
    set SG = (the Source of G); set TG = (the Target of G);
    set px = vs/.k; set px1 = vs/.(k+1);
     len vs = len c +1 by A2,Def7;
then k<=len vs & 1<=k+1 & k+1<=len vs by A4,NAT_1:12,XREAL_1:9;
then A5:  px = vs.k & px1 = vs.(k+1) by A4,FINSEQ_4:24;
        c.k joins px, px1 by A2,A4,Def7;
  then TG.(c.k)=px1 & SG.(c.k)=px or TG.(c.k)=px & SG.(c.k)=px1 by
GRAPH_1:def 9;
 hence vs.k <> vs.(k+1) by A1,A3,A5,Def8;
end;

theorem Th39: c alternates_vertices_in G & vs is_vertex_seq_of c
    implies rng vs = {(the Source of G).(c.1), (the Target of G).(c.1)} proof
 assume that
A1: c alternates_vertices_in G and
A2: vs is_vertex_seq_of c;
        c <>{} proof assume not thesis; then len c = 0 by FINSEQ_1:25;
                hence contradiction by A1,Def8; end;
     then G-VSet rng c = rng vs by A2,Th34;
    then card rng vs = 2 by A1,Def8;
   then consider x, y being set such that
A3:  x<>y & rng vs={x,y} by CARD_2:79;
    set SG = (the Source of G); set TG = (the Target of G);
    set px = vs/.1; set px1 = vs/.(1+1);
A4: 1<=len c by A1,Def8;
     len vs = len c +1 by A2,Def7;
then A5:  1<=len vs & 1<=1+1 & 1+1<=len vs by A4,NAT_1:12,XREAL_1:9;
then A6:  px = vs.1 & px1 = vs.(1+1) by FINSEQ_4:24;
        c.1 joins px, px1 by A2,A4,Def7;
then A7: TG.(c.1)=px1 & SG.(c.1)=px or TG.(c.1)=px & SG.(c.1)=px1 by
GRAPH_1:def 9;
       1 in dom vs & 1+1 in dom vs by A5,FINSEQ_3:27;
    then vs.1 in rng vs & vs.(1+1) in rng vs by FUNCT_1:def 5;
then A8:  (vs.1 = x or vs.1 = y) & (vs.(1+1) = x or vs.(1+1) = y) by A3,
TARSKI:def 2
;
       1 in dom c by A4,FINSEQ_3:27;
hence rng vs = {(the Source of G).(c.1), (the Target of G).(c.1)}
                                        by A1,A3,A6,A7,A8,Def8;
end;

theorem Th40: c alternates_vertices_in G & vs is_vertex_seq_of c
   implies vs is TwoValued Alternating FinSequence proof
   assume that
A1: c alternates_vertices_in G and
A2: vs is_vertex_seq_of c;
     set SG = (the Source of G); set TG = (the Target of G);
A3: 1<=len c & Card (G-VSet rng c) = 2 &
     for n st n in dom c holds SG.(c.n) <> TG.(c.n) by A1,Def8;
        c <>{} proof assume not thesis; then len c = 0 by FINSEQ_1:25;
                hence contradiction by A1,Def8; end;
then A4: card rng vs = 2 by A2,A3,Th34;
A5: len vs = len c + 1 &
     for n st 1<=n & n<=len c holds c.n joins vs/.n,vs/.(n+1)
                                                          by A2,Def7;
      now let k be Element of NAT; assume that
A6:  1<=k and
A7:  (k+1)<=len vs;
        k<=len c by A5,A7,XREAL_1:8; then k in dom c by A6,FINSEQ_3:27;
    hence vs.k <> vs.(k+1) by A1,A2,Th38;
   end; hence vs is TwoValued Alternating FinSequence
                                by A4,Def3,Def4;
end;

theorem Th41:  c alternates_vertices_in G implies
ex vs1,vs2 st vs1<>vs2 & vs1 is_vertex_seq_of c & vs2 is_vertex_seq_of c &
     for vs st vs is_vertex_seq_of c holds vs=vs1 or vs=vs2 proof assume
A1: c alternates_vertices_in G;
then A2: 1<=len c by Def8;
    consider p1 being FinSequence of the Vertices of G such that
A3: p1 is_vertex_seq_of c by Th36;
A4: len p1 = len c + 1 by A3,Def7;
then A5: len p1 > 1 by A2,NAT_1:13;
    set X = (the Source of G).(c.1); set Y = (the Target of G).(c.1);
A6: rng p1 = {X, Y} by A1,A3,Th39;
A7: p1 is TwoValued Alternating FinSequence by A1,A3,Th40;
     A8: 1 in dom c & 1+1=2 by A2,FINSEQ_3:27;
then A9: p1.1 <> p1.2 by A1,A3,Th38;
    then consider p2 being TwoValued Alternating FinSequence such that
A10: rng p2 = {p1.2, p1.1} & len p2 = len p1 & p2.1=p1.2 by A5,Th23;
A11:  dom p1 = dom p2 by A10,FINSEQ_3:31;
       1+1<=len p1 by A5,NAT_1:13;
    then 1<=len p1 & 2<=len p1 by NAT_1:13;
    then 1 in dom p1 & 2 in dom p1 by FINSEQ_3:27;
    then p1.1 in rng p1 & p1.2 in rng p1 by FUNCT_1:def 5;
   then A12: (p1.1 = X or p1.1 = Y) & (p1.2 = X or p1.2 = Y) & {X, Y} = {Y, X}
                                         by A6,TARSKI:def 2;
    then rng p2 c= the Vertices of G by A1,A3,A6,A8,A10,Th38,FINSEQ_1:def 4;
    then reconsider p2 as FinSequence of the Vertices of G by FINSEQ_1:def 4;
 take p1, p2; thus
   p1<>p2 by A1,A3,A8,A10,Th38;
 thus p1 is_vertex_seq_of c by A3;
     now thus len p2 = len c + 1 by A3,A10,Def7;
   let n be Element of NAT such that
A13: 1<=n & n<=len c;
    set x = p1/.n; set y = p1/.(n+1);
A14: c.n joins x, y by A3,A13,Def7;
A15: 1<=n & n<=len p1 & 1<=n+1 & n+1<=len p1
                                  by A4,A13,XREAL_1:8,NAT_1:12;
     then n in dom p1 & n+1 in dom p1 by FINSEQ_3:27;
     then p1.n in {X, Y} & p1.(n+1) in {X, Y} &
     p2.n in {X, Y} & p2.(n+1) in {X, Y} by A1,A3,A6,A8,A10,A11,A12,Th38,
FUNCT_1:def 5;
 then (p1.n=X or p1.n=Y) & (p2.n=X or p2.n=Y) &
     (p1.(n+1)=X or p1.(n+1)=Y) & (p2.(n+1)=X or p2.(n+1)=Y) by TARSKI:def 2;
 then A16: y = p2.n & x = p2.(n+1) by A6,A7,A9,A10,A12,A15,Def4,Th21,
FINSEQ_4:24;
       p2/.n = p2.n & p2/.(n+1) = p2.(n+1) by A10,A15,FINSEQ_4:24;
   hence c.n joins p2/.n,p2/.(n+1) by A14,A16,Th31;
  end;
 hence p2 is_vertex_seq_of c by Def7;
 let p be FinSequence of the Vertices of G; assume
A17: p is_vertex_seq_of c;
  then reconsider p' = p as TwoValued Alternating FinSequence by A1,Th40;
A18: len p = len c + 1 by A17,Def7;
    rng p = {X, Y} by A1,A17,Th39;
 then p'=p1 or p'=p2 by A4,A6,A7,A9,A10,A12,A18,Th22;
 hence thesis;
end;

Lm8:
(for x, y st x in D & y in D holds x=y) implies Card D = 1 proof assume
A1: for x, y st x in D & y in D holds x=y;
   consider x being Element of D;  for y holds y in D iff y=x by A1;
   then D = {x} by TARSKI:def 1; hence Card D = 1 by CARD_2:60;
end;

theorem Th42:
 vs is_vertex_seq_of c implies
     (Card the Vertices of G = 1 or c <>{} & not c alternates_vertices_in G
 iff for vs1 st vs1 is_vertex_seq_of c holds vs1 = vs) proof assume
A1: vs is_vertex_seq_of c;
 hereby assume
A2: Card the Vertices of G = 1 or c <>{} & not c alternates_vertices_in G;
  per cases by A2;
  suppose
A3: Card the Vertices of G = 1;
   then reconsider tVG = the Vertices of G as finite set by CARD_4:1;
   consider X being set such that
A4: tVG = {X} by A3,CARD_2:60;
   let vs1; assume
A5: vs1 is_vertex_seq_of c; assume
A6: vs1 <> vs;
A7: len vs1 = len c +1 & len vs = len c +1 by A1,A5,Def7;
A8:   Seg len vs1 = dom vs1 & Seg len vs = dom vs by FINSEQ_1:def 3;
   then consider j such that
A9:  j in dom vs & vs1.j <> vs.j by A6,A7,FINSEQ_2:10;
A10:  vs1.j in rng vs1 & vs.j in rng vs by A7,A8,A9,FUNCT_1:def 5;
       rng vs1 c= {X} & rng vs c= {X} by A4,FINSEQ_1:def 4;
    then vs.j = X & vs1.j = X by A10,TARSKI:def 1;
   hence contradiction by A9;
  end;
  suppose
A11: c <>{} & not c alternates_vertices_in G;
  thus for vs1 st vs1 is_vertex_seq_of c holds vs1 = vs proof let vs1; assume
A12: vs1 is_vertex_seq_of c; assume
A13: vs1 <> vs;
  set SG = the Source of G; set TG = the Target of G;
A14: len vs1 = len c +1 & len vs = len c +1 by A1,A12,Def7;
then A15:  dom vs1 = dom vs by FINSEQ_3:31;
   defpred P[Element of NAT] means $1 in dom vs1 & vs1.$1<>vs.$1;
      Seg len vs1 = dom vs1 & Seg len vs = dom vs by FINSEQ_1:def 3;
then A16: ex i st P[i] by A13,A14,FINSEQ_2:10;
   consider k such that
A17: P[k] and
A18: for n st P[n] holds k<=n from NAT_1:sch 5 (A16);
       0<=k; then A19: 0+1=1 & k=0 or 0+1<=k by NAT_1:13;
   per cases by A19,REAL_1:def 5;
   suppose k=0; hence contradiction by A17,FINSEQ_3:27;
   end;
   suppose
A20:  k=1; thus contradiction proof A21: 0+1=1 &(len c =0 or 0<len c);
    per cases by A21,NAT_1:13;
    suppose len c = 0; hence contradiction by A11,FINSEQ_1:25;
    end;
    suppose
A22:   1<=len c;
then 1<=k+1 & k+1<=len vs1 by A14,A20,XREAL_1:8;
then A23: vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) &
    vs/.k=vs.k & vs/.(k+1)=vs.(k+1) by A14,A15,A17,FINSEQ_4:24,def 4;
A24:  c.k joins vs1/.k, vs1/.(k+1) by A12,A20,A22,Def7;
     c.k joins vs/.k, vs/.(k+1) by A1,A20,A22,Def7;
then A25: (SG.(c.1)=vs1/.1 & TG.(c.1)=vs1/.(k+1) or
     SG.(c.1)=vs1/.(k+1) & TG.(c.1)=vs1/.1) &
    (SG.(c.1)=vs/.1 & TG.(c.1)=vs/.(k+1) or
     SG.(c.1)=vs/.(k+1) & TG.(c.1)=vs/.1) by A20,A24,GRAPH_1:def 9;
defpred P[Element of NAT] means $1 in dom c implies
           vs1/.$1<>vs/.$1 & vs1/.($1+1)<>vs/.($1+1) &
           (TG.(c.$1)=TG.(c.1) & SG.(c.$1)=SG.(c.1) or
            TG.(c.$1)=SG.(c.1) & SG.(c.$1)=TG.(c.1));
A26: P[0] by FINSEQ_3:27;
A27: now let n be Element of NAT; assume
A28:  P[n];
      thus P[n+1]
      proof
       assume (n+1) in dom c;
       then A29:    1<=n+1 & n+1<=len c by FINSEQ_3:27;
       thus vs1/.(n+1)<>vs/.(n+1) & vs1/.(n+1+1)<>vs/.(n+1+1) &
          (TG.(c.(n+1))=TG.(c.1) & SG.(c.(n+1))=SG.(c.1) or
          TG.(c.(n+1))=SG.(c.1) & SG.(c.(n+1))=TG.(c.1)) proof
     per cases;
      suppose
A30: n=0;  1<=len c by A29,XXREAL_0:2;
  then c.1 joins vs1/.1,vs1/.(1+1) & c.1 joins vs/.1,vs/.(1+1)
                                          by A1,A12,Def7;
then A31: (SG.(c.1)=vs1/.1 & TG.(c.1)=vs1/.(1+1) or
     SG.(c.1)=vs1/.(1+1) & TG.(c.1)=vs1/.1) &
    (SG.(c.1)=vs/.1 & TG.(c.1)=vs/.(1+1) or
     SG.(c.1)=vs/.(1+1) & TG.(c.1)=vs/.1) by GRAPH_1:def 9;
     thus vs1/.(n+1)<>vs/.(n+1) by A17,A20,A23,A30;
     thus vs1/.(n+1+1)<>vs/.(n+1+1) by A17,A20,A23,A30,A31;
     thus thesis by A30;
      end;
      suppose 0<n; then 0+1<=n & n<=n+1 by NAT_1:13;
then A32:  1<=n & n<=len c by A29,XXREAL_0:2;
then A33:  c.n joins vs1/.n, vs1/.(n+1) by A12,Def7;
     c.n joins vs/.n, vs/.(n+1) by A1,A32,Def7;
then A34: (SG.(c.n)=vs1/.n & TG.(c.n)=vs1/.(n+1) or
     SG.(c.n)=vs1/.(n+1) & TG.(c.n)=vs1/.n) &
    (SG.(c.n)=vs/.n & TG.(c.n)=vs/.(n+1) or
     SG.(c.n)=vs/.(n+1) & TG.(c.n)=vs/.n) by A33,GRAPH_1:def 9;
A35:  c.(n+1) joins vs1/.(n+1), vs1/.(n+1+1) by A12,A29,Def7;
     c.(n+1) joins vs/.(n+1), vs/.(n+1+1) by A1,A29,Def7;
then A36: (SG.(c.(n+1))=vs1/.(n+1) & TG.(c.(n+1))=vs1/.(n+1+1) or
     SG.(c.(n+1))=vs1/.(n+1+1) & TG.(c.(n+1))=vs1/.(n+1)) &
     (SG.(c.(n+1))=vs/.(n+1) & TG.(c.(n+1))=vs/.(n+1+1) or
     SG.(c.(n+1))=vs/.(n+1+1) & TG.(c.(n+1))=vs/.(n+1)) by A35,GRAPH_1:def 9;
     thus vs1/.(n+1)<> vs/.(n+1) by A28,A32,FINSEQ_3:27;
     thus vs1/.(n+1+1)<>vs/.(n+1+1) by A28,A32,A36,FINSEQ_3:27;
      thus thesis by A28,A32,A34,A36,FINSEQ_3:27;
     end;
     end;
     end;
    end;
A37: for n holds P[n] from NAT_1:sch 1(A26,A27);
A38: now let n;  n in dom c implies
       TG.(c.n)=TG.(c.1) & SG.(c.n)=SG.(c.1) or
       TG.(c.n)=SG.(c.1) & SG.(c.n)=TG.(c.1) by A37;
    hence n in dom c implies SG.(c.n) <> TG.(c.n) by A17,A20,A23,A25;
   end;
      now let x be set;
    hereby assume x in G-VSet rng c; then consider v such that
A39:  x=v & ex e being Element of the Edges of G st e in rng c &
             (v = (the Source of G).e or v = (the Target of G).e);
     consider e such that
A40: e in rng c & (v = SG.e or v = TG.e) by A39;
     consider d being set such that
A41:  d in dom c & e=c.d by A40,FUNCT_1:def 5;
     reconsider d as Element of NAT by A41;
              TG.(c.d)=TG.(c.1) & SG.(c.d)=SG.(c.1) or
            TG.(c.d)=SG.(c.1) & SG.(c.d)=TG.(c.1) by A37,A41;
hence x in {SG.(c.1), TG.(c.1)} by A39,A40,A41,TARSKI:def 2;
    end; assume A42: x in {SG.(c.1), TG.(c.1)};
        len c <> 0 by A11,FINSEQ_1:25; then 0< len c;
     then 0+1<=len c by NAT_1:13;
then A43:  1 in dom c & 1 in dom c by FINSEQ_3:27;
   then A44: c.1 in rng c & rng c c= the Edges of G
   by FINSEQ_1:def 4,FUNCT_1:def 5;
     then reconsider e=c.1 as Element of the Edges of G;
     reconsider s = SG.e as Element of the Vertices of G by A44,FUNCT_2:7;
     reconsider t = TG.e as Element of the Vertices of G by A44,FUNCT_2:7;
A45:  e in rng c by A43,FUNCT_1:def 5;
        x=s or x =t by A42,TARSKI:def 2; hence x in G-VSet rng c by A45;
   end;
 then G-VSet rng c = {SG.(c.1), TG.(c.1)} by TARSKI:2;
   then Card (G-VSet rng c) = 2 by A17,A20,A23,A25,CARD_2:76;
   hence contradiction by A11,A22,A38,Def8;
   end;
   end;
   end;
   suppose 1<k; then 1+1<=k by NAT_1:13;
    then consider k1 being Element of NAT such that
A46: 1<=k1 & k1<k & k=k1+1 by Th1;
A47: k<=len vs1 by A17,FINSEQ_3:27;
then A48: k1<=len c by A14,A46,XREAL_1:8;
A49: k1<=len vs1 by A46,A47,XXREAL_0:2;
then A50: k1 in dom vs1 by A46,FINSEQ_3:27;
A51: vs1/.k1=vs1.k1 & vs1/.k=vs1.k &
    vs/.k1=vs.k1 & vs/.k=vs.k
            by A14,A15,A17,A46,A49,FINSEQ_4:24,def 4;
A52: c.k1 joins vs1/.k1, vs1/.(k1+1) by A12,A46,A48,Def7;
   c.k1 joins vs/.k1, vs/.(k1+1) by A1,A46,A48,Def7;
    then (SG.(c.k1)=vs1/.k1 & TG.(c.k1)=vs1/.k or
     SG.(c.k1)=vs1/.k & TG.(c.k1)=vs1/.k1) &
    (SG.(c.k1)=vs/.k1 & TG.(c.k1)=vs/.k or
     SG.(c.k1)=vs/.k & TG.(c.k1)=vs/.k1) by A46,A52,GRAPH_1:def 9;
hence contradiction by A17,A18,A46,A50,A51;
  end;
  end;
 end;
 end; assume
A53: for vs1 st vs1 is_vertex_seq_of c holds vs1 = vs; assume
A54: Card the Vertices of G <> 1; assume
A55: c ={} or c alternates_vertices_in G;
   consider x, y such that
A56: x in the Vertices of G & y in the Vertices of G & x<>y by A54,Lm8;
   reconsider x as Element of the Vertices of G by A56;
   reconsider y as Element of the Vertices of G by A56;
 thus contradiction proof per cases by A55;
 suppose c ={};
  then <*x*> is_vertex_seq_of c & <*y*> is_vertex_seq_of c by Th35;
   then <*x*> = vs & <*y*> = vs by A53;
   then vs.1 = x & vs.1 = y by FINSEQ_1:57;
  hence contradiction by A56;
 end;
 suppose c alternates_vertices_in G; then consider vs1,vs2 such that
A57: vs1<>vs2 & vs1 is_vertex_seq_of c & vs2 is_vertex_seq_of c &
     for vs st vs is_vertex_seq_of c holds vs=vs1 or vs=vs2 by Th41;
      vs1 = vs & vs2 = vs by A53,A57;
  hence contradiction by A57;
 end;
 end;
end;

definition let G, c; assume
A1: Card the Vertices of G = 1 or c <>{} & not c alternates_vertices_in G;
 func vertex-seq c -> FinSequence of the Vertices of G means
    it is_vertex_seq_of c;
existence by Th36;
uniqueness by A1,Th42;
end;

theorem Th43: vs is_vertex_seq_of c & c1 = c|Seg n & vs1= vs|Seg (n+1)
    implies vs1 is_vertex_seq_of c1
proof assume A1: vs is_vertex_seq_of c;
then A2: len vs = len c + 1 &
    for n st 1<=n & n<=len c holds c.n joins vs/.n,vs/.(n+1)
                                                              by Def7;
 assume that
A3: c1 = c|Seg n and
A4: vs1= vs|Seg (n+1);
    now
  per cases;
  suppose
A5: n<=len c; then n+1<=len vs by A2,XREAL_1:8;
then A6: len c1 = n & len vs1 = n+1 by A3,A4,A5,FINSEQ_1:21;
   hence len vs1 = len c1 + 1;
   let k be Element of NAT;
   assume that
A7: 1<=k and
A8: k<=len c1;
A9: k<=len c by A5,A6,A8,XXREAL_0:2;
    then k in dom c & k in Seg n by A6,A7,A8,FINSEQ_1:3,FINSEQ_3:27;
then A10: c1.k = c.k by A3,FUNCT_1:72; A11: k<=len vs by A2,A9,NAT_1:12;
A12: k<=n+1 by A6,A8,NAT_1:12;
then A13: k in dom vs & k in Seg (n+1) by A7,A11,FINSEQ_1:3,FINSEQ_3:27;
A14: k+1<=len vs by A2,A9,XREAL_1:9;
A15: 1<=k+1 & k+1<=len c1 +1 by A8,NAT_1:12,XREAL_1:9;
    then k+1 in dom vs & k+1 in Seg (n+1) by A6,A14,FINSEQ_1:3,FINSEQ_3:27;
then A16: vs1.k=vs.k & vs1.(k+1)=vs.(k+1) by A4,A13,FUNCT_1:72;
A17: vs/.k=vs.k & vs/.(k+1)=vs.(k+1) by A7,A11,A14,A15,FINSEQ_4:24;
       vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) by A6,A7,A12,A15,FINSEQ_4:24;
   hence c1.k joins vs1/.k,vs1/.(k+1) by A1,A7,A9,A10,A16,A17,Def7;
  end;
  suppose
A18: len c <=n; then len vs<=n+1 by A2,XREAL_1:8;
    then c1 = c & vs1 = vs by A3,A4,A18,FINSEQ_2:23;
   hence len vs1 = len c1 + 1 &
         for k being Element of NAT st 1<=k & k<=len c1
            holds c1.k joins vs1/.k,vs1/.(k+1) by A1,Def7;
  end;
  end; hence vs1 is_vertex_seq_of c1 by Def7;
end;

theorem Th44:
1<=m & m<=n & n<=len c & q = (m,n)-cut c implies q is Chain of G
proof assume that
A1: 1<=m and
A2: m<=n and
A3: n<=len c; assume
A4: q = (m,n)-cut c; consider vs such that
A5: vs is_vertex_seq_of c by Th36; set p' = (m,n+1)-cut vs;
A6:  len vs = len c + 1 by A5,Def7;
then A7: m<=n+1 & n+1<=len vs by A2,A3,XREAL_1:8,NAT_1:12;
       A8: len q +m-m=n+1-m by A1,A3,A4,A7,Lm2;
then A9: len q = n-m+1;
        len p'+m-m=(n+1)+1-m by A1,A7,Def1;
then A10: len p' = n-m+1+1;
A11: now let k be Element of NAT;
     assume that
A12:   1<=k and
A13:   k<=len p';
          k in dom p' by A12,A13,FINSEQ_3:27;
then A14:    p'.k in rng p' by FUNCT_1:def 5;
          rng p' c= rng vs by Th11;
then A15:    p'.k in rng vs by A14;
          rng vs c= the Vertices of G by FINSEQ_1:def 4;
      hence p'.k in the Vertices of G by A15;
     end;
A16: now let k be Element of NAT;  1-1<=m-1 by A1,XREAL_1:11;
     then m-1 = m-'1 by BINARITH:def 3;
     then reconsider m1= m-1 as Element of NAT;
      reconsider i = m1+k as Element of NAT;
    assume that
A17: 1<=k and
A18: k<=len q;  0+1<=k by A17; then consider j such that
A19:  0<=j & j<len q & k=j+1 by A18,Th1;
A20:  i= m+j by A19;
     set v1 = vs/.i; set v2 = vs/.(i+1);
        1-1<=m-1 by A1,XREAL_1:11;
     then A21: 0+1<=m-1+k by A17,XREAL_1:9;
    i<=(m-1)+(n-(m-1)) by A9,A18,XREAL_1:8;
then A22:  1<=i & i<=len c by A3,A21,XXREAL_0:2;
then A23:  c.i joins v1, v2 by A5,Def7;
 A24: i<=len vs & 1<=i+1 & i+1<=len vs by A6,A22,NAT_1:12,XREAL_1:9;
A25:  i+1 = m+(j+1) by A19;
        j<len p' & j+1<len p' by A9,A10,A19,NAT_1:13,XREAL_1:8;
then A26:  p'.k = vs.i & p'.(k+1) = vs.(i+1) by A1,A7,A20,A25,Def1;
    take v1, v2;
    thus v1 = p'.k & v2 = p'.(k+1) & q.k joins v1, v2 by A1,A3,A4,A7,A19,A20
,A21,A23,A24,A26,Lm2,FINSEQ_4:24;
   end;
 thus q is Chain of G proof
  hereby let k be Element of NAT;
   assume that
A27: 1<=k and
A28: k<=len q;
      k in dom q by A27,A28,FINSEQ_3:27;
then A29: q.k in rng q by FUNCT_1:def 5;  rng q c= rng c by A4,Th11;
then A30: q.k in rng c by A29;  rng c c= the Edges of G by FINSEQ_1:def 4;
  hence q.k in the Edges of G by A30;
  end;
  take p'; thus len p' = len q + 1 by A8,A10;
  thus (for n st 1<=n & n<=len p' holds p'.n in the Vertices of G) by A11;
  thus thesis by A16;
   end;
end;

theorem Th45:
  1<=m & m<=n & n<=len c & c1 = (m,n)-cut c &
vs is_vertex_seq_of c & vs1 = (m,n+1)-cut vs
    implies vs1 is_vertex_seq_of c1 proof
    assume that
A1: 1<=m and
A2: m<=n and
A3: n<=len c; assume
A4: c1 = (m,n)-cut c;
   assume that
A5: vs is_vertex_seq_of c and
A6: vs1 = (m,n+1)-cut vs;
A7: len vs = len c + 1 by A5,Def7;
then A8: m<=n+1 & n+1<=len vs by A2,A3,XREAL_1:8,NAT_1:12;
A9: len c1 +m= n+1 by A1,A3,A4,A8,Lm2;
    len vs1 +m = n+1+1 by A1,A6,A8,Def1;
hence A10: len vs1 = len c1 + 1 by A9;
    let k be Element of NAT;
    assume that
A11: 1<=k and
A12: k<=len c1;  0+1<=k by A11; then consider j such that
A13:  0<=j & j<len c1 & k=j+1 by A12,Th1;
     set i = m+j; set v1 = vs/.i; set v2 = vs/.(i+1);
        m+k<=len c1 +m by A12,XREAL_1:9;
     then m+j+1-1<=len c1 +m-1 by A13,XREAL_1:11;
then A14:  1<=i & i<=len c by A1,A3,A9,XXREAL_0:2,NAT_1:12;
then A15:  c.i joins v1, v2 by A5,Def7;
         j<len vs1 by A10,A13,NAT_1:13;
then A16: vs1.k = vs.i by A1,A6,A8,A13,Def1;
A17:  j+1<len vs1 by A10,A13,XREAL_1:8;
         m+j+1 = m+(j+1);
then A18:  vs1.(k+1) = vs.(i+1) by A1,A6,A8,A13,A17,Def1;
      i<=len vs & 1<=i+1 & i+1<=len vs by A7,A14,NAT_1:12,XREAL_1:9;
then A19:  vs/.i=vs.i & vs/.(i+1)=vs.(i+1) by A14,FINSEQ_4:24;
         0+1=1;
then A20:   1<=k & k<=len c1 by A13,NAT_1:13;
   then k<=len vs1 & 1<=k+1 & k+1<=len vs1 by A10,NAT_1:12,XREAL_1:9;
then vs1/.k=vs1.k & vs1/.(k+1)=vs1.(k+1) by A20,FINSEQ_4:24;
    hence c1.k joins vs1/.k, vs1/.(k+1) by A1,A3,A4,A8,A13,A15,A16,A18,A19,Lm2
;
   end;

theorem Th46:
vs1 is_vertex_seq_of c1 & vs2 is_vertex_seq_of c2 & vs1.len vs1 = vs2.1
    implies c1^c2 is Chain of G proof
    assume that
A1: vs1 is_vertex_seq_of c1 and
A2: vs2 is_vertex_seq_of c2 & vs1.len vs1 = vs2.1;
A3: len vs1 = len c1 + 1 & len vs2 = len c2 + 1 by A1,A2,Def7;
then A4: vs1 <> {} & vs2 <> {} by FINSEQ_1:25;
  set q=c1^c2; set p=vs1^'vs2;  len p +1 = len vs1 +len vs2 by A4,Th13;
then A5: len p = len vs1 + len vs2 - 1;
then A6: len p = len c1 + len c2 + 1 by A3
         .= len q + 1 by FINSEQ_1:35;
A7: now let n be Element of NAT such that
A8:    1<=n & n<=len p;
      per cases;
      suppose
A9:     n<=len vs1;
then A10:     p.n = vs1.n by A8,Th14;  n in dom vs1 by A8,A9,FINSEQ_3:27;
hence p.n in the Vertices of G by A10,FINSEQ_2:13;
      end;
      suppose
A11:     n > len vs1; then consider m being Nat such that
A12:      n = len vs1 + m by NAT_1:10;
          reconsider m as Element of NAT by ORDINAL1:def 13;
          m <> 0 by A11,A12;
       then 0 < m; then A13: 0+1<=m by NAT_1:13;
A14:    1<=m+1 by NAT_1:12;
          len vs1 + m<=len vs1 + (len vs2 - 1) by A5,A8,A12;
       then m<=len vs2 - 1 by XREAL_1:8;
       then A15: m+1<=len vs2 - 1+1 by XREAL_1:8; then m<len vs2 by NAT_1:13;
then A16:     p.(len vs1 + m) = vs2.(m+1) by A13,Th15;
          m+1 in dom vs2 by A14,A15,FINSEQ_3:27;
hence p.n in the Vertices of G by A12,A16,FINSEQ_2:13;
    end;
    end;
A17: now let n be Element of NAT; assume
     1<=n & n<=len q;
then A18:  n in dom q by FINSEQ_3:27;
    per cases by A18,FINSEQ_1:38;
    suppose
A19:   n in dom c1;
then A20:   q.n = c1.n by FINSEQ_1:def 7;
     set v1=vs1/.n; set v2=vs1/.(n+1);
A21:   1<=n & n<=len c1 by A19,FINSEQ_3:27;
then A22:   c1.n joins v1, v2 by A1,Def7;
     A23:   n<=len vs1 by A3,A21,NAT_1:12;
        1<=n+1 & n+1<=len c1+1 by A21,XREAL_1:8,NAT_1:12;
then A24:  1<=n+1 & n+1<=len vs1 by A1,Def7;
then A25:  vs1/.n=vs1.n & vs1/.(n+1)=vs1.(n+1) by A21,A23,FINSEQ_4:24;
        p.n = vs1.n & p.(n+1) = vs1.(n+1) by A21,A23,A24,Th14;
    hence ex v1, v2 being Element of the Vertices of G st
           v1 = p.n & v2 = p.(n+1) & q.n joins v1, v2 by A20,A22,A25;
    end;
    suppose ex k being Element of NAT st k in dom c2 & n=len c1 + k;
     then consider k being Element of NAT such that
A26:   k in dom c2 & n = len c1 + k;
A27:   q.n = c2.k by A26,FINSEQ_1:def 7;
A28:   1<=k & k<=len c2 by A26,FINSEQ_3:27;
then A29:  1<=k+1 & k<=len vs2 & k+1<=len vs2 by A3,NAT_1:12,XREAL_1:9;
     set v1 = vs2/.k; set v2 = vs2/.(k+1);
A30: vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A28,A29,FINSEQ_4:24;
A31:   c2.k joins v1, v2 by A2,A28,Def7;
A32:  k<=len vs2 by A3,A28,NAT_1:12;
        0+1<=k by A26,FINSEQ_3:27; then consider j such that
A33:  0<=j & j<len vs2 & k=j+1 by A32,Th1;
A34:   p.n = vs2.k
     proof per cases by A28,REAL_1:def 5;
      suppose A35: 1=k;  0<len vs1 by A3;
then 0+1<=len vs1 by NAT_1:13;
       hence p.n = vs2.k by A2,A3,A26,A35,Th14;
      end;
      suppose 1<k; then A36:1<=j by A33,NAT_1:13;
       thus p.n = p.(len vs1 +j) by A3,A26,A33
        .= vs2.k by A33,A36,Th15;
     end;
     end;
A37:   k<len vs2 by A3,A28,NAT_1:13;
        p.(n+1) = p.(len c1 +1+k) by A26
            .= vs2.(k+1) by A3,A28,A37,Th15;
    hence ex v1, v2 being Element of the Vertices of G st
           v1 = p.n & v2 = p.(n+1) & q.n joins v1, v2 by A27,A30,A31,A34;
   end;
   end;
 thus c1^c2 is Chain of G proof
  hereby let n be Element of NAT; assume
     1<=n & n<=len q;
then A38:  n in dom q by FINSEQ_3:27;
    per cases by A38,FINSEQ_1:38;
    suppose
A39:   n in dom c1;
then A40:    1<=n & n<=len c1 by FINSEQ_3:27;
       q.n = c1.n by A39,FINSEQ_1:def 7;
      hence (c1^c2).n in the Edges of G by A40,GRAPH_1:def 11;
    end;
    suppose ex k being Element of NAT st k in dom c2 & n=len c1 + k;
     then consider k being Element of NAT such that
A41:   k in dom c2 & n = len c1 + k;
A42:   q.n = c2.k by A41,FINSEQ_1:def 7;
        1<=k & k<=
len c2 by A41,FINSEQ_3:27; hence (c1^c2).n in the Edges of G by A42,
GRAPH_1:def 11;
  end;
  end; thus thesis by A6,A7,A17;
 end;
end;

theorem Th47:
vs1 is_vertex_seq_of c1 & vs2 is_vertex_seq_of c2 & vs1.len vs1 = vs2.1 &
c = c1^c2 & vs = vs1^'vs2
   implies vs is_vertex_seq_of c proof
   assume that
A1: vs1 is_vertex_seq_of c1 and
A2: vs2 is_vertex_seq_of c2 & vs1.len vs1 = vs2.1;
   assume that
A3: c = c1^c2 and
A4: vs = vs1^'vs2;
A5: len vs1 = len c1 + 1 & len vs2 = len c2 + 1 by A1,A2,Def7;
then A6: vs1 <> {} & vs2 <> {} by FINSEQ_1:25;
  set q=c1^c2; set p=vs1^'vs2;
     len p +1 = len vs1 +len vs2 by A6,Th13;
  then len p = len vs1 + len vs2 - 1;
then A7: len p = len c1 + len c2 + 1 by A5
         .= len q + 1 by FINSEQ_1:35;
   reconsider p as FinSequence of the Vertices of G;
   now let n be Element of NAT;
     assume that
A8:  1<=n and
A9:  n<=len q;
     n<=len p & 1<=n+1 & n+1<=len p by A7,A9,NAT_1:12,XREAL_1:9;
then A10:  p/.n=p.n & p/.(n+1)=p.(n+1) by A8,FINSEQ_4:24;
A11:  n in dom q by A8,A9,FINSEQ_3:27;
    per cases by A11,FINSEQ_1:38;
    suppose
A12:   n in dom c1;
     set v1=vs1/.n; set v2=vs1/.(n+1);
A13:   1<=n & n<=len c1 by A12,FINSEQ_3:27;
then A14:   c1.n joins v1, v2 by A1,Def7;
     A15:   n<=len vs1 by A5,A13,NAT_1:12;
        1<=n+1 & n+1<=len c1+1 by A13,XREAL_1:8,NAT_1:12;
then A16:  1<=n+1 & n+1<=len vs1 by A1,Def7;
then A17:  vs1/.n=vs1.n & vs1/.(n+1)=vs1.(n+1) by A13,A15,FINSEQ_4:24;
        p.n = vs1.n & p.(n+1) = vs1.(n+1) by A13,A15,A16,Th14;
    hence q.n joins p/.n, p/.(n+1) by A10,A12,A14,A17,FINSEQ_1:def 7;
    end;
    suppose ex k being Element of NAT st k in dom c2 & n=len c1 + k;
     then consider k being Element of NAT such that
A18:   k in dom c2 & n = len c1 + k;
A19:   1<=k & k<=len c2 by A18,FINSEQ_3:27;
then A20:  1<=k+1 & k<=len vs2 & k+1<=len vs2 by A5,NAT_1:12,XREAL_1:9;
     set v1 = vs2/.k; set v2 = vs2/.(k+1);
A21:  vs2/.k=vs2.k & vs2/.(k+1)=vs2.(k+1) by A19,A20,FINSEQ_4:24;
A22:   c2.k joins v1, v2 by A2,A19,Def7;
A23:  k<=len vs2 by A5,A19,NAT_1:12;
        0+1<=k by A18,FINSEQ_3:27; then consider j such that
A24:  0<=j & j<len vs2 & k=j+1 by A23,Th1;
A25:   p.n = vs2.k proof per cases by A19,REAL_1:def 5;
      suppose A26: 1=k;  0<len vs1 by A5;
then 0+1<=len vs1 by NAT_1:13;
       hence p.n = vs2.k by A2,A5,A18,A26,Th14;
      end;
      suppose 1<k; then A27:1<=j by A24,NAT_1:13;
       thus p.n = p.(len vs1 +j) by A5,A18,A24
        .= vs2.k by A24,A27,Th15;
     end;
     end;
A28:   k<len vs2 by A5,A19,NAT_1:13;
        p.(n+1) = p.(len c1 +1+k) by A18
            .= vs2.(k+1) by A5,A19,A28,Th15;
    hence q.n joins p/.n,p/.(n+1) by A10,A18,A21,A22,A25,FINSEQ_1:def 7;
   end;
   end;
hence vs is_vertex_seq_of c by A3,A4,A7,Def7;
end;

begin :: Simple chains, paths and oriented chains

 Lm9: <*v*> is_vertex_seq_of {} proof
    set p = <*v*>, ec = {};
A1:  len p = 0 + 1 by FINSEQ_1:56 .= len ec + 1 by FINSEQ_1:25;
    for n st 1<=n & n<=len ec
      holds ec.n joins p/.n,p/.(n+1) by FINSEQ_1:25;
   hence p is_vertex_seq_of ec by A1,Def7;
end;

definition let G;
  let IT be Chain of G;
   attr IT is simple means
:Def10: ex vs st vs is_vertex_seq_of IT &
     (for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs);
end;

registration let G;
   cluster simple Chain of G;
existence proof consider q being empty Chain of G;
     take q; consider x being Element of the Vertices of G;
     reconsider p =<*x*> as FinSequence of the Vertices of G;
     take p;
  thus p is_vertex_seq_of q by Lm9;
   let n,m; assume 1<=n & n<m & m<=len p & p.n = p.m;
       then 1<m & m<=1 by XXREAL_0:2,FINSEQ_1:56;
   hence n =1 & m = len p;
 end;
end;

reserve sc for simple Chain of G;

canceled;

theorem  sc|(Seg n) is simple Chain of G proof consider vs such that
A1:  vs is_vertex_seq_of sc &
   (for n, m st 1<=n & n < m & m<=len vs & vs.n = vs.m holds
                                   n=1 & m=len vs) by Def10;
 reconsider q' = sc|Seg n as Chain of G by GRAPH_1:4;
reconsider p' = vs|Seg(n+1) as FinSequence of the Vertices of G by FINSEQ_1:23;
       now
     take p'; thus p' is_vertex_seq_of q' by A1,Th43;
     let k, m;
     assume that
A2:  1<=k and
A3:  k < m and
A4:  m<=len p' and
A5:   p'.k = p'.m;
         k<=len p' & 1<=m by A2,A3,A4,XXREAL_0:2;
then A6:   p'.k = vs.k & p'.m = vs.m by A2,A4,Th2;
A7:   len p'<=len vs by Th2;
then A8:   m<=len vs by A4,XXREAL_0:2;
     hence k=1 by A1,A2,A3,A5,A6;
        len p' = len vs or len p' < len vs by A7,REAL_1:def 5;
     hence m = len p' by A1,A2,A3,A4,A5,A6,A8;
    end; hence thesis by Def10;
end;

theorem Th50:
  2<len sc & vs1 is_vertex_seq_of sc & vs2 is_vertex_seq_of sc
     implies vs1=vs2 proof
     assume that
A1: 2<len sc and
A2: vs1 is_vertex_seq_of sc and
A3: vs2 is_vertex_seq_of sc; assume
A4: vs1 <> vs2;
  set SG = the Source of G; set TG = the Target of G;
A5: len vs1 = len sc +1 & len vs2 = len sc +1 by A2,A3,Def7;
   defpred P[Element of NAT] means $1 in dom vs1 & vs1.$1<>vs2.$1;
A6:  Seg len vs1 = dom vs1 & Seg len vs2 = dom vs2 by FINSEQ_1:def 3;
then A7: ex j st P[j] by A4,A5,FINSEQ_2:10;
   consider k such that
A8: P[k] and
A9: for n st P[n] holds k<=n from NAT_1:sch 5 (A7);
A10: 1<=k & k<=len sc +1 by A5,A8,FINSEQ_3:27;
   per cases by A10,REAL_1:def 5;
   suppose
A11:  k=1;
    A12: 1+1+1<=len vs1 by A1,A5,XREAL_1:8;
then A13: 1<=len vs1 & 1+1<=len vs1 by XXREAL_0:2;
    set v11=vs1/.1; set v12=vs1/.(1+1); set v13=vs1/.(1+1+1);
    set v21=vs2/.1; set v22=vs2/.(1+1); set v23=vs2/.(1+1+1);
A14:  v11=vs1.1 & v12=vs1.(1+1) & v13=vs1.(1+1+1) &
    v21=vs2.1 & v22=vs2.(1+1) & v23=vs2.(1+1+1) by A5,A12,A13,FINSEQ_4:24;
    1+1<=len sc & 1<=len sc by A1,XXREAL_0:2;
then A15:  sc.1 joins v11,v12 & sc.1 joins v21,v22 &
    sc.2 joins v12,v13 & sc.2 joins v22,v23 by A2,A3,Def7;
 then v11=v21 & v12=v22 or v11=v22 & v12=v21 by Th32;
 then A16:  v11=v13 & v21=v23 by A8,A11,A14,A15,Th32;
    consider vs such that
A17:  vs is_vertex_seq_of sc &
    for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs
                                                by Def10;
       sc <>{} by A1,FINSEQ_1:25;
then A18:  vs = vs1 or vs = vs2 by A2,A3,A4,A17,Th37;
then A19:  1+1+1 = len vs by A5,A12,A14,A16,A17;
       1+1+1<=
len sc by A1,NAT_1:13; hence contradiction by A5,A18,A19;
   end;
   suppose 1<k; then 1+1<=k by NAT_1:13;
    then consider k1 being Element of NAT such that
A20: 1<=k1 & k1<k & k=k1+1 by Th1;
A21: k<=len vs1 by A8,FINSEQ_3:27;
then A22: k1<=len sc by A5,A20,XREAL_1:8;
A23: k1<=len vs1 by A20,A21,XXREAL_0:2;
then A24: k1 in dom vs1 by A20,FINSEQ_3:27;
A25: vs1/.k1=vs1.k1 & vs1/.k=vs1.k &
    vs2/.k1=vs2.k1 & vs2/.k=vs2.k
                           by A5,A6,A8,A20,A23,FINSEQ_4:24,def 4;
A26: sc.k1 joins vs1/.k1, vs1/.(k1+1) by A2,A20,A22,Def7;
   sc.k1 joins vs2/.k1, vs2/.(k1+1) by A3,A20,A22,Def7;
    then (SG.(sc.k1)=vs1/.k1 & TG.(sc.k1)=vs1/.k or
     SG.(sc.k1)=vs1/.k & TG.(sc.k1)=vs1/.k1) &
    (SG.(sc.k1)=vs2/.k1 & TG.(sc.k1)=vs2/.k or
     SG.(sc.k1)=vs2/.k & TG.(sc.k1)=vs2/.k1)
                                          by A20,A26,GRAPH_1:def 9;
hence contradiction by A8,A9,A20,A24,A25;
end;
end;

theorem  vs is_vertex_seq_of sc implies
    for n, m st 1<=n & n<m & m<=len vs & vs.n = vs.m holds n=1 & m=len vs
proof assume
A1: vs is_vertex_seq_of sc;
 consider vs1 such that
A2: vs1 is_vertex_seq_of sc &
  (for n,m st 1<=n & n<m & m<=len vs1 & vs1.n=vs1.m holds n=1 & m=len vs1)
                                                by Def10;
   per cases;
   suppose A3: len sc <= 2;
    thus thesis proof
     per cases by A3,NAT_1:27;
     suppose len sc = 0;
then A4:    len vs = 0+1 by A1,Def7;
      let n,m;
      thus thesis by A4,XXREAL_0:2;
     end;
     suppose
       len sc = 1;
then A5:    len vs = 1+1 & len vs1 = 1+1 by A1,A2,Def7;
      let n,m; assume that
A6:   1<=n and
A7:   n<m and
A8:   m<=len vs and
          vs.n=vs.m;
A9:     n+1<=m by A7,NAT_1:13;
       then n+1<=1+1 by A5,A8,XXREAL_0:2; then n<=1 by XREAL_1:8;
     then n=1 by A6,XXREAL_0:1;
     hence thesis by A5,A8,A9,XXREAL_0:1;
     end;
     suppose
A10: len sc = 2;
then A11: len vs = 1+1+1 & len vs1 = 1+1+1 by A1,A2,Def7;
   set v1=vs/.1; set v2=vs/.(1+1); set v3=vs/.(1+1+1);
   set v11=vs1/.1; set v12=vs1/.(1+1); set v13=vs1/.(1+1+1);
A12: v1=vs.1 & v2=vs.(1+1) & v3=vs.(1+1+1) &
   v11=vs1.1 & v12=vs1.(1+1) & v13=vs1.(1+1+1) by A11,FINSEQ_4:24;
A13: sc.1 joins v1, v2 & sc.1 joins v11,v12 &
   sc.2 joins v2, v3 & sc.2 joins v12,v13 by A1,A2,A10,Def7;
then A14: v1=v11 & v2=v12 or v1=v12 & v2=v11 by Th32;
A15: v2=v12 & v3=v13 or v2=v13 & v3=v12 by A13,Th32;
      let n,m;
      assume that
A16:  1<=n and
A17:  n<m and
A18:  m<=len vs and
A19:  vs.n=vs.m;  n+1<=m by A17,NAT_1:13;
       then n+1<=1+1+1 by A11,A18,XXREAL_0:2;
       then A20:    n<=1+1 by XREAL_1:8;
      thus thesis proof
      per cases by A16,A20,NAT_1:9;
      suppose
A21:   n=1;  1<m by A16,A17,XXREAL_0:2;
then A22:   1+1<=m by NAT_1:13;
       thus thesis proof
        per cases by A11,A18,A22,NAT_1:9;
        suppose m=1+1; hence thesis by A2,A11,A12,A14,A19,A21;
        end;
        suppose m=1+1+1; hence thesis by A1,A10,A21,Def7;
       end;
       end;
      end;
      suppose
A23:    n=1+1; then 1+1+1<=m by A17,NAT_1:13;
       then m=1+1+1 by A11,A18,XXREAL_0:1;
       hence thesis by A2,A11,A12,A15,A19,A23;
      end;
      end;
    end;
    end;
   end;
   suppose 2<len sc; then vs=vs1 by A1,A2,Th50;
   hence thesis by A2;
end;
end;

:: A chain may have more than one simple chain spanning its endpoints.
:: The following chain:
::             .--->.
::             ^    |
::             |    v
::        .--->.<---.--->.
::             |    ^
::             v    |
::             .--->.
:: is a case in point:

theorem Th52:
not c is simple Chain of G & vs is_vertex_seq_of c implies
  ex fc being FinSubsequence of c, fvs being FinSubsequence of vs, c1, vs1
   st len c1 < len c & vs1 is_vertex_seq_of c1 & len vs1 < len vs &
      vs.1 = vs1.1 & vs.len vs = vs1.len vs1 & Seq fc = c1 & Seq fvs = vs1
proof assume that
A1: not c is simple Chain of G and
A2: vs is_vertex_seq_of c;
   consider n, m being Element of NAT such that
A3: 1<=n & n<m & m<=len vs & vs.n=vs.m & (n<>1 or m<>len vs) by A1,A2,Def10;
A4: len vs = len c + 1 by A2,Def7; A5: m-n>n-n by A3,XREAL_1:11;
   reconsider n1 = n-'1 as Element of NAT; A6: 1-1<=n-1 by A3,XREAL_1:11;
then A7: n-1 = n-'1 by BINARITH:def 3;
then A8: n1+1 = n;
   per cases by A3;
    suppose
A9:  n<>1 & m <> len vs;
    then 1 < n by A3,REAL_1:def 5;
    then 1 + 1<=n by NAT_1:13; then A10: 1+1-1<=n-1 by XREAL_1:11;
       n < len vs by A3,XXREAL_0:2
; then A11: n-1 < len c +1-1 by A4,XREAL_1:11;
A12: 1<=n1+1 by NAT_1:12; A13: m < len vs by A3,A9,REAL_1:def 5;
then A14:  m<=len c by A4,NAT_1:13;
     A15: 1<=m & m<=len c & len c <=len c by A3,A4,A13,XXREAL_0:2,NAT_1:13;
   reconsider c1 = (1,n1)-cut c as Chain of G by A7,A10,A11,Th44;
   reconsider c2 = (m,len c)-cut c as Chain of G by A15,Th44;
   set p2' = (m+1, len c +1)-cut vs;
   reconsider pp = (1,n)-cut vs as FinSequence of the Vertices of G;
   reconsider p2 = (m,len c+1)-cut vs as FinSequence of the Vertices of G;
   A16: 1<=n & n<=len vs by A3,XXREAL_0:2;
A17: 1<=m & m<=len c + 1 & len c + 1<=len vs by A2,A3,Def7,XXREAL_0:2;
A18: pp is_vertex_seq_of c1 by A2,A8,A10,A11,Th45;
A19: p2 is_vertex_seq_of c2 by A2,A15,Th45;
A20: len pp = len c1 + 1 by A18,Def7;
A21: len p2 = len c2 + 1 by A19,Def7;
        1-1<=m-1 by A15,XREAL_1:11;
     then m-'1 = m-1 by BINARITH:def 3;
     then reconsider m1 = m-1 as Element of NAT;
A22:  m = m1 +1;  len c2 +m = len c +1 by A15,Def1;
then A23: len c2 =len c - m + 1;
       A24: m-m<=len c -m by A14,XREAL_1:11;
     then len c2 -'1 = len c2 -1 by A23,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
A25:  m+lc21 = m1+len c2;
A26: 0+1=1 & 0<len p2 by A21;
then A27: 0+1=1 & 0<=1 & 1<=len p2 by NAT_1:13;
A28: p2 = (0+1, len p2)-cut p2 by Th7
       .= (0+1, 1)-cut p2 ^ (1+1, len p2)-cut p2 by A27,Th8;
        m1<=m by A22,NAT_1:12;
then A29: p2 = (m1+1, m)-cut vs ^ (m+1, len c +1)-cut vs by A3,A4,Th8;
A30:  m<=len c +1+1 by A3,A4,NAT_1:12;
        (1,1)-cut p2 = <*p2.(0+1)*> by A27,Th6
                 .= <*vs.(m+0)*> by A4,A15,A26,A30,Lm2
                 .= (m,m)-cut vs by A3,A15,Th6;
then A31: p2' = (2, len p2)-cut p2 by A28,A29,FINSEQ_1:46;
  set domfc = {k: 1<=k & k<=n1 or m<=k & k<= len c};
  deffunc F(set) = c.$1;
  consider fc being Function such that
A32: dom fc = domfc and
A33: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A34:    x = kk & (1<=kk & kk<=n1 or m<=kk & kk<= len c);
          1<=kk & kk<=len c by A7,A11,A15,A34,XXREAL_0:2;
      hence x in Seg len c by A34,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A32,FINSEQ_1:def 12;
A35: fc c= c proof let p be set; assume
A36:  p in fc; then consider x, y such that
A37:  [x,y] = p by RELAT_1:def 1;
A38:  x in dom fc & y = fc.x by A36,A37,FUNCT_1:8;
     then consider kk being Element of NAT such that
A39:  x = kk & (1<=kk & kk<=n1 or m<=kk & kk<= len c) by A32;
        1<=kk & kk<=len c by A7,A11,A15,A39,XXREAL_0:2;
then A40:  x in dom c by A39,FINSEQ_3:27;  y = c.kk by A32,A33,A38,A39;
hence p in c by A37,A39,A40,FUNCT_1:8;
  end;
  then reconsider fc as FinSubsequence of c by Def5;
  take fc;
  set domfvs = {k: 1<=k & k<=n or m+1<=k & k<= len vs};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A41: dom fvs = domfvs and
A42: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A43:    x = kk & (1<=kk & kk<=n or m+1<=kk & kk<= len vs);
          1<=m+1 by NAT_1:12; then 1<=kk & kk<=len vs by A16,A43,XXREAL_0:2;
      hence x in Seg len vs by A43,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A41,FINSEQ_1:def 12;
A44: fvs c= vs proof let p be set; assume
A45:  p in fvs; then consider x, y such that
A46:  [x,y] = p by RELAT_1:def 1;
A47:  x in dom fvs & y = fvs.x by A45,A46,FUNCT_1:8;
     then consider kk being Element of NAT such that
A48:  x = kk & (1<=kk & kk<=n or m+1<=kk & kk<= len vs) by A41;
        1<=m+1 by NAT_1:12;
     then 1<=kk & kk<=len vs by A16,A48,XXREAL_0:2;
then A49:  x in dom vs by A48,FINSEQ_3:27;  y = vs.kk by A41,A42,A47,A48;
hence p in vs by A46,A48,A49,FUNCT_1:8;
  end;
  then reconsider fvs as FinSubsequence of vs by Def5;
  take fvs;
     A50: p2.1 = vs.m & pp.len pp = vs.n by A16,A17,Th12;
  then reconsider c' = c1^c2 as Chain of G by A3,A18,A19,Th46;
  take c';
  take p1=pp^'p2;
A51: p1 = pp^p2' by A31;
     A52: len c1 +1 = n1+1 by A7,A11,A12,Lm2;
then A53: len c1 = n - 1 by A7;
        -(-(m-n)) = m-n; then A54: -(m-n) < 0 by A5,XREAL_1:60;
    len c' = (n+-1)+(len c +(-m +1)) by A23,A53,FINSEQ_1:35 .= len c + (n+ -m )
;
  hence
A55: len c' < len c by A54,REAL_2:173;
  thus p1 is_vertex_seq_of c' by A3,A18,A19,A50,Th47;
  then len p1 = len c' + 1 by Def7;
  hence len p1 < len vs by A4,A55,XREAL_1:8;
      1<=1 + len c1 by NAT_1:12; then 1<=len pp by A18,Def7;
 then p1.1 = pp.1 by Th14;
   hence vs.1 = p1.1 by A16,Th12;
A56: p2.len p2 = vs.(len c + 1) by A17,Th12;
     m-m<=len c -m by A14,XREAL_1:11;
  then 0+1<=len c -m+1 by XREAL_1:8; then 1<len p2 by A21,A23,NAT_1:13;
hence vs.len vs = p1.len p1 by A4,A56,Th16;
     set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n1};
     set DR = {kk where kk is Element of NAT: m<=kk & kk<= len c};
        now let x be set;
      hereby assume x in domfc; then consider k being Element of NAT such that
A57:     x=k & (1<=k & k<=n1 or m<=k & k<= len c);
           x in DL or x in DR by A57;
       hence x in DL \/ DR by XBOOLE_0:def 2;
      end; assume A58: x in DL \/ DR;
      per cases by A58,XBOOLE_0:def 2;
      suppose x in DL; then consider k being Element of NAT such that
A59:    x=k & 1<=k & k<=n1;
      thus x in domfc by A59;
      end;
      suppose x in DR; then consider k being Element of NAT such that
A60:    x=k & m<=k & k<=len c;
      thus x in domfc by A60;
     end;
     end;
then A61:  domfc = DL \/ DR by TARSKI:2;
A62:  DL c= Seg len c & DR c= Seg len c proof
      hereby let x be set; assume x in DL;
       then consider k being Element of NAT such that
A63:    x=k & 1<=k & k<=n1;  k<=len c by A7,A11,A63,XXREAL_0:2;
       hence x in Seg len c by A63,FINSEQ_1:3;
      end; let x be set; assume x in DR;
       then consider k being Element of NAT such that
A64:    x=k & m<=k & k<=len c;  1<=k by A15,A64,XXREAL_0:2;
       hence x in Seg len c by A64,FINSEQ_1:3;
     end;
     then reconsider DL as finite set by FINSET_1:13;
     reconsider DR as finite set by A62,FINSET_1:13;
    now let i,j; assume i in DL; then consider k being Element of NAT such that
A65:    k=i & 1<=k & k<=n1;
      assume j in DR; then consider l being Element of NAT such that
A66:    l=j & m<=l & l<=len c;
          n1<m by A3,A8,NAT_1:13; then k<m by A65,XXREAL_0:2;
      hence i < j by A65,A66,XXREAL_0:2;
     end;
then A67: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A62,FINSEQ_3:48;
  m+lc21 = len c by A23;
then A68:  card DR = len c2 + -1 +1 by Th4
        .= len c2;
A69:  len Sgm DL = card DL & len Sgm DR = card DR by A62,FINSEQ_3:44;  DL
=
Seg n1 by FINSEQ_1:def 1;
then A70:  Sgm DL = idseq n1 by FINSEQ_3:54;
then A71:  dom Sgm DL = Seg n1 by RELAT_1:71;
        rng Sgm DL = DL by A62,FINSEQ_1:def 13;
then A72: rng Sgm DL c= dom fc by A32,A61,XBOOLE_1:7;
       rng Sgm DR = DR by A62,FINSEQ_1:def 13;
then A73: rng Sgm DR c= dom fc by A32,A61,XBOOLE_1:7;
    set SL = Sgm DL; set SR = Sgm DR;
       now let p be set;
     hereby assume
A74:   p in c1; then consider x, y being set such that
A75:  p=[x,y] by RELAT_1:def 1;
A76:   x in dom c1 & y=c1.x by A74,A75,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A77:   1<=k & k<=len c1 by A76,FINSEQ_3:27;
then A78:   x in dom SL by A7,A53,A71,FINSEQ_1:3;
then A79:   k=SL.k by A70,A71,FUNCT_1:35;
A80:   k in domfc by A7,A53,A77;
then A81:   x in dom (fc*SL) by A32,A78,A79,FUNCT_1:21;  0+1<=k by A76,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A82:   0<=i & i<n1 & k=i+1 by A7,A53,A77,Th1;
         (fc*SL).x = fc.k by A79,A81,FUNCT_1:22 .= c.(1+i) by A32,A35,A80,A82,
GRFUNC_1:8
        .= y by A7,A11,A12,A53,A76,A82,Lm2;
      hence p in fc*(Sgm DL) by A75,A81,FUNCT_1:8;
     end; assume
A83:  p in fc*(Sgm DL); then consider x,y being set such that
A84:  p=[x,y] by RELAT_1:def 1;
A85:   x in dom (fc*SL) & y = (fc*SL).x by A83,A84,FUNCT_1:8;
then A86:  (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A87:   x in dom SL & SL.x in dom fc by A85,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n1}
       by A71,FINSEQ_1:def 1;
      then consider k such that
A88:   k=x & 1<=k & k<=n1;
A89:   k in dom fc by A32,A88;
A90:   k=SL.k by A70,A71,A87,A88,FUNCT_1:35;
A91: k in dom c1 by A7,A53,A88,FINSEQ_3:27;  0+1<=k by A88;
     then consider i being Element of NAT such that
A92: 0<=i & i<n1 & k=i+1 by A88,Th1;
       c1.k = c.k by A7,A11,A12,A53,A92,Lm2
         .= y by A35,A85,A86,A88,A89,A90,GRFUNC_1:8;
     hence p in c1 by A84,A88,A91,FUNCT_1:8;
    end;
then A93: c1 = fc*(Sgm DL) by TARSKI:2;
       now let p be set;
     hereby assume
A94:   p in c2; then consider x, y being set such that
A95:  p=[x,y] by RELAT_1:def 1;
A96:   x in dom c2 & y=c2.x by A94,A95,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A97:   1<=k & k<=len c2 by A96,FINSEQ_3:27;
A98:   x in dom SR by A68,A69,A96,FINSEQ_3:31;
A99:   m1+k=SR.k by A22,A23,A25,A97,Th5;
      A100: SR.k in rng SR by A98,FUNCT_1:def 5;
then A101:   x in dom (fc*SR) by A73,A98,FUNCT_1:21;  0+1<=k by A96,FINSEQ_3:27
;
      then consider i being Element of NAT such that
A102:   0<=i & i<len c2 & k=i+1 by A97,Th1;
         (fc*SR).x = fc.(m1+k) by A99,A101,FUNCT_1:22 .= c.(m+i) by  A35,A73
,A99,A100,GRFUNC_1:8,A102
        .= y by A15,A96,A102,Def1;
      hence p in fc*(Sgm DR) by A95,A101,FUNCT_1:8;
     end; assume
A103:  p in fc*(Sgm DR); then consider x,y being set such that
A104:  p=[x,y] by RELAT_1:def 1;
A105:   x in dom (fc*SR) & y = (fc*SR).x by A103,A104,FUNCT_1:8;
then A106:   x in dom SR & SR.x in dom fc by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A107:   k in dom c2 by A68,A69,A106,FINSEQ_3:31;
A108:   1<=k & k<=len c2 by A68,A69,A106,FINSEQ_3:27;
then A109:   m1+k=SR.k by A22,A23,A25,Th5;  0+1<=k by A106,FINSEQ_3:27;
     then consider i being Element of NAT such that
A110:   0<=i & i<len c2 & k=i+1 by A108,Th1;
        c2.k = c.(m1+1+i) by A15,A110,Def1
         .= fc.(SR.k) by  A110,A35,A106,A109,GRFUNC_1:8 .= y by
A105,A106,FUNCT_1:23;
     hence p in c2 by A104,A107,FUNCT_1:8;
    end;
then A111: c2 = fc*(Sgm DR) by TARSKI:2;
    Seq fc= fc*((Sgm DL)^(Sgm DR))by A32,A61,A67,FINSEQ_1:def 14;
  hence Seq fc = c' by A72,A73,A93,A111,Th26;
     set DL = {kk where kk is Element of NAT: 1<=kk & kk<=n};
     set DR = {kk where kk is Element of NAT: m+1<=kk & kk<= len vs};
        now let x be set;
      hereby assume x in domfvs; then consider k being Element of NAT such that
A112:     x=k & (1<=k & k<=n or m+1<=k & k<= len vs);
           x in DL or x in DR by A112;
       hence x in DL \/ DR by XBOOLE_0:def 2;
      end; assume A113: x in DL \/ DR;
      per cases by A113,XBOOLE_0:def 2;
      suppose x in DL; then consider k being Element of NAT such that
A114:    x=k & 1<=k & k<=n;
      thus x in domfvs by A114;
      end;
      suppose x in DR; then consider k being Element of NAT such that
A115:    x=k & m+1<=k & k<=len vs;
      thus x in domfvs by A115;
     end;
     end;
then A116:  domfvs = DL \/ DR by TARSKI:2;
A117:  DL c= Seg len vs & DR c= Seg len vs proof
      hereby let x be set; assume x in DL;
       then consider k being Element of NAT such that
A118:    x=k & 1<=k & k<=n;  k<=len vs by A16,A118,XXREAL_0:2;
       hence x in Seg len vs by A118,FINSEQ_1:3;
      end; let x be set; assume x in DR;
       then consider k being Element of NAT such that
A119:    x=k & m+1<=k & k<=len vs;
          1<=m+1 by NAT_1:12; then 1<=k by A119,XXREAL_0:2;
       hence x in Seg len vs by A119,FINSEQ_1:3;
     end;
     then reconsider DL as finite set by FINSET_1:13;
     reconsider DR as finite set by A117,FINSET_1:13;
    now let i,j; assume i in DL; then consider k being Element of NAT such that
A120:    k=i & 1<=k & k<=n;
      assume j in DR; then consider l being Element of NAT such that
A121:    l=j & m+1<=l & l<=len vs;  m<=m+1 by NAT_1:12;
then A122:   m<=l by A121,XXREAL_0:2;  k<m by A3,A120,XXREAL_0:2;
      hence i < j by A120,A121,A122,XXREAL_0:2;
     end;
then A123: Sgm (DL \/ DR) = (Sgm DL)^(Sgm DR) by A117,FINSEQ_3:48;
        1<=len p2 by A21,NAT_1:12;
     then 1-1<=len p2 -1 by XREAL_1:11;
     then len p2 -'1 = len p2 -1 by BINARITH:def 3;
     then reconsider lp21 = len p2 -1 as Element of NAT;
      lp21 -'1 = lp21 -1 by A21,A23,A24,BINARITH:def 3;
     then reconsider lp22 = lp21 -1 as Element of NAT;
 A124: m+1+lp22 = m+((lp21 -1)+1)
            .= m+(len c -m +1 +1 -1) by A19,A23,Def7
            .= len c +1;
then A125:  card DR = lp21 -1+1 by A4,Th4 .= lp21;
A126:  1<=m+1 & m+1<=len c +1+1 by A3,A4,XREAL_1:8,NAT_1:12;
         len p2 +m = len c +1+1 by A4,A15,A30,Lm2
     .= len p2' +(m+1) by A4,A126,Lm2 .= (len p2'+1)+m;
then A127: len p2 -1 = len p2'+1-1;
A128:  len Sgm DL = card DL & len Sgm DR = card DR by A117,FINSEQ_3:44;
A129:  len pp = n by A7,A20,A52;  DL = Seg n by FINSEQ_1:def 1;
then A130:  Sgm DL = idseq n by FINSEQ_3:54;
then A131:  dom Sgm DL = Seg n by RELAT_1:71;
       rng Sgm DL = DL by A117,FINSEQ_1:def 13;
then A132: rng Sgm DL c= dom fvs by A41,A116,XBOOLE_1:7;
       rng Sgm DR = DR by A117,FINSEQ_1:def 13;
then A133: rng Sgm DR c= dom fvs by A41,A116,XBOOLE_1:7;
    set SL = Sgm DL; set SR = Sgm DR;
       now let p be set;
     hereby assume
A134:   p in pp; then consider x, y being set such that
A135:  p=[x,y] by RELAT_1:def 1;
A136:   x in dom pp & y=pp.x by A134,A135,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A137:   1<=k & k<=len pp by A136,FINSEQ_3:27;
then A138:   x in dom SL by A129,A131,FINSEQ_1:3;
then A139:   k=SL.k by A130,A131,FUNCT_1:35;
A140:   k in domfvs by A129,A137;
then A141:   x in dom (fvs*SL) by A41,A138,A139,FUNCT_1:21;  0+1<=k by A136,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A142:   0<=i & i<n & k=i+1 by A129,A137,Th1;
         (fvs*SL).x = fvs.k by A139,A141,FUNCT_1:22 .= vs.(1+i) by A41,A44,A140
,A142,GRFUNC_1:8 .= y by A16,A129,A136,A142,Def1;
      hence p in fvs*(Sgm DL) by A135,A141,FUNCT_1:8;
     end; assume
A143:  p in fvs*(Sgm DL); then consider x,y being set such that
A144:  p=[x,y] by RELAT_1:def 1;
A145:   x in dom (fvs*SL) & y = (fvs*SL).x by A143,A144,FUNCT_1:8;
then A146:  (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A147:   x in dom SL & SL.x in dom fvs by A145,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n}
       by A131,FINSEQ_1:def 1;
      then consider k such that
A148:   k=x & 1<=k & k<=n;
A149:   k in dom fvs by A41,A148;
A150:   k=SL.k by A130,A131,A147,A148,FUNCT_1:35;
A151: k in dom pp by A129,A148,FINSEQ_3:27;  0+1<=k by A148;
     then consider i being Element of NAT such that
A152:  0<=i & i<n & k=i+1 by A148,Th1;
        pp.k = vs.k by A16,A129,A152,Def1
         .= y by A44,A145,A146,A148,A149,A150,GRFUNC_1:8;
     hence p in pp by A144,A148,A151,FUNCT_1:8;
    end;
then A153: pp = fvs*(Sgm DL) by TARSKI:2;
A154:  m+1+lp22 = m+lp21;
A155: 1<=m+1 & m+1<=len c +1+1 by A3,A4,NAT_1:12,XREAL_1:9;
       now let p be set;
     hereby assume
A156:   p in p2'; then consider x, y being set such that
A157:  p=[x,y] by RELAT_1:def 1;
A158:   x in dom p2' & y=p2'.x by A156,A157,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A159:   1<=k & k<=len p2' by A158,FINSEQ_3:27;
A160:   x in dom SR by A125,A127,A128,A158,FINSEQ_3:31;
A161:   m+k=SR.k by A4,A124,A127,A154,A159,Th5;
      A162: SR.k in rng SR by A160,FUNCT_1:def 5;
then A163:   x in dom (fvs*SR) by A133,A160,FUNCT_1:21;  0+1<=k by A158,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A164:   0<=i & i<len p2' & k=i+1 by A159,Th1;
         (fvs*SR).x = fvs.(m+k) by A161,A163,FUNCT_1:22 .= vs.(m+1+i) by  A44
,A133,A161,A162,GRFUNC_1:8,A164 .= y by A4,A155,A158,A164,Lm2;
      hence p in fvs*(Sgm DR) by A157,A163,FUNCT_1:8;
     end; assume
A165:  p in fvs*(Sgm DR); then consider x,y being set such that
A166:  p=[x,y] by RELAT_1:def 1;
A167:   x in dom (fvs*SR) & y = (fvs*SR).x by A165,A166,FUNCT_1:8;
then A168:   x in dom SR & SR.x in dom fvs by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A169:   k in dom p2' by A125,A127,A128,A168,FINSEQ_3:31;
A170:   1<=k & k<=len p2' by A125,A127,A128,A168,FINSEQ_3:27;
then A171:   m+k=SR.k by A4,A124,A127,A154,Th5;  0+1<=k by A168,FINSEQ_3:27;
     then consider i being Element of NAT such that
A172:   0<=i & i<len p2' & k=i+1 by A170,Th1;
        p2'.k = vs.(m+1+i) by A4,A155,A172,Lm2
         .= fvs.(SR.k) by  A172,A44,A168,A171,GRFUNC_1:8 .= y by
A167,A168,FUNCT_1:23;
     hence p in p2' by A166,A169,FUNCT_1:8;
    end;
then A173: p2' = fvs*(Sgm DR) by TARSKI:2;
    Seq fvs = fvs*((Sgm DL)^(Sgm DR)) by A41,A116,A123,FINSEQ_1:def 14;
  hence Seq fvs = p1 by A51,A132,A133,A153,A173,Th26;
 end;
 suppose
A174: n=1 & m <> len vs;
     then A175: m < len vs by A3,REAL_1:def 5;
then A176:  m<=len c by A4,NAT_1:13;
A177:  1 < m by A3,XXREAL_0:2;
A178: 1<=m & m<=len c & len c <=len c by A3,A4,A175,XXREAL_0:2,NAT_1:13;
   reconsider c2 = (m,len c)-cut c as Chain of G by A176,A177,Th44;
   set p2 = (m,len c+1)-cut vs;
A179: p2 is_vertex_seq_of c2 by A2,A176,A177,Th45;
  set domfc = {k: m<=k & k<= len c};
  deffunc F(set) = c.$1;
  consider fc being Function such that
A180: dom fc = domfc and
A181: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A182:    x = kk & m<=kk & kk<= len c;
          1<=kk & kk<=len c by A177,A182,XXREAL_0:2;
      hence x in Seg len c by A182,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A180,FINSEQ_1:def 12;
A183: fc c= c proof let p be set; assume
A184:  p in fc; then consider x, y such that
A185:  [x,y] = p by RELAT_1:def 1;
A186:  x in dom fc & y = fc.x by A184,A185,FUNCT_1:8;
     then consider kk being Element of NAT such that
A187:  x = kk & m<=kk & kk<= len c by A180;
        1<=kk & kk<=len c by A177,A187,XXREAL_0:2;
then A188:  x in dom c by A187,FINSEQ_3:27;
        y = c.kk by A180,A181,A186,A187;
     hence p in c by A185,A187,A188,FUNCT_1:8;
  end;
  then reconsider fc as FinSubsequence of c by Def5;
  take fc;
  set domfvs = {k: m<=k & k<= len vs};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A189: dom fvs = domfvs and
A190: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A191:    x = kk & m<=kk & kk<= len vs;
          1<=kk & kk<=len vs by A177,A191,XXREAL_0:2;
      hence x in Seg len vs by A191,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A189,FINSEQ_1:def 12;
A192: fvs c= vs proof let p be set; assume
A193:  p in fvs; then consider x, y such that
A194:  [x,y] = p by RELAT_1:def 1;
A195:  x in dom fvs & y = fvs.x by A193,A194,FUNCT_1:8;
     then consider kk being Element of NAT such that
A196:  x = kk & m<=kk & kk<= len vs by A189;
        1<=kk & kk<=len vs by A177,A196,XXREAL_0:2;
then A197:  x in dom vs by A196,FINSEQ_3:27;
        y = vs.kk by A189,A190,A195,A196;
     hence p in vs by A194,A196,A197,FUNCT_1:8;
  end;
  then reconsider fvs as FinSubsequence of vs by Def5;
  take fvs; take c1 = c2; take p1 = p2;
        len c2 +m = len c +1 by A3,A4,A174,Lm2;
then A198: len c2 =len c - m + 1;
       A199: m-m<=len c -m by A176,XREAL_1:11;
        1-1<=m-1 by A177,XREAL_1:11;
     then m-'1 = m-1 by BINARITH:def 3;
      then reconsider m1 = m-1 as Element of NAT;
A200:  m = m1 +1;
      len c2 -'1 = len c2 -1 by A198,A199,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
A201:  m+lc21 = m1+len c2;
A202: 1<=m & m<=len c +1+1 & len c +1<=len vs by A3,A4,A174,NAT_1:12;
     then len p2 +m = len c +1 +1 by Lm2;
then A203: len p2 = len c -m +1+1;
then A204: 1<=len p2 by A198,NAT_1:12;
A205: len c2 = len c +(-m+1) by A198;
        1-1 < m-1 by A177,XREAL_1:11;
   then 0 < -(-(m-1)); then -(m-1) < 0 by XREAL_1:60;
  hence
A206: len c1 < len c by A205,REAL_2:173;
  thus p1 is_vertex_seq_of c1 by A2,A178,Th45;  len p1 = len c1 + 1 by A179,
Def7
;
  hence len p1 < len vs by A4,A206,XREAL_1:8;
  thus vs.1 = p1.1 by A3,A4,A174,Th12;
   thus vs.len vs = p1.len p1 by A3,A4,A174,Th12;
A207:  domfc c= Seg len c proof
       let x be set; assume x in domfc;
        then consider k being Element of NAT such that
A208:    x=k & m<=k & k<=len c;  1<=k by A177,A208,XXREAL_0:2;
       hence x in Seg len c by A208,FINSEQ_1:3;
     end;
     then reconsider domfc as finite set by FINSET_1:13;
      len c2 -'1 = len c2 -1 by A198,A199,BINARITH:def 3;
     then reconsider lc21 = len c2 -1 as Element of NAT;
  m+lc21 = len c by A198;
then A209:  card domfc = len c2 + -1 +1 by Th4
        .= len c2;
A210: len Sgm domfc = card domfc by A207,FINSEQ_3:44;
    A211: rng Sgm domfc c= dom fc by A180,A207,FINSEQ_1:def 13;
    set SR = Sgm domfc;
       now let p be set;
     hereby assume
A212:   p in c2; then consider x, y being set such that
A213:  p=[x,y] by RELAT_1:def 1;
A214:   x in dom c2 & y=c2.x by A212,A213,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A215:   1<=k & k<=len c2 by A214,FINSEQ_3:27;
A216:   x in dom SR by A209,A210,A214,FINSEQ_3:31;
A217:   m1+k=SR.k by A198,A200,A201,A215,Th5;
      A218: SR.k in rng SR by A216,FUNCT_1:def 5;
then A219:   x in dom (fc*SR) by A211,A216,FUNCT_1:21;  0+1<=k by A214,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A220:   0<=i & i<len c2 & k=i+1 by A215,Th1;
         (fc*SR).x = fc.(m1+k) by A217,A219,FUNCT_1:22 .= c.(m+i) by  A183,A211
,A217,A218,GRFUNC_1:8,A220
        .= y by A3,A4,A174,A214,A220,Lm2;
      hence p in fc*(Sgm domfc) by A213,A219,FUNCT_1:8;
     end; assume
A221:  p in fc*(Sgm domfc); then consider x,y being set such that
A222:  p=[x,y] by RELAT_1:def 1;
A223:   x in dom (fc*SR) & y = (fc*SR).x by A221,A222,FUNCT_1:8;
then A224:   x in dom SR & SR.x in dom fc by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A225:   k in dom c2 by A209,A210,A224,FINSEQ_3:31;
A226:   1<=k & k<=len c2 by A209,A210,A224,FINSEQ_3:27;
then A227:   m1+k=SR.k by A198,A200,A201,Th5;  0+1<=k by A224,FINSEQ_3:27;
     then consider i being Element of NAT such that
A228:   0<=i & i<len c2 & k=i+1 by A226,Th1;
        c2.k = c.(m1+1+i) by A3,A4,A174,A228,Lm2
         .= fc.(SR.k) by  A228,A183,A224,A227,GRFUNC_1:8 .= y by
A223,A224,FUNCT_1:23;
     hence p in c2 by A222,A225,FUNCT_1:8;
    end;
 then c2 = fc*(Sgm domfc) by TARSKI:2;
 hence Seq fc = c1 by A180,FINSEQ_1:def 14;
A229:  domfvs c= Seg len vs proof
       let x be set; assume x in domfvs;
        then consider k being Element of NAT such that
A230:    x=k & m<=k & k<=len vs;  1<=k by A177,A230,XXREAL_0:2;
       hence x in Seg len vs by A230,FINSEQ_1:3;
     end;
     then reconsider domfvs as finite set by FINSET_1:13;
        1-1<=len p2 -1 by A204,XREAL_1:11;
     then len p2 -'1 = len p2 -1 by BINARITH:def 3;
     then reconsider lp21 = len p2 -1 as Element of NAT;
A231: m+lp21 = len c+1 by A203;
A232:  m+lp21 = m1+len p2;
A233:  card domfvs = len p2 + -1 +1 by A4,A231,Th4
        .= len p2;
A234: len Sgm domfvs = card domfvs by A229,FINSEQ_3:44;
    A235: rng Sgm domfvs c= dom fvs by A189,A229,FINSEQ_1:def 13;
    set SR = Sgm domfvs;
       now let p be set;
     hereby assume
A236:   p in p2; then consider x, y being set such that
A237:  p=[x,y] by RELAT_1:def 1;
A238:   x in dom p2 & y=p2.x by A236,A237,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A239:   1<=k & k<=len p2 by A238,FINSEQ_3:27;
A240:   x in dom SR by A233,A234,A238,FINSEQ_3:31;
A241:   m1+k=SR.k by A4,A200,A203,A232,A239,Th5; A242: SR.k in rng SR by A240,
FUNCT_1:def 5
;
then A243:   x in dom (fvs*SR) by A235,A240,FUNCT_1:21;  0+1<=k by A238,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A244:   0<=i & i<len p2 & k=i+1 by A239,Th1;
         (fvs*SR).x = fvs.(m1+k) by A241,A243,FUNCT_1:22 .= vs.(m+i) by  A192
,A235,A241,A242,GRFUNC_1:8,A244
        .= y by A202,A238,A244,Lm2;
      hence p in fvs*(Sgm domfvs) by A237,A243,FUNCT_1:8;
     end; assume
A245:  p in fvs*(Sgm domfvs); then consider x,y being set such that
A246:  p=[x,y] by RELAT_1:def 1;
A247:   x in dom (fvs*SR) & y = (fvs*SR).x by A245,A246,FUNCT_1:8;
then A248:   x in dom SR & SR.x in dom fvs by FUNCT_1:21;
      then reconsider k=x as Element of NAT;
A249:   k in dom p2 by A233,A234,A248,FINSEQ_3:31;
A250:   1<=k & k<=len p2 by A233,A234,A248,FINSEQ_3:27;
then A251:   m1+k=SR.k by A4,A200,A203,A232,Th5;  0+1<=k by A248,FINSEQ_3:27;
     then consider i being Element of NAT such that
A252:   0<=i & i<len p2 & k=i+1 by A250,Th1;
        p2.k = vs.(m1+1+i) by A202,A252,Lm2
        .= fvs.(SR.k) by  A252,A192,A248,A251,GRFUNC_1:8 .= y
by A247,A248,FUNCT_1:23
;
     hence p in p2 by A246,A249,FUNCT_1:8;
    end;
 then p2 = fvs*(Sgm domfvs) by TARSKI:2;
 hence Seq fvs = p1 by A189,FINSEQ_1:def 14;
 end;
 suppose
A253: n<>1 & m = len vs;
    then 1 < n by A3,REAL_1:def 5;
    then 1 + 1<=n by NAT_1:13; then A254: 1+1-1<=n-1 by XREAL_1:11;
        n < len vs by A3,XXREAL_0:2; then A255: n-1 < len c +1-1 by A4,
XREAL_1:11
;
A256: 1<=n1+1 by NAT_1:12;
   reconsider c1 = (1,n1)-cut c as Chain of G by A7,A254,A255,Th44;
   set pp = (1,n)-cut vs; A257: 1<=n & n<=len vs by A3,XXREAL_0:2;
A258: pp is_vertex_seq_of c1 by A2,A8,A254,A255,Th45;
then A259: len pp = len c1 + 1 by Def7;
  reconsider domfc = {k: 1<=k & k<=n1} as set;
  deffunc F(set) = c.$1;
  consider fc being Function such that
A260: dom fc = domfc and
A261: for x st x in domfc holds fc.x = F(x) from FUNCT_1:sch 3;
   domfc c= Seg len c proof let x be set; assume x in domfc;
       then consider kk being Element of NAT such that
A262:    x = kk & 1<=kk & kk<=n1;  1<=kk & kk<=len c by A7,A255,A262,XXREAL_0:2
;
      hence x in Seg len c by A262,FINSEQ_1:3;
     end;
  then reconsider fc as FinSubsequence by A260,FINSEQ_1:def 12;
A263: fc c= c proof let p be set; assume
A264:  p in fc; then consider x, y such that
A265:  [x,y] = p by RELAT_1:def 1;
A266:  x in dom fc & y = fc.x by A264,A265,FUNCT_1:8;
     then consider kk being Element of NAT such that
A267:  x = kk & 1<=kk & kk<=n1 by A260;
        1<=kk & kk<=len c by A7,A255,A267,XXREAL_0:2;
then A268:  x in dom c by A267,FINSEQ_3:27;  y = c.kk by A260,A261,A266,A267;
hence p in c by A265,A267,A268,FUNCT_1:8;
  end;
  then reconsider fc as FinSubsequence of c by Def5;
  take fc;
  set domfvs = {k: 1<=k & k<=n};
  deffunc F(set) = vs.$1;
  consider fvs being Function such that
A269: dom fvs = domfvs and
A270: for x st x in domfvs holds fvs.x = F(x) from FUNCT_1:sch 3;
   domfvs c= Seg len vs proof let x be set; assume x in domfvs;
       then consider kk being Element of NAT such that
A271:    x = kk & 1<=kk & kk<=n;  1<=kk & kk<=len vs by A257,A271,XXREAL_0:2;
      hence x in Seg len vs by A271,FINSEQ_1:3;
     end;
  then reconsider fvs as FinSubsequence by A269,FINSEQ_1:def 12;
A272: fvs c= vs proof let p be set; assume
A273:  p in fvs; then consider x, y such that
A274:  [x,y] = p by RELAT_1:def 1;
A275:  x in dom fvs & y = fvs.x by A273,A274,FUNCT_1:8;
     then consider kk being Element of NAT such that
A276:  x = kk & 1<=kk & kk<=n by A269;
        1<=kk & kk<=len vs by A257,A276,XXREAL_0:2;
then A277:  x in dom vs by A276,FINSEQ_3:27;  y = vs.kk by A269,A270,A275,A276
;
hence p in vs by A274,A276,A277,FUNCT_1:8;
  end;
  then reconsider fvs as FinSubsequence of vs by Def5;
  take fvs; take c' = c1; take p1 = pp;
        len c1+1=n1+1 by A7,A255,A256,Lm2;
then A278: len c1 = n - 1 by A6,BINARITH:def 3;
  hence
   len c' < len c by A255;
  thus p1 is_vertex_seq_of c' by A2,A8,A254,A255,Th45;  len p1 = len c1 + 1 by
A258,Def7;
  hence len p1 < len vs by A3,A278,XXREAL_0:2;
  thus vs.1 = p1.1 by A257,Th12;
  thus vs.len vs = p1.len p1 by A3,A253,Th12;
    domfc c= Seg len c proof
      let x be set; assume x in domfc;
       then consider k being Element of NAT such that
A279:    x=k & 1<=k & k<=n1;  k<=len c by A7,A255,A279,XXREAL_0:2;
       hence x in Seg len c by A279,FINSEQ_1:3;
     end;
     then reconsider domfc as finite set by FINSET_1:13;  domfc = Seg n1
by FINSEQ_1:def 1;
then A280:  Sgm domfc = idseq n1 by FINSEQ_3:54;
then A281:  dom Sgm domfc = Seg n1 by RELAT_1:71;
    set SL = Sgm domfc;
       now let p be set;
     hereby assume
A282:   p in c1; then consider x, y being set such that
A283:  p=[x,y] by RELAT_1:def 1;
A284:   x in dom c1 & y=c1.x by A282,A283,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A285:   1<=k & k<=len c1 by A284,FINSEQ_3:27;
then A286:   x in dom SL by A7,A278,A281,FINSEQ_1:3;
then A287:   k=SL.k by A280,A281,FUNCT_1:35;
A288:   k in domfc by A7,A278,A285;
then A289:   x in dom (fc*SL) by A260,A286,A287,FUNCT_1:21;  0+1<=k by A284,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A290:   0<=i & i<n1 & k=i+1 by A7,A278,A285,Th1;
         (fc*SL).x = fc.k by A287,A289,FUNCT_1:22 .= c.(1+i) by A260,A263,A288
,A290,GRFUNC_1:8
        .= y by A7,A255,A256,A278,A284,A290,Lm2;
      hence p in fc*(Sgm domfc) by A283,A289,FUNCT_1:8;
     end; assume
A291:  p in fc*(Sgm domfc); then consider x,y being set such that
A292:  p=[x,y] by RELAT_1:def 1;
A293:   x in dom (fc*SL) & y = (fc*SL).x by A291,A292,FUNCT_1:8;
then A294:  (fc*SL).x = fc.(SL.x) by FUNCT_1:22;
A295:   x in dom SL & SL.x in dom fc by A293,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n1}
       by A281,FINSEQ_1:def 1;
      then consider k such that
A296:   k=x & 1<=k & k<=n1;
A297:   k in dom fc by A260,A296;
A298:   k=SL.k by A280,A281,A295,A296,FUNCT_1:35;
A299: k in dom c1 by A7,A278,A296,FINSEQ_3:27;  0+1<=k by A296;
     then consider i being Element of NAT such that
A300: 0<=i & i<n1 & k=i+1 by A296,Th1;
       c1.k = c.k by A7,A255,A256,A278,A300,Lm2
         .= y by A263,A293,A294,A296,A297,A298,GRFUNC_1:8;
     hence p in c1 by A292,A296,A299,FUNCT_1:8;
    end; then c1 = fc*(Sgm domfc) by TARSKI:2;
  hence Seq fc = c' by A260,FINSEQ_1:def 14;
    domfvs c= Seg len vs proof
      let x be set; assume x in domfvs;
       then consider k being Element of NAT such that
A301:    x=k & 1<=k & k<=n;  k<=len vs by A257,A301,XXREAL_0:2;
       hence x in Seg len vs by A301,FINSEQ_1:3;
     end; then reconsider domfvs as finite set by FINSET_1:13;
        domfvs = Seg n by FINSEQ_1:def 1;
then A302:  Sgm domfvs = idseq n by FINSEQ_3:54;
then A303:  dom Sgm domfvs = Seg n by RELAT_1:71;
    set SL = Sgm domfvs;
       now let p be set;
     hereby assume
A304:   p in pp; then consider x, y being set such that
A305:  p=[x,y] by RELAT_1:def 1;
A306:   x in dom pp & y=pp.x by A304,A305,FUNCT_1:8;
      then reconsider k = x as Element of NAT;
A307:   1<=k & k<=len pp by A306,FINSEQ_3:27;
then A308:   x in dom SL by A259,A278,A303,FINSEQ_1:3;
then A309:   k=SL.k by A302,A303,FUNCT_1:35;
A310:   k in domfvs by A259,A278,A307;
then A311:   x in dom (fvs*SL) by A269,A308,A309,FUNCT_1:21;  0+1<=k by A306,
FINSEQ_3:27;
      then consider i being Element of NAT such that
A312:   0<=i & i<n & k=i+1 by A259,A278,A307,Th1;
         (fvs*SL).x = fvs.k by A309,A311,FUNCT_1:22 .= vs.(1+i) by A269,A272
,A310,A312,GRFUNC_1:8
        .= y by A257,A259,A278,A306,A312,Def1;
      hence p in fvs*(Sgm domfvs) by A305,A311,FUNCT_1:8;
     end; assume
A313:  p in fvs*(Sgm domfvs); then consider x,y being set such that
A314:  p=[x,y] by RELAT_1:def 1;
A315:   x in dom (fvs*SL) & y = (fvs*SL).x by A313,A314,FUNCT_1:8;
then A316:  (fvs*SL).x = fvs.(SL.x) by FUNCT_1:22;
A317:   x in dom SL & SL.x in dom fvs by A315,FUNCT_1:21;
      then x in {kk where kk is Element of NAT: 1<=kk & kk<=n}
       by A303,FINSEQ_1:def 1;
      then consider k such that
A318:   k=x & 1<=k & k<=n;
A319:   k in dom fvs by A269,A318;
A320:   k=SL.k by A302,A303,A317,A318,FUNCT_1:35;
A321: k in dom pp by A259,A278,A318,FINSEQ_3:27;  0+1<=k by A318;
     then consider i being Element of NAT such that
A322:  0<=i & i<n & k=i+1 by A318,Th1;
        pp.k = vs.k by A257,A259,A278,A322,Def1
         .= y by A272,A315,A316,A318,A319,A320,GRFUNC_1:8;
     hence p in pp by A314,A318,A321,FUNCT_1:8;
    end;
   then pp = fvs*(Sgm domfvs) by TARSKI:2;
  hence Seq fvs = p1 by A269,FINSEQ_1:def 14;
end;
end;

theorem  vs is_vertex_seq_of c implies
 ex fc being FinSubsequence of c, fvs being FinSubsequence of vs, sc, vs1
  st Seq fc = sc & Seq fvs = vs1 & vs1 is_vertex_seq_of sc &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 proof assume
A1: vs is_vertex_seq_of c;
 per cases;
 suppose A2: c is simple Chain of G;
  reconsider fc = c as FinSubsequence of c by Th28;
  reconsider fvs = vs as FinSubsequence of vs by Th28;
  reconsider sc = c as simple Chain of G by A2;
  take fc, fvs, sc, vs;
  thus Seq fc = sc & Seq fvs = vs by Lm7;
  thus vs is_vertex_seq_of sc by A1;
  thus vs.1 = vs.1 & vs.len vs = vs.len vs;
 end;
 suppose A3: not c is simple Chain of G;
defpred P[Element of NAT] means
 ex fc being FinSubsequence of c, fvs being FinSubsequence of vs, c1, vs1
  st Seq fc = c1 & Seq fvs = vs1 & vs1 is_vertex_seq_of c1 &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 &
     len c1 = $1 & not c1 is simple Chain of G;
     P[len c] proof
   reconsider fc = c as FinSubsequence of c by Th28;
   reconsider fvs = vs as FinSubsequence of vs by Th28;
   take fc, fvs, c,vs;
   thus Seq fc = c & Seq fvs = vs by Lm7;
   thus vs is_vertex_seq_of c by A1;
   thus vs.1 = vs.1 & vs.len vs = vs.len vs;
   thus len c = len c & not c is simple Chain of G by A3;
  end;
then A4: ex k st P[k]; consider k such that
A5:  P[k] & for n being Element of NAT st P[n] holds k<=n from NAT_1:sch 5(A4);
  consider fc being FinSubsequence of c, fvs being FinSubsequence of vs,
           c1, vs1 such that
A6:   Seq fc = c1 & Seq fvs = vs1 & vs1 is_vertex_seq_of c1 &
     vs.1 = vs1.1 & vs.len vs = vs1.len vs1 &
     len c1 = k & not c1 is simple Chain of G by A5;
  consider fc1 being FinSubsequence of c1, fvs1 being FinSubsequence of vs1,
           c2, vs2 such that
A7: len c2 < len c1 & vs2 is_vertex_seq_of c2 & len vs2 < len vs1 &
     vs1.1 = vs2.1 & vs1.len vs1 = vs2.len vs2 &
     Seq fc1 = c2 & Seq fvs1 = vs2 by A6,Th52;
   reconsider fc'=fc|rng((Sgm dom fc)|dom fc1) as FinSubsequence of c by Th29;
A8:    Seq fc' = c2 by A6,A7,Th30;
   reconsider fvs'=fvs|rng((Sgm dom fvs)|dom fvs1) as FinSubsequence of vs
                                                                      by Th29;
A9:    Seq fvs' = vs2 by A6,A7,Th30;
     then c2 is simple Chain of G implies thesis by A6,A7,A8;
 hence thesis by A5,A6,A7,A8,A9;
end;
end;

registration let G;
 cluster empty -> one-to-one Chain of G;
 correctness;
end;

theorem  p is Path of G implies p|Seg(n) is Path of G proof assume
A1: p is Path of G;
 reconsider q=p|(Seg n) as FinSequence;
    now let n1,m1 be Element of NAT; assume that
A2: 1<=n1 and
A3: n1 < m1 and
A4: m1<=len q;  1<m1 by A2,A3,XXREAL_0:2;
    then m1 in dom q by A4,FINSEQ_3:27;
then A5: q.m1=p.m1 by FUNCT_1:68;  n1 < len q by A3,A4,XXREAL_0:2;
    then n1 in dom q by A2,FINSEQ_3:27;
then A6: q.n1=p.n1 by FUNCT_1:68;
       dom q c= dom p by RELAT_1:89;
       then dom q c= Seg len p by FINSEQ_1:def 3
;
    then Seg len q c= Seg len p by FINSEQ_1:def 3;
    then len q<=len p by FINSEQ_1:7;
    then m1<=len p by A4,XXREAL_0:2;
   hence q.n1 <> q.m1 by A1,A2,A3,A5,A6,GRAPH_1:def 13;
  end; hence thesis by A1,GRAPH_1:4,def 13;
end;

registration let G;
 cluster simple Path of G;
existence proof consider q being empty Chain of G;
     reconsider q as one-to-one Chain of G;
     take q; consider x being Element of the Vertices of G;
     reconsider p = <*x*> as FinSequence of the Vertices of G;
     take p;
   thus p is_vertex_seq_of q by Lm9;
   let n,m; assume 1<=n & n<m & m<=len p & p.n = p.m;
       then 1<m & m<=1 by XXREAL_0:2,FINSEQ_1:56;
   hence n =1 & m = len p;
 end;
end;

theorem
     2<len sc implies sc is Path of G
proof assume
A1: 2 < len sc; assume not sc is Path of G; then consider m, n such that
A2: 1<=m & m<n & n<=len sc & sc.m = sc.n by GRAPH_1:def 13;
 consider vs such that
A3: vs is_vertex_seq_of sc &
   (for n, m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs)
                                                           by Def10;
A4: len vs = len sc + 1 by A3,Def7; A5: m<=len sc by A2,XXREAL_0:2;
A6: 1<=n by A2,XXREAL_0:2;
A7: 1<=m & m<n & n<=len vs by A2,A4,NAT_1:12;
A8: 1<=m+1 & m+1<n+1 & n+1<=len vs by A2,A4,XREAL_1:8,NAT_1:12;
     then A9: 1<=m & m<n+1 & n+1<=len vs by A2,NAT_1:12;
 set v1 = vs/.m; set v2 = vs/.(m+1);
A10: sc.m joins v1, v2 by A2,A3,A5,Def7;
 set v1' = vs/.n; set v2' = vs/.(n+1);
 A11: sc.n joins v1', v2' by A2,A3,A6,Def7;
    m<=len vs & m+1<=len vs & 1<=n+1 by A8,A9,XXREAL_0:2;
then A12: v1=vs.m & v2=vs.(m+1) & v1'=vs.n & v2'=vs.(n+1)
                                by A6,A7,A8,FINSEQ_4:24;
 per cases by A2,A10,A11,Th32;
 suppose A13: v1=v1' & v2=v2'; then m=1 & n=len vs by A3,A7,A12;
 hence contradiction by A3,A8,A12,A13;
 end;
 suppose
A14: v1=v2' & v2=v1';
then A15: m=1 & n+1=len vs by A3,A9,A12;
 thus contradiction by A1,A3,A4,A7,A12,A14,A15;
end;
end;

theorem  sc is Path of G iff len sc = 0 or len sc = 1 or sc.1<>sc.2 proof
 hereby assume
A1: sc is Path of G; assume
A2: not len sc = 0; assume
A3: not len sc = 1; assume
A4: sc.1 = sc.2;
     len sc >1 by A2,A3,NAT_1:26;
   then 1+1<=len sc by NAT_1:13; hence contradiction by A1,A4,GRAPH_1:def 13;
 end; assume
A5: len sc = 0 or len sc = 1 or sc.1<>sc.2;
  per cases by A5;
  suppose len sc = 0;
   then for n, m st 1<=n & n<m & m<=len sc holds sc.n<>sc.m;
   hence sc is Path of G by GRAPH_1:def 13;
  end;
  suppose len sc = 1;
    then for n,m st 1<=n & n<m & m<=len sc holds sc.n<>sc.m by XXREAL_0:2;
   hence sc is Path of G by GRAPH_1:def 13;
  end;
  suppose
A6: sc.1<>sc.2;
     now let n, m; assume that
A7: 1<=n and
A8: n<m and
A9: m<=len sc;
   thus sc.n<>sc.m proof assume
A10: not thesis;
   consider vs such that
A11: vs is_vertex_seq_of sc &
   for n,m st 1<=n & n<m & m<=len vs & vs.n=vs.m holds n=1 & m=len vs
                                           by Def10;
A12: len vs = len sc +1 by A11,Def7;
A13: n<=len sc & 1<=m by A7,A8,A9,XXREAL_0:2;
then A14: n<=len vs & m<=len vs by A9,A12,NAT_1:12;
A15: 1<=n+1 & n+1<=len vs & 1<=m+1 & m+1<=len vs
                             by A9,A12,A13,XREAL_1:8,NAT_1:12;
   set vn = vs/.n; set vn1 = vs/.(n+1);
   set vm = vs/.m; set vm1 = vs/.(m+1);
A16: vn=vs.n & vn1=vs.(n+1) & vm=vs.m & vm1=vs.(m+1)
     by A7,A13,A14,A15,FINSEQ_4:24;
 A17: sc.n joins vn, vn1 & sc.m joins vm, vm1 by A7,A9,A11,A13,Def7;
   per cases by A10,A17,Th32;
   suppose
A18: vn=vm & vn1=vm1;  n+1<m+1 by A8,XREAL_1:8;
    then n=1 & m=len vs & n+1=1 & m+1=len vs by A7,A8,A11,A14,A15,A16,A18;
   hence contradiction;
   end;
   suppose
A19: vn=vm1 & vn1=vm;
   thus contradiction proof A20: n+0<m+1 by A8,XREAL_1:10;
A21: n+1<=m by A8,NAT_1:13;
    per cases by A21,REAL_1:def 5;
    suppose
A22: n+1=m;  n=1 & m+1=len vs by A7,A11,A15,A16,A19,A20;
hence contradiction by A6,A10,A22;
    end;
    suppose
    n+1<m; then n=1 & m+1=len vs & n+1=1 & m=len vs
    by A7,A11,A14,A15,A16,A19,A20
;
    hence contradiction;
   end;
   end;
  end;
  end; end;
  hence sc is Path of G by GRAPH_1:def 13;
end;
end;

registration let G;
 cluster empty -> oriented Chain of G;
 correctness proof let p be Chain of G; assume A1: p is empty;
       for n st 1<=n & n < len p holds
       (the Source of G).(p.(n+1)) = (the Target of G).(p.n)
         by A1,FINSEQ_1:25;
   hence thesis by GRAPH_1:def 12;
  end;
end;

definition let G; let oc be oriented Chain of G; assume
A1: oc <> {};
 func vertex-seq oc -> FinSequence of the Vertices of G means
    it is_vertex_seq_of oc & it.1 = (the Source of G).(oc.1);
 existence proof
   set SG = the Source of G; set TG = the Target of G;
 deffunc F(Element of NAT) = SG.(oc.$1);
 consider z being FinSequence such that
A2: len z = len oc &
for j st j in Seg len oc holds z.j = F(j) from FINSEQ_1:sch 2;
A3: Seg len oc = dom oc & Seg len z = dom z by FINSEQ_1:def 3;
      len oc <> 0 by A1,FINSEQ_1:25; then 0<len oc & 0+1=1;
then A4: 1<=len oc by NAT_1:13;
then A5: 1 in dom z by A2,FINSEQ_3:27;
    set vs = z^<*TG.(oc.len oc)*>;
A6: len vs = len oc + len <*TG.(oc.len oc)*> by A2,FINSEQ_1:35
          .= len oc +1 by FINSEQ_1:56;
      rng vs c= the Vertices of G proof let y be set; assume
       y in rng vs; then consider x being set such that
A7:  x in dom vs & y = vs.x by FUNCT_1:def 5;
    reconsider x as Element of NAT by A7;
A8:  1<=x & x<=len vs by A7,FINSEQ_3:27;
    per cases by A6,A8,NAT_1:8;
    suppose
      x<=len oc;
then A9:   x in dom oc by A8,FINSEQ_3:27;
   then A10: oc.x in rng oc & rng oc c= the Edges of G by FINSEQ_1:def 4
,FUNCT_1:def 5;
     then reconsider e=oc.x as Element of the Edges of G;
A11:   SG.e in rng the Source of G by A10,FUNCT_2:6;
     A12: rng the Source of G c= the Vertices of G by RELSET_1:12;
        vs.x = z.x by A2,A3,A9,FINSEQ_1:def 7 .= SG.e by A2,A3,A9;
      hence y in the Vertices of G by A7,A11,A12;
    end;
    suppose A13:
     x=len oc +1;  len oc in dom oc by A4,FINSEQ_3:27;
     then A14: oc.len oc in rng oc & rng oc c= the Edges of G
                                      by FINSEQ_1:def 4,FUNCT_1:def 5;
     then reconsider e=oc.len oc as Element of the Edges of G;
A15:   TG.e in rng the Target of G by A14,FUNCT_2:6;
        rng the Target of G c= the Vertices of G by RELSET_1:12;
then y is Element of the Vertices of G by A2,A7,A13,A15,FINSEQ_1:59;
     hence y in the Vertices of G;
   end;
   end;
   then reconsider vs as FinSequence of the Vertices of G by FINSEQ_1:def 4;
  take vs;
     now thus len vs = len oc + 1 by A6;
   let n; assume that
A16: 1<=n and
A17: n<=len oc;
A18: n<=len vs & 1<=n+1 & n+1<=len vs by A6,A17,NAT_1:12,XREAL_1:9;
then A19: n in dom vs & n+1 in dom vs by A16,FINSEQ_3:27;
A20: n in dom oc by A16,A17,FINSEQ_3:27;
    reconsider vsn = vs.n as Element of the Vertices of G by A19,FINSEQ_2:13;
 reconsider vsn1 = vs.(n+1) as Element of the Vertices of G by A19,FINSEQ_2:13;
A21: vsn = z.n by A2,A3,A20,FINSEQ_1:def 7 .= SG.(oc.n) by A2,A3,A20;
   A22: vsn1 = TG.(oc.n) proof
    per cases by A17,REAL_1:def 5;
    suppose A23: n<len oc; then n+1<=len oc by NAT_1:13;
then A24:   n+1 in dom oc by A18,FINSEQ_3:27;
     hence vsn1 = z.(n+1) by A2,A3,FINSEQ_1:def 7 .= SG.(oc.(n+1)) by A2,A3,A24
               .= TG.(oc.n) by A16,A23,GRAPH_1:def 12;
    end;
    suppose n=len oc;
     hence thesis by A2,FINSEQ_1:59;
   end;
   end;
       vsn = vs/.n & vsn1 = vs/.(n+1) by A16,A18,FINSEQ_4:24;
   hence oc.n joins vs/.n, vs/.(n+1) by A21,A22,GRAPH_1:def 9;
  end;
  hence vs is_vertex_seq_of oc by Def7;
     z.1 = SG.(oc.1) by A2,A3,A5;
  hence vs.1 = SG.(oc.1) by A5,FINSEQ_1:def 7;
 end;
 uniqueness by A1,Th37;
end;

begin :: Addenda

:: from AMISTD_1, 2006.01.12, A.T.

theorem
 for f1 being non empty FinSequence of D, f2 being FinSequence of D
  holds (f1^'f2)/.1 = f1/.1
proof
  let f1 be non empty FinSequence of D,
      f2 be FinSequence of D;
A1:1 in dom f1 by FINSEQ_5:6;
A2:1 in dom (f1^(2, len f2)-cut f2) by FINSEQ_5:6;
  thus (f1^'f2)/.1 = (f1^(2, len f2)-cut f2).1 by A2,FINSEQ_4:def 4
    .= f1.1 by A1,FINSEQ_1:def 7
    .= f1/.1 by A1,FINSEQ_4:def 4;
end;

theorem
 for f1 being FinSequence of D, f2 being non trivial FinSequence of D
  holds (f1^'f2)/.len(f1^'f2) = f2/.len f2
proof
  let f1 be FinSequence of D,
      f2 be non trivial FinSequence of D;
        2 <= len f2 by REALSET1:13;
then A1: 1 < len f2 by XXREAL_0:2;
A2: 1+0 < len f1 + len f2 by A1,XREAL_1:10;
      len (f1^'f2) + 1 = len f1 + len f2 by Th13;
  then 1 <= len(f1^'f2) by A2,NAT_1:13;
  hence (f1^'f2)/.len(f1^'f2)
    = (f1^'f2).len(f1^'f2) by FINSEQ_4:24
   .= f2.len f2 by A1,Th16
   .= f2/.len f2 by A1,FINSEQ_4:24;
end;

theorem Th59:
 for f being FinSequence holds f^'{} = f
 proof
  let f be FinSequence;
      len {} = 0 by FINSEQ_1:25;
then A1: 2 > len{};
  thus f^'{} = f^{} by A1,Def1
        .= f by FINSEQ_1:47;
 end;

theorem
 for f being FinSequence holds f^'<*x*> = f
 proof
   let f be FinSequence;
       len <*x*> = 1 by FINSEQ_1:56;
   then (2, len <*x*>)-cut <*x*> = {} by Def1;
  hence f^'<*x*> = f by FINSEQ_1:47;
 end;

theorem  :: GRAPH_2:14
 for f1, f2 being FinSequence of D holds
  1<=n & n<=len f1 implies (f1^'f2)/.n = f1/.n
proof
  let f1, f2 be FinSequence of D;
  assume that
A1:  1<=n and
A2:  n<=len f1;
  per cases;
  suppose f2 = {};
  hence (f1^'f2)/.n = f1/.n by Th59;
  end;
  suppose
A3: f2 <> {};
then A4:len (f1^'f2) + 1 = len f1 + len f2 by Th13;
      len f2 <> 0 by A3,FINSEQ_1:25;
  then 0 < len f2;
  then len f1 + 0 < len f1 + len f2 by XREAL_1:8;
  then n < len (f1^'f2) + 1 by A2,A4,XXREAL_0:2;
  then n <= len (f1^'f2) by NAT_1:13;
  hence (f1^'f2)/.n = (f1^'f2).n by A1,FINSEQ_4:24
     .= f1.n by A1,A2,Th14
     .= f1/.n by A1,A2,FINSEQ_4:24;
end;
end;

theorem  :: GRAPH_2:15
 for f1, f2 being FinSequence of D holds
  1<=n & n<len f2 implies (f1^'f2)/.(len f1 + n) = f2/.(n+1)
proof
  let f1, f2 be FinSequence of D;
  assume that
A1:  1<=n and
A2:  n<len f2;
A3: 0+1 <= len f1 + n by A1,XREAL_1:9;
A4: now per cases;
      suppose f2 <> {};
then A5:   len (f1^'f2) + 1 = len f1 + len f2 by Th13;
          len f1 + n < len f1 + len f2 by A2,XREAL_1:8;
      hence len f1+n <= len (f1^'f2) by A5,NAT_1:13;
      end;
      suppose f2 = {};
      then len f2 = 0 by FINSEQ_1:25;
      hence len f1+n <= len (f1^'f2) by A2;
    end;
    end;
A6: 0+1 <= n+1 by XREAL_1:8;
A7: n+1 <= len f2 by A2,NAT_1:13;
  thus (f1^'f2)/.(len f1 + n)
     = (f1^'f2).(len f1 + n) by A3,A4,FINSEQ_4:24
    .= f2.(n+1) by A1,A2,Th15
    .= f2/.(n+1) by A6,A7,FINSEQ_4:24;
end;

