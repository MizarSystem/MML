:: Graphs
::  by Krzysztof Hryniewiecki
::
:: Received December 5, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, STRUCT_0, FUNCT_1, GLIB_000, XBOOLE_0,
      FUNCT_5, RELAT_1, PARTFUN1, RELAT_2, FINSET_1, TREES_2, FINSEQ_1,
      XXREAL_0, ARYTM_3, TARSKI, CARD_1, FUNCT_2, ZFMISC_1, MCART_1, GRAPH_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1,
      FUNCT_2, FUNCT_5, FINSEQ_1, FINSET_1, PARTFUN1, CARD_1, NUMBERS, NAT_1,
      MCART_1, XXREAL_0, STRUCT_0;
 constructors PARTFUN1, FUNCT_2, XXREAL_0, XREAL_0, NAT_1, FINSEQ_1, STRUCT_0,
      FUNCT_5, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, PARTFUN1, FINSET_1,
      XREAL_0, FINSEQ_1, ORDINAL1, STRUCT_0, CARD_1, FUNCT_2;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions FUNCT_1, TARSKI;
 theorems FUNCT_2, FUNCT_1, PARTFUN1, FINSEQ_1, NAT_1, ZFMISC_1, MCART_1,
      TARSKI, RELAT_1, FINSEQ_3, RELSET_1, XBOOLE_0, XBOOLE_1, XREAL_1,
      XXREAL_0, CARD_1;
 schemes FUNCT_2, TARSKI, XBOOLE_0;

begin

reserve x, y, z, v for set,
  n, m, k for Element of NAT;

definition
  struct(2-sorted) MultiGraphStruct (# carrier, carrier' -> set,
    Source, Target -> Function of the carrier', the carrier #);
end;

definition
  let G be MultiGraphStruct;
  mode Vertex of G is Element of G;
  mode Edge of G is Element of the carrier' of G;
end;

registration
  cluster strict non empty non void MultiGraphStruct;
  existence
  proof
 MultiGraphStruct(# 1, 1, op1, op1 #) is non empty non void;
    hence thesis;
  end;
end;

definition
  mode Graph is non empty MultiGraphStruct;
end;

reserve G, G1, G2, G3 for Graph;
 
:: from CAT_1, 2008.07.02, A.T.

definition
  let C be non void non empty MultiGraphStruct, f be Edge of C;
  func dom f -> Vertex of C equals
  (the Source of C).f;
  correctness;
  func cod f -> Vertex of C equals
  (the Target of C).f;
  correctness;
end;

definition
  let G1, G2;
  assume that
A1: (the Source of G1) tolerates (the Source of G2) and
A2: (the Target of G1) tolerates (the Target of G2);
  func G1 \/ G2 -> strict Graph means
  :Def3:
  the carrier of it = (the carrier of G1) \/ (the carrier of G2) &
  the carrier' of it = (the carrier' of G1) \/ (the carrier' of G2) &
  (for v st v in the carrier' of G1 holds
  (the Source of it).v = (the Source of G1).v &
  (the Target of it).v = (the Target of G1).v) &
  for v st v in the carrier' of G2 holds
  (the Source of it).v = (the Source of G2).v &
  (the Target of it).v = (the Target of G2).v;
  existence
  proof
    set V = (the carrier of G1) \/ (the carrier of G2);
    set E = (the carrier' of G1) \/ (the carrier' of G2);
 ex S being Function of E, V st
    (for v st v in the carrier' of G1 holds S.v = (the Source of G1).v) &
    for v st v in the carrier' of G2 holds S.v = (the Source of G2).v
    proof
      defpred P[set,set] means
      ($1 in the carrier' of G1 implies $2 = (the Source of G1).$1) &
      ($1 in the carrier' of G2 implies $2 = (the Source of G2).$1);
A4:   for x being set st x in E ex y being set st y in V & P[x,y]
      proof
        let x be set;
        assume
A5:     x in E;
A6:     x in the carrier' of G1 implies thesis
        proof
          assume
A7:       x in the carrier' of G1;
          take y = (the Source of G1).x qua set;
       y in the carrier of G1 by A7,FUNCT_2:7;
          hence y in V by XBOOLE_0:def 3;
          thus x in the carrier' of G1 implies y = (the Source of G1).x;
          assume x in the carrier' of G2;
then A10:      x in (the carrier' of G1) /\ (the carrier' of G2)
          by A7,XBOOLE_0:def 4;
      dom (the Source of G1) = the carrier' of G1 by FUNCT_2:def 1;
then       x in dom(the Source of G1) /\ dom (the Source of
          G2) by A10,FUNCT_2:def 1;
          hence y = (the Source of G2).x by A1,PARTFUN1:def 6;
        end;
    not x in the carrier' of G1 implies thesis
        proof
          assume
A14:      not x in the carrier' of G1;
then A15:      x in the carrier' of G2 by A5,XBOOLE_0:def 3;
          take y = (the Source of G2).x qua set;
      y in the carrier of G2 by A15,FUNCT_2:7;
          hence y in V by XBOOLE_0:def 3;
          thus thesis by A14;
        end;
        hence thesis by A6;
      end;
      consider S being Function of E, V such that
A17:  for x being set st x in E holds P[x,S.x] from FUNCT_2:sch 1(A4);
      take S;
      thus
      for v st v in the carrier' of G1 holds S.v = (the Source of G1).v
      proof
        let v;
        assume
A18:    v in the carrier' of G1;
then     v in E by XBOOLE_0:def 3;
        hence thesis by A17,A18;
      end;
      let v;
      assume
A20:  v in the carrier' of G2;
then   v in E by XBOOLE_0:def 3;
      hence thesis by A17,A20;
    end;
    then consider S being Function of E, V such that
A22: for v st v in the carrier' of G1 holds S.v = (the Source of G1).v and
A23: for v st v in the carrier' of G2 holds S.v = (the Source of G2).v;
 ex T being Function of E, V st
    (for v st v in the carrier' of G1 holds T.v = (the Target of G1).v) &
    for v st v in the carrier' of G2 holds T.v = (the Target of G2).v
    proof
      defpred P[set,set] means
      ($1 in the carrier' of G1 implies $2 = (the Target of G1).$1) &
      ($1 in the carrier' of G2 implies $2 = (the Target of G2).$1);
A25:  for x being set st x in E ex y being set st y in V & P[x,y]
      proof
        let x be set;
        assume
A26:    x in E;
A27:    x in the carrier' of G1 implies thesis
        proof
          assume
A28:      x in the carrier' of G1;
          take y = (the Target of G1).x qua set;
      y in the carrier of G1 by A28,FUNCT_2:7;
          hence y in V by XBOOLE_0:def 3;
          thus
          x in the carrier' of G1 implies y = (the Target of G1). x;
          assume x in the carrier' of G2;
then A31:      x in (the carrier' of G1) /\ (the carrier' of G2)
          by A28,XBOOLE_0:def 4;
      dom (the Target of G1) = the carrier' of G1 by FUNCT_2:def 1;
then       x in dom(the Target of G1) /\ dom (the Target of G2) by A31,
          FUNCT_2:def 1;
          hence y = (the Target of G2).x by A2,PARTFUN1:def 6;
        end;
    not x in the carrier' of G1 implies thesis
        proof
          assume
A35:      not x in the carrier' of G1;
then A36:      x in the carrier' of G2 by A26,XBOOLE_0:def 3;
          take y = (the Target of G2).x qua set;
      y in the carrier of G2 by A36,FUNCT_2:7;
          hence y in V by XBOOLE_0:def 3;
          thus thesis by A35;
        end;
        hence thesis by A27;
      end;
      consider S being Function of E, V such that
A38:  for x being set st x in E holds P[x,S.x] from FUNCT_2:sch 1(A25);
      take S;
      thus
      for v st v in the carrier' of G1 holds S.v = (the Target of G1).v
      proof
        let v;
        assume
A39:    v in the carrier' of G1;
then     v in E by XBOOLE_0:def 3;
        hence thesis by A38,A39;
      end;
      let v;
      assume
A41:  v in the carrier' of G2;
then   v in E by XBOOLE_0:def 3;
      hence thesis by A38,A41;
    end;
    then consider T being Function of E, V such that
A43: for v st v in the carrier' of G1 holds T.v = (the Target of G1).v and
A44: for v st v in the carrier' of G2 holds T.v = (the Target of G2).v;
    reconsider G = MultiGraphStruct(# V, E, S, T #)
    as strict Graph;
    take G;
    thus the carrier of G = V;
    thus the carrier' of G = E;
    thus for v st v in the carrier' of G1 holds
    (the Source of G).v = (the Source of G1).v &
    (the Target of G).v = (the Target of G1).v by A22,A43;
    let v;
    assume
A45: v in the carrier' of G2;
    hence (the Source of G).v = (the Source of G2).v by A23;
    thus thesis by A44,A45;
  end;
  uniqueness
  proof
    let G, G9 be strict Graph such that
A46: the carrier of G = (the carrier of G1) \/ (the carrier of G2) and
A47: the carrier' of G = (the carrier' of G1) \/ (the carrier' of G2) and
A48: for v st v in the carrier' of G1 holds (the Source of G).v = (the
    Source of G1).v & (the Target of G).v = (the Target of G1).v and
A49: for v st v in the carrier' of G2 holds (the Source of G).v = (the
    Source of G2).v & (the Target of G).v = (the Target of G2).v and
A50: the carrier of G9 = (the carrier of G1) \/ (the carrier of G2) and
A51: the carrier' of G9 = (the carrier' of G1) \/ (the carrier' of G2) and
A52: for v st v in the carrier' of G1 holds (the Source of G9).v = (the
    Source of G1).v & (the Target of G9).v = (the Target of G1).v and
A53: for v st v in the carrier' of G2 holds (the Source of G9).v = (the
    Source of G2).v & (the Target of G9).v = (the Target of G2).v;
    A54: dom
 (the Source of G) = the carrier' of G & dom (the Source of G9) = the
    carrier' of G by A47,A51,FUNCT_2:def 1;
    A55: dom
 (the Target of G) = the carrier' of G & dom (the Target of G9) = the
    carrier' of G by A47,A51,FUNCT_2:def 1;
 for x being set st x in the carrier' of G holds
    (the Source of G).x = (the Source of G9).x
    proof
      let x be set such that
A57:  x in the carrier' of G;
A58:  now
        assume
A59:    x in the carrier' of G1;
        hence (the Source of G).x = (the Source of G1).x by A48
          .= (the Source of G9).x by A52,A59;
      end;
  now
        assume
A61:    x in the carrier' of G2;
        hence (the Source of G).x = (the Source of G2).x by A49
          .= (the Source of G9).x by A53,A61;
      end;
      hence thesis by A47,A57,A58,XBOOLE_0:def 3;
    end;
then A62: the Source of G = the Source of G9 by A54,FUNCT_1:9;
 for x being set st x in the carrier' of G holds
    (the Target of G).x = (the Target of G9).x
    proof
      let x be set such that
A64:  x in the carrier' of G;
A65:  now
        assume
A66:    x in the carrier' of G1;
        hence (the Target of G).x = (the Target of G1).x by A48
          .= (the Target of G9).x by A52,A66;
      end;
  now
        assume
A68:    x in the carrier' of G2;
        hence (the Target of G).x = (the Target of G2).x by A49
          .= (the Target of G9).x by A53,A68;
      end;
      hence thesis by A47,A64,A65,XBOOLE_0:def 3;
    end;
    hence thesis by A46,A47,A50,A51,A55,A62,FUNCT_1:9;
  end;
end;

definition
  let G, G1, G2 be Graph;
  pred G is_sum_of G1, G2 means
  :Def4:
  (the Target of G1) tolerates (the Target of G2) &
  (the Source of G1) tolerates (the Source of G2) &
  the MultiGraphStruct of G = G1 \/ G2;
end;

definition
  let IT be Graph;
  attr IT is oriented means
  :Def5:
  for x,y st x in the carrier' of IT & y in the carrier' of IT &
  (the Source of IT).x = (the Source of IT).y &
  (the Target of IT).x = (the Target of IT).y holds x = y;
  attr IT is non-multi means
  :Def6:
  for x,y st x in the carrier' of IT & y in the carrier' of IT &
  ( (the Source of IT).x = (the Source of IT).y &
  (the Target of IT).x = (the Target of IT).y or
  (the Source of IT).x = (the Target of IT).y &
  (the Source of IT).y = (the Target of IT).x ) holds x = y;
  attr IT is simple means
  :Def7:
  not ex x st x in the carrier' of IT &
  (the Source of IT).x = (the Target of IT).x;
  attr IT is connected means
  :Def8:
  not ex G1, G2 being Graph st
  the carrier of G1 misses the carrier of G2 & IT is_sum_of G1, G2;
end;

definition
  let IT be MultiGraphStruct;
  attr IT is finite means
  :Def9:
  the carrier of IT is finite & the carrier' of IT is finite;
end;

registration
  cluster finite MultiGraphStruct;
  existence
  proof
    take G = MultiGraphStruct(# 1, 1, op1, op1 #);
    thus the carrier of G is finite;
    thus thesis;
  end;
  cluster finite non-multi oriented simple connected Graph;
  existence
  proof
    set V = {1};
    reconsider empty1 = {} as Function of {}, V by RELSET_1:25;
    reconsider G = MultiGraphStruct(# V, {}, empty1, empty1 #)
    as Graph;
    take G;
    thus G is finite by Def9;
 for x,y st x in the carrier' of G & y in the carrier' of G &
    ( (the Source of G).x = (the Source of G).y &
    (the Target of G).x = (the Target of G).y or
    (the Source of G).x = (the Target of G).y &
    (the Source of G).y = (the Target of G).x ) holds x = y;
    hence G is non-multi by Def6;
 for x,y st x in the carrier' of G & y in the carrier' of G &
    (the Source of G).x = (the Source of G).y &
    (the Target of G).x = (the Target of G).y holds x = y;
    hence G is oriented by Def5;
 not ex x st x in the carrier' of G &
    (the Source of G).x = (the Target of G).x;
    hence G is simple by Def7;
 not ex G1, G2 being Graph st
    (the carrier of G1) misses (the carrier of G2) & G is_sum_of G1, G2
    proof
      given G1, G2 being Graph such that
A5:   (the carrier of G1) misses (the carrier of G2) and
A6:   G is_sum_of G1, G2;
A7:   (the carrier of G1) /\ (the carrier of G2) = {} by A5,XBOOLE_0:def 7;
      A8:   (
the Source of G1) tolerates (the Source of G2) & (the Target of G1)
      tolerates (the Target of G2) by A6,Def4;
   G = G1 \/ G2 by A6,Def4;
then A10:  (the carrier of G1) \/ (the carrier of G2) = V by A8,Def3;
      consider x being Vertex of G1;
  x in the carrier of G1 & x in V by A10,XBOOLE_0:def 3;
then A12:  1 in the carrier of G1 by TARSKI:def 1;
      consider y being Vertex of G2;
  y in the carrier of G2 & y in V by A10,XBOOLE_0:def 3;
then   1 in the carrier of G2 by TARSKI:def 1;
      hence contradiction by A7,A12,XBOOLE_0:def 4;
    end;
    hence thesis by Def8;
  end;
end;

registration
  let G be finite MultiGraphStruct;
  cluster the carrier of G -> finite;
  coherence by Def9;
  cluster the carrier' of G -> finite;
  coherence by Def9;
end;

reserve x, y for Element of (the carrier of G);

definition
  let G;
  let x, y;
  let v;
  pred v joins x, y means
  (the Source of G).v = x & (the Target of G).v = y or
  (the Source of G).v = y & (the Target of G).v = x;
end;

definition
  let G;
  let x,y be Element of (the carrier of G);
  pred x,y are_incident means
  ex v being set st v in the carrier' of G & v joins x, y;
end;

definition
  let G be Graph;
  mode Chain of G -> FinSequence means
    :Def12:
    (for n st 1 <= n & n <= len it holds it.n in the carrier' of G) &
    ex p being FinSequence st len p = len it + 1 &
    (for n st 1 <= n & n <= len p holds p.n in the carrier of G) &
    for n st 1 <= n & n <= len it
    ex x9, y9 being Vertex of G st
    x9 = p.n & y9 = p.(n+1) & it.n joins x9, y9;
  existence
  proof
    take {};
    thus for n st 1 <= n & n <= len {} holds {}.n in the carrier' of G
    by XXREAL_0:2;
    consider x being Vertex of G;
A1: x in the carrier of G;
    take <*x*>;
    thus len <*x*> = len {} + 1 by FINSEQ_1:56;
    thus
    for n st 1 <= n & n <= len <*x*> holds <*x*>.n in the carrier of G
    proof
      let n;
      assume that
A2:   1 <= n and
A3:   n <= len <*x*>;
   n <= 1 by A3,FINSEQ_1:56;
then    n = 1 by A2,XXREAL_0:1;
      hence thesis by A1,FINSEQ_1:57;
    end;
    let n;
    thus thesis by XXREAL_0:2;
  end;
end;

Lm1: for G holds {} is Chain of G
proof
  let G;
  thus for n st 1 <= n & n <= len {} holds {}.n in the carrier' of G
  by XXREAL_0:2;
  consider x being Vertex of G;
A1: x in the carrier of G;
  take <*x*>;
  thus len <*x*> = len {} + 1 by FINSEQ_1:56;
  thus
  for n st 1 <= n & n <= len <*x*> holds <*x*>.n in the carrier of G
  proof
    let n;
    assume that
A2: 1 <= n and
A3: n <= len <*x*>;
 n <= 1 by A3,FINSEQ_1:56;
then  n = 1 by A2,XXREAL_0:1;
    hence thesis by A1,FINSEQ_1:57;
  end;
  let n;
  thus thesis by XXREAL_0:2;
end;

definition
  let G be Graph;
  redefine mode Chain of G -> FinSequence of the carrier' of G;
  coherence
  proof
    let c be Chain of G;
 rng c c= the carrier' of G
    proof
      let y be set;
      assume y in rng c;
      then consider x being set such that
A3:   x in dom c and
A4:   y=c.x by FUNCT_1:def 5;
      reconsider x as Element of NAT by A3;
   1<=x & x<=len c by A3,FINSEQ_3:27;
      hence thesis by A4,Def12;
    end;
    hence thesis by FINSEQ_1:def 4;
  end;
end;

definition
  let G be Graph;
  let IT be Chain of G;
  attr IT is oriented means
  for n st 1 <= n & n < len IT holds
  (the Source of G).(IT.(n+1)) = (the Target of G).(IT.n);
end;

registration
  let G be Graph;
  cluster empty Chain of G;
  existence
  proof
 {} is Chain of G by Lm1;
    hence thesis;
  end;
end;

registration
  let G be Graph;
  cluster empty -> oriented Chain of G;
  coherence
  proof
    let C be Chain of G;
    assume
A1: C is empty;
    let n;
    thus thesis by A1,CARD_1:47,XXREAL_0:2;
  end;
end;

definition
  let G be Graph;
  let IT be Chain of G;
  redefine attr IT is one-to-one means
  for n, m st 1 <= n & n < m & m <= len IT holds IT.n <> IT.m;
  compatibility
  proof
    thus IT is one-to-one implies
    for n, m st 1 <= n & n < m & m <= len IT holds IT.n <> IT.m
    proof
      assume
A1:   IT is one-to-one;
      let n,m;
      assume that
A2:   1 <= n and
A3:   n < m and
A4:   m <= len IT;
A5:   n <= len IT by A3,A4,XXREAL_0:2;
A6:   1 <= m by A2,A3,XXREAL_0:2;
A7:   n in dom IT by A2,A5,FINSEQ_3:27;
   m in dom IT by A4,A6,FINSEQ_3:27;
      hence thesis by A1,A3,A7,FUNCT_1:def 8;
    end;
    assume
A9: for n, m st 1 <= n & n < m & m <= len IT holds IT.n <> IT.m;
    let x1,x2 be set;
    assume that
A10: x1 in dom IT & x2 in dom IT and
A11: IT.x1 = IT.x2;
    reconsider x9 = x1, y9 = x2 as Element of NAT by A10;
A12: x9 <= len IT & 1 <= y9 by A10,FINSEQ_3:27;
A13: 1 <= x9 & y9 <= len IT by A10,FINSEQ_3:27;
    per cases;
    suppose
A14:  x9 <> y9;
  now per cases by A14,XXREAL_0:1;
        suppose
      x9 < y9;
          hence thesis by A9,A11,A13;
        end;
        suppose
      y9 < x9;
          hence thesis by A9,A11,A12;
        end;
      end;
      hence thesis;
    end;
    suppose
  x9 = y9;
      hence thesis;
    end;
  end;
end;

definition
  let G be Graph;
  mode Path of G is one-to-one Chain of G;
end;

definition
  let G be Graph;
  mode OrientedPath of G is one-to-one oriented Chain of G;
end;

definition
  let G be Graph;
  let IT be Path of G;
  canceled;
  attr IT is cyclic means
  ex p being FinSequence st len p = len IT + 1 &
  (for n st 1 <= n & n <= len p holds p.n in the carrier of G) &
  (for n st 1 <= n & n <= len IT
  ex x9, y9 being Vertex of G st
  x9 = p.n & y9 = p.(n+1) & IT.n joins x9, y9) & p.1 = p.(len p);
end;

registration
  let G be Graph;
  cluster empty -> cyclic Path of G;
  coherence
  proof
    let p be Path of G;
    assume
A1: p is empty;
    consider x being Vertex of G;
A2: x in the carrier of G;
    take <*x*>;
    thus len <*x*> = len p + 1 by A1,CARD_1:47,FINSEQ_1:56;
    thus
    for n st 1 <= n & n <= len <*x*> holds <*x*>.n in the carrier of G
    proof
      let n;
      assume that
A3:   1 <= n and
A4:   n <= len <*x*>;
   n <= 1 by A4,FINSEQ_1:56;
then    n = 1 by A3,XXREAL_0:1;
      hence thesis by A2,FINSEQ_1:57;
    end;
    thus for n st 1 <= n & n <= len p ex x9, y9 being Vertex of G st
    x9 = <*x*>.n & y9 = <*x*>.(n+1) & p.n joins x9, y9
    by A1,CARD_1:47,XXREAL_0:2;
    thus thesis by FINSEQ_1:56;
  end;
end;

definition
  let G be Graph;
  mode Cycle of G is cyclic Path of G;
end;

definition
  let G be Graph;
  mode OrientedCycle of G is cyclic OrientedPath of G;
end;

definition
  let G be Graph;
  canceled;
  mode Subgraph of G -> Graph means
    :Def18:
    the carrier of it c= the carrier of G &
    the carrier' of it c= the carrier' of G &
    for v st v in the carrier' of it holds
    (the Source of it).v = (the Source of G).v &
    (the Target of it).v = (the Target of G).v &
    (the Source of G).v in the carrier of it &
    (the Target of G).v in the carrier of it;
  existence
  proof
    take G;
    thus the carrier of G c= the carrier of G;
    thus the carrier' of G c= the carrier' of G;
    let v;
    assume v in the carrier' of G;
    hence thesis by FUNCT_2:7;
  end;
end;

registration
  let G be Graph;
  cluster strict Subgraph of G;
  existence
  proof
    reconsider S = MultiGraphStruct(# the carrier of G, the carrier' of G,
      the Source of G, the Target of G #) as Graph;
 for v st v in the carrier' of S holds
    (the Source of S).v = (the Source of G).v &
    (the Target of S).v = (the Target of G).v &
    (the Source of G).v in the carrier of S &
    (the Target of G).v in the carrier of S by FUNCT_2:7;
then  S is Subgraph of G by Def18;
    hence thesis;
  end;
end;

definition
  let G be finite Graph;
  func VerticesCount G -> Element of NAT means
  ex B being finite set st B = the carrier of G & it = card B;
  existence
  proof
    set B = the carrier of G;
    take card B, B;
    thus thesis;
  end;
  uniqueness;
  func EdgesCount G -> Element of NAT means
  ex B being finite set st B = the carrier' of G & it = card B;
  existence
  proof
    set B = the carrier' of G;
    take card B, B;
    thus thesis;
  end;
  uniqueness;
end;

definition
  let G be finite Graph;
  let x be Vertex of G;
  func EdgesIn x -> Element of NAT means
  ex X being finite set st (for z being set holds
  z in X iff z in the carrier' of G & (the Target of G).z = x) & it = card(X);
  existence
  proof
    defpred P[set] means (the Target of G).$1 = x;
    consider X being set such that
A1: for z being set holds z in X iff
    z in the carrier' of G & P[z] from XBOOLE_0:sch 1;
 for z being set st z in X holds z in the carrier' of G by A1;
then  X c= the carrier' of G by TARSKI:def 3;
    then reconsider X as finite set;
    take card(X);
    take X;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let n1, n2 be Element of NAT such that
A4: ex X being finite set st (for z being set holds
    z in X iff z in the carrier' of G & (the Target of G).z = x)
    & n1 = card(X) and
A5: ex X being finite set st (for z being set holds
    z in X iff z in the carrier' of G & (the Target of G).z = x)
    & n2 = card(X);
    consider X1 being finite set such that
A6: for z being set holds z in X1 iff z in the carrier' of G & (the Target
    of G).z = x and
A7: n1 = card(X1) by A4;
    consider X2 being finite set such that
A8: for z being set holds z in X2 iff z in the carrier' of G & (the Target
    of G).z = x and
A9: n2 = card(X2) by A5;
 for z being set holds z in X1 iff z in X2
    proof
      let z;
  z in X1 iff z in the carrier' of G & (the Target of G).z = x by A6;
      hence thesis by A8;
    end;
    hence thesis by A7,A9,TARSKI:2;
  end;
  func EdgesOut x -> Element of NAT means
  ex X being finite set st (for z being set holds
  z in X iff z in the carrier' of G & (the Source of G).z = x) & it = card(X);
  existence
  proof
    defpred P[set] means (the Source of G).$1 = x;
    consider X being set such that
A12: for z being set holds z in X iff
    z in the carrier' of G & P[z] from XBOOLE_0:sch 1;
 for z being set st z in X holds z in the carrier' of G by A12;
then  X c= the carrier' of G by TARSKI:def 3;
    then reconsider X as finite set;
    take card(X);
    take X;
    thus thesis by A12;
  end;
  uniqueness
  proof
    let n1, n2 be Element of NAT such that
A15: ex X being finite set st (for z being set holds
    z in X iff z in the carrier' of G & (the Source of G).z = x)
    & n1 = card(X) and
A16: ex X being finite set st (for z being set holds
    z in X iff z in the carrier' of G & (the Source of G).z = x)
    & n2 = card(X);
    consider X1 being finite set such that
A17: for z being set holds z in X1 iff z in the carrier' of G & (the Source
    of G).z = x and
A18: n1 = card(X1) by A15;
    consider X2 being finite set such that
A19: for z being set holds z in X2 iff z in the carrier' of G & (the Source
    of G).z = x and
A20: n2 = card(X2) by A16;
 for z being set holds z in X1 iff z in X2
    proof
      let z;
  z in X1 iff z in the carrier' of G & (the Source of G).z = x by A17;
      hence thesis by A19;
    end;
    hence thesis by A18,A20,TARSKI:2;
  end;
end;

definition
  let G be finite Graph;
  let x be Vertex of G;
  func Degree x -> Element of NAT equals
  EdgesIn(x) + EdgesOut(x);
  correctness;
end;

Lm2: for p being Chain of G holds p|Seg(n) is Chain of G
proof
  let p be Chain of G;
  reconsider q = p|Seg(n) as FinSequence by FINSEQ_1:19;
A1: for n st 1 <= n & n <= len q holds q.n in the carrier' of G
  proof
    let k be Element of NAT;
    assume that
A2: 1 <= k and
A3: k <= len q;
A4: k in dom q by A2,A3,FINSEQ_3:27;
 dom q c= dom p by RELAT_1:89;
then  k <= len p by A4,FINSEQ_3:27;
then  p.k in the carrier' of G by A2,Def12;
    hence thesis by A4,FUNCT_1:70;
  end;
 ex q1 being FinSequence st len q1 = len q + 1 &
  (for n st 1 <= n & n <= len q1 holds q1.n in the carrier of G) &
  for n st 1 <= n & n <= len q ex x9, y9 being Vertex of G st
  x9 = q1.n & y9 = q1.(n+1) & q.n joins x9, y9
  proof
    consider q1 being FinSequence such that
A9: len q1 = len p + 1 and
A10: for n st 1 <= n & n <= len q1 holds q1.n in the carrier of G and
A11: for n st 1 <= n & n <= len p ex x9, y9 being Vertex of G st x9 = q1.
    n & y9 = q1.(n+1) & p.n joins x9, y9
    by Def12;
    reconsider q2 = q1|Seg(n+1) as FinSequence by FINSEQ_1:19;
    take q2;
    thus
A12: len q2 = len q + 1
    proof
A13:  dom q2 = dom q1 /\ Seg(n+1) by RELAT_1:90
        .= Seg (len p + 1) /\ Seg (n+1) by A9,FINSEQ_1:def 3;
A14:  dom q = dom p /\ Seg n by RELAT_1:90
        .= Seg len p /\ Seg n by FINSEQ_1:def 3;
A15:  now
        assume
A16:    len p + 1 <= n + 1;
then     Seg(len p + 1) c= Seg(n + 1) by FINSEQ_1:7;
then     dom q2 = Seg(len p + 1) by A13,XBOOLE_1:28;
then A19:    len q2 = len p + 1 by FINSEQ_1:def 3;
    len p <= n by A16,XREAL_1:8;
then     Seg len p c= Seg n by FINSEQ_1:7;
then     dom q = Seg len p by A14,XBOOLE_1:28;
        hence thesis by A19,FINSEQ_1:def 3;
      end;
  now
        assume
A24:    n + 1 <= len p + 1;
then     Seg(n+1) c= Seg(len p + 1) by FINSEQ_1:7;
then     dom q2 = Seg(n+1) by A13,XBOOLE_1:28;
then A27:    len q2 = n+1 by FINSEQ_1:def 3;
    n <= len p by A24,XREAL_1:8;
then     Seg n c= Seg len p by FINSEQ_1:7;
then     dom q = Seg n by A14,XBOOLE_1:28;
        hence thesis by A27,FINSEQ_1:def 3;
      end;
      hence thesis by A15;
    end;
    thus for n st 1 <= n & n <= len q2 holds q2.n in the carrier of G
    proof
      let n be Element of NAT;
      assume that
A31:  1 <= n and
A32:  n <= len q2;
A33:  n in dom q2 by A31,A32,FINSEQ_3:27;
  dom q2 c= dom q1 by RELAT_1:89;
then   n <= len q1 by A33,FINSEQ_3:27;
then   q1.n in the carrier of G by A10,A31;
      hence thesis by A33,FUNCT_1:70;
    end;
    let k;
    assume that
A37: 1 <= k and
A38: k <= len q;
A39: k in dom q by A37,A38,FINSEQ_3:27;
 dom q c= dom p by RELAT_1:89;
then  k <= len p by A39,FINSEQ_3:27;
    then consider x9, y9 being Vertex of G such that
A42: x9 = q1.k and
A43: y9 = q1.(k+1) and
A44: p.k joins x9,y9 by A11,A37;
    take x9, y9;
 len q <= len q + 1 by NAT_1:11;
then  k <= len q2 by A12,A38,XXREAL_0:2;
then  k in dom q2 by A37,FINSEQ_3:27;
    hence x9 = q2.k by A42,FUNCT_1:70;
A48: k + 1 <= len q2 by A12,A38,XREAL_1:9;
 1 + 1 <= k + 1 by A37,XREAL_1:9;
then  1 <= k + 1 by XXREAL_0:2;
then  k+1 in dom q2 by A48,FINSEQ_3:27;
    hence y9 = q2.(k+1) by A43,FUNCT_1:70;
    thus thesis by A39,A44,FUNCT_1:70;
  end;
  hence thesis by A1,Def12;
end;

Lm3: for H1, H2 being strict Subgraph of G st
the carrier of H1 = the carrier of H2 & the carrier' of H1 = the carrier' of H2
holds H1 = H2
proof
  let H1, H2 be strict Subgraph of G such that
A1: the carrier of H1 = the carrier of H2 and
A2: the carrier' of H1 = the carrier' of H2;
  A3: dom(the Source of H1) = the carrier' of H1 & dom(the Source of H2) = the
  carrier' of H2 by FUNCT_2:def 1;
  A4: dom(the Target of H1) = the carrier' of H1 & dom(the Target of H2) = the
  carrier' of H2 by FUNCT_2:def 1;
 for x being set st x in the carrier' of H1 holds
  (the Source of H1).x = (the Source of H2).x
  proof
    let x be set;
    assume
A6: x in the carrier' of H1;
    hence (the Source of H1).x = (the Source of G).x by Def18
      .= (the Source of H2).x by A2,A6,Def18;
  end;
then A7: the Source of H1 = the Source of H2 by A2,A3,FUNCT_1:9;
 for x being set st x in the carrier' of H1 holds
  (the Target of H1).x = (the Target of H2).x
  proof
    let x be set;
    assume
A9: x in the carrier' of H1;
    hence (the Target of H1).x = (the Target of G).x by Def18
      .= (the Target of H2).x by A2,A9,Def18;
  end;
  hence thesis by A1,A2,A4,A7,FUNCT_1:9;
end;

definition
  let G1, G2 be Graph;
  pred G1 c= G2 means
  :Def24:
  G1 is Subgraph of G2;
  reflexivity
  proof
    let G;
 for v st v in the carrier' of G holds
    (the Source of G).v = (the Source of G).v &
    (the Target of G).v = (the Target of G).v &
    (the Source of G).v in the carrier of G &
    (the Target of G).v in the carrier of G by FUNCT_2:7;
    hence thesis by Def18;
  end;
end;

Lm4: for G being Graph, H being Subgraph of G holds
(the Source of H) in PFuncs(the carrier' of G, the carrier of G) &
(the Target of H) in PFuncs(the carrier' of G, the carrier of G)
proof
  let G be Graph, H be Subgraph of G;
  set EH = the carrier' of H;
  set VH = the carrier of H;
  set EG = the carrier' of G;
  set VG = the carrier of G;
 EH c= EG & VH c= VG by Def18;
  then
 Funcs(EH, VH) c= PFuncs(EH, VH) & PFuncs(EH, VH) c= PFuncs(EG, VG) by
  FUNCT_2:141,PARTFUN1:128;
then A3: Funcs(EH, VH) c= PFuncs(EG, VG) by XBOOLE_1:1;
 (the Source of H) in Funcs(EH, VH) by FUNCT_2:11;
  hence (the Source of H) in PFuncs(EG, VG) by A3;
 (the Target of H) in Funcs(EH, VH) by FUNCT_2:11;
  hence thesis by A3;
end;

definition
  let G be Graph;
  func bool G -> set means
  :Def25:
  for x being set holds x in it iff x is strict Subgraph of G;
  existence
  proof
    reconsider V = the carrier of G as non empty set;
    set E = the carrier' of G;
    set Prod = [: bool V, bool E, PFuncs(E, V), PFuncs(E, V) :];
    defpred P[set] means ex y being Element of Prod, M being Graph,
    v being non empty set, e being set, s being (Function of e, v),
    t being (Function of e, v)
    st y = $1 & v = y`1 & e = y`2 & s = y`3 & t = y`4 &
    M = MultiGraphStruct(# v, e, s, t #) & M is Subgraph of G;
    consider X being set such that
A1: for x being set holds x in X iff x in Prod & P[x] from XBOOLE_0:sch 1;
    defpred P[set, set] means ex y being Element of Prod,
    M being strict Graph, v being non empty set, e being set,
    s being Function of e, v, t being Function of e, v
    st y = $1 & v = y`1 & e = y`2 & s = y`3 & t = y`4 &
    M = MultiGraphStruct(# v, e, s, t #) & M is Subgraph of G & $2 = M;
A2: for a, b, c being set st P[a,b] & P[a, c] holds b = c;
    consider Y being set such that
A3: for z being set holds z in Y iff
    ex x being set st x in X & P[x, z] from TARSKI:sch 1(A2);
    take Y;
    let x be set;
    thus x in Y implies x is strict Subgraph of G
    proof
      assume x in Y;
then    ex z being set st z in X & P[z, x] by A3;
      hence thesis;
    end;
    assume x is strict Subgraph of G;
    then reconsider H = x as strict Subgraph of G;
 ex y being set st y in X & P[y, x]
    proof
      take y = [ the carrier of H, the carrier' of H,
      the Source of H, the Target of H ];
      A8:   (the carrier of H) c= V & (the carrier' of H) c= E by Def18;
         (
the Source of H) in PFuncs(E, V) & (the Target of H) in PFuncs(E, V) by
      Lm4;
      then reconsider y9 = y as Element of Prod by A8,MCART_1:84;
      reconsider v = the carrier of H as non empty set;
      set e = the carrier' of H;
      reconsider s = the Source of H as Function of e, v;
      reconsider t = the Target of H as Function of e, v;
A10:  v = y9`1 & e = y9`2 by MCART_1:59;
A11:  s = y9`3 & t = y9`4 by MCART_1:59;
      hence y in X by A1,A10;
      thus thesis by A10,A11;
    end;
    hence thesis by A3;
  end;
  uniqueness
  proof
    defpred P[set] means $1 is strict Subgraph of G;
    let X1, X2 be set such that
A12: for x being set holds x in X1 iff P[x] and
A13: for x being set holds x in X2 iff P[x];
    thus X1 = X2 from XBOOLE_0:sch 2(A12,A13);
  end;
end;

scheme GraphSeparation{G() -> Graph, P[set]}: ex X being set st
  for x being set holds x in X iff x is strict Subgraph of G() & P[x]
proof
  consider X being set such that
  A1: for
 x being set holds x in X iff x in bool G() & P[x] from XBOOLE_0:sch 1;
  take X;
  let x be set;
  thus x in X implies x is strict Subgraph of G() & P[x]
  proof
    assume
A2: x in X;
then  x in bool G() by A1;
    hence x is strict Subgraph of G() by Def25;
    thus thesis by A1,A2;
  end;
  assume that
A4: x is strict Subgraph of G() and
A5: P[x];
 x in bool G() by A4,Def25;
  hence thesis by A1,A5;
end;
 
:: Properties of graphs ::

theorem
  for G being Graph holds dom(the Source of G) = the carrier' of G &
  dom(the Target of G) = the carrier' of G by FUNCT_2:def 1;

theorem
  for x being Vertex of G holds x in the carrier of G;

theorem
  for v being set holds v in the carrier' of G implies
  (the Source of G).v in the carrier of G &
  (the Target of G).v in the carrier of G by FUNCT_2:7;
 
:: Chain ::

theorem
  for p being Chain of G holds p|Seg(n) is Chain of G by Lm2;
 
:: Sum of two graphs ::

theorem Th5:
  G1 c= G implies (the Source of G1) c= (the Source of G) &
  (the Target of G1) c= (the Target of G)
proof
  assume G1 c= G;
then A2: G1 is Subgraph of G by Def24;
 for v st v in (the Source of G1) holds v in (the Source of G)
  proof
    let v;
    assume
A4: v in (the Source of G1);
    then consider x, y being set such that
A5: [x,y] = v by RELAT_1:def 1;
A6: x in dom (the Source of G1) by A4,A5,FUNCT_1:8;
A7: y = (the Source of G1).x by A4,A5,FUNCT_1:8;
A8: x in the carrier' of G1 by A6;
 (the carrier' of G1) c= (the carrier' of G) by A2,Def18;
then  x in the carrier' of G by A8;
then A11: x in dom (the Source of G) by FUNCT_2:def 1;
 y = (the Source of G).x by A2,A6,A7,Def18;
    hence thesis by A5,A11,FUNCT_1:def 4;
  end;
  hence (the Source of G1) c= (the Source of G) by TARSKI:def 3;
 for v st v in (the Target of G1) holds v in (the Target of G)
  proof
    let v;
    assume
A14: v in (the Target of G1);
    then consider x, y being set such that
A15: [x,y] = v by RELAT_1:def 1;
A16: x in dom (the Target of G1) by A14,A15,FUNCT_1:8;
A17: y = (the Target of G1).x by A14,A15,FUNCT_1:8;
A18: x in the carrier' of G1 by A16;
 (the carrier' of G1) c= (the carrier' of G) by A2,Def18;
then  x in the carrier' of G by A18;
then A21: x in dom (the Target of G) by FUNCT_2:def 1;
 y = (the Target of G).x by A2,A16,A17,Def18;
    hence thesis by A15,A21,FUNCT_1:def 4;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem
  (the Source of G1) tolerates (the Source of G2) &
  (the Target of G1) tolerates (the Target of G2) implies
  (the Source of (G1 \/ G2)) = (the Source of G1) \/ (the Source of G2) &
  (the Target of (G1 \/ G2)) = (the Target of G1) \/ (the Target of G2)
proof
  assume
  A1: (the Source of G1) tolerates (the Source of G2) & (the Target of G1)
  tolerates (the Target of G2);
  set S12 = the Source of (G1 \/ G2);
  set S1 = the Source of G1;
  set S2 = the Source of G2;
  set T12 = the Target of (G1 \/ G2);
  set T1 = the Target of G1;
  set T2 = the Target of G2;
 for v holds v in S12 iff v in S1 \/ S2
  proof
    let v;
    thus v in S12 implies v in S1 \/ S2
    proof
      assume
A3:   v in S12;
      then consider x,y being set such that
A4:   [x,y] = v by RELAT_1:def 1;
   x in dom S12 by A3,A4,FUNCT_1:8;
then    x in the carrier' of (G1 \/ G2);
then A7:   x in (the carrier' of G1) \/ (the carrier' of G2) by A1,Def3;
A8:   now
        assume
A9:     x in the carrier' of G1;
then A10:    x in dom S1 by FUNCT_2:def 1;
    S1.x = S12.x by A1,A9,Def3
          .= y by A3,A4,FUNCT_1:8;
then     [x,y] in S1 or [x,y] in S2 by A10,FUNCT_1:def 4;
        hence [x,y] in S1 \/ S2 by XBOOLE_0:def 3;
      end;
  now
        assume
A14:    x in the carrier' of G2;
then A15:    x in dom S2 by FUNCT_2:def 1;
    S2.x = S12.x by A1,A14,Def3
          .= y by A3,A4,FUNCT_1:8;
then     [x,y] in S1 or [x,y] in S2 by A15,FUNCT_1:def 4;
        hence [x,y] in S1 \/ S2 by XBOOLE_0:def 3;
      end;
      hence thesis by A4,A7,A8,XBOOLE_0:def 3;
    end;
    assume
A18: v in S1 \/ S2;
A19: now
      assume
A20:  v in S1;
      then consider x,y being set such that
A21:  [x,y] = v by RELAT_1:def 1;
A22:  x in dom S1 by A20,A21,FUNCT_1:8;
A23:  y = S1.x by A20,A21,FUNCT_1:8;
  x in (the carrier' of G1) \/ (the carrier' of G2) by A22,
      XBOOLE_0:def 3;
then   x in the carrier' of (G1 \/ G2) by A1,Def3;
then A26:  x in dom S12 by FUNCT_2:def 1;
  S12.x = y by A1,A22,A23,Def3;
      hence thesis by A21,A26,FUNCT_1:def 4;
    end;
 now
      assume
A29:  v in S2;
      then consider x,y being set such that
A30:  [x,y] = v by RELAT_1:def 1;
A31:  x in dom S2 by A29,A30,FUNCT_1:8;
A32:  y = S2.x by A29,A30,FUNCT_1:8;
  x in (the carrier' of G1) \/ (the carrier' of G2) by A31,
      XBOOLE_0:def 3;
then   x in the carrier' of (G1 \/ G2) by A1,Def3;
then A35:  x in dom S12 by FUNCT_2:def 1;
  S12.x = y by A1,A31,A32,Def3;
      hence thesis by A30,A35,FUNCT_1:def 4;
    end;
    hence thesis by A18,A19,XBOOLE_0:def 3;
  end;
  hence S12 = S1 \/ S2 by TARSKI:2;
 for v holds v in T12 iff v in T1 \/ T2
  proof
    let v;
    thus v in T12 implies v in T1 \/ T2
    proof
      assume
A38:  v in T12;
      then consider x,y being set such that
A39:  [x,y] = v by RELAT_1:def 1;
  x in dom T12 by A38,A39,FUNCT_1:8;
then   x in the carrier' of (G1 \/ G2);
then A42:  x in (the carrier' of G1) \/ (the carrier' of G2) by A1,Def3;
A43:  now
        assume
A44:    x in the carrier' of G1;
then A45:    x in dom T1 by FUNCT_2:def 1;
    T1.x = T12.x by A1,A44,Def3
          .= y by A38,A39,FUNCT_1:8;
then     [x,y] in T1 or [x,y] in T2 by A45,FUNCT_1:def 4;
        hence [x,y] in T1 \/ T2 by XBOOLE_0:def 3;
      end;
  now
        assume
A49:    x in the carrier' of G2;
then A50:    x in dom T2 by FUNCT_2:def 1;
    T2.x = T12.x by A1,A49,Def3
          .= y by A38,A39,FUNCT_1:8;
then     [x,y] in T1 or [x,y] in T2 by A50,FUNCT_1:def 4;
        hence [x,y] in T1 \/ T2 by XBOOLE_0:def 3;
      end;
      hence thesis by A39,A42,A43,XBOOLE_0:def 3;
    end;
    assume
A53: v in T1 \/ T2;
A54: now
      assume
A55:  v in T1;
      then consider x,y being set such that
A56:  [x,y] = v by RELAT_1:def 1;
A57:  x in dom T1 by A55,A56,FUNCT_1:8;
A58:  y = T1.x by A55,A56,FUNCT_1:8;
  x in (the carrier' of G1) \/ (the carrier' of G2) by A57,
      XBOOLE_0:def 3;
then   x in the carrier' of (G1 \/ G2) by A1,Def3;
then A61:  x in dom T12 by FUNCT_2:def 1;
  T12.x = y by A1,A57,A58,Def3;
      hence thesis by A56,A61,FUNCT_1:def 4;
    end;
 now
      assume
A64:  v in T2;
      then consider x,y being set such that
A65:  [x,y] = v by RELAT_1:def 1;
A66:  x in dom T2 by A64,A65,FUNCT_1:8;
A67:  y = T2.x by A64,A65,FUNCT_1:8;
  x in (the carrier' of G1) \/ (the carrier' of G2) by A66,
      XBOOLE_0:def 3;
then   x in the carrier' of (G1 \/ G2) by A1,Def3;
then A70:  x in dom T12 by FUNCT_2:def 1;
  T12.x = y by A1,A66,A67,Def3;
      hence thesis by A65,A70,FUNCT_1:def 4;
    end;
    hence thesis by A53,A54,XBOOLE_0:def 3;
  end;
  hence thesis by TARSKI:2;
end;

theorem Th7:
  for G being strict Graph holds G = G \/ G
proof
  let G be strict Graph;
A1: (the carrier of (G \/ G))
  = (the carrier of G) \/ (the carrier of G) by Def3
    .= (the carrier of G);
A2: (the carrier' of (G \/ G)) = (the carrier' of G) \/ (the carrier' of G)
  by Def3
    .= (the carrier' of G);
then A3: dom (the Source of G) = the carrier' of (G \/ G) by FUNCT_2:def 1
    .= dom (the Source of (G \/ G)) by FUNCT_2:def 1;
 for v st v in dom (the Source of G) holds
  (the Source of G).v = (the Source of (G \/ G)).v by Def3;
then A5: (the Source of G) = (the Source of (G \/ G)) by A3,FUNCT_1:9;
A6: dom(the Target of G) = the carrier' of (G \/ G) by A2,FUNCT_2:def 1
    .= dom (the Target of (G \/ G)) by FUNCT_2:def 1;
 for v st v in dom (the Target of G) holds
  (the Target of G).v = (the Target of (G \/ G)).v by Def3;
  hence thesis by A1,A2,A5,A6,FUNCT_1:9;
end;

theorem Th8:
  (the Source of G1) tolerates (the Source of G2) &
  (the Target of G1) tolerates (the Target of G2) implies G1 \/ G2 = G2 \/ G1
proof
  assume
  A1: (the Source of G1) tolerates (the Source of G2) & (the Target of G1)
  tolerates (the Target of G2);
then A2: (the carrier of (G1 \/ G2)) = (the carrier of G2) \/ (the carrier
  of G1) by Def3
    .= (the carrier of (G2 \/ G1)) by A1,Def3;
A3: (the carrier' of (G1 \/ G2)) =
  (the carrier' of G2) \/ (the carrier' of G1) by A1,Def3
    .= (the carrier' of (G2 \/ G1)) by A1,Def3;
then A4: dom (the Source of (G1 \/ G2)) = the carrier' of (G2 \/
  G1) by FUNCT_2:def 1
    .= dom (the Source of (G2 \/ G1)) by FUNCT_2:def 1;
 for v st v in dom (the Source of (G1 \/ G2)) holds
  (the Source of (G1 \/ G2)).v = (the Source of (G2 \/ G1)).v
  proof
    let v;
    assume v in dom (the Source of (G1 \/ G2));
then  v in the carrier' of (G1 \/ G2);
then A8: v in (the carrier' of G1) \/ (the carrier' of G2) by A1,Def3;
A9: now
      assume
A10:  v in the carrier' of G1;
      hence
(the Source of (G1 \/ G2)).v = (the Source of G1).v by A1,Def3
        .= (the Source of (G2 \/ G1)).v by A1,A10,Def3;
    end;
 now
      assume
A12:  v in the carrier' of G2;
      hence
(the Source of (G1 \/ G2)).v = (the Source of G2).v by A1,Def3
        .= (the Source of (G2 \/ G1)).v by A1,A12,Def3;
    end;
    hence thesis by A8,A9,XBOOLE_0:def 3;
  end;
then
A13: the Source of (G1 \/ G2) = the Source of (G2 \/ G1) by A4,FUNCT_1:9;
A14: dom ( the Target of (G1 \/ G2))
  = the carrier' of (G2 \/ G1) by A3,FUNCT_2:def 1
    .= dom (the Target of (G2 \/ G1)) by FUNCT_2:def 1;
 for v st v in dom (the Target of (G1 \/ G2)) holds
  (the Target of (G1 \/ G2)).v = (the Target of (G2 \/ G1)).v
  proof
    let v;
    assume v in dom ( the Target of (G1 \/ G2));
then  v in the carrier' of (G1 \/ G2);
then A18: v in (the carrier' of G1) \/ (the carrier' of G2) by A1,Def3;
A19: now
      assume
A20:  v in the carrier' of G1;
      hence
(the Target of (G1 \/ G2)).v = (the Target of G1).v by A1,Def3
        .= (the Target of (G2 \/ G1)).v by A1,A20,Def3;
    end;
 now
      assume
A22:  v in the carrier' of G2;
      hence
(the Target of (G1 \/ G2)).v = (the Target of G2).v by A1,Def3
        .= (the Target of (G2 \/ G1)).v by A1,A22,Def3;
    end;
    hence thesis by A18,A19,XBOOLE_0:def 3;
  end;
  hence thesis by A2,A3,A13,A14,FUNCT_1:9;
end;

theorem Th9:
  (the Source of G1) tolerates (the Source of G2) &
  (the Target of G1) tolerates (the Target of G2) &
  (the Source of G1) tolerates (the Source of G3) &
  (the Target of G1) tolerates (the Target of G3) &
  (the Source of G2) tolerates (the Source of G3) &
  (the Target of G2) tolerates (the Target of G3) implies
  (G1 \/ G2) \/ G3 = G1 \/ (G2 \/ G3)
proof
  assume that
A1: (the Source of G1) tolerates (the Source of G2) and
A2: (the Target of G1) tolerates (the Target of G2) and
A3: (the Source of G1) tolerates (the Source of G3) and
A4: (the Target of G1) tolerates (the Target of G3) and
A5: (the Source of G2) tolerates (the Source of G3) and
A6: (the Target of G2) tolerates (the Target of G3);
  A7: for
 v st v in dom(the Source of (G1 \/ G2)) /\ dom(the Source of G3) holds
  (the Source of (G1 \/ G2)).v = (the Source of G3).v
  proof
    let v;
    assume
A8: v in dom (the Source of (G1 \/ G2)) /\ dom (the Source of G3);
then A9: v in dom (the Source of G3) by XBOOLE_0:def 4;
 v in dom (the Source of (G1 \/ G2)) by A8,XBOOLE_0:def 4;
then  v in (the carrier' of (G1 \/ G2));
then A12: v in (the carrier' of G1) \/ (the carrier' of G2) by A1,A2,Def3;
A13: now
      assume
A14:  v in (the carrier' of G1);
then   v in dom (the Source of G1) by FUNCT_2:def 1;
then A16:  v in dom (the Source of G1) /\ dom (the Source of G3) by A9,
      XBOOLE_0:def 4;
      thus (the Source of (G1 \/ G2)).v = (the Source of G1).v by A1,A2,A14,
      Def3
        .= (the Source of G3).v by A3,A16,PARTFUN1:def 6;
    end;
 now
      assume
A18:  v in (the carrier' of G2);
then   v in dom (the Source of G2) by FUNCT_2:def 1;
then A20:  v in dom (the Source of G2) /\ dom (the Source of G3) by A9,
      XBOOLE_0:def 4;
      thus (the Source of (G1 \/ G2)).v = (the Source of G2).v by A1,A2,A18,
      Def3
        .= (the Source of G3).v by A5,A20,PARTFUN1:def 6;
    end;
    hence thesis by A12,A13,XBOOLE_0:def 3;
  end;
  A21: for
 v st v in dom(the Target of (G1 \/ G2)) /\ dom(the Target of G3) holds
  (the Target of (G1 \/ G2)).v = (the Target of G3).v
  proof
    let v;
    assume
A22: v in dom (the Target of (G1 \/ G2)) /\ dom (the Target of G3);
then A23: v in dom (the Target of G3) by XBOOLE_0:def 4;
 v in dom (the Target of (G1 \/ G2)) by A22,XBOOLE_0:def 4;
then  v in (the carrier' of (G1 \/ G2));
then A26: v in (the carrier' of G1) \/ (the carrier' of G2) by A1,A2,Def3;
A27: now
      assume
A28:  v in (the carrier' of G1);
then   v in dom (the Target of G1) by FUNCT_2:def 1;
then A30:  v in dom (the Target of G1) /\ dom (the Target of G3) by A23,
      XBOOLE_0:def 4;
      thus (the Target of (G1 \/ G2)).v = (the Target of G1).v by A1,A2,A28,
      Def3
        .= (the Target of G3).v by A4,A30,PARTFUN1:def 6;
    end;
 now
      assume
A32:  v in (the carrier' of G2);
then   v in dom (the Target of G2) by FUNCT_2:def 1;
then A34:  v in dom (the Target of G2) /\ dom (the Target of G3) by A23,
      XBOOLE_0:def 4;
      thus (the Target of (G1 \/ G2)).v = (the Target of G2).v by A1,A2,A32,
      Def3
        .= (the Target of G3).v by A6,A34,PARTFUN1:def 6;
    end;
    hence thesis by A26,A27,XBOOLE_0:def 3;
  end;
A35: (the Source of (G1 \/ G2)) tolerates (the Source of G3) by A7,
  PARTFUN1:def 6;
A36: (the Target of (G1 \/ G2)) tolerates (the Target of G3) by A21,
  PARTFUN1:def 6;
A37: for
  v st v in dom (the Source of G1) /\ dom(the Source of (G2 \/ G3)) holds
  (the Source of G1).v = (the Source of (G2 \/ G3)).v
  proof
    let v;
    assume
A38: v in dom (the Source of G1) /\ dom(the Source of (G2 \/ G3));
then A39: v in dom(the Source of G1) by XBOOLE_0:def 4;
 v in (the carrier' of (G2 \/ G3)) by A38;
then  v in (the carrier' of G2) \/ (the carrier' of G3) by A5,A6,Def3;
then  v in (the carrier' of G2) \/ dom(the Source of G3) by
    FUNCT_2:def 1;
then A43: v in dom(the Source of G2) \/ dom (the Source of G3) by
    FUNCT_2:def 1;
A44: now
      assume
A45:  v in dom (the Source of G2);
then A46:  v in dom(the Source of G1) /\
      dom(the Source of G2) by A39,XBOOLE_0:def 4;
      thus (the Source of (G2 \/ G3)).v = (the Source of G2).v by A5,A6,A45,
      Def3
        .= (the Source of G1).v by A1,A46,PARTFUN1:def 6;
    end;
 now
      assume
A48:  v in dom (the Source of G3);
then A49:  v in dom(the Source of G1) /\
      dom(the Source of G3) by A39,XBOOLE_0:def 4;
      thus (the Source of (G2 \/ G3)).v = (the Source of G3).v by A5,A6,A48,
      Def3
        .= (the Source of G1).v by A3,A49,PARTFUN1:def 6;
    end;
    hence thesis by A43,A44,XBOOLE_0:def 3;
  end;
  A50: for
 v st v in dom (the Target of G1) /\ dom (the Target of (G2 \/ G3)) holds
  (the Target of G1).v = (the Target of (G2 \/ G3)).v
  proof
    let v;
    assume
A51: v in dom (the Target of G1) /\ dom(the Target of (G2 \/ G3));
then A52: v in dom(the Target of G1) by XBOOLE_0:def 4;
 v in (the carrier' of (G2 \/ G3)) by A51;
then  v in (the carrier' of G2) \/ (the carrier' of G3) by A5,A6,Def3;
then  v in (the carrier' of G2) \/ dom(the Target of G3) by
    FUNCT_2:def 1;
then A56: v in dom(the Target of G2) \/ dom (the Target of G3) by
    FUNCT_2:def 1;
A57: now
      assume
A58:  v in dom (the Target of G2);
then A59:  v in dom(the Target of G1) /\
      dom(the Target of G2) by A52,XBOOLE_0:def 4;
      thus (the Target of (G2 \/ G3)).v = (the Target of G2).v by A5,A6,A58,
      Def3
        .= (the Target of G1).v by A2,A59,PARTFUN1:def 6;
    end;
 now
      assume
A61:  v in dom(the Target of G3);
then A62:  v in dom(the Target of G1) /\
      dom(the Target of G3) by A52,XBOOLE_0:def 4;
      thus (the Target of (G2 \/ G3)).v = (the Target of G3).v by A5,A6,A61,
      Def3
        .= (the Target of G1).v by A4,A62,PARTFUN1:def 6;
    end;
    hence thesis by A56,A57,XBOOLE_0:def 3;
  end;
A63: (the Source of G1) tolerates (the Source of (G2 \/ G3)) by A37,
  PARTFUN1:def 6;
A64: (the Target of G1) tolerates (the Target of (G2 \/ G3)) by A50,
  PARTFUN1:def 6;
A65: the carrier' of ((G1 \/ G2) \/ G3) =
  (the carrier' of (G1 \/ G2)) \/ (the carrier' of G3) by A35,A36,Def3
    .= (the carrier' of G1) \/ (the carrier' of G2) \/ (the carrier' of G3)
  by A1,A2,Def3
    .= (the carrier' of G1) \/ ((the carrier' of G2) \/
  (the carrier' of G3)) by XBOOLE_1:4
    .= (the carrier' of G1) \/ (the carrier' of (G2 \/ G3)) by A5,A6,Def3
    .= (the carrier' of (G1 \/ (G2 \/ G3))) by A63,A64,Def3;
A66: the carrier of ((G1 \/ G2) \/ G3) =
  (the carrier of (G1 \/ G2)) \/ (the carrier of G3) by A35,A36,Def3
    .= (the carrier of G1) \/ (the carrier of G2) \/ (the carrier of G3)
  by A1,A2,Def3
    .= (the carrier of G1) \/
  ((the carrier of G2) \/ (the carrier of G3)) by XBOOLE_1:4
    .= (the carrier of G1) \/ (the carrier of (G2 \/ G3)) by A5,A6,Def3
    .= (the carrier of (G1 \/ (G2 \/ G3))) by A63,A64,Def3;
A67: dom (the Source of ((G1 \/ G2) \/ G3)) =the carrier' of (G1 \/ (G2 \/
  G3)) by A65,FUNCT_2:def 1
    .= dom (the Source of (G1 \/ (G2 \/ G3))) by FUNCT_2:def 1;
 for v st v in dom (the Source of ((G1 \/ G2) \/ G3)) holds
  (the Source of ((G1 \/ G2) \/ G3)).v = (the Source of (G1 \/ (G2 \/ G3))).v
  proof
    let v such that
A69: v in dom (the Source of ((G1 \/ G2) \/ G3));
 dom (the Source of ((G1 \/ G2) \/ G3)) =
    the carrier' of ((G1 \/ G2) \/ G3) by FUNCT_2:def 1
      .= (the carrier' of (G1 \/ G2)) \/ (the carrier' of G3) by A35,A36,Def3
      .= (the carrier' of G1) \/ (the carrier' of G2) \/ (the carrier' of G3)
    by A1,A2,Def3;
then A71: v in (the carrier' of G1) \/ (the carrier' of G2) or
    v in (the carrier' of G3) by A69,XBOOLE_0:def 3;
A72: now
      assume
A73:  v in the carrier' of G1;
        (
the carrier' of G1) c= (the carrier' of G1) \/ (the carrier' of G2)
      by XBOOLE_1:7;
then
  (the carrier' of G1) c= the carrier' of (G1 \/ G2) by A1,A2,Def3;
      hence (the Source of ((G1 \/ G2) \/ G3)).v =
      (the Source of (G1 \/ G2)).v by A35,A36,A73,Def3
        .= (the Source of G1).v by A1,A2,A73,Def3
        .= (the Source of (G1 \/ (G2 \/ G3))).v by A63,A64,A73,Def3;
    end;
A76: now
      assume
A77:  v in the carrier' of G2;
        (
the carrier' of G2) c= (the carrier' of G1) \/ (the carrier' of G2)
      by XBOOLE_1:7;
then A79:  (the carrier' of G2) c= the carrier' of (G1 \/ G2) by A1,A2,
      Def3;
        (
the carrier' of G2) c= (the carrier' of G2) \/ (the carrier' of G3)
      by XBOOLE_1:7;
then A81:  (the carrier' of G2) c= the carrier' of (G2 \/ G3) by A5,A6,
      Def3;
      thus (the Source of ((G1 \/ G2) \/ G3)).v =
      (the Source of (G1 \/ G2)).v by A35,A36,A77,A79,Def3
        .= (the Source of G2).v by A1,A2,A77,Def3
        .= (the Source of (G2 \/ G3)).v by A5,A6,A77,Def3
        .= (the Source of (G1 \/ (G2 \/ G3))).v by A63,A64,A77,A81,Def3;
    end;
 now
      assume
A83:  v in the carrier' of G3;
        (
the carrier' of G3) c= (the carrier' of G2) \/ (the carrier' of G3)
      by XBOOLE_1:7;
then A85:  (the carrier' of G3) c= the carrier' of (G2 \/ G3) by A5,A6,
      Def3;
      thus (the Source of ((G1 \/ G2) \/ G3)).v =
      (the Source of G3).v by A35,A36,A83,Def3
        .= (the Source of (G2 \/ G3)).v by A5,A6,A83,Def3
        .= (the Source of (G1 \/ (G2 \/ G3))).v by A63,A64,A83,A85,Def3;
    end;
    hence thesis by A71,A72,A76,XBOOLE_0:def 3;
  end;
  then
A86: (the Source of ((G1 \/ G2) \/ G3)) = (the Source of (G1 \/ (G2 \/ G3 )))
  by A67,FUNCT_1:9;
A87: dom (the Target of ((G1 \/ G2) \/ G3)) =the carrier' of (G1 \/ (G2 \/
  G3)) by A65,FUNCT_2:def 1
    .= dom (the Target of (G1 \/ (G2 \/ G3))) by FUNCT_2:def 1;
 for v st v in dom (the Target of ((G1 \/ G2) \/ G3)) holds
  (the Target of ((G1 \/ G2) \/ G3)).v = (the Target of (G1 \/ (G2 \/ G3))).v
  proof
    let v such that
A89: v in dom (the Target of ((G1 \/ G2) \/ G3));
 dom (the Target of ((G1 \/ G2) \/ G3)) =
    the carrier' of ((G1 \/ G2) \/ G3) by FUNCT_2:def 1
      .= (the carrier' of (G1 \/ G2)) \/ (the carrier' of G3) by A35,A36,Def3
      .= (the carrier' of G1) \/ (the carrier' of G2) \/ (the carrier' of G3)
    by A1,A2,Def3;
then A91: v in (the carrier' of G1) \/ (the carrier' of G2) or
    v in (the carrier' of G3) by A89,XBOOLE_0:def 3;
A92: now
      assume
A93:  v in the carrier' of G1;
        (
the carrier' of G1) c= (the carrier' of G1) \/ (the carrier' of G2)
      by XBOOLE_1:7;
then   (the carrier' of G1) c= the carrier' of (G1 \/ G2) by A1,A2,
      Def3;
      hence (the Target of ((G1 \/ G2) \/ G3)).v =
      (the Target of (G1 \/ G2)).v by A35,A36,A93,Def3
        .= (the Target of G1).v by A1,A2,A93,Def3
        .= (the Target of (G1 \/ (G2 \/ G3))).v by A63,A64,A93,Def3;
    end;
A96: now
      assume
A97:  v in the carrier' of G2;
        (
the carrier' of G2) c= (the carrier' of G1) \/ (the carrier' of G2)
      by XBOOLE_1:7;
then A99:  (the carrier' of G2) c= the carrier' of (G1 \/ G2) by A1,A2,
      Def3;
       (
the carrier' of G2) c= (the carrier' of G2) \/ (the carrier' of G3)
      by XBOOLE_1:7;
then A101: (the carrier' of G2) c= the carrier' of (G2 \/ G3) by A5,A6,
      Def3;
      thus (the Target of ((G1 \/ G2) \/ G3)).v =
      (the Target of (G1 \/ G2)).v by A35,A36,A97,A99,Def3
        .= (the Target of G2).v by A1,A2,A97,Def3
        .= (the Target of (G2 \/ G3)).v by A5,A6,A97,Def3
        .= (the Target of (G1 \/ (G2 \/ G3))).v by A63,A64,A97,A101,Def3;
    end;
 now
      assume
A103: v in the carrier' of G3;
       (
the carrier' of G3) c= (the carrier' of G2) \/ (the carrier' of G3)
      by XBOOLE_1:7;
then A105: (the carrier' of G3) c= the carrier' of (G2 \/ G3) by A5,A6,
      Def3;
      thus (the Target of ((G1 \/ G2) \/ G3)).v =
      (the Target of G3).v by A35,A36,A103,Def3
        .= (the Target of (G2 \/ G3)).v by A5,A6,A103,Def3
        .= (the Target of (G1 \/ (G2 \/ G3))).v by A63,A64,A103,A105,Def3;
    end;
    hence thesis by A91,A92,A96,XBOOLE_0:def 3;
  end;
  hence thesis by A65,A66,A86,A87,FUNCT_1:9;
end;

theorem Th10:
  G is_sum_of G1, G2 implies G is_sum_of G2, G1
proof
  assume
A1: G is_sum_of G1, G2;
then A2: (the Target of G1) tolerates (the Target of G2) by Def4;
  A3: (
the Source of G1) tolerates (the Source of G2) & the MultiGraphStruct of G
  = G1 \/ G2 by A1,Def4;
A4: (the Source of G2) tolerates (the Source of G1) by A1,Def4;
 the MultiGraphStruct of G = G2 \/ G1 by A2,A3,Th8;
  hence thesis by A2,A4,Def4;
end;

theorem
  for G being strict Graph holds G is_sum_of G, G
proof
  let G be strict Graph;
 G = G \/ G by Th7;
  hence thesis by Def4;
end;
 
:: Graphs' inclusion ::

theorem Th12:
  (ex G st G1 c= G & G2 c= G) implies G1 \/ G2 = G2 \/ G1
proof
  given G such that
A1: G1 c= G & G2 c= G;
  A2: ( the Source of G1) c= (the Source of G) & ( the Source of G2) c= (the
  Source of G) by A1,Th5;
  A3: ( the Target of G1) c= (the Target of G) & ( the Target of G2) c= (the
  Target of G) by A1,Th5;
A4: (the Source of G1) tolerates (the Source of G2) by A2,PARTFUN1:131;
 (the Target of G1) tolerates (the Target of G2) by A3,PARTFUN1:131;
  hence thesis by A4,Th8;
end;

theorem
  (ex G st G1 c= G & G2 c= G & G3 c= G) implies (G1 \/ G2) \/ G3 = G1 \/ ( G2
  \/ G3)
proof
  given G such that
A1: G1 c= G and
A2: G2 c= G and
A3: G3 c= G;
A4: (the Source of G1) c= (the Source of G) by A1,Th5;
A5: (the Source of G2) c= (the Source of G) by A2,Th5;
A6: (the Source of G3) c= (the Source of G) by A3,Th5;
A7: (the Target of G1) c= (the Target of G) by A1,Th5;
A8: (the Target of G2) c= (the Target of G) by A2,Th5;
A9: (the Target of G3) c= (the Target of G) by A3,Th5;
A10: (the Source of G1) tolerates (the Source of G2) by A4,A5,PARTFUN1:139;
A11: (the Source of G1) tolerates (the Source of G3) by A4,A6,PARTFUN1:139;
A12: (the Source of G2) tolerates (the Source of G3) by A5,A6,PARTFUN1:139;
A13: (the Target of G1) tolerates (the Target of G2) by A7,A8,PARTFUN1:139;
A14: (the Target of G1) tolerates (the Target of G3) by A7,A9,PARTFUN1:139;
 (the Target of G2) tolerates (the Target of G3) by A8,A9,PARTFUN1:139;
  hence thesis by A10,A11,A12,A13,A14,Th9;
end;

theorem
  {} is Chain of G by Lm1;

theorem
  for H1, H2 being strict Subgraph of G st
  the carrier of H1 = the carrier of H2 &
  the carrier' of H1 = the carrier' of H2
  holds H1 = H2 by Lm3;

theorem Th16:
  for G1,G2 being strict Graph holds G1 c= G2 & G2 c= G1 implies G1 = G2
proof
  let G1,G2 be strict Graph;
  assume that
A1: G1 c= G2 and
A2: G2 c= G1;
A3: G1 is Subgraph of G2 by A1,Def24;
A4: G2 is Subgraph of G1 by A2,Def24;
A5: (the carrier of G1) c= (the carrier of G2) by A3,Def18;
 (the carrier of G2) c= (the carrier of G1) by A4,Def18;
then A7: the carrier of G1 = the carrier of G2 by A5,XBOOLE_0:def 10;
A8: (the carrier' of G1) c= (the carrier' of G2) by A3,Def18;
 (the carrier' of G2) c= (the carrier' of G1) by A4,Def18;
then A10: the carrier' of G1 = the carrier' of G2 by A8,XBOOLE_0:def 10;
then A11: dom(the Source of G1) = the carrier' of G2 by FUNCT_2:def 1
    .= dom(the Source of G2) by FUNCT_2:def 1;
 for v st v in dom (the Source of G1) holds
  (the Source of G1).v = (the Source of G2).v by A3,Def18;
then A13: the Source of G1 = the Source of G2 by A11,FUNCT_1:9;
A14: dom (the Target of G1) = the carrier' of G2 by A10,FUNCT_2:def 1
    .= dom (the Target of G2) by FUNCT_2:def 1;
 for v st v in dom (the Target of G1) holds
  (the Target of G1).v = (the Target of G2).v by A3,Def18;
  hence thesis by A7,A10,A13,A14,FUNCT_1:9;
end;

theorem Th17:
  G1 c= G2 & G2 c= G3 implies G1 c= G3
proof
  assume that
A1: G1 c= G2 and
A2: G2 c= G3;
A3: G1 is Subgraph of G2 by A1,Def24;
A4: G2 is Subgraph of G3 by A2,Def24;
A5: (the carrier of G1) c= (the carrier of G2) by A3,Def18;
 (the carrier of G2) c= (the carrier of G3) by A4,Def18;
then A7: (the carrier of G1) c= (the carrier of G3) by A5,XBOOLE_1:1;
A8: (the carrier' of G1) c= (the carrier' of G2) by A3,Def18;
 (the carrier' of G2) c= (the carrier' of G3) by A4,Def18;
then A10: (the carrier' of G1) c= (the carrier' of G3) by A8,XBOOLE_1:1;
 for v st v in the carrier' of G1 holds
  (the Source of G1).v = (the Source of G3).v &
  (the Target of G1).v = (the Target of G3).v &
  (the Source of G3).v in the carrier of G1 &
  (the Target of G3).v in the carrier of G1
  proof
    let v;
    assume
A12: v in the carrier' of G1;
    thus
then A13: (the Source of G1).v = (the Source of G2).v by A3,Def18
      .= (the Source of G3).v by A4,A8,A12,Def18;
    thus
A14: (the Target of G1).v = (the Target of G2).v by A3,A12,Def18
      .= (the Target of G3).v by A4,A8,A12,Def18;
 v in dom (the Source of G1) by A12,FUNCT_2:def 1;
then  (the Source of G1).v in rng(the Source of G1) by FUNCT_1:def 5;
    hence (the Source of G3).v in the carrier of G1 by A13;
 v in dom(the Target of G1) by A12,FUNCT_2:def 1;
then  (the Target of G1).v in rng(the Target of G1) by FUNCT_1:def 5;
    hence thesis by A14;
  end;
then  G1 is Subgraph of G3 by A7,A10,Def18;
  hence thesis by Def24;
end;

theorem Th18:
  G is_sum_of G1, G2 implies G1 c= G & G2 c= G
proof
  assume
A1: G is_sum_of G1, G2;
A2: for G, G1, G2 being Graph st G is_sum_of G1, G2 holds G1 c= G
  proof
    let G, G1, G2 be Graph;
    assume
A3: G is_sum_of G1, G2;
    then
A4: (the Source of G1) tolerates (the Source of G2) & (the Target of G1)
    tolerates (the Target of G2) by Def4;
A5: the MultiGraphStruct of G = G1 \/ G2 by A3,Def4;
then  the carrier of G = (the carrier of G1) \/ (the carrier of G2)
    by A4,Def3;
then A7: (the carrier of G1) c= (the carrier of G) by XBOOLE_1:7;
 the carrier' of G = (the carrier' of G1) \/ (the carrier' of G2)
    by A4,A5,Def3;
then A9: (the carrier' of G1) c= (the carrier' of G) by XBOOLE_1:7;
 for v st v in the carrier' of G1 holds
    (the Source of G1).v = (the Source of G).v &
    (the Target of G1).v = (the Target of G).v &
    (the Source of G).v in the carrier of G1 &
    (the Target of G).v in the carrier of G1
    proof
      let v;
      assume
A11:  v in the carrier' of G1;
      thus
then A12:  (the Source of G1).v = (the Source of G).v by A4,A5,Def3;
      thus
A13:  (the Target of G1).v = (the Target of G).v by A4,A5,A11,Def3;
      thus (the Source of G).v in the carrier of G1 by A11,A12,FUNCT_2:7;
      thus thesis by A11,A13,FUNCT_2:7;
    end;
then  G1 is Subgraph of G by A7,A9,Def18;
    hence thesis by Def24;
  end;
  hence G1 c= G by A1;
  thus thesis by A1,A2,Th10;
end;

theorem Th19:
  (the Source of G1) tolerates (the Source of G2) &
  (the Target of G1) tolerates (the Target of G2) implies
  G1 c= G1 \/ G2 & G2 c= G1 \/ G2
proof
  assume (the Source of G1) tolerates (the Source of G2) & (the Target of G1)
  tolerates (the Target of G2);
then  (G1 \/ G2) is_sum_of G1, G2 by Def4;
  hence thesis by Th18;
end;

theorem Th20:
  (ex G st G1 c= G & G2 c= G) implies G1 c= G1 \/ G2 & G2 c= G1 \/ G2
proof
  given G such that
A1: G1 c= G & G2 c= G;
  A2: (
the Source of G1) c= (the Source of G) & (the Source of G2) c= (the Source
  of G) by A1,Th5;
  A3: (
the Target of G1) c= (the Target of G) & (the Target of G2) c= (the Target
  of G) by A1,Th5;
A4: (the Source of G1) tolerates (the Source of G2) by A2,PARTFUN1:139;
 (the Target of G1) tolerates (the Target of G2) by A3,PARTFUN1:139;
  hence thesis by A4,Th19;
end;

theorem Th21:
  G1 c= G3 & G2 c= G3 & G is_sum_of G1, G2 implies G c= G3
proof
  assume that
A1: G1 c= G3 and
A2: G2 c= G3 and
A3: G is_sum_of G1, G2;
  A4: (the Source of G1) tolerates (the Source of G2) & (the Target of G1)
  tolerates (the Target of G2) by A3,Def4;
A5: the MultiGraphStruct of G = G1 \/ G2 by A3,Def4;
A6: G1 is Subgraph of G3 by A1,Def24;
A7: G2 is Subgraph of G3 by A2,Def24;
A8: (the carrier of G1) c= (the carrier of G3) by A6,Def18;
 (the carrier of G2) c= (the carrier of G3) by A7,Def18;
then A10: (the carrier of G1) \/ (the carrier of G2) c= (the carrier of G3)
  by A8,XBOOLE_1:8;
A11: (the carrier' of G1) c= (the carrier' of G3) by A6,Def18;
 (the carrier' of G2) c= (the carrier' of G3) by A7,Def18;
then A13: ( the carrier' of G1) \/ (the carrier' of G2) c= (the carrier' of G3)
  by A11,XBOOLE_1:8;
 for v st v in the carrier' of (G1 \/ G2) holds
  (the Source of (G1 \/ G2)).v = (the Source of G3).v &
  (the Target of (G1 \/ G2)).v = (the Target of G3).v &
  (the Source of G3).v in the carrier of (G1 \/ G2) &
  (the Target of G3).v in the carrier of (G1 \/ G2)
  proof
    let v such that
A15: v in the carrier' of (G1 \/ G2);
    thus
A16: (the Source of (G1 \/ G2)).v = (the Source of G3).v &
    (the Target of (G1 \/ G2)).v = (the Target of G3).v
    proof
A17:  v in (the carrier' of G1) \/ (the carrier' of G2) by A4,A15,Def3;
A18:  now
        assume
A19:    v in the carrier' of G1;
then A20:    (the Source of G3).v = (the Source of G1).v by A6,Def18
          .= (the Source of (G1 \/ G2)).v by A4,A19,Def3;
    (the Target of G3).v = (the Target of G1).v by A6,A19,Def18
          .= (the Target of (G1 \/ G2)).v by A4,A19,Def3;
        hence thesis by A20;
      end;
  now
        assume
A23:    v in the carrier' of G2;
then A24:    (the Source of G3).v = (the Source of G2).v by A7,Def18
          .= (the Source of (G1 \/ G2)).v by A4,A23,Def3;
    (the Target of G3).v = (the Target of G2).v by A7,A23,Def18
          .= (the Target of (G1 \/ G2)).v by A4,A23,Def3;
        hence thesis by A24;
      end;
      hence thesis by A17,A18,XBOOLE_0:def 3;
    end;
    hence (the Source of G3).v in the carrier of (G1 \/ G2) by A15,
    FUNCT_2:7;
    thus thesis by A15,A16,FUNCT_2:7;
  end;
  hence the carrier of G c= the carrier of G3 &
  the carrier' of G c= the carrier' of G3 & for v st v in the carrier' of G
  holds
  (the Source of G).v = (the Source of G3).v &
  (the Target of G).v = (the Target of G3).v &
  (the Source of G3).v in the carrier of G &
  (the Target of G3).v in the carrier of G by A4,A5,A10,A13,Def3;
end;

theorem Th22:
  G1 c= G & G2 c= G implies (G1 \/ G2) c= G
proof
  assume
A1: G1 c= G & G2 c= G;
  then A2: (
the Source of G1) c= (the Source of G) & (the Source of G2) c= (the Source
  of G) by Th5;
  A3: (
the Target of G1) c= (the Target of G) & (the Target of G2) c= (the Target
  of G) by A1,Th5;
A4: (the Source of G1) tolerates (the Source of G2) by A2,PARTFUN1:139;
 (the Target of G1) tolerates (the Target of G2) by A3,PARTFUN1:139;
then  (G1 \/ G2) is_sum_of G1, G2 by A4,Def4;
  hence thesis by A1,Th21;
end;

theorem
  for G1,G2 being strict Graph holds
  G1 c= G2 implies G1 \/ G2 = G2 & G2 \/ G1 = G2
proof
  let G1,G2 be strict Graph;
  assume
A1: G1 c= G2;
then  (G1 \/ G2) c= G2 & G2 c= (G1 \/ G2) by Th20,Th22;
  thus
then  G1 \/ G2 = G2 by Th16;
  hence thesis by A1,Th12;
end;

theorem Th24:
  (the Source of G1) tolerates (the Source of G2) &
  (the Target of G1) tolerates (the Target of G2) &
  (G1 \/ G2 = G2 or G2 \/ G1 = G2) implies G1 c= G2
proof
  assume
  A1: (the Source of G1) tolerates (the Source of G2) & (the Target of G1)
  tolerates (the Target of G2);
  assume
A2: G1 \/ G2 = G2 or G2 \/ G1 = G2;
then
 the carrier of G2 = (the carrier of G1) \/ (the carrier of G2) by A1,
  Def3;
then A4: (the carrier of G1) c= the carrier of G2 by XBOOLE_1:7;
 the carrier' of G2 = (the carrier' of G1) \/ (the carrier' of G2)
  by A1,A2,Def3;
then A6: (the carrier' of G1) c= (the carrier' of G2) by XBOOLE_1:7;
 for v st v in the carrier' of G1 holds
  (the Source of G1).v = (the Source of G2).v &
  (the Target of G1).v = (the Target of G2).v &
  (the Source of G2).v in the carrier of G1 &
  (the Target of G2).v in the carrier of G1
  proof
    let v;
    assume
A8: v in the carrier' of G1;
    thus
then A9: (the Source of G1).v = (the Source of G2).v by A1,A2,Def3;
    thus
A10: (the Target of G1).v = (the Target of G2).v by A1,A2,A8,Def3;
    thus (the Source of G2).v in the carrier of G1 by A8,A9,FUNCT_2:7;
    thus thesis by A8,A10,FUNCT_2:7;
  end;
then  G1 is Subgraph of G2 by A4,A6,Def18;
  hence thesis by Def24;
end;

canceled 2;

theorem
  for G being oriented Graph st G1 c= G holds G1 is oriented
proof
  let G be oriented Graph;
  assume G1 c= G;
then A2: G1 is Subgraph of G by Def24;
   for x,y being set st x in the carrier' of G1 & y in the carrier' of G1 &
  (the Source of G1).x = (the Source of G1).y &
  (the Target of G1).x = (the Target of G1).y holds x = y
  proof
    let x, y be set;
    assume that
A4: x in the carrier' of G1 and
A5: y in the carrier' of G1 and
A6: (the Source of G1).x = (the Source of G1).y and
A7: (the Target of G1).x = (the Target of G1).y;
A8: (the carrier' of G1) c= (the carrier' of G) by A2,Def18;
A9: (the Source of G).x = (the Source of G1).y by A2,A4,A6,Def18
      .= (the Source of G).y by A2,A5,Def18;
 (the Target of G).x = (the Target of G1).y by A2,A4,A7,Def18
      .= (the Target of G).y by A2,A5,Def18;
    hence thesis by A4,A5,A8,A9,Def5;
  end;
  hence thesis by Def5;
end;

theorem
  for G being non-multi Graph st G1 c= G holds G1 is non-multi
proof
  let G be non-multi Graph;
  assume G1 c= G;
then A2: G1 is Subgraph of G by Def24;
   for x,y being set st x in the carrier' of G1 & y in the carrier' of G1 &
  ( (the Source of G1).x = (the Source of G1).y &
  (the Target of G1).x = (the Target of G1).y or
  (the Source of G1).x = (the Target of G1).y &
  (the Source of G1).y = (the Target of G1).x ) holds x = y
  proof
    let x,y be set such that
A4: x in the carrier' of G1 & y in the carrier' of G1;
    assume
A5: (the Source of G1).x = (the Source of G1).y &
    (the Target of G1).x = (the Target of G1).y or
    (the Source of G1).x = (the Target of G1).y &
    (the Source of G1).y = (the Target of G1).x;
A6: (the carrier' of G1) c= (the carrier' of G) by A2,Def18;
    A7: (
the Source of G1).x = (the Source of G).x & (the Source of G1).y = (the
    Source of G).y by A2,A4,Def18;
     (
the Target of G1).x = (the Target of G).x & (the Target of G1).y = (the
    Target of G).y by A2,A4,Def18;
    hence thesis by A4,A5,A6,A7,Def6;
  end;
  hence thesis by Def6;
end;

theorem
  for G being simple Graph st G1 c= G holds G1 is simple
proof
  let G be simple Graph;
  assume G1 c= G;
then A2: G1 is Subgraph of G by Def24;
 not ex x being set st x in the carrier' of G1 &
  (the Source of G1).x = (the Target of G1).x
  proof
    given x being set such that
A4: x in the carrier' of G1 and
A5: (the Source of G1).x = (the Target of G1).x;
A6: (the carrier' of G1) c= (the carrier' of G) by A2,Def18;
 (the Source of G).x = (the Target of G1).x by A2,A4,A5,Def18
      .= (the Target of G).x by A2,A4,Def18;
    hence contradiction by A4,A6,Def7;
  end;
  hence thesis by Def7;
end;
 
:: Set of all subgraphs ::

theorem
  for G1 being strict Graph holds G1 in bool G iff G1 c= G
proof
  let G1 be strict Graph;
  thus G1 in bool G implies G1 c= G
  proof
    assume G1 in bool G;
then  G1 is Subgraph of G by Def25;
    hence thesis by Def24;
  end;
  assume G1 c= G;
then  G1 is Subgraph of G by Def24;
  hence thesis by Def25;
end;

theorem Th31:
  for G being strict Graph holds G in bool G
proof
  let G be strict Graph;
 G is Subgraph of G by Def24;
  hence thesis by Def25;
end;

theorem
  for G1 being strict Graph holds G1 c= G2 iff bool G1 c= bool G2
proof
  let G1 be strict Graph;
  thus G1 c= G2 implies bool G1 c= bool G2
  proof
    assume
A1: G1 c= G2;
 for x being set st x in bool G1 holds x in bool G2
    proof
      let x be set;
      assume x in bool G1;
      then reconsider G = x as strict Subgraph of G1 by Def25;
   G c= G1 by Def24;
then    G c= G2 by A1,Th17;
then    G is strict Subgraph of G2 by Def24;
      hence thesis by Def25;
    end;
    hence thesis by TARSKI:def 3;
  end;
  assume
A7: bool G1 c= bool G2;
 G1 in bool G1 by Th31;
then  G1 is Subgraph of G2 by A7,Def25;
  hence thesis by Def24;
end;

canceled;

theorem
  for G being strict Graph holds { G } c= bool G
proof
  let G be strict Graph;
 G in bool G by Th31;
  hence thesis by ZFMISC_1:37;
end;

theorem
  for G1,G2 being strict Graph holds
  (the Source of G1) tolerates (the Source of G2) &
  (the Target of G1) tolerates (the Target of G2) &
  bool (G1 \/ G2) c= (bool G1) \/ (bool G2) implies G1 c= G2 or G2 c= G1
proof
  let G1,G2 be strict Graph;
  assume
  A1: (the Source of G1) tolerates (the Source of G2) & (the Target of G1)
  tolerates (the Target of G2);
  assume
A2: bool (G1 \/ G2) c= (bool G1) \/ (bool G2);
A3: G1 \/ G2 in bool (G1 \/ G2) by Th31;
A4: now
    assume G1 \/ G2 in bool G1;
then  G1 \/ G2 is Subgraph of G1 by Def25;
then A7: G1 \/ G2 c= G1 by Def24;
 G1 c= G1 \/ G2 by A1,Th19;
then  G1 \/ G2 = G1 by A7,Th16;
    hence G2 c= G1 by A1,Th24;
  end;
 now
    assume G1 \/ G2 in bool G2;
then  G1 \/ G2 is Subgraph of G2 by Def25;
then A13: G1 \/ G2 c= G2 by Def24;
 G2 c= G1 \/ G2 by A1,Th19;
then  G1 \/ G2 = G2 by A13,Th16;
    hence G1 c= G2 by A1,Th24;
  end;
  hence thesis by A2,A3,A4,XBOOLE_0:def 3;
end;

theorem
  (the Source of G1) tolerates (the Source of G2) &
  (the Target of G1) tolerates (the Target of G2) implies
  bool G1 \/ bool G2 c= bool (G1 \/ G2)
proof
  assume
  A1: (the Source of G1) tolerates (the Source of G2) & (the Target of G1)
  tolerates (the Target of G2);
A2: for v st v in bool G1 holds v in bool (G1 \/ G2)
  proof
    let v;
    assume v in bool G1;
    then reconsider G = v as strict Subgraph of G1 by Def25;
 G c= G1 & G1 c= G1 \/ G2 by A1,Def24,Th19;
then  G c= G1 \/ G2 by Th17;
then  G is strict Subgraph of (G1 \/ G2) by Def24;
    hence thesis by Def25;
  end;
A7: for v st v in bool G2 holds v in bool (G1 \/ G2)
  proof
    let v;
    assume v in bool G2;
    then reconsider G = v as strict Subgraph of G2 by Def25;
 G c= G2 & G2 c= G1 \/ G2 by A1,Def24,Th19;
then  G c= G1 \/ G2 by Th17;
then  G is strict Subgraph of (G1 \/ G2) by Def24;
    hence thesis by Def25;
  end;
 for v st v in bool G1 \/ bool G2 holds v in bool (G1 \/ G2)
  proof
    let v;
    assume v in bool G1 \/ bool G2;
then  v in bool G1 or v in bool G2 by XBOOLE_0:def 3;
    hence thesis by A2,A7;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem
  G1 in bool G & G2 in bool G implies (G1 \/ G2) in bool G
proof
  assume that
A1: G1 in bool G and
A2: G2 in bool G;
A3: G1 is Subgraph of G by A1,Def25;
A4: G2 is Subgraph of G by A2,Def25;
A5: G1 c= G by A3,Def24;
 G2 c= G by A4,Def24;
then  (G1 \/ G2) c= G by A5,Th22;
then  (G1 \/ G2) is Subgraph of G by Def24;
  hence thesis by Def25;
end;
