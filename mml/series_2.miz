:: Partial Sum of Some Series
::  by Ming Liang and Yuzhong Ding
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies ARYTM, SEQ_1, FUNCT_1, ARYTM_3, SERIES_1, GROUP_1, NAT_1,
      ARYTM_1, QC_LANG1, ABSVALUE, MATRIX_2, POWER;
 notations ORDINAL1, XCMPLX_0, XREAL_0, NAT_1, SEQ_1, COMPLEX1, NEWTON,
      SERIES_1, ABIAN, POWER;
 constructors REAL_1, NAT_1, SEQ_2, SEQM_3, PREPOWER, POWER, PARTFUN1,
      MEMBERED, SERIES_1, XBOOLE_0, INT_1, ORDINAL2, NEWTON, ABSVALUE, ABIAN,
      MATRIX_2, XREAL_0, XCMPLX_0, PEPIN, GROUP_3;
 registrations XREAL_0, NEWTON, MEMBERED;
 requirements REAL, NUMERALS, SUBSET, ARITHM;
 theorems NEWTON, XCMPLX_1, SERIES_1, NAT_1, AXIOMS, REAL_1, REAL_2, ABIAN,
      POWER, ABSVALUE, NAT_2;
 schemes NAT_1, BINOM;

begin

 reserve n,m,k for Nat,
         a,b,p,r for real number,
         s,s1,s2,s3 for Real_Sequence;

theorem Th1:
  abs ((-1)|^n) = 1
proof
 per cases;
  suppose n is even;then
  A1: ex k st n=2*k by ABIAN:def 2;
    (-1)|^n =(-1) to_power n by POWER:48
     .=1 to_power n by A1,POWER:54
     .=1 by POWER:31;
     hence thesis by ABSVALUE:def 1;
  end;
  suppose n is odd;then
      n mod 2 = 1 by NAT_2:24;then
   A2:ex k be Nat st
       n = 2*k+1 & 1<2 by NAT_1:def 2;
    (-1)|^n =(-1) to_power n by POWER:48
     .=-(1 to_power n) by A2,POWER:55
     .=-1 by POWER:31;then
     abs((-1)|^n)=-(-1) by ABSVALUE:def 1;
     hence thesis;
  end;
end;

Lm1:n |^ 2 = n * n & a|^2=a*a & n|^3=n*n*n
 proof
A1: n |^ (1+1) = n|^1 * n by NEWTON:11
              .= n* n by NEWTON:10;
A2: a|^(1+1) = a|^1 * a by NEWTON:11
            .=a*a by NEWTON:10;
    n|^(2+1)=n|^2*n by NEWTON:11;
    hence thesis by A1,A2;
 end;

Lm2: n|^3=n|^2 * n
 proof n |^ (2+1) = n|^2 * n by NEWTON:11;
   hence thesis;
 end;

Lm3:4=2|^2 & (a+b)|^2=a|^2+2*a*b+b|^2
  proof
   A1:2|^2= 2*2 by Lm1 .=4;
      (a+b)|^(1+1) = (a+b)|^1*(a+b) by NEWTON:11
      .= (a+b)*(a+b) by NEWTON:10
      .=(a*a+a*b)+(a*b+b*b)
      .=(a*a+a*b)+(a*b+b|^2) by Lm1
      .=(a|^2+a*b)+(a*b+b|^2) by Lm1
      .=a|^2+2*(a*b)+b|^2;
      hence thesis by A1;
  end;

Lm4: (n+1)|^3 = n|^3+3*n|^2+3*n+1
proof
    (n+1)|^(2+1)=((n+1)|^2)*(n+1) by NEWTON:11
      .=(n|^2+2*n*1+1|^2)*(n+1) by Lm3
      .=n|^2*n+n|^2*1+(2*n*n+2*n*1)+(1|^2*n+1|^2*1)
      .=n|^2*n+n|^2*1+(2*n*n+2*n*1)+(1|^2*n+1) by NEWTON:15
      .=n|^(2+1)+n|^2+(2*n*n+2*n)+(1|^2*n+1) by NEWTON:11
      .=n|^3+n|^2+(2*(n*n)+2*n)+(1*n+1) by NEWTON:15
      .=n|^3+n|^2+(2*(n|^1*n)+2*n)+(n+1) by NEWTON:10
      .=n|^3+n|^2+(2*n|^(1+1)+2*n)+(n+1) by NEWTON:11
      .=n|^3+n|^2+2*n|^2 +2*n+(n+1) by XCMPLX_1:1;
   hence thesis;
end;

Lm5: n*(2*n+1)*(3*n|^2+3*n-1)+ (n+1)|^3*30
     = (n+2)*(2*(n+1)+1)*(3*(n+1)|^2+3*(n+1)-1)
 proof
 A1:n*(2*n+1)*(3*n|^2+3*n-1) + (n+1)|^3*30
    =(2*(n*n)+n)*(3*n|^2+3*n-1) + (n+1)|^3*30
   .=(2*(n|^1*n)+n)*(3*n|^2+3*n-1) + (n+1)|^3*30 by NEWTON:10
   .=(2*(n|^(1+1))+n)*(3*n|^2+3*n-1) + (n+1)|^3*30 by NEWTON:11
   .=(2*n|^2+n)*(3*n|^2)+(2*n|^2+n)*(3*n)-(2*n|^2+n)*1+(n+1)|^3*30
            by XCMPLX_1:43
   .=2*(n|^2*n|^2)*3+n*n|^2*3+(2*(n|^2*n)*3+n*n*3)-(2*n|^2+n)+(n+1)|^3*30
   .=2*n|^(2+2)*3+n*n|^2*3+(2*(n|^2*n)*3+n*n*3)-(2*n|^2+n)+(n+1)|^3*30
            by NEWTON:13
   .=2*n|^4*3+n*n|^2*3+(2*(n|^(2+1))*3+n*n*3)-(2*n|^2+n)+(n+1)|^3*30
            by NEWTON:11
   .=2*n|^4*3+n*n|^2*3+(2*n|^3*3+n|^2*3)-(2*n|^2+n)+(n+1)|^3*30 by Lm1
   .=6*n|^4+n|^(2+1)*3+(6*n|^3+n|^2*3)-(2*n|^2+n)+(n+1)|^3*30 by NEWTON:11
   .=6*n|^4+3*n|^3+6*n|^3+3*n|^2-(2*n|^2+n)+(n+1)|^3*30 by XCMPLX_1:1
   .=6*n|^4+9*n|^3+1*n|^2-n+(n|^3+3*n|^2+3*n+1)*30 by Lm4
   .=6*n|^4+39*n|^3+91*n|^2+(90-1)*n+30;
   (n+2)*(2*(n+1)+1)*(3*(n+1)|^2+3*(n+1)-1)
    =(n+2)*(2*n+3)*(3*(n|^2+2*n*1+1|^2)+(3*n+3)-1) by Lm3
   .=((n*(2*n)+2*(2*n))+(n*3+2*3))*(3*(n|^2+2*n+1|^2)+3*n+3-1)
   .=((n*(2*n)+2*(2*n))+(n*3+2*3))*(3*(n|^2+2*n+1)+3*n+3-1) by NEWTON:15
   .=((n*n*2+2*2*n)+(n*3+6))*((n|^2*3+6*n+3)+3*n+3-1)
   .=(2*n|^2+4*n+(n*3+6))*(3*n|^2+6*n+3+3*n+3-1) by Lm1
   .=2*(n|^2*n|^2)*3+2*n|^2*n*9+10*n|^2+(7*n*n|^2*3+7*n*n*9+35*n)
     +(6*3*n|^2+6*9*n+30)
   .=2*n|^(2+2)*3+2*(n|^2*n)*9+10*n|^2+(7*(n*n|^2)*3+7*(n*n)*9+35*n)
     +(18*n|^2+54*n+30) by NEWTON:13
   .=2*n|^4*3+2*n|^(2+1)*9+10*n|^2+(7*(n*n|^2)*3+7*(n*n)*9+35*n)
     +(18*n|^2+54*n+30) by NEWTON:11
   .=6*n|^4+2*n|^3*9+10*n|^2+(7*n|^(2+1)*3+7*(n*n)*9+35*n)
     +(18*n|^2+54*n+30) by NEWTON:11;then
   (n+2)*(2*(n+1)+1)*(3*(n+1)|^2+3*(n+1)-1)
   =6*n|^4+18*n|^3+10*n|^2+(7*n|^3*3+7*n|^2*9+35*n)
     +(18*n|^2+54*n+30) by Lm1
   .=6*n|^4+39*n|^3+(73+18)*n|^2+89*n+30;
  hence thesis by A1;
 end;

Lm6:
  (n+1)|^4=n|^4+4*n|^3+6*n|^2+4*n+1 &
    (n+1)|^5=n|^5+5*n|^4+10*n|^3+10*n|^2+5*n+1
proof
 A1:(n+1)|^(3+1)=(n+1)|^3*(n+1) by NEWTON:11
   .=(n|^3+3*n|^2+3*n+1)*(n+1) by Lm4
   .=n|^3*n+n|^3+(3*(n|^2*n)+3*n|^2)+(3*(n*n)+3*n)+(n+1)
   .=n|^(3+1)+n|^3+(3*(n|^2*n)+3*n|^2)+(3*(n*n)+3*n)+(n+1) by NEWTON:11
   .=n|^(3+1)+n|^3+(3*n|^(2+1)+3*n|^2)+(3*(n*n)+3*n)+(n+1) by NEWTON:11
   .=n|^4+n|^3+(3*n|^3+3*n|^2)+(3*n|^2+3*n)+(n+1) by Lm1;
   (n+1)|^(3+2)=(n+1)|^3*(n+1)|^2 by NEWTON:13
   .=(n|^3+3*n|^2+3*n+1)*(n+1)|^2 by Lm4
   .=(n|^3+3*n|^2+3*n+1)*(n|^2+2*n*1+1|^2) by Lm3
   .=(n|^3+3*n|^2+3*n+1)*(n|^2+2*n+1) by NEWTON:15
   .=(n|^3*n|^2+2*n*n|^3+n|^3*1)+(3*n|^2*n|^2+2*n*(3*n|^2)+3*n|^2*1)
     +(3*n*n|^2+3*n*(2*n)+3*n*1)+(n|^2+2*n+1)
   .=(n|^(3+2)+2*(n*n|^3)+n|^3)+(3*(n|^2*n|^2)+2*(n*n|^2)*3+3*n|^2)
     +(3*(n*n|^2)+3*(n*n)*2+3*n)+(n|^2+2*n+1) by NEWTON:13
   .=(n|^(3+2)+2*(n*n|^3)+n|^3)+(3*n|^(2+2)+2*(n*n|^2)*3+3*n|^2)
     +(3*(n*n|^2)+3*(n*n)*2+3*n)+(n|^2+2*n+1) by NEWTON:13
   .=(n|^(3+2)+2*n|^(3+1)+n|^3)+(3*n|^(2+2)+2*(n*n|^2)*3+3*n|^2)
     +(3*(n*n|^2)+3*(n*n)*2+3*n)+(n|^2+2*n+1) by NEWTON:11
   .=(n|^(3+2)+2*n|^(3+1)+n|^3)+(3*n|^(2+2)+2*n|^(2+1)*3+3*n|^2)
     +(3*(n*n|^2)+3*(n*n)*2+3*n)+(n|^2+2*n+1) by NEWTON:11
   .=(n|^5+2*n|^4+n|^3)+(3*n|^4+2*n|^3*3+3*n|^2)
     +(3*n|^3+3*(n*n)*2+3*n)+(n|^2+2*n+1) by NEWTON:11
   .=(n|^5+2*n|^4+n|^3)+(3*n|^4+6*n|^3+3*n|^2)+(3*n|^3+3*n|^2*2+3*n)
      +(n|^2+2*n+1) by Lm1;
 hence thesis by A1;
end;

theorem
  (n+1)|^3=n|^3+3*n|^2+3*n+1 &
  (n+1)|^4=n|^4+4*n|^3+6*n|^2+4*n+1 &
  (n+1)|^5=n|^5+5*n|^4+10*n|^3+10*n|^2+5*n+1 by Lm4,Lm6;

theorem
  (for n holds s.n = n) implies for n holds Partial_Sums(s).n = n*(n+1)/2
proof
  assume
 A1:for n holds s.n = n;
  defpred X[Nat] means Partial_Sums(s).$1 = $1*($1+1)/2;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0*(0+1)/2 by A1;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume Partial_Sums(s).n = n*(n+1)/2;then
     Partial_Sums(s).(n+1) = n*(n+1)/2 + s.(n+1) by SERIES_1:def 1
     .=n*(n+1)/2 + (n+1) by A1
     .=(n*(n+1) + (n+1)*2)/2;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = 2*n) implies for n holds Partial_Sums(s).n = n*(n+1)
proof
  assume
 A1:for n holds s.n = 2*n;
  defpred X[Nat] means Partial_Sums(s).$1 = $1*($1+1);
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=2*0 by A1 .= 0*(0+1);then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume Partial_Sums(s).n = n*(n+1);then
     Partial_Sums(s).(n+1) = n*(n+1) + s.(n+1) by SERIES_1:def 1
     .=n*(n+1) + 2*(n+1) by A1
     .=(n + 2)*(n+1);
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = 2*n+1) implies for n holds Partial_Sums(s).n = (n+1)|^2
proof
  assume
A1:for n holds s.n = 2*n+1;
  defpred X[Nat] means Partial_Sums(s).$1 = ($1+1)|^2;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=2*0+1 by A1
   .= (0+1)|^2 by NEWTON:15;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume Partial_Sums(s).n = (n+1)|^2;then
     Partial_Sums(s).(n+1) = (n+1)|^2 + s.(n+1) by SERIES_1:def 1
     .=(n+1)|^2 + (2*(n+1)+1) by A1
     .=(n+1)|^2 +2*n+3
     .= n|^2+2*n*1+1|^2+2*n+3 by Lm3
     .= n|^2+2*n+1+2*n+3 by NEWTON:15
     .= n|^2+(2*n+2*n)+(1+3)
     .= n|^2+2*n*2+2|^2 by Lm3
     .= (n+2)|^2 by Lm3;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = n*(n+1)) implies
      for n holds Partial_Sums(s).n = n*(n+1)*(n+2)/3
proof
  assume
 A1:for n holds s.n = n*(n+1);
  defpred X[Nat] means Partial_Sums(s).$1 = $1*($1+1)*($1+2)/3;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0*(0+1)*(0+2)/3 by A1;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n;
   assume Partial_Sums(s).n = n*(n+1)*(n+2)/3;then
     Partial_Sums(s).(n+1) = n*(n+1)*(n+2)/3 + s.(n+1) by SERIES_1:def 1
     .=n*(n+1)*(n+2)/3 + (n+1)*(n+1+1) by A1
     .=(n+1)*(n+2)*(n+3)/3;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = n*(n+1)*(n+2)) implies
      for n holds Partial_Sums(s).n = n*(n+1)*(n+2)*(n+3)/4
proof
  assume
 A1:for n holds s.n = n*(n+1)*(n+2);
  defpred X[Nat] means Partial_Sums(s).$1 = $1*($1+1)*($1+2)*($1+3)/4;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0*(0+1)*(0+2)*(0+3)/4 by A1;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n;
   assume Partial_Sums(s).n =n*(n+1)*(n+2)*(n+3)/4;then
     Partial_Sums(s).(n+1) = n*(n+1)*(n+2)*(n+3)/4 + s.(n+1) by SERIES_1:def 1
     .=n*(n+1)*(n+2)*(n+3)/4+ (n+1)*(n+1+1)*(n+1+2) by A1
     .=(n+1)*(n+1+1)*(n+1+2)*(n+1+3)/4;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = n*(n+1)*(n+2)*(n+3)) implies
      for n holds Partial_Sums(s).n = n*(n+1)*(n+2)*(n+3)*(n+4)/5
proof
  assume
 A1:for n holds s.n = n*(n+1)*(n+2)*(n+3);
  defpred X[Nat] means Partial_Sums(s).$1 = $1*($1+1)*($1+2)*($1+3)*($1+4)/5;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1
                   .=0*(0+1)*(0+2)*(0+3)*(0+4)/4 by A1;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n;
    assume Partial_Sums(s).n =n*(n+1)*(n+2)*(n+3)*(n+4)/5;then
     Partial_Sums(s).(n+1) = n*(n+1)*(n+2)*(n+3)*(n+4)/5 + s.(n+1)
                                                 by SERIES_1:def 1
     .=n*(n+1)*(n+2)*(n+3)*(n+4)/5+ (n+1)*(n+1+1)*(n+1+2)*(n+1+3) by A1
     .=(n+1)*(n+2)*(n+3)*(n+4)*(n+5)/5;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

Lm7:for n holds n+1<>0 & n+2 <> 0 & 1-1/(n+2)=(n+1)/(n+2)
    & 1/(n+1)*(n+1)=1 & 1/((n+1)!) = (n+2)/(((n+1)!)*(n+2))
proof let n;
    n>=0 by NAT_1:18;then
 A1: n+1>0 by NAT_1:38;
     n+1>=0 by NAT_1:37;then
A2:  n+1+1>0 by NAT_1:38; then
A3:  n + 2 > 0;
 A4: (n+1)/(n+2)=(n+2-1)/(n+2)
     .=(n+2)/(n+2)-1/(n+2) by XCMPLX_1:121
     .=1-1/(n+2) by A2,XCMPLX_1:60;
     1/((n+1)!)=(1*(n+2))/(((n+1)!)*(n+2)) by A2,XCMPLX_1:92;
  hence thesis by A1,A3,A4,XCMPLX_1:88;
end;

theorem
  (for n holds s.n = 1/(n*(n+1))) implies
      for n holds Partial_Sums(s).n = 1-1/(n+1)
proof
  assume
 A1:for n holds s.n = 1/(n*(n+1));
  defpred X[Nat] means Partial_Sums(s).$1 = 1-1/($1+1);
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1  .=1/(0*(0+1)) by A1
     .=1-1/1 by XCMPLX_1:49;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n;
   assume Partial_Sums(s).n =1-1/(n+1);then
    Partial_Sums(s).(n+1) = 1-1/(n+1) + s.(n+1) by SERIES_1:def 1
     .=1-1/(n+1) + 1/((n+1)*(n+1+1)) by A1
     .=1-(1/(n+1) - 1/((n+1)*(n+2))) by XCMPLX_1:37
     .=1-(1*(1/(n+1)) - (1/(n+1))*(1/(n+2))) by XCMPLX_1:103
     .=1-1/(n+1)*(1 - 1/(n+2))
     .=1-1/(n+1)*((1*(n+2)-1)/(n+2)) by Lm7
     .=1-(1/(n+1)*(n+1))/(n+2) by XCMPLX_1:75
     .=1-1/(n+2) by Lm7;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

Lm8:1/(n+1)-2/((n+1)*(n+3))=1/(n+3)
proof
     n>=0 by NAT_1:18;then
  A1: n+1>0 by NAT_1:38;
     n+1>=0 by NAT_1:37;then
     n+1+1>0 by NAT_1:38;then
     n+2+1>0 by NAT_1:38;then
     1/(n+1)-2/((n+1)*(n+3))
     =1*(n+3)/((n+1)*(n+3))-2/((n+1)*(n+3)) by XCMPLX_1:92
     .=(n+3-2)/((n+1)*(n+3)) by XCMPLX_1:121
     .=(n+1)*1/((n+1)*(n+3))
     .=1/(n+3) by A1,XCMPLX_1:92;
  hence thesis;
end;

theorem
  (for n holds s.n = 1/(n*(n+1)*(n+2))) implies
      for n holds Partial_Sums(s).n = 1/4-1/(2*(n+1)*(n+2))
proof
  assume
 A1:for n holds s.n = 1/(n*(n+1)*(n+2));
  defpred X[Nat] means Partial_Sums(s).$1 =1/4-1/(2*($1+1)*($1+2));
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1  .=1/(0*(0+1)*(0+2)) by A1
     .=1/4-1/(2*(0+1)*(0+2)) by XCMPLX_1:49;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n;
   assume Partial_Sums(s).n =1/4-1/(2*(n+1)*(n+2));then
    Partial_Sums(s).(n+1) =1/4-1/(2*(n+1)*(n+2))+ s.(n+1)
                                     by SERIES_1:def 1
     .=1/4-1/(2*(n+1)*(n+2))+1/((n+1)*(n+1+1)*(n+1+2)) by A1
     .=1/4-(1/((2*(n+1))*(n+2))-1/((n+1)*(n+2)*(n+3))) by XCMPLX_1:37
     .=1/4-(1/((2*(n+2))*(n+1))-(1*2)/((n+2)*(n+1)*(n+3)*2))
                                                      by XCMPLX_1:92
     .=1/4-(1/((2*(n+2))*(n+1))-(1*2)/((n+2)*2*((n+1)*(n+3))))
     .=1/4-(1/((2*(n+2))*(n+1))-1/((n+2)*2)*(2/((n+1)*(n+3))))
                                  by XCMPLX_1:77
     .=1/4-(1/(2*(n+2))*(1/(n+1))-1/(2*(n+2))*(2/((n+1)*(n+3))))
                                  by XCMPLX_1:103
     .=1/4-1/(2*(n+2))*(1/(n+1)-2/((n+1)*(n+3)))
     .=1/4-1/(2*(n+2))*(1/(n+3)) by Lm8
     .=1/4-1/(2*(n+1+1)*(n+1+2)) by XCMPLX_1:103;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

Lm9:1/(n+1)-3/((n+1)*(n+4))=1/(n+4)
proof
     n>=0 by NAT_1:18;then
 A1: n+1>0 by NAT_1:38;
     n+1>=0 by NAT_1:37;then
     n+1+1>0 by NAT_1:38;then
     n+2+1>0 by NAT_1:38;then
     n+3+1>0 by NAT_1:38;then
     1/(n+1)-3/((n+1)*(n+4))
     =1*(n+4)/((n+1)*(n+4))-3/((n+1)*(n+4)) by XCMPLX_1:92
     .=(n+4-3)/((n+1)*(n+4)) by XCMPLX_1:121
     .=(n+1)*1/((n+1)*(n+4))
     .=1/(n+4) by A1,XCMPLX_1:92;
  hence thesis;
end;

theorem
  (for n holds s.n = 1/(n*(n+1)*(n+2)*(n+3))) implies
      for n holds Partial_Sums(s).n = 1/18-1/(3*(n+1)*(n+2)*(n+3))
proof
  assume
 A1:for n holds s.n = 1/(n*(n+1)*(n+2)*(n+3));
  defpred X[Nat] means Partial_Sums(s).$1
      = 1/18-1/(3*($1+1)*($1+2)*($1+3));
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1
     .= 1/(0*(0+1)*(0+2)*(0+3)) by A1
     .=1/18-1/(3*(0+1)*(0+2)*(0+3)) by XCMPLX_1:49;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n;
   assume Partial_Sums(s).n = 1/18-1/(3*(n+1)*(n+2)*(n+3));then
    Partial_Sums(s).(n+1)
     =1/18-1/(3*(n+1)*(n+2)*(n+3)) + s.(n+1) by SERIES_1:def 1
    .=1/18-1/(3*(n+1)*(n+2)*(n+3)) +1/((n+1)*(n+1+1)*(n+1+2)*(n+1+3)) by A1
    .=1/18-(1/(3*(n+1)*(n+2)*(n+3))-1/((n+1)*(n+2)*(n+3)*(n+4)))
         by XCMPLX_1:37
    .=1/18-(1/(3*(n+1)*(n+2)*(n+3))-(1*3)/((n+1)*(n+2)*(n+3)*(n+4)*3))
         by XCMPLX_1:92
    .=1/18-(1/((3*(n+2)*(n+3))*(n+1))-1*3/((3*(n+2)*(n+3))*((n+1)*(n+4))))
    .=1/18-(1/((3*(n+2)*(n+3))*(n+1))-1/(3*(n+2)*(n+3))*(3/((n+1)*(n+4))))
                                  by XCMPLX_1:77
    .=1/18-(1/(3*(n+2)*(n+3))*(1/(n+1))-1/(3*(n+2)*(n+3))*(3/((n+1)*(n+4))))
                                  by XCMPLX_1:103
    .=1/18-1/(3*(n+2)*(n+3))*(1/(n+1)-3/((n+1)*(n+4)))
    .=1/18-1/(3*(n+2)*(n+3))*(1/(n+4)) by Lm9
    .=1/18-1/(3*(n+1+1)*(n+1+2)*(n+1+3)) by XCMPLX_1:103;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = n |^ 2) implies for n holds
         Partial_Sums(s).n = n*(n+1)*(2*n+1)/6
proof
  assume
 A1:for n holds s.n = n |^ 2;
  defpred X[Nat] means Partial_Sums(s).$1 = $1*($1+1)*(2*$1+1)/6;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0 |^2 by A1
  .=0*(0+1)*(2*0+1)/6 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n = n*(n+1)*(2*n+1)/6;then
     Partial_Sums(s).(n+1) = n*(n+1)*(2*n+1)/6 + s.(n+1) by SERIES_1:def 1
     .=n*(n+1)*(2*n+1)/6 + (n+1) |^ 2 by A1
     .=(n*(n+1)*(2*n+1) + ((n+1) |^ 2)*6)/6
     .=((n+1)*n*(2*n+1) + (n+1)*(n+1)*6)/6 by Lm1;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

Lm10: n|^2+3*n+2 = (n+1)*(n+2)
 proof
  (n+1)*(n+2)=n*n+(2*n+1*n)+2
  .= n|^1*n+(2+1)*n+2 by NEWTON:10
  .= n|^(1+1)+(2+1)*n+2 by NEWTON:11;
  hence thesis;
end;

theorem
  (for n holds s.n =(-1)|^(n+1)*n|^2 ) implies for n holds
         Partial_Sums(s).n = (-1)|^(n+1)*n*(n+1)/2
proof
  assume
 A1:for n holds s.n = (-1)|^(n+1)*n|^2;
  defpred X[Nat] means Partial_Sums(s).$1 = (-1)|^($1+1)*$1*($1+1)/2;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=(-1)|^(0+1)*0|^2 by A1
  .=(-1)|^(0+1)*0*(0+1)/2 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n = (-1)|^(n+1)*n*(n+1)/2;then
     Partial_Sums(s).(n+1) = (-1)|^(n+1)*n*(n+1)/2 + s.(n+1) by SERIES_1:def 1
     .=(-1)|^(n+1)*n*(n+1)/2 + (-1)|^(n+1+1)*(n+1)|^2 by A1
     .=((-1)|^(n+1)*n*(n+1) + (-1)|^(n+1+1)*(n+1)|^2*2)/2
     .=((-1)|^(n+1)*n*(n+1) + (-1)|^(n+1)*(-1)*(n+1)|^2*2)/2 by NEWTON:11
     .=((-1)|^(n+1)*(-1)*((-1)*(n*(n+1))+(n+1)|^2*2))/2
     .=((-1)|^(n+1+1)*((-1)*(n*(n+1))+(n+1)|^2*2))/2 by NEWTON:11
     .=((-1)|^(n+2)*((-1)*n*n+(-1)*n*1+(n|^2+2*n*1+1|^2 )*2))/2 by Lm3
     .=((-1)|^(n+2)*((-1)*n*n+(-1)*n*1+(n|^2+2*n+1)*2))/2 by NEWTON:15
     .=((-1)|^(n+2)*((-1)*(n*n)+(-1)*n+(2*n|^2+2*n*2+1*2)))/2
     .=((-1)|^(n+2)*((-1)*(n|^1*n)+(-1)*n+(2*n|^2+2*n*2+1*2)))/2 by NEWTON:10
     .=((-1)|^(n+2)*((-1)*n|^(1+1)+(-1)*n+(2*n|^2+2*n*2+1*2)))/2 by NEWTON:11
     .=((-1)|^(n+2)*((-1)*n|^2+(-1)*n+(2*n|^2+4*n+2)))/2
     .=((-1)|^(n+2)*(1*n|^2+3*n+2))/2
     .=(-1)|^(n+2)*((n+1)*(n+2))/2 by Lm10
     .=(-1)|^(n+2)*(n+1)*(n+2)/2;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

Lm11:n*(4*n|^2) + 11*n + 12*n|^2+3 = (n+1)*(4*(n+1)|^2-1)
proof
   (n+1)*(4*(n+1)|^2-1) = (n+1)*(4*(n|^2+2*n*1+1|^2)-1) by Lm3
    .= (n+1)*(4*(n|^2+2*n+1)-1) by NEWTON:15
    .= n*(4*n|^2)+4*n|^2+(8*(n*n)+8*n)+(3*n+3*1)
    .= n*(4*n|^2)+4*n|^2+(8*n|^2+8*n)+(3*n+3*1) by Lm1
    .= n*(4*n|^2)+(4*n|^2+8*n|^2)+(8*n+3*n)+3;
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (2*n-1)|^2 & s.0 = 0) implies
  for n st n>=1 holds Partial_Sums(s).n = n*(4*n|^2-1)/3
proof
    assume
 A1:for n st n>=1 holds s.n = (2*n-1)|^2 & s.0 = 0;
  defpred X[Nat] means Partial_Sums(s).$1 = $1*(4*$1|^2-1)/3;
      Partial_Sums(s).(0+1) = Partial_Sums(s).0 + s.(0+1) by SERIES_1:def 1
    .=s.0 + s.1 by SERIES_1:def 1
    .=0 + s.1 by A1 .= 0 + (2*1-1)|^2 by A1
    .=1*(4*(1*1)-1)/3 by Lm1 .= 1*(4*1|^2-1)/3  by Lm1;then
  A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4: n>=1 & Partial_Sums(s).n = n*(4*n|^2-1)/3;
    A5: n+1>=1 by NAT_1:29;
     Partial_Sums(s).(n+1) =n*(4*n|^2-1)/3 + s.(n+1) by A4,SERIES_1:def 1
      .=n*(4*n|^2-1)/3 + (2*(n+1)-1)|^2 by A1,A5
      .=(n*(4*n|^2-1) + (2*(n+1)-1)|^2*3)/3
      .=(n*(4*n|^2-1) + (2*n+1)|^2*3)/3
      .=(n*(4*n|^2-1) + ((2*n)|^2+2*(2*n)*1+1|^2)*3)/3 by Lm3
      .=(n*4*n|^2-n*1 + ((2*n)|^2*3+2*(2*n)*3+1|^2*3))/3
      .=(n*(4*n|^2)-n + ((2|^2*n|^2)*3+2*2*n*3+1|^2*3))/3 by NEWTON:12
      .=(n*(4*n|^2)-n + ((2|^2*n|^2)*3+4*n*3+1*3))/3 by NEWTON:15
      .=(n*(4*n|^2)-n + ((2*2*n|^2)*3+12*n+3))/3 by Lm1
      .=(n*(4*n|^2) + (12-1)*n + 12*n|^2+3)/3
      .=(n+1)*(4*(n+1)|^2-1)/3 by Lm11;
      hence thesis;
    end;
    for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = n |^ 3) implies for n holds
         Partial_Sums(s).n = n|^2*(n+1)|^2/4
proof
  assume
 A1:for n holds s.n = n |^ 3;
  defpred X[Nat] means Partial_Sums(s).$1 = $1|^2*($1+1)|^2/4;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0 |^3 by A1
  .=0*(0+1)|^2/4 by NEWTON:16
  .=0|^2*(0+1)|^2/4 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n = n|^2*(n+1)|^2/4;then
     Partial_Sums(s).(n+1) = n|^2*(n+1)|^2/4 + s.(n+1) by SERIES_1:def 1
     .=n|^2*(n+1)|^2/4 + (n+1) |^ 3 by A1
     .=(n|^2*(n+1)|^2 + ((n+1) |^ 3)*4)/4
     .=(n|^2*(n+1)|^2 + (n+1)|^2*(n+1)*4)/4 by Lm2
     .=(n+1)|^2*(n|^2 +(2*2*n+4))/4
     .=(n+1)|^2*(n+2)|^2/4 by Lm3;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

Lm12:(n+1)|^2*(2*(n+1)|^2-1)=n|^2*n|^2*2+(12-1)*n|^2 + 8*n|^3+6*n+1
proof
  (n+1)|^2*(2*(n+1)|^2-1) = (n+1)|^2*(2*(n+1)|^2)-(n+1)|^2*1
   .=(n+1)|^2*(n+1)|^2*2-(n|^2+2*n*1+1|^2) by Lm3
   .=(n+1)|^2*(n+1)|^2*2-(n|^2+2*n+1) by NEWTON:15
   .=(n+1)|^2*(n|^2+2*n*1+1|^2)*2-(n|^2+2*n+1) by Lm3
   .=(n|^2+2*n*1+1|^2)*(n|^2+2*n+1|^2)*2-(n|^2+2*n+1) by Lm3
   .=(n|^2+2*n+1|^2)*(n|^2+2*n+1)*2-(n|^2+2*n+1) by NEWTON:15
   .=(n|^2+2*n+1)*(n|^2+2*n+1)*2-(n|^2+2*n+1) by NEWTON:15
   .=(n|^2*n|^2+2*n*n|^2+n|^2+(2*n*n|^2+2*n*n*2+2*n)+(n|^2+2*n+1))*2
      -(n|^2+2*n+1)
   .=(n|^2*n|^2+2*(n|^(2+1))+n|^2+(2*(n*n|^2)+2*(n*n)*2+2*n)+(n|^2+2*n+1))*2
      -(n|^2+2*n+1) by NEWTON:11
   .=(n|^2*n|^2+2*(n|^(2+1))+n|^2+(2*(n|^(2+1))+2*(n*n)*2+2*n)+(n|^2+2*n+1))*2
      -(n|^2+2*n+1) by NEWTON:11
   .=(n|^2*n|^2+2*n|^3+n|^2+(2*n|^3+2*(n|^1*n)*2+2*n)+(n|^2+2*n+1))*2
      -(n|^2+2*n+1) by NEWTON:10
   .=(n|^2*n|^2+2*n|^3+n|^2+(2*n|^3+2*n|^(1+1)*2+2*n)+(n|^2+2*n+1))*2
      -(n|^2+2*n+1) by NEWTON:11
   .=(n|^2*n|^2+2*n|^3+n|^2+(2*n|^3+2*n|^2*2+2*n)+(n|^2+2*n+1))*2
      -(n|^2+2*n+1);
 hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (2*n-1) |^ 3 & s.0 = 0) implies
  for n st n>=1 holds Partial_Sums(s).n = n|^2*(2*n|^2-1)
proof
  assume
 A1:for n st n>=1 holds s.n = (2*n-1) |^ 3 & s.0 = 0;
  defpred X[Nat] means Partial_Sums(s).$1 = $1|^2*(2*$1|^2-1);
  Partial_Sums(s).(1+0) =Partial_Sums(s).0 + s.(1+0) by SERIES_1:def 1
  .=s.0 + s.1 by SERIES_1:def 1
  .=s.1 + 0 by A1
  .= (2*1-1)|^3 by A1 .=1|^(2+1) .=1|^2*(2*(1*1)-1) by NEWTON:11
  .=1|^2*(2*1|^2-1) by Lm1;then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
   A4: n>=1 & Partial_Sums(s).n = n|^2*(2*n|^2-1);
   A5: n+1>=1 by NAT_1:29;
     Partial_Sums(s).(n+1) = n|^2*(2*n|^2-1) + s.(n+1) by A4,SERIES_1:def 1
     .=n|^2*(2*n|^2-1) + (2*(n+1)-1) |^ 3 by A1,A5
     .=n|^2*(2*n|^2-1) + (2*n+1) |^3
     .=n|^2*(2*n|^2-1) + ((2*n)|^3+3*(2*n)|^2+3*(2*n)+1) by Lm4
     .=n|^2*2*n|^2-n|^2 + (2*n)|^3+3*(2*n)|^2+3*2*n+1
     .=n|^2*2*n|^2-n|^2 + (2*n)|^3+3*(2|^2*n|^2)+6*n+1 by NEWTON:12
     .=n|^2*2*n|^2-n|^2 + 2|^3*n|^3+3*(2|^2*n|^2)+6*n+1 by NEWTON:12
     .=n|^2*2*n|^2-n|^2 + 2|^3*n|^3+3*(2*2*n|^2)+6*n+1 by Lm1
     .=n|^2*2*n|^2-n|^2 + 2*2*2*n|^3+3*(2*2*n|^2)+6*n+1 by Lm1
     .=n|^2*n|^2*2+(12-1)*n|^2 + 8*n|^3+6*n+1
     .=(n+1)|^2*(2*(n+1)|^2-1) by Lm12;
     hence thesis;
   end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = n |^ 4) implies for n holds
   Partial_Sums(s).n = n*(n+1)*(2*n+1)*(3*n|^2+3*n-1)/30
proof
  assume
 A1:for n holds s.n = n |^ 4;
  defpred X[Nat] means
     Partial_Sums(s).$1=$1*($1+1)*(2*$1+1)*(3*$1|^2+3*$1-1)/30;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0 |^4 by A1
  .=0*(0+1)*(2*0+1)*(3*0|^2+3*0-1)/30 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n = n*(n+1)*(2*n+1)*(3*n|^2+3*n-1)/30;then
     Partial_Sums(s).(n+1) =n*(n+1)*(2*n+1)*(3*n|^2+3*n-1)/30 + s.(n+1)
                                     by SERIES_1:def 1
     .=n*(n+1)*(2*n+1)*(3*n|^2+3*n-1)/30 + (n+1)|^4 by A1
     .=(n*(n+1)*(2*n+1)*(3*n|^2+3*n-1)+ (n+1)|^4*30)/30
     .=(n*(n+1)*(2*n+1)*(3*n|^2+3*n-1)+ (n+1)|^(3+1)*30)/30
     .=(n*(n+1)*(2*n+1)*(3*n|^2+3*n-1)+ (n+1)|^3*(n+1)*30)/30 by NEWTON:11
     .=(n+1)*(n*(2*n+1)*(3*n|^2+3*n-1)+ (n+1)|^3*30)/30
     .=(n+1)*((n+2)*(2*(n+1)+1)*(3*(n+1)|^2+3*(n+1)-1))/30 by Lm5;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

Lm13: (n+2)*((n+1)|^2+(n+1)-1)=n|^3+5*n|^2+7*n+2
 proof (n+2)*((n+1)|^2+(n+1)-1)
   =(n+2)*(n|^2+2*n*1+1|^2+(n+1)-1) by Lm3
   .=(n+2)*(n|^2+2*n*1+1+(n+1)-1) by NEWTON:15
   .=n*n|^2+2*n|^2+3*(n*n)+6*n+n+2
   .=n*n|^2+2*n|^2+3*n|^2+6*n+n+2 by Lm1
   .=n|^(2+1)+2*n|^2+3*n|^2+6*n+n+2 by NEWTON:11
   .=n|^(2+1)+(2+3)*n|^2+(6+1)*n+2;
  hence thesis;
 end;

theorem
  (for n holds s.n = (-1)|^(n+1)*n|^4) implies for n holds
   Partial_Sums(s).n = (-1)|^(n+1)*n*(n+1)*(n|^2+n-1)/2
proof
  assume
 A1:for n holds s.n = (-1)|^(n+1)*n|^4;
  defpred X[Nat] means
     Partial_Sums(s).$1=(-1)|^($1+1)*$1*($1+1)*($1|^2+$1-1)/2;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=(-1)|^(0+1)*0 |^4 by A1
  .=(-1)|^(0+1)*0*(0+1)*(0|^2+0-1)/2 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n = (-1)|^(n+1)*n*(n+1)*(n|^2+n-1)/2;then
     Partial_Sums(s).(n+1) =(-1)|^(n+1)*n*(n+1)*(n|^2+n-1)/2 + s.(n+1)
                                     by SERIES_1:def 1
     .=(-1)|^(n+1)*n*(n+1)*(n|^2+n-1)/2 + (-1)|^(n+1+1)*(n+1)|^4 by A1
     .=((-1)|^(n+1)*n*(n+1)*(n|^2+n-1) + (-1)|^(n+1+1)*(n+1)|^4*2)/2
  .=((-1)|^(n+1)*(-1)*(-1)*n*(n+1)*(n|^2+n-1) + (-1)|^(n+2)*(n+1)|^4*2)/2
  .=((-1)|^(n+1+1)*(-1)*n*(n+1)*(n|^2+n-1)+(-1)|^(n+2)*(n+1)|^4*2)/2
                                                          by NEWTON:11
  .=((-1)|^(n+2)*(-1)*n*(n+1)*(n|^2+n-1)+(-1)|^(n+2)*(n+1)|^4*2)/2
  .=(-1)|^(n+2)*((-1)*n*(n+1)*(n|^2+n-1)+(n+1)|^4*2)/2
  .=(-1)|^(n+2)*((-1)*n*(n+1)*(n|^2+n-1)+(n+1)|^(3+1)*2)/2
  .=(-1)|^(n+2)*((-1)*n*(n+1)*(n|^2+n-1)+(n+1)|^3*(n+1)*2)/2 by NEWTON:11
  .=(-1)|^(n+2)*(n+1)*((-1)*(n*n|^2+n*n-n*1)+(n+1)|^3*2)/2
  .=(-1)|^(n+2)*(n+1)*((-1)*(n|^(2+1)+n*n-n*1)+(n+1)|^3*2)/2 by NEWTON:11
  .=(-1)|^(n+2)*(n+1)*((-1)*(n|^3+n|^2-n)+(n+1)|^3*2)/2 by Lm1
  .=(-1)|^(n+2)*(n+1)*((-1)*(n|^3+n|^2-n)+(n|^3+3*n|^2+3*n+1)*2)/2 by Lm4
  .=(-1)|^(n+2)*(n+1)*(n|^3+5*n|^2+7*n+2)/2
  .=(-1)|^(n+2)*(n+1)*((n+2)*((n+1)|^2+(n+1)-1))/2 by Lm13
  .=(-1)|^(n+2)*(n+1)*(n+2)*((n+1)|^2+(n+1)-1)/2;
    hence thesis;
    end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

Lm14:
   n|^2*(2*n|^2+2*n-1)+(n+1)|^3*12=(n+2)|^2*(2*(n+1)|^2+2*(n+1)-1)
proof
 A1:  (n+2)|^2*(2*(n+1)|^2+2*(n+1)-1)
   =(n+2)|^2*(2*(n|^2+2*n*1+1|^2)+2*(n+1)-1) by Lm3
   .=(n|^2+2*n*2+2|^2)*(2*n|^2+2*n*2+2*1|^2+(2*n+2*1)-1) by Lm3
   .=(n|^2+2*n*2+2|^2)*(2*n|^2+4*n+2*1+(2*n+2)-1) by NEWTON:15
   .=(n|^2+4*n+2*2)*(2*n|^2+4*n+2+2*n+2-1) by Lm1
   .=2*(n|^2*n|^2)+6*(n*n|^2)+n|^2*3+4*n*(2*n|^2+6*n+3)+(2*n|^2*4+24*n+12)
   .=2*n|^(2+2)+6*(n*n|^2)+n|^2*3+4*n*(2*n|^2+6*n+3)+(2*n|^2*4+24*n+12)
     by NEWTON:13
   .=2*n|^(2+2)+6*n|^(2+1)+n|^2*3+4*n*(2*n|^2+6*n+3)+(2*n|^2*4+24*n+12)
     by NEWTON:11
   .=2*n|^4+6*n|^3+n|^2*3+(4*(n*n|^2)*2+4*(n*n)*6+4*n*3)+(8*n|^2+24*n+12)
   .=2*n|^4+6*n|^3+n|^2*3+(4*n|^(2+1)*2+24*(n*n)+4*n*3)+(8*n|^2+24*n+12)
      by NEWTON:11
   .=2*n|^4+6*n|^3+n|^2*3+(4*n|^3*2+24*n|^2+12*n)+(8*n|^2+24*n+12) by Lm1
   .=2*n|^4+14*n|^3+27*n|^2+12*n+8*n|^2+24*n+12;
    n|^2*(2*n|^2+2*n-1)+(n+1)|^3*12
    =(2*(n|^2*n|^2)+2*(n*n|^2)-n|^2)+(n+1)|^3*12
   .=(2*n|^(2+2)+2*(n*n|^2)-n|^2)+(n+1)|^3*12 by NEWTON:13
   .=(2*n|^(2+2)+2*n|^(2+1)-n|^2)+(n+1)|^3*12 by NEWTON:11
   .=(2*n|^4+2*n|^3-n|^2)+(n|^3+3*n|^2+3*n+1)*12 by Lm4
   .=2*n|^4+2*n|^3-n|^2+n|^3*12+36*n|^2+36*n+12;
  hence thesis by A1;
end;

theorem
  (for n holds s.n = n|^5) implies for n holds
   Partial_Sums(s).n = n|^2*(n+1)|^2*(2*n|^2+2*n-1)/12
proof
  assume
 A1:for n holds s.n = n|^5;
  defpred X[Nat] means
     Partial_Sums(s).$1=$1|^2*($1+1)|^2*(2*$1|^2+2*$1-1)/12;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0 |^5 by A1
  .=0*(0+1)|^2*(2*0|^2+2*0-1)/12 by NEWTON:16
  .= 0|^2*(0+1)|^2*(2*0|^2+2*0-1)/12 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n = n|^2*(n+1)|^2*(2*n|^2+2*n-1)/12;then
     Partial_Sums(s).(n+1) =n|^2*(n+1)|^2*(2*n|^2+2*n-1)/12 + s.(n+1)
                                     by SERIES_1:def 1
     .=n|^2*(n+1)|^2*(2*n|^2+2*n-1)/12 + (n+1)|^5 by A1
     .=(n|^2*(n+1)|^2*(2*n|^2+2*n-1) + (n+1)|^5*12)/12
     .=(n|^2*(n+1)|^2*(2*n|^2+2*n-1)+(n+1)|^(3+2)*12)/12
     .=(n|^2*(n+1)|^2*(2*n|^2+2*n-1)+(n+1)|^3*(n+1)|^2*12)/12 by NEWTON:13
     .=((n+1)|^2*(n|^2*(2*n|^2+2*n-1)+(n+1)|^3*12))/12
     .=((n+1)|^2*((n+2)|^2*(2*(n+1)|^2+2*(n+1)-1)))/12 by Lm14
     .=(n+1)|^2*(n+2)|^2*(2*(n+1)|^2+2*(n+1)-1)/12;
    hence thesis;
    end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

Lm15:
(n+2)*(2*(n+1)+1)*(3*(n+1)|^4+6*(n+1)|^3-3*(n+1)+1)=
6*n|^6+57*n|^5+216*n|^4+414*n|^3+419*n|^2+211*n+42
proof
   (n+2)*(2*(n+1)+1)*(3*(n+1)|^4+6*(n+1)|^3-3*(n+1)+1)
   =(n+2)*(2*n+3)*(3*(n+1)|^4+6*(n+1)|^3-3*n-2)
  .=(n+2)*(2*n+3)*(3*(n|^4+4*n|^3+6*n|^2+4*n+1)+6*(n+1)|^3-3*n-2) by Lm6
  .=(n+2)*(2*n+3)*(3*n|^4+12*n|^3+18*n|^2+12*n+3+6*(n|^3+3*n|^2+3*n+1)-3*n-2)
     by Lm4
  .=((2*(n*n)+3*n)+(4*n+6))*(3*n|^4+18*n|^3+36*n|^2+27*n+7)
  .=((2*n|^2+3*n)+(4*n+6))*(3*n|^4+18*n|^3+36*n|^2+27*n+7) by Lm1
  .=(3*n|^4*(2*n|^2+7*n+6)+18*n|^3*(2*n|^2+7*n+6)+36*n|^2*(2*n|^2+7*n+6))
    +(27*(n*n|^2)*2+27*(n*n)*7+162*n)+(14*n|^2+49*n+42)
  .=(3*n|^4*(2*n|^2+7*n+6)+18*n|^3*(2*n|^2+7*n+6)+36*n|^2*(2*n|^2+7*n+6))
    +(27*n|^(2+1)*2+27*(n*n)*7+162*n)+(14*n|^2+49*n+42) by NEWTON:11
  .=(3*n|^4*(2*n|^2+7*n+6)+18*n|^3*(2*n|^2+7*n+6)+36*n|^2*(2*n|^2+7*n+6))
    +(54*n|^(2+1)+27*n|^2*7+162*n)+(14*n|^2+49*n+42) by Lm1
  .=(3*n|^4*(2*n|^2+7*n+6)+18*n|^3*(2*n|^2+7*n+6)+36*n|^2*(2*n|^2+7*n+6))
    +(54*n|^3+27*n|^2*7+162*n)+(14*n|^2+49*n+42)
  .=(3*(n|^4*n|^2)*2+3*(n|^4*n)*7+18*n|^4+(18*(n|^3*n|^2)*2+18*(n|^3*n)*7
    +108*n|^3)+36*n|^2*(2*n|^2+7*n+6))+(54*n|^3+203*n|^2+211*n+42)
  .=(3*n|^(4+2)*2+3*(n|^4*n)*7+18*n|^4+(18*(n|^3*n|^2)*2+18*(n|^3*n)*7+108*
     n|^3)+36*n|^2*(2*n|^2+7*n+6))+(54*n|^3+203*n|^2+211*n+42) by NEWTON:13
  .=(6*n|^(4+2)+3*n|^(4+1)*7+18*n|^4+(36*(n|^3*n|^2)+126*(n|^3*n)+108*n|^3)+
     36*n|^2*(2*n|^2+7*n+6))+(54*n|^3+203*n|^2+211*n+42) by NEWTON:11
  .=(6*n|^(4+2)+21*n|^(4+1)+18*n|^4+(36*(n|^3*n|^2)+126*n|^(3+1)+108*n|^3)+
     36*n|^2*(2*n|^2+7*n+6))+(54*n|^3+203*n|^2+211*n+42) by NEWTON:11
  .=(6*n|^(4+2)+21*n|^(4+1)+18*n|^4+(36*n|^(3+2)+126*n|^(3+1)+108*n|^3)+
     36*n|^2*(2*n|^2+7*n+6))+(54*n|^3+203*n|^2+211*n+42) by NEWTON:13
  .=(6*n|^6+21*n|^5+18*n|^4+(36*n|^5+126*n|^4+108*n|^3)+(36*(n|^2*n|^2)*2
    +36*(n|^2*n)*7+216*n|^2))+(54*n|^3+203*n|^2+211*n+42)
  .=(6*n|^6+21*n|^5+18*n|^4+(36*n|^5+126*n|^4+108*n|^3)+(36*n|^(2+2)*2
    +252*(n|^2*n)+216*n|^2))+(54*n|^3+203*n|^2+211*n+42) by NEWTON:13
  .=(6*n|^6+21*n|^5+18*n|^4+(36*n|^5+126*n|^4+108*n|^3)+(72*n|^(2+2)
    +252*n|^(2+1)+216*n|^2))+(54*n|^3+203*n|^2+211*n+42) by NEWTON:11
  .=(6*n|^6+21*n|^5+18*n|^4+(36*n|^5+126*n|^4+108*n|^3)+(72*n|^4
    +252*n|^3+216*n|^2))+(54*n|^3+203*n|^2+211*n+42);
  hence thesis;
end;

theorem
  (for n holds s.n = n|^6) implies for n holds
   Partial_Sums(s).n = n*(n+1)*(2*n+1)*(3*n|^4+6*n|^3-3*n+1)/42
proof
  assume
 A1:for n holds s.n = n|^6;
  defpred X[Nat] means
  Partial_Sums(s).$1=$1*($1+1)*(2*$1+1)*(3*$1|^4+6*$1|^3-3*$1+1)/42;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0 |^6 by A1
  .= 0*(0+1)*(2*0+1)*(3*0|^4+6*0|^3-3*0+1)/42 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n = n*(n+1)*(2*n+1)*(3*n|^4+6*n|^3-3*n+1)/42;then
     Partial_Sums(s).(n+1)
     =n*(n+1)*(2*n+1)*(3*n|^4+6*n|^3-3*n+1)/42+s.(n+1) by SERIES_1:def 1
    .=n*(n+1)*(2*n+1)*(3*n|^4+6*n|^3-3*n+1)/42+(n+1)|^6 by A1
    .=(n*(n+1)*(2*n+1)*(3*n|^4+6*n|^3-3*n+1)+(n+1)|^6*42)/42
    .=(n*(n+1)*(2*n+1)*(3*n|^4+6*n|^3-3*n+1)+(n+1)|^(5+1)*42)/42
    .=(n*(n+1)*(2*n+1)*(3*n|^4+6*n|^3-3*n+1)+(n+1)|^5*(n+1)*42)/42
                                                        by NEWTON:11
    .=(n+1)*((2*(n*n)+n)*(3*n|^4+6*n|^3-3*n+1)+(n+1)|^5*42)/42
    .=(n+1)*((2*n|^2+n)*(3*n|^4+6*n|^3-3*n+1)+(n+1)|^5*42)/42 by Lm1
    .=(n+1)*(3*(n|^4*n|^2)*2+3*(n|^4*n)+(6*(n|^3*n|^2)*2+6*(n|^3*n))
      -(3*(n*n|^2)*2+3*(n*n))+(2*n|^2+n)+(n+1)|^5*42)/42
    .=(n+1)*(3*n|^(4+2)*2+3*(n|^4*n)+(6*(n|^3*n|^2)*2+6*(n|^3*n))
      -(3*(n*n|^2)*2+3*(n*n))+(2*n|^2+n)+(n+1)|^5*42)/42 by NEWTON:13
    .=(n+1)*(3*n|^(4+2)*2+3*(n|^4*n)+(6*n|^(3+2)*2+6*(n|^3*n))
      -(3*(n*n|^2)*2+3*(n*n))+(2*n|^2+n)+(n+1)|^5*42)/42 by NEWTON:13
    .=(n+1)*(3*n|^(4+2)*2+3*n|^(4+1)+(6*n|^(3+2)*2+6*(n|^3*n))
      -(3*(n*n|^2)*2+3*(n*n))+(2*n|^2+n)+(n+1)|^5*42)/42 by NEWTON:11
    .=(n+1)*(3*n|^(4+2)*2+3*n|^(4+1)+(6*n|^(3+2)*2+6*(n|^3*n))
      -(3*n|^(2+1)*2+3*(n*n))+(2*n|^2+n)+(n+1)|^5*42)/42 by NEWTON:11
    .=(n+1)*(3*n|^6*2+3*n|^5+(6*n|^5*2+6*(n|^3*n))
      -(3*n|^3*2+3*n|^2)+(2*n|^2+n)+(n+1)|^5*42)/42 by Lm1
    .=(n+1)*(6*n|^6+3*n|^5+(12*n|^5+6*n|^(3+1))
      -(6*n|^3+3*n|^2)+(2*n|^2+n)+(n+1)|^5*42)/42 by NEWTON:11
    .=(n+1)*(6*n|^6+3*n|^5+(12*n|^5+6*n|^4)-(6*n|^3+3*n|^2)+(2*n|^2+n)
       +(n+1)|^5*42)/42
    .=(n+1)*(6*n|^6+6*n|^4+15*n|^5-6*n|^3-1*n|^2+n+(n|^5+5*n|^4+10*n|^3
       +10*n|^2+5*n+1)*42)/42 by Lm6
    .=(n+1)*(6*n|^6+57*n|^5+216*n|^4+414*n|^3+419*n|^2+211*n+42)/42
    .=(n+1)*((n+2)*(2*(n+1)+1)*(3*(n+1)|^4+6*(n+1)|^3-3*(n+1)+1))/42
            by Lm15
    .=(n+1)*(n+1+1)*(2*(n+1)+1)*(3*(n+1)|^4+6*(n+1)|^3-3*(n+1)+1)/42;
    hence thesis;
    end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

Lm16:
  (n+2)|^2*(3*(n+1)|^4+6*(n+1)|^3-(n+1)|^2-4*(n+1)+2)
  =n|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)+ (n+1)|^5*24
proof
 A1: (n+2)|^2*(3*(n+1)|^4+6*(n+1)|^3-(n+1)|^2-4*(n+1)+2)
   =(n+2)|^2*(3*(n+1)|^4+6*(n+1)|^3-(n+1)|^2-4*n-4+2)
  .=(n+2)|^2*(3*(n+1)|^4+6*(n+1)|^3-(n|^2+2*n*1+1|^2)-4*n-4+2) by Lm3
  .=(n+2)|^2*(3*(n+1)|^4+6*(n+1)|^3-(n|^2+2*n*1+1|^2)-4*n-2)
  .=(n+2)|^2*(3*(n+1)|^4+6*(n|^3+3*n|^2+3*n+1)-(n|^2+2*n+1|^2)-4*n-2) by Lm4
  .=(n+2)|^2*(3*(n+1)|^4+6*(n|^3+3*n|^2+3*n+1)-(n|^2+2*n+1)-4*n-2) by NEWTON:15
  .=(n+2)|^2*(3*(n+1)|^4+6*n|^3+(18-1)*n|^2+(18-2-4)*n+3)
  .=(n+2)|^2*(3*(n|^4+4*n|^3+6*n|^2+4*n+1)+6*n|^3+17*n|^2+12*n+3) by Lm6
  .=(n+2)|^2*(3*n|^4+(12*n|^3+6*n|^3)+(18*n|^2+17*n|^2)+24*n+6)
  .=(n|^2+2*n*2+2*2)*(3*n|^4+18*n|^3+35*n|^2+24*n+6) by Lm3
  .=3*(n|^4*n|^2)+18*(n|^3*n|^2)+35*(n|^2*n|^2)+24*(n*n|^2)+n|^2*6+4*n*(3
    *n|^4+18*n|^3+35*n|^2+24*n+6)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24)
  .=3*n|^(4+2)+18*(n|^3*n|^2)+35*(n|^2*n|^2)+24*(n*n|^2)+n|^2*6+4*n*(3*n|^4+
    18*n|^3+35*n|^2+24*n+6)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24) by NEWTON:13
  .=3*n|^(4+2)+18*n|^(3+2)+35*(n|^2*n|^2)+24*(n*n|^2)+n|^2*6+4*n*(3*n|^4+
    18*n|^3+35*n|^2+24*n+6)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24) by NEWTON:13
  .=3*n|^(4+2)+18*n|^(3+2)+35*n|^(2+2)+24*(n*n|^2)+n|^2*6+4*n*(3*n|^4+
    18*n|^3+35*n|^2+24*n+6)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24) by NEWTON:13
  .=3*n|^(4+2)+18*n|^(3+2)+35*n|^(2+2)+24*n|^(2+1)+n|^2*6+4*n*(3*n|^4+
    18*n|^3+35*n|^2+24*n+6)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24) by NEWTON:11
  .=3*n|^6+18*n|^5+35*n|^4+24*n|^3+n|^2*6+(12*(n|^4*n)+72*(n|^3*n)+140*(n|^2
    *n)+96*(n*n)+24*n)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24)
  .=3*n|^6+18*n|^5+35*n|^4+24*n|^3+n|^2*6+(12*n|^(4+1)+72*(n|^3*n)+140*(n|^2
    *n)+96*(n*n)+24*n)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24) by NEWTON:11
  .=3*n|^6+18*n|^5+35*n|^4+24*n|^3+n|^2*6+(12*n|^(4+1)+72*n|^(3+1)+140*(n|^2
    *n)+96*(n*n)+24*n)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24) by NEWTON:11
  .=3*n|^6+18*n|^5+35*n|^4+24*n|^3+n|^2*6+(12*n|^(4+1)+72*n|^(3+1)+140*n|^(2
    +1)+96*(n*n)+24*n)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24) by NEWTON:11
  .=3*n|^6+18*n|^5+35*n|^4+24*n|^3+n|^2*6+(12*n|^5+72*n|^4+140*n|^3+96*n|^2
    +24*n)+((12*n|^4+72*n|^3+140*n|^2)+96*n+24) by Lm1
  .=3*n|^6+30*n|^5+119*n|^4+236*n|^3+242*n|^2+120*n+24;
   n|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)+ (n+1)|^5*24
   =3*(n|^4*n|^2)+6*(n|^3*n|^2)-n|^2*n|^2-4*(n*n|^2)+n|^2*2+ (n+1)|^5*24
  .=3*n|^(4+2)+6*(n|^3*n|^2)-n|^2*n|^2-4*(n*n|^2)+n|^2*2+ (n+1)|^5*24
      by NEWTON:13
  .=3*n|^(4+2)+6*n|^(3+2)-n|^2*n|^2-4*(n*n|^2)+n|^2*2+ (n+1)|^5*24 by NEWTON:13
  .=3*n|^(4+2)+6*n|^(3+2)-n|^(2+2)-4*(n*n|^2)+n|^2*2+ (n+1)|^5*24 by NEWTON:13
  .=3*n|^(4+2)+6*n|^(3+2)-n|^(2+2)-4*n|^(2+1)+n|^2*2+ (n+1)|^5*24 by NEWTON:11
  .=3*n|^6+6*n|^5-n|^4-4*n|^3+n|^2*2+(n|^5+5*n|^4+10*n|^3+10*n|^2+5*n+1)*24
     by Lm6
  .=3*n|^6+30*n|^5+119*n|^4+236*n|^3+n|^2*242+120*n+24;
  hence thesis by A1;
end;

theorem
  (for n holds s.n = n|^7) implies for n holds
   Partial_Sums(s).n = n|^2*(n+1)|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)/24
proof
  assume
 A1:for n holds s.n = n|^7;
  defpred X[Nat] means
     Partial_Sums(s).$1=$1|^2*($1+1)|^2*(3*$1|^4+6*$1|^3-$1|^2-4*$1+2)/24;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0 |^7 by A1
  .= 0*(0+1)|^2*(3*0|^4+6*0|^3-0|^2-4*0+2)/24 by NEWTON:16
  .= 0|^2*(0+1)|^2*(3*0|^4+6*0|^3-0|^2-4*0+2)/24 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n =n|^2*(n+1)|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)/24;then
     Partial_Sums(s).(n+1)
   =n|^2*(n+1)|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)/24+ s.(n+1) by SERIES_1:def 1
  .=n|^2*(n+1)|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)/24+ (n+1)|^7 by A1
  .=(n|^2*(n+1)|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)+ (n+1)|^7*24)/24
  .=(n|^2*(n+1)|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)+ (n+1)|^(5+2)*24)/24
  .=(n|^2*(n+1)|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)+ (n+1)|^5*(n+1)|^2*24)/24
    by NEWTON:13
  .=(n+1)|^2*(n|^2*(3*n|^4+6*n|^3-n|^2-4*n+2)+ (n+1)|^5*24)/24
  .=(n+1)|^2*((n+2)|^2*(3*(n+1)|^4+6*(n+1)|^3-(n+1)|^2-4*(n+1)+2))/24
          by Lm16
  .=(n+1)|^2*(n+2)|^2*(3*(n+1)|^4+6*(n+1)|^3-(n+1)|^2-4*(n+1)+2)/24
  .=(n+1)|^2*(n+1+1)|^2*(3*(n+1)|^4+6*(n+1)|^3-(n+1)|^2-4*(n+1)+2)/24;
    hence thesis;
    end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n holds s.n = n*(n+1)|^2) implies for n holds
   Partial_Sums(s).n = n*(n+1)*(n+2)*(3*n+5)/12
proof
  assume
 A1:for n holds s.n = n*(n+1)|^2;
  defpred X[Nat] means
     Partial_Sums(s).$1=$1*($1+1)*($1+2)*(3*$1+5)/12;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0*(0+1)|^2 by A1
  .=0*(0+1)*(0+2)*(3*0+5)/12;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n =n*(n+1)*(n+2)*(3*n+5)/12;then
     Partial_Sums(s).(n+1)
   =n*(n+1)*(n+2)*(3*n+5)/12+ s.(n+1) by SERIES_1:def 1
  .=n*(n+1)*(n+2)*(3*n+5)/12+ (n+1)*(n+1+1)|^2 by A1
  .=n*(n+1)*(n+2)*(3*n+5)/12+ (n+1)*(n+2)|^2*12/12
  .=(n+1)*(n*(n+2)*(3*n+5)+ (n+2)|^2*12)/12
  .=(n+1)*(n*(n+2)*(3*n+5)+ (n+2)*(n+2)*12)/12 by Lm1
  .=(n+1)*(n+2)*(n+3)*(3*(n+1)+5)/12;
    hence thesis;
    end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n holds s.n = n*(n+1)|^2*(n+2)) implies for n holds
   Partial_Sums(s).n = n*(n+1)*(n+2)*(n+3)*(2*n+3)/10
proof
  assume
 A1:for n holds s.n =n*(n+1)|^2*(n+2);
  defpred X[Nat] means
     Partial_Sums(s).$1=$1*($1+1)*($1+2)*($1+3)*(2*$1+3)/10;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .=0*(0+1)|^2*(0+2) by A1
  .=0*(0+1)*(0+2)*(0+3)*(2*0+3)/10;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n =n*(n+1)*(n+2)*(n+3)*(2*n+3)/10;then
     Partial_Sums(s).(n+1)
   =n*(n+1)*(n+2)*(n+3)*(2*n+3)/10+ s.(n+1) by SERIES_1:def 1
  .=n*(n+1)*(n+2)*(n+3)*(2*n+3)/10+ (n+1)*(n+1+1)|^2*(n+1+2) by A1
  .=n*(n+1)*(n+2)*(n+3)*(2*n+3)/10+ (n+1)*((n+2)*(n+2))*(n+3)*10/10 by Lm1
  .=(n+1)*(n+2)*(n+3)*(n+4)*(2*(n+1)+3)/10;
    hence thesis;
    end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n holds s.n = n*(n+1)*2|^n) implies for n holds
   Partial_Sums(s).n = 2|^(n+1)*(n|^2-n+2)-4
proof
  assume
 A1:for n holds s.n = n*(n+1)*2|^n;
  defpred X[Nat] means
     Partial_Sums(s).$1=2|^($1+1)*($1|^2-$1+2)-4;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1 .= 0*(0+1)*2|^0 by A1
  .=2*2-4 .=2|^(0+1)*(0-0+2)-4 by NEWTON:10
  .=2|^(0+1)*(0|^2-0+2)-4 by NEWTON:16;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
     Partial_Sums(s).n = 2|^(n+1)*(n|^2-n+2)-4;then
     Partial_Sums(s).(n+1)
   =2|^(n+1)*(n|^2-n+2)-4 + s.(n+1) by SERIES_1:def 1
   .=2|^(n+1)*(n|^2-n+2)-4 + (n+1)*(n+1+1)*2|^(n+1) by A1
   .=2|^(n+1)*2*((n|^2-n+2) + (n+1)*(n+2))/2-4
   .=2|^(n+1+1)*((n|^2-n+2) + (n+1)*(n+2))/2-4 by NEWTON:11
   .=2|^(n+2)*(n|^2-n+2 + n*n+1*n+n*2+2)/2-4
   .=2|^(n+2)*(n|^2-n+2 + n|^2+1*n+n*2+2)/2-4 by Lm1
   .=2|^(n+2)*(n|^2 + 2*n +1-1- n + 2)-4
   .=2|^(n+2)*(n|^2 + 2*n*1 +1|^2-1- n + 2)-4 by NEWTON:15
   .=2|^(n+2)*((n+1)|^2-1- n + 2)-4 by Lm3
   .=2|^(n+2)*((n+1)|^2-(1+ n) + 2)-4;
    hence thesis;
    end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=2 holds s.n = 1/((n-1)*(n+1)) & s.0=0 & s.1=0)
  implies for n st n>=2 holds Partial_Sums(s).n = 3/4-1/(2*n)-1/(2*(n+1))
proof
  assume
 A1:for n st n>=2 holds s.n = 1/((n-1)*(n+1)) & s.0 = 0 & s.1 = 0;
  defpred X[Nat] means
     Partial_Sums(s).$1=3/4-1/(2*$1)-1/(2*($1+1));
  Partial_Sums(s).(1+1) =Partial_Sums(s).(1+0)+s.2 by SERIES_1:def 1
  .=Partial_Sums(s).0+s.1+s.2 by SERIES_1:def 1
  .=s.0 + s.1 + s.2 by SERIES_1:def 1  .= 0 + s.1 + s.2 by A1
  .= 0 + s.2 by A1
  .= 1/((2-1)*(2+1)) by A1
  .=3/4-1/(2*2)-1/(2*(2+1));then
 A2: X[2];
 A3:for n st n>=2 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=2 & Partial_Sums(s).n = 3/4-1/(2*n)-1/(2*(n+1));
    A5:n>0 by A4,AXIOMS:22;then
    n+1>1 by REAL_1:69; then
    A6:n+1>=1+1 by NAT_1:38;
       n+2>=n by NAT_1:29;then
    A7:n+2>0 by A5,AXIOMS:22;
    Partial_Sums(s).(n+1) =3/4-1/(2*n)-1/(2*(n+1))+ s.(n+1)
                                     by A4,SERIES_1:def 1
   .=3/4-1/(2*n)-1/(2*(n+1))+1/((n+1-1)*(n+1+1)) by A1,A6
   .=3/4-1/(2*n)-1/(2*(n+1))+1/(n*(n+2))
   .=3/4-1/(2*(n+1))+(1/(n*(n+2))-1/(2*n))
   .=3/4-1/(2*(n+1))+(1/(n*(n+2))-1/2*(1/n)) by XCMPLX_1:103
   .=3/4-1/(2*(n+1))+(1/n*(1/(n+2))-1/2*(1/n)) by XCMPLX_1:103
   .=3/4-1/(2*(n+1))+(1/(n+2)-1/2)*(1/n)
   .=3/4-1/(2*(n+1))+(1*2-1*(n+2))/((n+2)*2)*(1/n) by A7,XCMPLX_1:131
   .=3/4-1/(2*(n+1))+(-n)/n*(1/((n+2)*2)) by XCMPLX_1:86
   .=3/4-1/(2*(n+1))+(-1)*(1/((n+2)*2)) by A5,XCMPLX_1:198
   .=3/4-1/(2*(n+1))+-(1*(1/((n+2)*2)))
   .=3/4-1/(2*(n+1))+-(1/(2*(n+1+1)));
    hence thesis;
    end;
   for n st n>=2 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = 1/((2*n-1)*(2*n+1)) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n = n/(2*n+1)
proof
  assume
 A1:for n st n>=1 holds s.n = 1/((2*n-1)*(2*n+1)) & s.0=0;
  defpred X[Nat] means
     Partial_Sums(s).$1= $1/(2*$1+1);
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=1/((2*1-1)*(2*1+1)) by A1
  .= 1/(2*1+1);then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n = n/(2*n+1);
    A5: n+1>=1 by NAT_1:29;
        2*n+3>=3 & 2*n+1>=1 by NAT_1:29;then
    A6: 2*n+3>0 & 2*n+1>0 by AXIOMS:22;
     Partial_Sums(s).(n+1) =n/(2*n+1)+ s.(n+1) by A4,SERIES_1:def 1
  .=n/(2*n+1)+1/((2*(n+1)-1)*(2*(n+1)+1)) by A1,A5
  .=n/(2*n+1)+1/(2*n+1)*(1/(2*n+3)) by XCMPLX_1:103
  .=n*(1/(2*n+1))+1/(2*n+1)*(1/(2*n+3)) by XCMPLX_1:100
  .=(n+1/(2*n+3))*(1/(2*n+1))
  .=((n*(2*n+3)+1)/(2*n+3))*(1/(2*n+1)) by A6,XCMPLX_1:114
  .=((n+1)*(2*n+1))/(2*n+3)*(1/(2*n+1))
  .=(2*n+1)*((n+1)/(2*n+3))*(1/(2*n+1)) by XCMPLX_1:75
  .=(n+1)/(2*(n+1)+1) by A6,XCMPLX_1:108;
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = 1/((3*n-2)*(3*n+1)) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n = n/(3*n+1)
proof
  assume
 A1:for n st n>=1 holds s.n = 1/((3*n-2)*(3*n+1)) & s.0=0;
  defpred X[Nat] means Partial_Sums(s).$1= $1/(3*$1+1);
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=1/((3*1-2)*(3*1+1)) by A1
  .= 1/(3*1+1);then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n = n/(3*n+1);
    A5: n+1>=1 by NAT_1:29;
        3*n+4>=4 & 3*n+1>=1 by NAT_1:29;then
    A6: 3*n+4>0 & 3*n+1>0 by AXIOMS:22;
     Partial_Sums(s).(n+1) =n/(3*n+1)+ s.(n+1) by A4,SERIES_1:def 1
  .=n/(3*n+1)+1/((3*(n+1)-2)*(3*(n+1)+1)) by A1,A5
  .=n/(3*n+1)+1/(3*n+1)*(1/(3*n+4)) by XCMPLX_1:103
  .=n*(1/(3*n+1))+1/(3*n+1)*(1/(3*n+4)) by XCMPLX_1:100
  .=(n+1/(3*n+4))*(1/(3*n+1))
  .=((n*(3*n+4)+1)/(3*n+4))*(1/(3*n+1)) by A6,XCMPLX_1:114
  .=((n+1)*(3*n+1))/(3*n+4)*(1/(3*n+1))
  .=(3*n+1)*((n+1)/(3*n+4))*(1/(3*n+1)) by XCMPLX_1:75
  .=(n+1)/(3*(n+1)+1) by A6,XCMPLX_1:108;
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = 1/((2*n-1)*(2*n+1)*(2*n+3)) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n =1/12-1/(4*(2*n+1)*(2*n+3))
proof
  assume
 A1:for n st n>=1 holds s.n =1/((2*n-1)*(2*n+1)*(2*n+3)) & s.0=0;
  defpred X[Nat] means
     Partial_Sums(s).$1= 1/12-1/(4*(2*$1+1)*(2*$1+3));
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=1/((2*1-1)*(2*1+1)*(2*1+3)) by A1
   .= 1/12-1/(4*(2*1+1)*(2*1+3));then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n =1/12-1/(4*(2*n+1)*(2*n+3));
    A5: n+1>=1 by NAT_1:29;
        2*n+5>=5 & 2*n+1>=1 by NAT_1:29;then
    A6: 2*n+5>0 & 2*n+1>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)=1/12-1/(4*(2*n+1)*(2*n+3))+ s.(n+1)
                                          by A4,SERIES_1:def 1
  .=1/12-1/(4*(2*n+1)*(2*n+3))+1/((2*(n+1)-1)*(2*(n+1)+1)*(2*(n+1)+3)) by A1,A5
  .=1/12-1/(4*((2*n+1)*(2*n+3)))+1/((2*n+1)*(2*n+3)*(2*n+5))
  .=1/12-(1/4)*(1/((2*n+1)*(2*n+3)))+1/((2*n+1)*(2*n+3)*(2*n+5))
                                                    by XCMPLX_1:103
  .=1/12-(1/4)*(1/((2*n+1)*(2*n+3)))+(1/((2*n+1)*(2*n+3)))*(1/(2*n+5))
                                                     by XCMPLX_1:103
  .=1/12-1/((2*n+1)*(2*n+3))*(1/4-1/(2*n+5))
  .=1/12-1/((2*n+1)*(2*n+3))*((1*(2*n+5)-1*4)/(4*(2*n+5))) by A6,XCMPLX_1:131
  .=1/12-(1/(2*n+1))*(1/(2*n+3))*((2*n+1)/(4*(2*n+5))) by XCMPLX_1:103
  .=1/12-(1/(2*n+1))*(1/(2*n+3))*(2*n+1)/(4*(2*n+5)) by XCMPLX_1:75
  .=1/12-(1/(2*n+3))/(4*(2*n+5)) by A6,XCMPLX_1:110
  .=1/12-1/((2*n+3)*(4*(2*n+5))) by XCMPLX_1:79
  .=1/12-1/(4*(2*(n+1)+1)*(2*(n+1)+3));
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = 1/((3*n-2)*(3*n+1)*(3*n+4)) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n =1/24-1/(6*(3*n+1)*(3*n+4))
proof
  assume
 A1:for n st n>=1 holds s.n =1/((3*n-2)*(3*n+1)*(3*n+4)) & s.0=0;
  defpred X[Nat] means
     Partial_Sums(s).$1= 1/24-1/(6*(3*$1+1)*(3*$1+4));
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=1/((3*1-2)*(3*1+1)*(3*1+4)) by A1
   .=1/24-1/(6*(3*1+1)*(3*1+4));then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n =1/24-1/(6*(3*n+1)*(3*n+4));
    A5: n+1>=1 by NAT_1:29;
        3*n+7>=7 & 3*n+1>=1 by NAT_1:29;then
    A6: 3*n+7>0 & 3*n+1>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)=1/24-1/(6*(3*n+1)*(3*n+4))+ s.(n+1)
                                          by A4,SERIES_1:def 1
  .=1/24-1/(6*(3*n+1)*(3*n+4))+1/((3*(n+1)-2)*(3*(n+1)+1)*(3*(n+1)+4)) by A1,A5
  .=1/24-1/(6*((3*n+1)*(3*n+4)))+1/(((3*n+1)*(3*n+4))*(3*n+7))
  .=1/24-(1/6)*(1/((3*n+1)*(3*n+4)))+1/(((3*n+1)*(3*n+4))*(3*n+7))
                                                    by XCMPLX_1:103
  .=1/24-(1/6)*(1/((3*n+1)*(3*n+4)))+(1/((3*n+1)*(3*n+4)))*(1/(3*n+7))
                                                    by XCMPLX_1:103
  .=1/24-(1/((3*n+1)*(3*n+4)))*(1/6-1/(3*n+7))
  .=1/24-(1/((3*n+1)*(3*n+4)))*((1*(3*n+7)-1*6)/(6*(3*n+7))) by A6,XCMPLX_1:131
  .=1/24-(1/(3*n+4))*(1/(3*n+1))*((3*n+1)/(6*(3*n+7))) by XCMPLX_1:103
  .=1/24-(1/(3*n+4))*(1/(3*n+1))*(3*n+1)/(6*(3*n+7)) by XCMPLX_1:75
  .=1/24-(1/(3*n+4))/(6*(3*n+7)) by A6,XCMPLX_1:110
  .=1/24-1/((3*n+4)*(6*(3*n+7))) by XCMPLX_1:79
  .=1/24-1/(6*(3*(n+1)+1)*(3*(n+1)+4));
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (2*n-1)/(n*(n+1)*(n+2)) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n = 3/4-2/(n+2)+1/(2*(n+1)*(n+2))
proof
  assume
 A1:for n st n>=1 holds s.n = (2*n-1)/(n*(n+1)*(n+2)) & s.0=0;
  defpred X[Nat] means
     Partial_Sums(s).$1= 3/4-2/($1+2)+1/(2*($1+1)*($1+2));
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=(2*1-1)/(1*(1+1)*(1+2)) by A1
  .= 3/4-2/(1+2)+1/(2*(1+1)*(1+2));then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n = 3/4-2/(n+2)+1/(2*(n+1)*(n+2));
    A5: n+1>=1+0 by NAT_1:29; then
    A6: n+1>0 by NAT_1:38;
        2*n+3>=3 & 2*n+1>=1 & n+2>=2 & n+3>=3 by NAT_1:29;then
    A7: 2*n+3>0 & 2*n+1>0 & n+2>0 &  n+3>0 by AXIOMS:22;then
    A8:(n+1)*(n+2)>0 by A6,REAL_2:122;
     Partial_Sums(s).(n+1) = 3/4-2/(n+2)+1/(2*(n+1)*(n+2))+ s.(n+1)
                                                        by A4,SERIES_1:def 1
  .= 3/4-2/(n+2)+1/(2*(n+1)*(n+2))
     + (2*(n+1)-1)/((n+1)*((n+1)+1)*((n+1)+2)) by A1,A5
  .= 3/4-2/(n+2)+1/(2*((n+1)*(n+2)))+(2*n+1)/(((n+1)*(n+2))*(n+3))
  .= 3/4-(2/(n+2)-1/(2*((n+1)*(n+2))))+(2*n+1)/(((n+1)*(n+2))*(n+3))
  .= 3/4-((2*(n+1))/((n+2)*(n+1))-1/(2*((n+1)*(n+2))))
                             +(2*n+1)/(((n+1)*(n+2))*(n+3)) by A6,XCMPLX_1:92
  .= 3/4-((2*(n+1)*2)/(2*(n+2)*(n+1))-1/(2*((n+1)*(n+2))))
                             +(2*n+1)/(((n+1)*(n+2))*(n+3)) by XCMPLX_1:92
  .= 3/4-((4*(n+1))-1)/(2*((n+2)*(n+1)))
                             +(2*n+1)/(((n+1)*(n+2))*(n+3)) by XCMPLX_1:121
  .= 3/4-((4*n+3)*(n+3))/(2*((n+2)*(n+1))*(n+3))
            +(2*n+1)/(((n+1)*(n+2))*(n+3)) by A7,XCMPLX_1:92
  .= 3/4-((4*n+3)*(n+3))/(2*((n+2)*(n+1))*(n+3))
            +((2*n+1)*2)/(((n+1)*(n+2))*(n+3)*2) by XCMPLX_1:92
  .= 3/4-(((4*n+3)*(n+3))/(2*((n+2)*(n+1))*(n+3))
            -((2*n+1)*2)/(((n+1)*(n+2))*(n+3)*2))
  .= 3/4-((4*n+3)*(n+3)-((2*n+1)*2))/(2*((n+2)*(n+1))*(n+3))by XCMPLX_1:121
  .= 3/4-(4*(n+1)*(n+2)-(n+1))/(2*((n+2)*(n+1))*(n+3))
  .= 3/4-((4*((n+1)*(n+2)))/(2*(n+3)*((n+2)*(n+1)))
         -(n+1)/(2*((n+2)*(n+1))*(n+3))) by XCMPLX_1:121
  .= 3/4-(4/(2*(n+3))-1*(n+1)/(2*(n+2)*(n+3)*(n+1))) by A8,XCMPLX_1:92
  .= 3/4-((2*2)/((n+3)*2)-1/(2*(n+2)*(n+3))) by A6,XCMPLX_1:92
  .= 3/4-(2/((n+1+2))-1/(2*(n+1+1)*(n+1+2))) by XCMPLX_1:92;
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (n+2)/(n*(n+1)*(n+3)) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n
           = 29/36-1/(n+3)-3/(2*(n+2)*(n+3))-4/(3*(n+1)*(n+2)*(n+3))
proof
  assume
 A1:for n st n>=1 holds s.n = (n+2)/(n*(n+1)*(n+3)) & s.0=0;
  defpred X[Nat] means Partial_Sums(s).$1
     = 29/36-1/($1+3)-3/(2*($1+2)*($1+3))-4/(3*($1+1)*($1+2)*($1+3));
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=(1+2)/(1*(1+1)*(1+3)) by A1
   .=29/36-1/(1+3)-3/(2*(1+2)*(1+3))-4/(3*(1+1)*(1+2)*(1+3));then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n
    = 29/36-1/(n+3)-3/(2*(n+2)*(n+3))-4/(3*(n+1)*(n+2)*(n+3));
    A5: n+1>=1+0 by NAT_1:29;then
    A6: n+1>0 by NAT_1:38;
        n+2>=2 & n+3>=3 & n+4>=4 by NAT_1:29;then
    A7: 2<>0 & n+2>0 & n+3>0 & n+4>0 by AXIOMS:22;then
    A8:(n+1)*(n+2)>0 & 2*(n+1)>0 by A6,REAL_2:122;then
    A9:(n+1)*(n+2)*(n+3)>0 &(n+1)*(n+2)*3>0 by A7,REAL_2:122;then
    A10:(n+1)*(n+2)*(n+3)*6>0 by REAL_2:122;
     Partial_Sums(s).(n+1)
   =29/36-1/(n+3)-3/(2*(n+2)*(n+3))-4/(3*(n+1)*(n+2)*(n+3))+s.(n+1)
                                                        by A4,SERIES_1:def 1
  .=29/36-1/(n+3)-3/(2*(n+2)*(n+3))-4/(3*(n+1)*(n+2)*(n+3))+(n+1+2)/((n+1)
    *(n+1+1)*(n+1+3)) by A1,A5
  .=29/36-(1*(n+2))/((n+3)*(n+2))-3/(2*(n+2)*(n+3))-4/(3*(n+1)*(n+2)
    *(n+3))+(n+3)/((n+1)*(n+2)*(n+4)) by A7,XCMPLX_1:92
  .=29/36-((n+2)*2)/((n+2)*(n+3)*2)-3/(2*(n+2)*(n+3))-4/(3*(n+1)*(n+2)
    *(n+3))+(n+3)/((n+1)*(n+2)*(n+4)) by XCMPLX_1:92
  .=29/36-(((n+2)*2)/((n+2)*(n+3)*2)+3/(2*(n+2)*(n+3)))-4/(3*(n+1)*(n+2)
    *(n+3))+(n+3)/((n+1)*(n+2)*(n+4))
  .=29/36-(((n+2)*2)+3)/(2*(n+2)*(n+3))-4/(3*(n+1)*(n+2)
    *(n+3))+(n+3)/((n+1)*(n+2)*(n+4)) by XCMPLX_1:63
  .=29/36-((n*2+7)*(n+1))/(2*(n+2)*(n+3)*(n+1))-4/(3*(n+1)*(n+2)*(n+3))
    +(n+3)/((n+1)*(n+2)*(n+4)) by A6,XCMPLX_1:92
  .=29/36-((n*2+7)*(n+1)*3)/(2*(n+2)*(n+3)*(n+1)*3)-4/(3*(n+1)*(n+2)*(n+3))
    +(n+3)/((n+1)*(n+2)*(n+4)) by XCMPLX_1:92
  .=29/36-((n*2+7)*(n+1)*3)/(2*(n+2)*(n+3)*(n+1)*3)-(4*2)/(3*(n+1)*(n+2)*(n+3)
   *2)+(n+3)/((n+1)*(n+2)*(n+4)) by XCMPLX_1:92
  .=29/36-((n*2+7)*(n+1)*3)/(6*(n+2)*(n+3)*(n+1))-8/(6*(n+1)*(n+2)*(n+3))
    +(n+3)/((n+1)*(n+2)*(n+4))
  .=29/36-(((n*2+7)*(n+1)*3)/(6*(n+2)*(n+3)*(n+1))+8/(6*(n+1)*(n+2)*(n+3)))
    +(n+3)/((n+1)*(n+2)*(n+4))
  .=29/36-(((n*2+7)*(n+1)*3*(n+4))/(6*(n+2)*(n+3)*(n+1)*(n+4))+
    8/(6*(n+1)*(n+2)*(n+3)))+(n+3)/((n+1)*(n+2)*(n+4)) by A7,XCMPLX_1:92
  .=29/36-(((n*2+7)*(n+1)*3*(n+4))/(6*(n+1)*(n+2)*(n+3)*(n+4))+(8*(n+4))/(6
   *(n+1)*(n+2)*(n+3)*(n+4)))+(n+3)/((n+1)*(n+2)*(n+4))by A7,XCMPLX_1:92
  .=29/36-((n*2+7)*(n+1)*3*(n+4)+8*(n+4))/(6*(n+1)*(n+2)*(n+3)*(n+4))+(n+3)
    /((n+1)*(n+2)*(n+4)) by XCMPLX_1:63
  .=29/36-((n*2+7)*(n+1)*3*(n+4)+8*(n+4))/(6*(n+1)*(n+2)*(n+3)*(n+4))+((n+3)
   *6)/((n+1)*(n+2)*(n+4)*6) by XCMPLX_1:92
  .=29/36-((n*2+7)*(n+1)*3*(n+4)+8*(n+4))/(6*(n+1)*(n+2)*(n+3)*(n+4))+((n+3)
   *6*(n+3))/((n+1)*(n+2)*(n+4)*6*(n+3)) by A7,XCMPLX_1:92
  .=29/36-(((n*2+7)*(n+1)*3*(n+4)+8*(n+4))/(6*(n+1)*(n+2)*(n+3)*(n+4))
   -((n+3)*6*(n+3))/((n+1)*(n+2)*(n+4)*6*(n+3)))
  .=29/36-(((n*2+7)*(n+1)*3*(n+4)+8*(n+4))-((n+3)*6*(n+3)))
     /(6*(n+1)*(n+2)*(n+3)*(n+4)) by XCMPLX_1:121
  .=29/36-(6*(n+1)*(n+2)*(n+3)+9*(n+1)*(n+2)+8*(n+1))
     /(6*(n+1)*(n+2)*(n+3)*(n+4))
  .=29/36-((6*(n+1)*(n+2)*(n+3)+9*(n+1)*(n+2))/(6*(n+1)*(n+2)*(n+3)*(n+4))
   +(8*(n+1))/(6*(n+1)*(n+2)*(n+3)*(n+4))) by XCMPLX_1:63
  .=29/36-((1*(6*(n+1)*(n+2)*(n+3)))/((n+4)*(6*(n+1)*(n+2)*(n+3)))
    +(9*(n+1)*(n+2))/(6*(n+1)*(n+2)*(n+3)*(n+4))
   +(8*(n+1))/(6*(n+1)*(n+2)*(n+3)*(n+4))) by XCMPLX_1:63
  .=29/36-(1/(n+4)+(3*(3*(n+1)*(n+2)))/(2*(n+3)*(n+4)*(3*(n+1)*(n+2)))
   +(8*(n+1))/(6*(n+1)*(n+2)*(n+3)*(n+4))) by A10,XCMPLX_1:92
  .=29/36-(1/(n+4)+3/(2*(n+3)*(n+4))
   +(4*(2*(n+1)))/(3*(n+2)*(n+3)*(n+4)*(2*(n+1)))) by A9,XCMPLX_1:92
  .=29/36-(1/(n+4)+3/(2*(n+3)*(n+4))+4/(3*(n+2)*(n+3)*(n+4)))
                  by A8,XCMPLX_1:92
  .=29/36-1/(n+4)-3/(2*(n+3)*(n+4))-4/(3*(n+2)*(n+3)*(n+4))
  .=29/36-1/(n+1+3)-3/(2*(n+1+2)*(n+1+3))-4/(3*(n+1+1)*(n+1+2)*(n+1+3));
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n holds s.n = ((n+1)*2|^n)/((n+2)*(n+3)) ) implies
   for n holds Partial_Sums(s).n = 2|^(n+1)/(n+3)-1/2
proof
  assume
 A1:for n holds s.n = ((n+1)*2|^n)/((n+2)*(n+3));
  defpred X[Nat] means Partial_Sums(s).$1 =2|^($1+1)/($1+3)-1/2;
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1
  .=((0+1)*2|^0)/((0+2)*(0+3)) by A1
  .=(1*1)/6 by NEWTON:9 .=2/3-1/2
  .=2|^(0+1)/(0+3)-1/2 by NEWTON:10;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
    A4: Partial_Sums(s).n = 2|^(n+1)/(n+3)-1/2;
        n+3>=3 & n+4>=4 by NAT_1:29;then
    A5:n+3>0 & n+4>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)
     = 2|^(n+1)/(n+3)-1/2 + s.(n+1) by A4,SERIES_1:def 1
     .= 2|^(n+1)/(n+3)-1/2 +((n+1+1)*2|^(n+1))/((n+1+2)*(n+1+3)) by A1
     .= 2|^(n+1)/(n+3)-1/2 +((n+2)*2|^(n+1))/((n+3)*(n+4))
     .= 2|^(n+1)/(n+3) +((n+2)*2|^(n+1))/((n+3)*(n+4))-1/2
     .= (2|^(n+1)*(n+4))/((n+3)*(n+4)) +((n+2)*2|^(n+1))/((n+3)*(n+4))-1/2
              by A5,XCMPLX_1:92
     .= (2|^(n+1)*(n+4)+(n+2)*2|^(n+1))/((n+3)*(n+4))-1/2 by XCMPLX_1:63
     .= (2|^(n+1)*2*(n+3))/((n+4)*(n+3))-1/2
     .= (2|^(n+1)*2)/(n+4)-1/2 by A5,XCMPLX_1:92
     .= 2|^(n+1+1)/(n+1+3)-1/2 by NEWTON:11;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (n|^2*4|^n)/((n+1)*(n+2)) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n =2/3+((n-1)*4|^(n+1))/(3*(n+2))
proof
  assume
 A1:for n st n>=1 holds s.n =(n|^2*4|^n)/((n+1)*(n+2)) & s.0=0;
  defpred X[Nat] means
     Partial_Sums(s).$1=2/3+(($1-1)*4|^($1+1))/(3*($1+2));
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=(1|^2*4|^1)/((1+1)*(1+2)) by A1
   .=(1*4|^1)/((1+1)*(1+2)) by NEWTON:15
   .=(1*4)/((1+1)*(1+2)) by NEWTON:10
   .=2/3+((1-1)*4|^(1+1))/(3*(1+2));then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n =2/3+((n-1)*4|^(n+1))/(3*(n+2));
    A5: n+1>=1 by NAT_1:29;
       n+2>=2 & n+3>=3 by NAT_1:29;then
    A6:n+2>0 & n+3>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)
     =2/3+((n-1)*4|^(n+1))/(3*(n+2))+ s.(n+1) by A4,SERIES_1:def 1
     .=2/3+((n-1)*4|^(n+1))/(3*(n+2))
        +((n+1)|^2*4|^(n+1))/(((n+1)+1)*((n+1)+2)) by A1,A5
     .=2/3+((n-1)*4|^(n+1)*(n+3))/(3*(n+2)*(n+3))
             +((n+1)|^2*4|^(n+1))/((n+2)*(n+3)) by A6,XCMPLX_1:92
     .=2/3+((n-1)*4|^(n+1)*(n+3))/(3*(n+2)*(n+3))
             +((n+1)|^2*4|^(n+1)*3)/((n+2)*(n+3)*3) by XCMPLX_1:92
     .=2/3+((n-1)*4|^(n+1)*(n+3))/(3*(n+2)*(n+3))
             +((n+1)|^2*4|^(n+1)*3)/(3*(n+2)*(n+3))
     .=2/3+(((n-1)*4|^(n+1)*(n+3))/(3*(n+2)*(n+3))
                +((n+1)|^2*4|^(n+1)*3)/(3*(n+2)*(n+3)))
     .=2/3+(((n-1)*4|^(n+1)*(n+3))+((n+1)|^2*4|^(n+1)*3))
                 /(3*(n+2)*(n+3)) by XCMPLX_1:63
     .=2/3+(((n-1)*(n+3)+(n+1)|^2*3)*4|^(n+1))/(3*(n+2)*(n+3))
     .=2/3+(((n-1)*(n+3)+(n+1)*(n+1)*3)*4|^(n+1))/(3*(n+2)*(n+3)) by Lm1
     .=2/3+(4*4|^(n+1)*n*(n+2))/(3*(n+3)*(n+2))
     .=2/3+(4|^(n+1)*4*n)/(3*(n+3)) by A6,XCMPLX_1:92
     .=2/3+((n+1-1)*4|^(n+1+1))/(3*(n+1+2)) by NEWTON:11;
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (n+2)/(n*(n+1)*2|^n) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n =1-1/((n+1)*2|^n)
proof
  assume
 A1:for n st n>=1 holds s.n =(n+2)/(n*(n+1)*2|^n) & s.0=0;
  defpred X[Nat] means
     Partial_Sums(s).$1=1-1/(($1+1)*2|^$1);
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=(1+2)/(1*(1+1)*2|^1) by A1
   .=(1+2)/(1*(1+1)*2) by NEWTON:10
   .=1-1/((1+1)*2)
   .=1-1/((1+1)*2|^1) by NEWTON:10;then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n =1-1/((n+1)*2|^n);
    A5: n+1>=1+0 by NAT_1:29;then
    A6: n+1>0 by NAT_1:38;
       n+2>=2 by NAT_1:29;then
    A7:n+2>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)
     =1-1/((n+1)*2|^n)+ s.(n+1) by A4,SERIES_1:def 1
     .=1-1/((n+1)*2|^n)+((n+1)+2)/((n+1)*((n+1)+1)*2|^(n+1)) by A1,A5
     .=1-1/((n+1)*2|^n)+(n+3)/((n+1)*(n+2)*2|^(n+1))
     .=1-(1/((n+1)*2|^n)-(n+3)/((n+1)*(n+2)*2|^(n+1)))
     .=1-(1*2/((n+1)*2|^n*2)-(n+3)/((n+1)*(n+2)*2|^(n+1))) by XCMPLX_1:92
     .=1-(1*2/((n+1)*(2|^n*2))-(n+3)/((n+1)*(n+2)*2|^(n+1)))
     .=1-(1*2/((n+1)*2|^(n+1))-(n+3)/((n+1)*(n+2)*2|^(n+1))) by NEWTON:11
     .=1-(2*(n+2)/((n+1)*2|^(n+1)*(n+2))-(n+3)/((n+1)*(n+2)*2|^(n+1)))
                     by A7,XCMPLX_1:92
     .=1-(2*(n+2)-(n+3))/((n+1)*(n+2)*2|^(n+1)) by XCMPLX_1:121
     .=1-(1*(n+1))/((n+2)*2|^(n+1)*(n+1))
     .=1-1/((n+1+1)*2|^(n+1)) by A6,XCMPLX_1:92;
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (2*n+3)/(n*(n+1)*3|^n) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n =1-1/((n+1)*3|^n)
proof
  assume
 A1:for n st n>=1 holds s.n =(2*n+3)/(n*(n+1)*3|^n) & s.0=0;
  defpred X[Nat] means
     Partial_Sums(s).$1=1-1/(($1+1)*3|^$1);
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=(2*1+3)/(1*(1+1)*3|^1) by A1
   .=(2*1+3)/(1*(1+1)*3) by NEWTON:10
   .=1-1/((1+1)*3)
   .=1-1/((1+1)*3|^1) by NEWTON:10;then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n =1-1/((n+1)*3|^n);
    A5: n+1>=1+0 by NAT_1:29;then
A6:    n+1>0 by NAT_1:38;
       n+2>=2 by NAT_1:29;then
    A7:n+2>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)
     =1-1/((n+1)*3|^n)+ s.(n+1) by A4,SERIES_1:def 1
     .=1-1/((n+1)*3|^n)+(2*(n+1)+3)/((n+1)*((n+1)+1)*3|^(n+1)) by A1,A5
     .=1-(1*3)/((n+1)*3|^n*3)+(2*n+5)/((n+1)*(n+2)*3|^(n+1)) by XCMPLX_1:92
     .=1-3/((n+1)*(3|^n*3))+(2*n+5)/((n+1)*(n+2)*3|^(n+1))
     .=1-3/((n+1)*3|^(n+1))+(2*n+5)/((n+1)*(n+2)*3|^(n+1)) by NEWTON:11
     .=1-(3*(n+2))/((n+1)*3|^(n+1)*(n+2))+(2*n+5)/((n+1)*(n+2)*3|^(n+1))
                by A7,XCMPLX_1:92
     .=1-((3*(n+2))/((n+1)*3|^(n+1)*(n+2))-(2*n+5)/((n+1)*(n+2)*3|^(n+1)))
     .=1-(3*(n+2)-(2*n+5))/((n+1)*(n+2)*3|^(n+1)) by XCMPLX_1:121
     .=1-(1*(n+1))/((n+2)*3|^(n+1)*(n+1))
     .=1-1/((n+1+1)*3|^(n+1)) by A6,XCMPLX_1:92;
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

Lm17:  m > 1 & n > 0 implies m |^ n > 1
proof
  assume A1:m > 1 & n > 0;
  then m <> 0;
  then A2:m > 0 by NAT_1:19;
  defpred P[Nat] means
    $1 > 0 implies m |^ $1 > 1;
  A3: P[0];
  A4: for n being Nat holds P[n] implies P[n+1]
  proof
    let n;
    assume A5:P[n];
    A6:m |^ (n+1) = (m |^ n)*(m |^ 1) by NEWTON:13
                 .= (m |^ n)*m by NEWTON:10;
    P[n+1]
    proof
        now per cases;
        suppose n = 0;
          hence thesis by A1,NEWTON:10;
        end;
        suppose n <> 0;
          then (m |^ n)*m > 1*m by A2,A5,NAT_1:19,REAL_1:70;
          hence thesis by A1,A6,AXIOMS:22;
      end;
      end;
    hence thesis;
    end;
  hence thesis;
  end;
  for n being Nat holds P[n] from NAT_1:sch 1(A3,A4);
  hence thesis by A1;
end;

Lm18: 2|^(n+2)+(-1)|^(n+2)>0
proof
     n+2>=2 by NAT_1:29;then
     n+2 > 0 & 2>1 by AXIOMS:22;then
     2|^(n+2) > 1 by Lm17;then
  A1:2|^(n+2)+(-1)|^(n+2)>1+(-1)|^(n+2) by REAL_1:67;
A2:  abs((-1)|^(n+2)) = 1 by Th1;
A3:  (-1)|^(n+2) = abs((-1)|^(n+2)) or -(-1)|^(n+2) = abs((-1)|^(n+2))
       by ABSVALUE:1;
      per cases by A2,A3;
      suppose (-1)|^(n+2) = -1;
       hence thesis by A1;
      end;
      suppose (-1)|^(n+2) = 1;
       hence thesis by A1,AXIOMS:22;
      end;
end;

Lm19: 2|^(n+3)+(-1)|^(n+3)>0
proof
     n+3>=3 by NAT_1:29;then
     n+3 > 0 & 2>1 by AXIOMS:22;then
     2|^(n+3) > 1 by Lm17;then
  A1:2|^(n+3)+(-1)|^(n+3)>1+(-1)|^(n+3) by REAL_1:67;
A2:  abs((-1)|^(n+3)) = 1 by Th1;
A3:  (-1)|^(n+3) = abs((-1)|^(n+3)) or -(-1)|^(n+3) = abs((-1)|^(n+3))
       by ABSVALUE:1;
      per cases by A2,A3;
      suppose (-1)|^(n+3) = -1;
       hence thesis by A1;
      end;
      suppose (-1)|^(n+3) = 1;
       hence thesis by A1,AXIOMS:22;
      end;
end;

theorem
  (for n holds s.n =((-1)|^n*2|^(n+1))
    /((2|^(n+1)+(-1)|^(n+1))*(2|^(n+2)+(-1)|^(n+2)))) implies
   for n holds Partial_Sums(s).n
    = 1/3+((-1)|^(n+2))/(3*(2|^(n+2)+(-1)|^(n+2)))
proof
  assume
 A1:for n holds s.n =((-1)|^n*2|^(n+1))
    /((2|^(n+1)+(-1)|^(n+1))*(2|^(n+2)+(-1)|^(n+2)));
  defpred X[Nat] means Partial_Sums(s).$1
   = 1/3+((-1)|^($1+2))/(3*(2|^($1+2)+(-1)|^($1+2)));
  Partial_Sums(s).0 = s.0 by SERIES_1:def 1
  .=((-1)|^0*2|^(0+1))
    /((2|^(0+1)+(-1)|^(0+1))*(2|^(0+2)+(-1)|^(0+2))) by A1
  .=(1*2|^(0+1))
    /((2|^(0+1)+(-1)|^(0+1))*(2|^(0+2)+(-1)|^(0+2))) by NEWTON:9
  .=(1*2)
    /((2|^(0+1)+(-1)|^(0+1))*(2|^(0+2)+(-1)|^(0+2))) by NEWTON:10
  .=(1*2)/((2+(-1)|^(0+1))*(2|^(0+2)+(-1)|^(0+2))) by NEWTON:10
  .=(1*2)/((2+(-1))*(2|^(0+2)+(-1)|^(0+2))) by NEWTON:10
  .=(1*2)/((2+(-1))*(2*2+(-1)|^(0+2))) by Lm1
  .=(1*2)/((2+(-1))*(2*2+(-1)*(-1))) by Lm1
  .=1/3+((-1)*(-1))/(3*(2*2+(-1)*(-1)))
  .=1/3+((-1)|^2)/(3*(2*2+(-1)*(-1))) by Lm1
  .=1/3+((-1)|^2)/(3*(2*2+(-1)|^2)) by Lm1
  .=1/3+((-1)|^(0+2))/(3*(2|^(0+2)+(-1)|^(0+2))) by Lm1;then
 A2: X[0];
 A3:for n st X[n] holds X[n+1]
    proof let n; assume
    A4: Partial_Sums(s).n =1/3+((-1)|^(n+2))/(3*(2|^(n+2)+(-1)|^(n+2)));
    A5:Partial_Sums(s).(n+1)
     =1/3+((-1)|^(n+2))/(3*(2|^(n+2)+(-1)|^(n+2)))+s.(n+1) by A4,SERIES_1:def 1
     .=1/3+((-1)|^(n+2))/(3*(2|^(n+2)+(-1)|^(n+2)))
       +((-1)|^(n+1)*2|^((n+1)+1))/((2|^((n+1)+1)+(-1)|^((n+1)+1))
       *(2|^((n+1)+2)+(-1)|^((n+1)+2))) by A1
     .=1/3+((-1)|^(n+2))/(3*(2|^(n+2)+(-1)|^(n+2)))+((-1)|^(n+1)*2|^(n+2))
       /((2|^(n+2)+(-1)|^(n+2))*(2|^(n+3)+(-1)|^(n+3)));
       set b=2|^(n+2)+(-1)|^(n+2);
       set p=2|^(n+3)+(-1)|^(n+3);
     A6:b>0 & p>0 by Lm18,Lm19;
       Partial_Sums(s).(n+1)
         =1/3+((-1)|^(n+2)*p)/(3*b*p)+((-1)|^(n+1)*2|^(n+2))/(b*p)
                                      by A5,A6,XCMPLX_1:92
        .=1/3+((-1)|^(n+2)*p)/(3*b*p)+((-1)|^(n+1)*2|^(n+2)*3)/(b*p*3)
                                      by XCMPLX_1:92
        .=1/3+((-1)|^(n+2)*p)/(3*b*p)+((-1)|^(n+1)*2|^(n+2)*3)/(3*b*p)
        .=1/3+(((-1)|^(n+2)*p)/(3*b*p)+((-1)|^(n+1)*2|^(n+2)*3)/(3*b*p))
        .=1/3+(((-1)|^(n+2)*p)+((-1)|^(n+1)*2|^(n+2)*3))/(3*b*p) by XCMPLX_1:63
   .=1/3+(((-1)|^(n+2)*(-1)/(-1)*p)+((-1)|^(n+1)*2|^(n+2)*3))/(3*b*p)
                            by XCMPLX_1:90
   .=1/3+(((-1)|^(n+2+1)/(-1)*p)+((-1)|^(n+1)*2|^(n+2)*3))/(3*b*p) by NEWTON:11
   .=1/3+(((-1)|^(n+3)/(-1)*p)+((-1)|^(n+1)*(-1)/(-1)*2|^(n+2)*3))
                           /(3*b*p) by XCMPLX_1:90
   .=1/3+(((-1)|^(n+3)/(-1)*p)+((-1)|^(n+1+1)/(-1)*2|^(n+2)*3))
                           /(3*b*p) by NEWTON:11
   .=1/3+(((-1)|^(n+3)/(-1)*p)+((-1)|^(n+2)*(-1)/((-1)*(-1))*2|^(n+2)*3))
                           /(3*b*p) by XCMPLX_1:92
   .=1/3+(((-1)|^(n+3)/(-1)*p)+((-1)|^(n+2+1)/((-1)*(-1))*2|^(n+2)*3))
                           /(3*b*p) by NEWTON:11
   .=1/3+((-(-1)|^(n+3))*p+(-1)|^(n+3)*2|^(n+2)*3)/(3*b*p) by XCMPLX_1:194
   .=1/3+((-1)|^(n+3)*(2|^(n+2)+2|^(n+2)*2-2|^(n+3)-(-1)|^(n+3)))/(3*b*p)
   .=1/3+((-1)|^(n+3)*(2|^(n+2)+2|^(n+2+1)-2|^(n+3)-(-1)|^(n+3)))/(3*b*p)
                  by NEWTON:11
   .=1/3+((-1)|^(n+3)*(2|^(n+2)-(-1)|^(n+2+1)))/(3*b*p) by XCMPLX_1:26
   .=1/3+((-1)|^(n+3)*(2|^(n+2)-(-1)|^(n+2)*(-1)))/(3*b*p) by NEWTON:11
   .=1/3+((-1)|^(n+3)*b)/(3*p*b)
   .=1/3+((-1)|^(n+2+1))/(3*(2|^(n+2+1)+(-1)|^(n+2+1))) by A6,XCMPLX_1:92;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A2,A3);
 hence thesis;
end;

theorem
  (for n holds s.n = n!*n) implies
  for n st n>=1 holds Partial_Sums(s).n = (n+1)!-1
proof
  assume
 A1:for n holds s.n = n!*n; then
A2: s.0 = 0!*0 .= 0;
  defpred X[Nat] means Partial_Sums(s).$1= ($1+1)!-1;
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A2
   .= 1!*1 by A1 .= (1+1)!-1 by NEWTON:19,20;then
 A3: X[1];
 A4:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A5:n>=1 & Partial_Sums(s).n =(n+1)!-1;
     Partial_Sums(s).(n+1)=(n+1)!-1+ s.(n+1) by A5,SERIES_1:def 1
   .=(n+1)!-1 +(n+1)!*(n+1) by A1
   .=(n+1)!*(n+1+1)-1
   .=(n+1+1)!-1 by NEWTON:21;
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A3,A4);
  hence thesis;
end;

theorem
  (for n holds s.n = n/((n+1)!)) implies
  for n st n>=1 holds Partial_Sums(s).n = 1-1/((n+1)!)
proof
  assume
 A1:for n holds s.n = n/((n+1)!); then
A2: s.0 = 0/((0+1)!) .= 0;
  defpred X[Nat] means Partial_Sums(s).$1= 1-1/(($1+1)!);
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A2
   .= 1-1/((1+1)!) by A1,NEWTON:20;then
 A3: X[1];
 A4:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A5:n>=1 & Partial_Sums(s).n =1-1/((n+1)!);
      n+2>=2 by NAT_1:29;then
    A6:n+2>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)=1-1/((n+1)!)+ s.(n+1) by A5,SERIES_1:def 1
   .=1-1/((n+1)!)+(n+1)/((n+1+1)!) by A1
   .=1-(1*(n+2))/(((n+1)!)*(n+1+1))+(n+1)/((n+2)!) by A6,XCMPLX_1:92
   .=1-(1*(n+2))/((n+1+1)!)+(n+1)/((n+2)!) by NEWTON:21
   .=1-((n+2)/((n+1+1)!)-(n+1)/((n+2)!)) by XCMPLX_1:37
   .=1-((n+2)-(n+1))/((n+2)!) by XCMPLX_1:121
   .=1-1/((n+1+1)!);
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A3,A4);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (n|^2+n-1)/((n+2)!) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n = 1/2-(n+1)/((n+2)!)
proof
  assume
 A1:for n st n>=1 holds s.n = (n|^2+n-1)/((n+2)!) & s.0=0;
  defpred X[Nat] means
     Partial_Sums(s).$1= 1/2-($1+1)/(($1+2)!);
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=(1|^2+1-1)/((1+2)!) by A1 .=(1*1)/((2+1)!) by Lm1
   .=1/(2*3) by NEWTON:20,21
   .=1/2-2/(2!*(2+1)) by NEWTON:20  .=1/2-2/((2+1)!) by NEWTON:21;then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n =1/2-(n+1)/((n+2)!);
    A5: n+1>=1 by NAT_1:29;
      n+3>=3 by NAT_1:29;then
    A6:n+3>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)=1/2-(n+1)/((n+2)!)+ s.(n+1) by A4,SERIES_1:def 1
   .=1/2-(n+1)/((n+2)!)+((n+1)|^2+(n+1)-1)/((n+1+2)!) by A1,A5
   .=1/2-((n+1)*(n+3))/((n+2)!*(n+2+1))+((n+1)|^2+n)/((n+3)!) by A6,XCMPLX_1:92
   .=1/2-((n+1)*(n+3))/((n+2+1)!)+((n+1)|^2+n)/((n+3)!) by NEWTON:21
   .=1/2-(((n+1)*(n+3))/((n+3)!)-((n+1)|^2+n)/((n+3)!)) by XCMPLX_1:37
   .=1/2-(((n+1)*(n+3))-((n+1)|^2+n))/((n+3)!) by XCMPLX_1:121
   .=1/2-(((n+1)*(n+3))-(n+1)|^2-n)/((n+3)!)
   .=1/2-((n+1)*(n+3)-(n+1)*(n+1)-n)/((n+3)!) by Lm1
   .=1/2-(n+1+1)/((n+1+2)!);
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;

theorem
  (for n st n>=1 holds s.n = (n*2|^n)/((n+2)!) & s.0=0) implies
  for n st n>=1 holds Partial_Sums(s).n = 1-2|^(n+1)/((n+2)!)
proof
  assume
 A1:for n st n>=1 holds s.n = (n*2|^n)/((n+2)!) & s.0=0;
  defpred X[Nat] means Partial_Sums(s).$1=1-2|^($1+1)/(($1+2)!);
  Partial_Sums(s).(1+0)=Partial_Sums(s).0+s.(1+0) by SERIES_1:def 1
   .= s.0 + s.1 by SERIES_1:def 1 .=0+s.1 by A1
   .=(1*2|^1)/((1+2)!) by A1 .=2/((2+1)!) by NEWTON:10
   .=2/(2*3) by NEWTON:20,21
   .=1-(2*2)/(2!*(2+1)) by NEWTON:20  .=1-2|^2/(2!*(2+1)) by Lm1
   .=1-2|^(1+1)/((1+2)!) by NEWTON:21;then
 A2: X[1];
 A3:for n st n>=1 & X[n] holds X[n+1]
    proof let n; assume
    A4:n>=1 & Partial_Sums(s).n =1-2|^(n+1)/((n+2)!);
    A5: n+1>=1 by NAT_1:29;
      n+3>=3 by NAT_1:29;then
    A6:n+3>0 by AXIOMS:22;
     Partial_Sums(s).(n+1)=1-2|^(n+1)/((n+2)!)+ s.(n+1) by A4,SERIES_1:def 1
   .=1-2|^(n+1)/((n+2)!)+((n+1)*2|^(n+1))/((n+1+2)!) by A1,A5
   .=1-(2|^(n+1)*(n+3))/((n+2)!*(n+2+1))+((n+1)*2|^(n+1))/((n+3)!)
                                         by A6,XCMPLX_1:92
   .=1-(2|^(n+1)*(n+3))/((n+2+1)!)+((n+1)*2|^(n+1))/((n+3)!) by NEWTON:21
   .=1-((2|^(n+1)*(n+3))/((n+2+1)!)-((n+1)*2|^(n+1))/((n+3)!))
   .=1-(2|^(n+1)*(n+3)-2|^(n+1)*(n+1))/((n+3)!) by XCMPLX_1:121
   .=1-(2|^(n+1)*2)/((n+3)!) .=1-2|^(n+1+1)/((n+1+2)!) by NEWTON:11;
    hence thesis;
    end;
   for n st n>=1 holds X[n] from BINOM:sch 1(A2,A3);
  hence thesis;
end;
