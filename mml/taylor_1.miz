:: The {T}aylor Expansions
::  by Yasunari Shidama
::
:: Received February 24, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, BOOLE, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, SUBSET_1, ORDINAL2, QC_LANG1, PARTFUN1, LIMFUNC1,
      FCONT_1, INT_1, RAT_1, SEQ_1, SEQ_2, SEQM_3, RCOMP_1, SEQFUNC, SIN_COS,
      FDIFF_1, PREPOWER, POWER, SERIES_1, GROUP_1, FINSEQ_4, SUPINF_2,
      TAYLOR_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, PARTFUN1, PARTFUN2,
      FUNCT_2, RFUNCT_2, LIMFUNC1, RCOMP_1, FCONT_1, ORDINAL2, XCMPLX_0,
      XREAL_0, RAT_1, ORDINAL1, NUMBERS, REAL_1, NAT_1, INT_1, COMPLEX1,
      FINSEQ_4, NEWTON, SEQ_1, SEQ_2, SEQM_3, SERIES_1, FDIFF_1, SEQFUNC,
      PREPOWER, POWER, SIN_COS;
 constructors ARYTM_0, REAL_1, NAT_1, PARTFUN1, PARTFUN2, LIMFUNC1, FCONT_1,
      RFUNCT_2, RCOMP_1, MEMBERED, SEQ_2, COMPLEX1, GROUP_1, SERIES_1,
      PREPOWER, COMSEQ_3, FDIFF_1, FINSEQ_4, SIN_COS, SEQFUNC, INT_2;
 registrations RELSET_1, XREAL_0, MEMBERED, NEWTON, NAT_1, RAT_1, RCOMP_1,
      INT_1, SEQ_1, SIN_COS, PREPOWER, PARTFUN1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 theorems AXIOMS, XBOOLE_1, ABSVALUE, REAL_1, REAL_2, TARSKI, XBOOLE_0,
      PARTFUN1, PARTFUN2, SUBSET_1, XCMPLX_1, RELAT_1, CARD_4, RCOMP_1, SEQ_1,
      SEQ_2, SEQM_3, SIN_COS, SERIES_1, FUNCT_1, RFUNCT_2, NAT_1, INT_1,
      NEWTON, FINSEQ_4, RAT_1, IRRAT_1, POWER, LIMFUNC1, FCONT_1, FCONT_3,
      FDIFF_1, FDIFF_2, ROLLE, SQUARE_1, SEQ_4, SEQFUNC, PREPOWER, RELSET_1,
      FUNCT_2, XREAL_0, ORDINAL2;
 schemes SEQ_1, RECDEF_1, NAT_1, INT_1, PARTFUN2, FUNCT_2;

begin :: The Logarithmic differentiation method

reserve
  n for natural number,
  i for Integer,
  p, x, x0, y for real number,
  q for Rational,
  f for PartFunc of REAL,REAL;

definition
let q be Integer;
func #Z q -> Function of REAL, REAL means :Def1:
for x be real number holds it.x = x #Z q;
existence
proof
   defpred X[set] means $1 in REAL;
   deffunc U(Real) = $1 #Z q;
  consider f being Function of REAL,REAL such that
  A1:   for d be Element of REAL holds f.d = U(d) from FUNCT_2:sch 4;
  take f;
  let d be real number;
   d is Real by XREAL_0:def 1;
  hence f.d = d #Z q by A1;
 end;
  uniqueness
   proof
     let f1,f2 be Function of REAL, REAL;
     assume
     A2: for d be real number holds f1.d=d #Z q;
     assume
     A3: for d be real number holds f2.d=d #Z q;
     for d being Element of REAL holds f1.d = f2.d
     proof
      let d be Element of REAL;
        thus f1.d=d #Z q by A2 .=f2.d by A3;
     end;
    hence f1=f2 by FUNCT_2:113;
 end;
end;

theorem Th1:
for m,n be natural number holds x #Z (n+m) = (x #Z n) * (x #Z m)
  proof
  let m,n be natural number;
  per cases;
  suppose  x <> 0;
  hence x #Z (n+m) =(x #Z n) *(x #Z m) by PREPOWER:54;
  end;
  suppose A1:  x = 0;
  thus thesis
  proof
    A2:0 <= n+m by NAT_1:18;
     then A3: 0 #Z (n+m) = 0|^abs(n+m) by PREPOWER:def 4
      .= 0|^(n+m) by A2,ABSVALUE:def 1
      .= 0|^n*0|^m  by NEWTON:13;
  per cases;
  suppose
A4: n = 0 & m = 0;
   x #Z (0+0) = 1 * (x #Z 0)
   .= (x #Z 0) * (x #Z 0) by PREPOWER:44;
  hence thesis by A4;
  end;
  suppose n <> 0;
  then A5: 0 < n by NAT_1:19;
  then A6: 0+1 <= n by NAT_1:38;
     then A7: 0 #Z (n+m) = 0*0|^m  by A3,NEWTON:16;
      (0 #Z n) *(0 #Z m) = (0 |^ abs(n)) * (0 #Z m) by A5,PREPOWER:def 4
      .= (0 |^ n) * (0 #Z m) by A5,ABSVALUE:def 1
      .= 0 * (0 #Z m) by A6,NEWTON:16;
    hence  x #Z (n+m)  =(x #Z n) *(x #Z m) by A1,A7;
  end;
  suppose m <> 0;
  then A8: 0 < m by NAT_1:19;
  then A9: 0+1 <= m by NAT_1:38;
     then A10: 0 #Z (n+m) = 0*0|^n  by A3,NEWTON:16;
      (0 #Z n) *(0 #Z m) = (0 |^ abs(m)) * (0 #Z n) by A8,PREPOWER:def 4
      .= (0 |^ m) * (0 #Z n) by A8,ABSVALUE:def 1
      .= 0 * (0 #Z n) by A9,NEWTON:16;
    hence  x #Z (n+m)  =(x #Z n) *(x #Z m) by A1,A10;
  end;
  end;
 end;
 end;

theorem Th2:
  #Z n is_differentiable_in x & diff(( #Zn),x) = n * x #Z (n-1)
proof
defpred P[natural number] means for x be real number holds
     #Z $1 is_differentiable_in x &
     diff( ( #Z $1),x) = $1 * x #Z ($1 -1);
A1:P[0]
proof
  set f= #Z 0;
  A2: dom(f)=REAL by FUNCT_2:def 1
          .=[#]REAL by SUBSET_1:def 4;
   now let y be set such that A3: y in {1};
       dom(f)=REAL by FUNCT_2:def 1;
      then consider x be set such that
      A4:  x in dom(f);
       dom (f) c= REAL by RELSET_1:12;
      then reconsider x1=x  as Real by A4;
       f.x=x1 #Z 0 by Def1
         .=1 by PREPOWER:44
         .=y by A3,TARSKI:def 1;
      hence y in rng (f) by A4,FUNCT_1:def 5;
    end;
   then A5:  {1} c= rng (f) by TARSKI:def 3;
     now let y be set such that
      A6: y in rng(f);
     consider x be set such that
      A7:  x in dom(f) and
      A8:  y=f.x by A6,FUNCT_1:def 5;
      reconsider x as Real by A7,FUNCT_2:def 1;
      reconsider y1=y as Real by A8;
       f.x =x #Z 0 by Def1 .=1 by PREPOWER:44;
      hence y in {1} by A8,TARSKI:def 1;
    end;
  then rng(f) c= {1} by TARSKI:def 3;
  then A9: rng(f)  =  {1} by A5,XBOOLE_0:def 10;
  let x be real number;
  A10: x is Real by XREAL_0:def 1;
    A11: dom(f)=REAL by FUNCT_2:def 1;
     then f is_differentiable_on dom(f) & (f`|dom(f)) .x = 0
        by A2,A9,A10,FCONT_3:4,FDIFF_1:19;
      hence f is_differentiable_in x & diff(f,x)= 0  * x #Z (0-1)
       by A2,A10,A11,FCONT_3:4,FDIFF_1:16,def 8;
end;
A12:for k be natural number st P[k] holds P[k+1]
proof
  let k be natural number such that
  A13: P[k];
  A14: REAL c= dom ( #Z 1) &
       REAL is open Subset of REAL &
       (for x be Real st x in REAL holds ( #Z 1).x = 1*x + 0)
 proof
 thus REAL c= dom ( #Z 1) by FUNCT_2:def 1;
 thus REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
  now let x be Real such that x in REAL;
    thus ( #Z 1).x=x  #Z 1 by Def1
                .=1*x + 0 by PREPOWER:45;
    end;
 hence thesis;
 end;
then A15:  ( #Z 1) is_differentiable_on REAL
       & for x be Real st x in REAL holds ( ( #Z 1)`|REAL).x = 1
   by FDIFF_1:31;
A16: for x be real number holds ( #Z 1) is_differentiable_in x &
         diff(( #Z 1),  x)=1
proof
let x be real number;
  A17: x is Real by XREAL_0:def 1;
  hence ( #Z 1) is_differentiable_in  x by A14,A15,FDIFF_1:16;
  thus diff(( #Z 1),  x)=( ( #Z 1)`|REAL).x by A15,A17,FDIFF_1:def 8
                        .=1 by A14,A17,FDIFF_1:31;
end;
 now per cases;
case A18: k = 0;
thus P[k+1]
proof
let x be real number;
thus #Z (k+1) is_differentiable_in x by A16,A18;
thus diff(( #Z (k+1)), x)=1 by A16,A18
        .=(k+1)*x #Z ((k+1)-1) by A18,PREPOWER:44;
end;
end;
case k<> 0;
 then 1 <= k by NAT_1:39;
then 1-1 <=k-1 by REAL_1:92;
then reconsider k1=k-1 as Nat by INT_1:16;
A19: dom(( #Z k))  /\ dom(( #Z 1))
      =REAL /\ dom(( #Z 1)) by FUNCT_2:def 1
      .=REAL /\ REAL by FUNCT_2:def 1;
a2:   REAL = dom( #Z (k+1)) by FUNCT_2:def 1;
A20: for x be set st x in dom( #Z (k+1))
          holds ( #Z (k+1)).x=( #Z k).x * ( #Z 1).x
 proof
 let x be set;
 assume
 x in dom( #Z (k+1));
    then reconsider x1 = x as Real by a2;
    thus ( #Z (k+1)).x = x1 #Z (k+1) by Def1
                       .=(x1 #Z k) * (x1 #Z 1) by Th1
                       .=( #Z k).x * x1 #Z (1) by Def1
                       .=( #Z k).x *( #Z 1).x by Def1;
 end;
 then A21:   ( #Z (k+1)) = ( #Z k) (#) ( #Z 1)
           by a2,A19,SEQ_1:def 5;
 let x be real number;
  A22: x is Real by XREAL_0:def 1;
   A23: ( #Z k) is_differentiable_in x by A13;
   A24: ( #Z 1) is_differentiable_in x by A16;
    hence #Z (k+1) is_differentiable_in x by A21,A22,A23,FDIFF_1:24;
    thus diff(( #Z (k+1)), x)
            = diff( ( #Z k) (#) ( #Z 1),x) by a2,A19,A20,SEQ_1:def 5
             .= ( #Z k).x *diff(( #Z 1),x)+diff(( #Z k),x)*( #Z 1).x
                by A22,A23,A24,FDIFF_1:24
              .= ( #Z k).x*1 + diff( #Z k,x)*( #Z 1).x by A16
              .= (x #Z k)*1 + diff( #Z k,x)*( #Z 1).x by Def1
              .= (x #Z k)*1 + diff( #Z k,x)*(x #Z 1) by Def1
             .= (x #Z k)+k*(x #Z k1)*(x #Z 1) by A13
             .= (x #Z k)+k*((x #Z k1)*(x #Z 1))
             .= (x #Z k)+k*(x #Z (k1+1)) by Th1
             .= (x #Z k)+k*(x #Z k)
             .= (k+1) * (x #Z k)
             .= (k+1) * x #Z ((k+1)-1);
 end;
end;
hence thesis;
end;
 for n be natural number holds P[n] from NAT_1:sch 2(A1,A12);
hence thesis;
end;

theorem
  f is_differentiable_in x0 implies
   ( #Z n) *f is_differentiable_in x0 &
   diff( ( #Z n) *f,x0) = n * (f.x0) #Z (n-1) *diff(f,x0)
proof
  assume
A1:      f is_differentiable_in x0;
A2: ( #Z n)  is_differentiable_in f.x0 by Th2;
A3: x0 is Real by XREAL_0:def 1;
hence ( #Z n)*f is_differentiable_in x0 by A1,A2,FDIFF_2:13;
thus diff(( #Z n)*f,x0)
               = diff(( #Z n),f.x0)*diff(f,x0) by A1,A2,A3,FDIFF_2:13
              .=n * (f.x0) #Z (n-1) *diff(f,x0) by Th2;
end;

Lm1:
(exp(x)) #R n = exp(n*x)
  proof
   reconsider x as Real by XREAL_0:def 1;
    A1:  exp(x) > 0 by SIN_COS:60;
   defpred P[natural number] means (exp(x)) #R $1 =exp( $1 * x);
  A2:P[0] by A1,PREPOWER:85,SIN_COS:56;
  A3: for k be natural number st P[k] holds  P[k+1]
    proof
    let k be natural number such that
    A4: P[k];
    reconsider k1 = k as Nat by ORDINAL2:def 21;
    thus
     (exp(x)) #R (k+1) = (exp(x)) #R k  * (exp(x)) #R 1
                             by A1,PREPOWER:89
                        .= (exp(x)) #R k  * exp(x) by A1,PREPOWER:86
                        .=exp(k1*x+x) by A4,SIN_COS:55
                        .=exp((k+1)*x);
    end;
    for n be natural number holds P[n] from NAT_1:sch 2(A2,A3);
   hence thesis;
   end;

theorem Th4:
  exp(-x) = 1/exp(x)
proof
  reconsider x as Real by XREAL_0:def 1;
   exp(-x)*exp(x)=exp((-x) +x ) by SIN_COS:55
                      .=1 by SIN_COS:56;
  hence thesis by XCMPLX_1:74;
end;

Lm2: (exp(x)) #R i =exp(i*x)
   proof
A1:  exp(x) > 0 by SIN_COS:60;
    consider n be Nat  such that
    A2:  i = n or i = - n by INT_1:8;
     now per cases by A2;
    case   i = n;
    hence (exp(x)) #R i =exp(i*x) by Lm1;
    end;
    case A3:  i = -n;
    hence exp(i*x) =exp(-(n*x))
                        .=1/(exp(n*x)) by Th4
                        .=1/( (exp(x)) #R n ) by Lm1
                        .=(exp(x)) #R i by A1,A3,PREPOWER:90;
     end;
    end;
 hence thesis;
end;

theorem Th5:
(exp(x)) #R (1/i) = exp(x/i)
  proof set n = i;
    per cases;
    suppose
    A1:  n <> 0;
    A2:  exp(x/n) > 0 by SIN_COS:60;
      exp(x) =exp((x/n)*n) by A1,XCMPLX_1:88
                   .=(exp(x/n)) #R n by Lm2;
   hence (exp(x))  #R (1/n)
           =(exp(x/n)) #R (n* (1/n) ) by A2,PREPOWER:105
           .=(exp(x/n)) #R 1 by A1,XCMPLX_1:107
           .=exp(x/n) by A2,PREPOWER:86;
    end;
     suppose
A3:    n = 0;
     A4:  exp(x) > 0 by SIN_COS:60;
      (exp(x)) #R (1/0) = (exp(x)) #R 0
       .= exp(0) by A4,PREPOWER:85,SIN_COS:56
       .= exp(x/0) by XCMPLX_1:49;
     hence thesis by A3;
    end;
  end;

theorem Th6:
for m,n be Integer holds (exp(x)) #R (m/n) = exp(m/n*x)
  proof
    let m,n be Integer;
    A1:  exp(x) > 0 by SIN_COS:60;
  thus exp(m/n*x)
    = exp(x/n*m) by XCMPLX_1:76
    .=(exp(x/n)) #R m by Lm2
    .=((exp(x)) #R (1/n)) #R m by Th5
    .=(exp(x)) #R ((1/n)*m) by A1,PREPOWER:105
    .=(exp(x)) #R ((m/n)*1) by XCMPLX_1:76
    .=(exp(x)) #R (m/n);
  end;

Lm3: (exp(x)) #R q = exp(q*x)
  proof
  consider m be Integer,n be Nat such that
      n <> 0 and
    A1: q=m/n by RAT_1:24;
     thus exp(q*x) =(exp(x)) #R q by A1,Th6;
  end;

theorem Th7:
(exp(x)) #Q q = exp(q*x)
proof
  exp(x) > 0 by SIN_COS:60;
  hence (exp(x)) #Q q = (exp(x)) #R q by PREPOWER:88
                      .=exp(q*x) by Lm3;
end;

theorem Th8:
(exp(x)) #R p = exp(p*x)
  proof
   exp(x) > 0 by SIN_COS:60;
    then consider s being Rational_Sequence such that
  A1: s is convergent and
  A2: lim s = p and
    (exp(x)) #Q s is convergent and
  A3: lim (exp(x))  #Q s = (exp(x)) #R p
 by PREPOWER:def 8;
A4: now let ii be set such that
   A5: ii in NAT;
  reconsider i=ii as Nat by A5;
  A6: rng (x(#)s) c= dom (exp) by SIN_COS:51;
  thus
     ( (exp(x)) #Q s ).ii =(exp(x)) #Q (s.i) by PREPOWER:def 7
    .=exp((s.i)*x) by Th7
    .=exp((x(#)s).i) by SEQ_1:13
    .=exp.((x(#)s).i) by SIN_COS:def 27
    .=(exp*((x(#)s))).ii by A6,RFUNCT_2:21;
  end;
 A7:  x(#)s is convergent by A1,SEQ_2:21;
 A8:  lim (x(#)s) = x*p by A1,A2,SEQ_2:22;
  exp is_differentiable_in x*p by SIN_COS:70;
  then A9:exp  is_continuous_in x*p by FDIFF_1:32;
   rng (x(#)s) c= dom (exp) by SIN_COS:51;
  then lim((exp*(x(#)s)) )
         = exp.(x*p) by A7,A8,A9,FCONT_1:def 1
        .= exp(p*x) by SIN_COS:def 27;
  hence thesis by A3,A4,SEQ_1:8;
 end;

theorem Th9:
  (exp(1)) #R x =exp(x) & (exp(1)) to_power x =exp(x) &
   number_e to_power x =exp(x) & number_e  #R x =exp(x)
proof
A1:exp(1) > 0 by SIN_COS:60;
   thus exp(x) =exp(x*1) .= (exp(1)) #R x by Th8;
   hence thesis by A1,IRRAT_1:def 7,POWER:def 2;
end;

theorem
 (exp.1) #R x =exp.x & (exp.1) to_power x =exp.x &
   number_e to_power x =exp.x & number_e #R x =exp.x
proof
thus (exp.1) #R x =(exp(1)) #R x by SIN_COS:def 27
                 .=exp(x) by Th9
                 .=exp.x by SIN_COS:def 27;
thus (exp.1) to_power x=(exp(1) ) to_power x
                     by SIN_COS:def 27
                         .=exp(x) by Th9
                         .=exp.x by SIN_COS:def 27;
thus number_e to_power x = exp(x) by Th9
                         .=exp.x by SIN_COS:def 27;
thus number_e #R x =exp(x) by Th9
                         .=exp.x by SIN_COS:def 27;
end;

theorem
number_e >= 2
proof
 exp.1 >=2
proof
A1: for th,th1,th2,th3 be real number
         for n be Nat holds th |^ 0=1
    & th |^ (2*n)=(th |^ n)|^ 2 & th |^ 1=th & th |^ 2=th * th
    & (-1)|^(2*n)=1 &(-1)|^ (2*n+1)=-1
 proof
   let th,th1,th2,th3 be real number;
   let n be Nat;
   thus th |^ 0=th GeoSeq.0 by PREPOWER:def 1
          .=1 by PREPOWER:4;
   thus th |^ (2*n)=(th |^ n)|^ 2 by NEWTON:14;
   thus A2:  th |^ 1=th GeoSeq.(0+1)by PREPOWER:def 1
       .=th GeoSeq.0 * th by PREPOWER:4
       .=1* th by PREPOWER:4
       .=th;
    thus th |^ 2=th |^ (1+1)
         .= th*th by A2,NEWTON:13;
    thus A3: (-1)|^(2*n)=(1)|^(2*n) by POWER:1
            .=1 by NEWTON:15;
    thus (-1)|^ (2*n+1)=(-1)|^ (2*n)*(-1) by NEWTON:11
             .=-1 by A3;
 end;

 A4:for n be Nat st 1<= n holds Partial_Sums(1 ExpSeq).n>=2
 proof
   defpred X[Integer] means Partial_Sums(1 ExpSeq).$1>=2;
    Partial_Sums(1 ExpSeq).1
      =Partial_Sums(1 ExpSeq).0+(1 ExpSeq).(0+1) by SERIES_1:def 1
     .=(1 ExpSeq).0+(1 ExpSeq).1 by SERIES_1:def 1
     .=(1 ExpSeq).0 + (1 |^ 1 /(1!)) by SIN_COS:def 9
      .=1 |^ 0 /(0!) + (1 |^ 1 /(1!)) by SIN_COS:def 9
      .=1 + (1 |^ 1 /(1!)) by A1,NEWTON:18
      .=1 + 1/1 by A1,NEWTON:19
      .=2;
  then A5: X[1];
  A6: for ni be Integer st 1 <=ni  holds X[ni] implies X[ni+1]
      proof
        let ni be Integer such that
        A7: 1 <=ni;
        assume
        A8: Partial_Sums(1 ExpSeq).ni>=2;
         0 <=ni by A7;
       then reconsider n=ni as Nat by INT_1:16;
        A9: Partial_Sums(1 ExpSeq).(n+1)
        =Partial_Sums(1 ExpSeq).n+(1 ExpSeq).(n+1) by SERIES_1:def 1
        .=Partial_Sums(1 ExpSeq).n+1 |^(n+1)/((n+1)!) by SIN_COS:def 9;
       A10: 1 |^(n+1) >0 by PREPOWER:13;
          (n+1)!>0 by SIN_COS:42;
       then 1 |^(n+1)/((n+1)!)>0 by A10,REAL_2:127;
       then Partial_Sums(1 ExpSeq).n+1 |^(n+1)/((n+1)!)>
       Partial_Sums(1 ExpSeq).n by REAL_1:69;
       hence thesis by A8,A9,AXIOMS:22;
      end;
       for n be Integer st 1<=n holds X[n] from INT_1:sch 2(A5,A6);
    hence thesis;
 end;

 A11:for n be Nat holds (Partial_Sums(1 ExpSeq)^\1).n>=2
   proof
   let n be Nat;
    A12:  (Partial_Sums(1 ExpSeq)^\1).n
      = Partial_Sums(1 ExpSeq).(n+1) by SEQM_3:def 9;
      1<=n+1 by NAT_1:29;
    hence (Partial_Sums(1 ExpSeq)^\1).n >=2 by A4,A12;
   end;
    (1 ExpSeq) is summable by SIN_COS:49;
  then Partial_Sums(1 ExpSeq) is convergent &
   lim(Partial_Sums(1 ExpSeq))=Sum(1 ExpSeq) by SERIES_1:def 2,def 3;
   then Partial_Sums(1 ExpSeq)^\1 is convergent &
          lim(Partial_Sums(1 ExpSeq)^\1)=Sum(1 ExpSeq) by SEQ_4:33;
 then Sum(1 ExpSeq)>=2 by A11,SIN_COS:41;
 hence thesis by SIN_COS:def 26;
end;
 hence number_e >= 2 by IRRAT_1:def 7,SIN_COS:def 27;
end;

then Lm4: number_e > 1 & number_e > 0 by AXIOMS:22;

theorem Th12:
log(number_e,exp(x)) = x
 proof
A1:exp(x) > 0 by SIN_COS:60;
   number_e to_power x =exp(x) by Th9;
  hence thesis by A1,Lm4,POWER:def 3;
 end;

theorem Th13:
log(number_e,exp.x) = x
proof
   thus log(number_e,exp.x) =log(number_e,exp(x)) by SIN_COS:def 27
    .=x by Th12;
 end;

theorem Th14:
y > 0 implies exp( log(number_e,y)) = y
proof
   assume y > 0;
   then number_e to_power log(number_e,y) = y by Lm4,POWER:def 3;
   hence thesis by Th9;
end;

theorem Th15:
y > 0 implies exp. ( log(number_e,y)) = y
 proof
   assume
  A1: y > 0;
  thus exp. ( log(number_e,y))
  =exp( log(number_e,y)) by SIN_COS:def 27
  .= y by A1,Th14;
 end;

theorem Th16:
  exp is one-to-one &
  exp is_differentiable_on REAL &
  exp is_differentiable_on [#](REAL) &
  (for x be Real holds  diff(exp,x)=exp.x ) &
  (for x be Real holds 0 < diff(exp,x) ) &
  dom(exp)=REAL & dom(exp)=[#](REAL) &
  rng(exp)=right_open_halfline(0)
  proof
   now let x1,x2 be set such that
   A1:  x1 in dom exp and
   A2:  x2 in dom exp and
   A3:  exp.x1=exp.x2;
   reconsider p1=x1 as Real by A1,SIN_COS:51;
   reconsider p2=x2 as Real by A2,SIN_COS:51;
    thus x1=log(number_e,exp.p1) by Th13
        .=log(number_e,exp.p2) by A3
        .=x2 by Th13;
    end;
hence exp is one-to-one by FUNCT_1:def 8;
thus exp is_differentiable_on REAL &
     exp is_differentiable_on [#](REAL)
     by SIN_COS:71,SUBSET_1:def 4;
 thus for x be Real holds diff(exp,x)=exp.x by SIN_COS:71;
hereby let x be Real;
   diff(exp,x)=exp.x by SIN_COS:71;
   hence diff(exp,x) > 0 by SIN_COS:59;
end;
thus dom(exp)=REAL & dom(exp)=[#](REAL)
 by SIN_COS:51,SUBSET_1:def 4;
thus rng(exp)=right_open_halfline(0)
proof
 now  let y be set such that
 A4: y in rng(exp);
   consider x be set such that
    A5:  x in dom(exp) and
    A6:  y=exp.x by A4,FUNCT_1:def 5;
    reconsider x as Real by A5,SIN_COS:51;
    reconsider y1=y as Real by A6;
     exp.x > 0 by SIN_COS:59;
    then y1 in {g where g is Real : 0<g} by A6;
    hence y in right_open_halfline(0) by LIMFUNC1:def 3;
end;
then A7: rng(exp)  c=  right_open_halfline(0) by TARSKI:def 3;
 now  let y be set such that
 A8: y in right_open_halfline(0);
     y in {g where g is Real : 0<g} by A8,LIMFUNC1:def 3;
   then consider g be Real such that
    A9: y=g & 0 < g;
   reconsider y1=y as Real by A9;
     y1=exp.( log(number_e,y1) ) by A9,Th15;
   hence  y in rng(exp) by FUNCT_1:def 5,SIN_COS:51;
end;
then right_open_halfline(0) c= rng(exp) by TARSKI:def 3;
 hence rng(exp)=right_open_halfline(0) by A7,XBOOLE_0:def 10;
end;
end;

registration
  cluster exp -> one-to-one;
  coherence by Th16;
end;

theorem Th17:
exp"  is_differentiable_on dom (exp") &
 for x be real number st x in dom (exp") holds diff(((exp)"),x) = 1/x
proof
thus
  exp"  is_differentiable_on dom (exp") by Th16,FDIFF_2:45;
   let x be real number such that
      A1: x in dom ((exp)");
      A2: x in rng (exp) by A1,FUNCT_1:55;
       diff(exp, ((exp)").x)=exp.( ((exp)").x) by Th16
                                .=x by A2,FUNCT_1:57;
      hence diff(((exp)"),x) = 1/x by A1,Th16,FDIFF_2:45;
end;

registration
  cluster right_open_halfline(0) -> non empty;
coherence
proof
   1 in {g where g is Real : 0<g};
  hence thesis by LIMFUNC1:def 3;
end;
end;

definition
let a be real number;
func log_ a -> PartFunc of REAL,REAL means :Def2:
 dom it= right_open_halfline(0) & for d be Element of
 right_open_halfline(0)  holds it.d= log(a,d);
 existence
 proof
   reconsider a as Real by XREAL_0:def 1;
   defpred X[set] means $1 in right_open_halfline(0);
   deffunc U(Real) = log(a,$1);
  consider f being PartFunc of REAL,  REAL such that
  A1:   for d be Element of REAL
              holds d in dom f iff X[d] and
  A2:   for d be Element of REAL
             st d in dom f holds f/.d = U(d)
       from PARTFUN2:sch 2;
  take f;
       dom(f) c=REAL  by RELSET_1:12;
     then for x be set st x in dom f
                 holds x in right_open_halfline(0) by A1;
       then A3: dom(f) c= right_open_halfline(0) by TARSKI:def 3;
     for x be set st x in right_open_halfline(0)
                holds x in dom f by A1;
       then right_open_halfline(0)  c=dom(f) by TARSKI:def 3;
 hence A4: dom(f)=right_open_halfline(0) by A3,XBOOLE_0:def 10;
     let d be Element of right_open_halfline(0);
      f/.d = log(a,d) by A2,A4;
      hence thesis by A4,FINSEQ_4:def 4;
 end;
 uniqueness
 proof
   let f1,f2 be PartFunc of REAL, REAL;
   assume
   A5: dom f1= right_open_halfline(0) & for d
      being Element of right_open_halfline(0) holds f1.d=log(a,d);
   assume
   A6: dom f2= right_open_halfline(0) & for d
      being Element of right_open_halfline(0) holds f2.d=log(a,d);
      for d being Element of REAL
     st d in right_open_halfline(0) holds f1.d = f2.d
   proof
    let d be Element of REAL such that
   A7:   d in  right_open_halfline(0);
      thus f1.d=log(a,d) by A5,A7
       .=f2.d by A6,A7;
   end;
  hence f1=f2 by A5,A6,PARTFUN1:34;
 end;
 end;

theorem Th18:
  log_ number_e=(exp)" &
  log_ number_e is one-to-one &
  dom(log_ number_e) =right_open_halfline(0) &
  rng(log_ number_e)  =REAL &
  log_ number_e  is_differentiable_on right_open_halfline(0) &
  (for x be Real st x > 0 holds log_ number_e  is_differentiable_in x) &
  (for x be Element of right_open_halfline(0)
     holds diff((log_ number_e),x)=1/x) &
  (for x  be Element of right_open_halfline(0)
     holds 0 < diff((log_ number_e),x))
proof
A1:  dom  ((exp)") =  right_open_halfline(0) by Th16,FUNCT_1:55;
  then A2:   dom  ((exp)") = dom log_ number_e
 by Def2;
  for d being Element of REAL
     st d in right_open_halfline(0) holds
     ((exp)").d = (log_ number_e).d
   proof
    let d  be  Element of REAL  such that
   A3:   d in  right_open_halfline(0);
    ((exp)").d=log(number_e,d)
   proof
      d in {g where g is Real : 0<g} by A3,LIMFUNC1:def 3;
     then consider g be Real such that
     A4:   g=d & g > 0;
      d=exp.(log(number_e,d)) by A4,Th15;
     hence
       ((exp)").d=log(number_e,d)
    by Th16,FUNCT_1:54;
   end;
   hence ((exp)").d=(log_ number_e).d by A3,Def2;
 end;
 hence
A5: log_ number_e=(exp)" by A1,A2,PARTFUN1:34;
 hence
  log_ number_e is one-to-one by FUNCT_1:62;
A6: dom(log_ number_e) = right_open_halfline(0)
   by Def2;
A7:  for x be Real st x > 0
     holds log_ number_e  is_differentiable_in x
  proof
     let x be Real such that
     A8: x > 0;
     A9:x in right_open_halfline(0)
       proof
          x in {g where g is Real : 0<g} by A8;
          hence thesis by LIMFUNC1:def 3;
       end;
      right_open_halfline(0) is open Subset of REAL
         &
          right_open_halfline(0) c= dom  (log_ number_e)
        by A1,Def2,Th17,FDIFF_1:17;
    hence
      (log_ number_e)   is_differentiable_in x
 by A1,A5,A9,Th17,FDIFF_1:16;
 end;

  for x be Element of right_open_halfline(0)
       holds  0 < diff((log_ number_e),x)
proof
    let x be Element of right_open_halfline(0);
       x in right_open_halfline(0);
      then x in {g where g is Real : 0<g}
   by LIMFUNC1:def 3;
    then consider g be Real such that
      A10: x=g & 0 < g;
    A11: x"> 0 by A10,REAL_1:72;
      1/x = x" by XCMPLX_1:217;
    hence 0 < diff((log_ number_e),x) by A1,A5,A11,Th17;
end;
hence thesis by A5,A6,A7,Th16,Th17,FUNCT_1:55;
end;

theorem
 f is_differentiable_in x0 implies
 exp*f is_differentiable_in x0 & diff(exp*f,x0) = exp.(f.x0)*diff(f,x0)
proof
  A1: x0 is Real by XREAL_0:def 1;
assume
 A2:      f is_differentiable_in x0;
 A3: exp  is_differentiable_in f.x0 by Th16,FCONT_3:4,FDIFF_1:16;
hence exp*f is_differentiable_in x0 by A1,A2,FDIFF_2:13;
thus diff(exp*f,x0) = diff(exp,f.x0)*diff(f,x0) by A1,A2,A3,FDIFF_2:13
              .=exp.(f.(x0)) *diff(f,x0) by Th16;
end;

theorem
  f is_differentiable_in x0 & f.x0 > 0 implies
  (log_ number_e) *f is_differentiable_in x0 &
   diff( (log_ number_e) *f,x0) = diff(f,x0)/(f.x0)
proof
assume that
 A1:      f is_differentiable_in x0
    and
 A2:      f.x0 > 0;
 A3:      f.x0 in right_open_halfline(0)
 proof
     f.x0 in {g where g is Real : 0<g} by A2;
  hence thesis by LIMFUNC1:def 3;
 end;
    right_open_halfline(0)
        is open Subset of REAL
       &
        right_open_halfline(0)
        c= dom  (log_ number_e)
      by Th18,FDIFF_1:17;
  then A4: (log_ number_e) is_differentiable_in f.x0 by A3,Th18,FDIFF_1:16;
A5:  x0 is Real by XREAL_0:def 1;
hence (log_ number_e) *f is_differentiable_in x0 by A1,A4,FDIFF_2:13;
thus diff((log_ number_e) *f,x0)
               = diff((log_ number_e),f.x0)*diff(f,x0)
             by A1,A4,A5,FDIFF_2:13
              .=(1/(f.(x0)))   *diff(f,x0)
             by A3,Th18
              .=diff(f,x0)/(f.(x0)) *1 by XCMPLX_1:76
              .=diff(f,x0)/(f.(x0));
end;

definition
let p be real number;
func #R p -> PartFunc of REAL,REAL means :Def3:
 dom it= right_open_halfline(0) & for d be Element of
 right_open_halfline(0) holds it.d= d #R p;
 existence
 proof
   reconsider p as Real by XREAL_0:def 1;
   defpred X[set] means $1 in right_open_halfline(0);
   deffunc U(Real) = $1 #R p;
  consider f being PartFunc of REAL,  REAL such that
  A1:   for d be Element of REAL
              holds d in dom f iff X[d] and
  A2:   for d be Element of REAL
             st d in dom f holds f/.d = U(d)
       from PARTFUN2:sch 2;
  take f;
       dom(f) c=REAL
                 by RELSET_1:12;
     then for x be set st x in dom f
                 holds x in right_open_halfline(0) by A1;
       then A3: dom(f) c= right_open_halfline(0) by TARSKI:def 3;
     for x be set st x in right_open_halfline(0)
                holds x in dom f by A1;
       then right_open_halfline(0)  c=dom(f) by TARSKI:def 3;
 hence A4: dom(f)=right_open_halfline(0)  by A3,XBOOLE_0:def 10;
     let d be Element of right_open_halfline(0);
       f/.d = d #R p by A2,A4;
      hence thesis by A4,FINSEQ_4:def 4;
 end;
 uniqueness
 proof
   let f1,f2 be PartFunc of REAL,  REAL;
   assume
   A5: dom f1= right_open_halfline(0) & for d
      being Element of right_open_halfline(0) holds f1.d=d #R p;
   assume
   A6: dom f2= right_open_halfline(0) & for d
      being Element of right_open_halfline(0) holds f2.d=d #R p;
      for d being Element of REAL
     st d in right_open_halfline(0) holds f1.d = f2.d
   proof
    let d  be  Element of REAL  such that
   A7:   d in  right_open_halfline(0);
      thus f1.d=d #R p by A5,A7
                    .=f2.d by A6,A7;
   end;
  hence f1=f2 by A5,A6,PARTFUN1:34;
 end;
 end;

theorem Th21:
x > 0  implies ( #R p) is_differentiable_in x & diff(( #R p),x)=p*(x #R (p-1))
proof
A1:  x is Real & p is Real by XREAL_0:def 1;
set f= #R p;
A2: dom(f)=right_open_halfline(0) by Def3;
A3: for x be Real st 0 < x holds
     exp*(p(#)(log_ number_e)) is_differentiable_in x &
     diff(exp*(p(#)(log_ number_e)),x) =p*(x #R (p-1))
proof
 let x be Real such that
  A4: x > 0;
  A5: 0 < x to_power p by A4,POWER:39;
  A6: x in right_open_halfline(0)
  proof
     x in {g where g is Real : 0<g} by A4;
  hence thesis by LIMFUNC1:def 3;
  end;
 then A7: x in dom (p(#)(log_ number_e)) by Th18,SEQ_1:def 6;

 A8: (log_ number_e) is_differentiable_in x by A4,Th18;
  then A9: p(#)(log_ number_e) is_differentiable_in x by A1,FDIFF_1:23;
  A10: exp  is_differentiable_in (p(#)(log_ number_e)).x by SIN_COS:70;
   hence  exp*(p(#)(log_ number_e))
   is_differentiable_in x by A9,FDIFF_2:13;
  A11:diff(exp,(p(#)(log_ number_e)).x)
      = exp.((p(#)(log_ number_e)).x) by Th16
      .=exp.(p*(log_ number_e).x ) by A7,SEQ_1:def 6
      .=exp.(p*log(number_e,x)) by A6,Def2
      .=exp.(log(number_e,x to_power p)) by A4,Lm4,POWER:63
      .=x to_power p by A5,Th15;
   diff((p(#)(log_ number_e)),x)
    =p*diff((log_ number_e),x) by A1,A8,FDIFF_1:23
    .=p*(1/x) by A6,Th18;
  hence
     diff(exp*(p(#)(log_ number_e)),x)
        =(x to_power p) *(p*(1/x) ) by A9,A10,A11,FDIFF_2:13
       .=p*((x to_power p) *(1/x) )
       .=p*((x to_power p) *(1/(x to_power 1) ) ) by POWER:30
       .=p*(  (x to_power p) *(x to_power (-1) ) ) by A4,POWER:33
       .=p*( x to_power (p+(-1))) by A4,POWER:32
       .=p*(x #R (p-1)) by A4,POWER:def 2;
end;
 exp*(p(#)(log_ number_e)) = f
proof
  A12: dom (exp*(p(#)(log_ number_e)))
        = right_open_halfline(0)
    proof
      rng (p(#)(log_ number_e)) c=dom (exp) by Th16,RELSET_1:12;
    hence
      dom (exp*(p(#)(log_ number_e)))
     = dom (p(#)(log_ number_e)) by RELAT_1:46
     .=right_open_halfline(0) by Th18,SEQ_1:def 6;
   end;

   for d being Element of REAL
      st d in right_open_halfline(0) holds (exp*(p(#)(log_ number_e))).d = f.d
   proof
    let d  be  Element of REAL  such that
   A13:   d in  right_open_halfline(0);
   A14: d in dom (p(#)(log_ number_e)) by A13,Th18,SEQ_1:def 6;
    d in {g where g is Real : 0<g} by A13,LIMFUNC1:def 3;
        then consider g be Real such that
        A15:   g=d & g > 0;
   A16: 0 < d to_power p by A15,POWER:39;
   thus
       (exp*(p(#)(log_ number_e))).d = (exp*(p(#)(log_ number_e)))/.d
     by A12,A13,FINSEQ_4:def 4
       .= exp/.((p(#)(log_ number_e))/.d) by A12,A13,PARTFUN2:6
      .= exp/.((p(#)(log_ number_e)).d) by A14,FINSEQ_4:def 4
     .= exp.((p(#)(log_ number_e)).d) by Th16,FINSEQ_4:def 4
       .=exp.(p*(log_ number_e).d ) by A14,SEQ_1:def 6
      .=exp.(p*log(number_e,d)) by A13,Def2
      .=exp.(log(number_e,d to_power p)) by A15,Lm4,POWER:63
      .=d to_power p by A16,Th15
       .=(d #R p) by A15,POWER:def 2
       .=f.d by A13,Def3;
     end;
  hence exp*(p(#)(log_ number_e)) = f by A2,A12,PARTFUN1:34;
 end;
hence thesis by A1,A3;
end;

theorem
   f is_differentiable_in x0 & f.x0 > 0 implies
    ( #R p) *f is_differentiable_in x0 &
     diff(  ( #R p) *f,x0) = p*( (f.x0) #R (p-1)) * diff(f,x0)
proof
 assume that
 A1:      f is_differentiable_in x0
    and
 A2:      f.x0 > 0;
  A3:  ( #R p)   is_differentiable_in f.x0 by A2,Th21;
A4: x0 is Real by XREAL_0:def 1;
hence ( #R p) *f is_differentiable_in x0 by A1,A3,FDIFF_2:13;
thus diff( ( #R p) *f,x0)
               = diff( ( #R p),f.x0)*diff(f,x0) by A1,A3,A4,FDIFF_2:13
              .=p*( (f.x0) #R (p-1))*diff(f,x0) by A2,Th21;
end;

begin ::The Taylor expansions

definition
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
func diff(f,Z) -> Functional_Sequence of REAL,REAL means :Def4:
   it.0 = f|Z & for i be natural number holds it.(i+1) = (it.i) `| Z;
existence
proof
      deffunc G(set,PartFunc of REAL,REAL)= ( $2 )`|Z;
      defpred R[set,set,set] means
      ex h being PartFunc of REAL, REAL st $2 = h & $3 = h `| Z;
A1:   for n being Nat for x being Element of PFuncs(REAL,REAL)
      ex y being Element of PFuncs(REAL,REAL) st R[n,x,y]
      proof
        let n be Nat;
        let x be Element of PFuncs (REAL,REAL);
        reconsider x' = x as PartFunc of REAL, REAL by PARTFUN1:120;
        reconsider y = x' `| Z as Element of PFuncs (REAL,REAL)
          by PARTFUN1:119;
        ex h being PartFunc of REAL, REAL st x = h & y = h `| Z;
        hence thesis;
      end;
      reconsider fZ = f|Z as Element of PFuncs(REAL,REAL) by PARTFUN1:119;
      consider g be Function of NAT, PFuncs (REAL,REAL) such that
      A2: g.0 = fZ
      & for n being Element of NAT holds R[n,g.n,g.(n+1)]
      from RECDEF_1:sch 2(A1);
      defpred P[Nat] means g.$1 is PartFunc of REAL,REAL;
A3:    dom g = NAT by FUNCT_2:def 1;
       A4: P[0] by A2;
       A5:  for k be Nat st P[k] holds P[k+1]
       proof
       let k be Nat such that
         P[k];
         R[k,g.k,g.(k+1)] by A2; then
         consider h being PartFunc of REAL, REAL such that
A6:      g.k = h & g.(k+1) = h `| Z;
         reconsider gk=g.k as PartFunc of REAL,REAL by A6;
         g.(k+1)=( gk )`|Z by A6;
         hence thesis;
       end;
       for n be Nat holds P[n] from NAT_1:sch 1(A4,A5);
       then reconsider g as Functional_Sequence of REAL,REAL by A3,SEQFUNC:1;
       take g;
       thus g.0 = f|Z by A2;
       let i be natural number;
       i is Nat by ORDINAL2:def 21; then
       R[i,g.i,g.(i+1)] by A2;
       hence thesis;
end;
uniqueness
proof
 let seq1,seq2 be Functional_Sequence of REAL,REAL;
 assume that A7: seq1.0=f|Z
            & for n be natural number holds
                seq1.(n+1)=( seq1.n )`|Z  and
                   A8: seq2.0=f|Z
            & for n be natural number holds
                seq2.(n+1)=( seq2.n )`|Z;
   defpred P[Nat] means seq1.$1 = seq2.$1;
 A9: P[0] by A7,A8;
 A10: for k be Nat st P[k] holds P[k+1]
     proof let k be Nat;
      assume
 A11:     P[k];
      thus seq1.(k+1) = ( seq1.k )`|Z by A7
                               .= seq2.(k+1) by A8,A11;
      end;
    for n be Nat  holds P[n] from NAT_1:sch 1(A9,A10);
 hence seq1 = seq2 by SEQFUNC:2;
end;
end;

definition
let f be PartFunc of REAL,REAL;
let n be natural number;
let Z be Subset of REAL;
pred f is_differentiable_on n, Z means :Def5:
     for i be Nat st i <= n-1
             holds diff(f,Z).i is_differentiable_on Z;
 end;

theorem Th23:
for f be PartFunc of REAL,REAL, Z be Subset of REAL, n be natural number
 st f is_differentiable_on n,Z
   for m be natural number st m <= n holds f is_differentiable_on m, Z
proof
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
let n be natural number such that
A1:    f is_differentiable_on n, Z;
let m be natural number such that
 A2: m <=n;
 now let i be Nat such that
   A3: i <=m-1;
    m-1 <=n-1  by A2,REAL_1:92;
   then i <=n-1 by A3,AXIOMS:22;
   hence
      diff(f,Z).i
     is_differentiable_on Z by A1,Def5;
end;
hence thesis by Def5;
end;

definition
  let f be PartFunc of REAL,REAL;
  let Z be Subset of REAL;
  let a,b be real number;
  func Taylor(f,Z,a,b) -> Real_Sequence means :Def6:
  for n be natural number holds it.n = (diff(f,Z).n).a * (b-a) |^ n / (n!);
existence
proof
deffunc F(Nat)=(diff(f,Z).$1).a * (b-a) |^ $1 /($1!);
consider seq be Real_Sequence such that
 A1:   for n be Nat holds seq.n=F(n)
       from SEQ_1:sch 1;
take seq;
let n be natural number;
 n is Nat by ORDINAL2:def 21;
hence thesis by A1;
end;
uniqueness
proof
deffunc F(natural number)=(diff(f,Z).$1).a * (b-a) |^ $1 /($1!);
let s1,s2 be Real_Sequence such that
 A2:   for n be natural number holds s1.n=F(n)
        and
 A3:   for n be natural number holds s2.n=F(n);
  now let x be set such that
 A4: x in NAT;
 reconsider n=x as Nat by A4;
 thus s1.x = F(n) by A2
        .=s2.x by A3;
 end;
 hence thesis by SEQ_1:8;
 end;
 end;

Lm5:
for b be Real holds
   ex g be PartFunc of REAL,REAL st (dom(g)=REAL &
            for x be Real holds
            g.x=(b-x) &
            for x be Real holds
                g is_differentiable_in x & diff(g,x)=-1 )
proof
let b  be Real;
defpred X[set] means $1 in REAL;
deffunc U(Real) = (b-$1);
consider g being PartFunc of REAL,REAL such that
  A1:   for d be Element of REAL
              holds d in dom g iff X[d] and
  A2:   for d be Element of REAL
             st d in dom g holds g/.d = U(d) from PARTFUN2:sch 2;
  take g;
     A3: dom(g)=REAL
         proof
          A4: dom(g) c= REAL by RELSET_1:12;
           for x be set st x in REAL
                  holds x in dom g by A1;
          then REAL  c=dom(g) by TARSKI:def 3;
          hence dom (g) =REAL by A4,XBOOLE_0:def 10;
        end;
   A5: for d be Element of REAL holds g.d = b-d
      proof
        let d be Element of REAL;
                    g/.d =  b - d by A2,A3;
        hence g.d = (b-d) by A3,FINSEQ_4:def 4;
      end;
    A6: for d be Real st d in  REAL
             holds g.d = (-1)*d + b
      proof
        let d be Real such that d in REAL;
        thus g.d=b-d by A5
           .= (-1)*d + b;
      end;

  A7: dom(g) is open Subset of REAL by A3,FCONT_3:4,SUBSET_1:def 4;

 then A8: g is_differentiable_on dom(g)
   & for x be Real st x in dom(g)  holds (g`|dom(g)).x = -1
  by A3,A6,FDIFF_1:31;
   for x be Real holds
                g  is_differentiable_in x & diff(g,x)=-1
      proof
        let d be Real;
        thus g is_differentiable_in d
        by A3,A7,A8,FDIFF_1:16;
        thus diff(g,d)=(g`|dom(g)).d
                 by A3,A8,FDIFF_1:def 8
                      .= -1 by A3,A6,A7,FDIFF_1:31;
      end;
 hence thesis by A3,A5;
 end;

Lm6:
for n be Nat
for l,b be Real holds
   ex g be PartFunc of REAL,REAL
     st ( dom(g)=REAL &
            for x be Real holds
            g.x=l*(b-x) |^ (n+1)  /((n+1)!) &
            for x be Real holds
            g is_differentiable_in x
              & diff(g,x)=-l* (b-x) |^ (n) /((n)!) )
proof
let n be Nat;
let l be Real;
let b  be Real;
defpred X[set] means $1 in REAL;
deffunc U(Real)
= l*(b-$1) |^ (n+1)  /((n+1)!);
consider g being PartFunc of REAL,  REAL such that
  A1:   for d be Element of REAL
              holds d in dom g iff X[d] and
  A2:   for d be Element of REAL
             st d in dom g holds g/.d = U(d)
       from PARTFUN2:sch 2;
  take g;
     A3: dom(g)=REAL
         proof
          A4: dom(g) c= REAL by RELSET_1:12;
           for x be set st x in REAL
                  holds x in dom g by A1;
          then REAL  c=dom(g) by TARSKI:def 3;
          hence dom (g) =REAL by A4,XBOOLE_0:def 10;
        end;
    A5:   for d be Element of REAL
             holds g.d = l*(b-d) |^ (n+1)  /((n+1)!)
      proof
        let d be Element of REAL;
          g/.d = l*(b-d) |^ (n+1)  /((n+1)!) by A2,A3;
        hence g.d = l*(b-d) |^ (n+1)  /((n+1)!)
                            by A3,FINSEQ_4:def 4;
      end;

   consider f be PartFunc of REAL,REAL such that
      A6:  dom(f)=REAL and
      A7: for x be Real holds f.x=(b-x) and
      A8:  for x be Real holds
                f is_differentiable_in x
               &   diff(f,x)=-1 by Lm5;
     A9: for x be Real holds
              ( #Z (n+1)) *f is_differentiable_in x
                &
              diff(( #Z (n+1)) *f,x) =- (n+1) *(b-x) #Z n
      proof
      let x be Real;
       A10: f is_differentiable_in x by A8;
       A11: #Z (n+1)  is_differentiable_in f.x  &
              diff(( #Z(n+1)),f.x) = (n+1) * (f.x) #Z ((n+1)-1) by Th2;
       hence ( #Z (n+1)) *f is_differentiable_in x by A10,FDIFF_2:13;
       thus diff(( #Z (n+1)) *f,x)
              = (n+1) *(f.x) #Z ((n+1)-1)*diff(f,x)
             by A10,A11,FDIFF_2:13
             .= (n+1) *(f.x) #Z ((n+1)-1)  *(-1) by A8
             .= (n+1) *(b-x) #Z ((n+1)-1)  *(-1) by A7
             .= -(n+1) *(b-x) #Z ((n+1)-1)
             .=-(n+1)*(b-x) #Z n;
      end;
      A12:( l/((n+1)!)) (#)  ( #Z (n+1) *f) =g
      proof
      A13: dom( #Z (n+1)) = REAL by FUNCT_2:def 1;
        rng (f) c= REAL by RELSET_1:12;
         then A14: dom ( #Z (n+1)  *f ) = dom(f) by A13,RELAT_1:46;
          then A15: dom (( l/((n+1)!)) (#) ( #Z (n+1)  *f ))
                           = dom(g) by A3,A6,SEQ_1:def 6;
       now let x be Element of REAL such that
        A16:  x in dom (  l/((n+1)!) (#) ( #Z (n+1) *f ) );
        thus (( l/((n+1)!)) (#) ( #Z (n+1) *f ) ).x
             = (l/((n+1)!))*(  #Z (n+1) *f ).x by A16,SEQ_1:def 6
             .= (l/((n+1)!))*(  #Z (n+1) *f )/.x by A6,A14,FINSEQ_4:def 4
             .= (l/((n+1)!))*  #Z (n+1)/.(f/.x) by A6,A14,PARTFUN2:6
             .= (l/((n+1)!))*  #Z (n+1)/.(f.x) by A6,FINSEQ_4:def 4
             .=l/((n+1)!)*(  #Z (n+1).(f.x) ) by A13,FINSEQ_4:def 4
            .=l/((n+1)!)*( (f.x) #Z (n+1)  ) by Def1
            .=l/((n+1)!)*( (f.x) |^ (n+1)  ) by PREPOWER:46
            .=l/((n+1)!)*( (b-x) |^ (n+1)  ) by A7
            .=l*(b-x) |^ (n+1)/((n+1)!) by XCMPLX_1:75
            .=g.x by A5;
      end;
      hence thesis by A15,PARTFUN1:34;
     end;

      now let x be Real;
     A17:   ( #Z (n+1) *f) is_differentiable_in x by A9;
     hence  ( l/((n+1)!) ) (#)  ( #Z (n+1) *f)
                   is_differentiable_in x by FDIFF_1:23;
     A18:   (n+1) /  ((n+1)!) = (1*(n+1))/ (n! * (n+1)) by NEWTON:21
            .=1/(n!) by XCMPLX_1:92;
     thus diff((l/((n+1)!)) (#)  ( #Z (n+1) *f),x)
            = (l/((n+1)!)) *diff( #Z (n+1) *f,x) by A17,FDIFF_1:23
            .= diff( #Z (n+1) *f,x)/((n+1)!)*l by XCMPLX_1:76
            .=l *( (- (n+1) *(b-x) #Z n )/  ((n+1)!)) by A9
            .=l *( (- (n+1) *(b-x) |^ n )/  ((n+1)!)) by PREPOWER:46
            .=l * (- (n+1) *(b-x) |^ n )/  ((n+1)!) by XCMPLX_1:75
            .=(-l) *((n+1) * (b-x) |^ n)/((n+1)!)
            .=(-l) *(((n+1) *(b-x) |^ n )/  ((n+1)!)) by XCMPLX_1:75
            .=(-l) *( (b-x) |^ n * ( (n+1) /  ((n+1)!)) ) by XCMPLX_1:75
            .=(-l) *( (b-x) |^ n /  (n!)  ) by A18,XCMPLX_1:100
            .=-(l*( (b-x) |^ n /  (n!))  )
            .=-(l*(b-x) |^ n /  (n!)  ) by XCMPLX_1:75;
     end;
    hence thesis by A3,A5,A12;
 end;

Lm7:
for n be Nat
for f be PartFunc of REAL,REAL
for Z be Subset of REAL
for b be Real holds
   ex g be PartFunc of REAL,REAL
     st ( dom(g)=Z & for x be Real st x in Z holds
            g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n )
proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
let b be Real;
defpred X[set] means $1 in Z;
deffunc U(Real) = f.b-Partial_Sums(Taylor(f,Z,$1,b)).n;
consider g being PartFunc of REAL,  REAL such that
  A1:   for d be Element of REAL
              holds d in dom g iff X[d] and
  A2:   for d be Element of REAL
             st d in dom g holds g/.d = U(d)
       from PARTFUN2:sch 2;
  take g;
     A3: dom(g)=Z
         proof
            dom(g) c=REAL by RELSET_1:12;
          then for x be set st x in dom g
                   holds x in Z by A1;
          then A4: dom(g) c= Z by TARSKI:def 3;
           for x be set st x in Z
                  holds x in dom g by A1;
          then Z  c=dom(g) by TARSKI:def 3;
          hence dom (g) =Z by A4,XBOOLE_0:def 10;
        end;
     for d be Real st d in Z
             holds g.d =f.b-Partial_Sums(Taylor(f,Z,d,b)).n
      proof
        let d be Real such that
         A5: d in Z;
            g/.d = f.b-Partial_Sums(Taylor(f,Z,d,b)).n by A2,A3,A5;
        hence g.d =f.b-Partial_Sums(Taylor(f,Z,d,b)).n
                            by A3,A5,FINSEQ_4:def 4;
      end;
    hence thesis by A3;
 end;

theorem Th24:
for f be PartFunc of REAL,REAL
for Z be Subset of REAL
for n be Nat
      st  f is_differentiable_on n,  Z
for a,b be Real st ( a < b & ].a,b.[ c= Z )
holds
    diff(f,Z).n | ].a,b.[ = diff(f,].a,b.[).n
proof
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
defpred P[Nat] means f is_differentiable_on $1,  Z implies
  for a,b be Real st
     ( a < b & ].a,b.[ c=Z ) holds
    diff(f,Z).$1  | ].a,b.[ = diff(f,].a,b.[).$1;
 A1: P[0]
    proof
    assume
     f is_differentiable_on 0,  Z;
    let a,b be Real such that
       A2: ( a < b & ].a,b.[ c=Z );
     thus (diff(f,Z).0) |].a,b.[
     =(f|Z)|].a,b.[ by Def4
     .=f|].a,b.[ by A2,FUNCT_1:82
     .=diff(f,].a,b.[).0 by Def4;
    end;
A3: for k be Nat st P[k] holds P[k+1]
   proof
   let k be Nat such that
     A4: P[k];
    assume
    A5:   f is_differentiable_on (k+1),  Z;
    let a,b be Real such that
     A6: ( a < b   & ].a,b.[ c=Z );
    A7: k <= k+1 by NAT_1:29;
     k <= (k+1)-1;
    then A8:(diff(f,Z).k) is_differentiable_on Z by A5,Def5;
    then A9: (diff(f,Z).k) is_differentiable_on  ].a,b.[
      by A6,FDIFF_1:34;
     A10: dom(((diff(f,Z).k)`|Z)| ].a,b.[)
     =dom((diff(f,Z).k)`|Z) /\ ].a,b.[ by FUNCT_1:68
     .=Z /\ ].a,b.[ by A8,FDIFF_1:def 8
     .=].a,b.[ by A6,XBOOLE_1:28;
     A11: dom((diff(f,Z).k)`| ].a,b.[) = ].a,b.[
     by A9,FDIFF_1:def 8;
     A12: now
       let x be Real such that
       A13: x in dom(((diff(f,Z).k)`|Z)| ].a,b.[);
      thus (((diff(f,Z).k)`|Z)| ].a,b.[).x
       =((diff(f,Z).k)`|Z).x by A10,A13,FUNCT_1:72
      .=diff((diff(f,Z).k),x) by A6,A8,A10,A13,FDIFF_1:def 8
      .=((diff(f,Z).k)`|].a,b.[).x
      by A9,A10,A13,FDIFF_1:def 8;
     end;
     thus diff(f,Z).(k+1) | ].a,b.[
       =((diff(f,Z).k)`|Z)| ].a,b.[ by Def4
      .=(diff(f,Z).k)`| ].a,b.[ by A10,A11,A12,PARTFUN1:34
      .= ((diff(f,Z).k)|].a,b.[) `| ].a,b.[ by A9,FDIFF_2:16
      .=(diff(f,].a,b.[).k)`| ].a,b.[ by A4,A5,A6,A7,Th23
      .=diff(f,].a,b.[).(k+1) by Def4;
   end;
  thus for k be Nat holds P[k] from NAT_1: sch 1(A1,A3);
 end;

Lm8:
for f be PartFunc of REAL,REAL
for Z be Subset of REAL
for n be Nat
      st f is_differentiable_on n,  Z
for a,b be Real
     st
     ( a < b
  & [.a,b.] c= Z
  &  (diff(f,Z).n) is_continuous_on [.a,b.]
  &  f is_differentiable_on (n+1), ].a,b.[  )
  for g be PartFunc of REAL,REAL st (
            dom(g)=Z
            &
            for x be Real st x in Z holds
            g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n
        )
     holds g.b=0
           &
           g is_continuous_on [.a,b.]
           &
           g is_differentiable_on  ].a,b.[
           &
           for x be Real st x in ].a,b.[ holds
             diff(g,x)=-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /(n!)
proof
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
defpred P[Nat] means f is_differentiable_on $1,  Z implies
  for a,b be Real st
     ( a < b
  & [.a,b.] c=Z
  &  (diff(f,Z).$1) is_continuous_on [.a,b.]
  &  f is_differentiable_on ($1+1), ].a,b.[ )
  for g be PartFunc of REAL,REAL
     st (
            dom(g)=Z
            &
            for x be Real st x in Z holds
            g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).$1
        )
     holds
           g.b=0
           &
           g is_continuous_on [.a,b.]
           &
           g is_differentiable_on  ].a,b.[
           &
           for x be Real st x in ].a,b.[  holds
            diff(g,x)=-(diff(f,].a,b.[).($1+1)).x
            * (b-x) |^ ($1) / ( ($1)!);

A1:P[0]
proof
assume f is_differentiable_on 0,  Z;
let a,b be Real such that
 A2: a < b and
 A3: [.a,b.] c= Z
    and
 A4:  (diff(f,Z).0) is_continuous_on [.a,b.]
    and
 A5: f is_differentiable_on (0+1), ].a,b.[;
 let g be PartFunc of REAL,REAL such that
 A6: dom(g)=Z and
 A7:
    for x be Real st x in Z holds
            g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).0;
  A8: ].a,b.[ c=[.a,b.] by RCOMP_1:15;
  then A9: ].a,b.[ c= Z by A3,XBOOLE_1:1;
  A10: b in [.a,b.] by A2,RCOMP_1:15;
  hence g.b=f.b-Partial_Sums(Taylor(f,Z,b,b)).(0) by A3,A7
      .=f.b-Taylor(f,Z,b,b).0 by SERIES_1:def 1
      .=f.b  - (diff(f,Z).(0)).b * (b-b) |^ (0) /((0)!) by Def6
      .=f.b  - (f|Z).b * (b-b) |^ (0) /((0)!) by Def4
      .=f.b  - f.b * (b-b) |^ (0) /((0)!) by A3,A10,FUNCT_1:72
      .=f.b  - f.b * 1 by NEWTON:9,18
      .=0;
  consider y be PartFunc of REAL,REAL such that
      A11:  dom(y)=REAL
          and
      A12: for x be Real holds y.x=(f.b-x)
          and
      A13:  for x be Real holds
                y is_differentiable_in x
               &   diff(y,x)=-1 by Lm5;
 f|Z is_continuous_on [.a,b.] by A4,Def4;
then (f|Z)| [.a,b.]   is_continuous_on [.a,b.] by FCONT_1:16;
then (f|[.a,b.])    is_continuous_on [.a,b.] by A3,FUNCT_1:82;
then A14:f is_continuous_on [.a,b.] by FCONT_1:16;
then A15: [.a,b.] c= dom(f) by FCONT_1:def 2;
A16: rng(f) c= REAL by RELSET_1:12;
then A17: dom(y*f) = dom(f) by A11,RELAT_1:46;
A18: dom((y*f)|[.a,b.])=dom(y*f) /\ [.a,b.] by FUNCT_1:68
                      .=[.a,b.] by A15,A17,XBOOLE_1:28
                      .=Z /\ [.a,b.] by A3,XBOOLE_1:28
                      .=dom(g|[.a,b.]) by A6,FUNCT_1:68;
A19: g|[.a,b.]=(y*f)|[.a,b.]
proof
  now let xx be set
   such that
   A20: xx in dom(g|[.a,b.]);
    dom(g|[.a,b.])=dom(g) /\ [.a,b.] by FUNCT_1:68;
   then A21: dom(g|[.a,b.]) c= [.a,b.]
 by XBOOLE_1:17;
    then dom(g|[.a,b.]) c= REAL
 by XBOOLE_1:1;
   then reconsider x=xx as Real by A20;
        A22:  (b-x) |^ (0) /((0)!)=1 by NEWTON:9,18;
    A23: x in [.a,b.] by A20,A21;
     thus
       (g|[.a,b.]).xx
       =g.x by A20,FUNCT_1:70
      .=f.b-Partial_Sums(Taylor(f,Z,x,b)).(0) by A3,A7,A23
      .=f.b-Taylor(f,Z,x,b).0 by SERIES_1:def 1
      .=f.b  - (diff(f,Z).(0)).x * (b-x) |^ (0) /((0)!) by Def6
       .=f.b  - (f|Z).x * (b-x) |^ (0) /((0)!) by Def4
       .=f.b  - f.x * (b-x) |^ (0) /((0)!) by A3,A23,FUNCT_1:72
      .=f.b  - f.x * ((b-x) |^ (0) /((0)!)) by XCMPLX_1:75
      .=y.(f.x) by A12,A22
      .=(y*f).x by A15,A23,FUNCT_1:23
      .=((y*f)|[.a,b.]).xx by A18,A20,FUNCT_1:70;
  end;
hence thesis by A18,FUNCT_1:9;
end;

 f.:[.a,b.] c= rng(f) by RELAT_1:144;
then A24: f.:[.a,b.] c= REAL by A16,XBOOLE_1:1;
A25: REAL  is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;

 for x be Real st x in REAL
   holds y is_differentiable_in x by A13;
then y is_differentiable_on REAL
     by A11,A25,FDIFF_1:16;
then y is_continuous_on REAL
     by FDIFF_1:33;
then y is_continuous_on (f.:[.a,b.]) by A24,FCONT_1:17;
then (y*f) is_continuous_on [.a,b.] by A14,FCONT_1:26;
then (y*f)|[.a,b.]  is_continuous_on [.a,b.] by FCONT_1:16;
hence
  g is_continuous_on [.a,b.] by A19,FCONT_1:16;

 0 <= (0+1)-1;
then diff(f,].a,b.[).0  is_differentiable_on ].a,b.[ by A5,Def5;
 then f|].a,b.[  is_differentiable_on ].a,b.[ by Def4;
then A26: ].a,b.[ c= dom(f)&
   for x be Real st x in ].a,b.[ holds
    f|].a,b.[  is_differentiable_in x by A8,A15,FDIFF_1:16,XBOOLE_1:1;
then A27: f is_differentiable_on ].a,b.[ by FDIFF_1:def 7;
 A28: for x be Real st x in ].a,b.[ holds
          y *f is_differentiable_in x &
              diff( y*f,x) =-diff(f,].a,b.[).(0+1).x * (b-x) |^ (0) / ( (0)!)
      proof
      let x be Real
        such that
       A29: x in ].a,b.[;
       A30:  (b-x) |^ (0) /((0)!)=1 by NEWTON:9,18;
       A31: y is_differentiable_in  f.x by A13;
       A32:  diff(f,].a,b.[).(0+1)=( diff(f,].a,b.[).0)`|].a,b.[ by Def4
                                  .= (f|].a,b.[)`|].a,b.[ by Def4
                                  .= f`|].a,b.[ by A27,FDIFF_2:16;
       A33: f is_differentiable_in  x by A27,A29,FDIFF_1:16;
       hence y*f is_differentiable_in x by A31,FDIFF_2:13;
       thus diff( y*f,x)
              =diff(y, f.x)*diff(f,x) by A31,A33,FDIFF_2:13
             .= diff(y,f.x)*(f`|].a,b.[).x by A27,A29,FDIFF_1:def 8
             .= (-1)*diff(f,].a,b.[).(0+1).x by A13,A32
             .=-diff(f,].a,b.[).(0+1).x*((b-x) |^ (0) /((0)!)) by A30
             .=-diff(f,].a,b.[).(0+1).x*(b-x) |^ (0) /((0)!) by XCMPLX_1:75;
      end;
  rng(f) c= dom(y) by A11,RELSET_1:12;
then A34: dom(y*f) = dom(f) by RELAT_1:46;
A35: dom((y*f)|].a,b.[) = dom(y*f) /\ ].a,b.[ by FUNCT_1:68
                      .= ].a,b.[ by A26,A34,XBOOLE_1:28
                      .= Z /\ ].a,b.[ by A9,XBOOLE_1:28
                      .= dom(g|].a,b.[) by A6,FUNCT_1:68;

A36: g|].a,b.[=(y*f)|].a,b.[
proof
  now let xx be set
   such that
   A37: xx in dom(g|].a,b.[);
    dom(g|].a,b.[)=dom(g) /\ ].a,b.[ by FUNCT_1:68;
   then A38: dom(g|].a,b.[) c= ].a,b.[ by XBOOLE_1:17;
    then dom(g|].a,b.[) c= REAL by XBOOLE_1:1;
   then reconsider x=xx as Real by A37;
        A39:  (b-x) |^ (0) /((0)!) =1 by NEWTON:9,18;
    A40: x in ].a,b.[ by A37,A38;
     thus
       (g|].a,b.[).xx =g.x by A37,FUNCT_1:70
      .=f.b-Partial_Sums(Taylor(f,Z,x,b)).(0) by A7,A9,A40
      .=f.b-Taylor(f,Z,x,b).0 by SERIES_1:def 1
      .=f.b  - (diff(f,Z).(0)).x * (b-x) |^ (0) /((0)!) by Def6
      .=f.b  - (f|Z).x * (b-x) |^ (0) /((0)!) by Def4
      .=f.b  - f.x * (b-x) |^ (0) /((0)!) by A9,A40,FUNCT_1:72
      .=f.b  - f.x * ((b-x) |^ (0) /((0)!)) by XCMPLX_1:75
      .=y.(f.x) by A12,A39
      .=(y*f).x by A26,A40,FUNCT_1:23
      .=((y*f)|].a,b.[).xx by A35,A37,FUNCT_1:70;
  end;
hence thesis by A35,FUNCT_1:9;
end;

 for x be Real st x in ].a,b.[ holds y *f is_differentiable_in x by A28;
then A41: y*f is_differentiable_on ].a,b.[ by A26,A34,FDIFF_1:16;
  then g|].a,b.[  is_differentiable_on ].a,b.[
     & (y*f)`|].a,b.[=(g|].a,b.[)`|].a,b.[ by A36,FDIFF_2:16;
then for x be Real st x in ].a,b.[ holds g|].a,b.[ is_differentiable_in x
by FDIFF_1:16;
hence A42:g is_differentiable_on ].a,b.[ by A6,A9,FDIFF_1:def 7;
 now let x be Real such that
  A43:  x in ].a,b.[;
  thus g is_differentiable_in x by A42,A43,FDIFF_1:16;
  thus diff(g,x)
            =(g`|].a,b.[).x by A42,A43,FDIFF_1:def 8
          .=((g|].a,b.[)`|].a,b.[).x by A42,FDIFF_2:16
          .= ((y*f)`|].a,b.[).x by A36,A41,FDIFF_2:16
          .=diff( y*f,x) by A41,A43,FDIFF_1:def 8
          .=-diff(f,].a,b.[).(0+1).x * (b-x) |^ (0) / ( (0)!)
        by A28,A43;
end;
hence thesis;
end;
A44:for k be Nat st P[k] holds P[k+1]
proof
let k be Nat such that
A45:   P[k];
assume
A46: f is_differentiable_on k+1, Z;
let a,b be Real such that
 A47: a < b and
 A48: [.a,b.] c=Z and
 A49:  (diff(f,Z).(k+1)) is_continuous_on [.a,b.] and
 A50: f is_differentiable_on ((k+1)+1), ].a,b.[;
   ].a,b.[ c=[.a,b.] by RCOMP_1:15;
 then A51: ].a,b.[ c= Z by A48,XBOOLE_1:1;
A52: k <=k+1 by NAT_1:29;
 k <=(k+1)-1;
then diff(f,Z).k is_differentiable_on Z by A46,Def5;
then diff(f,Z).k  is_continuous_on Z by FDIFF_1:33;
then A53:  diff(f,Z).k is_continuous_on [.a,b.] by A48,FCONT_1:17;
 (k+1) <= (k+1)+1 by NAT_1:29;
then A54: f is_differentiable_on (k+1), ].a,b.[ by A50,Th23;
 consider gk  be PartFunc of REAL,REAL such that
A55: dom(gk)=Z  and
A56: for x be Real st x in Z holds
            gk.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).k by Lm7;
A57: gk.b=0 & gk is_continuous_on [.a,b.] &
           gk is_differentiable_on  ].a,b.[ &
           for x be Real st x in ].a,b.[ holds
            diff(gk,x)=-(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
 by A45,A46,A47,A48,A52,A53,A54,A55,A56,Th23;

 now let gk1 be PartFunc of REAL,REAL such that
   A58: dom(gk1)=Z and
   A59: for x be Real st x in Z  holds
    gk1.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).(k+1);

consider h be PartFunc of REAL,REAL such that
A60:  dom(h)=REAL and
A61: for x be Real holds
      h.x=1*(b-x) |^ (k+1)  /((k+1)!) and
A62:for x be Real holds
            h is_differentiable_in x
              & diff(h,x)=-1* (b-x) |^ (k) /((k)!)
         by Lm6;
thus gk1.b=0
proof
  A63: b in [.a,b.] by A47,RCOMP_1:15;
  hence gk1.b=f.b-Partial_Sums(Taylor(f,Z,b,b)).(k+1) by A48,A59
      .=f.b-(Partial_Sums(Taylor(f,Z,b,b)).k+Taylor(f,Z,b,b).(k+1))
            by SERIES_1:def 1
     .=(f.b-Partial_Sums(Taylor(f,Z,b,b)).k)-Taylor(f,Z,b,b).(k+1)
     .=gk.b -Taylor(f,Z,b,b).(k+1) by A48,A56,A63
     .=0 -Taylor(f,Z,b,b).(k+1)
        by A45,A46,A47,A48,A52,A53,A54,A55,A56,Th23
     .=0  - (diff(f,Z).(k+1)).b * (b-b) |^ (k+1) /((k+1)!) by Def6
     .=0  - (diff(f,Z).(k+1)).b * (0|^k*0) /((k+1)!) by NEWTON:11
     .=0;
  end;
A64: diff(f,Z).(k+1)=( diff(f,Z).k)`|Z by Def4;
 k <=(k+1)-1;
then A65:diff(f,Z).k is_differentiable_on Z by A46,Def5;
A66: dom(diff(f,Z).(k+1) (#)h)
            = dom (diff(f,Z).(k+1))  /\ dom(h) by SEQ_1:def 5
                            .=  Z /\  REAL by A60,A64,A65,FDIFF_1:def 8
                            .=  Z by XBOOLE_1:28;
A67:dom(gk-diff(f,Z).(k+1) (#)h)
    =dom(gk) /\ dom(diff(f,Z).(k+1) (#)h) by SEQ_1:def 4
    .=Z by A55,A66;
thus gk1 is_continuous_on [.a,b.]
proof
set  ghk=gk-((diff(f,Z).(k+1)) (#) h);
  now let x be Real such that
  A68:       x in  Z;
  thus
   gk1.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).(k+1) by A59,A68
      .=f.b-(Partial_Sums(Taylor(f,Z,x,b)).k+Taylor(f,Z,x,b).(k+1))
            by SERIES_1:def 1
     .=f.b-Partial_Sums(Taylor(f,Z,x,b)).k-Taylor(f,Z,x,b).(k+1)
     .=gk.x -Taylor(f,Z,x,b).(k+1) by A56,A68
     .=gk.x  - (diff(f,Z).(k+1)).x * (b-x) |^ (k+1) /((k+1)!) by Def6
     .=gk.x  - (diff(f,Z).(k+1)).x * (1*(b-x) |^ (k+1) /((k+1)!))
     by XCMPLX_1:75
     .=gk.x  - (diff(f,Z).(k+1)).x * h.x by A61
     .=gk.x  - ((diff(f,Z).(k+1)) (#) h).x by A66,A68,SEQ_1:def 5
     .=(gk-((diff(f,Z).(k+1)) (#) h)).x by A67,A68,SEQ_1:def 4;
end;
then A69: gk1= ghk by A58,A67,PARTFUN1:34;
A70: REAL  is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
 for x be Real st x in REAL
   holds h is_differentiable_in x by A62;
   then h is_differentiable_on REAL by A60,A70,FDIFF_1:16;
then h is_continuous_on REAL by FDIFF_1:33;
then h is_continuous_on  [.a,b.] by FCONT_1:17;
 then (diff(f,Z).(k+1)) (#) h  is_continuous_on [.a,b.] /\  [.a,b.]
   by A49,FCONT_1:20;
 hence thesis by A57,A69,FCONT_1:20;
end;
A71: diff(f,].a,b.[).(k+1)=( diff(f,].a,b.[).k)`|].a,b.[ by Def4;
 k <=(k+1)+1-1 by NAT_1:29;
then A72:diff(f,].a,b.[).k is_differentiable_on ].a,b.[ by A50,Def5;
A73: dom(diff(f,].a,b.[).(k+1) (#)h)
            = dom (diff(f,].a,b.[).(k+1))  /\ dom(h) by SEQ_1:def 5
                            .=  ].a,b.[ /\  REAL by A60,A71,A72,FDIFF_1:def 8
                            .=  ].a,b.[ by XBOOLE_1:28;

then A74:dom(gk-diff(f, ].a,b.[).(k+1) (#)h)=Z /\  ].a,b.[ by A55,SEQ_1:def 4
    .= ].a,b.[ by A51,XBOOLE_1:28;
 k+1 <=(k+1)+1-1;
then A75: diff(f,].a,b.[).(k+1)  is_differentiable_on ].a,b.[ by A50,Def5;
set gfh=gk-diff(f,].a,b.[).(k+1) (#)h;
A76: dom(gfh)=dom(gk1)/\  ].a,b.[ by A51,A58,A74,XBOOLE_1:28;
A77:for x be Real st x in ].a,b.[
   holds
 (gk-diff(f,Z).(k+1) (#)h).x =(gk-diff(f,].a,b.[).(k+1) (#)h).x
 proof
 let x be Real
    such that
    A78: x in ].a,b.[;
     thus
       (gk-diff(f,].a,b.[).(k+1) (#)h).x
      =gk.x - (diff(f,].a,b.[).(k+1) (#)h).x by A74,A78,SEQ_1:def 4
     .=gk.x - (diff(f,].a,b.[).(k+1)).x * h.x by A73,A78,SEQ_1:def 5
     .=gk.x - ((diff(f,Z).(k+1))|].a,b.[).x * h.x by A46,A47,A51,Th24
     .=gk.x - (diff(f,Z).(k+1)).x * h.x by A78,FUNCT_1:72
     .=gk.x - ((diff(f,Z).(k+1)) (#)h).x by A51,A66,A78,SEQ_1:def 5
     .=(gk - ((diff(f,Z).(k+1)) (#)h)).x by A51,A67,A78,SEQ_1:def 4;
 end;
 now let xx be set
   such that
   A79: xx in dom(gfh);
   reconsider x=xx as Real by A74,A79;
    thus
       gk1.xx
       =f.b-Partial_Sums(Taylor(f,Z,x,b)).(k+1) by A51,A59,A74,A79
      .=f.b-(Partial_Sums(Taylor(f,Z,x,b)).k + Taylor(f,Z,x,b).(k+1))
            by SERIES_1:def 1
     .=f.b-Partial_Sums(Taylor(f,Z,x,b)).k -Taylor(f,Z,x,b).(k+1)
      .=gk.x - Taylor(f,Z,x,b).(k+1) by A51,A56,A74,A79
      .=gk.x - (diff(f,Z).(k+1)).x * (b-x) |^ (k+1) /((k+1)!) by Def6
      .=gk.x - (diff(f,Z).(k+1)).x * (1*(b-x) |^ (k+1) /((k+1)!))
    by XCMPLX_1:75
      .=gk.x - (diff(f,Z).(k+1)).x * h.x by A61
      .=gk.x - (diff(f,Z).(k+1) (#)h).x by A51,A66,A74,A79,SEQ_1:def 5
      .=(gk- (diff(f,Z).(k+1) (#)h)).x by A51,A67,A74,A79,SEQ_1:def 4
      .=gfh.xx by A74,A77,A79;
end;
then A80:  (gk1|].a,b.[)|].a,b.[= gfh|].a,b.[ by A76,FUNCT_1:68;
then A81: gfh|].a,b.[=gk1|].a,b.[ by FUNCT_1:82;

 for x be Real st x in ].a,b.[ holds h is_differentiable_in x by A62;
then A82: h is_differentiable_on ].a,b.[ by A60,FDIFF_1:16;
then A83: diff(f,].a,b.[).(k+1) (#)h is_differentiable_on ].a,b.[ &
     for x be Real st x in ].a,b.[ holds
      ((diff(f,].a,b.[).(k+1) (#)h)`|].a,b.[).x
        = (h.x)*diff(diff(f,].a,b.[).(k+1),x)
        + (diff(f,].a,b.[).(k+1).x)*diff(h,x)
    by A73,A75,FDIFF_1:29;
then A84: gfh  is_differentiable_on ].a,b.[
      &
     for x be Real
          st x in ].a,b.[  holds (gfh`|].a,b.[).x
       = diff(gk,x) - diff(diff(f,].a,b.[).(k+1) (#)h,x)
   by A57,A74,FDIFF_1:27;
 then for x be Real st x in ].a,b.[ holds
         gk1|].a,b.[ is_differentiable_in x by A81,FDIFF_1:def 7;
hence
A85: gk1 is_differentiable_on ].a,b.[ by A51,A58,FDIFF_1:def 7;
 now let x be Real
   such  that
 A86:   x in ].a,b.[;
 thus diff(gk1,x)=(gk1`|].a,b.[).x by A85,A86,FDIFF_1:def 8
                    .=((gk1|].a,b.[)`|].a,b.[).x by A85,FDIFF_2:16
                    .=((gfh|].a,b.[)`|].a,b.[).x by A80,FUNCT_1:82
                    .=(gfh`|].a,b.[).x by A84,FDIFF_2:16
                    .=diff(gk,x) - diff(diff(f,].a,b.[).(k+1) (#)h,x)
                           by A57,A74,A83,A86,FDIFF_1:27
                    .=-(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
                        -diff(diff(f,].a,b.[).(k+1) (#)h,x)
                           by A45,A46,A47,A48,A52,A53,A54,A55,A56,A86,Th23
                    .=-(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
                        - ((diff(f,].a,b.[).(k+1) (#)h)`|].a,b.[).x
                        by A83,A86,FDIFF_1:def 8
                    .= -(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
                        - ((h.x)*diff(diff(f,].a,b.[).(k+1),x)
                        + (diff(f,].a,b.[).(k+1).x)*diff(h,x))
                        by A73,A75,A82,A86,FDIFF_1:29
                    .= -(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
                        - (h.x)*diff(diff(f,].a,b.[).(k+1),x)
                        -(diff(f,].a,b.[).(k+1).x)*diff(h,x)
                    .= -(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
                        - (1*(b-x) |^ (k+1)  /((k+1)!) )
                        *diff(diff(f,].a,b.[).(k+1),x)
                        -(diff(f,].a,b.[).(k+1).x)*diff(h,x) by A61
                    .= -(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
                        - (b-x) |^ (k+1)  /((k+1)!)
                        *diff(diff(f,].a,b.[).(k+1),x)
                        -(diff(f,].a,b.[).(k+1).x)
                        *(-1* (b-x) |^ (k) /((k)!)) by A62
                    .= -(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
                        +(diff(f,].a,b.[).(k+1).x)*(1* (b-x) |^ (k) /((k)!))
                        - (b-x) |^ (k+1)  /((k+1)!)
                        *diff(diff(f,].a,b.[).(k+1),x)
                     .= -(diff(f,].a,b.[).(k+1)).x * (b-x) |^ (k) /((k)!)
                        +(diff(f,].a,b.[).(k+1)).x*  (b-x) |^ (k) /((k)!)
                        - (b-x) |^ (k+1)  /((k+1)!)
                        *diff(diff(f,].a,b.[).(k+1),x)
                              by XCMPLX_1:75
                     .=  - (b-x) |^ (k+1)  /((k+1)!)
                        *diff(diff(f,].a,b.[).(k+1),x)
                     .=  - (b-x) |^ (k+1)  /((k+1)!)
                     *(diff(f,].a,b.[).(k+1)`|].a,b.[).x
                        by A75,A86,FDIFF_1:def 8
                     .=    - (b-x) |^ (k+1)  /((k+1)!)
                                 * (diff(f,].a,b.[).((k+1)+1)).x
                           by Def4
                    .=    - (diff(f,].a,b.[).((k+1)+1)).x
                    * (b-x) |^ (k+1)  /((k+1)!)
                           by XCMPLX_1:75;
end;
hence for x be Real st x in ].a,b.[ holds
            diff(gk1,x) =-(diff(f,].a,b.[).((k+1)+1)).x
            * (b-x) |^ ((k+1)) /((k+1)!);
end;
hence thesis;
end;
 for k be Nat holds P[k] from NAT_1:sch 1(A1,A44);
hence thesis;
end;

Lm9:
for n be Nat
for f be PartFunc of REAL,REAL
for Z be Subset of REAL
     st  f is_differentiable_on n,  Z
for a,b be Real st
     ( a < b
  & [.a,b.] c= Z
  &  (diff(f,Z).n) is_continuous_on [.a,b.]
  &  f is_differentiable_on (n+1), ].a,b.[  )
 ex g be PartFunc of REAL,REAL
     st (
          dom(g)=Z
          &
          (for x be Real st x in Z holds
           g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n)
           &
           g.b=0
           &
           g is_continuous_on [.a,b.]
           &
           g is_differentiable_on  ].a,b.[
           &
           for x be Real st x in ].a,b.[ holds
             diff(g,x)=-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /(n!)
       )
proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL such that
A1:     f is_differentiable_on n,  Z;
let a,b be Real such that
A2: ( a < b
  & [.a,b.] c= Z
  &  (diff(f,Z).n) is_continuous_on [.a,b.]
  &  f is_differentiable_on (n+1), ].a,b.[  );

consider g be PartFunc of REAL,REAL such that
     A3: dom(g)=Z and
     A4: for x be Real st x in Z holds
            g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n by Lm7;
     take g;
  thus thesis by A1,A2,A3,A4,Lm8;
 end;

theorem Th25:
for n be Nat, f be PartFunc of REAL,REAL, Z be Subset of REAL st
 f is_differentiable_on n,Z
  for a,b be Real st
     ( a < b & [.a,b.] c=Z &
       (diff(f,Z).n) is_continuous_on [.a,b.] &
       f is_differentiable_on (n+1), ].a,b.[ )
   holds
   for l be Real, g be PartFunc of REAL,REAL st
    dom(g)=REAL &
    ( for x be Real holds
       g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n
              -l*(b-x) |^ (n+1)  /((n+1)!)  ) &
    f.b-Partial_Sums(Taylor(f,Z,a,b)).n
              -l*(b-a) |^ (n+1)  /((n+1)!) =0
    holds
      g is_differentiable_on ].a,b.[ &
      g.a=0 & g.b=0 &
      g is_continuous_on [.a,b.] &
      for x be Real st x in  ].a,b.[ holds
        diff(g,x) = -(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /((n)!)
                    +l* (b-x) |^ (n) /((n)!)
proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL
     such that
     A1:  f is_differentiable_on n,  Z;
let a,b be Real such that
    A2:  a < b and
    A3:  [.a,b.] c=Z and
    A4:  (diff(f,Z).n) is_continuous_on [.a,b.] and
    A5:  f is_differentiable_on (n+1), ].a,b.[;
 let l be Real;
 let g be PartFunc of REAL,REAL such that
     A6:dom(g)=REAL
     &
     for x be Real holds
            g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n
                   -l*(b-x) |^ (n+1)  /((n+1)!)
            and
     A7: f.b-Partial_Sums(Taylor(f,Z,a,b)).n
                        -l*(b-a) |^ (n+1)  /((n+1)!) =0;

consider h be PartFunc of REAL,REAL such that
       A8: dom(h)=Z
       &(for x be Real st x in Z holds
                h.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n )
       & h.b=0
       & h is_continuous_on [.a,b.]
       & h is_differentiable_on  ].a,b.[
       &  for x be Real st x in ].a,b.[ holds
             diff(h,x)=-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /(n!)
        by A1,A2,A3,A4,A5,Lm9;
 consider p be PartFunc of REAL,REAL such that
        A9:
           dom(p)=REAL
           &
            (for x be Real holds
            p.x=l*(b-x) |^ (n+1)  /((n+1)!))
           &
            for x be Real holds
            p is_differentiable_in x
              & diff(p,x)=-l* (b-x) |^ (n) /((n)!)
         by Lm6;

set hp=h-p;
A10: dom(hp)=dom(h) /\ dom(p) by SEQ_1:def 4
                .=Z by A8,A9,XBOOLE_1:28;
   ].a,b.[ c=[.a,b.] by RCOMP_1:15;
then A11: ].a,b.[ c= Z by A3,XBOOLE_1:1;

A12: dom(hp)=dom(g) /\  Z by A6,A10,XBOOLE_1:28;
A13: for x be set st x in dom(hp)  holds hp.x=g.x
  proof
  let x be set such that
   A14:   x in dom(hp);
   reconsider xx=x as Real by A10,A14;
   thus hp.x=h.xx-p.xx by A14,SEQ_1:def 4
               .=(f.b-Partial_Sums(Taylor(f,Z,xx,b)).n)
                  -p.xx by A8,A10,A14
               .=(f.b-Partial_Sums(Taylor(f,Z,xx,b)).n)
                  -(l*(b-xx) |^ (n+1)/((n+1)!)) by A9
               .=g.x by A6;
  end;
then A15: hp|[.a,b.] = (g|Z)|[.a,b.] by A12,FUNCT_1:68
                   .= g|[.a,b.]     by A3,FUNCT_1:82;
A16: hp|].a,b.[ = (g|Z)|].a,b.[ by A12,A13,FUNCT_1:68
                   .= g|].a,b.[     by A11,FUNCT_1:82;

A17:for x be Real st x in ].a,b.[ holds
      hp is_differentiable_in x
      &
      diff(hp,x)=-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /(n!)
                  +l* (b-x) |^ (n) /((n)!)
     proof
       let x be Real such that
          A18: x in ].a,b.[;
          A19: h is_differentiable_in x
                      by A8,A18,FDIFF_1:16;
          A20: p is_differentiable_in x by A9;
     hence hp is_differentiable_in x by A19,FDIFF_1:22;
     thus diff(hp,x)=diff(h,x)-diff(p,x) by A19,A20,FDIFF_1:22
                     .=-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /(n!)
                       -diff(p,x) by A8,A18
                     .=-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /(n!)
                       -(-l* (b-x) |^ (n) /((n)!)) by A9
                     .=-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /(n!)
                       +l* (b-x) |^ (n) /((n)!);
      end;

then for x be Real st x in ].a,b.[ holds
      hp is_differentiable_in x;
then A21: hp is_differentiable_on ].a,b.[ by A10,A11,FDIFF_1:16;
   then ].a,b.[ c=dom hp & for x be Real st x in ].a,b.[ holds
    hp|].a,b.[ is_differentiable_in x by FDIFF_1:def 7;
hence
A22: g is_differentiable_on ].a,b.[ by A6,A16,FDIFF_1:def 7;
thus g.a=0 by A6,A7;

 thus g.b=0
 proof
 A23: b in [.a,b.] by A2,RCOMP_1:15;
 hence g.b=hp.b by A3,A10,A13
                .=h.b-p.b by A3,A10,A23,SEQ_1:def 4
               .=0-(l*(b-b) |^ (n+1)/((n+1)!)) by A8,A9
               .=0-(l*(0|^ n*0)/((n+1)!)) by NEWTON:11
               .=0;
 end;

A24: REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;

 for x be Real st x in REAL
   holds p is_differentiable_in x by A9;
then p is_differentiable_on REAL
     by A9,A24,FDIFF_1:16;
 then p is_continuous_on REAL by FDIFF_1:33;
then p is_continuous_on  [.a,b.] by FCONT_1:17;
then hp is_continuous_on [.a,b.] by A8,FCONT_1:19;
then hp|[.a,b.]  is_continuous_on [.a,b.] by FCONT_1:16;
hence g is_continuous_on [.a,b.] by A15,FCONT_1:16;
 thus for x be Real st x in  ].a,b.[
            holds diff(g,x)
              =-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /((n)!)
              +l* (b-x) |^ (n) /((n)!)
 proof
   let x be Real such that
   A25:   x in ].a,b.[;
   thus diff(g,x) = (g`|].a,b.[).x
                  by A22,A25,FDIFF_1:def 8
               .=((g|].a,b.[)`|].a,b.[).x by A22,FDIFF_2:16
               .=(hp`|].a,b.[).x by A16,A21,FDIFF_2:16
               .=diff(hp,x) by A21,A25,FDIFF_1:def 8
               .=-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /(n!)
                  +l* (b-x) |^ (n) /((n)!) by A17,A25;
 end;
 thus thesis;
end;

theorem Th26:
for n be Nat
for f be PartFunc of REAL,REAL
for Z be Subset of REAL
for b,l be Real holds
   ex g be Function of REAL,REAL st
            ( for x be Real holds
            g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n
                   -l*(b-x) |^ (n+1)  /((n+1)!)  )
proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
let b,l be Real;
defpred X[set] means $1 in REAL;
deffunc U(Real) = f.b-Partial_Sums(Taylor(f,Z,$1,b)).n
-l*(b-$1) |^ (n+1) /((n+1)!);
consider g being Function of REAL, REAL such that
  A1:   for d be Element of REAL holds g.d = U(d) from FUNCT_2:sch 4;
  take g;
  thus thesis by A1;
  end;

theorem Th27:
for n be Nat, f be PartFunc of REAL,REAL, Z be Subset of REAL st
  f is_differentiable_on n,Z
    for a,b be Real st
     ( a < b & [.a,b.] c=Z &
       (diff(f,Z).n) is_continuous_on [.a,b.] &
       f is_differentiable_on (n+1), ].a,b.[ )
    holds
     ex c be Real st
       c in ].a, b.[ &
       f.b = Partial_Sums(Taylor(f,Z,a,b)).n
           + (diff(f,].a,b.[).(n+1)).c * (b-a) |^ (n+1)  /((n+1)!)
proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL such that
A1:     f is_differentiable_on n,  Z;
let a,b be Real such that
A2:a < b and
A3: [.a,b.] c=Z and
A4: (diff(f,Z).n) is_continuous_on [.a,b.] and
A5: f is_differentiable_on (n+1), ].a,b.[;
deffunc F(Real)
=f.b-Partial_Sums(Taylor(f,Z,$1,b)).n;
reconsider
  l=F(a)/(((b-a) |^ (n+1))/((n+1)!)) as Real;
consider g be Function of REAL,REAL such that
A6: for x be Real holds
       g.x=f.b-Partial_Sums(Taylor(f,Z,x,b)).n
          -l*(b-x) |^ (n+1) /((n+1)!) by Th26;
 b-a <> 0 by A2,XCMPLX_1:15;
then A7: (b-a) |^ (n+1) <> 0 by CARD_4:51;
     (n+1)! <> 0 by NEWTON:23;
then A8: (b-a) |^ (n+1)/((n+1)!) <> 0 by A7,XCMPLX_1:50;
A9: f.b-Partial_Sums(Taylor(f,Z,a,b)).n
       -l*(b-a) |^ (n+1)  /((n+1)!)
        =F(a)-(F(a)/(((b-a) |^ (n+1))/((n+1)!)))
                *((b-a) |^ (n+1)/((n+1)!))
              by XCMPLX_1:75
       .=F(a)-F(a) by A8,XCMPLX_1:88
       .=0;
     dom g = REAL by FUNCT_2:def 1;
then g is_differentiable_on ].a,b.[ & g.a=0 & g.b=0 &
        g is_continuous_on [.a,b.] &
        for x be Real st x in ].a,b.[
            holds diff(g,x)
              =-(diff(f,].a,b.[).(n+1)).x * (b-x) |^ (n) /((n)!)
              +l* (b-x) |^ (n) /((n)!)
            by A1,A2,A3,A4,A5,A6,A9,Th25;
then consider c be Real such that
A10: c in ].a,b.[ and
A11: diff(g,c)=0 by A2,ROLLE:1;
 dom g = REAL by FUNCT_2:def 1;
then -(diff(f,].a,b.[).(n+1)).c * (b-c) |^ (n) /((n)!)
              +l* (b-c) |^ (n) /((n)!) =0
            by A1,A2,A3,A4,A5,A6,A9,A10,A11,Th25;
then A12:-(-(diff(f,].a,b.[).(n+1)).c * (b-c) |^ (n) /((n)!)
              +l* (b-c) |^ (n) /((n)!)) =0;
A13: (n)! <> 0 by NEWTON:23;
 c in  {r where r is Real : a<r & r<b } by A10,RCOMP_1:def 2;
then consider r be Real such that
A14: c = r  & (a < r & r < b);
 b-c <> 0 by A14,XCMPLX_1:15;
then A15: (b-c) |^ (n) <> 0 by CARD_4:51;
 ((diff(f,].a,b.[).(n+1)).c * (b-c) |^ (n) /((n)!)
              -l* (b-c) |^ (n) /((n)!))*((n)!)
              =0*((n)!) by A12;
then (diff(f,].a,b.[).(n+1)).c * (b-c) |^ (n) /((n)!)*((n)!)
       -l* (b-c) |^ (n) /((n)!)*((n)!)  =0 by XCMPLX_1:40;
then (diff(f,].a,b.[).(n+1)).c * (b-c) |^ (n)
       -l* (b-c) |^ (n) /((n)!)*((n)!)  =0 by A13,XCMPLX_1:88;
then ((diff(f,].a,b.[).(n+1)).c * (b-c) |^ (n)
       -l* (b-c) |^ (n))/((b-c) |^ (n))
       =0/((b-c) |^ (n)) by A13,XCMPLX_1:88;
then (diff(f,].a,b.[).(n+1)).c * (b-c) |^ (n)/((b-c) |^ (n))
       -l* (b-c) |^ (n)/((b-c) |^ (n))   =0 by XCMPLX_1:121;
then (diff(f,].a,b.[).(n+1)).c
       -l* (b-c) |^ (n)/((b-c) |^ (n))   =0
by A15,XCMPLX_1:90;
then (diff(f,].a,b.[).(n+1)).c  -l   =0 by A15,XCMPLX_1:90;
then f.b-Partial_Sums(Taylor(f,Z,a,b)).n
       -((diff(f,].a,b.[).(n+1)).c)*(b-a) |^ (n+1)  /((n+1)!)
 =0 by A9,XCMPLX_1:15;
then f.b-Partial_Sums(Taylor(f,Z,a,b)).n
       =((diff(f,].a,b.[).(n+1)).c)*(b-a) |^ (n+1)  /((n+1)!)
by XCMPLX_1:15;
then f.b =((diff(f,].a,b.[).(n+1)).c)*(b-a) |^ (n+1)  /((n+1)!)
        +Partial_Sums(Taylor(f,Z,a,b)).n by XCMPLX_1:27;
hence thesis by A10;
end;

Lm10:
for f be PartFunc of REAL,REAL
for Z be Subset of REAL
for n be Nat st f is_differentiable_on n,  Z
for a,b be Real st ( a < b
  & [.a,b.] c= Z
  &  (diff(f,Z).n) is_continuous_on [.a,b.]
  &  f is_differentiable_on (n+1), ].a,b.[  )
  for g be PartFunc of REAL,REAL
     st ( dom(g)=Z &
            for x be Real st x in Z holds
            g.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).n ) holds
           g.a=0 &
           g is_continuous_on [.a,b.] &
           g is_differentiable_on ].a,b.[ &
           for x be Real st x in ].a,b.[ holds
             diff(g,x)=-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /(n!)
proof
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
defpred P[Nat] means
  f is_differentiable_on $1,  Z implies
  for a,b be Real st
     ( a < b
  & [.a,b.] c=Z
  &  (diff(f,Z).$1) is_continuous_on [.a,b.]
  &  f is_differentiable_on ($1+1), ].a,b.[ )
  for g be PartFunc of REAL,REAL
     st (
            dom(g)=Z
            &
            for x be Real st x in Z holds
            g.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).$1
        )
     holds g.a=0 &
           g is_continuous_on [.a,b.] &
           g is_differentiable_on ].a,b.[ &
           for x be Real st x in ].a,b.[  holds
            diff(g,x)=-(diff(f,].a,b.[).($1+1)).x
            * (a-x) |^ ($1) / ( ($1)!);

A1:P[0]
proof
assume f is_differentiable_on 0,  Z;
let a,b be Real
     such that
 A2: a < b
    and
 A3: [.a,b.] c= Z
    and
 A4:  (diff(f,Z).0) is_continuous_on [.a,b.]
    and
 A5: f is_differentiable_on (0+1), ].a,b.[;

 let g be PartFunc of REAL,REAL such that
 A6: dom(g)=Z and
 A7: for x be Real st x in Z holds
            g.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).0;
  A8: ].a,b.[ c=[.a,b.] by RCOMP_1:15;
  then A9: ].a,b.[ c= Z by A3,XBOOLE_1:1;
  A10: a in [.a,b.] by A2,RCOMP_1:15;
  hence g.a=f.a-Partial_Sums(Taylor(f,Z,a,a)).(0) by A3,A7
      .=f.a-Taylor(f,Z,a,a).0
            by SERIES_1:def 1
      .=f.a  - (diff(f,Z).(0)).a * (a-a) |^ (0) /((0)!) by Def6
      .=f.a  - (f|Z).a * (a-a) |^ (0) /((0)!) by Def4
      .=f.a  - f.a * (a-a) |^ (0) /((0)!) by A3,A10,FUNCT_1:72
      .=f.a  - f.a * 1 by NEWTON:9,18
      .=0;

  consider y be PartFunc of REAL,REAL such that
      A11:  dom(y)=REAL and
      A12: for x be Real holds y.x=(f.a-x) and
      A13: for x be Real holds y is_differentiable_in x &
           diff(y,x)=-1 by Lm5;
 f|Z is_continuous_on [.a,b.] by A4,Def4;
then (f|Z)| [.a,b.]   is_continuous_on [.a,b.] by FCONT_1:16;
then (f|[.a,b.])    is_continuous_on [.a,b.] by A3,FUNCT_1:82;
then A14:f is_continuous_on [.a,b.] by FCONT_1:16;
then A15: [.a,b.] c= dom(f) by FCONT_1:def 2;
A16: rng(f) c= REAL by RELSET_1:12;
then A17: dom(y*f) = dom(f) by A11,RELAT_1:46;
A18: dom((y*f)|[.a,b.])=dom(y*f) /\ [.a,b.] by FUNCT_1:68
                      .=[.a,b.] by A15,A17,XBOOLE_1:28
                      .=Z /\ [.a,b.] by A3,XBOOLE_1:28
                      .=dom(g|[.a,b.]) by A6,FUNCT_1:68;
A19: g|[.a,b.]=(y*f)|[.a,b.]
proof
  now let xx be set such that
   A20: xx in dom(g|[.a,b.]);
    dom(g|[.a,b.])=dom(g) /\ [.a,b.] by FUNCT_1:68;
   then A21: dom(g|[.a,b.]) c= [.a,b.] by XBOOLE_1:17;
    then dom(g|[.a,b.]) c= REAL by XBOOLE_1:1;
    then reconsider x=xx as Real by A20;
    A22: x in [.a,b.] by A20,A21;
    thus (g|[.a,b.]).xx
           =g.x by A20,FUNCT_1:70
          .=f.a-Partial_Sums(Taylor(f,Z,x,a)).(0) by A3,A7,A22
          .=f.a-Taylor(f,Z,x,a).0 by SERIES_1:def 1
          .=f.a - (diff(f,Z).(0)).x * (a-x) |^ (0) /((0)!) by Def6
          .=f.a - (f|Z).x * (a-x) |^ (0) /((0)!) by Def4
          .=f.a - f.x * (a-x) |^ (0) /((0)!) by A3,A22,FUNCT_1:72
          .=f.a  - f.x * 1 by NEWTON:9,18
          .=y.(f.x) by A12
          .=(y*f).x by A15,A22,FUNCT_1:23
          .=((y*f)|[.a,b.]).xx by A18,A20,FUNCT_1:70;
  end;
hence thesis by A18,FUNCT_1:9;
end;

 f.:[.a,b.] c= rng(f) by RELAT_1:144;
then A23: f.:[.a,b.] c= REAL by A16,XBOOLE_1:1;
A24: REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
 for x be Real st x in REAL
   holds y is_differentiable_in x by A13;
 then y is_differentiable_on REAL by A11,A24,FDIFF_1:16;
 then y is_continuous_on REAL by FDIFF_1:33;
    then y is_continuous_on (f.:[.a,b.]) by A23,FCONT_1:17;
then (y*f) is_continuous_on [.a,b.] by A14,FCONT_1:26;
then (y*f)|[.a,b.]  is_continuous_on [.a,b.] by FCONT_1:16;
hence g is_continuous_on [.a,b.] by A19,FCONT_1:16;

 0 <= (0+1)-1;
then diff(f,].a,b.[).0  is_differentiable_on ].a,b.[ by A5,Def5;
 then f|].a,b.[  is_differentiable_on ].a,b.[ by Def4;

then A25: ].a,b.[ c= dom(f)&
for x be Real st x in ].a,b.[ holds
 f|].a,b.[ is_differentiable_in x by A8,A15,FDIFF_1:16,XBOOLE_1:1;
then A26: f is_differentiable_on ].a,b.[ by FDIFF_1:def 7;
 A27: for x be Real st x in ].a,b.[ holds
                y *f is_differentiable_in x  &
              diff( y*f,x) =-diff(f,].a,b.[).(0+1).x * (a-x) |^ (0) / ( (0)!)
      proof
      let x be Real such that
      A28: x in ].a,b.[;
      A29: (a-x) |^ (0) /((0)!) = 1 by NEWTON:9,18;
      A30: y is_differentiable_in f.x by A13;
      A31: diff(f,].a,b.[).(0+1) = ( diff(f,].a,b.[).0)`|].a,b.[ by Def4
                                  .= (f|].a,b.[)`|].a,b.[ by Def4
                                  .= f`|].a,b.[ by A26,FDIFF_2:16;
      A32: f is_differentiable_in x by A26,A28,FDIFF_1:16;
      hence y*f is_differentiable_in x by A30,FDIFF_2:13;
      thus diff( y*f,x)
             = diff(y, f.x)*diff(f,x) by A30,A32,FDIFF_2:13
            .= diff(y,f.x)*(f`|].a,b.[).x by A26,A28,FDIFF_1:def 8
            .= (-1)*diff(f,].a,b.[).(0+1).x by A13,A31
            .=-diff(f,].a,b.[).(0+1).x *((a-x) |^ (0) /((0)!)) by A29
            .=-diff(f,].a,b.[).(0+1).x *(a-x) |^ (0) /((0)!) by XCMPLX_1:75;
      end;
  rng(f) c= dom(y) by A11,RELSET_1:12;
then A33: dom(y*f) = dom(f) by RELAT_1:46;
A34: dom((y*f)|].a,b.[)=dom(y*f) /\ ].a,b.[ by FUNCT_1:68
                      .=].a,b.[ by A25,A33,XBOOLE_1:28
                      .=Z /\ ].a,b.[ by A9,XBOOLE_1:28
                      .=dom(g|].a,b.[) by A6,FUNCT_1:68;

A35: g|].a,b.[=(y*f)|].a,b.[
proof
  now let xx be set such that
   A36: xx in dom(g|].a,b.[);
    dom(g|].a,b.[)=dom(g) /\ ].a,b.[ by FUNCT_1:68;
   then A37: dom(g|].a,b.[) c= ].a,b.[ by XBOOLE_1:17;
    then dom(g|].a,b.[) c= REAL by XBOOLE_1:1;
    then reconsider x=xx as Real by A36;
   A38:  (a-x) |^ (0) /((0)!)=1 by NEWTON:9,18;
  A39: x in ].a,b.[ by A36,A37;
  thus (g|].a,b.[).xx = g.x by A36,FUNCT_1:70
       .=f.a-Partial_Sums(Taylor(f,Z,x,a)).(0) by A7,A9,A39
       .=f.a- Taylor(f,Z,x,a).0 by SERIES_1:def 1
       .=f.a  - (diff(f,Z).(0)).x * (a-x) |^ (0) /((0)!) by Def6
       .=f.a  - (f|Z).x * (a-x) |^ (0) /((0)!) by Def4
       .=f.a  - f.x * (a-x) |^ (0) /((0)!) by A9,A39,FUNCT_1:72
       .=f.a  - f.x * ((a-x) |^ (0) /((0)!)) by XCMPLX_1:75
       .=y.(f.x) by A12,A38
       .=(y*f).x by A25,A39,FUNCT_1:23
       .=((y*f)|].a,b.[).xx by A34,A36,FUNCT_1:70;
  end;
  hence thesis by A34,FUNCT_1:9;
end;

 for x be Real st x in ].a,b.[ holds y *f is_differentiable_in x by A27;
then A40: y*f is_differentiable_on ].a,b.[ by A25,A33,FDIFF_1:16;
    then g|].a,b.[  is_differentiable_on ].a,b.[ &
     (y*f)`|].a,b.[=(g|].a,b.[)`|].a,b.[ by A35,FDIFF_2:16;
    then for x be Real st x in ].a,b.[ holds g|].a,b.[ is_differentiable_in x
    by FDIFF_1:16;
    hence
A41:  g  is_differentiable_on ].a,b.[ by A6,A9,FDIFF_1:def 7;

 now let x be Real such that
  A42: x in ].a,b.[;
  thus g is_differentiable_in x by A41,A42,FDIFF_1:16;
  thus diff(g,x)
            =(g`|].a,b.[).x by A41,A42,FDIFF_1:def 8
          .=((g|].a,b.[)`|].a,b.[).x by A41,FDIFF_2:16
          .= ((y*f)`|].a,b.[).x by A35,A40,FDIFF_2:16
          .=diff( y*f,x) by A40,A42,FDIFF_1:def 8
          .=-diff(f,].a,b.[).(0+1).x * (a-x) |^ (0) / ( (0)!) by A27,A42;
end;
hence thesis;
end;
A43:for k be Nat st P[k] holds P[k+1]
proof
let k be Nat such that
A44:P[k];
assume
A45:
    f is_differentiable_on k+1, Z;
let a,b be Real such that
 A46: a < b and
 A47: [.a,b.] c=Z and
 A48:  (diff(f,Z).(k+1)) is_continuous_on [.a,b.] and
 A49: f is_differentiable_on ((k+1)+1), ].a,b.[;
      ].a,b.[ c=[.a,b.] by RCOMP_1:15;
then A50: ].a,b.[ c= Z by A47,XBOOLE_1:1;
A51:  k <=k+1 by NAT_1:29;
      k <=(k+1)-1;
     then diff(f,Z).k  is_differentiable_on Z by A45,Def5;
     then diff(f,Z).k  is_continuous_on Z by FDIFF_1:33;
then A52:  diff(f,Z).k is_continuous_on [.a,b.] by A47,FCONT_1:17;
      (k+1) <= (k+1)+1 by NAT_1:29;
then A53: f is_differentiable_on (k+1), ].a,b.[ by A49,Th23;
 consider gk  be PartFunc of REAL,REAL such that
A54: dom(gk)=Z and
A55: for x be Real st x in Z holds
            gk.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).k by Lm7;
A56: gk.a=0 &
     gk is_continuous_on [.a,b.] &
     gk is_differentiable_on  ].a,b.[ &
     for x be Real st x in ].a,b.[ holds
       diff(gk,x)=-(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
   by A44,A45,A46,A47,A51,A52,A53,A54,A55,Th23;
 now let gk1 be PartFunc of REAL,REAL such that
   A57: dom(gk1)=Z and
   A58: for x be Real st x in Z  holds
    gk1.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).(k+1);
consider h be PartFunc of REAL,REAL such that
A59:  dom(h)=REAL and
A60: for x be Real holds
      h.x=1*(a-x) |^ (k+1)  /((k+1)!) and
A61:for x be Real holds h is_differentiable_in x &
      diff(h,x)=-1* (a-x) |^ (k) /((k)!) by Lm6;
    thus gk1.a=0
    proof
  A62: a in [.a,b.] by A46,RCOMP_1:15;
  hence gk1.a=f.a- Partial_Sums(Taylor(f,Z,a,a)).(k+1) by A47,A58
      .=f.a- (Partial_Sums(Taylor(f,Z,a,a)).k+Taylor(f,Z,a,a).(k+1))
            by SERIES_1:def 1
     .=f.a- Partial_Sums(Taylor(f,Z,a,a)).k-Taylor(f,Z,a,a).(k+1)
     .=gk.a -Taylor(f,Z,a,a).(k+1) by A47,A55,A62
     .=0 -Taylor(f,Z,a,a).(k+1)
        by A44,A45,A46,A47,A51,A52,A53,A54,A55,Th23
     .=0  - (diff(f,Z).(k+1)).a * (a-a) |^ (k+1) /((k+1)!) by Def6
     .=0  - (diff(f,Z).(k+1)).a * (0|^k*0) /((k+1)!) by NEWTON:11
     .=0;
  end;
A63: diff(f,Z).(k+1)=( diff(f,Z).k)`|Z by Def4;
 k <=(k+1)-1;
then A64:diff(f,Z).k is_differentiable_on Z by A45,Def5;
A65: dom(diff(f,Z).(k+1) (#)h)
            = dom (diff(f,Z).(k+1))  /\ dom(h) by SEQ_1:def 5
                            .=  Z /\  REAL by A59,A63,A64,FDIFF_1:def 8
                            .=  Z by XBOOLE_1:28;
A66:dom(gk-diff(f,Z).(k+1) (#)h)
    =dom(gk) /\ dom(diff(f,Z).(k+1) (#)h) by SEQ_1:def 4
    .=Z by A54,A65;
thus gk1 is_continuous_on [.a,b.]
proof
set  ghk=gk-((diff(f,Z).(k+1)) (#) h);
  now let x be Real such that
  A67:       x in  Z;
  thus
   gk1.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).(k+1) by A58,A67
      .=f.a- (Partial_Sums(Taylor(f,Z,x,a)).k+Taylor(f,Z,x,a).(k+1))
            by SERIES_1:def 1
     .=f.a-Partial_Sums(Taylor(f,Z,x,a)).k-Taylor(f,Z,x,a).(k+1)
     .=gk.x -Taylor(f,Z,x,a).(k+1) by A55,A67
     .=gk.x  - (diff(f,Z).(k+1)).x * (a-x) |^ (k+1) /((k+1)!) by Def6
     .=gk.x  - (diff(f,Z).(k+1)).x * (1*(a-x) |^ (k+1) /((k+1)!))
      by XCMPLX_1:75
     .=gk.x  - (diff(f,Z).(k+1)).x * h.x by A60
     .=gk.x  - ((diff(f,Z).(k+1)) (#) h).x by A65,A67,SEQ_1:def 5
     .=(gk-((diff(f,Z).(k+1)) (#) h)).x by A66,A67,SEQ_1:def 4;
end;
then A68: gk1= ghk by A57,A66,PARTFUN1:34;

A69: REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
 for x be Real st x in REAL
   holds h is_differentiable_in x by A61;
   then h is_differentiable_on REAL by A59,A69,FDIFF_1:16;
 then h is_continuous_on REAL by FDIFF_1:33;
then h is_continuous_on  [.a,b.] by FCONT_1:17;
 then (diff(f,Z).(k+1)) (#) h  is_continuous_on
   [.a,b.] /\  [.a,b.] by A48,FCONT_1:20;
 hence thesis by A56,A68,FCONT_1:20;
end;
A70: diff(f,].a,b.[).(k+1)=( diff(f,].a,b.[).k)`|].a,b.[ by Def4;
     k <=(k+1)+1-1 by NAT_1:29;
then A71:diff(f,].a,b.[).k is_differentiable_on ].a,b.[ by A49,Def5;
A72: dom(diff(f,].a,b.[).(k+1) (#)h)
            = dom (diff(f,].a,b.[).(k+1))  /\ dom(h) by SEQ_1:def 5
                            .=  ].a,b.[ /\  REAL by A59,A70,A71,FDIFF_1:def 8
                            .=  ].a,b.[ by XBOOLE_1:28;

A73:dom(gk-diff(f, ].a,b.[).(k+1) (#)h)=
      Z /\ dom(diff(f, ].a,b.[).(k+1) (#)h) by A54,SEQ_1:def 4
    .= ].a,b.[ by A50,A72,XBOOLE_1:28;
 k+1 <=(k+1)+1-1;
then A74: diff(f,].a,b.[).(k+1)  is_differentiable_on ].a,b.[ by A49,Def5;
set gfh=gk-diff(f,].a,b.[).(k+1) (#)h;
A75: dom(gfh)=dom(gk1)/\  ].a,b.[ by A50,A57,A73,XBOOLE_1:28;
A76:for x be Real st x in ].a,b.[  holds
 (gk-diff(f,Z).(k+1) (#)h).x =(gk-diff(f,].a,b.[).(k+1) (#)h).x
 proof
   let x be Real such that
    A77: x in ].a,b.[;
     thus
       (gk-diff(f,].a,b.[).(k+1) (#)h).x
      =gk.x - (diff(f,].a,b.[).(k+1) (#)h).x
             by A73,A77,SEQ_1:def 4
     .=gk.x - (diff(f,].a,b.[).(k+1)).x * h.x by A72,A77,SEQ_1:def 5
     .=gk.x - ((diff(f,Z).(k+1))|].a,b.[).x * h.x by A45,A46,A50,Th24
     .=gk.x - (diff(f,Z).(k+1)).x * h.x by A77,FUNCT_1:72
     .=gk.x - ((diff(f,Z).(k+1)) (#)h).x by A50,A65,A77,SEQ_1:def 5
     .=(gk - ((diff(f,Z).(k+1)) (#)h)).x by A50,A66,A77,SEQ_1:def 4;
 end;
  now let xx be set such that
   A78: xx in dom(gfh);
   reconsider x=xx as Real by A73,A78;
    thus gk1.xx
       =f.a-Partial_Sums(Taylor(f,Z,x,a)).(k+1) by A50,A58,A73,A78
      .=f.a- (Partial_Sums(Taylor(f,Z,x,a)).k + Taylor(f,Z,x,a).(k+1))
            by SERIES_1:def 1
      .=f.a-Partial_Sums(Taylor(f,Z,x,a)).k -Taylor(f,Z,x,a).(k+1)
      .=gk.x - Taylor(f,Z,x,a).(k+1) by A50,A55,A73,A78
      .=gk.x - (diff(f,Z).(k+1)).x * (a-x) |^ (k+1) /((k+1)!) by Def6
      .=gk.x - (diff(f,Z).(k+1)).x * (1*(a-x) |^ (k+1) /((k+1)!))
    by XCMPLX_1:75
      .=gk.x - (diff(f,Z).(k+1)).x * h.x by A60
      .=gk.x - (diff(f,Z).(k+1) (#)h).x by A50,A65,A73,A78,SEQ_1:def 5
      .=(gk- (diff(f,Z).(k+1) (#)h)).x by A50,A66,A73,A78,SEQ_1:def 4
      .=gfh.xx by A73,A76,A78;
end;
then A79: (gk1|].a,b.[)|].a,b.[= gfh|].a,b.[ by A75,FUNCT_1:68;
then A80: gfh|].a,b.[=gk1|].a,b.[ by FUNCT_1:82;
 for x be Real st x in ].a,b.[ holds h is_differentiable_in x by A61;
then A81: h is_differentiable_on ].a,b.[ by A59,FDIFF_1:16;
then A82: diff(f,].a,b.[).(k+1) (#)h is_differentiable_on ].a,b.[
      &
      for x be Real
         st x in ].a,b.[ holds ((diff(f,].a,b.[).(k+1) (#)h)`|].a,b.[).x
      = (h.x)*diff(diff(f,].a,b.[).(k+1),x)
      + (diff(f,].a,b.[).(k+1).x)*diff(h,x)
    by A72,A74,FDIFF_1:29;
then A83: gfh  is_differentiable_on ].a,b.[ &
     for x be Real
          st x in ].a,b.[  holds (gfh`|].a,b.[).x
       = diff(gk,x) - diff(diff(f,].a,b.[).(k+1) (#)h,x)
   by A56,A73,FDIFF_1:27;
     then for x be Real st x in ].a,b.[ holds
         gk1|].a,b.[ is_differentiable_in x by A80,FDIFF_1:def 7;
hence
A84: gk1 is_differentiable_on ].a,b.[ by A50,A57,FDIFF_1:def 7;
 now let x be Real such that
 A85:   x in ].a,b.[;
 thus diff(gk1,x)=(gk1`|].a,b.[).x by A84,A85,FDIFF_1:def 8
                          .=((gk1|].a,b.[)`|].a,b.[).x by A84,FDIFF_2:16
                          .=((gfh|].a,b.[)`|].a,b.[).x by A79,FUNCT_1:82
                          .=(gfh`|].a,b.[).x by A83,FDIFF_2:16
                     .=diff(gk,x) - diff(diff(f,].a,b.[).(k+1) (#)h,x)
                   by A56,A73,A82,A85,FDIFF_1:27
                     .=-(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
                             -diff(diff(f,].a,b.[).(k+1) (#)h,x)
                           by A44,A45,A46,A47,A51,A52,A53,A54,A55,A85,Th23
                     .=-(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
                             - ((diff(f,].a,b.[).(k+1) (#)h)`|].a,b.[).x
                        by A82,A85,FDIFF_1:def 8
                     .= -(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
                        - ((h.x)*diff(diff(f,].a,b.[).(k+1),x)
                        + (diff(f,].a,b.[).(k+1).x)*diff(h,x))
                        by A72,A74,A81,A85,FDIFF_1:29
                     .= -(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
                        - (h.x)*diff(diff(f,].a,b.[).(k+1),x)
                        -(diff(f,].a,b.[).(k+1).x)*diff(h,x)
                     .= -(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
                        - (1*(a-x) |^ (k+1)  /((k+1)!) )
                        *diff(diff(f,].a,b.[).(k+1),x)
                        -(diff(f,].a,b.[).(k+1).x)*diff(h,x) by A60
                     .= -(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
                        - (a-x) |^ (k+1)  /((k+1)!)
                        *diff(diff(f,].a,b.[).(k+1),x)
                        -(diff(f,].a,b.[).(k+1).x)
                        *(-1* (a-x) |^ (k) /((k)!)) by A61
                     .= -(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
                        +(diff(f,].a,b.[).(k+1).x)*(1* (a-x) |^ (k) /((k)!))
                        - (a-x) |^ (k+1)  /((k+1)!)
                        *diff(diff(f,].a,b.[).(k+1),x)
                     .= -(diff(f,].a,b.[).(k+1)).x * (a-x) |^ (k) /((k)!)
                        +(diff(f,].a,b.[).(k+1)).x*  (a-x) |^ (k) /((k)!)
                        - (a-x) |^ (k+1)  /((k+1)!)
                        *diff(diff(f,].a,b.[).(k+1),x)
                              by XCMPLX_1:75
                     .=  - (a-x) |^ (k+1)  /((k+1)!)
                        *diff(diff(f,].a,b.[).(k+1),x)
                     .=  - (a-x) |^ (k+1)  /((k+1)!)
                     *(diff(f,].a,b.[).(k+1)`|].a,b.[).x
                        by A74,A85,FDIFF_1:def 8
                     .=    - (a-x) |^ (k+1)  /((k+1)!)
                                 * (diff(f,].a,b.[).((k+1)+1)).x
                           by Def4
                    .=    - (diff(f,].a,b.[).((k+1)+1)).x
                    * (a-x) |^ (k+1)  /((k+1)!)
                           by XCMPLX_1:75;
end;
hence
 for x be Real st x in ].a,b.[ holds
            diff(gk1,x)
            =-(diff(f,].a,b.[).((k+1)+1)).x
            * (a-x) |^ ((k+1)) /((k+1)!);
end;
hence thesis;
end;
 for k be Nat holds P[k] from NAT_1:sch 1(A1,A43);
hence thesis;
end;

Lm11:
for n be Nat
for f be PartFunc of REAL,REAL
for Z be Subset of REAL
     st  f is_differentiable_on n,  Z
for a,b be Real
     st
     ( a < b
  & [.a,b.] c= Z
  &  (diff(f,Z).n) is_continuous_on [.a,b.]
  &  f is_differentiable_on (n+1), ].a,b.[  )
 ex g be PartFunc of REAL,REAL
     st (
          dom(g)=Z
          &
          (for x be Real st x in Z holds
           g.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).n)
           &
           g.a=0
           &
           g is_continuous_on [.a,b.]
           &
           g is_differentiable_on  ].a,b.[
           &
           for x be Real st x in ].a,b.[ holds
             diff(g,x)=-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /(n!)
       )
proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL such that
A1:     f is_differentiable_on n,  Z;
let a,b be Real such that
A2: ( a < b
  & [.a,b.] c= Z
  &  (diff(f,Z).n) is_continuous_on [.a,b.]
  &  f is_differentiable_on (n+1), ].a,b.[  );

consider g be PartFunc of REAL,REAL such that
     A3: dom(g)=Z and
     A4: for x be Real st x in Z holds
            g.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).n by Lm7;
     take g;
  thus thesis by A1,A2,A3,A4,Lm10;
 end;

theorem Th28:
for n be Nat, f be PartFunc of REAL,REAL, Z be Subset of REAL st
  f is_differentiable_on n,Z
   for a,b be Real st
     ( a < b & [.a,b.] c=Z & (diff(f,Z).n) is_continuous_on [.a,b.] &
       f is_differentiable_on (n+1), ].a,b.[ )
   holds
     for l be Real, g be PartFunc of REAL,REAL st
       dom(g)=REAL &
       ( for x be Real holds
           g.x = f.a-Partial_Sums(Taylor(f,Z,x,a)).n
                    -l*(a-x) |^ (n+1)  /((n+1)!) ) &
       f.a-Partial_Sums(Taylor(f,Z,b,a)).n-l*(a-b) |^ (n+1)/((n+1)!) =0
     holds
       g is_differentiable_on ].a,b.[ &
        g.b=0 & g.a=0 &
        g is_continuous_on [.a,b.] &
        for x be Real st x in  ].a,b.[ holds
         diff(g,x) = -(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /((n)!)
                       +l* (a-x) |^ (n) /((n)!)
proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL such that
     A1:  f is_differentiable_on n,  Z;
let a,b be Real such that
    A2:  a < b and
    A3:  [.a,b.] c=Z
          and
    A4:  (diff(f,Z).n) is_continuous_on [.a,b.]
         and
    A5:  f is_differentiable_on (n+1), ].a,b.[;
 let l be Real;
 let g be PartFunc of REAL,REAL such that
     A6:dom(g)=REAL &
     for x be Real holds
            g.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).n
                   -l*(a-x) |^ (n+1)  /((n+1)!) and
     A7: f.a-Partial_Sums(Taylor(f,Z,b,a)).n
                        -l*(a-b) |^ (n+1)  /((n+1)!) =0;

consider h  be PartFunc of REAL,REAL such that
       A8: dom(h)=Z
       &(for x be Real st x in Z holds
                h.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).n )
       & h.a=0
       & h is_continuous_on [.a,b.]
       & h is_differentiable_on  ].a,b.[
       &  for x be Real st x in ].a,b.[ holds
             diff(h,x)=-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /(n!)
        by A1,A2,A3,A4,A5,Lm11;

 consider p be PartFunc of REAL,REAL such that
        A9: dom(p)=REAL &
            (for x be Real holds
            p.x=l*(a-x) |^ (n+1)  /((n+1)!)) &
            for x be Real holds
            p is_differentiable_in x
              & diff(p,x)=-l* (a-x) |^ (n) /((n)!) by Lm6;
set hp=h-p;
A10: dom(hp)=dom(h) /\ dom(p) by SEQ_1:def 4
                .=Z by A8,A9,XBOOLE_1:28;
   ].a,b.[ c=[.a,b.] by RCOMP_1:15;
then A11: ].a,b.[ c= Z by A3,XBOOLE_1:1;

A12: dom(hp)=dom(g) /\ Z by A6,A10,XBOOLE_1:28;
A13: for x be set st x in dom(hp) holds hp.x=g.x
  proof
  let x be set such that
   A14:   x in dom(hp);
   reconsider xx=x as Real by A10,A14;
   thus hp.x=h.xx-p.xx by A14,SEQ_1:def 4
               .=(f.a-Partial_Sums(Taylor(f,Z,xx,a)).n)
                  -p.xx by A8,A10,A14
               .=(f.a-Partial_Sums(Taylor(f,Z,xx,a)).n)
                  -(l*(a-xx) |^ (n+1)/((n+1)!)) by A9
               .=g.x by A6;
  end;
then A15: hp|[.a,b.] = (g|Z)|[.a,b.] by A12,FUNCT_1:68
                   .= g|[.a,b.] by A3,FUNCT_1:82;
A16: hp|].a,b.[ = (g|Z)|].a,b.[ by A12,A13,FUNCT_1:68
                   .= g|].a,b.[ by A11,FUNCT_1:82;

A17:for x be Real st x in ].a,b.[ holds
      hp is_differentiable_in x
      &
      diff(hp,x)=-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /(n!)
                  +l* (a-x) |^ (n) /((n)!)
     proof
       let x be Real such that
          A18: x in ].a,b.[;
          A19: h is_differentiable_in x
                      by A8,A18,FDIFF_1:16;

          A20: p is_differentiable_in x by A9;
     hence hp is_differentiable_in x by A19,FDIFF_1:22;
     thus diff(hp,x)=diff(h,x)-diff(p,x) by A19,A20,FDIFF_1:22
                     .=-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /(n!)
                       -diff(p,x) by A8,A18
                     .=-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /(n!)
                       -(-l* (a-x) |^ (n) /((n)!)) by A9
                     .=-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /(n!)
                       +l* (a-x) |^ (n) /((n)!);
      end;

then for x be Real st x in ].a,b.[ holds
      hp is_differentiable_in x;
then A21: hp is_differentiable_on ].a,b.[ by A10,A11,FDIFF_1:16;
 then ].a,b.[ c=dom hp & for x be Real st x in ].a,b.[
   holds hp|].a,b.[ is_differentiable_in x by FDIFF_1:def 7;
hence
A22: g is_differentiable_on ].a,b.[ by A6,A16,FDIFF_1:def 7;
thus g.b=0 by A6,A7;

 thus g.a=0
 proof
 A23: a in [.a,b.] by A2,RCOMP_1:15;
 hence   g.a=hp.a by A3,A10,A13
                .=h.a-p.a by A3,A10,A23,SEQ_1:def 4
               .=0-(l*(a-a) |^ (n+1)/((n+1)!)) by A8,A9
               .=0-(l*(0|^ n*0)/((n+1)!)) by NEWTON:11
               .=0;
 end;
A24: REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
 for x be Real st x in REAL holds p is_differentiable_in x by A9;
   then p is_differentiable_on REAL by A9,A24,FDIFF_1:16;
 then p is_continuous_on REAL by FDIFF_1:33;
then p is_continuous_on  [.a,b.] by FCONT_1:17;
then hp is_continuous_on [.a,b.] by A8,FCONT_1:19;
then hp|[.a,b.] is_continuous_on [.a,b.] by FCONT_1:16;
hence g is_continuous_on [.a,b.] by A15,FCONT_1:16;
thus for x be Real st x in ].a,b.[ holds
       diff(g,x)
        = -(diff(f,].a,b.[).(n+1)).x
          * (a-x) |^ (n) /((n)!)+l* (a-x) |^ (n) /((n)!)
 proof
   let x be Real such that
   A25:   x in ].a,b.[;
   thus diff(g,x) = (g`|].a,b.[).x
                  by A22,A25,FDIFF_1:def 8
               .=((g|].a,b.[)`|].a,b.[).x by A22,FDIFF_2:16
               .=(hp`|].a,b.[).x by A16,A21,FDIFF_2:16
               .=diff(hp,x) by A21,A25,FDIFF_1:def 8
               .=-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /(n!)
                  +l* (a-x) |^ (n) /((n)!) by A17,A25;
 end;
 thus thesis;
end;

theorem Th29:
for n be Nat, f be PartFunc of REAL,REAL, Z be Subset of REAL st
 f is_differentiable_on n,Z
   for a,b be Real st
     ( a < b & [.a,b.] c=Z & (diff(f,Z).n) is_continuous_on [.a,b.] &
       f is_differentiable_on (n+1), ].a,b.[ )
   holds
     ex c be Real st
       c in ].a, b.[ &
       f.a = Partial_Sums(Taylor(f,Z,b,a)).n
            + (diff(f,].a,b.[).(n+1)).c * (a-b) |^ (n+1)  /((n+1)!)
proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL such that
A1:     f is_differentiable_on n,  Z;
let a,b be Real such that
A2:a < b and
A3: [.a,b.] c=Z and
A4: (diff(f,Z).n) is_continuous_on [.a,b.] and
A5: f is_differentiable_on (n+1), ].a,b.[;
deffunc F(Real) =f.a-Partial_Sums(Taylor(f,Z,$1,a)).n;
reconsider l=F(b)/(((a-b) |^ (n+1))/((n+1)!)) as Real;
consider g be Function of REAL,REAL such that
A6: for x be Real holds
       g.x=f.a-Partial_Sums(Taylor(f,Z,x,a)).n
          -l*(a-x) |^ (n+1)  /((n+1)!) by Th26;
 a-b <> 0 by A2,XCMPLX_1:15;
then A7: (a-b) |^ (n+1) <> 0 by CARD_4:51;
 (n+1)! <> 0 by NEWTON:23;
then A8: (a-b) |^ (n+1)/((n+1)!) <> 0 by A7,XCMPLX_1:50;
A9: f.a-Partial_Sums(Taylor(f,Z,b,a)).n
       -l*(a-b) |^ (n+1)  /((n+1)!)
        =F(b)-(F(b)/(((a-b) |^ (n+1))/((n+1)!)))
                *((a-b) |^ (n+1)/((n+1)!))
              by XCMPLX_1:75
       .=F(b)-F(b) by A8,XCMPLX_1:88
       .=0;
  dom g = REAL by FUNCT_2:def 1;
 then g is_differentiable_on ].a,b.[ &
        g.b=0 & g.a=0 &
        g is_continuous_on [.a,b.] &
        for x be Real st x in  ].a,b.[
            holds diff(g,x)
              =-(diff(f,].a,b.[).(n+1)).x * (a-x) |^ (n) /((n)!)
              +l* (a-x) |^ (n) /((n)!)
            by A1,A2,A3,A4,A5,A6,A9,Th28;
then consider c be Real such that
A10: c in ].a,b.[ and
A11: diff(g,c)=0 by A2,ROLLE:1;
 dom g = REAL by FUNCT_2:def 1;
then -(diff(f,].a,b.[).(n+1)).c * (a-c) |^ (n) /((n)!)
              +l* (a-c) |^ (n) /((n)!) =0
            by A1,A2,A3,A4,A5,A6,A9,A10,A11,Th28;
then A12:-(-(diff(f,].a,b.[).(n+1)).c * (a-c) |^ (n) /((n)!)
              +l* (a-c) |^ (n) /((n)!)) =0;
A13: (n)! <> 0 by NEWTON:23;
 c in  {r where r is Real : a<r & r<b } by A10,RCOMP_1:def 2;
then consider r be Real such that
A14: c = r  & (a < r & r < b);
 a-c <> 0 by A14,XCMPLX_1:15;
then A15: (a-c) |^ (n) <> 0 by CARD_4:51;

 ((diff(f,].a,b.[).(n+1)).c * (a-c) |^ (n) /((n)!)
              -l* (a-c) |^ (n) /((n)!))*((n)!)
              =0*((n)!) by A12;
then (diff(f,].a,b.[).(n+1)).c * (a-c) |^ (n) /((n)!)*((n)!)
       -l* (a-c) |^ (n) /((n)!)*((n)!)  =0 by XCMPLX_1:40;
then (diff(f,].a,b.[).(n+1)).c * (a-c) |^ (n)
       -l* (a-c) |^ (n) /((n)!)*((n)!)  =0 by A13,XCMPLX_1:88;
then ((diff(f,].a,b.[).(n+1)).c * (a-c) |^ (n)
       -l* (a-c) |^ (n))/((a-c) |^ (n))
       =0/((a-c) |^ (n)) by A13,XCMPLX_1:88;
then (diff(f,].a,b.[).(n+1)).c * (a-c) |^ (n)/((a-c) |^ (n))
       -l* (a-c) |^ (n)/((a-c) |^ (n))   =0 by XCMPLX_1:121;
then (diff(f,].a,b.[).(n+1)).c
       -l* (a-c) |^ (n)/((a-c) |^ (n))   =0 by A15,XCMPLX_1:90;
then (diff(f,].a,b.[).(n+1)).c  -l   =0 by A15,XCMPLX_1:90;
 then (diff(f,].a,b.[).(n+1)).c = l by XCMPLX_1:15;
then f.a-Partial_Sums(Taylor(f,Z,b,a)).n
       =((diff(f,].a,b.[).(n+1)).c)*(a-b) |^ (n+1)  /((n+1)!)
by A9,XCMPLX_1:15;
then f.a =((diff(f,].a,b.[).(n+1)).c)*(a-b) |^ (n+1)  /((n+1)!)
        +Partial_Sums(Taylor(f,Z,b,a)).n by XCMPLX_1:27;
hence thesis by A10;
end;

theorem Th30:
for f be PartFunc of REAL,REAL, Z be Subset of REAL, Z1 be open Subset of REAL
 st Z1 c= Z
  for n be Nat st f is_differentiable_on n,Z holds
    diff(f,Z).n | Z1 = diff(f,Z1).n
proof
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
let Z1 be open Subset of REAL such that
  A1:  Z1 c= Z;
defpred P[Nat] means f is_differentiable_on $1,Z implies
    diff(f,Z).$1  | Z1 = diff(f,Z1).$1;
 A2: P[0]
    proof
    assume f is_differentiable_on 0,  Z;
   thus (diff(f,Z).0) |Z1
     =(f|Z)|Z1 by Def4
     .=f|Z1 by A1,FUNCT_1:82
     .=diff(f,Z1).0 by Def4;
    end;
A3: for k be Nat st P[k] holds P[k+1]
   proof
   let k be Nat such that
     A4: P[k];
    assume
    A5:   f is_differentiable_on (k+1),  Z;
    A6: k <= k+1 by NAT_1:29;
     k <= (k+1)-1;
    then A7:(diff(f,Z).k) is_differentiable_on Z
      by A5,Def5;
    then A8: (diff(f,Z).k) is_differentiable_on  Z1
      by A1,FDIFF_1:34;
     A9: dom(((diff(f,Z).k)`|Z)| Z1)
     =dom((diff(f,Z).k)`|Z) /\ Z1 by FUNCT_1:68
     .=Z /\ Z1 by A7,FDIFF_1:def 8
     .=Z1 by A1,XBOOLE_1:28;
     A10: dom((diff(f,Z).k)`| Z1) = Z1
     by A8,FDIFF_1:def 8;
     A11: now let x be Real such that
       A12: x in dom(((diff(f,Z).k)`|Z)| Z1);
      thus (((diff(f,Z).k)`|Z)| Z1).x
       =((diff(f,Z).k)`|Z).x by A9,A12,FUNCT_1:72
      .=diff((diff(f,Z).k),x) by A1,A7,A9,A12,FDIFF_1:def 8
      .=((diff(f,Z).k)`|Z1).x
      by A8,A9,A12,FDIFF_1:def 8;
     end;
     thus diff(f,Z).(k+1)  | Z1
       =((diff(f,Z).k)`|Z)| Z1 by Def4
      .=(diff(f,Z).k)`| Z1 by A9,A10,A11,PARTFUN1:34
      .=(diff(f,Z1).k)`| Z1 by A4,A5,A6,A8,Th23,FDIFF_2:16
      .=diff(f,Z1).(k+1) by Def4;
   end;
 thus for k be Nat holds P[k] from NAT_1: sch 1(A2,A3);
 end;

theorem Th31:
for f be PartFunc of REAL,REAL, Z be Subset of REAL, Z1 be open Subset of REAL
 st Z1 c= Z
 for n be natural number st f is_differentiable_on n+1,Z holds
   f is_differentiable_on n+1,  Z1
proof
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
let Z1 be open Subset of REAL such that
  A1:  Z1 c= Z;
let n be natural number  such that
  A2: f is_differentiable_on n+1,  Z;
   now let k be Nat such that
  A3:   k <= (n+1)-1;
   n <= n+1 by NAT_1:29;
      then k <= n+1 by A3,AXIOMS:22;
    then A4:  f is_differentiable_on k,  Z
  by A2,Th23;
   (diff(f,Z).k) is_differentiable_on Z
      by A2,A3,Def5;
    then (diff(f,Z).k) is_differentiable_on Z1
      by A1,FDIFF_1:34;
    then (diff(f,Z).k)|Z1  is_differentiable_on Z1 by FDIFF_2:16;
     hence (diff(f,Z1).k)  is_differentiable_on Z1 by A1,A4,Th30;
 end;
  hence f is_differentiable_on n+1,Z1 by Def5;
end;

theorem Th32:
for f be PartFunc of REAL,REAL
for Z be Subset of REAL
for x be Real st x in Z
for n be Nat holds
     f.x = Partial_Sums(Taylor(f,Z,x,x)).n
proof
let f be PartFunc of REAL,REAL;
let Z be Subset of REAL;
let x be Real such that
  A1: x in Z;
let c be Real;
defpred P[Nat] means f.x=Partial_Sums(Taylor(f, Z,x,x)).$1;
A2:P[0]
  proof
 thus
    Partial_Sums(Taylor(f, Z,x,x)).0
   =Taylor(f, Z,x,x).0 by SERIES_1:def 1
   .=(diff(f,Z).0).x * (x-x) |^ 0 /(0!) by Def6
   .=(f|Z).x * (x-x) |^ 0 /(0!) by Def4
    .=(f|Z).x * 1/1 by NEWTON:9,18
    .=f.x by A1,FUNCT_1:72;
  end;

A3:for k be Nat st P[k] holds P[k+1]
  proof
  let k be Nat such that
    A4: P[k];
    thus Partial_Sums(Taylor(f, Z,x,x)).(k+1)
     =Partial_Sums(Taylor(f, Z,x,x)).k
    +Taylor(f, Z,x,x).(k+1)
    by SERIES_1:def 1
   .=f.x  +(diff(f,Z).(k+1)).x * (x-x) |^ (k+1) /((k+1)!)
       by A4,Def6
  .=f.x  + (diff(f,Z).(k+1)).x
                 * (0|^k*0)  /((k+1)!) by NEWTON:11
  .=f.x;
  end;
 for k be Nat holds P[k] from NAT_1: sch 1(A2,A3);
hence thesis;
end;

theorem
 for n be Nat, f be PartFunc of REAL,REAL, x0,r be Real st
  ( 0 < r & f is_differentiable_on n+1,  ].x0-r,x0+r.[ )
    for x be Real st x in ].x0-r, x0+r.[ holds
      ex s be Real st 0 < s &  s < 1 &
        f.x=Partial_Sums(Taylor(f, ].x0-r,x0+r.[,x0,x)).n
               + (diff(f,].x0-r,x0+r.[).(n+1)).(x0+s*(x-x0))
               * (x-x0) |^ (n+1)  /((n+1)!)
 proof
let n be Nat;
let f be PartFunc of REAL,REAL;
let x0,r be Real such that
 A1: 0 < r and
 A2: f is_differentiable_on n+1, ].x0-r,x0+r.[;
let x be Real such that
 A3: x in ].x0-r, x0+r.[;
   now per cases by REAL_1:def 5;
     case A4: x < x0;
   abs(x0-x0)=0 by ABSVALUE:7;
  then x0 in  ].x0-r, x0+r.[ by A1,RCOMP_1:8;
   then A5:   [.x,x0.] c=  ].x0-r,x0+r.[ by A3,RCOMP_1:17;
    ].x,x0.[ c=  [.x,x0.] by RCOMP_1:15;
   then A6:   ].x,x0.[ c=  ].x0-r,x0+r.[ by A5,XBOOLE_1:1;
    n <= n+1 by NAT_1:29;
   then A7:  f is_differentiable_on n,  ].x0-r,x0+r.[ by A2,Th23;
    n <=(n+1)-1;
   then diff(f,].x0-r,x0+r.[).n is_differentiable_on ].x0-r,x0+r.[
     by A2,Def5;
   then diff(f,].x0-r,x0+r.[).n is_continuous_on ].x0-r,x0+r.[
     by FDIFF_1:33;
   then A8:  diff(f,].x0-r,x0+r.[).n is_continuous_on [.x,x0.]
     by A5,FCONT_1:17;
      f is_differentiable_on n+1, ].x,x0.[ by A2,A6,Th31;
    then consider c be Real such that
    A9: c in ].x,x0.[ and
    A10: f.x=Partial_Sums(Taylor(f, ].x0-r,x0+r.[,x0,x)).n
         + (diff(f,].x,x0.[).(n+1)).c * (x-x0) |^ (n+1)  /((n+1)!)
        by A4,A5,A7,A8,Th29;
    A11: diff(f,].x,x0.[).(n+1).c
         = (diff(f,].x0-r,x0+r.[).(n+1) |].x,x0.[).c by A2,A6,Th30
                                     .= (diff(f,].x0-r,x0+r.[).(n+1)).c
                                   by A9,FUNCT_1:72;
    set      t= x0-x;
    A12:       t > 0 by A4,SQUARE_1:11;
    take s=(x0-c)/t;
     c in {p where p is Real : x0-t<p & p<x0} by A9,RCOMP_1:def 2;
   then A13: ex g be Real  st g=c & x0-t<g & g<x0;
   then 0<x0-c by SQUARE_1:11;
   then 0/t < (x0-c)/t by A12,REAL_1:73;
   hence 0<s;
    x0-t+t < c+t by A13,REAL_1:67;
   then x0-c < t by REAL_1:84;
   then (x0-c)/t<t/t by A12,REAL_1:73;
   hence s<1 by A12,XCMPLX_1:60;
    x0+s*(x-x0) =x0-((x0-c)/t)*t
   .=x0-(x0-c) by A12,XCMPLX_1:88
   .=c;
  hence f.x=Partial_Sums(Taylor(f, ].x0-r,x0+r.[,x0,x)).n
               + (diff(f,].x0-r,x0+r.[).(n+1)).(x0+s*(x-x0))
               * (x-x0) |^ (n+1)  /((n+1)!) by A10,A11;
   end;
   case A14: x = x0;
       set s=1/2;
       take s;
       thus 0 < s &  s < 1;
       set c = x0+s*(x-x0);
      thus Partial_Sums(Taylor(f, ].x0-r,x0+r.[,x0,x)).n
               + (diff(f,].x0-r,x0+r.[).(n+1)).(x0+s*(x-x0))
               * (x-x0) |^ (n+1)  /((n+1)!)
       =f.x + (diff(f,].x0-r,x0+r.[).(n+1)).c
               * (x-x) |^ (n+1)  /((n+1)!) by A3,A14,Th32
      .=f.x  + (diff(f,].x0-r,x0+r.[).(n+1)).c
                 * (0|^n*0)  /((n+1)!) by NEWTON:11
      .=f.x;
      end;
     case A15:  x > x0;
     abs(x0-x0)=0 by ABSVALUE:7;
    then x0 in  ].x0-r, x0+r.[ by A1,RCOMP_1:8;
    then A16:   [.x0,x.] c=  ].x0-r,x0+r.[ by A3,RCOMP_1:17;
            ].x0,x.[ c=  [.x0,x.] by RCOMP_1:15;
   then A17:   ].x0,x.[ c=  ].x0-r,x0+r.[ by A16,XBOOLE_1:1;
     n <= n+1 by NAT_1:29;
    then A18:  f is_differentiable_on n,  ].x0-r,x0+r.[ by A2,Th23;
     n <=(n+1)-1;
    then diff(f,].x0-r,x0+r.[).n is_differentiable_on  ].x0-r,x0+r.[
     by A2,Def5;
    then diff(f,].x0-r,x0+r.[).n is_continuous_on ].x0-r,x0+r.[
     by FDIFF_1:33;
    then A19:  diff(f,].x0-r,x0+r.[).n is_continuous_on  [.x0,x.]
     by A16,FCONT_1:17;
       f is_differentiable_on n+1,  ].x0,x.[
      by A2,A17,Th31;
     then consider c be Real such that
     A20: c in ].x0,x.[ and
     A21: f.x=Partial_Sums(Taylor(f, ].x0-r,x0+r.[,x0,x)).n
         + (diff(f,].x0,x.[).(n+1)).c * (x-x0) |^ (n+1)  /((n+1)!)
        by A15,A16,A18,A19,Th27;
    A22: diff(f,].x0,x.[).(n+1).c
          = (diff(f,].x0-r,x0+r.[).(n+1) |].x0,x.[).c by A2,A17,Th30
                                     .= (diff(f,].x0-r,x0+r.[).(n+1)).c
                                   by A20,FUNCT_1:72;
      set t=  x-x0;
    A23: t > 0 by A15,SQUARE_1:11;
    take s=(c-x0)/t;
     c in {p where p is Real : x0<p & p<x0+t}
      by A20,RCOMP_1:def 2;
   then A24: ex g be Real  st g=c & x0<g & g<x0+t;
   then 0<c-x0 by SQUARE_1:11;
   then 0/t < (c-x0)/t by A23,REAL_1:73;
   hence 0<s;
    c-x0<t by A24,REAL_1:84;
   then (c-x0)/t<t/t by A23,REAL_1:73;
   hence s<1 by A23,XCMPLX_1:60;
    x0+s*(x-x0)
    =x0+(c-x0) by A23,XCMPLX_1:88
   .=c;
  hence f.x=Partial_Sums(Taylor(f, ].x0-r,x0+r.[,x0,x)).n
               + (diff(f,].x0-r,x0+r.[).(n+1)).(x0+s*(x-x0))
               * (x-x0) |^ (n+1)  /((n+1)!) by A21,A22;
  end;
end;
hence thesis;
end;
