:: The Rank+Nullity Theorem
::  by Jesse Alama
::
:: Received July 31, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies RANKNULL, VECTSP_1, MATRLIN, VECTSP10, VECTSP_9, RLVECT_3,
      RLSUB_1, FUNCT_1, FINSET_1, SUBSET_1, BOOLE, CARD_1, RELAT_1, RLVECT_1,
      RLVECT_2, INCSP_1, RLSUB_2, FINSEQ_1, QC_LANG1, FUNCT_2, TARSKI, ARYTM_1,
      FUNCOP_1, LOPBAN_1, SEQ_1, FINSEQ_4, FUNCT_4, CAT_1, COMPLEX1, TDGROUP,
      ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, DOMAIN_1, RELAT_1, RELSET_1, FUNCT_1,
      NAT_1, NUMBERS, FUNCOP_1, PARTFUN1, FUNCT_2, FUNCT_4, XCMPLX_0, XXREAL_0,
      CARD_1, FINSET_1, FINSEQ_1, FINSEQOP, STRUCT_0, GROUP_1, RLVECT_1,
      RLVECT_2, VECTSP_1, FUNCT_7, VECTSP_4, VECTSP_5, VECTSP_6, VECTSP_7,
      MOD_2, VECTSP_9, LOPBAN_1;
 constructors PARTFUN1, NAT_1, FINSEQOP, HAHNBAN, VECTSP_6, VECTSP_7, MOD_2,
      VECTSP_9, REALSET1, RLVECT_2, WELLORD2, LOPBAN_1, VECTSP_5, FUNCT_7,
      FUNCT_4;
 registrations RELAT_1, FUNCT_1, FUNCT_2, STRUCT_0, CARD_1, FINSET_1, FRAENKEL,
      VECTSP_9, SUBSET_1, XBOOLE_0, VECTSP_7, MATRLIN, FUNCOP_1, ORDINAL1,
      XREAL_0;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM;
 definitions TARSKI, RELAT_1, FUNCT_1, FINSEQ_1, VECTSP_4, VECTSP_6, XBOOLE_0,
      RLVECT_1, STRUCT_0, MOD_2, MATRLIN, FUNCOP_1, LOPBAN_1, FUNCT_2;
 theorems TARSKI, ZFMISC_1, RELAT_1, FINSET_1, FINSEQ_1, FUNCT_1, VECTSP_7,
      VECTSP_9, CARD_2, XBOOLE_1, FUNCT_2, SUBSET_1, XBOOLE_0, VECTSP_1,
      RLVECT_1, VECTSP_4, VECTSP_6, STRUCT_0, RLVECT_2, MOD_2, MATRLIN, CARD_1,
      FUNCOP_1, VECTSP_5, FUNCT_7, FINSEQ_2, FUNCT_4, ENUMSET1, ORDINAL1,
      PARTFUN1;
 schemes FUNCT_1;

begin

theorem Th1:
for f,g being Function
  st g is one-to-one & f|(rng g) is one-to-one & rng g c= dom f
  holds f*g is one-to-one
proof
  let f,g be Function such that
    A1: g is one-to-one and
    A2: f|(rng g) is one-to-one and
    A3: rng g c= dom f;
  set h = f*g;
  A4: dom h = dom g
  proof
    thus dom h c= dom g
    proof
      let x be set such that A5: x in dom h;
      thus thesis by A5,FUNCT_1:21;
    end;
    thus dom g c= dom h
    proof
      let x be set such that A6: x in dom g;
      g.x in rng g by A6,FUNCT_1:12;
      hence thesis by A3,A6,FUNCT_1:21;
    end;
  end;
  for x1,x2 being set st x1 in dom h & x2 in dom h & h.x1 = h.x2
        holds x1 = x2
  proof
    let x1,x2 be set such that
      A7: x1 in dom h and
      A8: x2 in dom h and
      A9: h.x1 = h.x2;
    A10: h.x1 = f.(g.x1) by A7,FUNCT_1:22;
    A11: h.x2 = f.(g.x2) by A8,FUNCT_1:22;
    A12: g.x1 in rng g by A4,A7,FUNCT_1:12;
    A13: g.x2 in rng g by A4,A8,FUNCT_1:12;
    A14: f.(g.x1) = (f|(rng g)).(g.x1) by A12,FUNCT_1:72;
    A15: f.(g.x2) = (f|(rng g)).(g.x2) by A13,FUNCT_1:72;
    dom (f|(rng g)) = rng g by A3,RELAT_1:91; then
    A16: g.x1 in dom (f|(rng g)) by A4,A7,FUNCT_1:12;
    A17: g.x2 in dom (f|(rng g)) by A3,A13,RELAT_1:91;
    g.x1 = g.x2 by A2,A9,A10,A11,A14,A15,A16,A17,FUNCT_1:def 8;
    hence thesis by A1,A4,A7,A8,FUNCT_1:def 8;
  end;
  hence thesis by FUNCT_1:def 8;
end;

:: If a function is one-to-one on a set X, then it is one-to-one on
:: any subset of X.
theorem Th2:
for f being Function,
    X,Y being set
  st X c= Y & f|Y is one-to-one
  holds f|X is one-to-one
proof
  let f be Function,
      X,Y be set such that
  A1: X c= Y and
  A2: f|Y is one-to-one;
  f|X = (f|Y)|X by A1,RELAT_1:103;
  hence thesis by A2,FUNCT_1:84;
end;

theorem Th3:
for V being 1-sorted, X,Y being Subset of V
  holds X meets Y iff ex v being Element of V st v in X & v in Y
proof
  let V be 1-sorted, X,Y be Subset of V;
  X meets Y implies ex v being Element of V st v in X & v in Y
  proof
    assume A1: X meets Y;
    consider z being set such that
    A2: z in X and
    A3: z in Y by A1,XBOOLE_0:3;
    reconsider v = z as Element of V by A2;
    take v;
    thus thesis by A2,A3;
  end;
  hence thesis by XBOOLE_0:3;
end;

reserve F for Field,
        V,W for VectSp of F;

registration
  let F be Field,
      V be finite-dimensional VectSp of F;
  cluster finite Basis of V;
  existence
  proof
    consider A being finite Subset of V such that A1: A is Basis of V
      by MATRLIN:def 3;
    reconsider A as Basis of V by A1;
    take A;
    thus thesis;
  end;
end;

registration let F be Field,
                 V,W be VectSp of F;
  cluster linear Function of V,W;
  existence
  proof
    set f = FuncZero ([#]V,W);
    reconsider f as Function of V,W;
    A1: f is linear
    proof
      thus for x,y being Vector of V holds f.(x+y) = (f.x)+(f.y)
      proof
        let x,y be Vector of V;
        A2: f.(x+y) = 0.W by FUNCOP_1:13;
        A3: f.x = 0.W by FUNCOP_1:13;
        f.y = 0.W by FUNCOP_1:13;
        hence thesis by A2,A3,RLVECT_1:def 7;
      end;
      thus for a being Element of F,
               x being Element of V
             holds f.(a*x) = a*(f.x)
      proof
        let a be Element of F,
            x be Element of V;
        A4: f.(a*x) = 0.W by FUNCOP_1:13;
        f.x = 0.W by FUNCOP_1:13;
        hence thesis by A4,VECTSP_1:59;
      end;
    end;
    take f;
    thus thesis by A1;
  end;
end;

theorem Th4:
[#]V is finite implies V is finite-dimensional
proof
  assume A1: [#]V is finite;
  consider B being Basis of V;
  reconsider B as finite Subset of V by A1,FINSET_1:13;
  take B;
  thus thesis;
end;

theorem
for V being finite-dimensional VectSp of F
  st Card ([#]V) = 1
  holds dim V = 0
proof
  let V be finite-dimensional VectSp of F such that
  A1: Card ([#]V) = 1;
  A2: [#]V = {0.V}
  proof
    consider y being set such that
    A3: [#]V = {y} by A1,CARD_2:60;
    thus thesis by A3,TARSKI:def 1;
  end;
  (Omega).V = (0).V by A2,VECTSP_4:def 3;
  hence thesis by VECTSP_9:33;
end;

theorem
Card ([#]V) = 2 implies dim V = 1
proof
  assume A1: Card ([#]V) = 2;
  A2: [#]V,Card([#]V) are_equipotent by CARD_1:def 5; then
  A3: [#]V is finite by A1,CARD_1:68;
  reconsider C = [#]V as finite set by A1,A2,CARD_1:68;
  A4: Card ([#]V) = card (C);
  reconsider V as finite-dimensional VectSp of F by A3,Th4;
  ex v being Vector of V st v <> 0.V & (Omega).V = Lin ({v})
  proof
    consider x,y being set such that
    A5: x <> y and
    A6: [#]V = {x,y} by A1,A4,CARD_2:79;
    per cases by A6,TARSKI:def 2;
    suppose A7: x = 0.V;
      reconsider y as Element of V by A6,TARSKI:def 2;
      reconsider x as Element of V by A7;
      set L = Lin ({y});
      A8: for v being Element of V holds v in (Omega).V iff v in L
      proof
        let v be Element of V;
        v in (Omega).V implies v in L
        proof
          assume v in (Omega).V;
          A9: y in {y} by TARSKI:def 1;
          A10: 0.L in L by STRUCT_0:def 5;
          per cases by A6,TARSKI:def 2;
          suppose v = x;
            hence thesis by A7,A10,VECTSP_4:def 2;
          end;
          suppose v = y;
            hence thesis by A9,VECTSP_7:13;
          end;
        end;
        hence thesis by STRUCT_0:def 5;
      end;
      take y;
      thus thesis by A5,A7,A8,VECTSP_4:38;
    end;
    suppose A11: y = 0.V; then
      reconsider y as Element of V;
      reconsider x as Element of V by A6,TARSKI:def 2;
      set L = Lin ({x});
      A12: for v being Element of V holds v in (Omega).V iff v in L
      proof
        let v be Element of V;
        v in (Omega).V implies v in L
        proof
          assume v in (Omega).V;
          A13: x in {x} by TARSKI:def 1;
          A14: 0.L in L by STRUCT_0:def 5;
          per cases by A6,TARSKI:def 2;
          suppose v = y;
            hence thesis by A11,A14,VECTSP_4:def 2;
          end;
          suppose v = x;
            hence thesis by A13,VECTSP_7:13;
          end;
        end;
        hence thesis by STRUCT_0:def 5;
      end;
      take x;
      thus thesis by A5,A11,A12,VECTSP_4:38;
    end;
  end;
  hence thesis by VECTSP_9:34;
end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Basic facts of linear transformations
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

begin

definition
  let F be Field,
      V,W be VectSp of F;
  mode
    linear-transformation of V,W
  is
    linear Function of V,W;
end;

reserve T for linear-transformation of V,W;

theorem Th7:
for V, W being non empty 1-sorted, T being Function of V,W holds
dom T = [#]V & rng T c= [#]W
proof
  let V, W be non empty 1-sorted, T be Function of V,W;
  T is Element of Funcs([#]V,[#]W) by FUNCT_2:11;
  hence thesis by FUNCT_2:169;
end;

theorem Th8:
for x,y being Element of V
  holds T.x - T.y = T.(x - y)
proof
  let x,y be Element of V;
  A1: T.(x - y) = T.x + T.(-y) by MOD_2:def 5;
  A2: -y = (-1_F)*y by VECTSP_1:59;
  T.((-1_F)*y) = (-1_F)*(T.y) by MOD_2:def 5;
  hence thesis by A1,A2,VECTSP_1:59;
end;

theorem Th9:
T.(0.V) = 0.W
proof
  0.V = (0.F)*(0.V) by VECTSP_1:59;
  then T.(0.V) = (0.F)*T.(0.V) by MOD_2:def 5
  .= 0.W by VECTSP_1:59;
  hence thesis;
end;

definition
  let F be Field,
      V,W be VectSp of F,
      T be linear-transformation of V,W;
  func
    ker T -> strict Subspace of V
  means :Def1:
    [#]it = { u where u is Element of V : T.u = 0.W };
  existence
  proof
    set K = { u where u is Element of V : T.u = 0.W };
    K c= [#]V
    proof
      let x be set such that A1: x in K;
      consider u being Element of V such that
      A2: u = x and
      T.u = 0.W by A1;
      thus thesis by A2;
    end;
    then reconsider K as Subset of V;
    A3: for v being Element of V st v in K holds T.v = 0.W
    proof
      let v be Element of V such that A4: v in K;
      consider u being Element of V such that
        A5: u = v and
        A6: T.u = 0.W by A4;
      thus thesis by A5,A6;
    end;
    K <> {} & K is linearly-closed
    proof
      T.(0.V) = 0.W by Th9;
      then 0.V in K;
      hence K <> {};
      thus K is linearly-closed
      proof
        A7: now
          let u,v be Element of V such that
          A8: u in K and
          A9: v in K;
          A10: T.u = 0.W by A3,A8;
          T.v = 0.W by A3,A9; then
          T.(u+v) = 0.W + 0.W by A10,MOD_2:def 5
          .= 0.W by RLVECT_1:def 7;
          hence u+v in K;
        end;
        now
          let u be Element of V,
              a be Element of F such that
          A11: u in K;
          T.u = 0.W by A3,A11; then
          T.(a*u) = a*(0.W) by MOD_2:def 5
          .= 0.W by VECTSP_1:59;
          hence a*u in K;
        end;
        then for a being Element of F, u being Element of V st u in K
               holds a*u in K;
        hence thesis by A7,VECTSP_4:def 1;
      end;
    end;
    then consider W being strict Subspace of V such that
    A12: K = the carrier of W by VECTSP_4:42;
    take W;
    thus thesis by A12;
  end;
  uniqueness by VECTSP_4:37;
end;

theorem Th10:
for x being Element of V
  holds x in ker T iff T.x = 0.W
proof
  let x be Element of V;
  thus x in ker T implies T.x = 0.W
  proof
    assume x in ker T; then
    A1: x in [#]ker T by STRUCT_0:def 5;
    [#]ker T = { u where u is Element of V : T.u = 0.W } by Def1; then
    consider u being Element of V such that
    A2: u = x and
    A3: T.u = 0.W by A1;
    thus thesis by A2,A3;
  end;
  assume T.x = 0.W;
  then x in { u where u is Element of V : T.u = 0.W };
  then x in [#]ker T by Def1;
  hence thesis by STRUCT_0:def 5;
end;

definition
  let V,W be non empty 1-sorted,
      T be Function of V,W,
      X be Subset of V;
  redefine func T .: X -> Subset of W;
  coherence
  proof
    A1: rng T c= [#]W by Th7;
    T .: X c= rng T by RELAT_1:144;
    hence thesis by A1,XBOOLE_1:1;
  end;
end;

definition
  let F be Field,
      V,W be VectSp of F,
      T be linear-transformation of V,W;
  func
    im T -> strict Subspace of W
  means :Def2:
    [#]it = T .: [#]V;
  existence
  proof
    reconsider U = T .: [#]V as Subset of W;
    A1: for u being Element of W holds
          u in U iff ex v being Element of V st T.v = u
    proof
      let u be Element of W;
      thus u in U implies ex v being Element of V st T.v = u
      proof
        assume A2: u in U;
        consider x being set such that
          x in dom T and
          A3: x in [#]V and
          A4: u = T.x by A2,FUNCT_1:def 12;
        reconsider x as Element of V by A3;
        take x;
        thus thesis by A4;
      end;
      thus (ex v being Element of V st T.v = u) implies u in U
      proof
        given v being Element of V such that
        A5: T.v = u;
        A6: v in [#]V;
        v in dom T by A6,Th7;
        hence thesis by A5,FUNCT_1:def 12;
      end;
    end;
    U <> {} & U is linearly-closed
    proof
      thus U <> {}
      proof
        T.(0.V) = 0.W by Th9;
        hence thesis by A1;
      end;
      thus U is linearly-closed
      proof
A7:     now
          let u,v be Element of W such that
          A8: u in U and
          A9: v in U;
          consider x being Element of V such that
          A10: T.x = u by A1,A8;
          consider y being Element of V such that
          A11: T.y = v by A1,A9;
          u+v = T.(x+y) by A10,A11,MOD_2:def 5;
          hence u+v in U by A1;
        end;
        now
          let a be Element of F,
              w be Element of W such that
            A12: w in U;
          consider v being Element of V such that
            A13: T.v = w by A1,A12;
          T.(a*v) = a*w by A13,MOD_2:def 5;
          hence a*w in U by A1;
        end;
        hence thesis by A7,VECTSP_4:def 1;
      end;
    end;
    then consider A being strict Subspace of W such that
    A14: U = the carrier of A by VECTSP_4:42;
    take A;
    thus thesis by A14;
  end;
  uniqueness by VECTSP_4:37;
end;

theorem
0.V in ker T
proof
  T.(0.V) = 0.W
  proof
    0.V = (0.F)*(0.V) by VECTSP_1:59;
    then T.(0.V) = (0.F)*T.(0.V) by MOD_2:def 5
    .= 0.W by VECTSP_1:59;
    hence thesis;
  end;
  hence thesis by Th10;
end;

theorem Th12:
for X being Subset of V
  holds T .: X is Subset of im T
proof
  let X be Subset of V;
  set S = T .: X;
  [#](im T) = T .: [#]V by Def2;
  hence thesis by RELAT_1:156;
end;

theorem Th13:
for y being Element of W
  holds y in im T iff ex x being Element of V st y = T.x
proof
  let y be Element of W;
  A1: y in im T implies ex x being Element of V st y = T.x
  proof
    assume A2: y in im T;
    reconsider y as Element of im T by A2,STRUCT_0:def 5;
    A3: [#](im T) = T .: [#]V by Def2;
    consider x being set such that
      x in dom T and
      A4: x in [#]V and
      A5: y = T.x by A3,FUNCT_1:def 12;
    reconsider x as Element of V by A4;
    take x;
    thus thesis by A5;
  end;
  (ex x being Element of V st y = T.x) implies y in im T
  proof
    assume A6: ex x being Element of V st y = T.x;
    consider x being Element of V such that
    A7: y = T.x by A6;
    dom T = [#]V by Th7; then
    y in T .: [#]V by A7,FUNCT_1:def 12; then
    y in [#](im T) by Def2;
    hence thesis by STRUCT_0:def 5;
  end;
  hence thesis by A1;
end;

theorem
for x being Element of ker T
  holds T.x = 0.W
proof
  let x be Element of ker T;
  reconsider y = x as Element of V by VECTSP_4:18;
  y in ker T by STRUCT_0:def 5;
  hence thesis by Th10;
end;

theorem Th15:
T is one-to-one implies ker T = (0).V
proof
  assume A1: T is one-to-one;
  reconsider Z = (0).V as Subspace of ker T by VECTSP_4:50;
  for v being Element of ker T holds v in Z
  proof
    let v be Element of ker T;
    assume A2: not v in Z;
    A3: T.(0.V) = 0.W by Th9;
    A4: not v = 0.V by A2,VECTSP_4:46;
    A5: v in ker T by STRUCT_0:def 5;
    reconsider v as Element of V by VECTSP_4:18;
    A6: T.v = 0.W by A5,Th10;
    dom T = [#]V by Th7;
    hence thesis by A1,A3,A4,A6,FUNCT_1:def 8;
  end;
  hence thesis by VECTSP_4:40;
end;

theorem Th16:
for V being finite-dimensional VectSp of F
  holds dim ((0).V) = 0
proof
  let V be finite-dimensional VectSp of F;
  (Omega).((0).V) = (0).((0).V) by VECTSP_4:47;
  hence thesis by VECTSP_9:33;
end;

theorem Th17:
for x,y being Element of V
  st T.x = T.y
  holds x - y in ker T
proof
  let x,y be Element of V such that
    A1: T.x = T.y;
  T.(x - y) = T.x - T.y by Th8
  .= 0.W by A1,VECTSP_1:66;
  hence thesis by Th10;
end;

theorem Th18:
for A being Subset of V,
    x,y being Element of V
  st x - y in Lin A
  holds x in Lin (A \/ {y})
proof
  let A be Subset of V,
      x,y be Element of V such that
  A1: x - y in Lin A;
  A2: y in {y} by TARSKI:def 1;
  A3: y in Lin ({y}) by A2,VECTSP_7:13;
  A4: (x - y) + y = x - (y - y) by RLVECT_1:43
  .= x - 0.V by VECTSP_1:66
  .= x by RLVECT_1:26;
  Lin (A \/ {y}) = (Lin A) + (Lin {y}) by VECTSP_7:20;
  hence thesis by A1,A3,A4,VECTSP_5:5;
end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Some basic facts about linearly independent subsets and linear
:: combinations
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

begin

theorem Th19:
for X being Subset of V
  st V is Subspace of W
  holds X is Subset of W
proof
  let X be Subset of V;
  assume A1: V is Subspace of W;
  A2: [#]V c= [#]W by A1,VECTSP_4:def 2;
  X c= [#]W
  proof
    let x be set such that A3: x in X;
    x in [#]V by A3;
    hence thesis by A2;
  end;
  hence thesis;
end;

:: A linearly independent set is a basis of its linear span.
theorem Th20:
for A being Subset of V
  st A is linearly-independent
  holds A is Basis of Lin A
proof
  let A be Subset of V such that
  A1: A is linearly-independent;
  A c= [#](Lin A)
  proof
    let x be set such that A2: x in A;
    reconsider x as Element of V by A2;
    x in Lin A by A2,VECTSP_7:13;
    hence thesis by STRUCT_0:def 5;
  end;
  then reconsider B = A as Subset of Lin A;
  A3: B is linearly-independent by A1,VECTSP_9:16;
  Lin B = Lin A by VECTSP_9:21;
  hence thesis by A3,VECTSP_7:def 3;
end;

:: Adjoining an element x to A that is already in its linear span
:: results in a linearly dependent set.
theorem Th21:
for A being Subset of V,
    x being Element of V
  st x in Lin A & not x in A
  holds A \/ {x} is linearly-dependent
proof
  let A be Subset of V,
      x be Element of V such that
    A1: x in Lin A and
    A2: not x in A;
  per cases;
  suppose A is linearly-independent; then
    reconsider A' = A as Basis of Lin A by Th20;
    x in [#](Lin A) by A1,STRUCT_0:def 5;
    then reconsider X = {x} as Subset of Lin A by SUBSET_1:63;
    A3: X misses A'
    proof
      assume X meets A';
      then consider y being set such that
      A4: y in X and
      A5: y in A' by XBOOLE_0:3;
      thus contradiction by A2,A4,A5,TARSKI:def 1;
    end;
    reconsider B = A' \/ X as Subset of Lin A;
    A6: B is linearly-dependent by A3,VECTSP_9:19;
    reconsider C = B as Subset of V by Th19;
    thus thesis by A6,VECTSP_9:16;
  end;
  suppose A7: A is linearly-dependent;
    A8: A c= A \/ {x} by XBOOLE_1:7;
    reconsider B = A \/ {x} as Subset of V;
    thus thesis by A7,A8,VECTSP_7:2;
  end;
end;

theorem Th22:
for A being Subset of V,
    B being Basis of V
  st A is Basis of ker T & A c= B
  holds T|(B \ A) is one-to-one
proof
  let A be Subset of V,
      B be Basis of V such that
    A1: A is Basis of ker T and
    A2: A c= B;
  set f = T|(B \ A);
  let x1,x2 be set such that
    A3: x1 in dom f and
    A4: x2 in dom f and
    A5: f.x1 = f.x2 and
    A6: x1 <> x2;
  A7: dom T = [#]V by Th7;
  reconsider x1 as Element of V by A3;
  reconsider x2 as Element of V by A4;
  reconsider A' = A as Subset of V;
  A8: x1 in B \ A by A3,A7,RELAT_1:91;
  A9: x2 in B \ A by A4,A7,RELAT_1:91;
  A10: f.x1 = T.x1 by A8,FUNCT_1:72;
  A11: f.x2 = T.x2 by A9,FUNCT_1:72;
  A12: x1 - x2 in ker T by A5,A10,A11,Th17;
  reconsider A as Basis of ker T by A1;
  ker T = Lin A by VECTSP_7:def 3; then
  x1 - x2 in Lin A' by A12,VECTSP_9:21; then
  A13: x1 in Lin (A' \/ {x2}) by Th18;
  A14: (A' \/ {x2}) \/ {x1} = A' \/ {x1,x2}
  proof
    {x2} \/ {x1} = {x1,x2} by ENUMSET1:41;
    hence thesis by XBOOLE_1:4;
  end;
  A15: not x1 in (A' \/ {x2})
  proof
    assume A16: x1 in A' \/ {x2};
    per cases by A16,XBOOLE_0:def 2;
    suppose x1 in A';
      hence contradiction by A8,XBOOLE_0:def 4;
    end;
    suppose x1 in {x2};
      hence contradiction by A6,TARSKI:def 1;
    end;
  end;
  A17: (A' \/ {x2}) \/ {x1} is linearly-dependent by A13,A15,Th21;
  A18: A' \/ {x1,x2} c= B
  proof
    {x1,x2} c= B
    proof
      let z be set such that
        A19: z in {x1,x2};
      per cases by A19,TARSKI:def 2;
      suppose z = x1;
        hence thesis by A8,XBOOLE_0:def 4;
      end;
      suppose z = x2;
        hence thesis by A9,XBOOLE_0:def 4;
      end;
    end;
    hence thesis by A2,XBOOLE_1:8;
  end;
  B is linearly-independent by VECTSP_7:def 3;
  hence thesis by A14,A17,A18,VECTSP_7:2;
end;

theorem
for A being Subset of V,
    l being Linear_Combination of A,
    x being Element of V,
    a being Element of F
  holds l +* (x,a) is Linear_Combination of A \/ {x}
proof
  let A be Subset of V,
      l be Linear_Combination of A,
      x be Element of V,
      a be Element of F;
  set m = l +* (x,a);
  A1: m is Element of Funcs ([#]V,[#]F)
  proof
    A2: dom m = [#]V
    proof
      A3: dom l = [#]V by FUNCT_2:169;
      A4: m = l +* (x .--> a) by A3,FUNCT_7:def 3;
      A5: dom (x .--> a) = {x} by FUNCOP_1:19;
      dom m = (dom l) \/ (dom (x .--> a)) by A4,FUNCT_4:def 1;
      hence thesis by A3,A5,XBOOLE_1:12;
    end;
    rng m c= [#]F
    proof
      let y be set such that A6: y in rng m;
      consider x' being set such that
        A7: x' in dom m and
        A8: m.x' = y by A6,FUNCT_1:def 5;
      A9: x' in dom l by A2,A7,FUNCT_2:169;
      per cases;
      suppose A10: x' = x;
        m.x' = a by A9,A10,FUNCT_7:33;
        hence thesis by A8;
      end;
      suppose x' <> x;
        then A11: m.x' = l.x' by FUNCT_7:34;
        A12: l.x' in rng l by A9,FUNCT_1:12;
        rng l c= [#]F by FUNCT_2:169;
        hence thesis by A8,A11,A12;
      end;
    end;
    hence thesis by A2,FUNCT_2:def 2;
  end;
  reconsider m as Element of Funcs ([#]V,[#]F) by A1;
  set T = Carrier l \/ {x};
  for v being Element of V st not v in T holds m.v = 0.F
  proof
    let v be Element of V such that
    A13: not v in T;
    A14: not v in Carrier l by A13,XBOOLE_0:def 2;
    A15: not v in {x} by A13,XBOOLE_0:def 2;
    A16: v <> x by A15,TARSKI:def 1;
    m.v = l.v by A16,FUNCT_7:34;
    hence thesis by A14;
  end;
  then reconsider m as Linear_Combination of V by VECTSP_6:def 4;
  A17: Carrier m c= T
  proof
    let y be set such that A18: y in Carrier m;
    consider z being Element of V such that
      A19: y = z and
      A20: m.z <> 0.F by A18;
    per cases;
    suppose A21: z = x;
      A22: x in {x} by TARSKI:def 1;
      {x} c= T by XBOOLE_1:7;
      hence thesis by A19,A21,A22;
    end;
    suppose A23: z <> x;
      A24: m.z = l.z by A23,FUNCT_7:34;
      A25: z in Carrier l by A20,A24;
      Carrier l c= T by XBOOLE_1:7;
      hence thesis by A19,A25;
    end;
  end;
  A26: T c= A \/ {x}
  proof
    Carrier l c= A by VECTSP_6:def 7;
    hence thesis by XBOOLE_1:9;
  end;
  Carrier m c= A \/ {x} by A17,A26,XBOOLE_1:1;
  hence thesis by VECTSP_6:def 7;
end;

definition
  let V be 1-sorted,
      X be Subset of V;
  func
    V \ X -> Subset of V
  equals
    [#]V \ X;
  coherence;
end;

definition
  let F be Field,
      V be VectSp of F,
      l be Linear_Combination of V,
      X be Subset of V;
  redefine func l .: X -> Subset of F;
  coherence
  proof
    l .: X c= [#]F
    proof
      let y be set such that
        A1: y in l .: X;
      consider x being set such that
        A2: x in dom l and
        x in X and
        A3: y = l.x by A1,FUNCT_1:def 12;
      A4: rng l c= [#]F by FUNCT_2:169;
      y in rng l by A2,A3,FUNCT_1:12;
      hence thesis by A4;
    end;
    hence thesis;
  end;
end;

reserve l for Linear_Combination of V;

registration
  let F be Field,
      V be VectSp of F;
  cluster linearly-dependent Subset of V;
  existence
  proof
    reconsider S = {0.V} as Subset of V;
    A1: 0.V in S by TARSKI:def 1;
    take S;
    thus thesis by A1,VECTSP_7:3;
  end;
end;

:: Restricting a linear combination to a given set
definition
  let F be Field,
      V be VectSp of F,
      l be Linear_Combination of V,
      A be Subset of V;
  func
    l!A -> Linear_Combination of A
  equals
    (l|A) +* ((V \ A) --> 0.F);
  coherence
  proof
    set f = (l|A) +* ((V \ A) --> 0.F);
      A1: dom f = dom (l|A) \/ dom ((V \ A) --> 0.F) by FUNCT_4:def 1;
      A2: dom l = [#]V by FUNCT_2:169;
      A3: dom (l|A) = A by A2,RELAT_1:91;
      A4: dom ((V \ A) --> 0.F) = V \ A by FUNCOP_1:19;
      A5: A \/ ([#]V \ A) = [#]V by XBOOLE_1:45;
      A6: dom f = [#]V by A1,A3,A4,XBOOLE_1:45;
      A7: rng f c= [#]F
      proof
        let y be set such that A8: y in rng f;
        consider x being set such that
          A9: x in dom f and
          A10: y = f.x by A8,FUNCT_1:def 5;
        reconsider x as Element of V by A1,A3,A4,A9,XBOOLE_1:45;
        per cases by A5,XBOOLE_0:def 2;
        suppose A11: x in A;
          A12: not x in dom ((V \ A) --> 0.F) by A4,A11,XBOOLE_0:def 4;
          A13: f.x = (l|A).x by A12,FUNCT_4:12;
          (l|A).x = l.x by A11,FUNCT_1:72;
          hence thesis by A10,A13;
        end;
        suppose A14: x in V \ A; then
          x in dom ((V \ A) --> 0.F) by FUNCOP_1:19; then
          f.x = ((V \ A) --> 0.F).x by FUNCT_4:14
          .= 0.F by A14,FUNCOP_1:13;
          hence thesis by A10;
        end;
      end;
      reconsider f as Element of Funcs([#]V,[#]F) by A6,A7,FUNCT_2:def 2;
      A15: ex T being finite Subset of V st
            for v being Element of V st not v in T holds f.v = 0.F
      proof
        set C = Carrier l;
        set D = { v where v is Element of V : f.v <> 0.F };
        A16: D is Subset of V
        proof
          D c= [#]V
          proof
            let x be set such that A17: x in D;
            consider v being Element of V such that
              A18: x = v and
              f.v <> 0.F by A17;
            thus thesis by A18;
          end;
          hence thesis;
        end;
        reconsider D as Subset of V by A16;
        A19: D c= C
        proof
          let x be set such that A20: x in D;
          consider v being Element of V such that
            A21: x = v and
            A22: f.v <> 0.F by A20;
          A23: dom ((V \ A) --> 0.F) = V \ A by FUNCOP_1:19;
          A24:now
            assume A25: v in V \ A;
            then f.v = ((V \ A) --> 0.F).v by A1,A6,A23,FUNCT_4:def 1
            .= 0.F by A25,FUNCOP_1:13;
            hence contradiction by A22;
          end; then
          A26: not v in dom ((V \ A) --> 0.F) by FUNCOP_1:19;
          A27: f.v = (l|A).v by A26,FUNCT_4:12;
          A28: v in A by A24,XBOOLE_0:def 4;
          (l|A).v = l.v by A28,FUNCT_1:72;
          hence thesis by A21,A22,A27;
        end;
        reconsider D as finite Subset of V by A19,FINSET_1:13;
        take D;
        thus thesis;
      end;
      reconsider f as Linear_Combination of V by A15,VECTSP_6:def 4;
      Carrier f c= A
      proof
        let x be set such that
          A29: x in Carrier f;
        reconsider x as Element of V by A29;
        A30: f.x <> 0.F by A29,VECTSP_6:20;
        now
          assume not x in A; then
          A31: x in V \ A by XBOOLE_0:def 4; then
          A32: x in dom (l|A) \/ (dom ((V \ A) --> 0.F)) by A4,XBOOLE_0:def 2;
          f.x = ((V \ A) --> 0.F).x by A4,A31,A32,FUNCT_4:def 1;
          hence contradiction by A30,A31,FUNCOP_1:13;
        end;
        hence thesis;
      end;
      hence thesis by VECTSP_6:def 7;
  end;
end;

theorem Th24:
l = l!Carrier l
proof
  set f = l|(Carrier l);
  set g = (V \ Carrier l) --> 0.F;
  set m = f +* g;
  A1: dom l = [#]V by FUNCT_2:169;
  A2: dom f = Carrier l by A1,RELAT_1:91;
  A3: dom g = V \ (Carrier l) by FUNCOP_1:19; then
  A4: (dom f) \/ (dom g) = [#]V by A2,XBOOLE_1:45; then
  A5: dom l = dom m by A1,FUNCT_4:def 1;
  for x being set st x in dom l holds l.x = m.x
  proof
    let x be set such that
    A6: x in dom l;
    reconsider x as Element of V by A6;
    per cases;
    suppose A7: x in Carrier l;
      A8: not x in dom g by A3,A7,XBOOLE_0:def 4;
      m.x = f.x by A4,A8,FUNCT_4:def 1;
      hence thesis by A7,FUNCT_1:72;
    end;
    suppose A9: not x in Carrier l; then
      A10: x in V \ (Carrier l) by XBOOLE_0:def 4; then
      A11: m.x = g.x by A3,A4,FUNCT_4:def 1;
      g.x = 0.F by A10,FUNCOP_1:13;
      hence thesis by A9,A11;
    end;
  end;
  hence thesis by A5,FUNCT_1:def 17;
end;

Lm1: for X being Subset of V holds l .: X is finite
proof
  let X be Subset of V;
  l = l!(Carrier l) by Th24; then
  A1: rng l c= (rng (l|Carrier l)) \/ rng ((V \ (Carrier l)) --> 0.F)
    by FUNCT_4:18;
  A2: rng (l|Carrier l) is finite
  proof
    rng (l|Carrier l) = l .: Carrier l by RELAT_1:148;
    hence thesis;
  end;
  A3: rng ((V \ (Carrier l)) --> 0.F) c= {0.F}
  proof
    set f = ((V \ (Carrier l)) --> 0.F);
    per cases;
    suppose V \ (Carrier l) = {};
      then f = {} by ZFMISC_1:113;
      hence thesis by RELAT_1:60,XBOOLE_1:2;
    end;
    suppose V \ (Carrier l) <> {};
      hence thesis by FUNCOP_1:14;
    end;
  end;
  rng ((V \ (Carrier l)) --> 0.F) is finite by A3,FINSET_1:13; then
  (rng (l|Carrier l)) \/ rng ((V \ (Carrier l)) --> 0.F) is finite
         by A2,FINSET_1:14; then
  A4: rng l is finite by A1,FINSET_1:13;
  l .: X c= rng l by RELAT_1:144;
  hence thesis by A4,FINSET_1:13;
end;

theorem Th25:
for A being Subset of V,
    v being Element of V
  st v in A
  holds (l!A).v = l.v
proof
  let A be Subset of V,
      v be Element of V such that
  A1: v in A;
  A2: not v in V \ A by A1,XBOOLE_0:def 4;
  A3: not v in dom ((V \ A) --> 0.F) by A2,FUNCOP_1:19;
  dom (l!A) = [#]V by FUNCT_2:169; then
  (dom (l|A)) \/ (dom ((V \ A) --> 0.F)) = [#]V by FUNCT_4:def 1; then
  (l!A).v = (l|A).v by A3,FUNCT_4:def 1
  .= l.v by A1,FUNCT_1:72;
  hence thesis;
end;

theorem Th26:
for A being Subset of V,
    v being Element of V
  st not v in A
  holds (l!A).v = 0.F
proof
  let A be Subset of V,
      v be Element of V such that
    A1: not v in A;
  A2: dom ((V \ A) --> 0.F) = V \ A by FUNCOP_1:19;
  A3: dom (l!A) = (dom (l|A)) \/ (dom ((V \ A) --> 0.F)) by FUNCT_4:def 1;
  A4: dom (l!A) = [#]V by FUNCT_2:169;
  A5: v in V \ A by A1,XBOOLE_0:def 4;
  (l!A).v = ((V \ A) --> 0.F).v by A2,A3,A4,A5,FUNCT_4:def 1
  .= 0.F by A5,FUNCOP_1:13;
  hence thesis;
end;

theorem Th27:
for A,B being Subset of V,
    l being Linear_Combination of B
  st A c= B
  holds l = (l!A) + (l!(B\A))
proof
  let A,B be Subset of V,
      l be Linear_Combination of B such that
    A1: A c= B;
    set r = (l!A) + (l!(B\A));
    let v be Element of V;
    A2: (v in B) implies (v in A or v in B \ A)
    proof
      assume A3: v in B;
      B = A \/ (B \ A) by A1,XBOOLE_1:45;
      hence thesis by A3,XBOOLE_0:def 2;
    end;
    per cases by A2;
    suppose A4: v in A;
      A5: not v in B \ A by A4,XBOOLE_0:def 4;
      A6: (l!(B\A)).v = 0.F by A5,Th26;
      A7: (l!A).v = l.v by A4,Th25;
      r.v = l.v + 0.F by A6,A7,VECTSP_6:def 11
      .= l.v by RLVECT_1:10;
      hence l.v = r.v;
    end;
    suppose A8: v in B\A; then
      not v in A by XBOOLE_0:def 4; then
      A9: (l!A).v = 0.F by Th26;
      (l!(B\A)).v = l.v by A8,Th25; then
      r.v = 0.F + l.v by A9,VECTSP_6:def 11
      .= l.v by RLVECT_1:10;
      hence l.v = r.v;
    end;
    suppose A10: not v in B;
      A11: not v in B\A by A10,XBOOLE_0:def 4;
      A12: not v in A by A1,A10;
      A13: (l!A).v = 0.F by A12,Th26;
      A14: (l!(B\A)).v = 0.F by A11,Th26;
      A15: Carrier l c= B by VECTSP_6:def 7;
      A16: not v in Carrier l by A10,A15;
      r.v = 0.F + 0.F by A13,A14,VECTSP_6:def 11
      .= 0.F by RLVECT_1:10;
      hence l.v = r.v by A16;
    end;
end;

registration
  let F be Field,
      V be VectSp of F,
      l be Linear_Combination of V,
      X be Subset of V;
  cluster l .: X -> finite;
  coherence by Lm1;
end;

definition
  let V,W be non empty 1-sorted,
      T be Function of V,W,
      X be Subset of W;
  redefine func T"X -> Subset of V;
  coherence
  proof
    dom T = [#]V by Th7;
    hence thesis by RELAT_1:167;
  end;
end;

theorem Th28:
for X being Subset of V
  st X misses Carrier l
  holds l .: X c= {0.F}
proof
  let X be Subset of V such that
A1: X misses Carrier l;
  set M = l .: X;
  let y be set such that A2: y in M;
  consider x being set such that
    A3: x in dom l and
    A4: x in X and
    A5: y = l.x by A2,FUNCT_1:def 12;
  reconsider x as Element of V by A3;
  now
    assume l.x <> 0.F; then
    x in Carrier l; then
    x in (Carrier l) /\ X by A4,XBOOLE_0:def 3;
    hence contradiction by A1,XBOOLE_0:def 7;
  end;
  hence thesis by A5,TARSKI:def 1;
end;

:: The image of a linear combination under a linear transformation:
::
::   T(a1*v1 + a2*v2 + ... + an*vn)
::     =  a1*T(v1) + a2*T(v2) + ... + an*T(vn).
::
:: Linear combinations are represented as functions from the space to
:: the underlying field having finite support, so to define a new
:: linear combination it is enough to say what its values are for the
:: elements of W and to prove that its support is finite.
::
:: The only difficulty is that some values T(vi) and T(vj) may be
:: equal.  In this case, the new linear combination should be the sum
:: of the coefficients ai and aj, i.e., l(vi) and l(vj).
definition
  let F be Field,
      V,W be VectSp of F,
      l be Linear_Combination of V,
      T be linear-transformation of V,W;
  func
    T@l -> Linear_Combination of W
  means :Def5:
    for w being Element of W holds
      it.w = Sum (l .: (T"{w}));
  existence
  proof
    defpred P[set,set] means
      ex w being Element of W st $1 = w & $2 = Sum (l .: (T"{w}));
    A1: for x, y1, y2 being set st x in [#]W & P[x,y1] & P[x,y2] holds y1 = y2;
    A2: for x being set st x in [#]W holds ex y being set st P[x,y]
    proof
      let x be set such that A3: x in [#]W;
      reconsider x as Element of W by A3;
      take Sum (l .: (T"{x}));
      thus thesis;
    end;
    consider f being Function such that
      A4: dom f = [#]W and
      A5: for x being set st x in [#]W holds P[x,f.x]
            from FUNCT_1:sch 2(A1,A2);
   A6: for w being Element of W holds f.w = Sum (l .: (T"{w}))
   proof
     let w be Element of W;
     consider w' being Element of W such that
       A7: w = w' and
       A8: f.w = Sum (l .: (T"{w'})) by A5;
     thus thesis by A7,A8;
   end;
   A9: rng f c= [#]F
   proof
     let y be set such that A10: y in rng f;
     consider x being set such that
       A11: x in dom f and
       A12: f.x = y by A10,FUNCT_1:def 5;
     consider w being Element of W such that
       x = w and
       A13: f.x = Sum (l .: (T"{w})) by A4,A5,A11;
     thus thesis by A12,A13;
   end;
   reconsider f as Element of Funcs([#]W,[#]F) by A4,A9,FUNCT_2:def 2;
   A14: ex T being finite Subset of W
         st for w being Element of W st not w in T holds f.w = 0.F
   proof
     set C = Carrier l;
     reconsider TC = T .: C as Subset of W;
     set X = { w where w is Element of W : f.w <> 0.F };
     A15: X is Subset of W
     proof
       X c= [#]W
       proof
         let x be set such that A16: x in X;
         consider w being Element of W such that
         A17: x = w and
         f.w <> 0.F by A16;
         thus thesis by A17;
       end;
       hence thesis;
     end;
     reconsider X as Subset of W by A15;
     A18: X c= TC
     proof
       let x be set such that A19: x in X;
       consider w being Element of W such that
         A20: x = w and
         A21: f.w <> 0.F by A19;
       A22: T"{w} meets Carrier l
       proof
         assume A23: T"{w} misses Carrier l; then
         A24: l .: T"{w} c= {0.F} by Th28;
         Sum (l .: T"{w}) = 0.F
         proof
           per cases;
           suppose l .: T"{w} = {}F;
             hence thesis by RLVECT_2:14;
           end;
           suppose A25: l .: T"{w} <> {}F;
             l .: T"{w} = {0.F}
             proof
               thus l .: T"{w} c= {0.F} by A23,Th28;
               thus {0.F} c= l .: T"{w}
               proof
                 let y be set such that A26: y in {0.F};
                 A27: y = 0.F by A26,TARSKI:def 1;
                 consider z being set such that A28: z in l .: T"{w}
                   by A25,XBOOLE_0:def 1;
                 thus thesis by A24,A27,A28,TARSKI:def 1;
               end;
             end;
             hence thesis by RLVECT_2:15;
           end;
         end;
         hence contradiction by A6,A21;
       end;
       consider y being set such that
       A29: y in T"{w} and
       A30: y in Carrier l by A22,XBOOLE_0:3;
       reconsider y as Element of V by A30;
       A31: dom T = [#]V by Th7;
       T.y in {w} by A29,FUNCT_1:def 13; then
       T.y = w by TARSKI:def 1;
       hence thesis by A20,A30,A31,FUNCT_1:def 12;
     end;
     reconsider X as finite Subset of W by A18,FINSET_1:13;
     take X;
     thus thesis;
   end;
   reconsider f as Linear_Combination of W by A14,VECTSP_6:def 4;
   A32: for w being Element of W holds f.w = Sum (l .: (T"{w}))
   proof
     let w be Element of W;
     consider w' being Element of W such that
     A33: w = w' and
     A34: f.w = Sum (l .: (T"{w'})) by A5;
     thus thesis by A33,A34;
   end;
   take f;
   thus thesis by A32;
 end;
  uniqueness
  proof
    let f,g be Linear_Combination of W such that
      A35: for w being Element of W holds f.w = Sum (l .: (T"{w})) and
      A36: for w being Element of W holds g.w = Sum (l .: (T"{w}));
    A37: dom f = [#]W by FUNCT_2:169;
    A38: dom g = [#]W by FUNCT_2:169;
    for x being set st x in dom f holds f.x = g.x
    proof
      let x be set such that A39: x in dom f;
      reconsider x as Element of W by A39;
      f.x = Sum (l .: (T"{x})) by A35;
      hence thesis by A36;
    end;
    hence thesis by A37,A38,FUNCT_1:def 17;
  end;
end;

theorem Th29:
T@l is Linear_Combination of T .: (Carrier l)
proof
  Carrier (T@l) c= T .: (Carrier l)
  proof
    let w be set such that A1: w in Carrier (T@l);
    reconsider w as Element of W by A1;
    A2: (T@l).w <> 0.F by A1,VECTSP_6:20;
    now
      assume A3: T"{w} misses Carrier l;
      A4: l .: T"{w} c= {0.F} by A3,Th28;
      Sum (l .: T"{w}) = 0.F
      proof
        per cases;
        suppose l .: T"{w} = {}F;
          hence thesis by RLVECT_2:14;
        end;
        suppose A5: l .: T"{w} <> {}F;
          l .: T"{w} = {0.F}
          proof
            thus l .: T"{w} c= {0.F} by A3,Th28;
            thus {0.F} c= l .: T"{w}
            proof
              let y be set such that A6: y in {0.F};
              A7: y = 0.F by A6,TARSKI:def 1;
              consider z being set such that A8: z in l .: T"{w}
                by A5,XBOOLE_0:def 1;
              thus thesis by A4,A7,A8,TARSKI:def 1;
            end;
          end;
          hence thesis by RLVECT_2:15;
        end;
      end;
      hence contradiction by A2,Def5;
    end;
    then consider x being set such that
    A9: x in T"{w} and
    A10: x in Carrier l by XBOOLE_0:3;
    A11: x in dom T by A9,FUNCT_1:def 13;
    A12: T.x in {w} by A9,FUNCT_1:def 13;
    reconsider x as Element of V by A9;
    T.x = w by A12,TARSKI:def 1;
    hence thesis by A10,A11,FUNCT_1:def 12;
  end;
  hence thesis by VECTSP_6:def 7;
end;

theorem Th30:
Carrier (T@l) c= T .: (Carrier l)
proof
  T@l is Linear_Combination of T .: (Carrier l) by Th29;
  hence thesis by VECTSP_6:def 7;
end;

theorem Th31:
for l,m being Linear_Combination of V
  st (Carrier l) misses (Carrier m)
  holds Carrier (l + m) = (Carrier l) \/ (Carrier m)
proof
  let l,m be Linear_Combination of V such that
    A1: (Carrier l) misses (Carrier m);
  thus Carrier (l+m) c= (Carrier l) \/ (Carrier m) by VECTSP_6:51;
  thus (Carrier l) \/ (Carrier m) c= Carrier (l+m)
  proof
    let v be set such that
      A2: v in (Carrier l) \/ (Carrier m);
    per cases by A2,XBOOLE_0:def 2;
    suppose A3: v in Carrier l; then
      reconsider v as Element of V;
      A4: (l+m).v = (l.v) + (m.v) by VECTSP_6:def 11;
      A5: l.v <> 0.F by A3,VECTSP_6:20;
      not v in Carrier m by A1,A2,A3,XBOOLE_0:5; then
      m.v = 0.F; then
      (l+m).v = l.v by A4,RLVECT_1:10;
      hence thesis by A5;
    end;
    suppose A6: v in Carrier m; then
      reconsider v as Element of V;
      A7: (l+m).v = (l.v) + (m.v) by VECTSP_6:def 11;
      A8: m.v <> 0.F by A6,VECTSP_6:20;
      not v in Carrier l by A1,A2,A6,XBOOLE_0:5; then
      l.v = 0.F; then
      (l+m).v = m.v by A7,RLVECT_1:10;
      hence thesis by A8;
    end;
  end;
end;

theorem Th32:
for l,m being Linear_Combination of V
  st (Carrier l) misses (Carrier m)
  holds Carrier (l - m) = (Carrier l) \/ (Carrier m)
proof
  let l,m be Linear_Combination of V such that
  A1: (Carrier l) misses (Carrier m);
  Carrier (-m) = Carrier m by VECTSP_6:69;
  hence thesis by A1,Th31;
end;

theorem Th33:
for A,B being Subset of V
  st A c= B & B is Basis of V
  holds V is_the_direct_sum_of Lin A, Lin (B \ A)
proof
  let A,B be Subset of V such that
    A1: A c= B and
    A2: B is Basis of V;
  A3: (Omega).V = (Lin A) + (Lin (B \ A))
  proof
    set U = (Lin A) + (Lin (B \ A));
    [#]U = [#]V
    proof
      thus [#]U c= [#]V by VECTSP_4:def 2;
      thus [#]V c= [#]U
      proof
        let v be set such that
        A4: v in [#]V;
        reconsider v as Element of V by A4;
        A5: v in Lin B by A2,VECTSP_9:14;
        consider l being Linear_Combination of B such that
          A6: v = Sum l by A5,VECTSP_7:12;
        set m = l!A;
        set n = l!(B\A);
        A7: l = m + n by A1,Th27;
        A8: ex v1,v2 being Element of V
               st v1 in Lin A & v2 in Lin (B \ A) & v = v1 + v2
        proof
          take Sum m, Sum n;
          thus thesis by A6,A7,VECTSP_6:77,VECTSP_7:12;
        end;
        v in (Lin A) + (Lin (B \ A)) by A8,VECTSP_5:5;
        hence thesis by STRUCT_0:def 5;
      end;
    end;
    hence thesis by VECTSP_4:37;
  end;
  (Lin A) /\ (Lin (B \ A)) = (0).V
  proof
    set U = (Lin A) /\ (Lin (B \ A));
    reconsider W = (0).V as strict Subspace of U by VECTSP_4:50;
    for v being Element of U holds v in W
    proof
      let v be Element of U;
      A9: v in U by STRUCT_0:def 5;
      A10: v in Lin A by A9,VECTSP_5:7;
      A11: v in Lin (B \ A) by A9,VECTSP_5:7;
      consider l being Linear_Combination of A such that
      A12: v = Sum l by A10,VECTSP_7:12;
      consider m being Linear_Combination of B \ A such that
      A13: v = Sum m by A11,VECTSP_7:12;
      A14: 0.V = (Sum l) - (Sum m) by A12,A13,VECTSP_1:66
      .= Sum (l - m) by VECTSP_6:80;
      A15: Carrier (l - m) c= (Carrier l) \/ (Carrier m) by VECTSP_6:74;
      A16: Carrier l c= A by VECTSP_6:def 7;
      A17: Carrier m c= B \ A by VECTSP_6:def 7;
      A18: A \/ (B \ A) = B by A1,XBOOLE_1:45;
      A19: (Carrier l) \/ (Carrier m) c= A \/ (B \ A)
        by A16,A17,XBOOLE_1:13;
      A20: Carrier (l - m) c= B by A15,A18,A19,XBOOLE_1:1;
      reconsider n = l - m as Linear_Combination of B by A20,VECTSP_6:def 7;
      A21: B is linearly-independent by A2,VECTSP_7:def 3;
      A22: Carrier n = {} by A14,A21,VECTSP_7:def 1;
      A23: A misses (B \ A) by XBOOLE_1:79;
      A24: (Carrier l) misses (Carrier m) by A16,A17,A23,XBOOLE_1:64;
      A25: Carrier n = (Carrier l) \/ (Carrier m) by A24,Th32;
      A26: Carrier l = {} by A22,A25,XBOOLE_1:15;
      A27: l = ZeroLC(V) by A26,VECTSP_6:def 6;
      Sum l = 0.V by A27,VECTSP_6:41;
      hence thesis by A12,VECTSP_4:46;
    end;
    hence thesis by VECTSP_4:40;
  end;
  hence thesis by A3,VECTSP_5:def 4;
end;

theorem Th34:
for A being Subset of V,
    l being Linear_Combination of A,
    v being Element of V
  st T|A is one-to-one & v in A
  holds ex X being Subset of V st
    X misses A & T"{T.v} = {v} \/ X
proof
  let A be Subset of V,
      l be Linear_Combination of A,
      v be Element of V such that
    A1: T|A is one-to-one and
    A2: v in A;
  set X = T"{T.v} \ {v};
  A3: {v} c= T"{T.v}
  proof
    let x be set such that A4: x in {v};
    A5: x = v by A4,TARSKI:def 1;
    A6: dom T = [#]V by Th7;
    T.v in {T.v} by TARSKI:def 1;
    hence thesis by A5,A6,FUNCT_1:def 13;
  end;
  A7: X misses A
  proof
    assume X meets A; then
    consider x being set such that
    A8: x in X and
    A9: x in A by XBOOLE_0:3;
    A10: x in T"{T.v} by A8,XBOOLE_0:def 4;
    not x in {v} by A8,XBOOLE_0:def 4; then
    A11: x <> v by TARSKI:def 1;
    T.x in {T.v} by A10,FUNCT_1:def 13; then
    A12: T.x = T.v by TARSKI:def 1;
    T.x = (T|A).x by A9,FUNCT_1:72; then
    A13: (T|A).v = (T|A).x by A2,A12,FUNCT_1:72;
    dom T = [#]V by Th7; then
    dom (T|A) = A by RELAT_1:91;
    hence thesis by A1,A2,A9,A11,A13,FUNCT_1:def 8;
  end;
  take X;
  thus thesis by A3,A7,XBOOLE_1:45;
end;

theorem Th35:
for X being Subset of V
  st X misses Carrier l & X <> {}
  holds l .: X = {0.F}
proof
  let X be Subset of V such that
    A1: X misses Carrier l and
    A2: X <> {};
  A3: l .: X c= {0.F} by A1,Th28;
  dom l = [#]V by FUNCT_2:169; then
  l .: X <> {} by A2,RELAT_1:152;
  hence thesis by A3,ZFMISC_1:39;
end;

theorem Th36:
for w being Element of W
  st w in Carrier (T@l)
  holds T"{w} meets Carrier l
proof
  let w be Element of W such that
  A1: w in Carrier (T@l);
  A2: (T@l).w <> 0.F by A1,VECTSP_6:20;
  assume A3: T"{w} misses Carrier l;
  per cases;
  suppose T"{w} = {};
    then Sum (l .: T"{w}) = Sum ({}F) by RELAT_1:149
                         .= 0.F by RLVECT_2:14;
    hence thesis by A2,Def5;
  end;
  suppose T"{w} <> {};
    then l .: T"{w} = {0.F} by A3,Th35;
    then Sum (l .: T"{w}) = 0.F by RLVECT_2:15;
    hence thesis by A2,Def5;
  end;
end;

theorem Th37:
for v being Element of V
  st T|(Carrier l) is one-to-one & v in Carrier l
  holds (T@l).(T.v) = l.v
proof
  let v be Element of V such that
  A1: T|(Carrier l) is one-to-one and
  A2: v in Carrier l;
  consider X being Subset of V such that
  A3: X misses Carrier l and
  A4: T"{T.v} = {v} \/ X by A1,A2,Th34;
  per cases;
  suppose A5:X = {};
    A6: dom l = [#]V by FUNCT_2:169;
    l .: {v} = Im (l,v)
    .= {l.v} by A6,FUNCT_1:117; then
    Sum (l .: T"{T.v}) = l.v by A4,A5,RLVECT_2:15;
    hence thesis by Def5;
  end;
  suppose A7: X <> {};
    A8: l .: T"{T.v} = (l .: {v}) \/ (l .: X) by A4,RELAT_1:153;
    A9: dom l = [#]V by FUNCT_2:169;
    A10: l .: {v} = Im (l,v)
    .= {l.v} by A9,FUNCT_1:117;
    A11: l .: X = {0.F}
    proof
      A12: {0.F} c= l .: X
      proof
        let x be set such that A13: x in {0.F};
        A14: x = 0.F by A13,TARSKI:def 1;
        consider y being set such that A15: y in X by A7,XBOOLE_0:def 1;
        A16:now
          assume y in Carrier l;
          then y in (Carrier l) /\ X by A15,XBOOLE_0:def 3;
          hence contradiction by A3,XBOOLE_0:def 7;
        end;
        reconsider y as Element of V by A15;
        l.y = x by A14,A16;
        hence thesis by A9,A15,FUNCT_1:def 12;
      end;
      l .: X c= {0.F}
      proof
        let y be set such that A17: y in l .: X;
        consider x being set such that
        A18: x in dom l and
        A19: x in X and
        A20: y = l.x by A17,FUNCT_1:def 12;
        A21: now
          assume x in Carrier l;
          then x in (Carrier l) /\ X by A19,XBOOLE_0:def 3;
          hence contradiction by A3,XBOOLE_0:def 7;
        end;
        reconsider x as Element of V by A18;
        l.x = 0.F by A21;
        hence thesis by A20,TARSKI:def 1;
      end;
      hence thesis by A12,XBOOLE_0:def 10;
    end;
    A22: l .: X misses l .: {v}
    proof
      A23: dom l = [#]V by FUNCT_2:169;
      A24: l .: {v} = Im (l,v)
      .= {l.v} by A23,FUNCT_1:117;
      assume l .: X meets l .: {v};
      then consider x being set such that
      A25: x in l .: X and
      A26: x in l .: {v} by XBOOLE_0:3;
      A27: x = 0.F by A11,A25,TARSKI:def 1;
      x = l.v by A24,A26,TARSKI:def 1;
      hence thesis by A2,A27,VECTSP_6:20;
    end;
    Sum (l .: T"{T.v}) = (Sum (l .: {v})) + (Sum (l .: X)) by A8,A22,
RLVECT_2:18
    .= l.v + (Sum ({0.F})) by A10,A11,RLVECT_2:15
    .= l.v + 0.F by RLVECT_2:15
    .= l.v by RLVECT_1:10;
    hence thesis by Def5;
  end;
end;

theorem Th38:
for G being FinSequence of V
  st rng G = Carrier l & T|(Carrier l) is one-to-one
  holds T*(l (#) G) = (T@l) (#) (T*G)
proof
  let G be FinSequence of V such that
  A1: rng G = Carrier l and
  A2: T|(Carrier l) is one-to-one;
  reconsider L = T*(l (#) G) as FinSequence of W;
  reconsider R = (T@l) (#) (T*G) as FinSequence of W;
  A3: len L = len (l (#) G) by FINSEQ_2:37
  .= len G by VECTSP_6:def 8;
  A4: len R = len (T*G) by VECTSP_6:def 8
  .= len G by FINSEQ_2:37;
  for k being Nat st 1 <= k & k <= len L holds L.k = R.k
  proof
    let k be Nat such that
      A5: 1 <= k and
      A6: k <= len L;
    A7: len (l (#) G) = len G by VECTSP_6:def 8;
    A8: dom (l (#) G) = Seg len G by A7,FINSEQ_1:def 3;
    k in NAT by ORDINAL1:def 13;
    then
    A9: k in dom (l (#) G) by A3,A5,A6,A8;
    A10: k in dom G by A8,A9,FINSEQ_1:def 3;
    A11: G.k in rng G by A10,FUNCT_1:12;
    reconsider gk = G/.k as Element of V;
    A12: (l (#) G).k = (l.gk)*gk by A9,VECTSP_6:def 8;
    A13: G.k = G/.k by A10,PARTFUN1:def 8;
    reconsider Gk = G.k as Element of V by A13;
    A14: (T*G).k = T.Gk by A10,FUNCT_1:23;
    reconsider TGk = (T*G).k as Element of W by A14;
    A15: L.k = T.((l.gk)*gk) by A9,A12,FUNCT_1:23
    .= (l.gk)*(T.gk) by MOD_2:def 5
    .= (l.Gk)*TGk by A10,A13,FUNCT_1:23;
    A16: dom R = Seg len G by A4,FINSEQ_1:def 3;
    dom T = [#]V by Th7; then
    dom (T*G) = dom G by A1,RELAT_1:46; then
    A17: (T*G)/.k = (T*G).k by A10,PARTFUN1:def 8;
    (T@l).((T*G).k) = l.(G.k)
    proof
      (T*G).k = T.(G.k) by A10,FUNCT_1:23;
      hence thesis by A1,A2,A11,Th37;
    end;
    hence thesis by A8,A9,A15,A16,A17,VECTSP_6:def 8;
  end;
  hence thesis by A3,A4,FINSEQ_1:18;
end;

theorem Th39:
T|(Carrier l) is one-to-one
  implies T .: (Carrier l) = Carrier (T@l)
proof
 assume A1: T|(Carrier l) is one-to-one;
  A2: Carrier (T@l) c= T .: (Carrier l) by Th30;
  T .: (Carrier l) c= Carrier (T@l)
  proof
    let w be set such that A3: w in T .: (Carrier l);
    consider v being set such that
      A4: v in dom T and
      A5: v in Carrier l and
      A6: T.v = w by A3,FUNCT_1:def 12;
    reconsider v as Element of V by A4;
    A7: T.v in rng T by A4,FUNCT_1:12;
    reconsider w as Element of W by A6,A7;
    A8: (T@l).(T.v) = l.v by A1,A5,Th37;
    l.v <> 0.F by A5,VECTSP_6:20;
    hence thesis by A6,A8;
  end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th40:
for A being Subset of V,
    B being Basis of V,
    l being Linear_Combination of B \ A
  st A is Basis of ker T & A c= B
  holds T.(Sum l) = Sum (T@l)
proof
  let A be Subset of V,
      B be Basis of V,
      l be Linear_Combination of B \ A such that
   A1: A is Basis of ker T and
   A2: A c= B;
  consider G being FinSequence of V such that
    A3: G is one-to-one and
    A4: rng G = Carrier l and
    A5: Sum l = Sum (l (#) G) by VECTSP_6:def 9;
  set H = T*G;
  reconsider H as FinSequence of W;
  A6: T|(B \ A) is one-to-one by A1,A2,Th22;
  A7: Carrier l c= B \ A by VECTSP_6:def 7;
  A8: (T|(B \ A))|(Carrier l) = T|(Carrier l) by A7,RELAT_1:103;
  A9: T|(Carrier l) is one-to-one by A6,A8,FUNCT_1:84;
  A10: dom T = [#]V by Th7;
  A11: H is one-to-one by A3,A4,A9,A10,Th1;
  A12: rng H = T .: (Carrier l) by A4,RELAT_1:160
  .= Carrier (T@l) by A9,Th39;
  A13: T*(l (#) G) = (T@l) (#) H by A4,A9,Th38;
  Sum (T@l) = Sum ((T@l) (#) H) by A11,A12,VECTSP_6:def 9;
  hence thesis by A5,A13,MATRLIN:20;
end;

theorem Th41:
for X being Subset of V
  st X is linearly-dependent
  holds ex l being Linear_Combination of X
          st Carrier l <> {} & Sum l = 0.V
proof
  let X be Subset of V such that
    A1: X is linearly-dependent;
  not (for l being Linear_Combination of X
         st Sum l = 0.V
         holds Carrier l = {}) by A1,VECTSP_7:def 1;
  hence thesis;
end;

:: "Pulling back" a linear combination from the image space of a
:: linear transformation to the base space.
definition
  let F be Field,
      V,W be VectSp of F,
      X be Subset of V,
      T be linear-transformation of V,W,
      l be Linear_Combination of T .: X;
  assume A1: T|X is one-to-one;
  func
    T#l -> Linear_Combination of X
  equals :Def6:
    (l*T) +* ((V \ X) --> 0.F);
  coherence
  proof
    set f = (l*T) +* ((V \ X) --> 0.F);
    dom l = [#]W by FUNCT_2:169; then
    rng T c= dom l by Th7; then
    A2: dom (l*T) = dom T by RELAT_1:46;
    A3: dom ((V \ X) --> 0.F) = [#]V \ X by FUNCOP_1:19;
    A4: dom T = [#]V by Th7;
    [#]V \/ ([#]V \ X) = [#]V by XBOOLE_1:12; then
    A5: dom f = [#]V by A2,A3,A4,FUNCT_4:def 1;
    A6: rng f c= rng (l*T) \/ rng ((V \ X) --> 0.F) by FUNCT_4:18;
    A7: rng (l*T) c= rng l by RELAT_1:45;
    A8: rng ((V \ X) --> 0.F) c= {0.F} by FUNCOP_1:19;
    A9: rng ((V \ X) --> 0.F) c= [#]F by A8,XBOOLE_1:1;
    rng l c= [#]F by FUNCT_2:169; then
    rng (l*T) c= [#]F by A7,XBOOLE_1:1; then
    rng (l*T) \/ rng ((V \ X) --> 0.F) c= [#]F by A9,XBOOLE_1:8; then
    A10: rng f c= [#]F by A6,XBOOLE_1:1;
    reconsider f as Element of Funcs ([#]V,[#]F) by A5,A10,FUNCT_2:def 2;
    ex T being finite Subset of V st
      for v being Element of V st not v in T holds f.v = 0.F
    proof
      set C = { v where v is Element of V : f.v <> 0.F };
      C c= [#]V
      proof
        let x be set such that A11: x in C;
        consider v being Element of V such that
          A12: v = x and
          f.v <> 0.F by A11;
        thus thesis by A12;
      end;
      then reconsider C as Subset of V;
      A13: C is finite
      proof
        A14: Card C <=` Card Carrier l
        proof
          ex g being Function
            st g is one-to-one & dom g = C & rng g c= Carrier l
          proof
            set S = (T"(Carrier l)) /\ X;
            set g = T|S;
            A15: S = C
            proof
              A16: S c= C
              proof
                let x be set such that A17: x in S;
                A18: x in X by A17,XBOOLE_0:def 3;
                A19: x in T"(Carrier l) by A17,XBOOLE_0:def 3; then
                A20: x in dom T by FUNCT_1:def 13;
                A21: T.x in Carrier l by A19,FUNCT_1:def 13;
                reconsider x as Element of V by A17;
                A22: not x in dom ((V \ X) --> 0.F) by A3,A18,XBOOLE_0:def 4;
                A23: f.x = (l*T).x by A22,FUNCT_4:12;
                A24: (l*T).x = l.(T.x) by A20,FUNCT_1:23;
                l.(T.x) <> 0.F by A21,VECTSP_6:20;
                hence thesis by A23,A24;
              end;
              C c= S
              proof
                let x be set such that A25: x in C;
                consider v being Element of V such that
                  A26: v = x and
                  A27: f.v <> 0.F by A25;
                reconsider x as Element of V by A26;
                A28:now
                  assume not x in X; then
                  A29: x in V \ X by XBOOLE_0:def 4; then
                  x in dom ((V \ X) --> 0.F) by FUNCOP_1:19; then
                  f.x = ((V \ X) --> 0.F).x by FUNCT_4:14;
                  hence contradiction by A26,A27,A29,FUNCOP_1:13;
                end;
                x in T"(Carrier l)
                proof
                  A30: dom T = [#]V by Th7;
                  T.x in Carrier l
                  proof
                    not x in V \ X by A28,XBOOLE_0:def 4; then
                    A31: f.x = (l*T).x by A3,FUNCT_4:12;
                    (l*T).x = l.(T.x) by A30,FUNCT_1:23;
                    hence thesis by A26,A27,A31;
                  end;
                  hence thesis by A30,FUNCT_1:def 13;
                end;
                hence thesis by A28,XBOOLE_0:def 3;
              end;
              hence thesis by A16,XBOOLE_0:def 10;
            end;
            A32: g is one-to-one
            proof
              (T"(Carrier l)) /\ X c= X by XBOOLE_1:17;
              hence thesis by A1,Th2;
            end;
            A33: dom g = S
            proof
              dom T = [#]V by Th7;
              hence thesis by RELAT_1:91;
            end;
            A34: rng g c= Carrier l
            proof
              let y be set such that A35: y in rng g;
              consider x being set such that
                A36: x in dom g and
                A37: y = g.x by A35,FUNCT_1:def 5;
              x in T"(Carrier l) by A33,A36,XBOOLE_0:def 3; then
              T.x in Carrier l by FUNCT_1:def 13;
              hence thesis by A33,A36,A37,FUNCT_1:72;
            end;
            take g;
            thus thesis by A15,A32,A33,A34;
          end;
          hence thesis by CARD_1:26;
        end;
        thus thesis by A14,CARD_2:68;
      end;
      reconsider C as finite Subset of V by A13;
      take C;
      thus thesis;
    end;
    then reconsider f as Linear_Combination of V by VECTSP_6:def 4;
    Carrier f c= X
    proof
      let x be set such that A38: x in Carrier f;
      reconsider x as Element of V by A38;
      now assume not x in X; then
        A39: x in V \ X by XBOOLE_0:def 4; then
        f.x = ((V \ X) --> 0.F).x by A3,FUNCT_4:14
        .= 0.F by A39,FUNCOP_1:13;
        hence contradiction by A38,VECTSP_6:20;
      end;
      hence thesis;
    end;
    hence thesis by VECTSP_6:def 7;
  end;
end;

theorem Th42:
for X being Subset of V,
    l being Linear_Combination of T .: X,
    v being Element of V
  st v in X & T|X is one-to-one
  holds (T#l).v = l.(T.v)
proof
  let X be Subset of V,
      l be Linear_Combination of T .: X,
      v be Element of V such that
  A1: v in X and
  A2: T|X is one-to-one;
  dom ((V \ X) --> 0.F) = V \ X by FUNCOP_1:19; then
  A3: not v in dom ((V \ X) --> 0.F) by A1,XBOOLE_0:def 4;
  T#l = (l*T) +* ((V \ X) --> 0.F) by A2,Def6; then
  A4: (T#l).v = (l*T).v by A3,FUNCT_4:12;
  dom T = [#]V by Th7;
  hence thesis by A4,FUNCT_1:23;
end;


:: # is a right inverse of @
theorem Th43:
for X being Subset of V,
    l being Linear_Combination of T .: X
  st T|X is one-to-one
  holds T@(T#l) = l
proof
  let X be Subset of V,
      l be Linear_Combination of T .: X such that
  A1: T|X is one-to-one;
  set m = T@(T#l);
    let w be Element of W;
    per cases;
    suppose A2: w in Carrier l; then
      A3: l.w <> 0.F by VECTSP_6:20;
      A4: Carrier l c= T .: X by VECTSP_6:def 7;
      consider v being set such that
        A5: v in dom T and
        A6: v in X and
        A7: w = T.v by A2,A4,FUNCT_1:def 12;
      reconsider v as Element of V by A5;
      consider B being Subset of V such that
        A8: B misses X and
        A9: T"{T.v} = {v} \/ B by A1,A6,Th34;
      A10: dom (T#l) = [#]V by FUNCT_2:169;
      A11: (T#l).v = l.(T.v) by A1,A6,Th42;
      A12: (T#l) .: {v} = Im (T#l,v)
      .= {(T#l).v} by A10,FUNCT_1:117;
      A13: m.w = Sum ((T#l) .: T"{T.v}) by A7,Def5
      .= Sum ({l.(T.v)} \/ ((T#l) .: B)) by A9,A11,A12,RELAT_1:153;
      per cases;
      suppose B = {}; then
        m.w = Sum ({l.(T.v)} \/ {}F) by A13,RELAT_1:149
        .= l.w by A7,RLVECT_2:15;
        hence thesis;
      end;
      suppose A14: B <> {};
        A15: {l.(T.v)} misses {0.F} by A3,A7,ZFMISC_1:17;
        Carrier (T#l) c= X by VECTSP_6:def 7; then
        B misses Carrier (T#l) by A8,XBOOLE_1:63; then
        m.w = Sum ({l.(T.v)} \/ {0.F}) by A13,A14,Th35
        .= Sum ({l.(T.v)}) + Sum ({0.F}) by A15,RLVECT_2:18
        .= l.(T.v) + Sum ({0.F}) by RLVECT_2:15
        .= l.(T.v) + 0.F by RLVECT_2:15
        .= l.w by A7,RLVECT_1:10;
        hence thesis;
      end;
    end;
    suppose A16: not w in Carrier l; then
      A17: l.w = 0.F;
      now
        assume A18: m.w <> 0.F; then
        w in Carrier m; then
        T"{w} meets Carrier (T#l) by Th36; then
        consider v being Element of V such that
          A19: v in T"{w} and
          A20: v in Carrier (T#l) by Th3;
        T.v in {w} by A19,FUNCT_1:def 13; then
        A21: T.v = w by TARSKI:def 1;
        A22: Carrier (T#l) c= X by VECTSP_6:def 7; then
        T|(Carrier (T#l)) is one-to-one by A1,Th2; then
        m.w = (T#l).v by A20,A21,Th37
        .= 0.F by A1,A17,A20,A21,A22,Th42;
        hence contradiction by A18;
      end;
      hence thesis by A16;
    end;
end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: The rank+nullity theorem
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

begin

definition
  let F be Field,
      V,W be finite-dimensional VectSp of F,
      T be linear-transformation of V,W;
  func
    rank(T) -> Nat
  equals
    dim (im T);
  coherence;
  func
    nullity(T) -> Nat
  equals
    dim (ker T);
  coherence;
end;

theorem Th44:
for V,W being finite-dimensional VectSp of F,
    T being linear-transformation of V,W
  holds dim V = rank(T) + nullity(T)
proof
  let V,W be finite-dimensional VectSp of F,
      T be linear-transformation of V,W;
  consider A being finite Basis of ker T;
  reconsider A' = A as Subset of V by Th19;
  consider B being Basis of V such that
  A1: A c= B by VECTSP_9:17;
  reconsider B as finite Subset of V by VECTSP_9:24;
  reconsider X = B \ A' as finite Subset of B by XBOOLE_1:36;
  reconsider X as finite Subset of V;
  A2: X misses A by XBOOLE_1:79;
  A3: B = A \/ X by A1,XBOOLE_1:45;
  reconsider C = T .: X as finite Subset of W;
  reconsider A as finite Basis of ker T;
  reconsider B as finite Basis of V;
  A4: T|X is one-to-one by A1,Th22;
  A5: X c= dom (T|X)
  proof
    dom T = [#]V by Th7;
    hence thesis by RELAT_1:91;
  end;
  A6: card C = card X
  proof
    X,(T|X) .: X are_equipotent by A4,A5,CARD_1:60; then
    X,C are_equipotent by RELAT_1:162;
    hence thesis by CARD_1:21;
  end;
  A7: C is linearly-independent
  proof
    assume A8: C is linearly-dependent;
    consider l being Linear_Combination of C such that
    A9: Carrier l <> {} and
    A10: Sum l = 0.W by A8,Th41;
    A11: ex m being Linear_Combination of X st l = T@m
    proof
      reconsider l' = l as Linear_Combination of T .: X;
      set m = T#(l');
      take m;
      thus thesis by A4,Th43;
    end;
    consider m being Linear_Combination of B \ A' such that
    A12: l = T@m by A11;
    T.(Sum m) = 0.W by A1,A10,A12,Th40; then
    Sum m in ker T by Th10; then
    Sum m in Lin A by VECTSP_7:def 3; then
    Sum m in Lin A' by VECTSP_9:21; then
    consider n being Linear_Combination of A' such that
    A13: Sum m = Sum n by VECTSP_7:12;
    (Sum m) - (Sum n) = 0.V by A13,VECTSP_1:66; then
    A14: Sum (m - n) = 0.V by VECTSP_6:80;
    A15: Carrier (m - n) c= (Carrier m) \/ (Carrier n) by VECTSP_6:74;
    A16: Carrier m c= B \ A' by VECTSP_6:def 7;
    A17: Carrier n c= A by VECTSP_6:def 7;
    A18: (B \ A') \/ A' = B by A1,XBOOLE_1:45;
    A19: (Carrier m) \/ (Carrier n) c= (B \ A') \/ A by A16,A17,XBOOLE_1:13;
    Carrier (m - n) c= B by A15,A18,A19,XBOOLE_1:1; then
    reconsider o = m - n as Linear_Combination of B by VECTSP_6:def 7;
    B is linearly-independent by VECTSP_7:def 3; then
    A20: Carrier o = {} by A14,VECTSP_7:def 1;
    A' misses B \ A' by XBOOLE_1:79; then
    Carrier m misses Carrier n by A16,A17,XBOOLE_1:64; then
    Carrier (m - n) = (Carrier m) \/ (Carrier n) by Th32; then
    Carrier m = {} by A20,XBOOLE_1:15; then
    T .: (Carrier m) = {} by RELAT_1:149; then
    Carrier l c= {} by A12,Th30;
    hence thesis by A9,XBOOLE_1:3;
  end;
  reconsider C as finite Subset of im T by Th12;
  reconsider L = Lin C as strict Subspace of im T;
  for v being Element of im T holds v in L
  proof
    let v be Element of im T;
    A21: v in im T by STRUCT_0:def 5;
    reconsider v' = v as Element of W by VECTSP_4:18;
    consider u being Element of V such that
    A22: T.u = v' by A21,Th13;
    reconsider A' = A as Subset of V by Th19;
    V is_the_direct_sum_of Lin A', Lin (B \ A') by A1,Th33; then
    A23: (Omega).V = (Lin A') + (Lin (B \ A')) by VECTSP_5:def 4;
    A24: u in (Omega).V by STRUCT_0:def 5;
    consider u1, u2 being Element of V such that
    A25: u1 in Lin A' and
    A26: u2 in Lin (B \ A') and
    A27: u = u1 + u2 by A23,A24,VECTSP_5:5;
    A28: T.u = T.u1 + T.u2 by A27,MOD_2:def 5;
    Lin A = ker T by VECTSP_7:def 3; then
    u1 in ker T by A25,VECTSP_9:21; then
    T.u1 = 0.W by Th10; then
    A29: T.u = T.u2 by A28,RLVECT_1:10;
    consider l being Linear_Combination of B \ A' such that
      A30: u2 = Sum l by A26,VECTSP_7:12;
    A31: T@l is Linear_Combination of T .: (Carrier l) by Th29;
    Carrier l c= B \ A' by VECTSP_6:def 7; then
    A32: T .: (Carrier l) c= T .: (B \ A') by RELAT_1:156;
    reconsider C' = C as Subset of W;
    reconsider m = T@l as Linear_Combination of C' by A31,A32,VECTSP_6:25;
    ex m being Linear_Combination of C' st v = Sum m
    proof
      take m;
      thus thesis by A1,A22,A29,A30,Th40;
    end; then
    v in Lin C' by VECTSP_7:12;
    hence thesis by VECTSP_9:21;
  end;
  then A33: Lin C = im T by VECTSP_4:40;
  reconsider C as linearly-independent Subset of im T by A7,VECTSP_9:16;
  reconsider C as finite Basis of im T by A33,VECTSP_7:def 3;
  A34: nullity T = card A by VECTSP_9:def 2;
  A35: rank T = card C by VECTSP_9:def 2;
  dim V = card B by VECTSP_9:def 2
  .= rank T + nullity T by A2,A3,A6,A34,A35,CARD_2:53;
  hence thesis;
end;

theorem
for V,W being finite-dimensional VectSp of F,
    T being linear-transformation of V,W
  st T is one-to-one
  holds dim V = rank T
proof
  let V,W be finite-dimensional VectSp of F,
      T be linear-transformation of V,W such that
  A1: T is one-to-one;
  ker T = (0).V by A1,Th15; then
  A2: nullity(T) = 0 by Th16;
  dim V = rank(T) + nullity(T) by Th44
  .= rank(T) by A2;
  hence thesis;
end;
