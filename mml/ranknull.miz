:: The Rank+Nullity Theorem
::  by Jesse Alama
:: 
:: Received July 31, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies RANKNULL, VECTSP_1, MATRLIN, VECTSP10, VECTSP_9, RLVECT_3,
      RLSUB_1, FUNCT_1, FINSET_1, SUBSET_1, BOOLE, CARD_1, RELAT_1, RLVECT_1,
      RLVECT_2, INCSP_1, RLSUB_2, FINSEQ_1, QC_LANG1, FUNCT_2, TARSKI, ARYTM_1,
      FUNCOP_1, LOPBAN_1, SEQ_1, FINSEQ_4, FUNCT_4, CAT_1, COMPLEX1, TDGROUP;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, MCART_1, DOMAIN_1, RELAT_1,
      RELSET_1, FUNCT_1, ORDINAL1, REAL_1, NAT_1, NUMBERS, PARTFUN1, FUNCOP_1,
      FUNCT_2, FUNCT_4, FRAENKEL, XCMPLX_0, XXREAL_0, CARD_1, ARYTM_2,
      FINSET_1, FINSEQ_1, SEQ_1, FINSEQOP, FINSEQ_2, FINSEQ_3, FINSEQ_4,
      REALSET1, STRUCT_0, GROUP_1, RLSUB_2, RLVECT_1, RLVECT_2, RLVECT_3,
      VECTSP_1, FUNCT_7, VECTSP_2, VECTSP_4, VECTSP_5, VECTSP_6, VECTSP_7,
      FVSUM_1, VECTSP10, MATRLIN, MOD_2, VECTSP_9, LOPBAN_1;
 constructors TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, RELSET_1, FUNCT_1,
      PARTFUN1, FUNCT_2, FRAENKEL, NAT_1, CARD_1, FINSET_1, FINSEQ_1, FINSEQOP,
      MATRLIN, STRUCT_0, XCMPLX_0, HAHNBAN, RLVECT_1, RLSUB_2, VECTSP_1,
      VECTSP_4, VECTSP_6, VECTSP_7, MOD_2, VECTSP_9, REALSET1, RLVECT_2,
      WELLORD2, RLVECT_3, FUNCOP_1, LOPBAN_1, GROUP_1, VECTSP_5, VECTSP10,
      VECTSP_2, SEQ_1, FINSEQ_4, NUMBERS, ARYTM_2, XXREAL_0, NAT_D, DOMAIN_1,
      ORDINAL1, ENUMSET1, FUNCT_7, FINSEQ_3, FINSEQ_2, FVSUM_1, FUNCT_3,
      BINOP_1, FUNCT_4, LATTICES, CARD_2, XREAL_0;
 registrations ZFMISC_1, RELAT_1, RELSET_1, FUNCT_1, PARTFUN1, FUNCT_2,
      STRUCT_0, CARD_1, FINSET_1, FINSEQ_1, REALSET1, XCMPLX_0, FRAENKEL,
      VECTSP_4, VECTSP_9, SUBSET_1, XBOOLE_0, VECTSP_1, RLVECT_1, RLSUB_2,
      VECTSP_7, MOD_2, RLVECT_2, MATRLIN, RLVECT_3, FUNCOP_1, LOPBAN_1,
      GROUP_1, VECTSP_5, VECTSP10, VECTSP_2, SEQ_1, FINSEQ_4, NUMBERS, ARYTM_2,
      ORDINAL1, XREAL_0, ORDINAL2, XXREAL_0, FUNCT_7, FINSEQ_2, FVSUM_1,
      FUNCT_4, NAT_1, ARYTM_3;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM;
 definitions TARSKI, ZFMISC_1, FRAENKEL, HAHNBAN, RELAT_1, FUNCT_1, PARTFUN1,
      REALSET1, FINSET_1, FINSEQ_1, FINSEQOP, CARD_1, VECTSP_4, VECTSP_6,
      VECTSP_7, SUBSET_1, XBOOLE_0, VECTSP_1, RLSUB_2, RLVECT_1, STRUCT_0,
      RLVECT_2, MOD_2, MATRLIN, RLVECT_3, FUNCOP_1, LOPBAN_1, FINSEQ_4,
      NUMBERS, FINSEQ_2, FUNCT_4, RELSET_1, CARD_2, FUNCT_2;
 theorems TARSKI, ZFMISC_1, RELAT_1, FINSET_1, FINSEQ_1, FUNCT_1, VECTSP_7,
      VECTSP_9, CARD_2, XBOOLE_1, FUNCT_2, SUBSET_1, XBOOLE_0, VECTSP_1,
      RLVECT_1, VECTSP_4, VECTSP_6, STRUCT_0, RLVECT_2, MOD_2, MATRLIN, CARD_1,
      FUNCOP_1, VECTSP_5, FINSEQ_4, FUNCT_7, FINSEQ_2, FUNCT_4, ENUMSET1;
 schemes FUNCT_1;

begin

theorem Th31:
for f,g being Function
  st g is one-to-one & f|(rng g) is one-to-one & rng g c= dom f
  holds f*g is one-to-one
proof
  let f,g be Function such that
    A1: g is one-to-one and
    A2: f|(rng g) is one-to-one and
    A3: rng g c= dom f;
  set h = f*g;
  A4: dom h = dom g
  proof
    thus dom h c= dom g
    proof
      let x be set such that A5: x in dom h;
      thus thesis by FUNCT_1:21,A5;
    end;
    thus dom g c= dom h
    proof
      let x be set such that A6: x in dom g;
      g.x in rng g by FUNCT_1:12,A6;
      hence thesis by A6,A3,FUNCT_1:21;
    end;
  end;
  for x1,x2 being set st x1 in dom h & x2 in dom h & h.x1 = h.x2
        holds x1 = x2
  proof
    let x1,x2 be set such that
      A6: x1 in dom h and
      A7: x2 in dom h and
      A8: h.x1 = h.x2;
    A9: h.x1 = f.(g.x1) by FUNCT_1:22,A6;
    A10: h.x2 = f.(g.x2) by FUNCT_1:22,A7;
    A13: g.x1 in rng g by FUNCT_1:12,A6,A4;
    A14: g.x2 in rng g by A7,A4,FUNCT_1:12;
    A15: f.(g.x1) = (f|(rng g)).(g.x1) by FUNCT_1:72,A13;
    A16: f.(g.x2) = (f|(rng g)).(g.x2) by FUNCT_1:72,A14;
    dom (f|(rng g)) = rng g by A3,RELAT_1:91; then
    A19: g.x1 in dom (f|(rng g)) by FUNCT_1:12,A6,A4;
    A20: g.x2 in dom (f|(rng g)) by A3,RELAT_1:91,A14;
    g.x1 = g.x2 by A15,A16,A9,A10,A8,A19,A20,FUNCT_1:def 8,A2;
    hence thesis by A6,A4,A7,FUNCT_1:def 8,A1;
  end;
  hence thesis by FUNCT_1:def 8;
end;

:: If a function is one-to-one on a set X, then it is one-to-one on
:: any subset of X.
theorem Th40:
for f being Function,
    X,Y being set
  st X c= Y & f|Y is one-to-one
  holds f|X is one-to-one
proof
  let f be Function,
      X,Y be set such that
  A1: X c= Y and
  A2: f|Y is one-to-one;
  f|X = (f|Y)|X by RELAT_1:103,A1;
  hence thesis by A2,FUNCT_1:84;
end;

theorem Th42:
for V being 1-sorted, X,Y being Subset of V
  holds X meets Y iff ex v being Element of V st v in X & v in Y
proof
  let V be 1-sorted, X,Y be Subset of V;
  X meets Y implies ex v being Element of V st v in X & v in Y
  proof
    assume B1: X meets Y;
    consider z being set such that
    B2: z in X and
    B3: z in Y by B1,XBOOLE_0:3;
    reconsider v = z as Element of V by B2;
    take v;
    thus thesis by B2,B3;
  end;
  hence thesis by XBOOLE_0:3;
end;

reserve F for Field,
        V,W for VectSp of F;

registration
  let F be Field,
      V be finite-dimensional VectSp of F;
  cluster finite Basis of V;
  existence
  proof
    consider A being finite Subset of V such that A1: A is Basis of V
      by MATRLIN:def 3;
    reconsider A as Basis of V by A1;
    take A;
    thus thesis;
  end;
end;

registration let F be Field,
                 V,W be VectSp of F;
  cluster linear Function of V,W;
  existence
  proof
    set f = FuncZero ([#]V,W);
    reconsider f as Function of V,W;
    A1: f is linear
    proof
      thus for x,y being Vector of V holds f.(x+y) = (f.x)+(f.y)
      proof
        let x,y be Vector of V;
        A2: f.(x+y) = 0.W by FUNCOP_1:13;
        A3: f.x = 0.W by FUNCOP_1:13;
        f.y = 0.W by FUNCOP_1:13;
        hence thesis by RLVECT_1:def 7,A2,A3;
      end;
      thus for a being Element of F,
               x being Element of V
             holds f.(a*x) = a*(f.x)
      proof
        let a be Element of F,
            x be Element of V;
        A6: f.(a*x) = 0.W by FUNCOP_1:13;
        f.x = 0.W by FUNCOP_1:13;
        hence thesis by A6,VECTSP_1:59;
      end;
    end;
    take f;
    thus thesis by A1;
  end;
end;

theorem Th49:
[#]V is finite implies V is finite-dimensional
proof
  assume A1: [#]V is finite;
  consider B being Basis of V;
  reconsider B as finite Subset of V by A1,FINSET_1:13;
  take B;
  thus thesis;
end;

theorem
for V being finite-dimensional VectSp of F
  st Card ([#]V) = 1
  holds dim V = 0
proof
  let V be finite-dimensional VectSp of F such that
  A1: Card ([#]V) = 1;
  A2: [#]V = {0.V}
  proof
    consider y being set such that
    B2: [#]V = {y} by A1,CARD_2:60;
    thus thesis by B2,TARSKI:def 1;
  end;
  (Omega).V = (0).V by A2,VECTSP_4:def 3;
  hence thesis by VECTSP_9:33;
end;

theorem
Card ([#]V) = 2 implies dim V = 1
proof
  assume A1: Card ([#]V) = 2;
  A3: [#]V,Card([#]V) are_equipotent by CARD_1:def 5; then
  A4: [#]V is finite by CARD_1:68,A1;
  reconsider C = [#]V as finite set by A3,CARD_1:68,A1;
  A5: Card ([#]V) = card (C);
  reconsider V as finite-dimensional VectSp of F by A4,Th49;
  ex v being Vector of V st v <> 0.V & (Omega).V = Lin ({v})
  proof
    consider x,y being set such that
    B1: x <> y and
    B2: [#]V = {x,y} by A1,A5,CARD_2:79;
    per cases by B2,TARSKI:def 2;
    suppose B4: x = 0.V;
      reconsider y as Element of V by B2,TARSKI:def 2;
      reconsider x as Element of V by B4;
      set L = Lin ({y});
      C4: for v being Element of V holds v in (Omega).V iff v in L
      proof
        let v be Element of V;
        v in (Omega).V implies v in L
        proof
          assume v in (Omega).V;
          E31: y in {y} by TARSKI:def 1;
          E41: 0.L in L by STRUCT_0:def 5;
          per cases by B2,TARSKI:def 2;
          suppose v = x;
            hence thesis by B4,VECTSP_4:def 2,E41;
          end;
          suppose v = y;
            hence thesis by VECTSP_7:13,E31;
          end;
        end;
        hence thesis by STRUCT_0:def 5;
      end;
      take y;
      thus thesis by C4,VECTSP_4:38,B4,B1;
    end;
    suppose B5: y = 0.V; then
      reconsider y as Element of V;
      reconsider x as Element of V by B2,TARSKI:def 2;
      set L = Lin ({x});
      C4: for v being Element of V holds v in (Omega).V iff v in L
      proof
        let v be Element of V;
        v in (Omega).V implies v in L
        proof
          assume v in (Omega).V;
          E31: x in {x} by TARSKI:def 1;
          E41: 0.L in L by STRUCT_0:def 5;
          per cases by B2,TARSKI:def 2;
          suppose v = y;
            hence thesis by B5,VECTSP_4:def 2,E41;
          end;
          suppose v = x;
            hence thesis by VECTSP_7:13,E31;
          end;
        end;
        hence thesis by STRUCT_0:def 5;
      end;
      take x;
      thus thesis by C4,B5,B1,VECTSP_4:38;
    end;
  end;
  hence thesis by VECTSP_9:34;
end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Basic facts of linear transformations
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

begin

definition
  let F be Field,
      V,W be VectSp of F;
  mode
    linear-transformation of V,W
  is
    linear Function of V,W;
end;

reserve T for linear-transformation of V,W;

theorem Th3:
for V, W being non empty 1-sorted, T being Function of V,W holds
dom T = [#]V & rng T c= [#]W
proof
  let V, W be non empty 1-sorted, T be Function of V,W;
  T is Element of Funcs([#]V,[#]W) by FUNCT_2:11;
  hence thesis by FUNCT_2:169;
end;

theorem Th4:
for x,y being Element of V
  holds T.x - T.y = T.(x - y)
proof
  let x,y be Element of V;
  A2: T.(x - y) = T.x + T.(-y) by MOD_2:def 5;
  A3: -y = (-1_F)*y by VECTSP_1:59;
  T.((-1_F)*y) = (-1_F)*(T.y) by MOD_2:def 5;
  hence thesis by A2,A3,VECTSP_1:59;
end;

theorem Th11:
T.(0.V) = 0.W
proof
  0.V = (0.F)*(0.V) by VECTSP_1:59;
  then T.(0.V) = (0.F)*T.(0.V) by MOD_2:def 5
  .= 0.W by VECTSP_1:59;
  hence thesis;
end;

definition
  let F be Field,
      V,W be VectSp of F,
      T be linear-transformation of V,W;
  func
    ker T -> strict Subspace of V
  means :Def1:
    [#]it = { u where u is Element of V : T.u = 0.W };
  existence
  proof
    set K = { u where u is Element of V : T.u = 0.W };
    K c= [#]V
    proof
      let x be set such that A1: x in K;
      consider u being Element of V such that
      A2: u = x and
      T.u = 0.W by A1;
      thus thesis by A2;
    end;
    then reconsider K as Subset of V;
    A0: for v being Element of V st v in K holds T.v = 0.W
    proof
      let v be Element of V such that A1: v in K;
      consider u being Element of V such that
        A2: u = v and
        A3: T.u = 0.W by A1;
      thus thesis by A2,A3;
    end;
    K <> {} & K is linearly-closed
    proof
      T.(0.V) = 0.W by Th11;
      then 0.V in K;
      hence K <> {};
      thus K is linearly-closed
      proof
        A1: now
          let u,v be Element of V such that
          A1: u in K and
          A2: v in K;
          A3: T.u = 0.W by A1,A0;
          T.v = 0.W by A2,A0; then
          T.(u+v) = 0.W + 0.W by MOD_2:def 5,A3
          .= 0.W by RLVECT_1:def 7;
          hence u+v in K;
        end;
        now
          let u be Element of V,
              a be Element of F such that
          A1: u in K;
          T.u = 0.W by A1,A0; then
          T.(a*u) = a*(0.W) by MOD_2:def 5
          .= 0.W by VECTSP_1:59;
          hence a*u in K;
        end;
        then for a being Element of F, u being Element of V st u in K
               holds a*u in K;
        hence thesis by A1,VECTSP_4:def 1;
      end;
    end;
    then consider W being strict Subspace of V such that
    A1: K = the carrier of W by VECTSP_4:42;
    take W;
    thus thesis by A1;
  end;
  uniqueness by VECTSP_4:37;
end;

theorem Th12:
for x being Element of V
  holds x in ker T iff T.x = 0.W
proof
  let x be Element of V;
  thus x in ker T implies T.x = 0.W
  proof
    assume x in ker T; then
    A2: x in [#]ker T by STRUCT_0:def 5;
    [#]ker T = { u where u is Element of V : T.u = 0.W } by Def1; then
    consider u being Element of V such that
    A4: u = x and
    A5: T.u = 0.W by A2;
    thus thesis by A4,A5;
  end;
  assume T.x = 0.W;
  then x in { u where u is Element of V : T.u = 0.W };
  then x in [#]ker T by Def1;
  hence thesis by STRUCT_0:def 5;
end;

definition
  let V,W be non empty 1-sorted,
      T be Function of V,W,
      X be Subset of V;
  redefine func T .: X -> Subset of W;
  coherence
  proof
    A1: rng T c= [#]W by Th3;
    T .: X c= rng T by RELAT_1:144;
    hence thesis by A1,XBOOLE_1:1;
  end;
end;

definition
  let F be Field,
      V,W be VectSp of F,
      T be linear-transformation of V,W;
  func
    im T -> strict Subspace of W
  means :Def2:
    [#]it = T .: [#]V;
  existence
  proof
    reconsider U = T .: [#]V as Subset of W;
    A0: for u being Element of W holds
          u in U iff ex v being Element of V st T.v = u
    proof
      let u be Element of W;
      thus u in U implies ex v being Element of V st T.v = u
      proof
        assume A1: u in U;
        consider x being set such that
          x in dom T and
          A3: x in [#]V and
          A4: u = T.x by FUNCT_1:def 12,A1;
        reconsider x as Element of V by A3;
        take x;
        thus thesis by A4;
      end;
      thus (ex v being Element of V st T.v = u) implies u in U
      proof
        given v being Element of V such that
        A2: T.v = u;
        A3: v in [#]V;
        v in dom T by Th3,A3;
        hence thesis by A2,FUNCT_1:def 12;
      end;
    end;      
    U <> {} & U is linearly-closed
    proof
      thus U <> {}
      proof
        T.(0.V) = 0.W by Th11;
        hence thesis by A0;
      end;
      thus U is linearly-closed
      proof
A1:     now
          let u,v be Element of W such that
          A1: u in U and
          A2: v in U;
          consider x being Element of V such that
          A3: T.x = u by A0,A1;
          consider y being Element of V such that
          A4: T.y = v by A0,A2;
          u+v = T.(x+y) by A3,A4,MOD_2:def 5;
          hence u+v in U by A0;
        end;
        now
          let a be Element of F,
              w be Element of W such that
            A1: w in U;
          consider v being Element of V such that
            A2: T.v = w by A0,A1;
          T.(a*v) = a*w by A2,MOD_2:def 5;
          hence a*w in U by A0;
        end;
        hence thesis by A1,VECTSP_4:def 1;
      end;
    end;
    then consider A being strict Subspace of W such that
    A1: U = the carrier of A by VECTSP_4:42;
    take A;
    thus thesis by A1;
  end;
  uniqueness by VECTSP_4:37;
end;

theorem
0.V in ker T
proof
  T.(0.V) = 0.W
  proof
    0.V = (0.F)*(0.V) by VECTSP_1:59;
    then T.(0.V) = (0.F)*T.(0.V) by MOD_2:def 5
    .= 0.W by VECTSP_1:59;
    hence thesis;
  end;
  hence thesis by Th12;
end;

theorem Th15:
for X being Subset of V
  holds T .: X is Subset of im T
proof
  let X be Subset of V;
  set S = T .: X;
  [#](im T) = T .: [#]V by Def2;
  hence thesis by RELAT_1:156;
end;
  
theorem Th16:
for y being Element of W
  holds y in im T iff ex x being Element of V st y = T.x
proof
  let y be Element of W;
  B1: y in im T implies ex x being Element of V st y = T.x
  proof
    assume A0: y in im T;
    reconsider y as Element of im T by A0,STRUCT_0:def 5;
    A2: [#](im T) = T .: [#]V by Def2;
    consider x being set such that
      x in dom T and
      A5: x in [#]V and
      A6: y = T.x by A2,FUNCT_1:def 12;
    reconsider x as Element of V by A5;
    take x;
    thus thesis by A6;
  end;
  (ex x being Element of V st y = T.x) implies y in im T
  proof
    assume A0: ex x being Element of V st y = T.x;
    consider x being Element of V such that
    A1: y = T.x by A0;
    dom T = [#]V by Th3; then
    y in T .: [#]V by A1,FUNCT_1:def 12; then
    y in [#](im T) by Def2;
    hence thesis by STRUCT_0:def 5;
  end;
  hence thesis by B1;
end;

theorem
for x being Element of ker T
  holds T.x = 0.W
proof
  let x be Element of ker T;
  reconsider y = x as Element of V by VECTSP_4:18;
  y in ker T by STRUCT_0:def 5;
  hence thesis by Th12;
end;

theorem Th46:
T is one-to-one implies ker T = (0).V
proof
  assume A1: T is one-to-one;
  reconsider Z = (0).V as Subspace of ker T by VECTSP_4:50;
  for v being Element of ker T holds v in Z
  proof
    let v be Element of ker T;
    assume B0: not v in Z;
    B1: T.(0.V) = 0.W by Th11;
    B2: not v = 0.V by B0,VECTSP_4:46;
    B20: v in ker T by STRUCT_0:def 5;
    reconsider v as Element of V by VECTSP_4:18;
    B3: T.v = 0.W by Th12,B20;
    dom T = [#]V by Th3;
    hence thesis by B2,FUNCT_1:def 8,A1,B1,B3;
  end;
  hence thesis by VECTSP_4:40;
end;

theorem Th47:
for V being finite-dimensional VectSp of F
  holds dim ((0).V) = 0
proof
  let V be finite-dimensional VectSp of F;
  (Omega).((0).V) = (0).((0).V) by VECTSP_4:47;
  hence thesis by VECTSP_9:33;
end;

theorem Th29:
for x,y being Element of V
  st T.x = T.y
  holds x - y in ker T
proof
  let x,y be Element of V such that
    A1: T.x = T.y;
  T.(x - y) = T.x - T.y by Th4
  .= 0.W by A1,VECTSP_1:66;
  hence thesis by Th12;
end;

theorem Th10:
for A being Subset of V,
    x,y being Element of V
  st x - y in Lin A
  holds x in Lin (A \/ {y})
proof
  let A be Subset of V,
      x,y be Element of V such that
  A1: x - y in Lin A;
  A10: y in {y} by TARSKI:def 1;
  A2: y in Lin ({y}) by VECTSP_7:13,A10;
  A3: (x - y) + y = x - (y - y) by RLVECT_1:43
  .= x - 0.V by VECTSP_1:66
  .= x by RLVECT_1:26;
  Lin (A \/ {y}) = (Lin A) + (Lin {y}) by VECTSP_7:20;
  hence thesis by VECTSP_5:5,A1,A2,A3;
end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: Some basic facts about linearly independent subsets and linear
:: combinations
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

begin

theorem Th5:
for X being Subset of V
  st V is Subspace of W
  holds X is Subset of W
proof
  let X be Subset of V;
  assume A1: V is Subspace of W;
  A3: [#]V c= [#]W by A1,VECTSP_4:def 2;
  X c= [#]W
  proof
    let x be set such that A4: x in X;
    x in [#]V by A4;
    hence thesis by A3;
  end;
  hence thesis;
end;

:: A linearly independent set is a basis of its linear span.
theorem Th6:
for A being Subset of V
  st A is linearly-independent
  holds A is Basis of Lin A
proof
  let A be Subset of V such that
  A1: A is linearly-independent;
  A c= [#](Lin A)
  proof
    let x be set such that A11: x in A;
    reconsider x as Element of V by A11;
    x in Lin A by A11,VECTSP_7:13;
    hence thesis by STRUCT_0:def 5;
  end;
  then reconsider B = A as Subset of Lin A;
  A2: B is linearly-independent by A1,VECTSP_9:16;
  Lin B = Lin A by VECTSP_9:21;
  hence thesis by A2,VECTSP_7:def 3;
end;

:: Adjoining an element x to A that is already in its linear span
:: results in a linearly dependent set.
theorem Th8:
for A being Subset of V,
    x being Element of V
  st x in Lin A & not x in A
  holds A \/ {x} is linearly-dependent
proof
  let A be Subset of V,
      x be Element of V such that
    A1: x in Lin A and
    A3: not x in A;
  per cases;
  suppose A is linearly-independent; then
    reconsider A' = A as Basis of Lin A by Th6;
    x in [#](Lin A) by A1,STRUCT_0:def 5;
    then reconsider X = {x} as Subset of Lin A by SUBSET_1:63;
    A6: X misses A'
    proof
      assume X meets A';
      then consider y being set such that
      A60: y in X and
      A61: y in A' by XBOOLE_0:3;
      thus contradiction by A60,A61,A3,TARSKI:def 1;
    end;
    reconsider B = A' \/ X as Subset of Lin A;
    A7: B is linearly-dependent by A6,VECTSP_9:19;
    reconsider C = B as Subset of V by Th5;
    thus thesis by A7,VECTSP_9:16;
  end;
  suppose A5: A is linearly-dependent;
    A6: A c= A \/ {x} by XBOOLE_1:7;
    reconsider B = A \/ {x} as Subset of V;
    thus thesis by A5,A6,VECTSP_7:2;
  end;
end;

theorem Th30:
for A being Subset of V,
    B being Basis of V
  st A is Basis of ker T & A c= B
  holds T|(B \ A) is one-to-one
proof
  let A be Subset of V,
      B be Basis of V such that
    A1: A is Basis of ker T and
    A2: A c= B;
  set f = T|(B \ A);  
  let x1,x2 be set such that
    A5: x1 in dom f and
    A6: x2 in dom f and
    A7: f.x1 = f.x2 and
    A8: x1 <> x2;
  A80: dom T = [#]V by Th3;
  reconsider x1 as Element of V by A5;
  reconsider x2 as Element of V by A6;
  reconsider A' = A as Subset of V;
  A83: x1 in B \ A by RELAT_1:91,A80,A5;
  A84: x2 in B \ A by RELAT_1:91,A80,A6;
  A9: f.x1 = T.x1 by A83,FUNCT_1:72;
  A10: f.x2 = T.x2 by A84,FUNCT_1:72;
  A11: x1 - x2 in ker T by A7,A9,A10,Th29;
  reconsider A as Basis of ker T by A1;
  ker T = Lin A by VECTSP_7:def 3; then
  x1 - x2 in Lin A' by VECTSP_9:21,A11; then
  A15: x1 in Lin (A' \/ {x2}) by Th10;
  A16: (A' \/ {x2}) \/ {x1} = A' \/ {x1,x2}
  proof
    {x2} \/ {x1} = {x1,x2} by ENUMSET1:41;
    hence thesis by XBOOLE_1:4;
  end;
  A159: not x1 in (A' \/ {x2})
  proof
    assume B1: x1 in A' \/ {x2};
    per cases by B1,XBOOLE_0:def 2;
    suppose x1 in A';
      hence contradiction by A83,XBOOLE_0:def 4;
    end;
    suppose x1 in {x2};
      hence contradiction by A8,TARSKI:def 1;
    end;
  end;
  A160: (A' \/ {x2}) \/ {x1} is linearly-dependent by Th8,A15,A159;
  A18: A' \/ {x1,x2} c= B
  proof
    {x1,x2} c= B
    proof
      let z be set such that
        B3: z in {x1,x2};
      per cases by TARSKI:def 2,B3;
      suppose z = x1;
        hence thesis by A83,XBOOLE_0:def 4;
      end;
      suppose z = x2;
        hence thesis by A84,XBOOLE_0:def 4;
      end;
    end;
    hence thesis by A2,XBOOLE_1:8;
  end;
  B is linearly-independent by VECTSP_7:def 3;
  hence thesis by A16,A160,A18,VECTSP_7:2;
end;

theorem
for A being Subset of V,
    l being Linear_Combination of A,
    x being Element of V,
    a being Element of F
  holds l +* (x,a) is Linear_Combination of A \/ {x}
proof
  let A be Subset of V,
      l be Linear_Combination of A,
      x be Element of V,
      a be Element of F;
  set m = l +* (x,a);
  A1: m is Element of Funcs ([#]V,[#]F)
  proof
    A2: dom m = [#]V
    proof
      A3: dom l = [#]V by FUNCT_2:169;
      A6: m = l +* (x .--> a) by FUNCT_7:def 3,A3;
      A7: dom (x .--> a) = {x} by FUNCOP_1:19;
      dom m = (dom l) \/ (dom (x .--> a)) by FUNCT_4:def 1,A6;
      hence thesis by A7,A3,XBOOLE_1:12;
    end;
    rng m c= [#]F
    proof
      let y be set such that A4: y in rng m;
      consider x' being set such that
        A5: x' in dom m and
        A6: m.x' = y by A4,FUNCT_1:def 5;
      A81: x' in dom l by A5,A2,FUNCT_2:169;
      per cases;
      suppose A7: x' = x;
        m.x' = a by A7,A81,FUNCT_7:33;
        hence thesis by A6;
      end;
      suppose x' <> x;
        then A11: m.x' = l.x' by FUNCT_7:34;
        A12: l.x' in rng l by A81,FUNCT_1:12;
        rng l c= [#]F by FUNCT_2:169;
        hence thesis by A11,A6,A12;
      end;
    end;
    hence thesis by A2,FUNCT_2:def 2;
  end;
  reconsider m as Element of Funcs ([#]V,[#]F) by A1;
  set T = Carrier l \/ {x};
  for v being Element of V st not v in T holds m.v = 0.F
  proof
    let v be Element of V such that
    A3: not v in T;
    A4: not v in Carrier l by A3,XBOOLE_0:def 2;
    A5: not v in {x} by A3,XBOOLE_0:def 2;
    A6: v <> x by A5,TARSKI:def 1;
    m.v = l.v by A6,FUNCT_7:34;
    hence thesis by A4;
  end;
  then reconsider m as Linear_Combination of V by VECTSP_6:def 4;
  A3: Carrier m c= T
  proof
    let y be set such that A4: y in Carrier m;
    consider z being Element of V such that
      A5: y = z and
      A6: m.z <> 0.F by A4;
    per cases;
    suppose A7: z = x;
      A8: x in {x} by TARSKI:def 1;
      {x} c= T by XBOOLE_1:7;
      hence thesis by A7,A8,A5;
    end;
    suppose A11: z <> x;
      A12: m.z = l.z by FUNCT_7:34,A11;
      A14: z in Carrier l by A12,A6;
      Carrier l c= T by XBOOLE_1:7;
      hence thesis by A14,A5;
    end;
  end;
  A5: T c= A \/ {x}
  proof
    Carrier l c= A by VECTSP_6:def 7;
    hence thesis by XBOOLE_1:9;
  end;
  Carrier m c= A \/ {x} by A3,A5,XBOOLE_1:1;
  hence thesis by VECTSP_6:def 7;
end;

definition
  let V be 1-sorted,
      X be Subset of V;
  func
    V \ X -> Subset of V
  equals
    [#]V \ X;
  coherence;
end;

definition
  let F be Field,
      V be VectSp of F,
      l be Linear_Combination of V,
      X be Subset of V;
  redefine func l .: X -> Subset of F;
  coherence
  proof
    l .: X c= [#]F
    proof
      let y be set such that
        A1: y in l .: X;
      consider x being set such that
        A2: x in dom l and
        x in X and
        A4: y = l.x by A1,FUNCT_1:def 12;
      A5: rng l c= [#]F by FUNCT_2:169;
      y in rng l by A2,A4,FUNCT_1:12;
      hence thesis by A5;
    end;
    hence thesis;
  end;
end;

reserve l for Linear_Combination of V;

registration
  let F be Field,
      V be VectSp of F;
  cluster linearly-dependent Subset of V;
  existence
  proof
    reconsider S = {0.V} as Subset of V;
    A0: 0.V in S by TARSKI:def 1;
    take S;
    thus thesis by A0,VECTSP_7:3;
  end;
end;

:: Restricting a linear combination to a given set
definition
  let F be Field,
      V be VectSp of F,
      l be Linear_Combination of V,
      A be Subset of V;
  func
    l!A -> Linear_Combination of A
  equals
    (l|A) +* ((V \ A) --> 0.F);
  coherence
  proof
    set f = (l|A) +* ((V \ A) --> 0.F);
    per cases;
    suppose A <> [#]V;
      A2: dom f = dom (l|A) \/ dom ((V \ A) --> 0.F) by FUNCT_4:def 1;
      A3: dom l = [#]V by FUNCT_2:169;
      A5: dom (l|A) = A by A3,RELAT_1:91;
      A6: dom ((V \ A) --> 0.F) = V \ A by FUNCOP_1:19;
      A7: A \/ ([#]V \ A) = [#]V by XBOOLE_1:45;
      A1: dom f = [#]V by XBOOLE_1:45,A6,A5,A2;
      A200: rng f c= [#]F
      proof
        let y be set such that A8: y in rng f;
        consider x being set such that
          A9: x in dom f and
          A10: y = f.x by FUNCT_1:def 5,A8;
        reconsider x as Element of V by XBOOLE_1:45,A6,A5,A2,A9;
        per cases by A7,XBOOLE_0:def 2;
        suppose A12: x in A;
          A13: not x in dom ((V \ A) --> 0.F) by A6,XBOOLE_0:def 4,A12;
          A14: f.x = (l|A).x by A13,FUNCT_4:12;
          (l|A).x = l.x by A12,FUNCT_1:72;
          hence thesis by A14,A10;
        end;
        suppose A17: x in V \ A; then
          x in dom ((V \ A) --> 0.F) by FUNCOP_1:19; then
          f.x = ((V \ A) --> 0.F).x by FUNCT_4:14
          .= 0.F by A17,FUNCOP_1:13;
          hence thesis by A10;
        end;
      end;
      reconsider f as Element of Funcs([#]V,[#]F) by A1,A200,FUNCT_2:def 2;
      A3: ex T being finite Subset of V st
            for v being Element of V st not v in T holds f.v = 0.F
      proof
        set C = Carrier l;
        set D = { v where v is Element of V : f.v <> 0.F };
        A8: D is Subset of V
        proof
          D c= [#]V
          proof
            let x be set such that A9: x in D;
            consider v being Element of V such that
              A10: x = v and
              f.v <> 0.F by A9;
            thus thesis by A10;
          end;
          hence thesis;
        end;
        reconsider D as Subset of V by A8;
        A6: D c= C
        proof
          let x be set such that A9: x in D;
          consider v being Element of V such that
            A10: x = v and
            A11: f.v <> 0.F by A9;
          A12: dom ((V \ A) --> 0.F) = V \ A by FUNCOP_1:19;
          A13:now
            assume A13: v in V \ A;
            then f.v = ((V \ A) --> 0.F).v by A1,A2,FUNCT_4:def 1,A12
            .= 0.F by FUNCOP_1:13,A13;
            hence contradiction by A11;
          end; then
          A130: not v in dom ((V \ A) --> 0.F) by FUNCOP_1:19;
          A14: f.v = (l|A).v by FUNCT_4:12,A130;
          A15: v in A by A13,XBOOLE_0:def 4;
          (l|A).v = l.v by A15,FUNCT_1:72;
          hence thesis by A10,A14,A11;
        end;
        reconsider D as finite Subset of V by A6,FINSET_1:13;
        take D;
        thus thesis;
      end;
      reconsider f as Linear_Combination of V by VECTSP_6:def 4,A3;
      Carrier f c= A
      proof
        let x be set such that
          A5: x in Carrier f;
        reconsider x as Element of V by A5;
        A60: f.x <> 0.F by A5,VECTSP_6:20;
        now
          assume not x in A; then
          A71: x in V \ A by XBOOLE_0:def 4; then
          A73: x in dom (l|A) \/ (dom ((V \ A) --> 0.F)) by A6,XBOOLE_0:def 2;
          f.x = ((V \ A) --> 0.F).x by A71,A6,A73,FUNCT_4:def 1;
          hence contradiction by A60,A71,FUNCOP_1:13;
        end;
        hence thesis;
      end;
      hence thesis by VECTSP_6:def 7;
    end;
    suppose A1: A = [#]V; then
      V \ A = {} by XBOOLE_1:37; then
      (V \ A) --> 0.F = {} by ZFMISC_1:113; then
      A4: f = (l|A) by FUNCT_4:22;
      dom l = [#]V by FUNCT_2:169; then
      reconsider f as Linear_Combination of V by A4,RELAT_1:98,A1;
      Carrier f c= A by A1;
      hence thesis by VECTSP_6:def 7;
    end;
  end;
end;

theorem Th18:
l = l!Carrier l
proof
  set f = l|(Carrier l);
  set g = (V \ Carrier l) --> 0.F;
  set m = f +* g;
  A2: dom l = [#]V by FUNCT_2:169;
  A4: dom f = Carrier l by A2,RELAT_1:91;
  A5: dom g = V \ (Carrier l) by FUNCOP_1:19; then
  A7: (dom f) \/ (dom g) = [#]V by XBOOLE_1:45,A4; then
  A1: dom l = dom m by A2,FUNCT_4:def 1;
  for x being set st x in dom l holds l.x = m.x
  proof
    let x be set such that
    A9: x in dom l;
    reconsider x as Element of V by A9;
    per cases;
    suppose A100: x in Carrier l;
      A11: not x in dom g by A5,XBOOLE_0:def 4,A100;
      m.x = f.x by A11,A7,FUNCT_4:def 1;
      hence thesis by A100,FUNCT_1:72;
    end;
    suppose A14: not x in Carrier l; then
      A15: x in V \ (Carrier l) by XBOOLE_0:def 4; then
      A16: m.x = g.x by FUNCT_4:def 1,A5,A7;
      g.x = 0.F by FUNCOP_1:13,A15;
      hence thesis by A14,A16;
    end;
  end;
  hence thesis by A1,FUNCT_1:def 17;
end;

Th19: for X being Subset of V holds l .: X is finite
proof
  let X be Subset of V;
  l = l!(Carrier l) by Th18; then
  A2: rng l c= (rng (l|Carrier l)) \/ rng ((V \ (Carrier l)) --> 0.F)
    by FUNCT_4:18;
  A3: rng (l|Carrier l) is finite
  proof
    rng (l|Carrier l) = l .: Carrier l by RELAT_1:148;
    hence thesis;
  end;
  A4: rng ((V \ (Carrier l)) --> 0.F) c= {0.F}
  proof
    set f = ((V \ (Carrier l)) --> 0.F);
    per cases;
    suppose V \ (Carrier l) = {};
      then f = {} by ZFMISC_1:113;
      hence thesis by XBOOLE_1:2,RELAT_1:60;
    end;
    suppose V \ (Carrier l) <> {};
      hence thesis by FUNCOP_1:14;
    end;
  end;    
  rng ((V \ (Carrier l)) --> 0.F) is finite by A4,FINSET_1:13; then
  (rng (l|Carrier l)) \/ rng ((V \ (Carrier l)) --> 0.F) is finite
         by A3,FINSET_1:14; then
  A7: rng l is finite by A2,FINSET_1:13;
  l .: X c= rng l by RELAT_1:144;
  hence thesis by A7,FINSET_1:13;
end;

theorem Th23:
for A being Subset of V,
    v being Element of V
  st v in A
  holds (l!A).v = l.v
proof
  let A be Subset of V,
      v be Element of V such that
  A1: v in A;
  A20: not v in V \ A by A1,XBOOLE_0:def 4;
  A3: not v in dom ((V \ A) --> 0.F) by FUNCOP_1:19,A20;
  dom (l!A) = [#]V by FUNCT_2:169; then
  (dom (l|A)) \/ (dom ((V \ A) --> 0.F)) = [#]V by FUNCT_4:def 1; then
  (l!A).v = (l|A).v by A3,FUNCT_4:def 1
  .= l.v by FUNCT_1:72,A1;
  hence thesis;
end;

theorem Th24:
for A being Subset of V,
    v being Element of V
  st not v in A
  holds (l!A).v = 0.F
proof
  let A be Subset of V,
      v be Element of V such that
    A1: not v in A;
  A2: dom ((V \ A) --> 0.F) = V \ A by FUNCOP_1:19;
  A34: dom (l!A) = (dom (l|A)) \/ (dom ((V \ A) --> 0.F)) by FUNCT_4:def 1;
  A35: dom (l!A) = [#]V by FUNCT_2:169;
  A4: v in V \ A by A1,XBOOLE_0:def 4;
  (l!A).v = ((V \ A) --> 0.F).v by FUNCT_4:def 1,A34,A35,A4,A2
  .= 0.F by FUNCOP_1:13,A4;
  hence thesis;
end;

theorem Th25:
for A,B being Subset of V,
    l being Linear_Combination of B
  st A c= B
  holds l = (l!A) + (l!(B\A))
proof
  let A,B be Subset of V,
      l be Linear_Combination of B such that
    A1: A c= B;
    set r = (l!A) + (l!(B\A));
    let v be Element of V;
    A2: (v in B) implies (v in A or v in B \ A)
    proof
      assume A3: v in B;
      B = A \/ (B \ A) by XBOOLE_1:45,A1;
      hence thesis by A3,XBOOLE_0:def 2;
    end;
    per cases by A2;
    suppose A3: v in A;
      A4: not v in B \ A by XBOOLE_0:def 4,A3;
      A5: (l!(B\A)).v = 0.F by A4,Th24;
      A6: (l!A).v = l.v by A3,Th23;
      r.v = l.v + 0.F by A5,A6,VECTSP_6:def 11
      .= l.v by RLVECT_1:10;
      hence l.v = r.v;
    end;
    suppose A8: v in B\A; then
      not v in A by XBOOLE_0:def 4; then
      A11: (l!A).v = 0.F by Th24;
      (l!(B\A)).v = l.v by A8,Th23; then
      r.v = 0.F + l.v by A11,VECTSP_6:def 11
      .= l.v by RLVECT_1:10;
      hence l.v = r.v;
    end;
    suppose A14: not v in B;
      A15: not v in B\A by A14,XBOOLE_0:def 4;
      A16: not v in A by A14,A1;
      A17: (l!A).v = 0.F by A16,Th24;
      A18: (l!(B\A)).v = 0.F by A15,Th24;
      A19: Carrier l c= B by VECTSP_6:def 7;
      A20: not v in Carrier l by A19,A14;
      r.v = 0.F + 0.F by A17,A18,VECTSP_6:def 11
      .= 0.F by RLVECT_1:10;
      hence l.v = r.v by A20;
    end;
end;

registration
  let F be Field,
      V be VectSp of F,
      l be Linear_Combination of V,
      X be Subset of V;
  cluster l .: X -> finite;
  coherence by Th19;
end;

definition
  let V,W be non empty 1-sorted,
      T be Function of V,W,
      X be Subset of W;
  redefine func T"X -> Subset of V;
  coherence
  proof
    dom T = [#]V by Th3;
    hence thesis by RELAT_1:167;
  end;
end;

theorem Th20:
for X being Subset of V
  st X misses Carrier l
  holds l .: X c= {0.F}
proof
  let X be Subset of V such that
A1: X misses Carrier l;
  set M = l .: X;
  let y be set such that A2: y in M;
  consider x being set such that
    A3: x in dom l and
    A4: x in X and
    A5: y = l.x by FUNCT_1:def 12,A2;
  reconsider x as Element of V by A3;
  now
    assume l.x <> 0.F; then
    x in Carrier l; then
    x in (Carrier l) /\ X by A4,XBOOLE_0:def 3;
    hence contradiction by A1,XBOOLE_0:def 7;
  end;
  hence thesis by A5,TARSKI:def 1;
end;

:: The image of a linear combination under a linear transformation:
::
::   T(a1*v1 + a2*v2 + ... + an*vn)
::     =  a1*T(v1) + a2*T(v2) + ... + an*T(vn).
::
:: Linear combinations are represented as functions from the space to
:: the underlying field having finite support, so to define a new
:: linear combination it is enough to say what its values are for the
:: elements of W and to prove that its support is finite.
::
:: The only difficulty is that some values T(vi) and T(vj) may be
:: equal.  In this case, the new linear combination should be the sum
:: of the coefficients ai and aj, i.e., l(vi) and l(vj).
definition
  let F be Field,
      V,W be VectSp of F,
      l be Linear_Combination of V,
      T be linear-transformation of V,W;
  func
    T@l -> Linear_Combination of W
  means :Def5:
    for w being Element of W holds
      it.w = Sum (l .: (T"{w}));
  existence
  proof
    defpred P[set,set] means
      ex w being Element of W st $1 = w & $2 = Sum (l .: (T"{w}));
    A1: for x, y1, y2 being set st x in [#]W & P[x,y1] & P[x,y2] holds y1 = y2;
    A2: for x being set st x in [#]W holds ex y being set st P[x,y]
    proof
      let x be set such that A3: x in [#]W;
      reconsider x as Element of W by A3;
      take Sum (l .: (T"{x}));
      thus thesis;
    end;
    consider f being Function such that
      A3: dom f = [#]W and
      A4: for x being set st x in [#]W holds P[x,f.x]
            from FUNCT_1:sch 2(A1,A2);
   A400: for w being Element of W holds f.w = Sum (l .: (T"{w}))
   proof
     let w be Element of W;
     consider w' being Element of W such that
       A1: w = w' and
       A2: f.w = Sum (l .: (T"{w'})) by A4;
     thus thesis by A1,A2;
   end;
   A5: rng f c= [#]F
   proof
     let y be set such that A6: y in rng f;
     consider x being set such that
       A7: x in dom f and
       A8: f.x = y by FUNCT_1:def 5,A6;
     consider w being Element of W such that
       x = w and
       A12: f.x = Sum (l .: (T"{w})) by A3,A4,A7;
     thus thesis by A8,A12;
   end;
   reconsider f as Element of Funcs([#]W,[#]F) by A3,A5,FUNCT_2:def 2;
   A6: ex T being finite Subset of W
         st for w being Element of W st not w in T holds f.w = 0.F
   proof
     set C = Carrier l;
     reconsider TC = T .: C as Subset of W;
     set X = { w where w is Element of W : f.w <> 0.F };
     A10: X is Subset of W
     proof
       X c= [#]W
       proof
         let x be set such that A11: x in X;
         consider w being Element of W such that
         A12: x = w and
         f.w <> 0.F by A11;
         thus thesis by A12;
       end;
       hence thesis;
     end;
     reconsider X as Subset of W by A10;
     A11: X c= TC
     proof
       let x be set such that A12: x in X;
       consider w being Element of W such that
         A13: x = w and
         A14: f.w <> 0.F by A12;
       A15: T"{w} meets Carrier l
       proof
         assume A3: T"{w} misses Carrier l; then
         A4: l .: T"{w} c= {0.F} by Th20;
         Sum (l .: T"{w}) = 0.F
         proof
           per cases;
           suppose l .: T"{w} = {}F;
             hence thesis by RLVECT_2:14;
           end;
           suppose A6: l .: T"{w} <> {}F;
             l .: T"{w} = {0.F}
             proof
               thus l .: T"{w} c= {0.F} by A3,Th20;
               thus {0.F} c= l .: T"{w}
               proof
                 let y be set such that A100: y in {0.F};
                 A101: y = 0.F by A100,TARSKI:def 1;
                 consider z being set such that A102: z in l .: T"{w}
                   by A6,XBOOLE_0:def 1;
                 thus thesis by A102,A101,A4,TARSKI:def 1;
               end;
             end;
             hence thesis by RLVECT_2:15;
           end;
         end;
         hence contradiction by A14,A400;
       end;
       consider y being set such that
       A16: y in T"{w} and
       A17: y in Carrier l by A15,XBOOLE_0:3;
       reconsider y as Element of V by A17;
       A18: dom T = [#]V by Th3;
       T.y in {w} by A16,FUNCT_1:def 13; then
       T.y = w by TARSKI:def 1;
       hence thesis by A13,A17,A18,FUNCT_1:def 12;
     end;
     reconsider X as finite Subset of W by A11,FINSET_1:13;
     take X;
     thus thesis;
   end;
   reconsider f as Linear_Combination of W by A6,VECTSP_6:def 4;
   A7: for w being Element of W holds f.w = Sum (l .: (T"{w}))
   proof
     let w be Element of W;
     consider w' being Element of W such that
     A10: w = w' and
     A11: f.w = Sum (l .: (T"{w'})) by A4;
     thus thesis by A10,A11;
   end;
   take f;
   thus thesis by A7;
 end;
  uniqueness
  proof
    let f,g be Linear_Combination of W such that
      A1: for w being Element of W holds f.w = Sum (l .: (T"{w})) and
      A2: for w being Element of W holds g.w = Sum (l .: (T"{w}));
    A3: dom f = [#]W by FUNCT_2:169;
    A4: dom g = [#]W by FUNCT_2:169;
    for x being set st x in dom f holds f.x = g.x
    proof
      let x be set such that A6: x in dom f;
      reconsider x as Element of W by A6;
      f.x = Sum (l .: (T"{x})) by A1;
      hence thesis by A2;
    end;
    hence thesis by FUNCT_1:def 17,A3,A4;
  end;
end;

theorem Th21:
T@l is Linear_Combination of T .: (Carrier l)
proof
  Carrier (T@l) c= T .: (Carrier l)
  proof
    let w be set such that A1: w in Carrier (T@l);
    reconsider w as Element of W by A1;
    A2: (T@l).w <> 0.F by A1,VECTSP_6:20;
    now
      assume A3: T"{w} misses Carrier l;
      A4: l .: T"{w} c= {0.F} by A3,Th20;
      Sum (l .: T"{w}) = 0.F
      proof
        per cases;
        suppose l .: T"{w} = {}F;
          hence thesis by RLVECT_2:14;
        end;
        suppose A6: l .: T"{w} <> {}F;
          l .: T"{w} = {0.F}
          proof
            thus l .: T"{w} c= {0.F} by A3,Th20;
            thus {0.F} c= l .: T"{w}
            proof
              let y be set such that A100: y in {0.F};
              A101: y = 0.F by A100,TARSKI:def 1;
              consider z being set such that A102: z in l .: T"{w}
                by A6,XBOOLE_0:def 1;
              thus thesis by A101,A102,A4,TARSKI:def 1;
            end;
          end;
          hence thesis by RLVECT_2:15;
        end;
      end;
      hence contradiction by A2,Def5;
    end;
    then consider x being set such that
    A5: x in T"{w} and
    A11: x in Carrier l by XBOOLE_0:3;
    A6: x in dom T by A5,FUNCT_1:def 13;
    A7: T.x in {w} by A5,FUNCT_1:def 13;
    reconsider x as Element of V by A5;
    T.x = w by A7,TARSKI:def 1;
    hence thesis by A6,FUNCT_1:def 12,A11;
  end;
  hence thesis by VECTSP_6:def 7;
end;

theorem Th22:
Carrier (T@l) c= T .: (Carrier l)
proof
  T@l is Linear_Combination of T .: (Carrier l) by Th21;
  hence thesis by VECTSP_6:def 7;
end;

theorem Th26:
for l,m being Linear_Combination of V
  st (Carrier l) misses (Carrier m)
  holds Carrier (l + m) = (Carrier l) \/ (Carrier m)
proof
  let l,m be Linear_Combination of V such that
    A1: (Carrier l) misses (Carrier m);
  thus Carrier (l+m) c= (Carrier l) \/ (Carrier m) by VECTSP_6:51;
  thus (Carrier l) \/ (Carrier m) c= Carrier (l+m)
  proof
    let v be set such that
      A2: v in (Carrier l) \/ (Carrier m);
    per cases by A2,XBOOLE_0:def 2;
    suppose A3: v in Carrier l; then
      reconsider v as Element of V;
      A4: (l+m).v = (l.v) + (m.v) by VECTSP_6:def 11;
      A5: l.v <> 0.F by VECTSP_6:20,A3;
      not v in Carrier m by XBOOLE_0:5,A1,A2,A3; then
      m.v = 0.F; then
      (l+m).v = l.v by RLVECT_1:10,A4;
      hence thesis by A5;
    end;
    suppose A11: v in Carrier m; then
      reconsider v as Element of V;
      A4: (l+m).v = (l.v) + (m.v) by VECTSP_6:def 11;
      A5: m.v <> 0.F by VECTSP_6:20,A11;
      not v in Carrier l by XBOOLE_0:5,A1,A2,A11; then
      l.v = 0.F; then
      (l+m).v = m.v by RLVECT_1:10,A4;
      hence thesis by A5;
    end;
  end;
end;

theorem Th27:
for l,m being Linear_Combination of V
  st (Carrier l) misses (Carrier m)
  holds Carrier (l - m) = (Carrier l) \/ (Carrier m)
proof
  let l,m be Linear_Combination of V such that
  A1: (Carrier l) misses (Carrier m);
  Carrier (-m) = Carrier m by VECTSP_6:69;
  hence thesis by Th26,A1;
end;

theorem Th28:
for A,B being Subset of V
  st A c= B & B is Basis of V
  holds V is_the_direct_sum_of Lin A, Lin (B \ A)
proof
  let A,B be Subset of V such that
    A1: A c= B and
    A2: B is Basis of V;
  A4: (Omega).V = (Lin A) + (Lin (B \ A))
  proof
    set U = (Lin A) + (Lin (B \ A));
    [#]U = [#]V
    proof
      thus [#]U c= [#]V by VECTSP_4:def 2;
      thus [#]V c= [#]U
      proof
        let v be set such that
        A6: v in [#]V;
        reconsider v as Element of V by A6;
        A60: v in Lin B by A2,VECTSP_9:14;
        consider l being Linear_Combination of B such that
          A7: v = Sum l by A60,VECTSP_7:12;
        set m = l!A;
        set n = l!(B\A);
        A8: l = m + n by Th25,A1;
        A10: ex v1,v2 being Element of V
               st v1 in Lin A & v2 in Lin (B \ A) & v = v1 + v2
        proof
          take Sum m, Sum n;
          thus thesis by VECTSP_6:77,A7,A8,VECTSP_7:12;
        end;
        v in (Lin A) + (Lin (B \ A)) by A10,VECTSP_5:5;
        hence thesis by STRUCT_0:def 5;
      end;
    end;
    hence thesis by VECTSP_4:37;
  end;
  (Lin A) /\ (Lin (B \ A)) = (0).V
  proof
    set U = (Lin A) /\ (Lin (B \ A));
    reconsider W = (0).V as strict Subspace of U by VECTSP_4:50;
    for v being Element of U holds v in W
    proof
      let v be Element of U;
      A60: v in U by STRUCT_0:def 5;
      A6: v in Lin A by VECTSP_5:7,A60;
      A7: v in Lin (B \ A) by VECTSP_5:7,A60;
      consider l being Linear_Combination of A such that
      A8: v = Sum l by A6,VECTSP_7:12;
      consider m being Linear_Combination of B \ A such that
      A9: v = Sum m by A7,VECTSP_7:12;
      A11: 0.V = (Sum l) - (Sum m) by A8,A9,VECTSP_1:66
      .= Sum (l - m) by VECTSP_6:80;
      A12: Carrier (l - m) c= (Carrier l) \/ (Carrier m) by VECTSP_6:74;
      A120: Carrier l c= A by VECTSP_6:def 7;
      A121: Carrier m c= B \ A by VECTSP_6:def 7;
      A122: A \/ (B \ A) = B by XBOOLE_1:45,A1;
      A123: (Carrier l) \/ (Carrier m) c= A \/ (B \ A)
        by A120,A121,XBOOLE_1:13;
      A125: Carrier (l - m) c= B by A123,A122,A12,XBOOLE_1:1;
      reconsider n = l - m as Linear_Combination of B by A125,VECTSP_6:def 7;
      A131: B is linearly-independent by VECTSP_7:def 3,A2;
      A13: Carrier n = {} by A11,A131,VECTSP_7:def 1;
      A140: A misses (B \ A) by XBOOLE_1:79;
      A141: (Carrier l) misses (Carrier m) by A140,A120,A121,XBOOLE_1:64;
      A14: Carrier n = (Carrier l) \/ (Carrier m) by A141,Th27;
      A15: Carrier l = {} by A13,A14,XBOOLE_1:15;
      A17: l = ZeroLC(V) by VECTSP_6:def 6,A15;
      Sum l = 0.V by VECTSP_6:41,A17;
      hence thesis by A8,VECTSP_4:46;
    end;
    hence thesis by VECTSP_4:40;
  end;    
  hence thesis by A4,VECTSP_5:def 4;
end;

theorem Th32:
for A being Subset of V,
    l being Linear_Combination of A,
    v being Element of V
  st T|A is one-to-one & v in A
  holds ex X being Subset of V st
    X misses A & T"{T.v} = {v} \/ X
proof
  let A be Subset of V,
      l be Linear_Combination of A,
      v be Element of V such that
    A1: T|A is one-to-one and
    A2: v in A;
  set X = T"{T.v} \ {v};
  D0: {v} c= T"{T.v}
  proof
    let x be set such that E1: x in {v};
    E2: x = v by E1,TARSKI:def 1;
    E3: dom T = [#]V by Th3;
    T.v in {T.v} by TARSKI:def 1;
    hence thesis by E3,FUNCT_1:def 13,E2;
  end;
  D2: X misses A
  proof
    assume X meets A; then
    consider x being set such that
    E2: x in X and
    E3: x in A by XBOOLE_0:3;
    E4: x in T"{T.v} by E2,XBOOLE_0:def 4;
    not x in {v} by E2,XBOOLE_0:def 4; then
    E6: x <> v by TARSKI:def 1;
    T.x in {T.v} by E4,FUNCT_1:def 13; then
    E9: T.x = T.v by TARSKI:def 1;
    T.x = (T|A).x by E3,FUNCT_1:72; then
    E13: (T|A).v = (T|A).x by FUNCT_1:72,A2,E9;
    dom T = [#]V by Th3; then
    dom (T|A) = A by RELAT_1:91;
    hence thesis by E6,E13,A1,FUNCT_1:def 8,E3,A2;
  end;
  take X;
  thus thesis by D0,XBOOLE_1:45,D2;
end;

theorem Th33:
for X being Subset of V
  st X misses Carrier l & X <> {}
  holds l .: X = {0.F}
proof
  let X be Subset of V such that
    A1: X misses Carrier l and
    A2: X <> {};
  A3: l .: X c= {0.F} by Th20,A1;
  dom l = [#]V by FUNCT_2:169; then
  l .: X <> {} by A2,RELAT_1:152;
  hence thesis by A3,ZFMISC_1:39;
end;

theorem Th41:
for w being Element of W
  st w in Carrier (T@l)
  holds T"{w} meets Carrier l
proof
  let w be Element of W such that
  A1: w in Carrier (T@l);
  A2: (T@l).w <> 0.F by A1,VECTSP_6:20;
  assume A4: T"{w} misses Carrier l;
  per cases;
  suppose T"{w} = {};
    then Sum (l .: T"{w}) = Sum ({}F) by RELAT_1:149
                         .= 0.F by RLVECT_2:14;
    hence thesis by A2,Def5;
  end;
  suppose T"{w} <> {};
    then l .: T"{w} = {0.F} by Th33,A4;
    then Sum (l .: T"{w}) = 0.F by RLVECT_2:15;
    hence thesis by A2,Def5;
  end;
end;

theorem Th34:
for v being Element of V
  st T|(Carrier l) is one-to-one & v in Carrier l
  holds (T@l).(T.v) = l.v
proof
  let v be Element of V such that
  A1: T|(Carrier l) is one-to-one and
  A2: v in Carrier l;
  consider X being Subset of V such that
  C40: X misses Carrier l and
  C41: T"{T.v} = {v} \/ X by A1,A2,Th32;
  per cases;
  suppose c:X = {};
    C51: dom l = [#]V by FUNCT_2:169;
    l .: {v} = Im (l,v)
    .= {l.v} by FUNCT_1:117,C51; then
    Sum (l .: T"{T.v}) = l.v by RLVECT_2:15,c,C41;
    hence thesis by Def5;
  end;
  suppose C100: X <> {};
    D1: l .: T"{T.v} = (l .: {v}) \/ (l .: X) by RELAT_1:153,C41;
    D2: dom l = [#]V by FUNCT_2:169;
    D4: l .: {v} = Im (l,v)
    .= {l.v} by FUNCT_1:117,D2;
    D5: l .: X = {0.F}
    proof
      F1: {0.F} c= l .: X
      proof
        let x be set such that E1: x in {0.F};
        E2: x = 0.F by E1,TARSKI:def 1;
        consider y being set such that E3: y in X by C100,XBOOLE_0:def 1;
        E4:now
          assume y in Carrier l;
          then y in (Carrier l) /\ X by E3,XBOOLE_0:def 3;
          hence contradiction by C40,XBOOLE_0:def 7;
        end;
        reconsider y as Element of V by E3;
        l.y = x by E2,E4;
        hence thesis by E3,FUNCT_1:def 12,D2;
      end;
      l .: X c= {0.F}
      proof
        let y be set such that G1: y in l .: X;
        consider x being set such that
        G2: x in dom l and
        G3: x in X and
        G4: y = l.x by G1,FUNCT_1:def 12;
        G5: now
          assume x in Carrier l;
          then x in (Carrier l) /\ X by G3,XBOOLE_0:def 3;
          hence contradiction by C40,XBOOLE_0:def 7;
        end;
        reconsider x as Element of V by G2;
        l.x = 0.F by G5;
        hence thesis by G4,TARSKI:def 1;
      end;
      hence thesis by F1,XBOOLE_0:def 10;
    end;
    D6: l .: X misses l .: {v}
    proof
      E10: dom l = [#]V by FUNCT_2:169;
      E2: l .: {v} = Im (l,v)
      .= {l.v} by FUNCT_1:117,E10;
      assume l .: X meets l .: {v};
      then consider x being set such that
      E3: x in l .: X and
      E4: x in l .: {v} by XBOOLE_0:3;
      E5: x = 0.F by D5,E3,TARSKI:def 1;
      x = l.v by E2,E4,TARSKI:def 1;
      hence thesis by E5,A2,VECTSP_6:20;
    end;
    Sum (l .: T"{T.v}) = (Sum (l .: {v})) + (Sum (l .: X)) by RLVECT_2:18,D1,D6
    .= l.v + (Sum ({0.F})) by D4,D5,RLVECT_2:15
    .= l.v + 0.F by RLVECT_2:15
    .= l.v by RLVECT_1:10;
    hence thesis by Def5;
  end;
end;

theorem Th35:
for G being FinSequence of V
  st rng G = Carrier l & T|(Carrier l) is one-to-one
  holds T*(l (#) G) = (T@l) (#) (T*G)
proof
  let G be FinSequence of V such that
  A2: rng G = Carrier l and
  A3: T|(Carrier l) is one-to-one;
  reconsider L = T*(l (#) G) as FinSequence of W;
  reconsider R = (T@l) (#) (T*G) as FinSequence of W;
  B1: len L = len (l (#) G) by FINSEQ_2:37
  .= len G by VECTSP_6:def 8;
  B2: len R = len (T*G) by VECTSP_6:def 8
  .= len G by FINSEQ_2:37;
  for k being Element of NAT st 1 <= k & k <= len L holds L.k = R.k
  proof
    let k be Element of NAT such that
      A5: 1 <= k and
      A6: k <= len L;
    A71: len (l (#) G) = len G by VECTSP_6:def 8;
    A8: dom (l (#) G) = Seg len G by FINSEQ_1:def 3,A71;
    A9: k in dom (l (#) G) by A8,A5,A6,B1;
    A112: k in dom G by FINSEQ_1:def 3,A8,A9;
    A113: G.k in rng G by FUNCT_1:12,A112;
    reconsider gk = G/.k as Element of V;
    A12: (l (#) G).k = (l.gk)*gk by A9,VECTSP_6:def 8;
    A120: G.k = G/.k by A112,FINSEQ_4:def 4;
    reconsider Gk = G.k as Element of V by A120;
    A1200: (T*G).k = T.Gk by A112,FUNCT_1:23;
    reconsider TGk = (T*G).k as Element of W by A1200;
    A13: L.k = T.((l.gk)*gk) by A12,A9,FUNCT_1:23
    .= (l.gk)*(T.gk) by MOD_2:def 5
    .= (l.Gk)*TGk by FUNCT_1:23,A120,A112;
    A14: dom R = Seg len G by FINSEQ_1:def 3,B2;
    dom T = [#]V by Th3; then
    dom (T*G) = dom G by A2,RELAT_1:46; then
    A16: (T*G)/.k = (T*G).k by A112,FINSEQ_4:def 4;
    (T@l).((T*G).k) = l.(G.k)
    proof
      (T*G).k = T.(G.k) by A112,FUNCT_1:23;
      hence thesis by Th34,A3,A113,A2;
    end;
    hence thesis by A13,A16,VECTSP_6:def 8,A8,A9,A14;
  end;
  hence thesis by B1,B2,FINSEQ_1:18;
end;

theorem Th36:
T|(Carrier l) is one-to-one
  implies T .: (Carrier l) = Carrier (T@l)
proof
 assume A1: T|(Carrier l) is one-to-one;
  A2: Carrier (T@l) c= T .: (Carrier l) by Th22;
  T .: (Carrier l) c= Carrier (T@l)
  proof
    let w be set such that B1: w in T .: (Carrier l);
    consider v being set such that
      B2: v in dom T and
      B3: v in Carrier l and
      B4: T.v = w by B1,FUNCT_1:def 12;
    reconsider v as Element of V by B2;
    B7: T.v in rng T by B2,FUNCT_1:12;
    reconsider w as Element of W by B4,B7;
    B8: (T@l).(T.v) = l.v by B3,A1,Th34;
    l.v <> 0.F by B3,VECTSP_6:20;
    hence thesis by B8,B4;
  end;
  hence thesis by A2,XBOOLE_0:def 10;
end;

theorem Th38:
for A being Subset of V,
    B being Basis of V,
    l being Linear_Combination of B \ A
  st A is Basis of ker T & A c= B
  holds T.(Sum l) = Sum (T@l)
proof
  let A be Subset of V,
      B be Basis of V,
      l be Linear_Combination of B \ A such that
   A0: A is Basis of ker T and
   A1: A c= B;
  consider G being FinSequence of V such that
    A2: G is one-to-one and
    A3: rng G = Carrier l and
    A4: Sum l = Sum (l (#) G) by VECTSP_6:def 9;
  set H = T*G;
  reconsider H as FinSequence of W;
  A49: T|(B \ A) is one-to-one by A0,A1,Th30;
  A48: Carrier l c= B \ A by VECTSP_6:def 7;
  A47: (T|(B \ A))|(Carrier l) = T|(Carrier l) by A48,RELAT_1:103;
  A50: T|(Carrier l) is one-to-one by A47,FUNCT_1:84,A49;
  A52: dom T = [#]V by Th3;
  A6: H is one-to-one by Th31,A50,A2,A3,A52;
  A7: rng H = T .: (Carrier l) by A3,RELAT_1:160
  .= Carrier (T@l) by Th36,A50;
  A8: T*(l (#) G) = (T@l) (#) H by Th35,A3,A50;
  Sum (T@l) = Sum ((T@l) (#) H) by A6,A7,VECTSP_6:def 9;
  hence thesis by A8,A4,MATRLIN:20;
end;

theorem Th39:
for X being Subset of V
  st X is linearly-dependent
  holds ex l being Linear_Combination of X
          st Carrier l <> {} & Sum l = 0.V
proof
  let X be Subset of V such that
    A1: X is linearly-dependent;
  not (for l being Linear_Combination of X
         st Sum l = 0.V
         holds Carrier l = {}) by VECTSP_7:def 1,A1;
  hence thesis;
end;

:: "Pulling back" a linear combination from the image space of a
:: linear transformation to the base space.
definition
  let F be Field,
      V,W be VectSp of F,
      X be Subset of V,
      T be linear-transformation of V,W,
      l be Linear_Combination of T .: X;
  assume A0: T|X is one-to-one;
  func
    T#l -> Linear_Combination of X
  equals :Def4:
    (l*T) +* ((V \ X) --> 0.F);
  coherence
  proof
    set f = (l*T) +* ((V \ X) --> 0.F);
    dom l = [#]W by FUNCT_2:169; then
    rng T c= dom l by Th3; then
    A4: dom (l*T) = dom T by RELAT_1:46;
    A5: dom ((V \ X) --> 0.F) = [#]V \ X by FUNCOP_1:19;
    A6: dom T = [#]V by Th3;
    [#]V \/ ([#]V \ X) = [#]V by XBOOLE_1:12; then
    A8: dom f = [#]V by A4,A5,A6,FUNCT_4:def 1;
    A9: rng f c= rng (l*T) \/ rng ((V \ X) --> 0.F) by FUNCT_4:18;
    A10: rng (l*T) c= rng l by RELAT_1:45;
    A11: rng ((V \ X) --> 0.F) c= {0.F} by FUNCOP_1:19;
    A13: rng ((V \ X) --> 0.F) c= [#]F by A11,XBOOLE_1:1;
    rng l c= [#]F by FUNCT_2:169; then
    rng (l*T) c= [#]F by A10,XBOOLE_1:1; then
    rng (l*T) \/ rng ((V \ X) --> 0.F) c= [#]F by XBOOLE_1:8,A13; then
    A17: rng f c= [#]F by A9,XBOOLE_1:1;
    reconsider f as Element of Funcs ([#]V,[#]F) by A8,A17,FUNCT_2:def 2;
    ex T being finite Subset of V st
      for v being Element of V st not v in T holds f.v = 0.F
    proof
      set C = { v where v is Element of V : f.v <> 0.F };
      C c= [#]V
      proof
        let x be set such that D1: x in C;
        consider v being Element of V such that
          D2: v = x and
          f.v <> 0.F by D1;
        thus thesis by D2;
      end;
      then reconsider C as Subset of V;
      C2: C is finite
      proof
        D1: Card C <=` Card Carrier l
        proof
          ex g being Function
            st g is one-to-one & dom g = C & rng g c= Carrier l
          proof
            set S = (T"(Carrier l)) /\ X;
            set g = T|S;
            E0: S = C
            proof
              F1: S c= C
              proof
                let x be set such that G1: x in S;
                G2: x in X by G1,XBOOLE_0:def 3;
                G3: x in T"(Carrier l) by G1,XBOOLE_0:def 3; then
                G5: x in dom T by FUNCT_1:def 13;
                G6: T.x in Carrier l by G3,FUNCT_1:def 13;
                reconsider x as Element of V by G1;
                G8: not x in dom ((V \ X) --> 0.F) by G2,A5,XBOOLE_0:def 4;
                G9: f.x = (l*T).x by FUNCT_4:12,G8;
                G10: (l*T).x = l.(T.x) by G5,FUNCT_1:23;
                l.(T.x) <> 0.F by G6,VECTSP_6:20;
                hence thesis by G9,G10;
              end;
              C c= S
              proof
                let x be set such that G1: x in C;
                consider v being Element of V such that
                  G2: v = x and
                  G3: f.v <> 0.F by G1;
                reconsider x as Element of V by G2;  
                G4:now
                  assume not x in X; then
                  H2: x in V \ X by XBOOLE_0:def 4; then
                  x in dom ((V \ X) --> 0.F) by FUNCOP_1:19; then
                  f.x = ((V \ X) --> 0.F).x by FUNCT_4:14;
                  hence contradiction by G2,G3,H2,FUNCOP_1:13;
                end;
                x in T"(Carrier l)
                proof
                  H0: dom T = [#]V by Th3;
                  T.x in Carrier l
                  proof
                    not x in V \ X by G4,XBOOLE_0:def 4; then
                    I2: f.x = (l*T).x by A5,FUNCT_4:12;
                    (l*T).x = l.(T.x) by H0,FUNCT_1:23;
                    hence thesis by G3,G2,I2;
                  end;
                  hence thesis by H0,FUNCT_1:def 13;
                end;
                hence thesis by G4,XBOOLE_0:def 3;
              end;
              hence thesis by F1,XBOOLE_0:def 10;
            end;
            E1: g is one-to-one
            proof
              (T"(Carrier l)) /\ X c= X by XBOOLE_1:17;
              hence thesis by Th40,A0;
            end;
            E10: dom g = S
            proof
              dom T = [#]V by Th3;
              hence thesis by RELAT_1:91;
            end;
            E3: rng g c= Carrier l
            proof
              let y be set such that F1: y in rng g;
              consider x being set such that
                F2: x in dom g and
                F3: y = g.x by F1,FUNCT_1:def 5;
              x in T"(Carrier l) by F2,E10,XBOOLE_0:def 3; then
              T.x in Carrier l by FUNCT_1:def 13;
              hence thesis by F3,FUNCT_1:72,F2,E10;
            end;
            take g;
            thus thesis by E3,E0,E10,E1;
          end;
          hence thesis by CARD_1:26;
        end;
        thus thesis by D1,CARD_2:68;
      end;
      reconsider C as finite Subset of V by C2;
      take C;
      thus thesis;
    end;
    then reconsider f as Linear_Combination of V by VECTSP_6:def 4;
    Carrier f c= X
    proof
      let x be set such that D1: x in Carrier f;
      reconsider x as Element of V by D1;
      now assume not x in X; then
        D6: x in V \ X by XBOOLE_0:def 4; then
        f.x = ((V \ X) --> 0.F).x by FUNCT_4:14,A5
        .= 0.F by FUNCOP_1:13,D6;
        hence contradiction by D1,VECTSP_6:20;
      end;
      hence thesis;
    end;
    hence thesis by VECTSP_6:def 7;
  end;
end;

theorem Th43:
for X being Subset of V,
    l being Linear_Combination of T .: X,
    v being Element of V
  st v in X & T|X is one-to-one
  holds (T#l).v = l.(T.v)
proof
  let X be Subset of V,
      l be Linear_Combination of T .: X,
      v be Element of V such that
  A1: v in X and
  A0: T|X is one-to-one;
  dom ((V \ X) --> 0.F) = V \ X by FUNCOP_1:19; then
  A6: not v in dom ((V \ X) --> 0.F) by XBOOLE_0:def 4,A1;
  T#l = (l*T) +* ((V \ X) --> 0.F) by A0,Def4; then
  A7: (T#l).v = (l*T).v by A6,FUNCT_4:12;
  dom T = [#]V by Th3;
  hence thesis by A7,FUNCT_1:23;
end;


:: # is a right inverse of @
theorem Th44:
for X being Subset of V,
    l being Linear_Combination of T .: X
  st T|X is one-to-one
  holds T@(T#l) = l
proof
  let X be Subset of V,
      l be Linear_Combination of T .: X such that
  A1: T|X is one-to-one;
  set m = T@(T#l);
    let w be Element of W;
    per cases;
    suppose B1: w in Carrier l; then
      B2: l.w <> 0.F by VECTSP_6:20;
      B3: Carrier l c= T .: X by VECTSP_6:def 7;
      consider v being set such that
        B5: v in dom T and
        B6: v in X and
        B7: w = T.v by FUNCT_1:def 12,B1,B3;
      reconsider v as Element of V by B5;
      consider B being Subset of V such that
        B9: B misses X and
        B10: T"{T.v} = {v} \/ B by A1,Th32,B6;
      B11: dom (T#l) = [#]V by FUNCT_2:169;
      B13: (T#l).v = l.(T.v) by A1,B6,Th43;
      B14: (T#l) .: {v} = Im (T#l,v)
      .= {(T#l).v} by FUNCT_1:117,B11;
      B15: m.w = Sum ((T#l) .: T"{T.v}) by B7,Def5
      .= Sum ({l.(T.v)} \/ ((T#l) .: B)) by B10,B13,B14,RELAT_1:153;
      per cases;
      suppose B = {}; then
        m.w = Sum ({l.(T.v)} \/ {}F) by B15,RELAT_1:149
        .= l.w by B7,RLVECT_2:15;
        hence thesis;
      end;
      suppose C1: B <> {};
        C2: {l.(T.v)} misses {0.F} by ZFMISC_1:17,B7,B2;
        Carrier (T#l) c= X by VECTSP_6:def 7; then
        B misses Carrier (T#l) by XBOOLE_1:63,B9; then
        m.w = Sum ({l.(T.v)} \/ {0.F}) by B15,Th33,C1
        .= Sum ({l.(T.v)}) + Sum ({0.F}) by RLVECT_2:18,C2
        .= l.(T.v) + Sum ({0.F}) by RLVECT_2:15
        .= l.(T.v) + 0.F by RLVECT_2:15
        .= l.w by B7,RLVECT_1:10;
        hence thesis;
      end;
    end;
    suppose B1: not w in Carrier l; then
      B2: l.w = 0.F;
      now
        assume B3: m.w <> 0.F; then
        w in Carrier m; then
        T"{w} meets Carrier (T#l) by Th41; then
        consider v being Element of V such that
          B6: v in T"{w} and
          B7: v in Carrier (T#l) by Th42;
        T.v in {w} by B6,FUNCT_1:def 13; then
        B10: T.v = w by TARSKI:def 1;
        B11: Carrier (T#l) c= X by VECTSP_6:def 7; then
        T|(Carrier (T#l)) is one-to-one by A1,Th40; then
        m.w = (T#l).v by B7,Th34,B10
        .= 0.F by B2,B10,A1,Th43,B7,B11;
        hence contradiction by B3;
      end;
      hence thesis by B1;
    end;
end;

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
:: The rank+nullity theorem
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

begin

definition
  let F be Field,
      V,W be finite-dimensional VectSp of F,
      T be linear-transformation of V,W;
  func
    rank(T) -> Nat
  equals
    dim (im T);
  coherence;
  func
    nullity(T) -> Nat
  equals
    dim (ker T);
  coherence;
end;

theorem Th45:
for V,W being finite-dimensional VectSp of F,
    T being linear-transformation of V,W
  holds dim V = rank(T) + nullity(T)
proof
  let V,W be finite-dimensional VectSp of F,
      T be linear-transformation of V,W;
  consider A being finite Basis of ker T;
  reconsider A' = A as Subset of V by Th5;
  consider B being Basis of V such that
  A1: A c= B by VECTSP_9:17;
  reconsider B as finite Subset of V by VECTSP_9:24;
  reconsider X = B \ A' as finite Subset of B by XBOOLE_1:36;
  reconsider X as finite Subset of V;
  A0: X misses A by XBOOLE_1:79;
  A3: B = A \/ X by XBOOLE_1:45,A1;
  reconsider C = T .: X as finite Subset of W;
  reconsider A as finite Basis of ker T;
  reconsider B as finite Basis of V;
  A200: T|X is one-to-one by Th30,A1;
  A21: X c= dom (T|X)
  proof
    dom T = [#]V by Th3;
    hence thesis by RELAT_1:91;
  end;
  A2: card C = card X
  proof    
    X,(T|X) .: X are_equipotent by A200,A21,CARD_1:60; then
    X,C are_equipotent by RELAT_1:162;
    hence thesis by CARD_1:21;
  end;
  A20: C is linearly-independent
  proof
    assume B1: C is linearly-dependent;
    consider l being Linear_Combination of C such that
    B2: Carrier l <> {} and
    B3: Sum l = 0.W by Th39,B1;
    B4: ex m being Linear_Combination of X st l = T@m
    proof
      reconsider l' = l as Linear_Combination of T .: X;
      set m = T#(l');
      take m;
      thus thesis by Th44,A200;
    end;
    consider m being Linear_Combination of B \ A' such that
    B5: l = T@m by B4;
    T.(Sum m) = 0.W by B3,B5,Th38,A1; then
    Sum m in ker T by Th12; then
    Sum m in Lin A by VECTSP_7:def 3; then
    Sum m in Lin A' by VECTSP_9:21; then
    consider n being Linear_Combination of A' such that
    B11: Sum m = Sum n by VECTSP_7:12;
    (Sum m) - (Sum n) = 0.V by VECTSP_1:66,B11; then
    B13: Sum (m - n) = 0.V by VECTSP_6:80;
    B14: Carrier (m - n) c= (Carrier m) \/ (Carrier n) by VECTSP_6:74;
    B15: Carrier m c= B \ A' by VECTSP_6:def 7;
    B16: Carrier n c= A by VECTSP_6:def 7;
    B17: (B \ A') \/ A' = B by A1,XBOOLE_1:45;
    B18: (Carrier m) \/ (Carrier n) c= (B \ A') \/ A by B15,B16,XBOOLE_1:13;
    Carrier (m - n) c= B by B14,B18,B17,XBOOLE_1:1; then
    reconsider o = m - n as Linear_Combination of B by VECTSP_6:def 7;
    B is linearly-independent by VECTSP_7:def 3; then
    B22: Carrier o = {} by B13,VECTSP_7:def 1;
    A' misses B \ A' by XBOOLE_1:79; then
    Carrier m misses Carrier n by B15,B16,XBOOLE_1:64; then
    Carrier (m - n) = (Carrier m) \/ (Carrier n) by Th27; then
    Carrier m = {} by B22,XBOOLE_1:15; then
    T .: (Carrier m) = {} by RELAT_1:149; then
    Carrier l c= {} by Th22,B5;
    hence thesis by B2,XBOOLE_1:3;
  end;
  reconsider C as finite Subset of im T by Th15;
  reconsider L = Lin C as strict Subspace of im T;
  for v being Element of im T holds v in L
  proof
    let v be Element of im T;
    B1: v in im T by STRUCT_0:def 5;
    reconsider v' = v as Element of W by VECTSP_4:18;
    consider u being Element of V such that
    A200: T.u = v' by Th16,B1;
    reconsider A' = A as Subset of V by Th5;
    V is_the_direct_sum_of Lin A', Lin (B \ A') by A1,Th28; then
    A202: (Omega).V = (Lin A') + (Lin (B \ A')) by VECTSP_5:def 4;
    A2020: u in (Omega).V by STRUCT_0:def 5;
    consider u1, u2 being Element of V such that
    A203: u1 in Lin A' and
    A204: u2 in Lin (B \ A') and
    A205: u = u1 + u2 by A202,A2020,VECTSP_5:5;
    A206: T.u = T.u1 + T.u2 by A205,MOD_2:def 5;
    Lin A = ker T by VECTSP_7:def 3; then
    u1 in ker T by A203,VECTSP_9:21; then
    T.u1 = 0.W by Th12; then
    A208: T.u = T.u2 by RLVECT_1:10,A206;
    consider l being Linear_Combination of B \ A' such that
      A209: u2 = Sum l by A204,VECTSP_7:12;
    A2110: T@l is Linear_Combination of T .: (Carrier l) by Th21;
    Carrier l c= B \ A' by VECTSP_6:def 7; then
    A2112: T .: (Carrier l) c= T .: (B \ A') by RELAT_1:156;
    reconsider C' = C as Subset of W;
    reconsider m = T@l as Linear_Combination of C' by A2110,A2112,VECTSP_6:25;
    ex m being Linear_Combination of C' st v = Sum m
    proof
      take m;
      thus thesis by Th38,A1,A209,A208,A200;
    end; then
    v in Lin C' by VECTSP_7:12;
    hence thesis by VECTSP_9:21;
  end;
  then D1: Lin C = im T by VECTSP_4:40;
  reconsider C as linearly-independent Subset of im T by VECTSP_9:16,A20;
  reconsider C as finite Basis of im T by VECTSP_7:def 3,D1;
  A4: nullity T = card A by VECTSP_9:def 2;
  A5: rank T = card C by VECTSP_9:def 2;
  dim V = card B by VECTSP_9:def 2
  .= rank T + nullity T by A2,A4,A5,CARD_2:53,A0,A3;
  hence thesis;
end;

theorem
for V,W being finite-dimensional VectSp of F,
    T being linear-transformation of V,W
  st T is one-to-one
  holds dim V = rank T
proof
  let V,W be finite-dimensional VectSp of F,
      T be linear-transformation of V,W such that
  A1: T is one-to-one;
  ker T = (0).V by Th46,A1; then
  A3: nullity(T) = 0 by Th47;
  dim V = rank(T) + nullity(T) by Th45
  .= rank(T) by A3;
  hence thesis;
end;
