:: Trigonometric Form of Complex Numbers
::  by Robert Milewski
::
:: Received July 21, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies COMPLEX1, ARYTM_1, SQUARE_1, ABSVALUE, GROUP_1, FUNCT_1, POWER,
      RCOMP_1, SIN_COS, ARYTM_3, FDIFF_1, PRE_TOPC, BOOLE, RELAT_1,
      PARTFUN1, COMPTRIG, ARYTM, XCMPLX_0, ZF_LANG, SEQM_3,
      ORDINAL2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, ARYTM_0, XCMPLX_0,
      XREAL_0, COMPLEX1, REAL_1, SQUARE_1, NAT_1, NEWTON, POWER, RELAT_1,
      FUNCT_1, RFUNCT_2, FCONT_1, FDIFF_1, SEQ_1, PARTFUN1, PARTFUN2, RCOMP_1,
      SIN_COS, XXREAL_0;
 constructors PARTFUN1, ARYTM_0, REAL_1, SQUARE_1, NAT_1, RCOMP_1, PARTFUN2,
      RFUNCT_2, FCONT_1, FDIFF_1, COMSEQ_3, BINARITH, SIN_COS, SEQ_1, BINOP_2,
      POWER, RVSUM_1;
 registrations RELSET_1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, NAT_1, MEMBERED,
      PARTFUN2, NEWTON, SIN_COS, XBOOLE_0, VALUED_0, FCONT_1;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions RFUNCT_2, SQUARE_1, COMPLEX1, XCMPLX_0;
 theorems REAL_2, NAT_1, SQUARE_1, POWER, ABSVALUE, RELAT_1, FUNCT_1,
      RFUNCT_2, PARTFUN1, FCONT_1, FCONT_2, RCOMP_1, FDIFF_1, ROLLE, COMPLEX1,
      SIN_COS, SIN_COS2, XBOOLE_0, XCMPLX_1, ARYTM_0, XREAL_1, XXREAL_0,
      NEWTON, XCMPLX_0, XXREAL_1, XBOOLE_1;
 schemes NAT_1;

begin

reserve x for real number;

scheme Regrwithout0 { P[Nat] } : P[1]
provided
A1: ex k be non empty Nat st P[k]
and
A2: for k be non empty Nat st k <> 1 & P[k]
ex n be non empty Nat st n < k & P[n]
proof
  defpred R[Nat] means P[$1+1];
  consider t be non empty Nat such that
A3: P[t] by A1;
  consider s be Nat such that
A4: t = s+1 by NAT_1:6;
A5: ex k be Nat st R[k] by A3,A4;
A6: now
    let k be Nat;
    assume that
A7: k <> 0 and
A8: R[k];
    reconsider k1=k+1 as non empty Element of NAT;
    k > 0 by A7;
    then k+1 > 0+1 by XREAL_1:8;
    then consider n be non empty Nat such that
A9: n < k1 and
A10: P[n] by A2,A8;
    consider l be Nat such that
A11: n = l+1 by NAT_1:6;
    take l;
    thus l < k by A9,A11,XREAL_1:8;
    thus R[l] by A10,A11;
  end;
  R[0] from NAT_1:sch 7(A5,A6);
  hence thesis;
end;

canceled 2;

theorem Th3:
  for z be complex number holds Re z >= -|.z.|
proof
  let z be complex number;
  0 <= (Im z)^2 by XREAL_1:65;
  then (Re z)^2+0 <= (Re z)^2 + (Im z)^2 by XREAL_1:9;
  then 0 <= (Re z)^2 & (Re z)^2 <= (Re z)^2 + (Im z)^2 by XREAL_1:65;
  then sqrt(Re z)^2 <= |.z.| by SQUARE_1:94;
  then -sqrt(Re z)^2 >= -|.z.| by XREAL_1:26;
  then Re z >= -abs(Re z) & -abs(Re z) >= -|.z.| by ABSVALUE:11,COMPLEX1:158;
  hence thesis by XXREAL_0:2;
end;

theorem
  for z be complex number holds Im z >= -|.z.|
proof
  let z be complex number;
  0 <= (Re z)^2 by XREAL_1:65;
  then (Im z)^2+0 <= (Re z)^2 + (Im z)^2 by XREAL_1:9;
  then 0 <= (Im z)^2 & (Im z)^2 <= (Re z)^2 + (Im z)^2 by XREAL_1:65;
  then sqrt(Im z)^2 <= |.z.| by SQUARE_1:94;
  then -sqrt(Im z)^2 >= -|.z.| by XREAL_1:26;
  then Im z >= -abs(Im z) & -abs(Im z) >= -|.z.| by ABSVALUE:11,COMPLEX1:158;
  hence thesis by XXREAL_0:2;
end;

canceled 2;

theorem Th7:
  for z be complex number holds |.z.|^2 = (Re z)^2 + (Im z)^2
proof
  let z be complex number;
  thus |.z.|^2 = |.z*z.| by COMPLEX1:151
    .= (Re z)^2 + (Im z)^2 by COMPLEX1:154;
end;

canceled 9;

theorem Th17:
  for n be Nat st x >= 0 & n <> 0 holds (n-root x) |^ n = x
proof
  let n be Nat;
  assume that
A1: x >= 0 and
A2: n <> 0;
  n > 0 by A2;
  then n >= 0+1 by NAT_1:13;
  hence (n-root x) |^ n = x by A1,POWER:5;
end;

registration
  cluster PI -> non negative;
  coherence
  proof
    PI in ].0, 4.[ by SIN_COS:def 32;
    then 0 < PI by XXREAL_1:4;
    hence thesis;
  end;
end;

begin

canceled 3;

Lm1: 0 < PI/2
proof
  PI in ].0, 4.[ by SIN_COS:def 32;
  then 0 < PI & PI < 4 by XXREAL_1:4;
  then 0/2 < PI/2;
  hence 0 < PI/2;
end;

Lm2: PI/2 < PI
proof
  0 + PI/2 < PI/2 + PI/2 by Lm1,XREAL_1:8;
  hence PI/2 < PI;
end;

Lm3: PI < 2*PI
proof
  0 + PI < PI + PI by Lm2,XREAL_1:8;
  hence PI < 2*PI;
end;

Lm4: PI/2 < 3/2*PI
proof
  0+PI/2 < PI+PI/2 by Lm2,XREAL_1:8;
  hence PI/2 < 3/2*PI;
end;

Lm5: -PI/2 < 0 by Lm1;

Lm6: PI/2+PI/2 < PI+PI/2 by Lm2,XREAL_1:8;

Lm7: 3/2*PI < 2*PI
proof
  3/2*PI+PI/2 = 2*PI;
  hence 3/2*PI < 2*PI by Lm6,XREAL_1:8;
end;

Lm8: 0 < 3/2*PI by Lm6;

theorem
  0 < PI/2 & PI/2 < PI & 0 < PI & -PI/2 < PI/2 & PI < 2*PI &
  PI/2 < 3/2*PI & -PI/2 < 0 & 0 < 2*PI & PI < 3/2*PI & 3/2*PI < 2*PI &
  0 < 3/2*PI by Lm2,Lm3,Lm4,Lm6,Lm7;

theorem Th22:
  for a,b,c,x be real number st x in ].a,c.[ holds
  x in ].a,b.[ or x = b or x in ].b,c.[
proof
  let a,b,c,x be real number;
  assume that
A1: x in ].a,c.[ and
A2: not x in ].a,b.[ and
A3: not x = b;
A4: a < x & x < c by A1,XXREAL_1:4;
  x <= a or x >= b by A2,XXREAL_1:4;
  then x > b by A1,A3,XXREAL_0:1,XXREAL_1:4;
  hence x in ].b,c.[ by A4,XXREAL_1:4;
end;

theorem Th23:
  x in ].0,PI.[ implies sin.x > 0
proof
  assume
A1: x in ].0,PI.[;
  per cases by A1,Th22;
  suppose x in ].0,PI/2.[;
    then 0 < x & x < PI/2 by XXREAL_1:4;
    then 0-x < 0 & -x > -PI/2 by XREAL_1:26;
    then -x+PI/2 < 0+PI/2 & -x+PI/2 > -PI/2+PI/2 by XREAL_1:8;
    then PI/2-x in ].0,PI/2.[ by XXREAL_1:4;
    then cos.(PI/2-x) > 0 by SIN_COS:85;
    hence sin.x > 0 by SIN_COS:83;
  end;
  suppose x=PI/2;
    hence sin.x > 0 by SIN_COS:81;
  end;
  suppose x in ].PI/2,PI.[;
    then PI/2 < x & x < PI by XXREAL_1:4;
    then PI/2-PI/2 < x-PI/2 & x-PI/2 < PI-PI/2 by XREAL_1:11;
    then x-PI/2 in ].0,PI/2.[ by XXREAL_1:4;
    then cos.(-(PI/2-x)) > 0 by SIN_COS:85;
    then cos.(PI/2-x) > 0 by SIN_COS:33;
    hence sin.x > 0 by SIN_COS:83;
  end;
end;

theorem Th24:
  x in [.0,PI.] implies sin.x >= 0
proof
  assume x in [.0,PI.];
  then 0 <= x & x <= PI by XXREAL_1:1;
  then x = 0 or x = PI or (0 < x & x < PI) by XXREAL_0:1;
  then x = 0 or x = PI or x in ].0,PI.[ by XXREAL_1:4;
  hence sin.x >= 0 by Th23,SIN_COS:33,81;
end;

theorem Th25:
  x in ].PI,2*PI.[ implies sin.x < 0
proof
  assume x in ].PI,2*PI.[;
  then PI < x & x < 2*PI by XXREAL_1:4;
  then PI-PI < x-PI & x-PI < 2*PI-PI by XREAL_1:11;
  then
A1: x-PI in ].0,PI.[ by XXREAL_1:4;
  sin.(x-PI) = sin.(-(PI-x))
    .= -sin.(PI+-x) by SIN_COS:33
    .= --sin.(-x) by SIN_COS:83
    .= -sin.x by SIN_COS:33;
  then -sin.x > 0 by A1,Th23;
  hence sin.x < 0;
end;

theorem Th26:
  x in [.PI,2*PI.] implies sin.x <= 0
proof
  assume x in [.PI,2*PI.];
  then PI <= x & x <= 2*PI by XXREAL_1:1;
  then x = PI or x = 2*PI or (PI < x & x < 2*PI) by XXREAL_0:1;
  then x = PI or x = 2*PI or x in ].PI,2*PI.[ by XXREAL_1:4;
  hence sin.x <= 0 by Th25,SIN_COS:81;
end;

theorem Th27:
  x in ].-PI/2,PI/2.[ implies cos.x > 0
proof
  assume x in ].-PI/2,PI/2.[;
  then -PI/2 < x & x < PI/2 by XXREAL_1:4;
  then -PI/2+PI/2 < x+PI/2 & x+PI/2 < PI/2+PI/2 by XREAL_1:8;
  then
A1: x+PI/2 in ].0,PI.[ by XXREAL_1:4;
  sin.(x+PI/2) = cos.x by SIN_COS:83;
  hence cos.x > 0 by A1,Th23;
end;

theorem Th28:
  x in [.-PI/2,PI/2.] implies cos.x >= 0
proof
  assume x in [.-PI/2,PI/2.];
  then -PI/2 <= x & x <= PI/2 by XXREAL_1:1;
  then x = -PI/2 or x = PI/2 or (-PI/2 < x & x < PI/2) by XXREAL_0:1;
  then x = -PI/2 or x = PI/2 or x in ].-PI/2,PI/2.[ by XXREAL_1:4;
  hence cos.x >= 0 by Th27,SIN_COS:33,81;
end;

theorem Th29:
  x in ].PI/2,3/2*PI.[ implies cos.x < 0
proof
  assume x in ].PI/2,3/2*PI.[;
  then PI/2 < x & x < 3/2*PI by XXREAL_1:4;
  then PI/2+PI/2 < x+PI/2 & x+PI/2 < 3/2*PI+PI/2 by XREAL_1:8;
  then
A1: x+PI/2 in ].PI,2*PI.[ by XXREAL_1:4;
  sin.(x+PI/2) = cos.x by SIN_COS:83;
  hence cos.x < 0 by A1,Th25;
end;

theorem Th30:
  x in [.PI/2,3/2*PI.] implies cos.x <= 0
proof
  assume x in [.PI/2,3/2*PI.];
  then PI/2 <= x & x <= 3/2*PI by XXREAL_1:1;
  then x = PI/2 or x = 3/2*PI or (PI/2 < x & x < 3/2*PI) by XXREAL_0:1;
  then x = PI/2 or x = 3/2*PI or x in ].PI/2,3/2*PI.[ by XXREAL_1:4;
  hence cos.x <= 0 by Th29,SIN_COS:81;
end;

theorem Th31:
  x in ].3/2*PI,2*PI.[ implies cos.x > 0
proof
  assume x in ].3/2*PI,2*PI.[;
  then 3/2*PI < x & x < 2*PI by XXREAL_1:4;
  then 3/2*PI-PI < x-PI & x-PI < 2*PI-PI by XREAL_1:11;
  then PI/2 < x-PI & x-PI < 3/2*PI by Lm6,XXREAL_0:2;
  then
A1: x-PI in ].PI/2,3/2*PI.[ by XXREAL_1:4;
  cos.(x-PI) = cos.(-(PI-x))
    .= cos.(PI+-x) by SIN_COS:33
    .= -cos.(-x) by SIN_COS:83
    .= -cos.x by SIN_COS:33;
  then -cos.x < -0 by A1,Th29;
  hence cos.x > 0;
end;

theorem Th32:
  x in [.3/2*PI,2*PI.] implies cos.x >= 0
proof
  assume x in [.3/2*PI,2*PI.];
  then 3/2*PI <= x & x <= 2*PI by XXREAL_1:1;
  then x = 3/2*PI or x = 2*PI or (3/2*PI < x & x < 2*PI) by XXREAL_0:1;
  then x = 3/2*PI or x = 2*PI or x in ].3/2*PI,2*PI.[ by XXREAL_1:4;
  hence cos.x >= 0 by Th31,SIN_COS:81;
end;

theorem Th33:
  0 <= x & x < 2*PI & sin x = 0 implies x = 0 or x = PI
proof
  assume that
A1: 0 <= x and
A2: x < 2*PI and
A3: sin x = 0;
  sin.x = 0 by A3,SIN_COS:def 21;
  then not x in ].0,PI.[ & not x in ].PI,2*PI.[ by Th23,Th25;
  then x = 0 or x >= PI & PI >= x by A1,A2,XXREAL_1:4;
  hence x = 0 or x = PI by XXREAL_0:1;
end;

theorem Th34:
  0 <= x & x < 2*PI & cos x = 0 implies x = PI/2 or x = 3/2*PI
proof
  assume that
A1: 0 <= x and
A2: x < 2*PI and
A3: cos x = 0;
A4: cos.x = 0 by A3,SIN_COS:def 23;
  then not x in ].-PI/2,PI/2.[ & not x in ].PI/2,3/2*PI.[ by Th27,Th29;
  then (-PI/2 >= x or x >= PI/2) & (PI/2 >= x or x >= 3/2*PI) by XXREAL_1:4;
  then x = PI/2 or x = 3/2*PI or x > 3/2* PI by A1,Lm5,XXREAL_0:1;
  then x = PI/2 or x = 3/2*PI or x in ].3/2* PI,2*PI.[ by A2,XXREAL_1:4;
  hence x = PI/2 or x = 3/2*PI by A4,Th31;
end;

theorem Th35:
  sin|].-PI/2,PI/2.[ is increasing
proof
X: ].-PI/2,PI/2.[ c= dom sin by SIN_COS:27;
A1: sin is_differentiable_on ].-PI/2,PI/2.[
  proof
    ].-PI/2,PI/2.[ is open by RCOMP_1:25;
    hence sin is_differentiable_on ].-PI/2,PI/2.[ by FDIFF_1:34,SIN_COS:73;
  end;
  for x be Real st x in ].-PI/2,PI/2.[ holds diff(sin,x) > 0
  proof
    let x be Real;
    assume x in ].-PI/2,PI/2.[;
    then 0 < cos.x by Th27;
    hence diff(sin,x) > 0 by SIN_COS:73;
  end;
  hence thesis by A1,Lm5,X,ROLLE:9;
end;

theorem Th36:
  sin|].PI/2,3/2*PI.[ is decreasing
proof
X: ].PI/2,3/2*PI.[ c= dom sin by SIN_COS:27;
A1: sin is_differentiable_on ].PI/2,3/2*PI.[
  proof
    ].PI/2,3/2*PI.[ is open by RCOMP_1:25;
    hence sin is_differentiable_on ].PI/2,3/2*PI.[ by FDIFF_1:34,SIN_COS:73;
  end;
  for x be Real st x in ].PI/2,3/2*PI.[ holds diff(sin,x) < 0
  proof
    let x be Real;
    assume x in ].PI/2,3/2*PI.[;
    then 0 > cos.x by Th29;
    hence diff(sin,x) < 0 by SIN_COS:73;
  end;
  hence thesis by A1,Lm4,X,ROLLE:10;
end;

theorem Th37:
  cos|].0,PI.[ is decreasing
proof
X: ].0,PI.[ c= dom cos by SIN_COS:27;
A1: cos is_differentiable_on ].0,PI.[
  proof
    ].0,PI.[ is open by RCOMP_1:25;
    hence cos is_differentiable_on ].0,PI.[ by FDIFF_1:34,SIN_COS:72;
  end;
  for x be Real st x in ].0,PI.[ holds diff(cos,x) < 0
  proof
    let x be Real;
    assume x in ].0,PI.[;
    then 0 < sin.x by Th23;
    then 0-sin.x < 0;
    hence diff(cos,x) < 0 by SIN_COS:72;
  end;
  hence thesis by A1,Lm2,X,ROLLE:10;
end;

theorem Th38:
  cos|].PI,2*PI.[ is increasing
proof
X: ].PI,2*PI.[ c= dom cos by SIN_COS:27;
A1: cos is_differentiable_on ].PI,2*PI.[
  proof
    ].PI,2*PI.[ is open by RCOMP_1:25;
    hence cos is_differentiable_on ].PI,2*PI.[ by FDIFF_1:34,SIN_COS:72;
  end;
  for x be Real st x in ].PI,2*PI.[ holds diff(cos,x) > 0
  proof
    let x be Real;
    assume x in ].PI,2*PI.[;
    then 0-sin.x > 0 by Th25,XREAL_1:52;
    hence diff(cos,x) > 0 by SIN_COS:72;
  end;
  hence thesis by A1,Lm3,X,ROLLE:9;
end;

theorem
  sin|[.-PI/2,PI/2.] is increasing
proof
  now let r1,r2 be Real;
  assume that
A1: r1 in [.-PI/2,PI/2.] /\ dom sin and
A2: r2 in [.-PI/2,PI/2.] /\ dom sin and
A3: r1 < r2;
A4: r1 in [.-PI/2,PI/2.] & r1 in dom sin &
  r2 in [.-PI/2,PI/2.] & r2 in dom sin by A1,A2,XBOOLE_0:def 4;
  then
A5: -PI/2 <= r1 & r1 <= PI/2 & -PI/2 <= r2 & r2 <= PI/2 by XXREAL_1:1;
  set r3 = (r1+r2)/2;
  abs(sin r3 ) <= 1 by SIN_COS:30;
  then abs(sin.r3 ) <= 1 by SIN_COS:def 21;
  then
A6: sin.r3 >= -1 & sin.r3 <= 1 by ABSVALUE:12;
  abs(sin r2 ) <= 1 by SIN_COS:30;
  then abs(sin.r2 ) <= 1 by SIN_COS:def 21;
  then
A7: sin.r2 >= -1 by ABSVALUE:12;
A8: r1 < r3 & r3 < r2 by A3,XREAL_1:228;
  then -PI/2 < r3 & r3 < PI/2 by A5,XXREAL_0:2;
  then r3 in ].-PI/2,PI/2.[ & r3 in dom sin by SIN_COS:27,XXREAL_1:4;
  then
A9: r3 in ].-PI/2,PI/2.[ /\ dom sin by XBOOLE_0:def 4;
  now per cases by A5,XXREAL_0:1;
    suppose
A10:  -PI/2 < r1;
      then
A11:  -PI/2 < r2 by A3,XXREAL_0:2;
      now per cases by A5,XXREAL_0:1;
        suppose
A12:      r2 < PI/2;
          then r1 < PI/2 by A3,XXREAL_0:2;
          then r1 in ].-PI/2,PI/2.[ & r2 in ].-PI/2,PI/2.[
          by A10,A11,A12,XXREAL_1:4;
          then r1 in ].-PI/2,PI/2.[ /\ dom sin & r2 in ].-PI/2,PI/2.[
          /\ dom sin by A4,XBOOLE_0:def 4;
          hence sin.r2 > sin.r1 by A3,Th35,RFUNCT_2:43;
        end;
        suppose
A13:      r2 = PI/2;
          then r1 in ].-PI/2,PI/2.[ by A3,A10,XXREAL_1:4;
          then r1 in ].-PI/2,PI/2.[ /\ dom sin by A4,XBOOLE_0:def 4;
          then
A14:      sin.r3 > sin.r1 by A8,A9,Th35,RFUNCT_2:43;
          assume sin.r2 <= sin.r1;
          hence contradiction by A6,A13,A14,SIN_COS:81,XXREAL_0:2;
        end;
      end;
      hence sin.r2 > sin.r1;
    end;
    suppose
A15:  -PI/2 = r1;
      now per cases by A5,XXREAL_0:1;
        suppose r2 < PI/2;
          then r2 in ].-PI/2,PI/2.[ by A3,A15,XXREAL_1:4;
          then r2 in ].-PI/2,PI/2.[ /\ dom sin by A4,XBOOLE_0:def 4;
          then
A16:      sin.r2 > sin.r3 by A8,A9,Th35,RFUNCT_2:43;
          assume sin.r2 <= sin.r1;
          then sin.r2 <= -1 by A15,SIN_COS:33,81;
          hence contradiction by A6,A7,A16,XXREAL_0:1;
        end;
        suppose r2 = PI/2;
          hence sin.r2 > sin.r1 by A15,SIN_COS:33,81;
        end;
      end;
      hence sin.r2 > sin.r1;
    end;
  end;
  hence sin.r2 > sin.r1;
 end;
 hence thesis by RFUNCT_2:43;
end;

theorem
  sin|[.PI/2,3/2*PI.] is decreasing
proof
  now let r1,r2 be Real;
  assume that
A1: r1 in [.PI/2,3/2*PI.] /\ dom sin and
A2: r2 in [.PI/2,3/2*PI.] /\ dom sin and
A3: r1 < r2;
A4: r1 in [.PI/2,3/2*PI.] & r1 in dom sin &
  r2 in [.PI/2,3/2*PI.] & r2 in dom sin by A1,A2,XBOOLE_0:def 4;
  then
A5: PI/2 <= r1 & r1 <= 3/2*PI & PI/2 <= r2 & r2 <= 3/2*PI by XXREAL_1:1;
  abs(sin r1 ) <= 1 by SIN_COS:30;
  then abs(sin.r1 ) <= 1 by SIN_COS:def 21;
  then
A6: sin.r1 >= -1 by ABSVALUE:12;
  set r3 = (r1+r2)/2;
  abs(sin r3 ) <= 1 by SIN_COS:30;
  then abs(sin.r3 ) <= 1 by SIN_COS:def 21;
  then
A7: sin.r3 >= -1 & sin.r3 <= 1 by ABSVALUE:12;
  abs(sin r2 ) <= 1 by SIN_COS:30;
  then abs(sin.r2 ) <= 1 by SIN_COS:def 21;
  then
A8: sin.r2 <= 1 by ABSVALUE:12;
A9: r1 < r3 & r3 < r2 by A3,XREAL_1:228;
  then PI/2 < r3 & r3 < 3/2*PI by A5,XXREAL_0:2;
  then r3 in ].PI/2,3/2*PI.[ & r3 in dom sin by SIN_COS:27,XXREAL_1:4;
  then
A10: r3 in ].PI/2,3/2*PI.[ /\ dom sin by XBOOLE_0:def 4;
  now per cases by A5,XXREAL_0:1;
    suppose
A11:  PI/2 < r1;
      then
A12:  PI/2 < r2 by A3,XXREAL_0:2;
      now per cases by A5,XXREAL_0:1;
        suppose
A13:      r2 < 3/2*PI;
          then r1 < 3/2*PI by A3,XXREAL_0:2;
          then r1 in ].PI/2,3/2*PI.[ & r2 in
          ].PI/2,3/2*PI.[ by A11,A12,A13,XXREAL_1:4;
          then r1 in ].PI/2,3/2*PI.[ /\ dom sin &
          r2 in ].PI/2,3/2*PI.[ /\ dom sin by A4,XBOOLE_0:def 4;
          hence sin.r2 < sin.r1 by A3,Th36,RFUNCT_2:44;
        end;
        suppose
A14:      r2 = 3/2*PI;
          then r1 in ].PI/2,3/2*PI.[ by A3,A11,XXREAL_1:4;
          then r1 in ].PI/2,3/2*PI.[ /\ dom sin by A4,XBOOLE_0:def 4;
          then
A15:      sin.r3 < sin.r1 by A9,A10,Th36,RFUNCT_2:44;
          assume sin.r2 >= sin.r1;
          hence contradiction by A6,A7,A14,A15,SIN_COS:81,XXREAL_0:1;
        end;
      end;
      hence sin.r2 < sin.r1;
    end;
    suppose
A16:  PI/2 = r1;
      now per cases by A5,XXREAL_0:1;
        suppose r2 < 3/2*PI;
          then r2 in ].PI/2,3/2*PI.[ by A3,A16,XXREAL_1:4;
          then r2 in ].PI/2,3/2*PI.[ /\ dom sin by A4,XBOOLE_0:def 4;
          then
A17:      sin.r2 < sin.r3 by A9,A10,Th36,RFUNCT_2:44;
          assume sin.r2 >= sin.r1;
          hence contradiction by A7,A8,A16,A17,SIN_COS:81,XXREAL_0:1;
        end;
        suppose r2 = 3/2*PI;
          hence sin.r2 < sin.r1 by A16,SIN_COS:81;
        end;
      end;
      hence sin.r2 < sin.r1;
    end;
  end;
  hence sin.r2 < sin.r1;
  end;
 hence thesis by RFUNCT_2:44;
end;

theorem Th41:
  cos|[.0,PI.] is decreasing
proof
  now let r1,r2 be Real;
  assume that
A1: r1 in [.0,PI.] /\ dom cos and
A2: r2 in [.0,PI.] /\ dom cos and
A3: r1 < r2;
A4: r1 in [.0,PI.] & r1 in dom cos & r2 in [.0,PI.] & r2 in dom cos
  by A1,A2,XBOOLE_0:def 4;
  then
A5: 0 <= r1 & r1 <= PI & 0 <= r2 & r2 <= PI by XXREAL_1:1;
  abs(cos r1) <= 1 by SIN_COS:30;
  then abs(cos.r1) <= 1 by SIN_COS:def 23;
  then
A6: cos.r1 >= -1 by ABSVALUE:12;
  set r3 = (r1+r2)/2;
  abs(cos r3) <= 1 by SIN_COS:30;
  then abs(cos.r3) <= 1 by SIN_COS:def 23;
  then
A7: cos.r3 >= -1 & cos.r3 <= 1 by ABSVALUE:12;
  abs(cos r2) <= 1 by SIN_COS:30;
  then abs(cos.r2) <= 1 by SIN_COS:def 23;
  then
A8: cos.r2 <= 1 by ABSVALUE:12;
A9: r1 < r3 & r3 < r2 by A3,XREAL_1:228;
  then 0 < r3 & r3 < PI by A5,XXREAL_0:2;
  then r3 in ].0,PI.[ & r3 in dom cos by SIN_COS:27,XXREAL_1:4;
  then
A10: r3 in ].0,PI.[ /\ dom cos by XBOOLE_0:def 4;
  now per cases by A4,XXREAL_1:1;
    suppose
A11:  0 < r1;
      now per cases by A5,XXREAL_0:1;
        suppose
A12:      r2 < PI;
          then r1 < PI by A3,XXREAL_0:2;
          then r1 in ].0,PI.[ & r2 in ].0,PI.[ by A3,A11,A12,XXREAL_1:4;
          then r1 in ].0,PI.[ /\ dom cos & r2 in ].0,PI.[ /\ dom cos
          by A4,XBOOLE_0:def 4;
          hence cos.r2 < cos.r1 by A3,Th37,RFUNCT_2:44;
        end;
        suppose
A13:      r2 = PI;
          then r1 in ].0,PI.[ by A3,A11,XXREAL_1:4;
          then r1 in ].0,PI.[ /\ dom cos by A4,XBOOLE_0:def 4;
          then
A14:      cos.r3 < cos.r1 by A9,A10,Th37,RFUNCT_2:44;
          assume cos.r2 >= cos.r1;
          hence contradiction by A6,A7,A13,A14,SIN_COS:81,XXREAL_0:1;
        end;
      end;
      hence cos.r2 < cos.r1;
    end;
    suppose
A15:  0 = r1;
      now per cases by A5,XXREAL_0:1;
        suppose r2 < PI;
          then r2 in ].0,PI.[ by A3,A15,XXREAL_1:4;
          then r2 in ].0,PI.[ /\ dom cos by A4,XBOOLE_0:def 4;
          then
A16:      cos.r2 < cos.r3 by A9,A10,Th37,RFUNCT_2:44;
          assume cos.r2 >= cos.r1;
          hence contradiction by A7,A8,A15,A16,SIN_COS:33,XXREAL_0:1;
        end;
        suppose r2 = PI;
          hence cos.r2 < cos.r1 by A15,SIN_COS:33,81;
        end;
      end;
      hence cos.r2 < cos.r1;
    end;
  end;
  hence cos.r2 < cos.r1;
 end;
 hence thesis by RFUNCT_2:44;
end;

theorem Th42:
  cos|[.PI,2*PI.] is increasing
proof
  now let r1,r2 be Real;
  assume that
A1: r1 in [.PI,2*PI.] /\ dom cos and
A2: r2 in [.PI,2*PI.] /\ dom cos and
A3: r1 < r2;
A4: r1 in [.PI,2*PI.] & r1 in dom cos & r2 in [.PI,2*PI.] & r2 in
  dom cos by A1,A2,XBOOLE_0:def 4;
  then
A5: PI <= r1 & r1 <= 2*PI & PI <= r2 & r2 <= 2*PI by XXREAL_1:1;
  set r3 = (r1+r2)/2;
  abs(cos r3 ) <= 1 by SIN_COS:30;
  then abs(cos.r3 ) <= 1 by SIN_COS:def 23;
  then
A6: cos.r3 >= -1 & cos.r3 <= 1 by ABSVALUE:12;
  abs(cos r2 ) <= 1 by SIN_COS:30;
  then abs(cos.r2 ) <= 1 by SIN_COS:def 23;
  then
A7: cos.r2 >= -1 by ABSVALUE:12;
A8: r1 < r3 & r3 < r2 by A3,XREAL_1:228;
  then PI < r3 & r3 < 2*PI by A5,XXREAL_0:2;
  then r3 in ].PI,2*PI.[ & r3 in dom cos by SIN_COS:27,XXREAL_1:4;
  then
A9: r3 in ].PI,2*PI.[ /\ dom cos by XBOOLE_0:def 4;
  now per cases by A5,XXREAL_0:1;
    suppose
A10:  PI < r1;
      then
A11:  PI < r2 by A3,XXREAL_0:2;
      now per cases by A5,XXREAL_0:1;
        suppose
A12:      r2 < 2*PI;
          then r1 < 2*PI by A3,XXREAL_0:2;
          then r1 in ].PI,2*PI.[ & r2 in ].PI,2*PI.[ by A10,A11,A12,XXREAL_1:4;
          then r1 in ].PI,2*PI.[ /\ dom cos & r2 in ].PI,2*PI.[ /\ dom cos
          by A4,XBOOLE_0:def 4;
          hence cos.r2 > cos.r1 by A3,Th38,RFUNCT_2:43;
        end;
        suppose
A13:      r2 = 2*PI;
          then r1 in ].PI,2*PI.[ by A3,A10,XXREAL_1:4;
          then r1 in ].PI,2*PI.[ /\ dom cos by A4,XBOOLE_0:def 4;
          then
A14:      cos.r3 > cos.r1 by A8,A9,Th38,RFUNCT_2:43;
          assume cos.r2 <= cos.r1;
          hence contradiction by A6,A13,A14,SIN_COS:81,XXREAL_0:2;
        end;
      end;
      hence cos.r2 > cos.r1;
    end;
    suppose
A15:  PI = r1;
      now per cases by A5,XXREAL_0:1;
        suppose r2 < 2*PI;
          then r2 in ].PI,2*PI.[ by A3,A15,XXREAL_1:4;
          then r2 in ].PI,2*PI.[ /\ dom cos by A4,XBOOLE_0:def 4;
          then
A16:      cos.r2 > cos.r3 by A8,A9,Th38,RFUNCT_2:43;
          assume cos.r2 <= cos.r1;
          hence contradiction by A6,A7,A15,A16,SIN_COS:81,XXREAL_0:1;
        end;
        suppose r2 = 2*PI;
          hence cos.r2 > cos.r1 by A15,SIN_COS:81;
        end;
      end;
      hence cos.r2 > cos.r1;
    end;
  end;
  hence cos.r2 > cos.r1;
 end;
 hence thesis by RFUNCT_2:43;
end;

canceled 2;

theorem Th45:
  sin.x in [.-1,1 .] & cos.x in [.-1,1 .]
proof
  abs(sin x) <= 1 & abs(cos x) <= 1 by SIN_COS:30;
  then abs(sin.x) <= 1 & abs(cos.x) <= 1 by SIN_COS:def 21,def 23;
  then -1 <= sin.x & sin.x <= 1 & -1 <= cos.x & cos.x <= 1 by ABSVALUE:12;
  hence thesis by XXREAL_1:1;
end;

theorem
  rng sin = [.-1,1 .]
proof
  now
    let y be set;
    thus y in [.-1,1 .] implies ex x be set st x in dom sin & y = sin.x
    proof
      assume
A1:   y in [.-1,1 .];
      then reconsider y1=y as Real;
A2:   sin|[.-PI/2,PI/2.] is continuous;
X: [.-PI/2,PI/2 .] c= dom sin by SIN_COS:27;
      y1 in [.-1,1 .] \/ [.1,sin.(-PI/2).] by A1,XBOOLE_0:def 3;
      then y1 in [.sin.(-PI/2),sin.(PI/2).] \/ [.sin.(PI/2),sin.(-PI/2).]
      by SIN_COS:33,81;
      then consider x be Real such that x in [.-PI/2,PI/2 .] and
A3:   y1 = sin.x by A2,X,FCONT_2:16;
      take x;
      thus x in dom sin & y = sin.x by A3,SIN_COS:27;
    end;
    thus (ex x be set st x in dom sin & y = sin.x) implies y in [.-1,1 .]
     by Th45,SIN_COS:27;
  end;
  hence thesis by FUNCT_1:def 5;
end;

theorem
  rng cos = [.-1,1 .]
proof
  now
    let y be set;
    thus y in [.-1,1 .] implies ex x be set st x in dom cos & y = cos.x
    proof
      assume
A1:   y in [.-1,1 .];
      then reconsider y1=y as Real;
A2:   cos|[.0,PI.] is continuous;
X:    [.0,PI.] c= dom cos by SIN_COS:27;
      y1 in [.cos.0,cos.PI.] \/ [.cos.PI,cos.0 .]
      by A1,SIN_COS:33,81,XBOOLE_0:def 3;
      then consider x be Real such that x in [.0,PI.] and
A3:   y1 = cos.x by A2,X,FCONT_2:16;
      take x;
      thus x in dom cos & y = cos.x by A3,SIN_COS:27;
    end;
    thus (ex x be set st x in dom cos & y = cos.x) implies y in [.-1,1 .]
     by Th45,SIN_COS:27;
  end;
  hence thesis by FUNCT_1:def 5;
end;

theorem
  rng (sin|[.-PI/2,PI/2.]) = [.-1,1 .]
proof
  set sin1 = sin|[.-PI/2,PI/2.];
  now
    let y be set;
    thus y in [.-1,1 .] implies ex x be set st x in dom sin1 & y = sin1.x
    proof
      assume
A1:   y in [.-1,1 .];
      then reconsider y1=y as Real;
A2:   sin1|[.-PI/2,PI/2.] is continuous by FCONT_1:16;
X:    dom sin1 = [.-PI/2,PI/2 .] /\ REAL by RELAT_1:90,SIN_COS:27
           .= [.-PI/2,PI/2 .] by XBOOLE_1:28;
      -PI/2 in [.-PI/2,PI/2.] & PI/2 in [.-PI/2,PI/2.] by XXREAL_1:1;
      then sin1.(PI/2) = sin.(PI/2) & sin1.(-PI/2) = sin.(-PI/2)
      by FUNCT_1:72;
      then y1 in [.sin1.(-PI/2),sin1.(PI/2).] by A1,SIN_COS:33,81;
      then y1 in [.sin1.(-PI/2),sin1.(PI/2).]\/[.sin1.(PI/2),sin1.(-PI/2).]
      by XBOOLE_0:def 3;
      then consider x be Real such that
A3:   x in [.-PI/2,PI/2 .] and
A4:   y1 = sin1.x by A2,X,FCONT_2:16;
      take x;
      x in REAL /\ [.-PI/2,PI/2 .] by A3,XBOOLE_0:def 4;
      then x in dom sin /\ [.-PI/2,PI/2 .] by SIN_COS:27;
      hence x in dom sin1 & y = sin1.x by A4,FUNCT_1:68;
    end;
    thus (ex x be set st x in dom sin1 & y = sin1.x) implies y in [.-1,1 .]
    proof
      given x be set such that
A5:   x in dom sin1 and
A6:   y = sin1.x;
      dom sin1 c= dom sin by RELAT_1:89;
      then reconsider x1=x as Real by A5,SIN_COS:27;
      y = sin.x1 by A5,A6,FUNCT_1:70;
      hence y in [.-1,1 .] by Th45;
    end;
  end;
  hence thesis by FUNCT_1:def 5;
end;

theorem
  rng (sin|[.PI/2,3/2*PI.]) = [.-1,1 .]
proof
  set sin1 = sin|[.PI/2,3/2*PI.];
  now
    let y be set;
    thus y in [.-1,1 .] implies ex x be set st x in dom sin1 & y = sin1.x
    proof
      assume
A1:   y in [.-1,1 .];
      then reconsider y1=y as Real;
A2:   sin1|[.PI/2,3/2*PI.] is continuous  by FCONT_1:16;
X:    dom sin1 = [.PI/2,3/2*PI .] /\ REAL by RELAT_1:90,SIN_COS:27
           .= [.PI/2,3/2*PI .] by XBOOLE_1:28;
      PI/2 in [.PI/2,3/2*PI.] & 3/2*PI in [.PI/2,3/2*PI.] by Lm4,XXREAL_1:1;
      then sin1.(PI/2) = sin.(PI/2) & sin1.(3/2*PI) = sin.(3/2*PI)
      by FUNCT_1:72;
      then y1 in [.sin1.(PI/2),sin1.(3/2*PI).] \/
      [.sin1.(3/2*PI),sin1.(PI/2).] by A1,SIN_COS:81,XBOOLE_0:def 3;
      then consider x be Real such that
A3:   x in [.PI/2,3/2*PI.] and
A4:   y1 = sin1.x by A2,Lm4,X,FCONT_2:16;
      take x;
      x in REAL /\ [.PI/2,3/2*PI.] by A3,XBOOLE_0:def 4;
      then x in dom sin /\ [.PI/2,3/2*PI.] by SIN_COS:27;
      hence x in dom sin1 & y = sin1.x by A4,FUNCT_1:68;
    end;
    thus (ex x be set st x in dom sin1 & y = sin1.x) implies y in [.-1,1 .]
    proof
      given x be set such that
A5:   x in dom sin1 and
A6:   y = sin1.x;
      dom sin1 c= dom sin by RELAT_1:89;
      then reconsider x1=x as Real by A5,SIN_COS:27;
      y = sin.x1 by A5,A6,FUNCT_1:70;
      hence y in [.-1,1 .] by Th45;
    end;
  end;
  hence thesis by FUNCT_1:def 5;
end;

theorem Th50:
  rng (cos|[.0,PI.]) = [.-1,1 .]
proof
  set cos1 = cos|[.0,PI.];
  now
    let y be set;
    thus y in [.-1,1 .] implies ex x be set st x in dom cos1 & y = cos1.x
    proof
      assume
A1:   y in [.-1,1 .];
      then reconsider y1=y as Real;
A2:   cos1|[.0,PI.] is continuous  by FCONT_1:16;
X:    dom cos1 = [.0,PI .] /\ REAL by RELAT_1:90,SIN_COS:27
           .= [.0,PI .] by XBOOLE_1:28;
      0 in [.0,PI.] & PI in [.0,PI.] by XXREAL_1:1;
      then cos1.0 = cos.0 & cos1.PI = cos.PI by FUNCT_1:72;
      then y1 in [.cos1.0,cos1.PI.] \/ [.cos1.PI,cos1.0 .]
      by A1,SIN_COS:33,81,XBOOLE_0:def 3;
      then consider x be Real such that
A3:   x in [.0,PI.] and
A4:   y1 = cos1.x by A2,X,FCONT_2:16;
      take x;
      x in REAL /\ [.0,PI.] by A3,XBOOLE_0:def 4;
      then x in dom cos /\ [.0,PI.] by SIN_COS:27;
      hence x in dom cos1 & y = cos1.x by A4,FUNCT_1:68;
    end;
    thus (ex x be set st x in dom cos1 & y = cos1.x) implies y in [.-1,1 .]
    proof
      given x be set such that
A5:   x in dom cos1 and
A6:   y = cos1.x;
      dom cos1 c= dom cos by RELAT_1:89;
      then reconsider x1=x as Real by A5,SIN_COS:27;
      y = cos.x1 by A5,A6,FUNCT_1:70;
      hence y in [.-1,1 .] by Th45;
    end;
  end;
  hence thesis by FUNCT_1:def 5;
end;

theorem Th51:
  rng (cos|[.PI,2*PI.]) = [.-1,1 .]
proof
  set cos1 = cos|[.PI,2*PI.];
  now
    let y be set;
    thus y in [.-1,1 .] implies ex x be set st x in dom cos1 & y = cos1.x
    proof
      assume
A1:   y in [.-1,1 .];
      then reconsider y1=y as Real;
A2:   cos1|[.PI,2*PI.] is continuous  by FCONT_1:16;
X:    dom cos1 = [.PI,2*PI .] /\ REAL by RELAT_1:90,SIN_COS:27
           .= [.PI,2*PI .] by XBOOLE_1:28;
      PI in [.PI,2*PI.] & 2*PI in [.PI,2*PI.] by Lm3,XXREAL_1:1;
      then cos1.PI = cos.PI & cos1.(2*PI) = cos.(2*PI) by FUNCT_1:72;
      then y1 in [.cos1.PI,cos1.(2*PI).] \/ [.cos1.(2*PI),cos1.PI.]
      by A1,SIN_COS:81,XBOOLE_0:def 3;
      then consider x be Real such that
A3:   x in [.PI,2*PI.] and
A4:   y1 = cos1.x by A2,Lm3,X,FCONT_2:16;
      take x;
      x in REAL /\ [.PI,2*PI.] by A3,XBOOLE_0:def 4;
      then x in dom cos /\ [.PI,2*PI.] by SIN_COS:27;
      hence x in dom cos1 & y = cos1.x by A4,FUNCT_1:68;
    end;
    thus (ex x be set st x in dom cos1 & y = cos1.x) implies y in [.-1,1 .]
    proof
      given x be set such that
A5:   x in dom cos1 and
A6:   y = cos1.x;
      dom cos1 c= dom cos by RELAT_1:89;
      then reconsider x1=x as Real by A5,SIN_COS:27;
      y = cos.x1 by A5,A6,FUNCT_1:70;
      hence y in [.-1,1 .] by Th45;
    end;
  end;
  hence thesis by FUNCT_1:def 5;
end;

begin  :: Argument of Complex Number

definition
  let z be complex number;
  func Arg z -> Real means
  :Def1:
  z = |.z.|*cos it + |.z.|*sin it * <i> & 0 <= it & it < 2*PI if z <> 0
  otherwise it = 0;
  existence
  proof
    thus z <> 0 implies ex r be Real st
    z = |.z.|*cos r + |.z.|*sin r * <i> & 0 <= r & r < 2*PI
    proof
      assume
A1:   z <> 0;
      then
A2:   |.z.| <> 0 by COMPLEX1:131;
A3:   |.z.| >= 0 by COMPLEX1:132;
      Re z <= |.z.| by COMPLEX1:140;
      then
A4:   Re z/|.z.| <= 1 by A2,A3,REAL_2:117;
      reconsider z1=z as complex number;
      now per cases;
        suppose
A5:       Im z >= 0;
          set 0PI = [.0,PI.];
          reconsider cos1 = cos|0PI as PartFunc of 0PI,REAL by PARTFUN1:43;

reconsider cos1 as one-to-one PartFunc of 0PI,REAL by Th41,RFUNCT_2:82;
          reconsider r = cos1".(Re z/|.z.|) as Real;
          take r;
          Re z >= -|.z.| by Th3;
          then -1 <= Re z/|.z.| by A2,A3,REAL_2:117;
          then
A6:       Re z/|.z.| in rng cos1 by A4,Th50,XXREAL_1:1;
          then Re z/|.z.| in dom (cos1") by FUNCT_1:55;
          then r in rng (cos1") by FUNCT_1:def 5;
          then r in dom cos1 by FUNCT_1:55;
          then
A7:       r in [.0,PI.] by RELAT_1:86;
A8:       cos r = cos.r by SIN_COS:def 23
            .= cos1.r by A7,FUNCT_1:72
            .= Re z/|.z.| by A6,FUNCT_1:57;
          then
A9:       Re z = |.z.|*cos r by A1,COMPLEX1:131,XCMPLX_1:88;
A10:      |.z.|^2 = (Re z)^2 + (Im z)^2 by Th7;
          0 <= r & r <= PI by A7,XXREAL_1:1;
          then (0 = r or 0 < r) & (r = PI or r < PI) by XXREAL_0:1;
          then r = 0 or r = PI or r in ].0,PI.[ by XXREAL_1:4;
          then
A11:      sin.r >= 0 by Th23,SIN_COS:33,81;
          (cos.r)^2 + (sin.r)^2 = 1 by SIN_COS:31;
          then (sin.r)^2 = 1 - (cos.r)^2
            .= 1 - (Re z/|.z.|)^2 by A8,SIN_COS:def 23
            .= 1 - (Re z)^2/|.z.|^2 by XCMPLX_1:77
            .= |.z.|^2/|.z.|^2 - (Re z)^2/|.z.|^2 by A2,XCMPLX_1:60
            .= (|.z.|^2 - (Re z)^2)/|.z.|^2
            .= ((Im z)/|.z.|)^2 by A10,XCMPLX_1:77;
          then sin.r = sqrt((Im z)/|.z.|)^2 by A11,SQUARE_1:89
            .= abs((Im z)/|.z.|) by COMPLEX1:158
            .= abs(Im z)/abs |.z.| by COMPLEX1:153
            .= abs(Im z)/|.z.|;
          then abs(Im z) = |.z.|*sin.r by A1,COMPLEX1:131,XCMPLX_1:88;
          then Im z = |.z.|*sin.r by A5,ABSVALUE:def 1
            .= |.z.|*sin r by SIN_COS:def 21;
          hence z = |.z.|*cos r + |.z.|*sin r*<i> by A9,COMPLEX1:29;
          thus 0 <= r by A7,XXREAL_1:1;
          r <= PI by A7,XXREAL_1:1;
          hence r < 2*PI by Lm3,XXREAL_0:2;
        end;
        suppose
A12:      Im z < 0;
          now per cases;
            suppose Re z <> |.z.|;
              then
A13:          Re z/|.z.| <> 1 by XCMPLX_1:58;
              set 0PI = [.PI,2*PI.];
              reconsider cos1 = cos|0PI as PartFunc of 0PI,REAL by PARTFUN1:43;
              reconsider cos1 as one-to-one PartFunc of 0PI,REAL
              by Th42,RFUNCT_2:82;
              reconsider r = cos1".(Re z/|.z.|) as Real;
              take r;
              Re z >= -|.z.| by Th3;
              then -1 <= Re z/|.z.| by A2,A3,REAL_2:117;
              then
A14:          Re z/|.z.| in rng cos1 by A4,Th51,XXREAL_1:1;
              then
A15:          Re z/|.z.| in dom (cos1") by FUNCT_1:55;
              then r in rng (cos1") by FUNCT_1:def 5;
              then r in dom cos1 by FUNCT_1:55;
              then
A16:          r in [.PI,2*PI.] by RELAT_1:86;
A17:          cos r = cos.r by SIN_COS:def 23
                .= cos1.r by A16,FUNCT_1:72
                .= Re z/|.z.| by A14,FUNCT_1:57;
              then
A18:          Re z = |.z.|*cos r by A1,COMPLEX1:131,XCMPLX_1:88;
A19:          |.z.|^2 = (Re z)^2 + (Im z)^2 by Th7;
              PI <= r & r <= 2*PI by A16,XXREAL_1:1;
              then (PI = r or PI < r) & (r = 2*PI or r < 2*PI) by XXREAL_0:1;
              then r = PI or r = 2*PI or r in ].PI,2*PI.[ by XXREAL_1:4;
              then
A20:          sin.r <= 0 by Th25,SIN_COS:81;
              (cos.r)^2 + (sin.r)^2 = 1 by SIN_COS:31;
              then (sin.r)^2 = 1 - (cos.r)^2
                .= 1 - (Re z/|.z.|)^2 by A17,SIN_COS:def 23
                .= 1 - (Re z)^2/|.z.|^2 by XCMPLX_1:77
                .= |.z.|^2/|.z.|^2 - (Re z)^2/|.z.|^2 by A2,XCMPLX_1:60
                .= (|.z.|^2 - (Re z)^2)/|.z.|^2
                .= ((Im z)/|.z.|)^2 by A19,XCMPLX_1:77;
              then -sin.r = sqrt((Im z)/|.z.|)^2 by A20,SQUARE_1:90
                .= abs((Im z)/|.z.|) by COMPLEX1:158
                .= abs(Im z)/abs |.z.| by COMPLEX1:153
                .= abs(Im z)/|.z.|;
              then sin.r = (-abs(Im z))/|.z.|;
              then -abs(Im z) = |.z.|*sin.r by A1,COMPLEX1:131,XCMPLX_1:88;
              then
A21:          --Im z = |.z.|*sin.r by A12,ABSVALUE:def 1
                .= |.z.|*sin r by SIN_COS:def 21;
              z1 = Re z + (Im z)*<i> by COMPLEX1:29
                .= [*Re z,Im z*] by COMPLEX1:27;
              hence z = |.z.|*cos r + |.z.|*sin r*<i> by A18,A21,COMPLEX1:27;
              thus 0 <= r by A16,XXREAL_1:1;
A22:          r <> 2*PI
              proof
A23:            dom cos = REAL by SIN_COS:27;
A24:            cos1" is one-to-one by FUNCT_1:62;
                1 in rng cos1 by Th51,XXREAL_1:1;
                then
A25:            1 in dom (cos1") by FUNCT_1:55;
                assume
A26:            r = 2*PI;
A27:            2*PI in [.PI,2*PI.] by Lm3,XXREAL_1:1;
                then
A28:            2*PI in dom cos1 by A23,RELAT_1:86;
                cos1.(2*PI) = 1 by A27,FUNCT_1:72,SIN_COS:81;
                then cos1".1 = 2*PI by A28,FUNCT_1:54;
                hence contradiction by A13,A15,A24,A25,A26,FUNCT_1:def 8;
              end;
              r <= 2*PI by A16,XXREAL_1:1;
              hence r < 2*PI by A22,XXREAL_0:1;
            end;
            suppose
A29:          Re z = |.z.|;
              then (Re z)^2 = (Re z)^2 + (Im z)^2 by Th7;
              then
A30:          Im z = 0;
              reconsider r=0 as Real;
              take r;
              thus z = |.z.|*cos r+|.z.|*sin r*<i>
              by A29,A30,COMPLEX1:29,SIN_COS:34;
              thus 0 <= r;
              thus r < 2*PI by Lm7;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    thus thesis;
  end;
  uniqueness
  proof
    z <> 0 implies for x,y be Real st
    z = |.z.|*cos x + |.z.|*sin x*<i> & 0 <= x & x < 2*PI &
    z = |.z.|*cos y + |.z.|*sin y*<i> & 0 <= y & y < 2*PI holds x = y
    proof
      assume z <> 0;
      then
A31:  |.z.| <> 0 by COMPLEX1:131;
      let x,y be Real;
      assume that
A32:  z = |.z.|*cos x + |.z.|*sin x*<i> and
A33:  0 <= x and
A34:  x < 2*PI and
A35:  z = |.z.|*cos y + |.z.|*sin y*<i> and
A36:  0 <= y and
A37:  y < 2*PI;
      z = [*|.z.|*cos x,|.z.|*sin x*] &
      z = [*|.z.|*cos y,|.z.|*sin y*] by A32,A35,COMPLEX1:27;
      then |.z.|*cos x = |.z.|*cos y & |.z.|*sin x = |.z.|*sin y by ARYTM_0:12;
      then cos x = cos y & sin x = sin y by A31,XCMPLX_1:5;
      then
A38:  cos x = cos y & sin x = sin.y by SIN_COS:def 21;
      then cos x = cos.y & sin.x = sin.y by SIN_COS:def 21,def 23;
      then
A39:  cos.x = cos.y & sin.x = sin.y by SIN_COS:def 23;
A40:  dom cos = REAL by SIN_COS:27;
      now per cases;
        suppose x <= PI & y <= PI;
          then x in [.0,PI.] & y in [.0,PI.] by A33,A36,XXREAL_1:1;
          then
A41:      x in [.0,PI.] /\ dom cos & y in [.0,PI.] /\ dom cos
          by A40,XBOOLE_0:def 4;
          assume x <> y;
          then x < y or y < x by XXREAL_0:1;
          hence contradiction by A39,A41,Th41,RFUNCT_2:44;
        end;
        suppose x > PI & y > PI;
          then x in [.PI,2*PI.] & y in [.PI,2*PI.] by A34,A37,XXREAL_1:1;
          then
A42:      x in [.PI,2*PI.] /\ dom cos & y in [.PI,2*PI.] /\ dom cos
          by A40,XBOOLE_0:def 4;
          assume x <> y;
          then x < y or y < x by XXREAL_0:1;
          hence contradiction by A39,A42,Th42,RFUNCT_2:43;
        end;
        suppose x <= PI & y > PI;
          then x in [.0,PI.] & y in ].PI,2*PI.[ by A33,A37,XXREAL_1:1,4;
          then sin.x >= 0 & sin.y < 0 by Th24,Th25;
          hence x = y by A38,SIN_COS:def 21;
        end;
        suppose y <= PI & x > PI;
          then y in [.0,PI.] & x in ].PI,2*PI.[ by A34,A36,XXREAL_1:1,4;
          then sin.y >= 0 & sin.x < 0 by Th24,Th25;
          hence x = y by A38,SIN_COS:def 21;
        end;
      end;
      hence x = y;
    end;
    hence thesis;
  end;
  consistency;
end;

theorem Th52:
  for z be complex number holds 0 <= Arg z & Arg z < 2*PI
proof
  let z be complex number;
  0 <= Arg z & Arg z < 2*PI or z = 0 by Def1;
  hence thesis by Def1,Lm7,Lm8;
end;

theorem Th53:
  for x be Real st x >= 0 holds Arg x = 0
proof
  let x be Real;
  assume
A1: x >= 0;
A2: 0 <= Arg (x+0*<i>) & Arg (x+0*<i>) < 2*PI by Th52;
  per cases by A1;
  suppose
A3: x > -0;
    then
A4: |. (x+0*<i>) .| <> 0 by COMPLEX1:131;
A5: (x+0*<i>) = |. (x+0*<i>) .|*cos Arg (x+0*<i>)+
    |. (x+0*<i>) .|*sin Arg (x+0*<i>)*<i> by A3,Def1;
    then |. (x+0*<i>) .|*cos Arg (x+0*<i>) = x &
    |. (x+0*<i>) .|*sin Arg (x+0*<i>) = 0 by COMPLEX1:163;
    then sin Arg (x+0*<i>) = 0 by A4;
    then Arg (x+0*<i>) = 0 or |. (x+0*<i>) .|*-1 = x by A2,A5,Th33,SIN_COS:82;
    then Arg (x+0*<i>) = 0 or -|. (x+0*<i>) .| = x;
    then Arg (x+0*<i>) = 0 or |. (x+0*<i>) .| < 0 by A3;
    hence Arg x = 0 by COMPLEX1:132;
  end;
  suppose x+0*<i> = 0;
    hence thesis by Def1;
  end;
end;

theorem Th54:
  for x be Real st x < 0 holds Arg x = PI
proof
  let x be Real;
  assume
A1: x < 0;
A2: 0 <= Arg (x+0*<i>) & Arg (x+0*<i>) < 2*PI by Th52;
A3: |. (x+0*<i>) .| <> 0 by A1,COMPLEX1:131;
A4: (x+0*<i>) = |. (x+0*<i>) .|*cos Arg (x+0*<i>)+
  |. (x+0*<i>) .|*sin Arg (x+0*<i>)*<i> by A1,Def1;
  then |. (x+0*<i>) .|*cos Arg (x+0*<i>) = x &
  |. (x+0*<i>) .|*sin Arg (x+0*<i>) = 0 by COMPLEX1:163;
  then sin Arg (x+0*<i>) = 0 by A3;
  then Arg (x+0*<i>) = PI or |. (x+0*<i>) .|*1 = x by A2,A4,Th33,SIN_COS:34;
  hence thesis by A1,COMPLEX1:132;
end;

theorem Th55:
  for x be Real st x > 0 holds Arg (x*<i>) = PI/2
proof
  let x be Real;
  assume
A1: x > 0;
  then
A2: x > -0;
A3: 0 <= Arg (0+x*<i>) & Arg (0+x*<i>) < 2*PI by Th52;
A4: 0+x*<i> <> 0 by A1;
  then
A5: |.0+x*<i>.| <> 0 by COMPLEX1:131;
A6: (0+x*<i>) = |. (0+x*<i>) .|*cos Arg (0+x*<i>)+
  |. (0+x*<i>) .|*sin Arg (0+x*<i>)*<i> by A4,Def1;
  then |.0+x*<i>.|*cos Arg (0+x*<i>) = 0 &
  |. (0+x*<i>) .|*sin Arg (0+x*<i>) = x by COMPLEX1:163;
  then cos Arg (0+x*<i>) = 0 by A5;
  then Arg (0+x*<i>) = PI/2 or |. (0+x*<i>) .|*-1 = x
  by A3,A6,Th34,SIN_COS:82;
  then Arg (0+x*<i>) = PI/2 or -|. (0+x*<i>) .| = x;
  then Arg (0+x*<i>) = PI/2 or |. (0+x*<i>) .| < 0 by A2;
  hence thesis by COMPLEX1:132;
end;

theorem Th56:
  for x be Real st x < 0 holds Arg (x*<i>) = 3/2*PI
proof
  let x be Real;
  assume
A1: x < 0;
A2: 0 <= Arg (0+x*<i>) & Arg (0+x*<i>) < 2*PI by Th52;
A3: (0+x*<i>) <> 0 by A1;
  then
A4: |. (0+x*<i>) .| <> 0 by COMPLEX1:131;
A5: (0+x*<i>) = |. (0+x*<i>) .|*cos Arg (0+x*<i>)+
  |. (0+x*<i>) .|*sin Arg (0+x*<i>)*<i> by A3,Def1;
  then |. (0+x*<i>) .|*cos Arg (0+x*<i>) = 0 &
  |. (0+x*<i>) .|*sin Arg (0+x*<i>) = x by COMPLEX1:163;
  then cos Arg (0+x*<i>) = 0 by A4;
  then Arg (0+x*<i>) = 3/2*PI or |. (0+x*<i>) .|*1 = x
  by A2,A5,Th34,SIN_COS:82;
  hence thesis by A1,COMPLEX1:132;
end;

theorem
  Arg 1 = 0 by Th53;

theorem
  Arg <i> = PI/2
proof
  <i> = (0+1*<i>);
  hence thesis by Th55;
end;

theorem Th59:
  for z be complex number holds Arg z in ].0,PI/2.[ iff Re z > 0 & Im z > 0
proof
  let z be complex number;
  thus Arg z in ].0,PI/2.[ implies Re z > 0 & Im z > 0
  proof
    assume
A1: Arg z in ].0,PI/2.[;
    then
A2: Arg z > 0 & Arg z < PI/2 by XXREAL_1:4;
    then
A3: z <> 0 by Def1;
    then z = |.z.|*cos Arg z+|.z.|*sin Arg z*<i> by Def1;
    then
A4: Re z = |.z.|*cos Arg z & Im z = |.z.|*sin Arg z by COMPLEX1:28;
A5: |.z.| > 0 by A3,COMPLEX1:133;
    cos Arg z > 0 by A1,SIN_COS:86;
    hence Re z > 0 by A4,A5;
    Arg z < PI by A2,Lm2,XXREAL_0:2;
    then Arg z in ].0,PI.[ by A2,XXREAL_1:4;
    then sin.Arg z > 0 by Th23;
    then sin Arg z > 0 by SIN_COS:def 21;
    hence Im z > 0 by A4,A5;
  end;
  assume that
A6: Re z > 0 and
A7: Im z > 0;
  z = (Re z+Im z*<i>) by COMPLEX1:29;
  then
A8: z <> 0+0*<i> by A6,COMPLEX1:163;
  then
A9: |.z.| > 0 by COMPLEX1:133;
A10: 0 <= Arg z & Arg z < 2*PI by Th52;
  z = |.z.|*cos Arg z+|.z.|*sin Arg z*<i> by A8,Def1;
  then |.z.|*cos Arg z > 0 & |.z.|*sin Arg z > 0 by A6,A7,COMPLEX1:28;
  then cos Arg z > 0 & sin Arg z > 0 by A9;
  then
A11: cos.Arg z > 0 & sin.Arg z > 0 by SIN_COS:def 21,def 23;
  then not Arg z in [.PI,2*PI.] & not Arg z in [.PI/2,3/2*PI.] by Th26,Th30;
  then
A12: PI/2 > Arg z or PI > Arg z & 3/2*PI < Arg z by A10,XXREAL_1:1;
  Arg z > 0 by A10,A11,SIN_COS:33;
  hence thesis by A12,Lm6,XXREAL_0:2,XXREAL_1:4;
end;

theorem Th60:
  for z be complex number holds Arg z in ].PI/2,PI.[ iff Re z < 0 & Im z > 0
proof
  let z be complex number;
  thus Arg z in ].PI/2,PI.[ implies Re z < 0 & Im z > 0
  proof
    assume
A1: Arg z in ].PI/2,PI.[;
    then
A2: Arg z > PI/2 & Arg z < PI by XXREAL_1:4;
    then
A3: z <> 0 by Def1;
    then z = |.z.|*cos Arg z+|.z.|*sin Arg z*<i> by Def1;
    then
A4: Re z = |.z.|*cos Arg z & Im z = |.z.|*sin Arg z by COMPLEX1:28;
A5: |.z.| > 0 by A3,COMPLEX1:133;
    PI/2 < Arg z & Arg z < PI by A1,XXREAL_1:4;
    then PI/2 < Arg z & Arg z < 3/2*PI by Lm6,XXREAL_0:2;
    then Arg z in ].PI/2,3/2*PI.[ by XXREAL_1:4;
    then cos.Arg z < 0 by Th29;
    then cos Arg z < 0 by SIN_COS:def 23;
    hence Re z < 0 by A4,A5;
    Arg z in ].0,PI.[ by A2,XXREAL_1:4;
    then sin.Arg z > 0 by Th23;
    then sin Arg z > 0 by SIN_COS:def 21;
    hence Im z > 0 by A4,A5;
  end;
  assume that
A6: Re z < 0 and
A7: Im z > 0;
  z = (Re z+Im z*<i>) by COMPLEX1:29;
  then
A8: z <> 0+0*<i> by A6,COMPLEX1:163;
  then
A9: |.z.| > 0 by COMPLEX1:133;
A10: 0 <= Arg z & Arg z < 2*PI by Th52;
  z = |.z.|*cos Arg z+|.z.|*sin Arg z*<i> by A8,Def1;
  then |.z.|*cos Arg z < 0 & |.z.|*sin Arg z > 0 by A6,A7,COMPLEX1:28;
  then cos Arg z < 0 & sin Arg z > 0 by A9;
  then cos.Arg z < 0 & sin.Arg z > 0 by SIN_COS:def 21,def 23;
  then not Arg z in [.PI,2*PI.] & not Arg z in [.-PI/2,PI/2.] by Th26,Th28;
  then Arg z < PI & (Arg z < -PI/2 or Arg z > PI/2) by A10,XXREAL_1:1;
  hence thesis by Th52,XXREAL_1:4;
end;

theorem Th61:
  for z be complex number holds Arg z in ].PI,3/2*PI.[ iff Re z < 0 & Im z < 0
proof
  let z be complex number;
  thus Arg z in ].PI,3/2*PI.[ implies Re z < 0 & Im z < 0
  proof
    assume
A1: Arg z in ].PI,3/2*PI.[;
    then
A2: Arg z > PI & Arg z < 3/2*PI by XXREAL_1:4;
    then
A3: z <> 0 by Def1;
    then z = |.z.|*cos Arg z+|.z.|*sin Arg z*<i> by Def1;
    then
A4: Re z = |.z.|*cos Arg z & Im z = |.z.|*sin Arg z by COMPLEX1:28;
A5: |.z.| > 0 by A3,COMPLEX1:133;
    PI < Arg z & Arg z < 3/2*PI by A1,XXREAL_1:4;
    then PI/2 < Arg z & Arg z < 3/2*PI by Lm2,XXREAL_0:2;
    then Arg z in ].PI/2,3/2*PI.[ by XXREAL_1:4;
    then cos.Arg z < 0 by Th29;
    then cos Arg z < 0 by SIN_COS:def 23;
    hence Re z < 0 by A4,A5;
    Arg z < 2*PI by A2,Lm7,XXREAL_0:2;
    then Arg z in ].PI,2*PI.[ by A2,XXREAL_1:4;
    then sin.Arg z < 0 by Th25;
    then sin Arg z < 0 by SIN_COS:def 21;
    hence Im z < 0 by A4,A5;
  end;
  assume that
A6: Re z < 0 and
A7: Im z < 0;
  z = (Re z+Im z*<i>) by COMPLEX1:29;
  then
A8: z <> 0+0*<i> by A6,COMPLEX1:163;
  then
A9: |.z.| > 0 by COMPLEX1:133;
A10: 0 <= Arg z & Arg z < 2*PI by Th52;
  z = |.z.|*cos Arg z+|.z.|*sin Arg z*<i> by A8,Def1;
  then |.z.|*cos Arg z < 0 & |.z.|*sin Arg z < 0 by A6,A7,COMPLEX1:28;
  then cos Arg z < 0 & sin Arg z < 0 by A9;
  then cos.Arg z < 0 & sin.Arg z < 0 by SIN_COS:def 21,def 23;
  then not Arg z in [.0,PI.] & not Arg z in [.-PI/2,PI/2.] &
  not Arg z in [.3/2*PI,2*PI.] by Th24,Th28,Th32;
  then Arg z > PI & (Arg z < -PI/2 or Arg z > PI/2) & Arg z < 3/2*PI
  by A10,XXREAL_1:1;
  hence thesis by XXREAL_1:4;
end;

theorem Th62:
  for z be complex number holds
  Arg z in ].3/2*PI,2*PI.[ iff Re z > 0 & Im z < 0
proof
  let z be complex number;
  thus Arg z in ].3/2*PI,2*PI.[ implies Re z > 0 & Im z < 0
  proof
    assume
A1: Arg z in ].3/2*PI,2*PI.[;
    then
A2: Arg z > 3/2*PI & Arg z < 2*PI by XXREAL_1:4;
    then
A3: z <> 0 by Def1;
    then z = |.z.|*cos Arg z+|.z.|*sin Arg z*<i> by Def1;
    then
A4: Re z = |.z.|*cos Arg z & Im z = |.z.|*sin Arg z by COMPLEX1:28;
A5: |.z.| > 0 by A3,COMPLEX1:133;
    cos.Arg z > 0 by A1,Th31;
    then cos Arg z > 0 by SIN_COS:def 23;
    hence Re z > 0 by A4,A5;
    Arg z > PI by A2,Lm6,XXREAL_0:2;
    then Arg z in ].PI,2*PI.[ by A2,XXREAL_1:4;
    then sin.Arg z < 0 by Th25;
    then sin Arg z < 0 by SIN_COS:def 21;
    hence Im z < 0 by A4,A5;
  end;
  assume that
A6: Re z > 0 and
A7: Im z < 0;
  z = (Re z+Im z*<i>) by COMPLEX1:29;
  then
A8: z <> 0+0*<i> by A6,COMPLEX1:163;
  then
A9: |.z.| > 0 by COMPLEX1:133;
A10: 0 <= Arg z & Arg z < 2*PI by Th52;
  z = |.z.|*cos Arg z+|.z.|*sin Arg z*<i> by A8,Def1;
  then |.z.|*cos Arg z > 0 & |.z.|*sin Arg z < 0 by A6,A7,COMPLEX1:28;
  then cos Arg z > 0 & sin Arg z < 0 by A9;
  then cos.Arg z > 0 & sin.Arg z < 0 by SIN_COS:def 21,def 23;
  then not Arg z in [.0,PI.] & not Arg z in [.PI/2,3/2*PI.] by Th24,Th30;
  then Arg z > PI & (Arg z < PI/2 or Arg z > 3/2*PI) by A10,XXREAL_1:1;
  hence thesis by A10,Lm2,XXREAL_0:2,XXREAL_1:4;
end;

theorem Th63:
  for z be complex number st Im z > 0 holds sin Arg z > 0
proof
  let z be complex number;
  assume
A1: Im z > 0;
  Re z < 0 or Re z = 0 or Re z > 0;
  then Re z < 0 or Re z > 0 or z = (0+Im z*<i>) by COMPLEX1:29;
  then Arg z in ].PI/2,PI.[ or Arg z in ].0,PI/2.[ or Arg z = PI/2
  by A1,Th55,Th59,Th60;
  then PI/2 < Arg z & Arg z < PI or 0 < Arg z & Arg z < PI/2 or
  Arg z = PI/2 by XXREAL_1:4;
  then 0 < Arg z & Arg z < PI by Lm2,XXREAL_0:2;
  then Arg z in ].0,PI.[ by XXREAL_1:4;
  then sin.Arg z > 0 by Th23;
  hence thesis by SIN_COS:def 21;
end;

theorem Th64:
  for z be complex number st Im z < 0 holds sin Arg z < 0
proof
  let z be complex number;
  assume
A1: Im z < 0;
  Re z < 0 or Re z = 0 or Re z > 0;
  then Re z < 0 or Re z > 0 or z = (0+Im z*<i>) by COMPLEX1:29;
  then Arg z in ].PI,3/2*PI.[ or Arg z in ].3/2*PI,2*PI.[ or
  Arg z = 3/2*PI by A1,Th56,Th61,Th62;
  then PI < Arg z & Arg z < 3/2*PI or 3/2*PI < Arg z & Arg z < 2*PI or
  Arg z = 3/2*PI by XXREAL_1:4;
  then PI < Arg z & Arg z < 2*PI by Lm6,Lm7,XXREAL_0:2;
  then Arg z in ].PI,2*PI.[ by XXREAL_1:4;
  then sin.Arg z < 0 by Th25;
  hence thesis by SIN_COS:def 21;
end;

theorem
  for z be complex number st Im z >= 0 holds sin Arg z >= 0
proof
  let z be complex number;
  assume Im z >= 0;
  then Im z > 0 or Im z = 0;
  then sin Arg z > 0 or z = (Re z+0*<i>) & (Re z >= 0 or Re z < 0)
  by Th63,COMPLEX1:29;
  hence thesis by Th53,Th54,SIN_COS:34,82;
end;

theorem
  for z be complex number st Im z <= 0 holds sin Arg z <= 0
proof
  let z be complex number;
  assume Im z <= 0;
  then Im z < 0 or Im z = 0;
  then sin Arg z < 0 or z = (Re z+0*<i>) & (Re z >= 0 or Re z < 0)
  by Th64,COMPLEX1:29;
  hence thesis by Th53,Th54,SIN_COS:34,82;
end;

theorem Th67:
  for z be complex number st Re z > 0 holds cos Arg z > 0
proof
  let z be complex number;
  assume
A1: Re z > 0;
  Im z < 0 or Im z = 0 or Im z > 0;
  then Im z < 0 or Im z > 0 or z = (Re z+0*<i>) by COMPLEX1:29;
  then Arg z in ].0,PI/2.[ or Arg z in ].3/2*PI,2*PI.[ or Arg z = 0
  by A1,Th53,Th59,Th62;
  then cos.Arg z > 0 by Th31,SIN_COS:33,85;
  hence thesis by SIN_COS:def 23;
end;

theorem Th68:
  for z be complex number st Re z < 0 holds cos Arg z < 0
proof
  let z be complex number;
  assume
A1: Re z < 0;
  Im z < 0 or Im z = 0 or Im z > 0;
  then Im z < 0 or Im z > 0 or z = (Re z+0*<i>) by COMPLEX1:29;
  then Arg z in ].PI/2,PI.[ or Arg z in ].PI,3/2*PI.[ or Arg z = PI
  by A1,Th54,Th60,Th61;
  then PI/2 < Arg z & Arg z < PI or PI < Arg z & Arg z < 3/2*PI or
  Arg z = PI by XXREAL_1:4;
  then PI/2 < Arg z & Arg z < 3/2*PI by Lm2,Lm6,XXREAL_0:2;
  then Arg z in ].PI/2,3/2*PI.[ by XXREAL_1:4;
  then cos.Arg z < 0 by Th29;
  hence thesis by SIN_COS:def 23;
end;

theorem
  for z be complex number st Re z >= 0 holds cos Arg z >= 0
proof
  let z be complex number;
  assume Re z >= 0;
  then Re z > 0 or Re z = 0;
  then cos Arg z > 0 or z = (0+Im z*<i>) & (Im z > 0 or Im z < 0 or Im z = 0
  ) by Th67,COMPLEX1:29;
  hence thesis by Def1,Th55,Th56,SIN_COS:34,82;
end;

theorem
  for z be complex number st Re z <= 0 & z <> 0 holds cos Arg z <= 0
proof
  let z be complex number;
  assume that
A1: Re z <= 0 and
A2: z <> 0;
A3: 0 = 0+0*<i>;
  Re z < 0 or Re z = 0 by A1;
  then cos Arg z < 0 or z = 0+Im z*<i> & (Im z >= 0 or Im z < 0)
  by Th68,COMPLEX1:29;
  then cos Arg z < 0 or z = 0+Im z*<i> & (Im z > 0 or Im z < 0)
  by A2,A3;
  hence thesis by Th55,Th56,SIN_COS:82;
end;

theorem Th71:
  for x be real number, n be Nat holds
  (cos x+(sin x)*<i>)|^n = cos (n*x)+sin (n*x)*<i>
proof
  let x be real number;
  defpred P[Nat] means (cos x+(sin x)*<i>)|^$1 = cos ($1*x)+sin ($1*x)*<i>;
A1: P[0] by NEWTON:9,SIN_COS:34;
A2: now
    let n be Nat;
    assume P[n];
    then (cos x+(sin x)*<i>)|^(n+1) =
    (cos (n*x)+sin (n*x)*<i>)*(cos x+(sin x)*<i>) by NEWTON:11
      .= cos(n*x)*cos x - sin(n*x)*sin x
    +cos(n*x)*(sin x)*<i>+cos x*sin(n*x)*<i>
      .= cos(n*x+x)+cos(n*x)*(sin x)*<i>+cos x*sin(n*x)*<i> by SIN_COS:80
      .= cos(n*x+x)+(cos(n*x)*sin x+cos x*sin(n*x))*<i>
      .= cos ((n+1)*x)+sin ((n+1)*x)*<i> by SIN_COS:80;
    hence P[n+1];
  end;
  thus for n being Nat holds P[n] from NAT_1:sch 2(A1,A2);
end;

theorem
  for z be Element of COMPLEX for n be Nat st z <> 0 or n <> 0 holds
  z|^n = (|.z.| |^ n)*cos (n*Arg z)+(|.z.| |^ n)*sin (n*Arg z)*<i>
proof
  let z be Element of COMPLEX;
  let n be Nat;
  assume
A1: z <> 0 or n <> 0;
  per cases by A1;
  suppose z <> 0;
    hence z|^n = (|.z.|*cos Arg z+|.z.|*sin Arg z*<i>)|^n by Def1
      .= ((|.z.|+0*<i>)*(cos Arg z+(sin Arg z)*<i>))|^n
      .= ((|.z.|+0*<i>)|^n)*
    ((cos Arg z+(sin Arg z)*<i>))|^n by NEWTON:12
      .= (|.z.| |^ n+0*<i>)*(cos (n*Arg z)+sin (n*Arg z)*<i>) by Th71
      .= (|.z.| |^ n)*cos(n*Arg z)+ (|.z.| |^ n)*sin(n*Arg z)*<i>;
  end;
  suppose
A2: z = 0 & n > 0;
    then
A3: n >= 1+0 by NAT_1:13;
    hence z|^n = 0*cos(n*Arg z)+0*sin(n*Arg z)*<i> by A2,NEWTON:16
      .= 0*cos(n*Arg z)+(|.z.| |^ n)*sin(n*Arg z)*<i>
    by A2,A3,COMPLEX1:130,NEWTON:16
      .= (|.z.| |^ n)*cos(n*Arg z)+(|.z.| |^ n)*sin(n*Arg z)*<i>
    by A2,A3,COMPLEX1:130,NEWTON:16;
  end;
end;

theorem Th73:
  for x be Real, n,k be Nat st n <> 0 holds
  (cos((x+2*PI*k)/n)+sin((x+2*PI*k)/n)*<i>)|^n = (cos x+(sin x)*<i>)
proof
  let x be Real;
  let n,k be Nat;
  assume
A1: n <> 0;
  thus (cos((x+2*PI*k)/n)+sin((x+2*PI*k)/n)*<i>)|^n =
  cos(n*((x+2*PI*k)/n))+sin(n*((x+2*PI*k)/n))*<i> by Th71
    .= cos(x+2*PI*k)+sin(n*((x+2*PI*k)/n))*<i> by A1,XCMPLX_1:88
    .= cos(x+2*PI*k)+sin(x+2*PI*k)*<i> by A1,XCMPLX_1:88
    .= cos.(x+2*PI*k)+sin(x+2*PI*k)*<i> by SIN_COS:def 23
    .= cos.(x+2*PI*k)+sin.(x+2*PI*k)*<i> by SIN_COS:def 21
    .= cos.(x+2*PI*k)+sin.x*<i> by SIN_COS2:10
    .= cos.x+sin.x*<i> by SIN_COS2:11
    .= cos.x+sin x*<i> by SIN_COS:def 21
    .= (cos x+(sin x)*<i>) by SIN_COS:def 23;
end;

theorem Th74:
  for z be complex number for n,k be Nat st n <> 0 holds
  z = ((n-root |.z.|)*cos((Arg z+2*PI*k)/n)+
  (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*<i>)|^n
proof
  let z be complex number;
  let n,k be Nat;
  assume
A1: n <> 0;
  then n > 0;
  then
A2: n >= 0+1 by NAT_1:13;
  per cases;
  suppose
A3: z <> 0;
    thus ((n-root |.z.|)*cos((Arg z+2*PI*k)/n)+
    (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*<i>)|^n = ((n-root |.z.|+0*<i>)*
    (cos((Arg z+2*PI*k)/n)+sin((Arg z+2*PI*k)/n)*<i>))|^n
      .= ((n-root |.z.|+0*<i>))|^n*
    (cos((Arg z+2*PI*k)/n)+sin((Arg z+2*PI*k)/n)*<i>)|^n by NEWTON:12
      .= (n-root |.z.|+0*<i>)|^n*(cos Arg z+(sin Arg z)*<i>) by A1,Th73
      .= (|.z.|+0*<i>)*(cos Arg z+(sin Arg z)*<i>) by A1,Th17,COMPLEX1:132
      .= |.z.|*cos Arg z-0*sin Arg z+(|.z.|*sin Arg z+0*cos Arg z)*<i>
      .= z by A3,Def1;
  end;
  suppose
A4: z = 0;
    hence ((n-root |.z.|)*cos((Arg z+2*PI*k)/n)+
    (n-root |.z.|)*sin((Arg z+2*PI*k)/n)*<i>)|^n = (0*cos((Arg z+2*PI*k)/n)+
    (n-root 0)*sin((Arg z+2*PI*k)/n)*<i>)|^n by A2,COMPLEX1:130,POWER:6
      .= (0+0*sin((Arg z+2*PI*k)/n)*<i>)|^n by A2,POWER:6
      .= z by A2,A4,NEWTON:16;
  end;
end;

definition
  let x be complex number;
  let n be non empty Nat;
  mode CRoot of n,x -> complex number means
    :Def2:
    it|^n = x;
  existence
  proof
    reconsider z=(n-root |. x .|)*cos((Arg x+2*PI*0)/n)+
    (n-root |. x .|)*sin((Arg x+2*PI*0)/n)*<i>
    as Element of COMPLEX by XCMPLX_0:def 2;
    take z;
    thus thesis by Th74;
  end;
end;

theorem
  for x be Element of COMPLEX for n be non empty Nat for k be Nat holds
  (n-root |. x .|)*cos((Arg x+2*PI*k)/n)+
  (n-root |. x .|)*sin((Arg x+2*PI*k)/n)*<i> is CRoot of n,x
proof
  let x be Element of COMPLEX;
  let n be non empty Nat;
  let k be Nat;
  reconsider z = (n-root |. x .|)*cos((Arg x+2*PI*k)/n)+
  (n-root |. x .|)*sin((Arg x+2*PI*k)/n)*<i> as Element of COMPLEX
  by XCMPLX_0:def 2;
  z|^n = x by Th74;
  hence thesis by Def2;
end;

theorem
  for x be Element of COMPLEX for v be CRoot of 1,x holds v = x
proof
  let x be Element of COMPLEX;
  let v be CRoot of 1,x;
  v|^1 = x by Def2;
  hence v = x by NEWTON:10;
end;

theorem
  for n be non empty Nat for v be CRoot of n,0 holds v = 0
proof
  let n be non empty Nat;
  let v be CRoot of n,0;
  defpred P[Nat] means v|^$1 = 0;
A1: ex n be non empty Nat st P[n]
  proof
    take n;
    thus thesis by Def2;
  end;
  assume
A2: v <> 0;
A3: now
    let k be non empty Nat;
    assume that
A4: k <> 1 and
A5: P[k];
    consider t be Nat such that
A6: k = t+1 by NAT_1:6;
    reconsider t as non empty Nat by A4,A6;
    take t;
    thus t < k by A6,NAT_1:13;
    v|^k = v|^t*v by A6,NEWTON:11;
    hence P[t] by A2,A5;
  end;
  P[1] from Regrwithout0(A1,A3);
  hence thesis by A2,NEWTON:10;
end;

theorem
  for n be non empty Nat for x be Element of COMPLEX
  for v be CRoot of n,x st v = 0 holds x = 0
proof
  let n be non empty Nat;
  let x be Element of COMPLEX;
  let v be CRoot of n,x;
  assume
A1: v = 0;
A2: n >= 0+1 by NAT_1:13;
  0|^n = x by A1,Def2;
  hence thesis by A2,NEWTON:16;
end;

theorem :: COMPTRIG:17: AK, 21.02.2006
  for a being real number st 0 <= a & a < 2*PI & cos(a) = 1 holds a = 0
proof
  let a be real number such that
A1: 0 <= a & a < 2*PI and
A2: cos(a) = 1;
  1*1+(sin a)*(sin a) = 1 by A2,SIN_COS:32;
  then sin a = 0;
  hence a = 0 by A1,A2,Th33,SIN_COS:82;
end;

theorem
  for z being complex number holds z = |.z.|*cos Arg z + |.z.|*sin Arg z * <i>
proof
  let z be complex number;
  per cases;
  suppose z = 0;
    hence thesis by COMPLEX1:130;
  end;
  suppose z <> 0;
    hence thesis by Def1;
  end;
end;

