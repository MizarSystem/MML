:: Several Integrability Formulas of Special Functions -- Part {II}
::  by Bo Li , Yanping Zhuang , Yanhong Men and Xiquan Liang
::
:: Received October 14, 2008
:: Copyright (c) 2008 Association of Mizar Users

environ

 vocabularies ARYTM, RELAT_1, FUNCT_1, ARYTM_1, JGRAPH_2, SEQ_1, SIN_COS,
      INTEGRA1, FDIFF_1, BOOLE, SQUARE_1, ARYTM_3, ORDINAL2, RCOMP_1, PREPOWER,
      LATTICES, SIN_COS2, PRE_TOPC, PARTFUN1, TAYLOR_1, FINSEQ_1, SIN_COS4,
      SIN_COS6, SIN_COS9, SUBSET_1;
 notations TARSKI, XBOOLE_0, SEQ_1, SIN_COS, SUBSET_1, NUMBERS, VALUED_1,
      NAT_1, XXREAL_0, REAL_1, RELAT_1, RELSET_1, PARTFUN1, PARTFUN2, RCOMP_1,
      RFUNCT_1, PSCOMP_1, INTEGRA1, FCONT_1, SQUARE_1, INTEGRA5, PREPOWER,
      TAYLOR_1, FDIFF_1, JORDAN16, SEQ_2, SIN_COS2, FDIFF_9, SIN_COS6,
      SIN_COS9, BINOP_2;
 constructors SIN_COS, TAYLOR_1, SEQ_1, REAL_1, FDIFF_1, FCONT_1, PSCOMP_1,
      BINOP_2, SQUARE_1, PREPOWER, INTEGRA5, JORDAN16, SIN_COS2, SEQ_4,
      SIN_COS9, PARTFUN1, PARTFUN2, FUNCT_1, RCOMP_1, RELAT_1, SIN_COS6,
      RFUNCT_1, VALUED_1, FDIFF_9, SIN_COS4, LIMFUNC1, NAT_1, POWER, RELSET_1;
 registrations VALUED_1, NAT_1, XBOOLE_0, NUMBERS, MEMBERED, VALUED_0,
      INTEGRA1, INT_1, RELAT_1, ORDINAL1, FUNCT_2, RCOMP_1, FCONT_1, TOPREALB,
      RELSET_1, RFUNCT_1, XREAL_0, NEWTON, SQUARE_1, FUNCT_1, XXREAL_0, SEQM_3,
      SIN_COS9, FINSET_1, TAYLOR_1, FCONT_3;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions SIN_COS, VALUED_1, SUBSET_1, SQUARE_1, SIN_COS4, SIN_COS6, TARSKI,
      FCONT_1, FDIFF_9, XBOOLE_0, SIN_COS9, RCOMP_1, LIMFUNC1, NAT_1;
 theorems PARTFUN1, RFUNCT_1, FUNCT_1, FDIFF_1, FUNCT_2, SQUARE_1, XCMPLX_1,
      INTEGRA5, INTEGRA8, SIN_COS, FDIFF_4, TAYLOR_1, VALUED_1, FCONT_1,
      JORDAN16, SIN_COS5, SIN_COS4, SIN_COS2, SIN_COS6, XXREAL_1, XBOOLE_1,
      FDIFF_5, FDIFF_7, FDIFF_8, FDIFF_9, SIN_COS9, PREPOWER, FDIFF_6;

begin :: Differentiation Formulas

 reserve r,x,x0,a,b for Real;
 reserve n,m for Element of NAT;
 reserve A for closed-interval Subset of REAL;
 reserve Z for open Subset of REAL;

A1: [#]REAL = dom (AffineMap(1/2,0))
    & [#]REAL = dom (sin*AffineMap(2,0)) by FUNCT_2:def 1;
A2: dom ((1/4)(#)(sin*AffineMap(2,0)))=[#]REAL by FUNCT_2:def 1;

:: f.x = (1/2)*x-(1/4)*sin(2*x)
theorem Th1:
  AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)) is_differentiable_on REAL &
  for x holds ((AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
    = (sin.x)^2
proof
A3: dom (AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))) = [#]REAL
      by FUNCT_2:def 1;
B:  for x st x in REAL holds
      AffineMap(1/2,0).x=(1/2)*x + 0 by JORDAN16:def 3;then
A4: AffineMap(1/2,0) is_differentiable_on REAL
      & for x st x in REAL holds ((AffineMap(1/2,0))`|REAL).x
      = 1/2 by A1,FDIFF_1:31;
A5: for x st x in REAL holds AffineMap(2,0).x=2*x + 0 by JORDAN16:def 3; then
A6: sin*AffineMap(2,0) is_differentiable_on REAL &
      for x st x in REAL holds ((sin*AffineMap(2,0))`|REAL).x
        = 2* cos.(2*x+0) by A1,FDIFF_4:37;then
A7: (1/4)(#)(sin*AffineMap(2,0))
      is_differentiable_on REAL by A2,FDIFF_1:28;
A8: for x st x in REAL holds
      (((1/4)(#)(sin*AffineMap(2,0)))`|REAL).x =(1/2)*cos(2*x)
    proof
      let x;
      assume x in REAL;
      (((1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
        =(1/4)*diff((sin*AffineMap(2,0)),x) by A2,A6,FDIFF_1:28
       .=(1/4)*((sin*AffineMap(2,0))`|REAL).x by A6,FDIFF_1:def 8
       .=(1/4)*(2*cos.(2*x+0)) by A1,A5,FDIFF_4:37
       .=(1/2)*cos(2*x);
      hence thesis;
    end;
    for x st x in REAL holds
    ((AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x = (sin.x)^2
    proof
      let x;
      assume x in REAL;
      ((AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
        =diff(AffineMap(1/2,0),x) - diff((1/4)(#)(sin*AffineMap(2,0)),x)
        by A3,A4,A7,FDIFF_1:27
       .=((AffineMap(1/2,0))`|REAL).x
        -diff((1/4)(#)(sin*AffineMap(2,0)),x) by A4,FDIFF_1:def 8
       .=(1/2)-diff((1/4)(#)(sin*AffineMap(2,0)),x)
         by B,A1,FDIFF_1:31
       .=(1/2)-(((1/4)(#)(sin*AffineMap(2,0)))`|REAL).x by A7,FDIFF_1:def 8
       .=(1/2)-(1/2)*cos(2*x) by A8
       .=(1-cos(2*x))/2
       .=(sin(x))^2 by SIN_COS5:20;
      hence thesis;
    end;
    hence thesis by A3,A4,A7,FDIFF_1:27;
end;

:: f.x = (1/2)*x+(1/4)*sin(2*x)
theorem Th2:
 AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)) is_differentiable_on REAL &
   for x holds ((AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
     = (cos.x)^2
proof
A3: dom (AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)))
      = [#]REAL by FUNCT_2:def 1;
B:  for x st x in REAL holds
      AffineMap(1/2,0).x=(1/2)*x + 0 by JORDAN16:def 3;then
A4: AffineMap(1/2,0) is_differentiable_on REAL
      & for x st x in REAL holds ((AffineMap(1/2,0))`|REAL).x
      = 1/2 by A1,FDIFF_1:31;
A5: for x st x in REAL holds
      AffineMap(2,0).x=2*x + 0 by JORDAN16:def 3; then
A6: sin*AffineMap(2,0) is_differentiable_on REAL &
      for x st x in REAL holds ((sin*AffineMap(2,0))`|REAL).x
      = 2* cos.(2*x+0) by A1,FDIFF_4:37;then
A7: (1/4)(#)(sin*AffineMap(2,0))
      is_differentiable_on REAL by A2,FDIFF_1:28;
A8: for x st x in REAL holds
      (((1/4)(#)(sin*AffineMap(2,0)))`|REAL).x =(1/2)*cos(2*x)
    proof
      let x;
      assume x in REAL;
      (((1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
       =(1/4)*diff((sin*AffineMap(2,0)),x) by A2,A6,FDIFF_1:28
      .=(1/4)*((sin*AffineMap(2,0))`|REAL).x by A6,FDIFF_1:def 8
      .=(1/4)*(2*cos.(2*x+0)) by A1,A5,FDIFF_4:37
      .=(1/2)*cos(2*x);
      hence thesis;
    end;
    for x st x in REAL holds
      ((AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
      = (cos.x)^2
    proof
      let x;
      assume x in REAL;
      ((AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
        =diff(AffineMap(1/2,0),x) + diff((1/4)(#)(sin*AffineMap(2,0)),x)
         by A3,A4,A7,FDIFF_1:26
       .=((AffineMap(1/2,0))`|REAL).x
         +diff((1/4)(#)(sin*AffineMap(2,0)),x) by A4,FDIFF_1:def 8
       .=(1/2)+diff((1/4)(#)(sin*AffineMap(2,0)),x)
          by B,A1,FDIFF_1:31
       .=(1/2)+(((1/4)(#)(sin*AffineMap(2,0)))`|REAL).x by A7,FDIFF_1:def 8
       .=(1/2)+(1/2)*cos(2*x) by A8
       .=(1+cos(2*x))/2
       .=(cos(x))^2 by SIN_COS5:21;
      hence thesis;
    end;
    hence thesis by A3,A4,A7,FDIFF_1:26;
end;

:: f.x = (1/(n+1))*(sin.x)^(n+1)
theorem Th3:
  (1/(n+1))(#)( #Z (n+1)*sin) is_differentiable_on REAL
  & for x holds (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL).x
  = ((sin.x) #Z n)*cos.x
proof
A1: [#]REAL = dom ( #Z (n+1)) &
    [#]REAL = dom ( #Z (n+1)*sin) &
    [#]REAL = dom((1/(n+1))(#)( #Z (n+1)*sin)) by FUNCT_2:def 1;
A:  #Z (n+1)*sin is_differentiable_in x0
    proof
      sin is_differentiable_in x0 by SIN_COS:69;
      hence thesis by TAYLOR_1:3;
    end;
A2: #Z (n+1)*sin is_differentiable_on REAL
    proof
      for x0 st x0 in REAL holds #Z (n+1)*sin is_differentiable_in x0 by A;
      hence thesis by A1,FDIFF_1:16;
    end;
A3: for x st x in REAL holds
      (( #Z (n+1)*sin)`|REAL).x=(n+1) * ((sin.x) #Z n)*cos.x
    proof
      let x;
      assume x in REAL;
      set m=n+1;
      sin is_differentiable_in x by SIN_COS:69;then
      diff(( #Z m*sin),x) = m * (sin.x) #Z (m-1) *diff(sin,x) by TAYLOR_1:3
       .= m * (sin.x) #Z (m-1) *(cos .x) by SIN_COS:69;
      hence thesis by A2,FDIFF_1:def 8;
    end;
    for x st x in REAL holds (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL).x
       =(sin.x) #Z n *cos.x
    proof
      let x;
      assume x in REAL;
      (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL).x
        =(1/(n+1))*diff( #Z (n+1)*sin,x) by A1,A2,FDIFF_1:28
       .=(1/(n+1))*((( #Z (n+1)*sin)`|REAL).x) by A2,FDIFF_1:def 8
       .=(1/(n+1))*((n+1) * (sin.x) #Z n *cos.x) by A3
       .=(1/(n+1))*(n+1) * (sin.x) #Z n *cos.x
       .=(n+1)/(n+1) * (sin.x) #Z n *cos.x by XCMPLX_1:100
       .=1*(sin.x) #Z n *cos.x by XCMPLX_1:60;
      hence thesis;
    end;
    hence thesis by A1,A2,FDIFF_1:28;
end;

:: f.x = (-1/(n+1))*(cos.x)^(n+1)
theorem Th4:
  (-1/(n+1))(#)( #Z (n+1)*cos) is_differentiable_on REAL
  & for x holds (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL).x
  = ((cos.x) #Z n)*sin.x
proof
A1: [#]REAL = dom ( #Z (n+1)) & REAL = dom ( #Z (n+1)*cos) &
    [#]REAL = dom((-1/(n+1))(#)( #Z (n+1)*cos)) by FUNCT_2:def 1;
A:  #Z (n+1)*cos is_differentiable_in x0
    proof
      cos is_differentiable_in x0 by SIN_COS:68;
      hence thesis by TAYLOR_1:3;
    end;
A2: #Z (n+1)*cos is_differentiable_on REAL
    proof
      for x0 st x0 in REAL holds #Z (n+1)*cos is_differentiable_in x0 by A;
      hence thesis by A1,FDIFF_1:16;
    end;
A3: for x st x in REAL holds
      (( #Z (n+1)*cos)`|REAL).x=(-(n+1)) * ((cos.x) #Z n)*sin.x
    proof
      let x;
      assume x in REAL;
      set m=n+1;
      cos is_differentiable_in x by SIN_COS:68;then
      diff(( #Z m*cos),x) = m * (cos.x) #Z (m-1) *diff(cos,x) by TAYLOR_1:3
        .= m * (cos.x) #Z (m-1) *(-sin .x) by SIN_COS:68
        .= (-m) * (cos.x) #Z (m-1) *sin .x;
      hence thesis by A2,FDIFF_1:def 8;
    end;
    for x st x in REAL holds (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL).x
       =(cos.x) #Z n *sin.x
    proof
      let x;
      assume x in REAL;
      (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL).x
       =(-1/(n+1))*diff( #Z (n+1)*cos,x) by A1,A2,FDIFF_1:28
      .=(-1/(n+1))*((( #Z (n+1)*cos)`|REAL).x) by A2,FDIFF_1:def 8
      .=(-1/(n+1))*((-(n+1)) * (cos.x) #Z n *sin.x) by A3
      .=(1/(n+1))*(n+1) * (cos.x) #Z n *sin.x
      .=(n+1)/(n+1) * (cos.x) #Z n *sin.x by XCMPLX_1:100
      .=1*(cos.x) #Z n *sin.x by XCMPLX_1:60;
      hence thesis;
    end;
    hence thesis by A1,A2,FDIFF_1:28;
end;

:: f.x=sin.((m+n)*x)/(2*(m+n))+sin.((m-n)*x)/(2*(m-n))
theorem Th5:
  m+n<>0 & m-n<>0 implies ((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
  (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))) is_differentiable_on REAL
  & for x holds (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
  (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x = cos.(m*x) *cos.(n*x)
proof
    assume A: m+n <> 0 & m-n<>0;
A1: [#]REAL = dom ((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))
    & dom ((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))) = [#]REAL
    & dom (sin*AffineMap(m+n,0)) = [#]REAL
    & dom (sin*AffineMap(m-n,0)) = [#]REAL &
    dom ((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
    (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))) = [#]REAL by FUNCT_2:def 1;
A2: for x st x in REAL holds
    AffineMap(m+n,0).x=(m+n)*x + 0 by JORDAN16:def 3; then
A3: sin*AffineMap(m+n,0) is_differentiable_on REAL &
    for x st x in REAL holds ((sin*AffineMap(m+n,0))`|REAL).x
      = (m+n)* cos.((m+n)*x+0) by A1,FDIFF_4:37;
A4: (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))
      is_differentiable_on REAL by A1,A3,FDIFF_1:28;
A5: for x st x in REAL holds
      (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x=(1/2)*cos((m+n)*x)
    proof
      let x;
      assume x in REAL;
      (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
       =(1/(2*(m+n)))*diff((sin*AffineMap(m+n,0)),x)
         by A1,A3,FDIFF_1:28
      .=(1/(2*(m+n)))*((sin*AffineMap(m+n,0))`|REAL).x by A3,FDIFF_1:def 8
      .=(1/(2*(m+n)))*((m+n)* cos.((m+n)*x+0)) by A1,A2,FDIFF_4:37
      .=(m+n)*(1/(2*(m+n)))* cos.((m+n)*x+0)
      .=((1*(m+n))/(2*(m+n)))* cos.((m+n)*x+0) by XCMPLX_1:75
      .=(1/2)* cos((m+n)*x) by A,XCMPLX_1:92;
      hence thesis;
    end;
A6: for x st x in REAL holds
    AffineMap(m-n,0).x=(m-n)*x + 0 by JORDAN16:def 3; then
A7: sin*AffineMap(m-n,0) is_differentiable_on REAL &
    for x st x in REAL holds ((sin*AffineMap(m-n,0))`|REAL).x
      = (m-n)* cos.((m-n)*x+0) by A1,FDIFF_4:37;
A8: (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))
      is_differentiable_on REAL by A1,A7,FDIFF_1:28;
A9: for x st x in REAL holds
      (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x=(1/2)*cos((m-n)*x)
    proof
      let x;
      assume x in REAL;
      (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
        =(1/(2*(m-n)))*diff((sin*AffineMap(m-n,0)),x)
          by A1,A7,FDIFF_1:28
       .=(1/(2*(m-n)))*((sin*AffineMap(m-n,0))`|REAL).x by A7,FDIFF_1:def 8
       .=(1/(2*(m-n)))*((m-n)* cos.((m-n)*x+0)) by A1,A6,FDIFF_4:37
       .=(m-n)*(1/(2*(m-n)))* cos.((m-n)*x+0)
       .=((1*(m-n))/(2*(m-n)))* cos.((m-n)*x+0) by XCMPLX_1:75
       .=(1/2)* cos((m-n)*x) by A,XCMPLX_1:92;
      hence thesis;
    end;
    for x st x in REAL holds (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
      (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
     = cos.(m*x) *cos.(n*x)
    proof
      let x;
      assume x in REAL;
      (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
        (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
         = diff((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)),x)
         +diff((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)),x)
           by A1,A4,A8,FDIFF_1:26
        .= (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
         +diff((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)),x) by A4,FDIFF_1:def 8
        .= (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
         +(((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
           by A8,FDIFF_1:def 8
        .= (1/2)*cos((m+n)*x)+(((1/(2*(m-n)))
          (#)(sin*AffineMap(m-n,0)))`|REAL).x by A5
        .= (1/2)*cos((m+n)*x)+(1/2)* cos((m-n)*x) by A9
        .= (1/2)*(cos((m+n)*x)+cos((m-n)*x))
        .= (1/2)*(2*(cos(((m+n)*x+(m-n)*x)/2)
           *cos(((m+n)*x-(m-n)*x)/2))) by SIN_COS4:21
        .= cos.(m*x)*cos.(n*x);
       hence thesis;
     end;
     hence thesis by A1,A4,A8,FDIFF_1:26;
end;

:: f.x=sin.((m-n)*x)/(2*(m-n))-sin.((m+n)*x)/(2*(m+n))
theorem Th6:
  m+n<>0 & m-n<>0 implies ((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
  (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))) is_differentiable_on REAL
  & for x holds (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
  (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
  = sin.(m*x)*sin.(n*x)
proof
    assume A: m+n <> 0 & m-n <> 0;
A1: REAL = dom ((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))
    & dom ((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))) = [#]REAL
    & dom (sin*AffineMap(m+n,0)) = [#]REAL
    & dom (sin*AffineMap(m-n,0)) = [#]REAL &
    dom ((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
    (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))) = [#]REAL by FUNCT_2:def 1;
A2: for x st x in REAL holds
      AffineMap(m+n,0).x=(m+n)*x + 0 by JORDAN16:def 3; then
A3: sin*AffineMap(m+n,0) is_differentiable_on REAL &
    for x st x in REAL holds ((sin*AffineMap(m+n,0))`|REAL).x
      = (m+n)* cos.((m+n)*x+0) by A1,FDIFF_4:37;
A4: (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))
      is_differentiable_on REAL by A1,A3,FDIFF_1:28;
A5: for x st x in REAL holds
      (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x=(1/2)*cos((m+n)*x)
    proof
      let x;
      assume x in REAL;
      (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
        =(1/(2*(m+n)))*diff((sin*AffineMap(m+n,0)),x)
         by A1,A3,FDIFF_1:28
      .=(1/(2*(m+n)))*((sin*AffineMap(m+n,0))`|REAL).x by A3,FDIFF_1:def 8
      .=(1/(2*(m+n)))*((m+n)* cos.((m+n)*x+0)) by A1,A2,FDIFF_4:37
      .=(m+n)*(1/(2*(m+n)))* cos.((m+n)*x+0)
      .=((1*(m+n))/(2*(m+n)))* cos.((m+n)*x+0) by XCMPLX_1:75
      .=(1/2)* cos((m+n)*x) by A,XCMPLX_1:92;
      hence thesis;
    end;
A6: for x st x in REAL holds
      AffineMap(m-n,0).x=(m-n)*x + 0 by JORDAN16:def 3; then
A7: sin*AffineMap(m-n,0) is_differentiable_on REAL &
    for x st x in REAL holds (sin*(AffineMap(m-n,0))`|REAL).x
      = (m-n)* cos.((m-n)*x+0) by A1,FDIFF_4:37;
A8: (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))
      is_differentiable_on REAL by A1,A7,FDIFF_1:28;
A9: for x st x in REAL holds
      (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x=(1/2)*cos((m-n)*x)
    proof
      let x;
      assume x in REAL;
      (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
        =(1/(2*(m-n)))*diff((sin*AffineMap(m-n,0)),x)
         by A1,A7,FDIFF_1:28
       .=(1/(2*(m-n)))*((sin*AffineMap(m-n,0))`|REAL).x by A7,FDIFF_1:def 8
       .=(1/(2*(m-n)))*((m-n)* cos.((m-n)*x+0)) by A1,A6,FDIFF_4:37
       .=(m-n)*(1/(2*(m-n)))* cos.((m-n)*x+0)
       .=((1*(m-n))/(2*(m-n)))* cos.((m-n)*x+0) by XCMPLX_1:75
       .=(1/2)* cos((m-n)*x) by A,XCMPLX_1:92;
      hence thesis;
    end;
    for x st x in REAL holds
      (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
      (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x = sin.(m*x) *sin.(n*x)
    proof
      let x;
      assume x in REAL;
      (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
         (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
       = diff((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)),x)
        -diff((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)),x)
         by A1,A4,A8,FDIFF_1:27
      .= (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
        -diff((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)),x)
         by A8,FDIFF_1:def 8
      .= (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
        -(((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
           by A4,FDIFF_1:def 8
      .= (1/2)*cos((m-n)*x)-(((1/(2*(m+n)))
           (#)(sin*AffineMap(m+n,0)))`|REAL).x by A9
      .= (1/2)*cos((m-n)*x)-(1/2)* cos((m+n)*x) by A5
      .= (1/2)*(cos((m-n)*x)-cos((m+n)*x))
      .= (1/2)*(-2*(sin(((m-n)*x+(m+n)*x)/2)
         *sin(((m-n)*x-(m+n)*x)/2))) by SIN_COS4:22
      .= (1/2)*(-2*(sin(m*x)*sin(-(n*x))))
      .= (1/2)*(-2*(sin(m*x)*(-sin(n*x)))) by SIN_COS:34
      .= sin.(m*x)*sin.(n*x);
      hence thesis;
    end;
    hence thesis by A1,A4,A8,FDIFF_1:27;
end;

:: f.x=-cos.((m+n)*x)/(2*(m+n))-cos.((m-n)*x)/(2*(m-n))
theorem Th7:
  m+n<>0 & m-n<>0 implies ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0))) -
  (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0))) is_differentiable_on REAL
  & for x holds ((-((1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
  (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL).x
    = sin.(m*x) * cos.(n*x)
proof
    assume A: m+n <> 0 & m-n <> 0;
A1: dom (-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0))) = [#]REAL
    & dom ((1/(2*(m+n)))(#)(cos*AffineMap(m+n,0))) = [#]REAL
    & dom ((1/(2*(m-n)))(#)(cos*AffineMap(m-n,0))) = [#]REAL
    & dom (cos*AffineMap(m+n,0)) = [#]REAL
    & dom (cos*AffineMap(m-n,0)) = [#]REAL &
    REAL = dom ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
    (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0))) by FUNCT_2:def 1;
A2: for x st x in REAL holds
      AffineMap(m+n,0).x=(m+n)*x + 0 by JORDAN16:def 3; then
A3: cos*AffineMap(m+n,0) is_differentiable_on REAL &
      for x st x in REAL holds ((cos*AffineMap(m+n,0))`|REAL).x
      = -(m+n) * sin.((m+n)*x+0) by A1,FDIFF_4:38;
Z:  (-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0))) =
    (-1)(#) ((1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)));
    (1/(2*(m+n)))(#)(cos*AffineMap(m+n,0))
      is_differentiable_on REAL by A1,A3,FDIFF_1:28;then
A4: (-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))
      is_differentiable_on REAL by A1,Z,FDIFF_1:28;
A5: for x st x in REAL holds
      ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))`|REAL).x=(1/2)*sin((m+n)*x)
    proof
      let x;
      assume x in REAL;
C:    dom (((-1)/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))=[#]REAL by FUNCT_2:def 1;
      ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))`|REAL).x
        =((((-1)*(1/(2*(m+n))))
      (#)(cos*AffineMap(m+n,0)))`|REAL).x by RFUNCT_1:29
    .=(((-(1/(2*(m+n))))(#)(cos*AffineMap(m+n,0)))`|REAL).x
    .=((((-1)/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))`|REAL).x by XCMPLX_1:188
    .=((-1)/(2*(m+n)))*diff((cos*AffineMap(m+n,0)),x)
          by C,A3,FDIFF_1:28
    .=((-1)/(2*(m+n)))*((cos*AffineMap(m+n,0))`|REAL).x by A3,FDIFF_1:def 8
    .=((-1)/(2*(m+n)))*(-(m+n)* sin.((m+n)*x+0)) by A1,A2,FDIFF_4:38
    .=(-(-1)/(2*(m+n)))*(m+n)* sin.((m+n)*x+0)
    .=(1/(2*(m+n)))*(m+n)* sin.((m+n)*x+0) by XCMPLX_1:191
    .=((1*(m+n))/(2*(m+n)))* sin.((m+n)*x+0) by XCMPLX_1:75
    .=(1/2)* sin((m+n)*x) by A,XCMPLX_1:92;
     hence thesis;
    end;
A6: for x st x in REAL holds
      AffineMap(m-n,0).x=(m-n)*x + 0 by JORDAN16:def 3; then
A7: cos*(AffineMap(m-n,0)) is_differentiable_on REAL &
      for x st x in REAL holds ((cos*AffineMap(m-n,0))`|REAL).x
       = -(m-n)* sin.((m-n)*x+0) by A1,FDIFF_4:38;
A8: (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0))
      is_differentiable_on REAL by A1,A7,FDIFF_1:28;
A9: for x st x in REAL holds
      (((1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL).x=-(1/2)*sin((m-n)*x)
    proof
      let x;
      assume x in REAL;
      (((1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL).x
        =(1/(2*(m-n)))*diff((cos*AffineMap(m-n,0)),x)
         by A1,A7,FDIFF_1:28
       .=(1/(2*(m-n)))*((cos*AffineMap(m-n,0))`|REAL).x by A7,FDIFF_1:def 8
       .=(1/(2*(m-n)))*(-(m-n)* sin.((m-n)*x+0)) by A1,A6,FDIFF_4:38
       .=(-1/(2*(m-n)))*(m-n)* sin.((m-n)*x+0)
       .=((-1)/(2*(m-n)))*(m-n)* sin.((m-n)*x+0) by XCMPLX_1:188
       .=(((-1)*(m-n))/(2*(m-n)))* sin.((m-n)*x+0) by XCMPLX_1:75
       .=(-1)/2* sin((m-n)*x) by A,XCMPLX_1:92;
      hence thesis;
    end;
    for x st x in REAL holds
      ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0))-
    (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL).x
       = sin.(m*x) *cos.(n*x)
    proof
      let x;
      assume x in REAL;
      ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0))-
      (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL).x
         = diff((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0))),x)
         -diff((1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)),x)
          by A1,A4,A8,FDIFF_1:27
        .= ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))`|REAL).x
          -diff((1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)),x) by A4,FDIFF_1:def 8
        .= ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))`|REAL).x
          -(((1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL).x
           by A8,FDIFF_1:def 8
        .= (1/2)*sin((m+n)*x)-(((1/(2*(m-n)))
          (#)(cos*AffineMap(m-n,0)))`|REAL).x by A5
        .= (1/2)*sin((m+n)*x)-(-(1/2)* sin((m-n)*x)) by A9
        .= (1/2)*(sin((m+n)*x)+sin((m-n)*x))
        .= (1/2)*(2*(cos(((m+n)*x-(m-n)*x)/2)
          *sin(((m+n)*x+(m-n)*x)/2))) by SIN_COS4:19
        .= sin.(m*x)*cos.(n*x);
      hence thesis;
    end;
    hence thesis by A1,A4,A8,FDIFF_1:27;
end;

:: f.x = (1/(n^2))*sin.(n*x)-(1/n)*x*cos.(n*x)
theorem Th8:
 n<>0 implies ((1/(n^2))(#)(sin*AffineMap(n,0))-
(AffineMap(1/n,0))(#)(cos*AffineMap(n,0))) is_differentiable_on REAL
 & for x holds (((1/(n^2))(#)(sin*AffineMap(n,0))-
(AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL).x=x*sin.(n*x)
proof
 assume A: n<>0;
A1:dom ((1/(n^2))(#)(sin*AffineMap(n,0)))=REAL
 & dom ((AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))=REAL
 & dom (((1/(n^2))(#)(sin*AffineMap(n,0))-
 (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))) = [#]REAL
& dom (AffineMap(1/n,0))=REAL & dom (sin*AffineMap(n,0)) = [#]REAL
 & dom (cos*AffineMap(n,0)) = [#]REAL by FUNCT_2:def 1;
A2: for x st x in REAL holds
    AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3;then
A3: sin*(AffineMap(n,0)) is_differentiable_on REAL &
  for x st x in REAL holds ((sin*AffineMap(n,0))`|REAL).x
   = n* cos.(n*x+0) by A1,FDIFF_4:37;then
A4: (1/(n^2))(#)(sin*AffineMap(n,0))
  is_differentiable_on REAL by A1,FDIFF_1:28;
A5: for x st x in REAL holds
 (((1/(n^2))(#)(sin*AffineMap(n,0)))`|REAL).x = (1/n)*cos(n*x)
  proof
    let x;
    assume x in REAL;
    (((1/(n^2))(#)(sin*AffineMap(n,0)))`|REAL).x
  =(1/(n^2))*diff((sin*AffineMap(n,0)),x) by A1,A3,FDIFF_1:28
 .=(1/(n^2))*((sin*AffineMap(n,0))`|REAL).x by A3,FDIFF_1:def 8
 .=(1/(n^2))*(n * cos.(n*x+0)) by A1,A2,FDIFF_4:37
 .= n*(1/(n*n))*(cos.(n*x+0))
 .= (n*1)/(n*n)*(cos.(n*x+0)) by XCMPLX_1:75
 .= (1/n)*(cos.(n*x+0)) by A,XCMPLX_1:92;
   hence thesis;
   end;
B1: for x st x in REAL holds
 AffineMap(1/n,0).x=(1/n)*x + 0 by JORDAN16:def 3;then
A6: AffineMap(1/n,0) is_differentiable_on REAL & for x st x in REAL
 holds ((AffineMap((1/n),0))`|REAL).x = 1/n by A1,FDIFF_1:31;
A7: cos*AffineMap(n,0) is_differentiable_on REAL &
  for x st x in REAL holds ((cos*AffineMap(n,0))`|REAL).x
   = -n* sin.(n*x+0) by A1,A2,FDIFF_4:38;then
A8: ((AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))
  is_differentiable_on REAL by A1,A6,FDIFF_1:29;
A9: for x st x in REAL holds
  (((AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL).x
  = (1/n)*cos.(n*x)-x* sin.(n*x)
 proof
   let x;
   assume x in REAL;
  (((AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL).x
  = ((cos*AffineMap(n,0)).x)*diff(AffineMap(1/n,0),x)
   + (AffineMap(1/n,0).x)*diff(cos*(AffineMap(n,0)),x)
     by A1,A6,A7,FDIFF_1:29
 .= ((cos*AffineMap(n,0)).x)*((AffineMap(1/n,0)`|REAL).x)
    +(AffineMap(1/n,0).x)*diff((cos*AffineMap(n,0)),x)
     by A6,FDIFF_1:def 8
 .= ((cos*AffineMap(n,0)).x)*(1/n)+(AffineMap(1/n,0).x)
     *diff((cos*AffineMap(n,0)),x) by A1,B1,FDIFF_1:31
 .= ((cos*AffineMap(n,0)).x)*(1/n)+(AffineMap(1/n,0).x)
     *(((cos*AffineMap(n,0))`|REAL).x) by A7,FDIFF_1:def 8
 .= ((cos*AffineMap(n,0)).x)*(1/n)
    +(AffineMap(1/n,0).x)*(-n* sin.(n*x+0)) by A1,A2,FDIFF_4:38
 .= ((cos*AffineMap(n,0)).x)*(1/n)
     +((1/n)*x + 0)*(-n* sin.(n*x+0)) by JORDAN16:def 3
 .= (cos.(AffineMap(n,0).x))*(1/n)
     +((1/n)*x)*(-n* sin.(n*x+0)) by A1,FUNCT_1:22
 .= (1/n)*cos.(n*x)+(-((1/n)*n*x* sin.(n*x))) by JORDAN16:def 3
 .= (1/n)*cos.(n*x)+(-(1*x* sin.(n*x))) by A,XCMPLX_1:88
 .= (1/n)*cos.(n*x)-x* sin.(n*x);
 hence thesis;
 end;
 for x st x in REAL holds (((1/(n^2))(#)(sin*AffineMap(n,0))-
 (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL).x
 = x*sin.(n*x)
proof
 let x;
 assume x in REAL;
   (((1/(n^2))(#)(sin*AffineMap(n,0))-
   (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL).x
  = diff((1/(n^2))(#)(sin*AffineMap(n,0)),x)
   - diff((AffineMap(1/n,0))(#)(cos*AffineMap(n,0)),x)
      by A1,A4,A8,FDIFF_1:27
 .= ((((1/(n^2))(#)(sin*AffineMap(n,0)))`|REAL).x)
    - diff(AffineMap(1/n,0)(#)(cos*AffineMap(n,0)),x)
       by A4,FDIFF_1:def 8
 .= (1/n)*cos(n*x)-diff(AffineMap(1/n,0)
      (#)(cos*AffineMap(n,0)),x) by A5
 .= (1/n)*cos(n*x)-(((AffineMap(1/n,0)
    (#)(cos*AffineMap(n,0)))`|REAL).x) by A8,FDIFF_1:def 8
 .= (1/n)*cos(n*x)-((1/n)*cos.(n*x)-x* sin.(n*x)) by A9
 .= x* sin.(n*x);
 hence thesis;
end;
hence thesis by A1,A4,A8,FDIFF_1:27;
end;

:: f.x = (1/(n^2))*cos.(n*x)+(1/n)*x*sin.(n*x)
theorem Th9:
  n<>0 implies ((1/(n^2))(#)(cos*AffineMap(n,0))+
  (AffineMap(1/n,0))(#)(sin*AffineMap(n,0))) is_differentiable_on REAL
  & for x holds (((1/(n^2))(#)(cos*AffineMap(n,0))+
  (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL).x=x*cos.(n*x)
proof
   assume A: n<>0;
A1:dom ((1/(n^2))(#)(cos*AffineMap(n,0)))=REAL
   & dom ((AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))=REAL
   & dom (((1/(n^2))(#)(cos*AffineMap(n,0))+
   (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))) = [#]REAL
   & dom (AffineMap(1/n,0))=REAL & dom (sin*AffineMap(n,0)) = [#]REAL
   & dom (cos*AffineMap(n,0)) = [#]REAL by FUNCT_2:def 1;
A2:for x st x in REAL holds
     AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3;then
A3:cos*AffineMap(n,0) is_differentiable_on REAL &
    for x st x in REAL holds ((cos*AffineMap(n,0))`|REAL).x
     = -n* sin.(n*x+0) by A1,FDIFF_4:38;then
A4:(1/(n^2))(#)(cos*AffineMap(n,0))
     is_differentiable_on REAL by A1,FDIFF_1:28;
A5:for x st x in REAL holds
     (((1/(n^2))(#)(cos*AffineMap(n,0)))`|REAL).x = -(1/n)*sin(n*x)
   proof
     let x;
     assume x in REAL;
     (((1/(n^2))(#)(cos*AffineMap(n,0)))`|REAL).x
      =(1/(n^2))*diff((cos*AffineMap(n,0)),x) by A1,A3,FDIFF_1:28
 .=(1/(n^2))*((cos*AffineMap(n,0))`|REAL).x by A3,FDIFF_1:def 8
 .=(1/(n^2))*(-n * sin.(n*x+0)) by A1,A2,FDIFF_4:38
 .=(-1)*(n*(1/(n*n)))*(sin.(n*x+0))
 .=(-1)*(n/((n*n)/1))*(sin.(n*x+0)) by XCMPLX_1:80
 .=(-1)*((n*1)/(n*n))*(sin.(n*x+0))
 .=(-1)*(1/n)*(sin.(n*x+0)) by A,XCMPLX_1:92;
   hence thesis;
   end;
B1: for x st x in REAL holds
 AffineMap(1/n,0).x=(1/n)*x + 0 by JORDAN16:def 3;then
A6: AffineMap(1/n,0) is_differentiable_on REAL & for x st x in REAL
 holds ((AffineMap((1/n),0))`|REAL).x = 1/n by A1,FDIFF_1:31;
A7: sin*AffineMap(n,0) is_differentiable_on REAL &
  for x st x in REAL holds ((sin*AffineMap(n,0))`|REAL).x
   = n* cos.(n*x+0) by A1,A2,FDIFF_4:37;then
A8: ((AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))
  is_differentiable_on REAL by A1,A6,FDIFF_1:29;
A9: for x st x in REAL holds
  (((AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL).x
  = (1/n)*sin.(n*x)+x* cos.(n*x)
 proof
   let x;
   assume x in REAL;
  (((AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL).x
  = ((sin*AffineMap(n,0)).x)*diff((AffineMap(1/n,0)),x)
   + ((AffineMap(1/n,0)).x)*diff((sin*AffineMap(n,0)),x)
     by A1,A6,A7,FDIFF_1:29
 .= ((sin*AffineMap(n,0)).x)*((AffineMap(1/n,0)`|REAL).x)
    +((AffineMap(1/n,0)).x)*diff((sin*AffineMap(n,0)),x)
     by A6,FDIFF_1:def 8
 .= ((sin*AffineMap(n,0)).x)*(1/n)+(AffineMap(1/n,0).x)
     *diff((sin*AffineMap(n,0)),x)
     by A1,B1,FDIFF_1:31
 .= ((sin*AffineMap(n,0)).x)*(1/n)+(AffineMap(1/n,0).x)
     *(((sin*AffineMap(n,0))`|REAL).x) by A7,FDIFF_1:def 8
 .= ((sin*AffineMap(n,0)).x)*(1/n)
    +(AffineMap(1/n,0).x)*(n* cos.(n*x+0))
    by A1,A2,FDIFF_4:37
 .= ((sin*AffineMap(n,0)).x)*(1/n)
     +((1/n)*x + 0)*(n* cos.(n*x+0)) by JORDAN16:def 3
 .= (sin.(AffineMap(n,0).x))*(1/n)
     +((1/n)*x)*(n* cos.(n*x+0)) by A1,FUNCT_1:22
 .= (1/n)*sin.(n*x)+(1/n)*n*x* cos.(n*x) by JORDAN16:def 3
 .= (1/n)*sin.(n*x)+1*x* cos.(n*x) by A,XCMPLX_1:88
 .= (1/n)*sin.(n*x)+x* cos.(n*x);
 hence thesis;
 end;
 for x st x in REAL holds (((1/(n^2))(#)(cos*AffineMap(n,0))+
 (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL).x = x*cos.(n*x)
proof
 let x;
 assume x in REAL;
   (((1/(n^2))(#)(cos*AffineMap(n,0))+
   (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL).x
  = diff((1/(n^2))(#)(cos*AffineMap(n,0)),x)
   + diff((AffineMap(1/n,0))(#)(sin*AffineMap(n,0)),x)
      by A1,A4,A8,FDIFF_1:26
 .= ((((1/(n^2))(#)(cos*AffineMap(n,0)))`|REAL).x)
    + diff((AffineMap(1/n,0))(#)(sin*AffineMap(n,0)),x)
       by A4,FDIFF_1:def 8
 .= -(1/n)*sin(n*x)+diff(AffineMap(1/n,0)
      (#)(sin*AffineMap(n,0)),x) by A5
 .= -(1/n)*sin(n*x)+(((AffineMap(1/n,0)
    (#)(sin*AffineMap(n,0)))`|REAL).x) by A8,FDIFF_1:def 8
 .= -(1/n)*sin(n*x)+((1/n)*sin.(n*x)+x* cos.(n*x)) by A9
 .= x* cos.(n*x);
 hence thesis;
end;
hence thesis by A1,A4,A8,FDIFF_1:26;
end;

:: f.x = x*cosh.x-sinh.x
theorem Th10:
 (AffineMap(1,0))(#)cosh-sinh is_differentiable_on REAL
 & for x holds (((AffineMap(1,0))(#)cosh-sinh)`|REAL).x=x*sinh.x
proof
A1: dom ((AffineMap(1,0))(#)cosh-sinh) = [#]REAL
 & dom ((AffineMap(1,0))(#)cosh) = [#]REAL
 & dom (AffineMap(1,0)) = [#]REAL by FUNCT_2:def 1;
A2: for x st x in REAL holds
 AffineMap(1,0).x=1*x + 0 by JORDAN16:def 3;then
A3: AffineMap(1,0) is_differentiable_on REAL & for x st x in REAL
 holds ((AffineMap(1,0))`|REAL).x = 1 by A1,FDIFF_1:31;then
A4: ((AffineMap(1,0))(#)cosh)
  is_differentiable_on REAL by A1,SIN_COS2:35,FDIFF_1:29;
A5: for x st x in REAL holds
  (((AffineMap(1,0))(#)cosh)`|REAL).x = cosh.x+x*sinh.x
 proof
   let x;
   assume x in REAL;
  (((AffineMap(1,0))(#)cosh)`|REAL).x
  = cosh.x*diff((AffineMap(1,0)),x)+((AffineMap(1,0)).x)
     *diff(cosh,x) by A1,A3,SIN_COS2:35,FDIFF_1:29
 .= cosh.x*(((AffineMap(1,0))`|REAL).x)
    +((AffineMap(1,0)).x)*diff(cosh,x) by A3,FDIFF_1:def 8
 .= cosh.x*1+((AffineMap(1,0)).x)*diff(cosh,x)
     by A1,A2,FDIFF_1:31
 .= cosh.x+((AffineMap(1,0)).x)*sinh.x by SIN_COS2:35
 .= cosh.x+(1*x + 0)*sinh.x by JORDAN16:def 3
 .= cosh.x+x*sinh.x;
 hence thesis;
 end;
 for x st x in REAL holds
  (((AffineMap(1,0))(#)cosh-sinh)`|REAL).x = x*sinh.x
 proof
   let x;
   assume x in REAL;
  (((AffineMap(1,0))(#)cosh - sinh)`|REAL).x
 = diff(((AffineMap(1,0))(#)cosh),x)
  - diff(sinh,x) by A1,A4,SIN_COS2:34,FDIFF_1:27
.= (((AffineMap(1,0))(#)cosh)`|REAL).x
  - diff(sinh,x) by A4,FDIFF_1:def 8
.= cosh.x+x*sinh.x-diff(sinh,x) by A5
.= cosh.x+x*sinh.x-cosh.x by SIN_COS2:34
.= x*sinh.x;
 hence thesis;
 end;
hence thesis by A1,A4,SIN_COS2:34,FDIFF_1:27;
end;

:: f.x = x*sinh.x-cosh.x
theorem Th11:
 (AffineMap(1,0))(#)sinh-cosh is_differentiable_on REAL
 & for x holds (((AffineMap(1,0))(#)sinh-cosh)`|REAL).x=x*cosh.x
proof
A1: dom ((AffineMap(1,0))(#)sinh-cosh) = [#]REAL
 & dom ((AffineMap(1,0))(#)sinh) = [#]REAL
 & dom (AffineMap(1,0)) = [#]REAL by FUNCT_2:def 1;
A2: for x st x in REAL holds
 AffineMap(1,0).x=1*x + 0 by JORDAN16:def 3;then
A3: AffineMap(1,0) is_differentiable_on REAL & for x st x in REAL
 holds ((AffineMap(1,0))`|REAL).x = 1 by A1,FDIFF_1:31;then
A4: ((AffineMap(1,0))(#)sinh)
  is_differentiable_on REAL by A1,SIN_COS2:34,FDIFF_1:29;
A5: for x st x in REAL holds
  (((AffineMap(1,0))(#)sinh)`|REAL).x = sinh.x+x*cosh.x
 proof
   let x;
   assume x in REAL;
  (((AffineMap(1,0))(#)sinh)`|REAL).x
  = sinh.x*diff((AffineMap(1,0)),x)+((AffineMap(1,0)).x)
     *diff(sinh,x) by A1,A3,SIN_COS2:34,FDIFF_1:29
 .= sinh.x*(((AffineMap(1,0))`|REAL).x)
    +((AffineMap(1,0)).x)*diff(sinh,x) by A3,FDIFF_1:def 8
 .= sinh.x*1+((AffineMap(1,0)).x)*diff(sinh,x)
     by A1,A2,FDIFF_1:31
 .= sinh.x+((AffineMap(1,0)).x)*(cosh.x) by SIN_COS2:34
 .= sinh.x+(1*x + 0)*cosh.x by JORDAN16:def 3
 .= sinh.x+x*cosh.x;
 hence thesis;
 end;
 for x st x in REAL holds
  (((AffineMap(1,0))(#)sinh-cosh)`|REAL).x = x*(cosh.x)
 proof
   let x;
   assume x in REAL;
   (((AffineMap(1,0))(#)sinh - cosh)`|REAL).x
     = diff(((AffineMap(1,0))(#)sinh),x)
     - diff(cosh,x) by A1,A4,SIN_COS2:35,FDIFF_1:27
    .= (((AffineMap(1,0))(#)sinh)`|REAL).x
     - diff(cosh,x) by A4,FDIFF_1:def 8
    .= sinh.x+x*cosh.x-diff(cosh,x) by A5
    .= sinh.x+x*cosh.x-sinh.x by SIN_COS2:35
    .= x*cosh.x;
   hence thesis;
 end;
 hence thesis by A1,A4,SIN_COS2:35,FDIFF_1:27;
end;

:: f.x = (1/(a*(n+1)))*(a*x+b)^(n+1)
theorem Th12:
 a*(n+1) <> 0 implies (1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b))
 is_differentiable_on REAL & for x holds
 (((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))`|REAL).x
  = (a*x+b) #Z n
proof
 assume C: a*(n+1) <> 0;
A1: [#]REAL = dom((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))
 & [#]REAL = dom ( #Z (n+1)*AffineMap(a,b))
 & [#]REAL = dom (AffineMap(a,b)) by FUNCT_2:def 1;
B: for x st x in REAL holds
 AffineMap(a,b).x=a*x + b by JORDAN16:def 3;then
B1: AffineMap(a,b) is_differentiable_on REAL & for x st x in [#]REAL
  holds ((AffineMap(a,b))`|REAL).x = a by A1,FDIFF_1:31;
A: #Z (n+1)*AffineMap(a,b) is_differentiable_in x
 proof
   AffineMap(a,b) is_differentiable_in x by B1,FDIFF_1:16;
   hence thesis by TAYLOR_1:3;
 end;
A2: #Z (n+1)*AffineMap(a,b) is_differentiable_on REAL
 proof
   for x st x in REAL holds
   #Z (n+1)*AffineMap(a,b) is_differentiable_in x by A;
   hence thesis by A1,FDIFF_1:16;
 end;
A3: for x st x in REAL holds
(( #Z (n+1)*AffineMap(a,b))`|REAL).x=a*(n+1)*(AffineMap(a,b).x) #Z n
  proof
    let x;
    assume x in REAL;
    set m=n+1;
    AffineMap(a,b) is_differentiable_in x by B1,FDIFF_1:16;then
    diff(( #Z m*AffineMap(a,b)),x) = m * (AffineMap(a,b).x) #Z (m-1)
       * diff(AffineMap(a,b),x) by TAYLOR_1:3
    .= m * (AffineMap(a,b).x) #Z (m-1)
       * ((AffineMap(a,b))`|REAL).x by B1,FDIFF_1:def 8
    .= m * (AffineMap(a,b).x) #Z (m-1) *a by B,A1,FDIFF_1:31;
    hence thesis by A2,FDIFF_1:def 8;
  end;
  for x st x in REAL holds
    (((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))`|REAL).x =(a*x+b) #Z n
  proof
    let x;
    assume x in REAL;
    (((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))`|REAL).x
      =(1/(a*(n+1)))*diff( #Z (n+1)*AffineMap(a,b),x)
        by A1,A2,FDIFF_1:28
   .=(1/(a*(n+1)))*((( #Z (n+1)*AffineMap(a,b))`|REAL).x) by A2,FDIFF_1:def 8
   .=(1/(a*(n+1)))*(a*(n+1) * (AffineMap(a,b).x) #Z n ) by A3
   .=(1/(a*(n+1)))*(a*(n+1)) * (AffineMap(a,b).x) #Z n
   .=(a*(n+1))/(a*(n+1)) * (AffineMap(a,b).x) #Z n by XCMPLX_1:100
   .=1*(AffineMap(a,b).x) #Z n by C,XCMPLX_1:60
   .=(a*x+b) #Z n by JORDAN16:def 3;
    hence thesis;
  end;
  hence thesis by A1,A2,FDIFF_1:28;
end;

begin :: Integrability Formulas

:: f.x = (sin.x)^2
theorem Th13:
  integral(sin^2,A)
 = (AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))).(sup A)
  -(AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))).(inf A)
proof
A1: dom (sin^2)=REAL by SIN_COS:27,VALUED_1:11;
   (sin(#)sin)|A is continuous; then
A2: sin^2 is_integrable_on A
     & (sin^2)|A is bounded by A1,INTEGRA5:10,11;
A3:for x st x in dom ((AffineMap(1/2,0)
  -(1/4)(#)(sin*AffineMap(2,0)))`|REAL) holds
 ((AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
  = (sin^2).x
   proof
     let x;
     assume x in dom ((AffineMap(1/2,0)
   -(1/4)(#)(sin*AffineMap(2,0)))`|REAL);
  ((AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
     = (sin.x)^2 by Th1
    .= (sin^2).x by VALUED_1:11;
     hence thesis;
   end;
   dom ((AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)))`|REAL)
 = dom (sin^2) by A1,Th1,FDIFF_1:def 8;then
   ((AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)))`|REAL)
 = sin^2 by A3,PARTFUN1:34;
   hence thesis by A2,Th1,INTEGRA5:13;
end;

C: dom (AffineMap(2,0)) = [#]REAL &
 dom (AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0)))=REAL by FUNCT_2:def 1;

theorem
  A = [.0,PI.] implies integral(sin^2,A) = PI/2
proof
   assume A=[.0,PI.]; then
B: sup A=PI & inf A=0 by INTEGRA8:37;
 integral(sin^2,A)=(AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))).(PI)
  -(AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))).0 by B,Th13
 .= AffineMap(1/2,0).(PI)-((1/4)(#)(sin*AffineMap(2,0))).(PI)
  -(AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))).0 by C,VALUED_1:13
 .= AffineMap(1/2,0).(PI)-((1/4)(#)(sin*AffineMap(2,0))).(PI)
  -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by C,VALUED_1:13
 .= (1/2)*(PI)+0 -((1/4)(#)(sin*AffineMap(2,0))).(PI)
  -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:def 3
 .= (1/2)*(PI)-(1/4)*(sin*AffineMap(2,0)).(PI)
  -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by VALUED_1:6
 .= (1/2)*(PI)-(1/4)*(sin.(AffineMap(2,0).(PI)))
  -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by C,FUNCT_1:23
 .= (1/2)*(PI)-(1/4)*(sin.(2*PI+0))
  -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:def 3
 .= (1/2)*(PI)-(1/4)*sin.(2*PI+0)
  -(0-((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:25
 .= (1/2)*(PI)-(1/4)*sin.(2*PI+0)
  -0+((1/4)(#)(sin*AffineMap(2,0))).0
 .= (1/2)*(PI)-(1/4)*sin.(2*PI+0)
    +(1/4)*(sin*AffineMap(2,0)).0 by VALUED_1:6
 .= (1/2)*(PI)-(1/4)*sin.(2*PI+0)
   +(1/4)*(sin.(AffineMap(2,0).0)) by C,FUNCT_1:23
 .= (1/2)*(PI)-(1/4)*sin.(0+2*PI*1)+(1/4)*sin.0 by JORDAN16:25
 .= (1/2)*(PI)-(1/4)*sin.0+(1/4)*sin.0 by SIN_COS6:8
 .= PI/2;
  hence thesis;
end;

theorem
  A = [.0,2*PI.] implies integral(sin^2,A) = PI
proof
   assume A=[.0,2*PI.]; then
B: sup A=2*PI & inf A=0 by INTEGRA8:37;
   integral(sin^2,A)=(AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))).(2*PI)
     -(AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))).0 by B,Th13
    .= AffineMap(1/2,0).(2*PI)-((1/4)(#)(sin*AffineMap(2,0))).(2*PI)
     -(AffineMap(1/2,0)-(1/4)(#)(sin*AffineMap(2,0))).0 by C,VALUED_1:13
    .= AffineMap(1/2,0).(2*PI)-((1/4)(#)(sin*AffineMap(2,0))).(2*PI)
     -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by C,VALUED_1:13
    .= (1/2)*(2*PI)+0 -((1/4)(#)(sin*AffineMap(2,0))).(2*PI)
     -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:def 3
    .= (1/2)*(2*PI)-(1/4)*(sin*AffineMap(2,0)).(2*PI)
     -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by VALUED_1:6
    .= (1/2)*(2*PI)-(1/4)*(sin.(AffineMap(2,0).(2*PI)))
     -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by C,FUNCT_1:23
    .= (1/2)*(2*PI)-(1/4)*(sin.(2*(2*PI)+0))
     -(AffineMap(1/2,0).0-((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:def 3
    .= (1/2)*(2*PI)-(1/4)*sin.(2*2*PI+0)
     -(0-((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:25
    .= (1/2)*(2*PI)-(1/4)*sin.(2*2*PI+0)
     -0+((1/4)(#)(sin*AffineMap(2,0))).0
    .= (1/2)*(2*PI)-(1/4)*sin.(2*2*PI+0)
      +(1/4)*(sin*AffineMap(2,0)).0 by VALUED_1:6
    .= (1/2)*(2*PI)-(1/4)*sin.(2*2*PI+0)
      +(1/4)*(sin.(AffineMap(2,0).0)) by C,FUNCT_1:23
    .= (1/2)*(2*PI)-(1/4)*sin.(0+2*PI*2)+(1/4)*sin.0 by JORDAN16:25
    .= (1/2)*(2*PI)-(1/4)*sin.0+(1/4)*sin.0 by SIN_COS6:8
    .= PI;
    hence thesis;
end;

:: f.x = (cos.x)^2
theorem Th16:
  integral(cos^2,A)
    = (AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0))).(sup A)
    -(AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0))).(inf A)
proof
A1: dom (cos^2)=REAL by SIN_COS:27,VALUED_1:11;
    (cos(#)cos)|A is continuous; then
A2: cos^2 is_integrable_on A
     & (cos^2)|A is bounded by A1,INTEGRA5:10,11;
A3: for x st x in dom ((AffineMap(1/2,0)
     +(1/4)(#)(sin*AffineMap(2,0)))`|REAL) holds
    ((AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x = (cos^2).x
    proof
      let x;
      assume x in dom ((AffineMap(1/2,0)
       +(1/4)(#)(sin*AffineMap(2,0)))`|REAL);
      ((AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)))`|REAL).x
       = (cos.x)^2 by Th2
      .= (cos^2).x by VALUED_1:11;
      hence thesis;
    end;
    dom ((AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)))`|REAL)
     = dom (cos^2) by A1,Th2,FDIFF_1:def 8;then
     ((AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0)))`|REAL)
     = cos^2 by A3,PARTFUN1:34;
    hence thesis by A2,Th2,INTEGRA5:13;
end;

theorem
  A = [.0,PI.] implies integral(cos^2,A) = PI/2
proof
  assume A=[.0,PI.];
  then B: sup A=PI & inf A=0 by INTEGRA8:37;
 integral(cos^2,A)=(AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0))).(PI)
  -(AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0))).0 by B,Th16
 .= AffineMap(1/2,0).(PI)+((1/4)(#)(sin*AffineMap(2,0))).(PI)
  -(AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0))).0 by VALUED_1:1
 .= AffineMap(1/2,0).(PI)+((1/4)(#)(sin*AffineMap(2,0))).(PI)
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by VALUED_1:1
 .= (1/2)*(PI)+0 +((1/4)(#)(sin*AffineMap(2,0))).(PI)
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:def 3
 .= (1/2)*(PI)+(1/4)*(sin*AffineMap(2,0)).(PI)
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by VALUED_1:6
 .= (1/2)*(PI)+(1/4)*(sin.(AffineMap(2,0).(PI)))
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by C,FUNCT_1:23
 .= (1/2)*(PI)+(1/4)*(sin.(2*PI+0))
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:def 3
 .= (1/2)*(PI)+(1/4)*sin.(2*PI+0)
  -(0+((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:25
 .= (1/2)*(PI)+(1/4)*sin.(2*PI+0)
    -(1/4)*(sin*AffineMap(2,0)).0 by VALUED_1:6
 .= (1/2)*(PI)+(1/4)*sin.(2*PI+0)
   -(1/4)*(sin.(AffineMap(2,0).0)) by C,FUNCT_1:23
 .= (1/2)*(PI)+(1/4)*sin.(0+2*PI*1)-(1/4)*sin.0 by JORDAN16:25
 .= (1/2)*(PI)+(1/4)*sin.0-(1/4)*sin.0 by SIN_COS6:8
 .= PI/2;
  hence thesis;
end;

theorem
  A = [.0,2*PI.] implies integral(cos^2,A) = PI
proof
  assume A=[.0,2*PI.];
  then B: sup A=2*PI & inf A=0 by INTEGRA8:37;
 integral(cos^2,A)=(AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0))).(2*PI)
  -(AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0))).0 by B,Th16
 .= AffineMap(1/2,0).(2*PI)+((1/4)(#)(sin*AffineMap(2,0))).(2*PI)
  -(AffineMap(1/2,0)+(1/4)(#)(sin*AffineMap(2,0))).0 by VALUED_1:1
 .= AffineMap(1/2,0).(2*PI)+((1/4)(#)(sin*AffineMap(2,0))).(2*PI)
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by VALUED_1:1
 .= (1/2)*(2*PI)+0 +((1/4)(#)(sin*AffineMap(2,0))).(2*PI)
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:def 3
 .= (1/2)*(2*PI)+(1/4)*(sin*AffineMap(2,0)).(2*PI)
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by VALUED_1:6
 .= (1/2)*(2*PI)+(1/4)*(sin.(AffineMap(2,0).(2*PI)))
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by C,FUNCT_1:23
 .= (1/2)*(2*PI)+(1/4)*(sin.(2*(2*PI)+0))
  -(AffineMap(1/2,0).0+((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:def 3
 .= (1/2)*(2*PI)+(1/4)*sin.(2*2*PI+0)
    -(0+((1/4)(#)(sin*AffineMap(2,0))).0) by JORDAN16:25
 .= (1/2)*(2*PI)+(1/4)*sin.(2*2*PI+0)
    -(1/4)*(sin*AffineMap(2,0)).0 by VALUED_1:6
 .= (1/2)*(2*PI)+(1/4)*sin.(2*2*PI+0)
   -(1/4)*(sin.(AffineMap(2,0).0)) by C,FUNCT_1:23
 .= (1/2)*(2*PI)+(1/4)*sin.(0+2*PI*2)-(1/4)*sin.0 by JORDAN16:25
 .= (1/2)*(2*PI)+(1/4)*sin.0-(1/4)*sin.0 by SIN_COS6:8
 .= PI;
  hence thesis;
end;

:: f.x = (sin.x)^n*(cos.x)
theorem Th19:
  integral(( #Z n*sin)(#)cos,A)
 = ((1/(n+1))(#)( #Z (n+1)*sin)).(sup A)
  -((1/(n+1))(#)( #Z (n+1)*sin)).(inf A)
proof
A1: [#]REAL = dom (( #Z n*sin)(#)cos) & dom ( #Z n*sin)=REAL
 & [#]REAL = dom((1/(n+1))(#)( #Z (n+1)*sin)) &
 [#]REAL = dom ( #Z (n+1)*sin) by FUNCT_2:def 1;
A: #Z n*sin is_differentiable_in x0
 proof
  sin is_differentiable_in x0 by SIN_COS:69;
hence thesis by TAYLOR_1:3;
 end;
A2: #Z n*sin is_differentiable_on REAL
 proof
for x0 st x0 in REAL holds #Z n*sin is_differentiable_in x0 by A;
 hence thesis by A1,FDIFF_1:16;
 end;
  (( #Z n*sin)(#)cos)|REAL is continuous
  by A1,A2,SIN_COS:72,FDIFF_1:29,FDIFF_1:33;then
 (( #Z n*sin)(#)cos)|A is continuous by FCONT_1:17;
then A3: ( #Z n)*sin(#)cos is_integrable_on A
 & (( #Z n)*sin(#)cos)|A is bounded by A1,INTEGRA5:10,11;
A4:for x st x in dom (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL) holds
  (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL).x = (( #Z n*sin)(#)cos).x
   proof
     let x;
     assume x in dom (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL);
    (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL).x
   =(sin.x) #Z n *cos.x by Th3
  .=( #Z n).(sin.x)*cos.x by TAYLOR_1:def 1
  .=(( #Zn)*sin).x*cos.x by SIN_COS:27,FUNCT_1:23
   .= ((( #Z n)*sin)(#)cos).x by A1,VALUED_1:def 4;
  hence thesis;
   end;
  (1/(n+1))(#)( #Z (n+1)*sin) is_differentiable_on REAL by Th3; then
   dom (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL)
 = dom (( #Z n*sin)(#)cos) by A1,FDIFF_1:def 8;then
   (((1/(n+1))(#)( #Z (n+1)*sin))`|REAL)
 = (( #Z n*sin)(#)cos) by A4,PARTFUN1:34;
   hence thesis by A3,Th3,INTEGRA5:13;
end;

theorem
  A = [.0,PI.] implies integral(( #Z n*sin)(#)cos,A) = 0
proof
  assume A=[.0,PI.];
  then B: sup A=PI & inf A=0 by INTEGRA8:37;
 integral(( #Z n*sin)(#)cos,A)=((1/(n+1))(#)( #Z (n+1)*sin)).(PI)
   -((1/(n+1))(#)( #Z (n+1)*sin)).0 by B,Th19
 .= 1/(n+1)*( #Z (n+1)*sin).PI
   -((1/(n+1))(#)( #Z (n+1)*sin)).0 by VALUED_1:6
 .= 1/(n+1)*( #Z (n+1)*sin).PI
   -1/(n+1)*( #Z (n+1)*sin).0 by VALUED_1:6
 .= 1/(n+1)*(( #Z (n+1)).(sin.PI))
   -1/(n+1)*( #Z (n+1)*sin).0 by SIN_COS:27,FUNCT_1:23
 .= 1/(n+1)*(( #Z (n+1)).(sin.PI))
   -1/(n+1)*(( #Z (n+1)).(sin.0)) by SIN_COS:27,FUNCT_1:23
 .= 0 by SIN_COS:81,SIN_COS:33;
 hence thesis;
end;

theorem
  A = [.0,2*PI.] implies integral(( #Z n*sin)(#)cos,A) = 0
proof
  assume A=[.0,2*PI.];
  then B: sup A=2*PI & inf A=0 by INTEGRA8:37;
 integral(( #Z n*sin)(#)cos,A)=((1/(n+1))(#)( #Z (n+1)*sin)).(2*PI)
   -((1/(n+1))(#)( #Z (n+1)*sin)).0 by B,Th19
 .= 1/(n+1)*( #Z (n+1)*sin).(2*PI)
   -((1/(n+1))(#)( #Z (n+1)*sin)).0 by VALUED_1:6
 .= 1/(n+1)*( #Z (n+1)*sin).(2*PI)
   -1/(n+1)*( #Z (n+1)*sin).0 by VALUED_1:6
 .= 1/(n+1)*(( #Z (n+1)).(sin.(2*PI)))
   -1/(n+1)*( #Z (n+1)*sin).0 by SIN_COS:27,FUNCT_1:23
 .= 1/(n+1)*(( #Z (n+1)).(sin.(2*PI)))
   -1/(n+1)*(( #Z (n+1)).(sin.0)) by SIN_COS:27,FUNCT_1:23
 .= 0 by SIN_COS:81,SIN_COS:33;
 hence thesis;
end;

:: f.x = (cos.x)^n*(sin.x)
theorem Th22:
  integral(( #Z n*cos)(#)sin,A)
 = ((-1/(n+1))(#)( #Z (n+1)*cos)).(sup A)
  -((-1/(n+1))(#)( #Z (n+1)*cos)).(inf A)
proof
A1: [#]REAL=dom (( #Z n*cos)(#)sin) & dom ( #Z n*cos)=REAL
 & [#]REAL=dom((-1/(n+1))(#)( #Z (n+1)*cos)) &
 [#]REAL=dom ( #Z (n+1)*cos) by FUNCT_2:def 1;
A: #Z n*cos is_differentiable_in x0
 proof
  cos is_differentiable_in x0 by SIN_COS:68;
hence thesis by TAYLOR_1:3;
 end;
A2:  #Z n*cos is_differentiable_on REAL
 proof
for x0 st x0 in REAL holds #Z n*cos is_differentiable_in x0 by A;
 hence thesis by A1,FDIFF_1:16;
 end;
  (( #Z n*cos)(#)sin)|REAL is continuous
 by A1,A2,SIN_COS:73,FDIFF_1:29,FDIFF_1:33;then
 (( #Z n*cos)(#)sin)|A is continuous by FCONT_1:17;
then A3: ( #Z n*cos)(#)sin is_integrable_on A
 & (( #Z n*cos)(#)sin)|A is bounded by A1,INTEGRA5:10,11;
A4:for x st x in dom (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL) holds
  (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL).x = (( #Z n*cos)(#)sin).x
   proof
     let x;
     assume x in dom (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL);
    (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL).x
   =(cos.x) #Z n *sin.x by Th4
  .=( #Z n).(cos.x)*sin.x by TAYLOR_1:def 1
  .=(( #Zn)*cos).x*sin.x by SIN_COS:27,FUNCT_1:23
   .= ((( #Z n)*cos)(#)sin).x by A1,VALUED_1:def 4;
  hence thesis;
   end;
  (-1/(n+1))(#)( #Z (n+1)*cos) is_differentiable_on REAL by Th4; then
   dom (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL)
 = dom (( #Z n*cos)(#)sin) by A1,FDIFF_1:def 8;then
   (((-1/(n+1))(#)( #Z (n+1)*cos))`|REAL)
 = (( #Z n*cos)(#)sin) by A4,PARTFUN1:34;
   hence thesis by A3,Th4,INTEGRA5:13;
end;

theorem
  A = [.0,2*PI.] implies integral(( #Z n*cos)(#)sin,A) = 0
proof
  assume A=[.0,2*PI.];
  then B: sup A=2*PI & inf A=0 by INTEGRA8:37;
 integral(( #Z n*cos)(#)sin,A)=((-1/(n+1))(#)( #Z (n+1)*cos)).(2*PI)
   -((-1/(n+1))(#)( #Z (n+1)*cos)).0 by B,Th22
 .= (-1/(n+1))*( #Z (n+1)*cos).(2*PI)
   -((-1/(n+1))(#)( #Z (n+1)*cos)).0 by VALUED_1:6
 .= (-1/(n+1))*( #Z (n+1)*cos).(2*PI)
   -(-1/(n+1))*( #Z (n+1)*cos).0 by VALUED_1:6
 .= (-1/(n+1))*(( #Z (n+1)).(cos.(2*PI)))
   -(-1/(n+1))*( #Z (n+1)*cos).0 by SIN_COS:27,FUNCT_1:23
 .= (-1/(n+1))*(( #Z (n+1)).(cos.(2*PI)))
   -(-1/(n+1))*(( #Z (n+1)).(cos.0)) by SIN_COS:27,FUNCT_1:23
 .= 0 by SIN_COS:81,SIN_COS:33;
 hence thesis;
end;

theorem
  A = [.-PI/2,PI/2.] implies integral(( #Z n*cos)(#)sin,A) = 0
proof
  assume A=[.-PI/2,PI/2.];
  then B: sup A=PI/2 & inf A=-PI/2 by INTEGRA8:37;
 integral(( #Z n*cos)(#)sin,A)=((-1/(n+1))(#)( #Z (n+1)*cos)).(PI/2)
   -((-1/(n+1))(#)( #Z (n+1)*cos)).(-PI/2) by B,Th22
 .= (-1/(n+1))*( #Z (n+1)*cos).(PI/2)
   -((-1/(n+1))(#)( #Z (n+1)*cos)).(-PI/2) by VALUED_1:6
 .= (-1/(n+1))*( #Z (n+1)*cos).(PI/2)
   -(-1/(n+1))*( #Z (n+1)*cos).(-PI/2) by VALUED_1:6
 .= (-1/(n+1))*(( #Z (n+1)).(cos.(PI/2)))
   -(-1/(n+1))*( #Z (n+1)*cos).(-PI/2) by SIN_COS:27,FUNCT_1:23
 .= (-1/(n+1))*(( #Z (n+1)).(cos.(PI/2)))
   -(-1/(n+1))*(( #Z (n+1)).(cos.(-PI/2))) by SIN_COS:27,FUNCT_1:23
 .= (-1/(n+1))*(( #Z (n+1)).(cos.(PI/2)))
   -(-1/(n+1))*(( #Z (n+1)).(cos.(PI/2))) by SIN_COS:33
 .= 0;
 hence thesis;
end;

:: f.x = cos.(m*x)*cos.(n*x)
theorem
  m+n<>0 & m-n<>0 implies
 integral((cos*AffineMap(m,0))(#)(cos*AffineMap(n,0)),A)
 = ((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
 (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))).(sup A)
  -((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
 (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))).(inf A)
proof
 assume A: m+n<>0 & m-n<>0;
A1: [#]REAL = dom ((cos*AffineMap(m,0))(#)(cos*AffineMap(n,0)))
  & dom (cos*AffineMap(m,0)) = [#]REAL
  & dom (cos*AffineMap(n,0)) = [#]REAL by FUNCT_2:def 1; then
C: dom (cos*AffineMap(m,0)) /\ dom (cos*AffineMap(n,0)) = [#]REAL;
A2: dom (AffineMap(m,0))=REAL & dom (AffineMap(n,0))=[#]REAL by FUNCT_2:def 1;
  for x st x in REAL holds
 AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3; then
  AffineMap(n,0)|REAL is continuous
  by A2,FDIFF_1:31,FDIFF_1:33;then
A3: AffineMap(n,0)|A is continuous by FCONT_1:17;
 cos|((AffineMap(n,0)).:A) is continuous;then
A5: (cos*AffineMap(n,0))|A is continuous by A3,FCONT_1:26;
  for x st x in REAL holds
    AffineMap(m,0).x=m*x + 0 by JORDAN16:def 3;
    then
  AffineMap(m,0)|REAL is continuous
  by A2,FDIFF_1:31,FDIFF_1:33;then
A6: AffineMap(m,0)|A is continuous by FCONT_1:17;
 cos|((AffineMap(m,0)).:A) is continuous;then
 (cos*AffineMap(m,0))|A is continuous by A6,FCONT_1:26;then
 ((cos*AffineMap(m,0))(#)(cos*AffineMap(n,0)))|A
   is continuous by C,A5,FCONT_1:19;then
A7:((cos*AffineMap(m,0))(#)(cos*AffineMap(n,0))) is_integrable_on A
 & ((cos*AffineMap(m,0))(#)(cos*AffineMap(n,0)))|A
    is bounded by A1,INTEGRA5:10,11;
A8: ((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
   (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))
    is_differentiable_on REAL by A,Th5;
B1: for x st x in REAL holds AffineMap(m,0).x=m*x
  proof
    let x;
   assume x in REAL;
    (AffineMap(m,0)).x = m*x + 0 by JORDAN16:def 3
      .=m*x;
    hence thesis;
  end;
B2: for x st x in REAL holds AffineMap(n,0).x=n*x
  proof
    let x;
   assume x in REAL;
    (AffineMap(n,0)).x = n*x + 0 by JORDAN16:def 3
      .=n*x;
    hence thesis;
  end;
A9: for x st x in dom (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
  (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL) holds
   (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
  (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
  = ((cos*AffineMap(m,0))(#)(cos*AffineMap(n,0))).x
   proof
     let x;
     assume x in dom (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
  (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL);
      (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
  (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL).x
   = cos.(m*x)*cos.(n*x) by A,Th5
  .= cos.(AffineMap(m,0).x)*cos.(n*x) by B1
  .= (cos.(AffineMap(m,0).x))*(cos.((AffineMap(n,0)).x)) by B2
  .= ((cos*AffineMap(m,0)).x)
    *(cos.(AffineMap(n,0).x)) by A1,FUNCT_1:22
  .= ((cos*AffineMap(m,0)).x)*((cos*AffineMap(n,0)).x) by A1,FUNCT_1:22
  .= ((cos*AffineMap(m,0))(#)(cos*AffineMap(n,0))).x by VALUED_1:5;
    hence thesis;
   end;
   dom (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
   (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL)
  = dom ((cos*AffineMap(m,0))(#)(cos*AffineMap(n,0)))
        by A1,A8,FDIFF_1:def 8;then
    (((1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))+
   (1/(2*(m-n)))(#)(sin*AffineMap(m-n,0)))`|REAL)
 = (cos*AffineMap(m,0))(#)(cos*AffineMap(n,0)) by A9,PARTFUN1:34;
   hence thesis by A,A7,Th5,INTEGRA5:13;
end;

:: f.x = sin.(m*x)*sin.(n*x)
theorem
 m+n<>0 & m-n<>0 implies
 integral((sin*AffineMap(m,0))(#)(sin*AffineMap(n,0)),A)
 = ((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
 (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))).(sup A)
  -((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
 (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0))).(inf A)
proof
 assume A: m+n<>0 & m-n<>0;
A1: REAL=dom ((sin*AffineMap(m,0))(#)(sin*AffineMap(n,0)))
  & dom (sin*AffineMap(m,0)) = [#]REAL
  & dom (sin*AffineMap(n,0)) = [#]REAL by FUNCT_2:def 1;
then C: dom (sin*AffineMap(m,0)) /\ dom (sin*AffineMap(n,0)) = [#]REAL;
A2: dom (AffineMap(m,0))=[#]REAL & dom (AffineMap(n,0))=[#]REAL
 by FUNCT_2:def 1;
  for x st x in REAL holds
    AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3; then
  AffineMap(n,0)|REAL is continuous
    by A2,FDIFF_1:31,FDIFF_1:33;then
A3: AffineMap(n,0)|A is continuous by FCONT_1:17;
    sin|((AffineMap(n,0)).:A) is continuous;then
A5: (sin*AffineMap(n,0))|A is continuous by A3,FCONT_1:26;
    for x st x in REAL holds
      AffineMap(m,0).x=m*x + 0 by JORDAN16:def 3; then
    AffineMap(m,0)|REAL is continuous
      by A2,FDIFF_1:31,FDIFF_1:33;then
A6: AffineMap(m,0)|A is continuous by FCONT_1:17;
    sin|((AffineMap(m,0)).:A) is continuous;then
    (sin*AffineMap(m,0))|A is continuous by A6,FCONT_1:26;then
    ((sin*AffineMap(m,0))(#)(sin*AffineMap(n,0)))|A
      is continuous by C,A5,FCONT_1:19;then
A7: ((sin*AffineMap(m,0))(#)(sin*AffineMap(n,0))) is_integrable_on A
      & ((sin*AffineMap(m,0))(#)(sin*AffineMap(n,0)))|A
      is bounded by A1,INTEGRA5:10,11;
A8: ((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
    (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))
      is_differentiable_on REAL by A,Th6;
B1: for x st x in REAL holds AffineMap(m,0).x=m*x
    proof
      let x;
      assume x in REAL;
      (AffineMap(m,0)).x = m*x + 0 by JORDAN16:def 3
        .=m*x;
      hence thesis;
    end;
B2: for x st x in REAL holds AffineMap(n,0).x=n*x
    proof
      let x;
      assume x in REAL;
      (AffineMap(n,0)).x = n*x + 0 by JORDAN16:def 3
        .=n*x;
      hence thesis;
    end;
A9: for x st x in dom (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
     (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL) holds
     (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
     (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
      = ((sin*AffineMap(m,0))(#)(sin*AffineMap(n,0))).x
    proof
      let x;
      assume x in dom (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
      (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL);
      (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
      (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL).x
        = sin.(m*x)*sin.(n*x) by A,Th6
       .= sin.(AffineMap(m,0).x)*sin.(n*x) by B1
       .= (sin.(AffineMap(m,0).x))*(sin.(AffineMap(n,0).x)) by B2
       .= ((sin*(AffineMap(m,0))).x)
        *(sin.(AffineMap(n,0).x)) by A1,FUNCT_1:22
       .= ((sin*AffineMap(m,0)).x)*((sin*AffineMap(n,0)).x) by A1,FUNCT_1:22
       .= ((sin*AffineMap(m,0))(#)(sin*AffineMap(n,0))).x by VALUED_1:5;
      hence thesis;
    end;
    dom (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
    (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL)
       = dom ((sin*AffineMap(m,0))(#)(sin*AffineMap(n,0)))
        by A1,A8,FDIFF_1:def 8;then
    (((1/(2*(m-n)))(#)(sin*AffineMap(m-n,0))-
      (1/(2*(m+n)))(#)(sin*AffineMap(m+n,0)))`|REAL)
       = (sin*AffineMap(m,0))(#)(sin*AffineMap(n,0)) by A9,PARTFUN1:34;
    hence thesis by A,A7,Th6,INTEGRA5:13;
end;

:: f.x = sin.(m*x)*cos.(n*x)
theorem
 m+n<>0 & m-n<>0 implies
 integral((sin*AffineMap(m,0))(#)(cos*AffineMap(n,0)),A)
 = ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
 (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0))).(sup A)
  -((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
 (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0))).(inf A)
proof
 assume A: m+n<>0 & m-n<>0;
A1: [#]REAL=dom ((sin*AffineMap(m,0))(#)(cos*AffineMap(n,0)))
  & dom (sin*AffineMap(m,0)) = [#]REAL
  & dom (cos*AffineMap(n,0)) = [#]REAL by FUNCT_2:def 1;
then C: dom (sin*AffineMap(m,0)) /\ dom (cos*AffineMap(n,0)) = [#]REAL;
A2: dom (AffineMap(m,0))=[#]REAL & dom (AffineMap(n,0))=[#]REAL
by FUNCT_2:def 1;
  for x st x in REAL holds
 AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3; then
  AffineMap(n,0)|REAL is continuous
  by A2,FDIFF_1:31,FDIFF_1:33;then
A3: AffineMap(n,0)|A is continuous by FCONT_1:17;
 cos|((AffineMap(n,0)).:A) is continuous;then
A5: (cos*AffineMap(n,0))|A is continuous by A3,FCONT_1:26;
  for x st x in REAL holds
    AffineMap(m,0).x=m*x + 0 by JORDAN16:def 3; then
  AffineMap(m,0)|REAL is continuous
  by A2,FDIFF_1:31,FDIFF_1:33;then
A6: AffineMap(m,0)|A is continuous by FCONT_1:17;
 sin|((AffineMap(m,0)).:A) is continuous;then
 (sin*AffineMap(m,0))|A is continuous by A6,FCONT_1:26;then
 ((sin*AffineMap(m,0))(#)(cos*AffineMap(n,0)))|A
   is continuous by C,A5,FCONT_1:19;then
A7:((sin*AffineMap(m,0))(#)(cos*AffineMap(n,0))) is_integrable_on A
 & ((sin*AffineMap(m,0))(#)(cos*AffineMap(n,0)))|A
    is bounded by A1,INTEGRA5:10,11;
A8: ((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
   (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))
    is_differentiable_on REAL by A,Th7;
B1: for x st x in REAL holds AffineMap(m,0).x=m*x
  proof
    let x;
   assume x in REAL;
    (AffineMap(m,0)).x = m*x + 0 by JORDAN16:def 3
      .=m*x;
    hence thesis;
  end;
B2: for x st x in REAL holds AffineMap(n,0).x=n*x
  proof
    let x;
   assume x in REAL;
    (AffineMap(n,0)).x = n*x + 0 by JORDAN16:def 3
      .=n*x;
    hence thesis;
  end;
A9: for x st x in dom (((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
  (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL) holds
   (((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
 (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL).x
  = ((sin*AffineMap(m,0))(#)(cos*AffineMap(n,0))).x
   proof
     let x;
     assume x in dom (((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
  (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL);
      (((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
  (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL).x
   = sin.(m*x)*cos.(n*x) by A,Th7
  .= sin.(AffineMap(m,0).x)*cos.(n*x) by B1
  .= (sin.(AffineMap(m,0).x))*(cos.(AffineMap(n,0).x)) by B2
  .= ((sin*AffineMap(m,0)).x)*(cos.(AffineMap(n,0).x)) by A1,FUNCT_1:22
  .= ((sin*AffineMap(m,0)).x)*((cos*AffineMap(n,0)).x) by A1,FUNCT_1:22
  .= ((sin*AffineMap(m,0))(#)(cos*AffineMap(n,0))).x by VALUED_1:5;
    hence thesis;
   end;
   dom (((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
   (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL)
  = dom ((sin*AffineMap(m,0))(#)(cos*AffineMap(n,0)))
        by A1,A8,FDIFF_1:def 8;then
    (((-(1/(2*(m+n)))(#)(cos*AffineMap(m+n,0)))-
   (1/(2*(m-n)))(#)(cos*AffineMap(m-n,0)))`|REAL)
 = (sin*AffineMap(m,0))(#)(cos*AffineMap(n,0)) by A9,PARTFUN1:34;
   hence thesis by A,A7,Th7,INTEGRA5:13;
end;

:: f.x = x*sin.(n*x)
theorem
 n<>0 implies integral((AffineMap(1,0))(#)(sin*AffineMap(n,0)),A)
 = ((1/(n^2))(#)(sin*AffineMap(n,0))-
 (AffineMap(1/n,0))(#)(cos*AffineMap(n,0))).(sup A)
  -((1/(n^2))(#)(sin*AffineMap(n,0))-
 (AffineMap(1/n,0))(#)(cos*AffineMap(n,0))).(inf A)
proof
 assume A:n<>0;
A1: dom ((AffineMap(1,0))(#)(sin*AffineMap(n,0))) = [#]REAL
 & dom (AffineMap(1,0)) = [#]REAL & dom (sin*AffineMap(n,0)) = [#]REAL
 & [#]REAL = dom (AffineMap(n,0)) by FUNCT_2:def 1;then
C: dom (AffineMap(1,0)) /\ dom (sin*AffineMap(n,0)) = [#]REAL;
  for x st x in REAL holds
 AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3;then
  AffineMap(n,0)|REAL is continuous
  by A1,FDIFF_1:31,FDIFF_1:33;then
A2: AffineMap(n,0)|A is continuous by FCONT_1:17;
 sin|((AffineMap(n,0)).:A) is continuous;then
A3: (sin*AffineMap(n,0))|A is continuous by A2,FCONT_1:26;
 for x st x in REAL holds
 AffineMap(1,0).x=1*x + 0 by JORDAN16:def 3;then
  AffineMap(1,0)|REAL is continuous
  by A1,FDIFF_1:31,FDIFF_1:33;then
A4: AffineMap(1,0)|A is continuous by FCONT_1:17;
 ((AffineMap(1,0))(#)(sin*AffineMap(n,0)))|A
  is continuous by C,A3,A4,FCONT_1:19;then
A5: ((AffineMap(1,0))(#)(sin*AffineMap(n,0))) is_integrable_on A
  & ((AffineMap(1,0))(#)(sin*AffineMap(n,0)))|A
  is bounded by A1,INTEGRA5:10,11;
B1: for x st x in REAL holds AffineMap(1,0).x=x
  proof
    let x;
   assume x in REAL;
    (AffineMap(1,0)).x = 1*x + 0 by JORDAN16:def 3
      .=x;
    hence thesis;
  end;
B2: for x st x in REAL holds AffineMap(n,0).x=n*x
  proof
    let x;
   assume x in REAL;
    (AffineMap(n,0)).x = n*x + 0 by JORDAN16:def 3
      .=n*x;
    hence thesis;
  end;
A6: ((1/(n^2))(#)(sin*AffineMap(n,0))-
   (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))
    is_differentiable_on REAL by A,Th8;
A7:for x st x in dom (((1/(n^2))(#)(sin*AffineMap(n,0))-
 (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL) holds
    (((1/(n^2))(#)(sin*AffineMap(n,0))-
 (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL).x
 = ((AffineMap(1,0))(#)(sin*AffineMap(n,0))).x
   proof
     let x;
     assume x in dom (((1/(n^2))(#)(sin*AffineMap(n,0))-
 (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL);
      (((1/(n^2))(#)(sin*AffineMap(n,0))-
  (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL).x
    = x*sin.(n*x) by A,Th8
   .= x*(sin.(AffineMap(n,0).x)) by B2
   .= x*(sin*AffineMap(n,0)).x by A1,FUNCT_1:22
   .= (AffineMap(1,0).x)*(sin*AffineMap(n,0)).x by B1
   .= ((AffineMap(1,0))(#)(sin*AffineMap(n,0))).x by VALUED_1:5;
     hence thesis;
   end;
   dom (((1/(n^2))(#)(sin*AffineMap(n,0))-
  (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL)
  = dom ((AffineMap(1,0))(#)(sin*AffineMap(n,0)))
        by A1,A6,FDIFF_1:def 8;then
     (((1/(n^2))(#)(sin*AffineMap(n,0))-
   (AffineMap(1/n,0))(#)(cos*AffineMap(n,0)))`|REAL)
  = (AffineMap(1,0))(#)(sin*AffineMap(n,0)) by A7,PARTFUN1:34;
   hence thesis by A,A5,Th8,INTEGRA5:13;
end;

:: f.x = x*cos.(n*x)
theorem
 n<>0 implies integral((AffineMap(1,0))(#)(cos*AffineMap(n,0)),A)
 = ((1/(n^2))(#)(cos*AffineMap(n,0))+
 (AffineMap(1/n,0))(#)(sin*AffineMap(n,0))).(sup A)
  -((1/(n^2))(#)(cos*AffineMap(n,0))+
 (AffineMap(1/n,0))(#)(sin*AffineMap(n,0))).(inf A)
proof
 assume A:n<>0;
A1: dom ((AffineMap(1,0))(#)(cos*AffineMap(n,0))) = [#]REAL
 & dom (AffineMap(1,0)) = [#]REAL & dom (cos*AffineMap(n,0)) = [#]REAL
 & [#]REAL = dom (AffineMap(n,0)) by FUNCT_2:def 1;then
C: dom (AffineMap(1,0)) /\ dom (cos*AffineMap(n,0)) = [#]REAL;
  for x st x in REAL holds
 AffineMap(n,0).x=n*x + 0 by JORDAN16:def 3;then
  AffineMap(n,0)|REAL is continuous
  by A1,FDIFF_1:31,FDIFF_1:33;then
A2: AffineMap(n,0)|A is continuous by FCONT_1:17;
 cos|((AffineMap(n,0)).:A) is continuous;then
A3: (cos*AffineMap(n,0))|A is continuous by A2,FCONT_1:26;
 for x st x in REAL holds
 AffineMap(1,0).x=1*x + 0 by JORDAN16:def 3;then
  AffineMap(1,0)|REAL is continuous
  by A1,FDIFF_1:31,FDIFF_1:33;then
A4: AffineMap(1,0)|A is continuous by FCONT_1:17;
 ((AffineMap(1,0))(#)(cos*AffineMap(n,0)))|A
  is continuous by C,A3,A4,FCONT_1:19;then
A5: ((AffineMap(1,0))(#)(cos*AffineMap(n,0))) is_integrable_on A
  & ((AffineMap(1,0))(#)(cos*AffineMap(n,0)))|A
  is bounded by A1,INTEGRA5:10,11;
B1: for x st x in REAL holds AffineMap(1,0).x=x
  proof
    let x;
   assume x in REAL;
    (AffineMap(1,0)).x = 1*x + 0 by JORDAN16:def 3
      .=x;
    hence thesis;
  end;
B2: for x st x in REAL holds AffineMap(n,0).x=n*x
  proof
    let x;
   assume x in REAL;
    (AffineMap(n,0)).x = n*x + 0 by JORDAN16:def 3
      .=n*x;
    hence thesis;
  end;
A6: ((1/(n^2))(#)(cos*AffineMap(n,0))+
   (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))
    is_differentiable_on REAL by A,Th9;
A7:for x st x in dom (((1/(n^2))(#)(cos*AffineMap(n,0))+
 (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL) holds
    (((1/(n^2))(#)(cos*AffineMap(n,0))+
 (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL).x
 = ((AffineMap(1,0))(#)(cos*AffineMap(n,0))).x
   proof
     let x;
     assume x in dom (((1/(n^2))(#)(cos*AffineMap(n,0))+
 (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL);
      (((1/(n^2))(#)(cos*AffineMap(n,0))+
  (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL).x
    = x*cos.(n*x) by A,Th9
   .= x*(cos.(AffineMap(n,0).x)) by B2
   .= x*(cos*AffineMap(n,0)).x by A1,FUNCT_1:22
   .= (AffineMap(1,0).x)*(cos*AffineMap(n,0)).x by B1
   .= ((AffineMap(1,0))(#)(cos*AffineMap(n,0))).x by VALUED_1:5;
     hence thesis;
   end;
   dom (((1/(n^2))(#)(cos*AffineMap(n,0))+
  (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL)
  = dom ((AffineMap(1,0))(#)(cos*AffineMap(n,0)))
        by A1,A6,FDIFF_1:def 8;then
     (((1/(n^2))(#)(cos*AffineMap(n,0))+
   (AffineMap(1/n,0))(#)(sin*AffineMap(n,0)))`|REAL)
  = (AffineMap(1,0))(#)(cos*AffineMap(n,0)) by A7,PARTFUN1:34;
   hence thesis by A,A5,Th9,INTEGRA5:13;
end;

theorem
 integral((AffineMap(1,0))(#)sinh,A)
 = ((AffineMap(1,0))(#)cosh-sinh).(sup A)
  -((AffineMap(1,0))(#)cosh-sinh).(inf A)
proof
A1: dom ((AffineMap(1,0))(#)cosh-sinh) = [#]REAL
 & dom ((AffineMap(1,0))(#)sinh) = [#]REAL
 & dom (AffineMap(1,0)) = [#]REAL by FUNCT_2:def 1;
 for x st x in REAL holds
 AffineMap(1,0).x=1*x + 0 by JORDAN16:def 3;then
 AffineMap(1,0) is_differentiable_on REAL by A1,FDIFF_1:31;
  then ((AffineMap(1,0))(#)sinh)|REAL
 is continuous by A1,SIN_COS2:34,FDIFF_1:29,FDIFF_1:33; then
 ((AffineMap(1,0))(#)sinh)|A is continuous by FCONT_1:17; then
A2: (AffineMap(1,0))(#)sinh is_integrable_on A
    & ((AffineMap(1,0))(#)sinh)|A is bounded by A1,INTEGRA5:10,11;
A3: for x st x in dom (((AffineMap(1,0))(#)cosh-sinh)`|REAL) holds
 (((AffineMap(1,0))(#)cosh-sinh)`|REAL).x = ((AffineMap(1,0))(#)sinh).x
   proof
     let x;
     assume x in dom (((AffineMap(1,0))(#)cosh-sinh)`|REAL);
   (((AffineMap(1,0))(#)cosh-sinh)`|REAL).x
     = (1*x+0)*sinh.x by Th10
    .= ((AffineMap(1,0)).x)*sinh.x by JORDAN16:def 3
    .= ((AffineMap(1,0))(#)sinh).x by VALUED_1:5;
     hence thesis;
   end;
     dom (((AffineMap(1,0))(#)cosh-sinh)`|REAL)
  = dom ((AffineMap(1,0))(#)sinh) by A1,Th10,FDIFF_1:def 8; then
   (((AffineMap(1,0))(#)cosh-sinh)`|REAL)
  = (AffineMap(1,0))(#)sinh by A3,PARTFUN1:34;
   hence thesis by A2,Th10,INTEGRA5:13;
end;

theorem
 integral((AffineMap(1,0))(#)cosh,A)
 = ((AffineMap(1,0))(#)sinh-cosh).(sup A)
  -((AffineMap(1,0))(#)sinh-cosh).(inf A)
proof
A1: dom ((AffineMap(1,0))(#)sinh-cosh) = [#]REAL
 & dom ((AffineMap(1,0))(#)cosh) = [#]REAL
 & dom (AffineMap(1,0)) = [#]REAL by FUNCT_2:def 1;
 for x st x in REAL holds
 AffineMap(1,0).x=1*x + 0 by JORDAN16:def 3;then
 AffineMap(1,0) is_differentiable_on REAL by A1,FDIFF_1:31; then
 ((AffineMap(1,0))(#)cosh)|REAL is continuous
 by A1,SIN_COS2:35,FDIFF_1:29,FDIFF_1:33; then
 ((AffineMap(1,0))(#)cosh)|A is continuous by FCONT_1:17; then
A2: (AffineMap(1,0))(#)cosh is_integrable_on A
    & ((AffineMap(1,0))(#)cosh)|A is bounded by A1,INTEGRA5:10,11;
A3: for x st x in dom (((AffineMap(1,0))(#)sinh-cosh)`|REAL) holds
 (((AffineMap(1,0))(#)sinh-cosh)`|REAL).x = ((AffineMap(1,0))(#)cosh).x
   proof
     let x;
     assume x in dom (((AffineMap(1,0))(#)sinh-cosh)`|REAL);
   (((AffineMap(1,0))(#)sinh-cosh)`|REAL).x
     = (1*x+0)*cosh.x by Th11
    .= ((AffineMap(1,0)).x)*(cosh.x) by JORDAN16:def 3
    .= ((AffineMap(1,0))(#)cosh).x by VALUED_1:5;
     hence thesis;
   end;
     dom (((AffineMap(1,0))(#)sinh-cosh)`|REAL)
  = dom ((AffineMap(1,0))(#)cosh) by A1,Th11,FDIFF_1:def 8;
   then
   (((AffineMap(1,0))(#)sinh-cosh)`|REAL)
  = (AffineMap(1,0))(#)cosh by A3,PARTFUN1:34;
   hence thesis by A2,Th11,INTEGRA5:13;
end;

:: f.x = (a*x+b)^n
theorem
 a*(n+1) <> 0 implies integral( #Z n*AffineMap(a,b),A)
 = ((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b))).(sup A)
  -((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b))).(inf A)
proof
    assume C: a*(n+1) <> 0;
A1: [#]REAL=dom ( #Z n*AffineMap(a,b))
    & [#]REAL=dom((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b))) &
    [#]REAL=dom (AffineMap(a,b)) by FUNCT_2:def 1;
    for x st x in REAL holds
      AffineMap(a,b).x=a*x + b by JORDAN16:def 3;then
B1: AffineMap(a,b) is_differentiable_on REAL & for x st x in [#]REAL
      holds ((AffineMap(a,b))`|REAL).x = a by A1,FDIFF_1:31;
A:  #Z n*AffineMap(a,b) is_differentiable_in x
    proof
      AffineMap(a,b) is_differentiable_in x by B1,FDIFF_1:16;
      hence thesis by TAYLOR_1:3;
    end;
A2: #Z n*AffineMap(a,b) is_differentiable_on REAL
    proof
      for x st x in REAL holds
      #Z n*AffineMap(a,b) is_differentiable_in x by A;
      hence thesis by A1,FDIFF_1:16;
    end;
    ( #Z n*AffineMap(a,b))|REAL is continuous by A2,FDIFF_1:33;then
    ( #Z n*AffineMap(a,b))|A is continuous by FCONT_1:17; then
A3: #Z n*AffineMap(a,b) is_integrable_on A
    & ( #Z n*AffineMap(a,b))|A is bounded by A1,INTEGRA5:10,11;
A4: for x st x in dom (((1/(a*(n+1)))
      (#)( #Z (n+1)*AffineMap(a,b)))`|REAL) holds
      (((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))`|REAL).x
      = ( #Z n*AffineMap(a,b)).x
    proof
      let x;
      assume x in dom (((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))`|REAL);
      (((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))`|REAL).x
        =(a*x+b) #Z n by C,Th12
       .= (AffineMap(a,b).x) #Z n by JORDAN16:def 3
       .= ( #Z n).(AffineMap(a,b).x) by TAYLOR_1:def 1
       .=( #Z n*AffineMap(a,b)).x by A1,FUNCT_1:23;
      hence thesis;
    end;
    (1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b))
       is_differentiable_on REAL by C,Th12; then
    dom (((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))`|REAL)
      = dom ( #Z n*AffineMap(a,b)) by A1,FDIFF_1:def 8;then
    (((1/(a*(n+1)))(#)( #Z (n+1)*AffineMap(a,b)))`|REAL)
      = #Z n*AffineMap(a,b) by A4,PARTFUN1:34;
    hence thesis by C,A3,Th12,INTEGRA5:13;
end;

begin :: Addenda

 reserve f, f1, f2, f3, g for PartFunc of REAL, REAL;

:: f.x = (1/2)*x^2
theorem Th1:
  Z c= dom ((1/2)(#)f) & f=#Z 2 implies
  (1/2)(#)f is_differentiable_on Z &
  for x st x in Z holds (((1/2)(#)f)`|Z).x = x
proof
  assume
A1: Z c= dom ((1/2)(#)f) & f=#Z 2;
A2: Z c= dom f by A1,VALUED_1:def 5;
A3: f is_differentiable_on Z
  proof
    for x st x in Z holds f is_differentiable_in x by A1,TAYLOR_1:2;
    hence thesis by A2,FDIFF_1:16;
  end;
A5: for x st x in Z holds (f`|Z).x=2*x
  proof
    let x;
    assume
A6: x in Z;
    2 * (x #Z (2-1)) = 2 * x by PREPOWER:45;
    then diff(f,x) = 2 * x by A1,TAYLOR_1:2;
    hence thesis by A3,A6,FDIFF_1:def 8;
  end;
  for x st x in Z holds (((1/2)(#)f)`|Z).x=x
  proof
    let x;
    assume
A8: x in Z; then
    (((1/2)(#)f)`|Z).x=(1/2)*diff(f,x) by A1,A3,FDIFF_1:28
      .=(1/2)*(f`|Z).x by A3,A8,FDIFF_1:def 8
      .=(1/2)*(2*x) by A5,A8
      .=x;
    hence thesis;
  end;
  hence thesis by A1,A3,FDIFF_1:28;
end;

:: f.x = x
theorem
  A c= Z & f=#Z 2 & Z = dom ((1/2)(#)f) implies
  integral(id Z,A) = ((1/2)(#)f).(sup A)-((1/2)(#)f).(inf A)
proof
   assume
A1:A c= Z & f=#Z 2 & Z = dom ((1/2)(#)f);
AA:(id Z)|A is continuous;
   A c= dom id Z by A1,FUNCT_1:34; then
A2:id Z is_integrable_on A & (id Z)|A is bounded by AA,INTEGRA5:10,11;
B1: dom id Z=Z by FUNCT_1:34;
A3:((1/2)(#)f) is_differentiable_on Z by A1,Th1;
A4:for x st x in dom (((1/2)(#)f)`|Z) holds (((1/2)(#)f)`|Z).x = (id Z).x
   proof
     let x;
     assume x in dom (((1/2)(#)f)`|Z); then
 A5: x in Z by A3,FDIFF_1:def 8; then
     (((1/2)(#)f)`|Z).x = x by A1,Th1
      .= (id Z).x by A5,FUNCT_1:35;
     hence thesis;
   end;
   dom (((1/2)(#)f)`|Z) = dom (id Z) by B1,A3,FDIFF_1:def 8;
   then (((1/2)(#)f)`|Z) = id Z by A4,PARTFUN1:34;
   hence thesis by A1,A2,Th1,INTEGRA5:13;
end;

:: f.x = -1/x^2 :: INTEGRA9
theorem
  not 0 in Z & A c= Z & (for x st x in Z holds x<>0 & f.x = -1/x^2)
  & dom f = Z & f|A is continuous implies
  integral(f,A) = ((id Z)^).(sup A)-((id Z)^).(inf A)
proof
   set g = id Z;
   assume
A1:not 0 in Z & A c= Z & (for x st x in Z holds x<>0 & f.x = -1/x^2)
   & dom f = Z & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:g^ is_differentiable_on Z by A1,FDIFF_5:4;
A4:for x st x in dom ((g^)`|Z) holds ((g^)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((g^)`|Z); then
 A5: x in Z by A3,FDIFF_1:def 8; then
     ((g^)`|Z).x = -1/x^2 by A1,FDIFF_5:4
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((g^)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((g^)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = 2*x/(1+x^2)^2
theorem
  A c= Z & f1=#Z 2 & (for x st x in Z holds f2.x=1 & x<>0 & f.x=2*x/(1+x^2)^2)
  & dom (f1/(f2+f1))=Z & Z = dom f & f|A is continuous implies
  integral(f,A) = (f1/(f2+f1)).(sup A)-(f1/(f2+f1)).(inf A)
proof
   assume
A1:A c= Z & f1=#Z 2 & (for x st x in Z holds f2.x=1 & x<>0
   & f.x=2*x/(1+x^2)^2) & dom (f1/(f2+f1))=Z & Z = dom f &
   f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: Z c= dom (f1/(f2+f1)) & f1=#Z 2
   & (for x st x in Z holds f2.x=1 & x<>0) by A1; then
A3:(f1/(f2+f1)) is_differentiable_on Z by FDIFF_6:7;
A4:for x st x in dom ((f1/(f2+f1))`|Z) holds
   ((f1/(f2+f1))`|Z).x = f.x
   proof
     let x;
     assume x in dom ((f1/(f2+f1))`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((f1/(f2+f1))`|Z).x = 2*x/(1+x^2)^2 by B,FDIFF_6:7
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((f1/(f2+f1))`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((f1/(f2+f1))`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

theorem Th12:
  (Z c= dom (tan+sec) & for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0)
  implies tan+sec is_differentiable_on Z &
  for x st x in Z holds ((tan+sec)`|Z).x = 1/(1-sin.x)
proof
    assume that
A1: Z c= dom (tan+sec) and
A2: for x st x in Z holds 1+sin.x<>0 & 1-sin.x<>0;
    Z c= dom tan /\ dom (cos^) by A1,VALUED_1:def 1; then
A3: Z c= dom tan & Z c= dom (cos^) by XBOOLE_1:18; then
A4: for x st x in Z holds cos.x<>0 by FDIFF_8:1; then
A5: cos^ is_differentiable_on Z by FDIFF_4:39;
  for x st x in Z holds tan is_differentiable_in x
  proof
    let x;
    assume x in Z;
    then cos.x<>0 by A3,FDIFF_8:1;
    hence thesis by FDIFF_7:46;
  end; then
A6: tan is_differentiable_on Z by A3,FDIFF_1:16;
  for x st x in Z holds ((tan+sec)`|Z).x = 1/(1-sin.x)
  proof
    let x;
    assume
A7: x in Z; then
A8: cos.x<>0 & 1+sin.x<>0 & 1-sin.x<>0 by A2,A3,FDIFF_8:1;
    ((tan+sec)`|Z).x = diff(tan,x) + diff(cos^,x) by A1,A5,A6,A7,FDIFF_1:26
      .=1/(cos.x)^2 + diff(cos^,x) by A8,FDIFF_7:46
      .=1/(cos.x)^2 + ((cos^)`|Z).x by A5,A7,FDIFF_1:def 8
      .=1/(cos.x)^2 + sin.x/(cos.x)^2 by A4,A7,FDIFF_4:39
      .=(1+sin.x)/((cos.x)^2+(sin.x)^2-(sin.x)^2) by XCMPLX_1:63
      .=(1+sin.x)/(1-(sin.x)^2) by SIN_COS:31
      .=(1+sin.x)/((1+sin.x)*(1-sin.x))
      .=(1+sin.x)/(1+sin.x)/(1-sin.x) by XCMPLX_1:79
      .=1/(1-sin.x) by A8,XCMPLX_1:60;
    hence thesis;
  end;
  hence thesis by A1,A5,A6,FDIFF_1:26;
end;

:: f.x = 1/(1-sin.x)
theorem
  A c= Z & (for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0
  & f.x = 1/(1-sin.x)) & dom (tan+sec)=Z
  & Z = dom f & f|A is continuous implies
  integral(f,A) = (tan+sec).(sup A)-(tan+sec).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds (1+sin.x)<>0
   & (1-sin.x)<>0 & f.x = 1/(1-sin.x)) & dom (tan+sec)=Z
   & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0 by A1; then
A3:(tan+sec) is_differentiable_on Z by A1,Th12;
A4:for x st x in dom ((tan+sec)`|Z) holds ((tan+sec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((tan+sec)`|Z); then
 A5: x in Z by A3,FDIFF_1:def 8; then
     ((tan+sec)`|Z).x = 1/(1-sin.x) by A1,B,Th12
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((tan+sec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((tan+sec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

theorem Th14:
  (Z c= dom (tan-sec) & for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0)
  implies tan-sec is_differentiable_on Z &
  for x st x in Z holds ((tan-sec)`|Z).x = 1/(1+sin.x)
proof
  assume that
A1: Z c= dom (tan-sec) and
A2: for x st x in Z holds 1+sin.x<>0 & 1-sin.x<>0;
  Z c= dom tan /\ dom (cos^) by A1,VALUED_1:12; then
A3: Z c= dom tan & Z c= dom (cos^) by XBOOLE_1:18; then
A4: for x st x in Z holds cos.x<>0 by FDIFF_8:1; then
A5: cos^ is_differentiable_on Z by FDIFF_4:39;
  for x st x in Z holds tan is_differentiable_in x
  proof
    let x; assume x in Z;
    then cos.x<>0 by A3,FDIFF_8:1;
    hence thesis by FDIFF_7:46;
  end; then
A6: tan is_differentiable_on Z by A3,FDIFF_1:16;
  for x st x in Z holds ((tan-sec)`|Z).x = 1/(1+sin.x)
  proof
    let x;
    assume
A7: x in Z; then
A8: cos.x<>0 & 1+sin.x<>0 & 1-sin.x<>0 by A2,A3,FDIFF_8:1;
    ((tan-sec)`|Z).x = diff(tan,x) - diff(cos^,x) by A1,A5,A6,A7,FDIFF_1:27
      .=1/(cos.x)^2 - diff(cos^,x) by A8,FDIFF_7:46
      .=1/(cos.x)^2 - ((cos^)`|Z).x by A5,A7,FDIFF_1:def 8
      .=1/(cos.x)^2 - sin.x/(cos.x)^2 by A4,A7,FDIFF_4:39
      .=(1-sin.x)/((cos.x)^2+(sin.x)^2-(sin.x)^2) by XCMPLX_1:121
      .=(1-sin.x)/(1-(sin.x)^2) by SIN_COS:31
      .=(1-sin.x)/((1+sin.x)*(1-sin.x))
      .=(1-sin.x)/(1-sin.x)/(1+sin.x) by XCMPLX_1:79
      .=1/(1+sin.x) by A8,XCMPLX_1:60;
    hence thesis;
  end;
  hence thesis by A1,A5,A6,FDIFF_1:27;
end;

:: f.x = 1/(1+sin.x)
theorem
  A c= Z & (for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0
  & f.x = 1/(1+sin.x)) & dom (tan-sec)=Z
  & Z = dom f & f|A is continuous implies
  integral(f,A) = (tan-sec).(sup A)-(tan-sec).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds (1+sin.x)<>0
   & (1-sin.x)<>0 & f.x = 1/(1+sin.x)) & dom (tan-sec)=Z
   & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0 by A1; then
A3:(tan-sec) is_differentiable_on Z by A1,Th14;
A4:for x st x in dom ((tan-sec)`|Z) holds
   ((tan-sec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((tan-sec)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((tan-sec)`|Z).x = 1/(1+sin.x) by A1,B,Th14
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((tan-sec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((tan-sec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

theorem Th16:
  (Z c= dom (-cot+cosec) & for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0)
  implies -cot+cosec is_differentiable_on Z &
  for x st x in Z holds ((-cot+cosec)`|Z).x = 1/(1+cos.x)
proof
  assume that
A1: Z c= dom (-cot+cosec) and
A2: for x st x in Z holds 1+cos.x<>0 & 1-cos.x<>0;
  Z c= dom (-cot) /\ dom (sin^) by A1,VALUED_1:def 1; then
B: Z c= dom (-cot) & Z c= dom (sin^) by XBOOLE_1:18; then
A3:Z c= dom cot & Z c= dom (sin^) by VALUED_1:8; then
A4: for x st x in Z holds sin.x<>0 by FDIFF_8:2; then
A5: sin^ is_differentiable_on Z by FDIFF_4:40;
  for x st x in Z holds cot is_differentiable_in x
  proof
    let x; assume x in Z;
    then sin.x<>0 by A3,FDIFF_8:2;
    hence thesis by FDIFF_7:47;
  end; then
A6: cot is_differentiable_on Z by A3,FDIFF_1:16; then
AA: (-1)(#)cot is_differentiable_on Z & for x st x in Z holds
  (((-1)(#)cot)`|Z).x = (-1)*diff(cot,x) by B,FDIFF_1:28;
  for x st x in Z holds ((-cot+cosec)`|Z).x = 1/(1+cos.x)
  proof
    let x;
    assume
A7: x in Z; then
A8: sin.x<>0 & 1+cos.x<>0 & 1-cos.x<>0 by A2,A3,FDIFF_8:2;
    ((-cot+cosec)`|Z).x = diff(-cot,x) + diff(sin^,x)
      by A1,A5,AA,A7,FDIFF_1:26
      .=(((-1)(#)cot)`|Z).x + diff(sin^,x) by AA,A7,FDIFF_1:def 8
      .=(-1)*diff(cot,x) + diff(sin^,x) by B,A6,A7,FDIFF_1:28
      .=(-1)*(-1/(sin.x)^2)+ diff(sin^,x) by A8,FDIFF_7:47
      .=1/(sin.x)^2 + ((sin^)`|Z).x by A5,A7,FDIFF_1:def 8
      .=1/(sin.x)^2 + (-cos.x/(sin.x)^2) by A4,A7,FDIFF_4:40
      .=1/(sin.x)^2 - cos.x/(sin.x)^2
      .=(1-cos.x)/((sin.x)^2+(cos.x)^2-(cos.x)^2) by XCMPLX_1:121
      .=(1-cos.x)/(1-(cos.x)^2) by SIN_COS:31
      .=(1-cos.x)/((1-cos.x)*(1+cos.x))
      .=(1-cos.x)/(1-cos.x)/(1+cos.x) by XCMPLX_1:79
      .=1/(1+cos.x) by A8,XCMPLX_1:60;
    hence thesis;
  end;
  hence thesis by A1,A5,AA,FDIFF_1:26;
end;

:: f.x = 1/(1+cos.x)
theorem
  A c= Z & (for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0
   & f.x = 1/(1+cos.x)) & dom (-cot+cosec)=Z
  & Z = dom f & f|A is continuous implies
  integral(f,A) = (-cot+cosec).(sup A)-(-cot+cosec).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds (1+cos.x)<>0
   & (1-cos.x)<>0 & f.x = 1/(1+cos.x)) & dom (-cot+cosec)=Z
   & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0 by A1; then
A3:(-cot+cosec) is_differentiable_on Z by A1,Th16;
A4:for x st x in dom ((-cot+cosec)`|Z) holds ((-cot+cosec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((-cot+cosec)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((-cot+cosec)`|Z).x = 1/(1+cos.x) by A1,B,Th16
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((-cot+cosec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((-cot+cosec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

theorem Th18:
  (Z c= dom (-cot-cosec) & for x st x in Z holds (1+cos.x)<>0
  & (1-cos.x)<>0) implies -cot-cosec is_differentiable_on Z &
  for x st x in Z holds ((-cot-cosec)`|Z).x = 1/(1-cos.x)
proof
  assume that
A1: Z c= dom (-cot-cosec) and
A2: for x st x in Z holds 1+cos.x<>0 & 1-cos.x<>0;
  Z c= dom (-cot) /\ dom (sin^) by A1,VALUED_1:12; then
B: Z c= dom (-cot) & Z c= dom (sin^) by XBOOLE_1:18; then
A3:Z c= dom cot & Z c= dom (sin^) by VALUED_1:8; then
A4: for x st x in Z holds sin.x<>0 by FDIFF_8:2; then
A5: sin^ is_differentiable_on Z by FDIFF_4:40;
  for x st x in Z holds cot is_differentiable_in x
  proof
    let x;
    assume x in Z;
    then sin.x<>0 by A3,FDIFF_8:2;
    hence thesis by FDIFF_7:47;
  end; then
A6:cot is_differentiable_on Z by A3,FDIFF_1:16; then
AA:(-1)(#)cot is_differentiable_on Z & for x st x in Z holds
  (((-1)(#)cot)`|Z).x = (-1)*diff(cot,x) by B,FDIFF_1:28;
  for x st x in Z holds ((-cot-cosec)`|Z).x = 1/(1-cos.x)
  proof
    let x;
    assume
A7: x in Z; then
A8: sin.x<>0 & 1+cos.x<>0 & 1-cos.x<>0 by A2,A3,FDIFF_8:2;
    ((-cot-cosec)`|Z).x = diff(-cot,x) - diff(sin^,x)
      by A1,A5,AA,A7,FDIFF_1:27
      .=(((-1)(#)cot)`|Z).x - diff(sin^,x) by AA,A7,FDIFF_1:def 8
      .=(-1)*diff(cot,x) - diff(sin^,x) by B,A6,A7,FDIFF_1:28
      .=(-1)*(-1/(sin.x)^2)- diff(sin^,x) by A8,FDIFF_7:47
      .=1/(sin.x)^2 - ((sin^)`|Z).x by A5,A7,FDIFF_1:def 8
      .=1/(sin.x)^2 - (-cos.x/(sin.x)^2) by A4,A7,FDIFF_4:40
      .=1/(sin.x)^2 + cos.x/(sin.x)^2
      .=(1+cos.x)/((sin.x)^2+(cos.x)^2-(cos.x)^2) by XCMPLX_1:63
      .=(1+cos.x)/(1-(cos.x)^2) by SIN_COS:31
      .=(1+cos.x)/((1+cos.x)*(1-cos.x))
      .=(1+cos.x)/(1+cos.x)/(1-cos.x) by XCMPLX_1:79
      .=1/(1-cos.x) by A8,XCMPLX_1:60;
    hence thesis;
  end;
  hence thesis by A1,A5,AA,FDIFF_1:27;
end;

:: f.x = 1/(1-cos.x)
theorem
  A c= Z & (for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0
  & f.x = 1/(1-cos.x)) & dom (-cot-cosec)=Z
  & Z = dom f & f|A is continuous implies
  integral(f,A) = (-cot-cosec).(sup A)-(-cot-cosec).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds (1+cos.x)<>0
   & (1-cos.x)<>0 & f.x = 1/(1-cos.x)) & dom (-cot-cosec)=Z
   & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0 by A1; then
A3:(-cot-cosec) is_differentiable_on Z by A1,Th18;
A4:for x st x in dom ((-cot-cosec)`|Z) holds ((-cot-cosec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((-cot-cosec)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((-cot-cosec)`|Z).x = 1/(1-cos.x) by A1,B,Th18
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((-cot-cosec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((-cot-cosec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = 1/(1+x^2)
theorem
  A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds f.x=1/(1+x^2))
  & dom arctan=Z & Z = dom f & f|A is continuous implies
  integral(f,A) = arctan.(sup A) - arctan.(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds f.x=1/(1+x^2))
   & dom arctan=Z & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:arctan is_differentiable_on Z by A1,SIN_COS9:81;
A4:for x st x in dom ((arctan)`|Z) holds ((arctan)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((arctan)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((arctan)`|Z).x = 1/(1+x^2) by A1,SIN_COS9:81
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((arctan)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((arctan)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,SIN_COS9:81,INTEGRA5:13;
end;

:: f.x = r/(1+x^2)
theorem
  A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds f.x=r/(1+x^2))
  & dom (r(#)arctan)=Z & Z = dom f & f|A is continuous implies
  integral(f,A) = (r(#)arctan).(sup A) - (r(#)arctan).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds f.x=r/(1+x^2))
   & dom (r(#)arctan)=Z & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:r(#)arctan is_differentiable_on Z by A1,SIN_COS9:83;
A4:for x st x in dom ((r(#)arctan)`|Z) holds ((r(#)arctan)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((r(#)arctan)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((r(#)arctan)`|Z).x = r/(1+x^2) by A1,SIN_COS9:83
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((r(#)arctan)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((r(#)arctan)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,SIN_COS9:83,INTEGRA5:13;
end;

:: f.x = -1/(1+x^2)
theorem
  A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds f.x=-1/(1+x^2))
  & dom arccot=Z & Z = dom f & f|A is continuous implies
  integral(f,A) = arccot.(sup A) - arccot.(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds f.x=-1/(1+x^2))
   & dom arccot=Z & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:arccot is_differentiable_on Z by A1,SIN_COS9:82;
A4:for x st x in dom ((arccot)`|Z) holds ((arccot)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((arccot)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((arccot)`|Z).x = -1/(1+x^2) by A1,SIN_COS9:82
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((arccot)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((arccot)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,SIN_COS9:82,INTEGRA5:13;
end;

:: f.x = -r/(1+x^2)
theorem
  A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds f.x=-r/(1+x^2))
  & dom (r(#)arccot)=Z & Z = dom f & f|A is continuous implies
  integral(f,A) = (r(#)arccot).(sup A) - (r(#)arccot).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds f.x=-r/(1+x^2))
  & dom (r(#)arccot)=Z & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:r(#)arccot is_differentiable_on Z by A1,SIN_COS9:84;
A4:for x st x in dom ((r(#)arccot)`|Z) holds ((r(#)arccot)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((r(#)arccot)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((r(#)arccot)`|Z).x = -r/(1+x^2) by A1,SIN_COS9:84
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((r(#)arccot)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((r(#)arccot)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,SIN_COS9:84,INTEGRA5:13;
end;

:: f.x = x+cot.x-cosec.x
theorem Th24:
  (Z c= dom ((id Z)+cot-cosec) & for x st x in Z holds (1+cos.x)<>0 &
  (1-cos.x)<>0) implies (id Z)+cot-cosec is_differentiable_on Z &
  for x st x in Z holds ((id Z+cot-cosec)`|Z).x = cos.x/(1+cos.x)
proof
    assume that
A1: Z c= dom ((id Z)+cot-cosec) and
A2: for x st x in Z holds 1+cos.x<>0 & 1-cos.x<>0;
C:  Z c= dom ((id Z)+cot) /\ dom cosec by A1,VALUED_1:12; then
B1: Z c= dom (id Z+cot) & Z c= dom cosec by XBOOLE_1:18; then
    Z c= dom id Z /\ dom cot by VALUED_1:def 1;then
A3: Z c= dom cot & Z c= dom cosec & Z c= dom id Z by C,XBOOLE_1:18;
A5: cosec is_differentiable_on Z by B1,FDIFF_9:5;
    for x st x in Z holds cot is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A3,FDIFF_8:2;
      hence thesis by FDIFF_7:47;
    end; then
A6: cot is_differentiable_on Z by A3,FDIFF_1:16;
B3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35; then
B4: (id Z) is_differentiable_on Z & for x st x in Z
      holds ((id Z)`|Z).x = 1 by A3,FDIFF_1:31;
AA: id Z + cot is_differentiable_on Z by B1,B4,A6,FDIFF_1:26;
B5: for x st x in Z holds (((id Z) + cot)`|Z).x = -(cos.x)^2/(sin.x)^2
    proof
      let x;
      assume
A7:   x in Z; then
A8:   sin.x<>0 by A3,FDIFF_8:2; then
A9:   (sin.x)^2 >0 by SQUARE_1:74;
      ((id Z + cot)`|Z).x = diff((id Z),x)
          + diff(cot,x) by B1,A6,B4,A7,FDIFF_1:26
      .=((id Z)`|Z).x + diff(cot,x) by B4,A7,FDIFF_1:def 8
      .=1+diff(cot,x) by A3,B3,A7,FDIFF_1:31
      .=1+(-1/(sin.x)^2) by A8,FDIFF_7:47
      .=1-1/(sin.x)^2
      .=(sin.x)^2/(sin.x)^2-1/(sin.x)^2 by A9,XCMPLX_1:60
      .=((sin.x)^2-1)/(sin.x)^2 by XCMPLX_1:121
      .=((sin.x)^2-((sin.x)^2+(cos.x)^2))/(sin.x)^2 by SIN_COS:31
      .=(-(cos.x)^2)/(sin.x)^2
      .=-(cos.x)^2/(sin.x)^2 by XCMPLX_1:188;
      hence thesis;
    end;
    for x st x in Z holds ((id Z + cot-cosec)`|Z).x = cos.x/(1+cos.x)
    proof
      let x;
      assume
A10:  x in Z; then
A11:  sin.x<>0 & 1+cos.x<>0 & 1-cos.x<>0 by A2,A3,FDIFF_8:2;
      ((id Z + cot-cosec)`|Z).x=diff((id Z + cot),x)
        -diff(cosec,x) by A1,A5,AA,A10,FDIFF_1:27
      .=((id Z + cot)`|Z).x-diff(cosec,x) by AA,A10,FDIFF_1:def 8
      .=-(cos.x)^2/(sin.x)^2-diff(cosec,x) by B5,A10
      .=-(cos.x)^2/(sin.x)^2-((cosec)`|Z).x by A5,A10,FDIFF_1:def 8
      .=-(cos.x)^2/(sin.x)^2-(-cos.x/(sin.x)^2) by B1,A10,FDIFF_9:5
      .=cos.x/(sin.x)^2-(cos.x)^2/(sin.x)^2
      .=(cos.x-(cos.x)*(cos.x))/(sin.x)^2 by XCMPLX_1:121
      .=cos.x*(1-cos.x)/((sin.x)^2+(cos.x)^2-(cos.x)^2)
      .=cos.x*(1-cos.x)/(1-(cos.x)^2) by SIN_COS:31
      .=cos.x*(1-cos.x)/((1-cos.x)*(1+cos.x))
      .=cos.x*(1-cos.x)/(1-cos.x)/(1+cos.x) by XCMPLX_1:79
      .=cos.x*((1-cos.x)/(1-cos.x))/(1+cos.x) by XCMPLX_1:75
      .=cos.x*1/(1+cos.x) by A11,XCMPLX_1:60
      .=cos.x/(1+cos.x);
      hence thesis;
    end;
    hence thesis by A1,AA,A5,FDIFF_1:27;
end;

:: f.x = cos.x/(1+cos.x)
theorem
  A c= Z & (for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0
   & f.x = cos.x/(1+cos.x)) & dom (id Z+cot-cosec)=Z
  & Z = dom f & f|A is continuous implies
  integral(f,A) = (id Z+cot-cosec).(sup A)-(id Z+cot-cosec).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds (1+cos.x)<>0
   & (1-cos.x)<>0 & f.x = cos.x/(1+cos.x)) & dom (id Z+cot-cosec)=Z
   & Z = dom f & f|A is continuous; then
A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0 by A1; then
A3:(id Z+cot-cosec) is_differentiable_on Z by A1,Th24;
A4:for x st x in dom ((id Z+cot-cosec)`|Z) holds
   ((id Z+cot-cosec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((id Z+cot-cosec)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z+cot-cosec)`|Z).x = cos.x/(1+cos.x) by A1,B,Th24
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z+cot-cosec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((id Z+cot-cosec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = x+cot.x+cosec.x
theorem Th26:
  (Z c= dom ((id Z)+cot+cosec) & for x st x in Z holds (1+cos.x)<>0
   & (1-cos.x)<>0) implies (id Z)+cot+cosec is_differentiable_on Z &
 for x st x in Z holds ((id Z+cot+cosec)`|Z).x = cos.x/(cos.x-1)
proof
  assume that
A1: Z c= dom ((id Z)+cot+cosec) and
A2:for x st x in Z holds 1+cos.x<>0 & 1-cos.x<>0;
C: Z c= dom ((id Z)+cot) /\ dom cosec by A1,VALUED_1:def 1; then
B1:Z c= dom (id Z+cot) & Z c= dom cosec by XBOOLE_1:18; then
   Z c= dom id Z /\ dom cot by VALUED_1:def 1;then
A3: Z c= dom cot & Z c= dom cosec & Z c= dom id Z by C,XBOOLE_1:18;
A5: cosec is_differentiable_on Z by B1,FDIFF_9:5;
  for x st x in Z holds cot is_differentiable_in x
  proof
    let x;
    assume x in Z;
    then sin.x<>0 by A3,FDIFF_8:2;
    hence thesis by FDIFF_7:47;
  end; then
A6: cot is_differentiable_on Z by A3,FDIFF_1:16;
B3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35; then
B4: (id Z) is_differentiable_on Z & for x st x in Z
     holds ((id Z)`|Z).x = 1 by A3,FDIFF_1:31;
AA: id Z + cot is_differentiable_on Z by B1,B4,A6,FDIFF_1:26;
 B5: for x st x in Z holds (((id Z) + cot)`|Z).x = -(cos.x)^2/(sin.x)^2
  proof
    let x;
    assume
A7: x in Z; then
A8: sin.x<>0 by A3,FDIFF_8:2; then
A9: (sin.x)^2 >0 by SQUARE_1:74;
    ((id Z + cot)`|Z).x = diff((id Z),x)
          + diff(cot,x) by B1,A6,B4,A7,FDIFF_1:26
      .=((id Z)`|Z).x + diff(cot,x) by B4,A7,FDIFF_1:def 8
      .=1+diff(cot,x) by A3,B3,A7,FDIFF_1:31
      .=1+(-1/(sin.x)^2) by A8,FDIFF_7:47
      .=1-1/(sin.x)^2
      .=(sin.x)^2/(sin.x)^2-1/(sin.x)^2 by A9,XCMPLX_1:60
      .=((sin.x)^2-1)/(sin.x)^2 by XCMPLX_1:121
      .=((sin.x)^2-((sin.x)^2+(cos.x)^2))/(sin.x)^2 by SIN_COS:31
      .=(-(cos.x)^2)/(sin.x)^2
      .=-(cos.x)^2/(sin.x)^2 by XCMPLX_1:188;
    hence thesis;
  end;
  for x st x in Z holds ((id Z + cot+cosec)`|Z).x = cos.x/(cos.x-1)
  proof
    let x;
    assume
A10: x in Z; then
A11: sin.x<>0 & 1+cos.x<>0 & 1-cos.x<>0 by A2,A3,FDIFF_8:2;
  ((id Z + cot+cosec)`|Z).x=diff((id Z + cot),x)
        +diff(cosec,x) by A1,A5,AA,A10,FDIFF_1:26
      .=((id Z + cot)`|Z).x+diff(cosec,x) by AA,A10,FDIFF_1:def 8
      .=-(cos.x)^2/(sin.x)^2+diff(cosec,x) by B5,A10
      .=-(cos.x)^2/(sin.x)^2+((cosec)`|Z).x by A5,A10,FDIFF_1:def 8
      .=-(cos.x)^2/(sin.x)^2+(-cos.x/(sin.x)^2) by B1,A10,FDIFF_9:5
      .=-((cos.x)^2/(sin.x)^2+cos.x/(sin.x)^2)
      .=-((cos.x)*(cos.x)+cos.x)/(sin.x)^2 by XCMPLX_1:63
      .=-(cos.x)*(cos.x+1)/((sin.x)^2+(cos.x)^2-(cos.x)^2)
      .=-(cos.x)*(cos.x+1)/(1-(cos.x)^2) by SIN_COS:31
      .=-(cos.x)*(cos.x+1)/((1+cos.x)*(1-cos.x))
      .=-(cos.x)*(cos.x+1)/(1+cos.x)/(1-cos.x) by XCMPLX_1:79
      .=-(cos.x)*((1+cos.x)/(1+cos.x))/(1-cos.x) by XCMPLX_1:75
      .=-(cos.x)*1/(1-cos.x) by A11,XCMPLX_1:60
      .=(cos.x)/(-(1-cos.x)) by XCMPLX_1:189
      .=(cos.x)/(cos.x-1);
   hence thesis;
  end;
  hence thesis by A1,AA,A5,FDIFF_1:26;
end;

:: f.x = cos.x/(cos.x-1)
theorem
  A c= Z & (for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0
  & f.x = cos.x/(cos.x-1)) & dom (id Z+cot+cosec)=Z
  & Z = dom f & f|A is continuous implies
  integral(f,A) = (id Z+cot+cosec).(sup A)-(id Z+cot+cosec).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds (1+cos.x)<>0
   & (1-cos.x)<>0 & f.x = cos.x/(cos.x-1)) & dom (id Z+cot+cosec)=Z
  & Z = dom f & f|A is continuous; then
A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: for x st x in Z holds (1+cos.x)<>0 & (1-cos.x)<>0 by A1; then
A3: (id Z+cot+cosec) is_differentiable_on Z by A1,Th26;
A4:for x st x in dom ((id Z+cot+cosec)`|Z) holds
   ((id Z+cot+cosec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((id Z+cot+cosec)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z+cot+cosec)`|Z).x = cos.x/(cos.x-1) by A1,B,Th26
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z+cot+cosec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((id Z+cot+cosec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = x-tan.x+sec.x
theorem Th28:
  (Z c= dom (id Z-tan+sec) & for x st x in Z holds (1+sin.x)<>0
  & (1-sin.x)<>0) implies (id Z)-tan+sec is_differentiable_on Z &
  for x st x in Z holds ((id Z-tan+sec)`|Z).x = sin.x/(sin.x+1)
proof
  assume that
A1: Z c= dom (id Z-tan+sec) and
A2: for x st x in Z holds 1+sin.x<>0 & 1-sin.x<>0;
C1:  Z c= dom (id Z-tan) /\ dom sec by A1,VALUED_1:def 1; then
B1:Z c= dom (id Z-tan) & Z c= dom sec by XBOOLE_1:18; then
C2:  Z c= dom id Z /\ dom tan by VALUED_1:12;then
B2: Z c= dom tan & Z c= dom (id Z) by XBOOLE_1:18;
A3: Z c= dom tan & Z c= dom sec & Z c= dom id Z by C1,C2,XBOOLE_1:18;
A5: sec is_differentiable_on Z by B1,FDIFF_9:4;
  for x st x in Z holds tan is_differentiable_in x
  proof
    let x;
    assume x in Z;
    then cos.x<>0 by A3,FDIFF_8:1;
    hence thesis by FDIFF_7:46;
  end; then
A6: tan is_differentiable_on Z by A3,FDIFF_1:16;
B3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35; then
B4: (id Z) is_differentiable_on Z & for x st x in Z
     holds ((id Z)`|Z).x = 1 by B2,FDIFF_1:31;
AA: id Z-tan is_differentiable_on Z by B1,B4,A6,FDIFF_1:27;
B5: for x st x in Z holds ((id Z - tan)`|Z).x = -(sin.x)^2/(cos.x)^2
  proof
    let x;
    assume
A7: x in Z; then
A8: cos.x<>0 by A3,FDIFF_8:1; then
A9: (cos.x)^2 >0 by SQUARE_1:74;
    ((id Z - tan)`|Z).x = diff(id Z,x)
          - diff(tan,x) by B1,A6,B4,A7,FDIFF_1:27
      .=((id Z)`|Z).x - diff(tan,x) by B4,A7,FDIFF_1:def 8
      .=1-diff(tan,x) by B2,B3,A7,FDIFF_1:31
      .=1-1/(cos.x)^2 by A8,FDIFF_7:46
      .=1-((cos.x)^2+(sin.x)^2)/(cos.x)^2 by SIN_COS:31
      .=1-((cos.x)^2/(cos.x)^2+(sin.x)^2/(cos.x)^2) by XCMPLX_1:63
      .=1-(1+(sin.x)^2/(cos.x)^2) by A9,XCMPLX_1:60
      .=-(sin.x)^2/(cos.x)^2;
    hence thesis;
  end;
  for x st x in Z holds ((id Z-tan+sec)`|Z).x = sin.x/(sin.x+1)
  proof
    let x;
    assume
A10: x in Z; then
A11: cos.x<>0 & 1+sin.x<>0 & 1-sin.x<>0 by A2,B2,FDIFF_8:1;
     ((id Z-tan+sec)`|Z).x=diff((id Z-tan),x)
        +diff(sec,x) by A1,A5,AA,A10,FDIFF_1:26
      .=((id Z-tan)`|Z).x+diff(sec,x) by AA,A10,FDIFF_1:def 8
      .=-(sin.x)^2/(cos.x)^2+diff(sec,x) by B5,A10
      .=-(sin.x)^2/(cos.x)^2+((sec)`|Z).x by A5,A10,FDIFF_1:def 8
      .=-(sin.x)^2/(cos.x)^2+sin.x/(cos.x)^2 by B1,A10,FDIFF_9:4
      .=sin.x/(cos.x)^2-(sin.x)^2/(cos.x)^2
      .=(sin.x-(sin.x)*(sin.x))/(cos.x)^2 by XCMPLX_1:121
      .=(sin.x)*(1-sin.x)/((cos.x)^2+(sin.x)^2-(sin.x)^2)
      .=(sin.x)*(1-sin.x)/(1-(sin.x)^2) by SIN_COS:31
      .=(sin.x)*(1-sin.x)/((1-sin.x)*(1+sin.x))
      .=(sin.x)*(1-sin.x)/(1-sin.x)/(1+sin.x) by XCMPLX_1:79
      .=(sin.x)*((1-sin.x)/(1-sin.x))/(1+sin.x) by XCMPLX_1:75
      .=(sin.x)*1/(1+sin.x) by A11,XCMPLX_1:60
      .=sin.x/(1+sin.x);
   hence thesis;
  end;
  hence thesis by A1,AA,A5,FDIFF_1:26;
end;

:: f.x = sin.x/(1+sin.x)
theorem
  A c= Z & (for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0
   & f.x = sin.x/(1+sin.x)) & Z c= dom (id Z-tan+sec) &
  Z = dom f & f|A is continuous implies
  integral(f,A) = (id Z-tan+sec).(sup A)-(id Z-tan+sec).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds (1+sin.x)<>0
   & (1-sin.x)<>0 & f.x = sin.x/(1+sin.x)) & Z c= dom (id Z-tan+sec)
   & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0 by A1; then
A3:(id Z-tan+sec) is_differentiable_on Z by A1,Th28;
A4:for x st x in dom ((id Z-tan+sec)`|Z) holds ((id Z-tan+sec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((id Z-tan+sec)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z-tan+sec)`|Z).x = sin.x/(1+sin.x) by A1,B,Th28
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z-tan+sec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((id Z-tan+sec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = x-tan.x-sec.x
theorem Th30:
  (Z c= dom (id Z-tan-sec) & for x st x in Z holds (1+sin.x)<>0
   & (1-sin.x)<>0) implies id Z-tan-sec is_differentiable_on Z &
 for x st x in Z holds ((id Z-tan-sec)`|Z).x = sin.x/(sin.x-1)
proof
  assume that
A1: Z c= dom (id Z-tan-sec) and
A2: for x st x in Z holds 1+sin.x<>0 & 1-sin.x<>0;
C:  Z c= dom (id Z-tan) /\ dom sec by A1,VALUED_1:12; then
B1: Z c= dom (id Z-tan) & Z c= dom sec by XBOOLE_1:18; then
    Z c= dom id Z /\ dom tan by VALUED_1:12;then
A3: Z c= dom tan & Z c= dom sec & Z c= dom id Z by C,XBOOLE_1:18;
A5: sec is_differentiable_on Z by B1,FDIFF_9:4;
    for x st x in Z holds tan is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A3,FDIFF_8:1;
      hence thesis by FDIFF_7:46;
    end; then
A6: tan is_differentiable_on Z by A3,FDIFF_1:16;
B3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35; then
B4: (id Z) is_differentiable_on Z & for x st x in Z
     holds ((id Z)`|Z).x = 1 by A3,FDIFF_1:31;
AA: id Z-tan is_differentiable_on Z by B1,B4,A6,FDIFF_1:27;
B5: for x st x in Z holds ((id Z-tan)`|Z).x = -(sin.x)^2/(cos.x)^2
  proof
    let x;
    assume
A7: x in Z; then
A8: cos.x<>0 by A3,FDIFF_8:1; then
A9: (cos.x)^2 >0 by SQUARE_1:74;
    ((id Z - tan)`|Z).x = diff(id Z,x)
          - diff(tan,x) by B1,A6,B4,A7,FDIFF_1:27
      .=((id Z)`|Z).x - diff(tan,x) by B4,A7,FDIFF_1:def 8
      .=1-diff(tan,x) by A3,B3,A7,FDIFF_1:31
      .=1-1/(cos.x)^2 by A8,FDIFF_7:46
      .=1-((cos.x)^2+(sin.x)^2)/(cos.x)^2 by SIN_COS:31
      .=1-((cos.x)^2/(cos.x)^2+(sin.x)^2/(cos.x)^2) by XCMPLX_1:63
      .=1-(1+(sin.x)^2/(cos.x)^2) by A9,XCMPLX_1:60
      .=-(sin.x)^2/(cos.x)^2;
    hence thesis;
  end;
  for x st x in Z holds ((id Z-tan-sec)`|Z).x = sin.x/(sin.x-1)
  proof
    let x;
    assume
A10: x in Z; then
A11: cos.x<>0 & 1+sin.x<>0 & 1-sin.x<>0 by A2,A3,FDIFF_8:1;
    ((id Z-tan-sec)`|Z).x=diff((id Z-tan),x)
        -diff(sec,x) by A1,A5,AA,A10,FDIFF_1:27
      .=((id Z-tan)`|Z).x-diff(sec,x) by AA,A10,FDIFF_1:def 8
      .=-(sin.x)^2/(cos.x)^2-diff(sec,x) by B5,A10
      .=-(sin.x)^2/(cos.x)^2-((sec)`|Z).x by A5,A10,FDIFF_1:def 8
      .=-(sin.x)^2/(cos.x)^2-sin.x/(cos.x)^2 by B1,A10,FDIFF_9:4
      .=-(sin.x/(cos.x)^2+(sin.x)^2/(cos.x)^2)
      .=-((sin.x+(sin.x)^2)/(cos.x)^2) by XCMPLX_1:63
      .=-(sin.x)*(1+sin.x)/((cos.x)^2+(sin.x)^2-(sin.x)^2)
      .=-(sin.x)*(1+sin.x)/(1-(sin.x)^2) by SIN_COS:31
      .=-(sin.x)*(1+sin.x)/((1+sin.x)*(1-sin.x))
      .=-(sin.x)*(1+sin.x)/(1+sin.x)/(1-sin.x) by XCMPLX_1:79
      .=-(sin.x)*((1+sin.x)/(1+sin.x))/(1-sin.x) by XCMPLX_1:75
      .=-(sin.x)*1/(1-sin.x) by A11,XCMPLX_1:60
      .=sin.x/(-(1-sin.x)) by XCMPLX_1:189
      .=sin.x/(sin.x-1);
   hence thesis;
  end;
  hence thesis by A1,AA,A5,FDIFF_1:27;
end;

:: f.x = sin.x/(sin.x-1)
theorem
  A c= Z & (for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0
   & f.x = sin.x/(sin.x-1)) & Z c= dom (id Z-tan-sec)
  & Z = dom f & f|A is continuous implies
  integral(f,A) = (id Z-tan-sec).(sup A)-(id Z-tan-sec).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds (1+sin.x)<>0
   & (1-sin.x)<>0 & f.x = sin.x/(sin.x-1)) & Z c= dom (id Z-tan-sec)
   & Z = dom f & f|A is continuous; then
A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
B: for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0 by A1; then
A3: (id Z-tan-sec) is_differentiable_on Z by A1,Th30;
A4:for x st x in dom ((id Z-tan-sec)`|Z) holds
   ((id Z-tan-sec)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((id Z-tan-sec)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((id Z-tan-sec)`|Z).x = sin.x/(sin.x-1) by A1,B,Th30
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((id Z-tan-sec)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((id Z-tan-sec)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = tan.x-x
theorem Th32:
  Z c= dom (tan-id Z) implies tan-id Z is_differentiable_on Z &
  for x st x in Z holds ((tan-id Z)`|Z).x=(tan.x)^2
proof
  assume
A1: Z c= dom (tan-id Z);
  then Z c= dom tan /\ dom (id Z) by VALUED_1:12; then
A2: Z c= dom tan & Z c= dom (id Z) by XBOOLE_1:18;
A3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35; then
A4: id Z is_differentiable_on Z & for x st x in Z holds
  ((id Z)`|Z).x = 1 by A2,FDIFF_1:31;
  for x st x in Z holds tan is_differentiable_in x
  proof
    let x;
    assume x in Z;
    then cos.x<>0 by A2,FDIFF_8:1;
    hence thesis by FDIFF_7:46;
  end; then
A5: tan is_differentiable_on Z by A2,FDIFF_1:16;
  for x st x in Z holds ((tan-id Z)`|Z).x=(tan.x)^2
  proof
    let x;
    assume
A6: x in Z; then
A7: cos.x<>0 by A2,FDIFF_8:1; then
A8: (cos.x)^2 >0 by SQUARE_1:74;
    ((tan-id Z)`|Z).x= diff(tan,x) - diff(id Z,x) by A1,A4,A5,A6,FDIFF_1:27
      .=1/(cos.x)^2-diff(id Z,x) by A7,FDIFF_7:46
      .=1/(cos.x)^2-((id Z)`|Z).x by A4,A6,FDIFF_1:def 8
      .=1/(cos.x)^2-1 by A2,A3,A6,FDIFF_1:31
      .=1/(cos.x)^2-(cos.x)^2/(cos.x)^2 by A8,XCMPLX_1:60
      .=(1-(cos.x)^2)/(cos.x)^2 by XCMPLX_1:121
      .=((sin.x)^2+(cos.x)^2-(cos.x)^2)/(cos.x)^2 by SIN_COS:31
      .=(sin(x)/cos(x))*((sin.x)/(cos.x)) by XCMPLX_1:77
      .=(tan.x)*tan(x) by A2,A6,FDIFF_8:1,SIN_COS9:15
      .=(tan.x)^2 by A2,A6,FDIFF_8:1,SIN_COS9:15;
    hence thesis;
  end;
  hence thesis by A1,A4,A5,FDIFF_1:27;
end;

:: f.x = (tan.x)^2
theorem
  A c= Z & (for x st x in Z holds cos.x >0 & f.x = (tan.x)^2)
  & Z c= dom (tan-id Z) & Z = dom f & f|A is continuous
  implies integral(f,A) = (tan-id Z).(sup A)-(tan-id Z).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds cos.x >0 & f.x = (tan.x)^2)
  & Z c= dom (tan-id Z) & Z = dom f & f|A is continuous;
   then A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3: (tan-id Z) is_differentiable_on Z by A1,Th32;
A4:for x st x in dom ((tan-id Z)`|Z) holds ((tan-id Z)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((tan-id Z)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((tan-id Z)`|Z).x = (tan.x)^2 by A1,Th32
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((tan-id Z)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((tan-id Z)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = -cot.x-x
theorem Th34:
  Z c= dom (-cot-id Z) implies -cot-id Z is_differentiable_on Z &
  for x st x in Z holds ((-cot-id Z)`|Z).x = (cot.x)^2
proof
  assume
A1: Z c= dom (-cot-id Z);
  then Z c= dom (-cot) /\ dom (id Z) by VALUED_1:12; then
A2: Z c= dom (-cot) & Z c= dom (id Z) by XBOOLE_1:18; then
A3: Z c= dom cot by VALUED_1:8;
A4: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35; then
A5: id Z is_differentiable_on Z & for x st x in Z holds
  ((id Z)`|Z).x = 1 by A2,FDIFF_1:31;
  for x st x in Z holds cot is_differentiable_in x
  proof
    let x;
    assume x in Z;
    then sin.x<>0 by A3,FDIFF_8:2;
    hence thesis by FDIFF_7:47;
  end; then
A6: cot is_differentiable_on Z by A3,FDIFF_1:16; then
A7: (-1)(#)cot is_differentiable_on Z & for x st x in Z holds
  (((-1)(#)cot)`|Z).x = (-1)*diff(cot,x) by A2,FDIFF_1:28;
  set f = -cot;
  for x st x in Z holds ((-cot-id Z)`|Z).x = (cot.x)^2
  proof
    let x;
    assume
A8: x in Z; then
A9: sin.x<>0 by A3,FDIFF_8:2; then
A10: (sin.x)^2 >0 by SQUARE_1:74;
    ((f-id Z)`|Z).x=diff(f,x) - diff(id Z,x) by A1,A5,A7,A8,FDIFF_1:27
      .=(((-1)(#)cot)`|Z).x-diff(id Z,x) by A7,A8,FDIFF_1:def 8
      .=(-1)*diff(cot,x)-diff(id Z,x) by A2,A6,A8,FDIFF_1:28
      .=(-1)*(-1/(sin.x)^2)-diff(id Z,x) by A9,FDIFF_7:47
      .=1/(sin.x)^2-((id Z)`|Z).x by A5,A8,FDIFF_1:def 8
      .=1/(sin.x)^2-1 by A2,A4,A8,FDIFF_1:31
      .=1/(sin.x)^2-(sin.x)^2/(sin.x)^2 by A10,XCMPLX_1:60
      .=(1-(sin.x)^2)/(sin.x)^2 by XCMPLX_1:121
      .=((cos.x)^2+(sin.x)^2-(sin.x)^2)/(sin.x)^2 by SIN_COS:31
      .=(cos(x)/sin(x))*((cos.x)/(sin.x)) by XCMPLX_1:77
      .=(cot.x)*cot(x) by A3,FDIFF_8:2,A8,SIN_COS9:16
      .=(cot.x)^2 by A3,FDIFF_8:2,A8,SIN_COS9:16;
    hence thesis;
  end;
  hence thesis by A1,A5,A7,FDIFF_1:27;
end;

:: f.x = (cot.x)^2
theorem
  A c= Z & (for x st x in Z holds sin.x >0 & f.x = (cot.x)^2)
  & Z c= dom (-cot-id Z) & Z = dom f & f|A is continuous
  implies integral(f,A) = (-cot-id Z).(sup A)-(-cot-id Z).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds sin.x >0 & f.x = (cot.x)^2)
   & Z c= dom (-cot-id Z) & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:(-cot-id Z) is_differentiable_on Z by A1,Th34;
A4:for x st x in dom ((-cot-id Z)`|Z) holds
   ((-cot-id Z)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((-cot-id Z)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((-cot-id Z)`|Z).x = (cot.x)^2 by A1,Th34
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((-cot-id Z)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((-cot-id Z)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = 1/(cos.x)^2
theorem
  A c= Z & (for x st x in Z holds f.x = 1/(cos.x)^2 & cos.x <> 0)
  & dom tan=Z & Z = dom f & f|A is continuous
  implies integral(f,A) = tan.(sup A)-tan.(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = 1/(cos.x)^2 & cos.x <> 0)
    & dom tan=Z & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:tan is_differentiable_on Z by A1,INTEGRA8:33;
A4:for x st x in dom (tan`|Z) holds (tan`|Z).x = f.x
   proof
     let x;
     assume x in dom (tan`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     (tan`|Z).x = 1/(cos.x)^2 by A1,INTEGRA8:33
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom (tan`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   (tan`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = -1/(sin.x)^2
theorem
  A c= Z & (for x st x in Z holds f.x = -1/(sin.x)^2 & sin.x <> 0)
  & dom cot=Z & Z = dom f & f|A is continuous
  implies integral(f,A) = cot.(sup A)-cot.(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = -1/(sin.x)^2 & sin.x <> 0)
  & dom cot=Z & Z = dom f & f|A is continuous;
   then A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3: cot is_differentiable_on Z by A1,INTEGRA8:34;
A4:for x st x in dom (cot`|Z) holds
   (cot`|Z).x = f.x
   proof
     let x;
     assume x in dom (cot`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     (cot`|Z).x = -1/(sin.x)^2 by A1,INTEGRA8:34
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom (cot`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   (cot`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = (sin.x-(cos.x)^2)/(cos.x)^2
theorem
  A c= Z & (for x st x in Z holds f.x = (sin.x-(cos.x)^2)/(cos.x)^2)
  & Z c= dom (sec-id Z) & Z = dom f & f|A is continuous
  implies integral(f,A) = (sec-id Z).(sup A)-(sec-id Z).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = (sin.x-(cos.x)^2)/(cos.x)^2)
   & Z c= dom (sec-id Z) & Z = dom f & f|A is continuous;
   then A2: f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3: (sec-id Z) is_differentiable_on Z by A1,FDIFF_9:22;
A4:for x st x in dom ((sec-id Z)`|Z) holds
   ((sec-id Z)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((sec-id Z)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((sec-id Z)`|Z).x = (sin.x-(cos.x)^2)/(cos.x)^2 by A1,FDIFF_9:22
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((sec-id Z)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((sec-id Z)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,FDIFF_9:22,INTEGRA5:13;
end;

:: f.x = (cos.x-(sin.x)^2)/(sin.x)^2
theorem
  A c= Z & (for x st x in Z holds f.x = (cos.x-(sin.x)^2)/(sin.x)^2)
  & Z c= dom (-cosec-id Z) & Z = dom f & f|A is continuous
 implies integral(f,A) =(-cosec-id Z).(sup A)-(-cosec-id Z).(inf A)
proof
   assume
A1:A c= Z & (for x st x in Z holds f.x = (cos.x-(sin.x)^2)/(sin.x)^2)
   & Z c= dom (-cosec-id Z) & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:(-cosec-id Z) is_differentiable_on Z by A1,FDIFF_9:23;
A4:for x st x in dom ((-cosec-id Z)`|Z) holds
   ((-cosec-id Z)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((-cosec-id Z)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     ((-cosec-id Z)`|Z).x = (cos.x-(sin.x)^2)/(sin.x)^2 by A1,FDIFF_9:23
      .= f.x by A1,A5;
     hence thesis;
   end;
   dom ((-cosec-id Z)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((-cosec-id Z)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,FDIFF_9:23,INTEGRA5:13;
end;

:: f.x = cot(x)
theorem
  A c= Z & (for x st x in Z holds sin.x >0)
  & Z c= dom (ln*sin) & Z = dom cot & cot|A is continuous
  implies integral(cot,A) = (ln*sin).(sup A)-(ln*sin).(inf A)
proof
   set f = cot;
   assume
A1:A c= Z & (for x st x in Z holds sin.x >0)
     & Z c= dom (ln*sin) & Z = dom cot & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:(ln*sin) is_differentiable_on Z by A1,FDIFF_4:43;
A4:for x st x in dom ((ln*sin)`|Z) holds ((ln*sin)`|Z).x = f.x
   proof
     let x;
     assume x in dom ((ln*sin)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
AA:  x is Real & sin.x <> 0 by A1;
     ((ln*sin)`|Z).x = cot(x) by A5,A1,FDIFF_4:43
      .= f.x by SIN_COS9:16,AA;
     hence thesis;
   end;
   dom ((ln*sin)`|Z) = dom f by A1,A3,FDIFF_1:def 8;then
   ((ln*sin)`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,FDIFF_4:43,INTEGRA5:13;
end;

:: f.x = arcsin.x / sqrt(1-x^2)
theorem
  A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds
  f.x=arcsin.x/sqrt(1-x^2)) & Z c= dom ((1/2)(#)(( #Z 2)*(arcsin)))
  & Z = dom f & f|A is continuous implies
  integral(f,A) = ((1/2)(#)(( #Z 2)*(arcsin))).(sup A)
     -((1/2)(#)(( #Z 2)*(arcsin))).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds
   f.x=arcsin.x/sqrt(1-x^2)) & Z c= dom ((1/2)(#)(( #Z 2)*(arcsin)))
   & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:((1/2)(#)(( #Z 2)*(arcsin))) is_differentiable_on Z by A1,FDIFF_7:12;
A4:for x st x in dom (((1/2)(#)(( #Z 2)*(arcsin)))`|Z) holds
   (((1/2)(#)(( #Z 2)*(arcsin)))`|Z).x = f.x
   proof
     let x;
     assume x in dom (((1/2)(#)(( #Z 2)*(arcsin)))`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     (((1/2)(#)(( #Z 2)*(arcsin)))`|Z).x
      = arcsin.x / sqrt(1-x^2) by A1,FDIFF_7:12
     .= f.x by A1,A5;
     hence thesis;
   end;
   dom (((1/2)(#)(( #Z 2)*(arcsin)))`|Z) = dom f
            by A1,A3,FDIFF_1:def 8;then
   (((1/2)(#)(( #Z 2)*(arcsin)))`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,FDIFF_7:12,INTEGRA5:13;
end;

:: f.x = -arccos.x / sqrt(1-x^2)
theorem
  A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds
  f.x=-arccos.x / sqrt(1-x^2)) & Z c= dom ((1/2)(#)(( #Z 2)*(arccos)))
  & Z = dom f & f|A is continuous implies
  integral(f,A) = ((1/2)(#)(( #Z 2)*(arccos))).(sup A)
     -((1/2)(#)(( #Z 2)*(arccos))).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & (for x st x in Z holds
   f.x=-arccos.x / sqrt(1-x^2)) & Z c= dom ((1/2)(#)(( #Z 2)*(arccos)))
   & Z = dom f & f|A is continuous; then
A2:f is_integrable_on A & f|A is bounded by INTEGRA5:10,11;
A3:((1/2)(#)(( #Z 2)*(arccos))) is_differentiable_on Z by A1,FDIFF_7:13;
A4:for x st x in dom (((1/2)(#)(( #Z 2)*(arccos)))`|Z) holds
   (((1/2)(#)(( #Z 2)*(arccos)))`|Z).x = f.x
   proof
     let x;
     assume x in dom (((1/2)(#)(( #Z 2)*(arccos)))`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8; then
     (((1/2)(#)(( #Z 2)*(arccos)))`|Z).x
      = -arccos.x / sqrt(1-x^2) by A1,FDIFF_7:13
     .= f.x by A1,A5;
     hence thesis;
   end;
   dom (((1/2)(#)(( #Z 2)*(arccos)))`|Z) = dom f
            by A1,A3,FDIFF_1:def 8;then
   (((1/2)(#)(( #Z 2)*(arccos)))`|Z) = f by A4,PARTFUN1:34;
   hence thesis by A1,A2,FDIFF_7:13,INTEGRA5:13;
end;

:: f.x = arcsin.x
theorem
  A c= Z & Z c= ]. -1,1 .[ & f=f1-f2 & f2=#Z 2
  & (for x st x in Z holds f1.x=1 & f.x >0 & x<>0) & dom arcsin=Z
  & Z c= dom ((id Z)(#)(arcsin)+( #R (1/2))*f) implies
  integral(arcsin,A) = ((id Z)(#)(arcsin)+( #R (1/2))*f).(sup A)
     -((id Z)(#)(arcsin) + ( #R (1/2))*f).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & f=f1-f2 & f2=#Z 2
    & (for x st x in Z holds f1.x=1 & f.x >0 & x<>0) & dom arcsin=Z
    & Z c= dom ((id Z)(#)(arcsin)+( #R (1/2))*f);
    arcsin|A is continuous; then
A2:arcsin is_integrable_on A & arcsin|A is bounded by A1,INTEGRA5:10,11;
A3:((id Z)(#)(arcsin)+( #R (1/2))*f) is_differentiable_on Z by A1,FDIFF_7:23;
A4:for x st x in dom (((id Z)(#)(arcsin)+( #R (1/2))*f)`|Z) holds
   (((id Z)(#)(arcsin)+( #R (1/2))*f)`|Z).x = arcsin.x
   proof
     let x;
     assume x in dom (((id Z)(#)(arcsin)+( #R (1/2))*f)`|Z); then
     x in Z by A3,FDIFF_1:def 8;
     hence thesis by A1,FDIFF_7:23;
   end;
   dom (((id Z)(#)(arcsin)+( #R (1/2))*f)`|Z) = dom arcsin
     by A1,A3,FDIFF_1:def 8;then
   (((id Z)(#)(arcsin)+( #R (1/2))*f)`|Z) = arcsin by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = arcsin.(x/a)
theorem
  A c= Z & Z c= ]. -1,1 .[ & f=f1-f2 & f2=#Z 2
  & (for x st x in Z holds f1.x=a^2 & f.x >0 &
  f3.x=x/a & f3.x > -1 & f3.x < 1 & x<>0 & a>0) & dom ((arcsin)*f3)=Z
  & Z c= dom ((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)
  & ((arcsin)*f3)|A is continuous implies
  integral((arcsin)*f3,A) = ((id Z)(#)((arcsin)*f3)+( #R (1/2))*f).(sup A)
     -((id Z)(#)((arcsin)*f3)+( #R (1/2))*f).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & f=f1-f2 & f2=#Z 2
   & (for x st x in Z holds f1.x=a^2 & f.x >0 &
   f3.x=x/a & f3.x > -1 & f3.x < 1 & x<>0 & a>0) & dom ((arcsin)*f3)=Z
   & Z c= dom ((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)
   & ((arcsin)*f3)|A is continuous; then
A2:(arcsin)*f3 is_integrable_on A & ((arcsin)*f3)|A is bounded
     by INTEGRA5:10,11;
A3:((id Z)(#)((arcsin)*f3)+( #R (1/2))*f) is_differentiable_on Z
     by A1,FDIFF_7:28;
A4:for x st x in dom (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z) holds
   (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z).x = ((arcsin)*f3).x
   proof
     let x;
     assume x in dom (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8;
     (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z).x
        = arcsin.(x/a) by A1,A5,FDIFF_7:28
       .= arcsin.(f3.x) by A1,A5
       .= ((arcsin)*f3).x by A5,A1,FUNCT_1:22;
     hence thesis;
   end;
   dom (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z) = dom ((arcsin)*f3)
            by A1,A3,FDIFF_1:def 8;then
   (((id Z)(#)((arcsin)*f3)+( #R (1/2))*f)`|Z)
          = (arcsin)*f3 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = arccos.x
theorem
  A c= Z & Z c= ]. -1,1 .[ & f=f1-f2 & f2=#Z 2
  & (for x st x in Z holds f1.x=1 & f.x >0 & x<>0) & dom arccos=Z
  & Z c= dom ((id Z)(#)(arccos)-( #R (1/2))*f) implies
  integral(arccos,A) = ((id Z)(#)(arccos)-( #R (1/2))*f).(sup A)
     -((id Z)(#)(arccos)-( #R (1/2))*f).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & f=f1-f2 & f2=#Z 2
   & (for x st x in Z holds f1.x=1 & f.x >0 & x<>0) & dom arccos=Z
   & Z c= dom ((id Z)(#)(arccos)-( #R (1/2))*f);
   arccos|A is continuous; then
A2:arccos is_integrable_on A & arccos|A is bounded by A1,INTEGRA5:10,11;
A3:((id Z)(#)(arccos)-( #R (1/2))*f) is_differentiable_on Z
     by A1,FDIFF_7:24;
A4:for x st x in dom (((id Z)(#)(arccos)-( #R (1/2))*f)`|Z) holds
   (((id Z)(#)(arccos)-( #R (1/2))*f)`|Z).x = arccos.x
   proof
     let x;
     assume x in dom (((id Z)(#)(arccos)-( #R (1/2))*f)`|Z); then
     x in Z by A3,FDIFF_1:def 8;
     hence thesis by A1,FDIFF_7:24;
   end;
   dom (((id Z)(#)(arccos)-( #R (1/2))*f)`|Z) = dom arccos
     by A1,A3,FDIFF_1:def 8;then
   (((id Z)(#)(arccos)-( #R (1/2))*f)`|Z) = arccos by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = arccos.(x/a)
theorem
  A c= Z & Z c= ]. -1,1 .[ & f=f1-f2 & f2=#Z 2
  & (for x st x in Z holds f1.x=a^2 & f.x >0 &
  f3.x=x/a & f3.x > -1 & f3.x < 1 & x<>0 & a>0) & dom ((arccos)*f3)=Z
  & Z = dom ((id Z)(#)((arccos)*f3)-( #R (1/2))*f)
  & ((arccos)*f3)|A is continuous implies
  integral((arccos)*f3,A) = ((id Z)(#)((arccos)*f3)-( #R (1/2))*f).(sup A)
     -((id Z)(#)((arccos)*f3)-( #R (1/2))*f).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & f=f1-f2 & f2=#Z 2
   & (for x st x in Z holds f1.x=a^2 & f.x >0 &
   f3.x=x/a & f3.x > -1 & f3.x < 1 & x<>0 & a>0) & dom ((arccos)*f3)=Z
   & Z = dom ((id Z)(#)((arccos)*f3)-( #R (1/2))*f)
   & ((arccos)*f3)|A is continuous; then
A2:(arccos)*f3 is_integrable_on A & ((arccos)*f3)|A is bounded
    by INTEGRA5:10,11;
A3:((id Z)(#)((arccos)*f3)-( #R (1/2))*f) is_differentiable_on Z
    by A1,FDIFF_7:29;
A4:for x st x in dom (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z) holds
   (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z).x = ((arccos)*f3).x
   proof
     let x;
     assume x in dom (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z); then
A5:  x in Z by A3,FDIFF_1:def 8;
     (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z).x
        = arccos.(x/a) by A1,A5,FDIFF_7:29
       .= arccos.(f3.x) by A1,A5
       .= ((arccos)*f3).x by A5,A1,FUNCT_1:22;
     hence thesis;
   end;
   dom (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z) = dom ((arccos)*f3)
     by A1,A3,FDIFF_1:def 8;then
   (((id Z)(#)((arccos)*f3)-( #R (1/2))*f)`|Z)
     = (arccos)*f3 by A4,PARTFUN1:34;
   hence thesis by A1,A2,A3,INTEGRA5:13;
end;

:: f.x = arctan.x
theorem
  A c= Z & Z c= ]. -1,1 .[ & f2=#Z 2 & (for x st x in Z holds f1.x=1 )
  & Z = dom arctan & Z = dom ((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2)))
  implies
  integral(arctan,A) = ((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2))).(sup A)
     -((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2))).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & f2=#Z 2 & (for x st x in Z holds f1.x=1)
     & Z = dom arctan & Z = dom ((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2)));
Ab: ].-1,1 .[ c= [.-1,1 .] by XXREAL_1:25;
   A c= ].-1,1 .[ by A1,XBOOLE_1:1; then
   arctan|A is continuous by SIN_COS9:53,FCONT_1:17,Ab,XBOOLE_1:1; then
A2:arctan is_integrable_on A & arctan|A is bounded by INTEGRA5:10,11,A1;
A3:((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2))) is_differentiable_on Z
     by A1,SIN_COS9:103;
A4:for x st x in dom (((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2)))`|Z) holds
   (((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2)))`|Z).x = arctan.x
   proof
     let x;
     assume x in dom (((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2)))`|Z); then
     x in Z by A3,FDIFF_1:def 8;
     hence thesis by A1,SIN_COS9:103;
   end;
   dom (((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2)))`|Z) = dom arctan
            by A1,A3,FDIFF_1:def 8;then
   (((id Z)(#)(arctan)-(1/2)(#)(ln*(f1+f2)))`|Z)
          = arctan by A4,PARTFUN1:34;
   hence thesis by A1,A2,SIN_COS9:103,INTEGRA5:13;
end;

:: f.x = arccot.x
theorem
  A c= Z & Z c= ]. -1,1 .[ & f2=#Z 2 & (for x st x in Z holds f1.x=1)
  & dom arccot=Z & Z = dom ((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2)))
  implies
  integral(arccot,A) = ((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2))).(sup A)
     -((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2))).(inf A)
proof
   assume
A1:A c= Z & Z c= ]. -1,1 .[ & f2=#Z 2 & (for x st x in Z holds f1.x=1)
   & dom arccot=Z & Z = dom ((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2)));
Ab: ].-1,1 .[ c= [.-1,1 .] by XXREAL_1:25;
   A c= ].-1,1 .[ by A1,XBOOLE_1:1; then
   arccot|A is continuous by SIN_COS9:54,FCONT_1:17,Ab,XBOOLE_1:1; then
A2: arccot is_integrable_on A & arccot|A is bounded by INTEGRA5:10,11,A1;
A3:((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2))) is_differentiable_on Z
     by A1,SIN_COS9:104;
A4:for x st x in dom (((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2)))`|Z) holds
   (((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2)))`|Z).x = arccot.x
   proof
     let x;
     assume x in dom (((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2)))`|Z);then
     x in Z by A3,FDIFF_1:def 8;
     hence thesis by A1,SIN_COS9:104;
   end;
   dom (((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2)))`|Z) = dom arccot
            by A1,A3,FDIFF_1:def 8;then
   (((id Z)(#)(arccot)+(1/2)(#)(ln*(f1+f2)))`|Z)
          = arccot by A4,PARTFUN1:34;
   hence thesis by A1,A2,SIN_COS9:104,INTEGRA5:13;
end;

