:: Lines in $n$-Dimensional Euclidean Spaces
::  by Akihiro Kubo
::
:: Received August 8, 2003
:: Copyright (c) 2003 Association of Mizar Users
 
environ

 vocabularies REAL_1, NAT_1, SUBSET_1, NUMBERS, CARD_1, RELAT_1, ARYTM_3,
      EUCLID, FINSEQ_2, COMPLEX1, ARYTM_1, INCSP_1, TARSKI, XBOOLE_0, AFF_1,
      SQUARE_1, RVSUM_1, FINSEQ_1, XREAL_0, ORDINAL1, XXREAL_0, PRE_TOPC,
      SEQ_4, XXREAL_2, STRUCT_0, SUPINF_2, EUCLID_4;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XXREAL_2, XREAL_0,
      COMPLEX1, REAL_1, NAT_1, STRUCT_0, PRE_TOPC, FINSEQ_1, FINSEQ_2, RVSUM_1,
      SQUARE_1, RLVECT_1, EUCLID, SEQ_4, XXREAL_0;
 constructors REAL_1, SQUARE_1, BINOP_2, COMPLEX1, SEQ_4, FINSEQOP, MONOID_0,
      EUCLID, XXREAL_2, RELSET_1;
 registrations RELSET_1, NUMBERS, XREAL_0, BINOP_2, MEMBERED, STRUCT_0,
      MONOID_0, EUCLID, VALUED_0, VALUED_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, SQUARE_1, RVSUM_1, EUCLID, VALUED_1, XXREAL_2;
 theorems EUCLID, TARSKI, SQUARE_1, XBOOLE_0, SEQ_4, XCMPLX_1, RVSUM_1,
      EUCLID_2, XREAL_1, FINSEQOP, FINSEQ_1;

begin

reserve a,b,s,t,u,lambda for Real,
  n for Nat;
reserve x,x1,x2,x3,y1,y2 for Element of REAL n;

theorem Th1: :: EUCLID:31
  0 * x + x = x & x + 0*n = x
proof
  reconsider x9 = x as Element of n-tuples_on REAL;
  0 *x + x = 0 *x9 + 1*x9 by RVSUM_1:74
    .= (0+1)*x9 by RVSUM_1:72
    .= x by RVSUM_1:74;
  hence thesis by RVSUM_1:33;
end;

theorem Th2: :: EUCLID:32
  a*(0*n) = 0*n
proof
  |.a*(0*n).| = abs(a)*|.0*n.| by EUCLID:14
    .= abs(a)*0 by EUCLID:10
    .= 0;
  hence thesis by EUCLID:11;
end;

theorem Th3: :: EUCLID:33
  1*x = x & 0 * x = 0*n
proof
  reconsider x9 = x as Element of n-tuples_on REAL;
  (0)*x = (0)*x9 + x9 - x9 by RVSUM_1:63
    .= x9 - x9 by Th1
    .= 0*n by RVSUM_1:58;
  hence thesis by RVSUM_1:74;
end;

theorem :: EUCLID:34
  (a*b)*x = a*(b*x) by RVSUM_1:71;

theorem :: EUCLID:35
  a*x = 0*n implies a = 0 or x = 0*n
proof
  assume that
A1: a*x = 0*n and
A2: a <> 0;
  thus x = 1*x by Th3
    .= ((1/a)*a)*x by A2,XCMPLX_1:107
    .= (1/a)*(a*x) by RVSUM_1:71
    .= 0*n by A1,Th2;
end;

theorem :: EUCLID:36
  a*(x1 + x2) = a*x1 + a*x2 by RVSUM_1:73;

theorem :: EUCLID:37
  (a + b)*x = a*x + b*x by RVSUM_1:72;

theorem :: EUCLID:38
  a*x1 = a*x2 implies a = 0 or x1 = x2
proof
  assume that
A1: a*x1 = a*x2 and
A2: a <> 0;
  ((1/a)*a)*x1 = (1/a)*(a*x2) by A1,RVSUM_1:71;
  then ((1/a)*a)*x1 = ((1/a)*a)*x2 by RVSUM_1:71;
  then 1*x1 = ((1/a)*a)*x2 by A2,XCMPLX_1:107;
  then 1*x1 = 1*x2 by A2,XCMPLX_1:107;
  hence x1 = 1*x2 by Th3
    .= x2 by Th3;
end;

definition
  let n;
  let x1,x2 be Element of REAL n;
  func Line(x1,x2) -> Subset of REAL n equals
  { (1-lambda)*x1 + lambda*x2 :
  not contradiction };
  coherence
  proof
    set A = { (1-lambda)*x1 + lambda*x2 : not contradiction };
    A c= REAL n
    proof
      let x be set;
      assume x in A;
      then ex lambda st x = (1-lambda)*x1 + lambda*x2;
      hence thesis;
    end;
    hence thesis;
  end;
end;

registration
  let n;
  let x1,x2 be Element of REAL n;
  cluster Line(x1,x2) -> non empty;
  coherence
  proof
A1: 1 - 0 = 1 & 1 * x1 = x1 by Th3;
    0 * x2 = 0*n & x1 + 0*n = x1 by Th1,Th3;
    then x1 in Line(x1,x2) by A1;
    hence thesis;
  end;
end;

Lm1: Line(x1,x2) c= Line(x2,x1)
proof
  let z be set;
  assume z in Line(x1,x2);
  then consider t such that
A1: z = (1-t)*x1 + t*x2;
  z = (1-(1-t))*x2 + (1-t)*x1 by A1;
  hence thesis;
end;

definition
  let n;
  let x1,x2 be Element of REAL n;
  redefine func Line(x1,x2);
  commutativity
proof let x1,x2;
  Line(x1,x2) c= Line(x2,x1) & Line(x2,x1) c= Line(x1,x2) by Lm1;
  hence thesis by XBOOLE_0:def 10;
end;
end;

canceled;

theorem Th10: :: AFF_1:26
  x1 in Line(x1,x2) & x2 in Line(x1,x2)
proof
A1: 1 - 0 = 1 & 1*x1 = x1 by Th3;
A2: 1 - 1 = 0 & (0)*x1 = 0*n by Th3;
A3: 1*x2 = x2 & 0*n + x2 = x2 by Th1,Th3;
  (0)*x2 = 0*n & x1 + 0*n = x1 by Th1,Th3;
  hence thesis by A1,A2,A3;
end;

Lm2: x1 + (x2 + x3) = (x1 + x2) + x3 by FINSEQOP:29;

theorem Th11: :: AFF_1:27
  y1 in Line(x1,x2) & y2 in Line(x1,x2) implies Line(y1,y2) c= Line(x1,x2)
proof
  assume y1 in Line(x1,x2);
  then consider t such that
A1: y1 = (1-t)*x1 + t*x2;
  assume y2 in Line(x1,x2);
  then consider s such that
A2: y2 = (1-s)*x1 + s*x2;
  now
    let z be set;
    assume z in Line(y1,y2);
    then consider u such that
A3: z = (1-u)*y1 + u*y2;
    z = ((1-u)*((1-t)*x1)+(1-u)*(t*x2))+u*((1-s)*x1+s*x2) by A1,A2,A3,
RVSUM_1:73
      .= ((1-u)*((1-t)*x1)+(1-u)*(t*x2))+(u*((1-s)*x1)+u*(s*x2)) by RVSUM_1:73
      .= (((1-u)*(1-t))*x1+(1-u)*(t*x2))+(u*((1-s)*x1)+u*(s*x2)) by RVSUM_1:71
      .= (((1-u)*(1-t))*x1+((1-u)*t)*x2)+(u*((1-s)*x1)+u*(s*x2)) by RVSUM_1:71
      .= (((1-u)*(1-t))*x1+((1-u)*t)*x2)+((u*(1-s))*x1+u*(s*x2)) by RVSUM_1:71
      .= (((1-u)*(1-t))*x1+((1-u)*t)*x2)+((u*(1-s))*x1+(u*s)*x2) by RVSUM_1:71
      .= ((1-u)*(1-t))*x1+(((1-u)*t)*x2+((u*(1-s))*x1+(u*s)*x2)) by FINSEQOP:29
      .= ((1-u)*(1-t))*x1+((u*(1-s))*x1+(((1-u)*t)*x2+(u*s)*x2)) by Lm2
      .= (((1-u)*(1-t))*x1+(u*(1-s))*x1)+(((1-u)*t)*x2+(u*s)*x2) by FINSEQOP:29
      .= ((1-u)*(1-t)+u*(1-s))*x1+(((1-u)*t)*x2+(u*s)*x2) by RVSUM_1:72
      .= (1-(1*t-u*t+u*s))*x1+(1*t-u*t+u*s)*x2 by RVSUM_1:72;
    hence z in Line(x1,x2);
  end;
  hence thesis by TARSKI:def 3;
end;

theorem Th12: :: AFF_1:28
  y1 in Line(x1,x2) & y2 in Line(x1,x2) & y1<>y2 implies Line(x1,
  x2) c= Line(y1,y2)
proof
  assume y1 in Line(x1,x2);
  then consider t such that
A1: y1 = (1-t)*x1 + t*x2;
  assume y2 in Line(x1,x2);
  then consider s such that
A2: y2 = (1-s)*x1 + s*x2;
  assume y1<>y2;
  then
A3: t-s<>0 by A1,A2;
  then
  (1-((t-1)/(t-s)))*y1 + ((t-1)/(t-s))*y2 = ((1*(t-s)-(t-1))/(t-s))*y1 + (
  (t-1)/(t-s))*y2 by XCMPLX_1:128
    .= (((-s+1)/(t-s))*((1-t)*x1) + ((-s+1)/(t-s))*(t*x2)) + ((t-1)/(t-s))*(
  (1-s)*x1 + s*x2) by A1,A2,RVSUM_1:73
    .= (((-s+1)/(t-s))*((1-t)*x1) + ((-s+1)/(t-s))*(t*x2)) + (((t-1)/(t-s))*
  ((1-s)*x1) + ((t-1)/(t-s))*(s*x2)) by RVSUM_1:73
    .= ((((-s+1)/(t-s))*(1-t))*x1 + ((-s+1)/(t-s))*(t*x2)) + (((t-1)/(t-s))*
  ((1-s)*x1) + ((t-1)/(t-s))*(s*x2)) by RVSUM_1:71
    .= ((((-s+1)/(t-s))*(1-t))*x1 + (((-s+1)/(t-s))*t)*x2) + (((t-1)/(t-s))*
  ((1-s)*x1) + ((t-1)/(t-s))*(s*x2)) by RVSUM_1:71
    .= ((((-s+1)/(t-s))*(1-t))*x1 + (((-s+1)/(t-s))*t)*x2) + ((((t-1)/(t-s))
  *(1-s))*x1 + ((t-1)/(t-s))*(s*x2)) by RVSUM_1:71
    .= ((((-s+1)/(t-s))*(1-t))*x1 + (((-s+1)/(t-s))*t)*x2) + ((((t-1)/(t-s))
  *(1-s))*x1 + (((t-1)/(t-s))*s)*x2) by RVSUM_1:71
    .= ((((-s+1)*(1/(t-s)))*(1-t))*x1 + (((-s+1)/(t-s))*t)*x2) + ((((t-1)/(t
  -s))*(1-s))*x1 + (((t-1)/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s+1)*(1-t))*(1/(t-s)))*x1 + (((-s+1)/(t-s))*t)*x2) + ((((t-1)/(t
  -s))*(1-s))*x1 + (((t-1)/(t-s))*s)*x2)
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)/(t-s))*t)*x2) + ((((t-1)/(t-s))
  *(1-s))*x1 + (((t-1)/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*(1/(t-s)))*t)*x2) + ((((t-1)/(t
  -s))*(1-s))*x1 + (((t-1)/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*t)*(1/(t-s)))*x2) + ((((t-1)/(t
  -s))*(1-s))*x1 + (((t-1)/(t-s))*s)*x2)
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*t)/(t-s))*x2) + ((((t-1)/(t-s))
  *(1-s))*x1 + (((t-1)/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*t)/(t-s))*x2) + ((((t-1)*(1/(t-
  s)))*(1-s))*x1 + (((t-1)/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*t)/(t-s))*x2) + ((((t-1)*(1-s))
  *(1/(t-s)))*x1 + (((t-1)/(t-s))*s)*x2)
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*t)/(t-s))*x2) + ((((t-1)*(1-s))
  /(t-s))*x1 + (((t-1)/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*t)/(t-s))*x2) + ((((t-1)*(1-s))
  /(t-s))*x1 + (((t-1)*(1/(t-s)))*s)*x2) by XCMPLX_1:100
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*t)/(t-s))*x2) + ((((t-1)*(1-s))
  /(t-s))*x1 + (((t-1)*s)*(1/(t-s)))*x2)
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((-s+1)*t)/(t-s))*x2) + ((((t-1)*(1-s))
  /(t-s))*x1 + (((t-1)*s)/(t-s))*x2) by XCMPLX_1:100
    .= (((-s+1)*(1-t))/(t-s))*x1 + ((((-s+1)*t)/(t-s))*x2 + ((((t-1)*(1-s))/
  (t-s))*x1 + (((t-1)*s)/(t-s))*x2)) by FINSEQOP:29
    .= (((-s+1)*(1-t))/(t-s))*x1 + ((((t-1)*(1-s))/(t-s))*x1 + ((((-s+1)*t)/
  (t-s))*x2 + (((t-1)*s)/(t-s))*x2)) by Lm2
    .= ((((-s+1)*(1-t))/(t-s))*x1 + (((t-1)*(1-s))/(t-s))*x1) + ((((-s+1)*t)
  /(t-s))*x2 + (((t-1)*s)/(t-s))*x2) by FINSEQOP:29
    .= (((-s+1)*(1-t))/(t-s) + ((t-1)*(1-s))/(t-s))*x1 + ((((-s+1)*t)/(t-s))
  *x2 + (((t-1)*s)/(t-s))*x2) by RVSUM_1:72
    .= (((-s+1)*(1-t))/(t-s) + ((t-1)*(1-s))/(t-s))*x1 + (((-s+1)*t)/(t-s) +
  ((t-1)*s)/(t-s))*x2 by RVSUM_1:72
    .= (((-s+1)*(1-t)+(t-1)*(1-s))/(t-s))*x1 + (((-s+1)*t)/(t-s) + ((t-1)*s)
  /(t-s))*x2 by XCMPLX_1:63
    .= (((1-t)*(1-s)+-(1-t)*(1-s))/(t-s))*x1 + (((-s+1)*t + (t-1)*s)/(t-s))*
  x2 by XCMPLX_1:63
    .= 0*n + (((-s+1)*t + (t-1)*s)/(t-s))*x2 by Th3
    .= (1*(t-s)/(t-s))*x2 by Th1
    .= 1*x2 by A3,XCMPLX_1:90
    .= x2 by Th3;
  then
A4: x2 in Line(y1,y2);
  (1-(t/(t-s)))*y1 + (t/(t-s))*y2 = ((1*(t-s)-t)/(t-s))*y1 + (t/(t-s))*y2
  by A3,XCMPLX_1:128
    .= (((-s)/(t-s))*((1-t)*x1) + ((-s)/(t-s))*(t*x2)) + (t/(t-s))*((1-s)*x1
  + s*x2) by A1,A2,RVSUM_1:73
    .= (((-s)/(t-s))*((1-t)*x1) + ((-s)/(t-s))*(t*x2)) + ((t/(t-s))*((1-s)*
  x1) + (t/(t-s))*(s*x2)) by RVSUM_1:73
    .= ((((-s)/(t-s))*(1-t))*x1 + ((-s)/(t-s))*(t*x2)) + ((t/(t-s))*((1-s)*
  x1) + (t/(t-s))*(s*x2)) by RVSUM_1:71
    .= ((((-s)/(t-s))*(1-t))*x1 + (((-s)/(t-s))*t)*x2) + ((t/(t-s))*((1-s)*
  x1) + (t/(t-s))*(s*x2)) by RVSUM_1:71
    .= ((((-s)/(t-s))*(1-t))*x1 + (((-s)/(t-s))*t)*x2) + (((t/(t-s))*(1-s))*
  x1 + (t/(t-s))*(s*x2)) by RVSUM_1:71
    .= ((((-s)/(t-s))*(1-t))*x1 + (((-s)/(t-s))*t)*x2) + (((t/(t-s))*(1-s))*
  x1 + ((t/(t-s))*s)*x2) by RVSUM_1:71
    .= ((((-s)*(1/(t-s)))*(1-t))*x1 + (((-s)/(t-s))*t)*x2) + (((t/(t-s))*(1-
  s))*x1 + ((t/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s)*(1-t))*(1/(t-s)))*x1 + (((-s)/(t-s))*t)*x2) + (((t/(t-s))*(1-
  s))*x1 + ((t/(t-s))*s)*x2)
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)/(t-s))*t)*x2) + (((t/(t-s))*(1-s))*
  x1 + ((t/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*(1/(t-s)))*t)*x2) + (((t/(t-s))*(1-
  s))*x1 + ((t/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*t)*(1/(t-s)))*x2) + (((t/(t-s))*(1-
  s))*x1 + ((t/(t-s))*s)*x2)
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*t)/(t-s))*x2) + (((t/(t-s))*(1-s))*
  x1 + ((t/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*t)/(t-s))*x2) + (((t*(1/(t-s)))*(1-
  s))*x1 + ((t/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*t)/(t-s))*x2) + (((t*(1-s))*(1/(t-s
  )))*x1 + ((t/(t-s))*s)*x2)
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*t)/(t-s))*x2) + (((t*(1-s))/(t-s))*
  x1 + ((t/(t-s))*s)*x2) by XCMPLX_1:100
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*t)/(t-s))*x2) + (((t*(1-s))/(t-s))*
  x1 + ((t*(1/(t-s)))*s)*x2) by XCMPLX_1:100
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*t)/(t-s))*x2) + (((t*(1-s))/(t-s))*
  x1 + ((t*s)*(1/(t-s)))*x2)
    .= ((((-s)*(1-t))/(t-s))*x1 + (((-s)*t)/(t-s))*x2) + (((t*(1-s))/(t-s))*
  x1 + ((t*s)/(t-s))*x2) by XCMPLX_1:100
    .= (((-s)*(1-t))/(t-s))*x1 + ((((-s)*t)/(t-s))*x2 + (((t*(1-s))/(t-s))*
  x1 + ((t*s)/(t-s))*x2)) by FINSEQOP:29
    .= (((-s)*(1-t))/(t-s))*x1 + (((t*(1-s))/(t-s))*x1 + ((((-s)*t)/(t-s))*
  x2 + ((t*s)/(t-s))*x2)) by Lm2
    .= ((((-s)*(1-t))/(t-s))*x1 + ((t*(1-s))/(t-s))*x1) + ((((-s)*t)/(t-s))*
  x2 + ((t*s)/(t-s))*x2) by FINSEQOP:29
    .= (((-s)*(1-t))/(t-s) + (t*(1-s))/(t-s))*x1 + ((((-s)*t)/(t-s))*x2 + ((
  t*s)/(t-s))*x2) by RVSUM_1:72
    .= (((-s)*(1-t))/(t-s) + (t*(1-s))/(t-s))*x1 + (((-s)*t)/(t-s) + (t*s)/(
  t-s))*x2 by RVSUM_1:72
    .= (((-s)*(1-t)+t*(1-s))/(t-s))*x1 + (((-s)*t)/(t-s) + (t*s)/(t-s))*x2
  by XCMPLX_1:63
    .= (((-s)*(1-t)+t*(1-s))/(t-s))*x1 + (((-s)*t + t*s)/(t-s))*x2 by
XCMPLX_1:63
    .= (((-s)*(1-t)+t*(1-s))/(t-s))*x1 + 0*n by Th3
    .= ((1*(t-s))/(t-s))*x1 by Th1
    .= 1*x1 by A3,XCMPLX_1:90
    .= x1 by Th3;
  then x1 in Line(y1,y2);
  hence thesis by A4,Th11;
end;

definition
  let n;
  let A be Subset of REAL n;
  attr A is being_line means :: AFF_1:def 3
  :Def2:
  ex x1,x2 st x1<>x2 & A=Line(x1,x2);
end;

Lm3: for A be Subset of REAL n,x1,x2 holds A is being_line & x1 in A & x2 in A
& x1<>x2 implies A=Line(x1,x2)
proof
  let A be Subset of REAL n;
  let x1,x2;
  assume that
A1: A is being_line and
A2: x1 in A & x2 in A & x1<>x2;
  ex y1,y2 st y1<>y2 & A=Line(y1,y2) by A1,Def2;
  then Line(x1,x2) c= A & A c= Line(x1,x2) by A2,Th11,Th12;
  hence thesis by XBOOLE_0:def 10;
end;

theorem :: AFF_1:30
  for A,C be Subset of REAL n,x1,x2 holds A is being_line & C is
  being_line & x1 in A & x2 in A & x1 in C & x2 in C implies x1=x2 or A=C
proof
  let A,C be Subset of REAL n;
  let x1,x2;
  assume that
A1: A is being_line and
A2: C is being_line and
A3: x1 in A & x2 in A and
A4: x1 in C & x2 in C;
  assume
A5: x1<>x2;
  then A = Line(x1,x2) by A1,A3,Lm3;
  hence thesis by A2,A4,A5,Lm3;
end;

theorem Th14: ::AFF_1:31
  for A be Subset of REAL n st A is being_line holds ex x1,x2 st
  x1 in A & x2 in A & x1<>x2
proof
  let A be Subset of REAL n;
  assume A is being_line;
  then consider x1,x2 such that
A1: x1<>x2 and
A2: A = Line(x1,x2) by Def2;
  x1 in A & x2 in A by A2,Th10;
  hence thesis by A1;
end;

theorem :: AFF_1:32
  for A be Subset of REAL n st A is being_line holds ex x2 st x1<>x2 & x2 in A
proof
  let A be Subset of REAL n;
  assume A is being_line;
  then consider y1,y2 such that
A1: y1 in A and
A2: y2 in A & y1<>y2 by Th14;
  x1 = y1 implies x1<>y2 & y2 in A by A2;
  hence thesis by A1;
end;

canceled 4;

theorem :: EUCLID_2:13
  for x being Element of REAL n holds |.x.| = sqrt |(x,x)|;

theorem Th21: :: EUCLID_2:16
  for x being Element of REAL n holds |(x,x)| = 0 iff |.x.| = 0
proof
  let x be Element of REAL n;
  thus |(x,x)|=0 implies |.x.| = 0
  proof
    assume |(x,x)| = 0;
    then |.x.|^2 = 0 by EUCLID_2:12;
    hence thesis by XCMPLX_1:6;
  end;
  |.x.| = 0 implies |(x,x)| = 0^2 by EUCLID_2:12;
  hence thesis;
end;

theorem Th22: :: EUCLID_2:15
  for x being Element of REAL n holds |(x,x)|=0 iff x=0*n
proof
  let x be Element of REAL n;
  thus |(x,x)| = 0 implies x = 0*n
  proof
    assume |(x,x)| = 0;
    then |.x.| = 0 by Th21;
    hence thesis by EUCLID:11;
  end;
  thus x = 0*n implies |(x,x)| = 0
  proof
    assume x = 0*n;
    then |(x,x)| = 1/4*((|.(0*n+0*n).|)^2-(|.(0*n-0*n).|)^2) by EUCLID_2:10
      .= 1/4*((|.(0*n).|)^2-(|.(0*n-0*n).|)^2) by Th1
      .= 1/4*0 by RVSUM_1:53;
    hence thesis;
  end;
end;

theorem Th23: :: EUCLID_2:17
  for x being Element of REAL n holds |(x, 0*n)| = 0
proof
  let x be Element of REAL n;
  |(x,0*n)| = 1/4*((|.(x+0*n).|)^2-(|.(x-0*n).|)^2) by EUCLID_2:10
    .= 1/4*((|.x.|)^2-(|.(x-0*n).|)^2) by Th1
    .= 1/4*((|.x.|)^2-(|.x.|)^2) by RVSUM_1:53
    .= 1/4*0;
  hence thesis;
end;

theorem :: EUCLID_2:18
  for x being Element of REAL n holds |(0*n,x)| = 0 by Th23;

theorem Th25: :: EUCLID_2:19
  for x1,x2,x3 being Element of REAL n holds |((x1+x2),x3)| = |(x1
  ,x3)| + |(x2,x3)|
proof
  let x1,x2,x3 be Element of REAL n;
A1: len (x3) = n by FINSEQ_1:def 18;
  len x1 = n & len x2 = n by FINSEQ_1:def 18;
  hence thesis by A1,RVSUM_1:150;
end;

theorem Th26: ::EUCLID_2:20
  for x1,x2 being Element of REAL n,a being real number holds |(a*
  x1,x2)| = a*|(x1,x2)|
proof
  let x1,x2 be Element of REAL n,a be real number;
  len x1 = n & len x2 = n by FINSEQ_1:def 18;
  hence thesis by RVSUM_1:151;
end;

theorem :: EUCLID_2:21
  for x1,x2 being Element of REAL n,a being real number holds |(x1,a*x2
  )| = a*|(x1,x2)| by Th26;

theorem Th28: :: EUCLID_2:22
  for x1,x2 being Element of REAL n holds |(-x1, x2)| = - |(x1, x2 )|
proof
  let x1,x2 be Element of REAL n;
  len x1 = n & len x2 = n by FINSEQ_1:def 18;
  hence thesis by RVSUM_1:152;
end;

theorem :: EUCLID_2:23
  for x1,x2 being Element of REAL n holds |(x1, -x2)| = - |(x1, x2)| by Th28;

theorem :: EUCLID_2:24
  for x1,x2 being Element of REAL n holds |(-x1, -x2)| = |(x1, x2)|
proof
  let x1,x2 be Element of REAL n;
  thus |(-x1, -x2)| = - |(x1, -x2)| by Th28
    .= (-1)*|(x1, -x2)|
    .= (-1)*(- |(x1, x2)|) by Th28
    .= |(x1,x2)|;
end;

theorem Th31: :: EUCLID_2:25
  for x1,x2,x3 being Element of REAL n holds |(x1-x2, x3)| = |(x1,
  x3)| - |(x2, x3)|
proof
  let x1,x2,x3 be Element of REAL n;
A1: len (x3) = n by FINSEQ_1:def 18;
  len x1 = n & len x2 = n by FINSEQ_1:def 18;
  hence thesis by A1,RVSUM_1:154;
end;

theorem :: EUCLID_2:26
  for a,b being real number,x1,x2,x3 being Element of REAL n holds |( (a
  *x1+b*x2), x3 )| = a*|(x1,x3)| + b*|(x2,x3)|
proof
  let a,b be real number,x1,x2,x3 be Element of REAL n;
  thus |( (a*x1+b*x2), x3 )| = |(a*x1,x3)|+|(b*x2,x3)| by Th25
    .= a*|(x1,x3)|+|(b*x2,x3)| by Th26
    .= a*|(x1,x3)| + b*|(x2,x3)| by Th26;
end;

theorem :: EUCLID_2:27
  for x1,y1,y2 being Element of REAL n holds |(x1, y1+y2)| = |(x1, y1)|
  + |(x1, y2)| by Th25;

theorem :: EUCLID_2:28
  for x1,y1,y2 being Element of REAL n holds |(x1, y1-y2)| = |(x1, y1)|
  - |(x1, y2)| by Th31;

theorem Th35: :: EUCLID_2:29
  for x1,x2,y1,y2 being Element of REAL n holds |(x1+x2, y1+y2)| =
  |(x1, y1)| + |(x1, y2)| + |(x2, y1)| + |(x2, y2)|
proof
  let x1,x2,y1,y2 being Element of REAL n;
  thus |(x1+x2, y1+y2)| = |(x1,y1+y2)|+|(x2,y1+y2)| by Th25
    .= |(x1, y1)| + |(x1, y2)| + |(x2, y1+y2)| by Th25
    .= |(x1, y1)| + |(x1, y2)| + (|(x2, y1)| + |(x2, y2)|) by Th25
    .= |(x1, y1)| + |(x1, y2)| + |(x2, y1)| + |(x2, y2)|;
end;

theorem Th36: :: EUCLID_2:30
  for x1,x2,y1,y2 being Element of REAL n holds |(x1-x2, y1-y2)| =
  |(x1, y1)| - |(x1, y2)| - |(x2, y1)| + |(x2, y2)|
proof
  let x1,x2,y1,y2 being Element of REAL n;
  thus |(x1-x2, y1-y2)| = |(x1,y1-y2)| - |(x2,y1-y2)| by Th31
    .= |(x1,y1)| - |(x1,y2)| - |(x2,y1-y2)| by Th31
    .= |(x1,y1)| - |(x1,y2)| - (|(x2,y1)| - |(x2,y2)|) by Th31
    .= |(x1, y1)| - |(x1, y2)| - |(x2, y1)| + |(x2, y2)|;
end;

theorem Th37: :: EUCLID_2:31
  for x,y being Element of REAL n holds |(x+y, x+y)| = |(x, x)| +
  2*|(x, y)| + |(y, y)|
proof
  let x,y be Element of REAL n;
  thus |(x+y, x+y)| = |(x,x)|+|(x,y)|+|(y,x)|+|(y,y)| by Th35
    .= |(x,x)|+2*|(x,y)|+|(y,y)|;
end;

theorem Th38: :: EUCLID_2:32
  for x,y being Element of REAL n holds |(x-y, x-y)| = |(x, x)| -
  2*|(x, y)| + |(y, y)|
proof
  let x,y be Element of REAL n;
  thus |(x-y, x-y)| = |(x,x)| - |(x,y)| - |(y,x)| + |(y,y)| by Th36
    .= |(x, x)| - 2*|(x, y)| + |(y, y)|;
end;

theorem :: EUCLID_2:33
  for x,y being Element of REAL n holds |.x+y.|^2 = |.x.|^2 + 2*|(x, y)|
  + |.y.|^2
proof
  let x,y be Element of REAL n;
  thus |.x+y.|^2 = |(x+y,x+y)| by EUCLID_2:12
    .= |(x,x)|+2*|(x,y)|+|(y,y)| by Th37
    .= |.x.|^2 + 2*|(x, y)| + |(y,y)| by EUCLID_2:12
    .= |.x.|^2 + 2*|(x, y)| + |.y.|^2 by EUCLID_2:12;
end;

theorem :: EUCLID_2:34
  for x,y being Element of REAL n holds |.x-y.|^2 = |.x.|^2 - 2*|(x, y)|
  + |.y.|^2
proof
  let x,y be Element of REAL n;
  thus |.x-y.|^2 = |(x-y,x-y)| by EUCLID_2:12
    .= |(x,x)|-2*|(x,y)|+|(y,y)| by Th38
    .= |.x.|^2 - 2*|(x, y)| + |(y,y)| by EUCLID_2:12
    .= |.x.|^2 - 2*|(x, y)| + |.y.|^2 by EUCLID_2:12;
end;

theorem :: EUCLID_2:35
  for x,y being Element of REAL n holds |.x+y.|^2 + |.x-y.|^2 = 2*(|.x.|
  ^2 + |.y.|^2)
proof
  let x,y be Element of REAL n;
  len x = n & len y = n by FINSEQ_1:def 18;
  hence thesis by EUCLID_2:35;
end;

theorem :: EUCLID_2:36
  for x,y being Element of REAL n holds |.x+y.|^2 - |.x-y.|^2 = 4* |(x,y )|
proof
  let x,y be Element of REAL n;
  len x = n & len y = n by FINSEQ_1:def 18;
  hence thesis by EUCLID_2:36;
end;

theorem :: EUCLID_2:37  :: Schwartz
  for x,y being Element of REAL n holds abs |(x,y)| <= |.x.|*|.y.|
proof
  let x,y be Element of REAL n;
  len x = n & len y = n by FINSEQ_1:def 18;
  hence thesis by EUCLID_2:37;
end;

theorem :: EUCLID_2:38 :: Triangle
  for x,y being Element of REAL n holds |.x+y.| <= |.x.| + |.y.|
proof
  let x,y be Element of REAL n;
  len x = n & len y = n by FINSEQ_1:def 18;
  hence thesis by EUCLID_2:38;
end;

Lm4: -a*x = (-a)*x & -a*x = a*(-x)
proof
  reconsider p = x as Point of TOP-REAL n by EUCLID:25;
  reconsider x9= p as Element of n-tuples_on REAL;
  thus -a*x = ((-1)*a)*x9 by RVSUM_1:71
    .= (-a)*x;
  hence -a*x = (a*(-1))*x .= a*(-x) by RVSUM_1:71;
end;

Lm5: x1<>x2 implies |.x1-x2.|<>0
proof
  now
    assume that
A1: x1<>x2 and
A2: not |.x1-x2.| <> 0;
    |(x1-x2,x1-x2)| = 0^2 by A2,EUCLID_2:12;
    then x1 - x2 = 0*n by Th22;
    then x1 = x1 - (x1 + -x2) by RVSUM_1:53
      .= x1 - x1 -(-x2) by RVSUM_1:60
      .= 0*n -(-x2) by RVSUM_1:58
      .= x2 by RVSUM_1:54;
    hence contradiction by A1;
  end;
  hence thesis;
end;

Lm6: for x1, x2 being Element of REAL n holds y1 in Line(x1,x2) & y2 in Line(
x1,x2) implies ex a st y1 - y2 =a*(x1 - x2)
proof
  let x1, x2 be Element of REAL n;
  assume y1 in Line(x1,x2);
  then consider t such that
A1: y1 = (1-t)*x1 + t*x2;
  assume y2 in Line(x1,x2);
  then consider s such that
A2: y2 = (1-s)*x1 + s*x2;
  take s-t;
  y1 - y2 = (1-t)*x1 + t*x2 - (1-s)*x1 - s*x2 by A1,A2,RVSUM_1:60
    .= (1-t)*x1 + -(1-s)*x1 + t*x2 +- s*x2 by Lm2
    .= (1-t)*x1 + -(1-s)*x1 + (t*x2 +- s*x2) by FINSEQOP:29
    .= (1-t)*x1 + (-(1-s))*x1 + (t*x2 +- s*x2) by Lm4
    .= (1-t)*x1 + (-(1-s))*x1 + (t*x2 +(- s)*x2) by Lm4
    .= ((1-t)+(-(1-s)))*x1 + (t*x2 +(- s)*x2) by RVSUM_1:72
    .= ((1-t)-(1-s))*x1 + (t + (-s))*x2 by RVSUM_1:72
    .= (s-t)*x1 + (-(s +- t))*x2
    .= (s-t)*x1 + -(s - t)*x2 by Lm4
    .= (s-t)*x1 + (s - t)*(-x2) by Lm4
    .= (s-t)*(x1 - x2) by RVSUM_1:73;
  hence thesis;
end;

Lm7: for x1,x2,y1 being Element of REAL n ex y2 being Element of REAL n st y2
in Line(x1,x2) & x1-x2,y1-y2 are_orthogonal & for x being Element of REAL n st
x in Line(x1,x2) holds |.y1 - y2.| <= |.y1 - x.|
proof
  let x1,x2,y1 being Element of REAL n;
  now
    per cases;
    case
A1:   x1<>x2;
      set mu = - |(x1-x2,y1-x1)|/|.x1-x2.|^2;
      set y2 = (1-mu)*x1 + mu*x2;
      |.x1-x2.|<>0 by A1,Lm5;
      then
A2:   |.x1-x2.|^2 <> 0 by SQUARE_1:74;
      |(x1-x2,y1-y2)| = |(x1-x2,y1 - (1 + -mu)*x1 - mu*x2)| by RVSUM_1:60
        .= |(x1-x2,y1 - (1*x1 + (-mu)*x1) - mu*x2)| by RVSUM_1:72
        .= |(x1-x2,y1-1*x1-(-mu)*x1-mu*x2)| by RVSUM_1:60
        .= |(x1-x2,(y1-x1)-(-mu)*x1-mu*x2)| by Th3
        .= |(x1-x2,(y1-x1)-((-mu)*x1+mu*x2))| by RVSUM_1:60
        .= |(x1-x2,(y1-x1)-((-mu)*x1+ (-(-mu)*x2)))| by Lm4
        .= |(x1-x2,(y1-x1)-((-mu)*x1+ (-mu)*(-x2)))| by Lm4
        .= |(x1-x2,(y1-x1)-(-mu)*(x1 -x2))| by RVSUM_1:73
        .= |(x1-x2,y1-x1)| - |(x1-x2,(-mu)*(x1-x2))| by Th31
        .= |(x1-x2,y1-x1)| - (-mu)*|(x1-x2,x1-x2)| by Th26
        .= |(x1-x2,y1-x1)| + mu*|(x1-x2,x1-x2)|
        .= |(x1-x2,y1-x1)| + mu*|.x1-x2.|^2 by EUCLID_2:12
        .= |(x1-x2,y1-x1)| + (- |(x1-x2,y1-x1)|)/|.x1-x2.|^2*|.x1-x2.|^2 by
XCMPLX_1:188
        .= |(x1-x2,y1-x1)| + (- |(x1-x2,y1-x1)|) by A2,XCMPLX_1:88
        .= 0;
      hence y2 in Line(x1,x2) & x1-x2,y1-y2 are_orthogonal by RVSUM_1:def 18;
    end;
    case
A3:   x1=x2;
      let mu be Real;
      set y2 = (1-mu)*x1 + mu*x2;
      take y2;
      x1 - x2 = 0*n by A3,RVSUM_1:58;
      then |(x1-x2,y1-y2)| = 0 by Th23;
      hence y2 in Line(x1,x2) & x1-x2,y1-y2 are_orthogonal by RVSUM_1:def 18;
    end;
  end;
  then consider y2 such that
A4: y2 in Line(x1,x2) and
A5: x1-x2,y1-y2 are_orthogonal;
A6: |(x1 - x2, y1 - y2)| = 0 by A5,RVSUM_1:def 18;
  for x being Element of REAL n st x in Line(x1,x2) holds |.y1 - y2.| <=
  |.y1 - x.|
  proof
    let x be Element of REAL n;
    assume x in Line(x1,x2);
    then consider a such that
A7: y2 - x =a*(x1 - x2) by A4,Lm6;
    (y2 - x) + (y1 - y2) = (-x + y2) +(- y2) + y1 by Lm2
      .= (-x + (y2 - y2)) + y1 by FINSEQOP:29
      .= (-x + 0*n) + y1 by RVSUM_1:58
      .= y1 - x by Th1;
    then |.y1-x.|^2 = |(a*(x1 - x2) + (y1 - y2), a*(x1 - x2) + (y1 - y2))| by
A7,EUCLID_2:12
      .= |(a*(x1 - x2), a*(x1 - x2))| + 2*|(a*(x1 - x2), y1 - y2)| + |(y1 -
    y2, y1 - y2)| by Th37
      .= a*|(x1 - x2, a*(x1 - x2))| + 2*|(a*(x1 - x2), y1 - y2)| + |(y1 - y2
    , y1 - y2)| by Th26
      .= a*(a*|(x1 - x2, x1 - x2)|) + 2*|(a*(x1 - x2), y1 - y2)| + |(y1 - y2
    , y1 - y2)| by Th26
      .= a^2*|(x1 - x2, x1 - x2)| + 2*|(a*(x1 - x2), y1 - y2)| + |(y1 - y2,
    y1 - y2)|
      .= a^2*|.x1 - x2.|^2 + 2*|(a*(x1 - x2), y1 - y2)| + |(y1 - y2, y1 - y2
    )| by EUCLID_2:12
      .= a^2*|.x1 - x2.|^2 + 2*(a*|(x1 - x2, y1 - y2)|) + |(y1 - y2, y1 - y2
    )| by Th26
      .= (a*|.x1 - x2.|)^2 + |.y1 - y2.|^2 by A6,EUCLID_2:12;
    then 0 <= |.y1-x.|^2 - |.y1 - y2.|^2 by XREAL_1:65;
    then
A8: |.y1 - y2.|^2 <= |.y1 - x.|^2 by XREAL_1:51;
    0 <= |.y1 - y2.|^2 by XREAL_1:65;
    then sqrt |.y1 - y2.|^2 <= sqrt |.y1 - x.|^2 by A8,SQUARE_1:94;
    then |.y1 - y2.| <= sqrt |.y1 - x.|^2 by EUCLID_2:14,SQUARE_1:89;
    hence thesis by EUCLID_2:14,SQUARE_1:89;
  end;
  hence thesis by A4,A5;
end;

theorem
  for R being Subset of REAL,x1,x2,y1 being Element of REAL n st R={|.y1
-x.| where x is Element of REAL n: x in Line(x1,x2)} holds ex y2 being Element
  of REAL n st y2 in Line(x1,x2) & |.y1-y2.|=lower_bound R & x1-x2,y1-y2
  are_orthogonal
proof
  let R being Subset of REAL,x1,x2,y1 being Element of REAL n;
  consider y2 being Element of REAL n such that
A1: y2 in Line(x1,x2) and
A2: x1-x2,y1-y2 are_orthogonal and
A3: for x being Element of REAL n st x in Line(x1,x2) holds |.y1 - y2.|
  <= |.y1 - x.| by Lm7;
  assume
A4: R={|.y1-x.| where x is Element of REAL n: x in Line(x1,x2)};
A5: for s being real number st 0<s holds ex r being real number st r in R &
  r < |.y1-y2.|+s
  proof
    let s be real number;
    assume
A6: 0<s;
    take |.y1- y2.|;
    thus thesis by A4,A1,A6,XREAL_1:31;
  end;
  x1 in Line(x1,x2) by Th10;
  then
A7: |.y1-x1.| in R by A4;
A8: for r being real number st r in R holds |.y1-y2.| <=r
  proof
    let r be real number;
    assume r in R;
    then ex x0 being Element of REAL n st r= |.y1-x0.| & x0 in Line(x1,x2) by
A4;
    hence thesis by A3;
  end;
  R is bounded_below proof take |.y1-y2.|;
    let r be ext-real number;
    assume r in R;
    then ex x0 being Element of REAL n st r= |.y1-x0.| & x0 in Line(x1,x2) by
A4;
    hence thesis by A3;
  end;
  then |.y1-y2.| = lower_bound R by A7,A5,A8,SEQ_4:def 5;
  hence thesis by A1,A2;
end;

definition
  canceled 4;
  let n;
  let p1,p2 be Point of TOP-REAL n;
  func Line(p1,p2) -> Subset of TOP-REAL n equals
  { (1-lambda)*p1 + lambda*p2
  : not contradiction };
  coherence
  proof
    set A = { (1-lambda)*p1 + lambda*p2 : not contradiction };
    A c= the carrier of TOP-REAL n
    proof
      let x be set;
      assume x in A;
      then ex lambda st x = (1-lambda)*p1 + lambda*p2;
      hence x in the carrier of TOP-REAL n;
    end;
    hence thesis;
  end;
end;

registration
  let n;
  let p1,p2 be Point of TOP-REAL n;
  cluster Line(p1,p2) -> non empty;
  coherence
  proof
A1: 0 * p2 = 0.TOP-REAL n & p1 + 0.TOP-REAL n = p1 by EUCLID:31,33;
    1 - 0 = 1 & 1 * p1 = p1 by EUCLID:33;
    then p1 in Line(p1,p2) by A1;
    hence thesis;
  end;
end;

reserve p1,p2,q1,q2 for Point of TOP-REAL n;

Lm8: for p1,p2 being Point of TOP-REAL n ex x1,x2 being Element of REAL n st
p1=x1 & p2=x2 & Line(x1,x2)=Line(p1,p2)
proof
  let p1,p2 be Point of TOP-REAL n;
  reconsider x1=p1,x2=p2 as Element of REAL n by EUCLID:25;
  take x1,x2;
  thus p1=x1 & p2=x2;
  thus Line(x1,x2) c= Line(p1,p2)
  proof
    let e be set;
    assume e in Line(x1,x2);
    then consider lambda such that
A1: e = (1-lambda)*x1 + lambda*x2;
    (1-lambda)*p1 = (1-lambda)*x1 & lambda*p2 = lambda*x2 by EUCLID:69;
    then e = (1-lambda)*p1 + lambda*p2 by A1,EUCLID:68;
    hence e in Line(p1,p2);
  end;
  let e be set;
  assume e in Line(p1,p2);
  then consider lambda such that
A2: e = (1-lambda)*p1 + lambda*p2;
  (1-lambda)*p1 = (1-lambda)*x1 & lambda*p2 = lambda*x2 by EUCLID:69;
  then e = (1-lambda)*x1 + lambda*x2 by A2,EUCLID:68;
  hence e in Line(x1,x2);
end;

definition
  let n;
  let p1,p2 be Point of TOP-REAL n;
  redefine func Line(p1,p2);
  commutativity
proof let p1,p2;
  (ex x1, x2 be Element of REAL n st x1 = p1 & x2 = p2 & Line(x1,x2)=Line(
p1, p2) )& ex x29, x19 be Element of REAL n st x29 = p2 & x19 = p1 & Line(x29,
  x19)=Line(p2,p1) by Lm8;
  hence thesis;
end;
end;

canceled;

theorem Th47: :: AFF_1:26
  p1 in Line(p1,p2) & p2 in Line(p1,p2)
proof
  ex x1,x2 be Element of REAL n st x1 = p1 & x2 = p2 & Line(x1,x2)=Line(p1,
  p2) by Lm8;
  hence thesis by Th10;
end;

theorem Th48: :: AFF_1:27
  q1 in Line(p1,p2) & q2 in Line(p1,p2) implies Line(q1,q2) c= Line(p1,p2)
proof
  assume
A1: q1 in Line(p1,p2) & q2 in Line(p1,p2);
  (ex x1,x2 be Element of REAL n st x1 = p1 & x2 = p2 & Line(x1,x2)=Line(
  p1, p2) )& ex y1,y2 be Element of REAL n st y1 = q1 & y2 = q2 & Line(y1,y2)=
  Line(q1,q2) by Lm8;
  hence thesis by A1,Th11;
end;

theorem Th49: :: AFF_1:28
  q1 in Line(p1,p2) & q2 in Line(p1,p2) & q1<>q2 implies Line(p1,
  p2) c= Line(q1,q2)
proof
  assume
A1: q1 in Line(p1,p2) & q2 in Line(p1,p2) & q1<>q2;
  (ex x1,x2 be Element of REAL n st x1 = p1 & x2 = p2 & Line(x1,x2)=Line(
  p1, p2) )& ex y1,y2 be Element of REAL n st y1 = q1 & y2 = q2 & Line(y1,y2)=
  Line(q1,q2) by Lm8;
  hence thesis by A1,Th12;
end;

definition
  let n;
  let A be Subset of TOP-REAL n;
  attr A is being_line means :: AFF_1:def 3
  :Def8:
  ex p1,p2 st p1<>p2 & A=Line(p1,p2);
end;

Lm9: for A be Subset of TOP-REAL n,p1,p2 holds A is being_line & p1 in A & p2
in A & p1<>p2 implies A=Line(p1,p2)
proof
  let A be Subset of TOP-REAL n;
  let p1,p2;
  assume that
A1: A is being_line and
A2: p1 in A & p2 in A & p1<>p2;
  ex q1,q2 st q1<>q2 & A=Line(q1,q2) by A1,Def8;
  then Line(p1,p2) c= A & A c= Line(p1,p2) by A2,Th48,Th49;
  hence thesis by XBOOLE_0:def 10;
end;

theorem :: AFF_1:30
  for A,C being Subset of TOP-REAL n holds A is being_line & C is
  being_line & p1 in A & p2 in A & p1 in C & p2 in C implies p1=p2 or A=C
proof
  let A,C be Subset of TOP-REAL n;
  assume that
A1: A is being_line and
A2: C is being_line and
A3: p1 in A & p2 in A and
A4: p1 in C & p2 in C;
  assume
A5: p1<>p2;
  then A = Line(p1,p2) by A1,A3,Lm9;
  hence thesis by A2,A4,A5,Lm9;
end;

theorem Th51: :: AFF_1:31
  for A being Subset of TOP-REAL n st A is being_line holds ex p1,
  p2 st p1 in A & p2 in A & p1<>p2
proof
  let A be Subset of TOP-REAL n;
  assume A is being_line;
  then consider p1,p2 such that
A1: p1<>p2 and
A2: A = Line(p1,p2) by Def8;
  p1 in A & p2 in A by A2,Th47;
  hence thesis by A1;
end;

theorem :: AFF_1:32
  for A being Subset of TOP-REAL n st A is being_line holds ex p2 st p1
  <>p2 & p2 in A
proof
  let A be Subset of TOP-REAL n;
  assume A is being_line;
  then consider q1,q2 such that
A1: q1 in A and
A2: q2 in A & q1<>q2 by Th51;
  p1 = q1 implies p1<>q2 & q2 in A by A2;
  hence thesis by A1;
end;

definition
  let n;
  let p be Point of TOP-REAL n;
  func TPn2Rn (p) -> Element of REAL n equals
  p;
  correctness by EUCLID:25;
end;

theorem
  for R being Subset of REAL,p1,p2,q1 being Point of TOP-REAL n st R={|.
  q1-p .| where p is Point of TOP-REAL n: p in Line(p1,p2)} holds ex q2 being
Point of TOP-REAL n st q2 in Line(p1,p2) & |. q1-q2 .| =lower_bound R & p1-p2,
  q1-q2 are_orthogonal
proof
  let R being Subset of REAL,p1,p2,q1 being Point of TOP-REAL n;
  reconsider y1 = q1 as Element of REAL n by EUCLID:25;
  consider x1,x2 being Element of REAL n such that
A1: p1=x1 & p2=x2 and
A2: Line(x1,x2)=Line(p1,p2) by Lm8;
A3: x1-x2 = p1-p2 by A1,EUCLID:73;
  consider y2 being Element of REAL n such that
A4: y2 in Line(x1,x2) and
A5: x1-x2,y1-y2 are_orthogonal and
A6: for x being Element of REAL n st x in Line(x1,x2) holds |.y1 - y2.|
  <= |.y1 - x.| by Lm7;
  reconsider q2 = y2 as Point of TOP-REAL n by EUCLID:25;
  assume
A7: R={|. q1-p .| where p is Point of TOP-REAL n: p in Line(p1,p2)};
A8: for s being real number st 0<s holds ex r being real number st r in R &
  r < |.q1-q2.|+s
  proof
    let s be real number;
    assume
A9: 0<s;
    take |.q1- q2.|;
    thus thesis by A7,A2,A4,A9,XREAL_1:31;
  end;
  p1 in Line(p1,p2) by Th47;
  then
A10: |.q1-p1.| in R by A7;
A11: for r being real number st r in R holds |.q1-q2.| <=r
  proof
    let r be real number;
    assume r in R;
    then consider p0 being Point of TOP-REAL n such that
A12: r= |.q1-p0.| & p0 in Line(p1,p2) by A7;
    the carrier of Euclid n = the carrier of TOP-REAL n by EUCLID:71;
    then reconsider x = p0 as Element of REAL n;
    y1 - y2 = q1-q2 & y1 - x = q1-p0 by EUCLID:73;
    hence |.q1-q2.| <=r by A2,A6,A12;
  end;
   R is bounded_below
  proof
   take |.q1-q2.|;
    let r be ext-real number;
    assume r in R;
    then consider p0 being Point of TOP-REAL n such that
A12: r= |.q1-p0.| & p0 in Line(p1,p2) by A7;
    the carrier of Euclid n = the carrier of TOP-REAL n by EUCLID:71;
    then reconsider x = p0 as Element of REAL n;
    y1 - y2 = q1-q2 & y1 - x = q1-p0 by EUCLID:73;
    hence |.q1-q2.| <=r by A2,A6,A12;
  end;
  then y1-y2 = q1-q2 & |.q1-q2.| = lower_bound R by A10,A8,A11,EUCLID:73
,SEQ_4:def 5;
  hence thesis by A2,A4,A5,A3;
end;
