:: Properties of Connected Subsets of the Real Line
::  by Artur Korni{\l}owicz
::
:: Received February 22, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, RELAT_1, ARYTM, SUBSET_1, WELLORD1, FINSET_1, METRIC_1,
      FUNCT_1, ORDINAL2, ARYTM_1, SQUARE_1, ARYTM_3, RCOMP_1, TOPMETR, JORDAN1,
      BOOLE, SETFAM_1, TARSKI, COMPTS_1, FINSEQ_1, RELAT_2, RCOMP_3, FINSEQ_4,
      ORDERS_2, CARD_1, MEMBERED, SEQ_2, LATTICES, TOPS_1, LIMFUNC1, REALSET1,
      WELLORD2, SEQ_4;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, RELAT_1, SETFAM_1, FUNCT_1,
      RELSET_1, PARTFUN1, FUNCT_2, FINSET_1, WELLORD1, ORDERS_1, WELLORD2,
      RELAT_2, CARD_1, MEMBERED, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, REAL_1,
      NAT_1, NAT_D, LIMFUNC1, XXREAL_2,
      SEQM_3, FINSEQ_1, SEQ_4, RCOMP_1, RCOMP_2, PRE_CIRC,
      STRUCT_0, PRE_TOPC, TOPS_1, METRIC_1, TOPS_2, COMPTS_1, CONNSP_1, TEX_2,
      TOPMETR, PCOMPS_1, PSCOMP_1, BORSUK_4, TOPALG_2, TOPREALB, BINARITH,
      JCT_MISC;
 constructors SETFAM_1, WELLORD1, WELLORD2, NAT_1, ORDERS_1, SEQM_3, PROB_1,
      RCOMP_2, LIMFUNC1, BINARITH, PRE_CIRC, TOPS_1, CONNSP_1, COMPTS_1,
      REALSET2, PSCOMP_1, JCT_MISC, BORSUK_4, TOPREALB, SEQ_1, XXREAL_2,
      BORSUK_6, NAT_D, SEQ_4;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      INT_1, MEMBERED, FINSEQ_1, RCOMP_1, FCONT_3, REALSET1, RFINSEQ, STRUCT_0,
      PRE_TOPC, TOPS_1, METRIC_1, TOPMETR, PSCOMP_1, BORSUK_2, SPRECT_1,
      TOPREAL6, BORSUK_4, BORSUK_5, TOPALG_2, TOPREALB, FINSET_1, VALUED_0,
      XXREAL_2, CARD_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions FUNCT_1, TOPS_2, BORSUK_4, COMPTS_1, PRE_TOPC, RELAT_2, ORDERS_1,
      SEQ_4, TARSKI, XBOOLE_0, JCT_MISC, SEQM_3, CONNSP_1, TOPALG_2, SUBSET_1,
      WELLORD1, STRUCT_0, LIMFUNC1, PROB_1, SETFAM_1;
 theorems PRE_TOPC, XREAL_0, FUNCT_2, INT_1, TOPS_2, FUNCT_1, TOPMETR,
      RELAT_1, TOPALG_2, TARSKI, ZFMISC_1, JORDAN5A, FINSEQ_1,
      XBOOLE_1, COMPTS_1, SETFAM_1, XBOOLE_0, FINSEQ_3, NAT_1, RCOMP_1,
      SEQM_3, FINSEQ_5, HEINE, TOPS_1, CONNSP_1, TOPREALA, TOPREALB, ORDINAL1,
      FINSET_1, ORDERS_1, RELAT_2, CARD_2, MEMBERED, SEQ_4,
      BINARITH, FINSEQ_4, PSCOMP_1, TOPREAL6, JCT_MISC,
      TREAL_1, REALSET1, BORSUK_5, TOPS_3, TEX_1, XREAL_1, METRIC_1, WELLORD2,
      XXREAL_0, XXREAL_1, STRUCT_0, PARTFUN1, CARD_1, XXREAL_2;
 schemes NAT_1, RELSET_1, FINSET_1, FRAENKEL, FINSEQ_1, RECDEF_1;

begin :: Preliminaries

registration
  let X be non empty set;
  cluster [#]X -> non empty;
  coherence;
end;

registration
  cluster -> real-membered SubSpace of RealSpace;
  coherence
  proof
    let T be SubSpace of RealSpace;
    the carrier of T is Subset of RealSpace by TOPMETR:def 1;
    hence the carrier of T is real-membered by METRIC_1:def 14;
  end;
end;

theorem Th1:
  for X being non empty bounded_below (real-membered set),
  Y being closed Subset of REAL st X c= Y holds inf X in Y
proof
  let X be non empty bounded_below (real-membered set);
  let Y be closed Subset of REAL;
  assume that
A1: X c= Y;
  reconsider X as non empty bounded_below Subset of REAL by MEMBERED:3;
A2: Cl X c= Y by A1,PSCOMP_1:32;
A3: lower_bound X = lower_bound Cl X by TOPREAL6:77;
  lower_bound Cl X in Cl X by RCOMP_1:31;
  hence thesis by A2,A3;
end;

theorem Th2:
  for X being non empty bounded_above (real-membered set),
  Y being closed Subset of REAL st X c= Y holds upper_bound X in Y
proof
  let X be non empty bounded_above (real-membered set);
  let Y be closed Subset of REAL;
  assume that
A1: X c= Y;
  reconsider X as non empty bounded_above Subset of REAL by MEMBERED:3;
A2: Cl X c= Y by A1,PSCOMP_1:32;
A3: upper_bound X = upper_bound Cl X by TOPREAL6:78;
  upper_bound Cl X in Cl X by RCOMP_1:30;
  hence thesis by A2,A3;
end;

theorem Th3:
  for X, Y being Subset of REAL holds Cl(X \/ Y) = Cl X \/ Cl Y
proof
  let X, Y be Subset of REAL;
  reconsider A = X, B = Y as Subset of R^1 by TOPMETR:24;
  thus Cl(X \/ Y) = Cl(A \/ B) by TOPREAL6:80
    .= Cl A \/ Cl B by PRE_TOPC:50
    .= Cl X \/ Cl B by TOPREAL6:80
    .= Cl X \/ Cl Y by TOPREAL6:80;
end;

begin :: Intervals

reserve a, b, r, s for real number;

registration
  let r, s;
  cluster [.r,s.[ -> bounded Subset of REAL;
  coherence
  proof
A1: [.r,s.[ is bounded_below
    proof
      take r;
      thus thesis by XXREAL_1:3;
    end;
    [.r,s.[ is bounded_above
    proof
      take s;
      thus thesis by XXREAL_1:3;
    end;
    hence [.r,s.[ is bounded Subset of REAL by A1;
  end;
  cluster ].r,s.] -> bounded Subset of REAL;
  coherence
  proof
A2: ].r,s.] is bounded_below
    proof
      take r;
      thus thesis by XXREAL_1:2;
    end;
    ].r,s.] is bounded_above
    proof
      take s;
      thus thesis by XXREAL_1:2;
    end;
    hence ].r,s.] is bounded Subset of REAL by A2;
  end;
  cluster ].r,s.[ -> bounded Subset of REAL;
  coherence
  proof
A3: ].r,s.[ is bounded_below
    proof
      take r;
      thus thesis by XXREAL_1:4;
    end;
    ].r,s.[ is bounded_above
    proof
      take s;
      thus thesis by XXREAL_1:4;
    end;
    hence ].r,s.[ is bounded Subset of REAL by A3;
  end;
end;

registration
  cluster open bounded connected non empty Subset of REAL;
  existence
  proof
    take ].0,1.[;
    thus thesis;
  end;
end;

theorem Th4:
  r < s implies lower_bound [.r,s.[ = r
proof
  set X = [.r,s.[;
  assume
A1: r < s;
  then
A2: X is non empty by XXREAL_1:3;
A3: for a st a in X holds r <= a by XXREAL_1:3;
  reconsider r, s as Real by XREAL_0:def 1;
A4: r < (r+s)/2 & (r+s)/2 < s by A1,XREAL_1:228;
  for b st 0 < b ex a st a in X & a < r+b
  proof
    let b such that
A5: 0 < b and
A6: for a st a in X holds a >= r+b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose r + b > s;
      then (r+s)/2 in X & (r+s)/2 < r+b by A4,XXREAL_0:2,XXREAL_1:3;
      hence thesis by A6;
    end;
    suppose
A7:   r + b <= s;
A8:   r < r + b by A5,XREAL_1:31;
      then
A9:   r < (r+(r+b))/2 by XREAL_1:228;
A10:  (r+(r+b))/2 < r+b by A8,XREAL_1:228;
      then (r+(r+b))/2 < s by A7,XXREAL_0:2;
      then (r+(r+b))/2 in X by A9,XXREAL_1:3;
      hence thesis by A6,A10;
    end;
  end;
  hence thesis by A2,A3,SEQ_4:def 5;
end;

theorem Th5:
  r < s implies upper_bound [.r,s.[ = s
proof
  set X = [.r,s.[;
  assume
A1: r < s;
  then
A2: X is non empty by XXREAL_1:3;
A3: for a st a in X holds a <= s by XXREAL_1:3;
  reconsider r, s as Real by XREAL_0:def 1;
A4: r < (r+s)/2 & (r+s)/2 < s by A1,XREAL_1:228;
  for b st 0 < b ex a st a in X & s-b < a
  proof
    let b such that
A5: 0 < b and
A6: for a st a in X holds a <= s-b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose s - b <= r;
      then (r+s)/2 in X & (r+s)/2 > s-b by A4,XXREAL_0:2,XXREAL_1:3;
      hence thesis by A6;
    end;
    suppose
A7:   s - b > r;
A8:   s - b < s - 0 by A5,XREAL_1:17;
      then s-b < (s+(s-b))/2 by XREAL_1:228;
      then
A9:   r < (s+(s-b))/2 by A7,XXREAL_0:2;
A10:  (s+(s-b))/2 > s-b by A8,XREAL_1:228;
      (s+(s-b))/2 < s by A8,XREAL_1:228;
      then (s+(s-b))/2 in X by A9,XXREAL_1:3;
      hence thesis by A6,A10;
    end;
  end;
  hence thesis by A2,A3,SEQ_4:def 4;
end;

theorem Th6:
  r < s implies lower_bound ].r,s.] = r
proof
  set X = ].r,s.];
  assume
A1: r < s;
  then
A2: X is non empty by XXREAL_1:2;
A3: for a st a in X holds r <= a by XXREAL_1:2;
  reconsider r, s as Real by XREAL_0:def 1;
A4: r < (r+s)/2 & (r+s)/2 < s by A1,XREAL_1:228;
  for b st 0 < b ex a st a in X & a < r+b
  proof
    let b such that
A5: 0 < b and
A6: for a st a in X holds a >= r+b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose r + b > s;
      then (r+s)/2 in X & (r+s)/2 < r+b by A4,XXREAL_0:2,XXREAL_1:2;
      hence thesis by A6;
    end;
    suppose
A7:   r + b <= s;
A8:   r < r + b by A5,XREAL_1:31;
      then
A9:   r < (r+(r+b))/2 by XREAL_1:228;
A10:  (r+(r+b))/2 < r+b by A8,XREAL_1:228;
      then (r+(r+b))/2 < s by A7,XXREAL_0:2;
      then (r+(r+b))/2 in X by A9,XXREAL_1:2;
      hence thesis by A6,A10;
    end;
  end;
  hence thesis by A2,A3,SEQ_4:def 5;
end;

theorem Th7:
  r < s implies upper_bound ].r,s.] = s
proof
  set X = ].r,s.];
  assume
A1: r < s;
  then
A2: X is non empty by XXREAL_1:2;
A3: for a st a in X holds a <= s by XXREAL_1:2;
  reconsider r, s as Real by XREAL_0:def 1;
A4: r < (r+s)/2 & (r+s)/2 < s by A1,XREAL_1:228;
  for b st 0 < b ex a st a in X & s-b < a
  proof
    let b such that
A5: 0 < b and
A6: for a st a in X holds a <= s-b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose s - b <= r;
      then (r+s)/2 in X & (r+s)/2 > s-b by A4,XXREAL_0:2,XXREAL_1:2;
      hence thesis by A6;
    end;
    suppose
A7:   s - b > r;
A8:   s - b < s - 0 by A5,XREAL_1:17;
      then s-b < (s+(s-b))/2 by XREAL_1:228;
      then
A9:   r < (s+(s-b))/2 by A7,XXREAL_0:2;
A10:  (s+(s-b))/2 > s-b by A8,XREAL_1:228;
      (s+(s-b))/2 < s by A8,XREAL_1:228;
      then (s+(s-b))/2 in X by A9,XXREAL_1:2;
      hence thesis by A6,A10;
    end;
  end;
  hence thesis by A2,A3,SEQ_4:def 4;
end;

begin :: Halflines

canceled 12;

theorem Th20:
  a <= b implies
  [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
proof
  set A = left_closed_halfline(a);
  set B = right_closed_halfline(b);
  assume
A1: a <= b;
  thus [.a,b.] /\ (A \/ B) c= {a,b}
  proof
    let x be set;
    assume
A2: x in [.a,b.] /\ (A \/ B);
    then reconsider x as Real;
    x in A \/ B by A2,XBOOLE_0:def 4;
    then x in A or x in B by XBOOLE_0:def 3;
    then
A3: x <= a or x >= b by XXREAL_1:234,236;
    x in [.a,b.] by A2,XBOOLE_0:def 4;
    then a <= x & x <= b by XXREAL_1:1;
    then x = a or x = b by A3,XXREAL_0:1;
    hence thesis by TARSKI:def 2;
  end;
  let x be set;
  assume x in {a,b};
  then
A4: x = a or x = b by TARSKI:def 2;
A5: a in [.a,b.] & b in [.a,b.] by A1,XXREAL_1:1;
  a in A & b in B by XXREAL_1:234,236;
  then a in A \/ B & b in A \/ B by XBOOLE_0:def 3;
  hence thesis by A4,A5,XBOOLE_0:def 4;
end;

Lm2: now
  let a;
  assume left_open_halfline(a) is bounded_below;
  then consider b such that
A1: for r st r in left_open_halfline(a) holds b <= r by SEQ_4:def 2;
A2: a-1 < a-0 by XREAL_1:17;
  then a-1 in left_open_halfline(a) by XXREAL_1:233;
  then b-1 < a-1-0 by A1,XREAL_1:17;
  then b-1 < a by A2,XXREAL_0:2;
  then b-1 in left_open_halfline(a) by XXREAL_1:233;
  then b-0 <= b-1 by A1;
  hence contradiction by XREAL_1:17;
end;

Lm3: now
  let a;
  assume right_open_halfline(a) is bounded_above;
  then consider b such that
A1: for r st r in right_open_halfline(a) holds r <= b by SEQ_4:def 1;
A2: a+0 < a+1 by XREAL_1:8;
  then a+1 in right_open_halfline(a) by XXREAL_1:235;
  then a+1 <= b by A1;
  then a+1+0 <= b+1 by XREAL_1:9;
  then a < b+1 by A2,XXREAL_0:2;
  then b+1 in right_open_halfline(a) by XXREAL_1:235;
  then b+1 <= b+0 by A1;
  hence contradiction by XREAL_1:8;
end;

registration
  let a;
  cluster left_closed_halfline(a) -> non bounded_below bounded_above connected;
  coherence
  proof
    set A = left_closed_halfline(a);
X:  left_open_halfline(a) c= A by XXREAL_1:21;
    left_open_halfline a is not bounded_below by Lm2;
    hence A is non bounded_below by X,XXREAL_2:44;
    thus A is bounded_above
    proof
      take a;
      thus thesis by XXREAL_1:234;
    end;
    let r, s;
    assume r in A;
    assume s in A;
    then
A1: s <= a by XXREAL_1:234;
    let x be set;
    assume
A2: x in [.r,s.];
    then reconsider x as Real;
    x <= s by A2,XXREAL_1:1;
    then x <= a by A1,XXREAL_0:2;
    hence thesis by XXREAL_1:234;
  end;
  cluster left_open_halfline(a) -> non bounded_below bounded_above connected;
  coherence
  proof
    set A = left_open_halfline(a);
    thus A is non bounded_below by Lm2;
    thus A is bounded_above
    proof
      take a;
      thus thesis by XXREAL_1:233;
    end;
    let r, s;
    assume r in A;
    assume s in A;
    then
A3: s < a by XXREAL_1:233;
    let x be set;
    assume
A4: x in [.r,s.];
    then reconsider x as Real;
    x <= s by A4,XXREAL_1:1;
    then x < a by A3,XXREAL_0:2;
    hence thesis by XXREAL_1:233;
  end;
  cluster right_closed_halfline(a) ->
    bounded_below non bounded_above connected;
  coherence
  proof
    set A = right_closed_halfline(a);
    thus A is bounded_below
    proof
      take a;
      thus thesis by XXREAL_1:236;
    end;
X:  right_open_halfline(a) c= A by XXREAL_1:22;
    right_open_halfline a is not bounded_above by Lm3;
    hence A is non bounded_above by X,XXREAL_2:43;
    let r, s;
    assume r in A;
    then
A5: a <= r by XXREAL_1:236;
    assume s in A;
    let x be set;
    assume
A6: x in [.r,s.];
    then reconsider x as Real;
    r <= x by A6,XXREAL_1:1;
    then a <= x by A5,XXREAL_0:2;
    hence thesis by XXREAL_1:236;
  end;
  cluster right_open_halfline(a) -> bounded_below non bounded_above connected;
  coherence
  proof
    set A = right_open_halfline(a);
    thus A is bounded_below
    proof
      take a;
      thus thesis by XXREAL_1:235;
    end;
    thus A is non bounded_above by Lm3;
    let r, s;
    assume r in A;
    then
A7: a < r by XXREAL_1:235;
    assume s in A;
    let x be set;
    assume
A8: x in [.r,s.];
    then reconsider x as Real;
    r <= x by A8,XXREAL_1:1;
    then a < x by A7,XXREAL_0:2;
    hence thesis by XXREAL_1:235;
  end;
end;

theorem Th21:
  upper_bound left_closed_halfline(a) = a
proof
  set X = left_closed_halfline(a);
A1: for r st r in X holds r <= a by XXREAL_1:234;
  for s st 0 < s ex r st r in X & a-s < r
  proof
    let s such that
A2: 0 < s;
    take a;
    thus a in X by XXREAL_1:234;
    a-s < a-0 by A2,XREAL_1:17;
    hence thesis;
  end;
  hence thesis by A1,SEQ_4:def 4;
end;

theorem Th22:
  upper_bound left_open_halfline(a) = a
proof
  set X = left_open_halfline(a);
A1: for r st r in X holds r <= a by XXREAL_1:233;
  for s st 0 < s ex r st r in X & a-s < r
  proof
    let s;
    assume 0 < s;
    then a-s < a-0 by XREAL_1:17;
    then
A2: a-s < (a-s+a)/2 & (a-s+a)/2 < a by XREAL_1:228;
    take (a-s+a)/2;
    thus thesis by A2,XXREAL_1:233;
  end;
  hence thesis by A1,SEQ_4:def 4;
end;

theorem Th23:
  lower_bound right_closed_halfline(a) = a
proof
  set X = right_closed_halfline(a);
A1: for r st r in X holds a <= r by XXREAL_1:236;
  for s st 0 < s ex r st r in X & r < a+s
  proof
    let s such that
A2: 0 < s;
    take a;
    thus a in X by XXREAL_1:236;
    a+0 < a+s by A2,XREAL_1:8;
    hence thesis;
  end;
  hence thesis by A1,SEQ_4:def 5;
end;

theorem Th24:
  lower_bound right_open_halfline(a) = a
proof
  set X = right_open_halfline(a);
A1: for r st r in X holds a <= r by XXREAL_1:235;
  for s st 0 < s ex r st r in X & r < a+s
  proof
    let s;
    assume 0 < s;
    then a+0 < a+s by XREAL_1:8;
    then
A2: a < (a+(a+s))/2 & (a+(a+s))/2 < a+s by XREAL_1:228;
    take (a+a+s)/2;
    thus thesis by A2,XXREAL_1:235;
  end;
  hence thesis by A1,SEQ_4:def 5;
end;

begin :: Connectedness

registration
  cluster [#]REAL -> connected non bounded_below non bounded_above;
  coherence;
end;

theorem Th25:
  for X being bounded connected Subset of REAL st
  lower_bound X in X & upper_bound X in X
  holds X = [.lower_bound X,upper_bound X.]
proof
  let X be bounded connected Subset of REAL such that
A1: lower_bound X in X & upper_bound X in X;
  thus X c= [.lower_bound X,upper_bound X.] by TOPREAL6:24;
  thus thesis by A1,JCT_MISC:def 1;
end;

theorem Th26:
  for X being bounded Subset of REAL st not lower_bound X in X holds
  X c= ].lower_bound X,upper_bound X.]
proof
  let X be bounded Subset of REAL such that
A1: not lower_bound X in X;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
A3: x <= upper_bound X by A2,SEQ_4:def 4;
  lower_bound X <= x by A2,SEQ_4:def 5;
  then lower_bound X < x by A1,A2,XXREAL_0:1;
  hence thesis by A3,XXREAL_1:2;
end;

theorem Th27:
  for X being bounded connected Subset of REAL st
  not lower_bound X in X & upper_bound X in X
  holds X = ].lower_bound X,upper_bound X.]
proof
  let X be bounded connected Subset of REAL such that
A1: not lower_bound X in X and
A2: upper_bound X in X;
  thus X c= ].lower_bound X,upper_bound X.] by A1,Th26;
  let x be set;
  assume
A3: x in ].lower_bound X,upper_bound X.];
  then reconsider x as Real;
A4: x <= upper_bound X by A3,XXREAL_1:2;
  lower_bound X < x by A3,XXREAL_1:2;
  then lower_bound X - lower_bound X < x - lower_bound X by XREAL_1:16;
  then consider r such that
A5: r in X and
A6: r < lower_bound X + (x - lower_bound X) by A2,SEQ_4:def 5;
A7: [.r,upper_bound X.] c= X by A2,A5,JCT_MISC:def 1;
  x in [.r,upper_bound X.] by A4,A6,XXREAL_1:1;
  hence thesis by A7;
end;

theorem Th28:
  for X being bounded Subset of REAL st not upper_bound X in X holds
  X c= [.lower_bound X,upper_bound X.[
proof
  let X be bounded Subset of REAL such that
A1: not upper_bound X in X;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
A3: lower_bound X <= x by A2,SEQ_4:def 5;
  x <= upper_bound X by A2,SEQ_4:def 4;
  then x < upper_bound X by A1,A2,XXREAL_0:1;
  hence thesis by A3,XXREAL_1:3;
end;

theorem Th29:
  for X being bounded connected Subset of REAL st
  lower_bound X in X & not upper_bound X in X
  holds X = [.lower_bound X,upper_bound X.[
proof
  let X be bounded connected Subset of REAL such that
A1: lower_bound X in X and
A2: not upper_bound X in X;
  thus X c= [.lower_bound X,upper_bound X.[ by A2,Th28;
  let x be set;
  assume
A3: x in [.lower_bound X,upper_bound X.[;
  then reconsider x as Real;
A4: lower_bound X <= x by A3,XXREAL_1:3;
  x < upper_bound X by A3,XXREAL_1:3;
  then x - x < upper_bound X - x by XREAL_1:16;
  then consider r such that
A5: r in X and
A6: upper_bound X - (upper_bound X - x) < r by A1,SEQ_4:def 4;
A7: [.lower_bound X,r.] c= X by A1,A5,JCT_MISC:def 1;
  x in [.lower_bound X,r.] by A4,A6,XXREAL_1:1;
  hence thesis by A7;
end;

theorem Th30:
  for X being bounded Subset of REAL st
  not lower_bound X in X & not upper_bound X in X
  holds X c= ].lower_bound X,upper_bound X.[
proof
  let X be bounded Subset of REAL such that
A1: not lower_bound X in X and
A2: not upper_bound X in X;
  let x be set;
  assume
A3: x in X;
  then reconsider x as Real;
  lower_bound X <= x & x <= upper_bound X by A3,SEQ_4:def 4,def 5;
  then lower_bound X < x & x < upper_bound X by A1,A2,A3,XXREAL_0:1;
  hence thesis by XXREAL_1:4;
end;

theorem Th31:
  for X being non empty bounded connected Subset of REAL st
  not lower_bound X in X & not upper_bound X in X
  holds X = ].lower_bound X,upper_bound X.[
proof
  let X be non empty bounded connected Subset of REAL such that
A1: not lower_bound X in X and
A2: not upper_bound X in X;
  thus X c= ].lower_bound X,upper_bound X.[ by A1,A2,Th30;
  let x be set;
  assume
A3: x in ].lower_bound X,upper_bound X.[;
  then reconsider x as Real;
  lower_bound X < x by A3,XXREAL_1:4;
  then lower_bound X - lower_bound X < x - lower_bound X by XREAL_1:16;
  then consider s such that
A4: s in X and
A5: s < lower_bound X + (x - lower_bound X) by SEQ_4:def 5;
  x < upper_bound X by A3,XXREAL_1:4;
  then x - x < upper_bound X - x by XREAL_1:16;
  then consider r such that
A6: r in X and
A7: upper_bound X - (upper_bound X - x) < r by SEQ_4:def 4;
A8: [.s,r.] c= X by A4,A6,JCT_MISC:def 1;
  x in [.s,r.] by A5,A7,XXREAL_1:1;
  hence thesis by A8;
end;

theorem Th32:
  for X being Subset of REAL st X is bounded_above holds
  X c= left_closed_halfline(upper_bound X)
proof
  let X be Subset of REAL such that
A1: X is bounded_above;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
  x <= upper_bound X by A1,A2,SEQ_4:def 4;
  hence thesis by XXREAL_1:234;
end;

theorem Th33:
  for X being connected Subset of REAL st
  X is not bounded_below & X is bounded_above & upper_bound X in X holds
  X = left_closed_halfline(upper_bound X)
proof
  let X be connected Subset of REAL such that
A1: X is not bounded_below and
A2: X is bounded_above and
A3: upper_bound X in X;
  thus X c= left_closed_halfline(upper_bound X) by A2,Th32;
  let x be set;
  assume
A4: x in left_closed_halfline(upper_bound X);
  then reconsider x as Real;
  consider r such that
A5: r in X and
A6: x > r by A1,SEQ_4:def 2;
A7: x <= upper_bound X by A4,XXREAL_1:234;
A8: [.r,upper_bound X.] c= X by A3,A5,JCT_MISC:def 1;
  x in [.r,upper_bound X.] by A6,A7,XXREAL_1:1;
  hence thesis by A8;
end;

theorem Th34:
  for X being Subset of REAL
   st X is bounded_above & not upper_bound X in X holds
  X c= left_open_halfline(upper_bound X)
proof
  let X be Subset of REAL such that
A1: X is bounded_above and
A2: not upper_bound X in X;
  let x be set;
  assume
A3: x in X;
  then reconsider x as Real;
  x <= upper_bound X by A1,A3,SEQ_4:def 4;
  then x < upper_bound X by A2,A3,XXREAL_0:1;
  hence thesis by XXREAL_1:233;
end;

theorem Th35:
  for X being non empty connected Subset of REAL st
  X is not bounded_below & X is bounded_above & not upper_bound X in X holds
  X = left_open_halfline(upper_bound X)
proof
  let X be non empty connected Subset of REAL such that
A1: X is not bounded_below and
A2: X is bounded_above and
A3: not upper_bound X in X;
  thus X c= left_open_halfline(upper_bound X) by A2,A3,Th34;
  let x be set;
  assume
A4: x in left_open_halfline(upper_bound X);
  then reconsider x as Real;
  consider r such that
A5: r in X and
A6: x > r by A1,SEQ_4:def 2;
  x < upper_bound X by A4,XXREAL_1:233;
  then x - x < upper_bound X - x by XREAL_1:16;
  then consider s such that
A7: s in X and
A8: upper_bound X - (upper_bound X - x) < s by A2,SEQ_4:def 4;
A9: [.r,s.] c= X by A5,A7,JCT_MISC:def 1;
  x in [.r,s.] by A6,A8,XXREAL_1:1;
  hence thesis by A9;
end;

theorem Th36:
  for X being Subset of REAL st X is bounded_below holds
  X c= right_closed_halfline(lower_bound X)
proof
  let X be Subset of REAL such that
A1: X is bounded_below;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
  lower_bound X <= x by A1,A2,SEQ_4:def 5;
  hence thesis by XXREAL_1:236;
end;

theorem Th37:
  for X being connected Subset of REAL st
  X is bounded_below & X is not bounded_above & lower_bound X in X holds
  X = right_closed_halfline(lower_bound X)
proof
  let X be connected Subset of REAL such that
A1: X is bounded_below and
A2: X is not bounded_above and
A3: lower_bound X in X;
  thus X c= right_closed_halfline(lower_bound X) by A1,Th36;
  let x be set;
  assume
A4: x in right_closed_halfline(lower_bound X);
  then reconsider x as Real;
  consider r such that
A5: r in X and
A6: x < r by A2,SEQ_4:def 1;
A7: lower_bound X <= x by A4,XXREAL_1:236;
A8: [.lower_bound X,r.] c= X by A3,A5,JCT_MISC:def 1;
  x in [.lower_bound X,r.] by A6,A7,XXREAL_1:1;
  hence thesis by A8;
end;

theorem Th38:
  for X being Subset of REAL st
  X is bounded_below & not lower_bound X in X holds
  X c= right_open_halfline(lower_bound X)
proof
  let X be Subset of REAL such that
A1: X is bounded_below and
A2: not lower_bound X in X;
  let x be set;
  assume
A3: x in X;
  then reconsider x as Real;
  lower_bound X <= x by A1,A3,SEQ_4:def 5;
  then lower_bound X < x by A2,A3,XXREAL_0:1;
  hence thesis by XXREAL_1:235;
end;

theorem Th39:
  for X being non empty connected Subset of REAL st
  X is bounded_below & X is not bounded_above & not lower_bound X in X holds
  X = right_open_halfline(lower_bound X)
proof
  let X be non empty connected Subset of REAL such that
A1: X is bounded_below and
A2: X is not bounded_above and
A3: not lower_bound X in X;
  thus X c= right_open_halfline(lower_bound X) by A1,A3,Th38;
  let x be set;
  assume
A4: x in right_open_halfline(lower_bound X);
  then reconsider x as Real;
  consider r such that
A5: r in X and
A6: x < r by A2,SEQ_4:def 1;
  lower_bound X < x by A4,XXREAL_1:235;
  then lower_bound X - lower_bound X < x - lower_bound X by XREAL_1:16;
  then consider s such that
A7: s in X and
A8: s < lower_bound X + (x - lower_bound X) by A1,SEQ_4:def 5;
A9: [.s,r.] c= X by A5,A7,JCT_MISC:def 1;
  x in [.s,r.] by A6,A8,XXREAL_1:1;
  hence thesis by A9;
end;

theorem Th40:
  for X being connected Subset of REAL st
  X is not bounded_above & X is not bounded_below holds X = REAL
proof
  let X be connected Subset of REAL;
  assume that
A1: X is not bounded_above and
A2: X is not bounded_below;
  thus X c= REAL;
  let x be set;
  assume x in REAL;
  then reconsider x as Real;
  consider r such that
A3: r in X and
A4: r > x by A1,SEQ_4:def 1;
  consider s such that
A5: s in X and
A6: s < x by A2,SEQ_4:def 2;
A7: [.s,r.] c= X by A3,A5,JCT_MISC:def 1;
  x in [.s,r.] by A4,A6,XXREAL_1:1;
  hence thesis by A7;
end;

theorem Th41:
  for X being connected Subset of REAL holds X is empty or X = REAL or
  (ex a st X = left_closed_halfline(a)) or
  (ex a st X = left_open_halfline(a)) or
  (ex a st X = right_closed_halfline(a)) or
  (ex a st X = right_open_halfline(a)) or (ex a, b st a <= b & X = [.a,b.]) or
  (ex a, b st a < b & X = [.a,b.[) or (ex a, b st a < b & X = ].a,b.]) or
  (ex a, b st a < b & X = ].a,b.[)
proof
  let X be connected Subset of REAL;
  assume X is non empty;
  then reconsider X as non empty connected Subset of REAL;
  per cases;
  suppose X is bounded;
    then reconsider X as non empty bounded connected Subset of REAL;
    per cases;
    suppose X is trivial;
      then consider x being set such that
A1:   X = {x} by REALSET1:def 4;
      x in X by A1,TARSKI:def 1;
      then reconsider x as Real;
      X = [.x,x.] by A1,XXREAL_1:17;
      hence thesis;
    end;
    suppose X is non trivial;
      then consider p, q being set such that
A2:   p in X & q in X & p <> q by REALSET1:14;
A3:   lower_bound X < upper_bound X by A2,SEQ_4:25;
      per cases;
      suppose upper_bound X in X & lower_bound X in X;
        then X = [.lower_bound X,upper_bound X.] by Th25;
        hence thesis by A3;
      end;
      suppose upper_bound X in X & not lower_bound X in X;
        then X = ].lower_bound X,upper_bound X.] by Th27;
        hence thesis by A3;
      end;
      suppose not upper_bound X in X & lower_bound X in X;
        then X = [.lower_bound X,upper_bound X.[ by Th29;
        hence thesis by A3;
      end;
      suppose not upper_bound X in X & not lower_bound X in X;
        then X = ].lower_bound X,upper_bound X.[ by Th31;
        hence thesis by A3;
      end;
    end;
  end;
  suppose
A4: X is not bounded;
    per cases by A4;
    suppose
A5:   X is not bounded_below & X is bounded_above;
      per cases;
      suppose upper_bound X in X;
        then X = left_closed_halfline(upper_bound X) by A5,Th33;
        hence thesis;
      end;
      suppose not upper_bound X in X;
        then X = left_open_halfline(upper_bound X) by A5,Th35;
        hence thesis;
      end;
    end;
    suppose
A6:   X is not bounded_above & X is bounded_below;
      per cases;
      suppose lower_bound X in X;
        then X = right_closed_halfline(lower_bound X) by A6,Th37;
        hence thesis;
      end;
      suppose not lower_bound X in X;
        then X = right_open_halfline(lower_bound X) by A6,Th39;
        hence thesis;
      end;
    end;
    suppose X is not bounded_above & X is not bounded_below;
      hence thesis by Th40;
    end;
  end;
end;

theorem Th42:
  for X being non empty connected Subset of REAL st not r in X holds
  r <= lower_bound X or upper_bound X <= r
proof
  let X be non empty connected Subset of REAL such that
A1: not r in X;
  per cases by Th41;
  suppose X = REAL;
    hence thesis by A1,XREAL_0:def 1;
  end;
  suppose ex a st X = left_closed_halfline(a);
    then consider a such that
A2: X = left_closed_halfline(a);
    upper_bound X = a by A2,Th21;
    hence thesis by A1,A2,XXREAL_1:234;
  end;
  suppose ex a st X = left_open_halfline(a);
    then consider a such that
A3: X = left_open_halfline(a);
    upper_bound X = a by A3,Th22;
    hence thesis by A1,A3,XXREAL_1:233;
  end;
  suppose ex a st X = right_closed_halfline(a);
    then consider a such that
A4: X = right_closed_halfline(a);
    lower_bound X = a by A4,Th23;
    hence thesis by A1,A4,XXREAL_1:236;
  end;
  suppose ex a st X = right_open_halfline(a);
    then consider a such that
A5: X = right_open_halfline(a);
    lower_bound X = a by A5,Th24;
    hence thesis by A1,A5,XXREAL_1:235;
  end;
  suppose ex a, b st a <= b & X = [.a,b.];
    then consider a, b such that
A6: a <= b and
A7: X = [.a,b.];
    lower_bound X = a & upper_bound X = b by A6,A7,JORDAN5A:20;
    hence thesis by A1,A7,XXREAL_1:1;
  end;
  suppose ex a, b st a < b & X = [.a,b.[;
    then consider a, b such that
A8: a < b and
A9: X = [.a,b.[;
    lower_bound X = a & upper_bound X = b by A8,A9,Th4,Th5;
    hence thesis by A1,A9,XXREAL_1:3;
  end;
  suppose ex a, b st a < b & X = ].a,b.];
    then consider a, b such that
A10: a < b and
A11: X = ].a,b.];
    lower_bound X = a & upper_bound X = b by A10,A11,Th6,Th7;
    hence thesis by A1,A11,XXREAL_1:2;
  end;
  suppose ex a, b st a < b & X = ].a,b.[;
    then consider a, b such that
A12: a < b and
A13: X = ].a,b.[;
    lower_bound X = a & upper_bound X = b by A12,A13,TOPREAL6:22;
    hence thesis by A1,A13,XXREAL_1:4;
  end;
end;

theorem Th43:
  for X, Y being non empty bounded connected Subset of REAL
  st lower_bound X <= lower_bound Y & upper_bound Y <= upper_bound X &
  (lower_bound X = lower_bound Y & lower_bound Y in Y
  implies lower_bound X in X) &
  (upper_bound X = upper_bound Y &
  upper_bound Y in Y implies upper_bound X in X) holds Y c= X
proof
  let X, Y be non empty bounded connected Subset of REAL such that
A1: lower_bound X <= lower_bound Y and
A2: upper_bound Y <= upper_bound X and
A3: lower_bound X = lower_bound Y & lower_bound Y in Y
implies lower_bound X in X and
A4: upper_bound X = upper_bound Y &
upper_bound Y in Y implies upper_bound X in X;
  let x be set;
  assume
A5: x in Y;
  then reconsider x as Real;
  Y c= [.lower_bound Y,upper_bound Y.] by TOPREAL6:24;
  then
A6: lower_bound Y <= x & x <= upper_bound Y by A5,XXREAL_1:1;
  then
A7: lower_bound X <= x & x <= upper_bound X by A1,A2,XXREAL_0:2;
  per cases by Th41;
  suppose X = [#]REAL;
    hence thesis;
  end;
  suppose (ex a st X = left_closed_halfline(a)) or
    (ex a st X = left_open_halfline(a)) or
    (ex a st X = right_closed_halfline(a)) or
    ex a st X = right_open_halfline(a);
    hence thesis;
  end;
  suppose ex a, b st a <= b & X = [.a,b.];
    then consider a, b such that
A8: a <= b & X = [.a,b.];
    lower_bound X = a & upper_bound X = b by A8,JORDAN5A:20;
    hence thesis by A7,A8,XXREAL_1:1;
  end;
  suppose ex a, b st a < b & X = [.a,b.[;
    then consider a, b such that
A9: a < b & X = [.a,b.[;
A10: lower_bound X = a & upper_bound X = b by A9,Th4,Th5;
    per cases by Th41;
    suppose Y = [#]REAL;
      hence thesis;
    end;
    suppose (ex a st Y = left_closed_halfline(a)) or
      (ex a st Y = left_open_halfline(a)) or
      (ex a st Y = right_closed_halfline(a)) or
      ex a st Y = right_open_halfline(a);
      hence thesis;
    end;
    suppose ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A11:  a1 <= b1 & Y = [.a1,b1.];
A12:  lower_bound Y = a1 & upper_bound Y = b1 by A11,JORDAN5A:20;
      then b1 < b by A2,A4,A9,A10,A11,XXREAL_0:1,XXREAL_1:1,3;
      then x < b by A6,A12,XXREAL_0:2;
      hence thesis by A7,A9,A10,XXREAL_1:3;
    end;
    suppose ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A13:  a1 < b1 & Y = [.a1,b1.[;
A14:  lower_bound Y = a1 & upper_bound Y = b1 by A13,Th4,Th5;
      x < b1 by A5,A13,XXREAL_1:3;
      then x < b by A2,A10,A14,XXREAL_0:2;
      hence thesis by A7,A9,A10,XXREAL_1:3;
    end;
    suppose ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A15:  a1 < b1 & Y = ].a1,b1.];
A16:  lower_bound Y = a1 & upper_bound Y = b1 by A15,Th6,Th7;
      then b1 < b by A2,A4,A9,A10,A15,XXREAL_0:1,XXREAL_1:2,3;
      then x < b by A6,A16,XXREAL_0:2;
      hence thesis by A7,A9,A10,XXREAL_1:3;
    end;
    suppose ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A17:  a1 < b1 & Y = ].a1,b1.[;
A18:  lower_bound Y = a1 & upper_bound Y = b1 by A17,TOPREAL6:22;
      x < b1 by A5,A17,XXREAL_1:4;
      then x < b by A2,A10,A18,XXREAL_0:2;
      hence thesis by A7,A9,A10,XXREAL_1:3;
    end;
  end;
  suppose ex a, b st a < b & X = ].a,b.];
    then consider a, b such that
A19: a < b & X = ].a,b.];
A20: lower_bound X = a & upper_bound X = b by A19,Th6,Th7;
    per cases by Th41;
    suppose Y = [#]REAL;
      hence thesis;
    end;
    suppose (ex a st Y = left_closed_halfline(a)) or
      (ex a st Y = left_open_halfline(a)) or
      (ex a st Y = right_closed_halfline(a)) or
      ex a st Y = right_open_halfline(a);
      hence thesis;
    end;
    suppose ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A21:  a1 <= b1 & Y = [.a1,b1.];
A22:  lower_bound Y = a1 & upper_bound Y = b1 by A21,JORDAN5A:20;
      then a < a1 by A1,A3,A19,A20,A21,XXREAL_0:1,XXREAL_1:1,2;
      then a < x by A6,A22,XXREAL_0:2;
      hence thesis by A7,A19,A20,XXREAL_1:2;
    end;
    suppose ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A23:  a1 < b1 & Y = [.a1,b1.[;
      lower_bound Y = a1 & upper_bound Y = b1 by A23,Th4,Th5;
      then
A24:  a < a1 by A1,A3,A19,A20,A23,XXREAL_0:1,XXREAL_1:2,3;
      a1 <= x by A5,A23,XXREAL_1:3;
      then a < x by A24,XXREAL_0:2;
      hence thesis by A7,A19,A20,XXREAL_1:2;
    end;
    suppose ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A25:  a1 < b1 & Y = ].a1,b1.];
A26:  lower_bound Y = a1 & upper_bound Y = b1 by A25,Th6,Th7;
      a1 < x by A5,A25,XXREAL_1:2;
      then a < x by A1,A20,A26,XXREAL_0:2;
      hence thesis by A7,A19,A20,XXREAL_1:2;
    end;
    suppose ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A27:  a1 < b1 & Y = ].a1,b1.[;
A28:  lower_bound Y = a1 & upper_bound Y = b1 by A27,TOPREAL6:22;
      a1 < x by A5,A27,XXREAL_1:4;
      then a < x by A1,A20,A28,XXREAL_0:2;
      hence thesis by A7,A19,A20,XXREAL_1:2;
    end;
  end;
  suppose ex a, b st a < b & X = ].a,b.[;
    then consider a, b such that
A29: a < b & X = ].a,b.[;
A30: lower_bound X = a & upper_bound X = b by A29,TOPREAL6:22;
    per cases by Th41;
    suppose Y = [#]REAL;
      hence thesis;
    end;
    suppose (ex a st Y = left_closed_halfline(a)) or
      (ex a st Y = left_open_halfline(a)) or
      (ex a st Y = right_closed_halfline(a)) or
      ex a st Y = right_open_halfline(a);
      hence thesis;
    end;
    suppose ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A31:  a1 <= b1 & Y = [.a1,b1.];
A32:  lower_bound Y = a1 & upper_bound Y = b1 by A31,JORDAN5A:20;
A33:  x <= b1 by A5,A31,XXREAL_1:1;
      b1 < b by A2,A4,A29,A30,A31,A32,XXREAL_0:1,XXREAL_1:1,4;
      then
A34:  x < b by A33,XXREAL_0:2;
      a < a1 by A1,A3,A29,A30,A31,A32,XXREAL_0:1,XXREAL_1:1,4;
      then a < x by A6,A32,XXREAL_0:2;
      hence thesis by A29,A34,XXREAL_1:4;
    end;
    suppose ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A35:  a1 < b1 & Y = [.a1,b1.[;
A36:  lower_bound Y = a1 & upper_bound Y = b1 by A35,Th4,Th5;
      x < b1 by A5,A35,XXREAL_1:3;
      then
A37:  x < b by A2,A30,A36,XXREAL_0:2;
A38:  a < a1 by A1,A3,A29,A30,A35,A36,XXREAL_0:1,XXREAL_1:3,4;
      a1 <= x by A5,A35,XXREAL_1:3;
      then a < x by A38,XXREAL_0:2;
      hence thesis by A29,A37,XXREAL_1:4;
    end;
    suppose ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A39:  a1 < b1 & Y = ].a1,b1.];
A40:  lower_bound Y = a1 & upper_bound Y = b1 by A39,Th6,Th7;
A41:  x <= b1 by A5,A39,XXREAL_1:2;
      b1 < b by A2,A4,A29,A30,A39,A40,XXREAL_0:1,XXREAL_1:2,4;
      then
A42:  x < b by A41,XXREAL_0:2;
      a1 < x by A5,A39,XXREAL_1:2;
      then a < x by A1,A30,A40,XXREAL_0:2;
      hence thesis by A29,A42,XXREAL_1:4;
    end;
    suppose ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A43:  a1 < b1 & Y = ].a1,b1.[;
A44:  lower_bound Y = a1 & upper_bound Y = b1 by A43,TOPREAL6:22;
      x < b1 by A5,A43,XXREAL_1:4;
      then
A45:  x < b by A2,A30,A44,XXREAL_0:2;
      a1 < x by A5,A43,XXREAL_1:4;
      then a < x by A1,A30,A44,XXREAL_0:2;
      hence thesis by A29,A45,XXREAL_1:4;
    end;
  end;
end;

registration
  cluster open closed connected non empty non bounded Subset of REAL;
  existence
  proof
    take [#]REAL;
    thus thesis;
  end;
end;

begin :: R^1

theorem Th44:
  for X being Subset of R^1 st a <= b & X = [.a,b.] holds Fr X = {a,b}
proof
  let X be Subset of R^1 such that
A1: a <= b and
A2: X = [.a,b.];
A3: Cl X = Cl [.a,b.] by A2,TOPREAL6:80
    .= [.a,b.] by PSCOMP_1:34;
  set LO = R^1(left_open_halfline(a)), RC = R^1(right_closed_halfline(b)),
  RO = R^1(right_open_halfline(b)), LC = R^1(left_closed_halfline(a));
A4: LO = left_open_halfline(a) by TOPREALB:def 3;
A5: RC = right_closed_halfline(b) by TOPREALB:def 3;
A6: RO = right_open_halfline(b) by TOPREALB:def 3;
A7: LC = left_closed_halfline(a) by TOPREALB:def 3;
A8: [.a,b.]` = LO \/ RO by A4,A6,XXREAL_1:385;
A9: Cl X` = Cl [.a,b.]` by A2,TOPMETR:24,TOPREAL6:80
    .= Cl left_open_halfline(a) \/ Cl right_open_halfline(b) by A4,A6,A8,Th3
    .= Cl LO \/ Cl right_open_halfline(b) by A4,TOPREAL6:80
    .= Cl LO \/ Cl RO by A6,TOPREAL6:80
    .= LC \/ Cl RO by A7,BORSUK_5:77,TOPREALB:def 3
    .= LC \/ RC by A5,BORSUK_5:75,TOPREALB:def 3
    .= left_closed_halfline(a) \/ right_closed_halfline(b)
  by A5,TOPREALB:def 3;
  [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
  by A1,Th20;
  hence Fr X = {a,b} by A3,A9,TOPS_1:def 2;
end;

theorem
  for X being Subset of R^1 st a < b & X = ].a,b.[ holds Fr X = {a,b}
proof
  let X be Subset of R^1 such that
A1: a < b and
A2: X = ].a,b.[;
A3: Cl X = Cl ].a,b.[ by A2,TOPREAL6:80
    .= [.a,b.] by A1,TOPREAL6:82;
  set RC = R^1(right_closed_halfline(b)), LC = R^1(left_closed_halfline(a));
A4: RC = right_closed_halfline(b) by TOPREALB:def 3;
A5: LC = left_closed_halfline(a) by TOPREALB:def 3;
  then
A6: ].a,b.[` = LC \/ RC by A4,XXREAL_1:398;
A7: Cl X` = Cl ].a,b.[` by A2,TOPMETR:24,TOPREAL6:80
    .= Cl left_closed_halfline(a) \/ Cl right_closed_halfline(b)
  by A4,A5,A6,Th3
    .= Cl left_closed_halfline(a) \/ right_closed_halfline(b) by PSCOMP_1:34
    .= left_closed_halfline(a) \/ right_closed_halfline(b) by PSCOMP_1:34;
  [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
  by A1,Th20;
  hence Fr X = {a,b} by A3,A7,TOPS_1:def 2;
end;

theorem Th46:
  for X being Subset of R^1 st a < b & X = [.a,b.[ holds Fr X = {a,b}
proof
  let X be Subset of R^1 such that
A1: a < b and
A2: X = [.a,b.[;
A3: Cl X = [.a,b.] by A1,A2,BORSUK_5:58;
  set LO = R^1(left_open_halfline(a)), RC = R^1(right_closed_halfline(b)),
  LC = R^1(left_closed_halfline(a));
A4: LO = left_open_halfline(a) by TOPREALB:def 3;
A5: RC = right_closed_halfline(b) by TOPREALB:def 3;
A6: LC = left_closed_halfline(a) by TOPREALB:def 3;
A7: [.a,b.[` = LO \/ RC by A4,A5,XXREAL_1:382;
A8: Cl X` = Cl [.a,b.[` by A2,TOPMETR:24,TOPREAL6:80
    .= Cl left_open_halfline(a) \/ Cl right_closed_halfline(b) by A4,A5,A7,Th3
    .= Cl LO \/ Cl right_closed_halfline(b) by A4,TOPREAL6:80
    .= Cl LO \/ Cl RC by A5,TOPREAL6:80
    .= LC \/ Cl RC by A6,BORSUK_5:77,TOPREALB:def 3
    .= left_closed_halfline(a) \/ right_closed_halfline(b)
  by A5,A6,PRE_TOPC:52;
  [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
  by A1,Th20;
  hence Fr X = {a,b} by A3,A8,TOPS_1:def 2;
end;

theorem Th47:
  for X being Subset of R^1 st a < b & X = ].a,b.] holds Fr X = {a,b}
proof
  let X be Subset of R^1 such that
A1: a < b and
A2: X = ].a,b.];
A3: Cl X = [.a,b.] by A1,A2,BORSUK_5:59;
  set RO = R^1(right_open_halfline(b)), LC = R^1(left_closed_halfline(a));
A4: RO = right_open_halfline(b) by TOPREALB:def 3;
A5: LC = left_closed_halfline(a) by TOPREALB:def 3;
A6: ].a,b.]` = left_closed_halfline(a) \/ right_open_halfline(b)
 by XXREAL_1:399;
A7: Cl X` = Cl ].a,b.]` by A2,TOPMETR:24,TOPREAL6:80
    .= Cl left_closed_halfline(a) \/ Cl right_open_halfline(b) by A6,Th3
    .= Cl LC \/ Cl right_open_halfline(b) by A5,TOPREAL6:80
    .= Cl LC \/ Cl RO by A4,TOPREAL6:80
    .= LC \/ Cl RO by PRE_TOPC:52
    .= left_closed_halfline(a) \/ right_closed_halfline(b)
  by A5,BORSUK_5:75,TOPREALB:def 3;
  [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
  by A1,Th20;
  hence Fr X = {a,b} by A3,A7,TOPS_1:def 2;
end;

theorem
  for X being Subset of R^1 st X = [.a,b.] holds Int X = ].a,b.[
proof
  let X be Subset of R^1 such that
A1: X = [.a,b.];
  reconsider Y = ].a,b.[ as open Subset of R^1 by BORSUK_5:62,TOPMETR:24;
A2: Int X c= X by TOPS_1:44;
  thus Int X c= ].a,b.[
  proof
    let x be set;
    assume
A3: x in Int X;
    then reconsider x as Point of R^1;
A4: now
      assume
A5:   x = a or x = b;
A6:   Int X misses Fr X by TOPS_1:73;
      now
        assume a > b;
        then X = {}R^1 by A1,XXREAL_1:29;
        hence contradiction by A3;
      end;
      then Fr X = {a,b} by A1,Th44;
      then a in Fr X & b in Fr X by TARSKI:def 2;
      hence contradiction by A3,A5,A6,XBOOLE_0:3;
    end;
    a <= x & x <= b by A1,A2,A3,XXREAL_1:1;
    then a < x & x < b by A4,XXREAL_0:1;
    hence thesis by XXREAL_1:4;
  end;
  Y c= Int X by A1,TOPS_1:56,XXREAL_1:37;
  hence thesis;
end;

theorem
  for X being Subset of R^1 st X = ].a,b.[ holds Int X = ].a,b.[
proof
  let X be Subset of R^1;
  assume
A1: X = ].a,b.[;
  then reconsider X as open Subset of R^1 by BORSUK_5:62;
  Int X = X by TOPS_1:55;
  hence thesis by A1;
end;

theorem Th50:
  for X being Subset of R^1 st X = [.a,b.[ holds Int X = ].a,b.[
proof
  let X be Subset of R^1 such that
A1: X = [.a,b.[;
  reconsider Y = ].a,b.[ as open Subset of R^1 by BORSUK_5:62,TOPMETR:24;
A2: Int X c= X by TOPS_1:44;
  thus Int X c= ].a,b.[
  proof
    let x be set;
    assume
A3: x in Int X;
    then reconsider x as Point of R^1;
A4: now
      assume
A5:   x = a;
A6:   Int X misses Fr X by TOPS_1:73;
      now
        assume a >= b;
        then X = {}R^1 by A1,XXREAL_1:27;
        hence contradiction by A3;
      end;
      then Fr X = {a,b} by A1,Th46;
      then a in Fr X by TARSKI:def 2;
      hence contradiction by A3,A5,A6,XBOOLE_0:3;
    end;
    a <= x & x < b by A1,A2,A3,XXREAL_1:3;
    then a < x & x < b by A4,XXREAL_0:1;
    hence thesis by XXREAL_1:4;
  end;
  Y c= Int X by A1,TOPS_1:56,XXREAL_1:45;
  hence thesis;
end;

theorem Th51:
  for X being Subset of R^1 st X = ].a,b.] holds Int X = ].a,b.[
proof
  let X be Subset of R^1 such that
A1: X = ].a,b.];
  reconsider Y = ].a,b.[ as open Subset of R^1 by BORSUK_5:62,TOPMETR:24;
A2: Int X c= X by TOPS_1:44;
  thus Int X c= ].a,b.[
  proof
    let x be set;
    assume
A3: x in Int X;
    then reconsider x as Point of R^1;
A4: now
      assume
A5:   x = b;
A6:   Int X misses Fr X by TOPS_1:73;
      now
        assume a >= b;
        then X = {}R^1 by A1,XXREAL_1:26;
        hence contradiction by A3;
      end;
      then Fr X = {a,b} by A1,Th47;
      then b in Fr X by TARSKI:def 2;
      hence contradiction by A3,A5,A6,XBOOLE_0:3;
    end;
    a < x & x <= b by A1,A2,A3,XXREAL_1:2;
    then a < x & x < b by A4,XXREAL_0:1;
    hence thesis by XXREAL_1:4;
  end;
  Y c= Int X by A1,TOPS_1:56,XXREAL_1:41;
  hence thesis;
end;

registration
  let X be convex Subset of R^1;
  cluster R^1 | X -> convex;
  coherence
  proof
    thus [#](R^1|X) is convex Subset of R^1 by PRE_TOPC:def 10;
  end;
end;

registration
  let A be connected Subset of REAL;
  cluster R^1(A) -> convex;
  coherence
  proof
    let a, b be Point of R^1;
    R^1(A) = A by TOPREALB:def 3;
    hence thesis by JCT_MISC:def 1;
  end;
end;

theorem Th52:
  for X being Subset of R^1, Y being Subset of REAL st X = Y holds
  X is connected iff Y is connected
proof
  let X be Subset of R^1, Y be Subset of REAL such that
A1: X = Y;
  thus X is connected implies Y is connected
  proof
    assume
A2: X is connected;
    let a be real number;
    thus thesis by A1,A2,BORSUK_5:111;
  end;
  assume
A3: Y is connected;
  per cases by A1,A3,Th41;
  suppose X is empty;
    then reconsider A=X as empty Subset of R^1;
    A is connected;
    hence thesis;
  end;
  suppose
A4: X = REAL;
    R^1([#]REAL) = REAL by TOPREALB:def 3;
    then reconsider A=X as non empty convex Subset of R^1 by A4;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose ex a st X = left_closed_halfline(a);
    then consider a such that
A5: X = left_closed_halfline(a);
    R^1(left_closed_halfline(a)) = left_closed_halfline(a) by TOPREALB:def 3;
    then reconsider A=X as non empty convex Subset of R^1 by A5;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose ex a st X = left_open_halfline(a);
    then consider a such that
A6: X = left_open_halfline(a);
    R^1(left_open_halfline(a)) = left_open_halfline(a) by TOPREALB:def 3;
    then reconsider A=X as non empty convex Subset of R^1 by A6;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose ex a st X = right_closed_halfline(a);
    then consider a such that
A7: X = right_closed_halfline(a);
    R^1(right_closed_halfline(a)) = right_closed_halfline(a)
    by TOPREALB:def 3;
    then reconsider A=X as non empty convex Subset of R^1 by A7;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose ex a st X = right_open_halfline(a);
    then consider a such that
A8: X = right_open_halfline(a);
    R^1(right_open_halfline(a)) = right_open_halfline(a) by TOPREALB:def 3;
    then reconsider A=X as non empty convex Subset of R^1 by A8;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose ex a, b st a <= b & X = [.a,b.];
    then reconsider A=X as non empty convex Subset of R^1
    by TOPALG_2:8,XXREAL_1:1;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose ex a, b st a < b & X = [.a,b.[;
    then reconsider A=X as non empty convex Subset of R^1
    by TOPALG_2:10,XXREAL_1:3;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose ex a, b st a < b & X = ].a,b.];
    then reconsider A=X as non empty convex Subset of R^1
    by TOPALG_2:11,XXREAL_1:2;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose ex a, b st a < b & X = ].a,b.[;
    then reconsider A=X as non empty convex Subset of R^1
    by TOPALG_2:9,XXREAL_1:33;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
end;

begin :: Closed Interval TSpace

registration
  let r;
  cluster Closed-Interval-TSpace(r,r) -> trivial;
  coherence
  proof
A1: {r} is trivial;
    the carrier of Closed-Interval-TSpace(r,r) = [.r,r.] by TOPMETR:25;
    then the carrier of Closed-Interval-TSpace(r,r) is trivial
    by A1,XXREAL_1:17;
    hence thesis;
  end;
end;

theorem
  r <= s implies for A being Subset of Closed-Interval-TSpace(r,s) holds
  A is bounded Subset of REAL
proof
  assume r <= s;
  then
A1: the carrier of Closed-Interval-TSpace(r,s) = [.r,s.] by TOPMETR:25;
  let A be Subset of Closed-Interval-TSpace(r,s);
  A is bounded_above bounded_below by A1,XXREAL_2:43,44;
  hence A is bounded Subset of REAL by A1,XBOOLE_1:1;
end;

theorem Th54:
  r <= s implies for X being Subset of Closed-Interval-TSpace(r,s) st
  X = [.a,b.[ & r < a & b <= s holds Int X = ].a,b.[
proof
  set L = Closed-Interval-TSpace(r,s);
  assume
A1: r <= s;
  let X be Subset of Closed-Interval-TSpace(r,s) such that
A2: X = [.a,b.[ and
A3: r < a and
A4: b <= s;
  set c = (r+a)/2;
A5: r < c by A3,XREAL_1:228;
A6: c < a by A3,XREAL_1:228;
  set C1 = R^1(].c,b.[);
  reconsider A = X as Subset of R^1 by PRE_TOPC:39;
A7: C1 = ].c,b.[ by TOPREALB:def 3;
A8: the carrier of L = [.r,s.] by A1,TOPMETR:25;
A9: C1 c= the carrier of L
  proof
    let x be set;
    assume
A10: x in C1;
    then reconsider x as Real by A7;
    c < x & x < b by A7,A10,XXREAL_1:4;
    then r <= x & x <= s by A4,A5,XXREAL_0:2;
    hence thesis by A8,XXREAL_1:1;
  end;
  A c= C1
  proof
    let x be set;
    assume
A11: x in A;
    then reconsider x as Real by A2;
    a <= x & x < b by A2,A11,XXREAL_1:3;
    then c < x & x < b by A6,XXREAL_0:2;
    hence thesis by A7,XXREAL_1:4;
  end;
  then Int A = Int X by A9,TOPS_3:57;
  hence thesis by A2,Th50;
end;

theorem Th55:
  r <= s implies for X being Subset of Closed-Interval-TSpace(r,s) st
  X = ].a,b.] & r <= a & b < s holds Int X = ].a,b.[
proof
  set L = Closed-Interval-TSpace(r,s);
  assume
A1: r <= s;
  let X be Subset of Closed-Interval-TSpace(r,s) such that
A2: X = ].a,b.] and
A3: r <= a and
A4: b < s;
  set c = (b+s)/2;
A5: b < c by A4,XREAL_1:228;
A6: c < s by A4,XREAL_1:228;
  set C1 = R^1(].a,c.[);
  reconsider A = X as Subset of R^1 by PRE_TOPC:39;
A7: C1 = ].a,c.[ by TOPREALB:def 3;
A8: the carrier of L = [.r,s.] by A1,TOPMETR:25;
A9: C1 c= the carrier of L
  proof
    let x be set;
    assume
A10: x in C1;
    then reconsider x as Real by A7;
    a < x & x < c by A7,A10,XXREAL_1:4;
    then r <= x & x <= s by A3,A6,XXREAL_0:2;
    hence thesis by A8,XXREAL_1:1;
  end;
  A c= C1
  proof
    let x be set;
    assume
A11: x in A;
    then reconsider x as Real by A2;
    a < x & x <= b by A2,A11,XXREAL_1:2;
    then a < x & x < c by A5,XXREAL_0:2;
    hence thesis by A7,XXREAL_1:4;
  end;
  then Int A = Int X by A9,TOPS_3:57;
  hence thesis by A2,Th51;
end;

theorem Th56:
  for X being Subset of Closed-Interval-TSpace(r,s),
  Y being Subset of REAL st X = Y holds X is connected iff Y is connected
proof
  let X be Subset of Closed-Interval-TSpace(r,s),
  Y be Subset of REAL such that
A1: X = Y;
  reconsider Z = X as Subset of R^1 by A1,TOPMETR:24;
  hereby
    assume X is connected;
    then Z is connected by CONNSP_1:24;
    hence Y is connected by A1,Th52;
  end;
  assume Y is connected;
  then Z is connected by A1,Th52;
  hence thesis by CONNSP_1:24;
end;

registration
  let T be TopSpace;
  cluster open closed connected Subset of T;
  existence
  proof
    take {}T;
    thus thesis;
  end;
end;

registration
  let T be non empty connected TopSpace;
  cluster non empty open closed connected Subset of T;
  existence
  proof
    take [#]T;
    thus thesis by CONNSP_1:28;
  end;
end;

theorem Th57:
  r <= s implies
  for X being open connected Subset of Closed-Interval-TSpace(r,s) holds
  X is empty or X = [.r,s.] or
  (ex a being real number st r < a & a <= s & X = [.r,a.[) or
  (ex a being real number st r <= a & a < s & X = ].a,s.]) or
  (ex a, b being real number st r <= a & a < b & b <= s & X = ].a,b.[)
proof
  set L = Closed-Interval-TSpace(r,s);
  assume
A1: r <= s;
  let X be open connected Subset of L;
A2: the carrier of L = [#]L;
A3: the carrier of L = [.r,s.] by A1,TOPMETR:25;
  then X is bounded_above bounded_below Subset of REAL
  by XBOOLE_1:1,XXREAL_2:43,44;
  then reconsider Y = X as bounded connected Subset of REAL by Th56;
A4: L is connected by A1,TREAL_1:23;
A5: r in [.r,s.] by A1,XXREAL_1:1;
A6: s in [.r,s.] by A1,XXREAL_1:1;
  per cases by Th41;
  suppose Y is empty or Y = [#]REAL;
    hence thesis;
  end;
  suppose (ex a st Y = left_closed_halfline(a)) or
    (ex a st Y = left_open_halfline(a)) or
    (ex a st Y = right_closed_halfline(a)) or
    (ex a st Y = right_open_halfline(a));
    hence thesis;
  end;
  suppose ex a, b st a <= b & Y = [.a,b.];
    then consider a, b such that
A7: a <= b and
A8: Y = [.a,b.];
A9: r <= a & b <= s by A3,A7,A8,XXREAL_1:50;
A10: X <> {}L by A7,A8,XXREAL_1:1;
A11: X is closed by A8,A9,TOPREALA:44;
    now
      assume
A12:  r <> a or b <> s;
      per cases by A9,A12,XXREAL_0:1;
      suppose r < a;
        then not r in X by A8,XXREAL_1:1;
        hence contradiction by A2,A3,A4,A5,A10,A11,CONNSP_1:14;
      end;
      suppose b < s;
        then not s in X by A8,XXREAL_1:1;
        hence contradiction by A2,A3,A4,A6,A10,A11,CONNSP_1:14;
      end;
    end;
    hence thesis by A8;
  end;
  suppose ex a, b st a < b & Y = [.a,b.[;
    then consider a, b such that
A13: a < b and
A14: Y = [.a,b.[;
A15: r <= a & b <= s by A3,A13,A14,XXREAL_1:52;
    now
      assume r <> a;
      then
A16:  r < a by A15,XXREAL_0:1;
      now
        assume
A17:    Int X = X;
        Int X = ].a,b.[ by A1,A14,A15,A16,Th54;
        then not a in Int X by XXREAL_1:4;
        hence contradiction by A13,A14,A17,XXREAL_1:3;
      end;
      hence contradiction by TOPS_1:55;
    end;
    hence thesis by A13,A14,A15;
  end;
  suppose ex a, b st a < b & Y = ].a,b.];
    then consider a, b such that
A18: a < b and
A19: Y = ].a,b.];
A20: r <= a & b <= s by A3,A18,A19,XXREAL_1:53;
    now
      assume b <> s;
      then
A21:  b < s by A20,XXREAL_0:1;
      now
        assume
A22:    Int X = X;
        Int X = ].a,b.[ by A1,A19,A20,A21,Th55;
        then not b in Int X by XXREAL_1:4;
        hence contradiction by A18,A19,A22,XXREAL_1:2;
      end;
      hence contradiction by TOPS_1:55;
    end;
    hence thesis by A18,A19,A20;
  end;
  suppose ex a, b st a < b & Y = ].a,b.[;
    then consider a, b such that
A23: a < b and
A24: Y = ].a,b.[;
    r <= a & b <= s by A3,A23,A24,XXREAL_1:51;
    hence thesis by A23,A24;
  end;
end;

begin :: Minimal cover of an interval
deffunc F(set) = $1;
defpred P[set,set] means $1 c= $2;

theorem
  for T being 1-sorted, F being Subset-Family of T holds
  F is Cover of T iff F is Cover of [#]T;

theorem Th59:
  for T being 1-sorted, F being finite Subset-Family of T
  for F1 being Subset-Family of T st F is Cover of T &
  F1 = F \ {X where X is Subset of T:
  X in F & ex Y being Subset of T st Y in F & X c< Y}
  holds F1 is Cover of T
proof
  let T be 1-sorted, F be finite Subset-Family of T,
  F1 be Subset-Family of T such that
A1: the carrier of T c= union F and
A2: F1 = F \ {X where X is Subset of T:
  X in F & ex Y being Subset of T st Y in F & X c< Y};
  set ZAW = {X where X is Subset of T:
  X in F & ex Y being Subset of T st Y in F & X c< Y};
  thus the carrier of T c= union F1
  proof
    let t be set;
    assume t in the carrier of T;
    then consider Z being set such that
A3: t in Z and
A4: Z in F by A1,TARSKI:def 4;
    set ALL = {X where X is Subset of T: Z c< X & X in F};
    per cases;
    suppose
A5:   ALL is empty;
      now
        assume Z in ZAW;
        then consider X being Subset of T such that
A6:     Z = X and X in F and
A7:     ex Y being Subset of T st Y in F & X c< Y;
        consider Y being Subset of T such that
A8:     Y in F & X c< Y by A7;
        Y in ALL by A6,A8;
        hence contradiction by A5;
      end;
      then Z in F1 by A2,A4,XBOOLE_0:def 5;
      hence thesis by A3,TARSKI:def 4;
    end;
    suppose ALL is non empty;
      then consider w being set such that
A9:   w in ALL by XBOOLE_0:def 1;
      consider R being Relation of ALL such that
A10:  for x, y being set holds [x,y] in R iff x in ALL & y in ALL & P[x,y]
      from RELSET_1:sch 1;
A11:  R is_reflexive_in ALL
      proof
        let x be set;
        thus thesis by A10;
      end;
      then
A12:  field R = ALL by ORDERS_1:98;
      then
A13:  R is reflexive by A11,RELAT_2:def 9;
A14:  R partially_orders ALL
      proof
        thus R is_reflexive_in ALL by A11;
        thus R is_transitive_in ALL
        proof
          let x, y, z be set;
          assume
A15:      x in ALL & y in ALL & z in ALL;
          assume [x,y] in R & [y,z] in R;
          then x c= y & y c= z by A10;
          then x c= z by XBOOLE_1:1;
          hence [x,z] in R by A10,A15;
        end;
        let x, y be set;
        assume x in ALL & y in ALL;
        assume [x,y] in R & [y,x] in R;
        hence x c= y & y c= x by A10;
      end;
      ALL has_upper_Zorn_property_wrt R
      proof
        let Y be set such that
A16:    Y c= ALL and
A17:    R |_2 Y is_linear-order;
        per cases;
        suppose
A18:      Y is non empty;
          then consider y being set such that
A19:      y in Y by XBOOLE_0:def 1;
          y in ALL by A16,A19;
          then consider X being Subset of T such that
A20:      X = y and
A21:      Z c< X and X in F;
X:        Z c= X by A21,XBOOLE_0:def 8;
          take union Y;
A23:      X c= union Y by A19,A20,ZFMISC_1:92;
          then
A24:      Z c= union Y by X,XBOOLE_1:1;
A25:      for A, B being set st A in Y & B in Y holds A \/ B in Y
          proof
            let A, B be set such that
A26:        A in Y and
A27:        B in Y;
            R |_2 Y is connected by A17,ORDERS_1:def 5;
            then
A28:        R |_2 Y is_connected_in field (R |_2 Y) by RELAT_2:def 14;
            field(R |_2 Y) = Y by A12,A13,A16,ORDERS_1:181;
            then
A29:        [A,B] in R |_2 Y or [B,A] in R |_2 Y or A = B
            by A26,A27,A28,RELAT_2:def 6;
            R |_2 Y c= R by XBOOLE_1:17;
            then A c= B or B c= A by A10,A29;
            hence thesis by A26,A27,XBOOLE_1:12;
          end;
          defpred U[set] means $1 is non empty & $1 c= Y implies union $1 in Y;
A30:      ALL c= F
          proof
            let x be set;
            assume x in ALL;
            then ex X being Subset of T st X = x & Z c< X & X in F;
            hence thesis;
          end;
          then
A31:      Y c= F by A16,XBOOLE_1:1;
A32:      Y is finite by A16,A30;
A33:      U[{}];
A34:      for x, B being set st x in Y & B c= Y & U[B] holds U[B \/ {x}]
          proof
            let x, B be set such that
A35:        x in Y and
A36:        B c= Y and
A37:        U[B] and B \/ {x} is non empty & B \/ {x} c= Y;
A38:        union {x} = x by ZFMISC_1:31;
            per cases;
            suppose B is empty;
              hence thesis by A35,ZFMISC_1:31;
            end;
            suppose B is non empty;
              then x \/ union B in Y by A25,A35,A36,A37;
              hence thesis by A38,ZFMISC_1:96;
            end;
          end;
          U[Y] from FINSET_1:sch 2(A32,A33,A34);
          then
A39:      union Y in F by A18,A31;
          Z <> union Y by A23,A21,XBOOLE_0:def 8;
          then Z c< union Y by A24,XBOOLE_0:def 8;
          hence
A40:      union Y in ALL by A39;
          let z be set;
          assume
A41:      z in Y;
          then P[z,union Y] by ZFMISC_1:92;
          hence [z,union Y] in R by A10,A16,A40,A41;
        end;
        suppose
A42:      Y is empty;
          take w;
          thus w in ALL by A9;
          thus thesis by A42;
        end;
      end;
      then consider M being set such that
A43:  M is_maximal_in R by A12,A14,ORDERS_1:173;
A44:  M in field R by A43,ORDERS_1:def 11;
      then
A45:  ex X being Subset of T st X = M & Z c< X & X in F by A12;
      now
        assume M in ZAW;
        then consider H being Subset of T such that
A46:    M = H and H in F and
A47:    ex Y being Subset of T st Y in F & H c< Y;
        consider Y being Subset of T such that
A48:    Y in F and
A49:    H c< Y by A47;
A51:    Z c< Y by A45,A46,A49,XBOOLE_1:56;
X:      H c= Y by A49,XBOOLE_0:def 8;
A52:    Y in ALL by A48,A51;
        then [M,Y] in R by A10,A12,A44,A46,X;
        hence contradiction by A12,A43,A46,A49,A52,ORDERS_1:def 11;
      end;
      then
X:      M in F1 by A2,A45,XBOOLE_0:def 5;
       Z c= M by A45,XBOOLE_0:def 8;
      hence thesis by A3,X,TARSKI:def 4;
    end;
  end;
  thus thesis;
end;

theorem Th60:
  for S being trivial non empty 1-sorted, s being Point of S,
  F being Subset-Family of S st F is Cover of S holds {s} in F
proof
  let S be trivial non empty 1-sorted, s be Point of S,
  F be Subset-Family of S such that
A1: the carrier of S c= union F;
  s in the carrier of S;
  then consider Z being set such that
A2: s in Z and
A3: Z in F by A1,TARSKI:def 4;
  consider d being Element of S such that
A4: the carrier of S = {d} by TEX_1:def 1;
A5: s = d by REALSET1:14;
  Z = {s}
  proof
    thus for x being set st x in Z holds x in {s} by A3,A4,A5;
    let x be set;
    thus thesis by A2,TARSKI:def 1;
  end;
  hence thesis by A3;
end;

definition
  let T be TopStruct, F be Subset-Family of T;
  attr F is connected means
  :Def1:
  for X being Subset of T st X in F holds X is connected;
end;

registration
  let T be TopSpace;
  cluster non empty open closed connected Subset-Family of T;
  existence
  proof
    {{}T} c= bool the carrier of T
    proof
      let x be set;
      assume x in {{}T};
      then x = {}T by TARSKI:def 1;
      hence thesis;
    end;
    then reconsider F = {{}T} as Subset-Family of T;
    take F;
    thus F is non empty;

thus for P being Subset of T holds P in F implies P is open by TARSKI:def 1;
    thus for P being Subset of T holds P in F implies P is closed
    by TARSKI:def 1;
    thus for P being Subset of T holds P in F implies P is connected
    by TARSKI:def 1;
  end;
end;

reserve n, m for natural number,
  F for Subset-Family of Closed-Interval-TSpace(r,s);

Lm4: [.r,s.] in F & r <= s implies
rng <*[.r,s.]*> c= F & union rng <*[.r,s.]*> = [.r,s.] &
for n being natural number st 1 <= n holds
(n <= len <*[.r,s.]*> implies <*[.r,s.]*>/.n is non empty) &
(n+1 <= len <*[.r,s.]*> implies
lower_bound(<*[.r,s.]*>/.n) <= lower_bound(<*[.r,s.]*>/.(n+1)) &
upper_bound(<*[.r,s.]*>/.n) <= upper_bound(<*[.r,s.]*>/.(n+1)) &
lower_bound(<*[.r,s.]*>/.(n+1)) < upper_bound(<*[.r,s.]*>/.n)) &
(n+2 <= len <*[.r,s.]*> implies
upper_bound(<*[.r,s.]*>/.n) <= lower_bound(<*[.r,s.]*>/.(n+2)))
proof
  assume that
A1: [.r,s.] in F and
A2: r <= s;
  set f = <*[.r,s.]*>;
A3: rng f = {[.r,s.]} by FINSEQ_1:55;
  thus rng f c= F
  proof
    let a be set;
    assume a in rng f;
    hence thesis by A1,A3,TARSKI:def 1;
  end;
  thus union rng f = [.r,s.] by A3,ZFMISC_1:31;
  let n be natural number;
  assume
A4: 1 <= n;
  hereby
    assume n <= len f;
    then n <= 1 by FINSEQ_1:56;
    then n = 1 by A4,XXREAL_0:1;
    then f/.n = [.r,s.] by FINSEQ_4:25;
    hence f/.n is non empty by A2,XXREAL_1:1;
  end;
  hereby
    assume n+1 <= len f;
    then n+1 <= 0+1 by FINSEQ_1:56;
    hence lower_bound(f/.n) <= lower_bound(f/.(n+1)) &
    upper_bound(f/.n) <= upper_bound(f/.(n+1)) &
    lower_bound(f/.(n+1)) < upper_bound(f/.n) by A4,XREAL_1:8;
  end;
  assume n+2 <= len f;
  then n+1+1 <= 0+1 by FINSEQ_1:56;
  hence thesis by XREAL_1:8;
end;

theorem Th61:
  for L being TopSpace, G, G1 being Subset-Family of L st
  G is Cover of L & G is finite for ALL being set st
  G1 = G \ {X where X is Subset of L: X in G & ex Y being Subset of L st
  Y in G & X c< Y} &
  ALL = {C where C is Subset-Family of L: C is Cover of L & C c= G1}
  holds ALL has_lower_Zorn_property_wrt RelIncl ALL
proof
  let L be TopSpace;
  let G, G1 be Subset-Family of L;
  assume that
A1: G is Cover of L and
A2: G is finite;
  set ZAW = {X where X is Subset of L: X in G & ex Y being Subset of L st
  Y in G & X c< Y};
  let ALL be set;
  assume that
A3: G1 = G \ ZAW and
A4: ALL = {C where C is Subset-Family of L: C is Cover of L & C c= G1};
  set R = RelIncl ALL;
A5: G1 is finite by A2,A3;
A6: G1 is Cover of L by A1,A2,A3,Th59;
A7: field R = ALL by WELLORD2:def 1;
  let Y be set such that
A8: Y c= ALL and
A9: R |_2 Y is_linear-order;
  per cases;
  suppose
A10: Y is non empty;
    take x = meet Y;
    consider y being set such that
A11: y in Y by A10,XBOOLE_0:def 1;
    y in ALL by A8,A11;
    then consider C being Subset-Family of L such that
A12: y = C and C is Cover of L and
A13: C c= G1 by A4;
    reconsider X = x as Subset-Family of L by A11,A12,SETFAM_1:8;
A14: x c= G1 by A11,A12,A13,SETFAM_1:8;
A15: bool G1 is finite by A5;
    set E = {F(D) where D is Subset-Family of L: D in bool G1};
A16: E is finite from FRAENKEL:sch 21(A15);
    ALL c= E
    proof
      let a be set;
      assume a in ALL;
      then ex C being Subset-Family of L st a = C & C is Cover of L &
      C c= G1 by A4;
      hence thesis;
    end;
    then ALL is finite by A16;
    then
A17: Y is finite by A8;
    defpred A[set] means $1 is non empty implies meet $1 in Y;
A18: A[{}];
A19: for x, B being set st x in Y & B c= Y & A[B] holds A[B \/ {x}]
    proof
      let x, B be set such that
A20:  x in Y and B c= Y and
A21:  A[B] and B \/ {x} is non empty;
      per cases;
      suppose B is empty;
        hence thesis by A20,SETFAM_1:11;
      end;
      suppose
A22:    B is non empty;
        then
A23:    meet (B \/ {x}) = meet B /\ meet {x} by SETFAM_1:10
          .= meet B /\ x by SETFAM_1:11;
A24:    field (R |_2 Y) = Y by A7,A8,ORDERS_1:181,WELLORD2:2;
        R |_2 Y is connected by A9,ORDERS_1:def 5;
        then R |_2 Y is_connected_in field (R |_2 Y) by RELAT_2:def 14;
        then [x,meet B] in R |_2 Y or [meet B,x] in R |_2 Y or x = meet B
        by A20,A21,A22,A24,RELAT_2:def 6;

then [x,meet B] in R or [meet B,x] in R or x = meet B by XBOOLE_0:def 4;
        then meet B c= x or x c= meet B by A8,A20,A21,A22,WELLORD2:def 1;
        hence meet (B \/ {x}) in Y by A20,A21,A22,A23,XBOOLE_1:28;
      end;
    end;
A25: A[Y] from FINSET_1:sch 2(A17,A18,A19);
    X is Cover of L
    proof
        let a be set;
        assume
A26:    a in the carrier of L;
        meet Y in ALL by A8,A10,A25;
        then consider C being Subset-Family of L such that
X:       meet Y = C & C is Cover of L & C c= G1 by A4;
        the carrier of L c= union C by X,SETFAM_1:def 12;
        then a in union C by A26;
        then a in union X by X;
        hence thesis;
    end;
    hence
A29: x in ALL by A4,A14;
    let y be set;
    assume
A30: y in Y;
    then x c= y by SETFAM_1:8;
    hence [x,y] in R by A8,A29,A30,WELLORD2:def 1;
  end;
  suppose
A31: Y is empty;
    take G1;
    thus thesis by A4,A6,A31;
  end;
end;

theorem Th62:
  for L being TopSpace, G, ALL being set st
  ALL = {C where C is Subset-Family of L: C is Cover of L & C c= G}
  for M being set st M is_minimal_in RelIncl ALL & M in field RelIncl ALL
  for A1 being Subset of L st A1 in M holds not ex A2, A3 being Subset of L st
  A2 in M & A3 in M & A1 c= A2 \/ A3 & A1 <> A2 & A1 <> A3
proof
  let L be TopSpace;
  let G be set;
  let ALL be set such that
A1: ALL = {C where C is Subset-Family of L: C is Cover of L & C c= G};
  set R = RelIncl ALL;
A3: field R = ALL by WELLORD2:def 1;
  let M be set such that
A4: M is_minimal_in RelIncl ALL and
A5: M in field RelIncl ALL;
  consider C being Subset-Family of L such that
A6: M = C and
A7: C is Cover of L and
A8: C c= G by A1,A3,A5;
A9: union C = [#]L by A7,SETFAM_1:60;
  let A1 be Subset of L such that
A10: A1 in M;
  given A2, A3 being Subset of L such that
A11: A2 in M and
A12: A3 in M and
A13: A1 c= A2 \/ A3 and
A14: A1 <> A2 and
A15: A1 <> A3;
  set Y = C \ {A1};
  union Y = the carrier of L
  proof
    thus union Y c= the carrier of L;
    let x be set;
    assume
A16: x in the carrier of L;
    per cases;
    suppose
A17:  x in A1;
      per cases by A13,A17,XBOOLE_0:def 3;
      suppose
A18:    x in A2;
        A2 in Y by A6,A11,A14,ZFMISC_1:64;
        hence x in union Y by A18,TARSKI:def 4;
      end;
      suppose
A19:    x in A3;
        A3 in Y by A6,A12,A15,ZFMISC_1:64;
        hence x in union Y by A19,TARSKI:def 4;
      end;
    end;
    suppose
A20:  not x in A1;
      consider Z being set such that
A21:  x in Z and
A22:  Z in C by A9,A16,TARSKI:def 4;
      Z in Y by A20,A21,A22,ZFMISC_1:64;
      hence x in union Y by A21,TARSKI:def 4;
    end;
  end;
  then
A23: Y is Cover of L by SETFAM_1:def 12;
A24: Y <> M by A10,ZFMISC_1:64;
A25: Y c= M by A6,XBOOLE_1:36;
  then Y c= G by A6,A8,XBOOLE_1:1;
  then
A26: Y in ALL by A1,A23;
  then [Y,M] in R by A3,A5,A25,WELLORD2:def 1;
  hence contradiction by A3,A4,A24,A26,ORDERS_1:def 12;
end;

definition
  let r, s be real number;
  let F be Subset-Family of Closed-Interval-TSpace(r,s) such that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  mode IntervalCover of F -> FinSequence of bool REAL means
    :Def2:
    rng it c= F & union rng it = [.r,s.] &
    (for n being natural number st 1 <= n holds
    (n <= len it implies it/.n is non empty) & (n+1 <= len it implies
    lower_bound(it/.n) <= lower_bound(it/.(n+1)) & upper_bound(it/.n)
     <= upper_bound(it/.(n+1)) &
    lower_bound(it/.(n+1)) < upper_bound(it/.n)) &
    (n+2 <= len it implies upper_bound(it/.n) <= lower_bound(it/.(n+2)))) &
    ([.r,s.] in F implies it = <*[.r,s.]*>) & (not [.r,s.] in F implies
    (ex p being real number st r < p & p <= s & it.1 = [.r,p.[) &
    (ex p being real number st r <= p & p < s & it.len it = ].p,s.]) &
    (for n being natural number st 1 < n & n < len it
    ex p, q being real number st r <= p & p < q & q <= s & it.n = ].p,q.[));
  existence
  proof
    per cases;
    suppose
A5:   [.r,s.] in F;
      take f = <*[.r,s.]*>;
A6:   rng f = {[.r,s.]} by FINSEQ_1:55;
      thus rng f c= F
      proof
        let a be set;
        assume a in rng f;
        hence thesis by A5,A6,TARSKI:def 1;
      end;
      thus union rng f = [.r,s.] by A6,ZFMISC_1:31;
      thus thesis by A4,A5,Lm4;
    end;
    suppose
A7:   not [.r,s.] in F;
A8:   r in [.r,s.] by A4,XXREAL_1:1;
A9:   s in [.r,s.] by A4,XXREAL_1:1;

:: wlasnosci Closed-Interval-TSpace(r,s)
      set L = Closed-Interval-TSpace(r,s);
A10:  the carrier of L = [.r,s.] by A4,TOPMETR:25;
      reconsider T = L as non empty connected TopSpace by A4,TREAL_1:23;
A12:  F is Cover of [#]L by A1;
      L is compact by A4,HEINE:11;
      then [#]L is compact by COMPTS_1:10;
      then consider G being Subset-Family of L such that
A13:  G c= F and
A14:  G is Cover of [#]L and
A15:  G is finite by A2,A12,COMPTS_1:def 7;
A16:  G is Cover of L by A14;
      then
A17:  union G = [#]L by SETFAM_1:60;
A18:  now
        let A be Subset of L;
        thus A is bounded_above bounded_below by A10,XXREAL_2:43,44;
        hence A is bounded Subset of REAL by A10,XBOOLE_1:1;
      end;

:: wlasnosci G1
      set ZAW = {X where X is Subset of L: X in G &
      ex Y being Subset of L st Y in G & X c< Y};
      set G1 = G \ ZAW;
A19:  G1 c= G by XBOOLE_1:36;
A20:  G1 is finite by A15;
A21:  now
        assume
A22:    {} in G1;
        then
A23:    {} in G by XBOOLE_0:def 5;
        consider x being set such that
A24:    x in the carrier of L by XBOOLE_0:def 1;
        consider g being set such that
A25:    x in g and
A26:    g in G by A17,A24,TARSKI:def 4;
        {} c= g by XBOOLE_1:2;
        then {} c< g by A25,XBOOLE_0:def 8;
        then {} in ZAW by A23,A26;
        hence contradiction by A22,XBOOLE_0:def 5;
      end;
A27:  G1 c= F by A13,A19,XBOOLE_1:1;
A28:  for X being set st X in G1 holds X is connected Subset of REAL
      proof
        let X be set;
        assume X in G1;
        then reconsider X as connected Subset of L by A3,A27,Def1;
        reconsider Y = X as Subset of REAL by A10,XBOOLE_1:1;
        Y is connected by Th56;
        hence thesis;
      end;
      F c= bool REAL
      proof
        let a be set;
        assume a in F;
        then a c= REAL by A10,XBOOLE_1:1;
        hence thesis;
      end;
      then
A29:  G1 c= bool REAL by A27,XBOOLE_1:1;

:: konstrukcja pokrycia minimalnego
      set ALL = {C where C is Subset-Family of L: C is Cover of L & C c= G1};
      set R = RelIncl ALL;
A30:  R partially_orders ALL
      proof
        thus R is_reflexive_in ALL & R is_transitive_in ALL &
        R is_antisymmetric_in ALL by WELLORD2:29,30,31;
      end;
A31:  field R = ALL by WELLORD2:def 1;
      ALL has_lower_Zorn_property_wrt R by A15,A16,Th61;
      then consider M being set such that
A32:  M is_minimal_in R by A30,A31,ORDERS_1:174;
A33:  M in field R by A32,ORDERS_1:def 12;
      then consider C being Subset-Family of L such that
A34:  M = C and
A35:  C is Cover of L and
A36:  C c= G1 by A31;
A37:  union C = [#]L by A35,SETFAM_1:60;
      then consider R1 being set such that
A38:  r in R1 and
A39:  R1 in C by A8,A10,TARSKI:def 4;
      consider R2 being set such that
A40:  s in R2 and
A41:  R2 in C by A9,A10,A37,TARSKI:def 4;
A42:  R1 in G1 by A36,A39;
      then
A43:  R1 in F by A27;
A44:  R2 in G1 by A36,A41;
      then
A45:  R2 in F by A27;
      reconsider R1, R2 as open connected Subset of L
      by A2,A3,A27,A42,A44,Def1,TOPS_2:def 1;
      reconsider X = C as non empty finite Subset-Family of REAL
      by A15,A29,A36,A39,XBOOLE_1:1;
A46:  X c= F by A27,A36,XBOOLE_1:1;
A47:  for Z being Subset of REAL st Z in X holds
      Z is non empty open connected Subset of T
      proof
        let Z be Subset of REAL;
        assume
A48:    Z in X;
        then Z is non empty connected by A21,A28,A36;
        hence thesis by A2,A46,A48,Th56,TOPS_2:def 1;
      end;

:: odcinki koncowe
:: LEWY
      set LM = {upper_bound [.r,b.[ where b is Real: [.r,b.[ in G1};
      set LM1 = {upper_bound E where E is Subset of L: E in G1};
A49:  LM1 is finite from FRAENKEL:sch 21(A20);
A50:  LM c= LM1
      proof
        let x be set;
        assume x in LM;
        then ex b being Real st x = upper_bound [.r,b.[ & [.r,b.[ in G1;
        hence thesis;
      end;
A51:  now per cases by A4,A7,A38,A43,Th57;
        suppose ex a being real number st r < a & a <= s & R1 = [.r,a.[;
          then consider a being real number such that r < a and a <= s and
A52:      R1 = [.r,a.[;
          a is Real by XREAL_0:def 1;
          then upper_bound [.r,a.[ in LM by A36,A39,A52;
          hence LM is non empty;
        end;
        suppose ex a being real number st r <= a & a < s & R1 = ].a,s.];
          hence LM is non empty by A38,XXREAL_1:2;
        end;
        suppose ex a, b being real number st r <= a & a < b & b <= s &
          R1 = ].a,b.[;
          hence LM is non empty by A38,XXREAL_1:4;
        end;
      end;
      LM c= REAL
      proof
        let x be set;
        assume x in LM;
        then ex b being Real st x = upper_bound [.r,b.[ & [.r,b.[ in G1;
        hence thesis;
      end;
      then reconsider LM as non empty finite Subset of REAL
      by A49,A50,A51;
      reconsider kL = max LM as Real by XREAL_0:def 1;
      set LEWY = [.r,kL.[;
      kL in LM by XXREAL_2:def 8;
      then consider b being Real such that
A53:  kL = upper_bound [.r,b.[ and
A54:  [.r,b.[ in G1;
A55:  r < b by A21,A54,XXREAL_1:27;
A56:  kL = b by A21,A53,A54,Th5,XXREAL_1:27;
      r < kL by A53,A55,Th5;
      then
A57:  lower_bound LEWY = r by Th4;
A58:  upper_bound LEWY = kL by A21,A53,A54,Th5,XXREAL_1:27;
      reconsider LEWY as non empty Subset of L by A21,A53,A54,Th5,XXREAL_1:27;

:: PRAWY
      set RM = {lower_bound ].c,s.] where c is Real: ].c,s.] in G1};
      set RM1 = {lower_bound E where E is Subset of L: E in G1};
A59:  RM1 is finite from FRAENKEL:sch 21(A20);
A60:  RM c= RM1
      proof
        let x be set;
        assume x in RM;
        then ex b being Real st x = lower_bound ].b,s.] & ].b,s.] in G1;
        hence thesis;
      end;
A61:  now per cases by A4,A7,A40,A45,Th57;
        suppose ex a being real number st r < a & a <= s & R2 = [.r,a.[;
          hence RM is non empty by A40,XXREAL_1:3;
        end;
        suppose ex a being real number st r <= a & a < s & R2 = ].a,s.];
          then consider a being real number such that r <= a and a < s and
A62:      R2 = ].a,s.];
          a is Real by XREAL_0:def 1;
          then lower_bound ].a,s.] in RM by A36,A41,A62;
          hence RM is non empty;
        end;
        suppose ex a, b being real number st r <= a & a < b & b <= s &
          R2 = ].a,b.[;
          hence RM is non empty by A40,XXREAL_1:4;
        end;
      end;
      RM c= REAL
      proof
        let x be set;
        assume x in RM;
        then ex b being Real st x = lower_bound ].b,s.] & ].b,s.] in G1;
        hence thesis;
      end;
      then reconsider RM as non empty finite Subset of REAL
      by A59,A60,A61;
      reconsider pP = min RM as Real by XREAL_0:def 1;
      set PRAWY = ].pP,s.];
      pP in RM by XXREAL_2:def 7;
      then consider b being Real such that
A63:  pP = lower_bound ].b,s.] and
A64:  ].b,s.] in G1;
A65:  b < s by A21,A64,XXREAL_1:26;
A66:  pP = b by A21,A63,A64,Th6,XXREAL_1:26;
A67:  s in ].b,s.] by A65,XXREAL_1:2;
      pP < s by A63,A65,Th6;
      then
A68:  upper_bound PRAWY = s by Th7;
A69:  lower_bound PRAWY = pP by A21,A63,A64,Th6,XXREAL_1:26;
      reconsider PRAWY as non empty Subset of L by A21,A63,A64,Th6,XXREAL_1:26;
A70:  for A being Subset of L st r in A & A in G1 holds A = LEWY
      proof
        let A be Subset of L;
        assume that
A71:    r in A and
A72:    A in G1;
        assume
A73:    A <> LEWY;
A74:    A in F by A27,A72;
A75:    A is open by A2,A27,A72,TOPS_2:def 1;
A76:    A is connected by A3,A27,A72,Def1;
        now
          assume
A77:      (ex a being real number st r <= a & a < s & A = ].a,s.]) or
          ex a, b being real number st r <= a & a < b & b <= s & A = ].a,b.[;
          per cases by A77;
          suppose ex a being real number st r <= a & a < s & A = ].a,s.];
            hence contradiction by A71,XXREAL_1:2;
          end;
          suppose ex a, b being real number st r <= a & a < b & b <= s &
            A = ].a,b.[;
            hence contradiction by A71,XXREAL_1:4;
          end;
        end;
        then consider ak being real number such that
A78:    r < ak & ak <= s & A = [.r,ak.[ by A4,A7,A71,A74,A75,A76,Th57;
A79:    ak is Real by XREAL_0:def 1;
        A c= LEWY
        proof
          let a be set;
          assume
A80:      a in A;
          then a in [.r,s.] by A10;
          then reconsider a as Real;
A81:      r <= a by A78,A80,XXREAL_1:3;
A82:      a < ak by A78,A80,XXREAL_1:3;
          upper_bound A = ak by A78,Th5;
          then ak in LM by A72,A78,A79;
          then ak <= kL by XXREAL_2:def 8;
          then a < kL by A82,XXREAL_0:2;
          hence thesis by A81,XXREAL_1:3;
        end;
        then A c< LEWY by A73,XBOOLE_0:def 8;
        then A in ZAW by A19,A54,A56,A72;
        hence contradiction by A72,XBOOLE_0:def 5;
      end;
A83:  for A being Subset of L st s in A & A in G1 holds A = PRAWY
      proof
        let A be Subset of L;
        assume that
A84:    s in A and
A85:    A in G1;
        assume
A86:    A <> PRAWY;
A87:    A in F by A27,A85;
A88:    A is open by A2,A27,A85,TOPS_2:def 1;
A89:    A is connected by A3,A27,A85,Def1;
        now
          assume
A90:      (ex a being real number st r < a & a <= s & A = [.r,a.[) or
          ex a, b being real number st r <= a & a < b & b <= s & A = ].a,b.[;
          per cases by A90;
          suppose ex a being real number st r < a & a <= s & A = [.r,a.[;
            hence contradiction by A84,XXREAL_1:3;
          end;
          suppose ex a, b being real number st r <= a & a < b & b <= s &
            A = ].a,b.[;
            hence contradiction by A84,XXREAL_1:4;
          end;
        end;
        then consider ak being real number such that
A91:    r <= ak & ak < s & A = ].ak,s.] by A4,A7,A84,A87,A88,A89,Th57;
A92:    ak is Real by XREAL_0:def 1;
        A c= PRAWY
        proof
          let a be set;
          assume
A93:      a in A;
          then a in [.r,s.] by A10;
          then reconsider a as Real;
A94:      a <= s by A91,A93,XXREAL_1:2;
A95:      ak < a by A91,A93,XXREAL_1:2;
          lower_bound A = ak by A91,Th6;
          then ak in RM by A85,A91,A92;
          then pP <= ak by XXREAL_2:def 7;
          then pP < a by A95,XXREAL_0:2;
          hence thesis by A94,XXREAL_1:2;
        end;
        then A c< PRAWY by A86,XBOOLE_0:def 8;
        then A in ZAW by A19,A64,A66,A85;
        hence contradiction by A85,XBOOLE_0:def 5;
      end;
A96:  for A being Subset of L st A in G1 & A <> LEWY & A <> PRAWY
      ex a, b being Real st a < b & A = ].a,b.[
      proof
        let A be Subset of L such that
A97:    A in G1 and
A98:    A <> LEWY and
A99:    A <> PRAWY;
A100:   A in F by A27,A97;
A101:   A is open connected by A2,A3,A27,A97,Def1,TOPS_2:def 1;
        per cases by A4,A7,A21,A97,A100,A101,Th57;
        suppose ex a being real number st r < a & a <= s & A = [.r,a.[;
          then consider a being real number such that r < a & a <= s and
A102:     A = [.r,a.[;
          per cases;
          suppose a <= kL;
            then A c= LEWY by A102,XXREAL_1:38;
            then A c< LEWY by A98,XBOOLE_0:def 8;
            then A in ZAW by A19,A54,A56,A97;
            hence thesis by A97,XBOOLE_0:def 5;
          end;
          suppose a > kL;
            then LEWY c= A by A102,XXREAL_1:38;
            then LEWY c< A by A98,XBOOLE_0:def 8;
            then LEWY in ZAW by A19,A54,A56,A97;
            hence thesis by A54,A56,XBOOLE_0:def 5;
          end;
        end;
        suppose ex a being real number st r <= a & a < s & A = ].a,s.];
          then consider a being real number such that r <= a & a < s and
A103:     A = ].a,s.];
          per cases;
          suppose a >= pP;
            then A c= PRAWY by A103,XXREAL_1:42;
            then A c< PRAWY by A99,XBOOLE_0:def 8;
            then A in ZAW by A19,A64,A66,A97;
            hence thesis by A97,XBOOLE_0:def 5;
          end;
          suppose a < pP;
            then PRAWY c= A by A103,XXREAL_1:42;
            then PRAWY c< A by A99,XBOOLE_0:def 8;
            then PRAWY in ZAW by A19,A64,A66,A97;
            hence thesis by A64,A66,XBOOLE_0:def 5;
          end;
        end;
        suppose ex a, b being real number st r <= a & a < b & b <= s &
          A = ].a,b.[;
          then consider a, b being real number such that
A104:     r <= a & a < b & b <= s & A = ].a,b.[;
          reconsider a, b as Real by XREAL_0:def 1;
          take a, b;
          thus thesis by A104;
        end;
      end;
A105: for A being Subset of L st A in G1 & upper_bound A in A holds A = PRAWY
      proof
        let A be Subset of L such that
A106:   A in G1 and
A107:   upper_bound A in A and
A108:   A <> PRAWY;
        A <> LEWY by A58,A107,XXREAL_1:3;
        then consider a, b being Real such that
A109:   a < b & A = ].a,b.[ by A96,A106,A108;
        upper_bound A = b by A109,TOPREAL6:22;
        hence contradiction by A107,A109,XXREAL_1:4;
      end;

:: konstrukcja ciagu
      defpred F[set,set,set] means
      ex S being Element of X st S = $2 & upper_bound S in $3;
A110: for n being Element of NAT st 1 <= n & n < card X
      for x being Element of X ex y being Element of X st F[n,x,y]
      proof
        let n be Element of NAT such that 1 <= n & n < card X;
        let x be Element of X;
        reconsider x1 = x as Subset of REAL;
A111:   x1 is non empty by A47;
A112:   x c= union X by ZFMISC_1:92;
        then x c= [.r,s.] by A4,A37,TOPMETR:25;
        then x1 is bounded_above by XXREAL_2:43;
        then upper_bound x is Element of L by A10,A37,A111,A112,Th2;
        then consider y being set such that
A113:   upper_bound x in y and
A114:   y in X by A37,TARSKI:def 4;
        reconsider y as Element of X by A114;
        take y, x;
        thus thesis by A113;
      end;
      reconsider LLEWY = LEWY as Element of X by A36,A38,A39,A70;
      consider IT being FinSequence of X such that
A115: len IT = card X and
A116: IT.1 = LLEWY or card X = 0 and
A117: for n being Element of NAT st 1 <= n & n < card X holds
      F[n,IT.n,IT.(n+1)] from RECDEF_1:sch 4(A110);
A118: rng IT c= X;
      rng IT c= bool REAL by XBOOLE_1:1;
      then reconsider IT as FinSequence of bool REAL by FINSEQ_1:def 4;
      take IT;
      thus rng IT c= F by A46,A118,XBOOLE_1:1;
A119: IT is non empty by A115;
      then
A120: rng IT is non empty;
      then
A121: 1 in dom IT by FINSEQ_3:34;
      then
A122: IT/.1 = IT.1 by PARTFUN1:def 8;
A123: len IT in dom IT by A119,FINSEQ_5:6;
A124: for n being natural number st n in dom IT holds IT.n in X & IT/.n in X
      proof
        let n be natural number such that
A125:   n in dom IT;
A126:   IT.n = IT/.n by A125,PARTFUN1:def 8;
        IT.n in rng IT by A125,FUNCT_1:def 5;
        hence thesis by A118,A126;
      end;
A127: for n being natural number st n in dom IT holds IT.n in G1 & IT/.n in G1
      proof
        let n be natural number such that
A128:   n in dom IT;
A129:   IT.n = IT/.n by A128,PARTFUN1:def 8;
        IT.n in X by A124,A128;
        hence thesis by A36,A129;
      end;
A130: for i being natural number st i in dom IT
      for x being Point of L st x < upper_bound(IT/.i)
      ex j being natural number st j in dom IT & j <= i & x in IT/.j
      proof
        let i be natural number such that
A131:   i in dom IT;
        defpred Q[natural number] means
        $1 in dom IT implies for x being Point of L st x < upper_bound(IT/.$1)
        ex j being natural number st j in dom IT & j <= $1 & x in IT/.j;
A132:   Q[0] by FINSEQ_3:26;
A133:   Q[n] implies Q[n+1]
        proof
          assume that
A134:     Q[n] and
A135:     n+1 in dom IT;
          let x be Point of L such that
A136:     x < upper_bound(IT/.(n+1));
A137:     IT/.(n+1) = IT.(n+1) by A135,PARTFUN1:def 8;
          per cases by A135,TOPREALA:23;
          suppose
A138:       n = 0;
            take 1;
            thus 1 in dom IT by A120,FINSEQ_3:34;
            thus 1 <= n+1 by A138;
            r <= x by A10,XXREAL_1:1;
            hence x in IT/.1 by A58,A116,A136,A137,A138,XXREAL_1:3;
          end;
          suppose
A139:       n in dom IT;
            then
A140:       IT/.n = IT.n by PARTFUN1:def 8;
A141:       IT/.(n+1) in G1 by A127,A135;
A142:       IT/.(n+1) in X by A124,A135;
A143:       IT/.(n+1) is connected Subset of REAL by A28,A141;
A144:       IT/.(n+1) is non empty by A21,A127,A135;
A145:       IT/.(n+1) is bounded_below by A18,A142;
A146:       1 <= n by A139,FINSEQ_3:27;
            n+1 <= len IT by A135,FINSEQ_3:27;
            then n < len IT by NAT_1:13;
            then
A147:       ex S being Element of X st S = IT.n & upper_bound S in IT. (n+1)
            by A115,A117,A139,A146;
            then
A148:       lower_bound(IT/.(n+1)) <= upper_bound(IT/.n)
by A137,A140,A145,SEQ_4:def 5;
            per cases by XXREAL_0:1;
            suppose x < upper_bound(IT/.n);
              then consider j being natural number such that
A149:         j in dom IT and
A150:         j <= n and
A151:         x in IT/.j by A134,A139;
              take j;
              thus j in dom IT by A149;
              j+0 < n+1 by A150,XREAL_1:10;
              hence j <= n+1;
              thus x in IT/.j by A151;
            end;
            suppose
A152:         x = upper_bound(IT/.n);
              take n+1;
              thus n+1 in dom IT by A135;
              thus n+1 <= n+1;
              thus x in IT/.(n+1) by A137,A139,A147,A152,PARTFUN1:def 8;
            end;
            suppose
A153:         x > upper_bound(IT/.n);
              take n+1;
              thus n+1 in dom IT by A135;
              thus n+1 <= n+1;
              lower_bound(IT/.(n+1)) < x by A148,A153,XXREAL_0:2;
              hence x in IT/.(n+1) by A136,A143,A144,Th42;
            end;
          end;
        end;
        Q[n] from NAT_1:sch 2(A132,A133);
        hence thesis by A131;
      end;
A154: for i being natural number st i in dom IT
      for j being natural number st j in dom IT & i < j
      ex y being Point of L st y in IT/.j &
      for x being Point of L st x in IT/.i holds x < y
      proof
        let i be natural number such that
A155:   i in dom IT;
        defpred W[natural number] means $1 in dom IT & i < $1 implies
        ex y being Point of L st y in IT/.$1 &
        for x being Point of L st x in IT/.i holds x < y;
A156:   W[0];
A157:   for n holds W[n] implies W[n+1]
        proof
          let n;
          assume that
A158:     W[n] and
A159:     n+1 in dom IT;
A160:     IT/.(n+1) = IT.(n+1) by A159,PARTFUN1:def 8;
          assume i < n+1;
          then
A161:     i <= n by NAT_1:13;
          per cases by A159,TOPREALA:23;
          suppose n = 0;
            then i = 0 by A161;
            hence thesis by A155,FINSEQ_3:26;
          end;
          suppose
A162:       n in dom IT;
            then
A163:       IT/.n = IT.n by PARTFUN1:def 8;
            then
A164:       IT/.n in rng IT by A162,FUNCT_1:def 5;
A165:       IT/.n in X by A124,A162;
            then
A166:       IT/.n is bounded_above by A18;
A167:       IT/.n is non empty by A47,A118,A164;
A168:       IT/.n is Subset of L by A47,A118,A164;
A169:       n is Element of NAT by ORDINAL1:def 13;
A170:       1 <= n by A162,FINSEQ_3:27;
A171:       n+1 <= len IT by A159,FINSEQ_3:27;
            then n < card X by A115,NAT_1:13;
            then consider S being Element of X such that
A172:       S = IT.n and
A173:       upper_bound S in IT.(n+1) by A117,A169,A170;
A174:       IT/.(n+1) in rng IT by A159,A160,FUNCT_1:def 5;
A175:       IT/.(n+1) in X by A124,A159;
A176:       IT/.(n+1) is open connected Subset of L by A47,A118,A174;
            then
A177:       IT/.(n+1) is connected Subset of REAL by Th56;
A178:       IT/.(n+1) is bounded_below by A18,A176;
A179:       IT/.(n+1) is non empty by A47,A118,A174;
A180:       IT/.(n+1) is bounded_above by A18,A176;
            then
A181:       upper_bound S <= upper_bound(IT/.(n+1)) by A160,A173,SEQ_4:def 4;
A182:       lower_bound(IT/.(n+1)) <= upper_bound S
by A160,A173,A178,SEQ_4:def 5;
            upper_bound(IT/.(n+1)) in [.r,s.] by A10,A176,A179,A180,Th2;
            then
A183:       upper_bound(IT/.(n+1)) <= s by XXREAL_1:1;
            upper_bound(IT/.n) in [.r,s.] by A10,A166,A167,A168,Th2;
            then
A184:       upper_bound(IT/.n) <= s by XXREAL_1:1;
            per cases by A161,XXREAL_0:1;
            suppose i < n;
              then consider y being Point of L such that
A185:         y in IT/.n and
A186:         for x being Point of L st x in IT/.i holds x < y by A158,A162;
A187:         y <= upper_bound(IT/.n) by A166,A185,SEQ_4:def 4;
              per cases by A181,XXREAL_0:1;
              suppose
A188:           upper_bound S < upper_bound(IT/.(n+1));
                set y1 = (upper_bound S + upper_bound(IT/.(n+1))) / 2;
A189:           upper_bound S < y1 by A188,XREAL_1:228;
A190:           y1 < upper_bound(IT/.(n+1)) by A188,XREAL_1:228;
                then
A191:           y1 < s by A183,XXREAL_0:2;
                upper_bound S in [.r,s.] by A10,A163,A166,A167,A168,A172,Th2;
                then r <= upper_bound S by XXREAL_1:1;
                then r <= y1 by A189,XXREAL_0:2;
                then reconsider y1 as Point of L by A10,A191,XXREAL_1:1;
                take y1;
                lower_bound(IT/.(n+1)) < y1 by A182,A189,XXREAL_0:2;
                hence y1 in IT/.(n+1) by A177,A179,A190,Th42;
                let x be Point of L;
                assume x in IT/.i;
                then x < upper_bound(IT/.n) by A186,A187,XXREAL_0:2;
                hence x < y1 by A163,A172,A189,XXREAL_0:2;
              end;
              suppose upper_bound S = upper_bound(IT/.(n+1));
                then
A192:           IT/.(n+1) = PRAWY by A36,A105,A160,A173,A175;
                reconsider y1 = s as Point of L by A4,A10,XXREAL_1:1;
                take y1;
                thus y1 in IT/.(n+1) by A65,A66,A192,XXREAL_1:2;
                let x be Point of L;
                assume x in IT/.i;
                then x < upper_bound(IT/.n) by A186,A187,XXREAL_0:2;
                hence x < y1 by A184,XXREAL_0:2;
              end;
            end;
            suppose
A193:         i = n;

reconsider y1 = upper_bound(IT/.n) as Element of L by A10,A166,A167,A168,Th2;
              take y1;
              thus y1 in IT/.(n+1) by A160,A162,A172,A173,PARTFUN1:def 8;
              let x be Point of L;
              assume
A194:         x in IT/.i;
              then
A195:         x <= upper_bound(IT/.n) by A166,A193,SEQ_4:def 4;
              now
                assume x = upper_bound(IT/.n);
                then
A196:           IT/.n = PRAWY by A36,A105,A165,A193,A194;
                set IT1 = IT|Seg n;
A197:           rng IT1 c= rng IT by RELAT_1:99;
                rng IT1 c= bool the carrier of L
                proof
                  let A be set;
                  assume A in rng IT1;
                  then A in rng IT by A197;
                  then A in X by A118;
                  hence thesis;
                end;
                then reconsider FI = rng IT1 as Subset-Family of L;
A198:           now
                  assume
A199:             FI <> X;
A200:             FI c= X by A118,A197,XBOOLE_1:1;
                  then
A201:             FI c= G1 by A36,XBOOLE_1:1;
                  union FI = the carrier of L
                  proof
                    thus union FI c= the carrier of L;
                    let l be set;
                    assume l in the carrier of L;
                    then reconsider l as Point of L;
                    per cases;
                    suppose l < upper_bound(IT/.n);
                      then consider j being natural number such that
A202:                 j in dom IT and
A203:                 j <= n and
A204:                 l in IT/.j by A130,A162;
A205:                 IT.j = IT/.j by A202,PARTFUN1:def 8;
                      1 <= j by A202,FINSEQ_3:27;
                      then j in Seg n by A203,FINSEQ_1:3;
                      then IT.j in FI by A202,FUNCT_1:73;
                      hence thesis by A204,A205,TARSKI:def 4;
                    end;
                    suppose
A206:                 l >= upper_bound(IT/.n);
                      l <= s by A10,XXREAL_1:1;
                      then
A207:                 l = s by A68,A196,A206,XXREAL_0:1;
                      n in Seg n by A170,FINSEQ_1:3;
                      then IT.n in FI by A162,FUNCT_1:73;
                      hence thesis by A66,A67,A163,A196,A207,TARSKI:def 4;
                    end;
                  end;
                  then FI is Cover of L by SETFAM_1:def 12;
                  then
A208:             FI in ALL by A201;
                  then [FI,M] in R by A31,A33,A34,A200,WELLORD2:def 1;
                  hence contradiction by A31,A32,A34,A199,A208,ORDERS_1:def 12;
                end;
A209:           card rng IT1 <= card dom IT1 by CARD_2:66;
                Seg n c= dom IT
                proof
                  let x be set;
                  assume
A210:             x in Seg n;
                  then reconsider x as Nat;
A211:             1 <= x by A210,FINSEQ_1:3;
A212:             x <= n by A210,FINSEQ_1:3;
                  n+0 <= n+1 by XREAL_1:8;
                  then x <= n+1 by A212,XXREAL_0:2;
                  then x <= len IT by A171,XXREAL_0:2;
                  hence thesis by A211,FINSEQ_3:27;
                end;
                then dom IT1 = Seg n by RELAT_1:91;
                then card dom IT1 = n by FINSEQ_1:78;
                then n+1 <= n+0 by A115,A171,A198,A209,XXREAL_0:2;
                hence contradiction by XREAL_1:8;
              end;
              hence x < y1 by A195,XXREAL_0:1;
            end;
          end;
        end;
        for n holds W[n] from NAT_1:sch 2(A156,A157);
        hence thesis;
      end;
A213: IT is Function of dom IT,X by A118,FUNCT_2:4;
      dom IT = Seg len IT by FINSEQ_1:def 3;
      then
A214: card dom IT = card X by A115,FINSEQ_1:78;
A215: IT is one-to-one
      proof
        let i, j be set such that
A216:   i in dom IT and
A217:   j in dom IT and
A218:   IT.i = IT.j;
        assume
A219:   i <> j;
A220:   IT/.i = IT.i & IT/.j = IT.j by A216,A217,PARTFUN1:def 8;
        reconsider i, j as Nat by A216,A217;
        per cases by A219,XXREAL_0:1;
        suppose i < j;
          then ex y being Point of L st y in IT/.j &
          for x being Point of L st x in IT/.i holds x < y by A154,A216,A217;
          hence thesis by A218,A220;
        end;
        suppose j < i;
          then ex y being Point of L st y in IT/.i &
          for x being Point of L st x in IT/.j holds x < y by A154,A216,A217;
          hence thesis by A218,A220;
        end;
      end;
      then
A221: rng IT = X by A213,A214,FINSEQ_4:78;
      hence union rng IT = [.r,s.] by A4,A37,TOPMETR:25;
A222: for i, j being natural number st i in dom IT & j in dom IT & i <> j holds
      IT/.i <> IT/.j
      proof
        let i, j be natural number such that
A223:   i in dom IT and
A224:   j in dom IT and
A225:   i <> j;
        IT/.i = IT.i & IT/.j = IT.j by A223,A224,PARTFUN1:def 8;
        hence IT/.i <> IT/.j by A215,A223,A224,A225,FUNCT_1:def 8;
      end;
      ex Z being set st s in Z & Z in C by A9,A10,A37,TARSKI:def 4;
      then PRAWY in X by A36,A83;
      then consider i being set such that
A226: i in dom IT and
A227: IT.i = PRAWY by A221,FUNCT_1:def 5;
      reconsider i as Element of NAT by A226;
A228: 1 <= i by A226,FINSEQ_3:27;
A229: i <= len IT by A226,FINSEQ_3:27;
A230: IT/.i = IT.i by A226,PARTFUN1:def 8;
A231: now
        assume i <> len IT;
        then
A232:   i < len IT by A229,XXREAL_0:1;
        then
A233:   ex S being Element of X st S = IT.i & upper_bound S in IT.(i+1)
        by A115,A117,A228;
A234:   0+1 <= i+1 by XREAL_1:8;
        i+1 <= len IT by A232,NAT_1:13;
        then
A235:   i+1 in dom IT by A234,FINSEQ_3:27;
        then
A236:   IT/.(i+1) = IT.(i+1) by PARTFUN1:def 8;
        IT/.(i+1) in G1 by A127,A235;
        then i+0 = i+1 by A68,A83,A222,A226,A227,A230,A233,A235,A236;
        hence contradiction;
      end;
A237: for n being natural number st 1 < n & n < len IT
      ex a, b being Real st r <= a & a < b & b <= s & IT/.n = ].a,b.[
      proof
        let n be natural number such that
A238:   1 < n and
A239:   n < len IT;
A240:   n in dom IT by A238,A239,FINSEQ_3:27;
        then IT.n in rng IT by FUNCT_1:def 5;
        then
A241:   IT/.n in rng IT by A240,PARTFUN1:def 8;
        then
A242:   IT/.n in X by A118;
        then
A243:   IT/.n in G1 by A36;
A244:   IT/.n in F by A46,A242;
A245:   IT/.n is open connected Subset of L by A47,A118,A241;
        per cases by A4,A7,A21,A243,A244,A245,Th57;
        suppose ex a being real number st r < a & a <= s & IT/.n = [.r,a.[;
          then consider a being real number such that
A246:     r < a and a <= s and
A247:     IT/.n = [.r,a.[;
          r in [.r,a.[ by A246,XXREAL_1:3;
          then IT/.n = LEWY by A36,A70,A242,A247;
          hence thesis by A116,A121,A122,A222,A238,A240;
        end;
        suppose ex a being real number st r <= a & a < s & IT/.n = ].a,s.];
          then consider a being real number such that r <= a and
A248:     a < s and
A249:     IT/.n = ].a,s.];
A250:     upper_bound ].a,s.] = s by A248,Th7;
          s in ].a,s.] by A248,XXREAL_1:2;
          then IT/.n = PRAWY by A36,A105,A242,A249,A250;
          hence thesis by A123,A222,A227,A230,A231,A239,A240;
        end;
        suppose ex a, b being real number st r <= a & a < b & b <= s &
          IT/.n = ].a,b.[;
          then consider a, b being real number such that
A251:     r <= a & a < b & b <= s & IT/.n = ].a,b.[;
          reconsider a, b as Real by XREAL_0:def 1;
          take a, b;
          thus thesis by A251;
        end;
      end;

:: teza
A252: now
        let n be natural number such that
A253:   1 <= n;
        reconsider m = n as Element of NAT by ORDINAL1:def 13;
        hereby
          assume
A254:     n <= len IT;
          then
A255:     m in dom IT by A253,FINSEQ_3:27;
          IT/.n = IT.n by A253,A254,FINSEQ_4:24;
          then IT/.n in rng IT by A255,FUNCT_1:def 5;
          then IT/.n in X by A118;
          hence IT/.n is non empty by A21,A36;
        end;
        hereby
          assume
A256:     n+1 <= len IT;
          then
A257:     m < len IT by NAT_1:13;
          then
A258:     ex S being Element of X st S = IT.n & upper_bound S in IT.(n+1 )
          by A115,A117,A253;
A259:     IT/.n = IT.n by A253,A257,FINSEQ_4:24;
A260:     1 < m+1 by A253,NAT_1:13;
          then
A261:     IT/.(m+1) = IT.(m+1) by A256,FINSEQ_4:24;
A262:     m in dom IT by A253,A257,FINSEQ_3:27;
          then IT/.n in rng IT by A259,FUNCT_1:def 5;
          then
A263:     IT/.n in X by A118;
          then
A264:     IT/.n in G1 by A36;
A265:     n+1 in dom IT by A256,A260,FINSEQ_3:27;
          then
A266:     IT/.(n+1) in rng IT by A261,FUNCT_1:def 5;
          then
A267:     IT/.(n+1) in X by A118;
          then
A268:     IT/.(n+1) in G1 by A36;
          n+0 < n+1 by XREAL_1:8;
          then
A269:     IT/.n <> IT/.(n+1) by A222,A262,A265;
A270:     IT

/.n is non empty bounded connected Subset of REAL by A18,A21,A28,A36,A263;
A271:     IT/.(n+1) is non empty bounded connected Subset of REAL
          by A18,A21,A28,A36,A267;
          IT/.(n+1) c= union X by A118,A266,ZFMISC_1:92;
          then IT/.(n+1) c= [.r,s.] by A4,A37,TOPMETR:25;
          then
A272:     IT/.(n+1) is bounded_above by XXREAL_2:43;
          then
A273:     upper_bound(IT/.n) <= upper_bound(IT/.(n+1))
by A258,A259,A261,SEQ_4:def 4;
          hereby
            assume
A274:       lower_bound(IT/.n) > lower_bound(IT/.(n+1));
            upper_bound(IT/.(n+1)) =
            upper_bound(IT/.n) & upper_bound(IT/.n) in IT/.n implies
            upper_bound(IT/.(n+1)) in IT/.(n+1)
            by A36,A105,A123,A222,A227,A230,A231,A257,A262,A263;
            then IT/.n c= IT/.(n+1) by A270,A271,A273,A274,Th43;
            then IT/.n c< IT/.(n+1) by A269,XBOOLE_0:def 8;
            then IT/.n in ZAW by A19,A264,A268;
            hence contradiction by A36,A263,XBOOLE_0:def 5;
          end;
          thus upper_bound(IT/.n) <= upper_bound(IT/.(n+1))
          by A258,A259,A261,A272,SEQ_4:def 4;
          per cases by A256,XXREAL_0:1;
          suppose
A275:       n+1 = len IT;
            then pP < upper_bound(IT/.n) by A227,A231,A258,A259,XXREAL_1:2;

hence lower_bound(IT/.(n+1)) < upper_bound(IT/.n)
by A227,A230,A231,A275,Th6,XXREAL_1:26;
          end;
          suppose n+1 < len IT;
            then consider a1, b1 being Real such that r <= a1 and
A276:       a1 < b1 and b1 <= s and
A277:       IT/.(n+1) = ].a1,b1.[ by A237,A260;
            a1 < upper_bound(IT/.n) by A258,A259,A261,A277,XXREAL_1:4;
            hence lower_bound(IT/.(n+1)) < upper_bound(IT/.n)
            by A276,A277,TOPREAL6:22;
          end;
        end;
      end;
      hereby
        let n be natural number such that
A278:   1 <= n; thus
A279:   (n <= len IT implies IT/.n is non empty) & (n+1 <= len IT implies
        lower_bound(IT/.n) <= lower_bound(IT/.(n+1)) &
        upper_bound(IT/.n) <= upper_bound(IT/.(n+1)) &
        lower_bound(IT/.(n+1)) < upper_bound(IT/.n)) by A252,A278;
        assume
A280:   n+2 <= len IT;
        assume
A281:   upper_bound(IT/.n) > lower_bound(IT/.(n+2));
        reconsider m = n as Nat;
        0+1 <= n+1 by XREAL_1:8;
        then
A282:   upper_bound(IT/.(n+1)) <= upper_bound(IT/.(n+1+1)) by A252,A280;
A283:   n+0 < n+1 by XREAL_1:8;
        then
A284:   1 < m+1 by A278,XXREAL_0:2;
A285:   n+1+1 <= len IT by A280;
        then
A286:   m+1 < len IT by NAT_1:13;
        then consider a1, b1 being Real such that r <= a1 and
A287:   a1 < b1 and b1 <= s and
A288:   IT/.(n+1) = ].a1,b1.[ by A237,A284;
A289:   upper_bound ].a1,b1.[ = b1 by A287,TOPREAL6:22;
A290:   lower_bound ].a1,b1.[ = a1 by A287,TOPREAL6:22;
        m <= len IT by A283,A286,XXREAL_0:2;
        then
A291:   n in dom IT by A278,FINSEQ_3:27;
        then IT/.n = IT.n by PARTFUN1:def 8;
        then IT/.n in rng IT by A291,FUNCT_1:def 5;
        then
A292:   IT/.n in X by A118;
A293:   n+1 in dom IT by A284,A286,FINSEQ_3:27;
        then IT/.(n+1) = IT.(n+1) by PARTFUN1:def 8;
        then IT/.(n+1) in rng IT by A293,FUNCT_1:def 5;
        then
A294:   IT/.(n+1) in X by A118;
        m+1 <= m+2 by XREAL_1:8;
        then 1 <= m+2 by A284,XXREAL_0:2;
        then
A295:   m+2 in dom IT by A280,FINSEQ_3:27;
        then IT/.(n+2) = IT.(n+2) by PARTFUN1:def 8;
        then IT/.(n+2) in rng IT by A295,FUNCT_1:def 5;
        then
A296:   IT/.(n+2) in X by A118;
        n+0 < n+1 by XREAL_1:8;
        then
A297:   IT/.n <> IT/.(n+1) by A222,A291,A293;
        n+1 < n+2 by XREAL_1:8;
        then
A298:   IT/.(n+2) <> IT/.(n+1) by A222,A293,A295;
        IT/.(n+1) c= IT/.n \/ IT/.(n+2)
        proof
          let x be set;
          assume
A299:     x in IT/.(n+1);
          then reconsider x as Real;
A300:     a1 < x & x < b1 by A288,A299,XXREAL_1:4;
          per cases;
          suppose
A301:       x < upper_bound(IT/.n);
            per cases;
            suppose
A302:         n = 1;
              then lower_bound(IT/.n) <= x
              by A10,A57,A116,A122,A294,A299,XXREAL_1:1;

then x in IT/.n by A53,A56,A57,A116,A122,A301,A302,XXREAL_1:3;
              hence thesis by XBOOLE_0:def 3;
            end;
            suppose n <> 1;
              then
A303:         1 < n by A278,XXREAL_0:1;
              n+0 < n+2 by XREAL_1:8;
              then n < len IT by A280,XXREAL_0:2;
              then consider a, b being Real such that r <= a and
A304:         a < b and b <= s and
A305:         IT/.n = ].a,b.[ by A237,A303;
A306:         lower_bound(IT/.n) = a by A304,A305,TOPREAL6:22;
A307:         upper_bound(IT/.n) = b by A304,A305,TOPREAL6:22;
              lower_bound(IT/.n) < x
              by A279,A285,A288,A290,A300,NAT_1:13,XXREAL_0:2;
              then x in IT/.n by A301,A305,A306,A307,XXREAL_1:4;
              hence thesis by XBOOLE_0:def 3;
            end;
          end;
          suppose x >= upper_bound(IT/.n);
            then
A308:       x > lower_bound(IT/.(n+2)) by A281,XXREAL_0:2;
            per cases;
            suppose
A309:         len IT = n+2;
              x <= s by A10,A294,A299,XXREAL_1:1;
              then x in IT/.(n+2) by A69,A227,A230,A231,A308,A309,XXREAL_1:2;
              hence thesis by XBOOLE_0:def 3;
            end;
            suppose len IT <> n+2;
              then
A310:         n+1+1 < len IT by A280,XXREAL_0:1;
              n+1 < n+2 by XREAL_1:8;
              then 1 < n+2 by A284,XXREAL_0:2;
              then consider a2, b2 being Real such that r <= a2 and
A311:         a2 < b2 and b2 <= s and
A312:         IT/.(n+2) = ].a2,b2.[ by A237,A310;
A313:         upper_bound ].a2,b2.[ = b2 by A311,TOPREAL6:22;
A314:         lower_bound ].a2,b2.[ = a2 by A311,TOPREAL6:22;
              x < b2 by A282,A288,A289,A300,A312,A313,XXREAL_0:2;
              then x in IT/.(n+2) by A308,A312,A314,XXREAL_1:4;
              hence thesis by XBOOLE_0:def 3;
            end;
          end;
        end;
        hence contradiction by A32,A33,A34,A292,A294,A296,A297,A298,Th62;
      end;
      thus [.r,s.] in F implies IT = <*[.r,s.]*> by A7;
      assume not [.r,s.] in F;
      thus ex p being real number st r < p & p <= s & IT.1 = [.r,p.[
      proof
        take kL;
        thus r < kL by A53,A55,Th5;
        upper_bound LEWY <= upper_bound [.r,s.] by A10,SEQ_4:65;
        hence kL <= s by A4,A58,JORDAN5A:20;
        thus thesis by A116;
      end;
      thus ex p being real number st r <= p & p < s & IT.len IT = ].p,s.]
      proof
        take pP;
        lower_bound [.r,s.] <= lower_bound PRAWY by A10,SEQ_4:64;
        hence r <= pP by A4,A69,JORDAN5A:20;
        thus pP < s by A63,A65,Th6;
        thus thesis by A227,A231;
      end;
      let n be natural number such that
A315: 1 < n & n < len IT;
      consider a, b being Real such that
A316: r <= a and
A317: a < b and
A318: b <= s and
A319: IT/.n = ].a,b.[ by A237,A315;
      take a, b;
      thus thesis by A315,A316,A317,A318,A319,FINSEQ_4:24;
    end;
  end;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  [.r,s.] in F implies <*[.r,s.]*> is IntervalCover of F
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: [.r,s.] in F;
  set f = <*[.r,s.]*>;
  rng f c= F & union rng f = [.r,s.] &
  for n being natural number st 1 <= n holds
  (n <= len f implies f/.n is non empty) & (n+1 <= len f implies
  lower_bound(f/.n) <= lower_bound(f/.(n+1)) &
  upper_bound(f/.n) <= upper_bound(f/.(n+1)) &
  lower_bound(f/.(n+1)) < upper_bound(f/.n)) &
  (n+2 <= len f implies upper_bound(f/.n) <= lower_bound(f/.(n+2)))
  by A4,A5,Lm4;
  hence thesis by A1,A2,A3,A4,A5,Def2;
end;

reserve C for IntervalCover of F;

theorem Th64:
  for F being Subset-Family of Closed-Interval-TSpace(r,r),
  C being IntervalCover of F holds
  F is Cover of Closed-Interval-TSpace(r,r) & F is open connected
  implies C = <*[.r,r.]*>
proof
  set L = Closed-Interval-TSpace(r,r);
  let F be Subset-Family of L, C be IntervalCover of F;
  assume that
A1: F is Cover of L and
A2: F is open and
A3: F is connected;
A4: [.r,r.] = {r} by XXREAL_1:17;
  the carrier of L = [.r,r.] by TOPMETR:25;
  then r in the carrier of L by A4,TARSKI:def 1;
  then {r} in F by A1,Th60;
  hence thesis by A1,A2,A3,A4,Def2;
end;

theorem Th65:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected &
  r <= s implies 1 <= len C
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
A5: union rng C = [.r,s.] by A1,A2,A3,A4,Def2;
  assume not thesis;
  then len C+1 <= 0+1 by NAT_1:13;
  then len C <= 0 by XREAL_1:8;
  then len C = 0;
  then C is empty;
  hence thesis by A4,A5,RELAT_1:60,XXREAL_1:1,ZFMISC_1:2;
end;

theorem Th66:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected &
  r <= s & len C = 1 implies C = <*[.r,s.]*>
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: len C = 1;
A6: union rng C = [.r,s.] by A1,A2,A3,A4,Def2;
  C is non empty by A5;
  then rng C is non empty;
  then 1 in dom C by FINSEQ_3:34;
  then
A7: C.1 in rng C by FUNCT_1:def 5;
  C.1 = [.r,s.]
  proof
    thus for a being set st a in C.1 holds a in [.r,s.] by A6,A7,TARSKI:def 4;
    let a be set;
    assume a in [.r,s.];
    then consider Z being set such that
A8: a in Z and
A9: Z in rng C by A6,TARSKI:def 4;
A10: ex x being set st x in dom C & C.x = Z by A9,FUNCT_1:def 5;
    dom C = {1} by A5,FINSEQ_1:4,def 3;
    hence a in C.1 by A8,A10,TARSKI:def 1;
  end;
  hence thesis by A5,FINSEQ_1:57;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  n in dom C & m in dom C & n < m implies
  lower_bound(C/.n) <= lower_bound(C/.m)
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: n in dom C and
A6: m in dom C and
A7: n < m;
  defpred P[natural number] means
  $1 in dom C & n < $1 implies lower_bound(C/.n) <= lower_bound(C/.$1);
A8: P[0];
A9: for k being natural number st P[k] holds P[k+1]
  proof
    let k be natural number such that
A10: P[k] and
A11: k+1 in dom C and
A12: n < k+1;
    per cases by A11,TOPREALA:23;
    suppose k = 0;
      then n <= 0 by A12,NAT_1:13;
      then n = 0;
      hence thesis by A5,FINSEQ_3:26;
    end;
    suppose
A13:  k in dom C;
A14:  n <= k by A12,NAT_1:13;
A15:  1 <= k by A13,FINSEQ_3:27;
      k+1 <= len C by A11,FINSEQ_3:27;
      then lower_bound(C/.k) <= lower_bound(C/.(k+1)) by A1,A2,A3,A4,A15,Def2;
      hence thesis by A10,A13,A14,XXREAL_0:1,2;
    end;
  end;
  for k being natural number holds P[k] from NAT_1:sch 2(A8,A9);
  hence thesis by A6,A7;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  n in dom C & m in dom C & n < m
  implies upper_bound(C/.n) <= upper_bound(C/.m)
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: n in dom C and
A6: m in dom C and
A7: n < m;
  defpred P[natural number] means
  $1 in dom C & n < $1 implies upper_bound(C/.n) <= upper_bound(C/.$1);
A8: P[0];
A9: for k being natural number st P[k] holds P[k+1]
  proof
    let k be natural number such that
A10: P[k] and
A11: k+1 in dom C and
A12: n < k+1;
    per cases by A11,TOPREALA:23;
    suppose k = 0;
      then n <= 0 by A12,NAT_1:13;
      then n = 0;
      hence thesis by A5,FINSEQ_3:26;
    end;
    suppose
A13:  k in dom C;
A14:  n <= k by A12,NAT_1:13;
A15:  1 <= k by A13,FINSEQ_3:27;
      k+1 <= len C by A11,FINSEQ_3:27;
      then upper_bound(C/.k) <= upper_bound(C/.(k+1)) by A1,A2,A3,A4,A15,Def2;
      hence thesis by A10,A13,A14,XXREAL_0:1,2;
    end;
  end;
  for k being natural number holds P[k] from NAT_1:sch 2(A8,A9);
  hence thesis by A6,A7;
end;

theorem Th69:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 <= n & n+1 <= len C implies ].lower_bound(C/.(n+1)),upper_bound(C/.n).[
   is non empty
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: 1 <= n and
A6: n+1 <= len C;
  lower_bound(C/.(n+1)) < upper_bound(C/.n) by A1,A2,A3,A4,A5,A6,Def2;
  hence ].lower_bound(C/.(n+1)),upper_bound(C/.n).[ is non empty
  by XXREAL_1:33;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies lower_bound(C/.1) = r
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  1 <= len C by A1,A2,A3,A4,Th65;
  then
A5: C.1 = C/.1 by FINSEQ_4:24;
  per cases;
  suppose [.r,s.] in F;
    then C = <*[.r,s.]*> by A1,A2,A3,A4,Def2;
    then C/.1 = [.r,s.] by FINSEQ_4:25;
    hence thesis by A4,JORDAN5A:20;
  end;
  suppose not [.r,s.] in F;
    then ex p being real number st r < p & p <= s & C.1 = [.r,p.[
    by A1,A2,A3,A4,Def2;
    hence thesis by A5,Th4;
  end;
end;

theorem Th71:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies r in C/.1
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  1 <= len C by A1,A2,A3,A4,Th65;
  then
A5: C.1 = C/.1 by FINSEQ_4:24;
  per cases;
  suppose [.r,s.] in F;
    then C = <*[.r,s.]*> by A1,A2,A3,A4,Def2;
    then C/.1 = [.r,s.] by FINSEQ_4:25;
    hence thesis by A4,XXREAL_1:1;
  end;
  suppose not [.r,s.] in F;
    then ex p being real number st r < p & p <= s & C.1 = [.r,p.[
    by A1,A2,A3,A4,Def2;
    hence thesis by A5,XXREAL_1:3;
  end;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies upper_bound(C/.len C) = s
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  1 <= len C by A1,A2,A3,A4,Th65;
  then
A5: C.len C = C/.len C by FINSEQ_4:24;
  per cases;
  suppose [.r,s.] in F;
    then
A6: C = <*[.r,s.]*> by A1,A2,A3,A4,Def2;
    then
A7: C/.1 = [.r,s.] by FINSEQ_4:25;
    len C = 1 by A6,FINSEQ_1:56;
    hence thesis by A4,A7,JORDAN5A:20;
  end;
  suppose not [.r,s.] in F;
    then ex p being real number st r <= p & p < s & C.len C = ].p,s.]
    by A1,A2,A3,A4,Def2;
    hence thesis by A5,Th7;
  end;
end;

theorem Th73:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies s in C/.len C
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  1 <= len C by A1,A2,A3,A4,Th65;
  then
A5: C.len C = C/.len C by FINSEQ_4:24;
  per cases;
  suppose [.r,s.] in F;
    then
A6: C = <*[.r,s.]*> by A1,A2,A3,A4,Def2;
    then
A7: C/.1 = [.r,s.] by FINSEQ_4:25;
    len C = 1 by A6,FINSEQ_1:56;
    hence thesis by A4,A7,XXREAL_1:1;
  end;
  suppose not [.r,s.] in F;
    then ex p being real number st r <= p & p < s & C.len C = ].p,s.]
    by A1,A2,A3,A4,Def2;
    hence thesis by A5,XXREAL_1:2;
  end;
end;

definition
  let r, s be real number;
  let F be Subset-Family of Closed-Interval-TSpace(r,s),
  C be IntervalCover of F such that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  mode IntervalCoverPts of C -> FinSequence of REAL means
    :Def3:
    len it = len C + 1 & it.1 = r & it.len it = s &
    for n being natural number st 1 <= n & n+1 < len it holds
    it.(n+1) in ].lower_bound(C/.(n+1)),upper_bound(C/.n).[;
  existence
  proof
    set A = len C + 1;
    defpred P[natural number,set] means
    ($1 = 1 implies $2 = r) & ($1 = A implies $2 = s) &
    (2 <= $1 & $1 <= len C implies $2 in
     ].lower_bound(C/.$1),upper_bound(C/.($1-1)).[);
A5: 0+1 <= len C by A1,A2,A3,A4,Th65;
    then
A6: 0+1 < A by XREAL_1:8;
A7: for k being Nat st k in Seg A ex x being Element of REAL st P[k,x]
    proof
      let k be Nat;
      assume k in Seg A;
      then
A8:   1 <= k & k <= A by FINSEQ_1:3;
      reconsider r, s as Real by XREAL_0:def 1;
A9:   len C + 0 < A by XREAL_1:8;
      per cases by A8,XXREAL_0:1;
      suppose
A10:    k = 1;
        take r;
        thus thesis by A5,A10;
      end;
      suppose
A11:    k = A;
        take s;
        thus thesis by A1,A2,A3,A4,A9,A11,Th65;
      end;
      suppose that
A12:    1 < k and
A13:    k < A;
        k-1 in NAT by A12,INT_1:18;
        then
A14:    k-1 is Nat;
        1-1 < k-1 by A12,XREAL_1:16;
        then
A15:    0+1 <= k-1 by A14,NAT_1:13;
        k <= len C by A13,NAT_1:13;
        then ].lower_bound(C/.(k-1+1)),upper_bound(C/.(k-1)).[ is non empty
        by A1,A2,A3,A4,A14,A15,Th69;
        then consider x being set such that
A16:    x in ].lower_bound(C/.(k-1+1)),upper_bound(C/.(k-1)).[
by XBOOLE_0:def 1;
        reconsider x as Real by A16;
        take x;
        thus P[k,x] by A12,A13,A16;
      end;
    end;
    consider p being FinSequence of REAL such that
A17: dom p = Seg A and
A18: for k being Nat st k in Seg A holds P[k,p.k] from FINSEQ_1:sch 5(A7);
    take p; thus
A19: len p = len C + 1 by A17,FINSEQ_1:def 3;
    1 in Seg A by A6,FINSEQ_1:3;
    hence p.1 = r by A18;
    len p in Seg A by A6,A19,FINSEQ_1:3;
    hence p.len p = s by A18,A19;
    let n be natural number such that
A20: 1 <= n;
    assume
A21: n+1 < len p;
    then
A22: n+1 <= len C by A19,NAT_1:13;
    0+1 <= n+1 by XREAL_1:8;
    then
A23: n+1 in Seg A by A19,A21,FINSEQ_1:3;
    1+1 <= n+1 by A20,XREAL_1:8;
    then p.(n+1) in ].lower_bound(C/.(n+1)),upper_bound(C/.(n+1-1)).[
    by A18,A22,A23;
    hence thesis;
  end;
end;

reserve G for IntervalCoverPts of C;

theorem Th74:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies 2 <= len G
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  1 <= len C by A1,A2,A3,A4,Th65;
  then 1+1 <= len C + 1 by XREAL_1:8;
  hence thesis by A1,A2,A3,A4,Def3;
end;

theorem Th75:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  len C = 1 implies G = <*r,s*>
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: len C = 1;
A6: len G = len C + 1 by A1,A2,A3,A4,Def3;
  then G.1 = r & G.2 = s by A1,A2,A3,A4,A5,Def3;
  hence thesis by A5,A6,FINSEQ_1:61;
end;

theorem Th76:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 <= n & n+1 < len G implies G.(n+1) < upper_bound(C/.n)
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: 1 <= n and
A6: n+1 < len G;
  G.(n+1) in ].lower_bound(C/.(n+1)),upper_bound(C/.n).[
  by A1,A2,A3,A4,A5,A6,Def3;
  hence G.(n+1) < upper_bound(C/.n) by XXREAL_1:4;
end;

theorem Th77:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 < n & n <= len C implies lower_bound(C/.n) < G.n
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  set w = n-'1;
  assume that
A5: 1 < n and
A6: n <= len C;
A7: len G = len C + 1 by A1,A2,A3,A4,Def3;
  1-1 <= n-1 by A5,XREAL_1:11;
  then
A8: w = n-1 by XREAL_0:def 2;
  then n = w+1;
  then
A9: 1 <= w by A5,NAT_1:13;
  n < len C + 1 by A6,NAT_1:13;
  then G.(w+1) in ].lower_bound(C/.(w+1)),upper_bound(C/.w).[
  by A1,A2,A3,A4,A7,A8,A9,Def3;
  hence thesis by A8,XXREAL_1:4;
end;

theorem Th78:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 <= n & n < len C implies G.n <= lower_bound(C/.(n+1))
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  set w = n-'1;
  assume that
A5: 1 <= n and
A6: n < len C;
A7: n+1 <= len C by A6,NAT_1:13;
  per cases by A5,XXREAL_0:1;
  suppose
A8: n = 1;
A9: G.1 = r by A1,A2,A3,A4,Def3;
A10: rng C c= F by A1,A2,A3,A4,Def2;
    0+1 <= n+1 by XREAL_1:8;
    then
A11: C/.(n+1) is non empty by A1,A2,A3,A4,A7,Def2;
    1+1 <= len C by A6,A8,NAT_1:13;
    then
A12: 2 in dom C by FINSEQ_3:27;
    then C.2 in rng C by FUNCT_1:def 5;
    then C.2 in F by A10;
    then C/.2 in F by A12,PARTFUN1:def 8;
    then C/.(n+1) c= the carrier of Closed-Interval-TSpace(r,s) by A8;
    then
A13: C/.(n+1) c= [.r,s.] by A4,TOPMETR:25;
    then C/.(n+1) is bounded_below by XXREAL_2:44;
    then lower_bound(C/.(n+1)) in [.r,s.] by A11,A13,Th1;
    hence thesis by A8,A9,XXREAL_1:1;
  end;
  suppose 1 < n;
    then
A14: 1-1 < n-1 by XREAL_1:11;
    then
A15: w = n-1 by XREAL_0:def 2;
    then
A16: 0+1 <= w by A14,NAT_1:13;
    n-1 < n-0 by XREAL_1:17;
    then
A17: w+1 < n+1 by A15,XREAL_1:8;
    len G = len C + 1 by A1,A2,A3,A4,Def3;
    then n+1 < len G - 1+1 by A6,XREAL_1:8;
    then w+1 < len G by A17,XXREAL_0:2;
    then
A18: G.(w+1) < upper_bound(C/.w) by A1,A2,A3,A4,A16,Th76;
    n+1 <= len C by A6,NAT_1:13;
    then upper_bound(C/.w) <= lower_bound(C/.(w+2))
    by A1,A2,A3,A4,A15,A16,Def2;
    hence thesis by A15,A18,XXREAL_0:2;
  end;
end;

theorem Th79:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r < s
  implies G is increasing
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r < s;
  let m, n be Element of NAT such that
A5: m in dom G and
A6: n in dom G and
A7: m < n;
  defpred P[Nat] means m < $1 & m in dom G & $1 in dom G implies G.m < G.$1;
A8: P[0];
A9: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat such that
A10: P[k] and
A11: m < k+1 and
A12: m in dom G and
A13: k+1 in dom G;
A14: m <= k by A11,NAT_1:13;
A15: 1 <= m by A12,FINSEQ_3:27;
    then
A16: 1 <= k by A14,XXREAL_0:2;
A17: k+1 <= len G by A13,FINSEQ_3:27;
    k+0 <= k+1 by XREAL_1:8;
    then
A18: k <= len G by A17,XXREAL_0:2;
A19: len G = len C + 1 by A1,A2,A3,A4,Def3;
    per cases by A16,A17,XXREAL_0:1;
    suppose that
A20:  1 < k and
A21:  k+1 < len G;
      k < len C by A19,A21,XREAL_1:8;
      then
A22:  G.k <= lower_bound(C/.(k+1)) by A1,A2,A3,A4,A20,Th78;
      G.(k+1) in ].lower_bound(C/.(k+1)),upper_bound(C/.k).[
      by A1,A2,A3,A4,A20,A21,Def3;
      then lower_bound(C/.(k+1)) < G.(k+1) by XXREAL_1:4;
      then G.k < G.(k+1) by A22,XXREAL_0:2;
      hence G.m < G.(k+1) by A10,A12,A14,A18,A20,FINSEQ_3:27,XXREAL_0:1,2;
    end;
    suppose
A23:  k = 1;
      then
A24:  m <= 1 by A11,NAT_1:13;
A25:  1 <= len C by A1,A2,A3,A4,Th65;
      per cases by A25,XXREAL_0:1;
      suppose
A26:    1 < len C;
        then 1+1 <= len C by NAT_1:13;
        then
A27:    lower_bound(C/.2) < G.2 by A1,A2,A3,A4,Th77;
        G.1 <= lower_bound(C/.(1+1)) by A1,A2,A3,A4,A26,Th78;
        then G.1 < G.2 by A27,XXREAL_0:2;
        hence thesis by A15,A23,A24,XXREAL_0:1;
      end;
      suppose 1 = len C;
        then G = <*r,s*> by A1,A2,A3,A4,Th75;
        then G.1 = r & G.2 = s by FINSEQ_1:61;
        hence thesis by A4,A15,A23,A24,XXREAL_0:1;
      end;
    end;
    suppose
A28:  k+1 = len G;
      then
A29:  G.(k+1) = s by A1,A2,A3,A4,Def3;
      per cases by A15,XXREAL_0:1;
      suppose
A30:    1 < m;
        set z = m-'1;
        1-1 <= m-1 by A15,XREAL_1:11;
        then
A31:    z = m-1 by XREAL_0:def 2;
        1+1 <= m by A30,NAT_1:13;
        then
A32:    1+1-1 <= m-1 by XREAL_1:11;
        then
A33:    1 <= z by XREAL_0:def 2;
A34:    z+1 < len G by A11,A28,A31;
        then
A35:    G.m < upper_bound(C/.z) by A1,A2,A3,A4,A31,A32,Th76;
A36:    rng C c= F by A1,A2,A3,A4,Def2;
A37:    z <= len C by A19,A34,XREAL_1:8;
        then
A38:    z in dom C by A33,FINSEQ_3:27;
        then C.z in rng C by FUNCT_1:def 5;
        then C.z in F by A36;
        then C/.z in F by A38,PARTFUN1:def 8;
        then C/.z c= the carrier of Closed-Interval-TSpace(r,s);
        then
A39:    C/.z c= [.r,s.] by A4,TOPMETR:25;
A40:    C/.z is non empty by A1,A2,A3,A4,A33,A37,Def2;
        C/.z is bounded_above by A39,XXREAL_2:43;
        then upper_bound(C/.z) in [.r,s.] by A39,A40,Th2;
        then upper_bound(C/.z) <= s by XXREAL_1:1;
        hence G.m < G.(k+1) by A29,A35,XXREAL_0:2;
      end;
      suppose m = 1;
        hence thesis by A1,A2,A3,A4,A29,Def3;
      end;
    end;
  end;
  for k being Nat holds P[k] from NAT_1:sch 2(A8,A9);
  hence thesis by A5,A6,A7;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 <= n & n < len G implies [.G.n,G.(n+1).] c= C.n
proof
  set L = Closed-Interval-TSpace(r,s);
  assume that
A1: F is Cover of L and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: 1 <= n and
A6: n < len G;
A7: n+1 <= len G by A6,NAT_1:13;
A8: n in dom G by A5,A6,FINSEQ_3:27;
  0+1 <= n+1 by XREAL_1:8;
  then
A9: n+1 in dom G by A7,FINSEQ_3:27;
A10: len G = len C + 1 by A1,A2,A3,A4,Def3;
  then
A11: n <= len C by A6,NAT_1:13;
  then
A12: C/.n = C.n by A5,FINSEQ_4:24;
  n in dom C by A5,A11,FINSEQ_3:27;
  then
A13: C.n in rng C by FUNCT_1:def 5;
  rng C c= F by A1,A2,A3,A4,Def2;
  then C/.n in F by A12,A13;
  then C/.n is connected Subset of L by A3,Def1;
  then
A14: C/.n is connected by Th56;
A15: C/.n is non empty by A1,A2,A3,A4,A5,A11,Def2;
A16: n+0 < n+1 by XREAL_1:8;
  per cases by A4,XXREAL_0:1;
  suppose r = s;
    then C = <*[.r,r.]*> by A1,A2,A3,Th64;
    then
A17: len C = 1 by FINSEQ_1:57;
    then G = <*r,s*> by A1,A2,A3,A4,Th75;
    then
A18: G.1 = r & G.2 = s by FINSEQ_1:61;
A19: n = 1 by A5,A11,A17,XXREAL_0:1;
    C = <*[.r,s.]*> by A1,A2,A3,A4,A17,Th66;
    hence [.G.n,G.(n+1).] c= C.n by A18,A19,FINSEQ_1:57;
  end;
  suppose r < s;
    then G is increasing by A1,A2,A3,Th79;
    then
A20: G.n < G.(n+1) by A8,A9,A16,SEQM_3:def 1;
A21: 2 <= len G by A1,A2,A3,A4,Th74;
    per cases by A5,A7,A21,XXREAL_0:1;
    suppose that
A22:  n = 1 and
A23:  len G = 2;
      G = <*r,s*> by A1,A2,A3,A4,A10,A23,Th75;
      then
A24:  G.1 = r & G.2 = s by FINSEQ_1:61;
      C = <*[.r,s.]*> by A1,A2,A3,A4,A10,A23,Th66;
      hence [.G.n,G.(n+1).] c= C.n by A22,A24,FINSEQ_1:57;
    end;
    suppose that
A25:  n = 1 and
A26:  1+1 < len G;
A27:  G.(1+1) in ].lower_bound(C/.(1+1)),upper_bound(C/.1).[
by A1,A2,A3,A4,A26,Def3;
A28:  G.1 = r by A1,A2,A3,A4,Def3;
A29:  1+1 <= len C by A10,A26,NAT_1:13;
A30:  r in C/.1 by A1,A2,A3,A4,Th71;
A31:  G.(n+1) < upper_bound(C/.n) by A1,A2,A3,A4,A25,A26,Th76;
A32:  lower_bound(C/.1) <= lower_bound(C/.(1+1)) by A1,A2,A3,A4,A29,Def2;
      lower_bound(C/.(1+1)) < G.2 by A27,XXREAL_1:4;
      then lower_bound(C/.n) < G.(n+1) by A25,A32,XXREAL_0:2;
      then G.(n+1) in C.n by A12,A14,A15,A31,Th42;
      hence [.G.n,G.(n+1).] c= C.n by A12,A14,A25,A28,A30,JCT_MISC:def 1;
    end;
    suppose that
A33:  1 < n and
A34:  len G = n+1;
A35:  G.len G = s by A1,A2,A3,A4,Def3;
A36:  lower_bound(C/.n) < G.n by A1,A2,A3,A4,A10,A33,A34,Th77;
A37:  1-1 < n-1 by A33,XREAL_1:11;
      then
A38:  0+1 <= n-1 by INT_1:20;
A39:  n-1 is Element of NAT by A37,INT_1:16;
      then G.(n-1+1) in ].lower_bound(C/.(n-1+1)),upper_bound(C/.(n-1)).[
      by A1,A2,A3,A4,A16,A34,A38,Def3;
      then
A40:  G.n < upper_bound(C/.(n-1)) by XXREAL_1:4;
      upper_bound(C/.(n-1)) <= upper_bound(C/.(n-1+1))
      by A1,A2,A3,A4,A10,A34,A38,A39,Def2;
      then G.n < upper_bound(C/.n) by A40,XXREAL_0:2;
      then
A41:  G.n in C.n by A12,A14,A15,A36,Th42;
      G.(n+1) in C.n by A1,A2,A3,A4,A10,A12,A34,A35,Th73;
      hence [.G.n,G.(n+1).] c= C.n by A12,A14,A41,JCT_MISC:def 1;
    end;
    suppose that
A42:  1 < n and
A43:  n+1 < len G;
      n <= len C by A6,A10,NAT_1:13;
      then
A44:  lower_bound(C/.n) < G.n by A1,A2,A3,A4,A42,Th77;
A45:  G.(n+1) < upper_bound(C/.n) by A1,A2,A3,A4,A5,A43,Th76;
      then G.n < upper_bound(C/.n) by A20,XXREAL_0:2;
      then
A46:  G.n in C.n by A12,A14,A15,A44,Th42;
      lower_bound(C/.n) < G.(n+1) by A20,A44,XXREAL_0:2;
      then G.(n+1) in C.n by A12,A14,A15,A45,Th42;
      hence thesis by A12,A14,A46,JCT_MISC:def 1;
    end;
  end;
end;

