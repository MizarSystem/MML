:: Properties of Connected Subsets of the Real Line
::  by Artur Korni{\l}owicz
:: 
:: Received February 22, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, RELAT_1, ARYTM, SUBSET_1, WELLORD1, FINSET_1, METRIC_1,
      FUNCT_1, ORDINAL2, ARYTM_1, SQUARE_1, ARYTM_3, RCOMP_1, TOPMETR, JORDAN1,
      BOOLE, SETFAM_1, TARSKI, COMPTS_1, FINSEQ_1, RELAT_2, RCOMP_3, FINSEQ_4,
      ORDERS_2, CARD_1, MEMBERED, SEQ_2, LATTICES, TOPS_1, LIMFUNC1, REALSET1,
      WELLORD2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, RELAT_1, SETFAM_1, TEX_1,
      FUNCT_1, RELSET_1, FUNCT_2, SEQ_1, FINSET_1, ORDINAL2, WELLORD1,
      ORDERS_1, WELLORD2, RELAT_2, CARD_1, MEMBERED, NUMBERS, XCMPLX_0,
      XREAL_0, REAL_1, NAT_1, LIMFUNC1, SEQM_3, FINSEQ_1, SEQ_4, FINSEQ_4,
      RCOMP_1, RCOMP_2, PRE_CIRC, SFMASTR3, STRUCT_0, PRE_TOPC, TOPS_1,
      METRIC_1, TOPS_2, COMPTS_1, CONNSP_1, TEX_2, TOPMETR, PCOMPS_1, PSCOMP_1,
      BORSUK_6, TOPALG_2, TOPREALB, BINARITH, JCT_MISC, REALSET2;
 constructors CONNSP_1, COMPTS_1, REALSET2, COMPUT_1, PSCOMP_1, TOPREALB,
      FINSEQ_4, TEX_1, TRIANG_1, BINARITH, JCT_MISC, RCOMP_2, LIMFUNC1, TOPS_1,
      PRE_CIRC, PROB_1, CARD_4, WAYBEL35;
 registrations STRUCT_0, RELSET_1, XREAL_0, PRE_TOPC, SUBSET_1, INT_1,
      TOPALG_2, TOPS_1, YELLOW13, TOPREAL6, BORSUK_6, PSCOMP_1, TOPREALB,
      FINSEQ_1, FINSET_1, NAT_1, WAYBEL_7, TREES_2, MEMBERED, SPRECT_1,
      BORSUK_5, RFINSEQ, RCOMP_1, REALSET1, FCONT_3, METRIC_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions FUNCT_1, TOPS_2, BORSUK_6, COMPTS_1, PRE_TOPC, RELAT_2, ORDERS_1,
      SEQ_4, TARSKI, XBOOLE_0, JCT_MISC, SEQM_3, CONNSP_1, TOPALG_2;
 theorems PRE_TOPC, XREAL_0, FUNCT_2, INT_1, TOPS_2, FUNCT_1, TOPMETR,
      BORSUK_4, RELAT_1, BORSUK_6, TOPALG_2, TARSKI, ZFMISC_1, JORDAN5A,
      FINSEQ_1, XBOOLE_1, COMPTS_1, SETFAM_1, XBOOLE_0, REAL_1, FINSEQ_3,
      NAT_1, RCOMP_1, SEQM_3, FINSEQ_5, HEINE, TOPREAL5, TOPS_1, CONNSP_1,
      TOPREALA, TOPREALB, ORDINAL2, FINSET_1, ORDERS_1, RELAT_2, WELLORD1,
      SUBSET_1, CARD_2, MEMBERED, SEQ_4, PRE_CIRC, GRAPH_5, JORDAN6, BINARITH,
      FINSEQ_4, PSCOMP_1, TOPREAL6, JCT_MISC, RCOMP_2, TOPREAL3, LIMFUNC1,
      XCMPLX_1, SFMASTR3, TREAL_1, REALSET1, BORSUK_5, TOPS_3, TEX_1, REALSET2,
      XREAL_1, METRIC_1, WELLORD2, WAYBEL35;
 schemes NAT_1, RELSET_1, FINSET_1, FRAENKEL, FINSEQ_1, RECDEF_1;

begin :: Preliminaries

Lm8: REAL = [#]REAL by SUBSET_1:def 4;

registration
  let X be non empty set;
  cluster [#]X -> non empty;
coherence by SUBSET_1:def 4;
end;

registration
  cluster -> real-membered SubSpace of RealSpace;
  coherence
  proof 
    let T be SubSpace of RealSpace;
    the carrier of T is Subset of RealSpace by TOPMETR:def 1;
    hence the carrier of T is real-membered by MEMBERED:17,METRIC_1:def 14;
  end;
end;

registration
  let S be real-membered 1-sorted;
  cluster the carrier of S -> real-membered;
coherence by BORSUK_6:def 1;
end;

registration
  cluster non empty finite bounded_below bounded_above (real-membered set);
existence
proof 
  consider X being non empty finite bounded Subset of REAL;
  take X;
  thus thesis;
end;
end;

theorem Th1:
  for X being non empty bounded_below (real-membered set),
      Y being closed Subset of REAL st X c= Y holds inf X in Y
proof 
  let X be non empty bounded_below (real-membered set);
  let Y be closed Subset of REAL;
  assume that
A1: X c= Y;
  reconsider X as non empty bounded_below Subset of REAL by MEMBERED:2;
A2: Cl X c= Y by A1,PSCOMP_1:32;
A3: inf X = inf Cl X by TOPREAL6:77;
  inf Cl X in Cl X by RCOMP_1:31;
  hence thesis by A2,A3;
end;

theorem Th2:
  for X being non empty bounded_above (real-membered set),
      Y being closed Subset of REAL st X c= Y holds sup X in Y
proof 
  let X be non empty bounded_above (real-membered set);
  let Y be closed Subset of REAL;
  assume that
A1: X c= Y;
  reconsider X as non empty bounded_above Subset of REAL by MEMBERED:2;
A2: Cl X c= Y by A1,PSCOMP_1:32;
A3: sup X = sup Cl X by TOPREAL6:78;
  sup Cl X in Cl X by RCOMP_1:30;
  hence thesis by A2,A3;
end;

theorem Th3:
  for X, Y being Subset of REAL holds Cl(X \/ Y) = Cl X \/ Cl Y
  proof 
    let X, Y be Subset of REAL;
    reconsider A = X, B = Y as Subset of R^1 by TOPMETR:24;
    thus Cl(X \/ Y) = Cl(A \/ B) by TOPREAL6:80
    .= Cl A \/ Cl B by PRE_TOPC:50
    .= Cl X \/ Cl B by TOPREAL6:80
    .= Cl X \/ Cl Y by TOPREAL6:80;
  end;

begin :: Intervals

reserve a, b, r, s for real number;

registration
  let r, s;
  cluster [.r,s.[ -> bounded;
coherence
proof 
  thus [.r,s.[ is bounded_below
  proof
    take r;
    thus thesis by RCOMP_2:3;
  end;
  take s;
  thus thesis by RCOMP_2:3;
end;
  cluster ].r,s.] -> bounded;
coherence
proof 
  thus ].r,s.] is bounded_below
  proof
    take r;
    thus thesis by RCOMP_2:4;
  end;
  take s;
  thus thesis by RCOMP_2:4;
end;
  cluster ].r,s.[ -> bounded;
coherence
proof 
  thus ].r,s.[ is bounded_below
  proof
    take r;
    thus thesis by JORDAN6:45;
  end;
  take s;
  thus thesis by JORDAN6:45;
end;
end;

registration
  let r, s;
  cluster [.r,s.] -> connected;
coherence
proof 
  let a, b;
  assume a in [.r,s.];
  then
A1: r <= a by TOPREAL5:1;
  assume b in [.r,s.];
  then
A2: b <= s by TOPREAL5:1;
  let x be set;
  assume
A3: x in [.a,b.];
  then reconsider x as Real;
  a <= x & x <= b by A3,TOPREAL5:1;
  then r <= x & x <= s by A1,A2,XREAL_1:2;
  hence thesis by TOPREAL5:1;
end;
  cluster [.r,s.[ -> connected;
coherence
proof 
  let a, b;
  assume a in [.r,s.[;
  then
A4: r <= a by RCOMP_2:3;
  assume b in [.r,s.[;
  then
A5: b < s by RCOMP_2:3;
  let x be set;
  assume
A6: x in [.a,b.];
  then reconsider x as Real;
  a <= x & x <= b by A6,TOPREAL5:1;
  then r <= x & x < s by A4,A5,XREAL_1:2;
  hence thesis by RCOMP_2:3;
end;
  cluster ].r,s.] -> connected;
coherence
proof 
  let a, b;
  assume a in ].r,s.];
  then
A7: r < a by RCOMP_2:4;
  assume b in ].r,s.];
  then
A8: b <= s by RCOMP_2:4;
  let x be set;
  assume
A9: x in [.a,b.];
  then reconsider x as Real;
  a <= x & x <= b by A9,TOPREAL5:1;
  then r < x & x <= s by A7,A8,XREAL_1:2;
  hence thesis by RCOMP_2:4;
end;
  cluster ].r,s.[ -> connected;
coherence
proof 
  let a, b;
  assume a in ].r,s.[;
  then
A10: r < a by JORDAN6:45;
  assume b in ].r,s.[;
  then
A11: b < s by JORDAN6:45;
  let x be set;
  assume
A12: x in [.a,b.];
  then reconsider x as Real;
  a <= x & x <= b by A12,TOPREAL5:1;
  then r < x & x < s by A10,A11,XREAL_1:2;
  hence thesis by JORDAN6:45;
end;
end;

registration
  cluster open bounded connected non empty Subset of REAL;
existence
proof 
  take ].0,1.[;
  thus thesis;
end;
end;

theorem Th4:
  r < s implies inf [.r,s.[ = r
proof 
  set X = [.r,s.[;
  assume
A1: r < s;
  then
A2: X is non empty by RCOMP_2:3;
A3: for a st a in X holds r <= a by RCOMP_2:3;
  reconsider r, s as Real by XREAL_0:def 1;
A4: r < (r+s)/2 & (r+s)/2 < s by A1,TOPREAL3:3;
    for b st 0 < b ex a st a in X & a < r+b
    proof
      let b such that
A5:    0 < b and
A6:    for a st a in X holds a >= r+b;
      reconsider b as Real by XREAL_0:def 1;
      per cases;
      suppose r + b > s;
      then (r+s)/2 in X & (r+s)/2 < r+b by A4,XREAL_1:2,RCOMP_2:3;
      hence thesis by A6;
      end;
      suppose
A7:    r + b <= s;
A8:  r < r + b by A5,XREAL_1:31;
      then
A9:  r < (r+(r+b))/2 by TOPREAL3:3;
A10:  (r+(r+b))/2 < r+b by A8,TOPREAL3:3;
      then (r+(r+b))/2 < s by A7,XREAL_1:2;
      then (r+(r+b))/2 in X by A9,RCOMP_2:3;
      hence thesis by A6,A10;
    end;
  end;
  hence thesis by A2,A3,SEQ_4:def 5;
end;

theorem Th5:
  r < s implies sup [.r,s.[ = s
proof 
  set X = [.r,s.[;
  assume
A1: r < s;
  then
A2: X is non empty by RCOMP_2:3;
A3: for a st a in X holds a <= s by RCOMP_2:3;
  reconsider r, s as Real by XREAL_0:def 1;
A4:r < (r+s)/2 & (r+s)/2 < s by A1,TOPREAL3:3;
  for b st 0 < b ex a st a in X & s-b < a
  proof
    let b such that
A5:    0 < b and
A6:    for a st a in X holds a <= s-b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose s - b <= r;
      then (r+s)/2 in X & (r+s)/2 > s-b by A4,XREAL_1:2,RCOMP_2:3;
      hence thesis by A6;
    end;
    suppose
A7:    s - b > r;
A8:  s - b < s - 0 by A5,XREAL_1:17;
      then s-b < (s+(s-b))/2 by TOPREAL3:3;
      then
A9:  r < (s+(s-b))/2 by A7,XREAL_1:2;
A10:  (s+(s-b))/2 > s-b by A8,TOPREAL3:3;
      (s+(s-b))/2 < s by A8,TOPREAL3:3;
      then (s+(s-b))/2 in X by A9,RCOMP_2:3;
      hence thesis by A6,A10;
    end;
  end;
  hence thesis by A2,A3,SEQ_4:def 4;
end;

theorem Th6:
  r < s implies inf ].r,s.] = r
proof 
  set X = ].r,s.];
  assume
A1: r < s;
  then
A2: X is non empty by RCOMP_2:4;
A3: for a st a in X holds r <= a by RCOMP_2:4;
  reconsider r, s as Real by XREAL_0:def 1;
A4: r < (r+s)/2 & (r+s)/2 < s by A1,TOPREAL3:3;
  for b st 0 < b ex a st a in X & a < r+b
  proof
    let b such that
A5:  0 < b and
A6:  for a st a in X holds a >= r+b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose r + b > s;
    then (r+s)/2 in X & (r+s)/2 < r+b by A4,XREAL_1:2,RCOMP_2:4;
    hence thesis by A6;
  end;
  suppose
A7:  r + b <= s;
A8:  r < r + b by A5,XREAL_1:31;
    then
A9:  r < (r+(r+b))/2 by TOPREAL3:3;
A10:  (r+(r+b))/2 < r+b by A8,TOPREAL3:3;
    then (r+(r+b))/2 < s by A7,XREAL_1:2;
    then (r+(r+b))/2 in X by A9,RCOMP_2:4;
    hence thesis by A6,A10;
  end;
end;
hence thesis by A2,A3,SEQ_4:def 5;
end;

theorem Th7:
  r < s implies sup ].r,s.] = s
proof 
  set X = ].r,s.];
  assume
A1: r < s;
  then
A2: X is non empty by RCOMP_2:4;
A3: for a st a in X holds a <= s by RCOMP_2:4;
  reconsider r, s as Real by XREAL_0:def 1;
A4:r < (r+s)/2 & (r+s)/2 < s by A1,TOPREAL3:3;
  for b st 0 < b ex a st a in X & s-b < a
  proof
    let b such that
A5:    0 < b and
A6:    for a st a in X holds a <= s-b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose s - b <= r;
      then (r+s)/2 in X & (r+s)/2 > s-b by A4,XREAL_1:2,RCOMP_2:4;
      hence thesis by A6;
    end;
    suppose
A7:    s - b > r;
A8:  s - b < s - 0 by A5,XREAL_1:17;
      then s-b < (s+(s-b))/2 by TOPREAL3:3;
      then
A9:  r < (s+(s-b))/2 by A7,XREAL_1:2;
A10:  (s+(s-b))/2 > s-b by A8,TOPREAL3:3;
      (s+(s-b))/2 < s by A8,TOPREAL3:3;
      then (s+(s-b))/2 in X by A9,RCOMP_2:4;
      hence thesis by A6,A10;
    end;
  end;
  hence thesis by A2,A3,SEQ_4:def 4;
end;

theorem
  (a <= b or r <= s) & [.a,b.] = [.r,s.] implies a = r & b = s
proof 
  assume that
A1: a <= b or r <= s and
A2: [.a,b.] = [.r,s.];
  [.a,b.] is non empty or [.r,s.] is non empty by A1,TOPREAL5:1;
  then a <= b & r <= s by A2,RCOMP_1:13;
  then inf [.a,b.] = a & inf [.r,s.] = r &
  sup [.a,b.] = b & sup [.r,s.] = s by JORDAN5A:20;
  hence thesis by A2;
end;

theorem
  (a < b or r < s) & ].a,b.[ = ].r,s.[ implies a = r & b = s
proof 
  assume that
A1: a < b or r < s and
A2: ].a,b.[ = ].r,s.[;
  ].a,b.[ is non empty or ].r,s.[ is non empty by A1,RCOMP_1:15;
  then a < b & r < s by A2,RCOMP_1:12;
  then inf ].a,b.[ = a & inf ].r,s.[ = r &
  sup ].a,b.[ = b & sup ].r,s.[ = s by TOPREAL6:22;
  hence thesis by A2;
end;

theorem
  (a < b or r < s) & ].a,b.] = ].r,s.] implies a = r & b = s
proof 
  assume that
A1: a < b or r < s and
A2: ].a,b.] = ].r,s.];
  ].a,b.] is non empty or ].r,s.] is non empty by A1,RCOMP_2:4;
  then a < b & r < s by A2,RCOMP_2:10;
  then inf ].a,b.] = a & inf ].r,s.] = r &
  sup ].a,b.] = b & sup ].r,s.] = s by Th6,Th7;
  hence thesis by A2;
end;

theorem
  (a < b or r < s) & [.a,b.[ = [.r,s.[ implies a = r & b = s
proof 
  assume that
A1: a < b or r < s and
A2: [.a,b.[ = [.r,s.[;
  [.a,b.[ is non empty or [.r,s.[ is non empty by A1,RCOMP_2:3;
  then a < b & r < s by A2,RCOMP_2:9;
  then inf [.a,b.[ = a & inf [.r,s.[ = r &
  sup [.a,b.[ = b & sup [.r,s.[ = s by Th4,Th5;
  hence thesis by A2;
end;

theorem Th12:
  a < b & [.a,b.[ c= [.r,s.] implies r <= a & b <= s
proof 
  assume that
A1: a < b and
A2: [.a,b.[ c= [.r,s.];
A3: a in [.a,b.[ by A1,RCOMP_2:3;
  hence r <= a by A2,TOPREAL5:1;
  sup [.a,b.[ in [.r,s.] by A2,A3,Th2;
  then b in [.r,s.] by A1,Th5;
  hence b <= s by TOPREAL5:1;
end;

theorem
  a < b & [.a,b.[ c= [.r,s.[ implies r <= a & b <= s
proof 
  assume that
A1: a < b and
A2: [.a,b.[ c= [.r,s.[;
  [.r,s.[ c= [.r,s.] by TOPREALA:11;
  then [.a,b.[ c= [.r,s.] by A2,XBOOLE_1:1;
  hence thesis by A1,Th12;
end;

theorem Th14:
  a < b & ].a,b.] c= [.r,s.] implies r <= a & b <= s
proof 
  assume that
A1: a < b and
A2: ].a,b.] c= [.r,s.];
A3: b in ].a,b.] by A1,RCOMP_2:4;
  inf ].a,b.] in [.r,s.] by A2,A3,Th1;
  then a in [.r,s.] by A1,Th6;
  hence r <= a by TOPREAL5:1;
  thus b <= s by A2,A3,TOPREAL5:1;
end;

theorem
  a < b & ].a,b.] c= ].r,s.] implies r <= a & b <= s
proof 
  assume that
A1: a < b and
A2: ].a,b.] c= ].r,s.];
  ].r,s.] c= [.r,s.] by TOPREALA:15;
  then ].a,b.] c= [.r,s.] by A2,XBOOLE_1:1;
  hence thesis by A1,Th14;
end;

begin :: Halflines

theorem Th16:
  [.a,b.]` = left_open_halfline(a) \/ right_open_halfline(b)
  proof 
    set A = left_open_halfline(a);
    set B = right_open_halfline(b);
    thus [.a,b.]` c= A \/ B
    proof
      let x be set;
      assume
A1:   x in [.a,b.]`;
      then reconsider x as Real;
      not x in [.a,b.] by A1,SUBSET_1:54;
      then x < a or x > b by TOPREAL5:1;
      then x in A or x in B by BORSUK_5:14,17;
      hence thesis by XBOOLE_0:def 2;
    end;
    let x be set;
    assume
A2: x in A \/ B;
    then reconsider x as Real;
    x in A or x in B by A2,XBOOLE_0:def 2;
    then x < a or x > b by BORSUK_5:14,17;
    then not x in [.a,b.] by TOPREAL5:1;
    hence thesis by SUBSET_1:50;
  end;

theorem Th17:
  ].a,b.[` = left_closed_halfline(a) \/ right_closed_halfline(b)
  proof 
    set A = left_closed_halfline(a);
    set B = right_closed_halfline(b);
    thus ].a,b.[` c= A \/ B
    proof
      let x be set;
      assume
A1:   x in ].a,b.[`;
      then reconsider x as Real;
      not x in ].a,b.[ by A1,SUBSET_1:54;
      then x <= a or x >= b by JORDAN6:45;
      then x in A or x in B by BORSUK_5:16,15;
      hence thesis by XBOOLE_0:def 2;
    end;
    let x be set;
    assume
A2: x in A \/ B;
    then reconsider x as Real;
    x in A or x in B by A2,XBOOLE_0:def 2;
    then x <= a or x >= b by BORSUK_5:16,15;
    then not x in ].a,b.[ by JORDAN6:45;
    hence thesis by SUBSET_1:50;
  end;

theorem Th18:
  [.a,b.[` = left_open_halfline(a) \/ right_closed_halfline(b)
  proof 
    set A = left_open_halfline(a);
    set B = right_closed_halfline(b);
    thus [.a,b.[` c= A \/ B
    proof
      let x be set;
      assume
A1:   x in [.a,b.[`;
      then reconsider x as Real;
      not x in [.a,b.[ by A1,SUBSET_1:54;
      then x < a or x >= b by RCOMP_2:3;
      then x in A or x in B by BORSUK_5:15,17;
      hence thesis by XBOOLE_0:def 2;
    end;
    let x be set;
    assume
A2: x in A \/ B;
    then reconsider x as Real;
    x in A or x in B by A2,XBOOLE_0:def 2;
    then x < a or x >= b by BORSUK_5:15,17;
    then not x in [.a,b.[ by RCOMP_2:3;
    hence thesis by SUBSET_1:50;
  end;

theorem Th19:
  ].a,b.]` = left_closed_halfline(a) \/ right_open_halfline(b)
  proof 
    set A = left_closed_halfline(a);
    set B = right_open_halfline(b);
    thus ].a,b.]` c= A \/ B
    proof
      let x be set;
      assume
A1:   x in ].a,b.]`;
      then reconsider x as Real;
      not x in ].a,b.] by A1,SUBSET_1:54;
      then x <= a or x > b by RCOMP_2:4;
      then x in A or x in B by BORSUK_5:14,16;
      hence thesis by XBOOLE_0:def 2;
    end;
    let x be set;
    assume
A2: x in A \/ B;
    then reconsider x as Real;
    x in A or x in B by A2,XBOOLE_0:def 2;
    then x <= a or x > b by BORSUK_5:14,16;
    then not x in ].a,b.] by RCOMP_2:4;
    hence thesis by SUBSET_1:50;
  end;

theorem Th20:
  a <= b implies
  [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
  proof
    set A = left_closed_halfline(a);
    set B = right_closed_halfline(b);
    assume
A1: a <= b;
    thus [.a,b.] /\ (A \/ B) c= {a,b}
    proof
      let x be set;
      assume
A2:   x in [.a,b.] /\ (A \/ B);
      then reconsider x as Real;
      x in A \/ B by A2,XBOOLE_0:def 3;
      then x in A or x in B by XBOOLE_0:def 2;
      then
A3:   x <= a or x >= b by BORSUK_5:15,16;
      x in [.a,b.] by A2,XBOOLE_0:def 3;
      then a <= x & x <= b by TOPREAL5:1;
      then x = a or x = b by A3,XREAL_1:1;
      hence thesis by TARSKI:def 2;
    end;
    let x be set;
    assume x in {a,b};
    then
A4: x = a or x = b by TARSKI:def 2;
A5: a in [.a,b.] & b in [.a,b.] by A1,RCOMP_1:15;
    a in A & b in B by BORSUK_5:15,16;
    then a in A \/ B & b in A \/ B by XBOOLE_0:def 2;
    hence thesis by A4,A5,XBOOLE_0:def 3;
  end;

Lm11:
now
  let a;
  assume left_open_halfline(a) is bounded_below;
  then consider b such that
A1: for r st r in left_open_halfline(a) holds b <= r by SEQ_4:def 2;
A2: a-1 < a-0 by XREAL_1:17;
  then a-1 in left_open_halfline(a) by BORSUK_5:17;
  then b <= a-1 by A1;
  then b-1 < a-1-0 by XREAL_1:17;
  then b-1 < a by A2,XREAL_1:2;
  then b-1 in left_open_halfline(a) by BORSUK_5:17;
  then b-0 <= b-1 by A1;
  hence contradiction by XREAL_1:17;
end;

Lm12:
now
  let a;
  assume right_open_halfline(a) is bounded_above;
  then consider b such that
A1: for r st r in right_open_halfline(a) holds r <= b by SEQ_4:def 1;
A2: a+0 < a+1 by XREAL_1:8;
  then a+1 in right_open_halfline(a) by BORSUK_5:14;
  then a+1 <= b by A1;
  then a+1+0 <= b+1 by XREAL_1:9;
  then a < b+1 by A2,XREAL_1:2;
  then b+1 in right_open_halfline(a) by BORSUK_5:14;
  then b+1 <= b+0 by A1;
  hence contradiction by XREAL_1:8;
end;

registration
  let a;
  cluster left_closed_halfline(a) -> non bounded_below bounded_above connected;
coherence
proof 
  set A = left_closed_halfline(a);
A1: left_open_halfline(a) c= A by LIMFUNC1:15;
  not left_open_halfline(a) is bounded_below by Lm11;
  hence A is non bounded_below by A1,RCOMP_1:3;
  thus A is bounded_above
  proof
    take a;
    thus thesis by BORSUK_5:16;
  end;
  let r, s;
  assume r in A;
  assume s in A;
  then
A2: s <= a by BORSUK_5:16;
  let x be set;
  assume
A3: x in [.r,s.];
  then reconsider x as Real;
  x <= s by A3,TOPREAL5:1;
  then x <= a by A2,XREAL_1:2;
  hence thesis by BORSUK_5:16;
end;
  cluster left_open_halfline(a) -> non bounded_below bounded_above connected;
coherence
proof 
  set A = left_open_halfline(a);
  thus A is non bounded_below by Lm11;
  thus A is bounded_above
  proof
    take a;
    thus thesis by BORSUK_5:17;
  end;
  let r, s;
  assume r in A;
  assume s in A;
  then
A4: s < a by BORSUK_5:17;
  let x be set;
  assume
A5: x in [.r,s.];
  then reconsider x as Real;
  x <= s by A5,TOPREAL5:1;
  then x < a by A4,XREAL_1:2;
  hence thesis by BORSUK_5:17;
end;
  cluster right_closed_halfline(a) ->
          bounded_below non bounded_above connected;
coherence
proof 
  set A = right_closed_halfline(a);
  thus A is bounded_below
  proof
    take a;
    thus thesis by BORSUK_5:15;
  end;
A6: right_open_halfline(a) c= A by LIMFUNC1:10;
  not right_open_halfline(a) is bounded_above by Lm12;
  hence A is non bounded_above by A6,RCOMP_1:4;
  let r, s;
  assume r in A;
  then
A7: a <= r by BORSUK_5:15;
  assume s in A;
  let x be set;
  assume
A8: x in [.r,s.];
  then reconsider x as Real;
  r <= x by A8,TOPREAL5:1;
  then a <= x by A7,XREAL_1:2;
  hence thesis by BORSUK_5:15;
end;
  cluster right_open_halfline(a) -> bounded_below non bounded_above connected;
coherence
proof 
  set A = right_open_halfline(a);
  thus A is bounded_below
  proof
    take a;
    thus thesis by BORSUK_5:14;
  end;
  thus A is non bounded_above by Lm12;
  let r, s;
  assume r in A;
  then
A9: a < r by BORSUK_5:14;
  assume s in A;
  let x be set;
  assume
A10: x in [.r,s.];
  then reconsider x as Real;
  r <= x by A10,TOPREAL5:1;
  then a < x by A9,XREAL_1:2;
  hence thesis by BORSUK_5:14;
end;
end;

theorem Th21:
  sup left_closed_halfline(a) = a
proof 
  set X = left_closed_halfline(a);
A1: for r st r in X holds r <= a by BORSUK_5:16;
  for s st 0 < s ex r st r in X & a-s < r
  proof
    let s such that
A2:   0 < s;
    take a;
    thus a in X by BORSUK_5:16;
    a-s < a-0 by A2,XREAL_1:17;
    hence thesis;
  end;
  hence thesis by A1,SEQ_4:def 4;
end;

theorem Th22:
  sup left_open_halfline(a) = a
proof 
  set X = left_open_halfline(a);
A1: for r st r in X holds r <= a by BORSUK_5:17;
  for s st 0 < s ex r st r in X & a-s < r
  proof
    let s;
    assume 0 < s;
    then a-s < a-0 by XREAL_1:17;
    then
A2: a-s < (a-s+a)/2 & (a-s+a)/2 < a by TOPREAL3:3;
    take (a-s+a)/2;
    thus thesis by A2,BORSUK_5:17;
  end;
  hence thesis by A1,SEQ_4:def 4;
end;

theorem Th23:
  inf right_closed_halfline(a) = a
proof 
  set X = right_closed_halfline(a);
A1: for r st r in X holds a <= r by BORSUK_5:15;
  for s st 0 < s ex r st r in X & r < a+s
  proof
    let s such that
A2:   0 < s;
    take a;
    thus a in X by BORSUK_5:15;
    a+0 < a+s by A2,XREAL_1:8;
    hence thesis;
  end;
  hence thesis by A1,SEQ_4:def 5;
end;

theorem Th24:
  inf right_open_halfline(a) = a
proof 
  set X = right_open_halfline(a);
A1: for r st r in X holds a <= r by BORSUK_5:14;
  for s st 0 < s ex r st r in X & r < a+s
  proof
    let s;
    assume 0 < s;
    then a+0 < a+s by XREAL_1:8;
    then
A2: a < (a+(a+s))/2 & (a+(a+s))/2 < a+s by TOPREAL3:3;
    take (a+a+s)/2;
    thus thesis by A2,BORSUK_5:14;
  end;
  hence thesis by A1,SEQ_4:def 5;
end;

begin :: Connectedness

registration
  cluster [#]REAL -> connected non bounded_below non bounded_above;
coherence
proof 
  thus [#]REAL is connected
  proof
    let r, s be real number;
    [.r,s.] c= REAL;
    hence thesis by SUBSET_1:def 4;
  end;
  right_open_halfline(0) c= [#]REAL & left_open_halfline(0) c= [#]REAL by Lm8;
  hence thesis by RCOMP_1:3,4;
end;
end;

theorem Th25:
  for X being bounded connected Subset of REAL st
  inf X in X & sup X in X holds X = [.inf X,sup X.]
  proof 
    let X be bounded connected Subset of REAL such that
A1: inf X in X & sup X in X;
    thus X c= [.inf X,sup X.] by TOPREAL6:24;
    thus thesis by A1,JCT_MISC:def 3;
  end;

theorem Th26:
  for X being bounded Subset of REAL st not inf X in X holds
  X c= ].inf X,sup X.]
proof 
  let X be bounded Subset of REAL such that
A1: not inf X in X;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
A3: x <= sup X by A2,SEQ_4:def 4;
  inf X <= x by A2,SEQ_4:def 5;
  then inf X < x by A1,A2,REAL_1:def 5;
  hence thesis by A3,RCOMP_2:4;
end;

theorem Th27:
  for X being bounded connected Subset of REAL st
  not inf X in X & sup X in X holds X = ].inf X,sup X.]
proof 
  let X be bounded connected Subset of REAL such that
A1: not inf X in X and
A2: sup X in X;
  thus X c= ].inf X,sup X.] by A1,Th26;
  let x be set;
  assume
A3: x in ].inf X,sup X.];
    then reconsider x as Real;
A4: x <= sup X by A3,RCOMP_2:4;
    inf X < x by A3,RCOMP_2:4;
    then inf X - inf X < x - inf X by XREAL_1:16;
    then consider r such that
A5: r in X and
A6: r < inf X + (x - inf X) by A2,SEQ_4:def 5;
A7: [.r,sup X.] c= X by A5,A2,JCT_MISC:def 3;
    x in [.r,sup X.] by A4,A6,TOPREAL5:1;
    hence thesis by A7;
end;

theorem Th28:
  for X being bounded Subset of REAL st not sup X in X holds
  X c= [.inf X,sup X.[
proof 
  let X be bounded Subset of REAL such that
A1: not sup X in X;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
A3: inf X <= x by A2,SEQ_4:def 5;
  x <= sup X by A2,SEQ_4:def 4;
  then x < sup X by A1,A2,REAL_1:def 5;
  hence thesis by A3,RCOMP_2:3;
end;

theorem Th29:
  for X being bounded connected Subset of REAL st
  inf X in X & not sup X in X holds X = [.inf X,sup X.[
proof 
  let X be bounded connected Subset of REAL such that
A1: inf X in X and
A2: not sup X in X;
  thus X c= [.inf X,sup X.[ by A2,Th28;
  let x be set;
  assume
A3: x in [.inf X,sup X.[;
    then reconsider x as Real;
A4: inf X <= x by A3,RCOMP_2:3;
    x < sup X by A3,RCOMP_2:3;
    then x - x < sup X - x by XREAL_1:16;
    then consider r such that
A5: r in X and
A6: sup X - (sup X - x) < r by A1,SEQ_4:def 4;
A7: [.inf X,r.] c= X by A5,A1,JCT_MISC:def 3;
    x in [.inf X,r.] by A4,A6,TOPREAL5:1;
    hence thesis by A7;
end;

theorem Th30:
  for X being bounded Subset of REAL st not inf X in X & not sup X in X
  holds X c= ].inf X,sup X.[
proof 
  let X be bounded Subset of REAL such that
A1: not inf X in X and
A2: not sup X in X;
  let x be set;
  assume
A3: x in X;
  then reconsider x as Real;
  inf X <= x & x <= sup X by A3,SEQ_4:def 4,def 5;
  then inf X < x & x < sup X by A1,A2,A3,REAL_1:def 5;
  hence thesis by JORDAN6:45;
end;

theorem Th31:
  for X being non empty bounded connected Subset of REAL st
  not inf X in X & not sup X in X holds X = ].inf X,sup X.[
proof 
  let X be non empty bounded connected Subset of REAL such that
A1: not inf X in X and
A2: not sup X in X;
  thus X c= ].inf X,sup X.[ by A2,A1,Th30;
  let x be set;
  assume
A3: x in ].inf X,sup X.[;
    then reconsider x as Real;
    inf X < x by A3,JORDAN6:45;
    then inf X - inf X < x - inf X by XREAL_1:16;
    then consider s such that
A4: s in X and
A5: s < inf X + (x - inf X) by SEQ_4:def 5;
    x < sup X by A3,JORDAN6:45;
    then x - x < sup X - x by XREAL_1:16;
    then consider r such that
A6: r in X and
A7: sup X - (sup X - x) < r by SEQ_4:def 4;
A8: [.s,r.] c= X by A6,A4,JCT_MISC:def 3;
    x in [.s,r.] by A7,A5,TOPREAL5:1;
    hence thesis by A8;
end;

theorem Th32:
  for X being Subset of REAL st X is bounded_above holds
  X c= left_closed_halfline(sup X)
  proof 
    let X be Subset of REAL such that
A1: X is bounded_above;
    let x be set;
    assume
A2: x in X;
    then reconsider x as Real;
    x <= sup X by A2,A1,SEQ_4:def 4;
    hence thesis by BORSUK_5:16;
  end;

theorem Th33:
  for X being connected Subset of REAL st
  X is not bounded_below & X is bounded_above & sup X in X holds
  X = left_closed_halfline(sup X)
proof 
  let X be connected Subset of REAL such that
A1: X is not bounded_below and
A2: X is bounded_above and
A3: sup X in X;
  thus X c= left_closed_halfline(sup X) by A2,Th32;
  let x be set;
  assume
A4: x in left_closed_halfline(sup X);
  then reconsider x as Real;
  consider r such that
A5: r in X and
A6: x > r by A1,SEQ_4:def 2;
A7: x <= sup X by A4,BORSUK_5:16;
A8: [.r,sup X.] c= X by A5,A3,JCT_MISC:def 3;
  x in [.r,sup X.] by A7,A6,TOPREAL5:1;
  hence thesis by A8;
end;

theorem Th34:
  for X being Subset of REAL st X is bounded_above & not sup X in X holds
  X c= left_open_halfline(sup X)
  proof 
    let X be Subset of REAL such that
A1: X is bounded_above and
A2: not sup X in X;
    let x be set;
    assume
A3: x in X;
    then reconsider x as Real;
    x <= sup X by A3,A1,SEQ_4:def 4;
    then x < sup X by A2,A3,REAL_1:def 5;
    hence thesis by BORSUK_5:17;
  end;

theorem Th35:
  for X being non empty connected Subset of REAL st
  X is not bounded_below & X is bounded_above & not sup X in X holds
  X = left_open_halfline(sup X)
proof 
  let X be non empty connected Subset of REAL such that
A1: X is not bounded_below and
A2: X is bounded_above and
A3: not sup X in X;
  thus X c= left_open_halfline(sup X) by A2,A3,Th34;
  let x be set;
  assume
A4: x in left_open_halfline(sup X);
  then reconsider x as Real;
  consider r such that
A5: r in X and
A6: x > r by A1,SEQ_4:def 2;
    x < sup X by A4,BORSUK_5:17;
    then x - x < sup X - x by XREAL_1:16;
    then consider s such that
A7: s in X and
A8: sup X - (sup X - x) < s by A2,SEQ_4:def 4;
A9: [.r,s.] c= X by A5,A7,JCT_MISC:def 3;
  x in [.r,s.] by A8,A6,TOPREAL5:1;
  hence thesis by A9;
end;

theorem Th36:
  for X being Subset of REAL st X is bounded_below holds
  X c= right_closed_halfline(inf X)
  proof 
    let X be Subset of REAL such that
A1: X is bounded_below;
    let x be set;
    assume
A2: x in X;
    then reconsider x as Real;
    inf X <= x by A2,A1,SEQ_4:def 5;
    hence thesis by BORSUK_5:15;
  end;

theorem Th37:
  for X being connected Subset of REAL st
  X is bounded_below & X is not bounded_above & inf X in X holds
  X = right_closed_halfline(inf X)
proof 
  let X be connected Subset of REAL such that
A1: X is bounded_below and
A2: X is not bounded_above and
A3: inf X in X;
  thus X c= right_closed_halfline(inf X) by A1,Th36;
  let x be set;
  assume
A4: x in right_closed_halfline(inf X);
  then reconsider x as Real;
  consider r such that
A5: r in X and
A6: x < r by A2,SEQ_4:def 1;
A7: inf X <= x by A4,BORSUK_5:15;
A8: [.inf X,r.] c= X by A5,A3,JCT_MISC:def 3;
  x in [.inf X,r.] by A7,A6,TOPREAL5:1;
  hence thesis by A8;
end;

theorem Th38:
  for X being Subset of REAL st X is bounded_below & not inf X in X holds
  X c= right_open_halfline(inf X)
  proof 
    let X be Subset of REAL such that
A1: X is bounded_below and
A2: not inf X in X;
    let x be set;
    assume
A3: x in X;
    then reconsider x as Real;
    inf X <= x by A3,A1,SEQ_4:def 5;
    then inf X < x by A2,A3,REAL_1:def 5;
    hence thesis by BORSUK_5:14;
  end;

theorem Th39:
  for X being non empty connected Subset of REAL st
  X is bounded_below & X is not bounded_above & not inf X in X holds
  X = right_open_halfline(inf X)
proof 
  let X be non empty connected Subset of REAL such that
A1: X is bounded_below and
A2: X is not bounded_above and
A3: not inf X in X;
  thus X c= right_open_halfline(inf X) by A1,A3,Th38;
  let x be set;
  assume
A4: x in right_open_halfline(inf X);
  then reconsider x as Real;
  consider r such that
A5: r in X and
A6: x < r by A2,SEQ_4:def 1;
    inf X < x by A4,BORSUK_5:14;
    then inf X - inf X < x - inf X by XREAL_1:16;
    then consider s such that
A7: s in X and
A8: s < inf X + (x - inf X) by A1,SEQ_4:def 5;
A9: [.s,r.] c= X by A5,A7,JCT_MISC:def 3;
  x in [.s,r.] by A8,A6,TOPREAL5:1;
  hence thesis by A9;
end;

theorem Th40:
  for X being connected Subset of REAL st
  X is not bounded_above & X is not bounded_below
  holds X = REAL
proof 
  let X be connected Subset of REAL;
  assume that
A1: X is not bounded_above and
A2: X is not bounded_below;
  thus X c= REAL;
  let x be set;
  assume x in REAL;
  then reconsider x as Real;
  consider r such that
A3: r in X and
A4: r > x by A1,SEQ_4:def 1;
  consider s such that
A5: s in X and
A6: s < x by A2,SEQ_4:def 2;
A7: [.s,r.] c= X by A3,A5,JCT_MISC:def 3;
  x in [.s,r.] by A4,A6,TOPREAL5:1;
  hence thesis by A7;
end;

theorem Th41:
  for X being connected Subset of REAL holds X is empty or X = REAL or
  (ex a st X = left_closed_halfline(a)) or
  (ex a st X = left_open_halfline(a)) or
  (ex a st X = right_closed_halfline(a)) or
  (ex a st X = right_open_halfline(a)) or
  (ex a, b st a <= b & X = [.a,b.]) or
  (ex a, b st a < b & X = [.a,b.[) or
  (ex a, b st a < b & X = ].a,b.]) or
  (ex a, b st a < b & X = ].a,b.[)
proof 
  let X be connected Subset of REAL;
  assume X is non empty;
  then reconsider X as non empty connected Subset of REAL;
  per cases;
  suppose X is bounded;
    then reconsider X as non empty bounded connected Subset of REAL;
    per cases;
    suppose X is trivial;
      then consider x being set such that
A1:   X = {x} by REALSET1:def 4;
      x in X by A1,TARSKI:def 1;
      then reconsider x as Real;
      X = [.x,x.] by A1,RCOMP_1:14;
      hence thesis;
    end;
    suppose X is non trivial;
      then consider p, q being set such that
A2:   p in X & q in X & p <> q by REALSET1:14;
      p is Real & q is Real by A2;
      then
A3:   inf X < sup X by A2,SEQ_4:25;
      per cases;
      suppose sup X in X & inf X in X;
        then X = [.inf X,sup X.] by Th25;
        hence thesis by A3;
      end;
      suppose sup X in X & not inf X in X;
        then X = ].inf X,sup X.] by Th27;
        hence thesis by A3;
      end;
      suppose not sup X in X & inf X in X;
        then X = [.inf X,sup X.[ by Th29;
        hence thesis by A3;
      end;
      suppose not sup X in X & not inf X in X;
        then X = ].inf X,sup X.[ by Th31;
        hence thesis by A3;
      end;
    end;
  end;
  suppose
A4: X is not bounded;
    per cases by A4,SEQ_4:def 3;
    suppose
A5:   X is not bounded_below & X is bounded_above;
      per cases;
      suppose sup X in X;
        then X = left_closed_halfline(sup X) by A5,Th33;
        hence thesis;
      end;
      suppose not sup X in X;
        then X = left_open_halfline(sup X) by A5,Th35;
        hence thesis;
      end;
    end;
    suppose
A6:   X is not bounded_above & X is bounded_below;
      per cases;
      suppose inf X in X;
        then X = right_closed_halfline(inf X) by A6,Th37;
        hence thesis;
      end;
      suppose not inf X in X;
        then X = right_open_halfline(inf X) by A6,Th39;
        hence thesis;
      end;
    end;
    suppose X is not bounded_above & X is not bounded_below;
      hence thesis by Th40;
    end;
  end;
end;

theorem Th42:
  for X being non empty connected Subset of REAL st not r in X holds
  r <= inf X or sup X <= r
proof 
  let X be non empty connected Subset of REAL such that
A1: not r in X;
  per cases by Th41;
  suppose X = REAL;
    hence thesis by A1,XREAL_0:def 1;
  end;
  suppose ex a st X = left_closed_halfline(a);
    then consider a such that
A2:   X = left_closed_halfline(a);
    sup X = a by A2,Th21;
    hence thesis by A1,A2,BORSUK_5:16;
  end;
  suppose ex a st X = left_open_halfline(a);
    then consider a such that
A3:   X = left_open_halfline(a);
    sup X = a by A3,Th22;
    hence thesis by A1,A3,BORSUK_5:17;
  end;
  suppose ex a st X = right_closed_halfline(a);
    then consider a such that
A4:   X = right_closed_halfline(a);
    inf X = a by A4,Th23;
    hence thesis by A1,A4,BORSUK_5:15;
  end;
  suppose ex a st X = right_open_halfline(a);
    then consider a such that
A5:   X = right_open_halfline(a);
    inf X = a by A5,Th24;
    hence thesis by A1,A5,BORSUK_5:14;
  end;
  suppose ex a, b st a <= b & X = [.a,b.];
    then consider a, b such that
A6:   a <= b and
A7:   X = [.a,b.];
    inf X = a & sup X = b by A6,A7,JORDAN5A:20;
    hence thesis by A1,A7,TOPREAL5:1;
  end;
  suppose ex a, b st a < b & X = [.a,b.[;
    then consider a, b such that
A8:   a < b and
A9:   X = [.a,b.[;
    inf X = a & sup X = b by A8,A9,Th4,Th5;
    hence thesis by A1,A9,RCOMP_2:3;
  end;
  suppose ex a, b st a < b & X = ].a,b.];
    then consider a, b such that
A10:   a < b and
A11:   X = ].a,b.];
    inf X = a & sup X = b by A10,A11,Th6,Th7;
    hence thesis by A1,A11,RCOMP_2:4;
  end;
  suppose ex a, b st a < b & X = ].a,b.[;
    then consider a, b such that
A12:   a < b and
A13:   X = ].a,b.[;
    inf X = a & sup X = b by A12,A13,TOPREAL6:22;
    hence thesis by A1,A13,JORDAN6:45;
  end;
end;

theorem Th43:
  for X, Y being non empty bounded connected Subset of REAL
  st inf X <= inf Y & sup Y <= sup X &
  (inf X = inf Y & inf Y in Y implies inf X in X) &
  (sup X = sup Y & sup Y in Y implies sup X in X)
  holds Y c= X
proof 
  let X, Y be non empty bounded connected Subset of REAL such that
A1: inf X <= inf Y and
A2: sup Y <= sup X and
A3: inf X = inf Y & inf Y in Y implies inf X in X and
A4: sup X = sup Y & sup Y in Y implies sup X in X;
  let x be set;
  assume
A5:  x in Y;
  then reconsider x as Real;
  Y c= [.inf Y,sup Y.] by TOPREAL6:24;
  then
A6: inf Y <= x & x <= sup Y by A5,TOPREAL5:1;
  then
A7: inf X <= x & x <= sup X by A1,A2,XREAL_1:2;
  per cases by Th41;
  suppose X = REAL;
    hence thesis by Lm8;
  end;
  suppose (ex a st X = left_closed_halfline(a)) or
    (ex a st X = left_open_halfline(a)) or
    (ex a st X = right_closed_halfline(a)) or
    ex a st X = right_open_halfline(a);
    hence thesis;
  end;
  suppose ex a, b st a <= b & X = [.a,b.];
    then consider a, b such that
A8: a <= b & X = [.a,b.];
    inf X = a & sup X = b by A8,JORDAN5A:20;
    hence thesis by A8,A7,TOPREAL5:1;
  end;
  suppose ex a, b st a < b & X = [.a,b.[;
    then consider a, b such that
A9: a < b & X = [.a,b.[;
A10: inf X = a & sup X = b by A9,Th4,Th5;
    per cases by Th41;
    suppose Y = REAL;
      hence thesis by Lm8;
    end;
    suppose (ex a st Y = left_closed_halfline(a)) or
      (ex a st Y = left_open_halfline(a)) or
      (ex a st Y = right_closed_halfline(a)) or
      ex a st Y = right_open_halfline(a);
      hence thesis;
    end;
    suppose ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A11:   a1 <= b1 & Y = [.a1,b1.];
A12:   inf Y = a1 & sup Y = b1 by A11,JORDAN5A:20;
      then b1 < b by A2,A4,A10,A9,A11,TOPREAL5:1,RCOMP_2:3,REAL_1:def 5;
      then x < b by A6,A12,XREAL_1:2;
      hence thesis by A9,A7,A10,RCOMP_2:3;
    end;
    suppose ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A13:   a1 < b1 & Y = [.a1,b1.[;
A14:   inf Y = a1 & sup Y = b1 by A13,Th4,Th5;
      x < b1 by A5,A13,RCOMP_2:3;
      then x < b by A14,A2,A10,XREAL_1:2;
      hence thesis by A9,A7,A10,RCOMP_2:3;
    end;
    suppose ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A15:   a1 < b1 & Y = ].a1,b1.];
A16:   inf Y = a1 & sup Y = b1 by A15,Th6,Th7;
      then b1 < b by A2,A4,A10,A9,A15,RCOMP_2:3,4,REAL_1:def 5;
      then x < b by A6,A16,XREAL_1:2;
      hence thesis by A9,A7,A10,RCOMP_2:3;
    end;
    suppose ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A17:   a1 < b1 & Y = ].a1,b1.[;
A18:   inf Y = a1 & sup Y = b1 by A17,TOPREAL6:22;
      x < b1 by A5,A17,JORDAN6:45;
      then x < b by A18,A2,A10,XREAL_1:2;
      hence thesis by A9,A7,A10,RCOMP_2:3;
    end;
  end;
  suppose ex a, b st a < b & X = ].a,b.];
    then consider a, b such that
A19: a < b & X = ].a,b.];
A20: inf X = a & sup X = b by A19,Th6,Th7;
    per cases by Th41;
    suppose Y = REAL;
      hence thesis by Lm8;
    end;
    suppose (ex a st Y = left_closed_halfline(a)) or
      (ex a st Y = left_open_halfline(a)) or
      (ex a st Y = right_closed_halfline(a)) or
      ex a st Y = right_open_halfline(a);
      hence thesis;
    end;
    suppose ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A21:   a1 <= b1 & Y = [.a1,b1.];
A22:   inf Y = a1 & sup Y = b1 by A21,JORDAN5A:20;
      then a < a1 by A1,A3,A20,A19,A21,TOPREAL5:1,RCOMP_2:4,REAL_1:def 5;
      then a < x by A6,A22,XREAL_1:2;
      hence thesis by A19,A7,A20,RCOMP_2:4;
    end;
    suppose ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A23:   a1 < b1 & Y = [.a1,b1.[;
      inf Y = a1 & sup Y = b1 by A23,Th4,Th5;
      then
A24:   a < a1 by A1,A3,A20,A19,A23,RCOMP_2:3,4,REAL_1:def 5;
      a1 <= x by A5,A23,RCOMP_2:3;
      then a < x by A24,XREAL_1:2;
      hence thesis by A19,A7,A20,RCOMP_2:4;
    end;
    suppose ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A25:   a1 < b1 & Y = ].a1,b1.];
A26:   inf Y = a1 & sup Y = b1 by A25,Th6,Th7;
      a1 < x by A5,A25,RCOMP_2:4;
      then a < x by A26,A1,A20,XREAL_1:2;
      hence thesis by A19,A7,A20,RCOMP_2:4;
    end;
    suppose ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A27:   a1 < b1 & Y = ].a1,b1.[;
A28:   inf Y = a1 & sup Y = b1 by A27,TOPREAL6:22;
      a1 < x by A5,A27,JORDAN6:45;
      then a < x by A28,A1,A20,XREAL_1:2;
      hence thesis by A19,A7,A20,RCOMP_2:4;
    end;
  end;
  suppose ex a, b st a < b & X = ].a,b.[;
    then consider a, b such that
A29: a < b & X = ].a,b.[;
A30: inf X = a & sup X = b by A29,TOPREAL6:22;
    per cases by Th41;
    suppose Y = REAL;
      hence thesis by Lm8;
    end;
    suppose (ex a st Y = left_closed_halfline(a)) or
      (ex a st Y = left_open_halfline(a)) or
      (ex a st Y = right_closed_halfline(a)) or
      ex a st Y = right_open_halfline(a);
      hence thesis;
    end;
    suppose ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A31:   a1 <= b1 & Y = [.a1,b1.];
A32:   inf Y = a1 & sup Y = b1 by A31,JORDAN5A:20;
A33:   x <= b1 by A5,A31,TOPREAL5:1;
      b1 < b by A2,A4,A30,A29,A31,A32,REAL_1:def 5,TOPREAL5:1,JORDAN6:45;
      then
A34:   x < b by A33,XREAL_1:2;
      a < a1 by A32,A1,A3,A30,A29,A31,TOPREAL5:1,REAL_1:def 5,JORDAN6:45;
      then a < x by A6,A32,XREAL_1:2;
      hence thesis by A29,A34,JORDAN6:45;
    end;
    suppose ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A35:   a1 < b1 & Y = [.a1,b1.[;
A36:   inf Y = a1 & sup Y = b1 by A35,Th4,Th5;
      x < b1 by A5,A35,RCOMP_2:3;
      then
A37:   x < b by A30,A2,A36,XREAL_1:2;
A38:   a < a1 by A1,A3,A30,A29,A35,A36,RCOMP_2:3,REAL_1:def 5,JORDAN6:45;
      a1 <= x by A5,A35,RCOMP_2:3;
      then a < x by A38,XREAL_1:2;
      hence thesis by A29,A37,JORDAN6:45;
    end;
    suppose ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A39:   a1 < b1 & Y = ].a1,b1.];
A40:   inf Y = a1 & sup Y = b1 by A39,Th6,Th7;
A41:   x <= b1 by A5,A39,RCOMP_2:4;
      b1 < b by A2,A4,A30,A29,A39,A40,RCOMP_2:4,REAL_1:def 5,JORDAN6:45;
      then
A42:   x < b by A41,XREAL_1:2;
      a1 < x by A5,A39,RCOMP_2:4;
      then a < x by A40,A1,A30,XREAL_1:2;
      hence thesis by A29,A42,JORDAN6:45;
    end;
    suppose ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A43:   a1 < b1 & Y = ].a1,b1.[;
A44:   inf Y = a1 & sup Y = b1 by A43,TOPREAL6:22;
      x < b1 by A5,A43,JORDAN6:45;
      then
A45:   x < b by A30,A2,A44,XREAL_1:2;
      a1 < x by A5,A43,JORDAN6:45;
      then a < x by A44,A1,A30,XREAL_1:2;
      hence thesis by A29,A45,JORDAN6:45;
    end;
  end;
end;

registration
  cluster open closed connected non empty non bounded Subset of REAL;
existence
proof 
  take [#]REAL;
  thus thesis by SEQ_4:def 3;
end;
end;

begin :: R^1

theorem Th44:
  for X being Subset of R^1 st a <= b & X = [.a,b.] holds Fr X = {a,b}
  proof 
    let X be Subset of R^1 such that
A1: a <= b and
A2: X = [.a,b.];
A3: Cl X = Cl [.a,b.] by A2,TOPREAL6:80
    .= [.a,b.] by PSCOMP_1:34;
    set LO = R^1(left_open_halfline(a)),
    RC = R^1(right_closed_halfline(b)),
    RO = R^1(right_open_halfline(b)),
    LC = R^1(left_closed_halfline(a));
A4: LO = left_open_halfline(a) by TOPREALB:def 3;
A5: RC = right_closed_halfline(b) by TOPREALB:def 3;
A6: RO = right_open_halfline(b) by TOPREALB:def 3;
A7: LC = left_closed_halfline(a) by TOPREALB:def 3;
A8: [.a,b.]` = LO \/ RO by A4,A6,Th16;
A9: Cl X` = Cl [.a,b.]` by A2,TOPREAL6:80,TOPMETR:24
    .= Cl left_open_halfline(a) \/ Cl right_open_halfline(b)
    by A8,A4,A6,Th3
    .= Cl LO \/ Cl right_open_halfline(b) by A4,TOPREAL6:80
    .= Cl LO \/ Cl RO by A6,TOPREAL6:80
    .= LC \/ Cl RO by A4,A7,BORSUK_5:77
    .= LC \/ RC by A5,A6,BORSUK_5:75
    .= left_closed_halfline(a) \/ right_closed_halfline(b)
    by A5,TOPREALB:def 3;
    [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
    by A1,Th20;
    hence Fr X = {a,b} by A3,A9,TOPS_1:def 2;
  end;

theorem
  for X being Subset of R^1 st a < b & X = ].a,b.[ holds Fr X = {a,b}
  proof 
    let X be Subset of R^1 such that
A1: a < b and
A2: X = ].a,b.[;
A3: Cl X = Cl ].a,b.[ by A2,TOPREAL6:80
    .= [.a,b.] by A1,TOPREAL6:82;
    set RC = R^1(right_closed_halfline(b)),
    LC = R^1(left_closed_halfline(a));
A4: RC = right_closed_halfline(b) by TOPREALB:def 3;
A5: LC = left_closed_halfline(a) by TOPREALB:def 3;
A6: ].a,b.[` = LC \/ RC by A4,A5,Th17;
A7: Cl X` = Cl ].a,b.[` by A2,TOPREAL6:80,TOPMETR:24
    .= Cl left_closed_halfline(a) \/ Cl right_closed_halfline(b)
    by A6,A4,A5,Th3
    .= Cl left_closed_halfline(a) \/ right_closed_halfline(b) by PSCOMP_1:34
    .= left_closed_halfline(a) \/ right_closed_halfline(b) by PSCOMP_1:34;
    [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
    by A1,Th20;
    hence Fr X = {a,b} by A3,A7,TOPS_1:def 2;
  end;

theorem Th46:
  for X being Subset of R^1 st a < b & X = [.a,b.[ holds Fr X = {a,b}
  proof 
    let X be Subset of R^1 such that
A1: a < b and
A2: X = [.a,b.[;
A3: Cl X = [.a,b.] by A1,A2,BORSUK_5:58;
    set LO = R^1(left_open_halfline(a)),
    RC = R^1(right_closed_halfline(b)),
    LC = R^1(left_closed_halfline(a));
A4: LO = left_open_halfline(a) by TOPREALB:def 3;
A5: RC = right_closed_halfline(b) by TOPREALB:def 3;
A6: LC = left_closed_halfline(a) by TOPREALB:def 3;
A7: [.a,b.[` = LO \/ RC by A4,A5,Th18;
A8: Cl X` = Cl [.a,b.[` by A2,TOPREAL6:80,TOPMETR:24
    .= Cl left_open_halfline(a) \/ Cl right_closed_halfline(b)
    by A7,A4,A5,Th3
    .= Cl LO \/ Cl right_closed_halfline(b) by A4,TOPREAL6:80
    .= Cl LO \/ Cl RC by A5,TOPREAL6:80
    .= LC \/ Cl RC by A4,A6,BORSUK_5:77
    .= left_closed_halfline(a) \/ right_closed_halfline(b)
    by A5,A6,PRE_TOPC:52;
    [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
    by A1,Th20;
    hence Fr X = {a,b} by A3,A8,TOPS_1:def 2;
  end;

theorem Th47:
  for X being Subset of R^1 st a < b & X = ].a,b.] holds Fr X = {a,b}
  proof 
    let X be Subset of R^1 such that
A1: a < b and
A2: X = ].a,b.];
A3: Cl X = [.a,b.] by A1,A2,BORSUK_5:59;
    set RO = R^1(right_open_halfline(b)),
    LC = R^1(left_closed_halfline(a));
A4: RO = right_open_halfline(b) by TOPREALB:def 3;
A5: LC = left_closed_halfline(a) by TOPREALB:def 3;
A6: ].a,b.]` = left_closed_halfline(a) \/ right_open_halfline(b) by Th19;
A7: Cl X` = Cl ].a,b.]` by A2,TOPREAL6:80,TOPMETR:24
    .= Cl left_closed_halfline(a) \/ Cl right_open_halfline(b) by A6,Th3
    .= Cl LC \/ Cl right_open_halfline(b) by A5,TOPREAL6:80
    .= Cl LC \/ Cl RO by A4,TOPREAL6:80
    .= LC \/ Cl RO by PRE_TOPC:52
    .= left_closed_halfline(a) \/ right_closed_halfline(b)
    by A4,A5,BORSUK_5:75;
    [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
    by A1,Th20;
    hence Fr X = {a,b} by A3,A7,TOPS_1:def 2;
  end;

theorem
  for X being Subset of R^1 st X = [.a,b.] holds Int X = ].a,b.[
  proof 
    let X be Subset of R^1 such that
A1: X = [.a,b.];
    reconsider Y = ].a,b.[ as open Subset of R^1 by TOPMETR:24,BORSUK_5:62;
A2: Int X c= X by TOPS_1:44;
    thus Int X c= ].a,b.[
    proof
      let x be set;
      assume
A3:   x in Int X;
      then reconsider x as Point of R^1;
A4:   now
        assume
A5:     x = a or x = b;
A6:     Int X misses Fr X by TOPS_1:73;
        now
          assume a > b;
          then X = {}R^1 by A1,RCOMP_1:13;
          hence contradiction by A3,TOPS_1:47;
        end;
        then Fr X = {a,b} by A1,Th44;
        then a in Fr X & b in Fr X by TARSKI:def 2;
        hence contradiction by A6,A3,A5,XBOOLE_0:3;
      end;
      a <= x & x <= b by A1,A3,A2,TOPREAL5:1;
      then a < x & x < b by A4,REAL_1:def 5;
      hence thesis by JORDAN6:45;
    end;
    ].a,b.[ c= [.a,b.] by TOPREALA:19;
    then Y c= Int X by A1,TOPS_1:56;
    hence thesis;
  end;

theorem
  for X being Subset of R^1 st X = ].a,b.[ holds Int X = ].a,b.[
  proof 
    let X be Subset of R^1;
    assume
A1: X = ].a,b.[;
    then reconsider X as open Subset of R^1 by BORSUK_5:62;
    Int X = X by TOPS_1:55;
    hence thesis by A1;
  end;

theorem Th50:
  for X being Subset of R^1 st X = [.a,b.[ holds Int X = ].a,b.[
  proof 
    let X be Subset of R^1 such that
A1: X = [.a,b.[;
    reconsider Y = ].a,b.[ as open Subset of R^1 by TOPMETR:24,BORSUK_5:62;
A2: Int X c= X by TOPS_1:44;
    thus Int X c= ].a,b.[
    proof
      let x be set;
      assume
A3:   x in Int X;
      then reconsider x as Point of R^1;
A4:   now
        assume
A5:     x = a;
A6:     Int X misses Fr X by TOPS_1:73;
        now
          assume a >= b;
          then X = {}R^1 by A1,RCOMP_2:9;
          hence contradiction by A3,TOPS_1:47;
        end;
        then Fr X = {a,b} by A1,Th46;
        then a in Fr X by TARSKI:def 2;
        hence contradiction by A6,A3,A5,XBOOLE_0:3;
      end;
      a <= x & x < b by A1,A3,A2,RCOMP_2:3;
      then a < x & x < b by A4,REAL_1:def 5;
      hence thesis by JORDAN6:45;
    end;
    ].a,b.[ c= [.a,b.[ by TOPREALA:20;
    then Y c= Int X by A1,TOPS_1:56;
    hence thesis;
  end;

theorem Th51:
  for X being Subset of R^1 st X = ].a,b.] holds Int X = ].a,b.[
  proof 
    let X be Subset of R^1 such that
A1: X = ].a,b.];
    reconsider Y = ].a,b.[ as open Subset of R^1 by TOPMETR:24,BORSUK_5:62;
A2: Int X c= X by TOPS_1:44;
    thus Int X c= ].a,b.[
    proof
      let x be set;
      assume
A3:   x in Int X;
      then reconsider x as Point of R^1;
A4:   now
        assume
A5:     x = b;
A6:     Int X misses Fr X by TOPS_1:73;
        now
          assume a >= b;
          then X = {}R^1 by A1,RCOMP_2:10;
          hence contradiction by A3,TOPS_1:47;
        end;
        then Fr X = {a,b} by A1,Th47;
        then b in Fr X by TARSKI:def 2;
        hence contradiction by A6,A3,A5,XBOOLE_0:3;
      end;
      a < x & x <= b by A1,A3,A2,RCOMP_2:4;
      then a < x & x < b by A4,REAL_1:def 5;
      hence thesis by JORDAN6:45;
    end;
    ].a,b.[ c= ].a,b.] by TOPREALA:21;
    then Y c= Int X by A1,TOPS_1:56;
    hence thesis;
  end;

registration
  let X be convex Subset of R^1;
  cluster R^1 | X -> convex;
  coherence
  proof 
    thus [#](R^1|X) is convex Subset of R^1 by PRE_TOPC:def 10;
  end;
end;

registration
  let A be connected Subset of REAL;
  cluster R^1(A) -> convex;
  coherence
  proof 
    let a, b be Point of R^1;
    R^1(A) = A by TOPREALB:def 3;
    hence thesis by JCT_MISC:def 3;
end;
end;

theorem Th52:
  for X being Subset of R^1, Y being Subset of REAL st X = Y holds
  X is connected iff Y is connected
  proof 
    let X be Subset of R^1,
    Y be Subset of REAL such that
A1: X = Y;
    thus X is connected implies Y is connected
    proof
      assume
A2:   X is connected;
      let a be real number;
      thus thesis by A1,A2,BORSUK_5:111;
    end;
    assume
A3: Y is connected;
    per cases by A1,A3,Th41;
    suppose X is empty;
      then reconsider A=X as empty Subset of R^1;
      A is connected;
      hence thesis;
    end;
    suppose
A4:   X = REAL;
      R^1([#]REAL) = [#]REAL by TOPREALB:def 3
      .= REAL by SUBSET_1:def 4;
      then reconsider A=X as non empty convex Subset of R^1 by A4;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
    suppose ex a st X = left_closed_halfline(a);
      then consider a such that
A5:   X = left_closed_halfline(a);
      R^1(left_closed_halfline(a)) = left_closed_halfline(a) by TOPREALB:def 3;
      then reconsider A=X as non empty convex Subset of R^1 by A5;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
    suppose ex a st X = left_open_halfline(a);
      then consider a such that
A6:   X = left_open_halfline(a);
      R^1(left_open_halfline(a)) = left_open_halfline(a) by TOPREALB:def 3;
      then reconsider A=X as non empty convex Subset of R^1 by A6;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
    suppose ex a st X = right_closed_halfline(a);
      then consider a such that
A7:   X = right_closed_halfline(a);
      R^1(right_closed_halfline(a)) = right_closed_halfline(a)
      by TOPREALB:def 3;
      then reconsider A=X as non empty convex Subset of R^1 by A7;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
    suppose ex a st X = right_open_halfline(a);
      then consider a such that
A8:   X = right_open_halfline(a);
      R^1(right_open_halfline(a)) = right_open_halfline(a) by TOPREALB:def 3;
      then reconsider A=X as non empty convex Subset of R^1 by A8;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
    suppose ex a, b st a <= b & X = [.a,b.];
      then reconsider A=X as non empty convex Subset of R^1
      by TOPALG_2:8,RCOMP_1:15;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
    suppose ex a, b st a < b & X = [.a,b.[;
      then reconsider A=X as non empty convex Subset of R^1
      by TOPALG_2:10,RCOMP_2:3;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
    suppose ex a, b st a < b & X = ].a,b.];
      then reconsider A=X as non empty convex Subset of R^1
      by TOPALG_2:11,RCOMP_2:4;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
    suppose ex a, b st a < b & X = ].a,b.[;
      then reconsider A=X as non empty convex Subset of R^1
      by TOPALG_2:9,RCOMP_1:15;
      R^1|A is connected;
      hence R^1|X is connected;
    end;
  end;

begin :: Closed Interval TSpace

registration
  let r;
  cluster Closed-Interval-TSpace(r,r) -> trivial;
  coherence
  proof 
A1: {r} is trivial;
    the carrier of Closed-Interval-TSpace(r,r) = [.r,r.] by TOPMETR:25;
    then the carrier of Closed-Interval-TSpace(r,r) is trivial
    by A1,RCOMP_1:14;
    hence thesis by REALSET2:def 5;
  end;
end;

theorem
  r <= s implies for A being Subset of Closed-Interval-TSpace(r,s) holds
  A is bounded Subset of REAL
  proof 
    assume r <= s;
    then
A1: the carrier of Closed-Interval-TSpace(r,s) = [.r,s.] by TOPMETR:25;
    let A be Subset of Closed-Interval-TSpace(r,s);
    A is Subset of REAL by A1,XBOOLE_1:1;
    then A is bounded_above bounded_below by A1,RCOMP_1:3,4;
    hence A is bounded Subset of REAL by A1,XBOOLE_1:1,SEQ_4:def 3;
  end;

theorem Th54:
  r <= s implies for X being Subset of Closed-Interval-TSpace(r,s) st
  X = [.a,b.[ & r < a & b <= s holds Int X = ].a,b.[
  proof 
    set L = Closed-Interval-TSpace(r,s);
    assume
A1: r <= s;
    let X be Subset of Closed-Interval-TSpace(r,s) such that
A2: X = [.a,b.[ and
A3: r < a and
A4: b <= s;
    set c = (r+a)/2;
A5: r < c by A3,TOPREAL3:3;
A6: c < a by A3,TOPREAL3:3;
    set C1 = R^1(].c,b.[);
    reconsider A = X as Subset of R^1 by PRE_TOPC:39;
A7: C1 = ].c,b.[ by TOPREALB:def 3;
A8: the carrier of L = [.r,s.] by A1,TOPMETR:25;
A9: C1 c= the carrier of L
    proof
      let x be set;
      assume
A10:   x in C1;
      then reconsider x as Real by A7;
      c < x & x < b by A10,A7,JORDAN6:45;
      then r <= x & x <= s by A4,A5,XREAL_1:2;
      hence thesis by A8,TOPREAL5:1;
    end;
    A c= C1
    proof
      let x be set;
      assume
A11:   x in A;
      then reconsider x as Real by A2;
      a <= x & x < b by A11,A2,RCOMP_2:3;
      then c < x & x < b by A6,XREAL_1:2;
      hence thesis by A7,JORDAN6:45;
    end;
    then Int A = Int X by A9,TOPS_3:57;
    hence thesis by A2,Th50;
end;

theorem Th55:
  r <= s implies for X being Subset of Closed-Interval-TSpace(r,s) st
  X = ].a,b.] & r <= a & b < s holds Int X = ].a,b.[
  proof 
    set L = Closed-Interval-TSpace(r,s);
    assume
A1: r <= s;
    let X be Subset of Closed-Interval-TSpace(r,s) such that
A2: X = ].a,b.] and
A3: r <= a and
A4: b < s;
    set c = (b+s)/2;
A5: b < c by A4,TOPREAL3:3;
A6: c < s by A4,TOPREAL3:3;
    set C1 = R^1(].a,c.[);
    reconsider A = X as Subset of R^1 by PRE_TOPC:39;
A7: C1 = ].a,c.[ by TOPREALB:def 3;
A8: the carrier of L = [.r,s.] by A1,TOPMETR:25;
A9: C1 c= the carrier of L
    proof
      let x be set;
      assume
A10:   x in C1;
      then reconsider x as Real by A7;
      a < x & x < c by A10,A7,JORDAN6:45;
      then r <= x & x <= s by A3,A6,XREAL_1:2;
      hence thesis by A8,TOPREAL5:1;
    end;
    A c= C1
    proof
      let x be set;
      assume
A11:   x in A;
      then reconsider x as Real by A2;
      a < x & x <= b by A11,A2,RCOMP_2:4;
      then a < x & x < c by A5,XREAL_1:2;
      hence thesis by A7,JORDAN6:45;
    end;
    then Int A = Int X by A9,TOPS_3:57;
    hence thesis by A2,Th51;
end;

theorem Th56:
  for X being Subset of Closed-Interval-TSpace(r,s),
      Y being Subset of REAL st X = Y holds
  X is connected iff Y is connected
proof 
  let X be Subset of Closed-Interval-TSpace(r,s),
      Y be Subset of REAL such that
A1: X = Y;
  reconsider Z = X as Subset of R^1 by A1,TOPMETR:24;
  hereby
    assume X is connected;
    then Z is connected by CONNSP_1:24;
    hence Y is connected by A1,Th52;
  end;
  assume Y is connected;
  then Z is connected by A1,Th52;
  hence thesis by CONNSP_1:24;
end;

registration
  let T be TopSpace;
  cluster open closed connected Subset of T;
  existence
  proof 
    take {}T;
    thus thesis;
  end;
end;

registration
  let T be non empty connected TopSpace;
  cluster non empty open closed connected Subset of T;
  existence
  proof 
    take [#]T;
    thus thesis by CONNSP_1:28;
  end;
end;

theorem Th57:
  r <= s implies
  for X being open connected Subset of Closed-Interval-TSpace(r,s) holds
    X is empty or X = [.r,s.] or
    (ex a being real number st r < a & a <= s & X = [.r,a.[) or
    (ex a being real number st r <= a & a < s & X = ].a,s.]) or
    (ex a, b being real number st r <= a & a < b & b <= s &
        X = ].a,b.[)
proof 
  set L = Closed-Interval-TSpace(r,s);
  assume
A1: r <= s;
  let X be open connected Subset of L;
A2: the carrier of L = [#]L by PRE_TOPC:12;
A3: the carrier of L = [.r,s.] by A1,TOPMETR:25;
  then X is bounded_above bounded_below Subset of REAL
  by RCOMP_1:3,4,XBOOLE_1:1;
  then reconsider Y = X as bounded connected Subset of REAL by Th56;
A4: L is connected by A1,TREAL_1:23;
A5: r in [.r,s.] by A1,RCOMP_1:15;
A6: s in [.r,s.] by A1,RCOMP_1:15;
  per cases by Th41;
  suppose Y is empty;
    hence thesis;
  end;
  suppose Y = REAL;
    hence thesis by Lm8;
  end;
  suppose (ex a st Y = left_closed_halfline(a)) or
    (ex a st Y = left_open_halfline(a)) or
    (ex a st Y = right_closed_halfline(a)) or
    (ex a st Y = right_open_halfline(a));
    hence thesis;
  end;
  suppose ex a, b st a <= b & Y = [.a,b.];
    then consider a, b such that
A7: a <= b and
A8: Y = [.a,b.];
A9: r <= a & b <= s by A3,A8,A7,BORSUK_4:14;
A10: X <> {}L by A7,A8,RCOMP_1:15;
A11: X is closed by A8,A9,TOPREALA:44;
    now
      assume
A12:   r <> a or b <> s;
      per cases by A9,A12,REAL_1:def 5;
      suppose r < a;
        then not r in X by A8,TOPREAL5:1;
        hence contradiction by A3,A2,A5,A10,A11,A4,CONNSP_1:14;
      end;
      suppose b < s;
        then not s in X by A8,TOPREAL5:1;
        hence contradiction by A3,A2,A6,A10,A11,A4,CONNSP_1:14;
      end;
    end;
    hence thesis by A8;
  end;
  suppose ex a, b st a < b & Y = [.a,b.[;
    then consider a, b such that
A13: a < b and
A14: Y = [.a,b.[;
A15: r <= a & b <= s by A3,A14,A13,Th12;
    now
      assume r <> a;
      then
A16:  r < a by A15,REAL_1:def 5;
      now
        assume
A17:    Int X = X;
        Int X = ].a,b.[ by A1,A14,A15,A16,Th54;
        then not a in Int X by JORDAN6:45;
        hence contradiction by A17,A14,A13,RCOMP_2:3;
      end;
      hence contradiction by TOPS_1:55;
    end;
    hence thesis by A13,A14,A15;
  end;
  suppose ex a, b st a < b & Y = ].a,b.];
    then consider a, b such that
A18: a < b and
A19: Y = ].a,b.];
A20: r <= a & b <= s by A3,A19,A18,Th14;
    now
      assume b <> s;
      then
A21:  b < s by A20,REAL_1:def 5;
      now
        assume
A22:    Int X = X;
        Int X = ].a,b.[ by A1,A19,A20,A21,Th55;
        then not b in Int X by JORDAN6:45;
        hence contradiction by A22,A19,A18,RCOMP_2:4;
      end;
      hence contradiction by TOPS_1:55;
    end;
    hence thesis by A18,A19,A20;
  end;
  suppose ex a, b st a < b & Y = ].a,b.[;
    then consider a, b such that
A23: a < b and
A24: Y = ].a,b.[;
    r <= a & b <= s by A3,A24,A23,BORSUK_4:15;
    hence thesis by A23,A24;
  end;
end;

begin :: Minimal cover of an interval

deffunc F(set) = $1;
defpred P[set,set] means $1 c= $2;

theorem Th58:
  for T being 1-sorted, F being Subset-Family of T holds
  F is_a_cover_of T iff F is_a_cover_of [#]T
proof 
  let T be 1-sorted, F be Subset-Family of T;
  thus F is_a_cover_of T implies F is_a_cover_of [#]T
  proof
    assume F is_a_cover_of T;
    hence [#]T c= union F by PRE_TOPC:def 8;
    thus thesis;
  end;
  assume F is_a_cover_of [#]T;
  hence [#]T c= union F by COMPTS_1:def 1;
  let a be set;
  assume a in union F;
  then a in the carrier of T;
  hence a in [#]T by PRE_TOPC:12;
end;

theorem Th59:
  for T being 1-sorted, F being finite Subset-Family of T
  for F1 being Subset-Family of T st
  F is_a_cover_of T &
  F1 = F \ {X where X is Subset of T:
    X in F & ex Y being Subset of T st Y in F & X c= Y & X <> Y}
  holds F1 is_a_cover_of T
  proof 
    let T be 1-sorted, F be finite Subset-Family of T,
    F1 be Subset-Family of T such that
A1: [#]T = union F and
A2: F1 = F \ {X where X is Subset of T:
    X in F & ex Y being Subset of T st Y in F & X c= Y & X <> Y};
    set ZAW = {X where X is Subset of T:
    X in F & ex Y being Subset of T st Y in F & X c= Y & X <> Y};
A3: [#]T = the carrier of T by PRE_TOPC:12;
    thus [#]T c= union F1
    proof
      let t be set;
      assume t in [#]T;
      then consider Z being set such that
A4:   t in Z and
A5:   Z in F by A1,TARSKI:def 4;
      set ALL = {X where X is Subset of T: Z c= X & X in F & Z <> X};
      per cases;
      suppose
A6:     ALL is empty;
        now
          assume Z in ZAW;
          then consider X being Subset of T such that
A7:       Z = X and X in F and
A8:       ex Y being Subset of T st Y in F & X c= Y & X <> Y;
          consider Y being Subset of T such that
A9:       Y in F & X c= Y & X <> Y by A8;
          Y in ALL by A7,A9;
          hence contradiction by A6;
        end;
        then Z in F1 by A2,A5,XBOOLE_0:def 4;
        hence thesis by A4,TARSKI:def 4;
      end;
      suppose ALL is non empty;
        then consider w being set such that
A10:    w in ALL by XBOOLE_0:def 1;
        consider R being Relation of ALL such that
A11:     for x, y being set holds [x,y] in R iff x in ALL & y in ALL & P[x,y]
        from RELSET_1:sch 1;
A12:   R is_reflexive_in ALL
      proof
        let x be set;
        thus thesis by A11;
      end;
A13:   field R = ALL by A12,ORDERS_1:98;
      then
A14:   R is reflexive by A12,RELAT_2:def 9;
A15:   R partially_orders ALL
      proof
        thus R is_reflexive_in ALL by A12;
        thus R is_transitive_in ALL
        proof
          let x, y, z be set;
          assume
A16:       x in ALL & y in ALL & z in ALL;
          assume [x,y] in R & [y,z] in R;
          then x c= y & y c= z by A11;
          then x c= z by XBOOLE_1:1;
          hence [x,z] in R by A16,A11;
        end;
        let x, y be set;
        assume x in ALL & y in ALL;
        assume [x,y] in R & [y,x] in R;
        hence x c= y & y c= x by A11;
      end;
      ALL has_upper_Zorn_property_wrt R
      proof
        let Y be set such that
A17:     Y c= ALL and
A18:     R |_2 Y is_linear-order;
        per cases;
        suppose
A19:       Y is non empty;
          then consider y being set such that
A20:      y in Y by XBOOLE_0:def 1;
          y in ALL by A17,A20;
          then consider X being Subset of T such that
A21:      X = y and
A22:      Z c= X and
          X in F and
A23:      Z <> X;
          take union Y;
A24:      X c= union Y by A20,A21,ZFMISC_1:92;
          then
A25:       Z c= union Y by A22,XBOOLE_1:1;
A26:      for A, B being set st A in Y & B in Y holds A \/ B in Y
          proof
            let A, B be set such that
A27:        A in Y and
A28:        B in Y;
            R |_2 Y is connected by A18,ORDERS_1:def 5;
            then
A29:        R |_2 Y is_connected_in field (R |_2 Y) by RELAT_2:def 14;
            field(R |_2 Y) = Y by A14,A17,A13,ORDERS_1:181;
            then
A30:        [A,B] in R |_2 Y or [B,A] in R |_2 Y or A = B
            by A27,A28,A29,RELAT_2:def 6;
            R |_2 Y c= R by WELLORD1:15;
            then A c= B or B c= A by A30,A11;
            hence thesis by A27,A28,XBOOLE_1:12;
          end;
          defpred U[set] means $1 is non empty & $1 c= Y implies union $1 in Y;
          ALL c= F
          proof
            let x be set;
            assume x in ALL;
            then ex X being Subset of T st X = x & Z c= X & X in F & Z <> X;
            hence thesis;
          end;
          then
A31:      Y c= F by A17,XBOOLE_1:1;
          then
A32:      Y is finite by FINSET_1:13;
A33:      U[{}];
A34:      for x, B being set st x in Y & B c= Y & U[B] holds U[B \/ {x}]
          proof
            let x, B be set such that
A35:        x in Y and
A36:        B c= Y and
A37:        U[B] and
            B \/ {x} is non empty & B \/ {x} c= Y;
A38:        union {x} = x by ZFMISC_1:31;
            per cases;
            suppose B is empty;
              hence thesis by A35,ZFMISC_1:31;
            end;
            suppose B is non empty;
              then x \/ union B in Y by A36,A37,A35,A26;
              hence thesis by A38,ZFMISC_1:96;
            end;
          end;
          U[Y] from FINSET_1:sch 2(A32,A33,A34);
          then
A39:       union Y in F by A19,A31;
          Z <> union Y by A24,A22,A23,XBOOLE_0:def 10;
          hence
A40:       union Y in ALL by A25,A39;
          let z be set;
          assume
A41:       z in Y;
          then P[z,union Y] by ZFMISC_1:92;
          hence [z,union Y] in R by A11,A40,A17,A41;
        end;
        suppose
A42:       Y is empty;
          take w;
          thus w in ALL by A10;
          thus thesis by A42;
        end;
      end;
      then consider M being set such that
A43:   M is_maximal_in R by A15,A13,ORDERS_1:173;
A44:  M in field R by A43,ORDERS_1:def 11;
      then
A45:  ex X being Subset of T st X = M & Z c= X & X in F & Z <> X by A13;
      now
        assume M in ZAW;
        then consider H being Subset of T such that
A49:    M = H and
        H in F and
A50:    ex Y being Subset of T st Y in F & H c= Y & H <> Y;
        consider Y being Subset of T such that
A51:    Y in F and
A52:    H c= Y and
A53:    H <> Y by A50;
A54:    Z c= Y by A45,A49,A52,XBOOLE_1:1;
        Z <> Y by A45,A49,A52,XBOOLE_0:def 10;
        then
A55:    Y in ALL by A51,A54;
        then [M,Y] in R by A11,A13,A44,A52,A49;
        hence contradiction by A13,A53,A49,A55,A43,ORDERS_1:def 11;
      end;
      then M in F1 by A45,A2,XBOOLE_0:def 4;
      hence thesis by A4,A45,TARSKI:def 4;
    end;
    end;
    thus thesis by A3;
  end;

theorem Th60:
  for S being trivial non empty 1-sorted, s being Point of S,
  F being Subset-Family of S st F is_a_cover_of S holds
  {s} in F
  proof 
    let S be trivial non empty 1-sorted,
    s be Point of S,
    F be Subset-Family of S such that
A1: [#]S = union F;
    the carrier of S = [#]S by PRE_TOPC:12;
    then consider Z being set such that
A2: s in Z and
A3: Z in F by A1,TARSKI:def 4;
    consider d being Element of S such that
A4: the carrier of S = {d} by TEX_1:def 1;
A5: s = d by REALSET1:14;
    Z = {s}
    proof
      thus for x being set st x in Z holds x in {s} by A3,A4,A5;
      let x be set;
      thus thesis by A2,TARSKI:def 1;
    end;
    hence thesis by A3;
  end;

definition
  let T be TopStruct, F be Subset-Family of T;
  attr F is connected means :Def1:
  for X being Subset of T st X in F holds X is connected;
end;

registration
  let T be TopSpace;
  cluster non empty open closed connected Subset-Family of T;
existence
proof 
  {{}T} c= bool the carrier of T
  proof
    let x be set;
    assume x in {{}T};
    then x = {}T by TARSKI:def 1;
    hence thesis;
  end;
  then reconsider F = {{}T} as Subset-Family of T by SETFAM_1:def 7;
  take F;
  thus F is non empty;
  thus for P being Subset of T holds P in F implies P is open by TARSKI:def 1;
  thus for P being Subset of T holds P in F implies P is closed
    by TARSKI:def 1;
  thus for P being Subset of T holds P in F implies P is connected
    by TARSKI:def 1;
end;
end;

reserve
  n, m for natural number,
  F for Subset-Family of Closed-Interval-TSpace(r,s);

Lm13:
  [.r,s.] in F & r <= s implies
  rng <*[.r,s.]*> c= F & union rng <*[.r,s.]*> = [.r,s.] &
  for n being natural number st 1 <= n holds
    (n <= len <*[.r,s.]*> implies <*[.r,s.]*>/.n is non empty) &
    (n+1 <= len <*[.r,s.]*> implies
      inf(<*[.r,s.]*>/.n) <= inf(<*[.r,s.]*>/.(n+1)) &
      sup(<*[.r,s.]*>/.n) <= sup(<*[.r,s.]*>/.(n+1)) &
      inf(<*[.r,s.]*>/.(n+1)) < sup(<*[.r,s.]*>/.n)) &
    (n+2 <= len <*[.r,s.]*> implies
      sup(<*[.r,s.]*>/.n) <= inf(<*[.r,s.]*>/.(n+2)))
proof 
  assume that
A1:  [.r,s.] in F and
A2:  r <= s;
  set f = <*[.r,s.]*>;
A3: rng f = {[.r,s.]} by FINSEQ_1:55;
  thus rng f c= F
  proof
    let a be set;
    assume a in rng f;
    hence thesis by A1,A3,TARSKI:def 1;
  end;
  thus union rng f = [.r,s.] by A3,ZFMISC_1:31;
  let n be natural number;
  assume
A4: 1 <= n;
  hereby
    assume n <= len f;
    then n <= 1 by FINSEQ_1:56;
    then n = 1 by A4,XREAL_1:1;
    then f/.n = [.r,s.] by FINSEQ_4:25;
    hence f/.n is non empty by A2,RCOMP_1:15;
  end;
  hereby
    assume n+1 <= len f;
    then n+1 <= 0+1 by FINSEQ_1:56;
    hence inf(f/.n) <= inf(f/.(n+1)) &
      sup(f/.n) <= sup(f/.(n+1)) &
      inf(f/.(n+1)) < sup(f/.n) by A4,XREAL_1:8;
  end;
  assume n+2 <= len f;
  then n+1+1 <= 0+1 by FINSEQ_1:56;
  then n+1 <= 0 by XREAL_1:8;
  hence thesis by NAT_1:18;
end;

theorem Th61:
  for L being TopSpace, G, G1 being Subset-Family of L st
  G is_a_cover_of L & G is finite
  for ALL being set st
  G1 = G \ {X where X is Subset of L: X in G & ex Y being Subset of L st
  Y in G & X c= Y & X <> Y} &
  ALL = {C where C is Subset-Family of L: C is_a_cover_of L & C c= G1}
  holds ALL has_lower_Zorn_property_wrt RelIncl ALL
  proof
    let L be TopSpace;
  let G, G1 be Subset-Family of L;
  assume that
A2: G is_a_cover_of L and
A3: G is finite;
    set ZAW = {X where X is Subset of L: X in G & ex Y being Subset of L st
    Y in G & X c= Y & X <> Y};
  let ALL be set;
  assume that
A4: G1 = G \ ZAW and
A5: ALL = {C where C is Subset-Family of L: C is_a_cover_of L & C c= G1};
    set R = RelIncl ALL;
  G1 c= G by A4,XBOOLE_1:36;
  then
A6: G1 is finite by A3,FINSET_1:13;
A7: G1 is_a_cover_of L by A2,A3,A4,Th59;
A8: field R = ALL by WELLORD2:def 1;
A9: R is reflexive by WELLORD2:2;
  let Y be set such that
A10:   Y c= ALL and
A11:   R |_2 Y is_linear-order;
    per cases;
    suppose
A12:   Y is non empty;
    take x = meet Y;
    consider y being set such that
A13:   y in Y by A12,XBOOLE_0:def 1;
    y in ALL by A10,A13;
    then consider C being Subset-Family of L such that
A14:   y = C and
A15:   C is_a_cover_of L and
A16:   C c= G1 by A5;
    x c= bool the carrier of L by A13,A14,SETFAM_1:8;
    then reconsider X = x as Subset-Family of L by SETFAM_1:def 7;
A17: x c= G1 by A14,A16,A13,SETFAM_1:8;
A18: bool G1 is finite by A6,FINSET_1:24;
    set E = {F(D) where D is Element of bool bool the carrier of L:
      D in bool G1};
A19: E is finite from FRAENKEL:sch 21(A18);
    ALL c= E
    proof
      let a be set;
      assume a in ALL;
      then ex C being Subset-Family of L st a = C & C is_a_cover_of L &
        C c= G1 by A5;
      hence thesis;
    end;
    then ALL is finite by A19,FINSET_1:13;
    then
A20: Y is finite by A10,FINSET_1:13;
    defpred A[set] means $1 is non empty implies meet $1 in Y;
A21: A[{}];
A22: for x, B being set st x in Y & B c= Y & A[B] holds A[B \/ {x}]
    proof
      let x, B be set such that
A23:     x in Y and B c= Y and
A24:     A[B] and B \/ {x} is non empty;
      per cases;
      suppose B is empty;
      hence thesis by A23,SETFAM_1:11;
      end;
      suppose
A25:     B is non empty;
      then
A26:   meet (B \/ {x}) = meet B /\ meet {x} by SETFAM_1:10
        .= meet B /\ x by SETFAM_1:11;
A27:   field (R |_2 Y) = Y by A8,A9,A10,ORDERS_1:181;
      R |_2 Y is connected by A11,ORDERS_1:def 5;
      then
A28:   R |_2 Y is_connected_in field (R |_2 Y) by RELAT_2:def 14;
      [x,meet B] in R |_2 Y or [meet B,x] in R |_2 Y or x = meet B
        by A23,A24,A25,A27,A28,RELAT_2:def 6;
      then [x,meet B] in R or [meet B,x] in R or x = meet B by WELLORD1:16;
      then meet B c= x or x c= meet B by A23,A10,A24,A25,WELLORD2:def 1;
      hence meet (B \/ {x}) in Y by A23,A26,A24,A25,XBOOLE_1:28;
      end;
    end;
A29: A[Y] from FINSET_1:sch 2(A20,A21,A22);
    X is_a_cover_of L
    proof
      thus [#]L c= union X
      proof
        let a be set;
        assume
A30:       a in [#]L;
        meet Y in ALL by A29,A10,A12;
        then ex C being Subset-Family of L st
          meet Y = C & C is_a_cover_of L & C c= G1 by A5;
        hence thesis by A30,PRE_TOPC:def 8;
      end;
      let a be set;
      assume a in union X;
      then consider Z being set such that
A31:     a in Z and
A32:     Z in X by TARSKI:def 4;
A33:   Z in y by A13,A32,SETFAM_1:def 1;
      [#]L = union C by A15,PRE_TOPC:def 8;
      hence thesis by A31,A14,A33,TARSKI:def 4;
    end;
    hence
A34:   x in ALL by A17,A5;
    let y be set;
    assume
A35:   y in Y;
    then x c= y by SETFAM_1:8;
    hence [x,y] in R by A10,A34,A35,WELLORD2:def 1;
    end;
    suppose
A36:   Y is empty;
    take G1;
    thus thesis by A7,A36,A5;
    end;
end;

theorem Th62:
  for L being TopSpace, G, ALL being set st
  ALL = {C where C is Subset-Family of L: C is_a_cover_of L & C c= G}
  for M being set st M is_minimal_in RelIncl ALL & M in field RelIncl ALL
  for A1 being Subset of L st A1 in M holds
  not ex A2, A3 being Subset of L st
  A2 in M & A3 in M & A1 c= A2 \/ A3 & A1 <> A2 & A1 <> A3
  proof 
    let L be TopSpace;
    let G be set;
    let ALL be set such that
A1:  ALL = {C where C is Subset-Family of L: C is_a_cover_of L & C c= G};
A2: [#]L = the carrier of L by PRE_TOPC:12;
     set R = RelIncl ALL;
A3: field R = ALL by WELLORD2:def 1;
     let M be set such that
A4: M is_minimal_in RelIncl ALL and
A5: M in field RelIncl ALL;
     consider C being Subset-Family of L such that
A6: M = C and
A7: C is_a_cover_of L and
A8: C c= G by A3,A5,A1;
A9: union C = [#]L by A7,PRE_TOPC:def 8;
     let A1 be Subset of L such that
A10: A1 in M;
     given A2, A3 being Subset of L such that
A11: A2 in M and
A12: A3 in M and
A13: A1 c= A2 \/ A3 and
A14: A1 <> A2 and
A15: A1 <> A3;
     set Y = C \ {A1};
     union Y = the carrier of L
     proof
       thus union Y c= the carrier of L;
       let x be set;
       assume
A16:   x in the carrier of L;
       per cases;
       suppose
A17:     x in A1;
         per cases by A17,A13,XBOOLE_0:def 2;
         suppose
A18:       x in A2;
           A2 in Y by A6,A14,A11,ZFMISC_1:64;
           hence x in union Y by A18,TARSKI:def 4;
         end;
         suppose
A19:       x in A3;
           A3 in Y by A6,A15,A12,ZFMISC_1:64;
           hence x in union Y by A19,TARSKI:def 4;
         end;
       end;
       suppose
A20:     not x in A1;
         consider Z being set such that
A21:     x in Z and
A22:     Z in C by A9,A2,A16,TARSKI:def 4;
         Z in Y by A20,A21,A22,ZFMISC_1:64;
         hence x in union Y by A21,TARSKI:def 4;
       end;
     end;
     then
A23: Y is_a_cover_of L by A2,PRE_TOPC:def 8;
A24: Y <> M by A10,ZFMISC_1:64;
A25: Y c= M by A6,XBOOLE_1:36;
     then Y c= G by A6,A8,XBOOLE_1:1;
     then
A26: Y in ALL by A1,A23;
     then [Y,M] in R by A3,A5,A25,WELLORD2:def 1;
     hence contradiction by A4,A3,A26,A24,ORDERS_1:def 12;
   end;

definition
  let r, s be real number;
  let F be Subset-Family of Closed-Interval-TSpace(r,s) such that
A1:   F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2:   F is open and
A3:   F is connected and
A4:   r <= s;
  mode IntervalCover of F -> FinSequence of bool REAL means :Def2:
  rng it c= F & union rng it = [.r,s.] &

  (for n being natural number st 1 <= n holds
    (n <= len it implies it/.n is non empty) &
    (n+1 <= len it implies
      inf(it/.n) <= inf(it/.(n+1)) &
      sup(it/.n) <= sup(it/.(n+1)) &
      inf(it/.(n+1)) < sup(it/.n)) &
    (n+2 <= len it implies sup(it/.n) <= inf(it/.(n+2)))) &

  ([.r,s.] in F implies it = <*[.r,s.]*>) &
  (not [.r,s.] in F implies
  (ex p being real number st r < p & p <= s & it.1 = [.r,p.[) &
  (ex p being real number st r <= p & p < s & it.len it = ].p,s.]) &
  (for n being natural number st 1 < n & n < len it
    ex p, q being real number st r <= p & p < q & q <= s & it.n = ].p,q.[));
existence
proof 
  per cases;
  suppose
A5: [.r,s.] in F;
  take f = <*[.r,s.]*>;
A6: rng f = {[.r,s.]} by FINSEQ_1:55;
  thus rng f c= F
  proof
    let a be set;
    assume a in rng f;
    hence thesis by A5,A6,TARSKI:def 1;
  end;
  thus union rng f = [.r,s.] by A6,ZFMISC_1:31;
  thus thesis by A4,A5,Lm13;
  end;
  suppose
A7: not [.r,s.] in F;
A8: r in [.r,s.] by A4,TOPREAL5:1;
A9: s in [.r,s.] by A4,TOPREAL5:1;

:: wlasnosci Closed-Interval-TSpace(r,s)
  set L = Closed-Interval-TSpace(r,s);
A10: the carrier of L = [.r,s.] by A4,TOPMETR:25;
A11: [#]L = the carrier of L by PRE_TOPC:12;
  reconsider T = L as non empty connected TopSpace by A4,TREAL_1:23;
A12: F is_a_cover_of [#]L by A1,Th58;
  L is compact by A4,HEINE:11;
  then [#]L is compact by COMPTS_1:10;
  then consider G being Subset-Family of L such that
A13: G c= F and
A14: G is_a_cover_of [#]L and
A15: G is finite by A12,A2,COMPTS_1:def 7;
A16: G is_a_cover_of L by Th58,A14;
  then
A17: union G = [#]L by PRE_TOPC:def 8;
A18: now
    let A be Subset of L;
    A is Subset of REAL by A10,XBOOLE_1:1;
    hence A is bounded_above bounded_below by A10,RCOMP_1:3,4;
    hence A is bounded Subset of REAL by A10,XBOOLE_1:1,SEQ_4:def 3;
  end;

:: wlasnosci G1
  set ZAW = {X where X is Subset of L: X in G &
    ex Y being Subset of L st Y in G & X c= Y & X <> Y};
  set G1 = G \ ZAW;
A19: G1 c= G by XBOOLE_1:36;
  then
A20: G1 is finite by A15,FINSET_1:13;
A21: now
    assume
A22:   {} in G1;
    then
A23: {} in G by XBOOLE_0:def 4;
    consider x being set such that
A24:   x in the carrier of L by XBOOLE_0:def 1;
    consider g being set such that
A25:   x in g and
A26:   g in G by A11,A24,A17,TARSKI:def 4;
    {} c= g & {} is Subset of REAL by XBOOLE_1:2;
    then {} in ZAW by A23,A26,A25;
    hence contradiction by A22,XBOOLE_0:def 4;
  end;
A27: G1 c= F by A13,A19,XBOOLE_1:1;
A28: for X being set st X in G1 holds X is connected Subset of REAL
  proof
    let X be set;
    assume X in G1;
    then reconsider X as connected Subset of L by A3,A27,Def1;
    reconsider Y = X as Subset of REAL by A10,XBOOLE_1:1;
    Y is connected by Th56;
    hence thesis;
  end;
  F c= bool REAL
  proof
    let a be set;
    assume a in F;
    then a c= REAL by A10,XBOOLE_1:1;
    hence thesis;
  end;
  then
A29: G1 c= bool REAL by A27,XBOOLE_1:1;

:: konstrukcja pokrycia minimalnego
  set ALL = {C where C is Subset-Family of L: C is_a_cover_of L & C c= G1};
  set R = RelIncl ALL;
A30: R partially_orders ALL
     proof
       thus R is_reflexive_in ALL & R is_transitive_in ALL &
       R is_antisymmetric_in ALL by WAYBEL35:1,2,3;
     end;
A31: field R = ALL by WELLORD2:def 1;
  ALL has_lower_Zorn_property_wrt R by A16,A15,Th61;
  then consider M being set such that
A32: M is_minimal_in R by A30,A31,ORDERS_1:174;
A33: M in field R by A32,ORDERS_1:def 12;
  then consider C being Subset-Family of L such that
A34: M = C and
A35: C is_a_cover_of L and
A36: C c= G1 by A31;
A37: union C = [#]L by A35,PRE_TOPC:def 8;
  consider R1 being set such that
A38: r in R1 and
A39: R1 in C by A10,A37,A11,A8,TARSKI:def 4;
  consider R2 being set such that
A40: s in R2 and
A41: R2 in C by A10,A37,A11,A9,TARSKI:def 4;
A42: R1 in G1 by A36,A39;
  then
A43: R1 in F by A27;
A44: R2 in G1 by A36,A41;
  then
A45: R2 in F by A27;
  reconsider R1, R2 as open connected Subset of L
  by A27,A42,A44,A2,A3,Def1,TOPS_2:def 1;
  reconsider X = C as non empty finite Subset of bool REAL
    by A20,A36,A29,A39,XBOOLE_1:1,FINSET_1:13;
A46: X c= F by A36,A27,XBOOLE_1:1;
A47: for Z being Subset of REAL st Z in X holds
      Z is non empty open connected Subset of T
    proof
      let Z be Subset of REAL;
      assume
A48:  Z in X;
      then Z is non empty connected by A36,A28,A21;
      hence thesis by A46,A48,Th56,A2,TOPS_2:def 1;
    end;

:: odcinki koncowe
:: LEWY
  set LM = {sup [.r,b.[ where b is Real: [.r,b.[ in G1};
  set LM1 = {sup E where E is Subset of L: E in G1};
A49: LM1 is finite from FRAENKEL:sch 21(A20);
A50: LM c= LM1
  proof
    let x be set;
    assume x in LM;
    then ex b being Real st x = sup [.r,b.[ & [.r,b.[ in G1;
    hence thesis;
  end;
A51: now per cases by A4,A38,A7,A43,Th57;
    suppose ex a being real number st r < a & a <= s & R1 = [.r,a.[;
      then consider a being real number such that r < a and a <= s and
A52:  R1 = [.r,a.[;
      a is Real by XREAL_0:def 1;
      then sup [.r,a.[ in LM by A36,A39,A52;
      hence LM is non empty;
    end;
    suppose ex a being real number st r <= a & a < s & R1 = ].a,s.];
      hence LM is non empty by A38,RCOMP_2:4;
    end;
    suppose ex a, b being real number st r <= a & a < b & b <= s &
      R1 = ].a,b.[;
      hence LM is non empty by A38,JORDAN6:45;
    end;
  end;
  LM c= REAL
  proof
    let x be set;
    assume x in LM;
    then ex b being Real st x = sup [.r,b.[ & [.r,b.[ in G1;
    hence thesis;
  end;
  then reconsider LM as non empty finite Subset of REAL
  by A50,A49,A51,FINSET_1:13;
  reconsider kL = max LM as Real by XREAL_0:def 1;
  set LEWY = [.r,kL.[;
  kL in LM by PRE_CIRC:def 1;
  then consider b being Real such that
A53: kL = sup [.r,b.[ and
A54: [.r,b.[ in G1;
A55: r < b by A54,A21,RCOMP_2:9;
  then
A56: kL = b by A53,Th5;
A57: r in [.r,b.[ by A55,RCOMP_2:3;
      r < kL by A53,A55,Th5;
  then
A58: inf LEWY = r by Th4;
A59: sup LEWY = kL by A55,A53,Th5;
  reconsider LEWY as non empty Subset of L by A54,A57,A53,A55,Th5;

:: PRAWY
  set RM = {inf ].c,s.] where c is Real: ].c,s.] in G1};
  set RM1 = {inf E where E is Subset of L: E in G1};
A60: RM1 is finite from FRAENKEL:sch 21(A20);
A61: RM c= RM1
  proof
    let x be set;
    assume x in RM;
    then ex b being Real st x = inf ].b,s.] & ].b,s.] in G1;
    hence thesis;
  end;
A62: now per cases by A4,A40,A7,A45,Th57;
    suppose ex a being real number st r < a & a <= s & R2 = [.r,a.[;
      hence RM is non empty by A40,RCOMP_2:3;
    end;
    suppose ex a being real number st r <= a & a < s & R2 = ].a,s.];
       then consider a being real number such that r <= a and a < s and
A63:  R2 = ].a,s.];
      a is Real by XREAL_0:def 1;
      then inf ].a,s.] in RM by A36,A41,A63;
      hence RM is non empty;
    end;
    suppose ex a, b being real number st r <= a & a < b & b <= s &
      R2 = ].a,b.[;
      hence RM is non empty by A40,JORDAN6:45;
    end;
  end;
  RM c= REAL
  proof
    let x be set;
    assume x in RM;
    then ex b being Real st x = inf ].b,s.] & ].b,s.] in G1;
    hence thesis;
  end;
  then reconsider RM as non empty finite Subset of REAL
  by A61,A60,A62,FINSET_1:13;
  reconsider pP = min RM as Real by XREAL_0:def 1;
  set PRAWY = ].pP,s.];
  pP in RM by SFMASTR3:def 1;
  then consider b being Real such that
A64: pP = inf ].b,s.] and
A65: ].b,s.] in G1;
A66: b < s by A65,A21,RCOMP_2:10;
  then
A67: pP = b by A64,Th6;
A68: s in ].b,s.] by A66,RCOMP_2:4;
       pP < s by A64,A66,Th6;
  then
A69: sup PRAWY = s by Th7;
A70: inf PRAWY = pP by A64,A66,Th6;
  reconsider PRAWY as non empty Subset of L by A65,A68,A64,A66,Th6;
A71: for A being Subset of L st r in A & A in G1 holds A = LEWY
  proof
    let A be Subset of L;
    assume that
A72:   r in A and
A73:   A in G1;
    assume
A74:   A <> LEWY;
A75: A in F by A73,A27;
A76: A is open by A73,A27,A2,TOPS_2:def 1;
A77: A is connected by A73,A27,A3,Def1;
    now
      assume
A78:     (ex a being real number st r <= a & a < s & A = ].a,s.]) or
      ex a, b being real number st r <= a & a < b & b <= s &
        A = ].a,b.[;
      per cases by A78;
      suppose ex a being real number st r <= a & a < s & A = ].a,s.];
        hence contradiction by A72,RCOMP_2:4;
      end;
      suppose ex a, b being real number st r <= a & a < b & b <= s &
        A = ].a,b.[;
        hence contradiction by A72,JORDAN6:45;
      end;
    end;
    then consider ak being real number such that
A79:   r < ak & ak <= s & A = [.r,ak.[ by A72,A75,A76,A77,A4,A7,Th57;
A80: ak is Real by XREAL_0:def 1;
    A c= LEWY
    proof
      let a be set;
      assume
A81:     a in A;
      then a in [.r,s.] by A10;
      then reconsider a as Real;
A82:   r <= a by A79,A81,RCOMP_2:3;
A83:   a < ak by A79,A81,RCOMP_2:3;
      sup A = ak by A79,Th5;
      then ak in LM by A73,A79,A80;
      then ak <= kL by PRE_CIRC:def 1;
      then a < kL by A83,XREAL_1:2;
      hence thesis by A82,RCOMP_2:3;
    end;
    then A in ZAW by A73,A74,A19,A54,A56;
    hence contradiction by A73,XBOOLE_0:def 4;
  end;
A84: for A being Subset of L st s in A & A in G1 holds A = PRAWY
  proof
    let A be Subset of L;
    assume that
A85:   s in A and
A86:   A in G1;
    assume
A87:   A <> PRAWY;
A88: A in F by A86,A27;
A89: A is open by A86,A27,A2,TOPS_2:def 1;
A90: A is connected by A86,A27,A3,Def1;
    now
      assume
A91:     (ex a being real number st r < a & a <= s & A = [.r,a.[) or
      ex a, b being real number st r <= a & a < b & b <= s &
        A = ].a,b.[;
      per cases by A91;
      suppose ex a being real number st r < a & a <= s & A = [.r,a.[;
        hence contradiction by A85,RCOMP_2:3;
      end;
      suppose ex a, b being real number st r <= a & a < b & b <= s &
        A = ].a,b.[;
        hence contradiction by A85,JORDAN6:45;
      end;
    end;
    then consider ak being real number such that
A92:   r <= ak & ak < s & A = ].ak,s.] by A85,A88,A89,A90,A4,A7,Th57;
A93: ak is Real by XREAL_0:def 1;
    A c= PRAWY
    proof
      let a be set;
      assume
A94:     a in A;
      then a in [.r,s.] by A10;
      then reconsider a as Real;
A95:   a <= s by A92,A94,RCOMP_2:4;
A96:   ak < a by A92,A94,RCOMP_2:4;
      inf A = ak by A92,Th6;
      then ak in RM by A86,A92,A93;
      then pP <= ak by SFMASTR3:def 1;
      then pP < a by A96,XREAL_1:2;
      hence thesis by A95,RCOMP_2:4;
    end;
    then A in ZAW by A86,A87,A19,A65,A67;
    hence contradiction by A86,XBOOLE_0:def 4;
  end;
A97: for A being Subset of L st A in G1 & A <> LEWY & A <> PRAWY
     ex a, b being Real st a < b & A = ].a,b.[
  proof
    let A be Subset of L such that
A98: A in G1 and
A99: A <> LEWY and
A100: A <> PRAWY;
A101: A in F by A98,A27;
A102: A is open connected by A2,A3,Def1,A98,A27,TOPS_2:def 1;
    per cases by A102,A4,A98,A101,A7,A21,Th57;
    suppose ex a being real number st r < a & a <= s & A = [.r,a.[;
      then consider a being real number such that
      r < a & a <= s and
A103:  A = [.r,a.[;
      per cases;
      suppose a <= kL;
        then A c= LEWY by A103,TOPREALA:12;
        then A in ZAW by A99,A98,A54,A56,A19;
        hence thesis by A98,XBOOLE_0:def 4;
      end;
      suppose a > kL;
        then LEWY c= A by A103,TOPREALA:12;
        then LEWY in ZAW by A99,A98,A54,A56,A19;
        hence thesis by A54,A56,XBOOLE_0:def 4;
      end;
    end;
    suppose ex a being real number st r <= a & a < s & A = ].a,s.];
      then consider a being real number such that
        r <= a & a < s and
A104:    A = ].a,s.];
      per cases;
      suppose a >= pP;
        then A c= PRAWY by A104,TOPREALA:17;
        then A in ZAW by A100,A98,A19,A67,A65;
        hence thesis by A98,XBOOLE_0:def 4;
      end;
      suppose a < pP;
        then PRAWY c= A by A104,TOPREALA:17;
        then PRAWY in ZAW by A100,A98,A19,A67,A65;
        hence thesis by A67,A65,XBOOLE_0:def 4;
      end;
    end;
    suppose ex a, b being real number st r <= a & a < b & b <= s &
      A = ].a,b.[;
      then consider a, b being real number such that
A105:   r <= a & a < b & b <= s & A = ].a,b.[;
      reconsider a, b as Real by XREAL_0:def 1;
      take a, b;
      thus thesis by A105;
    end;
  end;
A106: for A being Subset of L st A in G1 & sup A in A holds A = PRAWY
  proof
    let A be Subset of L such that
A107: A in G1 and
A108: sup A in A and
A109: A <> PRAWY;
    A <> LEWY by A108,A59,RCOMP_2:3;
    then consider a, b being Real such that
A110: a < b & A = ].a,b.[ by A107,A109,A97;
    sup A = b by A110,TOPREAL6:22;
    hence contradiction by A108,A110,JORDAN6:45;
  end;

:: konstrukcja ciagu
  defpred F[set,set,set] means
    ex S being Element of X st S = $2 & sup S in $3;
A111:  for n being Nat st 1 <= n & n < card X
      for x being Element of X ex y being Element of X st F[n,x,y]
  proof
    let n be Nat such that 1 <= n & n < card X;
    let x be Element of X;
    reconsider x1 = x as Subset of REAL;
A112: x1 is non empty by A47;
A113: x c= union X by ZFMISC_1:92;
    then x c= [.r,s.] by A4,A11,A37,TOPMETR:25;
    then x1 is bounded_above by RCOMP_1:4;
    then sup x is Element of L by A10,A112,A113,Th2,A11,A37;
    then consider y being set such that
A114:   sup x in y and
A115:   y in X by A11,A37,TARSKI:def 4;
    reconsider y as Element of X by A115;
    take y, x;
    thus thesis by A114;
  end;
  reconsider LLEWY = LEWY as Element of X by A38,A71,A39,A36;
  consider IT being FinSequence of X such that
A116: len IT = card X and
A117: IT.1 = LLEWY or card X = 0 and
A118: for n being Nat st 1 <= n & n < card X holds F[n,IT.n,IT.(n+1)]
      from RECDEF_1:sch 6(A111);
A119: rng IT c= X;
  rng IT c= bool REAL by XBOOLE_1:1;
  then reconsider IT as FinSequence of bool REAL by FINSEQ_1:def 4;
  take IT;
  thus rng IT c= F by A46,A119,XBOOLE_1:1;
  card X <> 0 by CARD_2:59;
  then
A120: IT is non empty by A116,FINSEQ_1:25;
    then
A121: rng IT is non empty by FINSEQ_1:27;
    then
A122: 1 in dom IT by FINSEQ_3:34;
    then
A123: IT/.1 = IT.1 by FINSEQ_4:def 4;
A124: len IT in dom IT by A120,FINSEQ_5:6;
A125: for n being natural number st n in dom IT holds
      IT.n in X & IT/.n in X
  proof
    let n be natural number such that
A126: n in dom IT;
A127: IT.n = IT/.n by A126,FINSEQ_4:def 4;
    IT.n in rng IT by A126,FUNCT_1:def 5;
    hence thesis by A119,A127;
  end;
A128: for n being natural number st n in dom IT holds
      IT.n in G1 & IT/.n in G1
  proof
    let n be natural number such that
A129: n in dom IT;
A130: IT.n = IT/.n by A129,FINSEQ_4:def 4;
    IT.n in X by A129,A125;
    hence thesis by A130,A36;
end;

A131: for i being natural number st i in dom IT
for x being Point of L st x < sup(IT/.i)
ex j being natural number st j in dom IT & j <= i & x in IT/.j
proof
  let i be natural number such that
A132: i in dom IT;
 defpred Q[natural number] means
  $1 in dom IT implies for x being Point of L st x < sup(IT/.$1)
  ex j being natural number st j in dom IT & j <= $1 & x in IT/.j;
A133: Q[0] by FINSEQ_3:26;
A134: Q[n] implies Q[n+1]
  proof
    assume that
A135: Q[n] and
A136: n+1 in dom IT;
    let x be Point of L such that
A137: x < sup(IT/.(n+1));
A138: IT/.(n+1) = IT.(n+1) by A136,FINSEQ_4:def 4;
    per cases by A136,TOPREALA:23;
    suppose
A139:   n = 0;
      take 1;
      thus 1 in dom IT by A121,FINSEQ_3:34;
      thus 1 <= n+1 by A139;
      r <= x by A10,TOPREAL5:1;
      hence x in IT/.1 by A139,A59,A137,A117,A138,CARD_2:59,RCOMP_2:3;
    end;
    suppose
A140:   n in dom IT;
      then
A141:   IT/.n = IT.n by FINSEQ_4:def 4;
A142:  IT/.(n+1) in G1 by A136,A128;
A143:  IT/.(n+1) in X by A136,A125;
A144:  IT/.(n+1) is connected Subset of REAL by A142,A28;
A145:  IT/.(n+1) is non empty by A21,A136,A128;
A146:  IT/.(n+1) is bounded_below by A143,A18;
A147:  1 <= n by A140,FINSEQ_3:27;
      n+1 <= len IT by A136,FINSEQ_3:27;
      then n < len IT by NAT_1:38;
      then
A148:  ex S being Element of X st S = IT.n & sup S in IT.(n+1)
      by A147,A118,A140,A116;
      then
A149:  inf(IT/.(n+1)) <= sup(IT/.n) by A138,A141,A146,SEQ_4:def 5;
      per cases by REAL_1:def 5;
      suppose x < sup(IT/.n);
        then consider j being natural number such that
A150:     j in dom IT and
A151:    j <= n and
A152:    x in IT/.j by A135,A140;
        take j;
        thus j in dom IT by A150;
        j+0 < n+1 by A151,XREAL_1:10;
        hence j <= n+1;
        thus x in IT/.j by A152;
      end;
      suppose
A153:    x = sup(IT/.n);
        take n+1;
        thus n+1 in dom IT by A136;
        thus n+1 <= n+1;
        thus x in IT/.(n+1) by A148,A138,A140,A153,FINSEQ_4:def 4;
      end;
      suppose
A154:    x > sup(IT/.n);
        take n+1;
        thus n+1 in dom IT by A136;
        thus n+1 <= n+1;
        inf(IT/.(n+1)) < x by A154,A149,XREAL_1:2;
        hence x in IT/.(n+1) by A137,A144,A145,Th42;
      end;
    end;
  end;
  Q[n] from NAT_1:sch 2(A133,A134);
  hence thesis by A132;
end;

A155: for i being natural number st i in dom IT
for j being natural number st j in dom IT & i < j
ex y being Point of L st y in IT/.j &
for x being Point of L st x in IT/.i holds x < y
proof
  let i be natural number such that
A156: i in dom IT;
  defpred W[natural number] means $1 in dom IT & i < $1 implies
  ex y being Point of L st y in IT/.$1 &
  for x being Point of L st x in IT/.i holds x < y;
A157: W[0] by FINSEQ_3:26;
A158: for n holds W[n] implies W[n+1]
  proof
    let n;
    assume that
A159: W[n] and
A160: n+1 in dom IT;
A161: IT/.(n+1) = IT.(n+1) by A160,FINSEQ_4:def 4;
    assume i < n+1;
    then
A162: i <= n by NAT_1:38;
    per cases by A160,TOPREALA:23;
    suppose n = 0;
      then i = 0 by A162,NAT_1:18;
      hence thesis by A156,FINSEQ_3:26;
    end;
    suppose
A163: n in dom IT;
    then
A164: IT/.n = IT.n by FINSEQ_4:def 4;
    then
A165: IT/.n in rng IT by A163,FUNCT_1:def 5;
A166: IT/.n in X by A163,A125;
    then
A167: IT/.n is bounded_above by A18;
A168: IT/.n is non empty by A165,A119,A47;
A169: IT/.n is Subset of L by A165,A119,A47;
A170: n is Nat by ORDINAL2:def 21;
A171: 1 <= n by A163,FINSEQ_3:27;
A172: n+1 <= len IT by A160,FINSEQ_3:27;
     then n < card X by A116,NAT_1:38;
     then consider S being Element of X such that
A173: S = IT.n and
A174: sup S in IT.(n+1) by A118,A170,A171;
A175: IT/.(n+1) in rng IT by A160,A161,FUNCT_1:def 5;
A176: IT/.(n+1) in X by A160,A125;
A177: IT/.(n+1) is open connected Subset of L by A175,A119,A47;
     then
A178: IT/.(n+1) is connected Subset of REAL by Th56;
A179: IT/.(n+1) is bounded_below by A177,A18;
A180: IT/.(n+1) is non empty by A175,A119,A47;
A181: IT/.(n+1) is bounded_above by A177,A18;
     then
A182: sup S <= sup(IT/.(n+1)) by A174,A161,SEQ_4:def 4;
A183: inf(IT/.(n+1)) <= sup S by A174,A161,A179,SEQ_4:def 5;
     sup(IT/.(n+1)) in [.r,s.] by A177,A10,A180,A181,Th2;
     then
A184: sup(IT/.(n+1)) <= s by TOPREAL5:1;
     sup(IT/.n) in [.r,s.] by A168,A167,Th2,A169,A10;
     then
A185: sup(IT/.n) <= s by TOPREAL5:1;
    per cases by A162,REAL_1:def 5;
    suppose i < n;
      then consider y being Point of L such that
A186:   y in IT/.n and
A187:   for x being Point of L st x in IT/.i holds x < y by A159,A163;
A188:  y <= sup(IT/.n) by A186,A167,SEQ_4:def 4;
      per cases by A182,REAL_1:def 5;
      suppose
A189:    sup S < sup(IT/.(n+1));
        set y1 = (sup S + sup(IT/.(n+1))) / 2;
A190:    sup S < y1 by A189,TOPREAL3:3;
A191:    y1 < sup(IT/.(n+1)) by A189,TOPREAL3:3;
        then
A192:    y1 < s by A184,XREAL_1:2;
        sup S in [.r,s.] by A10,A173,A164,A169,A167,A168,Th2;
        then r <= sup S by TOPREAL5:1;
        then r <= y1 by A190,XREAL_1:2;
        then reconsider y1 as Point of L by A10,A192,TOPREAL5:1;
        take y1;
        inf(IT/.(n+1)) < y1 by A190,A183,XREAL_1:2;
        hence y1 in IT/.(n+1) by A178,A180,A191,Th42;
        let x be Point of L;
        assume x in IT/.i;
        then x < y by A187;
        then x < sup(IT/.n) by A188,XREAL_1:2;
        hence x < y1 by A164,A190,A173,XREAL_1:2;
      end;
      suppose sup S = sup(IT/.(n+1));
        then
A193:    IT/.(n+1) = PRAWY by A174,A161,A176,A36,A106;
        reconsider y1 = s as Point of L by A10,A4,TOPREAL5:1;
        take y1;
        thus y1 in IT/.(n+1) by A193,A66,A67,RCOMP_2:4;
        let x be Point of L;
        assume x in IT/.i;
        then x < y by A187;
        then x < sup(IT/.n) by A188,XREAL_1:2;
        hence x < y1 by A185,XREAL_1:2;
      end;
    end;
    suppose
A194:  i = n;
      reconsider y1 = sup(IT/.n) as Element of L by A168,A167,Th2,A169,A10;
      take y1;
      thus y1 in IT/.(n+1) by A161,A163,A173,A174,FINSEQ_4:def 4;
      let x be Point of L;
      assume
A195:  x in IT/.i;
      then
A196:  x <= sup(IT/.n) by A194,A167,SEQ_4:def 4;
      now
        assume x = sup(IT/.n);
        then
A197:    IT/.n = PRAWY by A166,A36,A106,A195,A194;
        set IT1 = IT|Seg n;
A198:     rng IT1 c= rng IT by RELAT_1:99;
        rng IT1 c= bool the carrier of L
        proof
          let A be set;
          assume A in rng IT1;
          then A in rng IT by A198;
          then A in X by A119;
          hence thesis;
        end;
        then reconsider FI = rng IT1 as Subset-Family of L by SETFAM_1:def 7;
A199:    now
          assume
A200:       FI <> X;
A201:       FI c= X by A119,A198,XBOOLE_1:1;
          then
A202:       FI c= G1 by A36,XBOOLE_1:1;
          union FI = the carrier of L
          proof
            thus union FI c= the carrier of L;
            let l be set;
            assume l in the carrier of L;
            then reconsider l as Point of L;
            per cases;
            suppose l < sup(IT/.n);
              then consider j being natural number such that
A203:           j in dom IT and
A204:           j <= n and
A205:           l in IT/.j by A163,A131;
A206:           IT.j = IT/.j by A203,FINSEQ_4:def 4;
              1 <= j by A203,FINSEQ_3:27;
              then j in Seg n by A204,FINSEQ_1:3;
              then IT.j in FI by A203,FUNCT_1:73;
              hence thesis by A205,A206,TARSKI:def 4;
            end;
            suppose
A207:           l >= sup(IT/.n);
              l <= s by A10,TOPREAL5:1;
              then
A208:           l = s by A197,A69,A207,XREAL_1:1;
              n in Seg n by A171,FINSEQ_1:3;
              then IT.n in FI by A163,FUNCT_1:73;
              hence thesis by A67,A68,A164,A197,A208,TARSKI:def 4;
            end;
          end;
          then FI is_a_cover_of L by A11,PRE_TOPC:def 8;
          then
A209:       FI in ALL by A202;
          then [FI,M] in R by A201,A33,A31,A34,WELLORD2:def 1;
          hence contradiction by A200,A209,A32,A31,A34,ORDERS_1:def 12;
        end;
A210:    card rng IT1 <= card dom IT1 by GRAPH_5:1;
        Seg n c= dom IT
        proof
          let x be set;
          assume
A211:       x in Seg n;
          then reconsider x as Nat;
A212:       1 <= x by A211,FINSEQ_1:3;
A213:       x <= n by A211,FINSEQ_1:3;
          n+0 <= n+1 by XREAL_1:8;
          then x <= n+1 by A213,XREAL_1:2;
          then x <= len IT by A172,XREAL_1:2;
          hence thesis by A212,FINSEQ_3:27;
        end;
        then dom IT1 = Seg n by RELAT_1:91;
        then card dom IT1 = n by FINSEQ_1:78;
        then n+1 <= n+0 by A116,A172,A199,A210,XREAL_1:2;
        hence contradiction by XREAL_1:8;
      end;
      hence x < y1 by A196,REAL_1:def 5;
    end;
  end;
  end;
  for n holds W[n] from NAT_1:sch 2(A157,A158);
  hence thesis;
end;

A214: IT is Function of dom IT,X by A119,FUNCT_2:4;
     dom IT = Seg len IT by FINSEQ_1:def 3;
     then
A215: card dom IT = card X by A116,FINSEQ_1:78;
A216: IT is one-to-one
     proof
       let i, j be set such that
A217:    i in dom IT and
A218:    j in dom IT and
A219:    IT.i = IT.j;
       assume
A220:    i <> j;
A221:    IT/.i = IT.i & IT/.j = IT.j by A217,A218,FINSEQ_4:def 4;
       reconsider i, j as Nat by A217,A218;
       per cases by A220,REAL_1:def 5;
       suppose i < j;
         then ex y being Point of L st y in IT/.j &
         for x being Point of L st x in IT/.i holds x < y by A217,A218,A155;
         hence thesis by A221,A219;
       end;
       suppose j < i;
         then ex y being Point of L st y in IT/.i &
         for x being Point of L st x in IT/.j holds x < y by A217,A218,A155;
         hence thesis by A221,A219;
       end;
     end;
     then
A222: rng IT = X by A215,A214,FINSEQ_4:78;
  hence union rng IT = [.r,s.] by A4,A11,A37,TOPMETR:25;
A223: for i, j being natural number st i in dom IT & j in dom IT & i <> j holds
     IT/.i <> IT/.j
     proof
       let i, j be natural number such that
A224:      i in dom IT and
A225:      j in dom IT and
A226:      i <> j;
       IT/.i = IT.i & IT/.j = IT.j by A224,A225,FINSEQ_4:def 4;
       hence IT/.i <> IT/.j by A224,A225,A226,A216,FUNCT_1:def 8;
     end;
     ex Z being set st s in Z & Z in C by A37,A9,A11,A10,TARSKI:def 4;
     then PRAWY in X by A36,A84;
     then consider i being set such that
A227: i in dom IT and
A228: IT.i = PRAWY by A222,FUNCT_1:def 5;
     reconsider i as Nat by A227;
A229: 1 <= i by A227,FINSEQ_3:27;
A230: i <= len IT by A227,FINSEQ_3:27;
A231: IT/.i = IT.i by A227,FINSEQ_4:def 4;
A232: now
       assume i <> len IT;
       then
A233:  i < len IT by A230,REAL_1:def 5;
       then
A234:   ex S being Element of X st S = IT.i & sup S in IT.(i+1)
       by A116,A118,A229;
       0 <= i by NAT_1:18;
       then
A235:  0+1 <= i+1 by XREAL_1:8;
       i+1 <= len IT by A233,NAT_1:38;
       then
A236:  i+1 in dom IT by A235,FINSEQ_3:27;
       then
A237:   IT/.(i+1) = IT.(i+1) by FINSEQ_4:def 4;
       IT/.(i+1) in G1 by A236,A128;
       then IT/.(i+1) = PRAWY by A69,A228,A234,A237,A84;
       then i+0 = i+1 by A228,A231,A223,A227,A236;
       hence contradiction by XCMPLX_1:2;
     end;
A238: for n being natural number st 1 < n & n < len IT
     ex a, b being Real st r <= a & a < b & b <= s & IT/.n = ].a,b.[
  proof
    let n be natural number such that
A239:   1 < n and
A240:   n < len IT;
A241: n in dom IT by A239,A240,FINSEQ_3:27;
    then IT.n in rng IT by FUNCT_1:def 5;
    then
A242: IT/.n in rng IT by A241,FINSEQ_4:def 4;
    then
A243: IT/.n in X by A119;
    then
A244: IT/.n in G1 by A36;
A245: IT/.n in F by A243,A46;
A246: IT/.n is open connected Subset of L by A242,A119,A47;
    per cases by A246,A4,A244,A245,A7,A21,Th57;
    suppose ex a being real number st r < a & a <= s & IT/.n = [.r,a.[;
      then consider a being real number such that
A247:     r < a and a <= s and
A248:    IT/.n = [.r,a.[;
      r in [.r,a.[ by A247,RCOMP_2:3;
      then IT/.n = LEWY by A243,A36,A248,A71;
      hence thesis by A239,A241,A122,A117,A123,A223,CARD_2:59;
    end;
    suppose ex a being real number st r <= a & a < s & IT/.n = ].a,s.];
      then consider a being real number such that
        r <= a and
A249:     a < s and
A250:    IT/.n = ].a,s.];
A251:  sup ].a,s.] = s by A249,Th7;
      s in ].a,s.] by A249,RCOMP_2:4;
      then IT/.n = PRAWY by A243,A250,A251,A106,A36;
      hence thesis by A240,A241,A124,A232,A223,A228,A231;
    end;
    suppose ex a, b being real number st r <= a & a < b & b <= s &
      IT/.n = ].a,b.[;
      then consider a, b being real number such that
A252:   r <= a & a < b & b <= s & IT/.n = ].a,b.[;
      reconsider a, b as Real by XREAL_0:def 1;
      take a, b;
      thus thesis by A252;
    end;
  end;

:: teza
A253: now
  let n be natural number such that
A254:  1 <= n;
  reconsider m = n as Nat by ORDINAL2:def 21;
  hereby
    assume
A255:   n <= len IT;
    then
A256: m in dom IT by A254,FINSEQ_3:27;
    then IT/.n = IT.n by A254,A255,FINSEQ_4:24;
    then IT/.n in rng IT by A256,FUNCT_1:def 5;
    then IT/.n in X by A119;
    hence IT/.n is non empty by A21,A36;
  end;
  hereby
    assume
A257:   n+1 <= len IT;
    then
A258: m < len IT by NAT_1:38;
    then
A259: ex S being Element of X st S = IT.n & sup S in IT.(n+1)
      by A116,A118,A254;
A261: IT/.n = IT.n by A254,A258,FINSEQ_4:24;
A262: 1 < m+1 by A254,NAT_1:38;
    then
A263: IT/.(m+1) = IT.(m+1) by A257,FINSEQ_4:24;
A264: m in dom IT by A254,A258,FINSEQ_3:27;
    then IT/.n in rng IT by A261,FUNCT_1:def 5;
    then
A265: IT/.n in X by A119;
    then
A266: IT/.n in G1 by A36;
A267: n+1 in dom IT by A262,A257,FINSEQ_3:27;
    then
A268: IT/.(n+1) in rng IT by A263,FUNCT_1:def 5;
    then
A269: IT/.(n+1) in X by A119;
    then
A270: IT/.(n+1) in G1 by A36;
    n+0 < n+1 by XREAL_1:8;
    then
A271: IT/.n <> IT/.(n+1) by A223,A267,A264;
A272: IT/.n is non empty bounded connected Subset of REAL by A265,A36,A28,A21,
A18;
A273: IT/.(n+1) is non empty bounded connected Subset of REAL
    by A269,A36,A28,A21,A18;
    IT/.(n+1) c= union X by A268,A119,ZFMISC_1:92;
    then IT/.(n+1) c= [.r,s.] by A4,A11,A37,TOPMETR:25;
    then
A274: IT/.(n+1) is bounded_above by RCOMP_1:4;
    then
A275: sup(IT/.n) <= sup(IT/.(n+1)) by A259,A261,A263,SEQ_4:def 4;
    hereby
      assume
A276:  inf(IT/.n) > inf(IT/.(n+1));
      sup(IT/.(n+1)) = sup(IT/.n) & sup(IT/.n) in IT/.n implies
      sup(IT/.(n+1)) in IT/.(n+1)
      proof
        assume sup(IT/.(n+1)) = sup(IT/.n);
        assume sup(IT/.n) in IT/.n;
        then IT/.n = PRAWY by A265,A36,A106;
        hence thesis by A124,A232,A223,A264,A258,A228,A231;
      end;
      then IT/.n c= IT/.(n+1) by A275,A273,A272,A276,Th43;
      then IT/.n in ZAW by A266,A270,A19,A271;
      hence contradiction by A265,A36,XBOOLE_0:def 4;
    end;
    thus sup(IT/.n) <= sup(IT/.(n+1)) by A274,A259,A261,A263,SEQ_4:def 4;
    per cases by A257,REAL_1:def 5;
    suppose
A277:   n+1 = len IT;
      then pP < sup(IT/.n) by A259,A261,A232,A228,RCOMP_2:4;
      hence inf(IT/.(n+1)) < sup(IT/.n) by A277,A232,A228,A231,A64,A66,Th6;
    end;
    suppose n+1 < len IT;
      then consider a1, b1 being Real such that
      r <= a1 and
A278:   a1 < b1 and
      b1 <= s and
A279:   IT/.(n+1) = ].a1,b1.[ by A262,A238;
      a1 < sup(IT/.n) by A259,A261,A263,A279,JORDAN6:45;
      hence inf(IT/.(n+1)) < sup(IT/.n) by A278,A279,TOPREAL6:22;
    end;
  end;
  end;
  hereby
  let n be natural number such that
A280: 1 <= n;
  thus
A281: (n <= len IT implies IT/.n is non empty) &
     (n+1 <= len IT implies
       inf(IT/.n) <= inf(IT/.(n+1)) &
       sup(IT/.n) <= sup(IT/.(n+1)) &
       inf(IT/.(n+1)) < sup(IT/.n)) by A280,A253;
  assume
A282: n+2 <= len IT;
  assume
A283: sup(IT/.n) > inf(IT/.(n+2));
  reconsider m = n as Nat by ORDINAL2:def 21;
  0 <= n by NAT_1:18;
  then 0+1 <= n+1 by XREAL_1:8;
  then
A284: sup(IT/.(n+1)) <= sup(IT/.(n+1+1)) by A282,A253;
A285: n+0 < n+1 by XREAL_1:8;
  then
A286: 1 < m+1 by A280,XREAL_1:2;
A287: n+1+1 <= len IT by A282;
  then
A288: m+1 < len IT by NAT_1:38;
  then consider a1, b1 being Real such that
  r <= a1 and
A289:  a1 < b1 and b1 <= s and
A290:  IT/.(n+1) = ].a1,b1.[ by A286,A238;
A291: sup ].a1,b1.[ = b1 by A289,TOPREAL6:22;
A292: inf ].a1,b1.[ = a1 by A289,TOPREAL6:22;
  m <= len IT by A285,A288,XREAL_1:2;
  then
A293: n in dom IT by A280,FINSEQ_3:27;
  then IT/.n = IT.n by FINSEQ_4:def 4;
  then IT/.n in rng IT by A293,FUNCT_1:def 5;
  then
A294: IT/.n in X by A119;
A295: n+1 in dom IT by A286,A288,FINSEQ_3:27;
  then IT/.(n+1) = IT.(n+1) by FINSEQ_4:def 4;
  then IT/.(n+1) in rng IT by A295,FUNCT_1:def 5;
  then
A296: IT/.(n+1) in X by A119;
  m+1 <= m+2 by XREAL_1:8;
  then 1 <= m+2 by A286,XREAL_1:2;
  then
A297: m+2 in dom IT by A282,FINSEQ_3:27;
  then IT/.(n+2) = IT.(n+2) by FINSEQ_4:def 4;
  then IT/.(n+2) in rng IT by A297,FUNCT_1:def 5;
  then
A298: IT/.(n+2) in X by A119;
  n+0 < n+1 by XREAL_1:8;
  then
A299: IT/.n <> IT/.(n+1) by A223,A295,A293;
  n+1 < n+2 by XREAL_1:8;
  then
A300: IT/.(n+2) <> IT/.(n+1) by A223,A295,A297;
  IT/.(n+1) c= IT/.n \/ IT/.(n+2)
  proof
    let x be set;
    assume
A301:   x in IT/.(n+1);
    then reconsider x as Real;
A302: a1 < x & x < b1 by A301,A290,JORDAN6:45;
    per cases;
    suppose
A303:  x < sup(IT/.n);
      per cases;
      suppose
A304:      n = 1;
        then inf(IT/.n) <= x by A123,A301,A10,A117,A58,A296
,TOPREAL5:1,CARD_2:59;
        then x in IT/.n by A304,A303,A123,A117,A58,A53,A56,RCOMP_2:3,CARD_2:59;
        hence thesis by XBOOLE_0:def 2;
      end;
      suppose n <> 1;
        then
A305:    1 < n by A280,REAL_1:def 5;
        n+0 < n+2 by XREAL_1:8;
        then n < len IT by A282,XREAL_1:2;
        then consider a, b being Real such that
        r <= a and
A306:      a < b and b <= s and
A307:      IT/.n = ].a,b.[ by A305,A238;
A308:    inf(IT/.n) = a by A306,A307,TOPREAL6:22;
A309:    sup(IT/.n) = b by A306,A307,TOPREAL6:22;
        inf(IT/.n) < x by A287,A290,A292,A302,A281,XREAL_1:2,NAT_1:38;
        then x in IT/.n by A303,A309,A307,A308,JORDAN6:45;
        hence thesis by XBOOLE_0:def 2;
      end;
    end;
    suppose x >= sup(IT/.n);
    then
A310: x > inf(IT/.(n+2)) by A283,XREAL_1:2;
    per cases;
    suppose
A311:  len IT = n+2;
      x <= s by A10,A301,A296,TOPREAL5:1;
      then x in IT/.(n+2) by A70,A310,A311,A232,A228,A231,RCOMP_2:4;
      hence thesis by XBOOLE_0:def 2;
    end;
    suppose len IT <> n+2;
      then
A312:  n+1+1 < len IT by A282,REAL_1:def 5;
      n+1 < n+2 by XREAL_1:8;
      then 1 < n+2 by A286,XREAL_1:2;
      then consider a2, b2 being Real such that
      r <= a2 and
A313:    a2 < b2 and b2 <= s and
A314:    IT/.(n+2) = ].a2,b2.[ by A312,A238;
A315:  sup ].a2,b2.[ = b2 by A313,TOPREAL6:22;
A316:  inf ].a2,b2.[ = a2 by A313,TOPREAL6:22;
      x < b2 by A284,A302,A290,A291,A314,A315,XREAL_1:2;
      then x in IT/.(n+2) by A314,A316,A310,JORDAN6:45;
      hence thesis by XBOOLE_0:def 2;
    end;
    end;
  end;
  hence contradiction by A294,A296,A298,A299,A300,A32,A33,A34,Th62;
  end;
  thus [.r,s.] in F implies IT = <*[.r,s.]*> by A7;
  assume not [.r,s.] in F;
  thus ex p being real number st r < p & p <= s & IT.1 = [.r,p.[
  proof
    take kL;
    thus r < kL by A55,A53,Th5;
    sup LEWY <= sup [.r,s.] by A10,SEQ_4:65;
    hence kL <= s by A59,A4,JORDAN5A:20;
    thus thesis by A117,CARD_2:59;
  end;
  thus ex p being real number st r <= p & p < s & IT.len IT = ].p,s.]
  proof
    take pP;
    inf [.r,s.] <= inf PRAWY by A10,SEQ_4:64;
    hence r <= pP by A70,A4,JORDAN5A:20;
    thus pP < s by A66,A64,Th6;
    thus thesis by A228,A232;
  end;
  let n be natural number such that
A317: 1 < n & n < len IT;
  consider a, b being Real such that
A318: r <= a and
A319: a < b and
A320: b <= s and
A321: IT/.n = ].a,b.[ by A317,A238;
  take a, b;
  n is Nat by ORDINAL2:def 21;
  hence thesis by A318,A319,A320,A321,A317,FINSEQ_4:24;
  end;
end;
end;

theorem
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  [.r,s.] in F implies
  <*[.r,s.]*> is IntervalCover of F
proof 
  assume that
A1:  F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2:  F is open and
A3:  F is connected and
A4:  r <= s and
A5:  [.r,s.] in F;
  set f = <*[.r,s.]*>;
  rng f c= F & union rng f = [.r,s.] &
  for n being natural number st 1 <= n holds
   (n <= len f implies f/.n is non empty) &
   (n+1 <= len f implies
    inf(f/.n) <= inf(f/.(n+1)) &
    sup(f/.n) <= sup(f/.(n+1)) &
    inf(f/.(n+1)) < sup(f/.n)) &
   (n+2 <= len f implies sup(f/.n) <= inf(f/.(n+2)))
     by A4,A5,Lm13;
  hence thesis by A3,A1,A2,A4,A5,Def2;
end;

reserve C for IntervalCover of F;

theorem Th64:
  for F being Subset-Family of Closed-Interval-TSpace(r,r),
  C being IntervalCover of F holds
  F is_a_cover_of Closed-Interval-TSpace(r,r) & F is open connected
  implies C = <*[.r,r.]*>
  proof 
    set L = Closed-Interval-TSpace(r,r);
    let F be Subset-Family of L,
    C be IntervalCover of F;
    assume that
A1: F is_a_cover_of L and
A2: F is open and
A3: F is connected;
A4: [.r,r.] = {r} by RCOMP_1:14;
    the carrier of L = [.r,r.] by TOPMETR:25;
    then r in the carrier of L by A4,TARSKI:def 1;
    then {r} in F by A1,Th60;
    hence thesis by A1,A2,A3,A4,Def2;
  end;

theorem Th65:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected &
  r <= s implies 1 <= len C
proof 
  assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3:  F is connected and
A4: r <= s;
A5: union rng C = [.r,s.] by A1,A2,A4,Def2,A3;
  assume not thesis;
  then len C+1 <= 0+1 by NAT_1:38;
  then len C <= 0 by XREAL_1:8;
  then len C = 0 by NAT_1:18;
  then C is empty by FINSEQ_1:25;
  hence thesis by A4,A5,RCOMP_1:15,RELAT_1:60,ZFMISC_1:2;
end;

theorem Th66:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected &
  r <= s & len C = 1 implies C = <*[.r,s.]*>
proof 
  assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: len C = 1;
A6: union rng C = [.r,s.] by A1,A2,A4,Def2,A3;
  C is non empty by A5,FINSEQ_1:25;
  then rng C is non empty by FINSEQ_1:27;
  then 1 in dom C by FINSEQ_3:34;
  then
A7: C.1 in rng C by FUNCT_1:def 5;
  C.1 = [.r,s.]
  proof
    thus for a being set st a in C.1 holds a in [.r,s.] by A6,A7,TARSKI:def 4;
    let a be set;
    assume a in [.r,s.];
    then consider Z being set such that
A8:   a in Z and
A9:   Z in rng C by A6,TARSKI:def 4;
A10: ex x being set st x in dom C & C.x = Z by A9,FUNCT_1:def 5;
    dom C = {1} by A5,FINSEQ_1:4,def 3;
    hence a in C.1 by A8,A10,TARSKI:def 1;
  end;
  hence thesis by A5,FINSEQ_1:57;
end;

theorem
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  n in dom C & m in dom C & n < m implies
  inf(C/.n) <= inf(C/.m)
proof 
  assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: n in dom C and
A6: m in dom C and
A7: n < m;
  defpred P[natural number] means
    $1 in dom C & n < $1 implies inf(C/.n) <= inf(C/.$1);
A8: P[0] by FINSEQ_3:26;
A9: for k being natural number st P[k] holds P[k+1]
  proof
    let k be natural number such that
A10:   P[k] and
A11:   k+1 in dom C and
A12:  n < k+1;
    per cases by A11,TOPREALA:23;
    suppose k = 0;
    then n <= 0 by A12,NAT_1:38;
    then n = 0 by NAT_1:18;
    hence thesis by A5,FINSEQ_3:26;
    end;
    suppose
A13:  k in dom C;
A14: n <= k by A12,NAT_1:38;
A15: 1 <= k by A13,FINSEQ_3:27;
    k+1 <= len C by A11,FINSEQ_3:27;
    then inf(C/.k) <= inf(C/.(k+1)) by A1,A2,A4,A15,Def2,A3;
    hence thesis by A14,A10,A13,REAL_1:def 5,XREAL_1:2;
    end;
  end;
  for k being natural number holds P[k] from NAT_1:sch 2(A8,A9);
  hence thesis by A6,A7;
end;

theorem
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  n in dom C & m in dom C & n < m implies
  sup(C/.n) <= sup(C/.m)
proof 
  assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: n in dom C and
A6: m in dom C and
A7: n < m;
  defpred P[natural number] means
    $1 in dom C & n < $1 implies sup(C/.n) <= sup(C/.$1);
A8: P[0] by FINSEQ_3:26;
A9: for k being natural number st P[k] holds P[k+1]
  proof
    let k be natural number such that
A10:   P[k] and
A11:   k+1 in dom C and
A12:  n < k+1;
    per cases by A11,TOPREALA:23;
    suppose k = 0;
    then n <= 0 by A12,NAT_1:38;
    then n = 0 by NAT_1:18;
    hence thesis by A5,FINSEQ_3:26;
    end;
    suppose
A13:  k in dom C;
A14: n <= k by A12,NAT_1:38;
A15: 1 <= k by A13,FINSEQ_3:27;
    k+1 <= len C by A11,FINSEQ_3:27;
    then sup(C/.k) <= sup(C/.(k+1)) by A1,A2,A4,A15,Def2,A3;
    hence thesis by A14,A10,A13,REAL_1:def 5,XREAL_1:2;
    end;
  end;
  for k being natural number holds P[k] from NAT_1:sch 2(A8,A9);
  hence thesis by A6,A7;
end;

theorem Th69:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 <= n & n+1 <= len C implies
  ].inf(C/.(n+1)),sup(C/.n).[ is non empty
proof 
  assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: 1 <= n and
A6: n+1 <= len C;
  inf(C/.(n+1)) < sup(C/.n) by A1,A2,A4,A5,A6,Def2,A3;
  hence ].inf(C/.(n+1)),sup(C/.n).[ is non empty by RCOMP_1:15;
end;

theorem
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies inf(C/.1) = r
  proof 
    assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
    1 <= len C by A1,A2,A3,A4,Th65;
    then
A5: C.1 = C/.1 by FINSEQ_4:24;
    per cases;
    suppose [.r,s.] in F;
      then C = <*[.r,s.]*> by A1,A2,A3,A4,Def2;
      then C/.1 = [.r,s.] by FINSEQ_4:25;
      hence thesis by A4,JORDAN5A:20;
    end;
    suppose not [.r,s.] in F;
      then ex p being real number st r < p & p <= s & C.1 = [.r,p.[
      by A1,A2,A3,A4,Def2;
      hence thesis by A5,Th4;
    end;
  end;

theorem Th71:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies r in C/.1
  proof 
    assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
    1 <= len C by A1,A2,A3,A4,Th65;
    then
A5: C.1 = C/.1 by FINSEQ_4:24;
    per cases;
    suppose [.r,s.] in F;
      then C = <*[.r,s.]*> by A1,A2,A3,A4,Def2;
      then C/.1 = [.r,s.] by FINSEQ_4:25;
      hence thesis by A4,TOPREAL5:1;
    end;
    suppose not [.r,s.] in F;
      then ex p being real number st r < p & p <= s & C.1 = [.r,p.[
      by A1,A2,A3,A4,Def2;
      hence thesis by A5,RCOMP_2:3;
    end;
  end;

theorem
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies sup(C/.len C) = s
  proof 
    assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
    1 <= len C by A1,A2,A3,A4,Th65;
    then
A5: C.len C = C/.len C by FINSEQ_4:24;
    per cases;
    suppose [.r,s.] in F;
      then
A6:   C = <*[.r,s.]*> by A1,A2,A3,A4,Def2;
      then
A7:   C/.1 = [.r,s.] by FINSEQ_4:25;
      len C = 1 by A6,FINSEQ_1:56;
      hence thesis by A4,A7,JORDAN5A:20;
    end;
    suppose not [.r,s.] in F;
      then ex p being real number st r <= p & p < s & C.len C = ].p,s.]
      by A1,A2,A3,A4,Def2;
      hence thesis by A5,Th7;
    end;
  end;

theorem Th73:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies s in C/.len C
  proof 
    assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
    1 <= len C by A1,A2,A3,A4,Th65;
    then
A5: C.len C = C/.len C by FINSEQ_4:24;
    per cases;
    suppose [.r,s.] in F;
      then
A6:   C = <*[.r,s.]*> by A1,A2,A3,A4,Def2;
      then
A7:   C/.1 = [.r,s.] by FINSEQ_4:25;
      len C = 1 by A6,FINSEQ_1:56;
      hence thesis by A4,A7,TOPREAL5:1;
    end;
    suppose not [.r,s.] in F;
      then ex p being real number st r <= p & p < s & C.len C = ].p,s.]
      by A1,A2,A3,A4,Def2;
      hence thesis by A5,RCOMP_2:4;
    end;
  end;

definition
  let r, s be real number;
  let F be Subset-Family of Closed-Interval-TSpace(r,s),
      C be IntervalCover of F such that
A1:   F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2:   F is open and
A3:   F is connected and
A4:   r <= s;
  mode IntervalCoverPts of C -> FinSequence of REAL means :Def3:
  len it = len C + 1 & it.1 = r & it.len it = s &
  for n being natural number st 1 <= n & n+1 < len it holds
   it.(n+1) in ].inf(C/.(n+1)),sup(C/.n).[;
existence
proof 
  set A = len C + 1;
  defpred P[natural number,set] means
  ($1 = 1 implies $2 = r) & ($1 = A implies $2 = s) &
  (2 <= $1 & $1 <= len C implies
    $2 in ].inf(C/.$1),sup(C/.($1-1)).[);
A5: 0+1 <= len C by A1,A2,A4,Th65,A3;
  then
A6: 0+1 < A by XREAL_1:8;
A7: for k being Nat st k in Seg A ex x being Element of REAL st P[k,x]
  proof
    let k be Nat;
    assume k in Seg A;
    then
A8: 1 <= k & k <= A by FINSEQ_1:3;
    reconsider r, s as Real by XREAL_0:def 1;
A9: len C + 0 < A by XREAL_1:8;
    per cases by A8,REAL_1:def 5;
    suppose
A10:   k = 1;
    take r;
    thus thesis by A10,A5,XREAL_1:8;
    end;
    suppose
A11:   k = A;
    take s;
    thus thesis by A11,A9,A1,A2,A4,Th65,A3;
    end;
    suppose that
A12:   1 < k and
A13:   k < A;
A14: k-1 is Nat by A12,INT_1:18;
    1-1 < k-1 by A12,XREAL_1:16;
    then
A15: 0+1 <= k-1 by A14,NAT_1:38;
    k <= len C by A13,NAT_1:38;
    then ].inf(C/.(k-1+1)),sup(C/.(k-1)).[ is non empty
      by A14,A15,A1,A2,A4,Th69,A3;
    then consider x being set such that
A16:   x in ].inf(C/.(k-1+1)),sup(C/.(k-1)).[ by XBOOLE_0:def 1;
    reconsider x as Real by A16;
    take x;
    thus P[k,x] by A16,A12,A13;
    end;
  end;
  consider p being FinSequence of REAL such that
A17: dom p = Seg A and
A18: for k being Nat st k in Seg A holds P[k,p.k] from FINSEQ_1:sch 5(A7);
  take p;
  thus
A19: len p = len C + 1 by A17,FINSEQ_1:def 3;
  1 in Seg A by A6,FINSEQ_1:3;
  hence p.1 = r by A18;
  len p in Seg A by A19,A6,FINSEQ_1:3;
  hence p.len p = s by A18,A19;
  let n be natural number such that
A20: 1 <= n;
  assume
A21: n+1 < len p;
  then
A22: n+1 <= len C by A19,NAT_1:38;
  0 <= n by NAT_1:18;
  then 0+1 <= n+1 by XREAL_1:8;
  then
A23: n+1 in Seg A by A21,A19,FINSEQ_1:3;
  1+1 <= n+1 by A20,XREAL_1:8;
  then p.(n+1) in ].inf(C/.(n+1)),sup(C/.(n+1-1)).[
    by A18,A23,A22;
  hence thesis;
end;
end;

reserve G for IntervalCoverPts of C;

theorem Th74:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s
  implies 2 <= len G
proof 
  assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  1 <= len C by A1,A2,A4,Th65,A3;
  then 1+1 <= len C + 1 by XREAL_1:8;
  hence thesis by A1,A2,A4,Def3,A3;
end;

theorem Th75:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  len C = 1 implies G = <*r,s*>
proof 
  assume that
A1: F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: len C = 1;
A6: len G = len C + 1 by A1,A2,A4,Def3,A3;
  G.1 = r & G.2 = s by A1,A2,A5,A6,A4,Def3,A3;
  hence thesis by A5,A6,FINSEQ_1:61;
end;

theorem Th76:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 <= n & n+1 < len G implies G.(n+1) < sup(C/.n)
proof 
  assume that
A1:  F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2:  F is open and
A3:  F is connected and
A4:  r <= s and
A5:  1 <= n and
A6:  n+1 < len G;
  G.(n+1) in ].inf(C/.(n+1)),sup(C/.n).[
    by A1,A2,A4,A5,A6,Def3,A3;
  hence G.(n+1) < sup(C/.n) by JORDAN6:45;
end;

theorem Th77:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 < n & n <= len C implies inf(C/.n) < G.n
proof 
  assume that
A1:  F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2:  F is open and
A3:  F is connected and
A4:  r <= s;
  set w = n-'1;
  assume that
A5: 1 < n and
A6: n <= len C;
A7: len G = len C + 1 by A1,A2,A4,Def3,A3;
  1-1 <= n-1 by A5,XREAL_1:11;
  then
A8: w = n-1 by BINARITH:def 3;
  then n = w+1;
  then
A9: 1 <= w by A5,NAT_1:38;
  n < len C + 1 by A6,NAT_1:38;
  then G.(w+1) in ].inf(C/.(w+1)),sup(C/.w).[
    by A1,A2,A8,A7,A9,A4,Def3,A3;
  hence thesis by A8,JORDAN6:45;
end;

theorem Th78:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 <= n & n < len C
  implies G.n <= inf(C/.(n+1))
proof 
  assume that
A1:  F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2:  F is open and
A3:  F is connected and
A4:  r <= s;
  set w = n-'1;
  assume that
A5: 1 <= n and
A6: n < len C;
A7: n+1 <= len C by A6,NAT_1:38;
  per cases by A5,REAL_1:def 5;
  suppose
A8: n = 1;
A9: G.1 = r by A1,A2,A4,Def3,A3;
A10: rng C c= F by A1,A2,A4,Def2,A3;
   0 <= n by NAT_1:18;
   then 0+1 <= n+1 by XREAL_1:8;
   then
A11: C/.(n+1) is non empty by A1,A2,A4,A7,Def2,A3;
  1+1 <= len C by A6,A8,NAT_1:38;
  then
A12: 2 in dom C by FINSEQ_3:27;
  then C.2 in rng C by FUNCT_1:def 5;
  then C.2 in F by A10;
  then C/.2 in F by A12,FINSEQ_4:def 4;
  then C/.(n+1) c= the carrier of Closed-Interval-TSpace(r,s) by A8;
  then
A13: C/.(n+1) c= [.r,s.] by A4,TOPMETR:25;
  then C/.(n+1) is bounded_below by RCOMP_1:3;
  then inf(C/.(n+1)) in [.r,s.] by A11,A13,Th1;
  hence thesis by A9,A8,TOPREAL5:1;
  end;
  suppose 1 < n;
  then
A14: 1-1 < n-1 by XREAL_1:11;
  then
A15: w = n-1 by BINARITH:def 3;
  then
A16: 0+1 <= w by A14,NAT_1:38;
  n-1 < n-0 by XREAL_1:17;
  then
A17: w+1 < n+1 by A15,XREAL_1:8;
     len G = len C + 1 by A1,A2,A4,Def3,A3;
  then n+1 < len G - 1+1 by A6,XREAL_1:8;
  then w+1 < len G by A17,XREAL_1:2;
  then
A18: G.(w+1) < sup(C/.w) by A1,A2,A4,A16,Th76,A3;
  n+1 <= len C by A6,NAT_1:38;
  then sup(C/.w) <= inf(C/.(w+2)) by A1,A2,A4,A16,A15,Def2,A3;
  hence thesis by A18,A15,XREAL_1:2;
  end;
end;

theorem Th79:
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r < s
  implies G is increasing
proof 
  assume that
A1:  F is_a_cover_of Closed-Interval-TSpace(r,s) and
A2:  F is open and
A3:  F is connected and
A4:  r < s;
  let m, n being Nat such that
A5: m in dom G and
A6: n in dom G and
A7: m < n;
  defpred P[Nat] means m < $1 & m in dom G & $1 in dom G implies G.m < G.$1;
A8: P[0] by FINSEQ_3:27;
A9: for k being Nat st P[k] holds P[k+1]
  proof
    let k be Nat such that
A10:   P[k] and
A11:   m < k+1 and
A12:   m in dom G and
A13:   k+1 in dom G;
A14: m <= k by A11,NAT_1:38;
A15: 1 <= m by A12,FINSEQ_3:27;
A16: 1 <= k by A14,A15,XREAL_1:2;
A17: k+1 <= len G by A13,FINSEQ_3:27;
    k+0 <= k+1 by XREAL_1:8;
    then
A18: k <= len G by A17,XREAL_1:2;
A19: len G = len C + 1 by A1,A2,A3,A4,Def3;
    per cases by A16,A17,REAL_1:def 5;
    suppose that
A20:   1 < k and
A21:   k+1 < len G;
    k < len C by A21,A19,XREAL_1:8;
    then
A22: G.k <= inf(C/.(k+1)) by A1,A2,A4,A20,Th78,A3;
    G.(k+1) in ].inf(C/.(k+1)),sup(C/.k).[
      by A3,A20,A1,A2,A4,A21,Def3;
    then inf(C/.(k+1)) < G.(k+1) by JORDAN6:45;
    then G.k < G.(k+1) by A22,XREAL_1:2;
    hence G.m < G.(k+1) by A14,A10,A12,A20,A18
,FINSEQ_3:27,REAL_1:def 5,XREAL_1:2;
    end;
    suppose
A23:   k = 1;
    then
A24: m <= 1 by A11,NAT_1:38;
A25: 1 <= len C by A1,A2,A4,Th65,A3;
    per cases by A25,REAL_1:def 5;
    suppose
A26:   1 < len C;
    then 1+1 <= len C by NAT_1:38;
    then
A27: inf(C/.2) < G.2 by A1,A2,A4,Th77,A3;
    G.1 <= inf(C/.(1+1)) by A1,A2,A4,A26,Th78,A3;
    then G.1 < G.2 by A27,XREAL_1:2;
    hence thesis by A23,A24,A15,XREAL_1:1;
    end;
    suppose 1 = len C;
    then G = <*r,s*> by A1,A2,A4,Th75,A3;
    then G.1 = r & G.2 = s by FINSEQ_1:61;
    hence thesis by A23,A24,A15,A4,XREAL_1:1;
    end;
    end;
    suppose
A28:   k+1 = len G;
    then
A29: G.(k+1) = s by A1,A2,A4,Def3,A3;
    per cases by A15,REAL_1:def 5;
    suppose
A30:   1 < m;
    set z = m-'1;
    1-1 <= m-1 by A15,XREAL_1:11;
    then
A31: z = m-1 by BINARITH:def 3;
    1+1 <= m by A30,NAT_1:38;
    then
A32: 1+1-1 <= m-1 by XREAL_1:11;
    then
A33: 1 <= z by BINARITH:def 3;
A34: z+1 < len G by A31,A11,A28;
    then
A35: G.m < sup(C/.z) by A1,A2,A4,A31,Th76,A32,A3;
A36: rng C c= F by A1,A2,A4,Def2,A3;
A37: z <= len C by A19,A34,XREAL_1:8;
    then
A38: z in dom C by A33,FINSEQ_3:27;
    then C.z in rng C by FUNCT_1:def 5;
    then C.z in F by A36;
    then C/.z in F by A38,FINSEQ_4:def 4;
    then C/.z c= the carrier of Closed-Interval-TSpace(r,s);
    then
A39: C/.z c= [.r,s.] by A4,TOPMETR:25;
A40: C/.z is non empty by A1,A2,A4,A33,A37,Def2,A3;
    C/.z is bounded_above by A39,RCOMP_1:4;
    then sup(C/.z) in [.r,s.] by A40,A39,Th2;
    then sup(C/.z) <= s by TOPREAL5:1;
    hence G.m < G.(k+1) by A35,A29,XREAL_1:2;
    end;
    suppose m = 1;
    hence thesis by A4,A29,A1,A2,Def3,A3;
    end;
    end;
  end;
  for k being Nat holds P[k] from NAT_1:sch 1(A8,A9);
  hence thesis by A7,A5,A6;
end;

theorem
  F is_a_cover_of Closed-Interval-TSpace(r,s) & F is open connected & r <= s &
  1 <= n & n < len G implies [.G.n,G.(n+1).] c= C.n
  proof 
  set L = Closed-Interval-TSpace(r,s);
  assume that
A1: F is_a_cover_of L and
A2: F is open and
A3: F is connected and
A4: r <= s and
A5: 1 <= n and
A6: n < len G;
A7: n+1 <= len G by A6,NAT_1:38;
A8: n is Nat by ORDINAL2:def 21;
A9: n in dom G by A5,A6,FINSEQ_3:27;
  0 <= n by NAT_1:18;
  then 0+1 <= n+1 by XREAL_1:8;
  then
A10: n+1 in dom G by A7,FINSEQ_3:27;
A11: len G = len C + 1 by A1,A2,A4,Def3,A3;
  then
A12: n <= len C by A6,NAT_1:38;
  then
A13: C/.n = C.n by A5,A8,FINSEQ_4:24;
  n in dom C by A5,A12,FINSEQ_3:27;
  then
A14: C.n in rng C by FUNCT_1:def 5;
  rng C c= F by A3,A1,A2,A4,Def2;
  then C/.n in F by A13,A14;
  then C/.n is connected Subset of L by A3,Def1;
  then
A15: C/.n is connected by Th56;
A16: C/.n is non empty by A3,A1,A2,A4,A5,A12,Def2;
A17: n+0 < n+1 by XREAL_1:8;
  per cases by A4,REAL_1:def 5;
  suppose r = s;
    then C = <*[.r,r.]*> by A1,A2,A3,Th64;
    then
A18: len C = 1 by FINSEQ_1:57;
    then G = <*r,s*> by A1,A2,A3,A4,Th75;
    then
A19: G.1 = r & G.2 = s by FINSEQ_1:61;
A20: n = 1 by A18,A12,A5,XREAL_1:1;
    C = <*[.r,s.]*> by A1,A2,A3,A4,A18,Th66;
    hence [.G.n,G.(n+1).] c= C.n by A20,A19,FINSEQ_1:57;
  end;
  suppose r < s;
  then G is increasing by A1,A2,Th79,A3;
  then
A21: G.n < G.(n+1) by A17,A9,A10,SEQM_3:def 1;
A22: 2 <= len G by A1,A2,A3,A4,Th74;
  per cases by A5,A7,A22,REAL_1:def 5;
  suppose that
A23: n = 1 and
A24: len G = 2;
A25: len C = 2 - 1 by A11,A24;
    then G = <*r,s*> by A1,A2,A3,A4,Th75;
    then
A26: G.1 = r & G.2 = s by FINSEQ_1:61;
    C = <*[.r,s.]*> by A1,A2,A3,A4,A25,Th66;
    hence [.G.n,G.(n+1).] c= C.n by A23,A26,FINSEQ_1:57;
  end;
  suppose that
A27: n = 1 and
A28: 1+1 < len G;
A29: G.(1+1) in ].inf(C/.(1+1)),sup(C/.1).[ by A1,A2,A4,Def3,A3,A28;
A30: G.1 = r by A1,A2,A4,Def3,A3;
A31: 1+1 <= len C by A28,A11,NAT_1:38;
A32: r in C/.1 by A1,A2,A4,A3,Th71;
A33: G.(n+1) < sup(C/.n) by A1,A2,A4,A28,A27,Th76,A3;
A34: inf(C/.1) <= inf(C/.(1+1)) by A1,A2,A4,A31,A3,Def2;
  inf(C/.(1+1)) < G.2 by A29,JORDAN6:45;
  then inf(C/.n) < G.(n+1) by A27,A34,XREAL_1:2;
  then G.(n+1) in C.n by A33,A13,A16,A15,Th42;
  hence [.G.n,G.(n+1).] c= C.n by A27,A30,A13,A32,A15,JCT_MISC:def 3;
  end;
  suppose that
A35: 1 < n and
A36: len G = n+1;
A37: G.len G = s by A1,A2,A4,Def3,A3;
A38: len C = n by A11,A36,XCMPLX_1:2;
  then
A39: inf(C/.n) < G.n by A1,A2,A4,A35,Th77,A3;
A40: 1-1 < n-1 by A35,XREAL_1:11;
  then
A41: 0+1 <= n-1 by INT_1:20;
A42: n-1 is Nat by A40,INT_1:16;
  then G.(n-1+1) in ].inf(C/.(n-1+1)),sup(C/.(n-1)).[
  by A1,A2,A4,A41,A36,A17,Def3,A3;
  then
A43: G.n < sup(C/.(n-1)) by JORDAN6:45;
  sup(C/.(n-1)) <= sup(C/.(n-1+1)) by A3,A38,A42,A1,A2,A4,A41,Def2;
  then G.n < sup(C/.n) by A43,XREAL_1:2;
   then
A44: G.n in C.n by A39,A13,A16,A15,Th42;
  G.(n+1) in C.n by A11,A13,A1,A2,A4,A3,A37,A38,Th73;
  hence [.G.n,G.(n+1).] c= C.n by A44,A13,A15,JCT_MISC:def 3;
  end;
  suppose that
A45: 1 < n and
A46: n+1 < len G;
  n <= len C by A6,A11,NAT_1:38;
  then
A47: inf(C/.n) < G.n by A1,A2,A4,A45,Th77,A3;
A48: G.(n+1) < sup(C/.n) by A1,A2,A4,A5,A46,Th76,A3;
  then G.n < sup(C/.n) by A21,XREAL_1:2;
  then
A49: G.n in C.n by A47,A13,A16,A15,Th42;
  inf(C/.n) < G.(n+1) by A47,A21,XREAL_1:2;
  then G.(n+1) in C.n by A48,A13,A16,A15,Th42;
  hence thesis by A13,A49,A15,JCT_MISC:def 3;
  end;
  end;
end;
