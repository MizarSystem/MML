:: Properties of Connected Subsets of the Real Line
::  by Artur Korni{\l}owicz
::
:: Received February 22, 2005
:: Copyright (c) 2005 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, MEMBERED, PRE_TOPC, METRIC_1,
      STRUCT_0, XXREAL_2, RCOMP_1, TARSKI, ORDINAL2, SEQ_4, TOPMETR, XREAL_0,
      ORDINAL1, XXREAL_1, RELAT_2, CARD_1, ARYTM_3, XXREAL_0, REAL_1, ARYTM_1,
      LIMFUNC1, ZFMISC_1, TOPS_1, CONVEX1, RELAT_1, SETFAM_1, FINSET_1,
      ORDERS_1, WELLORD1, FINSEQ_1, PARTFUN1, WELLORD2, FUNCT_1, NAT_1,
      RCOMP_3, MEASURE5;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, RELAT_1, SETFAM_1, FUNCT_1,
      RELSET_1, PARTFUN1, FUNCT_2, FINSET_1, WELLORD1, ORDERS_1, WELLORD2,
      RELAT_2, CARD_1, MEMBERED, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, REAL_1,
      NAT_1, NAT_D, LIMFUNC1, XXREAL_1, XXREAL_2, SEQM_3, FINSEQ_1, SEQ_4,
      RCOMP_1, STRUCT_0, PRE_TOPC, TOPS_1, METRIC_1, TOPS_2, COMPTS_1,
      CONNSP_1, TEX_2, TOPMETR, MEASURE6, BORSUK_6, TOPALG_2, TOPREALB;
 constructors SETFAM_1, WELLORD1, WELLORD2, NAT_1, ORDERS_1, SEQM_3, PROB_1,
      LIMFUNC1, BINARITH, TOPS_1, CONNSP_1, COMPTS_1, REALSET2, MEASURE6,
      JCT_MISC, BORSUK_4, TOPREALB, SEQ_1, XXREAL_2, BORSUK_6, NAT_D, SEQ_4;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      INT_1, MEMBERED, FINSEQ_1, RCOMP_1, FCONT_3, RFINSEQ, STRUCT_0, PRE_TOPC,
      TOPS_1, METRIC_1, TOPMETR, MEASURE6, BORSUK_2, SPRECT_1, TOPREAL6,
      BORSUK_4, TOPALG_2, TOPREALB, FINSET_1, VALUED_0, XXREAL_2, CARD_1,
      ZFMISC_1, REAL_1, BORSUK_6;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions FUNCT_1, TOPS_2, BORSUK_4, COMPTS_1, PRE_TOPC, RELAT_2, ORDERS_1,
      SEQ_4, TARSKI, XBOOLE_0, SEQM_3, CONNSP_1, TOPALG_2, SUBSET_1, WELLORD1,
      STRUCT_0, LIMFUNC1, PROB_1, SETFAM_1, TOPMETR, XXREAL_2, JCT_MISC;
 theorems PRE_TOPC, XREAL_0, FUNCT_2, INT_1, TOPS_2, FUNCT_1, TOPMETR, RELAT_1,
      TARSKI, ZFMISC_1, JORDAN5A, FINSEQ_1, XBOOLE_1, COMPTS_1, SETFAM_1,
      XBOOLE_0, FINSEQ_3, NAT_1, RCOMP_1, SEQM_3, FINSEQ_5, HEINE, TOPS_1,
      CONNSP_1, TOPREALA, TOPREALB, ORDINAL1, ORDERS_1, RELAT_2, CARD_2,
      MEMBERED, SEQ_4, FINSEQ_4, TOPREAL6, TREAL_1, REALSET1, BORSUK_5, TOPS_3,
      TEX_1, XREAL_1, METRIC_1, WELLORD2, XXREAL_0, XXREAL_1, PARTFUN1,
      XXREAL_2, MEASURE6;
 schemes NAT_1, RELSET_1, FINSET_1, FRAENKEL, FINSEQ_1, RECDEF_1;

begin :: Preliminaries

registration
  let X be non empty set;
  cluster [#]X -> non empty;
  coherence;
end;

registration
  cluster -> real-membered SubSpace of RealSpace;
  coherence
  proof
    let T be SubSpace of RealSpace;
    the carrier of T is Subset of RealSpace by TOPMETR:def 1;
    hence the carrier of T is real-membered by METRIC_1:def 14;
  end;
end;

theorem Th1:
  for X being non empty bounded_below (real-membered set), Y being
  closed Subset of REAL st X c= Y holds lower_bound X in Y
proof
  let X be non empty bounded_below (real-membered set);
  let Y be closed Subset of REAL;
  assume
A1: X c= Y;
  reconsider X as non empty bounded_below Subset of REAL by MEMBERED:3;
A2: lower_bound X = lower_bound Cl X & lower_bound Cl X in Cl X by RCOMP_1:31
,TOPREAL6:77;
  Cl X c= Y by A1,MEASURE6:93;
  hence thesis by A2;
end;

theorem Th2:
  for X being non empty bounded_above (real-membered set), Y being
  closed Subset of REAL st X c= Y holds upper_bound X in Y
proof
  let X be non empty bounded_above (real-membered set);
  let Y be closed Subset of REAL;
  assume
A1: X c= Y;
  reconsider X as non empty bounded_above Subset of REAL by MEMBERED:3;
A2: upper_bound X = upper_bound Cl X & upper_bound Cl X in Cl X by RCOMP_1:30
,TOPREAL6:78;
  Cl X c= Y by A1,MEASURE6:93;
  hence thesis by A2;
end;

theorem Th3:
  for X, Y being Subset of REAL holds Cl(X \/ Y) = Cl X \/ Cl Y
proof
  let X, Y be Subset of REAL;
  reconsider A = X, B = Y as Subset of R^1 by TOPMETR:24;
  thus Cl(X \/ Y) = Cl(A \/ B) by TOPREAL6:80
    .= Cl A \/ Cl B by PRE_TOPC:50
    .= Cl X \/ Cl B by TOPREAL6:80
    .= Cl X \/ Cl Y by TOPREAL6:80;
end;

begin :: Intervals

reserve a, b, r, s for real number;

registration
  let r, s;
  cluster [.r,s.[ -> bounded Subset of REAL;
  coherence
  proof
A1: [.r,s.[ is bounded_above
    proof
      take s;
      let x be ext-real number;
      thus thesis by XXREAL_1:3;
    end;
    [.r,s.[ is bounded_below
    proof
      take r;
      let x be ext-real number;
      thus thesis by XXREAL_1:3;
    end;
    hence thesis by A1;
  end;
  cluster ].r,s.] -> bounded Subset of REAL;
  coherence
  proof
A2: ].r,s.] is bounded_above
    proof
      take s;
      let x be ext-real number;
      thus thesis by XXREAL_1:2;
    end;
    ].r,s.] is bounded_below
    proof
      take r;
      let x be ext-real number;
      thus thesis by XXREAL_1:2;
    end;
    hence thesis by A2;
  end;
  cluster ].r,s.[ -> bounded Subset of REAL;
  coherence
  proof
A3: ].r,s.[ is bounded_above
    proof
      take s;
      let x be ext-real number;
      thus thesis by XXREAL_1:4;
    end;
    ].r,s.[ is bounded_below
    proof
      take r;
      let x be ext-real number;
      thus thesis by XXREAL_1:4;
    end;
    hence thesis by A3;
  end;
end;

registration
  cluster open bounded interval non empty Subset of REAL;
  existence
  proof
    take ].0,1.[;
    thus thesis;
  end;
end;

theorem Th4:
  r < s implies lower_bound [.r,s.[ = r
proof
  set X = [.r,s.[;
  assume
A1: r < s;
A2: for a st a in X holds r <= a by XXREAL_1:3;
  reconsider r, s as Real by XREAL_0:def 1;
A3: (r+s)/2 < s by A1,XREAL_1:228;
A4: r < (r+s)/2 by A1,XREAL_1:228;
A5: for b st 0 < b ex a st a in X & a < r+b
  proof
    let b such that
A6: 0 < b and
A7: for a st a in X holds a >= r+b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose
      r + b > s;
      then
A8:   (r+s)/2 < r+b by A3,XXREAL_0:2;
      (r+s)/2 in X by A4,A3,XXREAL_1:3;
      hence thesis by A7,A8;
    end;
    suppose
A9:   r + b <= s;
A10:  r < r + b by A6,XREAL_1:31;
      then (r+(r+b))/2 < r+b by XREAL_1:228;
      then
A11:  (r+(r+b))/2 < s by A9,XXREAL_0:2;
      r < (r+(r+b))/2 by A10,XREAL_1:228;
      then (r+(r+b))/2 in X by A11,XXREAL_1:3;
      hence thesis by A7,A10,XREAL_1:228;
    end;
  end;
  X is non empty by A1,XXREAL_1:3;
  hence thesis by A2,A5,SEQ_4:def 5;
end;

theorem Th5:
  r < s implies upper_bound [.r,s.[ = s
proof
  set X = [.r,s.[;
  assume
A1: r < s;
A2: for a st a in X holds a <= s by XXREAL_1:3;
  reconsider r, s as Real by XREAL_0:def 1;
A3: r < (r+s)/2 by A1,XREAL_1:228;
A4: (r+s)/2 < s by A1,XREAL_1:228;
A5: for b st 0 < b ex a st a in X & s-b < a
  proof
    let b such that
A6: 0 < b and
A7: for a st a in X holds a <= s-b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose
      s - b <= r;
      then
A8:   (r+s)/2 > s-b by A3,XXREAL_0:2;
      (r+s)/2 in X by A3,A4,XXREAL_1:3;
      hence thesis by A7,A8;
    end;
    suppose
A9:   s - b > r;
A10:  s - b < s - 0 by A6,XREAL_1:17;
      then s-b < (s+(s-b))/2 by XREAL_1:228;
      then
A11:  r < (s+(s-b))/2 by A9,XXREAL_0:2;
      (s+(s-b))/2 < s by A10,XREAL_1:228;
      then (s+(s-b))/2 in X by A11,XXREAL_1:3;
      hence thesis by A7,A10,XREAL_1:228;
    end;
  end;
  X is non empty by A1,XXREAL_1:3;
  hence thesis by A2,A5,SEQ_4:def 4;
end;

theorem Th6:
  r < s implies lower_bound ].r,s.] = r
proof
  set X = ].r,s.];
  assume
A1: r < s;
A2: for a st a in X holds r <= a by XXREAL_1:2;
  reconsider r, s as Real by XREAL_0:def 1;
A3: (r+s)/2 < s by A1,XREAL_1:228;
A4: r < (r+s)/2 by A1,XREAL_1:228;
A5: for b st 0 < b ex a st a in X & a < r+b
  proof
    let b such that
A6: 0 < b and
A7: for a st a in X holds a >= r+b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose
      r + b > s;
      then
A8:   (r+s)/2 < r+b by A3,XXREAL_0:2;
      (r+s)/2 in X by A4,A3,XXREAL_1:2;
      hence thesis by A7,A8;
    end;
    suppose
A9:   r + b <= s;
A10:  r < r + b by A6,XREAL_1:31;
      then (r+(r+b))/2 < r+b by XREAL_1:228;
      then
A11:  (r+(r+b))/2 < s by A9,XXREAL_0:2;
      r < (r+(r+b))/2 by A10,XREAL_1:228;
      then (r+(r+b))/2 in X by A11,XXREAL_1:2;
      hence thesis by A7,A10,XREAL_1:228;
    end;
  end;
  X is non empty by A1,XXREAL_1:2;
  hence thesis by A2,A5,SEQ_4:def 5;
end;

theorem Th7:
  r < s implies upper_bound ].r,s.] = s
proof
  set X = ].r,s.];
  assume
A1: r < s;
A2: for a st a in X holds a <= s by XXREAL_1:2;
  reconsider r, s as Real by XREAL_0:def 1;
A3: r < (r+s)/2 by A1,XREAL_1:228;
A4: (r+s)/2 < s by A1,XREAL_1:228;
A5: for b st 0 < b ex a st a in X & s-b < a
  proof
    let b such that
A6: 0 < b and
A7: for a st a in X holds a <= s-b;
    reconsider b as Real by XREAL_0:def 1;
    per cases;
    suppose
      s - b <= r;
      then
A8:   (r+s)/2 > s-b by A3,XXREAL_0:2;
      (r+s)/2 in X by A3,A4,XXREAL_1:2;
      hence thesis by A7,A8;
    end;
    suppose
A9:   s - b > r;
A10:  s - b < s - 0 by A6,XREAL_1:17;
      then s-b < (s+(s-b))/2 by XREAL_1:228;
      then
A11:  r < (s+(s-b))/2 by A9,XXREAL_0:2;
      (s+(s-b))/2 < s by A10,XREAL_1:228;
      then (s+(s-b))/2 in X by A11,XXREAL_1:2;
      hence thesis by A7,A10,XREAL_1:228;
    end;
  end;
  X is non empty by A1,XXREAL_1:2;
  hence thesis by A2,A5,SEQ_4:def 4;
end;

begin :: Halflines

canceled 12;

theorem Th20:
  a <= b implies [.a,b.] /\ (left_closed_halfline(a) \/
  right_closed_halfline(b)) = {a,b}
proof
  set A = left_closed_halfline(a);
  set B = right_closed_halfline(b);
  assume a <= b;
  then
A1: a in [.a,b.] & b in [.a,b.] by XXREAL_1:1;
  thus [.a,b.] /\ (A \/ B) c= {a,b}
  proof
    let x be set;
    assume
A2: x in [.a,b.] /\ (A \/ B);
    then reconsider x as Real;
    x in A \/ B by A2,XBOOLE_0:def 4;
    then x in A or x in B by XBOOLE_0:def 3;
    then
A3: x <= a or x >= b by XXREAL_1:234,236;
    x in [.a,b.] by A2,XBOOLE_0:def 4;
    then a <= x & x <= b by XXREAL_1:1;
    then x = a or x = b by A3,XXREAL_0:1;
    hence thesis by TARSKI:def 2;
  end;
  let x be set;
  a in A by XXREAL_1:234;
  then
A4: a in A \/ B by XBOOLE_0:def 3;
  b in B by XXREAL_1:236;
  then
A5: b in A \/ B by XBOOLE_0:def 3;
  assume x in {a,b};
  then x = a or x = b by TARSKI:def 2;
  hence thesis by A1,A4,A5,XBOOLE_0:def 4;
end;

Lm1: now
  let a;
  assume left_open_halfline(a) is bounded_below;
  then consider b such that
B1: b is LowerBound of left_open_halfline(a) by XXREAL_2:def 9;
A1: for r st r in left_open_halfline(a) holds b <= r by B1,XXREAL_2:def 2;
A2: a-1 < a-0 by XREAL_1:17;
  then a-1 in left_open_halfline(a) by XXREAL_1:233;
  then b-1 < a-1-0 by A1,XREAL_1:17;
  then b-1 < a by A2,XXREAL_0:2;
  then b-1 in left_open_halfline(a) by XXREAL_1:233;
  then b-0 <= b-1 by A1;
  hence contradiction by XREAL_1:17;
end;

Lm2: now
  let a;
  assume right_open_halfline(a) is bounded_above;
  then consider b such that
B1: b is UpperBound of right_open_halfline(a) by XXREAL_2:def 10;
A1: for r st r in right_open_halfline(a) holds r <= b by B1, XXREAL_2:def 1;
A2: a+0 < a+1 by XREAL_1:8;
  then a+1 in right_open_halfline(a) by XXREAL_1:235;
  then a+1 <= b by A1;
  then a+1+0 <= b+1 by XREAL_1:9;
  then a < b+1 by A2,XXREAL_0:2;
  then b+1 in right_open_halfline(a) by XXREAL_1:235;
  then b+1 <= b+0 by A1;
  hence contradiction by XREAL_1:8;
end;

registration
  let a;
  cluster left_closed_halfline(a) -> non bounded_below bounded_above interval;
  coherence
  proof
    set A = left_closed_halfline(a);
    left_open_halfline a is not bounded_below by Lm1;
    hence A is non bounded_below by XXREAL_1:21,XXREAL_2:44;
    thus A is bounded_above
    proof
      take a;
      let x be ext-real number;
      thus thesis by XXREAL_1:234;
    end;
    let r,s be ext-real number;
    assume
Z:    r in A & s in A;
     then reconsider rr=r, ss=s as Real;
A1: s <= a by Z,XXREAL_1:234;
    let x be set;
    assume
A2: x in [.r,s.];
    then x in [.rr,ss.];
    then reconsider x as Real;
    x <= s by A2,XXREAL_1:1;
    then x <= a by A1,XXREAL_0:2;
    hence thesis by XXREAL_1:234;
  end;
  cluster left_open_halfline(a) -> non bounded_below bounded_above interval;
  coherence
  proof
    set A = left_open_halfline(a);
    thus A is non bounded_below by Lm1;
    thus A is bounded_above
    proof
      take a;
      let x be ext-real number;
      thus thesis by XXREAL_1:233;
    end;
    let r, s be ext-real number;
    assume
Z1:  r in A;
    assume
Z2:    s in A;
    then
A3: s < a by XXREAL_1:233;
     reconsider rr=r, ss=s as Real by Z1,Z2;
    let x be set;
    assume
A4: x in [.r,s.];
    then x in [.rr,ss.];
    then reconsider x as Real;
    x <= s by A4,XXREAL_1:1;
    then x < a by A3,XXREAL_0:2;
    hence thesis by XXREAL_1:233;
  end;
  cluster right_closed_halfline(a) -> bounded_below non bounded_above
    interval;
  coherence
  proof
    set A = right_closed_halfline(a);
    thus A is bounded_below
    proof
      take a;
      let x be ext-real number;
      thus thesis by XXREAL_1:236;
    end;
    right_open_halfline a is not bounded_above by Lm2;
    hence A is non bounded_above by XXREAL_1:22,XXREAL_2:43;
    let r, s be ext-real number;
    assume
Z:   r in A;
    then
A5: a <= r by XXREAL_1:236;
    assume s in A;
     then reconsider rr=r, ss=s as Real by Z;
    let x be set;
    assume
A6: x in [.r,s.];
    then x in [.rr,ss.];
    then reconsider x as Real;
    r <= x by A6,XXREAL_1:1;
    then a <= x by A5,XXREAL_0:2;
    hence thesis by XXREAL_1:236;
  end;
  cluster right_open_halfline(a) -> bounded_below non bounded_above interval;
  coherence
  proof
    set A = right_open_halfline(a);
    thus A is bounded_below
    proof
      take a;
      let x be ext-real number;
      thus thesis by XXREAL_1:235;
    end;
    thus A is non bounded_above by Lm2;
    let r, s be ext-real number;
    assume
Z:   r in A;
    then
A7: a < r by XXREAL_1:235;
    assume s in A;
     then reconsider rr=r, ss=s as Real by Z;
    let x be set;
    assume
A8: x in [.r,s.];
    then x in [.rr,ss.];
    then reconsider x as Real;
    r <= x by A8,XXREAL_1:1;
    then a < x by A7,XXREAL_0:2;
    hence thesis by XXREAL_1:235;
  end;
end;

theorem Th21:
  upper_bound left_closed_halfline(a) = a
proof
  set X = left_closed_halfline(a);
A1: for s st 0 < s ex r st r in X & a-s < r
  proof
    let s;
    assume 0 < s;
    then
A2: a-s < a-0 by XREAL_1:17;
    take a;
    thus a in X by XXREAL_1:234;
    thus thesis by A2;
  end;
  for r st r in X holds r <= a by XXREAL_1:234;
  hence thesis by A1,SEQ_4:def 4;
end;

theorem Th22:
  upper_bound left_open_halfline(a) = a
proof
  set X = left_open_halfline(a);
A1: for s st 0 < s ex r st r in X & a-s < r
  proof
    let s;
    assume 0 < s;
    then
A2: a-s < a-0 by XREAL_1:17;
    take (a-s+a)/2;
    (a-s+a)/2 < a by A2,XREAL_1:228;
    hence thesis by A2,XREAL_1:228,XXREAL_1:233;
  end;
  for r st r in X holds r <= a by XXREAL_1:233;
  hence thesis by A1,SEQ_4:def 4;
end;

theorem Th23:
  lower_bound right_closed_halfline(a) = a
proof
  set X = right_closed_halfline(a);
A1: for s st 0 < s ex r st r in X & r < a+s
  proof
    let s;
    assume 0 < s;
    then
A2: a+0 < a+s by XREAL_1:8;
    take a;
    thus a in X by XXREAL_1:236;
    thus thesis by A2;
  end;
  for r st r in X holds a <= r by XXREAL_1:236;
  hence thesis by A1,SEQ_4:def 5;
end;

theorem Th24:
  lower_bound right_open_halfline(a) = a
proof
  set X = right_open_halfline(a);
A1: for s st 0 < s ex r st r in X & r < a+s
  proof
    let s;
    assume 0 < s;
    then
A2: a+0 < a+s by XREAL_1:8;
    take (a+a+s)/2;
    a < (a+(a+s))/2 by A2,XREAL_1:228;
    hence thesis by A2,XREAL_1:228,XXREAL_1:235;
  end;
  for r st r in X holds a <= r by XXREAL_1:235;
  hence thesis by A1,SEQ_4:def 5;
end;

begin :: Connectedness

registration
  cluster [#]REAL -> interval non bounded_below non bounded_above;
  coherence;
end;

theorem Th25:
  for X being bounded interval Subset of REAL st lower_bound X in
  X & upper_bound X in X holds X = [.lower_bound X,upper_bound X.]
proof
  let X be bounded interval Subset of REAL such that
A1: lower_bound X in X & upper_bound X in X;
  thus X c= [.lower_bound X,upper_bound X.] by TOPREAL6:24;
  thus thesis by A1,XXREAL_2:def 12;
end;

theorem Th26:
  for X being bounded Subset of REAL st not lower_bound X in X
  holds X c= ].lower_bound X,upper_bound X.]
proof
  let X be bounded Subset of REAL such that
A1: not lower_bound X in X;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
  lower_bound X <= x by A2,SEQ_4:def 5;
  then
A3: lower_bound X < x by A1,A2,XXREAL_0:1;
  x <= upper_bound X by A2,SEQ_4:def 4;
  hence thesis by A3,XXREAL_1:2;
end;

theorem Th27:
  for X being bounded interval Subset of REAL st not lower_bound
  X in X & upper_bound X in X holds X = ].lower_bound X,upper_bound X.]
proof
  let X be bounded interval Subset of REAL such that
A1: not lower_bound X in X and
A2: upper_bound X in X;
  thus X c= ].lower_bound X,upper_bound X.] by A1,Th26;
  let x be set;
  assume
A3: x in ].lower_bound X,upper_bound X.];
  then reconsider x as Real;
  lower_bound X < x by A3,XXREAL_1:2;
  then lower_bound X - lower_bound X < x - lower_bound X by XREAL_1:16;
  then consider r such that
A4: r in X and
A5: r < lower_bound X + (x - lower_bound X) by A2,SEQ_4:def 5;
  x <= upper_bound X by A3,XXREAL_1:2;
  then
A6: x in [.r,upper_bound X.] by A5,XXREAL_1:1;
  [.r,upper_bound X.] c= X by A2,A4,XXREAL_2:def 12;
  hence thesis by A6;
end;

theorem Th28:
  for X being bounded Subset of REAL st not upper_bound X in X
  holds X c= [.lower_bound X,upper_bound X.[
proof
  let X be bounded Subset of REAL such that
A1: not upper_bound X in X;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
  x <= upper_bound X by A2,SEQ_4:def 4;
  then
A3: x < upper_bound X by A1,A2,XXREAL_0:1;
  lower_bound X <= x by A2,SEQ_4:def 5;
  hence thesis by A3,XXREAL_1:3;
end;

theorem Th29:
  for X being bounded interval Subset of REAL st lower_bound X in
  X & not upper_bound X in X holds X = [.lower_bound X,upper_bound X.[
proof
  let X be bounded interval Subset of REAL such that
A1: lower_bound X in X and
A2: not upper_bound X in X;
  thus X c= [.lower_bound X,upper_bound X.[ by A2,Th28;
  let x be set;
  assume
A3: x in [.lower_bound X,upper_bound X.[;
  then reconsider x as Real;
  x < upper_bound X by A3,XXREAL_1:3;
  then x - x < upper_bound X - x by XREAL_1:16;
  then consider r such that
A4: r in X and
A5: upper_bound X - (upper_bound X - x) < r by A1,SEQ_4:def 4;
  lower_bound X <= x by A3,XXREAL_1:3;
  then
A6: x in [.lower_bound X,r.] by A5,XXREAL_1:1;
  [.lower_bound X,r.] c= X by A1,A4,XXREAL_2:def 12;
  hence thesis by A6;
end;

theorem Th30:
  for X being bounded Subset of REAL st not lower_bound X in X &
  not upper_bound X in X holds X c= ].lower_bound X,upper_bound X.[
proof
  let X be bounded Subset of REAL such that
A1: not lower_bound X in X and
A2: not upper_bound X in X;
  let x be set;
  assume
A3: x in X;
  then reconsider x as Real;
  x <= upper_bound X by A3,SEQ_4:def 4;
  then
A4: x < upper_bound X by A2,A3,XXREAL_0:1;
  lower_bound X <= x by A3,SEQ_4:def 5;
  then lower_bound X < x by A1,A3,XXREAL_0:1;
  hence thesis by A4,XXREAL_1:4;
end;

theorem Th31:
  for X being non empty bounded interval Subset of REAL st not
  lower_bound X in X & not upper_bound X in X holds X = ].lower_bound X,
  upper_bound X.[
proof
  let X be non empty bounded interval Subset of REAL;
  assume ( not lower_bound X in X)& not upper_bound X in X;
  hence X c= ].lower_bound X,upper_bound X.[ by Th30;
  let x be set;
  assume
A1: x in ].lower_bound X,upper_bound X.[;
  then reconsider x as Real;
  lower_bound X < x by A1,XXREAL_1:4;
  then lower_bound X - lower_bound X < x - lower_bound X by XREAL_1:16;
  then consider s such that
A2: s in X & s < lower_bound X + (x - lower_bound X) by SEQ_4:def 5;
  x < upper_bound X by A1,XXREAL_1:4;
  then x - x < upper_bound X - x by XREAL_1:16;
  then consider r such that
A3: r in X & upper_bound X - (upper_bound X - x) < r by SEQ_4:def 4;
  [.s,r.] c= X & x in [.s,r.] by A2,A3,XXREAL_2:def 12,XXREAL_1:1;
  hence thesis;
end;

theorem Th32:
  for X being Subset of REAL st X is bounded_above holds X c=
  left_closed_halfline(upper_bound X)
proof
  let X be Subset of REAL such that
A1: X is bounded_above;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
  x <= upper_bound X by A1,A2,SEQ_4:def 4;
  hence thesis by XXREAL_1:234;
end;

theorem Th33:
  for X being interval Subset of REAL st X is not bounded_below &
  X is bounded_above & upper_bound X in X holds X = left_closed_halfline(
  upper_bound X)
proof
  let X be interval Subset of REAL such that
A1: X is not bounded_below and
A2: X is bounded_above and
A3: upper_bound X in X;
  thus X c= left_closed_halfline(upper_bound X) by A2,Th32;
  let x be set;
  assume
A4: x in left_closed_halfline(upper_bound X);
  then reconsider x as Real;
  x is not LowerBound of X by A1,XXREAL_2:def 9;
  then consider r being ext-real number such that
A5: r in X and
A6: x > r by XXREAL_2:def 2;
  reconsider r as real number by A5;
  x <= upper_bound X by A4,XXREAL_1:234;
  then
A7: x in [.r,upper_bound X.] by A6,XXREAL_1:1;
  [.r,upper_bound X.] c= X by A3,A5,XXREAL_2:def 12;
  hence thesis by A7;
end;

theorem Th34:
  for X being Subset of REAL st X is bounded_above & not
  upper_bound X in X holds X c= left_open_halfline(upper_bound X)
proof
  let X be Subset of REAL such that
A1: X is bounded_above and
A2: not upper_bound X in X;
  let x be set;
  assume
A3: x in X;
  then reconsider x as Real;
  x <= upper_bound X by A1,A3,SEQ_4:def 4;
  then x < upper_bound X by A2,A3,XXREAL_0:1;
  hence thesis by XXREAL_1:233;
end;

theorem Th35:
  for X being non empty interval Subset of REAL st X is not
  bounded_below & X is bounded_above & not upper_bound X in X holds X =
  left_open_halfline(upper_bound X)
proof
  let X be non empty interval Subset of REAL such that
A1: X is not bounded_below and
A2: X is bounded_above and
A3: not upper_bound X in X;
  thus X c= left_open_halfline(upper_bound X) by A2,A3,Th34;
  let x be set;
  assume
A4: x in left_open_halfline(upper_bound X);
  then reconsider x as Real;
  x is not LowerBound of X by A1,XXREAL_2:def 9;
  then consider r being ext-real number such that
A5: r in X & x > r by XXREAL_2:def 2;
  reconsider r as real number by A5;
  x < upper_bound X by A4,XXREAL_1:233;
  then x - x < upper_bound X - x by XREAL_1:16;
  then consider s such that
A6: s in X & upper_bound X - (upper_bound X - x) < s by A2,SEQ_4:def 4;
  [.r,s.] c= X & x in [.r,s.] by A5,A6,XXREAL_2:def 12,XXREAL_1:1;
  hence thesis;
end;

theorem Th36:
  for X being Subset of REAL st X is bounded_below holds X c=
  right_closed_halfline(lower_bound X)
proof
  let X be Subset of REAL such that
A1: X is bounded_below;
  let x be set;
  assume
A2: x in X;
  then reconsider x as Real;
  lower_bound X <= x by A1,A2,SEQ_4:def 5;
  hence thesis by XXREAL_1:236;
end;

theorem Th37:
  for X being interval Subset of REAL st X is bounded_below & X
  is not bounded_above & lower_bound X in X holds X = right_closed_halfline(
  lower_bound X)
proof
  let X be interval Subset of REAL such that
A1: X is bounded_below and
A2: X is not bounded_above and
A3: lower_bound X in X;
  thus X c= right_closed_halfline(lower_bound X) by A1,Th36;
  let x be set;
  assume
A4: x in right_closed_halfline(lower_bound X);
  then reconsider x as Real;
  x is not UpperBound of X by A2,XXREAL_2:def 10;
  then consider r being ext-real number such that
A5: r in X and
A6: x < r by XXREAL_2:def 1;
  reconsider r as real number by A5;
  lower_bound X <= x by A4,XXREAL_1:236;
  then
A7: x in [.lower_bound X,r.] by A6,XXREAL_1:1;
  [.lower_bound X,r.] c= X by A3,A5,XXREAL_2:def 12;
  hence thesis by A7;
end;

theorem Th38:
  for X being Subset of REAL st X is bounded_below & not
  lower_bound X in X holds X c= right_open_halfline(lower_bound X)
proof
  let X be Subset of REAL such that
A1: X is bounded_below and
A2: not lower_bound X in X;
  let x be set;
  assume
A3: x in X;
  then reconsider x as Real;
  lower_bound X <= x by A1,A3,SEQ_4:def 5;
  then lower_bound X < x by A2,A3,XXREAL_0:1;
  hence thesis by XXREAL_1:235;
end;

theorem Th39:
  for X being non empty interval Subset of REAL st X is
  bounded_below & X is not bounded_above & not lower_bound X in X holds X =
  right_open_halfline(lower_bound X)
proof
  let X be non empty interval Subset of REAL such that
A1: X is bounded_below and
A2: X is not bounded_above and
A3: not lower_bound X in X;
  thus X c= right_open_halfline(lower_bound X) by A1,A3,Th38;
  let x be set;
  assume
A4: x in right_open_halfline(lower_bound X);
  then reconsider x as Real;
  x is not UpperBound of X by A2,XXREAL_2:def 10;
  then consider r being ext-real number such that
A5: r in X & x < r by XXREAL_2:def 1;
  lower_bound X < x by A4,XXREAL_1:235;
  then lower_bound X - lower_bound X < x - lower_bound X by XREAL_1:16;
  then consider s such that
A6: s in X & s < lower_bound X + (x - lower_bound X) by A1,SEQ_4:def 5;
  reconsider r as real number by A5;
  [.s,r.] c= X & x in [.s,r.] by A5,A6,XXREAL_2:def 12,XXREAL_1:1;
  hence thesis;
end;

theorem Th40:
  for X being interval Subset of REAL st X is not bounded_above &
  X is not bounded_below holds X = REAL
proof
  let X be interval Subset of REAL;
  assume that
A1: X is not bounded_above and
A2: X is not bounded_below;
  thus X c= REAL;
  let x be set;
  assume x in REAL;
  then reconsider x as Real;
  x is not UpperBound of X by A1,XXREAL_2:def 10;
  then consider r being ext-real number such that
A3: r in X & r > x by XXREAL_2:def 1;
  reconsider r as real number by A3;
  x is not LowerBound of X by A2,XXREAL_2:def 9;
  then consider s being ext-real number such that
A4: s in X & s < x by XXREAL_2:def 2;
  reconsider s as real number by A4;
  [.s,r.] c= X & x in [.s,r.] by A3,A4,XXREAL_2:def 12,XXREAL_1:1;
  hence thesis;
end;

theorem Th41:
  for X being interval Subset of REAL holds X is empty or X =
  REAL or (ex a st X = left_closed_halfline(a)) or (ex a st X =
left_open_halfline(a)) or (ex a st X = right_closed_halfline(a)) or (ex a st X
= right_open_halfline(a)) or (ex a, b st a <= b & X = [.a,b.]) or (ex a, b st a
< b & X = [.a,b.[) or (ex a, b st a < b & X = ].a,b.]) or ex a, b st a < b & X
  = ].a,b.[
proof
  let X be interval Subset of REAL;
  assume X is non empty;
  then reconsider X as non empty interval Subset of REAL;
  per cases;
  suppose
    X is bounded;
    then reconsider X as non empty bounded interval Subset of REAL;
    per cases;
    suppose
      X is trivial;
      then consider x being set such that
A1:   X = {x} by REALSET1:def 4;
      x in X by A1,TARSKI:def 1;
      then reconsider x as Real;
      X = [.x,x.] by A1,XXREAL_1:17;
      hence thesis;
    end;
    suppose
      X is non trivial;
      then ex p, q being set st p in X & q in X & p <> q by REALSET1:14;
      then
A2:   lower_bound X < upper_bound X by SEQ_4:25;
      per cases;
      suppose
        upper_bound X in X & lower_bound X in X;
        then X = [.lower_bound X,upper_bound X.] by Th25;
        hence thesis by A2;
      end;
      suppose
        upper_bound X in X & not lower_bound X in X;
        then X = ].lower_bound X,upper_bound X.] by Th27;
        hence thesis by A2;
      end;
      suppose
        not upper_bound X in X & lower_bound X in X;
        then X = [.lower_bound X,upper_bound X.[ by Th29;
        hence thesis by A2;
      end;
      suppose
        not upper_bound X in X & not lower_bound X in X;
        then X = ].lower_bound X,upper_bound X.[ by Th31;
        hence thesis by A2;
      end;
    end;
  end;
  suppose
A3: X is not bounded;
    per cases by A3;
    suppose
A4:   X is not bounded_below & X is bounded_above;
      per cases;
      suppose
        upper_bound X in X;
        then X = left_closed_halfline(upper_bound X) by A4,Th33;
        hence thesis;
      end;
      suppose
        not upper_bound X in X;
        then X = left_open_halfline(upper_bound X) by A4,Th35;
        hence thesis;
      end;
    end;
    suppose
A5:   X is not bounded_above & X is bounded_below;
      per cases;
      suppose
        lower_bound X in X;
        then X = right_closed_halfline(lower_bound X) by A5,Th37;
        hence thesis;
      end;
      suppose
        not lower_bound X in X;
        then X = right_open_halfline(lower_bound X) by A5,Th39;
        hence thesis;
      end;
    end;
    suppose
      X is not bounded_above & X is not bounded_below;
      hence thesis by Th40;
    end;
  end;
end;

theorem Th42:
  for X being non empty interval Subset of REAL st not r in X
  holds r <= lower_bound X or upper_bound X <= r
proof
  let X be non empty interval Subset of REAL such that
A1: not r in X;
  per cases by Th41;
  suppose
    X = REAL;
    hence thesis by A1,XREAL_0:def 1;
  end;
  suppose
    ex a st X = left_closed_halfline(a);
    then consider a such that
A2: X = left_closed_halfline(a);
    upper_bound X = a by A2,Th21;
    hence thesis by A1,A2,XXREAL_1:234;
  end;
  suppose
    ex a st X = left_open_halfline(a);
    then consider a such that
A3: X = left_open_halfline(a);
    upper_bound X = a by A3,Th22;
    hence thesis by A1,A3,XXREAL_1:233;
  end;
  suppose
    ex a st X = right_closed_halfline(a);
    then consider a such that
A4: X = right_closed_halfline(a);
    lower_bound X = a by A4,Th23;
    hence thesis by A1,A4,XXREAL_1:236;
  end;
  suppose
    ex a st X = right_open_halfline(a);
    then consider a such that
A5: X = right_open_halfline(a);
    lower_bound X = a by A5,Th24;
    hence thesis by A1,A5,XXREAL_1:235;
  end;
  suppose
    ex a, b st a <= b & X = [.a,b.];
    then consider a, b such that
A6: a <= b and
A7: X = [.a,b.];
    lower_bound X = a & upper_bound X = b by A6,A7,JORDAN5A:20;
    hence thesis by A1,A7,XXREAL_1:1;
  end;
  suppose
    ex a, b st a < b & X = [.a,b.[;
    then consider a, b such that
A8: a < b and
A9: X = [.a,b.[;
    lower_bound X = a & upper_bound X = b by A8,A9,Th4,Th5;
    hence thesis by A1,A9,XXREAL_1:3;
  end;
  suppose
    ex a, b st a < b & X = ].a,b.];
    then consider a, b such that
A10: a < b and
A11: X = ].a,b.];
    lower_bound X = a & upper_bound X = b by A10,A11,Th6,Th7;
    hence thesis by A1,A11,XXREAL_1:2;
  end;
  suppose
    ex a, b st a < b & X = ].a,b.[;
    then consider a, b such that
A12: a < b and
A13: X = ].a,b.[;
    lower_bound X = a & upper_bound X = b by A12,A13,TOPREAL6:22;
    hence thesis by A1,A13,XXREAL_1:4;
  end;
end;

theorem Th43:
  for X, Y being non empty bounded interval Subset of REAL st
lower_bound X <= lower_bound Y & upper_bound Y <= upper_bound X & (lower_bound
  X = lower_bound Y & lower_bound Y in Y implies lower_bound X in X) & (
upper_bound X = upper_bound Y & upper_bound Y in Y implies upper_bound X in X)
  holds Y c= X
proof
  let X, Y be non empty bounded interval Subset of REAL such that
A1: lower_bound X <= lower_bound Y and
A2: upper_bound Y <= upper_bound X and
A3: lower_bound X = lower_bound Y & lower_bound Y in Y implies
  lower_bound X in X and
A4: upper_bound X = upper_bound Y & upper_bound Y in Y implies
  upper_bound X in X;
  let x be set;
  assume
A5: x in Y;
  then reconsider x as Real;
A6: Y c= [.lower_bound Y,upper_bound Y.] by TOPREAL6:24;
  then
A7: lower_bound Y <= x by A5,XXREAL_1:1;
  then
A8: lower_bound X <= x by A1,XXREAL_0:2;
A9: x <= upper_bound Y by A5,A6,XXREAL_1:1;
  then
A10: x <= upper_bound X by A2,XXREAL_0:2;
  per cases by Th41;
  suppose
    X = [#]REAL;
    hence thesis;
  end;
  suppose
    (ex a st X = left_closed_halfline(a)) or (ex a st X =
left_open_halfline(a)) or (ex a st X = right_closed_halfline(a)) or ex a st X =
    right_open_halfline(a);
    hence thesis;
  end;
  suppose
    ex a, b st a <= b & X = [.a,b.];
    then consider a, b such that
A11: a <= b and
A12: X = [.a,b.];
    lower_bound X = a & upper_bound X = b by A11,A12,JORDAN5A:20;
    hence thesis by A8,A10,A12,XXREAL_1:1;
  end;
  suppose
    ex a, b st a < b & X = [.a,b.[;
    then consider a, b such that
A13: a < b and
A14: X = [.a,b.[;
A15: lower_bound X = a by A13,A14,Th4;
A16: upper_bound X = b by A13,A14,Th5;
    per cases by Th41;
    suppose
      Y = [#]REAL;
      hence thesis;
    end;
    suppose
      (ex a st Y = left_closed_halfline(a)) or (ex a st Y =
left_open_halfline(a)) or (ex a st Y = right_closed_halfline(a)) or ex a st Y =
      right_open_halfline(a);
      hence thesis;
    end;
    suppose
      ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A17:  a1 <= b1 & Y = [.a1,b1.];
A18:  upper_bound Y = b1 by A17,JORDAN5A:20;
      then b1 < b by A2,A4,A14,A16,A17,XXREAL_0:1,XXREAL_1:1,3;
      then x < b by A9,A18,XXREAL_0:2;
      hence thesis by A8,A14,A15,XXREAL_1:3;
    end;
    suppose
      ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A19:  a1 < b1 & Y = [.a1,b1.[;
      upper_bound Y = b1 & x < b1 by A5,A19,Th5,XXREAL_1:3;
      then x < b by A2,A16,XXREAL_0:2;
      hence thesis by A8,A14,A15,XXREAL_1:3;
    end;
    suppose
      ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A20:  a1 < b1 & Y = ].a1,b1.];
A21:  upper_bound Y = b1 by A20,Th7;
      then b1 < b by A2,A4,A14,A16,A20,XXREAL_0:1,XXREAL_1:2,3;
      then x < b by A9,A21,XXREAL_0:2;
      hence thesis by A8,A14,A15,XXREAL_1:3;
    end;
    suppose
      ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A22:  a1 < b1 & Y = ].a1,b1.[;
      upper_bound Y = b1 & x < b1 by A5,A22,TOPREAL6:22,XXREAL_1:4;
      then x < b by A2,A16,XXREAL_0:2;
      hence thesis by A8,A14,A15,XXREAL_1:3;
    end;
  end;
  suppose
    ex a, b st a < b & X = ].a,b.];
    then consider a, b such that
A23: a < b and
A24: X = ].a,b.];
A25: lower_bound X = a by A23,A24,Th6;
A26: upper_bound X = b by A23,A24,Th7;
    per cases by Th41;
    suppose
      Y = [#]REAL;
      hence thesis;
    end;
    suppose
      (ex a st Y = left_closed_halfline(a)) or (ex a st Y =
left_open_halfline(a)) or (ex a st Y = right_closed_halfline(a)) or ex a st Y =
      right_open_halfline(a);
      hence thesis;
    end;
    suppose
      ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A27:  a1 <= b1 & Y = [.a1,b1.];
A28:  lower_bound Y = a1 by A27,JORDAN5A:20;
      then a < a1 by A1,A3,A24,A25,A27,XXREAL_0:1,XXREAL_1:1,2;
      then a < x by A7,A28,XXREAL_0:2;
      hence thesis by A10,A24,A26,XXREAL_1:2;
    end;
    suppose
      ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A29:  a1 < b1 and
A30:  Y = [.a1,b1.[;
      lower_bound Y = a1 by A29,A30,Th4;
      then
A31:  a < a1 by A1,A3,A24,A25,A29,A30,XXREAL_0:1,XXREAL_1:2,3;
      a1 <= x by A5,A30,XXREAL_1:3;
      then a < x by A31,XXREAL_0:2;
      hence thesis by A10,A24,A26,XXREAL_1:2;
    end;
    suppose
      ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A32:  a1 < b1 & Y = ].a1,b1.];
      lower_bound Y = a1 & a1 < x by A5,A32,Th6,XXREAL_1:2;
      then a < x by A1,A25,XXREAL_0:2;
      hence thesis by A10,A24,A26,XXREAL_1:2;
    end;
    suppose
      ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A33:  a1 < b1 & Y = ].a1,b1.[;
      lower_bound Y = a1 & a1 < x by A5,A33,TOPREAL6:22,XXREAL_1:4;
      then a < x by A1,A25,XXREAL_0:2;
      hence thesis by A10,A24,A26,XXREAL_1:2;
    end;
  end;
  suppose
    ex a, b st a < b & X = ].a,b.[;
    then consider a, b such that
A34: a < b and
A35: X = ].a,b.[;
A36: lower_bound X = a by A34,A35,TOPREAL6:22;
A37: upper_bound X = b by A34,A35,TOPREAL6:22;
    per cases by Th41;
    suppose
      Y = [#]REAL;
      hence thesis;
    end;
    suppose
      (ex a st Y = left_closed_halfline(a)) or (ex a st Y =
left_open_halfline(a)) or (ex a st Y = right_closed_halfline(a)) or ex a st Y =
      right_open_halfline(a);
      hence thesis;
    end;
    suppose
      ex a, b st a <= b & Y = [.a,b.];
      then consider a1, b1 being real number such that
A38:  a1 <= b1 and
A39:  Y = [.a1,b1.];
      upper_bound Y = b1 by A38,A39,JORDAN5A:20;
      then
A40:  b1 < b by A2,A4,A35,A37,A38,A39,XXREAL_0:1,XXREAL_1:1,4;
      x <= b1 by A5,A39,XXREAL_1:1;
      then
A41:  x < b by A40,XXREAL_0:2;
A42:  lower_bound Y = a1 by A38,A39,JORDAN5A:20;
      then a < a1 by A1,A3,A35,A36,A38,A39,XXREAL_0:1,XXREAL_1:1,4;
      then a < x by A7,A42,XXREAL_0:2;
      hence thesis by A35,A41,XXREAL_1:4;
    end;
    suppose
      ex a, b st a < b & Y = [.a,b.[;
      then consider a1, b1 being real number such that
A43:  a1 < b1 and
A44:  Y = [.a1,b1.[;
      lower_bound Y = a1 by A43,A44,Th4;
      then
A45:  a < a1 by A1,A3,A35,A36,A43,A44,XXREAL_0:1,XXREAL_1:3,4;
      a1 <= x by A5,A44,XXREAL_1:3;
      then
A46:  a < x by A45,XXREAL_0:2;
      upper_bound Y = b1 & x < b1 by A5,A43,A44,Th5,XXREAL_1:3;
      then x < b by A2,A37,XXREAL_0:2;
      hence thesis by A35,A46,XXREAL_1:4;
    end;
    suppose
      ex a, b st a < b & Y = ].a,b.];
      then consider a1, b1 being real number such that
A47:  a1 < b1 and
A48:  Y = ].a1,b1.];
      upper_bound Y = b1 by A47,A48,Th7;
      then
A49:  b1 < b by A2,A4,A35,A37,A47,A48,XXREAL_0:1,XXREAL_1:2,4;
      x <= b1 by A5,A48,XXREAL_1:2;
      then
A50:  x < b by A49,XXREAL_0:2;
      lower_bound Y = a1 & a1 < x by A5,A47,A48,Th6,XXREAL_1:2;
      then a < x by A1,A36,XXREAL_0:2;
      hence thesis by A35,A50,XXREAL_1:4;
    end;
    suppose
      ex a, b st a < b & Y = ].a,b.[;
      then consider a1, b1 being real number such that
A51:  a1 < b1 & Y = ].a1,b1.[;
      lower_bound Y = a1 & a1 < x by A5,A51,TOPREAL6:22,XXREAL_1:4;
      then
A52:  a < x by A1,A36,XXREAL_0:2;
      upper_bound Y = b1 & x < b1 by A5,A51,TOPREAL6:22,XXREAL_1:4;
      then x < b by A2,A37,XXREAL_0:2;
      hence thesis by A35,A52,XXREAL_1:4;
    end;
  end;
end;

registration
  cluster open closed interval non empty non bounded Subset of REAL;
  existence
  proof
    take [#]REAL;
    thus thesis;
  end;
end;

begin :: R^1

theorem Th44:
  for X being Subset of R^1 st a <= b & X = [.a,b.] holds Fr X = { a,b}
proof
  let X be Subset of R^1 such that
A1: a <= b and
A2: X = [.a,b.];
A3: Cl X = Cl [.a,b.] by A2,TOPREAL6:80
    .= [.a,b.] by MEASURE6:95;
A4: [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b
  } by A1,Th20;
  set LO = R^1(left_open_halfline(a)), RC = R^1(right_closed_halfline(b)), RO
  = R^1(right_open_halfline(b)), LC = R^1(left_closed_halfline(a));
A5: RC = right_closed_halfline(b) by TOPREALB:def 3;
A6: LC = left_closed_halfline(a) by TOPREALB:def 3;
A7: RO = right_open_halfline(b) by TOPREALB:def 3;
A8: LO = left_open_halfline(a) by TOPREALB:def 3;
  then
A9: [.a,b.]` = LO \/ RO by A7,XXREAL_1:385;
  Cl X` = Cl [.a,b.]` by A2,TOPMETR:24,TOPREAL6:80
    .= Cl left_open_halfline(a) \/ Cl right_open_halfline(b) by A8,A7,A9,Th3
    .= Cl LO \/ Cl right_open_halfline(b) by A8,TOPREAL6:80
    .= Cl LO \/ Cl RO by A7,TOPREAL6:80
    .= LC \/ Cl RO by A6,BORSUK_5:77,TOPREALB:def 3
    .= LC \/ RC by A5,BORSUK_5:75,TOPREALB:def 3
    .= left_closed_halfline(a) \/ right_closed_halfline(b) by A5,TOPREALB:def 3
;
  hence thesis by A3,A4,TOPS_1:def 2;
end;

theorem
  for X being Subset of R^1 st a < b & X = ].a,b.[ holds Fr X = {a,b}
proof
  let X be Subset of R^1 such that
A1: a < b and
A2: X = ].a,b.[;
A3: Cl X = Cl ].a,b.[ by A2,TOPREAL6:80
    .= [.a,b.] by A1,TOPREAL6:82;
  set RC = R^1(right_closed_halfline(b)), LC = R^1(left_closed_halfline(a));
A4: RC = right_closed_halfline(b) & LC = left_closed_halfline(a) by
TOPREALB:def 3;
  then
A5: ].a,b.[` = LC \/ RC by XXREAL_1:398;
A6: [.a,b.] /\ (left_closed_halfline(a) \/ right_closed_halfline(b)) = {a,b}
  by A1,Th20;
  Cl X` = Cl ].a,b.[` by A2,TOPMETR:24,TOPREAL6:80
    .= Cl left_closed_halfline(a) \/ Cl right_closed_halfline(b) by A4,A5,Th3
    .= Cl left_closed_halfline(a) \/ right_closed_halfline(b) by MEASURE6:95
    .= left_closed_halfline(a) \/ right_closed_halfline(b) by MEASURE6:95;
  hence thesis by A3,A6,TOPS_1:def 2;
end;

theorem Th46:
  for X being Subset of R^1 st a < b & X = [.a,b.[ holds Fr X = {a ,b}
proof
  let X be Subset of R^1 such that
A1: a < b and
A2: X = [.a,b.[;
A3: Cl X = [.a,b.] & [.a,b.] /\ (left_closed_halfline(a) \/
  right_closed_halfline(b)) = {a,b} by A1,A2,Th20,BORSUK_5:58;
  set LO = R^1(left_open_halfline(a)), RC = R^1(right_closed_halfline(b)), LC
  = R^1(left_closed_halfline(a));
A4: RC = right_closed_halfline(b) by TOPREALB:def 3;
A5: LO = left_open_halfline(a) by TOPREALB:def 3;
  then
A6: [.a,b.[` = LO \/ RC by A4,XXREAL_1:382;
A7: LC = left_closed_halfline(a) by TOPREALB:def 3;
  Cl X` = Cl [.a,b.[` by A2,TOPMETR:24,TOPREAL6:80
    .= Cl left_open_halfline(a) \/ Cl right_closed_halfline(b) by A5,A4,A6,Th3
    .= Cl LO \/ Cl right_closed_halfline(b) by A5,TOPREAL6:80
    .= Cl LO \/ Cl RC by A4,TOPREAL6:80
    .= LC \/ Cl RC by A7,BORSUK_5:77,TOPREALB:def 3
    .= left_closed_halfline(a) \/ right_closed_halfline(b) by A4,A7,PRE_TOPC:52
;
  hence thesis by A3,TOPS_1:def 2;
end;

theorem Th47:
  for X being Subset of R^1 st a < b & X = ].a,b.] holds Fr X = {a ,b}
proof
  let X be Subset of R^1 such that
A1: a < b and
A2: X = ].a,b.];
A3: Cl X = [.a,b.] & [.a,b.] /\ (left_closed_halfline(a) \/
  right_closed_halfline(b)) = {a,b} by A1,A2,Th20,BORSUK_5:59;
A4: ].a,b.]` = left_closed_halfline(a) \/ right_open_halfline(b) by
XXREAL_1:399;
  set RO = R^1(right_open_halfline(b)), LC = R^1(left_closed_halfline(a));
A5: RO = right_open_halfline(b) by TOPREALB:def 3;
A6: LC = left_closed_halfline(a) by TOPREALB:def 3;
  Cl X` = Cl ].a,b.]` by A2,TOPMETR:24,TOPREAL6:80
    .= Cl left_closed_halfline(a) \/ Cl right_open_halfline(b) by A4,Th3
    .= Cl LC \/ Cl right_open_halfline(b) by A6,TOPREAL6:80
    .= Cl LC \/ Cl RO by A5,TOPREAL6:80
    .= LC \/ Cl RO by PRE_TOPC:52
    .= left_closed_halfline(a) \/ right_closed_halfline(b) by A6,BORSUK_5:75
,TOPREALB:def 3;
  hence thesis by A3,TOPS_1:def 2;
end;

theorem
  for X being Subset of R^1 st X = [.a,b.] holds Int X = ].a,b.[
proof
  let X be Subset of R^1 such that
A1: X = [.a,b.];
A2: Int X c= X by TOPS_1:44;
  thus Int X c= ].a,b.[
  proof
    let x be set;
    assume
A3: x in Int X;
    then reconsider x as Point of R^1;
A4: now
      now
        assume a > b;
        then X = {}R^1 by A1,XXREAL_1:29;
        hence contradiction by A3;
      end;
      then Fr X = {a,b} by A1,Th44;
      then
A5:   a in Fr X & b in Fr X by TARSKI:def 2;
A6:   Int X misses Fr X by TOPS_1:73;
      assume x = a or x = b;
      hence contradiction by A3,A6,A5,XBOOLE_0:3;
    end;
    x <= b by A1,A2,A3,XXREAL_1:1;
    then
A7: x < b by A4,XXREAL_0:1;
    a <= x by A1,A2,A3,XXREAL_1:1;
    then a < x by A4,XXREAL_0:1;
    hence thesis by A7,XXREAL_1:4;
  end;
  reconsider Y = ].a,b.[ as open Subset of R^1 by BORSUK_5:62,TOPMETR:24;
  Y c= Int X by A1,TOPS_1:56,XXREAL_1:37;
  hence thesis;
end;

theorem
  for X being Subset of R^1 st X = ].a,b.[ holds Int X = ].a,b.[
proof
  let X be Subset of R^1;
  assume
A1: X = ].a,b.[;
  then reconsider X as open Subset of R^1 by BORSUK_5:62;
  Int X = X by TOPS_1:55;
  hence thesis by A1;
end;

theorem Th50:
  for X being Subset of R^1 st X = [.a,b.[ holds Int X = ].a,b.[
proof
  let X be Subset of R^1 such that
A1: X = [.a,b.[;
A2: Int X c= X by TOPS_1:44;
  thus Int X c= ].a,b.[
  proof
    let x be set;
    assume
A3: x in Int X;
    then reconsider x as Point of R^1;
A4: now
      now
        assume a >= b;
        then X = {}R^1 by A1,XXREAL_1:27;
        hence contradiction by A3;
      end;
      then Fr X = {a,b} by A1,Th46;
      then
A5:   Int X misses Fr X & a in Fr X by TARSKI:def 2,TOPS_1:73;
      assume x = a;
      hence contradiction by A3,A5,XBOOLE_0:3;
    end;
    a <= x by A1,A2,A3,XXREAL_1:3;
    then
A6: a < x by A4,XXREAL_0:1;
    x < b by A1,A2,A3,XXREAL_1:3;
    hence thesis by A6,XXREAL_1:4;
  end;
  reconsider Y = ].a,b.[ as open Subset of R^1 by BORSUK_5:62,TOPMETR:24;
  Y c= Int X by A1,TOPS_1:56,XXREAL_1:45;
  hence thesis;
end;

theorem Th51:
  for X being Subset of R^1 st X = ].a,b.] holds Int X = ].a,b.[
proof
  let X be Subset of R^1 such that
A1: X = ].a,b.];
A2: Int X c= X by TOPS_1:44;
  thus Int X c= ].a,b.[
  proof
    let x be set;
    assume
A3: x in Int X;
    then reconsider x as Point of R^1;
A4: now
      now
        assume a >= b;
        then X = {}R^1 by A1,XXREAL_1:26;
        hence contradiction by A3;
      end;
      then Fr X = {a,b} by A1,Th47;
      then
A5:   Int X misses Fr X & b in Fr X by TARSKI:def 2,TOPS_1:73;
      assume x = b;
      hence contradiction by A3,A5,XBOOLE_0:3;
    end;
    x <= b by A1,A2,A3,XXREAL_1:2;
    then
A6: x < b by A4,XXREAL_0:1;
    a < x by A1,A2,A3,XXREAL_1:2;
    hence thesis by A6,XXREAL_1:4;
  end;
  reconsider Y = ].a,b.[ as open Subset of R^1 by BORSUK_5:62,TOPMETR:24;
  Y c= Int X by A1,TOPS_1:56,XXREAL_1:41;
  hence thesis;
end;

registration let T be real-membered TopStruct, X be interval Subset of T;
 cluster T|X -> interval;
 coherence
  proof
   thus [#](T|X) is interval by PRE_TOPC:def 10;
  end;
end;

registration
  let A be interval Subset of REAL;
  cluster R^1(A) -> interval;
  coherence by TOPREALB:def 3;
end;

registration
 cluster connected -> interval Subset of R^1;
 coherence
  proof let X be Subset of R^1;
    assume
A2: X is connected;
    let a be ext-real number;
    thus thesis by A2,BORSUK_5:111;
  end;
 cluster interval -> connected Subset of R^1;
 coherence
  proof let X be Subset of R^1 such that
A1: X is interval;
A2: X is Subset of REAL by MEMBERED:3;
  per cases by A1,A2,Th41;
  suppose
    X is empty;
    then reconsider A=X as empty Subset of R^1;
    A is interval;
    hence thesis;
  end;
  suppose
 X = REAL;
    then reconsider A=X as non empty interval Subset of R^1;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose
    ex a st X = left_closed_halfline(a);
    then consider a such that
A5: X = left_closed_halfline(a);
    reconsider A=X as non empty interval Subset of R^1 by A5;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose
    ex a st X = left_open_halfline(a);
    then consider a such that
A6: X = left_open_halfline(a);
    reconsider A=X as non empty interval Subset of R^1 by A6;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose
    ex a st X = right_closed_halfline(a);
    then consider a such that
A7: X = right_closed_halfline(a);
    reconsider A=X as non empty interval Subset of R^1 by A7;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose
    ex a st X = right_open_halfline(a);
    then consider a such that
A8: X = right_open_halfline(a);
    reconsider A=X as non empty interval Subset of R^1 by A8;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose
    ex a, b st a <= b & X = [.a,b.];
    then reconsider A=X as non empty interval Subset of R^1 by XXREAL_1:1;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose
    ex a, b st a < b & X = [.a,b.[;
    then reconsider A=X as non empty interval Subset of R^1 by XXREAL_1:3;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose
    ex a, b st a < b & X = ].a,b.];
    then reconsider A=X as non empty interval Subset of R^1 by XXREAL_1:2;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  suppose
    ex a, b st a < b & X = ].a,b.[;
    then reconsider A=X as non empty interval Subset of R^1 by XXREAL_1:33;
    R^1|A is connected;
    hence R^1|X is connected;
  end;
  end;
end;

begin :: Closed Interval TSpace

registration
  let r;
  cluster Closed-Interval-TSpace(r,r) -> trivial;
  coherence
  proof
    {r} is trivial & the carrier of Closed-Interval-TSpace(r,r) = [.r,r.]
    by TOPMETR:25;
    hence thesis by XXREAL_1:17;
  end;
end;

canceled;

theorem
  r <= s implies for A being Subset of Closed-Interval-TSpace(r,s) holds
  A is bounded Subset of REAL
proof
  assume r <= s;
  then
A1: the carrier of Closed-Interval-TSpace(r,s) = [.r,s.] by TOPMETR:25;
  let A be Subset of Closed-Interval-TSpace(r,s);
  A is bounded_above bounded_below by A1,XXREAL_2:43,44;
  hence thesis by A1,XBOOLE_1:1;
end;

theorem Th54:
  r <= s implies for X being Subset of Closed-Interval-TSpace(r,s)
  st X = [.a,b.[ & r < a & b <= s holds Int X = ].a,b.[
proof
  set L = Closed-Interval-TSpace(r,s);
  set c = (r+a)/2;
  set C1 = R^1(].c,b.[);
A1: C1 = ].c,b.[ by TOPREALB:def 3;
  assume r <= s;
  then
A2: the carrier of L = [.r,s.] by TOPMETR:25;
  let X be Subset of Closed-Interval-TSpace(r,s) such that
A3: X = [.a,b.[ and
A4: r < a and
A5: b <= s;
A6: r < c by A4,XREAL_1:228;
A7: C1 c= the carrier of L
  proof
    let x be set;
    assume
A8: x in C1;
    then reconsider x as Real by A1;
    x < b by A1,A8,XXREAL_1:4;
    then
A9: x <= s by A5,XXREAL_0:2;
    c < x by A1,A8,XXREAL_1:4;
    then r <= x by A6,XXREAL_0:2;
    hence thesis by A2,A9,XXREAL_1:1;
  end;
  reconsider A = X as Subset of R^1 by PRE_TOPC:39;
A10: c < a by A4,XREAL_1:228;
  A c= C1
  proof
    let x be set;
    assume
A11: x in A;
    then reconsider x as Real by A3;
    a <= x by A3,A11,XXREAL_1:3;
    then
A12: c < x by A10,XXREAL_0:2;
    x < b by A3,A11,XXREAL_1:3;
    hence thesis by A1,A12,XXREAL_1:4;
  end;
  then Int A = Int X by A7,TOPS_3:57;
  hence thesis by A3,Th50;
end;

theorem Th55:
  r <= s implies for X being Subset of Closed-Interval-TSpace(r,s)
  st X = ].a,b.] & r <= a & b < s holds Int X = ].a,b.[
proof
  set L = Closed-Interval-TSpace(r,s);
  set c = (b+s)/2;
  set C1 = R^1(].a,c.[);
A1: C1 = ].a,c.[ by TOPREALB:def 3;
  assume r <= s;
  then
A2: the carrier of L = [.r,s.] by TOPMETR:25;
  let X be Subset of Closed-Interval-TSpace(r,s) such that
A3: X = ].a,b.] and
A4: r <= a and
A5: b < s;
A6: c < s by A5,XREAL_1:228;
A7: C1 c= the carrier of L
  proof
    let x be set;
    assume
A8: x in C1;
    then reconsider x as Real by A1;
    x < c by A1,A8,XXREAL_1:4;
    then
A9: x <= s by A6,XXREAL_0:2;
    a < x by A1,A8,XXREAL_1:4;
    then r <= x by A4,XXREAL_0:2;
    hence thesis by A2,A9,XXREAL_1:1;
  end;
  reconsider A = X as Subset of R^1 by PRE_TOPC:39;
A10: b < c by A5,XREAL_1:228;
  A c= C1
  proof
    let x be set;
    assume
A11: x in A;
    then reconsider x as Real by A3;
    x <= b by A3,A11,XXREAL_1:2;
    then
A12: x < c by A10,XXREAL_0:2;
    a < x by A3,A11,XXREAL_1:2;
    hence thesis by A1,A12,XXREAL_1:4;
  end;
  then Int A = Int X by A7,TOPS_3:57;
  hence thesis by A3,Th51;
end;

theorem Th56:
  for X being Subset of Closed-Interval-TSpace(r,s), Y being
  Subset of REAL st X = Y holds X is connected iff Y is interval
proof
  let X be Subset of Closed-Interval-TSpace(r,s), Y be Subset of REAL such
  that
A1: X = Y;
  reconsider Z = X as Subset of R^1 by A1,TOPMETR:24;
  hereby
    assume X is connected;
    then Z is connected by CONNSP_1:24;
    hence Y is interval by A1;
  end;
  assume Y is interval;
  then Z is connected by A1;
  hence thesis by CONNSP_1:24;
end;

registration
  let T be TopSpace;
  cluster open closed connected Subset of T;
  existence
  proof
    take {}T;
    thus thesis;
  end;
end;

registration
  let T be non empty connected TopSpace;
  cluster non empty open closed connected Subset of T;
  existence
  proof
    take [#]T;
    thus thesis by CONNSP_1:28;
  end;
end;

theorem Th57:
  r <= s implies for X being open connected Subset of
Closed-Interval-TSpace(r,s) holds X is empty or X = [.r,s.] or (ex a being real
number st r < a & a <= s & X = [.r,a.[) or (ex a being real number st r <= a &
a < s & X = ].a,s.]) or ex a, b being real number st r <= a & a < b & b <= s &
  X = ].a,b.[
proof
  set L = Closed-Interval-TSpace(r,s);
  assume
A1: r <= s;
  then
A2: the carrier of L = [.r,s.] by TOPMETR:25;
  let X be open connected Subset of L;
  X is bounded_above bounded_below Subset of REAL by A2,XBOOLE_1:1,XXREAL_2:43
,44;
  then reconsider Y = X as bounded interval Subset of REAL by Th56;
A3: the carrier of L = [#]L & L is connected by A1,TREAL_1:23;
A4: s in [.r,s.] by A1,XXREAL_1:1;
A5: r in [.r,s.] by A1,XXREAL_1:1;
  per cases by Th41;
  suppose
    Y is empty or Y = [#]REAL;
    hence thesis;
  end;
  suppose
    (ex a st Y = left_closed_halfline(a)) or (ex a st Y =
left_open_halfline(a)) or (ex a st Y = right_closed_halfline(a)) or ex a st Y =
    right_open_halfline(a);
    hence thesis;
  end;
  suppose
    ex a, b st a <= b & Y = [.a,b.];
    then consider a, b such that
A6: a <= b and
A7: Y = [.a,b.];
A8: X <> {}L by A6,A7,XXREAL_1:1;
A9: r <= a & b <= s by A2,A6,A7,XXREAL_1:50;
    then
A10: X is closed by A7,TOPREALA:44;
    now
      assume
A11:  r <> a or b <> s;
      per cases by A9,A11,XXREAL_0:1;
      suppose
        r < a;
        then not r in X by A7,XXREAL_1:1;
        hence contradiction by A2,A3,A5,A8,A10,CONNSP_1:14;
      end;
      suppose
        b < s;
        then not s in X by A7,XXREAL_1:1;
        hence contradiction by A2,A3,A4,A8,A10,CONNSP_1:14;
      end;
    end;
    hence thesis by A7;
  end;
  suppose
    ex a, b st a < b & Y = [.a,b.[;
    then consider a, b such that
A12: a < b and
A13: Y = [.a,b.[;
A14: b <= s by A2,A12,A13,XXREAL_1:52;
A15: r <= a by A2,A12,A13,XXREAL_1:52;
    now
      assume r <> a;
      then
A16:  r < a by A15,XXREAL_0:1;
      now
        Int X = ].a,b.[ by A1,A13,A14,A16,Th54;
        then
A17:    not a in Int X by XXREAL_1:4;
        assume Int X = X;
        hence contradiction by A12,A13,A17,XXREAL_1:3;
      end;
      hence contradiction by TOPS_1:55;
    end;
    hence thesis by A12,A13,A14;
  end;
  suppose
    ex a, b st a < b & Y = ].a,b.];
    then consider a, b such that
A18: a < b and
A19: Y = ].a,b.];
A20: r <= a by A2,A18,A19,XXREAL_1:53;
A21: b <= s by A2,A18,A19,XXREAL_1:53;
    now
      assume b <> s;
      then
A22:  b < s by A21,XXREAL_0:1;
      now
        Int X = ].a,b.[ by A1,A19,A20,A22,Th55;
        then
A23:    not b in Int X by XXREAL_1:4;
        assume Int X = X;
        hence contradiction by A18,A19,A23,XXREAL_1:2;
      end;
      hence contradiction by TOPS_1:55;
    end;
    hence thesis by A18,A19,A20;
  end;
  suppose
    ex a, b st a < b & Y = ].a,b.[;
    then consider a, b such that
A24: a < b & Y = ].a,b.[;
    r <= a & b <= s by A2,A24,XXREAL_1:51;
    hence thesis by A24;
  end;
end;

begin :: Minimal cover of an interval

deffunc F(set) = $1;
defpred P[set,set] means $1 c= $2;

canceled;

theorem Th59:
  for T being 1-sorted, F being finite Subset-Family of T for F1
being Subset-Family of T st F is Cover of T & F1 = F \ {X where X is Subset of
T: X in F & ex Y being Subset of T st Y in F & X c< Y} holds F1 is Cover of T
proof
  let T be 1-sorted, F be finite Subset-Family of T, F1 be Subset-Family of T
  such that
A1: the carrier of T c= union F and
A2: F1 = F \ {X where X is Subset of T: X in F & ex Y being Subset of T
  st Y in F & X c< Y};
  set ZAW = {X where X is Subset of T: X in F & ex Y being Subset of T st Y in
  F & X c< Y};
  thus the carrier of T c= union F1
  proof
    let t be set;
    assume t in the carrier of T;
    then consider Z being set such that
A3: t in Z and
A4: Z in F by A1,TARSKI:def 4;
    set ALL = {X where X is Subset of T: Z c< X & X in F};
    per cases;
    suppose
A5:   ALL is empty;
      now
        assume Z in ZAW;
        then consider X being Subset of T such that
A6:     Z = X and
        X in F and
A7:     ex Y being Subset of T st Y in F & X c< Y;
        consider Y being Subset of T such that
A8:     Y in F & X c< Y by A7;
        Y in ALL by A6,A8;
        hence contradiction by A5;
      end;
      then Z in F1 by A2,A4,XBOOLE_0:def 5;
      hence thesis by A3,TARSKI:def 4;
    end;
    suppose
      ALL is non empty;
      then consider w being set such that
A9:   w in ALL by XBOOLE_0:def 1;
      consider R being Relation of ALL such that
A10:  for x, y being set holds [x,y] in R iff x in ALL & y in ALL & P
      [x,y] from RELSET_1:sch 1;
A11:  R is_reflexive_in ALL
      proof
        let x be set;
        thus thesis by A10;
      end;
      then
A12:  field R = ALL by ORDERS_1:98;
A13:  R partially_orders ALL
      proof
        thus R is_reflexive_in ALL by A11;
        thus R is_transitive_in ALL
        proof
          let x, y, z be set;
          assume that
A14:      x in ALL and
          y in ALL and
A15:      z in ALL;
          assume [x,y] in R & [y,z] in R;
          then x c= y & y c= z by A10;
          then x c= z by XBOOLE_1:1;
          hence thesis by A10,A14,A15;
        end;
        let x, y be set;
        assume that
        x in ALL and
        y in ALL;
        assume [x,y] in R & [y,x] in R;
        hence x c= y & y c= x by A10;
      end;
A16:  R is reflexive by A11,A12,RELAT_2:def 9;
      ALL has_upper_Zorn_property_wrt R
      proof
        let Y be set such that
A17:    Y c= ALL and
A18:    R |_2 Y is being_linear-order;
        per cases;
        suppose
A19:      Y is non empty;
          defpred U[set] means $1 is non empty & $1 c= Y implies union $1 in Y;
          take union Y;
A20:      U[{}];
A21:      for A, B being set st A in Y & B in Y holds A \/ B in Y
          proof
A22:        R |_2 Y c= R by XBOOLE_1:17;
            R |_2 Y is connected by A18,ORDERS_1:def 5;
            then
A23:        R |_2 Y is_connected_in field (R |_2 Y) by RELAT_2:def 14;
            let A, B be set such that
A24:        A in Y & B in Y;
            field(R |_2 Y) = Y by A12,A16,A17,ORDERS_1:181;
            then [A,B] in R |_2 Y or [B,A] in R |_2 Y or A = B by A24,A23,
RELAT_2:def 6;
            then A c= B or B c= A by A10,A22;
            hence thesis by A24,XBOOLE_1:12;
          end;
A25:      for x, B being set st x in Y & B c= Y & U[B] holds U[B \/ {x}]
          proof
            let x, B be set such that
A26:        x in Y and
A27:        B c= Y & U[B] and
            B \/ {x} is non empty and
            B \/ {x} c= Y;
A28:        union {x} = x by ZFMISC_1:31;
            per cases;
            suppose
              B is empty;
              hence thesis by A26,ZFMISC_1:31;
            end;
            suppose
              B is non empty;
              then x \/ union B in Y by A21,A26,A27;
              hence thesis by A28,ZFMISC_1:96;
            end;
          end;
          consider y being set such that
A29:      y in Y by A19,XBOOLE_0:def 1;
          y in ALL by A17,A29;
          then consider X being Subset of T such that
A30:      X = y and
A31:      Z c< X and
          X in F;
A32:      X c= union Y by A29,A30,ZFMISC_1:92;
          then
A33:      Z <> union Y by A31,XBOOLE_0:def 8;
          Z c= X by A31,XBOOLE_0:def 8;
          then Z c= union Y by A32,XBOOLE_1:1;
          then
A34:      Z c< union Y by A33,XBOOLE_0:def 8;
A35:      ALL c= F
          proof
            let x be set;
            assume x in ALL;
            then ex X being Subset of T st X = x & Z c< X & X in F;
            hence thesis;
          end;
          then
A36:      Y c= F by A17,XBOOLE_1:1;
A37:      Y is finite by A17,A35;
          U[Y] from FINSET_1:sch 2(A37,A20,A25);
          then union Y in F by A19,A36;
          hence
A38:      union Y in ALL by A34;
          let z be set;
          assume
A39:      z in Y;
          then P[z,union Y] by ZFMISC_1:92;
          hence thesis by A10,A17,A38,A39;
        end;
        suppose
A40:      Y is empty;
          take w;
          thus w in ALL by A9;
          thus thesis by A40;
        end;
      end;
      then consider M being set such that
A41:  M is_maximal_in R by A12,A13,ORDERS_1:173;
A42:  M in field R by A41,ORDERS_1:def 11;
      then
A43:  ex X being Subset of T st X = M & Z c< X & X in F by A12;
      now
        assume M in ZAW;
        then consider H being Subset of T such that
A44:    M = H and
        H in F and
A45:    ex Y being Subset of T st Y in F & H c< Y;
        consider Y being Subset of T such that
A46:    Y in F and
A47:    H c< Y by A45;
        Z c< Y by A43,A44,A47,XBOOLE_1:56;
        then
A48:    Y in ALL by A46;
        H c= Y by A47,XBOOLE_0:def 8;
        then [M,Y] in R by A10,A12,A42,A44,A48;
        hence contradiction by A12,A41,A44,A47,A48,ORDERS_1:def 11;
      end;
      then
A49:  M in F1 by A2,A43,XBOOLE_0:def 5;
      Z c= M by A43,XBOOLE_0:def 8;
      hence thesis by A3,A49,TARSKI:def 4;
    end;
  end;
end;

theorem Th60:
  for S being trivial non empty 1-sorted, s being Point of S, F
  being Subset-Family of S st F is Cover of S holds {s} in F
proof
  let S be trivial non empty 1-sorted, s be Point of S, F be Subset-Family of
  S such that
A1: the carrier of S c= union F;
  consider d being Element of S such that
A2: the carrier of S = {d} by TEX_1:def 1;
  s in the carrier of S;
  then consider Z being set such that
A3: s in Z and
A4: Z in F by A1,TARSKI:def 4;
A5: s = d by REALSET1:14;
  Z = {s}
  proof
    thus for x being set st x in Z holds x in {s} by A4,A2,A5;
    let x be set;
    thus thesis by A3,TARSKI:def 1;
  end;
  hence thesis by A4;
end;

definition
  let T be TopStruct, F be Subset-Family of T;
  attr F is connected means
  :Def1:
  for X being Subset of T st X in F holds X is connected;
end;

registration
  let T be TopSpace;
  cluster non empty open closed connected Subset-Family of T;
  existence
  proof
    {{}T} c= bool the carrier of T
    proof
      let x be set;
      assume x in {{}T};
      then x = {}T by TARSKI:def 1;
      hence thesis;
    end;
    then reconsider F = {{}T} as Subset-Family of T;
    take F;
    thus F is non empty;
    thus for P being Subset of T holds P in F implies P is open by TARSKI:def 1
;
    thus for P being Subset of T holds P in F implies P is closed by
TARSKI:def 1;
    thus for P being Subset of T holds P in F implies P is connected by
TARSKI:def 1;
  end;
end;

reserve n, m for natural number,
  F for Subset-Family of Closed-Interval-TSpace (r,s);

Lm3: [.r,s.] in F & r <= s implies rng <*[.r,s.]*> c= F & union rng <*[.r,s.]
*> = [.r,s.] & for n being natural number st 1 <= n holds (n <= len <*[.r,s.]*>
implies <*[.r,s.]*>/.n is non empty) & (n+1 <= len <*[.r,s.]*> implies
lower_bound(<*[.r,s.]*>/.n) <= lower_bound(<*[.r,s.]*>/.(n+1)) & upper_bound(<*
[.r,s.]*>/.n) <= upper_bound(<*[.r,s.]*>/.(n+1)) & lower_bound(<*[.r,s.]*>/.(n+
1)) < upper_bound(<*[.r,s.]*>/.n)) & (n+2 <= len <*[.r,s.]*> implies
upper_bound(<*[.r,s.]*>/.n) <= lower_bound(<*[.r,s.]*>/.(n+2)))
proof
  assume that
A1: [.r,s.] in F and
A2: r <= s;
  set f = <*[.r,s.]*>;
A3: rng f = {[.r,s.]} by FINSEQ_1:55;
  thus rng f c= F
  proof
    let a be set;
    assume a in rng f;
    hence thesis by A1,A3,TARSKI:def 1;
  end;
  thus union rng f = [.r,s.] by A3,ZFMISC_1:31;
  let n be natural number;
  assume
A4: 1 <= n;
  hereby
    assume n <= len f;
    then n <= 1 by FINSEQ_1:56;
    then n = 1 by A4,XXREAL_0:1;
    then f/.n = [.r,s.] by FINSEQ_4:25;
    hence f/.n is non empty by A2,XXREAL_1:1;
  end;
  hereby
    assume n+1 <= len f;
    then n+1 <= 0+1 by FINSEQ_1:56;
    hence lower_bound(f/.n) <= lower_bound(f/.(n+1)) & upper_bound(f/.n) <=
    upper_bound(f/.(n+1)) & lower_bound(f/.(n+1)) < upper_bound(f/.n) by A4,
XREAL_1:8;
  end;
  assume n+2 <= len f;
  then n+1+1 <= 0+1 by FINSEQ_1:56;
  hence thesis by XREAL_1:8;
end;

theorem Th61:
  for L being TopSpace, G, G1 being Subset-Family of L st G is
Cover of L & G is finite for ALL being set st G1 = G \ {X where X is Subset of
L: X in G & ex Y being Subset of L st Y in G & X c< Y} & ALL = {C where C is
  Subset-Family of L: C is Cover of L & C c= G1} holds ALL
  has_lower_Zorn_property_wrt RelIncl ALL
proof
  let L be TopSpace;
  let G, G1 be Subset-Family of L;
  assume that
A1: G is Cover of L and
A2: G is finite;
  let ALL be set;
  set ZAW = {X where X is Subset of L: X in G & ex Y being Subset of L st Y in
  G & X c< Y};
  assume that
A3: G1 = G \ ZAW and
A4: ALL = {C where C is Subset-Family of L: C is Cover of L & C c= G1};
A5: G1 is Cover of L by A1,A2,A3,Th59;
  set R = RelIncl ALL;
A6: field R = ALL by WELLORD2:def 1;
  let Y be set such that
A7: Y c= ALL and
A8: R |_2 Y is being_linear-order;
  per cases;
  suppose
A9: Y is non empty;
    defpred A[set] means $1 is non empty implies meet $1 in Y;
    set E = {F(D) where D is Subset-Family of L: D in bool G1};
    take x = meet Y;
A10: ALL c= E
    proof
      let a be set;
      assume a in ALL;
      then ex C being Subset-Family of L st a = C & C is Cover of L & C c= G1
      by A4;
      hence thesis;
    end;
A11: bool G1 is finite by A2,A3;
    E is finite from FRAENKEL:sch 21(A11);
    then
A12: Y is finite by A7,A10;
A13: for x, B being set st x in Y & B c= Y & A[B] holds A[B \/ {x}]
    proof
      let x, B be set such that
A14:  x in Y and
      B c= Y and
A15:  A[B] and
      B \/ {x} is non empty;
      per cases;
      suppose
        B is empty;
        hence thesis by A14,SETFAM_1:11;
      end;
      suppose
A16:    B is non empty;
        R |_2 Y is connected by A8,ORDERS_1:def 5;
        then
A17:    R |_2 Y is_connected_in field (R |_2 Y) by RELAT_2:def 14;
        field (R |_2 Y) = Y by A6,A7,ORDERS_1:181,WELLORD2:2;
        then [x,meet B] in R |_2 Y or [meet B,x] in R |_2 Y or x = meet B by
A14,A15,A16,A17,RELAT_2:def 6;
        then [x,meet B] in R or [meet B,x] in R or x = meet B by XBOOLE_0:def 4
;
        then
A18:    meet B c= x or x c= meet B by A7,A14,A15,A16,WELLORD2:def 1;
        meet (B \/ {x}) = meet B /\ meet {x} by A16,SETFAM_1:10
          .= meet B /\ x by SETFAM_1:11;
        hence thesis by A14,A15,A16,A18,XBOOLE_1:28;
      end;
    end;
    consider y being set such that
A19: y in Y by A9,XBOOLE_0:def 1;
    y in ALL by A7,A19;
    then
A20: ex C being Subset-Family of L st y = C & C is Cover of L & C c= G1 by A4;
    then reconsider X = x as Subset-Family of L by A19,SETFAM_1:8;
A21: A[{}];
A22: A[Y] from FINSET_1:sch 2(A12,A21,A13);
A23: X is Cover of L
    proof
      let a be set;
      assume
A24:  a in the carrier of L;
      meet Y in ALL by A7,A9,A22;
      then consider C being Subset-Family of L such that
A25:  meet Y = C and
A26:  C is Cover of L and
      C c= G1 by A4;
      the carrier of L c= union C by A26,SETFAM_1:def 12;
      hence thesis by A24,A25;
    end;
    x c= G1 by A19,A20,SETFAM_1:8;
    hence
A27: x in ALL by A4,A23;
    let y be set;
    assume
A28: y in Y;
    then x c= y by SETFAM_1:8;
    hence thesis by A7,A27,A28,WELLORD2:def 1;
  end;
  suppose
A29: Y is empty;
    take G1;
    thus thesis by A4,A5,A29;
  end;
end;

theorem Th62:
  for L being TopSpace, G, ALL being set st ALL = {C where C is
  Subset-Family of L: C is Cover of L & C c= G} for M being set st M
is_minimal_in RelIncl ALL & M in field RelIncl ALL for A1 being Subset of L st
  A1 in M holds not ex A2, A3 being Subset of L st A2 in M & A3 in M & A1 c= A2
  \/ A3 & A1 <> A2 & A1 <> A3
proof
  let L be TopSpace;
  let G be set;
  let ALL be set such that
A1: ALL = {C where C is Subset-Family of L: C is Cover of L & C c= G};
  set R = RelIncl ALL;
  let M be set such that
A2: M is_minimal_in RelIncl ALL and
A3: M in field RelIncl ALL;
A4: field R = ALL by WELLORD2:def 1;
  then consider C being Subset-Family of L such that
A5: M = C and
A6: C is Cover of L and
A7: C c= G by A1,A3;
  let A1 be Subset of L such that
A8: A1 in M;
  set Y = C \ {A1};
A9: Y <> M by A8,ZFMISC_1:64;
  given A2, A3 being Subset of L such that
A10: A2 in M and
A11: A3 in M and
A12: A1 c= A2 \/ A3 and
A13: A1 <> A2 and
A14: A1 <> A3;
A15: union C = [#]L by A6,SETFAM_1:60;
  union Y = the carrier of L
  proof
    thus union Y c= the carrier of L;
    let x be set;
    assume
A16: x in the carrier of L;
    per cases;
    suppose
A17:  x in A1;
      per cases by A12,A17,XBOOLE_0:def 3;
      suppose
A18:    x in A2;
        A2 in Y by A5,A10,A13,ZFMISC_1:64;
        hence thesis by A18,TARSKI:def 4;
      end;
      suppose
A19:    x in A3;
        A3 in Y by A5,A11,A14,ZFMISC_1:64;
        hence thesis by A19,TARSKI:def 4;
      end;
    end;
    suppose
A20:  not x in A1;
      consider Z being set such that
A21:  x in Z and
A22:  Z in C by A15,A16,TARSKI:def 4;
      Z in Y by A20,A21,A22,ZFMISC_1:64;
      hence thesis by A21,TARSKI:def 4;
    end;
  end;
  then
A23: Y is Cover of L by SETFAM_1:def 12;
A24: Y c= M by A5,XBOOLE_1:36;
  then Y c= G by A5,A7,XBOOLE_1:1;
  then
A25: Y in ALL by A1,A23;
  then [Y,M] in R by A4,A3,A24,WELLORD2:def 1;
  hence contradiction by A4,A2,A9,A25,ORDERS_1:def 12;
end;

definition
  let r, s be real number;
  let F be Subset-Family of Closed-Interval-TSpace(r,s) such that
A1: F is Cover of Closed-Interval-TSpace(r,s) and
A2: F is open and
A3: F is connected and
A4: r <= s;
  mode IntervalCover of F -> FinSequence of bool REAL means
    :Def2:
    rng it c= F
& union rng it = [.r,s.] & (for n being natural number st 1 <= n holds (n <=
len it implies it/.n is non empty) & (n+1 <= len it implies lower_bound(it/.n)
    <= lower_bound(it/.(n+1)) & upper_bound(it/.n) <= upper_bound(it/.(n+1)) &
    lower_bound(it/.(n+1)) < upper_bound(it/.n)) & (n+2 <= len it implies
upper_bound(it/.n) <= lower_bound(it/.(n+2)))) & ([.r,s.] in F implies it = <*
[.r,s.]*>) & (not [.r,s.] in F implies (ex p being real number st r < p & p <=
s & it.1 = [.r,p.[) & (ex p being real number st r <= p & p < s & it.len it =
].p,s.]) & for n being natural number st 1 < n & n < len it ex p, q being real
    number st r <= p & p < q & q <= s & it.n = ].p,q.[ );
  existence
  proof
    per cases;
    suppose
A5:   [.r,s.] in F;
      take f = <*[.r,s.]*>;
A6:   rng f = {[.r,s.]} by FINSEQ_1:55;
      thus rng f c= F
      proof
        let a be set;
        assume a in rng f;
        hence thesis by A5,A6,TARSKI:def 1;
      end;
      thus union rng f = [.r,s.] by A6,ZFMISC_1:31;
      thus thesis by A4,A5,Lm3;
    end;
    suppose
A7:   not [.r,s.] in F;
      set L = Closed-Interval-TSpace(r,s);
A8:   the carrier of L = [.r,s.] by A4,TOPMETR:25;
A9:   now
        let A be Subset of L;
        thus A is bounded_above bounded_below by A8,XXREAL_2:43,44;
        hence A is bounded Subset of REAL by A8,XBOOLE_1:1;
      end;
      L is compact by A4,HEINE:11;
      then [#]L is compact by COMPTS_1:10;
      then consider G being Subset-Family of L such that
A10:  G c= F and
A11:  G is Cover of [#]L and
A12:  G is finite by A1,A2,COMPTS_1:def 7;
      set ZAW = {X where X is Subset of L: X in G & ex Y being Subset of L st
      Y in G & X c< Y};
      set G1 = G \ ZAW;
      set ALL = {C where C is Subset-Family of L: C is Cover of L & C c= G1};
      set R = RelIncl ALL;
A13:  R is_antisymmetric_in ALL by WELLORD2:31;
      set RM = {lower_bound ].c,s.] where c is Real: ].c,s.] in G1};
      set LM = {upper_bound [.r,b.[ where b is Real: [.r,b.[ in G1};
A14:  G1 c= G by XBOOLE_1:36;
      then
A15:  G1 c= F by A10,XBOOLE_1:1;
A16:  for X being set st X in G1 holds X is interval Subset of REAL
      proof
        let X be set;
        assume X in G1;
        then reconsider X as connected Subset of L by A3,A15,Def1;
        reconsider Y = X as Subset of REAL by A8,XBOOLE_1:1;
        Y is interval by Th56;
        hence thesis;
      end;
      reconsider T = L as non empty connected TopSpace by A4,TREAL_1:23;
      set LM1 = {upper_bound E where E is Subset of L: E in G1};
A17:  LM c= LM1
      proof
        let x be set;
        assume x in LM;
        then ex b being Real st x = upper_bound [.r,b.[ & [.r,b.[ in G1;
        hence thesis;
      end;
A18:  LM c= REAL
      proof
        let x be set;
        assume x in LM;
        then ex b being Real st x = upper_bound [.r,b.[ & [.r,b.[ in G1;
        hence thesis;
      end;
      set RM1 = {lower_bound E where E is Subset of L: E in G1};
A19:  RM c= RM1
      proof
        let x be set;
        assume x in RM;
        then ex b being Real st x = lower_bound ].b,s.] & ].b,s.] in G1;
        hence thesis;
      end;
A20:  RM c= REAL
      proof
        let x be set;
        assume x in RM;
        then ex b being Real st x = lower_bound ].b,s.] & ].b,s.] in G1;
        hence thesis;
      end;
A21:  field R = ALL by WELLORD2:def 1;
      R is_reflexive_in ALL & R is_transitive_in ALL by WELLORD2:29,30;
      then R partially_orders ALL by A13,ORDERS_1:def 7;
      then consider M being set such that
A22:  M is_minimal_in R by A11,A12,A21,Th61,ORDERS_1:174;
A23:  M in field R by A22,ORDERS_1:def 12;
      then consider C being Subset-Family of L such that
A24:  M = C and
A25:  C is Cover of L and
A26:  C c= G1 by A21;
A27:  union C = [#]L by A25,SETFAM_1:60;
A28:  s in [.r,s.] by A4,XXREAL_1:1;
      then consider R2 being set such that
A29:  s in R2 and
A30:  R2 in C by A8,A27,TARSKI:def 4;
      r in [.r,s.] by A4,XXREAL_1:1;
      then consider R1 being set such that
A31:  r in R1 and
A32:  R1 in C by A8,A27,TARSKI:def 4;
A33:  R1 in G1 by A26,A32;
      then
A34:  R1 in F by A15;
A35:  R2 in G1 by A26,A30;
      then
A36:  R2 in F by A15;
      reconsider R1, R2 as open connected Subset of L by A2,A3,A15,A33,A35,Def1
,TOPS_2:def 1;
A37:  now
        per cases by A4,A7,A29,A36,Th57;
        suppose
          ex a being real number st r < a & a <= s & R2 = [.r,a.[;
          hence RM is non empty by A29,XXREAL_1:3;
        end;
        suppose
          ex a being real number st r <= a & a < s & R2 = ].a,s.];
          then consider a being real number such that
          r <= a and
          a < s and
A38:      R2 = ].a,s.];
          a is Real by XREAL_0:def 1;
          then lower_bound ].a,s.] in RM by A26,A30,A38;
          hence RM is non empty;
        end;
        suppose
          ex a, b being real number st r <= a & a < b & b <= s & R2 = ].a,b.[;
          hence RM is non empty by A29,XXREAL_1:4;
        end;
      end;
A39:  now
        per cases by A4,A7,A31,A34,Th57;
        suppose
          ex a being real number st r < a & a <= s & R1 = [.r,a.[;
          then consider a being real number such that
          r < a and
          a <= s and
A40:      R1 = [.r,a.[;
          a is Real by XREAL_0:def 1;
          then upper_bound [.r,a.[ in LM by A26,A32,A40;
          hence LM is non empty;
        end;
        suppose
          ex a being real number st r <= a & a < s & R1 = ].a,s.];
          hence LM is non empty by A31,XXREAL_1:2;
        end;
        suppose
          ex a, b being real number st r <= a & a < b & b <= s & R1 = ].a,b.[;
          hence LM is non empty by A31,XXREAL_1:4;
        end;
      end;
A41:  G1 is finite by A12;
      RM1 is finite from FRAENKEL:sch 21(A41);
      then reconsider RM as non empty finite Subset of REAL by A19,A37,A20;
      F c= bool REAL
      proof
        let a be set;
        assume a in F;
        then a c= REAL by A8,XBOOLE_1:1;
        hence thesis;
      end;
      then G1 c= bool REAL by A15,XBOOLE_1:1;
      then reconsider X = C as non empty finite Subset-Family of REAL by A12
,A26,A32,XBOOLE_1:1;
      LM1 is finite from FRAENKEL:sch 21(A41);
      then reconsider LM as non empty finite Subset of REAL by A17,A39,A18;
      reconsider kL = max LM as Real by XREAL_0:def 1;
      set LEWY = [.r,kL.[;
      kL in LM by XXREAL_2:def 8;
      then consider b being Real such that
A42:  kL = upper_bound [.r,b.[ and
A43:  [.r,b.[ in G1;
A44:  union G = [#]L by A11,SETFAM_1:60;
A45:  now
        consider x being set such that
A46:    x in the carrier of L by XBOOLE_0:def 1;
        consider g being set such that
A47:    x in g and
A48:    g in G by A44,A46,TARSKI:def 4;
        {} c= g by XBOOLE_1:2;
        then
A49:    {} c< g by A47,XBOOLE_0:def 8;
        assume
A50:    {} in G1;
        then {} in G by XBOOLE_0:def 5;
        then {} in ZAW by A48,A49;
        hence contradiction by A50,XBOOLE_0:def 5;
      end;
      then
A51:  upper_bound LEWY = kL by A42,A43,Th5,XXREAL_1:27;
A52:  r < b by A45,A43,XXREAL_1:27;
      then r < kL by A42,Th5;
      then
A53:  lower_bound LEWY = r by Th4;
      reconsider LEWY as non empty Subset of L by A45,A42,A43,Th5,XXREAL_1:27;
A54:  kL = b by A45,A42,A43,Th5,XXREAL_1:27;
A55:  for A being Subset of L st r in A & A in G1 holds A = LEWY
      proof
        let A be Subset of L;
        assume that
A56:    r in A and
A57:    A in G1;
A58:    A in F & A is open by A2,A15,A57,TOPS_2:def 1;
A59:    now
          assume
A60:      (ex a being real number st r <= a & a < s & A = ].a,s.])
or ex a, b being real number st r <= a & a < b & b <= s & A = ].a,b.[;
          per cases by A60;
          suppose
            ex a being real number st r <= a & a < s & A = ].a,s.];
            hence contradiction by A56,XXREAL_1:2;
          end;
          suppose
            ex a, b being real number st r <= a & a < b & b <= s & A = ].a,b.[;
            hence contradiction by A56,XXREAL_1:4;
          end;
        end;
        A is connected by A3,A15,A57,Def1;
        then consider ak being real number such that
A61:    r < ak and
        ak <= s and
A62:    A = [.r,ak.[ by A4,A7,A56,A58,A59,Th57;
A63:    ak is Real by XREAL_0:def 1;
A64:    A c= LEWY
        proof
          upper_bound A = ak by A61,A62,Th5;
          then ak in LM by A57,A62,A63;
          then
A65:      ak <= kL by XXREAL_2:def 8;
          let a be set;
          assume
A66:      a in A;
          then a in [.r,s.] by A8;
          then reconsider a as Real;
          a < ak by A62,A66,XXREAL_1:3;
          then
A67:      a < kL by A65,XXREAL_0:2;
          r <= a by A62,A66,XXREAL_1:3;
          hence thesis by A67,XXREAL_1:3;
        end;
        assume A <> LEWY;
        then A c< LEWY by A64,XBOOLE_0:def 8;
        then A in ZAW by A14,A43,A54,A57;
        hence contradiction by A57,XBOOLE_0:def 5;
      end;
      then reconsider LLEWY = LEWY as Element of X by A26,A31,A32;
      reconsider pP = min RM as Real by XREAL_0:def 1;
      set PRAWY = ].pP,s.];
      pP in RM by XXREAL_2:def 7;
      then consider b being Real such that
A68:  pP = lower_bound ].b,s.] and
A69:  ].b,s.] in G1;
A70:  lower_bound PRAWY = pP by A45,A68,A69,Th6,XXREAL_1:26;
A71:  b < s by A45,A69,XXREAL_1:26;
      then pP < s by A68,Th6;
      then
A72:  upper_bound PRAWY = s by Th7;
      reconsider PRAWY as non empty Subset of L by A45,A68,A69,Th6,XXREAL_1:26;
A73:  pP = b by A45,A68,A69,Th6,XXREAL_1:26;
A74:  for A being Subset of L st A in G1 & A <> LEWY & A <> PRAWY ex a,
      b being Real st a < b & A = ].a,b.[
      proof
        let A be Subset of L such that
A75:    A in G1 and
A76:    A <> LEWY and
A77:    A <> PRAWY;
A78:    A in F & A is open connected by A2,A3,A15,A75,Def1,TOPS_2:def 1;
        per cases by A4,A7,A45,A75,A78,Th57;
        suppose
          ex a being real number st r < a & a <= s & A = [.r,a.[;
          then consider a being real number such that
          r < a and
          a <= s and
A79:      A = [.r,a.[;
          per cases;
          suppose
            a <= kL;
            then A c= LEWY by A79,XXREAL_1:38;
            then A c< LEWY by A76,XBOOLE_0:def 8;
            then A in ZAW by A14,A43,A54,A75;
            hence thesis by A75,XBOOLE_0:def 5;
          end;
          suppose
            a > kL;
            then LEWY c= A by A79,XXREAL_1:38;
            then LEWY c< A by A76,XBOOLE_0:def 8;
            then LEWY in ZAW by A14,A43,A54,A75;
            hence thesis by A43,A54,XBOOLE_0:def 5;
          end;
        end;
        suppose
          ex a being real number st r <= a & a < s & A = ].a,s.];
          then consider a being real number such that
          r <= a and
          a < s and
A80:      A = ].a,s.];
          per cases;
          suppose
            a >= pP;
            then A c= PRAWY by A80,XXREAL_1:42;
            then A c< PRAWY by A77,XBOOLE_0:def 8;
            then A in ZAW by A14,A69,A73,A75;
            hence thesis by A75,XBOOLE_0:def 5;
          end;
          suppose
            a < pP;
            then PRAWY c= A by A80,XXREAL_1:42;
            then PRAWY c< A by A77,XBOOLE_0:def 8;
            then PRAWY in ZAW by A14,A69,A73,A75;
            hence thesis by A69,A73,XBOOLE_0:def 5;
          end;
        end;
        suppose
          ex a, b being real number st r <= a & a < b & b <= s & A = ].a,b.[;
          then consider a, b being real number such that
          r <= a and
A81:      a < b and
          b <= s and
A82:      A = ].a,b.[;
          reconsider a, b as Real by XREAL_0:def 1;
          take a, b;
          thus thesis by A81,A82;
        end;
      end;
A83:  for A being Subset of L st A in G1 & upper_bound A in A holds A = PRAWY
      proof
        let A be Subset of L such that
A84:    A in G1 and
A85:    upper_bound A in A and
A86:    A <> PRAWY;
        A <> LEWY by A51,A85,XXREAL_1:3;
        then consider a, b being Real such that
A87:    a < b and
A88:    A = ].a,b.[ by A74,A84,A86;
        upper_bound A = b by A87,A88,TOPREAL6:22;
        hence contradiction by A85,A88,XXREAL_1:4;
      end;
      defpred F[set,set,set] means ex S being Element of X st S = $2 &
      upper_bound S in $3;
A89:  X c= F by A15,A26,XBOOLE_1:1;
A90:  for Z being Subset of REAL st Z in X holds Z is non empty open
      connected Subset of T
      proof
        let Z be Subset of REAL;
        assume
A91:    Z in X;
        then Z is non empty interval by A45,A16,A26;
        hence thesis by A2,A89,A91,Th56,TOPS_2:def 1;
      end;
A92:  for n being Element of NAT st 1 <= n & n < card X for x being
      Element of X ex y being Element of X st F[n,x,y]
      proof
        let n be Element of NAT such that
        1 <= n and
        n < card X;
        let x be Element of X;
        reconsider x1 = x as Subset of REAL;
A93:    x1 is non empty by A90;
A94:    x c= union X by ZFMISC_1:92;
        then x c= [.r,s.] by A4,A27,TOPMETR:25;
        then x1 is bounded_above by XXREAL_2:43;
        then upper_bound x is Element of L by A8,A27,A93,A94,Th2;
        then consider y being set such that
A95:    upper_bound x in y and
A96:    y in X by A27,TARSKI:def 4;
        reconsider y as Element of X by A96;
        take y, x;
        thus thesis by A95;
      end;
      consider IT being FinSequence of X such that
A97:  len IT = card X and
A98:  IT.1 = LLEWY or card X = 0 and
A99:  for n being Element of NAT st 1 <= n & n < card X holds F[n,IT
      .n,IT.(n+1)] from RECDEF_1:sch 4(A92);
A100: rng IT c= X;
      rng IT c= bool REAL by XBOOLE_1:1;
      then reconsider IT as FinSequence of bool REAL by FINSEQ_1:def 4;
A101: IT is non empty by A97;
      then
A102: rng IT is non empty;
      then
A103: 1 in dom IT by FINSEQ_3:34;
      then
A104: IT/.1 = IT.1 by PARTFUN1:def 8;
A105: for n being natural number st n in dom IT holds IT.n in X & IT/.n in X
      proof
        let n be natural number;
        assume n in dom IT;
        then IT.n = IT/.n & IT.n in rng IT by FUNCT_1:def 5,PARTFUN1:def 8;
        hence thesis by A100;
      end;
A106: for n being natural number st n in dom IT holds IT.n in G1 & IT/.n in G1
      proof
        let n be natural number;
        assume n in dom IT;
        then IT.n = IT/.n & IT.n in X by A105,PARTFUN1:def 8;
        hence thesis by A26;
      end;
A107: for i being natural number st i in dom IT for x being Point of L
st x < upper_bound(IT/.i) ex j being natural number st j in dom IT & j <= i & x
      in IT/.j
      proof
        defpred Q[natural number] means $1 in dom IT implies for x being Point
of L st x < upper_bound(IT/.$1) ex j being natural number st j in dom IT & j <=
        $1 & x in IT/.j;
A108:   Q[n] implies Q[n+1]
        proof
          assume that
A109:     Q[n] and
A110:     n+1 in dom IT;
A111:     IT/.(n+1) = IT.(n+1) by A110,PARTFUN1:def 8;
          let x be Point of L such that
A112:     x < upper_bound(IT/.(n+1));
          per cases by A110,TOPREALA:23;
          suppose
A113:       n = 0;
            take 1;
            thus 1 in dom IT by A102,FINSEQ_3:34;
            thus 1 <= n+1 by A113;
            r <= x by A8,XXREAL_1:1;
            hence thesis by A51,A98,A112,A111,A113,XXREAL_1:3;
          end;
          suppose
A114:       n in dom IT;
            n+1 <= len IT by A110,FINSEQ_3:27;
            then
A115:       n < len IT by NAT_1:13;
            1 <= n by A114,FINSEQ_3:27;
            then
A116:       ex S being Element of X st S = IT.n & upper_bound S in IT. (
            n+1) by A97,A99,A114,A115;
            IT/.(n+1) in X by A105,A110;
            then
A117:       IT/.(n+1) is bounded_below by A9;
            IT/.n = IT.n by A114,PARTFUN1:def 8;
            then
A118:       lower_bound(IT/.(n+1)) <= upper_bound(IT/.n) by A111,A117,A116,
SEQ_4:def 5;
A119:       IT/.(n+1) is interval Subset of REAL & IT/.(n+1) is non
            empty by A45,A16,A106,A110;
            per cases by XXREAL_0:1;
            suppose
              x < upper_bound(IT/.n);
              then consider j being natural number such that
A120:         j in dom IT and
A121:         j <= n and
A122:         x in IT/.j by A109,A114;
              take j;
              thus j in dom IT by A120;
              j+0 < n+1 by A121,XREAL_1:10;
              hence j <= n+1;
              thus thesis by A122;
            end;
            suppose
A123:         x = upper_bound(IT/.n);
              take n+1;
              thus n+1 in dom IT by A110;
              thus n+1 <= n+1;
              thus thesis by A111,A114,A116,A123,PARTFUN1:def 8;
            end;
            suppose
A124:         x > upper_bound(IT/.n);
              take n+1;
              thus n+1 in dom IT by A110;
              thus n+1 <= n+1;
              lower_bound(IT/.(n+1)) < x by A118,A124,XXREAL_0:2;
              hence thesis by A112,A119,Th42;
            end;
          end;
        end;
A125:   Q[0] by FINSEQ_3:26;
A126:   Q[n] from NAT_1:sch 2(A125,A108);
        let i be natural number;
        assume i in dom IT;
        hence thesis by A126;
      end;
A127: s in ].b,s.] by A71,XXREAL_1:2;
A128: for i being natural number st i in dom IT for j being natural
number st j in dom IT & i < j ex y being Point of L st y in IT/.j & for x being
      Point of L st x in IT/.i holds x < y
      proof
        let i be natural number such that
A129:   i in dom IT;
        defpred W[natural number] means $1 in dom IT & i < $1 implies ex y
being Point of L st y in IT/.$1 & for x being Point of L st x in IT/.i holds x
        < y;
A130:   for n holds W[n] implies W[n+1]
        proof
          let n;
          assume that
A131:     W[n] and
A132:     n+1 in dom IT;
A133:     IT/.(n+1) = IT.(n+1) by A132,PARTFUN1:def 8;
          assume
A134:     i < n+1;
          then
A135:     i <= n by NAT_1:13;
          per cases by A132,TOPREALA:23;
          suppose
            n = 0;
            then i = 0 by A134,NAT_1:13;
            hence thesis by A129,FINSEQ_3:26;
          end;
          suppose
A136:       n in dom IT;
            then
A137:       IT/.n in X by A105;
            then
A138:       IT/.n is bounded_above by A9;
A139:       IT/.n = IT.n by A136,PARTFUN1:def 8;
            then IT/.n in rng IT by A136,FUNCT_1:def 5;
            then
A140:       IT/.n is non empty & IT/.n is Subset of L by A90,A100;
            then upper_bound(IT/.n) in [.r,s.] by A8,A138,Th2;
            then
A141:       upper_bound(IT/.n) <= s by XXREAL_1:1;
A142:       IT/.(n+1) in X by A105,A132;
A143:       1 <= n by A136,FINSEQ_3:27;
A144:       IT/.(n+1) in rng IT by A132,A133,FUNCT_1:def 5;
            then
A145:       IT/.(n+1) is open connected Subset of L by A90,A100;
            then
A146:       IT/.(n+1) is interval Subset of REAL by Th56;
A147:       n+1 <= len IT by A132,FINSEQ_3:27;
            then n is Element of NAT & n < card X by A97,NAT_1:13
,ORDINAL1:def 13;
            then consider S being Element of X such that
A148:       S = IT.n and
A149:       upper_bound S in IT.(n+1) by A99,A143;
            IT/.(n+1) is bounded_below by A9,A145;
            then
A150:       lower_bound(IT/.(n+1)) <= upper_bound S by A133,A149,SEQ_4:def 5;
A151:       IT/.(n+1) is bounded_above by A9,A145;
            then
A152:       upper_bound S <= upper_bound(IT/.(n+1)) by A133,A149,SEQ_4:def 4;
A153:       IT/.(n+1) is non empty by A90,A100,A144;
            then upper_bound(IT/.(n+1)) in [.r,s.] by A8,A145,A151,Th2;
            then
A154:       upper_bound(IT/.(n+1)) <= s by XXREAL_1:1;
            per cases by A135,XXREAL_0:1;
            suppose
              i < n;
              then consider y being Point of L such that
A155:         y in IT/.n and
A156:         for x being Point of L st x in IT/.i holds x < y by A131,A136;
A157:         y <= upper_bound(IT/.n) by A138,A155,SEQ_4:def 4;
              per cases by A152,XXREAL_0:1;
              suppose
A158:           upper_bound S < upper_bound(IT/.(n+1));
                set y1 = (upper_bound S + upper_bound(IT/.(n+1))) / 2;
A159:           upper_bound S < y1 by A158,XREAL_1:228;
                upper_bound S in [.r,s.] by A8,A139,A138,A140,A148,Th2;
                then r <= upper_bound S by XXREAL_1:1;
                then
A160:           r <= y1 by A159,XXREAL_0:2;
                y1 < upper_bound(IT/.(n+1)) by A158,XREAL_1:228;
                then y1 < s by A154,XXREAL_0:2;
                then reconsider y1 as Point of L by A8,A160,XXREAL_1:1;
                take y1;
                lower_bound(IT/.(n+1)) < y1 by A150,A159,XXREAL_0:2;
                hence y1 in IT/.(n+1) by A146,A153,A158,Th42,XREAL_1:228;
                let x be Point of L;
                assume x in IT/.i;
                then x < upper_bound(IT/.n) by A156,A157,XXREAL_0:2;
                hence thesis by A139,A148,A159,XXREAL_0:2;
              end;
              suppose
A161:           upper_bound S = upper_bound(IT/.(n+1));
                reconsider y1 = s as Point of L by A4,A8,XXREAL_1:1;
                take y1;
                IT/.(n+1) = PRAWY by A26,A83,A133,A149,A142,A161;
                hence y1 in IT/.(n+1) by A71,A73,XXREAL_1:2;
                let x be Point of L;
                assume x in IT/.i;
                then x < upper_bound(IT/.n) by A156,A157,XXREAL_0:2;
                hence thesis by A141,XXREAL_0:2;
              end;
            end;
            suppose
A162:         i = n;
              reconsider y1 = upper_bound(IT/.n) as Element of L by A8,A138
,A140,Th2;
              take y1;
              thus y1 in IT/.(n+1) by A133,A136,A148,A149,PARTFUN1:def 8;
              let x be Point of L;
              assume
A163:         x in IT/.i;
A164:         now
                set IT1 = IT|Seg n;
A165:           rng IT1 c= rng IT by RELAT_1:99;
                rng IT1 c= bool the carrier of L
                proof
                  let A be set;
                  assume A in rng IT1;
                  then A in rng IT by A165;
                  then A in X by A100;
                  hence thesis;
                end;
                then reconsider FI = rng IT1 as Subset-Family of L;
                assume x = upper_bound(IT/.n);
                then
A166:           IT/.n = PRAWY by A26,A83,A137,A162,A163;
A167:           now
                  union FI = the carrier of L
                  proof
                    thus union FI c= the carrier of L;
                    let l be set;
                    assume l in the carrier of L;
                    then reconsider l as Point of L;
                    per cases;
                    suppose
                      l < upper_bound(IT/.n);
                      then consider j being natural number such that
A168:                 j in dom IT and
A169:                 j <= n and
A170:                 l in IT/.j by A107,A136;
                      1 <= j by A168,FINSEQ_3:27;
                      then j in Seg n by A169,FINSEQ_1:3;
                      then
A171:                 IT.j in FI by A168,FUNCT_1:73;
                      IT.j = IT/.j by A168,PARTFUN1:def 8;
                      hence thesis by A170,A171,TARSKI:def 4;
                    end;
                    suppose
A172:                 l >= upper_bound(IT/.n);
                      n in Seg n by A143,FINSEQ_1:3;
                      then
A173:                 IT.n in FI by A136,FUNCT_1:73;
                      l <= s by A8,XXREAL_1:1;
                      then l = s by A72,A166,A172,XXREAL_0:1;
                      hence thesis by A73,A127,A139,A166,A173,TARSKI:def 4;
                    end;
                  end;
                  then
A174:             FI is Cover of L by SETFAM_1:def 12;
                  assume
A175:             FI <> X;
A176:             FI c= X by A100,A165,XBOOLE_1:1;
                  then FI c= G1 by A26,XBOOLE_1:1;
                  then
A177:             FI in ALL by A174;
                  then [FI,M] in R by A21,A23,A24,A176,WELLORD2:def 1;
                  hence contradiction by A21,A22,A24,A175,A177,ORDERS_1:def 12;
                end;
                Seg n c= dom IT
                proof
                  let x be set;
A178:             n+0 <= n+1 by XREAL_1:8;
                  assume
A179:             x in Seg n;
                  then reconsider x as Nat;
                  x <= n by A179,FINSEQ_1:3;
                  then x <= n+1 by A178,XXREAL_0:2;
                  then
A180:             x <= len IT by A147,XXREAL_0:2;
                  1 <= x by A179,FINSEQ_1:3;
                  hence thesis by A180,FINSEQ_3:27;
                end;
                then dom IT1 = Seg n by RELAT_1:91;
                then card rng IT1 <= card dom IT1 & card dom IT1 = n by
CARD_2:66,FINSEQ_1:78;
                then n+1 <= n+0 by A97,A147,A167,XXREAL_0:2;
                hence contradiction by XREAL_1:8;
              end;
              x <= upper_bound(IT/.n) by A138,A162,A163,SEQ_4:def 4;
              hence thesis by A164,XXREAL_0:1;
            end;
          end;
        end;
A181:   W[0];
        for n holds W[n] from NAT_1:sch 2(A181,A130);
        hence thesis;
      end;
A182: IT is one-to-one
      proof
        let i, j be set such that
A183:   i in dom IT & j in dom IT and
A184:   IT.i = IT.j;
A185:   IT/.i = IT.i & IT/.j = IT.j by A183,PARTFUN1:def 8;
        assume
A186:   i <> j;
        reconsider i, j as Nat by A183;
        per cases by A186,XXREAL_0:1;
        suppose
          i < j;
          then ex y being Point of L st y in IT/.j & for x being Point of L
          st x in IT/.i holds x < y by A128,A183;
          hence thesis by A184,A185;
        end;
        suppose
          j < i;
          then ex y being Point of L st y in IT/.i & for x being Point of L
          st x in IT/.j holds x < y by A128,A183;
          hence thesis by A184,A185;
        end;
      end;
A187: for i, j being natural number st i in dom IT & j in dom IT & i <>
      j holds IT/.i <> IT/.j
      proof
        let i, j be natural number such that
A188:   i in dom IT & j in dom IT and
A189:   i <> j;
        IT/.i = IT.i & IT/.j = IT.j by A188,PARTFUN1:def 8;
        hence thesis by A182,A188,A189,FUNCT_1:def 8;
      end;
A190: for A being Subset of L st s in A & A in G1 holds A = PRAWY
      proof
        let A be Subset of L;
        assume that
A191:   s in A and
A192:   A in G1;
A193:   A in F & A is open by A2,A15,A192,TOPS_2:def 1;
A194:   now
          assume
A195:     (ex a being real number st r < a & a <= s & A = [.r,a.[)
or ex a, b being real number st r <= a & a < b & b <= s & A = ].a,b.[;
          per cases by A195;
          suppose
            ex a being real number st r < a & a <= s & A = [.r,a.[;
            hence contradiction by A191,XXREAL_1:3;
          end;
          suppose
            ex a, b being real number st r <= a & a < b & b <= s & A = ].a,b.[;
            hence contradiction by A191,XXREAL_1:4;
          end;
        end;
        A is connected by A3,A15,A192,Def1;
        then consider ak being real number such that
        r <= ak and
A196:   ak < s and
A197:   A = ].ak,s.] by A4,A7,A191,A193,A194,Th57;
A198:   ak is Real by XREAL_0:def 1;
A199:   A c= PRAWY
        proof
          lower_bound A = ak by A196,A197,Th6;
          then ak in RM by A192,A197,A198;
          then
A200:     pP <= ak by XXREAL_2:def 7;
          let a be set;
          assume
A201:     a in A;
          then a in [.r,s.] by A8;
          then reconsider a as Real;
          ak < a by A197,A201,XXREAL_1:2;
          then
A202:     pP < a by A200,XXREAL_0:2;
          a <= s by A197,A201,XXREAL_1:2;
          hence thesis by A202,XXREAL_1:2;
        end;
        assume A <> PRAWY;
        then A c< PRAWY by A199,XBOOLE_0:def 8;
        then A in ZAW by A14,A69,A73,A192;
        hence contradiction by A192,XBOOLE_0:def 5;
      end;
      take IT;
      thus rng IT c= F by A89,A100,XBOOLE_1:1;
      dom IT = Seg len IT by FINSEQ_1:def 3;
      then
A203: card dom IT = card X by A97,FINSEQ_1:78;
      IT is Function of dom IT,X by A100,FUNCT_2:4;
      then
A204: rng IT = X by A203,A182,FINSEQ_4:78;
      hence union rng IT = [.r,s.] by A4,A27,TOPMETR:25;
      ex Z being set st s in Z & Z in C by A28,A8,A27,TARSKI:def 4;
      then PRAWY in X by A26,A190;
      then consider i being set such that
A205: i in dom IT and
A206: IT.i = PRAWY by A204,FUNCT_1:def 5;
      reconsider i as Element of NAT by A205;
A207: i <= len IT by A205,FINSEQ_3:27;
A208: IT/.i = IT.i by A205,PARTFUN1:def 8;
A209: 1 <= i by A205,FINSEQ_3:27;
A210: now
        assume i <> len IT;
        then
A211:   i < len IT by A207,XXREAL_0:1;
        then
A212:   ex S being Element of X st S = IT.i & upper_bound S in IT.(i+1)
        by A97,A99,A209;
        0+1 <= i+1 & i+1 <= len IT by A211,NAT_1:13;
        then
A213:   i+1 in dom IT by FINSEQ_3:27;
        then IT/.(i+1) = IT.(i+1) & IT/.(i+1) in G1 by A106,PARTFUN1:def 8;
        then i+0 = i+1 by A72,A190,A187,A205,A206,A208,A212,A213;
        hence contradiction;
      end;
A214: len IT in dom IT by A101,FINSEQ_5:6;
A215: for n being natural number st 1 < n & n < len IT ex a, b being
      Real st r <= a & a < b & b <= s & IT/.n = ].a,b.[
      proof
        let n be natural number such that
A216:   1 < n and
A217:   n < len IT;
A218:   n in dom IT by A216,A217,FINSEQ_3:27;
        then IT.n in rng IT by FUNCT_1:def 5;
        then
A219:   IT/.n in rng IT by A218,PARTFUN1:def 8;
        then
A220:   IT/.n in X by A100;
        then
A221:   IT/.n in G1 & IT/.n in F by A26,A89;
A222:   IT/.n is open connected Subset of L by A90,A100,A219;
        per cases by A4,A7,A45,A221,A222,Th57;
        suppose
          ex a being real number st r < a & a <= s & IT/.n = [.r,a.[;
          then consider a being real number such that
A223:     r < a and
          a <= s and
A224:     IT/.n = [.r,a.[;
          r in [.r,a.[ by A223,XXREAL_1:3;
          hence thesis by A26,A55,A98,A103,A104,A187,A216,A218,A220,A224;
        end;
        suppose
          ex a being real number st r <= a & a < s & IT/.n = ].a,s.];
          then consider a being real number such that
          r <= a and
A225:     a < s and
A226:     IT/.n = ].a,s.];
          upper_bound ].a,s.] = s & s in ].a,s.] by A225,Th7,XXREAL_1:2;
          hence
          thesis by A26,A83,A214,A187,A206,A208,A210,A217,A218,A220,A226;
        end;
        suppose
          ex a, b being real number st r <= a & a < b & b <= s & IT
          /.n = ].a,b.[;
          then consider a, b being real number such that
A227:     r <= a & a < b & b <= s & IT/.n = ].a,b.[;
          reconsider a, b as Real by XREAL_0:def 1;
          take a, b;
          thus thesis by A227;
        end;
      end;
A228: now
        let n be natural number such that
A229:   1 <= n;
        reconsider m = n as Element of NAT by ORDINAL1:def 13;
        hereby
          assume n <= len IT;
          then m in dom IT & IT/.n = IT.n by A229,FINSEQ_3:27,FINSEQ_4:24;
          then IT/.n in rng IT by FUNCT_1:def 5;
          then IT/.n in X by A100;
          hence IT/.n is non empty by A45,A26;
        end;
        hereby
          assume
A230:     n+1 <= len IT;
          then
A231:     m < len IT by NAT_1:13;
          then
A232:     IT/.n = IT.n by A229,FINSEQ_4:24;
A233:     m in dom IT by A229,A231,FINSEQ_3:27;
          then IT/.n in rng IT by A232,FUNCT_1:def 5;
          then
A234:     IT/.n in X by A100;
          then
A235:     IT/.n in G1 by A26;
A236:     IT /.n is non empty bounded interval Subset of REAL by A9,A45,A16
,A26,A234;
A237:     ex S being Element of X st S = IT.n & upper_bound S in IT.(n+1
          ) by A97,A99,A229,A231;
A238:     1 < m+1 by A229,NAT_1:13;
          then
A239:     IT/.(m+1) = IT.(m+1) by A230,FINSEQ_4:24;
A240:     n+1 in dom IT by A230,A238,FINSEQ_3:27;
          then
A241:     IT/.(n+1) in rng IT by A239,FUNCT_1:def 5;
          then
A242:     IT/.(n+1) in X by A100;
          then
A243:     IT/.(n+1) in G1 by A26;
          n+0 < n+1 by XREAL_1:8;
          then
A244:     IT/.n <> IT/.(n+1) by A187,A233,A240;
A245:     IT/.(n+1) is non empty bounded interval Subset of REAL by A9,A45,A16
,A26,A242;
          IT/.(n+1) c= union X by A100,A241,ZFMISC_1:92;
          then IT/.(n+1) c= [.r,s.] by A4,A27,TOPMETR:25;
          then
A246:     IT/.(n+1) is bounded_above by XXREAL_2:43;
          then
A247:     upper_bound(IT/.n) <= upper_bound(IT/.(n+1)) by A237,A232,A239,
SEQ_4:def 4;
          hereby
            assume
A248:       lower_bound(IT/.n) > lower_bound(IT/.(n+1));
            upper_bound(IT/.(n+1)) = upper_bound(IT/.n) & upper_bound(IT
/.n) in IT/.n implies upper_bound(IT/.(n+1)) in IT/.(n+1) by A26,A83,A214,A187
,A206,A208,A210,A231,A233,A234;
            then IT/.n c= IT/.(n+1) by A236,A245,A247,A248,Th43;
            then IT/.n c< IT/.(n+1) by A244,XBOOLE_0:def 8;
            then IT/.n in ZAW by A14,A235,A243;
            hence contradiction by A26,A234,XBOOLE_0:def 5;
          end;
          thus upper_bound(IT/.n) <= upper_bound(IT/.(n+1)) by A237,A232,A239
,A246,SEQ_4:def 4;
          per cases by A230,XXREAL_0:1;
          suppose
A249:       n+1 = len IT;
            then pP < upper_bound(IT/.n) by A206,A210,A237,A232,XXREAL_1:2;
            hence
            lower_bound(IT/.(n+1)) < upper_bound(IT/.n) by A206,A208,A210,A249
,Th6,XXREAL_1:26;
          end;
          suppose
            n+1 < len IT;
            then consider a1, b1 being Real such that
            r <= a1 and
A250:       a1 < b1 and
            b1 <= s and
A251:       IT/.(n+1) = ].a1,b1.[ by A215,A238;
            a1 < upper_bound(IT/.n) by A237,A232,A239,A251,XXREAL_1:4;
            hence lower_bound(IT/.(n+1)) < upper_bound(IT/.n) by A250,A251,
TOPREAL6:22;
          end;
        end;
      end;
      hereby
        let n be natural number such that
A252:   1 <= n;
        thus
A253:   (n <= len IT implies IT/.n is non empty) & (n+1 <= len IT
implies lower_bound(IT/.n) <= lower_bound(IT/.(n+1)) & upper_bound(IT/.n) <=
upper_bound(IT/.(n+1)) & lower_bound(IT/.(n+1)) < upper_bound(IT/.n)) by A228
,A252;
        reconsider m = n as Nat;
A254:   n+0 < n+1 by XREAL_1:8;
        then
A255:   1 < m+1 by A252,XXREAL_0:2;
        assume
A256:   n+2 <= len IT;
        then
A257:   n+1+1 <= len IT;
        then
A258:   m+1 < len IT by NAT_1:13;
        then
A259:   n+1 in dom IT by A255,FINSEQ_3:27;
        then IT/.(n+1) = IT.(n+1) by PARTFUN1:def 8;
        then IT/.(n+1) in rng IT by A259,FUNCT_1:def 5;
        then
A260:   IT/.(n+1) in X by A100;
        0+1 <= n+1 by XREAL_1:8;
        then
A261:   upper_bound(IT/.(n+1)) <= upper_bound(IT/.(n+1+1)) by A228,A256;
        assume
A262:   upper_bound(IT/.n) > lower_bound(IT/.(n+2));
        consider a1, b1 being Real such that
        r <= a1 and
A263:   a1 < b1 and
        b1 <= s and
A264:   IT/.(n+1) = ].a1,b1.[ by A215,A255,A258;
A265:   lower_bound ].a1,b1.[ = a1 by A263,TOPREAL6:22;
A266:   upper_bound ].a1,b1.[ = b1 by A263,TOPREAL6:22;
A267:   IT/.(n+1) c= IT/.n \/ IT/.(n+2)
        proof
          let x be set;
          assume
A268:     x in IT/.(n+1);
          then reconsider x as Real;
A269:     a1 < x by A264,A268,XXREAL_1:4;
A270:     x < b1 by A264,A268,XXREAL_1:4;
          per cases;
          suppose
A271:       x < upper_bound(IT/.n);
            per cases;
            suppose
A272:         n = 1;
              then lower_bound(IT/.n) <= x by A8,A53,A98,A104,A260,A268,
XXREAL_1:1;
              then x in IT/.n by A42,A54,A53,A98,A104,A271,A272,XXREAL_1:3;
              hence thesis by XBOOLE_0:def 3;
            end;
            suppose
A273:         n <> 1;
              n+0 < n+2 by XREAL_1:8;
              then
A274:         n < len IT by A256,XXREAL_0:2;
A275:         lower_bound(IT/.n) < x by A253,A257,A264,A265,A269,NAT_1:13
,XXREAL_0:2;
              1 < n by A252,A273,XXREAL_0:1;
              then consider a, b being Real such that
              r <= a and
A276:         a < b and
              b <= s and
A277:         IT/.n = ].a,b.[ by A215,A274;
              lower_bound(IT/.n) = a & upper_bound(IT/.n) = b by A276,A277,
TOPREAL6:22;
              then x in IT/.n by A271,A277,A275,XXREAL_1:4;
              hence thesis by XBOOLE_0:def 3;
            end;
          end;
          suppose
            x >= upper_bound(IT/.n);
            then
A278:       x > lower_bound(IT/.(n+2)) by A262,XXREAL_0:2;
            per cases;
            suppose
A279:         len IT = n+2;
              x <= s by A8,A260,A268,XXREAL_1:1;
              then x in IT/.(n+2) by A70,A206,A208,A210,A278,A279,XXREAL_1:2;
              hence thesis by XBOOLE_0:def 3;
            end;
            suppose
A280:         len IT <> n+2;
              n+1 < n+2 by XREAL_1:8;
              then
A281:         1 < n+2 by A255,XXREAL_0:2;
              n+1+1 < len IT by A256,A280,XXREAL_0:1;
              then consider a2, b2 being Real such that
              r <= a2 and
A282:         a2 < b2 and
              b2 <= s and
A283:         IT/.(n+2) = ].a2,b2.[ by A215,A281;
              upper_bound ].a2,b2.[ = b2 by A282,TOPREAL6:22;
              then
A284:         x < b2 by A261,A264,A266,A270,A283,XXREAL_0:2;
              lower_bound ].a2,b2.[ = a2 by A282,TOPREAL6:22;
              then x in IT/.(n+2) by A278,A283,A284,XXREAL_1:4;
              hence thesis by XBOOLE_0:def 3;
            end;
          end;
        end;
        m+1 <= m+2 by XREAL_1:8;
        then 1 <= m+2 by A255,XXREAL_0:2;
        then
A285:   m+2 in dom IT by A256,FINSEQ_3:27;
        then IT/.(n+2) = IT.(n+2) by PARTFUN1:def 8;
        then IT/.(n+2) in rng IT by A285,FUNCT_1:def 5;
        then
A286:   IT/.(n+2) in X by A100;
        m <= len IT by A254,A258,XXREAL_0:2;
        then
A287:   n in dom IT by A252,FINSEQ_3:27;
        then IT/.n = IT.n by PARTFUN1:def 8;
        then IT/.n in rng IT by A287,FUNCT_1:def 5;
        then
A288:   IT/.n in X by A100;
        n+1 < n+2 by XREAL_1:8;
        then
A289:   IT/.(n+2) <> IT/.(n+1) by A187,A259,A285;
        n+0 < n+1 by XREAL_1:8;
        then IT/.n <> IT/.(n+1) by A187,A287,A259;
        hence contradiction by A22,A23,A24,A288,A260,A286,A289,A267,Th62;
      end;
      thus [.r,s.] in F implies IT = <*[.r,s.]*> by A7;
      assume not [.r,s.] in F;
      thus ex p being real number st r < p & p <= s & IT.1 = [.r,p.[
      proof
        take kL;
        thus r < kL by A42,A52,Th5;
        upper_bound LEWY <= upper_bound [.r,s.] by A8,SEQ_4:65;
        hence kL <= s by A4,A51,JORDAN5A:20;
        thus thesis by A98;
      end;
      thus ex p being real number st r <= p & p < s & IT.len IT = ].p,s.]
      proof
        take pP;
        lower_bound [.r,s.] <= lower_bound PRAWY by A8,SEQ_4:64;
        hence r <= pP by A4,A70,JORDAN5A:20;
        thus pP < s by A68,A71,Th6;
        thus thesis by A206,A210;
      end;
      let n be natural number such that
A290: 1 < n & n < len IT;
      consider a, b being Real such that
A291: r <= a & a < b & b <= s & IT/.n = ].a,b.[ by A215,A290;
      take a, b;
      thus thesis by A290,A291,FINSEQ_4:24;
    end;
  end;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <=
  s & [.r,s.] in F implies <*[.r,s.]*> is IntervalCover of F
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is connected and
A2: r <= s & [.r,s.] in F;
  set f = <*[.r,s.]*>;
A3: for n being natural number st 1 <= n holds (n <= len f implies f/.n is
non empty) & (n+1 <= len f implies lower_bound(f/.n) <= lower_bound(f/.(n+1)) &
  upper_bound(f/.n) <= upper_bound(f/.(n+1)) & lower_bound(f/.(n+1)) <
upper_bound(f/.n)) & (n+2 <= len f implies upper_bound(f/.n) <= lower_bound(f/.
  (n+2))) by A2,Lm3;
  rng f c= F & union rng f = [.r,s.] by A2,Lm3;
  hence thesis by A1,A2,A3,Def2;
end;

reserve C for IntervalCover of F;

theorem Th64:
  for F being Subset-Family of Closed-Interval-TSpace(r,r), C
being IntervalCover of F holds F is Cover of Closed-Interval-TSpace(r,r) & F is
  open connected implies C = <*[.r,r.]*>
proof
  set L = Closed-Interval-TSpace(r,r);
  let F be Subset-Family of L, C be IntervalCover of F;
  assume that
A1: F is Cover of L and
A2: F is open & F is connected;
A3: [.r,r.] = {r} by XXREAL_1:17;
  the carrier of L = [.r,r.] by TOPMETR:25;
  then r in the carrier of L by A3,TARSKI:def 1;
  then {r} in F by A1,Th60;
  hence thesis by A1,A2,A3,Def2;
end;

theorem Th65:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s implies 1 <= len C
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is connected and
A2: r <= s;
  assume not thesis;
  then len C+1 <= 0+1 by NAT_1:13;
  then
A3: C is empty by XREAL_1:8;
  union rng C = [.r,s.] by A1,A2,Def2;
  hence thesis by A2,A3,RELAT_1:60,XXREAL_1:1,ZFMISC_1:2;
end;

theorem Th66:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s & len C = 1 implies C = <*[.r,s.]*>
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s and
A2: len C = 1;
A3: union rng C = [.r,s.] by A1,Def2;
  C is non empty by A2;
  then rng C is non empty;
  then 1 in dom C by FINSEQ_3:34;
  then
A4: C.1 in rng C by FUNCT_1:def 5;
  C.1 = [.r,s.]
  proof
    thus for a being set st a in C.1 holds a in [.r,s.] by A3,A4,TARSKI:def 4;
    let a be set;
A5: dom C = {1} by A2,FINSEQ_1:4,def 3;
    assume a in [.r,s.];
    then consider Z being set such that
A6: a in Z and
A7: Z in rng C by A3,TARSKI:def 4;
    ex x being set st x in dom C & C.x = Z by A7,FUNCT_1:def 5;
    hence thesis by A6,A5,TARSKI:def 1;
  end;
  hence thesis by A2,FINSEQ_1:57;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <=
s & n in dom C & m in dom C & n < m implies lower_bound(C/.n) <= lower_bound(C
  /.m)
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s and
A2: n in dom C and
A3: m in dom C & n < m;
  defpred P[natural number] means $1 in dom C & n < $1 implies lower_bound(C/.
  n) <= lower_bound(C/.$1);
A4: for k being natural number st P[k] holds P[k+1]
  proof
    let k be natural number such that
A5: P[k] and
A6: k+1 in dom C and
A7: n < k+1;
    per cases by A6,TOPREALA:23;
    suppose
      k = 0;
      then n = 0 by A7,NAT_1:13;
      hence thesis by A2,FINSEQ_3:26;
    end;
    suppose
A8:   k in dom C;
A9:   k+1 <= len C by A6,FINSEQ_3:27;
A10:  n <= k by A7,NAT_1:13;
      1 <= k by A8,FINSEQ_3:27;
      then lower_bound(C/.k) <= lower_bound(C/.(k+1)) by A1,A9,Def2;
      hence thesis by A5,A8,A10,XXREAL_0:1,2;
    end;
  end;
A11: P[0];
  for k being natural number holds P[k] from NAT_1:sch 2(A11,A4);
  hence thesis by A3;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <=
s & n in dom C & m in dom C & n < m implies upper_bound(C/.n) <= upper_bound(C
  /.m)
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s and
A2: n in dom C and
A3: m in dom C & n < m;
  defpred P[natural number] means $1 in dom C & n < $1 implies upper_bound(C/.
  n) <= upper_bound(C/.$1);
A4: for k being natural number st P[k] holds P[k+1]
  proof
    let k be natural number such that
A5: P[k] and
A6: k+1 in dom C and
A7: n < k+1;
    per cases by A6,TOPREALA:23;
    suppose
      k = 0;
      then n = 0 by A7,NAT_1:13;
      hence thesis by A2,FINSEQ_3:26;
    end;
    suppose
A8:   k in dom C;
A9:   k+1 <= len C by A6,FINSEQ_3:27;
A10:  n <= k by A7,NAT_1:13;
      1 <= k by A8,FINSEQ_3:27;
      then upper_bound(C/.k) <= upper_bound(C/.(k+1)) by A1,A9,Def2;
      hence thesis by A5,A8,A10,XXREAL_0:1,2;
    end;
  end;
A11: P[0];
  for k being natural number holds P[k] from NAT_1:sch 2(A11,A4);
  hence thesis by A3;
end;

theorem Th69:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
& r <= s & 1 <= n & n+1 <= len C implies ].lower_bound(C/.(n+1)),upper_bound(C
  /.n).[ is non empty
proof
  assume F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s & 1 <= n & n+1 <= len C;
  then lower_bound(C/.(n+1)) < upper_bound(C/.n) by Def2;
  hence thesis by XXREAL_1:33;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <=
  s implies lower_bound(C/.1) = r
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is connected and
A2: r <= s;
  1 <= len C by A1,A2,Th65;
  then
A3: C.1 = C/.1 by FINSEQ_4:24;
  per cases;
  suppose
    [.r,s.] in F;
    then C = <*[.r,s.]*> by A1,A2,Def2;
    then C/.1 = [.r,s.] by FINSEQ_4:25;
    hence thesis by A2,JORDAN5A:20;
  end;
  suppose
    not [.r,s.] in F;
    then ex p being real number st r < p & p <= s & C.1 = [.r,p.[ by A1,A2,Def2
;
    hence thesis by A3,Th4;
  end;
end;

theorem Th71:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s implies r in C/.1
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is connected and
A2: r <= s;
  1 <= len C by A1,A2,Th65;
  then
A3: C.1 = C/.1 by FINSEQ_4:24;
  per cases;
  suppose
    [.r,s.] in F;
    then C = <*[.r,s.]*> by A1,A2,Def2;
    then C/.1 = [.r,s.] by FINSEQ_4:25;
    hence thesis by A2,XXREAL_1:1;
  end;
  suppose
    not [.r,s.] in F;
    then ex p being real number st r < p & p <= s & C.1 = [.r,p.[ by A1,A2,Def2
;
    hence thesis by A3,XXREAL_1:3;
  end;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <=
  s implies upper_bound(C/.len C) = s
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is connected and
A2: r <= s;
  1 <= len C by A1,A2,Th65;
  then
A3: C.len C = C/.len C by FINSEQ_4:24;
  per cases;
  suppose
    [.r,s.] in F;
    then C = <*[.r,s.]*> by A1,A2,Def2;
    then C/.1 = [.r,s.] & len C = 1 by FINSEQ_1:56,FINSEQ_4:25;
    hence thesis by A2,JORDAN5A:20;
  end;
  suppose
    not [.r,s.] in F;
    then
    ex p being real number st r <= p & p < s & C.len C = ].p,s.] by A1,A2,Def2;
    hence thesis by A3,Th7;
  end;
end;

theorem Th73:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s implies s in C/.len C
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is connected and
A2: r <= s;
  1 <= len C by A1,A2,Th65;
  then
A3: C.len C = C/.len C by FINSEQ_4:24;
  per cases;
  suppose
    [.r,s.] in F;
    then C = <*[.r,s.]*> by A1,A2,Def2;
    then C/.1 = [.r,s.] & len C = 1 by FINSEQ_1:56,FINSEQ_4:25;
    hence thesis by A2,XXREAL_1:1;
  end;
  suppose
    not [.r,s.] in F;
    then
    ex p being real number st r <= p & p < s & C.len C = ].p,s.] by A1,A2,Def2;
    hence thesis by A3,XXREAL_1:2;
  end;
end;

definition
  let r, s be real number;
  let F be Subset-Family of Closed-Interval-TSpace(r,s), C be IntervalCover of
  F such that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s;
  mode IntervalCoverPts of C -> FinSequence of REAL means
    :Def3:
    len it = len
C + 1 & it.1 = r & it.len it = s & for n being natural number st 1 <= n & n+1 <
    len it holds it.(n+1) in ].lower_bound(C/.(n+1)),upper_bound(C/.n).[;
  existence
  proof
    set A = len C + 1;
    defpred P[natural number,set] means ($1 = 1 implies $2 = r) & ($1 = A
implies $2 = s) & (2 <= $1 & $1 <= len C implies $2 in ].lower_bound(C/.$1),
    upper_bound(C/.($1-1)).[);
A2: 0+1 <= len C by A1,Th65;
    then
A3: 0+1 < A by XREAL_1:8;
A4: for k being Nat st k in Seg A ex x being Element of REAL st P[k,x]
    proof
      reconsider r, s as Real by XREAL_0:def 1;
      let k be Nat;
A5:   len C + 0 < A by XREAL_1:8;
      assume k in Seg A;
      then
A6:   1 <= k & k <= A by FINSEQ_1:3;
      per cases by A6,XXREAL_0:1;
      suppose
A7:     k = 1;
        take r;
        thus thesis by A2,A7;
      end;
      suppose
A8:     k = A;
        take s;
        thus thesis by A1,A5,A8,Th65;
      end;
      suppose that
A9:     1 < k and
A10:    k < A;
A11:    k-1 in NAT by A9,INT_1:18;
A12:    k <= len C by A10,NAT_1:13;
        1-1 < k-1 by A9,XREAL_1:16;
        then 0+1 <= k-1 by A11,NAT_1:13;
        then
        ].lower_bound(C/.(k-1+1)),upper_bound(C/.(k-1)).[ is non empty by A1
,A11,A12,Th69;
        then consider x being set such that
A13:    x in ].lower_bound(C/.(k-1+1)),upper_bound(C/.(k-1)).[ by
XBOOLE_0:def 1;
        reconsider x as Real by A13;
        take x;
        thus thesis by A9,A10,A13;
      end;
    end;
    consider p being FinSequence of REAL such that
A14: dom p = Seg A and
A15: for k being Nat st k in Seg A holds P[k,p.k] from FINSEQ_1:sch 5(
    A4);
    take p;
    thus
A16: len p = len C + 1 by A14,FINSEQ_1:def 3;
    1 in Seg A by A3,FINSEQ_1:3;
    hence p.1 = r by A15;
    len p in Seg A by A3,A16,FINSEQ_1:3;
    hence p.len p = s by A15,A16;
    let n be natural number;
    assume 1 <= n;
    then
A17: 1+1 <= n+1 by XREAL_1:8;
    assume
A18: n+1 < len p;
    0+1 <= n+1 by XREAL_1:8;
    then
A19: n+1 in Seg A by A16,A18,FINSEQ_1:3;
    n+1 <= len C by A16,A18,NAT_1:13;
    then p.(n+1) in ].lower_bound(C/.(n+1)),upper_bound(C/.(n+1-1)).[ by A15
,A19,A17;
    hence thesis;
  end;
end;

reserve G for IntervalCoverPts of C;

theorem Th74:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s implies 2 <= len G
proof
  assume
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s;
  then 1 <= len C by Th65;
  then 1+1 <= len C + 1 by XREAL_1:8;
  hence thesis by A1,Def3;
end;

theorem Th75:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s & len C = 1 implies G = <*r,s*>
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s and
A2: len C = 1;
A3: G.1 = r by A1,Def3;
A4: len G = len C + 1 by A1,Def3;
  then G.2 = s by A1,A2,Def3;
  hence thesis by A2,A4,A3,FINSEQ_1:61;
end;

theorem Th76:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s & 1 <= n & n+1 < len G implies G.(n+1) < upper_bound(C/.n)
proof
  assume F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s & 1 <= n & n+1 < len G;
  then G.(n+1) in ].lower_bound(C/.(n+1)),upper_bound(C/.n).[ by Def3;
  hence thesis by XXREAL_1:4;
end;

theorem Th77:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s & 1 < n & n <= len C implies lower_bound(C/.n) < G.n
proof
  set w = n-'1;
  assume
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is
  connected & r <= s;
  then
A2: len G = len C + 1 by Def3;
  assume that
A3: 1 < n and
A4: n <= len C;
A5: n < len C + 1 by A4,NAT_1:13;
  1-1 <= n-1 by A3,XREAL_1:11;
  then
A6: w = n-1 by XREAL_0:def 2;
  then n = w+1;
  then 1 <= w by A3,NAT_1:13;
  then G.(w+1) in ].lower_bound(C/.(w+1)),upper_bound(C/.w).[ by A1,A2,A6,A5
,Def3;
  hence thesis by A6,XXREAL_1:4;
end;

theorem Th78:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r <= s & 1 <= n & n < len C implies G.n <= lower_bound(C/.(n+1))
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is connected and
A2: r <= s;
  set w = n-'1;
  assume that
A3: 1 <= n and
A4: n < len C;
A5: n+1 <= len C by A4,NAT_1:13;
  per cases by A3,XXREAL_0:1;
  suppose
A6: n = 1;
    0+1 <= n+1 by XREAL_1:8;
    then
A7: C/.(n+1) is non empty by A1,A2,A5,Def2;
A8: G.1 = r by A1,A2,Def3;
A9: rng C c= F by A1,A2,Def2;
    1+1 <= len C by A4,A6,NAT_1:13;
    then
A10: 2 in dom C by FINSEQ_3:27;
    then C.2 in rng C by FUNCT_1:def 5;
    then C.2 in F by A9;
    then C/.2 in F by A10,PARTFUN1:def 8;
    then C/.(n+1) c= the carrier of Closed-Interval-TSpace(r,s) by A6;
    then
A11: C/.(n+1) c= [.r,s.] by A2,TOPMETR:25;
    then C/.(n+1) is bounded_below by XXREAL_2:44;
    then lower_bound(C/.(n+1)) in [.r,s.] by A7,A11,Th1;
    hence thesis by A6,A8,XXREAL_1:1;
  end;
  suppose
    1 < n;
    then
A12: 1-1 < n-1 by XREAL_1:11;
    then
A13: w = n-1 by XREAL_0:def 2;
    then
A14: 0+1 <= w by A12,NAT_1:13;
    len G = len C + 1 by A1,A2,Def3;
    then
A15: n+1 < len G - 1+1 by A4,XREAL_1:8;
    n-1 < n-0 by XREAL_1:17;
    then w+1 < n+1 by A13,XREAL_1:8;
    then w+1 < len G by A15,XXREAL_0:2;
    then
A16: G.(w+1) < upper_bound(C/.w) by A1,A2,A14,Th76;
    n+1 <= len C by A4,NAT_1:13;
    then upper_bound(C/.w) <= lower_bound(C/.(w+2)) by A1,A2,A13,A14,Def2;
    hence thesis by A13,A16,XXREAL_0:2;
  end;
end;

theorem Th79:
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected
  & r < s implies G is increasing
proof
  assume that
A1: F is Cover of Closed-Interval-TSpace(r,s) & F is open & F is connected and
A2: r < s;
  let m, n be Element of NAT such that
A3: m in dom G & n in dom G & m < n;
  defpred P[Nat] means m < $1 & m in dom G & $1 in dom G implies G.m < G.$1;
A4: for k being Nat st P[k] holds P[k+1]
  proof
A5: len G = len C + 1 by A1,A2,Def3;
    let k be Nat such that
A6: P[k] and
A7: m < k+1 and
A8: m in dom G and
A9: k+1 in dom G;
A10: 1 <= m by A8,FINSEQ_3:27;
A11: k+1 <= len G by A9,FINSEQ_3:27;
    k+0 <= k+1 by XREAL_1:8;
    then
A12: k <= len G by A11,XXREAL_0:2;
A13: m <= k by A7,NAT_1:13;
    then
A14: 1 <= k by A10,XXREAL_0:2;
    per cases by A14,A11,XXREAL_0:1;
    suppose that
A15:  1 < k and
A16:  k+1 < len G;
      G.(k+1) in ].lower_bound(C/.(k+1)),upper_bound(C/.k).[ by A1,A2,A15,A16
,Def3;
      then
A17:  lower_bound(C/.(k+1)) < G.(k+1) by XXREAL_1:4;
      k < len C by A5,A16,XREAL_1:8;
      then G.k <= lower_bound(C/.(k+1)) by A1,A2,A15,Th78;
      then G.k < G.(k+1) by A17,XXREAL_0:2;
      hence thesis by A6,A8,A13,A12,A15,FINSEQ_3:27,XXREAL_0:1,2;
    end;
    suppose
A18:  k = 1;
A19:  1 <= len C by A1,A2,Th65;
A20:  m <= 1 by A7,A18,NAT_1:13;
      per cases by A19,XXREAL_0:1;
      suppose
A21:    1 < len C;
        then 1+1 <= len C by NAT_1:13;
        then
A22:    lower_bound(C/.2) < G.2 by A1,A2,Th77;
        G.1 <= lower_bound(C/.(1+1)) by A1,A2,A21,Th78;
        then G.1 < G.2 by A22,XXREAL_0:2;
        hence thesis by A10,A18,A20,XXREAL_0:1;
      end;
      suppose
        1 = len C;
        then G = <*r,s*> by A1,A2,Th75;
        then G.1 = r & G.2 = s by FINSEQ_1:61;
        hence thesis by A2,A10,A18,A20,XXREAL_0:1;
      end;
    end;
    suppose
A23:  k+1 = len G;
      then
A24:  G.(k+1) = s by A1,A2,Def3;
      per cases by A10,XXREAL_0:1;
      suppose
A25:    1 < m;
        set z = m-'1;
        1-1 <= m-1 by A10,XREAL_1:11;
        then
A26:    z = m-1 by XREAL_0:def 2;
        then
A27:    z+1 < len G by A7,A23;
        then
A28:    z <= len C by A5,XREAL_1:8;
        1+1 <= m by A25,NAT_1:13;
        then
A29:    1+1-1 <= m-1 by XREAL_1:11;
        then
A30:    1 <= z by XREAL_0:def 2;
        then
A31:    C/.z is non empty by A1,A2,A28,Def2;
A32:    rng C c= F by A1,A2,Def2;
A33:    z in dom C by A30,A28,FINSEQ_3:27;
        then C.z in rng C by FUNCT_1:def 5;
        then C.z in F by A32;
        then C/.z in F by A33,PARTFUN1:def 8;
        then C/.z c= the carrier of Closed-Interval-TSpace(r,s);
        then
A34:    C/.z c= [.r,s.] by A2,TOPMETR:25;
        then C/.z is bounded_above by XXREAL_2:43;
        then upper_bound(C/.z) in [.r,s.] by A34,A31,Th2;
        then
A35:    upper_bound(C/.z) <= s by XXREAL_1:1;
        G.m < upper_bound(C/.z) by A1,A2,A26,A29,A27,Th76;
        hence thesis by A24,A35,XXREAL_0:2;
      end;
      suppose
        m = 1;
        hence thesis by A1,A2,A24,Def3;
      end;
    end;
  end;
A36: P[0];
  for k being Nat holds P[k] from NAT_1:sch 2(A36,A4);
  hence thesis by A3;
end;

theorem
  F is Cover of Closed-Interval-TSpace(r,s) & F is open connected & r <=
  s & 1 <= n & n < len G implies [.G.n,G.(n+1).] c= C.n
proof
  set L = Closed-Interval-TSpace(r,s);
  assume that
A1: F is Cover of L & F is open and
A2: F is connected and
A3: r <= s and
A4: 1 <= n and
A5: n < len G;
A6: len G = len C + 1 by A1,A2,A3,Def3;
  then
A7: n <= len C by A5,NAT_1:13;
  then
A8: C/.n = C.n by A4,FINSEQ_4:24;
  n in dom C by A4,A7,FINSEQ_3:27;
  then
A9: C.n in rng C by FUNCT_1:def 5;
  rng C c= F by A1,A2,A3,Def2;
  then C/.n in F by A8,A9;
  then C/.n is connected Subset of L by A2,Def1;
  then
A10: C/.n is interval by Th56;
A11: C/.n is non empty by A1,A2,A3,A4,A7,Def2;
A12: n+1 <= len G by A5,NAT_1:13;
  0+1 <= n+1 by XREAL_1:8;
  then
A13: n+1 in dom G by A12,FINSEQ_3:27;
A14: n in dom G by A4,A5,FINSEQ_3:27;
A15: n+0 < n+1 by XREAL_1:8;
  per cases by A3,XXREAL_0:1;
  suppose
    r = s;
    then C = <*[.r,r.]*> by A1,A2,Th64;
    then
A16: len C = 1 by FINSEQ_1:57;
    then G = <*r,s*> by A1,A2,A3,Th75;
    then
A17: G.1 = r & G.2 = s by FINSEQ_1:61;
    n = 1 & C = <*[.r,s.]*> by A1,A2,A3,A4,A7,A16,Th66,XXREAL_0:1;
    hence thesis by A17,FINSEQ_1:57;
  end;
  suppose
    r < s;
    then G is increasing by A1,A2,Th79;
    then
A18: G.n < G.(n+1) by A14,A13,A15,SEQM_3:def 1;
A19: 2 <= len G by A1,A2,A3,Th74;
    per cases by A4,A12,A19,XXREAL_0:1;
    suppose that
A20:  n = 1 and
A21:  len G = 2;
      G = <*r,s*> by A1,A2,A3,A6,A21,Th75;
      then
A22:  G.1 = r & G.2 = s by FINSEQ_1:61;
      C = <*[.r,s.]*> by A1,A2,A3,A6,A21,Th66;
      hence thesis by A20,A22,FINSEQ_1:57;
    end;
    suppose that
A23:  n = 1 and
A24:  1+1 < len G;
      G.(1+1) in ].lower_bound(C/.(1+1)),upper_bound(C/.1).[ by A1,A2,A3,A24
,Def3;
      then
A25:  lower_bound(C/.(1+1)) < G.2 by XXREAL_1:4;
      1+1 <= len C by A6,A24,NAT_1:13;
      then lower_bound(C/.1) <= lower_bound(C/.(1+1)) by A1,A2,A3,Def2;
      then
A26:  lower_bound(C/.n) < G.(n+1) by A23,A25,XXREAL_0:2;
A27:  G.1 = r & r in C/.1 by A1,A2,A3,Def3,Th71;
      G.(n+1) < upper_bound(C/.n) by A1,A2,A3,A23,A24,Th76;
      then G.(n+1) in C.n by A8,A10,A11,A26,Th42;
      hence thesis by A8,A10,A23,A27,XXREAL_2:def 12;
    end;
    suppose that
A28:  1 < n and
A29:  len G = n+1;
      1-1 < n-1 by A28,XREAL_1:11;
      then
A30:  0+1 <= n-1 & n-1 is Element of NAT by INT_1:16,20;
      then G.(n-1+1) in ].lower_bound(C/.(n-1+1)),upper_bound(C/.(n-1)).[ by A1
,A2,A3,A15,A29,Def3;
      then
A31:  G.n < upper_bound(C/.(n-1)) by XXREAL_1:4;
      upper_bound(C/.(n-1)) <= upper_bound(C/.(n-1+1)) by A1,A2,A3,A6,A29,A30
,Def2;
      then
A32:  G.n < upper_bound(C/.n) by A31,XXREAL_0:2;
      G.len G = s by A1,A2,A3,Def3;
      then
A33:  G.(n+1) in C.n by A1,A2,A3,A6,A8,A29,Th73;
      lower_bound(C/.n) < G.n by A1,A2,A3,A6,A28,A29,Th77;
      then G.n in C.n by A8,A10,A11,A32,Th42;
      hence thesis by A8,A10,A33,XXREAL_2:def 12;
    end;
    suppose that
A34:  1 < n and
A35:  n+1 < len G;
A36:  G.(n+1) < upper_bound(C/.n) by A1,A2,A3,A4,A35,Th76;
      n <= len C by A5,A6,NAT_1:13;
      then
A37:  lower_bound(C/.n) < G.n by A1,A2,A3,A34,Th77;
      then lower_bound(C/.n) < G.(n+1) by A18,XXREAL_0:2;
      then
A38:  G.(n+1) in C.n by A8,A10,A11,A36,Th42;
      G.n < upper_bound(C/.n) by A18,A36,XXREAL_0:2;
      then G.n in C.n by A8,A10,A11,A37,Th42;
      hence thesis by A8,A10,A38,XXREAL_2:def 12;
    end;
  end;
end;
