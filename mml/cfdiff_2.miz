:: Cauchy-Riemann Differential Equations of Complex Functions
::  by Hiroshi Yamazaki , Yasunari Shidama , Chanapat Pacharapokin and Yatsuka Nakamura
:: 
:: Received April 7, 2009
:: Copyright (c) 2009 Association of Mizar Users

environ

 vocabularies PRE_TOPC, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, FINSEQ_1, FINSEQ_7,
      RVSUM_1, RELAT_1, COMPLEX1, ABSVALUE, XCMPLX_0, EUCLID, VECTSP_1,
      SQUARE_1, ORDINAL2, SEQ_1, SEQ_2, RCOMP_1, FINSEQ_4, PARTFUN1, NORMSP_1,
      LOPBAN_1, FDIFF_1, PDIFF_1, REAL_NS1, BORSUK_1, CFUNCT_1, CFDIFF_1,
      ARYTM_3, ANPROJ_1, SEQM_3, COMSEQ_1, MCART_1, OPPCAT_1;
 notations TARSKI, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_2, RELSET_1, PARTFUN1,
      BINOP_1, BINOP_2, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0,
      COMPLEX1, REAL_1, NAT_1, VALUED_1, SEQ_1, SEQ_2, RLVECT_1, COMSEQ_1,
      COMSEQ_2, COMSEQ_3, CFUNCT_1, PRE_TOPC, STRUCT_0, RVSUM_1, FINSEQ_1,
      FINSEQ_2, FINSEQ_7, SQUARE_1, RCOMP_1, EUCLID, NORMSP_1, REAL_NS1,
      LOPBAN_1, NFCONT_1, FDIFF_1, NDIFF_1, PDIFF_1, CFDIFF_1;
 constructors REAL_1, SQUARE_1, RSSPACE3, PARTFUN1, PCOMPS_1, SEQ_1, SEQ_2,
      BINOP_2, FINSEQ_7, REVROT_1, NFCONT_1, RCOMP_1, FDIFF_1, NDIFF_1,
      PDIFF_1, CFDIFF_1, COMSEQ_2, COMSEQ_3, RVSUM_1, RELSET_1;
 registrations RELSET_1, STRUCT_0, ORDINAL1, MEMBERED, NUMBERS, XBOOLE_0,
      XREAL_0, XXREAL_0, XCMPLX_0, FUNCT_2, REAL_NS1, COMSEQ_3, VALUED_0,
      VALUED_1, SEQM_3, SEQ_4, FDIFF_1, CFDIFF_1, FINSEQ_2;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions LOPBAN_1, EUCLID, COMPLEX1, PCOMPS_1, PDIFF_1, VALUED_0, VALUED_1,
      RVSUM_1, XCMPLX_0, NAT_1, ORDINAL1;
 theorems TARSKI, ABSVALUE, XBOOLE_1, XREAL_0, EUCLID, XREAL_1, XXREAL_0,
      RCOMP_1, RLVECT_1, SEQ_4, COMPLEX1, SEQ_1, SEQ_2, FINSEQ_1, XCMPLX_1,
      VALUED_1, BINOP_2, FINSEQ_2, FINSEQ_7, RVSUM_1, RELAT_1, FUNCT_1,
      FUNCT_2, NORMSP_1, LOPBAN_1, PARTFUN1, NFCONT_1, FDIFF_1, NDIFF_1,
      REAL_NS1, NUMBERS, FUNCT_3, SQUARE_1, CFUNCT_1, CFDIFF_1, PDIFF_1,
      SUBSET_1, XCMPLX_0, COMSEQ_1, COMSEQ_2, COMSEQ_3, JGRAPH_1, VALUED_0,
      ORDINAL1;
 schemes SEQ_1, FUNCT_2;

begin

 reserve k, k1, n, n1, m, m1 for Element of NAT;
 reserve X, y for set;
 reserve Y for Subset of COMPLEX;
 reserve f, f1, f2 for PartFunc of COMPLEX,COMPLEX;
 reserve r, p for Real;
 reserve a, a1, a2, b, b1, b2, x, x0, y, y0, z, z0 for Complex;

LM03:
for seq be Real_Sequence, cseq be Complex_Sequence
st seq = cseq holds seq is convergent iff cseq is convergent
proof
  let seq be Real_Sequence,
  cseq be Complex_Sequence such that
  A1: seq = cseq;
  deffunc F(Element of NAT) = 0;
  consider bseq being Real_Sequence such that
  A2: for n holds bseq.n = F(n) from SEQ_1:sch 1;
  now
    let n be Nat;
    n in NAT by ORDINAL1:def 13;
    hence bseq.n = 0 by A2;
  end; then
  A3a: bseq is constant by VALUED_0:def 18;
  for n holds Re (cseq.n) = seq.n & Im (cseq.n) = bseq.n
  proof
    let n;
    thus Re (cseq.n) = seq.n by A1, COMPLEX1:def 2;
    Im (cseq.n) = Im (seq.n) by A1; then
    Im (cseq.n) = 0 by COMPLEX1:def 3;
    hence Im (cseq.n) = bseq.n by A2;
  end;
  hence thesis by A3a, COMSEQ_3:38;
end;

LM04:
for seq be Real_Sequence, cseq be Complex_Sequence,
rlim be real number st seq=cseq & seq is convergent & lim seq = rlim
holds lim cseq = rlim
proof
  let seq be Real_Sequence, cseq be Complex_Sequence,
  rlim be real number such that
  A1: seq = cseq and
  A2: seq is convergent and
  A4: lim seq = rlim;
  reconsider clim = rlim as Complex by XCMPLX_0:def 2;
  A5: cseq is convergent by A1, A2, LM03;
  for p be Real st 0<p ex n st for m st n<=m holds abs(cseq.m-clim)<p
  by A1, A2, A4, SEQ_2:def 7;
  hence thesis by A5, COMSEQ_2:def 5;
end;

LM05:
for seq be Real_Sequence, cseq be Complex_Sequence
st seq=cseq & seq is convergent & lim seq = 0
holds lim cseq = 0 by LM04;

LM06:
for seq be Real_Sequence, cseq be Complex_Sequence,
rlim be Real st seq=cseq & cseq is convergent & lim cseq = rlim
holds lim seq = rlim
proof
  let seq be Real_Sequence, cseq be Complex_Sequence,
  rlim be Real such that
  A1: seq = cseq and
  A2: cseq is convergent and
  A4: lim cseq = rlim;
  A5: seq is convergent by A1, A2, LM03;
  reconsider clim = rlim as Complex by XCMPLX_0:def 2;
  now let p be real number;
    A41: p is Real by XREAL_0:def 1;
    assume 0 < p;
    then consider n such that
    A6: for m st n<=m holds |.cseq.m - clim.| < p
    by A41,A4,A2,COMSEQ_2:def 5;
    take n;
    thus for m st n<=m holds |.seq.m - rlim.| < p by A1, A6;
  end;
  hence lim seq = rlim by A5,SEQ_2:def 7;
end;

LM08:
for seq be Real_Sequence, cseq be Complex_Sequence
st seq=cseq holds seq is convergent_to_0 iff cseq is convergent_to_0
proof
  let seq be Real_Sequence, cseq be Complex_Sequence such that
  A1: seq = cseq;
  thus seq is convergent_to_0 implies cseq is convergent_to_0
  proof
    assume seq is convergent_to_0; then
    seq is non-zero & seq is convergent & lim seq = 0 by FDIFF_1:def 1; then
    cseq is non-zero & cseq is convergent & lim cseq = 0 by A1, LM03, LM05;
    hence cseq is convergent_to_0 by CFDIFF_1:def 1;
  end;
  assume
  A3a: cseq is convergent_to_0; then
  A3: cseq is non-zero & cseq is convergent & lim cseq = 0 by CFDIFF_1:def 1;
  A4: seq is convergent by A1, LM03, A3a;
  lim seq = 0 by A1, A3, LM06;
  hence seq is convergent_to_0 by A1, A3a, A4, FDIFF_1:def 1;
end;

definition let f be PartFunc of COMPLEX,COMPLEX; ::: to be removed
  func Re f -> PartFunc of COMPLEX,REAL means :Def2:
  dom f = dom it &
  for z be Complex st z in dom it holds it.z = Re(f/.z);
  existence
  proof
    deffunc f(set) = Re(f/.$1);
    defpred P[set] means $1 in dom f;
    set X = dom f;
    consider F be PartFunc of COMPLEX,REAL such that
    A1: for z be Complex holds z in dom F iff P[z] and
    A2: for z be Complex st z in dom F holds F.z = f(z) from SEQ_1:sch 3;
    take F;
    thus dom F = dom f by A1,SUBSET_1:8;
    let z be Complex;assume z in dom F;
    hence thesis by A2;
  end;
  uniqueness
  proof
    deffunc f(set) = Re (f/.$1);
    thus for h,g being PartFunc of COMPLEX,REAL st
    (dom h=dom f & for z be Complex st z in dom h holds h.z = f(z)) &
    (dom g=dom f & for z be Complex st z in dom g holds g.z = f(z))
    holds h = g from SEQ_1:sch 4;
  end;
end;

definition let f be PartFunc of COMPLEX,COMPLEX; ::: to be removed
  func Im f -> PartFunc of COMPLEX,REAL means :Def3:
  dom f = dom it & for z be Complex st z in dom it holds it.z=Im (f/.z);
  existence
  proof
    deffunc f(set) = Im (f/.$1);
    defpred P[set] means $1 in dom f;
    set X = dom f;
    consider F be PartFunc of COMPLEX,REAL such that
    A1: for z be Complex holds z in dom F iff P[z] and
    A2: for z be Complex st z in dom F holds F.z = f(z) from SEQ_1:sch 3;
    take F;
    thus dom F = dom f by A1,SUBSET_1:8;
    let z be Complex;assume z in dom F;
    hence thesis by A2;
  end;
  uniqueness
  proof
    deffunc f(set) = Im (f/.$1);
    thus for h,g being PartFunc of COMPLEX,REAL st
    (dom h=dom f & for z be Complex st z in dom h holds h.z = f(z)) &
    (dom g=dom f & for z be Complex st z in dom g holds g.z = f(z))
    holds h = g from SEQ_1:sch 4;
  end;
end;

theorem LM09:
  for f be PartFunc of COMPLEX,COMPLEX st f is total
  holds dom (Re f) = COMPLEX & dom (Im f) = COMPLEX
  proof
    let f be PartFunc of COMPLEX,COMPLEX;
    assume f is total; then
    dom f = COMPLEX by PARTFUN1:def 4;
    hence thesis by Def2, Def3;
  end;

LM10:
for seq, cseq be Complex_Sequence
st cseq = <i> (#) seq holds seq is non-zero iff cseq is non-zero
proof
  let seq, cseq be Complex_Sequence such that
  A1: cseq = <i> (#) seq;
  A3: <i> (#) seq is non-zero implies seq is non-zero
  proof
    assume
    A4: <i> (#) seq is non-zero;
    now
      let n;
      (<i> (#) seq).n <> 0c by A4,COMSEQ_1:4; then
      <i> * seq.n <> 0c by VALUED_1:6;
      hence seq.n <> 0c;
    end;
    hence thesis by COMSEQ_1:4;
  end;
  thus seq is non-zero implies cseq is non-zero by COMSEQ_1:41, A1;
  assume cseq is non-zero;
  hence seq is non-zero by A1, A3;
end;

LM11:
for seq, cseq be Complex_Sequence st cseq = <i> (#) seq holds
seq is convergent_to_0 iff cseq is convergent_to_0
proof
  let seq, cseq be Complex_Sequence such that
  A1: cseq = <i> (#) seq;
  thus seq is convergent_to_0 implies cseq is convergent_to_0
  proof
    assume
    A02: seq is convergent_to_0; then
    A2: seq is non-zero & seq is convergent & lim seq = 0 by CFDIFF_1:def 1;
    A3: <i> (#) seq is non-zero & <i> (#) seq is convergent
    by A02, COMSEQ_1:41, COMSEQ_2:17;
    lim (<i> (#) seq) = <i> * 0 by A2, COMSEQ_2:18
    .= 0;
    hence cseq is convergent_to_0 by A1, A3, CFDIFF_1:def 1;
  end;
  assume
  A4: cseq is convergent_to_0;
  (-<i>) (#) (<i> (#) seq ) is convergent by A4, A1, COMSEQ_2:17; then
  ((-<i>) * <i> ) (#) seq is convergent by CFUNCT_1:31; then
  A5: seq is convergent by CFUNCT_1:35;
  A6: seq is non-zero by A1, A4, LM10;
  lim (<i> (#) seq) = 0 by A1, A4, CFDIFF_1:def 1; then
  <i> * lim seq = 0 by A5, COMSEQ_2:18;
  hence seq is convergent_to_0 by A5, A6, CFDIFF_1:def 1;
end;

:: Cauchy-Riemann
theorem Th01:
  for f be PartFunc of COMPLEX, COMPLEX,
  u,v be PartFunc of REAL 2, REAL,
  z0 be Complex,
  x0, y0 be Real,
  xy0 be Element of REAL 2 st
  (for x, y be Real st x+y*<i> in dom f holds
  <*x,y*> in dom u & u.<*x,y*> = (Re f).(x+y*<i>)) &
  (for x,y be Real st x+y*<i> in dom f holds
  <*x,y*> in dom v & v.<*x,y*> = (Im f).(x+y*<i>)) &
  z0 = x0+y0*<i> & xy0 = <*x0,y0*> & f is_differentiable_in z0 holds
  u is_partial_differentiable_in xy0,1 & u is_partial_differentiable_in xy0,2 &
  v is_partial_differentiable_in xy0,1 & v is_partial_differentiable_in xy0,2 &
  Re diff(f,z0) = partdiff(u,xy0,1) & Re diff(f,z0) = partdiff(v,xy0,2) &
  Im diff(f,z0) =-partdiff(u,xy0,2) & Im diff(f,z0) = partdiff(v,xy0,1)
  proof
    let f be PartFunc of COMPLEX,COMPLEX,
    u,v be PartFunc of REAL 2,REAL,
    z0 be Complex,
    x0,y0 be Real,xy0 be Element of REAL 2 such that
    A2: (for x,y be Real st x+y*<i> in dom f holds
    <*x,y*> in dom u & u.<*x,y*> = (Re f).(x+y*<i>)) and
    A3: (for x,y be Real st x+y*<i> in dom f holds
    <*x,y*> in dom v & v.<*x,y*> = (Im f).(x+y*<i>)) and
    A4: z0 = x0+y0*<i> & xy0 = <*x0,y0*> & f is_differentiable_in z0;
    consider N being Neighbourhood of z0 such that
P1: N c= dom f & ex L be C_LINEAR, R be C_REST st
    diff(f,z0) = L/.1r & for z be Complex
    st z in N holds f/.z-f/.z0 = L/.(z-z0)+R/.(z-z0) by A4,CFDIFF_1:def 7;
    consider L be C_LINEAR, R be C_REST such that
P2: diff(f,z0) = L/.1r & for z be Complex
    st z in N holds f/.z-f/.z0 = L/.(z-z0)+R/.(z-z0) by P1;
    P2R: dom R = COMPLEX by PARTFUN1:def 4;
    P3:
    for z be Complex st z in N holds f/.z-f/.z0 = diff(f,z0)*(z-z0)+R/.(z-z0)
    proof
      let z be Complex;
      assume
      AS: z in N;
      consider a0 be Complex such that
      ASS: for w be Complex holds L/.w = a0*w by CFDIFF_1:def 4;
      L/.(1r*(z-z0)) = a0*1r*(z-z0) by ASS
      .= (L/.1r)*(z-z0) by ASS;
      hence thesis by AS, P2;
    end;
P4: for x,y be Real st x+y*<i> in N holds
    f/.(x+y*<i>)-f/.(x0+y0*<i>)
    = diff(f,z0)*((x+y*<i>)-(x0+y0*<i>)) + R/.((x+y*<i>)-(x0+y0*<i>))
    by A4, P3;
    P5: for x, y be Real holds
    diff(f,z0)*((x+y*<i>)-(x0+y0*<i>))
    = ((Re diff(f,z0)) * (x-x0)-(Im diff(f,z0))*(y-y0))
    + ((Im diff(f,z0)) * (x-x0)+(Re diff(f,z0))*(y-y0))*<i>
    proof
      let x, y be Real; thus
      diff(f,z0)*((x+y*<i>)-(x0+y0*<i>))
      = ( Re ( diff(f,z0)) + (Im diff(f,z0)) * <i> )*((x-x0)+(y-y0)*<i>)
      by COMPLEX1:29
      .= ((Re diff(f,z0)) * (x-x0)-(Im diff(f,z0))*(y-y0))
      + ((Im diff(f,z0)) * (x-x0)+(Re diff(f,z0))*(y-y0))*<i>;
    end;
    P5r:
    for x,y be Real holds
    Re (diff(f,z0)*((x+y*<i>)-(x0+y0*<i>)))
    = (Re diff(f,z0)) * (x-x0)-(Im diff(f,z0))*(y-y0)
    proof
      let x, y be Real; thus
      Re (diff(f,z0)*((x+y*<i>)-(x0+y0*<i>)))
      = Re (((Re diff(f,z0)) * (x-x0)-(Im diff(f,z0))*(y-y0))
      + ((Im diff(f,z0)) * (x-x0)+(Re diff(f,z0))*(y-y0))*<i> ) by P5
      .= (Re diff(f,z0)) * (x-x0)-(Im diff(f,z0))*(y-y0) by COMPLEX1:28;
    end;
    P5i:
    for x,y be Real holds
    Im (diff(f,z0)*((x+y*<i>)-(x0+y0*<i>)))
    = (Im diff(f,z0)) * (x-x0)+(Re diff(f,z0))*(y-y0)
    proof
      let x, y be Real; thus
      Im (diff(f,z0)*((x+y*<i>)-(x0+y0*<i>)))
      = Im (((Re diff(f,z0)) * (x-x0)-(Im diff(f,z0))*(y-y0))
      + ((Im diff(f,z0)) * (x-x0)+(Re diff(f,z0))*(y-y0))*<i> ) by P5
      .= (Im diff(f,z0)) * (x-x0)+(Re diff(f,z0))*(y-y0) by COMPLEX1:28;
    end;
    consider r0 be Real such that
    P991: 0<r0 & {y where y is Complex: |.y-z0.| < r0} c= N by CFDIFF_1:def 5;
    set Nx0 = ].x0-r0,x0+r0.[;
    reconsider Nx0 as Neighbourhood of x0 by RCOMP_1:def 7,P991;
    P9x0: for x be Real st x in Nx0 holds x+y0*<i> in N
    proof
      let x be Real;
      assume x in Nx0; then
      K1: |.x-x0.| < r0 by RCOMP_1:8;
      K2: x+y0*<i> is Complex by XCMPLX_0:def 2;
      |. (x+y0*<i>) - z0 .| < r0 by A4, K1; then
      (x+y0*<i>) in {y where y is Complex : |.y-z0.| < r0} by K2;
      hence (x+y0*<i>) in N by P991;
    end;
    set Ny0 = ].y0-r0,y0+r0.[;
    reconsider Ny0 as Neighbourhood of y0 by RCOMP_1:def 7, P991;
    P9y0: for y be Real st y in Ny0 holds x0+y*<i> in N
    proof
      let y be Real;
      assume
      K1: y in Ny0;
      K2: x0+y*<i> is Complex by XCMPLX_0:def 2;
      |. (x0+y*<i>) - z0 .| = |.(y-y0)*<i>.| by A4; then
      |. (x0+y*<i>) - z0 .| = |.(y-y0).|*|.<i>.| by COMPLEX1:151; then
      |. (x0+y*<i>) - z0 .| < r0 by K1, RCOMP_1:8, COMPLEX1:135; then
      (x0+y*<i>) in {w where w is Complex : |.w-z0.| < r0} by K2;
      hence (x0+y*<i>) in N by P991;
    end;
    P10u: for x be Real st x in Nx0 holds
    u.<*x,y0*> - u.<*x0,y0*>
    = (Re diff(f,z0)) * (x-x0) + (Re R).((x-x0)+0 *<i>)
    proof
      let x be Real;
      assume
      P61: x in Nx0; then
      P611: x+y0*<i> in N by P9x0; then
      x+y0*<i> in dom f by P1; then
      P63: x+y0*<i> in dom Re f by Def2;
      P064: x0+y0*<i> in N by A4, CFDIFF_1:7; then
      x0+y0*<i> in dom f by P1; then
      P65: x0+y0*<i> in dom Re f by Def2;
      (x+y0*<i>)-(x0+y0*<i>) in dom R by P2R,XCMPLX_0:def 2; then
      P67: (x-x0) in dom Re R by Def2;
      u.<*x,y0*> - u.<*x0,y0*>
      = (Re f).(x+y0*<i>) - u.<*x0,y0*> by A2, P611, P1
      .=(Re f).(x+y0*<i>) - (Re f).(x0+y0*<i>) by A2, P064, P1
      .= Re (f/.(x+y0*<i>)) -(Re f).(x0+y0*<i>) by Def2,P63
      .= Re (f/.(x+y0*<i>)) -Re (f/.(x0+y0*<i>)) by Def2,P65
      .= Re (f/.(x+y0*<i>)-f/.(x0+y0*<i>)) by COMPLEX1:48
      .= Re ( diff(f,z0)*((x+y0*<i>)-(x0+y0*<i>))
      + R/.((x+y0*<i>)-(x0+y0*<i>))) by P4, P61, P9x0
      .= Re ( diff(f,z0)*((x+y0*<i>)-(x0+y0*<i>)))
      + Re (R/.((x+y0*<i>)-(x0+y0*<i>))) by COMPLEX1:19
      .= (Re diff(f,z0)) * (x-x0)-(Im diff(f,z0))*(y0-y0)
      + Re (R/.((x+y0*<i>)-(x0+y0*<i>))) by P5r
      .= (Re diff(f,z0)) * (x-x0) + (Re R).((x-x0)+0 *<i>) by Def2,P67;
      hence thesis;
    end;
    P11u: for y be Real st y in Ny0 holds
    u.<*x0,y*> - u.<*x0,y0*> =
    -(Im diff(f,z0))*(y-y0) + (Re R).((x0-x0)+ (y-y0)*<i>)
    proof
      let y be Real;
      assume
      P61: y in Ny0; then
      P611: x0+y*<i> in N by P9y0; then
      x0+y*<i> in dom f by P1; then
      P63: x0+y*<i> in dom Re f by Def2;
      P064: x0+y0*<i> in N by A4,CFDIFF_1:7; then
      x0+y0*<i> in dom f by P1; then
      P65: x0+y0*<i> in dom Re f by Def2;
      (x0+y*<i>)-(x0+y0*<i>) in dom R by P2R,XCMPLX_0:def 2; then
      P67: (y-y0)*<i> in dom Re R by Def2;
      u.<*x0,y*> - u.<*x0,y0*>
      = (Re f).(x0+y*<i>) - u.<*x0,y0*> by A2, P611, P1
      .=(Re f).(x0+y*<i>) - (Re f).(x0+y0*<i>) by A2, P064, P1
      .= Re (f/.(x0+y*<i>)) -(Re f).(x0+y0*<i>) by Def2,P63
      .= Re (f/.(x0+y*<i>)) -Re (f/.(x0+y0*<i>)) by Def2,P65
      .= Re (f/.(x0+y*<i>)-f/.(x0+y0*<i>)) by COMPLEX1:48
      .= Re ( diff(f,z0)*((x0+y*<i>)-(x0+y0*<i>))
      +R/.((x0+y*<i>)-(x0+y0*<i>))) by P4, P61, P9y0
      .= Re ( diff(f,z0)*((x0+y*<i>)-(x0+y0*<i>)))
      +Re (R/.((x0+y*<i>)-(x0+y0*<i>))) by COMPLEX1:19
      .= (Re diff(f,z0))*(x0-x0)-(Im diff(f,z0))*(y-y0)
      +Re (R/.((x0+y*<i>)-(x0+y0*<i>))) by P5r
      .= -(Im diff(f,z0))*(y-y0) + (Re R).((x0-x0)+ (y-y0)*<i>) by Def2,P67;
      hence thesis;
    end;

    P10v: for x be Real st x in Nx0 holds
    v.<*x,y0*> - v.<*x0,y0*>  =
    (Im diff(f,z0)) * (x-x0)+ (Im R).((x-x0) + 0 *<i>)
    proof
      let x be Real;
      assume
      P61: x in Nx0; then
      P62: x+y0*<i> in N by P9x0; then
      x+y0*<i> in dom f by P1; then
      P63: x+y0*<i> in dom Im f by Def3;
      P064: x0+y0*<i> in N by A4, CFDIFF_1:7; then
      x0+y0*<i> in dom f by P1; then
      P65: x0+y0*<i> in dom Im f by Def3;
      (x+y0*<i>)-(x0+y0*<i>) in dom R by P2R,XCMPLX_0:def 2; then
      P67: x-x0 in dom Im R by Def3;
      v.<*x,y0*> - v.<*x0,y0*> = (Im f).(x+y0*<i>) - v.<*x0,y0*> by A3,P1,P62
      .=(Im f).(x+y0*<i>) - (Im f).(x0+y0*<i>) by A3, P1, P064
      .= Im (f/.(x+y0*<i>)) -(Im f).(x0+y0*<i>) by Def3,P63
      .= Im (f/.(x+y0*<i>)) -Im (f/.(x0+y0*<i>)) by Def3,P65
      .= Im (f/.(x+y0*<i>)-f/.(x0+y0*<i>)) by COMPLEX1:48
      .= Im ( diff(f,z0)*((x+y0*<i>)-(x0+y0*<i>))
      +R/.((x+y0*<i>)-(x0+y0*<i>))) by P4, P61, P9x0
      .= Im ( diff(f,z0)*((x+y0*<i>)-(x0+y0*<i>)))
      + Im (R/.((x+y0*<i>)-(x0+y0*<i>))) by COMPLEX1:19
      .= (Im diff(f,z0)) * (x-x0)+(Re diff(f,z0))*(y0-y0)
      + Im (R/.((x+y0*<i>)-(x0+y0*<i>))) by P5i
      .= (Im diff(f,z0)) * (x-x0)+ (Im R).((x-x0) + 0 *<i>) by Def3,P67;
      hence thesis;
    end;
    P11v: for y be Real st y in Ny0 holds
    v.<*x0,y*> - v.<*x0,y0*> =
    (Re diff(f,z0))*(y-y0) + (Im R).((x0-x0)+(y-y0)*<i>)
    proof
      let y be Real;
      assume
      P61: y in Ny0; then
      P611: x0+y*<i> in N by P9y0; then
      x0+y*<i> in dom f by P1; then
      P63: x0+y*<i> in dom Im f by Def3;
      P064: x0+y0*<i> in N by A4,CFDIFF_1:7; then
      x0+y0*<i> in dom f by P1; then
      P65: x0+y0*<i> in dom Im f by Def3;
      (x0+y*<i>)-(x0+y0*<i>) in dom R by P2R,XCMPLX_0:def 2; then
      P67: (y-y0)*<i> in dom Im R by Def3;
      v.<*x0,y*> - v.<*x0,y0*>
      = (Im f).(x0+y*<i>) - v.<*x0,y0*> by A3, P1, P611
      .=(Im f).(x0+y*<i>) - (Im f).(x0+y0*<i>) by A3, P1, P064
      .= Im (f/.(x0+y*<i>)) -(Im f).(x0+y0*<i>) by Def3,P63
      .= Im (f/.(x0+y*<i>)) -Im (f/.(x0+y0*<i>)) by Def3,P65
      .= Im (f/.(x0+y*<i>)-f/.(x0+y0*<i>)) by COMPLEX1:48
      .= Im ( diff(f,z0)*((x0+y*<i>)-(x0+y0*<i>))
      +R/.((x0+y*<i>)-(x0+y0*<i>))) by P4, P61, P9y0
      .= Im ( diff(f,z0)*((x0+y*<i>)-(x0+y0*<i>)))
      + Im (R/.((x0+y*<i>)-(x0+y0*<i>))) by COMPLEX1:19
      .= (Im diff(f,z0)) * (x0-x0)+(Re diff(f,z0))*(y-y0)
      + Im (R/.((x0+y*<i>)-(x0+y0*<i>))) by P5i
      .= (Re diff(f,z0))*(y-y0)  + (Im R).((x0-x0)+(y-y0)*<i>) by Def3,P67;
      hence thesis;
    end;
    deffunc RF1(Real) = (Re R).$1;
    consider R1 be Function of REAL,REAL such that
    P500: for x be Real holds R1.x = RF1(x) from FUNCT_2:sch 4;
    deffunc RF2(Real) = (Re R).($1*<i>);
    consider R2 be Function of REAL,REAL such that
    P501: for y be Real holds R2.y =RF2(y) from FUNCT_2:sch 4;
    deffunc RF3(Real) = (Im R).$1;
    consider R3 be Function of REAL, REAL such that
    P502: for y be Real holds R3.y =RF3(y) from FUNCT_2:sch 4;
    deffunc RF4(Real) = (Im R).($1*<i>);
    consider R4 be Function  of REAL, REAL such that
    P503: for y be Real holds R4.y =RF4(y) from FUNCT_2:sch 4;
    Z2: for h be convergent_to_0 Real_Sequence holds
    (h")(#)(R1/*h) is convergent & lim ((h")(#)(R1/*h)) = 0
    proof
      let h be convergent_to_0 Real_Sequence;
      rng h c= COMPLEX by NUMBERS:11,XBOOLE_1:1; then
      reconsider hz = h as Complex_Sequence by FUNCT_2:8;
      reconsider hz as convergent_to_0 Complex_Sequence by LM08;
      now let n be Element of NAT;
        T7: dom R1=REAL by PARTFUN1:def 4; then
        T1: rng h c= dom R1;
        dom R1 c= dom Re R by LM09, T7, NUMBERS:11; then
        T2: h.n in dom Re R by TARSKI:def 3, T7;
        dom R= COMPLEX by PARTFUN1:def 4; then
        T3: rng hz c= dom R;
        T4: Im ((h.n)") = 0 & Re ((h.n)") = (h.n)"
        by COMPLEX1:def 2, COMPLEX1:def 3;
        thus ((h")(#)(R1/*h)).n = (h").n*((R1/*h).n) by SEQ_1:12
        .= (h.n)"*((R1/*h).n) by VALUED_1:10
        .= (h.n)"*(R1/.(h.n)) by T1, FUNCT_2:186
        .= (h.n)"* (Re R).(h.n) by P500
        .= Re ((h.n)")* Re (R/.(h.n)) - Im ((h.n)")* Im (R/.(h.n))
        by T2, Def2, T4
        .= Re ((h.n)"* (R/.(h.n))) by COMPLEX1:24
        .= Re ((hz").n * (R/.(hz.n))) by VALUED_1:10
        .= Re ((hz").n * (R/*hz).n) by T3, FUNCT_2:186
        .= Re (( (hz")(#)(R/*hz)  ).n ) by VALUED_1:5;
      end; then
      Z20: (h")(#)(R1/*h) = Re ((hz")(#)(R/*hz)) by COMSEQ_3:def 3;
      (hz")(#)(R/*hz) is convergent & lim ((hz")(#)(R/*hz)) = 0
      by CFDIFF_1:def 3;
      hence thesis by Z20, COMPLEX1:12, COMSEQ_3:41;
    end;
    reconsider R1 as REST by FDIFF_1:def 3, Z2;
    for h be convergent_to_0 Real_Sequence holds
    (h")(#)(R2/*h) is convergent & lim ((h")(#)(R2/*h)) = 0
    proof
      let h be convergent_to_0 Real_Sequence;
      rng h c= COMPLEX by NUMBERS:11,XBOOLE_1:1; then
      reconsider hz0 =h as Complex_Sequence by FUNCT_2:8;
      reconsider hz0 as convergent_to_0 Complex_Sequence by LM08;
      set hz=<i>(#)hz0;
      reconsider hz as convergent_to_0 Complex_Sequence by LM11;
Z201: now let n be Element of NAT;
        dom R2=REAL by PARTFUN1:def 4; then
        T1: rng h c= dom R2;
        dom Re R = COMPLEX by LM09; then
        T2: (h.n)*<i> in dom Re R by XCMPLX_0:def 2;
        dom R= COMPLEX by PARTFUN1:def 4; then
        T3: rng hz c= dom R;
        T4: Im ((h.n)" ) = 0 & Re ((h.n)") = (h.n)" by COMPLEX1:def 2,def 3;
        T8: hz.n=(h.n)*<i> by VALUED_1:6;
        thus ((h")(#)(R2/*h)).n = (h").n*((R2/*h).n) by SEQ_1:12
        .= (h.n)"*((R2/*h).n) by VALUED_1:10
        .= (h.n)"*(R2.(h.n)) by T1, FUNCT_2:185
        .= (h.n)"* (Re R).((h.n)*<i>) by P501
        .= Re ((h.n)" )* Re (R/.((h.n)*<i>))
        -Im ((h.n)") *Im (R/.((h.n)*<i>)) by T2, Def2, T4
        .= Re (((hz.n)/<i>) "* (R/.(hz.n))) by COMPLEX1:24, T8
        .= Re ((<i> /(hz.n)) * (R/.(hz.n))) by XCMPLX_1:215
        .= Re ( <i>*(hz").n * (R/.(hz.n))) by VALUED_1:10
        .= Re ( <i>*((hz").n * (R/.(hz.n))))
        .= Re ( <i> ) * Re ((hz").n * (R/.(hz.n)))
        - Im ( <i> ) * Im ((hz").n * (R/.(hz.n))) by COMPLEX1:24
        .= -Im ((hz").n * (R/*hz).n ) by COMPLEX1:17, T3, FUNCT_2:186
        .= -Im (( (hz")(#)(R/*hz)).n ) by VALUED_1:5
        .= - (Im ((hz")(#)(R/*hz))).n by COMSEQ_3:def 4;
      end;
      Z20: (h")(#)(R2/*h) = - Im ((hz")(#)(R/*hz)) by Z201, SEQ_1:14;
      X22: (hz")(#)(R/*hz) is convergent & lim ((hz")(#)(R/*hz)) = 0
      by CFDIFF_1:def 3; then
      Im ((hz")(#)(R/*hz)) is convergent & lim Im ((hz")(#)(R/*hz)) = Im 0
      by COMSEQ_3:41; then
      lim ((h")(#)(R2/*h)) = -Im 0 by Z20, SEQ_2:24
      .= 0 by COMPLEX1:12;
      hence thesis by X22, Z20, SEQ_2:23;
    end; then
    reconsider R2 as REST by FDIFF_1:def 3;
    Z6: for h be convergent_to_0 Real_Sequence holds
    (h")(#)(R3/*h) is convergent & lim ((h")(#)(R3/*h)) = 0
    proof
      let h be convergent_to_0 Real_Sequence;
      rng h c= COMPLEX by NUMBERS:11,XBOOLE_1:1; then
      reconsider hz =h as Complex_Sequence by FUNCT_2:8;
      reconsider hz as convergent_to_0 Complex_Sequence by LM08;
      now let n be Element of NAT;
        T7: dom R3=REAL by PARTFUN1:def 4; then
        T1: rng h c= dom R3;
        dom R3 c= dom Im R by T7, LM09, NUMBERS:11; then
        T2: h.n in dom Im R by TARSKI:def 3, T7;
        dom R = COMPLEX by PARTFUN1:def 4; then
        T3: rng hz c= dom R;
        T4: Im ((h.n)") = 0 & Re ((h.n)") = (h.n)" by COMPLEX1:def 2,def 3;
        thus ((h")(#)(R3/*h)).n
        = (h").n*((R3/*h).n) by SEQ_1:12
        .= (h.n)"*((R3/*h).n) by VALUED_1:10
        .= (h.n)"*(R3.(h.n)) by T1, FUNCT_2:185
        .= (h.n)"* (Im R).(h.n) by P502
        .= Re ((h.n)")*Im (R/.(h.n)) + Re (R/.(h.n))*Im ((h.n)")
        by T2, Def3, T4
        .= Im ((h.n)"* (R/.(h.n))) by COMPLEX1:24
        .= Im ((hz").n * (R/.(hz.n))) by VALUED_1:10
        .= Im ((hz").n * (R/*hz).n) by T3, FUNCT_2:186
        .= Im (((hz")(#)(R/*hz)).n) by VALUED_1:5;
      end; then
      Z20: (h")(#)(R3/*h) = Im ((hz")(#)(R/*hz)) by COMSEQ_3:def 4;
      (hz")(#)(R/*hz) is convergent & lim ((hz")(#)(R/*hz)) = 0
      by CFDIFF_1:def 3;
      hence thesis by Z20, COMSEQ_3:41, COMPLEX1:12;
    end;
    reconsider R3 as REST by FDIFF_1:def 3, Z6;
    Z8: for h be convergent_to_0 Real_Sequence
    holds (h")(#)(R4/*h) is convergent & lim ((h")(#)(R4/*h)) = 0
    proof
      let h be convergent_to_0 Real_Sequence;
      rng h c= COMPLEX by NUMBERS:11,XBOOLE_1:1; then
      reconsider hz0 =h as Complex_Sequence by FUNCT_2:8;
      reconsider hz0 as convergent_to_0 Complex_Sequence by LM08;
      set hz=<i>(#)hz0;
      reconsider hz as convergent_to_0 Complex_Sequence by LM11;
      now let n be Element of NAT;
        dom R4=REAL by PARTFUN1:def 4; then
        T1: rng h c= dom R4;
        (h.n)*<i> in COMPLEX by XCMPLX_0:def 2; then
        T21: (h.n)*<i> in dom Im R by LM09;
        dom R= COMPLEX by PARTFUN1:def 4; then
        T3: rng hz c= dom R;
        T4: Im ((h.n)" ) = 0 & Re ((h.n)") = (h.n)" by COMPLEX1:def 2,def 3;
        T6: hz.n = (h.n)*<i> by VALUED_1:6;
        thus ((h")(#)(R4/*h)).n = (h").n*((R4/*h).n) by SEQ_1:12
        .= (h.n)"*((R4/*h).n) by VALUED_1:10
        .= (h.n)"*(R4.(h.n)) by T1, FUNCT_2:185
        .= (h.n)"* (Im R).((h.n)*<i>) by P503
        .= Re ((h.n)" )* Im (R/.((h.n)*<i>))
        + Re (R/.((h.n)*<i>)) * Im ((h.n)") by Def3, T21, T4
        .= Im (((hz.n)/<i>) "* (R/.(hz.n))) by COMPLEX1:24, T6
        .= Im ((<i> /(hz.n)) * (R/.(hz.n))) by XCMPLX_1:215
        .= Im ( <i>*(hz").n * (R/.(hz.n))) by VALUED_1:10
        .= Im ( <i>*((hz").n * (R/.(hz.n))))
        .= Re ( <i> ) * Im ((hz").n * (R/.(hz.n)))
        + Re ((hz").n * (R/.(hz.n)))*Im ( <i> ) by COMPLEX1:24
        .= Re ((hz").n * (R/*hz).n ) by T3, FUNCT_2:186, COMPLEX1:17
        .= Re (( (hz")(#)(R/*hz)).n ) by VALUED_1:5;
      end; then
      Z20: (h")(#)(R4/*h) = Re ((hz")(#)(R/*hz)) by COMSEQ_3:def 3;
      (hz")(#)(R/*hz) is convergent & lim ((hz")(#)(R/*hz)) = 0
      by CFDIFF_1:def 3;
      hence thesis by Z20, COMSEQ_3:41, COMPLEX1:12;
    end;
    reconsider R4 as REST by FDIFF_1:def 3, Z8;
    P141: proj(1,2).xy0 = xy0.1 by PDIFF_1:def 1
    .= x0 by A4, FINSEQ_1:61;
    P142: proj(2,2).xy0 = xy0.2 by PDIFF_1:def 1
    .= y0 by A4, FINSEQ_1:61;
    P15u: for x be Real holds (u*reproj(1,xy0)).x=u.<*x,y0*>
    proof
      let x be Real;
      x in REAL; then
      x in dom (reproj(1,xy0)) by PDIFF_1:def 5;
      hence (u*reproj(1,xy0)).x = u.((reproj(1,xy0)).x) by FUNCT_1:23
      .= u.(Replace(xy0,1,x)) by PDIFF_1:def 5
      .= u.<*x,y0*> by A4,FINSEQ_7:15;
    end;
    P15v: for x be Real holds (v*reproj(1,xy0)).x=v.<*x,y0*>
    proof
      let x be Real;
      x in REAL; then
      P1: x in dom (reproj(1,xy0)) by PDIFF_1:def 5;
      thus (v*reproj(1,xy0)).x = v.((reproj(1,xy0)).x) by P1,FUNCT_1:23
      .= v.(Replace(xy0,1,x)) by PDIFF_1:def 5
      .= v.<*x,y0*> by A4,FINSEQ_7:15;
    end;
    P16u: for y be Real holds (u*reproj(2,xy0)).y=u.<*x0,y*>
    proof
      let y be Real;
      y in REAL; then
      P1: y in dom (reproj(2,xy0)) by PDIFF_1:def 5;
      thus (u*reproj(2,xy0)).y = u.((reproj(2,xy0)).y) by P1,FUNCT_1:23
      .= u.(Replace(xy0,2,y)) by PDIFF_1:def 5
      .= u.<*x0,y*> by A4, FINSEQ_7:16;
    end;
    P16v: for y be Real holds (v*reproj(2,xy0)).y = v.<*x0,y*>
    proof
      let y be Real;
      y in REAL; then
      P1: y in dom (reproj(2,xy0)) by PDIFF_1:def 5;
      thus (v*reproj(2,xy0)).y = v.((reproj(2,xy0)).y) by P1,FUNCT_1:23
      .=v.(Replace(xy0,2,y)) by PDIFF_1:def 5
      .=v.<*x0,y*> by A4, FINSEQ_7:16;
    end;
    deffunc LDF1(Real) = (Re diff(f,z0)) * $1;
    consider LD1 be Function  of REAL, REAL such that
    P17: for x be Real holds LD1.x = LDF1(x) from FUNCT_2:sch 4;
    deffunc LDF2(Real) = (Im diff(f,z0))* $1;
    consider LD2 be Function of REAL, REAL such that
    P18: for x be Real holds LD2.x = LDF2(x) from FUNCT_2:sch 4;
    deffunc LDF3(Real) = -(Im diff(f,z0)) * $1;
    consider LD3 be Function  of REAL,REAL such that
    P19: for x be Real holds LD3.x = LDF3(x) from FUNCT_2:sch 4;
    reconsider LD1 as LINEAR by FDIFF_1:def 4, P17;
    reconsider LD2 as LINEAR by FDIFF_1:def 4, P18;
    for x be Real holds LD3.x = (-(Im diff(f,z0)))* x
    proof
      let x be Real;
      thus LD3.x = -(Im diff(f,z0)) * x by P19
      .= (-(Im diff(f,z0)))* x;
    end; then
    reconsider LD3 as LINEAR by FDIFF_1:def 4;
    P30u: for x be Real st x in Nx0 holds
    (u*reproj(1,xy0)).x - (u*reproj(1,xy0)).x0 = LD1.(x-x0) + R1.(x-x0)
    proof
      let x be Real;
      assume
      AS30: x in Nx0;
      thus (u*reproj(1,xy0)).x - (u*reproj(1,xy0)).x0
      = u.<*x,y0*> - (u*reproj(1,xy0)).x0 by P15u
      .= u.<*x,y0*> - u.<*x0,y0*> by P15u
      .= (Re diff(f,z0)) * (x-x0) + (Re R).((x-x0)+0 *<i>) by AS30, P10u
      .= LD1.(x-x0) + (Re R).((x-x0)+0 *<i>) by P17
      .= LD1.(x-x0) + R1.(x-x0) by P500;
    end;
    P31u: for y be Real st y in Ny0 holds
    (u*reproj(2,xy0)).y - (u*reproj(2,xy0)).y0 = LD3.(y-y0) + R2.(y-y0)
    proof
      let y be Real;
      assume
      AS31: y in Ny0;
      thus (u*reproj(2,xy0)).y - (u*reproj(2,xy0)).y0
      = u.<*x0,y*> - (u*reproj(2,xy0)).y0 by P16u
      .= u.<*x0,y*> - u.<*x0,y0*> by P16u
      .= -(Im diff(f,z0))*(y-y0) + (Re R).((x0-x0)+(y-y0)*<i>) by AS31,P11u
      .= LD3.(y-y0) + (Re R).((x0-x0)+ (y-y0)*<i>) by P19
      .= LD3.(y-y0) + R2.(y-y0) by P501;
    end;
    P40v: for x be Real st x in Nx0 holds
    (v*reproj(1,xy0)).x - (v*reproj(1,xy0)).x0 = LD2.(x-x0)+ R3.(x-x0)
    proof
      let x be Real;
      assume
      AS40: x in Nx0;
      thus (v*reproj(1,xy0)).x - (v*reproj(1,xy0)).x0
      = v.<*x,y0*> - (v*reproj(1,xy0)).x0 by P15v
      .= v.<*x,y0*> - v.<*x0,y0*> by P15v
      .= (Im diff(f,z0))*(x-x0) + (Im R).((x-x0)+ 0 *<i>) by AS40,P10v
      .= LD2.(x-x0) + (Im R).((x-x0)+ 0 *<i>) by P18
      .= LD2.(x-x0) + R3.(x-x0) by P502;
     end;
     P41v: for y be Real st y in Ny0 holds
     (v*reproj(2,xy0)).y - (v*reproj(2,xy0)).y0 = LD1.(y-y0) + R4.(y-y0)
     proof
       let y be Real;
       assume
       AS41: y in Ny0;
       thus (v*reproj(2,xy0)).y - (v*reproj(2,xy0)).y0
       = v.<*x0,y*> - (v*reproj(2,xy0)).y0 by P16v
       .= v.<*x0,y*> - v.<*x0,y0*> by P16v
       .= (Re diff(f,z0))*(y-y0) + (Im R).((x0-x0)+(y-y0)*<i>) by AS41,P11v
       .= LD1.(y-y0) + (Im R).((x0-x0)+(y-y0) *<i>) by P17
       .= LD1.(y-y0) + R4.(y-y0) by P503;
     end;
     PL1: dom (reproj(1,xy0)) = REAL by FUNCT_2:def 1;
     now let s be set;
       assume s in (reproj(1,xy0)).:Nx0; then
       consider x being Element of REAL such that
       PL2: x in Nx0 & s = (reproj(1,xy0)).x by FUNCT_2:116;
       PL3: s=Replace(xy0,1,x) by PDIFF_1:def 5,PL2
       .= <* x, y0 *> by A4,FINSEQ_7:15;
       x+y0*<i> in N by P9x0,PL2;
       hence s in dom u by A2, P1, PL3;
     end; then
     (reproj(1,xy0)).:Nx0 c= dom u by TARSKI:def 3; then
     P61: Nx0 c= dom (u*reproj(1,xy0)) by PL1, FUNCT_3:3; then
     P71: u*reproj(1,xy0) is_differentiable_in proj(1,2).xy0
     by FDIFF_1:def 5, P30u, P141;
     Q51:LD3.1 = - (Im diff(f,z0))*1 by P19
     .= - (Im diff(f,z0));
     QL1: dom (reproj(2,xy0)) = REAL by FUNCT_2:def 1;
     now let s be set;
       assume s in (reproj(2,xy0)).:Ny0; then
       consider y being Element of REAL such that
       PL2: y in Ny0 & s = (reproj(2,xy0)).y by FUNCT_2:116;
       PL3: s=Replace(xy0,2,y) by PDIFF_1:def 5,PL2
       .= <* x0, y *> by A4,FINSEQ_7:16;
       x0+y*<i> in N by P9y0, PL2;
       hence s in dom u by A2, P1, PL3;
     end; then
     (reproj(2,xy0)).:Ny0 c= dom u by TARSKI:def 3; then
     Q61: Ny0 c= dom (u*reproj(2,xy0)) by QL1, FUNCT_3:3; then
     Q71: u*reproj(2,xy0) is_differentiable_in proj(2,2).xy0
     by FDIFF_1:def 5,P31u,P142; then
     Q74: partdiff(u,xy0,2) = -Im diff(f,z0)
     by FDIFF_1:def 6, Q51, Q61, P31u, P142;
     R51: LD2.1 = (Im diff(f,z0))*1 by P18
     .= Im diff(f,z0);
     PL1: dom (reproj(1,xy0)) = REAL by FUNCT_2:def 1;
     now let s be set;
       assume s in (reproj(1,xy0)).:Nx0; then
       consider x being Element of REAL such that
       PL2: x in Nx0 & s = (reproj(1,xy0)).x by FUNCT_2:116;
       PL3: s=Replace(xy0,1,x) by PDIFF_1:def 5, PL2
       .= <* x, y0 *> by A4, FINSEQ_7:15;
       x+y0*<i> in N by P9x0, PL2;
       hence s in dom v by A3, P1, PL3;
     end; then
     (reproj(1,xy0)).:Nx0 c= dom v by TARSKI:def 3; then
     R61: Nx0 c= dom (v*reproj(1,xy0)) by PL1, FUNCT_3:3; then
     R71: v*reproj(1,xy0) is_differentiable_in proj(1,2).xy0
     by FDIFF_1:def 5,P40v,P141;
     S51: LD1.1 = (Re diff(f,z0))*1 by P17
     .= Re diff(f,z0);
     QL1: dom (reproj(2,xy0)) = REAL by FUNCT_2:def 1;
     now let s be set;
       assume s in (reproj(2,xy0)).:Ny0; then
       consider y being Element of REAL such that
       PL2: y in Ny0 & s = (reproj(2,xy0)).y by FUNCT_2:116;
       PL3: s=Replace(xy0,2,y) by PDIFF_1:def 5,PL2
       .= <* x0, y *> by A4,FINSEQ_7:16;
       x0+y*<i> in N by P9y0, PL2;
       hence s in dom v by A3, P1, PL3;
     end; then
     (reproj(2,xy0)).:Ny0 c= dom v by TARSKI:def 3; then
     S61: Ny0 c= dom (v*reproj(2,xy0)) by QL1, FUNCT_3:3; then
     v*reproj(2,xy0) is_differentiable_in proj(2,2).xy0
     by FDIFF_1:def 5, P41v, P142;
     hence thesis by P71, Q71, Q74, R71, R51, R61, P40v, P141, P61, P30u,
     S51, S61, P41v, P142, FDIFF_1:def 6, PDIFF_1:def 11;
   end;

   Lm012: for x,y be Real,vx, vy be Point of REAL-NS 1 st vx=<*x*> & vy=<*y*>
   holds vx+vy=<*x+y*> & vx-vy = <*x-y*>
   proof
     let x, y be Real,vx, vy be Point of REAL-NS 1;
     assume
     A1: vx=<*x*> & vy=<*y*>;
     reconsider vx1= vx, vy1 =vy as Element of REAL 1 by REAL_NS1:def 4;
     thus vx+vy = vx1+vy1 by REAL_NS1:2
     .= <*x+y*> by A1, RVSUM_1:29;
     thus vx-vy = vx1-vy1 by REAL_NS1:5
     .= <*x-y*> by A1, RVSUM_1:50;
   end;
   Lm014: for x, y, z, w be Real, vx, vy be Element of REAL 2
   st vx = <*x,y*> & vy = <*z,w*> holds
   vx+vy = <*x+z,y+w*> & vx-vy = <*x-z,y-w*>
   proof
     let x, y, z, w be Real, vx, vy be Element of REAL 2;
     assume
     A1: vx = <*x,y*> & vy = <*z,w*>;
     reconsider x1=x,y1=y,z1=z,w1=w as Element of REAL;
     thus
     vx + vy = <* addreal.(x1,z1), addreal.(y1,w1) *> by A1, FINSEQ_2:89
     .= <*x1+z1,addreal.(y1,w1) *> by BINOP_2:def 9
     .= <*x+z,y+w*> by BINOP_2:def 9;
     thus
     vx - vy = <* diffreal.(x1,z1), diffreal.(y1,w1) *> by A1, FINSEQ_2:89
     .= <*x1-z1,diffreal.(y1,w1) *> by BINOP_2:def 10
     .= <*x-z,y-w*> by BINOP_2:def 10;
   end;
   Lm015: for x, y, a be Real, vx be Element of REAL 2 st vx=<*x,y*>
   holds a*vx=<* a*x,a*y *>
   proof
     let x, y, a be Real, vx be Element of REAL 2;
     assume
     A1: vx = <*x,y*>;
     reconsider x1=x,y1=y as Element of REAL;
     thus
     a*vx = <* (a multreal).x1, (a multreal).y1 *> by A1, FINSEQ_2:40
     .= <*a*x1,(a multreal).y1 *> by RVSUM_1:19
     .= <*a*x,a*y*> by RVSUM_1:19;
   end;
   Lm021: for x,y be Real holds <*x,y*> is Point of REAL-NS 2
   proof
     let x, y be Real;
     reconsider x1=x, y1=y as Element of REAL;
     <*x1,y1*> in REAL 2 by FINSEQ_2:121;
     hence thesis by REAL_NS1:def 4;
   end;
   Lm212: for x, y, z, w be Real, vx, vy be Point of REAL-NS 2
   st vx = <*x,y*> & vy = <*z,w*>
   holds vx+vy = <*x+z,y+w*> & vx-vy = <*x-z,y-w*>
   proof
     let x, y, z, w be Real, vx, vy be Point of REAL-NS 2;
     assume
     A1: vx = <*x,y*> & vy = <*z,w*>;
     reconsider vx1 = vx, vy1 = vy as Element of REAL 2 by REAL_NS1:def 4;
     thus vx+vy = vx1+vy1 by REAL_NS1:2
     .= <*x+z,y+w*> by A1,Lm014;
     thus vx-vy = vx1-vy1 by REAL_NS1:5
     .= <*x-z,y-w*> by A1,Lm014;
   end;
   Lm213: for x,y,a be Real,vx be Point of REAL-NS 2 st vx=<*x,y*>
   holds a*vx = <* a*x,a*y *>
   proof
     let x, y, a be Real, vx be Point of REAL-NS 2;
     assume
     A1: vx = <*x,y*>;
     reconsider vx1 = vx as Element of REAL 2 by REAL_NS1:def 4;
     thus a*vx = a*vx1 by REAL_NS1:3
     .= <*a*x, a*y *> by A1,Lm015;
   end;
   Lm022: for u be PartFunc of REAL 2, REAL, xy be Element of REAL 2
   st xy in dom u
   holds (<>*u).xy = <*u.xy*>
   proof
     let u be PartFunc of REAL 2, REAL, xy be Element of REAL 2;
     assume xy in dom u;
     hence
     (<>*u).xy = (proj(1,1) qua Function" ).(u.xy) by FUNCT_1:23
     .= <*u.xy*> by PDIFF_1:1;
   end;

   Lm023: for u be PartFunc of REAL 2, REAL, x, y be Real st <*x,y*> in dom u
   holds (<>*u)/.(<*x,y*>) =<*u/.(<*x,y*>)*>
   proof
     let u be PartFunc of REAL 2, REAL;
     let x, y be Real;
     assume
     A1: <*x,y*> in dom u;
     set W = proj(1,1)qua Function";
     rng u c= dom W by PDIFF_1:2; then
     dom <>*u = dom u by RELAT_1:46;
     hence
     (<>*u)/.(<*x,y*>) = (<>*u).(<*x,y*>) by A1, PARTFUN1:def 8
     .= (proj(1,1) qua Function" ).(u.(<*x,y*>)) by A1, FUNCT_1:23
     .= <*u.(<*x,y*>)*> by PDIFF_1:1
     .= <*u/.(<*x,y*>)*> by A1, PARTFUN1:def 8;
   end;
   Lm034: for x,y be Real,z be Complex,v be Element of REAL-NS 2
   st z=x+y*<i> & v=<* x,y *> holds |.z.|= ||.v.||
   proof
     let x, y be Real,z be Complex,v be Element of REAL-NS 2;
     assume
     A1: z=x+y*<i> & v = <* x,y *>;
     reconsider xx = <* x,y *> as Element of REAL 2 by FINSEQ_2:121;
     reconsider xx1 = xx as Point of TOP-REAL 2 by EUCLID:25;
     A3: xx1`1 = x by FINSEQ_1:61;
     A4: xx1`2 = y by FINSEQ_1:61;
     A5: len sqr xx = 2 by FINSEQ_1:def 18;
     A6: (sqr xx).1 = x^2 by A3, VALUED_1:11;
     (sqr xx).2 = y^2 by A4, VALUED_1:11; then
     A2: sqr xx = <*x^2,y^2*> by A5, A6, FINSEQ_1:61;
     A8: |.xx.| = ||.v.|| by A1, REAL_NS1:1;
     Re z = x & Im z = y by A1, COMPLEX1:28;
     hence thesis by A2, A8, RVSUM_1:107;
   end;

   Lm035: for x,y be Real,z be Complex st z=x+y*<i>
   holds |.z.| <= abs(x)+abs(y)
   proof
     let x, y be Real,z be Complex;
     assume z = x+y*<i>; then
     Re z = x & Im z = y by COMPLEX1:28;
     hence thesis by COMPLEX1:164;
   end;
   Lm038: for x, y be Real holds abs(x) <= |.x+y*<i>.| & abs(y) <= |.x+y*<i>.|
   proof
     let x, y be Real;
     set z = x+y*<i>;
     Re z = x & Im z = y by COMPLEX1:28;
     hence thesis by COMPLEX1:165;
  end;
  Lm036: for x,y be Real,v be Element of REAL-NS 2 st v=<* x,y *>
  holds ||.v.|| <= abs(x)+abs(y)
  proof
    let x, y be Real,v be Element of REAL-NS 2;
    assume
    A1: v = <* x,y *>;
    reconsider z = x+y*<i> as Complex by XCMPLX_0:def 2;
    |.z.|= ||.v.|| by A1, Lm034;
    hence thesis by Lm035;
  end;

theorem Lmseqr:
  for seq be Real_Sequence holds (seq is convergent & lim seq = 0)
  iff (abs seq is convergent & lim abs seq = 0)
  proof
    let seq be Real_Sequence;
    thus seq is convergent & lim seq = 0
    implies abs seq is convergent & lim abs seq = 0
    proof
      assume
      A1: seq is convergent & lim seq = 0; then
      lim abs seq = abs(0) by SEQ_4:27
      .= 0 by ABSVALUE:7;
      hence thesis by A1, SEQ_4:26;
    end;
    thus thesis by SEQ_4:28;
  end;

theorem Lmseqn:
  for X be RealNormSpace, seq be sequence of X
  holds (seq is convergent & lim seq = 0.X ) iff
  (||. seq .|| is convergent & lim ||. seq .|| = 0)
  proof
    let X be RealNormSpace, seq be sequence of X;
    thus seq is convergent & lim seq = 0.X
    implies ||. seq .|| is convergent & lim ||. seq .|| = 0
    proof
      assume seq is convergent & lim seq = 0.X; then
      ||. seq .|| is convergent & lim ||. seq .|| = ||.0.X.|| by LOPBAN_1:24;
      hence thesis by NORMSP_1:5;
    end;
      assume
      A1: ||. seq .|| is convergent & lim ||. seq .|| = 0;
      A2:
      now let p be Real;
        assume 0 < p; then
        consider m be Element of NAT such that
        A3: for n be Element of NAT st m <= n holds
        abs(||. seq .||.n - 0) < p by A1,SEQ_2:def 7;
        take m;
        hereby let n be Element of NAT;
          assume m <= n; then
          abs(||. seq .||.n - 0) < p by A3; then
          A4: abs ||. seq.n .|| < p by NORMSP_1:def 10;
          0 <= ||. seq.n .|| by NORMSP_1:8; then
          ||. seq.n .|| < p by A4,ABSVALUE:def 1;
          hence ||.seq.n-0.X.|| < p by RLVECT_1:26;
        end;
      end; then
      seq is convergent by NORMSP_1:def 9;
      hence thesis by A2,NORMSP_1:def 11;
  end;

Lm030: for x be Real, vx be Element of REAL-NS 2 st vx = <* x,0 *>
holds ||.vx.|| = abs x
proof
  let x be Real, vx be Element of REAL-NS 2;
  assume
  A1: vx = <* x,0 *>;
  reconsider xx = <* x,0 *> as Element of REAL 2 by FINSEQ_2:121;
  reconsider xx1 = xx as Point of TOP-REAL 2 by EUCLID:25;
  A3: xx1`1 = x by FINSEQ_1:61;
  A4: xx1`2 = 0 by FINSEQ_1:61;
  A5: len sqr xx = 2 by FINSEQ_1:def 18;
  A6: (sqr xx).1 = x^2 by A3, VALUED_1:11;
  (sqr xx).2 = 0^2 by A4, VALUED_1:11; then
  sqr xx = <*x^2,0^2*> by A5,A6,FINSEQ_1:61; then
  A2: sqrt Sum sqr xx = sqrt ( x^2+0^2 ) by RVSUM_1:107
  .= sqrt ( x^2+0 * 0) by SQUARE_1:def 3;
  ||.vx.|| = |.xx.| by A1, REAL_NS1:1;
  hence ||.vx.|| = abs x by A2, COMPLEX1:158;
end;

Lm031:
for x be Real,vx be Element of REAL-NS 2 st vx = <* 0,x*>
holds ||.vx.|| = abs x
proof
  let x be Real, vx be Element of REAL-NS 2;
  assume
  A1: vx = <* 0,x *>;
  reconsider xx = <* 0,x *> as Element of REAL 2 by FINSEQ_2:121;
  reconsider xx1 = xx as Point of TOP-REAL 2 by EUCLID:25;
  A3: xx1`1 = 0 by FINSEQ_1:61;
  A4: xx1`2 = x by FINSEQ_1:61;
  A5:len sqr xx = 2 by FINSEQ_1:def 18;
  A6:(sqr xx).1 = 0^2 by A3, VALUED_1:11;
  (sqr xx).2 = x^2 by A4, VALUED_1:11; then
  sqr xx = <*0^2,x^2*> by A5,A6,FINSEQ_1:61; then
  A2:  sqrt Sum sqr xx = sqrt ( 0^2+x^2 ) by RVSUM_1:107
  .= sqrt ( (0 * 0)+x^2 ) by SQUARE_1:def 3
  .= sqrt ( x^2 );
  ||.vx.|| = |.xx.| by A1, REAL_NS1:1;
  hence ||.vx.|| = abs x by A2, COMPLEX1:158;
end;

Lm032:
for x be Real,vx be Element of REAL-NS 1 st vx = <* x *>
holds ||.vx.|| = abs x
proof
  let x be Real, vx be Element of REAL-NS 1;
  assume
  A1: vx = <* x *>;
  reconsider xx = vx as Element of REAL 1 by REAL_NS1:def 4;
  sqrt Sum sqr xx = sqrt Sum <*x^2*> by A1, RVSUM_1:81; then
  A2: sqrt Sum sqr xx = sqrt (x^2) by RVSUM_1:103;
  ||.vx.|| = |.xx.| by REAL_NS1:1;
  hence ||.vx.|| = abs x by A2,COMPLEX1:158;
end;

Lm037: for v be Element of REAL-NS 1 holds abs( proj(1,1).v ) = ||.v.||
proof
  let v be Element of REAL-NS 1;
  reconsider x=proj(1,1).v as Real;
  reconsider w=v as Element of REAL 1 by REAL_NS1:def 4;
  len w = 1 & w.1 = x by PDIFF_1:def 1,FINSEQ_1:def 18; then
  <* x *> = w by FINSEQ_1:57;
  hence thesis by Lm032;
end;

theorem Th02:
  for u be PartFunc of REAL 2, REAL,
  x0, y0 be Real,
  xy0 be Element of REAL 2
  st xy0 = <*x0,y0*> & <>*u is_differentiable_in xy0
  holds
  u is_partial_differentiable_in xy0,1 &
  u is_partial_differentiable_in xy0,2 &
  <* partdiff(u,xy0,1) *> = diff(<>*u,xy0).(<* 1,0 *>) &
  <* partdiff(u,xy0,2) *> = diff(<>*u,xy0).(<* 0,1 *>)
  proof
    let u be PartFunc of REAL 2,REAL,
    x0, y0 be Real,
    xy0 be Element of REAL 2 such that
    A2: xy0 = <*x0,y0*> and
    A3: <>*u is_differentiable_in xy0;
    reconsider zxy0=xy0 as Point of REAL-NS 2 by A2,Lm021;
    reconsider ex0 = <* 1, 0 *> as Point of REAL-NS 2 by Lm021;
    reconsider ey0 = <* 0, 1 *> as Point of REAL-NS 2 by Lm021;
    consider g be PartFunc of REAL-NS 2,REAL-NS 1,
    gxy0 be Point of REAL-NS 2 such that
    X1: <>*u=g & xy0=gxy0 & g is_differentiable_in gxy0 by A3,PDIFF_1:def 7;
    consider f be PartFunc of REAL-NS 2,REAL-NS 1,
    fxy0 be Point of REAL-NS 2 such that
    X2: <>*u=f & xy0=fxy0 & diff(<>*u,xy0)=diff(f,fxy0) by A3,PDIFF_1:def 8;
    consider N being Neighbourhood of gxy0 such that
    X3: N c= dom g &
    ex R be REST of REAL-NS 2,REAL-NS 1
    st for xy be Point of REAL-NS 2 st xy in N
    holds g/.xy-g/.gxy0 = diff(g,gxy0).(xy-gxy0) + R/.(xy-gxy0)
    by NDIFF_1:def 7, X1;
    consider R be REST of REAL-NS 2,REAL-NS 1 such that
    X4: for xy be Point of REAL-NS 2 st xy in N
    holds g/.xy-g/.gxy0 = diff(g,gxy0).(xy-gxy0) + R/.(xy-gxy0) by X3;
    set W = proj(1,1)qua Function";
    rng u c= dom W by PDIFF_1:2; then
    V01: dom <>*u = dom u by RELAT_1:46;
    LmXY:
    for xy be Element of REAL 2, L1, R1 be Real, z be Element of REAL 2
    st xy in N & z=xy-xy0 & <*L1*>=diff(<>*u,xy0).z & <*R1*>=R.z
    holds u.xy-u.xy0 = L1+R1
    proof
      let xy be Element of REAL 2,
      L1, R1 be Real, z be Element of REAL 2;
      assume
      AS1: xy in N & z=xy-xy0 & <*L1*>=diff(<>*u,xy0).z & <*R1*>=R.z;
      set W = proj(1,1)qua Function";
      P11: xy0 in N by X1, NFCONT_1:4;
      reconsider zxy=xy as Point of REAL-NS 2 by REAL_NS1:def 4;
      reconsider dxy=z as Point of REAL-NS 2 by REAL_NS1:def 4;
      P1: g/.zxy-g/.gxy0 = diff(g,gxy0).(zxy-gxy0) + R/.(zxy-gxy0) by X4,AS1;
      P21: g/.zxy = g.xy by PARTFUN1:def 8, AS1, X3
      .=(<>*u)/.xy by PARTFUN1:def 8, AS1, X1, X3;
      P22: g/.gxy0 = g.xy0 by PARTFUN1:def 8, X1, X3, P11
      .=(<>*u)/.xy0 by PARTFUN1:def 8, P11, X1, X3;
      P23: (<>*u)/.xy = (<>*u).xy by AS1, X3, X1, PARTFUN1:def 8
      .= <*u.xy*> by Lm022, AS1, X3, X1, V01;
      P24: (<>*u)/.xy0 = (<>*u).xy0 by P11, X1, X3, PARTFUN1:def 8
      .= <*u.xy0*> by Lm022, P11, X1, X3, V01;
      P2: g/.zxy-g/.gxy0
      = <*u.xy*> -<*u.xy0*> by P21, P22, REAL_NS1:5, P23, P24
      .= <*u.xy*> +<*-u.xy0*> by RVSUM_1:38
      .= <* u.xy-u.xy0 *> by RVSUM_1:29;
      P9: zxy-gxy0 = z by AS1, X1, REAL_NS1:5;
      R is total by NDIFF_1:def 5; then
      dom R = the carrier of REAL-NS 2 by PARTFUN1:def 4; then
      R/.(zxy-gxy0) = <*R1*> by P9, PARTFUN1:def 8, AS1; then
      diff(g,gxy0).(zxy-gxy0) + R/.(zxy-gxy0) = <*L1+R1*>
      by X1, X2, AS1, P9, Lm012;
      hence u.xy - u.xy0 = (<*L1+R1*>).1 by FINSEQ_1:def 8, P1, P2
      .= L1+R1 by FINSEQ_1:def 8;
    end;
    consider r0 be Real such that
    P991: 0 < r0 &
    {xy where xy is Point of REAL-NS 2: ||.xy-gxy0.|| < r0} c= N
    by NFCONT_1:def 3;
    set Nx0 = ].x0-r0,x0+r0.[;
    reconsider Nx0 as Neighbourhood of x0 by P991, RCOMP_1:def 7;
    P9x0: for x be Real st x in Nx0 holds <*x,y0*> in N
    proof
      let x be Real;
      assume x in Nx0; then
      K1: |.x-x0.| < r0 by RCOMP_1:8;
      reconsider xy=<*x,y0*> as Point of REAL-NS 2 by Lm021;
      K2: xy-gxy0 =<*x-x0,y0-y0*> by X1, A2, Lm212
      .=<* x-x0,0 *>;
      reconsider xx=<* x-x0,0 *> as Element of REAL 2 by FINSEQ_2:121;
      reconsider xx1 =xx as Point of TOP-REAL 2 by EUCLID:25;
      K4: xx1`2 = 0 by FINSEQ_1:61;
      K5: abs(xx1`1) = |.x-x0.| by FINSEQ_1:61;
      K6: abs(xx1`2) = 0 by K4, ABSVALUE:7;
      K7: |. xx .| <= abs(xx1`1)+abs(xx1`2) by JGRAPH_1:48;
      ||. xy-gxy0 .|| = |.xx.| by K2, REAL_NS1:1; then
      ||. xy-gxy0 .|| < r0 by K1, K7, K5, K6, XXREAL_0:2; then
      xy in {z where z is Point of REAL-NS 2: ||.z-gxy0.|| < r0};
      hence thesis by P991;
    end;
    set Ny0 = ].y0-r0,y0+r0.[;
    reconsider Ny0 as Neighbourhood of y0 by P991, RCOMP_1:def 7;
    P9y0: for y be Real st y in Ny0 holds <*x0,y*> in N
    proof
      let y be Real;
      assume y in Ny0; then
      K1: |.y-y0.| < r0 by RCOMP_1:8;
      reconsider xy=<*x0,y*> as Point of REAL-NS 2 by Lm021;
      K2: xy-gxy0 =<*x0-x0,y-y0*> by X1,A2,Lm212
      .=<* 0,y-y0 *>;
      reconsider xx=<* 0,y-y0 *> as Element of REAL 2 by FINSEQ_2:121;
      reconsider xx1 = xx as Point of TOP-REAL 2 by EUCLID:25;
      K3: xx1`1 = 0 by FINSEQ_1:61;
      K5: abs(xx1`1) = 0 by K3, ABSVALUE:7;
      |. xx .| <= abs(xx1`1)+abs(xx1`2) by JGRAPH_1:48; then
      K7: |.xx.| <= |.y-y0.| by K5, FINSEQ_1:61;
      ||. xy-gxy0 .|| = |.xx.| by K2, REAL_NS1:1; then
      ||. xy-gxy0 .|| < r0 by K1, K7, XXREAL_0:2; then
      xy in {z where z is Point of REAL-NS 2: ||.z-gxy0.|| < r0};
      hence thesis by P991;
    end;
    defpred P1[Element of REAL,Element of REAL] means
    ex vx be Element of  REAL 2 st vx=<*$1,0 *> & <*$2*> = R.vx;
    S1:
    now let x be Element of REAL;
      R is total by NDIFF_1:def 5; then
      S10: dom R = the carrier of REAL-NS 2 by PARTFUN1:def 4;
      reconsider vx = <*x,0 *> as Element of REAL 2 by FINSEQ_2:121;
      vx is Element of REAL-NS 2 by REAL_NS1:def 4; then
      R.vx in the carrier of  REAL-NS 1 by S10, PARTFUN1:27; then
      R.vx is Element of REAL 1 by REAL_NS1:def 4; then
      consider y being Element of REAL such that
      S11: <*y*> = R.vx by FINSEQ_2:117;
      take y;
      thus P1[x,y] by S11;
    end;
    consider R1 be Function of REAL,REAL such that
    P501: for x being Element of REAL
    holds P1[x, R1.x] from FUNCT_2:sch 10(S1);
    P500:
    now let x be Real, vx be Element of  REAL 2;
      assume
      AS1: vx=<*x,0 *>;
      ex vx1 be Element of REAL 2 st vx1=<*x,0 *> & <*R1.x*> = R.vx1 by P501;
      hence <*R1.x*> = R.vx by AS1;
    end;
    defpred P2[Element of REAL,Element of REAL] means
    ex vy be Element of REAL 2 st vy=<* 0,$1 *> & <*$2*> = R.vy;
    S1:
    now let x be Element of REAL;
      R is total by NDIFF_1:def 5; then
      S10: dom R = the carrier of REAL-NS 2 by PARTFUN1:def 4;
      reconsider vx = <*0,x *> as Element of REAL 2 by FINSEQ_2:121;
      vx is Element of REAL-NS 2 by REAL_NS1:def 4; then
      R.vx in the carrier of REAL-NS 1 by S10, PARTFUN1:27; then
      R.vx is Element of REAL 1 by REAL_NS1:def 4; then
      consider y being Element of REAL such that
      S11: <*y*> = R.vx by FINSEQ_2:117;
      take y;
      thus P2[x,y] by S11;
    end;
    consider R3 be Function of REAL,REAL such that
    P501: for x being Element of REAL holds P2[x, R3.x]
    from FUNCT_2:sch 10(S1);
    P502:
    now let y be Real, vy be Element of REAL 2;
      assume
      AS1: vy=<* 0,y *>;
      ex vy1 be Element of REAL 2 st vy1=<* 0,y *> & <*R3.y*> = R.vy1 by P501;
      hence <*R3.y*> = R.vy by AS1;
    end;
    Z2: for h be convergent_to_0 Real_Sequence
    holds (h")(#)(R1/*h) is convergent & lim ((h")(#)(R1/*h)) = 0
    proof
      let h be convergent_to_0 Real_Sequence;
      defpred H1[Element of NAT,Element of REAL-NS 2] means
      $2 = <* h.$1,0 *>;
      S1:
      now let n be Element of NAT;
        <* h.n,0 *> in REAL 2 by FINSEQ_2:121; then
        reconsider y = <* h.n, 0 *> as Element of REAL-NS 2 by REAL_NS1:def 4;
        take y;
        thus H1[n,y];
      end;
      consider h1 be Function of NAT,REAL-NS 2 such that
      Z21: for n being Element of NAT holds H1[n, h1.n]
      from FUNCT_2:sch 10(S1);
      reconsider h1 as sequence of REAL-NS 2;
      now let n be Element of NAT;
        J1: h1.n = <*h.n,0 *> by Z21;
        set m = 2;
        h is non-zero by FDIFF_1:def 1; then
        J2: h.n <> 0 by SEQ_1:7;
        now assume
          h1.n = 0.(REAL-NS 2); then
          h1.n = 0*2 by REAL_NS1:def 4
             .= <* 0,0 *> by EUCLID:58,74;
          hence contradiction by J1, J2, FINSEQ_1:98;
        end;
        hence h1.n <> 0.(REAL-NS 2);
      end; then
      Z22: h1 is non-zero by NDIFF_1:7;
      now let n be Element of NAT;
        Z221: h1.n =<*h.n,0 *> by Z21;
        thus ||.h1.|| .n = ||. h1.n .|| by NORMSP_1:def 10
        .= abs (h.n) by Z221,Lm030
        .= (abs(h)).n by VALUED_1:18;
      end; then
      Z28:||.h1.|| = abs h by FUNCT_2:113;
      h is convergent & lim h = 0 by FDIFF_1:def 1; then
      abs h is convergent & lim abs h = 0 by Lmseqr; then
      h1 is convergent & lim h1 = 0.(REAL-NS 2) by Z28, Lmseqn; then
      h1 is convergent_to_0 by Z22, NDIFF_1:def 4; then
      Z23: (||.h1.||")(#)(R/*h1) is convergent &
      lim ((||.h1.||")(#)(R/*h1)) = 0.(REAL-NS 1) by NDIFF_1:def 5;
      set g = (||.h1.||")(#)(R/*h1);
      Z24: ||.g.|| is convergent & lim ||.g.|| = 0 by Z23,Lmseqn;
      now let n be Element of NAT;
        R is total by NDIFF_1:def 5; then
        T2: dom R = the carrier of REAL-NS 2 by PARTFUN1:def 4; then
        T3: rng h1 c= dom R;
        dom R1 = REAL by PARTFUN1:def 4; then
        T4: rng h c= dom R1;
        T5: h1.n= <* h.n, 0 *> by Z21;
        reconsider v = h1.n as Element of REAL 2 by REAL_NS1:def 4;
        T6: R/.(h1.n) = R.v by T2, PARTFUN1:def 8
        .= <* R1.(h.n) *> by P500, T5;
        Z221: h1.n =<*h.n,0 *> by Z21;
        thus ||.g.||.n = ||. g.n .|| by NORMSP_1:def 10
        .= ||. (||.h1.||").n *(R/*h1) .n .|| by NDIFF_1:def 2
        .= ||. ((||.h1.||).n)" *(R/*h1) .n .|| by VALUED_1:10
        .= ||. (||.h1.n.||)" *(R/*h1) .n .|| by NORMSP_1:def 10
        .= ||. (abs (h.n))" *(R/*h1) .n .|| by Z221,Lm030
        .= ||. (abs (h.n))" *(R/.(h1.n)) .|| by T3, FUNCT_2:186
        .= ||. abs((h.n)"  ) *(R/.(h1.n)) .|| by COMPLEX1:152
        .= abs(abs((h.n)" )) *||. (R/.(h1.n)) .|| by NORMSP_1:def 2
        .= abs((h.n)")*abs(R1.(h.n)) by T6, Lm032
        .= abs ((h.n)" * (R1.(h.n)) ) by COMPLEX1:151
        .= abs ((h.n)" * ((R1/*h).n)) by T4, FUNCT_2:185
        .= abs ((h".n) * (R1/*h).n) by VALUED_1:10
        .= abs ((h"(#)(R1/*h)) .n) by VALUED_1:5
        .= abs (h"(#)(R1/*h)) .n by VALUED_1:18;
      end; then
      abs ((h")(#)(R1/*h)) = ||.g.|| by FUNCT_2:113;
      hence
      (h")(#)(R1/*h) is convergent & lim ((h")(#)(R1/*h)) = 0 by Z24, Lmseqr;
    end;
    reconsider R1 as REST by FDIFF_1:def 3, Z2;
    Z6: for h be convergent_to_0 Real_Sequence holds
    (h")(#)(R3/*h) is convergent & lim ((h")(#)(R3/*h)) = 0
    proof
      let h be convergent_to_0 Real_Sequence;
      defpred H1[Element of NAT,Element of REAL-NS 2] means
      $2= <* 0,h.$1 *>;
      S1:
      now let n be Element of NAT;
        <* 0,h.n *> in REAL 2 by FINSEQ_2:121; then
        reconsider y = <* 0,h.n *> as Element of REAL-NS 2 by REAL_NS1:def 4;
        take y;
        thus H1[n,y];
      end;
      consider h1 be Function of NAT,REAL-NS 2 such that
      Z21: for n being Element of NAT holds H1[n, h1.n]
      from FUNCT_2:sch 10(S1);
      reconsider h1 as sequence of REAL-NS 2;
      now let n be Element of NAT;
        J1: h1.n = <* 0,h.n *> by Z21;
        set m=2;
        h is non-zero by FDIFF_1:def 1; then
        J2: h.n <> 0 by SEQ_1:7;
        now assume
          h1.n = 0.(REAL-NS 2); then
          h1.n = 0*2 by REAL_NS1:def 4
             .= <* 0,0 *> by EUCLID:58,74;
          hence contradiction by J1, FINSEQ_1:98, J2;
        end;
        hence h1.n <> 0.(REAL-NS 2);
      end; then
      Z22: h1 is non-zero by NDIFF_1:7;
      now let n be Element of NAT;
        Z221: h1.n = <* 0,h.n *> by Z21;
        thus ||.h1.||.n = ||. h1.n .|| by NORMSP_1:def 10
        .= abs (h.n) by Z221,Lm031
        .= (abs(h)).n by VALUED_1:18;
      end; then
      Z28: ||.h1.|| = abs h by FUNCT_2:113;
      h is convergent & lim h = 0 by FDIFF_1:def 1; then
      abs h is convergent & lim abs h = 0 by Lmseqr; then
      h1 is convergent & lim h1 = 0.(REAL-NS 2) by Z28, Lmseqn; then
      h1 is convergent_to_0 by Z22, NDIFF_1:def 4; then
      Z23: (||.h1.||")(#)(R/*h1) is convergent &
      lim ((||.h1.||")(#)(R/*h1)) = 0.(REAL-NS 1) by NDIFF_1:def 5;
      set g=(||.h1.||")(#)(R/*h1);
      Z24: ||.g.|| is convergent & lim ||.g.|| = 0 by Z23,Lmseqn;
      now let n be Element of NAT;
        R is total by NDIFF_1:def 5; then
        T03: dom R = the carrier of REAL-NS 2 by PARTFUN1:def 4; then
        T3: rng h1 c= dom R;
        dom R3 = REAL by PARTFUN1:def 4; then
        T4: rng h c= dom R3;
        T5: h1.n= <* 0, h.n *> by Z21;
        reconsider v = h1.n as Element of REAL 2 by REAL_NS1:def 4;
        T6: R/.(h1.n) = R.v by T03, PARTFUN1:def 8
        .= <* R3.(h.n) *> by P502,T5;
        Z221: h1.n =<* 0,h.n *> by Z21;
        thus ||.g.||.n = ||. g.n .|| by NORMSP_1:def 10
        .= ||. (||.h1.||").n *(R/*h1) .n .|| by NDIFF_1:def 2
        .= ||. ((||.h1.||).n)" *(R/*h1) .n .|| by VALUED_1:10
        .= ||. (||.h1.n.||)" *(R/*h1) .n .|| by NORMSP_1:def 10
        .= ||. (abs (h.n))" *(R/*h1) .n .|| by Z221,Lm031
        .= ||. (abs (h.n))" *(R/.(h1.n)) .|| by T3, FUNCT_2:186
        .= ||. abs((h.n)"  ) *(R/.(h1.n)) .|| by COMPLEX1:152
        .= abs(abs((h.n)" )) *||. (R/.(h1.n)) .|| by NORMSP_1:def 2
        .= abs((h.n)")*abs(R3.(h.n)) by T6, Lm032
        .= abs((h.n)" * (R3.(h.n))) by COMPLEX1:151
        .= abs((h.n)" * ((R3/*h).n)) by T4, FUNCT_2:185
        .= abs((h".n) * (R3/*h).n) by VALUED_1:10
        .= abs((h"(#)(R3/*h)).n) by VALUED_1:5
        .= abs(h"(#)(R3/*h)).n by VALUED_1:18;
      end; then
      abs ((h")(#)(R3/*h)) = ||.g.|| by FUNCT_2:113;
      hence
      (h")(#)(R3/*h) is convergent & lim ((h")(#)(R3/*h)) = 0 by Z24, Lmseqr;
    end;
    reconsider R3 as REST by FDIFF_1:def 3, Z6;
    P141: proj(1,2).xy0=xy0.1 by PDIFF_1:def 1
    .= x0 by A2,FINSEQ_1:61;
    P142: proj(2,2).xy0=xy0.2 by PDIFF_1:def 1
    .= y0 by A2,FINSEQ_1:61;
    P15u: for x be Real holds (u*reproj(1,xy0)).x=u.<*x,y0*>
    proof
      let x be Real;
      x in REAL; then
      P1: x in dom (reproj(1,xy0)) by PDIFF_1:def 5;
      thus (u*reproj(1,xy0)).x = u.((reproj(1,xy0)).x) by P1,FUNCT_1:23
      .=u.(Replace(xy0,1,x)) by PDIFF_1:def 5
      .=u.<*x,y0*> by A2,FINSEQ_7:15;
    end;
    P16u: for y be Real holds (u*reproj(2,xy0)).y=u.<*x0,y*>
    proof
      let y be Real;
      y in REAL; then
      y in dom (reproj(2,xy0)) by PDIFF_1:def 5;
      hence (u*reproj(2,xy0)).y = u.((reproj(2,xy0)).y) by FUNCT_1:23
      .=u.(Replace(xy0,2,y)) by PDIFF_1:def 5
      .=u.<*x0,y*> by A2,FINSEQ_7:16;
    end;
    ex0 is Element of REAL 2 by REAL_NS1:def 4; then
    diff(<>*u,xy0).ex0 is Element of REAL 1 by FUNCT_2:7; then
    consider Dux be Real such that
    P17a: <*Dux*> = diff(<>*u,xy0).ex0 by FINSEQ_2:117;
    deffunc LDF1(Real) = Dux* $1;
    consider LD1 be Function  of REAL,REAL such that
    P17: for x be Real holds LD1.x = LDF1(x) from FUNCT_2:sch 4;
    ey0 is Element of REAL 2 by REAL_NS1:def 4; then
    diff(<>*u,xy0).ey0 is Element of REAL 1 by FUNCT_2:7; then
    consider Duy be Real such that
    P18a: <*Duy*> = diff(<>*u,xy0).ey0 by FINSEQ_2:117;
    deffunc LDF3(Real) = Duy* $1;
    consider LD3 be Function of REAL, REAL such that
    P18: for x be Real holds LD3.x = LDF3(x) from FUNCT_2:sch 4;
    reconsider LD1 as LINEAR by FDIFF_1:def 4, P17;
    reconsider LD3 as LINEAR by FDIFF_1:def 4, P18;
    P30u: for x be Real st x in Nx0 holds
    (u*reproj(1,xy0)).x - (u*reproj(1,xy0)).x0 = LD1.(x-x0) + R1.(x-x0)
    proof
      let x be Real;
      assume
      AS30: x in Nx0;
      reconsider xy =<*x,y0*> as Element of REAL 2 by FINSEQ_2:121;
      ex0 is Element of REAL 2 by REAL_NS1:def 4; then
      reconsider D1=diff(<>*u,xy0).ex0 as Element of REAL 1 by FUNCT_2:7;
      L01: xy-xy0 = <* x-x0, y0-y0 *> by Lm014, A2
      .= <* (x-x0)*1, (x-x0)*0 *>
      .= (x-x0)* ex0 by Lm213;
      L02: diff(f,fxy0) is LinearOperator of REAL-NS 2,REAL-NS 1
      by LOPBAN_1:def 10;
      L0: (x-x0)*D1 = (x-x0)*(diff(f,fxy0).ex0) by X2, REAL_NS1:3
      .= (diff(<>*u,xy0)).(xy-xy0) by L01, L02, LOPBAN_1:def 6, X2;
      LD1. (x-x0)= Dux*(x-x0) by P17; then
      L1:<*LD1. (x-x0)*> = (diff(<>*u,xy0)).(xy-xy0) by P17a, RVSUM_1:69, L0;
      <*x-x0,0 *> = <*x-x0,y0-y0 *>
      .= xy-xy0 by Lm014,A2; then
      L2: <*R1.(x-x0) *> = R.(xy-xy0) by P500;
      thus (u*reproj(1,xy0)).x - (u*reproj(1,xy0)).x0
      = u.<*x,y0*> - (u*reproj(1,xy0)).x0 by P15u
      .= u.xy - u.xy0 by P15u, A2
      .= LD1.(x-x0) + R1.(x-x0) by L1, L2, AS30, P9x0, LmXY;
    end;
    P31u: for y be Real st y in Ny0 holds
    (u*reproj(2,xy0)).y - (u*reproj(2,xy0)).y0 = LD3.(y-y0) + R3.(y-y0)
    proof
      let y be Real;
      assume
      AS31: y in Ny0;
      reconsider xy =<*x0,y*> as Element of REAL 2 by FINSEQ_2:121;
      ey0 is Element of REAL 2 by REAL_NS1:def 4; then
      reconsider D1=diff(<>*u,xy0).ey0 as Element of REAL 1 by FUNCT_2:7;
      L01: xy-xy0 = <* x0-x0,y-y0 *> by Lm014,A2
      .= <* (y-y0)*0,(y-y0)*1 *>
      .= (y-y0)* ey0 by Lm213;
      L02: diff(f,fxy0) is LinearOperator of REAL-NS 2,REAL-NS 1
      by LOPBAN_1:def 10;
      L0: (y-y0)*D1 = (y-y0)*(diff(f,fxy0).ey0) by X2, REAL_NS1:3
      .= (diff(<>*u,xy0)).(xy-xy0) by L01, L02, LOPBAN_1:def 6, X2;
      LD3.(y-y0)= Duy*(y-y0) by P18; then
      L1: <*LD3.(y-y0)*> = (diff(<>*u,xy0)).(xy-xy0) by P18a, RVSUM_1:69, L0;
      <* 0, y-y0 *> = <* x0-x0, y-y0 *>
      .= xy - xy0 by Lm014,A2; then
      L2: <*R3.(y-y0) *> = R.(xy-xy0) by P502;
      thus (u*reproj(2,xy0)).y - (u*reproj(2,xy0)).y0
      = u.<*x0,y*> - (u*reproj(2,xy0)).y0 by P16u
      .= u.xy - u.xy0 by P16u, A2
      .= LD3. (y-y0) + R3.(y-y0) by L1, L2, AS31, P9y0, LmXY;
    end;
    P51: LD1.1 = Dux*1 by P17
    .= Dux;
    PL01: dom (reproj(1,xy0)) = REAL by FUNCT_2:def 1;
    now let s be set;
      assume s in (reproj(1,xy0)).:Nx0; then
      consider x being Element of REAL such that
      PL2: x in Nx0 & s = (reproj(1,xy0)).x by FUNCT_2:116;
      PL3: s=Replace(xy0,1,x) by PDIFF_1:def 5,PL2
      .= <* x, y0 *> by A2,FINSEQ_7:15;
      <* x,y0 *> in N by P9x0,PL2;
      hence s in dom u by X3, X1, V01, PL3;
    end; then
    (reproj(1,xy0)).:Nx0 c= dom u by TARSKI:def 3; then
    P61: Nx0 c= dom (u*reproj(1,xy0)) by PL01, FUNCT_3:3; then
    P71: u*reproj(1,xy0) is_differentiable_in proj(1,2).xy0
    by FDIFF_1:def 5, P30u, P141;
    Q51: LD3.1 = Duy *1 by P18
    .=Duy;
    PL01: dom (reproj(2,xy0)) = REAL by FUNCT_2:def 1;
    now let s be set;
      assume s in (reproj(2,xy0)).:Ny0; then
      consider y being Element of REAL such that
      PL2: y in Ny0 & s = (reproj(2,xy0)).y by FUNCT_2:116;
      PL3: s=Replace(xy0,2,y) by PDIFF_1:def 5,PL2
      .= <* x0, y *> by A2,FINSEQ_7:16;
      <* x0,y *> in N by P9y0,PL2;
      hence s in dom u by X3, X1, V01, PL3;
    end; then
    (reproj(2,xy0)).:Ny0 c= dom u by TARSKI:def 3; then
    Q61: Ny0 c= dom (u*reproj(2,xy0)) by PL01, FUNCT_3:3; then
    u*reproj(2,xy0) is_differentiable_in proj(2,2).xy0
    by FDIFF_1:def 5, P31u, P142;
    hence thesis by P71, P17a, FDIFF_1:def 6, P51, P61, P30u, P141,
    Q51, Q61, P31u, P142, P18a, PDIFF_1:def 11;
  end;

theorem
  for f be PartFunc of COMPLEX, COMPLEX,
  u,v be PartFunc of REAL 2, REAL,
  z0 be Complex,
  x0, y0 be Real,
  xy0 be Element of REAL 2 st
  (for x,y be Real st <*x,y*> in dom v holds x+y*<i> in dom f) &
  (for x,y be Real st x+y*<i> in dom f holds
  <*x,y*> in dom u & u.<*x,y*> = (Re f).(x+y*<i>)) &
  (for x,y be Real st x+y*<i> in dom f holds
  <*x,y*> in dom v & v.<*x,y*> = (Im f).(x+y*<i>)) &
  z0 = x0+y0*<i> & xy0 = <*x0,y0*> &
  <>*u is_differentiable_in xy0 &
  <>*v is_differentiable_in xy0 &
  partdiff(u,xy0,1) = partdiff(v,xy0,2) &
  partdiff(u,xy0,2) = -partdiff(v,xy0,1)
  holds
  f is_differentiable_in z0 &
  u is_partial_differentiable_in xy0,1 & u is_partial_differentiable_in xy0,2 &
  v is_partial_differentiable_in xy0,1 & v is_partial_differentiable_in xy0,2 &
  Re diff(f,z0) = partdiff(u,xy0,1) & Re diff(f,z0) = partdiff(v,xy0,2) &
  Im diff(f,z0) =-partdiff(u,xy0,2) & Im diff(f,z0) = partdiff(v,xy0,1)
  proof
    let f be PartFunc of COMPLEX, COMPLEX,
    u, v be PartFunc of REAL 2, REAL,
    z0 be Complex,
    x0, y0 be Real,
    xy0 be Element of REAL 2 such that
    AG2:for x,y be Real st <*x,y*> in dom v holds x+y*<i> in dom f and
    A2: (for x,y be Real st x+y*<i> in dom f holds
    <*x,y*> in dom u & u.<*x,y*> = (Re f).(x+y*<i>)) and
    A3: (for x,y be Real st x+y*<i> in dom f holds
    <*x,y*> in dom v & v.<*x,y*> = (Im f).(x+y*<i>)) and
    A4: z0 = x0+y0*<i> & xy0 = <*x0,y0*> and
    AG5: <>*u is_differentiable_in xy0 and
    AG6: <>*v is_differentiable_in xy0 and
    AG7: partdiff(u,xy0,1) = partdiff(v,xy0,2) and
    AG8: partdiff(u,xy0,2) = -partdiff(v,xy0,1);
    P1: u is_partial_differentiable_in xy0,1 &
    u is_partial_differentiable_in xy0,2 &
    <* partdiff(u,xy0,1) *> = diff(<>*u,xy0).(<* 1,0 *>) &
    <* partdiff(u,xy0,2) *> = diff(<>*u,xy0).(<* 0,1 *>) by Th02, A4, AG5;
    P2: v is_partial_differentiable_in xy0,1 &
    v is_partial_differentiable_in xy0,2 &
    <* partdiff(v,xy0,1) *> = diff(<>*v,xy0).(<* 1,0 *>) &
    <* partdiff(v,xy0,2) *> = diff(<>*v,xy0).(<* 0,1 *>) by Th02, A4, AG6;
    consider gu be PartFunc of REAL-NS 2,REAL-NS 1,
    guxy0 be Point of REAL-NS 2 such that
    X1: <>*u=gu & xy0=guxy0 & gu is_differentiable_in guxy0
    by AG5, PDIFF_1:def 7;
    consider hu be PartFunc of REAL-NS 2,REAL-NS 1,
    huxy0 be Point of REAL-NS 2 such that
    X2: <>*u=hu & xy0=huxy0 & diff(<>*u,xy0)=diff(hu,huxy0)
    by AG5, PDIFF_1:def 8;
    consider Nu being Neighbourhood of guxy0 such that
    X3: Nu c= dom gu &
    ex Ru be REST of REAL-NS 2,REAL-NS 1 st
    for xy be Point of REAL-NS 2 st xy in Nu
    holds gu/.xy-gu/.guxy0 = diff(gu,guxy0).(xy-guxy0) + Ru/.(xy-guxy0)
    by NDIFF_1:def 7, X1;
    consider Ru be REST of REAL-NS 2,REAL-NS 1 such that
    X4: for xy be Point of REAL-NS 2 st xy in Nu holds
    gu/.xy-gu/.guxy0 = diff(gu,guxy0).(xy-guxy0) + Ru/.(xy-guxy0) by X3;
    consider r1 be Real such that
    X5: 0 < r1 &
    {xy where xy is Point of REAL-NS 2: ||.xy-guxy0.|| < r1 } c= Nu
    by NFCONT_1:def 3;
    Z5: for x, y be Element of REAL st <*x,y*> in Nu holds
    u.<*x,y*> - u.<*x0,y0*>
    = partdiff(u,xy0,1)*(x-x0) + partdiff(u,xy0,2)*(y-y0)
    + proj(1,1).(Ru.<*x-x0,y-y0*>)
    proof
      let x, y be Element of REAL;
      assume
      Z510: <*x,y*> in Nu;
      <*x,y*> in REAL 2 by FINSEQ_2:121; then
      reconsider xy = <*x,y*> as Point of REAL-NS 2 by REAL_NS1:def 4;
      <* 1,0 *> in REAL 2 by FINSEQ_2:121; then
      reconsider e1 = <* 1,0 *> as Point of REAL-NS 2 by REAL_NS1:def 4;
      <* 0,1 *> in REAL 2 by FINSEQ_2:121; then
      reconsider e2 = <* 0,1 *> as Point of REAL-NS 2 by REAL_NS1:def 4;
      rng u c= dom (proj(1,1)qua Function") by PDIFF_1:2; then
      Z514: dom <>*u = dom u by RELAT_1:46;
      gu/.xy = gu.xy by X3, Z510, PARTFUN1:def 8
      .= (<>*u)/.<*x,y*> by X1, X3, Z510, PARTFUN1:def 8
      .= <* u/.<*x,y*> *> by X1, X3, Z510, Z514, Lm023
      .= <* u.<*x,y*> *> by X1, X3, Z510, Z514, PARTFUN1:def 8; then
      Z513: proj(1,1).(gu/.xy ) = u.<*x,y*> by PDIFF_1:1;
      ||.guxy0-guxy0.|| < r1 by NORMSP_1:10,X5; then
      guxy0 in {wxy where wxy is Point of REAL-NS 2: ||.wxy-guxy0.|| < r1 };
      then
      W0511: <*x0,y0 *> in Nu by X1, A4, X5;
      gu/.guxy0 = gu.guxy0 by A4, X1, X3, W0511, PARTFUN1:def 8
      .= (<>*u)/.<*x0,y0 *> by A4, X1, X3, W0511, PARTFUN1:def 8
      .= <* u/.<*x0,y0 *> *> by X1, X3, W0511, Z514, Lm023
      .= <* u.<*x0,y0 *> *> by W0511, X1, X3, Z514, PARTFUN1:def 8; then
      W513: proj(1,1).(gu/.guxy0) = u.<*x0,y0 *> by PDIFF_1:1;
      W600: u.<*x,y*>-u.<*x0,y0*>
      = proj(1,1).(gu/.xy-gu/.guxy0 ) by PDIFF_1:4, Z513, W513
      .= proj(1,1).(diff(gu,guxy0).(xy-guxy0) + Ru/.(xy-guxy0)) by X4, Z510
      .= proj(1,1).(diff(gu,guxy0).(xy-guxy0)) + proj(1,1).(Ru/.(xy-guxy0))
      by PDIFF_1:4;
      E0: xy-guxy0 = <*x-x0,y-y0*> by Lm212, X1, A4;
      E1: (x-x0)*e1 = <*(x-x0)*1,(x-x0)*0 *> by Lm213
      .= <*(x-x0),0 *>;
      E2: (y-y0)*e2 = <*(y-y0)*0,(y-y0)*1 *> by Lm213
      .= <* 0,(y-y0) *>;
      (x-x0)*e1 + (y-y0)*e2 = <*(x-x0)+0, 0+(y-y0) *> by Lm212, E1, E2
      .=<*(x-x0), (y-y0)*>; then
      E4: xy - guxy0 = (x-x0)*e1 + (y-y0)*e2 by Lm212, X1, A4;
      L0: diff(gu,guxy0) is LinearOperator of REAL-NS 2,REAL-NS 1
      by LOPBAN_1:def 10;
      W610: diff(gu,guxy0).(xy-guxy0)
      = diff(gu,guxy0).((x-x0)*e1) + diff(gu,guxy0).((y-y0)*e2)
      by E4, L0, LOPBAN_1:def 5
      .= (x-x0)* diff(gu,guxy0).e1 + diff(gu,guxy0).((y-y0)*e2)
      by L0, LOPBAN_1:def 6
      .= (x-x0)* diff(gu,guxy0).e1 + (y-y0)*diff(gu,guxy0).e2
      by L0, LOPBAN_1:def 6;
      Ru is total by NDIFF_1:def 5; then
      W0700: dom Ru = the carrier of REAL-NS 2 by PARTFUN1:def 4;
      thus u.<*x,y*>-u.<*x0,y0*>
      = proj(1,1).((x-x0)*(diff(gu,guxy0).e1))
      + proj(1,1).((y-y0)*(diff(gu,guxy0).e2)) + proj(1,1).(Ru/.(xy-guxy0))
      by W600, W610, PDIFF_1:4
      .= (x-x0)*proj(1,1).((diff(gu,guxy0).e1))
      + proj(1,1).((y-y0)*(diff(gu,guxy0).e2)) + proj(1,1).(Ru/.(xy-guxy0))
      by PDIFF_1:4
      .= (x-x0)*proj(1,1).(<* partdiff(u,xy0,1) *>)
      + (y-y0)*proj(1,1).(<* partdiff(u,xy0,2) *>)
      + proj(1,1).(Ru/.(xy-guxy0)) by PDIFF_1:4, X1, X2, P1
      .= (x-x0)* partdiff(u,xy0,1)
      + (y-y0)*proj(1,1).(<* partdiff(u,xy0,2) *>)
      + proj(1,1).(Ru/.(xy-guxy0)) by PDIFF_1:1
      .= (x-x0)*partdiff(u,xy0,1)
      + (y-y0)*partdiff(u,xy0,2) + proj(1,1).(Ru/.(xy-guxy0)) by PDIFF_1:1
      .= partdiff(u,xy0,1)*(x-x0)+partdiff(u,xy0,2)*(y-y0)
      + proj(1,1).(Ru.<*x-x0,y-y0*>) by W0700, PARTFUN1:def 8, E0;
    end;
    consider gv be PartFunc of REAL-NS 2,REAL-NS 1,
    gvxy0 be Point of REAL-NS 2 such that
    Y1: <>*v=gv & xy0=gvxy0 & gv is_differentiable_in gvxy0
    by AG6, PDIFF_1:def 7;
    consider hv be PartFunc of REAL-NS 2,REAL-NS 1,
    hvxy0 be Point of REAL-NS 2 such that
    Y2: <>*v=hv & xy0=hvxy0 & diff(<>*v,xy0) = diff(hv,hvxy0)
    by AG6, PDIFF_1:def 8;
    consider Nv being Neighbourhood of gvxy0 such that
    Y3: Nv c= dom gv &
    ex Rv be REST of REAL-NS 2,REAL-NS 1 st
    for xy be Point of REAL-NS 2 st xy in Nv
    holds gv/.xy-gv/.gvxy0 = diff(gv,gvxy0).(xy-gvxy0) + Rv/.(xy-gvxy0)
    by NDIFF_1:def 7, Y1;
    consider Rv be REST of REAL-NS 2,REAL-NS 1 such that
    Y4: for xy be Point of REAL-NS 2 st xy in Nv holds
    gv/.xy-gv/.gvxy0 = diff(gv,gvxy0).(xy-gvxy0) + Rv/.(xy-gvxy0) by Y3;
    consider r2 be Real such that
    Y5: 0 < r2 &
    {xy where xy is Point of REAL-NS 2: ||.xy-gvxy0.|| < r2 } c= Nv
    by NFCONT_1:def 3;
    W5: for x, y be Element of REAL st <*x,y*> in Nv holds
    v.<*x,y*>-v.<*x0,y0*>
    = partdiff(v,xy0,1)*(x-x0)
    + partdiff(v,xy0,2)*(y-y0) + proj(1,1).(Rv.<*x-x0,y-y0*>)
    proof
      let x, y be Element of REAL;
      assume
      Z510: <*x,y*> in Nv;
      <*x,y*> in REAL 2 by FINSEQ_2:121; then
      reconsider xy = <*x,y*> as Point of REAL-NS 2 by REAL_NS1:def 4;
      <* 1,0 *> in REAL 2 by FINSEQ_2:121; then
      reconsider e1 = <* 1,0 *> as Point of REAL-NS 2 by REAL_NS1:def 4;
      <* 0,1 *> in REAL 2 by FINSEQ_2:121; then
      reconsider e2 = <* 0,1 *> as Point of REAL-NS 2 by REAL_NS1:def 4;
      rng v c= dom (proj(1,1)qua Function") by PDIFF_1:2; then
      Z514: dom <>*v = dom v by RELAT_1:46;
      gv/.xy = gv.xy by Y3, Z510, PARTFUN1:def 8
      .=(<>*v)/.<*x,y*> by Y1, Y3, Z510, PARTFUN1:def 8
      .=<* v/.<*x,y*> *> by Z510, Y3, Y1, Z514, Lm023
      .=<* v.<*x,y*> *> by Z510, Y3, Y1, Z514, PARTFUN1:def 8; then
      Z513: proj(1,1).(gv/.xy ) = v.<*x,y*> by PDIFF_1:1;
      ||.gvxy0-gvxy0.|| < r2 by NORMSP_1:10,Y5; then
      gvxy0 in {wxy where wxy is Point of REAL-NS 2 : ||.wxy-gvxy0.|| < r2 };
      then
      W0511: <*x0,y0 *> in Nv by Y1, A4, Y5;
      gv/.gvxy0 = gv.gvxy0 by A4, Y1, Y3, W0511,PARTFUN1:def 8
      .= (<>*v)/.<*x0,y0 *> by A4, Y1, Y3, W0511,PARTFUN1:def 8
      .= <* v/.<*x0,y0 *> *> by Y1, Y3, W0511, Z514, Lm023
      .= <* v.<*x0,y0 *> *> by Y1, Y3, W0511, Z514, PARTFUN1:def 8; then
      W513: proj(1,1).(gv/.gvxy0) = v.<*x0,y0 *> by PDIFF_1:1;
      W600: v.<*x,y*>-v.<*x0,y0*>
      = proj(1,1).(gv/.xy-gv/.gvxy0 ) by PDIFF_1:4, Z513, W513
      .= proj(1,1).(diff(gv,gvxy0).(xy-gvxy0) + Rv/.(xy-gvxy0)) by Y4, Z510
      .= proj(1,1).(diff(gv,gvxy0).(xy-gvxy0)) + proj(1,1).(Rv/.(xy-gvxy0))
      by PDIFF_1:4;
      E0: xy-gvxy0 = <*x-x0,y-y0*> by Lm212, Y1, A4;
      E1: (x-x0)*e1 = <*(x-x0)*1,(x-x0)*0 *> by Lm213
      .= <*(x-x0),0 *>;
      E2: (y-y0)*e2 = <*(y-y0)*0,(y-y0)*1 *> by Lm213
      .= <* 0,(y-y0) *>;
      E3:
      (x-x0)*e1 + (y-y0)*e2 = <* (x-x0)+0, 0+(y-y0) *> by Lm212, E1, E2
      .= <*(x-x0), (y-y0)*>;
      L0: diff(gv,gvxy0) is LinearOperator of REAL-NS 2,REAL-NS 1
      by LOPBAN_1:def 10;
      W610: diff(gv,gvxy0).(xy-gvxy0)
      = diff(gv,gvxy0).((x-x0)*e1) + diff(gv,gvxy0).((y-y0)*e2)
      by E0, E3, L0, LOPBAN_1:def 5
      .= (x-x0)* diff(gv,gvxy0).e1 + diff(gv,gvxy0).((y-y0)*e2)
      by L0,LOPBAN_1:def 6
      .= (x-x0)* diff(gv,gvxy0).e1 + (y-y0)*diff(gv,gvxy0).e2
      by L0,LOPBAN_1:def 6;
      Rv is total by NDIFF_1:def 5; then
      W0700: dom Rv = the carrier of REAL-NS 2 by PARTFUN1:def 4;
      thus v.<*x,y*>-v.<*x0,y0*>
      = proj(1,1).((x-x0)* (diff(gv,gvxy0).e1))
      + proj(1,1).((y-y0)* (diff(gv,gvxy0).e2))
      + proj(1,1).(Rv/.(xy-gvxy0)) by W600, W610, PDIFF_1:4
      .= (x-x0)*proj(1,1).((diff(gv,gvxy0).e1))
      + proj(1,1).((y-y0)* (diff(gv,gvxy0).e2))
      + proj(1,1).(Rv/.(xy-gvxy0)) by PDIFF_1:4
      .= (x-x0)*proj(1,1).((diff(gv,gvxy0).e1))
      + (y-y0)*proj(1,1).((diff(gv,gvxy0).e2))
      + proj(1,1).(Rv/.(xy-gvxy0)) by PDIFF_1:4
      .= (x-x0)* partdiff(v,xy0,1)
      + (y-y0)*proj(1,1). (<* partdiff(v,xy0,2) *> )
      + proj(1,1).(Rv/.(xy-gvxy0)) by Y1, Y2, P2, PDIFF_1:1
      .= (x-x0)* partdiff(v,xy0,1) + (y-y0)* partdiff(v,xy0,2)
      + proj(1,1).(Rv/.(xy-gvxy0)) by PDIFF_1:1
      .= partdiff(v,xy0,1)*(x-x0) + partdiff(v,xy0,2)*(y-y0)
      + proj(1,1).(Rv.<*x-x0,y-y0*>) by W0700, PARTFUN1:def 8, E0;
    end;
    set r0 = min(r1/2,r2/2);
    set N = {y where y is Complex : |.y-z0.| < r0};
    LmZ0:
    for x,y be Element of REAL st x+y*<i> in N holds
    abs(x-x0)<r1/2 & abs(y-y0)<r1/2 & abs(x-x0)<r2/2 & abs(y-y0)<r2/2
    proof
      let x, y be Element of REAL;
      assume
      Z1: x+y*<i> in N;
      Q0: r0 <=r1/2 & r0 <=r2/2 by XXREAL_0:17;
      Q01: ex w be Complex st w=x+y*<i> & |.w-z0.| < r0 by Z1;
      abs(x-x0) <= |. (x-x0) + (y-y0)*<i> .| by Lm038; then
      Z01:abs(x-x0) < r0 by Q01, A4, XXREAL_0:2;
      abs(y-y0) <= |. (x-x0) + (y-y0)*<i> .| by Lm038; then
      Z02: abs(y-y0) < r0 by Q01, A4, XXREAL_0:2;
      <*x,y*> in REAL 2 by FINSEQ_2:121; then
      reconsider xy=<*x,y*> as Point of REAL-NS 2 by REAL_NS1:def 4;
      thus abs(x-x0) < r1/2 by Q0,XXREAL_0:2, Z01;
      thus abs(y-y0) < r1/2 by Q0,XXREAL_0:2, Z02;
      thus abs(x-x0) < r2/2 by Q0,XXREAL_0:2, Z01;
      thus abs(y-y0) < r2/2 by Q0,XXREAL_0:2, Z02;
    end;
    Z0:
    now let x, y be Element of REAL;
      assume
      Z1: x+y*<i> in N;
      rng v c= dom (proj(1,1) qua Function") by PDIFF_1:2; then
      ZZZ: dom <>*v = dom v by RELAT_1:46;
      Q0: r0 <= r1/2 & r0 <= r2/2 by XXREAL_0:17;
      Q01: ex w be Complex st w=x+y*<i> & |.w-z0.| < r0 by Z1;
      abs(x-x0)<=|. (x-x0) + (y-y0)*<i> .| by Lm038; then
      Z01:abs(x-x0) < r0 by Q01, A4, XXREAL_0:2;
      abs(y-y0)<=|. (x-x0) + (y-y0)*<i>  .| by Lm038; then
      Z02:abs(y-y0) < r0 by Q01, A4, XXREAL_0:2;
      <*x,y*> in REAL 2 by FINSEQ_2:121; then
      reconsider xy=<*x,y*> as Point of REAL-NS 2 by REAL_NS1:def 4;
      ZX1: xy-guxy0 = <* x-x0,y-y0 *> by X1,A4,Lm212;
      Z03: abs(x-x0) < r1/2 by Q0,XXREAL_0:2,Z01;
      Z04: abs(y-y0) < r1/2 by Q0,XXREAL_0:2,Z02;
      Z41: ||.xy-guxy0.|| <=abs(x-x0) + abs(y-y0) by ZX1,Lm036;
      abs(x-x0) + abs(y-y0) < r1/2+r1/2 by Z03,Z04, XREAL_1:10; then
      ||.xy-guxy0.|| < r1 by Z41,XXREAL_0:2; then
      Z0031:
      xy in {wxy where wxy is Point of REAL-NS 2 : ||.wxy-guxy0.|| < r1 };
      ZY1: xy-gvxy0 = <* x-x0,y-y0 *> by Y1,A4,Lm212;
      Z05:abs(x-x0) < r2/2 by Q0,XXREAL_0:2,Z01;
      Z06:abs(y-y0) < r2/2 by Q0,XXREAL_0:2,Z02;
      Z61:||.xy-gvxy0.|| <=abs(x-x0) + abs(y-y0) by ZY1,Lm036;
      abs(x-x0) + abs(y-y0) < r2/2+r2/2 by Z05,Z06, XREAL_1:10; then
      ||.xy-gvxy0.|| < r2 by Z61,XXREAL_0:2; then
      Z0041:
      xy in {wxy where wxy is Point of REAL-NS 2 : ||.wxy-gvxy0.|| < r2 };
      then <*x,y *> in Nv by Y5; then
      Z02: x+y*<i> in dom f by ZZZ, Y1, Y3, AG2; then
      Z02R: x+y*<i> in dom (Re f) by Def2;
      Z02I: x+y*<i> in dom (Im f ) by Def3, Z02;
      ||.gvxy0-gvxy0.|| < r2 by NORMSP_1:10, Y5; then
      gvxy0 in {wxy where wxy is Point of REAL-NS 2 : ||.wxy-gvxy0.|| < r2 };
      then <* x0, y0 *> in Nv by A4, Y1, Y5; then
      Z021: x0+y0*<i> in dom f by ZZZ, Y1, Y3, AG2; then
      Z021R: x0+y0*<i> in dom (Re f) by Def2;
      Z021I:  x0+y0*<i> in dom (Im f) by Z021,Def3;
      Z05: f/.(x+y*<i>) = Re (f/.(x+y*<i>)) + Im(f/.(x+y*<i>))*<i>
      by COMPLEX1:29
      .= (Re f).(x+y*<i>) + Im(f/.(x+y*<i>))*<i> by Z02R, Def2
      .=(Re f).(x+y*<i>) + (Im f).(x+y*<i>)*<i> by Z02I, Def3;
      Z06: f/.(x0+y0*<i>) = Re (f/.(x0+y0*<i>)) + Im(f/.(x0+y0*<i>))*<i>
      by COMPLEX1:29
      .= (Re f).(x0+y0*<i>) +  Im(f/.(x0+y0*<i>))*<i> by Z021R,Def2
      .= (Re f).(x0+y0*<i>) + (Im f).(x0+y0*<i>)*<i> by Z021I,Def3;
      Z07: f/.(x+y*<i>) - f/.(x0+y0*<i>)
      = (u.<*x,y*> + ((Im f).(x+y*<i>))*<i>)
      -((Re f).(x0+y0*<i>) + ((Im f).(x0+y0*<i>))*<i>) by Z05, Z06, A2, Z02
      .=(u.<*x,y*> + ( v.<*x,y*> ) * <i> )
      -((Re f).(x0+y0*<i>) + ((Im f).(x0+y0*<i>)) * <i> ) by A3, Z02
      .=(u.<*x,y*> + ( v.<*x,y*> )*<i>)
      -(u.<*x0,y0 *> + ((Im f).(x0+y0*<i>)) * <i> ) by A2,Z021
      .= (u.<*x,y*> + ( v.<*x,y*> ) * <i> )
      -(u.<*x0,y0 *> + ( v.<*x0,y0*> ) * <i> ) by A3, Z021
      .= (u.<*x,y*> - u.<*x0,y0*>) + (v.<*x,y*>- v.<*x0,y0*>) *<i>;
      Z08: u.<*x,y*> - u.<*x0,y0*>
      = partdiff(u,xy0,1)*(x-x0)+partdiff(u,xy0,2)*(y-y0)
      + proj(1,1).(Ru.<*x-x0,y-y0*>) by Z5, Z0031, X5
      .= partdiff(u,xy0,1)*(x-x0)+<i>*partdiff(v,xy0,1)*(y-y0)*<i>
      + proj(1,1).(Ru.<*x-x0,y-y0*>) by AG8;
      Z09: (v.<*x,y*>-v.<*x0,y0*>)*<i>
      = (partdiff(v,xy0,1)*(x-x0)+partdiff(u,xy0,1)*(y-y0)
      + proj(1,1).(Rv.<*x-x0,y-y0*>))*<i> by W5, Z0041, Y5, AG7;
      thus f/.(x+y*<i>) - f/.(x0+y0*<i>)
      =(partdiff(u,xy0,1)+<i>*partdiff(v,xy0,1)) *((x-x0)+(y-y0)*<i>)
      + (proj(1,1).(Ru.<*x-x0,y-y0*>)
      + (proj(1,1).(Rv.<*x-x0,y-y0*>))*<i>) by Z07, Z08, Z09;
    end;
    reconsider a = (partdiff(u,xy0,1)+<i>*partdiff(v,xy0,1)) as Complex
    by XCMPLX_0:def 2;
    deffunc L0(Element of COMPLEX) = a* $1;
    consider L be Function  of COMPLEX,COMPLEX such that
    Z20: for x be Element of COMPLEX holds L.x= L0(x) from FUNCT_2:sch 4;
    L/.z = a*z by Z20; then
    reconsider L as C_LINEAR by CFDIFF_1:def 4;
    defpred R0[Element of COMPLEX, Element of COMPLEX] means
    $2 = proj(1,1).(Ru.<* Re $1, Im $1 *>) +
    (proj(1,1).(Rv.<* Re $1, Im $1 *>))*<i>;
    S1:
    now let z be Element of COMPLEX;
      reconsider y = proj(1,1).(Ru.<* Re z, Im z *>)
      + (proj(1,1).(Rv.<* Re z, Im z *>))*<i>
      as Element of COMPLEX by XCMPLX_0:def 2;
      take y;
      thus R0[z,y];
    end;
    consider R be Function of COMPLEX,COMPLEX such that
    Z30: for z be Element of COMPLEX holds R0[z, R.z]
    from FUNCT_2:sch 10(S1);
    for h be convergent_to_0 Complex_Sequence holds
    (h")(#)(R/*h) is convergent & lim ((h")(#)(R/*h)) = 0
    proof
      let h be convergent_to_0 Complex_Sequence;
      V1:
      now let r be Real;
        assume
        AZ01: 0 < r;
        Ru is total by NDIFF_1:def 5; then
        consider d1 be Real such that
        AZ2: d1 > 0 &
        for z be Point of REAL-NS 2 st z <> 0.(REAL-NS 2) &
        ||.z.|| < d1 holds
        ||.z.||" * ||. Ru/.z .|| < r/2 by NDIFF_1:26, AZ01;
        Rv is total by NDIFF_1:def 5; then
        consider d2 be Real such that
        AZ3: d2 > 0 &
        for z be Point of REAL-NS 2 st z <> 0.(REAL-NS 2) & ||.z.|| < d2
        holds ||.z.||" * ||. Rv/.z .|| < r/2 by NDIFF_1:26, AZ01;
        AZ31: h is convergent & lim h = 0 by CFDIFF_1:def 1;
        set d = min(d1,d2);
        0 < d by AZ2,AZ3,XXREAL_0:21; then
        consider M be Element of NAT such that
        AZ4: for n be Element of NAT st M<=n holds |. h.n-0 .| < d
        by AZ31, COMSEQ_2:def 5;
        XXX: d <= d1 & d <=d2 by XXREAL_0:17;
        now let n be Element of NAT;
          assume
          AZ0: M <= n;
          set x = Re (h.n), y = Im (h.n);
          <*x,y*> in REAL 2 by FINSEQ_2:121; then
          reconsider z=<*x,y*> as Point of REAL-NS 2 by REAL_NS1:def 4;
          Ru is total by NDIFF_1:def 5; then
          W0700: dom Ru = the carrier of REAL-NS 2 by PARTFUN1:def 4;
          Rv is total by NDIFF_1:def 5; then
          V0700: dom Rv = the carrier of REAL-NS 2 by PARTFUN1:def 4;
          AZ5: z <> 0.(REAL-NS 2)
          proof
            set m=2;
            assume z = 0.(REAL-NS 2); then
            z = 0*2 by REAL_NS1:def 4
             .= <* 0,0 *> by EUCLID:58,74; then
            x=0 & y=0 by FINSEQ_1:98; then
            h.n = 0 by COMPLEX1:9, COMPLEX1:12;
            hence contradiction by COMSEQ_1:4;
          end;
          YYY0: |. h.n-0 .| < d by AZ4, AZ0;
          h.n = x+y*<i> by COMPLEX1:29; then
          AZ6: ||.z.|| = |.h.n.| by Lm034;
          AZ7: ||.z.|| < d1 by AZ6, YYY0, XXX, XXREAL_0:2;
          AZ8: ||.z.|| < d2 by AZ6, YYY0, XXX, XXREAL_0:2;
          AZ9: ||.z.||"* ||. Ru/.z .|| < r/2 by AZ5,AZ7,AZ2;
          AZ10: ||.z.||"* ||. Rv/.z .|| < r/2 by AZ5, AZ8, AZ3;
          AZ11:
          R/.(h.n) = proj(1,1).(Ru.<* Re (h.n), Im (h.n) *>) +
          (proj(1,1).(Rv.<* Re (h.n), Im (h.n) *>))*<i> by Z30;
          h.n <> 0 by COMSEQ_1:4; then
          0 < |.h.n.| by COMPLEX1:133; then
          AZS1: |.h.n.|"* |. R/.(h.n) .|
          <= |.h.n.|"* (abs(proj(1,1).(Ru.<* Re (h.n), Im (h.n) *>))
          + abs(proj(1,1).(Rv.<* Re (h.n), Im (h.n) *>)))
          by AZ11, Lm035, XREAL_1:66;
          AZ013: abs(proj(1,1).(Ru.<* Re (h.n), Im (h.n) *>))
          = abs(proj(1,1).(Ru/.z )) by PARTFUN1:def 8, W0700
          .= ||. Ru/.z .|| by Lm037;
          AZ014:
          abs(proj(1,1).(Rv.<* Re (h.n), Im (h.n) *>))
          = abs(proj(1,1).(Rv/.z )) by PARTFUN1:def 8, V0700
          .= ||. Rv/.z .|| by Lm037;
          |.h.n.|"* abs(proj(1,1).(Ru.<* Re (h.n), Im (h.n) *>))
          + |.h.n.|"* abs(proj(1,1).(Rv.<* Re (h.n), Im (h.n) *>))
          < r/2 +r/2 by XREAL_1:10, AZ014, AZ10, AZ013, AZ6, AZ9; then
          |.h.n.|"* |. R/.(h.n) .| < r by AZS1,XXREAL_0:2; then
          |.(h.n)".|* |. R/.(h.n) .| < r by COMPLEX1:152; then
          AZ141: |.(h.n)"* (R/.(h.n)) .| < r by COMPLEX1:151;
          dom R = COMPLEX by FUNCT_2:def 1; then
          rng h c= dom R; then
          |. (h.n)"*((R/*h).n) .| < r by AZ141, FUNCT_2:186; then
          |. (h".n)*((R/*h).n) .| < r by VALUED_1:10;
          hence |.((h")(#)(R/*h)).n-0 .| < r by VALUED_1:5;
        end;
        hence
        ex M be Element of NAT st for n be Element of NAT st M <= n holds
        |.((h")(#)(R/*h)).n-0c .| < r;
      end; then
      (h")(#)(R/*h) is convergent by COMSEQ_2:def 4;
      hence thesis by COMSEQ_2:def 5, V1;
    end; then
    reconsider R as C_REST by CFDIFF_1:def 3;
    reconsider N as Neighbourhood of z0 by X5, Y5, XXREAL_0:21, CFDIFF_1:6;
    now let z be set;
      assume
      AZ1: z in N; then
      reconsider zz=z as Complex;
      set x=Re zz, y=Im zz;
      AZ2: z=x+y*<i> by COMPLEX1:29;
      Z1: x+y*<i> in N by AZ1, COMPLEX1:29;
      rng v c= dom (proj(1,1)qua Function") by PDIFF_1:2; then
      ZZZ: dom <>*v = dom v by RELAT_1:46;
      <*x,y*> in REAL 2 by FINSEQ_2:121; then
      reconsider xy=<*x,y*> as Point of REAL-NS 2 by REAL_NS1:def 4;
      ZY1: xy-gvxy0 = <* x-x0,y-y0 *> by Y1,A4,Lm212;
      Z05: abs(x-x0) < r2/2 by Z1, LmZ0;
      Z06: abs(y-y0) < r2/2 by Z1, LmZ0;
      Z61: ||.xy-gvxy0.|| <=abs(x-x0) + abs(y-y0) by ZY1,Lm036;
      abs(x-x0) + abs(y-y0) < r2/2+r2/2 by Z05,Z06, XREAL_1:10; then
      ||.xy-gvxy0.|| < r2 by Z61,XXREAL_0:2; then
      xy in {wxy where wxy is Point of REAL-NS 2 : ||.wxy-gvxy0.|| < r2 };
      then <*x,y *> in Nv by Y5;
      hence z in dom f by AZ2, ZZZ, Y1, Y3, AG2;
    end; then
    Z50: N c= dom f by TARSKI:def 3;
    now let z be Complex;
      assume
      Z51: z in N;
      set x = Re z, y = Im z;
      Z53: z = x+y*<i> by COMPLEX1:29;
      Z55: z-z0 = (x-x0)+(y-y0)*<i> by A4, Z53;
      Re (z-z0)=x-x0 by Z55,COMPLEX1:28; then
      Z57: <* Re(z-z0), Im(z-z0) *> = <* x-x0, y-y0 *> by Z55, COMPLEX1:28;
      thus f/.z - f/.z0
      = (partdiff(u,xy0,1)+<i>*partdiff(v,xy0,1))*((x-x0)+(y-y0)*<i>)
      + (proj(1,1).(Ru.<*x-x0,y-y0*>)
      + (proj(1,1).(Rv.<*x-x0,y-y0*>))*<i>) by A4, Z53, Z0, Z51
      .= L/.(z-z0) + (proj(1,1).(Ru.<*x-x0,y-y0*>)
      + (proj(1,1).(Rv.<*x-x0,y-y0*>))*<i>) by Z20, A4, Z53
      .= L/.(z-z0) + R/.(z-z0) by Z30, Z57;
    end; then
    f is_differentiable_in z0 by CFDIFF_1:def 6,Z50;
    hence thesis by A2, A3, A4, Th01;
  end;
