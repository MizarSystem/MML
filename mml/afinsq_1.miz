:: Zero Based Finite Sequences
::  by Tetsuya Tsunetou , Grzegorz Bancerek and Yatsuka Nakamura
::
:: Received September 28, 2001
:: Copyright (c) 2001 Association of Mizar Users

environ

 vocabularies NUMBERS, SUBSET_1, FUNCT_1, ARYTM_3, XXREAL_0, XBOOLE_0, TARSKI,
      NAT_1, ORDINAL1, FINSEQ_1, CARD_1, FINSET_1, RELAT_1, PARTFUN1, FUNCOP_1,
      ORDINAL4, ORDINAL2, ARYTM_1, REAL_1, ZFMISC_1, FUNCT_4, VALUED_0,
      AFINSQ_1, PRGCOR_2, CAT_1, AMISTD_1, AMISTD_3, AMISTD_2, VALUED_1,
      PRE_POLY, CONNSP_3, BINOP_1, FINSOP_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, CARD_1, NUMBERS, RELAT_1,
      FUNCT_1, ORDINAL1, ORDINAL2, ORDINAL4, XCMPLX_0, REAL_1, NAT_1, PARTFUN1,
      BINOP_1, FINSOP_1, NAT_D,
      FINSET_1, FINSEQ_1, FUNCOP_1, FUNCT_4, FUNCT_7, XXREAL_0, VALUED_0,
      VALUED_1;
 constructors WELLORD2, FUNCT_4, XXREAL_0, ORDINAL4, FUNCT_7, ORDINAL3,
      VALUED_1, ENUMSET1, NAT_D, XXREAL_2, BINOP_1, FINSOP_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCOP_1,
      XXREAL_0, XREAL_0, NAT_1, CARD_1, ORDINAL2, NUMBERS, FINSEQ_1, VALUED_1,
      XXREAL_2, MEMBERED;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, ORDINAL1, XBOOLE_0, FUNCOP_1,RELAT_1, VALUED_1, NAT_1,
      FUNCT_1;
 theorems TARSKI, AXIOMS, FUNCT_1, NAT_1, ZFMISC_1, RELAT_1, RELSET_1,
      ORDINAL1, CARD_1, FINSEQ_1, FUNCT_7, ORDINAL4, CARD_2, FUNCT_4, ORDINAL3,
      XBOOLE_0, XBOOLE_1, FINSET_1, FUNCOP_1, XREAL_1, VALUED_0, ENUMSET1,
      XXREAL_0, XREAL_0, GRFUNC_1, XXREAL_2, NAT_D, VALUED_1, BINOP_1,
      INT_1;
 schemes FUNCT_1, SUBSET_1, NAT_1, XBOOLE_0, CLASSES1, FINSEQ_1, BINOP_1;

begin

reserve k,n for Nat,
  x,y,z,y1,y2,X,Y for set,
  f,g for Function;

:: Extended Segments of Natural Numbers

canceled 2;

theorem
  k = k /\ n implies k <= n by NAT_1:47;

theorem Th4:
  n \/ { n } = n+1
proof
  n+1 = succ n;
  hence thesis;
end;

theorem Th5:
  Seg n c= n+1
proof
  let x;
  assume
A1: x in Seg n;
  then reconsider x as Element of NAT;
  x<=n by A1,FINSEQ_1:3;
  then x<n+1 by NAT_1:13;
  hence thesis by NAT_1:45;
end;

theorem
  n+1 = {0} \/ Seg n
proof
  thus n+1 c= {0} \/ Seg n
  proof
    let x;
    assume x in n+1;
    then x in {j where j is Element of NAT: j<n+1} by AXIOMS:30;
    then consider j being Element of NAT such that
A1: j=x and
A2: j<n+1;
    j=0 or 1<j+1 & j<=n by A2,NAT_1:13,XREAL_1:31;
    then j=0 or 1<=j & j<=n by NAT_1:13;
    then x in {0} or x in Seg n by A1,FINSEQ_1:3,TARSKI:def 1;
    hence thesis by XBOOLE_0:def 3;
  end;
  1 <= n+1 by NAT_1:11; then
A3: {0} c= n+1 by CARD_1:87,NAT_1:40;
  Seg(n) c= n+1 by Th5;
  hence thesis by A3,XBOOLE_1:8;
end;

::  Finite ExFinSequences

theorem Th7:
  for r being Function holds r is finite T-Sequence-like iff
  ex n st dom r = n
proof
  let r be Function;
  hereby
    assume
A1: r is finite T-Sequence-like;
    then dom r is finite by FINSET_1:29;
    hence ex n st dom r = n by A1;
  end;
  assume ex n st dom r = n;
  hence thesis by FINSET_1:29,ORDINAL1:def 7;
end;

definition
  mode XFinSequence is finite T-Sequence;
end;

reserve p,q,r,s,t for XFinSequence;

registration let p;
  cluster dom p -> natural;
  coherence
  proof
    ex n st dom p = n by Th7;
    hence thesis;
  end;
end;

notation let p;
  synonym len p for card p;
end;

registration let p;
  identify len p with dom p;
  compatibility
  proof
    thus len p = card dom p by CARD_1:104
      .= dom p by CARD_1:def 5;
  end;
end;

definition let p;
  redefine func len p -> Element of NAT;
  coherence
  proof
    card p = card p;
    hence thesis;
  end;
end;

definition let p;
  canceled;
  redefine func dom p -> Subset of NAT;
  coherence
  proof
    {i where i is Element of NAT:i<len p} c= NAT
    proof
      let x be set;
      assume x in {i where i is Element of NAT:i<len p};
      then ex i being Element of NAT st i=x & i<len p;
      hence thesis;
    end;
    hence thesis by AXIOMS:30;
  end;
end;

theorem
  (ex k st dom f c= k) implies ex p st f c= p
proof
  given k such that
A1: dom f c= k;
  deffunc F(set) = f.$1;
  consider g such that
A2: dom g = k & for x st x in k holds g.x = F(x) from FUNCT_1:sch 3;
  reconsider g as XFinSequence by A2,Th7;
  take g;
  let y,z;
  assume A4: [y,z] in f;
  then
A5: y in dom f by RELAT_1:def 4;
  then
A6: [y,g.y] in g by A1,A2,FUNCT_1:8;
  f.y = z by A4,A5,FUNCT_1:def 4;
  hence thesis by A1,A2,A5,A6;
end;

scheme XSeqEx{A()->Nat,P[set,set]}:
  ex p st dom p = A() & for k st k in A() holds P[k,p.k]
provided
A1: for k st k in A() ex x st P[k,x]
proof
A2: for x st x in A() ex y st P[x,y]
  proof
    let x;
    assume
A3: x in A();
    A()={i where i is Element of NAT: i<A()} by AXIOMS:30;
    then ex i being Element of NAT st i=x & i<A() by A3;
    hence thesis by A1,A3;
  end;
  consider f being Function such that
A4: dom f = A() & for x st x in A() holds P[x,f.x] from CLASSES1:sch 1(A2);
  reconsider p=f as XFinSequence by A4,Th7;
  take p;
  thus thesis by A4;
end;

scheme
  XSeqLambda{A()->Nat,F(set) -> set}: ex p being XFinSequence st len p = A() &
  for k st k in A() holds p.k=F(k) proof
  consider f being Function such that
A1: dom f = A() & for x st x in A() holds f.x=F(x) from FUNCT_1:sch 3;
  reconsider p=f as XFinSequence by A1,Th7;
  take p;
  thus thesis by A1;
end;

theorem
  z in p implies ex k st k in dom p & z=[k,p.k]
proof
  assume
A1: z in p;
  then consider x,y such that
A2: z=[x,y] by RELAT_1:def 1;
  x in dom p by A1,A2,FUNCT_1:8;
  then reconsider k = x as Element of NAT;
  take k;
  thus thesis by A1,A2,FUNCT_1:8;
end;

theorem Th10:
  dom p = dom q & (for k st k in dom p holds p.k = q.k) implies p = q
proof
  assume that
A1: dom p = dom q and
A2: for k st k in dom p holds p.k = q.k;
  for x st x in dom p holds p.x=q.x by A2;
  hence thesis by A1,FUNCT_1:9;
end;

theorem Th11:
  ( len p = len q & for k st k < len p holds p.k=q.k ) implies p=q
proof
  assume that
A1: len p = len q and
A2: for k st k<len p holds p.k = q.k;
  now
    let x;
    assume
A3: x in dom p;
    then reconsider k=x as Element of NAT;
    k < len p by A3,NAT_1:45;
    hence p.x = q.x by A2;
  end;
  hence thesis by A1,FUNCT_1:9;
end;

canceled;

registration let p,n;
  cluster p|n -> finite;
  coherence
proof
A1: len p <= n or n <= len p;
  dom(p| n) = len p /\ n by RELAT_1:90;
  then dom(p| n) = len p or dom(p| n) = n by A1,NAT_1:47;
  hence thesis by Th7;
end;
end;

theorem
  rng p c= dom f implies f*p is XFinSequence
proof
  assume rng p c= dom f;
  then dom(f*p) = len p by RELAT_1:46;
  hence thesis by Th7;
end;

theorem Th13:
  k < len p implies dom(p|k) = k
proof
  assume k < len p;
  then k c= len p by NAT_1:40;
  hence thesis by RELAT_1:91;
end;

:: XFinSequences of D

registration let D be set;
  cluster finite T-Sequence of D;
  existence
  proof
    {} is T-Sequence of D by ORDINAL1:45;
    hence thesis;
  end;
end;

definition let D be set;
  mode XFinSequence of D is finite T-Sequence of D;
end;

theorem Th15:
  for D being set, f being XFinSequence of D holds f is PartFunc of NAT,D
proof
  let D be set, f be XFinSequence of D;
  dom f c= NAT & rng f c= D by RELAT_1:def 19;
  hence thesis by RELSET_1:11;
end;

registration
  cluster empty -> T-Sequence-like Function;
  coherence;
end;

reserve D for set;

theorem Th3:
  for k being Nat,a being set holds k --> a is XFinSequence
proof
  let k be Nat,a be set;
  reconsider p = (k --> a) as Function;
  k in NAT & dom p = k by FUNCOP_1:19,ORDINAL1:def 13;
  hence thesis by Th7;
end;

theorem Th17:
  for D being non empty set ex p being XFinSequence of D st len p = k
proof
  let D be non empty set;
  consider y being Element of D;
  set p = k --> y;
  dom p = k by FUNCOP_1:19;
  then reconsider p = k --> y as XFinSequence by Th7;
  reconsider p as XFinSequence of D;
  take p;
  thus thesis by FUNCOP_1:19;
end;

::                                ::
::    The Empty XFinSequence      ::
::                                ::

theorem
  len p = 0 iff p = {};

theorem Th19:
  for D be set holds {} is XFinSequence of D
proof
  let D be set;
  rng {} c= D by XBOOLE_1:2;
  hence thesis by RELAT_1:def 19;
end;

registration let D be set;
  cluster empty XFinSequence of D;
  existence
  proof
    {} is XFinSequence of D by Th19;
    hence thesis;
  end;
end;

registration
  let D be non empty set;
  cluster non empty XFinSequence of D;
  existence
  proof
    set k = 1;
    ex p being XFinSequence of D st len p = k by Th17; then
    consider p being XFinSequence of D such that
a1: len p = k;
    p <> {} by a1;
    hence thesis;
  end;
end;

definition let x;
  func <%x%> -> set equals
  0 .--> x;
  coherence;
end;

registration let x;
  cluster <%x%> -> non empty;
  coherence;
end;

definition let D be set;
  func <%>D -> XFinSequence of D equals
  {};
  coherence by Th19;
end;

registration
  let D be set;
  cluster <%>D -> empty;
  coherence;
end;

definition let p,q;
  redefine func p^q means
:Def4: dom it = len p + len q & (for k st k in dom p
  holds it.k=p.k) & for k st k in dom q holds it.(len p + k) = q.k;
  compatibility
  proof
    let pq be T-Sequence;
A1: len p +^ len q = len p + len q by CARD_2:49;
    hereby
      assume
A2:   pq = p^q;
      hence dom pq = len p + len q by A1,ORDINAL4:def 1;
      thus for k st k in dom p holds pq.k=p.k by A2,ORDINAL4:def 1;
      let k;
      assume k in dom q;
      then pq.(len p +^ k) = q.k & k in NAT by A2,ORDINAL4:def 1;
      hence pq.(len p + k) = q.k by CARD_2:49;
    end;
    assume that
A3: dom pq = len p + len q and
A4: for k st k in dom p holds pq.k=p.k and
A5: for k st k in dom q holds pq.(len p + k) = q.k;
A6: now
      let a be Ordinal;
      assume
A7:   a in dom q;
      then reconsider k = a as Element of NAT;
      thus pq.(dom p +^ a) = pq.(len p + k) by CARD_2:49
        .= q.a by A5,A7;
    end;
    for a be Ordinal st a in dom p holds pq.a = p.a by A4;
    hence thesis by A1,A3,A6,ORDINAL4:def 1;
  end;
end;

registration
  let p,q;
  cluster p^q -> finite;
  coherence
  proof
    dom (p^q) = (dom p)+^dom q by ORDINAL4:def 1;
    hence thesis by Th7;
  end;
end;

theorem Th20:
  len(p^q) = len p + len q by Def4;

theorem Th21:
  len p <= k & k < len p + len q implies (p^q).k=q.(k-len p)
proof
  assume that
A1: len p <= k and
A2: k < len p + len q;
  consider m being Nat such that
A3: len p + m = k by A1,NAT_1:10;
  k - len p < len p + len q - len p by A2,XREAL_1:16;
  then m in dom q by A3,NAT_1:45;
  hence thesis by A3,Def4;
end;

theorem Th22:
  len p <= k & k < len(p^q) implies (p^q).k = q.(k - len p)
proof
  assume that
A1: len p <= k and
A2: k < len(p^q);
  k < len p + len q by A2,Def4;
  hence thesis by A1,Th21;
end;

theorem Th23:
  k in dom (p^q) implies (k in dom p or ex n st n in dom q & k=len
  p + n )
proof
  assume k in dom(p^q);
  then k in (len p + len q) by Def4;
  then
A1: k < len p + len q by NAT_1:45;
  now
    assume len p <= k;
    then consider n being Nat such that
A2: k=len p + n by NAT_1:10;
    n + len p - len p < len q + len p - len p by A1,A2,XREAL_1:16;
    then n in len q by NAT_1:45;
    hence thesis by A2;
  end;
  hence thesis by NAT_1:45;
end;

theorem Th24:
  for p,q being T-Sequence holds dom p c= dom(p^q)
proof
  let p,q be T-Sequence;
  dom(p^q) = (dom p)+^(dom q) by ORDINAL4:def 1;
  hence thesis by ORDINAL3:27;
end;

theorem Th25:
  x in dom q implies ex k st k=x & len p + k in dom(p^q)
proof
  assume
A1: x in dom q;
  then reconsider k=x as Element of NAT;
  take k;
  k < len q by A1,NAT_1:45;
  then len p + k < len p + len q by XREAL_1:10;
  then len p + k in (len p + len q) by NAT_1:45;
  hence thesis by Def4;
end;

theorem Th26:
  k in dom q implies len p + k in dom(p^q)
proof
  assume k in dom q;
  then ex n st n=k & len p + n in dom(p^q) by Th25;
  hence thesis;
end;

theorem Th27:
  rng p c= rng(p^q)
proof
  now
A1: dom p c= dom(p^q) by Th24;
    let x;
    assume x in rng p;
    then consider y such that
A2: y in dom p and
A3: x=p.y by FUNCT_1:def 5;
    reconsider k=y as Element of NAT by A2;
    (p^q).k=p.k by A2,Def4;
    hence x in rng(p^q) by A2,A3,A1,FUNCT_1:12;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem Th28:
  rng q c= rng(p^q)
proof
  now
    let x;
    assume x in rng q;
    then consider y such that
A1: y in dom q and
A2: x=q.y by FUNCT_1:def 5;
    reconsider k=y as Element of NAT by A1;
    len p + k in dom(p^q) & (p^q).(len p + k) = q.k by A1,Def4,Th26;
    hence x in rng(p^q) by A2,FUNCT_1:12;
  end;
  hence thesis by TARSKI:def 3;
end;

theorem Th29:
  rng(p^q) = rng p \/ rng q
proof
  now
    let x;
    assume x in rng(p^q);
    then consider y such that
A1: y in dom(p^q) and
A2: x=(p^q).y by FUNCT_1:def 5;
    reconsider k=y as Element of NAT by A1;
    y in (len p + len q) by A1,Def4;
    then
A3: k < len p + len q by NAT_1:45;
A4: now
      assume
A5:   len p <= k;
      then consider m being Nat such that
A6:   len p + m = k by NAT_1:10;
      m + len p - len p < len p + len q - len p by A3,A6,XREAL_1:16;
      then
A7:   m in len q by NAT_1:45;
      q.(k-len p) = x by A2,A3,A5,Th21;
      hence x in rng q by A6,A7,FUNCT_1:12;
    end;
    now
      assume not len p <= k;
      then
A8:   k in dom p by NAT_1:45;
      then p.k = x by A2,Def4;
      hence x in rng p by A8,FUNCT_1:12;
    end;
    hence x in rng p \/ rng q by A4,XBOOLE_0:def 3;
  end; then
A9: rng(p^q) c= rng p \/ rng q by TARSKI:def 3;
  rng p c= rng(p^q) & rng q c= rng(p^q) by Th27,Th28;
  then (rng p \/ rng q) c= rng(p^q) by XBOOLE_1:8;
  hence thesis by A9,XBOOLE_0:def 10;
end;

theorem Th30:
  p^q^r = p^(q^r)
proof
A1: for k st k in dom p holds ((p^q)^r).k=p.k
  proof
    let k;
    assume
A2: k in dom p;
    dom p c= dom(p^q) by Th24;
    hence (p^q^r).k=(p^q).k by A2,Def4
      .=p.k by A2,Def4;
  end;
A3: for k st k in dom(q^r) holds ((p^q)^r).(len p + k)=(q^r).k
  proof
    let k;
    assume
A4: k in dom(q^r);
A5: now
      assume not k in dom q;
      then consider n such that
A6:   n in dom r and
A7:   k=len q + n by A4,Th23;
      thus (p^q^r).(len p + k) =(p^q^r).(len p + len q + n) by A7
        .=(p^q^r).(len(p^q) + n) by Def4
        .=r.n by A6,Def4
        .=(q^r).k by A6,A7,Def4;
    end;
    now
      assume
A8:   k in dom q;
      then (len p + k) in dom(p^q) by Th26;
      hence (p^q^r).(len p + k) = (p^q).(len p + k) by Def4
        .=q.k by A8,Def4
        .=(q^r).k by A8,Def4;
    end;
    hence thesis by A5;
  end;
  dom ((p^q)^r) = (len (p^q) + len r) by Def4
    .= (len p + len q + len r) by Def4
    .= (len p + (len q + len r))
    .= (len p + len(q^r)) by Def4;
  hence thesis by A1,A3,Def4;
end;

theorem Th31:
  p^r = q^r or r^p = r^q implies p = q
proof
A1: now
    assume
A2: p^r = q^r;
    then len p + len r = len(q^r) by Def4;
    then
A3: len p + len r = len q + len r by Def4;
    for k st k in dom p holds p.k=q.k
    proof
      let k;
      assume
A4:   k in dom p;
      hence p.k=(q^r).k by A2,Def4
        .=q.k by A3,A4,Def4;
    end;
    hence thesis by A3,Th10;
  end;
A5: now
    assume
A6: r^p=r^q;
    then
A7: len r + len p = len(r^q) by Def4
      .=len r + len q by Def4;
    for k st k in dom p holds p.k=q.k
    proof
      let k;
      assume
A8:   k in dom p;
      hence p.k = (r^q).(len r + k) by A6,Def4
        .= q.k by A7,A8,Def4;
    end;
    hence thesis by A7,Th10;
  end;
  assume p^r = q^r or r^p = r^q;
  hence thesis by A1,A5;
end;

theorem Th32:
  p^{} = p & {}^p = p
proof
A1: for k st k in dom p holds p.k=(p^{}).k by Def4;
  dom(p^{}) = len p + len {} by Def4
    .= dom p;
  hence p^{} = p by A1,Th10;
A2: for k st k in dom p holds p.k = ({}^p).k
  proof
    let k;
    assume
A3: k in dom p;
    thus ({}^p).k =({}^p).(len {} + k)
      .=p.k by A3,Def4;
  end;
  dom({}^p) = (len {} + len p) by Def4
    .= dom p;
  hence thesis by A2,Th10;
end;

theorem Th33:
  p^q = {} implies p={} & q={}
proof
  assume p^q={};
  then 0 = len (p^q)
    .= len p + len q by Def4;
  hence thesis;
end;

registration
  let D be set;
  let p,q be XFinSequence of D;
  cluster p^q -> D-valued;
  coherence
  proof
A1: rng q c= D by RELAT_1:def 19;
    rng(p^q) = rng p \/ rng q & rng p c= D by Th29,RELAT_1:def 19;
    then rng(p^q) c= D by A1,XBOOLE_1:8;
    hence thesis by RELAT_1:def 19;
  end;
end;

Lm1: for x1, y1 being set holds [x,y] in {[x1,y1]} implies x = x1 & y = y1
proof
  let x1, y1 be set;
  assume [x,y] in {[x1,y1]};
  then [x,y] = [x1,y1] by TARSKI:def 1;
  hence thesis by ZFMISC_1:33;
end;

definition
  let x;
  redefine func <%x%> -> Function means
  :Def5:
  dom it = 1 & it.0 = x;
  coherence;
  compatibility
  proof
    let f be Function;
    thus f = <%x%> implies dom f = 1 & f.0 = x by CARD_1:87,FUNCOP_1:19,87;
    assume that
A1: dom f = 1 and
A2: f.0 = x;
    reconsider g = { [0,f.0] } as Function;
    [y,z] in f iff [y,z] in g
    proof
      hereby
        assume
A3:     [y,z] in f;
        then y in {0} by A1,CARD_1:87,RELAT_1:def 4;
        then
A4:     y = 0 by TARSKI:def 1;
A5:     rng f = {f.0} by A1,CARD_1:87,FUNCT_1:14;
        z in rng f by A3,RELAT_1:def 5;
        then z = f.0 by A5,TARSKI:def 1;
        hence [y,z] in g by A4,TARSKI:def 1;
      end;
      assume [y,z] in g;
      then
A6:   y = 0 & z = f.0 by Lm1;
      0 in dom f by A1,CARD_1:87,TARSKI:def 1;
      hence thesis by A6,FUNCT_1:def 4;
    end;
    then f = { [0,f.0] } by RELAT_1:def 2;
    hence thesis by A2,FUNCT_4:87;
  end;
end;

registration
  let x;
  cluster <%x%> -> Function-like Relation-like;
  coherence;
end;

registration
  let x;
  cluster <%x%> -> finite T-Sequence-like;
  coherence
  proof
    dom <%x%> = 1 by Def5;
    hence thesis by FINSET_1:29,ORDINAL1:def 7;
  end;
end;

theorem
  p^q is XFinSequence of D implies p is XFinSequence of D & q is
  XFinSequence of D
proof
  assume p^q is XFinSequence of D;
  then rng(p^q) c= D by RELAT_1:def 19;
  then
A1: rng p \/ rng q c= D by Th29;
  rng p c= rng p \/ rng q by XBOOLE_1:7;
  then rng p c= D by A1,XBOOLE_1:1;
  hence p is XFinSequence of D by RELAT_1:def 19;
  rng q c= rng p \/ rng q by XBOOLE_1:7;
  then rng q c= D by A1,XBOOLE_1:1;
  hence thesis by RELAT_1:def 19;
end;

definition
  let x,y;
  func <%x,y%> -> set equals
  <%x%>^<%y%>;
  correctness;
  let z;
  func <%x,y,z%> -> set equals
  <%x%>^<%y%>^<%z%>;
  correctness;
end;

registration
  let x,y;
  cluster <%x,y%> -> Function-like Relation-like;
  coherence;
  let z;
  cluster <%x,y,z%> -> Function-like Relation-like;
  coherence;
end;

registration
  let x,y;
  cluster <%x,y%> -> finite T-Sequence-like;
  coherence;
  let z;
  cluster <%x,y,z%> -> finite T-Sequence-like;
  coherence;
end;

theorem
  <%x%> = { [0,x] } by FUNCT_4:87;

theorem Th36:
  p=<%x%> iff dom p = 1 & rng p = {x}
proof
  thus p = <%x%> implies dom p = 1 & rng p = {x}
  proof
    assume
A1: p = <%x%>;
    hence dom p = 1 by Def5;
    dom p = {0} by A1,Def5,CARD_1:87;
    then rng p = {p.0} by FUNCT_1:14;
    hence thesis by A1,Def5;
  end;
  assume that
A2: dom p = 1 and
A3: rng p = {x};
  1=0+1;
  then p.0 in {x} by A2,A3,NAT_1:46,FUNCT_1:12;
  then p.0 = x by TARSKI:def 1;
  hence thesis by A2,Def5;
end;

canceled;

theorem
  p = <%x%> iff len p = 1 & p.0 = x by Def5;

theorem Th39:
  (<%x%>^p).0 = x
proof
  0 in 1 by CARD_1:87,TARSKI:def 1;
  then 0 in dom <%x%> by Def5;
  then (<%x%>^p).0 = <%x%>.0 by Def4;
  hence thesis by Def5;
end;

theorem Th40:
  (p^<%x%>).(len p)=x
proof
  dom <%x%> = 1 & 1 = (0+1) by Def5;
  then
A1: 0 in dom <%x%> by NAT_1:46;
  len p + 0 = len p;
  hence (p^<%x%>).(len p) = <%x%>.0 by A1,Def4
    .=x by Def5;
end;

theorem
  <%x,y,z%>=<%x%>^<%y,z%> & <%x,y,z%>=<%x,y%>^<%z%> by Th30;

theorem Th42:
  p = <%x,y%> iff len p = 2 & p.0=x & p.1=y
proof
  thus p = <%x,y%> implies len p = 2 & p.0=x & p.1=y
  proof
    assume
A1: p=<%x,y%>;
    hence len p = len <%x%> + len <%y%> by Def4
      .= 1 + len <%y%> by Th36
      .= 1 + 1 by Th36
      .=2;
A2: 0 in {0} by TARSKI:def 1;
    then
A3: 0 in dom <%y%> by Def5,CARD_1:87;
    0 in dom <%x%> by A2,Def5,CARD_1:87;
    hence p.0 = <%x%>.0 by A1,Def4
      .= x by Def5;
    thus p.1 = (<%x%>^<%y%>).(len <%x%> + 0) by A1,Th36
      .= <%y%>.0 by A3,Def4
      .= y by Def5;
  end;
  assume that
A4: len p = 2 and
A5: p.0=x and
A6: p.1=y;
A7: for k st k in dom <%y%> holds p.((len <%x%>)+k)=<%y%>.k
  proof
    let k;
    assume k in dom <%y%>;
    then
A8: k in {0} by Def5,CARD_1:87;
    thus p.((len <%x%>) + k) = p.(1+k) by Th36
      .=p.(1+0) by A8,TARSKI:def 1
      .=<%y%>.0 by A6,Def5
      .= <%y%>.k by A8,TARSKI:def 1;
  end;
A9: for k st k in dom <%x%> holds p.k=<%x%>.k
  proof
    let k;
    assume k in dom <%x%>;
    then k in {0} by Def5,CARD_1:87;
    then k=0 by TARSKI:def 1;
    hence thesis by A5,Def5;
  end;
  dom p = (1+1) by A4
    .= (len <%x%> + 1) by Th36
    .= (len <%x%> + len <%y%>) by Th36;
  hence thesis by A9,A7,Def4;
end;

theorem Th43:
  p = <%x,y,z%> iff len p = 3 & p.0 = x & p.1 = y & p.2 = z
proof
  thus p = <%x,y,z%> implies len p = 3 & p.0 = x & p.1 = y & p.2 = z
  proof
A1: 0 in {0} by TARSKI:def 1;
    then
A2: 0 in dom <%x%> by Def5,CARD_1:87;
A3: 0 in dom <%z%> by A1,Def5,CARD_1:87;
    assume
A4: p =<%x,y,z%>;
    hence len p =len <%x,y%> + len <%z%> by Def4
      .=2 + len <%z%> by Th42
      .=2+1 by Th36
      .=3;
    thus p.0 = (<%x%>^<%y,z%>).0 by A4,Th30
      .=<%x%>.0 by A2,Def4
      .=x by Def5;
    1 in (1+1) & len <%x,y%> = 2 by NAT_1:46,Th42;
    hence p.1 =<%x,y%>.1 by A4,Def4
      .=y by Th42;
    thus p.2 =(<%x,y%>^<%z%>).(len (<%x,y%>) + 0) by A4,Th42
      .= <%z%>.0 by A3,Def4
      .= z by Def5;
  end;
  assume that
A5: len p = 3 and
A6: p.0 = x and
A7: p.1 = y and
A8: p.2 = z;
A9: for k st k in dom <%x,y%> holds p.k=<%x,y%>.k
  proof
A10: len <%x,y%> = 2 by Th42;
    let k such that
A11: k in dom <%x,y%>;
A12: k=1 implies p.k=<%x,y%>.k by A7,Th42;
    k=0 implies p.k=<%x,y%>.k by A6,Th42;
    hence thesis by A11,A10,A12,CARD_1:88,TARSKI:def 2;
  end;
A13: for k st k in dom <%z%> holds p.( (len <%x,y%>) + k) = <%z%>.k
  proof
    let k;
    assume k in dom <%z%>;
    then k in {0} by Def5,CARD_1:87;
    then
A14: k = 0 by TARSKI:def 1;
    hence p.( (len <%x,y%>) + k) = p.(2+0) by Th42
      .=<%z%>.k by A8,A14,Def5;
  end;
  dom p = (2+1) by A5
    .= ((len <%x,y%>) + 1) by Th42
    .= ((len <%x,y%>) + len <%z%>) by Th36;
  hence thesis by A9,A13,Def4;
end;

theorem Tex:
  p <> {} implies ex q,x st p=q^<%x%>
proof
  assume p <> {};
  then consider n being Nat such that
A1: len p = n+1 by NAT_1:6;
  reconsider n as Element of NAT by ORDINAL1:def 13;
  set q=p| n;
  n <= len p by A1,NAT_1:11;
  then dom q = len p /\ n & n c= len p by NAT_1:40,RELAT_1:90;
  then
A2: dom q = n by XBOOLE_1:28;
A3: for x st x in dom p holds p.x = (q^<%p.(len p - 1)%>).x
  proof
    let x;
    assume
A4: x in dom p;
    then reconsider k = x as Element of NAT;
A5: now
      assume
A6:   k in n;
      hence p.k=q.k by A2,FUNCT_1:70
        .=(q^<%p.(len p - 1)%>).k by A2,A6,Def4;
    end;
A7: now
      0 in (0+1) by NAT_1:46;
      then
A8:   0 in dom <%p.(len p - 1)%> by Def5;
      assume
A9:   k in {n};
      hence (q^<%p.(len p - 1)%>).k =(q^<%p.(len p - 1)%>).(len q + 0) by A2,
      TARSKI:def 1
        .=<%p.(len p - 1)%>.0 by A8,Def4
        .=p.n by A1,Def5
        .=p.k by A9,TARSKI:def 1;
    end;
    k in n \/ {n} by A1,A4,Th4;
    hence thesis by A5,A7,XBOOLE_0:def 3;
  end;
  take q;
  take p.(len p - 1);
  dom(q^<%p.(len p - 1)%>) = (len q + len <%p.(len p - 1)%>) by Def4
    .= dom p by A1,A2,Th36;
  hence q^<%p.(len p - 1)%>=p by A3,FUNCT_1:9;
end;

registration
  let D be non empty set;
  let d1 be Element of D;
  cluster <%d1%> -> D -valued;
  coherence;
  let d2 be Element of D;
  cluster <%d1,d2%> -> D -valued;
  coherence;
  let d3 be Element of D;
  cluster <%d1,d2,d3%> -> D -valued;
  coherence;
end;

:: Scheme of induction for extended finite sequences

scheme
  IndXSeq{P[XFinSequence]}: for p holds P[p]
provided
A1: P[{}] and
A7: for p,x st P[p] holds P[p^<%x%>]
proof
  defpred P1[Real] means for p st len p = $1 holds P[p];
  let p;
  consider X being Subset of REAL such that
A2: for x being Real holds x in X iff P1[x] from SUBSET_1:sch 3;
  for k holds k in X
  proof
    defpred R[Nat] means $1 in X;
    for p st len p = 0 holds P[p]
    proof
      let p;
      assume len p = 0;
      then p = {};
      hence thesis by A1;
    end;
    then
A3: R[0] by A2;
A4: for n st R[n] holds R[n+1]
    proof
      let n;
      assume
A5:   R[n];
      P1[n+1]
      proof
        let p;
        assume
A8:     len p = n+1;
        then p <> {};
        then consider w being XFinSequence, x such that
A6:     p = w^<%x%> by Tex;
        len p = len w + len <%x%> by A6,Def4
        .= len w+1 by Def5;
        then P[w] by A2,A5,A8;
        hence P[p] by A6,A7;
      end;
      hence thesis by A2;
    end;
    thus for k holds R[k] from NAT_1:sch 2(A3,A4);
  end;
  then len p in X;
  hence thesis by A2;
end;

theorem
  for p,q,r,s being XFinSequence st p^q = r^s & len p <= len r ex t
  being XFinSequence st p^t = r
proof
  defpred P[XFinSequence] means for p,q,s st p^q=$1^s & len p <= len $1 holds
  ex t being XFinSequence st p^t=$1;
A1: for r,x st P[r] holds P[r^<%x%>]
  proof
    let r,x;
    assume
A2: for p,q,s st p^q=r^s & len p <= len r ex t st p^t=r;
    let p,q,s;
    assume that
A3: p^q=(r^<%x%>)^s and
A4: len p <= len (r^<%x%>);
A5: now
      assume len p <> len(r^<%x%>);
      then len p <> len r + len <%x%> by Def4;
      then
A6:   len p <> len r + 1 by Th36;
      len p <= len r + len <%x%> by A4,Def4;
      then
A7:   len p <= len r + 1 by Th36;
      p^q=r^(<%x%>^s) by A3,Th30;
      then consider t being XFinSequence such that
A8:   p^t = r by A2,A6,A7,NAT_1:8;
      p^(t^<%x%>) = r^<%x%> by A8,Th30;
      hence thesis;
    end;
    now
      assume
A9:   len p = len(r^<%x%>);
A10:  for k st k in dom p holds p.k=(r^<%x%>).k
      proof
        let k;
        assume
A11:    k in dom p;
        hence p.k = (r^<%x%>^s).k by A3,Def4
          .=(r^<%x%>).k by A9,A11,Def4;
      end;
      p^{} = p by Th32
        .=r^<%x%> by A9,A10,Th10;
      hence thesis;
    end;
    hence thesis by A5;
  end;
A12: P[{}]
  proof
    let p,q,s;
    assume that
    p^q={}^s and
A13: len p <= len {};
    take {};
    thus p^{} = p by Th32
      .= {} by A13;
  end;
  for r holds P[r] from IndXSeq(A12,A1);
  hence thesis;
end;

definition
  let D be set;
  func D^omega -> set means
  :Def8:
  x in it iff x is XFinSequence of D;
  existence
  proof
    defpred P[set] means $1 is XFinSequence of D;
    consider X such that
A1: x in X iff x in bool [:NAT,D:] & P[x] from XBOOLE_0:sch 1;
    take X;
    let x;
    thus x in X implies x is XFinSequence of D by A1;
    assume x is XFinSequence of D;
    then reconsider p = x as XFinSequence of D;
    reconsider p as PartFunc of NAT,D by Th15;
    p c= [:NAT,D:];
    hence thesis by A1;
  end;
  uniqueness
  proof
    defpred P[set] means $1 is XFinSequence of D;
    thus for X1,X2 being set st (for x being set holds x in X1 iff P[x]) & (
    for x being set holds x in X2 iff P[x]) holds X1 = X2 from XBOOLE_0:sch 3;
  end;
end;

registration
  let D be set;
  cluster D^omega -> non empty;
  coherence
  proof
    consider f being XFinSequence of D;
    f in D^omega by Def8;
    hence thesis;
  end;
end;

theorem
  x in D^omega iff x is XFinSequence of D by Def8;

theorem
  {} in D^omega
proof
  {} = <%>D;
  hence thesis by Def8;
end;

scheme
  SepXSeq{D()->non empty set, P[XFinSequence]}: ex X st for x holds x in X iff
  ex p st p in D()^omega & P[p] & x=p proof
  defpred P1[set] means ex p st P[p] & $1=p;
  consider Y such that
A1: x in Y iff x in D()^omega & P1[x] from XBOOLE_0:sch 1;
  take Y;
  x in Y implies ex p st p in D()^omega & P[p] & x=p
  proof
    assume x in Y;
    then x in D()^omega & ex p st P[p] & x=p by A1;
    hence thesis;
  end;
  hence thesis by A1;
end;

notation
  let p be XFinSequence;
  let i,x be set;
  synonym Replace(p,i,x) for p+*(i,x);
end;

registration
  let p be XFinSequence;
  let i,x be set;
  cluster p+*(i,x) -> finite T-Sequence-like;
  coherence
  proof
    dom (p+*(i,x)) = dom p by FUNCT_7:32;
    hence thesis by FINSET_1:29,ORDINAL1:def 7;
  end;
end;

theorem
  for p being XFinSequence, i being Element of NAT, x being set holds
  len Replace(p,i,x) = len p & (i < len p implies Replace(p,i,x).i = x) & for j
  being Element of NAT st j <> i holds Replace(p,i,x).j = p.j
proof
  let p be XFinSequence;
  let i be Element of NAT, x be set;
  set f = Replace(p,i,x);
  thus len f = len p by FUNCT_7:32;
  i < len p implies not dom p c= i by NAT_1:40;
  hence i < len p implies f.i = x by FUNCT_7:33,ORDINAL1:26;
  thus thesis by FUNCT_7:34;
end;

registration
  let D be non empty set;
  let p be XFinSequence of D;
  let i be Element of NAT, a be Element of D;
  cluster Replace(p,i,a) -> D -valued;
  coherence
  proof
      per cases;
      suppose
        i in dom p;
        then Replace(p,i,a) = p+*(i.-->a) by FUNCT_7:def 3;
        then
A1:     rng Replace(p,i,a) c= rng p \/ rng (i.-->a) by FUNCT_4:18;
        rng (i.-->a) = {a} by FUNCOP_1:14;
        then
A2:     rng (i.-->a) c= D by ZFMISC_1:37;
        rng p c= D by RELAT_1:def 19;
        then rng p \/ rng (i.-->a) c= D by A2,XBOOLE_1:8;
        hence rng Replace(p,i,a) c= D by A1,XBOOLE_1:1;
      end;
      suppose
        not i in dom p;
        then Replace(p,i,a) = p by FUNCT_7:def 3;
        hence rng Replace(p,i,a) c= D by RELAT_1:def 19;
      end;
    end;
end;

:: missing, 2008.02.02, A.K.

registration
  cluster -> real-valued XFinSequence of REAL;
  coherence
  proof
    let F be XFinSequence of REAL;
    rng F c= REAL by RELAT_1:def 19;
    hence thesis by VALUED_0:def 3;
  end;
end;

registration
  cluster -> natural-valued XFinSequence of NAT;
  coherence
  proof
    let F be XFinSequence of NAT;
    rng F c= NAT by RELAT_1:def 19;
    hence thesis by VALUED_0:def 6;
  end;
end;

:: 2009.0929, A.T.

theorem Th87:
  for x1, x2, x3, x4 being set st
   p = <%x1%>^<%x2%>^<%x3%>^<%x4%>
  holds len p = 4 & p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4
proof
  let x1, x2, x3, x4 be set;
  assume
A1: p = <%x1%>^<%x2%>^<%x3%>^<%x4%>;
  set p13 = <%x1%>^<%x2%>^<%x3%>;
A2: p13 = <%x1, x2, x3%>;
  then
A3: len p13 = 3 by Th43;
A4: p13.0 = x1 & p13.1 = x2 by A2,Th43;
A5: p13.2 = x3 by A2,Th43;
  thus len p = len p13 + len <%x4%> by A1,Def4
    .= 3 + 1 by A3,Th36
    .= 4;
   0 in 3 & 1 in 3 & 2 in 3 by CARD_1:89,ENUMSET1:def 1;
  hence p.0 = x1 & p.1 = x2 & p.2 = x3 by A1,A4,A5,Def4,A3;
  thus p.3 = p.len p13 by A2,Th43
    .= x4 by A1,Th40;
end;

theorem Th88:
  for x1, x2, x3, x4, x5 being set st
     p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>
  holds len p = 5 & p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5
proof
  let x1, x2, x3, x4, x5 be set;
  assume
A1: p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>;
  set p14 = <%x1%>^<%x2%>^<%x3%>^<%x4%>;
A2: len p14 = 4 by Th87;
A3: p14.0 = x1 & p14.1 = x2 by Th87;
A4: p14.2 = x3 & p14.3 = x4 by Th87;
  thus len p = len p14 + len <%x5%> by A1,Def4
    .= 4 + 1 by A2,Th36
    .= 5;
   0 in 4 & 1 in 4 & 2 in 4 & 3 in 4 by CARD_1:90,ENUMSET1:def 2;
  hence p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 by A1,A3,A4,Def4,A2;
  thus p.4 = p.len p14 by Th87
    .= x5 by A1,Th40;
end;

theorem Th89:
  for x1, x2, x3, x4, x5, x6 being set st
     p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>
  holds len p = 6 & p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5 &
  p.5 = x6
proof
  let x1, x2, x3, x4, x5, x6 be set;
  assume
A1: p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>;
  set p15 = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>;
A2: len p15 = 5 by Th88;
A3: p15.0 = x1 & p15.1 = x2 by Th88;
A4: p15.2 = x3 & p15.3 = x4 by Th88;
A5: p15.4 = x5 by Th88;
  thus len p = len p15 + len <%x6%> by A1,Def4
    .= 5 + 1 by A2,Th36
    .= 6;
   0 in 5 & 1 in 5 & 2 in 5 & 3 in 5 & 4 in 5 by CARD_1:91,ENUMSET1:def 3;
  hence p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5
  by A1,A3,A4,A5,Def4,A2;
  thus p.5 = p.len p15 by Th88
    .= x6 by A1,Th40;
end;

theorem Th90:
  for x1, x2, x3, x4, x5, x6, x7 being set st
     p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>^<%x7%>
  holds len p = 7 & p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5 &
  p.5 = x6 & p.6 = x7
proof
  let x1, x2, x3, x4, x5, x6, x7 be set;
  assume
A1: p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>^<%x7%>;
  set p16 = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>;
A2: len p16 = 6 by Th89;
A3: p16.0 = x1 & p16.1 = x2 by Th89;
A4: p16.2 = x3 & p16.3 = x4 by Th89;
A5: p16.4 = x5 & p16.5 = x6 by Th89;
  thus len p = len p16 + len <%x7%> by A1,Def4
    .= 6 + 1 by A2,Th36
    .= 7;
   0 in 6 & 1 in 6 & 2 in 6 & 3 in 6 & 4 in 6 & 5 in 6
    by CARD_1:92,ENUMSET1:def 4;
  hence p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5 & p.5 = x6
  by A1,A3,A4,A5,Def4,A2;
  thus p.6 = p.len p16 by Th89
    .= x7 by A1,Th40;
end;

theorem Th91:
  for x1,x2,x3,x4, x5, x6, x7, x8 being set st
    p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>^<%x7%>^<%x8%>
  holds len p = 8 & p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5 &
  p.5 = x6 & p.6 = x7 & p.7 = x8
proof
  let x1, x2, x3, x4, x5, x6, x7, x8 be set;
  assume
A1: p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>^<%x7%>^<%x8%>;
  set p17 = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>^<%x7%>;
A2: len p17 = 7 by Th90;
A3: p17.0 = x1 & p17.1 = x2 by Th90;
A4: p17.2 = x3 & p17.3 = x4 by Th90;
A5: p17.4 = x5 & p17.5 = x6 by Th90;
A6: p17.6 = x7 by Th90;
  thus len p = len p17 + len <%x8%> by A1,Def4
    .= 7 + 1 by A2,Th36
    .= 8;
   0 in 7 & 1 in 7 & 2 in 7 & 3 in 7 & 4 in 7 & 5 in 7 & 6 in 7
    by CARD_1:93,ENUMSET1:def 5;
  hence p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5 & p.5 = x6 &
  p.6 = x7 by A1,A3,A4,A5,A6,Def4,A2;
  thus p.7 = p.len p17 by Th90
    .= x8 by A1,Th40;
end;

theorem
  for x1,x2,x3,x4,x5,x6,x7, x8, x9 being set st
     p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>^<%x7%>^<%x8%>^<%x9%>
  holds len p = 9 & p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5 &
  p.5 = x6 & p.6 = x7 & p.7 = x8 & p.8 = x9
proof
  let x1, x2, x3, x4, x5, x6, x7, x8, x9 be set;
  assume
A1: p = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>^<%x7%>^<%x8%>^<%x9%>;
  set p17 = <%x1%>^<%x2%>^<%x3%>^<%x4%>^<%x5%>^<%x6%>^<%x7%>^<%x8%>;
A2: len p17 = 8 by Th91;
A3: p17.0 = x1 & p17.1 = x2 by Th91;
A4: p17.2 = x3 & p17.3 = x4 by Th91;
A5: p17.4 = x5 & p17.5 = x6 by Th91;
A6: p17.6 = x7 & p17.7 = x8 by Th91;
  thus len p = len p17 + len <%x9%> by A1,Def4
    .= 8 + 1 by A2,Th36
    .= 9;
   0 in 8 & 1 in 8 & 2 in 8 & 3 in 8 & 4 in 8 & 5 in 8 & 6 in 8 & 7 in 8
    by CARD_1:94,ENUMSET1:def 6;
  hence p.0 = x1 & p.1 = x2 & p.2 = x3 & p.3 = x4 & p.4 = x5 & p.5 = x6 &
  p.6 = x7 & p.7 = x8 by A1,A3,A4,A5,A6,Def4,A2;
  thus p.8 = p.len p17 by Th91
    .= x9 by A1,Th40;
end;

:: K.P. 12.2009

theorem :: FINSEQ_2:7
   n <len p implies (p^q).n=p.n
proof
  assume n <len p;
  then n in dom p by NAT_1:45;
  hence thesis by Def4;
end;

theorem :: FINSEQ_2:10
  len p <= n implies (p|n) = p
proof
  assume len p<=n;
  then (len p) /\ n=len p by NAT_1:47;
  hence thesis by RELAT_1:97,XBOOLE_1:18;
end;

theorem Th111: :: FINSEQ_1:11
  n <=len p & k in n
  implies (p|n).k = p.k & k in dom p
proof
  assume that
A1: n <=len p and
A2: k in n;
A3: n c= dom p by A1,NAT_1:40;
  then n = dom p /\ n by XBOOLE_1:28
    .= dom(p|n) by RELAT_1:90;
  hence thesis by A2,A3,FUNCT_1:70;
end;

theorem Th121: :: FINSEQ_1:12
  n <= len p implies len(p|n) = n
proof
  assume n <= len p;
  then n c= (len p) by NAT_1:40;
  hence thesis by RELAT_1:91;
end;

theorem :: FINSEQ_1:13
  len(p|n) <= n
proof
  dom (p|n) c= n by RELAT_1:87;
  hence thesis by NAT_1:40;
end;

theorem TTT: :: FINSEQ_1:14
  len p = n+1 implies p = (p|n) ^ <% p.n %>
proof
  set pn = p|n;
  set x=p.n;
  assume
A1: len p = n+1;
then A3: n < len p by NAT_1:13;
then A2: len pn = n by Th121;
A4: now
    let m be Nat;
    assume m in dom p;
    then m<len p by NAT_1:45;
    then
A5: m <= len pn by A1,A2,NAT_1:13;
    now
      per cases;
      case
        m = len pn;
        hence p.m = (pn^<%x%>).m by A2,Th40;
      end;
      case
        m <> len pn;
        then m< len pn by A5,XXREAL_0:1;
        then
A6:     m in dom pn by NAT_1:45;
        hence (pn^<%x%>).m = pn.m by Def4
          .= p.m by A3,A2,A6,Th111;
      end;
    end;
    hence p.m = (pn^<%x%>).m;
  end;
  len (pn^<%x%>) = n + len <%x%> by A2,Def4
    .= len p by A1,Def5;
  hence thesis by A4,Th10;
end;

theorem Th1: :: CATALAN2:1
  (p^q)|dom p = p
proof
    set r=(p^q)|(dom p);
A1: now
    let k such that
A2: k < len p;
A3: k in dom p by A2,NAT_1:45;
    then
A4: (p^q).k=p.k by Def4;
    k+0<len p+len q by A2,XREAL_1:10;
    then k in len p+len q by NAT_1:45;
    then k in dom (p^q) by Def4;
    then k in dom (p^q)/\ dom p by A3,XBOOLE_0:def 4;
    hence r.k=p.k by A4,FUNCT_1:71;
  end;
  dom p c= dom (p^q) by Th24;
  then len r= len p by RELAT_1:91;
  hence thesis by A1,Th11;
end;

theorem :: CATALAN2:2
  n <= dom p implies (p^q)|n = p|n
proof
  assume n <= dom p;
  then n c= dom p by NAT_1:40;
  then ((p^q)|dom p)|n=(p^q)|n by RELAT_1:103;
  hence thesis by Th1;
end;

theorem :: CATALAN2:3
  n = dom p + k implies (p^q)|n = p^(q|k)
proof
  assume
A1: n = dom p + k;
  now
    per cases;
    suppose
A2:   n>=len (p^q);
      then n>=len p+len q by Def4;
      then k >= len q by A1,XREAL_1:10;
      then dom q c= k by NAT_1:40;
      then
A3:   q|k = q by RELAT_1:97;
      dom (p^q) c= n by A2,NAT_1:40;
      hence thesis by A3,RELAT_1:97;
    end;
    suppose
A4:   n<len (p^q);
      then
A5:   len ((p^q)|n)=n by Th13;
      n<len p+len q by A4,Def4;
      then k < len q by A1,XREAL_1:8;
      then len (q|k)=k by Th13;
      then
A6:   len (p^(q|k))=len p + k by Def4;
      now
        let m be Nat such that
A7:     m in dom ((p^q)|n);
B1:  m < len ((p^q)|n) by A7,NAT_1:45;then
        m <len (p^q) by A4,A5,XXREAL_0:2;
        then
A8:     m in len (p^q) by NAT_1:45;
        m in n by A4,Th13,A7;
        then
A9:     m in dom (p^q) /\ n by A8,XBOOLE_0:def 4;
        then
A10:    ((p^q)|n).m=(p^q).m by FUNCT_1:71;
        now
          per cases;
          suppose
            m<dom p;
            then m in dom p by NAT_1:45;
            then (p^(q|k)).m=p.m & (p^q).m=p.m by Def4;
            hence ((p^q)|n).m=(p^(q|k)).m by A9,FUNCT_1:71;
          end;
          suppose
A11:        m>=dom p;
            m < len (p^q) by A4,A5,XXREAL_0:2,B1;
            then
A12:        q.(m-len p)=(p^q).m by A11,Th22;
A13:        m-len p+len p< len (p^q) by A4,A5,XXREAL_0:2,B1;
A14:        m-len p is Nat by A11,NAT_1:21;
            len (p^q)=len p+len q by Def4;
            then m-len p<len q by A13,XREAL_1:8;
            then
A15:        m-len p in len q by A14,NAT_1:45;
            m-len p < k by A1,A5,A13,XREAL_1:8,B1;
            then m-len p in k by A14,NAT_1:45;
            then
A16:        m-len p in k/\dom q by A15,XBOOLE_0:def 4;
            (p^(q|k)).m=(q|k).(m-len p) by A1,A6,A5,A11,B1,Th22;
            hence ((p^q)|n).m=(p^(q|k)).m by A10,A12,A16,FUNCT_1:71;
          end;
        end;
        hence ((p^q)|n).m=(p^(q|k)).m;
      end;
      hence thesis by Th10,A6,A1,A4,Th13;
    end;
  end;
  hence thesis;
end;

theorem :: CATALAN2:4
  ex q st p = (p|n)^q
proof
  now
    per cases;
    suppose
      n > len p;
      then len p c= n by NAT_1:40;
      then
A1:   p|n=p by RELAT_1:97;
      p^{}=p by Th32;
      hence thesis by A1;
    end;
    suppose
      n <= len p;
      then reconsider n1=len p-n as Element of NAT by NAT_1:21;
      defpred P[Nat] means for k st k= len p-$1 holds ex q st p=(p|k)^q;
A2:   for m be Nat st P[m] holds P[m+1]
      proof
        let m be Nat such that
A3:     P[m];
        let k such that
A4:     k = len p-(m+1);
        consider q such that
A5:     p=(p|(k+1))^q by A3,A4;
        k<=k+1 by NAT_1:11;
        then k c= k+1 by NAT_1:40;
        then
A6:     (p|(k+1))|k =p|k by RELAT_1:103;
         len p-m<=len p-0 by XREAL_1:12;
         then k+1=len p & dom (p | len p)=len p or k+1<len p by A4,
         RELAT_1:97,XXREAL_0:1;
    then len (p | (k+1)) = k+1 by Th121;
        then p|(k+1)=(p|(k+1))|k^<%(p|(k+1)).k%> by TTT;
        then p=(p|k)^(<%(p|(k+1)).k%>^q) by A5,A6,Th30;
        hence thesis;
      end;
      p|(len p-0)=p & p^{}=p by Th32,RELAT_1:97;
      then
A9:   P[0];
A10:      for m be Nat holds P[m] from NAT_1:sch 2(A9,A2);
      n=len p-n1;
      hence thesis by A10;
    end;
  end;
  hence thesis;
end;

theorem :: FLANG_1:10
 len p = n + k implies ex q1, q2 being
   XFinSequence st len q1 = n & len q2 = k & p = q1 ^ q2
proof
  defpred P[Nat] means for p being XFinSequence, i, j be Nat
st len p = $1 & len p =
  i + j ex q1, q2 being XFinSequence st len q1 = i & len q2 = j & p = q1 ^ q2;
A1: now
    let n;
    assume
A2: P[n];
    thus P[n + 1]
    proof
      let p be XFinSequence;
      let i, j be Nat;
      assume that
A3:   len p = n + 1 and
A4:   len p = i + j;
      per cases;
      suppose
A5:     j = 0;
        take q1 = p;
        take q2 = {};
        thus thesis by A4,A5,Th32;
      end;
      suppose
        j > 0;
        then consider k such that
A6:     j = k + 1 by NAT_1:6;
        p <> {} by A3;
        then consider q being XFinSequence, x such that
A7:     p = q ^ <%x%> by Tex;
A8:     n + 1 = len q + len <%x%> by A3,A7,Def4
          .= len q + 1 by Th36;
        n = i + k by A3,A4,A6;
        then consider q1, q2 being XFinSequence such that
A9:     len q1 = i and
A10:    len q2 = k and
A11:    q = q1 ^ q2 by A2,A8;
A12:    len (q2 ^ <%x%>) = len q2 + len <%x%> by Def4
          .= j by A6,A10,Th36;
        p = q1 ^ (q2 ^ <%x%>) by A7,A11,Th30;
        hence thesis by A9,A12;
      end;
    end;
  end;
A13: P[0]
  proof
    let p be XFinSequence;
    let i, j be Nat;
    assume that
A14: len p = 0 and
A15: len p = i + j;
    p = {} by A14;
    then
A16: p = {} ^ {} by Th32;
    len {} = i by A14,A15;
    hence thesis by A14,A15,A16;
  end;
  for n holds P[n] from NAT_1:sch 2(A13, A1);
  hence thesis;
end;

theorem :: FSM_3:6
   <%x%>^p = <%y%>^q implies x = y & p = q
proof
  assume A1: <%x%>^p = <%y%>^q;
  (<%x%>^p).0 = x by Th39;
  then x = y by A1,Th39;
  hence thesis by A1,Th31;
end;

definition
  let D be set,q be FinSequence of D;
  func FS2XFS q -> XFinSequence of D means :Def1:
  len it=len q & for i being Nat st i < len q holds q.(i+1)=it.i;
  existence
  proof
    deffunc F(Nat) =q.($1 +1);
    ex p being XFinSequence st len p = len q & for k be Nat
    st k in len q holds p.k=F(k) from XSeqLambda;
    then consider p being XFinSequence such that
A1: len p = len q and
A2: for k be Nat st k in len q holds p.k=F(k);
    rng p c= D
    proof
      let y be set;
A3:   rng q c= D by FINSEQ_1:def 4;
      assume y in rng p;
      then consider x being set such that
A4:   x in dom p and
A5:   y=p.x by FUNCT_1:def 5;
      reconsider nx=x as Element of NAT by A4;
      nx<len q by A1,A4,NAT_1:45;
      then
A6:   nx+1<=len q by NAT_1:13;
      0+1<=nx+1 by NAT_1:13;
      then nx+1 in Seg len q by A6,FINSEQ_1:3;
      then nx+1 in dom q by FINSEQ_1:def 3;
      then
A7:   q.(nx+1) in rng q by FUNCT_1:def 5;
      p.nx= q.(nx +1) by A1,A2,A4;
      hence thesis by A5,A7,A3;
    end;
    then
A8: p is XFinSequence of D by RELAT_1:def 19;
    for i being Nat st i<len q holds q.(i+1)=p.i
    proof
      let i be Nat;
      assume i<len q;
      then i in NAT & i in len q by NAT_1:45,ORDINAL1:def 13;
      hence thesis by A2;
    end;
    hence thesis by A1,A8;
  end;
  uniqueness
  proof
    thus for p1,p2 being XFinSequence of D st
    (len p1=len q & for i be Nat st i<len q holds
    q.(i+1)=p1.i)& (len p2=len q & for i be Nat
    st i<len q holds q.(i+1)=p2.i) holds
    p1=p2
    proof
      let p1,p2 be XFinSequence of D;
      assume that
A9:   len p1=len q and
A10:  for i be Nat st i<len q holds q.(i+1)=p1.i and
A11:  len p2=len q and
A12:  for i be Nat st i<len q holds q.(i+1)=p2.i;
      for i be Nat st i<len p1 holds p1.i=p2.i
      proof
        let i be Nat;
        assume
A13:    i<len p1;
        then q.(i+1)=p1.i by A9,A10;
        hence thesis by A9,A12,A13;
      end;
      hence thesis by A9,A11,Th11;
    end;
  end;
end;

reserve i for Nat;

definition
  let D be set,q be XFinSequence of D;
  func XFS2FS q -> FinSequence of D means
  len it=len q & for i be Nat st 1<=i & i<= len q holds q.(i-'1)=it.i;
  existence
  proof
    deffunc F(Nat) = q.($1-'1);
    ex p being FinSequence st len p = len q &
    for k being Nat st k in dom p holds p.k=F(k) from FINSEQ_1:sch 2;
    then consider p being FinSequence such that
A1: len p = len q and
A2: for k being Nat st k in dom p holds p.k=F(k);
    rng p c= D
    proof
      let y be set;
A3:   rng q c= D by RELAT_1:def 19;
      assume y in rng p;
      then consider x being set such that
A4:   x in dom p and
A5:   y=p.x by FUNCT_1:def 5;
      reconsider nx=x as Element of NAT by A4;
A6:   nx in Seg len q by A1,A4,FINSEQ_1:def 3;
      then 1<=nx by FINSEQ_1:3;
      then nx-1>=0 by XREAL_1:50;
      then
A7:   nx-1=nx-'1 by XREAL_0:def 2;
A8:   nx-'1<nx-'1+1 by NAT_1:13;
      nx<=len q by A6,FINSEQ_1:3;
      then nx-'1<len q by A7,A8,XXREAL_0:2;
      then nx-'1 in dom q by NAT_1:45;
      then
A9:   q.(nx-'1) in rng q by FUNCT_1:def 5;
      p.nx= q.(nx -'1) by A2,A4;
      hence thesis by A5,A9,A3;
    end;
    then
A10: p is FinSequence of D by FINSEQ_1:def 4;
A11: dom p = Seg len q by A1,FINSEQ_1:def 3;
    for i be Nat st 1<=i & i<=len q holds q.(i-'1)=p.i
    proof
      let i be Nat;
      assume 1<=i & i<=len q;
      then i in Seg len q by FINSEQ_1:3;
      hence thesis by A2,A11;
    end;
    hence thesis by A1,A10;
  end;
  uniqueness
  proof
    thus for p1,p2 being FinSequence of D st (len p1=len q & for i st 1<=i & i
<=len q holds q.(i-'1)=p1.i)& (len p2=len q & for i st 1<=i & i<=len q holds q.
    (i-'1)=p2.i) holds p1=p2
    proof
      let p1,p2 be FinSequence of D;
      assume that
A12:  len p1=len q and
A13:  for i st 1<=i & i<=len q holds q.(i-'1)=p1.i and
A14:  len p2=len q and
A15:  for i st 1<=i & i<=len q holds q.(i-'1)=p2.i;
      for i be Nat st 1<=i & i<=len p1 holds p1.i=p2.i
      proof
        let i be Nat;
        assume
A16:    1<=i & i<=len p1;
        then q.(i-'1)=p1.i by A12,A13;
        hence thesis by A12,A15,A16;
      end;
      hence thesis by A12,A14,FINSEQ_1:18;
    end;
  end;
end;

theorem Th4:
  for D being set, n being Nat, r being set st r in D holds
  (n-->r) is XFinSequence of D
proof
  let D be set, n be Nat, r be set;
  reconsider p=(n-->r) as XFinSequence by Th3;
  assume
A1: r in D;
  now
    per cases;
    case
      n<>0;
      then
A2:   rng p={r} by FUNCOP_1:14;
      rng p c= D
      proof
        let x be set;
        assume x in rng p;
        hence thesis by A1,A2,TARSKI:def 1;
      end;
      hence thesis by RELAT_1:def 19;
    end;
    case
      n=0;
      then (n-->r)={};
      hence thesis by Th19;
    end;
  end;
  hence thesis;
end;

definition
  let D be non empty set;
  let q be FinSequence of D, n be Nat;
  assume that
A1: n>len q and
A2: NAT c= D;
  func FS2XFS*(q,n) -> non empty XFinSequence of D means
  len q = it.0 &
  len it=n & (for i be Nat st 1<=i & i<= len q holds it.i=q.i)&
  for j being Nat st len q
  <j & j<n holds it.j=0;
  existence
  proof
    len q in NAT;
    then reconsider x=len q as Element of D by A2;
    0 in NAT;
    then reconsider r=0 as Element of D by A2;
    reconsider q5= ((n-'len q-'1)-->r) as XFinSequence of D by Th4;
    <%x%> ^ (FS2XFS q) <>{} by Th33;
    then reconsider
    p0=<%x%> ^ (FS2XFS q)^q5 as non empty XFinSequence of D by Th33;
    0 in 1 by NAT_1:45;
    then
A3: 0 in dom (<%x%>) by Def5;
A4: len <%x%>=1 by Def5;
    0 in len <%x%> + len (FS2XFS q) by NAT_1:45;
    then 0 in len (<%x%> ^ (FS2XFS q)) by Th20;
    then
A5: p0.0=(<%x%> ^ (FS2XFS q)).0 by Def4
      .=(<%x%>).0 by A3,Def4
      .=x by Def5;
A6: for i st 1<=i & i<= len q holds p0.i=q.i
    proof
      let i;
      assume that
A7:   1<=i and
A8:   i<= len q;
      i-1>=0 by A7,XREAL_1:50;
      then
A9:   i-'1=i-1 by XREAL_0:def 2;
      i<i+1 by NAT_1:13;
      then i-1<i+1-1 by XREAL_1:11;
      then
A10:  i-'1 <len q by A8,A9,XXREAL_0:2;
      then i-'1 in len q by NAT_1:45;
      then
A11:  i-'1 in len (FS2XFS q) by Def1;
      i<1+len q by A8,NAT_1:13;
      then i< (len (<%x%>)+len (FS2XFS q)) by A4,Def1;
      then i in (len (<%x%>)+len (FS2XFS q)) by NAT_1:45;
      then i in len (<%x%> ^ (FS2XFS q)) by Th20;
      then p0.i =(<%x%>^(FS2XFS q)).(1+(i-'1)) by A9,Def4
        .=(FS2XFS q).(i-'1) by A4,A11,Def4
        .=q.(i-'1+1) by A10,Def1
        .=q.i by A9;
      hence thesis;
    end;
A12: n-len q>0 by A1,XREAL_1:52;
    then
A13: n-'len q=n-len q by XREAL_0:def 2;
    then n-'len q>=0+1 by A12,NAT_1:13;
    then
A14: n-'len q -1>=0 by XREAL_1:50;
A15: len q5=(n-'len q-'1) by FUNCOP_1:19;
A16: for j being Nat st len q<j & j<n holds p0.j=0
    proof
      let j be Nat;
      assume that
A17:  len q<j and
A18:  j<n;
A19:  len (<%x%> ^ (FS2XFS q)) =len (<%x%>) + len (FS2XFS q) by Th20
        .=1+len q by A4,Def1;
      len q<n by A17,A18,XXREAL_0:2;
      then
A20:  n-len q>0 by XREAL_1:52;
      then
A21:  n-'len q=n-len q by XREAL_0:def 2;
      then n-len q>=0+1 by A20,NAT_1:13;
      then n-'len q-1>=0 by A21,XREAL_1:50;
      then
A22:  n-'len q-'1 =n-(len q+1) by A21,XREAL_0:def 2;
      1+len q<=j by A17,NAT_1:13;
      then j-(1+len q)>=0 by XREAL_1:50;
      then
A23:  j-'(1+len q)=j-(1+len q) by XREAL_0:def 2;
      j-(len q+1)< n-(len q+1) by A18,XREAL_1:11;
      then
A24:  j-'len (<%x%> ^ (FS2XFS q)) in (n-'len q-'1) by A19,A23,A22,NAT_1:45;
      j =len (<%x%> ^ (FS2XFS q))+(j-'len (<%x%> ^ (FS2XFS q))) by A19,A23;
      then p0.j=q5.(j-'len (<%x%> ^ (FS2XFS q))) by A15,A24,Def4
        .=0 by A24,FUNCOP_1:13;
      hence thesis;
    end;
    len p0=len (<%x%> ^ (FS2XFS q)) + len q5 by Th20
      .=len <%x%> + len (FS2XFS q) + len q5 by Th20
      .= 1 + len (FS2XFS q) + len q5 by Th36
      .=1 + len q + len q5 by Def1
      .=1+len q+(n-'len q-'1) by FUNCOP_1:19
      .=(n-(len q+1))+(len q+1) by A13,A14,XREAL_0:def 2
      .=n;
    hence thesis by A5,A6,A16;
  end;
  uniqueness
  proof
    let p1,p2 be non empty XFinSequence of D;
    assume that
A25: len q = (p1.0) and
A26: len p1=n and
A27: for i st 1<=i & i<= len q holds p1.i=q.i and
A28: for j being Nat st len q<j & j<n holds p1.j=0 and
A29: len q = (p2.0) and
A30: len p2=n and
A31: for i st 1<=i & i<= len q holds p2.i=q.i and
A32: for j being Nat st len q<j & j<n holds p2.j=0;
    for i be Nat st i<n holds p1.i=p2.i
    proof
      let i be Nat;
      assume i<n; then
A33:  i<0+1 or 1<=i & i<=len q or len q<i & i<n;
      now
        per cases by A33,NAT_1:13;
        case i=0;
          hence thesis by A25,A29;
        end;
        case
A34:      1<=i & i<=len q;
          then p1.i=q.i by A27;
          hence thesis by A31,A34;
        end;
        case
A35:      len q<i & i<n;
          then p1.i=0 by A28;
          hence thesis by A32,A35;
        end;
      end;
      hence thesis;
    end;
    hence thesis by A26,A30,Th11;
  end;
end;

reserve m for Nat,
        D for non empty set;

definition
  let D be non empty set;
  let p be XFinSequence of D;
  assume that
A1: p.0 is Nat and
A2: p.0 in len p;
  func XFS2FS*(p) -> FinSequence of D means :Def4b:
  for m be Nat st m = p.0 holds
  len it =m & for i st 1<=i & i<= m holds it.i=p.i;
  existence
  proof
    reconsider m0=p.0 as Element of NAT by A1,ORDINAL1:def 13;
    deffunc F(set)= p.$1;
    ex q being FinSequence st len q = m0 & for k being Nat st k in dom q
    holds q.k=F(k) from FINSEQ_1:sch 2;
    then consider q being FinSequence such that
A3: len q = m0 and
A4: for k being Nat st k in dom q holds q.k=F(k);
    rng q c= D
    proof
A5:   m0 < len p by A2,NAT_1:45;
      let y be set;
      assume y in rng q;
      then consider x being set such that
A6:   x in dom q and
A7:   y=q.x by FUNCT_1:def 5;
      reconsider k0=x as Element of NAT by A6;
      k0 in Seg m0 by A3,A6,FINSEQ_1:def 3;
      then k0<=m0 by FINSEQ_1:3;
      then k0 < len p by A5,XXREAL_0:2;
      then
A8:   k0 in dom p by NAT_1:45;
      y=p.k0 by A4,A6,A7;
      then rng p c= D & y in rng p by A8,FUNCT_1:def 5,RELAT_1:def 19;
      hence thesis;
    end;
    then reconsider q0=q as FinSequence of D by FINSEQ_1:def 4;
A9: dom q = Seg m0 by A3,FINSEQ_1:def 3;
    for m be Nat st
    m = (p.0) holds len q0 =m & for i st 1<=i & i<= m holds q0.i =p.i
    proof
      let m be Nat;
A10:  for i st 1<=i & i<= m0 holds q0.i=p.i
      proof
        let i;
        assume 1<=i & i<= m0;
        then i in Seg m0 by FINSEQ_1:3;
        hence thesis by A4,A9;
      end;
      assume m = p.0;
      hence thesis by A3,A10;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    reconsider m2=p.0 as Nat by A1;
    let g1,g2 be FinSequence of D;
    assume that
A11: for m st m = p.0 holds len g1 =m & for i st 1<=i & i<= m holds g1
    .i=p. i and
A12: for m st m = p.0 holds len g2 =m & for i st 1<=i & i<= m holds g2
    . i=p.i;
A13: len g1=m2 by A11;
A14: for i be Nat st 1<=i & i<=len g1 holds g1.i=g2.i
    proof
      let i be Nat;
      assume
A15:  1<=i & i<=len g1;
      then g1.i=p.i by A11,A13;
      hence thesis by A12,A13,A15;
    end;
    len g2=m2 by A12;
    hence thesis by A11,A14,FINSEQ_1:18;
  end;
end;

theorem
  for p being XFinSequence of D st p.0=0 & 0<len p holds
  XFS2FS*(p)={}
proof
  let p be XFinSequence of D;
  assume that
A1: p.0=0 and
A2: 0<len p;
  set q= XFS2FS*(p);
  0 in len p by A2,NAT_1:45;
  then len q=0 by A1,Def4b;
  then q=<*>D;
  hence thesis;
end;

:: from SCMNORM, 2010.01.11, A.T.

definition
  let F be Function;
  attr F is initial means
:Def13:
  for m,n being Nat st n in dom F & m < n holds m in dom F;
end;

registration
  cluster empty -> initial Function;
  coherence
  proof
    let F be Function such that
A1: F is empty;
    let n be Nat;
    thus thesis by A1;
  end;
end;

registration
  cluster -> initial XFinSequence;
  coherence
  proof
    let p be XFinSequence;
    let m,n being Nat such that
A1: n in dom p;
    assume m < n;
    then m in n by NAT_1:45;
    hence m in dom p by A1,ORDINAL1:19;
  end;
end;

:: following, 2010.01.11, A.T.

registration
 cluster -> NAT-defined XFinSequence;
 coherence
  proof let f be XFinSequence;
   thus dom f c= NAT;
  end;
end;

theorem Th69:
 for F being non empty initial NAT-defined Function holds 0 in dom F
proof
 let F be non empty initial NAT-defined Function;
  consider x such that
W: x in dom F by XBOOLE_0:def 1;
  dom F c= NAT by RELAT_1:def 18;
  then reconsider x as Element of NAT by W;
  x = 0 or 0 < x;
 hence 0 in dom F by W,Def13;
end;

registration
 cluster initial finite NAT-defined -> T-Sequence-like Function;
 coherence
  proof let F be Function;
   assume
Z0:   F is initial finite NAT-defined;
    then
XX:   dom F c= NAT by RELAT_1:def 18;
   thus dom F is epsilon-transitive
    proof let x;
     assume
Z1:    x in dom F;
      then reconsider i = x as Nat by XX;
     let y;
     assume
Z2:   y in x;
      x in NAT by Z1,XX;
      then x c= NAT by ORDINAL1:def 2;
      then y in NAT by Z2;
      then reconsider j = y as Nat;
      j < i by Z2,NAT_1:45;
     hence y in dom F by Z0,Z1,Def13;
    end;
  let x,y;
  assume x in dom F & y in dom F;
    then reconsider x,y as Ordinal by XX;
    x in y or x = y or y in x by ORDINAL1:24;
   hence thesis;
  end;
end;

theorem Th70:
 for F being finite initial NAT-defined Function
 for n being Nat holds
  n in dom F iff n < card F by NAT_1:45;

:: from AMISTD_2, 2010.04.16, A.T.

theorem
  for F being initial NAT-defined Function,
  G being NAT-defined Function st dom F = dom G holds G is initial
proof
  let F be initial NAT-defined Function,
  G be NAT-defined Function;
  assume dom F = dom G;
  hence for m,l being Nat st l in dom G & m < l holds m in dom G by Def13;
end;

theorem
  for F being initial NAT-defined finite Function
  holds dom F = { k where k is Element of NAT: k < card F }
proof
  let F be initial NAT-defined finite Function;
  hereby
    let x be set;
    assume
A2: x in dom F;
    then reconsider f = x as Element of NAT;
    f < card F by A2,Th70;
    hence x in { k where k is Element of NAT: k < card F };
  end;
  let x be set;
  assume x in { k where k is Element of NAT: k < card F };
  then ex k being Element of NAT st x = k & k < card F;
  hence thesis by Th70;
end;

theorem
  for F being initial non empty NAT-defined finite Function, G being non
  empty NAT-defined finite Function holds F c= G & LastLoc F = LastLoc G
  implies F = G
proof
  let F be initial non empty NAT-defined finite Function, G be non empty NAT
  -defined finite Function such that
A1: F c= G and
A2: LastLoc F = LastLoc G;
  dom F = dom G
  proof
    thus dom F c= dom G by A1,GRFUNC_1:8;
    let x be set;
    assume
A3: x in dom G;
    dom G c= NAT by RELAT_1:def 18;
    then reconsider x as Element of NAT by A3;
A4: LastLoc F in dom F by VALUED_1:31;
    x <= LastLoc F by A2,A3,VALUED_1:33;
    then x < LastLoc F or x = LastLoc F by XXREAL_0:1;
    hence thesis by A4,Def13;
  end;
  hence thesis by A1,GRFUNC_1:9;
end;

theorem
  for F being initial non empty NAT-defined finite Function holds
  LastLoc F = card F -' 1
proof
  let F be initial non empty NAT-defined finite Function;
  consider k being natural number such that
A1: LastLoc F = k;
  reconsider k as Element of NAT by ORDINAL1:def 13;
  LastLoc F in dom F by VALUED_1:31;
  then k < card F by A1,Th70;
  then
A2: k <= card F -' 1 by NAT_D:49;
  per cases by A2,XXREAL_0:1;
  suppose
    k < card F -' 1;
    then k+1 < card F -' 1 + 1 by XREAL_1:8;
    then k+1 < card F by NAT_1:14,XREAL_1:237;
    then k+1 in dom F by Th70;
    then
A3: k+1 <= k by A1,VALUED_1:33;
    k <= k+1 by NAT_1:11;
    then k+0 = k+1 by A3,XXREAL_0:1;
    hence thesis;
  end;
  suppose
    k = card F -' 1;
    hence thesis by A1;
  end;
end;

theorem
  for F being initial non empty NAT-defined finite Function holds
  FirstLoc F = 0
proof
  let F be initial non empty NAT-defined finite Function;
  0 in dom F by Th69;
  then FirstLoc F <= 0 by VALUED_1:36;
  hence thesis;
end;

registration
  let F be initial non empty NAT-defined finite Function;
  cluster CutLastLoc F -> initial;
  coherence
  proof
    set G = CutLastLoc F;
    per cases;
    suppose G is empty;
      then reconsider H = G as empty finite Function;
      H is initial;
      hence thesis;
    end;
    suppose G is non empty;
      then reconsider G as non empty finite Function;
      G is initial
      proof
        let m,l be Nat such that
A1:     l in dom G and
A2:     m < l;
        set M = dom F;
        reconsider R = {[LastLoc F, F.LastLoc F]} as Relation;
        R = LastLoc F .--> (F.LastLoc F) by FUNCT_4:87;
        then
A5:     dom R = {LastLoc F} by FUNCOP_1:19;
        then
A6:     dom F \ dom R = dom G by VALUED_1:37;
        then
       l in dom F by A1,XBOOLE_0:def 5;
        then
A7:     m in dom F by A2,Def13;
        l in M by A6,A1,XBOOLE_0:def 5;
        then m <> LastLoc F by A2,XXREAL_2:def 8;
        then not m in {LastLoc F} by TARSKI:def 1;
        hence thesis by A5,A6,A7,XBOOLE_0:def 5;
      end;
      hence thesis;
    end;
  end;
end;

reserve l for Element of NAT;

theorem
 for I being finite initial NAT-defined Function, J being Function
 holds dom I misses dom Shift(J,card I)
proof let I be finite initial NAT-defined Function, J be Function;
  assume
A1: dom I meets dom Shift(J,card I);
  dom Shift(J,card I) = { l+card I: l in dom J } by VALUED_1:def 12;
  then consider x being set such that
A2: x in dom I and
A3: x in { l+card I: l in dom J } by A1,XBOOLE_0:3;
  consider l such that
A4: x = l+card I and
  l in dom J by A3;
  l+card I < card I by A2,A4,Th70;
  hence contradiction by NAT_1:11;
end;

:: from SCMPDS_4, 2010.05.14, A.T.

theorem
  not m in dom p implies not succ m in dom p
proof
  assume not m in dom p;
  then
A1: m >= card p by Th70;
  m+1 >= m by NAT_1:11;
  then m+1 >= card p by A1,XXREAL_0:2;
  hence thesis by Th70;
end;

:: from SCM_COMP, 2010.05.16, A.T.

registration let D be set;
 cluster D^omega -> functional;
 coherence
  proof
   let x be set;
   assume x in D^omega;
   hence x is Function by Def8;
  end;
end;

registration let D be set;
  cluster -> finite T-Sequence-like Element of D^omega;
  coherence by Def8;
end;

definition let D be set;
 let f be XFinSequence of D;
 func Down f -> Element of D^omega equals
 f;
 coherence by Def8;
end;

definition let D be set;
 let f be XFinSequence of D, g be Element of D^omega;
 redefine func f^g -> Element of D^omega;
 coherence
  proof
    reconsider g as XFinSequence of D by Def8;
    f^g is XFinSequence of D;
   hence thesis by Def8;
  end;
end;

definition let D be set;
 let f, g be Element of D^omega;
 redefine func f^g -> Element of D^omega;
 coherence
  proof
    reconsider f,g as XFinSequence of D by Def8;
    f^g is XFinSequence of D;
   hence thesis by Def8;
  end;
end;

:: missing, 2010.05.15, A.T.

theorem Th78:
 p c= p^q
proof
A: dom p c= dom(p^q) by Th24;
  for x st x in dom p holds (p^q).x = p.x
  proof let x;
   assume
Z:  x in dom p;
    then reconsider x as Nat;
    (p^q).x = p.x by Z,Def4;
   hence thesis;
  end;
 hence thesis by A,GRFUNC_1:8;
end;

theorem
  len(p^<%x%>) = len p + 1
 proof
  thus len(p^<%x%>) = len p + len<%x%> by Th20
      .= len p + 1 by Th36;
 end;

theorem
 <%x,y%> = (0,1) --> (x,y)
proof
A: dom<%x,y%> = len<%x,y%>
    .= {0,1} by Th42,CARD_1:88;
B: <%x,y%>.0 = x by Th42;
  <%x,y%>.1 = y by Th42;
 hence <%x,y%> = (0,1) --> (x,y) by A,B,FUNCT_4:69;
end;

reserve M for Element of NAT;

theorem Th81:
 p^q = p +* Shift(q, card p)
proof
F: dom Shift(q, card p) = { M+card p:M in dom q } by VALUED_1:def 12;
   for x holds x in dom(p^q) iff x in dom p or x in dom Shift(q, card p)
    proof let x;
     thus x in dom(p^q) implies x in dom p or x in dom Shift(q, card p)
      proof assume
ZZ:     x in dom(p^q);
        then reconsider k = x as Nat;
       per cases by ZZ,Th23;
       suppose k in dom p;
       hence x in dom p or x in dom Shift(q, card p);
       end;
       suppose ex n st n in dom q & k=len p + n;
       hence x in dom p or x in dom Shift(q, card p) by F;
       end;
      end;
     assume
Z:    x in dom p or x in dom Shift(q, card p);
     per cases by Z;
     suppose
S:    x in dom p;
      dom p c= dom(p^q) by Th24;
     hence x in dom(p^q) by S;
     end;
     suppose x in dom Shift(q, card p);
      then ex M st x = M+card p & M in dom q by F;
     hence x in dom(p^q) by Th26;
     end;
    end;
   then
A: dom(p^q) = dom p \/ dom Shift(q, card p) by XBOOLE_0:def 3;
  for x st x in dom p \/ dom Shift(q, card p)
   holds (x in dom Shift(q, card p) implies (p^q).x = Shift(q, card p).x) &
    (not x in dom Shift(q, card p) implies (p^q).x = p.x)
  proof let x such that
Z:   x in dom p \/ dom Shift(q, card p);
   hereby assume
ZZ:  x in dom Shift(q, card p);
     then reconsider k = x as Nat;
     consider M such that
W1:   x = M+card p and
W2:   M in dom q by ZZ,F;
     set m = k -' len p;
A: len p + m = k by NAT_D:34,W1;
    hence (p^q).x = q.m by W1,W2,Def4
       .= Shift(q, card p).x by W1,W2,A,VALUED_1:def 12;
   end;
   assume not x in dom Shift(q, card p);
    then x in dom p by Z,XBOOLE_0:def 3;
   hence (p^q).x = p.x by Def4;
  end;
 hence p^q = p +* Shift(q, card p) by A,FUNCT_4:def 1;
end;

theorem
 p +* (p ^ q) = p ^ q & (p ^ q) +* p = p ^ q
proof
  p c= p ^ q by Th78;
 hence thesis by FUNCT_4:79;
end;

reserve m,n for Element of NAT;

theorem Th83:
 for I being finite initial NAT-defined Function, J being Function
 holds dom Shift(I,n) misses dom Shift(J,n+card I)
proof let I be finite initial NAT-defined Function, J be Function;
  assume
A1: dom Shift(I,n) meets dom Shift(J,n+card I);
  dom Shift(J,n+card I) = { l+(n+card I): l in dom J } by VALUED_1:def 12;
  then consider x being set such that
A2: x in dom Shift(I,n) and
A3: x in { l+(n+card I): l in dom J } by A1,XBOOLE_0:3;
 dom Shift(I,n) = { m+n:m in dom I } by VALUED_1:def 12;
  then consider m such that
W1: x = m+n and
W2: m in dom I by A2;
  consider l such that
A4: x = l+(n+card I) and
  l in dom J by A3;
  m < card I by W2,Th70;
  then m+n < n+card I by XREAL_1:8;
  then l+(n+card I) < n+card I by W1,A4;
  hence contradiction by NAT_1:11;
end;

theorem Th84:
 Shift(p,n) c= Shift(p^q,n)
 proof
    p^q = p +* Shift(q, card p) by Th81;
    then
A:   Shift(p^q,n) = Shift(p,n) +* Shift(Shift(q,card p),n) by VALUED_1:24;
    Shift(Shift(q,card p),n) = Shift(q,n+card p) by VALUED_1:22;
    then dom Shift(p,n) misses dom Shift(Shift(q,card p),n) by Th83;
   hence Shift(p,n) c=  Shift(p^q,n) by A,FUNCT_4:33;
 end;

theorem Th85:
 Shift(q,n+card p) c= Shift(p^q,n)
 proof
A: Shift(Shift(q,card p),n) = Shift(q,n+card p) by VALUED_1:22;
    p^q = p +* Shift(q, card p) by Th81;
    then Shift(p^q,n) = Shift(p,n) +* Shift(Shift(q,card p),n) by VALUED_1:24;
   hence thesis by A,FUNCT_4:26;
 end;

theorem
 Shift(p^q,n) c= X implies Shift(p,n) c= X
 proof assume
Z: Shift(p^q,n) c= X;
   Shift(p,n) c= Shift(p^q,n) by Th84;
  hence Shift(p,n) c= X by Z,XBOOLE_1:1;
 end;

theorem
 Shift(p^q,n) c= X implies Shift(q,n+card p) c= X
 proof assume
Z: Shift(p^q,n) c= X;
   Shift(q,n+card p) c= Shift(p^q,n) by Th85;
  hence thesis by Z,XBOOLE_1:1;
 end;

