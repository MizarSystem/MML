:: On the Representation of Natural Numbers in Positional Numeral Systems
::  by Adam Naumowicz
::
:: Received December 31, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies BOOLE, NUMERAL1, ARYTM, AFINSQ_1, FUNCT_1, FINSEQ_1, ARYTM_1,
      RELAT_1, RLVECT_1, ALGSEQ_1, GROUP_1, NAT_1, MONOID_0, FINSOP_1, SEQ_1,
      SERIES_1, PREPOWER, POWER, ARYTM_3;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1, BINOP_1,
      NUMBERS, XCMPLX_0, XXREAL_0, NAT_1, NAT_D, BINOP_2, VALUED_1, SEQ_1,
      NEWTON, PREPOWER, POWER, SERIES_1, STIRL2_1, FUNCT_2, RECDEF_1, AFINSQ_1;
 constructors BINOP_1, SETWISEO, XXREAL_0, REAL_1, NAT_1, NAT_D, BINOP_2,
      SEQ_1, NEWTON, PREPOWER, SERIES_1, STIRL2_1, VALUED_1, PARTFUN1,
      RECDEF_1;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, ORDINAL1, NUMBERS, XXREAL_0,
      XREAL_0, NAT_1, INT_1, BINOP_2, MEMBERED, NEWTON, RFINSEQ, AFINSQ_1,
      CARD_1, VALUED_1, FUNCT_2, POWER;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions TARSKI, STIRL2_1;
 theorems ORDINAL1, FUNCT_1, STIRL2_1, AFINSQ_1, CARD_5, TARSKI, RELAT_1,
      XBOOLE_1, FUNCT_2, NAT_2, XREAL_1, NAT_1, INT_1, NEWTON, BINOP_2,
      SERIES_1, SEQ_1, PREPOWER, POWER, XCMPLX_1, PRGCOR_2, PEPIN,
      PRE_FF, NAT_D, XXREAL_0;
 schemes AFINSQ_1, STIRL2_1, NAT_1;

begin :: Preliminaries



theorem Th1:
  for d,e being XFinSequence of NAT holds Sum(d^e)=Sum(d)+Sum(e)
proof
  let d,e be XFinSequence of NAT;
  thus Sum(d^e)=addnat.(Sum(d),Sum(e)) by STIRL2_1:47
    .= Sum(d)+Sum(e) by BINOP_2:def 23;
end;

theorem Th2:
  for S being Real_Sequence, d being XFinSequence of NAT, n being Nat
  st d=S|(n+1) holds Sum(d)=Partial_Sums(S).n
proof
  let S be Real_Sequence, d be XFinSequence of NAT, n be Nat;
  assume
A1: d=S|(n+1);
  then
A2: dom d = dom S /\ (n+1) & for x being set st x in dom d holds
  d.x = S.x by FUNCT_1:68;
  n+1 c= NAT & REAL<>{} by ORDINAL1:def 2;
  then n+1 c= dom S by FUNCT_2:def 1;
  then
A3: dom d = n+1 by A2,XBOOLE_1:28;
  then consider f be Function of NAT,NAT such that
A5: f.0 = d.0 & (for m being Element of NAT st m+1 < len d holds
  f.(m + 1) = addnat.(f.m,d.(m + 1))) & addnat "**" d = f.(len d-1)
  by STIRL2_1:def 3;
  defpred P[Element of NAT] means $1 in dom d implies f.$1=Partial_Sums(S).$1;
  Partial_Sums(S).0=S.0 by SERIES_1:def 1;
  then
A6: P[0] by A1,A5,FUNCT_1:68;
A7: now
    let k be Element of NAT;
    assume
A8: P[k];
    thus P[k+1]
    proof
      assume
A9:   k+1 in dom d;
      then
A10:  k<k+1 & k+1 < len d by NAT_1:13,PRGCOR_2:1;
      then
B11:    k<len d by XXREAL_0:2;
      thus f.(k+1)= addnat.(f.k,d.(k + 1)) by A5,A10
        .= (f.k)+d.(k + 1) by BINOP_2:def 23
        .= (f.k)+S.(k+1) by A1,A9,FUNCT_1:68
        .= Partial_Sums(S).(k+1) by A8,B11,SERIES_1:def 1,PRGCOR_2:1;
    end;
  end;
A12: for m being Element of NAT holds P[m] from NAT_1:sch 1(A6,A7);
  reconsider N=n as Element of NAT by ORDINAL1:def 13;
  N in dom d by A3,AFINSQ_1:1;
  hence Sum(d) = Partial_Sums(S).n by A3,A5,A12;
end;

theorem Th3:
  for k,l,m being Nat holds (k(#)(l GeoSeq))|m is XFinSequence of NAT
proof
  let k,l,m be Nat;
  set g=(k(#)(l GeoSeq))|m;
A1: dom (k(#)(l GeoSeq)) = NAT by FUNCT_2:def 1;
  then m in dom (k(#)(l GeoSeq)) by ORDINAL1:def 13;
  then m c= dom (k(#)(l GeoSeq)) by A1,ORDINAL1:def 2;
  then dom g = m by RELAT_1:91;
  then reconsider g'=g as XFinSequence by ORDINAL1:def 7;
  rng g' c= NAT
  proof
    let a be set;
    assume a in rng g';
    then consider o being set such that
A2: o in dom g & a=g.o by FUNCT_1:def 5;
    reconsider o as Element of NAT by A2;
    g.o = (k(#)(l GeoSeq)).o by A2,FUNCT_1:68
      .= k*((l GeoSeq).o) by SEQ_1:13
      .= k*(l|^o) by PREPOWER:def 1;
    hence a in NAT by A2,ORDINAL1:def 13;
  end;
  hence g is XFinSequence of NAT by ORDINAL1:def 8;
end;

theorem Th4:
  for d,e being XFinSequence of NAT st len d >= 1 & len d = len e &
  for i being Nat st i in dom d holds d.i<=e.i holds Sum d <= Sum e
proof
  let d,e be XFinSequence of NAT such that
A1: len d >=1 & len d = len e & for i being Nat st i in dom d holds d.i<=e.i;
  consider f being Function of NAT,NAT such that
A2: f.0 = d.0 & (for n being Element of NAT st n+1 < len d holds
  f.(n + 1) = addnat.(f.n,d.(n + 1))) & Sum d = f.(len d-1)
  by A1,STIRL2_1:def 3;
  consider g being Function of NAT,NAT such that
A3: g.0 = e.0 & (for n being Element of NAT st n+1 < len e holds
  g.(n + 1) = addnat.(g.n,e.(n + 1))) & Sum e = g.(len e-1)
  by A1,STIRL2_1:def 3;
  defpred P[Element of NAT] means $1 in dom d implies f.$1 <= g.$1;
A4: P[0] by A1,A2,A3;
A5: now
    let i be Element of NAT;
    assume
A6: P[i];
    thus P[i+1]
    proof
      assume
A7:   i+1 in dom d;
      then
A8:   i+1 < len d by PRGCOR_2:1;
      then
B9:   i < len d by NAT_1:13;
A10:  f.(i+1) = addnat.(f.i,d.(i + 1)) by A2,A8
        .= f.i + d.(i+1) by BINOP_2:def 23;
A11:  g.(i+1) = addnat.(g.i,e.(i + 1)) by A1,A3,A8
        .= g.i + e.(i+1) by BINOP_2:def 23;
      d.(i+1) <= e.(i+1) by A1,A7;
      hence f.(i+1) <= g.(i+1) by A6,B9,A10,A11,XREAL_1:9,PRGCOR_2:1;
    end;
  end;
A12: for i being Element of NAT holds P[i] from NAT_1:sch 1(A4,A5);
  reconsider ld=len d-1 as Element of NAT by A1,NAT_1:21;
  len d-1 < len d - 0 by XREAL_1:12;
  then ld in len d by PRGCOR_2:1;
  hence Sum d <= Sum e by A1,A2,A3,A12;
end;

theorem Th5:
  for d being XFinSequence of NAT, n being Nat st
  for i being Nat st i in dom d holds n divides d.i holds n divides Sum d
proof
  let d be XFinSequence of NAT, n be Nat such that
A1: for i being Nat st i in dom d holds n divides d.i;
  per cases;
  suppose len d=0;
    then Sum d = 0 by STIRL2_1:53;
    hence n divides Sum d by NAT_D:6;
  end;
  suppose
A2: len d > 0;
    then consider f being Function of NAT,NAT such that
A3: f.0 = d.0 & (for n being Element of NAT st n+1 < len d holds
    f.(n + 1) = addnat.(f.n,d.(n + 1))) & Sum d = f.(len d-1)
    by STIRL2_1:def 3;
    defpred P[Element of NAT] means $1 < len d implies n divides f.$1;
    0 in len d by A2,PRGCOR_2:1;
    then
A4: P[0] by A1,A3;
A5: now
      let k be Element of NAT;
      assume
A6:   P[k];
      thus P[k+1]
      proof
        assume
A7:     k+1 < len d;
        then k+1 in len d by PRGCOR_2:1;
        then
A8:     n divides d.(k+1) by A1;
        f.(k+1) = addnat.(f.k,d.(k+1)) by A3,A7
          .= f.k + d.(k+1)
        by BINOP_2:def 23;
        hence n divides f.(k+1) by A6,A7,A8,NAT_1:13,NAT_D:8;
      end;
    end;
A9: for k being Element of NAT holds P[k] from NAT_1:sch 1(A4,A5);
    reconsider ld=len d-1 as Element of NAT by A2,NAT_1:20;
    ld < len d by XREAL_1:149;
    hence n divides Sum d by A3,A9;
  end;
end;

theorem Th6:
  for d,e being XFinSequence of NAT, n being Nat st dom d = dom e &
  for i being Nat st i in dom d holds e.i = d.i mod n
  holds Sum d mod n = Sum e mod n
proof
  let d,e be XFinSequence of NAT, n be Nat such that
A1: dom d = dom e & for i being Nat st i in dom d holds e.i = d.i mod n;
  defpred P[XFinSequence of NAT] means
  for e being XFinSequence of NAT st dom $1=dom e &
  for i being Nat st i in dom $1 holds e.i = $1.i mod n
  holds (Sum $1) mod n = (Sum e) mod n;
A2: P[<%>NAT]
  proof
    let e be XFinSequence of NAT;
    assume dom <%>NAT=dom e &
    for i being Nat st i in dom <%>NAT holds e.i = <%>NAT.i mod n;
    then len e = 0;
    hence (Sum <%>NAT) mod n = (Sum e) mod n by AFINSQ_1:18;
  end;
A3: for p being XFinSequence of NAT,l being Element of NAT
  st P[p] holds P[p^<%l%>]
  proof
    let p be XFinSequence of NAT,l be Element of NAT;
    assume
A4: P[p];
    thus P[p^<%l%>]
    proof
      let e be XFinSequence of NAT;
      assume
A5:   dom (p^<%l%>)=dom e & for i being Nat st i in dom (p^<%l%>) holds
      e.i = (p^<%l%>).i mod n;
      defpred Q[set,set] means $2=p.$1 mod n;
      reconsider dop=dom p as Element of NAT by ORDINAL1:def 13;
A6:   for k being Element of NAT st k in dop
      ex x being Element of NAT st Q[k,x];
      consider p' being XFinSequence of NAT such that
A7:   dom p' = dop & for k being Element of NAT st k in dop holds Q[k,p'.k]
      from STIRL2_1:sch 6(A6);
A8:   for i being Nat st i in dom p holds p'.i = p.i mod n by A7;
A9:   dom e=len p + len <%l%> by A5,AFINSQ_1:def 4
        .= dom p + 1 by AFINSQ_1:36
        .= len p' + len <%l mod n%> by A7,AFINSQ_1:36;
A10:  now
        let k be Element of NAT;
        assume
A11:    k in dom p';
        then k < dom p' by PRGCOR_2:1;
        then k < len p + 1 by A7,NAT_1:13;
        then k < len p + len <%l%> by AFINSQ_1:36;
        then k in len p + len <%l%> by PRGCOR_2:1;
        then k in dom (p^<%l%>) by AFINSQ_1:def 4;
        hence e.k = (p^<%l%>).k mod n by A5
          .= p.k mod n by A7,A11,AFINSQ_1:def 4
          .= p'.k by A7,A11;
      end;
      now
        let k be Element of NAT;
        assume k in dom <%l mod n%>;
        then
A12:    k in 1 by AFINSQ_1:36;
        then k < 1 by PRGCOR_2:1;
        then
A13:    k = 0 by NAT_1:14;
        k in dom <%l%> by A12,AFINSQ_1:36;
        hence e.(len p' + k) = (p^<%l%>).len p mod n by A5,A7,A13,AFINSQ_1:26
          .= l mod n by AFINSQ_1:40
          .= <%l mod n%>.k by A13,AFINSQ_1:38;
      end;
      then
A15:  e=p'^<%l mod n%> by A9,A10,AFINSQ_1:def 4;
      thus Sum (p^<%l%>) mod n = (Sum p + Sum <%l%>) mod n by Th1
        .= (Sum p + l) mod n by STIRL2_1:44
        .= ((Sum p) mod n + l) mod n by NAT_D:22
        .= ((Sum p) mod n + (l mod n)) mod n by NAT_D:22
        .= ((Sum p') mod n + (l mod n)) mod n by A4,A7,A8
        .= (Sum p' + (l mod n)) mod n by NAT_D:22
        .= (Sum p' + Sum <%l mod n%>) mod n by STIRL2_1:44
        .= (Sum e) mod n by A15,Th1;
    end;
  end;
  for p being XFinSequence of NAT holds P[p] from STIRL2_1:sch 5(A2,A3);
  hence (Sum d) mod n = (Sum e) mod n by A1;
end;

begin :: Representation of numbers in the base-$b$ numeral system

definition
  let d be XFinSequence of NAT;
  let b be Nat;
  func value(d,b) -> Nat means
  :Def1:
  ex d' being XFinSequence of NAT st (dom d' = dom d &
  for i being Nat st i in dom d' holds d'.i = (d.i)*(b|^i)) & it = Sum d';
  existence
  proof
    deffunc F(Nat)=(d.$1)*(b|^$1);
    consider d' being XFinSequence such that
A1: len d' = len d &
    for i being Element of NAT st i in len d holds d'.i = F(i)
    from AFINSQ_1:sch 2;
    rng d' c= NAT
    proof
      let a be set;
      assume a in rng d';
      then consider i being set such that
A2:   i in dom d' & d'.i=a by FUNCT_1:def 5;
      reconsider i as Element of NAT by A2;
      a=F(i) by A1,A2;
      hence a in NAT;
    end;
    then reconsider d' as XFinSequence of NAT by ORDINAL1:def 8;
A4: for i being Nat st i in dom d' holds d'.i = (d.i)*(b|^i) by A1;
    take Sum d';
    thus thesis by A1,A4;
  end;
  uniqueness
  proof
    let k,l be Nat;
    given k' being XFinSequence of NAT such that
A5: (dom k' = dom d &
    for i being Nat st i in dom k' holds k'.i = (d.i)*(b|^i)) & k = Sum k';
    given l' being XFinSequence of NAT such that
A6: (dom l' = dom d &
    for i being Nat st i in dom l' holds l'.i = (d.i)*(b|^i)) & l = Sum l';
    now
      let i be Element of NAT;
      assume
A7:   i in dom d;
      hence k'.i=(d.i)*(b|^i) by A5
        .= l'.i by A6,A7;
    end;
    hence k=l by A5,A6,AFINSQ_1:10;
  end;
end;

definition
  let n,b be Nat;
  assume
A1: b>1;
  func digits(n,b) -> XFinSequence of NAT means
  :Def2:
  value(it,b)=n & it.(len(it)-1) <> 0 & for i being Nat st i in dom it holds
  0 <= it.i & it.i < b if n <> 0 otherwise it = <%0%>;
  consistency;
  existence
  proof
    reconsider N=n,B=b as Element of NAT by ORDINAL1:def 13;
    thus n<>0 implies ex d being XFinSequence of NAT st
    value(d,b)=n & d.(len(d)-1) <> 0 &
    for i being Nat st i in dom d holds 0 <= d.i & d.i < b
    proof
      assume
A2:   n<>0;
      deffunc G(Nat,Element of NAT)=$2 div B;
      consider f being Function of NAT,NAT such that
A3:   f.0=N &
      for i being Nat holds f.(i+1)=G(i,(f.i) qua Element of NAT)
      from NAT_1:sch 12;
      defpred R[Nat] means f.$1 = 0;
      defpred Q[Nat] means ex i being Element of NAT st f.i = $1;
A4:   ex k being Nat st Q[k] by A3;
A5:   for k being Nat st k <> 0 & Q[k] ex l being Nat st l < k & Q[l]
      proof
        let k be Nat;
        assume
A6:     k <> 0 & Q[k];
        then consider i being Element of NAT such that
A7:     f.i=k;
        take l=k div b;
        k>0 & b > 1 by A1,A6;
        hence l<k by INT_1:83;
        take i+1;
        thus thesis by A3,A7;
      end;
      Q[0] from NAT_1:sch 7(A4,A5);
      then
A8:   ex l being Nat st R[l];
      consider l being Nat such that
A9:   R[l] & for i being Nat st R[i] holds l<=i from NAT_1:sch 5(A8);
      consider m being Nat such that
A10:  m+1=l by A2,A3,A9,NAT_1:6;
      reconsider m as Element of NAT by ORDINAL1:def 13;
      m < l by A10,NAT_1:13;
      then
A11:  f.m<>0 by A9;
A12:  R[l] by A9;
A13:  now
        let i be Nat such that l <= i;
        assume R[i];
        then f.(i+1)=0 div b by A3;
        hence R[i+1] by NAT_2:4;
      end;
A15:  for i being Nat st l <= i holds R[i] from NAT_1:sch 8(A12,A13);
A16:  now
        let i be Element of NAT;
        assume m < i;
        then l<=i by A10,NAT_1:13;
        hence f.i=0 by A15;
      end;
      dom f=NAT by FUNCT_2:def 1;
      then m+1 c= dom f by ORDINAL1:def 2;
      then dom (f|(m+1)) = m+1 by RELAT_1:91;
      then
A17:  f|(m+1) is XFinSequence by ORDINAL1:def 7;
      rng (f|(m+1)) c= NAT;
      then reconsider g=f|(m+1) as XFinSequence of NAT by A17,ORDINAL1:def 8;
      defpred P[Element of NAT,Element of NAT] means $2=g.$1 mod b;
A18:  for i being Element of NAT st i in m+1
      ex x being Element of NAT st P[i,x];
      consider d being XFinSequence of NAT such that
A19:  dom d = m+1 & for i being Element of NAT st i in m+1 holds
      P[i,(d.i)qua Element of NAT] from STIRL2_1:sch 6(A18);
      take d;
      deffunc F(Element of NAT)=(d.$1)*(b|^$1);
      consider d' being XFinSequence such that
A20:  len d' = len d & for i being Element of NAT st i in len d holds
      d'.i = F(i) from AFINSQ_1:sch 2;
      rng d' c= NAT
      proof
        let a be set;
        assume a in rng d';
        then consider i being set such that
A21:    i in dom d' & d'.i=a by FUNCT_1:def 5;
        reconsider i as Element of NAT by A21;
        a=F(i) by A20,A21;
        hence a in NAT;
      end;
      then reconsider d' as XFinSequence of NAT by ORDINAL1:def 8;
A23:  for i being Nat st i in dom d' holds d'.i = (d.i)*(b|^i) by A20;
A24:  m<m+1 by NAT_1:13;
      g.m = f.m by AFINSQ_1:1,FUNCT_1:72;
      then f.(m+1)=g.m div b by A3;
      then
A25:  g.m div b = 0 by A16,A24;
      consider s being Function of NAT,NAT such that
A27:  s.0 = d'.0 & (for i being Element of NAT st i+1 < len d' holds
      s.(i + 1) = addnat.(s.i,d'.(i + 1))) & addnat "**" d' = s.(len d'-1)
      by A20,STIRL2_1:def 3,A19;
      defpred I[Element of NAT] means
      $1<len d' implies s.$1=n-f.($1+1)*b|^($1+1);
A28:  0 in m+1 by PRGCOR_2:1;
      then d'.0=(d.0)*(b|^0) by A19,A20
        .= (g.0 mod b)*(b|^0) by A19,A28
        .= (f.0 mod b)*(b|^0) by A28,FUNCT_1:72
        .= (n mod b)*1 by A3,NEWTON:9
        .= N-B*(N div B) by A1,NEWTON:77
        .= n-(n div b)*b|^1 by NEWTON:10
        .= n-f.(0 qua Nat+1)*b|^(0 qua Nat+1) by A3;
      then
A29:  I[0] by A27;
A30:  now
        let i be Element of NAT;
        assume
A31:    I[i];
        now
          assume
A32:      i+1<len d';
          then
A33:      i+1 in dom d' by PRGCOR_2:1;
          thus s.(i+1)=addnat.(s.i,d'.(i + 1)) by A27,A32
            .= n-f.(i+1)*b|^(i+1) + d'.(i+1) by A31,A32,BINOP_2:def 23,NAT_1:13
            .= n-f.(i+1)*b|^(i+1) + (d.(i+1)*(b|^(i+1))) by A20,A33
            .= n-f.(i+1)*b|^(i+1) + ((g.(i+1) mod b)*(b|^(i+1))) by A19,A20,A33
            .= n-f.(i+1)*b|^(i+1) + ((f.(i+1) mod b)*(b|^(i+1)))
          by A19,A20,A33,FUNCT_1:72
            .= n-f.(i+1)*b|^(i+1) + ((f.(i+1) - b*(f.(i+1) div B))*(b|^(i+1)))
          by A1,NEWTON:77

.= n-f.(i+1)*b|^(i+1) + ((f.(i+1) - b*(f.(i+1+1)))*(b|^(i+1))) by A3
            .= n-f.(i+1)*b|^(i+1) + ((f.(i+1)*(b|^(i+1)) -
          (f.(i+1+1))*(b*(b|^(i+1)))))
            .= n-f.(i+1)*b|^(i+1) + ((f.(i+1)*(b|^(i+1)) -
          (f.(i+1+1))*((b|^(i+1+1))))) by NEWTON:11
            .= n-f.(i+1+1)*b|^(i+1+1);
        end;
        hence I[i+1];
      end;
A34:  for i being Element of NAT holds I[i] from NAT_1:sch 1(A29,A30);
      reconsider ld=len d'-1 as Element of NAT by A20,A19;
      s.(ld)= n-f.(m+1)*b|^(m+1) by A20,A19,A34,XREAL_1:46
        .=n by A9,A10;
      then n=Sum d' by A27;
      hence value(d,b)=n by A20,A23,Def1;
      d.(len(d)-1) = g.m mod b by A19,AFINSQ_1:1
        .= g.m by A1,A25,NAT_2:14,NAT_D:24;
      hence d.(len(d)-1) <> 0 by A11,AFINSQ_1:1,FUNCT_1:72;
      let i be Nat;
      assume i in dom d;
      then d.i=g.i mod b by A19;
      hence 0 <= d.i & d.i < b by A1,NAT_D:1;
    end;
    assume n=0;
    reconsider d=<%0%> as XFinSequence of NAT;
    take d; thus thesis;
  end;
  uniqueness
  proof
    reconsider b1=b-1 as Element of NAT by A1,NAT_1:20;
    let d,e be XFinSequence of NAT;
    thus n<>0 & (value(d,b)=n & d.(len(d)-1) <> 0 &
    for i being Nat st i in dom d holds 0 <= d.i & d.i < b) &
    (value(e,b)=n & e.(len(e)-1) <> 0 &
    for i being Nat st i in dom e holds 0 <= e.i & e.i < b) implies d=e
    proof
      assume n<>0;
      then
A35:  n>0;
      assume
A36:  (value(d,b)=n & d.(len(d)-1) <> 0 &
      for i being Nat st i in dom d holds 0 <= d.i & d.i < b);
      assume
A37:  (value(e,b)=n & e.(len(e)-1) <> 0 &
      for i being Nat st i in dom e holds 0 <= e.i & e.i < b);
      consider D being XFinSequence of NAT such that
A38:  (dom D = dom d &
      for i being Nat st i in dom D holds D.i = (d.i)*(b|^i)) &
      n = Sum D by A36,Def1;
A39:  len(d)-1 in dom d by A36,FUNCT_1:def 4;
      then reconsider k=len(d)-1 as Element of NAT;
      d.k>=1 by A36,NAT_1:14;
      then
A40:  1*(b|^k)<=(d.k)*(b|^k) by NAT_1:4;
      k in dom D by A36,A38,FUNCT_1:def 4;
      then
A41:  len D > 0;
A42:  b|^k>0 & b|^(k+1)>0 by A1,PREPOWER:13;
      log(2,b)>log(2,1) by A1,POWER:65;
      then
A43:  log(2,b)>0 by POWER:59;
      D.k = (d.k)*(b|^k) by A38,A39;
      then (d.k)*(b|^k) <= n by A38,A39,A41,STIRL2_1:52;
      then b|^k <= n by A40,XXREAL_0:2;
      then log(2,b to_power k) <= log(2,n) by A42,PRE_FF:12;
      then k*log(2,b) <= log(2,n) by A1,POWER:63;
      then k*log(2,b)/log(2,b) <= log(2,n)/log(2,b) by A43,XREAL_1:74;
      then
A44:  k <= log(2,n)/log(2,b) by A43,XCMPLX_1:90;
      consider E being XFinSequence of NAT such that
A45:  (dom E = dom e &
      for i being Nat st i in dom E holds E.i = (e.i)*(b|^i)) &
      n = Sum E by A37,Def1;
      reconsider g=((b1)(#)(b GeoSeq))|len d as XFinSequence of NAT by Th3;
C46:  dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
      then len d c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
      then
A46:  dom g = len d by RELAT_1:91;
A47:  1-b <> 0 by A1;
      g=((b-1)(#)(b GeoSeq))|(k+1);
      then
A48:  Sum g = Partial_Sums((b-1)(#)(b GeoSeq)).k by Th2
        .= ((b-1)(#)Partial_Sums(b GeoSeq)).k by SERIES_1:12
        .= (b-1)*(Partial_Sums(b GeoSeq).k) by SEQ_1:13
        .= (b-1)*((1 - (b to_power (k+1)))/(1-b)) by A1,SERIES_1:26
        .= -((1-b)*((1 - (b |^ (k+1)))/(1-b)))
        .= -(1 - (b |^ (k+1))) by A47,XCMPLX_1:88
        .= b|^(k+1)-1;
A49:  len D >= 0 qua Nat+1 by A41,NAT_1:13;
A50:  len D = len g by C46,RELAT_1:91,A38;
      for i being Nat st i in dom D holds D.i <= g.i
      proof
        let i be Nat;
        reconsider I=i as Element of NAT by ORDINAL1:def 13;
        assume
A51:    i in dom D;
        then
A52:    g.I = ((b-1)(#)(b GeoSeq)).I by A46,FUNCT_1:68,A38
          .= (b-1)*((b GeoSeq).I) by SEQ_1:13
          .= b1*(b|^I) by PREPOWER:def 1;
        d.i < b1+1 by A36,A38,A51;
        then
A53:    d.i <= b1 by NAT_1:13;
        D.i = (d.i)*(b|^i) by A38,A51;
        hence D.i <= g.i by A52,A53,XREAL_1:66;
      end;
      then n < b|^(k+1)-1+1 by XREAL_1:41,A38,A48,A49,A50,Th4;
      then log(2,n) < log(2,b to_power (k+1)) by A35,POWER:65;
      then log(2,n) < (k+1)*log(2,b) by A1,POWER:63;
      then log(2,n)/log(2,b) < (k+1)*log(2,b)/log(2,b) by A43,XREAL_1:76;
      then
A54:  log(2,n)/log(2,b) < k+1 by A43,XCMPLX_1:90;
A55:  len(e)-1 in dom e by A37,FUNCT_1:def 4;
      then reconsider l=len(e)-1 as Element of NAT;
      e.l>=1 by A37,NAT_1:14;
      then
A56:  1*(b|^l)<=(e.l)*(b|^l) by NAT_1:4;
      l in dom E by A37,A45,FUNCT_1:def 4;
      then
A57:  len E > 0;
A58:  b|^l>0 & b|^(l+1)>0 by A1,PREPOWER:13;
      log(2,b)>log(2,1) by A1,POWER:65;
      then
A59:  log(2,b)>0 by POWER:59;
      E.l = (e.l)*(b|^l) by A45,A55;
      then (e.l)*(b|^l) <= n by A45,A55,A57,STIRL2_1:52;
      then b|^l <= n by A56,XXREAL_0:2;
      then log(2,b to_power l) <= log(2,n) by A58,PRE_FF:12;
      then l*log(2,b) <= log(2,n) by A1,POWER:63;
      then l*log(2,b)/log(2,b) <= log(2,n)/log(2,b) by A59,XREAL_1:74;
      then
A60:  l <= log(2,n)/log(2,b) by A59,XCMPLX_1:90;
      reconsider g=((b1)(#)(b GeoSeq))|len e as XFinSequence of NAT by Th3;
C61:  dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
      then len e c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
      then
A61:  dom g = len e by RELAT_1:91;
A62:  1-b <> 0 by A1;
      g=((b-1)(#)(b GeoSeq))|(l+1);
      then
A63:  Sum g = Partial_Sums((b-1)(#)(b GeoSeq)).l by Th2
        .= ((b-1)(#)Partial_Sums(b GeoSeq)).l by SERIES_1:12
        .= (b-1)*(Partial_Sums(b GeoSeq).l) by SEQ_1:13
        .= (b-1)*((1 - (b to_power (l+1)))/(1-b)) by A1,SERIES_1:26
        .= -((1-b)*((1 - (b |^ (l+1)))/(1-b)))
        .= -(1 - (b |^ (l+1))) by A62,XCMPLX_1:88
        .= b|^(l+1)-1;
A64:  len E >= 0 qua Nat+1 by A57,NAT_1:13;
A65:  len E = len g by C61,RELAT_1:91,A45;
      for i being Nat st i in dom E holds E.i <= g.i
      proof
        let i be Nat;
        reconsider I=i as Element of NAT by ORDINAL1:def 13;
        assume
A66:    i in dom E;
        then
A67:    g.I = ((b-1)(#)(b GeoSeq)).I by A61,FUNCT_1:68,A45
          .= (b-1)*((b GeoSeq).I) by SEQ_1:13
          .= b1*(b|^I) by PREPOWER:def 1;
        e.i < b1+1 by A37,A45,A66;
        then
A68:    e.i <= b1 by NAT_1:13;
        E.i = (e.i)*(b|^i) by A45,A66;
        hence E.i <= g.i by A67,A68,XREAL_1:66;
      end;
      then n < b|^(l+1)-1+1 by XREAL_1:41,A45,A63,A64,A65,Th4;
      then log(2,n) < log(2,b to_power (l+1)) by A35,POWER:65;
      then log(2,n) < (l+1)*log(2,b) by A1,POWER:63;
      then log(2,n)/log(2,b) < (l+1)*log(2,b)/log(2,b) by A59,XREAL_1:76;
      then log(2,n)/log(2,b) < l+1 by A59,XCMPLX_1:90;
      then k<l+1 by A44,XXREAL_0:2;
      then
A69:  k<=l by NAT_1:13;
      l<k+1 by A54,A60,XXREAL_0:2;
      then l<=k by NAT_1:13;
      then
A70:  k=l by A69,XXREAL_0:1;
      now
        let a be set;
        assume
A72:    a in dom d;
        then reconsider o=a as Element of NAT;
A75:    o < len d by PRGCOR_2:1,A72;
        then reconsider oo=len d - o as Element of NAT by NAT_1:21;
        o<k+1 by A72,PRGCOR_2:1;
        then
A76:    o<=k by NAT_1:13;
        per cases by A76,XXREAL_0:1;
        suppose
A77:      o = 0 & o = k;
          then len D = 1 by A38;
          then
A78:      D = <%D.0%> by AFINSQ_1:38
            .= <%d.0*b|^0%> by A38,A72,A77
            .= <%d.0*1%> by NEWTON:9;
          len E = 1 by A45,A70,A77;
          then
A79:      E = <%E.0%> by AFINSQ_1:38
            .= <%e.0*b|^0%> by A45,A70,A72,A77
            .= <%e.0*1%> by NEWTON:9;
          thus d.a = n by A38,A77,A78,STIRL2_1:44
            .= e.a by A45,A77,A79,STIRL2_1:44;
        end;
        suppose
A80:      o = 0 & o < k;
          set d'=D;
          reconsider do=<%>NAT as XFinSequence of NAT;
A83:      len do=o by A80;
          then
A84:      dom D = len do + len d' by A80;
A85:      for x being Element of NAT st x in dom do holds D.x=do.x;
          for x being Element of NAT st x in dom d' holds D.(len do+x)=d'.x
          by A80,A83;
          then D=do^d' by A84,A85,AFINSQ_1:def 4;
          then
A86:      n = Sum do + Sum d' by A38,Th1;
          reconsider bo=b|^o as Element of NAT by ORDINAL1:def 13;
A87:      bo <> 0 by A1,PREPOWER:12;
          Sum do = 0 by A80,A83,STIRL2_1:53;
          then
A88:      Sum do div (b|^o) = 0 by NAT_2:4;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume k in dom d';
            then d'.K = d.(o+k)*b|^(o+k) by A38,A80
              .= d.(o+k)*(b|^o*b|^k) by NEWTON:13
              .= (d.(o+k)*(b|^k))*(b|^o);
            hence b|^o divides d'.k by NAT_D:def 3;
          end;
          then
A89:      b|^o divides Sum d' by Th5;
          then Sum d' mod (b|^o) = 0 by A87,PEPIN:6;
          then n div (b|^o) = (Sum d' div (b|^o)) + (Sum do div (b|^o))
          by A86,NAT_D:19;
          then
A90:      (n div (b|^o)) * b|^o = Sum d' by A88,A89,NAT_D:3;
          reconsider o1=o+1 as Element of NAT;
          o1 <= k & k < len d by A80,NAT_1:13,XREAL_1:149;
          then
A91:      o1 < len d by XXREAL_0:2;
          reconsider oo1=len d - o1 as Element of NAT by A80;
          defpred P[Element of NAT,set] means $2=D.($1+o1);
A92:      for u being Element of NAT st u in oo1
          ex x be Element of NAT st P[u,x];
          consider d'1 being XFinSequence of NAT such that
A93:      dom d'1=oo1 & for x being Element of NAT st x in oo1 holds
          P[x,d'1.x] from STIRL2_1:sch 6(A92);
          reconsider do1=D|o1 as XFinSequence of NAT by AFINSQ_1:16;
A95:      len d = len D by A38;
          then
A96:      len do1=o1 by A91,AFINSQ_1:14;
          then
A97:      dom D = len do1 + len d'1 by A38,A93;
A98:      for x being Element of NAT st x in dom do1 holds
          D.x=do1.x by FUNCT_1:70;
          for x being Element of NAT st x in dom d'1 holds D.(len do1+x)=d'1.x
          by A93,A96;
          then D=do1^d'1 by A97,A98,AFINSQ_1:def 4;
          then
A99:      n = Sum do1 + Sum d'1 by A38,Th1;
          reconsider bo1=b|^o1 as Element of NAT by ORDINAL1:def 13;
A100:     bo1 <> 0 by A1,PREPOWER:12;
          consider ok1 being Nat such that
A101:     ok1+1=o1;
          reconsider ok1 as Element of NAT by ORDINAL1:def 13;
          reconsider g1=((b1)(#)(b GeoSeq))|(ok1+1) as XFinSequence of NAT
          by Th3;
          dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
          then
B102:      o1 c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
          then
A102:     dom g1 = o1 by A101,RELAT_1:91;
A103:     1-b <> 0 by A1;
A104:     Sum g1 = Partial_Sums((b-1)(#)(b GeoSeq)).ok1 by Th2
            .= ((b-1)(#)Partial_Sums(b GeoSeq)).ok1 by SERIES_1:12
            .= (b-1)*(Partial_Sums(b GeoSeq).ok1) by SEQ_1:13
            .= (b-1)*((1 - (b to_power (ok1+1)))/(1-b)) by A1,SERIES_1:26
            .= -((1-b)*((1 - (b |^ o1))/(1-b))) by A101
            .= -(1 - (b |^ o1)) by A103,XCMPLX_1:88
            .= b|^o1-1;
A105:     len do1>=0 qua Nat+1 by A96,NAT_1:13;
A106:     len do1 = len g1 by A96,B102,A101,RELAT_1:91;
          for i being Nat st i in dom do1 holds do1.i <= g1.i
          proof
            let i be Nat;
            reconsider I=i as Element of NAT by ORDINAL1:def 13;
            assume
A107:       i in dom do1;
            then i in o1 by A91,A95,AFINSQ_1:14;
            then
A108:       g1.I = ((b-1)(#)(b GeoSeq)).I by A102,FUNCT_1:68
              .= (b-1)*((b GeoSeq).I) by SEQ_1:13
              .= b1*(b|^I) by PREPOWER:def 1;
A109:       dom do1 c= dom D by RELAT_1:89;
            then d.i < b1+1 by A36,A38,A107;
            then
A110:       d.i <= b1 by NAT_1:13;
            do1.i = D.i by A107,FUNCT_1:70
              .= (d.i)*(b|^i) by A38,A107,A109;
            hence do1.i <= g1.i by A108,A110,XREAL_1:66;
          end;
          then Sum do1 < b|^o1 -1 + 1 by XREAL_1:147,A104,A105,A106,Th4;
          then
A111:     Sum do1 div (b|^o1) = 0 by NAT_D:27;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume
A112:       k in dom d'1;
            then k < len d'1 by PRGCOR_2:1;
            then o1+k < dom D by A96,A97,XREAL_1:10;
            then o1+k in dom D by PRGCOR_2:1;
            then D.(o1+k) = d.(o1+k)*b|^(o1+k) by A38;
            then d'1.K = d.(o1+k)*b|^(o1+k) by A93,A112
              .= d.(o1+k)*(b|^o1*b|^k) by NEWTON:13
              .= (d.(o1+k)*(b|^k))*(b|^o1);
            hence b|^o1 divides d'1.k by NAT_D:def 3;
          end;
          then
A113:     b|^o1 divides Sum d'1 by Th5;
          then Sum d'1 mod (b|^o1) = 0 by A100,PEPIN:6;
          then n div (b|^o1) = (Sum d'1 div (b|^o1)) + (Sum do1 div (b|^o1))
          by A99,NAT_D:19;
          then
A114:     (n div (b|^o1)) * b|^o1 = Sum d'1 by A111,A113,NAT_D:3;
A115:     dom d'= 1 + dom d'1 by A38,A80,A93
            .= len <%D.o%> + len d'1 by AFINSQ_1:38;
A116:     now
            let k be Element of NAT;
            assume k in dom <%D.o%>;
            then k in 1 by AFINSQ_1:36;
            then k < 1 by PRGCOR_2:1;
            then k=0 by NAT_1:14;
            hence d'.k = <%D.o%>.k by A80,AFINSQ_1:def 5;
          end;
          now
            let k be Element of NAT;
            assume
A117:       k in dom d'1;
            thus d'.(len <%D.o%> + k) = D.(len do1 + k) by A80,A96,AFINSQ_1:36
              .= d'1.k by A93,A96,A117;
          end;
          then d'=<%D.o%>^d'1 by A115,A116,AFINSQ_1:def 4;
          then Sum d' = Sum <%D.o%> + Sum d'1 by Th1;
          then D.o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * b|^o1
          by A90,A114,STIRL2_1:44;
          then d.o*b|^o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * b|^o1
          by A38,A72;
          then d.o*b|^o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * (b|^o*b|^1
          ) by NEWTON:13;
          then d.o*b|^o = b|^o*((n div (b|^o)) - (n div (b|^o1)) * b|^1);
          then (b|^o*d.o)/b|^o = (b|^o*((n div (b|^o)) - (n div (b|^o1)) * b))
          /b|^o by NEWTON:10;
          then
A118:     d.o = (b|^o*((n div (b|^o)) - (n div (b|^o1)) * b))/b |^o
          by A87,XCMPLX_1:90;
          set d'=E;
          reconsider do=<%>NAT as XFinSequence of NAT;
A121:     len do=o by A80;
          then
A122:     dom E = len do + len d' by A80;
A123:     for x being Element of NAT st x in dom do holds E.x=do.x;
          for x being Element of NAT st x in dom d' holds E.(len do+x)=d'.x
          by A80,A121;
          then E=do^d' by A122,A123,AFINSQ_1:def 4;
          then
A124:     n = Sum do + Sum d' by A45,Th1;
          reconsider bo=b|^o as Element of NAT by ORDINAL1:def 13;
A125:     bo <> 0 by A1,PREPOWER:12;
          Sum do = 0 by A80,A121,STIRL2_1:53;
          then
A126:     Sum do div (b|^o) = 0 by NAT_2:4;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume k in dom d';
            then d'.K = e.(o+k)*b|^(o+k) by A45,A80
              .= e.(o+k)*(b|^o*b|^k) by NEWTON:13
              .= (e.(o+k)*(b|^k))*(b|^o);
            hence b|^o divides d'.k by NAT_D:def 3;
          end;
          then
A127:     b|^o divides Sum d' by Th5;
          then Sum d' mod (b|^o) = 0 by A125,PEPIN:6;
          then n div (b|^o) = (Sum d' div (b|^o)) + (Sum do div (b|^o))
          by A124,NAT_D:19;
          then
A128:     (n div (b|^o)) * b|^o = Sum d' by A126,A127,NAT_D:3;
          reconsider o1=o+1 as Element of NAT;
          o1 <= k & k < len d by A80,NAT_1:13,XREAL_1:149;
          then
A129:     o1 < len d by XXREAL_0:2;
          reconsider oo1=len d - o1 as Element of NAT by A80;
          defpred P[Element of NAT,set] means $2=E.($1+o1);
A130:     for u being Element of NAT st u in oo1
          ex x be Element of NAT st P[u,x];
          consider d'1 being XFinSequence of NAT such that
A131:     dom d'1=oo1 & for x being Element of NAT st x in oo1 holds
          P[x,d'1.x] from STIRL2_1:sch 6(A130);
          reconsider do1=E|o1 as XFinSequence of NAT by AFINSQ_1:16;
A133:     len e = len E by A45;
          then
A134:     len do1=o1 by A70,A129,AFINSQ_1:14;
          then
A135:     dom E = len do1 + len d'1 by A45,A70,A131;
A136:     for x being Element of NAT st x in dom do1 holds E.x=do1.x
          by FUNCT_1:70;
          for x being Element of NAT st x in dom d'1 holds E.(len do1+x)=d'1.x
          by A131,A134;
          then E=do1^d'1 by A135,A136,AFINSQ_1:def 4;
          then
A137:     n = Sum do1 + Sum d'1 by A45,Th1;
          reconsider bo1=b|^o1 as Element of NAT by ORDINAL1:def 13;
A138:     bo1 <> 0 by A1,PREPOWER:12;
          consider ok1 being Nat such that
A139:     ok1+1=o1;
          reconsider ok1 as Element of NAT by ORDINAL1:def 13;
          reconsider g1=((b1)(#)(b GeoSeq))|(ok1+1) as XFinSequence of NAT
          by Th3;
          dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
          then
B140:      o1 c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
          then
A140:     dom g1 = o1 by A139,RELAT_1:91;
A141:     1-b <> 0 by A1;
A142:     Sum g1 = Partial_Sums((b-1)(#)(b GeoSeq)).ok1 by Th2
            .= ((b-1)(#)Partial_Sums(b GeoSeq)).ok1 by SERIES_1:12
            .= (b-1)*(Partial_Sums(b GeoSeq).ok1) by SEQ_1:13
            .= (b-1)*((1 - (b to_power (ok1+1)))/(1-b)) by A1,SERIES_1:26
            .= -((1-b)*((1 - (b |^ o1))/(1-b))) by A139
            .= -(1 - (b |^ o1)) by A141,XCMPLX_1:88
            .= b|^o1-1;
A143:     len do1>=0 qua Nat+1 by A134,NAT_1:13;
A144:     len do1 = len g1 by A134,B140,A139,RELAT_1:91;
          for i being Nat st i in dom do1 holds do1.i <= g1.i
          proof
            let i be Nat;
            reconsider I=i as Element of NAT by ORDINAL1:def 13;
            assume
A145:       i in dom do1;
            then i in o1 by A70,A129,A133,AFINSQ_1:14;
            then
A146:       g1.I = ((b-1)(#)(b GeoSeq)).I by A140,FUNCT_1:68
              .= (b-1)*((b GeoSeq).I) by SEQ_1:13
              .= b1*(b|^I) by PREPOWER:def 1;
A147:       dom do1 c= dom E by RELAT_1:89;
            then e.i < b1+1 by A37,A45,A145;
            then
A148:       e.i <= b1 by NAT_1:13;
            do1.i = E.i by A145,FUNCT_1:70
              .= (e.i)*(b|^i) by A45,A145,A147;
            hence do1.i <= g1.i by A146,A148,XREAL_1:66;
          end;
          then Sum do1 < b|^o1 -1 + 1 by XREAL_1:147,A142,A143,A144,Th4;
          then
A149:     Sum do1 div (b|^o1) = 0 by NAT_D:27;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume
A150:       k in dom d'1;
            then k < len d'1 by PRGCOR_2:1;
            then o1+k < dom E by A134,A135,XREAL_1:10;
            then o1+k in dom E by PRGCOR_2:1;
            then E.(o1+k) = e.(o1+k)*b|^(o1+k) by A45;
            then d'1.K = e.(o1+k)*b|^(o1+k) by A131,A150
              .= e.(o1+k)*(b|^o1*b|^k) by NEWTON:13
              .= (e.(o1+k)*(b|^k))*(b|^o1);
            hence b|^o1 divides d'1.k by NAT_D:def 3;
          end;
          then
A151:     b|^o1 divides Sum d'1 by Th5;
          then Sum d'1 mod (b|^o1) = 0 by A138,PEPIN:6;
          then n div (b|^o1) = (Sum d'1 div (b|^o1)) + (Sum do1 div (b|^o1))
          by A137,NAT_D:19;
          then
A152:     (n div (b|^o1)) * b|^o1 = Sum d'1 by A149,A151,NAT_D:3;
A154:     dom d'= 1 + dom d'1 by A45,A70,A80,A131
            .= len <%E.o%> + len d'1 by AFINSQ_1:38;
A155:     now
            let k be Element of NAT;
            assume k in dom <%E.o%>;
            then k in 1 by AFINSQ_1:36;
            then k < 1 by PRGCOR_2:1;
            then k=0 by NAT_1:14;
            hence d'.k = <%E.o%>.k by A80,AFINSQ_1:def 5;
          end;
          now
            let k be Element of NAT;
            assume
A156:       k in dom d'1;
            thus d'.(len <%E.o%> + k) = E.(len do1 + k)
            by A80,A134,AFINSQ_1:36
              .= d'1.k by A131,A134,A156;
          end;
          then d'=<%E.o%>^d'1 by A154,A155,AFINSQ_1:def 4;
          then Sum d' = Sum <%E.o%> + Sum d'1 by Th1;
          then E.o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * b|^o1
          by A128,A152,STIRL2_1:44;
          then e.o*b|^o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * b|^o1 by
          A45,A70,A72;
          then e.o*b|^o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * (b|^o*b|^1
          ) by NEWTON:13;
          then e.o*b|^o = b|^o*((n div (b|^o)) - (n div (b|^o1)) * b|^1);
          then (b|^o*e.o)/b|^o = (b|^o*((n div (b|^o)) - (n div (b|^o1)) * b))
          /b|^o by NEWTON:10;
          hence d.a=e.a by A118,A125,XCMPLX_1:90;
        end;
        suppose
A157:     o>0 & o = k;
          set d'=<%D.o%>;
          reconsider do=D|o as XFinSequence of NAT by AFINSQ_1:16;
A158:     len d'=oo by A157,AFINSQ_1:38;
A159:     len d = len D by A38;
          then
A160:     len do=o by A75,AFINSQ_1:14;
          then
A161:     dom D = len do + len d' by A38,A158;
A162:     for x being Element of NAT st x in dom do holds D.x=do.x
          by FUNCT_1:70;
A163:     for x being Element of NAT st x in dom d' holds D.(len do+x)=d' .x
          proof
            let x be Element of NAT;
            assume x in dom d';
            then x < 1 by PRGCOR_2:1,A157,A158;
            then x = 0 by NAT_1:14;
            hence D.(len do+x)=d'.x by A160,AFINSQ_1:38;
          end;
          then D=do^d' by A161,A162,AFINSQ_1:def 4;
          then
A164:     n = Sum do + Sum d' by A38,Th1;
          reconsider bo=b|^o as Element of NAT by ORDINAL1:def 13;
A165:     bo <> 0 by A1,PREPOWER:12;
          consider ok being Nat such that
A166:     ok+1=o by A157,NAT_1:6;
          reconsider ok as Element of NAT by ORDINAL1:def 13;
          reconsider g=((b1)(#)(b GeoSeq))|(ok+1) as XFinSequence of NAT
          by Th3;
          dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
          then
B167:      o c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
          then
A167:     dom g = o by A166,RELAT_1:91;
A168:     1-b <> 0 by A1;
A169:     Sum g = Partial_Sums((b-1)(#)(b GeoSeq)).ok by Th2
            .= ((b-1)(#)Partial_Sums(b GeoSeq)).ok by SERIES_1:12
            .= (b-1)*(Partial_Sums(b GeoSeq).ok) by SEQ_1:13
            .= (b-1)*((1 - (b to_power (ok+1)))/(1-b)) by A1,SERIES_1:26
            .= -((1-b)*((1 - (b |^ o))/(1-b))) by A166
            .= -(1 - (b |^ o)) by A168,XCMPLX_1:88
            .= b|^o-1;
A170:     len do>=0 qua Nat+1 by A157,A160,NAT_1:13;
A171:     len do = len g by A160,B167,A166,RELAT_1:91;
          for i being Nat st i in dom do holds do.i <= g.i
          proof
            let i be Nat;
            reconsider I=i as Element of NAT by ORDINAL1:def 13;
            assume
A172:       i in dom do;
            then i in o by A75,A159,AFINSQ_1:14;
            then
A173:       g.I = ((b-1)(#)(b GeoSeq)).I by A167,FUNCT_1:68
              .= (b-1)*((b GeoSeq).I) by SEQ_1:13
              .= b1*(b|^I) by PREPOWER:def 1;
A174:       dom do c= dom D by RELAT_1:89;
            then d.i < b1+1 by A36,A38,A172;
            then
A175:       d.i <= b1 by NAT_1:13;
            do.i = D.i by A172,FUNCT_1:70
              .= (d.i)*(b|^i) by A38,A172,A174;
            hence do.i <= g.i by A173,A175,XREAL_1:66;
          end;
          then Sum do < b|^o -1 + 1 by XREAL_1:147,A169,A170,A171,Th4;
          then
A176:     Sum do div (b|^o) = 0 by NAT_D:27;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume
A177:       k in dom d';
            then k < len d' by PRGCOR_2:1;
            then o+k < dom D by A160,A161,XREAL_1:10;
            then o+k in dom D by PRGCOR_2:1;
            then D.(o+k) = d.(o+k)*b|^(o+k) by A38;
            then d'.K = d.(o+k)*b|^(o+k) by A160,A163,A177
              .= d.(o+k)*(b|^o*b|^k) by NEWTON:13
              .= (d.(o+k)*(b|^k))*(b|^o);
            hence b|^o divides d'.k by NAT_D:def 3;
          end;
          then
A178:     b|^o divides Sum d' by Th5;
          then Sum d' mod (b|^o) = 0 by A165,PEPIN:6;
          then n div (b|^o) = (Sum d' div (b|^o)) + (Sum do div (b|^o))
          by A164,NAT_D:19;
          then (n div (b|^o)) * b|^o = Sum d' by A176,A178,NAT_D:3;
          then D.o = (n div (b|^o)) * b|^o by STIRL2_1:44;
          then d.o*b|^o/b|^o = (n div (b|^o)) * b|^o/b|^o by A38,A72;
          then
A179:     d.o = (n div (b|^o)) * b|^o/b|^o by A165,XCMPLX_1:90;
          set d'=<%E.o%>;
          reconsider do=E|o as XFinSequence of NAT by AFINSQ_1:16;
A180:     len d'=oo by A157,AFINSQ_1:38;
A181:     len e = len E by A45;
          then
A182:     len do=o by A70,A75,AFINSQ_1:14;
          then
A183:     dom E = len do + len d' by A45,A70,A180;
A184:     for x being Element of NAT st x in dom do holds E.x=do.x
          by FUNCT_1:70;
A185:     for x being Element of NAT st x in dom d' holds E.(len do+x)=d' .x
          proof
            let x be Element of NAT;
            assume x in dom d';
            then x < 1 by PRGCOR_2:1,A157,A180;
            then x = 0 by NAT_1:14;
            hence E.(len do+x)=d'.x by A182,AFINSQ_1:38;
          end;
          then E=do^d' by A183,A184,AFINSQ_1:def 4;
          then
A186:     n = Sum do + Sum d' by A45,Th1;
          reconsider bo=b|^o as Element of NAT by ORDINAL1:def 13;
A187:     bo <> 0 by A1,PREPOWER:12;
          consider ok being Nat such that
A188:     ok+1=o by A157,NAT_1:6;
          reconsider ok as Element of NAT by ORDINAL1:def 13;
          reconsider g=((b1)(#)(b GeoSeq))|(ok+1) as XFinSequence of NAT
          by Th3;
          dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
          then
B189:      o c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
          then
A189:     dom g = o by A188,RELAT_1:91;
A190:     1-b <> 0 by A1;
A191:     Sum g = Partial_Sums((b-1)(#)(b GeoSeq)).ok by Th2
            .= ((b-1)(#)Partial_Sums(b GeoSeq)).ok by SERIES_1:12
            .= (b-1)*(Partial_Sums(b GeoSeq).ok) by SEQ_1:13
            .= (b-1)*((1 - (b to_power (ok+1)))/(1-b)) by A1,SERIES_1:26
            .= -((1-b)*((1 - (b |^ o))/(1-b))) by A188
            .= -(1 - (b |^ o)) by A190,XCMPLX_1:88
            .= b|^o-1;
A192:     len do>=0 qua Nat+1 by A157,A182,NAT_1:13;
A193:     len do = len g by A182,B189,A188,RELAT_1:91;
          for i being Nat st i in dom do holds do.i <= g.i
          proof
            let i be Nat;
            reconsider I=i as Element of NAT by ORDINAL1:def 13;
            assume
A194:       i in dom do;
            then i in o by A70,A75,A181,AFINSQ_1:14;
            then
A195:       g.I = ((b-1)(#)(b GeoSeq)).I by A189,FUNCT_1:68
              .= (b-1)*((b GeoSeq).I) by SEQ_1:13
              .= b1*(b|^I) by PREPOWER:def 1;
A196:       dom do c= dom E by RELAT_1:89;
            then e.i < b1+1 by A37,A45,A194;
            then
A197:       e.i <= b1 by NAT_1:13;
            do.i = E.i by A194,FUNCT_1:70
              .= (e.i)*(b|^i) by A45,A194,A196;
            hence do.i <= g.i by A195,A197,XREAL_1:66;
          end;
          then Sum do < b|^o -1 + 1 by XREAL_1:147,A191,A192,A193,Th4;
          then
A198:     Sum do div (b|^o) = 0 by NAT_D:27;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume
A199:       k in dom d';
            then k < len d' by PRGCOR_2:1;
            then o+k < dom E by A182,A183,XREAL_1:10;
            then o+k in dom E by PRGCOR_2:1;
            then E.(o+k) = e.(o+k)*b|^(o+k) by A45;
            then d'.K = e.(o+k)*b|^(o+k) by A182,A185,A199
              .= e.(o+k)*(b|^o*b|^k) by NEWTON:13
              .= (e.(o+k)*(b|^k))*(b|^o);
            hence b|^o divides d'.k by NAT_D:def 3;
          end;
          then
A200:     b|^o divides Sum d' by Th5;
          then Sum d' mod (b|^o) = 0 by A187,PEPIN:6;
          then n div (b|^o) = (Sum d' div (b|^o)) + (Sum do div (b|^o))
          by A186,NAT_D:19;
          then (n div (b|^o)) * b|^o = Sum d' by A198,A200,NAT_D:3;
          then E.o = (n div (b|^o)) * b|^o by STIRL2_1:44;
          then e.o*b|^o/b|^o = (n div (b|^o)) * b|^o/b|^o by A45,A70,A72;
          hence d.a=e.a by A179,A187,XCMPLX_1:90;
        end;
        suppose
A201:     o>0 & o < k;
          defpred P[Element of NAT,set] means $2=D.($1+o);
A202:     for u being Element of NAT st u in oo
          ex x be Element of NAT st P[u,x];
          consider d' being XFinSequence of NAT such that
A203:     dom d'=oo & for x being Element of NAT st x in oo holds
          P[x,d'.x] from STIRL2_1:sch 6(A202);
          reconsider do=D|o as XFinSequence of NAT by AFINSQ_1:16;
A205:     len d = len D by A38;
          then
A206:     len do=o by A75,AFINSQ_1:14;
          then
A207:     dom D = len do + len d' by A38,A203;
A208:     for x being Element of NAT st x in dom do holds D.x=do.x
          by FUNCT_1:70;
          for x being Element of NAT st x in dom d' holds D.(len do+x)=d' .x
          by A203,A206;
          then D=do^d' by A207,A208,AFINSQ_1:def 4;
          then
A209:     n = Sum do + Sum d' by A38,Th1;
          reconsider bo=b|^o as Element of NAT by ORDINAL1:def 13;
A210:     bo <> 0 by A1,PREPOWER:12;
          consider ok being Nat such that
A211:     ok+1=o by A201,NAT_1:6;
          reconsider ok as Element of NAT by ORDINAL1:def 13;
          reconsider g=((b1)(#)(b GeoSeq))|(ok+1) as XFinSequence of NAT
          by Th3;
          dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
          then
B212:      o c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
          then
A212:     dom g = o by A211,RELAT_1:91;
A213:     1-b <> 0 by A1;
A214:     Sum g = Partial_Sums((b-1)(#)(b GeoSeq)).ok by Th2
            .= ((b-1)(#)Partial_Sums(b GeoSeq)).ok by SERIES_1:12
            .= (b-1)*(Partial_Sums(b GeoSeq).ok) by SEQ_1:13
            .= (b-1)*((1 - (b to_power (ok+1)))/(1-b)) by A1,SERIES_1:26
            .= -((1-b)*((1 - (b |^ o))/(1-b))) by A211
            .= -(1 - (b |^ o)) by A213,XCMPLX_1:88
            .= b|^o-1;
A215:     len do>=0 qua Nat+1 by A201,A206,NAT_1:13;
A216:     len do = len g by A206,B212,A211,RELAT_1:91;
          for i being Nat st i in dom do holds do.i <= g.i
          proof
            let i be Nat;
            reconsider I=i as Element of NAT by ORDINAL1:def 13;
            assume
A217:       i in dom do;
            then i in o by A75,A205,AFINSQ_1:14;
            then
A218:       g.I = ((b-1)(#)(b GeoSeq)).I by A212,FUNCT_1:68
              .= (b-1)*((b GeoSeq).I) by SEQ_1:13
              .= b1*(b|^I) by PREPOWER:def 1;
A219:       dom do c= dom D by RELAT_1:89;
            then d.i < b1+1 by A36,A38,A217;
            then
A220:       d.i <= b1 by NAT_1:13;
            do.i = D.i by A217,FUNCT_1:70
              .= (d.i)*(b|^i) by A38,A217,A219;
            hence do.i <= g.i by A218,A220,XREAL_1:66;
          end;
          then Sum do < b|^o -1 + 1 by XREAL_1:147,A214,A215,A216,Th4;
          then
A221:     Sum do div (b|^o) = 0 by NAT_D:27;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume
A222:       k in dom d';
            then k < len d' by PRGCOR_2:1;
            then o+k < dom D by A206,A207,XREAL_1:10;
            then o+k in dom D by PRGCOR_2:1;
            then D.(o+k) = d.(o+k)*b|^(o+k) by A38;
            then d'.K = d.(o+k)*b|^(o+k) by A203,A222
              .= d.(o+k)*(b|^o*b|^k) by NEWTON:13
              .= (d.(o+k)*(b|^k))*(b|^o);
            hence b|^o divides d'.k by NAT_D:def 3;
          end;
          then
A223:     b|^o divides Sum d' by Th5;
          then Sum d' mod (b|^o) = 0 by A210,PEPIN:6;
          then n div (b|^o) = (Sum d' div (b|^o)) + (Sum do div (b|^o))
          by A209,NAT_D:19;
          then
A224:     (n div (b|^o)) * b|^o = Sum d' by A221,A223,NAT_D:3;
          reconsider o1=o+1 as Element of NAT;
A225:     o1 <= k & k < len d by A201,NAT_1:13,XREAL_1:149;
          then
A226:     o1 < len d by XXREAL_0:2;
          reconsider oo1=len d - o1 as Element of NAT by A225,NAT_1:21
          ,XXREAL_0:2;
          defpred P[Element of NAT,set] means $2=D.($1+o1);
A227:     for u being Element of NAT st u in oo1
          ex x be Element of NAT st P[u,x];
          consider d'1 being XFinSequence of NAT such that
A228:     dom d'1=oo1 & for x being Element of NAT st x in oo1 holds
          P[x,d'1.x] from STIRL2_1:sch 6(A227);
          reconsider do1=D|o1 as XFinSequence of NAT by AFINSQ_1:16;
A230:     len d = len D by A38;
          then
A231:     len do1=o1 by A226,AFINSQ_1:14;
          then
A232:     dom D = len do1 + len d'1 by A38,A228;
A233:     for x being Element of NAT st x in dom do1 holds D.x=do1.x
          by FUNCT_1:70;
          for x being Element of NAT st x in dom d'1 holds
          D.(len do1+x)=d'1.x by A228,A231;
          then D=do1^d'1 by A232,A233,AFINSQ_1:def 4;
          then
A234:     n = Sum do1 + Sum d'1 by A38,Th1;
          reconsider bo1=b|^o1 as Element of NAT by ORDINAL1:def 13;
A235:     bo1 <> 0 by A1,PREPOWER:12;
          consider ok1 being Nat such that
A236:     ok1+1=o1;
          reconsider ok1 as Element of NAT by ORDINAL1:def 13;
          reconsider g1=((b1)(#)(b GeoSeq))|(ok1+1) as XFinSequence of NAT
          by Th3;
          dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
          then
B237:      o1 c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
          then
A237:     dom g1 = o1 by A236,RELAT_1:91;
A238:     1-b <> 0 by A1;
A239:     Sum g1 = Partial_Sums((b-1)(#)(b GeoSeq)).ok1 by Th2
            .= ((b-1)(#)Partial_Sums(b GeoSeq)).ok1 by SERIES_1:12
            .= (b-1)*(Partial_Sums(b GeoSeq).ok1) by SEQ_1:13
            .= (b-1)*((1 - (b to_power (ok1+1)))/(1-b)) by A1,SERIES_1:26
            .= -((1-b)*((1 - (b |^ o1))/(1-b))) by A236
            .= -(1 - (b |^ o1)) by A238,XCMPLX_1:88
            .= b|^o1-1;
A240:     len do1>=0 qua Nat+1 by A231,NAT_1:13;
A241:     len do1 = len g1 by A231,B237,A236,RELAT_1:91;
          for i being Nat st i in dom do1 holds do1.i <= g1.i
          proof
            let i be Nat;
            reconsider I=i as Element of NAT by ORDINAL1:def 13;
            assume
A242:       i in dom do1;
            then i in o1 by A226,A230,AFINSQ_1:14;
            then
A243:       g1.I = ((b-1)(#)(b GeoSeq)).I by A237,FUNCT_1:68
              .= (b-1)*((b GeoSeq).I) by SEQ_1:13
              .= b1*(b|^I) by PREPOWER:def 1;
A244:       dom do1 c= dom D by RELAT_1:89;
            then d.i < b1+1 by A36,A38,A242;
            then
A245:       d.i <= b1 by NAT_1:13;
            do1.i = D.i by A242,FUNCT_1:70
              .= (d.i)*(b|^i) by A38,A242,A244;
            hence do1.i <= g1.i by A243,A245,XREAL_1:66;
          end;
          then Sum do1 < b|^o1 -1 + 1 by XREAL_1:147,A239,A240,A241,Th4;
          then
A246:     Sum do1 div (b|^o1) = 0 by NAT_D:27;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume
A247:       k in dom d'1;
            then k < len d'1 by PRGCOR_2:1;
            then o1+k < dom D by A231,A232,XREAL_1:10;
            then o1+k in dom D by PRGCOR_2:1;
            then D.(o1+k) = d.(o1+k)*b|^(o1+k) by A38;
            then d'1.K = d.(o1+k)*b|^(o1+k) by A228,A247
              .= d.(o1+k)*(b|^o1*b|^k) by NEWTON:13
              .= (d.(o1+k)*(b|^k))*(b|^o1);
            hence b|^o1 divides d'1.k by NAT_D:def 3;
          end;
          then
A248:     b|^o1 divides Sum d'1 by Th5;
          then Sum d'1 mod (b|^o1) = 0 by A235,PEPIN:6;
          then n div (b|^o1) = (Sum d'1 div (b|^o1)) + (Sum do1 div (b|^o1))
          by A234,NAT_D:19;
          then
A249:     (n div (b|^o1)) * b|^o1 = Sum d'1 by A246,A248,NAT_D:3;
A250:     dom d' = 1 + dom d'1 by A203,A228
            .= len <%D.o%> + len d'1 by AFINSQ_1:38;
A251:     now
            let k be Element of NAT;
            assume k in dom <%D.o%>;
            then k in 1 by AFINSQ_1:36;
            then k < 1 by PRGCOR_2:1;
            then
A252:       k=0 by NAT_1:14;
            oo > 0 by A75,XREAL_1:52;
            then k in dom d' by A203,A252,PRGCOR_2:1;
            hence d'.k=D.(k+o) by A203
              .= <%D.o%>.k by A252,AFINSQ_1:def 5;
          end;
          now
            let k be Element of NAT;
            assume
A253:       k in dom d'1;
            then k < oo1 by A228,PRGCOR_2:1;
            then k+1 < oo1+1 by XREAL_1:10;
            then
A254:       k+1 in dom d' by A203,PRGCOR_2:1;
            thus d'.(len <%D.o%> + k) = d'.(1 + k) by AFINSQ_1:36
              .= D.(len do+(k+1)) by A203,A206,A254
              .= D.(len do1 + k) by A206,A231
              .= d'1.k by A228,A231,A253;
          end;
          then d'=<%D.o%>^d'1 by A250,A251,AFINSQ_1:def 4;
          then Sum d' = Sum <%D.o%> + Sum d'1 by Th1;
          then D.o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * b|^o1
          by A224,A249,STIRL2_1:44;
          then d.o*b|^o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * b|^o1
          by A38,A72;
          then d.o*b|^o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * (b|^o*b|^1
          ) by NEWTON:13;
          then d.o*b|^o = b|^o*((n div (b|^o)) - (n div (b|^o1)) * b|^1);
          then (b|^o*d.o)/b|^o = (b|^o*((n div (b|^o)) - (n div (b|^o1)) * b))
          /b|^o by NEWTON:10;
          then
A255:     d.o = (b|^o*((n div (b|^o)) - (n div (b|^o1)) * b))/b |^o
          by A210,XCMPLX_1:90;
          defpred P[Element of NAT,set] means $2=E.($1+o);
A256:     for u being Element of NAT st u in oo
          ex x be Element of NAT st P[u,x];
          consider d' being XFinSequence of NAT such that
A257:     dom d'=oo & for x being Element of NAT st x in oo holds
          P[x,d'.x] from STIRL2_1:sch 6(A256);
          reconsider do=E|o as XFinSequence of NAT by AFINSQ_1:16;
A259:     len e = len E by A45;
          then
A260:     len do=o by A70,A75,AFINSQ_1:14;
          then
A261:     dom E = len do + len d' by A45,A70,A257;
A262:     for x being Element of NAT st x in dom do holds E.x=do.x
          by FUNCT_1:70;
          for x being Element of NAT st x in dom d' holds E.(len do+x)=d' .x
          by A257,A260;
          then E=do^d' by A261,A262,AFINSQ_1:def 4;
          then
A263:     n = Sum do + Sum d' by A45,Th1;
          reconsider bo=b|^o as Element of NAT by ORDINAL1:def 13;
A264:     bo <> 0 by A1,PREPOWER:12;
          consider ok being Nat such that
A265:     ok+1=o by A201,NAT_1:6;
          reconsider ok as Element of NAT by ORDINAL1:def 13;
          reconsider g=((b1)(#)(b GeoSeq))|(ok+1) as XFinSequence of NAT
          by Th3;
          dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
          then
B266:      o c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
          then
A266:     dom g = o by A265,RELAT_1:91;
A267:     1-b <> 0 by A1;
A268:     Sum g = Partial_Sums((b-1)(#)(b GeoSeq)).ok by Th2
            .= ((b-1)(#)Partial_Sums(b GeoSeq)).ok by SERIES_1:12
            .= (b-1)*(Partial_Sums(b GeoSeq).ok) by SEQ_1:13
            .= (b-1)*((1 - (b to_power (ok+1)))/(1-b)) by A1,SERIES_1:26
            .= -((1-b)*((1 - (b |^ o))/(1-b))) by A265
            .= -(1 - (b |^ o)) by A267,XCMPLX_1:88
            .= b|^o-1;
A269:     len do>=0 qua Nat+1 by A201,A260,NAT_1:13;
A270:     len do = len g by A260,B266,A265,RELAT_1:91;
          for i being Nat st i in dom do holds do.i <= g.i
          proof
            let i be Nat;
            reconsider I=i as Element of NAT by ORDINAL1:def 13;
            assume
A271:       i in dom do;
            then i in o by A70,A75,A259,AFINSQ_1:14;
            then
A272:       g.I = ((b-1)(#)(b GeoSeq)).I by A266,FUNCT_1:68
              .= (b-1)*((b GeoSeq).I) by SEQ_1:13
              .= b1*(b|^I) by PREPOWER:def 1;
A273:       dom do c= dom E by RELAT_1:89;
            then e.i < b1+1 by A37,A45,A271;
            then
A274:       e.i <= b1 by NAT_1:13;
            do.i = E.i by A271,FUNCT_1:70
              .= (e.i)*(b|^i) by A45,A271,A273;
            hence do.i <= g.i by A272,A274,XREAL_1:66;
          end;
          then Sum do < b|^o -1 + 1 by XREAL_1:147,A268,A269,A270,Th4;
          then
A275:     Sum do div (b|^o) = 0 by NAT_D:27;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume
A276:       k in dom d';
            then k < len d' by PRGCOR_2:1;
            then o+k < dom E by A260,A261,XREAL_1:10;
            then o+k in dom E by PRGCOR_2:1;
            then E.(o+k) = e.(o+k)*b|^(o+k) by A45;
            then d'.K = e.(o+k)*b|^(o+k) by A257,A276
              .= e.(o+k)*(b|^o*b|^k) by NEWTON:13
              .= (e.(o+k)*(b|^k))*(b|^o);
            hence b|^o divides d'.k by NAT_D:def 3;
          end;
          then
A277:     b|^o divides Sum d' by Th5;
          then Sum d' mod (b|^o) = 0 by A264,PEPIN:6;
          then n div (b|^o) = (Sum d' div (b|^o)) + (Sum do div (b|^o))
          by A263,NAT_D:19;
          then
A278:     (n div (b|^o)) * b|^o = Sum d' by A275,A277,NAT_D:3;
          reconsider o1=o+1 as Element of NAT;
A279:     o1 <= k & k < len d by A201,NAT_1:13,XREAL_1:149;
          then
A280:     o1 < len d by XXREAL_0:2;
          reconsider oo1=len d - o1 as Element of NAT by A279,NAT_1:21
          ,XXREAL_0:2;
          defpred P[Element of NAT,set] means $2=E.($1+o1);
A281:     for u being Element of NAT st u in oo1
          ex x be Element of NAT st P[u,x];
          consider d'1 being XFinSequence of NAT such that
A282:     dom d'1=oo1 & for x being Element of NAT st x in oo1 holds
          P[x,d'1.x] from STIRL2_1:sch 6(A281);
          reconsider do1=E|o1 as XFinSequence of NAT by AFINSQ_1:16;
A284:     len e = len E by A45;
          then
A285:     len do1=o1 by A70,A280,AFINSQ_1:14;
          then
A286:     dom E = len do1 + len d'1 by A45,A70,A282;
A287:     for x being Element of NAT st x in dom do1 holds E.x=do1.x
          by FUNCT_1:70;
          for x being Element of NAT st x in dom d'1 holds
          E.(len do1+x)=d'1.x by A282,A285;
          then E=do1^d'1 by A286,A287,AFINSQ_1:def 4;
          then
A288:     n = Sum do1 + Sum d'1 by A45,Th1;
          reconsider bo1=b|^o1 as Element of NAT by ORDINAL1:def 13;
A289:     bo1 <> 0 by A1,PREPOWER:12;
          consider ok1 being Nat such that
A290:     ok1+1=o1;
          reconsider ok1 as Element of NAT by ORDINAL1:def 13;
          reconsider g1=((b1)(#)(b GeoSeq))|(ok1+1) as XFinSequence of NAT
          by Th3;
          dom ((b-1)(#)(b GeoSeq)) = NAT by FUNCT_2:def 1;
          then
B291:       o1 c= dom ((b-1)(#)(b GeoSeq)) by ORDINAL1:def 2;
          then
A291:     dom g1 = o1 by A290,RELAT_1:91;
A292:     1-b <> 0 by A1;
A293:     Sum g1 = Partial_Sums((b-1)(#)(b GeoSeq)).ok1 by Th2
            .= ((b-1)(#)Partial_Sums(b GeoSeq)).ok1 by SERIES_1:12
            .= (b-1)*(Partial_Sums(b GeoSeq).ok1) by SEQ_1:13
            .= (b-1)*((1 - (b to_power (ok1+1)))/(1-b)) by A1,SERIES_1:26
            .= -((1-b)*((1 - (b |^ o1))/(1-b))) by A290
            .= -(1 - (b |^ o1)) by A292,XCMPLX_1:88
            .= b|^o1-1;
A294:     len do1>=0 qua Nat+1 by A285,NAT_1:13;
A295:     len do1 = len g1 by A285,B291,A290,RELAT_1:91;
          for i being Nat st i in dom do1 holds do1.i <= g1.i
          proof
            let i be Nat;
            reconsider I=i as Element of NAT by ORDINAL1:def 13;
            assume
A296:       i in dom do1;
            then i in o1 by A70,A280,A284,AFINSQ_1:14;
            then
A297:       g1.I = ((b-1)(#)(b GeoSeq)).I by A291,FUNCT_1:68
              .= (b-1)*((b GeoSeq).I) by SEQ_1:13
              .= b1*(b|^I) by PREPOWER:def 1;
A298:       dom do1 c= dom E by RELAT_1:89;
            then e.i < b1+1 by A37,A45,A296;
            then
A299:       e.i <= b1 by NAT_1:13;
            do1.i = E.i by A296,FUNCT_1:70
              .= (e.i)*(b|^i) by A45,A296,A298;
            hence do1.i <= g1.i by A297,A299,XREAL_1:66;
          end;
          then Sum do1 < b|^o1 -1 + 1 by XREAL_1:147,A293,A294,A295,Th4;
          then
A300:     Sum do1 div (b|^o1) = 0 by NAT_D:27;
          now
            let k be Nat;
            reconsider K=k as Element of NAT by ORDINAL1:def 13;
            assume
A301:       k in dom d'1;
            then k < len d'1 by PRGCOR_2:1;
            then o1+k < dom E by A285,A286,XREAL_1:10;
            then o1+k in dom E by PRGCOR_2:1;
            then E.(o1+k) = e.(o1+k)*b|^(o1+k) by A45;
            then d'1.K = e.(o1+k)*b|^(o1+k) by A282,A301
              .= e.(o1+k)*(b|^o1*b|^k) by NEWTON:13
              .= (e.(o1+k)*(b|^k))*(b|^o1);
            hence b|^o1 divides d'1.k by NAT_D:def 3;
          end;
          then
A302:     b|^o1 divides Sum d'1 by Th5;
          then Sum d'1 mod (b|^o1) = 0 by A289,PEPIN:6;
          then n div (b|^o1) = (Sum d'1 div (b|^o1)) + (Sum do1 div (b|^o1))
          by A288,NAT_D:19;
          then
A303:     (n div (b|^o1)) * b|^o1 = Sum d'1 by A300,A302,NAT_D:3;
A305:     dom d' = 1 + dom d'1 by A257,A282
            .= len <%E.o%> + len d'1 by AFINSQ_1:38;
A306:     now
            let k be Element of NAT;
            assume k in dom <%E.o%>;
            then k in 1 by AFINSQ_1:36;
            then k < 1 by PRGCOR_2:1;
            then
A307:       k=0 by NAT_1:14;
            oo > 0 by A75,XREAL_1:52;
            then k in dom d' by A257,A307,PRGCOR_2:1;
            hence d'.k=E.(k+o) by A257
              .= <%E.o%>.k by A307,AFINSQ_1:def 5;
          end;
          now
            let k be Element of NAT;
            assume
A308:       k in dom d'1;
            then k < oo1 by A282,PRGCOR_2:1;
            then k+1 < oo1+1 by XREAL_1:10;
            then
A309:       k+1 in oo by PRGCOR_2:1;
            thus d'.(len <%E.o%> + k) = d'.(1 + k) by AFINSQ_1:36
              .= E.(len do+(k+1)) by A257,A260,A309
              .= E.(len do1 + k) by A260,A285
              .= d'1.k by A282,A285,A308;
          end;
          then d'=<%E.o%>^d'1 by A305,A306,AFINSQ_1:def 4;
          then Sum d' = Sum <%E.o%> + Sum d'1 by Th1;
          then E.o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * b|^o1
          by A278,A303,STIRL2_1:44;
          then e.o*b|^o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * b|^o1
          by A45,A70,A72;
          then e.o*b|^o = (n div (b|^o)) * b|^o - (n div (b|^o1)) * (b|^o*b|^1
          ) by NEWTON:13;
          then e.o*b|^o = b|^o*((n div (b|^o)) - (n div (b|^o1)) * b|^1);
          then (b|^o*e.o)/b|^o = (b|^o*((n div (b|^o)) -
          (n div (b|^o1)) * b))/b|^o by NEWTON:10;
          hence d.a=e.a by A255,A264,XCMPLX_1:90;
        end;
      end;
      hence d=e by A70,FUNCT_1:9;
    end;
    assume n=0 & d=<%0%> & e=<%0%>;
    hence thesis;
  end;
end;

theorem Th7:
  for n,b being Nat st b > 1 holds len digits(n,b) >= 1
proof
  let n,b be Nat;
  assume
A1: b > 1;
  per cases;
  suppose n=0;
    then digits(n,b)=<%0%> by A1,Def2;
    hence len digits(n,b) >= 1 by AFINSQ_1:36;
  end;
  suppose n<>0;
    then digits(n,b).(len digits(n,b) -1) <> 0 by A1,Def2;
    then len digits(n,b) -1 in dom digits(n,b) by FUNCT_1:def 4;
    hence len digits(n,b) >= 1 by NAT_1:14;
  end;
end;

theorem Th8:
  for n,b being Nat st b > 1 holds value(digits(n,b),b)=n
proof
  let n,b be Nat;
  assume
A1: b>1;
  per cases;
  suppose n<>0;
    hence thesis by A1,Def2;
  end;
  suppose
A2: n=0;
    then
A3: digits(n,b)= <%0%> by A1,Def2;
    now
      let i be Nat;
      assume i in dom <%0%>;
      then i in {0} by AFINSQ_1:def 5,CARD_5:1;
      then
A4:   i=0 by TARSKI:def 1;
      hence <%0%>.i = 0*(b|^i) by AFINSQ_1:def 5
        .= (digits(n,b).i)*(b|^i) by A3,A4,AFINSQ_1:def 5;
    end;
    then
A5: value(digits(n,b),b)=Sum <%0%> by A3,Def1;
    now
      let i be Element of NAT;
      assume i in dom <%0%>;
      then i in {0} by AFINSQ_1:def 5,CARD_5:1;
      then i=0 by TARSKI:def 1;
      hence <%0%>.i=0 by AFINSQ_1:def 5;
    end;
    hence thesis by A2,A5,STIRL2_1:53;
  end;
end;

begin :: Selected divisibility criteria

theorem Th9:
  for n,k being Nat st k=10|^n - 1 holds 9 divides k
proof
  let n,k be Nat;
  assume
A1: k=10|^n - 1;
  defpred P[Nat] means ex k being Nat st k=(10|^$1)-1 & 9 divides k;
A2: (10|^0) -1 = 1-1 by NEWTON:9
    .= 0;
A3: P[0] by A2,NAT_D:6;
A4: now
    let k be Nat;
    assume P[k];
    then consider l being Nat such that
A5: l=(10|^k)-1 & 9 divides l;
    consider m being Nat such that
A6: l = 9 * m by A5,NAT_D:def 3;
A7: 10|^(k+1)-1 = (9*m+1)*10 -1 by A5,A6,NEWTON:11
      .= 9*(m*10+1);
    9 divides 9*(m*10+1) by NAT_D:def 3;
    hence P[k+1] by A7;
  end;
  for k being Nat holds P[k] from NAT_1:sch 2(A3,A4);
  then consider l being Nat such that
A8: l=10|^n -1 & 9 divides l;
  thus 9 divides k by A1,A8;
end;

theorem
  for n,b being Nat st b>1 holds b divides n iff digits(n,b).0 = 0
proof
  let n,b be Nat;
  reconsider B=b as Element of NAT by ORDINAL1:def 13;
  assume
A1: b>1;
  thus b divides n implies digits(n,b).0 = 0
  proof
    assume
A2: b divides n;
    consider d being XFinSequence of NAT such that
A3: (dom d = dom digits(n,b) & for i being Nat st i in dom d holds
    d.i = (digits(n,b).i)*(b|^i)) & value(digits(n,b),b) = Sum d by Def1;
A4: len digits(n,b)>=1 by A1,Th7;
    per cases by A4,XXREAL_0:1;
    suppose
A5:   len digits(n,b)=1;
      then
A6:   0 in dom digits(n,b) by PRGCOR_2:1;
      len d = 1 by A5,A3;
      then d=<%d.0%> by AFINSQ_1:38;
      then
A7:   Sum d = d.0 by STIRL2_1:44
        .= digits(n,b).0*(b|^0) by A3,A6;
      b divides Sum d by A1,A2,A3,Th8;
      then
A8:   b divides digits(n,b).0*1 by A7,NEWTON:9;
      per cases;
      suppose n=0;
        then digits(n,b) = <%0%> by A1,Def2;
        hence digits(n,b).0 = 0 by AFINSQ_1:38;
      end;
      suppose n<>0;
        then digits(n,b).0 < b by A1,A6,Def2;
        then digits(n,b).0 <= 0 by A8,NAT_D:7;
        hence digits(n,b).0 = 0;
      end;
    end;
    suppose
A9:   len digits(n,b)>1;
      then reconsider k=len digits(n,b)-1 as Element of NAT by NAT_1:21;
A10:  0 in dom digits(n,b) by PRGCOR_2:1,A9;
      defpred P[Element of NAT,set] means $2=d.($1+1);
A11:  for u being Element of NAT st u in k ex x be Element of NAT st P[u,x];
      consider d' being XFinSequence of NAT such that
A12:  dom d'=k & for x being Element of NAT st x in k holds P[x,d'.x]
      from STIRL2_1:sch 6(A11);
      now
        let i be Nat;
        assume
A13:    i in dom d';
        then i < k by A12,PRGCOR_2:1;
        then i+1 < k+1 by XREAL_1:10;
        then
A14:    i+1 in dom d by PRGCOR_2:1,A3;
        d'.i=d.(i+1) by A12,A13
          .= digits(n,b).(i+1)*b|^(i+1) by A3,A14
          .= digits(n,b).(i+1)*(b|^i*b) by NEWTON:11
          .= b*(digits(n,b).(i+1)*b|^i);
        hence b divides d'.i by NAT_D:def 3;
      end;
      then b divides Sum d' by Th5;
      then
A15:  Sum d' mod B = 0 by A1,PEPIN:6;
A16:  dom d = k+1 by A3
        .= len <%d.0%> + len d' by AFINSQ_1:36,A12;
A17:  now
        let l be Element of NAT;
        assume l in dom <%d.0%>;
        then l in 1 by AFINSQ_1:def 5;
        then l < 1 by PRGCOR_2:1;
        then l=0 by NAT_1:14;
        hence d.l=<%d.0%>.l by AFINSQ_1:38;
      end;
      now
        let l be Element of NAT;
        assume
A18:    l in dom d';
        len <%d.0%> + l = l+1 by AFINSQ_1:38;
        hence d.(len <%d.0%> + l) = d'.l by A12,A18;
      end;
      then d=<%d.0%>^d' by A16,A17,AFINSQ_1:def 4;
      then Sum d = Sum <%d.0%> + Sum d' by Th1;
      then n = Sum <%d.0%> + Sum d' by A1,A3,Th8;
      then n = d.0 + Sum d' by STIRL2_1:44;
      then (d.0 + Sum d') mod B = 0 by A1,A2,PEPIN:6;
      then d.0 mod b = 0 by A15,NAT_D:17;
      then B divides d.0 by A1,PEPIN:6;
      then b divides digits(n,b).0*(b|^0) by A3,A10;
      then
A19:  b divides digits(n,b).0*1 by NEWTON:9;
      per cases;
      suppose n=0;
        then digits(n,b) = <%0%> by A1,Def2;
        hence digits(n,b).0 = 0 by AFINSQ_1:38;
      end;
      suppose n<>0;
        then digits(n,b).0 < b by A1,A10,Def2;
        then digits(n,b).0 <= 0 by A19,NAT_D:7;
        hence digits(n,b).0 = 0;
      end;
    end;
  end;
  assume
A20: digits(n,b).0=0;
  consider d being XFinSequence of NAT such that
A21: (dom d = dom digits(n,b) & for i being Nat st i in dom d holds
  d.i = (digits(n,b).i)*(b|^i)) & value(digits(n,b),b) = Sum d by Def1;
  now
    let i be Nat;
    assume
A22: i in dom d;
    per cases;
    suppose i=0;
      then d.i = (digits(n,b).0)*b|^0 by A21,A22
        .= digits(n,b).0*1 by NEWTON:9;
      hence b divides d.i by A20,NAT_D:6;
    end;
    suppose i>0;
      then consider j being Nat such that
A23:  j+1=i by NAT_1:6;
      d.i = digits(n,b).i*(b|^(j+1)) by A21,A22,A23
        .= digits(n,b).i*((b|^j)*b) by NEWTON:11
        .= b*(digits(n,b).i*(b|^j));
      hence b divides d.i by NAT_D:def 3;
    end;
  end;
  then b divides value(digits(n,b),b) by A21,Th5;
  hence b divides n by A1,Th8;
end;

theorem
  for n being Nat holds 2 divides n iff 2 divides digits(n,10).0
proof
  let n be Nat;
  thus 2 divides n implies 2 divides digits(n,10).0
  proof
    assume
A1: 2 divides n;
    consider d being XFinSequence of NAT such that
A2: (dom d = dom digits(n,10) & for i being Nat st i in dom d holds
    d.i = (digits(n,10).i)*(10|^i)) & value(digits(n,10),10) = Sum d by Def1;
A3: len digits(n,10)>=1 by Th7;
    per cases by A3,XXREAL_0:1;
    suppose
A4:   len digits(n,10)=1;
      then
A5:   0 in dom digits(n,10) by PRGCOR_2:1;
      len d = 1 by A4,A2;
      then d=<%d.0%> by AFINSQ_1:38;
      then
A6:   Sum d = d.0 by STIRL2_1:44
        .= digits(n,10).0*(10|^0) by A2,A5;
      2 divides Sum d by A1,A2,Th8;
      then 2 divides digits(n,10).0*1 by A6,NEWTON:9;
      hence 2 divides digits(n,10).0;
    end;
    suppose
A7:   len digits(n,10)>1;
      then reconsider k=len digits(n,10)-1 as Element of NAT by NAT_1:21;
A8:   0 in dom digits(n,10) by PRGCOR_2:1,A7;
      defpred P[Element of NAT,set] means $2=d.($1+1);
A9:   for u being Element of NAT st u in k ex x be Element of NAT st P[u,x];
      consider d' being XFinSequence of NAT such that
A10:  dom d'=k & for x being Element of NAT st x in k holds P[x,d'.x]
      from STIRL2_1:sch 6(A9);
      now
        let i be Nat;
        assume
A11:    i in dom d';
        then i < k by A10,PRGCOR_2:1;
        then i+1 < k+1 by XREAL_1:10;
        then
A12:    i+1 in dom d by PRGCOR_2:1,A2;
        d'.i=d.(i+1) by A10,A11
          .= digits(n,10).(i+1)*10|^(i+1) by A2,A12
          .= digits(n,10).(i+1)*(10|^i*10) by NEWTON:11
          .= 2*(digits(n,10).(i+1)*10|^i*5);
        hence 2 divides d'.i by NAT_D:def 3;
      end;
      then 2 divides Sum d' by Th5;
      then
A13:  Sum d' mod 2 = 0 by PEPIN:6;
A14:  dom d = k+1 by A2
        .= len <%d.0%> + len d' by AFINSQ_1:36,A10;
A15:  now
        let l be Element of NAT;
        assume l in dom <%d.0%>;
        then l in 1 by AFINSQ_1:def 5;
        then l < 1 by PRGCOR_2:1;
        then l=0 by NAT_1:14;
        hence d.l=<%d.0%>.l by AFINSQ_1:38;
      end;
      now
        let l be Element of NAT;
        assume
A16:    l in dom d';
        len <%d.0%> + l = l+1 by AFINSQ_1:38;
        hence d.(len <%d.0%> + l) = d'.l by A10,A16;
      end;
      then d=<%d.0%>^d' by A14,A15,AFINSQ_1:def 4;
      then Sum d = Sum <%d.0%> + Sum d' by Th1;
      then n = Sum <%d.0%> + Sum d' by A2,Th8;
      then n = d.0 + Sum d' by STIRL2_1:44;
      then (d.0 + Sum d') mod 2 = 0 by A1,PEPIN:6;
      then d.0 mod 2 = 0 by A13,NAT_D:17;
      then 2 divides d.0 by PEPIN:6;
      then 2 divides digits(n,10).0*(10|^0) by A2,A8;
      then 2 divides digits(n,10).0*1 by NEWTON:9;
      hence 2 divides digits(n,10).0;
    end;
  end;
  assume
A17: 2 divides digits(n,10).0;
  consider d being XFinSequence of NAT such that
A18: (dom d = dom digits(n,10) & for i being Nat st i in dom d holds
  d.i = (digits(n,10).i)*(10|^i)) & value(digits(n,10),10) = Sum d by Def1;
  now
    let i be Nat;
    assume
A19: i in dom d;
    per cases;
    suppose i=0;
      then d.i = (digits(n,10).0)*10|^0 by A18,A19
        .= digits(n,10).0*1 by NEWTON:9;
      hence 2 divides d.i by A17;
    end;
    suppose i>0;
      then consider j being Nat such that
A20:  j+1=i by NAT_1:6;
      d.i = digits(n,10).i*(10|^(j+1)) by A18,A19,A20
        .= digits(n,10).i*((10|^j)*10) by NEWTON:11
        .= 2*(digits(n,10).i*(10|^j)*5);
      hence 2 divides d.i by NAT_D:def 3;
    end;
  end;
  then 2 divides value(digits(n,10),10) by A18,Th5;
  hence 2 divides n by Th8;
end;

theorem
  for n being Nat holds 3 divides n iff 3 divides Sum digits(n,10)
proof
  let n be Nat;
  consider p being XFinSequence of NAT such that
A1: (dom p = dom digits(n,10) & for i being Nat st i in dom p holds
  p.i = (digits(n,10).i)*(10|^i)) & value(digits(n,10),10) = Sum p by Def1;
  defpred Q[set,set] means $2=p.$1 mod 3;
  reconsider dop=dom p as Element of NAT by ORDINAL1:def 13;
A2: for k being Element of NAT st k in dop
  ex x being Element of NAT st Q[k,x];
  consider p' being XFinSequence of NAT such that
A3: dom p' = dop & for k being Element of NAT st k in dop holds Q[k,p'.k]
  from STIRL2_1:sch 6(A2);
A4: for i being Nat st i in dom p holds p'.i = p.i mod 3 by A3;
A5: now
    let i be Nat;
    reconsider dz=10|^i - 1 as Nat by NAT_1:20,NEWTON:102;
    reconsider dz as Element of NAT by ORDINAL1:def 13;
    3 divides 3*3 & 9 divides dz by Th9,NAT_D:def 3;
    then 3 divides dz by NAT_D:4;
    then 3 divides (digits(n,10).i)*dz by NAT_D:9;
    then
A6: (digits(n,10).i)*dz mod 3 = 0 by PEPIN:6;
    assume
A7: i in dom p;
    hence p'.i = p.i mod 3 by A3
      .= (digits(n,10).i)*(dz+1) mod 3 by A1,A7
      .= ((digits(n,10).i)*dz+ digits(n,10).i) mod 3
      .= (0 qua Nat+digits(n,10).i) mod 3 by A6,NAT_D:22
      .= digits(n,10).i mod 3;
  end;
  thus 3 divides n implies 3 divides Sum digits(n,10)
  proof
    assume 3 divides n;
    then 3 divides Sum p by A1,Th8;
    then Sum p mod 3 = 0 by PEPIN:6;
    then Sum p' mod 3 = 0 by A3,A4,Th6;
    then Sum digits(n,10) mod 3 = 0 by A1,A3,A5,Th6;
    hence 3 divides Sum digits(n,10) by PEPIN:6;
  end;
  assume 3 divides Sum digits(n,10);
  then Sum digits(n,10) mod 3 = 0 by PEPIN:6;
  then Sum p' mod 3 = 0 by A1,A3,A5,Th6;
  then Sum p mod 3 = 0 by A3,A4,Th6;
  then 3 divides Sum p by PEPIN:6;
  hence 3 divides n by A1,Th8;
end;

theorem
  for n being Nat holds 5 divides n iff 5 divides digits(n,10).0
proof
  let n be Nat;
  thus 5 divides n implies 5 divides digits(n,10).0
  proof
    assume
A1: 5 divides n;
    consider d being XFinSequence of NAT such that
A2: (dom d = dom digits(n,10) & for i being Nat st i in dom d holds
    d.i = (digits(n,10).i)*(10|^i)) & value(digits(n,10),10) = Sum d by Def1;
A3: len digits(n,10)>=1 by Th7;
    per cases by A3,XXREAL_0:1;
    suppose
A4:   len digits(n,10)=1;
      then
A5:   0 in dom digits(n,10) by PRGCOR_2:1;
      len d = 1 by A4,A2;
      then d=<%d.0%> by AFINSQ_1:38;
      then
A6:   Sum d = d.0 by STIRL2_1:44
        .= digits(n,10).0*(10|^0) by A2,A5;
      5 divides Sum d by A1,A2,Th8;
      then 5 divides digits(n,10).0*1 by A6,NEWTON:9;
      hence 5 divides digits(n,10).0;
    end;
    suppose
A7:   len digits(n,10)>1;
      then reconsider k=len digits(n,10)-1 as Element of NAT by NAT_1:21;
A8:   0 in dom digits(n,10) by PRGCOR_2:1,A7;
      defpred P[Element of NAT,set] means $2=d.($1+1);
A9:   for u being Element of NAT st u in k ex x be Element of NAT st P[u,x];
      consider d' being XFinSequence of NAT such that
A10:  dom d'=k & for x being Element of NAT st x in k holds P[x,d'.x]
      from STIRL2_1:sch 6(A9);
      now
        let i be Nat;
        assume
A11:    i in dom d';
        then i < k by A10,PRGCOR_2:1;
        then i+1 < k+1 by XREAL_1:10;
        then
A12:    i+1 in dom d by PRGCOR_2:1,A2;
        d'.i=d.(i+1) by A10,A11
          .= digits(n,10).(i+1)*10|^(i+1) by A2,A12
          .= digits(n,10).(i+1)*(10|^i*10) by NEWTON:11
          .= 5*(digits(n,10).(i+1)*10|^i*2);
        hence 5 divides d'.i by NAT_D:def 3;
      end;
      then 5 divides Sum d' by Th5;
      then
A13:  Sum d' mod 5 = 0 by PEPIN:6;
A14:  dom d = k+1 by A2
        .= len <%d.0%> + len d' by AFINSQ_1:36,A10;
A15:  now
        let l be Element of NAT;
        assume l in dom <%d.0%>;
        then l in 1 by AFINSQ_1:def 5;
        then l < 1 by PRGCOR_2:1;
        then l=0 by NAT_1:14;
        hence d.l=<%d.0%>.l by AFINSQ_1:38;
      end;
      now
        let l be Element of NAT;
        assume
A16:    l in dom d';
        len <%d.0%> + l = l+1 by AFINSQ_1:38;
        hence d.(len <%d.0%> + l) = d'.l by A10,A16;
      end;
      then d=<%d.0%>^d' by A14,A15,AFINSQ_1:def 4;
      then Sum d = Sum <%d.0%> + Sum d' by Th1;
      then n = Sum <%d.0%> + Sum d' by A2,Th8;
      then n = d.0 + Sum d' by STIRL2_1:44;
      then (d.0 + Sum d') mod 5 = 0 by A1,PEPIN:6;
      then d.0 mod 5 = 0 by A13,NAT_D:17;
      then 5 divides d.0 by PEPIN:6;
      then 5 divides digits(n,10).0*(10|^0) by A2,A8;
      then 5 divides digits(n,10).0*1 by NEWTON:9;
      hence 5 divides digits(n,10).0;
    end;
  end;
  assume
A17: 5 divides digits(n,10).0;
  consider d being XFinSequence of NAT such that
A18: (dom d = dom digits(n,10) & for i being Nat st i in dom d holds
  d.i = (digits(n,10).i)*(10|^i)) & value(digits(n,10),10) = Sum d by Def1;
  now
    let i be Nat;
    assume
A19: i in dom d;
    per cases;
    suppose i=0;
      then d.i = (digits(n,10).0)*10|^0 by A18,A19
        .= digits(n,10).0*1 by NEWTON:9;
      hence 5 divides d.i by A17;
    end;
    suppose i>0;
      then consider j being Nat such that
A20:  j+1=i by NAT_1:6;
      d.i = digits(n,10).i*(10|^(j+1)) by A18,A19,A20
        .= digits(n,10).i*((10|^j)*10) by NEWTON:11
        .= 5*(digits(n,10).i*(10|^j)*2);
      hence 5 divides d.i by NAT_D:def 3;
    end;
  end;
  then 5 divides value(digits(n,10),10) by A18,Th5;
  hence 5 divides n by Th8;
end;

