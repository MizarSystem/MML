:: The {J}ordan-H\"older Theorem
::  by Marco Riccardi
::
:: Received April 20, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies REALSET1, FINSEQ_1, GROUP_3, FUNCT_1, RLSUB_1, GROUP_2, RELAT_1,
      GROUP_6, BOOLE, QC_LANG1, WELLORD1, GROUP_1, VECTSP_1, GRAPH_1, SETFAM_1,
      ISOCAT_1, FUNCT_2, ARYTM_1, ARYTM, ARYTM_3, ORDINAL2, FINSET_1, FINSEQ_2,
      TARSKI, INT_1, MATRIX_2, CARD_1, SGRAPH1, SUBSET_1, GROUP_4, NAT_1,
      NATTRA_1, FINSEQ_5, LATTICES, SQUARE_1, PARTFUN1, RFINSEQ, MEMBERED,
      RFUNCT_2, FINSEQ_4, CARD_3, BINOP_1, COHSP_1, COMPLEX1, GROUP_9;
 notations TARSKI, XBOOLE_0, SUBSET_1, XCMPLX_0, ORDINAL1, XREAL_0, RELAT_1,
      FUNCT_1, RELSET_1, FUNCT_2, STRUCT_0, PARTFUN1, FINSEQ_1, ZFMISC_1,
      CARD_1, PRE_CIRC, FINSET_1, INT_1, NAT_1, FINSEQ_2, FINSEQ_3, GROUP_1,
      GROUP_2, GROUP_3, XXREAL_0, SETFAM_1, GROUP_4, FINSEQ_5, RFUNCT_2,
      NUMBERS, MEMBERED, MATRIX_2, FINSEQ_4, RFINSEQ, BINOP_1, REALSET1,
      GROUP_6, NAT_D;
 constructors SETFAM_1, PARTFUN1, BINOP_1, XXREAL_0, NAT_1, NAT_D, FINSEQ_4,
      RFUNCT_1, RFUNCT_2, RFINSEQ, BINARITH, FINSEQ_5, PRE_CIRC, REALSET2,
      GROUP_4, GROUP_6, MATRIX_2;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCT_2,
      FINSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, INT_1, MEMBERED, FINSEQ_1,
      SEQ_1, PRE_CIRC, STRUCT_0, GROUP_1, GROUP_2, GROUP_3, GROUP_6, MATRIX_2;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions GROUP_2, GROUP_6, FINSEQ_3, FINSEQ_5, TARSKI, SUBSET_1, FINSEQ_1,
      GROUP_1, REALSET1, RELAT_1, GROUP_4;
 theorems FINSEQ_1, GROUP_2, GROUP_3, TARSKI, GROUP_6, FINSEQ_2, FUNCT_1,
      FUNCT_2, RELAT_1, XBOOLE_0, XBOOLE_1, NAT_1, GROUP_1, XREAL_1, RELSET_1,
      PARTFUN1, FINSEQ_3, INT_1, ZFMISC_1, CARD_1, CARD_2, FINSET_1, XCMPLX_1,
      ORDINAL1, PRE_CIRC, RFUNCT_2, SUBSET_1, MATRIX_2, MATRIX_7, FINSEQ_5,
      FINSEQ_4, MEMBERED, XREAL_0, GLIB_001, RFINSEQ, XXREAL_0, WSIERP_1,
      GROUP_4, SETFAM_1, STRUCT_0, REALSET1, REAL_1, NAT_D;
 schemes XBOOLE_0, FUNCT_1, FINSEQ_1, NAT_1, FUNCT_2, DOMAIN_1;

begin :: Actions and Groups with Operators

:: ALG I.3.1 Definition 1
definition
  let O,E be set;
  mode Action of O,E is Function of O, Funcs(E,E);
end;

:: ALG I.3.2 Definition 2
definition
  let O,E be set;
  let A be Action of O,E;
  let IT be set;
  pred IT is_stable_under_the_action_of A means :Def1:
  for o being Element of O, f being Function of E, E st o in O & f = A.o
    holds (f .: IT) c= IT;
end;

Lm1: for O,E being set, A being Action of O,E
  holds [#]E is_stable_under_the_action_of A
proof
  let O,E be set;
  let A be Action of O,E;
  for o being Element of O, f being Function of E, E
    st o in O & f = A.o holds (f .: [#]E) c= [#]E;
  hence [#]E is_stable_under_the_action_of A by Def1;
end;

definition
  let O,E be set;
  let A be Action of O,E;
  let X be Subset of E;
  func the_stable_subset_generated_by(X,A) -> Subset of E means :Def2:
  X c= it & it is_stable_under_the_action_of A & for Y being Subset of E
    st Y is_stable_under_the_action_of A & X c= Y holds it c= Y;
  existence
  proof
    defpred P[set] means ex B being Subset of E
      st $1 = B & X c= $1 & B is_stable_under_the_action_of A;
    consider XX be set such that
    A1: for Y being set holds
      Y in XX iff Y in bool E & P[Y] from XBOOLE_0:sch 1;
    set M = meet XX;
    [#]E is_stable_under_the_action_of A & E in bool E
      by Lm1,ZFMISC_1:def 1; then
    A2: E in XX by A1; then
    for x being set st x in M holds x in E by SETFAM_1:def 1; then
    reconsider M as Subset of E by TARSKI:def 3;
    take M;
    now let x be set;
      assume A3: x in X;
      now
        let Y be set;
        assume Y in XX; then
        ex B being Subset of E st Y = B & X c= Y &
          B is_stable_under_the_action_of A by A1;
        hence x in Y by A3;
      end;
      hence x in M by A2,SETFAM_1:def 1;
    end;
    hence X c= M by TARSKI:def 3;
    now
      let o be Element of O;
      let f be Function of E, E;
      assume A4: o in O;
      assume A5: f = A.o;
      now let y be set;
        assume A6: y in f .: M;
        now
          let Y be set;
          assume A7: Y in XX; then
          ex B being Subset of E st Y = B & X c= Y
            & B is_stable_under_the_action_of A by A1; then
          A8: (f .: Y) c= Y by A4,A5,Def1;
          M c= Y by A7,SETFAM_1:4; then
          f .: M c= f .: Y by RELAT_1:156; then
          f .: M c= Y by A8,XBOOLE_1:1;
          hence y in Y by A6;
        end;
        hence y in M by A2,SETFAM_1:def 1;
      end;
      hence (f .: M) c= M by TARSKI:def 3;
    end;
    hence M is_stable_under_the_action_of A by Def1;
    now
      let Y be Subset of E;
      assume Y is_stable_under_the_action_of A & X c= Y; then
      Y in XX by A1;
      hence M c= Y by SETFAM_1:4;
    end;
    hence thesis;
  end;
  uniqueness
  proof let B1,B2 be Subset of E;
    assume A9: X c= B1 & B1 is_stable_under_the_action_of A &
     (for Y being Subset of E
      st Y is_stable_under_the_action_of A & X c= Y holds B1 c= Y) &
       X c= B2 & B2 is_stable_under_the_action_of A &
     (for Y being Subset of E
      st Y is_stable_under_the_action_of A & X c= Y holds B2 c= Y);
    B1 c= B2 & B2 c= B1 by A9;
    hence B1=B2 by XBOOLE_0:def 10;
  end;
end;

definition
  let O,E be set;
  let A be Action of O,E;
  let F be FinSequence of O;
  func Product(F,A) -> Function of E,E means :Def3:
  it = id E if len F = 0 otherwise
  ex PF being FinSequence of Funcs(E,E) st
    it = PF.(len F) & len PF = len F & PF.1 = A.(F.1) &
    (for n being natural number st n<>0 & n<len F
       holds ex f,g being Function of E,E
         st f = PF.n & g = A.(F.(n+1)) & PF.(n+1) = f*g);
  existence
  proof
    per cases;
    suppose len F=0; hence thesis; end;
    suppose A1: len F<>0;
      defpred P[Element of NAT] means
        for F being FinSequence of O st len F = $1 & len F <> 0 holds
        (ex PF being FinSequence of Funcs(E,E), IT being Function of E,E
          st IT = PF.(len PF) & len PF = len F & PF.1 = A.(F.1) &
          (for k being natural number st k<>0 & k<len F
             holds ex f,g being Function of E,E
               st f = PF.k & g = A.(F.(k+1)) & PF.(k+1) = f*g));
      A2: P[0];
      A3: for k being Element of NAT st P[k] holds P[k + 1]
      proof
        let k be Element of NAT;
        assume A4: P[k];
        thus P[k + 1]
        proof
          let F be FinSequence of O;
          assume A5: len F = k+1 & len F <> 0;
          reconsider G = F | Seg k as FinSequence of O by FINSEQ_1:23;
          A6: len G = k by A5,FINSEQ_3:59;
          per cases;
          suppose A7: len G = 0;
            set IT=A.(F.1);
            1 in Seg len F by A5,A6,A7; then
            1 in dom F by FINSEQ_1:def 3; then
            F.1 in rng F by FUNCT_1:12; then
            F.1 in O; then
            F.1 in dom A by FUNCT_2:def 1; then
            IT in rng A by FUNCT_1:12; then
            reconsider IT as Element of Funcs(E,E);
            set PF=<*IT*>;
            ex f being Function st IT = f & dom f = E & rng f c= E
              by FUNCT_2:def 2; then
            reconsider IT as Function of E,E by FUNCT_2:4;
            take PF, IT;
            len PF = 1 & PF.1 = IT by FINSEQ_1:57;
            hence IT = PF.(len PF);
            thus len PF = len F by A5,A6,A7,FINSEQ_1:57;
            thus PF.1 = A.(F.1) by FINSEQ_1:57;
            let k be natural number;
            assume A8: k<>0 & k<len F;
            consider f,g be Function of E,E;
            take f,g;
            thus f = PF.k & g = A.(F.(k+1)) by A5,A6,A7,A8,NAT_1:14;
            thus PF.(k+1) = f*g by A5,A6,A7,A8,NAT_1:14;
          end;
          suppose A9: len G <> 0; then
            consider PFk be FinSequence of Funcs(E,E), ITk be Function of E,E
            such that
            A10: ITk = PFk.(len PFk) & len PFk = len G & PFk.1 = A.(G.1) and
            A11: for k being natural number st k<>0 & k<len G
             holds ex f,g being Function of E,E
             st f = PFk.k & g = A.(G.(k+1)) & PFk.(k+1) = f*g by A6,A4;
            set f = PFk.k; set g=A.(F.(k+1));
            0<k by A9,A6; then
            A12: 0+1<k+1 by XREAL_1:8; then
            A13: 1<=k by NAT_1:13; then
            k in Seg len PFk by A6,A10; then
            A14: k in dom PFk by FINSEQ_1:def 3; then
            PFk.k in Funcs(E,E) by FINSEQ_2:13; then
            ex f being Function st PFk.k = f & dom f = E & rng f c= E
              by FUNCT_2:def 2; then
            reconsider f as Function of E,E by FUNCT_2:4;
            k+1 in Seg len F by A12,A5; then
            k+1 in dom F by FINSEQ_1:def 3; then
            F.(k+1) in rng F by FUNCT_1:12; then
            F.(k+1) in O; then
            F.(k+1) in dom A by FUNCT_2:def 1; then
            A15: g in rng A by FUNCT_1:12;
            ex f being Function st g = f & dom f = E & rng f c= E
              by A15,FUNCT_2:def 2; then
            reconsider g as Function of E,E by FUNCT_2:4;
            set IT = f * g;
            set PF = PFk ^ <*IT*>;
            IT in Funcs(E,E) by FUNCT_2:12; then
            <*IT*> is FinSequence of Funcs(E,E) by FINSEQ_1:95; then
            reconsider PF as FinSequence of Funcs(E,E) by FINSEQ_1:96;
            A16: len PF = len G + len <*IT*> by A10,FINSEQ_1:35
            .= k + 1 by A6,FINSEQ_1:56; then
            A17: len PF = len PFk + 1 by A10,A5,FINSEQ_3:59;
            take PF, IT;
            thus A18: IT=PF.(len PF) & len PF=len F by A5,A16,A17,FINSEQ_1:59;
            0<len G by A9; then
            0+1<len G+1 by XREAL_1:8; then
            1<=len G by NAT_1:13; then
            1 in Seg len PFk by A10; then
            1 in dom PFk by FINSEQ_1:def 3; then
            A19: PF.1 = A.(G.1) by A10,FINSEQ_1:def 7;
            A20: 1 in Seg k by A13;
            0+k<=k+1 by XREAL_1:8; then
            1 in Seg (k+1) /\ Seg k by A20,FINSEQ_1:9; then
            1 in dom F /\ Seg k by A5,FINSEQ_1:def 3;
            hence PF.1 = A.(F.1) by A19,FUNCT_1:71;
            let n be natural number;
            assume A21: n<>0;
            assume n<len F; then
            A22: n<=k by NAT_1:13,A5;
            per cases;
            suppose A23: n >= k;
              A24: n = k by A23,A22,XXREAL_0:1;
              A.(F.(n+1)) = g by A23,A22,XXREAL_0:1; then
              reconsider g'=A.(F.(n+1)) as Function of E,E;
              reconsider f'=PF.n as Function of E,E by A24,A14,FINSEQ_1:def 7;
              take f',g';
              thus f' = PF.n & g' = A.(F.(n+1));
              thus PF.(n+1) = f'*g' by A24,A14,FINSEQ_1:def 7,A18,A16;
            end;
            suppose A25: n < k; then
              consider f',g' be Function of E,E such that
              A26: f' = PFk.n & g' = A.(G.(n+1)) & PFk.(n+1) = f'*g'
                by A11,A21,A6;
              0<n by A21; then
              A27: 0+1<n+1 by XREAL_1:8; then
              1<=n & n<=k by A25,NAT_1:13; then
              n in Seg len PFk by A10,A6,FINSEQ_1:3; then
              A28: n in dom PFk by FINSEQ_1:def 3;
              A29: 1<=n+1 & n+1<=k by A25,A27,NAT_1:13; then
              n+1 in Seg len PFk by A10,A6,FINSEQ_1:3; then
              A30: n+1 in dom PFk by FINSEQ_1:def 3;
              take f',g';
              A31: n+1 in Seg k by A29,FINSEQ_1:3;
              0+k<=1+k by XREAL_1:8; then
              n+1 in Seg (k+1) /\ Seg k by A31,FINSEQ_1:9; then
              n+1 in dom F /\ Seg k by A5,FINSEQ_1:def 3;
              hence f' = PF.n & g' = A.(F.(n+1))
                by A26,A28,FUNCT_1:71,FINSEQ_1:def 7;
              thus PF.(n+1) = f'*g' by A26,A30,FINSEQ_1:def 7;
            end;
          end;
        end;
      end;
      A32: for k being Element of NAT holds P[k] from NAT_1:sch 1(A2,A3);
      reconsider k=len F as Element of NAT;
      ex PF being FinSequence of Funcs(E,E), IT being Function of E,E
          st IT = PF.(len PF) & len PF = len F & PF.1 = A.(F.1) &
          (for k being natural number st k<>0 & k<len F
            holds ex f,g being Function of E,E
              st f = PF.k & g = A.(F.(k+1)) & PF.(k+1) = f*g) by A32,A1;
      hence thesis;
    end;
  end;
  uniqueness
  proof
    per cases;
    suppose len F=0; hence thesis; end;
    suppose len F<>0;
      now
        let IT1,IT2 be Function of E,E;
        given PF1 be FinSequence of Funcs(E,E) such that
        A33: IT1 = PF1.(len F) & len PF1 = len F & PF1.1 = A.(F.1) &
        (for k being natural number st k<>0 & k<len F holds
          ex f,g being Function of E,E
            st f = PF1.k & g = A.(F.(k+1)) & PF1.(k+1) = f*g);
        given PF2 be FinSequence of Funcs(E,E) such that
        A34: IT2 = PF2.(len F) & len PF2 = len F & PF2.1 = A.(F.1) &
        (for k being natural number st k<>0 & k<len F holds
          ex f,g being Function of E,E
            st f = PF2.k & g = A.(F.(k+1)) & PF2.(k+1) = f*g);
        defpred P[Element of NAT] means
          1 <= $1 & $1 <= len PF1 implies PF1.$1 = PF2.$1;
        A35: P[0];
        A36: for k being Element of NAT st P[k] holds P[k+1]
        proof
          let k be Element of NAT;
          assume A37: P[k];
          now
            assume 1 <= k+1;
            assume A38: k+1 <= len PF1; then
            A39: k < len PF1 by NAT_1:13;
            per cases;
            suppose k=0; hence PF1.(k+1) = PF2.(k+1) by A33,A34; end;
            suppose A40: k<>0;
              consider f1,g1 be Function of E,E such that
              A41: f1=PF1.k & g1=A.(F.(k+1)) & PF1.(k+1)=f1*g1 by A33,A39,A40;
              consider f2,g2 be Function of E,E such that
              A42: f2=PF2.k & g2=A.(F.(k+1)) & PF2.(k+1)=f2*g2 by A33,A34,A39,
A40;
              0 < k by A40; then
              0+1 < k+1 by XREAL_1:8;
              hence PF1.(k+1) = PF2.(k+1) by A37,A41,A42,A38,NAT_1:13;
            end;
          end;
          hence thesis;
        end;
        for k be Element of NAT holds P[k] from NAT_1:sch 1(A35,A36);
        hence IT1=IT2 by A33,A34,FINSEQ_1:18;
      end;
      hence thesis;
    end;
  end;
  consistency;
end;

:: ALG I.3.4 Definition 6
definition
  let O be set;
  let G be Group;
  let IT be Action of O, the carrier of G;
  attr IT is distributive means :Def4:
  for o being Element of O st o in O holds IT.o is Homomorphism of G, G;
end;

definition
  let O be set;
  struct (HGrStr) HGrWOpStr over O
  (# carrier -> set,
     mult    -> BinOp of the carrier,
     action  -> Action of O, the carrier #);
end;

registration
  let O be set;
  cluster non empty HGrWOpStr over O;
  existence
  proof
    consider A be non empty set, m be BinOp of A, h be Action of O,A;
    take HGrWOpStr(#A,m,h#);
    thus thesis by STRUCT_0:def 1;
  end;
end;

definition
  let O be set;
  let IT be non empty HGrWOpStr over O;
  attr IT is distributive means :Def5:
  for G being Group, a being Action of O, the carrier of G
    st a = the action of IT & the HGrStr of G = the HGrStr of IT
    holds a is distributive;
end;

Lm2: for O,E being set holds [:O,{id E}:] is Action of O, E
proof
  let O,E be set;
  set h = [:O,{id E}:];
  now let x be set;
    assume x in {id E}; then
    reconsider f=x as Function of E,E by TARSKI:def 1;
    f in Funcs(E,E) by FUNCT_2:12;
    hence x in Funcs(E,E);
  end; then
  {id E} c= Funcs(E,E) by TARSKI:def 3; then
  h c= [:O,Funcs(E,E):] by ZFMISC_1:118; then
  reconsider h as Relation of O,Funcs(E,E) by RELSET_1:def 1;
  A1: now
    thus ((Funcs(E,E)={} implies O={}) implies O = dom h)
    proof
      assume Funcs(E,E)={} implies O={};
        now let x be set;
          assume A2: x in O;
          consider y be set such that
          A3: y=id E;
          take y;
          y in {id E} by A3,TARSKI:def 1;
          hence [x,y] in h by A2,ZFMISC_1:def 2;
        end;
        hence O = dom h by RELSET_1:22;
    end;
    assume O = {}; hence h = {} by ZFMISC_1:113;
  end;
  now let x,y1,y2 be set;
    assume A4: [x,y1] in h & [x,y2] in h; then
    consider x',y' be set such that
    A5: x' in O & y' in {id E} & [x,y1]=[x',y'] by ZFMISC_1:def 2;
    consider x'',y'' be set such that
    A6: x'' in O & y'' in {id E} & [x,y2]=[x'',y''] by A4,ZFMISC_1:def 2;
    A7: y'=id E & y''=id E by A5,A6,TARSKI:def 1;
    y1=y' & y2=y'' by A5,A6,ZFMISC_1:33;
    hence y1 = y2 by A7;
  end; then
  reconsider h as PartFunc of O,Funcs(E,E)
    by FUNCT_1:def 1;
  h is Action of O, E by A1,FUNCT_2:def 1;
  hence [:O,{id E}:] is Action of O, E;
end;

Lm3: for O being set, G being strict Group holds
  ex H being non empty HGrWOpStr over O
    st H is strict distributive Group-like associative & G = the HGrStr of H
proof
  let O be set;
  let G be strict Group;
  set A = the carrier of G;
  set m = the mult of G;
  reconsider h=[:O,{id the carrier of G}:] as Action of O, the carrier of G
    by Lm2;
  set GO = HGrWOpStr(#A,m,h#);
  reconsider GO as non empty HGrWOpStr over O by STRUCT_0:def 1;
  reconsider G'=GO as non empty HGrStr;
  A1: now
    set e=1_G;
    reconsider e'=e as Element of G';
    take e';
    let h' be Element of G';
    reconsider h=h' as Element of G;
    h'*e' = h*e .= h by GROUP_1:def 5;
    hence h' * e' = h';
    e'*h' = e*h .= h by GROUP_1:def 5;
    hence e' * h' = h';
    set g=h";
    reconsider g'=g as Element of G';
    take g';
    h'*g' = h*g .= 1_G by GROUP_1:def 6;
    hence h' * g' = e';
    g'*h' = g*h .= 1_G by GROUP_1:def 6;
    hence g' * h' = e';
  end;
  A2: now
    let x',y',z' be Element of G';
    reconsider x=x',y=y',z=z' as Element of G;
    (x'*y')*z' = (x*y)*z .= x*(y*z) by GROUP_1:def 4;
    hence (x'*y')*z' = x'*(y'*z');
  end;
  A3: now
    let G'' be Group;
    let a be Action of O, the carrier of G'';
    assume A4: a = the action of GO;
    assume A5: the HGrStr of G'' = the HGrStr of GO;
    now
      let o be Element of O;
      assume o in O; then
      o in dom h by FUNCT_2:def 1; then
      [o,h.o] in [:O,{id the carrier of G''}:] by A5,FUNCT_1:8; then
      consider x,y be set such that
      A6: x in O & y in {id the carrier of G''} & [o,h.o]=[x,y]
        by ZFMISC_1:def 2;
      y = id the carrier of G'' & h.o = y by A6,TARSKI:def 1,ZFMISC_1:33;
      hence a.o is Homomorphism of G'', G'' by A4,GROUP_6:47;
    end;
    hence a is distributive by Def4;
  end;
  take GO;
  thus thesis by A1,A2,A3,Def5,GROUP_1:def 4,GROUP_1:def 3;
end;

registration
  let O be set;
  cluster strict distributive Group-like associative
          (non empty HGrWOpStr over O);
  existence
  proof
    consider G be strict Group;
    consider H be non empty HGrWOpStr over O such that
    A1: H is strict distributive Group-like associative &
      the HGrStr of H = G by Lm3;
    take H;
    thus thesis by A1;
  end;
end;

:: ALG I.4.2 Definition 2
definition
  let O be set;
  mode GroupWithOperators of O
    is distributive Group-like associative (non empty HGrWOpStr over O);
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let o be Element of O;
  func G^o -> Homomorphism of G, G equals :Def6:
  (the action of G).o if o in O otherwise id the carrier of G;
  correctness
  proof
    now
      assume A1: o in O;
      consider G' be Group such that
      A2: the HGrStr of G' = the HGrStr of G;
      reconsider a=the action of G as Action of O, the carrier of G' by A2;
      A3: a is distributive by A2,Def5;
      reconsider f'=a.o as Homomorphism of G', G' by A3,A1,Def4;
      reconsider f=f' as Function of G, G by A2;
      now let g1, g2 be Element of G;
        reconsider g1'=g1,g2'=g2 as Element of G' by A2;
        f.(g1 * g2) = f'.(g1' * g2') by A2
        .= f'.g1' * f'.g2' by GROUP_6:def 7
        .= (the mult of G).(f.g1,f.g2) by A2;
        hence f.(g1 * g2) = f.g1 * f.g2;
      end;
      hence (the action of G).o is Homomorphism of G,G by GROUP_6:def 7;
    end;
    hence thesis by GROUP_6:47;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  mode StableSubgroup of G -> distributive Group-like associative
                              (non empty HGrWOpStr over O) means :Def7:
  it is Subgroup of G &
    for o being Element of O holds it^o = (G^o)|the carrier of it;
  correctness
  proof
    set H=G;
    take H;
    now
      let o be Element of O;
      set f=G^o;
      dom f = the carrier of G by FUNCT_2:def 1;
      hence H^o = (G^o)|the carrier of H by RELAT_1:98;
    end;
    hence thesis by GROUP_2:63;
  end;
end;

Lm4: for O being set, G being GroupWithOperators of O
  holds the HGrWOpStr of G is StableSubgroup of G
proof
  let O be set;
  let G be GroupWithOperators of O;
    reconsider G' = the HGrWOpStr of G as non empty HGrStr by STRUCT_0:def 1;
    A1: now
      set e=1_G;
      reconsider e'=e as Element of G';
      take e';
      let h' be Element of G';
      reconsider h=h' as Element of G;
      h'*e' = h*e .= h by GROUP_1:def 5;
      hence h' * e' = h';
      e'*h' = e*h .= h by GROUP_1:def 5;
      hence e' * h' = h';
      set g=h";
      reconsider g'=g as Element of G';
      take g';
      h'*g' = h*g .= 1_G by GROUP_1:def 6;
      hence h' * g' = e';
      g'*h' = g*h .= 1_G by GROUP_1:def 6;
      hence g' * h' = e';
    end;
    A2: now
      let x',y',z' be Element of G';
      reconsider x=x',y=y',z=z' as Element of G;
      (x'*y')*z' = (x*y)*z .= x*(y*z) by GROUP_1:def 4;
      hence (x'*y')*z' = x'*(y'*z');
    end;
    reconsider G' as strict Group-like associative
    (non empty HGrWOpStr over O) by A1,A2,GROUP_1:def 4,GROUP_1:def 3;
    A3: for G being Group, a being Action of O, the carrier of G
      st a = the action of G' & the HGrStr of G = the HGrStr of G'
      holds a is distributive by Def5;
    reconsider G' as distributive Group-like associative
                              (non empty HGrWOpStr over O) by A3,Def5;
    dom(the mult of G) = [:the carrier of G',the carrier of G':]
      by FUNCT_2:def 1; then
    the mult of G' = (the mult of G)||the carrier of G' by RELAT_1:97; then
    A4: G' is Subgroup of G by GROUP_2:def 5;
    now
      let o be Element of O;
      A5: dom(G'^o) = the carrier of G by FUNCT_2:def 1;
      now
         per cases;
         suppose A6: o in O; then
           G'^o=(the action of G').o by Def6;
           hence G'^o = G^o by A6,Def6;
         end;
         suppose not o in O; then
           G'^o = id the carrier of G' & G^o = id the carrier of G by Def6;
           hence G'^o = G^o;
         end;
      end;
      hence G'^o = (G^o)|the carrier of G' by A5,RELAT_1:98;
    end;
  hence the HGrWOpStr of G is StableSubgroup of G by A4,Def7;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster strict StableSubgroup of G;
  correctness
  proof
    reconsider G' = the HGrWOpStr of G as StableSubgroup of G by Lm4;
    take G';
    thus thesis;
  end;
end;

:: like GROUP_2:68
Lm5: for O being set, G being GroupWithOperators of O,
      H1,H2 being strict StableSubgroup of G
  st the carrier of H1 = the carrier of H2 holds H1=H2
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be strict StableSubgroup of G;
  assume A1: the carrier of H1 = the carrier of H2;
  reconsider H1'=H1,H2'=H2 as Subgroup of G by Def7;
  A2: the HGrStr of H1' = the HGrStr of H2' by A1,GROUP_2:68;
  A3: dom the action of H2 = O by FUNCT_2:def 1
  .= dom the action of H1 by FUNCT_2:def 1;
  now let x be set;
    assume A4: x in dom the action of H2; then
    reconsider o=x as Element of O;
    A5: H1^o = (the action of H1).o & H2^o = (the action of H2).o by A4,Def6;
    H1^o = (G^o)|the carrier of H2 by A1,Def7 .= H2^o by Def7;
    hence (the action of H1).x = (the action of H2).x by A5;
  end;
  hence H1 = H2 by A3,FUNCT_1:9,A2;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  func (1).G -> strict StableSubgroup of G means :Def8:
  the carrier of it = {1_G};
  existence
  proof
    set G'=(1).G;
    consider H be non empty HGrWOpStr over O such that
    A1: H is strict distributive Group-like associative &
      G' = the HGrStr of H by Lm3;
    reconsider H as strict GroupWithOperators of O by A1;
    A2: the carrier of H c= the carrier of G &
        the mult of H = (the mult of G)||the carrier of H
        by A1,GROUP_2:def 5; then
    A3: H is Subgroup of G by GROUP_2:def 5;
    now
      let o be Element of O;
      reconsider f'=H^o,f=(G^o)|the carrier of H as Function;
      A4: dom f'= the carrier of (1).G by A1,FUNCT_2:def 1;
      A5: dom f = dom((G^o)*(id the carrier of H)) by RELAT_1:94
      .= dom(G^o) /\ the carrier of H by FUNCT_1:37
      .= (the carrier of G) /\ the carrier of H by FUNCT_2:def 1
      .= the carrier of (1).G by A1,A2,XBOOLE_1:28;
      now
        let x be set;
        assume A6: x in dom f; then
        x in {1_G} by A5,GROUP_2:def 7; then
        A7: x = 1_G by TARSKI:def 1; then
        x = 1_H by A3,GROUP_2:53; then
        A8: f'.x = 1_H by GROUP_6:40;
        A9: x in dom id the carrier of H by A5,A6,A1,RELAT_1:71;
        f.x = ((G^o)*(id the carrier of H)).x by RELAT_1:94
        .= (G^o).((id the carrier of H).x) by A9,FUNCT_1:23
        .= (G^o).x by A1,A5,A6,FUNCT_1:35
        .= 1_G by A7,GROUP_6:40;
        hence f.x = f'.x by A8,A3,GROUP_2:53;
      end;
      hence H^o = (G^o)|the carrier of H by A4,A5,FUNCT_1:9;
    end; then
    reconsider H as strict StableSubgroup of G by A3,Def7;
    take H;
    thus the carrier of H = {1_G} by A1,GROUP_2:def 7;
  end;
  uniqueness by Lm5;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  func (Omega).G -> strict StableSubgroup of G equals
  the HGrWOpStr of G;
  correctness by Lm4;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be StableSubgroup of G;
  attr IT is normal means :Def10:
  for H being strict Subgroup of G st H = the HGrStr of IT holds H is normal;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster strict normal StableSubgroup of G;
  existence
  proof
    set H=(1).G;
    set H'=H;
    reconsider H as StableSubgroup of G;
    take H';
    now
      let H'' be strict Subgroup of G;
      assume A1: H'' = the HGrStr of H;
      reconsider G'=G as Group;
      A2: the mult of (1).G'
      = (the mult of G')||the carrier of (1).G' by GROUP_2:def 5;
      the carrier of (1).G' = {1_G'} by GROUP_2:def 7
      .= the carrier of (1).G by Def8;
      hence H'' is normal by A1,A2,GROUP_2:def 5;
    end;
    hence thesis by Def10;
  end;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  cluster normal StableSubgroup of H;
  existence
  proof
    reconsider H'=(1).H as GroupWithOperators of O;
    reconsider H' as StableSubgroup of H;
    take H';
    now
      let H'' be strict Subgroup of H;
      assume A1: the HGrStr of H' = H'';
      reconsider H as Group;
      the carrier of H'' = {1_H} by A1,Def8; then
      H'' = (1).H by GROUP_2:def 7;
      hence H'' is normal;
    end;
    hence H' is normal by Def10;
  end;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster (1).G -> normal;
  correctness
  proof
    now
      let H be strict Subgroup of G;
      reconsider G'=G as Group;
      reconsider H'=H as strict Subgroup of G';
      assume H = the HGrStr of (1).G; then
      the carrier of H = {1_G} by Def8; then
      H' = (1).G' by GROUP_2:def 7;
      hence H is normal;
    end;
    hence (1).G is normal by Def10;
  end;
  cluster (Omega).G -> normal;
  correctness
  proof
    now
      let H be strict Subgroup of G;
      reconsider G'=G as Group;
      reconsider H'=H as strict Subgroup of G';
      assume H = the HGrStr of (Omega).G; then
      H' = (Omega).G';
      hence H is normal;
    end;
    hence (Omega).G is normal by Def10;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  func the_stable_subgroups_of G -> set means :Def11:
  for x being set holds x in it iff x is strict StableSubgroup of G;
  existence
  proof
    defpred P[set] means ex H being strict StableSubgroup of G
      st $1 = the carrier of H;
    consider B being set such that
      A1: for x being set holds x in B iff x in bool the carrier of G & P[x]
             from XBOOLE_0:sch 1;
     defpred P[set,set] means ex H being strict StableSubgroup of G
                st $2 = H & $1 = the carrier of H;
      A2: for x,y1,y2 being set st P[x,y1] & P[x,y2] holds y1 = y2 by Lm5;
     consider f being Function such that
      A3: for x,y being set holds [x,y] in f iff x in B & P[x,y]
         from FUNCT_1:sch 1(A2);
         for x being set holds x in B iff ex y being set st [x,y] in f
       proof let x be set;
        thus x in B implies ex y being set st [x,y] in f
         proof assume A4: x in B;
           then consider H being strict StableSubgroup of G such that
           A5: x = the carrier of H by A1;
           reconsider y = H as set;
          take y;
          thus thesis by A3,A4,A5;
         end;
         given y be set such that A6: [x,y] in f;
        thus thesis by A3,A6;
       end;
      then A7: B = dom f by RELAT_1:def 4;
         for y being set holds y in rng f iff y is strict StableSubgroup of G
       proof let y be set;
        thus y in rng f implies y is strict StableSubgroup of G
         proof assume y in rng f;
           then consider x be set such that
             A8: x in dom f & y = f.x by FUNCT_1:def 5;
               [x,y] in f by A8,FUNCT_1:def 4;
            then ex H being strict StableSubgroup of G st y = H &
                    x = the carrier of H by A3;
          hence thesis;
         end;
         assume y is strict StableSubgroup of G;
          then reconsider H = y as strict StableSubgroup of G;
          reconsider x = the carrier of H as set;
           H is Subgroup of G by Def7; then
              the carrier of H c= the carrier of G by GROUP_2:def 5;
           then A9: x in dom f by A1,A7;
           then [x,y] in f by A3,A7;
           then y = f.x by A9,FUNCT_1:def 4;
        hence thesis by A9,FUNCT_1:def 5;
       end;
    hence thesis;
  end;
  uniqueness
  proof let A1,A2 be set;
    defpred P[set] means $1 is strict StableSubgroup of G;
    assume A10: for x being set holds x in A1 iff P[x];
    assume A11: for x being set holds x in A2 iff P[x];
    thus thesis from XBOOLE_0:sch 2(A10,A11);
  end;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster the_stable_subgroups_of G -> non empty;
  correctness
  proof
    (1).G in the_stable_subgroups_of G by Def11;
    hence the_stable_subgroups_of G is non empty;
  end;
end;

definition
  let IT be Group;
  attr IT is simple means :Def12:
  IT is not trivial &
  not ex H being strict normal Subgroup of IT st H <> (Omega).IT & H <> (1).IT;
end;

Lm6: Group_of_Perm 2 is simple
proof
  set G = Group_of_Perm 2;
  A1: now
    let H be strict normal Subgroup of G;
    assume A2: H <> (Omega).G;
    assume A3: H <> (1).G;
    the carrier of H c= the carrier of G by GROUP_2:def 5; then
    A4: the carrier of H c= {<*1,2*>,<*2,1*>} by MATRIX_7:3,MATRIX_2:def 13;
    1_G in H by GROUP_2:55; then
    1_G in the carrier of H by STRUCT_0:def 5; then
    {1_G} c= the carrier of H by ZFMISC_1:37; then
    {<*1,2*>} c= the carrier of H by FINSEQ_2:61,MATRIX_2:28; then
    A5: <*1,2*> in the carrier of H by ZFMISC_1:37;
    per cases by A4,ZFMISC_1:42;
    suppose the carrier of H = {}; hence contradiction; end;
    suppose the carrier of H = {<*1,2*>}; then
      {1_G} = the carrier of H by FINSEQ_2:61,MATRIX_2:28;
      hence contradiction by A3,GROUP_2:def 7;
    end;
    suppose the carrier of H = {<*2,1*>}; then
      <*2,1*>.1 = <*1,2*>.1 by A5,TARSKI:def 1; then
      2 = <*1,2*>.1 by FINSEQ_1:61;
      hence contradiction by FINSEQ_1:61;
    end;
    suppose the carrier of H = {<*1,2*>,<*2,1*>}; then
      the carrier of H = the carrier of G by MATRIX_7:3,MATRIX_2:def 13;
      hence contradiction by A2,GROUP_2:70;
    end;
  end;
  now assume G is trivial; then
    consider e be set such that
    A6: the carrier of G = {e} by GROUP_6:def 2;
    Permutations 2 = {e} by A6,MATRIX_2:def 13; then
    <*2,1*> = <*1,2*> by ZFMISC_1:9,MATRIX_7:3; then
    2 = <*1,2*>.1 by FINSEQ_1:61;
    hence contradiction by FINSEQ_1:61;
  end;
  hence thesis by A1,Def12;
end;

registration
  cluster strict simple Group;
  existence by Lm6;
end;

:: ALG I.4.4 Definition 7
definition
  let O be set;
  let IT be GroupWithOperators of O;
  attr IT is simple means :Def13:
  IT is not trivial &
    not ex H being strict normal StableSubgroup of IT
     st H <> (Omega).IT & H <> (1).IT;
end;

Lm7:
  for O being set, G being GroupWithOperators of O,
      N being normal StableSubgroup of G holds
  the HGrStr of N is strict normal Subgroup of G
proof
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  set H = the HGrStr of N;
  reconsider H as non empty HGrStr;
  now
    set e=1_N;
    reconsider e'=e as Element of H;
    take e';
    let h' be Element of H;
    reconsider h=h' as Element of N;
    h'*e' = h*e .= h by GROUP_1:def 5;
    hence h' * e' = h';
    e'*h' = e*h .= h by GROUP_1:def 5;
    hence e' * h' = h';
    set g=h";
    reconsider g'=g as Element of H;
    take g';
    h'*g' = h*g .= 1_N by GROUP_1:def 6;
    hence h' * g' = e';
    g'*h' = g*h .= 1_N by GROUP_1:def 6;
    hence g' * h' = e';
  end; then
  reconsider H as Group-like (non empty HGrStr) by GROUP_1:def 3;
  N is Subgroup of G by Def7; then
  the carrier of H c= the carrier of G &
    the mult of H = (the mult of G)||the carrier of H by GROUP_2:def 5; then
  reconsider H as Subgroup of G by GROUP_2:def 5;
  H is normal by Def10;
  hence thesis;
end;

Lm8:
  for G1, G2 being Group,
      A1 being Subset of G1, A2 being Subset of G2,
      H1 being strict Subgroup of G1, H2 being strict Subgroup of G2
  st the HGrStr of G1 = the HGrStr of G2 & A1 = A2 & H1 = H2
  holds A1 * H1 = A2 * H2 & H1 * A1 = H2 * A2
proof
  let G1, G2 be Group;
  let A1 be Subset of G1;
  let A2 be Subset of G2;
  let H1 be strict Subgroup of G1;
  let H2 be strict Subgroup of G2;
  assume A1: the HGrStr of G1 = the HGrStr of G2;
  assume A2: A1 = A2;
  assume A3: H1 = H2;
  A4: now
    let A1,B1 be Subset of G1;
    let A2,B2 be Subset of G2;
    assume A5: A1=A2 & B1=B2;
    set X={g*h where g,h is Element of G1: g in A1 & h in B1};
    set Y={g*h where g,h is Element of G2: g in A2 & h in B2};
    A6: now let x be set;
      assume x in X; then
      consider g,h be Element of G1 such that
      A7: x=g*h & g in A1 & h in B1;
      set g'=g; set h'=h;
      reconsider g',h' as Element of G2 by A1;
      g*h = g'*h' by A1;
      hence x in Y by A5,A7;
    end;
    now let x be set;
      assume x in Y; then
      consider g,h be Element of G2 such that
      A8: x=g*h & g in A2 & h in B2;
      reconsider g'=g,h'=h as Element of G1 by A1;
      g*h = g'*h' by A1;
      hence x in X by A5,A8;
    end;
    hence X=Y by A6,TARSKI:2;
  end;
  thus A1 * H1 = A2 * H2 by A3,A2,A4;
  thus H1 * A1 = H2 * A2 by A3,A2,A4;
end;

registration
  let O be set;
  cluster strict simple GroupWithOperators of O;
  existence
  proof
    set Gp2 = Group_of_Perm 2;
    consider G be non empty HGrWOpStr over O such that
    A1: G is strict distributive Group-like associative
      & Gp2 = the HGrStr of G by Lm3;
    reconsider G as strict GroupWithOperators of O by A1;
    take G;
    now assume A2: G is not simple;
      per cases by A2,Def13;
      suppose G is trivial; then
        consider e be set such that
        A3: the carrier of G = {e} by GROUP_6:def 2;
        Gp2 is trivial by GROUP_6:def 2,A3,A1;
        hence contradiction by Lm6,Def12;
      end;
      suppose ex H being strict normal StableSubgroup of G
        st H <> (Omega).G & H <> (1).G; then
        consider H be strict normal StableSubgroup of G such that
        A4: H <> (Omega).G & H <> (1).G;
        reconsider H' = the HGrStr of H as strict normal Subgroup of G by Lm7;
        reconsider G' = G as Group;
        reconsider H' as strict normal Subgroup of G';
        set H''=H';
        the carrier of H'' c= the carrier of G' &
          the mult of H'' = (the mult of G')||the carrier of H''
            by GROUP_2:def 5; then
        reconsider H'' as strict Subgroup of Gp2 by A1,GROUP_2:def 5;
        now
          let A be Subset of Gp2;
          reconsider A'=A as Subset of G' by A1;
          A * H'' = A' * H' by A1,Lm8 .= H' * A' by GROUP_3:143;
          hence A * H'' = H'' * A by A1,Lm8;
        end; then
        reconsider H'' as strict normal Subgroup of Gp2 by GROUP_3:143;
        A5: now assume H'' = (1).Gp2; then
          A6: the carrier of H'' = {1_Gp2} by GROUP_2:def 7;
          reconsider e = 1_Gp2 as Element of G by A1;
          now
            let h be Element of G;
            reconsider h'=h as Element of Gp2 by A1;
            h * e = h' * 1_Gp2 by A1
            .= h' by GROUP_1:def 5;
            hence h * e = h;
            e * h = 1_Gp2 * h' by A1
            .= h' by GROUP_1:def 5;
            hence e * h = h;
          end; then
          the carrier of H = {1_G} by A6,GROUP_1:def 5;
          hence contradiction by A4,Def8;
        end;
        H'' <> (Omega).Gp2 by A1,A4,Lm5;
        hence contradiction by A5,Lm6,Def12;
      end;
    end;
    hence thesis;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func Cosets N -> set means :Def14:
  for H being strict normal Subgroup of G st H = the HGrStr of N
    holds it = Cosets H;
  existence
  proof
    reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
    set x = Cosets H;
    take x;
    let H be strict normal Subgroup of G;
    assume H = the HGrStr of N;
    hence x = Cosets H;
  end;
  uniqueness
  proof
    let y1,y2 be set;
    assume A1: for H being strict normal Subgroup of G
      st H = the HGrStr of N holds y1 = Cosets H;
    assume A2: for H being strict normal Subgroup of G
      st H = the HGrStr of N holds y2 = Cosets H;
    reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
    y1 = Cosets H by A1;
    hence y1=y2 by A2;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func CosOp N -> BinOp of Cosets N means :Def15:
  for H being strict normal Subgroup of G st H = the HGrStr of N
    holds it = CosOp H;
  existence
  proof
    reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
    A1: Cosets N = Cosets H by Def14;
    reconsider x = CosOp H as BinOp of Cosets N by A1;
    take x;
    let H be strict normal Subgroup of G;
    assume H = the HGrStr of N;
    hence x = CosOp H;
  end;
  uniqueness
  proof
    let y1,y2 be BinOp of Cosets N;
    assume A2: for H being strict normal Subgroup of G
      st H = the HGrStr of N holds y1 = CosOp H;
    assume A3: for H being strict normal Subgroup of G
      st H = the HGrStr of N holds y2 = CosOp H;
    reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
    y1 = CosOp H by A2;
    hence y1=y2 by A3;
  end;
end;

Lm9: for G being Group, N being normal Subgroup of G,
     A being Element of Cosets N, g being Element of G
 holds g in A iff A = g * N
proof
  let G be Group;
  let N be normal Subgroup of G;
  let A be Element of Cosets N;
  let g be Element of G;
  hereby
    assume A1: g in A;
    A in Cosets N; then
    consider a be Element of G such that
    A2: A = a * N by GROUP_2:def 15;
    consider h be Element of G such that
    A3: g = a*h & h in N by A2,A1,GROUP_2:125;
    g" * a = h"*a"*a by A3,GROUP_1:25
    .= h"*(a"*a) by GROUP_1:def 4
    .= h"*1_G by GROUP_1:def 6
    .= h" by GROUP_1:def 5; then
    g" * a in N by A3,GROUP_2:60;
    hence A = g * N by A2,GROUP_2:137;
  end;
  assume A4: A = g * N;
  g = g * 1_G & 1_G in N by GROUP_2:55,GROUP_1:def 5;
  hence g in A by A4,GROUP_2:125;
end;

Lm10: for O being set, o being Element of O, G being GroupWithOperators of O,
      H being StableSubgroup of G, g being Element of G
  st g in H holds (G^o).g in H
proof
  let O be set;
  let o be Element of O;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  let g be Element of G;
  assume g in H; then
  A1: g in the carrier of H by STRUCT_0:def 5;
  set f=G^o;
  f.g = (f|the carrier of H).g by A1,FUNCT_1:72; then
  A2: f.g = (H^o).g by Def7;
  (H^o).g in the carrier of H by FUNCT_2:7,A1;
  hence (G^o).g in H by A2,STRUCT_0:def 5;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func CosAc N -> Action of O, Cosets N means :Def16:
  for o being Element of O holds
  it.o = {[A,B] where A,B is Element of Cosets N:
    ex g,h being Element of G st g in A & h in B & h = (G^o).g}
    if O is not empty
  otherwise it=[:{},{id Cosets N}:];
  existence
  proof
    A1: now assume A2: O is not empty;
      deffunc F(set) = {[A,B] where A,B is Element of Cosets N:
        for o being Element of O st $1=o holds
          ex g,h being Element of G st g in A & h in B & h = (G^o).g};
      reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
      A3: Cosets N = Cosets H by Def14;
      A4: now
        let x be set;
        assume A5: x in O;
        set f=F(x);
        A6: now
          let y be set;
          assume y in f; then
          consider A,B be Element of Cosets N such that
          A7: y = [A,B] & for o being Element of O st x=o holds
            ex g,h being Element of G st g in A & h in B & h = (G^o).g;
          reconsider A,B as set;
          take A,B;
          thus y = [A,B] by A7;
        end;
        A8: now
          let y,y1,y2 be set;
          assume [y,y1] in f; then
          consider A1,B1 be Element of Cosets N such that
          A9: [y,y1] = [A1,B1] & for o being Element of O st x=o holds
            ex g,h being Element of G st g in A1 & h in B1 & h = (G^o).g;
          assume [y,y2] in f; then
          consider A2,B2 be Element of Cosets N such that
          A10: [y,y2] = [A2,B2] & for o being Element of O st x=o holds
            ex g,h being Element of G st g in A2 & h in B2 & h = (G^o).g;
          A11: y=A1 & y1=B1 & y=A2 & y2=B2 by A9,A10,ZFMISC_1:33;
          reconsider o=x as Element of O by A5;
          consider g1,h1 be Element of G such that
          A12: g1 in A1 & h1 in B1 & h1 = (G^o).g1 by A9;
          consider g2,h2 be Element of G such that
          A13: g2 in A2 & h2 in B2 & h2 = (G^o).g2 by A10;
          reconsider A1,A2,B1,B2 as Element of Cosets H by Def14;
          set f=G^o;
          A1 = g1 * H & A2 = g2 * H by A12,A13,Lm9; then
          g2" * g1 in H by A11,GROUP_2:137; then
          g2" * g1 in the carrier of H by STRUCT_0:def 5; then
          g2" * g1 in N by STRUCT_0:def 5; then
          f.(g2" * g1) in N by Lm10; then
          f.(g2") * f.g1 in N by GROUP_6:def 7; then
          h2" * h1 in N by A12,A13,GROUP_6:41; then
          h2" * h1 in the carrier of N by STRUCT_0:def 5; then
          A14: h2" * h1 in H by STRUCT_0:def 5;
          B1 = h1 * H & B2 = h2 * H by A12,A13,Lm9;
          hence y1 = y2 by A11,A14,GROUP_2:137;
        end;
        reconsider f as Function by A6,A8,FUNCT_1:def 1,RELAT_1:def 1;
        now
          let y1 be set;
          hereby
            assume A15: y1 in Cosets N; then
            y1 in Cosets H by Def14; then
            consider g be Element of G such that
            A16: y1 = g * H & y1 = H * g by GROUP_6:15;
            reconsider o=x as Element of O by A5;
            reconsider A=y1 as Element of Cosets N by A15;
            set h = (G^o).g;
            reconsider B = h * H as Element of Cosets N by A3,GROUP_2:def 15;
            reconsider y2=B as set;
            take y2;
            now
              let o be Element of O;
              assume A17: x=o;
              take g,h;
              thus g in A by A16,Lm9,A3;
              thus h in B by Lm9,A3;
              thus h = (G^o).g by A17;
            end;
            hence [y1,y2] in f;
          end;
          given y2 be set such that
          A18: [y1,y2] in f;
          consider A,B be Element of Cosets N such that
          A19: [y1,y2] = [A,B] & for o being Element of O st x=o holds
            ex g,h being Element of G st g in A & h in B & h = (G^o).g by A18;
          A in Cosets N by A3;
          hence y1 in Cosets N by A19,ZFMISC_1:33;
        end; then
        A20: dom f = Cosets N by RELAT_1:def 4;
        now
          let y2 be set;
          assume y2 in rng f; then
          consider y1 be set such that
          A21: [y1,y2] in f by RELAT_1:def 5;
          consider A,B be Element of Cosets N such that
          A22: [y1,y2] = [A,B] & for o being Element of O st x=o holds
            ex g,h being Element of G st g in A & h in B & h = (G^o).g by A21;
          B in Cosets N by A3;
          hence y2 in Cosets N by A22,ZFMISC_1:33;
        end; then
        rng f c= Cosets N by TARSKI:def 3;
        hence F(x) in Funcs(Cosets N,Cosets N) by A20,FUNCT_2:def 2;
      end;
      ex f being Function of O,Funcs(Cosets N,Cosets N) st
        for x being set st x in O holds f.x = F(x) from FUNCT_2:sch 2(A4); then
      consider IT be Function of O,Funcs(Cosets N,Cosets N) such that
      A23: for x being set st x in O holds IT.x = F(x);
      reconsider IT as Action of O, Cosets N;
      take IT;
      let o be Element of O;
      reconsider x=o as set;
      set X = {[A,B] where A,B is Element of Cosets N:
        for o being Element of O st x=o holds
          ex g,h being Element of G st g in A & h in B & h = (G^o).g};
      set Y = {[A,B] where A,B is Element of Cosets N:
          ex g,h being Element of G st g in A & h in B & h = (G^o).g};
      A24: now
        let y be set;
        hereby
          assume y in X; then
          consider A,B be Element of Cosets N such that
          A25: y = [A,B] & for o being Element of O st x=o holds
            ex g,h being Element of G st g in A & h in B & h = (G^o).g;
          ex g,h being Element of G st g in A & h in B & h = (G^o).g by A25;
          hence y in Y by A25;
        end;
        assume y in Y; then
        consider A,B be Element of Cosets N such that
        A26: y = [A,B] & ex g,h being Element of G
          st g in A & h in B & h = (G^o).g;
        for o being Element of O st x=o holds
          ex g,h being Element of G st g in A & h in B & h = (G^o).g by A26;
        hence y in X by A26;
      end;
      IT.o = X by A2,A23;
      hence IT.o = Y by A24,TARSKI:2;
    end;
    now assume A27: O is empty;
      reconsider IT=[:{},{id Cosets N}:] as Action of O, Cosets N by A27,Lm2;
      take IT;
      thus IT=[:{},{id Cosets N}:];
    end;
    hence thesis by A1;
  end;
  uniqueness
  proof
    A28: now
      assume O is not empty;
      let IT1,IT2 be Action of O, Cosets N;
      assume A29: for o being Element of O holds
        IT1.o = {[A,B] where A,B is Element of Cosets N:
        ex g,h being Element of G st g in A & h in B & h = (G^o).g};
      assume A30: for o being Element of O holds
        IT2.o = {[A,B] where A,B is Element of Cosets N:
        ex g,h being Element of G st g in A & h in B & h = (G^o).g};
      A31: dom IT1 = O & dom IT2 = O by FUNCT_2:def 1;
      now let x be set;
        assume A32: x in dom IT1;
        reconsider o=x as Element of O by A32;
        IT1.o = {[A,B] where A,B is Element of Cosets N:
          ex g,h being Element of G st g in A & h in B & h = (G^o).g} by A29;
        hence IT1.x=IT2.x by A30;
      end;
      hence IT1=IT2 by A31,FUNCT_1:9;
    end;
    thus thesis by A28;
  end;
  correctness;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func G./.N -> HGrWOpStr over O equals
  HGrWOpStr (# Cosets N, CosOp N, CosAc N #);
  correctness;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  cluster G./.N -> non empty;
  correctness
  proof
    reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
    Cosets N = Cosets H by Def14;
    hence thesis by STRUCT_0:def 1;
  end;
  cluster G./.N -> distributive Group-like associative;
  correctness
  proof
    set G' = the HGrStr of G./.N;
    reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
    A1: the carrier of G./.N = the carrier of G./.H by Def14;
    A2: G' is Group-like associative by Def15,A1;
    A3: now
      set e'=1_(G./.H);
      reconsider e=e' as Element of G./.N by Def14;
      take e;
      let h be Element of G./.N;
      reconsider h'=h as Element of G./.H by Def14;
      h*e = h'*e' by Def15 .= h' by GROUP_1:def 5;
      hence h * e = h;
      e*h = e'*h' by Def15 .= h' by GROUP_1:def 5;
      hence e * h = h;
      set g=h'"; set g'=g;
      reconsider g as Element of G./.N by Def14;
      take g;
      h*g = h'*g' by Def15 .= 1_(G./.H) by GROUP_1:def 6;
      hence h * g = e;
      g*h = g'*h' by Def15 .= 1_(G./.H) by GROUP_1:def 6;
      hence g * h = e;
    end;
    A4: now
      let x,y,z be Element of G./.N;
      reconsider x'=x,y'=y,z'=z as Element of G';
      A5: (x'*y')*z' = (x*y)*z;
      x'*(y'*z') = x*(y*z);
      hence (x*y)*z = x*(y*z) by A2,GROUP_1:def 4,A5;
    end;
    now
      let G' be Group;
      let a be Action of O, the carrier of G';
      assume A6: a = the action of G./.N;
      assume A7: the HGrStr of G' = the HGrStr of G./.N;
      now
        let o be Element of O;
        assume A8: o in O;
        A9: a.o = {[A,B] where A,B is Element of Cosets N: ex g,h
          being Element of G st g in A & h in B & h = (G^o).g} by A8,A6,Def16;
        a.o in Funcs(Cosets N, Cosets N) by A8,A6,FUNCT_2:7; then
        consider f be Function such that
        A10: a.o = f & dom f = Cosets N & rng f c= Cosets N by FUNCT_2:def 2;
        reconsider f as Function of the carrier of G',the carrier of G'
          by A7,A10,FUNCT_2:4;
        now
          let A1,A2 be Element of G';
          set A3=A1*A2;
          set B1=f.A1,B2=f.A2,B3=f.A3;
          A11: [A1,B1] in f & [A2,B2] in f & [A3,B3] in f by A10,A7,FUNCT_1:8;
          consider A1',B1' be Element of Cosets N such that
          A12: [A1,B1] = [A1',B1'] & ex g1,h1 being Element of G
            st g1 in A1' & h1 in B1' & h1 = (G^o).g1 by A11,A10,A9;
          consider A2',B2' be Element of Cosets N such that
          A13: [A2,B2] = [A2',B2'] & ex g2,h2 being Element of G
            st g2 in A2' & h2 in B2' & h2 = (G^o).g2 by A11,A10,A9;
          consider A3',B3' be Element of Cosets N such that
          A14: [A3,B3] = [A3',B3'] & ex g3,h3 being Element of G
            st g3 in A3' & h3 in B3' & h3 = (G^o).g3 by A11,A10,A9;
          consider g1,h1 be Element of G such that
          A15: g1 in A1' & h1 in B1' & h1 = (G^o).g1 by A12;
          consider g2,h2 be Element of G such that
          A16: g2 in A2' & h2 in B2' & h2 = (G^o).g2 by A13;
          consider g3,h3 be Element of G such that
          A17: g3 in A3' & h3 in B3' & h3 = (G^o).g3 by A14;
          reconsider A1',A2',A3',B1',B2',B3' as Element of Cosets H by Def14;
          A18: A2' = g2 * H & B2' = h2 * H by Lm9,A16;
          A19: A3' = g3 * H & B3' = h3 * H by Lm9,A17;
          reconsider A1',A2',A3',B1',B2',B3' as Element of G./.H;
          A20: A2=g2 * H & B2=h2 * H by A18,A13,ZFMISC_1:33;
          A21: A3=g3 * H & B3=h3 * H by A19,A14,ZFMISC_1:33;
          A22: @((nat_hom H).g1)=(nat_hom H).g1;
          A23: @((nat_hom H).g2)=(nat_hom H).g2;
          A24: (nat_hom H).g1=g1*H by GROUP_6:def 9;
          A25: (nat_hom H).g2=g2*H by GROUP_6:def 9;
          A1*A2 = (the mult of G').(A1',A2') by A20,A18,A12,ZFMISC_1:33
          .= @(A1'*A2') by Def15,A7 .= @A1' * @A2' by GROUP_6:25; then
          A1*A2 = (g1 * H)*(g2 * H) by A15,A18,Lm9
          .= ((nat_hom H).g1)*((nat_hom H).g2) by A22,A23,A24,A25,GROUP_6:24
          .= (nat_hom H).(g1*g2) by GROUP_6:def 7
          .= (g1*g2) * H by GROUP_6:def 9; then
          g3 * H = (g1*g2) * H by A19,A14,ZFMISC_1:33; then
          g3" * (g1*g2) in H by GROUP_2:137; then
          g3" * (g1*g2) in the carrier of H by STRUCT_0:def 5; then
          g3" * (g1*g2) in N by STRUCT_0:def 5; then
          (G^o).(g3" * (g1*g2)) in N by Lm10; then
          (G^o).(g3") * ((G^o).(g1*g2)) in N by GROUP_6:def 7; then
          (G^o).(g3") * (((G^o).g1)*(G^o).g2) in N by GROUP_6:def 7; then
          h3" * (h1*h2) in N by GROUP_6:41,A15,A16,A17; then
          h3" * (h1*h2) in the carrier of N by STRUCT_0:def 5; then
          A26: h3" * (h1*h2) in H by STRUCT_0:def 5;
          A27: @((nat_hom H).h1)=(nat_hom H).h1;
          A28: @((nat_hom H).h2)=(nat_hom H).h2;
          A29: (nat_hom H).h1=h1*H by GROUP_6:def 9;
          A30: (nat_hom H).h2=h2*H by GROUP_6:def 9;
          B1*B2 = (the mult of G').(B1',B2') by A18,A20,A12,ZFMISC_1:33
          .= @(B1'*B2') by Def15,A7 .= @B1' * @B2' by GROUP_6:25; then
          B1*B2 = (h1 * H)*(h2 * H) by Lm9,A15,A18
          .= ((nat_hom H).h1)*((nat_hom H).h2) by A27,A28,A29,A30,GROUP_6:24
          .= (nat_hom H).(h1*h2) by GROUP_6:def 7
          .= (h1*h2) * H by GROUP_6:def 9;
          hence f.A3 = f.A1 * f.A2 by A26,A21,GROUP_2:137;
        end;
        hence a.o is Homomorphism of G',G' by A10,GROUP_6:def 7;
      end;
      hence a is distributive by Def4;
    end;
    hence thesis by A4,GROUP_1:def 4,A3,GROUP_1:def 3,Def5;
  end;
end;

:: ALG I.4.2 Definition 3

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  let f be Function of G, H;
  attr f is homomorphic means :Def18:
    for o being Element of O, g being Element of G
      holds f.((G^o).g) = (H^o).(f.g);
end;

registration
  let O be set;
  let G,H be GroupWithOperators of O;
  cluster multiplicative homomorphic Function of G, H;
  existence
  proof
    set f = 1:(G,H);
    reconsider f as Function of G, H;
    take f;
    thus f is multiplicative;
    let o be Element of O;
    let g be Element of G;
    (H^o).(f.g) = (H^o).(1_H) by GROUP_6:def 8 .= 1_H by GROUP_6:40;
    hence f.((G^o).g) = (H^o).(f.g) by GROUP_6:def 8;
  end;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  mode Homomorphism of G, H is multiplicative homomorphic Function of G, H;
end;

:: like GROUP_6:48
Lm11:
  for O being set, G,H,I being GroupWithOperators of O,
      h being Homomorphism of G, H
  for h1 being Homomorphism of H, I holds
  h1 * h is Homomorphism of G,I
proof
  let O be set;
  let G,H,I be GroupWithOperators of O;
  let h be Homomorphism of G, H;
  let h1 be Homomorphism of H, I;
  reconsider f = h1 * h as Function of G, I;
  now
    let o be Element of O;
    let g be Element of G;
    thus f.((G^o).g) = h1.(h.((G^o).g)) by FUNCT_2:21
    .= h1.((H^o).(h.g)) by Def18
    .= (I^o).(h1.(h.g)) by Def18
    .= (I^o).(f.g) by FUNCT_2:21;
  end;
  hence thesis by Def18;
end;

definition
  let O be set;
  let G,H,I be GroupWithOperators of O;
  let h be Homomorphism of G, H;
  let h1 be Homomorphism of H, I;
  redefine func h1 * h -> Homomorphism of G,I;
  correctness by Lm11;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  let h be Homomorphism of G,H;
  attr h is being_monomorphism means :Def19:
  h is one-to-one;
  attr h is being_epimorphism means :Def20:
  rng h = the carrier of H;
end;

notation
  let O be set;
  let G,H be GroupWithOperators of O;
  let h be Homomorphism of G,H;
  synonym h is_monomorphism for h is being_monomorphism;
  synonym h is_epimorphism for h is being_epimorphism;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  let h be Homomorphism of G,H;
  attr h is being_isomorphism means :Def21:
  h is_epimorphism & h is_monomorphism;
end;

notation
  let O be set;
  let G,H be GroupWithOperators of O;
  let h be Homomorphism of G,H;
  synonym h is_isomorphism for h is being_isomorphism;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  pred G,H are_isomorphic means :Def22:
  ex h being Homomorphism of G,H st h is_isomorphism;
  reflexivity
  proof
    let G be GroupWithOperators of O;
    reconsider G'=G as Group;
    set h = id the carrier of G';
    now
      let o be Element of O;
      let g be Element of G;
      h.((G^o).g) = (G^o).g & h.g = g by FUNCT_1:34;
      hence h.((G^o).g) = (G^o).(h.g);
    end; then
    reconsider h as Homomorphism of G, G by GROUP_6:47,Def18;
    take h;
    A1: h is_monomorphism by Def19;
    rng h = the carrier of G by RELAT_1:71; then
    A2: h is_epimorphism by Def20;
    thus h is_isomorphism by A1,A2,Def21;
  end;
end;

:: like GROUP_6:77
Lm12: for O being set, G,H being GroupWithOperators of O holds
    G,H are_isomorphic implies H,G are_isomorphic
proof
  let O be set;
  let G,H be GroupWithOperators of O;
  assume G,H are_isomorphic; then
  consider f be Homomorphism of G,H such that
  A1: f is_isomorphism by Def22;
  f is_epimorphism & f is_monomorphism by A1,Def21; then
  A2: rng f = the carrier of H & f is one-to-one by Def19,Def20;
  A3: dom f = the carrier of G by FUNCT_2:def 1;
  set f' = f";
  A4: dom f' = the carrier of H & rng f' = the carrier of G
    by A2,A3,FUNCT_1:55; then
  reconsider f' as Function of H,G by FUNCT_2:3;
A5:now
    let h1,h2 be Element of H;
    set g1=f'.h1; set g2=f'.h2;
    A6: f.g1=h1 & f.g2=h2 by A2,FUNCT_1:57;
    thus f'.(h1*h2) = f'.(f.(g1*g2)) by A6,GROUP_6:def 7
    .= f'.h1 * f'.h2 by A2,A3,FUNCT_1:56;
  end;
  now
    let o be Element of O;
    let h be Element of H;
    set g=f'.h;
    thus f'.((H^o).h) = f'.((H^o).(f.g)) by A2,FUNCT_1:57
    .= f'.(f.((G^o).g)) by Def18
    .= (G^o).(f'.h) by A2,A3,FUNCT_1:56;
  end; then
  reconsider f' as Homomorphism of H,G by A5,GROUP_6:def 7,Def18;
  f' is one-to-one by A2,FUNCT_1:62; then
  A7: f' is_epimorphism & f' is_monomorphism by A4,Def19,Def20;
  take f';
  thus thesis by A7,Def21;
end;

definition
  let O be set, G,H be GroupWithOperators of O;
  redefine pred G,H are_isomorphic;
  symmetry by Lm12;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func nat_hom N -> Homomorphism of G, G./.N means :Def23:
  for H being strict normal Subgroup of G st H = the HGrStr of N
    holds it = nat_hom H;
  existence
  proof
    set H = the HGrStr of N;
    reconsider H as strict normal Subgroup of G by Lm7;
    set IT = nat_hom H;
    reconsider K = G./.N as GroupWithOperators of O;
    A1: Cosets H = Cosets N by Def14;
    reconsider IT' = IT as Function of G, K by A1;
A2: now
      let a, b be Element of G;
      IT'.(a * b) = IT.a * IT.b by GROUP_6:def 7
      .= IT'.a * IT'.b by Def15;
      hence IT'.(a * b) = IT'.a * IT'.b;
    end;
    now
      let o be Element of O;
      let g be Element of G;
      per cases;
      suppose A3: O<>{};
        (the action of K).o in Funcs(the carrier of K, the carrier of K)
          by A3,FUNCT_2:7; then
        consider f be Function such that
        A4: f=(the action of K).o & dom f = the carrier of K &
          rng f c= the carrier of K by FUNCT_2:def 2;
        A5: (K^o).(IT'.g) = f.(IT'.g) by A3,A4,Def6;
        A6: f = {[A,B] where A,B is Element of Cosets N:
          ex g,h being Element of G st g in A & h in B & h = (G^o).g}
          by A4,A3,Def16;
        [IT'.g, f.(IT'.g)] in f by A4,FUNCT_1:def 4; then
        consider A,B be Element of Cosets N such that
        A7: [IT'.g, f.(IT'.g)] = [A,B] &
          ex g,h being Element of G st g in A & h in B & h = (G^o).g by A6;
        A8: IT'.g = A & f.(IT'.g) = B by A7,ZFMISC_1:33;
        consider g',h' be Element of G such that
        A9: g' in A & h' in B & h' = (G^o).g' by A7;
        reconsider A,B as Element of Cosets H by Def14;
        A10: A = g' * H & B = h' * H by Lm9,A9;
        g * H = g' * H by A8,A10,GROUP_6:def 9; then
        g'" * g in H by GROUP_2:137; then
        g'" * g in the carrier of N by STRUCT_0:def 5; then
        g'" * g in N by STRUCT_0:def 5; then
        (G^o).(g'" * g) in N by Lm10; then
        (G^o).(g'" * g) in the carrier of N by STRUCT_0:def 5; then
        A11: (G^o).(g'" * g) in H by STRUCT_0:def 5;
        A12: (G^o).(g'" * g) = (G^o).(g'") * (G^o).g by GROUP_6:def 7
        .= ((G^o).g')" * (G^o).g by GROUP_6:41;
        IT'.((G^o).g) = ((G^o).g) * H by GROUP_6:def 9
        .= ((G^o).g') * H by A12,A11,GROUP_2:137
        .= B by Lm9,A9;
        hence IT'.((G^o).g) = (K^o).(IT'.g) by A7,ZFMISC_1:33,A5;
      end;
      suppose O={}; then
        G^o = id the carrier of G & K^o = id the carrier of K by Def6; then
        (G^o).g = g & (K^o).(IT'.g) = IT'.g by FUNCT_1:35;
        hence IT'.((G^o).g) = (K^o).(IT'.g);
      end;
    end; then
    reconsider IT' as Homomorphism of G, K by A2,Def18,GROUP_6:def 7;
    reconsider IT' as Homomorphism of G, G./.N;
    take IT';
    let H be strict normal Subgroup of G;
    assume H = the HGrStr of N;
    hence IT' = nat_hom H;
  end;
  uniqueness
  proof
    let IT1,IT2 be Homomorphism of G, G./.N;
    assume A13: for H being strict normal Subgroup of G st H = the HGrStr of N
      holds IT1 = nat_hom H;
    assume A14: for H being strict normal Subgroup of G st H = the HGrStr of N
      holds IT2 = nat_hom H;
    reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
    IT1 = nat_hom H & IT2 = nat_hom H by A13,A14;
    hence thesis;
  end;
end;

:: like GROUP_6:40
Lm13: for O being set, G,H being GroupWithOperators of O,
  g being Homomorphism of G,H holds g.(1_G)=1_H
proof
  let O be set;
  let G,H be GroupWithOperators of O;
  let g be Homomorphism of G,H;
  g.(1_G) = g.(1_G * 1_G) by GROUP_1:def 5
  .= g.(1_G) * g.(1_G) by GROUP_6:def 7;
  hence thesis by GROUP_1:15;
end;

:: like GROUP_6:41
Lm14: for O being set, G,H being GroupWithOperators of O,
  a being Element of G, g being Homomorphism of G,H
  holds g.(a")=(g.a)"
proof
  let O be set;
  let G,H be GroupWithOperators of O;
  let a be Element of G;
  let g be Homomorphism of G,H;
  g.(a") * g.a = g.(a" * a) by GROUP_6:def 7
  .= g.(1_G) by GROUP_1:def 6 .= 1_H by Lm13;
  hence thesis by GROUP_1:20;
end;

:: like GROUP_2:61
Lm15: for O being set, G being GroupWithOperators of O, A being Subset of G
    st A <> {} &
      (for g1,g2 being Element of G st g1 in A & g2 in A holds g1 * g2 in A) &
      (for g being Element of G st g in A holds g" in A) &
      (for o being Element of O, g being Element of G
         st g in A holds (G^o).g in A) holds
    ex H being strict StableSubgroup of G st the carrier of H = A
proof
  let O be set;
  let G be GroupWithOperators of O;
  let A be Subset of G;
  assume A1: A <> {};
  assume A2: for g1,g2 being Element of G st g1 in A & g2 in A
    holds g1 * g2 in A;
  assume A3: for g being Element of G st g in A holds g" in A;
  assume A4: for o being Element of O, g being Element of G st g in A
    holds (G^o).g in A;
  consider H' be strict Subgroup of G such that
  A5: the carrier of H' = A by A1,A2,A3,GROUP_2:61;
  set A' = the carrier of H';
  set m' = the mult of H';
  A6: now
    let H be non empty HGrWOpStr over O;
    let a' be Action of O,A';
    assume A7: H = HGrWOpStr (#A',m',a'#);
    now
      let x,y,z be Element of H;
      reconsider x'=x,y'=y,z'=z as Element of H' by A7;
      (x*y)*z = (x'*y')*z' by A7
      .= x'*(y'*z') by GROUP_1:def 4;
      hence (x*y)*z = x*(y*z) by A7;
    end;
    hence H is associative by GROUP_1:def 4;
    now
      set e'=1_H';
      reconsider e=e' as Element of H by A7;
      take e;
      let h be Element of H;
      reconsider h'=h as Element of H' by A7;
      h*e = h'*e' by A7 .= h' by GROUP_1:def 5;
      hence h * e = h;
      e*h = e'*h' by A7 .= h' by GROUP_1:def 5;
      hence e * h = h;
      set g'=h'";
      reconsider g=g' as Element of H by A7;
      take g;
      h*g = h'*g' by A7 .= 1_H' by GROUP_1:def 6;
      hence h * g = e;
      g*h = g'*h' by A7 .= 1_H' by GROUP_1:def 6;
      hence g * h = e;
    end;
    hence H is Group-like by GROUP_1:def 3;
  end;
  per cases;
  suppose A8: O is empty;
    set a' = [:{},{id A'}:];
    reconsider a' as Action of O,A' by A8,Lm2;
    set H = HGrWOpStr (#A',m',a'#);
    reconsider H as (non empty HGrWOpStr over O) by STRUCT_0:def 1;
    now
      let G' be Group;
      let a be Action of O, the carrier of G';
      assume a = the action of H;
      assume the HGrStr of G' = the HGrStr of H;
      for o being Element of O st o in O
        holds a.o is Homomorphism of G', G' by A8;
      hence a is distributive by Def4;
    end; then
    reconsider H as GroupWithOperators of O by A6,Def5;
    A9: the carrier of H c= the carrier of G &
    the mult of H = (the mult of G)||the carrier of H by GROUP_2:def 5; then
    A10: H is Subgroup of G by GROUP_2:def 5;
    now
      let o be Element of O;
      A11: now let x,y be set;
        assume [x,y] in id the carrier of H; then
        A12: x in the carrier of H & x=y by RELAT_1:def 10; then
        [x,y] in id the carrier of G by A9,RELAT_1:def 10;
        hence [x,y] in (id the carrier of G)|the carrier of H
          by A12,RELAT_1:def 11;
      end;
      A13: now let x,y be set;
        assume [x,y] in (id the carrier of G)|the carrier of H; then
        A14: x in the carrier of H & [x,y] in id the carrier of G
          by RELAT_1:def 11; then
        x in the carrier of G & x=y by RELAT_1:def 10;
        hence [x,y] in id the carrier of H by A14,RELAT_1:def 10;
      end;
      H^o = id the carrier of H by Def6,A8
      .= (id the carrier of G)|the carrier of H by A11,A13,RELAT_1:def 2;
      hence H^o = (G^o)|the carrier of H by A8,Def6;
    end; then
    reconsider H as strict StableSubgroup of G by A10,Def7;
    take H;
    thus the carrier of H = A by A5;
  end;
  suppose A15: O is not empty;
    set a' = {[o,(G^o)|A'] where o is Element of O : not contradiction};
    now let x be set;
      assume x in a'; then
      consider o be Element of O such that
      A16: x=[o,(G^o)|A'];
      thus ex y1,y2 being set st x = [y1,y2] by A16;
    end; then
    reconsider a' as Relation by RELAT_1:def 1;
    A17: now
      A18: now let x be set;
        assume x in O; then
        reconsider o=x as Element of O;
        reconsider y = (G^o)|A' as set;
        take y;
        thus [x,y] in a';
      end;
      now let x be set;
        given y be set such that
        A19: [x,y] in a';
        consider o be Element of O such that
        A20: [x,y] = [o,(G^o)|A'] by A19;
        o in O by A15;
        hence x in O by A20,ZFMISC_1:33;
      end;
      hence dom a' = O by A18,RELAT_1:def 4;
    end;
    now
      let x,y1,y2 be set;
      assume [x,y1] in a'; then
      consider o1 be Element of O such that
      A21: [x,y1]=[o1,(G^o1)|A'];
      assume [x,y2] in a'; then
      consider o2 be Element of O such that
      A22: [x,y2]=[o2,(G^o2)|A'];
      x=o1 & y1=(G^o1)|A' & x=o2 & y2=(G^o2)|A' by A21,A22,ZFMISC_1:33;
      hence y1 = y2;
    end; then
    reconsider a' as Function by FUNCT_1:def 1;
    now let y be set;
      assume y in rng a'; then
      consider x be set such that
      A23: x in dom a' & y = a'.x by FUNCT_1:def 5;
      [x,y] in a' by A23,FUNCT_1:8; then
      consider o be Element of O such that
      A24: [x,y]=[o,(G^o)|A'];
      now
        reconsider f = (G^o)|A' as Function;
        take f;
        thus y = f by A24,ZFMISC_1:33;
        A25: A' c= the carrier of G by GROUP_2:def 5;
        A26: dom((G^o)|A') = dom((G^o)*(id A')) by RELAT_1:94
        .= dom(G^o) /\ A' by FUNCT_1:37
        .= (the carrier of G) /\ A' by FUNCT_2:def 1;
        hence A27: dom f = A' by A25,XBOOLE_1:28;
        now let y be set;
          A28: dom f = dom((G^o)*(id A')) by RELAT_1:94;
          assume y in rng f; then
          consider x be set such that
          A29: x in dom f & y = f.x by FUNCT_1:def 5;
          A30: y = ((G^o)*(id A')).x by A29,RELAT_1:94
          .= (G^o).((id A').x) by A28,A29,FUNCT_1:22
          .= (G^o).x by FUNCT_1:35,A29,A27;
          reconsider x as Element of G by A26,A29;
          thus y in A' by A30,A4,A5,A27,A29;
        end;
        hence rng f c= A' by TARSKI:def 3;
      end;
      hence y in Funcs(A',A') by FUNCT_2:def 2;
    end; then
    rng a' c= Funcs(A',A') by TARSKI:def 3; then
    reconsider a' as Action of O,A' by A17,FUNCT_2:4;
    reconsider H = HGrWOpStr (#A',m',a'#) as (non empty HGrWOpStr over O)
      by STRUCT_0:def 1;
    A31: H is Group-like by A6;
    the carrier of H c= the carrier of G &
    the mult of H = (the mult of G)||the carrier of H by GROUP_2:def 5; then
    A32: H is Subgroup of G by GROUP_2:def 5,A31;
    now
      let G' be Group;
      let a be Action of O, the carrier of G';
      assume A33: a = the action of H;
      assume A34: the HGrStr of G' = the HGrStr of H;
      now
        let o be Element of O;
        assume o in O; then
        A35: o in dom a by FUNCT_2:def 1; then
        a.o in rng a by FUNCT_1:12; then
        consider f be Function such that
        A36: a.o = f & dom f = the carrier of G'
          & rng f c= the carrier of G' by FUNCT_2:def 2;
        [o,a.o] in a' by A33,A35,FUNCT_1:8; then
        consider o' be Element of O such that
        A37: [o,a.o] = [o',(G^o')|A'];
        A38: o=o' & a.o=(G^o')|A' by A37,ZFMISC_1:33;
        reconsider f as Function of G',G' by A36,FUNCT_2:4;
        now
          let a', b' be Element of G';
          a'*b' in the carrier of H' by A34; then
          A39: a'*b' in dom id A' by RELAT_1:71;
          a' in the carrier of H' & b' in the carrier of H' by A34; then
          A40: a' in dom id A' & b' in dom id A' by RELAT_1:71;
          reconsider a=a',b=b' as Element of H by A34;
          reconsider g1=a,g2=b as Element of G by GROUP_2:51;
          reconsider h1=(G^o).g1,h2=(G^o).g2 as Element of H by A4,A5;
          A41: f.a' = ((G^o)*(id A')).a' by RELAT_1:94,A38,A36
          .= (G^o).((id A').a') by A40,FUNCT_1:23
          .= h1 by FUNCT_1:35;
          A42: f.b' = ((G^o)*(id A')).b' by RELAT_1:94,A38,A36
          .= (G^o).((id A').b') by A40,FUNCT_1:23
          .= h2 by FUNCT_1:35;
          thus f.(a' * b') = ((G^o)*(id A')).(a'*b') by RELAT_1:94,A38,A36
          .= (G^o).((id A').(a'*b')) by A39,FUNCT_1:23
          .= (G^o).(a*b) by A34,FUNCT_1:35
          .= (G^o).(g1*g2) by GROUP_2:52,A32
          .= ((G^o).g1) * ((G^o).g2) by GROUP_6:def 7
          .= h1 * h2 by GROUP_2:52,A32
          .= f.a' * f.b' by A41,A42,A34;
        end;
        hence a.o is Homomorphism of G',G' by A36,GROUP_6:def 7;
      end;
      hence a is distributive by Def4;
    end; then
    reconsider H as GroupWithOperators of O by A6,Def5;
    now
      let o be Element of O;
      o in O by A15; then
      A43: o in dom a' by FUNCT_2:def 1;
      [o,a'.o] in a' by A43,FUNCT_1:8; then
      consider o' be Element of O such that
      A44: [o,a'.o] = [o',(G^o')|A'];
      o=o' & a'.o=(G^o')|A' by A44,ZFMISC_1:33;
      hence H^o = (G^o)|the carrier of H by A15,Def6;
    end; then
    reconsider H as strict StableSubgroup of G by A32,Def7;
    take H;
    thus the carrier of H = A by A5;
  end;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  let g be Homomorphism of G, H;
  func Ker g -> strict StableSubgroup of G means :Def24:
  the carrier of it = {a where a is Element of G: g.a = 1_H};
  existence
  proof
    defpred P[set] means g.$1 = 1_H;
    reconsider A = {a where a is Element of G: P[a]} as Subset of G
      from DOMAIN_1:sch 7;
    g.(1_G) = 1_H by Lm13; then
    A1: 1_G in A;
    A2: now let a,b be Element of G;
      assume that A3: a in A and A4: b in A;
      A5: ex a1 being Element of G st a1 = a & g.a1 = 1_H by A3;
      A6: ex b1 being Element of G st b1 = b & g.b1 = 1_H by A4;
      g.(a * b) = g.a * g.b by GROUP_6:def 7
      .= 1_H by A5,A6,GROUP_1:def 5;
      hence a * b in A;
    end;
    A7: now let a be Element of G;
      assume a in A;
      then ex a1 being Element of G st a1 = a & g.a1 = 1_H;
      then g.(a") = (1_H)" by Lm14 .= 1_H by GROUP_1:16;
      hence a" in A;
    end;
    now
      let o be Element of O;
      let a be Element of G;
      assume a in A; then
      A8: ex a1 being Element of G st a1 = a & g.a1 = 1_H;
      g.((G^o).a) = (H^o).(1_H) by A8,Def18 .= 1_H by GROUP_6:40;
      hence (G^o).a in A;
    end; then
    consider B be strict StableSubgroup of G such that
    A9: the carrier of B = A by A1,A2,A7,Lm15;
    take B;
    thus thesis by A9;
  end;
  uniqueness by Lm5;
end;

registration
  let O be set;
  let G,H be GroupWithOperators of O;
  let g be Homomorphism of G, H;
  cluster Ker g -> normal;
  correctness
  proof
    now
      let N be strict Subgroup of G;
      assume A1: N = the HGrStr of Ker g;
      reconsider G'=G,H'=H as Group;
      reconsider g'=g as Homomorphism of G', H';
      the carrier of Ker g' = {a where a is Element of G: g.a = 1_H}
        by GROUP_6:def 10; then
      the carrier of Ker g' = the carrier of N by A1,Def24;
      hence N is normal by GROUP_2:68;
    end;
    hence Ker g is normal by Def10;
  end;
end;

Lm16: for O being set, G being GroupWithOperators of O,
      H being StableSubgroup of G holds
  the HGrStr of H is strict Subgroup of G
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  reconsider H'=the HGrStr of H as non empty HGrStr;
  now
    set e=1_H;
    reconsider e'=e as Element of H';
    take e';
    let h' be Element of H';
    reconsider h=h' as Element of H;
    h'*e' = h*e .= h by GROUP_1:def 5;
    hence h' * e' = h';
    e'*h' = e*h .= h by GROUP_1:def 5;
    hence e' * h' = h';
    set g=h";
    reconsider g'=g as Element of H';
    take g';
    h'*g' = h*g .= 1_H by GROUP_1:def 6;
    hence h' * g' = e';
    g'*h' = g*h .= 1_H by GROUP_1:def 6;
    hence g' * h' = e';
  end; then
  reconsider H' as Group-like (non empty HGrStr) by GROUP_1:def 3;
  H is Subgroup of G by Def7; then
  the carrier of H' c= the carrier of G &
    the mult of H' = (the mult of G)||the carrier of H' by GROUP_2:def 5;
  hence thesis by GROUP_2:def 5;
end;

Lm17: for O being set, G,H being GroupWithOperators of O,
      G' being strict StableSubgroup of G,
      f being Homomorphism of G,H
  holds ex H' being strict StableSubgroup of H
        st the carrier of H' = f.:(the carrier of G')
proof
  let O be set;
  let G,H be GroupWithOperators of O;
  let G' be strict StableSubgroup of G;
  let f be Homomorphism of G,H;
  reconsider G'' = the HGrStr of G' as strict Subgroup of G by Lm16;
  set A = {f.g where g is Element of G:g in G''};
  1_G in G'' by GROUP_2:55; then
  f.(1_G) in A; then
  reconsider A as non empty set;
  now let x be set;
    assume x in A; then
    consider g be Element of G such that
    A1: x=f.g & g in G'';
    thus x in the carrier of H by A1;
  end; then
  reconsider A as Subset of H by TARSKI:def 3;
  A2: now let h1,h2 be Element of H;
    assume A3: h1 in A & h2 in A;
    consider a be Element of G such that
    A4: h1=f.a & a in G'' by A3;
    consider b be Element of G such that
    A5: h2=f.b & b in G'' by A3;
    A6: f.(a*b) = h1*h2 by A4,A5,GROUP_6:def 7;
    a*b in G'' by A4,A5,GROUP_2:59;
    hence h1*h2 in A by A6;
  end;
  A7: now let h be Element of H;
    assume A8: h in A;
    consider g be Element of G such that
    A9: h=f.g & g in G'' by A8;
    A10: g" in G'' by A9,GROUP_2:60;
    h" = f.(g") by A9,Lm14;
    hence h" in A by A10;
  end;
  now
    let o be Element of O;
    let h be Element of H;
    assume h in A; then
    consider g be Element of G such that
    A11: h=f.g & g in G'';
    A12: (H^o).h = f.((G^o).g) by Def18,A11;
    g in the carrier of G'' by A11,STRUCT_0:def 5; then
    g in G' by STRUCT_0:def 5; then
    (G^o).g in G' by Lm10; then
    (G^o).g in the carrier of G' by STRUCT_0:def 5; then
    (G^o).g in G'' by STRUCT_0:def 5;
    hence (H^o).h in A by A12;
  end; then
  consider H'' be strict StableSubgroup of H such that
  A13: the carrier of H'' = A by Lm15,A2,A7;
  reconsider H'=the HGrStr of H'' as strict Subgroup of H by Lm16;
  take H'';
  now
    let h be Element of H;
    set R = f;
    reconsider R as Relation of the carrier of G, the carrier of H;
    hereby
      assume h in A; then
      consider g be Element of G such that
      A14: h=f.g & g in G'';
      dom f = the carrier of G by FUNCT_2:def 1; then
      A15: [g,h] in f by A14,FUNCT_1:8;
      g in the carrier of G' by A14,STRUCT_0:def 5;
      hence h in f.:(the carrier of G') by A15,RELSET_1:52;
    end;
    assume A16: h in f.:(the carrier of G');
    consider g be Element of G such that
    A17: [g,h] in R & g in the carrier of G' by A16,RELSET_1:52;
    A18: f.g=h by A17,FUNCT_1:8;
    g in G'' by A17,STRUCT_0:def 5;
    hence h in A by A18;
  end;
  hence the carrier of H'' = f.:(the carrier of G') by A13,SUBSET_1:8;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  let g be Homomorphism of G, H;
  func Image g -> strict StableSubgroup of H means :Def25:
  the carrier of it = g.:(the carrier of G);
  existence
  proof
    reconsider G' = the HGrWOpStr of G as strict StableSubgroup of G by Lm4;
    consider H' be strict StableSubgroup of H such that
    A1: the carrier of H' = g.:(the carrier of G') by Lm17;
    take H';
    thus the carrier of H' = g.:(the carrier of G) by A1;
  end;
  uniqueness by Lm5;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  func carr(H) -> Subset of G equals
  the carrier of H;
  coherence
  proof
    reconsider H'=H as Subgroup of G by Def7;
    carr(H') is Subset of G;
    hence thesis;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be StableSubgroup of G;
  func H1 * H2 -> Subset of G equals
  carr H1 * carr H2;
  coherence;
end;

:: like GROUP_2:55
Lm18: for O being set, G being GroupWithOperators of O,
  H being StableSubgroup of G holds 1_G in H
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  H is Subgroup of G by Def7;
  hence 1_G in H by GROUP_2:55;
end;

:: like GROUP_2:59
Lm19: for O being set, G being GroupWithOperators of O,
      H being StableSubgroup of G,
      g1,g2 being Element of G holds
 g1 in H & g2 in H implies g1 * g2 in H
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  let g1,g2 be Element of G;
  A1: H is Subgroup of G by Def7;
  assume g1 in H & g2 in H;
  hence g1 * g2 in H by A1,GROUP_2:59;
end;

:: like GROUP_2:60
Lm20: for O being set, G being GroupWithOperators of O,
      H being StableSubgroup of G, g being Element of G holds
 g in H implies g" in H
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  let g be Element of G;
  A1: H is Subgroup of G by Def7;
  assume g in H;
  hence g" in H by A1,GROUP_2:60;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be StableSubgroup of G;
  func H1 /\ H2 -> strict StableSubgroup of G means :Def28:
  the carrier of it = carr(H1) /\ carr(H2);
  existence
  proof
    set A = carr(H1) /\ carr(H2);
    1_G in H1 & 1_G in H2 by Lm18; then
    1_G in the carrier of H1 & 1_G in the carrier of H2 by STRUCT_0:def 5;
    then A1: A <> {} by XBOOLE_0:def 3;
    A2: now let g1,g2 be Element of G;
      assume g1 in A & g2 in A; then
      g1 in carr(H1) & g2 in carr(H1) & g1 in carr(H2) & g2 in carr(H2)
        by XBOOLE_0:def 3; then
      g1 in H1 & g2 in H1 & g1 in H2 & g2 in H2 by STRUCT_0:def 5; then
      g1 * g2 in H1 & g1 * g2 in H2 by Lm19; then
      g1 * g2 in carr(H1) & g1 * g2 in carr(H2) by STRUCT_0:def 5;
      hence g1 * g2 in A by XBOOLE_0:def 3;
    end;
    A3: now let g be Element of G;
      assume g in A; then
      g in carr(H1) & g in carr(H2) by XBOOLE_0:def 3; then
      g in H1 & g in H2 by STRUCT_0:def 5; then
      g" in H1 & g" in H2 by Lm20; then
      g" in carr(H1) & g" in carr(H2) by STRUCT_0:def 5;
      hence g" in A by XBOOLE_0:def 3;
    end;
    now
      let o be Element of O;
      let a be Element of G;
      assume a in A; then
      a in carr(H1) & a in carr(H2) by XBOOLE_0:def 3; then
      a in H1 & a in H2 by STRUCT_0:def 5; then
      (G^o).a in H1 & (G^o).a in H2 by Lm10; then
      (G^o).a in carr(H1) & (G^o).a in carr(H2) by STRUCT_0:def 5;
      hence (G^o).a in A by XBOOLE_0:def 3;
    end;
    hence thesis by A1,A2,A3,Lm15;
  end;
  uniqueness by Lm5;
  commutativity;
end;

:: like GROUP_2:66
Lm21: for O being set, G being GroupWithOperators of O,
      H1,H2 being StableSubgroup of G holds
the carrier of H1 c= the carrier of H2 implies H1 is StableSubgroup of H2
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be StableSubgroup of G;
  reconsider H1'=H1,H2'=H2 as Subgroup of G by Def7;
  assume A1: the carrier of H1 c= the carrier of H2; then
  A2: H1' is Subgroup of H2' by GROUP_2:66;
  now
    let o be Element of O;
    thus H1^o = (G^o)|the carrier of H1 by Def7
    .= ((G^o)|the carrier of H2)|the carrier of H1 by A1,RELAT_1:103
    .= (H2^o)|the carrier of H1 by Def7;
  end;
  hence H1 is StableSubgroup of H2 by A2,Def7;
end;

:: like GROUP_4:def 5
definition
  let O be set;
  let G be GroupWithOperators of O;
  let A be Subset of G;
  func the_stable_subgroup_of A -> strict StableSubgroup of G means :Def29:
  A c= the carrier of it &
     (for H being strict StableSubgroup of G st A c= the carrier of H
        holds it is StableSubgroup of H);
  existence
  proof
    defpred P[set] means
      ex H being strict StableSubgroup of G st $1 = carr H & A c= $1;
    consider X be set such that
    A1: for Y being set holds
      Y in X iff Y in bool the carrier of G & P[Y] from XBOOLE_0:sch 1;
    set M = meet X;
    A2: the carrier of G in bool the carrier of G &
    the carrier of the HGrWOpStr of G = the carrier of (Omega).G &
    A c= the carrier of G &
    carr (Omega).G = the carrier of (Omega).G by ZFMISC_1:def 1;
    then A3: the carrier of G in X by A1;
    A4: X <> {} by A1,A2;
    now let Y be set;
      assume Y in X; then
      consider H be strict StableSubgroup of G such that
      A5: Y = carr H and A c= Y by A1;
      1_G in H by Lm18;
      hence 1_G in Y by A5,STRUCT_0:def 5;
    end;
    then A6: M <> {} by A4,SETFAM_1:def 1;
    M c= the carrier of G
    proof consider x be set such that A7: x in X by A3;
      consider H be strict StableSubgroup of G such that
      A8: x = carr H and A c= x by A1,A7;
      let y be set;
      assume y in M;
      then y in carr H & carr H c= the carrier of G by A7,A8,SETFAM_1:def 1;
      hence thesis;
    end;
    then reconsider M as Subset of G;
    A9: now let a,b be Element of G;
      assume A10: a in M & b in M;
      now let Y be set;
        assume A11: Y in X; then
        consider H be strict StableSubgroup of G such that
        A12: Y = carr H and A c= Y by A1;
        a in carr H & b in carr H by A10,A11,A12,SETFAM_1:def 1; then
        a in H & b in H by STRUCT_0:def 5; then
        a * b in H by Lm19;
        hence a * b in Y by A12,STRUCT_0:def 5;
      end;
      hence a * b in M by A4,SETFAM_1:def 1;
    end;
    A13: now let a be Element of G;
      assume A14: a in M;
      now let Y be set;
        assume A15: Y in X; then
        consider H be strict StableSubgroup of G such that
        A16: Y = carr H and A c= Y by A1;
        a in carr H by A14,A15,A16,SETFAM_1:def 1; then
        a in H by STRUCT_0:def 5; then
        a" in H by Lm20;
        hence a" in Y by A16,STRUCT_0:def 5;
      end;
      hence a" in M by A4,SETFAM_1:def 1;
    end;
    now
      let o be Element of O;
      let a be Element of G;
      assume A17: a in M;
      now let Y be set;
        assume A18: Y in X; then
        consider H be strict StableSubgroup of G such that
        A19: Y = carr H and A c= Y by A1;
        a in carr H by A17,A18,A19,SETFAM_1:def 1; then
        a in H by STRUCT_0:def 5; then
        (G^o).a in H by Lm10;
        hence (G^o).a in Y by A19,STRUCT_0:def 5;
      end;
      hence (G^o).a in M by A4,SETFAM_1:def 1;
    end; then
    consider H be strict StableSubgroup of G such that
    A20: the carrier of H = M by A6,A9,A13,Lm15;
    take H;
    now let Y be set;
      assume Y in X;
      then ex H being strict StableSubgroup of G
      st Y = carr H & A c= Y by A1;
      hence A c= Y;
    end;
    hence A c= the carrier of H by A4,A20,SETFAM_1:6;
    let H1 be strict StableSubgroup of G;
    assume A21: A c= the carrier of H1;
    the carrier of H1 = carr H1;
    then the carrier of H1 in X by A1,A21;
    then M c= the carrier of H1 by SETFAM_1:4;
    hence H is StableSubgroup of H1 by A20,Lm21;
  end;
  uniqueness
  proof let H1,H2 be strict StableSubgroup of G;
    assume A c= the carrier of H1 &
     (for H being strict StableSubgroup of G
       st A c= the carrier of H holds H1 is StableSubgroup of H) &
     A c= the carrier of H2 &
     (for H being strict StableSubgroup of G
       st A c= the carrier of H holds H2 is StableSubgroup of H); then
    H1 is StableSubgroup of H2 & H2 is StableSubgroup of H1; then
    H1 is Subgroup of H2 & H2 is Subgroup of H1 by Def7; then
    the carrier of H1 c= the carrier of H2 &
      the carrier of H2 c= the carrier of H1 by GROUP_2:def 5; then
    the carrier of H1 = the carrier of H2 by XBOOLE_0:def 10;
    hence thesis by Lm5;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be StableSubgroup of G;
  func H1 "\/" H2 -> strict StableSubgroup of G equals
  the_stable_subgroup_of (carr H1 \/ carr H2);
  correctness;
end;

begin :: Some Theorems on Groups reformulated for Groups with Operators

reserve x,O for set,
        o for Element of O,
        G,H,I for GroupWithOperators of O,
        A,B for Subset of G,
        N for normal StableSubgroup of G,
        H1,H2,H3 for StableSubgroup of G,
        g1,g2 for Element of G,
        h1,h2 for Element of H1,
        h for Homomorphism of G,H;

:: GROUP_2:49
theorem Th1:
  x in H1 implies x in G
proof
  A1: H1 is Subgroup of G by Def7;
  assume x in H1; hence x in G by A1,GROUP_2:49;
end;

:: GROUP_2:51
theorem Th2:
  h1 is Element of G
proof
  H1 is Subgroup of G by Def7;
  hence h1 is Element of G by GROUP_2:51;
end;

:: GROUP_2:52
theorem Th3:
  h1 = g1 & h2 = g2 implies h1 * h2 = g1 * g2
proof
  A1: H1 is Subgroup of G by Def7;
  assume h1 = g1 & h2 = g2; hence h1 * h2 = g1 * g2 by A1,GROUP_2:52;
end;

:: GROUP_2:53
theorem Th4:
  1_G = 1_H1
proof
  reconsider H1'=H1 as Subgroup of G by Def7;
  1_H1 = 1_H1';
  hence thesis by GROUP_2:53;
end;

:: GROUP_2:55
theorem
  1_G in H1 by Lm18;

:: GROUP_2:57
theorem Th6:
  h1 = g1 implies h1" = g1"
proof
  assume A1: h1 = g1;
  reconsider g' = h1" as Element of G by Th2;
  h1 * h1" = 1_H1 by GROUP_1:def 6;
  then g1 * g' = 1_H1 by A1,Th3
  .= 1_G by Th4;
  hence thesis by GROUP_1:20;
end;

:: GROUP_2:59
theorem
  g1 in H1 & g2 in H1 implies g1 * g2 in H1 by Lm19;

:: GROUP_2:60
theorem
  g1 in H1 implies g1" in H1 by Lm20;

:: GROUP_2:61
theorem
  A <> {} &
    (for g1,g2 st g1 in A & g2 in A holds g1 * g2 in A) &
    (for g1 st g1 in A holds g1" in A) &
    (for o,g1 st g1 in A holds (G^o).g1 in A) implies
  ex H being strict StableSubgroup of G st the carrier of H = A by Lm15;

:: GROUP_2:63
theorem Th10:
  G is StableSubgroup of G
proof
  A1: G is Subgroup of G by GROUP_2:63;
  now
    let o be Element of O;
    dom(G^o) c= the carrier of G;
    hence G^o = (G^o)|the carrier of G by RELAT_1:97;
  end;
  hence G is StableSubgroup of G by A1,Def7;
end;

:: GROUP_2:65
theorem Th11:
  for G1,G2,G3 being GroupWithOperators of O holds
    G1 is StableSubgroup of G2 & G2 is StableSubgroup of G3
      implies G1 is StableSubgroup of G3
proof
  let G1,G2,G3 be GroupWithOperators of O;
  assume A1: G1 is StableSubgroup of G2 & G2 is StableSubgroup of G3;
  A2: G1 is Subgroup of G2 & G2 is Subgroup of G3 by A1,Def7; then
  A3: G1 is Subgroup of G3 by GROUP_2:65;
  now
    let o be Element of O;
    A4: the carrier of G1 c= the carrier of G2 by A2,GROUP_2:def 5;
    G1^o = (G2^o)|the carrier of G1 by A1,Def7
    .= ((G3^o)|the carrier of G2)|the carrier of G1 by A1,Def7
    .= (G3^o)|((the carrier of G2) /\ the carrier of G1) by RELAT_1:100;
    hence G1^o = (G3^o)|the carrier of G1 by A4,XBOOLE_1:28;
  end;
  hence G1 is StableSubgroup of G3 by A3,Def7;
end;

:: GROUP_2:66
theorem
  the carrier of H1 c= the carrier of H2
    implies H1 is StableSubgroup of H2 by Lm21;

:: GROUP_2:67
theorem Th13:
  (for g being Element of G st g in H1 holds g in H2)
    implies H1 is StableSubgroup of H2
proof
  assume A1: for g being Element of G st g in H1 holds g in H2;
  the carrier of H1 c= the carrier of H2
  proof let x;
    assume x in the carrier of H1;
    then reconsider g = x as Element of H1;
    reconsider g as Element of G by Th2;
    g in H1 by STRUCT_0:def 5;
    then g in H2 by A1;
    hence thesis by STRUCT_0:def 5;
  end;
  hence thesis by Lm21;
end;

:: GROUP_2:68
theorem
  for H1,H2 being strict StableSubgroup of G
    st the carrier of H1 = the carrier of H2 holds H1 = H2 by Lm5;

:: GROUP_2:75
theorem Th15:
  (1).G = (1).H1
proof
  A1: (1).H1 is StableSubgroup of G by Th11;
  A2: the carrier of (1).H1 = {1_H1} & the carrier of (1).G = {1_G} by Def8;
  1_H1 = 1_G by Th4;
  hence thesis by A1,A2,Lm5;
end;

:: GROUP_2:77
theorem Th16:
  (1).G is StableSubgroup of H1
proof
  (1).G = (1).H1 by Th15;
  hence thesis;
end;

:: GROUP_2:93
theorem Th17:
  carr H1 * carr H2 = carr H2 * carr H1 implies
    ex H being strict StableSubgroup of G st the carrier of H=carr H1 * carr H2
proof
  A1: H1 is Subgroup of G & H2 is Subgroup of G by Def7;
  assume A2: carr H1 * carr H2 = carr H2 * carr H1;
  A3: carr H1 * carr H2 <> {} by GROUP_2:13;
  A4: now let g1,g2 be Element of G;
    assume that A5: g1 in carr(H1) * carr(H2) and
    A6: g2 in carr(H1) * carr(H2);
    consider a1,b1 be Element of G such that
    A7: g1 = a1 * b1 & a1 in carr(H1) & b1 in carr(H2) by A5;
    consider a2,b2 be Element of G such that
    A8: g2 = a2 * b2 & a2 in carr H1 & b2 in carr H2 by A6;
    A9: g1 * g2 = a1 * b1 * a2 * b2 by A7,A8,GROUP_1:def 4
    .= a1 * (b1 * a2) * b2 by GROUP_1:def 4;
    b1 * a2 in carr H1 * carr H2 by A2,A7,A8;
    then consider a,b be Element of G such that
    A10: b1 * a2 = a * b & a in carr H1 & b in carr H2;
    A11: g1 * g2 = a1 * a * b * b2 by A9,A10,GROUP_1:def 4
    .= a1 * a * (b * b2) by GROUP_1:def 4;
    a1 in H1 & a in H1 by A7,A10,STRUCT_0:def 5; then
    a1 * a in H1 by A1,GROUP_2:59; then
    A12: a1 * a in carr H1 by STRUCT_0:def 5;
    b2 in H2 & b in H2 by A8,A10,STRUCT_0:def 5; then
    b * b2 in H2 by A1,GROUP_2:59; then
    b * b2 in carr H2 by STRUCT_0:def 5;
    hence g1 * g2 in carr H1 * carr H2 by A12,A11;
  end;
  A13: now let g be Element of G;
    assume A14: g in carr H1 * carr H2;
    then consider a,b be Element of G such that
    A15: g = a * b & a in carr H1 & b in carr H2;
    consider b1,a1 be Element of G such that
    A16: a * b = b1 * a1 & b1 in carr H2 & a1 in carr H1 by A2,A14,A15;
    A17: g" = a1" * b1" by A15,A16,GROUP_1:25;
    b1 in H2 & a1 in H1 by A16,STRUCT_0:def 5; then
    b1" in H2 & a1" in H1 by A1,GROUP_2:60; then
    a1" in carr H1 & b1" in carr H2 by STRUCT_0:def 5;
    hence g" in carr H1 * carr H2 by A17;
  end;
  now
    let o be Element of O;
    let g be Element of G;
    assume g in carr H1 * carr H2; then
    consider a,b be Element of G such that
    A18: g = a * b & a in carr H1 & b in carr H2;
    b in H2 & a in H1 by A18,STRUCT_0:def 5; then
    (G^o).b in H2 & (G^o).a in H1 by Lm10; then
    (G^o).b in carr H2 & (G^o).a in carr H1 by STRUCT_0:def 5; then
    ((G^o).a) * ((G^o).b) in carr H1 * carr H2;
    hence (G^o).g in carr H1 * carr H2 by A18,GROUP_6:def 7;
  end;
  hence thesis by A3,A4,A13,Lm15;
end;

:: GROUP_2:97
theorem Th18:
 (for H being StableSubgroup of G st H = H1 /\ H2 holds
    the carrier of H = (the carrier of H1) /\ (the carrier of H2)) &
 for H being strict StableSubgroup of G holds
  the carrier of H = (the carrier of H1) /\ (the carrier of H2) implies
    H = H1 /\ H2
proof
  thus (for H being StableSubgroup of G st H = H1 /\ H2
    holds the carrier of H = (the carrier of H1) /\ (the carrier of H2))
  proof
    let H be StableSubgroup of G;
    assume H = H1 /\ H2;
    hence the carrier of H = carr(H1)/\carr(H2) by Def28
    .= (the carrier of H1)/\(the carrier of H2);
  end;
  let H be strict StableSubgroup of G;
  assume A1: the carrier of H = (the carrier of H1) /\ (the carrier of H2);
  the carrier of H1 = carr(H1) & the carrier of H2 = carr(H2);
  hence H = H1 /\ H2 by A1,Def28;
end;

:: GROUP_2:100
theorem Th19:
  for H being strict StableSubgroup of G holds H /\ H = H
proof
  let H be strict StableSubgroup of G;
  the carrier of H /\ H = carr(H) /\ carr(H) by Def28 .= the carrier of H;
  hence thesis by Lm5;
end;

:: GROUP_2:102
theorem Th20:
  H1 /\ H2 /\ H3 = H1 /\ (H2 /\ H3)
proof
  the carrier of H1 /\ H2 /\ H3 = carr(H1 /\ H2) /\ carr(H3) by Def28
  .= carr(H1) /\ carr(H2) /\ carr(H3) by Def28
  .= carr(H1) /\ (carr(H2) /\ carr(H3)) by XBOOLE_1:16
  .= carr(H1) /\ carr(H2 /\ H3) by Def28
  .= the carrier of H1 /\ (H2 /\ H3) by Def28;
  hence thesis by Lm5;
end;

Lm22:
  for H1 being strict StableSubgroup of G holds
    H1 is StableSubgroup of H2 iff H1 /\ H2 = H1
proof
  let H1 be strict StableSubgroup of G;
  thus H1 is StableSubgroup of H2 implies H1 /\ H2 = H1
  proof assume H1 is StableSubgroup of H2; then
    H1 is Subgroup of H2 by Def7; then
    the carrier of H1 c= the carrier of H2 by GROUP_2:def 5;
     then (the carrier of H1) /\ (the carrier of H2) = the carrier of H1 &
          the carrier of H1 /\ H2 = carr(H1) /\ carr (H2) &
          carr(H1) = the carrier of H1 & carr(H2) = the carrier of H2
                                              by Def28,XBOOLE_1:28;
    hence thesis by Lm5;
   end;
   assume H1 /\ H2 = H1;
    then the carrier of H1 = carr(H1) /\ carr(H2) by Def28
     .= (the carrier of H1) /\ (the carrier of H2);
    then the carrier of H1 c= the carrier of H2 by XBOOLE_1:17;
  hence thesis by Lm21;
end;

:: GROUP_2:103
theorem Th21:
   (1).G /\ H1 = (1).G & H1 /\ (1).G = (1).G
proof
  A1: (1).G is StableSubgroup of H1 by Th16;
  hence (1).G /\ H1 = (1).G by Lm22;
  thus thesis by A1,Lm22;
end;

:: GROUP_2:167
theorem Th22:
  union Cosets N = the carrier of G
proof
  reconsider H = the HGrStr of N as strict normal Subgroup of G by Lm7;
  A1: Cosets N = Cosets H by Def14;
  A2: union Left_Cosets H c= the carrier of G;
  now
    now
      let x;
      assume x in the carrier of G; then
      reconsider a = x as Element of G;
      consider h be Element of H;
      reconsider g = h as Element of G by GROUP_2:51;
      A3: h in H by STRUCT_0:def 5;
      a = a * 1_G by GROUP_1:def 5
      .= a * (g" * g) by GROUP_1:def 6
      .= a * g" * g by GROUP_1:def 4;
      then a in a * g" * H & a * g" * H in Cosets H
        by A3,GROUP_2:def 15,GROUP_2:125;
      hence x in union Cosets H by TARSKI:def 4;
    end;
    hence the carrier of G c= union Cosets H by TARSKI:def 3;
  end;
  hence thesis by A2,A1,XBOOLE_0:def 10;
end;

:: GROUP_3:149
theorem Th23:
  for N1,N2 being strict normal StableSubgroup of G
    ex N being strict normal StableSubgroup of G
    st the carrier of N = carr N1 * carr N2
proof
  let N1,N2 be strict normal StableSubgroup of G;
  set N1' = the HGrStr of N1; set N2' = the HGrStr of N2;
  reconsider N1',N2' as strict normal Subgroup of G by Lm7;
  set A = carr N1' * carr N2';
  set B = carr N1'; set C = carr N2';
  carr N1' * carr N2' = carr N2' * carr N1' by GROUP_3:148;
  then consider H' be strict Subgroup of G such that
  A1: the carrier of H' = A by GROUP_2:93;
  now let a be Element of G;
    thus a * H' = a * N1' * C by A1,GROUP_2:35
                .= N1' * a * C by GROUP_3:140
                .= B * (a * N2') by GROUP_2:36
                .= B * (N2' * a) by GROUP_3:140
                .= H' * a by  GROUP_2:37,A1;
  end;
  then A2: H' is normal Subgroup of G by GROUP_3:140;
  A3: now let g1,g2 be Element of G;
    assume g1 in A & g2 in A; then
    g1 in H' & g2 in H' by A1,STRUCT_0:def 5; then
    g1 * g2 in H' by GROUP_2:59;
    hence g1 * g2 in A by A1,STRUCT_0:def 5;
  end;
  A4: now let g be Element of G;
    assume g in A; then
    g in H' by A1,STRUCT_0:def 5; then
    g" in H' by GROUP_2:60;
    hence g" in A by A1,STRUCT_0:def 5;
  end;
  now
    let o be Element of O;
    let g be Element of G;
    assume g in A; then
    consider a,b be Element of G such that
    A5: g = a * b & a in carr N1 & b in carr N2;
    b in N2 & a in N1 by A5,STRUCT_0:def 5; then
    (G^o).b in N2 & (G^o).a in N1 by Lm10; then
    (G^o).b in carr N2 & (G^o).a in carr N1 by STRUCT_0:def 5; then
    ((G^o).a) * ((G^o).b) in carr N1 * carr N2;
    hence (G^o).g in A by A5,GROUP_6:def 7;
  end; then
  consider H be strict StableSubgroup of G such that
  A6: the carrier of H = A by Lm15,A1,A3,A4;
  for H'' being strict Subgroup of G st H'' = the HGrStr of H
    holds H'' is normal by A2,A1,A6,GROUP_2:68; then
  H is normal by Def10;
  hence thesis by A6;
end;

Lm23: for F1 being FinSequence, y being Element of NAT st y in dom F1 holds
 len F1 - y + 1 is Element of NAT & len F1 - y + 1 >= 1 &
 len F1 - y + 1 <= len F1
 proof let F1 be FinSequence, y be Element of NAT;
   assume A1: y in dom F1;
        now assume len F1 - y + 1 < 0;
       then 1 < 0 - (len F1 - y) by XREAL_1:22;
       then 1 < y - len F1;
       then len F1 + 1 < y & y <= len F1 by A1,FINSEQ_3:27,XREAL_1:22;
       hence contradiction by NAT_1:12;
      end;
    then reconsider n = len F1 - y + 1 as Element of NAT by INT_1:16;
        y + 0 <= len F1 by A1,FINSEQ_3:27;
     then A2: 0 + 1 = 1 & 0 <= len F1 - y by XREAL_1:21;
        n - 1 <= len F1 - y & y >= 1 by A1,FINSEQ_3:27;
     then n - 1 - y <= len F1 - y - 1 by REAL_1:92;
     then n - (y + 1) <= len F1 - (y + 1);
  hence thesis by A2,XREAL_1:8,XREAL_1:11;
end;

Lm24: for G,H being Group, F1 being FinSequence of the carrier of G,
      F2 being FinSequence of the carrier of H,
      I being FinSequence of INT, f being Homomorphism of G,H
  st (for k being Element of NAT st k in Seg len F1 holds F2.k = f.(F1.k))
      & len F1 = len I & len F2 = len I
  holds f.(Product(F1 |^ I)) = Product(F2 |^ I)
proof
  let G,H be Group;
  let F1 be FinSequence of the carrier of G;
  let F2 be FinSequence of the carrier of H;
  let I be FinSequence of INT;
  let f be Homomorphism of G,H;
  assume A1: (for k being Element of NAT
    st k in Seg len F1 holds F2.k = f.(F1.k))
    & len F1 = len I & len F2 = len I;
  defpred P[natural number] means
  for G,H being Group, F1 being FinSequence of the carrier of G,
      F2 being FinSequence of the carrier of H, I being FinSequence of INT,
      f being Homomorphism of G,H
  st (for k being Element of NAT st k in Seg len F1 holds F2.k = f.(F1.k))
      & len F1 = len I & len F2 = len I & $1 = len I
  holds f.(Product(F1 |^ I)) = Product(F2 |^ I);
  A2: P[0]
  proof
    let G,H be Group;
    let F1 be FinSequence of the carrier of G;
    let F2 be FinSequence of the carrier of H;
    let I be FinSequence of INT;
    let f be Homomorphism of G,H;
    assume for k being Element of NAT st k in Seg len F1 holds F2.k = f.(F1.k);
    assume len F1 = len I & len F2 = len I & 0 = len I; then
    len(F1 |^ I) = 0 & len(F2 |^ I) = 0 by GROUP_4:def 4; then
    F1 |^ I = <*> the carrier of G & F2 |^ I = <*> the carrier of H
      by FINSEQ_1:25; then
    Product(F1 |^ I) = 1_G & Product(F2 |^ I) = 1_H by GROUP_4:11;
    hence f.(Product(F1 |^ I)) = Product(F2 |^ I) by GROUP_6:40;
  end;
  A3: now
    let n be natural number;
    assume A4: P[n];
    thus P[n+1]
    proof
      let G,H be Group;
      let F1 be FinSequence of the carrier of G;
      let F2 be FinSequence of the carrier of H;
      let I be FinSequence of INT;
      let f be Homomorphism of G,H;
      assume A5: for k being Element of NAT
        st k in Seg len F1 holds F2.k = f.(F1.k);
      assume A6: len F1 = len I & len F2 = len I & n+1 = len I; then
      consider In be FinSequence of INT, i be Element of INT such that
      A7: I = In^<*i*> by FINSEQ_2:22;
      consider F1n be FinSequence of the carrier of G,
               g be Element of G such that
      A8: F1 = F1n^<*g*> by A6,FINSEQ_2:22;
      consider F2n be FinSequence of the carrier of H,
               h be Element of H such that
      A9: F2 = F2n^<*h*> by A6,FINSEQ_2:22;
      set I1=<*i*>; set F11=<*g*>; set F21=<*h*>;
      len I = len In + len <*i*> by A7,FINSEQ_1:35; then
      A10: n+1 = len In + 1 by A6,FINSEQ_1:57;
      A11: len F1 = len F1n + len <*g*> by A8,FINSEQ_1:35; then
      A12: n+1 = len F1n + 1 by A6,FINSEQ_1:57;
      A13: len F2 = len F2n + len <*h*> by A9,FINSEQ_1:35; then
      A14: n+1 = len F2n + 1 by A6,FINSEQ_1:57;
      A15: F1.(n+1)=(F1n^<*g*>).(len F1n +1) by A8,A11,A6,FINSEQ_1:57
      .= g by FINSEQ_1:59;
      A16: F2.(n+1)=(F2n^<*h*>).(len F2n +1) by A9,A13,A6,FINSEQ_1:57
      .= h by FINSEQ_1:59;
      A17: now
        let k be Element of NAT;
        assume A18: k in Seg len F1n; then
        A19: k in dom F2n & k in dom F1n by FINSEQ_1:def 3,A12,A14;
        0+n <= 1+n by XREAL_1:8; then
        A20: Seg len F1n c= Seg len F1 by A6,A12,FINSEQ_1:7;
        thus F2n.k = F2.k by A9,A19,FINSEQ_1:def 7
        .= f.(F1.k) by A18,A20,A5
        .= f.(F1n.k) by A8,A19,FINSEQ_1:def 7;
      end;
      A21: len F11 = 1 by FINSEQ_1:57
      .=len I1 by FINSEQ_1:57;
      A22: len F21 = 1 by FINSEQ_1:57
      .=len I1 by FINSEQ_1:57;
      A23: Product(F1 |^ I) = Product((F1n |^ In)^(F11 |^ I1))
        by A21,GROUP_4:25,A12,A10,A7,A8
      .= Product(F1n |^ In) * Product(F11 |^ I1) by GROUP_4:8;
      A24: Product(F2 |^ I) = Product((F2n |^ In)^(F21 |^ I1))
        by A22,GROUP_4:25,A14,A10,A7,A9
      .= Product(F2n |^ In) * Product(F21 |^ I1) by GROUP_4:8;
      A25: n+1 in Seg len F1 by A6,FINSEQ_1:6;
      A26: f.Product(F11 |^ I1) = f.Product(<*g*>|^<*@i*>)
      .= f.Product <*g|^i*> by GROUP_4:28
      .= f.(g|^i) by GROUP_4:12
      .= (f.g)|^i by GROUP_6:46
      .= h|^i by A25,A5,A15,A16
      .= Product <*h|^i*> by GROUP_4:12
      .= Product(<*h*>|^<*@i*>) by GROUP_4:28
      .= Product(F21 |^ I1);
      f.(Product(F1 |^ I)) = f.Product(F1n |^ In) * f.Product(F11 |^ I1)
        by A23,GROUP_6:def 7
      .= Product(F2n |^ In) * Product(F21 |^ I1) by A26,A17,A4,A10,A14,A12;
      hence f.(Product(F1 |^ I)) = Product(F2 |^ I) by A24;
    end;
  end;
  A27: for n being natural number holds P[n] from NAT_1:sch 2(A2,A3);
  set n=len F1;
  thus thesis by A1,A27;
end;

:: GROUP_4:37
theorem Th24:
  g1 in the_stable_subgroup_of A iff
    ex F being FinSequence of the carrier of G,
       I being FinSequence of INT, C being Subset of G
    st C = the_stable_subset_generated_by (A, the action of G) &
       len F = len I & rng F c= C & Product(F |^ I) = g1
proof
  thus g1 in the_stable_subgroup_of A implies
    ex F being FinSequence of the carrier of G,
       I being FinSequence of INT, C being Subset of G
    st C = the_stable_subset_generated_by (A, the action of G) &
       len F = len I & rng F c= C & Product(F |^ I) = g1
  proof
    assume A1: g1 in the_stable_subgroup_of A;
    defpred P[set] means
      ex F being FinSequence of the carrier of G,
         I being FinSequence of INT, C being Subset of G
      st C = the_stable_subset_generated_by (A, the action of G) &
         $1 = Product (F |^ I) & len F = len I & rng F c= C;
    reconsider B = {b where b is Element of G : P[b]}
      as Subset of G from DOMAIN_1:sch 7;
    1_G = Product <*> the carrier of G & <*> the carrier of G = {} &
      (<*> the carrier of G) |^ (<*> INT) = {} &
      rng <*> the carrier of G = {} &
      {} c= the_stable_subset_generated_by (A, the action of G) &
      len {} = 0 & len<*>INT = 0
           by GROUP_4:11,GROUP_4:27,FINSEQ_1:25,XBOOLE_1:2; then
    A2: 1_G in B;
    A3: now let c,d be Element of G;
    assume that A4: c in B and A5: d in B;
    A6: ex d1 being Element of G st c = d1 &
      ex F being FinSequence of the carrier of G,
         I being FinSequence of INT, C being Subset of G
      st C = the_stable_subset_generated_by (A, the action of G) &
         d1 = Product(F |^ I) & len F = len I & rng F c= C by A4;
      A7: ex d2 being Element of G st d = d2 &
        ex F being FinSequence of the carrier of G,
           I being FinSequence of INT, C being Subset of G
        st C = the_stable_subset_generated_by (A, the action of G) &
           d2 = Product(F |^ I) & len F = len I & rng F c= C by A5;
      consider F1 be FinSequence of the carrier of G,
               I1 be FinSequence of INT, C be Subset of G such that
      A8: C = the_stable_subset_generated_by (A, the action of G) and
      A9: c = Product(F1 |^ I1) and
      A10: len F1 = len I1 and
      A11: rng F1 c= C by A6;
      consider F2 be FinSequence of the carrier of G,
               I2 be FinSequence of INT, C be Subset of G such that
      A12: C = the_stable_subset_generated_by (A, the action of G) and
      A13: d = Product(F2 |^ I2) and
      A14: len F2 = len I2 and
      A15: rng F2 c= C by A7;
      A16: c * d = Product((F1 |^ I1) ^ (F2 |^ I2)) by A9,A13,GROUP_4:8
      .= Product((F1 ^ F2) |^ (I1 ^ I2)) by A10,A14,GROUP_4:25;
      rng(F1 ^ F2) = rng F1 \/ rng F2 by FINSEQ_1:44; then
      A17: rng(F1 ^ F2) c= C &
      C = the_stable_subset_generated_by (A, the action of G)
        by A11,A15,XBOOLE_1:8,A8,A12;
      len(F1 ^ F2) = len I1 + len I2 by A10,A14,FINSEQ_1:35
      .= len(I1 ^ I2) by FINSEQ_1:35;
      hence c * d in B by A16,A17;
    end;
    A18: now let c be Element of G;
      assume c in B;
      then ex d1 being Element of G st c = d1 &
          ex F being FinSequence of the carrier of G,
             I being FinSequence of INT, C being Subset of G
          st C = the_stable_subset_generated_by (A, the action of G) &
           d1 = Product(F |^ I) & len F = len I & rng F c= C;
      then consider F1 be FinSequence of the carrier of G,
          I1 be FinSequence of INT, C be Subset of G such that
      A19: C = the_stable_subset_generated_by (A, the action of G) and
      A20: c = Product(F1 |^ I1) and
      A21: len F1 = len I1 and
      A22: rng F1 c= C;
      A23: dom F1 = dom I1 by A21,FINSEQ_3:31;
      deffunc F(Element of NAT) = F1.(len F1 - $1 + 1);
      consider F2 being FinSequence such that A24: len F2 = len F1 and
      A25: for k being Element of NAT st k in Seg len F1
        holds F2.k = F(k) from FINSEQ_1:sch 2;
      A26: dom F2 = dom F1 by A24,FINSEQ_3:31;
      A27: Seg len F1 = dom F1 by FINSEQ_1:def 3;
      defpred P[Element of NAT,set] means
      ex i being Integer st i = I1.(len I1 - $1 + 1) & $2 = - i;
      A28: for k being Element of NAT,y1,y2 being set
        st k in Seg len I1 & P[k,y1] & P[k,y2] holds y1 = y2;
      A29: for k being Element of NAT st k in Seg len I1 ex x st P[k,x]
      proof let k be Element of NAT;
        assume k in Seg len I1;
        then A30: k in dom I1 by FINSEQ_1:def 3;
        then reconsider n = len I1 - k + 1 as Element of NAT by Lm23;
        1 <= n & n <= len I1 by A30,Lm23;
        then n in dom I1 by FINSEQ_3:27;
        then I1.n in rng I1 & rng I1 c= INT by FUNCT_1:def 5;
        then reconsider i = I1.n as Element of INT qua non empty set;
        reconsider i as Integer;
        reconsider x = - i as set;
        take x,i;
        thus thesis;
      end;
      consider I2 being FinSequence such that
      A31: dom I2 = Seg len I1 and
      A32: for k be Element of NAT st k in Seg len I1 holds P[k,I2.k]
          from FINSEQ_1:sch 1(A28,A29);
      A33: Seg len I1 = dom I1 by FINSEQ_1:def 3;
      A34: len F2 = len I2 by A21,A24,A31,FINSEQ_1:def 3;
      A35: dom F2 = dom I2 by A21,A24,A31,FINSEQ_1:def 3;
      A36: rng F2 c= rng F1
      proof let x;
        assume x in rng F2;
        then consider y be set such that
        A37: y in dom F2 & F2.y = x by FUNCT_1:def 5;
        reconsider y as Element of NAT by A37;
        A38: x = F1.(len F1 - y + 1) by A25,A26,A27,A37;
        reconsider n = len F1 - y + 1 as Element of NAT by A24,A37,Lm23;
        A39: 1 <= n by A24,A37,Lm23;
        n <= len F1 by A24,A37,Lm23;
        then n in dom F1 by A39,FINSEQ_3:27;
        hence thesis by A38,FUNCT_1:def 5;
      end;
      then A40: rng F2 c= C by A22,XBOOLE_1:1;
      rng F2 c= the carrier of G by A36,XBOOLE_1:1;
      then reconsider F2 as FinSequence of the carrier of G by FINSEQ_1:def 4;
      rng I2 c= INT
      proof let x;
        assume x in rng I2;
        then consider y be set such that A41: y in dom I2 and A42: x = I2.y
          by FUNCT_1:def 5;
        reconsider y as Element of NAT by A41;
        ex i being Integer st i = I1.(len I1 - y + 1) & x = - i
          by A31,A32,A41,A42;
        hence thesis by INT_1:def 2;
      end;
      then reconsider I2 as FinSequence of INT by FINSEQ_1:def 4;
      set p = F1 |^ I1; set q = F2 |^ I2;
      A43: len p = len F1 & len q = len F2 by GROUP_4:def 4;
      then A44: dom p = dom F1 & dom q = dom F2 by FINSEQ_3:31;
      now let k be Element of NAT;
        assume A45: k in dom q; then
        reconsider n = len p - k + 1 as Element of NAT by A24,A43,Lm23;
        1 <= n & len p >= n by A24,A43,A45,Lm23;
        then A46: n in dom I2 & n in Seg len I2
          by A24,A34,A43,FINSEQ_3:27;
        dom q = dom I1 by A21,A24,A43,FINSEQ_3:31;
        then consider i be Integer such that
        A47: i = I1.n & I2.k = - i by A21,A32,A33,A43,A45;
        I2.k = I2/.k & I2/.k = @(I2/.k)
          by A35,A44,A45,FINSEQ_4:def 4;
        then q.k = (F2/.k) |^ (- i) & F2/.k = F2.k & F2.k = F1.n & F1/.n= F1.n
          by A21,A25,A26,A27,A31,A43,A44,A45,A46,A47,
              GROUP_4:def 4,FINSEQ_4:def 4;
        then A48: q.k = ((F1/.n) |^ i)" by GROUP_1:70;
        I1.n = I1/.n & I1/.n = @(I1/.n)
                              by A31,A33,A46,FINSEQ_4:def 4;
        then p.n = (F1/.n) |^ i by A23,A31,A33,A46,A47,GROUP_4:def 4;
        then (p/.n)" = ((F1/.n) |^ i)" & q/.k = q.k
                              by A23,A31,A33,A44,A45,A46,FINSEQ_4:def 4;
        then p/.n = (q/.k)" by A48,GROUP_1:19;
        hence (q/.k)" = p.(len p - k + 1)
                 by A23,A31,A33,A44,A46,FINSEQ_4:def 4;
      end;
      then Product p" = Product q by A24,A43,GROUP_4:17;
      hence c" in B by A19,A20,A34,A40;
    end;
    now
      let o be Element of O;
      let c be Element of G;
      assume c in B; then
      ex d1 being Element of G st c = d1 &
           ex F being FinSequence of the carrier of G,
              I being FinSequence of INT, C being Subset of G
           st C = the_stable_subset_generated_by (A, the action of G) &
              d1 = Product(F |^ I) & len F = len I & rng F c= C;
      then consider F1 be FinSequence of the carrier of G,
           I1 be FinSequence of INT, C be Subset of G such that
      A49: C = the_stable_subset_generated_by (A, the action of G) and
      A50: c = Product(F1 |^ I1) and
      A51: len F1 = len I1 and
      A52: rng F1 c= C;
      deffunc F(Element of NAT) = (G^o).(F1.$1);
      consider F2 being FinSequence such that
      A53: len F2 = len F1 and
      A54: for k being Element of NAT
        st k in Seg len F1 holds F2.k = F(k) from FINSEQ_1:sch 2;
      A55: Seg len F1 = dom F1 by FINSEQ_1:def 3;
      now let y be set;
        assume y in rng F2; then
        consider x such that
        A56: x in dom F2 & y = F2.x by FUNCT_1:def 5;
        A57: x in Seg len F1 by A56,A53,FINSEQ_1:def 3;
        reconsider x as Element of NAT by A56;
        A58: F2.x = (G^o).(F1.x) by A57,A54;
        A59: F1.x in rng F1 by A57,A55,FUNCT_1:12;
        A60: C is_stable_under_the_action_of the action of G
          by A49,Def2;
        per cases;
        suppose A61: O<>{}; then
          A62: G^o = (the action of G).o by Def6;
          set f = (the action of G).o;
          reconsider f as Function of G, G by A62;
          A63: dom f = the carrier of G by FUNCT_2:def 1;
          A64: y in f .: C by A56,A58,A62,A63,A59,A52,FUNCT_1:def 12;
          (f .: C) c= C by A60,A61,Def1;
          hence y in C by A64;
        end;
        suppose O={}; then
          A65: G^o = id the carrier of G by Def6;
          (G^o).(F1.x) = F1.x by A65,A59,FUNCT_1:35;
          hence y in C by A56,A59,A52,A58;
        end;
      end; then
      A66: rng F2 c= C by TARSKI:def 3;
      rng F2 c= the carrier of G by A66,XBOOLE_1:1; then
      reconsider F2 as FinSequence of the carrier of G by FINSEQ_1:def 4;
      set p = F1 |^ I1; set q = F2 |^ I1;
      (G^o).c = Product(F2 |^ I1) by Lm24,A54,A51,A53,A50;
      hence (G^o).c in B by A66,A49,A53,A51;
    end;
    then consider H be strict StableSubgroup of G such that
    A67: the carrier of H = B by A2,A3,A18,Lm15;
    A c= B
    proof let x;
      assume A68: x in A;
      then reconsider a = x as Element of G;
      reconsider p = 1 as Integer;
      A69: len<* a *> = 1 & len<* @p *> = 1 by FINSEQ_1:56;
      A70: Product(<* a *> |^ <* @p *>) =
      Product<* a |^ 1 *> by GROUP_4:28
      .= a |^ 1 by GROUP_4:12
      .= a by GROUP_1:44;
      set C = the_stable_subset_generated_by (A, the action of G);
      A c= C & {a} c= A by A68,Def2,ZFMISC_1:37; then
      rng<* a *> = {a} & {a} c= C by FINSEQ_1:56,XBOOLE_1:1;
      hence thesis by A69,A70;
    end; then
    the_stable_subgroup_of A is StableSubgroup of H by A67,Def29; then
    the_stable_subgroup_of A is Subgroup of H by Def7;
    then g1 in H by A1,GROUP_2:49;
    then g1 in B by A67,STRUCT_0:def 5;
    then ex b being Element of G st
            b = g1 & ex F being FinSequence of the carrier of G,
                       I being FinSequence of INT, C being Subset of G
            st C = the_stable_subset_generated_by (A, the action of G) &
         b = Product(F |^ I) & len F = len I & rng F c= C;
    hence thesis;
  end;
  given F be FinSequence of the carrier of G, I be FinSequence of INT,
          C be Subset of G such that
  A71: C = the_stable_subset_generated_by (A, the action of G) and
  len F = len I and
  A72: rng F c= C and
  A73: Product(F |^ I) = g1;
  set H' = the_stable_subgroup_of A;
  A74: A c= the carrier of H' &
       (for H being strict StableSubgroup of G
         st A c= the carrier of H holds H' is StableSubgroup of H) by Def29;
  set Y = the carrier of H';
  H' is Subgroup of G by Def7; then
  reconsider Y as Subset of G by GROUP_2:def 5;
  now
    let o be Element of O;
    let f be Function of G, G;
    assume A75: o in O;
    assume A76: f = (the action of G).o;
    now let y be set;
      assume y in f .: Y; then
      consider x such that
      A77: x in dom f & x in Y & y = f.x by FUNCT_1:def 12;
      reconsider x as Element of G by A77;
      x in H' by A77,STRUCT_0:def 5; then
      (G^o).x in H' by Lm10; then
      f.x in H' by A75,A76,Def6;
      hence y in Y by A77,STRUCT_0:def 5;
    end;
    hence (f .: Y) c= Y by TARSKI:def 3;
  end; then
  A78: Y is_stable_under_the_action_of the action of G by Def1;
  reconsider H' as Subgroup of G by Def7;
  C c= the carrier of H' by A74,A78,A71,Def2;
  then rng F c= carr H' by A72,XBOOLE_1:1;
  hence thesis by A73,GROUP_4:26;
end;

Lm25: A is empty implies  the_stable_subgroup_of A = (1).G
proof
  assume A is empty; then
  A1: A c= the carrier of (1).G by XBOOLE_1:2;
  now
    let H be strict StableSubgroup of G;
    assume A c= the carrier of H;
    (1).G = (1).H by Th15;
    hence (1).G is StableSubgroup of H;
  end;
  hence the_stable_subgroup_of A = (1).G by A1,Def29;
end;

Lm26: for O being non empty set, E being set, o being Element of O,
      A being Action of O,E holds Product(<*o*>,A) = A.o
proof
  let O be non empty set;
  let E be set;
  let o be Element of O;
  let A be Action of O,E;
  A1: len <*o*> = 1 by FINSEQ_1:56; then
  consider PF be FinSequence of Funcs(E,E) such that
  A2: Product(<*o*>,A) = PF.(len <*o*>) & len PF = len <*o*> &
      PF.1 = A.(<*o*>.1) &
      (for k being natural number st k<>0 & k<len <*o*> holds
         ex f,g being Function of E,E
         st f = PF.k & g = A.(<*o*>.(k+1)) & PF.(k+1) = f*g) by Def3;
  thus Product(<*o*>,A) = A.o by A1,A2,FINSEQ_1:57;
end;

Lm27: for O being non empty set, E being set, o being Element of O,
  F being FinSequence of O, A being Action of O,E
  holds Product(F^<*o*>,A) = Product(F,A)*Product(<*o*>,A)
proof
  let O be non empty set;
  let E be set;
  let o be Element of O;
  let F be FinSequence of O;
  let A be Action of O,E;
  set F1=F^<*o*>;
  A1: len F1 = len F + len <*o*> by FINSEQ_1:35
  .= len F + 1 by FINSEQ_1:56; then
  consider PF1 be FinSequence of Funcs(E,E) such that
  A2: Product(F1,A) = PF1.(len F1) & len PF1 = len F1 & PF1.1 = A.(F1.1) &
    (for k being natural number st k<>0 & k<len F1 holds
      ex f,g being Function of E,E
           st f = PF1.k & g = A.(F1.(k+1)) & PF1.(k+1) = f*g) by Def3;
  per cases;
  suppose A3: len F <> 0;
    A4: len F<len F1 by A1,NAT_1:13; then
    consider f,g be Function of E,E such that
    A5: f = PF1.(len F) & g = A.(F1.(len F+1)) & PF1.(len F+1) = f*g by A2,A3;
    A6: Product(<*o*>,A) = A.o by Lm26
    .= A.(F1.(len F + 1)) by FINSEQ_1:59;
    reconsider PF=PF1|(Seg len F) as FinSequence of Funcs(E,E) by FINSEQ_1:23;
    set IT = PF.(len F);
    A7: len F in Seg len F by A3,FINSEQ_1:5;
    Seg len F c= Seg len PF1 by A4,A2,FINSEQ_1:7; then
    len F in Seg len PF1 by A7; then
    len F in dom PF1 by FINSEQ_1:def 3; then
    len F in dom PF1 /\ Seg len F by A7,XBOOLE_0:def 3; then
    len F in dom PF by RELAT_1:90; then
    IT in rng PF by FUNCT_1:12; then
    ex f being Function
      st IT = f & dom f = E & rng f c= E by FUNCT_2:def 2; then
    reconsider IT as Function of E,E by FUNCT_2:4;
    A8: len PF = len F by A2,A4,FINSEQ_1:21;
    0<len F by A3; then
    0+1<len F+1 by XREAL_1:8; then
    1<=len F by NAT_1:13; then
    A9: 1 in Seg len F; then
    A10: 1 in dom F by FINSEQ_1:def 3;
    A11: PF.1 = A.(F1.1) by A2,A9,FUNCT_1:72
    .= A.(F.1) by A10,FINSEQ_1:def 7;
    now
      let k be natural number;
      assume A12: k<>0;
      assume A13: k<len F; then
      k<len F1 by A1,NAT_1:13; then
      consider f,g be Function of E,E such that
      A14: f = PF1.k & g = A.(F1.(k+1)) & PF1.(k+1) = f*g by A2,A12;
      take f,g;
      0<k by A12; then
      A15: 0+1<k+1 by XREAL_1:8; then
      A16: 1<=k by NAT_1:13;
      A17: k in Seg len F by A13,A16,FINSEQ_1:3;
      k+1<=len F by NAT_1:13,A13; then
      A18: k+1 in Seg len F by A15,FINSEQ_1:3; then
      A19: k+1 in dom F by FINSEQ_1:def 3;
      thus f = PF.k by A14,A17,FUNCT_1:72;
      thus g = A.(F.(k+1)) by A14,A19,FINSEQ_1:def 7;
      thus PF.(k+1) = f*g by A14,A18,FUNCT_1:72;
    end; then
    A20: IT = Product(F,A) by A8,A11,A3,Def3;
    A21: len F in Seg len F by A3,FINSEQ_1:5;
    thus Product(F^<*o*>,A)
    = Product(F,A)*Product(<*o*>,A) by A5,A20,A21,A6,A1,A2,FUNCT_1:72;
  end;
  suppose A22: len F = 0;
    thus Product(F^<*o*>,A) = Product(<*>O^<*o*>,A) by A22,FINSEQ_1:32
    .= Product(<*o*>,A) by FINSEQ_1:47
    .= (id E)*Product(<*o*>,A) by FUNCT_2:23
    .= Product(F,A)*Product(<*o*>,A) by A22,Def3;
  end;
end;

Lm28: for O being non empty set, E being set, o being Element of O,
  F being FinSequence of O, A being Action of O,E
  holds Product(<*o*>^F,A) = Product(<*o*>,A)*Product(F,A)
proof
  let O be non empty set;
  let E be set;
  let o be Element of O;
  let F be FinSequence of O;
  let A be Action of O,E;
  defpred P[Element of NAT] means
    for F being FinSequence of O st len F = $1 holds
    Product(<*o*>^F,A) = Product(<*o*>,A)*Product(F,A);
  A1: P[0]
  proof
    let F be FinSequence of O;
    assume A2: len F = 0;
    thus Product(<*o*>^F,A) = Product(<*o*>^<*>O,A) by A2,FINSEQ_1:32
    .= Product(<*o*>,A) by FINSEQ_1:47
    .= Product(<*o*>,A)*id E by FUNCT_2:23
    .= Product(<*o*>,A)*Product(F,A) by A2,Def3;
  end;
  A3: for k being Element of NAT st P[k] holds P[k + 1]
  proof
    let k be Element of NAT;
    assume A4: P[k];
    now
      let F be FinSequence of O;
      assume A5: len F = k+1; then
      consider Fk be FinSequence of O, o' be Element of O such that
      A6: F = Fk^<*o'*> by FINSEQ_2:22;
      len F = len Fk + len <*o'*> by A6,FINSEQ_1:35; then
      A7: k+1 = len Fk + 1 by A5,FINSEQ_1:56;
      set F2k=<*o*>^Fk;
      thus Product(<*o*>^F,A) = Product(<*o*>^Fk^<*o'*>,A) by A6,FINSEQ_1:45
      .= Product(F2k,A)*Product(<*o'*>,A) by Lm27
      .= Product(<*o*>,A)*Product(Fk,A)*Product(<*o'*>,A) by A4,A7
      .= Product(<*o*>,A)*((Product(Fk,A))*Product(<*o'*>,A)) by RELAT_1:55
      .= Product(<*o*>,A)*Product(F,A) by A6,Lm27;
    end;
    hence P[k + 1];
  end;
  A8: for k being Element of NAT holds P[k] from NAT_1:sch 1(A1,A3);
  reconsider k = len F as Element of NAT;
  k = len F;
  hence thesis by A8;
end;

Lm29: for O being non empty set, E being set,
  F1,F2 being FinSequence of O, A being Action of O,E
  holds Product(F1^F2,A) = Product(F1,A) * Product(F2,A)
proof
  let O be non empty set, E be set;
  let F1,F2 be FinSequence of O;
  let A be Action of O,E;
  defpred P[Element of NAT] means
    for F1,F2 being FinSequence of O st len F1 = $1 holds
    Product(F1^F2,A) = Product(F1,A)*Product(F2,A);
  A1: P[0]
  proof
    let F1,F2 be FinSequence of O;
    assume A2: len F1 = 0;
    thus Product(F1^F2,A) = Product(<*>O^F2,A) by A2,FINSEQ_1:32
    .= Product(F2,A) by FINSEQ_1:47
    .= (id E)*Product(F2,A) by FUNCT_2:23
    .= Product(F1,A)*Product(F2,A) by A2,Def3;
  end;
  A3: for k being Element of NAT st P[k] holds P[k + 1]
  proof
    let k be Element of NAT;
    assume A4: P[k];
    now
      let F1,F2 be FinSequence of O;
      assume A5: len F1 = k+1; then
      consider F1k be FinSequence of O, o be Element of O such that
      A6: F1 = F1k^<*o*> by FINSEQ_2:22;
      len F1 = len F1k + len <*o*> by A6,FINSEQ_1:35; then
      A7: k+1 = len F1k + 1 by A5,FINSEQ_1:56;
      set F2k=<*o*>^F2;
      thus Product(F1^F2,A)
      = Product(F1k^F2k,A) by A6,FINSEQ_1:45
      .= Product(F1k,A)*Product(F2k,A) by A7,A4
      .= Product(F1k,A)*(Product(<*o*>,A)*Product(F2,A)) by Lm28
      .= (Product(F1k,A)*Product(<*o*>,A))*Product(F2,A) by RELAT_1:55
      .= Product(F1,A)*Product(F2,A) by A6,A7,A4;
    end;
    hence P[k + 1];
  end;
  A8: for k being Element of NAT holds P[k] from NAT_1:sch 1(A1,A3);
  reconsider k = len F1 as Element of NAT;
  k = len F1;
  hence thesis by A8;
end;

Lm30: for O,E being set, F being FinSequence of O,
  Y being Subset of E, A being Action of O,E
  st Y is_stable_under_the_action_of A holds Product(F,A) .: Y c= Y
proof
  let O,E be set;
  let F be FinSequence of O;
  let Y be Subset of E;
  let A be Action of O,E;
  assume A1: Y is_stable_under_the_action_of A;
  per cases;
  suppose O={}; then
    rng F = {} by XBOOLE_1:3; then
    F = {} by FINSEQ_1:27; then
    len F = 0 by FINSEQ_1:25; then
    Product(F,A) = id E by Def3;
    hence Product(F,A) .: Y c= Y by FUNCT_1:162;
  end;
  suppose A2: O<>{};
    defpred P[Element of NAT] means
      for F being FinSequence of O st len F = $1 holds Product(F,A) .: Y c= Y;
    A3: P[0]
    proof
      let F be FinSequence of O;
      assume len F = 0; then
      Product(F,A) = id E by Def3;
      hence Product(F,A) .: Y c= Y by FUNCT_1:162;
    end;
    A4: for k being Element of NAT st P[k] holds P[k + 1]
    proof
      let k be Element of NAT;
      assume A5: P[k];
      now
        let F be FinSequence of O;
        assume A6: len F = k+1; then
        consider Fk be FinSequence of O, o be Element of O such that
        A7: F = Fk^<*o*> by A2,FINSEQ_2:22;
        reconsider F1 = <*o*> as FinSequence of O by A7,FINSEQ_1:50;
        len F = len Fk + len <*o*> by A7,FINSEQ_1:35; then
        A8: k+1 = len Fk + 1 by A6,FINSEQ_1:56;
        Product(F,A) = Product(Fk,A)*Product(F1,A) by A7,A2,Lm29; then
        A9: Product(F,A) .: Y = Product(Fk,A) .: (Product(F1,A) .: Y)
          by RELAT_1:159;
        Product(F1,A) = A.o & o in O by A2,Lm26; then
        Product(F1,A) .: Y c= Y by A1,Def1; then
        A10: Product(F,A) .: Y c= Product(Fk,A) .: Y by A9,RELAT_1:156;
        Product(Fk,A) .: Y c= Y by A8,A5;
        hence Product(F,A) .: Y c= Y by A10,XBOOLE_1:1;
      end;
      hence P[k+1];
    end;
    A11: for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A4);
    reconsider k = len F as Element of NAT;
    k = len F;
    hence thesis by A11;
  end;
end;

Lm31: for E being non empty set, A being Action of O,E holds
  for X being Subset of E, a being Element of E st X is not empty holds
    a in the_stable_subset_generated_by(X,A) iff
      ex F being FinSequence of O, x being Element of X st Product(F,A).x = a
proof
  let E be non empty set;
  let A be Action of O,E;
  let X be Subset of E;
  let a be Element of E;
  assume A1: X is not empty;
  defpred P[set] means
    ex F being FinSequence of O, x being Element of X st Product(F,A).x = $1;
  set B = {e where e is Element of E: P[e]};
  reconsider B as Subset of E from DOMAIN_1:sch 7;
  now let x;
    assume A2: x in X;
    set F=<*>O;
    len F = 0 by FINSEQ_1:32; then
    A3: Product(F,A).x = (id E).x by Def3
    .= x by A2,FUNCT_1:35;
    reconsider e=x as Element of E by A2;
    reconsider x'=e as Element of X by A2;
    Product(F,A).x' = e by A3;
    hence x in B;
  end; then
  A4: X c= B by TARSKI:def 3;
  now
    let o be Element of O;
    let f be Function of E, E;
    assume A5: o in O;
    assume A6: f = A.o;
    per cases;
    suppose O={}; hence (f .: B) c= B by A5; end;
    suppose A7: O<>{};
      now let y be set;
        assume y in f .: B; then
        consider x such that
        A8: x in dom f & x in B & y = f.x by FUNCT_1:def 12;
        consider e' be Element of E such that
        A9: e'=x & ex F' being FinSequence of O, x' being Element of X
        st Product(F',A).x' = e' by A8;
        consider F' be FinSequence of O, x' be Element of X such that
        A10: Product(F',A).x' = e' by A9;
        reconsider o as Element of O;
        reconsider F''=<*o*> as FinSequence of O by A7,FINSEQ_1:95;
        reconsider F=F''^F' as FinSequence of O;
        y in rng f by A8,FUNCT_1:12; then
        reconsider e=y as Element of E;
        x' in X by A1; then
        x' in E; then
        A11: x' in dom Product(F',A) by FUNCT_2:def 1;
        Product(F,A).x' = (Product(F'',A)*Product(F',A)).x' by A7,Lm29
        .= Product(F'',A).(Product(F',A).x') by A11,FUNCT_1:23
        .= e by A8,A7,Lm26,A9,A6,A10;
        hence y in B;
      end;
      hence (f .: B) c= B by TARSKI:def 3;
    end;
  end; then
  A12: B is_stable_under_the_action_of A by Def1;
  now
    let Y be Subset of E;
    assume A13: Y is_stable_under_the_action_of A;
    assume A14: X c= Y;
    now let x;
      assume x in B; then
      consider e be Element of E such that
      A15: x=e & ex F being FinSequence of O, x' being Element of X
        st Product(F,A).x' = e;
      consider F be FinSequence of O, x' be Element of X such that
      A16: Product(F,A).x' = e by A15;
      A17: x' in X by A1; then
      x' in E; then
      A18: x' in dom Product(F,A) by FUNCT_2:def 1;
      A19: Product(F,A).:Y c= Y by A13,Lm30;
      Product(F,A).x' in Product(F,A).:Y by A14,A17,A18,FUNCT_1:def 12;
      hence x in Y by A19,A16,A15;
    end;
    hence B c= Y by TARSKI:def 3;
  end; then
  A20: B = the_stable_subset_generated_by(X,A) by A4,A12,Def2;
  hereby
    assume a in the_stable_subset_generated_by(X,A); then
    consider e be Element of E such that
    A21: a=e & ex F being FinSequence of O, x being Element of X
      st Product(F,A).x = e by A20;
    consider F be FinSequence of O, x be Element of X such that
    A22: Product(F,A).x = e by A21;
    take F, x;
    thus Product(F,A).x = a by A21,A22;
  end;
  given F be FinSequence of O, x be Element of X such that
  A23: Product(F,A).x = a;
  thus a in the_stable_subset_generated_by(X,A) by A23,A20;
end;

:: GROUP_4:40
theorem Th25:
  for H being strict StableSubgroup of G holds
    the_stable_subgroup_of carr H = H
proof
  let H be strict StableSubgroup of G;
  for H1 be strict StableSubgroup of G st carr H c= the carrier of H1 holds
    H is StableSubgroup of H1 by Lm21;
  hence thesis by Def29;
end;

:: GROUP_4:41
theorem Th26:
  A c= B implies
    the_stable_subgroup_of A is StableSubgroup of the_stable_subgroup_of B
proof
  assume A1: A c= B;
  per cases;
  suppose A2: A is empty;
    reconsider H1 = (1).G,H2 = (1).(the_stable_subgroup_of B)
      as strict StableSubgroup of G by Th11;
    the carrier of H1 = {1_G} by Def8
    .= {1_(the_stable_subgroup_of B)} by Th4
    .= the carrier of H2 by Def8; then
    (1).G = (1).(the_stable_subgroup_of B) by Lm5;
    hence the_stable_subgroup_of A is
      StableSubgroup of the_stable_subgroup_of B by A2,Lm25;
  end;
  suppose A3: A is not empty;
    now let a be Element of G;
      assume a in the_stable_subgroup_of A; then
      consider F be FinSequence of the carrier of G,
        I be FinSequence of INT, C be Subset of G such that
      A4: C = the_stable_subset_generated_by (A, the action of G) &
        len F = len I & rng F c= C & Product(F |^ I) = a by Th24;
      set D = the_stable_subset_generated_by (B, the action of G);
      now let y be set;
        assume A5: y in C;
        reconsider b=y as Element of G by A5;
        consider F1 be FinSequence of O, x be Element of A such that
        A6: Product(F1,the action of G).x = b by A3,A4,A5,Lm31;
        x in A by A3;
        hence y in D by A1,A6,Lm31;
      end; then
      C c= D by TARSKI:def 3; then
      rng F c= D by A4,XBOOLE_1:1;
      hence a in the_stable_subgroup_of B by A4,Th24;
    end;
    hence thesis by Th13;
  end;
end;

scheme MeetSbgWOpEx{O() -> set, G() -> GroupWithOperators of O(), P[set]}:
 ex H being strict StableSubgroup of G() st
  the carrier of H =
    meet{A where A is Subset of G() :
      ex K being strict StableSubgroup of G() st A = the carrier of K & P[K]}
 provided
  A1: ex H being strict StableSubgroup of G() st P[H]
proof
  set X = {A where A is Subset of G(): ex K being strict StableSubgroup of G()
                   st A = the carrier of K & P[K]};
  consider T being strict StableSubgroup of G() such that
  A2: P[T] by A1;
  A3: carr T in X by A2;
  then reconsider Y = meet X as Subset of G() by SETFAM_1:8;
  now let Z be set;
    assume Z in X;
    then consider A being Subset of G() such that
    A4: Z = A and
    A5: ex K being strict StableSubgroup of G() st A = the carrier of K & P[K];
    consider H being StableSubgroup of G() such that
    A6: A = the carrier of H & P[H] by A5;
    1_G() in H by Lm18;
    hence 1_G() in Z by A4,A6,STRUCT_0:def 5;
    end;
    then A7: Y <> {} by A3,SETFAM_1:def 1;
    A8: now let a,b be Element of G();
      assume A9: a in Y & b in Y;
      now let Z be set;
        assume A10: Z in X;
        then consider A being Subset of G() such that
        A11: A = Z and
        A12: ex H being strict StableSubgroup of G()
          st A = the carrier of H & P[H];
        consider H being StableSubgroup of G() such that
        A13: A = the carrier of H & P[H] by A12;
        a in the carrier of H & b in the carrier of H
          by A9,A10,A11,A13,SETFAM_1:def 1;
        then a in H & b in H by STRUCT_0:def 5;
        then a * b in H by Lm19;
        hence a * b in Z by A11,A13,STRUCT_0:def 5;
      end;
      hence a * b in Y by A3,SETFAM_1:def 1;
    end;
    A14: now let a be Element of G();
      assume A15: a in Y;
      now let Z be set;
        assume A16: Z in X;
        then consider A being Subset of G() such that
        A17: A = Z and
        A18: ex H being strict StableSubgroup of G()
               st A = the carrier of H & P[H];
        consider H being StableSubgroup of G() such that
        A19: A = the carrier of H & P[H] by A18;
        a in the carrier of H by A15,A16,A17,A19,SETFAM_1:def 1;
        then a in H by STRUCT_0:def 5;
        then a" in H by Lm20;
        hence a" in Z by A17,A19,STRUCT_0:def 5;
      end;
      hence a" in Y by A3,SETFAM_1:def 1;
    end;
    now
      let o be Element of O();
      let a be Element of G();
      assume A20: a in Y;
      now let Z be set;
        assume A21: Z in X;
        then consider A being Subset of G() such that
        A22: A = Z and
        A23: ex H being strict StableSubgroup of G()
               st A = the carrier of H & P[H];
        consider H being StableSubgroup of G() such that
        A24: A = the carrier of H & P[H] by A23;
        a in the carrier of H by A20,A21,A22,A24,SETFAM_1:def 1;
        then a in H by STRUCT_0:def 5;
        then (G()^o).a in H by Lm10;
        hence (G()^o).a in Z by A22,A24,STRUCT_0:def 5;
      end;
      hence (G()^o).a in Y by A3,SETFAM_1:def 1;
    end;
    hence thesis by A7,A8,A14,Lm15;
  end;

:: GROUP_4:43
theorem Th27:
  the carrier of the_stable_subgroup_of A =
    meet{B where B is Subset of G: ex H being strict StableSubgroup of G
      st B = the carrier of H & A c= carr H}
proof
  defpred P[StableSubgroup of G] means A c= carr $1;
  A c= the carrier of G & the carrier of (Omega).G = carr (Omega).G &
  (Omega).G = the HGrWOpStr of G;
  then A1: ex H being strict StableSubgroup of G st P[H];
  consider H being strict StableSubgroup of G such that A2: the carrier of H =
  meet{B where B is Subset of G: ex H being strict StableSubgroup of G
    st B = the carrier of H & P[H]} from MeetSbgWOpEx(A1);
  set X = {B where B is Subset of G :ex H being strict StableSubgroup of G
  st B = the carrier of H & A c= carr H};
  A3: carr (Omega).G in X;
  now let Y be set;
    assume Y in X;
    then ex B being Subset of G st Y = B &
      ex H being strict StableSubgroup of G
        st B = the carrier of H & A c= carr H;
    hence A c= Y;
  end;
  then A4: A c= the carrier of H by A2,A3,SETFAM_1:6;
  now let H1 be strict StableSubgroup of G;
    assume A5: A c= the carrier of H1;
    the carrier of H1 = carr H1;
    then the carrier of H1 in X by A5;
    then the carrier of H c= the carrier of H1 by A2,SETFAM_1:4;
    hence H is StableSubgroup of H1 by Lm21;
  end;
  hence thesis by A2,A4,Def29;
end;

Lm32: B = the carrier of gr A implies
  the_stable_subgroup_of A = the_stable_subgroup_of B
proof
  assume A1: B = the carrier of gr A; then
  A2: the carrier of gr A c= the carrier of the_stable_subgroup_of B by Def29;
  A c= the carrier of gr A by GROUP_4:def 5; then
  A3: A c= the carrier of the_stable_subgroup_of B by A2,XBOOLE_1:1;
  now
    let H be strict StableSubgroup of G;
    assume A4: A c= the carrier of H;
    reconsider H'=the HGrStr of H as strict Subgroup of G by Lm16;
    gr A is Subgroup of H' by A4,GROUP_4:def 5; then
    B c= the carrier of H' by A1,GROUP_2:def 5;
    hence the_stable_subgroup_of B is StableSubgroup of H by Def29;
  end;
  hence the_stable_subgroup_of A = the_stable_subgroup_of B by A3,Def29;
end;

:: GROUP_4:64
theorem Th28:
  for N1,N2 being strict normal StableSubgroup of G
    holds N1 * N2 = N2 * N1
proof
  let N1,N2 be strict normal StableSubgroup of G;
  reconsider N1'= the HGrStr of N1,N2'= the HGrStr of N2
    as strict normal Subgroup of G by Lm7;
  thus N1 * N2 = carr N2' * carr N1' by GROUP_3:148 .= N2 * N1;
end;

:: GROUP_4:68
theorem Th29:
  H1 "\/" H2 = the_stable_subgroup_of(H1 * H2)
proof
  reconsider H1'=H1,H2'=H2 as Subgroup of G by Def7;
  reconsider Y = the carrier of (H1'"\/"H2') as Subset of G by GROUP_2:def 5;
  A1: Y = the carrier of gr(H1'*H2') by GROUP_4:68;
  H1 "\/" H2 = the_stable_subgroup_of Y by Lm32
  .= the_stable_subgroup_of(H1'*H2') by A1,Lm32;
  hence H1 "\/" H2 = the_stable_subgroup_of(H1 * H2);
end;

:: GROUP_4:69
theorem Th30:
  H1 * H2 = H2 * H1 implies the carrier of H1 "\/" H2 = H1 * H2
proof
  assume H1 * H2 = H2 * H1;
  then consider H being strict StableSubgroup of G such that
    A1: the carrier of H = carr H1 * carr H2 by Th17;
        carr H1 \/ carr H2 c= carr H1 * carr H2
  proof let x;
    assume A2: x in carr H1 \/ carr H2;
    then reconsider a = x as Element of G;
    now per cases by A2,XBOOLE_0:def 2;
      suppose A3: x in carr H1;
        1_G in H2 by Lm18;
        then 1_G in carr H2 & a * 1_G = a by GROUP_1:def 5,STRUCT_0:def 5;
        hence thesis by A3;
      end;
      suppose A4: x in carr H2;
        1_G in H1 by Lm18;
        then 1_G in carr H1 & 1_G * a = a by GROUP_1:def 5,STRUCT_0:def 5;
        hence thesis by A4;
      end;
    end;
    hence thesis;
  end;
  then A5: H1 "\/" H2 is StableSubgroup of H by A1,Def29;
  now let a be Element of G;
    assume a in H;
    then a in carr H1 * carr H2 by A1,STRUCT_0:def 5;
    then consider b,c be Element of G such that
    A6: a = b * c and
    A7: b in carr H1 & c in carr H2;
    set A = carr H1 \/ carr H2;
    set X = {B where B is Subset of G: ex H being strict StableSubgroup of G
      st B = the carrier of H & A c= carr H};
    A8: carr (Omega).G in X;
    now
      let Y be set;
      assume Y in X; then
      consider B be Subset of G such that
      A9: Y = B & ex H being strict StableSubgroup of G
        st B = the carrier of H & A c= carr H;
      consider H' be strict StableSubgroup of G such that
      A10: B = the carrier of H' & A c= carr H' by A9;
      A11: H' is Subgroup of G by Def7;
      b in A & c in A by A7,XBOOLE_0:def 2; then
      b in H' & c in H' by A10,STRUCT_0:def 5; then
      b * c in H' by A11,GROUP_2:59;
      hence a in Y by A9,A10,A6,STRUCT_0:def 5;
    end; then
    a in meet X by A8,SETFAM_1:def 1; then
    a in the carrier of the_stable_subgroup_of A by Th27;
    hence a in H1 "\/" H2 by STRUCT_0:def 5;
  end;
  then H is StableSubgroup of H1 "\/" H2 by Th13; then
  H is Subgroup of H1 "\/" H2 & H1 "\/" H2 is Subgroup of H by A5,Def7; then
  the carrier of H c= the carrier of H1 "\/" H2
  & the carrier of H1 "\/" H2 c= the carrier of H by GROUP_2:def 5;
  hence thesis by A1,XBOOLE_0:def 10;
end;

:: GROUP_4:71
theorem Th31:
  for N1,N2 being strict normal StableSubgroup of G
    holds the carrier of N1 "\/" N2 = N1 * N2
proof
  let N1,N2 be strict normal StableSubgroup of G;
  N1 * N2 = N2 * N1 by Th28;
  hence thesis by Th30;
end;

:: GROUP_4:72
theorem Th32:
  for N1,N2 being strict normal StableSubgroup of G
    holds N1 "\/" N2 is normal StableSubgroup of G
proof
  let N1,N2 be strict normal StableSubgroup of G;
  consider N be strict normal StableSubgroup of G such that
  A1: the carrier of N = carr N1 * carr N2 by Th23;
  the carrier of N1 "\/" N2 = N1 * N2 & N1 * N2 = carr N1 * carr N2 by Th31;
  hence thesis by A1,Lm5;
end;

:: GROUP_4:76
theorem Th33:
  for H being strict StableSubgroup of G holds
    (1).G "\/" H = H & H "\/" (1).G = H
proof
  let H be strict StableSubgroup of G;
  A1: carr(1).G = {1_G} by Def8;
  1_G in H by Lm18;
  then 1_G in carr H by STRUCT_0:def 5;
  then {1_G} c= carr H by ZFMISC_1:37;
  then {1_G} \/ carr H = carr H by XBOOLE_1:12;
  hence thesis by A1,Th25;
end;

:: GROUP_4:77
theorem Th34:
  (Omega).G "\/" H1 = (Omega).G & H1 "\/" (Omega).G = (Omega).G
proof
  (the carrier of (Omega).G) \/ carr H1 = [#] the carrier of G by SUBSET_1:28;
  hence thesis by Th25;
end;

:: Lm7 in GROUP_4
Lm33: H1 is StableSubgroup of H1 "\/" H2
proof
  carr H1 c= carr H1 \/ carr H2 &
  carr H1 \/ carr H2 c= the carrier of
    the_stable_subgroup_of (carr H1 \/ carr H2) &
  H1 "\/" H2 = the_stable_subgroup_of (carr H1 \/ carr H2) &
  the carrier of H1 = carr H1 by Def29,XBOOLE_1:7;
  then the carrier of H1 c= the carrier of H1 "\/" H2 by XBOOLE_1:1;
  hence thesis by Lm21;
end;

:: GROUP_4:78
theorem Th35:
  H1 is StableSubgroup of H1 "\/" H2 & H2 is StableSubgroup of H1 "\/" H2
proof
  H1 "\/" H2 = H2 "\/" H1;
  hence thesis by Lm33;
end;

:: GROUP_4:79
theorem Th36:
  for H2 being strict StableSubgroup of G holds
    H1 is StableSubgroup of H2 iff H1 "\/" H2 = H2
proof
  let H2 be strict StableSubgroup of G;
  thus H1 is StableSubgroup of H2 implies H1 "\/" H2 = H2
  proof assume H1 is StableSubgroup of H2; then
    H1 is Subgroup of H2 by Def7;
    then A1: the carrier of H1 c= the carrier of H2 &
    the carrier of H1 = carr H1 & carr H2 = the carrier of H2
      by GROUP_2:def 5;
    thus H1 "\/" H2 = the_stable_subgroup_of carr H2 by A1,XBOOLE_1:12
    .= H2 by Th25;
  end;
  thus thesis by Th35;
end;

:: GROUP_4:81
theorem Th37:
  for H3 being strict StableSubgroup of G holds
    H1 is StableSubgroup of H3 & H2 is StableSubgroup of H3 implies
    H1 "\/" H2 is StableSubgroup of H3
proof
  let H3 be strict StableSubgroup of G;
  assume A1: H1 is StableSubgroup of H3 & H2 is StableSubgroup of H3;
  H1 is Subgroup of H3 & H2 is Subgroup of H3 by A1,Def7; then
  carr H1 c= carr H3 & carr H2 c= carr H3 by GROUP_2:def 5; then
  carr H1 \/ carr H2 c= carr H3 by XBOOLE_1:8; then
  the_stable_subgroup_of(carr H1 \/ carr H2) is StableSubgroup of
    the_stable_subgroup_of carr H3 by Th26;
  hence thesis by Th25;
end;

:: GROUP_4:82
theorem Th38:
  for H2,H3 being strict StableSubgroup of G holds
  H1 is StableSubgroup of H2 implies H1 "\/" H3 is StableSubgroup of H2 "\/" H3
proof
  let H2,H3 be strict StableSubgroup of G;
  assume H1 is StableSubgroup of H2; then
  H1 is Subgroup of H2 by Def7; then
  carr H1 c= carr H2 by GROUP_2:def 5; then
  carr H1 \/ carr H3 c= carr H2 \/ carr H3 by XBOOLE_1:9;
  hence thesis by Th26;
end;

:: GROUP_6:3
theorem Th39:
  for X,Y being StableSubgroup of H1, X',Y' being StableSubgroup of G
    st X = X' & Y = Y' holds X' /\ Y' = X /\ Y
proof
  let X,Y be StableSubgroup of H1;
  let X',Y' be StableSubgroup of G;
  assume A1: X=X' & Y=Y';
  A2: the carrier of X /\ Y = (carr X) /\ (carr Y) &
  the carrier of X' /\ Y' = (carr X') /\ (carr Y') &
  carr X = the carrier of X & the carrier of Y = carr Y &
  carr X' = the carrier of X & the carrier of Y = carr Y' by A1,Def28;
  reconsider Z = X /\ Y as StableSubgroup of G by Th11;
  X' /\ Y' = Z by A2,Th18;
  hence thesis;
end;

:: GROUP_6:9
theorem Th40:
  N is StableSubgroup of H1 implies N is normal StableSubgroup of H1
proof
  assume N is StableSubgroup of H1; then
  reconsider N' = N as StableSubgroup of H1;
  now
    let H be strict Subgroup of H1;
    assume A1: H = the HGrStr of N';
    A2: H1 is Subgroup of G by Def7;
    reconsider N''=the HGrStr of N as normal Subgroup of G by Lm7;
    reconsider N as Subgroup of G by Def7;
    N'' is Subgroup of N by GROUP_2:66;
    hence H is normal by A1,A2,GROUP_6:9;
  end;
  hence N is normal StableSubgroup of H1 by Def10;
end;

:: Lm4 in GROUP_6
Lm34: H1 /\ H2 is StableSubgroup of H1
proof
  the carrier of H1 /\ H2 = (the carrier of H1) /\ (the carrier of H2) &
       (the carrier of H1) /\ (the carrier of H2) c= the carrier of H1
                                          by Th18,XBOOLE_1:17;
  hence thesis by Lm21;
end;

:: GROUP_6:10
theorem Th41:
  H1 /\ N is normal StableSubgroup of H1 &
    N /\ H1 is normal StableSubgroup of H1
proof
  thus H1 /\ N is normal StableSubgroup of H1
  proof
    reconsider A = H1 /\ N as StableSubgroup of H1 by Lm34;
    now
      let H be strict Subgroup of H1;
      assume A1: H = the HGrStr of A;
      reconsider N'=the HGrStr of N as normal Subgroup of G by Lm7;
      now let b be Element of H1;
        thus b * H c= H * b
        proof let x;
          assume x in b * H;
          then consider a be Element of H1 such that
          A2: x = b * a and
          A3: a in H by GROUP_2:125;
          a in the carrier of A by STRUCT_0:def 5,A1,A3; then
          a in carr(H1) /\ carr(N) by Def28; then
          a in carr N' by XBOOLE_0:def 3; then
          A4: a in N' by STRUCT_0:def 5;
          reconsider a' = a, b' = b as Element of G by Th2;
          x = b' * a' by A2,Th3;
          then x in b' * N' & b' * N' c= N' * b' by A4,GROUP_2:125,GROUP_3:141;
          then consider b1 be Element of G such that
          A5: x = b1 * b' and A6: b1 in N' by GROUP_2:126;
          reconsider x' = x as Element of H1 by A2;
          reconsider x'' = x as Element of G by A5;
          A7: b1 = x'' * b'" by A5,GROUP_1:22;
          A8: b1 in the carrier of N by A6,STRUCT_0:def 5;
          b'" = b" by Th6;
          then A9:b1 = x' * b" by A7,Th3; then
          A10: b1 in carr(H1) /\ carr(N) by A8,XBOOLE_0:def 3;
          reconsider b1' = b1 as Element of H1 by A9;
          b1' in the carrier of A by A10,Def28; then
          A11: b1' in H by A1,STRUCT_0:def 5;
          b1' * b = x by A5,Th3;
          hence x in H * b by A11,GROUP_2:126;
        end;
      end;
      hence H is normal by GROUP_3:141;
    end;
    hence thesis by Def10;
  end;
  hence thesis;
end;

:: GROUP_6:13
theorem Th42:
  for G being strict GroupWithOperators of O holds
    G is trivial implies (1).G = G
proof
  let G be strict GroupWithOperators of O;
  assume G is trivial; then
  A1: the carrier of G is trivial by STRUCT_0:def 9;
  reconsider H=G as StableSubgroup of G by Lm4;
  consider x be set such that
  A2: the carrier of G = {x} by A1,REALSET1:def 4;
  the carrier of H = {1_G} by A2,TARSKI:def 1;
  hence thesis by Def8;
end;

Lm35: for N' being normal Subgroup of G st N' = the HGrStr of N
  holds G./.N' = the HGrStr of G./.N & 1_(G./.N') = 1_(G./.N)
proof
  let N' be normal Subgroup of G;
  assume A1: N' = the HGrStr of N;
  Cosets N' = Cosets N by A1,Def14;
  hence G./.N' = the HGrStr of G./.N by A1,Def15;
  reconsider e=1_(G./.N') as Element of G./.N by A1,Def14;
  now
    let h be Element of G./.N;
    reconsider h'=h as Element of G./.N' by A1,Def14;
    thus h * e = h' * 1_(G./.N') by A1,Def15 .= h by GROUP_1:def 5;
    thus e * h = 1_(G./.N') * h' by A1,Def15 .= h by GROUP_1:def 5;
  end;
  hence 1_(G./.N') = 1_(G./.N) by GROUP_1:10;
end;

:: GROUP_6:29
theorem Th43:
  1_(G./.N) = carr N
proof
  reconsider N' = the HGrStr of N as normal Subgroup of G by Lm7;
  1_(G./.N') = carr N' by GROUP_6:29;
  hence thesis by Lm35;
end;

:: GROUP_6:35
theorem Th44:
  for M,N being strict normal StableSubgroup of G,
      MN being normal StableSubgroup of N
  st MN=M & M is StableSubgroup of N
  holds N./.MN is normal StableSubgroup of G./.M
proof
  let M,N be strict normal StableSubgroup of G;
  let MN be normal StableSubgroup of N;
  assume A1: MN=M;
  assume A2: M is StableSubgroup of N;
  reconsider N' = the HGrStr of N as normal Subgroup of G by Lm7;
  reconsider M' = the HGrStr of M as normal Subgroup of G by Lm7;
  reconsider MN' = the HGrStr of MN as normal Subgroup of N by Lm7;
  M is Subgroup of N by A2,Def7; then
  the carrier of M c= the carrier of N &
    the mult of M = (the mult of N)||the carrier of M
    by GROUP_2:def 5; then
  A3: M' is Subgroup of N' by GROUP_2:def 5;
  reconsider K=N'./.(N',M')`*` as normal Subgroup of G./.M' by A3,GROUP_6:35;
  A4: (N',M')`*` = MN' by A1,A3,GROUP_6:def 1;
  reconsider MN''=(N',M')`*` as normal Subgroup of N';
  A5: the carrier of G./.M = the carrier of G./.M' by Def14;
  A6: now let x;
    hereby
      assume x in Cosets MN'; then
      consider a be Element of N such that
      A7: x = a * MN' & x = MN' * a by GROUP_6:15;
      reconsider A = {a} as Subset of N by ZFMISC_1:37;
      reconsider a' = a as Element of N';
      reconsider A' = {a'} as Subset of N' by ZFMISC_1:37;
      now let y be set;
        hereby
          assume y in {g * h where g,h is Element of N:g in A & h in carr MN'};
          then consider g,h be Element of N such that
          A8: y = g*h & g in A & h in carr MN';
          reconsider g'=g as Element of N';
          reconsider h'=h as Element of N';
          y = g'*h' by A8;
          hence y in {g''*h'' where g'',h'' is Element of N':
            g'' in A' & h'' in carr MN''} by A8,A4;
        end;
        assume y in {g*h where g,h is Element of N': g in A' & h in carr MN''};
        then consider g,h be Element of N' such that
        A9: y = g*h & g in A' & h in carr MN'';
        reconsider g'=g as Element of N;
        reconsider h'=h as Element of N;
        y = g'*h' by A9;
        hence y in {g''*h'' where g'',h'' is Element of N:
        g'' in A & h'' in carr MN'} by A9,A4;
      end; then
      x = a' * MN'' by TARSKI:2,A7;
      hence x in Cosets MN'' by GROUP_6:16;
    end;
    assume x in Cosets MN''; then
    consider a' be Element of N' such that
    A10: x = a' * MN'' & x = MN'' * a'  by GROUP_6:15;
    reconsider A' = {a'} as Subset of N' by ZFMISC_1:37;
    reconsider a = a' as Element of N;
    reconsider A = {a} as Subset of N by ZFMISC_1:37;
    now let y be set;
      hereby
        assume y in {g * h where g,h is Element of N:g in A & h in carr MN'};
        then consider g,h be Element of N such that
        A11: y = g*h & g in A & h in carr MN';
        reconsider g'=g as Element of N';
        reconsider h'=h as Element of N';
        y = g'*h' by A11;
        hence y in {g''*h'' where g'',h'' is Element of N':
          g'' in A' & h'' in carr MN''} by A11,A4;
      end;
      assume y in {g*h where g,h is Element of N': g in A' & h in carr MN''};
      then consider g,h be Element of N' such that
      A12: y = g*h & g in A' & h in carr MN'';
      reconsider g'=g as Element of N;
      reconsider h'=h as Element of N;
      y = g'*h' by A12;
      hence y in {g''*h'' where g'',h'' is Element of N:
      g'' in A & h'' in carr MN'} by A12,A4;
    end; then
    x = a * MN' by TARSKI:2,A10;
    hence x in Cosets MN' by GROUP_6:16;
  end; then
  A13: Cosets MN'' = Cosets MN' by TARSKI:2;
  reconsider f=CosOp MN'' as BinOp of Cosets MN' by A13;
  A14: now
    let W1,W2 be Element of Cosets MN';
    let A1,A2 be Subset of N;
    assume A15: W1 = A1;
    assume A16: W2 = A2;
    reconsider W1'=W1,W2'=W2 as Element of Cosets MN'' by A6;
    reconsider A1'=A1,A2'=A2 as Subset of N';
    A17: now let x;
      hereby
        assume x in A1 * A2; then
        consider g,h be Element of N such that
        A18: x = g * h & g in A1 & h in A2;
        reconsider g'=g,h'=h as Element of N';
        x = g' * h' by A18;
        hence x in A1' * A2' by A18;
      end;
      assume x in A1' * A2'; then
      consider g',h' be Element of N' such that
      A19: x = g' * h' & g' in A1' & h' in A2';
      reconsider g=g',h=h' as Element of N;
      x = g * h by A19;
      hence x in A1 * A2 by A19;
    end;
    thus f.(W1,W2) = f.(W1',W2') .= A1' * A2' by A15,A16,GROUP_6:def 4
    .= A1 * A2 by A17,TARSKI:2;
  end;
  A20: the carrier of K = Cosets MN' by A6,TARSKI:2
  .= the carrier of N./.MN by Def14;
  A21: the mult of K = CosOp MN' by A14,GROUP_6:def 4
  .= the mult of N./.MN by Def15;
  A22: now
    let H be strict Subgroup of G./.M;
    assume A23: H = the HGrStr of N./.MN;
    now
      let a be Element of G./.M;
      reconsider a'=a as Element of G./.M' by Def14;
      now let x;
        assume x in a * carr H; then
        consider b be Element of G./.M such that
        A24: x = a * b & b in carr H by GROUP_2:33;
        reconsider b'=b as Element of G./.M' by Def14;
        A25: x = a' * b' by A24,Def15; then
        reconsider x'=x as Element of G./.M';
        A26: a' * K c= K * a' by GROUP_3:141;
        x' in a' * carr K by A23,A20,A24,A25,GROUP_2:33; then
        consider c' be Element of G./.M' such that
        A27: x' = c' * a' & c' in carr K by A26,GROUP_2:34;
        reconsider c = c' as Element of G./.M by Def14;
        x = c * a by Def15,A27;
        hence x in carr H * a by A23,A20,A27,GROUP_2:34;
      end;
      hence a * H c= H * a by TARSKI:def 3;
    end;
    hence H is normal by GROUP_3:141;
  end;
  A28: the carrier of N./.MN c= the carrier of G./.M by A20,A5,GROUP_2:def 5;
  the mult of N./.MN
  = (the mult of G./.M')||the carrier of K by A21,GROUP_2:def 5
  .= (the mult of G./.M)||the carrier of N./.MN by A20,Def15; then
  A29: N./.MN is Subgroup of G./.M by A28,GROUP_2:def 5;
  now
    let o be Element of O;
    per cases;
    suppose A30: not o in O;
      A31: the carrier of N./.MN c= the carrier of G./.M
        by A20,A5,GROUP_2:def 5;
      A32: now let x,y be set;
        assume [x,y] in id the carrier of N./.MN; then
        A33: x in the carrier of N./.MN & x=y by RELAT_1:def 10; then
        [x,y] in id the carrier of G./.M by A31,RELAT_1:def 10;
        hence [x,y] in (id the carrier of G./.M)|the carrier of N./.MN
          by A33,RELAT_1:def 11;
      end;
      A34: now let x,y be set;
        assume [x,y] in (id the carrier of G./.M)|the carrier of N./.MN; then
        A35: x in the carrier of N./.MN & [x,y] in id the carrier of G./.M
          by RELAT_1:def 11; then
        x in the carrier of G./.M & x=y by RELAT_1:def 10;
        hence [x,y] in id the carrier of N./.MN by A35,RELAT_1:def 10;
      end;
      thus (N./.MN)^o = id the carrier of N./.MN by A30,Def6
      .= (id the carrier of G./.M)|the carrier of N./.MN
        by A32,A34,RELAT_1:def 2
      .= ((G./.M)^o)|the carrier of N./.MN by A30,Def6;
    end;
    suppose A36: o in O;
      (the action of G./.M).o in Funcs(the carrier of G./.M,
        the carrier of G./.M) by A36,FUNCT_2:7; then
      consider f be Function such that
      A37: f=(the action of G./.M).o & dom f = the carrier of G./.M &
        rng f c= the carrier of G./.M by FUNCT_2:def 2;
      (the action of N./.MN).o in Funcs(the carrier of N./.MN,
        the carrier of N./.MN) by A36,FUNCT_2:7; then
      consider g be Function such that
      A38: g=(the action of N./.MN).o & dom g = the carrier of N./.MN &
        rng g c= the carrier of N./.MN by FUNCT_2:def 2;
      A39: g = {[A,B] where A,B is Element of Cosets MN:
        ex a,b being Element of N st a in A & b in B & b = (N^o).a}
        by A36,A38,Def16;
      A40: f = {[A,B] where A,B is Element of Cosets M:
        ex a,b being Element of G st a in A & b in B & b = (G^o).a}
        by A36,A37,Def16;
      A41: dom g = dom f /\ the carrier of N./.MN by A28,A37,A38,XBOOLE_1:28;
      A42: now
        let x;
        assume A43: x in dom g;
        [x,f.x] in f by  A28,A37,A38,FUNCT_1:8,A43; then
        consider A1,B1 be Element of Cosets M such that
        A44: [x,f.x]=[A1,B1] & ex a,b being Element of G st
          a in A1 & b in B1 & b = (G^o).a by A40;
        [x,g.x] in g by A43,FUNCT_1:8; then
        consider A2,B2 be Element of Cosets MN such that
        A45: [x,g.x]=[A2,B2] & ex a,b being Element of N st
          a in A2 & b in B2 & b = (N^o).a by A39;
        consider a1,b1 be Element of G such that
        A46: a1 in A1 & b1 in B1 & b1 = (G^o).a1 by A44;
        consider a2,b2 be Element of N such that
        A47: a2 in A2 & b2 in B2 & b2 = (N^o).a2 by A45;
        A48: A1=x & A2=x & B1=f.x & B2=g.x by A44,A45,ZFMISC_1:33;
        reconsider A1'=A1,B1'=B1 as Element of Cosets M' by Def14;
        reconsider A2'=A2,B2'=B2 as Element of Cosets MN' by Def14;
        A49: a2 in the carrier of N & b2 in the carrier of N;
        N is Subgroup of G by Def7; then
        A50: the carrier of N c= the carrier of G by GROUP_2:def 5; then
        reconsider a2'=a2,b2'=b2 as Element of G by A49;
        A51: now let x;
          hereby
            assume x in a2 * carr MN'; then
            consider h be Element of N such that
            A52: x = a2 * h & h in carr MN' by GROUP_2:33;
            h in the carrier of N; then
            reconsider h'=h as Element of G by A50;
            x = a2' * h' by A52,Th3;
            hence x in a2' * carr M' by A1,A52,GROUP_2:33;
          end;
          assume x in a2' * carr M'; then
          consider h be Element of G such that
          A53: x = a2' * h & h in carr M' by GROUP_2:33;
          h in carr MN' by A53,A1; then
          reconsider h'=h as Element of N;
          x = a2 * h' by A53,Th3;
          hence x in a2 * carr MN' by A1,A53,GROUP_2:33;
        end;
        A54: now let x;
          hereby
            assume x in b2 * carr MN'; then
            consider h be Element of N such that
            A55: x = b2 * h & h in carr MN' by GROUP_2:33;
            h in the carrier of N; then
            reconsider h'=h as Element of G by A50;
            x = b2' * h' by A55,Th3;
            hence x in b2' * carr M' by A1,A55,GROUP_2:33;
          end;
          assume x in b2' * carr M'; then
          consider h be Element of G such that
          A56: x = b2' * h & h in carr M' by GROUP_2:33;
          h in carr MN' by A56,A1; then
          reconsider h'=h as Element of N;
          x = b2 * h' by A56,Th3;
          hence x in b2 * carr MN' by A1,A56,GROUP_2:33;
        end;
        set fo = G^o;
        A57: A1' = a1 * M' by A46,Lm9;
        A58: A2' = a2 * MN' by A47,Lm9;
        A59: a2 * MN' = a2' * M' by A51,TARSKI:2;
        A60: b1" = fo.a1" by A46,GROUP_6:41;
        A61: b2' = ((G^o)|the carrier of N).a2 by A47,Def7
        .= fo.a2' by FUNCT_1:72;
        A62: b2' * M' = b2 * MN' by A54,TARSKI:2;
        a1" * a2' in M' by A48,A57,A58,A59,GROUP_2:137; then
        a1" * a2' in the carrier of M by STRUCT_0:def 5; then
        a1" * a2' in M by STRUCT_0:def 5; then
        fo.(a1" * a2') in M by Lm10; then
        b1" * b2' in M by A60,A61,GROUP_6:def 7; then
        b1" * b2' in the carrier of M by STRUCT_0:def 5; then
        A63: b1" * b2' in M' by STRUCT_0:def 5;
        A64: B1' = b1 * M' by A46,Lm9;
        B2' = b2 * MN' by A47,Lm9;
        hence g.x = f.x by A48,A64,A63,A62,GROUP_2:137;
      end;
      thus (N./.MN)^o = (the action of N./.MN).o by A36,Def6
      .= f|the carrier of N./.MN by A38,A42,A41,FUNCT_1:68
      .= ((G./.M)^o)|the carrier of N./.MN by A36,A37,Def6;
    end;
  end;
  hence N./.MN is normal StableSubgroup of G./.M by A22,A29,Def7,Def10;
end;

:: GROUP_6:40
theorem
  h.(1_G)=1_H by Lm13;

:: GROUP_6:41
theorem
  h.(g1")=(h.g1)" by Lm14;

:: GROUP_6:50
theorem Th47:
  g1 in Ker h iff h.g1 = 1_H
proof
  thus g1 in Ker h implies h.g1 = 1_H
  proof
    assume g1 in Ker h;
    then g1 in the carrier of Ker h by STRUCT_0:def 5;
    then g1 in {b where b is Element of G : h.b = 1_H} by Def24;
    then ex b being Element of G st g1 = b & h.b = 1_H;
    hence thesis;
  end;
  assume h.g1 = 1_H;
  then g1 in {b where b is Element of G: h.b = 1_H};
  then g1 in the carrier of Ker h by Def24;
  hence thesis by STRUCT_0:def 5;
end;

:: GROUP_6:52
theorem Th48:
  for N being strict normal StableSubgroup of G holds Ker nat_hom N = N
proof
  let N be strict normal StableSubgroup of G;
  reconsider N' = the HGrStr of N as strict normal Subgroup of G by Lm7;
  A1: nat_hom N = nat_hom N' by Def23;
  A2: 1_(G./.N) = 1_(G./.N') by Lm35;
  the carrier of Ker nat_hom N =
  {a where a is Element of G: (nat_hom N).a = 1_(G./.N)} by Def24
  .= {a where a is Element of G: (nat_hom N').a = 1_(G./.N')} by A2,A1
  .= the carrier of Ker nat_hom N' by GROUP_6:def 10
  .= the carrier of N by GROUP_6:52;
  hence thesis by Lm5;
end;

:: GROUP_6:53
theorem Th49:
  rng h = the carrier of Image h
proof
  the carrier of Image h = h .: (the carrier of G) by Def25
  .= h .: (dom h) by FUNCT_2:def 1
  .= rng h by RELAT_1:146;
  hence thesis;
end;

:: GROUP_6:57
theorem Th50:
  Image nat_hom N = G./.N
proof
  reconsider H = G./.N as strict StableSubgroup of G./.N by Lm4;
  reconsider N' = the HGrStr of N as strict normal Subgroup of G by Lm7;
  A1: G./.N' = the HGrStr of G./.N by Lm35;
  the carrier of Image nat_hom N = nat_hom N .: (the carrier of G) by Def25
  .= nat_hom N' .: (the carrier of G) by Def23
  .= the carrier of Image nat_hom N' by GROUP_6:def 11
  .= the carrier of H by A1,GROUP_6:57;
  hence thesis by Lm5;
end;

:: GROUP_6:67
theorem Th51:
  for H being strict GroupWithOperators of O, h being Homomorphism of G,H holds
    h is_epimorphism iff Image h = H
proof
  let H be strict GroupWithOperators of O, h be Homomorphism of G,H;
  thus h is_epimorphism implies Image h = H
  proof assume A1: rng h = the carrier of H;
    reconsider H'=H as strict StableSubgroup of H by Lm4;
    the carrier of H' = the carrier of Image h by A1,Th49;
    hence thesis by Lm5;
  end;
  assume A2: Image h = H;
  the carrier of H c= rng h
  proof let x;
    assume x in the carrier of H;
    then x in h .: (the carrier of G) by A2,Def25;
    then ex y being set st y in dom h & y in the carrier of G &
    h.y = x by FUNCT_1:def 12;
    hence thesis by FUNCT_1:def 5;
  end;
  then rng h = the carrier of H by XBOOLE_0:def 10;
  hence thesis by Def20;
end;

:: GROUP_6:68
theorem Th52:
  for H being strict GroupWithOperators of O, h being Homomorphism of G,H st
    h is_epimorphism holds
      for c being Element of H ex a being Element of G st h.a = c
proof
  let H be strict GroupWithOperators of O;
  let h be Homomorphism of G,H;
  assume h is_epimorphism; then
  A1: rng h = the carrier of H by Def20;
  let c be Element of H;
  consider a be set such that
  A2: a in dom h & c = h.a by A1,FUNCT_1:def 5;
  reconsider a as Element of G by A2;
  take a;
  thus h.a = c by A2;
end;

:: GROUP_6:69
theorem Th53:
  nat_hom N is_epimorphism
proof
  Image nat_hom N = G./.N by Th50;
  hence thesis by Th51;
end;

:: GROUP_6:75
theorem Th54:
  nat_hom (1).G is_isomorphism
proof
  set g = nat_hom (1).G;
  reconsider G'=G as Group;
  reconsider H = the HGrStr of (1).G as strict normal Subgroup of G by Lm7;
  A1: the carrier of H = {1_G'} by Def8;
  A2: nat_hom (1).G = nat_hom H by Def23
  .= nat_hom (1).G' by A1,GROUP_2:def 7;
  nat_hom (1).G' is_isomorphism by GROUP_6:75; then
  nat_hom (1).G' is_monomorphism by GROUP_6:def 14; then
  g is one-to-one by A2,GROUP_6:def 12; then
  g is_monomorphism & g is_epimorphism by Def19,Th53;
  hence nat_hom (1).G is_isomorphism by Def21;
end;

:: GROUP_6:78
theorem Th55:
  G,H are_isomorphic & H,I are_isomorphic implies G,I are_isomorphic
proof
  assume that
  A1: G,H are_isomorphic and
  A2: H,I are_isomorphic;
  consider g be Homomorphism of G,H such that
  A3: g is_isomorphism by A1,Def22;
  consider h1 be Homomorphism of H,I such that
  A4: h1 is_isomorphism by A2,Def22;
  g is_epimorphism & g is_monomorphism by A3,Def21; then
  A5: rng g = the carrier of H & g is one-to-one by Def19,Def20;
  h1 is_epimorphism & h1 is_monomorphism by A4,Def21; then
  A6: rng h1 = the carrier of I & h1 is one-to-one by Def19,Def20;
  dom h1 = rng g by A5,FUNCT_2:def 1; then
  A7: rng(h1 * g) = the carrier of I by A6,RELAT_1:47;
  A8: h1 * g is one-to-one by A5,A6,FUNCT_1:46;
  h1 * g is_epimorphism & h1 * g is_monomorphism by A7,A8,Def19,Def20; then
  (h1 * g) is_isomorphism by Def21;
  hence thesis by Def22;
end;

:: GROUP_6:82
theorem Th56:
  for G being strict GroupWithOperators of O holds G,G./.(1).G are_isomorphic
proof
  let G be strict GroupWithOperators of O;
  nat_hom (1).G is_isomorphism by Th54;
  hence G,G./.(1).G are_isomorphic by Def22;
end;

:: GROUP_6:83
theorem Th57:
  for G being strict GroupWithOperators of O holds G./.(Omega).G is trivial
proof
  let G be strict GroupWithOperators of O;
  reconsider H=the HGrStr of (Omega).G as strict normal Subgroup of G by Lm7;
  reconsider G'=G as Group;
  A1: H = (Omega).G';
  the carrier of G./.(Omega).G = Cosets H by Def14
  .= {the carrier of G} by A1,GROUP_2:172;
  hence thesis by GROUP_6:def 2;
end;

:: GROUP_6:87
theorem Th58:
  for G,H being strict GroupWithOperators of O holds
    G,H are_isomorphic & G is trivial implies H is trivial
proof
  let G,H be strict GroupWithOperators of O;
  assume that A1: G,H are_isomorphic and
  A2: G is trivial;
  consider g be Homomorphism of G,H such that
  A3: g is_isomorphism by A1,Def22;
  A4: g is_epimorphism & g is_monomorphism by A3,Def21;
  consider e be set such that
  A5: the carrier of G = {e} by A2,GROUP_6:def 2;
  e in the carrier of G by A5,TARSKI:def 1; then
  A6: e in dom g by FUNCT_2:def 1;
  the carrier of H = the carrier of Image g by A4,Th51
  .= Im(g,e) by A5,Def25
  .= {g.e} by A6,FUNCT_1:117;
  hence thesis by GROUP_6:def 2;
end;

:: GROUP_6:90
theorem Th59:
  G./.Ker h, Image h are_isomorphic
proof
  reconsider G'=G,H'=H as Group;
  reconsider h'=h as Homomorphism of G',H';
  consider g' be Homomorphism of G'./.Ker h', Image h' such that
  A1: g' is_isomorphism & h' = g' * nat_hom Ker h' by GROUP_6:91;
  A2: Ker h is Subgroup of G by Def7;
  now let x;
    hereby
      assume x in the carrier of Ker h; then
      x in {a where a is Element of G: h.a = 1_H} by Def24; then
      consider a be Element of G such that
      A3: x=a & h.a = 1_H;
      reconsider a as Element of G';
      x in {a' where a' is Element of G': h'.a' = 1_H'} by A3;
      hence x in the carrier of Ker h' by GROUP_6:def 10;
    end;
    assume x in the carrier of Ker h'; then
    x in {a' where a' is Element of G': h'.a' = 1_H'} by GROUP_6:def 10; then
    consider a' be Element of G such that
    A4: x=a' & h'.a' = 1_H';
    reconsider a' as Element of G;
    x in {a where a is Element of G: h.a = 1_H} by A4;
    hence x in the carrier of Ker h by Def24;
  end; then
  the carrier of Ker h' = the carrier of Ker h by TARSKI:2; then
  A5: the HGrStr of Ker h' = the HGrStr of Ker h by A2,GROUP_2:68;
  A6: the carrier of G'./.Ker h' = the carrier of G./.Ker h by A5,Def14;
  A7: Image h is Subgroup of H by Def7;
  A8: the carrier of Image h' = h' .: (the carrier of G') by GROUP_6:def 11
  .= the carrier of Image h by Def25; then
  A9: the HGrStr of Image h' = the HGrStr of Image h by A7,GROUP_2:68;
  reconsider g=g' as Function of G./.Ker h, Image h by A6,A8;
  A10: now
    let o be Element of O;
    let a be Element of G./.Ker h;
    per cases;
    suppose A11: O is empty;
      thus g.(((G./.Ker h)^o).a)
      = g.((id the carrier of (G./.Ker h)).a) by A11,Def6
      .= g.a by FUNCT_1:35
      .= (id the carrier of Image h).(g.a) by FUNCT_1:35
      .= ((Image h)^o).(g.a) by A11,Def6;
    end;
    suppose A12: O is not empty;
      reconsider G''=G./.Ker h as Group;
      set f = (the action of G./.Ker h).o;
      A13: f = {[A,B] where A,B is Element of Cosets Ker h:
        ex g,h being Element of G st g in A & h in B & h=(G^o).g} by A12,Def16;
      f = (G./.Ker h)^o by A12,Def6; then
      reconsider f as Homomorphism of G'', G'';
      a in the carrier of G''; then
      a in dom f by FUNCT_2:def 1; then
      [a,f.a] in f by FUNCT_1:8; then
      consider A,B be Element of Cosets Ker h such that
      A14: [A,B]=[a,f.a] &
        ex g1,g2 being Element of G st g1 in A & g2 in B & g2=(G^o).g1 by A13;
      reconsider A,B as Element of Cosets Ker h' by A5,Def14;
      consider g1,g2 be Element of G' such that
      A15: g1 in A & g2 in B & g2=(G^o).g1 by A14;
      A16: A = g1 * Ker h' & B = g2 * Ker h' by A15,Lm9;
      A17: g.(((G./.Ker h)^o).a) = g.(f.a) by A12,Def6
      .= g'.(g2 * Ker h') by A14,ZFMISC_1:33,A16;
      A18: ((Image h)^o).(g.a) = ((H^o)|the carrier of Image h).(g.a) by Def7
      .= (H^o).(g.a) by FUNCT_1:72
      .= (H^o).(g'.(g1 * Ker h')) by A14,ZFMISC_1:33,A16;
      g2 in the carrier of G'; then
      A19: g2 in dom nat_hom Ker h' by FUNCT_2:def 1;
      g1 in the carrier of G'; then
      A20: g1 in dom nat_hom Ker h' by FUNCT_2:def 1;
      h'.g2 = (H^o).(h'.g1) by A15,Def18; then
      g'.(nat_hom Ker h'.g2)=(H^o).((g' * nat_hom Ker h').g1)
        by A1,A19,FUNCT_1:23; then
      g'.(nat_hom Ker h'.g2)=(H^o).(g'.(nat_hom Ker h'.g1))
        by A20,FUNCT_1:23; then
      g'.(g2 * Ker h') = (H^o).(g'.(nat_hom Ker h'.g1)) by GROUP_6:def 9;
      hence g.(((G./.Ker h)^o).a)=((Image h)^o).(g.a) by A17,A18,GROUP_6:def 9;
    end;
  end;
  now
    let a, b be Element of G./.Ker h;
    reconsider a'=a as Element of G'./.Ker h' by A5,Def14;
    reconsider b'=b as Element of G'./.Ker h' by A5,Def14;
    thus g.(a * b) = g'.(a' * b') by A5,Def15
    .= g'.a' * g'.b' by GROUP_6:def 7
    .= g.a * g.b by A9;
  end; then
  reconsider g as Homomorphism of G./.Ker h, Image h
    by A10,Def18,GROUP_6:def 7;
  A21: g' is_epimorphism & g' is_monomorphism by A1,GROUP_6:def 14; then
  A22: rng g = the carrier of Image h by A8,GROUP_6:def 13;
  A23: g is one-to-one by A21,GROUP_6:def 12;
  g is_epimorphism & g is_monomorphism by A22,A23,Def19,Def20; then
  g is_isomorphism by Def21;
  hence thesis by Def22;
end;

:: GRSOLV_1:1
theorem Th60:
  for H,F1,F2 being strict StableSubgroup of G
    st F1 is normal StableSubgroup of F2
    holds H /\ F1 is normal StableSubgroup of H /\ F2
proof
  let H,F1,F2 be strict StableSubgroup of G;
  assume A1:F1 is normal StableSubgroup of F2;
  then A2:F1 /\ H=(F1 /\ F2) /\ H by Lm22
              .=F1 /\ (F2 /\ H) by Th20;
  reconsider F1 as normal StableSubgroup of F2 by A1;
  reconsider F=F2 /\ H as StableSubgroup of F2 by Lm34;
  F1 /\ F is normal StableSubgroup of F by Th41;
  hence thesis by A2,Th39;
end;

begin :: Other Theorems on Actions and Groups with Operators

reserve E for set,
        A for Action of O,E,
        C for Subset of G,
        N1 for normal StableSubgroup of H1;

theorem
  [#]E is_stable_under_the_action_of A by Lm1;

theorem
  [:O,{id E}:] is Action of O, E by Lm2;

theorem
  for O being non empty set, E being set, o being Element of O,
      A being Action of O,E holds Product(<*o*>,A) = A.o by Lm26;

theorem
  for O being non empty set, E being set,
      F1,F2 being FinSequence of O, A being Action of O,E
  holds Product(F1^F2,A) = Product(F1,A) * Product(F2,A) by Lm29;

theorem
  for F being FinSequence of O, Y being Subset of E
  st Y is_stable_under_the_action_of A holds Product(F,A) .: Y c= Y by Lm30;

theorem
  for E being non empty set, A being Action of O,E holds
    for X being Subset of E, a being Element of E st X is not empty holds
      a in the_stable_subset_generated_by(X,A) iff
      ex F being FinSequence of O, x being Element of X
         st Product(F,A).x = a by Lm31;

theorem
  for G being strict Group holds
    ex H being strict GroupWithOperators of O st G = the HGrStr of H
proof
  let G be strict Group;
  consider H be non empty HGrWOpStr over O such that
  A1: H is strict distributive Group-like associative &
    G = the HGrStr of H by Lm3;
  reconsider H as strict GroupWithOperators of O by A1;
  take H;
  thus G = the HGrStr of H by A1;
end;

theorem
  the HGrStr of H1 is strict Subgroup of G by Lm16;

theorem
  the HGrStr of N is strict normal Subgroup of G by Lm7;

theorem
  g1 in H1 implies (G^o).g1 in H1 by Lm10;

theorem
for O being set, G,H being GroupWithOperators of O,
      G' being strict StableSubgroup of G,
      f being Homomorphism of G,H
  holds ex H' being strict StableSubgroup of H
        st the carrier of H' = f.:(the carrier of G') by Lm17;

theorem
  B is empty implies the_stable_subgroup_of B = (1).G by Lm25;

theorem
  B = the carrier of gr C implies
    the_stable_subgroup_of C = the_stable_subgroup_of B by Lm32;

theorem
for N' being normal Subgroup of G st N' = the HGrStr of N
  holds G./.N' = the HGrStr of G./.N & 1_(G./.N') = 1_(G./.N) by Lm35;

theorem Th75:
  the carrier of H1 = the carrier of H2 implies
    the HGrWOpStr of H1 = the HGrWOpStr of H2
proof
  assume A1: the carrier of H1 = the carrier of H2;
  reconsider H1'=H1,H2'=H2 as Subgroup of G by Def7;
  A2: the HGrStr of H1' = the HGrStr of H2' by A1,GROUP_2:68;
  A3: dom the action of H2 = O by FUNCT_2:def 1
  .= dom the action of H1 by FUNCT_2:def 1;
  now let x;
    assume A4: x in dom the action of H2;
    reconsider o=x as Element of O by A4;
    A5: H1^o = (the action of H1).o & H2^o = (the action of H2).o by A4,Def6;
    H1^o = (G^o)|the carrier of H2 by A1,Def7 .= H2^o by Def7;
    hence (the action of H1).x = (the action of H2).x by A5;
  end;
  hence the HGrWOpStr of H1 = the HGrWOpStr of H2 by A3,FUNCT_1:9,A2;
end;

theorem Th76:
  H1./.N1 is trivial implies the HGrWOpStr of H1 = the HGrWOpStr of N1
proof
  assume H1./.N1 is trivial; then
  consider e be set such that
  A1: the carrier of H1./.N1 = {e} by GROUP_6:def 2;
  set H=H1;
  reconsider N=the HGrStr of N1 as normal Subgroup of H by Lm7;
  Cosets N1 = Cosets N by Def14; then
  consider e be set such that
  A2: the carrier of H./.N = {e} by A1;
  A3: the carrier of H = union {e} by A2,GROUP_2:167;
  A4: now assume not the carrier of H c= the carrier of N; then
    (the carrier of H) \ (the carrier of N) <> {} by XBOOLE_1:37; then
    consider x such that
    A5: x in (the carrier of H) \ (the carrier of N) by XBOOLE_0:def 1;
    reconsider x as Element of H1 by A5;
    A6: now assume x * N = e; then
      A7: x * N = the carrier of H by ZFMISC_1:31,A3;
      consider x' be Element of the carrier of H such that
      A8: 1_H = x * x' & x' in N by A7,GROUP_2:125;
      x'=x" by A8,GROUP_1:20; then
      x"" in N by A8,GROUP_2:60; then
      x in N by GROUP_1:19; then
      x in carr(N) by STRUCT_0:def 5;
      hence contradiction by A5,XBOOLE_0:def 4;
    end;
    x * N in Cosets N by GROUP_6:16;
    hence contradiction by A6,A2,TARSKI:def 1;
  end;
  reconsider N'=N1 as StableSubgroup of G by Th11;
  the carrier of N c= the carrier of H by GROUP_2:def 5; then
  the carrier of N' = the carrier of H1 by A4,XBOOLE_0:def 10;
  hence the HGrWOpStr of H1 = the HGrWOpStr of N1 by Th75;
end;

theorem Th77:
  the carrier of H1 = the carrier of N1 implies H1./.N1 is trivial
proof
  reconsider N1' = the HGrStr of N1 as strict normal Subgroup of H1 by Lm7;
  A1: Cosets N1 = Cosets N1' by Def14;
  assume A2: the carrier of H1 = the carrier of N1;
  now
    let x be set;
    hereby
      assume A3: x in Left_Cosets N1';
      reconsider A=x as Subset of H1 by A3;
      consider a be Element of H1 such that
      A4: A = a * N1' by A3,GROUP_2:def 15;
      A = a * [#]the carrier of H1 by A4,A2;
      hence x = the carrier of H1 by GROUP_2:21;
    end;
    assume A5: x = the carrier of H1;
    x c= the carrier of H1 by A5; then
    reconsider A=x as Subset of H1;
    the carrier of H1 = 1_H1 * [#]the carrier of H1 by GROUP_2:21; then
    the carrier of H1 = 1_H1 * N1' by A2;
    hence x in Left_Cosets N1' by A5,GROUP_2:def 15;
  end; then
  {the carrier of H1} = Left_Cosets N1' by TARSKI:def 1;
  hence H1./.N1 is trivial by A1,GROUP_6:def 2;
end;

:: ALG I.4.6 Proposition 7(a)
theorem Th78:
  for G,H being GroupWithOperators of O,
      N being StableSubgroup of G,
      H' being strict StableSubgroup of H,
      f being Homomorphism of G,H
  st N = Ker f
  holds ex G' being strict StableSubgroup of G
    st the carrier of G' = f"(the carrier of H') &
    (H' is normal implies N is normal StableSubgroup of G' & G' is normal)
proof
  let G,H be GroupWithOperators of O;
  let N be StableSubgroup of G;
  let H' be strict StableSubgroup of H;
  let f be Homomorphism of G,H;
  assume A1: N = Ker f;
  reconsider H'' = the HGrStr of H' as strict Subgroup of H by Lm16;
  set A = {g where g is Element of G:f.g in H''};
  A2: 1_H in H'' by GROUP_2:55; then
  f.(1_G) in H'' by Lm13; then
  1_G in A; then
  reconsider A as non empty set;
  now let x be set;
    assume x in A; then
    consider g be Element of G such that
    A3: x=g & f.g in H'';
    thus x in the carrier of G by A3;
  end; then
  reconsider A as Subset of G by TARSKI:def 3;
  A4: now let g1,g2 be Element of G;
    assume A5: g1 in A & g2 in A;
    consider a be Element of G such that
    A6: a=g1 & f.a in H'' by A5;
    consider b be Element of G such that
    A7: b=g2 & f.b in H'' by A5;
    A8: f.(a*b) = f.a * f.b by GROUP_6:def 7;
    set fa = f.a;
    set fb = f.b;
    fa * fb in H'' by A6,A7,GROUP_2:59;
    hence g1*g2 in A by A6,A7,A8;
  end;
  A9: now let g be Element of G;
    assume g in A; then
    consider a be Element of G such that
    A10: a=g & f.a in H'';
    (f.a)" in H'' by A10,GROUP_2:60; then
    f.(a") in H'' by Lm14;
    hence g" in A by A10;
  end;
  now
    let o be Element of O;
    let g be Element of G;
    assume g in A; then
    consider a be Element of G such that
    A11: a=g & f.a in H'';
    f.a in the carrier of H'' by A11,STRUCT_0:def 5; then
    f.a in H' by STRUCT_0:def 5; then
    (H^o).(f.g) in H' by A11,Lm10; then
    f.((G^o).g) in H' by Def18; then
    f.((G^o).g) in the carrier of H' by STRUCT_0:def 5; then
    f.((G^o).g) in H'' by STRUCT_0:def 5;
    hence (G^o).g in A;
  end; then
  consider G'' be strict StableSubgroup of G such that
  A12: the carrier of G'' = A by Lm15,A4,A9;
  reconsider G' = the HGrStr of G'' as strict Subgroup of G by Lm16;
  take G'';
  now
    let g be Element of G;
    reconsider R = f as Relation of the carrier of G, the carrier of H;
    hereby
      assume g in A; then
      consider a be Element of G such that
      A13: a=g & f.a in H'';
      dom f = the carrier of G by FUNCT_2:def 1; then
      A14: [g,f.g] in f by FUNCT_1:8;
      f.g in the carrier of H' by A13,STRUCT_0:def 5;
      hence g in f"(the carrier of H') by A14,RELSET_1:53;
    end;
    assume A15: g in f"(the carrier of H');
    consider h be Element of H such that
    A16: [g,h] in R & h in (the carrier of H') by A15,RELSET_1:53;
    A17: f.g=h by A16,FUNCT_1:8;
    h in H'' by A16,STRUCT_0:def 5;
    hence g in A by A17;
  end;
  hence the carrier of G'' = f"(the carrier of H') by A12,SUBSET_1:8;
  now assume A18: H' is normal;
     now
       let g be Element of G;
       assume g in N; then
       f.g = 1_H by A1,Th47; then
       g in the carrier of G'' by A2,A12;
       hence g in G'' by STRUCT_0:def 5;
     end;
     hence N is normal StableSubgroup of G'' by A1,Th40,Th13;
     A19: now
       let g be Element of G;
       now let x be set;
         assume x in g * G'; then
         consider h be Element of G such that
         A20: x=g*h & h in A by A12,GROUP_2:33;
         consider a be Element of G such that
         A21: a=h & f.a in H'' by A20;
         set h'=g*h*g";
         A22: h'*g = (g*h)*(g"*g) by GROUP_1:def 4
         .= (g*h)*1_G by GROUP_1:def 6
         .= x by A20,GROUP_1:def 5;
         H'' is normal by A18,Def10; then
         A23: H'' |^ (f.g)" = H'' by GROUP_3:def 13;
         f.h' = f.(g*h) * f.(g") by GROUP_6:def 7
         .= f.g * f.h * f.(g") by GROUP_6:def 7
         .= f.g * f.h * (f.g)" by Lm14
         .= ((f.g)")" * f.h * (f.g)" by GROUP_1:19
         .= f.h |^ (f.g)" by GROUP_3:def 2; then
         f.h' in H'' by A21,A23,GROUP_3:70; then
         h' in A;
         hence x in G' * g by A12,A22,GROUP_2:34;
       end;
       hence g * G' c= G' * g by TARSKI:def 3;
     end;
     for H being strict Subgroup of G st H = the HGrStr of G''
       holds H is normal by A19,GROUP_3:141;
     hence G'' is normal by Def10;
  end;
  hence thesis;
end;

:: ALG I.4.6 Proposition 7(b)
theorem Th79:
  for G,H being GroupWithOperators of O,
      N being StableSubgroup of G,
      G' being strict StableSubgroup of G,
      f being Homomorphism of G,H
  st N = Ker f
  holds ex H' being strict StableSubgroup of H
        st the carrier of H' = f.:(the carrier of G') &
           f"(the carrier of H') = the carrier of G'"\/"N &
           (f is_epimorphism & G' is normal implies H' is normal)
proof
  let G,H be GroupWithOperators of O;
  let N be StableSubgroup of G;
  let G' be strict StableSubgroup of G;
  let f be Homomorphism of G,H;
  assume A1: N = Ker f;
  reconsider G'' = the HGrStr of G' as strict Subgroup of G by Lm16;
  set A = {f.g where g is Element of G:g in G''};
  1_G in G'' by GROUP_2:55; then
  f.(1_G) in A; then
  reconsider A as non empty set;
  now let x be set;
    assume x in A; then
    consider g be Element of G such that
    A2: x=f.g & g in G'';
    thus x in the carrier of H by A2;
  end; then
  reconsider A as Subset of H by TARSKI:def 3;
  A3: now let h1,h2 be Element of H;
    assume A4: h1 in A & h2 in A;
    consider a be Element of G such that
    A5: h1=f.a & a in G'' by A4;
    consider b be Element of G such that
    A6: h2=f.b & b in G'' by A4;
    A7: f.(a*b) = h1*h2 by A5,A6,GROUP_6:def 7;
    a*b in G'' by A5,A6,GROUP_2:59;
    hence h1*h2 in A by A7;
  end;
  A8: now let h be Element of H;
    assume A9: h in A;
    consider g be Element of G such that
    A10: h=f.g & g in G'' by A9;
    A11: g" in G'' by A10,GROUP_2:60;
    h"=f.(g") by A10,Lm14;
    hence h" in A by A11;
  end;
  now
    let o be Element of O;
    let h be Element of H;
    assume h in A; then
    consider g be Element of G such that
    A12: h=f.g & g in G'';
    A13: (H^o).h = f.((G^o).g) by Def18,A12;
    g in the carrier of G'' by A12,STRUCT_0:def 5; then
    g in G' by STRUCT_0:def 5; then
    (G^o).g in G' by Lm10; then
    (G^o).g in the carrier of G' by STRUCT_0:def 5; then
    (G^o).g in G'' by STRUCT_0:def 5;
    hence (H^o).h in A by A13;
  end; then
  consider H'' be strict StableSubgroup of H such that
  A14: the carrier of H'' = A by Lm15,A3,A8;
  reconsider H' = the HGrStr of H'' as strict Subgroup of H by Lm16;
  take H'';
  A15: now
    let h be Element of H;
    reconsider R = f as Relation of the carrier of G, the carrier of H;
    hereby
      assume h in A; then
      consider g be Element of G such that
      A16: h=f.g & g in G'';
      dom f = the carrier of G by FUNCT_2:def 1; then
      A17: [g,h] in f by A16,FUNCT_1:8;
      g in the carrier of G' by A16,STRUCT_0:def 5;
      hence h in f.:(the carrier of G') by A17,RELSET_1:52;
    end;
    assume A18: h in f.:(the carrier of G');
    consider g be Element of G such that
    A19: [g,h] in R & g in the carrier of G' by A18,RELSET_1:52;
    A20: f.g=h by A19,FUNCT_1:8;
    g in G'' by A19,STRUCT_0:def 5;
    hence h in A by A20;
  end;
  hence A21: the carrier of H'' = f.:(the carrier of G') by A14,SUBSET_1:8;
  reconsider N'=the HGrStr of N as strict Subgroup of G by Lm16;
  N' is normal by A1,Def10; then
  A22: carr G'' * N' = N' * carr G'' by GROUP_3:143;
  A23: dom f = the carrier of G by FUNCT_2:def 1;
  A24: now let x;
    assume A25: x in f"(the carrier of H'); then
    x in dom f & f.x in the carrier of H' by FUNCT_1:def 13; then
    consider g1 be set such that
    A26: g1 in dom f & g1 in the carrier of G' & f.g1 = f.x
      by FUNCT_1:def 12,A21;
    reconsider g1,g2=x as Element of G by A25,A26;
    consider g3 be Element of G such that
    A27: g2 = g1 * g3 by GROUP_1:23;
    f.g2 = f.g2*f.g3 by A26,A27,GROUP_6:def 7; then
    f.g3 = 1_H by GROUP_1:15; then
    g3 in Ker f by Th47; then
    g3 in the carrier of N by A1,STRUCT_0:def 5;
    hence x in G'' * N' by A27,A26;
  end;
  now let x;
    assume A28: x in G'' * N'; then
    consider g1,g2 be Element of G such that
    A29: x = g1*g2 & g1 in carr G' & g2 in carr N';
    A30: g2 in Ker f by A1,STRUCT_0:def 5,A29;
    A31: f.x = f.g1*f.g2 by A29,GROUP_6:def 7
    .= f.g1*1_H by A30,Th47
    .= f.g1 by GROUP_1:def 5;
    f.x in f.:(the carrier of G') by A23,A29,A31,FUNCT_1:def 12; then
    x in f"(f.:(the carrier of G')) by A28,A23,FUNCT_1:def 13;
    hence x in f"(the carrier of H') by A15,A14,SUBSET_1:8;
  end; then
  A32: f"(the carrier of H') = carr G' * carr N by A24,TARSKI:2;
  A33: G''*N' = G'*N;
  N'*G'' = N*G';
  hence f"(the carrier of H'') = the carrier of G'"\/"N
    by A22,A33,A32,Th30;
  now assume A34: f is_epimorphism & G' is normal; then
    A35: G'' is normal by Def10;
    now
      let h1 be Element of H;
      now let x be set;
        assume x in h1 * H'; then
        consider h2 be Element of H such that
        A36: x=h1*h2 & h2 in A by A14,GROUP_2:33;
        h2 in f.:(the carrier of G') by A36,A15; then
        consider g2 be set such that
        A37: g2 in dom f & g2 in the carrier of G'' & f.g2 = h2
          by FUNCT_1:def 12;
        set h2' = h1*h2*h1";
        A38: h2'*h1 = (h1*h2)*(h1"*h1) by GROUP_1:def 4
        .= (h1*h2)*1_H by GROUP_1:def 6
        .= x by A36,GROUP_1:def 5;
        A39: rng f = the carrier of H by A34,Def20;
        consider g1 be set such that
        A40: g1 in dom f & h1 = f.g1 by A39,FUNCT_1:def 5;
        reconsider g1,g2 as Element of G by A40,A37;
        set g2'=g1*g2*g1";
        g2'=(g1"")*g2*g1" by GROUP_1:19; then
        g2'=g2 |^ g1" & g2 in G'' by GROUP_3:def 2,A37,STRUCT_0:def 5; then
        g2' in G'' |^ g1" by GROUP_3:70; then
        A41: g2' in the carrier of G'' |^ g1" by STRUCT_0:def 5;
        G'' |^ g1" is Subgroup of G'' by A35,GROUP_3:145; then
        A42: the carrier of G'' |^ g1" c= the carrier of G''
          by GROUP_2:def 5;
        h2' = f.g1*f.g2*f.(g1") by A40,A37,Lm14
        .= f.(g1*g2)*f.(g1") by GROUP_6:def 7
        .= f.g2' by GROUP_6:def 7; then
        h2' in f.:(the carrier of G'') by A23,A42,A41,FUNCT_1:def 12; then
        h2' in A by A15;
        hence x in H' * h1 by A38,A14,GROUP_2:34;
      end;
      hence h1 * H' c= H' * h1 by TARSKI:def 3;
    end; then
    for H1 being strict Subgroup of H
      st H1 = the HGrStr of H'' holds H1 is normal by GROUP_3:141;
    hence H'' is normal by Def10;
  end;
  hence thesis;
end;

theorem Th80:
  for G being strict GroupWithOperators of O,
      N being strict normal StableSubgroup of G,
      H being strict StableSubgroup of G./.N
  st the carrier of G = (nat_hom N)"(the carrier of H)
  holds H = (Omega).(G./.N)
proof
  let G be strict GroupWithOperators of O;
  let N be strict normal StableSubgroup of G;
  let H be strict StableSubgroup of G./.N;
  reconsider N' = the HGrStr of N as strict normal Subgroup of G by Lm7;
  reconsider H' = the HGrStr of H as strict Subgroup of G./.N by Lm16;
  A1: the carrier of H' c= the carrier of G./.N &
    the mult of H' = (the mult of G./.N)||the carrier of H' by GROUP_2:def 5;
  A2: the carrier of G./.N = the carrier of G./.N' by Def14;
  the mult of G./.N = the mult of G./.N' by Def15; then
  reconsider H' as strict Subgroup of G./.N' by A1,A2,GROUP_2:def 5;
  assume the carrier of G = (nat_hom N)"(the carrier of H); then
  A3: the carrier of G = (nat_hom N')"(the carrier of H') by Def23;
  now
    let h be Element of G./.N';
    thus h in H' implies h in (Omega).(G./.N') by STRUCT_0:def 5;
    assume h in (Omega).(G./.N');
    h in Left_Cosets N'; then
    consider g be Element of G such that
    A4: h = g * N' by GROUP_2:def 15;
    reconsider R = nat_hom N'
      as Relation of the carrier of G, the carrier of G./.N';
    consider h' be Element of G./.N' such that
    A5: [g,h'] in R & h' in (the carrier of H') by A3,RELSET_1:53;
    (nat_hom N').g = h' by A5,FUNCT_1:8; then
    h in the carrier of H' by A4,A5,GROUP_6:def 9;
    hence h in H' by STRUCT_0:def 5;
  end; then
  H' = (Omega).(G./.N') by GROUP_2:def 6; then
  the carrier of H = Cosets N by Def14;
  hence H = (Omega).(G./.N) by Lm5;
end;

theorem Th81:
  for G being strict GroupWithOperators of O,
      N being strict normal StableSubgroup of G,
      H being strict StableSubgroup of G./.N
  st the carrier of N = (nat_hom N)"(the carrier of H) holds H = (1).(G./.N)
proof
  let G be strict GroupWithOperators of O;
  let N be strict normal StableSubgroup of G;
  let H be strict StableSubgroup of G./.N;
  assume A1: the carrier of N = (nat_hom N)"(the carrier of H);
  assume not H = (1).(G./.N); then
  not the carrier of H = {1_(G./.N)} by Def8; then
  consider h be set such that
  A2: not (h in the carrier of H iff h in {1_(G./.N)}) by TARSKI:2;
  reconsider N' = the HGrStr of N as strict normal Subgroup of G by Lm7;
  reconsider H' = the HGrStr of H as strict Subgroup of G./.N by Lm16;
  A3: the carrier of H' c= the carrier of G./.N &
    the mult of H' = (the mult of G./.N)||the carrier of H' by GROUP_2:def 5;
  A4: the carrier of G./.N = the carrier of G./.N' by Def14;
  the mult of G./.N = the mult of G./.N' by Def15; then
  reconsider H' as strict Subgroup of G./.N' by A3,A4,GROUP_2:def 5;
  A5: the carrier of N' = (nat_hom N')"(the carrier of H') by Def23,A1;
  per cases by A2;
  suppose A6: h in the carrier of H & not h in {1_(G./.N)}; then
    h <> 1_(G./.N) by TARSKI:def 1; then
    A7: h <> carr N by Th43;
    A8: the carrier of H' c= the carrier of G./.N' by GROUP_2:def 5;
    {h} c= the carrier of H by A6,ZFMISC_1:37; then
    A9: (nat_hom N')"{h} c= the carrier of N' by A5,RELAT_1:178;
    rng nat_hom N' = the carrier of Image nat_hom N' by GROUP_6:53
    .= the carrier of G./.N' by GROUP_6:57; then
    consider x be set such that
    A10: x in dom nat_hom N' & (nat_hom N').x = h by A6,A8,FUNCT_1:def 5;
    (nat_hom N').x in {h} by A10,TARSKI:def 1; then
    x in (nat_hom N')"{h} by A10,FUNCT_1:def 13; then
    A11: x in N' by A9,STRUCT_0:def 5;
    reconsider x as Element of G by A10;
    x * N' = h by A10,GROUP_6:def 9;
    hence contradiction by A7,A11,GROUP_2:136;
  end;
  suppose not h in the carrier of H & h in {1_(G./.N)}; then
    h = 1_(G./.N) & not h in H by TARSKI:def 1,STRUCT_0:def 5;
    hence contradiction by Lm18;
  end;
end;

theorem Th82:
  for G,H being strict GroupWithOperators of O
    st G,H are_isomorphic & G is simple holds H is simple
proof
  let G,H be strict GroupWithOperators of O;
  assume A1: G,H are_isomorphic;
  assume A2: G is simple;
  assume A3: H is not simple;
  per cases by A3,Def13;
  suppose H is trivial; then
    G is trivial by A1,Th58;
    hence contradiction by A2,Def13;
  end;
  suppose ex H' being strict normal StableSubgroup of H
    st H' <> (Omega).H & H' <> (1).H; then
    consider H' be strict normal StableSubgroup of H such that
    A4: H' <> (Omega).H & H' <> (1).H;
    reconsider H'' = the HGrStr of H' as strict normal Subgroup of H by Lm7;
    consider f be Homomorphism of G,H such that
    A5: f is_isomorphism by A1,Def22;
    A6: f is_epimorphism by A5,Def21;
    set A = {g where g is Element of G: f.g in H''};
    1_H in H'' by GROUP_2:55; then
    f.(1_G) in H'' by Lm13; then
    1_G in A; then
    reconsider A as non empty set;
    now let x be set;
      assume x in A; then
      consider g be Element of G such that
      A7: x=g & f.g in H'';
      thus x in the carrier of G by A7;
    end; then
    reconsider A as Subset of G by TARSKI:def 3;
    A8: now let g1,g2 be Element of G;
      assume A9: g1 in A & g2 in A;
      consider a be Element of G such that
      A10: a=g1 & f.a in H'' by A9;
      consider b be Element of G such that
      A11: b=g2 & f.b in H'' by A9;
      A12: f.(a*b) = f.a * f.b by GROUP_6:def 7;
      set fa = f.a;
      set fb = f.b;
      fa * fb in H'' by A10,A11,GROUP_2:59;
      hence g1*g2 in A by A10,A11,A12;
    end;
    A13: now let g be Element of G;
      assume A14: g in A;
      consider a be Element of G such that
      A15: a=g & f.a in H'' by A14;
      (f.a)" in H'' by A15,GROUP_2:60; then
      f.(a") in H'' by Lm14;
      hence g" in A by A15;
    end;
    now
      let o be Element of O;
      let g be Element of G;
      assume g in A; then
      consider a be Element of G such that
      A16: a=g & f.a in H'';
      f.a in the carrier of H'' by A16,STRUCT_0:def 5; then
      f.a in H' by STRUCT_0:def 5; then
      (H^o).(f.g) in H' by A16,Lm10; then
      f.((G^o).g) in H' by Def18; then
      f.((G^o).g) in the carrier of H' by STRUCT_0:def 5; then
      f.((G^o).g) in H'' by STRUCT_0:def 5;
      hence (G^o).g in A;
    end; then
    consider G'' be strict StableSubgroup of G such that
    A17: the carrier of G'' = A by Lm15,A8,A13;
    reconsider G'=the HGrStr of G'' as strict Subgroup of G by Lm16;
    the carrier of H'<>{1_H} by A4,Def8; then
    consider x be set such that
    A18: x in the carrier of H' & x<>1_H by ZFMISC_1:41;
    A19: x in H'' by A18,STRUCT_0:def 5; then
    x in H by GROUP_2:49; then
    reconsider x as Element of H by STRUCT_0:def 5;
    consider y be Element of G such that
    A20: f.y = x by A6,Th52;
    A21: y in the carrier of G'' by A20,A19,A17;
    y<>1_G by A18,A20,Lm13; then
    the carrier of G'' <> {1_G} by A21,TARSKI:def 1; then
    A22: G''<>(1).G by Def8;
    the HGrStr of H' <> the HGrStr of H by Lm5,A4; then
    consider h be Element of H such that
    A23: not h in H'' by GROUP_2:71;
    consider g be Element of G such that
    A24: f.g = h by A6,Th52;
    now assume g in A; then
      consider g' be Element of G such that
      A25: g'=g & f.g' in H'';
      thus contradiction by A25,A23,A24;
    end; then
    A26: G''<>(Omega).G by A17;
    now
       let g be Element of G;
       now let x be set;
         assume x in g * G'; then
         consider h be Element of G such that
         A27: x=g*h & h in A by A17,GROUP_2:33;
         consider a be Element of G such that
         A28: a=h & f.a in H'' by A27;
         set h'=g*h*g";
         A29: h'*g = (g*h)*(g"*g) by GROUP_1:def 4
         .= (g*h)*1_G by GROUP_1:def 6
         .= x by A27,GROUP_1:def 5;
         A30: H'' |^ (f.g)" = H'' by GROUP_3:def 13;
         f.h' = f.(g*h) * f.(g") by GROUP_6:def 7
         .= f.g * f.h * f.(g") by GROUP_6:def 7
         .= f.g * f.h * (f.g)" by Lm14
         .= ((f.g)")" * f.h * (f.g)" by GROUP_1:19
         .= f.h |^ (f.g)" by GROUP_3:def 2; then
         f.h' in H'' by A28,A30,GROUP_3:70; then
         h' in A;
         hence x in G' * g by A17,A29,GROUP_2:34;
       end;
       hence g * G' c= G' * g by TARSKI:def 3;
    end; then
    for H being strict Subgroup of G st H = the HGrStr of G''
      holds H is normal by GROUP_3:141; then
    G'' is normal by Def10;
    hence contradiction by A2,A22,A26,Def13;
  end;
end;

theorem Th83:
  for G being GroupWithOperators of O,
      H being StableSubgroup of G,
      FG being FinSequence of the carrier of G,
      FH being FinSequence of the carrier of H,
      I be FinSequence of INT
  st FG=FH & len FG = len I
holds Product(FG |^ I) = Product(FH |^ I)
proof
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  let FG be FinSequence of the carrier of G;
  let FH be FinSequence of the carrier of H;
  let I be FinSequence of INT;
  assume A1: FG=FH & len FG = len I;
  defpred P[natural number] means
  for FG being FinSequence of the carrier of G,
      FH being FinSequence of the carrier of H,
      I being FinSequence of INT st len FG = $1 & FG=FH & len FG = len I
  holds Product(FG |^ I) = Product(FH |^ I);
  A2: P[0]
  proof
    let FG be FinSequence of the carrier of G;
    let FH be FinSequence of the carrier of H;
    let I be FinSequence of INT;
    assume A3: len FG = 0; then
    len(FG |^ I) = 0 by GROUP_4:def 4; then
    FG |^ I = <*> the carrier of G by FINSEQ_1:25; then
    A4: Product(FG |^ I) = 1_G by GROUP_4:11;
    assume FG=FH & len FG = len I; then
    len(FH |^ I) = 0 by A3,GROUP_4:def 4; then
    FH |^ I = <*> the carrier of H by FINSEQ_1:25; then
    A5: Product(FH |^ I) = 1_H by GROUP_4:11;
    thus Product(FG |^ I) = Product(FH |^ I) by A4,A5,Th4;
  end;
  A6: now
    let n be natural number;
    assume A7: P[n];
    thus P[n+1]
    proof
      let FG be FinSequence of the carrier of G;
      let FH be FinSequence of the carrier of H;
      let I be FinSequence of INT;
      assume A8: len FG = n+1;
      assume A9: FG=FH & len FG = len I;
      consider In be FinSequence of INT, i be Element of INT such that
      A10: I = In^<*i*> by A8,A9,FINSEQ_2:22;
      consider FGn be FinSequence of the carrier of G,
               g be Element of G such that
      A11: FG = FGn^<*g*> by A8,FINSEQ_2:22;
      consider FHn be FinSequence of the carrier of H,
               h be Element of H such that
      A12: FH = FHn^<*h*> by A8,A9,FINSEQ_2:22;
      set I1=<*i*>;
      set FG1=<*g*>;
      set FH1=<*h*>;
      len I = len In + len <*i*> by A10,FINSEQ_1:35; then
      A13: n+1 = len In + 1 by A8,A9,FINSEQ_1:57;
      A14: len FG = len FGn + len <*g*> by A11,FINSEQ_1:35; then
      A15: n+1 = len FGn + 1 by A8,FINSEQ_1:57;
      A16: len FH = len FHn + len <*h*> by A12,FINSEQ_1:35; then
      A17: n+1 = len FHn + 1 by A8,A9,FINSEQ_1:57;
      A18: FG.(n+1)=(FGn^<*g*>).(len FGn +1) by A11,A14,A8,FINSEQ_1:57
      .= g by FINSEQ_1:59;
      A19: FH.(n+1)=(FHn^<*h*>).(len FHn +1) by A12,A16,A8,A9,FINSEQ_1:57
      .= h by FINSEQ_1:59;
      FGn = FHn by FINSEQ_1:46,A19,A11,A12,A9,A18; then
      A20: Product(FGn |^ In) = Product(FHn |^ In) by A13,A17,A7;
      A21: len FG1 = 1 by FINSEQ_1:57
      .=len I1 by FINSEQ_1:57;
      A22: len FH1 = 1 by FINSEQ_1:57
      .=len I1 by FINSEQ_1:57;
      A23: Product(FG |^ I)
      = Product((FGn |^ In)^(FG1 |^ I1)) by A21,GROUP_4:25,A15,A13,A10,A11
      .= Product(FGn |^ In) * Product(FG1 |^ I1) by GROUP_4:8;
      A24: now
        reconsider H'=H as Subgroup of G by Def7;
        reconsider h'=h as Element of H';
        g|^i = h'|^i by GROUP_4:4,A19,A9,A18;
        hence g|^i = h|^i;
      end;
      Product(FG1 |^ I1) = Product(<*g*>|^<*@i*>)
      .= Product <*g|^i*> by GROUP_4:28
      .= h|^i by A24,GROUP_4:12
      .= Product <*h|^i*> by GROUP_4:12
      .= Product(<*h*>|^<*@i*>) by GROUP_4:28
      .= Product(FH1 |^ I1); then
      Product(FG |^ I) = Product(FHn |^ In) * Product(FH1 |^ I1) by A23,A20,Th3
      .= Product((FHn |^ In)^(FH1 |^ I1)) by GROUP_4:8
      .= Product((FHn^FH1) |^ (In^I1)) by A22,GROUP_4:25,A17,A13;
      hence thesis by A10,A12;
    end;
  end;
  A25: for n being natural number holds P[n] from NAT_1:sch 2(A2,A6);
  set n=len FG;
  thus thesis by A1,A25;
end;

theorem Th84:
  for O,E1,E2 being set,
      A1 being Action of O,E1, A2 being Action of O,E2,
      F being FinSequence of O
  st E1 c= E2 &
     (for o being Element of O, f1 being Function of E1,E1,
          f2 being Function of E2,E2 st f1=A1.o & f2=A2.o
     holds f1 = f2|E1)
  holds Product(F,A1) = Product(F,A2)|E1
proof
  let O,E1,E2 be set;
  let A1 be Action of O,E1;
  let A2 be Action of O,E2;
  let F be FinSequence of O;
  assume A1: E1 c= E2;
  assume A2: for o being Element of O, f1 being Function of E1,E1,
                  f2 being Function of E2,E2 st f1=A1.o & f2=A2.o
                  holds f1 = f2|E1;
  defpred P[Element of NAT] means
    for F being FinSequence of O st len F = $1 holds
    Product(F,A1) = Product(F,A2)|E1;
  A3: P[0]
  proof
    let F be FinSequence of O;
    assume A4: len F = 0;
    E1 = E2 /\ E1 by A1,XBOOLE_1:28; then
    dom id E1 = E2 /\ E1 by RELAT_1:71; then
    A5: dom id E1 = dom id E2 /\ E1 by RELAT_1:71;
    A6: now
      let x;
      assume A7: x in dom id E1; then
      A8: x in E1;
      thus (id E1).x = x by A7,FUNCT_1:35
      .= (id E2).x by A1,A8,FUNCT_1:35;
    end;
    thus Product(F,A1) = id E1 by A4,Def3
    .= (id E2)|E1 by A5,A6,FUNCT_1:68
    .= Product(F,A2)|E1 by A4,Def3;
  end;
  per cases;
  suppose O is empty; then
    rng F = {} by XBOOLE_1:3; then
    F = {} by RELAT_1:64; then
    len F = 0 by FINSEQ_1:25;
    hence Product(F,A1) = Product(F,A2)|E1 by A3;
  end;
  suppose A9: O is non empty;
    A10: for k being Element of NAT st P[k] holds P[k + 1]
    proof
      let k be Element of NAT;
      assume A11: P[k];
      now
        let F be FinSequence of O;
        assume A12: len F = k+1; then
        consider Fk be FinSequence of O, o be Element of O such that
        A13: F = Fk^<*o*> by A9,FINSEQ_2:22;
        len F = len Fk + len <*o*> by A13,FINSEQ_1:35; then
        A14: k+1 = len Fk + 1 by A12,FINSEQ_1:56;
        Product(F,A1) in Funcs(E1,E1) by FUNCT_2:12; then
        consider f1 be Function such that
        A15: Product(F,A1) = f1 & dom f1 = E1 & rng f1 c= E1 by FUNCT_2:def 2;
        Product(F,A2) in Funcs(E2,E2) by FUNCT_2:12; then
        consider f2 be Function such that
        A16: Product(F,A2) = f2 & dom f2 = E2 & rng f2 c= E2 by FUNCT_2:def 2;
        A17: dom(Product(F,A2)|E1) = E2 /\ E1 by A16,RELAT_1:90
        .= E1 by A1,XBOOLE_1:28;
        now
          let x;
          assume A18: x in dom Product(F,A1); then
          A19: x in E1;
          A20: o in O by A9;
          {o} c= O by A9,ZFMISC_1:37; then
          rng <*o*> c= O by FINSEQ_1:55; then
          reconsider Fo=<*o*> as FinSequence of O by FINSEQ_1:def 4;
          o in dom A1 by A20,FUNCT_2:def 1; then
          A1.o in rng A1 by FUNCT_1:12; then
          consider f1 be Function such that
          A21: f1=A1.o & dom f1 = E1 & rng f1 c= E1 by FUNCT_2:def 2;
          o in dom A2 by A20,FUNCT_2:def 1; then
          A2.o in rng A2 by FUNCT_1:12; then
          consider f2 be Function such that
          A22: f2=A2.o & dom f2 = E2 & rng f2 c= E2 by FUNCT_2:def 2;
          A23: Product(Fo,A1) = f1 by A21,A9,Lm26;
          A24: Product(Fo,A2) = f2 by A22,A9,Lm26;
          A25: Product(F,A1) = (Product(Fk,A1)*Product(Fo,A1)) by A9,A13,Lm29
          .= Product(Fk,A1)*f1 by A21,A9,Lm26;
          A26: Product(F,A2) = (Product(Fk,A2)*Product(Fo,A2)) by A9,A13,Lm29
          .= Product(Fk,A2)*f2 by A22,A9,Lm26;
          A27: f1.x in rng f1 by A21,A18,FUNCT_1:12;
          thus Product(F,A1).x = Product(Fk,A1).(f1.x) by A25,A21,A18,FUNCT_1:
23
          .= (Product(Fk,A2)|E1).(f1.x) by A11,A14
          .= Product(Fk,A2).(f1.x) by A27,A21,FUNCT_1:72
          .= Product(Fk,A2).((f2|E1).x) by A24,A23,A2,A21,A22
          .= Product(Fk,A2).(f2.x) by A18,FUNCT_1:72
          .= (Product(Fk,A2)*f2).x by A22,A19,A1,FUNCT_1:23
          .= (Product(F,A2)|E1).x by A26,A18,FUNCT_1:72;
        end;
        hence Product(F,A1) = Product(F,A2)|E1 by A17,A15,FUNCT_1:9;
      end;
      hence P[k + 1];
    end;
    A28: for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A10);
    reconsider k = len F as Element of NAT;
    k = len F;
    hence thesis by A28;
  end;
end;

theorem Th85:
  for N1,N2 being strict StableSubgroup of H1,
      N1',N2' being strict StableSubgroup of G
  st N1 = N1' & N2 = N2' holds N1' * N2' = N1 * N2
proof
  let N1,N2 be strict StableSubgroup of H1;
  let N1',N2' be strict StableSubgroup of G;
  assume A1: N1=N1' & N2=N2';
  set X={g * h where g,h is Element of G: g in carr N1' & h in carr N2'};
  set Y={g * h where g,h is Element of H1: g in carr N1 & h in carr N2};
  A2: now let x;
    assume x in X; then
    consider g,h be Element of G such that
    A3: x=g*h & g in carr N1' &  h in carr N2';
    N1 is Subgroup of H1 & N2 is Subgroup of H1 by Def7; then
    A4: the carrier of N1 c= the carrier of H1 &
        the carrier of N2 c= the carrier of H1 by GROUP_2:def 5;
    reconsider g,h as Element of H1 by A4,A3,A1;
    x=g*h by A3,Th3;
    hence x in Y by A1,A3;
  end;
  now let x;
    assume x in Y; then
    consider g,h be Element of H1 such that
    A5: x=g*h & g in carr N1 & h in carr N2;
    reconsider g,h as Element of G by Th2;
    x=g*h by A5,Th3;
    hence x in X by A1,A5;
  end;
  hence thesis by A2,TARSKI:2;
end;

theorem Th86:
  for N1,N2 being strict StableSubgroup of H1,
      N1',N2' being strict StableSubgroup of G
  st N1 = N1' & N2 = N2' holds N1' "\/" N2' = N1 "\/" N2
proof
  let N1,N2 be strict StableSubgroup of H1;
  let N1',N2' be strict StableSubgroup of G;
  assume A1: N1=N1' & N2=N2';
  A2: N1' "\/" N2' = the_stable_subgroup_of(N1'*N2') by Th29;
  A3: N1 "\/" N2 = the_stable_subgroup_of(N1*N2) by Th29;
  set S1 = the_stable_subgroup_of(N1'*N2');
  reconsider S2 = the_stable_subgroup_of(N1*N2) as StableSubgroup of G by Th11;
  set X1={B where B is Subset of G :
        ex H being strict StableSubgroup of G st B = the carrier of H &
          N1'*N2' c= carr H};
  set X2={B where B is Subset of H1 :
        ex H being strict StableSubgroup of H1 st B = the carrier of H &
          N1*N2 c= carr H};
  A4: now
    set x'=carr H1;
    reconsider x=x' as set;
    take x;
    now
      set H=(Omega).H1;
      take H;
      thus x' = the carrier of H;
      thus N1*N2 c= carr H;
    end;
    hence x in X2;
  end;
  A5: the carrier of the_stable_subgroup_of(N1'*N2') = meet X1 by Th27;
  A6: the carrier of the_stable_subgroup_of(N1*N2) = meet X2 by Th27;
  now let x;
    assume A7: x in the carrier of the_stable_subgroup_of(N1*N2);
    the_stable_subgroup_of(N1*N2) is Subgroup of H1 by Def7; then
    the carrier of the_stable_subgroup_of(N1*N2) c= the carrier of H1
      by GROUP_2:def 5; then
    reconsider g=x as Element of H1 by A7;
    g in the_stable_subgroup_of(N1*N2) by A7,STRUCT_0:def 5; then
    consider F be FinSequence of the carrier of H1,
             I be FinSequence of INT, C be Subset of H1 such that
    A8: C = the_stable_subset_generated_by (N1*N2, the action of H1) &
      len F = len I & rng F c= C & Product(F |^ I) = g by Th24;
    reconsider g as Element of G by Th2;
    now let x;
      assume A9: x in the_stable_subset_generated_by (N1*N2, the action of H1);
      then reconsider a=x as Element of H1;
      N1 is Subgroup of H1 & N2 is Subgroup of H1 by Def7; then
      1_H1 in N1 & 1_H1 in N2 by GROUP_2:55; then
      1_H1 in carr N1 & 1_H1 in carr N2 & 1_H1=1_H1*1_H1
        by STRUCT_0:def 5,GROUP_1:def 5; then
      A10: 1_H1 in carr N1 * carr N2; then
      consider F be FinSequence of O, h be Element of N1*N2 such that
      A11: Product(F,the action of H1).h = a by A9,Lm31;
      A12: now
        let o be Element of O;
        let f1 be Function of the carrier of H1,the carrier of H1;
        let f2 be Function of the carrier of G,the carrier of G;
        assume A13: f1=(the action of H1).o & f2=(the action of G).o;
        per cases;
        suppose o in O; then
          H1^o = f1 & G^o = f2 by A13,Def6;
          hence f1 = f2|the carrier of H1 by Def7;
        end;
        suppose not o in O; then
          not o in dom the action of H1 & not o in dom the action of G;
          hence f1 = f2|the carrier of H1 by A13,FUNCT_1:def 4;
        end;
      end;
      H1 is Subgroup of G by Def7; then
      A14: the carrier of H1 c= the carrier of G by GROUP_2:def 5; then
      A15: Product(F,the action of H1) =
        Product(F,the action of G)|the carrier of H1 by A12,Th84;
      A16: h in N1*N2 by A10;
      reconsider a as Element of G by TARSKI:def 3,A14;
      reconsider h as Element of N1'*N2' by A1,Th85;
      Product(F,the action of G).h = a & N1'*N2' is non empty
        by A1,Th85,A15,A11,FUNCT_1:72,A16;
      hence x in the_stable_subset_generated_by (N1'*N2', the action of G)
        by Lm31;
    end; then
    A17: the_stable_subset_generated_by (N1*N2, the action of H1) c=
      the_stable_subset_generated_by (N1'*N2', the action of G)
      by TARSKI:def 3;
    A18: rng F c= the_stable_subset_generated_by (N1'*N2', the action of G)
      by A17,A8,XBOOLE_1:1;
    H1 is Subgroup of G by Def7; then
    A19: the carrier of H1 c= the carrier of G by GROUP_2:def 5;
    rng F c= the carrier of G by A19,XBOOLE_1:1; then
    reconsider F as FinSequence of the carrier of G by FINSEQ_1:def 4;
    Product(F |^ I) = g by A8,Th83; then
    A20: g in the_stable_subgroup_of(N1'*N2') by A8,A18,Th24;
    assume not x in the carrier of the_stable_subgroup_of(N1'*N2');
    hence contradiction by A20,STRUCT_0:def 5;
  end; then
  A21: meet X2 c= meet X1 by A5,A6,TARSKI:def 3;
  now let x;
    assume x in X2; then
    consider B be Subset of H1 such that
    A22: x=B & ex H being strict StableSubgroup of H1 st B = the carrier of H &
      N1*N2 c= carr H;
    now
      consider H be strict StableSubgroup of H1 such that
      A23: B = the carrier of H & N1*N2 c= carr H by A22;
      reconsider H as strict StableSubgroup of G by Th11;
      take H;
      thus B = the carrier of H & N1'*N2' c= carr H by A1,A23,Th85;
    end;
    hence x in X1 by A22;
  end; then
  X2 c= X1 by TARSKI:def 3; then
  meet X1 c= meet X2 by A4,SETFAM_1:7; then
  A24: the carrier of S1 = the carrier of S2 by A5,A6,A21,XBOOLE_0:def 10;
  thus thesis by A2,A3,A24,Lm5;
end;

theorem Th87:
  for N1,N2 being strict StableSubgroup of G
    st N1 is normal StableSubgroup of H1 & N2 is normal StableSubgroup of H1
    holds N1 "\/" N2 is normal StableSubgroup of H1
proof
  let N1,N2 be strict StableSubgroup of G;
  assume A1: N1 is normal StableSubgroup of H1 &
    N2 is normal StableSubgroup of H1;
  reconsider N1'=N1,N2'=N2 as StableSubgroup of H1 by A1;
  N1 "\/" N2 = N1' "\/" N2' by Th86;
  hence N1 "\/" N2 is normal StableSubgroup of H1 by A1,Th32;
end;

theorem Th88:
  for f being Homomorphism of G,H holds
  for g being Homomorphism of H,I holds
    the carrier of Ker(g*f) = f"(the carrier of Ker g)
proof
  let f be Homomorphism of G,H;
  let g be Homomorphism of H,I;
  A1: dom f = the carrier of G by FUNCT_2:def 1;
  A2: now let x;
    assume x in the carrier of Ker(g*f); then
    x in {a where a is Element of G: (g*f).a = 1_I} by Def24; then
    consider a be Element of G such that
    A3: x=a & (g*f).a =1_I;
    reconsider b=f.a as Element of H;
    g.b = 1_I by A3,A1,FUNCT_1:23; then
    f.x in {b' where b' is Element of H: g.b' = 1_I} by A3; then
    f.x in the carrier of Ker g by Def24;
    hence x in f"(the carrier of Ker g) by A3,A1,FUNCT_1:def 13;
  end;
  now let x;
    assume A4: x in f"(the carrier of Ker g); then
    A5: x in dom f & f.x in the carrier of Ker g by FUNCT_1:def 13;
    consider a be Element of G such that
    A6: x=a by A4;
    f.x in {b where b is Element of H: g.b = 1_I} by A5,Def24; then
    consider b be Element of H such that
    A7: b=f.x & g.b = 1_I;
    1_I = (g*f).x by A5,A7,FUNCT_1:23; then
    x in {a' where a' is Element of G: (g*f).a' = 1_I} by A6;
    hence x in the carrier of Ker(g*f) by Def24;
  end;
  hence thesis by A2,TARSKI:2;
end;

theorem Th89:
  for G' being StableSubgroup of G,
      H' being StableSubgroup of H,
      f being Homomorphism of G,H
  st the carrier of H' = f.:(the carrier of G') or
     the carrier of G' = f"(the carrier of H')
  holds f|(the carrier of G') is Homomorphism of G',H'
proof
  let G' be StableSubgroup of G;
  let H' be StableSubgroup of H;
  let f be Homomorphism of G,H;
  assume A1: the carrier of H' = f.:(the carrier of G') or
     the carrier of G' = f"(the carrier of H');
  G' is Subgroup of G by Def7; then
  A2: the carrier of G' c= the carrier of G by GROUP_2:def 5; then
  A3: the carrier of G' c= dom f by FUNCT_2:def 1;
  A4: for x st x in the carrier of G' holds f.x in the carrier of H'
  proof
    let x;
    assume A5: x in the carrier of G';
    per cases by A1;
    suppose A6: the carrier of H' = f.:(the carrier of G');
      assume not f.x in the carrier of H';
      hence contradiction by A5,A3,A6,FUNCT_1:def 12;
    end;
    suppose the carrier of G' = f"(the carrier of H');
      hence f.x in the carrier of H' by A5,FUNCT_1:def 13;
    end;
  end;
  set g=f|(the carrier of G');
  now
    A7: dom g = the carrier of G' by A3,RELAT_1:91;
    now let y be set;
      assume y in rng g; then
      consider x such that
      A8: x in dom g & y = g.x by FUNCT_1:def 5;
      A9: x in the carrier of G' by A3,RELAT_1:91,A8; then
      y = f.x by A8,FUNCT_1:72;
      hence y in the carrier of H' by A4,A9;
    end; then
    rng g c= the carrier of H' by TARSKI:def 3;
    hence g is Function of G',H' by A7,FUNCT_2:def 1,RELSET_1:11;
  end; then
  reconsider g as Function of G',H';
A10:now
    let a',b' be Element of G';
    reconsider a=a',b=b' as Element of G by A2,TARSKI:def 3;
    A11: f.a = g.a' & f.b = g.b' by FUNCT_1:72;
    thus g.(a'* b') = f.(a'*b') by FUNCT_1:72
    .= f.(a*b) by Th3
    .= f.a * f.b by GROUP_6:def 7
    .= g.a' * g.b' by A11,Th3;
  end;
  now
    let o be Element of O;
    let a' be Element of G';
    reconsider a=a' as Element of G by A2,TARSKI:def 3;
    thus g.((G'^o).a') = f.((G'^o).a') by FUNCT_1:72
    .= f.(((G^o)|the carrier of G').a') by Def7
    .= f.((G^o).a) by FUNCT_1:72
    .= (H^o).(f.a) by Def18
    .= (H^o).(g.a') by FUNCT_1:72
    .= ((H^o)|the carrier of H').(g.a') by FUNCT_1:72
    .= (H'^o).(g.a') by Def7;
  end;
  hence f|(the carrier of G') is Homomorphism of G',H'
    by A10,Def18,GROUP_6:def 7;
end;

:: ALG I.4.6 Corollary 2
theorem Th90:
  for G,H being strict GroupWithOperators of O,
      N,L,G' being strict StableSubgroup of G,
      f being Homomorphism of G,H
  st N = Ker f & L is strict normal StableSubgroup of G'
  holds L"\/"(G'/\N) is normal StableSubgroup of G' &
        L"\/"N is normal StableSubgroup of G'"\/"N &
        (for N1 being strict normal StableSubgroup of G'"\/"N,
             N2 being strict normal StableSubgroup of G'
             st N1=L"\/"N & N2=L"\/"(G'/\N)
             holds (G'"\/"N)./.N1 , G'./.N2 are_isomorphic)
proof
  let G,H be strict GroupWithOperators of O;
  let N,L,G' be strict StableSubgroup of G;
  let f be Homomorphism of G,H;
  assume A1: N = Ker f;
  assume A2: L is strict normal StableSubgroup of G';
  reconsider Ns = the HGrStr of N as strict normal Subgroup of G by A1,Lm7;
  reconsider Gs' = the HGrStr of G' as strict Subgroup of G by Lm16;
  consider H' be strict StableSubgroup of H such that
  A3: the carrier of H' = f.:(the carrier of G') &
       f"(the carrier of H') = the carrier of G'"\/"N &
       (f is_epimorphism & G' is normal implies H' is normal) by A1,Th79;
  reconsider f'=f|(the carrier of G') as Homomorphism of G',H' by A3,Th89;
  A4: f'.:(the carrier of G') = f.:(the carrier of G') by RELAT_1:162;
  H' is strict StableSubgroup of H' by Lm4; then
  A5: Image f' = H' by A4,A3,Def25;
  reconsider L'=L as strict StableSubgroup of G' by A2;
  reconsider N'=G'/\N as StableSubgroup of G' by Lm34;
  A6: now let x;
    assume x in the carrier of N'; then
    A7: x in carr G' /\ carr N by Def28; then
    x in carr G' & x in carr N by XBOOLE_0:def 3; then
    A8: x in the carrier of G' & x in N by STRUCT_0:def 5;
    reconsider a'=x as Element of G' by A7,XBOOLE_0:def 3;
    reconsider a''=a' as Element of G by Th2;
    f.a'' = 1_H by A1,A8,Th47; then
    f.a' = 1_H' by Th4; then
    f'.a' = 1_H' by FUNCT_1:72;
    hence x in {a where a is Element of G': f'.a = 1_H'};
  end;
  now let x;
    assume x in {a where a is Element of G': f'.a = 1_H'}; then
    consider a be Element of G' such that
    A9: x=a & f'.a = 1_H';
    reconsider a as Element of G by Th2;
    f.a = 1_H' by A9,FUNCT_1:72; then
    f.a = 1_H by Th4; then
    x in the carrier of G' & x in N by A1,A9,Th47; then
    x in carr G' & x in carr N by STRUCT_0:def 5; then
    x in carr G' /\ carr N by XBOOLE_0:def 3;
    hence x in the carrier of N' by Def28;
  end; then
  the carrier of N' = {a where a is Element of G': f'.a = 1_H'}
    by TARSKI:2,A6; then
  A10: N' = Ker f' by Def24; then
  consider H'' be strict StableSubgroup of H' such that
  A11: the carrier of H'' = f'.:(the carrier of L') &
      f'"(the carrier of H'') = the carrier of L'"\/"N' &
      (f' is_epimorphism & L' is normal implies H'' is normal) by Th79;
  consider N2 be strict StableSubgroup of G' such that
  A12: the carrier of N2 = f'"(the carrier of H'') &
      (H'' is normal implies N' is normal StableSubgroup of N2 & N2 is normal)
    by A10,Th78;
  A13: rng f' = the carrier of H' by A5,Th49;
  A14: N2 = L'"\/"N' by A11,A12,Lm5; then
  A15: N2 = L"\/"(G'/\N) by Th86;
  thus L"\/"(G'/\N) is normal StableSubgroup of G'
    by A11,A14,Th86,A12,A13,A2,Def20;
  reconsider f'' = f|(the carrier of G'"\/"N)
    as Homomorphism of G'"\/"N,H' by A3,Th89;
  A16: f''.:(the carrier of (G'"\/"N))=f.:(the carrier of (G'"\/"N))
    by RELAT_1:162;
  carr Gs' * Ns = Ns * carr Gs' by GROUP_3:143; then
  A17: G' * N = N * G';
  A18: dom f = the carrier of G by FUNCT_2:def 1;
  A19: now let y be set;
    assume y in f.:(the carrier of G'); then
    consider x such that
    A20: x in dom f & x in the carrier of G' & y = f.x by FUNCT_1:def 12;
    reconsider x as Element of G by A20;
    consider x' be set such that
    A21: x'=x*1_G;
    A22: x' in dom f by A20,A21,GROUP_1:def 5;
    f.(1_G) = 1_H by Lm13; then
    1_G in Ker f by Th47; then
    x in carr Gs' & 1_G in carr N by A1,A20,STRUCT_0:def 5; then
    A23: x' in G'*N by A21;
    y = f.x * 1_H by A20,GROUP_1:def 5
    .= f.x * f.(1_G) by Lm13
    .= f.x' by A21,GROUP_6:def 7;
    hence y in f.:(G'*N) by A22,A23,FUNCT_1:def 12;
  end;
  now let y be set;
    assume y in f.:(G'*N); then
    consider x such that
    A24: x in dom f & x in G'*N & y = f.x by FUNCT_1:def 12;
    reconsider x as Element of G by A24;
    consider g1,g2 be Element of G such that
    A25: x = g1*g2 & g1 in carr G' & g2 in carr N by A24;
    A26: g1 in the carrier of G' & g2 in N by A25,STRUCT_0:def 5;
    y = f.g1*f.g2 by A24,A25,GROUP_6:def 7
    .= f.g1*1_H by A26,A1,Th47
    .= f.g1 by GROUP_1:def 5;
    hence y in f.:(the carrier of G') by A18,FUNCT_1:def 12,A25;
  end; then
  f.:(the carrier of G') = f.:(G'*N) by A19,TARSKI:2; then
  A27: the carrier of H' = f.:(the carrier of (G'"\/"N)) by A17,Th30,A3;
  reconsider H'' as normal StableSubgroup of H' by A11,A13,A2,Def20;
  set l = nat_hom H'';
  set f1 = l*f'';
  A28: L"\/"N is StableSubgroup of G'"\/"N by A2,Th38;
  reconsider N1=L"\/"N as StableSubgroup of G'"\/"N by A2,Th38;
  reconsider L''=L as Subgroup of G by Def7;
  carr L'' * Ns = Ns * carr L'' by GROUP_3:143; then
  A29: L * N = N * L;
  L is Subgroup of G' by A2,Def7; then
  the carrier of L c= the carrier of G' by GROUP_2:def 5; then
  A30: f'.:(the carrier of L) = f.:(the carrier of L) by RELAT_1:162;
  A31: now let x;
    assume A32: x in f''"(f.:(the carrier of L));
    A33: x in ((the carrier of G'"\/"N) /\ f"(f.:(the carrier of L)))
      by A32,FUNCT_1:139; then
    x in f"(f.:(the carrier of L)) by XBOOLE_0:def 3; then
    x in dom f & f.x in f.:(the carrier of L) by FUNCT_1:def 13; then
    consider g1 be set such that
    A34: g1 in dom f & g1 in the carrier of L & f.x = f.g1 by FUNCT_1:def 12;
    reconsider g1,g2=x as Element of G by A33,A34;
    consider g3 be Element of G such that
    A35: g2 = g1 * g3 by GROUP_1:23;
    f.g2 = f.g2*f.g3 by A34,A35,GROUP_6:def 7; then
    f.g3 = 1_H by GROUP_1:15; then
    g3 in Ker f by Th47; then
    g3 in the carrier of N by A1,STRUCT_0:def 5;
    hence x in L * N by A34,A35;
  end;
  now let x;
    assume A36: x in L * N; then
    A37: x in the carrier of L"\/"N by A29,Th30;
    L"\/"N is Subgroup of G'"\/"N by A28,Def7; then
    A38: the carrier of L"\/"N c= the carrier of G'"\/"N
      by GROUP_2:def 5;
    x in G'"\/"N by STRUCT_0:def 5,A38,A37; then
    x in G by Th1; then
    A39: x in dom f by A18,STRUCT_0:def 5;
    consider g1,g2 be Element of G such that
    A40: x = g1*g2 & g1 in carr L & g2 in carr N by A36;
    A41: g1 in the carrier of L & g2 in N by A40,STRUCT_0:def 5;
    A42: f.x = f.g1*f.g2 by A40,GROUP_6:def 7
    .= f.g1*1_H by A41,A1,Th47
    .= f.g1 by GROUP_1:def 5;
    f.x in f.:(the carrier of L) by A18,A40,A42,FUNCT_1:def 12; then
    x in f"(f.:(the carrier of L)) by A39,FUNCT_1:def 13; then
    x in ((the carrier of G'"\/"N) /\ f"(f.:(the carrier of L)))
      by A38,A37,XBOOLE_0:def 3;
    hence x in f''"(f.:(the carrier of L)) by FUNCT_1:139;
  end; then
  f''"(f'.:(the carrier of L)) = L * N by A30,A31,TARSKI:2; then
  A43: f''"(the carrier of H'') = the carrier of N1 by A11,A29,Th30;
  A44: f''"(the carrier of Ker l) = f''"(the carrier of H'') by Th48;
  A45: the carrier of Ker f1 = the carrier of N1 by A43,A44,Th88; then
  A46: Ker f1 = N1 by Lm5;
  thus L"\/"N is normal StableSubgroup of G'"\/"N by A45,Lm5;
  now
    let N1' be strict normal StableSubgroup of G'"\/"N;
    let N2' be strict normal StableSubgroup of G';
    assume A47: N1'=L"\/"N;
    assume A48: N2'=L"\/"(G'/\N);
    A49: (G'"\/"N)./.Ker f1, Image f1 are_isomorphic by Th59;
    set f2 = l*f';
    A50: Image f2, G'./.Ker f2 are_isomorphic by Th59;
    A51: f''.:(the carrier of G'"\/"N) = f'.:(the carrier of G')
      by A16,RELAT_1:162,A3,A27;
    A52: f1.:(the carrier of G'"\/"N) = l.:(f''.:(the carrier of G'"\/"N))
      by RELAT_1:159;
    A53: f1.:(the carrier of G'"\/"N)=f2.:(the carrier of G')
      by A52,A51,RELAT_1:159;
    A54: the carrier of Image f1=f1.:(the carrier of G'"\/"N) by Def25
    .= the carrier of Image f2 by A53,Def25;
    A55: Image f1 = Image f2 by A54,Lm5;
    A56: f'"(the carrier of Ker l) = f'"(the carrier of H'') by Th48;
    the carrier of Ker f2 = the carrier of N2 by A12,A56,Th88; then
    A57: N2'=Ker f2 by A48,A15,Lm5;
    thus (G'"\/"N)./.N1',G'./.N2' are_isomorphic
      by A49,A50,A55,A57,Th55,A47,A46;
  end;
  hence thesis;
end;

:: ALG I.4.7 Lemma 1
begin :: The Zassenhaus Butterfly Lemma

theorem Th91:
   for H,K,H',K' being strict StableSubgroup of G,
       JH being normal StableSubgroup of H'"\/"(H/\K),
       HK being normal StableSubgroup of H/\K
   st H' is normal StableSubgroup of H & K' is normal StableSubgroup of K
     & JH = H'"\/"(H/\K') & HK=(H'/\K)"\/"(K'/\H)
   holds (H'"\/"(H/\K))./.JH , (H/\K)./.HK are_isomorphic
proof
  let H,K,H',K' be strict StableSubgroup of G;
  let JH be normal StableSubgroup of H'"\/"(H/\K);
  let HK be normal StableSubgroup of H/\K;
  assume A1: H' is normal StableSubgroup of H &
    K' is normal StableSubgroup of K;
  assume A2: JH = H'"\/"(H/\K') & HK=(H'/\K)"\/"(K'/\H);
  reconsider GG = H as GroupWithOperators of O;
  reconsider N' = H' as normal StableSubgroup of GG by A1;
  reconsider N = N' as StableSubgroup of GG;
  reconsider HH = GG./.N' as GroupWithOperators of O;
  reconsider f = nat_hom N' as Homomorphism of GG,HH;
  set G'=H/\K;
  reconsider G' as strict StableSubgroup of GG by Lm34;
  set L=H/\K';
  A3: L is normal StableSubgroup of G' by A1,Th60;
  reconsider L as StableSubgroup of GG by A3,Th11;
  A4: L"\/"N = (H/\K')"\/"H' by Th86 .=JH by A2;
  set N1=G'/\N;
  A5: N1=(H/\K)/\H' by Th39;
  A6: L"\/"N1 = (H/\K')"\/"((H/\K)/\H') by A5,Th86
  .= ((H'/\H)/\K)"\/"(K'/\H) by Th20
  .= HK by A1,A2,Lm22;
  A7: N = Ker f by Th48;
  A8: G'"\/"N = (H/\K) "\/" H' by Th86 .= H'"\/"(H/\K);
  thus (H'"\/"(H/\K))./.JH , (H/\K)./.HK are_isomorphic
    by A3,A7,A6,A8,A4,Th90;
end;

theorem Th92:
   for H,K,H',K' being strict StableSubgroup of G
   st H' is normal StableSubgroup of H & K' is normal StableSubgroup of K
   holds H'"\/"(H/\K') is normal StableSubgroup of H'"\/"(H/\K)
proof
  let H,K,H',K' be strict StableSubgroup of G;
  assume A1: H' is normal StableSubgroup of H &
    K' is normal StableSubgroup of K;
  reconsider GG=H as GroupWithOperators of O;
  reconsider N'=H' as normal StableSubgroup of GG by A1;
  reconsider N=N' as StableSubgroup of GG;
  reconsider HH=GG./.N' as GroupWithOperators of O;
  reconsider f=nat_hom N' as Homomorphism of GG,HH;
  reconsider G'=H/\K as strict StableSubgroup of GG by Lm34;
  set L=H/\K';
  A2: L is strict normal StableSubgroup of G' by A1,Th60;
  reconsider L as strict StableSubgroup of GG by A2,Th11;
  A3: N = Ker f by Th48;
  A4: L"\/"N = (H/\K')"\/"H' by Th86 .= H'"\/"(H/\K');
  A5: G'"\/"N = (H/\K)"\/"H' by Th86 .= H'"\/"(H/\K);
  thus H'"\/"(H/\K') is normal StableSubgroup of H'"\/"(H/\K)
    by A2,A5,A4,A3,Th90;
end;

theorem Th93:
   for H,K,H',K' being strict StableSubgroup of G,
       JH being normal StableSubgroup of H'"\/"(H/\K),
       JK being normal StableSubgroup of K'"\/"(K/\H)
   st JH = H'"\/"(H/\K') & JK= K'"\/"(K/\H') &
      H' is normal StableSubgroup of H & K' is normal StableSubgroup of K
holds (H'"\/"(H/\K))./.JH , (K'"\/"(K/\H))./.JK are_isomorphic
proof
  let H,K,H',K' be strict StableSubgroup of G;
  let JH be normal StableSubgroup of H'"\/"(H/\K);
  let JK be normal StableSubgroup of K'"\/"(K/\H);
  assume A1: JH = H'"\/"(H/\K') & JK= K'"\/"(K/\H');
  assume A2: H' is normal StableSubgroup of H;
  assume A3: K' is normal StableSubgroup of K;
  set HK=(H'/\K)"\/"(K'/\H);
  H'/\K is normal StableSubgroup of H/\K &
  K'/\H is normal StableSubgroup of H/\K by A3,A2,Th60; then
  reconsider HK as normal StableSubgroup of H/\K by Th87;
  A4: (H'"\/"(H/\K))./.JH , (H/\K)./.HK are_isomorphic by Th91,A1,A2,A3;
  HK=(K'/\H)"\/"(H'/\K); then
  (K'"\/"(K/\H))./.JK , (H/\K)./.HK are_isomorphic by Th91,A1,A2,A3;
  hence thesis by A4,Th55;
end;

begin :: Composition Series

:: ALG I.4.7 Definition 9
definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be FinSequence of the_stable_subgroups_of G;
  attr IT is composition_series means :Def31:
  IT.1=(Omega).G & IT.(len IT)=(1).G &
  for i being natural number st i in dom IT & i+1 in dom IT
    for H1,H2 being StableSubgroup of G st H1=IT.i & H2=IT.(i+1)
      holds H2 is normal StableSubgroup of H1;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster composition_series FinSequence of the_stable_subgroups_of G;
  existence
  proof
    take H=<*(Omega).G,(1).G*>;
    (Omega).G is Element of the_stable_subgroups_of G &
    (1).G is Element of the_stable_subgroups_of G by Def11; then
    reconsider H as FinSequence of the_stable_subgroups_of G by FINSEQ_2:15;
    A1: H.1=(Omega).G by FINSEQ_1:61;
    A2: H.(len H) = H.2 by FINSEQ_1:61 .=(1).G by FINSEQ_1:61;
    for i being natural number st i in dom H & i+1 in dom H
      for H1,H2 being StableSubgroup of G st H1=H.i & H2=H.(i+1)
        holds H2 is normal StableSubgroup of H1
    proof
      let i be natural number;
      assume A3: i in dom H;
      assume A4: i+1 in dom H;
      len H = 2 by FINSEQ_1:61; then
      A5: dom H = {1,2} by FINSEQ_1:def 3,FINSEQ_1:4;
      per cases by A3,A5,TARSKI:def 2;
      suppose A6: i=1;
        let H1,H2 be StableSubgroup of G;
        assume H1=H.i;
        assume H2=H.(i+1); then
        A7: H2=(1).G by A6,FINSEQ_1:61;
        reconsider H2 as StableSubgroup of H1 by A7,Th16;
        now let H be strict Subgroup of H1;
          assume the HGrStr of H2 = H; then
          A8: the carrier of H = {1_G} by A7,Def8;
          reconsider H1 as Subgroup of G by Def7;
          the carrier of H = {1_H1} by A8,GROUP_2:53; then
          H = (1).H1 by GROUP_2:def 7;
          hence H is normal;
        end;
        hence thesis by Def10;
      end;
      suppose i=2;
        hence thesis by A4,A5,TARSKI:def 2;
      end;
    end;
    hence thesis by A1,A2,Def31;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  mode CompositionSeries of G is
    composition_series FinSequence of the_stable_subgroups_of G;
end;

:: ALG I.4.7 Definition 9
definition
  let O be set;
  let G be GroupWithOperators of O;
  let s1,s2 be CompositionSeries of G;
  pred s1 is_finer_than s2 means :Def32:
  ex x being set st x c= dom s1 & s2 = s1 * Sgm x;
  reflexivity
  proof
    now
      let s1 be CompositionSeries of G;
      set x=dom s1;
      reconsider x as set;
      take x;
      thus x c= dom s1;
      set i=len s1;
      Sgm x = Sgm Seg i by FINSEQ_1:def 3 .= idseq i by FINSEQ_3:54;
      hence s1 = s1 * Sgm x by FINSEQ_2:64;
    end;
    hence thesis;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be CompositionSeries of G;
  attr IT is strictly_decreasing means :Def33:
  for i being natural number st i in dom IT & i+1 in dom IT
    for H being StableSubgroup of G,
        N being normal StableSubgroup of H st H=IT.i & N=IT.(i+1)
      holds H./.N is not trivial;
end;

:: ALG I.4.7 Definition 10
definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be CompositionSeries of G;
  attr IT is jordan_holder means :Def34:
  IT is strictly_decreasing &
    not ex s being CompositionSeries of G
      st s<>IT & s is strictly_decreasing & s is_finer_than IT;
end;

:: ALG I.4.7 Definition 9
definition
  let O be set;
  let G1,G2 be GroupWithOperators of O;
  let s1 be CompositionSeries of G1;
  let s2 be CompositionSeries of G2;
  pred s1 is_equivalent_with s2 means :Def35:
  len s1 = len s2 & for n being natural number st n + 1 = len s1 holds
    ex p being Permutation of Seg n st
      for H1 being StableSubgroup of G1, H2 being StableSubgroup of G2,
          N1 being normal StableSubgroup of H1,
          N2 being normal StableSubgroup of H2, i,j being natural number
      st 1<=i & i<=n & j=p.i & H1=s1.i & H2=s2.j & N1=s1.(i+1) & N2=s2.(j+1)
      holds H1./.N1,H2./.N2 are_isomorphic;
end;

:: ALG I.4.7 Definition 9
definition
  let O be set;
  let G be GroupWithOperators of O;
  let s be CompositionSeries of G;
  func the_series_of_quotients_of s -> FinSequence means :Def36:
  len s = len it + 1 & for i being natural number st i in dom it
    for H being StableSubgroup of G, N being normal StableSubgroup of H
    st H=s.i & N=s.(i+1) holds it.i = H./.N if len s > 1
  otherwise it={};
  existence
  proof
    now
      assume len s > 1; then
      A1: len s - 1 > 1 - 1 by XREAL_1:11;
      set i=len s - 1;
      reconsider i as Element of NAT by A1,INT_1:16;
      defpred P[set,set] means
      for H being StableSubgroup of G, N being normal StableSubgroup of H,
          j being natural number st $1 in Seg i & j=$1 & H=s.j & N=s.(j+1)
      holds $2 = H./.N;
      A2: for k being Element of NAT, y1,y2 being set
      st k in Seg i & P[k,y1] & P[k,y2] holds y1=y2
      proof
        let k being Element of NAT;
        let y1,y2 be set;
        assume A3: k in Seg i; then
        A4: 1<=k & k<=i by FINSEQ_1:3; then
        A5: k+1<=len s - 1 + 1 by XREAL_1:8;
        1+1<=k+1 by A4,XREAL_1:8; then
        A6: 1<=k+1 by XXREAL_0:2;
        0+k<=1+k by XREAL_1:8; then
        k<=len s by A5,XXREAL_0:2; then
        k in Seg len s & k+1 in Seg len s by A4,A5,A6; then
        A7: k in dom s & k+1 in dom s by FINSEQ_1:def 3;
        reconsider H=s.k, N=s.(k+1) as Element of the_stable_subgroups_of G
          by A7,FINSEQ_2:13;
        reconsider H,N as StableSubgroup of G by Def11;
        reconsider N as normal StableSubgroup of H by A7,Def31;
        assume P[k,y1]; then
        A8: y1=H./.N by A3;
        assume P[k,y2];
        hence y1=y2 by A3,A8;
      end;
      A9: for k being Element of NAT st k in Seg i ex x being set st P[k,x]
      proof
        let k be Element of NAT;
        assume k in Seg i; then
        A10: 1<=k & k<=i by FINSEQ_1:3; then
        A11: k+1<=len s - 1 + 1 by XREAL_1:8;
        1+1<=k+1 by A10,XREAL_1:8; then
        A12: 1<=k+1 by XXREAL_0:2;
        0+k<=1+k by XREAL_1:8; then
        k<=len s by A11,XXREAL_0:2; then
        k in Seg len s & k+1 in Seg len s by A10,A11,A12; then
        A13: k in dom s & k+1 in dom s by FINSEQ_1:def 3;
        reconsider H=s.k, N=s.(k+1) as Element of the_stable_subgroups_of G
          by A13,FINSEQ_2:13;
        reconsider H,N as StableSubgroup of G by Def11;
        reconsider N as normal StableSubgroup of H by A13,Def31;
        set x=H./.N;
        take x;
        thus thesis;
      end;
      consider f be FinSequence such that
      A14: dom f = Seg i &
        for k being Element of NAT st k in Seg i holds P[k,f.k]
        from FINSEQ_1:sch 1(A2,A9);
      take f;
      len f = i by A14,FINSEQ_1:def 3;
      hence len s = len f + 1;
      let j be natural number;
      assume A15: j in dom f;
      let H be StableSubgroup of G;
      let N be normal StableSubgroup of H;
      assume A16: H=s.j;
      assume N=s.(j+1);
      hence f.j = H./.N by A14,A15,A16;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let f1,f2 be FinSequence;
    now
      assume len s > 1; then
      A17: len s - 1 > 1 - 1 by XREAL_1:11;
      set i=len s - 1;
      i in NAT by A17,INT_1:16; then
      reconsider i as Nat by ORDINAL1:def 13;
      assume A18: len s = len f1 + 1;
      assume A19: for i being natural number st i in dom f1
        for H1 being StableSubgroup of G,
            N1 being normal StableSubgroup of H1
        st H1=s.i & N1=s.(i+1) holds f1.i = H1./.N1;
      assume A20: len s = len f2 + 1;
      assume A21: for i being natural number st i in dom f2
        for H1 being StableSubgroup of G,
            N1 being normal StableSubgroup of H1
        st H1=s.i & N1=s.(i+1) holds f2.i = H1./.N1;
      for k being Element of NAT st 1 <=k & k <= len f1 holds f1.k=f2.k
      proof
        let k be Element of NAT;
        assume A22: 1 <=k & k <= len f1; then
        A23: k in Seg len f1; then
        A24: k in dom f1 by FINSEQ_1:def 3;
        A25: k in dom f2 by A18,A20,A23,FINSEQ_1:def 3;
        A26: k+1<=len s - 1 + 1 by A18,A22,XREAL_1:8;
        1+1<=k+1 by A22,XREAL_1:8; then
        A27: 1<=k+1 by XXREAL_0:2;
        0+k<=1+k by XREAL_1:8; then
        k<=len s by A26,XXREAL_0:2; then
        k in Seg len s & k+1 in Seg len s by A22,A26,A27; then
        A28: k in dom s & k+1 in dom s by FINSEQ_1:def 3;
        reconsider H1=s.k,N1=s.(k+1) as Element of the_stable_subgroups_of G
          by A28,FINSEQ_2:13;
        reconsider H1,N1 as StableSubgroup of G by Def11;
        reconsider N1 as normal StableSubgroup of H1 by A28,Def31;
        f1.k=H1./.N1 by A24,A19;
        hence f1.k=f2.k by A25,A21;
      end;
      hence
      f1=f2 by A18,A20,FINSEQ_1:18;
    end;
    hence thesis;
  end;
  consistency;
end;

definition
  let O be set;
  let f1,f2 be FinSequence;
  let p be Permutation of dom f1;
  pred f1,f2 are_equivalent_under p,O means :Def37:
  len f1 = len f2 &
    for H1,H2 being GroupWithOperators of O, i,j being natural number
    st i in dom f1 & j=p".i & H1=f1.i & H2=f2.j
    holds H1,H2 are_isomorphic;
end;

reserve y for set,
        H1',H2' for StableSubgroup of G,
        N1' for normal StableSubgroup of H1',
        s1,s1',s2,s2' for CompositionSeries of G,
        fs for FinSequence of the_stable_subgroups_of G,
        f1,f2 for FinSequence,
        i,j,n for natural number;

theorem Th94:
  i in dom s1 & i+1 in dom s1 & s1.i=s1.(i+1) & fs=Del(s1,i)
    implies fs is composition_series
proof
  assume A1: i in dom s1;
  assume A2: i+1 in dom s1;
  assume A3: s1.i=s1.(i+1);
  assume A4: fs = Del(s1,i);
  A5: i in Seg len s1 by A1,FINSEQ_1:def 3; then
  A6: 1<=i & i<=len s1 by FINSEQ_1:3; then
  A7: 1+1<=i+1 by XREAL_1:8;
  i+1 in Seg len s1 by A2,FINSEQ_1:def 3; then
  A8: 1<=i+1 & i+1<=len s1 by FINSEQ_1:3;
  consider k be Nat such that
  A9: len s1 = k + 1 & len Del(s1,i) = k by A1,FINSEQ_3:113;
  1+1 <= len fs+1 by A4,A9,A7,A8,XXREAL_0:2; then
  A10: 1 <= len fs by XREAL_1:8;
  per cases by A10,XXREAL_0:1;
  suppose A11: len fs = 1;
    A12: 1<=i & i<=1 by A11,A4,A9,A5,FINSEQ_1:3,A8,XREAL_1:8; then
    A13: i=1 by XXREAL_0:1;
    dom s1 = Seg 2 by A11,FINSEQ_1:def 3,A9,A4; then
    A14: 1 in dom s1;
    A15: s1.1=(Omega).G & s1.2=(1).G by A11,A9,A4,Def31;
    A16: len s1 = len fs+1 & i in dom s1 & i<=1 & 1<=len fs
      by A9,A4,A12,XXREAL_0:1,A11,A14;
    A17: fs.(len fs) = s1.(1+1) by A11,A4,FINSEQ_3:120,A16
    .= (1).G by A11,A9,A4,Def31;
    now let n be natural number;
      assume n in dom fs; then
      n in Seg 1 by A11,FINSEQ_1:def 3; then
      A18: n=1 by TARSKI:def 1,FINSEQ_1:4;
      assume n+1 in dom fs; then
      A19: 2 in Seg 1 by A18,FINSEQ_1:def 3,A11;
      let H1,H2;
      assume H1=fs.n & H2=fs.(n+1);
      thus H2 is normal StableSubgroup of H1 by A19,TARSKI:def 1,FINSEQ_1:4;
    end;
    hence fs is composition_series by A11,A15,A13,A3,A17,Def31;
  end;
  suppose A20: len fs > 1;
    A21: fs.1=(Omega).G
    proof
      per cases by A6,XXREAL_0:1;
      suppose A22: i=1;
        fs.1 = s1.(1+1) by A22,A4,A20,A1,A9,FINSEQ_3:120;
        hence thesis by Def31,A22,A3;
      end;
      suppose A23: i>1;
        reconsider i as Element of NAT by INT_1:16;
        fs.1 = Del(s1,i).1 by A4
        .= s1.1 by A23,A9,FINSEQ_3:119;
        hence thesis by Def31;
      end;
    end;
    A24: i<=len fs by XREAL_1:8,A8,A4,A9;
    fs.(len fs) = s1.(len s1) by A4,A9,A1,A24,FINSEQ_3:120; then
    A25: fs.(len fs)=(1).G by Def31;
    now
      let n be natural number;
      assume n in dom fs & n+1 in dom fs; then
      A26: n in Seg len fs & n+1 in Seg len fs by FINSEQ_1:def 3;
      A27: 0+len fs < 1+len fs by XREAL_1:8;
      A28: Seg len fs c= Seg len s1 by A27,A4,A9,FINSEQ_1:7;
      let H1, H2;
      assume A29: H1 = fs.n;
      assume A30: H2 = fs.(n+1);
      reconsider n1=n+1 as Nat;
      n1 <= len fs by A26,FINSEQ_1:3; then
      n1 < len s1 by A27,A4,A9,XXREAL_0:2; then
      A31: n1 + 1 <= k + 1 by NAT_1:13,A9;
      A32: n1 <= k by A26,FINSEQ_1:3,A9,A4;
      n in Seg len s1 by A28,A26; then
      A33: n in dom s1 by FINSEQ_1:def 3;
      n1 in Seg len s1 by A28,A26; then
      A34: n1 in dom s1 by FINSEQ_1:def 3;
      Seg(n1+1) c= Seg len s1 by A9,A31,FINSEQ_1:7; then
      A35: Seg(n1+1) c= dom s1 by FINSEQ_1:def 3;
      A36: n1+1 in Seg(n1+1) by FINSEQ_1:6;
      A37: n <= k by A26,FINSEQ_1:3,A9,A4;
      reconsider i,n as Nat;
      per cases;
      suppose A38: n<i; then
        A39: n1<=i by NAT_1:13;
        reconsider n'=n,i as Element of NAT by INT_1:16;
        A40: Del(s1,i).n' = s1.n' by FINSEQ_3:119,A38,A9;
        per cases by A39,XXREAL_0:1;
        suppose A41: n1<i;
          reconsider n1'=n1,i as Element of NAT by INT_1:16;
          Del(s1,i).n1' = s1.n1' by FINSEQ_3:119,A41,A9;
          hence H2 is normal StableSubgroup of H1 by Def31,A29,A4,A30,A40,A33,
A34;
        end;
        suppose A42: n1=i; then
          Del(s1,i).n1 = s1.(n1+1) by A32,A1,A9,FINSEQ_3:120;
          hence H2 is normal StableSubgroup of H1
            by Def31,A29,A4,A30,A40,A42,A34,A3,A33;
        end;
      end;
      suppose A43: n>=i;
        reconsider n'=n,i as Element of NAT by INT_1:16;
        A44: Del(s1,i).n' = s1.(n'+1) by A43,FINSEQ_3:120,A37,A1,A9;
        reconsider n1'=n1,i,k as Element of NAT by INT_1:16;
        0+n<=n+1 by XREAL_1:8; then
        A45: len s1 = k+1 & i in dom s1 & i<=n1' & n1'<=k
          by A26,FINSEQ_1:3,A4,A1,A9,A43,XXREAL_0:2;
        Del(s1,i).n1' = s1.(n1'+1) by A45,FINSEQ_3:120;
        hence H2 is normal StableSubgroup of H1
          by Def31,A29,A4,A30,A44,A36,A35,A34;
      end;
    end;
    hence fs is composition_series by A21,A25,Def31;
  end;
end;

theorem Th95:
  s1 is_finer_than s2 implies ex n st len s1 = len s2 + n
proof
  assume s1 is_finer_than s2; then
  consider x such that
  A1: x c= dom s1 & s2 = s1 * Sgm x by Def32;
  A2: x c= Seg len s1 by A1,FINSEQ_1:def 3;
  reconsider x as finite set by A1,FINSET_1:13;
  now let y1 be set;
    assume y1 in dom s2; then
    y1 in dom Sgm x & (Sgm x).y1 in dom s1 by A1,FUNCT_1:21; then
    A3: y1 in Seg card x by A2,FINSEQ_3:45;
    card x <= card dom s1 by A1,CARD_1:80; then
    Seg card x c= Seg card dom s1 by FINSEQ_1:7; then
    y1 in Seg card dom s1 by A3; then
    y1 in Seg card Seg len s1 by FINSEQ_1:def 3; then
    y1 in Seg len s1 by FINSEQ_1:78;
    hence y1 in dom s1 by FINSEQ_1:def 3;
  end; then
  dom s2 c= dom s1 by TARSKI:def 3; then
  Seg len s2 c= dom s1 by FINSEQ_1:def 3; then
  Seg len s2 c= Seg len s1 by FINSEQ_1:def 3; then
  len s2 <= len s1 by FINSEQ_1:7; then
  A4: len s2 - len s2 <= len s1 - len s2 by XREAL_1:11;
  set n=len s1 - len s2;
  n in NAT by A4,INT_1:16; then
  reconsider n as natural number by ORDINAL1:def 13;
  take n;
  thus len s1 = len s2 + n;
end;

theorem Th96:
  len s2 = len s1 & s2 is_finer_than s1 implies s1 = s2
proof
  assume A1: len s2 = len s1;
  assume s2 is_finer_than s1; then
  consider x such that
  A2: x c= dom s2 & s1 = s2 * Sgm x by Def32;
  A3: dom s1 = Seg len s2 by A1,FINSEQ_1:def 3
  .= dom s2 by FINSEQ_1:def 3;
  A4: x c= Seg len s2 by A2,FINSEQ_1:def 3; then
  x = rng Sgm x by FINSEQ_1:def 13; then
  A5: dom(s2 * Sgm x)=dom Sgm x by A2,RELAT_1:46;
  reconsider X = Seg len s2 as finite set;
  set y = X \ x;
  reconsider x,y as finite set by A2,FINSET_1:13;
  A6: X = X \/ x by XBOOLE_1:12,A4
  .= x \/ y by XBOOLE_1:39;
  x misses y by XBOOLE_1:79; then
  card(x \/ y) = (card x) + (card y) by CARD_2:53; then
  A7: len s2 = (card x) + (card y) by A6,FINSEQ_1:78;
  dom Sgm x = Seg len s2 by A5,A2,A3,FINSEQ_1:def 3; then
  len Sgm x = len s2 by FINSEQ_1:def 3; then
  card x = len s2 by FINSEQ_3:44,A4; then
  y = {} by A7,CARD_2:59; then
  Sgm x = idseq len s2 by A6,FINSEQ_3:54;
  hence s1 = s2 by A2,FINSEQ_2:64;
end;

theorem Th97:
  s1 is not empty & s2 is_finer_than s1 implies s2 is not empty
proof
  assume A1: s1 is not empty;
  assume A2: s2 is_finer_than s1;
  consider i such that
  A3: len s2 = len s1 + i by A2,Th95;
    now assume A4: s2 is empty;
      now set x={};
        take x;
        thus x c= dom s1 by XBOOLE_1:2;
        thus s2 = s1 * Sgm x by A4,RELAT_1:62,FINSEQ_3:49;
      end; then
      s1 is_finer_than s2 by Def32; then
      consider j be natural number such that
      A5: len s1 = len s2 + j by Th95;
      0=i+j by A3,A5; then
      i=0 by NAT_1:7;
      hence contradiction by A4,A3,A2,A1,Th96;
    end;
  hence thesis;
end;

theorem Th98:
  s1 is_finer_than s2 & s1 is jordan_holder & s2 is jordan_holder implies s1=s2
proof
  assume A1: s1 is_finer_than s2;
  assume A2: s1 is jordan_holder;
  assume A3: s2 is jordan_holder & s1 <> s2;
  s1 is strictly_decreasing & not ex s2 st s1<>s2 & s2 is strictly_decreasing
    & s2 is_finer_than s1 by Def34,A2;
  hence contradiction by A1,A3,Def34;
end;

Lm36: for P,R being Relation holds P = (rng P)|R iff P~ = (R~)|(dom (P~))
proof
  let P,R be Relation;
  hereby
    assume A1: P = (rng P)|R;
    now
      let x,y be set;
      hereby
        assume A2: [x,y] in P~; then
        [y,x] in P by RELAT_1:def 7; then
        x in rng P & [y,x] in R by A1,RELAT_1:def 12; then
        x in dom(P~) & [x,y] in R~ by A2,RELAT_1:def 4,RELAT_1:def 7;
        hence [x,y] in R~|(dom (P~)) by RELAT_1:def 11;
      end;
      assume [x,y] in R~|(dom (P~)); then
      x in dom(P~) & [x,y] in R~ by RELAT_1:def 11; then
      x in rng P & [y,x] in R by RELAT_1:37,RELAT_1:def 7; then
      [y,x] in (rng P)|R by RELAT_1:def 12;
      hence [x,y] in P~ by A1,RELAT_1:def 7;
    end;
    hence P~ = (R~)|(dom (P~)) by RELAT_1:def 2;
  end;
  assume A3: P~ = (R~)|(dom (P~));
  now
    let x,y be set;
    hereby
      assume [x,y] in P; then
      [y,x] in P~ by RELAT_1:def 7; then
      y in dom(P~) & [y,x] in R~ by A3,RELAT_1:def 11; then
      y in rng P & [x,y] in R by RELAT_1:37,RELAT_1:def 7;
      hence [x,y] in (rng P)|R by RELAT_1:def 12;
    end;
    assume [x,y] in (rng P)|R; then
    y in rng P & [x,y] in R by RELAT_1:def 12; then
    y in dom(P~) & [y,x] in R~ by RELAT_1:def 7,RELAT_1:37; then
    [y,x] in R~|(dom (P~)) by RELAT_1:def 11;
    hence [x,y] in P by A3,RELAT_1:def 7;
  end;
  hence P = (rng P)|R by RELAT_1:def 2;
end;

Lm37: for X being set, P,R being Relation holds P*(R|X) = (X|P)*R
proof
  let X be set;
  let P,R be Relation;
  A1: now let x be set;
    assume A2: x in P*(R|X); then
    consider y,z be set such that
    A3: x = [y,z] by RELAT_1:def 1;
    consider w be set such that
    A4: [y,w] in P & [w,z] in R|X by A2,A3,RELAT_1:def 8;
    A5: w in X & [w,z] in R by A4,RELAT_1:def 11; then
    [y,w] in X|P by A4,RELAT_1:def 12;
    hence x in (X|P)*R by A3,A5,RELAT_1:def 8;
  end;
  now let x be set;
    assume A6: x in (X|P)*R; then
    consider y,z be set such that
    A7: x = [y,z] by RELAT_1:def 1;
    consider w be set such that
    A8: [y,w] in X|P & [w,z] in R by A6,A7,RELAT_1:def 8;
    A9: w in X & [y,w] in P by A8,RELAT_1:def 12; then
    [w,z] in R|X by A8,RELAT_1:def 11;
    hence x in P*(R|X) by A7,A9,RELAT_1:def 8;
  end;
  hence P*(R|X) = (X|P)*R by A1,TARSKI:2;
end;

Lm38: for n being natural number, X being set, f being PartFunc of REAL, REAL
  st X c= Seg n & X c= dom f & f is_increasing_on X & f.:X c= NAT \ {0}
  holds Sgm(f.:X) = f * Sgm X
proof
  let n be natural number;
  let X be set;
  let f be PartFunc of REAL, REAL;
  assume A1: X c= Seg n;
  assume A2: X c= dom f;
  assume A3: f is_increasing_on X;
  assume A4: f.:X c= NAT \ {0};
  reconsider k = n as Element of NAT by ORDINAL1:def 13;
  A5: rng Sgm X = X by A1,FINSEQ_1:def 13;
  per cases;
  suppose A6: X misses dom f;
    f.:X = {} by A6,RELAT_1:151; then
    Sgm(f.:X) = {} by FINSEQ_1:4,FINSEQ_1:72;
    hence thesis by A5,A6,RELAT_1:67;
  end;
  suppose A7: X meets dom f;
    set fs = f * Sgm X;
    set fX = f.:X;
    rng Sgm X c= dom f by FINSEQ_1:def 13,A1,A2; then
    reconsider fs as FinSequence by FINSEQ_1:20;
    f.:(rng Sgm X) c= NAT \ {0} by FINSEQ_1:def 13,A1,A4; then
    A8: rng fs c= NAT \ {0} by RELAT_1:160;
    A9: NAT \ {0} c= NAT by XBOOLE_1:36; then
    rng fs c= NAT by A8,XBOOLE_1:1; then
    reconsider fs as FinSequence of NAT by FINSEQ_1:def 4;
    reconsider f'=f as Function;
    reconsider X'=X as finite set by FINSET_1:13,A1;
    f'.:X' is finite; then
    reconsider fX as finite non empty real-membered set by A7,RELAT_1:151;
    set k = max fX;
    k in fX by PRE_CIRC:def 1; then
    k in NAT \ {0} by A4; then
    reconsider k as natural number by A9,ORDINAL1:def 13;
    now let x be set;
      assume A10: x in f.:X; then
      A11: x in NAT \ {0} by A4;
      reconsider k'=x as natural number by A9,A11,ORDINAL1:def 13;
      A12: k' <= k by A10,PRE_CIRC:def 1;
      not k' in {0} by A10,A4,XBOOLE_0:def 4; then
      k' <> 0 by TARSKI:def 1; then
      0 < k'; then
      0+1 < k'+1 by XREAL_1:8; then
      1 <= k' by NAT_1:13;
      hence x in Seg k by A12,FINSEQ_1:3;
    end; then
    A13: f.:X c= Seg k by TARSKI:def 3;
    A14: rng fs = f.:X by A5,RELAT_1:160;
    now
      let l,m,k1,k2 be natural number;
      assume A15: 1 <= l & l < m & m <= len fs;
      assume A16: k1 = fs.l & k2 = fs.m;
      A17: dom fs = Seg len fs by FINSEQ_1:def 3;
      1 <= l & l <= len fs by A15,XXREAL_0:2; then
      A18: l in dom fs by FINSEQ_1:3,A17; then
      A19: l in dom Sgm X & (Sgm X).l in dom f by FUNCT_1:21;
      1 <= m & m <= len fs by A15,XXREAL_0:2; then
      A20: m in dom fs by FINSEQ_1:3,A17; then
      A21: m in dom Sgm X & (Sgm X).m in dom f by FUNCT_1:21;
      A22: k1 = f.((Sgm X).l) & k2 = f.((Sgm X).m) by FUNCT_1:22,A16,A18,A20;
      set k1'=(Sgm X).l;
      set k2'=(Sgm X).m;
      A23: k1' in X & k2' in X by A5,A19,A21,FUNCT_1:12; then
      k1' in Seg n & k2' in Seg n by A1; then
      reconsider k1',k2' as natural number by ORDINAL1:def 13;
      m in Seg len Sgm X by A21,FINSEQ_1:def 3; then
      1 <= l & l < m & m <= len Sgm X by A15,FINSEQ_1:3; then
      A24: k1' < k2' by FINSEQ_1:def 13,A1;
      reconsider k1',k2' as Element of NAT by ORDINAL1:def 13;
      reconsider k1',k2' as Element of REAL;
      k1' in X /\ dom f & k2' in X /\ dom f by A23,A19,A21,XBOOLE_0:def 3;
      hence k1 < k2 by A22,A3,A24,RFUNCT_2:def 2;
    end;
    hence thesis by A13,A14,FINSEQ_1:def 13;
  end;
end;

Lm39: y c= Seg(n+1) & i in Seg(n+1) & not i in y
  implies ex x st Sgm x = Sgm(Seg(n+1)\{i})" * Sgm y & x c= Seg n
proof
  assume A1: y c= Seg(n+1);
  assume A2: i in Seg(n+1); then
  A3: 1<=i & i<=n+1 by FINSEQ_1:3;
  assume A4: not i in y;
  set x1 = {k where k is Element of NAT: k in y & k<i};
  set x2 = {k-1 where k is Element of NAT: k in y & k>i};
  set x = x1 \/ x2;
  take x;
  A5: now let z be set;
    assume A6: z in x;
    per cases by A6,XBOOLE_0:def 2;
    suppose z in x1; then
      consider k be Element of NAT such that
      A7: k = z & k in y & k < i;
      reconsider z'=z as Element of NAT by A7;
      A8: 1<=z' & z'<=n+1 by A7,A1,FINSEQ_1:3;
      z'<n+1 by A7,A3,XXREAL_0:2; then
      z'<=n by NAT_1:13;
      hence z in Seg n by A8;
    end;
    suppose z in x2; then
      consider k be Element of NAT such that
      A9: k-1 = z & k in y & k > i;
      1<=k & k<=n+1 by A1,A9,FINSEQ_1:3; then
      A10: k-1<=n+1-1 by XREAL_1:11;
      reconsider z'=z as integer number by A9;
      1<k by A9,A3,XXREAL_0:2; then
      1+1<k+1 by XREAL_1:8; then
      2<=k by NAT_1:13; then
      A11: 2-1<=k-1 by XREAL_1:11; then
      reconsider z' as Element of NAT by A9,INT_1:16;
      z'<=n by A10,A9;
      hence z in Seg n by A11,A9;
    end;
  end; then
  A12: x c= Seg n by TARSKI:def 3;
  set p = Sgm(Seg(n+1)\{i})" * Sgm y;
  A13: Seg(n+1)\{i} c= Seg(n+1) by XBOOLE_1:36; then
  Sgm(Seg(n+1)\{i}) is one-to-one by FINSEQ_3:99; then
  A14: rng Sgm(Seg(n+1)\{i})=dom(Sgm(Seg(n+1)\{i})") by FUNCT_1:55;
  now let x be set;
    assume A15: x in y; then
    not x in {i} by A4,TARSKI:def 1;
    hence x in Seg(n+1)\{i} by A15,A1,XBOOLE_0:def 4;
  end; then
  y c= Seg(n+1)\{i} by TARSKI:def 3; then
  y c= dom(Sgm(Seg(n+1)\{i})") by A14,FINSEQ_1:def 13,A13; then
  rng Sgm y c= dom(Sgm(Seg(n+1)\{i})") by A1,FINSEQ_1:def 13; then
  reconsider p as FinSequence by FINSEQ_1:20;
  reconsider x'=x,y'=y as finite set by A12,A1,FINSET_1:13;
  set f1 = id x1;
  set f2 = {[k-1,k] where k is Element of NAT:k in y' & k>i};
  now
    let x be set;
    assume x in f2; then
    consider k be Element of NAT such that
    A16: [k-1,k] = x & k in y' & k > i;
    reconsider y=k-1,z=k as set;
    take y,z;
    thus x = [y,z] by A16;
  end; then
  reconsider f2 as Relation by RELAT_1:def 1;
  set f = f1 \/ f2;
  A17: dom f1 = x1 by RELAT_1:71;
  A18: now let x be set;
    assume x in dom f2; then
    consider y be set such that
    A19: [x,y] in f2 by RELAT_1:def 4;
    consider k be Element of NAT such that
    A20: [k-1,k] = [x,y] & k in y' & k > i by A19;
    k-1 = x by A20,ZFMISC_1:33;
    hence x in x2 by A20;
  end;
  now let x be set;
    assume x in x2; then
    consider k be Element of NAT such that
    A21: k-1 = x & k in y' & k > i;
    reconsider y=k as set;
    [x,y] in f2 by A21;
    hence x in dom f2 by RELAT_1:def 4;
  end; then
  A22: dom f2 = x2 by A18,TARSKI:2;
  now
    let x,y1,y2 be set;
    assume A23: [x,y1] in f;
    assume A24: [x,y2] in f;
    per cases by A23,XBOOLE_0:def 2;
    suppose A25: [x,y1] in f1; then
      A26: x in dom f1 by RELAT_1:def 4; then
      f1.x = x by FUNCT_1:34; then
      A27: y1 = x by A25,A26,FUNCT_1:def 4;
      per cases by A24,XBOOLE_0:def 2;
      suppose A28: [x,y2] in f1; then
        A29: x in dom f1 by RELAT_1:def 4; then
        f1.x = x by FUNCT_1:34;
        hence y1 = y2 by A27,A28,A29,FUNCT_1:def 4;
      end;
      suppose A30: [x,y2] in f2;
        x in x2 by A30,A22,RELAT_1:def 4; then
        consider k be Element of NAT such that
        A31: k-1=x & k in y & k>i;
        x in x1 by A26; then
        consider k' be Element of NAT such that
        A32: k'=x & k' in y & k'<i;
        k'+1>i by A31,A32;
        hence y1 = y2 by A32,NAT_1:13;
      end;
    end;
    suppose [x,y1] in f2; then
      consider k be Element of NAT such that
      A33: [k-1,k] = [x,y1] & k in y' & k > i;
      A34: k-1=x & k=y1 & k in y & k>i by A33,ZFMISC_1:33;
      per cases by A24,XBOOLE_0:def 2;
      suppose [x,y2] in f1; then
        x in dom f1 by RELAT_1:def 4; then
        x in x1; then
        consider k' be Element of NAT such that
        A35: k'=x & k' in y & k'<i;
        k'=k-1 by A35,A33,ZFMISC_1:33; then
        k'+1>i by A33;
        hence y1 = y2 by A35,NAT_1:13;
      end;
      suppose [x,y2] in f2; then
        consider k' be Element of NAT such that
        A36: [k'-1,k'] = [x,y2] & k' in y' & k' > i;
        k'-1=x & k'=y2 by A36,ZFMISC_1:33;
        hence y1 = y2 by A34;
      end;
    end;
  end; then
  reconsider f as Function by FUNCT_1:def 1;
  A37: now
    let x be set;
    assume A38: x in dom f1; then
    A39: [x,f1.x] in f1 by FUNCT_1:def 4;
    dom f = dom f1 \/ dom f2 by RELAT_1:13; then
    A40: dom f1 c= dom f by XBOOLE_1:7;
    f1 c= f by XBOOLE_1:7;
    hence f.x = f1.x by A39,A40,A38,FUNCT_1:def 4;
  end;
  now
    let x,y1,y2 be set;
    assume [x,y1] in f2; then
    consider k be Element of NAT such that
    A41: [k-1,k] = [x,y1] & k in y' & k > i;
    assume [x,y2] in f2; then
    consider k' be Element of NAT such that
    A42: [k'-1,k'] = [x,y2] & k' in y' & k' > i;
    A43: k-1 = x & k = y1 by A41,ZFMISC_1:33;
    A44: k'-1 = x & k' = y2 by A42,ZFMISC_1:33;
    thus y1 = y2 by A43,A44;
  end; then
  reconsider f2 as Function by FUNCT_1:def 1;
  A45: now
    let x be set;
    assume A46: x in dom f2; then
    A47: [x,f2.x] in f2 by FUNCT_1:def 4;
    dom f = dom f1 \/ dom f2 by RELAT_1:13; then
    A48: dom f2 c= dom f by XBOOLE_1:7;
    f2 c= f by XBOOLE_1:7;
    hence f.x = f2.x by A46,A47,A48,FUNCT_1:def 4;
  end;
  now
    let y1,y2 be set;
    assume y1 in dom f; then
    A49: y1 in dom f1 \/ dom f2 by RELAT_1:13;
    assume y2 in dom f; then
    A50: y2 in dom f1 \/ dom f2 by RELAT_1:13;
    assume A51: f.y1 = f.y2;
    per cases by A49,XBOOLE_0:def 2;
    suppose A52: y1 in dom f1; then
      A53: f1.y1 = y1 by FUNCT_1:34; then
      A54: f.y1 = y1 by A37,A52;
      per cases by A50,XBOOLE_0:def 2;
      suppose A55: y2 in dom f1; then
        f1.y2 = y2 by FUNCT_1:34;
        hence y1 = y2 by A37,A54,A51,A55;
      end;
      suppose A56: y2 in dom f2; then
        f.y2 = f2.y2 by A45; then
        [y2,f.y2] in f2 by A56,FUNCT_1:def 4; then
        consider k be Element of NAT such that
        A57: [k-1,k] = [y2,f.y2] & k in y' & k>i;
        f.y1 = f1.y1 by A37,A52; then
        f.y1 in x1 by A52,A53; then
        consider k' be Element of NAT such that
        A58: k'=f.y1 & k' in y & k'<i;
        thus y1 = y2 by A57,A58,A51,ZFMISC_1:33;
      end;
    end;
    suppose A59: y1 in dom f2; then
      f.y1 = f2.y1 by A45; then
      [y1,f.y1] in f2 by A59,FUNCT_1:def 4; then
      consider k be Element of NAT such that
      A60: [k-1,k] = [y1,f.y1] & k in y' & k>i;
      A61: k = f.y1 & k-1 = y1 by A60,ZFMISC_1:33;
      per cases by A50,XBOOLE_0:def 2;
      suppose A62: y2 in dom f1; then
        f1.y2 = y2 by FUNCT_1:34; then
        f.y2 in dom f1 by A62,A37; then
        f.y2 in x1; then
        consider k' be Element of NAT such that
        A63: k'=f.y2 & k' in y & k'<i;
        thus y1 = y2 by A60,ZFMISC_1:33,A51,A63;
      end;
      suppose A64: y2 in dom f2; then
        f.y2 = f2.y2 by A45; then
        [y2,f.y2] in f2 by A64,FUNCT_1:def 4; then
        consider k be Element of NAT such that
        A65: [k-1,k] = [y2,f.y2] & k in y' & k>i;
        k = f.y2 & k-1 = y2 by A65,ZFMISC_1:33;
        hence y1 = y2 by A51,A61;
      end;
    end;
  end; then
  A66: f is one-to-one by FUNCT_1:def 8;
  A67: dom f = x' by A17,A22,RELAT_1:13;
  A68: now
    let z be set;
    assume z in rng f; then
    A69: z in (rng f1 \/ rng f2) by RELAT_1:26;
    per cases by A69,XBOOLE_0:def 2;
    suppose z in rng f1; then
      z in x1; then
      consider k be Element of NAT such that
      A70: k = z & k in y & k < i;
      thus z in y' by A70;
    end;
    suppose z in rng f2; then
      consider x'' be set such that
      A71: [x'',z] in f2 by RELAT_1:def 5;
      consider k be Element of NAT such that
      A72: [k-1,k] = [x'',z] & k in y' & k > i by A71;
      thus z in y' by A72,ZFMISC_1:33;
    end;
  end;
  now
    let z be set;
    assume A73: z in y';
    set k=z;
    k in Seg(n+1) by A73,A1; then
    reconsider k as Element of NAT;
    per cases;
    suppose k <= i; then
      k = z & k in y & k < i by A4,A73,XXREAL_0:1; then
      z in x1; then
      z in rng f1 by RELAT_1:71; then
      z in (rng f1 \/ rng f2) by XBOOLE_0:def 2;
      hence z in rng f by RELAT_1:26;
    end;
    suppose A74: k > i;
      set x''=k-1;
      [x'',z] in f2 by A73,A74; then
      z in rng f2 by RELAT_1:def 5; then
      z in (rng f1 \/ rng f2) by XBOOLE_0:def 2;
      hence z in rng f by RELAT_1:26;
    end;
  end; then
  A75: rng f = y' by A68,TARSKI:2;
  A76: y = rng Sgm y by FINSEQ_1:def 13,A1;
  reconsider g=f" as PartFunc of dom(f"), rng(f") by PARTFUN1:24;
  dom f c= NAT & rng f c= NAT by A1,A12,A67,A75,XBOOLE_1:1; then
  dom g c= NAT & rng g c= NAT by A66,FUNCT_1:55; then
  dom g c= REAL & rng g c= REAL by XBOOLE_1:1; then
  reconsider g as PartFunc of REAL,REAL by PARTFUN1:32;
  A77: dom(f") = y & rng(f") = x by A67,A75,A66,FUNCT_1:55; then
  A78: x = (f").: y by RELAT_1:146;
  now
    let r1,r2 be Element of REAL;
    assume r1 in y /\ dom g; then
    A79: [r1,g.r1] in g by A77,FUNCT_1:8;
    assume r2 in y /\ dom g; then
    A80: [r2,g.r2] in g by A77,FUNCT_1:8;
    assume A81: r1 < r2;
    A82: g = (f1 \/ f2)~ by A66,FUNCT_1:def 9
    .= f1~ \/ f2~ by RELAT_1:40;
    per cases by A79,A82,XBOOLE_0:def 2;
    suppose [r1,g.r1] in f1~; then
      [r1,g.r1] in id x1 by RELAT_1:72; then
      A83: r1 in x1 & r1=g.r1 by RELAT_1:def 10; then
      consider k' be Element of NAT such that
      A84: g.r1=k' & k' in y & k'<i;
      per cases by A82,A80,XBOOLE_0:def 2;
      suppose [r2,g.r2] in f1~; then
        [r2,g.r2] in id x1 by RELAT_1:72;
        hence g.r1 < g.r2 by A83,A81,RELAT_1:def 10;
      end;
      suppose [r2,g.r2] in f2~; then
        [g.r2,r2] in f2 by RELAT_1:def 7; then
        consider k'' be Element of NAT such that
        A85: [k''-1,k'']=[g.r2,r2] & k'' in y' & k''>i;
        k''-1=g.r2 by A85,ZFMISC_1:33; then
        A86: i-1 < g.r2 by A85,XREAL_1:11;
        reconsider k'''=g.r2,i'=i-1 as integer number by A85,ZFMISC_1:33;
        i'+1<=k''' by A86,INT_1:20;
        hence g.r1 < g.r2 by A84,XXREAL_0:2;
      end;
    end;
    suppose [r1,g.r1] in f2~; then
      [g.r1,r1] in f2 by RELAT_1:def 7; then
      consider k' be Element of NAT such that
      A87: [k'-1,k']=[g.r1,r1] & k' in y' & k'>i;
      A88: k'-1=g.r1 & r1=k' by A87,ZFMISC_1:33;
      per cases by A82,A80,XBOOLE_0:def 2;
      suppose [r2,g.r2] in f1~;  then
        [r2,g.r2] in id x1 by RELAT_1:72; then
        r2 in x1 & r2=g.r2 by RELAT_1:def 10; then
        consider k'' be Element of NAT such that
        A89: r2=k'' & k'' in y & k''<i;
        thus g.r1 < g.r2 by A89,A87,A88,A81,XXREAL_0:2;
      end;
      suppose [r2,g.r2] in f2~; then
        [g.r2,r2] in f2 by RELAT_1:def 7; then
        consider k'' be Element of NAT such that
        A90: [k''-1,k'']=[g.r2,r2] & k'' in y' & k''>i;
        k''-1=g.r2 & r2=k'' by A90,ZFMISC_1:33;
        hence g.r1 < g.r2 by A81,A88,XREAL_1:11;
      end;
    end;
  end; then
  A91: g is_increasing_on y by RFUNCT_2:def 2;
  now let x' be set;
    assume A92: x' in g.:y; then
    A93: x' in Seg n by A78,A5;
    not x'=0 by A92,A78,A12,FINSEQ_1:3; then
    not x' in {0} by TARSKI:def 1;
    hence x' in NAT \ {0} by A93,XBOOLE_0:def 4;
  end; then
  A94: g.:y c= NAT \ {0} by TARSKI:def 3;
  A95: f" = f~ by A66,FUNCT_1:def 9;
  Sgm(Seg(n+1)\{i}) is one-to-one by FINSEQ_3:99,A13; then
  A96: Sgm(Seg(n+1)\{i})"=Sgm(Seg(n+1)\{i})~ by FUNCT_1:def 9;
  now
    let a,b be set;
    hereby
      assume A97: [a,b] in f;
      per cases by A97,XBOOLE_0:def 2;
      suppose [a,b] in f1; then
        A98: a = b & a in x1 by RELAT_1:def 10; then
        consider a' be Element of NAT such that
        A99: a' = a & a' in y & a'<i;
        reconsider i'=i,n'=n as Element of NAT by ORDINAL1:def 13;
        i<=n+1 by A2,FINSEQ_1:3; then
        a'<n+1 by A99,XXREAL_0:2; then
        A100: a'<=n by NAT_1:13;
        A101: 1<=a' by A1,A99,FINSEQ_1:3; then
        A102: a in Seg n by A99,A100; then
        a in Seg len Sgm(Seg(n+1)\{i}) by A2,FINSEQ_3:116; then
        A103: a in dom Sgm(Seg(n+1)\{i}) by FINSEQ_1:def 3;
        a' = Sgm(Seg(n'+1)\{i'}).a' by A2,A99,A102,A101,FINSEQ_3:117; then
        [a,b] in Sgm(Seg(n+1)\{i}) by A98,A99,A103,FUNCT_1:8;
        hence [a,b] in (rng f)|Sgm(Seg(n+1)\{i}) by A99,A98,A75,RELAT_1:def 12;
      end;
      suppose [a,b] in f2; then
        consider b' be Element of NAT such that
        A104: [a,b] = [b'-1,b'] & b' in y' & b'>i;
        A105: a = b'-1 & b = b' by A104,ZFMISC_1:33;
        reconsider a'=b'-1 as integer number;
        reconsider i'=i,n'=n as Element of NAT by ORDINAL1:def 13;
        1<=b' & b'<=n+1 by A1,A104,FINSEQ_1:3; then
        A106: b'-1<=n+1-1 by XREAL_1:11;
        i+1<=b' by NAT_1:13,A104; then
        A107: i+1-1<=b'-1 by XREAL_1:11;
        A108: 1<=a' by A107,XXREAL_0:2,A3;
        reconsider a' as Element of NAT by A107,INT_1:16;
        A109: a' in Seg n by A106,A108; then
        a in Seg n by A104,ZFMISC_1:33; then
        a in Seg len Sgm(Seg(n+1)\{i}) by A2,FINSEQ_3:116; then
        A110: a in dom Sgm(Seg(n+1)\{i}) by FINSEQ_1:def 3;
        a'+1 = Sgm(Seg(n'+1)\{i'}).a'
          by A2,A106,A109,A107,FINSEQ_3:117; then
        [a,b] in Sgm(Seg(n+1)\{i}) by A105,A110,FUNCT_1:8;
        hence [a,b] in (rng f)|Sgm(Seg(n+1)\{i}) by A104,A75,RELAT_1:def 12;
      end;
    end;
    assume A111: [a,b] in (rng f)|Sgm(Seg(n+1)\{i}); then
    A112: b in rng f & [a,b] in Sgm(Seg(n+1)\{i}) by RELAT_1:def 12;
    A113: b in y by A111,A75,RELAT_1:def 12;
    A114: b in Seg(n+1) & not b=i by A112,A75,A1,A4;
    A115: a in dom Sgm(Seg(n+1)\{i}) by A112,RELAT_1:def 4; then
    a in Seg len Sgm(Seg(n+1)\{i}) by FINSEQ_1:def 3; then
    A116: a in Seg n by A2,FINSEQ_3:116;
    reconsider a'=a,b'=b as Element of NAT by A114,A115;
    reconsider i,n as Element of NAT by ORDINAL1:def 13;
    A117: 1<=a' & a'<=n by A116,FINSEQ_1:3;
    per cases;
    suppose A118: a'<i; then
      Sgm(Seg(n+1)\{i}).a' = a' by A2,A116,A117,FINSEQ_3:117; then
      A119: b=a by FUNCT_1:8,A112; then
      a' in x1 by A118,A113; then
      [a,b] in id x1 by A119,RELAT_1:def 10;
      hence [a,b] in f by XBOOLE_0:def 2;
    end;
    suppose A120: i<=a'; then
      Sgm(Seg(n+1)\{i}).a' = a'+1 by A2,A116,A117,FINSEQ_3:117; then
      A121: b'=a'+1 by FUNCT_1:8,A112; then
      A122: b' in y' & b'>i by A120,NAT_1:13,A111,A75,RELAT_1:def 12;
      a = b'-1 by A121; then
      [a,b] in f2 by A122;
      hence [a,b] in f by XBOOLE_0:def 2;
    end;
  end; then
  f = (rng f)|Sgm(Seg(n+1)\{i}) by RELAT_1:def 2; then
  A123: f" = (Sgm(Seg(n+1)\{i})~)|dom(f") by Lm36,A95;
  Sgm x = Sgm (g.:y) by A77,RELAT_1:146
  .= ((Sgm(Seg(n+1)\{i})")|y) * Sgm y by A123,A96,A94,A91,A77,A1,Lm38
  .= Sgm(Seg(n+1)\{i})" * y|Sgm y by Lm37
  .= Sgm(Seg(n+1)\{i})" * Sgm y by A76,RELAT_1:126;
  hence Sgm x = Sgm(Seg(n+1)\{i})" * Sgm y;
  thus thesis by A5,TARSKI:def 3;
end;

theorem Th99:
  i in dom s1 & i+1 in dom s1 & s1.i = s1.(i+1) & s1' = Del(s1,i) &
    s2 is jordan_holder & s1 is_finer_than s2 implies s1' is_finer_than s2
proof
  assume A1: i in dom s1 & i+1 in dom s1;
  assume A2: s1.i = s1.(i+1);
  assume A3: s1' = Del(s1,i);
  assume A4: s2 is jordan_holder;
  assume s1 is_finer_than s2; then
  consider z be set such that
  A5: z c= dom s1 & s2 = s1 * Sgm z by Def32;
  set k = len s1 - 1;
  A6: i in Seg len s1 & i+1 in Seg len s1 by A1,FINSEQ_1:def 3; then
  A7: 1 <= i & i<=len s1 by FINSEQ_1:3; then
  1 <= len s1 by XXREAL_0:2; then
  A8: 1-1 <= len s1 - 1 by XREAL_1:11;
  reconsider k as integer number;
  reconsider k as Element of NAT by A8,INT_1:16;
  A9: dom s1 = Seg(k+1) by FINSEQ_1:def 3;
  now
    per cases;
    suppose A10: not i in z;
      set y = z;
      take y;
      thus y c= Seg(k+1) by A5,FINSEQ_1:def 3;
      thus not i in y by A10;
      thus s2 = s1 * Sgm y by A5;
    end;
    suppose A11: i in z;
      reconsider y = (z \/ {i+1}) \ {i} as set;
      take y;
      {i+1} c= Seg(k+1) by A6,ZFMISC_1:37; then
      z \/ {i+1} c= Seg(k+1) by A5,A9,XBOOLE_1:8;
      hence A12: y c= Seg(k+1) by XBOOLE_1:1;
      now assume i in y; then
        not i in {i} by XBOOLE_0:def 4;
        hence contradiction by TARSKI:def 1;
      end;
      hence not i in y;
      reconsider y'=y,z as finite set by A5,A12,FINSET_1:13;
      i in z \/ {i+1} by A11,XBOOLE_0:def 2; then
      {i} c= z \/ {i+1} by ZFMISC_1:37; then
      card((z \/ {i+1})\{i}) = card(z \/ {i+1}) - card{i} by CARD_2:63; then
      A13: card y' = card(z \/ {i+1}) - 1 by CARD_1:79;
      A14: now assume A15: i+1 in z;
        A16: i in rng Sgm z & i+1 in rng Sgm z
          by A15,A11,A5,A9,FINSEQ_1:def 13; then
        consider x' be set such that
        A17: x' in dom Sgm z & i =  Sgm(z).x' by FUNCT_1:def 5;
        consider x'' be set such that
        A18: x'' in dom Sgm z & i+1 =  Sgm(z).x'' by A16,FUNCT_1:def 5;
        A19: dom Sgm z = Seg len Sgm z by FINSEQ_1:def 3;
        reconsider x',x'' as Element of NAT by A17,A18;
        A20: 0+i < 1+i by XREAL_1:8; then
        i < i+1 & 1 <= x'' & x' <= len Sgm z by A19,A17,A18,FINSEQ_1:3; then
        A21: x' < x'' by A5,A9,A17,A18,FINSEQ_3:46;
        reconsider l = x'' - x' as Element of NAT by A21,INT_1:18;
        per cases;
        suppose l = 0; hence contradiction by A20,A17,A18; end;
        suppose 0 < l; then
          A22: 0+1 < l+1 by XREAL_1:8;
          set x''' = x'+1;
          x'+1-x' <= x''-x' by A22,NAT_1:13; then
          A23: x''' <= x'' by XREAL_1:11;
          A24: 1+x' > 0+x' by XREAL_1:8;
          A25: 1 <= x' by A19,A17,FINSEQ_1:3; then
          A26: 1 <= x''' by A24,XXREAL_0:2;
          x'' <= len Sgm z by A19,A18,FINSEQ_1:3; then
          A27: x''' <= len Sgm z by XXREAL_0:2,A23; then
          A28: x''' in dom Sgm z by A26,A19;
          reconsider k1= Sgm(z).x',k2= Sgm(z).x'',k3= Sgm(z).x'''
            as Element of NAT by A17,A18,A28,FINSEQ_2:13;
          A29: (1 <= x''' & x''' < x'' & x'' <= len Sgm z) or x''' = x''
            by A25,A24,XXREAL_0:2,A23,A19,A18,FINSEQ_1:3,XXREAL_0:1;
          k3 <= i+1 & i < k3
            by A29,A17,A18,A5,A9,FINSEQ_1:def 13,A27,A24,A25; then
          A30: i+1 <= k3 by NAT_1:13;
          A31: s2.x' = s1.(Sgm(z).x') by FUNCT_1:23,A17,A5
          .= s2.(x'+1)
            by FUNCT_1:23,A2,A17,A30,A29,A18,A5,A9,FINSEQ_1:def 13;
          A32: x' in dom s2 & x'+1 in dom s2
            by FUNCT_1:21,A1,A17,A18,A30,A29,A5,A9,FINSEQ_1:def 13; then
          reconsider H1=s2.x',H2=s2.(x'+1)
            as Element of the_stable_subgroups_of G by FINSEQ_2:13;
          reconsider H1,H2 as StableSubgroup of G by Def11;
          reconsider H1 as GroupWithOperators of O;
          reconsider H2 as normal StableSubgroup of H1 by Def31,A32;
          the carrier of H1 = the carrier of H2 by A31; then
          A33: H1./.H2 is trivial by Th77;
          s2 is strictly_decreasing by Def34,A4;
          hence contradiction by A33,A32,Def33;
        end;
      end; then
      A34: card(z \/ {i+1}) = card z + 1 by CARD_2:54;
      A35: dom Sgm y' = Seg card y' by A12,FINSEQ_3:45;
      A36: dom Sgm y' = dom Sgm z by A35,A34,A13,A5,A9,FINSEQ_3:45;
      y c= dom s1 by A12,FINSEQ_1:def 3; then
      A37: rng Sgm y c= dom s1 by A12,FINSEQ_1:def 13;
      rng Sgm z c= dom s1 by A9,A5,FINSEQ_1:def 13; then
      A38: dom(s1 * Sgm z) = dom Sgm z by RELAT_1:46;
      A39: dom(s1 * Sgm y) = dom(s1 * Sgm z) by A37,RELAT_1:46,A38,A36;
      A40: z c= Seg(k+1) by FINSEQ_1:def 3,A5;
      set z1 = {x where x is Element of NAT: x in z & x < i};
      set z2 = {x where x is Element of NAT: x in z & i+1 < x};
      A41: now let x;
        assume A42: x in z; then
        A43: x in Seg(k+1) by A40;
        reconsider x'=x as Element of NAT by A43;
        x'<=i or i+1<=x' by NAT_1:13; then
        x'<i or x'=i or i+1<x' by A42,A14,XXREAL_0:1; then
        x in z1 or x in {i} or x in z2 by TARSKI:def 1,A42; then
        x in z1 \/ {i} or x in z2 by XBOOLE_0:def 2;
        hence x in z1 \/ {i} \/ z2 by XBOOLE_0:def 2;
      end;
      A44: now let x;
        assume x in z1 \/ {i} \/ z2; then
        x in z1 \/ {i} or x in z2 by XBOOLE_0:def 2; then
        x in z1 or x in {i} or x in z2 by XBOOLE_0:def 2; then
        consider x',x'' be Element of NAT such that
        A45: (x = x' & x' in z & x'<i) or x in {i}
        or (x = x'' & x'' in z & i+1<x'');
        per cases by A45;
        suppose x = x' & x' in z & x'<i;
          hence x in z;
        end;
        suppose x in {i};
          hence x in z by A11,TARSKI:def 1;
        end;
        suppose x = x'' & x'' in z & i+1<x'';
          hence x in z;
        end;
      end; then
      A46: z = z1 \/ {i} \/ z2 by A41,TARSKI:2; then
      A47: z1 \/ {i} c= z & z2 c= z by XBOOLE_1:7; then
      A48: z1 \/ {i} c= Seg(k+1) & z2 c= Seg(k+1) by A40,XBOOLE_1:1;
      now
        let m,n be Element of NAT;
        assume m in z1 \/ {i}; then
        m in z1 or m in {i} by XBOOLE_0:def 2; then
        consider x' be Element of NAT such that
        A49: (m = x' & x' in z & x'<i) or m in {i};
        assume n in z2; then
        consider x'' be Element of NAT such that
        A50: n = x'' & x'' in z & i+1<x'';
        0+i<1+i by XREAL_1:8; then
        m<i+1 by A49,TARSKI:def 1,XXREAL_0:2;
        hence m < n by A50,XXREAL_0:2;
      end; then
      A51: Sgm z = Sgm(z1 \/ {i}) ^ Sgm z2 by A48,A46,FINSEQ_3:48;
      z1 c= z & {i} c= z by A47,XBOOLE_1:11; then
      A52: z1 c= Seg(k+1) & {i} c= Seg(k+1) by A40,XBOOLE_1:1;
      now
        let m,n be Element of NAT;
        assume m in z1; then
        consider x' be Element of NAT such that
        A53: (m = x' & x' in z & x'<i);
        assume n in {i};
        hence m < n by A53,TARSKI:def 1;
      end; then
      A54: Sgm(z1 \/ {i}) = Sgm z1 ^ Sgm {i} by A52,FINSEQ_3:48;
      A55: Sgm z = Sgm z1 ^ <*i*> ^ Sgm z2 by A7,A54,A51,FINSEQ_3:50;
      now let x;
        assume x in {i+1} /\ {i}; then
        x in {i+1} & x in {i} by XBOOLE_0:def 3; then
        x=i+1 & x=i by TARSKI:def 1;
        hence contradiction;
      end; then
      {i+1} /\ {i} = {} by XBOOLE_0:def 1; then
      A56: {i+1} misses {i} by XBOOLE_0:def 7;
      now let x;
        assume x in z2 /\ {i}; then
        A57: x in z2 & x in {i} by XBOOLE_0:def 3; then
        consider x' be Element of NAT such that
        A58: x'=x & x' in z & i+1<x';
        i+1<x' & x'=i by A57,A58,TARSKI:def 1; then
        i+1-i<i-i by XREAL_1:11; then
        1<0;
        hence contradiction;
      end; then
      z2 /\ {i} = {} by XBOOLE_0:def 1; then
      A59: z2 misses {i} by XBOOLE_0:def 7;
      now let x;
        assume x in z1 /\ {i}; then
        A60: x in z1 & x in {i} by XBOOLE_0:def 3; then
        consider x' be Element of NAT such that
        A61: x'=x & x' in z & x'<i;
        thus contradiction by TARSKI:def 1,A60,A61;
      end; then
      z1 /\ {i} = {} by XBOOLE_0:def 1; then
      A62: z1 misses {i} by XBOOLE_0:def 7;
      A63: y = (z1 \/ {i} \/ z2 \/ {i+1}) \ {i} by A44,A41,TARSKI:2
      .= ((z1 \/ {i} \/ z2)\{i}) \/ ({i+1}\{i}) by XBOOLE_1:42
      .= ((z1 \/ {i} \/ z2)\{i}) \/ {i+1} by A56,XBOOLE_1:83
      .= ((z1 \/ {i})\{i}) \/ (z2\{i}) \/ {i+1} by XBOOLE_1:42
      .= ((z1 \/ {i})\{i}) \/ z2 \/ {i+1} by A59,XBOOLE_1:83
      .= (z1\{i}) \/ ({i}\{i}) \/ z2 \/ {i+1} by XBOOLE_1:42
      .= (z1\{i}) \/ {} \/ z2 \/ {i+1} by XBOOLE_1:37
      .= z1 \/ {} \/ z2 \/ {i+1} by A62,XBOOLE_1:83
      .= z1 \/ {i+1} \/ z2 by XBOOLE_1:4; then
      A64: z1 \/ {i+1} c= y & z2 c= y by XBOOLE_1:7; then
      A65: z1 \/ {i+1} c= Seg(k+1) & z2 c= Seg(k+1) by A12,XBOOLE_1:1;
      now
        let m,n be Element of NAT;
        assume m in z1 \/ {i+1}; then
        m in z1 or m in {i+1} by XBOOLE_0:def 2; then
        consider x' be Element of NAT such that
        A66: (m = x' & x' in z & x'<i) or m in {i+1};
        assume n in z2; then
        consider x'' be Element of NAT such that
        A67: n = x'' & x'' in z & i+1<x'';
        0+i<1+i by XREAL_1:8; then
        m<i+1 or m=i+1 by A66,TARSKI:def 1,XXREAL_0:2;
        hence m < n by A67,XXREAL_0:2;
      end; then
      A68: Sgm y = Sgm(z1 \/ {i+1}) ^ Sgm z2 by A65,A63,FINSEQ_3:48;
      z1 c= y & {i+1} c= y by A64,XBOOLE_1:11; then
      A69: z1 c= Seg(k+1) & {i+1} c= Seg(k+1)
        by A12,XBOOLE_1:1;
      now
        let m,n be Element of NAT;
        assume m in z1; then
        consider x' be Element of NAT such that
        A70: (m = x' & x' in z & x'<i);
        assume n in {i+1}; then
        A71: n=i+1 by TARSKI:def 1;
        0+i<1+i by XREAL_1:8;
        hence m < n by A70,A71,XXREAL_0:2;
      end; then
      A72: Sgm(z1 \/ {i+1}) = Sgm z1 ^ Sgm {i+1} by A69,FINSEQ_3:48;
      A73: Sgm y = Sgm z1 ^ <*i+1*> ^ Sgm z2 by A72,A68,FINSEQ_3:50;
      A74: now
        let x;
        assume A75: x in dom Sgm z;
        reconsider x'=x as Element of NAT by A75;
        A76: len(Sgm z1 ^ <*i*>) = len Sgm z1 + len <*i*> by FINSEQ_1:35
        .= len Sgm z1 + 1 by FINSEQ_1:57
        .= len Sgm z1 + len <*i+1*> by FINSEQ_1:57
        .= len(Sgm z1 ^ <*i+1*>) by FINSEQ_1:35;
        A77: dom(Sgm z1 ^ <*i*>) = Seg len(Sgm z1 ^ <*i*>) by FINSEQ_1:def 3
        .= dom(Sgm z1 ^ <*i+1*>) by A76,FINSEQ_1:def 3;
        per cases by A75,A55,FINSEQ_1:38;
        suppose A78: x' in dom(Sgm z1 ^ <*i*>);
          per cases by A78,FINSEQ_1:38;
          suppose A79: x' in dom Sgm z1; then
            A80: (Sgm z1).x' = (Sgm z1 ^ <*i*>).x' by FINSEQ_1:def 7
            .= (Sgm z1 ^ <*i*> ^ Sgm z2).x' by A78,FINSEQ_1:def 7
            .= (Sgm z).x' by A7,A54,A51,FINSEQ_3:50;
            (Sgm z1).x' = (Sgm z1 ^ <*i+1*>).x' by A79,FINSEQ_1:def 7
            .= (Sgm z1 ^ <*i+1*> ^ Sgm z2).x' by A78,A77,FINSEQ_1:def 7
            .= (Sgm y).x' by A72,A68,FINSEQ_3:50;
            hence ((Sgm z).x <> i implies (Sgm y).x = (Sgm z).x) by A80;
            thus (Sgm z).x = i implies (Sgm y).x = i+1
            proof
              assume (Sgm z).x = i; then
              i in rng Sgm z1 by FUNCT_1:12,A79,A80; then
              i in z1 by FINSEQ_1:def 13,A52; then
              consider x''' be Element of NAT such that
              A81: x'''=i & x''' in z & x'''<i;
              thus (Sgm y).x = i+1 by A81;
            end;
          end;
          suppose ex x'' being Element of NAT st
            x'' in dom <*i*> & x'=len Sgm z1 + x''; then
            consider x'' be Element of NAT such that
            A82: x'' in dom <*i*> & x'=len Sgm z1 + x'';
            A83: x'' in Seg 1 by A82,FINSEQ_1:55; then
            A84: x'' = 1 by TARSKI:def 1,FINSEQ_1:4; then
            i = <*i*>.x'' by FINSEQ_1:57
            .= (Sgm z1 ^ <*i*>).x' by A82,FINSEQ_1:def 7
            .= (Sgm z1 ^ <*i*> ^ Sgm z2).x' by A78,FINSEQ_1:def 7
            .= (Sgm z).x' by A7,A54,A51,FINSEQ_3:50;
            hence ((Sgm z).x <> i implies (Sgm y).x = (Sgm z).x);
            thus (Sgm z).x = i implies (Sgm y).x = i+1
            proof
              assume (Sgm z).x = i;
              A85: x'' in dom <*i+1*> by A83,FINSEQ_1:55;
              i+1 = <*i+1*>.x'' by A84,FINSEQ_1:57
              .= (Sgm z1 ^ <*i+1*>).x' by A82,A85,FINSEQ_1:def 7
              .= (Sgm z1 ^ <*i+1*> ^ Sgm z2).x' by A77,A78,FINSEQ_1:def 7;
              hence (Sgm y).x = i+1 by A72,A68,FINSEQ_3:50;
            end;
          end;
        end;
        suppose ex x'' being Element of NAT st
          x'' in dom Sgm z2 & x'=len(Sgm z1 ^ <*i*>) + x''; then
          consider x'' be Element of NAT such that
          A86: x'' in dom Sgm z2 & x'=len(Sgm z1 ^ <*i*>) + x'';
          (Sgm y).x' = (Sgm z2).x'' by A76,A86,A73,FINSEQ_1:def 7;
          hence ((Sgm z).x <> i implies (Sgm y).x = (Sgm z).x)
            by A86,A55,FINSEQ_1:def 7;
          thus (Sgm z).x = i implies (Sgm y).x = i+1
          proof
            assume (Sgm z).x = i; then
            (Sgm z2).x'' = i by A86,A55,FINSEQ_1:def 7; then
            i in rng Sgm z2 by FUNCT_1:12,A86; then
            i in z2 by FINSEQ_1:def 13,A65; then
            consider x''' be Element of NAT such that
            A87: x'''=i & x''' in z & i+1<x''';
            i+1-i<i-i by A87,XREAL_1:11; then
            1<0;
            hence (Sgm y).x = i+1;
          end;
        end;
      end;
      now
        let x;
        assume A88: x in dom(s1 * Sgm y); then
        A89: x in dom(s1 * Sgm z) by A37,RELAT_1:46,A38,A36;
        A90: x in dom Sgm z & (Sgm z).x in dom s1 by A88,A39,FUNCT_1:21;
        per cases;
        suppose A91: (Sgm z).x = i;
          (s1 * Sgm y).x = s1.((Sgm y).x) by A88,FUNCT_1:22
          .= s1.((Sgm z).x) by A2,A91,A90,A74
          .= (s1 * Sgm z).x by A88,A39,FUNCT_1:22;
          hence (s1 * Sgm y).x = (s1 * Sgm z).x;
        end;
        suppose A92: (Sgm z).x <> i;
          (s1 * Sgm y).x = s1.((Sgm y).x) by A88,FUNCT_1:22
          .= s1.((Sgm z).x) by A92,A90,A74
          .= (s1 * Sgm z).x by A89,FUNCT_1:22;
          hence (s1 * Sgm y).x = (s1 * Sgm z).x;
        end;
      end;
      hence s2 = s1 * Sgm y by A5,A39,FUNCT_1:9;
    end;
  end; then
  consider y be set such that
  A93: y c= Seg(k+1) & not i in y & s2 = s1 * Sgm y;
  now
    consider x such that
    A94: Sgm x = Sgm(Seg(k+1)\{i})" * Sgm y & x c= Seg k by A6,A93,Lm39;
    take x;
    consider m be Nat such that
    A95: len s1 = m + 1 & len Del(s1,i) = m by A1,FINSEQ_3:113;
    thus x c= dom s1' by A95,A3,A94,FINSEQ_1:def 3;
    A96: Seg(k+1)\{i} c= Seg(k+1) by XBOOLE_1:36;
    set f = Sgm (Seg(k+1)\{i});
    set X = dom f;
    set Y = rng f;
    reconsider f as Function of X,Y by FUNCT_2:3;
    A97: rng f = Seg(k+1)\{i} by A96,FINSEQ_1:def 13;
    A98: f is one-to-one by A96,FINSEQ_3:99;
    A99: now assume A100: rng f = {};
      A101: 1<=i & i+1<=len s1 by A6,FINSEQ_1:3; then
      1+1<=i+1 by XREAL_1:8; then
      2<=len s1 by A101,XXREAL_0:2; then
      Seg 2 c= Seg(k+1) by FINSEQ_1:7; then
      Seg 2\{i} c= rng f by A97,XBOOLE_1:33; then
      A102: Seg 2\{i} = {} by A100,XBOOLE_1:3;
      per cases by A102,ZFMISC_1:66;
      suppose Seg 2 = {};
        hence contradiction by FINSEQ_1:4;
      end;
      suppose Seg 2 = {i};
        hence contradiction by ZFMISC_1:9,FINSEQ_1:4;
      end;
    end;
    now let x' be set;
      assume x' in y; then
      x' in Seg(k+1) & not x' in {i} by A93,TARSKI:def 1;
      hence x' in rng f by A97,XBOOLE_0:def 4;
    end; then
    y c= rng f by TARSKI:def 3; then
    A103: rng Sgm y c= rng f by A93,FINSEQ_1:def 13;
    s1' * Sgm x = s1 * f * Sgm x by A3,FINSEQ_1:def 3
    .= s1 * f * f" * Sgm y by A94,RELAT_1:55
    .= s1 * (f * f") * Sgm y by RELAT_1:55
    .= s1 * id rng f * Sgm y by A98,FUNCT_2:35,A99
    .= s1 * (id rng f * Sgm y) by RELAT_1:55
    .= s1 * Sgm y by A103,RELAT_1:79;
    hence s2 = s1' * Sgm x by A93;
  end;
  hence s1' is_finer_than s2 by Def32;
end;

theorem Th100:
  len s1 > 1 & s2<>s1 & s2 is strictly_decreasing
    & s2 is_finer_than s1 implies
  ex i,j st i in dom s1 & i in dom s2 & i+1 in dom s1 & i+1 in dom s2 &
     j in dom s2 & i+1<j & s1.i=s2.i & s1.(i+1)<>s2.(i+1) & s1.(i+1)=s2.j
proof
  assume len s1 > 1; then
  len s1 >= 1+1 by NAT_1:13; then
  Seg 2 c= Seg len s1 by FINSEQ_1:7; then
  A1: Seg 2 c= dom s1 by FINSEQ_1:def 3;
  assume A2: s2<>s1;
  assume A3: s2 is strictly_decreasing;
  assume A4: s2 is_finer_than s1;
  consider n such that
  A5: len s2 = len s1 + n by A4,Th95;
  n<>0 by A2,A5,A4,Th96; then
  0<n; then
  A6: 0 + len s1 < n + len s1 by XREAL_1:8;
  Seg len s1 c= Seg len s2 by A6,A5,FINSEQ_1:7; then
  Seg len s1 c= dom s2 by FINSEQ_1:def 3; then
  A7: dom s1 c= dom s2 by FINSEQ_1:def 3;
  now
    set fX = {k where k is Element of NAT: k in dom s1 & s1.k=s2.k};
    now let x;
      assume x in fX; then
      consider k be Element of NAT such that
      A8: x=k & k in dom s1 & s1.k=s2.k;
      thus x in dom s1 by A8;
    end; then
    A9: fX c= dom s1 by TARSKI:def 3;
    A10: s1.1 = (Omega).G & s2.1 = (Omega).G by Def31;
    1 in Seg 2 & 2 in Seg 2; then
    A11: 1 in fX by A1,A10;
    A12: fX c= NAT by A9,XBOOLE_1:1;
    A13: now let x;
      assume x in fX; then
      x in NAT by A12;
      hence x is real by XREAL_0:def 1;
    end;
    reconsider fX as finite non empty real-membered set
      by A9,FINSET_1:13,A11,A13,MEMBERED:def 3;
    set i = max fX;
    i in fX by PRE_CIRC:def 1; then
    consider k be Element of NAT such that
    A14: i=k & k in dom s1 & s1.k=s2.k;
    reconsider i as Element of NAT by A14;
    take i;
    thus i in dom s1 & s1.i=s2.i by A14;
    A15: now assume not i+1 in dom s1; then
      A16: not i+1 in Seg len s1 by FINSEQ_1:def 3;
      per cases by A16;
      suppose 1>i+1; then
        1-1>i+1-1 by XREAL_1:11; then
        0>i;
        hence contradiction;
      end;
      suppose i+1>len s1; then
        A17: i>=len s1 by NAT_1:13;
        i in Seg len s1 by A14,FINSEQ_1:def 3; then
        i<=len s1 by FINSEQ_1:3; then
        A18: i=len s1 by A17,XXREAL_0:1; then
        A19: s2.i=(1).G by Def31,A14;
        A20: 0+1<=i+1 by XREAL_1:8;
        i+1<=len s2 by A6,A5,A18,NAT_1:13; then
        i+1 in Seg len s2 by A20; then
        A21: i+1 in dom s2 by FINSEQ_1:def 3;
        reconsider H1=s2.i,H2=s2.(i+1)
          as Element of the_stable_subgroups_of G by A21,A14,FINSEQ_2:13;
        reconsider H1,H2 as StableSubgroup of G by Def11;
        reconsider H2 as normal StableSubgroup of H1 by A7,A14,A21,Def31;
        H2 is Subgroup of (1).G by A19,Def7; then
        the carrier of H2 c= the carrier of (1).G by GROUP_2:def 5; then
        A22: the carrier of H2 c= {1_G} by Def8;
        1_G in H2 by Lm18; then
        1_G in the carrier of H2 by STRUCT_0:def 5; then
        {1_G} c= the carrier of H2 by ZFMISC_1:37; then
        A23: the carrier of H2 = {1_G} by A22,XBOOLE_0:def 10;
        the carrier of H1 = {1_G} by A19,Def8; then
        H1./.H2 is trivial by Th77,A23;
        hence contradiction by A3,Def33,A7,A14,A21;
      end;
    end;
    hence i+1 in dom s1;
    now assume A24: s1.(i+1)=s2.(i+1);
      1+i>0+i by XREAL_1:8; then
      consider k be Element of NAT such that
      A25: k>i & k in dom s1 & s1.k=s2.k by A24,A15;
      k in fX by A25;
      hence contradiction by A25,PRE_CIRC:def 1;
    end;
    hence s1.(i+1)<>s2.(i+1);
  end; then
  consider i such that
  A26: i in dom s1 & i+1 in dom s1 & s1.i=s2.i & s1.(i+1)<>s2.(i+1);
  now
    consider x such that
    A27: x c= dom s2 & s1 = s2 * Sgm x by Def32,A4;
    A28: x c= Seg len s2 by A27,FINSEQ_1:def 3;
    set j = (Sgm x).(i+1);
    A29: i+1 in dom Sgm x by A27,A26,FUNCT_1:21; then
    j in rng Sgm x by FUNCT_1:12; then
    j in x by A28,FINSEQ_1:def 13; then
    A30: j in Seg len s2 by A28; then
    reconsider j as Element of NAT;
    take j;
    thus j in dom s2 by A30,FINSEQ_1:def 3;
    thus s1.(i+1)=s2.j by A27,A26,FUNCT_1:22;
    A31: i+1 <= j by A29,A28,GLIB_001:2;
    j<>i+1 by A27,FUNCT_1:22,A26;
    hence i+1<j by A31,XXREAL_0:1;
  end; then
  consider j such that
  A32: j in dom s2 & i+1<j & s1.(i+1)=s2.j;
  take i,j;
  thus i in dom s1 & i in dom s2 by A26,A7;
  thus i+1 in dom s1 & i+1 in dom s2 by A26,A7;
  thus j in dom s2 & i+1<j by A32;
  thus s1.i=s2.i & s1.(i+1)<>s2.(i+1) by A26;
  thus s1.(i+1)=s2.j by A32;
end;

theorem Th101:
  i in dom s1 & j in dom s1 & i<=j & H1 = s1.i & H2 = s1.j
    implies H2 is StableSubgroup of H1
proof
  assume A1: i in dom s1 & j in dom s1;
  assume A2: i<=j;
  assume A3: H1 = s1.i & H2 = s1.j;
  defpred P[natural number] means
    for n,H2 st i+$1 in dom s1 & H2 = s1.(i+$1)
    holds H2 is StableSubgroup of H1;
  A4: P[0] by A3,Th10;
  A5: for n st P[n] holds P[n+1]
  proof
    let n;
    assume A6: P[n];
    set H2 = s1.(i+n);
    per cases;
    suppose A7: i+n in dom s1; then
      reconsider H2 as Element of the_stable_subgroups_of G by FINSEQ_2:13;
      reconsider H2 as StableSubgroup of G by Def11;
      A8: H2 is StableSubgroup of H1 by A6,A7;
      now
        let k be Element of NAT;
        let H3;
        assume A9: i+(n+1) in dom s1;
        assume A10: H3 = s1.(i+(n+1));
        i+n+1 in dom s1 by A9; then
        H3 is StableSubgroup of H2 by A10,A7,Def31;
        hence H3 is StableSubgroup of H1 by A8,Th11;
      end;
      hence thesis;
    end;
    suppose not i+n in dom s1; then
      A11: not i+n in Seg len s1 by FINSEQ_1:def 3;
      per cases by A11,FINSEQ_1:3;
      suppose i+n<0+1; then
        A12: i+n<=0 by NAT_1:13;
        i+n=0 by A12; then
        n=0 by NAT_1:7;
        hence thesis by A1,A3,Def31;
      end;
      suppose i+n>len s1; then
        A13: i+n+1>len s1+1 by XREAL_1:8;
        1+len s1>0+len s1 by XREAL_1:8; then
        i+n+1>len s1 by A13,XXREAL_0:2; then
        not i+n+1 in Seg len s1 by FINSEQ_1:3;
        hence thesis by FINSEQ_1:def 3;
      end;
    end;
  end;
  A14: for n holds P[n] from NAT_1:sch 2(A4,A5);
  set n=j-i;
  i-i<=j-i by A2,XREAL_1:11; then
  reconsider n as Element of NAT by INT_1:16;
  reconsider n as natural number;
  j=i+n;
  hence H2 is StableSubgroup of H1 by A1,A3,A14;
end;

theorem Th102:
  y in rng the_series_of_quotients_of s1
    implies y is strict GroupWithOperators of O
proof
  assume A1: y in rng the_series_of_quotients_of s1;
  set f1=the_series_of_quotients_of s1;
  len f1=0 or len f1>0; then
  A2: len f1 = 0 or len f1 >= 0+1 by NAT_1:13;
  per cases by A2;
  suppose len f1 = 0;
    hence thesis by A1,RELAT_1:60,FINSEQ_1:25;
  end;
  suppose A3: len f1 >= 1;
    A4: now assume not len s1 > 1; then
      f1 = {} by Def36;
      hence contradiction by A3,FINSEQ_1:25;
    end; then
    A5: len s1 = len f1 + 1 by Def36;
    consider i be set such that
    A6: i in dom f1 & f1.i=y by A1,FUNCT_1:def 5;
    reconsider i as natural number by A6,ORDINAL1:def 13;
    A7: 0+i<=1+i by XREAL_1:8;
    i in Seg len f1 by FINSEQ_1:def 3,A6; then
    A8: 1<=i & i<=len f1 by FINSEQ_1:3; then
    1<=i & i+1<=len f1 + 1 by XREAL_1:8; then
    1<=i & i<=len s1 by XXREAL_0:2,A7,A5; then
    i in Seg len s1 by FINSEQ_1:3; then
    A9: i in dom s1 by FINSEQ_1:def 3; then
    A10: s1.i in the_stable_subgroups_of G by FINSEQ_2:13;
    reconsider H1=s1.i as strict StableSubgroup of G by A10,Def11;
    1+1<=i+1 & i + 1<=len f1 + 1 by A8,XREAL_1:8; then
    1<=i+1 & i+1<=len s1 by XXREAL_0:2,A4,Def36; then
    i+1 in Seg len s1 by FINSEQ_1:3; then
    A11: i+1 in dom s1 by FINSEQ_1:def 3; then
    s1.(i+1) in the_stable_subgroups_of G by FINSEQ_2:13; then
    reconsider N1=s1.(i+1) as strict StableSubgroup of G by Def11;
    reconsider N1 as normal StableSubgroup of H1 by A9,A11,Def31;
    y = H1./.N1 by A6,A4,Def36;
    hence thesis;
  end;
end;

theorem Th103:
  i in dom the_series_of_quotients_of s1 &
      (for H st H=(the_series_of_quotients_of s1).i holds H is trivial)
    implies i in dom s1 & i+1 in dom s1 & s1.i=s1.(i+1)
proof
  assume A1: i in dom the_series_of_quotients_of s1;
  assume A2: for H st H=(the_series_of_quotients_of s1).i holds H is trivial;
  set f1 = the_series_of_quotients_of s1;
  len f1=0 or len f1>0; then
  A3: len f1 = 0 or len f1 >= 0+1 by NAT_1:13;
  per cases by A3,XXREAL_0:1;
  suppose len f1 = 0;
    hence thesis by A1,RELAT_1:60,FINSEQ_1:25;
  end;
  suppose A4: len f1 = 1;
    A5: now assume not len s1 > 1; then
      f1 = {} by Def36;
      hence contradiction by A4,FINSEQ_1:25;
    end; then
    A6: len s1 = len f1 + 1 by Def36;
    A7: s1.1=(Omega).G & s1.2=(1).G by A4,A6,Def31;
    i in Seg 1 by A1,A4,FINSEQ_1:def 3; then
    A8: i=1 by FINSEQ_1:4,TARSKI:def 1;
    A9: for H1, N1 st H1=s1.i & N1=s1.(i+1)
      holds f1.i = H1./.N1 by A5,A1,Def36;
    set H1=(Omega).G;
    reconsider N1=(1).G as StableSubgroup of H1 by Th16;
    A10: (1).G = (1).H1 by Th15; then
    reconsider N1 as normal StableSubgroup of H1;
    f1.i in rng f1 by A1,FUNCT_1:12; then
    reconsider H=f1.i as strict GroupWithOperators of O by Th102;
    H is trivial by A2; then
    A11: H1./.N1 is trivial by A7,A9,A8;
    H1,H1./.N1 are_isomorphic by A10,Th56; then
    A12: H1 is trivial by A11,Th58;
    i in Seg 2 by A8;
    hence i in dom s1 by A4,A6,FINSEQ_1:def 3;
    i+1 in Seg 2 by A8;
    hence i+1 in dom s1 by A4,A6,FINSEQ_1:def 3;
    thus s1.i=s1.(i+1) by A12,A10,A7,A8,Th42;
  end;
  suppose A13: len f1 > 1;
    A14: now assume not len s1 > 1; then
      f1 = {} by Def36;
      hence contradiction by A13,FINSEQ_1:25;
    end; then
    A15: len s1 = len f1 + 1 by Def36;
    A16: 0+i<=1+i by XREAL_1:8;
    i in Seg len f1 by FINSEQ_1:def 3,A1; then
    A17: 1<=i & i<=len f1 by FINSEQ_1:3; then
    1<=i & i+1<=len f1 + 1 by XREAL_1:8; then
    1<=i & i<=len s1 by XXREAL_0:2,A16,A15; then
    A18: i in Seg len s1 by FINSEQ_1:3; then
    A19: i in dom s1 by FINSEQ_1:def 3; then
    A20: s1.i in the_stable_subgroups_of G by FINSEQ_2:13;
    reconsider H1=s1.i as strict StableSubgroup of G by A20,Def11;
    1+1<=i+1 & i + 1<=len f1 + 1 by A17,XREAL_1:8; then
    1<=i+1 & i+1<=len s1 by XXREAL_0:2,A14,Def36; then
    A21: i+1 in Seg len s1 by FINSEQ_1:3; then
    A22: i+1 in dom s1 by FINSEQ_1:def 3; then
    s1.(i+1) in the_stable_subgroups_of G by FINSEQ_2:13; then
    reconsider N1=s1.(i+1) as strict StableSubgroup of G by Def11;
    reconsider N1 as normal StableSubgroup of H1 by A19,A22,Def31;
    f1.i in rng f1 by A1,FUNCT_1:12; then
    reconsider H = f1.i as strict GroupWithOperators of O by Th102;
    H is trivial by A2; then
    A23: H1./.N1 is trivial by A14,A1,Def36;
    thus i in dom s1 by A18,FINSEQ_1:def 3;
    thus i+1 in dom s1 by A21,FINSEQ_1:def 3;
    thus s1.i=s1.(i+1) by Th76,A23;
  end;
end;

theorem Th104:
  i in dom s1 & i+1 in dom s1 & s1.i=s1.(i+1) & s2=Del(s1,i)
    implies the_series_of_quotients_of s2=Del(the_series_of_quotients_of s1,i)
proof
  assume A1: i in dom s1;
  assume A2: i+1 in dom s1;
  assume A3: s1.i=s1.(i+1);
  assume A4: s2=Del(s1,i);
  set f1 = the_series_of_quotients_of s1;
  A5: i in Seg len s1 by A1,FINSEQ_1:def 3; then
  1<=i & i<=len s1 by FINSEQ_1:3; then
  A6: 1+1<=i+1 by XREAL_1:8;
  i+1 in Seg len s1 by A2,FINSEQ_1:def 3; then
  A7: 1<=i+1 & i+1<=len s1 by FINSEQ_1:3; then
  A8: 2 <= len s1 by A6,XXREAL_0:2;
  consider k be Nat such that
  A9: len s1 = k + 1 & len Del(s1,i) = k by A1,FINSEQ_3:113;
  A10: 1 < len s1 by XXREAL_0:2,A8; then
  A11: len s1 = len f1 + 1 by Def36;
  1+1 <= len s2+1 by A4,A9,A6,A7,XXREAL_0:2; then
  A12: 1 <= len s2 by XREAL_1:8;
  per cases by A12,XXREAL_0:1;
  suppose A13: len s2 = 1; then
    A14: the_series_of_quotients_of s2 = {} by Def36;
    1 in Seg len f1 by A11,A13,A4,A9; then
    1 in dom f1 by FINSEQ_1:def 3; then
    consider k1 be Nat such that
    A15: len f1 = k1 + 1 & len Del(f1,1) = k1 by FINSEQ_3:113;
    1<=i & i<=1 by A13,A4,A9,A5,FINSEQ_1:3,A7,XREAL_1:8; then
    len Del(f1,i) = 0 by XXREAL_0:1,A15,A11,A13,A4,A9;
    hence thesis by A14,FINSEQ_1:25;
  end;
  suppose A16: len s2 > 1;
    i+1-1<=len s1-1 by A7,XREAL_1:11; then
    1 <= i & i <= len f1 by A11,A5,FINSEQ_1:3; then
    i in Seg len f1 by FINSEQ_1:3; then
    A17: i in dom f1 by FINSEQ_1:def 3; then
    consider k1 be Nat such that
    A18: len f1 = k1 + 1 & len Del(f1,i) = k1 by FINSEQ_3:113;
    now
      let n;
      assume A19: n in dom Del(f1,i); then
      A20: n in Seg len Del(f1,i) by FINSEQ_1:def 3;
      let H1, N1;
      assume A21: H1 = s2.n;
      assume A22: N1 = s2.(n+1);
      len f1-len Del(f1,i)+len Del(f1,i)>0+len Del(f1,i) by A18,XREAL_1:8; then
      Seg len Del(f1,i) c= Seg len f1 by FINSEQ_1:7; then
      n in Seg len f1 by A20; then
      A23: n in dom f1 by FINSEQ_1:def 3;
      A24: 1<=n & n<=len Del(f1,i) by FINSEQ_1:3,A20;
      A25: n<=k1 by FINSEQ_1:3,A20,A18;
      set n1 = n+1;
      A26: n1<=k by A18,A9,A11,A25,XREAL_1:8;
      0+n<1+n by XREAL_1:8; then
      A27: n<=k by XXREAL_0:2,A26;
      1+1<=n+1 by A24,XREAL_1:8; then
      1<=n1 by XXREAL_0:2; then
      n1 in Seg len f1 by A9,A11,A26,FINSEQ_1:3; then
      A28: n1 in dom f1 by FINSEQ_1:def 3;
      reconsider n1 as Nat;
      per cases;
      suppose A29: n<i; then
        A30: n1<=i by NAT_1:13;
        per cases by A30,XXREAL_0:1;
        suppose A31: n1<i;
          reconsider n'=n as Element of NAT by INT_1:16;
          s1.n' = H1 & s1.(n'+1) = N1
            by A21,A22,A29,A4,A9,A1,FINSEQ_3:119,A31; then
          f1.n = H1./.N1 by A23,A10,Def36;
          hence Del(f1,i).n = H1./.N1 by A1,A19,A18,A29,FINSEQ_3:119;
        end;
        suppose n1=i; then
          s1.n = H1 & s1.(n+1) = N1
            by A26,A21,A22,A29,A9,A1,A19,FINSEQ_3:119,A4,A3,FINSEQ_3:120; then
          f1.n = H1./.N1 by A23,A10,Def36;
          hence Del(f1,i).n = H1./.N1 by A1,A19,A18,A29,FINSEQ_3:119;
        end;
      end;
      suppose A32: n>=i;
        A33: 0+i<1+i by XREAL_1:8;
        n+1>=i+1 by XREAL_1:8,A32; then
        A34: n1>=i by A33,XXREAL_0:2;
        reconsider n1'=n1 as Element of NAT by INT_1:16;
        s1.n1' = H1 & s1.(n1'+1) = N1
          by A34,A26,A21,A22,A19,A27,A32,A1,A9,A4,FINSEQ_3:120; then
        f1.n1 = H1./.N1 by A28,A10,Def36;
        hence Del(f1,i).n = H1./.N1 by A25,A17,A18,A32,A19,FINSEQ_3:120;
      end;
    end;
    hence thesis by A16,A11,A4,A9,A18,Def36;
  end;
end;

theorem Th105:
  f1=the_series_of_quotients_of s1 & i in dom f1 &
    (for H st H = f1.i holds H is trivial)
  implies Del(s1,i) is CompositionSeries of G &
      for s2 st s2 = Del(s1,i) holds the_series_of_quotients_of s2 = Del(f1,i)
proof
  assume A1: f1=the_series_of_quotients_of s1;
  assume A2: i in dom f1;
  assume A3: for H st H = f1.i holds H is trivial;
  A4: i in dom s1 & i+1 in dom s1 & s1.i=s1.(i+1) by Th103,A1,A2,A3;
  hence Del(s1,i) is CompositionSeries of G by FINSEQ_3:114,Th94;
  let s2;
  assume s2 = Del(s1,i);
  hence the_series_of_quotients_of s2 = Del(f1,i) by A1,A4,Th104;
end;

theorem Th106:
  f1 = the_series_of_quotients_of s1 & f2 = the_series_of_quotients_of s2
    & i in dom f1 & (for H st H = f1.i holds H is trivial)
    & (ex p being Permutation of dom f1
       st f1,f2 are_equivalent_under p,O & j = p".i)
  implies ex p' being Permutation of dom Del(f1,i)
        st Del(f1,i),Del(f2,j) are_equivalent_under p',O
proof
  assume A1: f1 = the_series_of_quotients_of s1;
  assume A2: f2 = the_series_of_quotients_of s2;
  assume A3: i in dom f1;
  assume A4: for H st H = f1.i holds H is trivial;
  given p be Permutation of dom f1 such that
  A5: f1,f2 are_equivalent_under p,O & j = p".i;
  rng(p") c= dom f1; then
  A6: rng(p") c= Seg len f1 by FINSEQ_1:def 3;
  p".i in rng(p") by A3,FUNCT_2:6; then
  A7: p".i in Seg len f1 by A6;
  A8: len f1 = len f2 by A5,Def37; then
  A9: j in dom f2 by A5,A7,FINSEQ_1:def 3;
  A10: f1.i in rng f1 by A3,FUNCT_1:12;
  f2.j in rng f2 by A9,FUNCT_1:12; then
  reconsider H=f1.i,H2=f2.j
    as strict GroupWithOperators of O by A1,A2,A10,Th102;
  A11: H is trivial by A4;
  A12: H,H2 are_isomorphic by Def37,A5,A3;
  A13: for H st H = f2.j holds H is trivial by A12,A11,Th58;
  reconsider s1'=Del(s1,i) as CompositionSeries of G by A1,A3,A4,Th105;
  reconsider s2'=Del(s2,j) as CompositionSeries of G by A2,A9,A13,Th105;
  consider k1 be Nat such that
  A14: len f1 = k1 + 1 & len Del(f1,i) = k1 by A3,FINSEQ_3:113;
  consider k2 be Nat such that
  A15: len f2 = k2 + 1 & len Del(f2,j) = k2 by A9,FINSEQ_3:113;
  len f1=0 or len f1>0; then
  A16: len f1=0 or len f1>=0+1 by NAT_1:13;
  per cases by A16,XXREAL_0:1;
  suppose len f1 = 0; then
    A17: f1 = {} by FINSEQ_1:25;
    consider p' be Permutation of dom Del(f1,i);
    A18: for H1,H2 being GroupWithOperators of O,l being natural number ,n
      st l in dom Del(f1,i) & n=p'".l & H1=Del(f1,i).l & H2=Del(f2,j).n
      holds H1,H2 are_isomorphic by A17,RELAT_1:62,RELAT_1:60;
    take p';
    thus Del(f1,i),Del(f2,j) are_equivalent_under p',O
      by A18,Def37,A15,A14,A8;
  end;
  suppose A19: len f1 = 1;
    A20: Del(f1,i)={} by A19,A14,FINSEQ_1:25;
    reconsider p'={} as Function of dom {}, rng {} by FUNCT_2:3;
    reconsider p' as Function of {},{};
    p' is onto by FUNCT_2:def 3; then
    reconsider p' as Permutation of dom Del(f1,i) by FUNCT_2:def 4,A20;
    take p';
    A21: for H1,H2 being GroupWithOperators of O,l being natural number,n
      st l in dom Del(f1,i) & n=p'".l & H1=Del(f1,i).l & H2=Del(f2,j).n
      holds H1,H2 are_isomorphic by A19,A14,FINSEQ_1:25;
    thus Del(f1,i),Del(f2,j) are_equivalent_under p',O
      by A15,A14,A8,A21,Def37;
  end;
  suppose A22: len f1 > 1;
    set X = (dom f1)\{i};
    set Y = (dom f2)\{j};
    set p'=(Sgm X)" * p * Sgm Y;
    X c= dom f1 by XBOOLE_1:36; then
    A23: X c= Seg len f1 by FINSEQ_1:def 3;
    A24: rng Sgm X = X by A23,FINSEQ_1:def 13;
    A25: dom f1 = Seg len f1 by FINSEQ_1:def 3
    .= Seg len f2 by A5,Def37
    .= dom f2 by FINSEQ_1:def 3;
    A26: p.j = (p*p").i by A3,A5,FUNCT_2:21
    .= (id dom f1).i by FUNCT_2:88
    .= i by A3,FUNCT_1:35;
    Y c= dom f2 by XBOOLE_1:36; then
    A27: Y c= Seg len f2 by FINSEQ_1:def 3;
    Y c= dom f2 by XBOOLE_1:36; then
    A28: Y c= Seg len f2 by FINSEQ_1:def 3; then
    A29: Sgm Y is one-to-one by FINSEQ_3:99;
    A30: rng Sgm Y = Y by A28,FINSEQ_1:def 13;
    A31: now assume Y={}; then
      A32: dom f2 c= {j} by XBOOLE_1:37;
      {j} c= dom f2 by A9,ZFMISC_1:37; then
      A33: dom f2 = {j} by A32,XBOOLE_0:def 10;
      consider k be Element of NAT such that
      A34: dom f2 = Seg k by FINSEQ_1:def 2;
      k = len f2 by A34,FINSEQ_1:def 3; then
      k >= 1+1 by A8,A22,NAT_1:13; then
      Seg 2 c= Seg k by FINSEQ_1:7; then
      { 1,2 } = {j} by A34,FINSEQ_1:4,A33,ZFMISC_1:27;
      hence contradiction by ZFMISC_1:9;
    end;
    A35: Sgm Y is Function of dom Sgm Y, rng Sgm Y by FUNCT_2:3;
    A36: p' is Permutation of dom Del(f1,i) &
    p'" = (Sgm Y)" * (p") * (Sgm X)
    proof
      A37: Sgm X is one-to-one by FINSEQ_3:99,A23;
      A38: dom Del(f1,i) = Seg k1 by A14,FINSEQ_1:def 3;
      A39: {i} c= dom f1 by A3,ZFMISC_1:37;
      A40: X \/ {i} = dom f1 \/ {i} by XBOOLE_1:39
      .= dom f1 by A39,XBOOLE_1:12;
      X misses {i} by XBOOLE_1:79; then
      card(X \/ {i}) = (card X) + card {i} by CARD_2:53; then
      A41: (card X) + 1 = card(X \/ {i}) by CARD_1:50
      .= Card Seg len f1 by A40,FINSEQ_1:def 3
      .= k1+1 by FINSEQ_1:78,A14;
      A42: {j} c= dom f2 by A9,ZFMISC_1:37;
      A43: Y \/ {j} = dom f2 \/ {j} by XBOOLE_1:39
      .= dom f2 by A42,XBOOLE_1:12;
      Y misses {j} by XBOOLE_1:79; then
      card(Y \/ {j}) = (card Y) + card {j} by CARD_2:53; then
      (card Y) + 1 = card(Y \/ {j}) by CARD_1:50
      .= Card Seg len f2 by A43,FINSEQ_1:def 3
      .= Card Seg len f1 by A5,Def37
      .= k1+1 by A14,FINSEQ_1:78; then
      A44: dom(Sgm Y) = Seg k1 by A27,FINSEQ_3:45;
      A45: dom((Sgm X)") = X by A24,A37,FUNCT_1:55; then
      dom((Sgm X)") c= dom f1 by XBOOLE_1:36; then
      A46: dom((Sgm X)") c= rng p by FUNCT_2:def 3;
      A47: now let x;
        assume x in dom((Sgm X)" * p); then
        A48: x in dom p & p.x in dom((Sgm X)") by FUNCT_1:21; then
        x in dom f1 & p.x in X by A24,A37,FUNCT_1:55; then
        not p.x in {i} by XBOOLE_0:def 4; then
        p.x <> i by TARSKI:def 1; then
        x in dom f2 & not x in {j} by A26,A48,A25,TARSKI:def 1;
        hence x in Y by XBOOLE_0:def 4;
      end;
      now let x;
        assume A49: x in Y; then
        x in dom f2 & not x in {j} by XBOOLE_0:def 4; then
        A50: x <> j by TARSKI:def 1;
        dom f1 = dom p by FUNCT_2:def 1,A3; then
        A51: x in dom p by A25,A49,XBOOLE_0:def 4;
        dom f1 = rng p by FUNCT_2:def 3; then
        A52: p.x in dom f1 by A51,FUNCT_1:12;
        p.x <> i by A9,A25,A26,A51,A50,FUNCT_2:77; then
        not p.x in {i} by TARSKI:def 1; then
        p.x in X by A52,XBOOLE_0:def 4;
        hence x in dom((Sgm X)" * p) by A45,A51,FUNCT_1:21;
      end; then
      dom((Sgm X)" * p) = Y by A47,TARSKI:2; then
      A53: dom((Sgm X)" * p) = rng(Sgm Y) by A27,FINSEQ_1:def 13;
      rng((Sgm X)" * p * Sgm Y) = rng((Sgm X)" * p) by A53,RELAT_1:47
      .= rng((Sgm X)") by A46,RELAT_1:47
      .= dom(Sgm X) by A37,FUNCT_1:55; then
      A54: rng p'=Seg k1 by A41,A23,FINSEQ_3:45;
      A55: dom p'=Seg k1 by A44,A53,RELAT_1:46;
      reconsider p' as Function of dom Del(f1,i),dom Del(f1,i)
        by A55,A54,A38,FUNCT_2:3;
      A56: p' is onto by A54,A38,FUNCT_2:def 3;
      A57: Sgm Y is one-to-one by FINSEQ_3:99,A27;
      (Sgm X)" is one-to-one by A37,FUNCT_1:62; then
      (Sgm X)" * p is one-to-one by FUNCT_1:46; then
      p' is one-to-one by A57,FUNCT_1:46; then
      reconsider p' as Permutation of dom Del(f1,i) by A56,FUNCT_2:def 4;
      set p''=(Sgm Y)" * (p") * (Sgm X);
      A58: Sgm Y is one-to-one by FINSEQ_3:99,A27;
      set R1=(Sgm Y)"; set R2=Sgm Y;
      set R3=(Sgm X)"; set R4=Sgm X;
      set R5=p"; set R6=p;
      set R7=p';
      reconsider R1,R2,R3,R4,R5,R6,R7,p',p'' as Function;
      A59: R3=R4~ by A37,FUNCT_1:def 9;
      A60: R5=R6~ by FUNCT_1:def 9;
      reconsider R1,R2,R3,R4,R5,R6,R7 as Relation;
      p'"=R7~ by FUNCT_1:def 9
      .=((R6*R3)~)*(R2~) by RELAT_1:54
      .=((R3)~*(R6~))*(R2~) by RELAT_1:54
      .=((R4~)~*R5)*R1 by A58,FUNCT_1:def 9,A59,A60
      .=p'' by RELAT_1:55;
      hence thesis;
    end; then
    reconsider p' as Permutation of dom Del(f1,i);
    take p';
    now let H1,H2 be GroupWithOperators of O,l being natural number ,n;
      assume A61: l in dom Del(f1,i);
      assume A62: n=p'".l;
      assume A63: H1 = Del(f1,i).l & H2 = Del(f2,j).n;
      A64: l in dom (p'") by A61,FUNCT_2:def 1; then
      n in rng (p'") by A62,FUNCT_1:12; then
      n in dom Del(f1,i); then
      n in Seg len Del(f2,j) by FINSEQ_1:def 3,A15,A14,A8; then
      A65: n in dom Del(f2,j) by FINSEQ_1:def 3;
      A66: dom Del(f1,i) c= dom Sgm X by RELAT_1:44;
      A67: dom Del(f2,j) c= dom Sgm Y by RELAT_1:44;
      set l1=(Sgm X).l;
      set n1=(Sgm Y).n;
      A68: l1 in rng Sgm X by A61,A66,FUNCT_1:12; then
      A69: l1 in dom f1 by A24,XBOOLE_0:def 4;
      reconsider l1 as natural number by A68,ORDINAL1:def 13;
      n1 in rng Sgm Y by A65,A67,FUNCT_1:12; then
      reconsider n1 as natural number by ORDINAL1:def 13;
      A70: H1 = f1.l1 by A63,A61,A66,FUNCT_1:23;
      A71: H2 = f2.n1 by A63,A67,A65,FUNCT_1:23;
      A72: (Sgm Y)*(p'")
      = (Sgm Y) * ((Sgm Y)" * ((p") * (Sgm X))) by A36,RELAT_1:55
      .= ((Sgm Y) * (Sgm Y)") * ((p") * (Sgm X)) by RELAT_1:55
      .= id Y * ((p") * (Sgm X)) by A35,A29,A30,A31,FUNCT_2:35
      .= id Y * p" * (Sgm X) by RELAT_1:55
      .= (Y|p")*(Sgm X) by RELAT_1:123;
      A73: dom f1 = rng p by FUNCT_2:def 3;
      A74: l1 in dom(p") by A73,FUNCT_1:55,A69; then
      A75: p".l1 in rng(p") by FUNCT_1:12;
      now assume p".l1 in {j}; then
        A76: p".l1=p".i by A5,TARSKI:def 1;
        i in dom(p") by A73,A3,FUNCT_1:55; then
        l1=i by A74,A76,FUNCT_1:def 8; then
        i in rng Sgm X by A61,A66,FUNCT_1:12; then
        not i in {i} by A24,XBOOLE_0:def 4;
        hence contradiction by TARSKI:def 1;
      end; then
      A77: p".l1 in Y by A25,A75,XBOOLE_0:def 4;
      n1 = ((Sgm Y)*(p'")).l by A62,A64,FUNCT_1:23
      .= (Y|p").l1 by A72,A61,A66,FUNCT_1:23
      .= p".l1 by A69,A77,FUNCT_2:41;
      hence H1,H2 are_isomorphic by A69,A70,A71,A5,Def37;
    end;
    hence Del(f1,i),Del(f2,j) are_equivalent_under p',O
      by A15,A14,A8,Def37;
  end;
end;

theorem Th107:
  for G1,G2 being GroupWithOperators of O,
      s1 being CompositionSeries of G1, s2 being CompositionSeries of G2
    st s1 is empty & s2 is empty holds s1 is_equivalent_with s2
proof
  let G1,G2 be GroupWithOperators of O;
  let s1 be CompositionSeries of G1;
  let s2 be CompositionSeries of G2;
  assume A1: s1 is empty;
  assume A2: s2 is empty;
  for n st n + 1 = len s1 holds
    ex p being Permutation of Seg n st
      for H1 being StableSubgroup of G1, H2 being StableSubgroup of G2,
          N1 being normal StableSubgroup of H1,
          N2 being normal StableSubgroup of H2, i,j
      st 1<=i & i<=n & j=p.i & H1=s1.i & H2=s2.j &
          N1=s1.(i+1) & N2=s2.(j+1)
    holds H1./.N1,H2./.N2 are_isomorphic by A1,FINSEQ_1:25;
  hence s1 is_equivalent_with s2 by A2,A1,Def35;
end;

theorem Th108:
  for G1,G2 being GroupWithOperators of O,
      s1 be CompositionSeries of G1, s2 be CompositionSeries of G2
    st s1 is not empty & s2 is not empty holds s1 is_equivalent_with s2 iff
  ex p being Permutation of dom the_series_of_quotients_of s1 st
    the_series_of_quotients_of s1,the_series_of_quotients_of s2
    are_equivalent_under p,O
proof
  let G1,G2 be GroupWithOperators of O;
  let s1 be CompositionSeries of G1,s2 be CompositionSeries of G2;
  assume A1: s1 is not empty & s2 is not empty;
  set f1 = the_series_of_quotients_of s1;
  set f2 = the_series_of_quotients_of s2;
  hereby
    assume A2: s1 is_equivalent_with s2;
    A3: len s1 = len s2 by Def35,A2;
    per cases;
    suppose A4: len s1 <= 1;
      consider p be Permutation of dom the_series_of_quotients_of s1;
      take p;
      reconsider fs1=f1,fs2=f2 as FinSequence;
      A5: fs1 = {} by A4,Def36;
      A6: fs2 = {} by A4,A3,Def36;
      reconsider pf=p as Permutation of dom fs1;
      A7: len f1 = len f2 by A4,Def36,A6;
      for H1,H2 being GroupWithOperators of O, i,j
        st i in dom fs1 & j=pf".i & H1=fs1.i & H2=fs2.j
        holds H1,H2 are_isomorphic by FINSEQ_1:26,A5;
      hence the_series_of_quotients_of s1,the_series_of_quotients_of s2
        are_equivalent_under p,O by A7,Def37;
    end;
    suppose A8: len s1 > 1; then
      A9: len s1 - 1 > 1-1 by XREAL_1:11;
      set n = len s1 - 1;
      n in NAT by A9,INT_1:16; then
      reconsider n as natural number by ORDINAL1:def 13;
      n+1 = len s1; then
      consider p be Permutation of Seg n such that
      A10: for H1 being StableSubgroup of G1, H2 being StableSubgroup of G2,
            N1 being normal StableSubgroup of H1,
            N2 being normal StableSubgroup of H2, i,j
        st 1<=i & i<=n & j=p.i & H1=s1.i & H2=s2.j &
            N1=s1.(i+1) & N2=s2.(j+1)
          holds H1./.N1,H2./.N2 are_isomorphic by A2,Def35;
      A11: len s1 = len the_series_of_quotients_of s1 + 1 by Def36,A8;
      A12: dom the_series_of_quotients_of s1 = Seg n by A11,FINSEQ_1:def 3;
      reconsider p'=p" as Permutation of dom the_series_of_quotients_of s1
        by A12;
      take p';
      reconsider fs1=f1,fs2=f2 as FinSequence;
      reconsider pf=p' as Permutation of dom fs1;
      A13: len s2 = len the_series_of_quotients_of s2 + 1 by Def36,A8,A3;
      A14: pf" = p by FUNCT_1:65;
      now
        let H1',H2' be GroupWithOperators of O;
        let i,j;
        assume A15: i in dom fs1;
        assume A16: j = pf".i;
        assume A17: H1'=fs1.i & H2'=fs2.j;
        set H1=s1.i;
        set H2=s2.j;
        set N1=s1.(i+1);
        set N2=s2.(j+1);
        A18: i in Seg len fs1 by A15,FINSEQ_1:def 3; then
        A19: 1<=i & i<=len fs1 by FINSEQ_1:3;
        0+len fs1<1+len fs1 by XREAL_1:8; then
        i<=len s1 by A19,A11,XXREAL_0:2; then
        i in Seg len s1 by A19,FINSEQ_1:3; then
        A20: i in dom s1 by FINSEQ_1:def 3;
        0+i<1+i by XREAL_1:8; then
        A21: 1<=i+1 by A19,XXREAL_0:2;
        i+1<=len fs1+1 by A19,XREAL_1:8; then
        i+1 in Seg len s1 by A11,A21,FINSEQ_1:3; then
        A22: i+1 in dom s1 by FINSEQ_1:def 3;
        A23: i in dom p by A11,A18,FUNCT_2:def 1;
        A24: j in rng p by A14,A16,A23,FUNCT_1:12; then
        A25: j in Seg len f2 by A13,A3;
        A26: 1<=j & j<=len fs2 by A13,A3,A24,FINSEQ_1:3;
        0+len fs2<1+len fs2 by XREAL_1:8; then
        j<=len s2 by A26,A13,XXREAL_0:2; then
        j in Seg len s2 by A26,FINSEQ_1:3; then
        A27: j in dom s2 by FINSEQ_1:def 3;
        0+j<1+j by XREAL_1:8; then
        A28: 1<=j+1 by A26,XXREAL_0:2;
        j+1<=len fs2+1 by A26,XREAL_1:8; then
        j+1 in Seg len s2 by A13,A28,FINSEQ_1:3; then
        A29: j+1 in dom s2 by FINSEQ_1:def 3;
        reconsider H1,N1 as Element of the_stable_subgroups_of G1
          by A20,A22,FINSEQ_2:13;
        reconsider H1,N1 as StableSubgroup of G1 by Def11;
        reconsider N1 as normal StableSubgroup of H1 by A20,A22,Def31;
        reconsider H2,N2 as Element of the_stable_subgroups_of G2
          by A27,A29,FINSEQ_2:13;
        reconsider H2,N2 as StableSubgroup of G2 by Def11;
        reconsider N2 as normal StableSubgroup of H2 by A27,A29,Def31;
        A30: j in dom fs2 by A25,FINSEQ_1:def 3;
        dom fs1 = Seg n by A11,FINSEQ_1:def 3; then
        A31: 1<=i & i<=n by FINSEQ_1:3,A15;
        A32: H1./.N1,H2./.N2 are_isomorphic by A16,A14,A10,A31;
        H2./.N2 = H2' by A17,A30,A8,A3,Def36;
        hence H1',H2' are_isomorphic by A32,A15,A8,A17,Def36;
      end;
      hence the_series_of_quotients_of s1,the_series_of_quotients_of s2
        are_equivalent_under p',O by A13,A11,A3,Def37;
    end;
  end;
  given p be Permutation of dom the_series_of_quotients_of s1 such that
  A33: the_series_of_quotients_of s1,the_series_of_quotients_of s2
    are_equivalent_under p,O;
  A34: len f1 = len f2 &
    for H1 being StableSubgroup of G1, H2 being StableSubgroup of G2, i,j
      st i in dom f1 & j=p".i & H1=f1.i & H2=f2.j
      holds H1,H2 are_isomorphic by A33,Def37;
  per cases;
  suppose A35: len s1<=1;
    len s1<>0 by FINSEQ_1:25,A1; then
    len s1>0; then
    A36: len s1>=0+1 by NAT_1:13;
    A37: f1 = {} by Def36,A35;
    now assume A38: len s2<>1;
      len s2<>0 by FINSEQ_1:25,A1; then
      len s2>0; then
      len s2>=0+1 by NAT_1:13; then
      len s2>1 by XXREAL_0:1,A38; then
      len f2 + 1 > 0 + 1 by Def36;
      hence contradiction by A37,A34,FINSEQ_1:25;
    end; then
    A39: len s1 = len s2 by A36,A35,XXREAL_0:1;
    now
      let n;
      assume n + 1 = len s1; then
      n+1=1 by A36,A35,XXREAL_0:1; then
      A40: n=0;
      consider p be Permutation of Seg n;
      take p;
      let H1 be StableSubgroup of G1;
      let H2 be StableSubgroup of G2;
      let N1 be normal StableSubgroup of H1;
      let N2 be normal StableSubgroup of H2;
      let i,j;
      assume A41: 1<=i & i<=n & j=p.i;
      assume H1=s1.i & H2=s2.j;
      assume N1=s1.(i+1) & N2=s2.(j+1);
      thus H1./.N1,H2./.N2 are_isomorphic by A40,A41;
    end;
    hence s1 is_equivalent_with s2 by A39,Def35;
  end;
  suppose A42: len s1>1; then
    A43: len s1 = len f1 + 1 & for i st i in dom f1
      for H1 being StableSubgroup of G1, N1 being normal StableSubgroup of H1
      st H1=s1.i & N1=s1.(i+1) holds f1.i = H1./.N1 by Def36;
    A44: now assume len s2<=1; then
      f2 = {} by Def36; then
      len f2 = 0 by FINSEQ_1:25;
      hence contradiction by A42,A43,A34;
    end;
    A45: len s1 = len s2 by A43,A44,A34,Def36;
    now
      let n;
      assume A46: n + 1 = len s1;
      A47: dom f1 = Seg n by A46,A43,FINSEQ_1:def 3; then
      reconsider p'=p" as Permutation of Seg n;
      take p';
      let H1 be StableSubgroup of G1;
      let H2 be StableSubgroup of G2;
      let N1 be normal StableSubgroup of H1;
      let N2 be normal StableSubgroup of H2;
      let i,j;
      assume 1<=i & i<=n; then
      A48: i in dom f1 by A47,FINSEQ_1:3;
      A49: i in dom p' by A48,FUNCT_2:def 1;
      assume A50: j=p'.i;
      assume A51: H1=s1.i & H2=s2.j;
      assume A52: N1=s1.(i+1) & N2=s2.(j+1);
      A53: f1.i = H1./.N1 by A42,Def36,A48,A51,A52;
      j in rng p' by A50,A49,FUNCT_1:12; then
      A54: j in Seg n;
      A55: j in dom f2 by A54,A46,A43,A34,FINSEQ_1:def 3; then
      A56: f2.j = H2./.N2 by A44,Def36,A51,A52;
      set H1'=f1.i;
      set H2'=f2.j;
      H1' in rng f1 & H2' in rng f2 by A48,A55,FUNCT_1:12; then
      reconsider H1',H2' as Group by Th102;
      thus H1./.N1,H2./.N2 are_isomorphic by A33,Def37,A48,A50,A53,A56;
    end;
    hence s1 is_equivalent_with s2 by A45,Def35;
  end;
end;

theorem Th109:
  s1 is_finer_than s2 & s2 is jordan_holder & len s1 > len s2
    implies ex i st i in dom the_series_of_quotients_of s1 &
      for H st H = (the_series_of_quotients_of s1).i holds H is trivial
proof
  assume A1: s1 is_finer_than s2;
  assume A2: s2 is jordan_holder;
  assume A3: len s1 > len s2; then
  not s1 is strictly_decreasing by A1,Def34,A2; then
  not for i st i in dom s1 & i+1 in dom s1
  for H1,N1 st H1=s1.i & N1=s1.(i+1) holds not H1./.N1 is trivial
    by Def33; then
  consider i,H1,N1 such that
  A4: i in dom s1 & i+1 in dom s1 and
  A5: H1=s1.i & N1=s1.(i+1) and
  A6: H1./.N1 is trivial;
  i+1 in Seg len s1 by A4,FINSEQ_1:def 3; then
  A7: i+1 <= len s1 by FINSEQ_1:3;
  0+1 <= i+1 by XREAL_1:8; then
  A8: 1 <= len s1 by A7,XXREAL_0:2;
  per cases;
  suppose len s1 <= 1; then
    A9: len s1 = 1 by A8,XXREAL_0:1;
    now
      let i;
      assume i in dom s1; then
      i in Seg 1 by A9,FINSEQ_1:def 3; then
      A10: i = 1 by TARSKI:def 1,FINSEQ_1:4;
      assume i+1 in dom s1; then
      A11: 2 in Seg 1 by A10,A9,FINSEQ_1:def 3;
      let H1,N1;
      assume H1=s1.i;
      assume N1=s1.(i+1);
      assume H1./.N1 is trivial;
      thus contradiction by A11,TARSKI:def 1,FINSEQ_1:4;
    end; then
    s1 is strictly_decreasing by Def33;
    hence thesis by A1,A3,A2,Def34;
  end;
  suppose A12: len s1 > 1; then
    A13: len s1 = len the_series_of_quotients_of s1 + 1 &
      for i st i in dom the_series_of_quotients_of s1
      for H1, N1 st H1=s1.i & N1=s1.(i+1) holds
        (the_series_of_quotients_of s1).i = H1./.N1 by Def36;
    i in Seg len s1 by A4,FINSEQ_1:def 3; then
    1 <= i & i+1-1 <= len s1 - 1 by FINSEQ_1:3,A7,XREAL_1:11; then
    A14: i in Seg len the_series_of_quotients_of s1 by A13,FINSEQ_1:3;
    take i;
    thus A15: i in dom the_series_of_quotients_of s1 by A14,FINSEQ_1:def 3;
    let H;
    assume H = (the_series_of_quotients_of s1).i;
    hence H is trivial by A12,Def36,A15,A5,A6;
  end;
end;

:: this is Lm15 of WSIERP_1
Lm40:
for k,m being Element of NAT
holds
 k<m iff k<=m-1
proof
   let k,m be Element of NAT;
 A1:now assume k<m;
  then k+1<=m by INT_1:20;
  hence k<=m-1 by XREAL_1:21;
 end;
    now assume k<=m-1;
  then A2:k+1<=m by XREAL_1:21;
     k<k+1 by XREAL_1:31;
  hence k<m by A2,XXREAL_0:2;
 end;
 hence thesis by A1;
end;

:: this is Lm19 of WSIERP_1
Lm41:
for a being Element of NAT,
    fs being FinSequence
holds
  a in dom fs implies ex fs1,fs2 being FinSequence
  st fs=fs1^<*fs.a*>^fs2 &
    len fs1=a-1 & len fs2=len fs -a
proof
  let a be Element of NAT;
  let fs be FinSequence;
 assume A1: a in dom fs;
 then a>=1 & a<=len fs by FINSEQ_3:27;
 then reconsider b=len fs-a, d=a-1 as Element of NAT by INT_1:18;
    len fs=a+b;
 then consider fs3,fs2 be FinSequence such that
     A2: len fs3=a & len fs2=b & fs=fs3^fs2 by FINSEQ_2:25;
    a<>0 by A1,FINSEQ_3:27;
 then fs3 <> {} by A2,FINSEQ_1:25;
 then a in dom fs3 by A2,FINSEQ_5:6;
 then A3: fs3.a=fs.a by A2,FINSEQ_1:def 7;
 a=d+1;
 then consider fs1 be FinSequence,v be set such that
A4: fs3=fs1^<*v*> by A2,FINSEQ_2:21;
A5: len fs1 + 1=d+1 by A2,A4,FINSEQ_2:19;
    fs.a=v by A3,A4,A5,FINSEQ_1:59;
 hence thesis by A2,A4,A5;
end;

:: this is Lm20 of WSIERP_1
Lm42:
for a being Element of NAT,
    fs,fs1,fs2 being FinSequence,
    v being set
holds
 a in dom fs & fs=fs1^<*v*>^fs2 & len fs1=a-1 implies Del(fs,a)=fs1^fs2
proof
 let a be Element of NAT;
 let fs,fs1,fs2 be FinSequence;
 let v be set;
 assume A1: a in dom fs & fs=fs1^<*v*>^fs2 & len fs1=a-1;
 then A2: len fs=len(fs1^<*v*>)+len fs2 by FINSEQ_1:35
           .=len fs1 +1 +len fs2 by FINSEQ_2:19
           .=a +len fs2 by A1;
 A3: fs=fs1^(<*v*>^fs2) by A1,FINSEQ_1:45;
 A4: len<*v*>=1 by FINSEQ_1:56;
    len fs=(a-1) + len(<*v*>^fs2) by A1,A3,FINSEQ_1:35;
 then A5: len(<*v*>^fs2)=len fs -(a-1);
 A6: len(Del(fs,a))+1=len fs by A1,WSIERP_1:def 1;
 then len(Del(fs,a)) =len fs2 +len fs1 by A1,A2;
 then A7: len(fs1^fs2)=len(Del(fs,a)) by FINSEQ_1:35;
    now let e be Element of NAT; assume A8: 1<=e & e<=len Del(fs,a);
     now per cases;
   suppose A9: e<a;
    then e<=a-1 by Lm40;
then A10:  e in dom fs1 by A1,A8,FINSEQ_3:27;
    hence (fs1^fs2).e=fs1.e by FINSEQ_1:def 7
             .=fs.e by A3,A10,FINSEQ_1:def 7
             .=(Del(fs,a)).e by A1,A9,WSIERP_1:def 1;
   end;
   suppose A11: e>=a;
    then A12: e>a-1 by Lm40;
    then A13: e+1>a by XREAL_1:21;
    then e+1-a>0 by XREAL_1:52;
    then A14: e+1-a+1>0+1 by XREAL_1:8;
       a>a-1 by XREAL_1:148; then A15: e+1>a-1 by A13,XXREAL_0:2;
    then e+1-(a-1)>0 by XREAL_1:52;
    then reconsider f=e+1-(a-1) as Element of NAT by INT_1:16;
    A16: e+1<=len fs by A6,A8,XREAL_1:8;
    then A17: e+1-(a-1)<=len(<*v*>^fs2) by A5,XREAL_1:11;
    thus (fs1^fs2).e=fs2.(e-len fs1) by A1,A7,A8,A12,FINSEQ_1:37
             .=fs2.(f-1) by A1
             .=(<*v*>^fs2).f by A4,A14,A17,FINSEQ_1:37
             .=(fs1^(<*v*>^fs2)).(e+1) by A1,A3,A15,A16,FINSEQ_1:37
             .=(Del(fs,a)).e by A1,A3,A11,WSIERP_1:def 1;
  end;
  end;
  hence (fs1^fs2).e=(Del(fs,a)).e;
 end;
 hence thesis by A7,FINSEQ_1:18;
end;

:: this is Lm22 of WSIERP_1
Lm43:
for a being Element of NAT,
    fs1,fs2 being FinSequence
holds
 (a<=len fs1 implies Del(fs1^fs2,a)=Del(fs1,a)^fs2) &
 (a>=1 implies Del(fs1^fs2,len fs1 +a)=fs1^Del(fs2,a))
proof
 let a be Element of NAT;
 let fs1,fs2 be FinSequence;
 set f=fs1^fs2;
 A1:len f=len fs1 + len fs2 &
   len((Del(fs1,a))^fs2)=len(Del(fs1,a)) + len fs2 &
   len(fs1^Del(fs2,a))=len fs1 + len Del(fs2,a) by FINSEQ_1:35;
 A2:now assume A3:a<=len fs1;
  set f1= Del(f,a);
  set f2=((Del(fs1,a))^fs2);
  set f3=<*f.a*>;
     len fs1<=len f by A1,NAT_1:11;
  then A4: a<=len f by A3,XXREAL_0:2;
     now per cases;
   suppose a<1; then A5: not a in dom fs1 & not a in dom f by FINSEQ_3:27;
     hence f1=f by WSIERP_1:def 1 .=
     f2 by A5,WSIERP_1:def 1;
   end;
   suppose a>=1; then A6: a in dom f & a in dom fs1 by A3,A4,FINSEQ_3:27;
    then consider g1,g2 being FinSequence such that A7:
         f=g1^f3^g2 & len g1=a-1 & len g2=len f -a by Lm41;
       len(g1^f3)=a-1+1 by A7,FINSEQ_2:19
             .=a;
    then consider t being FinSequence such that A8:
        fs1=g1^f3^t by A3,A7,FINSEQ_1:64;
    A9: Del(fs1,a)=g1^t by A6,A7,A8,Lm42;
       g1^f3^g2=g1^f3^(t^fs2) by A7,A8,FINSEQ_1:45;
    then A10: g2=t^fs2 by FINSEQ_1:46;
    thus f1=g1^g2 by A6,A7,Lm42
          .=f2 by A9,A10,FINSEQ_1:45;
  end;
  end;
  hence f1=f2;
 end;
    now assume A11: a>=1;
  set f1= Del(f,len fs1 + a);
  set f2=fs1^Del(fs2,a);
    now per cases;
   suppose A12: a>len fs2; then A13: not a in dom fs2 by FINSEQ_3:27;
       len fs1 + a>len f by A1,A12,XREAL_1:8;
    then not (len fs1 + a) in dom f by FINSEQ_3:27;
    hence f1=fs1^fs2 by WSIERP_1:def 1
           .=f2 by A13,WSIERP_1:def 1;
   end;
   suppose A14: a<=len fs2; then A15: a in dom fs2 by A11,FINSEQ_3:27;
    A16: len fs1 + a <= len f by A1,A14,XREAL_1:8;
       len fs1 + a>=1 by A11,NAT_1:12;
    then A17: (len fs1 + a) in dom f by A16,FINSEQ_3:27;
    then consider g1,g2 being FinSequence such that A18:
       f=g1^<*f.(len fs1 +a)*>^g2 & len g1=len fs1 +a -1
       & len g2=len f -(len fs1 +a) by Lm41;
    A19: f1=g1^g2 by A17,A18,Lm42;
    A20: f=g1^(<*f.(len fs1 +a)*>^g2) by A18,FINSEQ_1:45;
    a-1>=0 by A11,XREAL_1:50; then
    (a-1)+len fs1>=0+len fs1 by XREAL_1:8;
    then consider t being FinSequence such that A21:
      fs1^t=g1 by A18,A20,FINSEQ_1:64;
       fs1^(t^<*f.(len fs1 +a)*>^g2)=fs1^(t^<*f.(len fs1 +a)*>)^g2 by
          FINSEQ_1:45
          .=f by A18,A21,FINSEQ_1:45;
    then A22: fs2=t^<*f.(len fs1 +a)*>^g2 by FINSEQ_1:46;
     len fs1 +(a-1)=len fs1 +len t by A18,A21,FINSEQ_1:35;
    then Del(fs2,a)=t^g2 by A15,A22,Lm42;
    hence f1=f2 by A19,A21,FINSEQ_1:45;
  end;
  end;
  hence f1=f2;
 end;
 hence thesis by A2;
end;

Lm44: for D being non empty set, f being FinSequence of D,
  p being Element of D, n being Element of NAT
  st n in dom f holds f = Del(Ins(f,n,p),n+1)
proof
  let D be non empty set;
  let f be FinSequence of D;
  let p be Element of D;
  let n be Element of NAT;
  assume n in dom f; then
  n in Seg len f by FINSEQ_1:def 3; then
  A1: 1<=n & n<= len f by FINSEQ_1:3;
  set fs1=f|n^<*p*>;
  set fs2=(f/^n);
  A2: len(f|n) = n by A1,FINSEQ_1:80;
  A3: len fs1 = len(f|n) + len <*p*> by FINSEQ_1:35
  .= n + 1 by A2,FINSEQ_1:56;
  Del(Ins(f,n,p),n+1) = Del(fs1,n+1)^fs2 by A3,Lm43
  .= f|n^fs2 by A2,WSIERP_1:48;
  hence thesis by RFINSEQ:21;
end;

:: ALG I.4.7 Proposition 9
theorem Th110:
  len s1 > 1 implies (s1 is jordan_holder iff
  for i st i in dom the_series_of_quotients_of s1 holds
  (the_series_of_quotients_of s1).i is strict simple GroupWithOperators of O)
proof
  assume A1: len s1 > 1;
  A2: now
    assume A3: s1 is jordan_holder;
    assume not for i st i in dom the_series_of_quotients_of s1 holds
      (the_series_of_quotients_of s1).i is
       strict simple GroupWithOperators of O; then
    consider i such that
    A4: i in dom the_series_of_quotients_of s1 and
    A5: (the_series_of_quotients_of s1).i is
      not strict simple GroupWithOperators of O;
    A6: len s1 = len the_series_of_quotients_of s1 + 1 by A1,Def36;
    0+len the_series_of_quotients_of s1<
    1+len the_series_of_quotients_of s1 by XREAL_1:8; then
    A7: len the_series_of_quotients_of s1 < len s1 by A1,Def36;
    A8: i in Seg len the_series_of_quotients_of s1 by A4,FINSEQ_1:def 3; then
    A9: 1<=i & i<=len the_series_of_quotients_of s1 by FINSEQ_1:3; then
    A10: i<=len s1 by A7,XXREAL_0:2; then
    i in Seg len s1 by A9,FINSEQ_1:3; then
    A11: i in dom s1 by FINSEQ_1:def 3;
    A12: 0+1<=i+1 by XREAL_1:8;
    A13: i+1 <= len s1 by A6,A9,XREAL_1:8; then
    i+1 in Seg len s1 by A12,FINSEQ_1:3; then
    A14: i+1 in dom s1 by FINSEQ_1:def 3;
    reconsider H1=s1.i,N1=s1.(i+1) as Element of the_stable_subgroups_of G
      by A11,A14,FINSEQ_2:13;
    reconsider H1,N1 as strict StableSubgroup of G by Def11;
    reconsider N1 as strict normal StableSubgroup of H1 by A11,A14,Def31;
    A15: H1./.N1 is not strict simple GroupWithOperators of O
      by A5,A1,A4,Def36;
    per cases by A15,Def13;
    suppose A16: H1./.N1 is trivial;
      s1 is strictly_decreasing by A3,Def34;
      hence contradiction by A16,A11,A14,Def33;
    end;
    suppose ex H being strict normal StableSubgroup of H1./.N1
      st H <> (Omega).(H1./.N1) & H <> (1).(H1./.N1); then
      consider H be strict normal StableSubgroup of H1./.N1 such that
      A17: H <> (Omega).(H1./.N1) & H <> (1).(H1./.N1);
      N1 = Ker nat_hom N1 by Th48; then
      consider N2 be strict StableSubgroup of H1 such that
      A18: the carrier of N2 = (nat_hom N1)"(the carrier of H) &
        (H is normal implies N1 is normal StableSubgroup of N2 &
         N2 is normal) by Th78;
      reconsider i as Element of NAT by ORDINAL1:def 13;
      N2 is strict StableSubgroup of G by Th11; then
      reconsider N2 as Element of the_stable_subgroups_of G by Def11;
      set s2 = Ins(s1,i,N2);
      A19: 1<=i & s1 is non empty by A1,A8,FINSEQ_1:25,FINSEQ_1:3;
      1 <= len s1 + 1 by A1,NAT_1:13; then
      A20: 1 <= len s2 by FINSEQ_5:72; then
      1 in Seg len s2; then
      A21: 1 in dom s2 by FINSEQ_1:def 3;
      1 in Seg len s1 by A1; then
      A22: 1 in dom s1 by FINSEQ_1:def 3;
      len s2 in Seg len s2 by A20; then
      A23: len s2 in dom s2 by FINSEQ_1:def 3;
      len s1 in Seg len s1 by A1; then
      A24: len s1 in dom s1 by FINSEQ_1:def 3;
      A25: s2.1 = s2/.1 by A21,FINSEQ_4:def 4
      .= s1/.1 by A19,FINSEQ_5:78
      .= s1.1 by A22,FINSEQ_4:def 4
      .= (Omega).G by Def31;
      A26: s2.(len s2) = s2/.(len s2) by A23,FINSEQ_4:def 4
      .= s2/.(len s1+1) by FINSEQ_5:72
      .= s1/.(len s1) by A13,FINSEQ_5:77
      .= s1.(len s1) by A24,FINSEQ_4:def 4
      .= (1).G by Def31;
      A27: len s2 = len s1 + 1 by FINSEQ_5:72; then
      A28: s1<>s2;
      now
        let j be natural number;
        assume A29: j in dom s2; then
        j in Seg len s2 by FINSEQ_1:def 3; then
        A30: 1<=j & j<=len s2 by FINSEQ_1:3;
        assume A31: j+1 in dom s2; then
        j+1 in Seg len s2 by FINSEQ_1:def 3; then
        A32: 1<=j+1 & j+1<=len s2 by FINSEQ_1:3;
        let H1',H2';
        assume A33: H1'=s2.j;
        assume A34: H2'=s2.(j+1);
        j<i or j=i or j>i by XXREAL_0:1; then
        j+1<=i or j=i or j>=i+1 by NAT_1:13; then
        A35: j+1-1<=i-1 or j=i or j>=i+1 by XREAL_1:11;
        per cases by A35,XXREAL_0:1;
        suppose A36: j<=i-1;
          i <= len s1 by A9,A7,XXREAL_0:2; then
          A37: Seg len(s1|i) = Seg i by FINSEQ_1:80;
          A38: dom(s1|i) c= dom s1 by RELAT_1:89;
          -1+i<0+i by XREAL_1:8; then
          j<=i by A36,XXREAL_0:2; then
          j in Seg len (s1|i) by A37,FINSEQ_1:3,A30; then
          A39: j in dom(s1|i) by FINSEQ_1:def 3;
          j+1<=i-1+1 by A36,XREAL_1:8; then
          j+1 in Seg len (s1|i) by A37,FINSEQ_1:3,A32; then
          A40: j+1 in dom(s1|i) by FINSEQ_1:def 3;
          A41: s2.j = s2/.j by A29,FINSEQ_4:def 4
          .= s1/.j by A39,FINSEQ_5:75
          .= s1.j by A39,A38,FINSEQ_4:def 4;
          s2.(j+1) = s2/.(j+1) by A31,FINSEQ_4:def 4
          .= s1/.(j+1) by A40,FINSEQ_5:75
          .= s1.(j+1) by A40,A38,FINSEQ_4:def 4;
          hence H2' is normal StableSubgroup of H1'
            by A41,A33,A34,A39,A38,A40,Def31;
        end;
        suppose A42: j=i;
          A43: s2.(j+1) = s2/.(i+1) by A42,A31,FINSEQ_4:def 4
          .= N2 by A10,FINSEQ_5:76;
          reconsider N2 as normal StableSubgroup of H1 by A18;
          A44: Seg len(s1|i) = Seg i by FINSEQ_1:80,A10;
          A45: dom(s1|i) c= dom s1 by RELAT_1:89;
          j in Seg i by A42,A30; then
          A46: j in dom(s1|i) by A44,FINSEQ_1:def 3;
          s2.j = s2/.j by A29,FINSEQ_4:def 4
          .= s1/.j by A46,FINSEQ_5:75
          .= s1.j by A46,A45,FINSEQ_4:def 4;
          hence H2' is normal StableSubgroup of H1' by A43,A34,A18,A42,A33;
        end;
        suppose A47: j=i+1;
          i+1<=len s1+1 by A10,XREAL_1:8; then
          1<=i+1 & i+1<=len s2 by A12,FINSEQ_5:72; then
          i+1 in Seg len s2; then
          A48: i+1 in dom s2 by FINSEQ_1:def 3;
          A49: H1' = s2/.(i+1) by A33,A47,A48,FINSEQ_4:def 4
          .= N2 by A10,FINSEQ_5:76;
          i+1+1<=len s1+1 by A13,XREAL_1:8; then
          1<=i+1+1 & i+1+1<=len s2 by XREAL_1:8,A12,FINSEQ_5:72; then
          i+1+1 in Seg len s2; then
          A50: i+1+1 in dom s2 by FINSEQ_1:def 3;
          H2' = s2/.(i+1+1) by A50,FINSEQ_4:def 4,A34,A47
          .= s1/.(i+1) by A13,FINSEQ_5:77
          .= N1 by A14,FINSEQ_4:def 4;
          hence H2' is normal StableSubgroup of H1' by A18,A49;
        end;
        suppose A51: i+1<j; then
          i+1+1<=j by NAT_1:13; then
          A52: i+1+1-1<=j-1 by XREAL_1:11;
          set j'=j-1;
          0+1<=i+1 by XREAL_1:8; then
          A53: 0+1<j by A51,XXREAL_0:2; then
          A54: 0+1-1<j-1 by XREAL_1:11; then
          reconsider j' as Element of NAT by INT_1:16;
          A55: j-1<=len s2-1 by A30,XREAL_1:11;
          0+j'<1+j' by XREAL_1:8; then
          A56: i+1 <= j'+1 by A52,XXREAL_0:2;
          A57: j+1-1<=len s2-1 by A32,XREAL_1:11;
          A58: 0+1<=j' by A54,NAT_1:13;
          j' in Seg len s1 by A55,A27,A58; then
          A59: j' in dom s1 by FINSEQ_1:def 3;
          j'+1 in Seg len s1 by A53,A57,A27; then
          A60: j'+1 in dom s1 by FINSEQ_1:def 3;
          A61: s2.j = s2/.(j'+1) by A29,FINSEQ_4:def 4
          .= s1/.j' by A52,A55,A27,FINSEQ_5:77
          .= s1.j' by A59,FINSEQ_4:def 4;
          s2.(j+1) = s2/.(j'+1+1) by A31,FINSEQ_4:def 4
          .= s1/.(j'+1) by A56,A57,A27,FINSEQ_5:77
          .=s1.(j'+1) by A60,FINSEQ_4:def 4;
          hence H2' is normal StableSubgroup of H1' by A61,A33,A34,Def31,A59,
A60;
        end;
      end; then
      reconsider s2 as CompositionSeries of G by A25,A26,Def31;
      now
        let j be natural number;
        assume A62: j in dom s2; then
        j in Seg len s2 by FINSEQ_1:def 3; then
        A63: 1<=j & j<=len s2 by FINSEQ_1:3;
        assume A64: j+1 in dom s2; then
        j+1 in Seg len s2 by FINSEQ_1:def 3; then
        A65: 1<=j+1 & j+1<=len s2 by FINSEQ_1:3;
        let H1',N1';
        assume A66: H1'=s2.j;
        assume A67: N1'=s2.(j+1);
        j<i or j=i or j>i by XXREAL_0:1; then
        j+1<=i or j=i or j>=i+1 by NAT_1:13; then
        A68: j+1-1<=i-1 or j=i or j>=i+1 by XREAL_1:11;
        A69: s1 is strictly_decreasing by A3,Def34;
        per cases by A68,XXREAL_0:1;
        suppose A70: j<=i-1;
          A71: Seg len(s1|i) = Seg i by A10,FINSEQ_1:80;
          A72: dom(s1|i) c= dom s1 by RELAT_1:89;
          -1+i<0+i by XREAL_1:8; then
          j<=i by A70,XXREAL_0:2; then
          j in Seg len (s1|i) by A71,FINSEQ_1:3,A63; then
          A73: j in dom(s1|i) by FINSEQ_1:def 3;
          j+1<=i-1+1 by A70,XREAL_1:8; then
          j+1 in Seg len (s1|i) by A71,FINSEQ_1:3,A65; then
          A74: j+1 in dom(s1|i) by FINSEQ_1:def 3;
          A75: s2.j = s2/.j by A62,FINSEQ_4:def 4
          .= s1/.j by A73,FINSEQ_5:75
          .= s1.j by A73,A72,FINSEQ_4:def 4;
          s2.(j+1) = s2/.(j+1) by A64,FINSEQ_4:def 4
          .= s1/.(j+1) by A74,FINSEQ_5:75
          .= s1.(j+1) by A74,A72,FINSEQ_4:def 4;
          hence H1'./.N1' is not trivial by A75,A66,A67,Def33,A73,A74,A72,A69;
        end;
        suppose A76: j=i;
          A77: s2.(j+1) = s2/.(i+1) by A76,A64,FINSEQ_4:def 4
          .= N2 by A10,FINSEQ_5:76;
          reconsider N2 as normal StableSubgroup of H1 by A18;
          A78: Seg len(s1|i) = Seg i by A10,FINSEQ_1:80;
          A79: dom(s1|i) c= dom s1 by RELAT_1:89;
          j in Seg i by A76,A63; then
          A80: j in dom(s1|i) by A78,FINSEQ_1:def 3;
          A81: s2.j = s2/.j by A62,FINSEQ_4:def 4
          .= s1/.j by A80,FINSEQ_5:75
          .= s1.j by A80,A79,FINSEQ_4:def 4;
          now assume H1'./.N1' is trivial; then
            H1 = N2 by A81,Th76,A76,A66,A77,A67;
            hence contradiction by A17,A18,Th80;
          end;
          hence H1'./.N1' is not trivial;
        end;
        suppose A82: j=i+1;
          i+1<=len s1+1 by A10,XREAL_1:8; then
          1<=i+1 & i+1<=len s2 by A12,FINSEQ_5:72; then
          i+1 in Seg len s2; then
          A83: i+1 in dom s2 by FINSEQ_1:def 3;
          A84: H1' = s2/.(i+1) by A83,FINSEQ_4:def 4,A66,A82
          .= N2 by A10,FINSEQ_5:76;
          i+1+1<=len s1+1 by A13,XREAL_1:8; then
          1<=i+1+1 & i+1+1<=len s2 by XREAL_1:8,FINSEQ_5:72,A12; then
          i+1+1 in Seg len s2; then
          A85: i+1+1 in dom s2 by FINSEQ_1:def 3;
          A86: N1' = s2/.(i+1+1) by A85,FINSEQ_4:def 4,A82,A67
          .= s1/.(i+1) by A13,FINSEQ_5:77
          .= N1 by A14,FINSEQ_4:def 4;
          now assume H1'./.N1' is trivial; then
            the carrier of N1 = (nat_hom N1)"(the carrier of H)
              by A18,A84,A86,Th76;
            hence contradiction by A17,Th81;
          end;
          hence H1'./.N1' is not trivial;
        end;
        suppose A87: i+1<j; then
          i+1+1<=j by NAT_1:13; then
          A88: i+1+1-1<=j-1 by XREAL_1:11;
          set j'=j-1;
          A89: 0+1<=i+1 by XREAL_1:8; then
          0+1<j by A87,XXREAL_0:2; then
          A90: 0+1-1<j-1 by XREAL_1:11; then
          reconsider j' as Element of NAT by INT_1:16;
          A91: j-1<=len s2-1 by A63,XREAL_1:11;
          0+j'<1+j' by XREAL_1:8; then
          A92: i+1 <= j'+1 by A88,XXREAL_0:2;
          A93: j+1-1<=len s2-1 by A65,XREAL_1:11;
          A94: 0+1<=j' by A90,NAT_1:13;
          j' in Seg len s1 by A91,A27,A94; then
          A95: j' in dom s1 by FINSEQ_1:def 3;
          1<=j'+1 by A89,A87,XXREAL_0:2; then
          j'+1 in Seg len s1 by A93,A27; then
          A96: j'+1 in dom s1 by FINSEQ_1:def 3;
          A97: s2.j = s2/.(j'+1) by A62,FINSEQ_4:def 4
          .= s1/.j' by A88,A91,A27,FINSEQ_5:77
          .= s1.j' by A95,FINSEQ_4:def 4;
          s2.(j+1) = s2/.(j'+1+1) by A64,FINSEQ_4:def 4
          .= s1/.(j'+1) by A92,A93,A27,FINSEQ_5:77
          .=s1.(j'+1) by A96,FINSEQ_4:def 4;
          hence H1'./.N1' is not trivial by A97,A66,A67,Def33,A95,A96,A69;
        end;
      end; then
      A98: s2 is strictly_decreasing by Def33;
      A99: (dom s2) \ {i+1} c= dom s2 by XBOOLE_1:36;
      s1 = Del(s2,i+1) by A11,Lm44; then
      s2 is_finer_than s1 by A99,Def32;
      hence contradiction by A3,Def34,A28,A98;
    end;
  end;
  now
    assume A100: for i st i in dom the_series_of_quotients_of s1 holds
  (the_series_of_quotients_of s1).i is strict simple GroupWithOperators of O;
    assume A101: not s1 is jordan_holder;
      per cases by A101,Def34;
      suppose s1 is not strictly_decreasing; then
        not for i st i in dom s1 & i+1 in dom s1 for H1,N1
          st H1=s1.i & N1=s1.(i+1) holds H1./.N1 is not trivial by Def33; then
        consider i,H1,N1 such that
        A102: i in dom s1 & i+1 in dom s1 and
        A103: H1=s1.i & N1=s1.(i+1) and
        A104: H1./.N1 is trivial;
        i in Seg len s1 & i+1 in Seg len s1 by A102,FINSEQ_1:def 3; then
        A105: 1 <= i & i+1 <= len s1 by FINSEQ_1:3; then
        1+1 <= i+1 by XREAL_1:8; then
        1+1 <= len s1 by A105,XXREAL_0:2; then
        A106: len s1 > 1 by NAT_1:13; then
        len the_series_of_quotients_of s1 + 1 = len s1 by Def36; then
        len the_series_of_quotients_of s1 = len s1 - 1; then
        i+1-1 <= len the_series_of_quotients_of s1 by XREAL_1:11,A105; then
        i in Seg len the_series_of_quotients_of s1 by A105,FINSEQ_1:3; then
        A107: i in dom the_series_of_quotients_of s1 by FINSEQ_1:def 3; then
        H1./.N1 is trivial & (the_series_of_quotients_of s1).i=H1./.N1
          by A103,A104,A106,Def36; then
        H1./.N1 is strict simple GroupWithOperators of O by A107,A100;
        hence contradiction by Def13,A104;
      end;
      suppose ex s2 st s2<>s1
        & s2 is strictly_decreasing & s2 is_finer_than s1; then
        consider s2 such that
        A108: s2<>s1 & s2 is strictly_decreasing & s2 is_finer_than s1;
        consider i,j such that
        A109: i in dom s1 & i in dom s2 & i+1 in dom s1 & i+1 in dom s2 &
          j in dom s2 and
        A110: i+1<j & s1.i=s2.i & s1.(i+1)<>s2.(i+1) &
          s1.(i+1)=s2.j by Th100,A1,A108;
        reconsider H1=s1.i,H2=s1.(i+1),H=s2.(i+1)
          as Element of the_stable_subgroups_of G by A109,FINSEQ_2:13;
        reconsider H1,H2,H as strict StableSubgroup of G by Def11;
        reconsider H as strict normal StableSubgroup of H1 by A109,A110,Def31;
        reconsider H2 as strict normal StableSubgroup of H1 by A109,Def31;
        reconsider H3 = the HGrWOpStr of H2
          as strict normal StableSubgroup of H by Th40,A109,A110,Th101;
        reconsider H2'=H2 as normal StableSubgroup of H
          by A109,A110,Th101,Th40;
        reconsider J = H./.H2' as strict normal StableSubgroup of H1./.H2
          by Th44;
        A111: now assume J = (Omega).(H1./.H2); then
        the carrier of H = union Cosets H2 by Th22; then
          A112: the carrier of H = the carrier of H1  by Th22; then
          A113: H = H1 by Lm5;
          reconsider H1 as strict normal StableSubgroup of H by A113;
          H1 = (Omega).H by A112,Lm5; then
          H./.H1 is trivial by Th57;
          hence contradiction by A113,A108,A109,Def33,A110;
        end;
        now assume J = (1).(H1./.H2); then
          union Cosets H3 = union {1_(H1./.H2)} by Def8; then
          the carrier of H = union {1_(H1./.H2)} by Th22; then
          the carrier of H = 1_(H1./.H2) by ZFMISC_1:31; then
          the carrier of H = carr H2 by Th43;
          hence contradiction by A110,Lm5;
        end; then
        A114: H1./.H2 is not simple GroupWithOperators of O by A111,Def13;
        i in Seg len s1 & i+1 in Seg len s1 by A109,FINSEQ_1:def 3; then
        A115: 1 <= i & i+1 <= len s1 by FINSEQ_1:3; then
        1+1 <= i+1 by XREAL_1:8; then
        1+1 <= len s1 by A115,XXREAL_0:2; then
        A116: len s1 > 1 by NAT_1:13; then
        len the_series_of_quotients_of s1 + 1 = len s1 by Def36; then
        len the_series_of_quotients_of s1 = len s1 - 1; then
        i+1-1 <= len the_series_of_quotients_of s1 by XREAL_1:11,A115; then
        i in Seg len the_series_of_quotients_of s1 by A115,FINSEQ_1:3; then
        A117: i in dom the_series_of_quotients_of s1 by FINSEQ_1:def 3; then
        (the_series_of_quotients_of s1).i=H1./.H2 by A116,Def36;
        hence contradiction by A114,A100,A117;
      end;
  end;
  hence thesis by A2;
end;

theorem Th111:
  1<=i & i<=len s1-1 implies
    s1.i is strict StableSubgroup of G & s1.(i+1) is strict StableSubgroup of G
proof
  assume A1: 1<=i & i<=len s1-1;
  A2: 0+i<=1+i by XREAL_1:8; then
  A3: 1<=i+1 by A1,XXREAL_0:2;
  A4: i+1<=len s1-1+1 by A1,XREAL_1:8; then
  i+1 in Seg len s1 by A3,FINSEQ_1:3; then
  A5: i+1 in dom s1 by FINSEQ_1:def 3;
  i<=len s1 by A2,A4,XXREAL_0:2; then
  i in Seg len s1 by A1,FINSEQ_1:3; then
  A6: i in dom s1 by FINSEQ_1:def 3;
  A7: s1.i is Element of the_stable_subgroups_of G by A6,FINSEQ_2:13;
  A8: s1.(i+1) is Element of the_stable_subgroups_of G by A5,FINSEQ_2:13;
  thus s1.i is strict StableSubgroup of G by A7,Def11;
  thus s1.(i+1) is strict StableSubgroup of G by A8,Def11;
end;

theorem Th112:
  1<=i & i<=len s1-1 & H1=s1.i & H2=s1.(i+1)
    implies H2 is normal StableSubgroup of H1
proof
  assume A1: 1<=i & i<=len s1-1;
  assume A2: H1=s1.i & H2=s1.(i+1);
  A3: 0+i<=1+i by XREAL_1:8; then
  A4: 1<=i+1 by A1,XXREAL_0:2;
  A5: i+1<=len s1-1+1 by A1,XREAL_1:8; then
  i+1 in Seg len s1 by A4,FINSEQ_1:3; then
  A6: i+1 in dom s1 by FINSEQ_1:def 3;
  i<=len s1 by A3,A5,XXREAL_0:2; then
  i in Seg len s1 by A1,FINSEQ_1:3; then
  A7: i in dom s1 by FINSEQ_1:def 3;
  thus H2 is normal StableSubgroup of H1 by A2,A6,A7,Def31;
end;

theorem Th113:
  s1 is_equivalent_with s1
proof
  per cases;
  suppose s1 is empty; hence thesis by Th107; end;
  suppose A1: s1 is not empty;
    set f1=the_series_of_quotients_of s1;
    now
      set p = id dom f1;
      reconsider p as Function of dom f1,dom f1;
      rng p = dom f1 by RELAT_1:71; then
      p is onto by FUNCT_2:def 3; then
      reconsider p as Permutation of dom f1 by FUNCT_2:def 4;
      take p;
      A2: len f1 = len f1;
      now
        let H1,H2 be GroupWithOperators of O;
        let i,j;
        assume A3: i in dom f1 & j=p".i;
        assume A4: H1=f1.i & H2=f1.j;
        p" = p by FUNCT_1:67;
        hence H1,H2 are_isomorphic by A3,A4,FUNCT_1:35;
      end;
      hence f1,f1 are_equivalent_under p,O by A2,Def37;
    end;
    hence s1 is_equivalent_with s1 by A1,Th108;
  end;
end;

theorem Th114:
  (len s1<=1 or len s2<=1) & len s1<=len s2 implies s2 is_finer_than s1
proof
  assume A1: len s1<=1 or len s2<=1;
  assume A2: len s1<=len s2;
  A3: len s1 <=1 by A1,A2,XXREAL_0:2;
  per cases;
  suppose A4: len s1=1; then
    A5: s1 = <* s1.1 *> by FINSEQ_1:57;
    now
      set x={1};
      take x;
      A6: 1 in Seg len s2 by A4,A2; then
      1 in dom s2 by FINSEQ_1:def 3;
      hence x c= dom s2 by ZFMISC_1:37;
      set f=s2;
      set p=<*1*>;
      reconsider D=Seg len s2 as non empty set by A4,A2,FINSEQ_1:3;
      A7: dom f = Seg len s2 by FINSEQ_1:def 3;
      rng f c= the_stable_subgroups_of G; then
      reconsider f as Function of D, the_stable_subgroups_of G by A7,FUNCT_2:4;
      {1} c= D by A6,ZFMISC_1:37; then
      rng p c= D by FINSEQ_1:55; then
      reconsider p as FinSequence of D by FINSEQ_1:def 4;
      A8: Sgm x = p by FINSEQ_3:50;
      f * p = <* f.1 *> by FINSEQ_2:39; then
      s2 * Sgm x = <* (Omega).G *> by A8,Def31;
      hence s1 = s2 * Sgm x by A5,Def31;
    end;
    hence s2 is_finer_than s1 by Def32;
  end;
  suppose len s1<>1; then
    len s1<0+1 by A3,XXREAL_0:1; then
    len s1<=0 by NAT_1:13; then
    len s1=0; then
    A9: s1={} by FINSEQ_1:25;
    now
      set x={};
      take x;
      thus x c= dom s2 by XBOOLE_1:2;
      thus s1 = s2 * Sgm x by A9,RELAT_1:62,FINSEQ_3:49;
    end;
    hence s2 is_finer_than s1 by Def32;
  end;
end;

theorem Th115:
  s1 is_equivalent_with s2 & s1 is jordan_holder implies s2 is jordan_holder
proof
  assume A1: s1 is_equivalent_with s2;
  assume A2: s1 is jordan_holder;
  per cases;
  suppose A3: len s1<=0+1;
    per cases by A3,NAT_1:26;
    suppose A4: len s1=0; then
      A5: s1={} by FINSEQ_1:25;
      len s2=0 by A4,A1,Def35;
      hence s2 is jordan_holder by A2,A5,FINSEQ_1:25;
    end;
    suppose A6: len s1=1; then
      A7: s1.1=(1).G by Def31;
      A8: len s2=1 by A6,A1,Def35;
      s1 = <* s1.1 *> by A6,FINSEQ_1:57
      .= <* s2.1 *> by A7,A8,Def31
      .= s2 by A8,FINSEQ_1:57;
      hence s2 is jordan_holder by A2;
    end;
  end;
  suppose A9: len s1>1; then
    A10: s1 is not empty by FINSEQ_1:25;
    A11: len s2>1 by A9,A1,Def35; then
    s2 is not empty by FINSEQ_1:25; then
    consider p be Permutation of dom the_series_of_quotients_of s1 such that
    A12: the_series_of_quotients_of s1,the_series_of_quotients_of s2
        are_equivalent_under p,O by A10,A1,Th108;
    set f1 = the_series_of_quotients_of s1;
    set f2 = the_series_of_quotients_of s2;
    A13: len f1 = len f2 &
    for H1,H2 being GroupWithOperators of O,i,j
      st i in dom f1 & j=p".i & H1=f1.i & H2=f2.j
      holds H1,H2 are_isomorphic by A12,Def37;
    now
      let j;
      assume A14: j in dom f2;
      set i=p.j;
      set H1=f1.i;
      set H2=f2.j;
      A15: dom f1 = Seg len f1 by FINSEQ_1:def 3
      .= Seg len f2 by A12,Def37
      .= dom f2 by FINSEQ_1:def 3;
      A16: p.j in dom f2 by A15,A14,FUNCT_2:7;
      A17: p.j in Seg len f2 by A16,FINSEQ_1:def 3;
      reconsider i as Element of NAT by A16;
      A18: i in dom f1 by A13,A17,FINSEQ_1:def 3; then
      A19: f1.i in rng f1 by FUNCT_1:12;
      f2.j in rng f2 by A14,FUNCT_1:12; then
      reconsider H1,H2 as strict GroupWithOperators of O by A19,Th102;
      i in dom f1 & j=p".i & H1=f1.i & H2=f2.j
        by A15,A14,FUNCT_2:32,FUNCT_2:7; then
      A20: H1,H2 are_isomorphic by A12,Def37;
      H1 is strict simple GroupWithOperators of O by A18,Th110,A9,A2;
      hence f2.j is strict simple GroupWithOperators of O by A20,Th82;
    end;
    hence s2 is jordan_holder by A11,Th110;
  end;
end;

Lm45: for k,l being natural number
  st k in Seg l & len s1>1 & len s2>1 & l=(len s1-1)*(len s2-1)+1
  holds k=(len s1-1)*(len s2-1)+1 or
   (ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1)
proof
  let k,l be natural number;
  assume A1: k in Seg l; then
  A2: 1<=k & k<=l by FINSEQ_1:3;
  assume A3: len s1>1 & len s2>1 & l=(len s1-1)*(len s2-1)+1;
  assume not k = (len s1-1)*(len s2-1)+1; then
  A4: k<l by A3,XXREAL_0:1,A2;
  set l' = len s1-1;
  len s1-1>1-1 by A3,XREAL_1:11; then
  reconsider l' as Element of NAT by INT_1:16;
  set l'' = len s2-1;
  A5: len s2-1>1-1 by A3,XREAL_1:11; then
  reconsider l'' as Element of NAT by INT_1:16;
  len s1+1>1+1 & len s2+1>1+1 by A3,XREAL_1:8; then
  len s1>=2 & len s2>=2 by NAT_1:13; then
  A6: len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11;
  A7: k = (k div l'')*l'' + (k mod l'') by A5,NAT_D:2;
  per cases;
  suppose A8: k mod l''=0;
    set j=l'';
    set i=k div l'';
    take i,j;
    thus k = (i-1)*(len s2-1)+j by A8,A7;
    i<>0 by A1,A8,A7,FINSEQ_1:3; then
    i>0; then
    i+1>0+1 by XREAL_1:8;
    hence 1<=i by NAT_1:13;
    i*l''<=(len s1-1)*l'' by A8,A7,A3,A4,INT_1:20; then
    i*l''/l''<=(len s1-1)*l''/l'' by XREAL_1:74; then
    i<=(len s1-1)*l''/l'' by XCMPLX_1:90,A5;
    hence i<=len s1-1 by XCMPLX_1:90,A5;
    thus 1<=j & j<=len s2-1 by A6;
  end;
  suppose A9: k mod l''<>0;
    set j=k mod l'';
    set i=k div l''+1;
    take i,j;
    thus k = (i-1)*(len s2-1)+j by A5,NAT_D:2;
    0+1<=k div l''+1 by XREAL_1:8;
    hence 1<=i;
    A10: k<>l'*l'' by A9,NAT_D:13;
    k+1<=l by INT_1:20,A4; then
    k+1-1<=l-1 by XREAL_1:11; then
    A11: k<(len s1-1)*l'' by A3,A10,XXREAL_0:1;
    k mod l'' + l''*(k div l'')>=0+l''*(k div l'') by XREAL_1:8; then
    (k div l'')*l''<=k by NAT_D:2,A5; then
    (k div l'')*l''<(len s1-1)*l'' by XXREAL_0:2,A11; then
    (k div l'')*l''/l''<(len s1-1)*l''/l'' by XREAL_1:76,A5; then
    k div l''<(len s1-1)*l''/l'' by XCMPLX_1:90,A5; then
    k div l''<len s1-1 by XCMPLX_1:90,A5;
    hence i<=len s1-1 by INT_1:20;
    j>0 by A9; then
    j+1>0+1 by XREAL_1:8;
    hence 1<=j by NAT_1:13;
    thus j<=len s2-1 by A5,NAT_D:1;
  end;
end;

Lm46: for i1,j1,i2,j2 being natural number, s1,s2
  st len s1>1 & len s2>1 &
      (i1-1)*(len s2-1)+j1=(i2-1)*(len s2-1)+j2 &
      1<=i1 & i1<=len s1-1 & 1<=j1 & j1<=len s2-1 &
      1<=i2 & i2<=len s1-1 & 1<=j2 & j2<=len s2-1
  holds j1=j2 & i1=i2
proof
  let i1,j1,i2,j2 be natural number;
  let s1,s2;
  assume A1: len s1>1 & len s2>1;
  assume A2: (i1-1)*(len s2-1)+j1=(i2-1)*(len s2-1)+j2;
  assume A3: 1<=i1 & i1<=len s1-1 & 1<=j1 & j1<=len s2-1;
  assume A4: 1<=i2 & i2<=len s1-1 & 1<=j2 & j2<=len s2-1;
  set l' = len s1-1;
  len s1-1>1-1 by A1,XREAL_1:11; then
  reconsider l' as Element of NAT by INT_1:16;
  set l'' = len s2-1;
  A5: len s2-1>1-1 by A1,XREAL_1:11; then
  reconsider l'' as Element of NAT by INT_1:16;
  set i1' = i1-1;
  i1-1>=1-1 by A3,XREAL_1:11; then
  reconsider i1' as Element of NAT by INT_1:16;
  set i2' = i2-1;
  i2-1>=1-1 by A4,XREAL_1:11; then
  reconsider i2' as Element of NAT by INT_1:16;
  A6: j1 mod l'' =  (i1'*l''+j1) mod l'' by NAT_D:21
  .= (i2'*l''+j2) mod l'' by A2
  .= j2 mod l'' by NAT_D:21;
  A7: j1=j2
  proof
    per cases;
    suppose A8: j1=l'';
      assume j2<>j1; then
      j2<l'' by A8,A4,XXREAL_0:1; then
      j2 = j1 mod l'' by A6,NAT_D:24;
      hence contradiction by A4,A8,NAT_D:25;
    end;
    suppose j1<>l''; then
      j1<l'' by A3,XXREAL_0:1; then
      A9: j1 = j2 mod l'' by A6,NAT_D:24;
      per cases;
      suppose j2=l'';
        hence thesis by A3,A9,NAT_D:25;
      end;
      suppose j2<>l''; then
        j2<l'' by A4,XXREAL_0:1;
        hence thesis by A9,NAT_D:24;
      end;
    end;
  end;
  hence j1=j2;
  A10: l''/l''=1 by A5,XCMPLX_1:60;
  i1'*(l''/l'')=i2'*l''/l'' by A2,A7,XCMPLX_1:75; then
  i1'*1=i2'*1 by A10,XCMPLX_1:75;
  hence thesis;
end;

Lm47: for k being integer number, i,j being natural number, s1,s2
  st len s1>1 & len s2>1 & k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
      & 1<=j & j<=len s2-1
  holds 1<=k & k<=(len s1-1)*(len s2-1)
proof
  let k be integer number;
  let i,j be natural number;
  let s1,s2;
  assume A1: len s1>1 & len s2>1;
  assume A2: k=(i-1)*(len s2-1)+j;
  assume A3: 1<=i & i<=len s1-1;
  assume A4: 1<=j & j<=len s2-1;
  set l'=len s1-1;
  set l''=len s2-1;
  A5: len s1-1>1-1 & len s2-1>1-1 by A1,XREAL_1:11;
  1-1<=i-1 by A3,XREAL_1:11; then
  0*(len s2-1)<=(i-1)*(len s2-1) by A5,XREAL_1:66; then
  A6: 0+1<=(i-1)*(len s2-1)+j by A4,XREAL_1:9;
  i-1<=l'-1 by A3,XREAL_1:11; then
  (i-1)*l''<=(l'-1)*l'' by A5,XREAL_1:66; then
  k<=l'*l''-1*l''+l'' by A2,A4,XREAL_1:9;
  hence 1<=k & k<=(len s1-1)*(len s2-1) by A6,A2;
end;

begin :: The Schreier Refinement Theorem

definition
  let O,G,s1,s2;
  assume A1: len s1>1 & len s2>1;
  func the_schreier_series_of(s1,s2) -> CompositionSeries of G means :Def38:
    for k,i,j being natural number, H1,H2,H3 being StableSubgroup of G holds
     (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
          H1=s1.(i+1) & H2=s1.i & H3=s2.j implies it.k = H1"\/"(H2/\H3)) &
     (k=(len s1-1)*(len s2-1) + 1 implies it.k = (1).G) &
    len it = (len s1-1)*(len s2-1) + 1;
  existence
  proof
    defpred P[set,set] means
    for i,j being natural number, H1,H2,H3 being StableSubgroup of G holds
     ($1=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
          H1=s1.(i+1) & H2=s1.i & H3=s2.j implies $2 = H1"\/"(H2/\H3)) &
     ($1=(len s1-1)*(len s2-1) + 1 implies $2 = (1).G);
    len s1+1>1+1 & len s2+1>1+1 by A1,XREAL_1:8; then
    len s1>=2 & len s2>=2 by NAT_1:13; then
    A2: len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11; then
    (len s1-1)*(len s2-1)>=0*(len s2-1) by XREAL_1:66; then
    A3: (len s1-1)*(len s2-1)+1>=0+1 by XREAL_1:8;
    A4: for i,s1,H st 1<=i & i<=len s1-1 & H=s1.i
    holds s1.(i+1) is normal StableSubgroup of H
    proof
      let i,s1,H;
      assume A5: 1<=i & i<=len s1-1;
      assume A6: H=s1.i;
      A7: 0+i<=1+i by XREAL_1:8; then
      A8: 1<=i+1 by A5,XXREAL_0:2;
      A9: i+1<=len s1-1+1 by A5,XREAL_1:8; then
      i+1 in Seg len s1 by A8,FINSEQ_1:3; then
      A10: i+1 in dom s1 by FINSEQ_1:def 3;
      i<=len s1 by A7,A9,XXREAL_0:2; then
      i in Seg len s1 by A5,FINSEQ_1:3; then
      A11: i in dom s1 by FINSEQ_1:def 3;
      reconsider H1=s1.i,H2=s1.(i+1) as StableSubgroup of G by Th111,A5;
      H2 is normal StableSubgroup of H1 by A10,A11,Def31;
      hence s1.(i+1) is normal StableSubgroup of H by A6;
    end;
    reconsider l=(len s1-1)*(len s2-1)+1 as Element of NAT by A3,INT_1:16;
    len s1-1>1-1 by A1,XREAL_1:11; then
    reconsider l' = len s1-1 as Element of NAT by INT_1:16;
    len s2-1>1-1 by A1,XREAL_1:11; then
    reconsider l'' = len s2-1 as Element of NAT by INT_1:16;
    A12: for k being Element of NAT ,y1,y2 being set
      st k in Seg l & P[k,y1] & P[k,y2] holds y1=y2
    proof
      let k be Element of NAT;
      let y1,y2 be set;
      assume A13: k in Seg l;
      assume A14: P[k,y1];
      assume A15: P[k,y2];
      per cases by A1,Lm45,A13;
      suppose ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1; then
        consider i,j such that
        A16: k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
        reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G
          by Th111,A16;
        y1=H1"\/"(H2/\H3) & y2=H1"\/"(H2/\H3) by A16,A14,A15;
        hence thesis;
      end;
      suppose k=(len s1-1)*(len s2-1)+1;
        hence thesis by A14,A15;
      end;
    end;
    A17: for k being Element of NAT st k=(len s1-1)*(len s2-1)+1 holds
    not ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1
    proof
      let k be Element of NAT;
      assume A18: k=(len s1-1)*(len s2-1)+1;
      assume ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j
        & j<=len s2-1; then
      consider i,j such that
      A19: k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
      set i' = i-1;
      i-1>=1-1 by A19,XREAL_1:11; then
      reconsider i' as Element of NAT by INT_1:16;
      A20: 1 mod l'' = (l'*l''+1) mod l'' by NAT_D:21
      .= (i'*l''+j) mod l'' by A18,A19
      .= j mod l'' by NAT_D:21;
      A21: j=1
      proof
        per cases;
        suppose A22: j=l'';
          assume A23: j<>1;
          1<l'' by A22,A23,XXREAL_0:1,A19; then
          1 = j mod l'' by A20,NAT_D:24;
          hence contradiction by A22,NAT_D:25;
        end;
        suppose j<>l''; then
          j<l'' by A19,XXREAL_0:1; then
          A24: j mod l'' = j by NAT_D:24;
          per cases;
          suppose 1=l'';
            hence thesis by A19,XXREAL_0:1;
          end;
          suppose A25: 1<>l'';
            1<=l'' by A19,XXREAL_0:2; then
            1<l'' by A25,XXREAL_0:1;
            hence thesis by A24,A20,NAT_D:24;
          end;
        end;
      end;
      A26: l''/l''=1 by A19,XCMPLX_1:60;
      l'*(l''/l'')=i'*l''/l'' by A21,A18,A19,XCMPLX_1:75; then
      A27: l'*1=i'*1 by A26,XCMPLX_1:75;
      -1+i<0+i by XREAL_1:8;
      hence contradiction by A19,A27;
    end;
    A28: for k being Element of NAT st k in Seg l ex x st P[k,x]
    proof
      let k be Element of NAT;
      assume A29: k in Seg l;
      per cases by A1,Lm45,A29;
      suppose A30: ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1;
        consider i,j such that
        A31: k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 &
          1<=j & j<=len s2-1 by A30;
        reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G
          by Th111,A31;
        set x=H1"\/"(H2/\H3);
        take x;
        now
          let i1,j1 be natural number;
          let H1,H2,H3 be StableSubgroup of G;
          thus k=(i1-1)*(len s2-1)+j1 & 1<=i1 & i1<=len s1-1 & 1<=j1
            & j1<=len s2-1 & H1=s1.(i1+1) & H2=s1.i1 & H3=s2.j1
              implies x = H1"\/"(H2/\H3)
          proof
            assume A32: k=(i1-1)*(len s2-1)+j1 & 1<=i1 & i1<=len s1-1 & 1<=j1
              & j1<=len s2-1;
            assume A33: H1=s1.(i1+1) & H2=s1.i1 & H3=s2.j1;
            j=j1 & i=i1 by A1,Lm46,A32,A31;
            hence x = H1"\/"(H2/\H3) by A33;
          end;
          assume k=(len s1-1)*(len s2-1) + 1;
          hence x = (1).G by A30,A17;
        end;
        hence P[k,x];
      end;
      suppose A34: k=(len s1-1)*(len s2-1)+1;
        set x=(1).G;
        take x;
        thus P[k,x] by A34,A17;
      end;
    end;
    A35: for k,i,j being natural number
    st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
      & 1<=j & j<=len s2-1 holds k in Seg l
    proof
      let k,i,j be natural number;
      assume A36: k=(i-1)*(len s2-1)+j;
      assume A37: 1<=i & i<=len s1-1;
      assume A38: 1<=j & j<=len s2-1;
      1-1<=i-1 by A37,XREAL_1:11; then
      0*(len s2-1)<=(i-1)*(len s2-1) by A2,XREAL_1:66; then
      A39: 0+1<=(i-1)*(len s2-1)+j by A38,XREAL_1:9;
      A40: 0+l'*l''<=1+l'*l'' by XREAL_1:8;
      i-1<=l'-1 by A37,XREAL_1:11; then
      (i-1)*l''<=(l'-1)*l'' by XREAL_1:66; then
      k<=l'*l''-1*l''+l'' by A36,A38,XREAL_1:9; then
      k<=(len s1-1)*(len s2-1)+1 by A40,XXREAL_0:2;
      hence k in Seg l by FINSEQ_1:3,A39,A36;
    end;
    consider f be FinSequence such that
      A41: dom f = Seg l & for k being Element of NAT
        st k in Seg l holds P[k,f.k] from FINSEQ_1:sch 1(A12,A28);
    for k being Element of NAT st k in dom f
      holds f.k in the_stable_subgroups_of G
    proof
      let k be Element of NAT;
      assume A42: k in dom f;
      A43: for i,j being natural number, H1,H2,H3 being StableSubgroup of G
        holds (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
          H1=s1.(i+1) & H2=s1.i & H3=s2.j implies f.k = H1"\/"(H2/\H3)) &
          (k=(len s1-1)*(len s2-1) + 1 implies f.k = (1).G) by A41,A42;
      per cases by A1,Lm45,A42,A41;
      suppose A44: ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1;
        consider i,j such that
        A45: k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 &
          1<=j & j<=len s2-1 by A44;
        reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G
          by Th111,A45;
        f.k = H1"\/"(H2/\H3) by A42,A41,A45;
        hence f.k in the_stable_subgroups_of G by Def11;
      end;
      suppose A46: k=(len s1-1)*(len s2-1)+1;
        thus f.k in the_stable_subgroups_of G by Def11,A46,A43;
      end;
    end; then
    reconsider f as FinSequence of the_stable_subgroups_of G by FINSEQ_2:14;
    A47: 1 in Seg l & l in Seg l by A3; then
    A48: P[1,f.1] & P[l,f.l] by A41;
    len s1-1>1-1 & len s2-1>1-1 by A1,XREAL_1:11; then
    (len s1-1)*(len s2-1)>0*(len s2-1) by XREAL_1:70; then
    1<>l; then
    consider i,j such that
    A49: 1=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
      & 1<=j & j<=len s2-1 by A1,Lm45,A47;
    reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G by Th111,A49;
    A50: f.1 = H1"\/"(H2/\H3) by A47,A41,A49;
    set i' = i-1;
    i-1>=1-1 by A49,XREAL_1:11; then
    reconsider i' as Element of NAT by INT_1:16;
    1 mod l''=(i'*l''+j) mod l'' by A49; then
    A51: 1 mod l''=j mod l'' by NAT_D:21;
    A52: j=1
    proof
      per cases;
      suppose l''=1;
        hence thesis by XXREAL_0:1,A49;
      end;
      suppose l''<>1; then
        1<l'' by A2,XXREAL_0:1; then
        A53: 1=j mod l'' by A51,NAT_D:14; then
        j<>l'' by NAT_D:25; then
        l''>j by A49,XXREAL_0:1;
        hence thesis by A53,NAT_D:24;
      end;
    end; then
    A54: i'*l''/l''=0/l'' by A49;
    len s2-1>1-1 by A1,XREAL_1:11; then
    A55: l''/l''=1 by XCMPLX_1:60;
    i'*1=0 by A55,A54,XCMPLX_1:75; then
    A56: H2=(Omega).G by Def31;
    H3=(Omega).G by A52,Def31; then
    A57: f.1 = H1"\/"(Omega).G by A50,A56,Th19;
    A58: f.1=(Omega).G by A57,Th34;
    A59: f.(len f)=(1).G by A48,A41,FINSEQ_1:def 3;
    for k being natural number st k in dom f & k+1 in dom f
    for H1,H2 st H1=f.k & H2=f.(k+1) holds H2 is normal StableSubgroup of H1
    proof
      let k be natural number;
      assume A60: k in dom f;
      assume A61: k+1 in dom f;
      k+1<=l by FINSEQ_1:3,A61,A41; then
      k<>l by NAT_1:13; then
      consider i,j such that
      A62: k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1 by A60,A41,A1,Lm45;
      let H1,H2;
      assume A63: H1 = f.k;
      assume A64: H2 = f.(k+1);
      reconsider H1'=s1.(i+1),H2'=s1.i,H3'=s2.j as strict StableSubgroup of G
        by Th111,A62;
      A65: H1' is normal StableSubgroup of H2' by A4,A62;
      A66: f.k = H1'"\/"(H2'/\H3') by A62,A41,A60;
      set k'=k+1;
      per cases;
      suppose j<>len s2-1; then
        A67: j<len s2-1 by A62,XXREAL_0:1;
        reconsider j'=j+1 as natural number;
        reconsider H3'' = s2.j' as strict StableSubgroup of G by Th111,A62;
        A68: j'<=len s2-1 by A67,INT_1:20;
        0+j<=1+j by XREAL_1:8; then
        A69: 1<=j' by XXREAL_0:2,A62;
        k'=(i-1)*(len s2-1)+j' by A62; then
        A70: H2 = H1'"\/"(H2'/\H3'') by A64,A62,A61,A41,A69,A68;
        H3'' is normal StableSubgroup of H3' by A4,A62;
        hence H2 is normal StableSubgroup of H1 by A66,A70,A63,Th92,A65;
      end;
      suppose A71: j=len s2-1;
        per cases;
        suppose i<>len s1-1; then
          i<l' by A62,XXREAL_0:1; then
          A72: i+1<=l' by NAT_1:13;
          set i'=i+1;
          set j'=1;
          A73: k'=(i'-1)*(len s2-1)+j' by A62,A71;
          1+1<=i+1 by A62,XREAL_1:8; then
          A74: 1<=i' by XXREAL_0:2;
          len s2-1>1-1 by A1,XREAL_1:11; then
          A75: l''>=0+1 by NAT_1:13;
          reconsider H1''=s1.(i'+1),H2''=s1.i',H3''=s2.j'
            as strict StableSubgroup of G by Th111,A74,A72,A75;
          A76: H2 = H1''"\/"(H2''/\H3'') by A64,A74,A73,A72,A75,A61,A41;
          A77: H3'' = (Omega).G by Def31;
          now let x be set;
            assume A78: x in the carrier of H2'';
            H2'' is Subgroup of G by Def7; then
            the carrier of H2'' c= the carrier of G by GROUP_2:def 5;
            hence x in the carrier of (Omega).G by A78;
          end; then
          the carrier of H2'' c= the carrier of (Omega).G by TARSKI:def 3; then
          the carrier of H2'' = (the carrier of H2'')
            /\ (the carrier of (Omega).G) by XBOOLE_1:28; then
          A79: H2 = H1''"\/"H2'' by A76,A77,Th18;
          A80: 0+i'<=1+i' by XREAL_1:8; then
          A81: 1<=i'+1 by A74,XXREAL_0:2;
          A82: i'+1<=len s1-1+1 by A72,XREAL_1:8; then
          i'+1 in Seg len s1 by A81,FINSEQ_1:3; then
          A83: i'+1 in dom s1 by FINSEQ_1:def 3;
          i'<=len s1 by A80,A82,XXREAL_0:2; then
          i' in Seg len s1 by A74,FINSEQ_1:3; then
          i' in dom s1 by FINSEQ_1:def 3; then
          H1'' is normal StableSubgroup of H2'' by A83,Def31; then
          A84: H2 = H1' by A79,Th36;
          A85: H2'/\H3' is StableSubgroup of H2' by Lm34;
          A86: H1 is StableSubgroup of H2'
            by A65,A66,A63,A85,Th37; then
          A87: H1 is Subgroup of H2' by Def7;
          H1' is StableSubgroup of H1 by A66,A63,Th35; then
          H1' is Subgroup of H1 by Def7; then
          A88: the carrier of H1' c= the carrier of H1 by GROUP_2:def 5;
          now
            let H' be strict Subgroup of H1;
            assume A89: H' = the HGrStr of H2;
            now
              let a be Element of H1;
              reconsider a'=a as Element of H2' by A86,Th2;
              now let x be set;
                assume x in a * H'; then
                consider b be Element of H1 such that
                A90: x = a * b & b in H' by GROUP_2:125;
                set b'=b;
                reconsider H1s'=the HGrStr of H1' as normal Subgroup of H2'
                  by A65,Lm7;
                A91: a' * H1s' c= H1s' * a' by GROUP_3:141;
                A92: H1 is Subgroup of H2' by A86,Def7; then
                reconsider b' as Element of H2' by GROUP_2:51;
                x = a' * b' by A90,A92,GROUP_2:52; then
                x in a' * H1s' by GROUP_2:125,A84,A89,A90; then
                consider b'' be Element of H2' such that
                A93: x = b'' * a' & b'' in H1s' by A91,GROUP_2:126;
                b'' in the carrier of H1' by A93,STRUCT_0:def 5; then
                reconsider b'' as Element of H1 by A88;
                x = b'' * a by A93,A87,GROUP_2:52;
                hence x in H' * a by GROUP_2:126,A89,A84,A93;
              end;
              hence a * H' c= H' * a by TARSKI:def 3;
            end;
            hence H' is normal by GROUP_3:141;
          end;
          hence H2 is normal StableSubgroup of H1 by A66,A63,Th35,A84,Def10;
        end;
        suppose i=len s1-1; then
          H2 = (1).G by A64,A61,A41,A62,A71
          .= (1).H1 by Th15;
          hence H2 is normal StableSubgroup of H1;
        end;
      end;
    end; then
    reconsider f as CompositionSeries of G by A58,A59,Def31;
    take f;
    let k,i,j be natural number,
        H1,H2,H3 be StableSubgroup of G;
    now assume A94: k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
      & 1<=j & j<=len s2-1 & H1=s1.(i+1) & H2=s1.i & H3=s2.j; then
      k in Seg l by A35;
      hence f.k = H1"\/"(H2/\H3) by A94,A41;
    end;
    hence (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
      H1=s1.(i+1) & H2=s1.i & H3=s2.j implies f.k = H1"\/"(H2/\H3));
    now assume A95: k=(len s1-1)*(len s2-1) + 1; then
      k in Seg l by A3;
      hence f.k = (1).G by A41,A95;
    end;
    hence thesis by A41,FINSEQ_1:def 3;
  end;
  uniqueness
  proof
    let f1,f2 be CompositionSeries of G;
    assume A96: for k,i,j being natural number,
      H1,H2,H3 being StableSubgroup of G
      holds (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
      H1=s1.(i+1) & H2=s1.i & H3=s2.j implies f1.k = H1"\/"(H2/\H3)) &
      (k=(len s1-1)*(len s2-1) + 1 implies f1.k = (1).G) &
      len f1 = (len s1-1)*(len s2-1) + 1;
    assume A97: for k,i,j being natural number,
      H1,H2,H3 being StableSubgroup of G
      holds (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
      H1=s1.(i+1) & H2=s1.i & H3=s2.j implies f2.k = H1"\/"(H2/\H3)) &
      (k=(len s1-1)*(len s2-1) + 1 implies f2.k = (1).G) &
      len f2 = (len s1-1)*(len s2-1) + 1;
    A98: dom f1 = Seg len f2 by A96,A97,FINSEQ_1:def 3
    .= dom f2 by FINSEQ_1:def 3;
    now
      let k be Element of NAT;
      assume A99: k in dom f1;
      set l=len f1;
      A100: k in Seg l by A99,FINSEQ_1:def 3;
      per cases by A96,A1,Lm45,A100;
      suppose ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1; then
        consider i,j such that
        A101: k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
        reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G
          by Th111,A101;
        f1.k=H1"\/"(H2/\H3) & f2.k=H1"\/"(H2/\H3) by A101,A96,A97;
        hence f1.k = f2.k;
      end;
      suppose k=(len s1-1)*(len s2-1)+1; then
        f1.k=(1).G & f2.k=(1).G by A96,A97;
        hence f1.k = f2.k;
      end;
    end;
    hence f1=f2 by A98,FINSEQ_1:17;
  end;
end;

theorem Th116:
  len s1>1 & len s2>1 implies the_schreier_series_of(s1,s2) is_finer_than s1
proof
  assume A1: len s1>1 & len s2>1;
  now
    set g={[k,(k-1)*(len s2-1)+1] where k is Element of NAT:1<=k & k<=len s1};
    now
      let x;
      assume x in g; then
      consider k be Element of NAT such that
      A2: [k,(k-1)*(len s2-1)+1] = x & 1<=k & k<=len s1;
      set y=k;
      set z=(k-1)*(len s2-1)+1;
      reconsider y,z as set;
      take y,z;
      thus x = [y,z] by A2;
    end; then
    reconsider g as Relation by RELAT_1:def 1;
    now let x;
      assume x in dom g; then
      consider y such that
      A3: [x,y] in g by RELAT_1:def 4;
      consider k be Element of NAT such that
      A4: [k,(k-1)*(len s2-1)+1] = [x,y] & 1<=k & k<=len s1 by A3;
      k=x by A4,ZFMISC_1:33;
      hence x in NAT;
    end; then
    A5: dom g c= NAT by TARSKI:def 3;
    A6: now let y;
      assume y in rng g; then
      consider x such that
      A7: [x,y] in g by RELAT_1:def 5;
      consider k be Element of NAT such that
      A8: [k,(k-1)*(len s2-1)+1] = [x,y] & 1<=k & k<=len s1 by A7;
      (k-1)*(len s2-1)+1=y by A8,ZFMISC_1:33;
      hence y in REAL by XREAL_0:def 1;
    end;
    now
      let x,y1,y2 be set;
      assume [x,y1] in g; then
      consider k be Element of NAT such that
      A9: [k,(k-1)*(len s2-1)+1] = [x,y1] & 1<=k & k<=len s1;
      assume [x,y2] in g; then
      consider k' be Element of NAT such that
      A10: [k',(k'-1)*(len s2-1)+1] = [x,y2] & 1<=k' & k'<=len s1;
      A11: k=x & (k-1)*(len s2-1)+1=y1 & 1<=k & k<=len s1 by A9,ZFMISC_1:33;
      A12: k'=x & (k'-1)*(len s2-1)+1=y2 & 1<=k' & k'<=len s1 by A10,ZFMISC_1:
33;
      thus y1=y2 by A11,A12;
    end; then
    reconsider g as Function by FUNCT_1:def 1;
    reconsider f=g as PartFunc of dom g, rng g by PARTFUN1:24;
    dom g c= REAL & rng g c= REAL by A5,A6,XBOOLE_1:1,TARSKI:def 3; then
    reconsider f as PartFunc of REAL,REAL by PARTFUN1:32;
    set l = (len s1-1)*(len s2-1) + 1;
    len s1+1>1+1 & len s2+1>1+1 by A1,XREAL_1:8; then
    len s1>=2 & len s2>=2 by NAT_1:13; then
    A13: len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11; then
    (len s1-1)*(len s2-1)>=0*(len s2-1) by XREAL_1:66; then
    (len s1-1)*(len s2-1)+1>=0+1 by XREAL_1:8; then
    (len s1-1)*(len s2-1)+1 in NAT by INT_1:16; then
    reconsider l as natural number by ORDINAL1:def 13;
    set X = Seg len s1;
    now let x;
      assume A14: x in X;
      reconsider k=x as Element of NAT by A14;
      set y=(k-1)*(len s2-1)+1;
      [k,(k-1)*(len s2-1)+1] = [x,y] & 1<=k & k<=len s1 by A14,FINSEQ_1:3; then
      [x,y] in f;
      hence x in dom f by RELAT_1:def 4;
    end; then
    A15: X c= dom f by TARSKI:def 3;
    now
      let r1,r2 be Element of REAL;
      assume r1 in X /\ dom f; then
      r1 in dom f by XBOOLE_0:def 3; then
      [r1,f.r1] in f by FUNCT_1:8; then
      consider k' be Element of NAT such that
      A16: [k',(k'-1)*(len s2-1)+1]=[r1,f.r1] & 1<=k' & k'<=len s1;
      assume r2 in X /\ dom f; then
      r2 in dom f by XBOOLE_0:def 3; then
      [r2,f.r2] in f by FUNCT_1:8; then
      consider k'' be Element of NAT such that
      A17: [k'',(k''-1)*(len s2-1)+1]=[r2,f.r2] & 1<=k'' & k''<=len s1;
      assume A18: r1<r2;
      A19: k'=r1 & (k'-1)*(len s2-1)+1=f.r1 by A16,ZFMISC_1:33;
      A20: k''=r2 & (k''-1)*(len s2-1)+1=f.r2 by A17,ZFMISC_1:33; then
      A21: k'-1<k''-1 by A18,A19,XREAL_1:11;
      (k'-1)*(len s2-1)<(k''-1)*(len s2-1) by A13,A21,XREAL_1:70;
      hence f.r1 < f.r2 by A19,A20,XREAL_1:8;
    end; then
    A22: f is_increasing_on X  by RFUNCT_2:def 2;
    now let y be set;
      assume y in f.:X; then
      consider x such that
      A23: [x,y] in g & x in X by RELAT_1:def 13;
      consider k be Element of NAT such that
      A24: [k,(k-1)*(len s2-1)+1]=[x,y] & 1<=k & k<=len s1 by A23;
      A25: (k-1)*(len s2-1)+1=y by A24,ZFMISC_1:33;
      reconsider y'=y as integer number by A24,ZFMISC_1:33;
      k-1>=1-1 by A24,XREAL_1:11; then
      (k-1)*(len s2-1)>=0*(len s2-1) by A13,XREAL_1:66; then
      A26: (k-1)*(len s2-1)+1>=0+1 by XREAL_1:8; then
      A27: y' in NAT by A25,INT_1:16;
      not y in {0} by A25,A26,TARSKI:def 1;
      hence y in NAT \ {0} by A27,XBOOLE_0:def 4;
    end; then
    A28: f.:X c= NAT \ {0} by TARSKI:def 3;
    set fX = f.:X;
    take fX;
    set l=(len s1-1)*(len s2-1) + 1;
    len s1+1>1+1 & len s2+1>1+1 by A1,XREAL_1:8; then
    len s1>=2 & len s2>=2 by NAT_1:13; then
    len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11; then
    (len s1-1)*(len s2-1)>=0*(len s2-1) by XREAL_1:66; then
    (len s1-1)*(len s2-1)+1>=0+1 by XREAL_1:8; then
    reconsider l as Element of NAT by INT_1:16;
    now let y be set;
      assume y in fX; then
      consider x such that
      A29: [x,y] in g & x in X by RELAT_1:def 13;
      consider k be Element of NAT such that
      A30: [k,(k-1)*(len s2-1)+1]=[x,y] & 1<=k & k<=len s1 by A29;
      A31: (k-1)*(len s2-1)+1=y by A30,ZFMISC_1:33;
      reconsider y'=y as integer number by A30,ZFMISC_1:33;
      k-1>=1-1 by A30,XREAL_1:11; then
      (k-1)*(len s2-1)>=0*(len s2-1) by A13,XREAL_1:66; then
      A32: (k-1)*(len s2-1)+1>=0+1 by XREAL_1:8; then
      A33: y'>=1 by A30,ZFMISC_1:33;
      y'>0 by A32,A30,ZFMISC_1:33; then
      reconsider y' as Element of NAT by INT_1:16;
      k-1<=len s1-1 by A30,XREAL_1:11; then
      (k-1)*(len s2-1)<=(len s1-1)*(len s2-1) by A13,XREAL_1:66; then
      y'<=l by A31,XREAL_1:8;
      hence y in Seg l by A33;
    end; then
    A34: fX c= Seg l by TARSKI:def 3;
    A35: len the_schreier_series_of(s1,s2) = l by A1,Def38; then
    A36: dom the_schreier_series_of(s1,s2) = Seg l by FINSEQ_1:def 3;
    thus fX c= dom the_schreier_series_of(s1,s2) by A35,A34,FINSEQ_1:def 3;
    set t=the_schreier_series_of(s1,s2);
    A37: dom s1 c= dom f by A15,FINSEQ_1:def 3;
    now let x;
      assume x in dom f; then
      consider y such that
      A38: [x,y] in f by RELAT_1:def 4;
      consider k be Element of NAT such that
      A39: [k,(k-1)*(len s2-1)+1] = [x,y] & 1<=k & k<=len s1 by A38;
      k in Seg len s1 & k=x by A39,ZFMISC_1:33;
      hence x in dom s1 by FINSEQ_1:def 3;
    end; then
    dom f c= dom s1 by TARSKI:def 3; then
    A40: dom s1 = dom f by A37,XBOOLE_0:def 10;
    X =  dom f by A40,FINSEQ_1:def 3; then
    A41: rng f c= Seg l by A34,RELAT_1:146;
    A42: dom s1 = dom(t*f) by A40,A41,A36,RELAT_1:46;
    A43: now
      let x be set;
      assume A44: x in dom s1; then
      A45: x in dom(t*f) by A40,A41,A36,RELAT_1:46;
      [x,f.x] in f by A44,FUNCT_1:def 4,A37; then
      consider i be Element of NAT such that
      A46: [i,(i-1)*(len s2-1)+1] = [x,f.x] & 1<=i & i<=len s1;
      A47: i=x & (i-1)*(len s2-1)+1=f.x by A46,ZFMISC_1:33;
      set k=(i-1)*(len s2-1)+1;
      k in rng f by A47,A44,FUNCT_1:12,A37; then
      k in Seg l by A41; then
      reconsider k as Element of NAT;
      per cases;
      suppose A48: i=len s1;
        (t*f).x = t.(f.x) by A45,FUNCT_1:22
        .= t.k by A46,ZFMISC_1:33
        .= (1).G by A48,A1,Def38
        .= s1.(len s1) by Def31;
        hence s1.x = (t*f).x by A48,A46,ZFMISC_1:33;
      end;
      suppose i<>len s1; then
        i<len s1 by XXREAL_0:1,A46; then
        A49: i+1<=len s1 by NAT_1:13; then
        A50: i+1-1<=len s1-1 by XREAL_1:11;
        len s2-1>1-1 by A1,XREAL_1:11; then
        A51: len s2-1>=0+1 by INT_1:20;
        A52: s2.1=(Omega).G by Def31; then
        reconsider H1=s1.(i+1),H2=s1.i,H3=s2.1 as strict StableSubgroup of G
          by Th111,A46,A50;
        now let x be set;
          assume A53: x in the carrier of H2;
          H2 is Subgroup of G by Def7; then
          the carrier of H2 c= the carrier of G by GROUP_2:def 5;
          hence x in the carrier of (Omega).G by A53;
        end; then
        the carrier of H2 c= the carrier of (Omega).G by TARSKI:def 3; then
        A54: the carrier of H2 =
          (the carrier of H2) /\ (the carrier of H3) by A52,XBOOLE_1:28;
        0+i<=1+i by XREAL_1:8; then
        A55: 1<=i+1 by A46,XXREAL_0:2;
        i+1 in Seg len s1 by A55,A49; then
        A56: i+1 in dom s1 by FINSEQ_1:def 3;
        i in Seg len s1 by A46; then
        i in dom s1 by FINSEQ_1:def 3; then
        A57: H1 is normal StableSubgroup of H2 by A56,Def31;
        (t*f).x = t.(f.x) by A45,FUNCT_1:22
        .= t.k by A46,ZFMISC_1:33
        .= H1"\/"(H2/\H3) by A46,A50,A51,A1,Def38
        .= H1"\/"H2 by A54,Th18
        .= H2 by A57,Th36;
        hence s1.x = (t*f).x by A46,ZFMISC_1:33;
      end;
    end;
    A58: the_schreier_series_of(s1,s2) * Sgm fX
    = the_schreier_series_of(s1,s2) * (f * Sgm X) by A15,A22,A28,Lm38
    .= (the_schreier_series_of(s1,s2) * f) * Sgm X by RELAT_1:55
    .= s1 * Sgm X by A43,A42,FUNCT_1:9
    .= s1 * idseq len s1 by FINSEQ_3:54
    .= s1 * id Seg len s1 by FINSEQ_2:def 1
    .= s1 * id dom s1 by FINSEQ_1:def 3;
    set R=s1;
    set dR=dom s1;
    set rR=rng s1;
    reconsider R as Relation of dR,rR by FUNCT_2:3;
    (id dR)*R = R by FUNCT_2:23;
    hence s1 = the_schreier_series_of(s1,s2) * Sgm fX by A58;
  end;
  hence the_schreier_series_of(s1,s2) is_finer_than s1 by Def32;
end;

theorem Th117:
  len s1>1 & len s2>1 implies
    the_schreier_series_of(s1,s2) is_equivalent_with
    the_schreier_series_of(s2,s1)
proof
  assume A1: len s1>1 & len s2>1;
  set s12 = the_schreier_series_of(s1,s2);
  set s21 = the_schreier_series_of(s2,s1);
  len s1-1>1-1 & len s2-1>1-1 by A1,XREAL_1:11; then
  A2: (len s1-1)*(len s2-1)>0*(len s2-1) by XREAL_1:70; then
  A3: (len s1-1)*(len s2-1)+1>0+1 by XREAL_1:8;
  A4: len s12 = (len s1-1)*(len s2-1) + 1 &
  len s21 = (len s1-1)*(len s2-1) + 1 by A1,Def38; then
  A5: s12 is not empty & s21 is not empty by A3,FINSEQ_1:25;
  now
    set p = {[(j-1)*(len s1-1)+i,(i-1)*(len s2-1)+j]
      where i,j is Element of NAT: 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1};
    set X = dom the_series_of_quotients_of s12;
    set f1=the_series_of_quotients_of s12;
    set f2=the_series_of_quotients_of s21;
    A6: len s12>1 & len s21>1 by A1,Def38,A3;
    A7: len f1 + 1= len s12 & len f2 + 1 = len s21 by Def36,A6;
    A8: len s12 = (len s1-1)*(len s2-1)+1 by A1,Def38;
    A9: X = Seg len f1 by FINSEQ_1:def 3;
    now
      let x;
      assume x in p; then
      consider i,j be Element of NAT such that
      A10: [(j-1)*(len s1-1)+i,(i-1)*(len s2-1)+j]=x &
      1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
      set y=(j-1)*(len s1-1)+i;
      set z=(i-1)*(len s2-1)+j;
      reconsider y,z as set;
      take y,z;
      thus x = [y,z] by A10;
    end; then
    reconsider p as Relation by RELAT_1:def 1;
    now
      let x,y1,y2 be set;
      assume [x,y1] in p; then
      consider i1,j1 be Element of NAT such that
      A11: [(j1-1)*(len s1-1)+i1,(i1-1)*(len s2-1)+j1]=[x,y1] &
      1<=i1 & i1<=len s1-1 & 1<=j1 & j1<=len s2-1;
      assume [x,y2] in p; then
      consider i2,j2 be Element of NAT such that
      A12: [(j2-1)*(len s1-1)+i2,(i2-1)*(len s2-1)+j2]=[x,y2] &
      1<=i2 & i2<=len s1-1 & 1<=j2 & j2<=len s2-1;
      (j1-1)*(len s1-1)+i1=x & (i1-1)*(len s2-1)+j1=y1 &
          (j2-1)*(len s1-1)+i2=x & (i2-1)*(len s2-1)+j2=y2
         by A11,A12,ZFMISC_1:33; then
      A13: i1=i2 & j1=j2 by A11,A12,A1,Lm46;
      thus y1=y2 by A11,A12,ZFMISC_1:33,A13;
    end; then
    reconsider p as Function by FUNCT_1:def 1;
    now let x;
      assume x in dom p; then
      consider y such that
      A14: [x,y] in p by RELAT_1:def 4;
      consider i,j be Element of NAT such that
      A15: [(j-1)*(len s1-1)+i,(i-1)*(len s2-1)+j]=[x,y] &
      1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 by A14;
      set k=x;
      A16: k=(j-1)*(len s1-1)+i by A15,ZFMISC_1:33;
      reconsider k as integer number by A15,ZFMISC_1:33;
      1<=k & k<=(len s1-1)*(len s2-1) by Lm47,A16,A15,A1; then
      reconsider k as Element of NAT by INT_1:16;
      1<=k & k<=len f1 by A8,A7,Lm47,A16,A15,A1;
      hence x in X by A9;
    end; then
    A17: dom p c= X by TARSKI:def 3;
    now let x;
      assume A18: x in X;
      reconsider k=x as Element of NAT by A18;
      A19: 1<=k & k<=(len s1-1)*(len s2-1)
        by A8,A7,A18,A9,FINSEQ_1:3;
      set l'=(len s1-1)*(len s2-1);
      reconsider l' as Element of NAT by A2,INT_1:16;
      0+(len s1-1)*(len s2-1)<=1+(len s1-1)*(len s2-1) by XREAL_1:8; then
      k<=l'+1 by A19,XXREAL_0:2; then
      A20: k in Seg(l'+1) by A19;
      A21: k <> l'+1 by A19,NAT_1:13;
      reconsider l=l'+1 as natural number;
      consider j,i be natural number such that
      A22: k=(j-1)*(len s1-1)+i & 1<=j & j<=len s2-1 & 1<=i & i<=len s1-1
        by A1,Lm45,A20,A21;
      reconsider j,i as Element of NAT by INT_1:16;
      set y=(i-1)*(len s2-1)+j;
      reconsider y as set;
      [x,y] in p by A22;
      hence x in dom p by RELAT_1:def 4;
    end; then
    A23: X c= dom p by TARSKI:def 3; then
    A24: dom p = X by A17,XBOOLE_0:def 10;
    now let y;
      assume y in rng p; then
      consider x such that
      A25: [x,y] in p by RELAT_1:def 5;
      consider i,j be Element of NAT such that
      A26: [(j-1)*(len s1-1)+i,(i-1)*(len s2-1)+j]=[x,y] &
      1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 by A25;
      set k=y;
      A27: k=(i-1)*(len s2-1)+j by A26,ZFMISC_1:33;
      reconsider k as integer number by A26,ZFMISC_1:33;
      1<=k & k<=(len s1-1)*(len s2-1) by Lm47,A27,A26,A1; then
      reconsider k as Element of NAT by INT_1:16;
      1<=k & k<=len f1 by Lm47,A27,A26,A1,A8,A7;
      hence y in X by A9;
    end; then
    A28: rng p c= X by TARSKI:def 3;
    now let y;
      assume A29: y in X; then
      A30: y in Seg len f1 by FINSEQ_1:def 3;
      reconsider k=y as Element of NAT by A29;
      A31: 1<=k & k<=(len s1-1)*(len s2-1) by A8,A7,A30,FINSEQ_1:3;
      set l'=(len s1-1)*(len s2-1);
      reconsider l' as Element of NAT by INT_1:16,A2;
      0+(len s1-1)*(len s2-1)<=1+(len s1-1)*(len s2-1) by XREAL_1:8; then
      k<=l'+1 by A31,XXREAL_0:2; then
      A32: k in Seg(l'+1) by A31;
      A33: k <> l'+1 by A31,NAT_1:13;
      set l=l'+1;
      reconsider l as natural number;
      consider i,j be natural number such that
      A34: k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1
        by A32,A1,Lm45,A33;
      reconsider j,i as Element of NAT by INT_1:16;
      set x=(j-1)*(len s1-1)+i;
      reconsider x as set;
      [x,y] in p by A34;
      hence y in rng p by RELAT_1:def 5;
    end; then
    A35: X c= rng p by TARSKI:def 3; then
    A36: rng p = X by A28,XBOOLE_0:def 10; then
    reconsider p as Function of X,X by A24,FUNCT_2:3;
    now let x1,x2 be set;
      assume x1 in X & x2 in X; then
      A37: [x1,p.x1] in p & [x2,p.x2] in p by A23,FUNCT_1:def 4;
      assume A38: p.x1 = p.x2;
      consider i1,j1 be Element of NAT such that
      A39: [(j1-1)*(len s1-1)+i1,(i1-1)*(len s2-1)+j1]=[x1,p.x1] &
      1<=i1 & i1<=len s1-1 & 1<=j1 & j1<=len s2-1 by A37;
      consider i2,j2 be Element of NAT such that
      A40: [(j2-1)*(len s1-1)+i2,(i2-1)*(len s2-1)+j2]=[x2,p.x2] &
      1<=i2 & i2<=len s1-1 & 1<=j2 & j2<=len s2-1 by A37;
      (j1-1)*(len s1-1)+i1=x1 & (i1-1)*(len s2-1)+j1=p.x1 &
          (j2-1)*(len s1-1)+i2=x2 & (i2-1)*(len s2-1)+j2=p.x2
         by A39,A40,ZFMISC_1:33; then
      i1=i2 & j1=j2 by A38,A39,A40,A1,Lm46;
      hence x1 = x2 by A39,A40,ZFMISC_1:33;
    end; then
    A41: p is one-to-one by FUNCT_2:77;
    p is onto by A36,FUNCT_2:def 3; then
    reconsider p as Permutation of X by A41,FUNCT_2:def 4;
    take p;
    now
      let H1,H2 be GroupWithOperators of O;
      let k1,k2 be natural number;
      assume A42: k1 in dom f1 & k2=p".k1;
      assume A43: H1=f1.k1 & H2=f2.k2;
      rng(p") c= X; then
      A44: rng(p") c= Seg len f1 by FINSEQ_1:def 3;
      A45: p".k1 in rng(p") by A42,FUNCT_2:6; then
      p".k1 in Seg len f1 by A44; then
      A46: k2 in dom f2 by A42,A4,A7,FINSEQ_1:def 3;
      set l=(len s1-1)*(len s2-1)+1;
      len s1+1>1+1 & len s2+1>1+1 by A1,XREAL_1:8; then
      len s1>=2 & len s2>=2 by NAT_1:13; then
      len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11; then
      (len s1-1)*(len s2-1)>=0*(len s2-1) by XREAL_1:66; then
      (len s1-1)*(len s2-1)+1>=0+1 by XREAL_1:8; then
      reconsider l as Element of NAT by INT_1:16;
      A47: k1 in Seg len f1 by A42,FINSEQ_1:def 3;
      A48: len s12 = (len s1-1)*(len s2-1)+1 by A1,Def38;
      A49: 0+(len s1-1)*(len s2-1)<=1+(len s1-1)*(len s2-1) by XREAL_1:8;
      A50: Seg len f1 c= Seg l by FINSEQ_1:7,A48,A7,A49;
      k1 <= len f1 by A47,FINSEQ_1:3; then
      A51: k1<>(len s1-1)*(len s2-1)+1 by A48,A7,NAT_1:13;
      consider i,j be natural number such that
      A52: k1=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 &
        1<=j & j<=len s2-1 by A1,A51,Lm45,A50,A47;
      p.k2 = k1 by A42,A35,FUNCT_1:57; then
      [k2,k1] in p by A45,A24,A42,FUNCT_1:8; then
      consider i',j' be Element of NAT such that
      A53: [k2,k1]=[(j'-1)*(len s1-1)+i',(i'-1)*(len s2-1)+j'] &
        1<=i' & i'<=len s1-1 & 1<=j' & j'<=len s2-1;
      (i-1)*(len s2-1)+j=(i'-1)*(len s2-1)+j' by A53,A52,ZFMISC_1:33; then
      A54: i=i' & j=j' by Lm46,A1,A53,A52; then
      A55: k2=(j-1)*(len s1-1)+i by A53,ZFMISC_1:33;
      reconsider H=s1.i,K=s2.j,H'=s1.(i+1),K'=s2.(j+1)
        as strict StableSubgroup of G by A52,Th111;
      set JH=H'"\/"(H/\K');
      set JK=K'"\/"(K/\H');
      A56: s12.k1=H'"\/"(H/\K) by A1,A52,Def38;
      A57: now
        per cases;
        suppose A58: j=len s2-1;
          per cases;
          suppose A59: i<>len s1-1;
            set i'=i+1;
            set j'=1;
            set H1 = s1.(i'+1);
            set H2 = s1.i';
            set H3 = s2.j';
            i<len s1-1 by A59,A52,XXREAL_0:1; then
            A60: i+1<=len s1-1 by INT_1:20;
            A61: k1+1=(i'-1)*(len s2-1)+j' by A58,A52;
            1+1<=i+1 by A52,XREAL_1:8; then
            A62: 1<=i' by XXREAL_0:2;
            len s2-1>1-1 by A1,XREAL_1:11; then
            A63: len s2-1>=0+1 by INT_1:20;
            reconsider H1,H2,H3 as strict StableSubgroup of G
              by Th111,A62,A60,A63;
            A64: s12.(k1+1)=H1"\/"(H2/\H3) by A1,A61,Def38,A62,A60,A63;
            A65: H3 = (Omega).G by Def31;
            now let x be set;
              assume A66: x in the carrier of H2;
              H2 is Subgroup of G by Def7; then
              the carrier of H2 c= the carrier of G by GROUP_2:def 5;
              hence x in the carrier of (Omega).G by A66;
            end; then
            the carrier of H2 c=
              the carrier of (Omega).G by TARSKI:def 3; then
            the carrier of H2 = (the carrier of H2)
              /\ (the carrier of (Omega).G) by XBOOLE_1:28; then
            A67: s12.(k1+1) = H1"\/"H2 by A64,A65,Th18;
            A68: 0+i'<=1+i' by XREAL_1:8; then
            A69: 1<=i'+1 by A62,XXREAL_0:2;
            A70: i'+1<=len s1-1+1 by A60,XREAL_1:8; then
            i'+1 in Seg len s1 by A69,FINSEQ_1:3; then
            A71: i'+1 in dom s1 by FINSEQ_1:def 3;
            i'<=len s1 by A68,A70,XXREAL_0:2; then
            i' in Seg len s1 by A62,FINSEQ_1:3; then
            i' in dom s1 by FINSEQ_1:def 3; then
            A72: H1 is normal StableSubgroup of H2 by A71,Def31;
            JH = H'"\/"(H/\(1).G) by A58,Def31
            .= H'"\/"(1).G by Th21
            .= H' by Th33;
            hence s12.(k1+1)=JH by A72,A67,Th36;
          end;
          suppose A73: i=len s1-1;
            A74: k1 = (len s1-1)*(len s2-1) by A73,A52,A58;
            A75: H' = (1).G by A73,Def31;
            A76: K' = (1).G by A58,Def31;
            JH = (1).G"\/"(1).G by A75,A76,Th21
            .= (1).G by Th33;
            hence s12.(k1+1)=JH by A74,Def38,A1;
          end;
        end;
        suppose j<>len s2-1; then
          j<len s2-1 by XXREAL_0:1,A52; then
          A77: j+1<=len s2-1 by INT_1:20;
          1+1<=j+1 by A52,XREAL_1:8; then
          A78: 1<=j+1 by XXREAL_0:2;
          set k1'=k1+1;
          set j'=j+1;
          k1'=(i-1)*(len s2-1)+j' by A52;
          hence s12.(k1+1)=JH by A1,A52,A77,A78,Def38;
        end;
      end;
      A79: s21.k2=K'"\/"(K/\H) by A1,A52,A55,Def38;
      A80: now
        per cases;
        suppose A81: i=len s1-1;
          per cases;
          suppose A82: j<>len s2-1;
            set j'=j+1;
            set i'=1;
            set H1 = s2.(j'+1);
            set H2 = s2.j';
            set H3 = s1.i';
            j<len s2-1 by A82,A52,XXREAL_0:1; then
            A83: j+1<=len s2-1 by INT_1:20;
            A84: k2+1=(j'-1)*(len s1-1)+i' by A81,A54,A53,ZFMISC_1:33;
            1+1<=j+1 by A52,XREAL_1:8; then
            A85: 1<=j' by XXREAL_0:2;
            len s1-1>1-1 by A1,XREAL_1:11; then
            A86: len s1-1>=0+1 by INT_1:20;
            reconsider H1,H2,H3 as strict StableSubgroup of G
              by Th111,A85,A83,A86;
            A87: s21.(k2+1)=H1"\/"(H2/\H3) by A1,A84,Def38,A85,A83,A86;
            A88: H3 = (Omega).G by Def31;
            now let x be set;
              assume A89: x in the carrier of H2;
              H2 is Subgroup of G by Def7; then
              the carrier of H2 c= the carrier of G by GROUP_2:def 5;
              hence x in the carrier of (Omega).G by A89;
            end; then
            the carrier of H2 c=
              the carrier of (Omega).G by TARSKI:def 3; then
            the carrier of H2 = (the carrier of H2)
              /\ (the carrier of (Omega).G) by XBOOLE_1:28; then
            A90: s21.(k2+1) = H1"\/"H2 by A87,A88,Th18;
            A91: 0+j'<=1+j' by XREAL_1:8; then
            A92: 1<=j'+1 by A85,XXREAL_0:2;
            A93: j'+1<=len s2-1+1 by A83,XREAL_1:8; then
            j'+1 in Seg len s2 by A92,FINSEQ_1:3; then
            A94: j'+1 in dom s2 by FINSEQ_1:def 3;
            j'<=len s2 by A91,A93,XXREAL_0:2; then
            j' in Seg len s2 by A85,FINSEQ_1:3; then
            j' in dom s2 by FINSEQ_1:def 3; then
            A95: H1 is normal StableSubgroup of H2 by A94,Def31;
            A96: H' = (1).G by A81,Def31;
            JK = K'"\/"(1).G by A96,Th21
            .= K' by Th33;
            hence s21.(k2+1)=JK by A95,A90,Th36;
          end;
          suppose A97: j=len s2-1;
            A98: k2 = (len s1-1)*(len s2-1) by A97,A54,A81,A53,ZFMISC_1:33;
            A99: H' = (1).G by A81,Def31;
            A100: K' = (1).G by A97,Def31;
            JK = (1).G"\/"(1).G by A99,A100,Th21
            .= (1).G by Th33;
            hence s21.(k2+1)=JK by A98,Def38,A1;
          end;
        end;
        suppose i<>len s1-1; then
          i<len s1-1 by XXREAL_0:1,A52; then
          A101: i+1<=len s1-1 by INT_1:20;
          1+1<=i+1 by A52,XREAL_1:8; then
          A102: 1<=i+1 by XXREAL_0:2;
          A103: k2+1=(j-1)*(len s1-1)+i+1 by A54,A53,ZFMISC_1:33;
          set k2'=k2+1;
          set i'=i+1;
          k2'=(j-1)*(len s1-1)+i' by A103;
          hence s21.(k2+1)=JK by A1,A52,A101,A102,Def38;
        end;
      end;
      A104: H' is normal StableSubgroup of H &
        K' is normal StableSubgroup of K by A52,Th112;
      reconsider JH as normal StableSubgroup of H'"\/"(H/\K) by A104,Th92;
      reconsider JK as normal StableSubgroup of K'"\/"(K/\H) by A104,Th92;
      A105: H1 = (H'"\/"(H/\K))./.JH by A43,A56,A57,Def36,A42,A6;
      A106: H2 = (K'"\/"(K/\H))./.JK by A43,A79,A80,Def36,A46,A6;
      thus H1,H2 are_isomorphic by A104,A105,A106,Th93;
    end;
    hence f1,f2 are_equivalent_under p,O by A7,Def37,A4;
  end;
  hence thesis by A5,Th108;
end;

:: ALG I.4.7 Theorem 5
theorem Th118:
  ex s1',s2' st s1' is_finer_than s1 & s2' is_finer_than s2
    & s1' is_equivalent_with s2'
proof
  per cases;
  suppose A1: len s1>1 & len s2>1;
    set s1'=the_schreier_series_of(s1,s2);
    set s2'=the_schreier_series_of(s2,s1);
    take s1',s2';
    thus s1' is_finer_than s1 & s2' is_finer_than s2 by A1,Th116;
    thus s1' is_equivalent_with s2' by A1,Th117;
  end;
  suppose A2: len s1<=1 or len s2<=1;
    per cases;
    suppose A3: len s1<=len s2;
      set s1'=s2;
      set s2'=s2;
      take s1',s2';
      thus s1' is_finer_than s1 & s2' is_finer_than s2 by A2,A3,Th114;
      thus s1' is_equivalent_with s2' by Th113;
    end;
    suppose A4: len s1>len s2;
      set s1'=s1;
      set s2'=s1;
      take s1',s2';
      thus s1' is_finer_than s1 & s2' is_finer_than s2 by A2,A4,Th114;
      thus s1' is_equivalent_with s2' by Th113;
    end;
  end;
end;

begin :: The Jordan-H\"older Theorem

:: ALG I.4.7 Theorem 6
theorem
  s1 is jordan_holder & s2 is jordan_holder implies s1 is_equivalent_with s2
proof
  assume A1: s1 is jordan_holder;
  assume A2: s2 is jordan_holder;
  per cases;
  suppose A3: s1 is empty;
    now assume A4: s2 is not empty;
      A5: s2 is strictly_decreasing by A2,Def34;
      now set x={};
        take x;
        thus x c= dom s2 by XBOOLE_1:2;
        thus s1 = s2 * Sgm x by A3,RELAT_1:62,FINSEQ_3:49;
      end; then
      s2 is_finer_than s1 by Def32;
      hence contradiction by Def34,A1,A3,A4,A5;
    end;
    hence thesis by Th107,A3;
  end;
  suppose A6: s1 is not empty;
    A7: now assume A8: s2 is empty;
      A9: s1 is strictly_decreasing by A1,Def34;
      now set x={};
        take x;
        thus x c= dom s1 by XBOOLE_1:2;
        thus s2 = s1 * Sgm x by A8,RELAT_1:62,FINSEQ_3:49;
      end; then
      s1 is_finer_than s2 by Def32;
      hence contradiction by Def34,A2,A6,A8,A9;
    end;
    consider s1',s2' such that
    A10: s1' is_finer_than s1 & s2' is_finer_than s2
      & s1' is_equivalent_with s2' by Th118;
    A11: s1' is not empty & s2' is not empty by A10,A6,A7,Th97; then
    A12: ex p' being Permutation of dom the_series_of_quotients_of s1' st
    the_series_of_quotients_of s1',the_series_of_quotients_of s2'
      are_equivalent_under p',O by A10,Th108;
    defpred P[natural number] means
      for s1',s2' st s1' is not empty & s2' is not empty & len s1'=len s1+$1 &
        s1' is_finer_than s1 & s2' is_finer_than s2 &
        ex p being Permutation of dom the_series_of_quotients_of s1' st
        the_series_of_quotients_of s1',the_series_of_quotients_of s2'
          are_equivalent_under p,O
      holds
        ex p being Permutation of dom the_series_of_quotients_of s1 st
        the_series_of_quotients_of s1,the_series_of_quotients_of s2
          are_equivalent_under p,O;
    A13: P[0]
    proof
      let s1',s2';
      assume A14: s1' is not empty & s2' is not empty;
      assume A15: len s1' = len s1+0 & s1' is_finer_than s1;
      assume A16: s2' is_finer_than s2;
      given p be Permutation of dom the_series_of_quotients_of s1' such that
      A17: the_series_of_quotients_of s1',the_series_of_quotients_of s2'
        are_equivalent_under p,O;
      A18: s1' is_equivalent_with s2' by A14,A17,Th108;
      s1'=s1 by A15,Th96; then
      s2' is jordan_holder by A1,A18,Th115; then
      s2'=s2 by A16,A2,Th98; then
      s1 is_equivalent_with s2 by A18,A15,Th96;
      hence thesis by A6,A7,Th108;
    end;
    A19: for n st P[n] holds P[n+1]
    proof
      let n;
      assume A20: P[n];
      now
        let s1',s2';
        assume s1' is not empty & s2' is not empty;
        assume A21: len s1' = len s1 + n+1;
        assume A22: s1' is_finer_than s1;
        assume A23: s2' is_finer_than s2;
        set f1=the_series_of_quotients_of s1';
        set f2=the_series_of_quotients_of s2';
        given p be Permutation of dom f1 such that
        A24: f1,f2 are_equivalent_under p,O;
        A25: len f1 = len f2 by A24,Def37;
        n+1+len s1>0+len s1 by XREAL_1:8; then
        consider i such that
        A26: i in dom f1 &
          for H st H=f1.i holds H is trivial by A21,Th109,A22,A1;
        A27: i in dom s1' & i+1 in dom s1' & s1'.i=s1'.(i+1) by A26,Th103;
        set j = p".i;
        rng(p") c= dom f1; then
        A28: rng(p") c= Seg len f1 by FINSEQ_1:def 3;
        (p").i in rng(p") by A26,FUNCT_2:6; then
        A29: (p").i in Seg len f1 by A28; then
        reconsider j as natural number by ORDINAL1:def 13;
        A30: j in dom f2 by A29,A25,FINSEQ_1:def 3;
        set H1=f1.i;
        set H2=f2.j;
        A31: f1.i in rng f1 by FUNCT_1:12,A26;
        f2.j in rng f2 by FUNCT_1:12,A30; then
        reconsider H1,H2 as strict GroupWithOperators of O by A31,Th102;
        A32: H1 is trivial by A26;
        H1,H2 are_isomorphic by A24,A26,Def37; then
        for H st H=f2.j holds H is trivial by Th58,A32; then
        A33: j in dom s2' & j+1 in dom s2' & s2'.j=s2'.(j+1) by A30,Th103;
        reconsider s1''=Del(s1',i) as FinSequence of the_stable_subgroups_of G
          by FINSEQ_3:114;
        reconsider s1'' as CompositionSeries of G by Th94,A27;
        reconsider s2''=Del(s2',j) as FinSequence of the_stable_subgroups_of G
          by FINSEQ_3:114;
        reconsider s2'' as CompositionSeries of G by Th94,A33;
        A34: len s1'' = len s1 + n by FINSEQ_3:118,A21,A27;
        A35: s1'' is_finer_than s1 by Th99,A27,A22,A1;
        A36: s2'' is_finer_than s2 by Th99,A33,A23,A2;
        A37: the_series_of_quotients_of s1''= Del(f1,i) by Th104,A27;
        A38: the_series_of_quotients_of s2''= Del(f2,j) by Th104,A33;
        A39: s1'' is not empty & s2'' is not empty by A35,A36,A6,A7,Th97;
        ex p be Permutation of dom the_series_of_quotients_of s1''
          st the_series_of_quotients_of s1'',the_series_of_quotients_of s2''
          are_equivalent_under p,O by A24,A26,A37,A38,Th106;
        hence thesis by A39,A34,A35,A36,A20;
      end;
      hence thesis;
    end;
    A40: for n holds P[n] from NAT_1:sch 2(A13,A19);
    ex n st len s1' = len s1 + n by A10,Th95; then
    ex p being Permutation of dom the_series_of_quotients_of s1 st
      the_series_of_quotients_of s1,the_series_of_quotients_of s2
      are_equivalent_under p,O by A11,A10,A12,A40;
    hence s1 is_equivalent_with s2 by A6,A7,Th108;
  end;
end;

begin :: Appendix

theorem
  for P,R being Relation holds P = (rng P)|R iff P~ = (R~)|(dom (P~)) by Lm36;

theorem
  for X being set, P,R being Relation holds P*(R|X) = (X|P)*R by Lm37;

theorem
  for n being natural number, X being set, f being PartFunc of REAL, REAL
  st X c= Seg n & X c= dom f & f is_increasing_on X & f.:X c= NAT \ {0}
  holds Sgm(f.:X) = f * Sgm X by Lm38;

theorem
  for y being set, i,n being natural number
  st y c= Seg(n+1) & i in Seg(n+1) & not i in y
  holds ex x st Sgm x = Sgm(Seg(n+1)\{i})" * Sgm y & x c= Seg n by Lm39;

theorem
  for D being non empty set, f being FinSequence of D,
      p being Element of D, n being Element of NAT
  st n in dom f holds f = Del(Ins(f,n,p),n+1) by Lm44;

theorem
  for G,H being Group, F1 being FinSequence of the carrier of G,
      F2 being FinSequence of the carrier of H,
      I being FinSequence of INT, f being Homomorphism of G,H
  st (for k being Element of NAT st k in Seg len F1 holds F2.k = f.(F1.k))
      & len F1 = len I & len F2 = len I
  holds f.(Product(F1 |^ I)) = Product(F2 |^ I) by Lm24;
