:: The {J}ordan-H\"older Theorem
::  by Marco Riccardi
::
:: Received April 20, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies REALSET1, FINSEQ_1, GROUP_3, FUNCT_1, RLSUB_1, GROUP_2, RELAT_1,
      GROUP_6, BOOLE, QC_LANG1, WELLORD1, GROUP_1, VECTSP_1, GRAPH_1, SETFAM_1,
      ISOCAT_1, FUNCT_2, ARYTM_1, ARYTM, ARYTM_3, FINSET_1, FINSEQ_2, TARSKI,
      INT_1, MATRIX_2, CARD_1, SGRAPH1, SUBSET_1, GROUP_4, NAT_1, NATTRA_1,
      FINSEQ_5, LATTICES, SQUARE_1, PARTFUN1, RFINSEQ, MEMBERED, FINSEQ_4,
      CARD_3, BINOP_1, COHSP_1, COMPLEX1, GROUP_9, ALGSTR_0, ORDINAL2;
 notations TARSKI, XBOOLE_0, SUBSET_1, XCMPLX_0, ORDINAL1, XREAL_0, RELAT_1,
      FUNCT_1, RELSET_1, FUNCT_2, STRUCT_0, ALGSTR_0, PARTFUN1, FINSEQ_1,
      ZFMISC_1, CARD_1, XXREAL_2, FINSET_1, INT_1, NAT_1, FINSEQ_2,
      FINSEQ_3, GROUP_1, GROUP_2, GROUP_3, XXREAL_0, SETFAM_1, GROUP_4,
      FINSEQ_5, NUMBERS, MEMBERED, MATRIX_2, RFINSEQ, BINOP_1, REALSET1,
      GROUP_6, NAT_D, RFUNCT_2;
 constructors SETFAM_1, PARTFUN1, BINOP_1, XXREAL_0, REAL_1, NAT_1, NAT_D,
      RFUNCT_2, RFINSEQ, BINARITH, FINSEQ_5, REALSET2, GROUP_4,
      GROUP_6, MATRIX_2, SEQ_1, XXREAL_2, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCT_2,
      FINSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, INT_1, MEMBERED, FINSEQ_1,
      PRE_CIRC, STRUCT_0, GROUP_1, GROUP_2, GROUP_3, GROUP_6, MATRIX_2,
      VALUED_0, ALGSTR_0, XXREAL_2, CARD_1, RELSET_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions GROUP_2, GROUP_6, FINSEQ_3, FINSEQ_5, TARSKI, SUBSET_1, FINSEQ_1,
      REALSET1, RELAT_1, GROUP_4, ALGSTR_0, FUNCT_2;
 theorems FINSEQ_1, GROUP_2, GROUP_3, TARSKI, GROUP_6, FINSEQ_2, FUNCT_1,
      FUNCT_2, RELAT_1, XBOOLE_0, XBOOLE_1, NAT_1, GROUP_1, XREAL_1, RELSET_1,
      PARTFUN1, FINSEQ_3, INT_1, ZFMISC_1, CARD_1, CARD_2, XCMPLX_1, ORDINAL1,
      RFUNCT_2, SUBSET_1, MATRIX_2, MATRIX_7, FINSEQ_5, GLIB_001, RFINSEQ,
      XXREAL_0, WSIERP_1, GROUP_4, SETFAM_1, STRUCT_0, REALSET1, NAT_D,
      XXREAL_2, XREAL_0;
 schemes XBOOLE_0, FUNCT_1, FINSEQ_1, NAT_1, FUNCT_2, DOMAIN_1;

begin :: Actions and Groups with Operators

:: ALG I.3.1 Definition 1

definition
  let O,E be set;
  mode Action of O,E is Function of O, Funcs(E,E);
end;

:: ALG I.3.2 Definition 2

definition
  let O,E be set;
  let A be Action of O,E;
  let IT be set;
  pred IT is_stable_under_the_action_of A means
  :Def1:
  for o being Element of O, f being Function of E, E st o in O & f = A.o
  holds (f .: IT) c= IT;
end;

Lm1: for O,E being set, A being Action of O,E
holds [#]E is_stable_under_the_action_of A
proof
  let O,E be set;
  let A be Action of O,E;
  for o being Element of O, f being Function of E, E
  st o in O & f = A.o holds (f .: [#]E) c= [#]E;
  hence thesis by Def1;
end;

definition
  let O,E be set;
  let A be Action of O,E;
  let X be Subset of E;
  func the_stable_subset_generated_by(X,A) -> Subset of E means
  :Def2:
  X c= it & it is_stable_under_the_action_of A & for Y being Subset of E
  st Y is_stable_under_the_action_of A & X c= Y holds it c= Y;
  existence
  proof
    defpred P[set] means ex B being Subset of E
    st $1 = B & X c= $1 & B is_stable_under_the_action_of A;
    consider XX be set such that
A1: for Y being set holds Y in XX iff Y in bool E & P[Y] from XBOOLE_0:sch 1;
    set M = meet XX;
    [#]E is_stable_under_the_action_of A & E in bool E by Lm1,ZFMISC_1:def 1;
    then
A2: E in XX by A1;
    then for x being set st x in M holds x in E by SETFAM_1:def 1;
    then reconsider M as Subset of E by TARSKI:def 3;
    take M;
    now
      let x be set;
      assume
A3:   x in X;
      now
        let Y be set;
        assume Y in XX;
        then ex B being Subset of E st Y = B & X c= Y &
        B is_stable_under_the_action_of A by A1;
        hence x in Y by A3;
      end;
      hence x in M by A2,SETFAM_1:def 1;
    end;
    hence X c= M by TARSKI:def 3;
    now
      let o be Element of O;
      let f be Function of E, E;
      assume
A4:   o in O;
      assume
A5:   f = A.o;
      now
        let y be set;
        assume
A6:     y in f .: M;
        now
          let Y be set;
          assume
A7:       Y in XX;
          then ex B being Subset of E st Y = B & X c= Y
          & B is_stable_under_the_action_of A by A1;
          then
A8:       (f .: Y) c= Y by A4,A5,Def1;
          f .: M c= f .: Y by A7,RELAT_1:156,SETFAM_1:4;
          then f .: M c= Y by A8,XBOOLE_1:1;
          hence y in Y by A6;
        end;
        hence y in M by A2,SETFAM_1:def 1;
      end;
      hence (f .: M) c= M by TARSKI:def 3;
    end;
    hence M is_stable_under_the_action_of A by Def1;
    now
      let Y be Subset of E;
      assume Y is_stable_under_the_action_of A & X c= Y;
      then Y in XX by A1;
      hence M c= Y by SETFAM_1:4;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let B1,B2 be Subset of E;
    assume X c= B1 & B1 is_stable_under_the_action_of A &
    (for Y being Subset of E
    st Y is_stable_under_the_action_of A & X c= Y holds B1 c= Y) &
    X c= B2 & B2 is_stable_under_the_action_of A & (for Y being Subset of E
    st Y is_stable_under_the_action_of A & X c= Y holds B2 c= Y);
    then B1 c= B2 & B2 c= B1;
    hence thesis by XBOOLE_0:def 10;
  end;
end;

definition
  let O,E be set;
  let A be Action of O,E;
  let F be FinSequence of O;
  func Product(F,A) -> Function of E,E means
  :Def3:
  it = id E if len F = 0 otherwise ex PF being FinSequence of Funcs(E,E) st
  it = PF.(len F) & len PF = len F & PF.1 = A.(F.1) &
  (for n being Nat st n<>0 & n<len F holds ex f,g being Function of E,E
  st f = PF.n & g = A.(F.(n+1)) & PF.(n+1) = f*g);
  existence
  proof
    per cases;
    suppose len F=0;
      hence thesis;
    end;
    suppose
A1:   len F<>0;
      defpred P[Element of NAT] means
      for F being FinSequence of O st len F = $1 & len F <> 0 holds
      (ex PF being FinSequence of Funcs(E,E), IT being Function of E,E
      st IT = PF.(len PF) & len PF = len F & PF.1 = A.(F.1) &
      (for k being Nat st k<>0 & k<len F holds ex f,g being Function of E,E
      st f = PF.k & g = A.(F.(k+1)) & PF.(k+1) = f*g));
A2:   P[0];
A3:   for k being Element of NAT st P[k] holds P[k + 1]
      proof
        let k be Element of NAT;
        assume
A4:     P[k];
        thus P[k + 1]
        proof
          let F be FinSequence of O;
          assume
A5:       len F = k+1 & len F <> 0;
          reconsider G = F | Seg k as FinSequence of O by FINSEQ_1:23;
A6:       len G = k by A5,FINSEQ_3:59;
          per cases;
          suppose
A7:         len G = 0;
            set IT=A.(F.1);
            1 in Seg len F by A5,A6,A7;
            then 1 in dom F by FINSEQ_1:def 3;
            then F.1 in rng F by FUNCT_1:12;
            then F.1 in O;
            then F.1 in dom A by FUNCT_2:def 1;
            then IT in rng A by FUNCT_1:12;
            then reconsider IT as Element of Funcs(E,E);
            set PF=<*IT*>;
            ex f being Function st IT = f & dom f = E & rng f c= E
            by FUNCT_2:def 2;
            then reconsider IT as Function of E,E by FUNCT_2:4;
            take PF, IT;
            len PF = 1 & PF.1 = IT by FINSEQ_1:57;
            hence IT = PF.(len PF);
            thus len PF = len F by A5,A6,A7,FINSEQ_1:57;
            thus PF.1 = A.(F.1) by FINSEQ_1:57;
            let k be Nat;
            assume
A8:         k<>0 & k<len F;
            consider f,g be Function of E,E;
            take f,g;
            thus f = PF.k & g = A.(F.(k+1)) by A5,A6,A7,A8,NAT_1:14;
            thus thesis by A5,A6,A7,A8,NAT_1:14;
          end;
          suppose
A9:         len G <> 0;
            then consider PFk be FinSequence of Funcs(E,E), ITk be Function of
            E,E such that
A10:        ITk = PFk.(len PFk) & len PFk = len G & PFk.1 = A.(G.1) and
A11:        for k being Nat st k<>0 & k<len G
            holds ex f,g being Function of E,E
            st f = PFk.k & g = A.(G.(k+1)) & PFk.(k+1) = f*g by A4,A6;
            set f = PFk.k;
            set g=A.(F.(k+1));
            0<k by A6,A9;
            then
A12:        0+1<k+1 by XREAL_1:8;
            then
A13:        1<=k by NAT_1:13;
            then k in Seg len PFk by A6,A10;
            then
A14:        k in dom PFk by FINSEQ_1:def 3;
            then PFk.k in Funcs(E,E) by FINSEQ_2:13;
            then ex f being Function st PFk.k = f & dom f = E & rng f c= E
            by FUNCT_2:def 2;
            then reconsider f as Function of E,E by FUNCT_2:4;
            k+1 in Seg len F by A5,A12;
            then k+1 in dom F by FINSEQ_1:def 3;
            then F.(k+1) in rng F by FUNCT_1:12;
            then F.(k+1) in O;
            then F.(k+1) in dom A by FUNCT_2:def 1;
            then g in rng A by FUNCT_1:12;
            then ex f being Function st g = f & dom f = E & rng f c= E
            by FUNCT_2:def 2;
            then reconsider g as Function of E,E by FUNCT_2:4;
            set IT = f * g;
            set PF = PFk ^ <*IT*>;
            IT in Funcs(E,E) by FUNCT_2:12;
            then <*IT*> is FinSequence of Funcs(E,E) by FINSEQ_1:95;
            then reconsider PF as FinSequence of Funcs(E,E) by FINSEQ_1:96;
A15:        len PF = len G + len <*IT*> by A10,FINSEQ_1:35
              .= k + 1 by A6,FINSEQ_1:56;
            then
A16:        len PF = len PFk + 1 by A5,A10,FINSEQ_3:59;
            take PF, IT; thus
A17:        IT=PF.(len PF) & len PF=len F by A5,A15,A16,FINSEQ_1:59;
            0<len G by A9;
            then 0+1<len G+1 by XREAL_1:8;
            then 1<=len G by NAT_1:13;
            then 1 in Seg len PFk by A10;
            then 1 in dom PFk by FINSEQ_1:def 3;
            then
A18:        PF.1 = A.(G.1) by A10,FINSEQ_1:def 7;
A19:        1 in Seg k by A13;
            0+k<=k+1 by XREAL_1:8;
            then 1 in Seg (k+1) /\ Seg k by A19,FINSEQ_1:9;
            then 1 in dom F /\ Seg k by A5,FINSEQ_1:def 3;
            hence PF.1 = A.(F.1) by A18,FUNCT_1:71;
            let n be Nat;
            assume
A20:        n<>0;
            assume n<len F;
            then
A21:        n<=k by A5,NAT_1:13;
            per cases;
            suppose
A22:          n >= k;
              then
A23:          n = k by A21,XXREAL_0:1;
              A.(F.(n+1)) = g by A21,A22,XXREAL_0:1;
              then reconsider g'=A.(F.(n+1)) as Function of E,E;
              reconsider f'=PF.n as Function of E,E by A14,A23,FINSEQ_1:def 7;
              take f',g';
              thus f' = PF.n & g' = A.(F.(n+1));
              thus thesis by A14,A15,A17,A23,FINSEQ_1:def 7;
            end;
            suppose
A24:          n < k;
              then consider f',g' be Function of E,E such that
A25:          f' = PFk.n & g' = A.(G.(n+1)) & PFk.(n+1) = f'*g' by A6,A11,A20;
              0<n by A20;
              then
A26:          0+1<n+1 by XREAL_1:8;
              then 1<=n & n<=k by A24,NAT_1:13;
              then n in Seg len PFk by A6,A10,FINSEQ_1:3;
              then
A27:          n in dom PFk by FINSEQ_1:def 3;
A28:          1<=n+1 & n+1<=k by A24,A26,NAT_1:13;
              then n+1 in Seg len PFk by A6,A10;
              then
A29:          n+1 in dom PFk by FINSEQ_1:def 3;
              take f',g';
A30:          n+1 in Seg k by A28;
              0+k<=1+k by XREAL_1:8;
              then n+1 in Seg (k+1) /\ Seg k by A30,FINSEQ_1:9;
              then n+1 in dom F /\ Seg k by A5,FINSEQ_1:def 3;
              hence f' = PF.n & g' = A.(F.(n+1))
              by A25,A27,FINSEQ_1:def 7,FUNCT_1:71;
              thus thesis by A25,A29,FINSEQ_1:def 7;
            end;
          end;
        end;
      end;
A31:  for k being Element of NAT holds P[k] from NAT_1:sch 1(A2,A3);
      ex PF being FinSequence of Funcs(E,E), IT being Function of E,E
      st IT = PF.(len PF) & len PF = len F & PF.1 = A.(F.1) &
      (for k being Nat st k<>0 & k<len F holds ex f,g being Function of E,E
      st f = PF.k & g = A.(F.(k+1)) & PF.(k+1) = f*g) by A1,A31;
      hence thesis;
    end;
  end;
  uniqueness
  proof
    now
      let IT1,IT2 be Function of E,E;
      given PF1 be FinSequence of Funcs(E,E) such that
A32:  IT1 = PF1.(len F) & len PF1 = len F & PF1.1 = A.(F.1) &
      (for k being Nat st k<>0 & k<len F holds ex f,g being Function of E,E
      st f = PF1.k & g = A.(F.(k+1)) & PF1.(k+1) = f*g);
      given PF2 be FinSequence of Funcs(E,E) such that
A33:  IT2 = PF2.(len F) & len PF2 = len F & PF2.1 = A.(F.1) &
      (for k being Nat st k<>0 & k<len F holds ex f,g being Function of E,E
      st f = PF2.k & g = A.(F.(k+1)) & PF2.(k+1) = f*g);
      defpred P[Nat] means 1 <= $1 & $1 <= len PF1 implies PF1.$1 = PF2.$1;
A34:  P[0];
A35:  for k being Nat st P[k] holds P[k+1]
      proof
        let k be Nat;
        assume
A36:    P[k];
        now
          assume 1 <= k+1;
          assume
A37:      k+1 <= len PF1;
          then
A38:      k < len PF1 by NAT_1:13;
          per cases;
          suppose k=0;
            hence PF1.(k+1) = PF2.(k+1) by A32,A33;
          end;
          suppose
A39:        k<>0;
            then consider f1,g1 be Function of E,E such that
A40:        f1=PF1.k & g1=A.(F.(k+1)) & PF1.(k+1)=f1*g1 by A32,A38;
            consider f2,g2 be Function of E,E such that
A41:        f2=PF2.k & g2=A.(F.(k+1)) & PF2.(k+1)=f2*g2 by A32,A33,A38,A39;
            0 < k by A39;
            then 0+1 < k+1 by XREAL_1:8;
            hence PF1.(k+1) = PF2.(k+1) by A36,A37,A40,A41,NAT_1:13;
          end;
        end;
        hence thesis;
      end;
      for k be Nat holds P[k] from NAT_1:sch 2(A34,A35);
      hence IT1=IT2 by A32,A33,FINSEQ_1:18;
    end;
    hence thesis;
  end;
  consistency;
end;

:: ALG I.3.4 Definition 6

definition
  let O be set;
  let G be Group;
  let IT be Action of O, the carrier of G;
  attr IT is distributive means
  :Def4:
  for o being Element of O st o in O holds IT.o is Homomorphism of G, G;
end;

definition
  let O be set;
  struct (multMagma) HGrWOpStr over O (# carrier -> set,
    multF -> BinOp of the carrier, action -> Action of O, the carrier #);
end;

registration
  let O be set;
  cluster non empty HGrWOpStr over O;
  existence
  proof
    consider A be non empty set, m be BinOp of A, h be Action of O,A;
    take HGrWOpStr(#A,m,h#);
    thus thesis;
  end;
end;

definition
  let O be set;
  let IT be non empty HGrWOpStr over O;
  attr IT is distributive means
  :Def5:
  for G being Group, a being Action of O, the carrier of G
  st a = the action of IT & the multMagma of G = the multMagma of IT
  holds a is distributive;
end;

Lm2: for O,E being set holds [:O,{id E}:] is Action of O, E
proof
  let O,E be set;
  set h = [:O,{id E}:];
  now
    let x be set;
    assume x in {id E};
    then reconsider f=x as Function of E,E by TARSKI:def 1;
    f in Funcs(E,E) by FUNCT_2:12;
    hence x in Funcs(E,E);
  end;
  then {id E} c= Funcs(E,E) by TARSKI:def 3;
  then h c= [:O,Funcs(E,E):] by ZFMISC_1:118;
  then reconsider h as Relation of O,Funcs(E,E) ;
A1: now
    thus ((Funcs(E,E)={} implies O={}) implies O = dom h)
    proof
      assume Funcs(E,E)={} implies O={};
      now
        let x be set;
        assume
A2:     x in O;
        consider y be set such that
A3:     y=id E;
        take y;
        y in {id E} by A3,TARSKI:def 1;
        hence [x,y] in h by A2,ZFMISC_1:def 2;
      end;
      hence thesis by RELSET_1:22;
    end;
    assume O = {};
    hence h = {};
  end;
  now
    let x,y1,y2 be set;
    assume
A4: [x,y1] in h & [x,y2] in h;
    then consider x',y' be set such that
A5: x' in O & y' in {id E} & [x,y1]=[x',y'] by ZFMISC_1:def 2;
    consider x'',y'' be set such that
A6: x'' in O & y'' in {id E} & [x,y2]=[x'',y''] by A4,ZFMISC_1:def 2;
A7: y'=id E & y''=id E by A5,A6,TARSKI:def 1;
    y1=y' & y2=y'' by A5,A6,ZFMISC_1:33;
    hence y1 = y2 by A7;
  end;
  then reconsider h as PartFunc of O,Funcs(E,E) by FUNCT_1:def 1;
  h is Action of O, E by A1,FUNCT_2:def 1;
  hence thesis;
end;

Lm3: for O being set, G being strict Group holds
ex H being non empty HGrWOpStr over O
st H is strict distributive Group-like associative & G = the multMagma of H
proof
  let O be set;
  let G be strict Group;
  set A = the carrier of G;
  set m = the multF of G;
  reconsider h=[:O,{id the carrier of G}:] as Action of O, the carrier of G
  by Lm2;
  set GO = HGrWOpStr(#A,m,h#);
  reconsider GO as non empty HGrWOpStr over O;
  reconsider G'=GO as non empty multMagma;
A1: now
    set e=1_G;
    reconsider e'=e as Element of G';
    take e';
    let h' be Element of G';
    reconsider h=h' as Element of G;
    h'*e' = h*e
      .= h by GROUP_1:def 5;
    hence h' * e' = h';
    e'*h' = e*h
      .= h by GROUP_1:def 5;
    hence e' * h' = h';
    set g=h";
    reconsider g'=g as Element of G';
    take g';
    h'*g' = h*g
      .= 1_G by GROUP_1:def 6;
    hence h' * g' = e';
    g'*h' = g*h
      .= 1_G by GROUP_1:def 6;
    hence g' * h' = e';
  end;
A2: now
    let x',y',z' be Element of G';
    reconsider x=x',y=y',z=z' as Element of G;
    (x'*y')*z' = (x*y)*z
      .= x*(y*z) by GROUP_1:def 4;
    hence (x'*y')*z' = x'*(y'*z');
  end;
A3: now
    let G'' be Group;
    let a be Action of O, the carrier of G'';
    assume
A4: a = the action of GO;
    assume
A5: the multMagma of G'' = the multMagma of GO;
    now
      let o be Element of O;
      assume o in O;
      then o in dom h by FUNCT_2:def 1;
      then [o,h.o] in [:O,{id the carrier of G''}:] by A5,FUNCT_1:8;
      then consider x,y be set such that
A6:   x in O & y in {id the carrier of G''} & [o,h.o]=[x,y] by ZFMISC_1:def 2;
      y = id the carrier of G'' & h.o = y by A6,TARSKI:def 1,ZFMISC_1:33;
      hence a.o is Homomorphism of G'', G'' by A4,GROUP_6:47;
    end;
    hence a is distributive by Def4;
  end;
  take GO;
  thus thesis by A1,A2,A3,Def5,GROUP_1:def 3,def 4;
end;

registration
  let O be set;
  cluster strict distributive Group-like associative
    (non empty HGrWOpStr over O);
  existence
  proof
    consider G be strict Group;
    consider H be non empty HGrWOpStr over O such that
A1: H is strict distributive Group-like associative &
    the multMagma of H = G by Lm3;
    take H;
    thus thesis by A1;
  end;
end;

:: ALG I.4.2 Definition 2

definition
  let O be set;
  mode GroupWithOperators of O
    is distributive Group-like associative (non empty HGrWOpStr over O);
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let o be Element of O;
  func G^o -> Homomorphism of G, G equals
  :Def6:
  (the action of G).o if o in O otherwise id the carrier of G;
  correctness
  proof
    now
      assume
A1:   o in O;
      consider G' be Group such that
A2:   the multMagma of G' = the multMagma of G;
      reconsider a=the action of G as Action of O, the carrier of G' by A2;
      a is distributive by A2,Def5;
      then reconsider f'=a.o as Homomorphism of G', G' by A1,Def4;
      reconsider f=f' as Function of G, G by A2;
      now
        let g1, g2 be Element of G;
        reconsider g1'=g1,g2'=g2 as Element of G' by A2;
        f.(g1 * g2) = f'.(g1' * g2') by A2
          .= f'.g1' * f'.g2' by GROUP_6:def 7
          .= (the multF of G).(f.g1,f.g2) by A2;
        hence f.(g1 * g2) = f.g1 * f.g2;
      end;
      hence (the action of G).o is Homomorphism of G,G by GROUP_6:def 7;
    end;
    hence thesis by GROUP_6:47;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  mode StableSubgroup of G -> distributive Group-like associative
    (non empty HGrWOpStr over O) means
    :Def7:
    it is Subgroup of G &
    for o being Element of O holds it^o = (G^o)|the carrier of it;
  correctness
  proof
    set H=G;
    take H;
    now
      let o be Element of O;
      set f=G^o;
      dom f = the carrier of G by FUNCT_2:def 1;
      hence H^o = (G^o)|the carrier of H by RELAT_1:98;
    end;
    hence thesis by GROUP_2:63;
  end;
end;

Lm4: for O being set, G being GroupWithOperators of O
holds the HGrWOpStr of G is StableSubgroup of G
proof
  let O be set;
  let G be GroupWithOperators of O;
  reconsider G' = the HGrWOpStr of G as non empty multMagma;
A1: now
    set e=1_G;
    reconsider e'=e as Element of G';
    take e';
    let h' be Element of G';
    reconsider h=h' as Element of G;
    h'*e' = h*e
      .= h by GROUP_1:def 5;
    hence h' * e' = h';
    e'*h' = e*h
      .= h by GROUP_1:def 5;
    hence e' * h' = h';
    set g=h";
    reconsider g'=g as Element of G';
    take g';
    h'*g' = h*g
      .= 1_G by GROUP_1:def 6;
    hence h' * g' = e';
    g'*h' = g*h
      .= 1_G by GROUP_1:def 6;
    hence g' * h' = e';
  end;
  now
    let x',y',z' be Element of G';
    reconsider x=x',y=y',z=z' as Element of G;
    (x'*y')*z' = (x*y)*z
      .= x*(y*z) by GROUP_1:def 4;
    hence (x'*y')*z' = x'*(y'*z');
  end;
  then reconsider G' as strict Group-like associative
  (non empty HGrWOpStr over O) by A1,GROUP_1:def 3,def 4;
  for G being Group, a being Action of O, the carrier of G
  st a = the action of G' & the multMagma of G = the multMagma of G'
  holds a is distributive by Def5;
  then reconsider G' as distributive Group-like associative
  (non empty HGrWOpStr over O) by Def5;
  dom(the multF of G) = [:the carrier of G',the carrier of G':]
  by FUNCT_2:def 1;
  then the multF of G' = (the multF of G)||the carrier of G' by RELAT_1:97;
  then
A2: G' is Subgroup of G by GROUP_2:def 5;
  now
    let o be Element of O;
A3: dom(G'^o) = the carrier of G by FUNCT_2:def 1;
    now
      per cases;
      suppose
A4:     o in O;
        then G'^o=(the action of G').o by Def6;
        hence G'^o = G^o by A4,Def6;
      end;
      suppose not o in O;
        then G'^o = id the carrier of G' & G^o = id the carrier of G by Def6;
        hence G'^o = G^o;
      end;
    end;
    hence G'^o = (G^o)|the carrier of G' by A3,RELAT_1:98;
  end;
  hence thesis by A2,Def7;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster strict StableSubgroup of G;
  correctness
  proof
    reconsider G' = the HGrWOpStr of G as StableSubgroup of G by Lm4;
    take G';
    thus thesis;
  end;
end;

:: like GROUP_2:68

Lm5: for O being set, G being GroupWithOperators of O,
H1,H2 being strict StableSubgroup of G
st the carrier of H1 = the carrier of H2 holds H1=H2
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be strict StableSubgroup of G;
  assume
A1: the carrier of H1 = the carrier of H2;
  reconsider H1'=H1,H2'=H2 as Subgroup of G by Def7;
A2: the multMagma of H1' = the multMagma of H2' by A1,GROUP_2:68;
A3: dom the action of H2 = O by FUNCT_2:def 1
    .= dom the action of H1 by FUNCT_2:def 1;
  now
    let x be set;
    assume
A4: x in dom the action of H2;
    then reconsider o=x as Element of O;
A5: H1^o = (the action of H1).o & H2^o = (the action of H2).o by A4,Def6;
    H1^o = (G^o)|the carrier of H2 by A1,Def7
      .= H2^o by Def7;
    hence (the action of H1).x = (the action of H2).x by A5;
  end;
  hence thesis by A2,A3,FUNCT_1:9;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  func (1).G -> strict StableSubgroup of G means
  :Def8:
  the carrier of it = {1_G};
  existence
  proof
    set G'=(1).G;
    consider H be non empty HGrWOpStr over O such that
A1: H is strict distributive Group-like associative &
    G' = the multMagma of H by Lm3;
    reconsider H as strict GroupWithOperators of O by A1;
A2: the carrier of H c= the carrier of G &
    the multF of H = (the multF of G)||the carrier of H by A1,GROUP_2:def 5;
    then
A3: H is Subgroup of G by GROUP_2:def 5;
    now
      let o be Element of O;
      reconsider f'=H^o,f=(G^o)|the carrier of H as Function;
A4:   dom f'= the carrier of (1).G by A1,FUNCT_2:def 1;
A5:   dom f = dom((G^o)*(id the carrier of H)) by RELAT_1:94
        .= dom(G^o) /\ the carrier of H by FUNCT_1:37
        .= (the carrier of G) /\ the carrier of H by FUNCT_2:def 1
        .= the carrier of (1).G by A1,A2,XBOOLE_1:28;
      now
        let x be set;
        assume
A6:     x in dom f;
        then x in {1_G} by A5,GROUP_2:def 7;
        then
A7:     x = 1_G by TARSKI:def 1;
        then x = 1_H by A3,GROUP_2:53;
        then
A8:     f'.x = 1_H by GROUP_6:40;
A9:     x in dom id the carrier of H by A1,A5,A6,RELAT_1:71;
        f.x = ((G^o)*(id the carrier of H)).x by RELAT_1:94
          .= (G^o).((id the carrier of H).x) by A9,FUNCT_1:23
          .= (G^o).x by A1,A5,A6,FUNCT_1:35
          .= 1_G by A7,GROUP_6:40;
        hence f.x = f'.x by A3,A8,GROUP_2:53;
      end;
      hence H^o = (G^o)|the carrier of H by A4,A5,FUNCT_1:9;
    end;
    then reconsider H as strict StableSubgroup of G by A3,Def7;
    take H;
    thus thesis by A1,GROUP_2:def 7;
  end;
  uniqueness by Lm5;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  func (Omega).G -> strict StableSubgroup of G equals

  the HGrWOpStr of G;
  correctness by Lm4;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be StableSubgroup of G;
  attr IT is normal means
  :Def10:
  for H being strict Subgroup of G
  st H = the multMagma of IT holds H is normal;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster strict normal StableSubgroup of G;
  existence
  proof
    set H=(1).G;
    set H'=H;
    reconsider H as StableSubgroup of G;
    take H';
    now
      let H'' be strict Subgroup of G;
      assume
A1:   H'' = the multMagma of H;
      reconsider G'=G as Group;
A2:   the multF of (1).G'
      = (the multF of G')||the carrier of (1).G' by GROUP_2:def 5;
      the carrier of (1).G' = {1_G'} by GROUP_2:def 7
        .= the carrier of (1).G by Def8;
      hence H'' is normal by A1,A2,GROUP_2:def 5;
    end;
    hence thesis by Def10;
  end;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  cluster normal StableSubgroup of H;
  existence
  proof
    reconsider H'=(1).H as GroupWithOperators of O;
    reconsider H' as StableSubgroup of H;
    take H';
    now
      let H'' be strict Subgroup of H;
      assume
A1:   the multMagma of H' = H'';
      reconsider H as Group;
      the carrier of H'' = {1_H} by A1,Def8;
      then H'' = (1).H by GROUP_2:def 7;
      hence H'' is normal;
    end;
    hence thesis by Def10;
  end;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster (1).G -> normal;
  correctness
  proof
    now
      let H be strict Subgroup of G;
      reconsider G'=G as Group;
      reconsider H'=H as strict Subgroup of G';
      assume H = the multMagma of (1).G;
      then the carrier of H = {1_G} by Def8;
      then H' = (1).G' by GROUP_2:def 7;
      hence H is normal;
    end;
    hence thesis by Def10;
  end;
  cluster (Omega).G -> normal;
  correctness
  proof
    now
      let H be strict Subgroup of G;
      reconsider G'=G as Group;
      reconsider H'=H as strict Subgroup of G';
      assume H = the multMagma of (Omega).G;
      then H' = (Omega).G';
      hence H is normal;
    end;
    hence thesis by Def10;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  func the_stable_subgroups_of G -> set means
  :Def11:
  for x being set holds x in it iff x is strict StableSubgroup of G;
  existence
  proof
    defpred P[set] means ex H being strict StableSubgroup of G
    st $1 = the carrier of H;
    consider B being set such that
A1: for x being set holds x in B iff x in bool the carrier of G & P[x]
    from XBOOLE_0:sch 1;
    defpred P[set,set] means ex H being strict StableSubgroup of G
    st $2 = H & $1 = the carrier of H;
A2: for x,y1,y2 being set st P[x,y1] & P[x,y2] holds y1 = y2 by Lm5;
    consider f being Function such that
A3: for x,y being set holds [x,y] in f iff x in B & P[x,y]
    from FUNCT_1:sch 1(A2);
    for x being set holds x in B iff ex y being set st [x,y] in f
    proof
      let x be set;
      thus x in B implies ex y being set st [x,y] in f
      proof
        assume
A4:     x in B;
        then consider H being strict StableSubgroup of G such that
A5:     x = the carrier of H by A1;
        reconsider y = H as set;
        take y;
        thus thesis by A3,A4,A5;
      end;
      given y be set such that
A6:   [x,y] in f;
      thus thesis by A3,A6;
    end;
    then
A7: B = dom f by RELAT_1:def 4;
    for y being set holds y in rng f iff y is strict StableSubgroup of G
    proof
      let y be set;
      thus y in rng f implies y is strict StableSubgroup of G
      proof
        assume y in rng f;
        then consider x be set such that
A8:     x in dom f & y = f.x by FUNCT_1:def 5;
        [x,y] in f by A8,FUNCT_1:def 4;
        then ex H being strict StableSubgroup of G st y = H &
        x = the carrier of H by A3;
        hence thesis;
      end;
      assume y is strict StableSubgroup of G;
      then reconsider H = y as strict StableSubgroup of G;
      reconsider x = the carrier of H as set;
      H is Subgroup of G by Def7;
      then the carrier of H c= the carrier of G by GROUP_2:def 5;
      then
A9:   x in dom f by A1,A7;
      then [x,y] in f by A3,A7;
      then y = f.x by A9,FUNCT_1:def 4;
      hence thesis by A9,FUNCT_1:def 5;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let A1,A2 be set;
    defpred P[set] means $1 is strict StableSubgroup of G;
    assume
A10: for x being set holds x in A1 iff P[x];
    assume
A11: for x being set holds x in A2 iff P[x];
    thus thesis from XBOOLE_0:sch 2(A10,A11);
  end;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster the_stable_subgroups_of G -> non empty;
  correctness
  proof
    (1).G in the_stable_subgroups_of G by Def11;
    hence thesis;
  end;
end;

definition
  let IT be Group;
  attr IT is simple means
  :Def12:
  IT is not trivial &
  not ex H being strict normal Subgroup of IT st H <> (Omega).IT & H <> (1).IT;
end;

Lm6: Group_of_Perm 2 is simple
proof
  set G = Group_of_Perm 2;
A1: now
    let H be strict normal Subgroup of G;
    assume
A2: H <> (Omega).G;
    assume
A3: H <> (1).G;
    the carrier of H c= the carrier of G by GROUP_2:def 5;
    then
A4: the carrier of H c= {<*1,2*>,<*2,1*>} by MATRIX_2:def 13,MATRIX_7:3;
    1_G in H by GROUP_2:55;
    then 1_G in the carrier of H by STRUCT_0:def 5;
    then {1_G} c= the carrier of H by ZFMISC_1:37;
    then {<*1,2*>} c= the carrier of H by FINSEQ_2:61,MATRIX_2:28;
    then
A5: <*1,2*> in the carrier of H by ZFMISC_1:37;
    per cases by A4,ZFMISC_1:42;
    suppose the carrier of H = {};
      hence contradiction;
    end;
    suppose the carrier of H = {<*1,2*>};
      then {1_G} = the carrier of H by FINSEQ_2:61,MATRIX_2:28;
      hence contradiction by A3,GROUP_2:def 7;
    end;
    suppose the carrier of H = {<*2,1*>};
      then <*2,1*>.1 = <*1,2*>.1 by A5,TARSKI:def 1;
      then 2 = <*1,2*>.1 by FINSEQ_1:61;
      hence contradiction by FINSEQ_1:61;
    end;
    suppose the carrier of H = {<*1,2*>,<*2,1*>};
      then the carrier of H = the carrier of G by MATRIX_2:def 13,MATRIX_7:3;
      hence contradiction by A2,GROUP_2:70;
    end;
  end;
  now
    assume G is trivial;
    then consider e be set such that
A6: the carrier of G = {e} by GROUP_6:def 2;
    Permutations 2 = {e} by A6,MATRIX_2:def 13;
    then <*2,1*> = <*1,2*> by MATRIX_7:3,ZFMISC_1:9;
    then 2 = <*1,2*>.1 by FINSEQ_1:61;
    hence contradiction by FINSEQ_1:61;
  end;
  hence thesis by A1,Def12;
end;

registration
  cluster strict simple Group;
  existence by Lm6;
end;

:: ALG I.4.4 Definition 7

definition
  let O be set;
  let IT be GroupWithOperators of O;
  attr IT is simple means
  :Def13:
  IT is not trivial &
  not ex H being strict normal StableSubgroup of IT
  st H <> (Omega).IT & H <> (1).IT;
end;

Lm7: for O being set, G being GroupWithOperators of O,
N being normal StableSubgroup of G holds
the multMagma of N is strict normal Subgroup of G
proof
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  set H = the multMagma of N;
  reconsider H as non empty multMagma;
  now
    set e=1_N;
    reconsider e'=e as Element of H;
    take e';
    let h' be Element of H;
    reconsider h=h' as Element of N;
    h'*e' = h*e
      .= h by GROUP_1:def 5;
    hence h' * e' = h';
    e'*h' = e*h
      .= h by GROUP_1:def 5;
    hence e' * h' = h';
    set g=h";
    reconsider g'=g as Element of H;
    take g';
    h'*g' = h*g
      .= 1_N by GROUP_1:def 6;
    hence h' * g' = e';
    g'*h' = g*h
      .= 1_N by GROUP_1:def 6;
    hence g' * h' = e';
  end;
  then reconsider H as Group-like (non empty multMagma) by GROUP_1:def 3;
  N is Subgroup of G by Def7;
  then the carrier of H c= the carrier of G &
  the multF of H = (the multF of G)||the carrier of H by GROUP_2:def 5;
  then reconsider H as Subgroup of G by GROUP_2:def 5;
  H is normal by Def10;
  hence thesis;
end;

Lm8: for G1, G2 being Group, A1 being Subset of G1, A2 being Subset of G2,
H1 being strict Subgroup of G1, H2 being strict Subgroup of G2
st the multMagma of G1 = the multMagma of G2 & A1 = A2 & H1 = H2
holds A1 * H1 = A2 * H2 & H1 * A1 = H2 * A2
proof
  let G1, G2 be Group;
  let A1 be Subset of G1;
  let A2 be Subset of G2;
  let H1 be strict Subgroup of G1;
  let H2 be strict Subgroup of G2;
  assume
A1: the multMagma of G1 = the multMagma of G2;
  assume
A2: A1 = A2;
  assume
A3: H1 = H2;
A4: now
    let A1,B1 be Subset of G1;
    let A2,B2 be Subset of G2;
    assume
A5: A1=A2 & B1=B2;
    set X={g*h where g,h is Element of G1: g in A1 & h in B1};
    set Y={g*h where g,h is Element of G2: g in A2 & h in B2};
A6: now
      let x be set;
      assume x in X;
      then consider g,h be Element of G1 such that
A7:   x=g*h & g in A1 & h in B1;
      set g'=g;
      set h'=h;
      reconsider g',h' as Element of G2 by A1;
      g*h = g'*h' by A1;
      hence x in Y by A5,A7;
    end;
    now
      let x be set;
      assume x in Y;
      then consider g,h be Element of G2 such that
A8:   x=g*h & g in A2 & h in B2;
      reconsider g'=g,h'=h as Element of G1 by A1;
      g*h = g'*h' by A1;
      hence x in X by A5,A8;
    end;
    hence X=Y by A6,TARSKI:2;
  end;
  hence A1 * H1 = A2 * H2 by A2,A3;
  thus thesis by A2,A3,A4;
end;

registration
  let O be set;
  cluster strict simple GroupWithOperators of O;
  existence
  proof
    set Gp2 = Group_of_Perm 2;
    consider G be non empty HGrWOpStr over O such that
A1: G is strict distributive Group-like associative
    & Gp2 = the multMagma of G by Lm3;
    reconsider G as strict GroupWithOperators of O by A1;
    take G;
    now
      assume
A2:   G is not simple;
      per cases by A2,Def13;
      suppose G is trivial;
        then consider e be set such that
A3:     the carrier of G = {e} by GROUP_6:def 2;
        Gp2 is trivial by A1,A3,GROUP_6:def 2;
        hence contradiction by Def12,Lm6;
      end;
      suppose ex H being strict normal StableSubgroup of G
        st H <> (Omega).G & H <> (1).G;
        then consider H be strict normal StableSubgroup of G such that
A4:     H <> (Omega).G & H <> (1).G;
        reconsider H' = the multMagma of H as strict normal Subgroup of G
        by Lm7;
        reconsider G' = G as Group;
        reconsider H' as strict normal Subgroup of G';
        set H''=H';
        the carrier of H'' c= the carrier of G' &
        the multF of H'' = (the multF of G')||the carrier of H''
        by GROUP_2:def 5;
        then reconsider H'' as strict Subgroup of Gp2 by A1,GROUP_2:def 5;
        now
          let A be Subset of Gp2;
          reconsider A'=A as Subset of G' by A1;
          A * H'' = A' * H' by A1,Lm8
            .= H' * A' by GROUP_3:143;
          hence A * H'' = H'' * A by A1,Lm8;
        end;
        then reconsider H'' as strict normal Subgroup of Gp2 by GROUP_3:143;
A5:     now
          assume H'' = (1).Gp2;
          then
A6:       the carrier of H'' = {1_Gp2} by GROUP_2:def 7;
          reconsider e = 1_Gp2 as Element of G by A1;
          now
            let h be Element of G;
            reconsider h'=h as Element of Gp2 by A1;
            h * e = h' * 1_Gp2 by A1
              .= h' by GROUP_1:def 5;
            hence h * e = h;
            e * h = 1_Gp2 * h' by A1
              .= h' by GROUP_1:def 5;
            hence e * h = h;
          end;
          then the carrier of H = {1_G} by A6,GROUP_1:def 5;
          hence contradiction by A4,Def8;
        end;
        H'' <> (Omega).Gp2 by A1,A4,Lm5;
        hence contradiction by A5,Def12,Lm6;
      end;
    end;
    hence thesis;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func Cosets N -> set means
  :Def14:
  for H being strict normal Subgroup of G st H = the multMagma of N
  holds it = Cosets H;
  existence
  proof
    reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
    set x = Cosets H;
    take x;
    let H be strict normal Subgroup of G;
    assume H = the multMagma of N;
    hence thesis;
  end;
  uniqueness
  proof
    let y1,y2 be set;
    assume
A1: for H being strict normal Subgroup of G
    st H = the multMagma of N holds y1 = Cosets H;
    assume
A2: for H being strict normal Subgroup of G
    st H = the multMagma of N holds y2 = Cosets H;
    reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
    y1 = Cosets H by A1;
    hence thesis by A2;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func CosOp N -> BinOp of Cosets N means
  :Def15:
  for H being strict normal Subgroup of G st H = the multMagma of N
  holds it = CosOp H;
  existence
  proof
    reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
    Cosets N = Cosets H by Def14;
    then reconsider x = CosOp H as BinOp of Cosets N;
    take x;
    let H be strict normal Subgroup of G;
    assume H = the multMagma of N;
    hence thesis;
  end;
  uniqueness
  proof
    let y1,y2 be BinOp of Cosets N;
    assume
A1: for H being strict normal Subgroup of G
    st H = the multMagma of N holds y1 = CosOp H;
    assume
A2: for H being strict normal Subgroup of G
    st H = the multMagma of N holds y2 = CosOp H;
    reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
    y1 = CosOp H by A1;
    hence y1=y2 by A2;
  end;
end;

Lm9: for G being Group, N being normal Subgroup of G,
A being Element of Cosets N, g being Element of G holds g in A iff A = g * N
proof
  let G be Group;
  let N be normal Subgroup of G;
  let A be Element of Cosets N;
  let g be Element of G;
  hereby
    assume
A1: g in A;
    consider a be Element of G such that
A2: A = a * N by GROUP_2:def 15;
    consider h be Element of G such that
A3: g = a*h & h in N by A1,A2,GROUP_2:125;
    g" * a = h"*a"*a by A3,GROUP_1:25
      .= h"*(a"*a) by GROUP_1:def 4
      .= h"*1_G by GROUP_1:def 6
      .= h" by GROUP_1:def 5;
    then g" * a in N by A3,GROUP_2:60;
    hence A = g * N by A2,GROUP_2:137;
  end;
  g = g * 1_G & 1_G in N by GROUP_1:def 5,GROUP_2:55;
  hence thesis by GROUP_2:125;
end;

Lm10: for O being set, o being Element of O, G being GroupWithOperators of O,
H being StableSubgroup of G, g being Element of G st g in H holds (G^o).g in H
proof
  let O be set;
  let o be Element of O;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  let g be Element of G;
  assume g in H;
  then
A1: g in the carrier of H by STRUCT_0:def 5;
  set f=G^o;
  f.g = (f|the carrier of H).g by A1,FUNCT_1:72;
  then
A2: f.g = (H^o).g by Def7;
  (H^o).g in the carrier of H by A1,FUNCT_2:7;
  hence thesis by A2,STRUCT_0:def 5;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func CosAc N -> Action of O, Cosets N means
  :Def16:
  for o being Element of O holds
  it.o = {[A,B] where A,B is Element of Cosets N:
  ex g,h being Element of G st g in A & h in B & h = (G^o).g}
  if O is not empty otherwise it=[:{},{id Cosets N}:];
  existence
  proof
A1: now
      assume
A2:   O is not empty;
      deffunc F(set) = {[A,B] where A,B is Element of Cosets N:
      for o being Element of O st $1=o holds
      ex g,h being Element of G st g in A & h in B & h = (G^o).g};
      reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
A3:   Cosets N = Cosets H by Def14;
A4:   now
        let x be set;
        assume
A5:     x in O;
        set f=F(x);
A6:     now
          let y be set;
          assume y in f;
          then consider A,B be Element of Cosets N such that
A7:       y = [A,B] & for o being Element of O st x=o holds
          ex g,h being Element of G st g in A & h in B & h = (G^o).g;
          reconsider A,B as set;
          take A,B;
          thus y = [A,B] by A7;
        end;
        now
          let y,y1,y2 be set;
          assume [y,y1] in f;
          then consider A1,B1 be Element of Cosets N such that
A8:       [y,y1] = [A1,B1] & for o being Element of O st x=o holds
          ex g,h being Element of G st g in A1 & h in B1 & h = (G^o).g;
          assume [y,y2] in f;
          then consider A2,B2 be Element of Cosets N such that
A9:       [y,y2] = [A2,B2] & for o being Element of O st x=o holds
          ex g,h being Element of G st g in A2 & h in B2 & h = (G^o).g;
A10:      y=A1 & y1=B1 & y=A2 & y2=B2 by A8,A9,ZFMISC_1:33;
          reconsider o=x as Element of O by A5;
          consider g1,h1 be Element of G such that
A11:      g1 in A1 & h1 in B1 & h1 = (G^o).g1 by A8;
          consider g2,h2 be Element of G such that
A12:      g2 in A2 & h2 in B2 & h2 = (G^o).g2 by A9;
          reconsider A1,A2,B1,B2 as Element of Cosets H by Def14;
          set f=G^o;
          A1 = g1 * H & A2 = g2 * H by A11,A12,Lm9;
          then g2" * g1 in H by A10,GROUP_2:137;
          then g2" * g1 in the carrier of H by STRUCT_0:def 5;
          then g2" * g1 in N by STRUCT_0:def 5;
          then f.(g2" * g1) in N by Lm10;
          then f.(g2") * f.g1 in N by GROUP_6:def 7;
          then h2" * h1 in N by A11,A12,GROUP_6:41;
          then h2" * h1 in the carrier of N by STRUCT_0:def 5;
          then
A13:      h2" * h1 in H by STRUCT_0:def 5;
          B1 = h1 * H & B2 = h2 * H by A11,A12,Lm9;
          hence y1 = y2 by A10,A13,GROUP_2:137;
        end;
        then reconsider f as Function by A6,FUNCT_1:def 1,RELAT_1:def 1;
        now
          let y1 be set;
          hereby
            assume
A14:        y1 in Cosets N;
            then y1 in Cosets H by Def14;
            then consider g be Element of G such that
A15:        y1 = g * H & y1 = H * g by GROUP_6:15;
            reconsider o=x as Element of O by A5;
            reconsider A=y1 as Element of Cosets N by A14;
            set h = (G^o).g;
            reconsider B = h * H as Element of Cosets N by A3,GROUP_2:def 15;
            reconsider y2=B as set;
            take y2;
            now
              let o be Element of O;
              assume
A16:          x=o;
              take g,h;
              thus g in A by A3,A15,Lm9;
              thus h in B by A3,Lm9;
              thus h = (G^o).g by A16;
            end;
            hence [y1,y2] in f;
          end;
          given y2 be set such that
A17:      [y1,y2] in f;
          consider A,B be Element of Cosets N such that
A18:      [y1,y2] = [A,B] & for o being Element of O st x=o holds
          ex g,h being Element of G st g in A & h in B & h = (G^o).g by A17;
          A in Cosets N by A3;
          hence y1 in Cosets N by A18,ZFMISC_1:33;
        end;
        then
A19:    dom f = Cosets N by RELAT_1:def 4;
        now
          let y2 be set;
          assume y2 in rng f;
          then consider y1 be set such that
A20:      [y1,y2] in f by RELAT_1:def 5;
          consider A,B be Element of Cosets N such that
A21:      [y1,y2] = [A,B] & for o being Element of O st x=o holds
          ex g,h being Element of G st g in A & h in B & h = (G^o).g by A20;
          B in Cosets N by A3;
          hence y2 in Cosets N by A21,ZFMISC_1:33;
        end;
        then rng f c= Cosets N by TARSKI:def 3;
        hence F(x) in Funcs(Cosets N,Cosets N) by A19,FUNCT_2:def 2;
      end;
      ex f being Function of O,Funcs(Cosets N,Cosets N) st
      for x being set st x in O holds f.x = F(x) from FUNCT_2:sch 2(A4);
      then consider IT be Function of O,Funcs(Cosets N,Cosets N) such that
A22:  for x being set st x in O holds IT.x = F(x);
      reconsider IT as Action of O, Cosets N;
      take IT;
      let o be Element of O;
      reconsider x=o as set;
      set X = {[A,B] where A,B is Element of Cosets N:
      for o being Element of O st x=o holds
      ex g,h being Element of G st g in A & h in B & h = (G^o).g};
      set Y = {[A,B] where A,B is Element of Cosets N:
      ex g,h being Element of G st g in A & h in B & h = (G^o).g};
A23:  now
        let y be set;
        hereby
          assume y in X;
          then consider A,B be Element of Cosets N such that
A24:      y = [A,B] & for o being Element of O st x=o holds
          ex g,h being Element of G st g in A & h in B & h = (G^o).g;
          ex g,h being Element of G st g in A & h in B & h = (G^o).g by A24;
          hence y in Y by A24;
        end;
        assume y in Y;
        then consider A,B be Element of Cosets N such that
A25:    y = [A,B] & ex g,h being Element of G
        st g in A & h in B & h = (G^o).g;
        for o being Element of O st x=o holds
        ex g,h being Element of G st g in A & h in B & h = (G^o).g by A25;
        hence y in X by A25;
      end;
      IT.o = X by A2,A22;
      hence IT.o = Y by A23,TARSKI:2;
    end;
    now
      assume O is empty;
      then reconsider IT=[:{},{id Cosets N}:] as Action of O, Cosets N by Lm2;
      take IT;
      thus IT=[:{},{id Cosets N}:];
    end;
    hence thesis by A1;
  end;
  uniqueness
  proof
    now
      assume O is not empty;
      let IT1,IT2 be Action of O, Cosets N;
      assume
A26:  for o being Element of O holds
      IT1.o = {[A,B] where A,B is Element of Cosets N:
      ex g,h being Element of G st g in A & h in B & h = (G^o).g};
      assume
A27:  for o being Element of O holds
      IT2.o = {[A,B] where A,B is Element of Cosets N:
      ex g,h being Element of G st g in A & h in B & h = (G^o).g};
A28:  dom IT1 = O & dom IT2 = O by FUNCT_2:def 1;
      now
        let x be set;
        assume x in dom IT1;
        then reconsider o=x as Element of O;
        IT1.o = {[A,B] where A,B is Element of Cosets N:
        ex g,h being Element of G st g in A & h in B & h = (G^o).g} by A26;
        hence IT1.x=IT2.x by A27;
      end;
      hence IT1=IT2 by A28,FUNCT_1:9;
    end;
    hence thesis;
  end;
  correctness;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func G./.N -> HGrWOpStr over O equals
  HGrWOpStr (# Cosets N, CosOp N, CosAc N #);
  correctness;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  cluster G./.N -> non empty;
  correctness
  proof
    reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
    Cosets N = Cosets H by Def14;
    hence thesis;
  end;
  cluster G./.N -> distributive Group-like associative;
  correctness
  proof
    set G' = the multMagma of G./.N;
    reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
    the carrier of G./.N = the carrier of G./.H by Def14;
    then
A1: G' is Group-like associative by Def15;
A2: now
      set e'=1_(G./.H);
      reconsider e=e' as Element of G./.N by Def14;
      take e;
      let h be Element of G./.N;
      reconsider h'=h as Element of G./.H by Def14;
      h*e = h'*e' by Def15
        .= h' by GROUP_1:def 5;
      hence h * e = h;
      e*h = e'*h' by Def15
        .= h' by GROUP_1:def 5;
      hence e * h = h;
      set g=h'";
      set g'=g;
      reconsider g as Element of G./.N by Def14;
      take g;
      h*g = h'*g' by Def15
        .= 1_(G./.H) by GROUP_1:def 6;
      hence h * g = e;
      g*h = g'*h' by Def15
        .= 1_(G./.H) by GROUP_1:def 6;
      hence g * h = e;
    end;
A3: now
      let x,y,z be Element of G./.N;
      reconsider x'=x,y'=y,z'=z as Element of G';
A4:   (x'*y')*z' = (x*y)*z;
      x'*(y'*z') = x*(y*z);
      hence (x*y)*z = x*(y*z) by A1,A4,GROUP_1:def 4;
    end;
    now
      let G' be Group;
      let a be Action of O, the carrier of G';
      assume
A5:   a = the action of G./.N;
      assume
A6:   the multMagma of G' = the multMagma of G./.N;
      now
        let o be Element of O;
        assume
A7:     o in O;
        then
A8:     a.o = {[A,B] where A,B is Element of Cosets N: ex g,h
        being Element of G st g in A & h in B & h = (G^o).g} by A5,Def16;
        a.o in Funcs(Cosets N, Cosets N) by A5,A7,FUNCT_2:7;
        then consider f be Function such that
A9:     a.o = f & dom f = Cosets N & rng f c= Cosets N by FUNCT_2:def 2;
        reconsider f as Function of the carrier of G',the carrier of G'
        by A6,A9,FUNCT_2:4;
        now
          let A1,A2 be Element of G';
          set A3=A1*A2;
          set B1=f.A1,B2=f.A2,B3=f.A3;
A10:      [A1,B1] in f & [A2,B2] in f & [A3,B3] in f by A6,A9,FUNCT_1:8;
          then consider A1',B1' be Element of Cosets N such that
A11:      [A1,B1] = [A1',B1'] & ex g1,h1 being Element of G
          st g1 in A1' & h1 in B1' & h1 = (G^o).g1 by A8,A9;
          consider A2',B2' be Element of Cosets N such that
A12:      [A2,B2] = [A2',B2'] & ex g2,h2 being Element of G
          st g2 in A2' & h2 in B2' & h2 = (G^o).g2 by A8,A9,A10;
          consider A3',B3' be Element of Cosets N such that
A13:      [A3,B3] = [A3',B3'] & ex g3,h3 being Element of G
          st g3 in A3' & h3 in B3' & h3 = (G^o).g3 by A8,A9,A10;
          consider g1,h1 be Element of G such that
A14:      g1 in A1' & h1 in B1' & h1 = (G^o).g1 by A11;
          consider g2,h2 be Element of G such that
A15:      g2 in A2' & h2 in B2' & h2 = (G^o).g2 by A12;
          consider g3,h3 be Element of G such that
A16:      g3 in A3' & h3 in B3' & h3 = (G^o).g3 by A13;
          reconsider A1',A2',A3',B1',B2',B3' as Element of Cosets H by Def14;
A17:      A2' = g2 * H & B2' = h2 * H by A15,Lm9;
A18:      A3' = g3 * H & B3' = h3 * H by A16,Lm9;
          reconsider A1',A2',B1',B2' as Element of G./.H;
A19:      A2=g2 * H & B2=h2 * H by A12,A17,ZFMISC_1:33;
A20:      A3=g3 * H & B3=h3 * H by A13,A18,ZFMISC_1:33;
A21:      @((nat_hom H).g1)=(nat_hom H).g1;
A22:      @((nat_hom H).g2)=(nat_hom H).g2;
A23:      (nat_hom H).g1=g1*H by GROUP_6:def 9;
A24:      (nat_hom H).g2=g2*H by GROUP_6:def 9;
          A1*A2 = (the multF of G').(A1',A2') by A11,A17,A19,ZFMISC_1:33
            .= @(A1'*A2') by A6,Def15
            .= @A1' * @A2' by GROUP_6:25;
          then A1*A2 = (g1 * H)*(g2 * H) by A14,A17,Lm9
            .= ((nat_hom H).g1)*((nat_hom H).g2) by A21,A22,A23,A24,GROUP_6:24
            .= (nat_hom H).(g1*g2) by GROUP_6:def 7
            .= (g1*g2) * H by GROUP_6:def 9;
          then g3 * H = (g1*g2) * H by A13,A18,ZFMISC_1:33;
          then g3" * (g1*g2) in H by GROUP_2:137;
          then g3" * (g1*g2) in the carrier of H by STRUCT_0:def 5;
          then g3" * (g1*g2) in N by STRUCT_0:def 5;
          then (G^o).(g3" * (g1*g2)) in N by Lm10;
          then (G^o).(g3") * ((G^o).(g1*g2)) in N by GROUP_6:def 7;
          then (G^o).(g3") * (((G^o).g1)*(G^o).g2) in N by GROUP_6:def 7;
          then h3" * (h1*h2) in N by A14,A15,A16,GROUP_6:41;
          then h3" * (h1*h2) in the carrier of N by STRUCT_0:def 5;
          then
A25:      h3" * (h1*h2) in H by STRUCT_0:def 5;
A26:      @((nat_hom H).h1)=(nat_hom H).h1;
A27:      @((nat_hom H).h2)=(nat_hom H).h2;
A28:      (nat_hom H).h1=h1*H by GROUP_6:def 9;
A29:      (nat_hom H).h2=h2*H by GROUP_6:def 9;
          B1*B2 = (the multF of G').(B1',B2') by A11,A17,A19,ZFMISC_1:33
            .= @(B1'*B2') by A6,Def15
            .= @B1' * @B2' by GROUP_6:25;
          then B1*B2 = (h1 * H)*(h2 * H) by A14,A17,Lm9
            .= ((nat_hom H).h1)*((nat_hom H).h2) by A26,A27,A28,A29,GROUP_6:24
            .= (nat_hom H).(h1*h2) by GROUP_6:def 7
            .= (h1*h2) * H by GROUP_6:def 9;
          hence f.A3 = f.A1 * f.A2 by A20,A25,GROUP_2:137;
        end;
        hence a.o is Homomorphism of G',G' by A9,GROUP_6:def 7;
      end;
      hence a is distributive by Def4;
    end;
    hence thesis by A2,A3,Def5,GROUP_1:def 3,def 4;
  end;
end;

:: ALG I.4.2 Definition 3

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  let f be Function of G, H;
  attr f is homomorphic means
  :Def18:
  for o being Element of O, g being Element of G
  holds f.((G^o).g) = (H^o).(f.g);
end;

registration
  let O be set;
  let G,H be GroupWithOperators of O;
  cluster multiplicative homomorphic Function of G, H;
  existence
  proof
    set f = 1:(G,H);
    reconsider f as Function of G, H;
    take f;
    thus f is multiplicative;
    let o be Element of O;
    let g be Element of G;
    (H^o).(f.g) = (H^o).(1_H) by GROUP_6:def 8
      .= 1_H by GROUP_6:40;
    hence thesis by GROUP_6:def 8;
  end;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  mode Homomorphism of G, H is multiplicative homomorphic Function of G, H;
end;

:: like GROUP_6:48

Lm11: for O being set, G,H,I being GroupWithOperators of O,
h being Homomorphism of G, H for h1 being Homomorphism of H, I holds
h1 * h is Homomorphism of G,I
proof
  let O be set;
  let G,H,I be GroupWithOperators of O;
  let h be Homomorphism of G, H;
  let h1 be Homomorphism of H, I;
  reconsider f = h1 * h as Function of G, I;
  now
    let o be Element of O;
    let g be Element of G;
    thus f.((G^o).g) = h1.(h.((G^o).g)) by FUNCT_2:21
      .= h1.((H^o).(h.g)) by Def18
      .= (I^o).(h1.(h.g)) by Def18
      .= (I^o).(f.g) by FUNCT_2:21;
  end;
  hence thesis by Def18;
end;

definition
  let O be set;
  let G,H,I be GroupWithOperators of O;
  let h be Homomorphism of G, H;
  let h1 be Homomorphism of H, I;
  redefine func h1 * h -> Homomorphism of G,I;
  correctness by Lm11;
end;

definition
 canceled 3;
  let O be set;
  let G,H be GroupWithOperators of O;
  pred G,H are_isomorphic means
  :Def22:
  ex h being Homomorphism of G,H st h is bijective;
  reflexivity
  proof
    let G be GroupWithOperators of O;
    reconsider G'=G as Group;
    set h = id the carrier of G';
    now
      let o be Element of O;
      let g be Element of G;
      h.((G^o).g) = (G^o).g & h.g = g by FUNCT_1:34;
      hence h.((G^o).g) = (G^o).(h.g);
    end;
    then reconsider h as Homomorphism of G, G by Def18,GROUP_6:47;
    take h;
    rng h = the carrier of G by RELAT_1:71;
    then h is onto by FUNCT_2:def 3;
    hence thesis;
  end;
end;

:: like GROUP_6:77

Lm12: for O being set, G,H being GroupWithOperators of O holds
G,H are_isomorphic implies H,G are_isomorphic
proof
  let O be set;
  let G,H be GroupWithOperators of O;
  assume G,H are_isomorphic;
  then consider f be Homomorphism of G,H such that
A1: f is bijective by Def22;
  f is onto & f is one-to-one by A1;
  then
A2: rng f = the carrier of H & f is one-to-one by FUNCT_2:def 3;
A3: dom f = the carrier of G by FUNCT_2:def 1;
  set f' = f";
A4: dom f' = the carrier of H & rng f' = the carrier of G by A2,A3,FUNCT_1:55;
  then reconsider f' as Function of H,G by FUNCT_2:3;
A5: now
    let h1,h2 be Element of H;
    set g1=f'.h1;
    set g2=f'.h2;
    f.g1=h1 & f.g2=h2 by A2,FUNCT_1:57;
    hence f'.(h1*h2) = f'.(f.(g1*g2)) by GROUP_6:def 7
      .= f'.h1 * f'.h2 by A2,A3,FUNCT_1:56;
  end;
  now
    let o be Element of O;
    let h be Element of H;
    set g=f'.h;
    thus f'.((H^o).h) = f'.((H^o).(f.g)) by A2,FUNCT_1:57
      .= f'.(f.((G^o).g)) by Def18
      .= (G^o).(f'.h) by A2,A3,FUNCT_1:56;
  end;
  then reconsider f' as Homomorphism of H,G by A5,Def18,GROUP_6:def 7;
  f' is one-to-one by A2;
  then
A6: f' is onto & f' is one-to-one by A4,FUNCT_2:def 3;
  take f';
  thus thesis by A6;
end;

definition
  let O be set, G,H be GroupWithOperators of O;
  redefine pred G,H are_isomorphic;
  symmetry by Lm12;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let N be normal StableSubgroup of G;
  func nat_hom N -> Homomorphism of G, G./.N means
  :Def23:
  for H being strict normal Subgroup of G st H = the multMagma of N
  holds it = nat_hom H;
  existence
  proof
    set H = the multMagma of N;
    reconsider H as strict normal Subgroup of G by Lm7;
    set IT = nat_hom H;
    reconsider K = G./.N as GroupWithOperators of O;
    Cosets H = Cosets N by Def14;
    then reconsider IT' = IT as Function of G, K;
A1: now
      let a, b be Element of G;
      IT'.(a * b) = IT.a * IT.b by GROUP_6:def 7
        .= IT'.a * IT'.b by Def15;
      hence IT'.(a * b) = IT'.a * IT'.b;
    end;
    now
      let o be Element of O;
      let g be Element of G;
      per cases;
      suppose
A2:     O<>{};
        then (the action of K).o in Funcs(the carrier of K, the carrier of K)
        by FUNCT_2:7;
        then consider f be Function such that
A3:     f=(the action of K).o & dom f = the carrier of K &
        rng f c= the carrier of K by FUNCT_2:def 2;
A4:     (K^o).(IT'.g) = f.(IT'.g) by A2,A3,Def6;
A5:     f = {[A,B] where A,B is Element of Cosets N:
        ex g,h being Element of G st g in A & h in B & h = (G^o).g}
        by A2,A3,Def16;
        [IT'.g, f.(IT'.g)] in f by A3,FUNCT_1:def 4;
        then consider A,B be Element of Cosets N such that
A6:     [IT'.g, f.(IT'.g)] = [A,B] &
        ex g,h being Element of G st g in A & h in B & h = (G^o).g by A5;
A7:     IT'.g = A & f.(IT'.g) = B by A6,ZFMISC_1:33;
        consider g',h' be Element of G such that
A8:     g' in A & h' in B & h' = (G^o).g' by A6;
        reconsider A,B as Element of Cosets H by Def14;
        A = g' * H & B = h' * H by A8,Lm9;
        then g * H = g' * H by A7,GROUP_6:def 9;
        then g'" * g in H by GROUP_2:137;
        then g'" * g in the carrier of N by STRUCT_0:def 5;
        then g'" * g in N by STRUCT_0:def 5;
        then (G^o).(g'" * g) in N by Lm10;
        then (G^o).(g'" * g) in the carrier of N by STRUCT_0:def 5;
        then
A9:     (G^o).(g'" * g) in H by STRUCT_0:def 5;
A10:    (G^o).(g'" * g) = (G^o).(g'") * (G^o).g by GROUP_6:def 7
          .= ((G^o).g')" * (G^o).g by GROUP_6:41;
        IT'.((G^o).g) = ((G^o).g) * H by GROUP_6:def 9
          .= ((G^o).g') * H by A9,A10,GROUP_2:137
          .= B by A8,Lm9;
        hence IT'.((G^o).g) = (K^o).(IT'.g) by A4,A6,ZFMISC_1:33;
      end;
      suppose O={};
        then G^o = id the carrier of G & K^o = id the carrier of K by Def6;
        then (G^o).g = g & (K^o).(IT'.g) = IT'.g by FUNCT_1:35;
        hence IT'.((G^o).g) = (K^o).(IT'.g);
      end;
    end;
    then reconsider IT' as Homomorphism of G, K by A1,Def18,GROUP_6:def 7;
    reconsider IT' as Homomorphism of G, G./.N;
    take IT';
    let H be strict normal Subgroup of G;
    assume H = the multMagma of N;
    hence thesis;
  end;
  uniqueness
  proof
    let IT1,IT2 be Homomorphism of G, G./.N;
    assume
A11: for H being strict normal Subgroup of G st H = the multMagma of N
    holds IT1 = nat_hom H;
    assume
A12: for H being strict normal Subgroup of G st H = the multMagma of N
    holds IT2 = nat_hom H;
    reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
    IT1 = nat_hom H & IT2 = nat_hom H by A11,A12;
    hence thesis;
  end;
end;

:: like GROUP_6:40

Lm13: for O being set, G,H being GroupWithOperators of O,
g being Homomorphism of G,H holds g.(1_G)=1_H
proof
  let O be set;
  let G,H be GroupWithOperators of O;
  let g be Homomorphism of G,H;
  g.(1_G) = g.(1_G * 1_G) by GROUP_1:def 5
    .= g.(1_G) * g.(1_G) by GROUP_6:def 7;
  hence thesis by GROUP_1:15;
end;

:: like GROUP_6:41

Lm14: for O being set, G,H being GroupWithOperators of O,
a being Element of G, g being Homomorphism of G,H holds g.(a")=(g.a)"
proof
  let O be set;
  let G,H be GroupWithOperators of O;
  let a be Element of G;
  let g be Homomorphism of G,H;
  g.(a") * g.a = g.(a" * a) by GROUP_6:def 7
    .= g.(1_G) by GROUP_1:def 6
    .= 1_H by Lm13;
  hence thesis by GROUP_1:20;
end;

:: like GROUP_2:61

Lm15: for O being set, G being GroupWithOperators of O, A being Subset of G
st A <> {} &
(for g1,g2 being Element of G st g1 in A & g2 in A holds g1 * g2 in A) &
(for g being Element of G st g in A holds g" in A) &
(for o being Element of O, g being Element of G
st g in A holds (G^o).g in A) holds
ex H being strict StableSubgroup of G st the carrier of H = A
proof
  let O be set;
  let G be GroupWithOperators of O;
  let A be Subset of G;
  assume
A1: A <> {};
  assume
A2: for g1,g2 being Element of G st g1 in A & g2 in A holds g1 * g2 in A;
  assume
A3: for g being Element of G st g in A holds g" in A;
  assume
A4: for o being Element of O, g being Element of G st g in A
  holds (G^o).g in A;
  consider H' be strict Subgroup of G such that
A5: the carrier of H' = A by A1,A2,A3,GROUP_2:61;
  set A' = the carrier of H';
  set m' = the multF of H';
A6: now
    let H be non empty HGrWOpStr over O;
    let a' be Action of O,A';
    assume
A7: H = HGrWOpStr (#A',m',a'#);
    now
      let x,y,z be Element of H;
      reconsider x'=x,y'=y,z'=z as Element of H' by A7;
      (x*y)*z = (x'*y')*z' by A7
        .= x'*(y'*z') by GROUP_1:def 4;
      hence (x*y)*z = x*(y*z) by A7;
    end;
    hence H is associative by GROUP_1:def 4;
    now
      set e'=1_H';
      reconsider e=e' as Element of H by A7;
      take e;
      let h be Element of H;
      reconsider h'=h as Element of H' by A7;
      h*e = h'*e' by A7
        .= h' by GROUP_1:def 5;
      hence h * e = h;
      e*h = e'*h' by A7
        .= h' by GROUP_1:def 5;
      hence e * h = h;
      set g'=h'";
      reconsider g=g' as Element of H by A7;
      take g;
      h*g = h'*g' by A7
        .= 1_H' by GROUP_1:def 6;
      hence h * g = e;
      g*h = g'*h' by A7
        .= 1_H' by GROUP_1:def 6;
      hence g * h = e;
    end;
    hence H is Group-like by GROUP_1:def 3;
  end;
  per cases;
  suppose
A8: O is empty;
    set a' = [:{},{id A'}:];
    reconsider a' as Action of O,A' by A8,Lm2;
    set H = HGrWOpStr (#A',m',a'#);
    reconsider H as (non empty HGrWOpStr over O);
    now
      let G' be Group;
      let a be Action of O, the carrier of G';
      assume a = the action of H;
      assume the multMagma of G' = the multMagma of H;
      for o being Element of O st o in O
      holds a.o is Homomorphism of G', G' by A8;
      hence a is distributive by Def4;
    end;
    then reconsider H as GroupWithOperators of O by A6,Def5;
A9: the carrier of H c= the carrier of G &
    the multF of H = (the multF of G)||the carrier of H by GROUP_2:def 5;
    then
A10: H is Subgroup of G by GROUP_2:def 5;
    now
      let o be Element of O;
A11:  now
        let x,y be set;
        assume [x,y] in id the carrier of H;
        then
A12:    x in the carrier of H & x=y by RELAT_1:def 10;
        then [x,y] in id the carrier of G by A9,RELAT_1:def 10;
        hence [x,y] in (id the carrier of G)|the carrier of H
        by A12,RELAT_1:def 11;
      end;
A13:  now
        let x,y be set;
        assume [x,y] in (id the carrier of G)|the carrier of H;
        then
A14:    x in the carrier of H & [x,y] in id the carrier of G
        by RELAT_1:def 11;
        then x in the carrier of G & x=y by RELAT_1:def 10;
        hence [x,y] in id the carrier of H by A14,RELAT_1:def 10;
      end;
      H^o = id the carrier of H by A8,Def6
        .= (id the carrier of G)|the carrier of H by A11,A13,RELAT_1:def 2;
      hence H^o = (G^o)|the carrier of H by A8,Def6;
    end;
    then reconsider H as strict StableSubgroup of G by A10,Def7;
    take H;
    thus thesis by A5;
  end;
  suppose
A15: O is not empty;
    set a' = {[o,(G^o)|A'] where o is Element of O : not contradiction};
    now
      let x be set;
      assume x in a';
      then consider o be Element of O such that
A16:  x=[o,(G^o)|A'];
      thus ex y1,y2 being set st x = [y1,y2] by A16;
    end;
    then reconsider a' as Relation by RELAT_1:def 1;
A17: now
A18:  now
        let x be set;
        assume x in O;
        then reconsider o=x as Element of O;
        reconsider y = (G^o)|A' as set;
        take y;
        thus [x,y] in a';
      end;
      now
        let x be set;
        given y be set such that
A19:    [x,y] in a';
        consider o be Element of O such that
A20:    [x,y] = [o,(G^o)|A'] by A19;
        o in O by A15;
        hence x in O by A20,ZFMISC_1:33;
      end;
      hence dom a' = O by A18,RELAT_1:def 4;
    end;
    now
      let x,y1,y2 be set;
      assume [x,y1] in a';
      then consider o1 be Element of O such that
A21:  [x,y1]=[o1,(G^o1)|A'];
      assume [x,y2] in a';
      then consider o2 be Element of O such that
A22:  [x,y2]=[o2,(G^o2)|A'];
      x=o1 & y1=(G^o1)|A' & x=o2 & y2=(G^o2)|A' by A21,A22,ZFMISC_1:33;
      hence y1 = y2;
    end;
    then reconsider a' as Function by FUNCT_1:def 1;
    now
      let y be set;
      assume y in rng a';
      then consider x be set such that
A23:  x in dom a' & y = a'.x by FUNCT_1:def 5;
      [x,y] in a' by A23,FUNCT_1:8;
      then consider o be Element of O such that
A24:  [x,y]=[o,(G^o)|A'];
      now
        reconsider f = (G^o)|A' as Function;
        take f;
        thus y = f by A24,ZFMISC_1:33;
A25:    A' c= the carrier of G by GROUP_2:def 5;
        dom((G^o)|A') = dom((G^o)*(id A')) by RELAT_1:94
          .= dom(G^o) /\ A' by FUNCT_1:37
          .= (the carrier of G) /\ A' by FUNCT_2:def 1;
        hence
A27:    dom f = A' by A25,XBOOLE_1:28;
        now
          let y be set;
A28:      dom f = dom((G^o)*(id A')) by RELAT_1:94;
          assume y in rng f;
          then consider x be set such that
A29:      x in dom f & y = f.x by FUNCT_1:def 5;
A30:      y = ((G^o)*(id A')).x by A29,RELAT_1:94
            .= (G^o).((id A').x) by A28,A29,FUNCT_1:22
            .= (G^o).x by A27,A29,FUNCT_1:35;
          thus y in A' by A4,A5,A27,A29,A30;
        end;
        hence rng f c= A' by TARSKI:def 3;
      end;
      hence y in Funcs(A',A') by FUNCT_2:def 2;
    end;
    then rng a' c= Funcs(A',A') by TARSKI:def 3;
    then reconsider a' as Action of O,A' by A17,FUNCT_2:4;
    reconsider H = HGrWOpStr (#A',m',a'#) as (non empty HGrWOpStr over O);
A31: H is Group-like by A6;
    the carrier of H c= the carrier of G &
    the multF of H = (the multF of G)||the carrier of H by GROUP_2:def 5;
    then
A32: H is Subgroup of G by A31,GROUP_2:def 5;
    now
      let G' be Group;
      let a be Action of O, the carrier of G';
      assume
A33:  a = the action of H;
      assume
A34:  the multMagma of G' = the multMagma of H;
      now
        let o be Element of O;
        assume o in O;
        then
A35:    o in dom a by FUNCT_2:def 1;
        then a.o in rng a by FUNCT_1:12;
        then consider f be Function such that
A36:    a.o = f & dom f = the carrier of G'
        & rng f c= the carrier of G' by FUNCT_2:def 2;
        [o,a.o] in a' by A33,A35,FUNCT_1:8;
        then consider o' be Element of O such that
A37:    [o,a.o] = [o',(G^o')|A'];
A38:    o=o' & a.o=(G^o')|A' by A37,ZFMISC_1:33;
        reconsider f as Function of G',G' by A36,FUNCT_2:4;
        now
          let a', b' be Element of G';
          a'*b' in the carrier of H' by A34;
          then
A39:      a'*b' in dom id A' by RELAT_1:71;
          a' in the carrier of H' & b' in the carrier of H' by A34;
          then
A40:      a' in dom id A' & b' in dom id A' by RELAT_1:71;
          reconsider a=a',b=b' as Element of H by A34;
          reconsider g1=a,g2=b as Element of G by GROUP_2:51;
          reconsider h1=(G^o).g1,h2=(G^o).g2 as Element of H by A4,A5;
A41:      f.a' = ((G^o)*(id A')).a' by A36,A38,RELAT_1:94
            .= (G^o).((id A').a') by A40,FUNCT_1:23
            .= h1 by FUNCT_1:35;
A42:      f.b' = ((G^o)*(id A')).b' by A36,A38,RELAT_1:94
            .= (G^o).((id A').b') by A40,FUNCT_1:23
            .= h2 by FUNCT_1:35;
          thus f.(a' * b') = ((G^o)*(id A')).(a'*b') by A36,A38,RELAT_1:94
            .= (G^o).((id A').(a'*b')) by A39,FUNCT_1:23
            .= (G^o).(a*b) by A34,FUNCT_1:35
            .= (G^o).(g1*g2) by A32,GROUP_2:52
            .= ((G^o).g1) * ((G^o).g2) by GROUP_6:def 7
            .= h1 * h2 by A32,GROUP_2:52
            .= f.a' * f.b' by A34,A41,A42;
        end;
        hence a.o is Homomorphism of G',G' by A36,GROUP_6:def 7;
      end;
      hence a is distributive by Def4;
    end;
    then reconsider H as GroupWithOperators of O by A6,Def5;
    now
      let o be Element of O;
      o in O by A15;
      then o in dom a' by FUNCT_2:def 1;
      then [o,a'.o] in a' by FUNCT_1:8;
      then consider o' be Element of O such that
A43:  [o,a'.o] = [o',(G^o')|A'];
      o=o' & a'.o=(G^o')|A' by A43,ZFMISC_1:33;
      hence H^o = (G^o)|the carrier of H by A15,Def6;
    end;
    then reconsider H as strict StableSubgroup of G by A32,Def7;
    take H;
    thus thesis by A5;
  end;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  let g be Homomorphism of G, H;
  func Ker g -> strict StableSubgroup of G means
  :Def24:
  the carrier of it = {a where a is Element of G: g.a = 1_H};
  existence
  proof
    defpred P[set] means g.$1 = 1_H;
    reconsider A = {a where a is Element of G: P[a]} as Subset of G
    from DOMAIN_1:sch 7;
    g.(1_G) = 1_H by Lm13;
    then
A1: 1_G in A;
A2: now
      let a,b be Element of G;
      assume that
A3:   a in A and
A4:   b in A;
A5:   ex a1 being Element of G st a1 = a & g.a1 = 1_H by A3;
A6:   ex b1 being Element of G st b1 = b & g.b1 = 1_H by A4;
      g.(a * b) = g.a * g.b by GROUP_6:def 7
        .= 1_H by A5,A6,GROUP_1:def 5;
      hence a * b in A;
    end;
A7: now
      let a be Element of G;
      assume a in A;
      then ex a1 being Element of G st a1 = a & g.a1 = 1_H;
      then g.(a") = (1_H)" by Lm14
        .= 1_H by GROUP_1:16;
      hence a" in A;
    end;
    now
      let o be Element of O;
      let a be Element of G;
      assume a in A;
      then ex a1 being Element of G st a1 = a & g.a1 = 1_H;
      then g.((G^o).a) = (H^o).(1_H) by Def18
        .= 1_H by GROUP_6:40;
      hence (G^o).a in A;
    end;
    then consider B be strict StableSubgroup of G such that
A8: the carrier of B = A by A1,A2,A7,Lm15;
    take B;
    thus thesis by A8;
  end;
  uniqueness by Lm5;
end;

registration
  let O be set;
  let G,H be GroupWithOperators of O;
  let g be Homomorphism of G, H;
  cluster Ker g -> normal;
  correctness
  proof
    now
      let N be strict Subgroup of G;
      assume
A1:   N = the multMagma of Ker g;
      reconsider G'=G,H'=H as Group;
      reconsider g'=g as Homomorphism of G', H';
      the carrier of Ker g' = {a where a is Element of G: g.a = 1_H}
      by GROUP_6:def 10;
      then the carrier of Ker g' = the carrier of N by A1,Def24;
      hence N is normal by GROUP_2:68;
    end;
    hence thesis by Def10;
  end;
end;

Lm16: for O being set, G being GroupWithOperators of O,
H being StableSubgroup of G holds the multMagma of H is strict Subgroup of G
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  reconsider H'=the multMagma of H as non empty multMagma;
  now
    set e=1_H;
    reconsider e'=e as Element of H';
    take e';
    let h' be Element of H';
    reconsider h=h' as Element of H;
    h'*e' = h*e
      .= h by GROUP_1:def 5;
    hence h' * e' = h';
    e'*h' = e*h
      .= h by GROUP_1:def 5;
    hence e' * h' = h';
    set g=h";
    reconsider g'=g as Element of H';
    take g';
    h'*g' = h*g
      .= 1_H by GROUP_1:def 6;
    hence h' * g' = e';
    g'*h' = g*h
      .= 1_H by GROUP_1:def 6;
    hence g' * h' = e';
  end;
  then reconsider H' as Group-like (non empty multMagma) by GROUP_1:def 3;
  H is Subgroup of G by Def7;
  then the carrier of H' c= the carrier of G &
  the multF of H' = (the multF of G)||the carrier of H' by GROUP_2:def 5;
  hence thesis by GROUP_2:def 5;
end;

Lm17: for O being set, G,H being GroupWithOperators of O,
G' being strict StableSubgroup of G, f being Homomorphism of G,H
holds ex H' being strict StableSubgroup of H
st the carrier of H' = f.:(the carrier of G')
proof
  let O be set;
  let G,H be GroupWithOperators of O;
  let G' be strict StableSubgroup of G;
  let f be Homomorphism of G,H;
  reconsider G'' = the multMagma of G' as strict Subgroup of G by Lm16;
  set A = {f.g where g is Element of G:g in G''};
  1_G in G'' by GROUP_2:55;
  then f.(1_G) in A;
  then reconsider A as non empty set;
  now
    let x be set;
    assume x in A;
    then consider g be Element of G such that
A1: x=f.g & g in G'';
    thus x in the carrier of H by A1;
  end;
  then reconsider A as Subset of H by TARSKI:def 3;
A2: now
    let h1,h2 be Element of H;
    assume
A3: h1 in A & h2 in A;
    then consider a be Element of G such that
A4: h1=f.a & a in G'';
    consider b be Element of G such that
A5: h2=f.b & b in G'' by A3;
A6: f.(a*b) = h1*h2 by A4,A5,GROUP_6:def 7;
    a*b in G'' by A4,A5,GROUP_2:59;
    hence h1*h2 in A by A6;
  end;
A7: now
    let h be Element of H;
    assume h in A;
    then consider g be Element of G such that
A8: h=f.g & g in G'';
A9: g" in G'' by A8,GROUP_2:60;
    h" = f.(g") by A8,Lm14;
    hence h" in A by A9;
  end;
  now
    let o be Element of O;
    let h be Element of H;
    assume h in A;
    then consider g be Element of G such that
A10: h=f.g & g in G'';
A11: (H^o).h = f.((G^o).g) by A10,Def18;
    g in the carrier of G'' by A10,STRUCT_0:def 5;
    then g in G' by STRUCT_0:def 5;
    then (G^o).g in G' by Lm10;
    then (G^o).g in the carrier of G' by STRUCT_0:def 5;
    then (G^o).g in G'' by STRUCT_0:def 5;
    hence (H^o).h in A by A11;
  end;
  then consider H'' be strict StableSubgroup of H such that
A12: the carrier of H'' = A by A2,A7,Lm15;
  take H'';
  now
    let h be Element of H;
    set R = f;
    reconsider R as Relation of the carrier of G, the carrier of H;
    hereby
      assume h in A;
      then consider g be Element of G such that
A13:  h=f.g & g in G'';
      dom f = the carrier of G by FUNCT_2:def 1;
      then
A14:  [g,h] in f by A13,FUNCT_1:8;
      g in the carrier of G' by A13,STRUCT_0:def 5;
      hence h in f.:(the carrier of G') by A14,RELSET_1:52;
    end;
    assume h in f.:(the carrier of G');
    then consider g be Element of G such that
A15: [g,h] in R & g in the carrier of G' by RELSET_1:52;
A16: f.g=h by A15,FUNCT_1:8;
    g in G'' by A15,STRUCT_0:def 5;
    hence h in A by A16;
  end;
  hence thesis by A12,SUBSET_1:8;
end;

definition
  let O be set;
  let G,H be GroupWithOperators of O;
  let g be Homomorphism of G, H;
  func Image g -> strict StableSubgroup of H means
  :Def25:
  the carrier of it = g.:(the carrier of G);
  existence
  proof
    reconsider G' = the HGrWOpStr of G as strict StableSubgroup of G by Lm4;
    consider H' be strict StableSubgroup of H such that
A1: the carrier of H' = g.:(the carrier of G') by Lm17;
    take H';
    thus thesis by A1;
  end;
  uniqueness by Lm5;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  func carr(H) -> Subset of G equals

  the carrier of H;
  coherence
  proof
    reconsider H'=H as Subgroup of G by Def7;
    carr(H') is Subset of G;
    hence thesis;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be StableSubgroup of G;
  func H1 * H2 -> Subset of G equals

  carr H1 * carr H2;
  coherence;
end;

:: like GROUP_2:55

Lm18: for O being set, G being GroupWithOperators of O,
H being StableSubgroup of G holds 1_G in H
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  H is Subgroup of G by Def7;
  hence thesis by GROUP_2:55;
end;

:: like GROUP_2:59

Lm19: for O being set, G being GroupWithOperators of O,
H being StableSubgroup of G, g1,g2 being Element of G holds
g1 in H & g2 in H implies g1 * g2 in H
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  let g1,g2 be Element of G;
A1: H is Subgroup of G by Def7;
  assume g1 in H & g2 in H;
  hence thesis by A1,GROUP_2:59;
end;

:: like GROUP_2:60

Lm20: for O being set, G being GroupWithOperators of O,
H being StableSubgroup of G, g being Element of G holds g in H implies g" in H
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  let g be Element of G;
A1: H is Subgroup of G by Def7;
  assume g in H;
  hence thesis by A1,GROUP_2:60;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be StableSubgroup of G;
  func H1 /\ H2 -> strict StableSubgroup of G means
  :Def28:
  the carrier of it = carr(H1) /\ carr(H2);
  existence
  proof
    set A = carr(H1) /\ carr(H2);
    1_G in H1 & 1_G in H2 by Lm18;
    then 1_G in the carrier of H1 & 1_G in the carrier of H2 by STRUCT_0:def 5;
    then
A1: A <> {} by XBOOLE_0:def 4;
A2: now
      let g1,g2 be Element of G;
      assume g1 in A & g2 in A;
      then g1 in carr(H1) & g2 in carr(H1) & g1 in carr(H2) & g2 in carr(H2)
      by XBOOLE_0:def 4;
      then g1 in H1 & g2 in H1 & g1 in H2 & g2 in H2 by STRUCT_0:def 5;
      then g1 * g2 in H1 & g1 * g2 in H2 by Lm19;
      then g1 * g2 in carr(H1) & g1 * g2 in carr(H2) by STRUCT_0:def 5;
      hence g1 * g2 in A by XBOOLE_0:def 4;
    end;
A3: now
      let g be Element of G;
      assume g in A;
      then g in carr(H1) & g in carr(H2) by XBOOLE_0:def 4;
      then g in H1 & g in H2 by STRUCT_0:def 5;
      then g" in H1 & g" in H2 by Lm20;
      then g" in carr(H1) & g" in carr(H2) by STRUCT_0:def 5;
      hence g" in A by XBOOLE_0:def 4;
    end;
    now
      let o be Element of O;
      let a be Element of G;
      assume a in A;
      then a in carr(H1) & a in carr(H2) by XBOOLE_0:def 4;
      then a in H1 & a in H2 by STRUCT_0:def 5;
      then (G^o).a in H1 & (G^o).a in H2 by Lm10;
      then (G^o).a in carr(H1) & (G^o).a in carr(H2) by STRUCT_0:def 5;
      hence (G^o).a in A by XBOOLE_0:def 4;
    end;
    hence thesis by A1,A2,A3,Lm15;
  end;
  uniqueness by Lm5;
  commutativity;
end;

:: like GROUP_2:66

Lm21: for O being set, G being GroupWithOperators of O,
H1,H2 being StableSubgroup of G holds
the carrier of H1 c= the carrier of H2 implies H1 is StableSubgroup of H2
proof
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be StableSubgroup of G;
  reconsider H1'=H1,H2'=H2 as Subgroup of G by Def7;
  assume
A1: the carrier of H1 c= the carrier of H2;
  then
A2: H1' is Subgroup of H2' by GROUP_2:66;
  now
    let o be Element of O;
    thus H1^o = (G^o)|the carrier of H1 by Def7
      .= ((G^o)|the carrier of H2)|the carrier of H1 by A1,RELAT_1:103
      .= (H2^o)|the carrier of H1 by Def7;
  end;
  hence thesis by A2,Def7;
end;

:: like GROUP_4:def 5

definition
  let O be set;
  let G be GroupWithOperators of O;
  let A be Subset of G;
  func the_stable_subgroup_of A -> strict StableSubgroup of G means
  :Def29:
  A c= the carrier of it &
  (for H being strict StableSubgroup of G st A c= the carrier of H
  holds it is StableSubgroup of H);
  existence
  proof
    defpred P[set] means
    ex H being strict StableSubgroup of G st $1 = carr H & A c= $1;
    consider X be set such that
A1: for Y being set holds
    Y in X iff Y in bool the carrier of G & P[Y] from XBOOLE_0:sch 1;
    set M = meet X;
A2: the carrier of G in bool the carrier of G &
    the carrier of the HGrWOpStr of G = the carrier of (Omega).G &
    A c= the carrier of G &
    carr (Omega).G = the carrier of (Omega).G by ZFMISC_1:def 1;
    then
A3: the carrier of G in X by A1;
A4: X <> {} by A1,A2;
    now
      let Y be set;
      assume Y in X;
      then consider H be strict StableSubgroup of G such that
A5:   Y = carr H and A c= Y by A1;
      1_G in H by Lm18;
      hence 1_G in Y by A5,STRUCT_0:def 5;
    end;
    then
A6: M <> {} by A4,SETFAM_1:def 1;
    M c= the carrier of G
    proof consider x be set such that
A7:   x in X by A3;
      consider H be strict StableSubgroup of G such that
A8:   x = carr H and A c= x by A1,A7;
      let y be set;
      assume y in M;
      then y in carr H & carr H c= the carrier of G by A7,A8,SETFAM_1:def 1;
      hence thesis;
    end;
    then reconsider M as Subset of G;
A9: now
      let a,b be Element of G;
      assume
A10:  a in M & b in M;
      now
        let Y be set;
        assume
A11:    Y in X;
        then consider H be strict StableSubgroup of G such that
A12:    Y = carr H and A c= Y by A1;
        a in carr H & b in carr H by A10,A11,A12,SETFAM_1:def 1;
        then a in H & b in H by STRUCT_0:def 5;
        then a * b in H by Lm19;
        hence a * b in Y by A12,STRUCT_0:def 5;
      end;
      hence a * b in M by A4,SETFAM_1:def 1;
    end;
A13: now
      let a be Element of G;
      assume
A14:  a in M;
      now
        let Y be set;
        assume
A15:    Y in X;
        then consider H be strict StableSubgroup of G such that
A16:    Y = carr H and A c= Y by A1;
        a in carr H by A14,A15,A16,SETFAM_1:def 1;
        then a in H by STRUCT_0:def 5;
        then a" in H by Lm20;
        hence a" in Y by A16,STRUCT_0:def 5;
      end;
      hence a" in M by A4,SETFAM_1:def 1;
    end;
    now
      let o be Element of O;
      let a be Element of G;
      assume
A17:  a in M;
      now
        let Y be set;
        assume
A18:    Y in X;
        then consider H be strict StableSubgroup of G such that
A19:    Y = carr H and A c= Y by A1;
        a in carr H by A17,A18,A19,SETFAM_1:def 1;
        then a in H by STRUCT_0:def 5;
        then (G^o).a in H by Lm10;
        hence (G^o).a in Y by A19,STRUCT_0:def 5;
      end;
      hence (G^o).a in M by A4,SETFAM_1:def 1;
    end;
    then consider H be strict StableSubgroup of G such that
A20: the carrier of H = M by A6,A9,A13,Lm15;
    take H;
    now
      let Y be set;
      assume Y in X;
      then ex H being strict StableSubgroup of G st Y = carr H & A c= Y by A1;
      hence A c= Y;
    end;
    hence A c= the carrier of H by A4,A20,SETFAM_1:6;
    let H1 be strict StableSubgroup of G;
    assume
A21: A c= the carrier of H1;
    the carrier of H1 = carr H1;
    then the carrier of H1 in X by A1,A21;
    hence thesis by A20,Lm21,SETFAM_1:4;
  end;
  uniqueness
  proof
    let H1,H2 be strict StableSubgroup of G;
    assume A c= the carrier of H1 & (for H being strict StableSubgroup of G
    st A c= the carrier of H holds H1 is StableSubgroup of H) &
    A c= the carrier of H2 & (for H being strict StableSubgroup of G
    st A c= the carrier of H holds H2 is StableSubgroup of H);
    then H1 is StableSubgroup of H2 & H2 is StableSubgroup of H1;
    then H1 is Subgroup of H2 & H2 is Subgroup of H1 by Def7;
    then the carrier of H1 c= the carrier of H2 &
    the carrier of H2 c= the carrier of H1 by GROUP_2:def 5;
    then the carrier of H1 = the carrier of H2 by XBOOLE_0:def 10;
    hence thesis by Lm5;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let H1,H2 be StableSubgroup of G;
  func H1 "\/" H2 -> strict StableSubgroup of G equals
  the_stable_subgroup_of (carr H1 \/ carr H2);
  correctness;
end;

begin :: Some Theorems on Groups reformulated for Groups with Operators

reserve x,O for set,
  o for Element of O,
  G,H,I for GroupWithOperators of O,
  A,B for Subset of G,
  N for normal StableSubgroup of G,
  H1,H2,H3 for StableSubgroup of G,
  g1,g2 for Element of G,
  h1,h2 for Element of H1,
  h for Homomorphism of G,H;

:: GROUP_2:49

theorem Th1:
  x in H1 implies x in G
proof
A1: H1 is Subgroup of G by Def7;
  assume x in H1;
  hence thesis by A1,GROUP_2:49;
end;

:: GROUP_2:51

theorem Th2:
  h1 is Element of G
proof
  H1 is Subgroup of G by Def7;
  hence thesis by GROUP_2:51;
end;

:: GROUP_2:52

theorem Th3:
  h1 = g1 & h2 = g2 implies h1 * h2 = g1 * g2
proof
A1: H1 is Subgroup of G by Def7;
  assume h1 = g1 & h2 = g2;
  hence thesis by A1,GROUP_2:52;
end;

:: GROUP_2:53

theorem Th4:
  1_G = 1_H1
proof
  reconsider H1'=H1 as Subgroup of G by Def7;
  1_H1 = 1_H1';
  hence thesis by GROUP_2:53;
end;

:: GROUP_2:55

theorem
  1_G in H1 by Lm18;

:: GROUP_2:57

theorem Th6:
  h1 = g1 implies h1" = g1"
proof
  assume
A1: h1 = g1;
  reconsider g' = h1" as Element of G by Th2;
  h1 * h1" = 1_H1 by GROUP_1:def 6;
  then g1 * g' = 1_H1 by A1,Th3
    .= 1_G by Th4;
  hence thesis by GROUP_1:20;
end;

:: GROUP_2:59

theorem
  g1 in H1 & g2 in H1 implies g1 * g2 in H1 by Lm19;

:: GROUP_2:60

theorem
  g1 in H1 implies g1" in H1 by Lm20;

:: GROUP_2:61

theorem
  A <> {} & (for g1,g2 st g1 in A & g2 in A holds g1 * g2 in A) &
  (for g1 st g1 in A holds g1" in A) &
  (for o,g1 st g1 in A holds (G^o).g1 in A) implies
  ex H being strict StableSubgroup of G st the carrier of H = A by Lm15;

:: GROUP_2:63

theorem Th10:
  G is StableSubgroup of G
proof
A1: G is Subgroup of G by GROUP_2:63;
  now
    let o be Element of O;
    dom(G^o) c= the carrier of G;
    hence G^o = (G^o)|the carrier of G by RELAT_1:97;
  end;
  hence thesis by A1,Def7;
end;

:: GROUP_2:65

theorem Th11:
  for G1,G2,G3 being GroupWithOperators of O holds
  G1 is StableSubgroup of G2 & G2 is StableSubgroup of G3
  implies G1 is StableSubgroup of G3
proof
  let G1,G2,G3 be GroupWithOperators of O;
  assume
A1: G1 is StableSubgroup of G2 & G2 is StableSubgroup of G3;
  then
A2: G1 is Subgroup of G2 & G2 is Subgroup of G3 by Def7;
  then
A3: G1 is Subgroup of G3 by GROUP_2:65;
  now
    let o be Element of O;
A4: the carrier of G1 c= the carrier of G2 by A2,GROUP_2:def 5;
    G1^o = (G2^o)|the carrier of G1 by A1,Def7
      .= ((G3^o)|the carrier of G2)|the carrier of G1 by A1,Def7
      .= (G3^o)|((the carrier of G2) /\ the carrier of G1) by RELAT_1:100;
    hence G1^o = (G3^o)|the carrier of G1 by A4,XBOOLE_1:28;
  end;
  hence thesis by A3,Def7;
end;

:: GROUP_2:66

theorem
  the carrier of H1 c= the carrier of H2
  implies H1 is StableSubgroup of H2 by Lm21;

:: GROUP_2:67

theorem Th13:
  (for g being Element of G st g in H1 holds g in H2)
  implies H1 is StableSubgroup of H2
proof
  assume
A1: for g being Element of G st g in H1 holds g in H2;
  the carrier of H1 c= the carrier of H2
  proof
    let x;
    assume x in the carrier of H1;
    then reconsider g = x as Element of H1;
    reconsider g as Element of G by Th2;
    g in H1 by STRUCT_0:def 5;
    then g in H2 by A1;
    hence thesis by STRUCT_0:def 5;
  end;
  hence thesis by Lm21;
end;

:: GROUP_2:68

theorem
  for H1,H2 being strict StableSubgroup of G
  st the carrier of H1 = the carrier of H2 holds H1 = H2 by Lm5;

:: GROUP_2:75

theorem Th15:
  (1).G = (1).H1
proof
A1: (1).H1 is StableSubgroup of G by Th11;
A2: the carrier of (1).H1 = {1_H1} & the carrier of (1).G = {1_G} by Def8;
  1_H1 = 1_G by Th4;
  hence thesis by A1,A2,Lm5;
end;

:: GROUP_2:77

theorem Th16:
  (1).G is StableSubgroup of H1
proof
  (1).G = (1).H1 by Th15;
  hence thesis;
end;

:: GROUP_2:93

theorem Th17:
  carr H1 * carr H2 = carr H2 * carr H1 implies
  ex H being strict StableSubgroup of G st the carrier of H=carr H1 * carr H2
proof
A1: H1 is Subgroup of G & H2 is Subgroup of G by Def7;
  assume
A2: carr H1 * carr H2 = carr H2 * carr H1;
A3: carr H1 * carr H2 <> {} by GROUP_2:13;
A4: now
    let g1,g2 be Element of G;
    assume that
A5: g1 in carr(H1) * carr(H2) and
A6: g2 in carr(H1) * carr(H2);
    consider a1,b1 be Element of G such that
A7: g1 = a1 * b1 & a1 in carr(H1) & b1 in carr(H2) by A5;
    consider a2,b2 be Element of G such that
A8: g2 = a2 * b2 & a2 in carr H1 & b2 in carr H2 by A6;
A9: g1 * g2 = a1 * b1 * a2 * b2 by A7,A8,GROUP_1:def 4
      .= a1 * (b1 * a2) * b2 by GROUP_1:def 4;
    b1 * a2 in carr H1 * carr H2 by A2,A7,A8;
    then consider a,b be Element of G such that
A10: b1 * a2 = a * b & a in carr H1 & b in carr H2;
A11: g1 * g2 = a1 * a * b * b2 by A9,A10,GROUP_1:def 4
      .= a1 * a * (b * b2) by GROUP_1:def 4;
    a1 in H1 & a in H1 by A7,A10,STRUCT_0:def 5;
    then a1 * a in H1 by A1,GROUP_2:59;
    then
A12: a1 * a in carr H1 by STRUCT_0:def 5;
    b2 in H2 & b in H2 by A8,A10,STRUCT_0:def 5;
    then b * b2 in H2 by A1,GROUP_2:59;
    then b * b2 in carr H2 by STRUCT_0:def 5;
    hence g1 * g2 in carr H1 * carr H2 by A11,A12;
  end;
A13: now
    let g be Element of G;
    assume
A14: g in carr H1 * carr H2;
    then consider a,b be Element of G such that
A15: g = a * b & a in carr H1 & b in carr H2;
    consider b1,a1 be Element of G such that
A16: a * b = b1 * a1 & b1 in carr H2 & a1 in carr H1 by A2,A14,A15;
A17: g" = a1" * b1" by A15,A16,GROUP_1:25;
    b1 in H2 & a1 in H1 by A16,STRUCT_0:def 5;
    then b1" in H2 & a1" in H1 by A1,GROUP_2:60;
    then a1" in carr H1 & b1" in carr H2 by STRUCT_0:def 5;
    hence g" in carr H1 * carr H2 by A17;
  end;
  now
    let o be Element of O;
    let g be Element of G;
    assume g in carr H1 * carr H2;
    then consider a,b be Element of G such that
A18: g = a * b & a in carr H1 & b in carr H2;
    b in H2 & a in H1 by A18,STRUCT_0:def 5;
    then (G^o).b in H2 & (G^o).a in H1 by Lm10;
    then (G^o).b in carr H2 & (G^o).a in carr H1 by STRUCT_0:def 5;
    then ((G^o).a) * ((G^o).b) in carr H1 * carr H2;
    hence (G^o).g in carr H1 * carr H2 by A18,GROUP_6:def 7;
  end;
  hence thesis by A3,A4,A13,Lm15;
end;

:: GROUP_2:97

theorem Th18:
  (for H being StableSubgroup of G st H = H1 /\ H2 holds
  the carrier of H = (the carrier of H1) /\ (the carrier of H2)) &
  for H being strict StableSubgroup of G holds
  the carrier of H = (the carrier of H1) /\ (the carrier of H2) implies
  H = H1 /\ H2
proof
  thus (for H being StableSubgroup of G st H = H1 /\ H2
  holds the carrier of H = (the carrier of H1) /\ (the carrier of H2))
  proof
    let H be StableSubgroup of G;
    assume H = H1 /\ H2;
    hence the carrier of H = carr(H1)/\carr(H2) by Def28
      .= (the carrier of H1)/\(the carrier of H2);
  end;
  let H be strict StableSubgroup of G;
  assume
A1: the carrier of H = (the carrier of H1) /\ (the carrier of H2);
  the carrier of H1 = carr(H1) & the carrier of H2 = carr(H2);
  hence thesis by A1,Def28;
end;

:: GROUP_2:100

theorem Th19:
  for H being strict StableSubgroup of G holds H /\ H = H
proof
  let H be strict StableSubgroup of G;
  the carrier of H /\ H = carr(H) /\ carr(H) by Def28
    .= the carrier of H;
  hence thesis by Lm5;
end;

:: GROUP_2:102

theorem Th20:
  H1 /\ H2 /\ H3 = H1 /\ (H2 /\ H3)
proof
  the carrier of H1 /\ H2 /\ H3 = carr(H1 /\ H2) /\ carr(H3) by Def28
    .= carr(H1) /\ carr(H2) /\ carr(H3) by Def28
    .= carr(H1) /\ (carr(H2) /\ carr(H3)) by XBOOLE_1:16
    .= carr(H1) /\ carr(H2 /\ H3) by Def28
    .= the carrier of H1 /\ (H2 /\ H3) by Def28;
  hence thesis by Lm5;
end;

Lm22: for H1 being strict StableSubgroup of G holds
H1 is StableSubgroup of H2 iff H1 /\ H2 = H1
proof
  let H1 be strict StableSubgroup of G;
  thus H1 is StableSubgroup of H2 implies H1 /\ H2 = H1
  proof
    assume H1 is StableSubgroup of H2;
    then H1 is Subgroup of H2 by Def7;
    then the carrier of H1 c= the carrier of H2 by GROUP_2:def 5;
    then (the carrier of H1) /\ (the carrier of H2) = the carrier of H1 &
    the carrier of H1 /\ H2 = carr(H1) /\ carr (H2) &
    carr(H1) = the carrier of H1 & carr(H2) = the carrier of H2
    by Def28,XBOOLE_1:28;
    hence thesis by Lm5;
  end;
  assume H1 /\ H2 = H1;
  then the carrier of H1 = carr(H1) /\ carr(H2) by Def28
    .= (the carrier of H1) /\ (the carrier of H2);
  hence thesis by Lm21,XBOOLE_1:17;
end;

:: GROUP_2:103

theorem Th21:
  (1).G /\ H1 = (1).G & H1 /\ (1).G = (1).G
proof
A1: (1).G is StableSubgroup of H1 by Th16;
  hence (1).G /\ H1 = (1).G by Lm22;
  thus thesis by A1,Lm22;
end;

:: GROUP_2:167

theorem Th22:
  union Cosets N = the carrier of G
proof
  reconsider H = the multMagma of N as strict normal Subgroup of G by Lm7;
A1: Cosets N = Cosets H by Def14;
  now
    now
      let x;
      assume x in the carrier of G;
      then reconsider a = x as Element of G;
      consider h be Element of H;
      reconsider g = h as Element of G by GROUP_2:51;
A2:   h in H by STRUCT_0:def 5;
      a = a * 1_G by GROUP_1:def 5
        .= a * (g" * g) by GROUP_1:def 6
        .= a * g" * g by GROUP_1:def 4;
      then a in a * g" * H & a * g" * H in Cosets H by A2,GROUP_2:125,def 15;
      hence x in union Cosets H by TARSKI:def 4;
    end;
    hence the carrier of G c= union Cosets H by TARSKI:def 3;
  end;
  hence thesis by A1,XBOOLE_0:def 10;
end;

:: GROUP_3:149

theorem Th23:
  for N1,N2 being strict normal StableSubgroup of G
  ex N being strict normal StableSubgroup of G
  st the carrier of N = carr N1 * carr N2
proof
  let N1,N2 be strict normal StableSubgroup of G;
  set N1' = the multMagma of N1;
  set N2' = the multMagma of N2;
  reconsider N1',N2' as strict normal Subgroup of G by Lm7;
  set A = carr N1' * carr N2';
  set B = carr N1';
  set C = carr N2';
  carr N1' * carr N2' = carr N2' * carr N1' by GROUP_3:148;
  then consider H' be strict Subgroup of G such that
A1: the carrier of H' = A by GROUP_2:93;
  now
    let a be Element of G;
    thus a * H' = a * N1' * C by A1,GROUP_2:35
      .= N1' * a * C by GROUP_3:140
      .= B * (a * N2') by GROUP_2:36
      .= B * (N2' * a) by GROUP_3:140
      .= H' * a by A1,GROUP_2:37;
  end;
  then
A2: H' is normal Subgroup of G by GROUP_3:140;
A3: now
    let g1,g2 be Element of G;
    assume g1 in A & g2 in A;
    then g1 in H' & g2 in H' by A1,STRUCT_0:def 5;
    then g1 * g2 in H' by GROUP_2:59;
    hence g1 * g2 in A by A1,STRUCT_0:def 5;
  end;
A4: now
    let g be Element of G;
    assume g in A;
    then g in H' by A1,STRUCT_0:def 5;
    then g" in H' by GROUP_2:60;
    hence g" in A by A1,STRUCT_0:def 5;
  end;
  now
    let o be Element of O;
    let g be Element of G;
    assume g in A;
    then consider a,b be Element of G such that
A5: g = a * b & a in carr N1 & b in carr N2;
    b in N2 & a in N1 by A5,STRUCT_0:def 5;
    then (G^o).b in N2 & (G^o).a in N1 by Lm10;
    then (G^o).b in carr N2 & (G^o).a in carr N1 by STRUCT_0:def 5;
    then ((G^o).a) * ((G^o).b) in carr N1 * carr N2;
    hence (G^o).g in A by A5,GROUP_6:def 7;
  end;
  then consider H be strict StableSubgroup of G such that
A6: the carrier of H = A by A1,A3,A4,Lm15;
  for H'' being strict Subgroup of G st H'' = the multMagma of H
  holds H'' is normal by A1,A2,A6,GROUP_2:68;
  then H is normal by Def10;
  hence thesis by A6;
end;

Lm23: for F1 being FinSequence, y being Element of NAT st y in dom F1 holds
len F1 - y + 1 is Element of NAT & len F1 - y + 1 >= 1 &
len F1 - y + 1 <= len F1
proof
  let F1 be FinSequence, y be Element of NAT;
  assume
A1: y in dom F1;
  now
    assume len F1 - y + 1 < 0;
    then 1 < 0 qua Nat - (len F1 - y) by XREAL_1:22;
    then 1 < y - len F1;
    then len F1 + 1 < y & y <= len F1 by A1,FINSEQ_3:27,XREAL_1:22;
    hence contradiction by NAT_1:12;
  end;
  then reconsider n = len F1 - y + 1 as Element of NAT by INT_1:16;
  y + 0 <= len F1 by A1,FINSEQ_3:27;
  then
A2: 0 + 1 = 1 & 0 <= len F1 - y by XREAL_1:21;
  n - 1 <= len F1 - y & y >= 1 by A1,FINSEQ_3:27;
  then n - 1 - y <= len F1 - y - 1 by XREAL_1:15;
  then n - (y + 1) <= len F1 - (y + 1);
  hence thesis by A2,XREAL_1:8,11;
end;

Lm24: for G,H being Group, F1 being FinSequence of the carrier of G,
F2 being FinSequence of the carrier of H,
I being FinSequence of INT, f being Homomorphism of G,H
st (for k being Nat st k in dom F1 holds F2.k = f.(F1.k))
& len F1 = len I & len F2 = len I
holds f.(Product(F1 |^ I)) = Product(F2 |^ I)
proof
  let G,H be Group;
  let F1 be FinSequence of the carrier of G;
  let F2 be FinSequence of the carrier of H;
  let I be FinSequence of INT;
  let f be Homomorphism of G,H;
  assume
A1: (for k being Nat st k in dom F1 holds F2.k = f.(F1.k))
  & len F1 = len I & len F2 = len I;
  defpred P[Nat] means
  for G,H being Group, F1 being FinSequence of the carrier of G,
  F2 being FinSequence of the carrier of H, I being FinSequence of INT,
  f being Homomorphism of G,H
  st (for k being Nat st k in dom F1 holds F2.k = f.(F1.k))
  & len F1 = len I & len F2 = len I & $1 = len I
  holds f.(Product(F1 |^ I)) = Product(F2 |^ I);
A2: P[0]
  proof
    let G,H be Group;
    let F1 be FinSequence of the carrier of G;
    let F2 be FinSequence of the carrier of H;
    let I be FinSequence of INT;
    let f be Homomorphism of G,H;
    assume for k being Nat st k in dom F1 holds F2.k = f.(F1.k);
    assume len F1 = len I & len F2 = len I & 0 = len I;
    then len(F1 |^ I) = 0 & len(F2 |^ I) = 0 by GROUP_4:def 4;
    then F1 |^ I = <*> the carrier of G & F2 |^ I = <*> the carrier of H;
    then Product(F1 |^ I) = 1_G & Product(F2 |^ I) = 1_H by GROUP_4:11;
    hence thesis by GROUP_6:40;
  end;
A3: now
    let n be Nat;
    assume
A4: P[n];
    thus P[n+1]
    proof
      let G,H be Group;
      let F1 be FinSequence of the carrier of G;
      let F2 be FinSequence of the carrier of H;
      let I be FinSequence of INT;
      let f be Homomorphism of G,H;
      assume
A5:   for k being Nat st k in dom F1 holds F2.k = f.(F1.k);
      assume
A6:   len F1 = len I & len F2 = len I & n+1 = len I;
      then consider In be FinSequence of INT, i be Element of INT such that
A7:   I = In^<*i*> by FINSEQ_2:22;
      1 <= n+1 by NAT_1:11;
      then
A8:    n+1 in dom I by A6,FINSEQ_3:27;
A9:    dom F1 = dom F2 by A6,FINSEQ_3:31;
A10:    dom F2 = dom I by A6,FINSEQ_3:31;
      consider F1n be FinSequence of the carrier of G,
      g be Element of G such that
A11:   F1 = F1n^<*g*> by A6,FINSEQ_2:22;
      consider F2n be FinSequence of the carrier of H,
      h be Element of H such that
A12:   F2 = F2n^<*h*> by A6,FINSEQ_2:22;
      set I1=<*i*>;
      set F11=<*g*>;
      set F21=<*h*>;
      len I = len In + len <*i*> by A7,FINSEQ_1:35;
      then
A13:  n+1 = len In + 1 by A6,FINSEQ_1:57;
A14:  len F1 = len F1n + len <*g*> by A11,FINSEQ_1:35;
      then
A15:  n+1 = len F1n + 1 by A6,FINSEQ_1:57;
A16:  len F2 = len F2n + len <*h*> by A12,FINSEQ_1:35;
      then
A17:  n+1 = len F2n + 1 by A6,FINSEQ_1:57;
A18:  F1.(n+1)=(F1n^<*g*>).(len F1n +1) by A6,A11,A14,FINSEQ_1:57
        .= g by FINSEQ_1:59;
A19:  F2.(n+1)=(F2n^<*h*>).(len F2n +1) by A6,A12,A16,FINSEQ_1:57
        .= h by FINSEQ_1:59;
A20:  now
        let k be Nat;
        assume
A21:    k in dom F1n;
        then
A22:    k in dom F2n & k in dom F1n by A15,A17,FINSEQ_3:31;
        0+n <= 1+n by XREAL_1:8;
        then
A23:    dom F1n c= dom F1 by A6,A15,FINSEQ_3:32;
        thus F2n.k = F2.k by A12,A22,FINSEQ_1:def 7
          .= f.(F1.k) by A5,A21,A23
          .= f.(F1n.k) by A11,A22,FINSEQ_1:def 7;
      end;
A24:  len F11 = 1 by FINSEQ_1:57
        .=len I1 by FINSEQ_1:57;
A25:  len F21 = 1 by FINSEQ_1:57
        .=len I1 by FINSEQ_1:57;
A26:  Product(F1 |^ I) = Product((F1n |^ In)^(F11 |^ I1))
      by A7,A11,A13,A15,A24,GROUP_4:25
        .= Product(F1n |^ In) * Product(F11 |^ I1) by GROUP_4:8;
A27:  Product(F2 |^ I) = Product((F2n |^ In)^(F21 |^ I1))
      by A7,A12,A13,A17,A25,GROUP_4:25
        .= Product(F2n |^ In) * Product(F21 |^ I1) by GROUP_4:8;
A28:  f.Product(F11 |^ I1) = f.Product(<*g*>|^<*@i*>)
        .= f.Product <*g|^i*> by GROUP_4:28
        .= f.(g|^i) by GROUP_4:12
        .= (f.g)|^i by GROUP_6:46
        .= h|^i by A5,A18,A19,A8,A9,A10
        .= Product <*h|^i*> by GROUP_4:12
        .= Product(<*h*>|^<*@i*>) by GROUP_4:28
        .= Product(F21 |^ I1);
      f.(Product(F1 |^ I)) = f.Product(F1n |^ In) * f.Product(F11 |^ I1)
      by A26,GROUP_6:def 7
        .= Product(F2n |^ In) * Product(F21 |^ I1) by A4,A13,A15,A17,A20,A28;
      hence thesis by A27;
    end;
  end;
  for n being Nat holds P[n] from NAT_1:sch 2(A2,A3);
  hence thesis by A1;
end;

:: GROUP_4:37

theorem Th24:
  g1 in the_stable_subgroup_of A iff
  ex F being FinSequence of the carrier of G,
  I being FinSequence of INT, C being Subset of G
  st C = the_stable_subset_generated_by (A, the action of G) &
  len F = len I & rng F c= C & Product(F |^ I) = g1
proof
  thus g1 in the_stable_subgroup_of A implies
  ex F being FinSequence of the carrier of G,
  I being FinSequence of INT, C being Subset of G
  st C = the_stable_subset_generated_by (A, the action of G) &
  len F = len I & rng F c= C & Product(F |^ I) = g1
  proof
    assume
A1: g1 in the_stable_subgroup_of A;
    defpred P[set] means ex F being FinSequence of the carrier of G,
    I being FinSequence of INT, C being Subset of G
    st C = the_stable_subset_generated_by (A, the action of G) &
    $1 = Product (F |^ I) & len F = len I & rng F c= C;
    reconsider B = {b where b is Element of G : P[b]}
    as Subset of G from DOMAIN_1:sch 7;
    1_G = Product <*> the carrier of G & <*> the carrier of G = {} &
    (<*> the carrier of G) |^ (<*> INT) = {} & rng <*> the carrier of G = {} &
    {} c= the_stable_subset_generated_by (A, the action of G) &
    len {} = 0 & len<*>INT = 0 by GROUP_4:11,27,XBOOLE_1:2;
    then
A2: 1_G in B;
A3: now
      let c,d be Element of G;
      assume that
A4:   c in B and
A5:   d in B;
A6:   ex d1 being Element of G st c = d1 &
      ex F being FinSequence of the carrier of G,
      I being FinSequence of INT, C being Subset of G
      st C = the_stable_subset_generated_by (A, the action of G) &
      d1 = Product(F |^ I) & len F = len I & rng F c= C by A4;
A7:   ex d2 being Element of G st d = d2 &
      ex F being FinSequence of the carrier of G,
      I being FinSequence of INT, C being Subset of G
      st C = the_stable_subset_generated_by (A, the action of G) &
      d2 = Product(F |^ I) & len F = len I & rng F c= C by A5;
      consider F1 be FinSequence of the carrier of G,
      I1 be FinSequence of INT, C be Subset of G such that
A8:   C = the_stable_subset_generated_by (A, the action of G) and
A9:   c = Product(F1 |^ I1) and
A10:  len F1 = len I1 and
A11:  rng F1 c= C by A6;
      consider F2 be FinSequence of the carrier of G,
      I2 be FinSequence of INT, C be Subset of G such that
A12:  C = the_stable_subset_generated_by (A, the action of G) and
A13:  d = Product(F2 |^ I2) and
A14:  len F2 = len I2 and
A15:  rng F2 c= C by A7;
A16:  c * d = Product((F1 |^ I1) ^ (F2 |^ I2)) by A9,A13,GROUP_4:8
        .= Product((F1 ^ F2) |^ (I1 ^ I2)) by A10,A14,GROUP_4:25;
      rng(F1 ^ F2) = rng F1 \/ rng F2 by FINSEQ_1:44;
      then
A17:  rng(F1 ^ F2) c= C &
      C = the_stable_subset_generated_by (A, the action of G)
      by A8,A11,A12,A15,XBOOLE_1:8;
      len(F1 ^ F2) = len I1 + len I2 by A10,A14,FINSEQ_1:35
        .= len(I1 ^ I2) by FINSEQ_1:35;
      hence c * d in B by A16,A17;
    end;
A18: now
      let c be Element of G;
      assume c in B;
      then ex d1 being Element of G st c = d1 &
      ex F being FinSequence of the carrier of G,
      I being FinSequence of INT, C being Subset of G
      st C = the_stable_subset_generated_by (A, the action of G) &
      d1 = Product(F |^ I) & len F = len I & rng F c= C;
      then consider F1 be FinSequence of the carrier of G,
      I1 be FinSequence of INT, C be Subset of G such that
A19:  C = the_stable_subset_generated_by (A, the action of G) and
A20:  c = Product(F1 |^ I1) and
A21:  len F1 = len I1 and
A22:  rng F1 c= C;
A23:  dom F1 = dom I1 by A21,FINSEQ_3:31;
      deffunc F(Nat) = F1.(len F1 - $1 + 1);
      consider F2 being FinSequence such that
A24:  len F2 = len F1 and
A25:  for k being Nat st k in dom F2
      holds F2.k = F(k) from FINSEQ_1:sch 2;
A27:  Seg len F1 = dom F1 by FINSEQ_1:def 3;
      defpred P[Nat,set] means
      ex i being Integer st i = I1.(len I1 - $1 + 1) & $2 = - i;
A29:  for k being Nat st k in Seg len I1 ex x st P[k,x]
      proof
        let k be Nat;
        assume k in Seg len I1;
        then
A30:    k in dom I1 by FINSEQ_1:def 3;
        then reconsider n = len I1 - k + 1 as Element of NAT by Lm23;
        1 <= n & n <= len I1 by A30,Lm23;
        then n in dom I1 by FINSEQ_3:27;
        then I1.n in rng I1 & rng I1 c= INT by FUNCT_1:def 5;
        then reconsider i = I1.n as Element of INT qua non empty set;
        reconsider i as Integer;
        reconsider x = - i as set;
        take x,i;
        thus thesis;
      end;
      consider I2 being FinSequence such that
A31:  dom I2 = Seg len I1 and
A32:  for k be Nat st k in Seg len I1 holds P[k,I2.k]
      from FINSEQ_1:sch 1(A29);
A33:  Seg len I1 = dom I1 by FINSEQ_1:def 3;
A34:  len F2 = len I2 by A21,A24,A31,FINSEQ_1:def 3;
A35:  dom F2 = dom I2 by A21,A24,A31,FINSEQ_1:def 3;
A36:  rng F2 c= rng F1
      proof
        let x;
        assume x in rng F2;
        then consider y be set such that
A37:    y in dom F2 & F2.y = x by FUNCT_1:def 5;
        reconsider y as Element of NAT by A37;
A38:    x = F1.(len F1 - y + 1) by A25,A37;
        reconsider n = len F1 - y + 1 as Element of NAT by A24,A37,Lm23;
A39:    1 <= n by A24,A37,Lm23;
        n <= len F1 by A24,A37,Lm23;
        then n in dom F1 by A39,FINSEQ_3:27;
        hence thesis by A38,FUNCT_1:def 5;
      end;
      then
A40:  rng F2 c= C by A22,XBOOLE_1:1;
      rng F2 c= the carrier of G by A36,XBOOLE_1:1;
      then reconsider F2 as FinSequence of the carrier of G by FINSEQ_1:def 4;
      rng I2 c= INT
      proof
        let x;
        assume x in rng I2;
        then consider y be set such that
A41:    y in dom I2 and
A42:    x = I2.y by FUNCT_1:def 5;
        reconsider y as Element of NAT by A41;
        ex i being Integer st i = I1.(len I1 - y + 1) & x = - i
        by A31,A32,A41,A42;
        hence thesis by INT_1:def 2;
      end;
      then reconsider I2 as FinSequence of INT by FINSEQ_1:def 4;
      set p = F1 |^ I1;
      set q = F2 |^ I2;
A43:  len p = len F1 & len q = len F2 by GROUP_4:def 4;
      then
A44:  dom p = dom F1 & dom q = dom F2 by FINSEQ_3:31;
      now
        let k be Element of NAT;
        assume
A45:    k in dom q;
        then reconsider n = len p - k + 1 as Element of NAT by A24,A43,Lm23;
        1 <= n & len p >= n by A24,A43,A45,Lm23;
        then
A46:    n in dom I2 & n in Seg len I2 by A24,A34,A43,FINSEQ_3:27;
        dom q = dom I1 by A21,A24,A43,FINSEQ_3:31;
        then consider i be Integer such that
A47:    i = I1.n & I2.k = - i by A21,A32,A33,A43,A45;
        I2.k = I2/.k & I2/.k = @(I2/.k) by A35,A44,A45,PARTFUN1:def 8;
        then q.k = (F2/.k) |^ (- i) & F2/.k = F2.k & F2.k = F1.n & F1/.n= F1.n
        by A21,A25,A27,A31,A43,A44,A45,A46,A47,GROUP_4:def 4,PARTFUN1:def 8;
        then
A48:    q.k = ((F1/.n) |^ i)" by GROUP_1:70;
        I1.n = I1/.n & I1/.n = @(I1/.n) by A31,A33,A46,PARTFUN1:def 8;
        then p.n = (F1/.n) |^ i by A23,A31,A33,A46,A47,GROUP_4:def 4;
        then (p/.n)" = ((F1/.n) |^ i)" & q/.k = q.k
        by A23,A31,A33,A44,A45,A46,PARTFUN1:def 8;
        then p/.n = (q/.k)" by A48;
        hence (q/.k)" = p.(len p - k + 1)
        by A23,A31,A33,A44,A46,PARTFUN1:def 8;
      end;
      then Product p" = Product q by A24,A43,GROUP_4:17;
      hence c" in B by A19,A20,A34,A40;
    end;
    now
      let o be Element of O;
      let c be Element of G;
      assume c in B;
      then ex d1 being Element of G st c = d1 &
      ex F being FinSequence of the carrier of G,
      I being FinSequence of INT, C being Subset of G
      st C = the_stable_subset_generated_by (A, the action of G) &
      d1 = Product(F |^ I) & len F = len I & rng F c= C;
      then consider F1 be FinSequence of the carrier of G,
      I1 be FinSequence of INT, C be Subset of G such that
A49:  C = the_stable_subset_generated_by (A, the action of G) and
A50:  c = Product(F1 |^ I1) and
A51:  len F1 = len I1 and
A52:  rng F1 c= C;
      deffunc F(Nat) = (G^o).(F1.$1);
      consider F2 being FinSequence such that
A53:  len F2 = len F1 and
A54:  for k being Nat
      st k in dom F2 holds F2.k = F(k) from FINSEQ_1:sch 2;
A55:    dom F2 = dom F1 by A53,FINSEQ_3:31;
A56:  Seg len F1 = dom F1 by FINSEQ_1:def 3;
      now
        let y be set;
        assume y in rng F2;
        then consider x such that
A57:    x in dom F2 & y = F2.x by FUNCT_1:def 5;
A58:    x in Seg len F1 by A53,A57,FINSEQ_1:def 3;
        reconsider x as Element of NAT by A57;
A59:    F2.x = (G^o).(F1.x) by A54,A57;
A60:    F1.x in rng F1 by A56,A58,FUNCT_1:12;
A61:    C is_stable_under_the_action_of the action of G by A49,Def2;
        per cases;
        suppose
A62:      O<>{};
          then
A63:      G^o = (the action of G).o by Def6;
          set f = (the action of G).o;
          reconsider f as Function of G, G by A63;
          dom f = the carrier of G by FUNCT_2:def 1;
          then
A64:      y in f .: C by A52,A57,A59,A60,A63,FUNCT_1:def 12;
          (f .: C) c= C by A61,A62,Def1;
          hence y in C by A64;
        end;
        suppose O={};
          then G^o = id the carrier of G by Def6;
          then (G^o).(F1.x) = F1.x by A60,FUNCT_1:35;
          hence y in C by A52,A57,A59,A60;
        end;
      end;
      then
A65:  rng F2 c= C by TARSKI:def 3;
      then rng F2 c= the carrier of G by XBOOLE_1:1;
      then reconsider F2 as FinSequence of the carrier of G by FINSEQ_1:def 4;
      (G^o).c = Product(F2 |^ I1) by A50,A51,A53,A54,Lm24,A55;
      hence (G^o).c in B by A49,A51,A53,A65;
    end;
    then consider H be strict StableSubgroup of G such that
A66: the carrier of H = B by A2,A3,A18,Lm15;
    A c= B
    proof
      let x;
      assume
A67:  x in A;
      then reconsider a = x as Element of G;
      reconsider p = 1 as Integer;
A68:  len<* a *> = 1 & len<* @p *> = 1 by FINSEQ_1:56;
A69:  Product(<* a *> |^ <* @p *>) = Product<* a |^ 1 *> by GROUP_4:28
        .= a |^ 1 by GROUP_4:12
        .= a by GROUP_1:44;
      set C = the_stable_subset_generated_by (A, the action of G);
      A c= C & {a} c= A by A67,Def2,ZFMISC_1:37;
      then rng<* a *> = {a} & {a} c= C by FINSEQ_1:56,XBOOLE_1:1;
      hence thesis by A68,A69;
    end;
    then the_stable_subgroup_of A is StableSubgroup of H by A66,Def29;
    then the_stable_subgroup_of A is Subgroup of H by Def7;
    then g1 in H by A1,GROUP_2:49;
    then g1 in B by A66,STRUCT_0:def 5;
    then ex b being Element of G st
    b = g1 & ex F being FinSequence of the carrier of G,
    I being FinSequence of INT, C being Subset of G
    st C = the_stable_subset_generated_by (A, the action of G) &
    b = Product(F |^ I) & len F = len I & rng F c= C;
    hence thesis;
  end;
  given F be FinSequence of the carrier of G, I be FinSequence of INT,
  C be Subset of G such that
A70: C = the_stable_subset_generated_by (A, the action of G) and
  len F = len I and
A71: rng F c= C and
A72: Product(F |^ I) = g1;
  set H' = the_stable_subgroup_of A;
A73: A c= the carrier of H' & (for H being strict StableSubgroup of G
  st A c= the carrier of H holds H' is StableSubgroup of H) by Def29;
  set Y = the carrier of H';
  H' is Subgroup of G by Def7;
  then reconsider Y as Subset of G by GROUP_2:def 5;
  now
    let o be Element of O;
    let f be Function of G, G;
    assume
A74: o in O;
    assume
A75: f = (the action of G).o;
    now
      let y be set;
      assume y in f .: Y;
      then consider x such that
A76:  x in dom f & x in Y & y = f.x by FUNCT_1:def 12;
      reconsider x as Element of G by A76;
      x in H' by A76,STRUCT_0:def 5;
      then (G^o).x in H' by Lm10;
      then f.x in H' by A74,A75,Def6;
      hence y in Y by A76,STRUCT_0:def 5;
    end;
    hence (f .: Y) c= Y by TARSKI:def 3;
  end;
  then
A77: Y is_stable_under_the_action_of the action of G by Def1;
  reconsider H' as Subgroup of G by Def7;
  C c= the carrier of H' by A70,A73,A77,Def2;
  then rng F c= carr H' by A71,XBOOLE_1:1;
  hence thesis by A72,GROUP_4:26;
end;

Lm25: A is empty implies the_stable_subgroup_of A = (1).G
proof
  assume A is empty;
  then
A1: A c= the carrier of (1).G by XBOOLE_1:2;
  now
    let H be strict StableSubgroup of G;
    assume A c= the carrier of H;
    (1).G = (1).H by Th15;
    hence (1).G is StableSubgroup of H;
  end;
  hence thesis by A1,Def29;
end;

Lm26: for O being non empty set, E being set, o being Element of O,
A being Action of O,E holds Product(<*o*>,A) = A.o
proof
  let O be non empty set;
  let E be set;
  let o be Element of O;
  let A be Action of O,E;
A1: len <*o*> = 1 by FINSEQ_1:56;
  consider PF be FinSequence of Funcs(E,E) such that
A2: Product(<*o*>,A) = PF.(len <*o*>) & len PF = len <*o*> &
  PF.1 = A.(<*o*>.1) & (for k being Nat st k<>0 & k<len <*o*> holds
  ex f,g being Function of E,E
  st f = PF.k & g = A.(<*o*>.(k+1)) & PF.(k+1) = f*g) by Def3;
  thus thesis by A1,A2,FINSEQ_1:57;
end;

Lm27: for O being non empty set, E being set, o being Element of O,
F being FinSequence of O, A being Action of O,E
holds Product(F^<*o*>,A) = Product(F,A)*Product(<*o*>,A)
proof
  let O be non empty set;
  let E be set;
  let o be Element of O;
  let F be FinSequence of O;
  let A be Action of O,E;
  set F1=F^<*o*>;
A1: len F1 = len F + len <*o*> by FINSEQ_1:35
    .= len F + 1 by FINSEQ_1:56;
  consider PF1 be FinSequence of Funcs(E,E) such that
A2: Product(F1,A) = PF1.(len F1) & len PF1 = len F1 & PF1.1 = A.(F1.1) &
  (for k being Nat st k<>0 & k<len F1 holds ex f,g being Function of E,E
  st f = PF1.k & g = A.(F1.(k+1)) & PF1.(k+1) = f*g) by Def3;
  per cases;
  suppose
A3: len F <> 0;
A4: len F<len F1 by A1,NAT_1:13;
    then consider f,g be Function of E,E such that
A5: f = PF1.(len F) & g = A.(F1.(len F+1)) & PF1.(len F+1) = f*g by A2,A3;
A6: Product(<*o*>,A) = A.o by Lm26
      .= A.(F1.(len F + 1)) by FINSEQ_1:59;
    reconsider PF=PF1|(Seg len F) as FinSequence of Funcs(E,E) by FINSEQ_1:23;
    set IT = PF.(len F);
A7: len F in Seg len F by A3,FINSEQ_1:5;
    Seg len F c= Seg len PF1 by A2,A4,FINSEQ_1:7;
    then len F in Seg len PF1 by A7;
    then len F in dom PF1 by FINSEQ_1:def 3;
    then len F in dom PF1 /\ Seg len F by A7,XBOOLE_0:def 4;
    then len F in dom PF by RELAT_1:90;
    then IT in rng PF by FUNCT_1:12;
    then ex f being Function
    st IT = f & dom f = E & rng f c= E by FUNCT_2:def 2;
    then reconsider IT as Function of E,E by FUNCT_2:4;
A8: len PF = len F by A2,A4,FINSEQ_1:21;
    0<len F by A3;
    then 0+1<len F+1 by XREAL_1:8;
    then 1<=len F by NAT_1:13;
    then
A9: 1 in Seg len F;
    then
A10: 1 in dom F by FINSEQ_1:def 3;
A11: PF.1 = A.(F1.1) by A2,A9,FUNCT_1:72
      .= A.(F.1) by A10,FINSEQ_1:def 7;
    now
      let k be Nat;
      assume
A12:  k<>0;
      assume
A13:  k<len F;
      then k<len F1 by A1,NAT_1:13;
      then consider f,g be Function of E,E such that
A14:  f = PF1.k & g = A.(F1.(k+1)) & PF1.(k+1) = f*g by A2,A12;
      take f,g;
      0<k by A12;
      then
A15:  0+1<k+1 by XREAL_1:8;
      then 1<=k by NAT_1:13;
      then
A16:  k in Seg len F by A13,FINSEQ_1:3;
      k+1<=len F by A13,NAT_1:13;
      then
A17:  k+1 in Seg len F by A15;
      then
A18:  k+1 in dom F by FINSEQ_1:def 3;
      thus f = PF.k by A14,A16,FUNCT_1:72;
      thus g = A.(F.(k+1)) by A14,A18,FINSEQ_1:def 7;
      thus PF.(k+1) = f*g by A14,A17,FUNCT_1:72;
    end;
    then
A19: IT = Product(F,A) by A3,A8,A11,Def3;
    len F in Seg len F by A3,FINSEQ_1:5;
    hence thesis by A1,A2,A5,A6,A19,FUNCT_1:72;
  end;
  suppose
A20: len F = 0;
    hence Product(F^<*o*>,A) = Product(<*>O^<*o*>,A) by FINSEQ_1:32
      .= Product(<*o*>,A) by FINSEQ_1:47
      .= (id E)*Product(<*o*>,A) by FUNCT_2:23
      .= Product(F,A)*Product(<*o*>,A) by A20,Def3;
  end;
end;

Lm28: for O being non empty set, E being set, o being Element of O,
F being FinSequence of O, A being Action of O,E
holds Product(<*o*>^F,A) = Product(<*o*>,A)*Product(F,A)
proof
  let O be non empty set;
  let E be set;
  let o be Element of O;
  let F be FinSequence of O;
  let A be Action of O,E;
  defpred P[Element of NAT] means
  for F being FinSequence of O st len F = $1 holds
  Product(<*o*>^F,A) = Product(<*o*>,A)*Product(F,A);
A1: P[0]
  proof
    let F be FinSequence of O;
    assume
A2: len F = 0;
    hence Product(<*o*>^F,A) = Product(<*o*>^<*>O,A) by FINSEQ_1:32
      .= Product(<*o*>,A) by FINSEQ_1:47
      .= Product(<*o*>,A)*id E by FUNCT_2:23
      .= Product(<*o*>,A)*Product(F,A) by A2,Def3;
  end;
A3: for k being Element of NAT st P[k] holds P[k + 1]
  proof
    let k be Element of NAT;
    assume
A4: P[k];
    now
      let F be FinSequence of O;
      assume
A5:   len F = k+1;
      then consider Fk be FinSequence of O, o' be Element of O such that
A6:   F = Fk^<*o'*> by FINSEQ_2:22;
      len F = len Fk + len <*o'*> by A6,FINSEQ_1:35;
      then
A7:   k+1 = len Fk + 1 by A5,FINSEQ_1:56;
      set F2k=<*o*>^Fk;
      thus Product(<*o*>^F,A) = Product(<*o*>^Fk^<*o'*>,A) by A6,FINSEQ_1:45
        .= Product(F2k,A)*Product(<*o'*>,A) by Lm27
        .= Product(<*o*>,A)*Product(Fk,A)*Product(<*o'*>,A) by A4,A7
        .= Product(<*o*>,A)*((Product(Fk,A))*Product(<*o'*>,A)) by RELAT_1:55
        .= Product(<*o*>,A)*Product(F,A) by A6,Lm27;
    end;
    hence thesis;
  end;
A8: for k being Element of NAT holds P[k] from NAT_1:sch 1(A1,A3);
  reconsider k = len F as Element of NAT;
  k = len F;
  hence thesis by A8;
end;

Lm29: for O being non empty set, E being set,
F1,F2 being FinSequence of O, A being Action of O,E
holds Product(F1^F2,A) = Product(F1,A) * Product(F2,A)
proof
  let O be non empty set, E be set;
  let F1,F2 be FinSequence of O;
  let A be Action of O,E;
  defpred P[Element of NAT] means
  for F1,F2 being FinSequence of O st len F1 = $1 holds
  Product(F1^F2,A) = Product(F1,A)*Product(F2,A);
A1: P[0]
  proof
    let F1,F2 be FinSequence of O;
    assume
A2: len F1 = 0;
    hence Product(F1^F2,A) = Product(<*>O^F2,A) by FINSEQ_1:32
      .= Product(F2,A) by FINSEQ_1:47
      .= (id E)*Product(F2,A) by FUNCT_2:23
      .= Product(F1,A)*Product(F2,A) by A2,Def3;
  end;
A3: for k being Element of NAT st P[k] holds P[k + 1]
  proof
    let k be Element of NAT;
    assume
A4: P[k];
    now
      let F1,F2 be FinSequence of O;
      assume
A5:   len F1 = k+1;
      then consider F1k be FinSequence of O, o be Element of O such that
A6:   F1 = F1k^<*o*> by FINSEQ_2:22;
      len F1 = len F1k + len <*o*> by A6,FINSEQ_1:35;
      then
A7:   k+1 = len F1k + 1 by A5,FINSEQ_1:56;
      set F2k=<*o*>^F2;
      thus Product(F1^F2,A) = Product(F1k^F2k,A) by A6,FINSEQ_1:45
        .= Product(F1k,A)*Product(F2k,A) by A4,A7
        .= Product(F1k,A)*(Product(<*o*>,A)*Product(F2,A)) by Lm28
        .= (Product(F1k,A)*Product(<*o*>,A))*Product(F2,A) by RELAT_1:55
        .= Product(F1,A)*Product(F2,A) by A4,A6,A7;
    end;
    hence thesis;
  end;
A8: for k being Element of NAT holds P[k] from NAT_1:sch 1(A1,A3);
  reconsider k = len F1 as Element of NAT;
  k = len F1;
  hence thesis by A8;
end;

Lm30: for O,E being set, F being FinSequence of O,
Y being Subset of E, A being Action of O,E
st Y is_stable_under_the_action_of A holds Product(F,A) .: Y c= Y
proof
  let O,E be set;
  let F be FinSequence of O;
  let Y be Subset of E;
  let A be Action of O,E;
  assume
A1: Y is_stable_under_the_action_of A;
  per cases;
  suppose O={};
    then rng F = {};
    then F = {};
    then len F = 0;
    then Product(F,A) = id E by Def3;
    hence thesis by FUNCT_1:162;
  end;
  suppose
A2: O<>{};
    defpred P[Element of NAT] means
    for F being FinSequence of O st len F = $1 holds Product(F,A) .: Y c= Y;
A3: P[0]
    proof
      let F be FinSequence of O;
      assume len F = 0;
      then Product(F,A) = id E by Def3;
      hence thesis by FUNCT_1:162;
    end;
A4: for k being Element of NAT st P[k] holds P[k + 1]
    proof
      let k be Element of NAT;
      assume
A5:   P[k];
      now
        let F be FinSequence of O;
        assume
A6:     len F = k+1;
        then consider Fk be FinSequence of O, o be Element of O such that
A7:     F = Fk^<*o*> by FINSEQ_2:22;
        reconsider F1 = <*o*> as FinSequence of O by A7,FINSEQ_1:50;
        len F = len Fk + len <*o*> by A7,FINSEQ_1:35;
        then
A8:     k+1 = len Fk + 1 by A6,FINSEQ_1:56;
        Product(F,A) = Product(Fk,A)*Product(F1,A) by A2,A7,Lm29;
        then
A9:     Product(F,A) .: Y = Product(Fk,A) .: (Product(F1,A) .: Y)
        by RELAT_1:159;
        Product(F1,A) = A.o & o in O by A2,Lm26;
        then Product(F1,A) .: Y c= Y by A1,Def1;
        then
A10:    Product(F,A) .: Y c= Product(Fk,A) .: Y by A9,RELAT_1:156;
        Product(Fk,A) .: Y c= Y by A5,A8;
        hence Product(F,A) .: Y c= Y by A10,XBOOLE_1:1;
      end;
      hence thesis;
    end;
A11: for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A4);
    reconsider k = len F as Element of NAT;
    k = len F;
    hence thesis by A11;
  end;
end;

Lm31: for E being non empty set, A being Action of O,E holds
for X being Subset of E, a being Element of E st X is not empty holds
a in the_stable_subset_generated_by(X,A) iff
ex F being FinSequence of O, x being Element of X st Product(F,A).x = a
proof
  let E be non empty set;
  let A be Action of O,E;
  let X be Subset of E;
  let a be Element of E;
  assume
A1: X is not empty;
  defpred P[set] means
  ex F being FinSequence of O, x being Element of X st Product(F,A).x = $1;
  set B = {e where e is Element of E: P[e]};
  reconsider B as Subset of E from DOMAIN_1:sch 7;
  now
    let x;
    assume
A2: x in X;
    set F=<*>O;
    len F = 0;
    then
A3: Product(F,A).x = (id E).x by Def3
      .= x by A2,FUNCT_1:35;
    reconsider e=x as Element of E by A2;
    reconsider x'=e as Element of X by A2;
    Product(F,A).x' = e by A3;
    hence x in B;
  end;
  then
A4: X c= B by TARSKI:def 3;
  now
    let o be Element of O;
    let f be Function of E, E;
    assume
A5: o in O;
    assume
A6: f = A.o;
    per cases;
    suppose O={};
      hence (f .: B) c= B by A5;
    end;
    suppose
A7:   O<>{};
      now
        let y be set;
        assume y in f .: B;
        then consider x such that
A8:     x in dom f & x in B & y = f.x by FUNCT_1:def 12;
        consider e' be Element of E such that
A9:     e'=x & ex F' being FinSequence of O, x' being Element of X
        st Product(F',A).x' = e' by A8;
        consider F' be FinSequence of O, x' be Element of X such that
A10:    Product(F',A).x' = e' by A9;
        reconsider o as Element of O;
        reconsider F''=<*o*> as FinSequence of O by A7,FINSEQ_1:95;
        reconsider F=F''^F' as FinSequence of O;
        y in rng f by A8,FUNCT_1:12;
        then reconsider e=y as Element of E;
        x' in X by A1;
        then x' in E;
        then
A11:    x' in dom Product(F',A) by FUNCT_2:def 1;
        Product(F,A).x' = (Product(F'',A)*Product(F',A)).x' by A7,Lm29
          .= Product(F'',A).(Product(F',A).x') by A11,FUNCT_1:23
          .= e by A6,A7,A8,A9,A10,Lm26;
        hence y in B;
      end;
      hence (f .: B) c= B by TARSKI:def 3;
    end;
  end;
  then
A12: B is_stable_under_the_action_of A by Def1;
  now
    let Y be Subset of E;
    assume
A13: Y is_stable_under_the_action_of A;
    assume
A14: X c= Y;
    now
      let x;
      assume x in B;
      then consider e be Element of E such that
A15:  x=e & ex F being FinSequence of O, x' being Element of X
      st Product(F,A).x' = e;
      consider F be FinSequence of O, x' be Element of X such that
A16:  Product(F,A).x' = e by A15;
A17:  x' in X by A1;
      then x' in E;
      then
A18:  x' in dom Product(F,A) by FUNCT_2:def 1;
A19:  Product(F,A).:Y c= Y by A13,Lm30;
      Product(F,A).x' in Product(F,A).:Y by A14,A17,A18,FUNCT_1:def 12;
      hence x in Y by A15,A16,A19;
    end;
    hence B c= Y by TARSKI:def 3;
  end;
  then
A20: B = the_stable_subset_generated_by(X,A) by A4,A12,Def2;
  hereby
    assume a in the_stable_subset_generated_by(X,A);
    then consider e be Element of E such that
A21: a=e & ex F being FinSequence of O, x being Element of X
    st Product(F,A).x = e by A20;
    consider F be FinSequence of O, x be Element of X such that
A22: Product(F,A).x = e by A21;
    take F, x;
    thus Product(F,A).x = a by A21,A22;
  end;
  given F be FinSequence of O, x be Element of X such that
A23: Product(F,A).x = a;
  thus thesis by A20,A23;
end;

:: GROUP_4:40

theorem Th25:
  for H being strict StableSubgroup of G holds
  the_stable_subgroup_of carr H = H
proof
  let H be strict StableSubgroup of G;
  for H1 be strict StableSubgroup of G st carr H c= the carrier of H1 holds
  H is StableSubgroup of H1 by Lm21;
  hence thesis by Def29;
end;

:: GROUP_4:41

theorem Th26:
  A c= B implies
  the_stable_subgroup_of A is StableSubgroup of the_stable_subgroup_of B
proof
  assume
A1: A c= B;
  per cases;
  suppose
A2: A is empty;
    reconsider H1 = (1).G,H2 = (1).(the_stable_subgroup_of B)
    as strict StableSubgroup of G by Th11;
    the carrier of H1 = {1_G} by Def8
      .= {1_(the_stable_subgroup_of B)} by Th4
      .= the carrier of H2 by Def8;
    then (1).G = (1).(the_stable_subgroup_of B) by Lm5;
    hence thesis by A2,Lm25;
  end;
  suppose
A3: A is not empty;
    now
      let a be Element of G;
      assume a in the_stable_subgroup_of A;
      then consider F be FinSequence of the carrier of G,
      I be FinSequence of INT, C be Subset of G such that
A4:   C = the_stable_subset_generated_by (A, the action of G) &
      len F = len I & rng F c= C & Product(F |^ I) = a by Th24;
      set D = the_stable_subset_generated_by (B, the action of G);
      now
        let y be set;
        assume
A5:     y in C;
        then reconsider b=y as Element of G;
        consider F1 be FinSequence of O, x be Element of A such that
A6:     Product(F1,the action of G).x = b by A3,A4,A5,Lm31;
        x in A by A3;
        hence y in D by A1,A6,Lm31;
      end;
      then C c= D by TARSKI:def 3;
      then rng F c= D by A4,XBOOLE_1:1;
      hence a in the_stable_subgroup_of B by A4,Th24;
    end;
    hence thesis by Th13;
  end;
end;

scheme MeetSbgWOpEx{O() -> set, G() -> GroupWithOperators of O(), P[set]}:
  ex H being strict StableSubgroup of G() st the carrier of H =
  meet{A where A is Subset of G() :
  ex K being strict StableSubgroup of G() st A = the carrier of K & P[K]}
provided
A1: ex H being strict StableSubgroup of G() st P[H]
proof
  set X = {A where A is Subset of G(): ex K being strict StableSubgroup of G()
  st A = the carrier of K & P[K]};
  consider T being strict StableSubgroup of G() such that
A2: P[T] by A1;
A3: carr T in X by A2;
  then reconsider Y = meet X as Subset of G() by SETFAM_1:8;
  now
    let Z be set;
    assume Z in X;
    then consider A being Subset of G() such that
A4: Z = A and
A5: ex K being strict StableSubgroup of G() st A = the carrier of K & P[K];
    consider H being StableSubgroup of G() such that
A6: A = the carrier of H & P[H] by A5;
    1_G() in H by Lm18;
    hence 1_G() in Z by A4,A6,STRUCT_0:def 5;
  end;
  then
A7: Y <> {} by A3,SETFAM_1:def 1;
A8: now
    let a,b be Element of G();
    assume
A9: a in Y & b in Y;
    now
      let Z be set;
      assume
A10:  Z in X;
      then consider A being Subset of G() such that
A11:  A = Z and
A12:  ex H being strict StableSubgroup of G() st A = the carrier of H & P[H];
      consider H being StableSubgroup of G() such that
A13:  A = the carrier of H & P[H] by A12;
      a in the carrier of H & b in the carrier of H
      by A9,A10,A11,A13,SETFAM_1:def 1;
      then a in H & b in H by STRUCT_0:def 5;
      then a * b in H by Lm19;
      hence a * b in Z by A11,A13,STRUCT_0:def 5;
    end;
    hence a * b in Y by A3,SETFAM_1:def 1;
  end;
A14: now
    let a be Element of G();
    assume
A15: a in Y;
    now
      let Z be set;
      assume
A16:  Z in X;
      then consider A being Subset of G() such that
A17:  A = Z and
A18:  ex H being strict StableSubgroup of G() st A = the carrier of H & P[H];
      consider H being StableSubgroup of G() such that
A19:  A = the carrier of H & P[H] by A18;
      a in the carrier of H by A15,A16,A17,A19,SETFAM_1:def 1;
      then a in H by STRUCT_0:def 5;
      then a" in H by Lm20;
      hence a" in Z by A17,A19,STRUCT_0:def 5;
    end;
    hence a" in Y by A3,SETFAM_1:def 1;
  end;
  now
    let o be Element of O();
    let a be Element of G();
    assume
A20: a in Y;
    now
      let Z be set;
      assume
A21:  Z in X;
      then consider A being Subset of G() such that
A22:  A = Z and
A23:  ex H being strict StableSubgroup of G() st A = the carrier of H & P[H];
      consider H being StableSubgroup of G() such that
A24:  A = the carrier of H & P[H] by A23;
      a in the carrier of H by A20,A21,A22,A24,SETFAM_1:def 1;
      then a in H by STRUCT_0:def 5;
      then (G()^o).a in H by Lm10;
      hence (G()^o).a in Z by A22,A24,STRUCT_0:def 5;
    end;
    hence (G()^o).a in Y by A3,SETFAM_1:def 1;
  end;
  hence thesis by A7,A8,A14,Lm15;
end;

:: GROUP_4:43

theorem Th27:
  the carrier of the_stable_subgroup_of A =
  meet{B where B is Subset of G: ex H being strict StableSubgroup of G
  st B = the carrier of H & A c= carr H}
proof
  defpred P[StableSubgroup of G] means A c= carr $1;
  A c= the carrier of G & the carrier of (Omega).G = carr (Omega).G &
  (Omega).G = the HGrWOpStr of G;
  then
A1: ex H being strict StableSubgroup of G st P[H];
  consider H being strict StableSubgroup of G such that
A2: the carrier of H =
  meet{B where B is Subset of G: ex H being strict StableSubgroup of G
  st B = the carrier of H & P[H]} from MeetSbgWOpEx(A1);
  set X = {B where B is Subset of G :ex H being strict StableSubgroup of G
  st B = the carrier of H & A c= carr H};
A3: carr (Omega).G in X;
  now
    let Y be set;
    assume Y in X;
    then ex B being Subset of G st Y = B &
    ex H being strict StableSubgroup of G
    st B = the carrier of H & A c= carr H;
    hence A c= Y;
  end;
  then
A4: A c= the carrier of H by A2,A3,SETFAM_1:6;
  now
    let H1 be strict StableSubgroup of G;
    assume
A5: A c= the carrier of H1;
    the carrier of H1 = carr H1;
    then the carrier of H1 in X by A5;
    hence H is StableSubgroup of H1 by A2,Lm21,SETFAM_1:4;
  end;
  hence thesis by A2,A4,Def29;
end;

Lm32: B = the carrier of gr A implies
the_stable_subgroup_of A = the_stable_subgroup_of B
proof
  assume
A1: B = the carrier of gr A;
  then
A2: the carrier of gr A c= the carrier of the_stable_subgroup_of B by Def29;
  A c= the carrier of gr A by GROUP_4:def 5;
  then
A3: A c= the carrier of the_stable_subgroup_of B by A2,XBOOLE_1:1;
  now
    let H be strict StableSubgroup of G;
    assume
A4: A c= the carrier of H;
    reconsider H'=the multMagma of H as strict Subgroup of G by Lm16;
    gr A is Subgroup of H' by A4,GROUP_4:def 5;
    then B c= the carrier of H' by A1,GROUP_2:def 5;
    hence the_stable_subgroup_of B is StableSubgroup of H by Def29;
  end;
  hence thesis by A3,Def29;
end;

:: GROUP_4:64

theorem Th28:
  for N1,N2 being strict normal StableSubgroup of G holds N1 * N2 = N2 * N1
proof
  let N1,N2 be strict normal StableSubgroup of G;
  reconsider N1'= the multMagma of N1,N2'= the multMagma of N2
  as strict normal Subgroup of G by Lm7;
  thus N1 * N2 = carr N2' * carr N1' by GROUP_3:148
    .= N2 * N1;
end;

:: GROUP_4:68

theorem Th29:
  H1 "\/" H2 = the_stable_subgroup_of(H1 * H2)
proof
  reconsider H1'=H1,H2'=H2 as Subgroup of G by Def7;
  reconsider Y = the carrier of (H1'"\/"H2') as Subset of G by GROUP_2:def 5;
A1: Y = the carrier of gr(H1'*H2') by GROUP_4:68;
  H1 "\/" H2 = the_stable_subgroup_of Y by Lm32
    .= the_stable_subgroup_of(H1'*H2') by A1,Lm32;
  hence thesis;
end;

:: GROUP_4:69

theorem Th30:
  H1 * H2 = H2 * H1 implies the carrier of H1 "\/" H2 = H1 * H2
proof
  assume H1 * H2 = H2 * H1;
  then consider H being strict StableSubgroup of G such that
A1: the carrier of H = carr H1 * carr H2 by Th17;
  carr H1 \/ carr H2 c= carr H1 * carr H2
  proof
    let x;
    assume
A2: x in carr H1 \/ carr H2;
    then reconsider a = x as Element of G;
    now per cases by A2,XBOOLE_0:def 3;
      suppose
A3:     x in carr H1;
        1_G in H2 by Lm18;
        then 1_G in carr H2 & a * 1_G = a by GROUP_1:def 5,STRUCT_0:def 5;
        hence thesis by A3;
      end;
      suppose
A4:     x in carr H2;
        1_G in H1 by Lm18;
        then 1_G in carr H1 & 1_G * a = a by GROUP_1:def 5,STRUCT_0:def 5;
        hence thesis by A4;
      end;
    end;
    hence thesis;
  end;
  then
A5: H1 "\/" H2 is StableSubgroup of H by A1,Def29;
  now
    let a be Element of G;
    assume a in H;
    then a in carr H1 * carr H2 by A1,STRUCT_0:def 5;
    then consider b,c be Element of G such that
A6: a = b * c and
A7: b in carr H1 & c in carr H2;
    set A = carr H1 \/ carr H2;
    set X = {B where B is Subset of G: ex H being strict StableSubgroup of G
    st B = the carrier of H & A c= carr H};
A8: carr (Omega).G in X;
    now
      let Y be set;
      assume Y in X;
      then consider B be Subset of G such that
A9:   Y = B & ex H being strict StableSubgroup of G
      st B = the carrier of H & A c= carr H;
      consider H' be strict StableSubgroup of G such that
A10:  B = the carrier of H' & A c= carr H' by A9;
A11:  H' is Subgroup of G by Def7;
      b in A & c in A by A7,XBOOLE_0:def 3;
      then b in H' & c in H' by A10,STRUCT_0:def 5;
      then b * c in H' by A11,GROUP_2:59;
      hence a in Y by A6,A9,A10,STRUCT_0:def 5;
    end;
    then a in meet X by A8,SETFAM_1:def 1;
    then a in the carrier of the_stable_subgroup_of A by Th27;
    hence a in H1 "\/" H2 by STRUCT_0:def 5;
  end;
  then H is StableSubgroup of H1 "\/" H2 by Th13;
  then H is Subgroup of H1 "\/" H2 & H1 "\/" H2 is Subgroup of H by A5,Def7;
  then the carrier of H c= the carrier of H1 "\/" H2
  & the carrier of H1 "\/" H2 c= the carrier of H by GROUP_2:def 5;
  hence thesis by A1,XBOOLE_0:def 10;
end;

:: GROUP_4:71

theorem Th31:
  for N1,N2 being strict normal StableSubgroup of G
  holds the carrier of N1 "\/" N2 = N1 * N2
proof
  let N1,N2 be strict normal StableSubgroup of G;
  N1 * N2 = N2 * N1 by Th28;
  hence thesis by Th30;
end;

:: GROUP_4:72

theorem Th32:
  for N1,N2 being strict normal StableSubgroup of G
  holds N1 "\/" N2 is normal StableSubgroup of G
proof
  let N1,N2 be strict normal StableSubgroup of G;
  consider N be strict normal StableSubgroup of G such that
A1: the carrier of N = carr N1 * carr N2 by Th23;
  the carrier of N1 "\/" N2 = N1 * N2 & N1 * N2 = carr N1 * carr N2 by Th31;
  hence thesis by A1,Lm5;
end;

:: GROUP_4:76

theorem Th33:
  for H being strict StableSubgroup of G holds
  (1).G "\/" H = H & H "\/" (1).G = H
proof
  let H be strict StableSubgroup of G;
A1: carr(1).G = {1_G} by Def8;
  1_G in H by Lm18;
  then 1_G in carr H by STRUCT_0:def 5;
  then {1_G} c= carr H by ZFMISC_1:37;
  then {1_G} \/ carr H = carr H by XBOOLE_1:12;
  hence thesis by A1,Th25;
end;

:: GROUP_4:77

theorem Th34:
  (Omega).G "\/" H1 = (Omega).G & H1 "\/" (Omega).G = (Omega).G
proof
  (the carrier of (Omega).G) \/ carr H1 = [#] the carrier of G by SUBSET_1:28;
  hence thesis by Th25;
end;

:: Lm7 in GROUP_4

Lm33: H1 is StableSubgroup of H1 "\/" H2
proof
  carr H1 c= carr H1 \/ carr H2 & carr H1 \/ carr H2 c= the carrier of
  the_stable_subgroup_of (carr H1 \/ carr H2) &
  H1 "\/" H2 = the_stable_subgroup_of (carr H1 \/ carr H2) &
  the carrier of H1 = carr H1 by Def29,XBOOLE_1:7;
  hence thesis by Lm21,XBOOLE_1:1;
end;

:: GROUP_4:78

theorem Th35:
  H1 is StableSubgroup of H1 "\/" H2 & H2 is StableSubgroup of H1 "\/" H2
proof
  H1 "\/" H2 = H2 "\/" H1;
  hence thesis by Lm33;
end;

:: GROUP_4:79

theorem Th36:
  for H2 being strict StableSubgroup of G holds
  H1 is StableSubgroup of H2 iff H1 "\/" H2 = H2
proof
  let H2 be strict StableSubgroup of G;
  thus H1 is StableSubgroup of H2 implies H1 "\/" H2 = H2
  proof
    assume H1 is StableSubgroup of H2;
    then H1 is Subgroup of H2 by Def7;
    then the carrier of H1 c= the carrier of H2 &
    the carrier of H1 = carr H1 & carr H2 = the carrier of H2
    by GROUP_2:def 5;
    hence H1 "\/" H2 = the_stable_subgroup_of carr H2 by XBOOLE_1:12
      .= H2 by Th25;
  end;
  thus thesis by Th35;
end;

:: GROUP_4:81

theorem Th37:
  for H3 being strict StableSubgroup of G holds
  H1 is StableSubgroup of H3 & H2 is StableSubgroup of H3 implies
  H1 "\/" H2 is StableSubgroup of H3
proof
  let H3 be strict StableSubgroup of G;
  assume H1 is StableSubgroup of H3 & H2 is StableSubgroup of H3;
  then H1 is Subgroup of H3 & H2 is Subgroup of H3 by Def7;
  then carr H1 c= carr H3 & carr H2 c= carr H3 by GROUP_2:def 5;
  then the_stable_subgroup_of(carr H1 \/ carr H2) is StableSubgroup of
  the_stable_subgroup_of carr H3 by Th26,XBOOLE_1:8;
  hence thesis by Th25;
end;

:: GROUP_4:82

theorem Th38:
  for H2,H3 being strict StableSubgroup of G holds
  H1 is StableSubgroup of H2 implies H1 "\/" H3 is StableSubgroup of H2 "\/" H3
proof
  let H2,H3 be strict StableSubgroup of G;
  assume H1 is StableSubgroup of H2;
  then H1 is Subgroup of H2 by Def7;
  then carr H1 c= carr H2 by GROUP_2:def 5;
  hence thesis by Th26,XBOOLE_1:9;
end;

:: GROUP_6:3

theorem Th39:
  for X,Y being StableSubgroup of H1, X',Y' being StableSubgroup of G
  st X = X' & Y = Y' holds X' /\ Y' = X /\ Y
proof
  let X,Y be StableSubgroup of H1;
  let X',Y' be StableSubgroup of G;
  assume X=X' & Y=Y';
  then
A1: the carrier of X /\ Y = (carr X) /\ (carr Y) &
  the carrier of X' /\ Y' = (carr X') /\ (carr Y') &
  carr X = the carrier of X & the carrier of Y = carr Y &
  carr X' = the carrier of X & the carrier of Y = carr Y' by Def28;
  reconsider Z = X /\ Y as StableSubgroup of G by Th11;
  X' /\ Y' = Z by A1,Th18;
  hence thesis;
end;

:: GROUP_6:9

theorem Th40:
  N is StableSubgroup of H1 implies N is normal StableSubgroup of H1
proof
  assume N is StableSubgroup of H1;
  then reconsider N' = N as StableSubgroup of H1;
  now
    let H be strict Subgroup of H1;
    assume
A1: H = the multMagma of N';
A2: H1 is Subgroup of G by Def7;
    reconsider N''=the multMagma of N as normal Subgroup of G by Lm7;
    reconsider N as Subgroup of G by Def7;
    N'' is Subgroup of N by GROUP_2:66;
    hence H is normal by A1,A2,GROUP_6:9;
  end;
  hence thesis by Def10;
end;

:: Lm4 in GROUP_6

Lm34: H1 /\ H2 is StableSubgroup of H1
proof
  the carrier of H1 /\ H2 = (the carrier of H1) /\ (the carrier of H2) &
  (the carrier of H1) /\ (the carrier of H2) c= the carrier of H1
  by Th18,XBOOLE_1:17;
  hence thesis by Lm21;
end;

:: GROUP_6:10

theorem Th41:
  H1 /\ N is normal StableSubgroup of H1 &
  N /\ H1 is normal StableSubgroup of H1
proof
  thus H1 /\ N is normal StableSubgroup of H1
  proof
    reconsider A = H1 /\ N as StableSubgroup of H1 by Lm34;
    now
      let H be strict Subgroup of H1;
      assume
A1:   H = the multMagma of A;
      reconsider N'=the multMagma of N as normal Subgroup of G by Lm7;
      now
        let b be Element of H1;
        thus b * H c= H * b
        proof
          let x;
          assume x in b * H;
          then consider a be Element of H1 such that
A2:       x = b * a and
A3:       a in H by GROUP_2:125;
          a in the carrier of A by A1,A3,STRUCT_0:def 5;
          then a in carr(H1) /\ carr(N) by Def28;
          then a in carr N' by XBOOLE_0:def 4;
          then
A4:       a in N' by STRUCT_0:def 5;
          reconsider a' = a, b' = b as Element of G by Th2;
          x = b' * a' by A2,Th3;
          then x in b' * N' & b' * N' c= N' * b' by A4,GROUP_2:125,GROUP_3:141;
          then consider b1 be Element of G such that
A5:       x = b1 * b' and
A6:       b1 in N' by GROUP_2:126;
          reconsider x' = x as Element of H1 by A2;
          reconsider x'' = x as Element of G by A5;
A7:       b1 = x'' * b'" by A5,GROUP_1:22;
A8:       b1 in the carrier of N by A6,STRUCT_0:def 5;
          b'" = b" by Th6;
          then
A9:       b1 = x' * b" by A7,Th3;
          then
A10:      b1 in carr(H1) /\ carr(N) by A8,XBOOLE_0:def 4;
          reconsider b1' = b1 as Element of H1 by A9;
          b1' in the carrier of A by A10,Def28;
          then
A11:      b1' in H by A1,STRUCT_0:def 5;
          b1' * b = x by A5,Th3;
          hence thesis by A11,GROUP_2:126;
        end;
      end;
      hence H is normal by GROUP_3:141;
    end;
    hence thesis by Def10;
  end;
  hence thesis;
end;

:: GROUP_6:13

theorem Th42:
  for G being strict GroupWithOperators of O holds
  G is trivial implies (1).G = G
proof
  let G be strict GroupWithOperators of O;
  assume G is trivial;
  then
A1: the carrier of G is trivial;
  reconsider H=G as StableSubgroup of G by Lm4;
  consider x be set such that
A2: the carrier of G = {x} by A1,REALSET1:def 4;
  the carrier of H = {1_G} by A2,TARSKI:def 1;
  hence thesis by Def8;
end;

Lm35: for N' being normal Subgroup of G st N' = the multMagma of N
holds G./.N' = the multMagma of G./.N & 1_(G./.N') = 1_(G./.N)
proof
  let N' be normal Subgroup of G;
  assume
A1: N' = the multMagma of N;
  then Cosets N' = Cosets N by Def14;
  hence G./.N' = the multMagma of G./.N by A1,Def15;
  reconsider e=1_(G./.N') as Element of G./.N by A1,Def14;
  now
    let h be Element of G./.N;
    reconsider h'=h as Element of G./.N' by A1,Def14;
    thus h * e = h' * 1_(G./.N') by A1,Def15
      .= h by GROUP_1:def 5;
    thus e * h = 1_(G./.N') * h' by A1,Def15
      .= h by GROUP_1:def 5;
  end;
  hence thesis by GROUP_1:10;
end;

:: GROUP_6:29

theorem Th43:
  1_(G./.N) = carr N
proof
  reconsider N' = the multMagma of N as normal Subgroup of G by Lm7;
  1_(G./.N') = carr N' by GROUP_6:29;
  hence thesis by Lm35;
end;

:: GROUP_6:35

theorem Th44:
  for M,N being strict normal StableSubgroup of G,
  MN being normal StableSubgroup of N st MN=M & M is StableSubgroup of N
  holds N./.MN is normal StableSubgroup of G./.M
proof
  let M,N be strict normal StableSubgroup of G;
  let MN be normal StableSubgroup of N;
  assume
A1: MN=M;
  assume
A2: M is StableSubgroup of N;
  reconsider N' = the multMagma of N as normal Subgroup of G by Lm7;
  reconsider M' = the multMagma of M as normal Subgroup of G by Lm7;
  reconsider MN' = the multMagma of MN as normal Subgroup of N by Lm7;
  M is Subgroup of N by A2,Def7;
  then the carrier of M c= the carrier of N &
  the multF of M = (the multF of N)||the carrier of M by GROUP_2:def 5;
  then
A3: M' is Subgroup of N' by GROUP_2:def 5;
  then reconsider K=N'./.(N',M')`*` as normal Subgroup of G./.M' by GROUP_6:35;
A4: (N',M')`*` = MN' by A1,A3,GROUP_6:def 1;
  reconsider MN''=(N',M')`*` as normal Subgroup of N';
A5: the carrier of G./.M = the carrier of G./.M' by Def14;
A6: now
    let x;
    hereby
      assume x in Cosets MN';
      then consider a be Element of N such that
A7:   x = a * MN' & x = MN' * a by GROUP_6:15;
      reconsider A = {a} as Subset of N by ZFMISC_1:37;
      reconsider a' = a as Element of N';
      reconsider A' = {a'} as Subset of N' by ZFMISC_1:37;
      now
        let y be set;
        hereby
          assume y in {g * h where g,h is Element of N:g in A & h in carr MN'};
          then consider g,h be Element of N such that
A8:       y = g*h & g in A & h in carr MN';
          reconsider g'=g as Element of N';
          reconsider h'=h as Element of N';
          y = g'*h' by A8;
          hence y in {g''*h'' where g'',h'' is Element of N':
          g'' in A' & h'' in carr MN''} by A4,A8;
        end;
        assume y in {g*h where g,h is Element of N': g in A' & h in carr MN''};
        then consider g,h be Element of N' such that
A9:     y = g*h & g in A' & h in carr MN'';
        reconsider g'=g as Element of N;
        reconsider h'=h as Element of N;
        y = g'*h' by A9;
        hence y in {g''*h'' where g'',h'' is Element of N:
        g'' in A & h'' in carr MN'} by A4,A9;
      end;
      then x = a' * MN'' by A7,TARSKI:2;
      hence x in Cosets MN'' by GROUP_6:16;
    end;
    assume x in Cosets MN'';
    then consider a' be Element of N' such that
A10: x = a' * MN'' & x = MN'' * a' by GROUP_6:15;
    reconsider A' = {a'} as Subset of N' by ZFMISC_1:37;
    reconsider a = a' as Element of N;
    reconsider A = {a} as Subset of N by ZFMISC_1:37;
    now
      let y be set;
      hereby
        assume y in {g * h where g,h is Element of N:g in A & h in carr MN'};
        then consider g,h be Element of N such that
A11:    y = g*h & g in A & h in carr MN';
        reconsider g'=g as Element of N';
        reconsider h'=h as Element of N';
        y = g'*h' by A11;
        hence y in {g''*h'' where g'',h'' is Element of N':
        g'' in A' & h'' in carr MN''} by A4,A11;
      end;
      assume y in {g*h where g,h is Element of N': g in A' & h in carr MN''};
      then consider g,h be Element of N' such that
A12:  y = g*h & g in A' & h in carr MN'';
      reconsider g'=g as Element of N;
      reconsider h'=h as Element of N;
      y = g'*h' by A12;
      hence y in {g''*h'' where g'',h'' is Element of N:
      g'' in A & h'' in carr MN'} by A4,A12;
    end;
    then x = a * MN' by A10,TARSKI:2;
    hence x in Cosets MN' by GROUP_6:16;
  end;
  then Cosets MN'' = Cosets MN' by TARSKI:2;
  then reconsider f=CosOp MN'' as BinOp of Cosets MN';
A13: now
    let W1,W2 be Element of Cosets MN';
    let A1,A2 be Subset of N;
    assume
A14: W1 = A1;
    assume
A15: W2 = A2;
    reconsider W1'=W1,W2'=W2 as Element of Cosets MN'' by A6;
    reconsider A1'=A1,A2'=A2 as Subset of N';
A16: now
      let x;
      hereby
        assume x in A1 * A2;
        then consider g,h be Element of N such that
A17:    x = g * h & g in A1 & h in A2;
        reconsider g'=g,h'=h as Element of N';
        x = g' * h' by A17;
        hence x in A1' * A2' by A17;
      end;
      assume x in A1' * A2';
      then consider g',h' be Element of N' such that
A18:  x = g' * h' & g' in A1' & h' in A2';
      reconsider g=g',h=h' as Element of N;
      x = g * h by A18;
      hence x in A1 * A2 by A18;
    end;
    thus f.(W1,W2) = f.(W1',W2')
      .= A1' * A2' by A14,A15,GROUP_6:def 4
      .= A1 * A2 by A16,TARSKI:2;
  end;
A19: the carrier of K = Cosets MN' by A6,TARSKI:2
    .= the carrier of N./.MN by Def14;
A20: the multF of K = CosOp MN' by A13,GROUP_6:def 4
    .= the multF of N./.MN by Def15;
A21: now
    let H be strict Subgroup of G./.M;
    assume
A22: H = the multMagma of N./.MN;
    now
      let a be Element of G./.M;
      reconsider a'=a as Element of G./.M' by Def14;
      now
        let x;
        assume x in a * carr H;
        then consider b be Element of G./.M such that
A23:    x = a * b & b in carr H by GROUP_2:33;
        reconsider b'=b as Element of G./.M' by Def14;
A24:    x = a' * b' by A23,Def15;
        then reconsider x'=x as Element of G./.M';
A25:    a' * K c= K * a' by GROUP_3:141;
        x' in a' * carr K by A19,A22,A23,A24,GROUP_2:33;
        then consider c' be Element of G./.M' such that
A26:    x' = c' * a' & c' in carr K by A25,GROUP_2:34;
        reconsider c = c' as Element of G./.M by Def14;
        x = c * a by A26,Def15;
        hence x in carr H * a by A19,A22,A26,GROUP_2:34;
      end;
      hence a * H c= H * a by TARSKI:def 3;
    end;
    hence H is normal by GROUP_3:141;
  end;
A27: the carrier of N./.MN c= the carrier of G./.M by A5,A19,GROUP_2:def 5;
  the multF of N./.MN
  = (the multF of G./.M')||the carrier of K by A20,GROUP_2:def 5
    .= (the multF of G./.M)||the carrier of N./.MN by A19,Def15;
  then
A28: N./.MN is Subgroup of G./.M by A27,GROUP_2:def 5;
  now
    let o be Element of O;
    per cases;
    suppose
A29:  not o in O;
A30:  the carrier of N./.MN c= the carrier of G./.M by A5,A19,GROUP_2:def 5;
A31:  now
        let x,y be set;
        assume [x,y] in id the carrier of N./.MN;
        then
A32:    x in the carrier of N./.MN & x=y by RELAT_1:def 10;
        then [x,y] in id the carrier of G./.M by A30,RELAT_1:def 10;
        hence [x,y] in (id the carrier of G./.M)|the carrier of N./.MN
        by A32,RELAT_1:def 11;
      end;
A33:  now
        let x,y be set;
        assume [x,y] in (id the carrier of G./.M)|the carrier of N./.MN;
        then
A34:    x in the carrier of N./.MN & [x,y] in id the carrier of G ./.M
        by RELAT_1:def 11;
        then x in the carrier of G./.M & x=y by RELAT_1:def 10;
        hence [x,y] in id the carrier of N./.MN by A34,RELAT_1:def 10;
      end;
      thus (N./.MN)^o = id the carrier of N./.MN by A29,Def6
        .= (id the carrier of G./.M)|the carrier of N./.MN
      by A31,A33,RELAT_1:def 2
        .= ((G./.M)^o)|the carrier of N./.MN by A29,Def6;
    end;
    suppose
A35:  o in O;
      then (the action of G./.M).o in Funcs(the carrier of G./.M,
      the carrier of G./.M) by FUNCT_2:7;
      then consider f be Function such that
A36:  f=(the action of G./.M).o & dom f = the carrier of G./.M &
      rng f c= the carrier of G./.M by FUNCT_2:def 2;
      (the action of N./.MN).o in Funcs(the carrier of N./.MN,
      the carrier of N./.MN) by A35,FUNCT_2:7;
      then consider g be Function such that
A37:  g=(the action of N./.MN).o & dom g = the carrier of N./.MN &
      rng g c= the carrier of N./.MN by FUNCT_2:def 2;
A38:  g = {[A,B] where A,B is Element of Cosets MN:
      ex a,b being Element of N st a in A & b in B & b = (N^o).a}
      by A35,A37,Def16;
A39:  f = {[A,B] where A,B is Element of Cosets M:
      ex a,b being Element of G st a in A & b in B & b = (G^o).a}
      by A35,A36,Def16;
A40:  dom g = dom f /\ the carrier of N./.MN by A27,A36,A37,XBOOLE_1:28;
A41:  now
        let x;
        assume
A42:    x in dom g;
        then [x,f.x] in f by A27,A36,A37,FUNCT_1:8;
        then consider A1,B1 be Element of Cosets M such that
A43:    [x,f.x]=[A1,B1] & ex a,b being Element of G st
        a in A1 & b in B1 & b = (G^o).a by A39;
        [x,g.x] in g by A42,FUNCT_1:8;
        then consider A2,B2 be Element of Cosets MN such that
A44:    [x,g.x]=[A2,B2] & ex a,b being Element of N st
        a in A2 & b in B2 & b = (N^o).a by A38;
        consider a1,b1 be Element of G such that
A45:    a1 in A1 & b1 in B1 & b1 = (G^o).a1 by A43;
        consider a2,b2 be Element of N such that
A46:    a2 in A2 & b2 in B2 & b2 = (N^o).a2 by A44;
A47:    A1=x & A2=x & B1=f.x & B2=g.x by A43,A44,ZFMISC_1:33;
        reconsider A1'=A1,B1'=B1 as Element of Cosets M' by Def14;
        reconsider A2'=A2,B2'=B2 as Element of Cosets MN' by Def14;
A48:    a2 in the carrier of N & b2 in the carrier of N;
        N is Subgroup of G by Def7;
        then
A49:    the carrier of N c= the carrier of G by GROUP_2:def 5;
        then reconsider a2'=a2,b2'=b2 as Element of G by A48;
A50:    now
          let x;
          hereby
            assume x in a2 * carr MN';
            then consider h be Element of N such that
A51:        x = a2 * h & h in carr MN' by GROUP_2:33;
            h in the carrier of N;
            then reconsider h'=h as Element of G by A49;
            x = a2' * h' by A51,Th3;
            hence x in a2' * carr M' by A1,A51,GROUP_2:33;
          end;
          assume x in a2' * carr M';
          then consider h be Element of G such that
A52:      x = a2' * h & h in carr M' by GROUP_2:33;
          h in carr MN' by A1,A52;
          then reconsider h'=h as Element of N;
          x = a2 * h' by A52,Th3;
          hence x in a2 * carr MN' by A1,A52,GROUP_2:33;
        end;
A53:    now
          let x;
          hereby
            assume x in b2 * carr MN';
            then consider h be Element of N such that
A54:        x = b2 * h & h in carr MN' by GROUP_2:33;
            h in the carrier of N;
            then reconsider h'=h as Element of G by A49;
            x = b2' * h' by A54,Th3;
            hence x in b2' * carr M' by A1,A54,GROUP_2:33;
          end;
          assume x in b2' * carr M';
          then consider h be Element of G such that
A55:      x = b2' * h & h in carr M' by GROUP_2:33;
          h in carr MN' by A1,A55;
          then reconsider h'=h as Element of N;
          x = b2 * h' by A55,Th3;
          hence x in b2 * carr MN' by A1,A55,GROUP_2:33;
        end;
        set fo = G^o;
A56:    A1' = a1 * M' by A45,Lm9;
A57:    A2' = a2 * MN' by A46,Lm9;
A58:    a2 * MN' = a2' * M' by A50,TARSKI:2;
A59:    b1" = fo.a1" by A45,GROUP_6:41;
A60:    b2' = ((G^o)|the carrier of N).a2 by A46,Def7
          .= fo.a2' by FUNCT_1:72;
A61:    b2' * M' = b2 * MN' by A53,TARSKI:2;
        a1" * a2' in M' by A47,A56,A57,A58,GROUP_2:137;
        then a1" * a2' in the carrier of M by STRUCT_0:def 5;
        then a1" * a2' in M by STRUCT_0:def 5;
        then fo.(a1" * a2') in M by Lm10;
        then b1" * b2' in M by A59,A60,GROUP_6:def 7;
        then b1" * b2' in the carrier of M by STRUCT_0:def 5;
        then
A62:    b1" * b2' in M' by STRUCT_0:def 5;
A63:    B1' = b1 * M' by A45,Lm9;
        B2' = b2 * MN' by A46,Lm9;
        hence g.x = f.x by A47,A61,A62,A63,GROUP_2:137;
      end;
      thus (N./.MN)^o = (the action of N./.MN).o by A35,Def6
        .= f|the carrier of N./.MN by A37,A40,A41,FUNCT_1:68
        .= ((G./.M)^o)|the carrier of N./.MN by A35,A36,Def6;
    end;
  end;
  hence thesis by A21,A28,Def7,Def10;
end;

:: GROUP_6:40

theorem
  h.(1_G)=1_H by Lm13;

:: GROUP_6:41

theorem
  h.(g1")=(h.g1)" by Lm14;

:: GROUP_6:50

theorem Th47:
  g1 in Ker h iff h.g1 = 1_H
proof
  thus g1 in Ker h implies h.g1 = 1_H
  proof
    assume g1 in Ker h;
    then g1 in the carrier of Ker h by STRUCT_0:def 5;
    then g1 in {b where b is Element of G : h.b = 1_H} by Def24;
    then ex b being Element of G st g1 = b & h.b = 1_H;
    hence thesis;
  end;
  assume h.g1 = 1_H;
  then g1 in {b where b is Element of G: h.b = 1_H};
  then g1 in the carrier of Ker h by Def24;
  hence thesis by STRUCT_0:def 5;
end;

:: GROUP_6:52

theorem Th48:
  for N being strict normal StableSubgroup of G holds Ker nat_hom N = N
proof
  let N be strict normal StableSubgroup of G;
  reconsider N' = the multMagma of N as strict normal Subgroup of G by Lm7;
A1: nat_hom N = nat_hom N' by Def23;
A2: 1_(G./.N) = 1_(G./.N') by Lm35;
  the carrier of Ker nat_hom N =
  {a where a is Element of G: (nat_hom N).a = 1_(G./.N)} by Def24
    .= {a where a is Element of G: (nat_hom N').a = 1_(G./.N')} by A1,A2
    .= the carrier of Ker nat_hom N' by GROUP_6:def 10
    .= the carrier of N by GROUP_6:52;
  hence thesis by Lm5;
end;

:: GROUP_6:53

theorem Th49:
  rng h = the carrier of Image h
proof
  the carrier of Image h = h .: (the carrier of G) by Def25
    .= h .: (dom h) by FUNCT_2:def 1
    .= rng h by RELAT_1:146;
  hence thesis;
end;

:: GROUP_6:57

theorem Th50:
  Image nat_hom N = G./.N
proof
  reconsider H = G./.N as strict StableSubgroup of G./.N by Lm4;
  reconsider N' = the multMagma of N as strict normal Subgroup of G by Lm7;
A1: G./.N' = the multMagma of G./.N by Lm35;
  the carrier of Image nat_hom N = nat_hom N .: (the carrier of G) by Def25
    .= nat_hom N' .: (the carrier of G) by Def23
    .= the carrier of Image nat_hom N' by GROUP_6:def 11
    .= the carrier of H by A1,GROUP_6:57;
  hence thesis by Lm5;
end;

:: GROUP_6:67

theorem Th51:
  for H being strict GroupWithOperators of O, h being Homomorphism of G,H holds
  h is onto iff Image h = H
proof
  let H be strict GroupWithOperators of O, h be Homomorphism of G,H;
  thus h is onto implies Image h = H
  proof
    assume
A1: rng h = the carrier of H;
    reconsider H'=H as strict StableSubgroup of H by Lm4;
    the carrier of H' = the carrier of Image h by A1,Th49;
    hence thesis by Lm5;
  end;
  assume
A2: Image h = H;
  the carrier of H c= rng h
  proof
    let x;
    assume x in the carrier of H;
    then x in h .: (the carrier of G) by A2,Def25;
    then ex y being set st y in dom h & y in the carrier of G &
    h.y = x by FUNCT_1:def 12;
    hence thesis by FUNCT_1:def 5;
  end;
  then rng h = the carrier of H by XBOOLE_0:def 10;
  hence thesis by FUNCT_2:def 3;
end;

:: GROUP_6:68

theorem Th52:
  for H being strict GroupWithOperators of O, h being Homomorphism of G,H st
  h is onto holds
  for c being Element of H ex a being Element of G st h.a = c
proof
  let H be strict GroupWithOperators of O;
  let h be Homomorphism of G,H;
  assume h is onto;
  then
A1: rng h = the carrier of H by FUNCT_2:def 3;
  let c be Element of H;
  consider a be set such that
A2: a in dom h & c = h.a by A1,FUNCT_1:def 5;
  reconsider a as Element of G by A2;
  take a;
  thus thesis by A2;
end;

:: GROUP_6:69

theorem Th53:
  nat_hom N is onto
proof
  Image nat_hom N = G./.N by Th50;
  hence thesis by Th51;
end;

:: GROUP_6:75

theorem Th54:
  nat_hom (1).G is bijective
proof
  set g = nat_hom (1).G;
  reconsider G'=G as Group;
  reconsider H = the multMagma of (1).G as strict normal Subgroup of G by Lm7;
A1: the carrier of H = {1_G'} by Def8;
A2: nat_hom (1).G = nat_hom H by Def23
    .= nat_hom (1).G' by A1,GROUP_2:def 7;
  nat_hom (1).G' is bijective by GROUP_6:75;
  then nat_hom (1).G' is one-to-one;
  then g is one-to-one by A2;
  then g is one-to-one & g is onto by Th53;
  hence thesis;
end;

:: GROUP_6:78

theorem Th55:
  G,H are_isomorphic & H,I are_isomorphic implies G,I are_isomorphic
proof
  assume that
A1: G,H are_isomorphic and
A2: H,I are_isomorphic;
  consider g be Homomorphism of G,H such that
A3: g is bijective by A1,Def22;
  consider h1 be Homomorphism of H,I such that
A4: h1 is bijective by A2,Def22;
  g is onto & g is one-to-one by A3;
  then
A5: rng g = the carrier of H & g is one-to-one by FUNCT_2:def 3;
  h1 is onto & h1 is one-to-one by A4;
  then
A6: rng h1 = the carrier of I & h1 is one-to-one by FUNCT_2:def 3;
  dom h1 = rng g by A5,FUNCT_2:def 1;
  then
A7: rng(h1 * g) = the carrier of I by A6,RELAT_1:47;
  h1 * g is one-to-one by A5,A6;
  then h1 * g is onto & h1 * g is one-to-one by A7,FUNCT_2:def 3;
  then (h1 * g) is bijective;
  hence thesis by Def22;
end;

:: GROUP_6:82

theorem Th56:
  for G being strict GroupWithOperators of O holds G,G./.(1).G are_isomorphic
proof
  let G be strict GroupWithOperators of O;
  nat_hom (1).G is bijective by Th54;
  hence thesis by Def22;
end;

:: GROUP_6:83

theorem Th57:
  for G being strict GroupWithOperators of O holds G./.(Omega).G is trivial
proof
  let G be strict GroupWithOperators of O;
  reconsider H=the multMagma of (Omega).G as strict normal Subgroup of G
  by Lm7;
  reconsider G'=G as Group;
A1: H = (Omega).G';
  the carrier of G./.(Omega).G = Cosets H by Def14
    .= {the carrier of G} by A1,GROUP_2:172;
  hence thesis by GROUP_6:def 2;
end;

:: GROUP_6:87

theorem Th58:
  for G,H being strict GroupWithOperators of O holds
  G,H are_isomorphic & G is trivial implies H is trivial
proof
  let G,H be strict GroupWithOperators of O;
  assume that
A1: G,H are_isomorphic and
A2: G is trivial;
  consider g be Homomorphism of G,H such that
A3: g is bijective by A1,Def22;
A4: g is onto & g is one-to-one by A3;
  consider e be set such that
A5: the carrier of G = {e} by A2,GROUP_6:def 2;
  e in the carrier of G by A5,TARSKI:def 1;
  then
A6: e in dom g by FUNCT_2:def 1;
  the carrier of H = the carrier of Image g by A4,Th51
    .= Im(g,e) by A5,Def25
    .= {g.e} by A6,FUNCT_1:117;
  hence thesis by GROUP_6:def 2;
end;

:: GROUP_6:90

theorem Th59:
  G./.Ker h, Image h are_isomorphic
proof
  reconsider G'=G,H'=H as Group;
  reconsider h'=h as Homomorphism of G',H';
  consider g' be Homomorphism of G'./.Ker h', Image h' such that
A1: g' is bijective & h' = g' * nat_hom Ker h' by GROUP_6:91;
A2: Ker h is Subgroup of G by Def7;
  now
    let x;
    hereby
      assume x in the carrier of Ker h;
      then x in {a where a is Element of G: h.a = 1_H} by Def24;
      then consider a be Element of G such that
A3:   x=a & h.a = 1_H;
      x in {a' where a' is Element of G': h'.a' = 1_H'} by A3;
      hence x in the carrier of Ker h' by GROUP_6:def 10;
    end;
    assume x in the carrier of Ker h';
    then x in {a' where a' is Element of G': h'.a' = 1_H'} by GROUP_6:def 10;
    then consider a' be Element of G such that
A4: x=a' & h'.a' = 1_H';
    x in {a where a is Element of G: h.a = 1_H} by A4;
    hence x in the carrier of Ker h by Def24;
  end;
  then the carrier of Ker h' = the carrier of Ker h by TARSKI:2;
  then
A5: the multMagma of Ker h' = the multMagma of Ker h by A2,GROUP_2:68;
  then
A6: the carrier of G'./.Ker h' = the carrier of G./.Ker h by Def14;
A7: Image h is Subgroup of H by Def7;
A8: the carrier of Image h' = h' .: (the carrier of G') by GROUP_6:def 11
    .= the carrier of Image h by Def25;
  then
A9: the multMagma of Image h' = the multMagma of Image h by A7,GROUP_2:68;
  reconsider g=g' as Function of G./.Ker h, Image h by A6,A8;
A10: now
    let o be Element of O;
    let a be Element of G./.Ker h;
    per cases;
    suppose
A11:  O is empty;
      hence g.(((G./.Ker h)^o).a)
      = g.((id the carrier of (G./.Ker h)).a) by Def6
        .= g.a by FUNCT_1:35
        .= (id the carrier of Image h).(g.a) by FUNCT_1:35
        .= ((Image h)^o).(g.a) by A11,Def6;
    end;
    suppose
A12:  O is not empty;
      reconsider G''=G./.Ker h as Group;
      set f = (the action of G./.Ker h).o;
A13:  f = {[A,B] where A,B is Element of Cosets Ker h:
      ex g,h being Element of G st g in A & h in B & h=(G^o).g} by A12,Def16;
      f = (G./.Ker h)^o by A12,Def6;
      then reconsider f as Homomorphism of G'', G'';
      a in the carrier of G'';
      then a in dom f by FUNCT_2:def 1;
      then [a,f.a] in f by FUNCT_1:8;
      then consider A,B be Element of Cosets Ker h such that
A14:  [A,B]=[a,f.a] &
      ex g1,g2 being Element of G st g1 in A & g2 in B & g2=(G^o).g1 by A13;
      reconsider A,B as Element of Cosets Ker h' by A5,Def14;
      consider g1,g2 be Element of G' such that
A15:  g1 in A & g2 in B & g2=(G^o).g1 by A14;
A16:  A = g1 * Ker h' & B = g2 * Ker h' by A15,Lm9;
A17:  g.(((G./.Ker h)^o).a) = g.(f.a) by A12,Def6
        .= g'.(g2 * Ker h') by A14,A16,ZFMISC_1:33;
A18:  ((Image h)^o).(g.a) = ((H^o)|the carrier of Image h).(g.a) by Def7
        .= (H^o).(g.a) by FUNCT_1:72
        .= (H^o).(g'.(g1 * Ker h')) by A14,A16,ZFMISC_1:33;
      g2 in the carrier of G';
      then
A19:  g2 in dom nat_hom Ker h' by FUNCT_2:def 1;
      g1 in the carrier of G';
      then
A20:  g1 in dom nat_hom Ker h' by FUNCT_2:def 1;
      h'.g2 = (H^o).(h'.g1) by A15,Def18;
      then g'.(nat_hom Ker h'.g2)=(H^o).((g' * nat_hom Ker h').g1)
      by A1,A19,FUNCT_1:23;
      then g'.(nat_hom Ker h'.g2)=(H^o).(g'.(nat_hom Ker h'.g1))
      by A20,FUNCT_1:23;
      then g'.(g2 * Ker h') = (H^o).(g'.(nat_hom Ker h'.g1)) by GROUP_6:def 9;
      hence g.(((G./.Ker h)^o).a)=((Image h)^o).(g.a) by A17,A18,GROUP_6:def 9;
    end;
  end;
  now
    let a, b be Element of G./.Ker h;
    reconsider a'=a as Element of G'./.Ker h' by A5,Def14;
    reconsider b'=b as Element of G'./.Ker h' by A5,Def14;
    thus g.(a * b) = g'.(a' * b') by A5,Def15
      .= g'.a' * g'.b' by GROUP_6:def 7
      .= g.a * g.b by A9;
  end;
  then reconsider g as Homomorphism of G./.Ker h, Image h
  by A10,Def18,GROUP_6:def 7;
A21: g' is onto & g' is one-to-one by A1;
  then
A22: rng g = the carrier of Image h by A8,FUNCT_2:def 3;
  g is one-to-one by A21;
  then g is onto & g is one-to-one by A22,FUNCT_2:def 3;
  then g is bijective;
  hence thesis by Def22;
end;

:: GRSOLV_1:1

theorem Th60:
  for H,F1,F2 being strict StableSubgroup of G
  st F1 is normal StableSubgroup of F2
  holds H /\ F1 is normal StableSubgroup of H /\ F2
proof
  let H,F1,F2 be strict StableSubgroup of G;
  assume
A1: F1 is normal StableSubgroup of F2;
  then
A2: F1 /\ H=(F1 /\ F2) /\ H by Lm22
    .=F1 /\ (F2 /\ H) by Th20;
  reconsider F1 as normal StableSubgroup of F2 by A1;
  reconsider F=F2 /\ H as StableSubgroup of F2 by Lm34;
  F1 /\ F is normal StableSubgroup of F by Th41;
  hence thesis by A2,Th39;
end;

begin :: Other Theorems on Actions and Groups with Operators

reserve E for set,
  A for Action of O,E,
  C for Subset of G,
  N1 for normal StableSubgroup of H1;

theorem
  [#]E is_stable_under_the_action_of A by Lm1;

theorem
  [:O,{id E}:] is Action of O, E by Lm2;

theorem
  for O being non empty set, E being set, o being Element of O,
  A being Action of O,E holds Product(<*o*>,A) = A.o by Lm26;

theorem
  for O being non empty set, E being set,
  F1,F2 being FinSequence of O, A being Action of O,E
  holds Product(F1^F2,A) = Product(F1,A) * Product(F2,A) by Lm29;

theorem
  for F being FinSequence of O, Y being Subset of E
  st Y is_stable_under_the_action_of A holds Product(F,A) .: Y c= Y by Lm30;

theorem
  for E being non empty set, A being Action of O,E holds
  for X being Subset of E, a being Element of E st X is not empty holds
  a in the_stable_subset_generated_by(X,A) iff
  ex F being FinSequence of O, x being Element of X
  st Product(F,A).x = a by Lm31;

theorem
  for G being strict Group holds
  ex H being strict GroupWithOperators of O st G = the multMagma of H
proof
  let G be strict Group;
  consider H be non empty HGrWOpStr over O such that
A1: H is strict distributive Group-like associative &
  G = the multMagma of H by Lm3;
  reconsider H as strict GroupWithOperators of O by A1;
  take H;
  thus thesis by A1;
end;

theorem
  the multMagma of H1 is strict Subgroup of G by Lm16;

theorem
  the multMagma of N is strict normal Subgroup of G by Lm7;

theorem
  g1 in H1 implies (G^o).g1 in H1 by Lm10;

theorem
  for O being set, G,H being GroupWithOperators of O,
  G' being strict StableSubgroup of G, f being Homomorphism of G,H
  holds ex H' being strict StableSubgroup of H
  st the carrier of H' = f.:(the carrier of G') by Lm17;

theorem
  B is empty implies the_stable_subgroup_of B = (1).G by Lm25;

theorem
  B = the carrier of gr C implies
  the_stable_subgroup_of C = the_stable_subgroup_of B by Lm32;

theorem
  for N' being normal Subgroup of G st N' = the multMagma of N
  holds G./.N' = the multMagma of G./.N & 1_(G./.N') = 1_(G./.N) by Lm35;

theorem Th75:
  the carrier of H1 = the carrier of H2 implies
  the HGrWOpStr of H1 = the HGrWOpStr of H2
proof
  assume
A1: the carrier of H1 = the carrier of H2;
  reconsider H1'=H1,H2'=H2 as Subgroup of G by Def7;
A2: the multMagma of H1' = the multMagma of H2' by A1,GROUP_2:68;
A3: dom the action of H2 = O by FUNCT_2:def 1
    .= dom the action of H1 by FUNCT_2:def 1;
  now
    let x;
    assume
A4: x in dom the action of H2;
    then reconsider o=x as Element of O;
A5: H1^o = (the action of H1).o & H2^o = (the action of H2).o by A4,Def6;
    H1^o = (G^o)|the carrier of H2 by A1,Def7
      .= H2^o by Def7;
    hence (the action of H1).x = (the action of H2).x by A5;
  end;
  hence thesis by A2,A3,FUNCT_1:9;
end;

theorem Th76:
  H1./.N1 is trivial implies the HGrWOpStr of H1 = the HGrWOpStr of N1
proof
  assume H1./.N1 is trivial;
  then consider e be set such that
A1: the carrier of H1./.N1 = {e} by GROUP_6:def 2;
  set H=H1;
  reconsider N=the multMagma of N1 as normal Subgroup of H by Lm7;
  Cosets N1 = Cosets N by Def14;
  then consider e be set such that
A2: the carrier of H./.N = {e} by A1;
A3: the carrier of H = union {e} by A2,GROUP_2:167;
A4: now
    assume not the carrier of H c= the carrier of N;
    then (the carrier of H) \ (the carrier of N) <> {} by XBOOLE_1:37;
    then consider x such that
A5: x in (the carrier of H) \ (the carrier of N) by XBOOLE_0:def 1;
    reconsider x as Element of H1 by A5;
A6: now
      assume x * N = e;
      then x * N = the carrier of H by A3,ZFMISC_1:31;
      then consider x' be Element of the carrier of H such that
A7:   1_H = x * x' & x' in N by GROUP_2:125;
      x'=x" by A7,GROUP_1:20;
      then x"" in N by A7,GROUP_2:60;
      then x in N;
      then x in carr(N) by STRUCT_0:def 5;
      hence contradiction by A5,XBOOLE_0:def 5;
    end;
    x * N in Cosets N by GROUP_6:16;
    hence contradiction by A2,A6,TARSKI:def 1;
  end;
  reconsider N'=N1 as StableSubgroup of G by Th11;
  the carrier of N c= the carrier of H by GROUP_2:def 5;
  then the carrier of N' = the carrier of H1 by A4,XBOOLE_0:def 10;
  hence thesis by Th75;
end;

theorem Th77:
  the carrier of H1 = the carrier of N1 implies H1./.N1 is trivial
proof
  reconsider N1' = the multMagma of N1 as strict normal Subgroup of H1 by Lm7;
A1: Cosets N1 = Cosets N1' by Def14;
  assume
A2: the carrier of H1 = the carrier of N1;
  now
    let x be set;
    hereby
      assume
A3:   x in Left_Cosets N1';
      then reconsider A=x as Subset of H1;
      consider a be Element of H1 such that
A4:   A = a * N1' by A3,GROUP_2:def 15;
      A = a * [#]the carrier of H1 by A2,A4;
      hence x = the carrier of H1 by GROUP_2:21;
    end;
    assume
A5: x = the carrier of H1;
    the carrier of H1 = 1_H1 * [#]the carrier of H1 by GROUP_2:21;
    then the carrier of H1 = 1_H1 * N1' by A2;
    hence x in Left_Cosets N1' by A5,GROUP_2:def 15;
  end;
  then {the carrier of H1} = Left_Cosets N1' by TARSKI:def 1;
  hence thesis by A1,GROUP_6:def 2;
end;

:: ALG I.4.6 Proposition 7(a)

theorem Th78:
  for G,H being GroupWithOperators of O, N being StableSubgroup of G,
  H' being strict StableSubgroup of H, f being Homomorphism of G,H
  st N = Ker f holds ex G' being strict StableSubgroup of G
  st the carrier of G' = f"(the carrier of H') &
  (H' is normal implies N is normal StableSubgroup of G' & G' is normal)
proof
  let G,H be GroupWithOperators of O;
  let N be StableSubgroup of G;
  let H' be strict StableSubgroup of H;
  let f be Homomorphism of G,H;
  assume
A1: N = Ker f;
  reconsider H'' = the multMagma of H' as strict Subgroup of H by Lm16;
  set A = {g where g is Element of G:f.g in H''};
A2: 1_H in H'' by GROUP_2:55;
  then f.(1_G) in H'' by Lm13;
  then 1_G in A;
  then reconsider A as non empty set;
  now
    let x be set;
    assume x in A;
    then consider g be Element of G such that
A3: x=g & f.g in H'';
    thus x in the carrier of G by A3;
  end;
  then reconsider A as Subset of G by TARSKI:def 3;
A4: now
    let g1,g2 be Element of G;
    assume
A5: g1 in A & g2 in A;
    then consider a be Element of G such that
A6: a=g1 & f.a in H'';
    consider b be Element of G such that
A7: b=g2 & f.b in H'' by A5;
A8: f.(a*b) = f.a * f.b by GROUP_6:def 7;
    set fa = f.a;
    set fb = f.b;
    fa * fb in H'' by A6,A7,GROUP_2:59;
    hence g1*g2 in A by A6,A7,A8;
  end;
A9: now
    let g be Element of G;
    assume g in A;
    then consider a be Element of G such that
A10: a=g & f.a in H'';
    (f.a)" in H'' by A10,GROUP_2:60;
    then f.(a") in H'' by Lm14;
    hence g" in A by A10;
  end;
  now
    let o be Element of O;
    let g be Element of G;
    assume g in A;
    then consider a be Element of G such that
A11: a=g & f.a in H'';
    f.a in the carrier of H'' by A11,STRUCT_0:def 5;
    then f.a in H' by STRUCT_0:def 5;
    then (H^o).(f.g) in H' by A11,Lm10;
    then f.((G^o).g) in H' by Def18;
    then f.((G^o).g) in the carrier of H' by STRUCT_0:def 5;
    then f.((G^o).g) in H'' by STRUCT_0:def 5;
    hence (G^o).g in A;
  end;
  then consider G'' be strict StableSubgroup of G such that
A12: the carrier of G'' = A by A4,A9,Lm15;
  reconsider G' = the multMagma of G'' as strict Subgroup of G by Lm16;
  take G'';
  now
    let g be Element of G;
    reconsider R = f as Relation of the carrier of G, the carrier of H;
    hereby
      assume g in A;
      then consider a be Element of G such that
A13:  a=g & f.a in H'';
      dom f = the carrier of G by FUNCT_2:def 1;
      then
A14:  [g,f.g] in f by FUNCT_1:8;
      f.g in the carrier of H' by A13,STRUCT_0:def 5;
      hence g in f"(the carrier of H') by A14,RELSET_1:53;
    end;
    assume g in f"(the carrier of H');
    then consider h be Element of H such that
A15: [g,h] in R & h in (the carrier of H') by RELSET_1:53;
A16: f.g=h by A15,FUNCT_1:8;
    h in H'' by A15,STRUCT_0:def 5;
    hence g in A by A16;
  end;
  hence the carrier of G'' = f"(the carrier of H') by A12,SUBSET_1:8;
  now
    assume
A17: H' is normal;
    now
      let g be Element of G;
      assume g in N;
      then f.g = 1_H by A1,Th47;
      then g in the carrier of G'' by A2,A12;
      hence g in G'' by STRUCT_0:def 5;
    end;
    hence N is normal StableSubgroup of G'' by A1,Th13,Th40;
    now
      let g be Element of G;
      now
        let x be set;
        assume x in g * G';
        then consider h be Element of G such that
A18:    x=g*h & h in A by A12,GROUP_2:33;
        consider a be Element of G such that
A19:    a=h & f.a in H'' by A18;
        set h'=g*h*g";
A20:    h'*g = (g*h)*(g"*g) by GROUP_1:def 4
          .= (g*h)*1_G by GROUP_1:def 6
          .= x by A18,GROUP_1:def 5;
        H'' is normal by A17,Def10;
        then
A21:    H'' |^ (f.g)" = H'' by GROUP_3:def 13;
        f.h' = f.(g*h) * f.(g") by GROUP_6:def 7
          .= f.g * f.h * f.(g") by GROUP_6:def 7
          .= f.g * f.h * (f.g)" by Lm14
          .= ((f.g)")" * f.h * (f.g)"
          .= f.h |^ (f.g)" by GROUP_3:def 2;
        then f.h' in H'' by A19,A21,GROUP_3:70;
        then h' in A;
        hence x in G' * g by A12,A20,GROUP_2:34;
      end;
      hence g * G' c= G' * g by TARSKI:def 3;
    end;
    then for H being strict Subgroup of G st H = the multMagma of G''
    holds H is normal by GROUP_3:141;
    hence G'' is normal by Def10;
  end;
  hence thesis;
end;

:: ALG I.4.6 Proposition 7(b)

theorem Th79:
  for G,H being GroupWithOperators of O, N being StableSubgroup of G,
  G' being strict StableSubgroup of G, f being Homomorphism of G,H
  st N = Ker f holds ex H' being strict StableSubgroup of H
  st the carrier of H' = f.:(the carrier of G') &
  f"(the carrier of H') = the carrier of G'"\/"N &
  (f is onto & G' is normal implies H' is normal)
proof
  let G,H be GroupWithOperators of O;
  let N be StableSubgroup of G;
  let G' be strict StableSubgroup of G;
  let f be Homomorphism of G,H;
  assume
A1: N = Ker f;
  reconsider G'' = the multMagma of G' as strict Subgroup of G by Lm16;
  set A = {f.g where g is Element of G:g in G''};
  1_G in G'' by GROUP_2:55;
  then f.(1_G) in A;
  then reconsider A as non empty set;
  now
    let x be set;
    assume x in A;
    then consider g be Element of G such that
A2: x=f.g & g in G'';
    thus x in the carrier of H by A2;
  end;
  then reconsider A as Subset of H by TARSKI:def 3;
A3: now
    let h1,h2 be Element of H;
    assume
A4: h1 in A & h2 in A;
    then consider a be Element of G such that
A5: h1=f.a & a in G'';
    consider b be Element of G such that
A6: h2=f.b & b in G'' by A4;
A7: f.(a*b) = h1*h2 by A5,A6,GROUP_6:def 7;
    a*b in G'' by A5,A6,GROUP_2:59;
    hence h1*h2 in A by A7;
  end;
A8: now
    let h be Element of H;
    assume h in A;
    then consider g be Element of G such that
A9: h=f.g & g in G'';
A10: g" in G'' by A9,GROUP_2:60;
    h"=f.(g") by A9,Lm14;
    hence h" in A by A10;
  end;
  now
    let o be Element of O;
    let h be Element of H;
    assume h in A;
    then consider g be Element of G such that
A11: h=f.g & g in G'';
A12: (H^o).h = f.((G^o).g) by A11,Def18;
    g in the carrier of G'' by A11,STRUCT_0:def 5;
    then g in G' by STRUCT_0:def 5;
    then (G^o).g in G' by Lm10;
    then (G^o).g in the carrier of G' by STRUCT_0:def 5;
    then (G^o).g in G'' by STRUCT_0:def 5;
    hence (H^o).h in A by A12;
  end;
  then consider H'' be strict StableSubgroup of H such that
A13: the carrier of H'' = A by A3,A8,Lm15;
  reconsider H' = the multMagma of H'' as strict Subgroup of H by Lm16;
  take H'';
A14: now
    let h be Element of H;
    reconsider R = f as Relation of the carrier of G, the carrier of H;
    hereby
      assume h in A;
      then consider g be Element of G such that
A15:  h=f.g & g in G'';
      dom f = the carrier of G by FUNCT_2:def 1;
      then
A16:  [g,h] in f by A15,FUNCT_1:8;
      g in the carrier of G' by A15,STRUCT_0:def 5;
      hence h in f.:(the carrier of G') by A16,RELSET_1:52;
    end;
    assume h in f.:(the carrier of G');
    then consider g be Element of G such that
A17: [g,h] in R & g in the carrier of G' by RELSET_1:52;
A18: f.g=h by A17,FUNCT_1:8;
    g in G'' by A17,STRUCT_0:def 5;
    hence h in A by A18;
  end;
  hence
A19: the carrier of H'' = f.:(the carrier of G') by A13,SUBSET_1:8;
  reconsider N'=the multMagma of N as strict Subgroup of G by Lm16;
  N' is normal by A1,Def10;
  then
A20: carr G'' * N' = N' * carr G'' by GROUP_3:143;
A21: dom f = the carrier of G by FUNCT_2:def 1;
A22: now
    let x;
    assume
A23: x in f"(the carrier of H');
    then x in dom f & f.x in the carrier of H' by FUNCT_1:def 13;
    then consider g1 be set such that
A24: g1 in dom f & g1 in the carrier of G' & f.g1 = f.x by A19,FUNCT_1:def 12;
    reconsider g1,g2=x as Element of G by A23,A24;
    consider g3 be Element of G such that
A25: g2 = g1 * g3 by GROUP_1:23;
    f.g2 = f.g2*f.g3 by A24,A25,GROUP_6:def 7;
    then f.g3 = 1_H by GROUP_1:15;
    then g3 in Ker f by Th47;
    then g3 in the carrier of N by A1,STRUCT_0:def 5;
    hence x in G'' * N' by A24,A25;
  end;
  now
    let x;
    assume
A26: x in G'' * N';
    then consider g1,g2 be Element of G such that
A27: x = g1*g2 & g1 in carr G' & g2 in carr N';
A28: g2 in Ker f by A1,A27,STRUCT_0:def 5;
    f.x = f.g1*f.g2 by A27,GROUP_6:def 7
      .= f.g1*1_H by A28,Th47
      .= f.g1 by GROUP_1:def 5;
    then f.x in f.:(the carrier of G') by A21,A27,FUNCT_1:def 12;
    then x in f"(f.:(the carrier of G')) by A21,A26,FUNCT_1:def 13;
    hence x in f"(the carrier of H') by A13,A14,SUBSET_1:8;
  end;
  then
A29: f"(the carrier of H') = carr G' * carr N by A22,TARSKI:2;
A30: G''*N' = G'*N;
  N'*G'' = N*G';
  hence f"(the carrier of H'') = the carrier of G'"\/"N by A20,A29,A30,Th30;
  now
    assume
A31: f is onto & G' is normal;
    then
A32: G'' is normal by Def10;
    now
      let h1 be Element of H;
      now
        let x be set;
        assume x in h1 * H';
        then consider h2 be Element of H such that
A33:    x=h1*h2 & h2 in A by A13,GROUP_2:33;
        h2 in f.:(the carrier of G') by A14,A33;
        then consider g2 be set such that
A34:    g2 in dom f & g2 in the carrier of G'' & f.g2 = h2 by FUNCT_1:def 12;
        set h2' = h1*h2*h1";
A35:    h2'*h1 = (h1*h2)*(h1"*h1) by GROUP_1:def 4
          .= (h1*h2)*1_H by GROUP_1:def 6
          .= x by A33,GROUP_1:def 5;
        rng f = the carrier of H by A31,FUNCT_2:def 3;
        then consider g1 be set such that
A36:    g1 in dom f & h1 = f.g1 by FUNCT_1:def 5;
        reconsider g1,g2 as Element of G by A34,A36;
        set g2'=g1*g2*g1";
        g2'=(g1"")*g2*g1";
        then g2'=g2 |^ g1" & g2 in G'' by A34,GROUP_3:def 2,STRUCT_0:def 5;
        then g2' in G'' |^ g1" by GROUP_3:70;
        then
A37:    g2' in the carrier of G'' |^ g1" by STRUCT_0:def 5;
        G'' |^ g1" is Subgroup of G'' by A32,GROUP_3:145;
        then
A38:    the carrier of G'' |^ g1" c= the carrier of G'' by GROUP_2:def 5;
        h2' = f.g1*f.g2*f.(g1") by A34,A36,Lm14
          .= f.(g1*g2)*f.(g1") by GROUP_6:def 7
          .= f.g2' by GROUP_6:def 7;
        then h2' in f.:(the carrier of G'') by A21,A37,A38,FUNCT_1:def 12;
        then h2' in A by A14;
        hence x in H' * h1 by A13,A35,GROUP_2:34;
      end;
      hence h1 * H' c= H' * h1 by TARSKI:def 3;
    end;
    then for H1 being strict Subgroup of H
    st H1 = the multMagma of H'' holds H1 is normal by GROUP_3:141;
    hence H'' is normal by Def10;
  end;
  hence thesis;
end;

theorem Th80:
  for G being strict GroupWithOperators of O,
  N being strict normal StableSubgroup of G,
  H being strict StableSubgroup of G./.N
  st the carrier of G = (nat_hom N)"(the carrier of H)
  holds H = (Omega).(G./.N)
proof
  let G be strict GroupWithOperators of O;
  let N be strict normal StableSubgroup of G;
  let H be strict StableSubgroup of G./.N;
  reconsider N' = the multMagma of N as strict normal Subgroup of G by Lm7;
  reconsider H' = the multMagma of H as strict Subgroup of G./.N by Lm16;
A1: the carrier of H' c= the carrier of G./.N &
  the multF of H' = (the multF of G./.N)||the carrier of H' by GROUP_2:def 5;
A2: the carrier of G./.N = the carrier of G./.N' by Def14;
  the multF of G./.N = the multF of G./.N' by Def15;
  then reconsider H' as strict Subgroup of G./.N' by A1,A2,GROUP_2:def 5;
  assume the carrier of G = (nat_hom N)"(the carrier of H);
  then
A3: the carrier of G = (nat_hom N')"(the carrier of H') by Def23;
  now
    let h be Element of G./.N';
    thus h in H' implies h in (Omega).(G./.N') by STRUCT_0:def 5;
    assume h in (Omega).(G./.N');
    h in Left_Cosets N';
    then consider g be Element of G such that
A4: h = g * N' by GROUP_2:def 15;
    reconsider R = nat_hom N'
    as Relation of the carrier of G, the carrier of G./.N';
    consider h' be Element of G./.N' such that
A5: [g,h'] in R & h' in (the carrier of H') by A3,RELSET_1:53;
    (nat_hom N').g = h' by A5,FUNCT_1:8;
    then h in the carrier of H' by A4,A5,GROUP_6:def 9;
    hence h in H' by STRUCT_0:def 5;
  end;
  then H' = (Omega).(G./.N') by GROUP_2:def 6;
  then the carrier of H = Cosets N by Def14;
  hence thesis by Lm5;
end;

theorem Th81:
  for G being strict GroupWithOperators of O,
  N being strict normal StableSubgroup of G,
  H being strict StableSubgroup of G./.N
  st the carrier of N = (nat_hom N)"(the carrier of H) holds H = (1).(G./.N)
proof
  let G be strict GroupWithOperators of O;
  let N be strict normal StableSubgroup of G;
  let H be strict StableSubgroup of G./.N;
  assume
A1: the carrier of N = (nat_hom N)"(the carrier of H);
  assume not H = (1).(G./.N);
  then not the carrier of H = {1_(G./.N)} by Def8;
  then consider h be set such that
A2: not (h in the carrier of H iff h in {1_(G./.N)}) by TARSKI:2;
  reconsider N' = the multMagma of N as strict normal Subgroup of G by Lm7;
  reconsider H' = the multMagma of H as strict Subgroup of G./.N by Lm16;
A3: the carrier of H' c= the carrier of G./.N &
  the multF of H' = (the multF of G./.N)||the carrier of H' by GROUP_2:def 5;
A4: the carrier of G./.N = the carrier of G./.N' by Def14;
  the multF of G./.N = the multF of G./.N' by Def15;
  then reconsider H' as strict Subgroup of G./.N' by A3,A4,GROUP_2:def 5;
A5: the carrier of N' = (nat_hom N')"(the carrier of H') by A1,Def23;
  per cases by A2;
  suppose
A6: h in the carrier of H & not h in {1_(G./.N)};
    then h <> 1_(G./.N) by TARSKI:def 1;
    then
A7: h <> carr N by Th43;
A8: the carrier of H' c= the carrier of G./.N' by GROUP_2:def 5;
    {h} c= the carrier of H by A6,ZFMISC_1:37;
    then
A9: (nat_hom N')"{h} c= the carrier of N' by A5,RELAT_1:178;
    rng nat_hom N' = the carrier of Image nat_hom N' by GROUP_6:53
      .= the carrier of G./.N' by GROUP_6:57;
    then consider x be set such that
A10: x in dom nat_hom N' & (nat_hom N').x = h by A6,A8,FUNCT_1:def 5;
    (nat_hom N').x in {h} by A10,TARSKI:def 1;
    then x in (nat_hom N')"{h} by A10,FUNCT_1:def 13;
    then
A11: x in N' by A9,STRUCT_0:def 5;
    reconsider x as Element of G by A10;
    x * N' = h by A10,GROUP_6:def 9;
    hence contradiction by A7,A11,GROUP_2:136;
  end;
  suppose not h in the carrier of H & h in {1_(G./.N)};
    then h = 1_(G./.N) & not h in H by STRUCT_0:def 5,TARSKI:def 1;
    hence contradiction by Lm18;
  end;
end;

theorem Th82:
  for G,H being strict GroupWithOperators of O
  st G,H are_isomorphic & G is simple holds H is simple
proof
  let G,H be strict GroupWithOperators of O;
  assume
A1: G,H are_isomorphic;
  assume
A2: G is simple;
  assume
A3: H is not simple;
  per cases by A3,Def13;
  suppose H is trivial;
    then G is trivial by A1,Th58;
    hence contradiction by A2,Def13;
  end;
  suppose ex H' being strict normal StableSubgroup of H
    st H' <> (Omega).H & H' <> (1).H;
    then consider H' be strict normal StableSubgroup of H such that
A4: H' <> (Omega).H & H' <> (1).H;
    reconsider H'' = the multMagma of H' as strict normal Subgroup of H by Lm7;
    consider f be Homomorphism of G,H such that
A5: f is bijective by A1,Def22;
A6: f is onto by A5;
    set A = {g where g is Element of G: f.g in H''};
    1_H in H'' by GROUP_2:55;
    then f.(1_G) in H'' by Lm13;
    then 1_G in A;
    then reconsider A as non empty set;
    now
      let x be set;
      assume x in A;
      then consider g be Element of G such that
A7:   x=g & f.g in H'';
      thus x in the carrier of G by A7;
    end;
    then reconsider A as Subset of G by TARSKI:def 3;
A8: now
      let g1,g2 be Element of G;
      assume
A9:   g1 in A & g2 in A;
      then consider a be Element of G such that
A10:  a=g1 & f.a in H'';
      consider b be Element of G such that
A11:  b=g2 & f.b in H'' by A9;
A12:  f.(a*b) = f.a * f.b by GROUP_6:def 7;
      set fa = f.a;
      set fb = f.b;
      fa * fb in H'' by A10,A11,GROUP_2:59;
      hence g1*g2 in A by A10,A11,A12;
    end;
A13: now
      let g be Element of G;
      assume g in A;
      then consider a be Element of G such that
A14:  a=g & f.a in H'';
      (f.a)" in H'' by A14,GROUP_2:60;
      then f.(a") in H'' by Lm14;
      hence g" in A by A14;
    end;
    now
      let o be Element of O;
      let g be Element of G;
      assume g in A;
      then consider a be Element of G such that
A15:  a=g & f.a in H'';
      f.a in the carrier of H'' by A15,STRUCT_0:def 5;
      then f.a in H' by STRUCT_0:def 5;
      then (H^o).(f.g) in H' by A15,Lm10;
      then f.((G^o).g) in H' by Def18;
      then f.((G^o).g) in the carrier of H' by STRUCT_0:def 5;
      then f.((G^o).g) in H'' by STRUCT_0:def 5;
      hence (G^o).g in A;
    end;
    then consider G'' be strict StableSubgroup of G such that
A16: the carrier of G'' = A by A8,A13,Lm15;
    reconsider G'=the multMagma of G'' as strict Subgroup of G by Lm16;
    the carrier of H'<>{1_H} by A4,Def8;
    then consider x be set such that
A17: x in the carrier of H' & x<>1_H by ZFMISC_1:41;
A18: x in H'' by A17,STRUCT_0:def 5;
    then x in H by GROUP_2:49;
    then reconsider x as Element of H by STRUCT_0:def 5;
    consider y be Element of G such that
A19: f.y = x by A6,Th52;
A20: y in the carrier of G'' by A16,A18,A19;
    y<>1_G by A17,A19,Lm13;
    then the carrier of G'' <> {1_G} by A20,TARSKI:def 1;
    then
A21: G''<>(1).G by Def8;
    the multMagma of H' <> the multMagma of H by A4,Lm5;
    then consider h be Element of H such that
A22: not h in H'' by GROUP_2:71;
    consider g be Element of G such that
A23: f.g = h by A6,Th52;
    now
      assume g in A;
      then consider g' be Element of G such that
A24:  g'=g & f.g' in H'';
      thus contradiction by A22,A23,A24;
    end;
    then
A25: G''<>(Omega).G by A16;
    now
      let g be Element of G;
      now
        let x be set;
        assume x in g * G';
        then consider h be Element of G such that
A26:    x=g*h & h in A by A16,GROUP_2:33;
        consider a be Element of G such that
A27:    a=h & f.a in H'' by A26;
        set h'=g*h*g";
A28:    h'*g = (g*h)*(g"*g) by GROUP_1:def 4
          .= (g*h)*1_G by GROUP_1:def 6
          .= x by A26,GROUP_1:def 5;
A29:    H'' |^ (f.g)" = H'' by GROUP_3:def 13;
        f.h' = f.(g*h) * f.(g") by GROUP_6:def 7
          .= f.g * f.h * f.(g") by GROUP_6:def 7
          .= f.g * f.h * (f.g)" by Lm14
          .= ((f.g)")" * f.h * (f.g)"
          .= f.h |^ (f.g)" by GROUP_3:def 2;
        then f.h' in H'' by A27,A29,GROUP_3:70;
        then h' in A;
        hence x in G' * g by A16,A28,GROUP_2:34;
      end;
      hence g * G' c= G' * g by TARSKI:def 3;
    end;
    then for H being strict Subgroup of G st H = the multMagma of G''
    holds H is normal by GROUP_3:141;
    then G'' is normal by Def10;
    hence contradiction by A2,A21,A25,Def13;
  end;
end;

theorem Th83:
  for G being GroupWithOperators of O, H being StableSubgroup of G,
  FG being FinSequence of the carrier of G,
  FH being FinSequence of the carrier of H, I be FinSequence of INT
  st FG=FH & len FG = len I holds Product(FG |^ I) = Product(FH |^ I)
proof
  let G be GroupWithOperators of O;
  let H be StableSubgroup of G;
  let FG be FinSequence of the carrier of G;
  let FH be FinSequence of the carrier of H;
  let I be FinSequence of INT;
  assume
A1: FG=FH & len FG = len I;
  defpred P[Nat] means for FG being FinSequence of the carrier of G,
  FH being FinSequence of the carrier of H,
  I being FinSequence of INT st len FG = $1 & FG=FH & len FG = len I
  holds Product(FG |^ I) = Product(FH |^ I);
A2: P[0]
  proof
    let FG be FinSequence of the carrier of G;
    let FH be FinSequence of the carrier of H;
    let I be FinSequence of INT;
    assume
A3: len FG = 0;
    then len(FG |^ I) = 0 by GROUP_4:def 4;
    then FG |^ I = <*> the carrier of G;
    then
A4: Product(FG |^ I) = 1_G by GROUP_4:11;
    assume FG=FH & len FG = len I;
    then len(FH |^ I) = 0 by A3,GROUP_4:def 4;
    then FH |^ I = <*> the carrier of H;
    then Product(FH |^ I) = 1_H by GROUP_4:11;
    hence thesis by A4,Th4;
  end;
A5: now
    let n be Nat;
    assume
A6: P[n];
    thus P[n+1]
    proof
      let FG be FinSequence of the carrier of G;
      let FH be FinSequence of the carrier of H;
      let I be FinSequence of INT;
      assume
A7:   len FG = n+1;
      assume
A8:   FG=FH & len FG = len I;
      then consider In be FinSequence of INT, i be Element of INT such that
A9:   I = In^<*i*> by A7,FINSEQ_2:22;
      consider FGn be FinSequence of the carrier of G,
      g be Element of G such that
A10:  FG = FGn^<*g*> by A7,FINSEQ_2:22;
      consider FHn be FinSequence of the carrier of H,
      h be Element of H such that
A11:  FH = FHn^<*h*> by A7,A8,FINSEQ_2:22;
      set I1=<*i*>;
      set FG1=<*g*>;
      set FH1=<*h*>;
      len I = len In + len <*i*> by A9,FINSEQ_1:35;
      then
A12:  n+1 = len In + 1 by A7,A8,FINSEQ_1:57;
A13:  len FG = len FGn + len <*g*> by A10,FINSEQ_1:35;
      then
A14:  n+1 = len FGn + 1 by A7,FINSEQ_1:57;
A15:  len FH = len FHn + len <*h*> by A11,FINSEQ_1:35;
      then
A16:  n+1 = len FHn + 1 by A7,A8,FINSEQ_1:57;
A17:  FG.(n+1)=(FGn^<*g*>).(len FGn +1) by A7,A10,A13,FINSEQ_1:57
        .= g by FINSEQ_1:59;
A18:  FH.(n+1)=(FHn^<*h*>).(len FHn +1) by A7,A8,A11,A15,FINSEQ_1:57
        .= h by FINSEQ_1:59;
      then FGn = FHn by A8,A10,A11,A17,FINSEQ_1:46;
      then
A19:  Product(FGn |^ In) = Product(FHn |^ In) by A6,A12,A16;
A20:  len FG1 = 1 by FINSEQ_1:57
        .=len I1 by FINSEQ_1:57;
A21:  len FH1 = 1 by FINSEQ_1:57
        .=len I1 by FINSEQ_1:57;
A22:  Product(FG |^ I)
      = Product((FGn |^ In)^(FG1 |^ I1)) by A9,A10,A12,A14,A20,GROUP_4:25
        .= Product(FGn |^ In) * Product(FG1 |^ I1) by GROUP_4:8;
A23:  now
        reconsider H'=H as Subgroup of G by Def7;
        reconsider h'=h as Element of H';
        g|^i = h'|^i by A8,A17,A18,GROUP_4:4;
        hence g|^i = h|^i;
      end;
      Product(FG1 |^ I1) = Product(<*g*>|^<*@i*>)
        .= Product <*g|^i*> by GROUP_4:28
        .= h|^i by A23,GROUP_4:12
        .= Product <*h|^i*> by GROUP_4:12
        .= Product(<*h*>|^<*@i*>) by GROUP_4:28
        .= Product(FH1 |^ I1);
      then Product(FG |^ I) = Product(FHn |^ In) * Product(FH1 |^ I1) by A19
,A22,Th3
        .= Product((FHn |^ In)^(FH1 |^ I1)) by GROUP_4:8
        .= Product((FHn^FH1) |^ (In^I1)) by A12,A16,A21,GROUP_4:25;
      hence thesis by A9,A11;
    end;
  end;
A24: for n being Nat holds P[n] from NAT_1:sch 2(A2,A5);
  thus thesis by A1,A24;
end;

theorem Th84:
  for O,E1,E2 being set, A1 being Action of O,E1, A2 being Action of O,E2,
  F being FinSequence of O st E1 c= E2 &
  (for o being Element of O, f1 being Function of E1,E1,
  f2 being Function of E2,E2 st f1=A1.o & f2=A2.o holds f1 = f2|E1)
  holds Product(F,A1) = Product(F,A2)|E1
proof
  let O,E1,E2 be set;
  let A1 be Action of O,E1;
  let A2 be Action of O,E2;
  let F be FinSequence of O;
  assume
A1: E1 c= E2;
  assume
A2: for o being Element of O, f1 being Function of E1,E1,
  f2 being Function of E2,E2 st f1=A1.o & f2=A2.o holds f1 = f2|E1;
  defpred P[Element of NAT] means
  for F being FinSequence of O st len F = $1 holds
  Product(F,A1) = Product(F,A2)|E1;
A3: P[0]
  proof
    let F be FinSequence of O;
    assume
A4: len F = 0;
    E1 = E2 /\ E1 by A1,XBOOLE_1:28;
    then dom id E1 = E2 /\ E1 by RELAT_1:71;
    then
A5: dom id E1 = dom id E2 /\ E1 by RELAT_1:71;
A6: now
      let x;
      assume
A7:   x in dom id E1;
      then
A8:   x in E1;
      thus (id E1).x = x by A7,FUNCT_1:35
        .= (id E2).x by A1,A8,FUNCT_1:35;
    end;
    thus Product(F,A1) = id E1 by A4,Def3
      .= (id E2)|E1 by A5,A6,FUNCT_1:68
      .= Product(F,A2)|E1 by A4,Def3;
  end;
  per cases;
  suppose O is empty;
    then rng F = {};
    then F = {};
    then len F = 0;
    hence thesis by A3;
  end;
  suppose
A9: O is non empty;
A10: for k being Element of NAT st P[k] holds P[k + 1]
    proof
      let k be Element of NAT;
      assume
A11:  P[k];
      now
        let F be FinSequence of O;
        assume
A12:    len F = k+1;
        then consider Fk be FinSequence of O, o be Element of O such that
A13:    F = Fk^<*o*> by FINSEQ_2:22;
        len F = len Fk + len <*o*> by A13,FINSEQ_1:35;
        then
A14:    k+1 = len Fk + 1 by A12,FINSEQ_1:56;
        Product(F,A1) in Funcs(E1,E1) by FUNCT_2:12;
        then consider f1 be Function such that
A15:    Product(F,A1) = f1 & dom f1 = E1 & rng f1 c= E1 by FUNCT_2:def 2;
        Product(F,A2) in Funcs(E2,E2) by FUNCT_2:12;
        then consider f2 be Function such that
A16:    Product(F,A2) = f2 & dom f2 = E2 & rng f2 c= E2 by FUNCT_2:def 2;
A17:    dom(Product(F,A2)|E1) = E2 /\ E1 by A16,RELAT_1:90
          .= E1 by A1,XBOOLE_1:28;
        now
          let x;
          assume
A18:      x in dom Product(F,A1);
          then
A19:      x in E1;
A20:      o in O by A9;
          {o} c= O by A9,ZFMISC_1:37;
          then rng <*o*> c= O by FINSEQ_1:55;
          then reconsider Fo=<*o*> as FinSequence of O by FINSEQ_1:def 4;
          o in dom A1 by A20,FUNCT_2:def 1;
          then A1.o in rng A1 by FUNCT_1:12;
          then consider f1 be Function such that
A21:      f1=A1.o & dom f1 = E1 & rng f1 c= E1 by FUNCT_2:def 2;
          o in dom A2 by A20,FUNCT_2:def 1;
          then A2.o in rng A2 by FUNCT_1:12;
          then consider f2 be Function such that
A22:      f2=A2.o & dom f2 = E2 & rng f2 c= E2 by FUNCT_2:def 2;
A23:      Product(Fo,A1) = f1 by A9,A21,Lm26;
A24:      Product(Fo,A2) = f2 by A9,A22,Lm26;
A25:      Product(F,A1) = (Product(Fk,A1)*Product(Fo,A1)) by A9,A13,Lm29
            .= Product(Fk,A1)*f1 by A9,A21,Lm26;
A26:      Product(F,A2) = (Product(Fk,A2)*Product(Fo,A2)) by A9,A13,Lm29
            .= Product(Fk,A2)*f2 by A9,A22,Lm26;
A27:      f1.x in rng f1 by A18,A21,FUNCT_1:12;
          thus Product(F,A1).x = Product(Fk,A1).(f1.x) by A18,A21,A25,
FUNCT_1:23
            .= (Product(Fk,A2)|E1).(f1.x) by A11,A14
            .= Product(Fk,A2).(f1.x) by A21,A27,FUNCT_1:72
            .= Product(Fk,A2).((f2|E1).x) by A2,A21,A22,A23,A24
            .= Product(Fk,A2).(f2.x) by A18,FUNCT_1:72
            .= (Product(Fk,A2)*f2).x by A1,A19,A22,FUNCT_1:23
            .= (Product(F,A2)|E1).x by A18,A26,FUNCT_1:72;
        end;
        hence Product(F,A1) = Product(F,A2)|E1 by A15,A17,FUNCT_1:9;
      end;
      hence thesis;
    end;
A28: for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A10);
    reconsider k = len F as Element of NAT;
    k = len F;
    hence thesis by A28;
  end;
end;

theorem Th85:
  for N1,N2 being strict StableSubgroup of H1,
  N1',N2' being strict StableSubgroup of G
  st N1 = N1' & N2 = N2' holds N1' * N2' = N1 * N2
proof
  let N1,N2 be strict StableSubgroup of H1;
  let N1',N2' be strict StableSubgroup of G;
  assume
A1: N1=N1' & N2=N2';
  set X={g * h where g,h is Element of G: g in carr N1' & h in carr N2'};
  set Y={g * h where g,h is Element of H1: g in carr N1 & h in carr N2};
A2: now
    let x;
    assume x in X;
    then consider g,h be Element of G such that
A3: x=g*h & g in carr N1' & h in carr N2';
    N1 is Subgroup of H1 & N2 is Subgroup of H1 by Def7;
    then the carrier of N1 c= the carrier of H1 &
    the carrier of N2 c= the carrier of H1 by GROUP_2:def 5;
    then reconsider g,h as Element of H1 by A1,A3;
    x=g*h by A3,Th3;
    hence x in Y by A1,A3;
  end;
  now
    let x;
    assume x in Y;
    then consider g,h be Element of H1 such that
A4: x=g*h & g in carr N1 & h in carr N2;
    reconsider g,h as Element of G by Th2;
    x=g*h by A4,Th3;
    hence x in X by A1,A4;
  end;
  hence thesis by A2,TARSKI:2;
end;

theorem Th86:
  for N1,N2 being strict StableSubgroup of H1,
  N1',N2' being strict StableSubgroup of G
  st N1 = N1' & N2 = N2' holds N1' "\/" N2' = N1 "\/" N2
proof
  let N1,N2 be strict StableSubgroup of H1;
  let N1',N2' be strict StableSubgroup of G;
  assume
A1: N1=N1' & N2=N2';
A2: N1' "\/" N2' = the_stable_subgroup_of(N1'*N2') by Th29;
A3: N1 "\/" N2 = the_stable_subgroup_of(N1*N2) by Th29;
  set S1 = the_stable_subgroup_of(N1'*N2');
  reconsider S2 = the_stable_subgroup_of(N1*N2) as StableSubgroup of G by Th11;
  set X1={B where B is Subset of G :
  ex H being strict StableSubgroup of G st B = the carrier of H &
  N1'*N2' c= carr H};
  set X2={B where B is Subset of H1 :
  ex H being strict StableSubgroup of H1 st B = the carrier of H &
  N1*N2 c= carr H};
A4: now
    set x'=carr H1;
    reconsider x=x' as set;
    take x;
    now
      set H=(Omega).H1;
      take H;
      thus x' = the carrier of H;
      thus N1*N2 c= carr H;
    end;
    hence x in X2;
  end;
A5: the carrier of the_stable_subgroup_of(N1'*N2') = meet X1 by Th27;
A6: the carrier of the_stable_subgroup_of(N1*N2) = meet X2 by Th27;
  now
    let x;
    assume
A7: x in the carrier of the_stable_subgroup_of(N1*N2);
    the_stable_subgroup_of(N1*N2) is Subgroup of H1 by Def7;
    then the carrier of the_stable_subgroup_of(N1*N2) c= the carrier of H1
    by GROUP_2:def 5;
    then reconsider g=x as Element of H1 by A7;
    g in the_stable_subgroup_of(N1*N2) by A7,STRUCT_0:def 5;
    then consider F be FinSequence of the carrier of H1,
    I be FinSequence of INT, C be Subset of H1 such that
A8: C = the_stable_subset_generated_by (N1*N2, the action of H1) &
    len F = len I & rng F c= C & Product(F |^ I) = g by Th24;
    reconsider g as Element of G by Th2;
    now
      let x;
      assume
A9:   x in the_stable_subset_generated_by (N1*N2, the action of H1);
      then reconsider a=x as Element of H1;
      N1 is Subgroup of H1 & N2 is Subgroup of H1 by Def7;
      then 1_H1 in N1 & 1_H1 in N2 by GROUP_2:55;
      then 1_H1 in carr N1 & 1_H1 in carr N2 & 1_H1=1_H1*1_H1
      by GROUP_1:def 5,STRUCT_0:def 5;
      then
A10:  1_H1 in carr N1 * carr N2;
      then consider F be FinSequence of O, h be Element of N1*N2 such that
A11:  Product(F,the action of H1).h = a by A9,Lm31;
A12:  now
        let o be Element of O;
        let f1 be Function of the carrier of H1,the carrier of H1;
        let f2 be Function of the carrier of G,the carrier of G;
        assume
A13:    f1=(the action of H1).o & f2=(the action of G).o;
        per cases;
        suppose o in O;
          then H1^o = f1 & G^o = f2 by A13,Def6;
          hence f1 = f2|the carrier of H1 by Def7;
        end;
        suppose not o in O;
          then not o in dom the action of H1 & not o in dom the action of G;
          hence f1 = f2|the carrier of H1 by A13,FUNCT_1:def 4;
        end;
      end;
      H1 is Subgroup of G by Def7;
      then
A14:  the carrier of H1 c= the carrier of G by GROUP_2:def 5;
      then
A15:  Product(F,the action of H1) =
      Product(F,the action of G)|the carrier of H1 by A12,Th84;
A16:  h in N1*N2 by A10;
      reconsider a as Element of G by A14,TARSKI:def 3;
      reconsider h as Element of N1'*N2' by A1,Th85;
      Product(F,the action of G).h = a & N1'*N2' is non empty
      by A1,A11,A15,A16,Th85,FUNCT_1:72;
      hence x in the_stable_subset_generated_by (N1'*N2', the action of G)
      by Lm31;
    end;
    then the_stable_subset_generated_by (N1*N2, the action of H1) c=
    the_stable_subset_generated_by (N1'*N2', the action of G) by TARSKI:def 3;
    then
A17: rng F c= the_stable_subset_generated_by (N1'*N2', the action of G )
    by A8,XBOOLE_1:1;
    H1 is Subgroup of G by Def7;
    then the carrier of H1 c= the carrier of G by GROUP_2:def 5;
    then rng F c= the carrier of G by XBOOLE_1:1;
    then reconsider F as FinSequence of the carrier of G by FINSEQ_1:def 4;
    Product(F |^ I) = g by A8,Th83;
    then
A18: g in the_stable_subgroup_of(N1'*N2') by A8,A17,Th24;
    assume not x in the carrier of the_stable_subgroup_of(N1'*N2');
    hence contradiction by A18,STRUCT_0:def 5;
  end;
  then
A19: meet X2 c= meet X1 by A5,A6,TARSKI:def 3;
  now
    let x;
    assume x in X2;
    then consider B be Subset of H1 such that
A20: x=B & ex H being strict StableSubgroup of H1 st B = the carrier of H &
    N1*N2 c= carr H;
    now
      consider H be strict StableSubgroup of H1 such that
A21:  B = the carrier of H & N1*N2 c= carr H by A20;
      reconsider H as strict StableSubgroup of G by Th11;
      take H;
      thus B = the carrier of H & N1'*N2' c= carr H by A1,A21,Th85;
    end;
    hence x in X1 by A20;
  end;
  then X2 c= X1 by TARSKI:def 3;
  then meet X1 c= meet X2 by A4,SETFAM_1:7;
  then the carrier of S1 = the carrier of S2 by A5,A6,A19,XBOOLE_0:def 10;
  hence thesis by A2,A3,Lm5;
end;

theorem Th87:
  for N1,N2 being strict StableSubgroup of G
  st N1 is normal StableSubgroup of H1 & N2 is normal StableSubgroup of H1
  holds N1 "\/" N2 is normal StableSubgroup of H1
proof
  let N1,N2 be strict StableSubgroup of G;
  assume
A1: N1 is normal StableSubgroup of H1 & N2 is normal StableSubgroup of H1;
  then reconsider N1'=N1,N2'=N2 as StableSubgroup of H1;
  N1 "\/" N2 = N1' "\/" N2' by Th86;
  hence thesis by A1,Th32;
end;

theorem Th88:
  for f being Homomorphism of G,H holds for g being Homomorphism of H,I holds
  the carrier of Ker(g*f) = f"(the carrier of Ker g)
proof
  let f be Homomorphism of G,H;
  let g be Homomorphism of H,I;
A1: dom f = the carrier of G by FUNCT_2:def 1;
A2: now
    let x;
    assume x in the carrier of Ker(g*f);
    then x in {a where a is Element of G: (g*f).a = 1_I} by Def24;
    then consider a be Element of G such that
A3: x=a & (g*f).a =1_I;
    reconsider b=f.a as Element of H;
    g.b = 1_I by A1,A3,FUNCT_1:23;
    then f.x in {b' where b' is Element of H: g.b' = 1_I} by A3;
    then f.x in the carrier of Ker g by Def24;
    hence x in f"(the carrier of Ker g) by A1,A3,FUNCT_1:def 13;
  end;
  now
    let x;
    assume
A4: x in f"(the carrier of Ker g);
    then
A5: x in dom f & f.x in the carrier of Ker g by FUNCT_1:def 13;
    consider a be Element of G such that
A6: x=a by A4;
    f.x in {b where b is Element of H: g.b = 1_I} by A5,Def24;
    then consider b be Element of H such that
A7: b=f.x & g.b = 1_I;
    1_I = (g*f).x by A5,A7,FUNCT_1:23;
    then x in {a' where a' is Element of G: (g*f).a' = 1_I} by A6;
    hence x in the carrier of Ker(g*f) by Def24;
  end;
  hence thesis by A2,TARSKI:2;
end;

theorem Th89:
  for G' being StableSubgroup of G, H' being StableSubgroup of H,
  f being Homomorphism of G,H st the carrier of H' = f.:(the carrier of G') or
  the carrier of G' = f"(the carrier of H')
  holds f|(the carrier of G') is Homomorphism of G',H'
proof
  let G' be StableSubgroup of G;
  let H' be StableSubgroup of H;
  let f be Homomorphism of G,H;
  assume
A1: the carrier of H' = f.:(the carrier of G') or
  the carrier of G' = f"(the carrier of H');
  G' is Subgroup of G by Def7;
  then
A2: the carrier of G' c= the carrier of G by GROUP_2:def 5;
  then
A3: the carrier of G' c= dom f by FUNCT_2:def 1;
A4: for x st x in the carrier of G' holds f.x in the carrier of H'
  proof
    let x;
    assume
A5: x in the carrier of G';
    per cases by A1;
    suppose
A6:   the carrier of H' = f.:(the carrier of G');
      assume not f.x in the carrier of H';
      hence contradiction by A3,A5,A6,FUNCT_1:def 12;
    end;
    suppose the carrier of G' = f"(the carrier of H');
      hence thesis by A5,FUNCT_1:def 13;
    end;
  end;
  set g=f|(the carrier of G');
  now
A7: dom g = the carrier of G' by A3,RELAT_1:91;
    now
      let y be set;
      assume y in rng g;
      then consider x such that
A8:   x in dom g & y = g.x by FUNCT_1:def 5;
A9:   x in the carrier of G' by A3,A8,RELAT_1:91;
      then y = f.x by A8,FUNCT_1:72;
      hence y in the carrier of H' by A4,A9;
    end;
    then rng g c= the carrier of H' by TARSKI:def 3;
    hence g is Function of G',H' by A7,RELSET_1:11;
  end;
  then reconsider g as Function of G',H';
A10: now
    let a',b' be Element of G';
    reconsider a=a',b=b' as Element of G by A2,TARSKI:def 3;
A11: f.a = g.a' & f.b = g.b' by FUNCT_1:72;
    thus g.(a'* b') = f.(a'*b') by FUNCT_1:72
      .= f.(a*b) by Th3
      .= f.a * f.b by GROUP_6:def 7
      .= g.a' * g.b' by A11,Th3;
  end;
  now
    let o be Element of O;
    let a' be Element of G';
    reconsider a=a' as Element of G by A2,TARSKI:def 3;
    thus g.((G'^o).a') = f.((G'^o).a') by FUNCT_1:72
      .= f.(((G^o)|the carrier of G').a') by Def7
      .= f.((G^o).a) by FUNCT_1:72
      .= (H^o).(f.a) by Def18
      .= (H^o).(g.a') by FUNCT_1:72
      .= ((H^o)|the carrier of H').(g.a') by FUNCT_1:72
      .= (H'^o).(g.a') by Def7;
  end;
  hence thesis
  by A10,Def18,GROUP_6:def 7;
end;

:: ALG I.4.6 Corollary 2

theorem Th90:
  for G,H being strict GroupWithOperators of O,
  N,L,G' being strict StableSubgroup of G, f being Homomorphism of G,H
  st N = Ker f & L is strict normal StableSubgroup of G'
  holds L"\/"(G'/\N) is normal StableSubgroup of G' &
  L"\/"N is normal StableSubgroup of G'"\/"N &
  (for N1 being strict normal StableSubgroup of G'"\/"N,
  N2 being strict normal StableSubgroup of G' st N1=L"\/"N & N2=L"\/"(G'/\N)
  holds (G'"\/"N)./.N1, G'./.N2 are_isomorphic)
proof
  let G,H be strict GroupWithOperators of O;
  let N,L,G' be strict StableSubgroup of G;
  let f be Homomorphism of G,H;
  assume
A1: N = Ker f;
  assume
A2: L is strict normal StableSubgroup of G';
  reconsider Ns = the multMagma of N as strict normal Subgroup of G by A1,Lm7;
  reconsider Gs' = the multMagma of G' as strict Subgroup of G by Lm16;
  consider H' be strict StableSubgroup of H such that
A3: the carrier of H' = f.:(the carrier of G') &
  f"(the carrier of H') = the carrier of G'"\/"N &
  (f is onto & G' is normal implies H' is normal) by A1,Th79;
  reconsider f'=f|(the carrier of G') as Homomorphism of G',H' by A3,Th89;
A4: f'.:(the carrier of G') = f.:(the carrier of G') by RELAT_1:162;
  H' is strict StableSubgroup of H' by Lm4;
  then
A5: Image f' = H' by A3,A4,Def25;
  reconsider L'=L as strict StableSubgroup of G' by A2;
  reconsider N'=G'/\N as StableSubgroup of G' by Lm34;
A6: now
    let x;
    assume x in the carrier of N';
    then
A7: x in carr G' /\ carr N by Def28;
    then x in carr G' & x in carr N by XBOOLE_0:def 4;
    then
A8: x in the carrier of G' & x in N by STRUCT_0:def 5;
    reconsider a'=x as Element of G' by A7,XBOOLE_0:def 4;
    reconsider a''=a' as Element of G by Th2;
    f.a'' = 1_H by A1,A8,Th47;
    then f.a' = 1_H' by Th4;
    then f'.a' = 1_H' by FUNCT_1:72;
    hence x in {a where a is Element of G': f'.a = 1_H'};
  end;
  now
    let x;
    assume x in {a where a is Element of G': f'.a = 1_H'};
    then consider a be Element of G' such that
A9: x=a & f'.a = 1_H';
    reconsider a as Element of G by Th2;
    f.a = 1_H' by A9,FUNCT_1:72;
    then f.a = 1_H by Th4;
    then x in the carrier of G' & x in N by A1,A9,Th47;
    then x in carr G' & x in carr N by STRUCT_0:def 5;
    then x in carr G' /\ carr N by XBOOLE_0:def 4;
    hence x in the carrier of N' by Def28;
  end;
  then the carrier of N' = {a where a is Element of G': f'.a = 1_H'}
  by A6,TARSKI:2;
  then
A10: N' = Ker f' by Def24;
  then consider H'' be strict StableSubgroup of H' such that
A11: the carrier of H'' = f'.:(the carrier of L') &
  f'"(the carrier of H'') = the carrier of L'"\/"N' &
  (f' is onto & L' is normal implies H'' is normal) by Th79;
  consider N2 be strict StableSubgroup of G' such that
A12: the carrier of N2 = f'"(the carrier of H'') &
  (H'' is normal implies N' is normal StableSubgroup of N2 & N2 is normal)
  by A10,Th78;
A13: rng f' = the carrier of H' by A5,Th49;
A14: N2 = L'"\/"N' by A11,A12,Lm5;
  then
A15: N2 = L"\/"(G'/\N) by Th86;
  thus L"\/"(G'/\N) is normal StableSubgroup of G'
  by A2,A11,A12,A13,A14,Th86,FUNCT_2:def 3;
  reconsider f'' = f|(the carrier of G'"\/"N)
  as Homomorphism of G'"\/"N,H' by A3,Th89;
A16: f''.:(the carrier of (G'"\/"N))=f.:(the carrier of (G'"\/"N))
  by RELAT_1:162;
  carr Gs' * Ns = Ns * carr Gs' by GROUP_3:143;
  then
A17: G' * N = N * G';
A18: dom f = the carrier of G by FUNCT_2:def 1;
A19: now
    let y be set;
    assume y in f.:(the carrier of G');
    then consider x such that
A20: x in dom f & x in the carrier of G' & y = f.x by FUNCT_1:def 12;
    reconsider x as Element of G by A20;
    consider x' be set such that
A21: x'=x*1_G;
A22: x' in dom f by A20,A21,GROUP_1:def 5;
    f.(1_G) = 1_H by Lm13;
    then 1_G in Ker f by Th47;
    then x in carr Gs' & 1_G in carr N by A1,A20,STRUCT_0:def 5;
    then
A23: x' in G'*N by A21;
    y = f.x * 1_H by A20,GROUP_1:def 5
      .= f.x * f.(1_G) by Lm13
      .= f.x' by A21,GROUP_6:def 7;
    hence y in f.:(G'*N) by A22,A23,FUNCT_1:def 12;
  end;
  now
    let y be set;
    assume y in f.:(G'*N);
    then consider x such that
A24: x in dom f & x in G'*N & y = f.x by FUNCT_1:def 12;
    reconsider x as Element of G by A24;
    consider g1,g2 be Element of G such that
A25: x = g1*g2 & g1 in carr G' & g2 in carr N by A24;
A26: g1 in the carrier of G' & g2 in N by A25,STRUCT_0:def 5;
    y = f.g1*f.g2 by A24,A25,GROUP_6:def 7
      .= f.g1*1_H by A1,A26,Th47
      .= f.g1 by GROUP_1:def 5;
    hence y in f.:(the carrier of G') by A18,A25,FUNCT_1:def 12;
  end;
  then f.:(the carrier of G') = f.:(G'*N) by A19,TARSKI:2;
  then
A27: the carrier of H' = f.:(the carrier of (G'"\/"N)) by A3,A17,Th30;
  reconsider H'' as normal StableSubgroup of H' by A2,A11,A13,FUNCT_2:def 3;
  set l = nat_hom H'';
  set f1 = l*f'';
A28: L"\/"N is StableSubgroup of G'"\/"N by A2,Th38;
  reconsider N1=L"\/"N as StableSubgroup of G'"\/"N by A2,Th38;
  reconsider L''=L as Subgroup of G by Def7;
  carr L'' * Ns = Ns * carr L'' by GROUP_3:143;
  then
A29: L * N = N * L;
  L is Subgroup of G' by A2,Def7;
  then the carrier of L c= the carrier of G' by GROUP_2:def 5;
  then
A30: f'.:(the carrier of L) = f.:(the carrier of L) by RELAT_1:162;
A31: now
    let x;
    assume x in f''"(f.:(the carrier of L));
    then
A32: x in ((the carrier of G'"\/"N) /\ f"(f.:(the carrier of L)))
    by FUNCT_1:139;
    then x in f"(f.:(the carrier of L)) by XBOOLE_0:def 4;
    then x in dom f & f.x in f.:(the carrier of L) by FUNCT_1:def 13;
    then consider g1 be set such that
A33: g1 in dom f & g1 in the carrier of L & f.x = f.g1 by FUNCT_1:def 12;
    reconsider g1,g2=x as Element of G by A32,A33;
    consider g3 be Element of G such that
A34: g2 = g1 * g3 by GROUP_1:23;
    f.g2 = f.g2*f.g3 by A33,A34,GROUP_6:def 7;
    then f.g3 = 1_H by GROUP_1:15;
    then g3 in Ker f by Th47;
    then g3 in the carrier of N by A1,STRUCT_0:def 5;
    hence x in L * N by A33,A34;
  end;
  now
    let x;
    assume
A35: x in L * N;
    then
A36: x in the carrier of L"\/"N by A29,Th30;
    L"\/"N is Subgroup of G'"\/"N by A28,Def7;
    then
A37: the carrier of L"\/"N c= the carrier of G'"\/"N by GROUP_2:def 5;
    then x in G'"\/"N by A36,STRUCT_0:def 5;
    then x in G by Th1;
    then
A38: x in dom f by A18,STRUCT_0:def 5;
    consider g1,g2 be Element of G such that
A39: x = g1*g2 & g1 in carr L & g2 in carr N by A35;
A40: g1 in the carrier of L & g2 in N by A39,STRUCT_0:def 5;
    f.x = f.g1*f.g2 by A39,GROUP_6:def 7
      .= f.g1*1_H by A1,A40,Th47
      .= f.g1 by GROUP_1:def 5;
    then f.x in f.:(the carrier of L) by A18,A39,FUNCT_1:def 12;
    then x in f"(f.:(the carrier of L)) by A38,FUNCT_1:def 13;
    then x in ((the carrier of G'"\/"N) /\ f"(f.:(the carrier of L)))
    by A36,A37,XBOOLE_0:def 4;
    hence x in f''"(f.:(the carrier of L)) by FUNCT_1:139;
  end;
  then f''"(f'.:(the carrier of L)) = L * N by A30,A31,TARSKI:2;
  then
A41: f''"(the carrier of H'') = the carrier of N1 by A11,A29,Th30;
A42: f''"(the carrier of Ker l) = f''"(the carrier of H'') by Th48;
  then
A43: the carrier of Ker f1 = the carrier of N1 by A41,Th88;
A44: Ker f1 = N1 by A41,A42,Lm5,Th88;
  thus L"\/"N is normal StableSubgroup of G'"\/"N by A43,Lm5;
  now
    let N1' be strict normal StableSubgroup of G'"\/"N;
    let N2' be strict normal StableSubgroup of G';
    assume
A45: N1'=L"\/"N;
    assume
A46: N2'=L"\/"(G'/\N);
A47: (G'"\/"N)./.Ker f1, Image f1 are_isomorphic by Th59;
    set f2 = l*f';
A48: Image f2, G'./.Ker f2 are_isomorphic by Th59;
A49: f''.:(the carrier of G'"\/"N) = f'.:(the carrier of G')
    by A3,A16,A27,RELAT_1:162;
    f1.:(the carrier of G'"\/"N) = l.:(f''.:(the carrier of G'"\/"N))
    by RELAT_1:159;
    then
A50: f1.:(the carrier of G'"\/"N)=f2.:(the carrier of G') by A49,RELAT_1:159;
    the carrier of Image f1=f1.:(the carrier of G'"\/"N) by Def25
      .= the carrier of Image f2 by A50,Def25;
    then
A51: Image f1 = Image f2 by Lm5;
    f'"(the carrier of Ker l) = f'"(the carrier of H'') by Th48;
    then N2'=Ker f2 by A12,A15,A46,Lm5,Th88;
    hence (G'"\/"N)./.N1',G'./.N2' are_isomorphic by A44,A45,A47,A48,A51,Th55;
  end;
  hence thesis;
end;

:: ALG I.4.7 Lemma 1

begin :: The Zassenhaus Butterfly Lemma

theorem Th91:
  for H,K,H',K' being strict StableSubgroup of G,
  JH being normal StableSubgroup of H'"\/"(H/\K),
  HK being normal StableSubgroup of H/\K
  st H' is normal StableSubgroup of H & K' is normal StableSubgroup of K
  & JH = H'"\/"(H/\K') & HK=(H'/\K)"\/"(K'/\H)
  holds (H'"\/"(H/\K))./.JH, (H/\K)./.HK are_isomorphic
proof
  let H,K,H',K' be strict StableSubgroup of G;
  let JH be normal StableSubgroup of H'"\/"(H/\K);
  let HK be normal StableSubgroup of H/\K;
  assume
A1: H' is normal StableSubgroup of H & K' is normal StableSubgroup of K;
  assume
A2: JH = H'"\/"(H/\K') & HK=(H'/\K)"\/"(K'/\H);
  reconsider GG = H as GroupWithOperators of O;
  reconsider N' = H' as normal StableSubgroup of GG by A1;
  reconsider N = N' as StableSubgroup of GG;
  reconsider HH = GG./.N' as GroupWithOperators of O;
  reconsider f = nat_hom N' as Homomorphism of GG,HH;
  set G'=H/\K;
  reconsider G' as strict StableSubgroup of GG by Lm34;
  set L=H/\K';
A3: L is normal StableSubgroup of G' by A1,Th60;
  then reconsider L as StableSubgroup of GG by Th11;
A4: L"\/"N = (H/\K')"\/"H' by Th86
    .=JH by A2;
  set N1=G'/\N;
  N1=(H/\K)/\H' by Th39;
  then
A5: L"\/"N1 = (H/\K')"\/"((H/\K)/\H') by Th86
    .= ((H'/\H)/\K)"\/"(K'/\H) by Th20
    .= HK by A1,A2,Lm22;
A6: N = Ker f by Th48;
  G'"\/"N = (H/\K) "\/" H' by Th86
    .= H'"\/"(H/\K);
  hence thesis by A3,A4,A5,A6,Th90;
end;

theorem Th92:
  for H,K,H',K' being strict StableSubgroup of G
  st H' is normal StableSubgroup of H & K' is normal StableSubgroup of K
  holds H'"\/"(H/\K') is normal StableSubgroup of H'"\/"(H/\K)
proof
  let H,K,H',K' be strict StableSubgroup of G;
  assume
A1: H' is normal StableSubgroup of H & K' is normal StableSubgroup of K;
  reconsider GG=H as GroupWithOperators of O;
  reconsider N'=H' as normal StableSubgroup of GG by A1;
  reconsider N=N' as StableSubgroup of GG;
  reconsider HH=GG./.N' as GroupWithOperators of O;
  reconsider f=nat_hom N' as Homomorphism of GG,HH;
  reconsider G'=H/\K as strict StableSubgroup of GG by Lm34;
  set L=H/\K';
A2: L is strict normal StableSubgroup of G' by A1,Th60;
  then reconsider L as strict StableSubgroup of GG by Th11;
A3: N = Ker f by Th48;
A4: L"\/"N = (H/\K')"\/"H' by Th86
    .= H'"\/"(H/\K');
  G'"\/"N = (H/\K)"\/"H' by Th86
    .= H'"\/"(H/\K);
  hence thesis
  by A2,A3,A4,Th90;
end;

theorem Th93:
  for H,K,H',K' being strict StableSubgroup of G,
  JH being normal StableSubgroup of H'"\/"(H/\K),
  JK being normal StableSubgroup of K'"\/"(K/\H)
  st JH = H'"\/"(H/\K') & JK= K'"\/"(K/\H') &
  H' is normal StableSubgroup of H & K' is normal StableSubgroup of K
  holds (H'"\/"(H/\K))./.JH, (K'"\/"(K/\H))./.JK are_isomorphic
proof
  let H,K,H',K' be strict StableSubgroup of G;
  let JH be normal StableSubgroup of H'"\/"(H/\K);
  let JK be normal StableSubgroup of K'"\/"(K/\H);
  assume
A1: JH = H'"\/"(H/\K') & JK= K'"\/"(K/\H');
  assume
A2: H' is normal StableSubgroup of H;
  assume
A3: K' is normal StableSubgroup of K;
  set HK=(H'/\K)"\/"(K'/\H);
  H'/\K is normal StableSubgroup of H/\K &
  K'/\H is normal StableSubgroup of H/\K by A2,A3,Th60;
  then reconsider HK as normal StableSubgroup of H/\K by Th87;
A4: (H'"\/"(H/\K))./.JH, (H/\K)./.HK are_isomorphic by A1,A2,A3,Th91;
  HK=(K'/\H)"\/"(H'/\K);
  then (K'"\/"(K/\H))./.JK, (H/\K)./.HK are_isomorphic by A1,A2,A3,Th91;
  hence thesis by A4,Th55;
end;

begin :: Composition Series

:: ALG I.4.7 Definition 9

definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be FinSequence of the_stable_subgroups_of G;
  attr IT is composition_series means
  :Def31:
  IT.1=(Omega).G & IT.(len IT)=(1).G &
  for i being Nat st i in dom IT & i+1 in dom IT
  for H1,H2 being StableSubgroup of G st H1=IT.i & H2=IT.(i+1)
  holds H2 is normal StableSubgroup of H1;
end;

registration
  let O be set;
  let G be GroupWithOperators of O;
  cluster composition_series FinSequence of the_stable_subgroups_of G;
  existence
  proof
    take H=<*(Omega).G,(1).G*>;
    (Omega).G is Element of the_stable_subgroups_of G &
    (1).G is Element of the_stable_subgroups_of G by Def11;
    then reconsider H as FinSequence of the_stable_subgroups_of G by
FINSEQ_2:15;
A1: H.1=(Omega).G by FINSEQ_1:61;
A2: H.(len H) = H.2 by FINSEQ_1:61
      .=(1).G by FINSEQ_1:61;
    for i being Nat st i in dom H & i+1 in dom H
    for H1,H2 being StableSubgroup of G st H1=H.i & H2=H.(i+1)
    holds H2 is normal StableSubgroup of H1
    proof
      let i be Nat;
      assume
A3:   i in dom H;
      assume
A4:   i+1 in dom H;
      len H = 2 by FINSEQ_1:61;
      then
A5:   dom H = {1,2} by FINSEQ_1:4,def 3;
      per cases by A3,A5,TARSKI:def 2;
      suppose
A6:     i=1;
        let H1,H2 be StableSubgroup of G;
        assume H1=H.i;
        assume H2=H.(i+1);
        then
A7:     H2=(1).G by A6,FINSEQ_1:61;
        then reconsider H2 as StableSubgroup of H1 by Th16;
        now
          let H be strict Subgroup of H1;
          assume the multMagma of H2 = H;
          then
A8:       the carrier of H = {1_G} by A7,Def8;
          reconsider H1 as Subgroup of G by Def7;
          the carrier of H = {1_H1} by A8,GROUP_2:53;
          then H = (1).H1 by GROUP_2:def 7;
          hence H is normal;
        end;
        hence thesis by Def10;
      end;
      suppose i=2;
        hence thesis by A4,A5,TARSKI:def 2;
      end;
    end;
    hence thesis by A1,A2,Def31;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  mode CompositionSeries of G is
    composition_series FinSequence of the_stable_subgroups_of G;
end;

:: ALG I.4.7 Definition 9

definition
  let O be set;
  let G be GroupWithOperators of O;
  let s1,s2 be CompositionSeries of G;
  pred s1 is_finer_than s2 means
  :Def32:
  ex x being set st x c= dom s1 & s2 = s1 * Sgm x;
  reflexivity
  proof
    now
      let s1 be CompositionSeries of G;
      set x=dom s1;
      reconsider x as set;
      take x;
      thus x c= dom s1;
      set i=len s1;
      Sgm x = Sgm Seg i by FINSEQ_1:def 3
        .= idseq i by FINSEQ_3:54;
      hence s1 = s1 * Sgm x by FINSEQ_2:64;
    end;
    hence thesis;
  end;
end;

definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be CompositionSeries of G;
  attr IT is strictly_decreasing means
  :Def33:
  for i being Nat st i in dom IT & i+1 in dom IT
  for H being StableSubgroup of G,
  N being normal StableSubgroup of H st H=IT.i & N=IT.(i+1)
  holds H./.N is not trivial;
end;

:: ALG I.4.7 Definition 10

definition
  let O be set;
  let G be GroupWithOperators of O;
  let IT be CompositionSeries of G;
  attr IT is jordan_holder means
  :Def34:
  IT is strictly_decreasing &
  not ex s being CompositionSeries of G
  st s<>IT & s is strictly_decreasing & s is_finer_than IT;
end;

:: ALG I.4.7 Definition 9

definition
  let O be set;
  let G1,G2 be GroupWithOperators of O;
  let s1 be CompositionSeries of G1;
  let s2 be CompositionSeries of G2;
  pred s1 is_equivalent_with s2 means
  :Def35:
  len s1 = len s2 & for n being Nat st n + 1 = len s1 holds
  ex p being Permutation of Seg n st
  for H1 being StableSubgroup of G1, H2 being StableSubgroup of G2,
  N1 being normal StableSubgroup of H1,
  N2 being normal StableSubgroup of H2, i,j being Nat
  st 1<=i & i<=n & j=p.i & H1=s1.i & H2=s2.j & N1=s1.(i+1) & N2=s2.(j+1)
  holds H1./.N1,H2./.N2 are_isomorphic;
end;

:: ALG I.4.7 Definition 9

definition
  let O be set;
  let G be GroupWithOperators of O;
  let s be CompositionSeries of G;
  func the_series_of_quotients_of s -> FinSequence means
  :Def36:
  len s = len it + 1 & for i being Nat st i in dom it
  for H being StableSubgroup of G, N being normal StableSubgroup of H
  st H=s.i & N=s.(i+1) holds it.i = H./.N if len s > 1 otherwise it={};
  existence
  proof
    now
      assume len s > 1;
      then
A1:   len s - 1 > 1 - 1 by XREAL_1:11;
      set i=len s - 1;
      reconsider i as Element of NAT by A1,INT_1:16;
      defpred P[set,set] means
      for H being StableSubgroup of G, N being normal StableSubgroup of H,
      j being Nat st $1 in Seg i & j=$1 & H=s.j & N=s.(j+1) holds $2 = H./.N;
A9:   for k being Nat st k in Seg i ex x being set st P[k,x]
      proof
        let k be Nat;
        assume k in Seg i;
        then
A10:    1<=k & k<=i by FINSEQ_1:3;
        then
A11:    k+1<=len s - 1 + 1 by XREAL_1:8;
        1+1<=k+1 by A10,XREAL_1:8;
        then
A12:    1<=k+1 by XXREAL_0:2;
        reconsider k1=k as Element of NAT by ORDINAL1:def 13;
        0+k<=1+k by XREAL_1:8;
        then k<=len s by A11,XXREAL_0:2;
        then k1 in Seg len s & k1+1 in Seg len s by A10,A11,A12;
        then
A13:    k in dom s & k+1 in dom s by FINSEQ_1:def 3;
        then reconsider H=s.k, N=s.(k+1) as
        Element of the_stable_subgroups_of G by FINSEQ_2:13;
        reconsider H,N as StableSubgroup of G by Def11;
        reconsider N as normal StableSubgroup of H by A13,Def31;
        take H./.N;
        thus thesis;
      end;
      consider f be FinSequence such that
A14:  dom f = Seg i & for k being Nat st k in Seg i holds P[k,f.k]
      from FINSEQ_1:sch 1(A9);
      take f;
      len f = i by A14,FINSEQ_1:def 3;
      hence len s = len f + 1;
      let j be Nat;
      assume
A15:  j in dom f;
      let H be StableSubgroup of G;
      let N be normal StableSubgroup of H;
      assume
A16:  H=s.j;
      assume N=s.(j+1);
      hence f.j = H./.N by A14,A15,A16;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let f1,f2 be FinSequence;
    now
      assume len s > 1;
      set i=len s - 1;
      assume
A18:  len s = len f1 + 1;
      assume
A19:  for i being Nat st i in dom f1 for H1 being StableSubgroup of G,
      N1 being normal StableSubgroup of H1
      st H1=s.i & N1=s.(i+1) holds f1.i = H1./.N1;
      assume
A20:  len s = len f2 + 1;
      assume
A21:  for i being Nat st i in dom f2 for H1 being StableSubgroup of G,
      N1 being normal StableSubgroup of H1
      st H1=s.i & N1=s.(i+1) holds f2.i = H1./.N1;
      for k being Nat st 1 <=k & k <= len f1 holds f1.k=f2.k
      proof
        let k be Nat;
        reconsider k1 = k as Element of NAT by ORDINAL1:def 13;
        assume
A22:    1 <=k & k <= len f1;
        then
A23:    k1 in Seg len f1;
        then
A24:    k in dom f1 by FINSEQ_1:def 3;
A25:    k in dom f2 by A18,A20,A23,FINSEQ_1:def 3;
A26:    k+1<=len s - 1 + 1 by A18,A22,XREAL_1:8;
        1+1<=k+1 by A22,XREAL_1:8;
        then
A27:    1<=k+1 by XXREAL_0:2;
        0+k<=1+k by XREAL_1:8;
        then k<=len s by A26,XXREAL_0:2;
        then k1 in Seg len s & k1+1 in Seg len s by A22,A26,A27;
        then
A28:    k in dom s & k+1 in dom s by FINSEQ_1:def 3;
        then reconsider H1=s.k,N1=s.(k+1) as
        Element of the_stable_subgroups_of G by FINSEQ_2:13;
        reconsider H1,N1 as StableSubgroup of G by Def11;
        reconsider N1 as normal StableSubgroup of H1 by A28,Def31;
        f1.k=H1./.N1 by A19,A24;
        hence thesis by A21,A25;
      end;
      hence f1=f2 by A18,A20,FINSEQ_1:18;
    end;
    hence thesis;
  end;
  consistency;
end;

definition
  let O be set;
  let f1,f2 be FinSequence;
  let p be Permutation of dom f1;
  pred f1,f2 are_equivalent_under p,O means
  :Def37:
  len f1 = len f2 &
  for H1,H2 being GroupWithOperators of O, i,j being Nat
  st i in dom f1 & j=p".i & H1=f1.i & H2=f2.j holds H1,H2 are_isomorphic;
end;

reserve y for set,
  H1',H2' for StableSubgroup of G,
  N1' for normal StableSubgroup of H1',
  s1,s1',s2,s2' for CompositionSeries of G,
  fs for FinSequence of the_stable_subgroups_of G,
  f1,f2 for FinSequence,
  i,j,n for Nat;

theorem Th94:
  i in dom s1 & i+1 in dom s1 & s1.i=s1.(i+1) & fs=Del(s1,i)
  implies fs is composition_series
proof
  assume
A1: i in dom s1;
  assume
A2: i+1 in dom s1;
  assume
A3: s1.i=s1.(i+1);
  assume
A4: fs = Del(s1,i);
A5: i in Seg len s1 by A1,FINSEQ_1:def 3;
  then
A6: 1<=i & i<=len s1 by FINSEQ_1:3;
  then
A7: 1+1<=i+1 by XREAL_1:8;
  i+1 in Seg len s1 by A2,FINSEQ_1:def 3;
  then
A8: 1<=i+1 & i+1<=len s1 by FINSEQ_1:3;
  consider k be Nat such that
A9: len s1 = k + 1 & len Del(s1,i) = k by A1,FINSEQ_3:113;
  1+1 <= len fs+1 by A4,A7,A8,A9,XXREAL_0:2;
  then
A10: 1 <= len fs by XREAL_1:8;
  per cases by A10,XXREAL_0:1;
  suppose
A11: len fs = 1;
    then
A12: 1<=i & i<=1 by A4,A5,A8,A9,FINSEQ_1:3,XREAL_1:8;
    then
A13: i=1 by XXREAL_0:1;
    dom s1 = Seg 2 by A4,A9,A11,FINSEQ_1:def 3;
    then
A14: 1 in dom s1;
A15: s1.1=(Omega).G & s1.2=(1).G by A4,A9,A11,Def31;
    len s1 = len fs+1 & i in dom s1 & i<=1 & 1<=len fs
    by A4,A9,A11,A12,A14,XXREAL_0:1;
    then
A16: fs.(len fs) = s1.(1+1) by A4,A11,FINSEQ_3:120
      .= (1).G by A4,A9,A11,Def31;
    now
      let n be Nat;
      assume n in dom fs;
      then n in Seg 1 by A11,FINSEQ_1:def 3;
      then
A17:  n=1 by FINSEQ_1:4,TARSKI:def 1;
      assume n+1 in dom fs;
      then
A18:  2 in Seg 1 by A11,A17,FINSEQ_1:def 3;
      let H1,H2;
      assume H1=fs.n & H2=fs.(n+1);
      thus H2 is normal StableSubgroup of H1 by A18,FINSEQ_1:4,TARSKI:def 1;
    end;
    hence thesis by A3,A11,A13,A15,A16,Def31;
  end;
  suppose
A19: len fs > 1;
A20: fs.1=(Omega).G
    proof
      per cases by A6,XXREAL_0:1;
      suppose
A21:    i=1;
        then fs.1 = s1.(1+1) by A1,A4,A9,A19,FINSEQ_3:120;
        hence thesis by A3,A21,Def31;
      end;
      suppose
A22:    i>1;
        reconsider i as Element of NAT by INT_1:16;
        fs.1 = Del(s1,i).1 by A4
          .= s1.1 by A9,A22,FINSEQ_3:119;
        hence thesis by Def31;
      end;
    end;
    i<=len fs by A4,A8,A9,XREAL_1:8;
    then fs.(len fs) = s1.(len s1) by A1,A4,A9,FINSEQ_3:120;
    then
A23: fs.(len fs)=(1).G by Def31;
    now
      let n be Nat;
      assume n in dom fs & n+1 in dom fs;
      then
A24:  n in Seg len fs & n+1 in Seg len fs by FINSEQ_1:def 3;
A25:  0+len fs < 1+len fs by XREAL_1:8;
      then
A26:  Seg len fs c= Seg len s1 by A4,A9,FINSEQ_1:7;
      let H1, H2;
      assume
A27:  H1 = fs.n;
      assume
A28:  H2 = fs.(n+1);
      reconsider n1=n+1 as Nat;
      n1 <= len fs by A24,FINSEQ_1:3;
      then n1 < len s1 by A4,A9,A25,XXREAL_0:2;
      then
A29:  n1 + 1 <= k + 1 by A9,NAT_1:13;
A30:  n1 <= k by A4,A9,A24,FINSEQ_1:3;
      n in Seg len s1 by A24,A26;
      then
A31:  n in dom s1 by FINSEQ_1:def 3;
      n1 in Seg len s1 by A24,A26;
      then
A32:  n1 in dom s1 by FINSEQ_1:def 3;
      Seg(n1+1) c= Seg len s1 by A9,A29,FINSEQ_1:7;
      then
A33:  Seg(n1+1) c= dom s1 by FINSEQ_1:def 3;
A34:  n1+1 in Seg(n1+1) by FINSEQ_1:6;
A35:  n <= k by A4,A9,A24,FINSEQ_1:3;
      reconsider i,n as Nat;
      per cases;
      suppose
A36:    n<i;
        then
A37:    n1<=i by NAT_1:13;
        reconsider n'=n,i as Element of NAT by INT_1:16;
A38:    Del(s1,i).n' = s1.n' by A9,A36,FINSEQ_3:119;
        per cases by A37,XXREAL_0:1;
        suppose
A39:      n1<i;
          reconsider n1'=n1,i as Element of NAT;
          Del(s1,i).n1' = s1.n1' by A9,A39,FINSEQ_3:119;
          hence H2 is normal StableSubgroup of H1 by A4,A27,A28,A31,A32,A38
,Def31;
        end;
        suppose
A40:      n1=i;
          then Del(s1,i).n1 = s1.(n1+1) by A1,A9,A30,FINSEQ_3:120;
          hence H2 is normal StableSubgroup of H1
          by A3,A4,A27,A28,A31,A32,A38,A40,Def31;
        end;
      end;
      suppose
A41:    n>=i;
        reconsider n'=n,i as Element of NAT by INT_1:16;
A42:    Del(s1,i).n' = s1.(n'+1) by A1,A9,A35,A41,FINSEQ_3:120;
        reconsider n1'=n1,i,k as Element of NAT by INT_1:16;
        0+n<=n+1 by XREAL_1:8;
        then len s1 = k+1 & i in dom s1 & i<=n1' & n1'<=k
        by A1,A4,A9,A24,A41,FINSEQ_1:3,XXREAL_0:2;
        then Del(s1,i).n1' = s1.(n1'+1) by FINSEQ_3:120;
        hence H2 is normal StableSubgroup of H1
        by A4,A27,A28,A32,A33,A34,A42,Def31;
      end;
    end;
    hence thesis by A20,A23,Def31;
  end;
end;

theorem Th95:
  s1 is_finer_than s2 implies ex n st len s1 = len s2 + n
proof
  assume s1 is_finer_than s2;
  then consider x such that
A1: x c= dom s1 & s2 = s1 * Sgm x by Def32;
A2: x c= Seg len s1 by A1,FINSEQ_1:def 3;
  reconsider x as finite set by A1;
  now
    let y1 be set;
    assume y1 in dom s2;
    then y1 in dom Sgm x & (Sgm x).y1 in dom s1 by A1,FUNCT_1:21;
    then
A3: y1 in Seg card x by A2,FINSEQ_3:45;
    card x <= card dom s1 by A1,NAT_1:44;
    then Seg card x c= Seg card dom s1 by FINSEQ_1:7;
    then y1 in Seg card dom s1 by A3;
    then y1 in Seg card Seg len s1 by FINSEQ_1:def 3;
    then y1 in Seg len s1 by FINSEQ_1:78;
    hence y1 in dom s1 by FINSEQ_1:def 3;
  end;
  then dom s2 c= dom s1 by TARSKI:def 3;
  then Seg len s2 c= dom s1 by FINSEQ_1:def 3;
  then Seg len s2 c= Seg len s1 by FINSEQ_1:def 3;
  then len s2 <= len s1 by FINSEQ_1:7;
  then
A4: len s2 - len s2 <= len s1 - len s2 by XREAL_1:11;
  set n=len s1 - len s2;
  n in NAT by A4,INT_1:16;
  then reconsider n as Nat;
  take n;
  thus thesis;
end;

theorem Th96:
  len s2 = len s1 & s2 is_finer_than s1 implies s1 = s2
proof
  assume
A1: len s2 = len s1;
  assume s2 is_finer_than s1;
  then consider x such that
A2: x c= dom s2 & s1 = s2 * Sgm x by Def32;
A3: dom s1 = Seg len s2 by A1,FINSEQ_1:def 3
    .= dom s2 by FINSEQ_1:def 3;
A4: x c= Seg len s2 by A2,FINSEQ_1:def 3;
  then x = rng Sgm x by FINSEQ_1:def 13;
  then
A5: dom(s2 * Sgm x)=dom Sgm x by A2,RELAT_1:46;
  reconsider X = Seg len s2 as finite set;
  set y = X \ x;
  reconsider x,y as finite set by A2;
A6: X = X \/ x by A4,XBOOLE_1:12
    .= x \/ y by XBOOLE_1:39;
  card(x \/ y) = (card x) + (card y) by CARD_2:53,XBOOLE_1:79;
  then
A7: len s2 = (card x) + (card y) by A6,FINSEQ_1:78;
  dom Sgm x = Seg len s2 by A2,A3,A5,FINSEQ_1:def 3;
  then len Sgm x = len s2 by FINSEQ_1:def 3;
  then card x = len s2 by A4,FINSEQ_3:44;
  then y = {} by A7;
  then Sgm x = idseq len s2 by A6,FINSEQ_3:54;
  hence thesis by A2,FINSEQ_2:64;
end;

theorem Th97:
  s1 is not empty & s2 is_finer_than s1 implies s2 is not empty
proof
  assume
A1: s1 is not empty;
  assume s2 is_finer_than s1;
  then consider i such that
A3: len s2 = len s1 + i by Th95;
  s2 is non empty by A1,A3;
  hence thesis;
end;

theorem Th98:
  s1 is_finer_than s2 & s1 is jordan_holder & s2 is jordan_holder implies s1=s2
proof
  assume
A1: s1 is_finer_than s2;
  assume
A2: s1 is jordan_holder;
  assume
A3: s2 is jordan_holder & s1 <> s2;
  s1 is strictly_decreasing & not ex s2 st s1<>s2 & s2 is strictly_decreasing
  & s2 is_finer_than s1 by A2,Def34;
  hence contradiction by A1,A3,Def34;
end;

Lm36: for P,R being Relation holds P = (rng P)|R iff P~ = (R~)|(dom (P~))
proof
  let P,R be Relation;
  hereby
    assume
A1: P = (rng P)|R;
    now
      let x,y be set;
      hereby
        assume
A2:     [x,y] in P~;
        then [y,x] in P by RELAT_1:def 7;
        then x in rng P & [y,x] in R by A1,RELAT_1:def 12;
        then x in dom(P~) & [x,y] in R~ by A2,RELAT_1:def 4,def 7;
        hence [x,y] in R~|(dom (P~)) by RELAT_1:def 11;
      end;
      assume [x,y] in R~|(dom (P~));
      then x in dom(P~) & [x,y] in R~ by RELAT_1:def 11;
      then x in rng P & [y,x] in R by RELAT_1:37,def 7;
      then [y,x] in (rng P)|R by RELAT_1:def 12;
      hence [x,y] in P~ by A1,RELAT_1:def 7;
    end;
    hence P~ = (R~)|(dom (P~)) by RELAT_1:def 2;
  end;
  assume
A3: P~ = (R~)|(dom (P~));
  now
    let x,y be set;
    hereby
      assume [x,y] in P;
      then [y,x] in P~ by RELAT_1:def 7;
      then y in dom(P~) & [y,x] in R~ by A3,RELAT_1:def 11;
      then y in rng P & [x,y] in R by RELAT_1:37,def 7;
      hence [x,y] in (rng P)|R by RELAT_1:def 12;
    end;
    assume [x,y] in (rng P)|R;
    then y in rng P & [x,y] in R by RELAT_1:def 12;
    then y in dom(P~) & [y,x] in R~ by RELAT_1:37,def 7;
    then [y,x] in R~|(dom (P~)) by RELAT_1:def 11;
    hence [x,y] in P by A3,RELAT_1:def 7;
  end;
  hence thesis by RELAT_1:def 2;
end;

Lm37: for X being set, P,R being Relation holds P*(R|X) = (X|P)*R
proof
  let X be set;
  let P,R be Relation;
A1: now
    let x be set;
    assume
A2: x in P*(R|X);
    then consider y,z be set such that
A3: x = [y,z] by RELAT_1:def 1;
    consider w be set such that
A4: [y,w] in P & [w,z] in R|X by A2,A3,RELAT_1:def 8;
A5: w in X & [w,z] in R by A4,RELAT_1:def 11;
    then [y,w] in X|P by A4,RELAT_1:def 12;
    hence x in (X|P)*R by A3,A5,RELAT_1:def 8;
  end;
  now
    let x be set;
    assume
A6: x in (X|P)*R;
    then consider y,z be set such that
A7: x = [y,z] by RELAT_1:def 1;
    consider w be set such that
A8: [y,w] in X|P & [w,z] in R by A6,A7,RELAT_1:def 8;
A9: w in X & [y,w] in P by A8,RELAT_1:def 12;
    then [w,z] in R|X by A8,RELAT_1:def 11;
    hence x in P*(R|X) by A7,A9,RELAT_1:def 8;
  end;
  hence thesis by A1,TARSKI:2;
end;

Lm38: for n being Nat, X being set, f being PartFunc of REAL, REAL
st X c= Seg n & X c= dom f & f|X is increasing & f.:X c= NAT \ {0}
holds Sgm(f.:X) = f * Sgm X
proof
  let n be Nat;
  let X be set;
  let f be PartFunc of REAL, REAL;
  assume
A1: X c= Seg n;
  assume
A2: X c= dom f;
  assume
A3: f|X is increasing;
  assume
A4: f.:X c= NAT \ {0};
A5: rng Sgm X = X by A1,FINSEQ_1:def 13;
  per cases;
  suppose
A6: X misses dom f;
    then f.:X = {} by RELAT_1:151;
    then Sgm(f.:X) = {} by FINSEQ_1:4,72;
    hence thesis by A5,A6,RELAT_1:67;
  end;
  suppose
A7: X meets dom f;
    set fs = f * Sgm X;
    set fX = f.:X;
    rng Sgm X c= dom f by A1,A2,FINSEQ_1:def 13;
    then reconsider fs as FinSequence by FINSEQ_1:20;
    f.:(rng Sgm X) c= NAT \ {0} by A1,A4,FINSEQ_1:def 13;
    then
A8: rng fs c= NAT \ {0} by RELAT_1:160;
 NAT \ {0} c= NAT by XBOOLE_1:36;
    then rng fs c= NAT by A8,XBOOLE_1:1;
    then reconsider fs as FinSequence of NAT by FINSEQ_1:def 4;
    reconsider f'=f as Function;
    reconsider X'=X as finite set by A1;
    f'.:X' is finite;
    then reconsider fX as finite non empty real-membered set by A7,RELAT_1:151;
    set k = max fX;
    k in fX by XXREAL_2:def 8;
    then k in NAT \ {0} by A4;
    then reconsider k as Nat;
    now
      let x be set;
      assume
A10:  x in f.:X;
      then x in NAT \ {0} by A4;
      then reconsider k'=x as Nat;
A11:  k' <= k by A10,XXREAL_2:def 8;
      not k' in {0} by A4,A10,XBOOLE_0:def 5;
      then k' <> 0 by TARSKI:def 1;
      then 0 < k';
      then 0+1 < k'+1 by XREAL_1:8;
      then 1 <= k' by NAT_1:13;
      hence x in Seg k by A11,FINSEQ_1:3;
    end;
    then
A12: f.:X c= Seg k by TARSKI:def 3;
A13: rng fs = f.:X by A5,RELAT_1:160;
    now
      let l,m,k1,k2 be Nat;
      assume
A14:  1 <= l & l < m & m <= len fs;
      assume
A15:  k1 = fs.l & k2 = fs.m;
A16:  dom fs = Seg len fs by FINSEQ_1:def 3;
      1 <= l & l <= len fs by A14,XXREAL_0:2;
      then
A17:  l in dom fs by A16,FINSEQ_1:3;
      then
A18:  l in dom Sgm X & (Sgm X).l in dom f by FUNCT_1:21;
      1 <= m & m <= len fs by A14,XXREAL_0:2;
      then
A19:  m in dom fs by A16,FINSEQ_1:3;
      then
A20:  m in dom Sgm X & (Sgm X).m in dom f by FUNCT_1:21;
A21:  k1 = f.((Sgm X).l) & k2 = f.((Sgm X).m) by A15,A17,A19,FUNCT_1:22;
      set k1'=(Sgm X).l;
      set k2'=(Sgm X).m;
A22:  k1' in X & k2' in X by A5,A18,A20,FUNCT_1:12;
      reconsider k1',k2' as Nat;
      m in Seg len Sgm X by A20,FINSEQ_1:def 3;
      then 1 <= l & l < m & m <= len Sgm X by A14,FINSEQ_1:3;
      then
A23:  k1' < k2' by A1,FINSEQ_1:def 13;
      reconsider k1',k2' as Element of NAT by ORDINAL1:def 13;
      reconsider k1',k2' as Element of REAL;
      k1' in X /\ dom f & k2' in X /\ dom f by A18,A20,A22,XBOOLE_0:def 4;
      hence k1 < k2 by A3,A21,A23,RFUNCT_2:43;
    end;
    hence thesis by A12,A13,FINSEQ_1:def 13;
  end;
end;

Lm39: y c= Seg(n+1) & i in Seg(n+1) & not i in y
implies ex x st Sgm x = Sgm(Seg(n+1)\{i})" * Sgm y & x c= Seg n
proof
  assume
A1: y c= Seg(n+1);
  assume
A2: i in Seg(n+1);
  then
A3: 1<=i & i<=n+1 by FINSEQ_1:3;
  assume
A4: not i in y;
  set x1 = {k where k is Element of NAT: k in y & k<i};
  set x2 = {k-1 where k is Element of NAT: k in y & k>i};
  set x = x1 \/ x2;
  take x;
A5: now
    let z be set;
    assume
A6: z in x;
    per cases by A6,XBOOLE_0:def 3;
    suppose z in x1;
      then consider k be Element of NAT such that
A7:   k = z & k in y & k < i;
      reconsider z'=z as Element of NAT by A7;
A8:   1<=z' & z'<=n+1 by A1,A7,FINSEQ_1:3;
      z'<n+1 by A3,A7,XXREAL_0:2;
      then z'<=n by NAT_1:13;
      hence z in Seg n by A8;
    end;
    suppose z in x2;
      then consider k be Element of NAT such that
A9:   k-1 = z & k in y & k > i;
      1<=k & k<=n+1 by A1,A9,FINSEQ_1:3;
      then
A10:  k-1<=n+1-1 by XREAL_1:11;
      reconsider z'=z as integer number by A9;
      1<k by A3,A9,XXREAL_0:2;
      then 1+1<k+1 by XREAL_1:8;
      then 2<=k by NAT_1:13;
      then
A11:  2-1<=k-1 by XREAL_1:11;
      then reconsider z' as Element of NAT by A9,INT_1:16;
      z'<=n by A9,A10;
      hence z in Seg n by A9,A11;
    end;
  end;
  then
A12: x c= Seg n by TARSKI:def 3;
  then reconsider x'=x,y'=y as finite set by A1;
  set f1 = id x1;
  set f2 = {[k-1,k] where k is Element of NAT:k in y' & k>i};
  now
    let x be set;
    assume x in f2;
    then consider k be Element of NAT such that
A15: [k-1,k] = x & k in y' & k > i;
    reconsider y=k-1,z=k as set;
    take y,z;
    thus x = [y,z] by A15;
  end;
  then reconsider f2 as Relation by RELAT_1:def 1;
  set f = f1 \/ f2;
A16: dom f1 = x1 by RELAT_1:71;
A17: now
    let x be set;
    assume x in dom f2;
    then consider y be set such that
A18: [x,y] in f2 by RELAT_1:def 4;
    consider k be Element of NAT such that
A19: [k-1,k] = [x,y] & k in y' & k > i by A18;
    k-1 = x by A19,ZFMISC_1:33;
    hence x in x2 by A19;
  end;
  now
    let x be set;
    assume x in x2;
    then consider k be Element of NAT such that
A20: k-1 = x & k in y' & k > i;
    reconsider y=k as set;
    [x,y] in f2 by A20;
    hence x in dom f2 by RELAT_1:def 4;
  end;
  then
A21: dom f2 = x2 by A17,TARSKI:2;
  now
    let x,y1,y2 be set;
    assume
A22: [x,y1] in f;
    assume
A23: [x,y2] in f;
    per cases by A22,XBOOLE_0:def 3;
    suppose
A24:  [x,y1] in f1;
      then
A25:  x in dom f1 by RELAT_1:def 4;
      then f1.x = x by FUNCT_1:34;
      then
A26:  y1 = x by A24,A25,FUNCT_1:def 4;
      per cases by A23,XBOOLE_0:def 3;
      suppose
A27:    [x,y2] in f1;
        then
A28:    x in dom f1 by RELAT_1:def 4;
        then f1.x = x by FUNCT_1:34;
        hence y1 = y2 by A26,A27,A28,FUNCT_1:def 4;
      end;
      suppose [x,y2] in f2;
        then x in x2 by A21,RELAT_1:def 4;
        then consider k be Element of NAT such that
A29:    k-1=x & k in y & k>i;
        x in x1 by A25;
        then consider k' be Element of NAT such that
A30:    k'=x & k' in y & k'<i;
        k'+1>i by A29,A30;
        hence y1 = y2 by A30,NAT_1:13;
      end;
    end;
    suppose [x,y1] in f2;
      then consider k be Element of NAT such that
A31:  [k-1,k] = [x,y1] & k in y' & k > i;
A32:  k-1=x & k=y1 & k in y & k>i by A31,ZFMISC_1:33;
      per cases by A23,XBOOLE_0:def 3;
      suppose [x,y2] in f1;
        then x in dom f1 by RELAT_1:def 4;
        then x in x1;
        then consider k' be Element of NAT such that
A33:    k'=x & k' in y & k'<i;
        k'=k-1 by A31,A33,ZFMISC_1:33;
        then k'+1>i by A31;
        hence y1 = y2 by A33,NAT_1:13;
      end;
      suppose [x,y2] in f2;
        then consider k' be Element of NAT such that
A34:    [k'-1,k'] = [x,y2] & k' in y' & k' > i;
        k'-1=x & k'=y2 by A34,ZFMISC_1:33;
        hence y1 = y2 by A32;
      end;
    end;
  end;
  then reconsider f as Function by FUNCT_1:def 1;
A35: now
    let x be set;
    assume
A36: x in dom f1;
    then
A37: [x,f1.x] in f1 by FUNCT_1:def 4;
    dom f = dom f1 \/ dom f2 by RELAT_1:13;
    then
A38: dom f1 c= dom f by XBOOLE_1:7;
    f1 c= f by XBOOLE_1:7;
    hence f.x = f1.x by A36,A37,A38,FUNCT_1:def 4;
  end;
  now
    let x,y1,y2 be set;
    assume [x,y1] in f2;
    then consider k be Element of NAT such that
A39: [k-1,k] = [x,y1] & k in y' & k > i;
    assume [x,y2] in f2;
    then consider k' be Element of NAT such that
A40: [k'-1,k'] = [x,y2] & k' in y' & k' > i;
A41: k-1 = x & k = y1 by A39,ZFMISC_1:33;
    k'-1 = x & k' = y2 by A40,ZFMISC_1:33;
    hence y1 = y2 by A41;
  end;
  then reconsider f2 as Function by FUNCT_1:def 1;
A42: now
    let x be set;
    assume
A43: x in dom f2;
    then
A44: [x,f2.x] in f2 by FUNCT_1:def 4;
    dom f = dom f1 \/ dom f2 by RELAT_1:13;
    then
A45: dom f2 c= dom f by XBOOLE_1:7;
    f2 c= f by XBOOLE_1:7;
    hence f.x = f2.x by A43,A44,A45,FUNCT_1:def 4;
  end;
  now
    let y1,y2 be set;
    assume y1 in dom f;
    then
A46: y1 in dom f1 \/ dom f2 by RELAT_1:13;
    assume y2 in dom f;
    then
A47: y2 in dom f1 \/ dom f2 by RELAT_1:13;
    assume
A48: f.y1 = f.y2;
    per cases by A46,XBOOLE_0:def 3;
    suppose
A49:  y1 in dom f1;
      then
A50:  f1.y1 = y1 by FUNCT_1:34;
      then
A51:  f.y1 = y1 by A35,A49;
      per cases by A47,XBOOLE_0:def 3;
      suppose
A52:    y2 in dom f1;
        then f1.y2 = y2 by FUNCT_1:34;
        hence y1 = y2 by A35,A48,A51,A52;
      end;
      suppose
A53:    y2 in dom f2;
        then f.y2 = f2.y2 by A42;
        then [y2,f.y2] in f2 by A53,FUNCT_1:def 4;
        then consider k be Element of NAT such that
A54:    [k-1,k] = [y2,f.y2] & k in y' & k>i;
        f.y1 = f1.y1 by A35,A49;
        then f.y1 in x1 by A49,A50;
        then consider k' be Element of NAT such that
A55:    k'=f.y1 & k' in y & k'<i;
        thus y1 = y2 by A48,A54,A55,ZFMISC_1:33;
      end;
    end;
    suppose
A56:  y1 in dom f2;
      then f.y1 = f2.y1 by A42;
      then [y1,f.y1] in f2 by A56,FUNCT_1:def 4;
      then consider k be Element of NAT such that
A57:  [k-1,k] = [y1,f.y1] & k in y' & k>i;
A58:  k = f.y1 & k-1 = y1 by A57,ZFMISC_1:33;
      per cases by A47,XBOOLE_0:def 3;
      suppose
A59:    y2 in dom f1;
        then f1.y2 = y2 by FUNCT_1:34;
        then f.y2 in dom f1 by A35,A59;
        then f.y2 in x1;
        then consider k' be Element of NAT such that
A60:    k'=f.y2 & k' in y & k'<i;
        thus y1 = y2 by A48,A57,A60,ZFMISC_1:33;
      end;
      suppose
A61:    y2 in dom f2;
        then f.y2 = f2.y2 by A42;
        then [y2,f.y2] in f2 by A61,FUNCT_1:def 4;
        then consider k be Element of NAT such that
A62:    [k-1,k] = [y2,f.y2] & k in y' & k>i;
        k = f.y2 & k-1 = y2 by A62,ZFMISC_1:33;
        hence y1 = y2 by A48,A58;
      end;
    end;
  end;
  then
A63: f is one-to-one by FUNCT_1:def 8;
A64: dom f = x' by A16,A21,RELAT_1:13;
A65: now
    let z be set;
    assume z in rng f;
    then
A66: z in (rng f1 \/ rng f2) by RELAT_1:26;
    per cases by A66,XBOOLE_0:def 3;
    suppose z in rng f1;
      then z in x1;
      then consider k be Element of NAT such that
A67:  k = z & k in y & k < i;
      thus z in y' by A67;
    end;
    suppose z in rng f2;
      then consider x'' be set such that
A68:  [x'',z] in f2 by RELAT_1:def 5;
      consider k be Element of NAT such that
A69:  [k-1,k] = [x'',z] & k in y' & k > i by A68;
      thus z in y' by A69,ZFMISC_1:33;
    end;
  end;
  now
    let z be set;
    assume
A70: z in y';
    set k=z;
    k in Seg(n+1) by A1,A70;
    then reconsider k as Element of NAT;
    per cases;
    suppose k <= i;
      then k = z & k in y & k < i by A4,A70,XXREAL_0:1;
      then z in x1;
      then z in rng f1 by RELAT_1:71;
      then z in (rng f1 \/ rng f2) by XBOOLE_0:def 3;
      hence z in rng f by RELAT_1:26;
    end;
    suppose
A71:  k > i;
      set x''=k-1;
      [x'',z] in f2 by A70,A71;
      then z in rng f2 by RELAT_1:def 5;
      then z in (rng f1 \/ rng f2) by XBOOLE_0:def 3;
      hence z in rng f by RELAT_1:26;
    end;
  end;
  then
A72: rng f = y' by A65,TARSKI:2;
A73: y = rng Sgm y by A1,FINSEQ_1:def 13;
  reconsider g=f" as PartFunc of dom(f"), rng(f") by RELSET_1:11;
  dom f c= NAT & rng f c= NAT by A1,A12,A64,A72,XBOOLE_1:1;
  then dom g c= NAT & rng g c= NAT by A63,FUNCT_1:55;
  then dom g c= REAL & rng g c= REAL by XBOOLE_1:1;
  then reconsider g as PartFunc of REAL,REAL by RELSET_1:17;
A74: dom(f") = y & rng(f") = x by A63,A64,A72,FUNCT_1:55;
  then
A75: x = (f").: y by RELAT_1:146;
  now
    let r1,r2 be Element of REAL;
    assume r1 in y /\ dom g;
    then
A76: [r1,g.r1] in g by A74,FUNCT_1:8;
    assume r2 in y /\ dom g;
    then
A77: [r2,g.r2] in g by A74,FUNCT_1:8;
    assume
A78: r1 < r2;
A79: g = (f1 \/ f2)~ by A63,FUNCT_1:def 9
      .= f1~ \/ f2~ by RELAT_1:40;
    per cases by A76,A79,XBOOLE_0:def 3;
    suppose [r1,g.r1] in f1~;
      then [r1,g.r1] in id x1 by RELAT_1:72;
      then
A80:  r1 in x1 & r1=g.r1 by RELAT_1:def 10;
      then consider k' be Element of NAT such that
A81:  g.r1=k' & k' in y & k'<i;
      per cases by A77,A79,XBOOLE_0:def 3;
      suppose [r2,g.r2] in f1~;
        then [r2,g.r2] in id x1 by RELAT_1:72;
        hence g.r1 < g.r2 by A78,A80,RELAT_1:def 10;
      end;
      suppose [r2,g.r2] in f2~;
        then [g.r2,r2] in f2 by RELAT_1:def 7;
        then consider k'' be Element of NAT such that
A82:    [k''-1,k'']=[g.r2,r2] & k'' in y' & k''>i;
        k''-1=g.r2 by A82,ZFMISC_1:33;
        then
A83:    i-1 < g.r2 by A82,XREAL_1:11;
        reconsider k'''=g.r2,i'=i-1 as integer number by A82,ZFMISC_1:33;
        i'+1<=k''' by A83,INT_1:20;
        hence g.r1 < g.r2 by A81,XXREAL_0:2;
      end;
    end;
    suppose [r1,g.r1] in f2~;
      then [g.r1,r1] in f2 by RELAT_1:def 7;
      then consider k' be Element of NAT such that
A84:  [k'-1,k']=[g.r1,r1] & k' in y' & k'>i;
A85:  k'-1=g.r1 & r1=k' by A84,ZFMISC_1:33;
      per cases by A77,A79,XBOOLE_0:def 3;
      suppose [r2,g.r2] in f1~;
        then [r2,g.r2] in id x1 by RELAT_1:72;
        then r2 in x1 & r2=g.r2 by RELAT_1:def 10;
        then consider k'' be Element of NAT such that
A86:    r2=k'' & k'' in y & k''<i;
        thus g.r1 < g.r2 by A78,A84,A85,A86,XXREAL_0:2;
      end;
      suppose [r2,g.r2] in f2~;
        then [g.r2,r2] in f2 by RELAT_1:def 7;
        then consider k'' be Element of NAT such that
A87:    [k''-1,k'']=[g.r2,r2] & k'' in y' & k''>i;
        k''-1=g.r2 & r2=k'' by A87,ZFMISC_1:33;
        hence g.r1 < g.r2 by A78,A85,XREAL_1:11;
      end;
    end;
  end;
  then
A88: g|y is increasing by RFUNCT_2:43;
  now
    let x' be set;
    assume
A89: x' in g.:y;
    then
A90: x' in Seg n by A5,A75;
    not x'=0 by A12,A75,A89,FINSEQ_1:3;
    then not x' in {0} by TARSKI:def 1;
    hence x' in NAT \ {0} by A90,XBOOLE_0:def 5;
  end;
  then
A91: g.:y c= NAT \ {0} by TARSKI:def 3;
A92: f" = f~ by A63,FUNCT_1:def 9;
  Sgm(Seg(n+1)\{i}) is one-to-one by FINSEQ_3:99,XBOOLE_1:36;
  then
A93: Sgm(Seg(n+1)\{i})"=Sgm(Seg(n+1)\{i})~ by FUNCT_1:def 9;
  now
    let a,b be set;
    hereby
      assume
A94:  [a,b] in f;
      per cases by A94,XBOOLE_0:def 3;
      suppose [a,b] in f1;
        then
A95:    a = b & a in x1 by RELAT_1:def 10;
        then consider a' be Element of NAT such that
A96:    a' = a & a' in y & a'<i;
        reconsider i'=i,n'=n as Element of NAT by ORDINAL1:def 13;
        i<=n+1 by A2,FINSEQ_1:3;
        then a'<n+1 by A96,XXREAL_0:2;
        then
A97:    a'<=n by NAT_1:13;
A98:    1<=a' by A1,A96,FINSEQ_1:3;
        then
A99:   a in Seg n by A96,A97;
        then a in Seg len Sgm(Seg(n+1)\{i}) by A2,FINSEQ_3:116;
        then
A100:   a in dom Sgm(Seg(n+1)\{i}) by FINSEQ_1:def 3;
        a' = Sgm(Seg(n'+1)\{i'}).a' by A2,A96,A98,A99,FINSEQ_3:117;
        then [a,b] in Sgm(Seg(n+1)\{i}) by A95,A96,A100,FUNCT_1:8;
        hence [a,b] in (rng f)|Sgm(Seg(n+1)\{i}) by A72,A95,A96,RELAT_1:def 12;
      end;
      suppose [a,b] in f2;
        then consider b' be Element of NAT such that
A101:   [a,b] = [b'-1,b'] & b' in y' & b'>i;
A102:   a = b'-1 & b = b' by A101,ZFMISC_1:33;
        reconsider a'=b'-1 as integer number;
        reconsider i'=i,n'=n as Element of NAT by ORDINAL1:def 13;
        1<=b' & b'<=n+1 by A1,A101,FINSEQ_1:3;
        then
A103:   b'-1<=n+1-1 by XREAL_1:11;
        i+1<=b' by A101,NAT_1:13;
        then
A104:   i+1-1<=b'-1 by XREAL_1:11;
        then
A105:   1<=a' by A3,XXREAL_0:2;
        reconsider a' as Element of NAT by A104,INT_1:16;
A106:   a' in Seg n by A103,A105;
        then a in Seg n by A101,ZFMISC_1:33;
        then a in Seg len Sgm(Seg(n+1)\{i}) by A2,FINSEQ_3:116;
        then
A107:   a in dom Sgm(Seg(n+1)\{i}) by FINSEQ_1:def 3;
        a'+1 = Sgm(Seg(n'+1)\{i'}).a' by A2,A103,A104,A106,FINSEQ_3:117;
        then [a,b] in Sgm(Seg(n+1)\{i}) by A102,A107,FUNCT_1:8;
        hence [a,b] in (rng f)|Sgm(Seg(n+1)\{i}) by A72,A101,RELAT_1:def 12;
      end;
    end;
    assume
A108: [a,b] in (rng f)|Sgm(Seg(n+1)\{i});
    then
A109: b in rng f & [a,b] in Sgm(Seg(n+1)\{i}) by RELAT_1:def 12;
A110: b in y by A72,A108,RELAT_1:def 12;
A111: b in Seg(n+1) & not b=i by A1,A4,A72,A109;
A112: a in dom Sgm(Seg(n+1)\{i}) by A109,RELAT_1:def 4;
    then a in Seg len Sgm(Seg(n+1)\{i}) by FINSEQ_1:def 3;
    then
A113: a in Seg n by A2,FINSEQ_3:116;
    reconsider a'=a,b'=b as Element of NAT by A111,A112;
    reconsider i,n as Element of NAT by ORDINAL1:def 13;
A114: 1<=a' & a'<=n by A113,FINSEQ_1:3;
    per cases;
    suppose
A115: a'<i;
      then Sgm(Seg(n+1)\{i}).a' = a' by A2,A113,A114,FINSEQ_3:117;
      then
A116: b=a by A109,FUNCT_1:8;
      then a' in x1 by A110,A115;
      then [a,b] in id x1 by A116,RELAT_1:def 10;
      hence [a,b] in f by XBOOLE_0:def 3;
    end;
    suppose
A117: i<=a';
      then Sgm(Seg(n+1)\{i}).a' = a'+1 by A2,A113,A114,FINSEQ_3:117;
      then
A118: b'=a'+1 by A109,FUNCT_1:8;
      then
A119: b' in y' & b'>i by A72,A108,A117,NAT_1:13,RELAT_1:def 12;
      a = b'-1 by A118;
      then [a,b] in f2 by A119;
      hence [a,b] in f by XBOOLE_0:def 3;
    end;
  end;
  then f = (rng f)|Sgm(Seg(n+1)\{i}) by RELAT_1:def 2;
  then
A120: f" = (Sgm(Seg(n+1)\{i})~)|dom(f") by A92,Lm36;
  Sgm x = Sgm (g.:y) by A74,RELAT_1:146
    .= ((Sgm(Seg(n+1)\{i})")|y) * Sgm y by A1,A74,A88,A91,A93,A120,Lm38
    .= Sgm(Seg(n+1)\{i})" * y|Sgm y by Lm37
    .= Sgm(Seg(n+1)\{i})" * Sgm y by A73,RELAT_1:126;
  hence Sgm x = Sgm(Seg(n+1)\{i})" * Sgm y;
  thus thesis by A5,TARSKI:def 3;
end;

theorem Th99:
  i in dom s1 & i+1 in dom s1 & s1.i = s1.(i+1) & s1' = Del(s1,i) &
  s2 is jordan_holder & s1 is_finer_than s2 implies s1' is_finer_than s2
proof
  assume
A1: i in dom s1 & i+1 in dom s1;
  assume
A2: s1.i = s1.(i+1);
  assume
A3: s1' = Del(s1,i);
  assume
A4: s2 is jordan_holder;
  assume s1 is_finer_than s2;
  then consider z be set such that
A5: z c= dom s1 & s2 = s1 * Sgm z by Def32;
  set k = len s1 - 1;
A6: i in Seg len s1 & i+1 in Seg len s1 by A1,FINSEQ_1:def 3;
  then
A7: 1 <= i & i<=len s1 by FINSEQ_1:3;
  then 1 <= len s1 by XXREAL_0:2;
  then
A8: 1-1 <= len s1 - 1 by XREAL_1:11;
  reconsider k as integer number;
  reconsider k as Element of NAT by A8,INT_1:16;
A9: dom s1 = Seg(k+1) by FINSEQ_1:def 3;
  now
    per cases;
    suppose
A10:  not i in z;
      set y = z;
      take y;
      thus y c= Seg(k+1) by A5,FINSEQ_1:def 3;
      thus not i in y by A10;
      thus s2 = s1 * Sgm y by A5;
    end;
    suppose
A11:  i in z;
      reconsider y = (z \/ {i+1}) \ {i} as set;
      take y;
      {i+1} c= Seg(k+1) by A6,ZFMISC_1:37;
      then
A12:  z \/ {i+1} c= Seg(k+1) by A5,A9,XBOOLE_1:8;
      hence
A13:  y c= Seg(k+1) by XBOOLE_1:1;
      now
        assume i in y;
        then not i in {i} by XBOOLE_0:def 5;
        hence contradiction by TARSKI:def 1;
      end;
      hence not i in y;
      reconsider y'=y,z as finite set by A5;
      i in z \/ {i+1} by A11,XBOOLE_0:def 3;
      then {i} c= z \/ {i+1} by ZFMISC_1:37;
      then card((z \/ {i+1})\{i}) = card(z \/ {i+1}) - card{i} by CARD_2:63;
      then
A14:  card y' = card(z \/ {i+1}) - 1 by CARD_1:50;
A15:  now
        assume i+1 in z;
        then
A16:    i in rng Sgm z & i+1 in rng Sgm z by A5,A9,A11,FINSEQ_1:def 13;
        then consider x' be set such that
A17:    x' in dom Sgm z & i = Sgm(z).x' by FUNCT_1:def 5;
        consider x'' be set such that
A18:    x'' in dom Sgm z & i+1 = Sgm(z).x'' by A16,FUNCT_1:def 5;
A19:    dom Sgm z = Seg len Sgm z by FINSEQ_1:def 3;
        reconsider x',x'' as Element of NAT by A17,A18;
A20:    0+i < 1+i by XREAL_1:8;
        then i < i+1 & 1 <= x'' & x' <= len Sgm z by A17,A18,A19,FINSEQ_1:3;
        then x' < x'' by A5,A9,A17,A18,FINSEQ_3:46;
        then reconsider l = x'' - x' as Element of NAT by INT_1:18;
        per cases;
        suppose l = 0;
          hence contradiction by A17,A18,A20;
        end;
        suppose 0 < l;
          then
A21:      0+1 < l+1 by XREAL_1:8;
          set x''' = x'+1;
          x'+1-x' <= x''-x' by A21,NAT_1:13;
          then
A22:      x''' <= x'' by XREAL_1:11;
A23:      1+x' > 0+x' by XREAL_1:8;
A24:      1 <= x' by A17,A19,FINSEQ_1:3;
          then
A25:      1 <= x''' by A23,XXREAL_0:2;
          x'' <= len Sgm z by A18,A19,FINSEQ_1:3;
          then
A26:      x''' <= len Sgm z by A22,XXREAL_0:2;
          then x''' in dom Sgm z by A19,A25;
          then reconsider k3= Sgm(z).x'''
          as Element of NAT by FINSEQ_2:13;
A27:      (1 <= x''' & x''' < x'' & x'' <= len Sgm z) or x''' = x''
          by A18,A19,A22,A23,A24,FINSEQ_1:3,XXREAL_0:1,2;
          then k3 <= i+1 & i < k3
          by A5,A9,A17,A18,A23,A24,A26,FINSEQ_1:def 13;
          then
A28:      i+1 <= k3 by NAT_1:13;
A29:      s2.x' = s1.(Sgm(z).x') by A5,A17,FUNCT_1:23
            .= s2.(x'+1)
          by A2,A5,A9,A17,A18,A27,A28,FINSEQ_1:def 13,FUNCT_1:23;
A30:      x' in dom s2 & x'+1 in dom s2
          by A1,A5,A9,A17,A18,A27,A28,FINSEQ_1:def 13,FUNCT_1:21;
          then reconsider H1=s2.x',H2=s2.(x'+1)
          as Element of the_stable_subgroups_of G by FINSEQ_2:13;
          reconsider H1,H2 as StableSubgroup of G by Def11;
          reconsider H1 as GroupWithOperators of O;
          reconsider H2 as normal StableSubgroup of H1 by A30,Def31;
          the carrier of H1 = the carrier of H2 by A29;
          then
A31:      H1./.H2 is trivial by Th77;
          s2 is strictly_decreasing by A4,Def34;
          hence contradiction by A30,A31,Def33;
        end;
      end;
      then
A32:  card(z \/ {i+1}) = card z + 1 by CARD_2:54;
      dom Sgm y' = Seg card y' by A12,FINSEQ_3:45,XBOOLE_1:1;
      then
A33:  dom Sgm y' = dom Sgm z by A5,A9,A14,A32,FINSEQ_3:45;
      y c= dom s1 by A13,FINSEQ_1:def 3;
      then
A34:  rng Sgm y c= dom s1 by A13,FINSEQ_1:def 13;
      rng Sgm z c= dom s1 by A5,A9,FINSEQ_1:def 13;
      then
A35:  dom(s1 * Sgm z) = dom Sgm z by RELAT_1:46;
      then
A36:  dom(s1 * Sgm y) = dom(s1 * Sgm z) by A33,A34,RELAT_1:46;
A37:  z c= Seg(k+1) by A5,FINSEQ_1:def 3;
      set z1 = {x where x is Element of NAT: x in z & x < i};
      set z2 = {x where x is Element of NAT: x in z & i+1 < x};
A38:  now
        let x;
        assume
A39:    x in z;
        then x in Seg(k+1) by A37;
        then reconsider x'=x as Element of NAT;
        x'<=i or i+1<=x' by NAT_1:13;
        then x'<i or x'=i or i+1<x' by A15,A39,XXREAL_0:1;
        then x in z1 or x in {i} or x in z2 by A39,TARSKI:def 1;
        then x in z1 \/ {i} or x in z2 by XBOOLE_0:def 3;
        hence x in z1 \/ {i} \/ z2 by XBOOLE_0:def 3;
      end;
A40:  now
        let x;
        assume x in z1 \/ {i} \/ z2;
        then x in z1 \/ {i} or x in z2 by XBOOLE_0:def 3;
        then x in z1 or x in {i} or x in z2 by XBOOLE_0:def 3;
        then consider x',x'' be Element of NAT such that
A41:    (x = x' & x' in z & x'<i) or x in {i}
        or (x = x'' & x'' in z & i+1<x'');
        per cases by A41;
        suppose x = x' & x' in z & x'<i;
          hence x in z;
        end;
        suppose x in {i};
          hence x in z by A11,TARSKI:def 1;
        end;
        suppose x = x'' & x'' in z & i+1<x'';
          hence x in z;
        end;
      end;
      then
A42:  z = z1 \/ {i} \/ z2 by A38,TARSKI:2;
      then z1 \/ {i} c= z & z2 c= z by XBOOLE_1:7;
      then
A43:  z1 \/ {i} c= Seg(k+1) & z2 c= Seg(k+1) by A37,XBOOLE_1:1;
      now
        let m,n be Element of NAT;
        assume m in z1 \/ {i};
        then m in z1 or m in {i} by XBOOLE_0:def 3;
        then consider x' be Element of NAT such that
A44:    (m = x' & x' in z & x'<i) or m in {i};
        assume n in z2;
        then consider x'' be Element of NAT such that
A45:    n = x'' & x'' in z & i+1<x'';
        0+i<1+i by XREAL_1:8;
        then m<i+1 by A44,TARSKI:def 1,XXREAL_0:2;
        hence m < n by A45,XXREAL_0:2;
      end;
      then
A46:  Sgm z = Sgm(z1 \/ {i}) ^ Sgm z2 by A42,A43,FINSEQ_3:48;
      z1 c= z & {i} c= z by A42,XBOOLE_1:7,11;
      then
A47:  z1 c= Seg(k+1) & {i} c= Seg(k+1) by A37,XBOOLE_1:1;
      now
        let m,n be Element of NAT;
        assume m in z1;
        then consider x' be Element of NAT such that
A48:    (m = x' & x' in z & x'<i);
        assume n in {i};
        hence m < n by A48,TARSKI:def 1;
      end;
      then
A49:  Sgm(z1 \/ {i}) = Sgm z1 ^ Sgm {i} by A47,FINSEQ_3:48;
      then
A50:  Sgm z = Sgm z1 ^ <*i*> ^ Sgm z2 by A7,A46,FINSEQ_3:50;
      now
        let x;
        assume x in {i+1} /\ {i};
        then x in {i+1} & x in {i} by XBOOLE_0:def 4;
        then x=i+1 & x=i by TARSKI:def 1;
        hence contradiction;
      end;
      then {i+1} /\ {i} = {} by XBOOLE_0:def 1;
      then
A51:  {i+1} misses {i} by XBOOLE_0:def 7;
      now
        let x;
        assume x in z2 /\ {i};
        then
A52:    x in z2 & x in {i} by XBOOLE_0:def 4;
        then consider x' be Element of NAT such that
A53:    x'=x & x' in z & i+1<x';
        i+1<x' & x'=i by A52,A53,TARSKI:def 1;
        then i+1-i<i-i by XREAL_1:11;
        then 1<0;
        hence contradiction;
      end;
      then z2 /\ {i} = {} by XBOOLE_0:def 1;
      then
A54:  z2 misses {i} by XBOOLE_0:def 7;
      now
        let x;
        assume x in z1 /\ {i};
        then
A55:    x in z1 & x in {i} by XBOOLE_0:def 4;
        then consider x' be Element of NAT such that
A56:    x'=x & x' in z & x'<i;
        thus contradiction by A55,A56,TARSKI:def 1;
      end;
      then z1 /\ {i} = {} by XBOOLE_0:def 1;
      then
A57:  z1 misses {i} by XBOOLE_0:def 7;
A58:  y = (z1 \/ {i} \/ z2 \/ {i+1}) \ {i} by A38,A40,TARSKI:2
        .= ((z1 \/ {i} \/ z2)\{i}) \/ ({i+1}\{i}) by XBOOLE_1:42
        .= ((z1 \/ {i} \/ z2)\{i}) \/ {i+1} by A51,XBOOLE_1:83
        .= ((z1 \/ {i})\{i}) \/ (z2\{i}) \/ {i+1} by XBOOLE_1:42
        .= ((z1 \/ {i})\{i}) \/ z2 \/ {i+1} by A54,XBOOLE_1:83
        .= (z1\{i}) \/ ({i}\{i}) \/ z2 \/ {i+1} by XBOOLE_1:42
        .= (z1\{i}) \/ {} \/ z2 \/ {i+1} by XBOOLE_1:37
        .= z1 \/ {} \/ z2 \/ {i+1} by A57,XBOOLE_1:83
        .= z1 \/ {i+1} \/ z2 by XBOOLE_1:4;
      then z1 \/ {i+1} c= y & z2 c= y by XBOOLE_1:7;
      then
A59:  z1 \/ {i+1} c= Seg(k+1) & z2 c= Seg(k+1) by A13,XBOOLE_1:1;
      now
        let m,n be Element of NAT;
        assume m in z1 \/ {i+1};
        then m in z1 or m in {i+1} by XBOOLE_0:def 3;
        then consider x' be Element of NAT such that
A60:    (m = x' & x' in z & x'<i) or m in {i+1};
        assume n in z2;
        then consider x'' be Element of NAT such that
A61:    n = x'' & x'' in z & i+1<x'';
        0+i<1+i by XREAL_1:8;
        then m<i+1 or m=i+1 by A60,TARSKI:def 1,XXREAL_0:2;
        hence m < n by A61,XXREAL_0:2;
      end;
      then
A62:  Sgm y = Sgm(z1 \/ {i+1}) ^ Sgm z2 by A58,A59,FINSEQ_3:48;
      z1 c= y & {i+1} c= y by A58,XBOOLE_1:7,11;
      then
A63:  z1 c= Seg(k+1) & {i+1} c= Seg(k+1) by A13,XBOOLE_1:1;
      now
        let m,n be Element of NAT;
        assume m in z1;
        then consider x' be Element of NAT such that
A64:    (m = x' & x' in z & x'<i);
        assume n in {i+1};
        then
A65:    n=i+1 by TARSKI:def 1;
        0+i<1+i by XREAL_1:8;
        hence m < n by A64,A65,XXREAL_0:2;
      end;
      then
A66:  Sgm(z1 \/ {i+1}) = Sgm z1 ^ Sgm {i+1} by A63,FINSEQ_3:48;
      then
A67:  Sgm y = Sgm z1 ^ <*i+1*> ^ Sgm z2 by A62,FINSEQ_3:50;
A68:  now
        let x;
        assume
A69:    x in dom Sgm z;
        then reconsider x'=x as Element of NAT;
A70:    len(Sgm z1 ^ <*i*>) = len Sgm z1 + len <*i*> by FINSEQ_1:35
          .= len Sgm z1 + 1 by FINSEQ_1:57
          .= len Sgm z1 + len <*i+1*> by FINSEQ_1:57
          .= len(Sgm z1 ^ <*i+1*>) by FINSEQ_1:35;
A71:    dom(Sgm z1 ^ <*i*>) = Seg len(Sgm z1 ^ <*i*>) by FINSEQ_1:def 3
          .= dom(Sgm z1 ^ <*i+1*>) by A70,FINSEQ_1:def 3;
        per cases by A50,A69,FINSEQ_1:38;
        suppose
A72:      x' in dom(Sgm z1 ^ <*i*>);
          per cases by A72,FINSEQ_1:38;
          suppose
A73:        x' in dom Sgm z1;
            then
A74:        (Sgm z1).x' = (Sgm z1 ^ <*i*>).x' by FINSEQ_1:def 7
              .= (Sgm z1 ^ <*i*> ^ Sgm z2).x' by A72,FINSEQ_1:def 7
              .= (Sgm z).x' by A7,A46,A49,FINSEQ_3:50;
            (Sgm z1).x' = (Sgm z1 ^ <*i+1*>).x' by A73,FINSEQ_1:def 7
              .= (Sgm z1 ^ <*i+1*> ^ Sgm z2).x' by A71,A72,FINSEQ_1:def 7
              .= (Sgm y).x' by A62,A66,FINSEQ_3:50;
            hence ((Sgm z).x <> i implies (Sgm y).x = (Sgm z).x) by A74;
            thus (Sgm z).x = i implies (Sgm y).x = i+1
            proof
              assume (Sgm z).x = i;
              then i in rng Sgm z1 by A73,A74,FUNCT_1:12;
              then i in z1 by A47,FINSEQ_1:def 13;
              then consider x''' be Element of NAT such that
A75:          x'''=i & x''' in z & x'''<i;
              thus thesis by A75;
            end;
          end;
          suppose ex x'' being Nat st x'' in dom <*i*> & x'=len Sgm z1 + x'';
            then consider x'' be Nat such that
A76:        x'' in dom <*i*> & x'=len Sgm z1 + x'';
A77:        x'' in Seg 1 by A76,FINSEQ_1:55;
            then
A78:        x'' = 1 by FINSEQ_1:4,TARSKI:def 1;
            then i = <*i*>.x'' by FINSEQ_1:57
              .= (Sgm z1 ^ <*i*>).x' by A76,FINSEQ_1:def 7
              .= (Sgm z1 ^ <*i*> ^ Sgm z2).x' by A72,FINSEQ_1:def 7
              .= (Sgm z).x' by A7,A46,A49,FINSEQ_3:50;
            hence ((Sgm z).x <> i implies (Sgm y).x = (Sgm z).x);
            thus (Sgm z).x = i implies (Sgm y).x = i+1
            proof
              assume (Sgm z).x = i;
A79:          x'' in dom <*i+1*> by A77,FINSEQ_1:55;
              i+1 = <*i+1*>.x'' by A78,FINSEQ_1:57
                .= (Sgm z1 ^ <*i+1*>).x' by A76,A79,FINSEQ_1:def 7
                .= (Sgm z1 ^ <*i+1*> ^ Sgm z2).x' by A71,A72,FINSEQ_1:def 7;
              hence thesis by A62,A66,FINSEQ_3:50;
            end;
          end;
        end;
        suppose ex x'' being Nat st
          x'' in dom Sgm z2 & x'=len(Sgm z1 ^ <*i*>) + x'';
          then consider x'' be Nat such that
A80:      x'' in dom Sgm z2 & x'=len(Sgm z1 ^ <*i*>) + x'';
          (Sgm y).x' = (Sgm z2).x'' by A67,A70,A80,FINSEQ_1:def 7;
          hence ((Sgm z).x <> i implies (Sgm y).x = (Sgm z).x)
          by A50,A80,FINSEQ_1:def 7;
          thus (Sgm z).x = i implies (Sgm y).x = i+1
          proof
            assume (Sgm z).x = i;
            then (Sgm z2).x'' = i by A50,A80,FINSEQ_1:def 7;
            then i in rng Sgm z2 by A80,FUNCT_1:12;
            then i in z2 by A59,FINSEQ_1:def 13;
            then consider x''' be Element of NAT such that
A81:        x'''=i & x''' in z & i+1<x''';
            i+1-i<i-i by A81,XREAL_1:11;
            then 1<0;
            hence thesis;
          end;
        end;
      end;
      now
        let x;
        assume
A82:    x in dom(s1 * Sgm y);
        then
A83:    x in dom(s1 * Sgm z) by A33,A34,A35,RELAT_1:46;
A84:    x in dom Sgm z & (Sgm z).x in dom s1 by A36,A82,FUNCT_1:21;
        per cases;
        suppose
A85:      (Sgm z).x = i;
          (s1 * Sgm y).x = s1.((Sgm y).x) by A82,FUNCT_1:22
            .= s1.((Sgm z).x) by A2,A68,A84,A85
            .= (s1 * Sgm z).x by A36,A82,FUNCT_1:22;
          hence (s1 * Sgm y).x = (s1 * Sgm z).x;
        end;
        suppose
A86:      (Sgm z).x <> i;
          (s1 * Sgm y).x = s1.((Sgm y).x) by A82,FUNCT_1:22
            .= s1.((Sgm z).x) by A68,A84,A86
            .= (s1 * Sgm z).x by A83,FUNCT_1:22;
          hence (s1 * Sgm y).x = (s1 * Sgm z).x;
        end;
      end;
      hence s2 = s1 * Sgm y by A5,A36,FUNCT_1:9;
    end;
  end;
  then consider y be set such that
A87: y c= Seg(k+1) & not i in y & s2 = s1 * Sgm y;
  now
    consider x such that
A88: Sgm x = Sgm(Seg(k+1)\{i})" * Sgm y & x c= Seg k by A6,A87,Lm39;
    take x;
    consider m be Nat such that
A89: len s1 = m + 1 & len Del(s1,i) = m by A1,FINSEQ_3:113;
    thus x c= dom s1' by A3,A88,A89,FINSEQ_1:def 3;
A90: Seg(k+1)\{i} c= Seg(k+1) by XBOOLE_1:36;
    set f = Sgm (Seg(k+1)\{i});
    set X = dom f;
    set Y = rng f;
    reconsider f as Function of X,Y by FUNCT_2:3;
A91: rng f = Seg(k+1)\{i} by A90,FINSEQ_1:def 13;
A92: f is one-to-one by FINSEQ_3:99,XBOOLE_1:36;
A93: now
      assume
A94:  rng f = {};
A95:  1<=i & i+1<=len s1 by A6,FINSEQ_1:3;
      then 1+1<=i+1 by XREAL_1:8;
      then 2<=len s1 by A95,XXREAL_0:2;
      then Seg 2 c= Seg(k+1) by FINSEQ_1:7;
      then
A96:  Seg 2\{i} c= rng f by A91,XBOOLE_1:33;
      per cases by A94,A96,XBOOLE_1:3,ZFMISC_1:66;
      suppose Seg 2 = {};
        hence contradiction;
      end;
      suppose Seg 2 = {i};
        hence contradiction by FINSEQ_1:4,ZFMISC_1:9;
      end;
    end;
    now
      let x' be set;
      assume x' in y;
      then x' in Seg(k+1) & not x' in {i} by A87,TARSKI:def 1;
      hence x' in rng f by A91,XBOOLE_0:def 5;
    end;
    then y c= rng f by TARSKI:def 3;
    then
A97: rng Sgm y c= rng f by A87,FINSEQ_1:def 13;
    s1' * Sgm x = s1 * f * Sgm x by A3,FINSEQ_1:def 3
      .= s1 * f * f" * Sgm y by A88,RELAT_1:55
      .= s1 * (f * f") * Sgm y by RELAT_1:55
      .= s1 * id rng f * Sgm y by A92,A93,FUNCT_2:35
      .= s1 * (id rng f * Sgm y) by RELAT_1:55
      .= s1 * Sgm y by A97,RELAT_1:79;
    hence s2 = s1' * Sgm x by A87;
  end;
  hence thesis by Def32;
end;

theorem Th100:
  len s1 > 1 & s2<>s1 & s2 is strictly_decreasing
  & s2 is_finer_than s1 implies
  ex i,j st i in dom s1 & i in dom s2 & i+1 in dom s1 & i+1 in dom s2 &
  j in dom s2 & i+1<j & s1.i=s2.i & s1.(i+1)<>s2.(i+1) & s1.(i+1)=s2.j
proof
  assume len s1 > 1;
  then len s1 >= 1+1 by NAT_1:13;
  then Seg 2 c= Seg len s1 by FINSEQ_1:7;
  then
A1: Seg 2 c= dom s1 by FINSEQ_1:def 3;
  assume
A2: s2<>s1;
  assume
A3: s2 is strictly_decreasing;
  assume
A4: s2 is_finer_than s1;
  then consider n such that
A5: len s2 = len s1 + n by Th95;
  n<>0 by A2,A4,A5,Th96;
  then 0<n;
  then
A6: 0 + len s1 < n + len s1 by XREAL_1:8;
  then Seg len s1 c= Seg len s2 by A5,FINSEQ_1:7;
  then Seg len s1 c= dom s2 by FINSEQ_1:def 3;
  then
A7: dom s1 c= dom s2 by FINSEQ_1:def 3;
  now
    set fX = {k where k is Element of NAT: k in dom s1 & s1.k=s2.k};
    now
      let x;
      assume x in fX;
      then consider k be Element of NAT such that
A8:   x=k & k in dom s1 & s1.k=s2.k;
      thus x in dom s1 by A8;
    end;
    then
A9: fX c= dom s1 by TARSKI:def 3;
A10: s1.1 = (Omega).G & s2.1 = (Omega).G by Def31;
    1 in Seg 2 & 2 in Seg 2;
    then
A11: 1 in fX by A1,A10;
    reconsider fX as finite non empty real-membered set
    by A9,A11;
    set i = max fX;
    i in fX by XXREAL_2:def 8;
    then consider k be Element of NAT such that
A13: i=k & k in dom s1 & s1.k=s2.k;
    reconsider i as Element of NAT by A13;
    take i;
    thus i in dom s1 & s1.i=s2.i by A13;
A14: now
      assume not i+1 in dom s1;
      then
A15:  not i+1 in Seg len s1 by FINSEQ_1:def 3;
      per cases by A15;
      suppose 1>i+1;
        then 1-1>i+1-1 by XREAL_1:11;
        then 0>i;
        hence contradiction;
      end;
      suppose i+1>len s1;
        then
A16:    i>=len s1 by NAT_1:13;
        i in Seg len s1 by A13,FINSEQ_1:def 3;
        then i<=len s1 by FINSEQ_1:3;
        then
A17:    i=len s1 by A16,XXREAL_0:1;
        then
A18:    s2.i=(1).G by A13,Def31;
A19:    0+1<=i+1 by XREAL_1:8;
        i+1<=len s2 by A5,A6,A17,NAT_1:13;
        then i+1 in Seg len s2 by A19;
        then
A20:    i+1 in dom s2 by FINSEQ_1:def 3;
        then reconsider H1=s2.i,H2=s2.(i+1)
        as Element of the_stable_subgroups_of G by A13,FINSEQ_2:13;
        reconsider H1,H2 as StableSubgroup of G by Def11;
        reconsider H2 as normal StableSubgroup of H1 by A7,A13,A20,Def31;
        H2 is Subgroup of (1).G by A18,Def7;
        then the carrier of H2 c= the carrier of (1).G by GROUP_2:def 5;
        then
A21:    the carrier of H2 c= {1_G} by Def8;
        1_G in H2 by Lm18;
        then 1_G in the carrier of H2 by STRUCT_0:def 5;
        then {1_G} c= the carrier of H2 by ZFMISC_1:37;
        then
A22:    the carrier of H2 = {1_G} by A21,XBOOLE_0:def 10;
        the carrier of H1 = {1_G} by A18,Def8;
        then H1./.H2 is trivial by A22,Th77;
        hence contradiction by A3,A7,A13,A20,Def33;
      end;
    end;
    hence i+1 in dom s1;
    now
      assume
A23:  s1.(i+1)=s2.(i+1);
      1+i>0+i by XREAL_1:8;
      then consider k be Element of NAT such that
A24:  k>i & k in dom s1 & s1.k=s2.k by A14,A23;
      k in fX by A24;
      hence contradiction by A24,XXREAL_2:def 8;
    end;
    hence s1.(i+1)<>s2.(i+1);
  end;
  then consider i such that
A25: i in dom s1 & i+1 in dom s1 & s1.i=s2.i & s1.(i+1)<>s2.(i+1);
  now
    consider x such that
A26: x c= dom s2 & s1 = s2 * Sgm x by A4,Def32;
A27: x c= Seg len s2 by A26,FINSEQ_1:def 3;
    set j = (Sgm x).(i+1);
A28: i+1 in dom Sgm x by A25,A26,FUNCT_1:21;
    then j in rng Sgm x by FUNCT_1:12;
    then j in x by A27,FINSEQ_1:def 13;
    then
A29: j in Seg len s2 by A27;
    then reconsider j as Element of NAT;
    take j;
    thus j in dom s2 by A29,FINSEQ_1:def 3;
    thus s1.(i+1)=s2.j by A25,A26,FUNCT_1:22;
A30: i+1 <= j by A27,A28,GLIB_001:2;
    j<>i+1 by A25,A26,FUNCT_1:22;
    hence i+1<j by A30,XXREAL_0:1;
  end;
  then consider j such that
A31: j in dom s2 & i+1<j & s1.(i+1)=s2.j;
  take i,j;
  thus i in dom s1 & i in dom s2 by A7,A25;
  thus i+1 in dom s1 & i+1 in dom s2 by A7,A25;
  thus j in dom s2 & i+1<j by A31;
  thus s1.i=s2.i & s1.(i+1)<>s2.(i+1) by A25;
  thus thesis by A31;
end;

theorem Th101:
  i in dom s1 & j in dom s1 & i<=j & H1 = s1.i & H2 = s1.j
  implies H2 is StableSubgroup of H1
proof
  assume
A1: i in dom s1 & j in dom s1;
  assume
A2: i<=j;
  assume
A3: H1 = s1.i & H2 = s1.j;
  defpred P[Nat] means for n,H2 st i+$1 in dom s1 & H2 = s1.(i+$1)
  holds H2 is StableSubgroup of H1;
A4: P[0] by A3,Th10;
A5: for n st P[n] holds P[n+1]
  proof
    let n;
    assume
A6: P[n];
    set H2 = s1.(i+n);
    per cases;
    suppose
A7:   i+n in dom s1;

then reconsider H2 as Element of the_stable_subgroups_of G by FINSEQ_2:13;
      reconsider H2 as StableSubgroup of G by Def11;
A8:   H2 is StableSubgroup of H1 by A6,A7;
      now
        let k be Element of NAT;
        let H3;
        assume
A9:     i+(n+1) in dom s1;
        assume
A10:    H3 = s1.(i+(n+1));
        i+n+1 in dom s1 by A9;
        then H3 is StableSubgroup of H2 by A7,A10,Def31;
        hence H3 is StableSubgroup of H1 by A8,Th11;
      end;
      hence thesis;
    end;
    suppose not i+n in dom s1;
      then
A11:  not i+n in Seg len s1 by FINSEQ_1:def 3;
      per cases by A11,FINSEQ_1:3;
      suppose i+n<0+1;
        then i+n<=0 by NAT_1:13;
        then i+n=0;
        then n=0;
        hence thesis by A1,A3,Def31;
      end;
      suppose i+n>len s1;
        then
A12:    i+n+1>len s1+1 by XREAL_1:8;
        1+len s1>0+len s1 by XREAL_1:8;
        then i+n+1>len s1 by A12,XXREAL_0:2;
        then not i+n+1 in Seg len s1 by FINSEQ_1:3;
        hence thesis by FINSEQ_1:def 3;
      end;
    end;
  end;
A13: for n holds P[n] from NAT_1:sch 2(A4,A5);
  set n=j-i;
  i-i<=j-i by A2,XREAL_1:11;
  then reconsider n as Element of NAT by INT_1:16;
  reconsider n as Nat;
  j=i+n;
  hence thesis by A1,A3,A13;
end;

theorem Th102:
  y in rng the_series_of_quotients_of s1
  implies y is strict GroupWithOperators of O
proof
  assume
A1: y in rng the_series_of_quotients_of s1;
  set f1=the_series_of_quotients_of s1;
A2: len f1 = 0 or len f1 >= 0+1 by NAT_1:13;
  per cases by A2;
  suppose len f1 = 0;
     then f1 = {};
    hence thesis by A1;
  end;
  suppose
A3: len f1 >= 1;
A4: len s1 > 1 by A3,Def36,CARD_1:47;
    then
A5: len s1 = len f1 + 1 by Def36;
    consider i be set such that
A6: i in dom f1 & f1.i=y by A1,FUNCT_1:def 5;
    reconsider i as Nat by A6;
A7: 0+i<=1+i by XREAL_1:8;
    i in Seg len f1 by A6,FINSEQ_1:def 3;
    then
A8: 1<=i & i<=len f1 by FINSEQ_1:3;
    then 1<=i & i+1<=len f1 + 1 by XREAL_1:8;
    then 1<=i & i<=len s1 by A5,A7,XXREAL_0:2;
    then i in Seg len s1 by FINSEQ_1:3;
    then
A9: i in dom s1 by FINSEQ_1:def 3;
    then s1.i in the_stable_subgroups_of G by FINSEQ_2:13;
    then reconsider H1=s1.i as strict StableSubgroup of G by Def11;
    1+1<=i+1 & i + 1<=len f1 + 1 by A8,XREAL_1:8;
    then 1<=i+1 & i+1<=len s1 by A4,Def36,XXREAL_0:2;
    then i+1 in Seg len s1;
    then
A10: i+1 in dom s1 by FINSEQ_1:def 3;
    then s1.(i+1) in the_stable_subgroups_of G by FINSEQ_2:13;
    then reconsider N1=s1.(i+1) as strict StableSubgroup of G by Def11;
    reconsider N1 as normal StableSubgroup of H1 by A9,A10,Def31;
    y = H1./.N1 by A4,A6,Def36;
    hence thesis;
  end;
end;

theorem Th103:
  i in dom the_series_of_quotients_of s1 &
  (for H st H=(the_series_of_quotients_of s1).i holds H is trivial)
  implies i in dom s1 & i+1 in dom s1 & s1.i=s1.(i+1)
proof
  assume
A1: i in dom the_series_of_quotients_of s1;
  assume
A2: for H st H=(the_series_of_quotients_of s1).i holds H is trivial;
  set f1 = the_series_of_quotients_of s1;
A3: len f1 = 0 or len f1 >= 0+1 by NAT_1:13;
  per cases by A3,XXREAL_0:1;
  suppose len f1 = 0;
     then f1 = {};
    hence thesis by A1;
  end;
  suppose
A4: len f1 = 1;
A5: len s1 > 1 by A4,Def36,CARD_1:47;
    then
A6: len s1 = len f1 + 1 by Def36;
    then
A7: s1.1=(Omega).G & s1.2=(1).G by A4,Def31;
    i in Seg 1 by A1,A4,FINSEQ_1:def 3;
    then
A8: i=1 by FINSEQ_1:4,TARSKI:def 1;
A9: for H1, N1 st H1=s1.i & N1=s1.(i+1) holds f1.i = H1./.N1 by A1,A5,Def36;
    set H1=(Omega).G;
    reconsider N1=(1).G as StableSubgroup of H1 by Th16;
A10: (1).G = (1).H1 by Th15;
    then reconsider N1 as normal StableSubgroup of H1;
    f1.i in rng f1 by A1,FUNCT_1:12;
    then reconsider H=f1.i as strict GroupWithOperators of O by Th102;
    H is trivial by A2;
    then
A11: H1./.N1 is trivial by A7,A8,A9;
A12: H1,H1./.N1 are_isomorphic by A10,Th56;
    i in Seg 2 by A8;
    hence i in dom s1 by A4,A6,FINSEQ_1:def 3;
    i+1 in Seg 2 by A8;
    hence i+1 in dom s1 by A4,A6,FINSEQ_1:def 3;
    thus thesis by A7,A8,A10,A11,A12,Th42,Th58;
  end;
  suppose
A13: len f1 > 1;
A14: len s1 > 1 by A13,Def36,CARD_1:47;
    then
A15: len s1 = len f1 + 1 by Def36;
A16: 0+i<=1+i by XREAL_1:8;
    i in Seg len f1 by A1,FINSEQ_1:def 3;
    then
A17: 1<=i & i<=len f1 by FINSEQ_1:3;
    then 1<=i & i+1<=len f1 + 1 by XREAL_1:8;
    then 1<=i & i<=len s1 by A15,A16,XXREAL_0:2;
    then
A18: i in Seg len s1 by FINSEQ_1:3;
    then
A19: i in dom s1 by FINSEQ_1:def 3;
    then s1.i in the_stable_subgroups_of G by FINSEQ_2:13;
    then reconsider H1=s1.i as strict StableSubgroup of G by Def11;
    1+1<=i+1 & i + 1<=len f1 + 1 by A17,XREAL_1:8;
    then 1<=i+1 & i+1<=len s1 by A14,Def36,XXREAL_0:2;
    then
A20: i+1 in Seg len s1;
    then
A21: i+1 in dom s1 by FINSEQ_1:def 3;
    then s1.(i+1) in the_stable_subgroups_of G by FINSEQ_2:13;
    then reconsider N1=s1.(i+1) as strict StableSubgroup of G by Def11;
    reconsider N1 as normal StableSubgroup of H1 by A19,A21,Def31;
    f1.i in rng f1 by A1,FUNCT_1:12;
    then reconsider H = f1.i as strict GroupWithOperators of O by Th102;
    H is trivial by A2;
    then
A22: H1./.N1 is trivial by A1,A14,Def36;
    thus i in dom s1 by A18,FINSEQ_1:def 3;
    thus i+1 in dom s1 by A20,FINSEQ_1:def 3;
    thus thesis by A22,Th76;
  end;
end;

theorem Th104:
  i in dom s1 & i+1 in dom s1 & s1.i=s1.(i+1) & s2=Del(s1,i)
  implies the_series_of_quotients_of s2=Del(the_series_of_quotients_of s1,i)
proof
  assume
A1: i in dom s1;
  assume
A2: i+1 in dom s1;
  assume
A3: s1.i=s1.(i+1);
  assume
A4: s2=Del(s1,i);
  set f1 = the_series_of_quotients_of s1;
A5: i in Seg len s1 by A1,FINSEQ_1:def 3;
  then 1<=i & i<=len s1 by FINSEQ_1:3;
  then
A6: 1+1<=i+1 by XREAL_1:8;
  i+1 in Seg len s1 by A2,FINSEQ_1:def 3;
  then
A7: 1<=i+1 & i+1<=len s1 by FINSEQ_1:3;
  then
A8: 2 <= len s1 by A6,XXREAL_0:2;
  consider k be Nat such that
A9: len s1 = k + 1 & len Del(s1,i) = k by A1,FINSEQ_3:113;
A10: 1 < len s1 by A8,XXREAL_0:2;
  then
A11: len s1 = len f1 + 1 by Def36;
  1+1 <= len s2+1 by A4,A6,A7,A9,XXREAL_0:2;
  then
A12: 1 <= len s2 by XREAL_1:8;
  per cases by A12,XXREAL_0:1;
  suppose
A13: len s2 = 1;
    then
A14: the_series_of_quotients_of s2 = {} by Def36;
    1 in Seg len f1 by A4,A9,A11,A13;
    then 1 in dom f1 by FINSEQ_1:def 3;
    then consider k1 be Nat such that
A15: len f1 = k1 + 1 & len Del(f1,1) = k1 by FINSEQ_3:113;
    1<=i & i<=1 by A4,A5,A7,A9,A13,FINSEQ_1:3,XREAL_1:8;
    then len Del(f1,i) = 0 by A4,A9,A11,A13,A15,XXREAL_0:1;
    hence thesis by A14;
  end;
  suppose
A16: len s2 > 1;
    i+1-1<=len s1-1 by A7,XREAL_1:11;
    then 1 <= i & i <= len f1 by A5,A11,FINSEQ_1:3;
    then i in Seg len f1 by FINSEQ_1:3;
    then
A17: i in dom f1 by FINSEQ_1:def 3;
    then consider k1 be Nat such that
A18: len f1 = k1 + 1 & len Del(f1,i) = k1 by FINSEQ_3:113;
    now
      let n;
      assume
A19:  n in dom Del(f1,i);
      then
A20:  n in Seg len Del(f1,i) by FINSEQ_1:def 3;
      let H1, N1;
      assume
A21:  H1 = s2.n;
      assume
A22:  N1 = s2.(n+1);
      len f1-len Del(f1,i)+len Del(f1,i)>0+len Del(f1,i) by A18,XREAL_1:8;
      then Seg len Del(f1,i) c= Seg len f1 by FINSEQ_1:7;
      then n in Seg len f1 by A20;
      then
A23:  n in dom f1 by FINSEQ_1:def 3;
A24:  1<=n & n<=len Del(f1,i) by A20,FINSEQ_1:3;
A25:  n<=k1 by A18,A20,FINSEQ_1:3;
      set n1 = n+1;
A26:  n1<=k by A9,A11,A18,A25,XREAL_1:8;
      0+n<1+n by XREAL_1:8;
      then
A27:  n<=k by A26,XXREAL_0:2;
      1+1<=n+1 by A24,XREAL_1:8;
      then 1<=n1 by XXREAL_0:2;
      then n1 in Seg len f1 by A9,A11,A26;
      then
A28:  n1 in dom f1 by FINSEQ_1:def 3;
      reconsider n1 as Nat;
      per cases;
      suppose
A29:    n<i;
        then
A30:    n1<=i by NAT_1:13;
        per cases by A30,XXREAL_0:1;
        suppose
A31:      n1<i;
          reconsider n'=n as Element of NAT by INT_1:16;
          s1.n' = H1 & s1.(n'+1) = N1
          by A1,A4,A9,A21,A22,A29,A31,FINSEQ_3:119;
          then f1.n = H1./.N1 by A10,A23,Def36;
          hence Del(f1,i).n = H1./.N1 by A1,A18,A19,A29,FINSEQ_3:119;
        end;
        suppose n1=i;
          then s1.n = H1 & s1.(n+1) = N1
          by A1,A3,A4,A9,A19,A21,A22,A26,A29,FINSEQ_3:119,120;
          then f1.n = H1./.N1 by A10,A23,Def36;
          hence Del(f1,i).n = H1./.N1 by A1,A18,A19,A29,FINSEQ_3:119;
        end;
      end;
      suppose
A32:    n>=i;
A33:    0+i<1+i by XREAL_1:8;
        n+1>=i+1 by A32,XREAL_1:8;
        then
A34:    n1>=i by A33,XXREAL_0:2;
        reconsider n1'=n1 as Element of NAT;
        s1.n1' = H1 & s1.(n1'+1) = N1
        by A1,A4,A9,A19,A21,A22,A26,A27,A32,A34,FINSEQ_3:120;
        then f1.n1 = H1./.N1 by A10,A28,Def36;
        hence Del(f1,i).n = H1./.N1 by A17,A18,A19,A25,A32,FINSEQ_3:120;
      end;
    end;
    hence thesis by A4,A9,A11,A16,A18,Def36;
  end;
end;

theorem
  f1=the_series_of_quotients_of s1 & i in dom f1 &
  (for H st H = f1.i holds H is trivial)
  implies Del(s1,i) is CompositionSeries of G &
  for s2 st s2 = Del(s1,i) holds the_series_of_quotients_of s2 = Del(f1,i)
proof
  assume
A1: f1=the_series_of_quotients_of s1;
  assume
A2: i in dom f1;
  assume for H st H = f1.i holds H is trivial;
  then
A3: i in dom s1 & i+1 in dom s1 & s1.i=s1.(i+1) by A1,A2,Th103;
  hence Del(s1,i) is CompositionSeries of G by Th94,FINSEQ_3:114;
  let s2;
  assume s2 = Del(s1,i);
  hence thesis by A1,A3,Th104;
end;

theorem Th106:
  f1 = the_series_of_quotients_of s1 & f2 = the_series_of_quotients_of s2
  & i in dom f1 & (for H st H = f1.i holds H is trivial)
  & (ex p being Permutation of dom f1
  st f1,f2 are_equivalent_under p,O & j = p".i)
  implies ex p' being Permutation of dom Del(f1,i)
  st Del(f1,i),Del(f2,j) are_equivalent_under p',O
proof
  assume
A1: f1 = the_series_of_quotients_of s1;
  assume
A2: f2 = the_series_of_quotients_of s2;
  assume
A3: i in dom f1;
  assume
 for H st H = f1.i holds H is trivial;
  given p be Permutation of dom f1 such that
A5: f1,f2 are_equivalent_under p,O & j = p".i;
  rng(p") c= dom f1;
  then
A6: rng(p") c= Seg len f1 by FINSEQ_1:def 3;
  p".i in rng(p") by A3,FUNCT_2:6;
  then
A7: p".i in Seg len f1 by A6;
A8: len f1 = len f2 by A5,Def37;
  then
A9: j in dom f2 by A5,A7,FINSEQ_1:def 3;
A10: f1.i in rng f1 by A3,FUNCT_1:12;
  f2.j in rng f2 by A9,FUNCT_1:12;
  then reconsider H=f1.i,H2=f2.j
  as strict GroupWithOperators of O by A1,A2,A10,Th102;
  consider k1 be Nat such that
A13: len f1 = k1 + 1 & len Del(f1,i) = k1 by A3,FINSEQ_3:113;
  consider k2 be Nat such that
A14: len f2 = k2 + 1 & len Del(f2,j) = k2 by A9,FINSEQ_3:113;
A15: len f1=0 or len f1>=0+1 by NAT_1:13;
  per cases by A15,XXREAL_0:1;
  suppose len f1 = 0;
    then
A16: f1 = {};
    consider p' be Permutation of dom Del(f1,i);
A17: for H1,H2 being GroupWithOperators of O,l being Nat ,n
    st l in dom Del(f1,i) & n=p'".l & H1=Del(f1,i).l & H2=Del(f2,j).n
    holds H1,H2 are_isomorphic by A16;
    take p';
    thus thesis
    by A8,A13,A14,A17,Def37;
  end;
  suppose
 len f1 = 1;
    then
A19: Del(f1,i)={} by A13;
    reconsider p'={} as Function of dom {}, rng {} by FUNCT_2:3;
    reconsider p' as Function of {},{};
    p' is onto by FUNCT_2:def 3;
    then reconsider p' as Permutation of dom Del(f1,i) by A19;
    take p';
    for H1,H2 being GroupWithOperators of O,l being Nat,n
    st l in dom Del(f1,i) & n=p'".l & H1=Del(f1,i).l & H2=Del(f2,j).n
    holds H1,H2 are_isomorphic;
    hence thesis by A8,A13,A14,Def37;
  end;
  suppose
A20: len f1 > 1;
    set X = (dom f1)\{i};
    set Y = (dom f2)\{j};
    set p'=(Sgm X)" * p * Sgm Y;
    X c= dom f1 by XBOOLE_1:36;
    then
A21: X c= Seg len f1 by FINSEQ_1:def 3;
    then
A22: rng Sgm X = X by FINSEQ_1:def 13;
A23: dom f1 = Seg len f1 by FINSEQ_1:def 3
      .= Seg len f2 by A5,Def37
      .= dom f2 by FINSEQ_1:def 3;
A24: p.j = (p*p").i by A3,A5,FUNCT_2:21
      .= (id dom f1).i by FUNCT_2:88
      .= i by A3,FUNCT_1:35;
    Y c= dom f2 by XBOOLE_1:36;
    then
A25: Y c= Seg len f2 by FINSEQ_1:def 3;
    Y c= dom f2 by XBOOLE_1:36;
    then
A26: Y c= Seg len f2 by FINSEQ_1:def 3;
    then
A27: Sgm Y is one-to-one by FINSEQ_3:99;
A28: rng Sgm Y = Y by A26,FINSEQ_1:def 13;
A29: now
      assume Y={};
      then
A30:  dom f2 c= {j} by XBOOLE_1:37;
      {j} c= dom f2 by A9,ZFMISC_1:37;
      then
A31:  dom f2 = {j} by A30,XBOOLE_0:def 10;
      consider k be Nat such that
A32:  dom f2 = Seg k by FINSEQ_1:def 2;
      k in NAT by ORDINAL1:def 13;
      then k = len f2 by A32,FINSEQ_1:def 3;
      then k >= 1+1 by A8,A20,NAT_1:13;
      then Seg 2 c= Seg k by FINSEQ_1:7;
      then { 1,2 } = {j} by A31,A32,FINSEQ_1:4,ZFMISC_1:27;
      hence contradiction by ZFMISC_1:9;
    end;
A33: Sgm Y is Function of dom Sgm Y, rng Sgm Y by FUNCT_2:3;
A34: p' is Permutation of dom Del(f1,i) & p'" = (Sgm Y)" * (p") * (Sgm X)
    proof
A35:  Sgm X is one-to-one by A21,FINSEQ_3:99;
A36:  dom Del(f1,i) = Seg k1 by A13,FINSEQ_1:def 3;
A37:  {i} c= dom f1 by A3,ZFMISC_1:37;
A38:  X \/ {i} = dom f1 \/ {i} by XBOOLE_1:39
        .= dom f1 by A37,XBOOLE_1:12;
      card(X \/ {i}) = (card X) + card {i} by CARD_2:53,XBOOLE_1:79;
      then
A39:  (card X) + 1 = card(X \/ {i}) by CARD_1:50
        .= card Seg len f1 by A38,FINSEQ_1:def 3
        .= k1+1 by A13,FINSEQ_1:78;
A40:  {j} c= dom f2 by A9,ZFMISC_1:37;
A41:  Y \/ {j} = dom f2 \/ {j} by XBOOLE_1:39
        .= dom f2 by A40,XBOOLE_1:12;
      card(Y \/ {j}) = (card Y) + card {j} by CARD_2:53,XBOOLE_1:79;
      then (card Y) + 1 = card(Y \/ {j}) by CARD_1:50
        .= card Seg len f2 by A41,FINSEQ_1:def 3
        .= card Seg len f1 by A5,Def37
        .= k1+1 by A13,FINSEQ_1:78;
      then
A42:  dom(Sgm Y) = Seg k1 by A25,FINSEQ_3:45;
A43:  dom((Sgm X)") = X by A22,A35,FUNCT_1:55;
      then dom((Sgm X)") c= dom f1 by XBOOLE_1:36;
      then
A44:  dom((Sgm X)") c= rng p by FUNCT_2:def 3;
A45:  now
        let x;
        assume x in dom((Sgm X)" * p);
        then
A46:    x in dom p & p.x in dom((Sgm X)") by FUNCT_1:21;
        then x in dom f1 & p.x in X by A22,A35,FUNCT_1:55;
        then not p.x in {i} by XBOOLE_0:def 5;
        then p.x <> i by TARSKI:def 1;
        then x in dom f2 & not x in {j} by A23,A24,A46,TARSKI:def 1;
        hence x in Y by XBOOLE_0:def 5;
      end;
      now
        let x;
        assume
A47:    x in Y;
        then x in dom f2 & not x in {j} by XBOOLE_0:def 5;
        then
A48:    x <> j by TARSKI:def 1;
        dom f1 = dom p by A3,FUNCT_2:def 1;
        then
A49:    x in dom p by A23,A47,XBOOLE_0:def 5;
        dom f1 = rng p by FUNCT_2:def 3;
        then
A50:    p.x in dom f1 by A49,FUNCT_1:12;
        p.x <> i by A9,A23,A24,A48,A49,FUNCT_2:77;
        then not p.x in {i} by TARSKI:def 1;
        then p.x in X by A50,XBOOLE_0:def 5;
        hence x in dom((Sgm X)" * p) by A43,A49,FUNCT_1:21;
      end;
      then dom((Sgm X)" * p) = Y by A45,TARSKI:2;
      then
A51:  dom((Sgm X)" * p) = rng(Sgm Y) by A25,FINSEQ_1:def 13;
      then rng((Sgm X)" * p * Sgm Y) = rng((Sgm X)" * p) by RELAT_1:47
        .= rng((Sgm X)") by A44,RELAT_1:47
        .= dom(Sgm X) by A35,FUNCT_1:55;
      then
A52:  rng p'=Seg k1 by A21,A39,FINSEQ_3:45;
      dom p'=Seg k1 by A42,A51,RELAT_1:46;
      then reconsider p' as Function of dom Del(f1,i),dom Del(f1,i)
      by A36,A52,FUNCT_2:3;
A53:  p' is onto by A36,A52,FUNCT_2:def 3;
A54:  Sgm Y is one-to-one by A25,FINSEQ_3:99;
      (Sgm X)" is one-to-one by A35;
      then (Sgm X)" * p is one-to-one;
      then p' is one-to-one by A54;
      then reconsider p' as Permutation of dom Del(f1,i) by A53;
      set p''=(Sgm Y)" * (p") * (Sgm X);
A55:  Sgm Y is one-to-one by A25,FINSEQ_3:99;
      set R1=(Sgm Y)";
      set R2=Sgm Y;
      set R3=(Sgm X)";
      set R4=Sgm X;
      set R5=p";
      set R6=p;
      set R7=p';
      reconsider R1,R2,R3,R4,R5,R6,R7,p',p'' as Function;
A56:  R3=R4~ by A35,FUNCT_1:def 9;
A57:  R5=R6~ by FUNCT_1:def 9;
      reconsider R1,R2,R3,R4,R5,R6,R7 as Relation;
      p'"=R7~ by FUNCT_1:def 9
        .=((R6*R3)~)*(R2~) by RELAT_1:54
        .=((R3)~*(R6~))*(R2~) by RELAT_1:54
        .=((R4~)~*R5)*R1 by A55,A56,A57,FUNCT_1:def 9
        .=p'' by RELAT_1:55;
      hence thesis;
    end;
    then reconsider p' as Permutation of dom Del(f1,i);
    take p';
    now
      let H1,H2 be GroupWithOperators of O,l being Nat ,n;
      assume
A58:  l in dom Del(f1,i);
      assume
A59:  n=p'".l;
      assume
A60:  H1 = Del(f1,i).l & H2 = Del(f2,j).n;
A61:  l in dom (p'") by A58,FUNCT_2:def 1;
      then n in rng (p'") by A59,FUNCT_1:12;
      then n in dom Del(f1,i);
      then n in Seg len Del(f2,j) by A8,A13,A14,FINSEQ_1:def 3;
      then
A62:  n in dom Del(f2,j) by FINSEQ_1:def 3;
A63:  dom Del(f1,i) c= dom Sgm X by RELAT_1:44;
A64:  dom Del(f2,j) c= dom Sgm Y by RELAT_1:44;
      set l1=(Sgm X).l;
      set n1=(Sgm Y).n;
      l1 in rng Sgm X by A58,A63,FUNCT_1:12;
      then
A65:  l1 in dom f1 by A22,XBOOLE_0:def 5;
      reconsider l1 as Nat;
      reconsider n1 as Nat;
A66:  H1 = f1.l1 by A58,A60,A63,FUNCT_1:23;
A67:  H2 = f2.n1 by A60,A62,A64,FUNCT_1:23;
A68:  (Sgm Y)*(p'")
      = (Sgm Y) * ((Sgm Y)" * ((p") * (Sgm X))) by A34,RELAT_1:55
        .= ((Sgm Y) * (Sgm Y)") * ((p") * (Sgm X)) by RELAT_1:55
        .= id Y * ((p") * (Sgm X)) by A27,A28,A29,A33,FUNCT_2:35
        .= id Y * p" * (Sgm X) by RELAT_1:55
        .= (Y|p")*(Sgm X) by RELAT_1:123;
A69:  dom f1 = rng p by FUNCT_2:def 3;
      then
A70:  l1 in dom(p") by A65,FUNCT_1:55;
      then
A71:  p".l1 in rng(p") by FUNCT_1:12;
      now
        assume p".l1 in {j};
        then
A72:    p".l1=p".i by A5,TARSKI:def 1;
        i in dom(p") by A3,A69,FUNCT_1:55;
        then l1=i by A70,A72,FUNCT_1:def 8;
        then i in rng Sgm X by A58,A63,FUNCT_1:12;
        then not i in {i} by A22,XBOOLE_0:def 5;
        hence contradiction by TARSKI:def 1;
      end;
      then
A73:  p".l1 in Y by A23,A71,XBOOLE_0:def 5;
      n1 = ((Sgm Y)*(p'")).l by A59,A61,FUNCT_1:23
        .= (Y|p").l1 by A58,A63,A68,FUNCT_1:23
        .= p".l1 by A65,A73,FUNCT_2:41;
      hence H1,H2 are_isomorphic by A5,A65,A66,A67,Def37;
    end;
    hence thesis by A8,A13,A14,Def37;
  end;
end;

theorem Th107:
  for G1,G2 being GroupWithOperators of O,
  s1 being CompositionSeries of G1, s2 being CompositionSeries of G2
  st s1 is empty & s2 is empty holds s1 is_equivalent_with s2
proof
  let G1,G2 be GroupWithOperators of O;
  let s1 be CompositionSeries of G1;
  let s2 be CompositionSeries of G2;
  assume
A1: s1 is empty;
  assume
A2: s2 is empty;
  for n st n + 1 = len s1 holds ex p being Permutation of Seg n st
  for H1 being StableSubgroup of G1, H2 being StableSubgroup of G2,
  N1 being normal StableSubgroup of H1,
  N2 being normal StableSubgroup of H2, i,j
  st 1<=i & i<=n & j=p.i & H1=s1.i & H2=s2.j & N1=s1.(i+1) & N2=s2.(j+1)
  holds H1./.N1,H2./.N2 are_isomorphic by A1;
  hence thesis by A1,A2,Def35;
end;

theorem Th108:
  for G1,G2 being GroupWithOperators of O,
  s1 be CompositionSeries of G1, s2 be CompositionSeries of G2
  st s1 is not empty & s2 is not empty holds s1 is_equivalent_with s2 iff
  ex p being Permutation of dom the_series_of_quotients_of s1 st
  the_series_of_quotients_of s1,the_series_of_quotients_of s2
  are_equivalent_under p,O
proof
  let G1,G2 be GroupWithOperators of O;
  let s1 be CompositionSeries of G1,s2 be CompositionSeries of G2;
  assume
A1: s1 is not empty & s2 is not empty;
  set f1 = the_series_of_quotients_of s1;
  set f2 = the_series_of_quotients_of s2;
  hereby
    assume
A2: s1 is_equivalent_with s2;
    then
A3: len s1 = len s2 by Def35;
    per cases;
    suppose
A4:   len s1 <= 1;
      consider p be Permutation of dom the_series_of_quotients_of s1;
      take p;
      reconsider fs1=f1,fs2=f2 as FinSequence;
A5:   fs1 = {} by A4,Def36;
A6:   fs2 = {} by A3,A4,Def36;
      reconsider pf=p as Permutation of dom fs1;
A7:   len f1 = len f2 by A4,A6,Def36;
      for H1,H2 being GroupWithOperators of O, i,j
      st i in dom fs1 & j=pf".i & H1=fs1.i & H2=fs2.j
      holds H1,H2 are_isomorphic by A5;
      hence the_series_of_quotients_of s1,the_series_of_quotients_of s2
      are_equivalent_under p,O by A7,Def37;
    end;
    suppose
A8:   len s1 > 1;
      then
A9:   len s1 - 1 > 1-1 by XREAL_1:11;
      set n = len s1 - 1;
      n in NAT by A9,INT_1:16;
      then reconsider n as Nat;
      n+1 = len s1;
      then consider p be Permutation of Seg n such that
A10:  for H1 being StableSubgroup of G1, H2 being StableSubgroup of G2,
      N1 being normal StableSubgroup of H1,
      N2 being normal StableSubgroup of H2, i,j
      st 1<=i & i<=n & j=p.i & H1=s1.i & H2=s2.j & N1=s1.(i+1) & N2=s2.(j+1)
      holds H1./.N1,H2./.N2 are_isomorphic by A2,Def35;
A11:  len s1 = len the_series_of_quotients_of s1 + 1 by A8,Def36;
      then dom the_series_of_quotients_of s1 = Seg n by FINSEQ_1:def 3;

then reconsider p'=p" as Permutation of dom the_series_of_quotients_of s1;
      take p';
      reconsider fs1=f1,fs2=f2 as FinSequence;
      reconsider pf=p' as Permutation of dom fs1;
A12:  len s2 = len the_series_of_quotients_of s2 + 1 by A3,A8,Def36;
A13:  pf" = p by FUNCT_1:65;
      now
        let H1',H2' be GroupWithOperators of O;
        let i,j;
        assume
A14:    i in dom fs1;
        assume
A15:    j = pf".i;
        assume
A16:    H1'=fs1.i & H2'=fs2.j;
        set H1=s1.i;
        set H2=s2.j;
        set N1=s1.(i+1);
        set N2=s2.(j+1);
A17:    i in Seg len fs1 by A14,FINSEQ_1:def 3;
        then
A18:    1<=i & i<=len fs1 by FINSEQ_1:3;
        0+len fs1<1+len fs1 by XREAL_1:8;
        then i<=len s1 by A11,A18,XXREAL_0:2;
        then i in Seg len s1 by A18,FINSEQ_1:3;
        then
A19:    i in dom s1 by FINSEQ_1:def 3;
        0+i<1+i by XREAL_1:8;
        then
A20:    1<=i+1 by A18,XXREAL_0:2;
        i+1<=len fs1+1 by A18,XREAL_1:8;
        then i+1 in Seg len s1 by A11,A20;
        then
A21:    i+1 in dom s1 by FINSEQ_1:def 3;
        i in dom p by A11,A17,FUNCT_2:def 1;
        then
A22:    j in rng p by A13,A15,FUNCT_1:12;
        then
A23:    j in Seg len f2 by A3,A12;
A24:    1<=j & j<=len fs2 by A3,A12,A22,FINSEQ_1:3;
        0+len fs2<1+len fs2 by XREAL_1:8;
        then j<=len s2 by A12,A24,XXREAL_0:2;
        then j in Seg len s2 by A24,FINSEQ_1:3;
        then
A25:    j in dom s2 by FINSEQ_1:def 3;
        0+j<1+j by XREAL_1:8;
        then
A26:    1<=j+1 by A24,XXREAL_0:2;
        j+1<=len fs2+1 by A24,XREAL_1:8;
        then j+1 in Seg len s2 by A12,A26;
        then
A27:    j+1 in dom s2 by FINSEQ_1:def 3;
        reconsider H1,N1 as Element of the_stable_subgroups_of G1
        by A19,A21,FINSEQ_2:13;
        reconsider H1,N1 as StableSubgroup of G1 by Def11;
        reconsider N1 as normal StableSubgroup of H1 by A19,A21,Def31;
        reconsider H2,N2 as Element of the_stable_subgroups_of G2
        by A25,A27,FINSEQ_2:13;
        reconsider H2,N2 as StableSubgroup of G2 by Def11;
        reconsider N2 as normal StableSubgroup of H2 by A25,A27,Def31;
A28:    j in dom fs2 by A23,FINSEQ_1:def 3;
        dom fs1 = Seg n by A11,FINSEQ_1:def 3;
        then 1<=i & i<=n by A14,FINSEQ_1:3;
        then
A29:    H1./.N1,H2./.N2 are_isomorphic by A10,A13,A15;
        H2./.N2 = H2' by A3,A8,A16,A28,Def36;
        hence H1',H2' are_isomorphic by A8,A14,A16,A29,Def36;
      end;
      hence the_series_of_quotients_of s1,the_series_of_quotients_of s2
      are_equivalent_under p',O by A3,A11,A12,Def37;
    end;
  end;
  given p be Permutation of dom the_series_of_quotients_of s1 such that
A30: the_series_of_quotients_of s1,the_series_of_quotients_of s2
  are_equivalent_under p,O;
A31: len f1 = len f2 &
  for H1 being StableSubgroup of G1, H2 being StableSubgroup of G2, i,j
  st i in dom f1 & j=p".i & H1=f1.i & H2=f2.j
  holds H1,H2 are_isomorphic by A30,Def37;
  per cases;
  suppose
A32: len s1<=1;
    len s1<>0 by A1;
    then len s1>0;
    then
A33: len s1>=0+1 by NAT_1:13;
A34: f1 = {} by A32,Def36;
    now
      assume
A35:  len s2<>1;
      len s2<>0 by A1;
      then len s2>0;
      then len s2>=0+1 by NAT_1:13;
      then len s2>1 by A35,XXREAL_0:1;
      then len f2 + 1 > 0 + 1 by Def36;
      hence contradiction by A31,A34;
    end;
    then
A36: len s1 = len s2 by A32,A33,XXREAL_0:1;
    now
      let n;
      assume n + 1 = len s1;
      then n+1=1 by A32,A33,XXREAL_0:1;
      then
A37:  n=0;
      consider p be Permutation of Seg n;
      take p;
      let H1 be StableSubgroup of G1;
      let H2 be StableSubgroup of G2;
      let N1 be normal StableSubgroup of H1;
      let N2 be normal StableSubgroup of H2;
      let i,j;
      assume
A38:  1<=i & i<=n & j=p.i;
      assume H1=s1.i & H2=s2.j;
      assume N1=s1.(i+1) & N2=s2.(j+1);
      thus H1./.N1,H2./.N2 are_isomorphic by A37,A38;
    end;
    hence thesis by A36,Def35;
  end;
  suppose
A39: len s1>1;
    then
A40: len s1 = len f1 + 1 & for i st i in dom f1
    for H1 being StableSubgroup of G1, N1 being normal StableSubgroup of H1
    st H1=s1.i & N1=s1.(i+1) holds f1.i = H1./.N1 by Def36;
A41: now
      assume len s2<=1;
      then f2 = {} by Def36;
      then len f2 = 0;
      hence contradiction by A31,A39,A40;
    end;
    then
A42: len s1 = len s2 by A31,A40,Def36;
    now
      let n;
      assume
A43:  n + 1 = len s1;
      then
A44:  dom f1 = Seg n by A40,FINSEQ_1:def 3;
      then reconsider p'=p" as Permutation of Seg n;
      take p';
      let H1 be StableSubgroup of G1;
      let H2 be StableSubgroup of G2;
      let N1 be normal StableSubgroup of H1;
      let N2 be normal StableSubgroup of H2;
      let i,j;
      assume 1<=i & i<=n;
      then
A45:  i in dom f1 by A44,FINSEQ_1:3;
      then
A46:  i in dom p' by FUNCT_2:def 1;
      assume
A47:  j=p'.i;
      assume
A48:  H1=s1.i & H2=s2.j;
      assume
A49:  N1=s1.(i+1) & N2=s2.(j+1);
      then
A50:  f1.i = H1./.N1 by A39,A45,A48,Def36;
      j in rng p' by A46,A47,FUNCT_1:12;
      then j in Seg n;
      then j in dom f2 by A31,A40,A43,FINSEQ_1:def 3;
      then f2.j = H2./.N2 by A41,A48,A49,Def36;
      hence H1./.N1,H2./.N2 are_isomorphic by A30,A45,A47,A50,Def37;
    end;
    hence thesis by A42,Def35;
  end;
end;

theorem Th109:
  s1 is_finer_than s2 & s2 is jordan_holder & len s1 > len s2
  implies ex i st i in dom the_series_of_quotients_of s1 &
  for H st H = (the_series_of_quotients_of s1).i holds H is trivial
proof
  assume
A1: s1 is_finer_than s2;
  assume
A2: s2 is jordan_holder;
  assume
A3: len s1 > len s2;
  then not s1 is strictly_decreasing by A1,A2,Def34;
  then not for i st i in dom s1 & i+1 in dom s1
  for H1,N1 st H1=s1.i & N1=s1.(i+1) holds not H1./.N1 is trivial by Def33;
  then consider i,H1,N1 such that
A4: i in dom s1 & i+1 in dom s1 and
A5: H1=s1.i & N1=s1.(i+1) and
A6: H1./.N1 is trivial;
  i+1 in Seg len s1 by A4,FINSEQ_1:def 3;
  then
A7: i+1 <= len s1 by FINSEQ_1:3;
  0+1 <= i+1 by XREAL_1:8;
  then
A8: 1 <= len s1 by A7,XXREAL_0:2;
  per cases;
  suppose len s1 <= 1;
    then
A9: len s1 = 1 by A8,XXREAL_0:1;
    now
      let i;
      assume i in dom s1;
      then i in Seg 1 by A9,FINSEQ_1:def 3;
      then
A10:  i = 1 by FINSEQ_1:4,TARSKI:def 1;
      assume i+1 in dom s1;
      then
A11:  2 in Seg 1 by A9,A10,FINSEQ_1:def 3;
      let H1,N1;
      assume H1=s1.i;
      assume N1=s1.(i+1);
      assume H1./.N1 is trivial;
      thus contradiction by A11,FINSEQ_1:4,TARSKI:def 1;
    end;
    then s1 is strictly_decreasing by Def33;
    hence thesis by A1,A2,A3,Def34;
  end;
  suppose
A12: len s1 > 1;
    then
A13: len s1 = len the_series_of_quotients_of s1 + 1 &
    for i st i in dom the_series_of_quotients_of s1
    for H1, N1 st H1=s1.i & N1=s1.(i+1) holds
    (the_series_of_quotients_of s1).i = H1./.N1 by Def36;
    i in Seg len s1 by A4,FINSEQ_1:def 3;
    then 1 <= i & i+1-1 <= len s1 - 1 by A7,FINSEQ_1:3,XREAL_1:11;
    then
A14: i in Seg len the_series_of_quotients_of s1 by A13,FINSEQ_1:3;
    take i; thus
A15: i in dom the_series_of_quotients_of s1 by A14,FINSEQ_1:def 3;
    let H;
    assume H = (the_series_of_quotients_of s1).i;
    hence thesis by A5,A6,A12,A15,Def36;
  end;
end;

:: this is Lm15 of WSIERP_1

Lm40: for k,m being Element of NAT holds k<m iff k<=m-1
proof
  let k,m be Element of NAT;
A1: now
    assume k<m;
    then k+1<=m by INT_1:20;
    hence k<=m-1 by XREAL_1:21;
  end;
  now
    assume k<=m-1;
    then
A2: k+1<=m by XREAL_1:21;
    k<k+1 by XREAL_1:31;
    hence k<m by A2,XXREAL_0:2;
  end;
  hence thesis by A1;
end;

:: this is Lm19 of WSIERP_1

Lm41: for a being Element of NAT, fs being FinSequence holds
a in dom fs implies ex fs1,fs2 being FinSequence st fs=fs1^<*fs.a*>^fs2 &
len fs1=a-1 & len fs2=len fs -a
proof
  let a be Element of NAT;
  let fs be FinSequence;
  assume
A1: a in dom fs;
  then a>=1 & a<=len fs by FINSEQ_3:27;
  then reconsider b=len fs-a, d=a-1 as Element of NAT by INT_1:18;
  len fs=a+b;
  then consider fs3,fs2 be FinSequence such that
A2: len fs3=a & len fs2=b & fs=fs3^fs2 by FINSEQ_2:25;
  a<>0 by A1,FINSEQ_3:27;
  then fs3 <> {} by A2;
  then a in dom fs3 by A2,FINSEQ_5:6;
  then
A3: fs3.a=fs.a by A2,FINSEQ_1:def 7;
  a=d+1;
  then consider fs1 be FinSequence,v be set such that
A4: fs3=fs1^<*v*> by A2,FINSEQ_2:21;
A5: len fs1 + 1=d+1 by A2,A4,FINSEQ_2:19;
  then fs.a=v by A3,A4,FINSEQ_1:59;
  hence thesis by A2,A4,A5;
end;

:: this is Lm20 of WSIERP_1

Lm42: for a being Element of NAT, fs,fs1,fs2 being FinSequence, v being set
holds a in dom fs & fs=fs1^<*v*>^fs2 & len fs1=a-1 implies Del(fs,a)=fs1^fs2
proof
  let a be Element of NAT;
  let fs,fs1,fs2 be FinSequence;
  let v be set;
  assume
A1: a in dom fs & fs=fs1^<*v*>^fs2 & len fs1=a-1;
  then
A2: len fs=len(fs1^<*v*>)+len fs2 by FINSEQ_1:35
    .=len fs1 +1 +len fs2 by FINSEQ_2:19
    .=a +len fs2 by A1;
A3: fs=fs1^(<*v*>^fs2) by A1,FINSEQ_1:45;
A4: len<*v*>=1 by FINSEQ_1:56;
  len fs=(a-1) + len(<*v*>^fs2) by A1,A3,FINSEQ_1:35;
  then
A5: len(<*v*>^fs2)=len fs -(a-1);
A6: len(Del(fs,a))+1=len fs by A1,WSIERP_1:def 1;
  then len(Del(fs,a)) =len fs2 +len fs1 by A1,A2;
  then
A7: len(fs1^fs2)=len(Del(fs,a)) by FINSEQ_1:35;
  now
    let e be Nat;
    assume
A8: 1<=e & e<=len Del(fs,a);
    reconsider e1=e as Element of NAT by ORDINAL1:def 13;
    now per cases;
      suppose
A9:     e<a;
        then e1<=a-1 by Lm40;
        then
A10:    e in dom fs1 by A1,A8,FINSEQ_3:27;
        hence (fs1^fs2).e=fs1.e by FINSEQ_1:def 7
          .=fs.e1 by A3,A10,FINSEQ_1:def 7
          .=(Del(fs,a)).e by A1,A9,WSIERP_1:def 1;
      end;
      suppose
A11:    e>=a;
        then
A12:    e1>a-1 by Lm40;
        then
A13:    e+1>a by XREAL_1:21;
        then e+1-a>0 by XREAL_1:52;
        then
A14:    e+1-a+1>0+1 by XREAL_1:8;
A15:    e+1>a-1 by A13,XREAL_1:148,XXREAL_0:2;
        then e+1-(a-1)>0 by XREAL_1:52;
        then reconsider f=e+1-(a-1) as Element of NAT by INT_1:16;
A16:    e+1<=len fs by A6,A8,XREAL_1:8;
        then
A17:    e+1-(a-1)<=len(<*v*>^fs2) by A5,XREAL_1:11;
        thus (fs1^fs2).e=fs2.(e-len fs1) by A1,A7,A8,A12,FINSEQ_1:37
          .=fs2.(f-1) by A1
          .=(<*v*>^fs2).f by A4,A14,A17,FINSEQ_1:37
          .=(fs1^(<*v*>^fs2)).(e1+1) by A1,A3,A15,A16,FINSEQ_1:37
          .=(Del(fs,a)).e by A1,A3,A11,WSIERP_1:def 1;
      end;
    end;
    hence (fs1^fs2).e=(Del(fs,a)).e;
  end;
  hence thesis by A7,FINSEQ_1:18;
end;

:: this is Lm22 of WSIERP_1

Lm43: for a being Element of NAT, fs1,fs2 being FinSequence holds
(a<=len fs1 implies Del(fs1^fs2,a)=Del(fs1,a)^fs2) &
(a>=1 implies Del(fs1^fs2,len fs1 +a)=fs1^Del(fs2,a))
proof
  let a be Element of NAT;
  let fs1,fs2 be FinSequence;
  set f=fs1^fs2;
A1: len f=len fs1 + len fs2 &
  len((Del(fs1,a))^fs2)=len(Del(fs1,a)) + len fs2 &
  len(fs1^Del(fs2,a))=len fs1 + len Del(fs2,a) by FINSEQ_1:35;
A2: now
    assume
A3: a<=len fs1;
    set f1= Del(f,a);
    set f2=((Del(fs1,a))^fs2);
    set f3=<*f.a*>;
    len fs1<=len f by A1,NAT_1:11;
    then
A4: a<=len f by A3,XXREAL_0:2;
    now per cases;
      suppose a<1;
        then
A5:     not a in dom fs1 & not a in dom f by FINSEQ_3:27;
        hence f1=f by WSIERP_1:def 1
          .= f2 by A5,WSIERP_1:def 1;
      end;
      suppose a>=1;
        then
A6:     a in dom f & a in dom fs1 by A3,A4,FINSEQ_3:27;
        then consider g1,g2 being FinSequence such that
A7:     f=g1^f3^g2 & len g1=a-1 & len g2=len f -a by Lm41;
        len(g1^f3)=a-1+1 by A7,FINSEQ_2:19
          .=a;
        then consider t being FinSequence such that
A8:     fs1=g1^f3^t by A3,A7,FINSEQ_1:64;
A9:     Del(fs1,a)=g1^t by A6,A7,A8,Lm42;
        g1^f3^g2=g1^f3^(t^fs2) by A7,A8,FINSEQ_1:45;
        then
A10:    g2=t^fs2 by FINSEQ_1:46;
        thus f1=g1^g2 by A6,A7,Lm42
          .=f2 by A9,A10,FINSEQ_1:45;
      end;
    end;
    hence f1=f2;
  end;
  now
    assume
A11: a>=1;
    set f1= Del(f,len fs1 + a);
    set f2=fs1^Del(fs2,a);
    now per cases;
      suppose
A12:    a>len fs2;
        then
A13:    not a in dom fs2 by FINSEQ_3:27;
        len fs1 + a>len f by A1,A12,XREAL_1:8;
        then not (len fs1 + a) in dom f by FINSEQ_3:27;
        hence f1=fs1^fs2 by WSIERP_1:def 1
          .=f2 by A13,WSIERP_1:def 1;
      end;
      suppose
A14:    a<=len fs2;
        then
A15:    a in dom fs2 by A11,FINSEQ_3:27;
A16:    len fs1 + a <= len f by A1,A14,XREAL_1:8;
        len fs1 + a>=1 by A11,NAT_1:12;
        then
A17:    (len fs1 + a) in dom f by A16,FINSEQ_3:27;
        then consider g1,g2 being FinSequence such that
A18:    f=g1^<*f.(len fs1 +a)*>^g2 & len g1=len fs1 +a -1
        & len g2=len f -(len fs1 +a) by Lm41;
A19:    f1=g1^g2 by A17,A18,Lm42;
A20:    f=g1^(<*f.(len fs1 +a)*>^g2) by A18,FINSEQ_1:45;
        a-1>=0 by A11,XREAL_1:50;
        then (a-1)+len fs1>=0+len fs1 by XREAL_1:8;
        then consider t being FinSequence such that
A21:    fs1^t=g1 by A18,A20,FINSEQ_1:64;
        fs1^(t^<*f.(len fs1 +a)*>^g2)=fs1^(t^<*f.(len fs1 +a)*>)^g2 by
FINSEQ_1:45
          .=f by A18,A21,FINSEQ_1:45;
        then
A22:    fs2=t^<*f.(len fs1 +a)*>^g2 by FINSEQ_1:46;
        len fs1 +(a-1)=len fs1 +len t by A18,A21,FINSEQ_1:35;
        then Del(fs2,a)=t^g2 by A15,A22,Lm42;
        hence f1=f2 by A19,A21,FINSEQ_1:45;
      end;
    end;
    hence f1=f2;
  end;
  hence thesis by A2;
end;

Lm44: for D being non empty set, f being FinSequence of D,
p being Element of D, n being Nat st n in dom f holds f = Del(Ins(f,n,p),n+1)
proof
  let D be non empty set;
  let f be FinSequence of D;
  let p be Element of D;
  let n be Nat;
  assume n in dom f;
  then n in Seg len f by FINSEQ_1:def 3;
  then
A1: 1<=n & n<= len f by FINSEQ_1:3;
  set fs1=f|n^<*p*>;
  set fs2=(f/^n);
A2: len(f|n) = n by A1,FINSEQ_1:80;
  len fs1 = len(f|n) + len <*p*> by FINSEQ_1:35
    .= n + 1 by A2,FINSEQ_1:56;
  then Del(Ins(f,n,p),n+1) = Del(fs1,n+1)^fs2 by Lm43
    .= f|n^fs2 by A2,WSIERP_1:48;
  hence thesis by RFINSEQ:21;
end;

:: ALG I.4.7 Proposition 9

theorem Th110:
  len s1 > 1 implies (s1 is jordan_holder iff
  for i st i in dom the_series_of_quotients_of s1 holds
  (the_series_of_quotients_of s1).i is strict simple GroupWithOperators of O)
proof
  assume
A1: len s1 > 1;
A2: now
    assume
A3: s1 is jordan_holder;
    assume not for i st i in dom the_series_of_quotients_of s1 holds
    (the_series_of_quotients_of s1).i is
    strict simple GroupWithOperators of O;
    then consider i such that
A4: i in dom the_series_of_quotients_of s1 and
A5: (the_series_of_quotients_of s1).i is
    not strict simple GroupWithOperators of O;
A6: len s1 = len the_series_of_quotients_of s1 + 1 by A1,Def36;
    0+len the_series_of_quotients_of s1<
    1+len the_series_of_quotients_of s1 by XREAL_1:8;
    then
A7: len the_series_of_quotients_of s1 < len s1 by A1,Def36;
A8: i in Seg len the_series_of_quotients_of s1 by A4,FINSEQ_1:def 3;
    then
A9: 1<=i & i<=len the_series_of_quotients_of s1 by FINSEQ_1:3;
    then
A10: i<=len s1 by A7,XXREAL_0:2;
    then i in Seg len s1 by A9,FINSEQ_1:3;
    then
A11: i in dom s1 by FINSEQ_1:def 3;
A12: 0+1<=i+1 by XREAL_1:8;
A13: i+1 <= len s1 by A6,A9,XREAL_1:8;
    then i+1 in Seg len s1 by A12;
    then
A14: i+1 in dom s1 by FINSEQ_1:def 3;
    then reconsider H1=s1.i,N1=s1.(i+1) as Element of the_stable_subgroups_of G
    by A11,FINSEQ_2:13;
    reconsider H1,N1 as strict StableSubgroup of G by Def11;
    reconsider N1 as strict normal StableSubgroup of H1 by A11,A14,Def31;
A15: H1./.N1 is not strict simple GroupWithOperators of O by A1,A4,A5,Def36;
    per cases by A15,Def13;
    suppose
A16:  H1./.N1 is trivial;
      s1 is strictly_decreasing by A3,Def34;
      hence contradiction by A11,A14,A16,Def33;
    end;
    suppose ex H being strict normal StableSubgroup of H1./.N1
      st H <> (Omega).(H1./.N1) & H <> (1).(H1./.N1);
      then consider H be strict normal StableSubgroup of H1./.N1 such that
A17:  H <> (Omega).(H1./.N1) & H <> (1).(H1./.N1);
      N1 = Ker nat_hom N1 by Th48;
      then consider N2 be strict StableSubgroup of H1 such that
A18:  the carrier of N2 = (nat_hom N1)"(the carrier of H) &
      (H is normal implies N1 is normal StableSubgroup of N2 &
      N2 is normal) by Th78;
      reconsider i as Element of NAT by ORDINAL1:def 13;
      N2 is strict StableSubgroup of G by Th11;
      then reconsider N2 as Element of the_stable_subgroups_of G by Def11;
      set s2 = Ins(s1,i,N2);
A19:  1<=i & s1 is non empty by A1,A8,FINSEQ_1:3;
      1 <= len s1 + 1 by A1,NAT_1:13;
      then
A20:  1 <= len s2 by FINSEQ_5:72;
      then 1 in Seg len s2;
      then
A21:  1 in dom s2 by FINSEQ_1:def 3;
      1 in Seg len s1 by A1;
      then
A22:  1 in dom s1 by FINSEQ_1:def 3;
      len s2 in Seg len s2 by A20;
      then
A23:  len s2 in dom s2 by FINSEQ_1:def 3;
      len s1 in Seg len s1 by A1;
      then
A24:  len s1 in dom s1 by FINSEQ_1:def 3;
A25:  s2.1 = s2/.1 by A21,PARTFUN1:def 8
        .= s1/.1 by A19,FINSEQ_5:78
        .= s1.1 by A22,PARTFUN1:def 8
        .= (Omega).G by Def31;
A26:  s2.(len s2) = s2/.(len s2) by A23,PARTFUN1:def 8
        .= s2/.(len s1+1) by FINSEQ_5:72
        .= s1/.(len s1) by A13,FINSEQ_5:77
        .= s1.(len s1) by A24,PARTFUN1:def 8
        .= (1).G by Def31;
A27:  len s2 = len s1 + 1 by FINSEQ_5:72;
      then
A28:  s1<>s2;
      now
        let j be Nat;
        assume
A29:    j in dom s2;
        then j in Seg len s2 by FINSEQ_1:def 3;
        then
A30:    1<=j & j<=len s2 by FINSEQ_1:3;
        assume
A31:    j+1 in dom s2;
        then j+1 in Seg len s2 by FINSEQ_1:def 3;
        then
A32:    1<=j+1 & j+1<=len s2 by FINSEQ_1:3;
        let H1',H2';
        assume
A33:    H1'=s2.j;
        assume
A34:    H2'=s2.(j+1);
        j<i or j=i or j>i by XXREAL_0:1;
        then j+1<=i or j=i or j>=i+1 by NAT_1:13;
        then
A35:    j+1-1<=i-1 or j=i or j>=i+1 by XREAL_1:11;
        per cases by A35,XXREAL_0:1;
        suppose
A36:      j<=i-1;
A37:      Seg len(s1|i) = Seg i by A7,A9,FINSEQ_1:80,XXREAL_0:2;
A38:      dom(s1|i) c= dom s1 by RELAT_1:89;
          -1+i<0+i by XREAL_1:8;
          then j<=i by A36,XXREAL_0:2;
          then j in Seg len (s1|i) by A30,A37,FINSEQ_1:3;
          then
A39:      j in dom(s1|i) by FINSEQ_1:def 3;
          j+1<=i-1+1 by A36,XREAL_1:8;
          then j+1 in Seg len (s1|i) by A32,A37;
          then
A40:      j+1 in dom(s1|i) by FINSEQ_1:def 3;
A41:      s2.j = s2/.j by A29,PARTFUN1:def 8
            .= s1/.j by A39,FINSEQ_5:75
            .= s1.j by A38,A39,PARTFUN1:def 8;
          s2.(j+1) = s2/.(j+1) by A31,PARTFUN1:def 8
            .= s1/.(j+1) by A40,FINSEQ_5:75
            .= s1.(j+1) by A38,A40,PARTFUN1:def 8;
          hence H2' is normal StableSubgroup of H1'
          by A33,A34,A38,A39,A40,A41,Def31;
        end;
        suppose
A42:      j=i;
          then
A43:      s2.(j+1) = s2/.(i+1) by A31,PARTFUN1:def 8
            .= N2 by A7,A9,FINSEQ_5:76,XXREAL_0:2;
A44:      Seg len(s1|i) = Seg i by A7,A9,FINSEQ_1:80,XXREAL_0:2;
A45:      dom(s1|i) c= dom s1 by RELAT_1:89;
          j in Seg i by A30,A42;
          then
A46:      j in dom(s1|i) by A44,FINSEQ_1:def 3;
          s2.j = s2/.j by A29,PARTFUN1:def 8
            .= s1/.j by A46,FINSEQ_5:75
            .= s1.j by A45,A46,PARTFUN1:def 8;
          hence H2' is normal StableSubgroup of H1' by A18,A33,A34,A42,A43;
        end;
        suppose
A47:      j=i+1;
          i+1<=len s1+1 by A10,XREAL_1:8;
          then 1<=i+1 & i+1<=len s2 by A12,FINSEQ_5:72;
          then i+1 in Seg len s2;
          then i+1 in dom s2 by FINSEQ_1:def 3;
          then
A48:      H1' = s2/.(i+1) by A33,A47,PARTFUN1:def 8
            .= N2 by A7,A9,FINSEQ_5:76,XXREAL_0:2;
          i+1+1<=len s1+1 by A13,XREAL_1:8;
          then 1<=i+1+1 & i+1+1<=len s2 by A12,FINSEQ_5:72,XREAL_1:8;
          then i+1+1 in Seg len s2;
          then i+1+1 in dom s2 by FINSEQ_1:def 3;
          then H2' = s2/.(i+1+1) by A34,A47,PARTFUN1:def 8
            .= s1/.(i+1) by A13,FINSEQ_5:77
            .= N1 by A14,PARTFUN1:def 8;
          hence H2' is normal StableSubgroup of H1' by A18,A48;
        end;
        suppose
A49:      i+1<j;
          then i+1+1<=j by NAT_1:13;
          then
A50:      i+1+1-1<=j-1 by XREAL_1:11;
          set j'=j-1;
          0+1<=i+1 by XREAL_1:8;
          then
A51:      0+1<j by A49,XXREAL_0:2;
          then
A52:      0+1-1<j-1 by XREAL_1:11;
          then reconsider j' as Element of NAT by INT_1:16;
A53:      j-1<=len s2-1 by A30,XREAL_1:11;
          0+j'<1+j' by XREAL_1:8;
          then
A54:      i+1 <= j'+1 by A50,XXREAL_0:2;
A55:      j+1-1<=len s2-1 by A32,XREAL_1:11;
          0+1<=j' by A52,NAT_1:13;
          then j' in Seg len s1 by A27,A53;
          then
A56:      j' in dom s1 by FINSEQ_1:def 3;
          j'+1 in Seg len s1 by A27,A51,A55;
          then
A57:      j'+1 in dom s1 by FINSEQ_1:def 3;
A58:      s2.j = s2/.(j'+1) by A29,PARTFUN1:def 8
            .= s1/.j' by A27,A50,A53,FINSEQ_5:77
            .= s1.j' by A56,PARTFUN1:def 8;
          s2.(j+1) = s2/.(j'+1+1) by A31,PARTFUN1:def 8
            .= s1/.(j'+1) by A27,A54,A55,FINSEQ_5:77
            .=s1.(j'+1) by A57,PARTFUN1:def 8;
          hence H2' is normal StableSubgroup of H1' by A33,A34,A56,A57,A58
,Def31;
        end;
      end;
      then reconsider s2 as CompositionSeries of G by A25,A26,Def31;
      now
        let j be Nat;
        assume
A59:    j in dom s2;
        then j in Seg len s2 by FINSEQ_1:def 3;
        then
A60:    1<=j & j<=len s2 by FINSEQ_1:3;
        assume
A61:    j+1 in dom s2;
        then j+1 in Seg len s2 by FINSEQ_1:def 3;
        then
A62:    1<=j+1 & j+1<=len s2 by FINSEQ_1:3;
        let H1',N1';
        assume
A63:    H1'=s2.j;
        assume
A64:    N1'=s2.(j+1);
        j<i or j=i or j>i by XXREAL_0:1;
        then j+1<=i or j=i or j>=i+1 by NAT_1:13;
        then
A65:    j+1-1<=i-1 or j=i or j>=i+1 by XREAL_1:11;
A66:    s1 is strictly_decreasing by A3,Def34;
        per cases by A65,XXREAL_0:1;
        suppose
A67:      j<=i-1;
A68:      Seg len(s1|i) = Seg i by A7,A9,FINSEQ_1:80,XXREAL_0:2;
A69:      dom(s1|i) c= dom s1 by RELAT_1:89;
          -1+i<0+i by XREAL_1:8;
          then j<=i by A67,XXREAL_0:2;
          then j in Seg len (s1|i) by A60,A68,FINSEQ_1:3;
          then
A70:      j in dom(s1|i) by FINSEQ_1:def 3;
          j+1<=i-1+1 by A67,XREAL_1:8;
          then j+1 in Seg len (s1|i) by A62,A68;
          then
A71:      j+1 in dom(s1|i) by FINSEQ_1:def 3;
A72:      s2.j = s2/.j by A59,PARTFUN1:def 8
            .= s1/.j by A70,FINSEQ_5:75
            .= s1.j by A69,A70,PARTFUN1:def 8;
          s2.(j+1) = s2/.(j+1) by A61,PARTFUN1:def 8
            .= s1/.(j+1) by A71,FINSEQ_5:75
            .= s1.(j+1) by A69,A71,PARTFUN1:def 8;
          hence H1'./.N1' is not trivial by A63,A64,A66,A69,A70,A71,A72,Def33;
        end;
        suppose
A73:      j=i;
          then
A74:      s2.(j+1) = s2/.(i+1) by A61,PARTFUN1:def 8
            .= N2 by A7,A9,FINSEQ_5:76,XXREAL_0:2;
          reconsider N2 as normal StableSubgroup of H1 by A18;
A75:      Seg len(s1|i) = Seg i by A7,A9,FINSEQ_1:80,XXREAL_0:2;
A76:      dom(s1|i) c= dom s1 by RELAT_1:89;
          j in Seg i by A60,A73;
          then
A77:      j in dom(s1|i) by A75,FINSEQ_1:def 3;
A78:      s2.j = s2/.j by A59,PARTFUN1:def 8
            .= s1/.j by A77,FINSEQ_5:75
            .= s1.j by A76,A77,PARTFUN1:def 8;
          now
            assume H1'./.N1' is trivial;
            then H1 = N2 by A63,A64,A73,A74,A78,Th76;
            hence contradiction by A17,A18,Th80;
          end;
          hence H1'./.N1' is not trivial;
        end;
        suppose
A79:      j=i+1;
          i+1<=len s1+1 by A10,XREAL_1:8;
          then 1<=i+1 & i+1<=len s2 by A12,FINSEQ_5:72;
          then i+1 in Seg len s2;
          then i+1 in dom s2 by FINSEQ_1:def 3;
          then
A80:      H1' = s2/.(i+1) by A63,A79,PARTFUN1:def 8
            .= N2 by A7,A9,FINSEQ_5:76,XXREAL_0:2;
          i+1+1<=len s1+1 by A13,XREAL_1:8;
          then 1<=i+1+1 & i+1+1<=len s2 by A12,FINSEQ_5:72,XREAL_1:8;
          then i+1+1 in Seg len s2;
          then i+1+1 in dom s2 by FINSEQ_1:def 3;
          then
A81:      N1' = s2/.(i+1+1) by A64,A79,PARTFUN1:def 8
            .= s1/.(i+1) by A13,FINSEQ_5:77
            .= N1 by A14,PARTFUN1:def 8;
          now
            assume H1'./.N1' is trivial;
            then the carrier of N1 = (nat_hom N1)"(the carrier of H)
            by A18,A80,A81,Th76;
            hence contradiction by A17,Th81;
          end;
          hence H1'./.N1' is not trivial;
        end;
        suppose
A82:      i+1<j;
          then i+1+1<=j by NAT_1:13;
          then
A83:      i+1+1-1<=j-1 by XREAL_1:11;
          set j'=j-1;
A84:      0+1<=i+1 by XREAL_1:8;
          then 0+1<j by A82,XXREAL_0:2;
          then
A85:      0+1-1<j-1 by XREAL_1:11;
          then reconsider j' as Element of NAT by INT_1:16;
A86:      j-1<=len s2-1 by A60,XREAL_1:11;
          0+j'<1+j' by XREAL_1:8;
          then
A87:      i+1 <= j'+1 by A83,XXREAL_0:2;
A88:      j+1-1<=len s2-1 by A62,XREAL_1:11;
          0+1<=j' by A85,NAT_1:13;
          then j' in Seg len s1 by A27,A86;
          then
A89:      j' in dom s1 by FINSEQ_1:def 3;
          1<=j'+1 by A82,A84,XXREAL_0:2;
          then j'+1 in Seg len s1 by A27,A88;
          then
A90:      j'+1 in dom s1 by FINSEQ_1:def 3;
A91:      s2.j = s2/.(j'+1) by A59,PARTFUN1:def 8
            .= s1/.j' by A27,A83,A86,FINSEQ_5:77
            .= s1.j' by A89,PARTFUN1:def 8;
          s2.(j+1) = s2/.(j'+1+1) by A61,PARTFUN1:def 8
            .= s1/.(j'+1) by A27,A87,A88,FINSEQ_5:77
            .=s1.(j'+1) by A90,PARTFUN1:def 8;
          hence H1'./.N1' is not trivial by A63,A64,A66,A89,A90,A91,Def33;
        end;
      end;
      then
A92:  s2 is strictly_decreasing by Def33;
A93:  (dom s2) \ {i+1} c= dom s2 by XBOOLE_1:36;
      s1 = Del(s2,i+1) by A11,Lm44;
      then s2 is_finer_than s1 by A93,Def32;
      hence contradiction by A3,A28,A92,Def34;
    end;
  end;
  now
    assume
A94: for i st i in dom the_series_of_quotients_of s1 holds
    (the_series_of_quotients_of s1).i is strict simple GroupWithOperators of O;
    assume
A95: not s1 is jordan_holder;
    per cases by A95,Def34;
    suppose s1 is not strictly_decreasing;
      then not for i st i in dom s1 & i+1 in dom s1 for H1,N1
      st H1=s1.i & N1=s1.(i+1) holds H1./.N1 is not trivial by Def33;
      then consider i,H1,N1 such that
A96:  i in dom s1 & i+1 in dom s1 and
A97:  H1=s1.i & N1=s1.(i+1) and
A98:  H1./.N1 is trivial;
      i in Seg len s1 & i+1 in Seg len s1 by A96,FINSEQ_1:def 3;
      then
A99:  1 <= i & i+1 <= len s1 by FINSEQ_1:3;
      then 1+1 <= i+1 by XREAL_1:8;
      then 1+1 <= len s1 by A99,XXREAL_0:2;
      then
A100: len s1 > 1 by NAT_1:13;
      then len the_series_of_quotients_of s1 + 1 = len s1 by Def36;
      then len the_series_of_quotients_of s1 = len s1 - 1;
      then i+1-1 <= len the_series_of_quotients_of s1 by A99,XREAL_1:11;
      then i in Seg len the_series_of_quotients_of s1 by A99,FINSEQ_1:3;
      then
A101: i in dom the_series_of_quotients_of s1 by FINSEQ_1:def 3;
      then H1./.N1 is trivial & (the_series_of_quotients_of s1).i=H1./.N1
      by A97,A98,A100,Def36;
      then H1./.N1 is strict simple GroupWithOperators of O by A94,A101;
      hence contradiction by A98,Def13;
    end;
    suppose ex s2 st s2<>s1 & s2 is strictly_decreasing & s2 is_finer_than s1;
      then consider s2 such that
A102: s2<>s1 & s2 is strictly_decreasing & s2 is_finer_than s1;
      consider i,j such that
A103: i in dom s1 & i in dom s2 & i+1 in dom s1 & i+1 in dom s2 &
      j in dom s2 and
A104: i+1<j & s1.i=s2.i & s1.(i+1)<>s2.(i+1) & s1.(i+1)=s2.j by A1,A102,Th100;
      reconsider H1=s1.i,H2=s1.(i+1),H=s2.(i+1)
      as Element of the_stable_subgroups_of G by A103,FINSEQ_2:13;
      reconsider H1,H2,H as strict StableSubgroup of G by Def11;
      reconsider H as strict normal StableSubgroup of H1 by A103,A104,Def31;
      reconsider H2 as strict normal StableSubgroup of H1 by A103,Def31;
      reconsider H3 = the HGrWOpStr of H2
      as strict normal StableSubgroup of H by A103,A104,Th40,Th101;
      reconsider H2'=H2 as normal StableSubgroup of H by A103,A104,Th40,Th101;
      reconsider J = H./.H2' as strict normal StableSubgroup of H1./.H2
      by Th44;
A105: now
        assume J = (Omega).(H1./.H2);
        then
A106:   the carrier of H = union Cosets H2 by Th22;
        then
A107:   H = H1 by Lm5,Th22;
        then reconsider H1 as strict normal StableSubgroup of H;
        H1 = (Omega).H by A106,Lm5,Th22;
        then H./.H1 is trivial by Th57;
        hence contradiction by A102,A103,A104,A107,Def33;
      end;
      now
        assume J = (1).(H1./.H2);
        then union Cosets H3 = union {1_(H1./.H2)} by Def8;
        then the carrier of H = union {1_(H1./.H2)} by Th22;
        then the carrier of H = 1_(H1./.H2) by ZFMISC_1:31;
        then the carrier of H = carr H2 by Th43;
        hence contradiction by A104,Lm5;
      end;
      then
A108: H1./.H2 is not simple GroupWithOperators of O by A105,Def13;
      i in Seg len s1 & i+1 in Seg len s1 by A103,FINSEQ_1:def 3;
      then
A109: 1 <= i & i+1 <= len s1 by FINSEQ_1:3;
      then 1+1 <= i+1 by XREAL_1:8;
      then 1+1 <= len s1 by A109,XXREAL_0:2;
      then
A110: len s1 > 1 by NAT_1:13;
      then len the_series_of_quotients_of s1 + 1 = len s1 by Def36;
      then len the_series_of_quotients_of s1 = len s1 - 1;
      then i+1-1 <= len the_series_of_quotients_of s1 by A109,XREAL_1:11;
      then i in Seg len the_series_of_quotients_of s1 by A109,FINSEQ_1:3;
      then
A111: i in dom the_series_of_quotients_of s1 by FINSEQ_1:def 3;
      then (the_series_of_quotients_of s1).i=H1./.H2 by A110,Def36;
      hence contradiction by A94,A108,A111;
    end;
  end;
  hence thesis by A2;
end;

theorem Th111:
  1<=i & i<=len s1-1 implies
  s1.i is strict StableSubgroup of G & s1.(i+1) is strict StableSubgroup of G
proof
  assume
A1: 1<=i & i<=len s1-1;
A2: 0+i<=1+i by XREAL_1:8;
  then
A3: 1<=i+1 by A1,XXREAL_0:2;
A4: i+1<=len s1-1+1 by A1,XREAL_1:8;
  then i+1 in Seg len s1 by A3;
  then
A5: i+1 in dom s1 by FINSEQ_1:def 3;
  i<=len s1 by A2,A4,XXREAL_0:2;
  then i in Seg len s1 by A1,FINSEQ_1:3;
  then i in dom s1 by FINSEQ_1:def 3;
  then
A6: s1.i is Element of the_stable_subgroups_of G by FINSEQ_2:13;
A7: s1.(i+1) is Element of the_stable_subgroups_of G by A5,FINSEQ_2:13;
  thus s1.i is strict StableSubgroup of G by A6,Def11;
  thus thesis by A7,Def11;
end;

theorem Th112:
  1<=i & i<=len s1-1 & H1=s1.i & H2=s1.(i+1)
  implies H2 is normal StableSubgroup of H1
proof
  assume
A1: 1<=i & i<=len s1-1;
  assume
A2: H1=s1.i & H2=s1.(i+1);
A3: 0+i<=1+i by XREAL_1:8;
  then
A4: 1<=i+1 by A1,XXREAL_0:2;
A5: i+1<=len s1-1+1 by A1,XREAL_1:8;
  then i+1 in Seg len s1 by A4;
  then
A6: i+1 in dom s1 by FINSEQ_1:def 3;
  i<=len s1 by A3,A5,XXREAL_0:2;
  then i in Seg len s1 by A1,FINSEQ_1:3;
  then i in dom s1 by FINSEQ_1:def 3;
  hence thesis by A2,A6,Def31;
end;

theorem Th113:
  s1 is_equivalent_with s1
proof
  per cases;
  suppose s1 is empty;
    hence thesis by Th107;
  end;
  suppose
A1: s1 is not empty;
    set f1=the_series_of_quotients_of s1;
    now
      set p = id dom f1;
      reconsider p as Function of dom f1,dom f1;
      rng p = dom f1 by RELAT_1:71;
      then p is onto by FUNCT_2:def 3;
      then reconsider p as Permutation of dom f1;
      take p;
A2:   len f1 = len f1;
      now
        let H1,H2 be GroupWithOperators of O;
        let i,j;
        assume
A3:     i in dom f1 & j=p".i;
        assume
A4:     H1=f1.i & H2=f1.j;
        p" = p by FUNCT_1:67;
        hence H1,H2 are_isomorphic by A3,A4,FUNCT_1:35;
      end;
      hence f1,f1 are_equivalent_under p,O by A2,Def37;
    end;
    hence thesis by A1,Th108;
  end;
end;

theorem Th114:
  (len s1<=1 or len s2<=1) & len s1<=len s2 implies s2 is_finer_than s1
proof
  assume
A1: len s1<=1 or len s2<=1;
  assume
A2: len s1<=len s2;
  then
A3: len s1 <=1 by A1,XXREAL_0:2;
  per cases;
  suppose
A4: len s1=1;
    then
A5: s1 = <* s1.1 *> by FINSEQ_1:57;
    now
      set x={1};
      take x;
A6:   1 in Seg len s2 by A2,A4;
      then 1 in dom s2 by FINSEQ_1:def 3;
      hence x c= dom s2 by ZFMISC_1:37;
      set f=s2;
      set p=<*1*>;
      reconsider D=Seg len s2 as non empty set by A2,A4;
A7:   dom f = Seg len s2 by FINSEQ_1:def 3;
      rng f c= the_stable_subgroups_of G;
      then reconsider f as Function of D, the_stable_subgroups_of G by A7,
FUNCT_2:4;
      {1} c= D by A6,ZFMISC_1:37;
      then rng p c= D by FINSEQ_1:55;
      then reconsider p as FinSequence of D by FINSEQ_1:def 4;
A8:   Sgm x = p by FINSEQ_3:50;
      f * p = <* f.1 *> by FINSEQ_2:39;
      then s2 * Sgm x = <* (Omega).G *> by A8,Def31;
      hence s1 = s2 * Sgm x by A5,Def31;
    end;
    hence thesis by Def32;
  end;
  suppose len s1<>1;
    then len s1<0+1 by A3,XXREAL_0:1;
    then len s1<=0 by NAT_1:13;
    then len s1=0;
    then
A9: s1={};
    now
      set x={};
      take x;
      thus x c= dom s2 by XBOOLE_1:2;
      thus s1 = s2 * Sgm x by A9,FINSEQ_3:49;
    end;
    hence thesis by Def32;
  end;
end;

theorem Th115:
  s1 is_equivalent_with s2 & s1 is jordan_holder implies s2 is jordan_holder
proof
  assume
A1: s1 is_equivalent_with s2;
  assume
A2: s1 is jordan_holder;
  per cases;
  suppose
A3: len s1<=0+1;
    per cases by A3,NAT_1:26;
    suppose
A4:   len s1=0;
      then
A5:   s1={};
      len s2=0 by A1,A4,Def35;
       then s2 = {};
      hence thesis by A2,A5;
    end;
    suppose
A6:   len s1=1;
      then
A7:   s1.1=(1).G by Def31;
A8:   len s2=1 by A1,A6,Def35;
      s1 = <* s1.1 *> by A6,FINSEQ_1:57
        .= <* s2.1 *> by A7,A8,Def31
        .= s2 by A8,FINSEQ_1:57;
      hence thesis by A2;
    end;
  end;
  suppose
A9: len s1>1;
    then
A10: s1 is not empty;
A11: len s2>1 by A1,A9,Def35;
    then s2 is not empty;
    then consider p be Permutation of dom the_series_of_quotients_of s1 such
    that
A12: the_series_of_quotients_of s1,the_series_of_quotients_of s2
    are_equivalent_under p,O by A1,A10,Th108;
    set f1 = the_series_of_quotients_of s1;
    set f2 = the_series_of_quotients_of s2;
A13: len f1 = len f2 & for H1,H2 being GroupWithOperators of O,i,j
    st i in dom f1 & j=p".i & H1=f1.i & H2=f2.j
    holds H1,H2 are_isomorphic by A12,Def37;
    now
      let j;
      assume
A14:  j in dom f2;
      set i=p.j;
      set H1=f1.i;
      set H2=f2.j;
A15:  dom f1 = Seg len f1 by FINSEQ_1:def 3
        .= Seg len f2 by A12,Def37
        .= dom f2 by FINSEQ_1:def 3;
      then
A16:  p.j in dom f2 by A14,FUNCT_2:7;
      then
A17:  p.j in Seg len f2 by FINSEQ_1:def 3;
      reconsider i as Element of NAT by A16;
A18:  i in dom f1 by A13,A17,FINSEQ_1:def 3;
      then
A19:  f1.i in rng f1 by FUNCT_1:12;
      f2.j in rng f2 by A14,FUNCT_1:12;
      then reconsider H1,H2 as strict GroupWithOperators of O by A19,Th102;
      i in dom f1 & j=p".i & H1=f1.i & H2=f2.j by A14,A15,FUNCT_2:7,32;
      then
A20:  H1,H2 are_isomorphic by A12,Def37;
      H1 is strict simple GroupWithOperators of O by A2,A9,A18,Th110;
      hence f2.j is strict simple GroupWithOperators of O by A20,Th82;
    end;
    hence thesis by A11,Th110;
  end;
end;

Lm45: for k,l being Nat
st k in Seg l & len s1>1 & len s2>1 & l=(len s1-1)*(len s2-1)+1
holds k=(len s1-1)*(len s2-1)+1 or
(ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1)
proof
  let k,l be Nat;
  assume
A1: k in Seg l;
  then
A2: 1<=k & k<=l by FINSEQ_1:3;
  assume
A3: len s1>1 & len s2>1 & l=(len s1-1)*(len s2-1)+1;
  assume not k = (len s1-1)*(len s2-1)+1;
  then
A4: k<l by A2,A3,XXREAL_0:1;
  set l' = len s1-1;
  len s1-1>1-1 by A3,XREAL_1:11;
  then reconsider l' as Element of NAT by INT_1:16;
  set l'' = len s2-1;
A5: len s2-1>1-1 by A3,XREAL_1:11;
  then reconsider l'' as Element of NAT by INT_1:16;
  len s1+1>1+1 & len s2+1>1+1 by A3,XREAL_1:8;
  then len s1>=2 & len s2>=2 by NAT_1:13;
  then
A6: len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11;
A7: k = (k div l'')*l'' + (k mod l'') by A5,NAT_D:2;
  per cases;
  suppose
A8: k mod l''=0;
    set j=l'';
    set i=k div l'';
    take i,j;
    thus k = (i-1)*(len s2-1)+j by A7,A8;
    i<>0 by A1,A7,A8,FINSEQ_1:3;
    then i>0;
    then i+1>0+1 by XREAL_1:8;
    hence 1<=i by NAT_1:13;
    i*l''<=(len s1-1)*l'' by A3,A4,A7,A8,INT_1:20;
    then i*l''/l''<=(len s1-1)*l''/l'' by XREAL_1:74;
    then i<=(len s1-1)*l''/l'' by A5,XCMPLX_1:90;
    hence i<=len s1-1 by A5,XCMPLX_1:90;
    thus thesis by A6;
  end;
  suppose
A9: k mod l''<>0;
    set j=k mod l'';
    set i=k div l''+1;
    take i,j;
    thus k = (i-1)*(len s2-1)+j by A5,NAT_D:2;
    0+1<=k div l''+1 by XREAL_1:8;
    hence 1<=i;
A10: k<>l'*l'' by A9,NAT_D:13;
    k+1<=l by A4,INT_1:20;
    then k+1-1<=l-1 by XREAL_1:11;
    then
A11: k<(len s1-1)*l'' by A3,A10,XXREAL_0:1;
    k mod l'' + l''*(k div l'')>=0+l''*(k div l'') by XREAL_1:8;
    then (k div l'')*l''<=k by A5,NAT_D:2;
    then (k div l'')*l''<(len s1-1)*l'' by A11,XXREAL_0:2;
    then (k div l'')*l''/l''<(len s1-1)*l''/l'' by A5,XREAL_1:76;
    then k div l''<(len s1-1)*l''/l'' by A5,XCMPLX_1:90;
    then k div l''<len s1-1 by A5,XCMPLX_1:90;
    hence i<=len s1-1 by INT_1:20;
    j>0 by A9;
    then j+1>0+1 by XREAL_1:8;
    hence 1<=j by NAT_1:13;
    thus thesis by A5,NAT_D:1;
  end;
end;

Lm46: for i1,j1,i2,j2 being Nat, s1,s2 st len s1>1 & len s2>1 &
(i1-1)*(len s2-1)+j1=(i2-1)*(len s2-1)+j2 &
1<=i1 & i1<=len s1-1 & 1<=j1 & j1<=len s2-1 &
1<=i2 & i2<=len s1-1 & 1<=j2 & j2<=len s2-1 holds j1=j2 & i1=i2
proof
  let i1,j1,i2,j2 be Nat;
  let s1,s2;
  assume
A1: len s1>1 & len s2>1;
  assume
A2: (i1-1)*(len s2-1)+j1=(i2-1)*(len s2-1)+j2;
  assume
A3: 1<=i1 & i1<=len s1-1 & 1<=j1 & j1<=len s2-1;
  assume
A4: 1<=i2 & i2<=len s1-1 & 1<=j2 & j2<=len s2-1;
  set l'' = len s2-1;
A5: len s2-1>1-1 by A1,XREAL_1:11;
  then reconsider l'' as Element of NAT by INT_1:16;
  set i1' = i1-1;
  i1-1>=1-1 by A3,XREAL_1:11;
  then reconsider i1' as Element of NAT by INT_1:16;
  set i2' = i2-1;
  i2-1>=1-1 by A4,XREAL_1:11;
  then reconsider i2' as Element of NAT by INT_1:16;
A6: j1 mod l'' = (i1'*l''+j1) mod l'' by NAT_D:21
    .= (i2'*l''+j2) mod l'' by A2
    .= j2 mod l'' by NAT_D:21;
A7: j1=j2
  proof
    per cases;
    suppose
A8:   j1=l'';
      assume j2<>j1;
      then j2<l'' by A4,A8,XXREAL_0:1;
      then j2 = j1 mod l'' by A6,NAT_D:24;
      hence contradiction by A4,A8,NAT_D:25;
    end;
    suppose j1<>l'';
      then j1<l'' by A3,XXREAL_0:1;
      then
A9:   j1 = j2 mod l'' by A6,NAT_D:24;
      per cases;
      suppose j2=l'';
        hence thesis by A3,A9,NAT_D:25;
      end;
      suppose j2<>l'';
        then j2<l'' by A4,XXREAL_0:1;
        hence thesis by A9,NAT_D:24;
      end;
    end;
  end;
  hence j1=j2;
A10: l''/l''=1 by A5,XCMPLX_1:60;
  i1'*(l''/l'')=i2'*l''/l'' by A2,A7,XCMPLX_1:75;
  then i1'*1=i2'*1 by A10,XCMPLX_1:75;
  hence thesis;
end;

Lm47: for k being integer number, i,j being Nat, s1,s2
st len s1>1 & len s2>1 & k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
& 1<=j & j<=len s2-1 holds 1<=k & k<=(len s1-1)*(len s2-1)
proof
  let k be integer number;
  let i,j be Nat;
  let s1,s2;
  assume
A1: len s1>1 & len s2>1;
  assume
A2: k=(i-1)*(len s2-1)+j;
  assume
A3: 1<=i & i<=len s1-1;
  assume
A4: 1<=j & j<=len s2-1;
  set l'=len s1-1;
  set l''=len s2-1;
A5: len s1-1>1-1 & len s2-1>1-1 by A1,XREAL_1:11;
  1-1<=i-1 by A3,XREAL_1:11;
  then 0*(len s2-1)<=(i-1)*(len s2-1) by A5;
  then
A6: 0+1<=(i-1)*(len s2-1)+j by A4,XREAL_1:9;
  i-1<=l'-1 by A3,XREAL_1:11;
  then (i-1)*l''<=(l'-1)*l'' by A5,XREAL_1:66;
  then k<=l'*l''-1*l''+l'' by A2,A4,XREAL_1:9;
  hence thesis by A2,A6;
end;

begin :: The Schreier Refinement Theorem

definition
  let O,G,s1,s2;
  assume
A1: len s1>1 & len s2>1;
  func the_schreier_series_of(s1,s2) -> CompositionSeries of G means
  :Def38:
  for k,i,j being Nat, H1,H2,H3 being StableSubgroup of G holds
  (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
  H1=s1.(i+1) & H2=s1.i & H3=s2.j implies it.k = H1"\/"(H2/\H3)) &
  (k=(len s1-1)*(len s2-1) + 1 implies it.k = (1).G) &
  len it = (len s1-1)*(len s2-1) + 1;
  existence
  proof
    defpred P[set,set] means
    for i,j being Nat, H1,H2,H3 being StableSubgroup of G holds
    ($1=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
    H1=s1.(i+1) & H2=s1.i & H3=s2.j implies $2 = H1"\/"(H2/\H3)) &
    ($1=(len s1-1)*(len s2-1) + 1 implies $2 = (1).G);
    len s1+1>1+1 & len s2+1>1+1 by A1,XREAL_1:8;
    then len s1>=2 & len s2>=2 by NAT_1:13;
    then
A2: len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11;
    then (len s1-1)*(len s2-1)>=0*(len s2-1);
    then
A3: (len s1-1)*(len s2-1)+1>=0+1 by XREAL_1:8;
A4: for i,s1,H st 1<=i & i<=len s1-1 & H=s1.i
    holds s1.(i+1) is normal StableSubgroup of H
    proof
      let i,s1,H;
      assume
A5:   1<=i & i<=len s1-1;
      assume
A6:   H=s1.i;
A7:   0+i<=1+i by XREAL_1:8;
      then
A8:   1<=i+1 by A5,XXREAL_0:2;
A9:   i+1<=len s1-1+1 by A5,XREAL_1:8;
      then i+1 in Seg len s1 by A8;
      then
A10:  i+1 in dom s1 by FINSEQ_1:def 3;
      i<=len s1 by A7,A9,XXREAL_0:2;
      then i in Seg len s1 by A5,FINSEQ_1:3;
      then
A11:  i in dom s1 by FINSEQ_1:def 3;
      reconsider H1=s1.i,H2=s1.(i+1) as StableSubgroup of G by A5,Th111;
      H2 is normal StableSubgroup of H1 by A10,A11,Def31;
      hence thesis by A6;
    end;
    reconsider l=(len s1-1)*(len s2-1)+1 as Element of NAT by A3,INT_1:16;
    len s1-1>1-1 by A1,XREAL_1:11;
    then reconsider l' = len s1-1 as Element of NAT by INT_1:16;
    len s2-1>1-1 by A1,XREAL_1:11;
    then reconsider l'' = len s2-1 as Element of NAT by INT_1:16;
A17: for k being Nat st k=(len s1-1)*(len s2-1)+1 holds
    not ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
    & 1<=j & j<=len s2-1
    proof
      let k be Nat;
      assume
A18:  k=(len s1-1)*(len s2-1)+1;
      assume ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j
      & j<=len s2-1;
      then consider i,j such that
A19:  k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
      set i' = i-1;
      i-1>=1-1 by A19,XREAL_1:11;
      then reconsider i' as Element of NAT by INT_1:16;
A20:  1 mod l'' = (l'*l''+1) mod l'' by NAT_D:21
        .= (i'*l''+j) mod l'' by A18,A19
        .= j mod l'' by NAT_D:21;
A21:  j=1
      proof
        per cases;
        suppose
A22:      j=l'';
          assume j<>1;
          then 1<l'' by A19,A22,XXREAL_0:1;
          then 1 = j mod l'' by A20,NAT_D:24;
          hence contradiction by A22,NAT_D:25;
        end;
        suppose j<>l'';
          then j<l'' by A19,XXREAL_0:1;
          then
A23:      j mod l'' = j by NAT_D:24;
          per cases;
          suppose 1=l'';
            hence thesis by A19,XXREAL_0:1;
          end;
          suppose
A24:        1<>l'';
            1<=l'' by A19,XXREAL_0:2;
            then 1<l'' by A24,XXREAL_0:1;
            hence thesis by A20,A23,NAT_D:24;
          end;
        end;
      end;
A25:  l''/l''=1 by A19,XCMPLX_1:60;
      l'*(l''/l'')=i'*l''/l'' by A18,A19,A21,XCMPLX_1:75;
      then
A26:  l'*1=i'*1 by A25,XCMPLX_1:75;
      -1+i<0+i by XREAL_1:8;
      hence contradiction by A19,A26;
    end;
A27: for k being Nat st k in Seg l ex x st P[k,x]
    proof
      let k be Nat;
      assume
A28:  k in Seg l;
      per cases by A1,A28,Lm45;
      suppose
A29:    ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1;
        then consider i,j such that
A30:    k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
        reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G
        by A30,Th111;
        take x=H1"\/"(H2/\H3);
        now
          let i1,j1 be Nat;
          let H1,H2,H3 be StableSubgroup of G;
          thus k=(i1-1)*(len s2-1)+j1 & 1<=i1 & i1<=len s1-1 & 1<=j1
          & j1<=len s2-1 & H1=s1.(i1+1) & H2=s1.i1 & H3=s2.j1
          implies x = H1"\/"(H2/\H3)
          proof
            assume
A31:        k=(i1-1)*(len s2-1)+j1 & 1<=i1 & i1<=len s1-1 & 1<=j1
            & j1<=len s2-1;
            assume
A32:        H1=s1.(i1+1) & H2=s1.i1 & H3=s2.j1;
            j=j1 & i=i1 by A1,A30,A31,Lm46;
            hence thesis by A32;
          end;
          assume k=(len s1-1)*(len s2-1) + 1;
          hence x = (1).G by A17,A29;
        end;
        hence thesis;
      end;
      suppose
A33:    k=(len s1-1)*(len s2-1)+1;
        take x=(1).G;
        thus thesis by A17,A33;
      end;
    end;
A34: for k,i,j being Nat st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
    & 1<=j & j<=len s2-1 holds k in Seg l
    proof
      let k,i,j be Nat;
      assume
A35:  k=(i-1)*(len s2-1)+j;
      assume
A36:  1<=i & i<=len s1-1;
      assume
A37:  1<=j & j<=len s2-1;
      1-1<=i-1 by A36,XREAL_1:11;
      then 0*(len s2-1)<=(i-1)*(len s2-1) by A2;
      then
A38:  0+1<=(i-1)*(len s2-1)+j by A37,XREAL_1:9;
A39:  0+l'*l''<=1+l'*l'' by XREAL_1:8;
      i-1<=l'-1 by A36,XREAL_1:11;
      then (i-1)*l''<=(l'-1)*l'' by XREAL_1:66;
      then k<=l'*l''-1*l''+l'' by A35,A37,XREAL_1:9;
      then k<=(len s1-1)*(len s2-1)+1 by A39,XXREAL_0:2;
      hence thesis by A35,A38,FINSEQ_1:3;
    end;
    consider f be FinSequence such that
A40: dom f = Seg l & for k being Nat
    st k in Seg l holds P[k,f.k] from FINSEQ_1:sch 1(A27);
    for k being Nat st k in dom f holds f.k in the_stable_subgroups_of G
    proof
      let k be Nat;
      assume
A41:  k in dom f;
      then
A42:  for i,j being Nat, H1,H2,H3 being StableSubgroup of G
      holds (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
      H1=s1.(i+1) & H2=s1.i & H3=s2.j implies f.k = H1"\/"(H2/\H3)) &
      (k=(len s1-1)*(len s2-1) + 1 implies f.k = (1).G) by A40;
      per cases by A1,A40,A41,Lm45;
      suppose ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1;
        then consider i,j such that
A43:    k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
        reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G
        by A43,Th111;
        f.k = H1"\/"(H2/\H3) by A40,A41,A43;
        hence thesis by Def11;
      end;
      suppose k=(len s1-1)*(len s2-1)+1;
        hence thesis by A42,Def11;
      end;
    end;
    then reconsider f as FinSequence of the_stable_subgroups_of G by
FINSEQ_2:14;
A44: 1 in Seg l & l in Seg l by A3;
    then
A45: P[1,f.1] & P[l,f.l] by A40;
    len s1-1>1-1 & len s2-1>1-1 by A1,XREAL_1:11;
    then (len s1-1)*(len s2-1)>0*(len s2-1) by XREAL_1:70;
    then 1<>l;
    then consider i,j such that
A46: 1=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
    & 1<=j & j<=len s2-1 by A1,A44,Lm45;
    reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G by A46,Th111;
A47: f.1 = H1"\/"(H2/\H3) by A40,A44,A46;
    set i' = i-1;
    i-1>=1-1 by A46,XREAL_1:11;
    then reconsider i' as Element of NAT by INT_1:16;
    1 mod l''=(i'*l''+j) mod l'' by A46;
    then
A48: 1 mod l''=j mod l'' by NAT_D:21;
A49: j=1
    proof
      per cases;
      suppose l''=1;
        hence thesis by A46,XXREAL_0:1;
      end;
      suppose l''<>1;
        then 1<l'' by A2,XXREAL_0:1;
        then
A50:    1=j mod l'' by A48,NAT_D:14;
        then j<>l'' by NAT_D:25;
        then l''>j by A46,XXREAL_0:1;
        hence thesis by A50,NAT_D:24;
      end;
    end;
    then
A51: i'*l''/l''=0/l'' by A46;
    len s2-1>1-1 by A1,XREAL_1:11;
    then l''/l''=1 by XCMPLX_1:60;
    then i'*1=0 by A51,XCMPLX_1:75;
    then
A52: H2=(Omega).G by Def31;
    H3=(Omega).G by A49,Def31;
    then f.1 = H1"\/"(Omega).G by A47,A52,Th19;
    then
A53: f.1=(Omega).G by Th34;
A54: f.(len f)=(1).G by A40,A45,FINSEQ_1:def 3;
    for k being Nat st k in dom f & k+1 in dom f
    for H1,H2 st H1=f.k & H2=f.(k+1) holds H2 is normal StableSubgroup of H1
    proof
      let k be Nat;
      assume
A55:  k in dom f;
      assume
A56:  k+1 in dom f;
      then k+1<=l by A40,FINSEQ_1:3;
      then k<>l by NAT_1:13;
      then consider i,j such that
A57:  k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
      & 1<=j & j<=len s2-1 by A1,A40,A55,Lm45;
      let H1,H2;
      assume
A58:  H1 = f.k;
      assume
A59:  H2 = f.(k+1);
      reconsider H1'=s1.(i+1),H2'=s1.i,H3'=s2.j as strict StableSubgroup of G
      by A57,Th111;
A60:  H1' is normal StableSubgroup of H2' by A4,A57;
A61:  f.k = H1'"\/"(H2'/\H3') by A40,A55,A57;
      set k'=k+1;
      per cases;
      suppose j<>len s2-1;
        then
A62:    j<len s2-1 by A57,XXREAL_0:1;
        reconsider j'=j+1 as Nat;
        reconsider H3'' = s2.j' as strict StableSubgroup of G by A57,Th111;
A63:    j'<=len s2-1 by A62,INT_1:20;
        0+j<=1+j by XREAL_1:8;
        then
A64:    1<=j' by A57,XXREAL_0:2;
        k'=(i-1)*(len s2-1)+j' by A57;
        then
A65:    H2 = H1'"\/"(H2'/\H3'') by A40,A56,A57,A59,A63,A64;
        H3'' is normal StableSubgroup of H3' by A4,A57;
        hence thesis by A58,A60,A61,A65,Th92;
      end;
      suppose
A66:    j=len s2-1;
        per cases;
        suppose i<>len s1-1;
          then i<l' by A57,XXREAL_0:1;
          then
A67:      i+1<=l' by NAT_1:13;
          set i'=i+1;
          set j'=1;
A68:      k'=(i'-1)*(len s2-1)+j' by A57,A66;
          1+1<=i+1 by A57,XREAL_1:8;
          then
A69:      1<=i' by XXREAL_0:2;
          len s2-1>1-1 by A1,XREAL_1:11;
          then
A70:      l''>=0+1 by NAT_1:13;
          then reconsider H1''=s1.(i'+1),H2''=s1.i',H3''=s2.j'
          as strict StableSubgroup of G by A67,A69,Th111;
A71:      H2 = H1''"\/"(H2''/\H3'') by A40,A56,A59,A67,A68,A69,A70;
A72:      H3'' = (Omega).G by Def31;
          now
            let x be set;
            assume
A73:        x in the carrier of H2'';
            H2'' is Subgroup of G by Def7;
            then the carrier of H2'' c= the carrier of G by GROUP_2:def 5;
            hence x in the carrier of (Omega).G by A73;
          end;
          then the carrier of H2'' c= the carrier of (Omega).G by TARSKI:def 3;
          then the carrier of H2'' = (the carrier of H2'')
          /\ (the carrier of (Omega).G) by XBOOLE_1:28;
          then
A74:      H2 = H1''"\/"H2'' by A71,A72,Th18;
A75:      0+i'<=1+i' by XREAL_1:8;
          then
A76:      1<=i'+1 by A69,XXREAL_0:2;
A77:      i'+1<=len s1-1+1 by A67,XREAL_1:8;
          then i'+1 in Seg len s1 by A76;
          then
A78:      i'+1 in dom s1 by FINSEQ_1:def 3;
          i'<=len s1 by A75,A77,XXREAL_0:2;
          then i' in Seg len s1 by A69;
          then i' in dom s1 by FINSEQ_1:def 3;
          then H1'' is normal StableSubgroup of H2'' by A78,Def31;
          then
A79:      H2 = H1' by A74,Th36;
          H2'/\H3' is StableSubgroup of H2' by Lm34;
          then
A80:      H1 is StableSubgroup of H2' by A58,A60,A61,Th37;
          then
A81:      H1 is Subgroup of H2' by Def7;
          H1' is StableSubgroup of H1 by A58,A61,Th35;
          then H1' is Subgroup of H1 by Def7;
          then
A82:      the carrier of H1' c= the carrier of H1 by GROUP_2:def 5;
          now
            let H' be strict Subgroup of H1;
            assume
A83:        H' = the multMagma of H2;
            now
              let a be Element of H1;
              reconsider a'=a as Element of H2' by A80,Th2;
              now
                let x be set;
                assume x in a * H';
                then consider b be Element of H1 such that
A84:            x = a * b & b in H' by GROUP_2:125;
                set b'=b;
                reconsider H1s'=the multMagma of H1' as normal Subgroup of H2'
                by A60,Lm7;
A85:            a' * H1s' c= H1s' * a' by GROUP_3:141;
A86:            H1 is Subgroup of H2' by A80,Def7;
                then reconsider b' as Element of H2' by GROUP_2:51;
                x = a' * b' by A84,A86,GROUP_2:52;
                then x in a' * H1s' by A79,A83,A84,GROUP_2:125;
                then consider b'' be Element of H2' such that
A87:            x = b'' * a' & b'' in H1s' by A85,GROUP_2:126;
                b'' in the carrier of H1' by A87,STRUCT_0:def 5;
                then reconsider b'' as Element of H1 by A82;
                x = b'' * a by A81,A87,GROUP_2:52;
                hence x in H' * a by A79,A83,A87,GROUP_2:126;
              end;
              hence a * H' c= H' * a by TARSKI:def 3;
            end;
            hence H' is normal by GROUP_3:141;
          end;
          hence thesis by A58,A61,A79,Def10,Th35;
        end;
        suppose i=len s1-1;
          then H2 = (1).G by A40,A56,A57,A59,A66
            .= (1).H1 by Th15;
          hence thesis;
        end;
      end;
    end;
    then reconsider f as CompositionSeries of G by A53,A54,Def31;
    take f;
    let k,i,j be Nat, H1,H2,H3 be StableSubgroup of G;
    now
      assume
A88:  k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
      & 1<=j & j<=len s2-1 & H1=s1.(i+1) & H2=s1.i & H3=s2.j;
      then k in Seg l by A34;
      hence f.k = H1"\/"(H2/\H3) by A40,A88;
    end;
    hence (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
    H1=s1.(i+1) & H2=s1.i & H3=s2.j implies f.k = H1"\/"(H2/\H3));
    now
      assume
A89:  k=(len s1-1)*(len s2-1) + 1;
      then k in Seg l by A3;
      hence f.k = (1).G by A40,A89;
    end;
    hence thesis by A40,FINSEQ_1:def 3;
  end;
  uniqueness
  proof
    let f1,f2 be CompositionSeries of G;
    assume
A90: for k,i,j being Nat, H1,H2,H3 being StableSubgroup of G
    holds (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
    H1=s1.(i+1) & H2=s1.i & H3=s2.j implies f1.k = H1"\/"(H2/\H3)) &
    (k=(len s1-1)*(len s2-1) + 1 implies f1.k = (1).G) &
    len f1 = (len s1-1)*(len s2-1) + 1;
    assume
A91: for k,i,j being Nat, H1,H2,H3 being StableSubgroup of G
    holds (k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 &
    H1=s1.(i+1) & H2=s1.i & H3=s2.j implies f2.k = H1"\/"(H2/\H3)) &
    (k=(len s1-1)*(len s2-1) + 1 implies f2.k = (1).G) &
    len f2 = (len s1-1)*(len s2-1) + 1;
    then
A92: dom f1 = Seg len f2 by A90,FINSEQ_1:def 3
      .= dom f2 by FINSEQ_1:def 3;
    now
      let k be Nat;
      assume
A93:  k in dom f1;
      set l=len f1;
A94:  k in Seg l by A93,FINSEQ_1:def 3;
      per cases by A1,A90,A94,Lm45;
      suppose ex i,j st k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1
        & 1<=j & j<=len s2-1;
        then consider i,j such that
A95:    k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
        reconsider H1=s1.(i+1),H2=s1.i,H3=s2.j as StableSubgroup of G
        by A95,Th111;
        f1.k=H1"\/"(H2/\H3) & f2.k=H1"\/"(H2/\H3) by A90,A91,A95;
        hence f1.k = f2.k;
      end;
      suppose k=(len s1-1)*(len s2-1)+1;
        then f1.k=(1).G & f2.k=(1).G by A90,A91;
        hence f1.k = f2.k;
      end;
    end;
    hence thesis by A92,FINSEQ_1:17;
  end;
end;

theorem Th116:
  len s1>1 & len s2>1 implies the_schreier_series_of(s1,s2) is_finer_than s1
proof
  assume
A1: len s1>1 & len s2>1;
  now
    set g={[k,(k-1)*(len s2-1)+1] where k is Element of NAT:1<=k & k<=len s1};
    now
      let x;
      assume x in g;
      then consider k be Element of NAT such that
A2:   [k,(k-1)*(len s2-1)+1] = x & 1<=k & k<=len s1;
      set y=k;
      set z=(k-1)*(len s2-1)+1;
      reconsider y,z as set;
      take y,z;
      thus x = [y,z] by A2;
    end;
    then reconsider g as Relation by RELAT_1:def 1;
    now
      let x;
      assume x in dom g;
      then consider y such that
A3:   [x,y] in g by RELAT_1:def 4;
      consider k be Element of NAT such that
A4:   [k,(k-1)*(len s2-1)+1] = [x,y] & 1<=k & k<=len s1 by A3;
      k=x by A4,ZFMISC_1:33;
      hence x in NAT;
    end;
    then
A5: dom g c= NAT by TARSKI:def 3;
A6: now
      let y;
      assume y in rng g;
      then consider x such that
A7:   [x,y] in g by RELAT_1:def 5;
      consider k be Element of NAT such that
A8:   [k,(k-1)*(len s2-1)+1] = [x,y] & 1<=k & k<=len s1 by A7;
      (k-1)*(len s2-1)+1=y by A8,ZFMISC_1:33;
      hence y in REAL by XREAL_0:def 1;
    end;
    now
      let x,y1,y2 be set;
      assume [x,y1] in g;
      then consider k be Element of NAT such that
A9:   [k,(k-1)*(len s2-1)+1] = [x,y1] & 1<=k & k<=len s1;
      assume [x,y2] in g;
      then consider k' be Element of NAT such that
A10:  [k',(k'-1)*(len s2-1)+1] = [x,y2] & 1<=k' & k'<=len s1;
A11:  k=x & (k-1)*(len s2-1)+1=y1 & 1<=k & k<=len s1 by A9,ZFMISC_1:33;
      k'=x & (k'-1)*(len s2-1)+1=y2 & 1<=k' & k'<=len s1 by A10,ZFMISC_1:33;
      hence y1=y2 by A11;
    end;
    then reconsider g as Function by FUNCT_1:def 1;
    reconsider f=g as PartFunc of dom g, rng g by RELSET_1:11;
    dom g c= REAL & rng g c= REAL by A5,A6,TARSKI:def 3,XBOOLE_1:1;
    then reconsider f as PartFunc of REAL,REAL by RELSET_1:17;
    len s1+1>1+1 & len s2+1>1+1 by A1,XREAL_1:8;
    then len s1>=2 & len s2>=2 by NAT_1:13;
    then
A12: len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11;
    set X = Seg len s1;
    now
      let x;
      assume
A13:  x in X;
      then reconsider k=x as Element of NAT;
      set y=(k-1)*(len s2-1)+1;
      [k,(k-1)*(len s2-1)+1] = [x,y] & 1<=k & k<=len s1 by A13,FINSEQ_1:3;
      then [x,y] in f;
      hence x in dom f by RELAT_1:def 4;
    end;
    then
A14: X c= dom f by TARSKI:def 3;
    now
      let r1,r2 be Element of REAL;
      assume r1 in X /\ dom f;
      then r1 in dom f by XBOOLE_0:def 4;
      then [r1,f.r1] in f by FUNCT_1:8;
      then consider k' be Element of NAT such that
A15:  [k',(k'-1)*(len s2-1)+1]=[r1,f.r1] & 1<=k' & k'<=len s1;
      assume r2 in X /\ dom f;
      then r2 in dom f by XBOOLE_0:def 4;
      then [r2,f.r2] in f by FUNCT_1:8;
      then consider k'' be Element of NAT such that
A16:  [k'',(k''-1)*(len s2-1)+1]=[r2,f.r2] & 1<=k'' & k''<=len s1;
      assume
A17:  r1<r2;
A18:  k'=r1 & (k'-1)*(len s2-1)+1=f.r1 by A15,ZFMISC_1:33;
A19:  k''=r2 & (k''-1)*(len s2-1)+1=f.r2 by A16,ZFMISC_1:33;
      then k'-1<k''-1 by A17,A18,XREAL_1:11;
      then (k'-1)*(len s2-1)<(k''-1)*(len s2-1) by A12,XREAL_1:70;
      hence f.r1 < f.r2 by A18,A19,XREAL_1:8;
    end;
    then
A20: f|X is increasing by RFUNCT_2:43;
    now
      let y be set;
      assume y in f.:X;
      then consider x such that
A21:  [x,y] in g & x in X by RELAT_1:def 13;
      consider k be Element of NAT such that
A22:  [k,(k-1)*(len s2-1)+1]=[x,y] & 1<=k & k<=len s1 by A21;
A23:  (k-1)*(len s2-1)+1=y by A22,ZFMISC_1:33;
      reconsider y'=y as integer number by A22,ZFMISC_1:33;
      k-1>=1-1 by A22,XREAL_1:11;
      then (k-1)*(len s2-1)>=0*(len s2-1) by A12;
      then
A24:  (k-1)*(len s2-1)+1>=0+1 by XREAL_1:8;
      then
A25:  y' in NAT by A23,INT_1:16;
      not y in {0} by A23,A24,TARSKI:def 1;
      hence y in NAT \ {0} by A25,XBOOLE_0:def 5;
    end;
    then
A26: f.:X c= NAT \ {0} by TARSKI:def 3;
    set fX = f.:X;
    take fX;
    set l=(len s1-1)*(len s2-1) + 1;
    len s1+1>1+1 & len s2+1>1+1 by A1,XREAL_1:8;
    then len s1>=2 & len s2>=2 by NAT_1:13;
    then len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11;
    then (len s1-1)*(len s2-1)>=0*(len s2-1);
    then (len s1-1)*(len s2-1)+1>=0+1 by XREAL_1:8;
    then reconsider l as Element of NAT by INT_1:16;
    now
      let y be set;
      assume y in fX;
      then consider x such that
A27:  [x,y] in g & x in X by RELAT_1:def 13;
      consider k be Element of NAT such that
A28:  [k,(k-1)*(len s2-1)+1]=[x,y] & 1<=k & k<=len s1 by A27;
A29:  (k-1)*(len s2-1)+1=y by A28,ZFMISC_1:33;
      reconsider y'=y as integer number by A28,ZFMISC_1:33;
      k-1>=1-1 by A28,XREAL_1:11;
      then (k-1)*(len s2-1)>=0*(len s2-1) by A12;
      then
A30:  (k-1)*(len s2-1)+1>=0+1 by XREAL_1:8;
      then
A31:  y'>=1 by A28,ZFMISC_1:33;
      y'>0 by A28,A30,ZFMISC_1:33;
      then reconsider y' as Element of NAT by INT_1:16;
      k-1<=len s1-1 by A28,XREAL_1:11;
      then (k-1)*(len s2-1)<=(len s1-1)*(len s2-1) by A12,XREAL_1:66;
      then y'<=l by A29,XREAL_1:8;
      hence y in Seg l by A31;
    end;
    then
A32: fX c= Seg l by TARSKI:def 3;
A33: len the_schreier_series_of(s1,s2) = l by A1,Def38;
    then
A34: dom the_schreier_series_of(s1,s2) = Seg l by FINSEQ_1:def 3;
    thus fX c= dom the_schreier_series_of(s1,s2) by A32,A33,FINSEQ_1:def 3;
    set t=the_schreier_series_of(s1,s2);
A35: dom s1 c= dom f by A14,FINSEQ_1:def 3;
    now
      let x;
      assume x in dom f;
      then consider y such that
A36:  [x,y] in f by RELAT_1:def 4;
      consider k be Element of NAT such that
A37:  [k,(k-1)*(len s2-1)+1] = [x,y] & 1<=k & k<=len s1 by A36;
      k in Seg len s1 & k=x by A37,ZFMISC_1:33;
      hence x in dom s1 by FINSEQ_1:def 3;
    end;
    then dom f c= dom s1 by TARSKI:def 3;
    then
A38: dom s1 = dom f by A35,XBOOLE_0:def 10;
    then X = dom f by FINSEQ_1:def 3;
    then
A39: rng f c= Seg l by A32,RELAT_1:146;
    then
A40: dom s1 = dom(t*f) by A34,A38,RELAT_1:46;
A41: now
      let x be set;
      assume
A42:  x in dom s1;
      then
A43:  x in dom(t*f) by A34,A38,A39,RELAT_1:46;
      [x,f.x] in f by A35,A42,FUNCT_1:def 4;
      then consider i be Element of NAT such that
A44:  [i,(i-1)*(len s2-1)+1] = [x,f.x] & 1<=i & i<=len s1;
A45:  i=x & (i-1)*(len s2-1)+1=f.x by A44,ZFMISC_1:33;
      set k=(i-1)*(len s2-1)+1;
      k in rng f by A35,A42,A45,FUNCT_1:12;
      then k in Seg l by A39;
      then reconsider k as Element of NAT;
      per cases;
      suppose
A46:    i=len s1;
        (t*f).x = t.(f.x) by A43,FUNCT_1:22
          .= t.k by A44,ZFMISC_1:33
          .= (1).G by A1,A46,Def38
          .= s1.(len s1) by Def31;
        hence s1.x = (t*f).x by A44,A46,ZFMISC_1:33;
      end;
      suppose i<>len s1;
        then i<len s1 by A44,XXREAL_0:1;
        then
A47:    i+1<=len s1 by NAT_1:13;
        then
A48:    i+1-1<=len s1-1 by XREAL_1:11;
        len s2-1>1-1 by A1,XREAL_1:11;
        then
A49:    len s2-1>=0+1 by INT_1:20;
A50:    s2.1=(Omega).G by Def31;
        then reconsider H1=s1.(i+1),H2=s1.i,H3=s2.1 as strict StableSubgroup of
        G by A44,A48,Th111;
        now
          let x be set;
          assume
A51:      x in the carrier of H2;
          H2 is Subgroup of G by Def7;
          then the carrier of H2 c= the carrier of G by GROUP_2:def 5;
          hence x in the carrier of (Omega).G by A51;
        end;
        then the carrier of H2 c= the carrier of (Omega).G by TARSKI:def 3;
        then
A52:    the carrier of H2 =
        (the carrier of H2) /\ (the carrier of H3) by A50,XBOOLE_1:28;
        0+i<=1+i by XREAL_1:8;
        then 1<=i+1 by A44,XXREAL_0:2;
        then i+1 in Seg len s1 by A47;
        then
A53:    i+1 in dom s1 by FINSEQ_1:def 3;
        i in Seg len s1 by A44;
        then i in dom s1 by FINSEQ_1:def 3;
        then
A54:    H1 is normal StableSubgroup of H2 by A53,Def31;
        (t*f).x = t.(f.x) by A43,FUNCT_1:22
          .= t.k by A44,ZFMISC_1:33
          .= H1"\/"(H2/\H3) by A1,A44,A48,A49,Def38
          .= H1"\/"H2 by A52,Th18
          .= H2 by A54,Th36;
        hence s1.x = (t*f).x by A44,ZFMISC_1:33;
      end;
    end;
A55: the_schreier_series_of(s1,s2) * Sgm fX
    = the_schreier_series_of(s1,s2) * (f * Sgm X) by A14,A20,A26,Lm38
      .= (the_schreier_series_of(s1,s2) * f) * Sgm X by RELAT_1:55
      .= s1 * Sgm X by A40,A41,FUNCT_1:9
      .= s1 * idseq len s1 by FINSEQ_3:54
      .= s1 * id Seg len s1 by FINSEQ_2:def 1
      .= s1 * id dom s1 by FINSEQ_1:def 3;
    set R=s1;
    set dR=dom s1;
    set rR=rng s1;
    reconsider R as Relation of dR,rR by FUNCT_2:3;
    (id dR)*R = R by FUNCT_2:23;
    hence s1 = the_schreier_series_of(s1,s2) * Sgm fX by A55;
  end;
  hence thesis by Def32;
end;

theorem Th117:
  len s1>1 & len s2>1 implies the_schreier_series_of(s1,s2) is_equivalent_with
  the_schreier_series_of(s2,s1)
proof
  assume
A1: len s1>1 & len s2>1;
  set s12 = the_schreier_series_of(s1,s2);
  set s21 = the_schreier_series_of(s2,s1);
  len s1-1>1-1 & len s2-1>1-1 by A1,XREAL_1:11;
  then
A2: (len s1-1)*(len s2-1)>0*(len s2-1) by XREAL_1:70;
  then
A3: (len s1-1)*(len s2-1)+1>0+1 by XREAL_1:8;
A4: len s12 = (len s1-1)*(len s2-1) + 1 &
  len s21 = (len s1-1)*(len s2-1) + 1 by A1,Def38;
  then
A5: s12 is not empty & s21 is not empty by A3;
  now
    set p = {[(j-1)*(len s1-1)+i,(i-1)*(len s2-1)+j]
    where i,j is Element of NAT: 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1};
    set X = dom the_series_of_quotients_of s12;
    set f1=the_series_of_quotients_of s12;
    set f2=the_series_of_quotients_of s21;
A6: len s12>1 & len s21>1 by A1,A3,Def38;
    then
A7: len f1 + 1= len s12 & len f2 + 1 = len s21 by Def36;
A8: len s12 = (len s1-1)*(len s2-1)+1 by A1,Def38;
A9: X = Seg len f1 by FINSEQ_1:def 3;
    now
      let x;
      assume x in p;
      then consider i,j be Element of NAT such that
A10:  [(j-1)*(len s1-1)+i,(i-1)*(len s2-1)+j]=x &
      1<=i & i<=len s1-1 & 1<=j & j<=len s2-1;
      set y=(j-1)*(len s1-1)+i;
      set z=(i-1)*(len s2-1)+j;
      reconsider y,z as set;
      take y,z;
      thus x = [y,z] by A10;
    end;
    then reconsider p as Relation by RELAT_1:def 1;
    now
      let x,y1,y2 be set;
      assume [x,y1] in p;
      then consider i1,j1 be Element of NAT such that
A11:  [(j1-1)*(len s1-1)+i1,(i1-1)*(len s2-1)+j1]=[x,y1] &
      1<=i1 & i1<=len s1-1 & 1<=j1 & j1<=len s2-1;
      assume [x,y2] in p;
      then consider i2,j2 be Element of NAT such that
A12:  [(j2-1)*(len s1-1)+i2,(i2-1)*(len s2-1)+j2]=[x,y2] &
      1<=i2 & i2<=len s1-1 & 1<=j2 & j2<=len s2-1;
      (j1-1)*(len s1-1)+i1=x & (i1-1)*(len s2-1)+j1=y1 &
      (j2-1)*(len s1-1)+i2=x & (i2-1)*(len s2-1)+j2=y2 by A11,A12,ZFMISC_1:33;
      then i1=i2 & j1=j2 by A1,A11,A12,Lm46;
      hence y1=y2 by A11,A12,ZFMISC_1:33;
    end;
    then reconsider p as Function by FUNCT_1:def 1;
    now
      let x;
      assume x in dom p;
      then consider y such that
A13:  [x,y] in p by RELAT_1:def 4;
      consider i,j be Element of NAT such that
A14:  [(j-1)*(len s1-1)+i,(i-1)*(len s2-1)+j]=[x,y] &
      1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 by A13;
      set k=x;
A15:  k=(j-1)*(len s1-1)+i by A14,ZFMISC_1:33;
      reconsider k as integer number by A14,ZFMISC_1:33;
      1<=k & k<=(len s1-1)*(len s2-1) by A1,A14,A15,Lm47;
      then reconsider k as Element of NAT by INT_1:16;
      1<=k & k<=len f1 by A1,A7,A8,A14,A15,Lm47;
      hence x in X by A9;
    end;
    then
A16: dom p c= X by TARSKI:def 3;
    now
      let x;
      assume
A17:  x in X;
      then reconsider k=x as Element of NAT;
A18:  1<=k & k<=(len s1-1)*(len s2-1) by A7,A8,A9,A17,FINSEQ_1:3;
      set l'=(len s1-1)*(len s2-1);
      reconsider l' as Element of NAT by A2,INT_1:16;
      0+(len s1-1)*(len s2-1)<=1+(len s1-1)*(len s2-1) by XREAL_1:8;
      then k<=l'+1 by A18,XXREAL_0:2;
      then
A19:  k in Seg(l'+1) by A18;
A20:  k <> l'+1 by A18,NAT_1:13;
      consider j,i be Nat such that
A21:  k=(j-1)*(len s1-1)+i & 1<=j & j<=len s2-1 & 1<=i & i<=len s1-1
      by A1,A19,A20,Lm45;
      reconsider j,i as Element of NAT by INT_1:16;
      set y=(i-1)*(len s2-1)+j;
      reconsider y as set;
      [x,y] in p by A21;
      hence x in dom p by RELAT_1:def 4;
    end;
    then
A22: X c= dom p by TARSKI:def 3;
    then
A23: dom p = X by A16,XBOOLE_0:def 10;
    now
      let y;
      assume y in rng p;
      then consider x such that
A24:  [x,y] in p by RELAT_1:def 5;
      consider i,j be Element of NAT such that
A25:  [(j-1)*(len s1-1)+i,(i-1)*(len s2-1)+j]=[x,y] &
      1<=i & i<=len s1-1 & 1<=j & j<=len s2-1 by A24;
      set k=y;
A26:  k=(i-1)*(len s2-1)+j by A25,ZFMISC_1:33;
      reconsider k as integer number by A25,ZFMISC_1:33;
      1<=k & k<=(len s1-1)*(len s2-1) by A1,A25,A26,Lm47;
      then reconsider k as Element of NAT by INT_1:16;
      1<=k & k<=len f1 by A1,A7,A8,A25,A26,Lm47;
      hence y in X by A9;
    end;
    then
A27: rng p c= X by TARSKI:def 3;
    now
      let y;
      assume
A28:  y in X;
      then
A29:  y in Seg len f1 by FINSEQ_1:def 3;
      reconsider k=y as Element of NAT by A28;
A30:  1<=k & k<=(len s1-1)*(len s2-1) by A7,A8,A29,FINSEQ_1:3;
      set l'=(len s1-1)*(len s2-1);
      reconsider l' as Element of NAT by A2,INT_1:16;
      0+(len s1-1)*(len s2-1)<=1+(len s1-1)*(len s2-1) by XREAL_1:8;
      then k<=l'+1 by A30,XXREAL_0:2;
      then
A31:  k in Seg(l'+1) by A30;
A32:  k <> l'+1 by A30,NAT_1:13;
      consider i,j be Nat such that
A33:  k=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 & 1<=j & j<=len s2-1
      by A1,A31,A32,Lm45;
      reconsider j,i as Element of NAT by INT_1:16;
      set x=(j-1)*(len s1-1)+i;
      reconsider x as set;
      [x,y] in p by A33;
      hence y in rng p by RELAT_1:def 5;
    end;
    then
A34: X c= rng p by TARSKI:def 3;
    then
A35: rng p = X by A27,XBOOLE_0:def 10;
    then reconsider p as Function of X,X by A23,FUNCT_2:3;
    now
      let x1,x2 be set;
      assume x1 in X & x2 in X;
      then
A36:  [x1,p.x1] in p & [x2,p.x2] in p by A22,FUNCT_1:def 4;
      assume
A37:  p.x1 = p.x2;
      consider i1,j1 be Element of NAT such that
A38:  [(j1-1)*(len s1-1)+i1,(i1-1)*(len s2-1)+j1]=[x1,p.x1] &
      1<=i1 & i1<=len s1-1 & 1<=j1 & j1<=len s2-1 by A36;
      consider i2,j2 be Element of NAT such that
A39:  [(j2-1)*(len s1-1)+i2,(i2-1)*(len s2-1)+j2]=[x2,p.x2] &
      1<=i2 & i2<=len s1-1 & 1<=j2 & j2<=len s2-1 by A36;
      (j1-1)*(len s1-1)+i1=x1 & (i1-1)*(len s2-1)+j1=p.x1 &
      (j2-1)*(len s1-1)+i2=x2 & (i2-1)*(len s2-1)+j2=p.x2
      by A38,A39,ZFMISC_1:33;
      then i1=i2 & j1=j2 by A1,A37,A38,A39,Lm46;
      hence x1 = x2 by A38,A39,ZFMISC_1:33;
    end;
    then
A40: p is one-to-one by FUNCT_2:77;
    p is onto by A35,FUNCT_2:def 3;
    then reconsider p as Permutation of X by A40;
    take p;
    now
      let H1,H2 be GroupWithOperators of O;
      let k1,k2 be Nat;
      assume
A41:  k1 in dom f1 & k2=p".k1;
      assume
A42:  H1=f1.k1 & H2=f2.k2;
      rng(p") c= X;
      then
A43:  rng(p") c= Seg len f1 by FINSEQ_1:def 3;
A44:  p".k1 in rng(p") by A41,FUNCT_2:6;
      then p".k1 in Seg len f1 by A43;
      then
A45:  k2 in dom f2 by A4,A7,A41,FINSEQ_1:def 3;
      set l=(len s1-1)*(len s2-1)+1;
      len s1+1>1+1 & len s2+1>1+1 by A1,XREAL_1:8;
      then len s1>=2 & len s2>=2 by NAT_1:13;
      then len s1-1>=2-1 & len s2-1>=2-1 by XREAL_1:11;
      then (len s1-1)*(len s2-1)>=0*(len s2-1);
      then (len s1-1)*(len s2-1)+1>=0+1 by XREAL_1:8;
      then reconsider l as Element of NAT by INT_1:16;
A46:  k1 in Seg len f1 by A41,FINSEQ_1:def 3;
A47:  len s12 = (len s1-1)*(len s2-1)+1 by A1,Def38;
      0+(len s1-1)*(len s2-1)<=1+(len s1-1)*(len s2-1) by XREAL_1:8;
      then
A48:  Seg len f1 c= Seg l by A7,A47,FINSEQ_1:7;
      k1 <= len f1 by A46,FINSEQ_1:3;
      then k1<>(len s1-1)*(len s2-1)+1 by A7,A47,NAT_1:13;
      then consider i,j be Nat such that
A49:  k1=(i-1)*(len s2-1)+j & 1<=i & i<=len s1-1 &
      1<=j & j<=len s2-1 by A1,A46,A48,Lm45;
      p.k2 = k1 by A34,A41,FUNCT_1:57;
      then [k2,k1] in p by A23,A41,A44,FUNCT_1:8;
      then consider i',j' be Element of NAT such that
A50:  [k2,k1]=[(j'-1)*(len s1-1)+i',(i'-1)*(len s2-1)+j'] &
      1<=i' & i'<=len s1-1 & 1<=j' & j'<=len s2-1;
      (i-1)*(len s2-1)+j=(i'-1)*(len s2-1)+j' by A49,A50,ZFMISC_1:33;
      then
A51:  i=i' & j=j' by A1,A49,A50,Lm46;
      then
A52:  k2=(j-1)*(len s1-1)+i by A50,ZFMISC_1:33;
      reconsider H=s1.i,K=s2.j,H'=s1.(i+1),K'=s2.(j+1)
      as strict StableSubgroup of G by A49,Th111;
      set JH=H'"\/"(H/\K');
      set JK=K'"\/"(K/\H');
A53:  s12.k1=H'"\/"(H/\K) by A1,A49,Def38;
A54:  now
        per cases;
        suppose
A55:      j=len s2-1;
          per cases;
          suppose
A56:        i<>len s1-1;
            set i'=i+1;
            set j'=1;
            set H1 = s1.(i'+1);
            set H2 = s1.i';
            set H3 = s2.j';
            i<len s1-1 by A49,A56,XXREAL_0:1;
            then
A57:        i+1<=len s1-1 by INT_1:20;
A58:        k1+1=(i'-1)*(len s2-1)+j' by A49,A55;
            1+1<=i+1 by A49,XREAL_1:8;
            then
A59:        1<=i' by XXREAL_0:2;
            len s2-1>1-1 by A1,XREAL_1:11;
            then
A60:        len s2-1>=0+1 by INT_1:20;
            then reconsider H1,H2,H3 as strict StableSubgroup of G
            by A57,A59,Th111;
A61:        s12.(k1+1)=H1"\/"(H2/\H3) by A1,A57,A58,A59,A60,Def38;
A62:        H3 = (Omega).G by Def31;
            now
              let x be set;
              assume
A63:          x in the carrier of H2;
              H2 is Subgroup of G by Def7;
              then the carrier of H2 c= the carrier of G by GROUP_2:def 5;
              hence x in the carrier of (Omega).G by A63;
            end;
            then the carrier of H2 c=
            the carrier of (Omega).G by TARSKI:def 3;
            then the carrier of H2 = (the carrier of H2)
            /\ (the carrier of (Omega).G) by XBOOLE_1:28;
            then
A64:        s12.(k1+1) = H1"\/"H2 by A61,A62,Th18;
A65:        0+i'<=1+i' by XREAL_1:8;
            then
A66:        1<=i'+1 by A59,XXREAL_0:2;
A67:        i'+1<=len s1-1+1 by A57,XREAL_1:8;
            then i'+1 in Seg len s1 by A66;
            then
A68:        i'+1 in dom s1 by FINSEQ_1:def 3;
            i'<=len s1 by A65,A67,XXREAL_0:2;
            then i' in Seg len s1 by A59;
            then i' in dom s1 by FINSEQ_1:def 3;
            then
A69:        H1 is normal StableSubgroup of H2 by A68,Def31;
            JH = H'"\/"(H/\(1).G) by A55,Def31
              .= H'"\/"(1).G by Th21
              .= H' by Th33;
            hence s12.(k1+1)=JH by A64,A69,Th36;
          end;
          suppose
A70:        i=len s1-1;
            then
A71:        k1 = (len s1-1)*(len s2-1) by A49,A55;
A72:        H' = (1).G by A70,Def31;
            K' = (1).G by A55,Def31;
            then JH = (1).G"\/"(1).G by A72,Th21
              .= (1).G by Th33;
            hence s12.(k1+1)=JH by A1,A71,Def38;
          end;
        end;
        suppose j<>len s2-1;
          then j<len s2-1 by A49,XXREAL_0:1;
          then
A73:      j+1<=len s2-1 by INT_1:20;
          1+1<=j+1 by A49,XREAL_1:8;
          then
A74:      1<=j+1 by XXREAL_0:2;
          set k1'=k1+1;
          set j'=j+1;
          k1'=(i-1)*(len s2-1)+j' by A49;
          hence s12.(k1+1)=JH by A1,A49,A73,A74,Def38;
        end;
      end;
A75:  s21.k2=K'"\/"(K/\H) by A1,A49,A52,Def38;
A76:  now
        per cases;
        suppose
A77:      i=len s1-1;
          per cases;
          suppose
A78:        j<>len s2-1;
            set j'=j+1;
            set i'=1;
            set H1 = s2.(j'+1);
            set H2 = s2.j';
            set H3 = s1.i';
            j<len s2-1 by A49,A78,XXREAL_0:1;
            then
A79:        j+1<=len s2-1 by INT_1:20;
A80:        k2+1=(j'-1)*(len s1-1)+i' by A50,A51,A77,ZFMISC_1:33;
            1+1<=j+1 by A49,XREAL_1:8;
            then
A81:        1<=j' by XXREAL_0:2;
            len s1-1>1-1 by A1,XREAL_1:11;
            then
A82:        len s1-1>=0+1 by INT_1:20;
            then reconsider H1,H2,H3 as strict StableSubgroup of G
            by A79,A81,Th111;
A83:        s21.(k2+1)=H1"\/"(H2/\H3) by A1,A79,A80,A81,A82,Def38;
A84:        H3 = (Omega).G by Def31;
            now
              let x be set;
              assume
A85:          x in the carrier of H2;
              H2 is Subgroup of G by Def7;
              then the carrier of H2 c= the carrier of G by GROUP_2:def 5;
              hence x in the carrier of (Omega).G by A85;
            end;
            then the carrier of H2 c=
            the carrier of (Omega).G by TARSKI:def 3;
            then the carrier of H2 = (the carrier of H2)
            /\ (the carrier of (Omega).G) by XBOOLE_1:28;
            then
A86:        s21.(k2+1) = H1"\/"H2 by A83,A84,Th18;
A87:        0+j'<=1+j' by XREAL_1:8;
            then
A88:        1<=j'+1 by A81,XXREAL_0:2;
A89:        j'+1<=len s2-1+1 by A79,XREAL_1:8;
            then j'+1 in Seg len s2 by A88;
            then
A90:        j'+1 in dom s2 by FINSEQ_1:def 3;
            j'<=len s2 by A87,A89,XXREAL_0:2;
            then j' in Seg len s2 by A81;
            then j' in dom s2 by FINSEQ_1:def 3;
            then
A91:        H1 is normal StableSubgroup of H2 by A90,Def31;
            H' = (1).G by A77,Def31;
            then JK = K'"\/"(1).G by Th21
              .= K' by Th33;
            hence s21.(k2+1)=JK by A86,A91,Th36;
          end;
          suppose
A92:        j=len s2-1;
            then
A93:        k2 = (len s1-1)*(len s2-1) by A50,A51,A77,ZFMISC_1:33;
A94:        H' = (1).G by A77,Def31;
            K' = (1).G by A92,Def31;
            then JK = (1).G"\/"(1).G by A94,Th21
              .= (1).G by Th33;
            hence s21.(k2+1)=JK by A1,A93,Def38;
          end;
        end;
        suppose i<>len s1-1;
          then i<len s1-1 by A49,XXREAL_0:1;
          then
A95:      i+1<=len s1-1 by INT_1:20;
          1+1<=i+1 by A49,XREAL_1:8;
          then
A96:      1<=i+1 by XXREAL_0:2;
A97:      k2+1=(j-1)*(len s1-1)+i+1 by A50,A51,ZFMISC_1:33;
          set k2'=k2+1;
          set i'=i+1;
          k2'=(j-1)*(len s1-1)+i' by A97;
          hence s21.(k2+1)=JK by A1,A49,A95,A96,Def38;
        end;
      end;
A98:  H' is normal StableSubgroup of H &
      K' is normal StableSubgroup of K by A49,Th112;
      then reconsider JH as normal StableSubgroup of H'"\/"(H/\K) by Th92;
      reconsider JK as normal StableSubgroup of K'"\/"(K/\H) by A98,Th92;
A99:  H1 = (H'"\/"(H/\K))./.JH by A6,A41,A42,A53,A54,Def36;
      H2 = (K'"\/"(K/\H))./.JK by A6,A42,A45,A75,A76,Def36;
      hence H1,H2 are_isomorphic by A98,A99,Th93;
    end;
    hence f1,f2 are_equivalent_under p,O by A4,A7,Def37;
  end;
  hence thesis by A5,Th108;
end;

:: ALG I.4.7 Theorem 5

theorem Th118:
  ex s1',s2' st s1' is_finer_than s1 & s2' is_finer_than s2
  & s1' is_equivalent_with s2'
proof
  per cases;
  suppose
A1: len s1>1 & len s2>1;
    set s1'=the_schreier_series_of(s1,s2);
    set s2'=the_schreier_series_of(s2,s1);
    take s1',s2';
    thus s1' is_finer_than s1 & s2' is_finer_than s2 by A1,Th116;
    thus thesis by A1,Th117;
  end;
  suppose
A2: len s1<=1 or len s2<=1;
    per cases;
    suppose
A3:   len s1<=len s2;
      set s1'=s2;
      set s2'=s2;
      take s1',s2';
      thus s1' is_finer_than s1 & s2' is_finer_than s2 by A2,A3,Th114;
      thus thesis by Th113;
    end;
    suppose
A4:   len s1>len s2;
      set s1'=s1;
      set s2'=s1;
      take s1',s2';
      thus s1' is_finer_than s1 & s2' is_finer_than s2 by A2,A4,Th114;
      thus thesis by Th113;
    end;
  end;
end;

begin :: The Jordan-H\"older Theorem

:: ALG I.4.7 Theorem 6

theorem
  s1 is jordan_holder & s2 is jordan_holder implies s1 is_equivalent_with s2
proof
  assume
A1: s1 is jordan_holder;
  assume
A2: s2 is jordan_holder;
  per cases;
  suppose
A3: s1 is empty;
    now
      assume
A4:   s2 is not empty;
A5:   s2 is strictly_decreasing by A2,Def34;
      now
        set x={};
        take x;
        thus x c= dom s2 by XBOOLE_1:2;
        thus s1 = s2 * Sgm x by A3,FINSEQ_3:49;
      end;
      then s2 is_finer_than s1 by Def32;
      hence contradiction by A1,A3,A4,A5,Def34;
    end;
    hence thesis by A3,Th107;
  end;
  suppose
A6: s1 is not empty;
A7: now
      assume
A8:   s2 is empty;
A9:   s1 is strictly_decreasing by A1,Def34;
      now
        set x={};
        take x;
        thus x c= dom s1 by XBOOLE_1:2;
        thus s2 = s1 * Sgm x by A8,FINSEQ_3:49;
      end;
      then s1 is_finer_than s2 by Def32;
      hence contradiction by A2,A6,A8,A9,Def34;
    end;
    consider s1',s2' such that
A10: s1' is_finer_than s1 & s2' is_finer_than s2
    & s1' is_equivalent_with s2' by Th118;
A11: s1' is not empty & s2' is not empty by A6,A7,A10,Th97;
    then
A12: ex p' being Permutation of dom the_series_of_quotients_of s1' st
    the_series_of_quotients_of s1',the_series_of_quotients_of s2'
    are_equivalent_under p',O by A10,Th108;
    defpred P[Nat] means
    for s1',s2' st s1' is not empty & s2' is not empty & len s1'=len s1+$1 &
    s1' is_finer_than s1 & s2' is_finer_than s2 &
    ex p being Permutation of dom the_series_of_quotients_of s1' st
    the_series_of_quotients_of s1',the_series_of_quotients_of s2'
    are_equivalent_under p,O holds
    ex p being Permutation of dom the_series_of_quotients_of s1 st
    the_series_of_quotients_of s1,the_series_of_quotients_of s2
    are_equivalent_under p,O;
A13: P[0]
    proof
      let s1',s2';
      assume
A14:  s1' is not empty & s2' is not empty;
      assume
A15:  len s1' = len s1+0 & s1' is_finer_than s1;
      assume
A16:  s2' is_finer_than s2;
      given p be Permutation of dom the_series_of_quotients_of s1' such that
A17:  the_series_of_quotients_of s1',the_series_of_quotients_of s2'
      are_equivalent_under p,O;
A18:  s1' is_equivalent_with s2' by A14,A17,Th108;
      s1'=s1 by A15,Th96;
      then s2' is jordan_holder by A1,A18,Th115;
      then s2'=s2 by A2,A16,Th98;
      then s1 is_equivalent_with s2 by A15,A18,Th96;
      hence thesis by A6,A7,Th108;
    end;
A19: for n st P[n] holds P[n+1]
    proof
      let n;
      assume
A20:  P[n];
      now
        let s1',s2';
        assume s1' is not empty & s2' is not empty;
        assume
A21:    len s1' = len s1 + n+1;
        assume
A22:    s1' is_finer_than s1;
        assume
A23:    s2' is_finer_than s2;
        set f1=the_series_of_quotients_of s1';
        set f2=the_series_of_quotients_of s2';
        given p be Permutation of dom f1 such that
A24:    f1,f2 are_equivalent_under p,O;
A25:    len f1 = len f2 by A24,Def37;
        n+1+len s1>0+len s1 by XREAL_1:8;
        then consider i such that
A26:    i in dom f1 & for H st H=f1.i holds H is trivial by A1,A21,A22,Th109;
A27:    i in dom s1' & i+1 in dom s1' & s1'.i=s1'.(i+1) by A26,Th103;
        set j = p".i;
        rng(p") c= dom f1;
        then
A28:    rng(p") c= Seg len f1 by FINSEQ_1:def 3;
        (p").i in rng(p") by A26,FUNCT_2:6;
        then
A29:    (p").i in Seg len f1 by A28;
        reconsider j as Nat;
A30:    j in dom f2 by A25,A29,FINSEQ_1:def 3;
        set H1=f1.i;
        set H2=f2.j;
A31:    f1.i in rng f1 by A26,FUNCT_1:12;
        f2.j in rng f2 by A30,FUNCT_1:12;
        then reconsider H1,H2 as strict GroupWithOperators of O by A31,Th102;
A32:    H1 is trivial by A26;
        H1,H2 are_isomorphic by A24,A26,Def37;
        then for H st H=f2.j holds H is trivial by A32,Th58;
        then
A33:    j in dom s2' & j+1 in dom s2' & s2'.j=s2'.(j+1) by A30,Th103;
        reconsider s1''=Del(s1',i) as FinSequence of the_stable_subgroups_of G
        by FINSEQ_3:114;
        reconsider s1'' as CompositionSeries of G by A27,Th94;
        reconsider s2''=Del(s2',j) as FinSequence of the_stable_subgroups_of G
        by FINSEQ_3:114;
        reconsider s2'' as CompositionSeries of G by A33,Th94;
A34:    len s1'' = len s1 + n by A21,A27,FINSEQ_3:118;
A35:    s1'' is_finer_than s1 by A1,A22,A27,Th99;
A36:    s2'' is_finer_than s2 by A2,A23,A33,Th99;
A37:    the_series_of_quotients_of s1''= Del(f1,i) by A27,Th104;
A38:    the_series_of_quotients_of s2''= Del(f2,j) by A33,Th104;
A39:    s1'' is not empty & s2'' is not empty by A1,A2,A6,A7,A22,A23,A27,A33
,Th97,Th99;
        ex p be Permutation of dom the_series_of_quotients_of s1''
        st the_series_of_quotients_of s1'',the_series_of_quotients_of s2''
        are_equivalent_under p,O by A24,A26,A37,A38,Th106;
        hence thesis by A20,A34,A35,A36,A39;
      end;
      hence thesis;
    end;
A40: for n holds P[n] from NAT_1:sch 2(A13,A19);
    ex n st len s1' = len s1 + n by A10,Th95;
    then ex p being Permutation of dom the_series_of_quotients_of s1 st
    the_series_of_quotients_of s1,the_series_of_quotients_of s2
    are_equivalent_under p,O by A10,A11,A12,A40;
    hence thesis by A6,A7,Th108;
  end;
end;

begin :: Appendix

theorem
  for P,R being Relation holds P = (rng P)|R iff P~ = (R~)|(dom (P~)) by Lm36;

theorem
  for X being set, P,R being Relation holds P*(R|X) = (X|P)*R by Lm37;

theorem
  for n being Nat, X being set, f being PartFunc of REAL, REAL
  st X c= Seg n & X c= dom f & f|X is increasing & f.:X c= NAT \ {0}
  holds Sgm(f.:X) = f * Sgm X by Lm38;

theorem
  for y being set, i,n being Nat st y c= Seg(n+1) & i in Seg(n+1) & not i in y
  holds ex x st Sgm x = Sgm(Seg(n+1)\{i})" * Sgm y & x c= Seg n by Lm39;

theorem
  for D being non empty set, f being FinSequence of D,
  p being Element of D, n being Nat
  st n in dom f holds f = Del(Ins(f,n,p),n+1) by Lm44;

theorem
  for G,H being Group, F1 being FinSequence of the carrier of G,
  F2 being FinSequence of the carrier of H,
  I being FinSequence of INT, f being Homomorphism of G,H
  st (for k being Nat st k in dom F1 holds F2.k = f.(F1.k))
  & len F1 = len I & len F2 = len I
  holds f.(Product(F1 |^ I)) = Product(F2 |^ I) by Lm24;

