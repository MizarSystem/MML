:: Reconstructions of Special Sequences
::  by Yatsuka Nakamura and Roman Matuszewski
::
:: Received December 10, 1996
:: Copyright (c) 1996 Association of Mizar Users

environ

 vocabularies ARYTM_1, FINSEQ_1, FUNCT_1, RELAT_1, FINSEQ_5, RFINSEQ, BOOLE,
      EUCLID, TOPREAL1, TARSKI, PRE_TOPC, MCART_1, SPPOL_2, GROUP_2, SQUARE_1,
      ARYTM_3, JORDAN3, FINSEQ_4, ORDINAL2, ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, ORDINAL2, NUMBERS, XREAL_0,
      XCMPLX_0, REAL_1, NAT_1, RFINSEQ, BINARITH, RELAT_1, FUNCT_1, FINSEQ_1,
      FINSEQ_4, FINSEQ_5, CQC_SIM1, STRUCT_0, TOPREAL1, PRE_TOPC, EUCLID,
      SPPOL_2, XXREAL_0;
 constructors GOBOARD9, BINARITH, RFINSEQ, REAL_1, REALSET1, FINSEQ_4,
      CQC_SIM1, MEMBERED, NAT_1, XXREAL_0;
 registrations SUBSET_1, STRUCT_0, RELSET_1, FUNCT_1, EUCLID, SPPOL_2,
      FINSEQ_5, XREAL_0, MEMBERED, ORDINAL2, NAT_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI;
 theorems TARSKI, TOPREAL1, FINSEQ_1, FINSEQ_4, RFINSEQ, SPPOL_1, FUNCT_1,
      NAT_1, REAL_1, REAL_2, BINARITH, TOPREAL3, SPPOL_2, FINSEQ_5, EUCLID,
      FINSEQ_6, ZFMISC_1, FINSEQ_3, RELAT_1, CQC_SIM1, PARTFUN2,
      AMI_5, XBOOLE_0, XBOOLE_1, FINSEQ_2, XCMPLX_1, XREAL_1;

begin

::-------------------------------------------:
::              Preliminaries                :
::-------------------------------------------:

reserve i,j,k,i1,i2,n for natural number;

theorem Th1:
 i-'i1>=1 or i-i1>=1 implies i-'i1=i-i1
proof
 assume A1: i-'i1>=1 or i-i1>=1;
 per cases by A1;
 suppose A2:i-'i1>=1;
  i-i1>=0 by A2,BINARITH:def 3;
 hence thesis by BINARITH:def 3;
 end;
 suppose i-i1>=1;
   then 0<=i-i1;
 hence thesis by BINARITH:def 3;
end;
end;

theorem Th2:n-'0=n
proof
  n-'0=n-0 by BINARITH:def 3 .=n;
 hence thesis;
end;

canceled;

theorem Th4: i1<=i2 implies n-'i2<=n-'i1
proof assume A1:i1<=i2;
 per cases;
 suppose A2:i2<=n; then i1<=n by A1,XREAL_1:2;
    then A3:n-'i1=n-i1 by BINARITH:50;
        n-'i2=n-i2 by A2,BINARITH:50;
  hence n-'i2<=n-'i1 by A1,A3,REAL_2:106;
 end;
 suppose i2>n; then n-i2<0 by REAL_2:105; then n-'i2=0 by BINARITH:def 3;
  hence n-'i2<=n-'i1;
end;
end;

theorem Th5: i1<=i2 implies i1-'n<=i2-'n
proof assume A1:i1<=i2;
 per cases;
 suppose i1-n>=0; then A2:i1-'n=i1-n by BINARITH:def 3;
     i1-n<=i2-n by A1,XREAL_1:11;
   hence thesis by A2,BINARITH:def 3;
 end;
 suppose i1-n<0; then i1-'n=0 by BINARITH:def 3;
  hence thesis;
end;
end;

theorem Th6:i-'i1>=1 or i-i1>=1 implies i-'i1+i1=i
proof assume i-'i1>=1 or i-i1>=1;
 then i-'i1+i1=i-i1+i1 by Th1 .=i;
 hence thesis;
end;

theorem Th7:i1<=i2 implies i1-'1<=i2
proof assume A1:i1<=i2;
 per cases;
 suppose i1-1>=0; then i1-'1=i1-1 by BINARITH:def 3;
  then i1-'1<=i1-1+1 by NAT_1:37;
  hence i1-'1<=i2 by A1,XREAL_1:2;
 end;
 suppose i1-1<0; then i1-'1=0 by BINARITH:def 3;
  hence i1-'1<=i2;
end;
end;

theorem Th8:i-'2=i-'1-'1
proof
 per cases;
 suppose A1:i>=2;
      1<=i by A1,XREAL_1:2;
  then i-1>=0 by XREAL_1:50;
  then A2:i-'1=i-1 by BINARITH:def 3;
      i-1>=1+1-1 by A1,XREAL_1:11;
  then i-1-1>=1-1 by XREAL_1:11;
  then i-'1-'1 =i-2 by A2,BINARITH:def 3;
  hence thesis by BINARITH:def 3;
 end;
 suppose A3:i<2; then i-2<2-2 by XREAL_1:11;
 then A4:i-'2=0 by BINARITH:def 3; A5: i+1-1<=1+1-1 by A3,NAT_1:38;
     now per cases;
 case 1<=i; then i=1 by A5,XREAL_1:1;
  then A6:i-'1-'1=0-'1 by BINARITH:51;
    0<=0-'1;
  hence thesis by A4,A6,BINARITH:52;
 end;
 case i<1; then i-1<1-1 by XREAL_1:11;
  then A7:i-'1-'1=0-'1 by BINARITH:def 3;
    0<=0-'1;
  hence thesis by A4,A7,BINARITH:52;
 end;
 end;
 hence thesis;
end;
end;

theorem Th9:
 i1+1<=i2 implies i1-'1<i2 & i1-'2<i2 & i1<=i2
proof assume
A1: i1+1<=i2;
then A2: i1<i2 by NAT_1:38;
     i1-'1<=i1 by BINARITH:52;
 hence A3:i1-'1<i2 by A2,XREAL_1:2;
 A4:i1-'1-'1=i1-'2 by Th8;
     i1-'1-'1<=i1-'1 by BINARITH:52;
 hence i1-'2<i2 by A3,A4,XREAL_1:2;
 thus thesis by A1,NAT_1:38;
end;

theorem Th10:i1+2<=i2 or i1+1+1<=i2
        implies i1+1<i2 & i1+1-'1<i2 & i1+1-'2<i2 & i1+1<=i2 &
                i1-'1+1<i2 & i1-'1+1-'1<i2 &
               i1<i2 & i1-'1<i2 & i1-'2<i2 & i1<=i2
proof assume i1+2<=i2 or i1+1+1<=i2;
  then i1+1+1<=i2;
hence A1:i1+1<i2 & i1+1-'1<i2 & i1+1-'2<i2 & i1+1<=i2 by Th9,NAT_1:38;
        i1-'1<=i1 by BINARITH:52; then i1-'1+1<=i1+1 by XREAL_1:8;
    then A2:i1-'1+1<i2 by A1,XREAL_1:2;
        i1-'1+1-'1<=i1-'1+1 by BINARITH:52;
hence i1-'1+1<i2 & i1-'1+1-'1<i2 by A2,XREAL_1:2;
thus i1<i2 & i1-'1<i2 & i1-'2<i2 & i1<=i2 by A1,Th9,NAT_1:38;
end;

theorem Th11:i1<=i2 or i1<=i2-'1
     implies i1<i2+1 & i1<=i2+1 & i1<i2+1+1 & i1<=i2+1+1 & i1<i2+2 & i1<=i2+2
proof assume A1:i1<=i2 or i1<=i2-'1;
  A2:now assume i1<=i2;
   then A3:i1<i2+1 by NAT_1:38;
       i2+1+1=i2+(1+1);
   hence i1<i2+1 & i1<=i2+1 & i1<i2+1+1 & i1<=i2+1+1 & i1<i2+2 & i1<=i2+2
                  by A3,NAT_1:38;
  end;
      now assume A4:i1<=i2-'1;
       i2-'1<=i2 by BINARITH:52;
  hence i1<i2+1 & i1<=i2+1 & i1<i2+1+1 & i1<=i2+1+1 & i1<i2+2 & i1<=i2+2
                      by A2,A4,XREAL_1:2;
  end;
 hence thesis by A1,A2;
end;

theorem Th12:i1<i2 or i1+1<=i2 implies i1<=i2-'1
proof assume A1:i1<i2 or i1+1<=i2;
 per cases by A1;
 suppose A2:i1<i2; then i1+1<=i2 by NAT_1:38; then A3: i1+1-1<=i2-1 by
XREAL_1:11; 0+1<=i2 by A2,NAT_1:38;
  hence i1<=i2-'1 by A3,BINARITH:50;
 end;
 suppose A4:i1+1<=i2; then A5: i1+1-1<=i2-1 by XREAL_1:11;
    0<i2 by A4;
  then 0+1<=i2 by NAT_1:38;
  hence i1<=i2-'1 by A5,BINARITH:50;
end;
end;

theorem Th13:i>=i1 implies i>=i1-'i2
proof assume A1:i>=i1;
      i1>=i1-'i2 by BINARITH:52;
  hence thesis by A1,XREAL_1:2;
end;

theorem
      1<=i & 1<=i1-'i implies i1-'i<i1
proof assume A1:1<=i & 1<=i1-'i;
 then A2:i1-i=(i1-'i)+i-i by Th6 .=i1-'i;
 A3:i>0 by A1;
      i1-(i1-'i) = i by A2;
 hence thesis by A3,REAL_2:106;
end;

reserve r,r1,r2 for Real;
reserve n,i,i1,i2,j,j1,j2 for Nat;
reserve D for non empty set;
reserve f for FinSequence of D;

theorem Th15:for p,q being FinSequence st len p<i
 & (i<=len p +len q or i<=len (p^q)) holds
  (p^q).i=q.(i-len p)
proof let p,q be FinSequence;assume A1:len p<i &
 (i<=len p +len q or i<=len (p^q));
 then len p +1<=i by NAT_1:38; then A2: len p +1-len p<=i-len p by XREAL_1:11;
 then A3:0<=i-len p;
 then A4:i-len p=i-'len p by BINARITH:def 3;
 A5:len p+(i-'len p)=len p+(i-len p) by A3,BINARITH:def 3 .=i;
     i<=len p + len q by A1,FINSEQ_1:35;
 then i-len p<=len p+len q -len p by XREAL_1:11;
 then i-'len p in dom q by A2,A4,FINSEQ_3:27;
 hence thesis by A5,FINSEQ_1:def 7;
end;

canceled;

theorem Th17:for x being set,f being FinSequence of D st 1<=len f holds
 (f^<*x*>).1=f.1 & (f^<*x*>).1=f/.1 &
 (<*x*>^f).(len f +1)=f.len f & (<*x*>^f).(len f +1)=f/.len f
proof let x be set,f be FinSequence of D;assume A1:1<=len f;
 then 1 in dom f by FINSEQ_3:27;
 then A2:(f^<*x*>).1=f.1 by FINSEQ_1:def 7;
 A3:len f in dom f by A1,FINSEQ_3:27;
     (<*x*>^f).(len f +1)
  =(<*x*>^f).(len <*x*>+len f) by FINSEQ_1:56
 .=f.len f by A3,FINSEQ_1:def 7;
 hence thesis by A1,A2,FINSEQ_4:24;
end;

theorem Th18:for f being FinSequence st len f=1 holds Rev f=f
proof let f be FinSequence;
 assume len f=1;
 then f=<*f.1*> by FINSEQ_1:57;
 hence Rev f=f by FINSEQ_5:63;
end;

theorem Th19:for f being FinSequence of D,k being Nat holds
       len (f/^k)=len f-'k
proof let f be FinSequence of D,k be Nat;
 per cases;
 suppose A1:k<=len f; then len f-'k=len f-k by BINARITH:50;
  hence len (f/^k)=len f-'k by A1,RFINSEQ:def 2;
 end;
 suppose A2:k>len f; then (f/^k)=<*>D by RFINSEQ:def 2;
   then A3:len (f/^k)=0 by FINSEQ_1:32;
       len f-k<0 by A2,REAL_2:105;
  hence len (f/^k)=len f-'k by A3,BINARITH:def 3;
end;
end;

canceled;

theorem Th21:for f being FinSequence of D,l1,l2 being Nat holds
         (f/^l1)|(l2-'l1)=(f|l2)/^l1
proof let f be FinSequence of D,l1,l2 be Nat;
 A1:now assume A2:l1<=l2;
      now per cases;
  case A3:l2<=len f;
    then A4:l1<=len f by A2,XREAL_1:2;
    then A5:len (f/^l1)=len f-l1 by RFINSEQ:def 2;
    A6:l1<=len (f|l2) by A2,A3,FINSEQ_1:80;
        l2-l1<=len f-l1 by A3,XREAL_1:11;
    then A7:l2-'l1<=len (f/^l1) by A2,A5,BINARITH:50;
    then A8:len ((f/^l1)|(l2-'l1)) = l2-'l1 by FINSEQ_1:80;
    A9:len ((f|l2)/^l1)=len (f|l2)-l1 by A6,RFINSEQ:def 2
                  .=l2-l1 by A3,FINSEQ_1:80
                  .=l2-'l1 by A2,BINARITH:50;
        for k being Nat st 1<=k & k<=len ((f|l2)/^l1) holds
    ((f/^l1)|(l2-'l1)).k=((f|l2)/^l1).k
    proof let k be Nat such that
A10: 1<=k and
A11: k<=len ((f|l2)/^l1);
     A12:k<=len (f/^l1) by A7,A9,A11,XREAL_1:2;
     A13:k in Seg (l2-'l1) by A9,A10,A11,FINSEQ_1:3;
     A14:k in dom (f/^l1) by A10,A12,FINSEQ_3:27;
         k<=l2-l1 by A2,A9,A11,BINARITH:50; then A15: k+l1<=l2-l1+l1 by
XREAL_1:8;
     A16:k in dom ((f|l2)/^l1) by A10,A11,FINSEQ_3:27;
         ((f/^l1)|(l2-'l1)).k
     =((f/^l1)|Seg (l2-'l1)).k by FINSEQ_1:def 15
     .=(f/^l1).k by A13,FUNCT_1:72
     .=f.(k+l1) by A4,A14,RFINSEQ:def 2
     .=(f|l2).(k+l1) by A15,FINSEQ_3:121
     .=((f|l2)/^l1).k by A6,A16,RFINSEQ:def 2;
     hence ((f/^l1)|(l2-'l1)).k=((f|l2)/^l1).k;
    end;
   hence (f/^l1)|(l2-'l1)=(f|l2)/^l1 by A8,A9,FINSEQ_1:18;
  end;
  case A17:l2>len f;
    then A18:f|l2=f by FINSEQ_1:79;
        len (f/^l1)=len f-'l1 by Th19;
    then len (f/^l1)<=l2-'l1 by A17,Th5;
   hence (f/^l1)|(l2-'l1)=(f|l2)/^l1 by A18,FINSEQ_1:79;
  end;
  end;
  hence (f/^l1)|(l2-'l1)=(f|l2)/^l1;
 end;
     now assume A19:l1>l2; then l1-l1>l2-l1 by XREAL_1:11; then l2-'l1=0 by
BINARITH:def 3;
   then (f/^l1)|(l2-'l1) =(f/^l1)|({} NAT) by FINSEQ_1:4,def 15;
   then dom ((f/^l1)|(l2-'l1)) =dom (f/^l1) /\ ({} NAT) by FUNCT_1:68
   .=({} NAT);
   then A20:(f/^l1)|(l2-'l1)=<*>(D) by FINSEQ_1:26;
       now per cases;
   case l1<=len f; then l2<len f by A19,XREAL_1:2;
    then l1>len (f|l2) by A19,FINSEQ_1:80;
    hence (f/^l1)|(l2-'l1)=(f|l2)/^l1 by A20,FINSEQ_5:35;
   end;
   case A21:l1>len f;
        len (f|l2)<=len f by FINSEQ_5:18;
    then l1>len (f|l2) by A21,XREAL_1:2;
    hence (f/^l1)|(l2-'l1)=(f|l2)/^l1 by A20,FINSEQ_5:35;
   end;
   end;
  hence (f/^l1)|(l2-'l1)=(f|l2)/^l1;
 end;
 hence (f/^l1)|(l2-'l1)=(f|l2)/^l1 by A1;
end;

begin
::-------------------------------------------:
::    Middle Function for Finite Sequences   :
::-------------------------------------------:

definition let D; let f be FinSequence of D,k1,k2 be Nat;
 func mid(f,k1,k2) -> FinSequence of D equals
 :Def1:(f/^(k1-'1))|(k2-'k1+1) if k1<=k2
   otherwise Rev ((f/^(k2-'1))|(k1-'k2+1));
correctness;
end;

theorem Th22:for f being FinSequence of D,k1,k2 being Nat
  st 1<=k1 & k1<=len f & 1<=k2 & k2<=len f
  holds Rev mid(f,k1,k2)=mid(Rev f,len f-'k2+1,len f-'k1+1)
proof let f be FinSequence of D,k1,k2 be Nat;
  assume A1: 1<=k1 & k1<=len f & 1<=k2 & k2<=len f;
 per cases;
 suppose A2:k1<=k2;
   A3:len f-'k1=len f-k1 by A1,BINARITH:50;
   A4:len f-'k2=len f-k2 by A1,BINARITH:50;
   A5:k1-'1<=len f by A1,Th13;
   then A6:(len f-'(k1-'1))+(k1-'1)
   =(len f-(k1-'1))+(k1-'1)by BINARITH:50
   .=len f;
   A7:len f-'(k1-'1)=len f-(k1-'1) by A5,BINARITH:50
   .=len f-(k1-1) by A1,BINARITH:50
   .=len f-k1+1 .=len f-'k1+1 by A1,BINARITH:50;
       len f-'k1>=len f-'k2 by A2,Th4;
   then A8:len f-'k1+1>=len f-'k2+1 by XREAL_1:8;
       k2-k1<=len f-k1 by A1,XREAL_1:11;
   then k2-'k1<=len f-k1 by A2,BINARITH:50;
   then k2-'k1<=len f-'k1 by A1,BINARITH:50;
   then k2-'k1+1<=len f-'k1+1 by XREAL_1:8;
   then A9:(len f-'(k1-'1)-'(k2-'k1+1))
    =(len f-'k1+1)-(k2-'k1+1) by A7,BINARITH:50
   .=(len f-k1+1)-(k2-k1+1) by A2,A3,BINARITH:50
   .=len f+1-1-k2
   .=len f-'k2 by A1,BINARITH:50;
   A10:len f-'k1>=len f-'k2 by A2,Th4;
       len f-'k1<=len f-'k1+1 by NAT_1:29;
   then len f-'k2<=len f-'k1+1 by A10,XREAL_1:2;
   then A11:len f-'(k1-'1)-'(len f-'k2)
   =len f-'k1+1-(len f-'k2) by A7,BINARITH:50
   .=len f-k1+1-(len f-k2) by A1,A3,BINARITH:50
   .=k2-k1+1
   .=k2-'k1+1 by A2,BINARITH:50;
   A12:k1-'1<=len f by A1,Th13;
   A13:(len (f/^(k1-'1))-'(k2-'k1+1))+(k2-'k1+1)
    =len f-'k2+(k2-'k1+1) by A9,Th19
   .=len f-k2+(k2-k1+1) by A2,A4,BINARITH:50
   .=len f-(k1-1)
   .=len f-(k1-'1) by A1,BINARITH:50
   .=len f-'(k1-'1) by A12,BINARITH:50
   .=len (f/^(k1-'1)) by Th19;
   A14:(len f-'k1+1)-'(len f-'k2+1)+1
    =(len f-'k1+1)-(len f-'k2+1)+1 by A8,BINARITH:50
    .=(len f-k1+1)-(len f-k2+1)+1 by A1,A3,BINARITH:50
    .=k2-k1+1
    .=k2-'k1+1 by A2,BINARITH:50;
         len f-'k1>=len f-'k2 by A2,Th4;
     then len f-'k1+1>=len f-'k2+1 by XREAL_1:8;
   then mid(Rev f,len f-'k2+1,len f-'k1+1)
    =(((Rev f)/^((len f-'k2+1)-'1))|(k2-'k1+1)) by A14,Def1
   .=(Rev f/^(len f-'k2))|(len f-'(k1-'1)-'(len f-'k2)) by A11,BINARITH:39
   .=(Rev f|(len f-'(k1-'1)))/^(len f-'k2) by Th21
   .=(Rev f|(len f-'(k1-'1)))/^(len (f/^(k1-'1))-'(k2-'k1+1)) by A9,Th19
   .=(Rev (f/^(k1-'1)))/^(len (f/^(k1-'1))-'(k2-'k1+1)) by A6,FINSEQ_6:90
   .=Rev (((f/^(k1-'1))|(k2-'k1+1))) by A13,FINSEQ_6:91
   .=Rev mid(f,k1,k2) by A2,Def1;
  hence Rev mid(f,k1,k2)=mid(Rev f,len f-'k2+1,len f-'k1+1);
 end;
 suppose A15:k1>k2;
   A16:len f-'k2=len f-k2 by A1,BINARITH:50;
   A17:len f-'k1=len f-k1 by A1,BINARITH:50;
   A18:k2-'1<=len f by A1,Th13;
   then A19:(len f-'(k2-'1))+(k2-'1)
   =(len f-(k2-'1))+(k2-'1)by BINARITH:50
   .=len f;
   A20:len f-'(k2-'1)=len f-(k2-'1) by A18,BINARITH:50
   .=len f-(k2-1) by A1,BINARITH:50
   .=len f-k2+1 .=len f-'k2+1 by A1,BINARITH:50;
       len f-'k2>=len f-'k1 by A15,Th4;
   then A21:len f-'k2+1>=len f-'k1+1 by XREAL_1:8;
       k1-k2<=len f-k2 by A1,XREAL_1:11;
   then k1-'k2<=len f-k2 by A15,BINARITH:50;
   then k1-'k2<=len f-'k2 by A1,BINARITH:50;
   then k1-'k2+1<=len f-'k2+1 by XREAL_1:8;
   then A22:(len f-'(k2-'1)-'(k1-'k2+1))
    =(len f-'k2+1)-(k1-'k2+1) by A20,BINARITH:50
   .=(len f-k2+1)-(k1-k2+1) by A15,A16,BINARITH:50
   .=len f+1-1-k1
   .=len f-'k1 by A1,BINARITH:50;
   A23:len f-'k2>=len f-'k1 by A15,Th4;
       len f-'k2<=len f-'k2+1 by NAT_1:29;
   then len f-'k1<=len f-'k2+1 by A23,XREAL_1:2;
   then A24:len f-'(k2-'1)-'(len f-'k1)
   =len f-'k2+1-(len f-'k1) by A20,BINARITH:50
   .=len f-k2+1-(len f-k1) by A1,A16,BINARITH:50
   .=k1-k2+1
   .=k1-'k2+1 by A15,BINARITH:50;
   A25:k2-'1<=len f by A1,Th13;
   A26:(len (f/^(k2-'1))-'(k1-'k2+1))+(k1-'k2+1)
    =len f-'k1+(k1-'k2+1) by A22,Th19
   .=len f-k1+(k1-k2+1) by A15,A17,BINARITH:50
   .=len f-(k2-1)
   .=len f-(k2-'1) by A1,BINARITH:50
   .=len f-'(k2-'1) by A25,BINARITH:50
   .=len (f/^(k2-'1)) by Th19;
   A27:(len f-'k2+1)-'(len f-'k1+1)+1
    =(len f-'k2+1)-(len f-'k1+1)+1 by A21,BINARITH:50
    .=(len f-k2+1)-(len f-k1+1)+1 by A1,A16,BINARITH:50
    .=k1-k2+1
    .=k1-'k2+1 by A15,BINARITH:50;
         len f-k2>len f-k1 by A15,REAL_2:105;
     then len f-'k2+1>len f-'k1+1 by A16,A17,XREAL_1:8;
   then mid(Rev f,len f-'k2+1,len f-'k1+1)
    =Rev((((Rev f)/^((len f-'k1+1)-'1))|(k1-'k2+1))) by A27,Def1
   .=Rev((Rev f/^(len f-'k1))|(len f-'(k2-'1)-'(len f-'k1))) by A24,BINARITH:39
   .=Rev((Rev f|(len f-'(k2-'1)))/^(len f-'k1)) by Th21
   .=Rev((Rev f|(len f-'(k2-'1)))/^(len (f/^(k2-'1))-'(k1-'k2+1))) by A22,Th19
   .=Rev((Rev (f/^(k2-'1)))/^(len (f/^(k2-'1))-'(k1-'k2+1))) by A19,FINSEQ_6:90
   .=Rev(Rev (((f/^(k2-'1))|(k1-'k2+1)))) by A26,FINSEQ_6:91
   .= Rev mid(f,k1,k2) by A15,Def1;
  hence Rev mid(f,k1,k2)=mid(Rev f,len f-'k2+1,len f-'k1+1);
end;
end;

theorem Th23:
 for n,m being Nat,f being FinSequence of D st
  1<= m &m+n<=len f holds (f/^n).m=f.(m+n)
proof let n,m be Nat,f be FinSequence of D;
  assume A1: 1<= m & m+n<=len f;
      n<=n+m by NAT_1:29;
  then A2:n<=len f by A1,XREAL_1:2;
  then A3:len (f/^n) = len f -n by RFINSEQ:def 2;
      m+n-n<=len f -n by A1,XREAL_1:11;
  then m in dom (f/^n) by A1,A3,FINSEQ_3:27;
 hence (f/^n).m=f.(m+n) by A2,RFINSEQ:def 2;
end;

theorem Th24: for i being Nat,f being FinSequence of D st
  1<=i & i<=len f holds (Rev f).i=f.(len f -i+1)
proof let i be Nat,f be FinSequence of D;
 assume 1<=i & i<=len f;
  then i in dom (f) by FINSEQ_3:27;
 hence (Rev f).i=f.(len f -i+1) by FINSEQ_5:61;
end;

theorem   for f being FinSequence of D,k being Nat st
 1<=k holds mid(f,1,k)=f|k
proof let f be FinSequence of D,k be Nat;
 assume A1:1<=k;
  then A2:mid(f,1,k)=(f/^(1-'1))|(k-'1+1) by Def1;
      1-'1=0 by BINARITH:51;
  then f/^(1-'1)=f by FINSEQ_5:31;
 hence mid(f,1,k)=f|k by A1,A2,BINARITH:53;
end;

theorem Th26:for f being FinSequence of D,k being Nat st
 k<=len f holds mid(f,k,len f)=f/^(k-'1)
proof let f be FinSequence of D,k be Nat;
 assume A1:k<=len f;
  then A2:mid(f,k,len f)=(f/^(k-'1))|(len f-'k+1) by Def1;
      k-'1<=len f by A1,Th7;
  then A3:len (f/^(k-'1))=len f-(k-'1) by RFINSEQ:def 2;
  A4:len f-'k+1=len f-k+1 by A1,BINARITH:50 .=len f -(k-1);
      now per cases;
  case A5:k=0; then len f-'k+1=len f +1 by Th2;
    then len f <= len f-'k+1 by NAT_1:29;
    then A6:Seg len f c= Seg (len f -'k+1) by FINSEQ_1:7;
        0-1<0;
    then k-'1=0 by A5,BINARITH:def 3;
    then f/^(k-'1)=f by FINSEQ_5:31;
    then dom (f/^(k-'1)) c= Seg (len f -'k+1) by A6,FINSEQ_1:def 3;
    then (f/^(k-'1))|Seg (len f -'k+1)=
      f/^(k-'1) by RELAT_1:97;
   hence mid(f,k,len f)=f/^(k-'1) by A2,FINSEQ_1:def 15;
  end;
  case k<>0; then k>0; then 0+1<=k by NAT_1:38;
    then len (f/^(k-'1))=len f-'k+1 by A3,A4,BINARITH:50;
   hence
        mid(f,k,len f)=(f/^(k-'1))|Seg len (f/^(k-'1)) by A2,FINSEQ_1:def 15
    .=(f/^(k-'1))|dom (f/^(k-'1)) by FINSEQ_1:def 3
    .=(f/^(k-'1)) by RELAT_1:97;
  end;
  end;
 hence mid(f,k,len f)=f/^(k-'1);
end;

theorem Th27:for f being FinSequence of D,k1,k2 being Nat st
 1<=k1 & k1<=len f & 1<=k2 & k2<=len f holds
 mid(f,k1,k2).1=f.k1 &
  (k1<=k2 implies len mid(f,k1,k2) = k2 -' k1 +1 &
    for i being Nat st 1<=i & i<=len mid(f,k1,k2)
        holds mid(f,k1,k2).i=f.(i+k1-'1))
  & (k1>k2 implies len mid(f,k1,k2) = k1 -' k2 +1 &
    for i being Nat st 1<=i & i<=len mid(f,k1,k2)
      holds mid(f,k1,k2).i=f.(k1-'i+1))
proof let f be FinSequence of D,k11,k21 be Nat;
  assume A1: 1<=k11 & k11<=len f & 1<=k21 & k21<=len f;
   A2:now let k1,k2 be Nat;
       now assume A3:k1<=k2 & 1<=k1 & k1<=len f;
     then A4:mid(f,k1,k2).1=((f/^(k1-'1))|(k2-'k1+1)).1 by Def1
                  .=((f/^(k1-'1))|(Seg (k2-'k1+1))).1 by FINSEQ_1:def 15;
         k1-1>=0 by A3,XREAL_1:50;
     then A5:1+(k1-'1)=1+(k1-1) by BINARITH:def 3 .=k1;
     then 1+(k1-'1)<=len f -(k1-'1)+(k1-'1) by A3;
     then A6:1<=len f - (k1-'1) by XREAL_1:8;
     A7:k1-'1<=len f by A3,Th7;
     then A8:len (f/^(k1-'1))=len f -(k1-'1) by RFINSEQ:def 2;
     0+1<=k2-'k1+1 by XREAL_1:8;
     then 1 in Seg (k2-'k1+1) & 1 in Seg(len (f/^(k1-'1))) by A6,A8,FINSEQ_1:3
;
     then A9:1 in dom (f/^(k1-'1)) & 1 in Seg(k2-'k1+1) by FINSEQ_1:def 3;
     then 1 in dom (f/^(k1-'1)) /\ Seg(k2-'k1+1) by XBOOLE_0:def 3;
     then 1 in dom ((f/^(k1-'1))|(Seg (k2-'k1+1))) by RELAT_1:90;
     then mid(f,k1,k2).1=(f/^(k1-'1)).1 by A4,FUNCT_1:70;
    hence mid(f,k1,k2).1=f.k1 by A5,A7,A9,RFINSEQ:def 2;
    end;
   hence k1<=k2 & 1<=k1 & k1<=len f implies mid(f,k1,k2).1=f.k1;
   end;
   thus mid(f,k11,k21).1=f.k11
   proof per cases;
    suppose k11<=k21;
    hence mid(f,k11,k21).1=f.k11 by A1,A2;
    end;
    suppose A10:k11>k21;
    A11:1<=(k11-'k21+1) by NAT_1:29;
        k21-'1<=len f by A1,Th7;
    then A12:len (f/^(k21-'1))=len f -(k21-'1) by RFINSEQ:def 2;
A13:  k21-1>=0 by A1,XREAL_1:50;
A14:  k11-k21>=0 by A10,XREAL_1:50;
    then A15:k11-'k21=k11-k21 by BINARITH:def 3;
        k11-'k21+1=k11-k21+1 by A14,BINARITH:def 3 .=k11-(k21-1)
                  .=k11-(k21-'1) by A13,BINARITH:def 3;
    then A16:k11-'k21+1 <=len (f/^(k21-'1)) by A1,A12,XREAL_1:11;
    then A17:len ((f/^(k21-'1))|(k11-'k21+1))=(k11-'k21+1) by FINSEQ_1:80;
    then A18:k11-'k21+1 in dom ((f/^(k21-'1))|(k11-'k21+1)) by A11,FINSEQ_3:27;
    A19:k11-'k21+1 in dom (f/^(k21-'1)) by A11,A16,FINSEQ_3:27;
    A20:k21-'1 +(k11-'k21+1)= k21-1+(k11-(k21-1)) by A13,A15,BINARITH:def 3
                           .=k11;
    A21:((f/^(k21-'1))|(k11-'k21+1)).len ((f/^(k21-'1))|(k11-'k21+1))
     =((f/^(k21-'1))|(k11-'k21+1))/.(k11-'k21+1) by A11,A17,FINSEQ_4:24
    .=(f/^(k21-'1))/.(k11-'k21+1) by A18,FINSEQ_4:85
    .=f/.((k21-'1 +(k11-'k21+1))) by A19,FINSEQ_5:30
    .=f.k11 by A1,A20,FINSEQ_4:24;
        1 in dom ((f/^(k21-'1))|(k11-'k21+1)) by A11,A17,FINSEQ_3:27;
    then (Rev ((f/^(k21-'1))|(k11-'k21+1))).1
    = ((f/^(k21-'1))|(k11-'k21+1))
    .(len ((f/^(k21-'1))|(k11-'k21+1))
    - 1 + 1) by FINSEQ_5:61
    .= f.k11 by A21;
    hence mid(f,k11,k21).1=f.k11 by A10,Def1;
   end;
   end;
  thus k11<=k21 implies len mid(f,k11,k21) = k21 -' k11 +1 &
    for i being Nat st 1<=i & i<=len mid(f,k11,k21)
        holds mid(f,k11,k21).i=f.(i+k11-'1)
   proof assume A22:k11<=k21;
    then A23:mid(f,k11,k21)=(f/^(k11-'1))|(k21-'k11+1) by Def1;
        k11-'1<=len f by A1,Th7;
    then A24:len (f/^(k11-'1))=len f -(k11-'1) by RFINSEQ:def 2;
A25:  k11-1>=0 by A1,XREAL_1:50;
    then A26:k11-'1=k11-1 by BINARITH:def 3;
A27:  k21-k11>=0 by A22,XREAL_1:50;
    then A28:k21-'k11=k21-k11 by BINARITH:def 3;
        k21-'k11+1=k21-k11+1 by A27,BINARITH:def 3 .=k21-(k11-1)
                  .=k21-(k11-'1) by A25,BINARITH:def 3;
    then A29:k21-'k11+1 <=len (f/^(k11-'1)) by A1,A24,XREAL_1:11;
then A30:  len ((f/^(k11-'1))|(k21-'k11+1))=(k21-'k11+1) by FINSEQ_1:80;
        for i being Nat st 1<=i & i<=len mid(f,k11,k21)
        holds mid(f,k11,k21).i=f.(i+k11-'1)
     proof let i be Nat;assume A31:1<=i & i<=len mid(f,k11,k21);
       then A32: i+(k11-1)<=k21-(k11-1)+(k11-1) by A23,A28,A30,XREAL_1:8;
       A33:i<= (k21-'k11+1) & (k21-'k11+1)<=len (f/^(k11-'1))
                           by A23,A29,A31,FINSEQ_1:80;
       A34:1<=i & i+(k11-'1)<=len f by A1,A26,A31,A32,XREAL_1:2;
           k11<=k11+i by NAT_1:29;
       then A35:i+k11>=1 by A1,XREAL_1:2;
       A36:i+(k11-'1)=i+(k11-1) by A25,BINARITH:def 3 .=i+k11-1
                    .=i+k11-'1 by A35,BINARITH:50;
           mid(f,k11,k21).i=(f/^(k11-'1))|(k21-'k11+1).i by A22,Def1
                       .=(f/^(k11-'1)).i by A33,FINSEQ_3:121
                       .=f.(i+(k11-'1)) by A34,Th23;
      hence mid(f,k11,k21).i=f.(i+k11-'1) by A36;
     end;
   hence len mid(f,k11,k21) = k21 -' k11 +1 &
    for i being Nat st 1<=i & i<=len mid(f,k11,k21)
        holds mid(f,k11,k21).i=f.(i+k11-'1) by A23,A29,FINSEQ_1:80;
   end;
   assume A37:k11>k21;
    then A38:mid(f,k11,k21)=Rev ((f/^(k21-'1))|(k11-'k21+1)) by Def1;
    then A39:len mid(f,k11,k21)=len ((f/^(k21-'1))|(k11-'k21+1)) by
FINSEQ_5:def 3;
        k21-'1<=len f by A1,Th7;
    then A40:len (f/^(k21-'1))=len f -(k21-'1) by RFINSEQ:def 2;
A41:  k21-1>=0 by A1,XREAL_1:50;
    then A42:k21-'1=k21-1 by BINARITH:def 3;
A43:  k11-k21>=0 by A37,XREAL_1:50;
    then A44:k11-'k21=k11-k21 by BINARITH:def 3;
        k11-'k21+1=k11-k21+1 by A43,BINARITH:def 3 .=k11-(k21-1)
                  .=k11-(k21-'1) by A41,BINARITH:def 3;
    then A45:k11-'k21+1 <=len (f/^(k21-'1)) by A1,A40,XREAL_1:11;
    then A46:len ((f/^(k21-'1))|(k11-'k21+1))=(k11-'k21+1) by FINSEQ_1:80;
   thus A47:len mid(f,k11,k21) = k11 -' k21 +1 by A39,A45,FINSEQ_1:80;
   thus for i being Nat st 1<=i & i<=len mid(f,k11,k21)
      holds mid(f,k11,k21).i=f.(k11-'i+1)
     proof let i be Nat;assume A48:1<=i & i<=len mid(f,k11,k21);
       then A49:i<=k11-'k21+1 by A39,A45,FINSEQ_1:80;
       A50: i+(k21-1)<=k11-(k21-1)+(k21-1) by A44,A47,A48,XREAL_1:8;
           i<=i+(k21-'1) by NAT_1:29;
       then A51:i<=k11 by A42,A50,XREAL_1:2;
       A52:k11-'k21+1-'i+1+(k21-'1)
        =k11-'k21+1-i+1+(k21-'1) by A47,A48,BINARITH:50
       .= k11-(k21-1)-i+1+(k21-1) by A41,A44,BINARITH:def 3
       .=k11-i+1
       .=k11-'i+1 by A51,BINARITH:50;
           k11-'k21+1<= k11-'k21+1+(i-'1) by NAT_1:29;
       then k11-'k21+1<= k11-'k21+1+(i-1) by A48,BINARITH:50;
       then k11-'k21+1-(i-1)<= k11-'k21+1+(i-1)-(i-1) by XREAL_1:11;
       then k11-'k21+1-i+1<= k11-'k21+1;
       then A53:  (k11-'k21+1-'i+1)<=(k11-'k21+1) by A49,BINARITH:50;
       A54:1+k11<=i+k11 by A48,XREAL_1:8;
           i+k11<=i+len f by A1,XREAL_1:8;
       then 1+k11<=i+len f by A54,XREAL_1:2;
       then 1+k11-i<=i+len f -i by XREAL_1:11;
       then k11-i+1<=len f;
       then A55:1<=k11-'k21+1-'i+1 & k11-'k21+1-'i+1+(k21-'1)<= len f
        by A51,A52,NAT_1:29,BINARITH:50;
           mid(f,k11,k21).i=((f/^(k21-'1))|(k11-'k21+1))
                      .(len ((f/^(k21-'1))|(k11-'k21+1)) -i+1)
                          by A38,A39,A48,Th24
          .=((f/^(k21-'1))|(k11-'k21+1))
                      .(len ((f/^(k21-'1))|(k11-'k21+1)) -'i+1)
                          by A39,A48,BINARITH:50
          .=(f/^(k21-'1)).(k11-'k21+1-'i+1) by A46,A53,FINSEQ_3:121
          .=f.(k11-'i+1) by A52,A55,Th23;
      hence mid(f,k11,k21).i=f.(k11-'i+1);
    end;
 end;

theorem Th28: for f being FinSequence of D,k1,k2 being Nat holds
  rng mid(f,k1,k2) c= rng f
proof let f be FinSequence of D,k1,k2 be Nat;
 per cases;
  suppose k1<=k2;
    then mid(f,k1,k2) = (f/^(k1-'1))|(k2-'k1+1) by Def1;
    then A1:rng mid(f,k1,k2) c= rng (f/^(k1-'1)) by FINSEQ_5:21;
        rng (f/^(k1-'1)) c= rng f by FINSEQ_5:36;
   hence rng mid(f,k1,k2) c= rng f by A1,XBOOLE_1:1;
  end;
  suppose k1>k2;
    then mid(f,k1,k2) = Rev ((f/^(k2-'1))|(k1-'k2+1)) by Def1;
    then rng mid(f,k1,k2) = rng ((f/^(k2-'1))|(k1-'k2+1)) by FINSEQ_5:60;
    then A2:rng mid(f,k1,k2) c= rng (f/^(k2-'1)) by FINSEQ_5:21;
        rng (f/^(k2-'1)) c= rng f by FINSEQ_5:36;
   hence rng mid(f,k1,k2) c= rng f by A2,XBOOLE_1:1;
end;
end;

theorem   for f being FinSequence of D st 1<=len f holds
   mid(f,1,len f)=f
proof let f be FinSequence of D;
 assume A1:1<=len f;
  then mid(f,1,len f)=(f/^(1-'1))|(len f-'1+1) by Def1
  .=(f/^0)|(len f-'1+1) by BINARITH:51
  .=f|(len f-'1+1) by FINSEQ_5:31
  .=f|len f by A1,BINARITH:53
  .=f by FINSEQ_1:79;
 hence mid(f,1,len f)=f;
end;

theorem
  for f being FinSequence of D st 1<=len f holds
   mid(f,len f,1)=Rev f
proof let f be FinSequence of D;
 assume A1:1<=len f;
  A2:1-'1=0 by BINARITH:51;
 per cases;
  suppose len f<>1; then 1<len f by A1,REAL_1:def 5;
    then mid(f,len f,1)=Rev ((f/^(1-'1))|(len f-'1+1)) by Def1
               .=Rev ((f/^0)|len f) by A1,A2,BINARITH:53
               .=Rev (f|len f) by FINSEQ_5:31
               .=Rev f by FINSEQ_1:79;
  hence mid(f,len f,1)=Rev f;
  end;
  suppose A3:len f=1;
   then A4:mid(f,len f,1)=(f/^(1-'1))|(1-'1+1) by Def1
                 .=f|1 by A2,FINSEQ_5:31;
   A5:f|1=f by A3,FINSEQ_1:79;
       len (f|1)=1 by A1,FINSEQ_1:80;
  hence mid(f,len f,1)=Rev f by A4,A5,Th18;
end;
end;

theorem Th31:for f being FinSequence of D, k1,k2,i being Nat
 st 1<=k1 & k1<=k2 & k2<=len f & 1<=i &
 (i<=k2-'k1+1 or i<=k2-k1+1 or i<=k2+1-k1) holds
 mid(f,k1,k2).i=f.(i+k1-'1) & mid(f,k1,k2).i=f.(i-'1+k1) &
 mid(f,k1,k2).i=f.(i+k1-1) & mid(f,k1,k2).i=f.(i-1+k1)
proof let f be FinSequence of D, k1,k2,i be Nat;
 assume A1:1<=k1 & k1<=k2 & k2<=len f & 1<=i &
 (i<=k2-'k1+1 or i<=k2-k1+1 or i<=k2+1-k1);
 then A2:1<=k2 by XREAL_1:2;
 A3:k1<=len f by A1,XREAL_1:2;
A4: i-1>=1-1 by A1,XREAL_1:11;
     i+k1>=1+k1 by A1,XREAL_1:8;
 then A5: i+k1-1>=1+k1-1 by XREAL_1:11;
 A6:len mid(f,k1,k2)=k2-'k1+1 by A1,A2,A3,Th27;
 A7:i<=k2-k1+1 implies i<=k2-'k1+1 by A1,BINARITH:50;
     i-'1+k1=i-1+k1 by A4,BINARITH:def 3 .=i+k1-'1 by A5,BINARITH:def 3;
 hence A8:mid(f,k1,k2).i=f.(i+k1-'1) & mid(f,k1,k2).i=f.(i-'1+k1)
   by A1,A2,A3,A6,A7,Th27;
 hence mid(f,k1,k2).i=f.(i+k1-1) by A5,BINARITH:def 3;
 thus mid(f,k1,k2).i=f.(i-1+k1) by A1,A8,BINARITH:50;
end;

theorem Th32:for f being FinSequence of D,k,i being Nat
 st 1<=i & i<=k & k<=len f holds
 mid(f,1,k).i=f.i
proof let f be FinSequence of D,k,i be Nat;
 assume A1: 1<=i & i<=k & k<=len f;
 then A2:1<=k & k<=len f & 1<=i & i<=k by XREAL_1:2;
     i<=k-1+1 by A1;
 then mid(f,1,k).i=f.(i+1-'1) by A2,Th31;
 hence thesis by BINARITH:39;
end;

theorem
 for f being FinSequence of D, k1,k2 being Nat
 st 1<=k1 & k1<=k2 & k2<=len f holds len mid(f,k1,k2)<=len f
proof let f be FinSequence of D, k1,k2 be Nat;
  assume A1:1<=k1 & k1<=k2 & k2<=len f;
  then A2:1<=k2 by XREAL_1:2;
      k1<=len f by A1,XREAL_1:2;
  then A3:len mid(f,k1,k2)=k2-'k1+1 by A1,A2,Th27;
      k2-k1>=0 by A1,XREAL_1:50;
  then A4:k2-'k1+1=k2-k1+1 by BINARITH:def 3 .=k2-(k1-1);
      k1-1>=0 by A1,XREAL_1:50;
  then A5:k1-1=k1-'1 by BINARITH:def 3;
      k2<=k2+(k1-'1) by NAT_1:29;
  then k2-(k1-'1)<=k2+(k1-'1)-(k1-'1) by XREAL_1:11;
 hence len mid(f,k1,k2)<=len f by A1,A3,A4,A5,XREAL_1:2;
end;

theorem
 for f being FinSequence of TOP-REAL n st 2<=len f
 holds f.1 in L~f & f/.1 in L~f & f.len f in L~f & f/.len f in L~f
proof let f be FinSequence of TOP-REAL n;
 assume A1:2<=len f;
  then A2: 1+1<=len f;
      f/.1 in LSeg(f/.1,f/.(1+1)) by TOPREAL1:6;
  then A3:f/.1 in LSeg(f,1) by A1,TOPREAL1:def 5;
      LSeg(f,1) in {LSeg(f,i):1<=i & i+1<=len f} by A2;
  then f/.1 in union{LSeg(f,i):1<=i & i+1<=len f} by A3,TARSKI:def 4;
  then A4:f/.1 in L~f by TOPREAL1:def 6;
  A5:1<=len f by A2,Th9;
  A6:1<=len f-'1 by A2,Th12;
  A7:len f-'1+1=len f by A5,BINARITH:53;
  then f/.len f in LSeg(f/.(len f-'1),f/.(len f-'1+1)) by TOPREAL1:6;
  then A8:f/.len f in LSeg(f,len f-'1) by A6,A7,TOPREAL1:def 5;
      LSeg(f,len f-'1) in {LSeg(f,i):1<=i & i+1<=len f} by A6,A7;
  then f/.len f in union{LSeg(f,i):1<=i & i+1<=len f} by A8,TARSKI:def 4;
  then f/.len f in L~f by TOPREAL1:def 6;
 hence f.1 in L~f & f/.1 in L~f & f.len f in L~f & f/.len f in L~f
   by A4,A5,FINSEQ_4:24;
end;

theorem Th35:for p1,p2,q1,q2 being Point of TOP-REAL 2 st
  (p1`1 = p2`1 or p1`2 = p2`2)& q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2)
  holds q1`1 = q2`1 or q1`2 = q2`2
proof let p1,p2,q1,q2 be Point of TOP-REAL 2;
 assume A1:(p1`1 = p2`1 or p1`2 = p2`2)& q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2);
  then q1 in { (1-r)*p1 + r*p2 : 0 <= r & r <= 1 } by TOPREAL1:def 3;
  then consider r1 such that A2:q1= (1-r1)*p1 + r1*p2 & 0 <= r1 & r1 <= 1;
      q2 in { (1-r)*p1 + r*p2 : 0 <= r & r <= 1 } by A1,TOPREAL1:def 3;
  then consider r2 such that A3:q2= (1-r2)*p1 + r2*p2 & 0 <= r2 & r2 <= 1;
      q1`1=((1-r1)*p1)`1+(r1*p2)`1 by A2,TOPREAL3:7;
  then q1`1=(1-r1)*(p1`1)+(r1*p2)`1 by TOPREAL3:9;
  then A4:q1`1=(1-r1)*(p1`1)+r1*(p2`1) by TOPREAL3:9;
      q1`2=((1-r1)*p1)`2+(r1*p2)`2 by A2,TOPREAL3:7;
  then q1`2=(1-r1)*(p1`2)+(r1*p2)`2 by TOPREAL3:9;
  then A5:q1`2=(1-r1)*(p1`2)+r1*(p2`2) by TOPREAL3:9;
      q2`1=((1-r2)*p1)`1+(r2*p2)`1 by A3,TOPREAL3:7;
  then q2`1=(1-r2)*(p1`1)+(r2*p2)`1 by TOPREAL3:9;
  then A6:q2`1=(1-r2)*(p1`1)+r2*(p2`1) by TOPREAL3:9;
      q2`2=((1-r2)*p1)`2+(r2*p2)`2 by A3,TOPREAL3:7;
  then q2`2=(1-r2)*(p1`2)+(r2*p2)`2 by TOPREAL3:9;
  then A7:q2`2=(1-r2)*(p1`2)+r2*(p2`2) by TOPREAL3:9;
 per cases by A1;
 suppose p1`1 = p2`1;
   hence thesis by A4,A6;
 end;
 suppose p1`2 = p2`2;
   hence thesis by A5,A7;
end;
end;

theorem Th36:for p1,p2,q1,q2 being Point of TOP-REAL 2 st
  (p1`1 = p2`1 or p1`2 = p2`2)& LSeg(q1,q2) c= LSeg(p1,p2)
  holds q1`1 = q2`1 or q1`2 = q2`2
proof let p1,p2,q1,q2 be Point of TOP-REAL 2;
      q1 in LSeg(q1,q2) & q2 in LSeg(q1,q2) by TOPREAL1:6;
 hence thesis by Th35;
end;

theorem Th37: for f being FinSequence of TOP-REAL 2,n being Nat
 st 2<=n & f is_S-Seq holds f|n is_S-Seq
proof let f be FinSequence of TOP-REAL 2,n be Nat;
 assume A1:2<=n & f is_S-Seq;
  then A2:f is one-to-one & len f >= 2
   & f is unfolded s.n.c. special by TOPREAL1:def 10;
   reconsider f'=f as s.n.c. special unfolded one-to-one
     FinSequence of TOP-REAL 2 by A1,TOPREAL1:def 10;
      now per cases;
  case n<=len f;
   hence len (f|n) >= 2 by A1,FINSEQ_1:80;
  end;
  case n>len f;
   hence len (f|n) >= 2 by A2,FINSEQ_1:79;
  end;
  end;
  then f'|n is one-to-one & len (f|n) >= 2
   & f'|n is unfolded s.n.c. special;
 hence f|n is_S-Seq by TOPREAL1:def 10;
end;

theorem Th38: for f being FinSequence of TOP-REAL 2,n being Nat
 st n<=len f & 2<=len f-'n & f is_S-Seq holds f/^n is_S-Seq
proof let f be FinSequence of TOP-REAL 2,n be Nat;
 assume A1:n<=len f & 2<=len f-'n & f is_S-Seq;
 then reconsider f' = f as
   one-to-one special s.n.c. unfolded FinSequence of TOP-REAL 2
     by TOPREAL1:def 10;
         len (f/^n)=len f-n by A1,RFINSEQ:def 2;
  then f'/^n is one-to-one & len (f'/^n) >= 2
   & f'/^n is unfolded s.n.c. special by A1,BINARITH:50;
 hence f/^n is_S-Seq by TOPREAL1:def 10;
end;

theorem   for f being FinSequence of TOP-REAL 2,k1,k2 being Nat
  st f is_S-Seq & 1<=k1 & k1<=len f & 1<=k2 & k2<=len f & k1<>k2 holds
  mid(f,k1,k2) is_S-Seq
proof let f be FinSequence of TOP-REAL 2,k1,k2 be Nat;
 assume A1: f is_S-Seq & 1<=k1 & k1<=len f & 1<=k2 & k2<=len f & k1<>k2;
 per cases;
  suppose A2:k1<=k2;
    then A3:mid(f,k1,k2)=(f/^(k1-'1))|(k2-'k1+1) by Def1;
    k1<k2 by A1,A2,REAL_1:def 5;
    then A4:k1+1<=k2 by NAT_1:38;
    then A5:k1+1<=len f by A1,XREAL_1:2;
    A6:k1-'1<=len f by A1,Th13;
    then A7:len f-'(k1-'1)= len f-(k1-'1) by BINARITH:50
          .=len f-(k1-1) by A1,BINARITH:50
    .=len f-k1+1;
    k1+1-k1<=len f-k1 by A5,XREAL_1:11;
    then 1+1<=len f-k1+1 by XREAL_1:8;
    then A8:f/^(k1-'1) is_S-Seq by A1,A6,A7,Th38;
    k1+1-k1<=k2-k1 by A4,XREAL_1:11;
    then 1<=k2-'k1 by Th1;
    then 1+1<=k2-'k1+1 by XREAL_1:8;
   hence mid(f,k1,k2) is_S-Seq by A3,A8,Th37;
  end;
  suppose A9:k1>k2;
    then A10:mid(f,k1,k2)= Rev ((f/^(k2-'1))|(k1-'k2+1)) by Def1;
    A11:k2+1<=k1 by A9,NAT_1:38;
    then A12:k2+1<=len f by A1,XREAL_1:2;
    A13:k2-'1<=len f by A1,Th13;
    then A14:len f-'(k2-'1)= len f-(k2-'1) by BINARITH:50
       .=len f-(k2-1) by A1,BINARITH:50
    .=len f-k2+1;
    k2+1-k2<=len f-k2 by A12,XREAL_1:11;
    then 1+1<=len f-k2+1 by XREAL_1:8;
    then A15:f/^(k2-'1) is_S-Seq by A1,A13,A14,Th38;
    k2+1-k2<=k1-k2 by A11,XREAL_1:11;
    then 1<=k1-'k2 by Th1;
    then 1+1<=k1-'k2+1 by XREAL_1:8;
    then (f/^(k2-'1))|(k1-'k2+1) is S-Sequence_in_R2 by A15,Th37;
   hence mid(f,k1,k2) is_S-Seq by A10,SPPOL_2:47;
end;
end;

begin
::---------------------------------------------------------:
:: A Concept of Index for Finite Sequences in TOP-REAL 2   :
::---------------------------------------------------------:

definition let f be FinSequence of TOP-REAL 2,p be Point of TOP-REAL 2;
  assume A1: p in L~f;
  func Index(p,f) -> Nat means
  :Def2: ex S being non empty Subset of NAT st
    it = min S & S = { i: p in LSeg(f,i) };
 existence
  proof set S = { i: p in LSeg(f,i) };
    consider i2 being Nat such that 1<=i2 & i2+1<=len f and
A2:   p in LSeg(f,i2) by A1,SPPOL_2:13;
A3: i2 in S by A2;
        S c= NAT
     proof let x be set;
      assume x in S;
       then ex i st x = i & p in LSeg(f,i);
      hence thesis;
     end;
    then reconsider S as non empty Subset of NAT by A3;
   take min S, S;
   thus thesis;
  end;
 uniqueness;
 end;

theorem Th40:
 for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2, i being Nat st p in LSeg(f,i)
   holds Index(p,f) <= i
proof let f being FinSequence of TOP-REAL 2;
 let p being Point of TOP-REAL 2, i0 be Nat;
A1: LSeg(f,i0) c= L~f by TOPREAL3:26;
 assume
A2: p in LSeg(f,i0);
  then consider S being non empty Subset of NAT such that
A3: Index(p,f) = min S and
A4: S = { i: p in LSeg(f,i) } by A1,Def2;
      i0 in S by A2,A4;
 hence Index(p,f) <= i0 by A3,CQC_SIM1:def 8;
end;

theorem Th41:
 for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st p in L~f
   holds 1<=Index(p,f) & Index(p,f) < len f
proof let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume p in L~f;
   then consider S being non empty Subset of NAT such that
A1: Index(p,f) = min S and
A2: S = { i: p in LSeg(f,i) } by Def2;
    Index(p,f) in S by A1,CQC_SIM1:def 8;
  then A3:  ex i st i = Index(p,f) & p in LSeg(f,i) by A2;
 hence 1 <= Index(p,f) by TOPREAL1:def 5;
 Index(p,f) + 1 <= len f by A3,TOPREAL1:def 5;
 hence thesis by NAT_1:38;
end;

theorem Th42:
 for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st p in L~f
 holds p in LSeg(f,Index(p,f))
proof let f be FinSequence of TOP-REAL 2;
 let p be Point of TOP-REAL 2;
 assume p in L~f;
  then consider S being non empty Subset of NAT such that
A1: Index(p,f) = min S and
A2: S = { i: p in LSeg(f,i) } by Def2;
  Index(p,f) in S by A1,CQC_SIM1:def 8;
  then ex i st i = Index(p,f) & p in LSeg(f,i) by A2;
 hence p in LSeg(f,Index(p,f));
end;

theorem Th43:
 for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
   st p in LSeg(f,1)
 holds Index(p,f) = 1
proof let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
 assume
A1: p in LSeg(f,1);
  then A2: Index(p,f) <= 1 by Th40;
  LSeg(f,1) c= L~f by TOPREAL3:26;
  then Index(p,f) >= 1 by A1,Th41;
  hence Index(p,f) = 1 by A2,XREAL_1:1;
end;

theorem Th44:
 for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
   st len f >= 2
 holds Index(f/.1,f) = 1
proof let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
 assume len f >= 2; then len f >= 1+1;
  then f/.1 in LSeg(f,1) by TOPREAL1:27;
 hence thesis by Th43;
end;

theorem Th45:
 for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2,i1 st f is_S-Seq
  & 1<i1 & i1<=len f & p=f.i1 holds Index(p,f) + 1 = i1
proof let f be FinSequence of TOP-REAL 2,
  p be Point of TOP-REAL 2,i1;
 assume
A1: f is_S-Seq;
 assume
A2: 1<i1 & i1<=len f;
 then A3: i1 in dom f by FINSEQ_3:27;
 0 < i1 by A2;
 then consider j being Nat such that
A4: i1 = j+1 by NAT_1:22;
A5: 1 + 0 <= j by A2,A4,NAT_1:38;
 assume p=f.i1;
  then A6: p = f/.i1 by A3,FINSEQ_4:def 4;
  then A7: p in LSeg(f,j) by A2,A4,A5,TOPREAL1:27;
  then A8: Index(p,f) <= j by Th40;
 assume Index(p,f) + 1 <> i1;
  then Index(p,f) < j by A4,A8,XREAL_1:1;
  then A9: Index(p,f) + 1 <= j by NAT_1:38;
A10:  LSeg(f,j) c= L~f by TOPREAL3:26;
  then A11: p in LSeg(f,Index(p,f)) by A7,Th42;
  per cases by A9,XREAL_1:1;
  suppose
A12: Index(p,f) + 1 = j;
A13: f is unfolded by A1,TOPREAL1:def 10;
       1 <= Index(p,f) & Index(p,f) + (1+1) <= len f by A2,A4,A7,A10,A12,Th41;
   then LSeg(f,Index(p,f)) /\ LSeg(f,j) = {f/.j} by A12,A13,TOPREAL1:def 8;
   then p in {f/.j} by A7,A11,XBOOLE_0:def 3;
   then A14:  p = f/.j by TARSKI:def 1;
A15: j < i1 by A4,NAT_1:38;
   j < len f by A2,A4,NAT_1:38;
   then A16: j in dom f by A5,FINSEQ_3:27;
   f is one-to-one by A1,TOPREAL1:def 10;
  hence contradiction by A3,A6,A14,A15,A16,PARTFUN2:17;
  end;
  suppose
A17: Index(p,f) + 1 < j;
      p in LSeg(f,Index(p,f)) /\ LSeg(f,j) by A7,A11,XBOOLE_0:def 3;
then A18: LSeg(f,Index(p,f)) meets LSeg(f,j) by XBOOLE_0:4;
      f is s.n.c. by A1,TOPREAL1:def 10;
 hence contradiction by A17,A18,TOPREAL1:def 9;
end;
end;

theorem Th46:
 for f be FinSequence of TOP-REAL 2
 for p be Point of TOP-REAL 2
 for i1 be Nat st f is s.n.c. & p in LSeg(f,i1) holds
  i1=Index(p,f) or i1=Index(p,f)+1
 proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  let i1 be Nat;
  assume that
   A1: f is s.n.c. and
   A2: p in LSeg(f,i1);
  A3: p in L~f by A2,SPPOL_2:17;
  A4: Index(p,f) <= i1 by A2,Th40;
  assume A5: not thesis;
  then Index(p,f) < i1 by A4,XREAL_1:1;
  then Index(p,f)+1 <= i1 by NAT_1:38;
  then A6: Index(p,f)+1 < i1 by A5,XREAL_1:1;
  p in LSeg(f,Index(p,f)) by A3,Th42;
  then p in LSeg(f,Index(p,f)) /\ LSeg(f,i1) by A2,XBOOLE_0:def 3;
  then LSeg(f,Index(p,f)) meets LSeg(f,i1) by XBOOLE_0:4;
  hence contradiction by A6,A1,TOPREAL1:def 9;
 end;

theorem Th47:
 for f be FinSequence of TOP-REAL 2
 for p be Point of TOP-REAL 2
 for i1 be Nat st f is unfolded s.n.c. &
  i1+1<=len f & p in LSeg(f,i1) & p <> f.i1 holds
   i1=Index(p,f)
 proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  let i1 be Nat;
  assume that
   A1: f is unfolded s.n.c. and
   A2: i1+1<=len f and
   A3: p in LSeg(f,i1);
  assume A4: p <> f.i1;
  A5: p in L~f by A3,SPPOL_2:17;
  A6: Index(p,f) <= i1 by A3,Th40;
  p in LSeg(f,Index(p,f)) by A5,Th42;
  then A7: p in LSeg(f,Index(p,f)) /\ LSeg(f,i1) by A3,XBOOLE_0:def 3;
  A8: 1 <= Index(p,f) by A5,Th41;
  A9: 1 <= Index(p,f)+1 by NAT_1:29;
  i1 < len f by A2,NAT_1:38;
  then Index(p,f) < len f by A6,XREAL_1:2;
  then Index(p,f)+1 <= len f by NAT_1:38;
  then A10: Index(p,f) + 1 in dom f by A9,FINSEQ_3:27;
  now assume A11: i1 = Index(p,f)+1;
   then Index(p,f) + (1+1) <= len f by A2;
   then p in {f/.(Index(p,f)+1)} by A1,A7,A8,A11,TOPREAL1:def 8;
   then p = f/.(Index(p,f)+1) by TARSKI:def 1;
   hence contradiction by A4,A10,A11,FINSEQ_4:def 4;
  end;
  hence thesis by A1,A3,Th46;
 end;

definition let g be FinSequence of TOP-REAL 2,
               p1,p2 be Point of TOP-REAL 2;
  pred g is_S-Seq_joining p1,p2 means
  :Def3: g is_S-Seq & g.1 = p1 & g.len g = p2;
end;

theorem Th48: for g being FinSequence of TOP-REAL 2,
  p1,p2 being Point of TOP-REAL 2 st
   g is_S-Seq_joining p1,p2
  holds Rev g is_S-Seq_joining p2,p1
proof let g be FinSequence of TOP-REAL 2,
  p1,p2 be Point of TOP-REAL 2;
 assume that
A1: g is_S-Seq and
A2: g.1=p1 and
A3: g.len g=p2;
 thus Rev g is_S-Seq by A1,SPPOL_2:47;
 thus (Rev g).1 = p2 by A3,FINSEQ_5:65;
      dom g = dom Rev g by FINSEQ_5:60;
 hence (Rev g).len Rev g = (Rev g).len g by FINSEQ_3:31
          .= p1 by A2,FINSEQ_5:65;
end;

theorem Th49: for f,g being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2,j st p in L~f
 & g=<*p*>^mid(f,Index(p,f)+1,len f) & 1<=j & j+1<=len g
 holds LSeg(g,j) c= LSeg(f,Index(p,f)+j-'1)
proof let f,g be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2,j;
 assume that
A1: p in L~f and
A2: g=<*p*>^mid(f,Index(p,f)+1,len f) and
A3: 1<=j and
A4: j+1<=len g;
  consider i such that A5:1<=i & i+1<=len f & p in LSeg(f,i) by A1,SPPOL_2:13;
  1<=i+1 by NAT_1:37;
  then A6:1<=len f by A5,XREAL_1:2;
  A7:len g=(len <*p*>)+ len(mid(f,Index(p,f)+1,len f))
         by A2,FINSEQ_1:35
      .=1+ len(mid(f,Index(p,f)+1,len f)) by FINSEQ_1:56;
  then A8: 1<=len g by NAT_1:29;
A9:  Index(p,f)<len f by A1,Th41;
     then Index(p,f)+1<=len f by NAT_1:38;
     then Index(p,f)+1-Index(p,f)<=len f - Index(p,f) by XREAL_1:11;
     then A10: 1-1<=len f - Index(p,f)-1 by XREAL_1:11;
     A11:Index(p,f)+1<=len f by A9,NAT_1:38;
A12:   0+1<=Index(p,f)+1 by NAT_1:38;
     then A13:len(mid(f,Index(p,f)+1,len f))=len f -' (Index(p,f)+1)+1
                            by A6,A11,Th27;
     A14:1<=Index(p,f)+1 & Index(p,f)+1<=len f & 1<=len f & len f<=len f
      by A11,A12,XREAL_1:2;
     A15:len g=1+(len f -(Index(p,f)+1)+1) by A7,A10,A13,BINARITH:def 3
          .=1+(len f - Index(p,f));
A16: len f -'(Index(p,f)+1)=len f -(Index(p,f)+1) by A10,BINARITH:def 3
                           .=len f - Index(p,f) -1;
     len g=len <*p*> + len mid(f,Index(p,f)+1,len f) by A2,FINSEQ_1:35;
     then A17:len g=1+len mid(f,Index(p,f)+1,len f) by FINSEQ_1:56;
     A18:j<=len g by A4,NAT_1:38;
     A19:1<=j+1 by A3,NAT_1:38;
     A20:j-'1=j-1 by A3,BINARITH:50;
        A21:j+1-'1=j+1-1 by A19,BINARITH:50;
        A22:j=1+(j-1)
        .=len <*p*> +(j-'1) by A20,FINSEQ_1:56;
        A23:j+1=len <*p*> +(j+1-1) by FINSEQ_1:56
        .=len <*p*> +(j+1-'1) by A19,BINARITH:50;
         A24:1<=Index(p,f)+j by A3,NAT_1:37;
then A25:     1-1<=Index(p,f)+j-1 by XREAL_1:11;
             j+1-1<=1+len mid(f,Index(p,f)+1,len f)-1
              by A4,A17,XREAL_1:11;
       then j+Index(p,f)<= len f -Index(p,f)+Index(p,f) by A13,A16,XREAL_1:8;
         then Index(p,f)+(j-1)+1 <= len f;
         then A26:Index(p,f)+j-'1+1 <= len f by A25,BINARITH:def 3;
A27:       j+1-1<=1+len mid(f,Index(p,f)+1,len f)-1
               by A4,A17,XREAL_1:11;
        A28:j+1-'1 in dom mid(f,Index(p,f)+1,len f)
          by A3,A21,A27,FINSEQ_3:27;
        j-'1<=j by BINARITH:52;
        then A29:j-'1<=len mid(f,Index(p,f)+1,len f) by A27,XREAL_1:2;
        A30:g.(j+1) =mid(f,Index(p,f)+1,len f).(j+1-'1)
                   by A2,A23,A28,FINSEQ_1:def 7
             .=f.(j+1-'1+(Index(p,f)+1)-'1) by A3,A14,A21,A27,Th27
             .=f.(j+1-'1+1+Index(p,f)-'1)
             .=f.(j+1+Index(p,f)-'1) by A19,BINARITH:53
             .=f.(Index(p,f)+j+1-'1)
             .=f.(Index(p,f)+j) by BINARITH:39
             .=f.(Index(p,f)+j-'1+1) by A24,BINARITH:53;
            j<=len f - Index(p,f) by A4,A15,XREAL_1:8;
        then A31: j+Index(p,f)<=len f - Index(p,f)+Index(p,f) by XREAL_1:8;
        then A32:Index(p,f)+j-'1+1<=len f by A24,BINARITH:53;
        A33:Index(p,f)+j-'1<=len f by A31,Th13;
        A34:1<=Index(p,f)+j-'1+1 by NAT_1:29;
            g/.(j+1)=g.(j+1) by A4,A19,FINSEQ_4:24;
        then A35:f/.(Index(p,f)+j-'1+1)=g/.(j+1) by A30,A32,A34,FINSEQ_4:24;
            1 <= Index(p,f) by A1,Th41;
        then 1+1 <= Index(p,f)+j by A3,XREAL_1:9;
        then 1<=Index(p,f)+j-1 by XREAL_1:21;
        then A36:1<=Index(p,f)+j-'1 by Th1;
         then A37:LSeg(f,Index(p,f)+j-'1)
             =LSeg(f/.(Index(p,f)+j-'1),f/.(Index(p,f)+j-'1+1))
                      by A26,TOPREAL1:def 5;
            now per cases by A3,REAL_1:def 5;
        case A38:1<j;
           then A39:j-'1=j-1 by BINARITH:50;
           then A40:1<=j-'1 by A38,SPPOL_1:6;
               j-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A17,A18,XREAL_1:11;
           then j-'1 in dom mid(f,Index(p,f)+1,len f) by A39,A40,FINSEQ_3:27;
           then A41:g.j =mid(f,Index(p,f)+1,len f).(j-'1)
                                       by A2,A22,FINSEQ_1:def 7
                 .=f.(j-'1+(Index(p,f)+1)-'1) by A14,A29,A40,Th27
                 .=f.(j-'1+1+Index(p,f)-'1)
                 .=f.(Index(p,f)+j-'1) by A3,BINARITH:53;
              g/.j=g.j by A3,A18,FINSEQ_4:24;
          then LSeg(f,Index(p,f)+j-'1)
              =LSeg(g/.j,g/.(j+1)) by A33,A35,A36,A37,A41,FINSEQ_4:24
             .=LSeg(g,j) by A3,A4,TOPREAL1:def 5;
         hence thesis;
        end;
        case A42:1=j;
           then 1<=j & j<=len <*p*> by FINSEQ_1:56;
           then j in dom <*p*> by FINSEQ_3:27;
           then A43:g.j =<*p*>.j by A2,FINSEQ_1:def 7
                 .=p by A42,FINSEQ_1:57;
          A44:g/.j=g.j by A8,A42,FINSEQ_4:24;
          A45:f/.(Index(p,f)+j-'1+1)
             in LSeg(f/.(Index(p,f)+j-'1),f/.(Index(p,f)+j-'1+1))
                           by TOPREAL1:6;
A46:        Index(p,f)+j-'1 = Index(p,f) by A42,BINARITH:39;
              p in LSeg(f,Index(p,f)) by A1,Th42;
          then LSeg(p,g/.(j+1))
                   c= LSeg(f,Index(p,f)+j-'1) by A35,A37,A45,A46,TOPREAL1:12;
        hence thesis by A3,A4,A43,A44,TOPREAL1:def 5;
        end;
        end;
 hence LSeg(g,j) c= LSeg(f,Index(p,f)+j-'1);
end;

theorem
     for f,g being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2
 st f is_S-Seq & p in L~f & p<>f.(Index(p,f)+1) &
    g=<*p*>^mid(f,Index(p,f)+1,len f)
  holds g is_S-Seq_joining p,f/.len f
 proof let f,g be FinSequence of TOP-REAL 2,p be Point of TOP-REAL 2;
  assume that
A1: f is_S-Seq and
A2: p in L~f and
A3: p<>f.(Index(p,f)+1) and
A4: g=<*p*>^mid(f,Index(p,f)+1,len f);
  consider i such that A5:1<=i & i+1<=len f & p in LSeg(f,i)
               by A2,SPPOL_2:13;
      1<=1+i by NAT_1:37;
  then A6:1<=len f by A5,XREAL_1:2;
  A7:len g=(len <*p*>)+ len(mid(f,Index(p,f)+1,len f))
          by A4,FINSEQ_1:35
      .=1+ len(mid(f,Index(p,f)+1,len f)) by FINSEQ_1:56;
  A8: f is unfolded by A1,TOPREAL1:def 10;
A9:  Index(p,f)<len f by A2,Th41;
     then Index(p,f)+1<=len f by NAT_1:38;
     then A10: Index(p,f)+1-Index(p,f)<=len f - Index(p,f) by XREAL_1:11;
     then A11: 1-1<=len f - Index(p,f)-1 by XREAL_1:11;
     A12:Index(p,f)<len f by A2,Th41;
     A13:Index(p,f)+1<=len f by A9,NAT_1:38;
     A14: len f - Index(p,f)>=0 by A12,XREAL_1:52;
A15:   0+1<=Index(p,f)+1 by NAT_1:29;
     then A16:len(mid(f,Index(p,f)+1,len f))=len f -' (Index(p,f)+1)+1
                            by A6,A13,Th27;
     then A17: len g=1+(len f -(Index(p,f)+1)+1) by A7,A11,BINARITH:def 3
          .=1+(len f - Index(p,f));
     then 0<len g -1 by A10;
     then A18:len g -'1=len g -1 by BINARITH:def 3;
        A19: len f -'(Index(p,f)+1)=len f -(Index(p,f)+1) by A11,BINARITH:def 3
                           .=len f - Index(p,f) -1;
     then A20:len f -'(Index(p,f)+1)+1=len f -Index(p,f);
     A21:len g -'1 in dom (mid(f,Index(p,f)+1,len f))
        by A10,A16,A17,A18,A19,FINSEQ_3:27;
     len g -1>=0 by A7;
     then 1+(len g -'1)=1+(len g -1) by BINARITH:def 3
       .=len g;
     then A22:g.len g =g.(len <*p*> + (len g -'1)) by FINSEQ_1:56
           .=mid(f,Index(p,f)+1,len f).(len g -'1)
                        by A4,A21,FINSEQ_1:def 7;
     A23:len f -Index(p,f)=len f -' Index(p,f) by A14,BINARITH:def 3;
     A24:len g -'1= len f -' Index(p,f) by A17,A18,BINARITH:def 3;
     A25:mid(f,Index(p,f)+1,len f).(len f -' Index(p,f))
     =f.(len f -' Index(p,f)+(Index(p,f)+1)-'1)
       by A6,A10,A13,A15,A16,A19,A23,Th27;
         len f -' Index(p,f)+(Index(p,f)+1)
     =len f - Index(p,f)+(Index(p,f)+1) by A14,BINARITH:def 3
     .= len f +1;
     then A26: g.len g=f.len f by A22,A24,A25,BINARITH:39;
         len g=len <*p*> + len mid(f,Index(p,f)+1,len f)
                                 by A4,FINSEQ_1:35;
     then A27:len g=1+len mid(f,Index(p,f)+1,len f) by FINSEQ_1:56;
     A28:f is one-to-one by A1,TOPREAL1:def 10;
     A29: for x1,x2 being set st x1 in dom g & x2 in dom g & g.x1=g.x2
      holds x1=x2
     proof let x1,x2 be set;assume A30:x1 in dom g & x2 in dom g & g.x1=g.x2;
       then reconsider n1=x1,n2=x2 as Nat;
       A31:1<=n1 & n1<=len g & 1<=n2 & n2<=len g by A30,FINSEQ_3:27;
           now per cases by A31,REAL_1:def 5;
       case n1=1 & n2=1;
        hence x1=x2;
        end;
        case that
A32:     n1=1 and
A33:     n2>1;
             1 <= Index(p,f) by A2,Th41;
         then 1 + 1 < n2+Index(p,f) by A33,XREAL_1:10;
then A34:     1< n2+Index(p,f)-1 by XREAL_1:22;
         then 0<=n2+Index(p,f)-1;
then A35:     n2+Index(p,f)-'1 = n2+Index(p,f)-1 by BINARITH:def 3;
            1<=n1 & n1<=len <*p*> by A32,FINSEQ_1:56;
        then n1 in dom <*p*> by FINSEQ_3:27;
        then A36: g.n1=<*p*>.n1 by A4,FINSEQ_1:def 7;
             n2-1>0 by A33,XREAL_1:52;
         then A37:n2-'1=n2-1 by BINARITH:def 3;
             n2-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A31,XREAL_1:11;
         then A38:1<=n2-'1 & n2-'1<=len mid(f,Index(p,f)+1,len f)
                       by A33,A37,SPPOL_1:6;
         then A39:n2-'1 in dom mid(f,Index(p,f)+1,len f) by FINSEQ_3:27;
             len <*p*>+(n2-'1)=1+(n2-1) by A37,FINSEQ_1:56 .=n2;
         then g.n2 =mid(f,Index(p,f)+1,len f).(n2-'1)
                                       by A4,A39,FINSEQ_1:def 7
             .=f.(n2-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A38,Th27
             .=f.(n2+Index(p,f)-'1) by A37;
         then A40:f.(n2+Index(p,f)-'1)=p by A30,A32,A36,FINSEQ_1:57;
               n2 -' 1 <= len f - Index(p,f) by A17,A18,A31,Th5;
           then n2-1 <= len f - Index(p,f) by A33,BINARITH:50;
           then n2-1+Index(p,f) <= len f - Index(p,f)+ Index(p,f) by XREAL_1:8
;
          hence contradiction by A1,A3,A34,A35,A40,Th45;
       end;
       case that
A41:     n1>1 and
A42:     n2=1;
             1 <= Index(p,f) by A2,Th41;
         then 1 + 1 < n1+Index(p,f) by A41,XREAL_1:10;
then A43:     1< n1+Index(p,f)-1 by XREAL_1:22;
         then 0<=n1+Index(p,f)-1;
then A44:     n1+Index(p,f)-'1 = n1+Index(p,f)-1 by BINARITH:def 3;
            1<=n2 & n2<=len <*p*> by A42,FINSEQ_1:56;
        then n2 in dom <*p*> by FINSEQ_3:27;
        then A45: g.n2=<*p*>.n2 by A4,FINSEQ_1:def 7;
             n1-1>0 by A41,XREAL_1:52;
         then A46:n1-'1=n1-1 by BINARITH:def 3;
             n1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A31,XREAL_1:11;
         then A47:1<=n1-'1 & n1-'1<=len mid(f,Index(p,f)+1,len f)
                       by A41,A46,SPPOL_1:6;
         then A48:n1-'1 in dom mid(f,Index(p,f)+1,len f) by FINSEQ_3:27;
             len <*p*>+(n1-'1)=1+(n1-1) by A46,FINSEQ_1:56 .=n1;
         then g.n1 =mid(f,Index(p,f)+1,len f).(n1-'1)
                                       by A4,A48,FINSEQ_1:def 7
             .=f.(n1-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A47,Th27
             .=f.(n1+Index(p,f)-'1) by A46;
         then A49:f.(n1+Index(p,f)-'1)=p by A30,A42,A45,FINSEQ_1:57;
               n1 -' 1 <= len f - Index(p,f) by A17,A18,A31,Th5;
           then n1-1 <= len f - Index(p,f) by A41,BINARITH:50;
           then n1-1+Index(p,f) <= len f - Index(p,f)+ Index(p,f) by XREAL_1:8
;
          hence contradiction by A1,A3,A43,A44,A49,Th45;
        end;
        case that
A50:     n1>1 and
A51:     n2>1;
         A52:n2-1>0 by A51,XREAL_1:52;
         then A53:n2-'1=n2-1 by BINARITH:def 3;
         then A54: 0+1<=n2-'1 by A52,NAT_1:38;
            A55: n2-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A31,XREAL_1:11;
         then A56:n2-'1 in dom mid(f,Index(p,f)+1,len f) by A53,A54,FINSEQ_3:27
;
             len <*p*>+(n2-'1)=1+(n2-1) by A53,FINSEQ_1:56 .=n2;
         then A57:g.n2 =mid(f,Index(p,f)+1,len f).(n2-'1)
                                       by A4,A56,FINSEQ_1:def 7
             .=f.(n2-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A53,A54,A55,Th27
             .=f.(n2+Index(p,f)-'1) by A53;
          A58: 1<=n2-'1 + Index(p,f) by A54,NAT_1:37;
        then 0<=n2+Index(p,f)-1 by A53;
          then A59:n2+Index(p,f)-'1 = n2+Index(p,f)-1 by BINARITH:def 3;
               n2-'1 <= len f -' Index(p,f) by A24,A31,Th5;
           then n2-'1+Index(p,f)<=len f - Index(p,f)+ Index(p,f)
             by A23,XREAL_1:8;
         then A60:n2+Index(p,f)-'1 in dom f by A53,A58,A59,FINSEQ_3:27;
         A61:n1-1>0 by A50,XREAL_1:52;
         then A62:n1-'1=n1-1 by BINARITH:def 3;
         then A63: 0+1<=n1-'1 by A61,NAT_1:38;
            A64: n1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A31,XREAL_1:11;
         then A65:n1-'1 in dom mid(f,Index(p,f)+1,len f) by A62,A63,FINSEQ_3:27
;
             len <*p*>+(n1-'1)=1+(n1-1) by A62,FINSEQ_1:56 .=n1;
         then A66:g.n1 =mid(f,Index(p,f)+1,len f).(n1-'1)
                                       by A4,A65,FINSEQ_1:def 7
             .=f.(n1-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A62,A63,A64,Th27
             .=f.(n1+Index(p,f)-'1) by A62;
             A67: 1<=n1-1+Index(p,f) by A62,A63,NAT_1:37;
          then 0<=n1+Index(p,f)-1;
          then A68:n1+Index(p,f)-'1 = n1+Index(p,f)-1 by BINARITH:def 3;
               n1-'1 <= len f -' Index(p,f) by A24,A31,Th5;
           then n1-'1+Index(p,f)<=len f - Index(p,f)+ Index(p,f)
            by A23,XREAL_1:8;
         then n1+Index(p,f)-'1 in dom f by A62,A67,A68,FINSEQ_3:27;
         then n1+Index(p,f)-'1=n2+Index(p,f)-'1
              by A28,A30,A57,A60,A66,FUNCT_1:def 8;
         then n1+Index(p,f)=n2+Index(p,f)-1+1 by A59,A68;
         hence x1=x2;
        end;
        end;
       hence x1=x2;
      end;
     A69: len g -1+1>=1+1 by A10,A17,XREAL_1:8;
     A70: for j st 1 <= j & j + 2 <= len g
       holds LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)}
       proof let j;assume A71:1 <= j & j + 2 <= len g;
        A72:j+2=j+1+1;
        then A73:j+1<=len g by A71,NAT_1:38;
        A74:1<j+1 by A71,NAT_1:38;
        A75:1<=(j+1) by A71,NAT_1:38;
        A76: 1+1-1<=(j+1)-1 by A71;
        A77:LSeg(g,j) c= LSeg(f,Index(p,f)+j-'1)
            by A2,A4,A71,A73,Th49;
            LSeg(g,(j+1)) c= LSeg(f,Index(p,f)+(j+1)-'1)
                    by A2,A4,A71,A72,A75,Th49;
        then A78:LSeg(g,j)/\ LSeg(g,(j+1))
            c= LSeg(f,Index(p,f)+j-'1)/\ LSeg(f,Index(p,f)+(j+1)-'1)
            by A77,XBOOLE_1:27;
             1<=Index(p,f) by A2,Th41;
         then A79:1<=Index(p,f)+j by NAT_1:37;
         then A80: 1-1<=Index(p,f)+j-1 by XREAL_1:11;
        A81:1<=Index(p,f)+j-'1+1 by NAT_1:29;
        A82:j+1=j+1-1+1 .=j+1-'1+1 by A74,BINARITH:50;
        then A83:j+1=len <*p*>+(j+1-'1) by FINSEQ_1:56;
        A84:1<=j+1-'1 & j+1-'1<=len mid(f,Index(p,f)+1,len f)
                       by A27,A73,A76,A82,XREAL_1:8;
        then j+1-'1 in dom mid(f,Index(p,f)+1,len f) by FINSEQ_3:27;
        then A85:g.(j+1) = mid(f,Index(p,f)+1,len f).(j+1-'1)
                                       by A4,A83,FINSEQ_1:def 7
             .=f.(j+1-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A84,Th27
             .=f.(j+1-'1+1+Index(p,f)-'1)
             .=f.(j+1+Index(p,f)-'1) by A74,BINARITH:53
             .=f.(Index(p,f)+j+1-'1)
             .=f.(Index(p,f)+j) by BINARITH:39
             .=f.(Index(p,f)+j-'1+1) by A79,BINARITH:53;
            1 <= Index(p,f) by A2,Th41;
        then 1+1 <= Index(p,f)+j by A71,XREAL_1:9;
        then 1<=Index(p,f)+j-1 by XREAL_1:21;
        then A86:1<=Index(p,f)+j-'1 by Th1;
         1<=Index(p,f)+j+1 by NAT_1:29;
         then A87:Index(p,f)+(j+1)-'1
           =Index(p,f)+j+1-1 by BINARITH:50
           .=Index(p,f)+j-1+1
           .=Index(p,f)+j-'1+1 by A79,BINARITH:50;
        A88:g/.(j+1)=g.(j+1) by A73,A74,FINSEQ_4:24;
        A89:LSeg(g/.(j+1),g/.(j+1+1))=LSeg(g,j+1)
                         by A71,A74,TOPREAL1:def 5;
             j+1+1-1<=1+len mid(f,Index(p,f)+1,len f)-1
              by A27,A71,XREAL_1:11;
         then A90: j+1+Index(p,f)<= len f -Index(p,f)+Index(p,f) by A16,A19,
XREAL_1:8;
         then A91:Index(p,f)+j+1 <= len f;
          Index(p,f)+j-1+(1+1) <= len f by A90;
         then Index(p,f)+j-'1+2 <= len f by A80,BINARITH:def 3;
         then A92:{f/.(Index(p,f)+j-'1+1)}
                = LSeg(f,Index(p,f)+j-'1)/\ LSeg(f,Index(p,f)+j-'1+1)
                  by A8,A86,TOPREAL1:def 8;
          Index(p,f)+(j-1)+1 <= len f by A91,Th9;
         then Index(p,f)+j-'1+1 <= len f by A80,BINARITH:def 3;
        then A93:f/.(Index(p,f)+j-'1+1)=g/.(j+1) by A81,A85,A88,FINSEQ_4:24;
          A94:LSeg(g,j)=LSeg(g/.j,g/.(j+1)) by A71,A73,TOPREAL1:def 5;
          A95:g/.(j+1) in LSeg(g/.j,g/.(j+1)) by TOPREAL1:6;
              g/.(j+1) in LSeg(g/.(j+1),g/.(j+1+1)) by TOPREAL1:6;
          then g/.(j+1) in LSeg(g/.j,g/.(j+1)) /\ LSeg(g/.(j+1),g/.(j+1+1))
                         by A95,XBOOLE_0:def 3;
         then {g/.(j+1)} c= LSeg(g,j) /\ LSeg(g,j+1) by A89,A94,ZFMISC_1:37;
        hence {g/.(j+1)} = LSeg(g,j)/\ LSeg(g,j+1)
           by A78,A87,A92,A93,XBOOLE_0:def 10;
       end;
      A96: f is s.n.c. by A1,TOPREAL1:def 10;
      A97: for j1,j2 st j1+1 < j2 holds LSeg(g,j1) misses LSeg(g,j2)
      proof let j1,j2;assume A98:j1+1 < j2;
           j1>=0;
then A99:  j1=0 or j1>=0+1 by NAT_1:38;
           now per cases by A99,REAL_1:def 5;
       case j1=0;
       then LSeg(g,j1)={} by TOPREAL1:def 5;
        then LSeg(g,j1) /\ LSeg(g,j2) = {};
        hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
       end;
       case that A100:j1=1 or j1>1 and
A101:    j2+1<=len g;
           j2<len g by A101,NAT_1:38;
        then A102:j1+1<=len g by A98,XREAL_1:2;
            1<j1+1 by A100,NAT_1:38;
        then A103:1<=j2 by A98,XREAL_1:2;
        A104:LSeg(g,j1) c= LSeg(f,Index(p,f)+j1-'1)
            by A2,A4,A100,A102,Th49;
            LSeg(g,j2) c= LSeg(f,Index(p,f)+j2-'1) by A2,A4,A101,A103,Th49;
        then A105:LSeg(g,j1)/\ LSeg(g,j2)
            c= LSeg(f,Index(p,f)+j1-'1)/\ LSeg(f,Index(p,f)+j2-'1)
            by A104,XBOOLE_1:27;
            Index(p,f)+(j1+1)<Index(p,f)+j2 by A98,XREAL_1:8;
        then Index(p,f)+j1+1-1<Index(p,f)+j2-1 by XREAL_1:11;
        then A106:Index(p,f)+j1-1+1<Index(p,f)+j2-1;
             1<=Index(p,f) by A2,Th41;
         then 1<=Index(p,f)+j1 by NAT_1:37;
         then 1-1<=Index(p,f)+j1-1 by XREAL_1:11;
         then A107:Index(p,f)+j1-1=Index(p,f)+j1-'1 by BINARITH:def 3;
        Index(p,f)+j1-'1+1<Index(p,f)+j2-'1
                         by A106,A107,BINARITH:def 3;
        then LSeg(f,Index(p,f)+j1-'1) misses LSeg(f,Index(p,f)+j2-'1) by A96,
TOPREAL1:def 9;
        then LSeg(f,Index(p,f)+j1-'1) /\ LSeg(f,Index(p,f)+j2-'1) = {}
          by XBOOLE_0:def 7;
        then LSeg(g,j1) /\ LSeg(g,j2) = {} by A105,XBOOLE_1:3;
        hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
       end;
       case j2+1>len g; then LSeg(g,j2)={} by TOPREAL1:def 5;
          then LSeg(g,j1) /\ LSeg(g,j2) = {};
          hence thesis by XBOOLE_0:def 7;
       end;
       end;
       hence LSeg(g,j1) misses LSeg(g,j2);
      end;
     A108: f is special by A1,TOPREAL1:def 10;
         for j st 1 <= j & j+1 <= len g holds
     (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
     proof let j;assume A109: 1 <= j & j+1 <= len g;
       then A110:LSeg(g,j) c= LSeg(f,Index(p,f)+j-'1) by A2,A4,Th49;
       A111:LSeg(g,j) = LSeg(g/.j,g/.(j+1)) by A109,TOPREAL1:def 5;
             1<=Index(p,f) by A2,Th41;
         then A112:1<Index(p,f)+1 by NAT_1:38;
             Index(p,f)+1<=Index(p,f)+j by A109,XREAL_1:8;
         then 1<Index(p,f)+j by A112,XREAL_1:2;
         then A113:1<=Index(p,f)+j-1 by SPPOL_1:6;
         then A114: 0<=Index(p,f)+j-1;
         then A115:Index(p,f)+j-1=Index(p,f)+j-'1 by BINARITH:def 3;
             j+1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A109,XREAL_1:11;
         then j+1-1<= len f -Index(p,f) by A6,A13,A15,A20,Th27;
         then j+Index(p,f)<= len f -Index(p,f)+Index(p,f) by XREAL_1:8;
         then Index(p,f)+(j-1)+1 <= len f;
         then A116:Index(p,f)+j-'1+1 <= len f by A114,BINARITH:def 3;
         then A117:LSeg(f,Index(p,f)+j-'1)
           = LSeg(f/.(Index(p,f)+j-'1),f/.(Index(p,f)+j-'1+1))
                            by A113,A115,TOPREAL1:def 5;
             (f/.(Index(p,f)+j-'1))`1 = (f/.(Index(p,f)+j-'1+1))`1
          or (f/.(Index(p,f)+j-'1))`2 = (f/.(Index(p,f)+j-'1+1))`2
                 by A108,A113,A115,A116,TOPREAL1:def 7;
      hence (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
                              by A110,A111,A117,Th36;
     end;
     then g is one-to-one & len g >= 2
     & g is unfolded s.n.c. special by A29,A69,A70,A97,FUNCT_1:def 8
,TOPREAL1:def 7,def 8,def 9;
     then g is_S-Seq & g.1 = p & g.len g = f/.len f by A4,A6,A26,FINSEQ_1:58
,FINSEQ_4:24,TOPREAL1:def 10;
 hence g is_S-Seq_joining p,f/.len f by Def3;
 end;

theorem Th51: for f,g being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2,j
 st p in L~f & 1<=j & j+1<=len g
 & g=mid(f,1,Index(p,f))^<*p*>
 holds LSeg(g,j) c= LSeg(f,j)
proof let f,g be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2,j;
 assume that
A1: p in L~f and
A2: 1<=j and
A3: j+1<=len g and
A4: g=mid(f,1,Index(p,f))^<*p*>;
  A5:1<=Index(p,f) by A1,Th41;
  A6:Index(p,f) < len f by A1,Th41;
  then A7:1<=1 & 1<=len f & 1<=Index(p,f) & Index(p,f)<=len f by A5,XREAL_1:2;
     j<=j+1 by NAT_1:29;
  then A8:j<=len g by A3,XREAL_1:2;
  A9:1<=j+1 by NAT_1:29;
      now
       len g=len mid(f,1,Index(p,f)) + len <*p*> by A4,FINSEQ_1:35
        .=len mid(f,1,Index(p,f))+1 by FINSEQ_1:56;
   then len g=Index(p,f)-'1+1+1 by A7,Th27;
   then A10:len g=Index(p,f)+1 by A5,BINARITH:53;
       Index(p,f)+1<=len f +1 by A6,XREAL_1:8;
   then j+1<=len f +1 by A3,A10,XREAL_1:2;
   then A11: j+1-1<=len f +1-1 by XREAL_1:11;
   A12:j<=Index(p,f) by A3,A10,XREAL_1:8;
A13: len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A7,Th27
     .=Index(p,f) by A5,BINARITH:53;
   then A14:j in dom mid(f,1,Index(p,f)) by A2,A12,FINSEQ_3:27;
   A15:g/.j=g.j by A2,A8,FINSEQ_4:24
    .=mid(f,1,Index(p,f)).j by A4,A14,FINSEQ_1:def 7
   .=f.(j+1-'1) by A2,A7,A12,A13,Th27
   .=f.j by BINARITH:39
   .=f/.j by A2,A11,FINSEQ_4:24;
        now per cases;
    case A16:j+1<=Index(p,f);
     then A17:j+1<=len f by A6,XREAL_1:2;
A18:  len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A7,Th27
     .=Index(p,f) by A5,BINARITH:53;
     then A19:j+1 in dom mid(f,1,Index(p,f)) by A9,A16,FINSEQ_3:27;
     A20:g/.(j+1)=g.(j+1) by A3,A9,FINSEQ_4:24
        .= mid(f,1,Index(p,f)).(j+1) by A4,A19,FINSEQ_1:def 7
     .=f.(j+1+1-'1) by A7,A9,A16,A18,Th27
     .=f.(j+1) by BINARITH:39
     .=f/.(j+1) by A9,A17,FINSEQ_4:24;
         LSeg(g,j)=LSeg(g/.j,g/.(j+1)) by A2,A3,TOPREAL1:def 5;
     hence LSeg(g,j) c= LSeg(f,j) by A2,A15,A17,A20,TOPREAL1:def 5;
    end;
    case j+1>Index(p,f);
     then j>=Index(p,f) by NAT_1:38;
     then A21:j=Index(p,f) by A12,XREAL_1:1;
         1<=1 & 1<=len <*p*> by FINSEQ_1:57;
     then A22:1 in dom <*p*> by FINSEQ_3:27;
     A23:len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A7,Th27
     .=Index(p,f) by A5,BINARITH:53;
     A24:g/.(j+1)=g.(j+1) by A3,A9,FINSEQ_4:24
        .=<*p*>.1 by A4,A21,A22,A23,FINSEQ_1:def 7
     .=p by FINSEQ_1:def 8;
     A25:f/.j in LSeg(f/.j,f/.(j+1)) by TOPREAL1:6;
     A26:p in LSeg(f,j) by A1,A21,Th42;
         now assume j+1>len f;
      then j>=len f by NAT_1:38;
     hence contradiction by A1,A21,Th41;
     end;
     then A27:LSeg(f,j)=LSeg(f/.j,f/.(j+1)) by A2,TOPREAL1:def 5;
     then LSeg(g/.j,g/.(j+1)) c= LSeg(f/.j,f/.(j+1))
                       by A15,A24,A25,A26,TOPREAL1:12;
     hence LSeg(g,j) c= LSeg(f,j) by A2,A3,A27,TOPREAL1:def 5;
    end;
    end;
   hence LSeg(g,j) c= LSeg(f,j);
  end;
 hence LSeg(g,j) c= LSeg(f,j);
end;

theorem Th52: for f,g being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2
 st f is_S-Seq & p in L~f & p<>f.1
 & g=mid(f,1,Index(p,f))^<*p*>
 holds g is_S-Seq_joining f/.1,p
 proof let f,g be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2;
 assume that
A1:f is_S-Seq and
A2: p in L~f and
A3: p<>f.1 and
A4: g=mid(f,1,Index(p,f))^<*p*>;
  consider i such that A5:1<=i & i+1<=len f & p in LSeg(f,i)
     by A2,SPPOL_2:13;
  A6:1<=Index(p,f) by A2,Th41;
  A7:Index(p,f)<=len f by A2,Th41;
      1<=1+i by NAT_1:37;
  then A8:1<=len f by A5,XREAL_1:2;
  then A9: len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A6,A7,Th27;
  then A10:len mid(f,1,Index(p,f))=Index(p,f) by A6,BINARITH:53;
      1<=len <*p*> by FINSEQ_1:56;
  then A11:  1 in dom <*p*> by FINSEQ_3:27;
        g.1=mid(f,1,Index(p,f)).1 by A4,A6,A10,Th17;
    then g.1=f.1 by A6,A7,A8,Th27;
    then A12: g.1=f/.1 by A8,FINSEQ_4:24;
A13:  len g = len mid(f,1,Index(p,f))+ len<*p*> by A4,FINSEQ_1:35
          .= len mid(f,1,Index(p,f))+ 1 by FINSEQ_1:56;
    then A14:  g.len g = p by A4,FINSEQ_1:59;
A15:    1+1<=len g by A6,A10,A13,XREAL_1:8;
     A16: f is one-to-one by A1,TOPREAL1:def 10;
     A17:for n1,n2 being Nat st 1<=n1 & n1<=len f
       & 1<=n2 & n2<=len f & f.n1=f.n2
       holds n1=n2
       proof let n1,n2 be Nat;assume A18:1<=n1 & n1<=len f
           & 1<=n2 & n2<=len f & f.n1=f.n2;
         then n1 in dom f & n2 in dom f by FINSEQ_3:27;
        hence n1=n2 by A16,A18,FUNCT_1:def 8;
       end;

     A19: for x1,x2 being set st x1 in dom g & x2 in dom g & g.x1=g.x2
      holds x1=x2
     proof let x1,x2 be set;assume A20:x1 in dom g & x2 in dom g & g.x1=g.x2;
       then reconsider n1=x1,n2=x2 as Nat;
       A21:1<=n1 & n1<=len g & 1<=n2 & n2<=len g by A20,FINSEQ_3:27;
           now
        A22:g.len g =<*p*>.1 by A4,A11,A13,FINSEQ_1:def 7
              .=p by FINSEQ_1:def 8;
            now per cases;
        case A23:n1=len g;
              now assume A24: n2<>len g; then n2<len g by A21,REAL_1:def 5;
           then A25:n2<=len mid(f,1,Index(p,f)) by A13,NAT_1:38;
           then g.n2=mid(f,1,Index(p,f)).n2 by A4,A21,FINSEQ_1:85;
           then g.n2=f.(n2+1-'1) by A6,A7,A8,A21,A25,Th27;
           then A26:p=f.n2 by A20,A22,A23,BINARITH:39;
A27:        n2 <= len f by A7,A10,A25,XREAL_1:2;
               1 < n2 by A3,A21,A26,XREAL_1:1;
           then Index(p,f)+1 = n2 by A1,A26,A27,Th45;
          hence contradiction by A6,A9,A13,A24,BINARITH:53;
          end;
         hence x1=x2 by A23;
        end;
        case A28:n2=len g;
              now assume A29: n1<>len g; then n1<len g by A21,REAL_1:def 5;
           then A30:n1<=len mid(f,1,Index(p,f)) by A13,NAT_1:38;
           then g.n1=mid(f,1,Index(p,f)).n1 by A4,A21,FINSEQ_1:85;
           then g.n1=f.(n1+1-'1) by A6,A7,A8,A21,A30,Th27;
           then A31:p=f.n1 by A20,A22,A28,BINARITH:39;
A32:        n1 <= len f by A7,A10,A30,XREAL_1:2;
               1 < n1 by A3,A21,A31,XREAL_1:1;
           then Index(p,f)+1 = n1 by A1,A31,A32,Th45;
          hence contradiction by A6,A9,A13,A29,BINARITH:53;
          end;
         hence x1=x2 by A28;
        end;
        case that
A33:       n1<>len g and
A34:       n2 <> len g;
             n1<len g by A21,A33,REAL_1:def 5;
         then A35:n1<=len mid(f,1,Index(p,f)) by A13,NAT_1:38;
         then A36:n1<=len f by A7,A10,XREAL_1:2;
         A37:g.n1=mid(f,1,Index(p,f)).n1 by A4,A21,A35,FINSEQ_1:85
             .=f.n1 by A7,A10,A21,A35,Th32;
              n2 < len g by A21,A34,XREAL_1:1;
          then A38:n2<=len mid(f,1,Index(p,f)) by A13,NAT_1:38;
          then A39:n2<=len f by A7,A10,XREAL_1:2;
              g.n2=mid(f,1,Index(p,f)).n2 by A4,A21,A38,FINSEQ_1:85
              .=f.n2 by A7,A10,A21,A38,Th32;
          hence x1=x2 by A17,A20,A21,A36,A37,A39;
        end;
        end;
        hence x1=x2;
       end;
     hence x1=x2;
     end;

A40: f is unfolded by A1,TOPREAL1:def 10;
  A41: for j st 1 <= j & j + 2 <= len g
       holds LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)}
  proof let j;assume A42: 1 <= j & j + 2 <= len g;
   then j+1<=len g by Th10;
   then A43:LSeg(g,j) c= LSeg(f,j) by A2,A4,A42,Th51;
   A44:j+1+1<=len g by A42;
   A45:1<=j+1 by A42,Th11;
   then LSeg(g,j+1) c= LSeg(f,j+1) by A2,A4,A44,Th51;
   then A46:LSeg(g,j)/\ LSeg(g,j+1) c= LSeg(f,j)/\ LSeg(f,j+1) by A43,
XBOOLE_1:27;
   A47:j+1<=len g by A42,Th10;
   then LSeg(g,j)=LSeg(g/.j,g/.(j+1)) by A42,TOPREAL1:def 5;
   then A48:g/.(j+1) in LSeg(g,j) by TOPREAL1:6;
   A49:g/.(j+1)=g.(j+1) by A45,A47,FINSEQ_4:24;
   A50:Index(p,f)<=len f by A2,Th41;
    LSeg(g,j+1)=LSeg(g/.(j+1),g/.(j+1+1)) by A42,A45,TOPREAL1:def 5;
   then g/.(j+1) in LSeg(g,j+1) by TOPREAL1:6;
   then g/.(j+1) in LSeg(g,j) /\ LSeg(g,j+1) by A48,XBOOLE_0:def 3;
   then A51:{g/.(j+1)} c= LSeg(g,j) /\ LSeg(g,j+1) by ZFMISC_1:37;
       now
     A52:len g=len mid(f,1,Index(p,f))+1 by A4,FINSEQ_2:19;
          Index(p,f)<=len f by A2,Th41;
      then A53: len g<=len f +1 by A10,A52,XREAL_1:8;
           now per cases by A53,REAL_1:def 5;
       case len g=len f +1;
        then len f =Index(p,f) by A10,A52;
        hence contradiction by A2,Th41;
       end;
       case len g<len f+1;
        then len g<=len f by NAT_1:38;
         then A54:j+2<=len f by A42,XREAL_1:2;
         A55:j+1<=Index(p,f) by A10,A44,A52,XREAL_1:8;
         then A56:j+1<=len f by A50,XREAL_1:2;
         A57:LSeg(g,j)/\ LSeg(g,j+1) c= {f/.(j+1)}
            by A40,A42,A46,A54,TOPREAL1:def 8;
         A58:f.(j+1)=f/.(j+1) by A45,A56,FINSEQ_4:24;
             g.(j+1)=mid(f,1,Index(p,f)).(j+1) by A4,A10,A45,A55,FINSEQ_1:85
               .=f.(j+1) by A7,A45,A55,Th32;
        hence LSeg(g,j)/\ LSeg(g,j+1) = {g/.(j+1)}
                         by A49,A51,A57,A58,XBOOLE_0:def 10;
       end;
       end;
      hence LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)};
   end;
   hence LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)};
  end;
      A59: f is s.n.c. by A1,TOPREAL1:def 10;
      A60: for j1,j2 st j1+1 < j2 holds LSeg(g,j1) misses LSeg(g,j2)
      proof let j1,j2;assume A61:j1+1 < j2;
           j1>=0;
then A62:  j1=0 or j1>=0+1 by NAT_1:38;
           now per cases by A62,REAL_1:def 5;
       case j1=0;
         then LSeg(g,j1)={} by TOPREAL1:def 5;
         then LSeg(g,j1) /\ LSeg(g,j2) = {};
        hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
       end;
       case that
A63:    j1=1 or j1>1 and
A64:    j2+1<=len g;
            j2<len g by A64,NAT_1:38;
         then j1+1<len g by A61,XREAL_1:2;
         then A65:LSeg(g,j1) c= LSeg(f,j1) by A2,A4,A63,Th51;
             1+1<=j1+1 by A63,XREAL_1:8;
         then 2<=j2 by A61,XREAL_1:2;
         then 1<=j2 by XREAL_1:2;
         then LSeg(g,j2) c= LSeg(f,j2) by A2,A4,A64,Th51;
         then A66:LSeg(g,j1)/\ LSeg(g,j2) c= LSeg(f,j1)/\ LSeg(f,j2)
                                  by A65,XBOOLE_1:27;
             LSeg(f,j1) misses LSeg(f,j2) by A59,A61,TOPREAL1:def 9;
         then LSeg(f,j1) /\ LSeg(f,j2) = {} by XBOOLE_0:def 7;
         then LSeg(g,j1) /\ LSeg(g,j2) = {} by A66,XBOOLE_1:3;
        hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
       end;
       case j2+1>len g; then LSeg(g,j2)={} by TOPREAL1:def 5;
        then LSeg(g,j1)/\ LSeg(g,j2)={};
        hence thesis by XBOOLE_0:def 7;
       end;
       end;
     hence LSeg(g,j1) misses LSeg(g,j2);
     end;
     A67: f is special by A1,TOPREAL1:def 10;
         for j st 1 <= j & j+1 <= len g holds
     (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
     proof let j;assume A68: 1 <= j & j+1 <= len g;
       A69: now
             j+1<=Index(p,f)+1 by A4,A10,A68,FINSEQ_2:19;
         then A70:j<=Index(p,f) by XREAL_1:8;
             Index(p,f)<len f by A2,Th41;
         then j<len f by A70,XREAL_1:2;
        hence j+1<=len f by NAT_1:38;
       end;
       A71:LSeg(g,j) c= LSeg(f,j) by A2,A4,A68,Th51;
       A72:LSeg(g,j) = LSeg(g/.j,g/.(j+1)) by A68,TOPREAL1:def 5;
         A73:LSeg(f,j)
           = LSeg(f/.j,f/.(j+1)) by A68,A69,TOPREAL1:def 5;
             (f/.j)`1 = (f/.(j+1))`1
          or (f/.j)`2 = (f/.(j+1))`2 by A67,A68,A69,TOPREAL1:def 7;
      hence (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
                              by A71,A72,A73,Th36;
     end;
     then g is one-to-one & len g >= 2 & g is unfolded s.n.c. special
     by A15,A19,A41,A60,FUNCT_1:def 8,TOPREAL1:def 7,def 8,def 9;
     then g is_S-Seq by TOPREAL1:def 10;
 hence g is_S-Seq_joining f/.1,p by A12,A14,Def3;
 end;

begin
::----------------------------------------------------------------------:
::  Left and Right Cutting Functions for Finite Sequences in TOP-REAL 2 :
::----------------------------------------------------------------------:

definition let f be FinSequence of TOP-REAL 2,p be Point of TOP-REAL 2;
 func L_Cut(f,p) -> FinSequence of TOP-REAL 2 equals
 :Def4:
   <*p*>^mid(f,Index(p,f)+1,len f) if p<>f.(Index(p,f)+1)
    otherwise mid(f,Index(p,f)+1,len f);
 correctness;
 func R_Cut(f,p) -> FinSequence of TOP-REAL 2 equals
 :Def5:
   mid(f,1,Index(p,f))^<*p*> if p<>f.1
   otherwise <*p*>;
 correctness;
end;

theorem Th53:
 for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
    st f is_S-Seq & p in L~f & p = f.(Index(p,f)+1) & p <> f.len f
 holds Index(p,Rev f) + Index(p,f) + 1 = len f
proof let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: f is_S-Seq and
A2: p in L~f and
A3: p = f.(Index(p,f)+1) and
A4: p <> f.len f;
A5: Rev f is_S-Seq by A1,SPPOL_2:47;
A6: len f = len Rev f by FINSEQ_5:def 3;
       len f <= len f + Index(p,f) by NAT_1:29;
   then A7: len f - Index(p,f) <= len Rev f by A6,XREAL_1:22;
       Index(p,f) < len f by A2,Th41;
   then A8: Index(p,f)+1 <= len f by NAT_1:38;
      1 <= Index(p,f)+1 by NAT_1:29;
  then Index(p,f)+1 in dom f by A8,FINSEQ_3:27;
  then A9: Index(p,f)+1 in dom Rev f by FINSEQ_5:60;
       Index(p,f)+1 < len f by A3,A4,A8,XREAL_1:1;
   then A10: 1 < len f - Index(p,f) by XREAL_1:22;
       Index(p,f) <= len f by A2,Th41;
   then A11: len f - Index(p,f) = len f -' Index(p,f) by BINARITH:50;
      p = (Rev Rev f).(Index(p,f)+1) by A3,FINSEQ_6:29
   .= (Rev f).(len Rev f - (Index(p,f)+1) + 1) by A9,FINSEQ_5:61
   .= (Rev f).(len Rev f - Index(p,f))
   .= (Rev f).(len f - Index(p,f)) by FINSEQ_5:def 3;
  then A12:  Index(p,Rev f) + 1 = len f -' Index(p,f) by A5,A7,A10,A11,Th45;
 thus Index(p,Rev f) + Index(p,f) + 1
           = Index(p,Rev f) + 1 + Index(p,f)
          .= len f by A11,A12;
end;

theorem Th54:
 for f be FinSequence of TOP-REAL 2
 for p be Point of TOP-REAL 2 st f is unfolded s.n.c. &
  p in L~f & p <> f.(Index(p,f)+1) holds
   Index(p,Rev f) + Index(p,f) = len f
 proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2 such that
   A1: f is unfolded s.n.c. and
   A2: p in L~f and
   A3: p <> f.(Index(p,f)+1);
  A4: Rev f is unfolded by A1,SPPOL_2:29;
  A5: Rev f is s.n.c. by A1,SPPOL_2:36;
  A6: Index(p,f) < len f by A2,Th41;
  then A7: len f -' Index(p,f) + Index(p,f) = len f by BINARITH:53;
  A8: len f = len Rev f by FINSEQ_5:def 3;
  p in LSeg(f,Index(p,f)) by A2,Th42;
  then A9: p in LSeg(Rev f,len f -' Index(p,f)) by A7,SPPOL_2:2;
  A10: Index(p,f) < len f by A2,Th41;
  then Index(p,f) + 1 <= len f by NAT_1:38;
  then 1<=len f - Index(p,f) by XREAL_1:21;
  then A11: 1<=len f -' Index(p,f) by Th1;
  0+1 <= Index(p,f) by A2,Th41;
  then len f + 0 < len f + Index(p,f) by XREAL_1:8;
  then len f - Index(p,f) < len f by XREAL_1:21;
  then A12: len f -' Index(p,f) < len f by A6,BINARITH:50;
  then A13: len f -' Index(p,f)+1<=len Rev f by A8,NAT_1:38;
  len f -' Index(p,f) in dom f by A11,A12,FINSEQ_3:27;
  then (Rev f).(len f -' Index(p,f))
      = f.(len f - (len f -' Index(p,f)) + 1) by FINSEQ_5:61
     .= f.(len f - (len f - Index(p,f)) + 1) by A10,BINARITH:50
     .= f.(0 + Index(p,f) + 1);
  then len f -' Index(p,f)=Index(p,Rev f) by A3,A4,A5,A9,A13,Th47;
  hence Index(p,Rev f) + Index(p,f) = len f by A10,BINARITH:53;
 end;

theorem Th55:
 for D for f being FinSequence of D, k being Nat, p being Element of D
  holds (<*p*>^f)|(k+1) = <*p*>^(f|k)
proof let D; let f be FinSequence of D, k be Nat, p be Element of D;
A1: f|Seg k = f|k by FINSEQ_1:def 15;
 thus (<*p*>^f)|(k+1) = (<*p*>^f)|Seg(k+1) by FINSEQ_1:def 15
          .= (<*p*>^f)|Seg(k+len<*p*>) by FINSEQ_1:56
          .= <*p*>^(f|k) by A1,FINSEQ_6:16;
end;

theorem Th56:
 for D for f being FinSequence of D, k1,k2 being Nat
  st k1 < k2 & k1 in dom f
 holds mid(f,k1,k2) = <*f.k1*>^ mid(f,k1+1,k2)
proof let D; let f be FinSequence of D, k1,k2 be Nat;
assume
A1: k1 < k2;
 then A2: k1+1 <= k2 by NAT_1:38;
A3: 1 <= k2 -' k1
proof
 per cases by A2,XREAL_1:1;
 suppose k1 + 1 = k2;
 hence 1 <= k2 -' k1 by BINARITH:39;
 end;
 suppose
A4: k1 + 1 < k2;
     k2-'k1 <= 1 implies k2 <= 1 + k1
proof assume
    k2-'k1 <= 1;
  then k2-'k1 +k1 <= 1 + k1 by XREAL_1:8;
 hence k2 <= 1 + k1 by A1,BINARITH:53;
end;
 hence 1 <= k2 -' k1 by A4;
end;
end;
 assume
A5: k1 in dom f;
  then A6: f.k1 = f/.k1 by FINSEQ_4:def 4;
      k2-'k1 = k2-k1 by A3,Th1;
  then A7: k2-'k1-'1 = k2-k1-1 by A3,BINARITH:50
    .= k2-(k1+1)
    .= k2-'(k1+1) by A2,BINARITH:50;
      1 <= k1 by A5,FINSEQ_3:27;
  then k1 -' 1 + 1 = k1 by BINARITH:53;
  then (f/^(k1-'1)) = <*f/.k1*>^(f/^k1) by A5,FINSEQ_5:34;
 hence mid(f,k1,k2) = (<*f/.k1*>^ (f/^k1))|(k2-'k1+1) by A1,Def1
     .= <*f.k1*>^ ((f/^k1)|(k2-'k1)) by A6,Th55
     .= <*f.k1*>^ ((f/^k1)|(k2-'(k1+1)+1)) by A3,A7,BINARITH:53
     .= <*f.k1*>^ ((f/^(k1+1-'1))|(k2-'(k1+1)+1)) by BINARITH:39
     .= <*f.k1*>^ mid(f,k1+1,k2) by A2,Def1;
end;

registration let f be non empty FinSequence;
 cluster Rev f -> non empty;
 coherence
  proof
   dom Rev f = dom f by FINSEQ_5:60;
   hence thesis by RELAT_1:60,64;
  end;
end;

theorem Th57:
 for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
    st f is_S-Seq & p in L~f holds
 L_Cut(Rev f,p) = Rev R_Cut(f,p)
proof
 let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: f is_S-Seq and
A2: p in L~f;
A3: f is unfolded s.n.c. by A1,TOPREAL1:def 10;
A4: Rev f is_S-Seq by A1,SPPOL_2:47; then
A5: Rev f is unfolded s.n.c. by TOPREAL1:def 10;
A6: p in L~Rev f by A2,SPPOL_2:22;
A7: Rev Rev f = f by FINSEQ_6:29;
A8: len f = len Rev f by FINSEQ_5:def 3;
A9: L~f = L~Rev f by SPPOL_2:22;
A10: dom Rev f = dom f by FINSEQ_5:60;
A11: 1 <= Index(p, Rev f)+1 by NAT_1:29;
       Index(p, Rev f) < len Rev f by A2,A9,Th41;
   then A12:  Index(p, Rev f) + 1 <= len f by A8,NAT_1:38;
   then A13: Index(p, Rev f)+1 in dom f by A11,FINSEQ_3:27;
A14: 1+1 <= len f by A1,TOPREAL1:def 10;
    then A15: 1 < len f by NAT_1:38;
A16: 1 <= Index(p,f) by A2,Th41;
A17: Index(p,f) < len f by A2,Th41;
A18: 1 in dom f by A15,FINSEQ_3:27;
A19: 2 in dom f by A14,FINSEQ_3:27;
A20: len f in dom f by A15,FINSEQ_3:27;
 per cases;
 suppose
A21:  p = f.len f;
   then A22:   p = (Rev f).1 by FINSEQ_5:65;
   then p = (Rev f)/.1 by A10,A18,FINSEQ_4:def 4;
   then A23: Index(p,Rev f) = 1 by A8,A14,Th44;
        f is one-to-one by A1,TOPREAL1:def 10;
   then A24: p<>f.1 by A15,A18,A20,A21,FUNCT_1:def 8;
   Rev f is one-to-one by A4,TOPREAL1:def 10;
   then A25: p <> (Rev f).(1+1) by A10,A18,A19,A22,FUNCT_1:def 8;
   then Index(p,Rev f) + Index(p,f) = len f by A5,A6,A7,A8,A23,Th54;
   then A26: Index(p,Rev f) = len f - Index(p,f);
  thus L_Cut(Rev f,p)
           = <*p*>^mid(Rev f,Index(p,Rev f)+1,len f) by A8,A23,A25,Def4
          .= <*p*>^mid(Rev f,len f -'Index(p,f)+1,len f) by A17,A26,BINARITH:50
          .= <*p*>^mid(Rev f,len f -'Index(p,f)+1,len f -' 1 + 1) by A15,
BINARITH:53
          .= <*p*>^Rev mid(f,1,Index(p,f)) by A15,A16,A17,Th22
          .= Rev(mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
          .= Rev R_Cut(f,p) by A24,Def5;
 end;
 suppose
A27:  p = f.1;
   then A28:   p = (Rev f).len f by FINSEQ_5:65;
   A29:len (Rev f/^(len Rev f-'1))=len Rev f-'(len Rev f-'1) by Th19;
A30: len Rev f-'1+1=len Rev f by A8,A15,BINARITH:53;
   then A31:(Rev f/^(len Rev f-'1)).1=Rev f.len Rev f by Th23;
       1<=len Rev f-(len Rev f-'1) by A30;
   then A32: 1<=len (Rev f/^(len Rev f-'1)) by A29,Th1;
  then 0<len (Rev f/^(len Rev f-'1));
  then A33:Rev f/^(len Rev f-'1) is non empty by FINSEQ_1:25;
      len Rev f-'len Rev f+1=len Rev f-len Rev f+1 by BINARITH:50
  .=1;
then A34: mid(Rev f,len Rev f,len Rev f)=(Rev f/^(len Rev f-'1))|1 by Def1
  .=<*(Rev f/^(len Rev f-'1))/.1 *> by A33,FINSEQ_5:23
  .=<*Rev f.len Rev f*> by A31,A32,FINSEQ_4:24;
       Index(p,Rev f) + 1 = len f by A4,A8,A15,A28,Th45;
  hence L_Cut(Rev f,p) = <*p*> by A8,A28,A34,Def4
           .= Rev <*p*> by FINSEQ_5:63
           .= Rev R_Cut(f,p) by A27,Def5;
 end;
 suppose that
A35: p <> f.1 and
A36: p <> f.len f and
A37: p = f.(Index(p,f)+1);
A38: p <> (Rev f).len f by A35,FINSEQ_5:65;
      len f = Index(p,Rev f) + Index(p,f) + 1 by A1,A2,A36,A37,Th53
        .= Index(p,Rev f) + (Index(p,f) + 1);
  then A39: p = f.(len f - (Index(p, Rev f)+1) + 1) by A37
   .= (Rev f).(Index(p, Rev f)+1) by A13,FINSEQ_5:61;
A40: len f = Index(p,Rev f) + Index(p,f) + 1 by A1,A2,A36,A37,Th53
        .= Index(p,f) + (Index(p,Rev f) + 1);
A41: len f -' Index(p,f) = len f - Index(p,f) by A17,BINARITH:50
         .= Index(p,Rev f)+1 by A40;
A42: Index(p, Rev f)+1 < len f by A12,A38,A39,XREAL_1:1;
  thus L_Cut(Rev f,p)
          = mid(Rev f,Index(p,Rev f)+1,len f) by A8,A39,Def4
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f)
                  by A10,A13,A39,A41,A42,Th56
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f-'1+1)
                      by A15,BINARITH:53
         .= <*p*>^Rev mid(f,1,Index(p,f)) by A15,A16,A17,Th22
         .= Rev (mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
         .= Rev R_Cut(f,p) by A35,Def5;
 end;
 suppose that
A43: p <> f.1 and
A44: p <> f.(Index(p,f)+1);
A45: p <> (Rev f).len f by A43,FINSEQ_5:65;
A46: now assume
A47:   p = (Rev f).(Index(p, Rev f)+1);
     then A48: len Rev f = Index(p,Rev Rev f) + Index(p,Rev f) + 1
               by A4,A6,A8,A45,Th53
        .= Index(p,f) + 1 + Index(p,Rev f) by A7;
         p = f.(len f - (Index(p, Rev f)+1) + 1) by A13,A47,FINSEQ_5:61
      .= f.(Index(p,f)+1) by A8,A48;
    hence contradiction by A44;
   end;
A49: len f = Index(p,Rev f) + Index(p,f) by A3,A2,A44,Th54;
A50: Index(p, f) < len f by A2,Th41;
    Index(p,Rev f) = len f - Index(p,f) by A49
               .= len f -' Index(p,f) by A50,BINARITH:50;
  hence L_Cut(Rev f,p)
          = <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f) by A8,A46,Def4
         .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f-'1+1)
               by A15,BINARITH:53
         .= <*p*>^Rev mid(f,1,Index(p,f)) by A15,A16,A17,Th22
         .= Rev (mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
         .= Rev R_Cut(f,p) by A43,Def5;
end;
end;

theorem Th58:
 for f being FinSequence of TOP-REAL 2,
     p being Point of TOP-REAL 2 st p in L~f holds
 (L_Cut(f,p)).1=p &
 for i st 1<i & i<=len L_Cut(f,p) holds
   (p = f.(Index(p,f)+1) implies (L_Cut(f,p)).i=f.(Index(p,f)+i)) &
   (p <> f.(Index(p,f)+1) implies (L_Cut(f,p)).i=f.(Index(p,f)+i-1))
proof let f be FinSequence of TOP-REAL 2,
          p be Point of TOP-REAL 2;
    assume
A1: p in L~f;
    then len f <> 0 by TOPREAL1:28; then
A2: f is non empty by FINSEQ_1:25;
   now per cases;
   suppose
A3:   p = f.(Index(p,f)+1);
     then A4:   L_Cut(f,p)=mid(f,Index(p,f)+1,len f) by Def4;
A5:   1 <= Index(p,f)+1 by NAT_1:29;
         Index(p,f) < len f by A1,Th41;
     then A6:   Index(p,f)+1 <= len f by NAT_1:38;
     1 in dom f by A2,FINSEQ_5:6;
     then 1 <= len f by FINSEQ_3:27;
    hence (L_Cut(f,p)).1 = p by A3,A4,A5,A6,Th27;
   end;
   suppose p<> f.(Index(p,f)+1);
    then L_Cut(f,p)=<*p*>^mid(f,Index(p,f)+1,len f) by Def4;
   hence (L_Cut(f,p)).1=p by FINSEQ_1:58;
  end;
  end;
 hence (L_Cut(f,p)).1=p;
 let i;assume A7: 1<i & i<=len L_Cut(f,p);
        Index(p,f) < len f by A1,Th41;
    then A8:1<=Index(p,f)+1 & Index(p,f)+1<=len f by NAT_1:29,38;
    then A9:1<=len f by XREAL_1:2;
    A10:len <*p*><=i by A7,FINSEQ_1:57;
    A11:len <*p*><i by A7,FINSEQ_1:56;
    A12: 1<=i-1 by A7,SPPOL_1:6;
    A13:(i-'1)+1=i-1+1 by A7,BINARITH:50 .=i;
    hereby assume p = f.(Index(p,f)+1);
     then L_Cut(f,p)=mid(f,Index(p,f)+1,len f) by Def4;
    hence (L_Cut(f,p)).i = f.(i+(Index(p,f)+1)-'1) by A7,A8,A9,Th27
                  .= f.(i+Index(p,f)+1-'1)
                  .= f.(Index(p,f)+i) by BINARITH:39;
   end;
  assume p <> f.(Index(p,f)+1);
   then A14: L_Cut(f,p)=<*p*>^mid(f,Index(p,f)+1,len f) by Def4;
   then A15: i<=len <*p*>+len mid(f,Index(p,f)+1,len f) by A7,FINSEQ_1:35;
        len mid(f,Index(p,f)+1,len f)=len f -'(Index(p,f)+1)+1
               by A8,A9,Th27;
    then len <*p*>+len mid(f,Index(p,f)+1,len f)
       =1+(len f -'(Index(p,f)+1)+1) by FINSEQ_1:57
       .=1+(len f -(Index(p,f)+1)+1) by A8,BINARITH:50
       .=(len f -Index(p,f))+1;
    then i-1<=len f -Index(p,f)+1-1 by A15,XREAL_1:11;
    then A16:1<=i-'1 & i-'1<=len f -(Index(p,f)+1)+1 by A12,Th1;
A17: (L_Cut(f,p)).i =mid(f,Index(p,f)+1,len f).(i-len <*p*>) by A7,A11,A14,Th15
                 .=mid(f,Index(p,f)+1,len f).(i-'len <*p*>) by A10,BINARITH:50
                 .=mid(f,Index(p,f)+1,len f).(i-'1) by FINSEQ_1:56
                 .=f.((i-'1)+(Index(p,f)+1)-'1) by A8,A16,Th31
                 .=f.(Index(p,f)+i-'1) by A13;
       i <=i+Index(p,f) by NAT_1:29; then 1<=i+Index(p,f) by A7,XREAL_1:2;
  hence (L_Cut(f,p)).i=f.(Index(p,f)+i-1) by A17,BINARITH:50;
end;

theorem Th59:
 for f be FinSequence of TOP-REAL 2
 for p be Point of TOP-REAL 2 st p in L~f holds
  R_Cut(f,p).(len R_Cut(f,p)) = p &
  for i be Nat st 1<=i & i<=Index(p,f) holds R_Cut(f,p).i = f.i
 proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume A1: p in L~f;
  A2: 1<=Index(p,f) & Index(p,f)<len f by A1,Th41;
  then len f > 1 by XREAL_1:2;
  then A3: len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A2,Th27
     .= Index(p,f) by A2,BINARITH:53;
  now per cases;
   suppose p<>f.1;
    then A4: R_Cut(f,p) = mid(f,1,Index(p,f))^<*p*> by Def5;
    len (mid(f,1,Index(p,f))^<*p*>)= len mid(f,1,Index(p,f))+len <*p*>
                                                              by FINSEQ_1:35
       .= len mid(f,1,Index(p,f))+1 by FINSEQ_1:56;
    hence (R_Cut(f,p)).(len R_Cut(f,p)) = p by A4,FINSEQ_1:59;
   end;
   suppose p=f.1;
    then A5: R_Cut(f,p) = <*p*> by Def5;
    then len R_Cut(f,p) = 1 by FINSEQ_1:57;
    hence (R_Cut(f,p)).(len R_Cut(f,p)) = p by A5,FINSEQ_1:57;
   end;
  end;
  hence (R_Cut(f,p)).(len R_Cut(f,p)) = p;
  thus for i be Nat st 1<=i & i<=Index(p,f) holds R_Cut(f,p).i=f.i
  proof let i be Nat;
   assume A6: 1<=i & i<=Index(p,f);
   now per cases;
    case p<>f.1;
     then (R_Cut(f,p)).i=(mid(f,1,Index(p,f))^<*p*>).i by Def5
        .= mid(f,1,Index(p,f)).i by A3,A6,FINSEQ_1:85
        .=f.i by A2,A6,Th32;
     hence R_Cut(f,p).i = f.i;
    end;
    case A7: p=f.1;
     then A8: R_Cut(f,p) = <*p*> by Def5;
     A9: len f > 1 by A2,XREAL_1:2;
     then A10: len f >= 1+1 by NAT_1:38;
     1 in dom f by A9,FINSEQ_3:27;
     then p = f/.1 by A7,FINSEQ_4:def 4;
     then Index(p,f) = 1 by A10,Th44;
     then i = 1 by A6,XREAL_1:1;
     hence R_Cut(f,p).i = f.i by A7,A8,FINSEQ_1:57;
    end;
   end;
   hence (R_Cut(f,p)).i = f.i;
  end;
 end;

theorem
 for f be FinSequence of TOP-REAL 2
 for p be Point of TOP-REAL 2 st p in L~f holds
  ( p<>f.1 implies len R_Cut(f,p)=Index(p,f)+1 ) &
  ( p=f.1 implies len R_Cut(f,p)=Index(p,f) )
 proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume A1: p in L~f;
  consider i be Nat such that
   A2: 1 <= i & i+1 <= len f & p in LSeg(f,i) by A1,SPPOL_2:13;
  i<=len f by A2,Th9;
  then A3: 1<=len f by A2,XREAL_1:2;
  A4: 1<=Index(p,f) & Index(p,f)<=len f by A1,Th41;
  now per cases;
   case p<>f.1;
    then R_Cut(f,p)=mid(f,1,Index(p,f))^<*p*> by Def5;
    hence len R_Cut(f,p) = len mid(f,1,Index(p,f))+len <*p*> by FINSEQ_1:35
       .= len mid(f,1,Index(p,f))+1 by FINSEQ_1:56
       .= Index(p,f)-'1+1+1 by A3,A4,Th27
       .= Index(p,f)+1 by A4,BINARITH:53;
   end;
   case A5: p=f.1;
    then A6: R_Cut(f,p)=<*p*> by Def5;
     len f > i by A2,NAT_1:38;
    then len f > 1 by A2,XREAL_1:2;
    then A7: len f >= 1+1 by NAT_1:38;
    f <> {} by A7,FINSEQ_1:25;
    then 1 in dom f by FINSEQ_5:6;
    then A8: p = f/.1 by A5,FINSEQ_4:def 4;
    thus len R_Cut(f,p)= 1 by A6,FINSEQ_1:56
       .= Index(p,f) by A7,A8,Th44;
   end;
  end;
  hence ( p<>f.1 implies len R_Cut(f,p)=Index(p,f)+1 ) &
   ( p=f.1 implies len R_Cut(f,p)=Index(p,f) );
 end;

theorem Th61:
 for f be FinSequence of TOP-REAL 2
 for p be Point of TOP-REAL 2 st p in L~f holds
  ( p=f.(Index(p,f)+1) implies len L_Cut(f,p) = len f-Index(p,f) ) &
  ( p<>f.(Index(p,f)+1) implies len L_Cut(f,p) = len f-Index(p,f)+1 )
 proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume A1: p in L~f;
  consider i be Nat such that
   A2: 1 <= i & i+1 <= len f & p in LSeg(f,i) by A1,SPPOL_2:13;
  i<=len f by A2,Th9;
  then A3: 1<=len f by A2,XREAL_1:2;
  1<=Index(p,f) & Index(p,f)<len f by A1,Th41;
  then A4: 1<Index(p,f)+1 & Index(p,f)+1+0<=len f by NAT_1:38;
  then A5: len f -(Index(p,f)+1) >= 0 by XREAL_1:21;
  now per cases;
   case p<>f.(Index(p,f)+1);
    then L_Cut(f,p)=<*p*>^mid(f,Index(p,f)+1,len f) by Def4;
    hence len L_Cut(f,p) = 1+len mid(f,Index(p,f)+1,len f) by FINSEQ_5:8
       .= len f -'(Index(p,f)+1)+1+1 by A3,A4,Th27
       .= len f -(Index(p,f)+1)+1+1 by A5,BINARITH:def 3
       .= len f-Index(p,f)+1;
   end;
   case p=f.(Index(p,f)+1);
    then A6: L_Cut(f,p)=mid(f,Index(p,f)+1,len f) by Def4;
    thus len L_Cut(f,p) = len f -' (Index(p,f)+1) + 1 by A3,A4,A6,Th27
       .= len f -(Index(p,f)+1)+1 by A5,BINARITH:def 3
       .= len f-Index(p,f);
   end;
  end;
  hence ( p=f.(Index(p,f)+1) implies len L_Cut(f,p)=len f-Index(p,f) ) &
   ( p<>f.(Index(p,f)+1) implies len L_Cut(f,p)=len f-Index(p,f)+1 );
 end;

definition let p1,p2,q1,q2 be Point of TOP-REAL 2;
 pred LE q1,q2,p1,p2 means
 :Def6:q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2) & for r1,r2 being Real st
 0<=r1 & r1<=1 & q1=(1-r1)*p1+r1*p2 &
 0<=r2 & r2<=1 & q2=(1-r2)*p1+r2*p2 holds
 r1<=r2;
end;

definition let p1,p2,q1,q2 be Point of TOP-REAL 2;
 pred LT q1,q2,p1,p2 means
 :Def7:LE q1,q2,p1,p2 & q1<>q2;
end;

theorem
    for p1,p2,q1,q2 being Point of TOP-REAL 2 st
 LE q1,q2,p1,p2 & LE q2,q1,p1,p2 holds q1=q2
proof let p1,p2,q1,q2 be Point of TOP-REAL 2;
 assume A1:LE q1,q2,p1,p2 & LE q2,q1,p1,p2;
 then A2:q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2) & for r1,r2 being Real st
 0<=r1 & r1<=1 & q1=(1-r1)*p1+r1*p2 &
 0<=r2 & r2<=1 & q2=(1-r2)*p1+r2*p2 holds
 r1<=r2 by Def6;
 then consider r1 such that A3: 0<=r1 & r1<=1 & q1 = (1-r1)*p1+r1*p2 by
SPPOL_1:21;
 consider r2 such that A4: 0<=r2 & r2<=1 & q2 = (1-r2)*p1+r2*p2 by A2,
SPPOL_1:21;
 A5:r1<=r2 by A1,A3,A4,Def6;
     r2<=r1 by A1,A3,A4,Def6;
 then r1=r2 by A5,XREAL_1:1;
hence q1=q2 by A3,A4;
end;

theorem Th63:for p1,p2,q1,q2 being Point of TOP-REAL 2 st
 q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2) & p1<>p2
 holds (LE q1,q2,p1,p2 or LT q2,q1,p1,p2)
 & not(LE q1,q2,p1,p2 & LT q2,q1,p1,p2)
proof let p1,p2,q1,q2 be Point of TOP-REAL 2;
 assume A1: q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2)& p1<>p2;
  then consider r1 such that
A2: 0 <= r1 & r1 <= 1 & q1= (1-r1)*p1 + r1*p2 by SPPOL_1:21;
  consider r2 such that
A3: 0 <= r2 & r2 <= 1& q2= (1-r2)*p1 + r2*p2 by A1,SPPOL_1:21;
  A4:now per cases;
  case A5:r1<=r2;
        for s1,s2 being Real st
    0<=s1 & s1<=1 & q1=(1-s1)*p1+s1*p2 &
    0<=s2 & s2<=1 & q2=(1-s2)*p1+s2*p2 holds s1<=s2
    proof let s1,s2 be Real;
     assume A6:  0<=s1 & s1<=1 & q1=(1-s1)*p1+s1*p2 &
      0<=s2 & s2<=1 & q2=(1-s2)*p1+s2*p2;
      then (1-s1)*p1+(s1*p2-s1*p2)=(1-r1)*p1+r1*p2-s1*p2 by A2,EUCLID:49;
      then (1-s1)*p1+(s1*p2-s1*p2)=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:49;
      then (1-s1)*p1+(0.REAL 2)=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:46;
      then (1-s1)*p1=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:31;
      then (1-s1)*p1=(1-r1)*p1+(r1-s1)*p2 by EUCLID:54;
      then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2+((1-r1)*p1-(1-r1)*p1) by EUCLID:49;
      then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2+(0.REAL 2) by EUCLID:46;
      then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2 by EUCLID:31;
      then ((1-s1)-(1-r1))*p1=(r1-s1)*p2 by EUCLID:54;
      then (r1-s1)=0 or p1=p2 by EUCLID:38;
      then A7:r1=s1 by A1;
          (1-s2)*p1+(s2*p2-s2*p2)=(1-r2)*p1+r2*p2-s2*p2 by A3,A6,EUCLID:49;
      then (1-s2)*p1+(s2*p2-s2*p2)=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:49;
      then (1-s2)*p1+(0.REAL 2)=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:46;
      then (1-s2)*p1=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:31;
      then (1-s2)*p1=(1-r2)*p1+(r2-s2)*p2 by EUCLID:54;
      then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2+((1-r2)*p1-(1-r2)*p1) by EUCLID:49;
      then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2+(0.REAL 2) by EUCLID:46;
      then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2 by EUCLID:31;
      then ((1-s2)-(1-r2))*p1=(r2-s2)*p2 by EUCLID:54;
      then (r2-s2)=0 or p1=p2 by EUCLID:38;
     hence s1<=s2 by A1,A5,A7;
    end;
   hence (LE q1,q2,p1,p2 or LT q2,q1,p1,p2) by A1,Def6;
  end;
  case A8:r1>r2;
       for s2,s1 being Real st
    0<=s2 & s2<=1 & q2=(1-s2)*p1+s2*p2 &
    0<=s1 & s1<=1 & q1=(1-s1)*p1+s1*p2 holds s1>=s2
    proof let s2,s1 be Real;
     assume A9:0<=s2 & s2<=1 & q2=(1-s2)*p1+s2*p2 &
      0<=s1 & s1<=1 & q1=(1-s1)*p1+s1*p2;
      then (1-s1)*p1+(s1*p2-s1*p2)=(1-r1)*p1+r1*p2-s1*p2 by A2,EUCLID:49;
      then (1-s1)*p1+(s1*p2-s1*p2)=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:49;
      then (1-s1)*p1+(0.REAL 2)=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:46;
      then (1-s1)*p1=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:31;
      then (1-s1)*p1=(1-r1)*p1+(r1-s1)*p2 by EUCLID:54;
      then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2+((1-r1)*p1-(1-r1)*p1) by EUCLID:49;
      then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2+(0.REAL 2) by EUCLID:46;
      then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2 by EUCLID:31;
      then ((1-s1)-(1-r1))*p1=(r1-s1)*p2 by EUCLID:54;
      then (r1-s1)=0 or p1=p2 by EUCLID:38;
      then A10:r1=s1 by A1;
          (1-s2)*p1+(s2*p2-s2*p2)=(1-r2)*p1+r2*p2-s2*p2 by A3,A9,EUCLID:49;
      then (1-s2)*p1+(s2*p2-s2*p2)=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:49;
      then (1-s2)*p1+(0.REAL 2)=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:46;
      then (1-s2)*p1=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:31
              .=(r2-s2)*p2+(1-r2)*p1 by EUCLID:54;
      then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2+((1-r2)*p1-(1-r2)*p1) by EUCLID:49;
      then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2+(0.REAL 2) by EUCLID:46;
      then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2 by EUCLID:31;
      then ((1-s2)-(1-r2))*p1=(r2-s2)*p2 by EUCLID:54;
      then (r2-s2)=0 or p1=p2 by EUCLID:38;
     hence s1>=s2 by A1,A8,A10;
    end;
   then LE q2,q1,p1,p2 & q1<>q2 by A1,A2,A3,A8,Def6;
   hence (LE q1,q2,p1,p2 or LT q2,q1,p1,p2) by Def7;
  end;
  end;
  now assume A11: LE q1,q2,p1,p2 & LT q2,q1,p1,p2;
   then A12:r1<=r2 by A2,A3,Def6;
      LE q2,q1,p1,p2 & q1<>q2 by A11,Def7;
   then r2<=r1 by A2,A3,Def6;
   then r1=r2 by A12,XREAL_1:1;
  hence contradiction by A2,A3,A11,Def7;
  end;
 hence (LE q1,q2,p1,p2 or LT q2,q1,p1,p2)
 & not(LE q1,q2,p1,p2 & LT q2,q1,p1,p2) by A4;
end;

theorem Th64:
 for f be FinSequence of TOP-REAL 2
 for p,q be Point of TOP-REAL 2 st
   p in L~f & q in L~f & Index(p,f)<Index(q,f) holds
  q in L~L_Cut(f,p)
 proof
  let f be FinSequence of TOP-REAL 2;
  let p,q be Point of TOP-REAL 2;
  assume that
   A1: p in L~f and
   A2: q in L~f and
   A3: Index(p,f)<Index(q,f);
  set i1 = Index(q,f)-'Index(p,f)+1;
  A4: 1<=Index(q,f) & Index(q,f)<len f by A2,Th41;
  A5: Index(p,f)+1<=Index(q,f) by A3,NAT_1:38;
  then A6: Index(p,f)+1-Index(p,f)<=Index(q,f)-Index(p,f) by XREAL_1:11;
  A7: 1<=Index(q,f)-'Index(p,f) by A6,BINARITH:def 3;
  then A8: 1<=Index(q,f)-'Index(p,f)+1 by Th11;
  1+1<=Index(q,f)-'Index(p,f)+1 by A7,XREAL_1:8;
  then A9: 1<Index(q,f)-'Index(p,f)+1 by XREAL_1:2;
  then A10: len <*p*><Index(q,f)-'Index(p,f)+1 by FINSEQ_1:57;
  then A11: len <*p*><Index(q,f)-'Index(p,f)+1+1 by NAT_1:38;
  A12: 1<=Index(q,f)-'Index(p,f)+1 by NAT_1:29;
  A13: 1<=Index(p,f)+1 & Index(p,f)+1<=len f by A4,A5,XREAL_1:2,NAT_1:29;
  1<len f by A4,XREAL_1:2;
  then len mid(f,Index(p,f)+1,len f) = len f -'(Index(p,f)+1)+1 by A13,Th27;
  then A14:len <*p*>+len mid(f,Index(p,f)+1,len f) =
        1+(len f -'(Index(p,f)+1)+1) by FINSEQ_1:57
     .= 1+(len f -(Index(p,f)+1)+1) by A13,BINARITH:50
     .= (len f -Index(p,f))+1;
  A15: Index(q,f)-Index(p,f)<=len f -Index(p,f) by A4,XREAL_1:11;
  then A16: Index(q,f)-Index(p,f)+1<=len f -Index(p,f)+1 by XREAL_1:8;
  then A17:Index(q,f)-'Index(p,f)+1<=len <*p*>+len mid(f,Index(p,f)+1,len f)
                                                       by A3,A14,BINARITH:50;
  Index(q,f)-Index(p,f)<=len f -Index(p,f)-1+1 by A4,XREAL_1:11;
  then A18:Index(q,f)-'Index(p,f)<=len f - (Index(p,f)+1)+1 by A3,BINARITH:50;
  A19: 1<=Index(q,f)-'Index(p,f)+1+1 by NAT_1:29;
  per cases;
   suppose A20: p = f.(Index(p,f)+1);
    then A21: len L_Cut(f,p)=len f -Index(p,f) by A1,Th61;
    then len L_Cut(f,p)>=Index(q,f)-'Index(p,f) by A3,A15,BINARITH:50;
    then (L_Cut(f,p))/.(Index(q,f)-'Index(p,f))
        = L_Cut(f,p).(Index(q,f)-'Index(p,f)) by A7,FINSEQ_4:24
       .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f))
                                                       by A20,Def4
       .= f.(Index(p,f)+1+(Index(q,f)-'Index(p,f))-1)
                                                    by A7,A13,A18,Th31
       .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f))-1) by A3,BINARITH:50
       .= f.Index(q,f);
    then A22: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f))=f/.Index(q,f)
                                                          by A4,FINSEQ_4:24;
    A23: Index(q,f)<len f by A2,Th41;
    then A24: Index(q,f)+1<=len f by NAT_1:38;
    then Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by XREAL_1:11;
    then Index(q,f)-Index(p,f)+1<=len f -Index(p,f);
    then A25:i1<=len L_Cut(f,p) by A6,A21,BINARITH:def 3;
    A26: 1<=Index(q,f) & Index(q,f)+1<=len f & q in LSeg(f,Index(q,f))
                                   by A2,A23,Th41,Th42,NAT_1:38;
    A27: Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by A24,XREAL_1:11;
    then Index(q,f)-Index(p,f)+1<=len f -Index(p,f);
    then A28:len L_Cut(f,p)>=Index(q,f)-'Index(p,f)+1 by A3,A21,BINARITH:50;
    Index(q,f)-Index(p,f)+1<=len f -Index(p,f) by A27;
    then A29: Index(q,f)-'Index(p,f)+1<=len f - (Index(p,f)+1)+1
                                                           by A3,BINARITH:50;
    A30: 1<=Index(q,f)+1 & Index(q,f)+1<=len f by A26,Th11;
    A31: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1)
        = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1) by A9,A28,FINSEQ_4:24
       .= (mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1)
                                                       by A20,Def4
       .= f.(Index(p,f)+1+(Index(q,f)-'Index(p,f)+1)-1)
                                                   by A8,A13,A29,Th31
       .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f)+1)-1) by A3,BINARITH:50
       .= f/.(Index(q,f)+1) by A30,FINSEQ_4:24;
    q in LSeg(f,Index(q,f)) by A2,Th42;
    then q in LSeg((L_Cut(f,p))/.((Index(q,f)-'Index(p,f))),
     (L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1)))
      by A4,A22,A24,A31,TOPREAL1:def 5;
    hence q in L~(L_Cut(f,p)) by A7,A25,SPPOL_2:15;
   end;
   suppose that
     A32: p <> f.(Index(p,f)+1);
     A33: len L_Cut(f,p)=len f -Index(p,f)+1 by A1,A32,Th61;
    then len L_Cut(f,p)>=Index(q,f)-'Index(p,f)+1 by A3,A16,BINARITH:50;
    then (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1)
        = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1) by A12,FINSEQ_4:24
       .= (<*p*>^mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1)
                                                       by A32,Def4
       .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1-len <*p*>)
                                                       by A10,A17,Th15
       .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1-1)
                                                              by FINSEQ_1:57
       .= f.(Index(p,f)+1+(Index(q,f)-'Index(p,f))-1)
                                                    by A7,A13,A18,Th31
       .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f))-1) by A3,BINARITH:50
       .= f.Index(q,f);
    then A34:(L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1)=f/.Index(q,f)
                                                          by A4,FINSEQ_4:24;
    A35: Index(q,f)<len f by A2,Th41;
    then A36: Index(q,f)+1<=len f by NAT_1:38;
    then Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by XREAL_1:11;
    then Index(q,f)-Index(p,f)+1<=len f -Index(p,f);
    then i1<=len f -Index(p,f) by A6,BINARITH:def 3;
    then A37:i1+1<=len L_Cut(f,p) by A33,XREAL_1:8;
    A38: 1<=Index(q,f) & Index(q,f)+1<=len f & q in LSeg(f,Index(q,f))
                                   by A2,A35,Th41,Th42,NAT_1:38;
    A39: Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by A36,XREAL_1:11;
    then A40: Index(q,f)-Index(p,f)+1+1<=len f -Index(p,f)+1 by XREAL_1:8;
    then A41: len L_Cut(f,p)>=Index(q,f)-'Index(p,f)+1+1
                                                       by A3,A33,BINARITH:50;
    A42: Index(q,f)-'Index(p,f)+1+1<=len <*p*>+len mid(f,Index(p,f)+1,len f)
                                                   by A3,A14,A40,BINARITH:50;
    Index(q,f)-Index(p,f)+1<=len f -Index(p,f) by A39;
    then A43: Index(q,f)-'Index(p,f)+1<=len f - (Index(p,f)+1)+1
                                                           by A3,BINARITH:50;
    A44: 1<=Index(q,f)+1 & Index(q,f)+1<=len f by A38,Th11;
    A45: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1+1)
        = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1+1) by A19,A41,FINSEQ_4:24
       .= (<*p*>^mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1+1)
                                                       by A32,Def4
       .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1+1-len <*p*>)
                                                       by A11,A42,Th15
       .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1+1-1)
                                                              by FINSEQ_1:57
       .= f.(Index(p,f)+1+(Index(q,f)-'Index(p,f)+1)-1)
                                                   by A8,A13,A43,Th31
       .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f)+1)-1) by A3,BINARITH:50
       .= f/.(Index(q,f)+1) by A44,FINSEQ_4:24;
    q in LSeg(f,Index(q,f)) by A2,Th42;
    then q in LSeg((L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1)),
     (L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1+1)))
      by A4,A34,A36,A45,TOPREAL1:def 5;
    hence q in L~(L_Cut(f,p)) by A12,A37,SPPOL_2:15;
   end;
 end;

theorem Th65:
 for p,q,p1,p2 being Point of TOP-REAL 2 st
  LE p,q,p1,p2 holds q in LSeg(p,p2) & p in LSeg(p1,q)
proof let p,q,p1,p2 be Point of TOP-REAL 2;
 assume A1: LE p,q,p1,p2;
  then A2:p in LSeg(p1,p2) & q in LSeg(p1,p2) & for r1,r2 being Real st
  0<=r1 & r1<=1 & p=(1-r1)*p1+r1*p2 &
  0<=r2 & r2<=1 & q=(1-r2)*p1+r2*p2 holds
  r1<=r2 by Def6;
  then consider s1 being Real such that
  A3:0<=s1 & s1<=1 & p=(1-s1)*p1+s1*p2 by SPPOL_1:21;
  consider s2 being Real such that
  A4:0<=s2 & s2<=1 & q=(1-s2)*p1+s2*p2 by A2,SPPOL_1:21;
  A5:s1<=s2 by A1,A3,A4,Def6;
  A6: 1-s1>=0 by A3,XREAL_1:50;
  A7:now per cases;
  case A8:1-s1<>0;
   A9:s2-s1>=0 by A5,XREAL_1:50;
   set s=(s2-s1)/(1-s1);
   A10:s>=0 by A6,A9,REAL_2:125;
   A11:(1-s1)*(((1-s2)/(1-s1)))=1-s2 by A8,XCMPLX_1:88;
   A12:(1-s1)*(((s2-s1)/(1-s1)))=s2-s1 by A8,XCMPLX_1:88;
   A13:(1-(s2-s1)/(1-s1))=(1*(1-s1)-(s2-s1))/(1-s1) by A8,XCMPLX_1:128
   .=((1-s1)+s1-s2)/(1-s1);
       1-s1>=s2-s1 by A4,XREAL_1:11;
   then (1-s1)/(1-s1)>=(s2-s1)/(1-s1) by A6,A8,REAL_1:73;
   then A14:1>=s by A8,XCMPLX_1:60;
       (1-s1)*((1-s)*p+s*p2)
               =(1-s1)*(((1-s2)/(1-s1))*((1-s1)*p1+s1*p2))
                  +(1-s1)*(((s2-s1)/(1-s1))*p2) by A3,A13,EUCLID:36
              .=(1-s1)*(((1-s2)/(1-s1)))*((1-s1)*p1+s1*p2)
                  +(1-s1)*(((s2-s1)/(1-s1))*p2) by EUCLID:34
              .=(1-s2)*((1-s1)*p1+s1*p2)
                  +(1-s1)*(((s2-s1)/(1-s1)))*p2 by A11,EUCLID:34
              .=(1-s2)*((1-s1)*p1)+(1-s2)*(s1*p2)
                  +(s2-s1)*p2 by A12,EUCLID:36
              .=(1-s2)*(1-s1)*p1+(1-s2)*(s1*p2)
                  +(s2-s1)*p2 by EUCLID:34
              .=(1-s2)*(1-s1)*p1+(1-s2)*s1*p2
                  +(s2-s1)*p2 by EUCLID:34
              .=(1-s2)*(1-s1)*p1+((1-s2)*s1*p2
                  +(s2-s1)*p2) by EUCLID:30
              .=(1-s2)*(1-s1)*p1+(1*s1-s2*s1+(s2-s1))*p2 by EUCLID:37
              .=(1-s1)*((1-s2)*p1)+(1-s1)*s2*p2 by EUCLID:34
              .=(1-s1)*((1-s2)*p1)+(1-s1)*(s2*p2) by EUCLID:34
              .=(1-s1)*q by A4,EUCLID:36;
             then q=(1-s)*p+s*p2 by A8,EUCLID:38;
    hence q in LSeg(p,p2) by A10,A14,SPPOL_1:22;
  end;
  case 1-s1=0;
    then 1=0+s1 .=s1;
    then s2=1 by A4,A5,XREAL_1:1;
    then q =0.REAL 2 + 1*p2 by A4,EUCLID:33
    .=0.REAL 2 + p2 by EUCLID:33
    .=p2 by EUCLID:31;
    hence q in LSeg(p,p2) by TOPREAL1:6;
  end;
  end;
      now per cases;
  case A15:s2<>0;
   set s=s1/s2;
   A16:s>=0 by A3,A4,REAL_2:125;
   A17:s2*(((s2-s1)/s2))=s2-s1 by A15,XCMPLX_1:88;
   A18: s2*((s1)/s2)=s1 by A15,XCMPLX_1:88;
   A19:(s2-s1)+s1*(1-s2)=s2*(1-s1);
       s2/s2>=s1/s2 by A4,A5,A15,REAL_1:73;
   then A20:1>=s by A15,XCMPLX_1:60;
       s2*((1-s)*p1+s*q)
               =s2*(((1*s2-s1)/s2)*p1
                  +(s1/s2)*((1-s2)*p1+s2*p2)) by A4,A15,XCMPLX_1:128
              .=s2*(((s2-s1)/s2)*p1)
                  +s2*((s1/s2)*((1-s2)*p1+s2*p2)) by EUCLID:36
              .=s2*(((s2-s1)/s2))*p1
                  +s2*((s1/s2)*((1-s2)*p1+s2*p2)) by EUCLID:34
              .=(s2-s1)*p1 +s2*(s1/s2)*((1-s2)*p1+s2*p2) by A17,EUCLID:34
              .=(s2-s1)*p1 +(s1*((1-s2)*p1)+s1*(s2*p2)) by A18,EUCLID:36
              .=(s2-s1)*p1 +(s1*(1-s2)*p1+s1*(s2*p2)) by EUCLID:34
              .=(s2-s1)*p1 +(s1*(1-s2)*p1+s1*s2*p2) by EUCLID:34
              .=(s2-s1)*p1 +s1*(1-s2)*p1+s1*s2*p2 by EUCLID:30
              .=((s2-s1)+s1*(1-s2))*p1+s1*s2*p2 by EUCLID:37
              .=s2*((1-s1)*p1)+s2*s1*p2 by A19,EUCLID:34
              .=s2*((1-s1)*p1)+s2*(s1*p2) by EUCLID:34
              .=s2*p by A3,EUCLID:36;
             then p=(1-s)*p1+s*q by A15,EUCLID:38;
    hence p in LSeg(p1,q) by A16,A20,SPPOL_1:22;
  end;
  case s2=0;
    then s1=0 by A1,A3,A4,Def6;
    then p =1*p1+(0.REAL 2) by A3,EUCLID:33
    .=p1+(0.REAL 2) by EUCLID:33
    .=p1 by EUCLID:31;
    hence p in LSeg(p1,q) by TOPREAL1:6;
  end;
  end;
 hence q in LSeg(p,p2) & p in LSeg(p1,q) by A7;
end;

theorem Th66:
 for f be FinSequence of TOP-REAL 2
 for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f & p<>q &
   Index(p,f) = Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1) holds
  q in L~L_Cut(f,p)
 proof
  let f be FinSequence of TOP-REAL 2;
  let p,q be Point of TOP-REAL 2;
  assume that
   A1: p in L~f and
   A2: q in L~f and
   A3: p<>q and
   A4: Index(p,f)=Index(q,f) and
   A5: LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
  set i1=Index(q,f)-'Index(p,f)+1;
  A6: 1<=Index(q,f) & Index(q,f)<len f by A2,Th41;
  Index(q,f)<len f by A2,Th41;
  then A7: Index(q,f)+1<=len f by NAT_1:38;
  A8: Index(q,f)-'Index(p,f)=Index(q,f)-Index(p,f) by A4,BINARITH:50
     .=0 by A4;
  1<0+1+1;
  then A9: len <*p*> < Index(q,f)-'Index(p,f)+1+1 by A8,FINSEQ_1:57;
  A10: 1<=Index(p,f)+1 by NAT_1:29;
  1<len f by A6,XREAL_1:2;
  then len mid(f,Index(p,f)+1,len f) = len f -'(Index(p,f)+1)+1
                                                    by A4,A7,A10,Th27;
  then A11: len <*p*> + len mid(f,Index(p,f)+1,len f) =
        1+(len f -'(Index(p,f)+1)+1) by FINSEQ_1:57
     .= 1+(len f -(Index(p,f)+1)+1) by A4,A7,BINARITH:50
     .= (len f -Index(p,f))+1;
  Index(q,f)-Index(p,f)<=len f -Index(p,f) by A6,XREAL_1:11;
  then A12: Index(q,f)-Index(p,f)+1<=len f -Index(p,f)+1 by XREAL_1:8;
  A13: now assume A14: p = f.(Index(p,f)+1);
   then A15: p = f/.(Index(p,f)+1) by A4,A7,A10,FINSEQ_4:24;
   q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A5,Def6;
   then consider r be Real such that
    A16: 0 <= r & r <= 1 and
    A17: q = (1-r)*f/.(Index(p,f))+r*f/.(Index(p,f)+1) by SPPOL_1:21;
   A18: p = 1 * p by EUCLID:33
      .= 0.REAL 2 + 1 * p by EUCLID:31
      .= (1-1)*f/.(Index(p,f))+1 * p by EUCLID:33;
   then 1<=r by A5,A15,A16,A17,Def6;
   then r = 1 by A16,XREAL_1:1;
   hence contradiction by A3,A4,A7,A10,A14,A17,A18,FINSEQ_4:24;
  end;
  A19: len L_Cut(f,p)=len f -Index(p,f)+1 by A1,A13,Th61;
  then A20: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1)
      = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1)
         by A4,A8,A12,FINSEQ_4:24
     .= (<*p*>^mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1)
                                                       by A13,Def4
     .= p by A8,FINSEQ_1:58;
  A21: Index(q,f)<len f by A2,Th41;
  then A22:Index(q,f)+1<=len f by NAT_1:38;
  then Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by XREAL_1:11;
  then Index(q,f)-Index(p,f)+1<=len f -Index(p,f);
  then i1<=len f -Index(p,f) by A4,BINARITH:def 3;
  then A23: i1+1<=len L_Cut(f,p) by A19,XREAL_1:8;
  A24: 1<=Index(q,f) & Index(q,f)+1<=len f & q in LSeg(f,Index(q,f))
                                   by A2,A21,Th41,Th42,NAT_1:38;
  A25: Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by A22,XREAL_1:11;
  then A26: Index(q,f)-Index(p,f)+1+1<=len f -Index(p,f)+1 by XREAL_1:8;
  A27: Index(q,f)-'Index(p,f)+1+1<=len <*p*>+len mid(f,Index(p,f)+1,len f)
                                                   by A4,A11,A26,BINARITH:50;
  Index(q,f)-Index(p,f)+1<=len f -Index(p,f) by A25;
  then A28: Index(q,f)-'Index(p,f)+1<=len f - (Index(p,f)+1)+1
                                                           by A4,BINARITH:50;
  A29: 1<=Index(q,f)+1 & Index(q,f)+1<=len f by A24,Th11;
  (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1+1)
      = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1+1)
         by A4,A8,A19,A26,FINSEQ_4:24
     .= (<*p*>^mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1+1)
                                                       by A13,Def4
     .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1+1-len <*p*>)
                                                       by A9,A27,Th15
     .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1+1-1)
                                                              by FINSEQ_1:57
     .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f)+1)-1)
        by A4,A7,A8,A10,A28,Th31
     .= f/.(Index(q,f)+1) by A29,FINSEQ_4:24;
  then q in LSeg((L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1)),
   (L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1+1))) by A4,A5,A20,Th65;
  hence q in L~(L_Cut(f,p)) by A8,A23,SPPOL_2:15;
 end;

begin
::--------------------------------------------------------:
::  Cutting Both Sides of a Finite Sequence and  :
::  a Discussion of Speciality of Sequences in TOP-REAL 2 :
::--------------------------------------------------------:

definition let f be FinSequence of TOP-REAL 2,p,q be Point of TOP-REAL 2;
 func B_Cut(f,p,q) -> FinSequence of TOP-REAL 2 equals
 :Def8:
   R_Cut(L_Cut(f,p),q) if p in L~f & q in L~f & Index(p,f)<Index(q,f)
                         or Index(p,f)=Index(q,f)
                         & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1)
   otherwise Rev (R_Cut(L_Cut(f,q),p));
 correctness;
end;

theorem Th67:for f being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2
  st f is_S-Seq & p in L~f & p<>f.1
 holds R_Cut(f,p) is_S-Seq_joining f/.1,p
proof let f be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2;
 assume that
A1: f is_S-Seq and
A2: p in L~f and
A3: p<>f.1;
      R_Cut(f,p)=mid(f,1,Index(p,f))^<*p*> by A3,Def5;
 hence thesis by A1,A2,A3,Th52;
end;

theorem Th68:
 for f being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2 st f is_S-Seq & p in L~f &
 p<>f.len f holds L_Cut(f,p) is_S-Seq_joining p,f/.len f
proof let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
 assume that
A1: f is_S-Seq & p in L~f and
A2: p<>f.len f;
    len f <> 0 by A1,TOPREAL1:28; then
A3: f <> {} by FINSEQ_1:25;
A4: Rev f is_S-Seq & p in L~Rev f by A1,SPPOL_2:22,47;
A5:  L_Cut(f,p) = L_Cut(Rev Rev f,p) by FINSEQ_6:29
     .= Rev R_Cut(Rev f,p) by A4,Th57;
       p <> (Rev f).1 by A2,FINSEQ_5:65;
   then R_Cut(Rev f,p) is_S-Seq_joining (Rev f)/.1,p by A4,Th67;
   then L_Cut(f,p) is_S-Seq_joining p,(Rev f)/.1 by A5,Th48;
  hence thesis by A3,FINSEQ_5:68;
end;

theorem Th69:for f being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2 st f is_S-Seq & p in L~f &
 p<>f.len f holds L_Cut(f,p) is_S-Seq
proof let f be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2;
 assume f is_S-Seq & p in L~f & p<>f.len f;
 then L_Cut(f,p) is_S-Seq_joining p,f/.len f by Th68;
 hence L_Cut(f,p) is_S-Seq by Def3;
end;

theorem Th70:for f being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2 st f is_S-Seq & p in L~f &
 p<>f.1 holds R_Cut(f,p) is_S-Seq
proof let f be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2;
 assume f is_S-Seq & p in L~f &
 p<>f.1;
 then R_Cut(f,p) is_S-Seq_joining f/.1,p by Th67;
 hence R_Cut(f,p) is_S-Seq by Def3;
end;

Lm1:
for f being FinSequence of TOP-REAL 2,
 p,q being Point of TOP-REAL 2 st f is_S-Seq & p in L~f & q in L~f &
 p<>q & (Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
    & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1))
  holds B_Cut(f,p,q) is_S-Seq_joining p,q
proof let f be FinSequence of TOP-REAL 2,
 p,q be Point of TOP-REAL 2;
 assume A1: f is_S-Seq & p in L~f & q in L~f & p<>q;
 assume A2: Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
    & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
    then A3:B_Cut(f,p,q)=R_Cut(L_Cut(f,p),q) by A1,Def8;
  A4:1<=Index(q,f) & Index(q,f)<len f by A1,Th41;
      Index(p,f) < len f by A1,Th41;
   then A5: Index(p,f)+1 <= len f by NAT_1:38;
  A6:1<=Index(p,f)+1 by NAT_1:29;
A7: 1<len f by A4,XREAL_1:2;
A8:  now per cases by A2;
    case Index(p,f)<Index(q,f);
    then A9:Index(p,f)+1<=Index(q,f) by NAT_1:38;
     assume p=f.len f;
     then len f <= Index(q,f) by A1,A7,A9,Th45;
     hence contradiction by A1,Th41;
    end;
    case
A10:  Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
A11: now assume A12: p = f.(Index(p,f)+1);
    then A13:    p = f/.(Index(p,f)+1) by A5,A6,FINSEQ_4:24;
         q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A10,Def6;
     then consider r being Real such that
A14:   0 <= r & r <= 1 and
A15:   q = (1-r)*f/.(Index(p,f))+r*f/.(Index(p,f)+1) by SPPOL_1:21;
A16:    p = 1*p by EUCLID:33
        .= 0.REAL 2 + 1*p by EUCLID:31
        .= (1-1)*f/.(Index(p,f))+1*p by EUCLID:33;
     then 1<=r by A10,A13,A14,A15,Def6;
     then r = 1 by A14,XREAL_1:1;
   hence contradiction by A1,A5,A6,A12,A15,A16,FINSEQ_4:24;
  end;
    assume p=f.len f;
    hence contradiction by A1,A7,A11,Th45;
   end;
   end;
    then A17:L_Cut(f,p) is_S-Seq_joining p,f/.len f by A1,Th68;
    A18:L_Cut(f,p) is_S-Seq by A1,A8,Th69;
        now per cases by A2;
    case Index(p,f)<Index(q,f);
     then q in L~(L_Cut(f,p)) by A1,Th64;
     hence ex i1 being Nat st 1<=i1 & i1+1<=len L_Cut(f,p)
        & q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
    end;
    case Index(p,f)=Index(q,f)
        & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
     then q in L~(L_Cut(f,p)) by A1,Th66;
     hence ex i1 being Nat st 1<=i1 & i1+1<=len L_Cut(f,p)
        & q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
    end;
    end;
    then A19:   q in L~L_Cut(f,p) by SPPOL_2:17;
    then 1<=Index(q,L_Cut(f,p)) & Index(q,L_Cut(f,p))<len L_Cut(f,p) by Th41;
    then A20: 1<=len L_Cut(f,p) by XREAL_1:2;
    A21:(L_Cut(f,p)).1=p by A17,Def3;
    then p=(L_Cut(f,p))/.(1) by A20,FINSEQ_4:24;
   hence B_Cut(f,p,q) is_S-Seq_joining p,q by A1,A3,A18,A19,A21,Th67;
end;

theorem Th71:
for f being FinSequence of TOP-REAL 2,
 p,q being Point of TOP-REAL 2 st f is_S-Seq & p in L~f & q in L~f &
 p<>q holds B_Cut(f,p,q) is_S-Seq_joining p,q
proof let f be FinSequence of TOP-REAL 2,
 p,q be Point of TOP-REAL 2;
 assume A1: f is_S-Seq & p in L~f & q in L~f & p<>q;
 per cases;
  suppose Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
    & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
  hence thesis by A1,Lm1;
  end;
  suppose A2:not(Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
    & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
    then A3:B_Cut(f,p,q)=Rev R_Cut(L_Cut(f,q),p) by Def8;
A4:  Index(q,f) < Index(p,f)
    or Index(p,f)=Index(q,f) & not LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1)
       by A2,XREAL_1:1;
A5:  now assume that
A6:   Index(p,f)=Index(q,f) and
A7:   not LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
A8: 1 <= Index(p,f) by A1,Th41;
A9:  Index(p,f) < len f by A1,Th41;
      then A10:   Index(p,f)+1 <= len f by NAT_1:38;
      then A11:   LSeg(f,Index(p,f)) = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
                    by A8,TOPREAL1:def 5;
      then A12:   p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A1,Th42;
A13:   q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A1,A6,A11,Th42;
A14:   Index(p,f) in dom f by A8,A9,FINSEQ_3:27;
          1 <= Index(p,f)+1 by NAT_1:29;
      then A15:   Index(p,f)+1 in dom f by A10,FINSEQ_3:27;
A16:   Index(p,f)+0 <> Index(p,f)+1;
          f is one-to-one by A1,TOPREAL1:def 10;
      then f/.(Index(p,f))<>f/.(Index(p,f)+1) by A14,A15,A16,PARTFUN2:17;
      then LT q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A7,A12,A13,Th63;
     hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A6,Def7;
    end;
   then A17:  B_Cut(f,q,p) is_S-Seq_joining q,p by A1,A4,Lm1;
       Rev B_Cut(f,q,p) = B_Cut(f,p,q) by A1,A3,A4,A5,Def8;
  hence thesis by A17,Th48;
end;
end;

theorem   for f being FinSequence of TOP-REAL 2,
 p,q being Point of TOP-REAL 2 st f is_S-Seq & p in L~f & q in L~f &
 p<>q holds B_Cut(f,p,q) is_S-Seq
proof let f be FinSequence of TOP-REAL 2,
 p,q be Point of TOP-REAL 2;
 assume f is_S-Seq & p in L~f & q in L~f &
 p<>q;
 then B_Cut(f,p,q) is_S-Seq_joining p,q by Th71;
 hence B_Cut(f,p,q) is_S-Seq by Def3;
end;

theorem Th73:for f,g being FinSequence of TOP-REAL 2 st f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1}
 holds f^mid(g,2,len g) is_S-Seq
proof let f,g be FinSequence of TOP-REAL 2;
 assume A1: f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1};
   then A2:f is one-to-one & len f >= 2
   & f is unfolded s.n.c. special by TOPREAL1:def 10;
   A3:g is one-to-one & len g >= 2
   & g is unfolded s.n.c. special by A1,TOPREAL1:def 10;
   A4:1<=len f by A2,XREAL_1:2;
   A5:1<=len g by A3,XREAL_1:2;
   A6:len (f^mid(g,2,len g))=len f + len mid(g,2,len g) by FINSEQ_1:35;
   then A7: len f<=len f + len mid(g,2,len g) &
       len f<=len (f^mid(g,2,len g)) by NAT_1:29;
   A8:len mid(g,2,len g)=len g -'2+1 by A3,A5,Th27
                     .=len g -2+1 by A3,BINARITH:50
                     .=len g -1;
   A9: for x1,x2 being set st
   x1 in dom (f^mid(g,2,len g)) & x2 in dom (f^mid(g,2,len g))
    & (f^mid(g,2,len g)).x1=(f^mid(g,2,len g)).x2 holds x1=x2
   proof let x1,x2 be set;
    assume A10: x1 in dom (f^mid(g,2,len g)) & x2 in dom (f^mid(g,2,len g))
     & (f^mid(g,2,len g)).x1=(f^mid(g,2,len g)).x2;
     then A11: x1 in Seg len (f^mid(g,2,len g)) & x2 in Seg len (f^mid(g,2,len
g))
                                    by FINSEQ_1:def 3;
     reconsider n1=x1,n2=x2 as Nat by A10;
     A12:1<=n1 & n1<=len (f^mid(g,2,len g))
     & 1<=n2 & n2<=len (f^mid(g,2,len g)) by A11,FINSEQ_1:3;
     then A13: n1-len f <=len f + len mid(g,2,len g)-len f by A6,XREAL_1:11;
        A14: n2-len f <=len f + len mid(g,2,len g)-len f by A6,A12,XREAL_1:11;
     A15:rng (f^mid(g,2,len g)) c= the carrier of TOP-REAL 2
                                     by FINSEQ_1:def 4;
         (f^mid(g,2,len g)).x1 in rng (f^mid(g,2,len g)) by A10,FUNCT_1:def 5;
     then reconsider q=(f^mid(g,2,len g)).x1 as Point of TOP-REAL 2 by A15;
     A16:rng f c= L~f by A2,SPPOL_2:18;
     A17:rng mid(g,2,len g) c= rng g by Th28;
     A18:rng g c=L~g by A3,SPPOL_2:18;
     A19:now assume A20:q in rng f & q in rng mid(g,2,len g);
       then q in rng g by A17;
       then A21: q in L~f /\ L~g by A16,A18,A20,XBOOLE_0:def 3;
       now assume A22:g.1 in rng mid(g,2,len g);
         2-'1=2-1 by BINARITH:50;
         then A23:g.1 in rng (g/^1) by A3,A22,Th26;
         A24:g|1=g|Seg 1 by FINSEQ_1:def 15;
             1 in Seg 1 by FINSEQ_1:5;
         then A25:(g|1).1=g.1 by A24,FUNCT_1:72;
         len (g|1)=1 by A5,FINSEQ_1:80;
         then 1 in dom (g|1) by FINSEQ_3:27;
         then A26:g.1 in rng (g|1) by A25,FUNCT_1:def 5;
         rng(g|1) misses rng (g/^1) by A3,FINSEQ_5:37;
        hence contradiction by A23,A26,XBOOLE_0:3;
       end;
      hence contradiction by A1,A20,A21,TARSKI:def 1;
     end;
         now per cases;
     case n1<=len f;
       then A27:n1 in dom f by A12,FINSEQ_3:27;
       then A28:(f^mid(g,2,len g)).x1
         =f.n1 by FINSEQ_1:def 7;
           now per cases;
       case n2<=len f;
         then A29:n2 in dom f by A12,FINSEQ_3:27;
         then (f^mid(g,2,len g)).x2
         =f.n2 by FINSEQ_1:def 7;
        hence x1=x2 by A2,A10,A27,A28,A29,FUNCT_1:def 8;
       end;
       case A30:n2>len f; then len f +1<=n2 by NAT_1:38;
         then len f +1 -len f <=n2 - len f by XREAL_1:11;
         then 1<=(n2-'len f) & (n2-'len f)<=len mid(g,2,len g)
            by A14,Th1;
         then A31:(n2-'len f) in dom mid(g,2,len g) by FINSEQ_3:27;
         then A32:(f^mid(g,2,len g)).(len f +(n2-'len f))
         =mid(g,2,len g).(n2-'len f) by FINSEQ_1:def 7;
             len f + (n2-'len f)=len f+(n2-len f) by A30,BINARITH:50
         .=n2;
        hence contradiction by A10,A19,A27,A28,A31,A32,FUNCT_1:def 5;
       end;
       end;
      hence x1=x2;
     end;
     case A33:n1>len f; then len f +1<=n1 by NAT_1:38;
         then A34: len f +1 -len f <=n1 - len f by XREAL_1:11;
         then A35:1<=n1-'len f by Th1;
         A36:n1-'len f<=len mid(g,2,len g) by A13,A34,Th1;
         then A37:n1-'len f in dom mid(g,2,len g) by A35,FINSEQ_3:27;
         then A38:(f^mid(g,2,len g)).(len f +(n1-'len f))
         =mid(g,2,len g).(n1-'len f) by FINSEQ_1:def 7;
             n1-'len f<=n1-'len f +2 by NAT_1:29;
         then 1<=n1-'len f +2 by A35,XREAL_1:2;
         then A39:n1-'len f +2-'1=n1-'len f +2-1 by BINARITH:50
         .=n1-'len f +((1+1)-1);
         A40:1<=n1-'len f+1 by A35,Th11;
             n1-'len f+1<=len g -1+1 by A8,A36,XREAL_1:8;
         then A41:n1-'len f+1 in dom g by A40,FINSEQ_3:27;
             len f + (n1-'len f)=len f+(n1-len f) by A33,BINARITH:50
         .=n1;
         then A42:(f^mid(g,2,len g)).n1
             =g.(n1-'len f+1) by A3,A5,A13,A34,A38,A39,Th27;
A43:     len f + (n1-'len f)=len f+(n1-len f) by A33,BINARITH:50
         .=n1;
           now per cases;
       case n2<=len f;
         then A44:n2 in dom f by A12,FINSEQ_3:27;
         then (f^mid(g,2,len g)).x2
          =f.n2 by FINSEQ_1:def 7;
        hence contradiction by A10,A19,A37,A38,A43,A44,FUNCT_1:def 5;
       end;
       case A45:n2>len f; then len f +1<=n2 by NAT_1:38;
         then A46: len f +1 -len f <=n2 - len f by XREAL_1:11;
         then A47:1<=n2-'len f by Th1;
         A48:1<=(n2-'len f) & (n2-'len f)<=len mid(g,2,len g)
                                by A14,A46,Th1;
         then (n2-'len f) in dom mid(g,2,len g) by FINSEQ_3:27;
         then A49:(f^mid(g,2,len g)).(len f +(n2-'len f))
         =mid(g,2,len g).(n2-'len f) by FINSEQ_1:def 7;
             n2-'len f<=n2-'len f +2 by NAT_1:29;
         then 1<=n2-'len f +2 by A47,XREAL_1:2;
         then A50:n2-'len f +2-'1=n2-'len f +2-1 by BINARITH:50
         .=n2-'len f+1;
         A51:1<=n2-'len f+1 by A47,Th11;
             n2-'len f+1<=len g -1+1 by A8,A48,XREAL_1:8;
         then A52:n2-'len f+1 in dom g by A51,FINSEQ_3:27;
             len f + (n2-'len f)=len f+(n2-len f) by A45,BINARITH:50
         .=n2;
         then (f^mid(g,2,len g)).n2
             =g.(n2-'len f+1) by A3,A5,A14,A46,A49,A50,Th27;
          then n1-'len f+1=n2-'len f+1 by A3,A10,A41,A42,A52,FUNCT_1:def 8;
          then n1-'len f=n2-'len f+1-1;
          then n1-len f=n2-'len f by A33,BINARITH:50;
          then n1-len f=n2-len f by A45,BINARITH:50;
        hence x1=x2;
       end;
       end;
      hence x1=x2;
     end;
     end;
    hence x1=x2;
   end;
   A53: for i st 1 <= i & i + 2 <= len (f^mid(g,2,len g))
       holds LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
       = {(f^mid(g,2,len g))/.(i+1)}
    proof let i be Nat;
     assume A54:1 <= i & i + 2 <= len (f^mid(g,2,len g));
            then A55:1<=i+1 by Th11;
             A56:1<=i+1+1 by A54,Th11;
            A57:i+1<=len (f^mid(g,2,len g)) by A54,Th10;
             A58:i<=len (f^mid(g,2,len g)) by A54,Th10;
              i+1+1<=len f + len mid(g,2,len g) by A54,FINSEQ_1:35;
             then i+1+1<=len f + (len g -'2+1) by A3,A5,Th27;
             then i+1+1<=len f + (len g -(1+1)+1) by A3,BINARITH:50;
             then A59: i+1+1-len f<=len f + (len g -(1+1)+1)-len f by
XREAL_1:11
;
             then A60: i+1-len f+1+1<=len g -1+1 by XREAL_1:8;
             then A61:i-len f+1+1+1<=len g;
          now per cases;
      case A62:i+2<=len f;
            then i+1+1<=len f;
            then A63:i+1<=len f by Th9;
            then A64:i<=len f by Th9;
           A65:(f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i
                             by A54,A58,FINSEQ_4:24;
              f/.i=f.i by A54,A64,FINSEQ_4:24;
          then A66:(f^mid(g,2,len g))/.i=f/.i by A54,A64,A65,FINSEQ_1:85;
           A67:(f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                                               by A55,A57,FINSEQ_4:24;
              f/.(i+1)=f.(i+1) by A55,A63,FINSEQ_4:24;
          then A68:(f^mid(g,2,len g))/.(i+1)=f/.(i+1)
            by A55,A63,A67,FINSEQ_1:85;
            LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               = LSeg(f^mid(g,2,len g),i) by A54,A57,TOPREAL1:def 5;
        then A69:LSeg(f^mid(g,2,len g),i)=LSeg(f,i)
                  by A54,A63,A66,A68,TOPREAL1:def 5;
           A70:(f^mid(g,2,len g))/.(i+1+1)=(f^mid(g,2,len g)).(i+1+1)
                                      by A54,A56,FINSEQ_4:24;
              f/.(i+1+1)=f.(i+1+1) by A56,A62,FINSEQ_4:24;
         then A71:LSeg((f^mid(g,2,len g))/.(i+1),(f^mid(g,2,len g))/.(i+1+1))
               =LSeg(f/.(i+1),f/.(i+1+1)) by A56,A62,A68,A70,FINSEQ_1:85;
             LSeg((f^mid(g,2,len g))/.(i+1),(f^mid(g,2,len g))/.(i+1+1))
               = LSeg(f^mid(g,2,len g),i+1) by A54,A55,TOPREAL1:def 5;
        then LSeg(f^mid(g,2,len g),i+1)=LSeg(f,i+1)
                     by A55,A62,A71,TOPREAL1:def 5;
       hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
        = {(f^mid(g,2,len g))/.(i+1)} by A2,A54,A62,A68,A69,TOPREAL1:def 8;
      end;
      case i+2>len f;
then A72:    i+2>=len f +1 by NAT_1:38;
            now per cases by A72,REAL_1:def 5;
        case A73: i+2>len f +1;
then A74:        i+1+1>len f+1;
             i+1+1>=len f+1+1 by A73,NAT_1:38;
            then i+1+1-(len f+1)>=len f+1+1-(len f+1) by XREAL_1:11;
            then A75:i-len f+1>=1;
             A76: i+1>=len f +1 by A74,NAT_1:38;
             then A77:i>=len f by XREAL_1:8;
             A78:i+1>=len f by A76,Th11;
              A79:i-'len f+1+1+1<=len g by A61,A77,BINARITH:50;
              then A80:i-'len f+1+1<=len g by Th9;
              then A81:i-'len f+1<=len g by Th9;
                  i-'len f+1+1-1<=len g -1 by A80,XREAL_1:11;
              then i-'len f+1<=len g -2+1;
              then A82:i-'len f+1<=len g -'2+1 by A3,BINARITH:50;
              then A83:i-'len f<=len g -'2+1 by Th9;
              A84:i+1-'len f+1+1<=len g by A60,A78,BINARITH:50;
                  i-len f+1<=len g -'2+1
                         by A77,A82,BINARITH:50;
              then i+1-len f<=len g -'2+1;
              then A85:i+1-'len f<=len g -'2+1 by A78,BINARITH:50;
             A86:i-'len f+1>=1 by A75,A77,BINARITH:50;
             then A87:i-'len f+1+1>=1 by Th11;
             then A88: i-len f+1+1>=1 by A77,BINARITH:50;
             then i+1-len f+1>=1;
             then A89:i+1-'len f+1>=1 by A78,BINARITH:50;
             then A90:i+1-'len f+1+1>=1 by Th11;
             A91:i+1+1-len f>=0 by A88;
              i+1-len f+1>=0+1 by A88;
             then A92:i+1-len f>=0 by XREAL_1:8;
              then A93:i+1-'len f+1+1<=len g
                        by A60,BINARITH:def 3;
             0<i+1+1-len f by A88;
              then A94: i+1+1-'len f<=len g-2+1 by A59,BINARITH:def 3;
                  i+1-len f+1=i-len f+1+1;
             then i+1-len f+1=i-'len f+1+1 by A77,BINARITH:50;
             then A95:i+1-'len f+1=i-'len f+1+1 by A78,BINARITH:50;
                 i+1+1-len f+1=i+1-len f+1+1;
             then A96: i+1+1-len f+1=i+1-'len f+1+1 by A92,BINARITH:def 3;
           A97:LSeg(f^mid(g,2,len g),i)
            =LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
                                   by A54,A57,TOPREAL1:def 5;
           A98:LSeg(g,i-'len f+1)=LSeg(g/.(i-'len f+1),g/.(i-'len f+1+1))
                                   by A80,A86,TOPREAL1:def 5;
             A99:g/.(i-'len f+1)=g.(i-'len f+1) by A81,A86,FINSEQ_4:24;
             A100:now assume A101:1<=i-'len f;
              then 1<=i-len f by Th1;
             then 1+len f<=i-len f +len f by XREAL_1:8;
              then A102:  len f<i by NAT_1:38;
              then (f^mid(g,2,len g)).i=mid(g,2,len g).(i-len f)
                                             by A58,Th15;
              then (f^mid(g,2,len g)).i=mid(g,2,len g).(i-'len f)
                                             by A102,BINARITH:50;
               then (f^mid(g,2,len g)).i=g.(i-'len f+2-1)
                    by A3,A83,A101,Th31;
              hence (f^mid(g,2,len g)).i=g.(i-'len f+1);
             end;
                 now assume 1>i-'len f;
               then i-'len f+1<=0+1 by NAT_1:38;
               then i-'len f<=0 by XREAL_1:8;
               then A103:i-'len f=0;
               then i-len f=0 by A77,BINARITH:50;
               then i=len f;
              hence (f^mid(g,2,len g)).i
                = g.(i-'len f+1) by A1,A4,A103,FINSEQ_1:85;
             end;
            then A104:(f^mid(g,2,len g))/.i=g/.(i-'len f+1) by A54,A58,A99
,A100,FINSEQ_4:24;
             A105:(f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                                         by A55,A57,FINSEQ_4:24;
             A106:g/.(i-'len f+1+1)=g.(i-'len f+1+1) by A80,A87,FINSEQ_4:24;
             A107:now assume A108:1<=(i+1)-'len f; then 1<=(i+1)-len f
                                             by Th1;
                then 1+len f<=(i+1)-len f +len f by XREAL_1:8;
              then A109:  len f<(i+1) by NAT_1:38;
              then (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).((i+1)-len f)
                                             by A57,Th15;
              then (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).((i+1)-'len f)
                                             by A109,BINARITH:50;
               then (f^mid(g,2,len g)).(i+1)=g.((i+1)-'len f+2-1)
                                        by A3,A85,A108,Th31;
              hence (f^mid(g,2,len g)).(i+1)=g.((i+1)-'len f+1);
             end;
A110:        now assume 1>(i+1)-'len f;
               then (i+1)-'len f+1<=0+1 by NAT_1:38;
               then (i+1)-'len f<=0 by XREAL_1:8;
               then A111:(i+1)-'len f=0;
               then (i+1)-len f=0 by A78,BINARITH:50;
               then (i+1)=len f;
              hence (f^mid(g,2,len g)).(i+1)
                = g.((i+1)-'len f+1) by A1,A4,A111,FINSEQ_1:85;
             end;
           A112:LSeg(f^mid(g,2,len g),i+1)
            =LSeg((f^mid(g,2,len g))/.(i+1),(f^mid(g,2,len g))/.(i+1+1))
                                   by A54,A55,TOPREAL1:def 5;
           A113:LSeg(g,i+1-'len f+1)
            =LSeg(g/.(i+1-'len f+1),g/.(i+1-'len f+1+1))
                                   by A84,A89,TOPREAL1:def 5;
             A114:(f^mid(g,2,len g))/.(i+1+1)=(f^mid(g,2,len g)).(i+1+1)
                                         by A54,A56,FINSEQ_4:24;
             A115:g/.(i+1-'len f+1+1)=g.(i+1-'len f+1+1)
                       by A90,A93,FINSEQ_4:24;
             A116:now assume A117:1<=(i+1+1)-'len f; then 1<=(i+1+1)-len f
                                             by Th1;
                then 1+len f<=(i+1+1)-len f +len f by XREAL_1:8;
              then A118:  len f<(i+1+1) by NAT_1:38;
              then (f^mid(g,2,len g)).(i+1+1)=mid(g,2,len g).((i+1+1)-len f)
                                             by A54,Th15;
              then (f^mid(g,2,len g)).(i+1+1)=mid(g,2,len g).((i+1+1)-'len f)
                                             by A118,BINARITH:50;
               then (f^mid(g,2,len g)).(i+1+1)=g.((i+1+1)-'len f+2-1)
                              by A3,A94,A117,Th31;
              hence (f^mid(g,2,len g)).(i+1+1)=g.((i+1+1)-'len f+1);
             end;
                 now assume 1>(i+1+1)-'len f;
               then (i+1+1)-'len f+1<=0+1 by NAT_1:38;
               then A119:(i+1+1)-'len f<=0 by XREAL_1:8;
               then A120:(i+1+1)-'len f=0;
                  (i+1+1)-len f=0 by A91,A119,BINARITH:def 3;
               then (i+1+1)=len f;
              hence (f^mid(g,2,len g)).(i+1+1)
               = g.((i+1+1)-'len f+1) by A1,A4,A120,FINSEQ_1:85;
             end;
          then A121:LSeg(f^mid(g,2,len g),i+1)=LSeg(g,i+1-'len f+1)
                  by A95,A96,A105,A106,A107,A110,A112,A113,A114,A115,A116,
BINARITH:def 3;
                    i-'len f+1+(1+1)<=len g by A79;
         hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
          = {(f^mid(g,2,len g))/.(i+1)} by A3,A86,A95,A97,A98,A104,A105,A106
,A107,A121,TOPREAL1:def 8;
        end;
        case i+2=len f +1; then i+(1+1)=len f+1;
          then A122: i+1+1=len f+1;
          then A123:i+1=len f;
          then A124:i<=len f by Th9;
              i=len f -1 by A123;
          then A125:i=len f-'1 by A4,BINARITH:50;
             A126:(f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i
                   by A54,A58,FINSEQ_4:24;
                f/.i=f.i by A54,A124,FINSEQ_4:24;
            then A127:(f^mid(g,2,len g))/.i=f/.i by A54,A124,A126,FINSEQ_1:85;
             A128:(f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                                               by A55,A57,FINSEQ_4:24;
             A129:f/.(i+1)=f.(i+1) by A55,A123,FINSEQ_4:24;
           then A130:f/.(i+1)=g/.1 by A1,A5,A123,FINSEQ_4:24;
            A131:(f^mid(g,2,len g))/.(i+1)=f/.(i+1)
                      by A55,A123,A128,A129,FINSEQ_1:85;
           A132:LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               = LSeg(f^mid(g,2,len g),i) by A54,A57,TOPREAL1:def 5;
          A133:LSeg(f,i)=LSeg(f/.i,f/.(i+1)) by A54,A123,TOPREAL1:def 5;
           A134:LSeg(f^mid(g,2,len g),i+1)
            =LSeg((f^mid(g,2,len g))/.(i+1),(f^mid(g,2,len g))/.(i+1+1))
                                   by A54,A55,TOPREAL1:def 5;
            A135:i+1-'len f+1 =0+1 by A123,BINARITH:51 .=1;
           then A136:LSeg(g,i+1-'len f+1)
            =LSeg(g/.(i+1-'len f+1),g/.(i+1-'len f+1+1))
                                   by A3,TOPREAL1:def 5;
             A137:(f^mid(g,2,len g))/.(i+1+1)=(f^mid(g,2,len g)).(i+1+1)
                                         by A54,A56,FINSEQ_4:24;
             A138:g/.(i+1-'len f+1+1)=g.(i+1-'len f+1+1)
                      by A3,A135,FINSEQ_4:24;
                 len g -2>=0 by A3,XREAL_1:50;
             then A139: 0+1<=len g-2+1 by XREAL_1:8;
                len f<(i+1+1) by A122,NAT_1:38;
              then (f^mid(g,2,len g)).(i+1+1)=mid(g,2,len g).((i+1+1)-len f)
                                             by A54,Th15;
              then A140:(f^mid(g,2,len g)).(i+1+1)=g.(2+1-'1) by A3,A122,A139,
Th31
                 .=g.2 by BINARITH:39;
            A141:LSeg(g,1)=LSeg(g/.1,g/.(1+1)) by A3,TOPREAL1:def 5;
            A142:g/.1=g.1 by A5,FINSEQ_4:24;
            then g/.1= f/.len f by A1,A4,FINSEQ_4:24;
            then A143:g/.1 in LSeg(f/.(len f-'1),f/.len f) by TOPREAL1:6;
                g/.1 in LSeg(g/.1,g/.(1+1)) by TOPREAL1:6;
            then g/.1 in LSeg(f,i) /\
 LSeg(g,1) by A123,A125,A133,A141,A143,XBOOLE_0:def 3;
           then A144:{g/.1} c= LSeg(f,i) /\ LSeg(g,1) by ZFMISC_1:37;
             A145:LSeg(f,i)c=L~f by TOPREAL3:26;
                 LSeg(g,1)c=L~g by TOPREAL3:26;
           then LSeg(f,i) /\ LSeg(g,1) c= {g/.1} by A1,A142,A145,XBOOLE_1:27;
         hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
          = {(f^mid(g,2,len g))/.(i+1)}
          by A127,A130,A131,A132,A133,A134,A135,A136,A137,A138,A140,A144,
XBOOLE_0:def 10;
        end;
        end;
       hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
        = {(f^mid(g,2,len g))/.(i+1)};
      end;
      end;
     hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
       = {(f^mid(g,2,len g))/.(i+1)};
    end;
    A146: for i,j st i+1 < j holds
    LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
     proof let i,j;assume A147: i+1 < j;
           now per cases;
       case A148:j<len f & j+1<=len (f^mid(g,2,len g));
         then A149:j<=len (f^mid(g,2,len g)) by Th9;
         then A150:i+1<len (f^mid(g,2,len g)) by A147,XREAL_1:2;
         then A151:i<=len (f^mid(g,2,len g)) by Th9;
         A152:i+1<len f by A147,A148,XREAL_1:2;
         then A153:i<len f by NAT_1:38;
             now per cases;
         case A154:1<=i;
              then A155:1<=i+1 by Th11;
              then A156:1<j by A147,XREAL_1:2;
              then A157:1<=j+1 by Th11;
           A158:(f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i
                 by A151,A154,FINSEQ_4:24;
              f/.i=f.i by A153,A154,FINSEQ_4:24;
          then A159:(f^mid(g,2,len g))/.i=f/.i by A153,A154,A158,FINSEQ_1:85;
           A160:(f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                                               by A150,A155,FINSEQ_4:24;
              f/.(i+1)=f.(i+1) by A152,A155,FINSEQ_4:24;
         then A161:LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               =LSeg(f/.i,f/.(i+1)) by A152,A155,A159,A160,FINSEQ_1:85;
            LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               = LSeg(f^mid(g,2,len g),i) by A150,A154,TOPREAL1:def 5;
         then A162:LSeg(f^mid(g,2,len g),i)=LSeg(f,i)
                       by A152,A154,A161,TOPREAL1:def 5;
            A163:j+1<=len f by A148,NAT_1:38;
           A164:(f^mid(g,2,len g))/.j=(f^mid(g,2,len g)).j
                 by A149,A156,FINSEQ_4:24;
              f/.j=f.j by A148,A156,FINSEQ_4:24;
          then A165:(f^mid(g,2,len g))/.j=f/.j by A148,A156,A164,FINSEQ_1:85;
           A166:(f^mid(g,2,len g))/.(j+1)=(f^mid(g,2,len g)).(j+1)
                                               by A148,A157,FINSEQ_4:24;
               f/.(j+1)=f.(j+1) by A157,A163,FINSEQ_4:24;
         then A167:LSeg((f^mid(g,2,len g))/.j,(f^mid(g,2,len g))/.(j+1))
               =LSeg(f/.j,f/.(j+1)) by A157,A163,A165,A166,FINSEQ_1:85;
         A168:LSeg((f^mid(g,2,len g))/.j,(f^mid(g,2,len g))/.(j+1))
               = LSeg(f^mid(g,2,len g),j) by A148,A156,TOPREAL1:def 5;
             LSeg(f,j)=LSeg(f/.j,f/.(j+1)) by A156,A163,TOPREAL1:def 5;
          then LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
                                by A2,A147,A162,A167,A168,TOPREAL1:def 9;
          hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
                                by XBOOLE_0:def 7;
         end;
         case i<1;
           then LSeg(f^mid(g,2,len g),i)={} by TOPREAL1:def 5;
          hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
         end;
         end;
        hence LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
          by XBOOLE_0:def 7;
       end;
       case A169:i+1<=len f & len f<=j & j+1<=len (f^mid(g,2,len g));
             now per cases by A147,A169,XREAL_1:1;
         case A170:i+1<len f & len f<=j;
               len f<=len f+len mid(g,2,len g)
                              by NAT_1:29;
           then A171:i+1<len (f^mid(g,2,len g)) by A6,A170,XREAL_1:2;
           A172: 1+1<=j by A2,A170,XREAL_1:2;
           then A173:1<=j by Th9;
           A174:len f<=j+1 by A170,Th11;
              now per cases;
          case A175:1<=i;
           then A176:1<=i+1 by Th11;
           A177:i+1+1<=len f by A170,NAT_1:38;
         A178: now
                1<=i & i<=len (f^mid(g,2,len g)) by A171,A175,Th9;
           then A179:(f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i by FINSEQ_4:24;
                1<=i & i<=len f by A169,A175,Th9;
           then A180:f/.i=f.i by FINSEQ_4:24;
                1<=i & i<=len f by A169,A175,Th9;
          then A181:(f^mid(g,2,len g))/.i=f/.i by A179,A180,FINSEQ_1:85;
           A182:(f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                                               by A171,A176,FINSEQ_4:24;
               f/.(i+1)=f.(i+1) by A169,A176,FINSEQ_4:24;
         then A183:LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               =LSeg(f/.i,f/.(i+1)) by A169,A176,A181,A182,FINSEQ_1:85;
             LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               = LSeg(f^mid(g,2,len g),i) by A171,A175,TOPREAL1:def 5;
         hence LSeg(f^mid(g,2,len g),i)=LSeg(f,i)
                          by A169,A175,A183,TOPREAL1:def 5;
         end;
           A184:1<=1+(j-'len f) by NAT_1:29;
               j+1+1<=len f+(len g-1)+1 by A6,A8,A169,XREAL_1:8;
           then j+1+1-len f<=len f +len g -len f by XREAL_1:11;
           then j-len f+1+1<=len g;
           then A185:j-'len f+1+1<=len g by A170,BINARITH:50;
           then j-'len f+1+1-1<=len g-1 by XREAL_1:11;
           then A186: j-'len f+1<=len g-2+1;
           then j-'len f+1<=len g-'2+1 by A3,BINARITH:50;
           then A187:j-'len f<=len g-'2+1 by Th9;
           A188:j-'len f+1=j-len f+1 by A169,BINARITH:50
                  .=j+1-len f
           .=j+1-'len f by A174,BINARITH:50;
           A189:LSeg(g,j-'len f+1)=LSeg(g/.(j-'len f+1),g/.(j-'len f+1+1))
                                   by A184,A185,TOPREAL1:def 5;
             A190:1<=j & j<=len (f^mid(g,2,len g)) by A169,A172,Th9;
                 1<=j-'len f+1 & j-'len f+1<=len g by A185,Th9,NAT_1:29;
             then A191:g/.(j-'len f+1)=g.(j-'len f+1) by FINSEQ_4:24;
             A192:now assume A193:1<=j-'len f;
             then 1<=j-len f by Th1;
             then 1+len f<=j-len f +len f by XREAL_1:8;
              then A194:  len f<j by NAT_1:38;
              then (f^mid(g,2,len g)).j=mid(g,2,len g).(j-len f)
                                             by A190,Th15;
              then (f^mid(g,2,len g)).j=mid(g,2,len g).(j-'len f)
                                             by A194,BINARITH:50;
               then (f^mid(g,2,len g)).j=g.(j-'len f+2-1)
                          by A3,A187,A193,Th31;
              hence (f^mid(g,2,len g)).j=g.(j-'len f+1);
             end;
                 now assume 1>j-'len f;
               then j-'len f+1<=0+1 by NAT_1:38;
               then j-'len f<=0 by XREAL_1:8;
               then A195:j-'len f=0;
               then j-len f=0 by A169,BINARITH:50;
               then j=len f;
              hence (f^mid(g,2,len g)).j
                = g.(j-'len f+1) by A1,A4,A195,FINSEQ_1:85;
             end;
            then A196:(f^mid(g,2,len g))/.j=g/.(j-'len f+1) by A190,A191,A192,
FINSEQ_4:24;
                1<=j+1 & j+1<=len (f^mid(g,2,len g)) by A169,A173,Th11;
             then A197:(f^mid(g,2,len g))/.(j+1)=(f^mid(g,2,len g)).(j+1)
                                         by FINSEQ_4:24;
                 1<=j-'len f+1+1 by NAT_1:29;
             then A198:g/.(j-'len f+1+1)=g.(j-'len f+1+1) by A185,FINSEQ_4:24;
             A199:now assume A200:1<=(j+1)-'len f; then 1<=(j+1)-len f
                                             by Th1;
                then 1+len f<=(j+1)-len f +len f by XREAL_1:8;
              then A201:  len f<(j+1) by NAT_1:38;
              then (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).((j+1)-len f)
                                             by A169,Th15;
              then (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).((j+1)-'len f)
                                             by A201,BINARITH:50;
               then (f^mid(g,2,len g)).(j+1)=g.((j+1)-'len f+2-1)
                                         by A3,A186,A188,A200,Th31;
              hence (f^mid(g,2,len g)).(j+1)=g.((j+1)-'len f+1);
             end;
                 now assume 1>(j+1)-'len f;
               then (j+1)-'len f+1<=0+1 by NAT_1:38;
               then (j+1)-'len f<=0 by XREAL_1:8;
               then A202:(j+1)-'len f=0;
               then (j+1)-len f=0 by A174,BINARITH:50;
               then (j+1)=len f;
              hence (f^mid(g,2,len g)).(j+1)
                = g.((j+1)-'len f+1) by A1,A4,A202,FINSEQ_1:85;
             end;
          then A203:LSeg(f^mid(g,2,len g),j)=LSeg(g,j-'len f+1)
                                by A169,A173,A188,A189,A196,A197,A198,A199,
TOPREAL1:def 5;
          A204: LSeg(f^mid(g,2,len g),i) c= L~f by A178,TOPREAL3:26;
               LSeg(f^mid(g,2,len g),j) c=L~g by A203,TOPREAL3:26;
           then A205:LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) c={g.
1}
                                     by A1,A204,XBOOLE_1:27;
               now per cases;
           case A206:i+1<len f-'1;
             A207: 1+1-1<=len f -1 by A2,XREAL_1:11;
             A208:1<=len f by A2,XREAL_1:2;
             then A209:len f-'1+1=len f-1+1 by BINARITH:50
             .=len f;
             then A210:1<=len f-'1 & len f-'1+1<=len f by A207;
                 now given x being set such that
               A211:x in LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j);
               A212:x=g.1 by A205,A211,TARSKI:def 1;
               A213:x in LSeg(f,i) by A178,A211,XBOOLE_0:def 3;
                   f/.len f in LSeg(f,len f-'1) by A209,A210,TOPREAL1:27;
               then g.1 in LSeg(f,len f-'1) by A1,A208,FINSEQ_4:24;
               then x in LSeg(f,i)/\ LSeg(f,len f-'1) by A212,A213,
XBOOLE_0:def 3;
               then LSeg(f,i) meets LSeg(f,len f-'1) by XBOOLE_0:4;
              hence contradiction by A2,A206,TOPREAL1:def 9;
             end;
            hence LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
             by XBOOLE_0:4;
           end;
           case i+1>=len f-'1; then i+1>=len f-1 by A4,BINARITH:50;
             then A214: i+1+1>=len f-1+1 by XREAL_1:8;
             then A215: i+1+1=len f by A177,XREAL_1:1;
             then i+1=len f-1;
             then A216:i+1=len f-1 & i+1=len f-'1 by A4,BINARITH:50;
             A217:i+1+1=len f & i+(1+1)=len f by A177,A214,XREAL_1:1;
             A218:i+1+1=len f & i+(1+1)=len f
               & 1<=i+1 & i+1<=len f by A215,NAT_1:29;
             A219:len f-'1+1=len f-1+1 by A4,BINARITH:50 .=len f;
             then 1+1<=len f-'1+1 by A1,TOPREAL1:def 10;
             then A220:1<=len f-'1 & len f-'1+1<=len f by A219,XREAL_1:8;
                 now given x being set such that
               A221:x in LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j);
               A222:x=g.1 by A205,A221,TARSKI:def 1;
               A223:x in LSeg(f,i) by A178,A221,XBOOLE_0:def 3;
                   f/.len f in LSeg(f,len f-'1) by A219,A220,TOPREAL1:27;
               then g.1 in LSeg(f,len f-'1) by A1,A4,FINSEQ_4:24;
               then A224:x in LSeg(f,i)/\ LSeg(f,len f-'1) by A222,A223,
XBOOLE_0:def 3;
                   LSeg(f,i)/\ LSeg(f,len f-'1)={f/.(i+1)}
                    by A2,A175,A216,A217,TOPREAL1:def 8;
               then f.len f=f/.(i+1) by A1,A222,A224,TARSKI:def 1;
               then A225:f.len f=f.(len f-'1) by A216,A218,FINSEQ_4:24;
               A226:len f in dom f by A4,FINSEQ_3:27;
               A227:len f-'1<=len f by BINARITH:52;
                   1+1-1<=len f-1 by A2,XREAL_1:11;
               then 1<=len f-'1 by Th1;
               then len f-'1 in dom f by A227,FINSEQ_3:27;
               then len f=len f-'1 by A2,A225,A226,FUNCT_1:def 8;
               then len f=len f-1 by A4,BINARITH:50;
               then len f+1=len f;
              hence contradiction;
             end;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
             by XBOOLE_0:def 1;
           end;
           end;
          hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
            by XBOOLE_0:def 7;
         end;
         case i<1;
           then LSeg(f^mid(g,2,len g),i)={} by TOPREAL1:def 5;
          hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
         end;
         end;
         hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
         end;
         case A228:i+1<=len f & len f<j;
               len f<len f+len mid(g,2,len g)
            proof
                 1+1-1<=len g-1 by A3,XREAL_1:11;
             then len f+1<=len f+len mid(g,2,len g) by A8,XREAL_1:9;
            hence thesis by NAT_1:38;
            end;
           then A229:i+1<len (f^mid(g,2,len g)) by A6,A228,XREAL_1:2;
           A230: 1+1<=j by A2,A228,XREAL_1:2;
           then A231:1<=j by Th9;
           A232:len f<=j+1 by A228,Th11;
              now per cases;
          case A233:1<=i;
           then A234:1<=i+1 by Th11;
         A235: now
                1<=i & i<=len (f^mid(g,2,len g)) by A229,A233,Th9;
           then A236:(f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i by FINSEQ_4:24;
                1<=i & i<=len f by A169,A233,Th9;
           then A237:f/.i=f.i by FINSEQ_4:24;
                1<=i & i<=len f by A169,A233,Th9;
          then A238:(f^mid(g,2,len g))/.i=f/.i by A236,A237,FINSEQ_1:85;
           A239:(f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                                               by A229,A234,FINSEQ_4:24;
               f/.(i+1)=f.(i+1) by A169,A234,FINSEQ_4:24;
         then A240:LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               =LSeg(f/.i,f/.(i+1)) by A169,A234,A238,A239,FINSEQ_1:85;
             LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               = LSeg(f^mid(g,2,len g),i) by A229,A233,TOPREAL1:def 5;
         hence LSeg(f^mid(g,2,len g),i)=LSeg(f,i)
                            by A169,A233,A240,TOPREAL1:def 5;
         end;
           A241:1<=1+(j-'len f) by NAT_1:29;
               j+1+1<=len f+(len g-1)+1 by A6,A8,A169,XREAL_1:8;
           then j+1+1-len f<=len f +len g -len f by XREAL_1:11;
           then j-len f+1+1<=len g;
           then A242:j-'len f+1+1<=len g by A228,BINARITH:50;
           then j-'len f+1+1-1<=len g-1 by XREAL_1:11;
           then A243: j-'len f+1<=len g-2+1;
           then j-'len f+1<=len g-'2+1 by A3,BINARITH:50;
           then A244:j-'len f<=len g-'2+1 by Th9;
           A245:j-'len f+1=j-len f+1 by A169,BINARITH:50
                 .=j+1-len f
           .=j+1-'len f by A232,BINARITH:50;
           A246:LSeg(g,j-'len f+1)=LSeg(g/.(j-'len f+1),g/.(j-'len f+1+1))
                                   by A241,A242,TOPREAL1:def 5;
             A247:1<=j & j<=len (f^mid(g,2,len g)) by A169,A230,Th9;
                 1<=j-'len f+1 & j-'len f+1<=len g by A242,Th9,NAT_1:29;
             then A248:g/.(j-'len f+1)=g.(j-'len f+1) by FINSEQ_4:24;
             A249:now assume A250:1<=j-'len f;
               then 1<=j-len f by Th1;
             then 1+len f<=j-len f +len f by XREAL_1:8;
              then A251:  len f<j by NAT_1:38;
              then (f^mid(g,2,len g)).j=mid(g,2,len g).(j-len f)
                                             by A247,Th15;
              then (f^mid(g,2,len g)).j=mid(g,2,len g).(j-'len f)
                                             by A251,BINARITH:50;
               then (f^mid(g,2,len g)).j=g.(j-'len f+2-1)
                          by A3,A244,A250,Th31;
              hence (f^mid(g,2,len g)).j=g.(j-'len f+1);
             end;
                 now assume 1>j-'len f;
               then j-'len f+1<=0+1 by NAT_1:38;
               then j-'len f<=0 by XREAL_1:8;
               then A252:j-'len f=0;
               then j-len f=0 by A169,BINARITH:50;
               then j=len f;
              hence (f^mid(g,2,len g)).j
               = g.(j-'len f+1) by A1,A4,A252,FINSEQ_1:85;
             end;
            then A253:(f^mid(g,2,len g))/.j=g/.(j-'len f+1) by A247,A248,A249,
FINSEQ_4:24;
                1<=j+1 & j+1<=len (f^mid(g,2,len g)) by A169,A231,Th11;
             then A254:(f^mid(g,2,len g))/.(j+1)=(f^mid(g,2,len g)).(j+1)
                                         by FINSEQ_4:24;
                 1<=j-'len f+1+1 by NAT_1:29;
             then A255:g/.(j-'len f+1+1)=g.(j-'len f+1+1) by A242,FINSEQ_4:24;
             A256:now assume A257:1<=(j+1)-'len f; then 1<=(j+1)-len f
                                             by Th1;
                then 1+len f<=(j+1)-len f +len f by XREAL_1:8;
              then A258:  len f<(j+1) by NAT_1:38;
              then (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).((j+1)-len f)
                                             by A169,Th15;
              then (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).((j+1)-'len f)
                                             by A258,BINARITH:50;
               then (f^mid(g,2,len g)).(j+1)=g.((j+1)-'len f+2-1)
                                     by A3,A243,A245,A257,Th31;
              hence (f^mid(g,2,len g)).(j+1)=g.((j+1)-'len f+1);
             end;
                 now assume 1>(j+1)-'len f;
               then (j+1)-'len f+1<=0+1 by NAT_1:38;
               then (j+1)-'len f<=0 by XREAL_1:8;
               then A259:(j+1)-'len f=0;
               then (j+1)-len f=0 by A232,BINARITH:50;
               then (j+1)=len f;
              hence (f^mid(g,2,len g)).(j+1)
               = g.((j+1)-'len f+1) by A1,A4,A259,FINSEQ_1:85;
             end;
          then A260:LSeg(f^mid(g,2,len g),j)=LSeg(g,j-'len f+1)
                     by A169,A231,A245,A246,A253,A254,A255,A256,TOPREAL1:def 5;
          A261: LSeg(f^mid(g,2,len g),i) c= L~f by A235,TOPREAL3:26;
               LSeg(f^mid(g,2,len g),j) c=L~g by A260,TOPREAL3:26;
           then A262:LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) c={g.
1}
                                     by A1,A261,XBOOLE_1:27;
                 now given x being set such that
               A263:x in LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j);
               A264:x=g.1 by A262,A263,TARSKI:def 1;
               then A265:g/.1=x by A5,FINSEQ_4:24;
               A266:LSeg(g,j-'len f+1)<>{} & x in LSeg(g,j-'len f+1)
                & 1+1<=len g by A1,A260,A263,TOPREAL1:def 10,XBOOLE_0:def 3;
               then x in LSeg(g,1) & x in LSeg(g,j-'len f+1)
               by A265,TOPREAL1:27;
               then A267:x in LSeg(g,1)/\ LSeg(g,j-'len f+1) by XBOOLE_0:def 3;
               then LSeg(g,1) meets LSeg(g,j-'len f+1) by XBOOLE_0:4;
               then 1+1 >= j-'len f+1 by A3,TOPREAL1:def 9;
               then 1>=j-'len f by XREAL_1:8;
               then 1>=j-len f by Th1;
               then 1+len f>=j-len f+len f by XREAL_1:8;
               then len f+1>=j & j>=len f+1 by A228,NAT_1:38;
               then A268: j=len f +1 & j-'len f+1=j-len f+1 by A228,XREAL_1:1
,BINARITH:50;
               then 1+2<=len g by A266,TOPREAL1:def 5;
               then LSeg(g,1)/\ LSeg(g,j-'len f+1)={g/.(1+1)} by A3,A268,
TOPREAL1:def 8;
               then A269: x=g/.(1+1) by A267,TARSKI:def 1 .=g.(1+1) by A3,
FINSEQ_4:24;
               A270:1 in dom g by A5,FINSEQ_3:27;
                   1+1 in dom g by A3,FINSEQ_3:27;
              hence contradiction by A3,A264,A269,A270,FUNCT_1:def 8;
             end;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
             by XBOOLE_0:def 1;
         end;
         case i<1;
           then LSeg(f^mid(g,2,len g),i)={} by TOPREAL1:def 5;
          hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
         end;
         end;
          hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
         end;
         end;
        hence LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
          by XBOOLE_0:def 7;
       end;
       case A271:len f<i+1 & j+1<=len (f^mid(g,2,len g));
            then A272:len f<=i by NAT_1:38;
            then A273: 1+1<=i by A2,XREAL_1:2;
            then A274:1<=i by Th9;
            then A275: 1<=i+1 by Th11;
            then A276:1<=j by A147,XREAL_1:2;
            A277:len f<j by A147,A271,XREAL_1:2;   j<=j+1 by NAT_1:29;
            then A278:len f<j+1 by A277,XREAL_1:2;
            A279:i-'len f=i-len f by A272,BINARITH:50;
            A280:j-'len f=j-len f by A277,BINARITH:50;
                i+1-len f<j-len f by A147,XREAL_1:11;
            then A281:i-'len f +1+1<j-'len f +1 by A279,A280,XREAL_1:8;
          A282:1<=(i-'len f)+1 by NAT_1:29;
          A283:1<=(j-'len f)+1 by NAT_1:29;
             now per cases;
         case A284:j+1<=len (f^mid(g,2,len g));
              then A285:j<=len (f^mid(g,2,len g)) by Th9;
          A286: now A287: i+1<len (f^mid(g,2,len g)) by A147,A285,XREAL_1:2;
              then i+1<len f+(len g-1) by A8,FINSEQ_1:35;
              then A288: i+1-len f<len f+(len g-1)-len f by XREAL_1:11;
              then A289: i-len f+1+1<len g-1+1 by XREAL_1:8;
            then A290:(i-'len f+1)<=len g by A279,Th9;
            A291:(i-'len f+1)<=len g-2+1 by A279,A288;
            A292:1<=i & i<=len (f^mid(g,2,len g)) by A273,A287,Th9;
           A293:g/.(i-'len f+1)=g.(i-'len f+1) by A282,A290,FINSEQ_4:24;
            A294: now per cases;
            case i<=len f;
              then A295:i=len f by A272,XREAL_1:1;
              then (f^mid(g,2,len g)).i
                    =g.(0+1) by A1,A274,FINSEQ_1:85
                   .=g.(i-'len f+1) by A295,BINARITH:51;
             hence (f^mid(g,2,len g))/.i=g/.(i-'len f+1) by A292,A293,
FINSEQ_4:24;
            end;
            case A296:i>len f; then len f+1<=i by NAT_1:38;
              then A297: len f+1-len f<=i-len f by XREAL_1:11;
                  i-'len f+1-1<=len g-1 by A290,XREAL_1:11;
              then A298:i-'len f<=len g -2+1;
                (f^mid(g,2,len g)).i
                =mid(g,2,len g).(i-'len f) by A279,A292,A296,Th15
               .=g.(i-'len f+2-1) by A3,A279,A297,A298,Th31
               .=g.(i-'len f +1);
             hence (f^mid(g,2,len g))/.i=g/.(i-'len f+1) by A292,A293,
FINSEQ_4:24;
            end;
            end;
               1<=i+1 & i+1<=len (f^mid(g,2,len g)) by A147,A285,A292,Th11,
XREAL_1:2;
           then A299:(f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                                               by FINSEQ_4:24;
               1<=(i-'len f+1)+1 & (i-'len f+1)+1<=len g by A272,A289,NAT_1:29
,BINARITH:50;
           then A300:g/.((i-'len f+1)+1)=g.((i-'len f+1)+1) by FINSEQ_4:24;
           A301:i-'len f+1+2-1 =i-'len f+1+1;
           A302: (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).(i+1-len f)
                                         by A271,A287,Th15
            .=g.(i-'len f +1+1) by A3,A279,A282,A291,A301,Th31;
             LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
               = LSeg(f^mid(g,2,len g),i) by A274,A287,TOPREAL1:def 5;
          hence LSeg(f^mid(g,2,len g),i)=LSeg(g,(i-'len f+1))
                                by A279,A282,A289,A294,A299,A300,A302,
TOPREAL1:def 5;
       end;
                 j+1<=len f+(len g-1) by A8,A284,FINSEQ_1:35;
              then A303: j+1-len f<=len f+(len g-1)-len f by XREAL_1:11;
              then A304: j-len f+1+1<=len g-1+1 by XREAL_1:8;
            then A305:(j-'len f+1)<=len g by A280,Th9;
            A306:(j-'len f+1)<=len g-2+1 by A280,A303;
            A307:1<=j & j<=len (f^mid(g,2,len g)) by A147,A275,A284,Th9,
XREAL_1:2;
           then A308:(f^mid(g,2,len g))/.j=(f^mid(g,2,len g)).j by FINSEQ_4:24;
           A309:g/.(j-'len f+1)=g.(j-'len f+1) by A283,A305,FINSEQ_4:24;
                 len f+1<=j by A277,NAT_1:38;
              then A310: len f+1-len f<=j-len f by XREAL_1:11;
                  j-'len f+1-1<=len g-1 by A305,XREAL_1:11;
              then A311:j-'len f<=len g -2+1;
            A312: (f^mid(g,2,len g)).j=mid(g,2,len g).(j-len f) by A277,A307,
Th15
               .=g.(j-'len f+2-1) by A3,A280,A310,A311,Th31
               .=g.(j-'len f +1);
               1<=j+1 & j+1<=len (f^mid(g,2,len g)) by A284,A307,Th11;
           then A313:(f^mid(g,2,len g))/.(j+1)=(f^mid(g,2,len g)).(j+1)
                                               by FINSEQ_4:24;
               1<=(j-'len f+1)+1 & (j-'len f+1)+1<=len g by A277,A304,NAT_1:29
,BINARITH:50;
           then A314:g/.((j-'len f+1)+1)=g.((j-'len f+1)+1) by FINSEQ_4:24;
            A315:j-'len f+1+2-1=j-'len f+1+1;
           A316: (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).(j+1-len f)
                                         by A278,A284,Th15
            .=g.(j-'len f +1+1) by A3,A280,A283,A306,A315,Th31;
             LSeg((f^mid(g,2,len g))/.j,(f^mid(g,2,len g))/.(j+1))
               = LSeg(f^mid(g,2,len g),j) by A276,A284,TOPREAL1:def 5;
           then LSeg(f^mid(g,2,len g),j)=LSeg(g,j-'len f+1)
                  by A280,A283,A304,A308,A309,A312,A313,A314,A316,
TOPREAL1:def 5;
          then LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
                                      by A3,A281,A286,TOPREAL1:def 9;
          hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
                                      by XBOOLE_0:def 7;
         end;
         case j+1>len (f^mid(g,2,len g));
           then LSeg(f^mid(g,2,len g),j)={} by TOPREAL1:def 5;
          hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
         end;
         end;
        hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
       end;
       case j+1>len (f^mid(g,2,len g));
         then LSeg(f^mid(g,2,len g),j) = {} by TOPREAL1:def 5;
        hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
       end;
       end;
      hence LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
        by XBOOLE_0:def 7;
     end;
       for i st 1 <= i & i+1 <= len (f^mid(g,2,len g)) holds
    ((f^mid(g,2,len g))/.i)`1 = ((f^mid(g,2,len g))/.(i+1))`1
    or ((f^mid(g,2,len g))/.i)`2 = ((f^mid(g,2,len g))/.(i+1))`2
    proof let i;assume A317: 1 <= i & i+1 <= len (f^mid(g,2,len g));
         now per cases;
     case A318:i<len f;
           A319:1<=i+1 by A317,Th11;
            A320:i+1<=len f by A318,NAT_1:38;
                1<=i & i<=len (f^mid(g,2,len g)) by A317,Th9;
           then A321:(f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i by FINSEQ_4:24;
              f/.i=f.i by A317,A318,FINSEQ_4:24;
          then A322:(f^mid(g,2,len g))/.i=f/.i by A317,A318,A321,FINSEQ_1:85;
           A323:(f^mid(g,2,len g))/.((i+1))=(f^mid(g,2,len g)).(i+1)
                             by A317,A319,FINSEQ_4:24;
           A324:f/.(i+1)=f.(i+1) by A319,A320,FINSEQ_4:24;
               (f^mid(g,2,len g)).(i+1)=f.(i+1) by A319,A320,FINSEQ_1:85;
      hence ((f^mid(g,2,len g))/.i)`1 = ((f^mid(g,2,len g))/.(i+1))`1
       or ((f^mid(g,2,len g))/.i)`2 = ((f^mid(g,2,len g))/.(i+1))`2
                                   by A2,A317,A320,A322,A323,A324,
TOPREAL1:def 7;
     end;
     case A325:i>=len f; then A326: i-len f>=0 by XREAL_1:50;
          then A327:i-'len f=i-len f by BINARITH:def 3;
          A328:i+1>=len f by A325,Th11;
          A329: i+1-len f<=len f+(len g-1)-len f by A6,A8,A317,XREAL_1:11;
          then A330: i-len f+1+1<=len g-1+1 by XREAL_1:8;
           then A331: 1<=i-'len f+1 & i-'len f+1+1<=len g by A326,
BINARITH:def 3,NAT_1:29;
                 i-'len f<=i-'len f+1 by NAT_1:29;
             then A332:i-'len f<=len g-2+1 by A327,A329,XREAL_1:2;
             A333:1<=i & i<=len (f^mid(g,2,len g)) by A317,Th9;
                 1<=i-'len f+1 & i-'len f+1<=len g by A327,A330,Th9,NAT_1:29;
             then A334:g/.(i-'len f+1)=g.(i-'len f+1) by FINSEQ_4:24;
              i+1-len f<=len g-2+1 by A329;
             then i+1-len f<=len g-'2+1 by A3,BINARITH:50;
             then A335:i+1-'len f<=len g -'2+1 by A328,BINARITH:50;
             A336:i+1-'len f+1=i+1-len f+1 by A328,BINARITH:50
             .=i-len f+1+1
             .=i-'len f+1+1 by A325,BINARITH:50;
                 1<=1+(i-'len f) by NAT_1:29;
             then 1<=1+(i-len f) by A325,BINARITH:50;
             then 1<=1+i-len f;
             then A337:1<=i+1-'len f by Th1;
             A338:now assume A339:1<=i-'len f;
             then 1<=i-len f by Th1;
               then 1+len f<=i-len f +len f by XREAL_1:8;
              then A340:  len f<i by NAT_1:38;
              then (f^mid(g,2,len g)).i=mid(g,2,len g).(i-len f)
                                             by A333,Th15;
              then (f^mid(g,2,len g)).i=mid(g,2,len g).(i-'len f)
                                             by A340,BINARITH:50;
               then (f^mid(g,2,len g)).i=g.(i-'len f+2-1)
                      by A3,A332,A339,Th31;
              hence (f^mid(g,2,len g)).i=g.(i-'len f+1);
             end;
                 now assume 1>i-'len f;
               then i-'len f+1<=0+1 by NAT_1:38;
               then i-'len f<=0 by XREAL_1:8;
               then A341:i-'len f=0;
               then i=len f by A327;
              hence (f^mid(g,2,len g)).i
                 = g.(i-'len f+1) by A1,A4,A341,FINSEQ_1:85;
             end;
            then A342:(f^mid(g,2,len g))/.i=g/.(i-'len f+1) by A333,A334,A338,
FINSEQ_4:24;
                1<=i+1 & i+1<=len (f^mid(g,2,len g)) by A317,Th11;
             then A343:(f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                                         by FINSEQ_4:24;
                 1<=i-'len f+1+1 & i-'len f+1+1<=len g by A326,A330,
BINARITH:def 3,NAT_1:29;
             then A344:g/.(i-'len f+1+1)=g.(i-'len f+1+1) by FINSEQ_4:24;
                 len f<(i+1) by A325,NAT_1:38;
              then (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).((i+1)-len f)
                                             by A317,Th15;
              then (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).((i+1)-'len f)
                                             by A328,BINARITH:50;
               then (f^mid(g,2,len g)).(i+1)=g.((i+1)-'len f+2-1)
                                         by A3,A335,A337,Th31;
      hence ((f^mid(g,2,len g))/.i)`1 = ((f^mid(g,2,len g))/.(i+1))`1
       or ((f^mid(g,2,len g))/.i)`2 = ((f^mid(g,2,len g))/.(i+1))`2
                      by A3,A331,A336,A342,A343,A344,TOPREAL1:def 7;
     end;
     end;
     hence ((f^mid(g,2,len g))/.i)`1 = ((f^mid(g,2,len g))/.(i+1))`1
      or ((f^mid(g,2,len g))/.i)`2 = ((f^mid(g,2,len g))/.(i+1))`2;
    end;
   then f^mid(g,2,len g) is one-to-one & len (f^mid(g,2,len g)) >= 2
   & f^mid(g,2,len g) is unfolded s.n.c. special
       by A2,A7,A9,A53,A146,XREAL_1:2,FUNCT_1:def 8,
         TOPREAL1:def 7,def 8,def 9;
 hence f^mid(g,2,len g) is_S-Seq by TOPREAL1:def 10;
end;

theorem Th74:for f,g being FinSequence of TOP-REAL 2 st f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1}
 holds f^mid(g,2,len g) is_S-Seq_joining f/.1,g/.len g
proof let f,g be FinSequence of TOP-REAL 2;
 assume A1: f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1};
  then A2:f^mid(g,2,len g) is_S-Seq by Th73;
   A3:f is one-to-one & len f >= 2
   & f is unfolded s.n.c. special by A1,TOPREAL1:def 10;
   A4:g is one-to-one & len g >= 2
   & g is unfolded s.n.c. special by A1,TOPREAL1:def 10;
   A5:1<=len f by A3,XREAL_1:2;
   A6:1<=len g by A4,XREAL_1:2;
   A7:len (f^mid(g,2,len g))=len f + len mid(g,2,len g) by FINSEQ_1:35;
   A8:len mid(g,2,len g)=len g -'2+1 by A4,A6,Th27;
   then A9:len mid(g,2,len g)
                     =len g -2+1 by A4,BINARITH:50
                     .=len g -1;
    A10: 1+1-1<=len g-1 by A4,XREAL_1:11;
    A11:len (mid(g,2,len g))+2-1=len g by A9;
  A12:(f^mid(g,2,len g)).1=f.1 by A5,FINSEQ_1:85 .=f/.1 by A5,FINSEQ_4:24;
      len g-1>=1+1-1 by A4,XREAL_1:11;
  then len f+1<=len (f^mid(g,2,len g)) by A7,A9,XREAL_1:8;
  then len f<len (f^mid(g,2,len g)) by NAT_1:38;
  then (f^mid(g,2,len g)).(len (f^mid(g,2,len g)))
   =(mid(g,2,len g)).(len (f^mid(g,2,len g))-len f) by Th15
   .=g.len g by A4,A7,A8,A10,A11,Th31
   .=g/.len g by A6,FINSEQ_4:24;
 hence f^mid(g,2,len g) is_S-Seq_joining f/.1,g/.len g
                         by A2,A12,Def3;
end;

theorem
 for f being FinSequence of TOP-REAL 2,
     n being Nat holds L~(f/^n) c= L~f
proof let f be FinSequence of TOP-REAL 2, n be Nat;
 let x be set;assume x in L~(f/^n);
   then x in union { LSeg(f/^n,i) : 1 <= i & i+1 <= len (f/^n) }
                             by TOPREAL1:def 6;
    then consider Y being set such that A1: x in Y & Y in
     { LSeg(f/^n,i) : 1 <= i & i+1 <= len (f/^n) } by TARSKI:def 4;
    consider i such that
    A2:Y= LSeg(f/^n,i) &(1 <= i & i+1 <= len (f/^n)) by A1;
        now per cases;
    case n<=len f;
      then LSeg(f/^n,i)=LSeg(f,n+i) by A2,SPPOL_2:4;
      then Y c=L~f by A2,TOPREAL3:26;
     hence x in L~f by A1;
    end;
    case n>len f;
      then f/^n=<*>(the carrier of TOP-REAL 2) by RFINSEQ:def 2;
      then i+1<=0 by A2,FINSEQ_1:32;
     hence contradiction;
    end;
    end;
   hence x in L~f;
end;

theorem Th76:
 for f being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2 st p in L~f holds L~R_Cut(f,p) c= L~f
proof let f be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2 such that
A1: p in L~f;
    len f <> 0 by A1,TOPREAL1:28;
    then len f > 0;
    then A2: len f >= 0+1 by NAT_1:38;
   A3:1<=Index(p,f) & Index(p,f)<=len f by A1,Th41;
 per cases;
 suppose p = f.1;
   then R_Cut(f,p)=<*p*> by Def5;
   then len R_Cut(f,p)=1 by FINSEQ_1:56;
   then L~R_Cut(f,p) = {} by TOPREAL1:28;
  hence thesis by XBOOLE_1:2;
 end;
 suppose p<>f.1;
  then A4: R_Cut(f,p)=mid(f,1,Index(p,f))^<*p*> by Def5;
         Index(p,f) < len f by A1,Th41;
     then A5:1<=Index(p,f)+1 & Index(p,f)+1<=len f by NAT_1:29,38;
     A6:len (mid(f,1,Index(p,f)))
              =Index(p,f)-'1+1 by A2,A3,Th27
              .=Index(p,f) by A3,BINARITH:53;
     then 0<len (mid(f,1,Index(p,f))) by A3;
     then (mid(f,1,Index(p,f)))<><*>(the carrier of TOP-REAL 2)
                                     by FINSEQ_1:32;
     then A7:L~(mid(f,1,Index(p,f))^<*p*>)
     = L~mid(f,1,Index(p,f)) \/ LSeg((mid(f,1,Index(p,f))/.Index(p,f)),p)
                          by A6,SPPOL_2:19;
      A8:(mid(f,1,Index(p,f)))/.Index(p,f)=mid(f,1,Index(p,f)).Index(p,f)
                          by A3,A6,FINSEQ_4:24
                                  .=f.(Index(p,f)) by A3,Th32
                                  .=f/.(Index(p,f)) by A3,FINSEQ_4:24;
          p in LSeg(f,Index(p,f)) by A1,Th42;
      then A9:p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A3,A5,
TOPREAL1:def 5;
          f/.(Index(p,f)) in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
                          by TOPREAL1:6;
      then A10:LSeg((mid(f,1,Index(p,f))/.Index(p,f)),p)
        c= LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A8,A9,TOPREAL1:12;
          LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) c= L~f by A3,A5,SPPOL_2:16;
      then A11:LSeg((mid(f,1,Index(p,f))/.Index(p,f)),p)c= L~f by A10,
XBOOLE_1:1;
         mid(f,1,Index(p,f)) =(f/^(1-'1))|(Index(p,f)-'1+1) by A3,Def1
         .=(f/^(0))|(Index(p,f)-'1+1) by BINARITH:51
         .=f|(Index(p,f)-'1+1) by FINSEQ_5:31
         .=f|Index(p,f) by A3,BINARITH:53;
      then L~mid(f,1,Index(p,f)) c= L~f by TOPREAL3:27;
     hence L~(R_Cut(f,p)) c= L~f by A4,A7,A11,XBOOLE_1:8;
end;
end;

Lm2:i<=j implies j+k-'i=j+k-i
proof assume i<=j; then i<=j+k by NAT_1:37;
 hence thesis by BINARITH:50;
end;

Lm3: (<*>D)|i=<*>D
proof len (<*>D)=0 by FINSEQ_1:32;
  then len (<*>D)<=i;
 hence thesis by FINSEQ_1:79;
end;

Lm4: Rev (<*>D)=<*>D;

Lm5: for f1 be FinSequence of D,k be Nat st 1<=k & k<=len f1
  holds mid(f1,k,k)=<*f1/.k*> & len mid(f1,k,k)=1
proof let f1 be FinSequence of D,k be Nat;assume A1:1<=k & k<=len f1;
   A2:len (f1/^(k-'1))=len f1-'(k-'1) by Th19;
      k-'1+1=k by A1,BINARITH:53;
   then A3:(f1/^(k-'1)).1=f1.k by A1,Th23;
   A4:f1/.k=f1.k by A1,FINSEQ_4:24;
      k-'1+1<=len f1 by A1,BINARITH:53;
   then k-'1+1-(k-'1)<=len f1-(k-'1) by XREAL_1:11;
   then A5:1<=1 & 1<=len (f1/^(k-'1)) by A2,Th1;
  then 0<len (f1/^(k-'1));
  then A6:f1/^(k-'1) is non empty by FINSEQ_1:25;
     k-'k+1=k-k+1 by BINARITH:50 .=1;
  then mid(f1,k,k)=(f1/^(k-'1))|1 by Def1
  .=<*(f1/^(k-'1))/.1*> by A6,FINSEQ_5:23;
 hence thesis by A3,A4,A5,FINSEQ_1:56,FINSEQ_4:24;
end;

Lm6: for f1 be FinSequence of D holds mid(f1,0,0)=f1|1
proof
  let f1 be FinSequence of D;
     0-'0+1=0-0+1 by BINARITH:50 .=1;
  then A1:mid(f1,0,0)=(f1/^(0-'1))|1 by Def1;  0-1<0;
  then 0-'1=0 by BINARITH:def 3;
 hence thesis by A1,FINSEQ_5:31;
end;

Lm7: for f1 be FinSequence of D,k be Nat st len f1<k
  holds mid(f1,k,k)=<*>D
proof let f1 be FinSequence of D,k be Nat;assume A1:len f1<k;
A2: 0+1<=k by A1,NAT_1:38;
     k-'k+1=k-k+1 by BINARITH:50 .=1;
  then A3:mid(f1,k,k)=(f1/^(k-'1))|1 by Def1;
     len f1+1<=k by A1,NAT_1:38;
  then len f1+1-1<=k-1 by XREAL_1:11;
  then len f1<=k-'1 by A2,BINARITH:50;
  then f1/^(k-'1)=<*>D by FINSEQ_5:35;
 hence thesis by A3,Lm3;
end;

Lm8: for f1 be FinSequence of D,
  i1,i2 holds mid(f1,i1,i2)=Rev mid(f1,i2,i1)
proof let f1 be FinSequence of D; let k1,k2 be Nat;
     now per cases;
  case A1:k1<=k2;
    then A2:mid(f1,k1,k2)=(f1/^(k1-'1))|(k2-'k1+1) by Def1;
       now per cases by A1,REAL_1:def 5;
    case k1<k2;
      then mid(f1,k2,k1)=Rev mid(f1,k1,k2) by A2,Def1;
     hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by FINSEQ_6:29;
    end;
    case A3:k1=k2;
         k1=0 or k1>0;
then A4:    k1=0 or 0+1<=k1 by NAT_1:38;
         now per cases by A4;
      case k1=0;
        then A5:mid(f1,k1,k2)=f1|1 by A3,Lm6;
           now per cases;
        case len f1=0; then f1=<*>D by FINSEQ_1:32;
          then f1|1=<*>D by Lm3;
         hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,A5,Lm4;
        end;
        case len f1<>0;
          then f1 <> <*>D by FINSEQ_1:32;
          then f1|1=<*f1/.1*> by FINSEQ_5:23;
         hence mid(f1,k1,k2)=Rev mid(f1,k2,k1)
            by A3,A5,FINSEQ_5:63;
        end;
        end;
       hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
      end;
      case 1<=k1 & k1<= len f1;
        then mid(f1,k1,k1)=<*f1/.k1*> by Lm5;
       hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,FINSEQ_5:63;
      end;
      case len f1<k1; then mid(f1,k1,k1)=<*>D by Lm7;
       hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,Lm4;
      end;
      end;
     hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
    end;
    end;
   hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
  end;
  case A6:k1>k2;
    then mid(f1,k1,k2)=Rev ((f1/^(k2-'1))|(k1-'k2+1)) by Def1;
   hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A6,Def1;
  end;
  end;
 hence thesis;
end;

Lm9:for h being FinSequence of TOP-REAL 2,i1,i2 st
1<=i1 & i1<=len h & 1<=i2 & i2<=len h holds L~mid(h,i1,i2) c= L~h
proof let h be FinSequence of TOP-REAL 2,i1,i2;
 assume A1:1<=i1 & i1<=len h & 1<=i2 & i2<=len h;
 thus L~mid(h,i1,i2) c= L~h
 proof let x be set;assume
   A2: x in L~mid(h,i1,i2);
      now per cases;
   case A3:i1<=i2;
        x in union { LSeg(mid(h,i1,i2),i) : 1 <= i & i+1 <= len mid(h,i1,i2) }
                           by A2,TOPREAL1:def 6;
     then consider Y being set such that A4: x in Y
      & Y in { LSeg(mid(h,i1,i2),i) : 1 <= i & i+1 <= len mid(h,i1,i2) }
        by TARSKI:def 4;
     consider i such that A5:Y=LSeg(mid(h,i1,i2),i)
            & (1 <= i & i+1 <= len mid(h,i1,i2)) by A4;
     A6:LSeg(mid(h,i1,i2),i)=LSeg(mid(h,i1,i2)/.i,mid(h,i1,i2)/.(i+1))
                             by A5,TOPREAL1:def 5;
        i<=i+1 by NAT_1:29;
     then A7:i<=len mid(h,i1,i2) by A5,XREAL_1:2;
     then A8:mid(h,i1,i2).i=h.(i+i1-'1) by A1,A3,A5,Th27;
     A9:len mid(h,i1,i2)=i2-'i1+1 by A1,A3,Th27;
     A10:1<=i+1 & i+1<=len mid(h,i1,i2) by A5,NAT_1:29;
     then A11:mid(h,i1,i2).(i+1)=h.(i+1+i1-'1) by A1,A3,Th27;
     A12:mid(h,i1,i2)/.i=mid(h,i1,i2).i by A5,A7,FINSEQ_4:24;
     A13:mid(h,i1,i2)/.(i+1)=mid(h,i1,i2).(i+1) by A10,FINSEQ_4:24;
     1+1<=i+i1 by A1,A5,XREAL_1:9;
then A14: 1+1-1<=i+i1-1 by XREAL_1:11;
     i+1-1<=i2-'i1+1-1 by A5,A9,XREAL_1:11;
     then i<=i2-i1 by A3,BINARITH:50;
     then A15: i+i1<=i2-i1+i1 by XREAL_1:8;
     then A16: i+i1<=len h by A1,XREAL_1:2;
     then A17:1<=i+i1-'1 & i+i1-'1<=len h by A1,A14,Lm2,Th7;
     then A18:h/.(i+i1-'1)=h.(i+i1-'1) by FINSEQ_4:24;
     A19:i+1+i1-'1=i+1+i1-1 by A1,Lm2 .=i+i1;
     then A20:i+1+i1-'1=i+i1-1+1 .=i+i1-'1+1 by A1,Lm2;
        i+i1-'1<=i+i1-'1+1 by NAT_1:29;
     then i+i1-'1<=i+i1-1+1 by A1,Lm2;
     then A21:1<=i+i1 & i+i1<=len h by A1,A15,A17,XREAL_1:2;
     then LSeg(mid(h,i1,i2),i)=LSeg(h/.(i+i1-'1),h/.(i+1+i1-'1))
                                  by A6,A8,A11,A12,A13,A18,A19,FINSEQ_4:24
                    .=LSeg(h,i+i1-'1) by A14,A16,A19,A20,TOPREAL1:def 5;
     then LSeg(mid(h,i1,i2),i) in { LSeg(h,j) : 1 <= j & j+1 <= len h}
       by A14,A19,A20,A21;
     then x in union { LSeg(h,j) : 1 <= j & j+1 <= len h} by A4,A5,TARSKI:def 4
;
    hence x in L~h by TOPREAL1:def 6;
   end;
   case A22:i1>i2;
        mid(h,i1,i2)=Rev mid(h,i2,i1) by Lm8;
     then x in L~mid(h,i2,i1) by A2,SPPOL_2:22;
     then x in union { LSeg(mid(h,i2,i1),i) : 1 <= i & i+1 <= len mid(h,i2,i1)
}
                           by TOPREAL1:def 6;
     then consider Y being set such that A23: x in Y
      & Y in { LSeg(mid(h,i2,i1),i) : 1 <= i & i+1 <= len mid(h,i2,i1) }
       by TARSKI:def 4;
     consider i such that A24:Y=LSeg(mid(h,i2,i1),i)
            & (1 <= i & i+1 <= len mid(h,i2,i1)) by A23;
     A25:LSeg(mid(h,i2,i1),i)=LSeg(mid(h,i2,i1)/.i,mid(h,i2,i1)/.(i+1))
                             by A24,TOPREAL1:def 5;
        i<=i+1 by NAT_1:29;
     then A26:i<=len mid(h,i2,i1) by A24,XREAL_1:2;
     then A27:mid(h,i2,i1).i=h.(i+i2-'1) by A1,A22,A24,Th27;
     A28:len mid(h,i2,i1)=i1-'i2+1 by A1,A22,Th27;
     A29:1<=i+1 & i+1<=len mid(h,i2,i1) by A24,NAT_1:29;
     then A30:mid(h,i2,i1).(i+1)=h.(i+1+i2-'1) by A1,A22,Th27;
     A31:mid(h,i2,i1)/.i=mid(h,i2,i1).i by A24,A26,FINSEQ_4:24;
     A32:mid(h,i2,i1)/.(i+1)=mid(h,i2,i1).(i+1) by A29,FINSEQ_4:24;
        1+1<=i+i2 by A1,A24,XREAL_1:9;
then A33: 1+1-1<=i+i2-1 by XREAL_1:11;
     then A34:1<=i+i2-'1 by A1,Lm2;
        i+1-1<=i1-'i2+1-1 by A24,A28,XREAL_1:11;
     then i<=i1-i2 by A22,BINARITH:50;
     then A35: i+i2<=i1-i2+i2 by XREAL_1:8;
     then A36:i+i2<=len h by A1,XREAL_1:2;
then  i+i2-'1<=len h by Th7;
     then A37:h/.(i+i2-'1)=h.(i+i2-'1) by A34,FINSEQ_4:24;
     A38:i+1+i2-'1=i+1+i2-1 by A1,Lm2 .=i+i2;
     then A39:i+1+i2-'1=i+i2-1+1 .=i+i2-'1+1 by A1,Lm2;
        i+i2-'1<=i+i2-'1+1 by NAT_1:29;
     then i+i2-'1<=i+i2-1+1 by A1,Lm2;
     then A40:1<=i+i2 & i+i2<=len h by A1,A34,A35,XREAL_1:2;
     then LSeg(mid(h,i2,i1),i)=LSeg(h/.(i+i2-'1),h/.(i+1+i2-'1))
                                  by A25,A27,A30,A31,A32,A37,A38,FINSEQ_4:24
                       .=LSeg(h,i+i2-'1) by A33,A36,A38,A39,TOPREAL1:def 5;
     then LSeg(mid(h,i2,i1),i) in { LSeg(h,j) : 1 <= j & j+1 <= len h}
        by A33,A38,A39,A40;
     then x in union { LSeg(h,j) : 1 <= j & j+1 <= len h} by A23,A24,
TARSKI:def 4;
    hence x in L~h by TOPREAL1:def 6;
   end;
   end;
  hence x in L~h;
 end;
end;

Lm10:
  i in dom f & j in dom f implies len mid(f,i,j) >= 1
proof
 assume i in dom f;
then A1: 1 <= i & i <= len f by FINSEQ_3:27;
 assume j in dom f;
then A2: 1 <= j & j <= len f by FINSEQ_3:27;
     i <= j or j < i;
  then len mid(f,i,j)=i-'j+1 or len mid(f,i,j)=j-'i+1 by A1,A2,Th27;
 hence thesis by NAT_1:29;
end;

Lm11:
 i in dom f & j in dom f implies mid(f,i,j) is non empty
proof
 assume i in dom f & j in dom f;
  then len mid(f,i,j) >= 1 by Lm10;
 hence mid(f,i,j) is non empty by FINSEQ_3:27,RELAT_1:60;
end;

Lm12:
 i in dom f & j in dom f implies (mid(f,i,j))/.1 = f/.i
proof
 assume
A1: i in dom f;
then A2: 1 <= i & i <= len f by FINSEQ_3:27;
 assume
 A3:j in dom f;
then A4: 1 <= j & j <= len f by FINSEQ_3:27;
     mid(f,i,j) is non empty by A1,A3,Lm11;
  then 1 in dom mid(f,i,j) by FINSEQ_5:6;
 hence (mid(f,i,j))/.1 = mid(f,i,j).1 by FINSEQ_4:def 4
      .= f.i by A2,A4,Th27
      .= f/.i by A1,FINSEQ_4:def 4;
end;

theorem Th77:
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st p in L~f holds
    L~L_Cut(f,p) c= L~f
 proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2 such that
   A1: p in L~f;
  len f <> 0 by A1,TOPREAL1:28;
  then len f > 0;
  then A2: len f >= 0+1 by NAT_1:38;
  then A3: len f in dom f by FINSEQ_3:27;
  A4: 1<=Index(p,f) & Index(p,f)<len f by A1,Th41;
  then A5: 1<Index(p,f)+1 & Index(p,f)+1<=len f by NAT_1:38;
  then A6: Index(p,f)+1 in dom f by FINSEQ_3:27;
  per cases;
   suppose p = f.(Index(p,f)+1);
    then L_Cut(f,p) = mid(f,Index(p,f)+1,len f) by Def4;
    hence thesis by A2,A5,Lm9;
   end;
   suppose p <> f.(Index(p,f)+1);
    then A7: L_Cut(f,p) = <*p*>^mid(f,Index(p,f)+1,len f) by Def4;
    len mid(f,Index(p,f)+1,len f) >= 0+1 by A6,A3,Lm10;
    then mid(f,Index(p,f)+1,len f) <> {} by FINSEQ_1:25;
    then A8: L~(<*p*>^mid(f,Index(p,f)+1,len f))
       = LSeg(p,mid(f,Index(p,f)+1,len f)/.1) \/ L~mid(f,Index(p,f)+1,len f)
                                                           by SPPOL_2:20;
    A9: mid(f,Index(p,f)+1,len f)/.1 = f/.(Index(p,f)+1)
                                              by A6,A3,Lm12;
    p in LSeg(f,Index(p,f)) by A1,Th42;
    then A10: p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
                                                   by A4,A5,TOPREAL1:def 5;
    f/.(Index(p,f)+1) in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
                                                              by TOPREAL1:6;
    then A11: LSeg(p,mid(f,Index(p,f)+1,len f)/.1) c=
     LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A9,A10,TOPREAL1:12;
    LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) c= L~f by A4,A5,SPPOL_2:16;
    then A12: LSeg(p,mid(f,Index(p,f)+1,len f)/.1) c= L~f by A11,XBOOLE_1:1;
    L~mid(f,Index(p,f)+1,len f) c= L~f by A2,A5,Lm9;
    hence L~(L_Cut(f,p)) c= L~f by A7,A8,A12,XBOOLE_1:8;
   end;
 end;

theorem Th78:for f,g being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2 st f.len f=g.1 & p in L~f &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>f.len f
 holds L_Cut(f,p)^mid(g,2,len g) is_S-Seq_joining p,g/.len g
proof let f,g be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2;
 assume that
A1: f.len f=g.1 and
A2: p in L~f and
A3: f is_S-Seq and
A4: g is_S-Seq and
A5: L~f /\ L~g={g.1} and
A6: p<>f.len f;
A7:f is one-to-one & len f >= 2
   & f is unfolded s.n.c. special by A3,TOPREAL1:def 10;
   A8:g is one-to-one & len g >= 2
   & g is unfolded s.n.c. special by A4,TOPREAL1:def 10;
   A9:1<=len f by A7,XREAL_1:2;
   A10:1<=len g by A8,XREAL_1:2;
     A11:L_Cut(f,p) is_S-Seq by A2,A3,A6,Th69;
      then A12:1+1<=len L_Cut(f,p) by TOPREAL1:def 10;
      then 1+1-1<=len L_Cut(f,p)-1 by XREAL_1:11;
      then A13:1<=len L_Cut(f,p)-'1 by Th1;
        L_Cut(f,p) is_S-Seq_joining p,f/.len f
                 by A2,A3,A6,Th68;
    then A14:L_Cut(f,p).1=p & L_Cut(f,p).(len L_Cut(f,p))=f/.len f by Def3;
    then A15:L_Cut(f,p).len L_Cut(f,p)=g.1 by A1,A9,FINSEQ_4:24;
    A16:1<=len L_Cut(f,p) by A12,XREAL_1:2;
        then L_Cut(f,p).1=(L_Cut(f,p))/.1 by FINSEQ_4:24;
    then A17:(L_Cut(f,p))/.(1)=p by A2,Th58;
         g/.1 in LSeg(g/.1,g/.(1+1)) by TOPREAL1:6;
     then g/.1 in LSeg(g,1) by A8,TOPREAL1:def 5;
     then g.1 in LSeg(g,1) by A10,FINSEQ_4:24;
    then A18:g.1 in L~g by SPPOL_2:17;
    A19:len L_Cut(f,p)-'1+1=len L_Cut(f,p) by A16,BINARITH:53;
    then (L_Cut(f,p))/.(len L_Cut(f,p)) in LSeg((L_Cut(f,p))/.(len L_Cut(f,p)
-'1),
     (L_Cut(f,p))/.(len L_Cut(f,p)-'1+1)) by TOPREAL1:6;
    then L_Cut(f,p).len L_Cut(f,p) in LSeg((L_Cut(f,p))/.(len L_Cut(f,p)-'1),
     (L_Cut(f,p))/.(len L_Cut(f,p)-'1+1)) by A16,FINSEQ_4:24;
    then L_Cut(f,p).len L_Cut(f,p) in LSeg(L_Cut(f,p),len L_Cut(f,p)-'1)
                                        by A13,A19,TOPREAL1:def 5;
    then f/.len f in L~(L_Cut(f,p)) by A14,SPPOL_2:17;
    then f.len f in L~(L_Cut(f,p)) by A9,FINSEQ_4:24;
    then g.1 in L~(L_Cut(f,p))/\ L~g by A1,A18,XBOOLE_0:def 3;
    then A20:{g.1}c= L~(L_Cut(f,p))/\ L~g by ZFMISC_1:37;
        L~(L_Cut(f,p)) c= L~f by A2,Th77;
    then L~(L_Cut(f,p))/\ L~g c= L~f /\ L~g by XBOOLE_1:27;
    then L~(L_Cut(f,p))/\ L~g={g.1} by A5,A20,XBOOLE_0:def 10;
  hence L_Cut(f,p)^mid(g,2,len g) is_S-Seq_joining p,g/.len g
           by A4,A11,A15,A17,Th74;
end;

theorem   for f,g being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2 st f.len f=g.1 & p in L~f &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>f.len f
 holds L_Cut(f,p)^mid(g,2,len g) is_S-Seq
proof let f,g be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2;assume f.len f=g.1 & p in L~f &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>f.len f;
  then L_Cut(f,p)^mid(g,2,len g) is_S-Seq_joining p,g/.len g by Th78;
 hence L_Cut(f,p)^mid(g,2,len g) is_S-Seq by Def3;
end;

theorem Th80:for f,g being FinSequence of TOP-REAL 2 st f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1}
 holds mid(f,1,len f-'1)^g is_S-Seq
proof let f,g be FinSequence of TOP-REAL 2;
 assume A1:f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1};
   then A2:f is one-to-one & len f >= 2
   & f is unfolded s.n.c. special by TOPREAL1:def 10;
   then A3:1<=len f by XREAL_1:2;
      A4: len f-1>=1+1-1 by A2,XREAL_1:11;
  A5:Rev f is_S-Seq by A1,SPPOL_2:47;
  A6:Rev g is_S-Seq by A1,SPPOL_2:47;
     L~(Rev f)=L~f by SPPOL_2:22;
  then A7:L~(Rev g)/\ L~(Rev f)={g.1} by A1,SPPOL_2:22;
  A8:(Rev f).1=f.len f by FINSEQ_5:65;
  A9:(Rev g).len (Rev g)=(Rev Rev g).1 by FINSEQ_5:65
       .=(Rev f).1 by A1,A8,FINSEQ_6:29;
  A10:len f-'1<=len f by Th13;
  then A11:len f-'(len f-'1)+1=len f-(len f-'1)+1 by BINARITH:50
  .=len f-(len f-1)+1 by A3,BINARITH:50 .=2;
  A12:len (Rev f)=len f by FINSEQ_5:def 3;
  A13:len f-'1+1=len f-1+1 by A3,BINARITH:50 .=len f;
      (Rev g)^(mid(Rev f,2,len (Rev f))) is_S-Seq by A1,A5,A6,A7,A8,A9,Th73;
  then (Rev g)^(Rev mid(f,1,len f-'1)) is_S-Seq
                         by A3,A4,A10,A11,A12,A13,Th22;
  then Rev (mid(f,1,len f-'1)^g) is_S-Seq by FINSEQ_5:67;
  then Rev Rev (mid(f,1,len f-'1)^g) is_S-Seq by SPPOL_2:47;
 hence mid(f,1,len f-'1)^g is_S-Seq by FINSEQ_6:29;
end;

theorem Th81:
 for f,g being FinSequence of TOP-REAL 2 st f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1}
 holds mid(f,1,len f-'1)^g is_S-Seq_joining f/.1,g/.len g
proof let f,g be FinSequence of TOP-REAL 2;
 assume A1: f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1};
  then A2:mid(f,1,len f-'1)^g is_S-Seq by Th80;
   A3:f is one-to-one & len f >= 2
   & f is unfolded s.n.c. special by A1,TOPREAL1:def 10;
   A4:g is one-to-one & len g >= 2
   & g is unfolded s.n.c. special by A1,TOPREAL1:def 10;
   A5:1<=len f by A3,XREAL_1:2;
   A6:1<=len g by A4,XREAL_1:2;
   A7:len f-'1<=len f by Th13;
       1+1-1<=len f-1 by A3,XREAL_1:11;
   then A8:1<=len f-'1 by Th1;
   A9:len (mid(f,1,len f-'1)^g)=len (mid(f,1,len f-'1))
     + len g by FINSEQ_1:35;
       len mid(f,1,len f-'1)=len f-'1-'1+1 by A5,A7,A8,Th27
                     .=len f-'1 -1+1 by A8,BINARITH:50
                     .=len f -'1;
  then A10:(mid(f,1,len f-'1)^g).1=mid(f,1,len f-'1).1 by A8,FINSEQ_1:85
   .=f.1 by A7,A8,Th32.=f/.1 by A5,FINSEQ_4:24;
      0<len g by A4;
  then 0+len (mid(f,1,len f-'1))<len g+len (mid(f,1,len f-'1)) by XREAL_1:8;
  then (mid(f,1,len f-'1)^g).(len (mid(f,1,len f-'1)^g))
   =g.(len (mid(f,1,len f-'1)^g)-len (mid(f,1,len f-'1))) by A9,Th15
   .=g/.len g by A6,A9,FINSEQ_4:24;
 hence mid(f,1,len f-'1)^g is_S-Seq_joining f/.1,g/.len g
                         by A2,A10,Def3;
end;

theorem Th82:for f,g being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2 st f.len f=g.1 & p in L~g &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>g.1
  holds mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq_joining f/.1,p
proof let f,g be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2;
 assume A1:f.len f=g.1 & p in L~g &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>g.1;
   then A2:f is one-to-one & len f >= 2 by TOPREAL1:def 10;
   A3:g is one-to-one & len g >= 2 by A1,TOPREAL1:def 10;
   A4:1<=len f by A2,XREAL_1:2;
   A5:1<=len g by A3,XREAL_1:2;
       1+1-1<=len f-1 by A2,XREAL_1:11;
   then A6:1<=len f-'1 by Th1;
   A7:len f-'1+1=len f by A4,BINARITH:53;
    A8:R_Cut(g,p) is_S-Seq by A1,Th70;
    then A9:1+1<=len R_Cut(g,p) by TOPREAL1:def 10;
    R_Cut(g,p) is_S-Seq_joining g/.1,p by A1,Th67;
    then A10:R_Cut(g,p).1=g/.1 & R_Cut(g,p).(len R_Cut(g,p))=p
                           by Def3;
    then A11:R_Cut(g,p).1=f.len f by A1,A5,FINSEQ_4:24;
    A12:1<=len R_Cut(g,p) by A9,XREAL_1:2;
then R_Cut(g,p).len R_Cut(g,p)=(R_Cut(g,p))/.(len R_Cut(g,p)) by FINSEQ_4:24;
    then A13:(R_Cut(g,p))/.(len R_Cut(g,p))=p by A1,Th59;
         f/.len f in LSeg(f/.(len f-'1),f/.(len f-'1+1)) by A7,TOPREAL1:6;
     then f/.len f in LSeg(f,len f-'1) by A6,A7,TOPREAL1:def 5;
     then f.len f in LSeg(f,len f-'1) by A4,FINSEQ_4:24;
    then A14:f.len f in L~f by SPPOL_2:17;
        (R_Cut(g,p))/.(1) in LSeg((R_Cut(g,p))/.(1),
     (R_Cut(g,p))/.(1+1)) by TOPREAL1:6;
    then R_Cut(g,p).1 in LSeg((R_Cut(g,p))/.(1),(R_Cut(g,p))/.(1+1))
                                     by A12,FINSEQ_4:24;
    then R_Cut(g,p).1 in LSeg(R_Cut(g,p),1) by A9,TOPREAL1:def 5;
    then g/.1 in L~(R_Cut(g,p)) by A10,SPPOL_2:17;
    then g.1 in L~(R_Cut(g,p)) by A5,FINSEQ_4:24;
    then f.len f in L~f /\ L~R_Cut(g,p) by A1,A14,XBOOLE_0:def 3;
    then A15:{f.len f}c= L~f /\ L~R_Cut(g,p) by ZFMISC_1:37;
        L~(R_Cut(g,p)) c= L~g by A1,Th76;
    then L~f /\ L~R_Cut(g,p) c= L~f /\ L~g by XBOOLE_1:27;
    then L~f /\ L~R_Cut(g,p)={R_Cut(g,p).1} by A1,A11,A15,XBOOLE_0:def 10;
 hence mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq_joining f/.1,p
                by A1,A8,A11,A13,Th81;
end;

theorem   for f,g being FinSequence of TOP-REAL 2,
 p being Point of TOP-REAL 2 st f.len f=g.1 & p in L~g &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>g.1
  holds mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq
proof let f,g be FinSequence of TOP-REAL 2,
 p be Point of TOP-REAL 2;
 assume f.len f=g.1 & p in L~g &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>g.1;
  then mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq_joining f/.1,p by Th82;
 hence mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq by Def3;
end;
