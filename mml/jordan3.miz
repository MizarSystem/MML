:: Reconstructions of Special Sequences
::  by Yatsuka Nakamura and Roman Matuszewski
::
:: Received December 10, 1996
:: Copyright (c) 1996 Association of Mizar Users

environ

 vocabularies ARYTM_1, FINSEQ_1, FUNCT_1, RELAT_1, FINSEQ_5, RFINSEQ, BOOLE,
      EUCLID, TOPREAL1, TARSKI, PRE_TOPC, MCART_1, SPPOL_2, GROUP_2, SQUARE_1,
      ARYTM_3, JORDAN3, FINSEQ_4, ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0, REAL_1,
      NAT_1, RFINSEQ, NAT_D, RELAT_1, FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_5,
      SEQ_4, STRUCT_0, TOPREAL1, PRE_TOPC, EUCLID, SPPOL_2;
 constructors PARTFUN1, XXREAL_0, REAL_1, NAT_1, SEQ_4, REALSET1, RFINSEQ,
      NAT_D, FINSEQ_5, TOPREAL1, BINOP_2;
 registrations XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, XXREAL_0, XREAL_0,
      NAT_1, MEMBERED, FINSEQ_1, FINSEQ_5, STRUCT_0, EUCLID, SPPOL_2,
      XXREAL_2;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI;
 theorems TARSKI, TOPREAL1, FINSEQ_1, FINSEQ_4, RFINSEQ, SPPOL_1, FUNCT_1,
      NAT_1, REAL_2, BINARITH, TOPREAL3, SPPOL_2, FINSEQ_5, EUCLID,
      FINSEQ_6, ZFMISC_1, FINSEQ_3, RELAT_1, SEQ_4, PARTFUN2, XBOOLE_0,
      XBOOLE_1, FINSEQ_2, XCMPLX_1, XREAL_1, XXREAL_0, ORDINAL1, PARTFUN1,
      CARD_1, XXREAL_2, XREAL_0, NAT_D;

begin

::-------------------------------------------:
::              Preliminaries                :
::-------------------------------------------:

reserve k for Nat;
reserve r,r1,r2 for Real;
reserve n,i,i1,i2,j,j1,j2 for Element of NAT;
reserve D for non empty set;
reserve f for FinSequence of D;

canceled 14;

theorem Th15:
  for i be Nat for p,q being FinSequence st len p<i
  & (i<=len p +len q or i<=len (p^q)) holds (p^q).i=q.(i-len p)
proof
  let i be Nat;
  let p,q be FinSequence;
  assume
A1: len p<i & (i<=len p +len q or i<=len (p^q));
  then len p +1<=i by NAT_1:13;
  then
A2: len p +1-len p<=i-len p by XREAL_1:11;
  then
A3: i-len p=i-'len p by XREAL_0:def 2;
A4: len p+(i-'len p)=len p+(i-len p) by A2,XREAL_0:def 2
    .=i;
  i<=len p + len q by A1,FINSEQ_1:35;
  then i-len p<=len p+len q -len p by XREAL_1:11;
  then i-'len p in dom q by A2,A3,FINSEQ_3:27;
  hence thesis by A4,FINSEQ_1:def 7;
end;

canceled;

theorem Th17:
  for x being set,f being FinSequence of D st 1<=len f holds
  (f^<*x*>).1=f.1 & (f^<*x*>).1=f/.1 &
  (<*x*>^f).(len f +1)=f.len f & (<*x*>^f).(len f +1)=f/.len f
proof
  let x be set,f be FinSequence of D;
  assume
A1: 1<=len f;
  then 1 in dom f by FINSEQ_3:27;
  then
A2: (f^<*x*>).1=f.1 by FINSEQ_1:def 7;
A3: len f in dom f by A1,FINSEQ_3:27;
  (<*x*>^f).(len f +1) =(<*x*>^f).(len <*x*>+len f) by FINSEQ_1:56
    .=f.len f by A3,FINSEQ_1:def 7;
  hence thesis by A1,A2,FINSEQ_4:24;
end;

theorem Th18:
  for f being FinSequence st len f=1 holds Rev f=f
proof
  let f be FinSequence;
  assume len f=1;
  then f=<*f.1*> by FINSEQ_1:57;
  hence Rev f=f by FINSEQ_5:63;
end;

theorem
  for f being FinSequence of D,k being Nat holds
  len (f/^k)=len f-'k by RFINSEQ:42;

canceled;

theorem
  for f being FinSequence of D,l1,l2 being Nat holds
  (f/^l1)|(l2-'l1)=(f|l2)/^l1 by FINSEQ_5:83;

begin

::-------------------------------------------:
::    Middle Function for Finite Sequences   :
::-------------------------------------------:

definition
  let D;
  let f be FinSequence of D,k1,k2 be Nat;
  func mid(f,k1,k2) -> FinSequence of D equals
  :Def1:
  (f/^(k1-'1))|(k2-'k1+1) if k1<=k2 otherwise Rev ((f/^(k2-'1))|(k1-'k2+1));
  correctness;
end;

theorem Th22:
  for f being FinSequence of D,k1,k2 being Element of NAT
  st 1<=k1 & k1<=len f & 1<=k2 & k2<=len f
  holds Rev mid(f,k1,k2)=mid(Rev f,len f-'k2+1,len f-'k1+1)
proof
  let f be FinSequence of D,k1,k2 be Element of NAT;
  assume
A1: 1<=k1 & k1<=len f & 1<=k2 & k2<=len f;
  per cases;
  suppose
A2: k1<=k2;
A3: len f-'k1=len f-k1 by A1,XREAL_1:235;
A4: len f-'k2=len f-k2 by A1,XREAL_1:235;
A5: (len f-'(k1-'1))+(k1-'1) =(len f-(k1-'1))+(k1-'1)
by A1,NAT_D:50,XREAL_1:235
      .=len f;
A6: len f-'(k1-'1)=len f-(k1-'1) by A1,NAT_D:50,XREAL_1:235
      .=len f-(k1-1) by A1,XREAL_1:235
      .=len f-k1+1
      .=len f-'k1+1 by A1,XREAL_1:235;
    len f-'k1>=len f-'k2 by A2,NAT_D:41;
    then
A7: len f-'k1+1>=len f-'k2+1 by XREAL_1:8;
    k2-k1<=len f-k1 by A1,XREAL_1:11;
    then k2-'k1<=len f-k1 by A2,XREAL_1:235;
    then k2-'k1<=len f-'k1 by A1,XREAL_1:235;
    then k2-'k1+1<=len f-'k1+1 by XREAL_1:8;
    then
A8: (len f-'(k1-'1)-'(k2-'k1+1)) =(len f-'k1+1)-(k2-'k1+1) by A6,XREAL_1:235
      .=(len f-k1+1)-(k2-k1+1) by A2,A3,XREAL_1:235
      .=len f+1-1-k2
      .=len f-'k2 by A1,XREAL_1:235;
A9: len f-'k1>=len f-'k2 by A2,NAT_D:41;
    len f-'k1<=len f-'k1+1 by NAT_1:11;
    then
A10: len f-'(k1-'1)-'(len f-'k2)
    =len f-'k1+1-(len f-'k2) by A6,A9,XREAL_1:235,XXREAL_0:2
      .=len f-k1+1-(len f-k2) by A1,A3,XREAL_1:235
      .=k2-k1+1
      .=k2-'k1+1 by A2,XREAL_1:235;
A11: (len (f/^(k1-'1))-'(k2-'k1+1))+(k2-'k1+1)
    =len f-'k2+(k2-'k1+1) by A8,RFINSEQ:42
      .=len f-k2+(k2-k1+1) by A2,A4,XREAL_1:235
      .=len f-(k1-1)
      .=len f-(k1-'1) by A1,XREAL_1:235
      .=len f-'(k1-'1) by A1,NAT_D:50,XREAL_1:235
      .=len (f/^(k1-'1)) by RFINSEQ:42;
A12: (len f-'k1+1)-'(len f-'k2+1)+1
    =(len f-'k1+1)-(len f-'k2+1)+1 by A7,XREAL_1:235
      .=(len f-k1+1)-(len f-k2+1)+1 by A1,A3,XREAL_1:235
      .=k2-k1+1
      .=k2-'k1+1 by A2,XREAL_1:235;
    len f-'k1>=len f-'k2 by A2,NAT_D:41;
    then len f-'k1+1>=len f-'k2+1 by XREAL_1:8;
    then mid(Rev f,len f-'k2+1,len f-'k1+1)
    =(((Rev f)/^((len f-'k2+1)-'1))|(k2-'k1+1)) by A12,Def1
      .=(Rev f/^(len f-'k2))|(len f-'(k1-'1)-'(len f-'k2)) by A10,NAT_D:34
      .=(Rev f|(len f-'(k1-'1)))/^(len f-'k2) by FINSEQ_5:83

.=(Rev f|(len f-'(k1-'1)))/^(len (f/^(k1-'1))-'(k2-'k1+1)) by A8,RFINSEQ:42
      .=(Rev (f/^(k1-'1)))/^(len (f/^(k1-'1))-'(k2-'k1+1)) by A5,FINSEQ_6:90
      .=Rev (((f/^(k1-'1))|(k2-'k1+1))) by A11,FINSEQ_6:91
      .=Rev mid(f,k1,k2) by A2,Def1;
    hence Rev mid(f,k1,k2)=mid(Rev f,len f-'k2+1,len f-'k1+1);
  end;
  suppose
A13: k1>k2;
A14: len f-'k2=len f-k2 by A1,XREAL_1:235;
A15: len f-'k1=len f-k1 by A1,XREAL_1:235;
A16: (len f-'(k2-'1))+(k2-'1) =(len f-(k2-'1))+(k2-'1)
by A1,NAT_D:50,XREAL_1:235
      .=len f;
A17: len f-'(k2-'1)=len f-(k2-'1) by A1,NAT_D:50,XREAL_1:235
      .=len f-(k2-1) by A1,XREAL_1:235
      .=len f-k2+1
      .=len f-'k2+1 by A1,XREAL_1:235;
    len f-'k2>=len f-'k1 by A13,NAT_D:41;
    then
A18: len f-'k2+1>=len f-'k1+1 by XREAL_1:8;
    k1-k2<=len f-k2 by A1,XREAL_1:11;
    then k1-'k2<=len f-k2 by A13,XREAL_1:235;
    then k1-'k2<=len f-'k2 by A1,XREAL_1:235;
    then k1-'k2+1<=len f-'k2+1 by XREAL_1:8;
    then
A19: (len f-'(k2-'1)-'(k1-'k2+1)) =(len f-'k2+1)-(k1-'k2+1) by A17,XREAL_1:235
      .=(len f-k2+1)-(k1-k2+1) by A13,A14,XREAL_1:235
      .=len f+1-1-k1
      .=len f-'k1 by A1,XREAL_1:235;
A20: len f-'k2>=len f-'k1 by A13,NAT_D:41;
    len f-'k2<=len f-'k2+1 by NAT_1:11;
    then
A21: len f-'(k2-'1)-'(len f-'k1)
    =len f-'k2+1-(len f-'k1) by A17,A20,XREAL_1:235,XXREAL_0:2
      .=len f-k2+1-(len f-k1) by A1,A14,XREAL_1:235
      .=k1-k2+1
      .=k1-'k2+1 by A13,XREAL_1:235;
A22: (len (f/^(k2-'1))-'(k1-'k2+1))+(k1-'k2+1)
    =len f-'k1+(k1-'k2+1) by A19,RFINSEQ:42
      .=len f-k1+(k1-k2+1) by A13,A15,XREAL_1:235
      .=len f-(k2-1)
      .=len f-(k2-'1) by A1,XREAL_1:235
      .=len f-'(k2-'1) by A1,NAT_D:50,XREAL_1:235
      .=len (f/^(k2-'1)) by RFINSEQ:42;
A23: (len f-'k2+1)-'(len f-'k1+1)+1
    =(len f-'k2+1)-(len f-'k1+1)+1 by A18,XREAL_1:235
      .=(len f-k2+1)-(len f-k1+1)+1 by A1,A14,XREAL_1:235
      .=k1-k2+1
      .=k1-'k2+1 by A13,XREAL_1:235;
    len f-k2>len f-k1 by A13,REAL_2:105;
    then len f-'k2+1>len f-'k1+1 by A14,A15,XREAL_1:8;
    then mid(Rev f,len f-'k2+1,len f-'k1+1)
    =Rev((((Rev f)/^((len f-'k1+1)-'1))|(k1-'k2+1))) by A23,Def1

.=Rev((Rev f/^(len f-'k1))|(len f-'(k2-'1)-'(len f-'k1))) by A21,NAT_D:34
      .=Rev((Rev f|(len f-'(k2-'1)))/^(len f-'k1)) by FINSEQ_5:83
      .=Rev((Rev f|(len f-'(k2-'1)))/^(len (f/^(k2-'1))-'(k1-'k2+1)))
    by A19,RFINSEQ:42

.=Rev((Rev (f/^(k2-'1)))/^(len (f/^(k2-'1))-'(k1-'k2+1))) by A16,FINSEQ_6:90
      .=Rev(Rev (((f/^(k2-'1))|(k1-'k2+1)))) by A22,FINSEQ_6:91
      .= Rev mid(f,k1,k2) by A13,Def1;
    hence Rev mid(f,k1,k2)=mid(Rev f,len f-'k2+1,len f-'k1+1);
  end;
end;

theorem Th23:
  for n,m being Element of NAT,f being FinSequence of D st
  1<= m &m+n<=len f holds (f/^n).m=f.(m+n)
proof
  let n,m be Element of NAT,f be FinSequence of D;
  assume
A1: 1<= m & m+n<=len f;
  n<=n+m by NAT_1:11;
  then
A2: n<=len f by A1,XXREAL_0:2;
  then
A3: len (f/^n) = len f -n by RFINSEQ:def 2;
  m+n-n<=len f -n by A1,XREAL_1:11;
  then m in dom (f/^n) by A1,A3,FINSEQ_3:27;
  hence (f/^n).m=f.(m+n) by A2,RFINSEQ:def 2;
end;

theorem Th24:
  for i being Element of NAT,f being FinSequence of D st
  1<=i & i<=len f holds (Rev f).i=f.(len f -i+1)
proof
  let i be Element of NAT,f be FinSequence of D;
  assume 1<=i & i<=len f;
  then i in dom (f) by FINSEQ_3:27;
  hence (Rev f).i=f.(len f -i+1) by FINSEQ_5:61;
end;

theorem
  for f being FinSequence of D,k being Nat st
  1<=k holds mid(f,1,k)=f|k
proof
  let f be FinSequence of D,k be Nat;
  assume
A1: 1<=k;
  then
A2: mid(f,1,k)=(f/^(1-'1))|(k-'1+1) by Def1;
  1-'1=0 by XREAL_1:234;
  then f/^(1-'1)=f by FINSEQ_5:31;
  hence mid(f,1,k)=f|k by A1,A2,XREAL_1:237;
end;

theorem Th26:
  for f being FinSequence of D,k being Element of NAT st
  k<=len f holds mid(f,k,len f)=f/^(k-'1)
proof
  let f be FinSequence of D,k be Element of NAT;
  assume
A1: k<=len f;
  then
A2: mid(f,k,len f)=(f/^(k-'1))|(len f-'k+1) by Def1;
  k-'1<=len f by A1,NAT_D:44;
  then
A3: len (f/^(k-'1))=len f-(k-'1) by RFINSEQ:def 2;
A4: len f-'k+1=len f-k+1 by A1,XREAL_1:235
    .=len f -(k-1);
  now per cases;
    case
A5:   k=0;
      then len f-'k+1=len f +1 by NAT_D:40;
      then len f <= len f-'k+1 by NAT_1:11;
      then
A6:   Seg len f c= Seg (len f -'k+1) by FINSEQ_1:7;
      0-1<0;
      then k-'1=0 by A5,XREAL_0:def 2;
      then f/^(k-'1)=f by FINSEQ_5:31;
      then dom (f/^(k-'1)) c= Seg (len f -'k+1) by A6,FINSEQ_1:def 3;
      then (f/^(k-'1))|Seg (len f -'k+1)= f/^(k-'1) by RELAT_1:97;
      hence mid(f,k,len f)=f/^(k-'1) by A2,FINSEQ_1:def 15;
    end;
    case k<>0;
      then k>0;
      then 0+1<=k by NAT_1:13;
      then len (f/^(k-'1))=len f-'k+1 by A3,A4,XREAL_1:235;

hence mid(f,k,len f)=(f/^(k-'1))|Seg len (f/^(k-'1)) by A2,FINSEQ_1:def 15
        .=(f/^(k-'1))|dom (f/^(k-'1)) by FINSEQ_1:def 3
        .=(f/^(k-'1)) by RELAT_1:97;
    end;
  end;
  hence mid(f,k,len f)=f/^(k-'1);
end;

theorem Th27:
  for f being FinSequence of D,k1,k2 being Element of NAT st
  1<=k1 & k1<=len f & 1<=k2 & k2<=len f holds mid(f,k1,k2).1=f.k1 &
  (k1<=k2 implies len mid(f,k1,k2) = k2 -' k1 +1 &
  for i being Element of NAT st 1<=i & i<=len mid(f,k1,k2)
  holds mid(f,k1,k2).i=f.(i+k1-'1))
  & (k1>k2 implies len mid(f,k1,k2) = k1 -' k2 +1 &
  for i being Element of NAT st 1<=i & i<=len mid(f,k1,k2)
  holds mid(f,k1,k2).i=f.(k1-'i+1))
proof
  let f be FinSequence of D,k11,k21 be Element of NAT;
  assume
A1: 1<=k11 & k11<=len f & 1<=k21 & k21<=len f;
A2: now
    let k1,k2 be Element of NAT;
    now
      assume
A3:   k1<=k2 & 1<=k1 & k1<=len f;
      then
A4:   mid(f,k1,k2).1=((f/^(k1-'1))|(k2-'k1+1)).1 by Def1
        .=((f/^(k1-'1))|(Seg (k2-'k1+1))).1 by FINSEQ_1:def 15;
      k1-1>=0 by A3,XREAL_1:50;
      then
A5:   1+(k1-'1)=1+(k1-1) by XREAL_0:def 2
        .=k1;
      then 1+(k1-'1)<=len f -(k1-'1)+(k1-'1) by A3;
      then
A6:   1<=len f - (k1-'1) by XREAL_1:8;
A7:   k1-'1<=len f by A3,NAT_D:44;
      then
A8:   len (f/^(k1-'1))=len f -(k1-'1) by RFINSEQ:def 2;
      0+1<=k2-'k1+1 by XREAL_1:8;

then 1 in Seg (k2-'k1+1) & 1 in Seg(len (f/^(k1-'1))) by A6,A8,FINSEQ_1:3;
      then
A9:   1 in dom (f/^(k1-'1)) & 1 in Seg(k2-'k1+1) by FINSEQ_1:def 3;
      then 1 in dom (f/^(k1-'1)) /\ Seg(k2-'k1+1) by XBOOLE_0:def 4;
      then 1 in dom ((f/^(k1-'1))|(Seg (k2-'k1+1))) by RELAT_1:90;
      then mid(f,k1,k2).1=(f/^(k1-'1)).1 by A4,FUNCT_1:70;
      hence mid(f,k1,k2).1=f.k1 by A5,A7,A9,RFINSEQ:def 2;
    end;
    hence k1<=k2 & 1<=k1 & k1<=len f implies mid(f,k1,k2).1=f.k1;
  end;
  thus mid(f,k11,k21).1=f.k11
  proof per cases;
    suppose k11<=k21;
      hence mid(f,k11,k21).1=f.k11 by A1,A2;
    end;
    suppose
A10:  k11>k21;
A11:  1<=(k11-'k21+1) by NAT_1:11;
      k21-'1<=len f by A1,NAT_D:44;
      then
A12:  len (f/^(k21-'1))=len f -(k21-'1) by RFINSEQ:def 2;
A13:  k21-1>=0 by A1,XREAL_1:50;
A14:  k11-k21>=0 by A10,XREAL_1:50;
      then
A15:  k11-'k21=k11-k21 by XREAL_0:def 2;
      k11-'k21+1=k11-k21+1 by A14,XREAL_0:def 2
        .=k11-(k21-1)
        .=k11-(k21-'1) by A13,XREAL_0:def 2;
      then
A16:  k11-'k21+1 <=len (f/^(k21-'1)) by A1,A12,XREAL_1:11;
      then
A17:  len ((f/^(k21-'1))|(k11-'k21+1))=(k11-'k21+1) by FINSEQ_1:80;
      then
A18:  k11-'k21+1 in dom ((f/^(k21-'1))|(k11-'k21+1)) by A11,FINSEQ_3:27;
A19:  k11-'k21+1 in dom (f/^(k21-'1)) by A11,A16,FINSEQ_3:27;
A20:  k21-'1 +(k11-'k21+1)= k21-1+(k11-(k21-1)) by A13,A15,XREAL_0:def 2
        .=k11;
A21:  ((f/^(k21-'1))|(k11-'k21+1)).len ((f/^(k21-'1))|(k11-'k21+1))
      =((f/^(k21-'1))|(k11-'k21+1))/.(k11-'k21+1) by A11,A17,FINSEQ_4:24
        .=(f/^(k21-'1))/.(k11-'k21+1) by A18,FINSEQ_4:85
        .=f/.((k21-'1 +(k11-'k21+1))) by A19,FINSEQ_5:30
        .=f.k11 by A1,A20,FINSEQ_4:24;
      1 in dom ((f/^(k21-'1))|(k11-'k21+1)) by A11,A17,FINSEQ_3:27;
      then (Rev ((f/^(k21-'1))|(k11-'k21+1))).1 = ((f/^(k21-'1))|(k11-'k21+1))
      .(len ((f/^(k21-'1))|(k11-'k21+1)) - 1 + 1) by FINSEQ_5:61
        .= f.k11 by A21;
      hence mid(f,k11,k21).1=f.k11 by A10,Def1;
    end;
  end;
  thus k11<=k21 implies len mid(f,k11,k21) = k21 -' k11 +1 &
  for i being Element of NAT st 1<=i & i<=len mid(f,k11,k21)
  holds mid(f,k11,k21).i=f.(i+k11-'1)
  proof
    assume
A22: k11<=k21;
    then
A23: mid(f,k11,k21)=(f/^(k11-'1))|(k21-'k11+1) by Def1;
    k11-'1<=len f by A1,NAT_D:44;
    then
A24: len (f/^(k11-'1))=len f -(k11-'1) by RFINSEQ:def 2;
A25: k11-1>=0 by A1,XREAL_1:50;
    then
A26: k11-'1=k11-1 by XREAL_0:def 2;
A27: k21-k11>=0 by A22,XREAL_1:50;
    then
A28: k21-'k11=k21-k11 by XREAL_0:def 2;
    k21-'k11+1=k21-k11+1 by A27,XREAL_0:def 2
      .=k21-(k11-1)
      .=k21-(k11-'1) by A25,XREAL_0:def 2;
    then
A29: k21-'k11+1 <=len (f/^(k11-'1)) by A1,A24,XREAL_1:11;
    then
A30: len ((f/^(k11-'1))|(k21-'k11+1))=(k21-'k11+1) by FINSEQ_1:80;
    for i being Element of NAT st 1<=i & i<=len mid(f,k11,k21)
    holds mid(f,k11,k21).i=f.(i+k11-'1)
    proof
      let i be Element of NAT;
      assume
A31:  1<=i & i<=len mid(f,k11,k21);
      then
A32:  i+(k11-1)<=k21-(k11-1)+(k11-1) by A23,A28,A30,XREAL_1:8;
A33:  i<= (k21-'k11+1) & (k21-'k11+1)<=len (f/^(k11-'1))
      by A23,A29,A31,FINSEQ_1:80;
A34:  1<=i & i+(k11-'1)<=len f by A1,A26,A31,A32,XXREAL_0:2;
A35:  k11<=k11+i by NAT_1:11;
A36:  i+(k11-'1)=i+(k11-1) by A25,XREAL_0:def 2
        .=i+k11-1
        .=i+k11-'1 by A1,A35,XREAL_1:235,XXREAL_0:2;
      mid(f,k11,k21).i=(f/^(k11-'1))|(k21-'k11+1).i by A22,Def1
        .=(f/^(k11-'1)).i by A33,FINSEQ_3:121
        .=f.(i+(k11-'1)) by A34,Th23;
      hence mid(f,k11,k21).i=f.(i+k11-'1) by A36;
    end;
    hence len mid(f,k11,k21) = k21 -' k11 +1 &
    for i being Element of NAT st 1<=i & i<=len mid(f,k11,k21)
    holds mid(f,k11,k21).i=f.(i+k11-'1) by A23,A29,FINSEQ_1:80;
  end;
  assume
A37: k11>k21;
  then
A38: mid(f,k11,k21)=Rev ((f/^(k21-'1))|(k11-'k21+1)) by Def1;
  then
A39: len mid(f,k11,k21)=len ((f/^(k21-'1))|(k11-'k21+1)) by FINSEQ_5:def 3;
  k21-'1<=len f by A1,NAT_D:44;
  then
A40: len (f/^(k21-'1))=len f -(k21-'1) by RFINSEQ:def 2;
A41: k21-1>=0 by A1,XREAL_1:50;
  then
A42: k21-'1=k21-1 by XREAL_0:def 2;
A43: k11-k21>=0 by A37,XREAL_1:50;
  then
A44: k11-'k21=k11-k21 by XREAL_0:def 2;
  k11-'k21+1=k11-k21+1 by A43,XREAL_0:def 2
    .=k11-(k21-1)
    .=k11-(k21-'1) by A41,XREAL_0:def 2;
  then
A45: k11-'k21+1 <=len (f/^(k21-'1)) by A1,A40,XREAL_1:11;
  then
A46: len ((f/^(k21-'1))|(k11-'k21+1))=(k11-'k21+1) by FINSEQ_1:80; thus
A47: len mid(f,k11,k21) = k11 -' k21 +1 by A39,A45,FINSEQ_1:80;
  thus for i being Element of NAT st 1<=i & i<=len mid(f,k11,k21)
  holds mid(f,k11,k21).i=f.(k11-'i+1)
  proof
    let i be Element of NAT;
    assume
A48: 1<=i & i<=len mid(f,k11,k21);
    then
A49: i<=k11-'k21+1 by A39,A45,FINSEQ_1:80;
A50: i+(k21-1)<=k11-(k21-1)+(k21-1) by A44,A47,A48,XREAL_1:8;
A51: i<=i+(k21-'1) by NAT_1:11;
    then
A52: i<=k11 by A42,A50,XXREAL_0:2;
A53: k11-'k21+1-'i+1+(k21-'1) =k11-'k21+1-i+1+(k21-'1) by A47,A48,XREAL_1:235
      .= k11-(k21-1)-i+1+(k21-1) by A41,A44,XREAL_0:def 2
      .=k11-i+1
      .=k11-'i+1 by A42,A50,A51,XREAL_1:235,XXREAL_0:2;
    k11-'k21+1<= k11-'k21+1+(i-'1) by NAT_1:11;
    then k11-'k21+1<= k11-'k21+1+(i-1) by A48,XREAL_1:235;
    then k11-'k21+1-(i-1)<= k11-'k21+1+(i-1)-(i-1) by XREAL_1:11;
    then k11-'k21+1-i+1<= k11-'k21+1;
    then
A54: (k11-'k21+1-'i+1)<=(k11-'k21+1) by A49,XREAL_1:235;
A55: 1+k11<=i+k11 by A48,XREAL_1:8;
    i+k11<=i+len f by A1,XREAL_1:8;
    then 1+k11<=i+len f by A55,XXREAL_0:2;
    then 1+k11-i<=i+len f -i by XREAL_1:11;
    then k11-i+1<=len f;
    then
A56: 1<=k11-'k21+1-'i+1 & k11-'k21+1-'i+1+(k21-'1)<= len f
    by A52,A53,NAT_1:11,XREAL_1:235;
    mid(f,k11,k21).i=((f/^(k21-'1))|(k11-'k21+1))
    .(len ((f/^(k21-'1))|(k11-'k21+1)) -i+1) by A38,A39,A48,Th24
      .=((f/^(k21-'1))|(k11-'k21+1)) .(len ((f/^(k21-'1))|(k11-'k21+1)) -'i+1)
    by A39,A48,XREAL_1:235
      .=(f/^(k21-'1)).(k11-'k21+1-'i+1) by A46,A54,FINSEQ_3:121
      .=f.(k11-'i+1) by A53,A56,Th23;
    hence mid(f,k11,k21).i=f.(k11-'i+1);
  end;
end;

theorem Th28:
  for f being FinSequence of D,k1,k2 being Element of NAT holds
  rng mid(f,k1,k2) c= rng f
proof
  let f be FinSequence of D,k1,k2 be Element of NAT;
  per cases;
  suppose k1<=k2;
    then mid(f,k1,k2) = (f/^(k1-'1))|(k2-'k1+1) by Def1;
    then
A1: rng mid(f,k1,k2) c= rng (f/^(k1-'1)) by FINSEQ_5:21;
    rng (f/^(k1-'1)) c= rng f by FINSEQ_5:36;
    hence rng mid(f,k1,k2) c= rng f by A1,XBOOLE_1:1;
  end;
  suppose k1>k2;
    then mid(f,k1,k2) = Rev ((f/^(k2-'1))|(k1-'k2+1)) by Def1;
    then rng mid(f,k1,k2) = rng ((f/^(k2-'1))|(k1-'k2+1)) by FINSEQ_5:60;
    then
A2: rng mid(f,k1,k2) c= rng (f/^(k2-'1)) by FINSEQ_5:21;
    rng (f/^(k2-'1)) c= rng f by FINSEQ_5:36;
    hence rng mid(f,k1,k2) c= rng f by A2,XBOOLE_1:1;
  end;
end;

theorem
  for f being FinSequence of D st 1<=len f holds mid(f,1,len f)=f
proof
  let f be FinSequence of D;
  assume
A1: 1<=len f;
  then mid(f,1,len f)=(f/^(1-'1))|(len f-'1+1) by Def1
    .=(f/^0)|(len f-'1+1) by XREAL_1:234
    .=f|(len f-'1+1) by FINSEQ_5:31
    .=f|len f by A1,XREAL_1:237
    .=f by FINSEQ_1:79;
  hence mid(f,1,len f)=f;
end;

theorem
  for f being FinSequence of D st 1<=len f holds mid(f,len f,1)=Rev f
proof
  let f be FinSequence of D;
  assume
A1: 1<=len f;
A2: 1-'1=0 by XREAL_1:234;
  per cases;
  suppose len f<>1;
    then 1<len f by A1,XXREAL_0:1;
    then mid(f,len f,1)=Rev ((f/^(1-'1))|(len f-'1+1)) by Def1
      .=Rev ((f/^0)|len f) by A1,A2,XREAL_1:237
      .=Rev (f|len f) by FINSEQ_5:31
      .=Rev f by FINSEQ_1:79;
    hence mid(f,len f,1)=Rev f;
  end;
  suppose
A3: len f=1;
    then
A4: mid(f,len f,1)=(f/^(1-'1))|(1-'1+1) by Def1
      .=f|1 by A2,FINSEQ_5:31;
    f|1=f by A3,FINSEQ_1:79;
    hence mid(f,len f,1)=Rev f by A1,A4,Th18,FINSEQ_1:80;
  end;
end;

theorem Th31:
  for f being FinSequence of D, k1,k2,i being Element of NAT
  st 1<=k1 & k1<=k2 & k2<=len f & 1<=i &
  (i<=k2-'k1+1 or i<=k2-k1+1 or i<=k2+1-k1) holds
  mid(f,k1,k2).i=f.(i+k1-'1) & mid(f,k1,k2).i=f.(i-'1+k1) &
  mid(f,k1,k2).i=f.(i+k1-1) & mid(f,k1,k2).i=f.(i-1+k1)
proof
  let f be FinSequence of D, k1,k2,i be Element of NAT;
  assume
A1: 1<=k1 & k1<=k2 & k2<=len f & 1<=i &
  (i<=k2-'k1+1 or i<=k2-k1+1 or i<=k2+1-k1);
  then
A2: 1<=k2 by XXREAL_0:2;
A3: k1<=len f by A1,XXREAL_0:2;
A4: i-1>=1-1 by A1,XREAL_1:11;
  i+k1>=1+k1 by A1,XREAL_1:8;
  then
A5: i+k1-1>=1+k1-1 by XREAL_1:11;
A6: len mid(f,k1,k2)=k2-'k1+1 by A1,A2,A3,Th27;
A7: i<=k2-k1+1 implies i<=k2-'k1+1 by A1,XREAL_1:235;
  i-'1+k1=i-1+k1 by A4,XREAL_0:def 2
    .=i+k1-'1 by A5,XREAL_0:def 2;
  hence
A8: mid(f,k1,k2).i=f.(i+k1-'1) & mid(f,k1,k2).i=f.(i-'1+k1)
  by A1,A2,A3,A6,A7,Th27;
  hence mid(f,k1,k2).i=f.(i+k1-1) by A5,XREAL_0:def 2;
  thus mid(f,k1,k2).i=f.(i-1+k1) by A1,A8,XREAL_1:235;
end;

theorem Th32:
  for f being FinSequence of D,k,i being Nat
  st 1<=i & i<=k & k<=len f holds mid(f,1,k).i=f.i
proof
  let f be FinSequence of D,k,i be Nat;
  assume
A1: 1<=i & i<=k & k<=len f;
  then
A2: 1<=k & k<=len f & 1<=i & i<=k by XXREAL_0:2;
X: i in NAT & k in NAT by ORDINAL1:def 13;
  i<=k-1+1 by A1;
  then mid(f,1,k).i=f.(i+1-'1) by A2,Th31,X;
  hence thesis by NAT_D:34;
end;

theorem
  for f being FinSequence of D, k1,k2 being Element of NAT
  st 1<=k1 & k1<=k2 & k2<=len f holds len mid(f,k1,k2)<=len f
proof
  let f be FinSequence of D, k1,k2 be Element of NAT;
  assume
A1: 1<=k1 & k1<=k2 & k2<=len f;
  then
A2: 1<=k2 by XXREAL_0:2;
  k1<=len f by A1,XXREAL_0:2;
  then
A3: len mid(f,k1,k2)=k2-'k1+1 by A1,A2,Th27;
  k2-k1>=0 by A1,XREAL_1:50;
  then
A4: k2-'k1+1=k2-k1+1 by XREAL_0:def 2
    .=k2-(k1-1);
  k1-1>=0 by A1,XREAL_1:50;
  then
A5: k1-1=k1-'1 by XREAL_0:def 2;
  k2<=k2+(k1-'1) by NAT_1:11;
  then k2-(k1-'1)<=k2+(k1-'1)-(k1-'1) by XREAL_1:11;
  hence len mid(f,k1,k2)<=len f by A1,A3,A4,A5,XXREAL_0:2;
end;

theorem
  for f being FinSequence of TOP-REAL n st 2<=len f
  holds f.1 in L~f & f/.1 in L~f & f.len f in L~f & f/.len f in L~f
proof
  let f be FinSequence of TOP-REAL n;
  assume
A1: 2<=len f;
  then
A2: 1+1<=len f;
  f/.1 in LSeg(f/.1,f/.(1+1)) by TOPREAL1:6;
  then
A3: f/.1 in LSeg(f,1) by A1,TOPREAL1:def 5;
  LSeg(f,1) in {LSeg(f,i):1<=i & i+1<=len f} by A2;
  then f/.1 in union{LSeg(f,i):1<=i & i+1<=len f} by A3,TARSKI:def 4;
  then
A4: f/.1 in L~f by TOPREAL1:def 6;
A5: 1<=len f by A2,NAT_D:46;
A6: 1<=len f-'1 by A2,NAT_D:49;
A7: len f-'1+1=len f by A5,XREAL_1:237;
  then f/.len f in LSeg(f/.(len f-'1),f/.(len f-'1+1)) by TOPREAL1:6;
  then
A8: f/.len f in LSeg(f,len f-'1) by A6,A7,TOPREAL1:def 5;
  LSeg(f,len f-'1) in {LSeg(f,i):1<=i & i+1<=len f} by A6,A7;
  then f/.len f in union{LSeg(f,i):1<=i & i+1<=len f} by A8,TARSKI:def 4;
  then f/.len f in L~f by TOPREAL1:def 6;
  hence f.1 in L~f & f/.1 in L~f & f.len f in L~f & f/.len f in L~f
  by A4,A5,FINSEQ_4:24;
end;

theorem Th35:
  for p1,p2,q1,q2 being Point of TOP-REAL 2 st
  (p1`1 = p2`1 or p1`2 = p2`2)& q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2)
  holds q1`1 = q2`1 or q1`2 = q2`2
proof
  let p1,p2,q1,q2 be Point of TOP-REAL 2;
  assume
A1: (p1`1 = p2`1 or p1`2 = p2`2)& q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2);
  then q1 in { (1-r)*p1 + r*p2 : 0 <= r & r <= 1 } by TOPREAL1:def 3;
  then consider r1 such that
A2: q1= (1-r1)*p1 + r1*p2 & 0 <= r1 & r1 <= 1;
  q2 in { (1-r)*p1 + r*p2 : 0 <= r & r <= 1 } by A1,TOPREAL1:def 3;
  then consider r2 such that
A3: q2= (1-r2)*p1 + r2*p2 & 0 <= r2 & r2 <= 1;
  q1`1=((1-r1)*p1)`1+(r1*p2)`1 by A2,TOPREAL3:7;
  then q1`1=(1-r1)*(p1`1)+(r1*p2)`1 by TOPREAL3:9;
  then
A4: q1`1=(1-r1)*(p1`1)+r1*(p2`1) by TOPREAL3:9;
  q1`2=((1-r1)*p1)`2+(r1*p2)`2 by A2,TOPREAL3:7;
  then q1`2=(1-r1)*(p1`2)+(r1*p2)`2 by TOPREAL3:9;
  then
A5: q1`2=(1-r1)*(p1`2)+r1*(p2`2) by TOPREAL3:9;
  q2`1=((1-r2)*p1)`1+(r2*p2)`1 by A3,TOPREAL3:7;
  then q2`1=(1-r2)*(p1`1)+(r2*p2)`1 by TOPREAL3:9;
  then
A6: q2`1=(1-r2)*(p1`1)+r2*(p2`1) by TOPREAL3:9;
  q2`2=((1-r2)*p1)`2+(r2*p2)`2 by A3,TOPREAL3:7;
  then q2`2=(1-r2)*(p1`2)+(r2*p2)`2 by TOPREAL3:9;
  then
A7: q2`2=(1-r2)*(p1`2)+r2*(p2`2) by TOPREAL3:9;
  per cases by A1;
  suppose p1`1 = p2`1;
    hence thesis by A4,A6;
  end;
  suppose p1`2 = p2`2;
    hence thesis by A5,A7;
  end;
end;

theorem Th36:
  for p1,p2,q1,q2 being Point of TOP-REAL 2 st
  (p1`1 = p2`1 or p1`2 = p2`2)& LSeg(q1,q2) c= LSeg(p1,p2)
  holds q1`1 = q2`1 or q1`2 = q2`2
proof
  let p1,p2,q1,q2 be Point of TOP-REAL 2;
  q1 in LSeg(q1,q2) & q2 in LSeg(q1,q2) by TOPREAL1:6;
  hence thesis by Th35;
end;

theorem Th37:
  for f being FinSequence of TOP-REAL 2,n being Element of NAT
  st 2<=n & f is_S-Seq holds f|n is_S-Seq
proof
  let f be FinSequence of TOP-REAL 2,n be Element of NAT;
  assume
A1: 2<=n & f is_S-Seq;
  then
A2: f is one-to-one & len f >= 2
  & f is unfolded s.n.c. special by TOPREAL1:def 10;
  reconsider f'=f as s.n.c. special unfolded one-to-one
  FinSequence of TOP-REAL 2 by A1,TOPREAL1:def 10;
  now per cases;
    case n<=len f;
      hence len (f|n) >= 2 by A1,FINSEQ_1:80;
    end;
    case n>len f;
      hence len (f|n) >= 2 by A2,FINSEQ_1:79;
    end;
  end;
  then f'|n is one-to-one & len (f|n) >= 2 & f'|n is unfolded s.n.c. special;
  hence f|n is_S-Seq by TOPREAL1:def 10;
end;

theorem Th38:
  for f being FinSequence of TOP-REAL 2,n being Element of NAT
  st n<=len f & 2<=len f-'n & f is_S-Seq holds f/^n is_S-Seq
proof
  let f be FinSequence of TOP-REAL 2,n be Element of NAT;
  assume
A1: n<=len f & 2<=len f-'n & f is_S-Seq;
  then reconsider f' = f as
  one-to-one special s.n.c. unfolded FinSequence of TOP-REAL 2
  by TOPREAL1:def 10;
  len (f/^n)=len f-n by A1,RFINSEQ:def 2;
  then f'/^n is one-to-one & len (f'/^n) >= 2
  & f'/^n is unfolded s.n.c. special by A1,XREAL_1:235;
  hence f/^n is_S-Seq by TOPREAL1:def 10;
end;

theorem
  for f being FinSequence of TOP-REAL 2,k1,k2 being Element of NAT
  st f is_S-Seq & 1<=k1 & k1<=len f & 1<=k2 & k2<=len f & k1<>k2 holds
  mid(f,k1,k2) is_S-Seq
proof
  let f be FinSequence of TOP-REAL 2,k1,k2 be Element of NAT;
  assume
A1: f is_S-Seq & 1<=k1 & k1<=len f & 1<=k2 & k2<=len f & k1<>k2;
  per cases;
  suppose
A2: k1<=k2;
    then
A3: mid(f,k1,k2)=(f/^(k1-'1))|(k2-'k1+1) by Def1;
    k1<k2 by A1,A2,XXREAL_0:1;
    then
A4: k1+1<=k2 by NAT_1:13;
    then
A5: k1+1<=len f by A1,XXREAL_0:2;
A6: len f-'(k1-'1)= len f-(k1-'1) by A1,NAT_D:50,XREAL_1:235
      .=len f-(k1-1) by A1,XREAL_1:235
      .=len f-k1+1;
    k1+1-k1<=len f-k1 by A5,XREAL_1:11;
    then 1+1<=len f-k1+1 by XREAL_1:8;
    then
A7: f/^(k1-'1) is_S-Seq by A1,A6,Th38,NAT_D:50;
    k1+1-k1<=k2-k1 by A4,XREAL_1:11;
    then 1<=k2-'k1 by NAT_D:39;
    then 1+1<=k2-'k1+1 by XREAL_1:8;
    hence mid(f,k1,k2) is_S-Seq by A3,A7,Th37;
  end;
  suppose
A8: k1>k2;
    then
A9: mid(f,k1,k2)= Rev ((f/^(k2-'1))|(k1-'k2+1)) by Def1;
A10: k2+1<=k1 by A8,NAT_1:13;
    then
A11: k2+1<=len f by A1,XXREAL_0:2;
A12: len f-'(k2-'1)= len f-(k2-'1) by A1,NAT_D:50,XREAL_1:235
      .=len f-(k2-1) by A1,XREAL_1:235
      .=len f-k2+1;
    k2+1-k2<=len f-k2 by A11,XREAL_1:11;
    then 1+1<=len f-k2+1 by XREAL_1:8;
    then
A13: f/^(k2-'1) is_S-Seq by A1,A12,Th38,NAT_D:50;
    k2+1-k2<=k1-k2 by A10,XREAL_1:11;
    then 1<=k1-'k2 by NAT_D:39;
    then 1+1<=k1-'k2+1 by XREAL_1:8;
    then (f/^(k2-'1))|(k1-'k2+1) is S-Sequence_in_R2 by A13,Th37;
    hence mid(f,k1,k2) is_S-Seq by A9,SPPOL_2:47;
  end;
end;

begin

::---------------------------------------------------------:
:: A Concept of Index for Finite Sequences in TOP-REAL 2   :
::---------------------------------------------------------:

definition
  let f be FinSequence of TOP-REAL 2,p be Point of TOP-REAL 2;
  assume
A1: p in L~f;
  func Index(p,f) -> Element of NAT means
  :Def2:
  ex S being non empty Subset of NAT st
  it = min S & S = { i: p in LSeg(f,i) };
  existence
  proof
    set S = { i: p in LSeg(f,i) };
    consider i2 being Element of NAT such that 1<=i2 & i2+1<=len f and
A2: p in LSeg(f,i2) by A1,SPPOL_2:13;
A3: i2 in S by A2;
    S c= NAT
    proof
      let x be set;
      assume x in S;
      then ex i st x = i & p in LSeg(f,i);
      hence thesis;
    end;
    then reconsider S as non empty Subset of NAT by A3;
    take min S, S;
    thus thesis;
  end;
  uniqueness;
end;

theorem Th40:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2, i being Element of NAT st p in LSeg(f,i)
  holds Index(p,f) <= i
proof
  let f being FinSequence of TOP-REAL 2;
  let p being Point of TOP-REAL 2, i0 be Element of NAT;
A1: LSeg(f,i0) c= L~f by TOPREAL3:26;
  assume
A2: p in LSeg(f,i0);
  then consider S being non empty Subset of NAT such that
A3: Index(p,f) = min S and
A4: S = { i: p in LSeg(f,i) } by A1,Def2;
  i0 in S by A2,A4;
  hence Index(p,f) <= i0 by A3,XXREAL_2:def 7;
end;

theorem Th41:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st p in L~f
  holds 1<=Index(p,f) & Index(p,f) < len f
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume p in L~f;
  then consider S being non empty Subset of NAT such that
A1: Index(p,f) = min S and
A2: S = { i: p in LSeg(f,i) } by Def2;
  Index(p,f) in S by A1,XXREAL_2:def 7;
  then
A3: ex i st i = Index(p,f) & p in LSeg(f,i) by A2;
  hence 1 <= Index(p,f) by TOPREAL1:def 5;
  Index(p,f) + 1 <= len f by A3,TOPREAL1:def 5;
  hence thesis by NAT_1:13;
end;

theorem Th42:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st p in L~f holds p in LSeg(f,Index(p,f))
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume p in L~f;
  then consider S being non empty Subset of NAT such that
A1: Index(p,f) = min S and
A2: S = { i: p in LSeg(f,i) } by Def2;
  Index(p,f) in S by A1,XXREAL_2:def 7;
  then ex i st i = Index(p,f) & p in LSeg(f,i) by A2;
  hence p in LSeg(f,Index(p,f));
end;

theorem Th43:
  for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
  st p in LSeg(f,1) holds Index(p,f) = 1
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume
A1: p in LSeg(f,1);
  then
A2: Index(p,f) <= 1 by Th40;
  LSeg(f,1) c= L~f by TOPREAL3:26;
  then Index(p,f) >= 1 by A1,Th41;
  hence Index(p,f) = 1 by A2,XXREAL_0:1;
end;

theorem Th44:
  for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
  st len f >= 2 holds Index(f/.1,f) = 1
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume len f >= 2;
  then len f >= 1+1;
  then f/.1 in LSeg(f,1) by TOPREAL1:27;
  hence thesis by Th43;
end;

theorem Th45:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2,i1 being Nat st f is_S-Seq
  & 1<i1 & i1<=len f & p=f.i1 holds Index(p,f) + 1 = i1
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2,i1 be Nat;
  assume
A1: f is_S-Seq;
  assume
A2: 1<i1 & i1<=len f;
  then
A3: i1 in dom f by FINSEQ_3:27;
  consider j being Nat such that
A4: i1 = j+1 by A2,NAT_1:6;
  reconsider j as Element of NAT by ORDINAL1:def 13;
A5: 1 + 0 <= j by A2,A4,NAT_1:13;
  assume p=f.i1;
  then
A6: p = f/.i1 by A3,PARTFUN1:def 8;
  then
A7: p in LSeg(f,j) by A2,A4,A5,TOPREAL1:27;
  then
A8: Index(p,f) <= j by Th40;
  assume Index(p,f) + 1 <> i1;
  then Index(p,f) < j by A4,A8,XXREAL_0:1;
  then
A9: Index(p,f) + 1 <= j by NAT_1:13;
A10: LSeg(f,j) c= L~f by TOPREAL3:26;
  then
A11: p in LSeg(f,Index(p,f)) by A7,Th42;
  per cases by A9,XXREAL_0:1;
  suppose
A12: Index(p,f) + 1 = j;
A13: f is unfolded by A1,TOPREAL1:def 10;
    1 <= Index(p,f) & Index(p,f) + (1+1) <= len f by A2,A4,A7,A10,A12,Th41;
    then LSeg(f,Index(p,f)) /\ LSeg(f,j) = {f/.j} by A12,A13,TOPREAL1:def 8;
    then p in {f/.j} by A7,A11,XBOOLE_0:def 4;
    then
A14: p = f/.j by TARSKI:def 1;
A15: j < i1 by A4,NAT_1:13;
    j < len f by A2,A4,NAT_1:13;
    then
A16: j in dom f by A5,FINSEQ_3:27;
    f is one-to-one by A1,TOPREAL1:def 10;
    hence contradiction by A3,A6,A14,A15,A16,PARTFUN2:17;
  end;
  suppose
A17: Index(p,f) + 1 < j;
    p in LSeg(f,Index(p,f)) /\ LSeg(f,j) by A7,A11,XBOOLE_0:def 4;
    then
A18: LSeg(f,Index(p,f)) meets LSeg(f,j) by XBOOLE_0:4;
    f is s.n.c. by A1,TOPREAL1:def 10;
    hence contradiction by A17,A18,TOPREAL1:def 9;
  end;
end;

theorem Th46:
  for f be FinSequence of TOP-REAL 2 for p be Point of TOP-REAL 2
  for i1 be Element of NAT st f is s.n.c. & p in LSeg(f,i1) holds
  i1=Index(p,f) or i1=Index(p,f)+1
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  let i1 be Element of NAT;
  assume that
A1: f is s.n.c. and
A2: p in LSeg(f,i1);
A3: p in L~f by A2,SPPOL_2:17;
A4: Index(p,f) <= i1 by A2,Th40;
  assume
A5: not thesis;
  then Index(p,f) < i1 by A4,XXREAL_0:1;
  then Index(p,f)+1 <= i1 by NAT_1:13;
  then
A6: Index(p,f)+1 < i1 by A5,XXREAL_0:1;
  p in LSeg(f,Index(p,f)) by A3,Th42;
  then p in LSeg(f,Index(p,f)) /\ LSeg(f,i1) by A2,XBOOLE_0:def 4;
  then LSeg(f,Index(p,f)) meets LSeg(f,i1) by XBOOLE_0:4;
  hence contradiction by A1,A6,TOPREAL1:def 9;
end;

theorem Th47:
  for f be FinSequence of TOP-REAL 2 for p be Point of TOP-REAL 2
  for i1 be Element of NAT st f is unfolded s.n.c. &
  i1+1<=len f & p in LSeg(f,i1) & p <> f.i1 holds i1=Index(p,f)
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  let i1 be Element of NAT;
  assume that
A1: f is unfolded s.n.c. and
A2: i1+1<=len f and
A3: p in LSeg(f,i1);
  assume
A4: p <> f.i1;
A5: p in L~f by A3,SPPOL_2:17;
A6: Index(p,f) <= i1 by A3,Th40;
  p in LSeg(f,Index(p,f)) by A5,Th42;
  then
A7: p in LSeg(f,Index(p,f)) /\ LSeg(f,i1) by A3,XBOOLE_0:def 4;
A8: 1 <= Index(p,f) by A5,Th41;
A9: 1 <= Index(p,f)+1 by NAT_1:11;
  i1 < len f by A2,NAT_1:13;
  then Index(p,f) < len f by A6,XXREAL_0:2;
  then Index(p,f)+1 <= len f by NAT_1:13;
  then
A10: Index(p,f) + 1 in dom f by A9,FINSEQ_3:27;
  now
    assume
A11: i1 = Index(p,f)+1;
    then Index(p,f) + (1+1) <= len f by A2;
    then p in {f/.(Index(p,f)+1)} by A1,A7,A8,A11,TOPREAL1:def 8;
    then p = f/.(Index(p,f)+1) by TARSKI:def 1;
    hence contradiction by A4,A10,A11,PARTFUN1:def 8;
  end;
  hence thesis by A1,A3,Th46;
end;

definition
  let g be FinSequence of TOP-REAL 2, p1,p2 be Point of TOP-REAL 2;
  pred g is_S-Seq_joining p1,p2 means
  :Def3:
  g is_S-Seq & g.1 = p1 & g.len g = p2;
end;

theorem Th48:
  for g being FinSequence of TOP-REAL 2, p1,p2 being Point of TOP-REAL 2 st
  g is_S-Seq_joining p1,p2 holds Rev g is_S-Seq_joining p2,p1
proof
  let g be FinSequence of TOP-REAL 2, p1,p2 be Point of TOP-REAL 2;
  assume that
A1: g is_S-Seq and
A2: g.1=p1 and
A3: g.len g=p2;
  thus Rev g is_S-Seq by A1,SPPOL_2:47;
  thus (Rev g).1 = p2 by A3,FINSEQ_5:65;
  dom g = dom Rev g by FINSEQ_5:60;
  hence (Rev g).len Rev g = (Rev g).len g by FINSEQ_3:31
    .= p1 by A2,FINSEQ_5:65;
end;

theorem Th49:
  for f,g being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2,j be Nat st p in L~f
  & g=<*p*>^mid(f,Index(p,f)+1,len f) & 1<=j & j+1<=len g
  holds LSeg(g,j) c= LSeg(f,Index(p,f)+j-'1)
proof
  let f,g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2,j be Nat;
  assume that
A1: p in L~f and
A2: g=<*p*>^mid(f,Index(p,f)+1,len f) and
A3: 1<=j and
A4: j+1<=len g;
  consider i such that
A5: 1<=i & i+1<=len f & p in LSeg(f,i) by A1,SPPOL_2:13;
  1<=i+1 by NAT_1:12;
  then
A6: 1<=len f by A5,XXREAL_0:2;
A7: len g=(len <*p*>)+ len(mid(f,Index(p,f)+1,len f)) by A2,FINSEQ_1:35
    .=1+ len(mid(f,Index(p,f)+1,len f)) by FINSEQ_1:56;
  then
A8: 1<=len g by NAT_1:11;
A9: Index(p,f)<len f by A1,Th41;
  then Index(p,f)+1<=len f by NAT_1:13;
  then Index(p,f)+1-Index(p,f)<=len f - Index(p,f) by XREAL_1:11;
  then
A10: 1-1<=len f - Index(p,f)-1 by XREAL_1:11;
A11: Index(p,f)+1<=len f by A9,NAT_1:13;
A12: 0+1<=Index(p,f)+1 by NAT_1:13;
  then
A13: len(mid(f,Index(p,f)+1,len f))=len f -' (Index(p,f)+1)+1 by A6,A11,Th27;
A14: 1<=Index(p,f)+1 & Index(p,f)+1<=len f & 1<=len f & len f<=len f
  by A9,A12,NAT_1:13;
A15: len g=1+(len f -(Index(p,f)+1)+1) by A7,A10,A13,XREAL_0:def 2
    .=1+(len f - Index(p,f));
A16: len f -'(Index(p,f)+1)=len f -(Index(p,f)+1) by A10,XREAL_0:def 2
    .=len f - Index(p,f) -1;
  len g=len <*p*> + len mid(f,Index(p,f)+1,len f) by A2,FINSEQ_1:35;
  then
A17: len g=1+len mid(f,Index(p,f)+1,len f) by FINSEQ_1:56;
A18: j<=len g by A4,NAT_1:13;
A19: 1<=j+1 by A3,NAT_1:13;
A20: j-'1=j-1 by A3,XREAL_1:235;
A21: j+1-'1=j+1-1 by A19,XREAL_1:235;
A22: j=1+(j-1)
    .=len <*p*> +(j-'1) by A20,FINSEQ_1:56;
A23: j+1=len <*p*> +(j+1-1) by FINSEQ_1:56
    .=len <*p*> +(j+1-'1) by A19,XREAL_1:235;
  1<=Index(p,f)+j by A3,NAT_1:12;
  then
A24: 1-1<=Index(p,f)+j-1 by XREAL_1:11;
  j+1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A4,A17,XREAL_1:11;
  then j+Index(p,f)<= len f -Index(p,f)+Index(p,f) by A13,A16,XREAL_1:8;
  then Index(p,f)+(j-1)+1 <= len f;
  then
A25: Index(p,f)+j-'1+1 <= len f by A24,XREAL_0:def 2;
A26: j+1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A4,A17,XREAL_1:11;
  then
A27: j+1-'1 in dom mid(f,Index(p,f)+1,len f) by A3,A21,FINSEQ_3:27;
  j-'1<=j by NAT_D:35;
  then
A28: j-'1<=len mid(f,Index(p,f)+1,len f) by A26,XXREAL_0:2;
A29: g.(j+1) =mid(f,Index(p,f)+1,len f).(j+1-'1) by A2,A23,A27,FINSEQ_1:def 7
    .=f.(j+1-'1+(Index(p,f)+1)-'1) by A3,A14,A21,A26,Th27
    .=f.(j+1-'1+1+Index(p,f)-'1)
    .=f.(j+1+Index(p,f)-'1) by A19,XREAL_1:237
    .=f.(Index(p,f)+j+1-'1)
    .=f.(Index(p,f)+j) by NAT_D:34
    .=f.(Index(p,f)+j-'1+1) by A3,NAT_1:12,XREAL_1:237;
  j<=len f - Index(p,f) by A4,A15,XREAL_1:8;
  then
A30: j+Index(p,f)<=len f - Index(p,f)+Index(p,f) by XREAL_1:8;
  then
A31: Index(p,f)+j-'1+1<=len f by A3,NAT_1:12,XREAL_1:237;
  g/.(j+1)=g.(j+1) by A4,A19,FINSEQ_4:24;
  then
A32: f/.(Index(p,f)+j-'1+1)=g/.(j+1) by A29,A31,FINSEQ_4:24,NAT_1:11;
  1 <= Index(p,f) by A1,Th41;
  then 1+1 <= Index(p,f)+j by A3,XREAL_1:9;
  then 1<=Index(p,f)+j-1 by XREAL_1:21;
  then
A33: 1<=Index(p,f)+j-'1 by NAT_D:39;
  then
A34: LSeg(f,Index(p,f)+j-'1)
  =LSeg(f/.(Index(p,f)+j-'1),f/.(Index(p,f)+j-'1+1)) by A25,TOPREAL1:def 5;
  now per cases by A3,XXREAL_0:1;
    case
A35:  1<j;
      then
A36:  j-'1=j-1 by XREAL_1:235;
      then
A37:  1<=j-'1 by A35,SPPOL_1:6;
      j-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A17,A18,XREAL_1:11;
      then j-'1 in dom mid(f,Index(p,f)+1,len f) by A36,A37,FINSEQ_3:27;
      then
A38:  g.j =mid(f,Index(p,f)+1,len f).(j-'1) by A2,A22,FINSEQ_1:def 7
        .=f.(j-'1+(Index(p,f)+1)-'1) by A14,A28,A37,Th27
        .=f.(j-'1+1+Index(p,f)-'1)
        .=f.(Index(p,f)+j-'1) by A3,XREAL_1:237;
      g/.j=g.j by A3,A18,FINSEQ_4:24;
      then LSeg(f,Index(p,f)+j-'1)
      =LSeg(g/.j,g/.(j+1)) by A30,A32,A33,A34,A38,FINSEQ_4:24,NAT_D:50
        .=LSeg(g,j) by A3,A4,TOPREAL1:def 5;
      hence thesis;
    end;
    case
A39:  1=j;
      then 1<=j & j<=len <*p*> by FINSEQ_1:56;
      then j in dom <*p*> by FINSEQ_3:27;
      then
A40:  g.j =<*p*>.j by A2,FINSEQ_1:def 7
        .=p by A39,FINSEQ_1:57;
A41:  g/.j=g.j by A8,A39,FINSEQ_4:24;
A42:  f/.(Index(p,f)+j-'1+1)
      in LSeg(f/.(Index(p,f)+j-'1),f/.(Index(p,f)+j-'1+1)) by TOPREAL1:6;
A43:  Index(p,f)+j-'1 = Index(p,f) by A39,NAT_D:34;
      p in LSeg(f,Index(p,f)) by A1,Th42;
      then LSeg(p,g/.(j+1))
      c= LSeg(f,Index(p,f)+j-'1) by A32,A34,A42,A43,TOPREAL1:12;
      hence thesis by A3,A4,A40,A41,TOPREAL1:def 5;
    end;
  end;
  hence LSeg(g,j) c= LSeg(f,Index(p,f)+j-'1);
end;

theorem
  for f,g being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
  st f is_S-Seq & p in L~f & p<>f.(Index(p,f)+1) &
  g=<*p*>^mid(f,Index(p,f)+1,len f) holds g is_S-Seq_joining p,f/.len f
proof
  let f,g be FinSequence of TOP-REAL 2,p be Point of TOP-REAL 2;
  assume that
A1: f is_S-Seq and
A2: p in L~f and
A3: p<>f.(Index(p,f)+1) and
A4: g=<*p*>^mid(f,Index(p,f)+1,len f);
  consider i such that
A5: 1<=i & i+1<=len f & p in LSeg(f,i) by A2,SPPOL_2:13;
  1<=1+i by NAT_1:12;
  then
A6: 1<=len f by A5,XXREAL_0:2;
A7: len g=(len <*p*>)+ len(mid(f,Index(p,f)+1,len f)) by A4,FINSEQ_1:35
    .=1+ len(mid(f,Index(p,f)+1,len f)) by FINSEQ_1:56;
A8: f is unfolded by A1,TOPREAL1:def 10;
A9: Index(p,f)<len f by A2,Th41;
  then Index(p,f)+1<=len f by NAT_1:13;
  then
A10: Index(p,f)+1-Index(p,f)<=len f - Index(p,f) by XREAL_1:11;
  then
A11: 1-1<=len f - Index(p,f)-1 by XREAL_1:11;
A12: Index(p,f)<len f by A2,Th41;
A13: Index(p,f)+1<=len f by A9,NAT_1:13;
A14: len f - Index(p,f)>=0 by A12,XREAL_1:52;
A15: 0+1<=Index(p,f)+1 by NAT_1:11;
  then
A16: len(mid(f,Index(p,f)+1,len f))=len f -' (Index(p,f)+1)+1 by A6,A13,Th27;
  then
A17: len g=1+(len f -(Index(p,f)+1)+1) by A7,A11,XREAL_0:def 2
    .=1+(len f - Index(p,f));
  then
A18: len g -'1=len g -1 by A10,XREAL_0:def 2;
A19: len f -'(Index(p,f)+1)=len f -(Index(p,f)+1) by A11,XREAL_0:def 2
    .=len f - Index(p,f) -1;
  then
A20: len f -'(Index(p,f)+1)+1=len f -Index(p,f);
A21: len g -'1 in dom (mid(f,Index(p,f)+1,len f))
  by A10,A16,A17,A18,A19,FINSEQ_3:27;
  1+(len g -'1)=1+(len g -1) by A7,XREAL_0:def 2
    .=len g;
  then
A22: g.len g =g.(len <*p*> + (len g -'1)) by FINSEQ_1:56
    .=mid(f,Index(p,f)+1,len f).(len g -'1) by A4,A21,FINSEQ_1:def 7;
A23: len f -Index(p,f)=len f -' Index(p,f) by A14,XREAL_0:def 2;
A24: len g -'1= len f -' Index(p,f) by A17,A18,XREAL_0:def 2;
A25: mid(f,Index(p,f)+1,len f).(len f -' Index(p,f))
  =f.(len f -' Index(p,f)+(Index(p,f)+1)-'1)
  by A6,A10,A13,A15,A16,A19,A23,Th27;
  len f -' Index(p,f)+(Index(p,f)+1)
  =len f - Index(p,f)+(Index(p,f)+1) by A14,XREAL_0:def 2
    .= len f +1;
  then
A26: g.len g=f.len f by A17,A18,A22,A25,NAT_D:34;
  len g=len <*p*> + len mid(f,Index(p,f)+1,len f) by A4,FINSEQ_1:35;
  then
A27: len g=1+len mid(f,Index(p,f)+1,len f) by FINSEQ_1:56;
A28: f is one-to-one by A1,TOPREAL1:def 10;
A29: for x1,x2 being set st x1 in dom g & x2 in dom g & g.x1=g.x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume
A30: x1 in dom g & x2 in dom g & g.x1=g.x2;
    then reconsider n1=x1,n2=x2 as Element of NAT;
A31: 1<=n1 & n1<=len g & 1<=n2 & n2<=len g by A30,FINSEQ_3:27;
    now per cases by A31,XXREAL_0:1;
      case n1=1 & n2=1;
        hence x1=x2;
      end;
      case that
A32:    n1=1 and
A33:    n2>1;
        1 <= Index(p,f) by A2,Th41;
        then 1 + 1 < n2+Index(p,f) by A33,XREAL_1:10;
        then
A34:    1< n2+Index(p,f)-1 by XREAL_1:22;
        then
A35:    n2+Index(p,f)-'1 = n2+Index(p,f)-1 by XREAL_0:def 2;
        1<=n1 & n1<=len <*p*> by A32,FINSEQ_1:56;
        then n1 in dom <*p*> by FINSEQ_3:27;
        then
A36:    g.n1=<*p*>.n1 by A4,FINSEQ_1:def 7;
        n2-1>0 by A33,XREAL_1:52;
        then
A37:    n2-'1=n2-1 by XREAL_0:def 2;
        n2-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A31,XREAL_1:11;
        then
A38:    1<=n2-'1 & n2-'1<=len mid(f,Index(p,f)+1,len f) by A33,A37,SPPOL_1:6;
        then
A39:    n2-'1 in dom mid(f,Index(p,f)+1,len f) by FINSEQ_3:27;
        len <*p*>+(n2-'1)=1+(n2-1) by A37,FINSEQ_1:56
          .=n2;
        then g.n2 =mid(f,Index(p,f)+1,len f).(n2-'1) by A4,A39,FINSEQ_1:def 7
          .=f.(n2-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A38,Th27
          .=f.(n2+Index(p,f)-'1) by A37;
        then
A40:    f.(n2+Index(p,f)-'1)=p by A30,A32,A36,FINSEQ_1:57;
        n2 -' 1 <= len f - Index(p,f) by A17,A18,A31,NAT_D:42;
        then n2-1 <= len f - Index(p,f) by A33,XREAL_1:235;
        then n2-1+Index(p,f) <= len f - Index(p,f)+ Index(p,f) by XREAL_1:8;
        hence contradiction by A1,A3,A34,A35,A40,Th45;
      end;
      case that
A41:    n1>1 and
A42:    n2=1;
        1 <= Index(p,f) by A2,Th41;
        then 1 + 1 < n1+Index(p,f) by A41,XREAL_1:10;
        then
A43:    1< n1+Index(p,f)-1 by XREAL_1:22;
        then
A44:    n1+Index(p,f)-'1 = n1+Index(p,f)-1 by XREAL_0:def 2;
        1<=n2 & n2<=len <*p*> by A42,FINSEQ_1:56;
        then n2 in dom <*p*> by FINSEQ_3:27;
        then
A45:    g.n2=<*p*>.n2 by A4,FINSEQ_1:def 7;
        n1-1>0 by A41,XREAL_1:52;
        then
A46:    n1-'1=n1-1 by XREAL_0:def 2;
        n1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A31,XREAL_1:11;
        then
A47:    1<=n1-'1 & n1-'1<=len mid(f,Index(p,f)+1,len f) by A41,A46,SPPOL_1:6;
        then
A48:    n1-'1 in dom mid(f,Index(p,f)+1,len f) by FINSEQ_3:27;
        len <*p*>+(n1-'1)=1+(n1-1) by A46,FINSEQ_1:56
          .=n1;
        then g.n1 =mid(f,Index(p,f)+1,len f).(n1-'1) by A4,A48,FINSEQ_1:def 7
          .=f.(n1-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A47,Th27
          .=f.(n1+Index(p,f)-'1) by A46;
        then
A49:    f.(n1+Index(p,f)-'1)=p by A30,A42,A45,FINSEQ_1:57;
        n1 -' 1 <= len f - Index(p,f) by A17,A18,A31,NAT_D:42;
        then n1-1 <= len f - Index(p,f) by A41,XREAL_1:235;
        then n1-1+Index(p,f) <= len f - Index(p,f)+ Index(p,f) by XREAL_1:8;
        hence contradiction by A1,A3,A43,A44,A49,Th45;
      end;
      case that
A50:    n1>1 and
A51:    n2>1;
A52:    n2-1>0 by A51,XREAL_1:52;
        then
A53:    n2-'1=n2-1 by XREAL_0:def 2;
        then
A54:    0+1<=n2-'1 by A52,NAT_1:13;
A55:    n2-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A31,XREAL_1:11;
        then
A56:    n2-'1 in dom mid(f,Index(p,f)+1,len f) by A53,A54,FINSEQ_3:27;
        len <*p*>+(n2-'1)=1+(n2-1) by A53,FINSEQ_1:56
          .=n2;
        then
A57:    g.n2 =mid(f,Index(p,f)+1,len f).(n2-'1) by A4,A56,FINSEQ_1:def 7
          .=f.(n2-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A53,A54,A55,Th27
          .=f.(n2+Index(p,f)-'1) by A53;
A58:    1<=n2-'1 + Index(p,f) by A54,NAT_1:12;
        then
A59:    n2+Index(p,f)-'1 = n2+Index(p,f)-1 by A53,XREAL_0:def 2;
        n2-'1 <= len f -' Index(p,f) by A24,A31,NAT_D:42;
        then n2-'1+Index(p,f)<=len f - Index(p,f)+ Index(p,f)
        by A23,XREAL_1:8;
        then
A60:    n2+Index(p,f)-'1 in dom f by A53,A58,A59,FINSEQ_3:27;
A61:    n1-1>0 by A50,XREAL_1:52;
        then
A62:    n1-'1=n1-1 by XREAL_0:def 2;
        then
A63:    0+1<=n1-'1 by A61,NAT_1:13;
A64:    n1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A31,XREAL_1:11;
        then
A65:    n1-'1 in dom mid(f,Index(p,f)+1,len f) by A62,A63,FINSEQ_3:27;
        len <*p*>+(n1-'1)=1+(n1-1) by A62,FINSEQ_1:56
          .=n1;
        then
A66:    g.n1 =mid(f,Index(p,f)+1,len f).(n1-'1) by A4,A65,FINSEQ_1:def 7
          .=f.(n1-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A62,A63,A64,Th27
          .=f.(n1+Index(p,f)-'1) by A62;
A67:    1<=n1-1+Index(p,f) by A62,A63,NAT_1:12;
        then
A68:    n1+Index(p,f)-'1 = n1+Index(p,f)-1 by XREAL_0:def 2;
        n1-'1 <= len f -' Index(p,f) by A24,A31,NAT_D:42;
        then n1-'1+Index(p,f)<=len f - Index(p,f)+ Index(p,f)
        by A23,XREAL_1:8;
        then n1+Index(p,f)-'1 in dom f by A62,A67,A68,FINSEQ_3:27;
        then n1+Index(p,f)-'1=n2+Index(p,f)-'1
        by A28,A30,A57,A60,A66,FUNCT_1:def 8;
        hence x1=x2 by A59,A68;
      end;
    end;
    hence x1=x2;
  end;
A69: len g -1+1>=1+1 by A10,A17,XREAL_1:8;
A70: for j be Nat st 1 <= j & j + 2 <= len g
  holds LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)}
  proof
    let j be Nat;
    assume
A71: 1 <= j & j + 2 <= len g;
A72: j+2=j+1+1;
    then
A73: j+1<=len g by A71,NAT_1:13;
A74: 1<j+1 by A71,NAT_1:13;
A75: 1<=(j+1) by A71,NAT_1:13;
A76: LSeg(g,j) c= LSeg(f,Index(p,f)+j-'1) by A2,A4,A71,A73,Th49;
    LSeg(g,(j+1)) c= LSeg(f,Index(p,f)+(j+1)-'1) by A2,A4,A71,A72,A75,Th49;
    then
A77: LSeg(g,j)/\ LSeg(g,(j+1))
    c= LSeg(f,Index(p,f)+j-'1)/\ LSeg(f,Index(p,f)+(j+1)-'1)
    by A76,XBOOLE_1:27;
A78: 1<=Index(p,f) by A2,Th41;
    then 1<=Index(p,f)+j by NAT_1:12;
    then
A79: 1-1<=Index(p,f)+j-1 by XREAL_1:11;
A80: j+1=j+1-1+1
      .=j+1-'1+1 by A74,XREAL_1:235;
    then
A81: j+1=len <*p*>+(j+1-'1) by FINSEQ_1:56;
A82: 1<=j+1-'1 & j+1-'1<=len mid(f,Index(p,f)+1,len f)
    by A27,A71,A73,A80,XREAL_1:8;
    then j+1-'1 in dom mid(f,Index(p,f)+1,len f) by FINSEQ_3:27;
    then
A83: g.(j+1) = mid(f,Index(p,f)+1,len f).(j+1-'1) by A4,A81,FINSEQ_1:def 7
      .=f.(j+1-'1+(Index(p,f)+1)-'1) by A6,A13,A15,A82,Th27
      .=f.(j+1-'1+1+Index(p,f)-'1)
      .=f.(j+1+Index(p,f)-'1) by A74,XREAL_1:237
      .=f.(Index(p,f)+j+1-'1)
      .=f.(Index(p,f)+j) by NAT_D:34
      .=f.(Index(p,f)+j-'1+1) by A78,NAT_1:12,XREAL_1:237;
    1 <= Index(p,f) by A2,Th41;
    then 1+1 <= Index(p,f)+j by A71,XREAL_1:9;
    then 1<=Index(p,f)+j-1 by XREAL_1:21;
    then
A84: 1<=Index(p,f)+j-'1 by NAT_D:39;
A85: Index(p,f)+(j+1)-'1 =Index(p,f)+j+1-1 by NAT_1:11,XREAL_1:235
      .=Index(p,f)+j-1+1
      .=Index(p,f)+j-'1+1 by A78,NAT_1:12,XREAL_1:235;
A86: g/.(j+1)=g.(j+1) by A73,A74,FINSEQ_4:24;
A87: LSeg(g/.(j+1),g/.(j+1+1))=LSeg(g,j+1) by A71,A74,TOPREAL1:def 5;
    j+1+1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A71,XREAL_1:11;
    then
A88: j+1+Index(p,f)<= len f -Index(p,f)+Index(p,f) by A16,A19,XREAL_1:8;
    then
A89: Index(p,f)+j+1 <= len f;
    Index(p,f)+j-1+(1+1) <= len f by A88;
    then Index(p,f)+j-'1+2 <= len f by A79,XREAL_0:def 2;
    then
A90: {f/.(Index(p,f)+j-'1+1)}
    = LSeg(f,Index(p,f)+j-'1)/\ LSeg(f,Index(p,f)+j-'1+1)
    by A8,A84,TOPREAL1:def 8;
    Index(p,f)+(j-1)+1 <= len f by A89,NAT_D:46;
    then Index(p,f)+j-'1+1 <= len f by A79,XREAL_0:def 2;
    then
A91: f/.(Index(p,f)+j-'1+1)=g/.(j+1) by A83,A86,FINSEQ_4:24,NAT_1:11;
A92: LSeg(g,j)=LSeg(g/.j,g/.(j+1)) by A71,A73,TOPREAL1:def 5;
A93: g/.(j+1) in LSeg(g/.j,g/.(j+1)) by TOPREAL1:6;
    g/.(j+1) in LSeg(g/.(j+1),g/.(j+1+1)) by TOPREAL1:6;
    then g/.(j+1) in LSeg(g/.j,g/.(j+1)) /\ LSeg(g/.(j+1),g/.(j+1+1))
    by A93,XBOOLE_0:def 4;
    then {g/.(j+1)} c= LSeg(g,j) /\ LSeg(g,j+1) by A87,A92,ZFMISC_1:37;
    hence {g/.(j+1)} = LSeg(g,j)/\ LSeg(g,j+1)
    by A77,A85,A90,A91,XBOOLE_0:def 10;
  end;
A94: f is s.n.c. by A1,TOPREAL1:def 10;
A95: for j1,j2 be Nat st j1+1 < j2 holds LSeg(g,j1) misses LSeg(g,j2)
  proof
    let j1,j2 be Nat;
    assume
A96: j1+1 < j2;
A97: j1=0 or j1>=0+1 by NAT_1:13;
    now per cases by A97,XXREAL_0:1;
      case j1=0;
        then LSeg(g,j1)={} by TOPREAL1:def 5;
        then LSeg(g,j1) /\ LSeg(g,j2) = {};
        hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
      end;
      case that
A98:    j1=1 or j1>1 and
A99:    j2+1<=len g;
        j2<len g by A99,NAT_1:13;
        then
A100:   j1+1<=len g by A96,XXREAL_0:2;
        1<j1+1 by A98,NAT_1:13;
        then
A101:   1<=j2 by A96,XXREAL_0:2;
A102:   LSeg(g,j1) c= LSeg(f,Index(p,f)+j1-'1) by A2,A4,A98,A100,Th49;
A103:   LSeg(g,j2) c= LSeg(f,Index(p,f)+j2-'1) by A2,A4,A99,A101,Th49;
        Index(p,f)+(j1+1)<Index(p,f)+j2 by A96,XREAL_1:8;
        then
A104:   Index(p,f)+j1+1-1<Index(p,f)+j2-1 by XREAL_1:11;
        1<=Index(p,f) by A2,Th41;
        then 1<=Index(p,f)+j1 by NAT_1:12;
        then 1-1<=Index(p,f)+j1-1 by XREAL_1:11;
        then Index(p,f)+j1-1=Index(p,f)+j1-'1 by XREAL_0:def 2;
        then Index(p,f)+j1-'1+1<Index(p,f)+j2-'1 by A104,XREAL_0:def 2;
        then LSeg(f,Index(p,f)+j1-'1) misses LSeg(f,Index(p,f)+j2-'1) by A94,
TOPREAL1:def 9;
        then LSeg(f,Index(p,f)+j1-'1) /\ LSeg(f,Index(p,f)+j2-'1) = {}
        by XBOOLE_0:def 7;
        then LSeg(g,j1) /\ LSeg(g,j2) = {} by A102,A103,XBOOLE_1:3,27;
        hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
      end;
      case j2+1>len g;
        then LSeg(g,j2)={} by TOPREAL1:def 5;
        then LSeg(g,j1) /\ LSeg(g,j2) = {};
        hence thesis by XBOOLE_0:def 7;
      end;
    end;
    hence LSeg(g,j1) misses LSeg(g,j2);
  end;
A105: f is special by A1,TOPREAL1:def 10;
  for j be Nat st 1 <= j & j+1 <= len g holds
  (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
  proof
    let j be Nat;
    assume
A106: 1 <= j & j+1 <= len g;
    then
A107: LSeg(g,j) c= LSeg(f,Index(p,f)+j-'1) by A2,A4,Th49;
A108: LSeg(g,j) = LSeg(g/.j,g/.(j+1)) by A106,TOPREAL1:def 5;
    1<=Index(p,f) by A2,Th41;
    then
A109: 1<Index(p,f)+1 by NAT_1:13;
    Index(p,f)+1<=Index(p,f)+j by A106,XREAL_1:8;
    then 1<Index(p,f)+j by A109,XXREAL_0:2;
    then
A110: 1<=Index(p,f)+j-1 by SPPOL_1:6;
    then
A111: Index(p,f)+j-1=Index(p,f)+j-'1 by XREAL_0:def 2;
    j+1-1<=1+len mid(f,Index(p,f)+1,len f)-1 by A27,A106,XREAL_1:11;
    then j+1-1<= len f -Index(p,f) by A6,A13,A15,A20,Th27;
    then
A112: j+Index(p,f)<= len f -Index(p,f)+Index(p,f) by XREAL_1:8;
    then
A113: LSeg(f,Index(p,f)+j-'1)
    = LSeg(f/.(Index(p,f)+j-'1),f/.(Index(p,f)+j-'1+1))
    by A110,A111,TOPREAL1:def 5;
    (f/.(Index(p,f)+j-'1))`1 = (f/.(Index(p,f)+j-'1+1))`1
    or (f/.(Index(p,f)+j-'1))`2 = (f/.(Index(p,f)+j-'1+1))`2
    by A105,A110,A111,A112,TOPREAL1:def 7;
    hence (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
    by A107,A108,A113,Th36;
  end;
  then g is one-to-one & len g >= 2
  & g is unfolded s.n.c. special by A29,A69,A70,A95,FUNCT_1:def 8
,TOPREAL1:def 7,def 8,def 9;
  then g is_S-Seq & g.1 = p & g.len g = f/.len f by A4,A6,A26,FINSEQ_1:58
,FINSEQ_4:24,TOPREAL1:def 10;
  hence g is_S-Seq_joining p,f/.len f by Def3;
end;

theorem Th51:
  for f,g being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2,j be Nat st p in L~f & 1<=j & j+1<=len g
  & g=mid(f,1,Index(p,f))^<*p*> holds LSeg(g,j) c= LSeg(f,j)
proof
  let f,g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2,j be Nat;
  assume that
A1: p in L~f and
A2: 1<=j and
A3: j+1<=len g and
A4: g=mid(f,1,Index(p,f))^<*p*>;
A5: j in NAT by ORDINAL1:def 13;
A6: 1<=Index(p,f) by A1,Th41;
A7: Index(p,f) < len f by A1,Th41;
  then
A8: 1<=1 & 1<=len f & 1<=Index(p,f) & Index(p,f)<=len f by A6,XXREAL_0:2;
  j<=j+1 by NAT_1:11;
  then
A9: j<=len g by A3,XXREAL_0:2;
A10: 1<=j+1 by NAT_1:11;
  now
    len g=len mid(f,1,Index(p,f)) + len <*p*> by A4,FINSEQ_1:35
      .=len mid(f,1,Index(p,f))+1 by FINSEQ_1:56;
    then len g=Index(p,f)-'1+1+1 by A8,Th27;
    then
A11: len g=Index(p,f)+1 by A6,XREAL_1:237;
    Index(p,f)+1<=len f +1 by A7,XREAL_1:8;
    then j+1<=len f +1 by A3,A11,XXREAL_0:2;
    then
A12: j+1-1<=len f +1-1 by XREAL_1:11;
A13: j<=Index(p,f) by A3,A11,XREAL_1:8;
A14: len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A8,Th27
      .=Index(p,f) by A6,XREAL_1:237;
    then
A15: j in dom mid(f,1,Index(p,f)) by A2,A13,FINSEQ_3:27;
A16: g/.j=g.j by A2,A9,FINSEQ_4:24
      .=mid(f,1,Index(p,f)).j by A4,A15,FINSEQ_1:def 7
      .=f.(j+1-'1) by A2,A5,A8,A13,A14,Th27
      .=f.j by NAT_D:34
      .=f/.j by A2,A12,FINSEQ_4:24;
    now per cases;
      case
A17:    j+1<=Index(p,f);
        then
A18:    j+1<=len f by A7,XXREAL_0:2;
A19:    len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A8,Th27
          .=Index(p,f) by A6,XREAL_1:237;
        then
A20:    j+1 in dom mid(f,1,Index(p,f)) by A10,A17,FINSEQ_3:27;
A21:    g/.(j+1)=g.(j+1) by A3,FINSEQ_4:24,NAT_1:11
          .= mid(f,1,Index(p,f)).(j+1) by A4,A20,FINSEQ_1:def 7
          .=f.(j+1+1-'1) by A8,A10,A17,A19,Th27
          .=f.(j+1) by NAT_D:34
          .=f/.(j+1) by A18,FINSEQ_4:24,NAT_1:11;
        LSeg(g,j)=LSeg(g/.j,g/.(j+1)) by A2,A3,TOPREAL1:def 5;
        hence LSeg(g,j) c= LSeg(f,j) by A2,A16,A18,A21,TOPREAL1:def 5;
      end;
      case j+1>Index(p,f);
        then j>=Index(p,f) by NAT_1:13;
        then
A22:    j=Index(p,f) by A13,XXREAL_0:1;
        1<=1 & 1<=len <*p*> by FINSEQ_1:57;
        then
A23:    1 in dom <*p*> by FINSEQ_3:27;
A24:    len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A8,Th27
          .=Index(p,f) by A6,XREAL_1:237;
A25:    g/.(j+1)=g.(j+1) by A3,FINSEQ_4:24,NAT_1:11
          .=<*p*>.1 by A4,A22,A23,A24,FINSEQ_1:def 7
          .=p by FINSEQ_1:def 8;
A26:    f/.j in LSeg(f/.j,f/.(j+1)) by TOPREAL1:6;
A27:    p in LSeg(f,j) by A1,A22,Th42;
        now
          assume j+1>len f;
          then j>=len f by NAT_1:13;
          hence contradiction by A1,A22,Th41;
        end;
        then
A28:    LSeg(f,j)=LSeg(f/.j,f/.(j+1)) by A2,TOPREAL1:def 5;
        then LSeg(g/.j,g/.(j+1)) c= LSeg(f/.j,f/.(j+1))
        by A16,A25,A26,A27,TOPREAL1:12;
        hence LSeg(g,j) c= LSeg(f,j) by A2,A3,A28,TOPREAL1:def 5;
      end;
    end;
    hence LSeg(g,j) c= LSeg(f,j);
  end;
  hence LSeg(g,j) c= LSeg(f,j);
end;

theorem Th52:
  for f,g being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
  st f is_S-Seq & p in L~f & p<>f.1 & g=mid(f,1,Index(p,f))^<*p*>
  holds g is_S-Seq_joining f/.1,p
proof
  let f,g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume that
A1: f is_S-Seq and
A2: p in L~f and
A3: p<>f.1 and
A4: g=mid(f,1,Index(p,f))^<*p*>;
  consider i such that
A5: 1<=i & i+1<=len f & p in LSeg(f,i) by A2,SPPOL_2:13;
A6: 1<=Index(p,f) by A2,Th41;
A7: Index(p,f)<=len f by A2,Th41;
  1<=1+i by NAT_1:12;
  then
A8: 1<=len f by A5,XXREAL_0:2;
  then
A9: len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A6,A7,Th27;
  then
A10: len mid(f,1,Index(p,f))=Index(p,f) by A6,XREAL_1:237;
  1<=len <*p*> by FINSEQ_1:56;
  then
A11: 1 in dom <*p*> by FINSEQ_3:27;
  g.1=mid(f,1,Index(p,f)).1 by A4,A6,A10,Th17;
  then g.1=f.1 by A6,A7,A8,Th27;
  then
A12: g.1=f/.1 by A8,FINSEQ_4:24;
A13: len g = len mid(f,1,Index(p,f))+ len<*p*> by A4,FINSEQ_1:35
    .= len mid(f,1,Index(p,f))+ 1 by FINSEQ_1:56;
  then
A14: g.len g = p by A4,FINSEQ_1:59;
A15: 1+1<=len g by A6,A10,A13,XREAL_1:8;
A16: f is one-to-one by A1,TOPREAL1:def 10;
A17: for n1,n2 being Element of NAT st 1<=n1 & n1<=len f
  & 1<=n2 & n2<=len f & f.n1=f.n2 holds n1=n2
  proof
    let n1,n2 be Element of NAT;
    assume
A18: 1<=n1 & n1<=len f & 1<=n2 & n2<=len f & f.n1=f.n2;
    then n1 in dom f & n2 in dom f by FINSEQ_3:27;
    hence n1=n2 by A16,A18,FUNCT_1:def 8;
  end;
A19: for x1,x2 being set st x1 in dom g & x2 in dom g & g.x1=g.x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume
A20: x1 in dom g & x2 in dom g & g.x1=g.x2;
    then reconsider n1=x1,n2=x2 as Element of NAT;
A21: 1<=n1 & n1<=len g & 1<=n2 & n2<=len g by A20,FINSEQ_3:27;
    now
A22:  g.len g =<*p*>.1 by A4,A11,A13,FINSEQ_1:def 7
        .=p by FINSEQ_1:def 8;
      now per cases;
        case
A23:      n1=len g;
          now
            assume
A24:        n2<>len g;
            then n2<len g by A21,XXREAL_0:1;
            then
A25:        n2<=len mid(f,1,Index(p,f)) by A13,NAT_1:13;
            then g.n2=mid(f,1,Index(p,f)).n2 by A4,A21,FINSEQ_1:85;
            then g.n2=f.(n2+1-'1) by A6,A7,A8,A21,A25,Th27;
            then
A26:        p=f.n2 by A20,A22,A23,NAT_D:34;
A27:        n2 <= len f by A7,A10,A25,XXREAL_0:2;
            1 < n2 by A3,A21,A26,XXREAL_0:1;
            then Index(p,f)+1 = n2 by A1,A26,A27,Th45;
            hence contradiction by A6,A9,A13,A24,XREAL_1:237;
          end;
          hence x1=x2 by A23;
        end;
        case
A28:      n2=len g;
          now
            assume
A29:        n1<>len g;
            then n1<len g by A21,XXREAL_0:1;
            then
A30:        n1<=len mid(f,1,Index(p,f)) by A13,NAT_1:13;
            then g.n1=mid(f,1,Index(p,f)).n1 by A4,A21,FINSEQ_1:85;
            then g.n1=f.(n1+1-'1) by A6,A7,A8,A21,A30,Th27;
            then
A31:        p=f.n1 by A20,A22,A28,NAT_D:34;
A32:        n1 <= len f by A7,A10,A30,XXREAL_0:2;
            1 < n1 by A3,A21,A31,XXREAL_0:1;
            then Index(p,f)+1 = n1 by A1,A31,A32,Th45;
            hence contradiction by A6,A9,A13,A29,XREAL_1:237;
          end;
          hence x1=x2 by A28;
        end;
        case that
A33:      n1<>len g and
A34:      n2 <> len g;
          n1<len g by A21,A33,XXREAL_0:1;
          then
A35:      n1<=len mid(f,1,Index(p,f)) by A13,NAT_1:13;
          then
A36:      n1<=len f by A7,A10,XXREAL_0:2;
A37:      g.n1=mid(f,1,Index(p,f)).n1 by A4,A21,A35,FINSEQ_1:85
            .=f.n1 by A7,A10,A21,A35,Th32;
          n2 < len g by A21,A34,XXREAL_0:1;
          then
A38:      n2<=len mid(f,1,Index(p,f)) by A13,NAT_1:13;
          then
A39:      n2<=len f by A7,A10,XXREAL_0:2;
          g.n2=mid(f,1,Index(p,f)).n2 by A4,A21,A38,FINSEQ_1:85
            .=f.n2 by A7,A10,A21,A38,Th32;
          hence x1=x2 by A17,A20,A21,A36,A37,A39;
        end;
      end;
      hence x1=x2;
    end;
    hence x1=x2;
  end;
A40: f is unfolded by A1,TOPREAL1:def 10;
A41: for j be Nat st 1 <= j & j + 2 <= len g
  holds LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)}
  proof
    let j be Nat;
    assume
A42: 1 <= j & j + 2 <= len g;
    then j+1<=len g by NAT_D:47;
    then
A43: LSeg(g,j) c= LSeg(f,j) by A2,A4,A42,Th51;
A44: j+1+1<=len g by A42;
A45: 1<=j+1 by A42,NAT_D:48;
    then LSeg(g,j+1) c= LSeg(f,j+1) by A2,A4,A44,Th51;
    then
A46: LSeg(g,j)/\ LSeg(g,j+1) c= LSeg(f,j)/\ LSeg(f,j+1) by A43,XBOOLE_1:27;
A47: j+1<=len g by A42,NAT_D:47;
    then LSeg(g,j)=LSeg(g/.j,g/.(j+1)) by A42,TOPREAL1:def 5;
    then
A48: g/.(j+1) in LSeg(g,j) by TOPREAL1:6;
A49: g/.(j+1)=g.(j+1) by A45,A47,FINSEQ_4:24;
A50: Index(p,f)<=len f by A2,Th41;
    LSeg(g,j+1)=LSeg(g/.(j+1),g/.(j+1+1)) by A42,A45,TOPREAL1:def 5;
    then g/.(j+1) in LSeg(g,j+1) by TOPREAL1:6;
    then g/.(j+1) in LSeg(g,j) /\ LSeg(g,j+1) by A48,XBOOLE_0:def 4;
    then
A51: {g/.(j+1)} c= LSeg(g,j) /\ LSeg(g,j+1) by ZFMISC_1:37;
    now
A52:  len g=len mid(f,1,Index(p,f))+1 by A4,FINSEQ_2:19;
      Index(p,f)<=len f by A2,Th41;
      then
A53:  len g<=len f +1 by A10,A52,XREAL_1:8;
      now per cases by A53,XXREAL_0:1;
        case len g=len f +1;
          hence contradiction by A2,A10,A52,Th41;
        end;
        case len g<len f+1;
          then len g<=len f by NAT_1:13;
          then
A54:      j+2<=len f by A42,XXREAL_0:2;
A55:      j+1<=Index(p,f) by A10,A44,A52,XREAL_1:8;
          then
A56:      j+1<=len f by A50,XXREAL_0:2;
A57:      LSeg(g,j)/\ LSeg(g,j+1) c= {f/.(j+1)}
          by A40,A42,A46,A54,TOPREAL1:def 8;
A58:      f.(j+1)=f/.(j+1) by A45,A56,FINSEQ_4:24;
          g.(j+1)=mid(f,1,Index(p,f)).(j+1) by A4,A10,A45,A55,FINSEQ_1:85
            .=f.(j+1) by A7,A45,A55,Th32;
          hence LSeg(g,j)/\ LSeg(g,j+1) = {g/.(j+1)}
          by A49,A51,A57,A58,XBOOLE_0:def 10;
        end;
      end;
      hence LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)};
    end;
    hence LSeg(g,j) /\ LSeg(g,j+1) = {g/.(j+1)};
  end;
A59: f is s.n.c. by A1,TOPREAL1:def 10;
A60: for j1,j2 be Nat st j1+1 < j2 holds LSeg(g,j1) misses LSeg(g,j2)
  proof
    let j1,j2 be Nat;
    assume
A61: j1+1 < j2;
A62: j1=0 or j1>=0+1 by NAT_1:13;
    now per cases by A62,XXREAL_0:1;
      case j1=0;
        then LSeg(g,j1)={} by TOPREAL1:def 5;
        then LSeg(g,j1) /\ LSeg(g,j2) = {};
        hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
      end;
      case that
A63:    j1=1 or j1>1 and
A64:    j2+1<=len g;
        j2<len g by A64,NAT_1:13;
        then j1+1<len g by A61,XXREAL_0:2;
        then
A65:    LSeg(g,j1) c= LSeg(f,j1) by A2,A4,A63,Th51;
        1+1<=j1+1 by A63,XREAL_1:8;
        then 2<=j2 by A61,XXREAL_0:2;
        then 1<=j2 by XXREAL_0:2;
        then
A66:    LSeg(g,j2) c= LSeg(f,j2) by A2,A4,A64,Th51;
        LSeg(f,j1) misses LSeg(f,j2) by A59,A61,TOPREAL1:def 9;
        then LSeg(f,j1) /\ LSeg(f,j2) = {} by XBOOLE_0:def 7;
        then LSeg(g,j1) /\ LSeg(g,j2) = {} by A65,A66,XBOOLE_1:3,27;
        hence LSeg(g,j1) misses LSeg(g,j2) by XBOOLE_0:def 7;
      end;
      case j2+1>len g;
        then LSeg(g,j2)={} by TOPREAL1:def 5;
        then LSeg(g,j1)/\ LSeg(g,j2)={};
        hence thesis by XBOOLE_0:def 7;
      end;
    end;
    hence LSeg(g,j1) misses LSeg(g,j2);
  end;
A67: f is special by A1,TOPREAL1:def 10;
  for j be Nat st 1 <= j & j+1 <= len g holds
  (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
  proof
    let j be Nat;
    assume
A68: 1 <= j & j+1 <= len g;
A69: now
      j+1<=Index(p,f)+1 by A4,A10,A68,FINSEQ_2:19;
      then
A70:  j<=Index(p,f) by XREAL_1:8;
      Index(p,f)<len f by A2,Th41;
      then j<len f by A70,XXREAL_0:2;
      hence j+1<=len f by NAT_1:13;
    end;
A71: LSeg(g,j) c= LSeg(f,j) by A2,A4,A68,Th51;
A72: LSeg(g,j) = LSeg(g/.j,g/.(j+1)) by A68,TOPREAL1:def 5;
A73: LSeg(f,j) = LSeg(f/.j,f/.(j+1)) by A68,A69,TOPREAL1:def 5;
    (f/.j)`1 = (f/.(j+1))`1
    or (f/.j)`2 = (f/.(j+1))`2 by A67,A68,A69,TOPREAL1:def 7;
    hence (g/.j)`1 = (g/.(j+1))`1 or (g/.j)`2 = (g/.(j+1))`2
    by A71,A72,A73,Th36;
  end;
  then g is one-to-one & len g >= 2 & g is unfolded s.n.c. special
  by A15,A19,A41,A60,FUNCT_1:def 8,TOPREAL1:def 7,def 8,def 9;
  then g is_S-Seq by TOPREAL1:def 10;
  hence g is_S-Seq_joining f/.1,p by A12,A14,Def3;
end;

begin

::----------------------------------------------------------------------:
::  Left and Right Cutting Functions for Finite Sequences in TOP-REAL 2 :
::----------------------------------------------------------------------:

definition
  let f be FinSequence of TOP-REAL 2,p be Point of TOP-REAL 2;
  func L_Cut(f,p) -> FinSequence of TOP-REAL 2 equals
  :Def4:
  <*p*>^mid(f,Index(p,f)+1,len f) if p<>f.(Index(p,f)+1)
  otherwise mid(f,Index(p,f)+1,len f);
  correctness;
  func R_Cut(f,p) -> FinSequence of TOP-REAL 2 equals
  :Def5:
  mid(f,1,Index(p,f))^<*p*> if p<>f.1 otherwise <*p*>;
  correctness;
end;

theorem Th53:
  for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
  st f is_S-Seq & p in L~f & p = f.(Index(p,f)+1) & p <> f.len f
  holds Index(p,Rev f) + Index(p,f) + 1 = len f
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: f is_S-Seq and
A2: p in L~f and
A3: p = f.(Index(p,f)+1) and
A4: p <> f.len f;
A5: len f = len Rev f by FINSEQ_5:def 3;
  len f <= len f + Index(p,f) by NAT_1:11;
  then
A6: len f - Index(p,f) <= len Rev f by A5,XREAL_1:22;
  Index(p,f) < len f by A2,Th41;
  then
A7: Index(p,f)+1 <= len f by NAT_1:13;
  1 <= Index(p,f)+1 by NAT_1:11;
  then Index(p,f)+1 in dom f by A7,FINSEQ_3:27;
  then
A8: Index(p,f)+1 in dom Rev f by FINSEQ_5:60;
  Index(p,f)+1 < len f by A3,A4,A7,XXREAL_0:1;
  then
A9: 1 < len f - Index(p,f) by XREAL_1:22;
  Index(p,f) <= len f by A2,Th41;
  then
A10: len f - Index(p,f) = len f -' Index(p,f) by XREAL_1:235;
  p = (Rev Rev f).(Index(p,f)+1) by A3,FINSEQ_6:29
    .= (Rev f).(len Rev f - (Index(p,f)+1) + 1) by A8,FINSEQ_5:61
    .= (Rev f).(len Rev f - Index(p,f))
    .= (Rev f).(len f - Index(p,f)) by FINSEQ_5:def 3;

then Index(p,Rev f) + 1 = len f -' Index(p,f) by A1,A6,A9,A10,Th45,SPPOL_2:47;
  hence Index(p,Rev f) + Index(p,f) + 1 = len f by A10;
end;

theorem Th54:
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st f is unfolded s.n.c. &
  p in L~f & p <> f.(Index(p,f)+1) holds Index(p,Rev f) + Index(p,f) = len f
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2 such that
A1: f is unfolded s.n.c. and
A2: p in L~f and
A3: p <> f.(Index(p,f)+1);
A4: Rev f is s.n.c. by A1,SPPOL_2:36;
A5: Index(p,f) < len f by A2,Th41;
  then
A6: len f -' Index(p,f) + Index(p,f) = len f by XREAL_1:237;
A7: len f = len Rev f by FINSEQ_5:def 3;
  p in LSeg(f,Index(p,f)) by A2,Th42;
  then
A8: p in LSeg(Rev f,len f -' Index(p,f)) by A6,SPPOL_2:2;
A9: Index(p,f) < len f by A2,Th41;
  then Index(p,f) + 1 <= len f by NAT_1:13;
  then 1<=len f - Index(p,f) by XREAL_1:21;
  then
A10: 1<=len f -' Index(p,f) by NAT_D:39;
  0+1 <= Index(p,f) by A2,Th41;
  then len f + 0 < len f + Index(p,f) by XREAL_1:8;
  then len f - Index(p,f) < len f by XREAL_1:21;
  then
A11: len f -' Index(p,f) < len f by A5,XREAL_1:235;
  then
A12: len f -' Index(p,f)+1<=len Rev f by A7,NAT_1:13;
  len f -' Index(p,f) in dom f by A10,A11,FINSEQ_3:27;
  then (Rev f).(len f -' Index(p,f))
  = f.(len f - (len f -' Index(p,f)) + 1) by FINSEQ_5:61
    .= f.(len f - (len f - Index(p,f)) + 1) by A9,XREAL_1:235
    .= f.(0 + Index(p,f) + 1);
  then len f -' Index(p,f)=Index(p,Rev f) by A1,A3,A4,A8,A12,Th47,SPPOL_2:29;
  hence Index(p,Rev f) + Index(p,f) = len f by A9,XREAL_1:237;
end;

theorem Th55:
  for D for f being FinSequence of D, k being Element of NAT,
  p being Element of D holds (<*p*>^f)|(k+1) = <*p*>^(f|k)
proof
  let D;
  let f be FinSequence of D, k be Element of NAT, p be Element of D;
A1: f|Seg k = f|k by FINSEQ_1:def 15;
  thus (<*p*>^f)|(k+1) = (<*p*>^f)|Seg(k+1) by FINSEQ_1:def 15
    .= (<*p*>^f)|Seg(k+len<*p*>) by FINSEQ_1:56
    .= <*p*>^(f|k) by A1,FINSEQ_6:16;
end;

theorem Th56:
  for D for f being FinSequence of D, k1,k2 being Element of NAT
  st k1 < k2 & k1 in dom f holds mid(f,k1,k2) = <*f.k1*>^ mid(f,k1+1,k2)
proof
  let D;
  let f be FinSequence of D, k1,k2 be Element of NAT;
  assume
A1: k1 < k2;
  then
A2: k1+1 <= k2 by NAT_1:13;
A3: 1 <= k2 -' k1
  proof
    per cases by A2,XXREAL_0:1;
    suppose k1 + 1 = k2;
      hence 1 <= k2 -' k1 by NAT_D:34;
    end;
    suppose
A4:   k1 + 1 < k2;
      k2-'k1 <= 1 implies k2 <= 1 + k1
      proof
        assume k2-'k1 <= 1;
        then k2-'k1 +k1 <= 1 + k1 by XREAL_1:8;
        hence k2 <= 1 + k1 by A1,XREAL_1:237;
      end;
      hence 1 <= k2 -' k1 by A4;
    end;
  end;
  assume
A5: k1 in dom f;
  then
A6: f.k1 = f/.k1 by PARTFUN1:def 8;
  k2-'k1 = k2-k1 by A3,NAT_D:39;
  then
A7: k2-'k1-'1 = k2-k1-1 by A3,XREAL_1:235
    .= k2-(k1+1)
    .= k2-'(k1+1) by A2,XREAL_1:235;
  1 <= k1 by A5,FINSEQ_3:27;
  then k1 -' 1 + 1 = k1 by XREAL_1:237;
  then (f/^(k1-'1)) = <*f/.k1*>^(f/^k1) by A5,FINSEQ_5:34;
  hence mid(f,k1,k2) = (<*f/.k1*>^ (f/^k1))|(k2-'k1+1) by A1,Def1
    .= <*f.k1*>^ ((f/^k1)|(k2-'k1)) by A6,Th55
    .= <*f.k1*>^ ((f/^k1)|(k2-'(k1+1)+1)) by A3,A7,XREAL_1:237
    .= <*f.k1*>^ ((f/^(k1+1-'1))|(k2-'(k1+1)+1)) by NAT_D:34
    .= <*f.k1*>^ mid(f,k1+1,k2) by A2,Def1;
end;

:: registration let f be non empty FinSequence;
::  cluster Rev f -> non empty;
::  coherence
::   proof
::    dom Rev f = dom f by FINSEQ_5:60;
::    hence thesis by RELAT_1:60,64;
::   end;
:: end;

theorem Th57:
  for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
  st f is_S-Seq & p in L~f holds L_Cut(Rev f,p) = Rev R_Cut(f,p)
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: f is_S-Seq and
A2: p in L~f;
A3: f is unfolded s.n.c. by A1,TOPREAL1:def 10;
A4: Rev f is_S-Seq by A1,SPPOL_2:47;
  then
A5: Rev f is unfolded s.n.c. by TOPREAL1:def 10;
A6: p in L~Rev f by A2,SPPOL_2:22;
A7: Rev Rev f = f by FINSEQ_6:29;
A8: len f = len Rev f by FINSEQ_5:def 3;
A9: L~f = L~Rev f by SPPOL_2:22;
A10: dom Rev f = dom f by FINSEQ_5:60;
A11: 1 <= Index(p, Rev f)+1 by NAT_1:11;
  Index(p, Rev f) < len Rev f by A2,A9,Th41;
  then
A12: Index(p, Rev f) + 1 <= len f by A8,NAT_1:13;
  then
A13: Index(p, Rev f)+1 in dom f by A11,FINSEQ_3:27;
A14: 1+1 <= len f by A1,TOPREAL1:def 10;
  then
A15: 1 < len f by NAT_1:13;
A16: 1 <= Index(p,f) by A2,Th41;
A17: Index(p,f) < len f by A2,Th41;
A18: 1 in dom f by A15,FINSEQ_3:27;
A19: 2 in dom f by A14,FINSEQ_3:27;
A20: len f in dom f by A15,FINSEQ_3:27;
  per cases;
  suppose
A21: p = f.len f;
    then
A22: p = (Rev f).1 by FINSEQ_5:65;
    then p = (Rev f)/.1 by A10,A18,PARTFUN1:def 8;
    then
A23: Index(p,Rev f) = 1 by A8,A14,Th44;
    f is one-to-one by A1,TOPREAL1:def 10;
    then
A24: p<>f.1 by A15,A18,A20,A21,FUNCT_1:def 8;
    Rev f is one-to-one by A4,TOPREAL1:def 10;
    then
A25: p <> (Rev f).(1+1) by A10,A18,A19,A22,FUNCT_1:def 8;
    then Index(p,Rev f) + Index(p,f) = len f by A5,A6,A7,A8,A23,Th54;
    then
A26: Index(p,Rev f) = len f - Index(p,f);
    thus L_Cut(Rev f,p)
    = <*p*>^mid(Rev f,Index(p,Rev f)+1,len f) by A8,A23,A25,Def4
      .= <*p*>^mid(Rev f,len f -'Index(p,f)+1,len f) by A17,A26,XREAL_1:235
      .= <*p*>^mid(Rev f,len f -'Index(p,f)+1,len f -' 1 + 1) by A15,
XREAL_1:237
      .= <*p*>^Rev mid(f,1,Index(p,f)) by A15,A16,A17,Th22
      .= Rev(mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
      .= Rev R_Cut(f,p) by A24,Def5;
  end;
  suppose
A27: p = f.1;
    then
A28: p = (Rev f).len f by FINSEQ_5:65;
A29: len (Rev f/^(len Rev f-'1))=len Rev f-'(len Rev f-'1) by RFINSEQ:42;
A30: len Rev f-'1+1=len Rev f by A8,A15,XREAL_1:237;
    then
A31: (Rev f/^(len Rev f-'1)).1=Rev f.len Rev f by Th23;
    1<=len Rev f-(len Rev f-'1) by A30;
    then
A32: 1<=len (Rev f/^(len Rev f-'1)) by A29,NAT_D:39;
    then
A33: Rev f/^(len Rev f-'1) is non empty by CARD_1:47;
    len Rev f-'len Rev f+1=len Rev f-len Rev f+1 by XREAL_1:235
      .=1;
    then
A34: mid(Rev f,len Rev f,len Rev f)=(Rev f/^(len Rev f-'1))|1 by Def1
      .=<*(Rev f/^(len Rev f-'1))/.1 *> by A33,FINSEQ_5:23
      .=<*Rev f.len Rev f*> by A31,A32,FINSEQ_4:24;
    Index(p,Rev f) + 1 = len f by A1,A8,A15,A28,Th45,SPPOL_2:47;
    hence L_Cut(Rev f,p) = <*p*> by A8,A28,A34,Def4
      .= Rev <*p*> by FINSEQ_5:63
      .= Rev R_Cut(f,p) by A27,Def5;
  end;
  suppose that
A35: p <> f.1 and
A36: p <> f.len f and
A37: p = f.(Index(p,f)+1);
A38: p <> (Rev f).len f by A35,FINSEQ_5:65;
    len f = Index(p,Rev f) + Index(p,f) + 1 by A1,A2,A36,A37,Th53
      .= Index(p,Rev f) + (Index(p,f) + 1);
    then
A39: p = f.(len f - (Index(p, Rev f)+1) + 1) by A37
      .= (Rev f).(Index(p, Rev f)+1) by A13,FINSEQ_5:61;
A40: len f = Index(p,Rev f) + Index(p,f) + 1 by A1,A2,A36,A37,Th53
      .= Index(p,f) + (Index(p,Rev f) + 1);
A41: len f -' Index(p,f) = len f - Index(p,f) by A17,XREAL_1:235
      .= Index(p,Rev f)+1 by A40;
A42: Index(p, Rev f)+1 < len f by A12,A38,A39,XXREAL_0:1;
    thus L_Cut(Rev f,p) = mid(Rev f,Index(p,Rev f)+1,len f) by A8,A39,Def4
      .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f)
    by A10,A13,A39,A41,A42,Th56
      .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f-'1+1) by A15,XREAL_1:237
      .= <*p*>^Rev mid(f,1,Index(p,f)) by A15,A16,A17,Th22
      .= Rev (mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
      .= Rev R_Cut(f,p) by A35,Def5;
  end;
  suppose that
A43: p <> f.1 and
A44: p <> f.(Index(p,f)+1);
A45: p <> (Rev f).len f by A43,FINSEQ_5:65;
A46: now
      assume
A47:  p = (Rev f).(Index(p, Rev f)+1);
      then
A48:  len Rev f = Index(p,Rev Rev f) + Index(p,Rev f) + 1
      by A1,A6,A8,A45,Th53,SPPOL_2:47
        .= Index(p,f) + 1 + Index(p,Rev f) by A7;
      p = f.(len f - (Index(p, Rev f)+1) + 1) by A13,A47,FINSEQ_5:61
        .= f.(Index(p,f)+1) by A8,A48;
      hence contradiction by A44;
    end;
A49: len f = Index(p,Rev f) + Index(p,f) by A2,A3,A44,Th54;
A50: Index(p, f) < len f by A2,Th41;
    Index(p,Rev f) = len f - Index(p,f) by A49
      .= len f -' Index(p,f) by A50,XREAL_1:235;
    hence L_Cut(Rev f,p)
    = <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f) by A8,A46,Def4
      .= <*p*>^mid(Rev f,len f -' Index(p,f)+1, len f-'1+1) by A15,XREAL_1:237
      .= <*p*>^Rev mid(f,1,Index(p,f)) by A15,A16,A17,Th22
      .= Rev (mid(f,1,Index(p,f))^<*p*>) by FINSEQ_5:66
      .= Rev R_Cut(f,p) by A43,Def5;
  end;
end;

theorem Th58:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st p in L~f holds (L_Cut(f,p)).1=p &
  for i st 1<i & i<=len L_Cut(f,p) holds
  (p = f.(Index(p,f)+1) implies (L_Cut(f,p)).i=f.(Index(p,f)+i)) &
  (p <> f.(Index(p,f)+1) implies (L_Cut(f,p)).i=f.(Index(p,f)+i-1))
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume
A1: p in L~f;
  then len f <> 0 by TOPREAL1:28;
  then
A2: f is non empty by CARD_1:47;
  now per cases;
    suppose
A3:   p = f.(Index(p,f)+1);
      then
A4:   L_Cut(f,p)=mid(f,Index(p,f)+1,len f) by Def4;
A5:   1 <= Index(p,f)+1 by NAT_1:11;
      Index(p,f) < len f by A1,Th41;
      then
A6:   Index(p,f)+1 <= len f by NAT_1:13;
      1 in dom f by A2,FINSEQ_5:6;
      then 1 <= len f by FINSEQ_3:27;
      hence (L_Cut(f,p)).1 = p by A3,A4,A5,A6,Th27;
    end;
    suppose p<> f.(Index(p,f)+1);
      then L_Cut(f,p)=<*p*>^mid(f,Index(p,f)+1,len f) by Def4;
      hence (L_Cut(f,p)).1=p by FINSEQ_1:58;
    end;
  end;
  hence (L_Cut(f,p)).1=p;
  let i;
  assume
A7: 1<i & i<=len L_Cut(f,p);
  Index(p,f) < len f by A1,Th41;
  then
A8: 1<=Index(p,f)+1 & Index(p,f)+1<=len f by NAT_1:11,13;
  then
A9: 1<=len f by XXREAL_0:2;
A10: len <*p*><=i by A7,FINSEQ_1:57;
A11: len <*p*><i by A7,FINSEQ_1:56;
A12: 1<=i-1 by A7,SPPOL_1:6;
A13: (i-'1)+1=i-1+1 by A7,XREAL_1:235
    .=i;
  hereby
    assume p = f.(Index(p,f)+1);
    then L_Cut(f,p)=mid(f,Index(p,f)+1,len f) by Def4;
    hence (L_Cut(f,p)).i = f.(i+(Index(p,f)+1)-'1) by A7,A8,A9,Th27
      .= f.(i+Index(p,f)+1-'1)
      .= f.(Index(p,f)+i) by NAT_D:34;
  end;
  assume p <> f.(Index(p,f)+1);
  then
A14: L_Cut(f,p)=<*p*>^mid(f,Index(p,f)+1,len f) by Def4;
  then
A15: i<=len <*p*>+len mid(f,Index(p,f)+1,len f) by A7,FINSEQ_1:35;
  len mid(f,Index(p,f)+1,len f)=len f -'(Index(p,f)+1)+1 by A8,A9,Th27;
  then len <*p*>+len mid(f,Index(p,f)+1,len f)
  =1+(len f -'(Index(p,f)+1)+1) by FINSEQ_1:57
    .=1+(len f -(Index(p,f)+1)+1) by A8,XREAL_1:235
    .=(len f -Index(p,f))+1;
  then i-1<=len f -Index(p,f)+1-1 by A15,XREAL_1:11;
  then
A16: 1<=i-'1 & i-'1<=len f -(Index(p,f)+1)+1 by A12,NAT_D:39;
A17: (L_Cut(f,p)).i =mid(f,Index(p,f)+1,len f).(i-len <*p*>) by A7,A11,A14,Th15
    .=mid(f,Index(p,f)+1,len f).(i-'len <*p*>) by A10,XREAL_1:235
    .=mid(f,Index(p,f)+1,len f).(i-'1) by FINSEQ_1:56
    .=f.((i-'1)+(Index(p,f)+1)-'1) by A8,A16,Th31
    .=f.(Index(p,f)+i-'1) by A13;
  i <=i+Index(p,f) by NAT_1:11;
  hence (L_Cut(f,p)).i=f.(Index(p,f)+i-1) by A7,A17,XREAL_1:235,XXREAL_0:2;
end;

theorem Th59:
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st p in L~f holds
  R_Cut(f,p).(len R_Cut(f,p)) = p &
  for i be Element of NAT st 1<=i & i<=Index(p,f) holds R_Cut(f,p).i = f.i
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume p in L~f;
  then
A1: 1<=Index(p,f) & Index(p,f)<len f by Th41;
  then len f > 1 by XXREAL_0:2;
  then
A2: len mid(f,1,Index(p,f))=Index(p,f)-'1+1 by A1,Th27
    .= Index(p,f) by A1,XREAL_1:237;
  now per cases;
    suppose p<>f.1;
      then
A3:   R_Cut(f,p) = mid(f,1,Index(p,f))^<*p*> by Def5;
      len (mid(f,1,Index(p,f))^<*p*>)= len mid(f,1,Index(p,f))+len <*p*>
      by FINSEQ_1:35
        .= len mid(f,1,Index(p,f))+1 by FINSEQ_1:56;
      hence (R_Cut(f,p)).(len R_Cut(f,p)) = p by A3,FINSEQ_1:59;
    end;
    suppose p=f.1;
      then
A4:   R_Cut(f,p) = <*p*> by Def5;
      then len R_Cut(f,p) = 1 by FINSEQ_1:57;
      hence (R_Cut(f,p)).(len R_Cut(f,p)) = p by A4,FINSEQ_1:57;
    end;
  end;
  hence (R_Cut(f,p)).(len R_Cut(f,p)) = p;
  thus for i be Element of NAT st 1<=i & i<=Index(p,f) holds R_Cut(f,p).i=f.i
  proof
    let i be Element of NAT;
    assume
A5: 1<=i & i<=Index(p,f);
    now per cases;
      case p<>f.1;
        then (R_Cut(f,p)).i=(mid(f,1,Index(p,f))^<*p*>).i by Def5
          .= mid(f,1,Index(p,f)).i by A2,A5,FINSEQ_1:85
          .=f.i by A1,A5,Th32;
        hence R_Cut(f,p).i = f.i;
      end;
      case
A6:     p=f.1;
        then
A7:     R_Cut(f,p) = <*p*> by Def5;
A8:     len f > 1 by A1,XXREAL_0:2;
        then
A9:     len f >= 1+1 by NAT_1:13;
        1 in dom f by A8,FINSEQ_3:27;
        then p = f/.1 by A6,PARTFUN1:def 8;
        then Index(p,f) = 1 by A9,Th44;
        then i = 1 by A5,XXREAL_0:1;
        hence R_Cut(f,p).i = f.i by A6,A7,FINSEQ_1:57;
      end;
    end;
    hence (R_Cut(f,p)).i = f.i;
  end;
end;

theorem
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st p in L~f holds
  ( p<>f.1 implies len R_Cut(f,p)=Index(p,f)+1 ) &
  ( p=f.1 implies len R_Cut(f,p)=Index(p,f) )
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume
A1: p in L~f;
  then consider i be Element of NAT such that
A2: 1 <= i & i+1 <= len f & p in LSeg(f,i) by SPPOL_2:13;
  i<=len f by A2,NAT_D:46;
  then
A3: 1<=len f by A2,XXREAL_0:2;
A4: 1<=Index(p,f) & Index(p,f)<=len f by A1,Th41;
  now per cases;
    case p<>f.1;
      then R_Cut(f,p)=mid(f,1,Index(p,f))^<*p*> by Def5;
      hence len R_Cut(f,p) = len mid(f,1,Index(p,f))+len <*p*> by FINSEQ_1:35
        .= len mid(f,1,Index(p,f))+1 by FINSEQ_1:56
        .= Index(p,f)-'1+1+1 by A3,A4,Th27
        .= Index(p,f)+1 by A4,XREAL_1:237;
    end;
    case
A5:   p=f.1;
      then
A6:   R_Cut(f,p)=<*p*> by Def5;
      len f > i by A2,NAT_1:13;
      then len f > 1 by A2,XXREAL_0:2;
      then
A7:   len f >= 1+1 by NAT_1:13;
      f <> {} by A2,CARD_1:47;
      then 1 in dom f by FINSEQ_5:6;
      then
A8:   p = f/.1 by A5,PARTFUN1:def 8;
      thus len R_Cut(f,p)= 1 by A6,FINSEQ_1:56
        .= Index(p,f) by A7,A8,Th44;
    end;
  end;
  hence ( p<>f.1 implies len R_Cut(f,p)=Index(p,f)+1 ) &
  ( p=f.1 implies len R_Cut(f,p)=Index(p,f) );
end;

theorem Th61:
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st p in L~f holds
  ( p=f.(Index(p,f)+1) implies len L_Cut(f,p) = len f-Index(p,f) ) &
  ( p<>f.(Index(p,f)+1) implies len L_Cut(f,p) = len f-Index(p,f)+1 )
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2;
  assume
A1: p in L~f;
  then consider i be Element of NAT such that
A2: 1 <= i & i+1 <= len f & p in LSeg(f,i) by SPPOL_2:13;
  i<=len f by A2,NAT_D:46;
  then
A3: 1<=len f by A2,XXREAL_0:2;
  1<=Index(p,f) & Index(p,f)<len f by A1,Th41;
  then
A4: 1<Index(p,f)+1 & Index(p,f)+1+0<=len f by NAT_1:13;
  then
A5: len f -(Index(p,f)+1) >= 0 by XREAL_1:21;
  now per cases;
    case p<>f.(Index(p,f)+1);
      then L_Cut(f,p)=<*p*>^mid(f,Index(p,f)+1,len f) by Def4;
      hence len L_Cut(f,p) = 1+len mid(f,Index(p,f)+1,len f) by FINSEQ_5:8
        .= len f -'(Index(p,f)+1)+1+1 by A3,A4,Th27
        .= len f -(Index(p,f)+1)+1+1 by A5,XREAL_0:def 2
        .= len f-Index(p,f)+1;
    end;
    case p=f.(Index(p,f)+1);
      then L_Cut(f,p)=mid(f,Index(p,f)+1,len f) by Def4;
      hence len L_Cut(f,p) = len f -' (Index(p,f)+1) + 1 by A3,A4,Th27
        .= len f -(Index(p,f)+1)+1 by A5,XREAL_0:def 2
        .= len f-Index(p,f);
    end;
  end;
  hence ( p=f.(Index(p,f)+1) implies len L_Cut(f,p)=len f-Index(p,f) ) &
  ( p<>f.(Index(p,f)+1) implies len L_Cut(f,p)=len f-Index(p,f)+1 );
end;

definition
  let p1,p2,q1,q2 be Point of TOP-REAL 2;
  pred LE q1,q2,p1,p2 means
  :Def6:
  q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2) & for r1,r2 being Real st
  0<=r1 & r1<=1 & q1=(1-r1)*p1+r1*p2 &
  0<=r2 & r2<=1 & q2=(1-r2)*p1+r2*p2 holds r1<=r2;
end;

definition
  let p1,p2,q1,q2 be Point of TOP-REAL 2;
  pred LT q1,q2,p1,p2 means
  :Def7:
  LE q1,q2,p1,p2 & q1<>q2;
end;

theorem
  for p1,p2,q1,q2 being Point of TOP-REAL 2 st
  LE q1,q2,p1,p2 & LE q2,q1,p1,p2 holds q1=q2
proof
  let p1,p2,q1,q2 be Point of TOP-REAL 2;
  assume
A1: LE q1,q2,p1,p2 & LE q2,q1,p1,p2;
  then
A2: q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2) & for r1,r2 being Real st
  0<=r1 & r1<=1 & q1=(1-r1)*p1+r1*p2 &
  0<=r2 & r2<=1 & q2=(1-r2)*p1+r2*p2 holds r1<=r2 by Def6;
  then consider r1 such that
A3: 0<=r1 & r1<=1 & q1 = (1-r1)*p1+r1*p2 by SPPOL_1:21;
  consider r2 such that
A4: 0<=r2 & r2<=1 & q2 = (1-r2)*p1+r2*p2 by A2,SPPOL_1:21;
A5: r1<=r2 by A1,A3,A4,Def6;
  r2<=r1 by A1,A3,A4,Def6;
  then r1=r2 by A5,XXREAL_0:1;
  hence q1=q2 by A3,A4;
end;

theorem Th63:
  for p1,p2,q1,q2 being Point of TOP-REAL 2 st
  q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2) & p1<>p2
  holds (LE q1,q2,p1,p2 or LT q2,q1,p1,p2)
  & not(LE q1,q2,p1,p2 & LT q2,q1,p1,p2)
proof
  let p1,p2,q1,q2 be Point of TOP-REAL 2;
  assume
A1: q1 in LSeg(p1,p2) & q2 in LSeg(p1,p2)& p1<>p2;
  then consider r1 such that
A2: 0 <= r1 & r1 <= 1 & q1= (1-r1)*p1 + r1*p2 by SPPOL_1:21;
  consider r2 such that
A3: 0 <= r2 & r2 <= 1& q2= (1-r2)*p1 + r2*p2 by A1,SPPOL_1:21;
A4: now per cases;
    case
A5:   r1<=r2;
      for s1,s2 being Real st 0<=s1 & s1<=1 & q1=(1-s1)*p1+s1*p2 &
      0<=s2 & s2<=1 & q2=(1-s2)*p1+s2*p2 holds s1<=s2
      proof
        let s1,s2 be Real;
        assume
A6:     0<=s1 & s1<=1 & q1=(1-s1)*p1+s1*p2 &
        0<=s2 & s2<=1 & q2=(1-s2)*p1+s2*p2;
        then (1-s1)*p1+(s1*p2-s1*p2)=(1-r1)*p1+r1*p2-s1*p2 by A2,EUCLID:49;
        then (1-s1)*p1+(s1*p2-s1*p2)=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:49;
        then (1-s1)*p1+(0.REAL 2)=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:46;
        then (1-s1)*p1=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:31;
        then (1-s1)*p1=(1-r1)*p1+(r1-s1)*p2 by EUCLID:54;
        then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2+((1-r1)*p1-(1-r1)*p1) by EUCLID:49;
        then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2+(0.REAL 2) by EUCLID:46;
        then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2 by EUCLID:31;
        then ((1-s1)-(1-r1))*p1=(r1-s1)*p2 by EUCLID:54;
        then
A7:     (r1-s1)=0 or p1=p2 by EUCLID:38;
        (1-s2)*p1+(s2*p2-s2*p2)=(1-r2)*p1+r2*p2-s2*p2 by A3,A6,EUCLID:49;
        then (1-s2)*p1+(s2*p2-s2*p2)=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:49;
        then (1-s2)*p1+(0.REAL 2)=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:46;
        then (1-s2)*p1=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:31;
        then (1-s2)*p1=(1-r2)*p1+(r2-s2)*p2 by EUCLID:54;
        then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2+((1-r2)*p1-(1-r2)*p1) by EUCLID:49;
        then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2+(0.REAL 2) by EUCLID:46;
        then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2 by EUCLID:31;
        then ((1-s2)-(1-r2))*p1=(r2-s2)*p2 by EUCLID:54;
        then (r2-s2)=0 or p1=p2 by EUCLID:38;
        hence s1<=s2 by A1,A5,A7;
      end;
      hence (LE q1,q2,p1,p2 or LT q2,q1,p1,p2) by A1,Def6;
    end;
    case
A8:   r1>r2;
      for s2,s1 being Real st 0<=s2 & s2<=1 & q2=(1-s2)*p1+s2*p2 &
      0<=s1 & s1<=1 & q1=(1-s1)*p1+s1*p2 holds s1>=s2
      proof
        let s2,s1 be Real;
        assume
A9:     0<=s2 & s2<=1 & q2=(1-s2)*p1+s2*p2 &
        0<=s1 & s1<=1 & q1=(1-s1)*p1+s1*p2;
        then (1-s1)*p1+(s1*p2-s1*p2)=(1-r1)*p1+r1*p2-s1*p2 by A2,EUCLID:49;
        then (1-s1)*p1+(s1*p2-s1*p2)=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:49;
        then (1-s1)*p1+(0.REAL 2)=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:46;
        then (1-s1)*p1=(1-r1)*p1+(r1*p2-s1*p2) by EUCLID:31;
        then (1-s1)*p1=(1-r1)*p1+(r1-s1)*p2 by EUCLID:54;
        then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2+((1-r1)*p1-(1-r1)*p1) by EUCLID:49;
        then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2+(0.REAL 2) by EUCLID:46;
        then (1-s1)*p1-(1-r1)*p1=(r1-s1)*p2 by EUCLID:31;
        then ((1-s1)-(1-r1))*p1=(r1-s1)*p2 by EUCLID:54;
        then
A10:    (r1-s1)=0 or p1=p2 by EUCLID:38;
        (1-s2)*p1+(s2*p2-s2*p2)=(1-r2)*p1+r2*p2-s2*p2 by A3,A9,EUCLID:49;
        then (1-s2)*p1+(s2*p2-s2*p2)=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:49;
        then (1-s2)*p1+(0.REAL 2)=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:46;
        then (1-s2)*p1=(1-r2)*p1+(r2*p2-s2*p2) by EUCLID:31
          .=(r2-s2)*p2+(1-r2)*p1 by EUCLID:54;
        then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2+((1-r2)*p1-(1-r2)*p1) by EUCLID:49;
        then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2+(0.REAL 2) by EUCLID:46;
        then (1-s2)*p1-(1-r2)*p1=(r2-s2)*p2 by EUCLID:31;
        then ((1-s2)-(1-r2))*p1=(r2-s2)*p2 by EUCLID:54;
        then (r2-s2)=0 or p1=p2 by EUCLID:38;
        hence s1>=s2 by A1,A8,A10;
      end;
      then LE q2,q1,p1,p2 & q1<>q2 by A1,A2,A3,A8,Def6;
      hence (LE q1,q2,p1,p2 or LT q2,q1,p1,p2) by Def7;
    end;
  end;
  now
    assume
A11: LE q1,q2,p1,p2 & LT q2,q1,p1,p2;
    then
A12: r1<=r2 by A2,A3,Def6;
    LE q2,q1,p1,p2 & q1<>q2 by A11,Def7;
    then r2<=r1 by A2,A3,Def6;
    then r1=r2 by A12,XXREAL_0:1;
    hence contradiction by A2,A3,A11,Def7;
  end;
  hence (LE q1,q2,p1,p2 or LT q2,q1,p1,p2)
  & not(LE q1,q2,p1,p2 & LT q2,q1,p1,p2) by A4;
end;

theorem Th64:
  for f be FinSequence of TOP-REAL 2 for p,q be Point of TOP-REAL 2 st
  p in L~f & q in L~f & Index(p,f)<Index(q,f) holds q in L~L_Cut(f,p)
proof
  let f be FinSequence of TOP-REAL 2;
  let p,q be Point of TOP-REAL 2;
  assume that
A1: p in L~f and
A2: q in L~f and
A3: Index(p,f)<Index(q,f);
  set i1 = Index(q,f)-'Index(p,f)+1;
A4: 1<=Index(q,f) & Index(q,f)<len f by A2,Th41;
A5: Index(p,f)+1<=Index(q,f) by A3,NAT_1:13;
  then
A6: Index(p,f)+1-Index(p,f)<=Index(q,f)-Index(p,f) by XREAL_1:11;
  then
A7: 1<=Index(q,f)-'Index(p,f) by XREAL_0:def 2;
  then
A8: 1<=Index(q,f)-'Index(p,f)+1 by NAT_D:48;
  1+1<=Index(q,f)-'Index(p,f)+1 by A7,XREAL_1:8;
  then
A9: 1<Index(q,f)-'Index(p,f)+1 by XXREAL_0:2;
  then
A10: len <*p*><Index(q,f)-'Index(p,f)+1 by FINSEQ_1:57;
  then
A11: len <*p*><Index(q,f)-'Index(p,f)+1+1 by NAT_1:13;
A12: 1<=Index(p,f)+1 & Index(p,f)+1<=len f by A4,A5,NAT_1:11,XXREAL_0:2;
  1<len f by A4,XXREAL_0:2;
  then len mid(f,Index(p,f)+1,len f) = len f -'(Index(p,f)+1)+1 by A12,Th27;
  then
A13: len <*p*>+len mid(f,Index(p,f)+1,len f) =
  1+(len f -'(Index(p,f)+1)+1) by FINSEQ_1:57
    .= 1+(len f -(Index(p,f)+1)+1) by A12,XREAL_1:235
    .= (len f -Index(p,f))+1;
A14: Index(q,f)-Index(p,f)<=len f -Index(p,f) by A4,XREAL_1:11;
  then
A15: Index(q,f)-Index(p,f)+1<=len f -Index(p,f)+1 by XREAL_1:8;
  then
A16: Index(q,f)-'Index(p,f)+1<=len <*p*>+len mid(f,Index(p,f)+1,len f)
  by A3,A13,XREAL_1:235;
  Index(q,f)-Index(p,f)<=len f -Index(p,f)-1+1 by A4,XREAL_1:11;
  then
A17: Index(q,f)-'Index(p,f)<=len f - (Index(p,f)+1)+1 by A3,XREAL_1:235;
  per cases;
  suppose
A18: p = f.(Index(p,f)+1);
    then
A19: len L_Cut(f,p)=len f -Index(p,f) by A1,Th61;
    then len L_Cut(f,p)>=Index(q,f)-'Index(p,f) by A3,A14,XREAL_1:235;
    then (L_Cut(f,p))/.(Index(q,f)-'Index(p,f))
    = L_Cut(f,p).(Index(q,f)-'Index(p,f)) by A7,FINSEQ_4:24
      .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)) by A18,Def4
      .= f.(Index(p,f)+1+(Index(q,f)-'Index(p,f))-1) by A7,A12,A17,Th31
      .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f))-1) by A3,XREAL_1:235
      .= f.Index(q,f);
    then
A20: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f))=f/.Index(q,f) by A4,FINSEQ_4:24;
A21: Index(q,f)<len f by A2,Th41;
    then
A22: Index(q,f)+1<=len f by NAT_1:13;
    then Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by XREAL_1:11;
    then Index(q,f)-Index(p,f)+1<=len f -Index(p,f);
    then
A23: i1<=len L_Cut(f,p) by A6,A19,XREAL_0:def 2;
A24: 1<=Index(q,f) & Index(q,f)+1<=len f & q in LSeg(f,Index(q,f))
    by A2,A21,Th41,Th42,NAT_1:13;
A25: Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by A22,XREAL_1:11;
    then Index(q,f)-Index(p,f)+1<=len f -Index(p,f);
    then
A26: len L_Cut(f,p)>=Index(q,f)-'Index(p,f)+1 by A3,A19,XREAL_1:235;
    Index(q,f)-Index(p,f)+1<=len f -Index(p,f) by A25;
    then
A27: Index(q,f)-'Index(p,f)+1<=len f - (Index(p,f)+1)+1 by A3,XREAL_1:235;
A28: 1<=Index(q,f)+1 & Index(q,f)+1<=len f by A24,NAT_D:48;
A29: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1)
    = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1) by A9,A26,FINSEQ_4:24
      .= (mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1) by A18,Def4
      .= f.(Index(p,f)+1+(Index(q,f)-'Index(p,f)+1)-1) by A8,A12,A27,Th31
      .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f)+1)-1) by A3,XREAL_1:235
      .= f/.(Index(q,f)+1) by A28,FINSEQ_4:24;
    q in LSeg(f,Index(q,f)) by A2,Th42;
    then q in LSeg((L_Cut(f,p))/.((Index(q,f)-'Index(p,f))),
    (L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1)))
    by A4,A20,A22,A29,TOPREAL1:def 5;
    hence q in L~(L_Cut(f,p)) by A7,A23,SPPOL_2:15;
  end;
  suppose that
A30: p <> f.(Index(p,f)+1);
A31: len L_Cut(f,p)=len f -Index(p,f)+1 by A1,A30,Th61;
    then len L_Cut(f,p)>=Index(q,f)-'Index(p,f)+1 by A3,A15,XREAL_1:235;
    then (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1)
    = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1) by FINSEQ_4:24,NAT_1:11
      .= (<*p*>^mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1)
    by A30,Def4
      .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1-len <*p*>)
    by A10,A16,Th15
      .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1-1)
    by FINSEQ_1:57
      .= f.(Index(p,f)+1+(Index(q,f)-'Index(p,f))-1)
    by A7,A12,A17,Th31
      .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f))-1) by A3,XREAL_1:235
      .= f.Index(q,f);
    then
A32: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1)=f/.Index(q,f) by A4,FINSEQ_4:24;
A33: Index(q,f)<len f by A2,Th41;
    then
A34: Index(q,f)+1<=len f by NAT_1:13;
    then Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by XREAL_1:11;
    then Index(q,f)-Index(p,f)+1<=len f -Index(p,f);
    then i1<=len f -Index(p,f) by A6,XREAL_0:def 2;
    then
A35: i1+1<=len L_Cut(f,p) by A31,XREAL_1:8;
A36: 1<=Index(q,f) & Index(q,f)+1<=len f & q in LSeg(f,Index(q,f))
    by A2,A33,Th41,Th42,NAT_1:13;
A37: Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by A34,XREAL_1:11;
    then
A38: Index(q,f)-Index(p,f)+1+1<=len f -Index(p,f)+1 by XREAL_1:8;
    then
A39: len L_Cut(f,p)>=Index(q,f)-'Index(p,f)+1+1 by A3,A31,XREAL_1:235;
A40: Index(q,f)-'Index(p,f)+1+1<=len <*p*>+len mid(f,Index(p,f)+1,len f)
    by A3,A13,A38,XREAL_1:235;
    Index(q,f)-Index(p,f)+1<=len f -Index(p,f) by A37;
    then
A41: Index(q,f)-'Index(p,f)+1<=len f - (Index(p,f)+1)+1 by A3,XREAL_1:235;
A42: 1<=Index(q,f)+1 & Index(q,f)+1<=len f by A36,NAT_D:48;
A43: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1+1)
    = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1+1) by A39,FINSEQ_4:24,NAT_1:11
      .= (<*p*>^mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1+1)
    by A30,Def4
      .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1+1-len <*p*>)
    by A11,A40,Th15
      .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1+1-1)
    by FINSEQ_1:57
      .= f.(Index(p,f)+1+(Index(q,f)-'Index(p,f)+1)-1)
    by A8,A12,A41,Th31
      .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f)+1)-1) by A3,XREAL_1:235
      .= f/.(Index(q,f)+1) by A42,FINSEQ_4:24;
    q in LSeg(f,Index(q,f)) by A2,Th42;
    then q in LSeg((L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1)),
    (L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1+1)))
    by A4,A32,A34,A43,TOPREAL1:def 5;
    hence q in L~(L_Cut(f,p)) by A35,NAT_1:11,SPPOL_2:15;
  end;
end;

theorem Th65:
  for p,q,p1,p2 being Point of TOP-REAL 2 st
  LE p,q,p1,p2 holds q in LSeg(p,p2) & p in LSeg(p1,q)
proof
  let p,q,p1,p2 be Point of TOP-REAL 2;
  assume
A1: LE p,q,p1,p2;
  then
A2: p in LSeg(p1,p2) & q in LSeg(p1,p2) & for r1,r2 being Real st
  0<=r1 & r1<=1 & p=(1-r1)*p1+r1*p2 & 0<=r2 & r2<=1 & q=(1-r2)*p1+r2*p2 holds
  r1<=r2 by Def6;
  then consider s1 being Real such that
A3: 0<=s1 & s1<=1 & p=(1-s1)*p1+s1*p2 by SPPOL_1:21;
  consider s2 being Real such that
A4: 0<=s2 & s2<=1 & q=(1-s2)*p1+s2*p2 by A2,SPPOL_1:21;
A5: s1<=s2 by A1,A3,A4,Def6;
A6: 1-s1>=0 by A3,XREAL_1:50;
A7: now per cases;
    case
A8:   1-s1<>0;
A9:   s2-s1>=0 by A5,XREAL_1:50;
      set s=(s2-s1)/(1-s1);
A10:  s>=0 by A6,A9;
A11:  (1-s1)*(((1-s2)/(1-s1)))=1-s2 by A8,XCMPLX_1:88;
A12:  (1-s1)*(((s2-s1)/(1-s1)))=s2-s1 by A8,XCMPLX_1:88;
A13:  (1-(s2-s1)/(1-s1))=(1*(1-s1)-(s2-s1))/(1-s1) by A8,XCMPLX_1:128
        .=((1-s1)+s1-s2)/(1-s1);
      1-s1>=s2-s1 by A4,XREAL_1:11;
      then (1-s1)/(1-s1)>=(s2-s1)/(1-s1) by A6,XREAL_1:74;
      then
A14:  1>=s by A8,XCMPLX_1:60;
      (1-s1)*((1-s)*p+s*p2) =(1-s1)*(((1-s2)/(1-s1))*((1-s1)*p1+s1*p2))
      +(1-s1)*(((s2-s1)/(1-s1))*p2) by A3,A13,EUCLID:36
        .=(1-s1)*(((1-s2)/(1-s1)))*((1-s1)*p1+s1*p2)
      +(1-s1)*(((s2-s1)/(1-s1))*p2) by EUCLID:34
        .=(1-s2)*((1-s1)*p1+s1*p2)
      +(1-s1)*(((s2-s1)/(1-s1)))*p2 by A11,EUCLID:34
        .=(1-s2)*((1-s1)*p1)+(1-s2)*(s1*p2) +(s2-s1)*p2 by A12,EUCLID:36
        .=(1-s2)*(1-s1)*p1+(1-s2)*(s1*p2) +(s2-s1)*p2 by EUCLID:34
        .=(1-s2)*(1-s1)*p1+(1-s2)*s1*p2 +(s2-s1)*p2 by EUCLID:34
        .=(1-s2)*(1-s1)*p1+((1-s2)*s1*p2 +(s2-s1)*p2) by EUCLID:30
        .=(1-s2)*(1-s1)*p1+(1*s1-s2*s1+(s2-s1))*p2 by EUCLID:37
        .=(1-s1)*((1-s2)*p1)+(1-s1)*s2*p2 by EUCLID:34
        .=(1-s1)*((1-s2)*p1)+(1-s1)*(s2*p2) by EUCLID:34
        .=(1-s1)*q by A4,EUCLID:36;
      then q=(1-s)*p+s*p2 by A8,EUCLID:38;
      hence q in LSeg(p,p2) by A10,A14,SPPOL_1:22;
    end;
    case 1-s1=0;
      then s2=1 by A4,A5,XXREAL_0:1;
      then q =0.REAL 2 + 1*p2 by A4,EUCLID:33
        .=0.REAL 2 + p2 by EUCLID:33
        .=p2 by EUCLID:31;
      hence q in LSeg(p,p2) by TOPREAL1:6;
    end;
  end;
  now per cases;
    case
A15:  s2<>0;
      set s=s1/s2;
A16:  s>=0 by A3,A4;
A17:  s2*(((s2-s1)/s2))=s2-s1 by A15,XCMPLX_1:88;
A18:  s2*((s1)/s2)=s1 by A15,XCMPLX_1:88;
A19:  (s2-s1)+s1*(1-s2)=s2*(1-s1);
      s2/s2>=s1/s2 by A4,A5,XREAL_1:74;
      then
A20:  1>=s by A15,XCMPLX_1:60;
      s2*((1-s)*p1+s*q) =s2*(((1*s2-s1)/s2)*p1
      +(s1/s2)*((1-s2)*p1+s2*p2)) by A4,A15,XCMPLX_1:128
        .=s2*(((s2-s1)/s2)*p1) +s2*((s1/s2)*((1-s2)*p1+s2*p2)) by EUCLID:36
        .=s2*(((s2-s1)/s2))*p1 +s2*((s1/s2)*((1-s2)*p1+s2*p2)) by EUCLID:34
        .=(s2-s1)*p1 +s2*(s1/s2)*((1-s2)*p1+s2*p2) by A17,EUCLID:34
        .=(s2-s1)*p1 +(s1*((1-s2)*p1)+s1*(s2*p2)) by A18,EUCLID:36
        .=(s2-s1)*p1 +(s1*(1-s2)*p1+s1*(s2*p2)) by EUCLID:34
        .=(s2-s1)*p1 +(s1*(1-s2)*p1+s1*s2*p2) by EUCLID:34
        .=(s2-s1)*p1 +s1*(1-s2)*p1+s1*s2*p2 by EUCLID:30
        .=((s2-s1)+s1*(1-s2))*p1+s1*s2*p2 by EUCLID:37
        .=s2*((1-s1)*p1)+s2*s1*p2 by A19,EUCLID:34
        .=s2*((1-s1)*p1)+s2*(s1*p2) by EUCLID:34
        .=s2*p by A3,EUCLID:36;
      then p=(1-s)*p1+s*q by A15,EUCLID:38;
      hence p in LSeg(p1,q) by A16,A20,SPPOL_1:22;
    end;
    case s2=0;
      then s1=0 by A1,A3,A4,Def6;
      then p =1*p1+(0.REAL 2) by A3,EUCLID:33
        .=p1+(0.REAL 2) by EUCLID:33
        .=p1 by EUCLID:31;
      hence p in LSeg(p1,q) by TOPREAL1:6;
    end;
  end;
  hence q in LSeg(p,p2) & p in LSeg(p1,q) by A7;
end;

theorem Th66:
  for f be FinSequence of TOP-REAL 2
  for p,q be Point of TOP-REAL 2 st p in L~f & q in L~f & p<>q &
  Index(p,f) = Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1) holds
  q in L~L_Cut(f,p)
proof
  let f be FinSequence of TOP-REAL 2;
  let p,q be Point of TOP-REAL 2;
  assume that
A1: p in L~f and
A2: q in L~f and
A3: p<>q and
A4: Index(p,f)=Index(q,f) and
A5: LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
  set i1=Index(q,f)-'Index(p,f)+1;
A6: 1<=Index(q,f) & Index(q,f)<len f by A2,Th41;
  Index(q,f)<len f by A2,Th41;
  then
A7: Index(q,f)+1<=len f by NAT_1:13;
A8: Index(q,f)-'Index(p,f)=Index(q,f)-Index(p,f) by A4,XREAL_1:235
    .=0 by A4;
  1<0+1+1;
  then
A9: len <*p*> < Index(q,f)-'Index(p,f)+1+1 by A8,FINSEQ_1:57;
A10: 1<=Index(p,f)+1 by NAT_1:11;
  1<len f by A6,XXREAL_0:2;
  then len mid(f,Index(p,f)+1,len f) = len f -'(Index(p,f)+1)+1
  by A4,A7,A10,Th27;
  then
A11: len <*p*> + len mid(f,Index(p,f)+1,len f) =
  1+(len f -'(Index(p,f)+1)+1) by FINSEQ_1:57
    .= 1+(len f -(Index(p,f)+1)+1) by A4,A7,XREAL_1:235
    .= (len f -Index(p,f))+1;
  Index(q,f)-Index(p,f)<=len f -Index(p,f) by A6,XREAL_1:11;
  then
A12: Index(q,f)-Index(p,f)+1<=len f -Index(p,f)+1 by XREAL_1:8;
A13: now
    assume
A14: p = f.(Index(p,f)+1);
    then
A15: p = f/.(Index(p,f)+1) by A4,A7,FINSEQ_4:24,NAT_1:11;
    q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A5,Def6;
    then consider r be Real such that
A16: 0 <= r & r <= 1 and
A17: q = (1-r)*f/.(Index(p,f))+r*f/.(Index(p,f)+1) by SPPOL_1:21;
A18: p = 1 * p by EUCLID:33
      .= 0.REAL 2 + 1 * p by EUCLID:31
      .= (1-1)*f/.(Index(p,f))+1 * p by EUCLID:33;
    then 1<=r by A5,A15,A16,A17,Def6;
    then r = 1 by A16,XXREAL_0:1;
    hence contradiction by A3,A4,A7,A14,A17,A18,FINSEQ_4:24,NAT_1:11;
  end;
  then
A19: len L_Cut(f,p)=len f -Index(p,f)+1 by A1,Th61;
  then
A20: (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1)
  = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1) by A4,A8,A12,FINSEQ_4:24
    .= (<*p*>^mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1)
  by A13,Def4
    .= p by A8,FINSEQ_1:58;
A21: Index(q,f)<len f by A2,Th41;
  then
A22: Index(q,f)+1<=len f by NAT_1:13;
  then Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by XREAL_1:11;
  then Index(q,f)-Index(p,f)+1<=len f -Index(p,f);
  then i1<=len f -Index(p,f) by A4,XREAL_0:def 2;
  then
A23: i1+1<=len L_Cut(f,p) by A19,XREAL_1:8;
A24: 1<=Index(q,f) & Index(q,f)+1<=len f & q in LSeg(f,Index(q,f))
  by A2,A21,Th41,Th42,NAT_1:13;
A25: Index(q,f)+1-Index(p,f)<=len f -Index(p,f) by A22,XREAL_1:11;
  then
A26: Index(q,f)-Index(p,f)+1+1<=len f -Index(p,f)+1 by XREAL_1:8;
  then
A27: Index(q,f)-'Index(p,f)+1+1<=len <*p*>+len mid(f,Index(p,f)+1,len f)
  by A4,A11,XREAL_1:235;
  Index(q,f)-Index(p,f)+1<=len f -Index(p,f) by A25;
  then
A28: Index(q,f)-'Index(p,f)+1<=len f - (Index(p,f)+1)+1 by A4,XREAL_1:235;
A29: 1<=Index(q,f)+1 & Index(q,f)+1<=len f by A24,NAT_D:48;
  (L_Cut(f,p))/.(Index(q,f)-'Index(p,f)+1+1)
  = L_Cut(f,p).(Index(q,f)-'Index(p,f)+1+1) by A4,A8,A19,A26,FINSEQ_4:24
    .= (<*p*>^mid(f,Index(p,f)+1,len f)).(Index(q,f)-'Index(p,f)+1+1)
  by A13,Def4
    .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1+1-len <*p*>)
  by A9,A27,Th15
    .= mid(f,Index(p,f)+1,len f).(Index(q,f)-'Index(p,f)+1+1-1)
  by FINSEQ_1:57
    .= f.(Index(p,f)+1+(Index(q,f)-Index(p,f)+1)-1)
  by A4,A7,A8,A10,A28,Th31
    .= f/.(Index(q,f)+1) by A29,FINSEQ_4:24;
  then q in LSeg((L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1)),
  (L_Cut(f,p))/.((Index(q,f)-'Index(p,f)+1+1))) by A4,A5,A20,Th65;
  hence q in L~(L_Cut(f,p)) by A8,A23,SPPOL_2:15;
end;

begin

::--------------------------------------------------------:
::  Cutting Both Sides of a Finite Sequence and :
::  a Discussion of Speciality of Sequences in TOP-REAL 2 :
::--------------------------------------------------------:

definition
  let f be FinSequence of TOP-REAL 2,p,q be Point of TOP-REAL 2;
  func B_Cut(f,p,q) -> FinSequence of TOP-REAL 2 equals
  :Def8:
  R_Cut(L_Cut(f,p),q) if p in L~f & q in L~f & Index(p,f)<Index(q,f)
  or Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1)
  otherwise Rev (R_Cut(L_Cut(f,q),p));
  correctness;
end;

theorem Th67:
  for f being FinSequence of TOP-REAL 2, p being Point of TOP-REAL 2
  st f is_S-Seq & p in L~f & p<>f.1 holds R_Cut(f,p) is_S-Seq_joining f/.1,p
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume that
A1: f is_S-Seq and
A2: p in L~f and
A3: p<>f.1;
  R_Cut(f,p)=mid(f,1,Index(p,f))^<*p*> by A3,Def5;
  hence thesis by A1,A2,A3,Th52;
end;

theorem Th68:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st f is_S-Seq & p in L~f &
  p<>f.len f holds L_Cut(f,p) is_S-Seq_joining p,f/.len f
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume that
A1: f is_S-Seq & p in L~f and
A2: p<>f.len f;
  len f <> 0 by A1,TOPREAL1:28;
  then
A3: f <> {} by CARD_1:47;
A4: Rev f is_S-Seq & p in L~Rev f by A1,SPPOL_2:22,47;
A5: L_Cut(f,p) = L_Cut(Rev Rev f,p) by FINSEQ_6:29
    .= Rev R_Cut(Rev f,p) by A4,Th57;
  p <> (Rev f).1 by A2,FINSEQ_5:65;
  then L_Cut(f,p) is_S-Seq_joining p,(Rev f)/.1 by A4,A5,Th48,Th67;
  hence thesis by A3,FINSEQ_5:68;
end;

theorem Th69:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st f is_S-Seq & p in L~f &
  p<>f.len f holds L_Cut(f,p) is_S-Seq
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume f is_S-Seq & p in L~f & p<>f.len f;
  then L_Cut(f,p) is_S-Seq_joining p,f/.len f by Th68;
  hence L_Cut(f,p) is_S-Seq by Def3;
end;

theorem Th70:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st f is_S-Seq & p in L~f &
  p<>f.1 holds R_Cut(f,p) is_S-Seq
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume f is_S-Seq & p in L~f & p<>f.1;
  then R_Cut(f,p) is_S-Seq_joining f/.1,p by Th67;
  hence R_Cut(f,p) is_S-Seq by Def3;
end;

Lm1: for f being FinSequence of TOP-REAL 2,
p,q being Point of TOP-REAL 2 st f is_S-Seq & p in L~f & q in L~f &
p<>q & (Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
& LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1))
holds B_Cut(f,p,q) is_S-Seq_joining p,q
proof
  let f be FinSequence of TOP-REAL 2, p,q be Point of TOP-REAL 2;
  assume
A1: f is_S-Seq & p in L~f & q in L~f & p<>q;
  assume
A2: Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
  & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
  then
A3: B_Cut(f,p,q)=R_Cut(L_Cut(f,p),q) by A1,Def8;
A4: 1<=Index(q,f) & Index(q,f)<len f by A1,Th41;
  Index(p,f) < len f by A1,Th41;
  then
A5: Index(p,f)+1 <= len f by NAT_1:13;
A6: 1<len f by A4,XXREAL_0:2;
A7: now per cases by A2;
    case Index(p,f)<Index(q,f);
      then
A8:   Index(p,f)+1<=Index(q,f) by NAT_1:13;
      assume p=f.len f;
      then len f <= Index(q,f) by A1,A6,A8,Th45;
      hence contradiction by A1,Th41;
    end;
    case
A9:   Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
A10:  now
        assume
A11:    p = f.(Index(p,f)+1);
        then
A12:    p = f/.(Index(p,f)+1) by A5,FINSEQ_4:24,NAT_1:11;
        q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A9,Def6;
        then consider r being Real such that
A13:    0 <= r & r <= 1 and
A14:    q = (1-r)*f/.(Index(p,f))+r*f/.(Index(p,f)+1) by SPPOL_1:21;
A15:    p = 1*p by EUCLID:33
          .= 0.REAL 2 + 1*p by EUCLID:31
          .= (1-1)*f/.(Index(p,f))+1*p by EUCLID:33;
        then 1<=r by A9,A12,A13,A14,Def6;
        then r = 1 by A13,XXREAL_0:1;
        hence contradiction by A1,A5,A11,A14,A15,FINSEQ_4:24,NAT_1:11;
      end;
      assume p=f.len f;
      hence contradiction by A1,A6,A10,Th45;
    end;
  end;
  then
A16: L_Cut(f,p) is_S-Seq_joining p,f/.len f by A1,Th68;
  now per cases by A2;
    case Index(p,f)<Index(q,f);
      then q in L~(L_Cut(f,p)) by A1,Th64;
      hence ex i1 being Element of NAT st 1<=i1 & i1+1<=len L_Cut(f,p)
      & q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
    end;
    case Index(p,f)=Index(q,f) & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
      then q in L~(L_Cut(f,p)) by A1,Th66;
      hence ex i1 being Element of NAT st 1<=i1 & i1+1<=len L_Cut(f,p)
      & q in LSeg(L_Cut(f,p),i1) by SPPOL_2:13;
    end;
  end;
  then
A17: q in L~L_Cut(f,p) by SPPOL_2:17;
  then 1<=Index(q,L_Cut(f,p)) & Index(q,L_Cut(f,p))<len L_Cut(f,p) by Th41;
  then
A18: 1<=len L_Cut(f,p) by XXREAL_0:2;
A19: (L_Cut(f,p)).1=p by A16,Def3;
  then p=(L_Cut(f,p))/.(1) by A18,FINSEQ_4:24;
  hence B_Cut(f,p,q) is_S-Seq_joining p,q by A1,A3,A7,A17,A19,Th67,Th69;
end;

theorem Th71:
  for f being FinSequence of TOP-REAL 2,
  p,q being Point of TOP-REAL 2 st f is_S-Seq & p in L~f & q in L~f &
  p<>q holds B_Cut(f,p,q) is_S-Seq_joining p,q
proof
  let f be FinSequence of TOP-REAL 2, p,q be Point of TOP-REAL 2;
  assume
A1: f is_S-Seq & p in L~f & q in L~f & p<>q;
  per cases;
  suppose Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
    & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
    hence thesis by A1,Lm1;
  end;
  suppose
A2: not(Index(p,f)<Index(q,f) or Index(p,f)=Index(q,f)
    & LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1));
    then
A3: B_Cut(f,p,q)=Rev R_Cut(L_Cut(f,q),p) by Def8;
A4: Index(q,f) < Index(p,f)
    or Index(p,f)=Index(q,f) & not LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1)
    by A2,XXREAL_0:1;
A5: now
      assume that
A6:   Index(p,f)=Index(q,f) and
A7:   not LE p,q,f/.(Index(p,f)),f/.(Index(p,f)+1);
A8:   1 <= Index(p,f) by A1,Th41;
A9:   Index(p,f) < len f by A1,Th41;
      then
A10:  Index(p,f)+1 <= len f by NAT_1:13;
      then
A11:  LSeg(f,Index(p,f)) = LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
      by A8,TOPREAL1:def 5;
      then
A12:  p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A1,Th42;
A13:  q in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A1,A6,A11,Th42;
A14:  Index(p,f) in dom f by A8,A9,FINSEQ_3:27;
      1 <= Index(p,f)+1 by NAT_1:11;
      then
A15:  Index(p,f)+1 in dom f by A10,FINSEQ_3:27;
A16:  Index(p,f)+0 <> Index(p,f)+1;
      f is one-to-one by A1,TOPREAL1:def 10;
      then f/.(Index(p,f))<>f/.(Index(p,f)+1) by A14,A15,A16,PARTFUN2:17;
      then LT q,p,f/.(Index(p,f)),f/.(Index(p,f)+1) by A7,A12,A13,Th63;
      hence LE q,p,f/.Index(q,f),f/.(Index(q,f)+1) by A6,Def7;
    end;
    then
A17: B_Cut(f,q,p) is_S-Seq_joining q,p by A1,A4,Lm1;
    Rev B_Cut(f,q,p) = B_Cut(f,p,q) by A1,A3,A4,A5,Def8;
    hence thesis by A17,Th48;
  end;
end;

theorem
  for f being FinSequence of TOP-REAL 2,
  p,q being Point of TOP-REAL 2 st f is_S-Seq & p in L~f & q in L~f &
  p<>q holds B_Cut(f,p,q) is_S-Seq
proof
  let f be FinSequence of TOP-REAL 2, p,q be Point of TOP-REAL 2;
  assume f is_S-Seq & p in L~f & q in L~f & p<>q;
  then B_Cut(f,p,q) is_S-Seq_joining p,q by Th71;
  hence B_Cut(f,p,q) is_S-Seq by Def3;
end;

theorem Th73:
  for f,g being FinSequence of TOP-REAL 2 st f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} holds f^mid(g,2,len g) is_S-Seq
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume
A1: f.len f=g.1 & f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1};
  then
A2: f is one-to-one & len f >= 2
  & f is unfolded s.n.c. special by TOPREAL1:def 10;
A3: g is one-to-one & len g >= 2
  & g is unfolded s.n.c. special by A1,TOPREAL1:def 10;
A4: 1<=len f by A2,XXREAL_0:2;
A5: 1<=len g by A3,XXREAL_0:2;
A6: len (f^mid(g,2,len g))=len f + len mid(g,2,len g) by FINSEQ_1:35;
  then
A7: len f<=len f + len mid(g,2,len g) &
  len f<=len (f^mid(g,2,len g)) by NAT_1:11;
A8: len mid(g,2,len g)=len g -'2+1 by A3,A5,Th27
    .=len g -2+1 by A3,XREAL_1:235
    .=len g -1;
A9: for x1,x2 being set st
  x1 in dom (f^mid(g,2,len g)) & x2 in dom (f^mid(g,2,len g))
  & (f^mid(g,2,len g)).x1=(f^mid(g,2,len g)).x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume
A10: x1 in dom (f^mid(g,2,len g)) & x2 in dom (f^mid(g,2,len g))
    & (f^mid(g,2,len g)).x1=(f^mid(g,2,len g)).x2;
    then
A11: x1 in Seg len (f^mid(g,2,len g)) & x2 in Seg len (f^mid(g,2,len g ))
    by FINSEQ_1:def 3;
    reconsider n1=x1,n2=x2 as Element of NAT by A10;
A12: 1<=n1 & n1<=len (f^mid(g,2,len g))
    & 1<=n2 & n2<=len (f^mid(g,2,len g)) by A11,FINSEQ_1:3;
    then
A13: n1-len f <=len f + len mid(g,2,len g)-len f by A6,XREAL_1:11;
A14: n2-len f <=len f + len mid(g,2,len g)-len f by A6,A12,XREAL_1:11;
A15: rng (f^mid(g,2,len g)) c= the carrier of TOP-REAL 2 by FINSEQ_1:def 4;
    (f^mid(g,2,len g)).x1 in rng (f^mid(g,2,len g)) by A10,FUNCT_1:def 5;
    then reconsider q=(f^mid(g,2,len g)).x1 as Point of TOP-REAL 2 by A15;
A16: rng f c= L~f by A2,SPPOL_2:18;
A17: rng mid(g,2,len g) c= rng g by Th28;
A18: rng g c=L~g by A3,SPPOL_2:18;
A19: now
      assume
A20:  q in rng f & q in rng mid(g,2,len g);
      then q in rng g by A17;
      then
A21:  q in L~f /\ L~g by A16,A18,A20,XBOOLE_0:def 4;
      now
        assume
A22:    g.1 in rng mid(g,2,len g);
        2-'1=2-1 by XREAL_1:235;
        then
A23:    g.1 in rng (g/^1) by A3,A22,Th26;
        g|1=g|Seg 1 by FINSEQ_1:def 15;
        then
A24:    (g|1).1=g.1 by FINSEQ_1:5,FUNCT_1:72;
        len (g|1)=1 by A3,FINSEQ_1:80,XXREAL_0:2;
        then 1 in dom (g|1) by FINSEQ_3:27;
        then
A25:    g.1 in rng (g|1) by A24,FUNCT_1:def 5;
        rng(g|1) misses rng (g/^1) by A3,FINSEQ_5:37;
        hence contradiction by A23,A25,XBOOLE_0:3;
      end;
      hence contradiction by A1,A20,A21,TARSKI:def 1;
    end;
    now per cases;
      case n1<=len f;
        then
A26:    n1 in dom f by A12,FINSEQ_3:27;
        then
A27:    (f^mid(g,2,len g)).x1 =f.n1 by FINSEQ_1:def 7;
        now per cases;
          case n2<=len f;
            then
A28:        n2 in dom f by A12,FINSEQ_3:27;
            then (f^mid(g,2,len g)).x2 =f.n2 by FINSEQ_1:def 7;
            hence x1=x2 by A2,A10,A26,A27,A28,FUNCT_1:def 8;
          end;
          case
A29:        n2>len f;
            then len f +1<=n2 by NAT_1:13;
            then len f +1 -len f <=n2 - len f by XREAL_1:11;
            then 1<=(n2-'len f) & (n2-'len f)<=len mid(g,2,len g)
            by A14,NAT_D:39;
            then
A30:        (n2-'len f) in dom mid(g,2,len g) by FINSEQ_3:27;
            then
A31:        (f^mid(g,2,len g)).(len f +(n2-'len f))
            =mid(g,2,len g).(n2-'len f) by FINSEQ_1:def 7;
            len f + (n2-'len f)=len f+(n2-len f) by A29,XREAL_1:235
              .=n2;
            hence contradiction by A10,A19,A26,A27,A30,A31,FUNCT_1:def 5;
          end;
        end;
        hence x1=x2;
      end;
      case
A32:    n1>len f;
        then len f +1<=n1 by NAT_1:13;
        then
A33:    len f +1 -len f <=n1 - len f by XREAL_1:11;
        then
A34:    1<=n1-'len f by NAT_D:39;
A35:    n1-'len f<=len mid(g,2,len g) by A13,A33,NAT_D:39;
        then
A36:    n1-'len f in dom mid(g,2,len g) by A34,FINSEQ_3:27;
        then
A37:    (f^mid(g,2,len g)).(len f +(n1-'len f))
        =mid(g,2,len g).(n1-'len f) by FINSEQ_1:def 7;
        n1-'len f<=n1-'len f +2 by NAT_1:11;
        then
A38:    n1-'len f +2-'1=n1-'len f +2-1 by A34,XREAL_1:235,XXREAL_0:2
          .=n1-'len f +((1+1)-1);
A39:    1<=n1-'len f+1 by A34,NAT_D:48;
        n1-'len f+1<=len g -1+1 by A8,A35,XREAL_1:8;
        then
A40:    n1-'len f+1 in dom g by A39,FINSEQ_3:27;
        len f + (n1-'len f)=len f+(n1-len f) by A32,XREAL_1:235
          .=n1;
        then
A41:    (f^mid(g,2,len g)).n1 =g.(n1-'len f+1) by A3,A5,A13,A33,A37,A38,Th27;
A42:    len f + (n1-'len f)=len f+(n1-len f) by A32,XREAL_1:235
          .=n1;
        now per cases;
          case n2<=len f;
            then
A43:        n2 in dom f by A12,FINSEQ_3:27;
            then (f^mid(g,2,len g)).x2 =f.n2 by FINSEQ_1:def 7;
            hence contradiction by A10,A19,A36,A37,A42,A43,FUNCT_1:def 5;
          end;
          case
A44:        n2>len f;
            then len f +1<=n2 by NAT_1:13;
            then
A45:        len f +1 -len f <=n2 - len f by XREAL_1:11;
            then
A46:        1<=n2-'len f by NAT_D:39;
A47:        1<=(n2-'len f) & (n2-'len f)<=len mid(g,2,len g)
            by A14,A45,NAT_D:39;
            then (n2-'len f) in dom mid(g,2,len g) by FINSEQ_3:27;
            then
A48:        (f^mid(g,2,len g)).(len f +(n2-'len f))
            =mid(g,2,len g).(n2-'len f) by FINSEQ_1:def 7;
            n2-'len f<=n2-'len f +2 by NAT_1:11;
            then
A49:        n2-'len f +2-'1=n2-'len f +2-1 by A46,XREAL_1:235,XXREAL_0:2
              .=n2-'len f+1;
A50:        1<=n2-'len f+1 by A46,NAT_D:48;
            n2-'len f+1<=len g -1+1 by A8,A47,XREAL_1:8;
            then
A51:        n2-'len f+1 in dom g by A50,FINSEQ_3:27;
            len f + (n2-'len f)=len f+(n2-len f) by A44,XREAL_1:235
              .=n2;
            then (f^mid(g,2,len g)).n2
            =g.(n2-'len f+1) by A3,A5,A14,A45,A48,A49,Th27;
            then n1-'len f+1=n2-'len f+1 by A3,A10,A40,A41,A51,FUNCT_1:def 8;
            then n1-len f=n2-'len f by A32,XREAL_1:235;
            then n1-len f=n2-len f by A44,XREAL_1:235;
            hence x1=x2;
          end;
        end;
        hence x1=x2;
      end;
    end;
    hence x1=x2;
  end;
A52: for i be Nat st 1 <= i & i + 2 <= len (f^mid(g,2,len g))
  holds LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
  = {(f^mid(g,2,len g))/.(i+1)}
  proof
    let i be Nat;
    assume
A53: 1 <= i & i + 2 <= len (f^mid(g,2,len g));
    then
A54: 1<=i+1 by NAT_D:48;
A55: 1<=i+1+1 by A53,NAT_D:48;
A56: i+1<=len (f^mid(g,2,len g)) by A53,NAT_D:47;
A57: i<=len (f^mid(g,2,len g)) by A53,NAT_D:47;
    i+1+1<=len f + len mid(g,2,len g) by A53,FINSEQ_1:35;
    then i+1+1<=len f + (len g -'2+1) by A3,A5,Th27;
    then i+1+1<=len f + (len g -(1+1)+1) by A3,XREAL_1:235;
    then
A58: i+1+1-len f<=len f + (len g -(1+1)+1)-len f by XREAL_1:11;
    then
A59: i+1-len f+1+1<=len g -1+1 by XREAL_1:8;
    then
A60: i-len f+1+1+1<=len g;
    now per cases;
      case
A61:    i+2<=len f;
        then i+1+1<=len f;
        then
A62:    i+1<=len f by NAT_D:46;
        then
A63:    i<=len f by NAT_D:46;
A64:    (f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i by A53,A57,FINSEQ_4:24;
        f/.i=f.i by A53,A63,FINSEQ_4:24;
        then
A65:    (f^mid(g,2,len g))/.i=f/.i by A53,A63,A64,FINSEQ_1:85;
A66:    (f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
        by A54,A56,FINSEQ_4:24;
        f/.(i+1)=f.(i+1) by A54,A62,FINSEQ_4:24;
        then
A67:    (f^mid(g,2,len g))/.(i+1)=f/.(i+1) by A54,A62,A66,FINSEQ_1:85;
        LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
        = LSeg(f^mid(g,2,len g),i) by A53,A56,TOPREAL1:def 5;
        then
A68:    LSeg(f^mid(g,2,len g),i)=LSeg(f,i) by A53,A62,A65,A67,TOPREAL1:def 5;
A69:    (f^mid(g,2,len g))/.(i+1+1)=(f^mid(g,2,len g)).(i+1+1)
        by A53,A55,FINSEQ_4:24;
        f/.(i+1+1)=f.(i+1+1) by A55,A61,FINSEQ_4:24;
        then
A70:    LSeg((f^mid(g,2,len g))/.(i+1),(f^mid(g,2,len g))/.(i+1+1) )
        =LSeg(f/.(i+1),f/.(i+1+1)) by A55,A61,A67,A69,FINSEQ_1:85;
        LSeg((f^mid(g,2,len g))/.(i+1),(f^mid(g,2,len g))/.(i+1+1))
        = LSeg(f^mid(g,2,len g),i+1) by A53,A54,TOPREAL1:def 5;
        then LSeg(f^mid(g,2,len g),i+1)=LSeg(f,i+1)
        by A54,A61,A70,TOPREAL1:def 5;
        hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
        = {(f^mid(g,2,len g))/.(i+1)} by A2,A53,A61,A67,A68,TOPREAL1:def 8;
      end;
      case i+2>len f;
        then
A71:    i+2>=len f +1 by NAT_1:13;
        now per cases by A71,XXREAL_0:1;
          case
A72:        i+2>len f +1;
            then
A73:        i+1+1>len f+1;
            i+1+1>=len f+1+1 by A72,NAT_1:13;
            then i+1+1-(len f+1)>=len f+1+1-(len f+1) by XREAL_1:11;
            then
A74:        i-len f+1>=1;
A75:        i+1>=len f +1 by A73,NAT_1:13;
            then
A76:        i>=len f by XREAL_1:8;
A77:        i+1>=len f by A75,NAT_D:48;
A78:        i-'len f+1+1+1<=len g by A60,A76,XREAL_1:235;
            then
A79:        i-'len f+1+1<=len g by NAT_D:46;
            then
A80:        i-'len f+1<=len g by NAT_D:46;
            i-'len f+1+1-1<=len g -1 by A79,XREAL_1:11;
            then i-'len f+1<=len g -2+1;
            then
A81:        i-'len f+1<=len g -'2+1 by A3,XREAL_1:235;
            then
A82:        i-'len f<=len g -'2+1 by NAT_D:46;
A83:        i+1-'len f+1+1<=len g by A59,A77,XREAL_1:235;
            i-len f+1<=len g -'2+1 by A76,A81,XREAL_1:235;
            then i+1-len f<=len g -'2+1;
            then
A84:        i+1-'len f<=len g -'2+1 by A77,XREAL_1:235;
A85:        i-'len f+1>=1 by A74,A76,XREAL_1:235;
            then
A86:        i-'len f+1+1>=1 by NAT_D:48;
            then
A87:        i-len f+1+1>=1 by A76,XREAL_1:235;
            then i+1-len f+1>=1;
            then
A88:        i+1-'len f+1>=1 by A77,XREAL_1:235;
            then
A89:        i+1-'len f+1+1>=1 by NAT_D:48;
            i+1-len f+1>=0+1 by A87;
            then
A90:        i+1-len f>=0 by XREAL_1:8;
            then
A91:        i+1-'len f+1+1<=len g by A59,XREAL_0:def 2;
A92:        i+1+1-'len f<=len g-2+1 by A58,A87,XREAL_0:def 2;
            i+1-len f+1=i-len f+1+1;
            then
A93:        i+1-len f+1=i-'len f+1+1 by A76,XREAL_1:235;
            then
A94:        i+1-'len f+1=i-'len f+1+1 by A77,XREAL_1:235;
            i+1+1-len f+1=i+1-len f+1+1;
            then
A95:        i+1+1-len f+1=i+1-'len f+1+1 by A90,XREAL_0:def 2;
A96:        LSeg(f^mid(g,2,len g),i)
            =LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
            by A53,A56,TOPREAL1:def 5;
A97:        LSeg(g,i-'len f+1)=LSeg(g/.(i-'len f+1),g/.(i-'len f+1+1))
            by A79,A85,TOPREAL1:def 5;
A98:        g/.(i-'len f+1)=g.(i-'len f+1) by A80,A85,FINSEQ_4:24;
A99:        now
              assume
A100:         1<=i-'len f;
              then 1<=i-len f by NAT_D:39;
              then 1+len f<=i-len f +len f by XREAL_1:8;
              then
A101:         len f<i by NAT_1:13;
              then (f^mid(g,2,len g)).i=mid(g,2,len g).(i-len f) by A57,Th15;
              then (f^mid(g,2,len g)).i=mid(g,2,len g).(i-'len f)
              by A101,XREAL_1:235;
              then (f^mid(g,2,len g)).i=g.(i-'len f+2-1) by A3,A82,A100,Th31;
              hence (f^mid(g,2,len g)).i=g.(i-'len f+1);
            end;
            now
              assume 1>i-'len f;
              then i-'len f+1<=0+1 by NAT_1:13;
              then i-'len f<=0 by XREAL_1:8;
              then
A102:         i-'len f=0;
              then i-len f=0 by A76,XREAL_1:235;
              hence (f^mid(g,2,len g)).i
              = g.(i-'len f+1) by A1,A4,A102,FINSEQ_1:85;
            end;
            then
A103:       (f^mid(g,2,len g))/.i=g/.(i-'len f+1) by A53,A57,A98,A99,
FINSEQ_4:24;
A104:       (f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
            by A54,A56,FINSEQ_4:24;
A105:       g/.(i-'len f+1+1)=g.(i-'len f+1+1) by A79,A86,FINSEQ_4:24;
A106:       now
              assume
A107:         1<=(i+1)-'len f;
              then 1<=(i+1)-len f by NAT_D:39;
              then 1+len f<=(i+1)-len f +len f by XREAL_1:8;
              then
A108:         len f<(i+1) by NAT_1:13;
              then (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).((i+1)-len f)
              by A56,Th15;
              then (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).((i+1)-'len f)
              by A108,XREAL_1:235;
              then (f^mid(g,2,len g)).(i+1)=g.((i+1)-'len f+2-1)
              by A3,A84,A107,Th31;
              hence (f^mid(g,2,len g)).(i+1)=g.((i+1)-'len f+1);
            end;
A109:       now
              assume 1>(i+1)-'len f;
              then (i+1)-'len f+1<=0+1 by NAT_1:13;
              then (i+1)-'len f<=0 by XREAL_1:8;
              then
A110:         (i+1)-'len f=0;
              then (i+1)-len f=0 by A77,XREAL_1:235;
              hence (f^mid(g,2,len g)).(i+1)
              = g.((i+1)-'len f+1) by A1,A4,A110,FINSEQ_1:85;
            end;
A111:       LSeg(f^mid(g,2,len g),i+1)
            =LSeg((f^mid(g,2,len g))/.(i+1),(f^mid(g,2,len g))/.(i+1+1))
            by A53,A54,TOPREAL1:def 5;
A112:       LSeg(g,i+1-'len f+1) =LSeg(g/.(i+1-'len f+1),g/.(i+1-'len f+1+1))
            by A83,A88,TOPREAL1:def 5;
A113:       (f^mid(g,2,len g))/.(i+1+1)=(f^mid(g,2,len g)).(i+1+1)
            by A53,A55,FINSEQ_4:24;
A114:       g/.(i+1-'len f+1+1)=g.(i+1-'len f+1+1) by A89,A91,FINSEQ_4:24;
A115:       now
              assume
A116:         1<=(i+1+1)-'len f;
              then 1<=(i+1+1)-len f by NAT_D:39;
              then 1+len f<=(i+1+1)-len f +len f by XREAL_1:8;
              then
A117:         len f<(i+1+1) by NAT_1:13;
              then (f^mid(g,2,len g)).(i+1+1)=mid(g,2,len g).((i+1+1)-len f)
              by A53,Th15;
              then (f^mid(g,2,len g)).(i+1+1)=mid(g,2,len g).((i+1+1)-'len f)
              by A117,XREAL_1:235;
              then (f^mid(g,2,len g)).(i+1+1)=g.((i+1+1)-'len f+2-1)
              by A3,A92,A116,Th31;
              hence (f^mid(g,2,len g)).(i+1+1)=g.((i+1+1)-'len f+1);
            end;
            now
              assume 1>(i+1+1)-'len f;
              then (i+1+1)-'len f+1<=0+1 by NAT_1:13;
              then
A118:         (i+1+1)-'len f<=0 by XREAL_1:8;
              then
A119:         (i+1+1)-'len f=0;
              (i+1+1)-len f=0 by A87,A118,XREAL_0:def 2;
              hence (f^mid(g,2,len g)).(i+1+1)
              = g.((i+1+1)-'len f+1) by A1,A4,A119,FINSEQ_1:85;
            end;
            then
A120:       LSeg(f^mid(g,2,len g),i+1)=LSeg(g,i+1-'len f+1)
            by A93,A95,A104,A105,A106,A109,A111,A112,A113,A114,A115,
XREAL_0:def 2;
            i-'len f+1+(1+1)<=len g by A78;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
            = {(f^mid(g,2,len g))/.(i+1)} by A3,A85,A94,A96,A97,A103,A104,A105
,A106,A120,TOPREAL1:def 8;
          end;
          case
A121:       i+2=len f +1;
            then i+1+1=len f+1;
            then
A122:       i<=len f by NAT_D:46;
            i=len f -1 by A121;
            then
A123:       i=len f-'1 by A2,XREAL_1:235,XXREAL_0:2;
A124:       (f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i by A53,A57,FINSEQ_4:24;
            f/.i=f.i by A53,A122,FINSEQ_4:24;
            then
A125:       (f^mid(g,2,len g))/.i=f/.i by A53,A122,A124,FINSEQ_1:85;
A126:       (f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
            by A54,A56,FINSEQ_4:24;
A127:       f/.(i+1)=f.(i+1) by A54,A121,FINSEQ_4:24;
            then
A128:       f/.(i+1)=g/.1 by A1,A5,A121,FINSEQ_4:24;
A129:       (f^mid(g,2,len g))/.(i+1)=f/.(i+1)
            by A54,A121,A126,A127,FINSEQ_1:85;
A130:       LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
            = LSeg(f^mid(g,2,len g),i) by A53,A56,TOPREAL1:def 5;
A131:       LSeg(f,i)=LSeg(f/.i,f/.(i+1)) by A53,A121,TOPREAL1:def 5;
A132:       LSeg(f^mid(g,2,len g),i+1)
            =LSeg((f^mid(g,2,len g))/.(i+1),(f^mid(g,2,len g))/.(i+1+1))
            by A53,A54,TOPREAL1:def 5;
A133:       i+1-'len f+1 =0+1 by A121,XREAL_1:234
              .=1;
            then
A134:       LSeg(g,i+1-'len f+1) =LSeg(g/.(i+1-'len f+1),g/.(i +1-'len f+1+1))
            by A3,TOPREAL1:def 5;
A135:       (f^mid(g,2,len g))/.(i+1+1)=(f^mid(g,2,len g)).(i+1+1)
            by A53,A55,FINSEQ_4:24;
A136:       g/.(i+1-'len f+1+1)=g.(i+1-'len f+1+1) by A3,A133,FINSEQ_4:24;
            len g -2>=0 by A3,XREAL_1:50;
            then
A137:       0+1<=len g-2+1 by XREAL_1:8;
            len f<(i+1+1) by A121,NAT_1:13;
            then (f^mid(g,2,len g)).(i+1+1)=mid(g,2,len g).((i+1+1)-len f)
            by A53,Th15;
            then
A138:       (f^mid(g,2,len g)).(i+1+1)=g.(2+1-'1) by A3,A121,A137,Th31
              .=g.2 by NAT_D:34;
A139:       LSeg(g,1)=LSeg(g/.1,g/.(1+1)) by A3,TOPREAL1:def 5;
A140:       g/.1=g.1 by A5,FINSEQ_4:24;
            then g/.1= f/.len f by A1,A4,FINSEQ_4:24;
            then
A141:       g/.1 in LSeg(f/.(len f-'1),f/.len f) by TOPREAL1:6;
            g/.1 in LSeg(g/.1,g/.(1+1)) by TOPREAL1:6;
            then g/.1 in LSeg(f,i) /\
            LSeg(g,1) by A121,A123,A131,A139,A141,XBOOLE_0:def 4;
            then
A142:       {g/.1} c= LSeg(f,i) /\ LSeg(g,1) by ZFMISC_1:37;
A143:       LSeg(f,i)c=L~f by TOPREAL3:26;
            LSeg(g,1)c=L~g by TOPREAL3:26;
            then LSeg(f,i) /\ LSeg(g,1) c= {g/.1} by A1,A140,A143,XBOOLE_1:27;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
            = {(f^mid(g,2,len g))/.(i+1)}
            by A125,A128,A129,A130,A131,A132,A133,A134,A135,A136,A138,A142,
XBOOLE_0:def 10;
          end;
        end;
        hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
        = {(f^mid(g,2,len g))/.(i+1)};
      end;
    end;
    hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),i+1)
    = {(f^mid(g,2,len g))/.(i+1)};
  end;
A144: for i,j be Nat st i+1 < j holds
  LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
  proof
    let i,j be Nat;
    assume
A145: i+1 < j;
    now per cases;
      case
A146:   j<len f & j+1<=len (f^mid(g,2,len g));
        then
A147:   j<=len (f^mid(g,2,len g)) by NAT_D:46;
        then
A148:   i+1<len (f^mid(g,2,len g)) by A145,XXREAL_0:2;
        then
A149:   i<=len (f^mid(g,2,len g)) by NAT_D:46;
A150:   i+1<len f by A145,A146,XXREAL_0:2;
        then
A151:   i<len f by NAT_1:13;
        now per cases;
          case
A152:       1<=i;
            then
A153:       1<=i+1 by NAT_D:48;
            then
A154:       1<j by A145,XXREAL_0:2;
            then
A155:       1<=j+1 by NAT_D:48;
A156:       (f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i
            by A149,A152,FINSEQ_4:24;
            f/.i=f.i by A151,A152,FINSEQ_4:24;
            then
A157:       (f^mid(g,2,len g))/.i=f/.i by A151,A152,A156,FINSEQ_1:85;
A158:       (f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
            by A148,A153,FINSEQ_4:24;
            f/.(i+1)=f.(i+1) by A150,A153,FINSEQ_4:24;
            then
A159:       LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+ 1))
            =LSeg(f/.i,f/.(i+1)) by A150,A153,A157,A158,FINSEQ_1:85;
            LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
            = LSeg(f^mid(g,2,len g),i) by A148,A152,TOPREAL1:def 5;
            then
A160:       LSeg(f^mid(g,2,len g),i)=LSeg(f,i)
            by A150,A152,A159,TOPREAL1:def 5;
A161:       j+1<=len f by A146,NAT_1:13;
A162:       (f^mid(g,2,len g))/.j=(f^mid(g,2,len g)).j
            by A147,A154,FINSEQ_4:24;
            f/.j=f.j by A146,A154,FINSEQ_4:24;
            then
A163:       (f^mid(g,2,len g))/.j=f/.j by A146,A154,A162,FINSEQ_1:85;
A164:       (f^mid(g,2,len g))/.(j+1)=(f^mid(g,2,len g)).(j+1)
            by A146,A155,FINSEQ_4:24;
            f/.(j+1)=f.(j+1) by A155,A161,FINSEQ_4:24;
            then
A165:       LSeg((f^mid(g,2,len g))/.j,(f^mid(g,2,len g))/.(j+ 1))
            =LSeg(f/.j,f/.(j+1)) by A155,A161,A163,A164,FINSEQ_1:85;
A166:       LSeg((f^mid(g,2,len g))/.j,(f^mid(g,2,len g))/.(j+1))
            = LSeg(f^mid(g,2,len g),j) by A146,A154,TOPREAL1:def 5;
            LSeg(f,j)=LSeg(f/.j,f/.(j+1)) by A154,A161,TOPREAL1:def 5;
            then LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
            by A2,A145,A160,A165,A166,TOPREAL1:def 9;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
            by XBOOLE_0:def 7;
          end;
          case i<1;
            then LSeg(f^mid(g,2,len g),i)={} by TOPREAL1:def 5;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
          end;
        end;
        hence LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
        by XBOOLE_0:def 7;
      end;
      case
A167:   i+1<=len f & len f<=j & j+1<=len (f^mid(g,2,len g));
        now per cases by A145,A167,XXREAL_0:1;
          case
A168:       i+1<len f & len f<=j;
            len f<=len f+len mid(g,2,len g) by NAT_1:11;
            then
A169:       i+1<len (f^mid(g,2,len g)) by A6,A168,XXREAL_0:2;
A170:       1+1<=j by A2,A168,XXREAL_0:2;
            then
A171:       1<=j by NAT_D:46;
A172:       len f<=j+1 by A168,NAT_D:48;
            now per cases;
              case
A173:           1<=i;
                then
A174:           1<=i+1 by NAT_D:48;
A175:           i+1+1<=len f by A168,NAT_1:13;
A176:           now
                  1<=i & i<=len (f^mid(g,2,len g)) by A169,A173,NAT_D:46;
                  then
A177:             (f^mid(g,2,len g))/.i=(f^mid(g,2,len g )).i by FINSEQ_4:24;
                  1<=i & i<=len f by A167,A173,NAT_D:46;
                  then
A178:             f/.i=f.i by FINSEQ_4:24;
                  1<=i & i<=len f by A167,A173,NAT_D:46;
                  then
A179:             (f^mid(g,2,len g))/.i=f/.i by A177,A178,FINSEQ_1:85;
A180:             (f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                  by A169,A174,FINSEQ_4:24;
                  f/.(i+1)=f.(i+1) by A167,A174,FINSEQ_4:24;
                  then
A181:             LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2, len g))/.(i+1))
                  =LSeg(f/.i,f/.(i+1)) by A167,A174,A179,A180,FINSEQ_1:85;
                  LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
                  = LSeg(f^mid(g,2,len g),i) by A169,A173,TOPREAL1:def 5;
                  hence LSeg(f^mid(g,2,len g),i)=LSeg(f,i)
                  by A167,A173,A181,TOPREAL1:def 5;
                end;
A182:           1<=1+(j-'len f) by NAT_1:11;
                j+1+1<=len f+(len g-1)+1 by A6,A8,A167,XREAL_1:8;
                then j+1+1-len f<=len f +len g -len f by XREAL_1:11;
                then j-len f+1+1<=len g;
                then
A183:           j-'len f+1+1<=len g by A168,XREAL_1:235;
                then j-'len f+1+1-1<=len g-1 by XREAL_1:11;
                then
A184:           j-'len f+1<=len g-2+1;
                then j-'len f+1<=len g-'2+1 by A3,XREAL_1:235;
                then
A185:           j-'len f<=len g-'2+1 by NAT_D:46;
A186:           j-'len f+1=j-len f+1 by A167,XREAL_1:235
                  .=j+1-len f
                  .=j+1-'len f by A172,XREAL_1:235;
A187:           LSeg(g,j-'len f+1)=LSeg(g/.(j-'len f+1),g/.(j-'len f+1+1))
                by A182,A183,TOPREAL1:def 5;
A188:           1<=j & j<=len (f^mid(g,2,len g)) by A167,A170,NAT_D:46;
                1<=j-'len f+1 & j-'len f+1<=len g
                by A183,NAT_1:11,NAT_D:46;
                then
A189:           g/.(j-'len f+1)=g.(j-'len f+1) by FINSEQ_4:24;
A190:           now
                  assume
A191:             1<=j-'len f;
                  then 1<=j-len f by NAT_D:39;
                  then 1+len f<=j-len f +len f by XREAL_1:8;
                  then
A192:             len f<j by NAT_1:13;
                  then (f^mid(g,2,len g)).j=mid(g,2,len g).(j-len f)
                  by A188,Th15;
                  then (f^mid(g,2,len g)).j=mid(g,2,len g).(j-'len f)
                  by A192,XREAL_1:235;
                  then (f^mid(g,2,len g)).j=g.(j-'len f+2-1)
                  by A3,A185,A191,Th31;
                  hence (f^mid(g,2,len g)).j=g.(j-'len f+1);
                end;
                now
                  assume 1>j-'len f;
                  then j-'len f+1<=0+1 by NAT_1:13;
                  then j-'len f<=0 by XREAL_1:8;
                  then
A193:             j-'len f=0;
                  then j-len f=0 by A167,XREAL_1:235;
                  hence (f^mid(g,2,len g)).j
                  = g.(j-'len f+1) by A1,A4,A193,FINSEQ_1:85;
                end;
                then
A194:           (f^mid(g,2,len g))/.j=g/.(j-'len f+1) by A188,A189,A190,
FINSEQ_4:24;
                1<=j+1 & j+1<=len (f^mid(g,2,len g)) by A167,A171,NAT_D:48;
                then
A195:           (f^mid(g,2,len g))/.(j+1)=(f^mid(g,2,len g )).(j+1)
                by FINSEQ_4:24;
A196:           g/.(j-'len f+1+1)=g.(j-'len f+1+1) by A183,FINSEQ_4:24,NAT_1:11
                ;
A197:           now
                  assume
A198:             1<=(j+1)-'len f;
                  then 1<=(j+1)-len f by NAT_D:39;
                  then 1+len f<=(j+1)-len f +len f by XREAL_1:8;
                  then
A199:             len f<(j+1) by NAT_1:13;
                  then (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).((j+1)-len f)
                  by A167,Th15;
                  then (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).((j+1)-'len f)
                  by A199,XREAL_1:235;
                  then (f^mid(g,2,len g)).(j+1)=g.((j+1)-'len f+2-1)
                  by A3,A184,A186,A198,Th31;
                  hence (f^mid(g,2,len g)).(j+1)=g.((j+1)-'len f+1);
                end;
                now
                  assume 1>(j+1)-'len f;
                  then (j+1)-'len f+1<=0+1 by NAT_1:13;
                  then (j+1)-'len f<=0 by XREAL_1:8;
                  then
A200:             (j+1)-'len f=0;
                  then (j+1)-len f=0 by A172,XREAL_1:235;
                  hence (f^mid(g,2,len g)).(j+1)
                  = g.((j+1)-'len f+1) by A1,A4,A200,FINSEQ_1:85;
                end;
                then
A201:           LSeg(f^mid(g,2,len g),j)=LSeg(g,j-'len f+1 )
                by A167,A171,A186,A187,A194,A195,A196,A197,TOPREAL1:def 5;
A202:           LSeg(f^mid(g,2,len g),i) c= L~f by A176,TOPREAL3:26;
                LSeg(f^mid(g,2,len g),j) c=L~g by A201,TOPREAL3:26;
                then
A203:           LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2 ,len g),j) c={g. 1}
                by A1,A202,XBOOLE_1:27;
                now per cases;
                  case
A204:               i+1<len f-'1;
A205:               1+1-1<=len f -1 by A2,XREAL_1:11;
A206:               1<=len f by A2,XXREAL_0:2;
A207:               len f-'1+1=len f-1+1 by A2,XREAL_1:235,XXREAL_0:2
                      .=len f;
                    now
                      given x being set such that
A208:                 x
                      in LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j);
A209:                 x=g.1 by A203,A208,TARSKI:def 1;
A210:                 x in LSeg(f,i) by A176,A208,XBOOLE_0:def 4;
                      f/.len f in LSeg(f,len f-'1) by A205,A207,TOPREAL1:27;
                      then g.1 in LSeg(f,len f-'1) by A1,A206,FINSEQ_4:24;
                      then x in LSeg(f,i)/\ LSeg(f,len f-'1) by A209,A210,
XBOOLE_0:def 4;
                      then LSeg(f,i) meets LSeg(f,len f-'1) by XBOOLE_0:4;
                      hence contradiction by A2,A204,TOPREAL1:def 9;
                    end;

hence LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j) by XBOOLE_0:4;
                  end;
                  case i+1>=len f-'1;
                    then i+1>=len f-1 by A2,XREAL_1:235,XXREAL_0:2;
                    then
A211:               i+1+1>=len f-1+1 by XREAL_1:8;
                    then
A212:               i+1+1=len f by A175,XXREAL_0:1;
                    then i+1=len f-1;
                    then
A213:               i+1=len f-1 & i+1=len f-'1 by A2,XREAL_1:235,XXREAL_0:2;
A214:               i+1+1=len f & i+(1+1)=len f by A175,A211,XXREAL_0:1;
A215:               i+1+1=len f & i+(1+1)=len f
                    & 1<=i+1 & i+1<=len f by A212,NAT_1:11;
A216:               len f-'1+1=len f-1+1 by A2,XREAL_1:235,XXREAL_0:2
                      .=len f;
                    then 1+1<=len f-'1+1 by A1,TOPREAL1:def 10;
                    then
A217:               1<=len f-'1 & len f-'1+1<=len f by A216,XREAL_1:8;
                    now
                      given x being set such that
A218:                 x
                      in LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j);
A219:                 x=g.1 by A203,A218,TARSKI:def 1;
A220:                 x in LSeg(f,i) by A176,A218,XBOOLE_0:def 4;
                      f/.len f in LSeg(f,len f-'1) by A216,A217,TOPREAL1:27;
                      then g.1 in LSeg(f,len f-'1) by A1,A4,FINSEQ_4:24;
                      then
A221:                 x in LSeg(f,i)/\ LSeg(f,len f -'1) by A219,A220,
XBOOLE_0:def 4;
                      LSeg(f,i)/\ LSeg(f,len f-'1)={f/.(i+1)}
                      by A2,A173,A213,A214,TOPREAL1:def 8;
                      then f.len f=f/.(i+1) by A1,A219,A221,TARSKI:def 1;
                      then
A222:                 f.len f=f.(len f-'1) by A213,A215,FINSEQ_4:24;
A223:                 len f in dom f by A4,FINSEQ_3:27;
A224:                 len f-'1<=len f by NAT_D:35;
                      1+1-1<=len f-1 by A2,XREAL_1:11;
                      then 1<=len f-'1 by NAT_D:39;
                      then len f-'1 in dom f by A224,FINSEQ_3:27;
                      then len f=len f-'1 by A2,A222,A223,FUNCT_1:def 8;
                      then len f=len f-1 by A2,XREAL_1:235,XXREAL_0:2;
                      hence contradiction;
                    end;

hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
                    by XBOOLE_0:def 1;
                  end;
                end;
                hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
                by XBOOLE_0:def 7;
              end;
              case i<1;
                then LSeg(f^mid(g,2,len g),i)={} by TOPREAL1:def 5;

hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
              end;
            end;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
          end;
          case
A225:       i+1<=len f & len f<j;
            len f<len f+len mid(g,2,len g)
            proof
              1+1-1<=len g-1 by A3,XREAL_1:11;
              then len f+1<=len f+len mid(g,2,len g) by A8,XREAL_1:9;
              hence thesis by NAT_1:13;
            end;
            then
A226:       i+1<len (f^mid(g,2,len g)) by A6,A225,XXREAL_0:2;
A227:       1+1<=j by A2,A225,XXREAL_0:2;
            then
A228:       1<=j by NAT_D:46;
A229:       len f<=j+1 by A225,NAT_D:48;
            now per cases;
              case
A230:           1<=i;
                then
A231:           1<=i+1 by NAT_D:48;
A232:           now
                  1<=i & i<=len (f^mid(g,2,len g)) by A226,A230,NAT_D:46;
                  then
A233:             (f^mid(g,2,len g))/.i=(f^mid(g,2,len g )).i by FINSEQ_4:24;
                  1<=i & i<=len f by A167,A230,NAT_D:46;
                  then
A234:             f/.i=f.i by FINSEQ_4:24;
                  1<=i & i<=len f by A167,A230,NAT_D:46;
                  then
A235:             (f^mid(g,2,len g))/.i=f/.i by A233,A234,FINSEQ_1:85;
A236:             (f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1)
                  by A226,A231,FINSEQ_4:24;
                  f/.(i+1)=f.(i+1) by A167,A231,FINSEQ_4:24;
                  then
A237:             LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2, len g))/.(i+1))
                  =LSeg(f/.i,f/.(i+1)) by A167,A231,A235,A236,FINSEQ_1:85;
                  LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
                  = LSeg(f^mid(g,2,len g),i) by A226,A230,TOPREAL1:def 5;
                  hence LSeg(f^mid(g,2,len g),i)=LSeg(f,i)
                  by A167,A230,A237,TOPREAL1:def 5;
                end;
A238:           1<=1+(j-'len f) by NAT_1:11;
                j+1+1<=len f+(len g-1)+1 by A6,A8,A167,XREAL_1:8;
                then j+1+1-len f<=len f +len g -len f by XREAL_1:11;
                then j-len f+1+1<=len g;
                then
A239:           j-'len f+1+1<=len g by A225,XREAL_1:235;
                then j-'len f+1+1-1<=len g-1 by XREAL_1:11;
                then
A240:           j-'len f+1<=len g-2+1;
                then j-'len f+1<=len g-'2+1 by A3,XREAL_1:235;
                then
A241:           j-'len f<=len g-'2+1 by NAT_D:46;
A242:           j-'len f+1=j-len f+1 by A167,XREAL_1:235
                  .=j+1-len f
                  .=j+1-'len f by A229,XREAL_1:235;
A243:           LSeg(g,j-'len f+1)=LSeg(g/.(j-'len f+1),g/.(j-'len f+1+1))
                by A238,A239,TOPREAL1:def 5;
A244:           1<=j & j<=len (f^mid(g,2,len g)) by A167,A227,NAT_D:46;
                1<=j-'len f+1 & j-'len f+1<=len g by A239,NAT_1:11,NAT_D:46;
                then
A245:           g/.(j-'len f+1)=g.(j-'len f+1) by FINSEQ_4:24;
A246:           now
                  assume
A247:             1<=j-'len f;
                  then 1<=j-len f by NAT_D:39;
                  then 1+len f<=j-len f +len f by XREAL_1:8;
                  then
A248:             len f<j by NAT_1:13;
                  then (f^mid(g,2,len g)).j=mid(g,2,len g).(j-len f)
                  by A244,Th15;
                  then (f^mid(g,2,len g)).j=mid(g,2,len g).(j-'len f)
                  by A248,XREAL_1:235;
                  then (f^mid(g,2,len g)).j=g.(j-'len f+2-1)
                  by A3,A241,A247,Th31;
                  hence (f^mid(g,2,len g)).j=g.(j-'len f+1);
                end;
                now
                  assume 1>j-'len f;
                  then j-'len f+1<=0+1 by NAT_1:13;
                  then j-'len f<=0 by XREAL_1:8;
                  then
A249:             j-'len f=0;
                  then j-len f=0 by A167,XREAL_1:235;
                  hence (f^mid(g,2,len g)).j
                  = g.(j-'len f+1) by A1,A4,A249,FINSEQ_1:85;
                end;
                then
A250:           (f^mid(g,2,len g))/.j=g/.(j-'len f+1) by A244,A245,A246,
FINSEQ_4:24;
                1<=j+1 & j+1<=len (f^mid(g,2,len g)) by A167,A228,NAT_D:48;
                then
A251:           (f^mid(g,2,len g))/.(j+1)=(f^mid(g,2,len g )).(j+1)
                by FINSEQ_4:24;
A252:           g/.(j-'len f+1+1)=g.(j-'len f+1+1) by A239,FINSEQ_4:24,NAT_1:11
                ;
A253:           now
                  assume
A254:             1<=(j+1)-'len f;
                  then 1<=(j+1)-len f by NAT_D:39;
                  then 1+len f<=(j+1)-len f +len f by XREAL_1:8;
                  then
A255:             len f<(j+1) by NAT_1:13;
                  then (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).((j+1)-len f)
                  by A167,Th15;
                  then (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).((j+1)-'len f)
                  by A255,XREAL_1:235;
                  then (f^mid(g,2,len g)).(j+1)=g.((j+1)-'len f+2-1)
                  by A3,A240,A242,A254,Th31;
                  hence (f^mid(g,2,len g)).(j+1)=g.((j+1)-'len f+1);
                end;
                now
                  assume 1>(j+1)-'len f;
                  then (j+1)-'len f+1<=0+1 by NAT_1:13;
                  then (j+1)-'len f<=0 by XREAL_1:8;
                  then
A256:             (j+1)-'len f=0;
                  then (j+1)-len f=0 by A229,XREAL_1:235;
                  hence (f^mid(g,2,len g)).(j+1)
                  = g.((j+1)-'len f+1) by A1,A4,A256,FINSEQ_1:85;
                end;
                then
A257:           LSeg(f^mid(g,2,len g),j)=LSeg(g,j-'len f+1 )
                by A167,A228,A242,A243,A250,A251,A252,A253,TOPREAL1:def 5;
A258:           LSeg(f^mid(g,2,len g),i) c= L~f by A232,TOPREAL3:26;
                LSeg(f^mid(g,2,len g),j) c=L~g by A257,TOPREAL3:26;
                then
A259:           LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2 ,len g),j) c={g. 1}
                by A1,A258,XBOOLE_1:27;
                now
                  given x being set such that
A260:             x in LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j);
A261:             x=g.1 by A259,A260,TARSKI:def 1;
                  then
A262:             g/.1=x by A5,FINSEQ_4:24;
A263:             LSeg(g,j-'len f+1)<>{} & x in LSeg(g,j-'len f+1)
                  & 1+1<=len g by A1,A257,A260,TOPREAL1:def 10,XBOOLE_0:def 4;
                  then x in LSeg(g,1) & x in LSeg(g,j-'len f+1)
                  by A262,TOPREAL1:27;
                  then
A264:             x in LSeg(g,1)/\ LSeg(g,j-'len f+1) by XBOOLE_0:def 4;
                  then LSeg(g,1) meets LSeg(g,j-'len f+1) by XBOOLE_0:4;
                  then 1+1 >= j-'len f+1 by A3,TOPREAL1:def 9;
                  then 1>=j-'len f by XREAL_1:8;
                  then 1>=j-len f by NAT_D:39;
                  then 1+len f>=j-len f+len f by XREAL_1:8;
                  then len f+1>=j & j>=len f+1 by A225,NAT_1:13;
                  then
A265:             j=len f +1 & j-'len f+1=j-len f+1 by A225,XREAL_1:235
,XXREAL_0:1;
                  then 1+2<=len g by A263,TOPREAL1:def 5;
                  then LSeg(g,1)/\ LSeg(g,j-'len f+1)={g/.(1+1)} by A3,A265,
TOPREAL1:def 8;
                  then
A266:             x=g/.(1+1) by A264,TARSKI:def 1
                    .=g.(1+1) by A3,FINSEQ_4:24;
A267:             1 in dom g by A5,FINSEQ_3:27;
                  1+1 in dom g by A3,FINSEQ_3:27;
                  hence contradiction by A3,A261,A266,A267,FUNCT_1:def 8;
                end;
                hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
                by XBOOLE_0:def 1;
              end;
              case i<1;
                then LSeg(f^mid(g,2,len g),i)={} by TOPREAL1:def 5;

hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
              end;
            end;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
          end;
        end;
        hence LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
        by XBOOLE_0:def 7;
      end;
      case
A268:   len f<i+1 & j+1<=len (f^mid(g,2,len g));
        then
A269:   len f<=i by NAT_1:13;
        then
A270:   1+1<=i by A2,XXREAL_0:2;
        then
A271:   1<=i by NAT_D:46;
        then
A272:   1<=i+1 by NAT_D:48;
        then
A273:   1<=j by A145,XXREAL_0:2;
A274:   len f<j by A145,A268,XXREAL_0:2;
        j<=j+1 by NAT_1:11;
        then
A275:   len f<j+1 by A274,XXREAL_0:2;
A276:   i-'len f=i-len f by A269,XREAL_1:235;
A277:   j-'len f=j-len f by A145,A268,XREAL_1:235,XXREAL_0:2;
        i+1-len f<j-len f by A145,XREAL_1:11;
        then
A278:   i-'len f +1+1<j-'len f +1 by A276,A277,XREAL_1:8;
A279:   1<=(i-'len f)+1 by NAT_1:11;
A280:   1<=(j-'len f)+1 by NAT_1:11;
        now per cases;
          case
A281:       j+1<=len (f^mid(g,2,len g));
            then
A282:       j<=len (f^mid(g,2,len g)) by NAT_D:46;
A283:       now
A284:         i+1<len (f^mid(g,2,len g)) by A145,A282,XXREAL_0:2;
              then i+1<len f+(len g-1) by A8,FINSEQ_1:35;
              then
A285:         i+1-len f<len f+(len g-1)-len f by XREAL_1:11;
              then
A286:         i-len f+1+1<len g-1+1 by XREAL_1:8;
              then
A287:         (i-'len f+1)<=len g by A276,NAT_D:46;
A288:         (i-'len f+1)<=len g-2+1 by A276,A285;
A289:         1<=i & i<=len (f^mid(g,2,len g)) by A270,A284,NAT_D:46;
A290:         g/.(i-'len f+1)=g.(i-'len f+1) by A287,FINSEQ_4:24,NAT_1:11;
A291:         now per cases;
                case i<=len f;
                  then
A292:             i=len f by A269,XXREAL_0:1;
                  then (f^mid(g,2,len g)).i =g.(0+1) by A1,A271,FINSEQ_1:85
                    .=g.(i-'len f+1) by A292,XREAL_1:234;
                  hence (f^mid(g,2,len g))/.i=g/.(i-'len f+1) by A289,A290,
FINSEQ_4:24;
                end;
                case
A293:             i>len f;
                  then len f+1<=i by NAT_1:13;
                  then
A294:             len f+1-len f<=i-len f by XREAL_1:11;
                  i-'len f+1-1<=len g-1 by A287,XREAL_1:11;
                  then
A295:             i-'len f<=len g -2+1;
                  (f^mid(g,2,len g)).i
                  =mid(g,2,len g).(i-'len f) by A276,A289,A293,Th15
                    .=g.(i-'len f+2-1) by A3,A276,A294,A295,Th31
                    .=g.(i-'len f +1);
                  hence (f^mid(g,2,len g))/.i=g/.(i-'len f+1) by A289,A290,
FINSEQ_4:24;
                end;
              end;

1<=i+1 & i+1<=len (f^mid(g,2,len g)) by A145,A282,A289,NAT_D:48,XXREAL_0:2;
              then
A296:         (f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).( i+1)
              by FINSEQ_4:24;
              1<=(i-'len f+1)+1 & (i-'len f+1)+1<=len g by A269,A286,NAT_1:11
,XREAL_1:235;
              then
A297:         g/.((i-'len f+1)+1)=g.((i-'len f+1)+1) by FINSEQ_4:24;
A298:         i-'len f+1+2-1 =i-'len f+1+1;
A299:         (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).(i+1-len f)
              by A268,A284,Th15
                .=g.(i-'len f +1+1) by A3,A276,A279,A288,A298,Th31;
              LSeg((f^mid(g,2,len g))/.i,(f^mid(g,2,len g))/.(i+1))
              = LSeg(f^mid(g,2,len g),i) by A271,A284,TOPREAL1:def 5;
              hence LSeg(f^mid(g,2,len g),i)=LSeg(g,(i-'len f+1))
              by A276,A279,A286,A291,A296,A297,A299,TOPREAL1:def 5;
            end;
            j+1<=len f+(len g-1) by A8,A281,FINSEQ_1:35;
            then
A300:       j+1-len f<=len f+(len g-1)-len f by XREAL_1:11;
            then
A301:       j-len f+1+1<=len g-1+1 by XREAL_1:8;
            then
A302:       (j-'len f+1)<=len g by A277,NAT_D:46;
A303:       (j-'len f+1)<=len g-2+1 by A277,A300;
A304:       1

<=j & j<=len (f^mid(g,2,len g)) by A145,A272,A281,NAT_D:46,XXREAL_0:2;
            then
A305:       (f^mid(g,2,len g))/.j=(f^mid(g,2,len g)).j by FINSEQ_4:24;
A306:       g/.(j-'len f+1)=g.(j-'len f+1) by A302,FINSEQ_4:24,NAT_1:11;
            len f+1<=j by A274,NAT_1:13;
            then
A307:       len f+1-len f<=j-len f by XREAL_1:11;
            j-'len f+1-1<=len g-1 by A302,XREAL_1:11;
            then
A308:       j-'len f<=len g -2+1;
A309:       (f^mid(g,2,len g)).j=mid(g,2,len g).(j-len f) by A274,A304,Th15
              .=g.(j-'len f+2-1) by A3,A277,A307,A308,Th31
              .=g.(j-'len f +1);
            1<=j+1 & j+1<=len (f^mid(g,2,len g)) by A281,A304,NAT_D:48;
            then
A310:       (f^mid(g,2,len g))/.(j+1)=(f^mid(g,2,len g)).(j+1) by FINSEQ_4:24;
            1<=(j-'len f+1)+1 & (j-'len f+1)+1<=len g by A274,A301,NAT_1:11
,XREAL_1:235;
            then
A311:       g/.((j-'len f+1)+1)=g.((j-'len f+1)+1) by FINSEQ_4:24;
A312:       j-'len f+1+2-1=j-'len f+1+1;
A313:       (f^mid(g,2,len g)).(j+1)=mid(g,2,len g).(j+1-len f)
            by A275,A281,Th15
              .=g.(j-'len f +1+1) by A3,A277,A280,A303,A312,Th31;
            LSeg((f^mid(g,2,len g))/.j,(f^mid(g,2,len g))/.(j+1))
            = LSeg(f^mid(g,2,len g),j) by A273,A281,TOPREAL1:def 5;
            then LSeg(f^mid(g,2,len g),j)=LSeg(g,j-'len f+1)
            by A277,A280,A301,A305,A306,A309,A310,A311,A313,TOPREAL1:def 5;
            then LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
            by A3,A278,A283,TOPREAL1:def 9;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {}
            by XBOOLE_0:def 7;
          end;
          case j+1>len (f^mid(g,2,len g));
            then LSeg(f^mid(g,2,len g),j)={} by TOPREAL1:def 5;
            hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
          end;
        end;
        hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
      end;
      case j+1>len (f^mid(g,2,len g));
        then LSeg(f^mid(g,2,len g),j) = {} by TOPREAL1:def 5;
        hence LSeg(f^mid(g,2,len g),i) /\ LSeg(f^mid(g,2,len g),j) = {};
      end;
    end;
    hence LSeg(f^mid(g,2,len g),i) misses LSeg(f^mid(g,2,len g),j)
    by XBOOLE_0:def 7;
  end;
  for i be Nat st 1 <= i & i+1 <= len (f^mid(g,2,len g)) holds
  ((f^mid(g,2,len g))/.i)`1 = ((f^mid(g,2,len g))/.(i+1))`1
  or ((f^mid(g,2,len g))/.i)`2 = ((f^mid(g,2,len g))/.(i+1))`2
  proof
    let i be Nat;
    assume
A314: 1 <= i & i+1 <= len (f^mid(g,2,len g));
    now per cases;
      case
A315:   i<len f;
A316:   1<=i+1 by A314,NAT_D:48;
A317:   i+1<=len f by A315,NAT_1:13;
        1<=i & i<=len (f^mid(g,2,len g)) by A314,NAT_D:46;
        then
A318:   (f^mid(g,2,len g))/.i=(f^mid(g,2,len g)).i by FINSEQ_4:24;
        f/.i=f.i by A314,A315,FINSEQ_4:24;
        then
A319:   (f^mid(g,2,len g))/.i=f/.i by A314,A315,A318,FINSEQ_1:85;
A320:   (f^mid(g,2,len g))/.((i+1))=(f^mid(g,2,len g)).(i+1)
        by A314,A316,FINSEQ_4:24;
A321:   f/.(i+1)=f.(i+1) by A316,A317,FINSEQ_4:24;
        (f^mid(g,2,len g)).(i+1)=f.(i+1) by A316,A317,FINSEQ_1:85;
        hence ((f^mid(g,2,len g))/.i)`1 = ((f^mid(g,2,len g))/.(i+1))`1
        or ((f^mid(g,2,len g))/.i)`2 = ((f^mid(g,2,len g))/.(i+1))`2
        by A2,A314,A317,A319,A320,A321,TOPREAL1:def 7;
      end;
      case
A322:   i>=len f;
        then
A323:   i-len f>=0 by XREAL_1:50;
        then
A324:   i-'len f=i-len f by XREAL_0:def 2;
A325:   i+1>=len f by A322,NAT_D:48;
A326:   i+1-len f<=len f+(len g-1)-len f by A6,A8,A314,XREAL_1:11;
        then
A327:   i-len f+1+1<=len g-1+1 by XREAL_1:8;
        then
A328:   1<=i-'len f+1 & i-'len f+1+1<=len g by A323,NAT_1:11,XREAL_0:def 2;
        i-'len f<=i-'len f+1 by NAT_1:11;
        then
A329:   i-'len f<=len g-2+1 by A324,A326,XXREAL_0:2;
A330:   1<=i & i<=len (f^mid(g,2,len g)) by A314,NAT_D:46;
        1<=i-'len f+1 & i-'len f+1<=len g by A324,A327,NAT_1:11,NAT_D:46;
        then
A331:   g/.(i-'len f+1)=g.(i-'len f+1) by FINSEQ_4:24;
        i+1-len f<=len g-2+1 by A326;
        then i+1-len f<=len g-'2+1 by A3,XREAL_1:235;
        then
A332:   i+1-'len f<=len g -'2+1 by A325,XREAL_1:235;
A333:   i+1-'len f+1=i+1-len f+1 by A325,XREAL_1:235
          .=i-len f+1+1
          .=i-'len f+1+1 by A322,XREAL_1:235;
        1<=1+(i-'len f) by NAT_1:11;
        then 1<=1+(i-len f) by A322,XREAL_1:235;
        then 1<=1+i-len f;
        then
A334:   1<=i+1-'len f by NAT_D:39;
A335:   now
          assume
A336:     1<=i-'len f;
          then 1<=i-len f by NAT_D:39;
          then 1+len f<=i-len f +len f by XREAL_1:8;
          then
A337:     len f<i by NAT_1:13;
          then (f^mid(g,2,len g)).i=mid(g,2,len g).(i-len f) by A330,Th15;
          then (f^mid(g,2,len g)).i=mid(g,2,len g).(i-'len f)
          by A337,XREAL_1:235;
          then (f^mid(g,2,len g)).i=g.(i-'len f+2-1) by A3,A329,A336,Th31;
          hence (f^mid(g,2,len g)).i=g.(i-'len f+1);
        end;
        now
          assume 1>i-'len f;
          then i-'len f+1<=0+1 by NAT_1:13;
          then i-'len f<=0 by XREAL_1:8;
          then i-'len f=0;
          hence (f^mid(g,2,len g)).i
          = g.(i-'len f+1) by A1,A4,A324,FINSEQ_1:85;
        end;
        then
A338:   (f^mid(g,2,len g))/.i=g/.(i-'len f+1) by A330,A331,A335,FINSEQ_4:24;
        1<=i+1 & i+1<=len (f^mid(g,2,len g)) by A314,NAT_D:48;
        then
A339:   (f^mid(g,2,len g))/.(i+1)=(f^mid(g,2,len g)).(i+1) by FINSEQ_4:24;
        1<=i-'len f+1+1 & i-'len f+1+1<=len g by A323,A327,NAT_1:11
,XREAL_0:def 2;
        then
A340:   g/.(i-'len f+1+1)=g.(i-'len f+1+1) by FINSEQ_4:24;
        len f<(i+1) by A322,NAT_1:13;
        then (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).((i+1)-len f)
        by A314,Th15;
        then (f^mid(g,2,len g)).(i+1)=mid(g,2,len g).((i+1)-'len f)
        by A325,XREAL_1:235;
        then (f^mid(g,2,len g)).(i+1)=g.((i+1)-'len f+2-1)
        by A3,A332,A334,Th31;
        hence ((f^mid(g,2,len g))/.i)`1 = ((f^mid(g,2,len g))/.(i+1))`1
        or ((f^mid(g,2,len g))/.i)`2 = ((f^mid(g,2,len g))/.(i+1))`2
        by A3,A328,A333,A338,A339,A340,TOPREAL1:def 7;
      end;
    end;
    hence ((f^mid(g,2,len g))/.i)`1 = ((f^mid(g,2,len g))/.(i+1))`1
    or ((f^mid(g,2,len g))/.i)`2 = ((f^mid(g,2,len g))/.(i+1))`2;
  end;
  then f^mid(g,2,len g) is one-to-one & len (f^mid(g,2,len g)) >= 2
  & f^mid(g,2,len g) is unfolded s.n.c. special
  by A2,A7,A9,A52,A144,FUNCT_1:def 8,TOPREAL1:def 7,def 8,def 9,XXREAL_0:2;
  hence f^mid(g,2,len g) is_S-Seq by TOPREAL1:def 10;
end;

theorem Th74:
  for f,g being FinSequence of TOP-REAL 2 st f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1}
  holds f^mid(g,2,len g) is_S-Seq_joining f/.1,g/.len g
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume
A1: f.len f=g.1 & f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1};
  then
A2: f^mid(g,2,len g) is_S-Seq by Th73;
A3: f is one-to-one & len f >= 2
  & f is unfolded s.n.c. special by A1,TOPREAL1:def 10;
A4: g is one-to-one & len g >= 2
  & g is unfolded s.n.c. special by A1,TOPREAL1:def 10;
A5: 1<=len f by A3,XXREAL_0:2;
A6: 1<=len g by A4,XXREAL_0:2;
A7: len (f^mid(g,2,len g))=len f + len mid(g,2,len g) by FINSEQ_1:35;
A8: len mid(g,2,len g)=len g -'2+1 by A4,A6,Th27;
  then
A9: len mid(g,2,len g) =len g -2+1 by A4,XREAL_1:235
    .=len g -1;
A10: 1+1-1<=len g-1 by A4,XREAL_1:11;
A11: len (mid(g,2,len g))+2-1=len g by A9;
A12: (f^mid(g,2,len g)).1=f.1 by A5,FINSEQ_1:85
    .=f/.1 by A5,FINSEQ_4:24;
  len g-1>=1+1-1 by A4,XREAL_1:11;
  then len f+1<=len (f^mid(g,2,len g)) by A7,A9,XREAL_1:8;
  then len f<len (f^mid(g,2,len g)) by NAT_1:13;
  then (f^mid(g,2,len g)).(len (f^mid(g,2,len g)))
  =(mid(g,2,len g)).(len (f^mid(g,2,len g))-len f) by Th15
    .=g.len g by A4,A7,A8,A10,A11,Th31
    .=g/.len g by A6,FINSEQ_4:24;
  hence f^mid(g,2,len g) is_S-Seq_joining f/.1,g/.len g by A2,A12,Def3;
end;

theorem
  for f being FinSequence of TOP-REAL 2,
  n being Element of NAT holds L~(f/^n) c= L~f
proof
  let f be FinSequence of TOP-REAL 2, n be Element of NAT;
  let x be set;
  assume x in L~(f/^n);
  then x in union { LSeg(f/^n,i) : 1 <= i & i+1 <= len (f/^n) }
  by TOPREAL1:def 6;
  then consider Y being set such that
A1: x in Y & Y in
  { LSeg(f/^n,i) : 1 <= i & i+1 <= len (f/^n) } by TARSKI:def 4;
  consider i such that
A2: Y= LSeg(f/^n,i) &(1 <= i & i+1 <= len (f/^n)) by A1;
  now per cases;
    case n<=len f;
      then LSeg(f/^n,i)=LSeg(f,n+i) by A2,SPPOL_2:4;
      then Y c=L~f by A2,TOPREAL3:26;
      hence x in L~f by A1;
    end;
    case n>len f;
      then f/^n=<*>(the carrier of TOP-REAL 2) by RFINSEQ:def 2;
      hence contradiction by A2,FINSEQ_1:32;
    end;
  end;
  hence x in L~f;
end;

theorem Th76:
  for f being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st p in L~f holds L~R_Cut(f,p) c= L~f
proof
  let f be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2 such that
A1: p in L~f;
  len f <> 0 by A1,TOPREAL1:28;
  then len f > 0;
  then
A2: len f >= 0+1 by NAT_1:13;
A3: 1<=Index(p,f) & Index(p,f)<=len f by A1,Th41;
  per cases;
  suppose p = f.1;
    then R_Cut(f,p)=<*p*> by Def5;
    then len R_Cut(f,p)=1 by FINSEQ_1:56;
    then L~R_Cut(f,p) = {} by TOPREAL1:28;
    hence thesis by XBOOLE_1:2;
  end;
  suppose p<>f.1;
    then
A4: R_Cut(f,p)=mid(f,1,Index(p,f))^<*p*> by Def5;
    Index(p,f) < len f by A1,Th41;
    then
A5: 1<=Index(p,f)+1 & Index(p,f)+1<=len f by NAT_1:11,13;
A6: len (mid(f,1,Index(p,f))) =Index(p,f)-'1+1 by A2,A3,Th27
      .=Index(p,f) by A3,XREAL_1:237;
    then (mid(f,1,Index(p,f)))<><*>(the carrier of TOP-REAL 2)
    by A3,FINSEQ_1:32;
    then
A7: L~(mid(f,1,Index(p,f))^<*p*>)
    = L~mid(f,1,Index(p,f)) \/ LSeg((mid(f,1,Index(p,f))/.Index(p,f)),p)
    by A6,SPPOL_2:19;
A8: (mid(f,1,Index(p,f)))/.Index(p,f)=mid(f,1,Index(p,f)).Index(p,f)
    by A3,A6,FINSEQ_4:24
      .=f.(Index(p,f)) by A3,Th32
      .=f/.(Index(p,f)) by A3,FINSEQ_4:24;
    p in LSeg(f,Index(p,f)) by A1,Th42;
    then
A9: p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A3,A5,TOPREAL1:def 5;
    f/.(Index(p,f)) in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by TOPREAL1:6;
    then
A10: LSeg((mid(f,1,Index(p,f))/.Index(p,f)),p)
    c= LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A8,A9,TOPREAL1:12;
    LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) c= L~f by A3,A5,SPPOL_2:16;
    then
A11: LSeg((mid(f,1,Index(p,f))/.Index(p,f)),p)c= L~f by A10,XBOOLE_1:1;
    mid(f,1,Index(p,f)) =(f/^(1-'1))|(Index(p,f)-'1+1) by A3,Def1
      .=(f/^(0))|(Index(p,f)-'1+1) by XREAL_1:234
      .=f|(Index(p,f)-'1+1) by FINSEQ_5:31
      .=f|Index(p,f) by A3,XREAL_1:237;
    then L~mid(f,1,Index(p,f)) c= L~f by TOPREAL3:27;
    hence L~(R_Cut(f,p)) c= L~f by A4,A7,A11,XBOOLE_1:8;
  end;
end;

Lm2: i<=j implies j+k-'i=j+k-i by NAT_1:12,XREAL_1:235;

Lm3: (<*>D)|i=<*>D
proof len (<*>D)=0 by FINSEQ_1:32;
  hence thesis by FINSEQ_1:79;
end;

Lm5: for f1 be FinSequence of D,k be Element of NAT st 1<=k & k<=len f1
holds mid(f1,k,k)=<*f1/.k*> & len mid(f1,k,k)=1
proof
  let f1 be FinSequence of D,k be Element of NAT;
  assume
A1: 1<=k & k<=len f1;
A2: len (f1/^(k-'1))=len f1-'(k-'1) by RFINSEQ:42;
  k-'1+1=k by A1,XREAL_1:237;
  then
A3: (f1/^(k-'1)).1=f1.k by A1,Th23;
A4: f1/.k=f1.k by A1,FINSEQ_4:24;
  k-'1+1<=len f1 by A1,XREAL_1:237;
  then k-'1+1-(k-'1)<=len f1-(k-'1) by XREAL_1:11;
  then
A5: 1<=1 & 1<=len (f1/^(k-'1)) by A2,NAT_D:39;
  then
A6: f1/^(k-'1) is non empty by CARD_1:47;
  k-'k+1=k-k+1 by XREAL_1:235
    .=1;
  then mid(f1,k,k)=(f1/^(k-'1))|1 by Def1
    .=<*(f1/^(k-'1))/.1*> by A6,FINSEQ_5:23;
  hence thesis by A3,A4,A5,FINSEQ_1:56,FINSEQ_4:24;
end;

Lm6: for f1 be FinSequence of D holds mid(f1,0,0)=f1|1
proof
  let f1 be FinSequence of D;
  0-'0+1=0-0+1 by XREAL_1:235
    .=1;
  then
A1: mid(f1,0,0)=(f1/^(0-'1))|1 by Def1;
  0-1<0;
  then 0-'1=0 by XREAL_0:def 2;
  hence thesis by A1,FINSEQ_5:31;
end;

Lm7: for f1 be FinSequence of D,k be Element of NAT st len f1<k
holds mid(f1,k,k)=<*>D
proof
  let f1 be FinSequence of D,k be Element of NAT;
  assume
A1: len f1<k;
  then
A2: 0+1<=k by NAT_1:13;
  k-'k+1=k-k+1 by XREAL_1:235
    .=1;
  then
A3: mid(f1,k,k)=(f1/^(k-'1))|1 by Def1;
  len f1+1<=k by A1,NAT_1:13;
  then len f1+1-1<=k-1 by XREAL_1:11;
  then len f1<=k-'1 by A2,XREAL_1:235;
  then f1/^(k-'1)=<*>D by FINSEQ_5:35;
  hence thesis by A3,Lm3;
end;

Lm8: for f1 be FinSequence of D, i1,i2 holds mid(f1,i1,i2)=Rev mid(f1,i2,i1)
proof
  let f1 be FinSequence of D;
  let k1,k2 be Element of NAT;
  now per cases;
    case
A1:   k1<=k2;
      then
A2:   mid(f1,k1,k2)=(f1/^(k1-'1))|(k2-'k1+1) by Def1;
      now per cases by A1,XXREAL_0:1;
        case k1<k2;
          then mid(f1,k2,k1)=Rev mid(f1,k1,k2) by A2,Def1;
          hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by FINSEQ_6:29;
        end;
        case
A3:       k1=k2;
A4:       k1=0 or 0+1<=k1 by NAT_1:13;
          now per cases by A4;
            case k1=0;
              then
A5:           mid(f1,k1,k2)=f1|1 by A3,Lm6;
              now per cases;
                case len f1=0;
                  then f1=<*>D by FINSEQ_1:32;
                  then f1|1=<*>D by Lm3;
                  hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,A5;
                end;
                case len f1<>0;
                  then f1 <> <*>D by FINSEQ_1:32;
                  then f1|1=<*f1/.1*> by FINSEQ_5:23;
                  hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,A5,FINSEQ_5:63;
                end;
              end;
              hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
            end;
            case 1<=k1 & k1<= len f1;
              then mid(f1,k1,k1)=<*f1/.k1*> by Lm5;
              hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3,FINSEQ_5:63;
            end;
            case len f1<k1;
              then mid(f1,k1,k1)=<*>D by Lm7;
              hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A3;
            end;
          end;
          hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
        end;
      end;
      hence mid(f1,k1,k2)=Rev mid(f1,k2,k1);
    end;
    case
A6:   k1>k2;
      then mid(f1,k1,k2)=Rev ((f1/^(k2-'1))|(k1-'k2+1)) by Def1;
      hence mid(f1,k1,k2)=Rev mid(f1,k2,k1) by A6,Def1;
    end;
  end;
  hence thesis;
end;

Lm9: for h being FinSequence of TOP-REAL 2,i1,i2 st
1<=i1 & i1<=len h & 1<=i2 & i2<=len h holds L~mid(h,i1,i2) c= L~h
proof
  let h be FinSequence of TOP-REAL 2,i1,i2;
  assume
A1: 1<=i1 & i1<=len h & 1<=i2 & i2<=len h;
  thus L~mid(h,i1,i2) c= L~h
  proof
    let x be set;
    assume
A2: x in L~mid(h,i1,i2);
    now per cases;
      case
A3:     i1<=i2;
        x in union { LSeg(mid(h,i1,i2),i) : 1 <= i & i+1 <= len mid(h,i1,i2) }
        by A2,TOPREAL1:def 6;
        then consider Y being set such that
A4:     x in Y
        & Y in { LSeg(mid(h,i1,i2),i) : 1 <= i & i+1 <= len mid(h,i1,i2) }
        by TARSKI:def 4;
        consider i such that
A5:     Y=LSeg(mid(h,i1,i2),i) & (1 <= i & i+1 <= len mid(h,i1,i2)) by A4;
A6:     LSeg(mid(h,i1,i2),i)=LSeg(mid(h,i1,i2)/.i,mid(h,i1,i2)/.(i+1))
        by A5,TOPREAL1:def 5;
        i<=i+1 by NAT_1:11;
        then
A7:     i<=len mid(h,i1,i2) by A5,XXREAL_0:2;
        then
A8:     mid(h,i1,i2).i=h.(i+i1-'1) by A1,A3,A5,Th27;
A9:     len mid(h,i1,i2)=i2-'i1+1 by A1,A3,Th27;
        1<=i+1 & i+1<=len mid(h,i1,i2) by A5,NAT_1:11;
        then
A10:    mid(h,i1,i2).(i+1)=h.(i+1+i1-'1) by A1,A3,Th27;
A11:    mid(h,i1,i2)/.i=mid(h,i1,i2).i by A5,A7,FINSEQ_4:24;
A12:    mid(h,i1,i2)/.(i+1)=mid(h,i1,i2).(i+1) by A5,FINSEQ_4:24,NAT_1:11;
        1+1<=i+i1 by A1,A5,XREAL_1:9;
        then
A13:    1+1-1<=i+i1-1 by XREAL_1:11;
        i+1-1<=i2-'i1+1-1 by A5,A9,XREAL_1:11;
        then i<=i2-i1 by A3,XREAL_1:235;
        then
A14:    i+i1<=i2-i1+i1 by XREAL_1:8;
        then
A15:    i+i1<=len h by A1,XXREAL_0:2;
        then 1<=i+i1-'1 & i+i1-'1<=len h by A1,A13,Lm2,NAT_D:44;
        then
A16:    h/.(i+i1-'1)=h.(i+i1-'1) by FINSEQ_4:24;
A17:    i+1+i1-'1=i+1+i1-1 by A1,NAT_1:12,XREAL_1:235
          .=i+i1;
        then
A18:    i+1+i1-'1=i+i1-1+1
          .=i+i1-'1+1 by A1,NAT_1:12,XREAL_1:235;
A19:    1<=i+i1 & i+i1<=len h by A1,A14,NAT_1:12,XXREAL_0:2;
        then LSeg(mid(h,i1,i2),i)=LSeg(h/.(i+i1-'1),h/.(i+1+i1-'1))
        by A6,A8,A10,A11,A12,A16,A17,FINSEQ_4:24
          .=LSeg(h,i+i1-'1) by A13,A15,A17,A18,TOPREAL1:def 5;
        then LSeg(mid(h,i1,i2),i) in { LSeg(h,j) : 1 <= j & j+1 <= len h}
        by A13,A17,A18,A19;

then x in union { LSeg(h,j) : 1 <= j & j+1 <= len h} by A4,A5,TARSKI:def 4;
        hence x in L~h by TOPREAL1:def 6;
      end;
      case
A20:    i1>i2;
        mid(h,i1,i2)=Rev mid(h,i2,i1) by Lm8;
        then x in L~mid(h,i2,i1) by A2,SPPOL_2:22;

then x in union { LSeg(mid(h,i2,i1),i) : 1 <= i & i+1 <= len mid(h,i2,i1) }
        by TOPREAL1:def 6;
        then consider Y being set such that
A21:    x in Y
        & Y in { LSeg(mid(h,i2,i1),i) : 1 <= i & i+1 <= len mid(h,i2,i1) }
        by TARSKI:def 4;
        consider i such that
A22:    Y=LSeg(mid(h,i2,i1),i) & (1 <= i & i+1 <= len mid(h,i2,i1)) by A21;
A23:    LSeg(mid(h,i2,i1),i)=LSeg(mid(h,i2,i1)/.i,mid(h,i2,i1)/.(i+1))
        by A22,TOPREAL1:def 5;
        i<=i+1 by NAT_1:11;
        then
A24:    i<=len mid(h,i2,i1) by A22,XXREAL_0:2;
        then
A25:    mid(h,i2,i1).i=h.(i+i2-'1) by A1,A20,A22,Th27;
A26:    len mid(h,i2,i1)=i1-'i2+1 by A1,A20,Th27;
        1<=i+1 & i+1<=len mid(h,i2,i1) by A22,NAT_1:11;
        then
A27:    mid(h,i2,i1).(i+1)=h.(i+1+i2-'1) by A1,A20,Th27;
A28:    mid(h,i2,i1)/.i=mid(h,i2,i1).i by A22,A24,FINSEQ_4:24;
A29:    mid(h,i2,i1)/.(i+1)=mid(h,i2,i1).(i+1) by A22,FINSEQ_4:24,NAT_1:11;
        1+1<=i+i2 by A1,A22,XREAL_1:9;
        then
A30:    1+1-1<=i+i2-1 by XREAL_1:11;
        then
A31:    1<=i+i2-'1 by A1,NAT_1:12,XREAL_1:235;
        i+1-1<=i1-'i2+1-1 by A22,A26,XREAL_1:11;
        then i<=i1-i2 by A20,XREAL_1:235;
        then
A32:    i+i2<=i1-i2+i2 by XREAL_1:8;
        then
A33:    i+i2<=len h by A1,XXREAL_0:2;
        then
A34:    h/.(i+i2-'1)=h.(i+i2-'1) by A31,FINSEQ_4:24,NAT_D:44;
A35:    i+1+i2-'1=i+1+i2-1 by A1,NAT_1:12,XREAL_1:235
          .=i+i2;
        then
A36:    i+1+i2-'1=i+i2-1+1
          .=i+i2-'1+1 by A1,NAT_1:12,XREAL_1:235;
A37:    1<=i+i2 & i+i2<=len h by A1,A32,NAT_1:12,XXREAL_0:2;
        then LSeg(mid(h,i2,i1),i)=LSeg(h/.(i+i2-'1),h/.(i+1+i2-'1))
        by A23,A25,A27,A28,A29,A34,A35,FINSEQ_4:24
          .=LSeg(h,i+i2-'1) by A30,A33,A35,A36,TOPREAL1:def 5;
        then LSeg(mid(h,i2,i1),i) in { LSeg(h,j) : 1 <= j & j+1 <= len h}
        by A30,A35,A36,A37;
        then x in union { LSeg(h,j) : 1 <= j & j+1 <= len h} by A21,A22,
TARSKI:def 4;
        hence x in L~h by TOPREAL1:def 6;
      end;
    end;
    hence x in L~h;
  end;
end;

Lm10: i in dom f & j in dom f implies len mid(f,i,j) >= 1
proof
  assume i in dom f;
  then
A1: 1 <= i & i <= len f by FINSEQ_3:27;
  assume j in dom f;
  then
A2: 1 <= j & j <= len f by FINSEQ_3:27;
  i <= j or j < i;
  then len mid(f,i,j)=i-'j+1 or len mid(f,i,j)=j-'i+1 by A1,A2,Th27;
  hence thesis by NAT_1:11;
end;

Lm11: i in dom f & j in dom f implies mid(f,i,j) is non empty
proof
  assume i in dom f & j in dom f;
  then len mid(f,i,j) >= 1 by Lm10;
  hence mid(f,i,j) is non empty by FINSEQ_3:27,RELAT_1:60;
end;

Lm12: i in dom f & j in dom f implies (mid(f,i,j))/.1 = f/.i
proof
  assume
A1: i in dom f;
  then
A2: 1 <= i & i <= len f by FINSEQ_3:27;
  assume
A3: j in dom f;
  then
A4: 1 <= j & j <= len f by FINSEQ_3:27;
  mid(f,i,j) is non empty by A1,A3,Lm11;
  then 1 in dom mid(f,i,j) by FINSEQ_5:6;
  hence (mid(f,i,j))/.1 = mid(f,i,j).1 by PARTFUN1:def 8
    .= f.i by A2,A4,Th27
    .= f/.i by A1,PARTFUN1:def 8;
end;

theorem Th77:
  for f be FinSequence of TOP-REAL 2
  for p be Point of TOP-REAL 2 st p in L~f holds L~L_Cut(f,p) c= L~f
proof
  let f be FinSequence of TOP-REAL 2;
  let p be Point of TOP-REAL 2 such that
A1: p in L~f;
  len f <> 0 by A1,TOPREAL1:28;
  then len f > 0;
  then
A2: len f >= 0+1 by NAT_1:13;
  then
A3: len f in dom f by FINSEQ_3:27;
A4: 1<=Index(p,f) & Index(p,f)<len f by A1,Th41;
  then
A5: 1<Index(p,f)+1 & Index(p,f)+1<=len f by NAT_1:13;
  then
A6: Index(p,f)+1 in dom f by FINSEQ_3:27;
  per cases;
  suppose p = f.(Index(p,f)+1);
    then L_Cut(f,p) = mid(f,Index(p,f)+1,len f) by Def4;
    hence thesis by A2,A5,Lm9;
  end;
  suppose p <> f.(Index(p,f)+1);
    then
A7: L_Cut(f,p) = <*p*>^mid(f,Index(p,f)+1,len f) by Def4;
    len mid(f,Index(p,f)+1,len f) >= 0+1 by A3,A6,Lm10;
    then mid(f,Index(p,f)+1,len f) <> {} by CARD_1:47;
    then
A8: L~(<*p*>^mid(f,Index(p,f)+1,len f))
    = LSeg(p,mid(f,Index(p,f)+1,len f)/.1) \/ L~mid(f,Index(p,f)+1,len f)
    by SPPOL_2:20;
A9: mid(f,Index(p,f)+1,len f)/.1 = f/.(Index(p,f)+1) by A3,A6,Lm12;
    p in LSeg(f,Index(p,f)) by A1,Th42;
    then
A10: p in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A4,A5,TOPREAL1:def 5;
    f/.(Index(p,f)+1) in LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1))
    by TOPREAL1:6;
    then
A11: LSeg(p,mid(f,Index(p,f)+1,len f)/.1) c=
    LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) by A9,A10,TOPREAL1:12;
    LSeg(f/.(Index(p,f)),f/.(Index(p,f)+1)) c= L~f by A4,A5,SPPOL_2:16;
    then
A12: LSeg(p,mid(f,Index(p,f)+1,len f)/.1) c= L~f by A11,XBOOLE_1:1;
    L~mid(f,Index(p,f)+1,len f) c= L~f by A2,A5,Lm9;
    hence L~(L_Cut(f,p)) c= L~f by A7,A8,A12,XBOOLE_1:8;
  end;
end;

theorem Th78:
  for f,g being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st f.len f=g.1 & p in L~f &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>f.len f
  holds L_Cut(f,p)^mid(g,2,len g) is_S-Seq_joining p,g/.len g
proof
  let f,g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume that
A1: f.len f=g.1 and
A2: p in L~f and
A3: f is_S-Seq and
A4: g is_S-Seq and
A5: L~f /\ L~g={g.1} and
A6: p<>f.len f;
A7: f is one-to-one & len f >= 2
  & f is unfolded s.n.c. special by A3,TOPREAL1:def 10;
A8: g is one-to-one & len g >= 2
  & g is unfolded s.n.c. special by A4,TOPREAL1:def 10;
A9: 1<=len f by A7,XXREAL_0:2;
A10: 1<=len g by A8,XXREAL_0:2;
A11: L_Cut(f,p) is_S-Seq by A2,A3,A6,Th69;
  then
A12: 1+1<=len L_Cut(f,p) by TOPREAL1:def 10;
  then
A13: 1+1-1<=len L_Cut(f,p)-1 by XREAL_1:11;
  L_Cut(f,p) is_S-Seq_joining p,f/.len f by A2,A3,A6,Th68;
  then
A14: L_Cut(f,p).1=p & L_Cut(f,p).(len L_Cut(f,p))=f/.len f by Def3;
A15: 1<=len L_Cut(f,p) by A12,XXREAL_0:2;
  then L_Cut(f,p).1=(L_Cut(f,p))/.1 by FINSEQ_4:24;
  then
A16: (L_Cut(f,p))/.(1)=p by A2,Th58;
  g/.1 in LSeg(g/.1,g/.(1+1)) by TOPREAL1:6;
  then g/.1 in LSeg(g,1) by A8,TOPREAL1:def 5;
  then g.1 in LSeg(g,1) by A10,FINSEQ_4:24;
  then
A17: g.1 in L~g by SPPOL_2:17;
A18: len L_Cut(f,p)-'1+1=len L_Cut(f,p) by A12,XREAL_1:237,XXREAL_0:2;
  then (L_Cut(f,p))/.(len L_Cut(f,p)) in LSeg((L_Cut(f,p))/.(len L_Cut(f,p)
  -'1), (L_Cut(f,p))/.(len L_Cut(f,p)-'1+1)) by TOPREAL1:6;
  then L_Cut(f,p).len L_Cut(f,p) in LSeg((L_Cut(f,p))/.(len L_Cut(f,p)-'1),
  (L_Cut(f,p))/.(len L_Cut(f,p)-'1+1)) by A15,FINSEQ_4:24;
  then L_Cut(f,p).len L_Cut(f,p) in LSeg(L_Cut(f,p),len L_Cut(f,p)-'1)
  by A13,A18,TOPREAL1:def 5;
  then f/.len f in L~(L_Cut(f,p)) by A14,SPPOL_2:17;
  then f.len f in L~(L_Cut(f,p)) by A9,FINSEQ_4:24;
  then g.1 in L~(L_Cut(f,p))/\ L~g by A1,A17,XBOOLE_0:def 4;
  then
A19: {g.1}c= L~(L_Cut(f,p))/\ L~g by ZFMISC_1:37;
  L~(L_Cut(f,p)) c= L~f by A2,Th77;
  then L~(L_Cut(f,p))/\ L~g c= L~f /\ L~g by XBOOLE_1:27;
  then L~(L_Cut(f,p))/\ L~g={g.1} by A5,A19,XBOOLE_0:def 10;
  hence L_Cut(f,p)^mid(g,2,len g) is_S-Seq_joining p,g/.len g
  by A1,A4,A9,A11,A14,A16,Th74,FINSEQ_4:24;
end;

theorem
  for f,g being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st f.len f=g.1 & p in L~f &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>f.len f
  holds L_Cut(f,p)^mid(g,2,len g) is_S-Seq
proof
  let f,g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume f.len f=g.1 & p in L~f &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>f.len f;
  then L_Cut(f,p)^mid(g,2,len g) is_S-Seq_joining p,g/.len g by Th78;
  hence L_Cut(f,p)^mid(g,2,len g) is_S-Seq by Def3;
end;

theorem Th80:
  for f,g being FinSequence of TOP-REAL 2 st f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1}
  holds mid(f,1,len f-'1)^g is_S-Seq
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume
A1: f.len f=g.1 & f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1};
  then
A2: f is one-to-one & len f >= 2
  & f is unfolded s.n.c. special by TOPREAL1:def 10;
  then
A3: 1<=len f by XXREAL_0:2;
A4: len f-1>=1+1-1 by A2,XREAL_1:11;
A5: Rev f is_S-Seq by A1,SPPOL_2:47;
A6: Rev g is_S-Seq by A1,SPPOL_2:47;
  L~(Rev f)=L~f by SPPOL_2:22;
  then
A7: L~(Rev g)/\ L~(Rev f)={g.1} by A1,SPPOL_2:22;
A8: (Rev f).1=f.len f by FINSEQ_5:65;
A9: (Rev g).len (Rev g)=(Rev Rev g).1 by FINSEQ_5:65
    .=(Rev f).1 by A1,A8,FINSEQ_6:29;
A10: len f-'1<=len f by NAT_D:50;
A11: len f-'(len f-'1)+1=len f-(len f-'1)+1 by NAT_D:50,XREAL_1:235
    .=len f-(len f-1)+1 by A2,XREAL_1:235,XXREAL_0:2
    .=2;
A12: len (Rev f)=len f by FINSEQ_5:def 3;
A13: len f-'1+1=len f-1+1 by A2,XREAL_1:235,XXREAL_0:2
    .=len f;
  (Rev g)^(mid(Rev f,2,len (Rev f))) is_S-Seq by A1,A5,A6,A7,A8,A9,Th73;
  then (Rev g)^(Rev mid(f,1,len f-'1)) is_S-Seq by A3,A4,A10,A11,A12,A13,Th22;
  then Rev (mid(f,1,len f-'1)^g) is_S-Seq by FINSEQ_5:67;
  then Rev Rev (mid(f,1,len f-'1)^g) is_S-Seq by SPPOL_2:47;
  hence mid(f,1,len f-'1)^g is_S-Seq by FINSEQ_6:29;
end;

theorem Th81:
  for f,g being FinSequence of TOP-REAL 2 st f.len f=g.1 &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1}
  holds mid(f,1,len f-'1)^g is_S-Seq_joining f/.1,g/.len g
proof
  let f,g be FinSequence of TOP-REAL 2;
  assume
A1: f.len f=g.1 & f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1};
  then
A2: mid(f,1,len f-'1)^g is_S-Seq by Th80;
A3: f is one-to-one & len f >= 2
  & f is unfolded s.n.c. special by A1,TOPREAL1:def 10;
A4: g is one-to-one & len g >= 2
  & g is unfolded s.n.c. special by A1,TOPREAL1:def 10;
A5: 1<=len f by A3,XXREAL_0:2;
A6: 1<=len g by A4,XXREAL_0:2;
A7: len f-'1<=len f by NAT_D:50;
  1+1-1<=len f-1 by A3,XREAL_1:11;
  then
A8: 1<=len f-'1 by NAT_D:39;
A9: len (mid(f,1,len f-'1)^g)=len (mid(f,1,len f-'1)) + len g by FINSEQ_1:35;
  len mid(f,1,len f-'1)=len f-'1-'1+1 by A5,A7,A8,Th27
    .=len f-'1 -1+1 by A8,XREAL_1:235
    .=len f -'1;
  then
A10: (mid(f,1,len f-'1)^g).1=mid(f,1,len f-'1).1 by A8,FINSEQ_1:85
    .=f.1 by A7,A8,Th32
    .=f/.1 by A5,FINSEQ_4:24;
  0+len (mid(f,1,len f-'1))<len g+len (mid(f,1,len f-'1)) by A4,XREAL_1:8;
  then (mid(f,1,len f-'1)^g).(len (mid(f,1,len f-'1)^g))
  =g.(len (mid(f,1,len f-'1)^g)-len (mid(f,1,len f-'1))) by A9,Th15
    .=g/.len g by A6,A9,FINSEQ_4:24;
  hence mid(f,1,len f-'1)^g is_S-Seq_joining f/.1,g/.len g by A2,A10,Def3;
end;

theorem Th82:
  for f,g being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st f.len f=g.1 & p in L~g &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>g.1
  holds mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq_joining f/.1,p
proof
  let f,g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume
A1: f.len f=g.1 & p in L~g &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>g.1;
  then
A2: f is one-to-one & len f >= 2 by TOPREAL1:def 10;
A3: g is one-to-one & len g >= 2 by A1,TOPREAL1:def 10;
A4: 1<=len f by A2,XXREAL_0:2;
A5: 1<=len g by A3,XXREAL_0:2;
A6: 1+1-1<=len f-1 by A2,XREAL_1:11;
A7: len f-'1+1=len f by A2,XREAL_1:237,XXREAL_0:2;
A8: R_Cut(g,p) is_S-Seq by A1,Th70;
  then
A9: 1+1<=len R_Cut(g,p) by TOPREAL1:def 10;
  R_Cut(g,p) is_S-Seq_joining g/.1,p by A1,Th67;
  then
A10: R_Cut(g,p).1=g/.1 & R_Cut(g,p).(len R_Cut(g,p))=p by Def3;
  then
A11: R_Cut(g,p).1=f.len f by A1,A5,FINSEQ_4:24;
A12: 1<=len R_Cut(g,p) by A9,XXREAL_0:2;
  then R_Cut(g,p).len R_Cut(g,p)=(R_Cut(g,p))/.(len R_Cut(g,p)) by FINSEQ_4:24;
  then
A13: (R_Cut(g,p))/.(len R_Cut(g,p))=p by A1,Th59;
  f/.len f in LSeg(f/.(len f-'1),f/.(len f-'1+1)) by A7,TOPREAL1:6;
  then f/.len f in LSeg(f,len f-'1) by A6,A7,TOPREAL1:def 5;
  then f.len f in LSeg(f,len f-'1) by A4,FINSEQ_4:24;
  then
A14: f.len f in L~f by SPPOL_2:17;
  (R_Cut(g,p))/.(1) in LSeg((R_Cut(g,p))/.(1),
  (R_Cut(g,p))/.(1+1)) by TOPREAL1:6;
  then R_Cut(g,p).1 in LSeg((R_Cut(g,p))/.(1),(R_Cut(g,p))/.(1+1))
  by A12,FINSEQ_4:24;
  then R_Cut(g,p).1 in LSeg(R_Cut(g,p),1) by A9,TOPREAL1:def 5;
  then g/.1 in L~(R_Cut(g,p)) by A10,SPPOL_2:17;
  then g.1 in L~(R_Cut(g,p)) by A5,FINSEQ_4:24;
  then f.len f in L~f /\ L~R_Cut(g,p) by A1,A14,XBOOLE_0:def 4;
  then
A15: {f.len f}c= L~f /\ L~R_Cut(g,p) by ZFMISC_1:37;
  L~(R_Cut(g,p)) c= L~g by A1,Th76;
  then L~f /\ L~R_Cut(g,p) c= L~f /\ L~g by XBOOLE_1:27;
  then L~f /\ L~R_Cut(g,p)={R_Cut(g,p).1} by A1,A11,A15,XBOOLE_0:def 10;
  hence mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq_joining f/.1,p
  by A1,A8,A11,A13,Th81;
end;

theorem
  for f,g being FinSequence of TOP-REAL 2,
  p being Point of TOP-REAL 2 st f.len f=g.1 & p in L~g &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>g.1
  holds mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq
proof
  let f,g be FinSequence of TOP-REAL 2, p be Point of TOP-REAL 2;
  assume f.len f=g.1 & p in L~g &
  f is_S-Seq & g is_S-Seq & L~f /\ L~g={g.1} & p<>g.1;
  then mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq_joining f/.1,p by Th82;
  hence mid(f,1,len f -'1)^R_Cut(g,p) is_S-Seq by Def3;
end;

