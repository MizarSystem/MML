:: The {M}aclaurin Expansions
::  by Akira Nishino and Yasunari Shidama
::
:: Received July 6, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, BOOLE, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, SUBSET_1, ORDINAL2, QC_LANG1, PARTFUN1, SEQ_1, SEQ_2,
      RCOMP_1, SIN_COS, FDIFF_1, SERIES_1, GROUP_1, SUPINF_2, TAYLOR_1,
      TAYLOR_2, RFUNCT_2, MATRIX_2, NAT_1;
 notations SUBSET_1, RELAT_1, PARTFUN1, RFUNCT_2, RCOMP_1, XXREAL_0, XCMPLX_0,
      XREAL_0, ORDINAL1, NUMBERS, COMPLEX1, REAL_1, NAT_1, SEQ_1, SEQ_2,
      SERIES_1, FDIFF_1, SEQFUNC, NEWTON, SIN_COS, TAYLOR_1, ABIAN;
 constructors ARYTM_0, REAL_1, NAT_1, SEQ_2, RCOMP_1, RFUNCT_2, LIMFUNC1,
      FDIFF_1, SERIES_1, COMSEQ_3, SIN_COS, ABIAN, TAYLOR_1;
 registrations RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, MEMBERED, SEQ_1,
      RCOMP_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TAYLOR_1;
 theorems XBOOLE_1, ABSVALUE, REAL_2, PARTFUN1, XCMPLX_1, RELAT_1, RCOMP_1,
      SEQ_1, SEQ_2, SIN_COS, SERIES_1, FUNCT_1, RFUNCT_2, NAT_1, NEWTON, POWER,
      FDIFF_1, FDIFF_2, TAYLOR_1, SIN_COS2, XREAL_1, RFUNCT_1, ABIAN, COMPLEX1,
      XXREAL_0;
 schemes NAT_1;

begin

theorem Th1:
  for x be Real, n be Element of NAT holds
    abs(x |^ n) = abs(x) |^ n
proof
  let x be Real, n be Element of NAT;
  defpred P[Element of NAT] means abs(x |^ $1) = abs(x) |^ $1;
A1: P[0]
  proof
     abs(x |^ 0) = abs(1) by NEWTON:9
              .= 1 by ABSVALUE:def 1;
    hence thesis by NEWTON:9;
  end;
A2: for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that
A3: P[k];
     abs(x |^ (k+1)) = abs(x * x |^ k) by NEWTON:11
                   .= abs(x) * abs(x |^ k) by COMPLEX1:151
                   .= abs(x) |^ (k+1) by NEWTON:11,A3;
  hence thesis;
  end;
   for n be Element of NAT holds P[n] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

definition
  let f be PartFunc of REAL,REAL,
      Z be Subset of REAL,
      a be real number;
  func Maclaurin(f,Z,a) -> Real_Sequence equals
   Taylor(f,Z,0,a);
  coherence;
end;

theorem Th2:
  for n be Element of NAT, f be PartFunc of REAL,REAL, r be Real st
  ( 0 < r & f is_differentiable_on n+1, ].-r,r.[ )
    for x be Real st x in ].-r, r.[ holds
      ex s be Real st 0 < s & s < 1 &
        f.x = Partial_Sums(Maclaurin(f,].-r,r.[,x)).n
               + (diff(f,].-r,r.[).(n+1)).(s*x)
               * x |^ (n+1) / ((n+1)!)
proof
  let n be Element of NAT,
      f be PartFunc of REAL,REAL,
      r be Real such that
A1: 0 < r and
A2: f is_differentiable_on n+1, ].-r,r.[;
  let x be Real such that
A3: x in ].-r, r.[;
  consider s be Real such that
A4: 0 < s & s < 1 and
A5: f.x=Partial_Sums(Taylor(f, ].0-r,0+r.[,0,x)).n
               + (diff(f,].0-r,0+r.[).(n+1)).(0+s*(x-0))
               * (x-0) |^ (n+1) / ((n+1)!) by TAYLOR_1:33,A1,A2,A3;
  take s;
  thus thesis by A4,A5;
end;

theorem
   for n be Element of NAT, f be PartFunc of REAL,REAL, x0, r be Real st
  ( 0 < r & f is_differentiable_on n+1, ].x0-r,x0+r.[ )
    for x be Real st x in ].x0-r, x0+r.[ holds
      ex s be Real st 0 < s & s < 1 &
        abs(f.x-Partial_Sums(Taylor(f, ].x0-r,x0+r.[,x0,x)).n)
        = abs( (diff(f,].x0-r,x0+r.[).(n+1)).(x0+s*(x-x0))
               * (x-x0) |^ (n+1) / ((n+1)!) )
proof
  let n be Element of NAT;
  let f be PartFunc of REAL,REAL;
  let x0,r be Real such that
A1: 0 < r and
A2: f is_differentiable_on n+1, ].x0-r,x0+r.[;
  let x be Real such that
A3: x in ].x0-r,x0+r.[;
  consider s be Real such that
A4: 0 < s & s < 1 and
A5: f.x=Partial_Sums(Taylor(f, ].x0-r,x0+r.[,x0,x)).n
               + (diff(f,].x0-r,x0+r.[).(n+1)).(x0+s*(x-x0))
               * (x-x0) |^ (n+1) /((n+1)!) by TAYLOR_1:33,A1,A2,A3;
 thus thesis by A4,A5;
end;

theorem Th4:
  for n be Element of NAT, f be PartFunc of REAL,REAL, r be Real st
  ( 0 < r & f is_differentiable_on n+1, ].-r,r.[ )
    for x be Real st x in ].-r, r.[ holds
      ex s be Real st 0 < s & s < 1 &
        abs(f.x-Partial_Sums(Maclaurin(f,].-r,r.[,x)).n)
               = abs( (diff(f,].-r,r.[).(n+1)).(s*x)
               * x |^ (n+1) / ((n+1)!) )
proof
  let n be Element of NAT;
  let f be PartFunc of REAL,REAL;
  let r be Real such that
A1: 0 < r and
A2: f is_differentiable_on n+1, ].-r,r.[;
  let x be Real such that
A3: x in ].-r,r.[;
  consider s be Real such that
A4: 0 < s & s < 1 and
A5: f.x=Partial_Sums(Maclaurin(f, ].-r,r.[,x)).n
               + (diff(f,].-r,r.[).(n+1)).(s*x)
               * x |^ (n+1) / ((n+1)!) by Th2,A1,A2,A3;
  thus thesis by A4,A5;
end;

theorem Th5:
  for r be Real holds
    exp_R `| ].-r,r.[ = exp_R | ].-r,r.[ &
    dom(exp_R | ].-r,r.[) = ].-r,r.[
proof
  let r be Real;
A1:exp_R is_differentiable_on ].-r,r.[ by FDIFF_1:34,TAYLOR_1:16;
then A2:dom (exp_R `| ].-r,r.[) = ].-r,r.[ by FDIFF_1:def 8;
A3:dom (exp_R | ].-r,r.[) = dom(exp_R) /\ ].-r,r.[ by FUNCT_1:68
                        .= ].-r,r.[ by XBOOLE_1:28,TAYLOR_1:16;
    for x be Real st x in ].-r,r.[ holds
         (exp_R `| ].-r,r.[).x=(exp_R | ].-r,r.[).x
  proof
    let x be Real such that
A4:x in ].-r,r.[;
    thus (exp_R `| ].-r,r.[).x=diff(exp_R,x) by FDIFF_1:def 8,A1,A4
                        .=exp_R.x by TAYLOR_1:16
                        .=(exp_R | ].-r,r.[).x by FUNCT_1:70,A4,A3;
  end;
  hence thesis by A2,A3,PARTFUN1:34;
end;

theorem Th6:
  for n be Element of NAT, r be Real
   holds diff(exp_R,].-r,r.[).n=exp_R | ].-r,r.[
proof
  let n be Element of NAT, r be Real;
  defpred P[Element of NAT] means diff(exp_R,].-r,r.[).$1 =exp_R | ].-r,r.[;
  A1: P[0] by TAYLOR_1:def 5;
  A2: for k be Element of NAT st P[k] holds P[k+1]
   proof
   let k be Element of NAT such that
    A3: P[k];
    A4: exp_R is_differentiable_on ].-r,r.[ by FDIFF_1:34,TAYLOR_1:16;
     diff(exp_R,].-r,r.[).(k+1)=(diff(exp_R,].-r,r.[).k) `| ].-r,r.[
                           by TAYLOR_1:def 5
                   .=exp_R `| ].-r,r.[ by FDIFF_2:16,A4,A3
                   .=exp_R | ].-r,r.[ by Th5;
   hence thesis;
   end;
   for n be Element of NAT holds P[n] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;

theorem Th7:
  for n be Element of NAT, r, x be Real st x in ].-r,r.[ holds
    (diff(exp_R,].-r,r.[).n).x = exp_R.x
proof
  let n be Element of NAT;
  let r,x be Real such that
A1: x in ].-r,r.[;
A2: x in dom(exp_R | ].-r,r.[) by Th5,A1;
   (diff(exp_R,].-r,r.[).n).x=(exp_R | ].-r,r.[).x by Th6
                         .=exp_R.x by FUNCT_1:70,A2;
  hence thesis;
end;

theorem
   for n be Element of NAT, r,x be Real st 0 < r holds
    Maclaurin(exp_R, ].-r,r.[,x).n = x |^ n / (n!)
proof
  let n be Element of NAT;
  let r,x be Real such that
A1: r > 0;
 abs(0-0)=0 by ABSVALUE:7;
then 0 in ].0-r,0+r.[ by A1,RCOMP_1:8;
then A2: 0 in dom(exp_R | ].-r,r.[) by Th5;
   Maclaurin(exp_R, ].-r,r.[,x).n =
(diff(exp_R,].-r,r.[).n).0 * (x-0) |^ n / (n!) by TAYLOR_1:def 7
    .=(exp_R | ].-r,r.[).0 * x |^ n / (n!) by Th6
    .=exp_R.0 * x |^ n / (n!) by A2,FUNCT_1:70
    .=x |^ n / (n!) by SIN_COS2:13;
  hence thesis;
end;

theorem
   for n be Element of NAT, r,x,s be Real st
    x in ].-r,r.[ & 0 < s & s < 1 holds
      abs((diff(exp_R,].-r,r.[).(n+1)).(s*x) * x |^ (n+1) / ((n+1)!) )
               <= abs(exp_R.(s*x)) * abs(x) |^ (n+1) / ((n+1)!)
proof
  let n be Element of NAT;
  let r,x,s be Real such that
A1:x in ].-r,r.[ and
A2: 0 < s & s < 1;
   x in ].0-r,0+r.[ by A1;
then A3: abs(x-0) < r by RCOMP_1:8;
A4: abs(s*x-0) = abs(s) * abs(x) by COMPLEX1:151
                .= s * abs(x) by A2,ABSVALUE:def 1;
   abs(x)>=0 by COMPLEX1:132;
  then abs(x) * s < r * 1 by A2,XREAL_1:99,A3;
  then (s*x) in ].0-r,0+r.[ by RCOMP_1:8,A4;
then A5: (s*x) in dom(exp_R | ].-r,r.[) by Th5;
   ((n+1)!) > 0 by NEWTON:23;
then A6: abs((n+1)!) = ((n+1)!) by ABSVALUE:def 1;
   abs( (diff(exp_R,].-r,r.[).(n+1)).(s*x) * x |^ (n+1) /((n+1)!) )
     = abs((exp_R | ].-r,r.[).(s*x) * x |^ (n+1) /((n+1)!) ) by Th6
    .= abs(exp_R.(s*x) * x |^ (n+1) /((n+1)!) ) by FUNCT_1:70,A5
    .= abs(exp_R.(s*x) * x |^ (n+1)) /abs((n+1)!) by COMPLEX1:153
    .= abs(exp_R.(s*x)) * abs(x |^ (n+1)) /((n+1)!) by COMPLEX1:151,A6;
  hence thesis by Th1;
end;

theorem Th10:
  for r be Real, n be Element of NAT holds
    exp_R is_differentiable_on n, ].-r,r.[
proof
  let r be Real, n be Element of NAT;
  let i be Element of NAT such that i <= n-1;
   reconsider i as Element of NAT;
  A1: dom(diff(exp_R,].-r,r.[).i) = dom(exp_R | ].-r,r.[) by Th6
                               .= ].-r,r.[ by Th5;
   for x be Real st x in ].-r,r.[ holds
              diff(exp_R,].-r,r.[).i | ].-r,r.[ is_differentiable_in x
  proof
    let x be Real such that
    A2: x in ].-r,r.[;
A3: diff(exp_R,].-r,r.[).i | ].-r,r.[ = exp_R | ].-r,r.[ | ].-r,r.[ by Th6
                             .= exp_R | ].-r,r.[ by FUNCT_1:82;
     exp_R is_differentiable_on ].-r,r.[ by FDIFF_1:34,TAYLOR_1:16;
    hence thesis by A3,A2,FDIFF_1:def 7;
  end;
  hence thesis by A1,FDIFF_1:def 7;
end;

theorem Th11:
  for r be Real st 0 < r
   ex M,L be Real st 0 <= M & 0 <= L &
    for n be Element of NAT holds
     for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
      abs((diff(exp_R,].-r,r.[).n).(s*x) * x |^ n /(n!))
       <= M * L |^ n / (n!)
  proof
  let r be Real such that
  A1: r > 0;
  take M = exp_R.r;
  take L = r;
   now let n be Element of NAT;
     now let x,s be Real such that
      A2: x in ].-r,r.[ & 0 < s & s < 1;
       x in ].0-r,0+r.[ by A2;
      then A3: abs(x-0) < r by RCOMP_1:8;
       abs(x) >= 0 by COMPLEX1:132;
      then s * abs(x) < 1 * r by A2,A3,XREAL_1:99;
      then abs(s) * abs(x) < r by ABSVALUE:def 1,A2;
      then abs(s*x - 0) < r by COMPLEX1:151;
      then A4:(s*x) in ].0-r,0+r.[ by RCOMP_1:8;
      A5: abs(exp_R.(s*x)) >= 0 by COMPLEX1:132;
      A6: abs(x |^ n /(n!)) >= 0 by COMPLEX1:132;
       exp_R is_differentiable_on [#]REAL &
        (for x0 be Real holds 0 <= diff(exp_R,x0)) by TAYLOR_1:16;
      then A7: exp_R is_non_decreasing_on [#](REAL) by FDIFF_2:39;
       (s*x) in {p where p is Real : -r<p & p<r} by A4,RCOMP_1:def 2;
      then A8: ex g be Real st g= s*x & -r<g & g<r;
      A9: r in [#](REAL) /\ dom exp_R by TAYLOR_1:16;
       exp_R.(s*x) >= 0 by SIN_COS:59;
      then abs(exp_R.(s*x)) = exp_R.(s*x) by ABSVALUE:def 1;
      then A10: abs(exp_R.(s*x)) <= M by A7,A8,TAYLOR_1:16,RFUNCT_2:48,A9;
      A11: n! > 0 by NEWTON:23;
      A12: abs(x |^ n /(n!)) =abs(x |^ n) / abs(n!) by COMPLEX1:153
                           .=abs(x |^ n) / (n!) by A11,ABSVALUE:def 1
                           .=abs(x) |^ n /(n!) by Th1;
      A13: for a,b be Real st 0 <=a & a<=b for n be Element of NAT holds
                     0<= a |^ n  & a |^ n <= b |^ n
      proof
        let a,b be Real such that
        A14: 0 <= a & a <= b;
        defpred P[Element of NAT] means 0 <= a |^ $1 & a |^ $1 <= b |^ $1;
        A15: P[0]
        proof
          A16: b |^ 0 = 1 by NEWTON:9;
          thus 0 <= a |^ 0 by NEWTON:9;
          thus thesis by A16,NEWTON:9;
        end;
        A17: for k be Element of NAT st P[k] holds P[k+1]
        proof
          let k be Element of NAT such that
          A18: P[k];
          A19: b |^ (k+1) = b |^ k*b by NEWTON:11;
           0*a <= a |^ k*a by A14,A18,XREAL_1:68;
          hence a |^ (k+1) >= 0 by NEWTON:11;
           a |^ k*a <= b |^ k*b by A14,A18,XREAL_1:68;
          hence thesis by NEWTON:11,A19;
        end;
         for n be Element of NAT holds P[n] from NAT_1:sch 1(A15,A17);
        hence thesis;
      end;
       abs(x) >= 0 by COMPLEX1:132;
      then abs(x) |^ n <= L |^ n by A13,A3;
then A20:  abs(x |^ n /(n!)) <=L |^ n / (n!) by A12,XREAL_1:74,A11;
       abs((diff(exp_R,].-r,r.[).n).(s*x) * x |^ n / (n!))
                         =abs(exp_R.(s*x)* x |^ n / (n!))
                                   by Th7,A4
                         .=abs(exp_R.(s*x)* (x |^ n / (n!)))
                                   by XCMPLX_1:75
                         .=abs(exp_R.(s*x))*abs(x |^ n / (n!))
                                   by COMPLEX1:151;
      then abs((diff(exp_R,].-r,r.[).n).(s*x) * x |^ n / (n!))
                         <= M*(L |^ n / (n!))
                                   by XREAL_1:68,A5,A6,A10,A20;
      hence abs((diff(exp_R,].-r,r.[).n).(s*x) * x |^ n /(n!))
                         <= M*L |^ n / (n!) by XCMPLX_1:75;
    end;
    hence for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
            abs((diff(exp_R,].-r,r.[).n).(s*x) * x |^ n / (n!))
                         <= M*L |^ n / (n!);
  end;
  hence thesis by SIN_COS:59,A1;
end;

theorem Th12:
  for M,L be Real st M >= 0 & L >= 0
    for e be Real st e > 0 holds
       ex n be Element of NAT st for m be Element of NAT st n <= m holds
          (M*L |^ m / (m!)) < e
proof
  let M,L be Real such that
  A1: M >=0 & L >= 0;
  A2: for n be Element of NAT holds M*(L |^ n /(n!))=(M(#)(L ExpSeq)).n
  proof
    let n be Element of NAT;
     M*(L |^ n /(n!)) = M*((L ExpSeq).n) by SIN_COS:def 9
                     .= (M(#)(L ExpSeq)).n by SEQ_1:13;
    hence thesis;
  end;
   L ExpSeq is summable by SIN_COS:49;
  then (L ExpSeq) is convergent & lim (L ExpSeq)=0
                           by SERIES_1:7;
  then A3: M(#)(L ExpSeq) is convergent & lim (M(#)(L ExpSeq))=M*0
                           by SEQ_2:21,22;
  let p be Real such that
    A4: p > 0;
    consider n be Element of NAT such that
    A5: for m be Element of NAT st n<=m holds abs((M(#)(L ExpSeq)).m - 0) < p
                           by A4,SEQ_2:def 7,A3;
    take n;
     now let m be Element of NAT such that
      A6: n <= m;
      A7: L |^ m >= 0 by POWER:3,A1;
       m! > 0 by NEWTON:23;
      then A8: L |^ m / (m!) >= 0 by A7,REAL_2:125;
       abs((M(#)(L ExpSeq)).m - 0) = abs(M*(L |^ m /(m!))) by A2
                                  .=abs(M)*abs(L |^ m /(m!))
                                             by COMPLEX1:151
                                  .=M*abs(L |^ m /(m!))
                                             by A1,ABSVALUE:def 1
                                  .=M*(L |^ m /(m!))
                                             by ABSVALUE:def 1,A8
                                  .=M*L |^ m /(m!) by XCMPLX_1:75;
      hence (M*L |^ m /(m!)) < p by A5,A6;
    end;
   hence thesis;
end;

theorem Th13:
  for r, e be Real st 0 < r & 0 < e holds
    ex n be Element of NAT st
      for m be Element of NAT st n <= m holds
        for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
          abs((diff(exp_R,].-r,r.[).m).(s*x) * x |^ m / (m!))< e
proof
  let r, e be Real such that
  A1: 0 < r and
  A2: 0 < e;
  consider M, L be Real such that
  A3: M >= 0 & L >= 0 and
  A4: for n be Element of NAT holds
          for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
             abs((diff(exp_R,].-r,r.[).n).(s*x) * x |^ n / (n!))
                     <= M*L |^ n / (n!) by Th11,A1;
   consider n be Element of NAT such that
A5: for m be Element of NAT st n <= m holds (M*L |^ m / (m!)) < e
 by A3,A2,Th12;
   take n;
     let m be Element of NAT such that
A6: n <= m;
     let x,s be Real such that
A7: x in ].-r,r.[ & 0 < s & s < 1;
A8:  abs((diff(exp_R,].-r,r.[).m).(s*x) * x |^ m /(m!))
                <= M*L |^ m /(m!) by A4,A7;
      (M*L |^ m / (m!)) < e by A5,A6;
     hence abs((diff(exp_R,].-r,r.[).m).(s*x) * x |^ m /(m!)) < e
                         by A8,XXREAL_0:2;
end;

theorem
   for r, e be Real st 0 < r & 0 < e holds
  ex n be Element of NAT st for m be Element of NAT st n <= m holds
    for x being real number st x in ].-r,r.[ holds
      abs(exp_R.x-Partial_Sums(Maclaurin(exp_R,].-r,r.[,x)).m) < e
proof
  let r, e be Real such that
A1: r > 0 and
A2: e > 0;
    consider n be Element of NAT such that
    A3: for m be Element of NAT st n <= m holds
            for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
                abs( (diff(exp_R,].-r,r.[).m).(s*x)
                 * x |^ m / (m!) )< e  by Th13,A1,A2;
    take n;
    let m be Element of NAT such that
    A4: n <= m;
    A5: exp_R is_differentiable_on m+1, ].-r,r.[ by Th10;
     now let x be Real such that
    A6: x in ].-r,r.[;
    consider s be Real such that
    A7: 0 < s & s < 1 and
    A8: abs(exp_R.x-Partial_Sums(Maclaurin(exp_R,].-r,r.[,x)).m)
             =abs( (diff(exp_R,].-r,r.[).(m+1)).(s*x)
             * x |^ (m+1) / ((m+1)!) ) by Th4,A5,A1,A6;
     m <= m+1 by NAT_1:11;
    then n <= m+1 by XXREAL_0:2,A4;
    hence abs(exp_R.x-Partial_Sums(Maclaurin(exp_R,].-r,r.[,x)).m) < e
      by A3,A6,A7,A8;
    end;
    hence thesis;
end;

theorem Th15:
  for x be Real holds x ExpSeq is absolutely_summable
proof
  let x be Real;
   for n be Element of NAT holds abs(x ExpSeq.n) = (abs(x) ExpSeq).n
  proof
    let n be Element of NAT;
A1: n! > 0 by NEWTON:23;
     abs(x ExpSeq.n) = abs(x |^ n / (n!)) by SIN_COS:def 9
                   .= abs(x |^ n) / abs(n!) by COMPLEX1:153
                   .= abs(x |^ n) / (n!) by ABSVALUE:def 1,A1
                   .= abs(x) |^ n / (n!) by Th1
                   .= (abs(x) ExpSeq).n by SIN_COS:def 9;
    hence thesis;
  end;
  then abs(x) ExpSeq = abs(x ExpSeq) by SEQ_1:16;
  then abs(x ExpSeq) is summable by SIN_COS:49;
  hence thesis by SERIES_1:def 5;
end;

theorem
   for r, x be Real st 0 < r holds
       Maclaurin(exp_R,].-r,r.[,x) = (x ExpSeq)
     & Maclaurin(exp_R,].-r,r.[,x) is absolutely_summable
     & exp_R.x=Sum(Maclaurin(exp_R,].-r,r.[,x))
proof
  let r, x be Real such that
  A1: r > 0;
   abs(0-0)=0 by ABSVALUE:7;
  then 0 in ].0-r,0+r.[ by A1,RCOMP_1:8;
  then A2: 0 in dom(exp_R | ].-r,r.[) by Th5;
   Maclaurin(exp_R,].-r,r.[,x) = (x ExpSeq)
  proof
     now let t be set such that
      A3: t in NAT;
      reconsider n=t as Element of NAT by A3;
      thus Maclaurin(exp_R,].-r,r.[,x).t
                         = (diff(exp_R,].-r,r.[).n).0 * (x-0) |^ n / (n!)
                                            by TAYLOR_1:def 7
                        .= (exp_R | ].-r,r.[).0 * x |^ n / (n!) by Th6
                        .= exp_R.0 * x |^ n / (n!) by A2,FUNCT_1:70
                        .= (x ExpSeq).t by SIN_COS:def 9,SIN_COS2:13;
    end;
    hence thesis by SEQ_1:8;
  end;
  hence thesis by Th15,SIN_COS:def 26;
end;

theorem Th17:
  for r be Real holds
          sin `| ].-r,r.[ = cos | ].-r,r.[
        & cos `| ].-r,r.[ = (-sin) | ].-r,r.[
        & dom(sin | ].-r,r.[) = ].-r,r.[
        & dom(cos | ].-r,r.[) = ].-r,r.[
proof
  let r be Real;
  A1: sin is_differentiable_on ].-r,r.[ by FDIFF_1:34,SIN_COS:73;
  then A2: dom (sin `| ].-r,r.[) = ].-r,r.[ by FDIFF_1:def 8;
  A3: cos is_differentiable_on ].-r,r.[ by FDIFF_1:34,SIN_COS:72;
  A4: dom ((-sin) | ].-r,r.[) = dom(-sin) /\ ].-r,r.[ by FUNCT_1:68
                              .= dom((-1)(#)sin) /\ ].-r,r.[
                                     by RFUNCT_1:38
                              .= dom(sin) /\ ].-r,r.[ by SEQ_1:def 6
                              .= ].-r,r.[ by XBOOLE_1:28,SIN_COS:27;
  A5: dom (sin | ].-r,r.[) = dom(sin) /\ ].-r,r.[ by FUNCT_1:68
                           .= ].-r,r.[ by XBOOLE_1:28,SIN_COS:27;
  A6: dom (cos | ].-r,r.[) = dom(cos) /\ ].-r,r.[ by FUNCT_1:68
                           .= ].-r,r.[ by XBOOLE_1:28,SIN_COS:27;
  A7: for x be Real st x in ].-r,r.[ holds
         (sin `| ].-r,r.[).x = (cos | ].-r,r.[).x
  proof
    let x be Real such that
    A8: x in ].-r,r.[;
     (sin `| ].-r,r.[).x=diff(sin,x) by FDIFF_1:def 8,A1,A8
                       .=cos.x by SIN_COS:69
                       .=(cos | ].-r,r.[).x by FUNCT_1:70,A8,A6;
    hence thesis;
  end;
  A9: dom (cos `| ].-r,r.[) = ].-r,r.[ by FDIFF_1:def 8,A3;
   for x be Real st x in ].-r,r.[ holds
    (cos `| ].-r,r.[).x = ((-sin) | ].-r,r.[).x
  proof
    let x be Real such that
A10: x in ].-r,r.[;
     x in dom (sin) by SIN_COS:27;
then A11: x in dom ((-1)(#)sin ) by SEQ_1:def 6;
     (cos `| ].-r,r.[).x=diff(cos,x) by FDIFF_1:def 8,A3,A10
                       .=-sin.x by SIN_COS:68
                       .=(-1)*sin.x
                       .= ((-1)(#)sin ).x by SEQ_1:def 6,A11
                       .=(-sin).x by RFUNCT_1:38
                       .=((-sin) | ].-r,r.[).x by FUNCT_1:70,A10,A4;
    hence thesis;
  end;
  hence thesis by A2,A4,A5,A6,A7,A9,PARTFUN1:34;
end;

theorem
   for f be PartFunc of REAL,REAL,
      Z be Subset of REAL st f is_differentiable_on Z holds
    (-f) `| Z = -f `| Z
proof
  let f be PartFunc of REAL,REAL,
      Z be Subset of REAL such that
A1: f is_differentiable_on Z;
A2: Z is open Subset of REAL by A1,FDIFF_1:17;
   (-f) `| Z = ( (-1)(#) f) `| Z by RFUNCT_1:38
            .= (-1)(#) (f `| Z) by FDIFF_2:19,A1,A2
            .= -f `| Z by RFUNCT_1:38;
  hence thesis;
end;

theorem Th19:
  for r be Real, n be Element of NAT holds
    diff(sin,].-r,r.[).(2*n) = (-1) |^ n (#) sin | ].-r,r.[ &
    diff(sin,].-r,r.[).(2*n+1) = (-1) |^ n (#) cos | ].-r,r.[ &
    diff(cos,].-r,r.[).(2*n) = (-1) |^ n (#)cos | ].-r,r.[ &
    diff(cos,].-r,r.[).(2*n+1) = (-1) |^ (n+1) (#)sin | ].-r,r.[
proof
  let r be Real, n be Element of NAT;
A1: sin is_differentiable_on ].-r,r.[ by FDIFF_1:34,SIN_COS:73;
A2: cos is_differentiable_on ].-r,r.[ by FDIFF_1:34,SIN_COS:72;
    defpred P[Element of NAT] means
      diff(sin,].-r,r.[).(2*$1) = (-1) |^ $1 (#) sin | ].-r,r.[ &
      diff(sin,].-r,r.[).(2*$1+1) = (-1) |^ $1 (#) cos | ].-r,r.[ &
      diff(cos,].-r,r.[).(2*$1) = (-1) |^ $1 (#) cos | ].-r,r.[ &
      diff(cos,].-r,r.[).(2*$1+1) = (-1) |^ ($1+1) (#) sin | ].-r,r.[;
A3: P[0]
    proof
      A4: diff(sin,].-r,r.[).(2*0) = sin | ].-r,r.[ by TAYLOR_1:def 5
                                  .= 1 (#) (sin | ].-r,r.[)
                                           by RFUNCT_1:33
                                  .= (-1) |^ 0 (#) (sin | ].-r,r.[)
                                           by NEWTON:9;
      A5: diff(sin,].-r,r.[).(2*0+1) = diff(sin,].-r,r.[).0 `| ].-r,r.[
                                               by TAYLOR_1:def 5
                               .= sin | ].-r,r.[ `| ].-r,r.[
                                               by TAYLOR_1:def 5
                               .= sin `| ].-r,r.[ by FDIFF_2:16,A1
                               .= cos | ].-r,r.[ by Th17
                               .= 1 (#) (cos | ].-r,r.[) by RFUNCT_1:33
                               .= (-1) |^ 0 (#) (cos | ].-r,r.[)
                                               by NEWTON:9;
      A6: diff(cos,].-r,r.[).(2*0) = cos | ].-r,r.[ by TAYLOR_1:def 5
                .= 1 (#) (cos | ].-r,r.[) by RFUNCT_1:33
                .= (-1) |^ 0 (#) (cos | ].-r,r.[) by NEWTON:9;
       diff(cos,].-r,r.[).(2*0+1) = diff(cos,].-r,r.[).0 `| ].-r,r.[
                                               by TAYLOR_1:def 5
                .= cos | ].-r,r.[ `| ].-r,r.[ by TAYLOR_1:def 5
                .= cos `| ].-r,r.[ by FDIFF_2:16,A2
                .= (-sin) | ].-r,r.[ by Th17
                .= 1 (#) (-sin) | ].-r,r.[ by RFUNCT_1:33
                .= (-1) |^ 0 (#) (-sin) | ].-r,r.[ by NEWTON:9
                .= ((-1) |^ 0) (#) ((-1) (#) sin) | ].-r,r.[ by RFUNCT_1:38
                .= ((-1) |^ 0) (#) ((-1) (#) sin | ].-r,r.[) by RFUNCT_1:65
                .= (((-1) |^ 0) * (-1)) (#) sin | ].-r,r.[ by RFUNCT_1:29
                .= (-1) |^ (0+1) (#) sin | ].-r,r.[ by NEWTON:11;
      hence thesis by A4,A5,A6;
    end;
    A7: for k be Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT such that
      A8: P[k];
      A9: (sin | ].-r,r.[) is_differentiable_on ].-r,r.[ &
           (cos | ].-r,r.[) is_differentiable_on ].-r,r.[
                                by FDIFF_2:16,A1,A2;
      A10: diff(sin,].-r,r.[).(2*(k+1))
             = diff(sin,].-r,r.[).((2*k+1)+1)
            .= diff(sin,].-r,r.[).(2*k+1) `| ].-r,r.[ by TAYLOR_1:def 5
            .= ((-1) |^ k) (#) ((cos | ].-r,r.[) `| ].-r,r.[)
                                by FDIFF_2:19,A9,A8
            .= ((-1) |^ k) (#) (cos `| ].-r,r.[) by FDIFF_2:16,A2
            .= ((-1) |^ k) (#) ((-sin) | ].-r,r.[) by Th17
            .= ((-1) |^ k) (#) (((-1) (#) sin) | ].-r,r.[) by RFUNCT_1:38
            .= ((-1) |^ k) (#) ((-1) (#) sin | ].-r,r.[) by RFUNCT_1:65
            .= (((-1) |^ k) * (-1)) (#) (sin | ].-r,r.[) by RFUNCT_1:29
            .= (-1) |^ (k+1) (#) sin | ].-r,r.[ by NEWTON:11;
      A11: diff(sin,].-r,r.[).(2*(k+1)+1)
                  = diff(sin,].-r,r.[).(2*(k+1)) `| ].-r,r.[
                                by TAYLOR_1:def 5
                  .= ((-1) |^ (k+1)) (#) ((sin | ].-r,r.[) `| ].-r,r.[)
                                by FDIFF_2:19,A9,A10
                  .= ((-1) |^ (k+1)) (#) (sin `| ].-r,r.[) by FDIFF_2:16,A1
                  .= (-1) |^ (k+1) (#) cos | ].-r,r.[ by Th17;
      A12: diff(cos,].-r,r.[).(2*(k+1))
                   = diff(cos,].-r,r.[).((2*k+1)+1)
                  .= diff(cos,].-r,r.[).(2*k+1) `| ].-r,r.[
                                by TAYLOR_1:def 5
                  .= ((-1) |^ (k+1)) (#) ((sin | ].-r,r.[) `| ].-r,r.[)
                                by FDIFF_2:19,A9,A8
                  .= (-1) |^ (k+1) (#) (sin `| ].-r,r.[) by FDIFF_2:16,A1
                  .= (-1) |^ (k+1) (#) cos | ].-r,r.[ by Th17;
       diff(cos,].-r,r.[).(2*(k+1)+1)
                   = diff(cos,].-r,r.[).(2*(k+1)) `| ].-r,r.[
                                by TAYLOR_1:def 5
                  .= ((-1) |^ (k+1)) (#) ((cos | ].-r,r.[) `| ].-r,r.[)
                                by FDIFF_2:19,A9,A12
                  .= ((-1) |^ (k+1)) (#) (cos `| ].-r,r.[)
                                by FDIFF_2:16,A2
                  .= ((-1) |^ (k+1)) (#) ((-sin) | ].-r,r.[) by Th17
                  .= ((-1) |^ (k+1)) (#) ((-1) (#) sin) | ].-r,r.[
                                by RFUNCT_1:38
                  .= ((-1) |^ (k+1)) (#) ((-1) (#) sin | ].-r,r.[)
                                by RFUNCT_1:65
                  .= (((-1) |^ (k+1)) * (-1)) (#) sin | ].-r,r.[
                                by RFUNCT_1:29
                  .= (-1) |^ ((k+1)+1) (#) sin | ].-r,r.[ by NEWTON:11;
      hence thesis by A10,A11,A12;
    end;
     for n be Element of NAT holds P[n] from NAT_1:sch 1(A3,A7);
    hence thesis;
end;

theorem Th20:
  for n be Element of NAT, r,x be Real st r > 0 holds
  Maclaurin(sin, ].-r,r.[,x).(2*n) = 0 &
  Maclaurin(sin, ].-r,r.[,x).(2*n+1) = (-1) |^ n * x |^ (2*n+1) / ((2*n+1)!) &
  Maclaurin(cos, ].-r,r.[,x).(2*n) = (-1) |^ n * x |^ (2*n) / ((2*n)!) &
  Maclaurin(cos, ].-r,r.[,x).(2*n+1) = 0
proof
  let n be Element of NAT, r,x be Real such that
  A1: r > 0;
   abs(0-0)=0 by ABSVALUE:7;
  then A2: 0 in ].0-r,0+r.[ by A1,RCOMP_1:8;
  A3: dom(((-1) |^ n) (#) (sin | ].-r,r.[))
        = dom(sin | ].-r,r.[) by SEQ_1:def 6
       .= ].-r,r.[ by Th17;
  A4: 0 in dom(sin | ].-r,r.[) by A2,Th17;
  A5: dom(((-1) |^ n) (#) (cos | ].-r,r.[))
        = dom(cos | ].-r,r.[) by SEQ_1:def 6
       .= ].-r,r.[ by Th17;
  A6: 0 in dom(cos | ].-r,r.[) by A2,Th17;
  A7: dom(((-1) |^ (n+1)) (#) (sin | ].-r,r.[))
        = dom(sin | ].-r,r.[) by SEQ_1:def 6
       .= ].-r,r.[ by Th17;
  A8: Maclaurin(sin, ].-r,r.[,x).(2*n)
         = (diff(sin,].-r,r.[).(2*n)).0 * (x-0) |^ (2*n) / ((2*n)!)
                     by TAYLOR_1:def 7
        .= ((-1) |^ n (#) sin | ].-r,r.[).0 * x |^ (2*n) / ((2*n)!) by Th19
        .= (((-1) |^ n) * (sin | ].-r,r.[).0) * x |^ (2*n) / ((2*n)!)
                     by SEQ_1:def 6,A2,A3
        .= ((-1) |^ n) * sin.0 * x |^ (2*n) / ((2*n)!) by FUNCT_1:70,A4
        .= 0 by SIN_COS:33;
  A9: Maclaurin(sin, ].-r,r.[,x).(2*n+1)
         = (diff(sin,].-r,r.[).(2*n+1)).0 * (x-0) |^ (2*n+1) / ((2*n+1)!)
                     by TAYLOR_1:def 7
         .= ((-1) |^ n (#) cos | ].-r,r.[).0 * x |^ (2*n+1) / ((2*n+1)!)
                     by Th19
         .= (((-1) |^ n) * (cos | ].-r,r.[).0) * x |^ (2*n+1) / ((2*n+1)!)
                     by SEQ_1:def 6,A5,A2
         .= ((-1) |^ n) * cos.0 * x |^ (2*n+1) / ((2*n+1)!) by FUNCT_1:70,A6
         .= (-1) |^ n * x |^ (2*n+1) / ((2*n+1)!) by SIN_COS:33;
  A10: Maclaurin(cos, ].-r,r.[,x).(2*n)
         = (diff(cos,].-r,r.[).(2*n)).0 * (x-0) |^ (2*n) / ((2*n)!)
                     by TAYLOR_1:def 7
         .= ((-1) |^ n (#) cos | ].-r,r.[).0 * x |^ (2*n) / ((2*n)!) by Th19
         .= (((-1) |^ n) * (cos | ].-r,r.[).0) * x |^ (2*n) / ((2*n)!)
                     by SEQ_1:def 6,A5,A2
         .= ((-1) |^ n) * cos.0 * x |^ (2*n) / ((2*n)!) by FUNCT_1:70,A6
         .= (-1) |^ n * x |^ (2*n) / ((2*n)!) by SIN_COS:33;
   Maclaurin(cos, ].-r,r.[,x).(2*n+1)
      = (diff(cos,].-r,r.[).(2*n+1)).0 * (x-0) |^ (2*n+1) / ((2*n+1)!)
                     by TAYLOR_1:def 7
      .= ((-1) |^ (n+1) (#) sin | ].-r,r.[).0 * x |^ (2*n+1) / ((2*n+1)!)
                     by Th19
      .= (((-1) |^ (n+1)) * (sin | ].-r,r.[).0) * x |^ (2*n+1) / ((2*n+1)!)
                     by SEQ_1:def 6,A7,A2
      .= ((-1) |^ (n+1)) * sin.0 * x |^ (2*n+1) / ((2*n+1)!) by FUNCT_1:70,A4
      .= 0 by SIN_COS:33;
  hence thesis by A8,A9,A10;
end;

theorem Th21:
  for r be Real, n be Element of NAT holds
    sin is_differentiable_on n, ].-r,r.[ &
    cos is_differentiable_on n, ].-r,r.[
proof
  let r be Real, n be Element of NAT;
   now let i be Element of NAT such that i <= n-1;
    A1: now per cases;
      suppose i is even;
        then consider j be Element of NAT such that
        A2: i=2*j by ABIAN:def 2;
        thus dom(diff(sin,].-r,r.[).i)
                    = dom((-1) |^ j (#) sin | ].-r,r.[) by Th19,A2
                   .= dom((sin | ].-r,r.[)) by SEQ_1:def 6
                   .= ].-r,r.[ by Th17;
      end;
      suppose i is odd;
        then consider j be Element of NAT such that
        A3: i=2*j+1 by ABIAN:9;
        thus dom(diff(sin,].-r,r.[).i)
                    = dom((-1) |^ j (#) cos | ].-r,r.[) by Th19,A3
                   .= dom((cos | ].-r,r.[)) by SEQ_1:def 6
                   .= ].-r,r.[ by Th17;
      end;
    end;
     for x be Real st x in ].-r,r.[ holds
      diff(sin,].-r,r.[).i | ].-r,r.[ is_differentiable_in x
    proof
      let x be Real such that
      A4: x in ].-r,r.[;
       now per cases;
        suppose i is even;
          then consider j be Element of NAT such that
          A5: i = 2*j by ABIAN:def 2;
          A6: diff(sin,].-r,r.[).i
                  = (-1) |^ j (#) sin | ].-r,r.[ by Th19,A5;
           sin is_differentiable_on ].-r,r.[ by FDIFF_1:34,SIN_COS:73;
          then (sin | ].-r,r.[) is_differentiable_in x by FDIFF_1:def 7,A4;
          hence diff(sin,].-r,r.[).i is_differentiable_in x by A6,FDIFF_1:23;
        end;
        suppose i is odd;
          then consider j be Element of NAT such that
          A7: i = 2*j+1 by ABIAN:9;
          A8: diff(sin,].-r,r.[).i
                  = (-1) |^ j (#) cos | ].-r,r.[ by Th19,A7;
           cos is_differentiable_on ].-r,r.[ by FDIFF_1:34,SIN_COS:72;
          then (cos | ].-r,r.[) is_differentiable_in x by FDIFF_1:def 7,A4;
          hence diff(sin,].-r,r.[).i is_differentiable_in x
                             by A8,FDIFF_1:23;
        end;
      end;
      hence thesis by RELAT_1:97,A1;
    end;
    hence diff(sin,].-r,r.[).i is_differentiable_on ].-r,r.[
                        by A1,FDIFF_1:def 7;
  end;
  hence sin is_differentiable_on n, ].-r,r.[ by TAYLOR_1:def 6;
   now let i be Element of NAT such that i <= n-1;
    A9: now per cases;
      suppose i is even;
        then consider j be Element of NAT such that
        A10: i = 2*j by ABIAN:def 2;
        thus dom(diff(cos,].-r,r.[).i)
                    = dom((-1) |^ j (#) cos | ].-r,r.[) by Th19,A10
                   .= dom((cos | ].-r,r.[)) by SEQ_1:def 6
                   .= ].-r,r.[ by Th17;
      end;
      suppose i is odd;
        then consider j be Element of NAT such that
        A11: i = 2*j+1 by ABIAN:9;
        thus dom(diff(cos,].-r,r.[).i)
                    = dom((-1) |^ (j+1) (#) sin | ].-r,r.[) by Th19,A11
                   .= dom((sin | ].-r,r.[)) by SEQ_1:def 6
                   .= ].-r,r.[ by Th17;
      end;
    end;
     for x be Real st x in ].-r,r.[ holds
           diff(cos,].-r,r.[).i | ].-r,r.[ is_differentiable_in x
    proof
      let x be Real such that
      A12: x in ].-r,r.[;
       now per cases;
        suppose i is even;
          then consider j be Element of NAT such that
          A13: i = 2*j by ABIAN:def 2;
          A14: diff(cos,].-r,r.[).i
                   = (-1) |^ j (#) cos | ].-r,r.[ by Th19,A13;
           cos is_differentiable_on ].-r,r.[
                             by FDIFF_1:34,SIN_COS:72;
          then (cos | ].-r,r.[) is_differentiable_in x by FDIFF_1:def 7,A12;
          hence diff(cos,].-r,r.[).i is_differentiable_in x by A14,FDIFF_1:23;
        end;
        suppose i is odd;
          then consider j be Element of NAT such that
          A15: i = 2*j+1 by ABIAN:9;
          A16: diff(cos,].-r,r.[).i
                  = (-1) |^ (j+1) (#) sin | ].-r,r.[ by Th19,A15;
           sin is_differentiable_on ].-r,r.[
                             by FDIFF_1:34,SIN_COS:73;
          then (sin | ].-r,r.[) is_differentiable_in x by FDIFF_1:def 7,A12;
          hence diff(cos,].-r,r.[).i is_differentiable_in x
                             by A16,FDIFF_1:23;
        end;
      end;
      hence thesis by RELAT_1:97,A9;
    end;
    hence diff(cos,].-r,r.[).i is_differentiable_on ].-r,r.[
                        by A9,FDIFF_1:def 7;
  end;
  hence thesis by TAYLOR_1:def 6;
end;

theorem Th22:
  for r be Real st r > 0
   ex r1,r2 be Real st r1 >= 0 & r2 >= 0 &
  for n be Element of NAT holds
    for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
        abs((diff(sin,].-r,r.[).n).(s*x) * x |^ n / (n!))
                <= r1 * r2 |^ n / (n!) &
        abs((diff(cos,].-r,r.[).n).(s*x) * x |^ n / (n!))
                <= r1 * r2 |^ n / (n!)
proof
  let r be Real such that
  A1: r > 0;
  take r1 = 1;
  take r2 = r;
   now let n be Element of NAT;
    let x,s be Real such that
      A2: x in ].-r,r.[ & s > 0 & s < 1;
       x in ].0-r,0+r.[ by A2;
      then A3: abs(x-0) < r by RCOMP_1:8;
       abs x >= 0 by COMPLEX1:132;
      then s * abs(x) < 1 * r by A2,A3,XREAL_1:99;
      then abs(s) * abs(x) < r by ABSVALUE:def 1,A2;
      then abs(s*x - 0) < r by COMPLEX1:151;
      then A4: (s*x) in ].0-r,0+r.[ by RCOMP_1:8;
      A5: n! > 0 by NEWTON:23;
      A6: abs(x |^ n / (n!)) = abs(x |^ n) / abs(n!) by COMPLEX1:153
                             .= abs(x |^ n) / (n!) by A5,ABSVALUE:def 1
                             .= abs(x) |^ n / (n!) by Th1;
      A7: abs((diff(sin,].-r,r.[).n).(s*x) * x |^ n / (n!))
                = abs((diff(sin,].-r,r.[).n).(s*x) * (x |^ n / (n!)))
                              by XCMPLX_1:75
               .= abs((diff(sin,].-r,r.[).n).(s*x)) * abs(x |^ n / (n!))
                              by COMPLEX1:151
               .= abs((diff(sin,].-r,r.[).n).(s*x)) * abs(x) |^ n / (n!)
                     by XCMPLX_1:75,A6;
      A8: abs((diff(cos,].-r,r.[).n).(s*x) * x |^ n / (n!))
                = abs((diff(cos,].-r,r.[).n).(s*x) * (x |^ n / (n!)))
                              by XCMPLX_1:75
               .= abs((diff(cos,].-r,r.[).n).(s*x)) * abs(x |^ n / (n!))
                              by COMPLEX1:151
               .= abs((diff(cos,].-r,r.[).n).(s*x)) * abs(x) |^ n / (n!)
                     by XCMPLX_1:75,A6;
      A9: abs((diff(sin,].-r,r.[).n).(s*x)) >= 0 by COMPLEX1:132;
      A10: abs((diff(cos,].-r,r.[).n).(s*x)) >= 0 by COMPLEX1:132;
      A11: abs(x) |^ n / (n!) >= 0 by A6,COMPLEX1:132;
      A12: for k be Element of NAT holds abs((-1) |^ k) = 1
      proof
        let k be Element of NAT;
        per cases;
          suppose k is even;
            then consider j be Element of NAT such that
            A13: k = 2*j by ABIAN:def 2;
            thus abs((-1) |^ k) = abs(((-1) |^ (1+1)) |^ j) by NEWTON:14,A13
                               .= abs(((-1) |^ 1 * (-1)) |^ j) by NEWTON:11
                               .= abs(((-1) * (-1)) |^ j) by NEWTON:10
                               .= abs(1) by NEWTON:15
                               .= 1 by ABSVALUE:def 1;
          end;
          suppose k is odd;
            then consider j be Element of NAT such that
            A14: k = 2*j+1 by ABIAN:9;
            thus abs((-1) |^ k) = abs((-1) |^ (2*j) * (-1))
                                               by NEWTON:11,A14
                   .= abs(((-1) |^ (1+1)) |^ j * (-1)) by NEWTON:14
                   .= abs(((-1) |^ 1 * (-1)) |^ j * (-1)) by NEWTON:11
                   .= abs(((-1) * (-1)) |^ j * (-1)) by NEWTON:10
                   .= abs(1 * (-1)) by NEWTON:15
                   .= abs 1 by COMPLEX1:138
                   .= 1 by ABSVALUE:def 1;
          end;
      end;
      A15: abs((diff(sin,].-r,r.[).n).(s*x)) <= r1
      proof
        per cases;
          suppose n is even;
            then consider k be Element of NAT such that
            A16: n = 2*k by ABIAN:def 2;
            A17: dom(((-1) |^ k) (#) (sin | ].-r,r.[))
                   = dom(sin | ].-r,r.[) by SEQ_1:def 6
                   .= ].-r,r.[ by Th17;
            A18: (s*x) in dom(sin | ].-r,r.[) by A4,Th17;
            A19: abs((diff(sin,].-r,r.[).n).(s*x))
                    = abs(((-1) |^ k (#) sin | ].-r,r.[).(s*x)) by Th19,A16
                    .= abs(((-1) |^ k) * ((sin | ].-r,r.[).(s*x)))
                        by SEQ_1:def 6,A17,A4
                    .= abs((-1) |^ k) * abs((sin | ].-r,r.[).(s*x))
                        by COMPLEX1:151
                    .= 1 * abs((sin | ].-r,r.[).(s*x)) by A12
                    .= abs(sin.(s*x)) by FUNCT_1:70,A18;
             abs(sin (s*x)) <= 1 by SIN_COS:30;
            hence thesis by A19,SIN_COS:def 21;
          end;
          suppose n is odd;
            then consider k be Element of NAT such that
            A20: n = 2*k+1 by ABIAN:9;
            A21: dom(((-1) |^ k) (#) (cos | ].-r,r.[))
                   = dom(cos | ].-r,r.[) by SEQ_1:def 6
                   .= ].-r,r.[ by Th17;
            A22: (s*x) in dom(cos | ].-r,r.[) by A4,Th17;
            A23: abs((diff(sin,].-r,r.[).n).(s*x))
                     = abs(((-1) |^ k (#) cos | ].-r,r.[).(s*x)) by Th19,A20
                    .= abs(((-1) |^ k) * ((cos | ].-r,r.[).(s*x)))
                        by SEQ_1:def 6,A21,A4
                    .= abs((-1) |^ k) * abs((cos | ].-r,r.[).(s*x))
                        by COMPLEX1:151
                    .= 1 * abs((cos | ].-r,r.[).(s*x)) by A12
                    .= abs(cos.(s*x)) by FUNCT_1:70,A22;
             abs(cos (s*x)) <= 1 by SIN_COS:30;
            hence thesis by A23,SIN_COS:def 23;
          end;
      end;
      A24: abs((diff(cos,].-r,r.[).n).(s*x)) <= r1
      proof
        per cases;
          suppose n is even;
            then consider k be Element of NAT such that
            A25: n = 2*k by ABIAN:def 2;
            A26: dom(((-1) |^ k) (#) (cos | ].-r,r.[))
                   = dom(cos | ].-r,r.[) by SEQ_1:def 6
                  .= ].-r,r.[ by Th17;
            A27: (s*x) in dom(cos | ].-r,r.[) by A4,Th17;
            A28: abs((diff(cos,].-r,r.[).n).(s*x))
                     = abs(((-1) |^ k (#) cos | ].-r,r.[).(s*x)) by Th19,A25
                    .= abs(((-1) |^ k) * ((cos | ].-r,r.[).(s*x)))
                        by SEQ_1:def 6,A26,A4
                    .= abs((-1) |^ k) * abs((cos | ].-r,r.[).(s*x))
                        by COMPLEX1:151
                    .= 1 * abs((cos | ].-r,r.[).(s*x)) by A12
                    .= abs(cos.(s*x)) by FUNCT_1:70,A27;
             abs(cos (s*x)) <= 1 by SIN_COS:30;
            hence thesis by A28,SIN_COS:def 23;
          end;
          suppose n is odd;
            then consider k be Element of NAT such that
            A29: n = 2*k+1 by ABIAN:9;
            A30: dom(((-1) |^ (k+1)) (#) (sin | ].-r,r.[))
                   = dom(sin | ].-r,r.[) by SEQ_1:def 6
                   .= ].-r,r.[ by Th17;
            A31: (s*x) in dom(sin | ].-r,r.[) by A4,Th17;
            A32: abs((diff(cos,].-r,r.[).n).(s*x))
                     = abs(((-1) |^ (k+1) (#) sin | ].-r,r.[).(s*x))
                        by Th19,A29
                    .= abs(((-1) |^ (k+1)) * ((sin | ].-r,r.[).(s*x)))
                        by SEQ_1:def 6,A30,A4
                    .= abs((-1) |^ (k+1)) * abs((sin | ].-r,r.[).(s*x))
                        by COMPLEX1:151
                    .= 1 * abs((sin | ].-r,r.[).(s*x)) by A12
                    .= abs(sin.(s*x)) by FUNCT_1:70,A31;
             abs(sin (s*x)) <= 1 by SIN_COS:30;
            hence thesis by A32,SIN_COS:def 21;
          end;
      end;
  abs(x) |^ n / (n!) <= r2 |^ n / (n!)
      proof
        defpred P[Element of NAT] means
         abs(x) |^ $1 / ($1!) <= r2 |^ $1 / ($1!);
   A33: P[0]
        proof
           abs(x) |^ 0 / (0!) = 1 / (0!) by NEWTON:9
                            .= r2 |^ 0 / (0!) by NEWTON:9;
          hence thesis;
        end;
   A34: for k be Element of NAT st P[k] holds P[k+1]
        proof
          let k be Element of NAT such that
     A35: P[k];
     A36: abs(x) |^ (k+1) / ((k+1)!)
                 = abs(x) |^ k * abs(x) / ((k+1)!) by NEWTON:11
                 .= abs(x) |^ k * abs(x) / (k! * (k+1)) by NEWTON:21
                 .= abs(x) |^ k * (abs(x) / (k! * (k+1))) by XCMPLX_1:75
                 .= abs(x) |^ k * (abs(x) / (k!) / (k+1)) by XCMPLX_1:79
                 .= abs(x) |^ k * (1 / ((k!) / abs(x)) / (k+1))
                        by XCMPLX_1:57
                 .= abs(x) |^ k * (1 / (k!) * abs(x) / (k+1))
                        by XCMPLX_1:83
                 .= (abs(x) |^ k * (1 / (k!) * abs(x))) / (k+1)
                        by XCMPLX_1:75
                 .= (abs(x) |^ k * (1 / (k!))) * abs(x) / (k+1)
                 .= (abs(x) |^ k * 1) / (k!) * abs(x) / (k+1)
                        by XCMPLX_1:75
                 .= abs(x) |^ k / (k!) * abs(x) / (k+1);
          A37: r2 |^ (k+1) / ((k+1)!)
                 = r2 |^ k * r2 / ((k+1)!) by NEWTON:11
                 .= r2 |^ k * r2 / (k! * (k+1)) by NEWTON:21
                 .= r2 |^ k * (r2 / (k! * (k+1))) by XCMPLX_1:75
                 .= r2 |^ k * (r2 / (k!) / (k+1)) by XCMPLX_1:79
                 .= r2 |^ k * (1 / ((k!) / r2) / (k+1)) by XCMPLX_1:57
                 .= r2 |^ k * (1 / (k!) * r2 / (k+1)) by XCMPLX_1:83
                 .= (r2 |^ k * (1 / (k!) * r2)) / (k+1) by XCMPLX_1:75
                 .= (r2 |^ k * (1 / (k!))) * r2 / (k+1)
                 .= (r2 |^ k * 1) / (k!) * r2 / (k+1) by XCMPLX_1:75
                 .= r2 |^ k / (k!) * r2 / (k+1);
          A38: k! > 0 by NEWTON:23;
           abs(x) |^ k / (k!) = abs(x |^ k) / (k!) by Th1
                             .= abs(x |^ k) / abs(k!) by A38,ABSVALUE:def 1
                             .= abs(x |^ k / (k!)) by COMPLEX1:153;
          then A39: abs(x) |^ k / (k!) >= 0 by COMPLEX1:132;
           abs x >= 0 by COMPLEX1:132;
          then abs(x) |^ k / (k!) * abs(x) <= r2 |^ k / (k!) * r2
                          by A35,A39,A3,XREAL_1:68;
          hence thesis by A36,A37,XREAL_1:74;
        end;
         for i be Element of NAT holds P[i] from NAT_1:sch 1(A33,A34);
        hence thesis;
      end;
      then abs((diff(sin,].-r,r.[).n).(s*x)) * (abs(x) |^ n / (n!))
          <= r1*(r2 |^ n / (n!)) &
      abs((diff(cos,].-r,r.[).n).(s*x)) * (abs(x) |^ n / (n!))
          <= r1*(r2 |^ n / (n!)) by XREAL_1:68,A9,A10,A11,A15,A24;
      hence abs((diff(sin,].-r,r.[).n).(s*x) * x |^ n / (n!))
          <= r1*r2 |^ n / (n!) &
            abs((diff(cos,].-r,r.[).n).(s*x) * x |^ n / (n!))
          <= r1*r2 |^ n / (n!) by A7,A8,XCMPLX_1:75;
  end;
  hence thesis by A1;
end;

theorem Th23:
  for r, e be Real st 0 < r & 0 < e holds
    ex n be Element of NAT st for m be Element of NAT st n <= m holds
       for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
       abs((diff(sin,].-r,r.[).m).(s*x) * x |^ m / (m!)) < e &
       abs((diff(cos,].-r,r.[).m).(s*x) * x |^ m / (m!)) < e
proof
  let r, e be Real such that
  A1: r > 0 and
  A2: e > 0;
  consider r1,r2 be Real such that
  A3: r1 >= 0 & r2 >= 0 &
       for n be Element of NAT holds
          for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
             abs((diff(sin,].-r,r.[).n).(s*x) * x |^ n / (n!))
                     <= r1*r2 |^ n / (n!) &
             abs((diff(cos,].-r,r.[).n).(s*x) * x |^ n / (n!))
                     <= r1*r2 |^ n / (n!) by Th22,A1;
  consider n be Element of NAT such that
  A4: for m be Element of NAT st n <= m holds
     (r1 * r2 |^ m / (m!)) < e by Th12,A2,A3;
  take n;
  let m be Element of NAT such that
A5: n <= m;
    let x,s be Real such that
A6: x in ].-r,r.[ & 0 < s & s < 1;
    A7: abs((diff(sin,].-r,r.[).m).(s*x) * x |^ m /(m!))
                <= r1*r2 |^ m /(m!) &
        abs((diff(cos,].-r,r.[).m).(s*x) * x |^ m /(m!))
                <= r1*r2 |^ m /(m!) by A3,A6;
     (r1*r2 |^ m /(m!)) < e by A4,A5;
    hence abs((diff(sin,].-r,r.[).m).(s*x) * x |^ m /(m!)) < e &
      abs((diff(cos,].-r,r.[).m).(s*x) * x |^ m /(m!)) < e by XXREAL_0:2,A7;
end;

theorem
   for r, e be Real st 0 < r & 0 < e holds
   ex n be Element of NAT st for m be Element of NAT st n <= m holds
      for x being real number st x in ].-r,r.[ holds
        abs(sin.x-Partial_Sums(Maclaurin(sin,].-r,r.[,x)).m) < e &
        abs(cos.x-Partial_Sums(Maclaurin(cos,].-r,r.[,x)).m) < e
proof
  let r, e be Real such that
  A1: r > 0 and
  A2: e > 0;
    consider n be Element of NAT such that
A3: for m be Element of NAT st n <= m holds
      for x,s be Real st x in ].-r,r.[ & 0 < s & s < 1 holds
      abs((diff(sin,].-r,r.[).m).(s*x) * x |^ m /(m!) )< e &
      abs((diff(cos,].-r,r.[).m).(s*x) * x |^ m /(m!))< e by Th23,A1,A2;
    take n;
    let m be Element of NAT such that
A4:n <= m;
A5: sin is_differentiable_on m+1, ].-r,r.[ &
        cos is_differentiable_on m+1, ].-r,r.[ by Th21;
     now let x be Real such that
      A6: x in ].-r,r.[;
      consider s be Real such that
      A7: 0 < s & s < 1 and
      A8: abs(sin.x-Partial_Sums(Maclaurin(sin,].-r,r.[,x)).m)
               =abs((diff(sin,].-r,r.[).(m+1)).(s*x)
               * x |^ (m+1) / ((m+1)!)) by Th4,A5,A1,A6;
       m <= m+1 by NAT_1:11;
  then A9: n <= m+1 by XXREAL_0:2,A4;
      hence abs(sin.x-Partial_Sums(Maclaurin(sin,].-r,r.[,x)).m) < e
                            by A3,A6,A7,A8;
      consider s be Real such that
A10: 0 < s & s < 1 and
A11: abs(cos.x-Partial_Sums(Maclaurin(cos,].-r,r.[,x)).m)
               =abs((diff(cos,].-r,r.[).(m+1)).(s*x)
               * x |^ (m+1) / ((m+1)!)) by Th4,A5,A1,A6;
      thus abs(cos.x-Partial_Sums(Maclaurin(cos,].-r,r.[,x)).m) < e
                            by A9,A3,A6,A10,A11;
    end;
  hence thesis;
end;

theorem Th25:
  for r, x be Real, m be Element of NAT st 0 < r holds
    Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*m+1)
       = Partial_Sums(x P_sin).m &
    Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*m+1)
       = Partial_Sums(x P_cos).m
proof
  let r, x be Real, m be Element of NAT such that
  A1: r > 0;
  thus Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*m+1)
                 = Partial_Sums(x P_sin).m
  proof
    defpred P[Element of NAT] means
       Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*$1+1)
          = Partial_Sums(x P_sin).$1;
    A2: P[0]
      proof
       Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*0+1)
           = Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*0)
                 + Maclaurin(sin,].-r,r.[,x).((2*0)+1) by SERIES_1:def 1
           .= Maclaurin(sin,].-r,r.[,x).(2*0)
                 + Maclaurin(sin,].-r,r.[,x).(2*0+1) by SERIES_1:def 1
           .= 0 + Maclaurin(sin,].-r,r.[,x).(2*0+1) by Th20,A1
           .= (-1) |^ 0 * x |^ (2*0+1) / ((2*0+1)!) by Th20,A1
           .= (x P_sin).0 by SIN_COS:def 24
           .= Partial_Sums(x P_sin).0 by SERIES_1:def 1;
      hence thesis;
    end;
A3: for k be Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT such that
      A4: P[k];
       Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*(k+1)+1)
             = Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*k+2)
                 + Maclaurin(sin,].-r,r.[,x).(2*k+3) by SERIES_1:def 1
            .= Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*k+2)
                 + (-1) |^ (k+1) * x |^ (2*(k+1)+1) / ((2*(k+1)+1)!)
                      by Th20,A1
            .= Partial_Sums(Maclaurin(sin,].-r,r.[,x)).((2*k+1)+1)
                 + (x P_sin).(k+1) by SIN_COS:def 24
            .= Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*k+1)
                 + Maclaurin(sin,].-r,r.[,x).(2*(k+1))
                    + (x P_sin).(k+1) by SERIES_1:def 1
            .= Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*k+1)
                 + 0 + (x P_sin).(k+1) by Th20,A1
            .= Partial_Sums(x P_sin).(k+1) by SERIES_1:def 1,A4;
      hence thesis;
    end;
     for n be Element of NAT holds P[n] from NAT_1:sch 1(A2,A3);
    hence thesis;
  end;
    defpred P[Element of NAT] means
             Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*$1+1)
                 = Partial_Sums(x P_cos).$1;
A5: P[0]
    proof
       Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*0+1)
           = Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*0)
                 + Maclaurin(cos,].-r,r.[,x).((2*0)+1) by SERIES_1:def 1
           .= Maclaurin(cos,].-r,r.[,x).(2*0)
                 + Maclaurin(cos,].-r,r.[,x).(2*0+1) by SERIES_1:def 1
           .= Maclaurin(cos,].-r,r.[,x).(2*0) + 0 by Th20,A1
           .= (-1) |^ 0 * x |^ (2*0) / ((2*0)!) by Th20,A1
           .= (x P_cos).0 by SIN_COS:def 25
           .= Partial_Sums(x P_cos).0 by SERIES_1:def 1;
      hence thesis;
    end;
A6: for k be Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT such that
 A7: P[k];
       Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*(k+1)+1)
             = Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*k+2)
                 + Maclaurin(cos,].-r,r.[,x).(2*(k+1)+1) by SERIES_1:def 1
            .= Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*k+2) + 0
                      by Th20,A1
            .= Partial_Sums(Maclaurin(cos,].-r,r.[,x)).((2*k+1)+1)
            .= Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*k+1)
                 + Maclaurin(cos,].-r,r.[,x).(2*k+2) by SERIES_1:def 1
            .= Partial_Sums(x P_cos).k
                 + (-1) |^ (k+1) * x |^ (2*(k+1)) / ((2*(k+1))!)
                      by Th20,A1,A7
            .= Partial_Sums(x P_cos).k + (x P_cos).(k+1) by SIN_COS:def 25
            .= Partial_Sums(x P_cos).(k+1) by SERIES_1:def 1;
      hence thesis;
    end;
     for n be Element of NAT holds P[n] from NAT_1:sch 1(A5,A6);
    hence thesis;
end;

theorem Th26:
  for r, x be Real, m being Element of NAT st 0 < r & m > 0 holds
    Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*m)
          = Partial_Sums(x P_sin).(m-1) &
    Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*m)
          = Partial_Sums(x P_cos).m
proof
  let r, x be Real, m be Element of NAT such that
  A1: r > 0 and
  A2: m > 0;
   2*m > 2*0 by A2,XREAL_1:70;
  then A3: 2*m-1 is Element of NAT by NAT_1:20;
  A4: m-1 is Element of NAT by NAT_1:20,A2;
  A5: Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*m)
         = Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*m-1)
           + Maclaurin(sin,].-r,r.[,x).((2*m-1)+1) by SERIES_1:def 1,A3
        .= Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*m-1) + 0
           by Th20,A1
        .= Partial_Sums(Maclaurin(sin,].-r,r.[,x)).(2*(m-1)+1)
        .= Partial_Sums(x P_sin).(m-1) by Th25,A1,A4;
   Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*m)
         = Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*m-1)
           + Maclaurin(cos,].-r,r.[,x).((2*m-1)+1) by SERIES_1:def 1,A3
        .= Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*m-1)
           + (-1) |^ m * x |^ (2*m) / ((2*m)!) by Th20,A1
        .= Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*(m-1)+1)
           + (x P_cos).m by SIN_COS:def 25
        .= Partial_Sums(x P_cos).((m-1)) + (x P_cos).((m-1)+1)
           by Th25,A1,A4
        .= Partial_Sums(x P_cos).m by SERIES_1:def 1,A4;
  hence thesis by A5;
end;

theorem Th27:
  for r, x be Real, m be Element of NAT st 0 < r holds
    Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*m) = Partial_Sums(x P_cos).m
proof
  let r, x be Real, m be Element of NAT such that
  A1: r > 0;
  defpred P[Element of NAT] means
             Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*$1)
                 = Partial_Sums(x P_cos).$1;
  A2: P[0]
  proof
    thus Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*0)
                  = Maclaurin(cos,].-r,r.[,x).(2*0) by SERIES_1:def 1
                 .= (-1) |^ 0 * x |^ (2*0) / ((2*0)!) by Th20,A1
                 .= (x P_cos).0 by SIN_COS:def 25
                 .= Partial_Sums(x P_cos).0 by SERIES_1:def 1;
  end;
  A3: for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that P[k];
    thus Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*(k+1))
      = Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*k+1)
        + Maclaurin(cos,].-r,r.[,x).((2*k+1)+1) by SERIES_1:def 1
     .= Partial_Sums(Maclaurin(cos,].-r,r.[,x)).(2*k+1)
        + (-1) |^ (k+1) * x |^ (2*(k+1)) / ((2*(k+1))!) by Th20,A1
     .= Partial_Sums(x P_cos).k
        + (-1) |^ (k+1) * x |^ (2*(k+1)) / ((2*(k+1))!) by Th25,A1
     .= Partial_Sums(x P_cos).k + (x P_cos).(k+1) by SIN_COS:def 25
     .= Partial_Sums(x P_cos).(k+1) by SERIES_1:def 1;
  end;
   for n be Element of NAT holds P[n] from NAT_1:sch 1(A2,A3);
  hence thesis;
end;

theorem
   for r, x be Real st r > 0 holds
    Partial_Sums(Maclaurin(sin,].-r,r.[,x)) is convergent &
    sin.x = Sum(Maclaurin(sin,].-r,r.[,x)) &
    Partial_Sums(Maclaurin(cos,].-r,r.[,x)) is convergent &
    cos.x = Sum(Maclaurin(cos,].-r,r.[,x))
proof
  let r, x be Real such that
A1: r > 0;
   Sum(x P_sin) = sin.x by SIN_COS:40;
then A2: lim Partial_Sums(x P_sin) = sin.x by SERIES_1:def 3;
A3: Partial_Sums(x P_sin) is convergent by SIN_COS:39;
  set g = sin.x;
  A4: for p being real number st p>0 holds
       ex n1 being Element of NAT st for m1 be Element of NAT st m1>=n1 holds
         abs(Partial_Sums(Maclaurin(sin,].-r,r.[,x)).m1-g)<p
  proof
  let p be real number such that
A5: p > 0;
    consider n be Element of NAT such that
A6: for m being Element of NAT st n<=m holds abs(Partial_Sums(x P_sin).m-g) < p
      by SEQ_2:def 7,A2,A3,A5;
    set n1=2*n+2;
     for m1 be Element of NAT st m1>=n1 holds
      abs(Partial_Sums(Maclaurin(sin,].-r,r.[,x)).m1-g) < p
    proof
    let m1 be Element of NAT such that
    A7: m1>=n1;
    per cases;
      suppose m1 is even;
        then consider j be Element of NAT such that
        A8: m1=2*j by ABIAN:def 2;
        A9: ((n+1)*2)/2 <= (j*2)/2 by XREAL_1:74,A7,A8;
         n+1 >= 0+1 by XREAL_1:8;
        then A10: j-1 is Element of NAT by NAT_1:20,A9;
         (n+1)-1 <= j-1 by A9,XREAL_1:11;
        then abs(Partial_Sums(x P_sin).(j-1)-g) < p by A6,A10;
        hence abs(Partial_Sums(Maclaurin(sin,].-r,r.[,x)).m1-g)<p
                   by A8,Th26,A1,A9;
      end;
      suppose m1 is odd;
        then consider j be Element of NAT such that
   A11: m1=2*j+1 by ABIAN:9;
   A12: n*2+0 <= n*2+1 by XREAL_1:8;
         (n*2+2)-1 <= (2*j+1)-1 by XREAL_1:11,A7,A11;
        then n*2 <= j*2 by A12,XXREAL_0:2;
        then (n*2)/2 <= (j*2)/2 by XREAL_1:74;
        then abs(Partial_Sums(x P_sin).j-g) < p by A6;
        hence abs(Partial_Sums(Maclaurin(sin,].-r,r.[,x)).m1-g)<p
                      by Th25,A1,A11;
      end;
    end;
    hence ex n1 being Element of NAT
     st for m1 be Element of NAT st m1>=n1 holds
     abs(Partial_Sums(Maclaurin(sin,].-r,r.[,x)).m1-g)<p;
  end;
  then Partial_Sums(Maclaurin(sin,].-r,r.[,x)) is convergent by SEQ_2:def 6;
  then A13: lim Partial_Sums(Maclaurin(sin,].-r,r.[,x)) = sin.x
               by SEQ_2:def 7,A4;
   Sum(x P_cos) = cos.x by SIN_COS:40;
  then A14: lim Partial_Sums(x P_cos) = cos.x by SERIES_1:def 3;
  A15: Partial_Sums(x P_cos) is convergent by SIN_COS:39;
  set g = cos.x;
  A16: for p being real number st p>0 holds
       (ex n1 being Element of NAT st for m1 be Element of NAT st m1>=n1 holds
         abs(Partial_Sums(Maclaurin(cos,].-r,r.[,x)).m1-g)<p)
  proof
  let p be real number such that
A17: p > 0;
    consider n be Element of NAT such that
A18: for m being Element of NAT
 st n<=m holds abs(Partial_Sums(x P_cos).m-g) < p
        by SEQ_2:def 7,A14,A15,A17;
    set n1=2*n+1;
     for m1 be Element of NAT st m1>=n1 holds
      abs(Partial_Sums(Maclaurin(cos,].-r,r.[,x)).m1-g)<p
    proof
    let m1 be Element of NAT such that
A19: m1>=n1;
    per cases;
      suppose m1 is even;
        then consider j be Element of NAT such that
   A20: m1=2*j by ABIAN:def 2;
     (n*2)+1 >= n*2 by XREAL_1:31;
        then n*2 <= j*2 by XXREAL_0:2,A19,A20;
        then (n*2)/2 <= (j*2)/2 by XREAL_1:74;
        then abs(Partial_Sums(x P_cos).j-g) < p by A18;
        hence abs(Partial_Sums(Maclaurin(cos,].-r,r.[,x)).m1-g)<p
                          by A20,Th27,A1;
      end;
      suppose m1 is odd;
        then consider j be Element of NAT such that
        A21: m1=2*j+1 by ABIAN:9;
         (n*2+1)-1 <= (j*2+1)-1 by XREAL_1:11,A19,A21;
        then (n*2)/2 <= (j*2)/2 by XREAL_1:74;
        then abs(Partial_Sums(x P_cos).j-g) < p by A18;
        hence abs(Partial_Sums(Maclaurin(cos,].-r,r.[,x)).m1-g)<p
                      by Th25,A1,A21;
      end;
    end;
    hence ex n1 being Element of NAT
     st for m1 be Element of NAT st m1>=n1 holds
        abs(Partial_Sums(Maclaurin(cos,].-r,r.[,x)).m1-g)<p;
  end;
  then Partial_Sums(Maclaurin(cos,].-r,r.[,x)) is convergent by SEQ_2:def 6;
  then lim Partial_Sums(Maclaurin(cos,].-r,r.[,x)) = cos.x by SEQ_2:def 7,A16;
  hence thesis by A4,A13,A16,SERIES_1:def 3,SEQ_2:def 6;
end;
