:: Niemytzki Plane -- an Example of {T}ychonoff Space Which Is Not $T_4$
::  by Grzegorz Bancerek
::
:: Received November 7, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FINSEQ_1, PRE_TOPC, ARYTM, FUNCT_1, BOOLE, SETFAM_1, ORDINAL2,
      EUCLID, METRIC_1, TOPGEN_2, PROB_1, PBOOLE, CANTOR_1, RELAT_1, FINSET_1,
      ZF_REFLE, TARSKI, MCART_1, ASYMPT_0, COMPLEX1, ARYTM_1, ARYTM_3, RCOMP_1,
      ABSVALUE, ZF_LANG, SQUARE_1, RLVECT_3, SUBSET_1, TOPS_1, PARTFUN1, RAT_1,
      TOPGEN_1, TOPGEN_3, CARD_1, CARD_2, CARD_3, TOPMETR, TOPS_2, SEQ_1,
      PRALG_1, FUNCT_2, CAT_1, FUNCT_5, LIMFUNC1, COMPTS_1, BORSUK_1, TOPGEN_5,
      FUNCOP_1, FUNCT_4, PRALG_2, SUPINF_1, CARD_5, GROUP_3, RLVECT_1;
 notations MEMBERED, TARSKI, XBOOLE_0, ZFMISC_1, RELAT_1, FINSET_1, SUBSET_1,
      MCART_1, SETFAM_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, BINOP_1,
      FINSEQ_1, FUNCT_4, FUNCOP_1, SEQ_1, PBOOLE, ORDINAL1, CARD_1, CARD_2,
      CARD_3, FUNCT_5, FUNCT_6, NUMBERS, XCMPLX_0, SQUARE_1, COMPLEX1, REAL_1,
      XREAL_0, XXREAL_2, RAT_1, RCOMP_1, LIMFUNC1, DOMAIN_1, STRUCT_0,
      PRE_TOPC, TOPS_1, TOPS_2, RLVECT_1, EUCLID, CANTOR_1, BORSUK_1, TOPMETR,
      TOPREAL9,
      URYSOHN1, COMPTS_1, TOPGEN_1, TOPGEN_2, TOPGEN_3, XXREAL_0;
 constructors SETFAM_1, WELLORD2, FUNCT_4, REAL_1, SQUARE_1, COMPLEX1, CARD_2,
      PROB_1, RCOMP_1, LIMFUNC1, FUNCT_6, TOPS_1, TOPS_2, COMPTS_1,
      URYSOHN1, TSP_1, MONOID_0, PSCOMP_1, BORSUK_4, TOPGEN_2, TOPREAL9,
      TOPGEN_1, TOPGEN_3, SEQ_1, XXREAL_2;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_2, FUNCOP_1,
      FINSET_1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, SQUARE_1, RAT_1, CARD_1,
      MEMBERED, FINSEQ_1, PBOOLE, ASYMPT_0, STRUCT_0, PRE_TOPC, TOPS_1,
      COMPLSP1, BORSUK_1, MONOID_0, EUCLID, TOPMETR, SPRECT_1, TOPGRP_1,
      BORSUK_4, BORSUK_6, TOPGEN_1, VALUED_0, XXREAL_2, ORDINAL1;
 requirements BOOLE, SUBSET, NUMERALS, ARITHM, REAL;
 definitions RELAT_1, TARSKI, XBOOLE_0, FUNCT_1, PARTFUN1, FUNCOP_1, WELLORD2,
      PBOOLE, PRE_TOPC, TOPS_3, URYSOHN1, TOPGEN_1, BORSUK_1, FUNCT_6, BINOP_1,
      SQUARE_1, SUBSET_1, STRUCT_0, LIMFUNC1, PROB_1, COMPTS_1;
 theorems TARSKI, XBOOLE_0, XBOOLE_1, ZFMISC_1, FUNCT_1, EUCLID, PBOOLE,
      EUCLID_2, TOPREAL9, TOPGEN_3, SETWISEO, XREAL_1, XREAL_0, XCMPLX_1,
      TOPRNS_1, TOPREAL6, ABSVALUE, SPPOL_2, FINSEQ_1, COMPLEX1, JGRAPH_1,
      YELLOW_9, NUMBERS, CARD_5, TOPGEN_2, SUBSET_1, YELLOW_8, TOPS_1,
      PRE_TOPC, TOPGEN_1, SQUARE_1, RAT_1, CARD_1, CARD_2, CARD_3, CARD_4,
      FINSEQ_3, MCART_1, FUNCT_6, BORSUK_1, TOPALG_1, TOPS_2, FUNCT_2, TIETZE,
      JGRAPH_5, URYSOHN1, TOPMETR, BOOLMARK, FUNCOP_1, FUNCT_4, RELAT_1,
      RELSET_1, CANTOR_1, PARTFUN1, SETFAM_1, JORDAN6, YELLOW12, FUNCT_7,
      DICKSON, FUNCT_5, FRECHET, TSP_1, JORDAN, XXREAL_0, TSEP_1, XXREAL_1,
      XXREAL_2;
 schemes SUBSET_1, FUNCT_1, FRAENKEL, PRE_CIRC, FUNCT_5, CLASSES1, FINSET_1,
      FUNCT_2;

begin :: Preliminaries

reserve x,y for Element of REAL,
  u,v,w for set,
  r for positive (real number);

theorem Th1:
  for f,g being Function st f tolerates g
  for A being set holds (f+*g)"A = (f"A)\/(g"A)
proof
  let f,g be Function;
  assume
A1: f tolerates g;
  let A be set;
  thus (f+*g)"A c= (f"A)\/(g"A)
  proof
    let x be set;
    assume x in (f+*g)"A;
    then
A2: x in dom (f+*g) & (f+*g).x in A by FUNCT_1:def 13;
    then x in dom f \/ dom g by FUNCT_4:def 1;
    then x in dom f or x in dom g by XBOOLE_0:def 3;
    then x in dom f & (f+*g).x = f.x or x in dom g & (f+*g).x = g.x
    by A1,FUNCT_4:14,16;
    then x in f"A or x in g"A by A2,FUNCT_1:def 13;
    hence thesis by XBOOLE_0:def 3;
  end;
  f c= f+*g & g c= f+*g by A1,FUNCT_4:26,29;
  then f"A c= (f+*g)"A & g"A c= (f+*g)"A by RELAT_1:179;
  hence thesis by XBOOLE_1:8;
end;

theorem
  for f,g being Function st dom f misses dom g
  for A being set holds (f+*g)"A = (f"A)\/(g"A) by Th1,PARTFUN1:138;

theorem Th3:
  for x,a being set, f being Function st a in dom f
  holds (commute (x .--> f)).a = x .--> f.a
proof
  let x,a be set;
  let f be Function;
  assume
A1: a in dom f;
  set g = x .--> f;
A2: dom g = {x} & rng g = {f} & g.x = f by FUNCOP_1:14,19,87;
A3: x in {x} by TARSKI:def 1;
  f in Funcs(dom f, rng f) by FUNCT_2:def 2;
  then rng g c= Funcs(dom f, rng f) by A2,ZFMISC_1:37;
  then g in Funcs({x}, Funcs(dom f, rng f)) by A2,FUNCT_2:def 2;

then dom ((commute g).a) = {x} & (commute g).a.x = f.a by A1,A2,A3,FUNCT_6:86;
  hence thesis by DICKSON:1;
end;

theorem
  for b being set, f being Function holds b in dom commute f iff
  ex a being set, g being Function st a in dom f & g = f.a & b in dom g
proof
  let b be set;
  let f be Function;
A1: dom commute f = proj2 dom uncurry f by FUNCT_5:30;
  hereby
    assume b in dom commute f;
    then consider a being set such that
A2: [a,b] in dom uncurry f by A1,RELAT_1:def 5;
    consider a' being set, g being Function, b' being set such that
A3: [a,b] = [a',b'] & a' in dom f & g = f.a' & b' in dom g
    by A2,FUNCT_5:def 4;
    take a, g;
    thus a in dom f & g = f.a & b in dom g by A3,ZFMISC_1:33;
  end;
  given a being set, g being Function such that
A4: a in dom f & g = f.a & b in dom g;
  [a,b] in dom uncurry f by A4,FUNCT_5:def 4;
  hence thesis by A1,RELAT_1:def 5;
end;

theorem Th5:
  for a,b being set, f being Function holds a in dom ((commute f).b) iff
  ex g being Function st a in dom f & g = f.a & b in dom g
proof
  let a,b be set;
  let f be Function;
  dom uncurry f c= [:dom f, proj1 union rng f:]
  proof
    let u;
    assume u in dom uncurry f;
    then consider a being set, g being Function, b being set such that
A1: u = [a,b] & a in dom f & g = f.a & b in dom g by FUNCT_5:def 4;
    g in rng f by A1,FUNCT_1:def 5;
    then g c= union rng f by ZFMISC_1:92;
    then proj1 g = dom g & proj1 g c= proj1 union rng f by FUNCT_5:5;
    hence thesis by A1,ZFMISC_1:def 2;
  end;
  then
A2: uncurry' commute f = uncurry f by FUNCT_5:57;
  hereby
    assume
A3: a in dom ((commute f).b);
    then b in dom commute f by FUNCT_1:def 4,RELAT_1:60;
    then [a,b] in dom uncurry f by A2,A3,FUNCT_5:46;
    then consider a' being set, g being Function, b' being set such that
A4: [a,b] = [a',b'] & a' in dom f & g = f.a' & b' in dom g by FUNCT_5:def 4;
    take g;
    thus a in dom f & g = f.a & b in dom g by A4,ZFMISC_1:33;
  end;
  given g being Function such that
A5: a in dom f & g = f.a & b in dom g;
  [a,b]`1 = a & [a,b]`2 = b & [a,b] in dom uncurry f
  by A5,FUNCT_5:def 4,MCART_1:7;
  hence thesis by FUNCT_5:29;
end;

theorem Th6:
  for a,b being set, f,g being Function st a in dom f & g = f.a & b in dom g
  holds (commute f).b.a = g.b
proof
  let a,b be set;
  let f,g be Function;
  assume
A1: a in dom f & g = f.a & b in dom g;
  then
A2: [a,b]`1 = a & [a,b]`2 = b & [a,b] in dom uncurry f
  by FUNCT_5:def 4,MCART_1:7;
  then (uncurry f).(a,b) = g.b by A1,FUNCT_5:def 4;
  hence thesis by A2,FUNCT_5:29;
end;

theorem Th7:
  for a being set, f,g,h being Function st h = f \/ g
  holds (commute h).a = (commute f).a \/ (commute g).a
proof
  let a be set;
  let f,g,h be Function;
  assume
A1: h = f \/ g;
  now
    let u,v;
    hereby
      assume [u,v] in (commute h).a;
      then
A2:   u in dom ((commute h).a) & (commute h).a.u = v by FUNCT_1:8;
      then consider k being Function such that
A3:   u in dom h & k = h.u & a in dom k by Th5;
A4:   (commute h).a.u = k.a by A3,Th6;
      [u,k] in h by A3,FUNCT_1:def 4;
      then [u,k] in f or [u,k] in g by A1,XBOOLE_0:def 3;
      then u in dom f & k = f.u or u in dom g & k = g.u by FUNCT_1:8;
      then u in dom ((commute f).a) & (commute f).a.u = k.a or
      u in dom ((commute g).a) & (commute g).a.u = k.a by A3,Th5,Th6;
      then [u,v] in (commute f).a or [u,v] in (commute g).a by A2,A4,FUNCT_1:8;
      hence [u,v] in (commute f).a \/ (commute g).a by XBOOLE_0:def 3;
    end;
    assume
A5: [u,v] in (commute f).a \/ (commute g).a;
    per cases by A5,XBOOLE_0:def 3;
    suppose [u,v] in (commute f).a;
      then
A6:   u in dom ((commute f).a) & (commute f).a.u = v by FUNCT_1:8;
      then consider k being Function such that
A7:   u in dom f & k = f.u & a in dom k by Th5;
      [u,k] in f by A7,FUNCT_1:8;
      then [u,k] in h by A1,XBOOLE_0:def 3;
      then u in dom h & k = h.u by FUNCT_1:8;
      then u in dom ((commute h).a) & (commute h).a.u = k.a &
      (commute f).a.u = k.a by A7,Th5,Th6;
      hence [u,v] in (commute h).a by A6,FUNCT_1:8;
    end;
    suppose [u,v] in (commute g).a;
      then
A8:   u in dom ((commute g).a) & (commute g).a.u = v by FUNCT_1:8;
      then consider k being Function such that
A9:   u in dom g & k = g.u & a in dom k by Th5;
      [u,k] in g by A9,FUNCT_1:8;
      then [u,k] in h by A1,XBOOLE_0:def 3;
      then u in dom h & k = h.u by FUNCT_1:8;
      then u in dom ((commute h).a) & (commute h).a.u = k.a &
      (commute g).a.u = k.a by A9,Th5,Th6;
      hence [u,v] in (commute h).a by A8,FUNCT_1:8;
    end;
  end;
  hence thesis by RELAT_1:def 2;
end;

canceled 4;

theorem Th12:
  for X,Y being set holds product <*X,Y*>, [:X,Y:] are_equipotent &
  card product <*X,Y*> = (card X)*`(card Y)
proof
  let X,Y be set;
  deffunc F(Function) = [$1.1,$1.2];
  consider f being Function such that
A1: dom f = product <*X,Y*> &
  for g being Function st g in product <*X,Y*> holds f.g = F(g)
  from FUNCT_5:sch 1;
A2: dom <*X,Y*> = {1,2} & <*X,Y*>.1 = X & <*X,Y*>.2 = Y
  by FINSEQ_1:4,61,FINSEQ_3:29;
  thus product <*X,Y*>, [:X,Y:] are_equipotent
  proof
    take f;
    thus f is one-to-one
    proof
      let x1,x2 be set;
      assume
A3:   x1 in dom f & x2 in dom f & f.x1 = f.x2;
      then consider g1 being Function such that
A4:   x1 = g1 & dom g1 = dom <*X,Y*> &
      for y being set st y in dom <*X,Y*> holds g1.y in <*X,Y*>.y
      by A1,CARD_3:def 5;
      consider g2 being Function such that
A5:   x2 = g2 & dom g2 = dom <*X,Y*> &
      for y being set st y in dom <*X,Y*> holds g2.y in <*X,Y*>.y
      by A1,A3,CARD_3:def 5;
A6:   [g1.1,g1.2] = f.x1 by A1,A3,A4
        .= [g2.1,g2.2] by A1,A3,A5;
      now
        let z being set;
        assume z in {1,2};
        then z = 1 or z = 2 by TARSKI:def 2;
        hence g1.z = g2.z by A6,ZFMISC_1:33;
      end;
      hence thesis by A2,A4,A5,FUNCT_1:9;
    end;
    thus dom f = product <*X,Y*> by A1;
    thus rng f c= [:X,Y:]
    proof
      let z be set;
      assume z in rng f;
      then consider t being set such that
A7:   t in dom f & z = f.t by FUNCT_1:def 5;
      consider g being Function such that
A8:   t = g & dom g = dom <*X,Y*> &
      for y being set st y in dom <*X,Y*> holds g.y in <*X,Y*>.y
      by A1,A7,CARD_3:def 5;
      1 in {1,2} & 2 in {1,2} by TARSKI:def 2;
      then z = [g.1,g.2] & g.1 in X & g.2 in Y by A1,A2,A7,A8;
      hence thesis by ZFMISC_1:106;
    end;
    let z be set;
    assume z in [:X,Y:];
    then
A9: z = [z`1,z`2] & z`1 in X & z`2 in Y by MCART_1:10,23;
    set g = <*z`1,z`2*>;
A10: dom g = {1,2} & g.1 = z`1 & g.2 = z`2 by FINSEQ_1:4,61,FINSEQ_3:29;
A11: g in product <*X,Y*> by A9,FUNCT_6:2;
    then f.g = z by A1,A9,A10;
    hence thesis by A1,A11,FUNCT_1:def 5;
  end;
  hence card product <*X,Y*> = card [:X,Y:] by CARD_1:21
    .= card [:card X, card Y:] by CARD_2:14
    .= (card X)*`card Y by CARD_2:def 2;
end;

scheme SCH1{P[set], A,B,C()-> non empty set, F1,F2(set)-> set}:
  ex f being Function of C(), B() st
  for a being Element of A() st a in C() holds
  (P[a] implies f.a = F1(a)) & (not P[a] implies f.a = F2(a))
provided
A1: C() c= A()
and
A2: for a being Element of A() st a in C() holds
(P[a] implies F1(a) in B()) & (not P[a] implies F2(a) in B())
proof
A3: for a being set st a in C() holds
  (P[a] implies F1(a) in B()) & (not P[a] implies F2(a) in B()) by A1,A2;
  consider f being Function of C(),B() such that
A4: for x being set st x in C() holds
  (P[ x] implies f.x = F1(x)) & (not P[ x] implies f.x = F2(x))
  from FUNCT_2:sch 5(A3);
  take f; thus thesis by A4;
end;

scheme SCH2{P,Q[set], A,B,C()-> non empty set, F1,F2,F3(set)-> set}:
  ex f being Function of C(), B() st
  for a being Element of A() st a in C() holds (P[a] implies f.a = F1(a)) &
  (not P[a] & Q[a] implies f.a = F2(a)) &
  (not P[a] & not Q[a] implies f.a = F3(a))
provided
A1: C() c= A()
and
A2: for a being Element of A() st a in C() holds (P[a] implies F1(a) in B()) &
(not P[a] & Q[a] implies F2(a) in B()) &
(not P[a] & not Q[a] implies F3(a) in B())
proof
  set D = {a where a is Element of C(): P[a] or Q[a]};
  per cases;
  suppose
A3: D = {};
    consider f being Function such that
A4: dom f = C() & for u st u in C() holds f.u = F3(u) from FUNCT_1:sch 3;
    rng f c= B()
    proof
      let v;
      assume v in rng f;
      then consider u such that
A5:   u in dom f & v = f.u by FUNCT_1:def 5;
      not u in D by A3;
      then (not P[u] & not Q[u]) & v = F3(u) by A4,A5;
      hence thesis by A1,A2,A4,A5;
    end;
    then reconsider f as Function of C(), B() by A4,FUNCT_2:4;
    take f;
    let a be Element of A();
    assume
A6: a in C();
    not a in D by A3;
    hence thesis by A4,A6;
  end;
  suppose D <> {};
    then reconsider D as non empty set;
A7: for a being set st a in D holds
    (P[a] implies F1(a) in B()) & (not P[a] implies F2(a) in B())
    proof
      let a be set;
      assume a in D;
      then ex b being Element of C() st a = b & (P[b] or Q[b]);
      then a in C() & (P[a] or Q[a]);
      hence thesis by A1,A2;
    end;
    consider g being Function of D,B() such that
A8: for x being set st x in D holds
    (P[ x] implies g.x = F1(x)) & (not P[ x] implies g.x = F2(x))
    from FUNCT_2:sch 5(A7);
    defpred PQ[set] means P[$1] or Q[$1];
    deffunc F12(set) = g.$1;
A9: for a being set st a in C() holds
    (PQ[a] implies F12(a) in B()) & (not PQ[a] implies F3(a) in B())
    proof
      let a be set;
      assume
A10:  a in C();
      hereby
        assume PQ[a];
        then a in D by A10;
        then (P[a] implies F1(a) in B()) & (not P[a] implies F2(a) in B()) &
        (P[a] implies g.a = F1(a)) & (not P[a] implies g.a = F2(a)) by A7,A8;
        hence F12(a) in B();
      end;
      thus thesis by A1,A2,A10;
    end;
    consider f being Function of C(),B() such that
A11: for x being set st x in C() holds
    (PQ[ x] implies f.x = F12(x)) & (not PQ[ x] implies f.x = F3(x))
    from FUNCT_2:sch 5(A9);
    take f;
    let a be Element of A();
    assume
A12: a in C();
    then PQ[a] implies f.a = g.a & a in D by A11;
    hence (P[a] implies f.a = F1(a)) &
    (not P[a] & Q[a] implies f.a = F2(a)) by A8;
    thus thesis by A11,A12;
  end;
end;

theorem Th13:
  for a,b being real number holds |.|[a,b]|.|^2 = a^2+b^2
proof
  let a,b be real number;
  |[a,b]|`1 = a & |[a,b]|`2 = b by EUCLID:56;
  hence thesis by JGRAPH_1:46;
end;

theorem Th14:
  for X being TopSpace, Y being non empty TopSpace
  for A,B being closed Subset of X for f being continuous Function of X|A, Y
  for g being continuous Function of X|B, Y st f tolerates g
  holds f+*g is continuous Function of X|(A \/ B), Y
proof
  let X be TopSpace, Y be non empty TopSpace;
  let A,B be closed Subset of X;
  let f be continuous Function of X|A, Y;
  let g be continuous Function of X|B, Y such that
A1: f tolerates g;
A2: the carrier of X|A = A & the carrier of X|B = B &
  the carrier of X|(A \/ B) = A \/ B by PRE_TOPC:29;
  then
A3: dom (f+*g) = dom f \/ dom g & dom f = A & dom g = B
  by FUNCT_2:def 1,FUNCT_4:def 1;
  rng f \/ rng g c= (the carrier of Y) \/ the carrier of Y &
  rng (f+*g) c= rng f \/ rng g by FUNCT_4:18;
  then reconsider h = f+*g as Function of X|(A \/ B), Y by A2,A3,FUNCT_2:4
,XBOOLE_1:1;
  h is continuous
  proof
    let C be Subset of Y;
    assume C is closed;
    then
A4: f"C is closed & g"C is closed by PRE_TOPC:def 12;
    then consider C1 being Subset of X such that
A5: C1 is closed & C1 /\ [#](X|A) = f"C by PRE_TOPC:43;
    consider C2 being Subset of X such that
A6: C2 is closed & C2 /\ [#](X|B) = g"C by A4,PRE_TOPC:43;
A7: [#](X|(A \/ B)) = A \/ B & [#](X|A) = A & [#](X|B) = B by PRE_TOPC:29;
A8: h"C = (f"C)\/(g"C) by A1,Th1
      .= ((f"C)\/(g"C))/\(A\/B) by A7,XBOOLE_1:13,28;
    C1 /\ A is closed & C2 /\ B is closed by A5,A6,TOPS_1:35;
    then C1/\A\/C2/\B is closed by TOPS_1:36;
    hence thesis by A5,A6,A7,A8,PRE_TOPC:43;
  end;
  hence thesis;
end;

theorem Th15:
  for X being TopSpace, Y being non empty TopSpace
  for A,B being closed Subset of X st A misses B
  for f being continuous Function of X|A, Y
  for g being continuous Function of X|B, Y
  holds f+*g is continuous Function of X|(A \/ B), Y
proof
  let X be TopSpace, Y be non empty TopSpace;
  let A,B be closed Subset of X such that
A1: A misses B;
  let f be continuous Function of X|A, Y;
  let g be continuous Function of X|B, Y;
  the carrier of X|A = A & the carrier of X|B = B &
  the carrier of X|(A \/ B) = A \/ B by PRE_TOPC:29;
  then dom f = A & dom g = B by FUNCT_2:def 1;
  hence thesis by A1,Th14,PARTFUN1:138;
end;

theorem Th16:
  for X being TopSpace, Y being non empty TopSpace
  for A being open closed Subset of X
  for f being continuous Function of X|A, Y
  for g being continuous Function of X|A`, Y
  holds f+*g is continuous Function of X, Y
proof
  let X be TopSpace;
  let Y be non empty TopSpace;
  let A be open closed Subset of X;
  let f be continuous Function of X|A, Y;
  let g be continuous Function of X|A`, Y;
  A\/A` = [#]X by PRE_TOPC:18;
  then
A1: X|(A\/A`) = the TopStruct of X & the TopStruct of Y = the TopStruct of Y
  by TSEP_1:100;
  A misses A` by XBOOLE_1:79;
  then f+*g is continuous Function of X|(A\/A`), Y by Th15;
  hence thesis by A1,YELLOW12:36;
end;

begin :: Niemytzki plane

theorem Th17:
  for n being Element of NAT for a being Point of TOP-REAL n
  for r being positive (real number) holds a in Ball(a,r)
proof
  let n be Element of NAT;
  let a be Point of TOP-REAL n;
  let r be positive (real number);
  a-a = 0.TOP-REAL n by EUCLID:46;
  then |.a-a.| = 0 by EUCLID_2:61;
  hence thesis by TOPREAL9:7;
end;

definition
  func y=0-line -> Subset of TOP-REAL 2 equals

  {|[x,0]|: not contradiction};
  coherence
  proof
    set A = {|[x,0]|: not contradiction};
    A c= the carrier of TOP-REAL 2
    proof
      let a be set;
      assume a in A;
      then ex x st a = |[x,0]|;
      hence thesis;
    end;
    hence thesis;
  end;
  func y>=0-plane -> Subset of TOP-REAL 2 equals

  {|[x,y]|: y >= 0};
  coherence
  proof
    set A = {|[x,y]|: y >= 0};
    A c= the carrier of TOP-REAL 2
    proof
      let a be set;
      assume a in A;
      then ex x,y st a = |[x,y]| & y >= 0;
      hence thesis;
    end;
    hence thesis;
  end;
end;

theorem
  for a,b being set holds <*a,b*> in y=0-line iff a in REAL & b = 0
proof
  let a,b be set;
A1: <*a,b*> in y=0-line iff ex x st <*a,b*> = |[x,0]|;
  hereby
    assume <*a,b*> in y=0-line;
    then consider x,y such that
A2: <*a,b*> = |[x,0]| by A1;
A3: <*a,b*>.1 = a & <*a,b*>.1 = x &
    <*a,b*>.2 = b & <*a,b*>.2 = 0 by A2,FINSEQ_1:61;
    hence a in REAL;
    thus b = 0 by A3;
  end;
  assume a in REAL;
  then reconsider x = a as Real;
  |[x,0]| = <*a,0 *>;
  hence thesis;
end;

theorem Th19:
  for a,b being real number holds |[a,b]| in y=0-line iff b = 0
proof
  let a,b be real number;
  (|[a,b]| in y=0-line iff ex x st |[a,b]| = |[x,0]|) &
  a is Real & b is Real by XREAL_0:def 1;
  hence thesis by SPPOL_2:1;
end;

theorem Th20:
  card y=0-line = continuum
proof
  deffunc F(Real) = |[$1,0]|;
  consider f being Function such that
A1: dom f = REAL and
A2: for r being Real holds f.r = F(r) from FUNCT_1:sch 4;
  REAL, y=0-line are_equipotent
  proof
    take f;
    thus f is one-to-one
    proof
      let x,y be set;
      assume x in dom f & y in dom f;
      then reconsider x,y as Real by A1;
      f.x = |[x,0]| & f.y = |[y,0]| by A2;
      hence thesis by SPPOL_2:1;
    end;
    thus dom f = REAL by A1;
    thus rng f c= y=0-line
    proof
      let a be set;
      assume a in rng f;
      then consider b being set such that
A3:   b in dom f & a = f.b by FUNCT_1:def 5;
      reconsider b as Real by A1,A3;
      a = |[b,0]| by A2,A3;
      hence thesis;
    end;
    let a be set;
    assume
A4: a in y=0-line;
    then reconsider a as Point of TOP-REAL 2;
A5: a = |[a`1,a`2]| by EUCLID:57;
    then a`2 = 0 by A4,Th19;
    then a = f.a`1 by A2,A5;
    hence thesis by A1,FUNCT_1:def 5;
  end;
  hence thesis by CARD_1:21,TOPGEN_3:def 4;
end;

theorem
  for a,b being set holds <*a,b*> in y>=0-plane iff
  a in REAL & ex y st b = y & y >= 0
proof
  let a,b be set;
  hereby
    assume <*a,b*> in y>=0-plane;
    then consider x,y such that
A1: <*a,b*> = |[x,y]| & y >= 0;
A2: <*a,b*>.1 = a & <*a,b*>.1 = x &
    <*a,b*>.2 = b & <*a,b*>.2 = y by A1,FINSEQ_1:61;
    hence a in REAL;
    take y; thus b = y & y >= 0 by A1,A2;
  end;
  assume a in REAL;
  then reconsider x = a as Real;
  given y such that
A3: b = y & y >= 0;
  |[x,y]| = <*a,b*> by A3;
  hence thesis by A3;
end;

theorem Th22:
  for a,b being real number holds |[a,b]| in y>=0-plane iff b >= 0
proof
  let a,b be real number;
  (|[a,b]| in y>=0-plane iff ex x,y st |[a,b]| = |[x,y]| & y >= 0) &
  a is Real & b is Real by XREAL_0:def 1;
  hence thesis by SPPOL_2:1;
end;

registration
  cluster y=0-line -> non empty;
  coherence by Th19;
  cluster y>=0-plane -> non empty;
  coherence by Th22;
end;

theorem Th23:
  y=0-line c= y>=0-plane
proof
  let x be set;
  assume x in y=0-line;
  then reconsider x as Element of y=0-line;
A1: x = |[x`1,x`2]| by EUCLID:57;
  then x`2 = 0 by Th19;
  hence thesis by A1;
end;

theorem Th24:
  for a,b,r being real number st r > 0 holds
  Ball(|[a,b]|,r) c= y>=0-plane iff r <= b
proof
  let a,b,r be real number such that
A1: r > 0;
  hereby
    assume that
A2: Ball(|[a,b]|,r) c= y>=0-plane and
A3: r > b;
    |[a,b]| in Ball(|[a,b]|,r) by A1,Th17;
    then reconsider b as non negative Real by A2,Th22,XREAL_0:def 1;
    reconsider br=b-r as negative Real by A3,XREAL_1:51;
    reconsider r as positive Real by A1,XREAL_0:def 1;
    b+r < r+r by A3,XREAL_1:8;
    then
A4: (b+r)/2 < (r+r)/2 by XREAL_1:76;
    set y = br/2;
    |[a,y]|-|[a,b]| = |[a-a,y-b]| & y-b is Real by EUCLID:66;
    then |.|[a,y]|-|[a,b]|.| = |.y-b.| by TOPREAL6:31
      .= |.b-y.| by COMPLEX1:146
      .= (b+r)/2 by ABSVALUE:def 1;
    then |[a,y]| in Ball(|[a,b]|,r) by A4,TOPREAL9:7;
    hence contradiction by A2,Th22;
  end;
  assume
A5: r <= b;
  let x be set;
  assume
A6: x in Ball(|[a,b]|,r);
  then reconsider z = x as Element of TOP-REAL 2;
A7: z = |[z`1,z`2]| by EUCLID:57;
  then z-|[a,b]| = |[z`1-a,z`2-b]| & |[z`1-a,z`2-b]|`1 = z`1-a &
  |[z`1-a,z`2-b]|`2 = z`2-b by EUCLID:56,66;
  then z`1-a is Real & z`2-b is Real &
  |.z-|[a,b]|.| = sqrt((z`1-a)^2+(z`2-b)^2) by JGRAPH_1:47;
  then abs(z`2-b) <= |.z-|[a,b]|.| &
  |.z-|[a,b]|.| < r by A6,COMPLEX1:165,TOPREAL9:7;
  then abs(z`2-b) < r by XXREAL_0:2;
  then
A8: abs(b-z`2) < r by COMPLEX1:146;
  now
    assume z`2 < 0;
    then b-z`2 > b by XREAL_1:48;
    then b-z`2 > r by A5,XXREAL_0:2;
    hence contradiction by A1,A8,ABSVALUE:def 1;
  end;
  hence thesis by A7;
end;

theorem Th25:
  for a,b,r being real number st r > 0 & b >= 0 holds
  Ball(|[a,b]|,r) misses y=0-line iff r <= b
proof
  let a,b,r be real number;
  assume
A1: r > 0 & b >= 0;
  hereby
    assume that
A2: Ball(|[a,b]|,r) misses y=0-line and
A3: r > b;
    |[a,0]|-|[a,b]| = |[a-a,0-b]| & 0-b is Real by EUCLID:66;
    then |.|[a,0]|-|[a,b]|.| = |.0-b.| by TOPREAL6:31
      .= |.b-0 .| by COMPLEX1:146;
    then |.|[a,0]|-|[a,b]|.| < r by A1,A3,ABSVALUE:def 1;
    then |[a,0]| in Ball(|[a,b]|,r) & |[a,0]| in y=0-line by Th19,TOPREAL9:7;
    hence contradiction by A2,XBOOLE_0:3;
  end;
  assume
A4: r <= b;
  assume Ball(|[a,b]|,r) meets y=0-line;
  then consider x being set such that
A5: x in Ball(|[a,b]|,r) & x in y=0-line by XBOOLE_0:3;
  reconsider x as Element of TOP-REAL 2 by A5;
A6: x = |[x`1,x`2]| by EUCLID:57;
  then x`2 = 0 by A5,Th19;
  then x-|[a,b]| = |[x`1-a,0-b]| by A6,EUCLID:66;
  then (x-|[a,b]|)`1 = x`1-a & (x-|[a,b]|)`2 = 0-b by EUCLID:56;
  then x`1-a is Real & 0-b is Real &
  |.x-|[a,b]|.| = sqrt((x`1-a)^2+(0-b)^2) by JGRAPH_1:47;
  then |.x-|[a,b]|.| >= abs(0-b) by COMPLEX1:165;
  then |.x-|[a,b]|.| >= abs(b-0) & |.x-|[a,b]|.| < r
  by A5,COMPLEX1:146,TOPREAL9:7;
  then abs b < r by XXREAL_0:2;
  hence contradiction by A1,A4,ABSVALUE:def 1;
end;

theorem Th26:
  for n being Element of NAT,
  a,b being Element of TOP-REAL n, r1,r2 being positive (real number)
  st |.a-b.| <= r1-r2 holds Ball(b,r2) c= Ball(a,r1)
proof
  let n be Element of NAT, a,b be Element of TOP-REAL n,
  r1,r2 be positive (real number);
  assume |.a-b.| <= r1-r2;
  then
A1: |.b-a.| <= r1-r2 by TOPRNS_1:28;
  let x be set;
  assume
A2: x in Ball(b,r2);
  then reconsider x as Element of TOP-REAL n;
  |.x-b.| < r2 by A2,TOPREAL9:7;
  then |.x-b.|+|.b-a.| < r2+(r1-r2) &
  |.x-a.| <= |.x-b.|+|.b-a.| by A1,TOPRNS_1:35,XREAL_1:10;
  then |.x-a.| < r2+(r1-r2) by XXREAL_0:2;
  hence thesis by TOPREAL9:7;
end;

theorem Th27:
  for a being real number, r1,r2 being positive (real number) st r1 <= r2
  holds Ball(|[a,r1]|,r1) c= Ball(|[a,r2]|,r2)
proof
  let a be real number;
  let r1,r2 be positive (real number);
  assume
A1: r1 <= r2;
  let x be set;
  assume
A2: x in Ball(|[a,r1]|,r1);
  then reconsider x as Element of TOP-REAL 2;
  |[a,r1]|-|[a,r2]| = |[a-a,r1-r2]| & r1-r2 is Real
  by EUCLID:66,XREAL_0:def 1;
  then |.|[a,r1]|-|[a,r2]|.| = |.r1-r2.| by TOPREAL6:31;
  then |.|[a,r1]|-|[a,r2]|.| = |.r2-r1.| &
  r2-r1 >= 0 by A1,COMPLEX1:146,XREAL_1:50;
  then |.|[a,r1]|-|[a,r2]|.| = r2-r1 &
  |.x-|[a,r1]|.| < r1 by A2,ABSVALUE:def 1,TOPREAL9:7;
  then |.x-|[a,r1]|.|+|.|[a,r1]|-|[a,r2]|.| < r1+(r2-r1) &
  |.x-|[a,r2]|.| <= |.x-|[a,r1]|.|+|.|[a,r1]|-|[a,r2]|.|
  by TOPRNS_1:35,XREAL_1:10;
  then |.x-|[a,r2]|.| < r2 by XXREAL_0:2;
  hence thesis by TOPREAL9:7;
end;

theorem Th28:
  for T1,T2 being non empty TopSpace for B1 being Neighborhood_System of T1
  for B2 being Neighborhood_System of T2 st B1 = B2
  holds the TopStruct of T1 = the TopStruct of T2
proof
  let T1,T2 be non empty TopSpace;
  let B1 be Neighborhood_System of T1;
  let B2 be Neighborhood_System of T2;
  assume
A1: B1 = B2;
A2: dom B1 = the carrier of T1 & dom B2 = the carrier of T2
 by PARTFUN1:def 4;
  UniCl Union B1 = the topology of T1 & UniCl Union B2 = the topology of T2
  by YELLOW_9:22;
  hence thesis by A1,A2;
end;

definition
  func Niemytzki-plane -> strict non empty TopSpace means
  :Def3:
  the carrier of it = y>=0-plane & ex B being Neighborhood_System of it st
  (for x holds B.(|[x,0]|) =
  {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0}) &
  (for x,y st y > 0 holds B.(|[x,y]|) =
  {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0});
  existence
  proof
    set X = y>=0-plane;
    deffunc F1(Element of TOP-REAL 2) =
    {Ball(|[$1`1,r]|,r) \/ {$1} where r is Element of REAL: r > 0};
    deffunc F2(Element of TOP-REAL 2) =
    {Ball($1,r) /\ X where r is Element of REAL: r > 0};
    defpred P[set] means $1 in y=0-line;
    consider B being ManySortedSet of X such that
A1: for a being Element of X st a in X holds (P[a] implies B.a = F1(a)) &
    (not P[a] implies B.a = F2(a)) from PRE_CIRC:sch 2;
    B is non-empty
    proof
      let z be set;
      assume z in X;
      then reconsider s = z as Element of X;
      per cases;
      suppose P[z];
        then
A2:     B.s = F1(s) by A1;
        consider r being positive Element of REAL;
        set a = |[s`1,r]|;
        Ball(a,r) \/ {s} in B.s by A2;
        hence thesis;
      end;
      suppose not P[z];
        then
A3:     B.s = F2(s) by A1;
        consider r being positive Element of REAL;
        Ball(s,r) /\ X in B.s by A3;
        hence thesis;
      end;
    end;
    then reconsider B as non-empty ManySortedSet of X;
A4: rng B c= bool bool X
    proof
      let w be set;
      assume w in rng B;
      then consider a being set such that
A5:   a in dom B & w = B.a by FUNCT_1:def 5;
      reconsider a as Element of X by A5,PARTFUN1:def 4;
      w c= bool X
      proof
        let z be set;
        assume
A6:     z in w;
        per cases by A1,A5;
        suppose w = F1(a);
          then consider r being Element of REAL such that
A7:       z = Ball(|[a`1,r]|,r) \/ {a} & r > 0 by A6;
          Ball(|[a`1,r]|,r) c= X & {a} c= X by A7,Th24;
          then z c= X by A7,XBOOLE_1:8;
          hence thesis;
        end;
        suppose w = F2(a);
          then ex r being Element of REAL st z = Ball(a,r) /\ X & r > 0 by A6;
          then z c= X by XBOOLE_1:17;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
A8: for x,U being set st x in X & U in B.x holds x in U
    proof
      let x,U be set;
      assume x in X;
      then reconsider a = x as Element of X;
      assume
A9:   U in B.x;
      per cases by A1;
      suppose B.x = F1(a);
        then consider r being Element of REAL such that
A10:    U = Ball(|[a`1,r]|,r) \/ {a} & r > 0 by A9;
        a in {a} by TARSKI:def 1;
        hence thesis by A10,XBOOLE_0:def 3;
      end;
      suppose B.x = F2(a);
        then consider r being Element of REAL such that
A11:    U = Ball(a,r) /\ X & r > 0 by A9;
        a in Ball(a,r) by A11,Th17;
        hence thesis by A11,XBOOLE_0:def 4;
      end;
    end;
A12: for x,y,U being set st x in U & U in B.y & y in X
    ex V being set st V in B.x & V c= U
    proof
      let x,y,U be set;
      assume
A13:  x in U;
      assume
A14:  U in B.y;
      assume y in X;
      then reconsider y as Element of X;
      per cases;
      suppose P[y];
        then B.y = F1(y) by A1;
        then consider r being Element of REAL such that
A15:    U = Ball(|[y`1,r]|,r) \/ {y} & r > 0 by A14;
A16:    x in Ball(|[y`1,r]|,r) or x = y by A13,A15,SETWISEO:6;
        then reconsider z = x as Element of TOP-REAL 2;
        now
          assume
A17:      x in Ball(|[y`1,r]|,r);
          Ball(|[y`1,r]|,r) c= X &
          Ball(|[y`1,r]|,r) misses y=0-line by A15,Th24,Th25;
          then x in X & not P[x] by A17,XBOOLE_0:3;
          then
A18:      B.z = F2(z) by A1;
          set r2 = r-|.z-|[y`1,r]|.|;
          take V = Ball(z,r2) /\ X;
          |.z-|[y`1,r]|.| < r by A17,TOPREAL9:7;
          then reconsider r1 = r, r2 as positive Real by XREAL_1:52;
          r2 > 0;
          hence V in B.x by A18;
          r1-r2 = |.|[y`1,r]|-z.| by TOPRNS_1:28;
          then V c= Ball(z,r2) & Ball(z,r2) c= Ball(|[y`1,r]|,r1)
          by Th26,XBOOLE_1:17;
          then V c= Ball(|[y`1,r]|,r1) & Ball(|[y`1,r]|,r) c= U
          by A15,XBOOLE_1:1,7;
          hence V c= U by XBOOLE_1:1;
        end;
        hence thesis by A14,A16;
      end;
      suppose not P[y];
        then B.y = F2(y) by A1;
        then consider r being Element of REAL such that
A19:    U = Ball(y,r) /\ X & r > 0 by A14;
        reconsider z = x as Element of X by A13,A19,XBOOLE_0:def 4;
        set r2 = r-|.z-y.|;
        z in Ball(y,r) by A13,A19,XBOOLE_0:def 4;
        then |.z-y.| < r by TOPREAL9:7;
        then reconsider r1 = r, r2 as positive Real by XREAL_1:52;
A20:    z = |[z`1,z`2]| by EUCLID:57;
        per cases;
        suppose P[z];
          then
A21:      B.z = F1(z) & z`2 = 0 by A1,A20,Th19;
          set V = Ball(|[z`1,r2/2]|,r2/2) \/ {z};
          take V;
          thus V in B.x by A21;
          |[z`1,r2/2]|-z = |[z`1-z`1,r2/2-0]| & z`1-z`1 = 0
          by A20,A21,EUCLID:66;
          then |.|[z`1,r2/2]|-z.| = abs(r2/2) by TOPREAL6:31
            .= r2/2 by ABSVALUE:def 1;
          then
A22:      |.|[z`1,r2/2]|-y.| <= r2/2+|.z-y.| by TOPRNS_1:35;
          then |.|[z`1,r2/2]|-y.| <= r-r2/2 & r-r2/2 < r by XREAL_1:46;
          then |.|[z`1,r2/2]|-y.| < r by XXREAL_0:2;
          then |[z`1,r2/2]| in Ball(y,r) & |.y-|[z`1,r2/2]|.| <= r-r2/2
          by A22,TOPREAL9:7,TOPRNS_1:28;
          then Ball(|[z`1,r2/2]|,r2/2) c= X &
          Ball(|[z`1,r2/2]|,r2/2) c= Ball(y,r1) by Th24,Th26;
          then Ball(|[z`1,r2/2]|,r2/2) c= U & {z} c= U
          by A13,A19,XBOOLE_1:19,ZFMISC_1:37;
          hence thesis by XBOOLE_1:8;
        end;
        suppose not P[z];
          then
A23:      B.z = F2(z) by A1;
          take V = Ball(z,r2) /\ X;
          thus V in B.x by A23;
          |.y-z.| = r1-r2 by TOPRNS_1:28;
          hence thesis by A19,Th26,XBOOLE_1:26;
        end;
      end;
    end;
    for x,U1,U2 being set st x in X & U1 in B.x & U2 in B.x
    ex U being set st U in B.x & U c= U1 /\ U2
    proof
      let x,U1,U2 be set;
      assume x in X;
      then reconsider z = x as Element of X;
      assume
A24:  U1 in B.x & U2 in B.x;
      per cases;
      suppose P[z];
        then
A25:    B.x = F1(z) by A1;
        then consider r1 being Real such that
A26:    U1 = Ball(|[z`1,r1]|,r1) \/ {z} & r1 > 0 by A24;
        consider r2 being Real such that
A27:    U2 = Ball(|[z`1,r2]|,r2) \/ {z} & r2 > 0 by A24,A25;
        r1 <= r2 or r1 >= r2;
        then consider U being set such that
A28:    r1 <= r2 & U = U1 or r1 >= r2 & U = U2;
        take U;
        thus U in B.x by A24,A28;
        U c= U1 & U c= U2 by A26,A27,A28,Th27,XBOOLE_1:9;
        hence thesis by XBOOLE_1:19;
      end;
      suppose not P[z];
        then
A29:    B.x = F2(z) by A1;
        then consider r1 being Real such that
A30:    U1 = Ball(z,r1) /\ X & r1 > 0 by A24;
        consider r2 being Real such that
A31:    U2 = Ball(z,r2) /\ X & r2 > 0 by A24,A29;
        r1 <= r2 or r1 >= r2;
        then consider U being set such that
A32:    r1 <= r2 & U = U1 or r1 >= r2 & U = U2;
        take U;
        thus U in B.x by A24,A32;
        U c= U1 & U c= U2 by A30,A31,A32,JORDAN:18,XBOOLE_1:26;
        hence thesis by XBOOLE_1:19;
      end;
    end;
    then consider P being Subset-Family of X such that
A33: P = Union B & for T being TopStruct
    st the carrier of T = X & the topology of T = UniCl P
    holds T is TopSpace & for T' being non empty TopSpace st T' = T
    holds B is Neighborhood_System of T' by A4,A8,A12,TOPGEN_3:3;
    set T = TopStruct(#X, UniCl P#);
    reconsider T as non empty strict TopSpace by A33;
    reconsider B as Neighborhood_System of T by A33;
    take T; thus the carrier of T = X;
    take B;
    hereby
      let x;
      deffunc a1(Real) = Ball(|[x,$1]|,$1) \/ {|[x,0]|};
      deffunc a2(Real) = Ball(|[|[x,0]|`1,$1]|,$1) \/ {|[x,0]|};
      defpred R[Real] means $1 > 0;
A34:  for r being Real holds a1(r) = a2(r) by EUCLID:56;
A35:  {a1(r) where r is Real: R[r]} = {a2(r) where r is Real: R[r]}
      from FRAENKEL:sch 5(A34);
      P[ |[x,0]| ] & |[x,0]| in X;
      hence B.(|[x,0]|) = {Ball(|[x,r]|,r) \/ {|[x,0]|} where
      r is Element of REAL: r > 0} by A1,A35;
    end;
    let x,y;
    assume y > 0;
    then |[x,y]| in X & not |[x,y]| in y=0-line by Th19;
    hence thesis by A1;
  end;
  uniqueness
  proof
    let T1,T2 be strict non empty TopSpace such that
A36: the carrier of T1 = y>=0-plane and
A37: ex B being Neighborhood_System of T1 st (for x holds B.(|[x,0]|) =
    {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0}) &
    (for x,y st y > 0 holds B.(|[x,y]|) =
    {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0}) and
A38: the carrier of T2 = y>=0-plane and
A39: ex B being Neighborhood_System of T2 st (for x holds B.(|[x,0]|) =
    {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0}) &
    (for x,y st y > 0 holds B.(|[x,y]|) =
    {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0});
    consider B1 being Neighborhood_System of T1 such that
A40: for x holds B1.(|[x,0]|) =
    {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0} and
A41: for x,y st y > 0 holds B1.(|[x,y]|) =
    {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0} by A37;
    consider B2 being Neighborhood_System of T2 such that
A42: for x holds B2.(|[x,0]|) =
    {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0} and
A43: for x,y st y > 0 holds B2.(|[x,y]|) =
    {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0} by A39;
    now
      let a be set;
      assume a in y>=0-plane;
      then reconsider z = a as Element of y>=0-plane;
A44:  z = |[z`1,z`2]| by EUCLID:57;
      then z`2 = 0 or z`2 > 0 by Th22;
      then z`2 = 0 & B1.z = {Ball(|[z`1,r]|,r) \/ {|[z`1,0]|} where
      r is Element of REAL: r > 0} & B2.z = {Ball(|[z`1,r]|,r) \/
      {|[z`1,0]|} where r is Element of REAL: r > 0} or
      z`2 > 0 & B1.z = {Ball(|[z`1,z`2]|,r) /\ y>=0-plane where
      r is Element of REAL: r > 0} & B2.z = {Ball(|[z`1,z`2]|,r) /\
      y>=0-plane where r is Element of REAL: r > 0} by A40,A41,A42,A43,A44;
      hence B1.a = B2.a;
    end;
    then B1 = B2 by A36,A38,PBOOLE:3;
    hence thesis by Th28;
  end;
end;

theorem Th29:
  y>=0-plane \ y=0-line is open Subset of Niemytzki-plane
proof
  consider BB being Neighborhood_System of Niemytzki-plane such that
  for x holds BB.(|[x,0]|) =
  {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0} and
A1: for x,y st y > 0 holds BB.(|[x,y]|) =
  {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0} by Def3;
A2: the carrier of Niemytzki-plane = y>=0-plane by Def3;
  then reconsider A = y>=0-plane \ y=0-line as Subset of Niemytzki-plane
  by XBOOLE_1:36;
  now
    let a be Point of Niemytzki-plane;
    assume a in A;
    then
A3: a in y>=0-plane & not a in y=0-line by XBOOLE_0:def 5;
    then consider x,y such that
A4: a = |[x,y]| & y >= 0;
    set B = Ball(|[x,y]|,y)/\y>=0-plane;
    reconsider B as Subset of Niemytzki-plane by A2,XBOOLE_1:17;
    take B;
A5: y <> 0 by A3,A4;
    then B in {Ball(|[x,y]|,r) /\ y>=0-plane where
    r is Element of REAL : r > 0} by A4;
    then
A6: B in BB.a & dom BB = the carrier of Niemytzki-plane
    by A1,A4,A5,PARTFUN1:def 4;
    hence B in Union BB by CARD_5:10;
    thus a in B by A6,YELLOW_8:21;
    Ball(|[x,y]|,y) c= y>=0-plane by A4,A5,Th24;
    then B = Ball(|[x,y]|,y) by XBOOLE_1:28;
    then B misses y=0-line by A4,A5,Th25;
    hence B c= A by A2,XBOOLE_1:86;
  end;
  hence thesis by YELLOW_9:31;
end;

Lm1: the carrier of Niemytzki-plane = y>=0-plane by Def3;

theorem Th30:
  y=0-line is closed Subset of Niemytzki-plane
proof
  reconsider A = y>=0-plane \ y=0-line as open Subset of Niemytzki-plane
  by Th29;
  reconsider B = y=0-line as Subset of Niemytzki-plane by Def3,Th23;
  B` = A by Def3;
  then A` = y=0-line;
  hence thesis;
end;

theorem Th31:
  for x being real number, r being positive (real number)
  holds Ball(|[x,r]|,r) \/ {|[x,0]|} is open Subset of Niemytzki-plane
proof
  let x be real number;
  let r be positive (real number);
  the carrier of Niemytzki-plane = y>=0-plane by Def3;
  then reconsider a = |[x,0]| as Point of Niemytzki-plane by Th22;
  consider BB being Neighborhood_System of Niemytzki-plane such that
A1: for x holds BB.(|[x,0]|) =
  {Ball(|[x,q]|,q) \/ {|[x,0]|} where q is Element of REAL: q > 0} and
  for x,y st y > 0 holds BB.(|[x,y]|) =
  {Ball(|[x,y]|,q) /\ y>=0-plane where q is Element of REAL: q > 0} by Def3;
A2: x is Real & r is Real by XREAL_0:def 1;
  then BB.(|[x,0]|) =
  {Ball(|[x,q]|,q) \/ {|[x,0]|} where q is Element of REAL: q > 0} by A1;
  then Ball(|[x,r]|,r) \/ {|[x,0]|} in BB.a by A2;
  hence thesis
  by YELLOW_8:21;
end;

theorem Th32:
  for x being real number for y,r being positive (real number)
  holds Ball(|[x,y]|,r) /\ y>=0-plane is open Subset of Niemytzki-plane
proof
  let x be real number;
  let y,r be positive (real number);
  the carrier of Niemytzki-plane = y>=0-plane by Def3;
  then reconsider a = |[x,y]| as Point of Niemytzki-plane by Th22;
  consider BB being Neighborhood_System of Niemytzki-plane such that
  for x holds BB.(|[x,0]|) =
  {Ball(|[x,q]|,q) \/ {|[x,0]|} where q is Element of REAL: q > 0} and
A1: for x,y st y > 0 holds BB.(|[x,y]|) =
  {Ball(|[x,y]|,q) /\ y>=0-plane where q is Element of REAL: q > 0} by Def3;
A2: x is Real & y is Real & r is Real by XREAL_0:def 1;
  then BB.(|[x,y]|) =
  {Ball(|[x,y]|,q) /\ y>=0-plane where q is Element of REAL: q > 0} by A1;
  then Ball(|[x,y]|,r) /\ y>=0-plane in BB.a by A2;
  hence thesis
  by YELLOW_8:21;
end;

theorem Th33:
  for x,y being real number for r being positive (real number) st r <= y
  holds Ball(|[x,y]|,r) is open Subset of Niemytzki-plane
proof
  let x,y be real number;
  let r be positive (real number);
  assume
A1: r <= y;
  then
A2: Ball(|[x,y]|,r) /\ y>=0-plane is open Subset of Niemytzki-plane by Th32;
  Ball(|[x,y]|,r) c= y>=0-plane
  proof
    let a be set;
    assume
A3: a in Ball(|[x,y]|,r);
    then reconsider z = a as Element of TOP-REAL 2;
A4: z = |[z`1,z`2]| by EUCLID:57;
    then z-|[x,y]| = |[z`1-x,z`2-y]| by EUCLID:66;
    then (z-|[x,y]|)`1 = z`1-x & (z-|[x,y]|)`2 = z`2-y by EUCLID:56;
    then z`1-x is Real & z`2-y is Real &
    |.z-|[x,y]|.| = sqrt((z`1-x)^2+(z`2-y)^2) by JGRAPH_1:47;
    then |.z-|[x,y]|.| >= abs(z`2-y) by COMPLEX1:165;
    then |.z-|[x,y]|.| >= abs(y-z`2) & |.z-|[x,y]|.| < r
    by A3,COMPLEX1:146,TOPREAL9:7;
    then
A5: abs(y-z`2) < r by XXREAL_0:2;
    z`2 < 0 implies y-z`2 > y & abs(y-z`2) = y-z`2
         by A1,ABSVALUE:def 1,XREAL_1:48;
    hence thesis by A1,A4,A5,XXREAL_0:2;
  end;
  hence thesis by A2,XBOOLE_1:28;
end;

theorem Th34:
  for p being Point of Niemytzki-plane for r being positive (real number)
  ex a being Point of TOP-REAL 2, U being open Subset of Niemytzki-plane
  st p in U & a in U &
  for b being Point of TOP-REAL 2 st b in U holds |.b-a.| < r
proof
  let p be Point of Niemytzki-plane;
  let r be positive (real number);
  consider BB being Neighborhood_System of Niemytzki-plane such that
A1: for x holds BB.(|[x,0]|) =
  {Ball(|[x,q]|,q) \/ {|[x,0]|} where q is Element of REAL: q > 0} and
A2: for x,y st y > 0 holds BB.(|[x,y]|) =
  {Ball(|[x,y]|,q) /\ y>=0-plane where q is Element of REAL: q > 0} by Def3;
A3: the carrier of Niemytzki-plane = y>=0-plane by Def3;
  p in the carrier of Niemytzki-plane;
  then reconsider p' = p as Point of TOP-REAL 2 by A3;
A4: p = |[p'`1,p'`2]| by EUCLID:57;
  per cases by A3,A4,Th22;
  suppose
A5: p'`2 = 0;
    then BB.p = {Ball(|[p'`1,q]|,q) \/ {|[p'`1,0]|} where q is Element of
    REAL: q > 0} & r/2 is Real by A1,A4;
    then Ball(|[p'`1,r/2]|,r/2) \/ {|[p'`1,0]|} in BB.p &
    BB.p c= the topology of Niemytzki-plane by YELLOW_8:def 2;
    then reconsider U = Ball(|[p'`1,r/2]|,r/2) \/ {p} as
    open Subset of Niemytzki-plane by A4,A5,PRE_TOPC:def 5;
    take a = |[p'`1,r/2]|, U;
    p'-a = |[p'`1-p'`1,0-r/2]| & 0-r/2 is Real by A4,A5,EUCLID:66;
    then
A6: |.p'-a.| = |.0-r/2.| by TOPREAL6:31
      .= |.r/2-0 .| by COMPLEX1:146
      .= r/2 by ABSVALUE:def 1;
    thus p in U by SETWISEO:6;
    a in Ball(a,r/2) by Th17;
    hence a in U by XBOOLE_0:def 3;
    let b be Point of TOP-REAL 2;
    assume b in U;
    then b in Ball(a,r/2) or b = p by SETWISEO:6;
    then |.b-a.| <= r/2 & r/2 < r/2+r/2 by A6,TOPREAL9:7,XREAL_1:31;
    hence thesis by XXREAL_0:2;
  end;
  suppose
A7: p'`2 > 0;
    then BB.p = {Ball(|[p'`1,p'`2]|,q) /\ y>=0-plane where q is Element of
    REAL: q > 0} & r/2 is Real by A2,A4;
    then Ball(p',r/2) /\ y>=0-plane in BB.p &
    BB.p c= the topology of Niemytzki-plane by A4,YELLOW_8:def 2;
    then reconsider U = Ball(p',r/2) /\ y>=0-plane as
    open Subset of Niemytzki-plane by PRE_TOPC:def 5;
    take a = p', U;
    p in Ball(a,r/2) & p in y>=0-plane by A4,A7,Th17;
    hence p in U & a in U by XBOOLE_0:def 4;
    let b be Point of TOP-REAL 2;
    assume b in U;
    then b in Ball(a,r/2) by XBOOLE_0:def 4;
    then |.b-a.| <= r/2 & r/2 < r/2+r/2 by TOPREAL9:7,XREAL_1:31;
    hence thesis by XXREAL_0:2;
  end;
end;

theorem Th35:
  for x,y being real number for r being positive (real number)
  ex w,v being rational number
  st |[w,v]| in Ball(|[x,y]|,r) & |[w,v]| <> |[x,y]|
proof
  let x,y be real number;
  let r be positive (real number);
  x < x+r/2 by XREAL_1:41;
  then consider w being rational number such that
A1: x < w & w < x+r/2 by RAT_1:22;
  y < y+r/2 by XREAL_1:41;
  then consider v being rational number such that
A2: y < v & v < y+r/2 by RAT_1:22;
  take w,v;
  w-x is Real & v-y is Real &
  |[w,v]|-|[x,v]| = |[w-x,v-v]| & |[x,v]|-|[x,y]| = |[x-x,v-y]|
  by EUCLID:66,XREAL_0:def 1;
  then |.|[w,v]|-|[x,v]|.| = abs(w-x) & |.|[x,v]|-|[x,y]|.| = |.v-y.| &
  w-x > 0 & v-y > 0 by A1,A2,TOPREAL6:31,XREAL_1:52;
  then |.|[w,v]|-|[x,v]|.| = w-x & |.|[x,v]|-|[x,y]|.| = v-y
  by ABSVALUE:def 1;
  then |.|[w,v]|-|[x,v]|.| < x+r/2-x & |.|[x,v]|-|[x,y]|.| <= y+r/2-y
  by A1,A2,XREAL_1:11;
  then |.|[w,v]|-|[x,y]|.| <= |.|[w,v]|-|[x,v]|.|+|.|[x,v]|-|[x,y]|.| &
  |.|[w,v]|-|[x,v]|.|+|.|[x,v]|-|[x,y]|.| < x+r/2-x+(y+r/2-y)
  by TOPRNS_1:35,XREAL_1:10;
  then |.|[w,v]|-|[x,y]|.| < r by XXREAL_0:2;
  hence |[w,v]| in Ball(|[x,y]|,r) by TOPREAL9:7;
  thus thesis by A2,SPPOL_2:1;
end;

theorem Th36:
  for A being Subset of Niemytzki-plane
  st A = (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>
  for x being set holds Cl (A \ {x}) = [#] Niemytzki-plane
proof
  let A be Subset of Niemytzki-plane;
  assume
A1: A = (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>;
  let s be set;
  thus Cl (A\{s}) c= [#] Niemytzki-plane;
  let x be set;
  assume x in [#] Niemytzki-plane;
  then reconsider a = x as Element of Niemytzki-plane;
  reconsider b = a as Element of y>=0-plane by Def3;
  consider BB being Neighborhood_System of Niemytzki-plane such that
A2: for x holds BB.(|[x,0]|) =
  {Ball(|[x,q]|,q) \/ {|[x,0]|} where q is Element of REAL: q > 0} and
A3: for x,y st y > 0 holds BB.(|[x,y]|) =
  {Ball(|[x,y]|,q) /\ y>=0-plane where q is Element of REAL: q > 0} by Def3;
A4: a = |[b`1,b`2]| by EUCLID:57;
  for U being set st U in BB.a holds U meets A\{s}
  proof
    let U be set;
    assume
A5: U in BB.a;
    per cases by A4,Th22;
    suppose
A6:   b`2 = 0;
      then BB.a = {Ball(|[b`1,q]|,q) \/ {|[b`1,0]|}
      where q is Element of REAL: q > 0} by A2,A4;
      then consider q being Real such that
A7:   U = Ball(|[b`1,q]|,q) \/ {a} & q > 0 by A4,A5,A6;
      reconsider q as positive Real by A7;
      consider w1,v1 being rational number such that
A8:   |[w1,v1]| in Ball(|[b`1,q]|,q) & |[w1,v1]| <> |[b`1,q]| by Th35;
      set q2 = |.|[w1,v1]|-|[b`1,q]|.|;
      |[w1,v1]|-|[b`1,q]| <> 0.TOP-REAL 2 by A8,EUCLID:47;
      then q2 >= 0 & q2 <> 0 by EUCLID_2:64;
      then reconsider q2 as positive (real number);
      consider w2,v2 being rational number such that
A9:   |[w2,v2]| in Ball(|[b`1,q]|,q2) & |[w2,v2]| <> |[b`1,q]| by Th35;
      |.|[w2,v2]|-|[b`1,q]|.| < q2 & q2 < q by A8,A9,TOPREAL9:7;
      then |.|[w2,v2]|-|[b`1,q]|.| < q by XXREAL_0:2;
      then
A10:  |[w2,v2]| <> |[w1,v1]| & |[w2,v2]| in Ball(|[b`1,q]|,q)
      by A9,TOPREAL9:7;
      Ball(|[b`1,q]|,q) c= y>=0-plane & Ball(|[b`1,q]|,q) misses
      y=0-line by Th24,Th25;
      then |[w1,v1]| = <*w1,v1*> & |[w2,v2]| = <*w2,v2*> &
      w1 in RAT & v1 in RAT & w2 in RAT & v2 in RAT &
      Ball(|[b`1,q]|,q) c= y>=0-plane \ y=0-line by RAT_1:def 2,XBOOLE_1:86;
      then |[w1,v1]| in y>=0-plane \ y=0-line & |[w1,v1]| in product <*RAT,
      RAT*> &
      |[w2,v2]| in y>=0-plane \ y=0-line & |[w2,v2]| in product <*RAT,RAT*>
      by A8,A10,FUNCT_6:2;
      then
A11:  |[w1,v1]| in U & |[w2,v2]| in U & |[w1,v1]| in A & |[w2,v2]| in A
      by A1,A7,A8,A10,XBOOLE_0:def 3,def 4;
      (s = |[w1,v1]| or s <> |[w1,v1]|);
      then |[w1,v1]| in A\{s} or |[w2,v2]| in A\{s} by A10,A11,ZFMISC_1:64;
      hence thesis by A11,XBOOLE_0:3;
    end;
    suppose
A12:  b`2 > 0;
      then BB.a = {Ball(|[b`1,b`2]|,q) /\ y>=0-plane
      where q is Element of REAL: q > 0} by A3,A4;
      then consider q being Real such that
A13:  U = Ball(b,q) /\ y>=0-plane & q > 0 by A4,A5;
      reconsider q, b2 = b`2 as positive Real by A12,A13;
      reconsider q1 = min(q,b2) as positive Real by XXREAL_0:def 9;
      consider w1,v1 being rational number such that
A14:  |[w1,v1]| in Ball(b,q1) & |[w1,v1]| <> b by A4,Th35;
      set q2 = |.|[w1,v1]|-b.|;
      |[w1,v1]|-b <> 0.TOP-REAL 2 by A14,EUCLID:47;
      then q2 >= 0 & q2 <> 0 by EUCLID_2:64;
      then reconsider q2 as positive (real number);
      consider w2,v2 being rational number such that
A15:  |[w2,v2]| in Ball(b,q2) & |[w2,v2]| <> b by A4,Th35;
A16:  |.|[w2,v2]|-b.| < q2 & q2 < q1 by A14,A15,TOPREAL9:7;
      then |.|[w2,v2]|-b.| < q1 & q1 <= q by XXREAL_0:2,17;
      then
A17:  |[w2,v2]| in Ball(b,q1) & |.|[w2,v2]|-b.| < q & q2 < q
      by A16,TOPREAL9:7,XXREAL_0:2;
      then
A18:  |[w2,v2]| <> |[w1,v1]| & |[w1,v1]| in Ball(b,q) &
      |[w2,v2]| in Ball(b,q) by A15,TOPREAL9:7;
      q1 <= b`2 by XXREAL_0:17;
      then
A19:  Ball(b,q1) c= y>=0-plane & Ball(b,q1) misses y=0-line by A4,Th24,Th25;
      then |[w1,v1]| = <*w1,v1*> & |[w2,v2]| = <*w2,v2*> &
      w1 in RAT & v1 in RAT & w2 in RAT & v2 in RAT &
      Ball(b,q1) c= y>=0-plane \ y=0-line by RAT_1:def 2,XBOOLE_1:86;
      then |[w1,v1]| in y>=0-plane \ y=0-line & |[w1,v1]| in product <*RAT,
      RAT*> &
      |[w2,v2]| in y>=0-plane \ y=0-line & |[w2,v2]| in product <*RAT,RAT*>
      by A14,A17,FUNCT_6:2;
      then
A20:  |[w1,v1]| in U & |[w2,v2]| in U & |[w1,v1]| in A & |[w2,v2]| in A
      by A1,A13,A14,A17,A18,A19,XBOOLE_0:def 4;
      (s = |[w1,v1]| or s <> |[w1,v1]|);
      then |[w1,v1]| in A\{s} or |[w2,v2]| in A\{s} by A18,A20,ZFMISC_1:64;
      hence thesis by A20,XBOOLE_0:3;
    end;
  end;
  hence thesis by TOPGEN_2:11;
end;

theorem Th37:
  for A being Subset of Niemytzki-plane st A = y>=0-plane \ y=0-line
  for x being set holds Cl (A \ {x}) = [#] Niemytzki-plane
proof
  let A be Subset of Niemytzki-plane;
  assume
A1: A = y>=0-plane \ y=0-line;
  reconsider B = A /\ product <*RAT,RAT*> as Subset of Niemytzki-plane;
  let s be set;
  thus Cl (A\{s}) c= [#] Niemytzki-plane;
  B\{s} c= A\{s} by XBOOLE_1:17,33;
  then Cl (B\{s}) c= Cl (A\{s}) by PRE_TOPC:49;
  hence thesis by A1,Th36;
end;

theorem Th38:
  for A being Subset of Niemytzki-plane st A = y>=0-plane \ y=0-line
  holds Cl A = [#] Niemytzki-plane
proof
  let A be Subset of Niemytzki-plane;
  A\{A} = A
  proof
    thus A\{A} c= A by XBOOLE_1:36;
    let x be set;
    not A in A;
    hence thesis by ZFMISC_1:64;
  end;
  hence thesis by Th37;
end;

theorem Th39:
  for A being Subset of Niemytzki-plane st A = y=0-line
  holds Cl A = A & Int A = {}
proof
  let A be Subset of Niemytzki-plane;
  assume
A1: A = y=0-line;
  then
A2: A` = y>=0-plane \ y=0-line by Def3;
  thus Cl A = A by A1,Th30,PRE_TOPC:52;
  thus Int A = (Cl A`)` by TOPS_1:def 1
    .= ([#] Niemytzki-plane)` by A2,Th38
    .= {} by XBOOLE_1:37;
end;

theorem Th40:
  (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>
  is dense Subset of Niemytzki-plane
proof
  (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*> c= y>=0-plane \ y=0-line &
  y>=0-plane \ y=0-line is Subset of Niemytzki-plane by Th29,XBOOLE_1:17;
  then reconsider A = (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*> as
  Subset of Niemytzki-plane by XBOOLE_1:1;
  A\{A} = A
  proof
    thus A\{A} c= A by XBOOLE_1:36;
    let x be set;
    not A in A;
    hence thesis by ZFMISC_1:64;
  end;
  then Cl A = [#] Niemytzki-plane by Th36;
  hence thesis by TOPS_1:def 3;
end;

theorem
  (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>
  is dense-in-itself Subset of Niemytzki-plane
proof
  (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*> c= y>=0-plane \ y=0-line &
  y>=0-plane \ y=0-line is Subset of Niemytzki-plane by Th29,XBOOLE_1:17;
  then reconsider A = (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*> as
  Subset of Niemytzki-plane by XBOOLE_1:1;
  A is dense-in-itself
  proof
    let a be set;
    assume a in A;
    then reconsider x = a as Point of Niemytzki-plane;
    Cl (A\{x}) = the carrier of Niemytzki-plane by Th36;
    then x is_an_accumulation_point_of A by TOPGEN_1:def 2;
    hence thesis by TOPGEN_1:def 3;
  end;
  hence thesis;
end;

theorem
  y>=0-plane \ y=0-line is dense Subset of Niemytzki-plane
proof
  reconsider A = y>=0-plane \ y=0-line as open Subset of Niemytzki-plane
  by Th29;
  Cl A = [#] Niemytzki-plane by Th38;
  hence thesis by TOPS_1:def 3;
end;

theorem
  y>=0-plane \ y=0-line is dense-in-itself Subset of Niemytzki-plane
proof
  the carrier of Niemytzki-plane = y>=0-plane by Def3;
  then reconsider A = y>=0-plane \ y=0-line as Subset of Niemytzki-plane
  by XBOOLE_1:36;
  A is dense-in-itself
  proof
    let a be set;
    assume a in A;
    then reconsider x = a as Point of Niemytzki-plane;
    Cl (A\{x}) = the carrier of Niemytzki-plane by Th37;
    then x is_an_accumulation_point_of A by TOPGEN_1:def 2;
    hence thesis by TOPGEN_1:def 3;
  end;
  hence thesis;
end;

theorem
  y=0-line is nowhere_dense Subset of Niemytzki-plane
proof
  reconsider A = y=0-line as Subset of Niemytzki-plane by Def3,Th23;
  A is nowhere_dense
  proof
    thus Int Cl A = Int A by Th30,PRE_TOPC:52
      .= {} by Th39;
  end;
  hence thesis;
end;

theorem Th45:
  for A being Subset of Niemytzki-plane st A = y=0-line holds Der A is empty
proof
  let A be Subset of Niemytzki-plane;
  assume that
A1: A = y=0-line and
A2: Der A is not empty;
  consider BB being Neighborhood_System of Niemytzki-plane such that
A3: for x holds BB.(|[x,0]|) =
  {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0} and
  for x,y st y > 0 holds BB.(|[x,y]|) =
  {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0} by Def3;
  consider a being Element of Der A;
  a in Der A by A2;
  then reconsider a as Point of Niemytzki-plane;
  the carrier of Niemytzki-plane = y>=0-plane by Def3;
  then a in y>=0-plane;
  then reconsider b = a as Point of TOP-REAL 2;
A4: a = |[b`1,b`2]| by EUCLID:57;
  a in Der A & Der A c= Cl A & Cl A = A by A1,A2,Th39,TOPGEN_1:30;
  then a is_an_accumulation_point_of A & a in A by TOPGEN_1:def 3;
  then
A5: a in Cl (A \ {a}) & b`2 = 0 by A1,A4,Th19,TOPGEN_1:def 2;
  BB.a = {Ball(|[b`1,r]|,r) \/ {|[b`1,0]|} where r is Element of REAL:r > 0 }
  by A3,A4,A5;
  then Ball(|[b`1,1]|,1) \/ {b} in BB.a by A4,A5;
  then Ball(|[b`1,1]|,1) \/ {b} meets A \ {a} by A5,TOPGEN_2:10;
  then consider z being set such that
A6: z in Ball(|[b`1,1]|,1) \/ {b} & z in A \ {a} by XBOOLE_0:3;
A7: z in A & z <> a by A6,ZFMISC_1:64;
  then
A8: z in Ball(|[b`1,1]|,1) by A6,SETWISEO:6;
  reconsider z as Point of TOP-REAL 2 by A6;
A9: z = |[z`1,z`2]| by EUCLID:57;
  then z`2 = 0 by A1,A7,Th19;
  then z-|[b`1,1]| = |[z`1-b`1,0-1]| & |[z`1-b`1,0-1]|`1 = z`1-b`1 &
  |[z`1-b`1,0-1]|`2 = 0-1 by A9,EUCLID:56,66;
  then |.z-|[b`1,1]|.| = sqrt((z`1-b`1)^2+(-1)^2) by JGRAPH_1:47
    .= sqrt((z`1-b`1)^2+1^2);
  then |.z-|[b`1,1]|.| >= abs 1 & |.z-|[b`1,1]|.| < 1
  by A8,COMPLEX1:165,TOPREAL9:7;
  then abs 1 < 1 by XXREAL_0:2;
  hence contradiction by ABSVALUE:11;
end;

theorem Th46:
  for A being Subset of y=0-line holds A is closed Subset of Niemytzki-plane
proof
  let A be Subset of y=0-line;
  reconsider B = y=0-line as closed Subset of Niemytzki-plane by Th30;
  A c= B;
  then reconsider A as Subset of Niemytzki-plane by XBOOLE_1:1;
  Der A c= Der B by TOPGEN_1:32;
  then Der A c= {} by Th45;
  then Der A = {};
  then Cl A = A \/ {} by TOPGEN_1:31;
  hence thesis;
end;

theorem Th47:
  RAT is dense Subset of Sorgenfrey-line
proof
A1: the carrier of Sorgenfrey-line = REAL by TOPGEN_3:def 2;
  reconsider A = RAT as Subset of Sorgenfrey-line
  by NUMBERS:12,TOPGEN_3:def 2;
  consider B being Subset-Family of REAL such that
A2: the topology of Sorgenfrey-line = UniCl B and
A3: B = {[.x,q.[ where x is Element of REAL, q is Element of REAL:
  x < q & q is rational} by TOPGEN_3:def 2;
A4: B is Basis of Sorgenfrey-line by A1,A2,YELLOW_9:22;
  A is dense
  proof
    thus Cl A c= the carrier of Sorgenfrey-line;
    let x be set;
    assume x in the carrier of Sorgenfrey-line;
    then reconsider x as Point of Sorgenfrey-line;
    now
      let C be Subset of Sorgenfrey-line;
      assume C in B;
      then consider y,q being Real such that
A5:   C = [.y,q.[ & y < q & q is rational by A3;
      consider r being rational set such that
A6:   y < r & r < q by A5,RAT_1:22;
      assume x in C;
      r in A & r in C by A5,A6,RAT_1:def 2,XXREAL_1:3;
      hence A meets C by XBOOLE_0:3;
    end;
    hence thesis by A4,YELLOW_9:37;
  end;
  hence thesis;
end;

theorem
  Sorgenfrey-line is separable
proof
  reconsider A = RAT as dense Subset of Sorgenfrey-line by Th47;
  density Sorgenfrey-line c= card A by TOPGEN_1:def 12;
  hence density Sorgenfrey-line c= omega by TOPGEN_3:17;
end;

theorem
  Niemytzki-plane is separable
proof
  reconsider A = (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>
  as dense Subset of Niemytzki-plane by Th40;
  A c= product <*RAT,RAT*> by XBOOLE_1:17;
  then density Niemytzki-plane c= card A & card A c= card product <*RAT,
  RAT*> by CARD_1:27,TOPGEN_1:def 12;
  then density Niemytzki-plane c= card product <*RAT,RAT*> by XBOOLE_1:1;
  hence density Niemytzki-plane c= omega by Th12,CARD_4:54,TOPGEN_3:17;
end;

theorem
  Niemytzki-plane is T_1
proof
  set T = Niemytzki-plane;
  let p,q be Point of T such that
A1: p <> q;
  p in the carrier of T & q in the carrier of T &
  the carrier of T = y>=0-plane by Def3;
  then reconsider p' = p, q' = q as Point of TOP-REAL 2;
  p'-q' <> 0.TOP-REAL 2 by A1,EUCLID:47;
  then |.p'-q'.| <> 0 & |.p'-q'.| >= 0 by EUCLID_2:64;
  then reconsider r = |.p'-q'.| as positive Real;
  consider ap being Point of TOP-REAL 2, Up being open Subset of T such that
A2: p in Up & ap in Up &
  for b being Point of TOP-REAL 2 st b in Up holds |.b-ap.| < r/2 by Th34;
  consider aq being Point of TOP-REAL 2, Uq being open Subset of T such that
A3: q in Uq & aq in Uq &
  for b being Point of TOP-REAL 2 st b in Uq holds |.b-aq.| < r/2 by Th34;
  take Up,Uq; thus Up is open & Uq is open & p in Up by A2;
  thus not q in Up
  proof
    assume q in Up;
    then |.q'-ap.| = |.ap-q'.| & |.q'-ap.| < r/2 & |.p'-ap.| < r/2
    by A2,TOPRNS_1:28;
    then |.p'-ap.|+|.ap-q'.| < r/2+r/2 & |.p'-ap.|+|.ap-q'.| >= r
    by TOPRNS_1:35,XREAL_1:10;
    hence contradiction;
  end;
  thus q in Uq by A3;
  assume p in Uq;
  then |.q'-aq.| = |.aq-q'.| & |.q'-aq.| < r/2 & |.p'-aq.| < r/2
  by A3,TOPRNS_1:28;
  then |.p'-aq.|+|.aq-q'.| < r/2+r/2 & |.p'-aq.|+|.aq-q'.| >= r
  by TOPRNS_1:35,XREAL_1:10;
  hence contradiction;
end;

theorem
  Niemytzki-plane is not normal
proof
  set T = Niemytzki-plane;
  assume
A1: for W, V being Subset of T st W <> {} & V <> {} &
  W is closed & V is closed & W misses V
  ex P, Q being Subset of T st P is open & Q is open &
  W c= P & V c= Q & P misses Q;
  reconsider C = (y>=0-plane \ y=0-line) /\ product <*RAT,RAT*>
  as dense Subset of Niemytzki-plane by Th40;
  defpred P[set,set] means ex U,V being open Subset of T st
  $2 = U /\ C & $1 c= U & y=0-line \ $1 c= V & U misses V;
A2: for a being set st a in bool y=0-line ex b being set st P[a,b]
  proof
    let a be set;
    assume a in bool y=0-line;
    then reconsider aa = a, a' = y=0-line \ a as Subset of y=0-line
    by XBOOLE_1:36;
    reconsider A = aa, B = a' as closed Subset of T by Th46;
    per cases;
    suppose
A3:   a = {};
      take b = {};
      take U = {}T, V = [#]T;
      thus thesis by A3,Def3,Th23,XBOOLE_1:65;
    end;
    suppose
A4:   a = y=0-line;
      take b = ([#]T)/\ C;
      take U = [#]T, V = {}T;
      thus thesis by A4,Def3,Th23,XBOOLE_1:37,65;
    end;
    suppose
A5:   a <> {} & a <> y=0-line;
      aa`` = a'` & aa`` = a & ({}y=0-line)` = [#]y=0-line;
      then B <> {}y=0-line & A misses B by A5,XBOOLE_1:79;
      then consider P, Q being Subset of T such that
A6:   P is open & Q is open & A c= P & B c= Q & P misses Q by A1,A5;
      take b = P /\ C;
      thus thesis by A6;
    end;
  end;
  consider G being Function such that
A7: dom G = bool y=0-line and
A8: for a being set st a in bool y=0-line holds P[a,G.a]
  from CLASSES1:sch 1(A2);
  rng G c= bool C
  proof
    let a be set;
    assume a in rng G;
    then consider b being set such that
A9: b in dom G & a = G.b by FUNCT_1:def 5;
    P[b,a] by A7,A8,A9;
    then a c= C by XBOOLE_1:17;
    hence thesis;
  end;
  then
A10: card rng G c= card bool C by CARD_1:27;
  card C c= card product <*RAT,RAT*> by CARD_1:27,XBOOLE_1:17;
  then card C c= omega by Th12,CARD_4:54,TOPGEN_3:17;
  then
A11: card bool C = exp(2, card C) & exp(2, card C) c= exp(2, omega)
  by CARD_2:44,CARD_3:139;
  G is one-to-one
  proof
    let x,y be set;
    assume x in dom G & y in dom G;
    then reconsider A = x, B = y as Subset of y=0-line by A7;
    consider UA,VA being open Subset of T such that
A12: G.A = UA /\ C & A c= UA & y=0-line \ A c= VA & UA misses VA by A8;
    consider UB,VB being open Subset of T such that
A13: G.B = UB /\ C & B c= UB & y=0-line \ B c= VB & UB misses VB by A8;
    assume
A14: G.x = G.y & x <> y;
    then consider z being set such that
A15: not (z in A iff z in B) by TARSKI:2;
A16: z in A\B or z in B\A by A15,XBOOLE_0:def 5;
    A` = y=0-line \ A & B` = y=0-line \ B & A\B = A/\B` & B\A = B/\A`
    by SUBSET_1:32;
    then A\B c= UA /\ VB & B\A c= UB /\ VA by A12,A13,XBOOLE_1:27;
    then z in UA /\ VB & UA /\ VB is open or
    z in UB /\ VA & UB /\ VA is open by A16,TOPS_1:38;
    then C meets UA /\ VB or C meets UB /\ VA by TOPS_1:80;
    then (ex z being set st z in C & z in UA /\ VB) or
    (ex z being set st z in C & z in UB /\ VA) by XBOOLE_0:3;
    then consider z being set such that
A17: z in C & (z in UA /\ VB or z in UB /\ VA);
    z in UA & z in VB or z in UB & z in VA by A17,XBOOLE_0:def 4;
    then z in UA & not z in UB or z in UB & not z in UA by A12,A13,XBOOLE_0:3;
    then z in G.A & not z in G.B or z in G.B & not z in G.A
    by A12,A13,A17,XBOOLE_0:def 4;
    hence thesis by A14;
  end;
  then card dom G c= card rng G by CARD_1:26;
  then card bool y=0-line c= card bool C by A7,A10,XBOOLE_1:1;
  then exp(2, continuum) c= card bool C by Th20,CARD_2:44;
  then exp(2, continuum) c= exp(2, omega) &
  exp(2, omega) in exp(2, exp(2, omega)) by A11,CARD_5:23,XBOOLE_1:1;
  then exp(2, omega) in exp(2, omega) by TOPGEN_3:29;
  hence contradiction;
end;

begin :: Tychonoff spaces

definition
  let T be TopSpace;
  attr T is Tychonoff means
  :Def4:
  for A being closed Subset of T, a being Point of T st a in A`
  ex f being continuous Function of T, I[01] st f.a = 0 & f.:A c= {1};
end;

registration
  cluster Tychonoff -> regular TopSpace;
  coherence
  proof
    let T be TopSpace;
    assume that
A2: for A being closed Subset of T, a being Point of T st a in A`
    ex f being continuous Function of T, I[01] st f.a = 0 & f.:A c= {1};
    let p be Point of T, P be Subset of T;
    assume
A3:  P is closed & p in P`;
    then consider f being continuous Function of T, I[01] such that
A4: f.p = 0 & f.:P c= {1} by A2;
    reconsider z = 0, j = 1, half = 1/2 as Element of I[01]
    by BORSUK_1:83,XXREAL_1:1;
    reconsider A = [.z,half.[, B = ].half,j.] as Subset of I[01]
    by BORSUK_1:83,XXREAL_1:35,36;
    reconsider A,B as open Subset of I[01] by TOPALG_1:4,5;
    take W = f"A, V = f"B;
    [#]I[01] <> {};
    hence W is open & V is open by TOPS_2:55;
    0 in A by XXREAL_1:3;
    hence p in W by A3,A4,FUNCT_2:46;
    1 in B by XXREAL_1:2;
    then {1} c= B by ZFMISC_1:37;
    then f.:P c= B & dom f = the carrier of T by A4,FUNCT_2:def 1,XBOOLE_1:1;
    hence P c= V by FUNCT_1:163;
    assume W meets V;
    then consider x being set such that
A5: x in W & x in V by XBOOLE_0:3;
A6: f.x in A & f.x in B by A5,FUNCT_1:def 13;
    then reconsider fx = f.x as Element of I[01];
    fx < half & fx > half by A6,XXREAL_1:2,3;
    hence thesis;
  end;
  cluster T_4 -> Tychonoff (non empty TopSpace);
  coherence
  proof
    let T be non empty TopSpace;
    assume
A7:  T is T_4;
    let A be closed Subset of T, a be Point of T;
    the carrier of Closed-Interval-TSpace(-1,1) = [.-1,1.] by TOPMETR:25;
    then reconsider j = 1, k = -1 as Point of Closed-Interval-TSpace(-1,1)
    by XXREAL_1:1;
    reconsider z = 0, o = 1 as Point of I[01] by BORSUK_1:83,XXREAL_1:1;
    per cases;
    suppose
A8:   A is empty;
      set f = T --> z;
      f = (the carrier of T) --> z & f.:A = {} by A8,RELAT_1:149;
      then f.a = z & f.:A c= {1} by FUNCOP_1:13,XBOOLE_1:2;
      hence thesis;
    end;
    suppose A is non empty;
      then reconsider aa = {a}, A' = A as non empty closed Subset of T
      by A7,URYSOHN1:27;
      reconsider B = A' \/ aa as closed Subset of T by TOPS_1:36;
      set h = (T|A' --> j)+*(T|aa --> k);
A9:   the carrier of T|A' = A & the carrier of T|aa = aa by PRE_TOPC:29;
A10:  T|A' --> j = A' --> 1 & T|aa --> k = aa --> k & (A' --> 1).:A c= {1}
      by BORSUK_1:6,PRE_TOPC:29;
      assume a in A`;
      then rng (T|A' --> j) c= {1} & rng (T|aa --> k) c= {k} & {j} misses {k}
      & not a in A by FUNCOP_1:19,XBOOLE_0:def 5,ZFMISC_1:17;
      then
A11:  A' misses aa & rng (T|A' --> j) misses rng (T|aa --> k)
      by XBOOLE_1:64,ZFMISC_1:56;
      then reconsider h as continuous Function of T|B, Closed-Interval-TSpace(-
      1,1) by Th15;
A12:  h.:A c= {1} by A10,A11,BOOLMARK:3;
A13:  dom (T|aa --> k) = aa & a in aa by A9,FUNCOP_1:19,TARSKI:def 1;
      then
A14:  h.a = (T|aa --> k).a by FUNCT_4:14
        .= -1 by A13,FUNCOP_1:13;
A15:  a in B & A c= B by A13,XBOOLE_0:def 3,XBOOLE_1:7;
      consider g being continuous Function of T, Closed-Interval-TSpace(-1,1)
      such that
A16:  g|B = h by A7,TIETZE:25;
      consider p being Function of I[01], Closed-Interval-TSpace(-1,1)
      such that
A17:  p is being_homeomorphism &
      (for r being Real st r in [.0,1.] holds p.r=2*r-1) &
      p.0=-1 & p.1=1 by JGRAPH_5:42;
      reconsider p' = p/" as continuous Function
      of Closed-Interval-TSpace(-1,1), I[01] by A17,TOPS_2:def 5;
      reconsider f = p'*g as continuous Function of T, I[01];
      take f;
  rng p = [#]Closed-Interval-TSpace(-1,1) & p is one-to-one
      by A17,TOPS_2:def 5;
      then
A19:  p' = p qua Function" by TOPS_2:def 4;
      then
A20:  p'.k = z by A17,FUNCT_2:32;
      thus f.a = p'.(g.a) by FUNCT_2:21
        .= 0 by A14,A15,A16,A20,FUNCT_1:72;
      f.:A = p'.:(g.:A) by RELAT_1:159
        .= p'.:(h.:A) by A15,A16,RELAT_1:162;
      then
A21:  f.:A c= Im(p',1) by A12,RELAT_1:156;
      p'.j = o by A17,A19,FUNCT_2:32;
      hence thesis by A21,SETWISEO:13;
    end;
  end;
end;

theorem
  for X being T_1 TopSpace st X is Tychonoff for B being prebasis of X
  for x being Point of X for V being Subset of X st x in V & V in B
  ex f being continuous Function of X, I[01] st f.x = 0 & f.:V` c= {1}
proof
  let X be T_1 TopSpace;
  assume
A1: X is Tychonoff;
  let B be prebasis of X;
  let x be Point of X;
  let V be Subset of X;
  assume
A2: x in V & V in B;
  B is open by YELLOW_9:28;
  then V is open by A2,TOPS_2:def 1;
  then V`` = V & V` is closed;
  hence thesis by A1,A2,Def4;
end;

theorem Th53:
  for X being TopSpace, R being non empty SubSpace of R^1
  for f,g being continuous Function of X,R for A being Subset of X st
  for x being Point of X holds x in A iff f.x <= g.x holds A is closed
proof
  let X be TopSpace;
  let R be non empty SubSpace of R^1;
  let f,g be continuous Function of X,R;
  let A be Subset of X;
  assume
A1: for x being Point of X holds x in A iff f.x <= g.x;
  now
    thus the topology of X is Basis of X by CANTOR_1:2;
    let p be Point of X;
    set r = f.p-g.p;
    reconsider U1 = ].f.p-r/2,f.p+r/2.[, V1 = ].g.p-r/2,g.p+r/2.[
    as open Subset of R^1 by JORDAN6:46,TOPMETR:24;
    reconsider U = U1/\[#]R, V = V1/\[#]R as open Subset of R by TOPS_2:32;
    assume
A2: p in A`;
    then not p in A by XBOOLE_0:def 5;
    then f.p > g.p by A1;
    then reconsider r as positive (real number) by XREAL_1:52;
    take B = f"U /\ g"V;
    f.p-r/2 < f.p & f.p < f.p+r/2 & g.p-r/2 < g.p & g.p < g.p+r/2
    by XREAL_1:31,46;
    then f.p in U1 & g.p in V1 & f.p in [#]R & g.p in [#]R
    by A2,FUNCT_2:7,XXREAL_1:4;
    then f.p in U & g.p in V by XBOOLE_0:def 4;
    then p in f"U & p in g"V & f"U is open & g"V is open
    by A2,FUNCT_2:46,TOPS_2:55;
    then p in B & B is open by TOPS_1:38,XBOOLE_0:def 4;
    hence B in the topology of X & p in B by PRE_TOPC:def 5;
    thus B c= A`
    proof
      let q be set;
      assume
A3:   q in B;
      then q in f"U & q in g"V by XBOOLE_0:def 4;
      then f.q in U & g.q in V by FUNCT_2:46;
      then f.q in U1 & g.q in V1 by XBOOLE_0:def 4;
      then f.q > f.p-r/2 & g.q < g.p+r/2 by XXREAL_1:4;
      then g.q < f.q by XXREAL_0:2;
      then not q in A by A1;
      hence thesis by A3,SUBSET_1:50;
    end;
  end;
  then A` is open by YELLOW_9:31;
  hence thesis by TOPS_1:29;
end;

theorem Th54:
  for X being TopSpace, R being non empty SubSpace of R^1
  for f,g being continuous Function of X,R
  ex h being continuous Function of X,R st
  for x being Point of X holds h.x = max(f.x,g.x)
proof
  let X be TopSpace;
  let R be non empty SubSpace of R^1;
  let f,g be continuous Function of X,R;
  defpred A[set] means f.$1 >= g.$1;
  consider A being Subset of X such that
A1: for a being set holds a in A iff a in the carrier of X & A[a]
  from SUBSET_1:sch 1;
  defpred B[set] means f.$1 <= g.$1;
  consider B being Subset of X such that
A2: for a being set holds a in B iff a in the carrier of X & B[a]
  from SUBSET_1:sch 1;
  per cases;
  suppose
A3: X is empty;
    consider h being continuous Function of X,R;
    take h;
    let x be Point of X;
    dom f = the carrier of X & dom g = the carrier of X &
    dom h = the carrier of X &
    the carrier of X is empty by A3,FUNCT_2:def 1;
    then h.x = 0 & f.x = 0 & g.x = 0 by FUNCT_1:def 4;
    hence thesis;
  end;
  suppose
A4: X is non empty & A is empty;
    take g;
    let x be Point of X;
    not x in A & the carrier of X <> {} by A4;
    then f.x < g.x by A1;
    hence thesis by XXREAL_0:def 10;
  end;
  suppose
A5: X is non empty & B is empty;
    take f;
    let x be Point of X;
    not x in B & the carrier of X <> {} by A5;
    then g.x < f.x by A2;
    hence thesis by XXREAL_0:def 10;
  end;
  suppose
A6: X is not empty & A is not empty & B is not empty;
    then reconsider X' = X as non empty TopSpace;
    for x being Point of X' holds (x in A iff f.x >= g.x) &
    (x in B iff f.x <= g.x) by A1,A2;
    then reconsider A' = A, B' = B as non empty closed Subset of X' by A6,Th53;
    reconsider ff = f, gg = g as continuous Function of X', R;
A7: dom ff = the carrier of X' & dom gg = the carrier of X' by FUNCT_2:def 1;
    then
A8: dom (ff|A') = A' & dom (gg|B') = B' by RELAT_1:91;
    the carrier of X'|A' = [#](X'|A')
      .= A' by PRE_TOPC:def 10;
    then reconsider f' = ff|A' as continuous Function of X'|A', R
    by A8,FUNCT_2:def 1,RELSET_1:33,TOPMETR:10;
    the carrier of X'|B' = [#](X'|B')
      .= B' by PRE_TOPC:def 10;
    then reconsider g' = gg|B' as continuous Function of X'|B', R
    by A8,FUNCT_2:def 1,RELSET_1:33,TOPMETR:10;
A9: A' \/ B' = the carrier of X'
    proof
      thus A' \/ B' c= the carrier of X';
      let a be set;
      f.a >= g.a or f.a <= g.a;
      then a in the carrier of X implies a in A' or a in B' by A1,A2;
      hence thesis by XBOOLE_0:def 3;
    end;
    then
A10: X'|(A' \/ B') = X' | [#]X'
      .= the TopStruct of X by TSEP_1:100;
A11: dom f' = A & dom g' = B by A7,RELAT_1:91;
A12: f' tolerates g'
    proof
      let a be set;
      assume a in dom f' /\ dom g';
      then
A13:  a in A & a in B by A11,XBOOLE_0:def 4;
      then f.a <= g.a & f.a >= g.a by A1,A2;
      then f.a = g.a by XXREAL_0:1;
      hence f'.a = g.a by A13,FUNCT_1:72
        .= g'.a by A13,FUNCT_1:72;
    end;
    then f'+*g' is continuous Function of X'|(A' \/ B'), R &
    the TopStruct of R = the TopStruct of R by Th14;

then reconsider h = f'+*g' as continuous Function of X,R by A10,YELLOW12:36;
    take h;
    let x be Point of X;
    x in the carrier of X' & (x in A' or x in B') by A9,XBOOLE_0:def 3;
    then x in A' & f.x >= g.x & h.x = f'.x & f.x = f'.x or
    x in B' & f.x <= g.x & h.x = g'.x & g.x = g'.x
    by A1,A2,A11,A12,FUNCT_1:72,FUNCT_4:14,16;
    hence thesis by XXREAL_0:def 10;
  end;
end;

theorem Th55:
  for X being non empty TopSpace, R being non empty SubSpace of R^1
  for A being finite non empty set for F being ManySortedFunction of A
  st for a being set st a in A holds F.a is continuous Function of X,R
  ex f being continuous Function of X,R st
  for x being Point of X, S being finite non empty Subset of REAL
  st S = rng ((commute F).x) holds f.x = max S
proof
  let X be non empty TopSpace;
  let R be non empty SubSpace of R^1;
  let A be finite non empty set;
  let F be ManySortedFunction of A;
  assume
A1: for a being set st a in A holds F.a is continuous Function of X,R;
  defpred P[set] means $1 is empty or ex f being continuous Function of X,R st
  for x being Point of X, S being finite non empty Subset of REAL
  st S = rng ((commute (F|$1)).x) holds f.x = max S;
A2: A is finite;
A3: P[{}];
A4: dom F = A by PARTFUN1:def 4;
  rng F c= Funcs(the carrier of X,the carrier of R)
  proof
    let a be set;
    assume a in rng F;
    then ex b being set st b in dom F & a = F.b by FUNCT_1:def 5;
    then a is Function of X,R by A1,A4;
    hence thesis by FUNCT_2:11;
  end;
  then
A5: F in Funcs(A,Funcs(the carrier of X,the carrier of R)) by A4,FUNCT_2:def 2;
A6: now
    let x,B be set;
    assume
A7: x in A;
    then reconsider fx = F.x as continuous Function of X,R by A1;
    assume
A8: B c= A;
    assume
A9: P[B];
    per cases;
    suppose
A10:  B = {};
      thus P[B \/ {x}]
      proof
        assume B \/ {x} is not empty;
        take fx;
        let a be Point of X, S be finite non empty Subset of REAL;
        assume
A11:    S = rng ((commute (F|(B \/ {x}))).a);
        F|{x} = x .--> (F.x) & dom fx = the carrier of X
        by A4,A7,FUNCT_2:def 1,FUNCT_7:6;
        then (commute (F|{x})).a = x .--> (fx.a) by Th3;
        then rng ((commute (F|{x})).a) = {fx.a} by FUNCOP_1:14;
        hence thesis by A10,A11,XXREAL_2:11;
      end;
    end;
    suppose
A12:  B <> {};
      then reconsider B' = B as non empty set;
      consider f being continuous Function of X,R such that
A13:  for x being Point of X, S being finite non empty Subset of REAL
      st S = rng ((commute (F|B')).x) holds f.x = max S by A9;
      consider h being continuous Function of X,R such that
A14:  for x being Point of X holds h.x = max(f.x,fx.x) by Th54;
      thus P[B \/ {x}]
      proof
        assume B \/ {x} is not empty;
        take h;
        let a be Point of X, S be finite non empty Subset of REAL;
        assume
A15:    S = rng ((commute (F|(B \/ {x}))).a);
        F is Function of A, Funcs(the carrier of X,the carrier of R)
        by A5,FUNCT_2:121;
        then F|B is Function of B, Funcs(the carrier of X,the carrier of R)
        by A8,FUNCT_2:38;
        then F|B in Funcs(B, Funcs(the carrier of X,the carrier of R))
        by FUNCT_2:11;
        then commute (F|B) in Funcs(the carrier of X, Funcs(B, the carrier
        of R)) by A12,FUNCT_6:85;
        then reconsider cFB = commute (F|B) as Function of the carrier of X,
        Funcs(B, the carrier of R) by FUNCT_2:121;
        reconsider cFBa = cFB.a as Function of B', the carrier of R
        by FUNCT_2:121;
        F|(B \/ {x}) = (F|B) \/ (F|{x}) by RELAT_1:107;
        then (commute (F|(B\/{x}))).a = cFB.a \/ (commute (F|{x})).a by Th7;
        then
A16:    S = rng cFBa \/ rng ((commute (F|{x})).a) by A15,RELAT_1:26;
        then rng cFBa c= S by XBOOLE_1:7;
        then reconsider S1 = rng cFBa as non empty finite Subset of REAL
        by XBOOLE_1:1;
A17:    f.a = max S1 by A13;
        F|{x} = x .--> (F.x) & dom fx = the carrier of X
        by A4,A7,FUNCT_2:def 1,FUNCT_7:6;
        then (commute (F|{x})).a = x .--> (fx.a) by Th3;
        then S = S1 \/ {fx.a} by A16,FUNCOP_1:14;
        then max S = max(f.a, max {fx.a}) by A17,XXREAL_2:67
          .= max(f.a, fx.a) by XXREAL_2:11;
        hence thesis by A14;
      end;
    end;
  end;
A18: P[A] from FINSET_1:sch 2(A2,A3,A6);
  F|A = F by A4,RELAT_1:98;
  hence thesis by A18;
end;

theorem Th56:
  for X being T_1 non empty TopSpace for B being prebasis of X
  st for x being Point of X for V being Subset of X st x in V & V in B
  ex f being continuous Function of X, I[01] st f.x = 0 & f.:V` c= {1}
  holds X is Tychonoff
proof
  let X be T_1 non empty TopSpace;
  let BB be prebasis of X such that
A1: for x being Point of X for V being Subset of X st x in V & V in BB
  ex f being continuous Function of X, I[01] st f.x = 0 & f.:V` c= {1};
  let A be closed Subset of X;
  let a be Point of X;
  assume
A2: a in A`;
  A` is open & FinMeetCl BB is Basis of X by YELLOW_9:23;
  then consider B being Subset of X such that
A3: B in FinMeetCl BB & a in B & B c= A` by A2,YELLOW_9:31;
  consider F being Subset-Family of X such that
A4: F c= BB & F is finite & B = Intersect F by A3,CANTOR_1:def 4;
  reconsider z = 0 as Point of I[01] by BORSUK_1:83,XXREAL_1:1;
  per cases;
  suppose F is empty;
    then B = the carrier of X by A4,SETFAM_1:def 10;
    then A`` = {}X & X-->z = (the carrier of X)-->z & X is non empty
    by A3,XBOOLE_1:37;
    then (X-->z).:A = {} & {} c= {1} & (X-->z).a = z & X-->z is continuous
    by FUNCOP_1:13,RELAT_1:149,XBOOLE_1:2;
    hence thesis;
  end;
  suppose F is non empty;
    then reconsider F as finite non empty Subset-Family of X by A4;
    defpred P[set,set] means
    ex S being Subset of X, f being continuous Function of X, I[01]
    st S = $1 & f = $2 & f.a = 0 & f.:S` c= {1};
A5: for x being set st x in F ex y being set st P[x,y]
    proof
      let x be set;
      assume
A6:   x in F;
      then reconsider S = x as Subset of X;
      a in S & S in BB by A3,A4,A6,SETFAM_1:58;
      then consider f being continuous Function of X, I[01] such that
A7:   f.a = 0 & f.:S` c= {1} by A1;
      take y = f;
      thus thesis by A7;
    end;
    consider G being Function such that
A8: dom G = F & for x being set st x in F holds P[x,G.x]
    from CLASSES1:sch 1(A5);
    G is Function-yielding
    proof
      let x be set;
      assume x in dom G;
      then P[x,G.x] by A8;
      hence thesis;
    end;
    then reconsider G as ManySortedFunction of F
     by A8,PARTFUN1:def 4,RELAT_1:def 18;
    now
      let a be set;
      assume a in F;
      then P[a,G.a] by A8;
      hence G.a is continuous Function of X, I[01];
    end;
    then consider f being continuous Function of X, I[01] such that
A9: for x being Point of X, S being finite non empty Subset of REAL
    st S = rng ((commute G).x) holds f.x = max S by Th55;
    take f;
    reconsider Sa = {0 qua Real} as finite non empty Subset of REAL;
    consider z being Element of F;
    set R = I[01];
    rng G c= Funcs(the carrier of X,the carrier of R)
    proof
      let u;
      assume u in rng G;
      then consider v such that
A10:  v in dom G & u = G.v by FUNCT_1:def 5;
      P[v,u] by A8,A10;
      hence thesis by FUNCT_2:11;
    end;
    then G is Function of F, Funcs(the carrier of X,the carrier of R)
    by A8,FUNCT_2:4;
    then
A11: G in Funcs(F, Funcs(the carrier of X,the carrier of R)) by FUNCT_2:11;
    then commute G in Funcs(the carrier of X, Funcs(F, the carrier of R))
    by FUNCT_6:85;
    then reconsider cG = commute G as Function of the carrier of X,
    Funcs(F, the carrier of R) by FUNCT_2:121;
    reconsider cGa = cG.a as Function of F, the carrier of R by FUNCT_2:121;
A12: dom cGa = F by FUNCT_2:def 1;
    Sa = rng ((commute G).a)
    proof
      thus Sa c= rng ((commute G).a)
      proof
        let x be set;
        assume x in Sa;
        then P[z,G.z] & x = 0 by A8,TARSKI:def 1;
        then x = (commute G).a.z by A11,FUNCT_6:86;
        hence thesis by A12,FUNCT_1:def 5;
      end;
      let x be set;
      assume x in rng ((commute G).a);
      then consider z being set such that
A13:  z in dom cGa & x = cGa.z by FUNCT_1:def 5;
      P[z,G.z] by A8,A13;
      then x = 0 by A11,A13,FUNCT_6:86;
      hence thesis by TARSKI:def 1;
    end;
    hence f.a = max Sa by A9
      .= 0 by XXREAL_2:11;
    let z be set;
    assume z in f.:A;
    then consider x being set such that
A14: x in dom f & x in A & z = f.x by FUNCT_1:def 12;
    reconsider x as Element of X by A14;
    reconsider cGx = cG.x as Function of F, the carrier of R by FUNCT_2:121;
    reconsider S = rng cGx as finite non empty Subset of REAL
    by BORSUK_1:83,XBOOLE_1:1;
A15: f.x = max S by A9;
A16: for r being ext-real number st r in S holds r <= 1
            by BORSUK_1:83,XXREAL_1:1;
      not x in B by A3,A14,XBOOLE_0:def 5;
      then consider w such that
A18:  w in F & not x in w by A4,SETFAM_1:58;
      consider T being Subset of X, g being continuous Function of X, R
      such that
A19:  T = w & g = G.w & g.a = 0 & g.:T` c= {1} by A8,A18;
      x in T` by A18,A19,SUBSET_1:50;
      then g.x in g.:T` by FUNCT_2:43;
      then g.x = 1 by A19,TARSKI:def 1;
      then w in dom cGx & cGx.w = 1 by A11,A18,A19,FUNCT_6:86;
    then 1 in S by FUNCT_1:def 5;
    then max S = 1 by A16,XXREAL_2:def 8;
    hence thesis by A14,A15,TARSKI:def 1;
  end;
end;

theorem Th57:
  Sorgenfrey-line is T_1
proof
  set T = Sorgenfrey-line;
  consider B being Subset-Family of REAL such that
A1: the topology of Sorgenfrey-line = UniCl B and
A2: B = {[.x,q.[ where x is Element of REAL, q is Element of REAL:
  x < q & q is rational} by TOPGEN_3:def 2;
A3: B c= the topology of T by A1,CANTOR_1:1;
  let x,y be Point of T;
  reconsider a = x, b = y as Real by TOPGEN_3:def 2;
  assume
A4: x <> y;
  per cases by A4,XXREAL_0:1;
  suppose
A5: a < b;
    then consider w being rational number such that
A6: a < w & w < b by RAT_1:22;
    b < b+1 by XREAL_1:31;
    then consider q being rational number such that
A7: b < q & q < b+1 by RAT_1:22;
    w is Real & q is Real by XREAL_0:def 1;
    then [.a,w.[ in B & [.b,q.[ in B by A2,A6,A7;
    then [.a,w.[ in the topology of T & [.b,q.[ in the topology of T by A3;
    then reconsider U = [.a,w.[, V = [.b,q.[ as open Subset of T
    by PRE_TOPC:def 5;
    take U,V; thus U is open & V is open;
    thus thesis by A5,A6,A7,XXREAL_1:3;
  end;
  suppose
A8: a > b;
    then consider w being rational number such that
A9: b < w & w < a by RAT_1:22;
    a < a+1 by XREAL_1:31;
    then consider q being rational number such that
A10: a < q & q < a+1 by RAT_1:22;
    w is Real & q is Real by XREAL_0:def 1;
    then [.b,w.[ in B & [.a,q.[ in B by A2,A9,A10;
    then [.b,w.[ in the topology of T & [.a,q.[ in the topology of T by A3;
    then reconsider V = [.b,w.[, U = [.a,q.[ as open Subset of T
    by PRE_TOPC:def 5;
    take U,V; thus U is open & V is open;
    thus thesis by A8,A9,A10,XXREAL_1:3;
  end;
end;

theorem Th58:
  for x being real number
  holds left_open_halfline x is closed Subset of Sorgenfrey-line
proof
  set T = Sorgenfrey-line;
A1: the carrier of T = REAL by TOPGEN_3:def 2;
  let x be real number;
  reconsider A = right_closed_halfline x as open Subset of T by TOPGEN_3:15;
  left_open_halfline x = A` by A1,XXREAL_1:224,294;
  hence thesis;
end;

theorem
  for x being real number
  holds left_closed_halfline x is closed Subset of Sorgenfrey-line
proof
  set T = Sorgenfrey-line;
A1: the carrier of T = REAL by TOPGEN_3:def 2;
  let x be real number;
  reconsider A = right_open_halfline x as open Subset of T by TOPGEN_3:14;
  (left_closed_halfline x)`` = A` by A1,XXREAL_1:224,288;
  hence thesis;
end;

theorem Th60:
  for x being real number
  holds right_closed_halfline x is closed Subset of Sorgenfrey-line
proof
  set T = Sorgenfrey-line;
A1: the carrier of T = REAL by TOPGEN_3:def 2;
  let x be real number;
  reconsider A = left_open_halfline x as open Subset of T by TOPGEN_3:13;
  (right_closed_halfline x)`` = A` by A1,XXREAL_1:224,294;
  hence thesis;
end;

theorem Th61:
  for x,y being real number holds [.x,y.[ is closed Subset of Sorgenfrey-line
proof
  set T = Sorgenfrey-line;
A1: the carrier of T = REAL by TOPGEN_3:def 2;
  let x,y be real number;
  reconsider A = right_closed_halfline x,
  B = left_open_halfline y as closed Subset of T by Th58,Th60;
  [.x,y.[ = [.x,y.[``
    .= (left_open_halfline(x) \/ right_closed_halfline(y))`
              by XXREAL_1:382
    .= (left_open_halfline x)` /\ (right_closed_halfline y)` by XBOOLE_1:53
    .= A`` /\ (right_closed_halfline y)` by A1,XXREAL_1:224,294
    .= A /\ B by XXREAL_1:224,294;
  hence thesis by TOPS_1:35;
end;

::<DESC name="1.4.4. Przyk/lad funkcji ci/ag/lej ze strza/lce w [0,1]" monograph="topology"/>

theorem Th62:
  for x being real number, w being rational number st x < w
  ex f being continuous Function of Sorgenfrey-line, I[01] st
  for a being Point of Sorgenfrey-line holds (a in [.x,w.[ implies f.a = 0) &
  (not a in [.x,w.[ implies f.a = 1)
proof
  set X = Sorgenfrey-line;
  let x be real number;
  let w be rational number;
  assume x < w;
  defpred P[set] means $1 in [.x,w.[;
  deffunc 00(set) = 0;
  deffunc 01(set) = 1;
A1: for a being set st a in the carrier of X holds
  (P[a] implies 00(a) in the carrier of I[01]) &
  (not P[a] implies 01(a) in the carrier of I[01]) by BORSUK_1:83,XXREAL_1:1;
  consider f being Function of X, I[01] such that
A2: for a being set st a in the carrier of X holds
  (P[a] implies f.a = 00(a)) & (not P[a] implies f.a = 01(a))
  from FUNCT_2:sch 5(A1);
  reconsider V = [.x,w.[ as open closed Subset of X by Th61,TOPGEN_3:11;
  reconsider 00 = 0, 01 = 1 as Element of I[01] by BORSUK_1:83,XXREAL_1:1;
  reconsider f1 = (X|V) --> 00 as continuous Function of X|V, I[01];
  reconsider f2 = (X|V`) --> 01 as continuous Function of X|V`, I[01];
A3: [#]X = the carrier of X & the carrier of X|V = V & the carrier of X|V` = V`
  by PRE_TOPC:29;
  then
A4: dom f = [#]X & dom f1 = V & dom f2 = V` by FUNCT_2:def 1;
A5: V \/ V` = [#]X by PRE_TOPC:18;
  now
    let u;
    assume u in dom f1 \/ dom f2;
    then reconsider x = u as Point of X by A4,PRE_TOPC:18;
    hereby
      assume u in dom f2;
      then not x in V & (V`-->1).u = 1 & f2 = V`-->1
      by A3,FUNCOP_1:13,XBOOLE_0:def 5;
      hence f.u = f2.u by A2;
    end;
    assume not u in dom f2;
    then
A6: x in V by A4,SUBSET_1:50;
    hence f.u = 0 by A2
      .= f1.u by A3,A6,FUNCOP_1:13;
  end;
  then f = f1+*f2 by A4,A5,FUNCT_4:def 1;
  then reconsider f as continuous Function of Sorgenfrey-line, I[01] by Th16;
  take f;
  let a be Point of Sorgenfrey-line; thus thesis by A2;
end;

theorem
  Sorgenfrey-line is Tychonoff
proof
  set X = Sorgenfrey-line;
A1: the carrier of X = REAL by TOPGEN_3:def 2;
  consider B being Subset-Family of REAL such that
A2: the topology of X = UniCl B and
A3: B = {[.x,q.[ where x is Element of REAL, q is Element of REAL:
  x < q & q is rational} by TOPGEN_3:def 2;
  B c= UniCl B by CANTOR_1:1;
  then B is Basis of X by A1,A2,CANTOR_1:def 2;
  then reconsider B as prebasis of X by YELLOW_9:27;
  now
    let x be Point of X;
    let V be Subset of X;
    assume
A4: x in V & V in B;
    then consider a,q being Real such that
A5: V = [.a,q.[ & a < q & q is rational by A3;
    consider f being continuous Function of X, I[01] such that
A6: for b being Point of Sorgenfrey-line holds
    (b in [.a,q.[ implies f.b = 0) &
    (not b in [.a,q.[ implies f.b = 1) by A5,Th62;
    take f;
    thus f.x = 0 by A4,A5,A6;
    thus f.:V` c= {1}
    proof
      let u;
      assume u in f.:V`;
      then consider b being Point of X such that
A7:   b in V` & u = f.b by FUNCT_2:116;
      not b in [.a,q.[ by A5,A7,XBOOLE_0:def 5;
      then u = 1 by A6,A7;
      hence thesis by TARSKI:def 1;
    end;
  end;
  hence thesis by Th56,Th57;
end;

begin :: Niemytzki plane is Tychonoff space

::<DESC name="1.4.5. Przyk/lad funkcji ci/ag/lej z p/laszczyzny Niemytzkiego w R^1" monograph="topology"/>

definition
  let x be real number, r be positive (real number);
  func +(x,r) -> Function of Niemytzki-plane, I[01] means
  :Def5:
  it.(|[x,0]|) = 0 &
  for a being real number, b being non negative (real number) holds
  ((a <> x or b <> 0) & not |[a,b]| in Ball(|[x,r]|,r) implies
  it.(|[a,b]|) = 1) & (|[a,b]| in Ball(|[x,r]|,r) implies
  it.(|[a,b]|) = |.|[x,0]|-|[a,b]|.|^2/(2*r*b));
  existence
  proof
    defpred P[set] means $1 = |[x,0]|;
    defpred Q[set] means not $1 in Ball(|[x,r]|, r);
    deffunc F1(Point of TOP-REAL 2) = 0;
    deffunc F2(Point of TOP-REAL 2) = 1;
    deffunc F3(Point of TOP-REAL 2) = |.|[x,0]|-$1.|^2/(2*r*$1`2);
    the carrier of Niemytzki-plane = y>=0-plane by Def3;
    then
A1: the carrier of Niemytzki-plane c= the carrier of TOP-REAL 2;
A2: for a being Point of TOP-REAL 2 st a in the carrier of Niemytzki-plane
    holds (P[a] implies F1(a) in the carrier of I[01]) &
    (not P[a] & Q[a] implies F2(a) in the carrier of I[01]) &
    (not P[a] & not Q[a] implies F3(a) in the carrier of I[01])
    proof
      let a be Point of TOP-REAL 2;
      assume a in the carrier of Niemytzki-plane;
      thus P[a] implies F1(a) in the carrier of I[01]
      by BORSUK_1:83,XXREAL_1:1;
      thus (not P[a] & Q[a] implies F2(a) in the carrier of I[01])
      by BORSUK_1:83,XXREAL_1:1;
      assume not P[a];
      assume not Q[a];
      then
A3:   a = |[a`1,a`2]| & |.a-|[x,r]|.| < r by EUCLID:57,TOPREAL9:7;
      then |.|[a`1-x,a`2-r]|.| < r & |.|[a`1-x,a`2-r]|.| >= 0 by EUCLID:66;
      then |[a`1-x,a`2-r]|`1 = a`1-x & |[a`1-x,a`2-r]|`2 = a`2-r &
      |.|[a`1-x,a`2-r]|.|^2 < r^2 by EUCLID:56,SQUARE_1:78;
      then (a`1-x)^2+(a`2-r)^2 < r^2 by JGRAPH_1:46;
      then (a`1-x)^2+(a`2^2-2*a`2*r)+r^2 < 0+r^2;
      then (a`1-x)^2+a`2^2-2*a`2*r < 0 by XREAL_1:8;
      then |[a`1-x,a`2]|`1 = a`1-x & |[a`1-x,a`2]|`2 = a`2 &
      (a`1-x)^2+(a`2-0)^2 < 2*r*a`2 by EUCLID:56,XREAL_1:50;
      then |.|[a`1-x,a`2-0]|.|^2 < 2*r*a`2 by JGRAPH_1:46;
      then |.a-|[x,0]|.|^2 < 2*r*a`2 by A3,EUCLID:66;
      then |.|[x,0]|-a.|^2 >= 0 & |.|[x,0]|-a.|^2 < 2*r*a`2 by TOPRNS_1:28;
      then 0 <= F3(a) & F3(a) <= 1 by XREAL_1:185;
      hence thesis by BORSUK_1:83,XXREAL_1:1;
    end;
    consider f being Function of Niemytzki-plane, I[01] such that
A4: for a being Point of TOP-REAL 2 st a in the carrier of Niemytzki-plane
    holds (P[a] implies f.a = F1(a)) & (not P[a] & Q[a] implies f.a = F2(a)) &
    (not P[a] & not Q[a] implies f.a = F3(a)) from SCH2(A1,A2);
    take f;
A5: the carrier of Niemytzki-plane = y>=0-plane by Def3;
    then |[x,0]| in the carrier of Niemytzki-plane by Th22;
    hence f.(|[x,0]|) = 0 by A4;
    let a be real number, b be non negative (real number);
A6: |[a,b]| in the carrier of Niemytzki-plane by A5,Th22;
    hereby
      assume a <> x or b <> 0;
      then not P[ |[a,b]| ] by SPPOL_2:1;
      hence not |[a,b]| in Ball(|[x,r]|,r) implies f.(|[a,b]|) = 1 by A4,A6;
    end;
    assume
A7: |[a,b]| in Ball(|[x,r]|,r);
    Ball(|[x,r]|,r) misses y=0-line & |[x,0]| in y=0-line by Th19,Th25;
    then not P[ |[a,b]| ] & |[a,b]|`2 = b by A7,EUCLID:56,XBOOLE_0:3;
    hence thesis by A4,A6,A7;
  end;
  uniqueness
  proof
    let f,g be Function of Niemytzki-plane, I[01] such that
A8: f.(|[x,0]|) = 0 &
    for a being real number, b being non negative (real number) holds
    ((a <> x or b <> 0) & not |[a,b]| in Ball(|[x,r]|,r) implies
    f.(|[a,b]|) = 1) & (|[a,b]| in Ball(|[x,r]|,r) implies
    f.(|[a,b]|) = |.|[x,0]|-|[a,b]|.|^2/(2*r*b)) and
A9: g.(|[x,0]|) = 0 &
    for a being real number, b being non negative (real number) holds
    ((a <> x or b <> 0) & not |[a,b]| in Ball(|[x,r]|,r) implies
    g.(|[a,b]|) = 1) & (|[a,b]| in Ball(|[x,r]|,r) implies
    g.(|[a,b]|) = |.|[x,0]|-|[a,b]|.|^2/(2*r*b));
A10: the carrier of Niemytzki-plane = y>=0-plane by Def3;
    now
      let p be Point of Niemytzki-plane;
      p in y>=0-plane by A10;
      then reconsider q = p as Point of TOP-REAL 2;
A11:  p = |[q`1,q`2]| by EUCLID:57;
      then reconsider q2 = q`2 as non negative (real number) by A10,Th22;
      per cases by EUCLID:57;
      suppose q`1 = x & q2 = 0;
        hence f.p = g.p by A8,A9,A11;
      end;
      suppose (q`1 <> x or q2 <> 0) & not |[q`1,q2]| in Ball(|[x,r]|,r);
        then f.p = 1 & g.p = 1 by A8,A9,A11;
        hence f.p = g.p;
      end;
      suppose p in Ball(|[x,r]|,r);
        then f.p = |.|[x,0]|-q.|^2/(2*r*q2) &
        g.p = |.|[x,0]|-q.|^2/(2*r*q2) by A8,A9,A11;
        hence f.p = g.p;
      end;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem Th64:
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x being real number, r being positive (real number) st +(x,r).p = 0
  holds p = |[x,0]|
proof
  let p be Point of TOP-REAL 2;
  assume
A1: p`2 >= 0;
  let x be real number;
  let r be positive (real number);
  assume
A2: +(x,r).p = 0;
  set p1 = p`1, p2 = p`2;
A3: p = |[p1,p2]| by EUCLID:57;
  assume
A4: p <> |[x,0]|;
  then p1 <> x or p2 <> 0 by EUCLID:57;
  then
A5: |[p1,p2]| in Ball(|[x,r]|,r) by A1,A2,A3,Def5;
  Ball(|[x,r]|,r) misses y=0-line by Th25;
  then not |[p1,p2]| in y=0-line by A5,XBOOLE_0:3;
  then p2 <> 0;
  then reconsider p2 as positive (real number) by A1;
  0/(2*r*p2) = |.|[x,0]|-|[p1,p2]|.|^2/(2*r*p2) by A2,A3,A5,Def5;
  then 0 = |.|[x,0]|-|[p1,p2]|.|^2;
  then 0 = |.|[x,0]|-p.| by A3;
  hence contradiction by A4,TOPRNS_1:29;
end;

theorem Th65:
  for x,y being real number, r being positive (real number) st x <> y
  holds +(x,r).(|[y,0]|) = 1
proof
  let x,y be real number;
  let r be positive (real number);
  assume
A1: x <> y;
  then x-y <> 0;
  then |.x-y.| > 0 by COMPLEX1:133;
  then (|.x-y.| = x-y or |.x-y.| = - (x-y)) &
  |.x-y.|^2 <> 0 & |.x-y.|^2 >= 0 & |[x-y,r]|`1 = x-y & |[x-y,r]|`2 = r
  by ABSVALUE:1,EUCLID:56;
  then (x-y)^2 > 0 & |.|[x-y,r]|.|^2 = r^2+(x-y)^2 by JGRAPH_1:46;
  then |.|[x-y,r]|.|^2 > r^2 & |.|[x-y,r]|.| >= 0 by XREAL_1:31;
  then |.|[x-y,r-0]|.| > r by SQUARE_1:77;
  then |.|[x,r]|-|[y,0]|.| > r by EUCLID:66;
  then |.|[y,0]|-|[x,r]|.| > r by TOPRNS_1:28;
  then not |[y,0]| in Ball(|[x,r]|,r) by TOPREAL9:7;
  hence thesis by A1,Def5;
end;

theorem Th66:
  for p being Point of TOP-REAL 2
  for x being real number, a,r being positive (real number)
  st a <= 1 & |.p-|[x,r*a]|.| = r*a & p`2 <> 0 holds +(x,r).p = a
proof
  let p be Point of TOP-REAL 2;
A1: p = |[p`1,p`2]| by EUCLID:57;
  set p1 = p`1, p2 = p`2;
  let x be real number;
  let a,r be positive (real number);
  assume
A2: a <= 1;
  assume
A3: |.p-|[x,r*a]|.| = r*a & p`2 <> 0;
  then |.|[p1-x,p2-r*a]|.|^2 = (r*a)^2 & |[p1-x,p2-r*a]|`1 = p1-x &
  |[p1-x,p2-r*a]|`2 = p2-r*a by A1,EUCLID:56,66;
  then
A4: (p1-x)^2+(p2-r*a)^2 = (r*a)^2 by JGRAPH_1:46;
  then
A5: (p1-x)^2+p2^2 = 2*p2*r*a;
  p2^2 > 0 & (p1-x)^2 >= 0 by A3,XREAL_1:65;
  then p2*(2*r*a) > 0+0 by A5;
  then p2 > 0/(2*r*a);
  then reconsider p2 as positive (real number);
A6: |[p1-x,p2-0]|`1 = p1-x & |[p1-x,p2]|`2 = p2 by EUCLID:56;
  per cases by A2,XXREAL_0:1;
  suppose a < 1;
    then r*a < r by XREAL_1:159;
    then reconsider s = r-r*a as positive (real number) by XREAL_1:52;
    |.p-|[x,r]|.|^2 = |.|[p1-x,p2-r]|.|^2 by A1,EUCLID:66
      .= (p1-x)^2+(p2-r)^2 by Th13
      .= (p1-x)^2+(p2-a*r)^2+((r-a*r)^2+2*(r-a*r)*(a*r-p2))
      .= |.|[p1-x,p2-a*r]|.|^2+((r-a*r)^2+2*(r-a*r)*(a*r-p2)) by Th13
      .= (a*r)^2+(r*r-r*p2+r*a*r-r*p2-(a*r*r-a*r*p2+(a*r)^2-a*r*p2)) by A1,A3,
EUCLID:66
      .= r^2-(1+1)*p2*s;
    then |.p-|[x,r]|.|^2 < r^2 by XREAL_1:46;
    then |.p-|[x,r]|.| < r by SQUARE_1:77;
    then p in Ball(|[x,r]|,r) by TOPREAL9:7;
    then +(x,r).p = |.|[x,0]|-p.|^2/(2*r*p2) by A1,Def5
      .= |.p-|[x,0]|.|^2/(2*r*p2) by TOPRNS_1:28
      .= |.|[p1-x,p2-0]|.|^2/(2*r*p2) by A1,EUCLID:66
      .= ((p1-x)^2+p2^2)/(2*r*p2) by A6,JGRAPH_1:46;
    then +(x,r).p = (2*p2*r*a)/(2*r*p2) & a*((2*p2*r)/(2*r*p2)) = a*1
    by A4,XCMPLX_1:60;
    hence thesis by XCMPLX_1:75;
  end;
  suppose
A7: a = 1;
    then not p in Ball(|[x,r]|,r) & p2 is non negative by A3,TOPREAL9:7;
    hence thesis by A1,A7,Def5;
  end;
end;

theorem Th67:
  for p being Point of TOP-REAL 2
  for x,a being real number, r being positive (real number)
  st 0 <= a & a <= 1 & |.p-|[x,r*a]|.| < r*a holds +(x,r).p < a
proof
  let p be Point of TOP-REAL 2;
A1: p = |[p`1,p`2]| by EUCLID:57;
  set p1 = p`1, p2 = p`2;
  let x,a be real number;
  let r be positive (real number);
  assume
A2: 0 <= a & a <= 1;
  assume
A3: |.p-|[x,r*a]|.| < r*a;
  then |.p-|[x,r*a]|.|^2 < (r*a)^2 by SQUARE_1:78;
  then |.|[p1-x,p2-r*a]|.|^2 < (r*a)^2 & |[p1-x,p2-r*a]|`1 = p1-x &
  |[p1-x,p2-r*a]|`2 = p2-r*a by A1,EUCLID:56,66;
  then (p1-x)^2+(p2-r*a)^2 < (r*a)^2 by JGRAPH_1:46;
  then (p1-x)^2+p2^2-2*p2*(r*a)+(r*a)^2 < (r*a)^2;
  then (p1-x)^2+p2^2-2*p2*r*a < 0 by XREAL_1:33;
  then
A4: (p1-x)^2+p2^2 < 2*p2*r*a by XREAL_1:50;
  set r1 = r*a, r2 = r*1;
  reconsider r1 as positive (real number) by A3;
  r1 <= r2 by A2,XREAL_1:66;
  then
A5: p in Ball(|[x,r1]|,r1) & Ball(|[x,r1]|,r1) c= Ball(|[x,r2]|,r2)
  by A3,Th27,TOPREAL9:7;
  Ball(|[x,r1]|,r1) c= y>=0-plane & Ball(|[x,r1]|,r1) misses y=0-line
  by Th24,Th25;
  then p2 >= 0 & not p in y=0-line & (p2 = 0 implies p in y=0-line)
  by A1,A5,Th22,XBOOLE_0:3;
  then reconsider p2 as positive (real number);
A6: |[p1-x,p2-0]|`1 = p1-x & |[p1-x,p2]|`2 = p2 by EUCLID:56;
  +(x,r).p = |.|[x,0]|-p.|^2/(2*r*p2) by A1,A5,Def5
    .= |.p-|[x,0]|.|^2/(2*r*p2) by TOPRNS_1:28
    .= |.|[p1-x,p2-0]|.|^2/(2*r*p2) by A1,EUCLID:66
    .= ((p1-x)^2+p2^2)/(2*r*p2) by A6,JGRAPH_1:46;
  then +(x,r).p < (2*p2*r*a)/(2*r*p2) & a*((2*p2*r)/(2*r*p2)) = a*1
  by A4,XCMPLX_1:60,XREAL_1:76;
  hence thesis by XCMPLX_1:75;
end;

theorem Th68:
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x,a being real number, r being positive (real number)
  st 0 <= a & a < 1 & |.p-|[x,r*a]|.| > r*a holds +(x,r).p > a
proof
  let p be Point of TOP-REAL 2 such that
A1: p`2 >= 0;
A2: p = |[p`1,p`2]| by EUCLID:57;
  then reconsider z = p as Element of Niemytzki-plane by A1,Lm1,Th22;
  set p1 = p`1, p2 = p`2;
  reconsider p2 as non negative (real number) by A1;
  let x,a be real number;
  let r be positive (real number);
  assume
A3: 0 <= a & a < 1;
  then reconsider a' = a as non negative (real number);
  reconsider ra = r*a as Real by XREAL_0:def 1;
  assume
A4: |.p-|[x,r*a]|.| > r*a;
A5: +(x,r).z in the carrier of I[01] by FUNCT_2:7;
  |.|[x,0]|-|[x,r*a]|.| = |.|[x,r*a]|-|[x,0]|.| by TOPRNS_1:28
    .= |.|[x-x,ra-0]|.| by EUCLID:66
    .= abs(ra) by TOPREAL6:31
    .= r*a' by ABSVALUE:def 1;
  then
A6: p1 <> x or p2 <> 0 by A4,EUCLID:57;
  per cases by A3;
  suppose
A7: a = 0;
    then p <> |[x,r*0]| by A4,TOPRNS_1:29;
    then +(x,r).p <> 0 by A1,Th64;
    hence thesis by A5,A7,BORSUK_1:83,XXREAL_1:1;
  end;
  suppose +(x,r).p = 1;
    hence thesis by A3;
  end;
  suppose
A8: a > 0 & +(x,r).z <> 1;
A9: |[p1-x,p2-0]|`1 = p1-x & |[p1-x,p2]|`2 = p2 by EUCLID:56;
    p2 is non negative;
    then
A10: p in Ball(|[x,r]|,r) by A2,A6,A8,Def5;
    then
A11: +(x,r).p = |.|[x,0]|-p.|^2/(2*r*p2) by A2,Def5
      .= |.p-|[x,0]|.|^2/(2*r*p2) by TOPRNS_1:28
      .= |.|[p1-x,p2-0]|.|^2/(2*r*p2) by A2,EUCLID:66
      .= ((p1-x)^2+p2^2)/(2*r*p2) by A9,JGRAPH_1:46;
    r*a >= r*0 by A3;
    then |.p-|[x,r*a]|.|^2 > (r*a)^2 by A4,SQUARE_1:78;
    then |.|[p1-x,p2-r*a]|.|^2 > (r*a)^2 & |[p1-x,p2-r*a]|`1 = p1-x &
    |[p1-x,p2-r*a]|`2 = p2-r*a by A2,EUCLID:56,66;
    then (p1-x)^2+(p2-r*a)^2 > (r*a)^2 by JGRAPH_1:46;
    then (p1-x)^2+p2^2-2*p2*(r*a)+(r*a)^2 > (r*a)^2;
    then (p1-x)^2+p2^2-2*p2*r*a > 0 by XREAL_1:34;
    then
A12: (p1-x)^2+p2^2 > 2*p2*r*a by XREAL_1:49;
    Ball(|[x,r]|,r) c= y>=0-plane & Ball(|[x,r]|,r) misses y=0-line
    by Th24,Th25;
    then p2 >= 0 & not p in y=0-line & (p2 = 0 implies p in y=0-line)
    by A2,A10,XBOOLE_0:3;
    then reconsider p2 as positive (real number);
    +(x,r).p > (2*p2*r*a)/(2*r*p2) & a*((2*p2*r)/(2*r*p2)) = a*1
    by A11,A12,XCMPLX_1:60,XREAL_1:76;
    hence thesis by XCMPLX_1:75;
  end;
end;

theorem Th69:
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x,a,b being real number, r being positive (real number)
  st 0 <= a & b <= 1 & +(x,r).p in ].a,b.[ ex r1 being positive (real number)
  st r1 <= p`2 & Ball(p,r1) c= +(x,r)"].a,b.[
proof
  let p be Point of TOP-REAL 2 such that
A1: p`2 >= 0;
A2: p = |[p`1,p`2]| by EUCLID:57;
  let x,a,b be real number, r be positive (real number);
  set r1 = min(r*b-|.p-|[x,r*b]|.|, |.p-|[x,r*a]|.|-r*a);
  assume
A3: 0 <= a & b <= 1 & +(x,r).p in ].a,b.[;
  then
A4: +(x,r).p > a & +(x,r).p < b by XXREAL_1:4;
  then
A5: p in Ball(|[x,r]|,r) or p`1 = x & p`2 = 0 & p <> |[x,0]| by A1,A2,A3,Def5;
  a < b by A4,XXREAL_0:2;
  then
A6: 0 < b & a < 1 by A3,XXREAL_0:2;
  (p`2 = 0 implies p in y=0-line) & Ball(|[x,r]|,r) misses y=0-line
  by A2,Th25;
  then reconsider p2 = p`2, b' = b as positive (real number)
  by A1,A3,A4,A5,EUCLID:57,XBOOLE_0:3;
  |.p-|[x,r]|.| < r & 2*r*p2 > 0 &
  +(x,r).p = |.|[x,0]|-p.|^2/(2*r*p`2) by A1,A2,A5,Def5,TOPREAL9:7;
  then |.|[x,0]|-p.|^2 > (2*r*p`2)*a & |.|[x,0]|-p.|^2 < (2*r*p`2)*b
  by A4,XREAL_1:79,81;
  then |.p-|[x,0]|.|^2 < (2*r*p`2)*b & |.p-|[x,0]|.|^2 > (2*r*p`2)*a
  by TOPRNS_1:28;
  then |.|[p`1-x,p`2-0]|.|^2 < (2*r*p`2)*b & |[p`1-x,p`2]|`1 = p`1-x &
  |.|[p`1-x,p`2-0]|.|^2 > (2*r*p`2)*a & |[p`1-x,p2]|`2 = p2
  by A2,EUCLID:56,66;
  then (p`1-x)^2+p2^2 < (2*r*p2)*b & (p`1-x)^2+p2^2 > (2*r*p2)*a
  by JGRAPH_1:46;
  then (p`1-x)^2+p2^2 - (2*r*p2)*b < 0 & (p`1-x)^2+p2^2 - (2*r*p2)*a > 0
  by XREAL_1:51,52;
  then (p`1-x)^2+p2^2 - (2*r*p2)*b + (r*b)^2 < (r*b)^2 &
  (p`1-x)^2+p2^2 - (2*r*p2)*a + (r*a)^2 > (r*a)^2 by XREAL_1:31,32;
  then (p`1-x)^2+(p2 - r*b)^2 < (r*b)^2 & |[p`1-x, p2-r*b]|`1 = p`1-x &
  (p`1-x)^2+(p2 - r*a)^2 > (r*a)^2 & |[p`1-x, p2-r*b]|`2 = p2-r*b &
  |[p`1-x, p2-r*a]|`1 = p`1-x & |[p`1-x, p2-r*a]|`2 = p2-r*a by EUCLID:56;
  then |.|[p`1-x, p2-r*b]|.|^2 < (r*b)^2 & |.|[p`1-x, p2-r*a]|.|^2 > (r*a)^2
  by JGRAPH_1:46;
  then |.p-|[x,r*b]|.|^2 < (r*b)^2 & r*b' >= 0 & |.p-|[x,r*a]|.| >= 0 &
  |.p-|[x,r*a]|.|^2 > (r*a)^2 by A2,EUCLID:66;
  then |.p-|[x,r*b]|.| < r*b & |.p-|[x,r*a]|.| > r*a by SQUARE_1:118;
  then r*b-|.p-|[x,r*b]|.| > 0 & r1 = r*b-|.p-|[x,r*b]|.| or
  |.p-|[x,r*a]|.|-r*a > 0 & r1 = |.p-|[x,r*a]|.|-r*a
  by XREAL_1:52,XXREAL_0:15;
  then reconsider r1 as positive (real number);
  take r1;
  |.p-|[p`1,0]|.| = |.|[p`1-p`1,p2-0]|.| by A2,EUCLID:66
    .= abs p2 by TOPREAL6:31
    .= p2 by ABSVALUE:def 1;
  then
A7: |.|[x,r*b]|-|[p`1,0]|.| <= |.|[x,r*b]|-p.|+p2 &
  r1 <= r*b-|.p-|[x,r*b]|.| & |.p-|[x,r*b]|.| = |.|[x,r*b]|-p.| &
  r*b-|.p-|[x,r*b]|.|+|.p-|[x,r*b]|.| = r*b by TOPRNS_1:28,35,XXREAL_0:17;
  then
A8: |.|[x,r*b]|-p.|+r1 <= r*b by XREAL_1:8;
  thus now
    assume r1 > p`2;
    then |.|[x,r*b]|-p.|+p2 < |.|[x,r*b]|-p.|+r1 by XREAL_1:10;
    then |.|[x,r*b]|-|[p`1,0]|.| < |.|[x,r*b]|-p.|+r1 by A7,XXREAL_0:2;
    then |.|[x,r*b]|-|[p`1,0]|.| < r*b by A8,XXREAL_0:2;
    then |.|[p`1,0]|-|[x,r*b]|.| < r*b by TOPRNS_1:28;
    then |[p`1,0]| in Ball(|[x,r*b]|,r*b) & |[p`1,0]| in y=0-line
    by TOPREAL9:7;
    then Ball(|[x,r*b]|,r*b') meets y=0-line by XBOOLE_0:3;
    hence contradiction by Th25;
  end;
  then
A9: Ball(p,r1) c= y>=0-plane & Ball(p,r1) misses y=0-line by A2,Th24,Th25;
  let u;
  assume
A10: u in Ball(p,r1);
  then reconsider q = u as Point of TOP-REAL 2;
  q = |[q`1,q`2]| by EUCLID:57;
  then
A11: q`2 >= 0 by A9,A10,Th22;
  |.p-|[x,r*a]|.|-r*a >= r1 & |.q-p.| < r1 by A10,TOPREAL9:7,XXREAL_0:17;
  then |.q-p.|+|.p-|[x,r*b]|.| < r1+|.p-|[x,r*b]|.| & |.p-q.| = |.q-p.| &
  |.p-|[x,r*a]|.| <= |.p-q.|+|.q-|[x,r*a]|.| & |.p-|[x,r*a]|.|-r*a > |.q-p.|
  by TOPRNS_1:28,35,XREAL_1:10,XXREAL_0:2;
  then |.q-|[x,r*b]|.| <= |.q-p.|+|.p-|[x,r*b]|.| &
  |.q-p.|+|.p-|[x,r*b]|.| < r*b &
  |.p-|[x,r*a]|.|-|.q-p.| > r*a & |.q-|[x,r*a]|.| >= |.p-|[x,r*a]|.|-|.q-p.|
  by A7,A8,TOPRNS_1:35,XREAL_1:14,22,XXREAL_0:2;
  then |.q-|[x,r*b]|.| < r*b & |.q-|[x,r*a]|.| > r*a by XXREAL_0:2;
  then +(x,r).q < b & +(x,r).q > a by A3,A6,A11,Th67,Th68;
  then +(x,r).q in ].a,b.[ by XXREAL_1:4;
  hence thesis by A9,A10,Lm1,FUNCT_2:46;
end;

theorem Th70:
  for x being real number, a,r being positive (real number)
  holds Ball(|[x,r*a]|,r*a) c= +(x,r)"].0,a.[
proof
  let x be real number;
  let a,r be positive (real number), u;
  assume
A1: u in Ball(|[x,r*a]|,r*a);
  then reconsider p = u as Point of TOP-REAL 2;
  Ball(|[x,r*a]|,r*a) c= y>=0-plane by Th24;
  then reconsider q = p as Point of Niemytzki-plane by A1,Def3;
A2: q = |[p`1,p`2]| & +(x,r).q in the carrier of I[01] by EUCLID:57,FUNCT_2:7;
  then
A3: +(x,r).q <= 1 & +(x,r).q >= 0 & p`2 >= 0
  by Lm1,Th22,BORSUK_1:83,XXREAL_1:1;
A4: now
    assume +(x,r).p = 0;
    then p = |[x,0]| by A3,Th64;
    then p in y=0-line & Ball(|[x,r*a]|,r*a) misses y=0-line by Th19,Th25;
    hence contradiction by A1,XBOOLE_0:3;
  end;
  per cases;
  suppose a > 1;
    then +(x,r).q < a & +(x,r).q > 0 by A3,A4,XXREAL_0:2;
    then +(x,r).q in ].0,a.[ by XXREAL_1:4;
    hence thesis by FUNCT_2:46;
  end;
  suppose
A5: a <= 1;
    |.p-|[x,r*a]|.| < r*a by A1,TOPREAL9:7;
    then +(x,r).p < a & +(x,r).q > 0 by A2,A4,A5,Th67,BORSUK_1:83,XXREAL_1:1;
    then +(x,r).q in ].0,a.[ by XXREAL_1:4;
    hence thesis by FUNCT_2:46;
  end;
end;

theorem Th71:
  for x being real number, a,r being positive (real number)
  holds Ball(|[x,r*a]|,r*a) \/ {|[x,0]|} c= +(x,r)"[.0,a.[
proof
  let x be real number;
  let a,r be positive (real number), u;
  assume
A1: u in Ball(|[x,r*a]|,r*a) \/ {|[x,0]|};
  then
A2: u in Ball(|[x,r*a]|,r*a) or u in {|[x,0]|} by XBOOLE_0:def 3;
  reconsider p = u as Point of TOP-REAL 2 by A1;
  Ball(|[x,r*a]|,r*a) c= y>=0-plane & |[x,0]| in y>=0-plane by Th22,Th24;
  then reconsider q = p as Point of Niemytzki-plane by A2,Lm1,TARSKI:def 1;
  +(x,r).q in the carrier of I[01] by FUNCT_2:7;
  then
A3: +(x,r).q <= 1 & +(x,r).q >= 0 by BORSUK_1:83,XXREAL_1:1;
  per cases by A2,TARSKI:def 1;
  suppose a > 1;
    then +(x,r).q < a by A3,XXREAL_0:2;
    then +(x,r).q in [.0,a.[ by A3,XXREAL_1:3;
    hence thesis by FUNCT_2:46;
  end;
  suppose
A4: a <= 1 & u in Ball(|[x,r*a]|,r*a);
    then |.p-|[x,r*a]|.| < r*a by TOPREAL9:7;
    then +(x,r).p < a by A4,Th67;
    then +(x,r).q in [.0,a.[ by A3,XXREAL_1:3;
    hence thesis by FUNCT_2:46;
  end;
  suppose u = |[x,0]|;
    then +(x,r).u = 0 by Def5;
    then +(x,r).q in [.0,a.[ by XXREAL_1:3;
    hence thesis by FUNCT_2:46;
  end;
end;

theorem Th72:
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x,a being real number, r being positive (real number)
  st 0 < +(x,r).p & +(x,r).p < a & a <= 1 holds p in Ball(|[x,r*a]|,r*a)
proof
  let p be Point of TOP-REAL 2;
  assume
A1: p`2 >= 0;
A2: p = |[p`1,p`2]| by EUCLID:57;
  let x,a be real number;
  let r be positive (real number);
  set r1 = r*a;
  assume
A3: 0 < +(x,r).p & +(x,r).p < a & a <= 1;
  then
A4: p <> |[x,0]| & (x <> p`1 implies p <> |[p`1,0]|) by Def5,Th65;
  assume not p in Ball(|[x,r1]|,r1);
  then |.p-|[x,r1]|.| >= r1 by TOPREAL9:7;
  then |.p-|[x,r1]|.| = r1 or |.p-|[x,r1]|.| > r1 & (a < 1 or a = 1)
  by A3,XXREAL_0:1;
  then +(x,r).p = a or (a < 1 & +(x,r).p > a) or
  a = 1 & not p in Ball(|[x,r]|,r) by A1,A2,A3,A4,Th66,Th68,TOPREAL9:7;
  hence thesis by A1,A2,A3,A4,Def5;
end;

theorem Th73:
  for p being Point of TOP-REAL 2 st p`2 > 0
  for x,a being real number, r being positive (real number)
  st 0 <= a & a < +(x,r).p ex r1 being positive (real number)
  st r1 <= p`2 & Ball(p,r1) c= +(x,r)"].a,1.]
proof
  let p be Point of TOP-REAL 2;
  assume
A1: p`2 > 0;
  let x,a be real number;
  let r be positive (real number);
  set f = +(x,r);
  assume
A2: 0 <= a & a < f.p;
A3: p = |[p`1,p`2]| by EUCLID:57;
  then p in the carrier of Niemytzki-plane by A1,Lm1;
  then f.p in the carrier of I[01] by FUNCT_2:7;
  then f.p <= 1 by BORSUK_1:83,XXREAL_1:1;
  then
A4: a < 1 by A2,XXREAL_0:2;
  per cases by A2;
  suppose
A5: a = 0;
    reconsider r1 = p`2 as positive (real number) by A1;
    take r1; thus r1 <= p`2;
    reconsider A = Ball(p,r1) as Subset of Niemytzki-plane by A3,Lm1,Th24;
    let u;
    assume
A6: u in Ball(p,r1);
    then reconsider q = u as Point of TOP-REAL 2;
    q in A by A6;
    then reconsider z = q as Element of Niemytzki-plane;
    y=0-line misses Ball(p,r1) by A3,Th25;
    then not q in y=0-line & q = |[q`1,q`2]| & q in A
    by A6,EUCLID:57,XBOOLE_0:3;
    then q`2 <> 0 & |[x,0]|`2 = 0 & q`2 >= 0 & f.z in the carrier of I[01]
    by Lm1,Th22,EUCLID:56,FUNCT_2:7;
    then f.q <> 0 & f.z >= 0 & f.z <= 1 by Th64,BORSUK_1:83,XXREAL_1:1;
    then f.z in ].a,1.] by A5,XXREAL_1:2;
    hence thesis by FUNCT_2:46;
  end;
  suppose a > 0;
    then reconsider b = a as positive (real number);
    set r1 = min(|.p-|[x,r*a]|.|-r*a, p`2);
    not a > f.p & b <> f.p by A2;
    then not |.p-|[x,r*a]|.| < r*a & |.p-|[x,r*a]|.| <> r*a
    by A1,A4,Th66,Th67;
    then
A7: |.p-|[x,r*a]|.| > r*a by XXREAL_0:1;
    r1 = |.p-|[x,r*a]|.|-r*a or r1 = p`2 by XXREAL_0:def 9;
    then reconsider r1 as positive (real number) by A1,A7,XREAL_1:52;
    take r1;
    thus r1 <= p`2 by XXREAL_0:17;
    then reconsider A = Ball(p,r1) as Subset of Niemytzki-plane by A3,Lm1,Th24;
    let u;
    assume
A8: u in Ball(p,r1);
    then reconsider q = u as Point of TOP-REAL 2;
    u in A by A8;
    then reconsider z = q as Point of Niemytzki-plane;
    q = |[q`1,q`2]| & q in A by A8,EUCLID:57;
    then
A9: q`2 >= 0 by Lm1,Th22;
A10: now
      assume not f.z > a;
      then |.q-|[x,r*a]|.| <= r*a by A2,A4,A9,Th68;
      then |.|[x,r*a]|-q.| <= r*a & |.q-p.| < r1 by A8,TOPREAL9:7,TOPRNS_1:28;
      then |.|[x,r*a]|-q.|+|.q-p.| >= |.|[x,r*a]|-p.| &
      |.p-|[x,r*a]|.|-r*a >= r1 & |.|[x,r*a]|-q.|+|.q-p.| < r*a+r1
      by TOPRNS_1:35,XREAL_1:10,XXREAL_0:17;
      then |.|[x,r*a]|-p.| < r*a+r1 & r*a+r1 <= |.p-|[x,r*a]|.|-r*a+r*a &
      |.|[x,r*a]|-p.| = |.p-|[x,r*a]|.| by TOPRNS_1:28,XREAL_1:8,XXREAL_0:2;
      hence contradiction;
    end;
    f.z in the carrier of I[01] by FUNCT_2:7;
    then f.z <= 1 by BORSUK_1:83,XXREAL_1:1;
    then f.z in ].a,1.] by A10,XXREAL_1:2;
    hence thesis by FUNCT_2:46;
  end;
end;

theorem Th74:
  for p being Point of TOP-REAL 2 st p`2 = 0
  for x being real number, r being positive (real number) st +(x,r).p = 1
  ex r1 being positive (real number)
  st Ball(|[p`1,r1]|,r1) \/ {p} c= +(x,r)"{1}
proof
  let p be Point of TOP-REAL 2;
  assume
A1: p`2 = 0;
  let x be real number;
  let r be positive (real number);
  set r1 = (|.p-|[x,r]|.|-r)/2;
  set f = +(x,r);
  assume
A2: f.p = 1;
A3: p =|[p`1,p`2]| by EUCLID:57;
  then p`1-x <> 0 by A1,A2,Def5;
  then (p`1-x)^2 > 0 by SQUARE_1:74;
  then (p`1-x)^2+(0-r)^2 > 0+(0-r)^2 & (0-r)^2 = (r-0)^2 by XREAL_1:8;
  then |.|[p`1-x,p`2-r]|.|^2 > r^2 by A1,Th13;
  then |.p-|[x,r]|.| >= 0 & |.p-|[x,r]|.|^2 > r^2 by A3,EUCLID:66;
  then |.p-|[x,r]|.| > r by SQUARE_1:77;
  then |.p-|[x,r]|.|-r > 0 by XREAL_1:52;
  then r1 > 0/2;
  then reconsider r1 as positive (real number);
  take r1;
  let u;
  assume
A4: u in Ball(|[p`1,r1]|,r1) \/ {p};
  then
A5: u in Ball(|[p`1,r1]|,r1) or u = p by SETWISEO:6;
  reconsider q = u as Point of TOP-REAL 2 by A4;
  Ball(|[p`1,r1]|,r1) c= y>=0-plane by Th24;
  then reconsider z = q as Point of Niemytzki-plane by A1,A3,A5,Lm1,Th22;
A6: q = |[q`1,q`2]| by EUCLID:57;
  z in y>=0-plane by Lm1;
  then
A7: q`2 >= 0 by A6,Th22;
  now
    assume
A8: q in Ball(|[p`1,r1]|,r1);
    then |.q-|[p`1,r1]|.| < r1 & r1 is Real by TOPREAL9:7;
    then |.q-|[p`1,r1]|.|+|.|[p`1,r1]|-p.| < r1+|.|[p`1,r1]|-p.| &
    |.q-|[p`1,r1]|.|+|.|[p`1,r1]|-p.| >= |.q-p.| & |.r1.| = r1 &
    |[p`1,r1]|-p = |[p`1-p`1,r1-0]| & |.|[0,r1]|.| = |.r1.|
    by A1,A3,ABSVALUE:def 1,EUCLID:66,TOPREAL6:31,TOPRNS_1:35,XREAL_1:8;
    then r1+r1 > |.q-p.| by XXREAL_0:2;
    then |.q-p.|+|.|[x,r]|-q.| >= |.|[x,r]|-p.| &
    |.p-|[x,r]|.|-r+|.|[x,r]|-q.| > |.q-p.|+|.|[x,r]|-q.|
    by TOPRNS_1:35,XREAL_1:8;
    then |.|[x,r]|-p.| < |.p-|[x,r]|.|-r+|.|[x,r]|-q.| by XXREAL_0:2;
    then |.p-|[x,r]|.| = |.|[x,r]|-p.| &
    |.|[x,r]|-p.|-(|.p-|[x,r]|.|-r) < |.|[x,r]|-q.| by TOPRNS_1:28,XREAL_1:21;
    then |.q-|[x,r]|.| > r by TOPRNS_1:28;
    hence not q in Ball(|[x,r]|,r) by TOPREAL9:7;
    q`2 = 0 implies q in y=0-line & Ball(|[p`1,r1]|,r1) misses y=0-line
    by A6,Th25;
    hence q`2 <> 0 by A8,XBOOLE_0:3;
  end;
  then f.z = 1 by A2,A4,A6,A7,Def5,SETWISEO:6;
  then f.z in {1} by TARSKI:def 1;
  hence thesis by FUNCT_2:46;
end;

theorem Th75:
  for T being non empty TopSpace, S being SubSpace of T
  for B being Basis of T holds
  {A/\[#]S where A is Subset of T: A in B & A meets [#]S} is Basis of S
proof
  let T be non empty TopSpace;
  let S be SubSpace of T;
  let B be Basis of T;
  set X = {A/\[#]S where A is Subset of T: A in B & A meets [#]S};
  X c= bool the carrier of S
  proof
    let u;
    assume u in X;
    then ex A being Subset of T st u = A/\[#]S & A in B & A meets [#]S;
    hence thesis;
  end;
  then reconsider X as Subset-Family of S;
A1: now
    let U be Subset of S;
    assume U is open;
    then consider U0 being Subset of T such that
A2: U0 is open & U = U0 /\ the carrier of S by TSP_1:def 1;
    let x be Point of S;
    assume
A3: x in U;
    then x in U0 & x in the carrier of S by A2,XBOOLE_0:def 4;
    then consider V0 being Subset of T such that
A4: V0 in B & x in V0 & V0 c= U0 by A2,YELLOW_9:31;
    reconsider V = V0 /\ [#]S as Subset of S;
    take V;
    V0 meets [#]S by A3,A4,XBOOLE_0:3;
    hence V in X by A4;
    thus x in V by A3,A4,XBOOLE_0:def 4;
    thus V c= U by A2,A4,XBOOLE_1:26;
  end;
  X c= the topology of S
  proof
    let u;
    assume u in X;
    then consider A being Subset of T such that
A5: u = A/\[#]S & A in B & A meets [#]S;
    B c= the topology of T by CANTOR_1:def 2;
    hence thesis by A5,PRE_TOPC:def 9;
  end;
  hence thesis by A1,YELLOW_9:32;
end;

theorem Th76:
  {].a,b.[ where a,b is Real: a < b} is Basis of R^1
proof
  set X = {].a,b.[ where a,b is Real: a < b};
  X c= bool REAL
  proof
    let u;
    assume u in X;
    then ex a,b being Real st u = ].a,b.[ & a < b;
    hence thesis;
  end;
  then reconsider X as Subset-Family of R^1 by TOPMETR:24;
A1: now
    let U be Subset of R^1 such that
A2: U is open;
    let x be Point of R^1 such that
A3: x in U;
    reconsider a = x as Real by TOPMETR:24;
    consider r being Real such that
A4: 0 < r & ].a-r,a+r.[ c= U by A2,A3,FRECHET:8;
    reconsider V = ].a-r,a+r.[ as Subset of R^1 by TOPMETR:24;
    take V;
A5: a-r < a & a < a+r by A4,XREAL_1:31,46;
    then a-r < a+r by XXREAL_0:2;
    hence V in X;
    thus x in V by A5,XXREAL_1:4;
    thus V c= U by A4;
  end;
  X c= the topology of R^1
  proof
    let u;
    assume u in X;
    then ex a, b being Real st u = ].a,b.[ & a < b;
    then u is open Subset of R^1 by JORDAN6:46,TOPMETR:24;
    hence thesis by PRE_TOPC:def 5;
  end;
  hence thesis by A1,YELLOW_9:32;
end;

theorem Th77:
  for T being TopSpace for U,V being Subset of T for B being set
  st U in B & V in B & B \/ {U \/ V} is Basis of T holds B is Basis of T
proof
  let T be TopSpace;
  let U,V be Subset of T;
  let B be set;
  assume
A1: U in B & V in B & B \/ {U \/ V} is Basis of T;
A2: B c= B \/ {U \/ V} by XBOOLE_1:7;
  then reconsider B' = B as Subset-Family of T by A1,XBOOLE_1:1;
  B \/ {U \/ V} c= the topology of T by A1,CANTOR_1:def 2;
  then
A3: B c= the topology of T by A2,XBOOLE_1:1;
  now
    let A be Subset of T such that
A4: A is open;
    let p be Point of T;
    assume p in A;
    then consider A' being Subset of T such that
A5: A' in B \/ {U\/V} & p in A' & A' c= A by A1,A4,YELLOW_9:31;
    assume
A6: not ex a being Subset of T st a in B' & p in a & a c= A;
A7: U c= U\/V & V c= U\/V by XBOOLE_1:7;
    A' in B or A' = U \/ V by A5,SETWISEO:6;
    then p in U & U c= A or p in V & V c= A by A5,A6,A7,XBOOLE_0:def 3
,XBOOLE_1:1;
    hence contradiction by A1,A6;
  end;
  hence thesis by A3,YELLOW_9:32;
end;

theorem Th78:
  {[.0,a.[ where a is Real: 0 < a & a <= 1} \/
  {].a,1.] where a is Real: 0 <= a & a < 1} \/
  {].a,b.[ where a,b is Real: 0 <= a & a < b & b <= 1} is Basis of I[01]
proof
  set S = I[01], T = R^1;
  set A1 = {[.0,a.[ where a is Real: 0 < a & a <= 1};
  set A2 = {].a,1.] where a is Real: 0 <= a & a < 1};
  set A3 = {].a,b.[ where a,b is Real: 0 <= a & a < b & b <= 1};
  reconsider B = {].a,b.[ where a,b is Real: a < b} as Basis of R^1 by Th76;
  set B' = {A/\[#]S where A is Subset of T: A in B & A meets [#]S};
A1: B' = A1 \/ A2 \/ A3 \/ {[#]S}
  proof
    thus B' c= A1 \/ A2 \/ A3 \/ {[#]S}
    proof
      let u;
      assume u in B';
      then consider A being Subset of T such that
A2:   u = A/\[#]S & A in B & A meets [#]S;
      consider a,b being Real such that
A3:   A = ].a,b.[ & a < b by A2;
      consider x being set such that
A4:   x in A & x in [#]S by A2,XBOOLE_0:3;
      reconsider x as Real by A3,A4;
A5:   0 <= x & x <= 1 & a < x & x < b by A3,A4,BORSUK_1:83,XXREAL_1:1,4;
      per cases;
      suppose
A6:     a < 0 & b <= 1;
        then u = [.0,b.[ & 0 < b by A2,A3,A5,BORSUK_1:83,XXREAL_1:148;
        then u in A1 by A6;
        then u in A1 \/ A2 by XBOOLE_0:def 3;
        then u in A1 \/ A2 \/ (A3 \/ {[#]S}) by XBOOLE_0:def 3;
        hence thesis by XBOOLE_1:4;
      end;
      suppose a < 0 & b > 1;
        then u = [#]S by A2,A3,BORSUK_1:83,XBOOLE_1:28,XXREAL_1:47;
        hence thesis by SETWISEO:6;
      end;
      suppose
A7:     a >= 0 & b <= 1;
        then u = A by A2,A3,BORSUK_1:83,XBOOLE_1:28,XXREAL_1:37;
        then u in A3 by A3,A7;
        then u in A1 \/ A2 \/ A3 by XBOOLE_0:def 3;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose
A8:     a >= 0 & b > 1;
        then u = ].a,1.] & a < 1 by A2,A3,A5,BORSUK_1:83,XXREAL_0:2
,XXREAL_1:149;
        then u in A2 by A8;
        then u in A1 \/ A2 by XBOOLE_0:def 3;
        then u in A1 \/ A2 \/ (A3 \/ {[#]S}) by XBOOLE_0:def 3;
        hence thesis by XBOOLE_1:4;
      end;
    end;
    let u;
    assume u in A1 \/ A2 \/ A3 \/ {[#]S};
    then u in A1 \/ A2 \/ A3 or u in {[#]S} by XBOOLE_0:def 3;
    then
A9: u in A1 \/ A2 or u in A3 or u in {[#]S} by XBOOLE_0:def 3;
    reconsider aa = ].-1,2.[ as Subset of T by TOPMETR:24;
    per cases by A9,XBOOLE_0:def 3;
    suppose u in {[#]S};
      then u = [#]S & [#]S c= aa by BORSUK_1:83,TARSKI:def 1,XXREAL_1:47;
      then u = aa/\[#]S & aa in B & aa meets [#]S by XBOOLE_1:28,68;
      hence thesis;
    end;
    suppose u in A1;
      then consider a being Real such that
A10:  u = [.0,a.[ & 0 < a & a <= 1;
      reconsider A = ].-1,a.[ as Subset of T by TOPMETR:24;
      0 in [.0,1.] & 0 in A & -1 < a by A10,XXREAL_1:1,4;
      then u = A /\ [.0,1.] & A in B & A meets [#]S
      by A10,BORSUK_1:83,XBOOLE_0:3,XXREAL_1:148;
      hence thesis by BORSUK_1:83;
    end;
    suppose u in A2;
      then consider a being Real such that
A11:  u = ].a,1.] & 0 <= a & a < 1;
      reconsider A = ].a,2.[ as Subset of T by TOPMETR:24;
      1 in [.0,1.] & 1 in A & 2 > a by A11,XXREAL_0:2,XXREAL_1:1,4;
      then u = A /\ [.0,1.] & A in B & A meets [#]S
      by A11,BORSUK_1:83,XBOOLE_0:3,XXREAL_1:149;
      hence thesis by BORSUK_1:83;
    end;
    suppose u in A3;
      then consider a,b being Real such that
A12:  u = ].a,b.[ & 0 <= a & a < b & b <= 1;
      reconsider A = ].a,b.[ as Subset of T by TOPMETR:24;
      a+a < a+b & a+b < b+b by A12,XREAL_1:10;
      then (a+a)/2 < (a+b)/2 & (a+b)/2 < (b+b)/2 by XREAL_1:76;
      then
A13:  (a+b)/2 in A by XXREAL_1:4;
      A c= [.0,1.] by A12,XXREAL_1:37;
      then u = A /\ [.0,1.] & A in B & A meets [#]S
      by A12,A13,BORSUK_1:83,XBOOLE_0:3,XBOOLE_1:28;
      hence thesis by BORSUK_1:83;
    end;
  end;
  reconsider U = [.0,2/3.[, V = ].1/3,1.] as Subset of I[01]
  by BORSUK_1:83,XXREAL_1:35,36;
  U in A1 & V in A2;
  then U in A1 \/ A2 & V in A1 \/ A2 by XBOOLE_0:def 3;
  then
A14: U in A1 \/ A2 \/ A3 & V in A1 \/ A2 \/ A3 by XBOOLE_0:def 3;
  U \/ V = [#]S by BORSUK_1:83,XXREAL_1:175;
  hence thesis by A1,A14,Th75,Th77;
end;

theorem Th79:
  for T being non empty TopSpace
  for f being Function of T, I[01] holds f is continuous iff
  for a,b being real number st 0 <= a & a < 1 & 0 < b & b <= 1
  holds f"[.0,b.[ is open & f"].a,1.] is open
proof
  let T be non empty TopSpace;
  set A1 = {[.0,a.[ where a is Real: 0 < a & a <= 1};
  set A2 = {].a,1.] where a is Real: 0 <= a & a < 1};
  set A3 = {].a,b.[ where a,b is Real: 0 <= a & a < b & b <= 1};
  reconsider B = A1 \/ A2 \/ A3 as Basis of I[01] by Th78;
  let f be Function of T, I[01];
  hereby
    assume
A1: f is continuous;
    let a,b be real number;
    reconsider x = a, y = b as Real by XREAL_0:def 1;
    assume 0 <= a & a < 1 & 0 < b & b <= 1;
    then [.0,y.[ in A1 & ].x,1.] in A2;
    then [.0,y.[ in A1 \/ A2 & ].x,1.] in A1 \/ A2 by XBOOLE_0:def 3;
    then [.0,y.[ in B & ].x,1.] in B by XBOOLE_0:def 3;
    hence f"[.0,b.[ is open & f"].a,1.] is open by A1,YELLOW_9:34;
  end;
  assume
A2: for a,b being real number st 0 <= a & a < 1 & 0 < b & b <= 1
  holds f"[.0,b.[ is open & f"].a,1.] is open;
  now
    let A be Subset of I[01];
    assume A in B;
    then
A3: A in A1 \/ A2 or A in A3 by XBOOLE_0:def 3;
    per cases by A3,XBOOLE_0:def 3;
    suppose A in A1;
      then ex a being Real st A = [.0,a.[ & 0 < a & a <= 1;
      hence f"A is open by A2;
    end;
    suppose A in A2;
      then ex a being Real st A = ].a,1.] & 0 <= a & a < 1;
      hence f"A is open by A2;
    end;
    suppose A in A3;
      then consider a,b being Real such that
A4:   A = ].a,b.[ & 0 <= a & a < b & b <= 1;
      0 < b & a < 1 by A4,XXREAL_0:2;

then reconsider U = f"[.0,b.[, V = f"].a,1.] as open Subset of T by A2,A4;
      A = [.0,b.[ /\ ].a,1.] by A4,XXREAL_1:153;
      then f"A = U /\ V by FUNCT_1:137;
      hence f"A is open by TOPS_1:38;
    end;
  end;
  hence thesis by YELLOW_9:34;
end;

registration
  let x be real number, r be positive (real number);
  cluster +(x,r) -> continuous;
  coherence
  proof
    set f = +(x,r);
    consider BB being Neighborhood_System of Niemytzki-plane such that
A1: for x holds BB.(|[x,0]|) =
    {Ball(|[x,q]|,q) \/ {|[x,0]|} where q is Element of REAL: q > 0} and
A2: for x,y st y > 0 holds BB.(|[x,y]|) =
    {Ball(|[x,y]|,q) /\ y>=0-plane where q is Element of REAL: q > 0} by Def3;
A3: dom BB = y>=0-plane by Lm1,PARTFUN1:def 4;
    now
      let a,b be real number;
      assume
A4:   0 <= a & a < 1 & 0 < b & b <= 1;
      now
        let c be Element of Niemytzki-plane;
        c in y>=0-plane by Lm1;
        then reconsider z = c as Point of TOP-REAL 2;
        z = |[z`1,z`2]| by EUCLID:57;
        then
A5:     z`2 >= 0 by Lm1,Th22;
        assume c in f"[.0,b.[;
        then
A6:     f.c in [.0,b.[ by FUNCT_1:def 13;
        per cases by A6,XXREAL_1:3;
        suppose f.c = 0;
          then
A7:       z = |[x,0]| & 0 < b by A4,A5,Th64;
          r*b > 0 * b by A4;
          then reconsider r1 = r*b as positive (real number);
          reconsider U = Ball(|[x,r1]|,r1) \/ {|[x,0]|} as
          Subset of Niemytzki-plane by Th31;
          take U;
A8:       r1 is Real & x is Real by XREAL_0:def 1;
          then U in {Ball(|[x,q]|,q) \/ {|[x,0]|} where q is Element of REAL:
          q > 0};
          then U in BB.(|[x,0]|) & |[x,0]| in y>=0-plane by A1,A8;
          hence U in Union BB & c in U & U c= f"[.0,b.[ by A3,A7,Th71,CARD_5:10
,SETWISEO:6;
        end;
        suppose 0 < f.c & f.c < b;
          then
A9:       z in Ball(|[x,r*b]|,r*b) & Ball(|[x,r*b]|,r*b) c= +(x, r)"].0,b.[ &
          0 < b by A4,A5,Th70,Th72;
          r*b > 0 * b by A4;
          then reconsider r1 = r*b as positive (real number);
          reconsider U = Ball(|[x,r1]|,r1) as Subset of Niemytzki-plane
          by Th33;
          take U;
          U c= y>=0-plane by Th24;
          then
A10:      U /\ y>=0-plane = U & r1 is Real & x is Real
          by XBOOLE_1:28,XREAL_0:def 1;
          then U in {Ball(|[x,r1]|,q) /\ y>=0-plane where q is Element of REAL
          : q > 0};
          then U in BB.(|[x,r1]|) & |[x,r1]| in y>=0-plane by A2,A10;
          hence U in Union BB by A3,CARD_5:10;
          f"].0,b.[ c= f"[.0,b.[ by RELAT_1:178,XXREAL_1:45;
          hence c in U & U c= f"[.0,b.[ by A9,XBOOLE_1:1;
        end;
      end;
      hence f"[.0,b.[ is open by YELLOW_9:31;
      now
        let c be Element of Niemytzki-plane;
        c in y>=0-plane by Lm1;
        then reconsider z = c as Point of TOP-REAL 2;
A11:    z = |[z`1,z`2]| by EUCLID:57;
        then
A12:    z`2 >= 0 by Lm1,Th22;
        assume c in f"].a,1.];
        then f.c in ].a,1.] by FUNCT_1:def 13;
        then
A13:    f.c > a & f.c <= 1 by XXREAL_1:2;
        per cases by A11,A13,Lm1,Th22,XXREAL_0:1;
        suppose z`2 > 0;
          then consider r1 being positive (real number) such that
A14:      r1 <= z`2 & Ball(z,r1) c= +(x,r)"].a,1.] by A4,A13,Th73;
          reconsider U = Ball(z,r1) as Subset of Niemytzki-plane by A11,A14
,Th33;
          take U;
          U c= y>=0-plane by A11,A14,Th24;
          then U /\ y>=0-plane = U & r1 is Real & x is Real
          by XBOOLE_1:28,XREAL_0:def 1;
          then U in {Ball(|[z`1,z`2]|,q) /\ y>=0-plane where q is Element of
          REAL: q > 0} & z`2 > 0 by A11,A14;
          then U in BB.(|[z`1,z`2]|) & |[z`1,z`2]| in y>=0-plane by A2;
          hence U in Union BB by A3,CARD_5:10;
          thus c in U & U c= f"].a,1.] by A14,Th17;
        end;
        suppose
A15:      f.c = 1 & z`2 = 0;
          then consider r1 being positive (real number) such that
A16:      Ball(|[z`1,r1]|,r1) \/ {z} c= +(x,r)"{1} by Th74;
          reconsider U = Ball(|[z`1,r1]|,r1) \/ {z} as
          Subset of Niemytzki-plane by A11,A15,Th31;
          take U;
          r1 is Real by XREAL_0:def 1;
          then U in {Ball(|[z`1,q]|,q) \/ {|[z`1,0]|}
          where q is Element of REAL: q > 0} by A11,A15;
          then U in BB.(|[z`1,z`2]|) & |[z`1,z`2]| in y>=0-plane by A1,A15;
          hence U in Union BB by A3,CARD_5:10;
          thus c in U by SETWISEO:6;
          1 in ].a,1.] by A4,XXREAL_1:2;
          then {1} c= ].a,1.] by ZFMISC_1:37;
          then f"{1} c= f"].a,1.] by RELAT_1:178;
          hence U c= f"].a,1.] by A16,XBOOLE_1:1;
        end;
        suppose a < f.c & f.c < 1;
          then f.c in ].a,1.[ by XXREAL_1:4;
          then consider r1 being positive (real number) such that
A17:      r1 <= z`2 & Ball(z,r1) c= +(x,r)"].a,1.[ by A4,A12,Th69;
          reconsider U = Ball(z,r1) as Subset of Niemytzki-plane by A11,A17
,Th33;
          take U;
          U c= y>=0-plane by A11,A17,Th24;
          then U /\ y>=0-plane = U & r1 is Real & x is Real
          by XBOOLE_1:28,XREAL_0:def 1;
          then U in {Ball(|[z`1,z`2]|,q) /\ y>=0-plane where q is Element of
          REAL: q > 0} & z`2 > 0 by A11,A17;
          then U in BB.(|[z`1,z`2]|) & |[z`1,z`2]| in y>=0-plane by A2;
          hence U in Union BB by A3,CARD_5:10;
          f"].a,1.[ c= f"].a,1.] by RELAT_1:178,XXREAL_1:41;
          hence c in U & U c= f"].a,1.] by A17,Th17,XBOOLE_1:1;
        end;
      end;
      hence f"].a,1.] is open by YELLOW_9:31;
    end;
    hence thesis by Th79;
  end;
end;

theorem Th80:
  for U being Subset of Niemytzki-plane
  for x,r st U = Ball(|[x,r]|,r) \/ {|[x,0]|}
  ex f being continuous Function of Niemytzki-plane, I[01] st
  f.(|[x,0]|) = 0 & for a,b being real number holds
  (|[a,b]| in U` implies f.(|[a,b]|) = 1) & (|[a,b]| in U\{|[x,0]|} implies
  f.(|[a,b]|) = |.|[x,0]|-|[a,b]|.|^2/(2*r*b))
proof
  let U be Subset of Niemytzki-plane;
  let x,r;
  assume
A1: U = Ball(|[x,r]|,r) \/ {|[x,0]|};
  take f = +(x,r);
  thus f.(|[x,0]|) = 0 by Def5;
  let a,b be real number;
  thus (|[a,b]| in U` implies f.(|[a,b]|) = 1)
  proof
    assume
A2: |[a,b]| in U`;
    then
A3: b >= 0 by Lm1,Th22;
    not |[a,b]| in U by A2,XBOOLE_0:def 5;
    then not |[a,b]| in Ball(|[x,r]|,r) & (a <> x or b <> 0) by A1,SETWISEO:6;
    hence thesis by A3,Def5;
  end;
  assume |[a,b]| in U\{|[x,0]|};
  then |[a,b]| in U & not |[a,b]| in {|[x,0]|} by XBOOLE_0:def 5;
  then b >= 0 & |[a,b]| in Ball(|[x,r]|,r) by A1,Lm1,Th22,XBOOLE_0:def 3;
  hence thesis by Def5;
end;

::<DESC name="1.4.5. Przyk/lad funkcji ci/ag/lej z p/laszczyzny Niemytzkiego w R^1" monograph="topology"/>

definition
  let x,y be real number, r be positive (real number);
  func +(x,y,r) -> Function of Niemytzki-plane, I[01] means
  :Def6:
  for a being real number, b being non negative (real number) holds
  (not |[a,b]| in Ball(|[x,y]|,r) implies it.(|[a,b]|) = 1) &
  (|[a,b]| in Ball(|[x,y]|,r) implies it.(|[a,b]|) = |.|[x,y]|-|[a,b]|.|/r);
  existence
  proof
    defpred P[set] means not $1 in Ball(|[x,y]|, r);
    deffunc F2(Point of TOP-REAL 2) = |.|[x,y]|-$1.|/r;
    deffunc F1(Point of TOP-REAL 2) = 1;
    the carrier of Niemytzki-plane = y>=0-plane by Def3;
    then
A1: the carrier of Niemytzki-plane c= the carrier of TOP-REAL 2;
A2: for a being Point of TOP-REAL 2 st a in the carrier of Niemytzki-plane
    holds (P[a] implies F1(a) in the carrier of I[01]) &
    (not P[a] implies F2(a) in the carrier of I[01])
    proof
      let a be Point of TOP-REAL 2;
      assume a in the carrier of Niemytzki-plane;
      thus P[a] implies F1(a) in the carrier of I[01]
      by BORSUK_1:83,XXREAL_1:1;
      assume not P[a];
      then |.a-|[x,y]|.| < r by TOPREAL9:7;
      then |.|[x,y]|-a.| < r & |.|[x,y]|-a.| >= 0 by TOPRNS_1:28;
      then 0/r <= F2(a) & F2(a) < r/r & r/r = 1 by XCMPLX_1:60,XREAL_1:76;
      hence thesis by BORSUK_1:83,XXREAL_1:1;
    end;
    consider f being Function of Niemytzki-plane, I[01] such that
A3: for a being Point of TOP-REAL 2 st a in the carrier of Niemytzki-plane
    holds (P[a] implies f.a = F1(a)) &
    (not P[a] implies f.a = F2(a)) from SCH1(A1,A2);
    take f;
    let a be real number, b be non negative (real number);
    |[a,b]| in the carrier of Niemytzki-plane by Lm1,Th22;
    hence thesis by A3;
  end;
  uniqueness
  proof
    let f,g be Function of Niemytzki-plane, I[01] such that
A4: for a being real number, b being non negative (real number) holds
    (not |[a,b]| in Ball(|[x,y]|,r) implies f.(|[a,b]|) = 1) &
    (|[a,b]| in Ball(|[x,y]|,r) implies f.(|[a,b]|) = |.|[x,y]|-|[a,b]|.|/r)
    and
A5: for a being real number, b being non negative (real number) holds
    (not |[a,b]| in Ball(|[x,y]|,r) implies g.(|[a,b]|) = 1) &
    (|[a,b]| in Ball(|[x,y]|,r) implies g.(|[a,b]|) = |.|[x,y]|-|[a,b]|.|/r);
A6: the carrier of Niemytzki-plane = y>=0-plane by Def3;
    now
      let p be Point of Niemytzki-plane;
      p in y>=0-plane by A6;
      then reconsider q = p as Point of TOP-REAL 2;
A7:   p = |[q`1,q`2]| by EUCLID:57;
      then reconsider q2 = q`2 as non negative (real number) by A6,Th22;
      per cases;
      suppose not |[q`1,q2]| in Ball(|[x,y]|,r);
        then f.p = 1 & g.p = 1 by A4,A5,A7;
        hence f.p = g.p;
      end;
      suppose |[q`1,q2]| in Ball(|[x,y]|,r);
        then f.p = |.|[x,y]|-q.|/r & g.p = |.|[x,y]|-q.|/r by A4,A5,A7;
        hence f.p = g.p;
      end;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem Th81:
  for p being Point of TOP-REAL 2 st p`2 >= 0
  for x being real number, y being non negative (real number)
  for r being positive (real number) holds +(x,y,r).p = 0 iff p = |[x,y]|
proof
  let p be Point of TOP-REAL 2;
  assume
A1: p`2 >= 0;
A2: p = |[p`1,p`2]| by EUCLID:57;
  let x be real number;
  let y be non negative (real number);
  let r be positive (real number);
  hereby
    assume
A3: +(x,y,r).p = 0;
    then p in Ball(|[x,y]|,r) by A1,A2,Def6;
    then 0 = |.|[x,y]|-p.|/r by A1,A2,A3,Def6;
    then 0 * r = |.|[x,y]|-p.|;
    hence p = |[x,y]| by TOPRNS_1:29;
  end;
  assume
A4: p = |[x,y]|;
  then p in Ball(|[x,y]|,r) by Th17;
  hence +(x,y,r).p = |.|[x,y]|-p.|/r by A4,Def6
    .= 0/r by A4,TOPRNS_1:29
    .= 0;
end;

theorem Th82:
  for x being real number, y being non negative (real number)
  for r,a being positive (real number) st a <= 1
  holds +(x,y,r)"[.0,a.[ = Ball(|[x,y]|,r*a) /\ y>=0-plane
proof
  let x be real number;
  let y be non negative (real number);
  let r,a be positive (real number);
  set f = +(x,y,r);
  assume
A1: a <= 1;
  thus +(x,y,r)"[.0,a.[ c= Ball(|[x,y]|,r*a) /\ y>=0-plane
  proof
    let u;
    assume
A2: u in f"[.0,a.[;
    then reconsider p = u as Point of Niemytzki-plane;
    p in y>=0-plane by Lm1;
    then reconsider q = p as Element of TOP-REAL 2;
A3: p = |[q`1,q`2]| by EUCLID:57;
    f.p in [.0,a.[ by A2,FUNCT_2:46;
    then
A4: f.p < a by XXREAL_1:3;
    then
A5: f.p < 1 & q`2 >= 0 by A1,A3,Lm1,Th22,XXREAL_0:2;
    then p in Ball(|[x,y]|,r) by A3,Def6;
    then f.p = |.|[x,y]|-q.|/r by A3,A5,Def6;
    then |.|[x,y]|-q.| = |.q-|[x,y]|.| & |.|[x,y]|-q.| < r*a
    by A4,TOPRNS_1:28,XREAL_1:79;
    then p in Ball(|[x,y]|,r*a) by TOPREAL9:7;
    hence thesis by Lm1,XBOOLE_0:def 4;
  end;
  let u;
  assume
A6: u in Ball(|[x,y]|,r*a) /\ y>=0-plane;
  then
A7: u in Ball(|[x,y]|,r*a) & u in y>=0-plane by XBOOLE_0:def 4;
  reconsider p = u as Point of Niemytzki-plane by A6,Lm1,XBOOLE_0:def 4;
  reconsider q = p as Element of TOP-REAL 2 by A6;
A8: p = |[q`1,q`2]| by EUCLID:57;
  then
A9: q`2 >= 0 by A7,Th22;
  r*a <= r*1 by A1,XREAL_1:66;
  then Ball(|[x,y]|,r*a) c= Ball(|[x,y]|,r) by JORDAN:18;
  then f.p = |.|[x,y]|-q.|/r by A7,A8,A9,Def6;
  then f.p in [.0,1.] & r*(f.p) = |.|[x,y]|-q.| & |.q-|[x,y]|.| < r*a &
  |.|[x,y]|-q.| = |.q-|[x,y]|.|
  by A7,BORSUK_1:83,FUNCT_2:7,TOPREAL9:7,TOPRNS_1:28,XCMPLX_1:88;
  then f.p >= 0 & f.p < a by XREAL_1:66;
  then f.p in [.0,a.[ by XXREAL_1:3;
  hence thesis by FUNCT_2:46;
end;

theorem Th83:
  for p being Point of TOP-REAL 2 st p`2 > 0
  for x being real number, a being non negative (real number)
  for y,r being positive (real number) st +(x,y,r).p > a
  holds |.|[x,y]|-p.| > r*a &
  Ball(p,|.|[x,y]|-p.|-r*a) /\ y>=0-plane c= +(x,y,r)"].a,1.]
proof
  let p be Point of TOP-REAL 2;
  assume
A1: p`2 > 0;
A2: p = |[p`1,p`2]| by EUCLID:57;
  let x be real number;
  let a be non negative (real number);
  let y,r be positive (real number);
  set f = +(x,y,r);
  assume
A3: f.p > a;
  p in y>=0-plane by A1,A2;
  then f.p in [.0,1.] by Lm1,BORSUK_1:83,FUNCT_2:7;
  then
A4: f.p <= 1 by XXREAL_1:1;
  then
A5: a < 1 by A3,XXREAL_0:2;
A6: |.|[x,y]|-p.| = |.p-|[x,y]|.| by TOPRNS_1:28;
  thus |.|[x,y]|-p.| > r*a
  proof per cases by A4,XXREAL_0:1;
    suppose f.p < 1;
      then p in Ball(|[x,y]|,r) by A1,A2,Def6;
      then f.p = |.|[x,y]|-p.|/r by A1,A2,Def6;
      hence thesis by A3,XREAL_1:81;
    end;
    suppose
A7:   f.p = 1;
      now
        assume p in Ball(|[x,y]|,r);
        then 1 = |.|[x,y]|-p.|/r & |.p-|[x,y]|.| < r & r/r = 1
        by A1,A2,A7,Def6,TOPREAL9:7,XCMPLX_1:60;
        hence contradiction by A6,XREAL_1:76;
      end;
      then |.p-|[x,y]|.| >= r & r*1 > r*a by A5,TOPREAL9:7,XREAL_1:70;
      hence thesis by A6,XXREAL_0:2;
    end;
  end;
  then reconsider r1 = |.|[x,y]|-p.|-r*a as positive (real number) by
XREAL_1:52;
  let u;
  assume
A8: u in Ball(p,|.|[x,y]|-p.|-r*a) /\ y>=0-plane;
  then
A9: u in Ball(p,|.|[x,y]|-p.|-r*a) & u in y>=0-plane by XBOOLE_0:def 4;
  reconsider z = u as Point of Niemytzki-plane by A8,Lm1,XBOOLE_0:def 4;
  reconsider q = z as Element of TOP-REAL 2 by A8;
A10: q = |[q`1,q`2]| by EUCLID:57;
  then
A11: q`2 >= 0 by Lm1,Th22;
  then
A12: not q in Ball(|[x,y]|,r) implies f.q = 1 by A10,Def6;
  |.q-p.| < r1 by A9,TOPREAL9:7;
  then |.|[x,y]|-q.|+|.q-p.| < |.|[x,y]|-q.|+r1 &
  |.|[x,y]|-p.| <= |.|[x,y]|-q.|+|.q-p.| by TOPRNS_1:35,XREAL_1:8;
  then |.|[x,y]|-p.| < |.|[x,y]|-q.|+r1 by XXREAL_0:2;
  then
A13: |.|[x,y]|-p.|-r1 < |.|[x,y]|-q.|+r1-r1 by XREAL_1:11;
A14: now
    assume q in Ball(|[x,y]|,r);
    then f.q = |.|[x,y]|-q.|/r ::& (r*a)/r = a
    by A10,A11,Def6;
    hence f.z > a by A13,XREAL_1:83;
  end;
  f.z in [.0,1.] by BORSUK_1:83,FUNCT_2:7;
  then f.z <= 1 by XXREAL_1:1;
  then f.z in ].a,1.] by A5,A12,A14,XXREAL_1:2;
  hence thesis by FUNCT_2:46;
end;

theorem Th84:
  for p being Point of TOP-REAL 2 st p`2 = 0
  for x being real number, a being non negative (real number)
  for y,r being positive (real number) st +(x,y,r).p > a
  holds |.|[x,y]|-p.| > r*a &
  ex r1 being positive (real number) st r1 = (|.|[x,y]|-p.|-r*a)/2 &
  Ball(|[p`1,r1]|,r1) \/ {p} c= +(x,y,r)"].a,1.]
proof
  let p be Point of TOP-REAL 2;
  assume
A1: p`2 = 0;
A2: p = |[p`1,p`2]| by EUCLID:57;
  then reconsider p' = p as Point of Niemytzki-plane by A1,Lm1,Th22;
  let x be real number;
  let a be non negative (real number);
  let y,r be positive (real number);
  set f = +(x,y,r);
  assume
A3: f.p > a;
  p in y>=0-plane by A1,A2;
  then f.p in [.0,1.] by Lm1,BORSUK_1:83,FUNCT_2:7;
  then
A4: f.p <= 1 by XXREAL_1:1;
  then
A5: a < 1 by A3,XXREAL_0:2;
A6: |.|[x,y]|-p.| = |.p-|[x,y]|.| by TOPRNS_1:28;
  thus |.|[x,y]|-p.| > r*a
  proof per cases by A4,XXREAL_0:1;
    suppose f.p < 1;
      then p in Ball(|[x,y]|,r) by A1,A2,Def6;
      then f.p = |.|[x,y]|-p.|/r by A1,A2,Def6;
      hence thesis by A3,XREAL_1:81;
    end;
    suppose
A7:   f.p = 1;
      now
        assume p in Ball(|[x,y]|,r);
        then 1 = |.|[x,y]|-p.|/r & |.p-|[x,y]|.| < r & r/r = 1
        by A1,A2,A7,Def6,TOPREAL9:7,XCMPLX_1:60;
        hence contradiction by A6,XREAL_1:76;
      end;
      then |.p-|[x,y]|.| >= r & r*1 > r*a by A5,TOPREAL9:7,XREAL_1:70;
      hence thesis by A6,XXREAL_0:2;
    end;
  end;
  then reconsider r' = |.|[x,y]|-p.|-r*a as positive (real number) by
XREAL_1:52;
  take r1 = r'/2;
  thus r1 = (|.|[x,y]|-p.|-r*a)/2;
  let u;
  assume
A8: u in Ball(|[p`1,r1]|,r1) \/ {p};
  then
A9: u in Ball(|[p`1,r1]|,r1) or u = p' by SETWISEO:6;
  Ball(|[p`1,r1]|,r1) c= y>=0-plane by Th24;
  then reconsider z = u as Point of Niemytzki-plane by A9,Def3;
  reconsider q = z as Element of TOP-REAL 2 by A8;
A10: q = |[q`1,q`2]| by EUCLID:57;
  then
A11: q`2 >= 0 by Lm1,Th22;
  then
A12: not q in Ball(|[x,y]|,r) implies f.q = 1 by A10,Def6;
  per cases by A8,SETWISEO:6;
  suppose u = p';
    then f.z in ].a,1.] by A3,A4,XXREAL_1:2;
    hence thesis by FUNCT_2:46;
  end;
  suppose u in Ball(|[p`1,r1]|,r1);
    then |.q-|[p`1,r1]|.| < r1 by TOPREAL9:7;
    then |.q-|[p`1,r1]|.|+|.|[p`1,r1]|-p.| < r1+|.|[p`1,r1]|-p.| &
    |.q-p.| <= |.q-|[p`1,r1]|.|+|.|[p`1,r1]|-p.| & r1 is Real
    by TOPRNS_1:35,XREAL_1:8;
    then |.q-p.| < r1+|.|[p`1,r1]|-p.| & |.|[p`1,r1]|-p.| = |.|[p`1-p`1,r1-0
    ]|.| & |.|[0,r1]|.| = abs r1 & abs r1 = r1
    by A1,A2,ABSVALUE:def 1,EUCLID:66,TOPREAL6:31,XXREAL_0:2;
    then |.|[x,y]|-q.|+|.q-p.| < |.|[x,y]|-q.|+(r1+r1) &
    |.|[x,y]|-p.| <= |.|[x,y]|-q.|+|.q-p.| by TOPRNS_1:35,XREAL_1:8;
    then |.|[x,y]|-p.| < |.|[x,y]|-q.|+(r1+r1) by XXREAL_0:2;
    then
A13: |.|[x,y]|-p.|-(r1+r1) < |.|[x,y]|-q.|+(r1+r1)-(r1+r1) by XREAL_1:11;
A14: now
      assume q in Ball(|[x,y]|,r);
      then f.q = |.|[x,y]|-q.|/r ::& (r*a)/r = a
      by A10,A11,Def6;
      hence f.z > a by A13,XREAL_1:83;
    end;
    f.z in [.0,1.] by BORSUK_1:83,FUNCT_2:7;
    then f.z <= 1 by XXREAL_1:1;
    then f.z in ].a,1.] by A5,A12,A14,XXREAL_1:2;
    hence thesis by FUNCT_2:46;
  end;
end;

registration
  let x be real number;
  let y,r be positive (real number);
  cluster +(x,y,r) -> continuous;
  coherence
  proof
    set f = +(x,y,r);
    consider BB being Neighborhood_System of Niemytzki-plane such that
A1: for x holds BB.(|[x,0]|) =
    {Ball(|[x,q]|,q) \/ {|[x,0]|} where q is Element of REAL: q > 0} and
A2: for x,y st y > 0 holds BB.(|[x,y]|) =
    {Ball(|[x,y]|,q) /\ y>=0-plane where q is Element of REAL: q > 0} by Def3;
A3: dom BB = y>=0-plane by Lm1,PARTFUN1:def 4;
    now
      let a,b be real number;
      assume
A4:   0 <= a & a < 1 & 0 < b & b <= 1;
      then r*b > r*0 & f"[.0,b.[ = Ball(|[x,y]|,r*b) /\ y>=0-plane by Th82;
      hence f"[.0,b.[ is open by Th32;
      now
        let c be Element of Niemytzki-plane;
        c in y>=0-plane by Lm1;
        then reconsider z = c as Point of TOP-REAL 2;
A5:     z = |[z`1,z`2]| by EUCLID:57;
        assume c in f"].a,1.];
        then f.c in ].a,1.] by FUNCT_1:def 13;
        then
A6:     f.c > a & f.c <= 1 by XXREAL_1:2;
        per cases by A5,Lm1,Th22;
        suppose
A7:       z`2 > 0;
          then |.|[x,y]|-z.| > r*a &
          Ball(z,|.|[x,y]|-z.|-r*a) /\ y>=0-plane c= f"].a,1.] by A4,A6,Th83;
          then reconsider r1 = |.|[x,y]|-z.|-r*a as positive (real number)
          by XREAL_1:52;
          reconsider U = Ball(z,r1) /\ y>=0-plane as Subset of Niemytzki-plane
          by A5,A7,Th32;
          take U;
          U in {Ball(|[z`1,z`2]|,q) /\ y>=0-plane where q is Element of REAL:
          q > 0} by A5;
          then U in BB.(|[z`1,z`2]|) & |[z`1,z`2]| in y>=0-plane by A2,A7;
          hence U in Union BB by A3,CARD_5:10;
          c in Ball(z,r1) by Th17;
          hence c in U & U c= f"].a,1.] by A4,A6,A7,Lm1,Th83,XBOOLE_0:def 4;
        end;
        suppose
A8:       z`2 = 0;
          then consider r1 being positive (real number) such that
A9:       r1 = (|.|[x,y]|-z.|-r*a)/2 &
          Ball(|[z`1,r1]|,r1) \/ {z} c= +(x,y,r)"].a,1.] by A4,A6,Th84;
          reconsider U = Ball(|[z`1,r1]|,r1) \/ {z} as
          Subset of Niemytzki-plane by A5,A8,Th31;
          take U;
          r1 is Real by XREAL_0:def 1;
          then U in {Ball(|[z`1,q]|,q) \/ {|[z`1,0]|}
          where q is Element of REAL: q > 0} by A5,A8;
          then U in BB.(|[z`1,z`2]|) & |[z`1,z`2]| in y>=0-plane by A1,A8;
          hence U in Union BB by A3,CARD_5:10;
          thus c in U by SETWISEO:6;
          thus U c= f"].a,1.] by A9;
        end;
      end;
      hence f"].a,1.] is open by YELLOW_9:31;
    end;
    hence thesis by Th79;
  end;
end;

theorem Th85:
  for U being Subset of Niemytzki-plane
  for x,y,r st y > 0 & U = Ball(|[x,y]|,r) /\ y>=0-plane
  ex f being continuous Function of Niemytzki-plane, I[01] st
  f.(|[x,y]|) = 0 & for a,b being real number holds
  (|[a,b]| in U` implies f.(|[a,b]|) = 1) &
  (|[a,b]| in U implies f.(|[a,b]|) = |.|[x,y]|-|[a,b]|.|/r)
proof
  let U be Subset of Niemytzki-plane;
  let x,y,r;
  assume
A1: y > 0 & U = Ball(|[x,y]|,r) /\ y>=0-plane;
  then reconsider y' = y as positive (real number);
  take f = +(x,y',r);
  |[x,y']|`2 = y by EUCLID:56;
  hence f.(|[x,y]|) = 0 by Th81;
  let a,b be real number;
  thus (|[a,b]| in U` implies f.(|[a,b]|) = 1)
  proof
    assume
A2: |[a,b]| in U`;
    then
A3: b >= 0 by Lm1,Th22;
    not |[a,b]| in U by A2,XBOOLE_0:def 5;
    then not |[a,b]| in Ball(|[x,y]|,r) by A1,A2,Lm1,XBOOLE_0:def 4;
    hence thesis by A3,Def6;
  end;
  assume |[a,b]| in U;
  then b >= 0 & |[a,b]| in Ball(|[x,y]|,r) by A1,Lm1,Th22,XBOOLE_0:def 4;
  hence thesis by Def6;
end;

theorem Th86:
  Niemytzki-plane is T_1
proof
  set X = Niemytzki-plane;
A1: the carrier of X = y>=0-plane by Def3;
  let x,y be Point of X;
  x in y>=0-plane & y in y>=0-plane by A1;
  then reconsider a = x, b = y as Point of TOP-REAL 2;
  assume x <> y;
  then |.a-b.| <> 0 by TOPRNS_1:29;
  then |.a-b.| > 0;
  then reconsider r = |.a-b.| as positive (real number);
  consider q being Point of TOP-REAL 2, U being open Subset of X such that
A2: x in U & q in U &
  for c being Point of TOP-REAL 2 st c in U holds |.c-q.| < r/2 by Th34;
  consider p being Point of TOP-REAL 2, V being open Subset of X such that
A3: y in V & p in V &
  for c being Point of TOP-REAL 2 st c in V holds |.c-p.| < r/2 by Th34;
  take U,V; thus U is open & V is open & x in U by A2;
  hereby
    assume y in U;
    then |.b-q.| < r/2 & |.a-q.| < r/2 by A2;
    then |.a-b.| <= |.a-q.|+|.q-b.| & |.q-b.| = |.b-q.| &
    |.a-q.|+|.b-q.| < (r/2)+r/2 by TOPRNS_1:28,35,XREAL_1:10;
    hence contradiction;
  end;
  thus y in V by A3;
  assume x in V;
  then |.b-p.| < r/2 & |.a-p.| < r/2 by A3;
  then |.a-b.| <= |.a-p.|+|.p-b.| & |.p-b.| = |.b-p.| &
  |.a-p.|+|.b-p.| < (r/2)+r/2 by TOPRNS_1:28,35,XREAL_1:10;
  hence contradiction;
end;

theorem
  Niemytzki-plane is Tychonoff
proof
  set X = Niemytzki-plane;
A1: the carrier of X = y>=0-plane by Def3;
  consider B being Neighborhood_System of X such that
A2: for x holds B.(|[x,0]|) =
  {Ball(|[x,r]|,r) \/ {|[x,0]|} where r is Element of REAL: r > 0} and
A3: for x,y st y > 0 holds B.(|[x,y]|) =
  {Ball(|[x,y]|,r) /\ y>=0-plane where r is Element of REAL: r > 0} by Def3;
  reconsider uB = Union B as prebasis of X by YELLOW_9:27;
  now
    let x be Point of X;
    let V be Subset of X;
    assume
A4: x in V & V in uB;
    uB is open by YELLOW_9:28;
    then V is open by A4,TOPS_2:def 1;
    then consider V' being Subset of X such that
A5: V' in B.x & V' c= V by A4,YELLOW_8:def 2;
    x in y>=0-plane by A1;
    then reconsider x' = x as Point of TOP-REAL 2;
A6: x = |[x'`1,x'`2]| by EUCLID:57;
    per cases by A1,A6,Th22;
    suppose
A7:   x'`2 = 0;
      then B.x =
      {Ball(|[x'`1,r]|,r) \/ {|[x'`1,0]|} where r is Element of REAL: r > 0}
      by A2,A6;
      then consider r being Real such that
A8:   V' = Ball(|[x'`1,r]|,r)\/{|[x'`1,0]|} & r > 0 by A5;
      consider f being continuous Function of Niemytzki-plane, I[01] such that
A9:   f.(|[x'`1,0]|) = 0 & for a,b being real number holds
      (|[a,b]| in V'` implies f.(|[a,b]|) = 1) &
      (|[a,b]| in V'\{|[x'`1,0]|} implies
      f.(|[a,b]|) = |.|[x'`1,0]|-|[a,b]|.|^2/(2*r*b)) by A8,Th80;
      take f;
      thus f.x = 0 by A7,A9,EUCLID:57;
      thus f.:V` c= {1}
      proof
        let u;
        assume u in f.:V`;
        then consider b being Point of X such that
A10:    b in V` & u = f.b by FUNCT_2:116;
        b in y>=0-plane by A1;
        then reconsider c = b as Element of TOP-REAL 2;
        V` c= V'` by A5,SUBSET_1:31;
        then b in V'` & b = |[c`1,c`2]| by A10,EUCLID:57;
        then u = 1 by A9,A10;
        hence thesis by TARSKI:def 1;
      end;
    end;
    suppose
A11:  x'`2 > 0;
      then B.x =
      {Ball(|[x'`1,x'`2]|,r) /\ y>=0-plane where r is Element of REAL: r > 0}
      by A3,A6;
      then consider r being Real such that
A12:  V' = Ball(|[x'`1,x'`2]|,r)/\y>=0-plane & r > 0 by A5;
      consider f being continuous Function of Niemytzki-plane, I[01] such that
A13:  f.(|[x'`1,x'`2]|) = 0 & for a,b being real number holds
      (|[a,b]| in V'` implies f.(|[a,b]|) = 1) &
      (|[a,b]| in V' implies f.(|[a,b]|) = |.|[x'`1,x'`2]|-|[a,b]|.|/r)
      by A11,A12,Th85;
      take f;
      thus f.x = 0 by A13,EUCLID:57;
      thus f.:V` c= {1}
      proof
        let u;
        assume u in f.:V`;
        then consider b being Point of X such that
A14:    b in V` & u = f.b by FUNCT_2:116;
        b in y>=0-plane by A1;
        then reconsider c = b as Element of TOP-REAL 2;
        V` c= V'` by A5,SUBSET_1:31;
        then b in V'` & b = |[c`1,c`2]| by A14,EUCLID:57;
        then u = 1 by A13,A14;
        hence thesis by TARSKI:def 1;
      end;
    end;
  end;
  hence thesis by Th56,Th86;
end;

