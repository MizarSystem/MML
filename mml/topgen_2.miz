:: On the characteristic and weight of a topological space
::  by Grzegorz Bancerek
:: 
:: Received December 10, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, RLVECT_3, PBOOLE, RELAT_1, FUNCT_1, PROB_1, CARD_1,
      ARYTM, BOOLE, FRECHET, SETFAM_1, TARSKI, WAYBEL23, CANTOR_1, ORDINAL1,
      CARD_4, CARD_2, FINSET_1, NATTRA_1, PUA2MSS1, SUBSET_1, TOPS_1, YELLOW_9,
      FINSUB_1, FUNCT_5, FINSEQ_1, EQREL_1, PCOMPS_1, TOPGEN_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, DOMAIN_1, SETFAM_1, RELAT_1, FUNCT_1,
      RELSET_1, FUNCT_2, FINSEQ_1, CARD_3, EQREL_1, PUA2MSS1, ORDINAL1,
      FINSET_1, FINSUB_1, CARD_1, CARD_2, CARD_4, FUNCT_5, STRUCT_0, GROUP_1,
      PRE_TOPC, TOPS_1, TOPS_2, TDLAT_3, PCOMPS_1, CANTOR_1, FRECHET, YELLOW_8,
      YELLOW_9, YELLOW18, WAYBEL23;
 constructors CARD_3, CARD_1, PRE_TOPC, TOPS_1, TOPS_2, CANTOR_1, FRECHET,
      GROUP_1, SUBSET_1, RELAT_1, RELSET_1, FUNCT_1, FUNCT_2, WELLORD2,
      PARTFUN1, DOMAIN_1, YELLOW_8, PBOOLE, ORDINAL1, ORDINAL2, NAT_1,
      WAYBEL23, CONNSP_2, CARD_2, CARD_4, PUA2MSS1, REALSET2, TDLAT_3,
      PCOMPS_1, YELLOW_9, FINSUB_1, MEMBERED;
 registrations SUBSET_1, RELSET_1, STRUCT_0, TOPS_1, CARD_1, YELLOW12,
      YELLOW16, SCMRING1, FINSET_1, TDLAT_3, RELAT_1, CARD_FIL, SETFAM_1,
      EQREL_1;
 requirements BOOLE, SUBSET, NUMERALS;
 definitions TARSKI, XBOOLE_0, FUNCT_1, CARD_4, STRUCT_0, GROUP_1, PRE_TOPC,
      FRECHET, YELLOW_8, YELLOW_9;
 theorems XBOOLE_1, ZFMISC_1, SETFAM_1, CANTOR_1, CARD_5, YELLOW_8, YELLOW_9,
      PBOOLE, TOPS_1, CARD_1, CARD_4, CARD_LAR, XBOOLE_0, TSEP_1, PRE_TOPC,
      WAYBEL31, TARSKI, TOPS_2, FUNCT_1, CARD_3, FINSET_1, STRUCT_0, TDLAT_3,
      GROUP_1, PUA2MSS1, CARD_2, EQREL_1, FUNCT_2, WELLORD2, WAYBEL23,
      SUBSET_1, BORSUK_5, FINSEQ_1, FUNCT_5, FIN_TOPO, FINSUB_1, WAYBEL19,
      PCOMPS_1, MSSUBFAM, ENUMSET1;
 schemes ORDINAL1, WELLORD2, PBOOLE, FUNCT_1, FUNCT_2;

begin :: The characteristic of a topological space
  
 reserve a,b,c for set;

::<DESC name="baza w punkcie a baza" monograph="topology"/>
theorem Th1:
  for T being non empty TopSpace, B being Basis of T
  for x being Element of T
  holds {U where U is Subset of T: x in U & U in B} is Basis of x
  proof
    let T be non empty TopSpace;
    let B be Basis of T;
    let x be Element of T;
    set Bx = {U where U is Subset of T: x in U & U in B};
A1: Bx c= B
    proof let a; assume a in Bx; then
      ex U being Subset of T st a = U & x in U & U in B;
      hence thesis;
    end; then
    Bx c= bool the carrier of T by XBOOLE_1:1; then
    reconsider Bx as Subset-Family of T by SETFAM_1:def 7;
    Bx is Basis of x
    proof
      B c= the topology of T by CANTOR_1:def 2;
      hence Bx c= the topology of T by A1,XBOOLE_1:1;
      now
        let a; assume a in Bx; then
        ex U being Subset of T st a = U & x in U & U in B;
        hence x in a;
      end;
      hence x in Intersect Bx by SETFAM_1:58;
      let S be Subset of T such that
A3:   S is open & x in S;
      consider V being Subset of T such that
A4:   V in B & x in V & V c= S by A3,YELLOW_9:31;
      V in Bx by A4;
      hence thesis by A4;
    end;
    hence thesis;
  end;
  
::<DESC name="baza w punkcie a baza" monograph="topology"/>
theorem Th2:
  for T being non empty TopSpace
  for B being ManySortedSet of T
    st for x being Element of T holds B.x is Basis of x
   holds Union B is Basis of T
   proof
     let T be non empty TopSpace;
     let B be ManySortedSet of T;
     assume
A1:  for x being Element of T holds B.x is Basis of x;
A2:  dom B = the carrier of T by PBOOLE:def 3;
     Union B c= bool the carrier of T
     proof let a; assume a in Union B; then
       consider b such that
A3:    b in dom B & a in B.b by CARD_5:10;
       reconsider b as Point of T by PBOOLE:def 3,A3;
       B.b is Basis of b by A1;
       hence a in bool the carrier of T by A3;
     end; then
     reconsider W = Union B as Subset-Family of T by SETFAM_1:def 7;
A4:  W c= the topology of T
     proof let a; assume a in W; then
       consider b such that
A3:    b in dom B & a in B.b by CARD_5:10;
       reconsider b as Point of T by PBOOLE:def 3,A3;
       B.b is Basis of b by A1; then
       B.b c= the topology of T by YELLOW_8:def 2;
       hence a in the topology of T by A3;
     end;
     now
       let A be Subset of T such that
A5:    A is open;
       let p be Point of T such that
A6:    p in A;
A8:    B.p is Basis of p by A1; then
       consider a being Subset of T such that
A7:    a in B.p & a c= A by A5,A6,YELLOW_8:def 2;
       take a;
       thus a in W by A2,A7,CARD_5:10;
       thus p in a by A8,A7,YELLOW_8:21;
       thus a c= A by A7;
     end;
     hence thesis by A4,YELLOW_9:32;
   end;
  
definition
  let T be non empty TopStruct;
  let x be Element of T;
::<DESC name="charakter punktu" monograph="topology"/>
  func Chi(x,T) -> cardinal number means:
Def1: 
  (ex B being Basis of x st it = Card B) &
  for B being Basis of x holds it <=` Card B;
  existence
  proof
    defpred P[ordinal number] means
    ex B being Basis of x st $1 = Card B;
    consider B0 being Basis of x;
    Card B0 is ordinal; then
A2: ex A being ordinal number st P[A];
    consider A being ordinal number such that
A3: P[A] and
A4: for B being ordinal number st P[B] holds A c= B from ORDINAL1:sch 1(A2);
    consider B1 being Basis of x such that
A5: A = Card B1 by A3;
    take Card B1;
    thus thesis by A4,A5;
  end;
  uniqueness
  proof let M1, M2 be cardinal number; assume
    (ex B being Basis of x st M1 = Card B) &
    (for B being Basis of x holds M1 <=` Card B) &
    (ex B being Basis of x st M2 = Card B) &
    for B being Basis of x holds M2 <=` Card B;
    hence  M1 c= M2 & M2 c= M1;
  end;
end;

theorem Lem1:
  for X being set
  st for a being set st a in X holds a is cardinal number
  holds union X is cardinal number
  proof
    let X be set such that
A1: for a being set st a in X holds a is cardinal number;
    now let a; assume
A2:   a in X; then
      a is cardinal number by A1;
      hence ex b being set st b in X & a c= b & b is cardinal set by A2;
    end;
    hence union X is cardinal number by CARD_LAR:33;
  end;
  
Lem2: now let T be non empty TopStruct;
        set X = {Chi(x,T) where x is Point of T: not contradiction};
        now let a; assume a in X; then
          ex x being Point of T st a = Chi(x,T);
          hence a is cardinal number;
        end; 
        hence union X is cardinal number by Lem1;
        let m be cardinal number such that
A0:     m = union X;
        hereby let x be Point of T;
          Chi(x,T) in X;
          hence Chi(x,T) <=` m by A0,ZFMISC_1:92;
        end;
        let M be cardinal number such that
A1:     for x being Point of T holds Chi(x,T) <=` M;
        now let a; assume a in X; then
          ex x being Point of T st a = Chi(x,T);
          hence a c= M by A1;
        end;
        hence m <=` M by A0,ZFMISC_1:94;
      end;

definition
  let T be non empty TopStruct;
::<DESC name="charakter przestrzeni" monograph="topology"/>
  func Chi(T) -> cardinal number means:
Def2: 
   (for x being Point of T holds Chi(x,T) <=` it) &
   for M being cardinal number
    st for x being Point of T holds Chi(x,T) <=` M
    holds it <=` M;
   existence
   proof
     set X = {Chi(x,T) where x is Point of T: not contradiction};
     reconsider m = union X as cardinal number by Lem2;
     take m; thus thesis by Lem2;
   end;
  uniqueness
  proof let M1, M2 be cardinal number; assume
    (for x being Point of T holds Chi(x,T) <=` M1) &
    (for M being cardinal number
    st for x being Point of T holds Chi(x,T) <=` M
    holds M1 <=` M) &
    (for x being Point of T holds Chi(x,T) <=` M2) &
    (for M being cardinal number
    st for x being Point of T holds Chi(x,T) <=` M
    holds M2 <=` M);
    hence  M1 c= M2 & M2 c= M1;
  end;
end;

theorem Lem3:
  for T being non empty TopStruct holds
    Chi(T) = union {Chi(x,T) where x is Point of T: not contradiction}
  proof
    let T be non empty TopStruct;
    set X = {Chi(x,T) where x is Point of T: not contradiction};
    reconsider m = union X as cardinal number by Lem2;
    (for x being Point of T holds Chi(x,T) <=` m) &
    for M being cardinal number
     st for x being Point of T holds Chi(x,T) <=` M
     holds m <=` M by Lem2;
    hence Chi(T) = union X by Def2;
  end;
  
theorem Lem4:
  for T being non empty TopStruct
  for x being Point of T holds Chi(x,T) <=` Chi(T)
  proof
    let T be non empty TopStruct;
    set X = {Chi(x,T) where x is Point of T: not contradiction};
    let x be Point of T;
    Chi(x,T) in X & Chi(T) = union X by Lem3;
    hence Chi(x,T) <=` Chi(T) by ZFMISC_1:92;
  end;

::<DESC name="pierwszy aksjomat przeliczalno/sci" monograph="topology"/>
theorem
  for T being non empty TopSpace holds
    T is first-countable iff Chi(T) <=` alef 0
  proof
    let T be non empty TopSpace;
    set X = {Chi(x,T) where x is Point of T: not contradiction};
A0: Chi(T) = union X by Lem3;
    thus T is first-countable implies Chi(T) <=` alef 0
    proof assume
A1:   for x be Point of T ex B be Basis of x st B is countable;
      now
        let a; assume a in X; then
        consider x being Point of T such that
A2:     a = Chi(x,T);
        consider B being Basis of x such that
A3:     B is countable by A1;
        Chi(x,T) <=` Card B & Card B <=` alef 0 by A3,Def1,CARD_4:def 1;
        hence a c= alef 0 by A2,XBOOLE_1:1;
      end;
      hence Chi(T) <=` alef 0 by A0,ZFMISC_1:94;
    end;
    assume
A4: Chi(T) <=` alef 0;
    let x be Point of T;
    consider B being Basis of x such that
A5: Chi(x,T) = Card B by Def1;
    take B;
    Chi(x,T) <=` Chi(T) by Lem4;
    hence Card B <=` alef 0 by A4,A5,XBOOLE_1:1;
  end;

begin :: Neighborhood system

definition
  let T be non empty TopSpace;
::<DESC name="uk/lad otocze/n" monograph="topology"/>
  mode Neighborhood_System of T -> ManySortedSet of T means:
Def3: 
  for x being Element of T holds it.x is Basis of x;
  existence
  proof consider B being Basis of T;
    deffunc F(Point of T) = {U where U is Subset of T: $1 in U & U in B};
    consider F being ManySortedSet of T such that
A1: for x being Point of T holds F.x = F(x) from PBOOLE:sch 5;
    take F; let x be Point of T;    
    F.x = F(x) by A1;
    hence thesis by Th1;
  end;
end;

definition
  let T be non empty TopSpace;
  let N be Neighborhood_System of T;
  redefine func Union N -> Basis of T;
  coherence
  proof
    for x being Point of T holds N.x is Basis of x by Def3;
    hence thesis by Th2;
  end;
  let x be Point of T;
  func N.x -> Basis of x;
  coherence by Def3;
end;

::<DESC name="uk/lad otocze/n: (BP1)" monograph="topology"/>
theorem
  for T being non empty TopSpace
  for N being Neighborhood_System of T
  for x being Element of T holds N.x is non empty &
  for U being set st U in N.x holds x in U
  by YELLOW_8:21; :: and clusters YELLOW12
  
::<DESC name="uk/lad otocze/n: (BP2)" monograph="topology"/>
theorem
  for T being non empty TopSpace
  for x,y being Point of T
  for B1 being Basis of x, B2 being Basis of y
  for U being set st x in U & U in B2
  ex V being open Subset of T st V in B1 & V c= U
  proof
    let T be non empty TopSpace;
    let x,y be Point of T;
    let B1 be Basis of x;
    let B2 be Basis of y;
    let U be set; assume
A1: x in U & U in B2; then
    U is open Subset of T by YELLOW_8:21; then
    consider V being Subset of T such that
A2: V in B1 & V c= U by A1,YELLOW_8:22;
    V is open by A2,YELLOW_8:21;
    hence thesis by A2;
  end;

::<DESC name="uk/lad otocze/n: (BP3)" monograph="topology"/>
theorem
  for T being non empty TopSpace
  for x being Point of T
  for B being Basis of x
  for U1,U2 being set st U1 in B & U2 in B
  ex V being open Subset of T st V in B & V c= U1 /\ U2
  proof
    let T be non empty TopSpace;
    let x be Point of T;
    let B be Basis of x;
    let U1,U2 be set; assume
A1: U1 in B & U2 in B; then
    reconsider U1, U2 as open Subset of T by YELLOW_8:21;
    x in U1 & x in U2 by A1,YELLOW_8:21; then
    x in U1/\U2 & U1/\U2 is open by XBOOLE_0:def 3,TOPS_1:38; then
    consider V being Subset of T such that
A2: V in B & V c= U1/\U2 by YELLOW_8:22;
    V is open by A2,YELLOW_8:21;
    hence thesis by A2;
  end;

Lem11: now
         let T be non empty TopSpace;
         let A be Subset of T;
         let x be Element of T such that
A1:      x in Cl A;
         let B be Basis of x, U be set; assume U in B; then
A2:      x in U & U is open Subset of T by YELLOW_8:21; then
         U meets Cl A by A1,XBOOLE_0:3;
         hence U meets A by A2,TSEP_1:40;
       end;
       
Lem12: now
         let T be non empty TopSpace;
         let A be Subset of T;
         let x be Element of T; given B being Basis of x such that
A1:      for U being set st U in B holds U meets A;
         now let U be Subset of T; assume
           U is open & x in U; then
           consider V being Subset of T such that
A2:        V in B & V c= U by YELLOW_8:def 2;
           V meets A by A1,A2;
           hence A meets U by A2,XBOOLE_1:63;
         end;
         hence x in Cl A by PRE_TOPC:def 13;
       end;
       
::<DESC name="1.1.1. Twierdzenie: Cl a baza w punkcie" monograph="topology"/>
theorem
  for T being non empty TopSpace
  for A being Subset of T
  for x being Element of T holds
    x in Cl A iff
    for B being Basis of x for U being set st U in B holds U meets A
  proof
    let T be non empty TopSpace;
    let A be Subset of T;
    let x be Element of T;
    thus x in Cl A implies
    for B being Basis of x for U being set st U in B holds U meets A by Lem11;
    consider B being Basis of x;
    assume
    for B being Basis of x for U being set st U in B holds U meets A; then
    for U being set st U in B holds U meets A;
    hence x in Cl A by Lem12;
   end;
    
::<DESC name="1.1.1. Twierdzenie: Cl a baza w punkcie" monograph="topology"/>
theorem
  for T being non empty TopSpace
  for A being Subset of T
  for x being Element of T holds
    x in Cl A iff
    ex B being Basis of x st for U being set st U in B holds U meets A
    proof
      let T be non empty TopSpace;
      let A be Subset of T;
      let x be Element of T;
      consider B being Basis of x;
      x in Cl A implies
      for U being set st U in B holds U meets A by Lem11;
      hence x in Cl A implies
      ex B being Basis of x st for U being set st U in B holds U meets A;
      thus thesis by Lem12;
    end;
    
registration
  let T be TopSpace;
  cluster open non empty Subset-Family of T;
  existence
  proof
    take B = the topology of T;
    thus thesis by CANTOR_1:3;
  end;
end;

begin :: The weight of a topological space
    
::<DESC name="1.1.14. Twierdzenie: istnienie podrodziny mocy niewi/ekszej od wagi przestrzeni i o unii r/ownej" monograph="topology"/>
theorem Th1_14:
  for T being non empty TopSpace
  for S being open Subset-Family of T
  ex G being open Subset-Family of T
  st G c= S & union G = union S & Card G <=` weight T
  proof
    let T be non empty TopSpace;
    let S be open Subset-Family of T;
    consider B being Basis of T such that
A1: Card B = weight T by WAYBEL31:2;
    set B1 = {W where W is Subset of T:
              W in B & ex U being set st U in S & W c= U};
A2: B1 c= B
    proof
      let a; assume a in B1; then
      ex W being Subset of T st a = W & W in B &
      ex U being set st U in S & W c= U;
      hence thesis;
    end;
    defpred P[set,set] means $1 c= $2;
A3: now
      let a; assume a in B1; then
      ex W being Subset of T st a = W & W in B &
      ex U being set st U in S & W c= U;
      hence ex b st b in S & P[a,b];
    end;
    consider f being Function such that
A4: dom f = B1 & rng f c= S and
A5: for a st a in B1 holds P[a,f.a] from WELLORD2:sch 1(A3);
    set G = rng f;
    G c= bool the carrier of T by A4,XBOOLE_1:1; then
    reconsider G as open Subset-Family of T by A4,SETFAM_1:def 7,TOPS_2:18;
    take G;
    thus G c= S & union G c= union S by A4,ZFMISC_1:95;
    thus union S c= union G
    proof
      let a; assume a in union S; then
      consider b such that
A6:   a in b & b in S by TARSKI:def 4;
      reconsider a as Point of T by A6;
      reconsider b as open Subset of T by A6,TOPS_2:def 1;
      consider W0 being Subset of T such that
A7:   W0 in B & a in W0 & W0 c= b by A6,YELLOW_9:31;
      W0 in B1 by A6,A7; then
      W0 c= f.W0 & f.W0 in G by A4,A5,FUNCT_1:def 5; then
      a in f.W0 & f.W0 c= union G by A7,ZFMISC_1:92;
      hence thesis;
    end;
    Card G <=` Card B1 & Card B1 <=` Card B by A2,A4,CARD_1:27,28;
    hence Card G <=` weight T by A1,XBOOLE_1:1;
  end;
  
definition
  let T be TopStruct;
  attr T is finite-weight means:
Def4: 
  weight T is finite;
end;

notation
  let T be TopStruct;
  antonym T is infinite-weight for T is finite-weight;
end;
  
registration
  cluster finite -> finite-weight TopStruct;
  coherence
  proof let T be TopStruct;
    consider B being Basis of T such that
A1: Card B = weight T by WAYBEL31:2;
    assume the carrier of T is finite; then
    bool the carrier of T is finite by FINSET_1:24; then
    B is finite by FINSET_1:13;
    hence weight T is finite by A1,CARD_4:1;
  end;
  cluster infinite-weight -> infinite TopStruct;
  coherence
  proof let T be TopStruct;
    consider B being Basis of T such that
A1: Card B = weight T by WAYBEL31:2;
    assume weight T is infinite; then
    B is infinite by A1,CARD_4:1; then
    bool the carrier of T is infinite by FINSET_1:13;
    hence the carrier of T is infinite by FINSET_1:24;
  end;
end;
  
registration
  cluster finite non empty TopSpace;
  existence
  proof
    consider A being finite non empty set;
    reconsider O = bool A as Subset-Family of A;
    reconsider T = TopStruct(#A, O#) as discrete non empty TopStruct
    by STRUCT_0:def 1,TDLAT_3:def 1;
    take T; thus thesis by GROUP_1:def 14;
  end;
end;

theorem Th18:
  for X being set holds Card SmallestPartition X = Card X
  proof
    let X be set; set A = SmallestPartition X;
    per cases;
    suppose X = {};
      hence thesis by EQREL_1:def 6;
    end;
    suppose X <> {}; then
      reconsider X as non empty set; consider x0 being Element of X;
A0:   A = {{x} where x is Element of X :not contradiction} by PUA2MSS1:13;
      deffunc F(set) = {$1};
A1:   for a st a in X holds F(a) in A by A0;
      consider f being Function of X,A such that
A2:   for a st a in X holds f.a = F(a) from FUNCT_2:sch 2(A1);
A6:   {x0} in A by A0; then
A3:   dom f = X & rng f c= A by FUNCT_2:def 1;
A4:   rng f = A
      proof thus rng f c= A;
        let a; assume a in A; then
        consider b being Element of X such that
A5:     a = {b} by A0;
        f.b = a by A2,A5;
        hence thesis by A6,FUNCT_2:6;
      end;
      f is one-to-one
      proof
        let a,b; assume
A7:     a in dom f & b in dom f;
        assume f.a = f.b; then
        {a} = f.b by A2,A7 .= {b} by A2,A7;
        hence thesis by ZFMISC_1:6;
      end; then
      X,A are_equipotent by A3,A4,WELLORD2:def 4;
      hence thesis by CARD_1:21;
    end;
  end;

theorem Th19:
  for T being discrete non empty TopStruct
  holds SmallestPartition the carrier of T is Basis of T &
  for B being Basis of T holds SmallestPartition the carrier of T c= B
  proof
    let T be discrete non empty TopStruct;
    set B0 = SmallestPartition the carrier of T;
A0: B0 = {{a} where a is Element of T: not contradiction} by PUA2MSS1:13;
A1: B0 c= the topology of T
    proof let a; assume
      a in B0; then
      reconsider a as Subset of T;
      a is open by TDLAT_3:17;
      hence thesis by PRE_TOPC:def 5;
    end;
    now
      let A be Subset of T such that
      A is open;
      let p be Point of T such that
A3:   p in A;
      reconsider a = {p} as Subset of T;
      take a;
      thus a in B0 by A0;
      thus p in a by TARSKI:def 1;
      thus a c= A by A3,ZFMISC_1:37;
    end;
    hence
A6: B0 is Basis of T by A1,YELLOW_9:32;
    let B be Basis of T;
    let a; assume
A5: a in B0; then
    consider b being Element of T such that
A4: a = {b} by A0;
    reconsider a as Subset of T by A5;
    a is open & b in a by A4,A5,A6,TARSKI:def 1,YELLOW_8:19; then
    consider U being Subset of T such that
A7: U in B & b in U & U c= a by YELLOW_9:31;
    a c= U by A4,A7,ZFMISC_1:37;
    hence thesis by A7,XBOOLE_0:def 10;
  end;

theorem Th20:
  for T being discrete non empty TopStruct
  holds weight T = Card the carrier of T
  proof
   let T be discrete non empty TopStruct;
    consider B being Basis of T such that
A1: Card B = weight T by WAYBEL31:2;
    reconsider B0 = SmallestPartition the carrier of T as Basis of T by Th19;
A2: Card B0 = Card the carrier of T by Th18;
    set M = {Card C where C is Basis of T: not contradiction};
A3: weight T = meet M by WAYBEL23:def 5;
    Card B0 in M;
    hence weight T <=` Card the carrier of T by A2,A3,SETFAM_1:4;
    B0 c= B by Th19;
    hence Card the carrier of T <=` weight T by A1,A2,CARD_1:27;
  end;

registration
  cluster infinite-weight TopSpace;
  existence
  proof
    consider A being infinite set;
    reconsider O = bool A as Subset-Family of A;
    reconsider T = TopStruct(#A, O#) as discrete non empty TopStruct
    by STRUCT_0:def 1,TDLAT_3:def 1;
    take T;
    the carrier of T is infinite & weight T = Card the carrier of T by Th20;
    hence weight T is infinite by CARD_4:1;
  end;
end;
  
canceled;
LemA2:
  for T being infinite-weight TopSpace
  for B being Basis of T
  ex B1 being Basis of T st B1 c= B & Card B1 = weight T
  proof
    let T be infinite-weight TopSpace;
    let B be Basis of T;
    consider B0 being Basis of T such that
A1: Card B0 = weight T by WAYBEL31:2;
A2: weight T is infinite by Def4;
    defpred P[set,set] means
    union $2 = $1 & Card $2 <=` weight T;
A3: now
      let a; assume a in B0; then
      reconsider W = a as open Subset of T by YELLOW_8:19;
      set Sa = {U where U is Subset of T: U in B & U c= a};
A10:  Sa c= B
      proof let b; assume b in Sa; then
        ex U being Subset of T st b = U & U in B & U c= a;
        hence thesis;
      end;
A9:   union Sa = W by YELLOW_8:18;
      B is open & Sa c= bool the carrier of T
      by A10,XBOOLE_1:1,YELLOW_9:27; then
      reconsider Sa as open Subset-Family of T by A10,SETFAM_1:def 7,TOPS_2:18;
      consider G being open Subset-Family of T such that
A8:   G c= Sa & union G = union Sa & Card G <=` weight T by Th1_14;
      reconsider b = G as set;
      take b;
      b c= B by A10,A8,XBOOLE_1:1;
      hence b in bool B;
      thus P[a,b] by A8,A9;
    end;
    consider f being Function such that
A4: dom f = B0 & rng f c= bool B and
A5: for a st a in B0 holds P[a,f.a] from WELLORD2:sch 1(A3);
A6: Union f = union rng f by CARD_3:def 4; then
    Union f c= union bool B by A4,ZFMISC_1:95; then
A11:Union f c= B by ZFMISC_1:99; then
    Union f c= bool the carrier of T by XBOOLE_1:1; then
    reconsider B1 = Union f as Subset-Family of T by SETFAM_1:def 7; 
    now
      B c= the topology of T by CANTOR_1:def 2;
      hence B1 c= the topology of T by A11,XBOOLE_1:1;
      let A be Subset of T such that
A12:  A is open;
      let p be Point of T; assume p in A; then
      consider U being Subset of T such that
A14:  U in B0 & p in U & U c= A by A12,YELLOW_9:31;
A13:  P[U,f.U] by A5,A14; then
      consider a such that
A15:  p in a & a in f.U by A14,TARSKI:def 4;
A16:  a in B1 by A4,A14,A15,CARD_5:10;
      a c= U by A13,A15,ZFMISC_1:92; then
      a is Subset of T & a c= A by A14,XBOOLE_1:1;
      hence ex a being Subset of T st a in B1 & p in a & a c= A by A15,A16;
    end; then
    reconsider B1 as Basis of T by YELLOW_9:32;
    take B1;
    thus B1 c= B by A11;
    now
      thus Card rng f <=` weight T by A1,A4,CARD_1:28;
      let a; assume a in rng f; then
      ex b st b in dom f & a = f.b by FUNCT_1:def 5;
      hence Card a <=` weight T by A4,A5;
    end; then
    Card B1 <=` (weight T)*`(weight T) by A6,CARD_4:60;
    hence Card B1 <=` weight T by A2,CARD_4:77;
    thus thesis by WAYBEL31:1;
  end;
  
theorem Th21:
  for T being finite-weight non empty TopSpace
  ex B0 being Basis of T st
  ex  f being Function of the carrier of T, the topology of T
  st
     B0 = rng f &
     for x being Point of T holds x in f.x &
     for U being open Subset of T st x in U holds f.x c= U
  proof
    let T be finite-weight non empty TopSpace;
    consider B being Basis of T such that
A1: Card B = weight T by WAYBEL31:2;
    weight T is finite by Def4; then
A5: B is finite by A1,CARD_4:1;
    deffunc F(set) = meet {U where U is Subset of T: $1 in U & U in B};
A2: now
      let a; assume a in the carrier of T; then
      reconsider x = a as Point of T;
      set S = {U where U is Subset of T: x in U & U in B};
      B c= the topology of T by CANTOR_1:def 2; then
      FinMeetCl B c= FinMeetCl the topology of T by CANTOR_1:16; then
A4:   FinMeetCl B c= the topology of T by CANTOR_1:5;
      [#]T = the carrier of T & [#]T is open by PRE_TOPC:12; then
      consider U being Subset of T such that
A7:   U in B & x in U and U c= [#]T by YELLOW_9:31;
A6:   S c= B
      proof
        let a; assume a in S; then
        ex U being Subset of T st a = U & x in U & U in B;
        hence thesis;
      end; then
      S c= bool the carrier of T by XBOOLE_1:1; then
      reconsider S as Subset-Family of T by SETFAM_1:def 7;
      S is finite by A5,A6,FINSET_1:13; then
      Intersect S in FinMeetCl B & U in S by A6,A7,CANTOR_1:def 4; then
      Intersect S in the topology of T & S <> {} by A4;
      hence F(a) in the topology of T by SETFAM_1:def 10;
    end;
    consider f being Function of the carrier of T, the topology of T such that
A3: for a st a in the carrier of T holds f.a = F(a) from FUNCT_2:sch 2(A2);
    set B0 = rng f;
    B0 c= bool the carrier of T by XBOOLE_1:1; then
    reconsider B0 as Subset-Family of T by SETFAM_1:def 7;
A2: now
      let a; assume a in the carrier of T; then
      reconsider x = a as Point of T;
      set S = {U where U is Subset of T: x in U & U in B};
      [#]T = the carrier of T & [#]T is open by PRE_TOPC:12; then
      consider U being Subset of T such that
A7:   U in B & x in U and U c= [#]T by YELLOW_9:31;
A8:   U in S & f.a = meet S by A3,A7;
      now
        let b; assume b in S; then
        ex U being Subset of T st b = U & a in U & U in B;
        hence a in b;
      end;
      hence a in f.a by A8,SETFAM_1:def 1;
    end;
A9: now
      let x be Point of T, V be Subset of T;
      set S = {U where U is Subset of T: x in U & U in B};
      assume x in V & V is open; then
      consider U being Subset of T such that
A7:   U in B & x in U & U c= V by YELLOW_9:31;
      U in S & f.x = meet S by A3,A7; then
      f.x c= U by SETFAM_1:4;
      hence f.x c= V by A7,XBOOLE_1:1;
    end;
    now
      let A be Subset of T;
      assume A5: A is open;
      let p be Point of T;
      assume A6: p in A;
      f.p in the topology of T; then
      reconsider a = f.p as Subset of T;
      take a;
      thus a in B0 by FUNCT_2:6;
      thus p in a by A2;
      thus a c= A by A5,A6,A9;
    end; then
    reconsider B0 as Basis of T by YELLOW_9:32;
    take B0,f; thus B0 = rng f;
    let x be Point of T;
    thus thesis by A2,A9;
  end;
     
theorem Th22:
  for T being TopSpace
  for B0,B being Basis of T
  for f being Function of the carrier of T, the topology of T
  st
     B0 = rng f &
     for x being Point of T holds x in f.x &
     for U being open Subset of T st x in U holds f.x c= U
  holds B0 c= B
  proof
    let T be TopSpace;
    let B0,B be Basis of T;
    let f be Function of the carrier of T, the topology of T;
    assume
A0: B0 = rng f;
    assume
A1: for x being Point of T holds x in f.x &
    for U being open Subset of T st x in U holds f.x c= U;
    let a; assume
A4: a in B0; then
    consider b such that
A2: b in dom f & a = f.b by A0,FUNCT_1:def 5;
    reconsider b as Element of T by A2;
    reconsider V = a as Subset of T by A4;
    V is open & b in V by A1,A2,A4,YELLOW_8:19; then
    consider U being Subset of T such that
A5: U in B & b in U & U c= V by YELLOW_9:31;
    U is open by A5,YELLOW_8:19; then
    a c= U by A1,A2,A5;
    hence thesis by A5,XBOOLE_0:def 10;
  end;
  
theorem Th23:
  for T being TopSpace
  for B0 being Basis of T
  for f being Function of the carrier of T, the topology of T
  st
     B0 = rng f &
     for x being Point of T holds x in f.x &
     for U being open Subset of T st x in U holds f.x c= U
  holds weight T = Card B0
  proof
    let T be TopSpace;
    let B0 be Basis of T;
    let f be Function of the carrier of T, the topology of T such that
A1: B0 = rng f &
    for x being Point of T holds x in f.x &
    for U being open Subset of T st x in U holds f.x c= U;
    consider B being Basis of T such that
A2: Card B = weight T by WAYBEL31:2;
    set M = {Card C where C is Basis of T: not contradiction};
A3: weight T = meet M by WAYBEL23:def 5;
    Card B0 in M;
    hence weight T <=` Card B0 by A3,SETFAM_1:4;
    B0 c= B by A1,Th22;
    hence thesis by A2,CARD_1:27;
  end;

LemA1:
  for T being finite-weight non empty TopSpace
  for B being Basis of T
  ex B1 being Basis of T st B1 c= B & Card B1 = weight T
  proof
    let T be finite-weight non empty TopSpace;
    let B be Basis of T;
    consider B0 being Basis of T,
       f being Function of the carrier of T, the topology of T such that
A1: B0 = rng f &
    for x being Point of T holds x in f.x &
    for U being open Subset of T st x in U holds f.x c= U by Th21;
    take B0;
    thus B0 c= B by A1,Th22;
    thus Card B0 <=` weight T by A1,Th23;
    thus thesis by WAYBEL31:1;
  end;

::<DESC name="1.1.15. Twierdzenie: o istnieniu bazy bed/acej podrodzin/a bazy i maj/acej moc r/own/a wadze przestrzeni" monograph="topology"/>
theorem
  for T being non empty TopSpace
  for B being Basis of T
  ex B1 being Basis of T st B1 c= B & Card B1 = weight T
  proof
    let T be non empty TopSpace,
        B be Basis of T;
    T is finite-weight or T is infinite-weight;
    hence thesis by LemA1,LemA2;
  end;

begin :: Example 1.1.8: Almost discrete topological space with infinity

definition
  let X, x0 be set; :: X is infinite, x0 in X
  func DiscrWithInfin(X,x0) -> strict TopStruct means:
Def7:
  the carrier of it = X &
  the topology of it = {U where U is Subset of X: not x0 in U} \/
                       {F` where F is Subset of X: F is finite};
  uniqueness;
  existence
  proof
    set O1 = {U where U is Subset of X: not x0 in U},
    O2 = {F` where F is Subset of X: F is finite},
    O = O1 \/ O2;
    O c= bool X
    proof
      let a; assume a in O; then
      a in O1 or a in O2 by XBOOLE_0:def 2; then
      (ex U being Subset of X st a = U & not x0 in U) or
      (ex F being Subset of X st a = F` & F is finite);
      hence thesis;
    end; then
    reconsider O as Subset-Family of X by SETFAM_1:def 7;
    take TopStruct(#X,O#);
    thus thesis;
  end;
end;

registration
  let X,x0 be set;
  cluster DiscrWithInfin(X,x0) -> TopSpace-like;
  coherence
  proof
    set T = DiscrWithInfin(X,x0);
    set O1 = {U where U is Subset of X: not x0 in U},
    O2 = {F` where F is Subset of X: F is finite},
    O = O1 \/ O2;
A1: the carrier of T = X & the topology of T = O by Def7;
    ({}X)` = [#] X by SUBSET_1:22 .= X by SUBSET_1:def 4; then
    X in O2;
    hence the carrier of T in the topology of T by A1,XBOOLE_0:def 2;
    hereby let a be Subset-Family of T such that
A2:  a c= the topology of T;
     per cases;
     suppose not a c= O1; then
      consider b such that
A3:   b in a & not b in O1 by TARSKI:def 3;
      b in O2 by A1,A2,A3,XBOOLE_0:def 2; then
      consider F being Subset of X such that
A4:   b = F` & F is finite;
      b c= union a by A3,ZFMISC_1:92; then
      (union a)` c= F by A1,A4,SUBSET_1:36; then
      (union a)` is finite & (union a)`` = union a by A4,FINSET_1:13; then
      union a in O2 by A1;
      hence union a in the topology of T by A1,XBOOLE_0:def 2;
     end;
     suppose
A5:   a c= O1;
      now assume x0 in union a; then
       consider b such that
A6:    x0 in b & b in a by TARSKI:def 4;
       b in O1 by A5,A6; then
       ex U being Subset of X st b = U & not x0 in U;
       hence contradiction by A6;
      end; then
      union a in O1 by A1;
      hence union a in the topology of T by A1,XBOOLE_0:def 2;
     end;
    end;
    let a,b be Subset of T;
    assume
A2: a in the topology of T & b in the topology of T;
    per cases by A1,A2,XBOOLE_0:def 2;
    suppose
A8:  a in O2 & b in O2; then
     consider F1 being Subset of X such that
A9:  a = F1` & F1 is finite;
     consider F2 being Subset of X such that
A10: b = F2` & F2 is finite by A8;
     a/\b = (F1\/F2)` & F1\/F2 is finite
     by A9,A10,SUBSET_1:29,FINSET_1:14; then
     a/\b in O2;
     hence a /\ b in the topology of T by A1,XBOOLE_0:def 2;
    end;
    suppose a in O1 or b in O1; then
     (ex U being Subset of X st a = U & not x0 in U) or
     (ex U being Subset of X st b = U & not x0 in U); then
     not x0 in a/\b by XBOOLE_0:def 3; then
     a/\b in O1 by A1;
     hence a /\ b in the topology of T by A1,XBOOLE_0:def 2;
    end;
  end;
end;

registration
  let X be non empty set, x0 be set;
  cluster DiscrWithInfin(X,x0) -> non empty;
  coherence
  proof
   thus the carrier of DiscrWithInfin(X,x0) is non empty by Def7;
  end;
end;

theorem Th31:
  for X,x0 being set, A being Subset of DiscrWithInfin(X,x0)
  holds A is open iff not x0 in A or A` is finite
  proof
   let X,x0 be set;
   set T = DiscrWithInfin(X,x0);
   set O1 = {U where U is Subset of X: not x0 in U};
   set O2 = {F` where F is Subset of X: F is finite};
   let A be Subset of T;
A: the carrier of T = X & the topology of T = O1 \/ O2 by Def7;
   thus A is open implies not x0 in A or A` is finite
   proof assume A in the topology of T; then
    A in O1 or A in O2 by A,XBOOLE_0:def 2; then
    (ex U being Subset of X st A = U & not x0 in U) or
    (ex F being Subset of X st A = F` & F is finite);
    hence x0 in A implies A` is finite by A;
   end;
   assume not x0 in A or A` is finite; then
   A in O1 or A`` in O2 by A;
   hence A in the topology of T by A,XBOOLE_0:def 2;
  end;

theorem Th32:
  for X,x0 being set, A being Subset of DiscrWithInfin(X,x0)
  holds A is closed iff (x0 in X implies x0 in A) or A is finite
  proof
   let X,x0 be set;
   set T = DiscrWithInfin(X,x0);
A0:the carrier of T = X by Def7;
   let A be Subset of DiscrWithInfin(X,x0);
   A is closed iff A` is open by TOPS_1:29; then
   A is closed iff not x0 in A` or A`` is finite by Th31;
   hence thesis by A0,SUBSET_1:50,54;
  end;

theorem
  for X,x0,x being set st x in X holds
  {x} is closed Subset of DiscrWithInfin(X,x0)
  proof
   let X,x0,x be set;
   set T = DiscrWithInfin(X,x0);
   the carrier of T = X by Def7;
   hence thesis by Th32,ZFMISC_1:37;
  end;

theorem Th36:
  for X,x0,x being set st x in X & x <> x0
  holds {x} is open Subset of DiscrWithInfin(X,x0)
  proof
   let X,x0,x be set;
   set T = DiscrWithInfin(X,x0);
A0:the carrier of T = X by Def7;
   assume x in X & x <> x0; then
   {x} c= the carrier of T & not x0 in {x} by A0,TARSKI:def 1,ZFMISC_1:37;
   hence thesis by Th31;
  end;

theorem
  for X,x0 being set st X is infinite
  for U being Subset of DiscrWithInfin(X,x0) st U = {x0} holds U is not open
  proof
   let X,x0 be set;
   set T = DiscrWithInfin(X,x0);
A0:the carrier of T = X by Def7;
   assume A8: X is infinite;
   let U be Subset of DiscrWithInfin(X,x0);
   assume A9: U = {x0}; then
   U` = X\{x0} & {x0} c= X by A0,SUBSET_1:def 5; then
   X = U` \/ {x0} by XBOOLE_1:45; then
   U` is infinite & x0 in U by A8,A9,TARSKI:def 1,FINSET_1:14;
   hence U is not open by Th31;
  end;
  
theorem Th34:
  for X,x0 being set
  for A being Subset of DiscrWithInfin(X,x0)
  st A is finite holds Cl A = A
  proof
   let X,x0 be set;
   let A be Subset of DiscrWithInfin(X,x0);
   assume A is finite; then
   A is closed by Th32;
   hence Cl A = A by PRE_TOPC:52;
  end;

theorem Th33:
  for T being non empty TopSpace
  for A being Subset of  T st A is not closed
  for a being Point of T st A \/ {a} is closed
  holds Cl A = A \/ {a}
  proof
    let T be non empty TopSpace;
    let A be Subset of T such that
A1: A is not closed;
    let a be Point of T such that
A4: A \/ {a} is closed;
    A c= A \/ {a} by XBOOLE_1:7; then
    A c= Cl A & Cl A c= Cl (A\/{a}) & Cl (A\/{a}) = A\/{a}
    by A4,PRE_TOPC:48,49,52;
    hence Cl A = A \/ {a} by A1,BORSUK_5:2;
  end;

theorem Th35:
  for X,x0 being set st x0 in X
  for A being Subset of DiscrWithInfin(X,x0)
  st A is infinite holds Cl A = A \/ {x0}
  proof
   let X,x0 be set such that
A1:x0 in X;
   set T = DiscrWithInfin(X,x0);
   the carrier of T = X by Def7; then
   reconsider T as non empty TopSpace by A1,STRUCT_0:def 1;
   reconsider x = x0 as Point of T by A1,Def7;
   let A be Subset of DiscrWithInfin(X,x0);
   reconsider B = {x} as Subset of T;
   reconsider A' = A as Subset of T;
   x0 in {x0} by TARSKI:def 1; then
   x0 in A' \/ B by XBOOLE_0:def 2; then
   A'\/B is closed by Th32; then
A2:Cl(A'\/B) = A'\/B by PRE_TOPC:52;
   assume A is infinite; then
   A' is not closed or x0 in A by A1,Th32;
   hence Cl A = A \/ {x0} by A2,Th33,ZFMISC_1:46;
  end;

theorem
  for X,x0 being set
  for A being Subset of DiscrWithInfin(X,x0)
  st A` is finite holds Int A = A
  proof
    let X,x0 be set;
    let A be Subset of DiscrWithInfin(X,x0);
    assume A` is finite; then
    Cl A` = A` by Th34;
    hence Int A = A`` by TOPS_1:def 1 .= A;
  end;

theorem
  for X,x0 being set st x0 in X
  for A being Subset of DiscrWithInfin(X,x0)
  st A` is infinite holds Int A = A \ {x0}
  proof
    let X,x0 be set such that
A1: x0 in X;
    set T = DiscrWithInfin(X,x0);
    the carrier of T = X by Def7; then
    reconsider T as non empty TopSpace by A1,STRUCT_0:def 1;
    reconsider x = x0 as Point of T by A1,Def7;
    let A be Subset of DiscrWithInfin(X,x0);
    reconsider A' = A as Subset of T;
    assume A` is infinite; then
    Cl A` = A'` \/ {x} by A1,Th35;
    hence Int A = (A'` \/ {x})` by TOPS_1:def 1
       .= (A' \ {x})`` by SUBSET_1:33 .= A \ {x0};
  end;

theorem Th43:
  for X, x0 being set holds
  ex B0 being Basis of DiscrWithInfin(X,x0) st
  B0 = ((SmallestPartition X) \ {{x0}}) \/
       {F` where F is Subset of X: F is finite}
  proof
    let X, x0 be set;
    set T = DiscrWithInfin(X,x0);
    set O1 = {U where U is Subset of X: not x0 in U};
    set O2 = {F` where F is Subset of X: F is finite};
A:  the carrier of T = X & the topology of T = O1 \/ O2 by Def7;
    set B1 = (SmallestPartition X) \ {{x0}};
    set B2 = {F` where F is Subset of X: F is finite};
A1: B1 c= bool the carrier of T
    proof let a; assume a in B1; then
     a in SmallestPartition X by ZFMISC_1:64;
     hence thesis by A;
    end;
    B2 c= bool the carrier of T
    proof
     let a; assume a in B2; then
     ex F being Subset of X st a = F` & F is finite;
     hence thesis by A;
    end; then
    B1 \/ B2 c= bool the carrier of T by A1,XBOOLE_1:8; then
    reconsider B0 = B1 \/ B2 as Subset-Family of T by SETFAM_1:def 7;
A2: B1 c= the topology of T
    proof let a; assume a in B1; then
C1:  a is Subset of T & a in SmallestPartition X & a <> {x0}
     by A1,ZFMISC_1:64; then
     reconsider X as non empty set by EQREL_1:def 6;
     SmallestPartition X = {{x} where x is Element of X: not contradiction}
     by PUA2MSS1:13; then
     ex x being Element of X st a = {x} by C1; then
     not x0 in a by C1,TARSKI:def 1; then
     a is open Subset of T by C1,Th31;
     hence thesis by PRE_TOPC:def 5;
    end;
    B2 c= the topology of T
    proof
     let a; assume a in B2; then
     consider F being Subset of X such that
C2:  a = F` & F is finite;
     F`` is finite by C2; then
     a is open Subset of T by A,C2,Th31;
     hence thesis by PRE_TOPC:def 5;
    end; then
A3: B0 c= the topology of T by A2,XBOOLE_1:8;
    now
     let A be Subset of T such that
B1:  A is open;
     let p be Point of T such that
B2:  p in A;
     reconsider X' = X as non empty set by A,B2;
     reconsider p' = p as Element of X' by A;  
     SmallestPartition X = {{x} where x is Element of X': not contradiction}
     by PUA2MSS1:13; then
B3:  {p'} in SmallestPartition X;
     not x0 in A or A` is finite by B1,Th31; then
     {p} <> {x0} or A`` in B2 by A,B2,ZFMISC_1:6; then
     not {p} in {{x0}} or A in B2 by TARSKI:def 1; then
     {p} in B1 or A in B2 by B3,XBOOLE_0:def 4; then
     {p} in B0 & p in {p} & {p} c= A or A in B0 & A c= A
      by B2,XBOOLE_0:def 2,ZFMISC_1:37;
     hence ex a being Subset of T st a in B0 & p in a & a c= A by B2;
    end; then
    reconsider B0 as Basis of DiscrWithInfin(X,x0) by A3,YELLOW_9:32;
    take B0;
    thus B0 = B1 \/ B2;
  end;

theorem
  for X being infinite set holds Card Fin X = Card X
  proof let X be infinite set;
    set FX = Fin X;
    deffunc f(set) = proj2 $1;
    consider f being Function such that
A1: dom f = X* & for a st a in X* holds f.a = f(a) from FUNCT_1:sch 3;
    FX c= rng f
    proof let a; assume a in FX; then
A2:  a c= X & a is finite by FINSUB_1:def 5; then
     consider p being FinSequence such that
A3:  a = rng p by FINSEQ_1:73;
     p is FinSequence of X by A2,A3,FINSEQ_1:def 4; then
     p in X* by FINSEQ_1:def 11; then
     f.p in rng f & f.p = proj2 p & proj2 p = a
      by A1,A3,FUNCT_1:def 5,FUNCT_5:21;
     hence thesis;
    end; then
    Card FX <=` Card (X*) by A1,CARD_1:28;
    hence Card FX <=` Card X by CARD_4:87;
    set SX = SmallestPartition X;
    SX c= FX
    proof let a; assume a in SX; then
     a in {{x} where x is Element of X: not contradiction} by PUA2MSS1:13; then
     ex x being Element of X st a = {x};
     hence thesis by FINSUB_1:def 5;
    end; then
    Card SX <=` Card FX by CARD_1:27;
    hence thesis by Th18;
   end;

theorem Th41:
  for X being infinite set holds
  Card {F` where F is Subset of X: F is finite} = Card X
  proof let X be infinite set;
    set FX = {F` where F is Subset of X: F is finite};
    deffunc f(set) = X\proj2 $1;
    consider f being Function such that
A1: dom f = X* & for a st a in X* holds f.a = f(a) from FUNCT_1:sch 3;
    FX c= rng f
    proof let a; assume a in FX; then
     consider F being Subset of X such that
A2:  a = F` & F is finite;
     consider p being FinSequence such that
A3:  F = rng p by A2,FINSEQ_1:73;
     p is FinSequence of X by A3,FINSEQ_1:def 4; then
     p in X* by FINSEQ_1:def 11; then
     f.p in rng f & f.p = X\proj2 p & proj2 p = F
      by A1,A3,FUNCT_1:def 5,FUNCT_5:21;
     hence thesis by A2,SUBSET_1:def 5;
    end; then
    Card FX <=` Card (X*) by A1,CARD_1:28;
    hence Card FX <=` Card X by CARD_4:87;
    deffunc f(set) = X\{$1};
    consider f being Function such that
A1: dom f = X & for a st a in X holds f.a = f(a) from FUNCT_1:sch 3;
A2: f is one-to-one
    proof let a,b; assume
A3:  a in dom f & b in dom f & f.a = f.b; then
     reconsider a,b as Element of X by A1;
     {a}` = f(a) by SUBSET_1:def 5 .= f.b by A1,A3 .= f(b) by A1
         .= {b}` by SUBSET_1:def 5; then
     {a} = {b} by FIN_TOPO:29;
     hence thesis by ZFMISC_1:6;
    end;
    rng f c= FX
    proof let a; assume a in rng f; then
     consider b such that
A3:  b in dom f & a = f.b by FUNCT_1:def 5;
     reconsider b as Element of X by A1,A3;
     {b}` in FX & a = X\{b} by A1,A3;
     hence thesis by SUBSET_1:def 5;
    end;
    hence thesis by A1,A2,CARD_1:26;
   end;

theorem Th42:
  for X being infinite set, x0 being set
  for B0 being Basis of DiscrWithInfin(X,x0) st
  B0 = ((SmallestPartition X) \ {{x0}}) \/
       {F` where F is Subset of X: F is finite}
  holds Card B0 = Card X
  proof
    let X be infinite set;
    let x0 be set;
    set T = DiscrWithInfin(X,x0);
    let B0 be Basis of T;
    set SX = SmallestPartition X,
        FX = {F` where F is Subset of X: F is finite};
01: Card FX = Card X by Th41;
    assume
A0: B0 = (SX \ {{x0}}) \/ FX;
A1: Card {{x0}} = 1 & Card SX = Card X by Th18,CARD_1:79;
A2: 1 <` Card X by CARD_4:13; then
    Card X +` 1 = Card X by CARD_4:34; then
A3: Card (SX \ {{x0}}) = Card X by A1,A2,CARD_4:76; then
    Card B0 <=` Card X +` Card X by A0,01,CARD_2:47;
    hence Card B0 <=` Card X by CARD_4:33;
    SX \ {{x0}} c= B0 by A0,XBOOLE_1:7;
    hence thesis by A3,CARD_1:27;
  end;

theorem Th44:
  for X being infinite set, x0 being set
  for B being Basis of DiscrWithInfin(X,x0) holds Card X <=` Card B
  proof
    let X be infinite set;
    let x0 be set;
    set T = DiscrWithInfin(X,x0);
00: the carrier of T = X by Def7;
    set SX = SmallestPartition X,
        FX = {F` where F is Subset of X: F is finite};
A1: Card {{x0}} = 1 & Card SX = Card X by Th18,CARD_1:79;
A2: 1 <` Card X by CARD_4:13; then
    Card X +` 1 = Card X by CARD_4:34; then
A3: Card (SX \ {{x0}}) = Card X by A1,A2,CARD_4:76;
A4: SX = {{x} where x is Element of X: not contradiction} by PUA2MSS1:13;
    let B be Basis of T;
    SX\{{x0}} c= B
    proof let a; assume a in SX\{{x0}}; then
     a in SX & not a in {{x0}} by XBOOLE_0:def 4; then
     (ex x being Element of X st a = {x}) & a <> {x0} by A4,TARSKI:def 1; then
     consider x being Element of T such that
A5:  a = {x} & x <> x0 by 00;
     x in a & a is open Subset of T by 00,A5,Th36,TARSKI:def 1; then
     consider U being Subset of T such that
A6:  U in B & x in U & U c= a by YELLOW_9:31;
     a = U proof thus a c= U by A5,A6,ZFMISC_1:37; thus thesis by A6; end;
     hence thesis by A6;
    end;
    hence thesis by A3,CARD_1:27;
  end;

theorem
  for X being infinite set, x0 being set holds
  weight DiscrWithInfin(X,x0) = Card X
  proof
    let X be infinite set;
    let x0 be set;
    set T = DiscrWithInfin(X,x0);
    consider B being Basis of T such that
A1: Card B = weight T by WAYBEL31:2;
    consider B0 being Basis of T such that
A2: B0 = ((SmallestPartition X) \ {{x0}}) \/
         {F` where F is Subset of X: F is finite} by Th43;
    Card B0 = Card X by A2,Th42;
    hence weight T <=` Card X by WAYBEL31:1;
    thus thesis by A1,Th44;
  end;

theorem
  for X being non empty set, x0 being set holds
  ex B0 being prebasis of DiscrWithInfin(X,x0) st
  B0 = ((SmallestPartition X) \ {{x0}}) \/
       {{x}` where x is Element of X: not contradiction}
  proof
    let X be non empty set;
    let x0 be set;
    set T = DiscrWithInfin(X,x0);
00: the carrier of T = X by Def7;
    set SX = SmallestPartition X,
        FX = {F` where F is Subset of X: F is finite},
        AX = {{x}` where x is Element of X: not contradiction};
    reconsider SX as Subset-Family of T by Def7;
    AX c= bool X
    proof let a; assume a in AX; then
     ex x being Element of X st a = {x}`;
     hence thesis;
    end; then
    reconsider AX as Subset-Family of T by 00,SETFAM_1:def 7;
    SX\{{x0}} c= SX by XBOOLE_1:36; then
    SX\{{x0}} c= bool X by XBOOLE_1:1; then
    (SX\{{x0}})\/AX c= bool X by 00,XBOOLE_1:8; then
    reconsider pB = (SX\{{x0}})\/AX as Subset-Family of T by 00,SETFAM_1:def 7;
    consider B0 being Basis of T such that
A2: B0 = ((SmallestPartition X) \ {{x0}}) \/ FX by Th43;
    AX c= FX
    proof let a; assume a in AX; then
     ex x being Element of X st a = {x}`;
     hence thesis;
    end; then
    pB c= B0 & B0 c= the topology of T by A2,XBOOLE_1:9,CANTOR_1:def 2; then
    pB c= the topology of T by XBOOLE_1:1; then
    FinMeetCl pB c= FinMeetCl the topology of T by CANTOR_1:16; then
A3: FinMeetCl pB c= the topology of T by CANTOR_1:5;
A4: pB c= FinMeetCl pB by CANTOR_1:4;
A5: FX c= FinMeetCl pB
    proof let a; assume a in FX; then
     consider F being Subset of T such that
A6:  a = F` & F is finite by 00;
     set SF = SmallestPartition F;
     bool F c= bool X by 00,ZFMISC_1:79; then
     SF c= bool X by XBOOLE_1:1; then
     reconsider SF as Subset-Family of T by 00,SETFAM_1:def 7;
     per cases;
     suppose F = {}; then
      a = ({}X)` by A6,Def7 .= [#]X by SUBSET_1:22 .= X by SUBSET_1:def 4;
      hence thesis by 00,CANTOR_1:8;
     end;
     suppose F <> {}; then
      reconsider F as non empty Subset of T;
      SF is a_partition of F; then
      reconsider SF as non empty Subset-Family of T;
      bool F is finite by A6,FINSET_1:24; then
      SF is finite by FINSET_1:13; then
A7:   COMPLEMENT SF is finite by TOPS_2:13;
      COMPLEMENT SF c= pB
      proof let a; assume
A9:    a in COMPLEMENT SF; then
       reconsider a as Subset of T;
       a` in SF by A9,SETFAM_1:def 8; then
       a` in {{b} where b is Element of F: not contradiction}
       by PUA2MSS1:13; then
       consider b being Element of F such that
A10:   a` = {b};
       reconsider b as Element of X by Def7;
       {b}` in AX;
       hence thesis by 00,A10,XBOOLE_0:def 2;
      end; then
A8:   Intersect COMPLEMENT SF in FinMeetCl pB by A7,CANTOR_1:def 4;
      F = union SF by EQREL_1:def 6; then
      a = meet COMPLEMENT SF by A6,TOPS_2:11;
      hence thesis by A8,SETFAM_1:def 10;
     end;
    end;
    B0 c= FinMeetCl pB
    proof let a; assume a in B0; then
     a in SX\{{x0}} or a in FX by A2,XBOOLE_0:def 2; then
     a in pB or a in FX by XBOOLE_0:def 2;
     hence thesis by A4,A5;
    end; then
    FinMeetCl pB is Basis of T by A3,WAYBEL19:22; then
    pB is prebasis of T by YELLOW_9:23;
    hence thesis;
  end;

begin :: Exercises

::<DSCR name="Exercise 1.1.B" monograph="topology"/>
theorem
  for T being TopSpace, F being Subset-Family of T
  for I being non empty Subset-Family of F
  st for G being set st G in I holds F\G is finite
  holds Cl union F = union clf F \/
        meet {Cl union G where G is Subset-Family of T: G in I}
  proof
   let T be TopSpace;
   let F be Subset-Family of T;
   let I be non empty Subset-Family of F;
   consider G0 being Element of I;
   G0 c= bool the carrier of T by XBOOLE_1:1; then
   reconsider G0 as Subset-Family of T by SETFAM_1:def 7;
   assume
A2: for G being set st G in I holds F\G is finite;
   set Z = {Cl union G where G is Subset-Family of T: G in I};
08: Cl union G0 in Z; then
   reconsider Z' = Z as non empty set;
   thus Cl union F c= union clf F \/ meet Z
   proof let a; assume
00: a in Cl union F & not a in union clf F \/ meet Z; then
    reconsider a as Point of T;
01: not a in meet Z' & not a in union clf F by 00,XBOOLE_0:def 2; then
    consider b such that
02: b in Z & not a in b by SETFAM_1:def 1;
    consider G being Subset-Family of T such that
03: b = Cl union G & G in I by 02;
04: T is non empty by 00,STRUCT_0:def 1;
    F = G \/ (F\G) by 03,XBOOLE_1:45; then
    union F = union G \/ union (F\G) by ZFMISC_1:96; then
    Cl union F = Cl union G \/ Cl union (F\G) & F\G c= F
    by XBOOLE_1:36,PRE_TOPC:50; then
    a in Cl union (F\G) & clf (F\G) c= clf F & G c= F & F\G is finite
    by A2,00,02,03,04,XBOOLE_0:def 2,PCOMPS_1:17; then
    a in union clf (F\G) & union clf (F\G) c= union clf F
    by 04,PCOMPS_1:19,ZFMISC_1:95;
    hence contradiction by 01;
   end;
   let a; assume
05: a in union clf F \/ meet Z;
    per cases by 05,XBOOLE_0:def 2;
    suppose a in union clf F; then
     consider b such that
06:  a in b & b in clf F by TARSKI:def 4;
     consider W being Subset of T such that
07:  b = Cl W & W in F by 06,PCOMPS_1:def 3;
     W c= union F by 07,ZFMISC_1:92; then
     b c= Cl union F by 07,PRE_TOPC:49;
     hence thesis by 06;
    end;
    suppose
00:   a in meet Z';
      union G0 c= union F by ZFMISC_1:95; then
      a in Cl union G0 & Cl union G0 c= Cl union F
      by 00,08,SETFAM_1:def 1,PRE_TOPC:49;
      hence thesis;
    end;
  end;

::<DSCR name="Exercise 1.1.B" monograph="topology"/>
theorem
  for T being TopSpace, F being Subset-Family of T
  holds Cl union F = union clf F \/
  meet {Cl union G where G is Subset-Family of T: G c= F & F\G is finite}
  proof
   let T be TopSpace;
   let F be Subset-Family of T;
   set Z = {Cl union G where G is Subset-Family of T: G c= F & F\G is finite};
   F c= F & F\F = {} by XBOOLE_1:37; then
08: Cl union F in Z; then
   reconsider Z' = Z as non empty set;
   thus Cl union F c= union clf F \/ meet Z
   proof let a; assume
00: a in Cl union F & not a in union clf F \/ meet Z; then
    reconsider a as Point of T;
01: not a in meet Z' & not a in union clf F by 00,XBOOLE_0:def 2; then
    consider b such that
02: b in Z & not a in b by SETFAM_1:def 1;
    consider G being Subset-Family of T such that
03: b = Cl union G & G c= F & F\G is finite by 02;
04: T is non empty by 00,STRUCT_0:def 1;
    F = G \/ (F\G) by 03,XBOOLE_1:45; then
    union F = union G \/ union (F\G) by ZFMISC_1:96; then
    Cl union F = Cl union G \/ Cl union (F\G) & F\G c= F :: & F\G is Subset-Family of T
    by XBOOLE_1:36,PRE_TOPC:50; then
    a in Cl union (F\G) & clf (F\G) c= clf F
    by 00,02,03,04,XBOOLE_0:def 2,PCOMPS_1:17; then
    a in union clf (F\G) & union clf (F\G) c= union clf F
    by 03,04,PCOMPS_1:19,ZFMISC_1:95;
    hence contradiction by 01;
   end;
   let a; assume
05: a in union clf F \/ meet Z;
    per cases by 05,XBOOLE_0:def 2;
    suppose a in union clf F; then
     consider b such that
06:  a in b & b in clf F by TARSKI:def 4;
     consider W being Subset of T such that
07:  b = Cl W & W in F by 06,PCOMPS_1:def 3;
     W c= union F by 07,ZFMISC_1:92; then
     b c= Cl union F by 07,PRE_TOPC:49;
     hence thesis by 06;
    end;
    suppose a in meet Z';
     hence thesis by 08,SETFAM_1:def 1;
    end;
  end;

::<DSCR name="Exercise 1.1.D" monograph="topology"/>
theorem
  for X being set, O being Subset-Family of bool X st
   for o being Subset-Family of X st o in O holds TopStruct(#X,o#) is TopSpace
  ex B being Subset-Family of X st
    B = Intersect O &
    TopStruct(#X,B#) is TopSpace &
    (for o being Subset-Family of X st o in O
      holds TopStruct(#X,o#) is TopExtension of TopStruct(#X,B#)) &
    (for T being TopSpace st the carrier of T = X &
      for o being Subset-Family of X st o in O
        holds TopStruct(#X,o#) is TopExtension of T
     holds TopStruct(#X,B#) is TopExtension of T)
     proof
      let X be set;
      let O be Subset-Family of bool X such that
00:   for o being Subset-Family of X st o in O
      holds TopStruct(#X,o#) is TopSpace;
      reconsider B = Intersect O as Subset-Family of X by SETFAM_1:def 7;
      set T = TopStruct(#X,B#);
      take B; thus B = Intersect O;
      T is TopSpace-like
      proof
       now
        thus the carrier of T in bool the carrier of T by ZFMISC_1:def 1;
        let a; assume
01:     a in O; then
        reconsider o = a as Subset-Family of X by SETFAM_1:def 7;
        TopStruct(#X,o#) is TopSpace by 00,01;
        hence the carrier of T in a by PRE_TOPC:def 1;
       end;
       hence the carrier of T in the topology of T by SETFAM_1:58;
       hereby
        let a be Subset-Family of T such that
02:     a c= the topology of T;
        now let b; assume
03:      b in O; then
         reconsider o = b as Subset-Family of X by SETFAM_1:def 7;
         B c= b by 03,MSSUBFAM:2; then
         a c= o & TopStruct(#X,o#) is TopSpace by 00,02,03,XBOOLE_1:1;
         hence union a in b by PRE_TOPC:def 1;
        end;
        hence union a in the topology of T by SETFAM_1:58;
       end;
       let a,b be Subset of T such that
04:    a in the topology of T & b in the topology of T;
       now let c; assume
05:     c in O; then
        reconsider o = c as Subset-Family of X by SETFAM_1:def 7;
        a in o & b in o & TopStruct(#X,o#) is TopSpace by 00,04,05,SETFAM_1:58;
        hence a /\ b in c by PRE_TOPC:def 1;
       end;
       hence a /\ b in the topology of T by SETFAM_1:58;
      end;
      hence T is TopSpace; then
      reconsider TT = T as TopSpace;
      thus for o being Subset-Family of X st o in O
         holds TopStruct(#X,o#) is TopExtension of T
      proof
       let o be Subset-Family of X such that
06:    o in O;
       reconsider S = TopStruct(#X,o#) as TopSpace by 00,06;
       S is TopExtension of T
       proof thus the carrier of T = the carrier of S;
        thus the topology of T c= the topology of S by 06,MSSUBFAM:2;
       end;
       hence thesis;
      end;
      let T' be TopSpace such that
08:   the carrier of T' = X and
07:   for o being Subset-Family of X st o in O
      holds TopStruct(#X,o#) is TopExtension of T';
      TT is TopExtension of T'
      proof
       thus the carrier of T' = the carrier of TT by 08;
       now
        let a; assume
09:     a in O; then
        reconsider o = a as Subset-Family of X by SETFAM_1:def 7;
        TopStruct(#X,o#) is TopExtension of T' by 07,09;
        hence the topology of T' c= a by YELLOW_9:def 5;
       end;
       hence the topology of T' c= the topology of TT by 08,MSSUBFAM:4;
      end;
      hence thesis;
     end;

::<DSCR name="Exercise 1.1.D" monograph="topology"/>
theorem
  for X being set, O being Subset-Family of bool X
  ex B being Subset-Family of X st
    B = UniCl FinMeetCl union O &
    TopStruct(#X,B#) is TopSpace &
    (for o being Subset-Family of X st o in O
      holds TopStruct(#X,B#) is TopExtension of TopStruct(#X,o#)) &
    (for T being TopSpace st the carrier of T = X &
      for o being Subset-Family of X st o in O
        holds T is TopExtension of TopStruct(#X,o#)
     holds T is TopExtension of TopStruct(#X,B#))
     proof
      let X be set;
      let O be Subset-Family of bool X;
      reconsider B = UniCl FinMeetCl union O as Subset-Family of X;
      take B; thus B = UniCl FinMeetCl union O;
      reconsider E = {} bool bool {} as empty Subset-Family of bool {}
      by SETFAM_1:def 7;
      reconsider e = {} bool {}, tE = {{}{}} as Subset-Family of {}
      by SETFAM_1:def 7;
00:   bool {} = tE by ZFMISC_1:1; then
      reconsider dT = TopStruct(#{},tE#) as discrete TopStruct
      by TDLAT_3:def 1;
03:   UniCl tE = {{},{}} & FinMeetCl e = tE by YELLOW_9:11,17;
05:   FinMeetCl tE = {{},{}} & {{},{}} = tE by YELLOW_9:11,ENUMSET1:69;
0B:   now assume
01:    X is empty; then union O c= tE by 00; hence
04:    union O = e or union O = tE by ZFMISC_1:39;
       thus FinMeetCl union E = the topology of dT by YELLOW_9:17;
       hence TopStruct(#X,B#) is TopSpace by 01,05,03,04;
      end;
      hence TopStruct(#X,B#) is TopSpace by CANTOR_1:17; then
      reconsider TT = TopStruct(#X,B#) as TopSpace;
      hereby
       let o be Subset-Family of X; set S = TopStruct(#X,o#);
       assume 07: o in O;
       TT is TopExtension of S
       proof
        thus the carrier of S = the carrier of TT;
        o c= union O & union O c= FinMeetCl union O
        by 07,CANTOR_1:4,ZFMISC_1:92; then
        o c= FinMeetCl union O & FinMeetCl union O c= B
        by CANTOR_1:1,XBOOLE_1:1;
        hence thesis by XBOOLE_1:1;
       end;
       hence TopStruct(#X,B#) is TopExtension of S;
      end;
      let T be TopSpace such that
09:   the carrier of T = X and
08:   for o being Subset-Family of X st o in O
      holds T is TopExtension of TopStruct(#X,o#);
      thus the carrier of T = the carrier of TopStruct(#X,B#) by 09;
      X in the topology of T by 09,PRE_TOPC:def 1; then
0C:   {X} c= the topology of T by ZFMISC_1:37;
      now
       let a; assume
0A:    a in O; then
       reconsider o = a as Subset-Family of X by SETFAM_1:def 7;
       T is TopExtension of TopStruct(#X,o#) by 08,0A;
       hence a c= the topology of T by YELLOW_9:def 5;
      end; then
      union O c= the topology of T by ZFMISC_1:94; then
      FinMeetCl union O c= FinMeetCl the topology of T by 09,CANTOR_1:16; then
0D:   B c= UniCl FinMeetCl the topology of T by 09,CANTOR_1:9;
      X <> {} implies T is non empty by 09,STRUCT_0:def 1;
      hence the topology of TopStruct(#X,B#) c= the topology of T
      by 0D,0C,0B,CANTOR_1:7,03,05;
     end;

