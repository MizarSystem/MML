:: The Class of Series-Parallel Graphs, {II}
::  by Krzysztof Retel
::
:: Received May 29, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies NECKLA_2, NECKLACE, CLASSES1, CLASSES2, ORDERS_1, RELAT_1,
      REALSET1, FUNCT_1, PRE_TOPC, BOOLE, SETFAM_1, CANTOR_1, CARD_1, PROB_1,
      SQUARE_1, ARYTM, FINSET_1, ORDINAL2, ORDINAL1;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, ZFMISC_1, RELSET_1, FINSET_1,
      CARD_1, ORDINAL1, ORDINAL2, NUMBERS, XCMPLX_0, XREAL_0, NAT_1, REALSET1,
      STRUCT_0, GROUP_1, REALSET2, SQUARE_1, ORDERS_2, RELAT_1, NECKLACE,
      PRE_TOPC, FUNCT_1, FUNCT_2, CLASSES2, SETFAM_1, CLASSES1, CARD_3,
      CARD_LAR;
 constructors ENUMSET1, NECKLACE, NAT_1, CLASSES2, CANTOR_1, GROUP_1, CARD_3,
      SQUARE_1, COHSP_1, CARD_LAR, REALSET2, MEMBERED;
 registrations RELSET_1, ORDERS_2, STRUCT_0, FINSET_1, NAT_1, NECKLACE,
      ORDINAL1, CLASSES2, YELLOW13, XREAL_0, COHSP_1, XBOOLE_0, CARD_1,
      ORDINAL2;
 requirements BOOLE, SUBSET, REAL, NUMERALS, ARITHM;
 definitions TARSKI;
 theorems FUNCT_1, FUNCT_2, ENUMSET1, RELSET_1, ZFMISC_1, XBOOLE_1, TARSKI,
      ORDINAL1, PARTFUN1, NECKLACE, CLASSES2, CLASSES1, XBOOLE_0, SETFAM_1,
      CARD_5, NAT_1, REALSET2, SQUARE_1, FINSEQ_2, AXIOMS, STRUCT_0, RELAT_1,
      REAL_1, CQC_THE1, CARD_2, ORDINAL2, CARD_4, CARD_1, GROUP_1, FINSET_1,
      SUBSET_1, CARD_3, REALSET1;
 schemes TARSKI, XBOOLE_0, RECDEF_1, NAT_1;

begin

reserve U for Universe;

theorem Th1:
  for X,Y being set st X in U & Y in U
      for R being Relation of X,Y holds R in U
  proof
   let X,Y being set;
   assume X in U & Y in U;
then [:X,Y:] in U by CLASSES2:67;
   hence thesis by CLASSES1:def 1;
 end;

theorem Th2:
  the InternalRel of Necklace 4 =
    {[0,1],[1,0],[1,2],[2,1],[2,3],[3,2]}
  proof
    set A = {[0,1],[1,0],[1,2],[2,1],[2,3],[3,2]},
        B = the InternalRel of Necklace 4;
    A1: B =
    {[i,i+1] where i is Nat:i+1 < 4} \/ {[i+1,i] where i is Nat:i+1 < 4}
    by NECKLACE:19;
     A2: [0,0+1] in {[i,i+1] where i is Nat:i+1 < 4};
     A3: [0+1,0] in {[i+1,i] where i is Nat:i+1 < 4};
     A4: [0+1,1+1] in {[i,i+1] where i is Nat:i+1 < 4};
     A5: [1+1,0+1] in {[i+1,i] where i is Nat:i+1 < 4};
     A6: [1+1,2+1] in {[i,i+1] where i is Nat:i+1 < 4};
     A7: [2+1,1+1] in {[i+1,i] where i is Nat:i+1 < 4};
    A8: A c= B
      proof
        let x be set;
        assume A9: x in A;
        per cases by A9,ENUMSET1:28;
        suppose x = [0,1];
        hence thesis by A1,A2,XBOOLE_0:def 2;
        end;
        suppose x=[1,0];
        hence thesis by A1,A3,XBOOLE_0:def 2;
        end;
        suppose x=[1,2];
        hence thesis by A1,A4,XBOOLE_0:def 2;
        end;
        suppose x=[2,1];
        hence thesis by A1,A5,XBOOLE_0:def 2;
        end;
        suppose x=[2,3];
        hence thesis by A1,A6,XBOOLE_0:def 2;
        end;
        suppose x=[3,2];
        hence thesis by A1,A7,XBOOLE_0:def 2;
      end;
      end;
         B c= A
      proof
        let x be set;
        assume A10: x in B;
        then consider y,z being set such that
        A11: x = [y,z] by RELAT_1:def 1;
        per cases by A1,A10,XBOOLE_0:def 2;
        suppose x in {[i,i+1] where i is Nat: i+1<4};
         then consider i be Nat such that
         A12: [y,z] = [i,i+1] & i+1 < 4 by A11;
         A13: y = i & z = i+1 by A12,ZFMISC_1:33;
               i + 1 < 1 + 3 by A12;
           then i < 2+1 by REAL_1:55;
           then i <= 2 by NAT_1:38;
           then y = 0 or y = 1 or y = 2 by A13,CQC_THE1:3;
         hence thesis by A11,A13,ENUMSET1:29;
        end;
        suppose x in {[i+1,i] where i is Nat: i+1<4};
         then consider i be Nat such that
         A14: [y,z] = [i+1,i] & i+1 < 4 by A11;
         A15: y = i+1 & z = i by A14,ZFMISC_1:33;
               i + 1 < 1 + 3 by A14;
           then i < 2+1 by REAL_1:55;
           then i <= 2 by NAT_1:38;
           then z = 0 or z = 1 or z = 2 by A15,CQC_THE1:3;
        hence thesis by A11,A15,ENUMSET1:29;
      end;
      end;
    hence thesis by A8,XBOOLE_0:def 10;
 end;

registration let n be natural number;
  cluster -> finite Element of Rank n;
  coherence
    proof
      let x be Element of Rank n;
      defpred P[natural number] means
           for x being set st x is Element of Rank $1
           holds x is finite;
      A1: P[0] by CLASSES1:33,SUBSET_1:def 2;
      A2: for k being natural number st P[k] holds P[k + 1]
       proof
         let k being natural number such that P[k];
         let x be set such that A3: x is Element of Rank (k+1);
         consider m being Nat such that A4: m = k by CARD_1:54;
               x is Element of Rank (succ m) by A3,A4,CARD_1:52;
           then A5: x is Element of bool Rank m by CLASSES1:34;
               Rank m is finite by CARD_3:57;
         hence thesis by A5,FINSET_1:13;
       end;
            for k being natural number holds P[k] from NAT_1:sch 2(A1,A2);
      hence thesis;
    end;
 end;

theorem Th3:
  for x be set st x in FinSETS holds x is finite
  proof
    let x be set such that A1: x in FinSETS;
         omega is_limit_ordinal by ORDINAL2:def 5;
    then consider n being Ordinal such that A2: n in omega &
    x in Rank n by A1,CLASSES1:35,CLASSES2:71;
    reconsider n as natural number by A2,ORDINAL2:def 21;
          x is Element of Rank n by A2;
    hence thesis;
  end;

registration
  cluster -> finite Element of FinSETS;
  coherence by Th3;
 end;

registration
  cluster finite ordinal -> natural number;
  coherence
   proof
     let n be number;
     assume n is finite ordinal;
       then n in omega by CARD_4:7;
     hence n is natural by ORDINAL2:def 21;
   end;
end;

definition let G be non empty RelStr;
  attr G is N-free means
:Def1: not G embeds Necklace 4;
end;

registration
 cluster strict finite N-free (non empty RelStr);
 existence
  proof
   set X = {0,1},
       Y = Necklace 4;
   reconsider r = id X as Relation of X by RELSET_1:29;
   take R = RelStr(#X,r#);
        now let f being map of Y,R;
     assume A1: f is one-to-one;
A2: dom f = the carrier of Y by FUNCT_2:def 1
             .= {0,1,2,3} by NECKLACE:2,21;
then A3: 1 in dom f by ENUMSET1:19;
A4: 2 in dom f by A2,ENUMSET1:19;
A5: 3 in dom f by A2,ENUMSET1:19;
then A6: f.1 <> f.3 by A1,A3,FUNCT_1:def 8;
A7: f.2 <> f.3 by A1,A4,A5,FUNCT_1:def 8;
A8: f.1 in X by A3,PARTFUN1:27;
A9: f.2 in X by A4,PARTFUN1:27;
A10: f.3 in X by A5,PARTFUN1:27;
A11: f.1 = 0 or f.1 = 1 by A8,TARSKI:def 2;
         f.2 = 0 or f.2 = 1 by A9,TARSKI:def 2;
     hence ex x,y being Element of Y st
     not ([x,y] in the InternalRel of Y
                iff [f.x,f.y] in the InternalRel of R) by A1,A3,A4,A6,A7,A10
,A11,FUNCT_1:def 8,TARSKI:def 2;
   end;
   then not R embeds Y by NECKLACE:def 2;
   hence thesis by Def1,GROUP_1:def 14;
  end;
end;

definition let R,S be RelStr;
  func union_of(R,S) -> strict RelStr means
:Def2: the carrier of it = (the carrier of R) \/ (the carrier of S) &
    the InternalRel of it = (the InternalRel of R) \/ (the InternalRel of S);
 existence
  proof
   set X = (the carrier of R) \/ (the carrier of S);
    the carrier of R c= X & the carrier of S c= X by XBOOLE_1:7;
   then reconsider IR = the InternalRel of R, IS = the InternalRel of S
     as Relation of X by RELSET_1:17;
   set D = IR \/ IS;
   reconsider D as Relation of X;
   take RelStr (# X, D #);
   thus thesis;
  end;
 uniqueness;
end;

definition let R, S be RelStr;
  func sum_of(R,S) -> strict RelStr means
:Def3: the carrier of it = (the carrier of R) \/ (the carrier of S) &
    the InternalRel of it = (the InternalRel of R) \/ (the InternalRel of S)
                         \/ [:the carrier of R, the carrier of S:]
                         \/ [:the carrier of S, the carrier of R:];
 existence
  proof
   set X = (the carrier of R) \/ (the carrier of S);
    the carrier of R c= X & the carrier of S c= X by XBOOLE_1:7;
   then reconsider IR = the InternalRel of R, IS = the InternalRel of S
              as Relation of X by RELSET_1:17;
A1: the carrier of R c= X by XBOOLE_1:7;
A2: the carrier of S c= X by XBOOLE_1:7;
     then [:the carrier of R, the carrier of S:] c= [:X,X:] by A1,ZFMISC_1:119
;
    then reconsider IQ = [:the carrier of R, the carrier of S:]
        as Relation of X by RELSET_1:def 1;
         [:the carrier of S, the carrier of R:] c= [:X,X:] by A1,A2,
ZFMISC_1:119
;
    then reconsider IP = [:the carrier of S, the carrier of R:]
        as Relation of X by RELSET_1:def 1;
   set D = IR \/ IS \/ IQ \/ IP;
   take RelStr (# X, D #);
   thus thesis;
  end;
 uniqueness;
end;

definition
 func fin_RelStr means
:Def4: for X being set holds
       X in it iff
       ex R being strict RelStr st X = R & the carrier of R in FinSETS;
 existence
  proof
   defpred P[set,set] means ex R being strict RelStr
           st $1 = [the carrier of R,the InternalRel of R] &
              $2 = R;
A1: for x,y,z being set st P[x,y] & P[x,z] holds y = z
   proof
    let x,y,z being set;
    given R1 being strict RelStr such that
A2: x = [the carrier of R1,the InternalRel of R1] and
A3: y = R1;
    given R2 being strict RelStr such that
A4: x = [the carrier of R2,the InternalRel of R2] and
A5: z = R2;
          the carrier of R1 = the carrier of R2 &
    the InternalRel of R1 = the InternalRel of R2 by A2,A4,ZFMISC_1:33;
    hence thesis by A3,A5;
   end;
   consider X being set such that A6: for x being set holds x in X iff
     ex y being set st y in FinSETS & P[y,x] from TARSKI:sch 1(A1);
   take X;
        for x being set holds x in X iff
      ex R being strict RelStr st x = R &
      the carrier of R in FinSETS
    proof
     let x being set;
     thus x in X implies
        ex R being strict RelStr st x = R &
        the carrier of R in FinSETS
      proof
       assume x in X;
       then consider y being set such that
       A7: y in FinSETS & ex R being strict RelStr
       st y = [the carrier of R,the InternalRel of R] & x = R by A6;
       consider R being strict RelStr
       such that A8: y = [the carrier of R,the InternalRel of R] & x = R
            by A7;
A9: {{the carrier of R, the InternalRel of R}, {the carrier of R}} in
        FinSETS by A7,A8,TARSKI:def 5;
A10: {the carrier of R} in
       {{the carrier of R, the InternalRel of R}, {the carrier of R}}
       by TARSKI:def 2;
             {{the carrier of R, the InternalRel of R}, {the carrier of R}}
       c= FinSETS by A9,ORDINAL1:def 2;
        then A11: {the carrier of R} c= FinSETS by A10,ORDINAL1:def 2;
             the carrier of R in {the carrier of R} by TARSKI:def 1;
       hence thesis by A8,A11;
      end;
      given R being strict RelStr such that A12: x = R
           & the carrier of R in FinSETS;
      consider R being strict RelStr such that
      A13: x = R & the carrier of R in FinSETS by A12;
           the InternalRel of R in FinSETS by A13,Th1;
       then [the carrier of R,the InternalRel of R] in FinSETS by A13,
CLASSES2:64;
      hence x in X by A6,A13;
    end;
   hence thesis;
  end;
 uniqueness proof
  defpred P[set] means
  ex R being strict RelStr st $1 = R & the carrier of R in FinSETS;
  thus for X1,X2 being set st
   (for x being set holds x in X1 iff P[x]) &
   (for x being set holds x in X2 iff P[x]) holds X1 = X2 from XBOOLE_0:sch 3;
  end;
end;

registration
   cluster fin_RelStr -> non empty;
   correctness
   proof
     reconsider R = {}[:{},{}:] as Relation of {} by RELSET_1:25;
          RelStr(#{},R#) in fin_RelStr
      proof
        set X = RelStr(#{},R#);
            the carrier of X in FinSETS by CLASSES1:5,CLASSES2:def 2;
        hence thesis by Def4;
      end;
     hence thesis;
    end;
  end;

definition
  func fin_RelStr_sp -> Subset of fin_RelStr means
:Def5: (for R be strict RelStr st the carrier of R is non empty trivial
        & the carrier of R in FinSETS holds R in it) &
      (for H1,H2 be strict RelStr st
       (the carrier of H1) misses (the carrier of H2) & H1 in it & H2 in it
            holds union_of(H1,H2) in it & sum_of(H1,H2) in it)
        &
      for M be Subset of fin_RelStr st
      ( (for R be strict RelStr st the carrier of R is non empty trivial &
         the carrier of R in FinSETS holds R in M) &
       for H1,H2 be strict RelStr st
       (the carrier of H1) misses (the carrier of H2) & H1 in M & H2 in M
            holds union_of(H1,H2) in M & sum_of(H1,H2) in M )
      holds it c= M;
 existence
  proof
   defpred P[set] means
     (for R being strict RelStr
           st the carrier of R is non empty trivial
           & the carrier of R in FinSETS holds
              R in $1 ) & (for H1,H2 be strict RelStr st
       (the carrier of H1) misses (the carrier of H2) & H1 in $1 & H2 in $1
            holds union_of(H1,H2) in $1 & sum_of(H1,H2) in $1);
   consider X being set such that A1: for x being set
            holds x in X iff x in bool fin_RelStr & P[x] from XBOOLE_0:sch 1;
         for x being set holds x in X implies x in bool fin_RelStr by A1;
     then X c= bool fin_RelStr by TARSKI:def 3;
   then reconsider X as Subset-Family of fin_RelStr by SETFAM_1:def 7;
   take IT = Intersect X;
   A2: fin_RelStr in bool fin_RelStr by ZFMISC_1:def 1;
       P[fin_RelStr]
    proof
     set A = fin_RelStr;
          for H1,H2 be strict RelStr st
         (the carrier of H1) misses (the carrier of H2) & H1 in A & H2 in A
          holds union_of(H1,H2) in A & sum_of(H1,H2) in A
          proof
            let H1,H2 be strict RelStr such that
                  (the carrier of H1) misses (the carrier of H2) and
            A3: H1 in A and
            A4: H2 in A;
            consider S1 being strict RelStr such that A5: S1 = H1 and
            A6: the carrier of S1 in FinSETS by A3,Def4;
            consider S2 being strict RelStr such that A7: S2 = H2 and
            A8: the carrier of S2 in FinSETS by A4,Def4;
            A9: (the carrier of H1) \/ (the carrier of H2)
                       in FinSETS by A5,A6,A7,A8,CLASSES2:66;
            reconsider RS = union_of(S1,S2) as strict RelStr;
                  the carrier of RS in FinSETS by A5,A7,A9,Def2;
            hence union_of(H1,H2) in A by A5,A7,Def4;
            reconsider RS' = sum_of(H1,H2) as strict RelStr;
                  the carrier of RS' in FinSETS by A9,Def3;
            hence thesis by Def4;
          end;
     hence thesis by Def4;
    end;
then A10: X <> {} by A1,A2;
then A11: IT = meet X by SETFAM_1:def 10;
        P[IT] & for X be Subset of fin_RelStr st P[X] holds
    IT c= X
    proof
    thus P[IT]
     proof
     A12: for R being strict RelStr st the carrier of R is non empty trivial
          & the carrier of R in FinSETS holds R in IT
          proof
           let R being strict RelStr such that
           A13: the carrier of R is non empty trivial and
           A14: the carrier of R in FinSETS;
                 for Y being set holds Y in X implies R in Y by A1,A13,A14
;
           hence thesis by A10,A11,SETFAM_1:def 1;
          end;
         for H1,H2 be strict RelStr st
         (the carrier of H1) misses (the carrier of H2) & H1 in IT & H2 in IT
         holds union_of(H1,H2) in IT & sum_of(H1,H2) in IT
          proof
           let H1,H2 be strict RelStr such that
           A15: (the carrier of H1) misses (the carrier of H2) and
           A16: H1 in IT and
           A17: H2 in IT;
           A18: for Y being set holds Y in X implies union_of(H1,H2) in Y
               proof
                 let Y being set; assume A19: Y in X;
                 then A20: H1 in Y by A11,A16,SETFAM_1:def 1;
                       H2 in Y by A11,A17,A19,SETFAM_1:def 1;
                 hence thesis by A1,A15,A19,A20;
               end;
                sum_of(H1,H2) in IT
            proof
                  for Y being set holds Y in X implies sum_of(H1,H2) in Y
               proof
                 let Y being set; assume A21: Y in X;
             then A22: H1 in Y by A11,A16,SETFAM_1:def 1;
                  H2 in Y by A11,A17,A21,SETFAM_1:def 1;
                hence thesis by A1,A15,A21,A22;
               end;
             hence thesis by A10,A11,SETFAM_1:def 1;
            end;
           hence thesis by A10,A11,A18,SETFAM_1:def 1;
          end;
       hence thesis by A12;
     end;
     let Y be Subset of fin_RelStr such that A23: P[Y];
          IT c= Y
      proof
       let x be set such that A24: x in IT;
             Y in X by A1,A23;
       hence thesis by A11,A24,SETFAM_1:def 1;
      end;
     hence thesis;
    end;
   hence thesis;
  end;
 uniqueness
   proof
     let X1,X2 be Subset of fin_RelStr;
     assume that
A25: (for R be strict RelStr st the carrier of R is non empty trivial &
         the carrier of R in FinSETS
        holds R in X1) and
A26: for H1,H2 be strict RelStr st
       (the carrier of H1) misses (the carrier of H2) & H1 in X1 & H2 in X1
            holds union_of(H1,H2) in X1 & sum_of(H1,H2) in X1
                    and
A27: for M be Subset of fin_RelStr st
      ( (for R be strict RelStr st the carrier of R is non empty trivial
         & the carrier of R in FinSETS
         holds R in M) &
       for H1,H2 be strict RelStr st
       (the carrier of H1) misses (the carrier of H2) & H1 in M & H2 in M
            holds union_of(H1,H2) in M & sum_of(H1,H2) in M )
      holds X1 c= M
            and
A28: (for R be strict RelStr st the carrier of R is non empty trivial &
          the carrier of R in FinSETS
        holds R in X2) and
A29: for H1,H2 be strict RelStr st
       (the carrier of H1) misses (the carrier of H2) & H1 in X2 & H2 in X2
            holds union_of(H1,H2) in X2 & sum_of(H1,H2) in X2
                    and
A30: for M be Subset of fin_RelStr st
      ( (for R be strict RelStr st the carrier of R is non empty trivial &
         the carrier of R in FinSETS
         holds R in M) &
       for H1,H2 be strict RelStr st
       (the carrier of H1) misses (the carrier of H2) & H1 in M & H2 in M
            holds union_of(H1,H2) in M & sum_of(H1,H2) in M )
      holds X2 c= M;
           X1 c= X2 & X2 c= X1 by A25,A26,A27,A28,A29,A30;
     hence thesis by XBOOLE_0:def 10;
   end;
end;

registration
   cluster fin_RelStr_sp -> non empty;
   correctness
    proof
      reconsider R = [:{0},{0}:] as Relation of {0} by RELSET_1:def 1;
           RelStr(#{0},R#) in fin_RelStr_sp
       proof
         set X = RelStr(#{0},R#);
         A1: the carrier of X is non empty trivial by REALSET1:def 4;
               0 in Tarski-Class {} by CLASSES2:62;
          then the carrier of X in FinSETS by CLASSES2:63,def 2;
         hence thesis by A1,Def5;
       end;
      hence thesis;
    end;
end;

theorem Th4:
  for X being set st X in fin_RelStr_sp holds
  X is finite strict non empty RelStr
 proof
   let X be set; assume A1: X in fin_RelStr_sp;
   then consider R being strict RelStr such that A2: X = R &
   the carrier of R in FinSETS by Def4;
   reconsider R=X as strict RelStr by A2;
   A3: now
     assume A4: R is empty;
     set M = fin_RelStr_sp \ {R},
         F = fin_RelStr_sp;
     reconsider M as Subset of fin_RelStr;
     A5:
     now let S be strict RelStr; assume
       A6: the carrier of S is non empty trivial &
       the carrier of S in FinSETS;
       then A7: S in F by Def5;
             S <> R by A4,A6,STRUCT_0:def 1;
        then not S in {R} by TARSKI:def 1;
       hence S in M by A7,XBOOLE_0:def 4;
      end;
          now let H1,H2 be strict RelStr;
       assume that
       A8: (the carrier of H1) misses (the carrier of H2) and
       A9: H1 in M and A10: H2 in M;
             H1 in F & H2 in F by A9,A10,XBOOLE_0:def 4;
       then A11: union_of(H1,H2) in F & sum_of(H1,H2) in F by A8,Def5;
        A12: not H1 in {R} by A9,XBOOLE_0:def 4;
            the carrier of H1 <> {}
        proof
          per cases by A12,TARSKI:def 1;
          suppose (the carrier of H1) <> (the carrier of R);
          hence thesis by A4,STRUCT_0:def 1;
          end;
          suppose
          A13: (the InternalRel of H1) <> (the InternalRel of R);
          set cR = the carrier of R,
              Inter_H1 = the InternalRel of H1,
              cH1 = the carrier of H1;
                cR is empty by A4,STRUCT_0:def 1;
           then Inter_H1 <> {} by A13,RELSET_1:26;
          then consider x being set such that
          A14:x in Inter_H1 by XBOOLE_0:def 1;
                ex a,b being set st x=[a,b] &
          a in cH1 & b in cH1 by A14,RELSET_1:6;
          hence thesis;
        end;
        end;
        then reconsider A = (the carrier of H1) as non empty set;
             A \/ (the carrier of H2) <> {};
        then the carrier of union_of(H1,H2) <> {} by Def2;
        then union_of(H1,H2) <> R by A4,STRUCT_0:def 1;
        then not union_of(H1,H2) in {R} by TARSKI:def 1;
       hence union_of(H1,H2) in M by A11,XBOOLE_0:def 4;
             the carrier of sum_of(H1,H2) =
       A \/ (the carrier of H2) by Def3;
        then sum_of(H1,H2) <> R by A4,STRUCT_0:def 1;
        then not sum_of(H1,H2) in {R} by TARSKI:def 1;
       hence sum_of(H1,H2) in M by A11,XBOOLE_0:def 4;
     end;
      then A15: F c= M by A5,Def5;
           R in {R} by TARSKI:def 1;
     hence contradiction by A1,A15,XBOOLE_0:def 4;
   end;
         the carrier of R is finite by A2,Th3;
   hence thesis by A3,GROUP_1:def 14;
 end;

theorem
      for R being RelStr st R in fin_RelStr_sp holds
   (the carrier of R) in FinSETS
 proof
   let R be RelStr such that A1: R in fin_RelStr_sp;
   consider S being strict RelStr such that A2: R = S &
   the carrier of S in FinSETS by A1,Def4;
   thus thesis by A2;
 end;

theorem Th6:
  for X being set st X in fin_RelStr_sp holds
  X is strict non empty trivial RelStr or
  ex H1,H2 being strict RelStr st
    (the carrier of H1) misses (the carrier of H2) &
    H1 in fin_RelStr_sp & H2 in fin_RelStr_sp &
    (X = union_of(H1,H2) or X = sum_of(H1,H2) )
  proof
    set Y = fin_RelStr_sp;
    let X be set such that A1: X in fin_RelStr_sp;
    assume A2: not X is strict non empty trivial RelStr;
    deffunc F(set,set) = $2 \/ {x where x is Element of fin_RelStr_sp:
    ex R1,R2 being strict RelStr st
    (the carrier of R1) misses (the carrier of R2) &
    R1 in $2 & R2 in $2 &
    (x = union_of(R1,R2) or x = sum_of(R1,R2) )};
    consider f being Function such that A3:dom f = NAT and
    A4:  f.0 = {x where x is Element of fin_RelStr_sp: x is trivial RelStr} and
    A5: for n being Element of NAT holds f.(n+1) = F(n,f.n) from RECDEF_1:sch 3
;
    A6: for i being Nat holds f.i c= f.(i+1)
     proof
       let i being Nat;
       let x be set such that A7: x in f.i;
          f.(i+1) = f.i \/ {b where b is Element of fin_RelStr_sp:
          ex R1,R2 being strict RelStr st
          (the carrier of R1) misses (the carrier of R2) &
          R1 in f.i & R2 in f.i &
          (b = union_of(R1,R2) or b = sum_of(R1,R2))} by A5;
       hence thesis by A7,XBOOLE_0:def 2;
     end;
    A8: for i,j being Nat st i <= j holds f.i c= f.j
       proof
         let i,j being Nat; assume i <= j;
         then consider k being Nat such that A9: j=i+k by NAT_1:28;
         defpred P[Nat] means
                f.i c= f.(i+$1);
         A10: P[0];
         A11: for k being Nat st P[k] holds P[k+1]
            proof
              let k being Nat such that A12: P[k];
                    f.(i+k) c= f.(i+k+1) by A6;
              hence thesis by A12,XBOOLE_1:1;
            end;
               for k being Nat holds P[k] from NAT_1:sch 1(A10,A11);
         hence thesis by A9;
       end;
    A13: Union f c= fin_RelStr_sp
     proof
       let y being set;
       assume y in Union f;
       then consider x being set such that
       A14: x in dom f and A15: y in f.x by CARD_5:10;
       defpred P[Nat] means f.$1 c= fin_RelStr_sp;
       A16: P[0]
        proof
          let y be set;
          assume y in f.0;
           then ex a being Element of fin_RelStr_sp st y = a &
          a is trivial RelStr by A4;
          hence thesis;
        end;
       A17: for k being Nat st P[k] holds P[k+1]
        proof
          let k being Nat such that A18: P[k];
          A19: f.(k+1) = f.k \/ {a where a is Element of fin_RelStr_sp:
          ex R1,R2 being strict RelStr st
          (the carrier of R1) misses (the carrier of R2) &
          R1 in f.k & R2 in f.k &
          (a = union_of(R1,R2) or a = sum_of(R1,R2))} by A5;
              {a where a is Element of fin_RelStr_sp:
          ex R1,R2 being strict RelStr st
          (the carrier of R1) misses (the carrier of R2) &
          R1 in f.k & R2 in f.k &
          (a = union_of(R1,R2) or a = sum_of(R1,R2))} c= fin_RelStr_sp
            proof
              set S = {a where a is Element of fin_RelStr_sp:
                ex R1,R2 being strict RelStr st
                (the carrier of R1) misses (the carrier of R2) &
                R1 in f.k & R2 in f.k &
                (a = union_of(R1,R2) or a = sum_of(R1,R2))};
              let x be set; assume x in S;
              then consider a being Element of fin_RelStr_sp such that
              A20: x = a & ex R1,R2 being strict RelStr st
              (the carrier of R1) misses (the carrier of R2) &
              R1 in f.k & R2 in f.k &
              (a = union_of(R1,R2) or a = sum_of(R1,R2));
              thus thesis by A20;
            end;
          hence thesis by A18,A19,XBOOLE_1:8;
        end;
             for k being Nat holds P[k] from NAT_1:sch 1(A16,A17);
         then f.x c= fin_RelStr_sp by A3,A14;
      hence thesis by A15;
     end;
     then reconsider M = Union f as Subset of fin_RelStr by XBOOLE_1:1;
     A21: for R be strict RelStr st the carrier of R is non empty trivial &
         the carrier of R in FinSETS holds R in M
         proof
           let R be strict RelStr such that
           A22: the carrier of R is non empty trivial and
           A23: the carrier of R in FinSETS;
           A24: R is Element of fin_RelStr_sp by A22,A23,Def5;
                 R is trivial RelStr by A22,REALSET2:def 5;
           then A25: R in f.0 by A4,A24;
                f.0 c= M
            proof
              let x be set; assume x in f.0;
              hence thesis by A3,CARD_5:10;
            end;
           hence thesis by A25;
         end;
          for H1,H2 be strict RelStr st
       (the carrier of H1) misses (the carrier of H2) & H1 in M & H2 in M
            holds union_of(H1,H2) in M & sum_of(H1,H2) in M
      proof
        let H1,H2 be strict RelStr such that
        A26: (the carrier of H1) misses (the carrier of H2) and
        A27: H1 in M and A28: H2 in M;
        consider x1 being set such that
        A29: x1 in dom f and A30: H1 in f.x1 by A27,CARD_5:10;
        consider x2 being set such that
        A31: x2 in dom f and A32: H2 in f.x2 by A28,CARD_5:10;
        A33: x1 is Nat by A3,A29;
             x2 is Nat by A3,A31;
        then reconsider x1,x2 as real number by A33;
        A34: max(x1,x2) is Nat by A3,A29,A31,FINSEQ_2:1;
         then A35: (max(x1,x2) + 1) in dom f by A3,AXIOMS:28;
              x1 <= max(x1,x2) by SQUARE_1:46;
          then A36: f.x1 c= f.max(x1,x2) by A3,A8,A29,A34;
              x2 <= max(x1,x2) by SQUARE_1:46;
          then A37: f.x2 c= f.max(x1,x2) by A3,A8,A31,A34;
        set W = {a where a is Element of fin_RelStr_sp:
           ex R1,R2 being strict RelStr st
           (the carrier of R1) misses (the carrier of R2) &
           R1 in f.max(x1,x2) & R2 in f.max(x1,x2) &
           (a = union_of(R1,R2) or a = sum_of(R1,R2))};
        A38: f.(max(x1,x2)+1) = f.max(x1,x2) \/ W by A5,A34;
              union_of(H1,H2) in fin_RelStr_sp by A13,A26,A27,A28,Def5;
          then union_of(H1,H2) in W by A26,A30,A32,A36,A37;
          then A39: union_of(H1,H2) in f.(max(x1,x2) + 1) by A38,XBOOLE_0:def 2
;
              sum_of(H1,H2) in fin_RelStr_sp by A13,A26,A27,A28,Def5;
          then sum_of(H1,H2) in W by A26,A30,A32,A36,A37;
          then sum_of(H1,H2) in f.(max(x1,x2)+1) by A38,XBOOLE_0:def 2;
        hence thesis by A35,A39,CARD_5:10;
      end;
      then A40: fin_RelStr_sp c= M by A21,Def5;
     then A41: Union f = fin_RelStr_sp by A13,XBOOLE_0:def 10;
         ex H1,H2 being strict RelStr st
    (the carrier of H1) misses (the carrier of H2) &
    H1 in Y & H2 in Y & ( X = union_of(H1,H2) or X = sum_of(H1,H2) )
     proof
        consider y being set such that
       A42: y in dom f and A43: X in f.y by A1,A40,CARD_5:10
;
       defpred P[Nat] means X in f.$1 implies
       ex H1,H2 being strict RelStr st
       (the carrier of H1) misses (the carrier of H2) &
       H1 in Y & H2 in Y & ( X = union_of(H1,H2) or X = sum_of(H1,H2) );
       A44: P[0]
        proof
         assume X in f.0;
         then consider a being Element of fin_RelStr_sp such that A45: X = a
and
         A46: a is trivial RelStr by A4;
              ex R being strict RelStr st a = R & the carrier of R in FinSETS
         by Def4;
          then reconsider a as RelStr;
         thus thesis by A2,A45,A46,Th4;
        end;
       A47: for k being Nat st P[k] holds P[k+1]
        proof
          let n be Nat such that A48: P[n];
          set W = {a where a is Element of fin_RelStr_sp:
          ex R1,R2 being strict RelStr st
          (the carrier of R1) misses (the carrier of R2) &
          R1 in f.n & R2 in f.n &
          (a = union_of(R1,R2) or a = sum_of(R1,R2))};
          A49: f.(n+1) = f.n \/ W by A5;
          assume A50: X in f.(n+1);
          per cases by A49,A50,XBOOLE_0:def 2;
          suppose X in f.n;
          hence thesis by A48;
          end;
          suppose X in W;
           then consider a being Element of fin_RelStr_sp such that
          A51: a = X and A52:
          ex R1,R2 being strict RelStr st
          (the carrier of R1) misses (the carrier of R2) &
          R1 in f.n & R2 in f.n &
          (a = union_of(R1,R2) or a = sum_of(R1,R2));
          consider R1,R2 being strict RelStr such that A53:
          (the carrier of R1) misses (the carrier of R2) and
          A54: R1 in f.n & R2 in f.n &
          (X = union_of(R1,R2) or X = sum_of(R1,R2)) by A51,A52;
                R1 in Y & R2 in Y by A3,A41,A54,CARD_5:10;
          hence thesis by A53,A54;
        end;
        end;
             for n being Nat holds P[n] from NAT_1:sch 1(A44,A47);
        hence thesis by A3,A42,A43;
     end;
    hence thesis;
  end;

Lm1: the carrier of Necklace 4 = {0,1,2,3} by NECKLACE:2,21;

theorem
  for R being strict non empty RelStr st
      R in fin_RelStr_sp holds R is N-free
  proof
    set N_4 = Necklace 4;
    let R being strict non empty RelStr; assume
    A1: R in fin_RelStr_sp;
    then consider S being strict RelStr such that A2: R = S and
    A3: the carrier of S in FinSETS by Def4;
    assume A4: R embeds N_4;
    defpred P[Nat] means ex S being strict non empty RelStr st
    S in fin_RelStr_sp &
    Card (the carrier of S) = $1 & S embeds N_4;
    reconsider C = the carrier of R as Element of FinSETS by A2,A3;
    set k = card C;
    A5: ex i being Nat st P[i]
    proof
            P[k] by A1,A4;
      hence thesis;
    end;
    A6: for m being Nat st m <> 0 & P[m] ex n being Nat st n < m & P[n]
    proof
      let m be Nat such that m <> 0 and A7: P[m];
      consider S being non empty strict RelStr such that
      A8: S in fin_RelStr_sp and A9: Card (the carrier of S) = m and
      A10: S embeds N_4 by A7;
      per cases by A8,Th6;
      suppose A11: S is strict non empty trivial RelStr;
       consider f being map of N_4,S such that A12: f is one-to-one
       & for x,y being Element of N_4 holds
       [x,y] in the InternalRel of N_4 iff
       [f.x,f.y] in the InternalRel of S by A10,NECKLACE:def 2;
       A13: the carrier of N_4 = {0,1,2,3} by NECKLACE:2,21;
        then A14: 0 in the carrier of N_4 by ENUMSET1:19;
       A15: 1 in the carrier of N_4 by A13,ENUMSET1:19;
             (0 = 0+1 or 1 = 0 +1);
         then [0,1] in the InternalRel of N_4 by A14,A15,NECKLACE:26;
        then A16: [f.0,f.1] in the InternalRel of S by A12,A14,A15;
       then A17: f.0 in dom (the InternalRel of S) &
       f.1 in rng (the InternalRel of S) by RELAT_1:20;
             dom (the InternalRel of S ) c= (the carrier of S) &
       rng (the InternalRel of S) c= (the carrier of S) by RELSET_1:12;
         then f.0 = f.1 by A11,A17,REALSET2:def 7;
       then [0,0] in (the InternalRel of N_4) by A12,A14,A16;
        then [0,0] = [0,1] or [0,0]=[1,0] or [0,0]=[1,2] or [0,0]=[2,1] or
       [0,0]=[2,3] or [0,0]=[3,2] by Th2,ENUMSET1:28;
      hence thesis by ZFMISC_1:33;
      end;
      suppose ex H1,H2 being strict RelStr st
        the carrier of H1 misses the carrier of H2 &
        H1 in fin_RelStr_sp & H2 in fin_RelStr_sp &
        (S = union_of(H1,H2) or S = sum_of(H1,H2));
         then consider H1,H2 being strict RelStr such that
        A18: the carrier of H1 misses the carrier of H2 and
        A19: H1 in fin_RelStr_sp & H2 in fin_RelStr_sp and
        A20: (S = union_of(H1,H2) or S = sum_of(H1,H2));
        A21: now assume A22: S = union_of(H1,H2);
            consider f being map of N_4,S such that A23: f is one-to-one
           and A24: for x,y being Element of N_4 holds
           [x,y] in the InternalRel of N_4 iff
           [f.x,f.y] in the InternalRel of S by A10,NECKLACE:def 2;
             the carrier of N_4 = {0,1,2,3} by NECKLACE:2,21;
            then A25: 0 in the carrier of N_4 &
           1 in the carrier of N_4 & 2 in the carrier of N_4 &
           3 in the carrier of N_4 by ENUMSET1:19;
           A26: [0,1] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A27: [1,0] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A28: [1,2] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A29: [2,1] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A30: [2,3] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A31: [3,2] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A32: dom (the InternalRel of S) c= (the carrier of S) &
           rng (the InternalRel of S) c= (the carrier of S) by RELSET_1:12;
           A33: [f.0,f.1] in the InternalRel of S by A24,A25,A26;
           A34: [f.1,f.0] in the InternalRel of S by A24,A25,A27;
                 f.0 in dom (the InternalRel of S) &
           f.1 in rng (the InternalRel of S) by A33,RELAT_1:20;
            then A35: f.0 in (the carrier of S) & f.1 in (the carrier of S)
                    by A32;
           A36: [f.1,f.2] in the InternalRel of S by A24,A25,A28;
           A37: [f.2,f.1] in the InternalRel of S by A24,A25,A29;
           A38: [f.2,f.3] in the InternalRel of S by A24,A25,A30;
           A39: [f.3,f.2] in the InternalRel of S by A24,A25,A31;
            A40: f.0 in (the carrier of H1) \/ (the carrier of H2) by A22,A35,
Def2;
           per cases by A40,XBOOLE_0:def 2;
           suppose A41: f.0 in the carrier of H1;
             A42: dom (the InternalRel of H1) c= (the carrier of H1)
             & rng (the InternalRel of H1) c= (the carrier of H1)
                   by RELSET_1:12;
             A43: dom (the InternalRel of H2) c= (the carrier of H2)
             & rng (the InternalRel of H2) c= (the carrier of H2)
                    by RELSET_1:12;
              A44: [f.0,f.1] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A33,Def2;
              A45: [f.1,f.0] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A34,Def2;
             A46: [f.0,f.1] in the InternalRel of H1
               proof
                      now assume [f.0,f.1] in the InternalRel of H2;
                    then f.0 in dom (the InternalRel of H2) &
                   f.1 in rng (the InternalRel of H2) by RELAT_1:20;
                   hence contradiction by A18,A41,A43,XBOOLE_0:3;
                   end;
                 hence thesis by A44,XBOOLE_0:def 2;
               end;
             A47: [f.1,f.0] in the InternalRel of H1
               proof
                      now assume [f.1,f.0] in the InternalRel of H2;
                    then f.1 in dom (the InternalRel of H2) &
                   f.0 in rng (the InternalRel of H2) by RELAT_1:20;
                   hence contradiction by A18,A41,A43,XBOOLE_0:3;
                 end;
                 hence thesis by A45,XBOOLE_0:def 2;
               end;
              A48: f.1 in rng (the InternalRel of H1) by A46,RELAT_1:20;
              A49: [f.1,f.2] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A36,Def2;
              A50: [f.2,f.1] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A37,Def2;
             A51: [f.1,f.2] in the InternalRel of H1
               proof
                      now assume [f.1,f.2] in the InternalRel of H2;
                     then f.1 in dom (the InternalRel of H2) &
                   f.2 in rng (the InternalRel of H2) by RELAT_1:20;
                   hence contradiction by A18,A42,A43,A48,XBOOLE_0:3;
                 end;
                hence thesis by A49,XBOOLE_0:def 2;
               end;
             A52: [f.2,f.1] in the InternalRel of H1
               proof
                      now assume [f.2,f.1] in the InternalRel of H2;
                     then f.2 in dom (the InternalRel of H2) &
                   f.1 in rng (the InternalRel of H2) by RELAT_1:20;
                   hence contradiction by A18,A42,A43,A48,XBOOLE_0:3;
                 end;
                 hence thesis by A50,XBOOLE_0:def 2;
               end;
              A53: f.2 in rng (the InternalRel of H1) by A51,RELAT_1:20;
              A54: [f.2,f.3] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A38,Def2;
              A55: [f.3,f.2] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A39,Def2;
             A56: [f.2,f.3] in the InternalRel of H1
               proof
                      now assume [f.2,f.3] in the InternalRel of H2;
                     then f.2 in dom (the InternalRel of H2) &
                   f.3 in rng (the InternalRel of H2) by RELAT_1:20;
                   hence contradiction by A18,A42,A43,A53,XBOOLE_0:3;
                 end;
                hence thesis by A54,XBOOLE_0:def 2;
               end;
             A57: [f.3,f.2] in the InternalRel of H1
               proof
                      now assume [f.3,f.2] in the InternalRel of H2;
                     then f.3 in dom (the InternalRel of H2) &
                   f.2 in rng (the InternalRel of H2) by RELAT_1:20;
                   hence contradiction by A18,A42,A43,A53,XBOOLE_0:3;
                 end;
                hence thesis by A55,XBOOLE_0:def 2;
               end;
              A58: f.3 in rng (the InternalRel of H1) by A56,RELAT_1:20;
                   H2 is non empty by A19,Th4;
             then A59: the carrier of H2 is non empty by STRUCT_0:def 1;
             A60: dom f = the carrier of N_4 by FUNCT_2:def 1;
                  rng f c= the carrier of H1
               proof
                 let y be set;
                 assume y in rng f;
                  then consider x being set such that A61: x in dom f &
                 y = f.x by FUNCT_1:def 5;
                 per cases by A60,A61,Lm1,ENUMSET1:18;
                 suppose x = 0;
                 hence thesis by A41,A61;
                 end;
                 suppose x = 1;
                 hence thesis by A42,A48,A61;
                 end;
                 suppose x = 2;
                 hence thesis by A42,A53,A61;
                 end;
                 suppose x = 3;
                 hence thesis by A42,A58,A61;
               end;
               end;
                then A62: f is Function of the carrier of N_4,the carrier of H1
                  by FUNCT_2:8;
             A63: H1 embeds N_4
              proof
                   for x,y being Element of N_4
               holds [x,y] in the InternalRel of N_4
               iff [f.x,f.y] in the InternalRel of H1
                proof
                 let x,y being Element of N_4;
                 thus [x,y] in the InternalRel of N_4 implies
                 [f.x,f.y] in the InternalRel of H1
                 proof
                  assume A64:[x,y] in the InternalRel of N_4;
                  per cases by A64,Th2,ENUMSET1:28;
                  suppose [x,y] = [0,1];
                     then x = 0 & y = 1 by ZFMISC_1:33;::as1
                  hence thesis by A46;
                  end;
                  suppose [x,y] = [1,0];
                     then x = 1 & y = 0 by ZFMISC_1:33;
                  hence thesis by A47;
                  end;
                  suppose [x,y] = [1,2];
                     then x = 1 & y = 2 by ZFMISC_1:33;
                  hence thesis by A51;
                  end;
                  suppose [x,y] = [2,1];
                     then x = 2 & y = 1 by ZFMISC_1:33;
                  hence thesis by A52;
                  end;
                  suppose [x,y] = [2,3];
                     then x = 2 & y = 3 by ZFMISC_1:33;
                  hence thesis by A56;
                  end;
                  suppose [x,y] = [3,2];
                     then x = 3 & y = 2 by ZFMISC_1:33;
                  hence thesis by A57;
                 end;
                 end;
                 thus [f.x,f.y] in the InternalRel of H1 implies
                 [x,y] in the InternalRel of N_4
                  proof
                   assume A65: [f.x,f.y] in the InternalRel of H1;
                         [f.x,f.y] in the InternalRel of S implies
                   [x,y] in the InternalRel of N_4 by A24;
                    then [f.x,f.y] in (the InternalRel of H1) \/
                   (the InternalRel of H2) implies
                   [x,y] in the InternalRel of N_4 by A22,Def2;
                   hence thesis by A65,XBOOLE_0:def 2;
                  end;
                end;
               hence thesis by A23,A62,NECKLACE:def 2;
              end;
            A66: H1 is strict non empty RelStr by A19,Th4;
            set cS = the carrier of S,
                cH1 = the carrier of H1,
                cH2 = the carrier of H2;
                  H1 is finite by A19,Th4;
            then reconsider cH1 as finite set by GROUP_1:def 14;
                  H2 is finite by A19,Th4;
            then reconsider cH2 as finite set by GROUP_1:def 14;
                  cS = cH1 \/ cH2 by A22,Def2;

            then reconsider cS as finite set;
            A67: cH1 <> cS
             proof
              assume A68: cH1 = cS;
              A69: cS = cH1 \/ cH2 by A22,Def2;
              consider x being set such that A70: x in cH2
                by A59,XBOOLE_0:def 1;
                    cH1 /\ cH2 = {} by A18,XBOOLE_0:def 7;
              then not x in cH1 by A70,XBOOLE_0:def 3;
              hence contradiction by A68,A69,A70,XBOOLE_0:def 2;
             end;
                  cS = cH1 \/ cH2 by A22,Def2;
            then cH1 c= cS by XBOOLE_1:7;
             then cH1 c< cS by A67,XBOOLE_0:def 8;
             then card cH1 < card cS by CARD_2:67;
           hence thesis by A9,A19,A63,A66;
           end;
           suppose A71: f.0 in the carrier of H2;
             A72: dom (the InternalRel of H2) c= (the carrier of H2)
             & rng (the InternalRel of H2) c= (the carrier of H2)
                   by RELSET_1:12;
             A73: dom (the InternalRel of H1) c= (the carrier of H1)
             & rng (the InternalRel of H1) c= (the carrier of H1)
                    by RELSET_1:12;
              A74: [f.0,f.1] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A33,Def2;
              A75: [f.1,f.0] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A34,Def2;
             A76: [f.0,f.1] in the InternalRel of H2
               proof
                      now assume [f.0,f.1] in the InternalRel of H1;
                    then f.0 in dom (the InternalRel of H1) &
                   f.1 in rng (the InternalRel of H1) by RELAT_1:20;
                   hence contradiction by A18,A71,A73,XBOOLE_0:3;
                   end;
                 hence thesis by A74,XBOOLE_0:def 2;
               end;
             A77: [f.1,f.0] in the InternalRel of H2
               proof
                      now assume [f.1,f.0] in the InternalRel of H1;
                    then f.1 in dom (the InternalRel of H1) &
                   f.0 in rng (the InternalRel of H1) by RELAT_1:20;
                   hence contradiction by A18,A71,A73,XBOOLE_0:3;
                 end;
                 hence thesis by A75,XBOOLE_0:def 2;
               end;
              A78: f.1 in rng (the InternalRel of H2) by A76,RELAT_1:20;
              A79: [f.1,f.2] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A36,Def2;
              A80: [f.2,f.1] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A37,Def2;
             A81: [f.1,f.2] in the InternalRel of H2
               proof
                      now assume [f.1,f.2] in the InternalRel of H1;
                     then f.1 in dom (the InternalRel of H1) &
                   f.2 in rng (the InternalRel of H1) by RELAT_1:20;
                   hence contradiction by A18,A72,A73,A78,XBOOLE_0:3;
                 end;
                hence thesis by A79,XBOOLE_0:def 2;
               end;
             A82: [f.2,f.1] in the InternalRel of H2
               proof
                      now assume [f.2,f.1] in the InternalRel of H1;
                     then f.2 in dom (the InternalRel of H1) &
                   f.1 in rng (the InternalRel of H1) by RELAT_1:20;
                   hence contradiction by A18,A72,A73,A78,XBOOLE_0:3;
                 end;
                 hence thesis by A80,XBOOLE_0:def 2;
               end;
              A83: f.2 in rng (the InternalRel of H2) by A81,RELAT_1:20;
              A84: [f.2,f.3] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A38,Def2;
              A85: [f.3,f.2] in (the InternalRel of H1) \/
             (the InternalRel of H2) by A22,A39,Def2;
             A86: [f.2,f.3] in the InternalRel of H2
               proof
                      now assume [f.2,f.3] in the InternalRel of H1;
                     then f.2 in dom (the InternalRel of H1) &
                   f.3 in rng (the InternalRel of H1) by RELAT_1:20;
                   hence contradiction by A18,A72,A73,A83,XBOOLE_0:3;
                 end;
                hence thesis by A84,XBOOLE_0:def 2;
               end;
             A87: [f.3,f.2] in the InternalRel of H2
               proof
                      now assume [f.3,f.2] in the InternalRel of H1;
                     then f.3 in dom (the InternalRel of H1) &
                   f.2 in rng (the InternalRel of H1) by RELAT_1:20;
                   hence contradiction by A18,A72,A73,A83,XBOOLE_0:3;
                 end;
                hence thesis by A85,XBOOLE_0:def 2;
               end;
              A88: f.3 in rng (the InternalRel of H2) by A86,RELAT_1:20;
                   H1 is non empty by A19,Th4;
             then A89: the carrier of H1 is non empty by STRUCT_0:def 1;
             A90: dom f = the carrier of N_4 by FUNCT_2:def 1;
                  rng f c= the carrier of H2
               proof
                 let y be set;
                 assume y in rng f;
                  then consider x being set such that A91: x in dom f &
                 y = f.x by FUNCT_1:def 5;
                 per cases by A90,A91,Lm1,ENUMSET1:18;
                 suppose x = 0;
                 hence thesis by A71,A91;
                 end;
                 suppose x = 1;
                 hence thesis by A72,A78,A91;
                 end;
                 suppose x = 2;
                 hence thesis by A72,A83,A91;
                 end;
                 suppose x = 3;
                 hence thesis by A72,A88,A91;
               end;
               end;
                then A92: f is Function of the carrier of N_4,the carrier of H2
                  by FUNCT_2:8;
             A93: H2 embeds N_4
              proof
                   for x,y being Element of N_4
               holds [x,y] in the InternalRel of N_4
               iff [f.x,f.y] in the InternalRel of H2
                proof
                 let x,y being Element of N_4;
                 thus [x,y] in the InternalRel of N_4 implies
                 [f.x,f.y] in the InternalRel of H2
                 proof
                  assume A94:[x,y] in the InternalRel of N_4;
                  per cases by A94,Th2,ENUMSET1:28;
                  suppose [x,y] = [0,1];
                     then x = 0 & y = 1 by ZFMISC_1:33;
                  hence thesis by A76;
                  end;
                  suppose [x,y] = [1,0];
                     then x = 1 & y = 0 by ZFMISC_1:33;
                  hence thesis by A77;
                  end;
                  suppose [x,y] = [1,2];
                     then x = 1 & y = 2 by ZFMISC_1:33;
                  hence thesis by A81;
                  end;
                  suppose [x,y] = [2,1];
                     then x = 2 & y = 1 by ZFMISC_1:33;
                  hence thesis by A82;
                  end;
                  suppose [x,y] = [2,3];
                     then x = 2 & y = 3 by ZFMISC_1:33;
                  hence thesis by A86;
                  end;
                  suppose [x,y] = [3,2];
                     then x = 3 & y = 2 by ZFMISC_1:33;
                  hence thesis by A87;
                 end;
                 end;
                 thus [f.x,f.y] in the InternalRel of H2 implies
                 [x,y] in the InternalRel of N_4
                  proof
                   assume A95: [f.x,f.y] in the InternalRel of H2;
                         [f.x,f.y] in the InternalRel of S implies
                   [x,y] in the InternalRel of N_4 by A24;
                    then [f.x,f.y] in (the InternalRel of H1) \/
                   (the InternalRel of H2) implies
                   [x,y] in the InternalRel of N_4 by A22,Def2;
                   hence thesis by A95,XBOOLE_0:def 2;
                  end;
                end;
               hence thesis by A23,A92,NECKLACE:def 2;
              end;
            A96: H2 is strict non empty RelStr by A19,Th4;
            set cS = the carrier of S,
                cH1 = the carrier of H1,
                cH2 = the carrier of H2;
                  H1 is finite by A19,Th4;
            then reconsider cH1 as finite set by GROUP_1:def 14;
                  H2 is finite by A19,Th4;
            then reconsider cH2 as finite set by GROUP_1:def 14;
                  cS = cH1 \/ cH2 by A22,Def2;
            then reconsider cS as finite set;
            A97: cH2 <> cS
             proof
              assume A98: cH2 = cS;
              A99: cS = cH1 \/ cH2 by A22,Def2;
              consider x being set such that A100: x in cH1
                by A89,XBOOLE_0:def 1;
                    cH1 /\ cH2 = {} by A18,XBOOLE_0:def 7;
              then not x in cH2 by A100,XBOOLE_0:def 3;
              hence contradiction by A98,A99,A100,XBOOLE_0:def 2;
             end;
                  cS = cH1 \/ cH2 by A22,Def2;
            then cH2 c= cS by XBOOLE_1:7;
             then cH2 c< cS by A97,XBOOLE_0:def 8;
             then card cH2 < card cS by CARD_2:67;
          hence thesis by A9,A19,A93,A96;
         end;
         end;
              now assume A101: S = sum_of(H1,H2);
           consider f being map of N_4,S such that A102: f is one-to-one
           and A103: for x,y being Element of N_4 holds
           [x,y] in the InternalRel of N_4 iff
           [f.x,f.y] in the InternalRel of S by A10,NECKLACE:def 2;
              the carrier of N_4 = {0,1,2,3} by NECKLACE:2,21;
            then A104: 0 in the carrier of N_4 &
           1 in the carrier of N_4 & 2 in the carrier of N_4 &
           3 in the carrier of N_4 by ENUMSET1:19;
            set A = the InternalRel of H1,
                B = the InternalRel of H2,
                C = [:the carrier of H1,the carrier of H2:],
                D = [:the carrier of H2, the carrier of H1:],
                cH1 = the carrier of H1,
                cH2 = the carrier of H2,
                cS = the carrier of S;
           A105: [0,1] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A106: [1,0] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A107: [1,2] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A108: [2,1] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A109: [2,3] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A110: [3,2] in the InternalRel of N_4 by Th2,ENUMSET1:29;
           A111: dom (the InternalRel of S) c= cS &
           rng (the InternalRel of S) c= cS by RELSET_1:12;
           A112: [f.0,f.1] in the InternalRel of S by A103,A104,A105;
           A113: [f.1,f.0] in the InternalRel of S by A103,A104,A106;
                 f.0 in dom (the InternalRel of S) &
           f.1 in rng (the InternalRel of S) by A112,RELAT_1:20;
            then A114: f.0 in cS & f.1 in cS by A111;
             then A115: f.1 in cH1 \/ cH2 by A101,Def3;
           A116: [f.1,f.2] in the InternalRel of S by A103,A104,A107;
            then f.1 in dom (the InternalRel of S) &
           f.2 in rng (the InternalRel of S) by RELAT_1:20;
             then f.1 in cS & f.2 in cS by A111;
             then A117: f.2 in cH1 \/ cH2 by A101,Def3;
           A118: [f.2,f.1] in the InternalRel of S by A103,A104,A108;
           A119: [f.2,f.3] in the InternalRel of S by A103,A104,A109;
            then f.2 in dom (the InternalRel of S) &
           f.3 in rng (the InternalRel of S) by RELAT_1:20;
             then f.3 in cS by A111;
             then A120: f.3 in cH1 \/ cH2 by A101,Def3;
           A121: [f.3,f.2] in the InternalRel of S by A103,A104,A110;
            A122: f.0 in cH1 \/ cH2 by A101,A114,Def3;
            A123: dom B c= cH2 & rng B c= cH2 by RELSET_1:12;
            A124: dom A c= cH1 & rng A c= cH1 by RELSET_1:12;
            A125: not [0,2] in the InternalRel of N_4
             proof
              assume A126: [0,2] in the InternalRel of N_4;
               per cases by A126,Th2,ENUMSET1:28;
               suppose [0,2] = [0,1];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,2] = [1,2];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,2] = [2,3];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,2] = [3,2];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,2] = [2,1];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,2] = [1,0];
               hence contradiction by ZFMISC_1:33;
             end;
             end;
            A127: not [0,3] in the InternalRel of N_4
             proof
               assume A128: [0,3] in the InternalRel of N_4;
               per cases by A128,Th2,ENUMSET1:28;
               suppose [0,3] = [0,1];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,3] = [1,2];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,3] = [2,3];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,3] = [3,2];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,3] = [2,1];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [0,3] = [1,0];
               hence contradiction by ZFMISC_1:33;
             end;
             end;
            A129: not [1,3] in the InternalRel of N_4
             proof
               assume A130: [1,3] in the InternalRel of N_4;
               per cases by A130,Th2,ENUMSET1:28;
               suppose [1,3] = [0,1];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [1,3] = [1,2];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [1,3] = [2,3];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [1,3] = [3,2];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [1,3] = [2,1];
               hence contradiction by ZFMISC_1:33;
               end;
               suppose [1,3] = [1,0];
               hence contradiction by ZFMISC_1:33;
             end;
             end;
           A131: A c= (the InternalRel of S) & B c= (the InternalRel of S) &
           C c= (the InternalRel of S) & D c= (the InternalRel of S)
           proof
            A132: A c= (the InternalRel of S)
             proof
               let x be set; assume x in A;
               then A133: x in A \/ B by XBOOLE_0:def 2;
                     (A \/ B) c= (A \/ B) \/ C by XBOOLE_1:7;
                then A134: x in (A \/ B) \/ C by A133;
                     ((A \/ B) \/ C) c= ((A \/ B) \/ C) \/ D by XBOOLE_1:7;
                then ((A \/ B) \/ C) c= the InternalRel of S by A101,Def3
;
               hence thesis by A134;
             end;
            A135: B c= (the InternalRel of S)
             proof
               let x be set; assume x in B;
               then A136: x in A \/ B by XBOOLE_0:def 2;
                     (A \/ B) c= (A \/ B) \/ C by XBOOLE_1:7;
                then A137: x in (A \/ B) \/ C by A136;
                     ((A \/ B) \/ C) c= ((A \/ B) \/ C) \/ D by XBOOLE_1:7;
                then ((A \/ B) \/ C) c= the InternalRel of S by A101,Def3
;
               hence thesis by A137;
             end;
            A138: C c= (the InternalRel of S)
             proof
               let x be set; assume x in C;
                then A139: x in (A \/ B) \/ C by XBOOLE_0:def 2;
                     ((A \/ B) \/ C) c= ((A \/ B) \/ C) \/ D by XBOOLE_1:7;
                then ((A \/ B) \/ C) c= the InternalRel of S by A101,Def3
;
               hence thesis by A139;
             end;
                 D c= (the InternalRel of S)
             proof
               let x be set; assume x in D;
                then A140: x in (B \/ C) \/ D by XBOOLE_0:def 2;
                     ((B \/ C) \/ D) c= A \/ ((B \/ C) \/ D) by XBOOLE_1:7;
                then ((B \/ C) \/ D) c= A \/ (B \/ (C \/ D)) by XBOOLE_1:4;
                then ((B \/ C) \/ D) c= (A \/ B) \/ (C \/ D) by XBOOLE_1:4;
                then ((B \/ C) \/ D) c= ((A \/ B) \/ C) \/ D by XBOOLE_1:4;
                then ((B \/ C) \/ D) c= the InternalRel of S by A101,Def3
;
               hence thesis by A140;
             end;
             hence thesis by A132,A135,A138;
            end;
           per cases by A122,XBOOLE_0:def 2;
           suppose A141: f.0 in cH1;
            A142: f.2 in cH1
             proof
                    now assume f.2 in cH2;
                  then [f.0,f.2] in C by A141,ZFMISC_1:106;
                 hence contradiction by A103,A104,A125,A131;
                end;
                hence thesis by A117,XBOOLE_0:def 2;
             end;
            A143: f.3 in cH1
             proof
                    now assume f.3 in cH2;
                  then [f.0,f.3] in C by A141,ZFMISC_1:106;
                 hence contradiction by A103,A104,A127,A131;
                end;
               hence thesis by A120,XBOOLE_0:def 2;
             end;
            A144: f.1 in cH1
             proof
                    now assume f.1 in cH2;
                  then [f.1,f.3] in D by A143,ZFMISC_1:106;
                 hence contradiction by A103,A104,A129,A131;
                end;
               hence thesis by A115,XBOOLE_0:def 2;
             end;
            set x1=[f.0,f.1], x2=[f.1,f.2], x3=[f.2,f.3],
                x4=[f.1,f.0], x5=[f.2,f.1], x6=[f.3,f.2];
                  x1 in (A \/ B \/ C \/ D) by A101,A112,Def3;
             then x1 in ((A \/ B) \/ C ) or x1 in D by XBOOLE_0:def 2;
             then A145: x1 in (A \/ B) or x1 in C or x1 in D by XBOOLE_0:def 2;
            A146: [f.0,f.1] in A
             proof
             A147: now assume x1 in B;
                then f.0 in dom B & f.1 in rng B by RELAT_1:20;
               hence contradiction by A18,A123,A141,XBOOLE_0:3;
              end;
             A148: now assume x1 in C;
                  then f.0 in cH1 & f.1 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A144,XBOOLE_0:3;
                end;
                  now assume x1 in D;
                  then f.0 in cH2 & f.1 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A141,XBOOLE_0:3;
                end;
              hence thesis by A145,A147,A148,XBOOLE_0:def 2;
             end;
                  x4 in (A \/ B \/ C \/ D) by A101,A113,Def3;
             then x4 in ((A\/ B) \/ C) or x4 in D by XBOOLE_0:def 2;
             then A149: x4 in (A \/ B) or x4 in C or x4 in D by XBOOLE_0:def 2;
            A150: [f.1,f.0] in A
             proof
               A151: now assume x4 in B;
                 then f.1 in dom B & f.0 in rng B by RELAT_1:20;
                hence contradiction by A18,A123,A141,XBOOLE_0:3;
               end;
               A152: now assume x4 in C;
                 then f.1 in cH1 & f.0 in cH2 by ZFMISC_1:106;
                hence contradiction by A18,A141,XBOOLE_0:3;
               end;
                    now assume x4 in D;
                 then f.1 in cH2 & f.0 in cH1 by ZFMISC_1:106;
                hence contradiction by A18,A144,XBOOLE_0:3;
               end;
              hence thesis by A149,A151,A152,XBOOLE_0:def 2;
             end;
                  x2 in (A \/ B \/ C \/ D) by A101,A116,Def3;
             then x2 in ((A\/ B) \/ C) or x2 in D by XBOOLE_0:def 2;
             then A153: x2 in (A \/ B) or x2 in C or x2 in D by XBOOLE_0:def 2;
            A154: [f.1,f.2] in A
             proof
               A155: now assume x2 in B;
                  then f.1 in dom B & f.2 in rng B by RELAT_1:20;
                 hence contradiction by A18,A123,A144,XBOOLE_0:3;
                end;
               A156: now assume x2 in C;
                  then f.1 in cH1 & f.2 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A142,XBOOLE_0:3;
                end;
                    now assume x2 in D;
                  then f.1 in cH2 & f.2 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A144,XBOOLE_0:3;
                end;
               hence thesis by A153,A155,A156,XBOOLE_0:def 2;
             end;
                  x5 in (A \/ B \/ C \/ D) by A101,A118,Def3;
             then x5 in ((A\/ B) \/ C) or x5 in D by XBOOLE_0:def 2;
             then A157: x5 in (A \/ B) or x5 in C or x5 in D by XBOOLE_0:def 2;
            A158: [f.2,f.1] in A
             proof
               A159: now assume x5 in B;
                  then f.2 in dom B & f.1 in rng B by RELAT_1:20;
                 hence contradiction by A18,A123,A144,XBOOLE_0:3;
                end;
               A160: now assume x5 in C;
                  then f.2 in cH1 & f.1 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A144,XBOOLE_0:3;
                end;
                    now assume x5 in D;
                  then f.2 in cH2 & f.1 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A142,XBOOLE_0:3;
                end;
              hence thesis by A157,A159,A160,XBOOLE_0:def 2;
             end;
                  x3 in (A \/ B \/ C \/ D) by A101,A119,Def3;
             then x3 in ((A\/ B) \/ C) or x3 in D by XBOOLE_0:def 2;
             then A161: x3 in (A \/ B) or x3 in C or x3 in D by XBOOLE_0:def 2;
            A162: [f.2,f.3] in A
             proof
               A163: now assume x3 in B;
                  then f.2 in dom B & f.3 in rng B by RELAT_1:20;
                 hence contradiction by A18,A123,A142,XBOOLE_0:3;
                end;
               A164: now assume x3 in C;
                  then f.2 in cH1 & f.3 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A143,XBOOLE_0:3;
                end;
                    now assume x3 in D;
                  then f.2 in cH2 & f.3 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A142,XBOOLE_0:3;
                end;
               hence thesis by A161,A163,A164,XBOOLE_0:def 2;
             end;
                  x6 in (A \/ B \/ C \/ D) by A101,A121,Def3;
             then x6 in ((A\/ B) \/ C) or x6 in D by XBOOLE_0:def 2;
             then A165: x6 in (A \/ B) or x6 in C or x6 in D by XBOOLE_0:def 2;
            A166: [f.3,f.2] in A
             proof
               A167: now assume x6 in B;
                  then f.3 in dom B & f.2 in rng B by RELAT_1:20;
                 hence contradiction by A18,A123,A142,XBOOLE_0:3;
                end;
               A168: now assume x6 in C;
                  then f.3 in cH1 & f.2 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A142,XBOOLE_0:3;
                end;
                    now assume x6 in D;
                  then f.3 in cH2 & f.2 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A143,XBOOLE_0:3;
                end;
               hence thesis by A165,A167,A168,XBOOLE_0:def 2;
             end;
                  H2 is non empty by A19,Th4;
            then A169: the carrier of H2 is non empty by STRUCT_0:def 1;
            A170: dom f = the carrier of N_4 by FUNCT_2:def 1;
                 rng f c= the carrier of H1
             proof
              let y be set;
              assume y in rng f;
               then consider x being set such that A171: x in dom f &
              y = f.x by FUNCT_1:def 5;
              per cases by A170,A171,Lm1,ENUMSET1:18;
              suppose x = 0;
              hence thesis by A141,A171;
              end;
              suppose x = 1;
              hence thesis by A144,A171;
              end;
              suppose x = 2;
              hence thesis by A142,A171;
              end;
              suppose x = 3;
              hence thesis by A143,A171;
             end;
             end;
               then A172: f is Function of the carrier of N_4,cH1 by FUNCT_2:8;
            A173: H1 embeds N_4
              proof
                   for x,y being Element of N_4
               holds [x,y] in the InternalRel of N_4
               iff [f.x,f.y] in the InternalRel of H1
                proof
                 let x,y being Element of N_4;
                 thus [x,y] in the InternalRel of N_4 implies
                 [f.x,f.y] in the InternalRel of H1
                 proof
                  assume A174:[x,y] in the InternalRel of N_4;
                  per cases by A174,Th2,ENUMSET1:28;
                  suppose [x,y] = [0,1];
                     then x = 0 & y = 1 by ZFMISC_1:33;
                  hence thesis by A146;
                  end;
                  suppose [x,y] = [1,0];
                     then x = 1 & y = 0 by ZFMISC_1:33;
                  hence thesis by A150;
                  end;
                  suppose [x,y] = [1,2];
                     then x = 1 & y = 2 by ZFMISC_1:33;
                  hence thesis by A154;
                  end;
                  suppose [x,y] = [2,1];
                     then x = 2 & y = 1 by ZFMISC_1:33;
                  hence thesis by A158;
                  end;
                  suppose [x,y] = [2,3];
                     then x = 2 & y = 3 by ZFMISC_1:33;
                  hence thesis by A162;
                  end;
                  suppose [x,y] = [3,2];
                     then x = 3 & y = 2 by ZFMISC_1:33;
                  hence thesis by A166;
                 end;
                 end;
                 thus [f.x,f.y] in the InternalRel of H1 implies
                 [x,y] in the InternalRel of N_4
                  proof
                   assume A175: [f.x,f.y] in the InternalRel of H1;
                         [f.x,f.y] in the InternalRel of S implies
                   [x,y] in the InternalRel of N_4 by A103;
                    then [f.x,f.y] in (((A \/ B) \/ C) \/ D) implies
                   [x,y] in the InternalRel of N_4 by A101,Def3;
                    then [f.x,f.y] in ((A \/ B) \/ (C\/ D)) implies
                   [x,y] in the InternalRel of N_4 by XBOOLE_1:4;
                    then [f.x,f.y] in (A \/ (B\/(C\/D))) implies
                   [x,y] in the InternalRel of N_4 by XBOOLE_1:4;
                   hence thesis by A175,XBOOLE_0:def 2;
                  end;
                end;
               hence thesis by A102,A172,NECKLACE:def 2;
              end;
            A176: H1 is strict non empty RelStr by A19,Th4;
                  H1 is finite by A19,Th4;
            then reconsider cH1 as finite set by GROUP_1:def 14;
                  H2 is finite by A19,Th4;
            then reconsider cH2 as finite set by GROUP_1:def 14;
                  cS = cH1 \/ cH2 by A101,Def3;
            then reconsider cS as finite set;
            A177: cH1 <> cS
             proof
              assume A178: cH1 = cS;
              A179: cS = cH1 \/ cH2 by A101,Def3;
              consider x being set such that A180: x in cH2
                by A169,XBOOLE_0:def 1;
                    cH1 /\ cH2 = {} by A18,XBOOLE_0:def 7;
              then not x in cH1 by A180,XBOOLE_0:def 3;
              hence contradiction by A178,A179,A180,XBOOLE_0:def 2;
             end;
                  cS = cH1 \/ cH2 by A101,Def3;
            then cH1 c= cS by XBOOLE_1:7;
             then cH1 c< cS by A177,XBOOLE_0:def 8;
             then card cH1 < card cS by CARD_2:67;
           hence thesis by A9,A19,A173,A176;
           end;
           suppose A181: f.0 in the carrier of H2;
            A182: f.2 in cH2
             proof
                    now assume f.2 in cH1;
                  then [f.0,f.2] in D by A181,ZFMISC_1:106;
                 hence contradiction by A103,A104,A125,A131;
                end;
                hence thesis by A117,XBOOLE_0:def 2;
             end;
            A183: f.3 in cH2
             proof
                    now assume f.3 in cH1;
                  then [f.0,f.3] in D by A181,ZFMISC_1:106;
                 hence contradiction by A103,A104,A127,A131;
                end;
               hence thesis by A120,XBOOLE_0:def 2;
             end;
            A184: f.1 in cH2
             proof
                    now assume f.1 in cH1;
                  then [f.1,f.3] in C by A183,ZFMISC_1:106;
                 hence contradiction by A103,A104,A129,A131;
                end;
               hence thesis by A115,XBOOLE_0:def 2;
             end;
            set x1=[f.0,f.1], x2=[f.1,f.2], x3=[f.2,f.3],
                x4=[f.1,f.0], x5=[f.2,f.1], x6=[f.3,f.2];
                  x1 in (A \/ B \/ C \/ D) by A101,A112,Def3;
             then x1 in ((A \/ B) \/ C ) or x1 in D by XBOOLE_0:def 2;
             then A185: x1 in (A \/ B) or x1 in C or x1 in D by XBOOLE_0:def 2;
            A186: [f.0,f.1] in B
             proof
             A187: now assume x1 in A;
                then f.0 in dom A & f.1 in rng A by RELAT_1:20;
               hence contradiction by A18,A124,A181,XBOOLE_0:3;
              end;
             A188: now assume x1 in C;
                  then f.0 in cH1 & f.1 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A181,XBOOLE_0:3;
                end;
                  now assume x1 in D;
                  then f.0 in cH2 & f.1 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A184,XBOOLE_0:3;
                end;
              hence thesis by A185,A187,A188,XBOOLE_0:def 2;
             end;
                  x4 in (A \/ B \/ C \/ D) by A101,A113,Def3;
             then x4 in ((A\/ B) \/ C) or x4 in D by XBOOLE_0:def 2;
             then A189: x4 in (A \/ B) or x4 in C or x4 in D by XBOOLE_0:def 2;
            A190: [f.1,f.0] in B
             proof
               A191: now assume x4 in A;
                 then f.1 in dom A & f.0 in rng A by RELAT_1:20;
                hence contradiction by A18,A124,A184,XBOOLE_0:3;
               end;
               A192: now assume x4 in C;
                 then f.1 in cH1 & f.0 in cH2 by ZFMISC_1:106;
                hence contradiction by A18,A184,XBOOLE_0:3;
               end;
                    now assume x4 in D;
                 then f.1 in cH2 & f.0 in cH1 by ZFMISC_1:106;
                hence contradiction by A18,A181,XBOOLE_0:3;
               end;
              hence thesis by A189,A191,A192,XBOOLE_0:def 2;
             end;
                  x2 in (A \/ B \/ C \/ D) by A101,A116,Def3;
             then x2 in ((A\/ B) \/ C) or x2 in D by XBOOLE_0:def 2;
             then A193: x2 in (A \/ B) or x2 in C or x2 in D by XBOOLE_0:def 2;
            A194: [f.1,f.2] in B
             proof
               A195: now assume x2 in A;
                  then f.1 in dom A & f.2 in rng A by RELAT_1:20;
                 hence contradiction by A18,A124,A182,XBOOLE_0:3;
                end;
               A196: now assume x2 in C;
                  then f.1 in cH1 & f.2 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A184,XBOOLE_0:3;
                end;
                    now assume x2 in D;
                  then f.1 in cH2 & f.2 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A182,XBOOLE_0:3;
                end;
               hence thesis by A193,A195,A196,XBOOLE_0:def 2;
             end;
                  x5 in (A \/ B \/ C \/ D) by A101,A118,Def3;
             then x5 in ((A\/ B) \/ C) or x5 in D by XBOOLE_0:def 2;
             then A197: x5 in (A \/ B) or x5 in C or x5 in D by XBOOLE_0:def 2;
            A198: [f.2,f.1] in B
             proof
               A199: now assume x5 in A;
                  then f.2 in dom A & f.1 in rng A by RELAT_1:20;
                 hence contradiction by A18,A124,A184,XBOOLE_0:3;
                end;
               A200: now assume x5 in C;
                  then f.2 in cH1 & f.1 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A182,XBOOLE_0:3;
                end;
                    now assume x5 in D;
                  then f.2 in cH2 & f.1 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A184,XBOOLE_0:3;
                end;
              hence thesis by A197,A199,A200,XBOOLE_0:def 2;
             end;
                  x3 in (A \/ B \/ C \/ D) by A101,A119,Def3;
             then x3 in ((A\/ B) \/ C) or x3 in D by XBOOLE_0:def 2;
             then A201: x3 in (A \/ B) or x3 in C or x3 in D by XBOOLE_0:def 2;
            A202: [f.2,f.3] in B
             proof
               A203: now assume x3 in A;
                  then f.2 in dom A & f.3 in rng A by RELAT_1:20;
                 hence contradiction by A18,A124,A182,XBOOLE_0:3;
                end;
               A204: now assume x3 in C;
                  then f.2 in cH1 & f.3 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A182,XBOOLE_0:3;
                end;
                    now assume x3 in D;
                  then f.2 in cH2 & f.3 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A183,XBOOLE_0:3;
                end;
               hence thesis by A201,A203,A204,XBOOLE_0:def 2;
             end;
                  x6 in (A \/ B \/ C \/ D) by A101,A121,Def3;
             then x6 in ((A\/ B) \/ C) or x6 in D by XBOOLE_0:def 2;
             then A205: x6 in (A \/ B) or x6 in C or x6 in D by XBOOLE_0:def 2;
            A206: [f.3,f.2] in B
             proof
               A207: now assume x6 in A;
                  then f.3 in dom A & f.2 in rng A by RELAT_1:20;
                 hence contradiction by A18,A124,A182,XBOOLE_0:3;
                end;
               A208: now assume x6 in C;
                  then f.3 in cH1 & f.2 in cH2 by ZFMISC_1:106;
                 hence contradiction by A18,A183,XBOOLE_0:3;
                end;
                    now assume x6 in D;
                  then f.3 in cH2 & f.2 in cH1 by ZFMISC_1:106;
                 hence contradiction by A18,A182,XBOOLE_0:3;
                end;
               hence thesis by A205,A207,A208,XBOOLE_0:def 2;
             end;
                  H1 is non empty by A19,Th4;
            then A209: the carrier of H1 is non empty by STRUCT_0:def 1;
            A210: dom f = the carrier of N_4 by FUNCT_2:def 1;
                 rng f c= the carrier of H2
             proof
              let y be set;
              assume y in rng f;
               then consider x being set such that A211: x in dom f &
              y = f.x by FUNCT_1:def 5;
              per cases by A210,A211,Lm1,ENUMSET1:18;
              suppose x = 0;
              hence thesis by A181,A211;
              end;
              suppose x = 1;
              hence thesis by A184,A211;
              end;
              suppose x = 2;
              hence thesis by A182,A211;
              end;
              suppose x = 3;
              hence thesis by A183,A211;
             end;
             end;
               then A212: f is Function of the carrier of N_4,cH2 by FUNCT_2:8;
            A213: H2 embeds N_4
              proof
                   for x,y being Element of N_4
               holds [x,y] in the InternalRel of N_4
               iff [f.x,f.y] in the InternalRel of H2
                proof
                 let x,y being Element of N_4;
                 thus [x,y] in the InternalRel of N_4 implies
                 [f.x,f.y] in the InternalRel of H2
                 proof
                  assume A214:[x,y] in the InternalRel of N_4;
                  per cases by A214,Th2,ENUMSET1:28;
                  suppose [x,y] = [0,1];
                     then x = 0 & y = 1 by ZFMISC_1:33;
                  hence thesis by A186;
                  end;
                  suppose [x,y] = [1,0];
                     then x = 1 & y = 0 by ZFMISC_1:33;
                  hence thesis by A190;
                  end;
                  suppose [x,y] = [1,2];
                     then x = 1 & y = 2 by ZFMISC_1:33;
                  hence thesis by A194;
                  end;
                  suppose [x,y] = [2,1];
                     then x = 2 & y = 1 by ZFMISC_1:33;
                  hence thesis by A198;
                  end;
                  suppose [x,y] = [2,3];
                     then x = 2 & y = 3 by ZFMISC_1:33;
                  hence thesis by A202;
                  end;
                  suppose [x,y] = [3,2];
                     then x = 3 & y = 2 by ZFMISC_1:33;
                  hence thesis by A206;
                 end;
                 end;
                 thus [f.x,f.y] in the InternalRel of H2 implies
                 [x,y] in the InternalRel of N_4
                  proof
                   assume A215: [f.x,f.y] in the InternalRel of H2;
                         [f.x,f.y] in the InternalRel of S implies
                   [x,y] in the InternalRel of N_4 by A103;
                    then [f.x,f.y] in (((A \/ B) \/ C) \/ D) implies
                   [x,y] in the InternalRel of N_4 by A101,Def3;
                    then [f.x,f.y] in ((A \/ B) \/ (C\/ D)) implies
                   [x,y] in the InternalRel of N_4 by XBOOLE_1:4;
                    then [f.x,f.y] in (B \/ (A\/(C\/D))) implies
                   [x,y] in the InternalRel of N_4 by XBOOLE_1:4;
                   hence thesis by A215,XBOOLE_0:def 2;
                  end;
                end;
               hence thesis by A102,A212,NECKLACE:def 2;
              end;
            A216: H2 is strict non empty RelStr by A19,Th4;
                  H1 is finite by A19,Th4;
            then reconsider cH1 as finite set by GROUP_1:def 14;
                  H2 is finite by A19,Th4;
            then reconsider cH2 as finite set by GROUP_1:def 14;
                  cS = cH1 \/ cH2 by A101,Def3;
            then reconsider cS as finite set;
            A217: cH2 <> cS
             proof
              assume A218: cH2 = cS;
              A219: cS = cH1 \/ cH2 by A101,Def3;
              consider x being set such that A220: x in cH1
                by A209,XBOOLE_0:def 1;
                    cH1 /\ cH2 = {} by A18,XBOOLE_0:def 7;
              then not x in cH2 by A220,XBOOLE_0:def 3;
              hence contradiction by A218,A219,A220,XBOOLE_0:def 2;
             end;
                  cS = cH1 \/ cH2 by A101,Def3;
            then cH2 c= cS by XBOOLE_1:7;
             then cH2 c< cS by A217,XBOOLE_0:def 8;
             then card cH2 < card cS by CARD_2:67;
           hence thesis by A9,A19,A213,A216;
         end;
         end;
       hence thesis by A20,A21;
    end;
    end;
          P[0] from NAT_1:sch 7(A5,A6);
    then consider S being strict non empty RelStr such that
          S in fin_RelStr_sp and A221: Card (the carrier of S) = 0 and
          S embeds N_4;
    thus thesis by A221,CARD_2:59;
  end;
