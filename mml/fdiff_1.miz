:: Real Function Differentiability
::  by Konrad Raczkowski and Pawe{\l} Sadowski
::
:: Received June 18, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies PRE_TOPC, SEQ_1, PARTFUN1, SEQ_2, ORDINAL2, FUNCT_1, ARYTM_3,
      RELAT_1, SEQM_3, INCSP_1, ARYTM_1, ARYTM, RCOMP_1, BOOLE, ABSVALUE,
      FCONT_1, LATTICES, FDIFF_1, FUNCOP_1, SUBSET_1,
      ANPROJ_1, OPPCAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1,
      ZFMISC_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      COMPLEX1, REAL_1, NAT_1, RELAT_1, FUNCT_2, FUNCOP_1,
      VALUED_0, VALUED_1, SEQ_1, SEQ_2,
      SEQM_3, RELSET_1, PARTFUN1, PARTFUN2, RFUNCT_2, RCOMP_1, FCONT_1,
      XXREAL_0;
 constructors PARTFUN1, REAL_1, NAT_1, COMPLEX1, SEQ_2, SEQM_3, RCOMP_1,
      PARTFUN2, RFUNCT_2, FCONT_1, VALUED_1, SEQ_1, REALSET1, FINSET_1,
      SETFAM_1;
 registrations FUNCT_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      MEMBERED, SEQM_3, RCOMP_1, XBOOLE_0, VALUED_0, VALUED_1, FUNCT_2,
      FUNCOP_1, SEQ_4, REALSET1, SETFAM_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FCONT_1, XBOOLE_0, VALUED_1, RCOMP_1, SUBSET_1;
 theorems TARSKI, NAT_1, FUNCT_1, ABSVALUE, ZFMISC_1, SEQ_1, SEQ_2,
      SEQM_3, SEQ_4, PARTFUN1, PARTFUN2, RFUNCT_1, RFUNCT_2, RCOMP_1, FCONT_1,
      FUNCT_2, RELAT_1, XREAL_0, XBOOLE_0, XBOOLE_1, XCMPLX_0, XCMPLX_1,
      FUNCOP_1, XREAL_1, COMPLEX1, VALUED_1, VALUED_0, ORDINAL1;
 schemes SEQ_1;

begin

reserve y,X for set;
reserve x,x0,x1,x2,g,g1,g2,r,r1,s,p,p1,p2 for Real;
reserve n,m,k for Element of NAT;
reserve Y for Subset of REAL;
reserve Z for open Subset of REAL;
reserve s1,s3 for Real_Sequence;
reserve f,f1,f2 for PartFunc of REAL,REAL;

theorem Th1:
  (for r holds r in Y iff r in REAL) iff Y=REAL
proof
  thus (for r holds r in Y iff r in REAL) implies Y=REAL
  proof
    assume for r holds r in Y iff r in REAL;
    then for y holds y in Y iff y in REAL;
    hence thesis by TARSKI:2;
  end;
  assume
A1: Y=REAL;
  let r;
  thus r in Y implies r in REAL;
  thus thesis by A1;
end;

definition
  let IT be Real_Sequence;
  attr IT is convergent_to_0 means
  :Def1:
  IT is non-zero & IT is convergent & lim IT = 0;
end;

registration
  cluster convergent_to_0 Real_Sequence;
  existence
  proof
    deffunc F(Element of NAT) = 1/($1+1);
    consider s1 such that
A1: for n holds s1.n=F(n) from SEQ_1:sch 1;
    take s1;
    now
      let n;
      (n+1)" <> 0 by XCMPLX_1:203;
      then 1/(n+1) <> 0 by XCMPLX_1:217;
      hence s1.n <> 0 by A1;
    end;
    hence s1 is non-zero by SEQ_1:7;
    thus thesis by A1,SEQ_4:45;
  end;
end;
reconsider cs = NAT --> 0 as Real_Sequence by FUNCOP_1:57;

reserve h for convergent_to_0 Real_Sequence;
reserve c for constant Real_Sequence;

definition
  let IT be PartFunc of REAL,REAL;
  canceled;
  attr IT is REST-like means
  :Def3:
  IT is total &
  for h holds (h")(#)(IT/*h) is convergent & lim ((h")(#)(IT/*h)) = 0;
end;
reconsider cf = REAL --> 0 as Function of REAL, REAL by FUNCOP_1:57;

registration
  cluster REST-like PartFunc of REAL,REAL;
  existence
  proof
    take f = cf;
A1: dom f = REAL by FUNCOP_1:19;
    thus f is total;
    now
      let h;
A2:   now
        let n be Nat;
X:   n in NAT by ORDINAL1:def 13;
A3:     rng h c= dom f by A1; thus
        ((h")(#)(f/*h)).n = (h").n*((f/*h).n) by SEQ_1:12,X
          .= (h").n*(f.(h.n)) by A3,FUNCT_2:185,X
          .= (h").n*0 by FUNCOP_1:13
          .= 0;
      end;
      then
A4:   (h")(#)(f/*h) is constant by VALUED_0:def 18;
      ((h")(#)(f/*h)).0 = 0 by A2;
      hence (h")(#)(f/*h) is convergent & lim ((h")(#)(f/*h)) = 0
      by A4,SEQ_4:40;
    end;
    hence thesis;
  end;
end;

definition
  mode REST is REST-like PartFunc of REAL,REAL;
end;

definition
  let IT be PartFunc of REAL,REAL;
  attr IT is linear means
  :Def4:
  IT is total & ex r st for p holds IT.p = r*p;
end;

registration
  cluster linear PartFunc of REAL,REAL;
  existence
  proof
    defpred P[set] means $1 in REAL;
    deffunc F(Real) = 1*$1;
    consider f such that
A1: (for r holds r in dom f iff P[r]) &
    for r st r in dom f holds f.r=F(r) from SEQ_1:sch 3;
    take f;
    for y holds y in REAL implies y in dom f by A1;
    then REAL c= dom f by TARSKI:def 3;
    then
A2: dom f = REAL by XBOOLE_0:def 10;
    hence f is total by PARTFUN1:def 4;
    for p holds f.p = 1*p by A1,A2;
    hence thesis;
  end;
end;

definition
  mode LINEAR is linear PartFunc of REAL,REAL;
end;

reserve R,R1,R2 for REST;
reserve L,L1,L2 for LINEAR;

canceled 4;

theorem Th6:
  L1+L2 is LINEAR & L1-L2 is LINEAR
proof
  consider g1 such that
A1: for p holds L1.p = g1*p by Def4;
  consider g2 such that
A2: for p holds L2.p = g2*p by Def4;
A3: L1 is total & L2 is total by Def4;
  now
    let p;
    thus (L1+L2).p = L1.p + L2.p by A3,RFUNCT_1:72
      .= g1*p + L2.p by A1
      .= g1*p + g2*p by A2
      .= (g1+g2)*p;
  end;
  hence L1+L2 is LINEAR by A3,Def4;
  now
    let p;
    thus (L1-L2).p = L1.p - L2.p by A3,RFUNCT_1:72
      .= g1*p - L2.p by A1
      .= g1*p - g2*p by A2
      .= (g1-g2)*p;
  end;
  hence thesis by A3,Def4;
end;

theorem Th7:
  r(#)L is LINEAR
proof
  consider g such that
A1: for p holds L.p = g*p by Def4;
A2: L is total by Def4;
  now
    let p;
    thus (r(#)L).p = r*L.p by A2,RFUNCT_1:73
      .= r*(g*p) by A1
      .= r*g*p;
  end;
  hence thesis by A2,Def4;
end;

theorem Th8:
  R1+R2 is REST & R1-R2 is REST & R1(#)R2 is REST
proof
A1: R1 is total & R2 is total by Def3;
  now
    let h;
A3: (h")(#)(R1/*h) is convergent & lim ((h")(#)(R1/*h)) = 0 by Def3;
A4: (h")(#)(R2/*h) is convergent & lim ((h")(#)(R2/*h)) = 0 by Def3;
A5: (h")(#)((R1+R2)/*h) = (h")(#)((R1/*h)+(R2/*h)) by A1,RFUNCT_2:32
      .= ((h")(#)(R1/*h))+((h")(#)(R2/*h)) by SEQ_1:24;
    hence (h")(#)((R1+R2)/*h) is convergent by A3,A4,SEQ_2:19;
    thus lim ((h")(#)((R1+R2)/*h)) = 0+0 by A3,A4,A5,SEQ_2:20
      .= 0;
  end;
  hence R1+R2 is REST by A1,Def3;
  now
    let h;
A7: (h")(#)(R1/*h) is convergent & lim ((h")(#)(R1/*h)) = 0 by Def3;
A8: (h")(#)(R2/*h) is convergent & lim ((h")(#)(R2/*h)) = 0 by Def3;
A9: (h")(#)((R1-R2)/*h) = (h")(#)((R1/*h)-(R2/*h)) by A1,RFUNCT_2:32
      .= ((h")(#)(R1/*h))-((h")(#)(R2/*h)) by SEQ_1:29;
    hence (h")(#)((R1-R2)/*h) is convergent by A7,A8,SEQ_2:25;
    thus lim ((h")(#)((R1-R2)/*h)) = 0-0 by A7,A8,A9,SEQ_2:26
      .= 0;
  end;
  hence R1-R2 is REST by A1,Def3;
  now
    let h;
A11: (h")(#)(R1/*h) is convergent & lim ((h")(#)(R1/*h)) = 0 by Def3;
A12: (h")(#)(R2/*h) is convergent & lim ((h")(#)(R2/*h)) = 0 by Def3;
A13: h is non-zero & h is convergent & lim h = 0 by Def1;
    then
A14: h" is non-zero by SEQ_1:41;
A15: h(#)((h")(#)(R1/*h)) is convergent by A11,A13,SEQ_2:28;
A16: lim (h(#)((h")(#)(R1/*h))) = 0*0 by A11,A13,SEQ_2:29
      .= 0;
A17: (h")(#)((R1(#)R2)/*h) = ((R1/*h)(#)(R2/*h))/"h by A1,RFUNCT_2:32
      .= ((R1/*h)(#)(R2/*h)(#)(h"))/"(h(#)(h")) by A14,SEQ_1:51
      .= ((R1/*h)(#)(R2/*h)(#)(h"))(#)((h"")(#)(h")) by SEQ_1:44
      .= h(#)(h")(#)((R1/*h)(#)((h")(#)(R2/*h))) by SEQ_1:22
      .= h(#)(h")(#)(R1/*h)(#)((h")(#)(R2/*h)) by SEQ_1:22
      .= h(#)((h")(#)(R1/*h))(#)((h")(#)(R2/*h)) by SEQ_1:22;
    hence (h")(#)((R1(#)R2)/*h) is convergent by A12,A15,SEQ_2:28;
    thus lim ((h")(#)((R1(#)R2)/*h)) = 0*0 by A12,A15,A16,A17,SEQ_2:29
      .= 0;
  end;
  hence thesis by A1,Def3;
end;

theorem Th9:
  r(#)R is REST
proof
A1: R is total by Def3;
  now
    let h;
A3: (h")(#)(R/*h) is convergent & lim ((h")(#)(R/*h)) = 0 by Def3;
A4: (h")(#)((r(#)R)/*h) = (h")(#)(r(#)(R/*h)) by A1,RFUNCT_2:33
      .= r(#)((h")(#)(R/*h)) by SEQ_1:27;
    hence (h")(#)((r(#)R)/*h) is convergent by A3,SEQ_2:21;
    thus lim ((h")(#)((r(#)R)/*h)) = r*0 by A3,A4,SEQ_2:22
      .= 0;
  end;
  hence thesis by A1,Def3;
end;

theorem Th10:
  L1(#)L2 is REST-like
proof
A1: L1 is total by Def4;
A2: L2 is total by Def4;
  hence
    L1(#)L2 is total by A1;
  consider x1 such that
A4: for p holds L1.p=x1*p by Def4;
  consider x2 such that
A5: for p holds L2.p=x2*p by Def4;
  now
    let h;
A6: h is convergent & lim h=0 by Def1;
    now
      let n;
      h is non-zero by Def1;
      then
A7:   h.n<>0 by SEQ_1:7;
      thus ((h")(#)((L1(#)L2)/*h)).n=(h").n *((L1(#)L2)/*h).n by SEQ_1:12
        .=(h").n*(L1(#)L2).(h.n) by A1,A2,FUNCT_2:192
        .=(h").n*(L1.(h.n)*L2.(h.n)) by A1,A2,RFUNCT_1:72
        .=(h").n*L1.(h.n)*L2.(h.n)
        .=((h.n)")*L1.(h.n)*L2.(h.n) by VALUED_1:10
        .=((h.n)")*((h.n)*x1)*L2.(h.n) by A4
        .=((h.n)")*(h.n)*x1*L2.(h.n)
        .=1*x1*L2.(h.n) by A7,XCMPLX_0:def 7
        .=x1*(x2*(h.n)) by A5
        .=x1*x2*(h.n)
        .=((x1*x2)(#)h).n by SEQ_1:13;
    end;
    then
A8: (h")(#)((L1(#)L2)/*h)=(x1*x2)(#)h by FUNCT_2:113;
    hence (h")(#)((L1(#)L2)/*h) is convergent by A6,SEQ_2:21;
    thus lim ((h")(#)((L1(#)L2)/*h)) = (x1*x2)*0 by A6,A8,SEQ_2:22
      .=0;
  end;
  hence thesis;
end;

theorem Th11:
  R(#)L is REST & L(#)R is REST
proof
A1: R is total by Def3;
A2: L is total by Def4;
  consider x1 such that
A4: for p holds L.p=x1*p by Def4;
A5: now
    let h;
A6: (h")(#)((R(#)L)/*h)=(h")(#)((R/*h)(#)(L/*h)) by A1,A2,RFUNCT_2:32
      .=((h")(#)(R/*h))(#)(L/*h) by SEQ_1:22;
A7: h is convergent & lim h=0 by Def1;
    now
      let n;
      thus (L/*h).n=L.(h.n) by A2,FUNCT_2:192
        .=x1*(h.n) by A4
        .=(x1(#)h).n by SEQ_1:13;
    end;
    then
A8: (L/*h)=x1(#)h by FUNCT_2:113;
    then
A9: L/*h is convergent by A7,SEQ_2:21;
A10: lim (L/*h)=x1*0 by A7,A8,SEQ_2:22
      .=0;
A11: (h")(#)(R/*h) is convergent & lim ((h")(#)(R/*h))=0 by Def3;
    hence (h")(#)((R(#)L)/*h) is convergent by A6,A9,SEQ_2:28;
    thus lim ((h")(#)((R(#)L)/*h))=0*0 by A6,A9,A10,A11,SEQ_2:29
      .=0;
  end;
  hence R(#)L is REST by A1,A2,Def3;
  thus thesis by A1,A2,A5,Def3;
end;

definition
  let f;
  let x0 be real number;
  pred f is_differentiable_in x0 means
  :Def5:
  ex N being Neighbourhood of x0 st N c= dom f &
  ex L,R st for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0);
end;

definition
  let f;
  let x0 be real number;
  assume
A1: f is_differentiable_in x0;
  func diff(f,x0) -> Real means
  :Def6:
  ex N being Neighbourhood of x0 st N c= dom f &
  ex L,R st it=L.1 & for x st x in N holds f.x-f.x0 = L.(x-x0) + R.(x-x0);
  existence
  proof
    consider N being Neighbourhood of x0 such that
A2: N c= dom f &
ex L,R st for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by A1,Def5;
    consider L,R such that
A3: for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by A2;
    consider r such that
A4: for p holds L.p = r*p by Def4;
    take r;
    L.1=r*1 by A4
      .=r;
    hence thesis by A2,A3;
  end;
  uniqueness
  proof
    let r,s;
    assume that
A5: ex N being Neighbourhood of x0 st N c= dom f &
    ex L,R st r=L.1 & for x st x in N holds f.x-f.x0 = L.(x-x0)+R.(x-x0) and
A6: ex N being Neighbourhood of x0 st N c= dom f &
    ex L,R st s=L.1 & for x st x in N holds f.x-f.x0 = L.(x-x0) + R.(x-x0);
    consider N being Neighbourhood of x0 such that
A7: N c= dom f &
    ex L,R st r=L.1 & for x st x in N holds f.x-f.x0 = L.(x-x0)+R.(x-x0) by A5;
    consider L,R such that
A8: r=L.1 & for x st x in N holds f.x-f.x0 = L.(x-x0) + R.(x-x0) by A7;
    consider N1 being Neighbourhood of x0 such that
A9: N1 c= dom f &
    ex L,R st s=L.1 & for x st x in N1 holds f.x-f.x0=L.(x-x0)+R.(x-x0) by A6;
    consider L1,R1 such that
A10: s =L1.1 & for x st x in N1 holds f.x-f.x0 = L1.(x-x0) + R1.(x-x0) by A9;
    consider r1 such that
A11: for p holds L.p = r1*p by Def4;
    consider p1 such that
A12: for p holds L1.p = p1*p by Def4;
A13: r=r1*1 by A8,A11;
A14: s=p1*1 by A10,A12;
A15: now
      let x;
      assume
A16:  x in N & x in N1;
      then f.x-f.x0 = L.(x-x0) + R.(x-x0) by A8;
      then L.(x-x0) + R.(x-x0) = L1.(x-x0) + R1.(x-x0) by A10,A16;
      then r1*(x-x0) + R.(x-x0) = L1.(x-x0) + R1.(x-x0) by A11;
      hence r*(x-x0) + R.(x-x0) = s*(x-x0) + R1.(x-x0) by A12,A13,A14;
    end;
    consider N0 be Neighbourhood of x0 such that
A17: N0 c= N & N0 c= N1 by RCOMP_1:38;
    consider g be real number such that
A18: 0<g & N0 = ].x0-g,x0+g.[ by RCOMP_1:def 7;
    deffunc F(Element of NAT) = g/($1+2);
    consider s1 such that
A19: for n holds s1.n = F(n) from SEQ_1:sch 1;
    now
      let n;
      0<>g/(n+2) by A18,XREAL_1:141;
      hence 0<>s1.n by A19;
    end;
    then
A20: s1 is non-zero by SEQ_1:7;
    s1 is convergent & lim s1 = 0 by A19,SEQ_4:46;
    then reconsider h = s1 as convergent_to_0 Real_Sequence by A20,Def1;
A21: for n ex x st x in N & x in N1 & h.n=x-x0
    proof
      let n;
A22:  0<g/(n+2) by A18,XREAL_1:141;
      0+1<n+1+1 by XREAL_1:8;
      then g/(n+2)<g/1 by A18,XREAL_1:78;
      then
A23:  x0 + g/(n+2)<x0 + g by XREAL_1:8;
      x0+-g<x0+g/(n+2) by A22,XREAL_1:8,A18;
      then
A24:  x0+g/(n+2) in ].x0-g,x0+g.[ by A23;
      take x=x0+g/(n+2);
      thus thesis by A17,A18,A19,A24;
    end;
A25: now
      let n be Nat;
X:   n in NAT by ORDINAL1:def 13;
      then ex x st x in N & x in N1 & h.n=x-x0 by A21;
      then
A26:  r*(h.n) + R.(h.n) = s*(h.n) + R1.(h.n) by A15;
      h is non-zero by Def1;
      then
A27:  h.n <> 0 by SEQ_1:7,X;
A28:  (r*(h.n))/(h.n)+(R.(h.n))/(h.n)=(s*(h.n)+R1.(h.n))/(h.n)
      by A26,XCMPLX_1:63;
A29:  (r*(h.n))/(h.n) = r*((h.n)/(h.n)) by XCMPLX_1:75
        .= r*1 by A27,XCMPLX_1:60
        .= r;
      (s*(h.n))/(h.n) = s*((h.n)/(h.n)) by XCMPLX_1:75
        .= s*1 by A27,XCMPLX_1:60
        .= s;
      then
A30:  r + (R.(h.n))/(h.n) = s + (R1.(h.n))/(h.n) by A28,A29,XCMPLX_1:63;
      R is total by Def3;
      then dom R = REAL by PARTFUN1:def 4;
      then
A31:  rng h c= dom R;
      R1 is total by Def3;
      then dom R1 = REAL by PARTFUN1:def 4;
      then
A32:  rng h c= dom R1;
A33:  (R.(h.n))/(h.n) = (R.(h.n))*(h.n)" by XCMPLX_0:def 9
        .= (R.(h.n))*(h".n) by VALUED_1:10
        .= ((R/*h).n)*(h".n) by A31,FUNCT_2:185,X
        .= ((h")(#)(R/*h)).n by SEQ_1:12,X;
      (R1.(h.n))/(h.n) = (R1.(h.n))*(h.n)" by XCMPLX_0:def 9
        .= (R1.(h.n))*(h".n) by VALUED_1:10
        .= ((R1/*h).n)*(h".n) by A32,FUNCT_2:185,X
        .= ((h")(#)(R1/*h)).n by SEQ_1:12,X;
      then r = s + (((h")(#)(R1/*h)).n - ((h")(#)(R/*h)).n) by A30,A33;
      hence r - s = (((h")(#)(R1/*h))-((h")(#)(R/*h))).n by RFUNCT_2:6,X;
    end;
    then
A34: ((h")(#)(R1/*h))-((h")(#)(R/*h)) is constant by VALUED_0:def 18;
    (((h")(#)(R1/*h))-((h")(#)(R/*h))).1 = r-s by A25;
    then
A35: lim (((h")(#)(R1/*h))-((h")(#)(R/*h))) = r-s by A34,SEQ_4:40;
A36: (h")(#)(R/*h) is convergent & lim ((h")(#)(R/*h)) = 0 by Def3;
    (h")(#)(R1/*h) is convergent & lim ((h")(#)(R1/*h)) = 0 by Def3;
    then r-s = 0-0 by A35,A36,SEQ_2:26;
    hence thesis;
  end;
end;

definition
  let f,X;
  pred f is_differentiable_on X means
  :Def7:
  X c= dom f & for x st x in X holds f|X is_differentiable_in x;
end;

canceled 3;

theorem Th15:
  f is_differentiable_on X implies X is Subset of REAL
proof
  assume f is_differentiable_on X;
  then X c=dom f by Def7;
  hence thesis by XBOOLE_1:1;
end;

theorem Th16:
  f is_differentiable_on Z iff
  Z c= dom f & for x st x in Z holds f is_differentiable_in x
proof
  thus f is_differentiable_on Z implies Z c=dom f &
  for x st x in Z holds f is_differentiable_in x
  proof
    assume
A1: f is_differentiable_on Z;
    hence Z c=dom f by Def7;
    let x0;
    assume
A2: x0 in Z;
    then f|Z is_differentiable_in x0 by A1,Def7;
    then consider N being Neighbourhood of x0 such that
A3: N c= dom(f|Z) &
    ex L,R st for x st x in N holds (f|Z).x-(f|Z).x0=L.(x-x0)+R.(x-x0) by Def5;
    take N;
    dom(f|Z)=dom f/\Z by RELAT_1:90;
    then dom(f|Z) c=dom f by XBOOLE_1:17;
    hence N c= dom f by A3,XBOOLE_1:1;
    consider L,R such that
A4: for x st x in N holds (f|Z).x - (f|Z).x0 = L.(x-x0) + R.(x-x0) by A3;
    take L,R;
    let x;
    assume
A5: x in N;
    then (f|Z).x-(f|Z).x0=L.(x-x0)+R.(x-x0) by A4;
    then f.x-(f|Z).x0=L.(x-x0)+R.(x-x0) by A3,A5,FUNCT_1:68;
    hence thesis by A2,FUNCT_1:72;
  end;
  assume
A6: Z c=dom f & for x st x in Z holds f is_differentiable_in x;
  hence Z c=dom f;
  let x0;
  assume
A7: x0 in Z;
  then f is_differentiable_in x0 by A6;
  then consider N being Neighbourhood of x0 such that
A8: N c= dom f &
  ex L,R st for x st x in N holds f.x-f.x0=L.(x-x0)+R.(x-x0) by Def5;
  consider N1 being Neighbourhood of x0 such that
A9: N1 c= Z by A7,RCOMP_1:39;
  consider N2 being Neighbourhood of x0 such that
A10: N2 c= N1 & N2 c= N by RCOMP_1:38;
  take N2;
A11: N2 c= dom f by A8,A10,XBOOLE_1:1;
  N2 c= Z by A9,A10,XBOOLE_1:1;
  then N2 c= dom f/\Z by A11,XBOOLE_1:19;
  hence
A12: N2 c= dom(f|Z) by RELAT_1:90;
  consider L,R such that
A13: for x st x in N holds f.x-f.x0=L.(x-x0)+R.(x-x0) by A8;
  take L,R;
  let x;
  assume
A14: x in N2;
  then f.x-f.x0=L.(x-x0)+R.(x-x0) by A10,A13;
  then
A15: (f|Z).x-f.x0=L.(x-x0)+R.(x-x0) by A12,A14,FUNCT_1:68;
  x0 in N2 by RCOMP_1:37;
  hence thesis by A12,A15,FUNCT_1:68;
end;

theorem
  f is_differentiable_on Y implies Y is open
proof
  assume
A1: f is_differentiable_on Y;
  now
    let x0 be real number;
    assume x0 in Y;
    then f|Y is_differentiable_in x0 by A1,Def7;
    then consider N being Neighbourhood of x0 such that
A2: N c= dom(f|Y) &
    ex L,R st for x st x in N holds (f|Y).x-(f|Y).x0=L.(x-x0)+R.(x-x0) by Def5;
    take N;
    dom(f|Y)=dom f/\Y by RELAT_1:90;
    then dom(f|Y) c=Y by XBOOLE_1:17;
    hence N c= Y by A2,XBOOLE_1:1;
  end;
  hence thesis by RCOMP_1:41;
end;

definition
  let f,X;
  assume
A1: f is_differentiable_on X;
  func f`|X -> PartFunc of REAL,REAL means
  :Def8:
  dom it = X & for x st x in X holds it.x = diff(f,x);
  existence
  proof
    defpred P[set] means $1 in X;
    deffunc F(Real) = diff(f,$1);
    consider F being PartFunc of REAL,REAL such that
A2: (for x holds x in dom F iff P[x]) &
    for x st x in dom F holds F.x = F(x) from SEQ_1:sch 3;
    take F;
    for y st y in dom F holds y in X by A2;
    then
A3: dom F c= X by TARSKI:def 3;
    now
      let y such that
A4:   y in X;
      X is Subset of REAL by A1,Th15;
      hence y in dom F by A2,A4;
    end;
    then X c= dom F by TARSKI:def 3;
    hence dom F = X by A3,XBOOLE_0:def 10;
    now
      let x;
      assume x in X;
      then x in dom F by A2;
      hence F.x = diff(f,x) by A2;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let F,G be PartFunc of REAL,REAL;
    assume that
A5: dom F = X & for x st x in X holds F.x = diff(f,x) and
A6: dom G = X & for x st x in X holds G.x = diff(f,x);
    now
      let x;
      assume
A7:   x in dom F;
      then F.x = diff(f,x) by A5;
      hence F.x=G.x by A5,A6,A7;
    end;
    hence thesis by A5,A6,PARTFUN1:34;
  end;
end;

canceled;

theorem
  (Z c= dom f & ex r st rng f = {r}) implies
  f is_differentiable_on Z & for x st x in Z holds (f`|Z).x = 0
proof
  assume
A1: Z c= dom f;
  given r such that
A2: rng f = {r};
A3: now
    let x0;
    assume x0 in dom f;
    then f.x0 in {r} by A2,FUNCT_1:def 5;
    hence f.x0 = r by TARSKI:def 1;
  end;
  set L = cf;
  for p holds L.p = 0*p by FUNCOP_1:13;
  then reconsider L as LINEAR by Def4;
  set R = cf;
A4: dom R = REAL by FUNCOP_1:19;
  now
    let h;
A5: now
      let n be Nat;
X:   n in NAT by ORDINAL1:def 13;
A6:   rng h c= dom R by A4;
      thus ((h")(#)(R/*h)).n = (h".n)*((R/*h).n) by SEQ_1:12,X
        .= (h".n)*(R.(h.n)) by A6,FUNCT_2:185,X
        .= (h".n)*0 by FUNCOP_1:13
        .= 0;
    end;
    then
A7: (h")(#)(R/*h) is constant by VALUED_0:def 18;
    hence (h")(#)(R/*h) is convergent;
    ((h")(#)(R/*h)).0 = 0 by A5;
    hence lim ((h")(#)(R/*h)) = 0 by A7,SEQ_4:40;
  end;
  then reconsider R as REST by Def3;
A8: now
    let x0;
    assume
A9: x0 in Z;
    then consider N being Neighbourhood of x0 such that
A10: N c= Z by RCOMP_1:39;
A11: N c= dom f by A1,A10,XBOOLE_1:1;
    for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0)
    proof
      let x;
      assume x in N;
      hence f.x - f.x0 = r - f.x0 by A3,A11
        .= r - r by A1,A3,A9
        .= L.(x-x0)+0 by FUNCOP_1:13
        .= L.(x-x0)+R.(x-x0) by FUNCOP_1:13;
    end;
    hence f is_differentiable_in x0 by A11,Def5;
  end;
  hence
A12: f is_differentiable_on Z by A1,Th16;
  let x0;
  assume
A13: x0 in Z;
  then
A14: f is_differentiable_in x0 by A8;
  then ex N being Neighbourhood of x0 st N c= dom f &
  ex L,R st for x st x in N holds f.x-f.x0=L.(x-x0)+R.(x-x0) by Def5;
  then consider N being Neighbourhood of x0 such that
A15: N c= dom f;
A16: for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0)
  proof
    let x;
    assume x in N;
    hence f.x - f.x0 = r - f.x0 by A3,A15
      .=r - r by A1,A3,A13
      .=L.(x-x0) + 0 by FUNCOP_1:13
      .=L.(x-x0) + R.(x-x0) by FUNCOP_1:13;
  end;
  thus (f`|Z).x0 = diff(f,x0) by A12,A13,Def8
    .= L.1 by A14,A15,A16,Def6
    .=0 by FUNCOP_1:13;
end;

registration
  let h,n;
  cluster h^\n -> convergent_to_0;
  coherence
  proof
    h^\n is convergent_to_0
    proof
A1:   h is non-zero & h is convergent & lim h = 0 by Def1;
      hence h^\n is non-zero;
      thus thesis by A1,SEQ_4:33;
    end;
    hence thesis;
  end;
end;

theorem Th20:
  for x0 being real number for N being Neighbourhood of x0 st
  f is_differentiable_in x0 & N c= dom f holds
  for h,c st rng c = {x0} & rng (h+c) c= N holds
  h"(#)(f/*(h+c) - f/*c) is convergent &
   diff(f,x0) = lim (h"(#)(f/*(h+c) - f/*c))
proof
  let x0 be real number;
  let N be Neighbourhood of x0;
  assume
A1: f is_differentiable_in x0 & N c= dom f;
  let h,c such that
A2: rng c = {x0} & rng (h+c) c= N;
  consider N1 be Neighbourhood of x0 such that
A3: N1 c= dom f &
  ex L,R st for x st x in N1 holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by A1,Def5;
  consider L,R such that
A4: for x st x in N1 holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by A3;
  consider N2 be Neighbourhood of x0 such that
A5: N2 c= N & N2 c= N1 by RCOMP_1:38;
  consider g be real number such that
A6: 0<g & N2 = ].x0-g,x0+g.[ by RCOMP_1:def 7;
A7: x0 in N2
  proof
A8: x0 + 0 < x0 + g by A6,XREAL_1:10;
    x0 - g < x0 - 0 by A6,XREAL_1:17;
    hence thesis by A6,A8;
  end;
  ex n st rng (c^\n) c= N2 & rng ((h+c)^\n) c= N2
  proof
    x0 in rng c by A2,TARSKI:def 1;
    then
A10: lim c = x0 by SEQ_4:40;
A11: h is convergent & lim h = 0 by Def1;
    then
A12: lim (h + c) = 0 + x0 by A10,SEQ_2:20
      .= x0;
    h + c is convergent by A11,SEQ_2:19;
    then consider n such that
A13: for m being Element of NAT st n<=m holds abs((h+c).m-x0)<g
    by A6,A12,SEQ_2:def 7;
A14: rng (c^\n) = {x0} by A2,VALUED_0:26;
    take n;
    thus rng (c^\n) c= N2
    proof
      let y;
      assume y in rng (c^\n);
      hence y in N2 by A7,A14,TARSKI:def 1;
    end;
    let y;
    assume y in rng ((h+c)^\n);
    then consider m such that
A15: y = ((h+c)^\n).m by FUNCT_2:190;
    n + 0 <= n+m by XREAL_1:9;
    then abs((h+c).(n+m)-x0)<g by A13;
    then -g < (h+c).(m+n) - x0 & (h+c).(m+n) - x0 < g by SEQ_2:9;
    then -g < ((h+c)^\n).m - x0 & ((h+c)^\n).m - x0 < g by NAT_1:def 3;
    then x0 +-g < ((h+c)^\n).m & ((h+c)^\n).m < x0 + g by XREAL_1:21,22;
    hence y in N2 by A6,A15;
  end;
  then consider n such that
A16: rng (c^\n) c= N2 & rng ((h+c)^\n) c= N2;
A17: rng (c^\n) c= dom f
  proof
    let y;
A18: rng (c^\n) = rng c by VALUED_0:26;
    assume y in rng (c^\n);
    then y = x0 by A2,A18,TARSKI:def 1;
    then y in N by A5,A7;
    hence thesis by A1;
  end;
A19: rng ((h+c)^\n) c= dom f
  proof
    let y;
    assume y in rng ((h+c)^\n);
    then y in N2 by A16;
    then y in N by A5;
    hence y in dom f by A1;
  end;
A20: rng c c= dom f
  proof
    let y;
    assume y in rng c;
    then y = x0 by A2,TARSKI:def 1;
    then y in N by A5,A7;
    hence thesis by A1;
  end;
A21: rng (h+c) c= dom f
  proof
    let y;
    assume y in rng (h+c);
    then y in N by A2;
    hence y in dom f by A1;
  end;
A22: for x st x in N2 holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by A4,A5;
A23: for k holds f.(((h+c)^\n).k) - f.((c^\n).k) = L.((h^\n).k) + R.((h^\n).k)
  proof
    let k;
    ((h+c)^\n).k in rng ((h+c)^\n) by VALUED_0:28;
    then
A24: ((h+c)^\n).k in N2 by A16;
A25: ((h+c)^\n).k - (c^\n).k = (h^\n + c^\n).k - (c^\n).k by SEQM_3:37
      .= (h^\n).k + (c^\n).k - (c^\n).k by SEQ_1:11
      .= (h^\n).k;
A26: (c^\n).k in rng (c^\n) by VALUED_0:28;
    rng (c^\n) = rng c by VALUED_0:26;
    then (c^\n).k = x0 by A2,A26,TARSKI:def 1;
    hence thesis by A4,A5,A24,A25;
  end;
A27: L is total by Def4;
A28: R is total by Def3;
A29: f/*((h+c)^\n) - f/*(c^\n) = L/*(h^\n) + R/*(h^\n)
  proof
    now
      let k;
      thus (f/*((h+c)^\n)-f/*(c^\n)).k = (f/*((h+c)^\n)).k-(f/*(c^\n)).k
      by RFUNCT_2:6
        .= f.(((h+c)^\n).k) - (f/*(c^\n)).k by A19,FUNCT_2:185
        .= f.(((h+c)^\n).k) - f.((c^\n).k) by A17,FUNCT_2:185
        .= L.((h^\n).k) + R.((h^\n).k) by A23
        .= (L/*(h^\n)).k + R.((h^\n).k) by A27,FUNCT_2:192
        .= (L/*(h^\n)).k + (R/*(h^\n)).k by A28,FUNCT_2:192
        .= (L/*(h^\n) + R/*(h^\n)).k by SEQ_1:11;
    end;
    hence thesis by FUNCT_2:113;
  end;
A30: (L/*(h^\n) + R/*(h^\n))(#)(h^\n)" is convergent &
  lim ((L/*(h^\n) + R/*(h^\n))(#)(h^\n)") = L.1
  proof
    deffunc F(Element of NAT) = L.1 + ((R/*(h^\n))(#)(h^\n)").$1;
    consider s1 such that
A31: for k holds s1.k = F(k) from SEQ_1:sch 1;
    consider s such that
A32: for p1 holds L.p1=s*p1 by Def4;
A33: L.1 = s*1 by A32
      .= s;
    now
      let m;
      h^\n is non-zero by Def1;
      then
A34:  (h^\n).m <> 0 by SEQ_1:7;
      thus ((L/*(h^\n) + R/*(h^\n))(#)(h^\n)").m
      = ((L/*(h^\n) + R/*(h^\n)).m)*((h^\n)").m by SEQ_1:12
        .= ((L/*(h^\n)).m + (R/*(h^\n)).m) *((h^\n)").m by SEQ_1:11
        .= ((L/*(h^\n)).m)*((h^\n)").m+((R/*(h^\n)).m)*((h^\n)").m
        .= ((L/*(h^\n)).m)*((h^\n)").m + ((R/*(h^\n))(#)(h^\n)").m by SEQ_1:12
        .= ((L/*(h^\n)).m)*((h^\n).m)" + ((R/*(h^\n))(#)(h^\n)").m
         by VALUED_1:10
        .= (L.((h^\n).m))*((h^\n).m)" + ((R/*(h^\n))(#)
      (h^\n)").m by A27,FUNCT_2:192
        .= (s*((h^\n).m))*((h^\n).m)" + ((R/*(h^\n))(#)(h^\n)").m by A32
        .= s*(((h^\n).m)*((h^\n).m)") + ((R/*(h^\n))(#)(h^\n)").m
        .= s*1 + ((R/*(h^\n))(#)(h^\n)").m by A34,XCMPLX_0:def 7
        .= s1.m by A31,A33;
    end;
    then
A35: (L/*(h^\n) + R/*(h^\n))(#)(h^\n)" = s1 by FUNCT_2:113;
A36: now
      let r be real number such that
A37:  0<r;
      ((h^\n)")(#)(R/*(h^\n)) is convergent & lim (((h^\n)")(#)
      (R/*(h^\n)))=0 by Def3;
      then consider m such that
A38:  for k st m<=k holds abs((((h^\n)")(#)
      (R/*(h^\n))).k - 0)<r by A37,SEQ_2:def 7;
      take n1=m;
      let k such that
A39:  n1<=k;
      abs(s1.k -L.1 ) = abs(L.1 + ((R/*(h^\n))(#)(h^\n)").k - L.1 ) by A31
        .= abs( (((h^\n)")(#)(R/*(h^\n))).k - 0 );
      hence abs(s1.k -L.1 )<r by A38,A39;
    end;
    hence (L/*(h^\n) + R/*(h^\n))(#)(h^\n)" is convergent by A35,SEQ_2:def 6;
    hence thesis by A35,A36,SEQ_2:def 7;
  end;
A40: N2 c= dom f by A1,A5,XBOOLE_1:1;
A41: ((L/*(h^\n) + R/*(h^\n))(#)(h^\n)") = ((((f/*(h+c))^\n)-f/*(c^\n))(#)
  (h^\n)") by A21,A29,VALUED_0:27
    .=((((f/*(h+c))^\n)-((f/*c)^\n))(#)(h^\n)") by A20,VALUED_0:27
    .=((((f/*(h+c))-(f/*c))^\n)(#)(h^\n)") by SEQM_3:39
    .=((((f/*(h+c))-(f/*c))^\n)(#)((h")^\n)) by SEQM_3:41
    .=((((f/*(h+c))-(f/*c))(#) h")^\n) by SEQM_3:42;
  then
A42: L.1 = lim ((h")(#)((f/*(h+c)) - (f/*c))) by A30,SEQ_4:36;
  thus h" (#) (f/*(h+c) - f/*c) is convergent by A30,A41,SEQ_4:35;
  thus diff(f,x0) = lim (h" (#) (f/*(h+c) - f/*c)) by A1,A22,A40,A42,Def6;
end;

theorem Th21:
  f1 is_differentiable_in x0 & f2 is_differentiable_in x0 implies
  f1+f2 is_differentiable_in x0 & diff(f1+f2,x0)=diff(f1,x0)+diff(f2,x0)
proof
  assume that
A1: f1 is_differentiable_in x0 and
A2: f2 is_differentiable_in x0;
  consider N1 be Neighbourhood of x0 such that
A3: N1 c= dom f1 & ex L,R st for x st x in N1 holds
  f1.x - f1.x0 = L.(x-x0) + R.(x-x0) by A1,Def5;
  consider L1,R1 such that
A4: for x st x in N1 holds f1.x - f1.x0 = L1.(x-x0) + R1.(x-x0) by A3;
  consider N2 be Neighbourhood of x0 such that
A5: N2 c= dom f2 & ex L,R st for x st x in N2 holds
  f2.x - f2.x0 = L.(x-x0) + R.(x-x0) by A2,Def5;
  consider L2,R2 such that
A6: for x st x in N2 holds f2.x - f2.x0 = L2.(x-x0) + R2.(x-x0) by A5;
  consider N be Neighbourhood of x0 such that
A7: N c= N1 & N c= N2 by RCOMP_1:38;
  reconsider L=L1+L2 as LINEAR by Th6;
A8: L1 is total & L2 is total by Def4;
  reconsider R=R1+R2 as REST by Th8;
A9: R1 is total & R2 is total by Def3;
A10: N c= dom f1 by A3,A7,XBOOLE_1:1;
  N c= dom f2 by A5,A7,XBOOLE_1:1;
  then N /\ N c= dom f1 /\ dom f2 by A10,XBOOLE_1:27;
  then
A11: N c= dom (f1+f2) by VALUED_1:def 1;
A12: now
    let x;
    assume
A13: x in N;
A14: x0 in N by RCOMP_1:37;
    thus (f1+f2).x - (f1+f2).x0 = (f1.x+f2.x) - (f1+f2).x0 by A11,A13,
VALUED_1:def 1
      .=f1.x+f2.x - (f1.x0+f2.x0) by A11,A14,VALUED_1:def 1
      .=(f1.x - f1.x0) + (f2.x - f2.x0)
      .=L1.(x-x0)+R1.(x-x0) + (f2.x - f2.x0) by A4,A7,A13
      .=L1.(x-x0)+R1.(x-x0) + (L2.(x-x0) + R2.(x-x0)) by A6,A7,A13
      .=(L1.(x-x0)+L2.(x-x0)) + (R1.(x-x0) + R2.(x-x0))
      .=L.(x-x0)+(R1.(x-x0) + R2.(x-x0)) by A8,RFUNCT_1:72
      .=L.(x-x0)+R.(x-x0) by A9,RFUNCT_1:72;
  end;
  hence f1+f2 is_differentiable_in x0 by A11,Def5;
  hence diff(f1+f2,x0)=L.1 by A11,A12,Def6
    .=L1.1 + L2.1 by A8,RFUNCT_1:72
    .=diff(f1,x0) + L2.1 by A1,A3,A4,Def6
    .=diff(f1,x0) + diff(f2,x0) by A2,A5,A6,Def6;
end;

theorem Th22:
  f1 is_differentiable_in x0 & f2 is_differentiable_in x0 implies
  f1-f2 is_differentiable_in x0 & diff(f1-f2,x0)=diff(f1,x0)-diff(f2,x0)
proof
  assume that
A1: f1 is_differentiable_in x0 and
A2: f2 is_differentiable_in x0;
  consider N1 be Neighbourhood of x0 such that
A3: N1 c= dom f1 & ex L,R st for x st x in N1 holds
  f1.x - f1.x0 = L.(x-x0) + R.(x-x0) by A1,Def5;
  consider L1,R1 such that
A4: for x st x in N1 holds f1.x - f1.x0 = L1.(x-x0) + R1.(x-x0) by A3;
  consider N2 be Neighbourhood of x0 such that
A5: N2 c= dom f2 & ex L,R st for x st x in N2 holds
  f2.x - f2.x0 = L.(x-x0) + R.(x-x0) by A2,Def5;
  consider L2,R2 such that
A6: for x st x in N2 holds f2.x - f2.x0 = L2.(x-x0) + R2.(x-x0) by A5;
  consider N be Neighbourhood of x0 such that
A7: N c= N1 & N c= N2 by RCOMP_1:38;
  reconsider L=L1-L2 as LINEAR by Th6;
A8: L1 is total & L2 is total by Def4;
  reconsider R=R1-R2 as REST by Th8;
A9: R1 is total & R2 is total by Def3;
A10: N c= dom f1 by A3,A7,XBOOLE_1:1;
  N c= dom f2 by A5,A7,XBOOLE_1:1;
  then N /\ N c= dom f1 /\ dom f2 by A10,XBOOLE_1:27;
  then
A11: N c= dom (f1-f2) by VALUED_1:12;
A12: now
    let x;
    assume
A13: x in N;
A14: x0 in N by RCOMP_1:37; thus
    (f1-f2).x - (f1-f2).x0 = (f1.x-f2.x) - (f1-f2).x0 by A11,A13,VALUED_1:13
      .=f1.x - f2.x - (f1.x0-f2.x0) by A11,A14,VALUED_1:13
      .=f1.x - f1.x0 - (f2.x - f2.x0)
      .=L1.(x-x0) + R1.(x-x0) - (f2.x - f2.x0) by A4,A7,A13
      .=L1.(x-x0) + R1.(x-x0) - (L2.(x-x0) + R2.(x-x0)) by A6,A7,A13
      .=L1.(x-x0) - L2.(x-x0) + (R1.(x-x0) - R2.(x-x0))
      .=L.(x-x0) + (R1.(x-x0) - R2.(x-x0)) by A8,RFUNCT_1:72
      .=L.(x-x0) + R.(x-x0) by A9,RFUNCT_1:72;
  end;
  hence f1-f2 is_differentiable_in x0 by A11,Def5;
  hence diff(f1-f2,x0)=L.1 by A11,A12,Def6
    .=L1.1 - L2.1 by A8,RFUNCT_1:72
    .=diff(f1,x0) - L2.1 by A1,A3,A4,Def6
    .=diff(f1,x0) - diff(f2,x0) by A2,A5,A6,Def6;
end;

theorem Th23:
  f is_differentiable_in x0 implies
  r(#)f is_differentiable_in x0 & diff((r(#)f),x0) = r*diff(f,x0)
proof
  assume
A1: f is_differentiable_in x0;
  then consider N1 be Neighbourhood of x0 such that
A2: N1 c= dom f &
  ex L,R st for x st x in N1 holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by Def5;
  consider L1,R1 such that
A3: for x st x in N1 holds f.x - f.x0 = L1.(x-x0) + R1.(x-x0) by A2;
  reconsider L = r(#)L1 as LINEAR by Th7;
  reconsider R = r(#)R1 as REST by Th9;
A4: L1 is total by Def4;
A5: R1 is total by Def3;
A6: N1 c= dom(r(#)f) by A2,VALUED_1:def 5;
A7: now
    let x;
    assume
A8: x in N1;
A9: x0 in N1 by RCOMP_1:37; thus
    (r(#)f).x - (r(#)f).x0 = r*(f.x) - (r(#)f).x0 by A6,A8,VALUED_1:def 5
      .= r*f.x - r*f.x0 by A6,A9,VALUED_1:def 5
      .= r*(f.x - f.x0)
      .= r*(L1.(x-x0) + R1.(x-x0)) by A3,A8
      .= r*L1.(x-x0) + r*R1.(x-x0)
      .= L.(x-x0) + r*R1.(x-x0) by A4,RFUNCT_1:73
      .= L.(x-x0) + R.(x-x0) by A5,RFUNCT_1:73;
  end;
  hence r(#)f is_differentiable_in x0 by A6,Def5;
  hence diff((r(#)f),x0) = L.1 by A6,A7,Def6
    .= r*L1.1 by A4,RFUNCT_1:73
    .= r*diff(f,x0) by A1,A2,A3,Def6;
end;

theorem Th24:
  f1 is_differentiable_in x0 & f2 is_differentiable_in x0 implies
  f1(#)f2 is_differentiable_in x0 &
  diff(f1(#)f2,x0)=(f2.x0)*diff(f1,x0)+(f1.x0)*diff(f2,x0)
proof
  assume that
A1: f1 is_differentiable_in x0 and
A2: f2 is_differentiable_in x0;
  consider N1 be Neighbourhood of x0 such that
A3: N1 c= dom f1 & ex L,R st for x st x in N1 holds
  f1.x - f1.x0 = L.(x-x0) + R.(x-x0) by A1,Def5;
  consider L1,R1 such that
A4: for x st x in N1 holds f1.x - f1.x0 = L1.(x-x0) + R1.(x-x0) by A3;
  consider N2 be Neighbourhood of x0 such that
A5: N2 c= dom f2 & ex L,R st for x st x in N2 holds
  f2.x - f2.x0 = L.(x-x0) + R.(x-x0) by A2,Def5;
  consider L2,R2 such that
A6: for x st x in N2 holds f2.x - f2.x0 = L2.(x-x0) + R2.(x-x0) by A5;
  consider N be Neighbourhood of x0 such that
A7: N c= N1 & N c= N2 by RCOMP_1:38;
  reconsider L11=(f2.x0)(#)L1 as LINEAR by Th7;
  reconsider L12=(f1.x0)(#)L2 as LINEAR by Th7;
A8: L11 is total & L12 is total & L1 is total & L2 is total by Def4;
  reconsider L=L11+L12 as LINEAR by Th6;
  reconsider R11=(f2.x0)(#)R1 as REST by Th9;
  reconsider R12=(f1.x0)(#)R2 as REST by Th9;
  reconsider R13=R11+R12 as REST by Th8;
  reconsider R14=L1(#)L2 as REST by Th10;
  reconsider R15=R13+R14 as REST by Th8;
  reconsider R16=R1(#)L2 as REST by Th11;
  reconsider R17=R1(#)R2 as REST by Th8;
  reconsider R18=R2(#)L1 as REST by Th11;
  reconsider R19=R16+R17 as REST by Th8;
  reconsider R20=R19+R18 as REST by Th8;
  reconsider R=R15+R20 as REST by Th8;
A9: R1 is total & R2 is total & R11 is total & R12 is total &
  R13 is total & R14 is total & R15 is total & R16 is total & R17 is total &
  R18 is total & R19 is total & R20 is total by Def3;
A10: N c= dom f1 by A3,A7,XBOOLE_1:1;
  N c= dom f2 by A5,A7,XBOOLE_1:1;
  then N /\ N c= dom f1 /\ dom f2 by A10,XBOOLE_1:27;
  then
A11: N c= dom (f1(#)f2) by VALUED_1:def 4;
A12: now
    let x;
    assume
A13: x in N;
    then
A14: f1.x - f1.x0 + f1.x0 = L1.(x-x0) + R1.(x-x0) + f1.x0 by A4,A7;
    thus (f1(#)f2).x - (f1(#)f2).x0 = (f1.x) * (f2.x) - (f1(#)
    f2).x0 by VALUED_1:5
      .=(f1.x)*(f2.x)+-(f1.x)*(f2.x0)+(f1.x)*(f2.x0)-(f1.x0)*(f2.x0) by
VALUED_1:5
      .=(f1.x)*((f2.x)-(f2.x0))+((f1.x)-(f1.x0))*(f2.x0)
      .=(f1.x)*((f2.x)-(f2.x0))+(L1.(x-x0)+R1.(x-x0))*(f2.x0) by A4,A7,A13
      .=(f1.x)*((f2.x)-(f2.x0))+((f2.x0)*L1.(x-x0)+R1.(x-x0)*(f2.x0))
.=(f1.x)*((f2.x)-(f2.x0))+(L11.(x-x0)+(f2.x0)*R1.(x-x0)) by A8,RFUNCT_1:73
.=(L1.(x-x0) + R1.(x-x0) + f1.x0)*((f2.x)-(f2.x0))+(L11.(x-x0)+R11.(x-x0))
    by A9,A14,RFUNCT_1:73
      .=(L1.(x-x0) + R1.(x-x0) + f1.x0)*(L2.(x-x0) + R2.(x-x0))+
    (L11.(x-x0)+R11.(x-x0)) by A6,A7,A13
      .=(L1.(x-x0) + R1.(x-x0))*(L2.(x-x0) + R2.(x-x0))+
    ((f1.x0)*L2.(x-x0)+(f1.x0)*R2.(x-x0)) + (L11.(x-x0)+R11.(x-x0))
      .=(L1.(x-x0) + R1.(x-x0))*(L2.(x-x0) + R2.(x-x0))+
    (L12.(x-x0)+(f1.x0)*R2.(x-x0)) + (L11.(x-x0)+R11.(x-x0)) by A8,RFUNCT_1:73
      .=(L1.(x-x0) + R1.(x-x0))*(L2.(x-x0) + R2.(x-x0))+
    (L12.(x-x0)+R12.(x-x0)) + (L11.(x-x0)+R11.(x-x0)) by A9,RFUNCT_1:73
      .=(L1.(x-x0) + R1.(x-x0))*(L2.(x-x0) + R2.(x-x0))+
    (L12.(x-x0)+(L11.(x-x0)+(R11.(x-x0)+R12.(x-x0))))
      .=(L1.(x-x0) + R1.(x-x0))*(L2.(x-x0) + R2.(x-x0))+
    (L12.(x-x0)+(L11.(x-x0)+R13.(x-x0))) by A9,RFUNCT_1:72
      .=(L1.(x-x0) + R1.(x-x0))*(L2.(x-x0) + R2.(x-x0))+
    (L11.(x-x0)+L12.(x-x0)+R13.(x-x0))
.=(L1.(x-x0)*L2.(x-x0)+L1.(x-x0)*R2.(x-x0))+R1.(x-x0)*(L2.(x-x0)+R2.(x-x0))+
    (L.(x-x0)+R13.(x-x0)) by A8,RFUNCT_1:72
      .=R14.(x-x0) + R2.(x-x0)*L1.(x-x0)+R1.(x-x0)*(L2.(x-x0)+R2.(x-x0))+
    (L.(x-x0)+R13.(x-x0)) by A8,RFUNCT_1:72
      .=R14.(x-x0) + R18.(x-x0)+(R1.(x-x0)*L2.(x-x0)+R1.(x-x0)*R2.(x-x0))+
    (L.(x-x0)+R13.(x-x0)) by A8,A9,RFUNCT_1:72
      .=R14.(x-x0) + R18.(x-x0)+(R16.(x-x0)+R1.(x-x0)*R2.(x-x0))+
    (L.(x-x0)+R13.(x-x0)) by A8,A9,RFUNCT_1:72
      .=R14.(x-x0) + R18.(x-x0)+(R16.(x-x0)+R17.(x-x0))+
    (L.(x-x0)+R13.(x-x0)) by A9,RFUNCT_1:72
.=R14.(x-x0) + R18.(x-x0)+R19.(x-x0)+(L.(x-x0)+R13.(x-x0)) by A9,RFUNCT_1:72
      .=R14.(x-x0) + (R19.(x-x0)+R18.(x-x0))+(L.(x-x0)+R13.(x-x0))
      .=L.(x-x0)+R13.(x-x0)+(R14.(x-x0) + R20.(x-x0)) by A9,RFUNCT_1:72
      .=L.(x-x0)+(R13.(x-x0)+R14.(x-x0) + R20.(x-x0))
      .=L.(x-x0)+(R15.(x-x0)+R20.(x-x0)) by A9,RFUNCT_1:72
      .=L.(x-x0)+R.(x-x0) by A9,RFUNCT_1:72;
  end;
  hence f1(#)f2 is_differentiable_in x0 by A11,Def5;
  hence diff(f1(#)f2,x0)=L.1 by A11,A12,Def6
    .= L11.1 + L12.1 by A8,RFUNCT_1:72
    .= f2.x0 * L1.1 + L12.1 by A8,RFUNCT_1:73
    .= f2.x0 * L1.1 + f1.x0 *L2.1 by A8,RFUNCT_1:73
    .= f2.x0 * diff(f1,x0) + f1.x0 * L2.1 by A1,A3,A4,Def6
    .= f2.x0 * diff(f1,x0) + f1.x0 * diff(f2,x0) by A2,A5,A6,Def6;
end;

theorem Th25:
  Z c= dom f & f|Z = id Z implies
  f is_differentiable_on Z & for x st x in Z holds (f`|Z).x = 1
proof
  assume that
A1: Z c= dom f and
A2: f|Z = id Z;
  reconsider L = id REAL as PartFunc of REAL,REAL;
  for p holds L.p = 1*p by FUNCT_1:35;
  then reconsider L as LINEAR by Def4;
  set R = cf;
A3: dom R = REAL by FUNCOP_1:19;
  now
    let h;
A4: now
      let n be Nat;
X:   n in NAT by ORDINAL1:def 13;
A5:   rng h c= dom R by A3;
      thus ((h")(#)(R/*h)).n = (h".n)*((R/*h).n) by SEQ_1:12,X
        .= (h".n)*(R.(h.n)) by A5,FUNCT_2:185,X
        .= (h".n)*0 by FUNCOP_1:13
        .= 0;
    end;
    then
A6: (h")(#)(R/*h) is constant by VALUED_0:def 18;
    hence (h")(#)(R/*h) is convergent;
    ((h")(#)(R/*h)).0 = 0 by A4;
    hence lim ((h")(#)(R/*h)) = 0 by A6,SEQ_4:40;
  end;
  then reconsider R as REST by Def3;
A7: now
    let x;
    assume
A8: x in Z;
    then f|Z.x = x by A2,FUNCT_1:35;
    hence f.x = x by A8,FUNCT_1:72;
  end;
A9: now
    let x0;
    assume
A10: x0 in Z;
    then consider N being Neighbourhood of x0 such that
A11: N c= Z by RCOMP_1:39;
A12: N c= dom f by A1,A11,XBOOLE_1:1;
    for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0)
    proof
      let x;
      assume x in N;
      hence f.x - f.x0 = x - f.x0 by A7,A11
        .= x - x0 by A7,A10
        .= L.(x-x0)+0 by FUNCT_1:35
        .= L.(x-x0) + R.(x-x0) by FUNCOP_1:13;
    end;
    hence f is_differentiable_in x0 by A12,Def5;
  end;
  hence
A13: f is_differentiable_on Z by A1,Th16;
  let x0;
  assume
A14: x0 in Z;
  then
A15: f is_differentiable_in x0 by A9;
  then ex N being Neighbourhood of x0 st N c= dom f &
  ex L,R st for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by Def5;
  then consider N being Neighbourhood of x0 such that
A16: N c= dom f;
  consider N1 being Neighbourhood of x0 such that
A17: N1 c= Z by A14,RCOMP_1:39;
  consider N2 being Neighbourhood of x0 such that
A18: N2 c= N1 & N2 c= N by RCOMP_1:38;
A19: N2 c= dom f by A16,A18,XBOOLE_1:1;
A20: for x st x in N2 holds f.x - f.x0 = L.(x-x0) + R.(x-x0)
  proof
    let x;
    assume x in N2;
    then x in N1 by A18;
    hence f.x - f.x0 = x - f.x0 by A7,A17
      .= x - x0 by A7,A14
      .= L.(x-x0)+0 by FUNCT_1:35
      .= L.(x-x0) + R.(x-x0) by FUNCOP_1:13;
  end;
  thus (f`|Z).x0 = diff(f,x0) by A13,A14,Def8
    .= L.1 by A15,A19,A20,Def6
    .= 1 by FUNCT_1:35;
end;

theorem
  Z c= dom (f1+f2) & f1 is_differentiable_on Z &
  f2 is_differentiable_on Z implies f1+f2 is_differentiable_on Z &
  for x st x in Z holds ((f1+f2)`|Z).x = diff(f1,x) + diff(f2,x)
proof
  assume that
A1: Z c= dom (f1+f2) and
A2: f1 is_differentiable_on Z and
A3: f2 is_differentiable_on Z;
  now
    let x0;
    assume
A4: x0 in Z;
    then
A5: f1 is_differentiable_in x0 by A2,Th16;
    f2 is_differentiable_in x0 by A3,A4,Th16;
    hence f1+f2 is_differentiable_in x0 by A5,Th21;
  end;
  hence
A6: f1+f2 is_differentiable_on Z by A1,Th16;
  now
    let x;
    assume
A7: x in Z;
    then
A8: f1 is_differentiable_in x by A2,Th16;
A9: f2 is_differentiable_in x by A3,A7,Th16;
    thus ((f1+f2)`|Z).x = diff((f1+f2),x) by A6,A7,Def8
      .= diff(f1,x) + diff(f2,x) by A8,A9,Th21;
  end;
  hence thesis;
end;

theorem
  Z c= dom (f1-f2) & f1 is_differentiable_on Z &
  f2 is_differentiable_on Z implies f1-f2 is_differentiable_on Z &
  for x st x in Z holds ((f1-f2)`|Z).x = diff(f1,x) - diff(f2,x)
proof
  assume that
A1: Z c= dom (f1-f2) and
A2: f1 is_differentiable_on Z and
A3: f2 is_differentiable_on Z;
  now
    let x0;
    assume
A4: x0 in Z;
    then
A5: f1 is_differentiable_in x0 by A2,Th16;
    f2 is_differentiable_in x0 by A3,A4,Th16;
    hence f1-f2 is_differentiable_in x0 by A5,Th22;
  end;
  hence
A6: f1-f2 is_differentiable_on Z by A1,Th16;
  now
    let x;
    assume
A7: x in Z;
    then
A8: f1 is_differentiable_in x by A2,Th16;
A9: f2 is_differentiable_in x by A3,A7,Th16;
    thus ((f1-f2)`|Z).x = diff((f1-f2),x) by A6,A7,Def8
      .= diff(f1,x) - diff(f2,x) by A8,A9,Th22;
  end;
  hence thesis;
end;

theorem
  Z c= dom (r(#)f) & f is_differentiable_on Z implies
  r(#)f is_differentiable_on Z & for x st x in Z holds ((r(#)
  f)`|Z).x =r*diff(f,x)
proof
  assume that
A1: Z c= dom (r(#)f) and
A2: f is_differentiable_on Z;
  now
    let x0;
    assume x0 in Z;
    then f is_differentiable_in x0 by A2,Th16;
    hence r(#)f is_differentiable_in x0 by Th23;
  end;
  hence
A3: r(#)f is_differentiable_on Z by A1,Th16;
  now
    let x;
    assume
A4: x in Z;
    then
A5: f is_differentiable_in x by A2,Th16;
    thus ((r(#)f)`|Z).x = diff((r(#)f),x) by A3,A4,Def8
      .= r*diff(f,x) by A5,Th23;
  end;
  hence thesis;
end;

theorem
  Z c= dom (f1(#)f2) & f1 is_differentiable_on Z &
  f2 is_differentiable_on Z implies f1(#)f2 is_differentiable_on Z &
for x st x in Z holds ((f1(#)f2)`|Z).x = (f2.x)*diff(f1,x) + (f1.x)*diff(f2,x)
proof
  assume that
A1: Z c= dom (f1(#)f2) and
A2: f1 is_differentiable_on Z and
A3: f2 is_differentiable_on Z;
  now
    let x0;
    assume
A4: x0 in Z;
    then
A5: f1 is_differentiable_in x0 by A2,Th16;
    f2 is_differentiable_in x0 by A3,A4,Th16;
    hence f1(#)f2 is_differentiable_in x0 by A5,Th24;
  end;
  hence
A6: f1(#)f2 is_differentiable_on Z by A1,Th16;
  now
    let x;
    assume
A7: x in Z;
    then
A8: f1 is_differentiable_in x by A2,Th16;
A9: f2 is_differentiable_in x by A3,A7,Th16;
    thus ((f1(#)f2)`|Z).x = diff((f1(#)f2),x) by A6,A7,Def8
      .= f2.x*diff(f1,x) + f1.x*diff(f2,x) by A8,A9,Th24;
  end;
  hence thesis;
end;

theorem
  Z c= dom f & f|Z is constant implies
  f is_differentiable_on Z & for x st x in Z holds (f`|Z).x = 0
proof
  assume
A1: Z c= dom f & f|Z is constant;
  then consider r such that
A2: for x st x in Z/\dom f holds f.x=r by PARTFUN2:76;
  set L = cf;
  for p holds L.p=0*p by FUNCOP_1:13;
  then reconsider L as LINEAR by Def4;
  set R = cf;
A3: dom R = REAL by FUNCOP_1:19;
  now
    let h;
A4: now
      let n be Nat;
X:   n in NAT by ORDINAL1:def 13;
A5:   rng h c= dom R by A3;
      thus ((h")(#)(R/*h)).n = (h".n)*((R/*h).n) by SEQ_1:12,X
        .=(h".n)*(R.(h.n)) by A5,FUNCT_2:185,X
        .=(h".n)*0 by FUNCOP_1:13
        .=0;
    end;
    then
A6: (h")(#)(R/*h) is constant by VALUED_0:def 18;
    hence (h")(#)(R/*h) is convergent;
    ((h")(#)(R/*h)).0 = 0 by A4;
    hence lim ((h")(#)(R/*h)) = 0 by A6,SEQ_4:40;
  end;
  then reconsider R as REST by Def3;
A7: now
    let x0;
    assume
A8: x0 in Z;
    then
A9: x0 in Z/\dom f by A1,XBOOLE_0:def 4;
    consider N being Neighbourhood of x0 such that
A10: N c= Z by A8,RCOMP_1:39;
A11: N c= dom f by A1,A10,XBOOLE_1:1;
    for x st x in N holds f.x-f.x0=L.(x-x0)+R.(x-x0)
    proof
      let x;
      assume x in N;
      then x in Z/\dom f by A10,A11,XBOOLE_0:def 4;
      hence f.x-f.x0=r-f.x0 by A2
        .=r - r by A2,A9
        .=L.(x-x0)+0 by FUNCOP_1:13
        .=L.(x-x0)+R.(x-x0) by FUNCOP_1:13;
    end;
    hence f is_differentiable_in x0 by A11,Def5;
  end;
  hence
A12: f is_differentiable_on Z by A1,Th16;
  let x0;
  assume
A13: x0 in Z;
  then
A14: x0 in Z/\dom f by A1,XBOOLE_0:def 4;
A15: f is_differentiable_in x0 by A7,A13;
  consider N being Neighbourhood of x0 such that
A16: N c= Z by A13,RCOMP_1:39;
A17: N c= dom f by A1,A16,XBOOLE_1:1;
A18: for x st x in N holds f.x-f.x0=L.(x-x0)+R.(x-x0)
  proof
    let x;
    assume x in N;
    then x in Z/\dom f by A16,A17,XBOOLE_0:def 4;
    hence f.x - f.x0 = r - f.x0 by A2
      .=r - r by A2,A14
      .=L.(x-x0) + 0 by FUNCOP_1:13
      .=L.(x-x0) + R.(x-x0) by FUNCOP_1:13;
  end;
  thus (f`|Z).x0 = diff(f,x0) by A12,A13,Def8
    .=L.1 by A15,A17,A18,Def6
    .=0 by FUNCOP_1:13;
end;

theorem
  Z c= dom f & (for x st x in Z holds f.x = r*x + p) implies
  f is_differentiable_on Z & for x st x in Z holds (f`|Z).x = r
proof
  assume
A1: Z c= dom f & for x st x in Z holds f.x = r*x + p;
  defpred P[set] means $1 in REAL;
  deffunc G(Real) = r*$1;
  consider L being PartFunc of REAL,REAL such that
A2: (for x holds x in dom L iff P[x]) &
  for x st x in dom L holds L.x=G(x) from SEQ_1:sch 3;
  dom L = REAL by A2,Th1;
  then
A3: L is total by PARTFUN1:def 4;
A4: now
    let x;
    x in dom L by A2;
    hence L.x=r*x by A2;
  end;
  then reconsider L as LINEAR by A3,Def4;
  set R = cf;
A5: dom R = REAL by FUNCOP_1:19;
  now
    let h;
A6: now
      let n be Nat;
X:   n in NAT by ORDINAL1:def 13;
A7:   rng h c= dom R by A5;
      thus ((h")(#)(R/*h)).n = (h".n)*((R/*h).n) by SEQ_1:12,X
        .=(h".n)*(R.(h.n)) by A7,FUNCT_2:185,X
        .=(h".n)*0 by FUNCOP_1:13
        .=0;
    end;
    then
A8: (h")(#)(R/*h) is constant by VALUED_0:def 18;
    hence (h")(#)(R/*h) is convergent;
    ((h")(#)(R/*h)).0 = 0 by A6;
    hence lim ((h")(#)(R/*h)) = 0 by A8,SEQ_4:40;
  end;
  then reconsider R as REST by Def3;
A9: now
    let x0;
    assume
A10: x0 in Z;
    then consider N being Neighbourhood of x0 such that
A11: N c= Z by RCOMP_1:39;
A12: N c= dom f by A1,A11,XBOOLE_1:1;
    for x st x in N holds f.x-f.x0=L.(x-x0)+R.(x-x0)
    proof
      let x;
      assume x in N;
      hence f.x-f.x0=r*x+p-f.x0 by A1,A11
        .=r*x+p - (r*x0+p) by A1,A10
        .=r*(x-x0)+0
        .=L.(x-x0)+0 by A4
        .=L.(x-x0)+R.(x-x0) by FUNCOP_1:13;
    end;
    hence f is_differentiable_in x0 by A12,Def5;
  end;
  hence
A13: f is_differentiable_on Z by A1,Th16;
  let x0;
  assume
A14: x0 in Z;
  then
A15: f is_differentiable_in x0 by A9;
  consider N being Neighbourhood of x0 such that
A16: N c= Z by A14,RCOMP_1:39;
A17: N c= dom f by A1,A16,XBOOLE_1:1;
A18: for x st x in N holds f.x-f.x0=L.(x-x0)+R.(x-x0)
  proof
    let x;
    assume x in N;
    hence f.x - f.x0 = r*x+p - f.x0 by A1,A16
      .=r*x+p-(r*x0+p) by A1,A14
      .=r*(x-x0)+0
      .=L.(x-x0) + 0 by A4
      .=L.(x-x0) + R.(x-x0) by FUNCOP_1:13;
  end;
  thus (f`|Z).x0 = diff(f,x0) by A13,A14,Def8
    .=L.1 by A15,A17,A18,Def6
    .=r*1 by A4
    .=r;
end;

theorem Th32:
  for x0 being real number holds
  f is_differentiable_in x0 implies f is_continuous_in x0
proof
  let x0 be real number;
  assume
A1: f is_differentiable_in x0;
  then consider N being Neighbourhood of x0 such that
A2: N c= dom f &
  ex L,R st for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by Def5;
  now
    let s1 such that
A4: rng s1 c= dom f & s1 is convergent & lim s1 = x0 & for n holds s1.n<>x0;
    consider g be real number such that
A5: 0<g & N=].x0-g,x0+g.[ by RCOMP_1:def 7;
    x0 in REAL by XREAL_0:def 1;
    then reconsider s2 = NAT --> x0 as Real_Sequence by FUNCOP_1:57;
    deffunc G(Real) = s1.$1-s2.$1;
    consider s3 such that
A6: for n holds s3.n=G(n) from SEQ_1:sch 1;
A8: lim s2 = s2.0 by SEQ_4:41
      .=x0 by FUNCOP_1:13;
A9: s3=s1-s2 by A6,RFUNCT_2:6;
    then
A10: s3 is convergent by A4,SEQ_2:25;
A11: lim s3 =x0-x0 by A4,A8,A9,SEQ_2:26
      .=0;
A12: now
      given n such that
A13:  s3.n=0;
      s1.n-s2.n=0 by A6,A13;
      hence contradiction by A4,FUNCOP_1:13;
    end;
    consider l be Element of NAT such that
A14: for m st l<=m holds abs(s1.m-x0)<g by A4,A5,SEQ_2:def 7;
A15: s3^\l is convergent & lim(s3^\l)=0 by A10,A11,SEQ_4:33;
A16: now
      given n such that
A17:  (s3^\l).n=0;
      s3.(n+l)=0 by A17,NAT_1:def 3;
      hence contradiction by A12;
    end;
    then s3^\l is non-zero by SEQ_1:7;
    then reconsider h=s3^\l as convergent_to_0 Real_Sequence by A15,Def1;
    reconsider c =s2^\l as constant Real_Sequence;
A18: rng c = {x0}
    proof
      thus rng c c= {x0}
      proof
        let y;
        assume y in rng c;
        then consider n such that
A19:    y=(s2^\l).n by FUNCT_2:190;
        y=s2.(n+l) by A19,NAT_1:def 3;
        then y=x0 by FUNCOP_1:13;
        hence y in {x0} by TARSKI:def 1;
      end;
      let y;
      assume y in {x0};
      then
A20:  y=x0 by TARSKI:def 1;
      c.0=s2.(0+l) by NAT_1:def 3
        .= y by A20,FUNCOP_1:13;
      hence y in rng c by VALUED_0:28;
    end;
    rng (h+c) c= N
    proof
      let y;
      assume y in rng(h+c);
      then consider n such that
A21:  y=(h+c).n by FUNCT_2:190;
      (h+c).n=((s1-s2+s2)^\l).n by A9,SEQM_3:37
        .=(s1-s2+s2).(n+l) by NAT_1:def 3
        .=(s1-s2).(n+l)+s2.(n+l) by SEQ_1:11
        .=s1.(n+l)-s2.(n+l)+s2.(n+l) by RFUNCT_2:6
        .=s1.(l+n);
      then abs((h+c).n-x0)<g by A14,NAT_1:12;
      hence y in N by A5,A21,RCOMP_1:8;
    end;
    then
A22: h"(#)(f/*(h+c) - f/*c) is convergent by A1,A2,A18,Th20;
    then
A23: lim (h(#)(h"(#)(f/*(h+c) - f/*c))) =0*lim(h"(#)
    (f/*(h+c) - f/*c)) by A15,SEQ_2:29
      .=0;
    now
      let n;
A24:  h.n<>0 by A16;
thus (h(#)(h"(#)(f/*(h+c) - f/*c))).n
         =h.n *(h"(#)(f/*(h+c) - f/*c)).n by SEQ_1:12
        .=h.n*((h").n*(f/*(h+c) - f/*c).n) by SEQ_1:12
        .=h.n*(((h.n)")*(f/*(h+c) - f/*c).n) by VALUED_1:10
        .=h.n*((h.n)")*(f/*(h+c) - f/*c).n
        .=1*(f/*(h+c) - f/*c).n by A24,XCMPLX_0:def 7
        .=(f/*(h+c) - f/*c).n;
    end;
    then
A25: h(#)(h"(#)(f/*(h+c) - f/*c))=f/*(h+c)-f/*c by FUNCT_2:113;
    then
A26: f/*(h+c)-f/*c is convergent by A10,A22,SEQ_2:28;
A27: now
      let p be real number such that
A28:  0<p;
      take n=0;
      let m such that n<=m;
      x0 in N by RCOMP_1:37;
      then rng c c= dom f by A2,A18,ZFMISC_1:37;
      then abs((f/*c).m-f.x0)=abs(f.(c.m)-f.x0) by FUNCT_2:185
        .=abs(f.(s2.(m+l))-f.x0) by NAT_1:def 3
        .=abs(f.x0-f.x0) by FUNCOP_1:13
        .=0 by ABSVALUE:7;
      hence abs((f/*c).m-f.x0)<p by A28;
    end;
    then
A29: f/*c is convergent by SEQ_2:def 6;
    then
A30: lim(f/*c)=f.x0 by A27,SEQ_2:def 7;
A31: f/*(h+c)-f/*c+f/*c is convergent by A26,A29,SEQ_2:19;
A32: lim(f/*(h+c)-f/*c+f/*c)=0+f.x0 by A23,A25,A26,A29,A30,SEQ_2:20
      .=f.x0;
    now
      let n;
      thus (f/*(h+c)-f/*c+f/*c).n=(f/*(h+c)-f/*c).n+(f/*c).n by SEQ_1:11
        .=(f/*(h+c)).n-(f/*c).n+(f/*c).n by RFUNCT_2:6
        .=(f/*(h+c)).n;
    end;
    then
A33: f/*(h+c)-f/*c+(f/*c)=f/*(h+c) by FUNCT_2:113;
    now
      let n;
      thus (h+c).n=((s1-s2+s2)^\l).n by A9,SEQM_3:37
        .=(s1-s2+s2).(n+l) by NAT_1:def 3
        .=(s1-s2).(n+l)+s2.(n+l) by SEQ_1:11
        .=s1.(n+l)-s2.(n+l)+s2.(n+l) by RFUNCT_2:6
        .=(s1^\l).n by NAT_1:def 3;
    end;
    then
A34: f/*(h+c)-f/*c+(f/*c)=f/*(s1^\l) by A33,FUNCT_2:113
      .=(f/*s1)^\l by A4,VALUED_0:27;
    hence f/*s1 is convergent by A31,SEQ_4:35;
    thus f.x0=lim(f/*s1) by A31,A32,A34,SEQ_4:36;
  end;
  hence thesis by FCONT_1:2;
end;

theorem
  f is_differentiable_on X implies f|X is continuous
proof
  assume
A1: f is_differentiable_on X;
  let x be real number;
A2: x is Real by XREAL_0:def 1;
  assume x in dom(f|X);
   then x in X by RELAT_1:86;
  then f|X is_differentiable_in x by A1,A2,Def7;
  hence f|X is_continuous_in x by Th32;
end;

theorem Th34:
  f is_differentiable_on X & Z c= X implies f is_differentiable_on Z
proof
  assume
A1: f is_differentiable_on X & Z c= X;
  then X c= dom f by Def7;
  hence Z c= dom f by A1,XBOOLE_1:1;
  let x0;
  assume
A2: x0 in Z;
  then f|X is_differentiable_in x0 by A1,Def7;
  then consider N being Neighbourhood of x0 such that
A3: N c= dom(f|X) &
  ex L,R st for x st x in N holds (f|X).x-(f|X).x0=L.(x-x0)+R.(x-x0) by Def5;
  consider N1 being Neighbourhood of x0 such that
A4: N1 c= Z by A2,RCOMP_1:39;
  consider N2 being Neighbourhood of x0 such that
A5: N2 c= N & N2 c= N1 by RCOMP_1:38;
  take N2;
  dom(f|X)=dom f/\X by RELAT_1:90;
  then dom(f|X) c=dom f by XBOOLE_1:17;
  then N c= dom f by A3,XBOOLE_1:1;
  then
A6: N2 c=dom f by A5,XBOOLE_1:1;
  N2 c= Z by A4,A5,XBOOLE_1:1;
  then N2 c=dom f/\Z by A6,XBOOLE_1:19;
  hence
A7: N2 c=dom(f|Z) by RELAT_1:90;
  consider L,R such that
A8: for x st x in N holds (f|X).x-(f|X).x0=L.(x-x0)+R.(x-x0) by A3;
  take L,R;
  let x;
  assume
A9: x in N2;
  then
A10: x in N by A5;
  (f|X).x-(f|X).x0=L.(x-x0)+R.(x-x0) by A5,A8,A9;
  then (f|X).x-f.x0=L.(x-x0)+R.(x-x0) by A1,A2,FUNCT_1:72;
  then f.x-f.x0=L.(x-x0)+R.(x-x0) by A3,A10,FUNCT_1:68;
  then f.x-(f|Z).x0=L.(x-x0)+R.(x-x0) by A2,FUNCT_1:72;
  hence thesis by A7,A9,FUNCT_1:68;
end;

theorem
  f is_differentiable_in x0 implies ex R st R.0=0 & R is_continuous_in 0
proof
  assume f is_differentiable_in x0;
  then consider N being Neighbourhood of x0 such that
A1: N c= dom f &
  ex L,R st for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by Def5;
  consider L,R such that
A2: for x st x in N holds f.x - f.x0 = L.(x-x0) + R.(x-x0) by A1;
  take R;
  consider p such that
A3: for r holds L.r = p*r by Def4;
  f.x0 - f.x0 = L.(x0-x0) + R.(x0-x0) by A2,RCOMP_1:37;
  then
A4: 0 = p*0 + R.0 by A3;
  hence R.0=0;
A6: now
    let h;
    (h")(#)(R/*h) is convergent & lim ((h")(#)(R/*h)) = 0 by Def3;
    then (h")(#)(R/*h) is bounded by SEQ_2:27;
    then consider M being real number such that
A7: M>0 & for n holds abs(((h")(#)(R/*h)).n)<M by SEQ_2:15;
    set s3 = cs;
A9: s3.1 = 0 by FUNCOP_1:13;
    then
A10: lim s3 = 0 by SEQ_4:40;
A11: h is non-zero & h is convergent & lim h=0 by Def1;
    then
A12: abs(h) is convergent by SEQ_4:26;
A13: lim abs(h) = abs(0) by A11,SEQ_4:27
      .=0 by ABSVALUE:7;
A14: M(#)abs(h) is convergent by A12,SEQ_2:21;
A15: lim (M(#)abs(h)) = M*0 by A12,A13,SEQ_2:22
      .=lim s3 by A9,SEQ_4:40;
A16: now
      let n;
A17:  h.n <>0 by A11,SEQ_1:7;
      abs(((h")(#)(R/*h)).n)=abs(((h").n)*(R/*h).n) by SEQ_1:12
        .=abs((h.n)"*(R/*h).n) by VALUED_1:10;
      then
A18:  abs((h.n)"*(R/*h).n)<=M by A7;
      0<=abs((R/*h).n) by COMPLEX1:132;
      then 0<=abs((R/*h)).n by SEQ_1:16;
      hence s3.n<=abs((R/*h)).n by FUNCOP_1:13;
      abs((h.n))>=0 by COMPLEX1:132;
      then abs((h.n))*abs((h.n)"*(R/*h).n)<=M*abs((h.n)) by A18,XREAL_1:66;
      then abs((h.n)*((h.n)"*(R/*h).n))<=M*abs((h.n)) by COMPLEX1:151;
      then abs((h.n)*(h.n)"*(R/*h).n)<=M*abs((h.n));
      then abs(1*(R/*h).n)<=M*abs((h.n)) by A17,XCMPLX_0:def 7;
      then abs((R/*h).n)<=M*abs(h).n by SEQ_1:16;
      then abs((R/*h).n)<=(M(#)abs(h)).n by SEQ_1:13;
      hence abs((R/*h)).n<=(M(#)abs (h)).n by SEQ_1:16;
    end;
    then
A19: abs(R/*h) is convergent by A14,A15,SEQ_2:33;
    lim abs(R/*h)=0 by A10,A14,A15,A16,SEQ_2:34;
    hence R/*h is convergent & lim (R/*h)=R.0 by A4,A19,SEQ_4:28;
  end;
     now
    let s1;
    assume
A21: rng s1 c= dom R & s1 is convergent & lim s1 = 0 & (for n holds s1.n<>0);
    then s1 is non-zero by SEQ_1:7;
    then s1 is convergent_to_0 Real_Sequence by A21,Def1;
    hence R/*s1 is convergent & lim (R/*s1)=R.0 by A6;
  end;
  hence thesis by FCONT_1:2;
end;

definition let f be PartFunc of REAL, REAL;
  attr f is differentiable means :DifDef:
    f is_differentiable_on dom f;
end;

Th3: {} REAL is closed
proof
  let a be Real_Sequence;
  assume rng a c= {} REAL & a is convergent;
  then rng a = {} by XBOOLE_1:3;
  then dom a = {} by RELAT_1:65;
  hence thesis by FUNCT_2:def 1;
end;

Th4: [#] REAL is open
proof ([#] REAL)` = {} REAL by XBOOLE_1:37;
  hence thesis by Th3,RCOMP_1:def 5;
end;

registration
  cluster differentiable Function of REAL, REAL;
  existence
  proof
    reconsider f = id REAL as Function of REAL, REAL;
    take f;
    reconsider Z = REAL as open Subset of REAL by Th4;
A1: Z = dom f by FUNCT_2:def 1; then
    f|Z = id Z by RELAT_1:97; then
    f is_differentiable_on Z by A1,Th25;
    hence thesis by DifDef,A1;
  end;
end;

theorem
  for f being differentiable PartFunc of REAL, REAL
    st Z c= dom f holds
   f is_differentiable_on Z
proof
  let f be differentiable PartFunc of REAL, REAL;
  assume
A1: Z c= dom f;
  f is_differentiable_on dom f by DifDef;
  hence thesis by A1,Th34;
end;
