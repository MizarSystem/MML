:: Preliminaries to Mathematical Morphology and Its Properties
::  by Yuzhong Ding and Xiquan Liang
:: 
:: Received January 7, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabulary EUCLID, PRE_TOPC, ARYTM_1, MATHMORP, SUBSET_1, BOOLE, ARYTM_3,
      JORDAN1, MONOID_0, QC_LANG1, TOPREAL1, ARYTM;
 notation TARSKI, ORDINAL1, EUCLID, TOPS_2, SUBSET_1, XBOOLE_0, XCMPLX_0,
      ZFMISC_1, STRUCT_0, PRE_TOPC, XREAL_0, NUMBERS, REAL_1, NAT_1, JORDAN1,
      TOPREAL1;
 constructors TOPS_1, RCOMP_1, JGRAPH_2, TOPREAL2, TOPGRP_1, CONNSP_1, TOPS_2,
      TOPREAL1, PSCOMP_1, REAL_1, EUCLID, XBOOLE_0, NAT_1, XCMPLX_0, JORDAN1;
 clusters STRUCT_0, RELSET_1, FUNCT_1, EUCLID, PRE_TOPC, XREAL_0, XBOOLE_0,
      MEMBERED;
 requirements REAL, BOOLE, SUBSET, NUMERALS, ARITHM;
 definitions TARSKI, XBOOLE_0, TOPREAL1;
 theorems XBOOLE_1, XCMPLX_1, TARSKI, SUBSET_1, ZFMISC_1, EUCLID, XBOOLE_0,
      REAL_1, REAL_2, JORDAN1, TOPREAL1, XREAL_0;

begin
:: 1.The definition of Erosion and Dilation and their Algebraic Properties

:: Translation of X according to p

definition let n be Nat,p be Point of TOP-REAL n,X be Subset of TOP-REAL n;
  func X+p -> Subset of TOP-REAL n equals
  :Def2:{q+p where q is Point of TOP-REAL n:q in X};
  coherence
   proof
   now let x be set;
    assume x in {q+p where q is Point of TOP-REAL n:q in X};then
    consider q being Point of TOP-REAL n such that 
 A1:x = q + p & q in X;
    thus x in the carrier of TOP-REAL n by A1;
   end;
   hence thesis by TARSKI:def 3;
 end;
end;

:: Reflected of X

definition let n be Nat,X be Subset of TOP-REAL n;
  func X! -> Subset of TOP-REAL n equals
  :Def3:{-q where q is Point of TOP-REAL n: q in X};
  coherence
  proof
    now let x be set;
    assume x in {-q where q is Point of TOP-REAL n: q in X};then
    consider q being Point of TOP-REAL n such that
 A1:x = -q & q in X;
    thus x in the carrier of TOP-REAL n by A1;
  end;
  hence thesis by TARSKI:def 3;
 end;
end;

:: Set Erosion

definition let n be Nat,X,B be Subset of TOP-REAL n;
  func X (-) B -> Subset of TOP-REAL n equals
  :Def4:{y where y is Point of TOP-REAL n:B+y c= X};
 coherence 
   proof
   now let x be set;
    assume x in {y where y is Point of TOP-REAL n:B+y c= X};then
    consider q being Point of TOP-REAL n such that 
 A1:x = q & B+q c= X;
    thus x in the carrier of TOP-REAL n by A1;
   end;
   hence thesis by TARSKI:def 3;
 end;
end;

:: Set Dilation

definition let n be Nat,X,B be Subset of TOP-REAL n;
  func X (+) B -> Subset of TOP-REAL n equals
  :Def5:{y+b where y,b is Point of TOP-REAL n: y in X & b in B};
  coherence
   proof
   now let x be set;
    assume x in {y+b where y,b is Point of TOP-REAL n:y in X & b in B};then
    consider y,b being Point of TOP-REAL n such that 
 A1:x = y+b & y in X & b in B;
    thus x in the carrier of TOP-REAL n by A1;
   end;
   hence thesis by TARSKI:def 3;
 end;
end;

 reserve n for Nat,
         X,Y,Z,B,C,B1,B2 for Subset of TOP-REAL n,
         x,y,p for Point of TOP-REAL n;

theorem Th1:
  (B!)! = B
proof
  thus (B!)! c= B
  proof let x be set;assume x in (B!)!;then
    x in {-q where q is Point of TOP-REAL n:q in B!} by Def3;then
    consider q being Point of TOP-REAL n such that
A3: x = -q & q in B!;
    q in {-q1 where q1 is Point of TOP-REAL n:q1 in B}by Def3,A3;then
    consider q1 being Point of TOP-REAL n such that
A5: q = -q1 & q1 in B;
    thus thesis by A3,EUCLID:39,A5;
 end;
   let x be set;assume
B1:x in B;then
   reconsider xx = x as Point of TOP-REAL n;
B3:xx = -(-xx) by EUCLID:39;
   -xx in {-q where q is Point of TOP-REAL n:q in B}by B1;then
   -xx in B! by Def3;then
   -(-xx) in {-q where q is Point of TOP-REAL n:q in B!};
   hence thesis by B3,Def3;
end;

theorem Th2:
  {0.REAL n} + x = {x}
proof thus {0.REAL n} + x c= {x}
   proof
    let a be set;assume a in {0.REAL n} + x;then
    a in {q+x where q is Point of TOP-REAL n:q in {0.REAL n}}by Def2;then
    consider q being Point of TOP-REAL n such that
 A3:a=q+x & q in {0.REAL n};
    {q} c= {0.REAL n} by ZFMISC_1:37,A3;then
    q = 0.REAL n by ZFMISC_1:24;then
    {a} c= {x} by A3,EUCLID:31;
    hence thesis by ZFMISC_1:37;
   end;
   let a be set;assume a in {x};then
   {a} c= {x} by ZFMISC_1:37;then
   a = x by ZFMISC_1:24;then
A7:a = 0.REAL n + x by EUCLID:31;
   0.REAL n in {0.REAL n} by ZFMISC_1:37;then
   a in {q+x where q is Point of TOP-REAL n:q in {0.REAL n}} by A7;
   hence thesis by Def2;
end;

theorem Th4:
  B1 c= B2 implies B1+p c= B2+p
proof
   assume
A1:B1 c= B2;
   let p1 be set;
   assume p1 in B1+p;then
A3:p1 in {q+p where q is Point of TOP-REAL n:q in B1} by Def2;
   consider p2 being Point of TOP-REAL n such that
A5:p1 =p2+p & p2 in B1 by A3;
   p1 in {q+p where q is Point of TOP-REAL n:q in B2}by A1,A5;
   hence thesis by Def2;
end;

theorem Ti1:
  for X st X = {} holds X+x = {}
proof let X;
  assume
AA:X = {};
  now given y being set such that
A1: y in X+x;
    y in {y1+x where y1 is Point of TOP-REAL n:y1 in X}by A1,Def2;then
    consider y1 being Point of TOP-REAL n such that
A3: y = y1+x & y1 in X;
    thus contradiction by AA,A3;
  end;
  hence thesis by XBOOLE_0:def 1;
end;

theorem
  X (-) {0.REAL n} = X
proof
  thus X (-) {0.REAL n} c= X
  proof
    let x be set; assume x in X (-) {0.REAL n};then
    x in {y where y is Point of TOP-REAL n:{0.REAL n}+y c= X}by Def4;then
    consider y being Point of TOP-REAL n such that
 A3:x=y & {0.REAL n}+y c= X;
    {x} c= X by A3,Th2;
    hence thesis by ZFMISC_1:37;
  end;
  let x be set;assume B1: x in X;then
B2:{x} c= X by ZFMISC_1:37;
   reconsider xx=x as Point of TOP-REAL n by B1;
   {0.REAL n}+xx c= X by B2,Th2;then
   x in {z where z is Point of TOP-REAL n:{0.REAL n}+z c= X};
   hence thesis by Def4;
end;

LLm:{x}+y = {y}+x
proof
  thus {x}+y c= {y}+x
  proof let x1 be set;assume x1 in {x}+y;then
    x1 in {p1+y where p1 is Point of TOP-REAL n:p1 in {x}}by Def2;then
    consider p1 being Point of TOP-REAL n such that
A1: x1=p1+y & p1 in {x};
    {p1} c= {x} by ZFMISC_1:37,A1;then
A3: x1=x+y by A1,ZFMISC_1:24;
    y in {y}by TARSKI:def 1;then
    x1 in {p2+x where p2 is Point of TOP-REAL n:p2 in {y}}by A3;
    hence thesis by Def2;
  end;
  let x1 be set;assume x1 in {y}+x;then
  x1 in {p1+x where p1 is Point of TOP-REAL n:p1 in {y}}by Def2;then
  consider p1 being Point of TOP-REAL n such that
A1:x1=p1+x & p1 in {y};
  {p1} c= {y} by ZFMISC_1:37,A1;then
A3:x1=x+y by A1,ZFMISC_1:24;
   x in {x}by TARSKI:def 1;then
   x1 in {p2+y where p2 is Point of TOP-REAL n:p2 in {x}}by A3;
   hence thesis by Def2;
end;

theorem
  X (+) {0.REAL n} = X
proof
  thus X (+) {0.REAL n} c= X
  proof let x be set; assume x in X (+) {0.REAL n};then
    x in {y+z where y,z is Point of TOP-REAL n:y in X&z in {0.REAL n}}by Def5;
    then consider y,z being Point of TOP-REAL n such that
A3: x=y+z &y in X&z in {0.REAL n};
    {z} c= {0.REAL n} by ZFMISC_1:37,A3;then
    z = 0.REAL n by ZFMISC_1:24;
    hence thesis by A3,EUCLID:31;
  end;
  let x be set; assume
C1:x in X;then
   reconsider x as Point of TOP-REAL n;
   0.REAL n in {0.REAL n}by TARSKI:def 1;then
   x+0.REAL n in {y+z where y,z is Point of TOP-REAL n:
   y in X&z in {0.REAL n}}by C1;then
   x+0.REAL n in X (+) {0.REAL n} by Def5;
   hence thesis by EUCLID:31;
end;

theorem
  X (+) {x} = X+x
proof
  thus X (+) {x} c= X+x
  proof let p be set;assume p in X (+) {x};then
   p in {y+z where y,z is Point of TOP-REAL n:y in X&z in {x}}by Def5;
   then consider y,z being Point of TOP-REAL n such that
A3:p=y+z &y in X&z in {x};
   {z} c= {x} by ZFMISC_1:37,A3;then
   p=y+x by A3,ZFMISC_1:24;then
   p in {q+x where q is Point of TOP-REAL n:q in X}by A3;
   hence thesis by Def2;
  end;
   let p be set;assume p in X+x;then
   p in {q+x where q is Point of TOP-REAL n:q in X}by Def2;then
   consider q being Point of TOP-REAL n such that
C3:p=q+x & q in X;
   x in {x} by TARSKI:def 1;then
   p in {y+z where y,z is Point of TOP-REAL n:y in X&z in {x}}by C3;
   hence thesis by Def5;
end;

theorem Ti2:
  for X,Y st Y = {} holds X (-) Y = REAL n
proof let X,Y;
   assume
AA:Y = {};
   {y where y is Point of TOP-REAL n:Y+y c= X} = REAL n
   proof
   thus {y where y is Point of TOP-REAL n:Y+y c= X} c= REAL n
   proof let x be set;assume
   x in {y where y is Point of TOP-REAL n:Y+y c= X};then
   consider y being Point of TOP-REAL n such that
A3:x=y & Y+y c= X;
   x in the carrier of TOP-REAL n by A3;
   hence thesis by EUCLID:25;
   end;
   let x be set;assume x in REAL n;then
   reconsider a=x as Point of TOP-REAL n by EUCLID:25;
   Y+a = {} by Ti1,AA;then
   Y+a c= X by XBOOLE_1:2;
   hence thesis;
  end;
  hence thesis by Def4;
end;

theorem Th3:
  X c= Y implies X (-) B c= Y (-) B & X (+) B c= Y (+) B
proof
   assume
A1:X c= Y;
   thus X (-) B c= Y (-) B
   proof
    let p be set; assume p in X (-) B;then
 A5:p in {y where y is Point of TOP-REAL n:B+y c= X} by Def4;
    consider p1 being Point of TOP-REAL n such that
 A6:p = p1 & B+p1 c= X by A5;
    p = p1 & B+p1 c= Y by A1,XBOOLE_1:1,A6;then
    p in {y where y is Point of TOP-REAL n:B+y c= Y};
    hence thesis by Def4;
    end;
   let p be set;assume p in X (+) B;then
B5:p in {y+b where y,b is Point of TOP-REAL n:y in X & b in B} by Def5;
   consider p1,p2 being Point of TOP-REAL n such that
B6:p = p1+p2 & p1 in X & p2 in B by B5;
   p in {y+b where y,b is Point of TOP-REAL n:y in Y & b in B} by A1,B6;
   hence thesis by Def5;
end;

theorem Th5:
  B1 c= B2 implies X (-) B2 c= X (-) B1 & X (+) B1 c= X (+) B2
proof assume
A1: B1 c= B2;
   thus X (-) B2 c= X (-) B1
    proof
    let p be set; assume p in X (-) B2;then
 A5:p in {y where y is Point of TOP-REAL n:B2+y c= X} by Def4;
    consider p1 being Point of TOP-REAL n such that
 A6:p = p1 & B2+p1 c= X by A5;
    B1+p1 c= B2 + p1 by Th4,A1;then
    B1+p1 c= X by A6,XBOOLE_1:1;then
    p in {y where y is Point of TOP-REAL n:B1+y c= X} by A6;
    hence thesis by Def4;
    end;
     let p be set; assume p in X (+) B1;then
 A10:p in {x+b where x,b is Point of TOP-REAL n:x in X  & b in B1} by Def5;
    consider x,b being Point of TOP-REAL n such that
 A12:p = x+b & x in X & b in B1 by A10;
    p in {x1+b1 where x1,b1 is Point of TOP-REAL n:x1 in X  & b1 in B2}
      by A1,A12;
    hence thesis by Def5;
end;

theorem
  0.REAL n in B implies X (-) B c= X & X c= X (+) B
proof assume
A1:0.REAL n in B;
   thus X (-) B c= X
   proof
     let p be set; assume p in X (-) B;then
A5:  p in {y where y is Point of TOP-REAL n:B+y c= X} by Def4;
     consider p1 being Point of TOP-REAL n such that
A6:  p = p1 & B+p1 c= X by A5;
     0.REAL n+p1 in {q+p1 where q is Point of TOP-REAL n:q in B} by A1;then
     0.REAL n+p1 in B+p1 by Def2;then
     0.REAL n+p1 in X by A6;
     hence thesis by A6,EUCLID:31;
   end;
   let p be set; assume
A8:p in X;then
   reconsider p as Point of TOP-REAL n;
   p + 0.REAL n in {x+b where x,b is Point of TOP-REAL n:x in X & b in B}
     by A1,A8;then
   p + 0.REAL n in X (+) B by Def5;
   hence thesis by EUCLID:31;
end;

theorem Th8:
  X (+) Y = Y (+) X
proof
  thus X (+) Y c= Y (+) X
  proof
    let p be set;assume p in X (+) Y;then
A2: p in {x+y where x,y is Point of TOP-REAL n:x in X  & y in Y} by Def5;
    consider p1,p2 being Point of TOP-REAL n such that
A4: p = p1+p2 & p1 in X & p2 in Y by A2;
    p in {y+x where y,x is Point of TOP-REAL n:y in Y  & x in X}by A4;
    hence thesis by Def5;
  end;
    let p be set;assume p in Y (+) X;then
A2: p in {x+y where x,y is Point of TOP-REAL n:x in Y  & y in X} by Def5;
    consider p1,p2 being Point of TOP-REAL n such that
A4: p = p1+p2 & p1 in Y & p2 in X by A2;
    p in {y+x where y,x is Point of TOP-REAL n:y in X  & x in Y}by A4;
    hence thesis by Def5;
end;

theorem Th11:
  Y+y c= X+x iff Y+(y-x) c= X
proof
  thus Y+y c= X+x implies Y+(y-x) c= X
  proof
    assume
A01:Y+y c= X+x;
    let p be set;assume p in Y+(y-x);then
    p in {q+(y-x) where q is Point of TOP-REAL n:q in Y} by Def2;then
    consider q1 being Point of TOP-REAL n such that
A05:p = q1 + (y-x) & q1 in Y;
    reconsider p as Point of TOP-REAL n by A05;
    p = q1 + y - x by EUCLID:49,A05;then
A09:p + x = q1 + y by EUCLID:52;
    q1 + y in {q+y where q is Point of TOP-REAL n:q in Y} by A05;then
    p + x in Y+y by A09,Def2;then
    p + x in X + x by A01;then
    p+x in {q+x where q is Point of TOP-REAL n:q in X} by Def2;then
    consider p1 being Point of TOP-REAL n such that
A11:p+x =p1+x & p1 in X;
    p +x-x= p1 by EUCLID:52, A11;
    hence thesis by A11,EUCLID:52;
  end;
  assume
A21:Y+(y-x) c= X;
    let p be set;assume p in Y+y;then
    p in {q+y where q is Point of TOP-REAL n:q in Y} by Def2;then
    consider q1 being Point of TOP-REAL n such that
A25: p = q1 + y & q1 in Y;
    reconsider p as Point of TOP-REAL n by A25;
A27: p-x = q1 + (y -x) by A25,EUCLID:49;
    q1+(y-x) in {q+(y-x) where q is Point of TOP-REAL n:q in Y} by A25;then
    q1+(y-x) in Y+(y-x) by Def2;then
    (p-x)+x in {q+x where q is Point of TOP-REAL n:q in X}by A21,A27;then
    (p-x)+x in X+x by Def2;
    hence thesis by EUCLID:52;
end;

theorem Th12:
  (X+p) (-) Y = (X (-) Y)+p
proof
  thus (X+p) (-) Y c= (X (-) Y)+p
  proof
    let x be set;assume x in (X+p) (-) Y;then
    x in {y1 where y1 is Point of TOP-REAL n:Y+y1 c= X+p} by Def4;then
    consider y being Point of TOP-REAL n such that
A03: x = y & Y+y c= X+p;
    Y+(y-p) c= X by Th11,A03;then
    y-p in {y1 where y1 is Point of TOP-REAL n:Y+y1 c= X};then
    y-p in X (-) Y by Def4;then
    (y-p)+p in {q+p where q is Point of TOP-REAL n:q in X (-) Y};then
    (y-p)+p in (X (-) Y)+p by Def2;
    hence thesis by A03,EUCLID:52;
  end;
  let x be set;assume x in (X (-) Y)+p;then
  x in {y1+p where y1 is Point of TOP-REAL n:y1 in X (-) Y} by Def2;then
  consider y being Point of TOP-REAL n such that
A13: x = y+p & y in X (-) Y;
  reconsider x as Point of TOP-REAL n by A13;
A15: x-p = y by A13,EUCLID:52;
  y in {y1 where y1 is Point of TOP-REAL n:Y+y1 c= X} by Def4,A13;then
  consider y2 being Point of TOP-REAL n such that
A17:y = y2 & Y+y2 c= X;
   Y + x c= X + p by Th11,A15,A17;then
   x in {y1 where y1 is Point of TOP-REAL n:Y+y1 c= X+p};
   hence thesis by Def4;
end;

theorem Th13:
  (X+p) (+) Y = (X (+) Y)+p
proof
  thus (X+p) (+) Y c= (X (+) Y)+p
  proof
    let x be set;assume x in (X+p) (+) Y;then
    x in {x1+y1 where x1,y1 is Point of TOP-REAL n:x1 in X+p & y1 in Y}
      by Def5;then
    consider x2,y2 being Point of TOP-REAL n such that
A03: x = x2+y2 & x2 in X+p & y2 in Y;
    x2 in {x4+p where x4 is Point of TOP-REAL n:x4 in X} by Def2,A03;then
    consider x3 being Point of TOP-REAL n such that
A05: x2 = x3+p & x3 in X;
A07: x=x3+y2+p by EUCLID:30,A03,A05;
    x3+y2 in {x1+y1 where x1,y1 is Point of TOP-REAL n:x1 in X & y1 in Y}
      by A03,A05;then
    x3+y2 in X (+) Y by Def5;then
    x in {y1+p where y1 is Point of TOP-REAL n:y1 in X (+) Y} by A07;
    hence thesis by Def2;
  end;
  let x be set;assume x in (X (+) Y)+p;then
   x in {x1+p where x1 is Point of TOP-REAL n:x1 in X (+) Y} by Def2;then
   consider x2 being Point of TOP-REAL n such that
A13: x = x2+p & x2 in X (+) Y;
   x2 in {x4+y4 where x4,y4 is Point of TOP-REAL n:x4 in X & y4 in Y}
      by Def5,A13;then
   consider x3,y3 being Point of TOP-REAL n such that
A15: x2 = x3+y3 & x3 in X & y3 in Y;
A17: x=x3+p+y3 by EUCLID:30,A13,A15;
    x3+p in {x1+p where x1 is Point of TOP-REAL n:x1 in X } by A15;then
    x3+p in X+p by Def2;then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X+p & y2 in Y}
      by A17,A15;
    hence thesis by Def5;
  end;

theorem Th14:
  (X+x)+y = X+(x+y)
proof
  thus X+x+y c= X+(x+y)
  proof
    let p be set; assume p in (X+x)+y;then
    p in {x1+y where x1 is Point of TOP-REAL n:x1 in X+x} by Def2;then
    consider x2 being Point of TOP-REAL n such that
 A3:p = x2+y & x2 in X+x;
    x2 in {x1+x where x1 is Point of TOP-REAL n:x1 in X} by Def2,A3;then
    consider x3 being Point of TOP-REAL n such that
 A5:x2 = x3+x & x3 in X;
    p=x3+(x+y) by A3,A5,EUCLID:30;then
    p in {x1+(x+y) where x1 is Point of TOP-REAL n:x1 in X} by A5;
    hence thesis by Def2;
  end;
   let p be set; assume p in X+(x+y);then
   p in {x1+(x+y) where x1 is Point of TOP-REAL n:x1 in X} by Def2;then
   consider x2 being Point of TOP-REAL n such that
A13:p = x2+(x+y) & x2 in X;
A14:p = x2 + x + y by EUCLID:30,A13;
   x2+x in {x1+x where x1 is Point of TOP-REAL n:x1 in X} by A13;then
   x2+x in X+x by Def2;then
   p in {x1+y where x1 is Point of TOP-REAL n:x1 in X+x} by A14;
   hence thesis by Def2;
end;

theorem Th15:
  X (-) (Y+p) = (X (-) Y)+(-p)
proof
 thus X (-) (Y+p) c= (X (-) Y)+(-p)
 proof
   let x be set;assume x in X (-) (Y+p);then
     x in {y1 where y1 is Point of TOP-REAL n:(Y+p)+y1 c= X} by Def4;then
     consider y being Point of TOP-REAL n such that
 A03: x = y & (Y+p)+y c= X;
     Y+(y+p) c= X by Th14,A03;then
     y+p in {y1 where y1 is Point of TOP-REAL n:Y+y1 c= X};then
     y+p in X (-) Y by Def4;then
     y+p+(-p) in {y1+(-p) where y1 is Point of TOP-REAL n:y1 in X (-) Y};then
     y+p+(-p) in (X (-) Y)+(-p) by Def2;then
     y+p-p in (X (-) Y)+(-p) by EUCLID:45;
     hence thesis by A03,EUCLID:52;     
  end;
   let x be set;assume x in (X (-) Y)+(-p);then
    x in {y1+(-p) where y1 is Point of TOP-REAL n:y1 in X (-) Y} by Def2;then
    consider y being Point of TOP-REAL n such that
A13: x = y+(-p) & y in X (-) Y;
    reconsider x as Point of TOP-REAL n by A13;
    x+p = y-p+p by A13,EUCLID:45;then
A15: x+p =y by EUCLID:52;
    y in {y1 where y1 is Point of TOP-REAL n:Y+y1 c= X} by Def4,A13;then
    consider y2 being Point of TOP-REAL n such that
A17:y = y2 & Y+y2 c= X;
    Y+p+x c= X by Th14,A15,A17;then
    x in {y1 where y1 is Point of TOP-REAL n:(Y+p)+y1 c= X};
    hence thesis by Def4;
end;

theorem
  X (+) (Y+p) = (X (+) Y)+p
proof
  thus X (+) (Y+p) c= (X (+) Y)+p
  proof
    let x be set;assume x in X (+) (Y+p);then
    x in {x1+y1 where x1,y1 is Point of TOP-REAL n:x1 in X & y1 in Y+p}
                                                          by Def5;then
    consider x2,y2 being Point of TOP-REAL n such that
A03: x = x2+y2 & x2 in X & y2 in Y+p;
    y2 in {y4+p where y4 is Point of TOP-REAL n:y4 in Y} by Def2,A03;then
    consider y3 being Point of TOP-REAL n such that
A05: y2 = y3+p & y3 in Y;
A07: x=x2+y3+p by EUCLID:30,A03,A05;
    x2+y3 in {x1+y1 where x1,y1 is Point of TOP-REAL n:x1 in X & y1 in Y}
      by A03,A05;then
    x2+y3 in X (+) Y by Def5;then
    x in {y1+p where y1 is Point of TOP-REAL n:y1 in X (+) Y} by A07;
    hence thesis by Def2;
  end;
   let x be set;assume x in (X (+) Y)+p;then
    x in {x1+p where x1 is Point of TOP-REAL n:x1 in X (+) Y} by Def2;then
    consider x2 being Point of TOP-REAL n such that
A13: x = x2+p & x2 in X (+) Y;
    x2 in {x4+y4 where x4,y4 is Point of TOP-REAL n:x4 in X & y4 in Y}
      by Def5,A13;then
    consider x3,y3 being Point of TOP-REAL n such that
A15: x2 = x3+y3 & x3 in X & y3 in Y;
A17: x=x3+(y3+p) by EUCLID:30,A13,A15;
    y3+p in {x1+p where x1 is Point of TOP-REAL n:x1 in Y } by A15;then
    y3+p in Y+p by Def2;then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X & y2 in Y+p}
      by A17,A15;
    hence thesis by Def5;
end;

theorem Th24:
  x in X implies B+x c= B (+) X
proof assume
A1: x in X;
    let y be set;assume y in B+x;then
    y in {y1+x where y1 is Point of TOP-REAL n:y1 in B} by Def2;then
    consider y1 being Point of TOP-REAL n such that
 A3:y = y1+x & y1 in B;
    y in {y2+x2 where y2,x2 is Point of TOP-REAL n:y2 in B & x2 in X}by A3,A1;
    hence thesis by Def5;
  end;

theorem Th240:
  X c= (X (+) B) (-) B
proof
    let x be set;assume
A1: x in X;then
    consider x1 being Point of TOP-REAL n such that
A3: x1=x;
    B+x1 c= B (+) X by Th24,A1,A3;then
    x1 in {y where y is  Point of TOP-REAL n:B+y c= B (+) X};then
    x in (B (+) X) (-) B by A3,Def4;
    hence thesis by Th8;  
end;

theorem Tha:
  X+0.REAL n = X
proof
  thus X+0.REAL n c= X   
  proof
    let x be set;assume
    x in X+0.REAL n;then
    x in {q+0.REAL n where q is Point of TOP-REAL n:q in X} by Def2;then
    consider q being Point of TOP-REAL n such that
 A1:x = q +0.REAL n& q in X;
    thus thesis by EUCLID:31,A1;
   end;
   let x be set;assume
A3: x in X;then reconsider x1=x as Point of TOP-REAL n;
    x1=x1+0.REAL n by EUCLID:31;then
    x1 in {q+0.REAL n where q is Point of TOP-REAL n:q in X} by A3;
    hence thesis by Def2;
end;

theorem
  X (-) {x} = X+(-x)
proof
  thus X (-) {x} c= X+(-x)
  proof
    let y be set;assume y in X (-) {x};then
    y in {p where p is Point of TOP-REAL n:{x}+p c= X}by Def4;then
    consider p being Point of TOP-REAL n such that
 A3:p=y & {x}+p c= X;
    {p}+x c= X by A3,LLm;then
    {p}+x+(-x) c= X+(-x) by Th4;then
    {p}+(x+(-x)) c= X+(-x) by Th14;then
    {p}+0.REAL n c= X+(-x) by EUCLID:40;then
    {p} c= X+(-x) by Tha;
    hence thesis by ZFMISC_1:37,A3;
   end;
   let y be set;assume y in X+(-x);then
   y in {p+(-x) where p is Point of TOP-REAL n:p in X}by Def2;then
   consider p being Point of TOP-REAL n such that
B3:y=p+(-x) & p in X;
   reconsider y as Point of TOP-REAL n by B3;
   y = p - x by EUCLID:45,B3;then
B5:y+x = p by EUCLID:52;
   {x}+y c= X
   proof
     let q be set;assume q in {x}+y;then
     q in {qq+y where qq is Point of TOP-REAL n:qq in {x}}by Def2;then
     consider qq being Point of TOP-REAL n such that
C1:  q=qq+y & qq in {x};
     {qq} c= {x} by C1,ZFMISC_1:37;
     hence thesis by B3,B5,C1,ZFMISC_1:24;
   end;then
   y in {yy where yy is Point of TOP-REAL n:{x}+yy c= X};
   hence thesis by Def4;
end;

Lm9: (X (-) B) (+) B c= X
    proof
    let x be set;assume
    x in (X (-) B) (+) B;then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X (-) B & y2 in B}
      by Def5;then
    consider y1,y2 being Point of TOP-REAL n such that
 A3:x=y1+y2 & y1 in X (-) B & y2 in B;
    y1 in {y where y is Point of TOP-REAL n:B+y c= X} by A3,Def4;then
    consider y being Point of TOP-REAL n such that
 A5:y1=y & B+y c= X;
    y2+y in {y3+y where y3 is Point of TOP-REAL n:y3 in B} by A3;then
    x in B+y by A3,A5,Def2;
    hence thesis by A5;
end;

theorem Th9:
  X (-) (Y (+) Z) = (X (-) Y) (-) Z
proof
  thus X (-) (Y (+) Z) c= (X (-) Y) (-) Z
   proof
    let p be set;assume p in X (-) (Y (+) Z);then
A2: p in {y where y is Point of TOP-REAL n:(Y (+) Z)+y c= X} by Def4;
    consider x being Point of TOP-REAL n such that
A3: p = x & (Y (+) Z)+x c= X by A2;
    (Y+x) (+) Z c= X by Th13,A3;then
    Z (+) (Y+x) c= X by Th8;then
A5: (Z (+) (Y+x)) (-) (Y+x) c= X (-) (Y+x) by Th3;
    Z c= (Z (+) (Y+x)) (-) (Y+x) by Th240;then
    Z c= X (-) (Y+x) by A5,XBOOLE_1:1;then
    Z c= (X (-) Y)+(-x) by Th15;then
    Z+x c= (X (-) Y)+(-x)+x by Th4;then
    Z+x c= (X (-) Y)+((-x)+x) by Th14;then
    Z+x c= (X (-) Y)+(x-x) by EUCLID:45;then
    Z+x c= (X (-) Y)+0.REAL n by EUCLID:46;then
    Z+x c= X (-) Y by Tha;then
    p in {y where y is Point of TOP-REAL n:Z+y c= X (-) Y}by A3;
    hence thesis by Def4;
   end;
   let p be set;assume p in (X (-) Y) (-) Z;then
   p in {y where y is Point of TOP-REAL n:Z+y c= X (-) Y} by Def4;then
   consider y being Point of TOP-REAL n such that
A13: p = y & Z+y c= X (-) Y;
A15: (Z+y) (+) Y c= (X (-) Y) (+) Y by Th3,A13;
   (X (-) Y) (+) Y c= X by Lm9;then
   (Z+y) (+) Y c= X by A15,XBOOLE_1:1;then
   (Z (+) Y)+y c= X by Th13;then
   p in {y5 where y5 is Point of TOP-REAL n:(Z (+) Y)+y5 c= X} by A13;then
   p in X (-) (Z (+) Y) by Def4;
   hence thesis by Th8;
end;

theorem
  X (-) (Y (+) Z) = (X (-) Z) (-) Y
proof
  X (-) (Y (+) Z) = X (-) (Z (+) Y) by Th8
     .=X (-) Z (-) Y by Th9;
  hence thesis;
end;

theorem
  X (+) (Y (-) Z) c= (X (+) Y) (-) Z
proof
  let x be set;assume x in X (+) (Y (-) Z);then
    x in {a+b where a,b is Point of TOP-REAL n:a in X & b in Y (-) Z}by Def5;
   then consider a,b being Point of TOP-REAL n such that
 A3:x=a+b & a in X & b in Y (-) Z;
    b in {c where c is Point of TOP-REAL n:Z+c c= Y}by A3,Def4;then
    consider c being Point of TOP-REAL n such that
 A5:b=c & Z+c c= Y;
    Z+b+a c= Y+a by A5,Th4;then
 A6:Z+(b+a) c= Y+a by Th14;
    Y+a c= Y (+) X by A3,Th24;then
    Z+(b+a) c= Y (+) X by XBOOLE_1:1,A6;then
    x in {d where d is Point of TOP-REAL n:Z+d c= Y (+) X}by A3;then
    x in (Y (+) X) (-) Z by Def4;
    hence thesis by Th8;
end;

theorem
  X (+) (Y (+) Z) = (X (+) Y) (+) Z
proof
  thus X (+) ( Y (+) Z) c= (X (+) Y) (+) Z
  proof
    let p be set;assume
    p in X (+) ( Y (+) Z);then
    p in {x+p1 where x,p1 is Point of TOP-REAL n:x in X & p1 in Y (+) Z}
      by Def5;then
     consider x1,p2 being Point of TOP-REAL n such that
  A4:p = x1+p2 & x1 in X & p2 in Y (+) Z;
     p2 in {y1+z1 where y1,z1 is Point of TOP-REAL n:y1 in Y & z1 in Z}
      by A4,Def5;then
     consider y,z being Point of TOP-REAL n such that
  A6:p2=y+z & y in Y & z in Z;
  A8:p=x1+y+z by A4,A6,EUCLID:30;
     set p3=x1+y;
     p3 in {x2+y2 where x2,y2 is Point of TOP-REAL n:x2 in X & y2 in Y} 
      by A4,A6;then
     p3 in X (+) Y by Def5;then
     p in {q2+z2 where q2,z2 is Point of TOP-REAL n:q2 in X (+) Y & z2 in Z}
      by A6,A8;
     hence thesis by Def5;
 end;
   let p be set;assume p in (X (+) Y) (+) Z;then
   p in {x+p1 where x,p1 is Point of TOP-REAL n:x in X (+) Y  & p1 in Z}
     by Def5;then
    consider x1,p2 being Point of TOP-REAL n such that
A41:p = x1+p2 & x1 in X (+) Y & p2 in Z;
    x1 in {y1+z1 where y1,z1 is Point of TOP-REAL n:y1 in X  & z1 in Y}
      by A41,Def5;then
    consider y,z being Point of TOP-REAL n such that
A61:x1=y+z & y in X & z in Y;
A81:p=y+(z+p2) by A41,A61,EUCLID:30;
    set p3=z+p2;
    p3 in {x2+y2 where x2,y2 is Point of TOP-REAL n:x2 in Y & y2 in Z}
      by A41,A61;then
    p3 in Y (+) Z by Def5;then
    p in {q2+z2 where q2,z2 is Point of TOP-REAL n:q2 in X & z2 in Y (+) Z}
      by A61,A81;
    hence thesis by Def5;
end;

theorem Th17:
  (B\/C)+y = (B+y) \/ (C+y)
 proof
   thus (B\/C)+y c= (B+y) \/ (C+y)
   proof let x be set;assume x in (B\/C)+y;then
     x in {y1+y where y1 is Point of TOP-REAL n:y1 in B\/C} by Def2;then
     consider y2 being Point of TOP-REAL n such that
  A3:x=y2+y & y2 in B\/C;
     y2 in B or y2 in C by XBOOLE_0:def 2,A3;then
     x in {y1+y where y1 is Point of TOP-REAL n:y1 in B} or
     x in {y1+y where y1 is Point of TOP-REAL n:y1 in C} by A3;then
     x in B+y or x in C+y by Def2;
     hence thesis by XBOOLE_0:def 2;
   end;
    let x be set;assume x in (B+y) \/ (C+y);then
    x in B+y or x in C+y by XBOOLE_0:def 2;then
    x in {y1+y where y1 is Point of TOP-REAL n:y1 in B} or
    x in {y1+y where y1 is Point of TOP-REAL n:y1 in C} by Def2;then
    consider y2 being Point of TOP-REAL n such that
A15:x=y2+y & y2 in B or x=y2+y & y2 in C;
    y2 in B\/C by XBOOLE_0:def 2,A15;then
    x in {y1+y where y1 is Point of TOP-REAL n:y1 in B\/C} by A15;
    hence thesis by Def2;
end;

theorem Th170:
  (B/\C)+y = (B+y) /\ (C+y)
proof
  thus (B/\C)+y c= (B+y) /\ (C+y)
  proof let x be set;assume x in (B/\C)+y;then
    x in {y1+y where y1 is Point of TOP-REAL n:y1 in B/\C} by Def2;then
    consider y2 being Point of TOP-REAL n such that
 A3:x=y2+y & y2 in B/\C;
    y2 in B & y2 in C by XBOOLE_0:def 3,A3;then
    x in {y1+y where y1 is Point of TOP-REAL n:y1 in B} &
    x in {y1+y where y1 is Point of TOP-REAL n:y1 in C} by A3;then
    x in B+y & x in C+y by Def2;
    hence thesis by XBOOLE_0:def 3;
  end;
    let x be set;assume x in (B+y) /\ (C+y);then
    x in B+y & x in C+y by XBOOLE_0:def 3;then
A13:x in {y1+y where y1 is Point of TOP-REAL n:y1 in B} &
    x in {y1+y where y1 is Point of TOP-REAL n:y1 in C} by Def2;then
    consider y3 being Point of TOP-REAL n such that
A15:x=y3+y & y3 in B;
    consider y2 being Point of TOP-REAL n such that
A17:x=y2+y & y2 in C by A13;
    y2+y-y=y3 by A15,A17,EUCLID:52;then
A19:y2=y3 by EUCLID:52;then
    y2 in B/\C by XBOOLE_0:def 3,A15,A17;then
    x in {y1+y where y1 is Point of TOP-REAL n:y1 in B/\C}by A15,A19;
    hence thesis by Def2;
end;

theorem
  X (-) (B\/C) = (X (-) B)/\(X (-) C)
proof
  thus X (-) (B\/C) c= (X (-) B)/\(X (-) C)
  proof
    let x be set;assume x in X (-) (B\/C);then
    x in {y1 where y1 is Point of TOP-REAL n:(B\/C)+y1 c= X} by Def4;then
    consider y being Point of TOP-REAL n such that
 A3:x = y & (B\/C)+y c= X;
    (B+y)\/(C+y) c= X by Th17,A3;then
    B+y c= X & C+y c= X by XBOOLE_1:11;then
    x in {y1 where y1 is Point of TOP-REAL n:B+y1 c= X} &
    x in {y1 where y1 is Point of TOP-REAL n:C+y1 c= X} by A3; then
    x in X (-) B & x in X (-) C by Def4;
    hence thesis by XBOOLE_0:def 3;
  end;
    let x be set;assume x in (X (-) B)/\(X (-) C);then
    x in X (-) B & x in X (-) C by XBOOLE_0:def 3;then
A13:x in {y1 where y1 is Point of TOP-REAL n:B+y1 c= X} &
    x in {y1 where y1 is Point of TOP-REAL n:C+y1 c= X} by Def4;then
    consider y being Point of TOP-REAL n such that
A15:x = y & B+y c= X;
    consider y2 being Point of TOP-REAL n such that
A17:x = y2 & C+y2 c= X by A13;
    (B+y)\/(C+y) c= X by A15,XBOOLE_1:8,A17;then
    (B\/C)+y c= X by Th17;then
    x in {y1 where y1 is Point of TOP-REAL n:(B\/C)+y1 c= X} by A15;
    hence thesis by Def4;
end;

theorem
  X (+) (B\/C) = (X (+) B)\/(X (+) C)
proof
  thus X (+) (B\/C) c= (X (+) B)\/(X (+) C)
  proof
    let x be set;assume x in X (+) (B\/C);then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X & y2 in (B\/C)}
                                                            by Def5;then
    consider y3,y4 being Point of TOP-REAL n such that
 A3:x = y3+y4 & y3 in X & y4 in (B\/C);
    y4 in B or y4 in C by XBOOLE_0:def 2,A3;then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X & y2 in B} or
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X & y2 in C}
                                                             by A3;then
    x in X (+) B or x in X (+) C by Def5;
    hence thesis by XBOOLE_0:def 2;
 end;
    let x be set;assume x in (X (+) B)\/(X (+) C);then
    x in X (+) B or x in X (+) C by XBOOLE_0:def 2;then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X & y2 in B} or
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X & y2 in C}
                                                         by Def5;then
    consider y3,y4 being Point of TOP-REAL n such that
A13:x=y3+y4 & y3 in X & y4 in B or x=y3+y4 & y3 in X & y4 in C;
    y4 in B\/C by A13,XBOOLE_0:def 2;then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X & y2 in B\/C}
                                                          by A13;
    hence thesis by Def5;
end;

theorem
  (X (-) B)\/(Y (-) B) c= (X\/Y) (-) B
proof
  let x be set;assume x in (X (-) B)\/(Y (-) B);then
  x in X (-) B or x in Y (-) B by XBOOLE_0:def 2;then
  x in {y where y is Point of TOP-REAL n:B+y c= X} or
  x in {y where y is Point of TOP-REAL n:B+y c= Y} by Def4; then
  consider y being Point of TOP-REAL n such that
A3:x=y & B+y c= X or x=y & B+y c= Y;
  B+y c= X \/ Y
  proof let z be set;assume z in B+y;
    hence thesis by XBOOLE_0:def 2,A3;
  end;then
  x in {y1 where y1 is Point of TOP-REAL n:B+y1 c= X \/ Y} by A3;
  hence thesis by Def4;
end;

theorem
  (X\/Y) (+) B = (X (+) B)\/(Y (+) B)
proof
   thus (X\/Y) (+) B c= (X (+) B)\/(Y (+) B)
   proof let x be set; assume x in (X\/Y) (+) B;then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X\/Y & y2 in B}
                                                       by Def5;then
    consider y1,y2 being Point of TOP-REAL n such that
 A3:x=y1+y2 & y1 in X\/Y & y2 in B;
    y1 in X or y1 in Y by XBOOLE_0:def 2,A3;then
    x in {y3+y4 where y3,y4 is Point of TOP-REAL n:y3 in X & y4 in B} or
    x in {y3+y4 where y3,y4 is Point of TOP-REAL n:y3 in Y & y4 in B}
                                                by A3;then
    x in X (+) B or x in Y (+) B by Def5;
    hence thesis by XBOOLE_0:def 2;
   end;
    let x be set; assume x in (X (+) B)\/(Y (+) B);then
    x in X (+) B or x in Y (+) B by XBOOLE_0:def 2;then
    x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X & y2 in B}
 or x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in Y & y2 in B}
                                                      by Def5;then
    consider y1,y2 being Point of TOP-REAL n such that
A13:x=y1+y2 & y1 in X & y2 in B or x=y1+y2 & y1 in Y & y2 in B;
    y1 in X\/Y by XBOOLE_0:def 2,A13;then
    x in {y3+y4 where y3,y4 is Point of TOP-REAL n:y3 in X\/Y & y4 in B}by A13;
   hence thesis by Def5;
end;

theorem
  (X/\Y) (-) B = (X (-) B)/\(Y (-) B)
proof
   thus (X/\Y) (-) B c= (X (-) B)/\(Y (-) B)  
   proof
   let x be set;assume
   x in (X/\Y) (-) B;then
   x in {y where y is Point of TOP-REAL n:B+y c= X/\Y} by Def4; then
   consider y being Point of TOP-REAL n such that
A3:x=y & B+y c= X/\Y;
   B+y c= X & B+y c= Y by XBOOLE_1:18,A3;then
   x in {y1 where y1 is Point of TOP-REAL n:B+y1 c= X} &
   x in {y1 where y1 is Point of TOP-REAL n:B+y1 c= Y} by A3;then
   x in X (-) B & x in Y (-) B by Def4;
   hence thesis by XBOOLE_0:def 3;
   end;
    let x be set;assume x in (X (-) B)/\(Y (-) B);then
    x in X (-) B & x in Y (-) B by XBOOLE_0:def 3;then
A11:x in {y where y is Point of TOP-REAL n:B+y c= X} &
    x in {y where y is Point of TOP-REAL n:B+y c= Y} by Def4; then
    consider y being Point of TOP-REAL n such that
A13:x=y & B+y c= X;
    consider y2 being Point of TOP-REAL n such that
A15:x=y2 & B+y2 c= Y by A11;
    B+y c= X/\Y
    proof let z be set;
      assume z in B+y;
      hence thesis by XBOOLE_0:def 3,A13,A15;
    end;then
    x in {y1 where y1 is Point of TOP-REAL n:B+y1 c= X/\Y} by A13;
    hence thesis by Def4;
end;

theorem Th23:
  (X/\Y) (+) B c= (X (+) B)/\(Y (+) B)
proof
  let x be set;assume x in (X/\Y) (+) B;then
  x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X/\Y & y2 in B}
   by Def5;then
  consider y1,y2 being Point of TOP-REAL n such that
A3:x=y1+y2 & y1 in X/\Y & y2 in B;
   y1 in X & y1 in Y by A3,XBOOLE_0:def 3;then
   x in {y3+y4 where y3,y4 is Point of TOP-REAL n:y3 in X & y4 in B} &
   x in {y3+y4 where y3,y4 is Point of TOP-REAL n:y3 in Y & y4 in B}
                                                       by A3;then
   x in X (+) B & x in Y (+) B by Def5;
   hence thesis by XBOOLE_0:def 3;
end;

theorem
  B (+) (X/\Y) c= (B (+) X)/\(B (+) Y)
proof
  B (+) (X/\Y) = (X/\Y) (+) B by Th8;then
  B (+) (X/\Y) c= (X (+) B)/\(Y (+) B) by Th23;then
  B (+) (X/\Y) c= (B (+) X)/\(Y (+) B) by Th8;
  hence thesis by Th8;
end;

theorem
  (B (-) X)\/(B (-) Y) c= B (-) (X/\Y)
proof
  let x be set;assume x in (B (-) X)\/(B (-) Y);then
  x in B (-) X or x in B (-) Y by XBOOLE_0:def 2;then
  x in {y where y is Point of TOP-REAL n:X+y c= B} or
  x in {y where y is Point of TOP-REAL n:Y+y c= B} by Def4; then
  consider y being Point of TOP-REAL n such that
A13:x=y & X+y c= B or x=y & Y+y c= B;
  (X+y)/\(Y+y) c= B
  proof
    let a be set;assume a in (X+y)/\(Y+y);then
B1: a in X+y & a in Y+y by XBOOLE_0:def 3;
    per cases by A13;
    suppose X+y c= B;
      hence thesis by B1;
    end;
    suppose Y+y c= B;
      hence thesis by B1;
    end;
  end;then
  (X/\Y)+y c= B by Th170;then
  x in {y5 where y5 is Point of TOP-REAL n:(X/\Y)+y5 c= B}by A13;
  hence thesis by Def4;
end;

theorem Y1:
  (X` (-) B)` = X (+) B!
proof
  thus ((X`) (-) B)` c= X (+) B!
  proof
   let x be set;assume
aa: x in ((X`) (-) B)`;then
    x in (the carrier of TOP-REAL n)\((X`) (-) B) by SUBSET_1:def 5;then
 A2:x in the carrier of TOP-REAL n & not x in (X`) (-) B by XBOOLE_0:def 4;
    reconsider x1=x as Point of TOP-REAL n by aa;
    not B+x1 c= X`
    proof assume B+x1 c= X`;then
    x in {y where y is Point of TOP-REAL n:B+y c= X`};
    hence contradiction by A2,Def4;
    end;then
    B+x1 meets X by SUBSET_1:43;then
    consider y being set such that
A13: y in (B+x1) & y in X by XBOOLE_0:3;
    y in {b1+x1 where b1 is Point of TOP-REAL n:b1 in B} by A13,Def2;then
    consider b1 being Point of TOP-REAL n such that
A15:y = b1+x1 & b1 in B;
    reconsider y1=y as Point of TOP-REAL n by A13;
    x1 = y1 - b1 by EUCLID:52,A15;then
A17:x1 = y1 + (-b1) by EUCLID:45;
    -b1 in {-q where q is Point of TOP-REAL n:q in B}by A15;then
    -b1 in B! by Def3;then
    y1+(-b1) in {y2+b2 where y2,b2 is Point of TOP-REAL n:y2 in X&b2 in B!}
             by A13;
    hence thesis by Def5,A17;   
  end;
  let x be set;
  assume x in X (+) B!;then
  x in {x1+b1 where x1,b1 is Point of TOP-REAL n:x1 in X & b1 in B!}by Def5;
  then consider x1,b1 being Point of TOP-REAL n such that
 B1:x=x1+b1 & x1 in X & b1 in B!;
   b1 in {-q where q is Point of TOP-REAL n:q in B}by B1,Def3;then
   consider b2 being Point of TOP-REAL n such that
 B3:b1=-b2 & b2 in B;
 B5:x=x1-b2 by EUCLID:45,B1,B3;
    reconsider xx=x as Point of TOP-REAL n by B1;
 B7:xx+b2=x1 by EUCLID:52,B5;
    b2+xx in {pb+xx where pb is Point of TOP-REAL n:pb in B}by B3;then
    x1 in B+xx by B7,Def2;then
b11:B+xx meets X by B1,XBOOLE_0:3;
 B15:not xx in ((X`) (-) B)
     proof
     assume xx in ((X`) (-) B);then
     xx in {y where y is Point of TOP-REAL n:B+y c= X`}by Def4;then
     consider yy being Point of TOP-REAL n such that
  C1:xx=yy & B+yy c= X`;
     thus contradiction by b11,C1,SUBSET_1:43;
     end;
     xx in (the carrier of TOP-REAL n)\((X`) (-) B) by B15,XBOOLE_0:def 4;
  hence thesis by SUBSET_1:def 5;
end;

theorem Y2:
  (X (-) B)` = X` (+) B!
proof
  thus (X (-) B)` c= X` (+) B!
  proof
   let x be set;assume
aa: x in (X (-) B)`;then
    x in (the carrier of TOP-REAL n)\(X (-) B) by SUBSET_1:def 5;then
 A2:x in (the carrier of TOP-REAL n)&not x in X (-) B by XBOOLE_0:def 4;
    reconsider x1=x as Point of TOP-REAL n by aa;
    not B+x1 c= X 
    proof assume B+x1 c= X;then
    x in {y where y is Point of TOP-REAL n:B+y c= X};
    hence contradiction by A2,Def4;
    end;then
    B+x1 meets X` by SUBSET_1:44;then
    consider y being set such that
 A13: y in B+x1 & y in X` by XBOOLE_0:3;
    y in {b1+x1 where b1 is Point of TOP-REAL n:b1 in B} by A13,Def2;then
    consider b1 being Point of TOP-REAL n such that
 A15:y = b1+x1 & b1 in B;
    reconsider y1=y as Point of TOP-REAL n by A13;
    x1 = y1 - b1 by EUCLID:52,A15;then
 A17:x1 = y1 + (-b1) by EUCLID:45;
    -b1 in {-q where q is Point of TOP-REAL n:q in B}by A15;then
    -b1 in B! by Def3;then
    y1+(-b1) in {y2+b2 where y2,b2 is Point of TOP-REAL n:y2 in X`&b2 in B!}
             by A13;
    hence thesis by Def5,A17;
  end;
   let x be set;assume
   x in X` (+) B!;then
   x in {x1+b1 where x1,b1 is Point of TOP-REAL n:x1 in X` & b1 in B!}by Def5;
   then consider x1,b1 being Point of TOP-REAL n such that 
 B1:x=x1+b1 & x1 in X` & b1 in B!;
   reconsider xx=x as  Point of TOP-REAL n by B1;
   b1 in {-q where q is Point of TOP-REAL n:q in B}by Def3,B1;then
   consider q being Point of TOP-REAL n such that 
 B3:b1=-q & q in B;
   xx=x1-q by EUCLID:45,B1,B3;then
 B5:xx+q=x1 by EUCLID:52;
   q+xx in {q1+xx where q1 is Point of TOP-REAL n:q1 in B}by B3;then
   x1 in B+xx by B5,Def2;then
b11: B+xx meets X` by B1,XBOOLE_0:3;
 B15:not xx in (X (-) B)
     proof
     assume xx in (X (-) B);then
     xx in {y where y is Point of TOP-REAL n:B+y c= X}by Def4;then
     consider yy being Point of TOP-REAL n such that
    C1:xx=yy & B+yy c= X;
     thus contradiction by b11,C1,SUBSET_1:44;
     end;
     xx in (the carrier of TOP-REAL n)\(X (-) B) by B15,XBOOLE_0:def 4;
  hence thesis by SUBSET_1:def 5;  
  end;

begin
:: 2. The definition of Adjunction Opening and Closing and
::    their Algebraic Properties

:: Adjunction Opening

definition let n be Nat,X,B be Subset of TOP-REAL n;
  func X (O) B -> Subset of TOP-REAL n equals
   :Def6: (X (-) B) (+) B;
  coherence;
end;

:: Adjunction Closing

definition let n be Nat,X,B be Subset of TOP-REAL n;
  func X (o) B -> Subset of TOP-REAL n equals
   :Def7: (X (+) B) (-) B;
  coherence;
end;

theorem 
  (X` (O) B!)` = X (o) B
proof
  (X` (O) B!)` = (X` (-) B! (+) B!)` by Def6
    .=(((X` (-) B!)` (-) B)`)` by Y1
    .=X (+) (B!)! (-) B by Y1
    .=X (+) B (-) B by Th1;
  hence thesis by Def7;
end;

theorem
  (X` (o) B!)` = X (O) B
proof
  (X` (o) B!)` = (X` (+) B! (-) B!)` by Def7
    .=(X` (+) B!)` (+) (B!)! by Y2
    .=(X` (+) B!)` (+) B by Th1
    .=(X (-) B)`` (+) B by Y2;
  hence thesis by Def6;
end;

theorem Th25:
  X (O) B c= X & X c= X (o) B
proof
  thus X (O) B c= X
  proof
  let x be set;assume x in X (O) B;then
  x in (X (-) B) (+) B by Def6;then
  x in {y1+y2 where y1,y2 is Point of TOP-REAL n:y1 in X (-) B & y2 in B}
                                      by Def5;then
  consider y1,y2 being Point of TOP-REAL n such that
A3:x=y1+y2 & y1 in X (-) B & y2 in B;
  y1 in {y where y is Point of TOP-REAL n:B+y c= X} by A3,Def4;then
  consider y being Point of TOP-REAL n such that
A5:y1=y & B+y c= X;
  y2+y in {y3+y where y3 is Point of TOP-REAL n:y3 in B} by A3;then
  x in B+y by A3,A5,Def2;
  hence thesis by A5;
  end;
  X c= (X (+) B) (-) B by Th240;
  hence thesis by Def7;
end;

theorem Thb:
  X (O) X = X
proof
  thus X (O) X c= X by Th25;
  let x be set;assume
A1:x in X;then reconsider x1=x as Point of TOP-REAL n;
   X+0.REAL n c= X by Tha;then
   0.REAL n in {x2 where x2 is Point of TOP-REAL n:X+x2 c= X};then
   0.REAL n in X (-) X by Def4;then
   0.REAL n+x1 in {a+b where a,b is Point of TOP-REAL n:a in X (-) X&b in X}
     by A1; then
   x1+0.REAL n in (X (-) X) (+) X by Def5;then
   x1 in (X (-) X) (+) X by EUCLID:31;
   hence thesis by Def6;
end;

theorem
  (X (O) B) (-) B c= X (-) B & (X (O) B) (+) B c= X (+) B
proof
  X (O) B c= X by Th25;
  hence thesis by Th3;
end;

theorem 
  X (-) B c= (X (o) B) (-) B & X (+) B c= (X (o) B) (+) B
proof
  X c= X (o) B by Th25;
  hence thesis by Th3;
end;

theorem Th26:
  X c= Y implies X (O) B c= Y (O) B & X (o) B c= Y (o) B
proof
   assume
A1:X c= Y;
   thus X (O) B c= Y (O) B
   proof
     let x be set;assume x in X (O) B;then
     x in (X (-) B) (+) B by Def6;then
     x in {x2+b2 where x2,b2 is Point of TOP-REAL n:x2 in X (-) B& b2 in B}
       by Def5;then consider x2,b2 being Point of TOP-REAL n such that
  B3:x=x2+b2 & x2 in X (-) B & b2 in B;
     x2 in {y where y is Point of TOP-REAL n:B+y c= X} by B3,Def4;then
     consider y being Point of TOP-REAL n such that
  B5:x2=y & B+y c= X;
     B+y c= Y by A1,B5,XBOOLE_1:1;then
     x2 in {y1 where y1 is Point of TOP-REAL n:B+y1 c= Y}by B5;then
     x2 in Y (-) B by Def4;then
     x in {x3+b3 where x3,b3 is Point of TOP-REAL n:x3 in Y (-) B& b3 in B}
                           by B3;then
     x in (Y (-) B) (+) B by Def5;
     hence thesis by Def6;
    end;
    let x be set;assume x in X (o) B;then
    x in (X (+) B) (-) B by Def7;then
    x in {x2 where x2 is Point of TOP-REAL n:B+x2 c= X (+) B}
      by Def4;then consider x2 being Point of TOP-REAL n such that
 C3:x=x2 & B+x2 c= X (+) B;
    X (+) B c= Y (+) B by Th3,A1;then
    B+x2 c= Y (+) B by XBOOLE_1:1,C3;then
    x in {x3 where x3 is Point of TOP-REAL n:B+x3 c= Y (+) B} by C3;then
    x in (Y (+) B) (-) B by Def4;
    hence thesis by Def7;
end;

theorem Th30:
  (X+p) (O) Y = (X (O) Y)+p
proof
  thus (X+p) (O) Y = ((X+p) (-) Y) (+) Y by Def6
         .= ((X (-) Y)+p) (+) Y by Th12
         .= ((X (-) Y) (+) Y)+p by Th13
         .= (X (O) Y)+p by Def6;
end;

theorem
  (X+p) (o) Y = (X (o) Y)+p
proof
  thus (X+p) (o) Y = ((X+p) (+) Y) (-) Y by Def7
         .= ((X (+) Y)+p) (-) Y by Th13
         .= ((X (+) Y) (-) Y)+p by Th12
         .= (X (o) Y)+p by Def7;
end;

theorem 
  C c= B implies X (O) B c= (X (-) C) (+) B
proof
   assume
A1:C c= B;
   let x be set;assume x in X (O) B;then
   x in (X (-) B) (+) B by Def6;then
   x in {x1+b1 where x1,b1 is Point of TOP-REAL n:x1 in X (-) B&b1 in B}
     by Def5;then
   consider x1,b1 being Point of TOP-REAL n such that
B3:x=x1+b1 & x1 in X (-) B & b1 in B;
   x1 in {x2 where x2 is Point of TOP-REAL n:B+x2 c= X} by Def4,B3;then
   consider x2 being Point of TOP-REAL n such that
B5:x1=x2 & B+x2 c= X;
   C+x2 c= B+x2 by Th4,A1;then
   C+x2 c= X by B5,XBOOLE_1:1;then
   x1 in {x3 where x3 is Point of TOP-REAL n:C+x3 c= X}by B5;then
   x1 in X (-) C by Def4;then
   x in {x4+b4 where x4,b4 is Point of TOP-REAL n:x4 in X (-) C&b4 in B}by B3;
   hence thesis by Def5;
end;

theorem 
  B c= C implies X (o) B c= (X (+) C) (-) B
proof assume
A1:B c= C;
   let x be set;assume x in X (o) B;then
   x in (X (+) B) (-) B by Def7;then
   x in {x2 where x2 is Point of TOP-REAL n:B+x2 c= X (+) B} by Def4;
   then consider x2 being Point of TOP-REAL n such that
B13:x=x2 & B+x2 c= X (+) B;
   X (+) B c= X (+) C by Th5,A1;then
   B+x2 c= X (+) C by B13,XBOOLE_1:1;then
   x in {x3 where x3 is Point of TOP-REAL n:B+x3 c= (X (+) C)} by B13;
   hence thesis by Def4;
end;

theorem T1:
  X (+) Y = (X (o) Y) (+) Y & X (-) Y = (X (O) Y) (-) Y
proof
  thus X (+) Y = (X (o) Y) (+) Y
  proof
    thus X (+) Y c= (X (o) Y) (+) Y
    proof
     X c= X (o) Y by Th25;
     hence thesis by Th3;
    end;
    (X (o) Y) (+) Y =((X (+) Y) (-) Y) (+) Y by Def7
      .=(X (+) Y) (O) Y by Def6;
    hence thesis by Th25;
  end;
  thus X (-) Y c= (X (O) Y) (-) Y
  proof
    (X (O) Y) (-) Y = ((X (-) Y) (+) Y) (-) Y by Def6;then
    (X (O) Y) (-) Y = (X (-) Y) (o) Y by Def7;
    hence thesis by Th25;
  end;
  X (O) Y c= X by Th25;
  hence thesis by Th3;
end;

theorem
  X (+) Y = (X (+) Y) (O) Y & X (-) Y = (X (-) Y) (o) Y
proof
  thus X (+) Y = (X (+) Y) (O) Y
  proof
    (X (+) Y) (O) Y = ((X (+) Y) (-) Y) (+) Y by Def6
     .=(X (o) Y) (+) Y by Def7;
    hence thesis by T1;
  end;
  (X (-) Y) (o) Y = (X (-) Y) (+) Y (-) Y by Def7
   .=(X (O) Y) (-) Y by Def6;
  hence thesis by T1;
end;

theorem
  (X (O) B) (O) B = X (O) B
proof
  thus (X (O) B) (O) B c= X (O) B by Th25;
  let x be set;assume x in X (O) B;then
  x in (X (-) B) (+) B by Def6;then
  x in {x1+b1 where x1,b1 is Point of TOP-REAL n:x1 in X (-) B&b1 in B}
                             by Def5;
  then consider x1,b1 being Point of TOP-REAL n such that
A3:x=x1+b1 & x1 in X (-) B & b1 in B;
  x1 in {x2 where x2 is Point of TOP-REAL n:B+x2 c= X}by A3,Def4;then
  consider x2 being Point of TOP-REAL n such that
A5:x1=x2 & B+x2 c= X;
  (B+x2) (O) B c= X (O) B by A5,Th26;then
  (B (O) B)+x2 c= X (O) B by Th30;then
  B+x2 c= X (O) B by Thb;then
  x1 in {x4 where x4 is Point of TOP-REAL n:B+x4 c= X (O) B}by A5;then
  x1 in (X (O) B) (-) B by Def4;then
  x in {x5+b5 where x5,b5 is Point of TOP-REAL n:x5 in (X (O) B) (-) B
   &b5 in B} by A3;then
  x in (X (O) B) (-) B (+) B by Def5;
  hence thesis by Def6;
end;

theorem
  (X (o) B) (o) B = X (o) B
proof
  thus (X (o) B) (o) B c= X (o) B
  proof
    let x be set;assume x in (X (o) B) (o) B;then
    x in (X (o) B) (+) B (-) B by Def7;then
    x in {x2 where x2 is Point of TOP-REAL n:B+x2 c= (X (o) B) (+) B}
    by Def4;then consider x2 being Point of TOP-REAL n such that
A3: x= x2 & B+x2 c= (X (o) B) (+) B;
    B+x2 c= X (+) B by T1,A3;then
    x in {x3 where x3 is Point of TOP-REAL n:B+x3 c= X (+) B}by A3;then
    x in (X (+) B) (-) B by Def4;
    hence thesis by Def7;
  end;
  thus X (o) B c= (X (o) B) (o) B by Th25;
end;

theorem
  X (O) B c= (X \/ Y) (O) B
proof
  X c= X \/ Y by XBOOLE_1:7;
  hence thesis by Th26;
end;

theorem
  B = B (O) B1 implies X (O) B c= X (O) B1
proof
    assume
A1: B = B (O) B1;
    let x be set;assume x in X (O) B;then
    x in (X (-) B) (+) B by Def6;then
    x in {x1+b1 where x1,b1 is Point of TOP-REAL n:x1 in X (-) B&b1 in B}
      by Def5;then
    consider x1,b1 being Point of TOP-REAL n such that
B5: x=x1+b1 & x1 in X (-) B & b1 in B;
    x1 in {x2 where x2 is Point of TOP-REAL n:B+x2 c= X} by Def4,B5;then
    consider x2 being Point of TOP-REAL n such that
B7:x1=x2 & B+x2 c= X;
    b1 in (B (-) B1) (+) B1 by A1,B5,Def6;then
    b1 in {x3+b2 where x3,b2 is Point of TOP-REAL n:x3 in B (-) B1&b2 in B1}
   by Def5;then
    consider x3,b2 being Point of TOP-REAL n such that
B9: b1=x3+b2 & x3 in B (-) B1 & b2 in B1;
    x3 in {x4 where x4 is Point of TOP-REAL n:B1+x4 c= B} by Def4,B9;then
    consider x4 being Point of TOP-REAL n such that
B11:x3=x4 & B1+x4 c= B;
    B1+x4+x2 c= B+x2 by Th4,B11;then
    B1+x3+x1 c= X by B7,B11,XBOOLE_1:1;then
    B1+(x3+x1) c= X by Th14;then
    x3+x1 in {x5 where x5 is Point of TOP-REAL n:B1+x5 c= X};then
    x1+x3 in X (-) B1 by Def4;then
    (x1+x3)+b2 in {x6+b6 where x6,b6 is Point of TOP-REAL n:x6 in X (-) B1
       &b6 in B1}by B9;then
    x1+x3+b2 in (X (-) B1) (+) B1 by Def5;then
    x in (X (-) B1) (+) B1 by B5,B9,EUCLID:30;
    hence thesis by Def6;
end;

begin
:: 3.The definition of Scaling transformation and its Algebraic Properties
:: Scaling transformation

definition let t be real number, n be Nat,A be Subset of TOP-REAL n;
  func t(.)A -> Subset of TOP-REAL n equals
  :Def8: {t*a where a is Point of TOP-REAL n:a in A};
  coherence
   proof
   now let x be set;
    assume x in {t*a where a is Point of TOP-REAL n:a in A};then
    consider q being Point of TOP-REAL n such that
 A1:x = t*q & q in A;
    thus x in the carrier of TOP-REAL n by A1;
   end;
  hence thesis by TARSKI:def 3;
 end;
end;

 reserve t,s,r1 for real number;

theorem
  for X being Subset of TOP-REAL n st X = {} holds 0(.)X = {}
proof
   let X be Subset of TOP-REAL n;assume
A1:X = {};
   now given x being set such that
A3: x in 0(.)X;
    x in {0 * a where a is Point of TOP-REAL n:a in X}by A3,Def8;then
    consider a being Point of TOP-REAL n such that
A5: x = 0 * a & a in X;
    thus contradiction by A1,A5;
  end;
  hence thesis by XBOOLE_0:def 1;
end;

theorem
  for X being non empty Subset of TOP-REAL n holds 0(.)X = {0.REAL n}
proof let X be non empty Subset of TOP-REAL n;
   thus 0(.)X c= {0.REAL n}
   proof
     let x be set;assume x in 0(.)X;then
     x in {0 * a where a is Point of TOP-REAL n:a in X}by Def8;then
     consider a being Point of TOP-REAL n such that
A1:  x=0 * a & a in X;
     x = 0.REAL n by EUCLID:33,A1;
     hence thesis by ZFMISC_1:37;
   end;
   let x be set;assume x in {0.REAL n};then
   {x} c= {0.REAL n} by ZFMISC_1:37;then
A3:x = 0.REAL n by ZFMISC_1:24;
   consider d being Element of X;
   reconsider d1=d as Point of TOP-REAL n;
   x = 0 * d1 by EUCLID:33,A3;then
   x in {0 * a2 where a2 is Point of TOP-REAL n:a2 in X};
   hence thesis by Def8;
end;

theorem Th332:
  1(.)X = X
proof
  thus 1(.)X c= X
  proof let x be set;assume x in 1(.)X;then
    x in {1 * z where z is Point of TOP-REAL n:z in X}by Def8;then
    consider z being Point of TOP-REAL n such that
A1: x=1*z & z in X;
    thus thesis by A1,EUCLID:33;
  end;
  let x be set;assume
B1:x in X;then
  reconsider x1=x as Point of TOP-REAL n;
  x1=1*x1 by EUCLID:33;then
  x in {1 * z1 where z1 is Point of TOP-REAL n:z1 in X}by B1;
  hence thesis by Def8;
end;

theorem
  2(.)X c= X (+) X
proof
  let x be set;
  assume x in 2(.)X;then
  x in {2 * z where z is Point of TOP-REAL n:z in X}by Def8;then
  consider z being Point of TOP-REAL n such that
A1:x=2*z & z in X;
  x = (1+1)*z by A1
   .= 1*z+1*z by EUCLID:37
   .= z + 1*z by EUCLID:33
   .= z + z by EUCLID:33;then
  x in {a+b where a,b is Point of TOP-REAL n:a in X & b in X}by A1;
  hence thesis by Def5;
end;

theorem Th334:
  (t*s)(.)X = t(.)(s(.)X)
proof
  thus (t*s)(.)X c= t(.)(s(.)X)
  proof
    let x be set;assume x in (t*s)(.)X;then
    x in {(t*s) * z where z is Point of TOP-REAL n:z in X}by Def8;then
    consider z being Point of TOP-REAL n such that
 A3:x = (t*s) * z & z in X;
 A5:x = t*(s*z) by EUCLID:34,A3;
    s*z in {s * z1 where z1 is Point of TOP-REAL n:z1 in X}by A3;then
    s*z in s(.)X by Def8;then
    x in {t * z2 where z2 is Point of TOP-REAL n:z2 in s(.)X}by A5;
    hence thesis by Def8;
  end;
   let x be set;assume x in t(.)(s(.)X);then
   x in {t * z where z is Point of TOP-REAL n:z in s(.)X}by Def8;then
   consider z being Point of TOP-REAL n such that
A13:x = t * z & z in s(.)X;
   z in {s * z1 where z1 is Point of TOP-REAL n:z1 in X}by Def8,A13;then
   consider z1 being Point of TOP-REAL n such that
A15:z = s * z1 & z1 in X;
   x = (t*s)*z1 by EUCLID:34,A13,A15;then
   x in {(t*s)*z2 where z2 is Point of TOP-REAL n:z2 in X}by A15;
   hence thesis by Def8;
end;

theorem Th33:
  X c= Y implies t(.)X c= t(.)Y
proof
   assume
A0:X c= Y;
   let x be set;assume x in t(.)X;then
   x in {t*a where a is Point of TOP-REAL n:a in X}by Def8;then
   consider a being Point of TOP-REAL n such that
A3:x = t*a & a in X;
   x in {t*a3 where a3 is Point of TOP-REAL n:a3 in Y} by A0,A3;
   hence thesis by Def8;
end;

theorem Th34:
  t(.)(X+x) = t(.)X+t*x
proof
  thus t(.)(X+x) c= t(.)X+t*x
  proof
   let b be set;assume b in t(.)(X+x);then
    b in {t*a where a is Point of TOP-REAL n:a in X+x}by Def8;then 
    consider a being Point of TOP-REAL n such that
 A3:b = t*a & a in X+x;
    a in {x1+x where x1 is Point of TOP-REAL n:x1 in X}by A3,Def2;then
    consider x1 being Point of TOP-REAL n such that
 A5:a=x1+x & x1 in X;
 A7:b=t*x1+t*x by EUCLID:36,A3,A5;
    t*x1 in {t*a1 where a1 is Point of TOP-REAL n:a1 in X}by A5;then
    t*x1 in t(.)X by Def8;then
    b in {x3+t*x where x3 is Point of TOP-REAL n:x3 in t(.)X}by A7;
    hence thesis by Def2;
  end;
   let b be set;assume b in t(.)X+t*x;then
    b in {x1+t*x where x1 is Point of TOP-REAL n:x1 in t(.)X}by Def2;
    then consider x1 being Point of TOP-REAL n such that
 A13:b = x1+t*x & x1 in t(.)X;
    x1 in {t*a where a is Point of TOP-REAL n:a in X}by Def8,A13;then 
    consider a being Point of TOP-REAL n such that
 A15:x1=t*a & a in X;
 A17:b=t*(a+x) by EUCLID:36,A13,A15;   
    a+x in {c+x where c is Point of TOP-REAL n:c in X}by A15;then
    a+x in X+x by Def2;then
    b in {t*aa where aa is Point of TOP-REAL n:aa in X+x}by A17;
    hence thesis by Def8; 
end;

theorem Th35:
  t(.)(X (+) Y) = t(.)X (+) t(.)Y
proof
  thus t(.)(X (+) Y) c= t(.)X (+) t(.)Y
  proof
   let b be set;assume b in t(.)(X (+) Y);then
    b in {t*a where a is Point of TOP-REAL n:a in X (+) Y}by Def8;then
    consider a being Point of TOP-REAL n such that
 A3:b = t*a & a in X (+) Y;
    a in {x+y where x,y is Point of TOP-REAL n:x in X & y in Y}by Def5,A3;
    then consider x,y being Point of TOP-REAL n such that
 A5:a=x+y & x in X & y in Y;
 A7:b=t*x+t*y by EUCLID:36,A3,A5;
    t*x in {t*c where c is Point of TOP-REAL n:c in X}by A5;then
 A9:t*x in t(.)X by Def8;
    t*y in {t*c where c is Point of TOP-REAL n:c in Y}by A5;then
    t*y in t(.)Y by Def8;then
    b in {x1+y1 where x1,y1 is Point of TOP-REAL n:x1 in t(.)X & y1 in t(.)Y}
    by A9,A7;
    hence thesis by Def5;
  end;
   let b be set;assume
    b in t(.)X (+) t(.)Y;then
    b in {x+y where x,y is Point of TOP-REAL n:x in t(.)X &y in t(.)Y}by Def5;
    then consider x,y being Point of TOP-REAL n such that
 A13:b = x+y & x in t(.)X &y in t(.)Y;
    x in {t*x1 where x1 is Point of TOP-REAL n:x1 in X}by Def8,A13;
    then consider x1 being Point of TOP-REAL n such that
 A15:x=t*x1 & x1 in X;
    y in {t*y1 where y1 is Point of TOP-REAL n:y1 in Y}by Def8,A13;
    then consider y1 being Point of TOP-REAL n such that
 A17:y=t*y1 & y1 in Y;
 A19:b=t*(x1+y1) by EUCLID:36,A13,A15,A17;
    x1+y1 in {x2+y2 where x2,y2 is Point of TOP-REAL n:x2 in X & y2 in Y}
 by A15,A17;then
    x1+y1 in X (+) Y by Def5;then
    b in {t*y3 where y3 is Point of TOP-REAL n:y3 in X (+) Y}by A19;
    hence thesis by Def8;
end;

theorem Th36:
  t<>0 implies t(.)(X (-) Y) = t(.)X (-) t(.)Y
proof
   assume
AA:t<>0;
  thus t(.)(X (-) Y) c= t(.)X (-) t(.)Y
  proof
    let b be set;assume b in t(.)(X (-) Y);then
    b in {t*a where a is Point of TOP-REAL n:a in X (-) Y}by Def8;then
    consider a being Point of TOP-REAL n such that
 A3:b = t*a & a in X (-) Y;
    a in {x where x is Point of TOP-REAL n:Y+x c= X}by Def4,A3;
    then consider x being Point of TOP-REAL n such that
 A5:a=x & Y+x c= X;
    t(.)(Y+x) c= t(.)X by A5,Th33;then
 A7:t(.)Y+t*x c= t(.)X by Th34;
    b in {x1 where x1 is Point of TOP-REAL n:t(.)Y+x1 c= t(.)X }
                                                 by A3,A5,A7;
    hence thesis by Def4;
  end;
   let b be set;assume
   b in t(.)X (-) t(.)Y;then
   b in {x where x is Point of TOP-REAL n:t(.)Y+x c= t(.)X}by Def4;
   then consider x being Point of TOP-REAL n such that
A13:b = x & t(.)Y+x c= t(.)X;
   set t1=1 / t;
   (1/t)(.)(t(.)Y+x) c= (1/t)(.)(t(.)X) by A13,Th33;then
   (1/t)(.)(t(.)Y+x) c= (1/t*t)(.)X by Th334;then
   (1/t)(.)(t(.)Y)+(1/t)*x c= (1/t*t)(.)X by Th34;then
   (1/t*t)(.)Y+(1/t)*x c= (1/t*t)(.)X by Th334;then
   1(.)Y+(1/t)*x c= (1/t*t)(.)X by XCMPLX_1:88,AA;then
   1(.)Y+(1/t)*x c= 1(.)X by XCMPLX_1:88,AA;then
   Y+(1/t)*x c= 1(.)X by Th332;then
   Y+(1/t)*x c= X by Th332;then
   (1/t)*x in {z where z is Point of TOP-REAL n:Y+z c= X};then
   (1/t)*x in X (-) Y by Def4;then
   t*((1/t)*x) in {t*a1 where a1 is Point of TOP-REAL n:a1 in X (-) Y};then
   t*((1/t)*x) in t(.)(X (-) Y) by Def8;then
   (1/t*t)*x in t(.)(X (-) Y) by EUCLID:34;then
   1*x in t(.)(X (-) Y) by XCMPLX_1:88,AA;
   hence thesis by A13,EUCLID:33;
end;

theorem
  t<>0 implies t(.)(X (O) Y) = t(.)X (O) t(.)Y
proof assume
A1:t<>0;
   t(.)(X (O) Y) = t(.)((X (-) Y) (+) Y) by Def6
    .=t(.)(X (-) Y) (+) t(.)Y by Th35
    .=t(.)X (-) t(.)Y (+) t(.)Y by Th36,A1;
   hence thesis by Def6;
end;

theorem
  t<>0 implies t(.)(X (o) Y) = t(.)X (o) t(.)Y
proof assume
A1:t<>0;
   t(.)(X (o) Y) = t(.)((X (+) Y) (-) Y) by Def7
    .=t(.)(X (+) Y) (-) t(.)Y by Th36,A1
    .=t(.)X (+) t(.)Y (-) t(.)Y by Th35;
   hence thesis by Def7;
end;

begin :: 4. The definition of Thinning and Thickening and their
      ::    Algebraic Properties
      :: "Hit or Miss" Transformation

definition let n be Nat,X,B1,B2 be Subset of TOP-REAL n;
  func X (*) (B1,B2) -> Subset of TOP-REAL n equals
  :Def9: (X (-) B1) /\ (X` (-) B2);
  coherence;
end;

:: Thickening

definition let n be Nat,X,B1,B2 be Subset of TOP-REAL n;
  func X (&) (B1,B2) -> Subset of TOP-REAL n equals
  :Defa: X \/ (X (*) (B1,B2));
  coherence;
end;

:: Thinning

definition let n be Nat,X,B1,B2 be Subset of TOP-REAL n;
  func X (@) (B1,B2) -> Subset of TOP-REAL n equals
  :Defb: X \ (X (*) (B1,B2));
  coherence;
end;

theorem
  B1 = {} implies X (*) (B1,B2) = X` (-) B2
proof
   assume
A1:B1 = {};
   X (*) (B1,B2) = (X (-) B1) /\ (X` (-) B2)by Def9;then
   X (*) (B1,B2) = (X` (-) B2) /\ (REAL n) by Ti2,A1;then
   X (*) (B1,B2) = (X` (-) B2)/\(the carrier of TOP-REAL n) by EUCLID:25;
   hence thesis by XBOOLE_1:28;
end;

theorem
  B2 = {} implies X (*) (B1,B2) = X (-) B1
proof
   assume
A1:B2 = {};
   X (*) (B1,B2) = (X (-) B1) /\ (X` (-) B2)by Def9;then
   X (*) (B1,B2) = (X (-) B1) /\ (REAL n) by Ti2,A1;then
   X (*) (B1,B2) = (X (-) B1)/\(the carrier of TOP-REAL n) by EUCLID:25;
   hence thesis by XBOOLE_1:28;
end;

theorem 
  0.REAL n in B1 implies X (*) (B1,B2) c= X
proof
   assume
A1:0.REAL n in B1;
   let x be set;assume x in X (*) (B1,B2);then
   x in (X (-) B1) /\ (X` (-) B2)by Def9;then
   x in X (-) B1 & x in X` (-) B2 by XBOOLE_0:def 3;then
   x in {y where y is Point of TOP-REAL n:B1+y c= X} by Def4;then
   consider y being Point of TOP-REAL n such that
A5:x=y & B1+y c= X;
   0.REAL n + y in {z+y where z is Point of TOP-REAL n:z in B1} by A1;then
   0.REAL n + y in B1+y by Def2;then
   x in B1+y by A5,EUCLID:31;
   hence thesis by A5;
end;

theorem
  0.REAL n in B2 implies (X (*) (B1,B2)) /\ X = {}
proof
    assume
 A1:0.REAL n in B2;
    now given x being set such that
 A3:x in (X (*) (B1,B2)) /\ X;
 A5:x in (X (*) (B1,B2)) & x in X by XBOOLE_0:def 3,A3;then
    x in (X (-) B1) /\ (X` (-) B2)by Def9;then
    x in X (-) B1 & x in X` (-) B2 by XBOOLE_0:def 3;then
    x in {y where y is Point of TOP-REAL n:B2+y c= X`} by Def4;then
    consider y being Point of TOP-REAL n such that
 A9:x=y & B2+y c= X`;
    0.REAL n + y in {z+y where z is Point of TOP-REAL n:z in B2} by A1;then
    0.REAL n + y in B2+y by Def2;then
    x in B2+y by A9,EUCLID:31;
    hence contradiction by A9,A5,SUBSET_1:54;
   end;
  hence thesis by XBOOLE_0:def 1;
end;

theorem 
  0.REAL n in B1 implies X (&) (B1,B2) = X
proof
    assume 
 A1:0.REAL n in B1;
    thus X (&) (B1,B2) c= X
    proof
    let x be set;assume x in X (&) (B1,B2);then
a5: x in X \/ (X (*) (B1,B2)) by Defa;
    per cases by a5,XBOOLE_0:def 2;
    suppose x in X;
      hence thesis;
    end;
    suppose x in X (*) (B1,B2);then
    x in (X (-) B1) /\ (X` (-) B2)by Def9;then
    x in X (-) B1 & x in X` (-) B2 by XBOOLE_0:def 3;then
    x in {y where y is Point of TOP-REAL n:B1+y c= X} by Def4;then
    consider y being Point of TOP-REAL n such that
 A6:x=y & B1+y c= X;
    0.REAL n + y in {z+y where z is Point of TOP-REAL n:z in B1} by A1;then
    0.REAL n + y in B1+y by Def2;then
    x in B1+y by A6,EUCLID:31;
    hence thesis by A6; 
    end;
    end;
    X (&) (B1,B2) = X \/ (X (*) (B1,B2)) by Defa;
    hence thesis by XBOOLE_1:7;
end;

theorem
  0.REAL n in B2 implies X (@) (B1,B2) = X
proof
    assume 
 A1:0.REAL n in B2;
    thus X (@) (B1,B2) c= X
    proof 
     let x be set;assume x in X (@) (B1,B2);then
    x in X \ (X (*) (B1,B2)) by Defb;
    hence thesis by XBOOLE_0:def 4;
    end;
    let x be set;assume
A11:x in X;
    not x in (X (*) (B1,B2))
  proof
    assume x in (X (*) (B1,B2));then
    x in (X (-) B1) /\ (X` (-) B2)by Def9;then
    x in X (-) B1 & x in X` (-) B2 by XBOOLE_0:def 3;then
    x in {y where y is Point of TOP-REAL n:B2+y c= X`} by Def4;then
    consider y being Point of TOP-REAL n such that
 A19:x=y & B2+y c= X`;
    0.REAL n + y in {z+y where z is Point of TOP-REAL n:z in B2} by A1;then
    0.REAL n + y in B2+y by Def2;then
    x in B2+y by A19,EUCLID:31;
    hence contradiction by A19,A11,SUBSET_1:54;
  end;then
   x in X\(X (*) (B1,B2)) by A11,XBOOLE_0:def 4;
   hence thesis by Defb;
end;

theorem
  X (&) (B2,B1) = (X` (@) (B1,B2))`
proof
  (X` (@) (B1,B2))` = (X` \ (X` (*) (B1,B2)))` by Defb
    .=(X` \ ((X` (-) B1) /\ ((X`)` (-) B2)))` by Def9
    .=(X`)` \/ ((X` (-) B1) /\ (X (-) B2)) by SUBSET_1:33
    .=X \/ (X (*) (B2,B1)) by Def9;
  hence thesis by Defa;
end;

theorem
  X (@) (B2,B1) = (X` (&) (B1,B2))`
proof
  (X` (&) (B1,B2))` = (X` \/ (X` (*) (B1,B2)))` by Defa
    .=(X` \/ ((X` (-) B1) /\ ((X`)` (-) B2)))` by Def9
    .=((X \ ((X` (-) B1) /\ ((X`)` (-) B2)))`)` by SUBSET_1:33
    .=X \ (X (*) (B2,B1)) by Def9;
  hence thesis by Defb;
end;

begin 
:: 5. Properties of Erosion, Dilation, Adjunction Opening,
::    Adjunction Closing on Convex sets

theorem TL1:
  for n be Nat,B be Subset of TOP-REAL n holds B is convex iff
    (for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1 &
      x in B & y in B holds r*x + (1-r)*y in B)
proof
  let n be Nat,B be Subset of TOP-REAL n;
  thus B is convex implies
     for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1
     & x in B & y in B holds r*x + (1-r)*y in B
  proof
    assume
B1: B is convex;
    for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1
     & x in B & y in B holds r*x + (1-r)*y in B
   proof
     let x,y be Point of TOP-REAL n,r be real number;
     assume
  C1:0 <= r & r <= 1 & x in B & y in B;then
 C2: LSeg(x,y) c= B by B1,JORDAN1:def 1;
     r is Real by XREAL_0:def 1; then
     (1-r)*y + r*x in {(1-r1)*y+r1*x where r1 is Real:0<=r1&r1<=1} by C1;then
     r*x + (1-r)*y in LSeg(x,y) by TOPREAL1:def 3;
     hence thesis by C2;
   end;
   hence thesis;
  end;
  (for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1
  & x in B & y in B holds r*x + (1-r)*y in B) implies B is convex
 proof
   assume
D1:for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1
     & x in B & y in B holds r*x + (1-r)*y in B;
   for x,y being Point of TOP-REAL n st x in B & y in B holds
     LSeg(x,y) c= B
   proof
     let x,y be Point of TOP-REAL n;
     assume
E0:  x in B & y in B;
     let p be set;assume p in LSeg(x,y);then
     p in {(1-r1) * y + r1*x where r1 is Real:0<=r1&r1<=1}
       by TOPREAL1:def 3;then
     consider r1 being Real such that
E3:  p = (1-r1) * y + r1 * x &(0<=r1&r1<=1);
     thus thesis by E0,D1,E3;
   end;
   hence thesis by JORDAN1:def 1;
  end;
  hence thesis;
end;

definition let n be Nat, B be Subset of TOP-REAL n;
  redefine attr B is convex means :Def22:
  for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1 &
    x in B & y in B holds r*x + (1-r)*y in B;
  compatibility by TL1;
end;

theorem
  X is convex implies X! is convex
proof
   assume
A1:X is convex;
   for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1
     & x in X! & y in X! holds r*x + (1-r)*y in X!
   proof
     let x,y be Point of TOP-REAL n,r be real number;
     assume
B1:  0 <= r & r <= 1 & x in X! & y in X!;then
     x in {-x1 where x1 is Point of TOP-REAL n:x1 in X}by Def3;then
     consider x1 being Point of TOP-REAL n such that
B3:  x=-x1 & x1 in X;
     y in {-x2 where x2 is Point of TOP-REAL n:x2 in X}by Def3,B1;then
     consider x2 being Point of TOP-REAL n such that
B5:  y=-x2 & x2 in X;
     r*x1 + (1-r)*x2 in X by A1,B1,B3,B5,Def22;then
     -(r*x1 + (1-r)*x2) in {-z where z is Point of TOP-REAL n:z in X};then
     -(r*x1 + (1-r)*x2) in X! by Def3;then
     -(r*x1) + -((1-r)*x2) in X! by EUCLID:42;then
     r*(-x1) + -((1-r)*x2) in X! by EUCLID:44;
     hence thesis by B3,B5,EUCLID:44;
  end;
  hence thesis by Def22;
end;

theorem Z1:
  X is convex & B is convex implies
    X (+) B is convex & X (-) B is convex
proof
  assume 
 A1:X is convex & B is convex;
    thus X (+) B is convex
    proof
      for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1
        & x in X (+) B & y in X (+) B holds r*x + (1-r)*y in X (+) B
     proof
       let x,y be Point of TOP-REAL n,r be real number;
       assume 
   B1: 0 <= r & r <= 1 & x in X (+) B & y in X (+) B;then
    B3:x in {x1+b1 where x1,b1 is Point of TOP-REAL n:x1 in X & b1 in B}&
       y in {x2+b2 where x2,b2 is Point of TOP-REAL n:x2 in X & b2 in B}
         by Def5;
       then consider x1,b1 being Point of TOP-REAL n such that
    B5:x=x1+b1 & x1 in X & b1 in B;
       consider x2,b2 being Point of TOP-REAL n such that
    B7:y=x2+b2& x2 in X & b2 in B by B3;
    B9:r*x1 + (1-r)*x2 in X by B5,B7,Def22,B1,A1;
       r*b1 + (1-r)*b2 in B by B5,B7,Def22,B1,A1;then
       (r*x1 + (1-r)*x2)+(r*b1 + (1-r)*b2)
        in {x3+b3 where x3,b3 is Point of TOP-REAL n:x3 in X&b3 in B}by B9;then
       (r*x1 + (1-r)*x2)+(r*b1 + (1-r)*b2) in X (+) B by Def5;then
       (r*x1 + (1-r)*x2)+r*b1 + (1-r)*b2 in X (+) B by EUCLID:30;then
       r*x1 + r*b1+(1-r)*x2 + (1-r)*b2 in X (+) B by EUCLID:30;then
       (r*x1 + r*b1)+((1-r)*x2 + (1-r)*b2) in X (+) B by EUCLID:30;then
       r*(x1 + b1)+((1-r)*x2 + (1-r)*b2) in X (+) B by EUCLID:36;
       hence thesis by B7,B5,EUCLID:36;
     end;
     hence thesis by Def22;
    end;
      for x,y be Point of TOP-REAL n,r be real number st 0 <= r & r <= 1
        & x in X (-) B & y in X (-) B holds r*x + (1-r)*y in X (-) B
     proof
       let x,y be Point of TOP-REAL n,r be real number;
       assume
   C1: 0 <= r & r <= 1 & x in X (-) B & y in X (-) B;then
    C3:x in {x1 where x1 is Point of TOP-REAL n:B+x1 c= X}&
       y in {y1 where y1 is Point of TOP-REAL n:B+y1 c= X}by Def4;then
       consider x1 being Point of TOP-REAL n such that
    C5:x=x1&B+x1 c= X;
       consider y1 being Point of TOP-REAL n such that
    C7:y=y1 & B+y1 c= X by C3;
       B+(r*x + (1-r)*y) c= X
       proof
         let b1 be set;assume b1 in B+(r*x + (1-r)*y);then
         b1 in {b+(r*x + (1-r)*y) where b is Point of TOP-REAL n:b in B}
         by Def2;then consider b being Point of TOP-REAL n such that
      D3:b1=b+(r*x + (1-r)*y) & b in B;
         b+x in {b2+x where b2 is Point of TOP-REAL n:b2 in B}by D3;then
      d5:b+x in B+x by Def2;
         b+y in {b2+y where b2 is Point of TOP-REAL n:b2 in B}by D3;then
         b+y in B+y by Def2;then
         r*(b+x) + (1-r)*(b+y) in X by d5,Def22,A1,C1,C7,C5;then
         r*b+r*x + (1-r)*(b+y) in X by EUCLID:36;then
         r*b+r*x + ((1-r)*b+(1-r)*y) in X by EUCLID:36;then
         r*b+r*x + (1-r)*b+(1-r)*y in X by EUCLID:30;then
         r*b+r*x + (1*b-r*b)+(1-r)*y in X by EUCLID:54;then
         r*b+r*x + 1*b-r*b+(1-r)*y in X by EUCLID:49;then
         r*x+1*b + r*b-r*b+(1-r)*y in X by EUCLID:30;then
         r*x+1*b +(1-r)*y in X by EUCLID:52;then
         1*b +(r*x+(1-r)*y) in X by EUCLID:30;
         hence thesis by D3,EUCLID:33;
      end;then
      r*x + (1-r)*y in {z where z is Point of TOP-REAL n:B+z c=X};
      hence thesis by Def4;
    end;
  hence thesis by Def22;
end;
 
theorem 
  X is convex & B is convex implies
    X (O) B is convex & X (o) B is convex
proof
  assume 
 A1:X is convex & B is convex;
    thus X (O) B is convex
    proof
      X (-) B is convex by A1,Z1;then
      X (-) B (+) B is convex by A1,Z1;
      hence thesis by Def6;
    end;
    X (+) B is convex by A1,Z1;then
    X (+) B (-) B is convex by A1,Z1;
    hence thesis by Def7;
end;

theorem 
  B is convex & 0 < t & 0 < s implies
    (s+t)(.)B = s(.)B (+) t(.)B
proof
   assume
 A1:B is convex & 0 < t & 0 < s;then
 A2:0+0 < s+t by REAL_1:67;then
 A4:0 < s/(s+t) by REAL_2:127,A1;
    s<s+t by REAL_1:69,A1;then
    s/(s+t) < (s+t)/(s+t) by REAL_1:73,A1;then
 A6:s/(s+t) < 1 by XCMPLX_1:60,A2;
    thus (s+t)(.)B c= s(.)B (+) t(.)B
    proof
      let x be set;assume x in (s+t)(.)B;then
       x in {(s+t)*b where b is Point of TOP-REAL n:b in B}by Def8;then
       consider b being Point of TOP-REAL n such that
    B3:x=(s+t)*b & b in B;
    B5:x = s*b +t*b by EUCLID:37,B3;
       s*b in {s*b1 where b1 is Point of TOP-REAL n:b1 in B}by B3;then
    B7:s*b in s(.)B by Def8;
       t*b in {t*b1 where b1 is Point of TOP-REAL n:b1 in B}by B3;then
       t*b in t(.)B by Def8;then
       s*b+t*b in {z1+z2 where z1,z2 is Point of TOP-REAL n:
         z1 in s(.)B&z2 in t(.)B} by B7;
       hence thesis by Def5,B5;
    end;
    let x be set;assume x in s(.)B (+) t(.)B;then
    x in {s1+s2 where s1,s2 is Point of TOP-REAL n:
    s1 in s(.)B&s2 in t(.)B} by Def5;then
    consider s1,s2 being Point of TOP-REAL n such that
 C3:x=s1+s2 & s1 in s(.)B & s2 in t(.)B;
    s1 in {s*b1 where b1 is Point of TOP-REAL n:b1 in B}by Def8,C3;then
    consider b1 being Point of TOP-REAL n such that
 C5:s1=s*b1 & b1 in B;
    s2 in {t*b2 where b2 is Point of TOP-REAL n:b2 in B}by Def8,C3;then
    consider b2 being Point of TOP-REAL n such that
 C7:s2=t*b2 & b2 in B;
    (s/(s+t))*b1 +(1-s/(s+t))*b2 in B by A1,A4,A6,C5,C7,Def22;then
    (s+t)*((s/(s+t))*b1 +(1-s/(s+t))*b2) in {(s+t)*zz where zz is Point 
     of TOP-REAL n: zz in B};then
    (s+t)*((s/(s+t))*b1 +(1-s/(s+t))*b2) in (s+t)(.)B by Def8;then
    (s+t)*((s/(s+t))*b1) +(s+t)*((1-s/(s+t))*b2) in (s+t)(.)B by EUCLID:36;
    then (s+t)*(s/(s+t))*b1 +(s+t)*((1-s/(s+t))*b2) in (s+t)(.)B by EUCLID:34;
    then (s+t)*(s/(s+t))*b1 +(s+t)*(1-s/(s+t))*b2 in (s+t)(.)B by EUCLID:34;
    then s*b1 +(s+t)*(1-s/(s+t))*b2 in (s+t)(.)B by XCMPLX_1:88,A2;
    then s*b1 +(s+t)*((s+t)/(s+t)-s/(s+t))*b2 in (s+t)(.)B by XCMPLX_1:60,A2;
    then s*b1 +(s+t)*((s+t-s)/(s+t))*b2 in (s+t)(.)B by XCMPLX_1:121;
    hence thesis by C3,C5,C7,XCMPLX_1:88,A2;
end;
