:: Several Differentiable Formulas of Special Functions -- Part {II}
::  by Yan Zhang , Bo Li and Xiquan Liang
::
:: Received November 23, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, BOOLE, FUNCT_1, ARYTM_1, ARYTM_3, RELAT_1, PARTFUN1,
      SEQ_1, SIN_COS, FDIFF_1, PREPOWER, POWER, GROUP_1, SQUARE_1, TAYLOR_1,
      FINSEQ_1, ABSVALUE, LIMFUNC1, NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, PARTFUN1, LIMFUNC1, RCOMP_1,
      NUMBERS, REAL_1, NAT_1, NAT_D, SEQ_1, FDIFF_1, NEWTON, INT_2, PREPOWER,
      POWER, TAYLOR_1, SIN_COS, RFUNCT_1, SQUARE_1, XXREAL_0;
 constructors PARTFUN1, REAL_1, SQUARE_1, NAT_1, RCOMP_1, PARTFUN2, RFUNCT_1,
      FDIFF_1, LIMFUNC1, PREPOWER, POWER, SIN_COS, TAYLOR_1, NAT_D;
 registrations RELSET_1, XREAL_0, MEMBERED, RCOMP_1, INT_1, SEQ_1, XXREAL_0,
      ORDINAL1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions SQUARE_1, TAYLOR_1;
 theorems FDIFF_2, TAYLOR_1, XBOOLE_1, XCMPLX_1, SEQ_1, FDIFF_1, FUNCT_1,
      TARSKI, PREPOWER, RFUNCT_1, SQUARE_1, XREAL_1, XCMPLX_0, NEWTON, SIN_COS,
      SIN_COS2, WSIERP_1, FDIFF_4, POWER, ABSVALUE;

begin

 reserve y for set,
         x,a for Real,
         n for Element of NAT,
         Z for open Subset of REAL,
         f,f1,f2 for PartFunc of REAL,REAL;

Lm1: 1-cos(2*x)=2*(sin x)^2
proof
  1-cos(2*x)=1-cos(x+x)
    .=1-((cos x) *(cos x)-(sin x) * (sin x)) by SIN_COS:80
    .=(cos x)^2+(sin x)^2-((cos x)^2-(sin x)^2) by SIN_COS:32
    .=2*(sin x)^2;
  hence thesis;
end;

Lm2: 1+cos(2*x)=2*(cos x)^2
proof
  1+cos(2*x)=1+cos(x+x)
    .=1+((cos x)^2-(sin x)^2) by SIN_COS:80
    .=(cos x)^2+(sin x)^2+((cos x)^2-(sin x)^2) by SIN_COS:32
    .=2*(cos x)^2;
  hence thesis;
end;

:: 39 f.x=2*(1-cos.x) #R (1/2)
Lm3: sin.x>0 implies sin.x=((1-cos.x)*(1+cos.x)) #R (1/2)
proof
  assume A1:sin.x>0;
  then sin.x=(sin.x) #R (2*(1/2)) by PREPOWER:86
  .=(sin.x) #R (1+1) #R (1/2) by A1,PREPOWER:105
  .=((sin.x) #R 1 * (sin.x) #R 1) #R (1/2) by PREPOWER:89,A1
  .=(sin.x * (sin.x) #R 1) #R (1/2) by A1,PREPOWER:86
  .=((sin.x)^2+(cos.x)^2-(cos.x)^2) #R (1/2) by A1,PREPOWER:86
  .=(1^2-(cos.x)^2) #R (1/2) by SIN_COS:31
  .=((1-cos.x)*(1+cos.x)) #R (1/2);
  hence thesis;
end;

Lm4: sin.x>0 & cos.x<1 & cos.x>-1 implies
   sin.x/(1-cos.x) #R (1/2) =(1+cos.x) #R (1/2)
proof
   assume that A1:sin.x>0 and A2:cos.x<1 and A3:cos.x>-1;
A4:1-cos.x>1-1 by A2,XREAL_1:17;
   1+cos.x>1+-1 by A3,XREAL_1:10;
   then A5:(1-cos.x)*(1+cos.x)>0 by A4,XREAL_1:131;
   sin.x/(1-cos.x) #R (1/2)
    =((1-cos.x)*(1+cos.x)) #R (1/2)/(1-cos.x) #R (1/2) by Lm3,A1
   .=(((1-cos.x)*(1+cos.x))/(1-cos.x)) #R (1/2) by PREPOWER:94,A4,A5
   .=(1+cos.x) #R (1/2) by A4,XCMPLX_1:90;
   hence thesis;
end;

:: a #R x=exp.(x*ln a)
theorem Th1:
  a>0 implies exp_R.(x*log(number_e,a))=a #R x
  proof
    assume A1:a>0;
    then A2:a #R x >0 by PREPOWER:95;
    number_e>0 & number_e<>1 by TAYLOR_1:11;
    then exp_R.(x*log(number_e,a))
    =exp_R.(log(number_e,a to_power x)) by POWER:63,A1
    .=exp_R.(log(number_e,a #R x)) by POWER:def 2,A1
    .=a #R x by TAYLOR_1:15,A2;
    hence thesis;
  end;

:: 15 a #R (-x)=exp.(-x*ln a)
theorem Th2:
  a>0 implies exp_R.(-x*log(number_e,a))=a #R (-x)
  proof
    assume A1:a>0;
    then A2:a #R (-x) >0 by PREPOWER:95;
A3: number_e>0 & number_e<>1 by TAYLOR_1:11;
    exp_R.(-x*log(number_e,a))
    =exp_R.((-x)*log(number_e,a))
    .=exp_R.(log(number_e,a to_power (-x))) by POWER:63,A1,A3
    .=exp_R.(log(number_e,a #R (-x))) by POWER:def 2,A1
    .=a #R (-x) by TAYLOR_1:15,A2;
    hence thesis;
  end;

:: 40 f.x=-2*(1+cos.x) #R (1/2)
Lm5: sin.x>0 & cos.x<1 & cos.x>-1
     implies sin.x/(1+cos.x) #R (1/2) =(1-cos.x) #R (1/2)
     proof
       assume that A1:sin.x>0 and A2:cos.x<1 and A3:cos.x>-1;
A4:    1-cos.x>1-1 by A2,XREAL_1:17;
A5:    1+cos.x>1+-1 by A3,XREAL_1:10;
       then A6:(1-cos.x)*(1+cos.x)>0 by A4,XREAL_1:131;
       sin.x/(1+cos.x) #R (1/2)
       =((1-cos.x)*(1+cos.x)) #R (1/2)/(1+cos.x) #R (1/2) by Lm3,A1
       .=(((1-cos.x)*(1+cos.x))/(1+cos.x)) #R (1/2) by PREPOWER:94,A5,A6
       .=(1-cos.x) #R (1/2) by A5,XCMPLX_1:90;
       hence thesis;
     end;

:: 1 f.x=a^2-x^2
theorem Th3:
  Z c= dom (f1-f2) & (for x st x in Z holds f1.x=a^2) & f2=#Z 2 implies
  f1-f2 is_differentiable_on Z &
  for x st x in Z holds ((f1-f2)`|Z).x = -2*x
  proof
    assume that A1:Z c= dom (f1-f2) and
A2: (for x st x in Z holds f1.x=a^2) and
A3: f2=#Z 2;
A4: f1+(-1)(#)f2=f1+-f2 by RFUNCT_1:38
    .=f1-f2 by RFUNCT_1:40;
A5: for x st x in Z holds f1.x=a^2+0*x by A2;
    for x st x in Z holds ((f1-f2)`|Z).x = -2*x
    proof
      let x;
      assume x in Z;
      hence ((f1-f2)`|Z).x =0+2*(-1)*x by A4,A1,A3,FDIFF_4:12,A5
      .=-2*x;
    end;
    hence thesis by A4,A1,A3,FDIFF_4:12,A5;
  end;

:: 2 f.x=(a^2+x^2)/(a^2-x^2)
theorem Th4:
  Z c= dom ((f1+f2)/(f1-f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & (f1-f2).x<>0) implies
  (f1+f2)/(f1-f2) is_differentiable_on Z &
  for x st x in Z holds (((f1+f2)/(f1-f2))`|Z).x = 4*a^2*x/(a^2-x|^2)^2
  proof
    assume that A1:Z c= dom ((f1+f2)/(f1-f2)) and A2:f2=#Z 2 and
A3: (for x st x in Z holds f1.x=a^2 & (f1-f2).x<>0);
    Z c=dom (f1+f2) /\ (dom (f1-f2) \ (f1-f2)"{0}) by A1,RFUNCT_1:def 4;
    then A4:Z c= dom (f1+f2) & Z c= dom (f1-f2) \ (f1-f2)"{0} by XBOOLE_1:18;
    A5:Z c= dom (f1-f2) by XBOOLE_1:1,A4;
A6: for x st x in Z holds f1.x=a^2 by A3;
    then A7:f1+f2 is_differentiable_on Z &
    for x st x in Z holds ((f1+f2)`|Z).x = 2*x by A2,A4,FDIFF_4:17;
A8: f1-f2 is_differentiable_on Z &
    for x st x in Z holds ((f1-f2)`|Z).x = -2*x by Th3,A2,A5,A6;
A9: for x st x in Z holds (f1-f2).x <> 0 by A3;
     then A10:(f1+f2)/(f1-f2) is_differentiable_on Z by A7,A8,FDIFF_2:21;
     for x st x in Z holds (((f1+f2)/(f1-f2))`|Z).x=4*a^2*x/(a^2-x|^2)^2
     proof
       let x;
       assume A11:x in Z;
       then A12:f1+f2 is_differentiable_in x by A7,FDIFF_1:16;
A13:   f1-f2 is_differentiable_in x by A8,FDIFF_1:16,A11;
A14:   f2.x=x #Z 2 by A2,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
A15:    (f1+f2).x=f1.x+f2.x by SEQ_1:def 3,A4,A11
       .=a^2+x |^2 by A11,A3,A14;
A16:    (f1-f2).x=f1.x-f2.x by A5,A11,SEQ_1:def 4
       .=a^2-x |^2 by A3,A11,A14;
       (f1-f2).x<>0 by A3,A11;
       then diff((f1+f2)/(f1-f2),x)
       =(diff(f1+f2,x)*(f1-f2).x - diff(f1-f2,x)*(f1+f2).x)/((f1-f2).x)^2
       by FDIFF_2:14,A12,A13
       .=(((f1+f2)`|Z).x * (f1-f2).x-diff(f1-f2,x) * (f1+f2).x)/((f1-f2).x)^2
       by A7,A11,FDIFF_1:def 8
       .= (((f1+f2)`|Z).x * (f1-f2).x-((f1-f2)`|Z).x * (f1+f2).x)/((f1-f2).x)^2
       by A8,A11,FDIFF_1:def 8
       .=((2*x)*(f1-f2).x-((f1-f2)`|Z).x * (f1+f2).x)/((f1-f2).x)^2
       by A11,A2,A4,FDIFF_4:17,A6
       .=((2*x)*(f1-f2).x-(-2*x) * (f1+f2).x)/((f1-f2).x)^2
       by A11,Th3,A2,A5,A6
       .=4*a^2*x/(a^2-x|^2)^2 by A15,A16;
       hence thesis by A11,A10,FDIFF_1:def 8;
     end;
     hence thesis by A9,A7,A8,FDIFF_2:21;
   end;

:: 3 f.x=ln((a^2+x^2)/(a^2-x^2))
theorem Th5:
  Z c= dom f & f=ln*((f1+f2)/(f1-f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & (f1-f2).x>0 & a<>0)
  implies f is_differentiable_on Z & for x
  st x in Z holds (f`|Z).x =4*a^2*x/(a|^4-x|^4)
  proof
    assume that A1:Z c= dom f and A2:f=ln*((f1+f2)/(f1-f2)) and
A3: f2=#Z 2 and A4:for x st x in Z holds f1.x=a^2 & (f1-f2).x>0 & a<>0;
    for y st y in Z holds y in dom ((f1+f2)/(f1-f2)) by FUNCT_1:21,A1,A2;
    then A5:Z c= dom ((f1+f2)/(f1-f2)) by TARSKI:def 3;
    then Z c=dom (f1+f2) /\ (dom (f1-f2) \ (f1-f2)"{0}) by RFUNCT_1:def 4;
    then A6:Z c= dom (f1+f2) & Z c= dom (f1-f2) \ (f1-f2)"{0} by XBOOLE_1:18;
    A7:Z c= dom (f1-f2) by XBOOLE_1:1,A6;
A8: for x st x in Z holds f1.x=a^2 & (f1-f2).x<>0 by A4;
     then A9:(f1+f2)/(f1-f2) is_differentiable_on Z & for x st x in Z holds
     (((f1+f2)/(f1-f2))`|Z).x = 4*a^2*x/(a^2-x|^2)^2 by Th4,A5,A3;
A10:  for x st x in Z holds ((f1+f2)/(f1-f2)).x >0
     proof
       let x;
       assume A11:x in Z;
       then A12:(f1-f2).x>0 by A4;
A13:   f2.x=x #Z 2 by A3,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
A14:    (f1+f2).x=f1.x+f2.x by A6,A11,SEQ_1:def 3
       .=a^2+x |^2 by A11,A4,A13;
       a<>0 by A11,A4;
       then A15:a^2>0 by SQUARE_1:74;
A16:    x |^2 = x^2 by NEWTON:100;
       x |^2>=0 by XREAL_1:65,A16;
       then A17:a^2+x |^2>0+0 by A15,XREAL_1:10;
       ((f1+f2)/(f1-f2)).x =(f1+f2).x * ((f1-f2).x)" by A5,A11,RFUNCT_1:def 4
       .=(f1+f2).x / (f1-f2).x by XCMPLX_0:def 9;
       hence thesis by A12,A17,A14,XREAL_1:141;
     end;
A18: for x st x in Z holds ln*((f1+f2)/(f1-f2))
     is_differentiable_in x
     proof
       let x;
       assume A19:x in Z;
       then A20:(f1+f2)/(f1-f2) is_differentiable_in x by FDIFF_1:16,A9;
       ((f1+f2)/(f1-f2)).x >0 by A10,A19;
       hence ln*((f1+f2)/(f1-f2)) is_differentiable_in x
       by TAYLOR_1:20,A20;
     end;
     then A21:f is_differentiable_on Z by A1,A2,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x = 4*a^2*x/(a|^4-x|^4)
     proof
       let x;
       assume A22:x in Z;
       then A23:(f1+f2)/(f1-f2) is_differentiable_in x by FDIFF_1:16,A9;
A24:   f2.x=x #Z 2 by A3,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
A25:    (f1+f2).x=f1.x+f2.x by A6,A22,SEQ_1:def 3
       .=a^2+x |^2 by A22,A4,A24;
A26:    (f1-f2).x=f1.x-f2.x by SEQ_1:def 4,A7,A22
       .=a^2-x |^2 by A4,A22,A24;
A27:   ((f1+f2)/(f1-f2)).x =(f1+f2).x * ((f1-f2).x)" by RFUNCT_1:def 4,A5,A22
       .=(a^2+x |^2)/(a^2-x |^2) by A25,A26,XCMPLX_0:def 9;
A28:    (a^2)^2=(a|^2)*(a^2) by NEWTON:100
       .=(a|^2)*(a|^2) by NEWTON:100
       .=(a|^2)|^2 by WSIERP_1:2
       .=a|^(2*2) by NEWTON:14
       .=a|^4;
A29:    (x |^2)^2=(x |^2)|^2 by WSIERP_1:2
       .=x|^(2*2) by NEWTON:14
       .=x|^4;
A30:   a^2-x |^2>0 by A22,A4,A26;
       ((f1+f2)/(f1-f2)).x >0 by A10,A22;
       then diff(ln*((f1+f2)/(f1-f2)),x)
       =diff(((f1+f2)/(f1-f2)),x)/(((f1+f2)/(f1-f2)).x) by TAYLOR_1:20,A23
       .=(((f1+f2)/(f1-f2))`|Z).x/(((f1+f2)/(f1-f2)).x) by A9,A22,FDIFF_1:def 8
       .= (4*a^2*x/(a^2-x|^2)^2)/((a^2+x |^2)/(a^2-x |^2))
         by A27,A8,Th4,A5,A3,A22
       .=(4*a^2*x/(a^2-x|^2)/(a^2-x|^2))/((a^2+x |^2)/(a^2-x |^2))
       by XCMPLX_1:79
       .=(4*a^2*x/(a^2-x|^2))/((a^2+x |^2)/(a^2-x |^2))/(a^2-x|^2)
       by XCMPLX_1:48
       .=(4*a^2*x)/(a^2+x |^2)/(a^2-x|^2) by A30,XCMPLX_1:55
       .=(4*a^2*x)/((a^2+x |^2)*(a^2-x|^2)) by XCMPLX_1:79
       .=4*a^2*x/(a|^4-x|^4) by A28,A29;
       hence thesis by A22,A21,A2,FDIFF_1:def 8;
     end;
     hence thesis by A18,A1,A2,FDIFF_1:16;
   end;

:: 4 f.x=(1/(4*a^2))*ln((a^2+x^2)/(a^2-x^2))
theorem
  Z c= dom ((1/(4*a^2))(#)f) & f=ln*((f1+f2)/(f1-f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & (f1-f2).x>0 & a<>0)
  implies (1/(4*a^2))(#)f is_differentiable_on Z & for x
  st x in Z holds (((1/(4*a^2))(#)f)`|Z).x =x/(a|^4-x|^4)
  proof
    assume that A1:Z c= dom ((1/(4*a^2))(#)f) and
A2: f=ln*((f1+f2)/(f1-f2)) and
A3: f2=#Z 2 and
A4: (for x st x in Z holds f1.x=a^2 & (f1-f2).x>0 & a<>0);
A5: Z c= dom f by SEQ_1:def 6,A1;
    then A6:f is_differentiable_on Z & for x
    st x in Z holds (f`|Z).x =4*a^2*x/(a|^4-x|^4) by Th5,A2,A3,A4;
    for x st x in Z holds (((1/(4*a^2))(#)f)`|Z).x =x/(a|^4-x|^4)
    proof
      let x;
      assume A7:x in Z;
      then a<>0 by A4;
      then a^2>0 by SQUARE_1:74;
      then A8:4*a^2>4*0 by XREAL_1:70;
      (((1/(4*a^2))(#)f)`|Z).x
      =(1/(4*a^2))*diff(f,x) by A6,A1,FDIFF_1:28,A7
      .=(1/(4*a^2))*(f`|Z).x by A7,A6,FDIFF_1:def 8
      .=(1/(4*a^2))*(4*a^2*x/(a|^4-x|^4)) by Th5,A2,A3,A4,A5,A7
      .=(1/(4*a^2))*((4*a^2)*(x/(a|^4-x|^4))) by XCMPLX_1:75
      .=(x/(a|^4-x|^4))*(1/(4*a^2)*(4*a^2))
      .=x/(a|^4-x|^4) by XCMPLX_1:109,A8;
      hence thesis;
    end;
    hence thesis by A1,FDIFF_1:28,A6;
  end;

:: 5 f.x=x^2/(1+x^2)
theorem Th7:
  Z c= dom (f1/(f2+f1)) & f1=#Z 2 & (for x st x in Z holds f2.x=1 & x<>0)
  implies f1/(f2+f1) is_differentiable_on Z & for x
  st x in Z holds ((f1/(f2+f1))`|Z).x =2*x/(1+x^2)^2
  proof
    assume that A1:Z c= dom (f1/(f2+f1)) and A2:f1=#Z 2 and
A3: for x st x in Z holds f2.x=1 & x<>0;
A4: for x st x in Z holds f2.x=1^2 by A3;
    Z c= dom f1 /\ (dom (f2+f1) \ (f2+f1)"{0}) by RFUNCT_1:def 4,A1;
    then A5:Z c= dom f1 & Z c= dom (f2+f1) \ (f2+f1)"{0} by XBOOLE_1:18;
    A6:Z c= dom (f2+f1) by XBOOLE_1:1,A5;
    then A7:f2+f1 is_differentiable_on Z &
    for x st x in Z holds ((f2+f1)`|Z).x = 2*x by A2,A4,FDIFF_4:17;
    for x st x in Z holds f1 is_differentiable_in x by A2,TAYLOR_1:2;
    then A8:f1 is_differentiable_on Z by A5,FDIFF_1:16;
A9: for x st x in Z holds (f1`|Z).x=2*x
     proof
       let x;
       assume A10:x in Z;
       2 * (x #Z (2-1))=2 * x by PREPOWER:45;
       then diff(f1,x) =2 * x by TAYLOR_1:2,A2;
       hence thesis by A8,A10,FDIFF_1:def 8;
     end;
A11: for x st x in Z holds (f2+f1).x <> 0
     proof
       let x;
       assume A12:x in Z;
       then A13:(f2+f1).x=f2.x+f1.x by A6,SEQ_1:def 3
       .=1+f1.x by A12,A3
       .=1+x #Z 2 by A2,TAYLOR_1:def 1
       .=1+x |^2 by PREPOWER:46
       .=1+x^2 by NEWTON:100;
       x^2 >=0 by XREAL_1:65;
       then 1+x^2>0+0 by XREAL_1:10;
       hence thesis by A13;
     end;
A14: f1/(f2+f1) is_differentiable_on Z by A7,A8,A11,FDIFF_2:21;
     for x st x in Z holds ((f1/(f2+f1))`|Z).x =2*x/(1+x^2)^2
     proof
       let x;
       assume A15:x in Z;
A16:   f1 is_differentiable_in x by A2,TAYLOR_1:2;
A17:   f2+f1 is_differentiable_in x by A7,FDIFF_1:16,A15;
A18:   f1.x=x #Z 2 by A2,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46
       .=x^2 by NEWTON:100;
A19:    (f2+f1).x=f2.x+f1.x by A6,A15,SEQ_1:def 3
       .=1+f1.x by A15,A3
       .=1+x #Z 2 by A2,TAYLOR_1:def 1
       .=1+x |^2 by PREPOWER:46
       .=1+x^2 by NEWTON:100;
       (f2+f1).x<>0 by A11,A15;
       then diff(f1/(f2+f1),x)
       =(diff(f1,x)*(f2+f1).x - diff(f2+f1,x)*f1.x)/((f2+f1).x)^2
       by FDIFF_2:14,A16,A17
       .=((f1`|Z).x * (f2+f1).x-diff(f2+f1,x) * f1.x)/((f2+f1).x)^2
       by A8,A15,FDIFF_1:def 8
       .= ((f1`|Z).x * (f2+f1).x-((f2+f1)`|Z).x * f1.x)/((f2+f1).x)^2
       by A7,A15,FDIFF_1:def 8
       .=((2*x)*(f2+f1).x-((f2+f1)`|Z).x * f1.x)/((f2+f1).x)^2
       by A15,A9
       .=((2*x)*(1+x^2)-(2*x)*x^2)/(1+x^2)^2 by A15,A6,A2,A4,FDIFF_4:17,A19,A18
       .=2*x/(1+x^2)^2;
       hence thesis by A15,A14,FDIFF_1:def 8;
     end;
     hence thesis by A7,A8,A11,FDIFF_2:21;
   end;

:: 6 f.x=(1/2)*ln(x^2/(1+x^2))
theorem
  Z c= dom ((1/2)(#)f) & f=ln*(f1/(f2+f1)) & f1=#Z 2 &
  (for x st x in Z holds f2.x=1 & x<>0)
  implies (1/2)(#)f is_differentiable_on Z & for x st x in Z holds
  (((1/2)(#)f)`|Z).x =1/(x*(1+x^2))
  proof
    assume that A1:Z c= dom ((1/2)(#)f) and
A2: f=ln*(f1/(f2+f1)) and
A3: f1=#Z 2 and
A4: (for x st x in Z holds f2.x=1 & x<>0);
A5: Z c= dom f by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom (f1/(f2+f1)) by FUNCT_1:21,A2,A5;
    then A6:Z c= dom (f1/(f2+f1)) by TARSKI:def 3;
    then A7:f1/(f2+f1) is_differentiable_on Z & for x
    st x in Z holds ((f1/(f2+f1))`|Z).x =2*x/(1+x^2)^2 by A3,A4,Th7;
    Z c= dom f1 /\ (dom (f2+f1) \ (f2+f1)"{0}) by RFUNCT_1:def 4,A6;
    then A8:Z c= dom f1 & Z c= dom (f2+f1) \ (f2+f1)"{0} by XBOOLE_1:18;
    A9:Z c= dom (f2+f1) by XBOOLE_1:1,A8;
A10: for x st x in Z holds (f1/(f1+f2)).x >0
     proof
       let x;
       assume A11:x in Z;
A12:    f1.x=x #Z 2 by A3,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46
       .=x^2 by NEWTON:100;
A13:    (f2+f1).x=f2.x+f1.x by A9,A11,SEQ_1:def 3
       .=1+x^2 by A12,A11,A4;
       x<>0 by A11,A4;
       then A14:f1.x>0 by A12,SQUARE_1:74;
       then A15:1+x^2>0+0 by A12,XREAL_1:10;
       (f1/(f2+f1)).x=f1.x * ((f2+f1).x)" by RFUNCT_1:def 4,A6,A11
       .=f1.x/(f2+f1).x by XCMPLX_0:def 9;
       hence thesis by A14,A15,A13,XREAL_1:141;
     end;
     for x st x in Z holds ln*(f1/(f2+f1))
     is_differentiable_in x
     proof
       let x;
       assume A16:x in Z;
       then A17:f1/(f2+f1) is_differentiable_in x by FDIFF_1:16,A7;
       (f1/(f1+f2)).x >0 by A10,A16;
       hence ln*(f1/(f2+f1)) is_differentiable_in x
       by TAYLOR_1:20,A17;
     end;
     then A18:f is_differentiable_on Z by A5,A2,FDIFF_1:16;
     for x st x in Z holds (((1/2)(#)f)`|Z).x =1/(x*(1+x^2))
     proof
       let x;
       assume A19:x in Z;
A20:    f1.x=x #Z 2 by A3,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46
       .=x^2 by NEWTON:100;
A21:    (f2+f1).x=f2.x+f1.x by A9,A19,SEQ_1:def 3
       .=1+x^2 by A20,A19,A4;
A22:    (f1/(f2+f1)).x=f1.x * ((f2+f1).x)" by RFUNCT_1:def 4,A6,A19
       .=x^2/(1+x^2) by A20,A21,XCMPLX_0:def 9;
A23:   f1/(f2+f1) is_differentiable_in x by FDIFF_1:16,A7,A19;
A24:   (f1/(f1+f2)).x >0 by A10,A19;
A25:   x<>0 by A19,A4;
       then x^2>0 by SQUARE_1:74;
       then A26:1+x^2>0+0 by XREAL_1:10;
       (((1/2)(#)f)`|Z).x
       =(1/2)*diff(ln*(f1/(f2+f1)),x) by A2,A18,A1,FDIFF_1:28,A19
       .=(1/2)*(diff((f1/(f2+f1)),x)/(f1/(f2+f1)).x) by TAYLOR_1:20,A23,A24
       .=(1/2)*(((f1/(f2+f1))`|Z).x/(f1/(f2+f1)).x) by A19,FDIFF_1:def 8,A7
       .=(1/2)*((2*x/(1+x^2)^2)/(x^2/(1+x^2))) by A22,A6,A3,A4,Th7,A19
       .=(1/2)*((2*x)/(1+x^2)^2)/(x^2/(1+x^2)) by XCMPLX_1:75
       .=((1/2)*(2*x))/(1+x^2)^2/(x^2/(1+x^2)) by XCMPLX_1:75
       .=x/(1+x^2)/(1+x^2)/(x^2/(1+x^2)) by XCMPLX_1:79
       .=x/(1+x^2)/(x^2/(1+x^2))/(1+x^2) by XCMPLX_1:48
       .=x/x^2/(1+x^2) by A26,XCMPLX_1:55
       .=x/x/x/(1+x^2) by XCMPLX_1:79
       .=1/x/(1+x^2) by XCMPLX_1:60,A25
       .=1/(x*(1+x^2)) by XCMPLX_1:79;
       hence thesis;
     end;
     hence thesis by A18,A1,FDIFF_1:28;
   end;

:: 7 f.x=ln(x|^n)
theorem
  Z c= dom (ln*( #Z n)) & (for x st x in Z holds x>0)
  implies ln*( #Z n) is_differentiable_on Z & for x
  st x in Z holds ((ln*( #Z n))`|Z).x =n/x
  proof
    assume that A1:Z c= dom (ln*( #Z n)) and
A2: for x st x in Z holds x > 0;
A3: for x st x in Z holds x #Z n > 0
    proof
      let x;
      assume x in Z;
      then x>0 by A2;
      hence x #Z n > 0 by PREPOWER:49;
    end;
A4: for x st x in Z holds ln*( #Z n) is_differentiable_in x
    proof
      let x;
      assume A5:x in Z;
A6:   #Z n is_differentiable_in x by TAYLOR_1:2;
      #Z n.x=x #Z n by TAYLOR_1:def 1;
      then #Z n.x>0 by A3,A5;
      hence ln*( #Z n) is_differentiable_in x
      by TAYLOR_1:20,A6;
    end;
    then A7:ln*( #Z n) is_differentiable_on Z
    by A1,FDIFF_1:16;
    for x st x in Z holds ((ln*( #Z n))`|Z).x =n/x
    proof
      let x;
      assume A8:x in Z;
      then A9:x>0 by A2;
      then A10:x |^n >0 by NEWTON:102;
A11:   #Z n is_differentiable_in x &
      diff(( #Z n),x) = n * x #Z (n-1) by TAYLOR_1:2;
A12:   #Z n.x=x #Z n by TAYLOR_1:def 1;
      then #Z n.x>0 by A3,A8;
      then diff(ln*( #Z n),x)
      = n * x #Z (n-1)/x #Z n by A12,TAYLOR_1:20,A11
      .=n * x #Z (n-1)/x |^ n by PREPOWER:46
      .=n * (x |^ n/x |^ 1)/x |^ n by PREPOWER:53,A9
      .=n * (x |^ n/x |^ 1/x |^ n) by XCMPLX_1:75
      .=n * (x |^ n/x |^ n/x |^ 1) by XCMPLX_1:48
      .=n * (1/(x |^ 1)) by A10,XCMPLX_1:60
      .=n * (1/x) by NEWTON:10
      .=n*1/x by XCMPLX_1:75
      .=n/x;
      hence thesis by A7,A8,FDIFF_1:def 8;
    end;
    hence thesis by A4,A1,FDIFF_1:16;
  end;

:: 8 f.x=1/x+ln((x-1)/x)
theorem
  Z c= dom (f2^+ln*(f1/f2)) & (for x st x in Z holds f2.x = x
  & f2.x>0 & f1.x=x-1 & f1.x >0 ) implies f2^+ln*(f1/f2)
  is_differentiable_on Z & for x st x in Z holds
  ( (f2^+ln*(f1/f2))`|Z).x= 1/(x^2*(x-1))
  proof
    assume that A1:Z c= dom (f2^+ln*(f1/f2)) and
A2: for x st x in Z holds f2.x = x & f2.x>0 & f1.x=x-1 & f1.x >0;
A3: Z c= dom (f2^) /\ dom (ln*(f1/f2)) by SEQ_1:def 3,A1;
    then A4:Z c= dom (f2^) by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then A5:Z c= dom f2 by A4,XBOOLE_1:1;
A6: for x st x in Z holds f2.x = 0+x & f2.x<>0 by A2;
    then A7:f2^ is_differentiable_on Z & for x st x in Z holds
    ((f2^)`|Z).x= -1/(0+x)^2 by A5,FDIFF_4:14;
A8: Z c=dom (ln*(f1/f2)) by XBOOLE_1:18,A3;
A9: for x st x in Z holds f1.x=x-1 & f1.x >0 & f2.x = x-0 & f2.x>0 by A2;
     then A10:ln*(f1/f2) is_differentiable_on Z & for x st x in Z
     holds ((ln*(f1/f2))`|Z).x=(1-0)/((x-1)*(x-0))
       by A8,FDIFF_4:24;
     for x st x in Z holds
     ((f2^+ln*(f1/f2))`|Z).x= 1/(x^2*(x-1))
     proof
       let x;
       assume A11:x in Z;
       then A12:f2.x = x & f2.x>0 & f1.x=x-1 & f1.x >0 by A2;
       ((f2^+ln*(f1/f2))`|Z).x
       =diff(f2^,x) + diff(ln*(f1/f2),x)
         by FDIFF_1:26,A7,A10,A1,A11
       .=((f2^)`|Z).x+ diff(ln*(f1/f2),x) by A7,A11,FDIFF_1:def 8
       .=((f2^)`|Z).x+((ln*(f1/f2))`|Z).x by A10,A11,FDIFF_1:def 8
       .= -1/(0+x)^2+((ln*(f1/f2))`|Z).x by A11,A6,A5,FDIFF_4:14
       .=-1/(0+x)^2+(1-0)/((x-1)*(x-0)) by A11,A9,A8,FDIFF_4:24
       .=-(1*(x-1))/(x^2*(x-1))+1/((x-1)*x) by A12,XCMPLX_1:92
       .=-(1*(x-1))/(x^2*(x-1))+(1*x)/((x-1)*x*x) by A12,XCMPLX_1:92
       .=(-(x-1))/(x^2*(x-1))+x/(x^2*(x-1)) by XCMPLX_1:188
       .=((-x+1)+x)/(x^2*(x-1)) by XCMPLX_1:63
       .=1/(x^2*(x-1));
       hence thesis;
     end;
     hence thesis by FDIFF_1:26,A7,A10,A1;
   end;

:: 10 f.x=a #R x=exp.(x*ln a)
theorem Th11:
  Z c= dom (exp_R*f) & (for x st x in Z holds f.x=x*log(number_e,a)) &
  a>0 implies
  exp_R*f is_differentiable_on Z &
  for x st x in Z holds ((exp_R*f)`|Z).x =a #R x*log(number_e,a)
  proof
    assume that A1:Z c= dom (exp_R*f) and
A2: for x st x in Z holds f.x=x*log(number_e,a) and
A3: a>0;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A4:Z c= dom f by TARSKI:def 3;
A5: for x st x in Z holds f.x = log(number_e,a)*x+0 by A2;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x =log(number_e,a) by FDIFF_1:31,A4;
A7: for x st x in Z holds exp_R*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A6;
      hence exp_R*f is_differentiable_in x by TAYLOR_1:19;
    end;
    then A8:exp_R*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((exp_R*f)`|Z).x = a #R x*log(number_e,a)
    proof
      let x;
      assume A9:x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A6;
      then diff(exp_R*f,x) = exp_R.(f.x)*diff(f,x) by TAYLOR_1:19
      .=exp_R.(f.x)*(f`|Z).x by FDIFF_1:def 8,A9,A6
      .=exp_R.(f.x)*(log(number_e,a)) by A5,FDIFF_1:31,A4,A9
      .=exp_R.(x*log(number_e,a))*(log(number_e,a)) by A9,A2
      .=a #R x*log(number_e,a) by A3,Th1;
      hence thesis by A9,A8,FDIFF_1:def 8;
    end;
    hence thesis by A7,A1,FDIFF_1:16;
  end;

:: 11 f.x=(a #R x)*(x-1/ln a)/ln a
theorem
  Z c= dom ((1/log(number_e,a))(#)((exp_R*f1)(#)f2)) &
  (for x st x in Z holds f1.x=x*log(number_e,a) & f2.x=x-1/log(number_e,a)) &
  a>0 & a<>1 implies
  (1/log(number_e,a))(#)((exp_R*f1)(#)f2) is_differentiable_on Z &
  for x st x in Z holds
  (((1/log(number_e,a))(#)((exp_R*f1)(#)f2))`|Z).x =x * a #R x
  proof
    assume that A1:Z c= dom ((1/log(number_e,a))(#)((exp_R*f1)(#)f2)) and
A2: (for x st x in Z holds f1.x=x*log(number_e,a) & f2.x=x-1/log(number_e,a))
    & a>0 & a<>1;
A3: (for x st x in Z holds f1.x=x*log(number_e,a)) & a>0 by A2;
A4: Z c= dom ((exp_R*f1)(#)f2) by SEQ_1:def 6,A1;
    then Z c= dom (exp_R*f1) /\ dom f2 by SEQ_1:def 5;
    then A5:Z c= dom (exp_R*f1) & Z c= dom f2 by XBOOLE_1:18;
    then A6:exp_R*f1 is_differentiable_on Z & for x st x in Z holds
    ((exp_R*f1)`|Z).x =a #R x*log(number_e,a) by A3,Th11;
A7: for x st x in Z holds f2.x = 1*x+-1/log(number_e,a)
     proof let x;
       assume A8:x in Z;
       1*x+-1/log(number_e,a)=1*x-1/log(number_e,a);
       hence thesis by A2,A8;
     end;
     then A9:f2 is_differentiable_on Z & for x st x in Z holds
     (f2`|Z).x=1 by FDIFF_1:31,A5;
     then A10:((exp_R*f1)(#)f2) is_differentiable_on Z by A4,A6,FDIFF_1:29;
A11:  log(number_e,a)<>0
     proof
       assume log(number_e,a)=0;
       then A12:log(number_e,a)=log(number_e,1) by SIN_COS2:13,TAYLOR_1:13;
A13:    number_e>0 & number_e<>1 by TAYLOR_1:11;
       then a=(number_e) to_power log(number_e,1) by A12,POWER:def 3,A2
       .=1 by POWER:def 3,A13;
       hence contradiction by A2;
     end;
     for x st x in Z holds
     (((1/log(number_e,a))(#)((exp_R*f1)(#)f2))`|Z).x =x * a #R x
     proof
       let x;
       assume A14:x in Z;
       then A15:(exp_R*f1).x=exp_R.(f1.x) by A5,FUNCT_1:22
       .=exp_R.(x*log(number_e,a)) by A14,A2
       .=a #R x by A2,Th1;
       (((1/log(number_e,a))(#)((exp_R*f1)(#)f2))`|Z).x
       =(1/log(number_e,a))*diff((exp_R*f1)(#)f2,x) by A1,A10,FDIFF_1:28,A14
       .=(1/log(number_e,a))*(((exp_R*f1)(#)f2)`|Z).x by FDIFF_1:def 8,A14,A10
       .=(1/log(number_e,a))*(f2.x*diff(exp_R*f1,x)+(exp_R*f1).x*diff(f2,x))
       by A14,A4,A6,A9,FDIFF_1:29
       .=(1/log(number_e,a))*(f2.x*((exp_R*f1)`|Z).x+(exp_R*f1).x*diff(f2,x))
       by A6,A14,FDIFF_1:def 8
       .=(1/log(number_e,a))*(f2.x*((exp_R*f1)`|Z).x+(exp_R*f1).x*(f2`|Z).x)
       by A9,A14,FDIFF_1:def 8
       .=(1/log(number_e,a))*(f2.x*(a #R x*log(number_e,a))
       +(exp_R*f1).x*(f2`|Z).x) by A5,A3,Th11,A14
       .=(1/log(number_e,a))*(f2.x*(a#R x*log(number_e,a))+(exp_R*f1).x*1)
       by A7,FDIFF_1:31,A5,A14
       .=(1/log(number_e,a))*((f2.x*log(number_e,a)+1)*a #R x) by A15
       .=(1/log(number_e,a))*(((x-1/log(number_e,a))*
         log(number_e,a)+1)*a #R x) by A2,A14
       .=(1/log(number_e,a))*(x*log(number_e,a)
       -1/log(number_e,a)*log(number_e,a)+1)*a #R x
       .=(1/log(number_e,a))*(x*log(number_e,a)-1+1)*a #R x by A11,XCMPLX_1:107
       .=1/log(number_e,a)*log(number_e,a)*x*a #R x
       .=1*x*a #R x by A11,XCMPLX_1:107
       .=x*a #R x;
       hence thesis;
     end;
     hence thesis by A1,FDIFF_1:28,A10;
   end;

:: 12 f.x=a #R x*exp(x)/(1+ln a)
theorem
  Z c= dom ((1/(1+log(number_e,a)))(#)((exp_R*f)(#)exp_R)) &
  (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 & a<>1/number_e
  implies
  (1/(1+log(number_e,a)))(#)((exp_R*f)(#)exp_R) is_differentiable_on Z &
  for x st x in Z holds
  (((1/(1+log(number_e,a)))(#)((exp_R*f)(#)exp_R))`|Z).x =a #R x*exp_R.x
  proof
    assume that A1:Z c= dom ((1/(1+log(number_e,a)))(#)((exp_R*f)(#)exp_R)) and
A2: (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 and
A3: a<>1/number_e;
A4: Z c= dom ((exp_R*f)(#)exp_R) by SEQ_1:def 6,A1;
    then Z c= dom (exp_R*f) /\ dom exp_R by SEQ_1:def 5;
    then A5:Z c= dom (exp_R*f) & Z c= dom exp_R by XBOOLE_1:18;
    then A6:exp_R*f is_differentiable_on Z & for x st x in Z holds
    ((exp_R*f)`|Z).x =a #R x*log(number_e,a) by A2,Th11;
A7: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A8:(exp_R*f)(#)exp_R is_differentiable_on Z by A4,A6,FDIFF_1:29;
A9: 1+log(number_e,a)<>0
    proof
      assume A10:1+log(number_e,a)=0;
A11:   number_e>0 & number_e<>1 by TAYLOR_1:11;
A12:   number_e*a>0*a by TAYLOR_1:11,XREAL_1:70,A2;
A13:   log(number_e,1)=0 by TAYLOR_1:13,SIN_COS2:13
      .=log(number_e,number_e)+log(number_e,a) by A10,POWER:60,A11
      .=log(number_e,number_e*a) by POWER:61,A11,A2;
      number_e*a=(number_e) to_power log(number_e,1) by A13,POWER:def 3,A11,A12
      .=1 by POWER:def 3,A11;
      hence contradiction by A3,XCMPLX_1:74;
    end;
    for x st x in Z holds
    (((1/(1+log(number_e,a)))(#)((exp_R*f)(#)exp_R))`|Z).x =a #R x*exp_R.x
    proof
      let x;
      assume A14:x in Z;
      then A15:(exp_R*f).x=exp_R.(f.x) by A5,FUNCT_1:22
      .=exp_R.(x*log(number_e,a)) by A14,A2
      .=a #R x by A2,Th1;
      (((1/(1+log(number_e,a)))(#)((exp_R*f)(#)exp_R))`|Z).x
      =(1/(1+log(number_e,a)))*diff((exp_R*f)(#)exp_R,x) by A1,A8,FDIFF_1:28,
A14
      .=(1/(1+log(number_e,a)))*(((exp_R*f)(#)exp_R)`|Z).x
        by FDIFF_1:def 8,A14,A8
      .=(1/(1+log(number_e,a)))*(exp_R.x*diff(exp_R*f,x)+
       (exp_R*f).x*diff(exp_R,x)) by A14,A7,A6,A4,FDIFF_1:29
      .=(1/(1+log(number_e,a)))*(exp_R.x*((exp_R*f)`|Z).x+
         (exp_R*f).x*diff(exp_R,x)) by A6,A14,FDIFF_1:def 8
      .=(1/(1+log(number_e,a)))*(exp_R.x*((exp_R*f)`|Z).x+(exp_R*f).x*exp_R.x)
      by TAYLOR_1:16
      .=(1/(1+log(number_e,a)))*((((exp_R*f)`|Z).x+(exp_R*f).x)*exp_R.x)
      .=(1/(1+log(number_e,a)))*((a #R x*log(number_e,a)+(exp_R*f).x)*exp_R.x)
      by A5,A2,Th11,A14
      .=(1/(1+log(number_e,a)))*(log(number_e,a)+1)*a #R x*exp_R.x by A15
      .=1*a #R x*exp_R.x by A9,XCMPLX_1:107
      .=a #R x*exp_R.x;
      hence thesis;
    end;
    hence thesis by A8,A1,FDIFF_1:28;
  end;

:: 13 f.x=exp_R(-x)
theorem Th14:
  Z c= dom (exp_R*f) & (for x st x in Z holds f.x=-x)
  implies exp_R*f is_differentiable_on Z &
  for x st x in Z holds ((exp_R*f)`|Z).x = -exp_R(-x)
  proof
    assume that A1:Z c= dom (exp_R*f) and
A2: for x st x in Z holds f.x=-x;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A3:Z c= dom f by TARSKI:def 3;
A4: for x st x in Z holds f.x=(-1)*x+0
    proof
      let x;
      assume x in Z;
      then f.x=-x by A2
      .=(-1)*x+0;
      hence thesis;
    end;
    then A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A3;
A6: for x st x in Z holds exp_R*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A5;
      hence exp_R*f is_differentiable_in x by TAYLOR_1:19;
    end;
    then A7:exp_R*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((exp_R*f)`|Z).x = -exp_R(-x)
    proof
      let x;
      assume A8:x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A5;
      then diff(exp_R*f,x) = exp_R.(f.x)*diff(f,x) by TAYLOR_1:19
      .=exp_R.(f.x)*(f`|Z).x by FDIFF_1:def 8,A8,A5
      .=exp_R.(f.x)*(-1) by A4,FDIFF_1:31,A3,A8
      .=exp_R.(-x)*(-1) by A8,A2
      .=-exp_R.(-x)
      .=-exp_R(-x) by SIN_COS:def 27;
      hence thesis by A8,A7,FDIFF_1:def 8;
    end;
    hence thesis by A6,A1,FDIFF_1:16;
  end;

:: 14 f.x=-(x+1)*exp_R(-x)
theorem
  Z c= dom (f1(#)(exp_R*f2)) & (for x st x in Z holds f1.x=-x-1 & f2.x=-x)
  implies f1(#)(exp_R*f2) is_differentiable_on Z &
  for x st x in Z holds ((f1(#)(exp_R*f2))`|Z).x = x/exp_R(x)
  proof
    assume that A1:Z c= dom (f1(#)(exp_R*f2)) and
A2: (for x st x in Z holds f1.x=-x-1 & f2.x=-x);
A3: for x st x in Z holds f2.x=-x by A2;
    Z c= dom f1 /\ dom (exp_R*f2) by SEQ_1:def 5,A1;
    then A4:Z c= dom f1 & Z c= dom (exp_R*f2) by XBOOLE_1:18;
A5: for x st x in Z holds f1.x=(-1)*x+-1
    proof
      let x;
      assume x in Z;
      then f1.x=-x-1 by A2
      .=(-1)*x+-1;
      hence thesis;
    end;
    then A6:f1 is_differentiable_on Z &
    for x st x in Z holds (f1`|Z).x = -1 by FDIFF_1:31,A4;
A7: exp_R*f2 is_differentiable_on Z &
    for x st x in Z holds ((exp_R*f2)`|Z).x = -exp_R(-x) by A3,A4,Th14;
    for x st x in Z holds ((f1(#)(exp_R*f2))`|Z).x = x/exp_R(x)
    proof
      let x;
      assume A8:x in Z;
      then A9:(exp_R*f2).x=exp_R.(f2.x) by A4,FUNCT_1:22
      .=exp_R.(-x) by A8,A2
      .=exp_R(-x) by SIN_COS:def 27;
      ((f1(#)(exp_R*f2))`|Z).x
      =((exp_R*f2).x)*diff(f1,x) + (f1.x)*diff(exp_R*f2,x)
        by A1,A7,A6,FDIFF_1:29,A8
      .=exp_R(-x)*diff(f1,x) + (-x-1)*diff(exp_R*f2,x) by A9,A8,A2
      .=exp_R(-x)*(f1`|Z).x + (-x-1)*diff(exp_R*f2,x) by A8,A6,FDIFF_1:def 8
      .=exp_R(-x)*(f1`|Z).x + (-x-1)*((exp_R*f2)`|Z).x by A8,A7,FDIFF_1:def 8
      .=exp_R(-x)*(-1) + (-x-1)*((exp_R*f2)`|Z).x by A5,FDIFF_1:31,A4,A8
      .=exp_R(-x)*(-1) + (-x-1)*(-exp_R(-x)) by A3,A4,Th14,A8
      .=x*exp_R(-x)
      .=x*(1/exp_R(x)) by TAYLOR_1:4
      .=x/exp_R(x) by XCMPLX_1:100;
      hence thesis;
    end;
    hence thesis by A1,A6,FDIFF_1:29,A7;
  end;

:: 16 f.x=a #R (-x)=exp_R.(-x*ln a)
theorem Th16:
  Z c= dom -(exp_R*f) & (for x st x in Z holds f.x=-x*log(number_e,a))
  & a>0 implies
  -(exp_R*f) is_differentiable_on Z &
  for x st x in Z holds ((-(exp_R*f))`|Z).x =a #R (-x)*log(number_e,a)
  proof
    assume that A1:Z c= dom -(exp_R*f) and
A2: (for x st x in Z holds f.x=-x*log(number_e,a)) and
A3: a>0;
A4: Z c= dom (exp_R*f) by A1,SEQ_1:def 7;
    for y st y in Z holds y in dom f by FUNCT_1:21,A4;
    then A5:Z c= dom f by TARSKI:def 3;
A6: for x st x in Z holds f.x = (-log(number_e,a))*x+0
    proof
      let x;
      assume x in Z;
      then f.x=-log(number_e,a)*x by A2
      .=(-log(number_e,a))*x+0;
      hence thesis;
    end;
    then A7:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x =-log(number_e,a) by FDIFF_1:31,A5;
    for x st x in Z holds exp_R*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A7;
      hence exp_R*f is_differentiable_in x by TAYLOR_1:19;
    end;
    then A8:exp_R*f is_differentiable_on Z by A4,FDIFF_1:16;
A9: Z c= dom ((-1)(#)(exp_R*f)) by A1,RFUNCT_1:38;
    then A10:(-1)(#)(exp_R*f) is_differentiable_on Z & for x st x in Z holds
    (((-1)(#)(exp_R*f))`|Z).x=(-1)*diff((exp_R*f),x) by FDIFF_1:28,A8;
    for x st x in Z holds ((-(exp_R*f))`|Z).x = a #R (-x)*log(number_e,a)
    proof
      let x;
      assume A11:x in Z;
      then A12:f is_differentiable_in x by FDIFF_1:16,A7;
      ((-(exp_R*f))`|Z).x=(((-1)(#)(exp_R*f))`|Z).x by RFUNCT_1:38
      .=(-1)*diff(exp_R*f,x) by A11,FDIFF_1:28,A9,A8
      .=(-1)*(exp_R.(f.x)*diff(f,x)) by TAYLOR_1:19,A12
      .=(-1)*(exp_R.(f.x)*(f`|Z).x ) by FDIFF_1:def 8,A11,A7
      .=(-1)*(exp_R.(f.x)*(-log(number_e,a))) by A6,FDIFF_1:31,A5,A11
      .=(-1)*(exp_R.(-x*log(number_e,a))*(-log(number_e,a))) by A11,A2
      .=(-1)*(a #R (-x)*(-log(number_e,a))) by A3,Th2
      .=a #R (-x)*log(number_e,a);
      hence thesis;
    end;
    hence thesis by RFUNCT_1:38,A10;
  end;

:: 17 f.x=-(a #R -x)*(x+1/ln a)/ln a
theorem
  Z c= dom ((1/log(number_e,a))(#)((-(exp_R*f1))(#)f2)) &
  (for x st x in Z holds f1.x=-x*log(number_e,a) & f2.x=x+1/log(number_e,a)) &
  a>0 & a<>1 implies
  (1/log(number_e,a))(#)((-(exp_R*f1))(#)f2) is_differentiable_on Z &
  for x st x in Z holds
  (((1/log(number_e,a))(#)((-(exp_R*f1))(#)f2))`|Z).x =x /a #R x
  proof
    assume that A1:
    Z c= dom ((1/log(number_e,a))(#)((-(exp_R*f1))(#)f2)) and
A2: (for x st x in Z holds f1.x=-x*log(number_e,a) & f2.x=x+1/log(number_e,a))
    & a>0 & a<>1;
A3: (for x st x in Z holds f1.x=-x*log(number_e,a)) & a>0 by A2;
A4: Z c= dom ((-(exp_R*f1))(#)f2) by SEQ_1:def 6,A1;
    then Z c= dom (-(exp_R*f1)) /\ dom f2 by SEQ_1:def 5;
    then A5:Z c= dom (-(exp_R*f1)) & Z c= dom f2 by XBOOLE_1:18;
    then A6: -(exp_R*f1) is_differentiable_on Z &
    for x st x in Z holds ((-(exp_R*f1))`|Z).x =a #R (-x)*log(number_e,a)
    by A3,Th16;
A7: for x st x in Z holds f2.x = 1*x+1/log(number_e,a) by A2;
     then A8:f2 is_differentiable_on Z & for x st x in Z holds
     (f2`|Z).x=1 by FDIFF_1:31,A5;
     then A9:(-(exp_R*f1))(#)f2 is_differentiable_on Z by A4,A6,FDIFF_1:29;
A10:  log(number_e,a)<>0
     proof
       assume A11:log(number_e,a)=0;
A12:    log(number_e,a)=log(number_e,1) by SIN_COS2:13,A11,TAYLOR_1:13;
A13:    number_e>0 & number_e<>1 by TAYLOR_1:11;
       then a=(number_e) to_power log(number_e,1) by A12,POWER:def 3,A2
       .=1 by POWER:def 3,A13;
       hence contradiction by A2;
     end;
     for x st x in Z holds
     (((1/log(number_e,a))(#)((-(exp_R*f1))(#)f2))`|Z).x =x /a #R x
     proof
       let x;
       assume A14:x in Z;
       then x in dom -(exp_R*f1) by A5;
       then A15:x in dom (exp_R*f1) by SEQ_1:def 7;
A16:    (-(exp_R*f1)).x=-((exp_R*f1).x) by SEQ_1:def 7,A14,A5
       .=-(exp_R.(f1.x)) by FUNCT_1:22,A15
       .=-(exp_R.(-x*log(number_e,a))) by A14,A2
       .=-a #R (-x) by A2,Th2;
       (((1/log(number_e,a))(#)((-(exp_R*f1))(#)f2))`|Z).x
       =(1/log(number_e,a))*diff((-(exp_R*f1))(#)f2,x) by A1,A9,FDIFF_1:28,A14
       .=(1/log(number_e,a))*(((-(exp_R*f1))(#)f2)`|Z).x by FDIFF_1:def 8,A14,
A9
       .=(1/log(number_e,a))*(f2.x*diff(-(exp_R*f1),x)+
        (-(exp_R*f1)).x*diff(f2,x)) by A14,A4,A6,A8,FDIFF_1:29
       .=(1/log(number_e,a))*(f2.x*((-(exp_R*f1))`|Z).x+
        (-(exp_R*f1)).x*diff(f2,x)) by A6,A14,FDIFF_1:def 8
       .=(1/log(number_e,a))*(f2.x*((-(exp_R*f1))`|Z).x+
         (-(exp_R*f1)).x*(f2`|Z).x)
       by A8,A14,FDIFF_1:def 8
       .=(1/log(number_e,a))*(f2.x*(a #R (-x)*log(number_e,a))
       +(-(exp_R*f1)).x*(f2`|Z).x) by A5,A3,Th16,A14
       .=(1/log(number_e,a))*(f2.x*(a #R (-x)*log(number_e,a))
       +(-(exp_R*f1)).x*1) by A7,FDIFF_1:31,A5,A14
       .=(1/log(number_e,a))*((f2.x*log(number_e,a)-1)*a #R (-x)) by A16
       .=(1/log(number_e,a))*(((x+1/log(number_e,a))*
         log(number_e,a)-1)*a #R (-x)) by A2,A14
       .=(1/log(number_e,a))*
       (x*log(number_e,a)+1/log(number_e,a)*log(number_e,a)-1)*a #R (-x)
       .=(1/log(number_e,a))*(x*log(number_e,a)+1-1)*a #R (-x)
         by A10,XCMPLX_1:107
       .=(1/log(number_e,a))*log(number_e,a)*x*a #R (-x)
       .=1*x*a #R (-x) by A10,XCMPLX_1:107
       .=x*(1/a #R x) by PREPOWER:90,A2
       .=x/a #R x by XCMPLX_1:100;
       hence thesis;
     end;
     hence thesis by A9,A1,FDIFF_1:28;
   end;

:: 18 f.x=(1/(ln a-1))*a #R x/exp_R(x)
theorem
  Z c= dom ((1/(log(number_e,a)-1))(#)((exp_R*f)/exp_R)) &
  (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 & a<>number_e
  implies
  (1/(log(number_e,a)-1))(#)((exp_R*f)/exp_R) is_differentiable_on Z &
  for x st x in Z holds
  (((1/(log(number_e,a)-1))(#)((exp_R*f)/exp_R))`|Z).x =a #R x/exp_R.x
  proof
    assume that A1:Z c= dom ((1/(log(number_e,a)-1))(#)((exp_R*f)/exp_R)) and
A2: (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 and
A3: a<>number_e;
    Z c= dom ((exp_R*f)/exp_R) by SEQ_1:def 6,A1;
    then Z c= dom (exp_R*f) /\ (dom exp_R \ (exp_R)"{0}) by RFUNCT_1:def 4;
    then A4:Z c= dom (exp_R*f) by XBOOLE_1:18;
A5: exp_R*f is_differentiable_on Z & for x st x in Z holds
    ((exp_R*f)`|Z).x =a #R x*log(number_e,a) by A2,A4,Th11;
A6: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for x st x in Z holds exp_R.x<>0 by SIN_COS:59;
    then A7:(exp_R*f)/exp_R is_differentiable_on Z by A5,A6,FDIFF_2:21;
A8: log(number_e,a)-1<>0
     proof
       assume A9:log(number_e,a)-1=0;
A10:    number_e>0 & number_e<>1 by TAYLOR_1:11;
A11:    log(number_e,a)=log(number_e,number_e) by A9,A10,POWER:60;
       a=(number_e) to_power log(number_e,number_e) by A11,POWER:def 3,A10,A2
       .=number_e by POWER:def 3,A10;
       hence contradiction by A3;
     end;
     for x st x in Z holds
     (((1/(log(number_e,a)-1))(#)((exp_R*f)/exp_R))`|Z).x =a #R x/exp_R.x
     proof
       let x;
       assume A12:x in Z;
       then A13:(exp_R*f).x=exp_R.(f.x) by FUNCT_1:22,A4
       .=exp_R.(x*log(number_e,a)) by A12,A2
       .=a #R x by A2,Th1;
A14:    exp_R is_differentiable_in x by SIN_COS:70;
A15:   exp_R*f is_differentiable_in x by A12,A5,FDIFF_1:16;
A16:   exp_R.x <>0 by SIN_COS:59;
       (((1/(log(number_e,a)-1))(#)((exp_R*f)/exp_R))`|Z).x
       =(1/(log(number_e,a)-1))*diff(((exp_R*f)/exp_R),x)
         by A1,A7,FDIFF_1:28,A12
       .=(1/(log(number_e,a)-1))*
       ((diff(exp_R*f,x) * exp_R.x - diff(exp_R,x) *(exp_R*f).x)/(exp_R.x)^2)
       by FDIFF_2:14,A14,A15,A16
       .=(1/(log(number_e,a)-1))*((diff(exp_R*f,x)*
       exp_R.x-exp_R.x*a #R x)/(exp_R.x)^2) by SIN_COS:70,A13
       .=(1/(log(number_e,a)-1))*(((diff(exp_R*f,x)-a #R x)*exp_R.x)/
         ((exp_R.x)*(exp_R.x)))
       .=(1/(log(number_e,a)-1))*((diff(exp_R*f,x)-a #R x)/(exp_R.x))
         by XCMPLX_1:92,A16
       .=(1/(log(number_e,a)-1))*(diff(exp_R*f,x)-a #R x)/(exp_R.x)
          by XCMPLX_1:75
       .=(1/(log(number_e,a)-1))*(((exp_R*f)`|Z).x-a #R x)/(exp_R.x)
       by A5,A12,FDIFF_1:def 8
       .=(1/(log(number_e,a)-1))*(a #R x*log(number_e,a)-a #R x)/(exp_R.x)
       by A2,A4,Th11,A12
       .=(1/(log(number_e,a)-1))*(log(number_e,a)-1)*a #R x/exp_R.x
       .=1* a #R x/exp_R.x by A8,XCMPLX_1:107
       .=a #R x/exp_R.x;
       hence thesis;
     end;
     hence thesis by A7,FDIFF_1:28,A1;
   end;

:: 19 f.x=(1/(1-ln a))*exp_R(x)/a #R x
theorem
  Z c= dom ((1/(1-log(number_e,a)))(#)(exp_R/(exp_R*f))) &
  (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 & a<>number_e
  implies
  (1/(1-log(number_e,a)))(#)(exp_R/(exp_R*f)) is_differentiable_on Z &
  for x st x in Z holds
  (((1/(1-log(number_e,a)))(#)(exp_R/(exp_R*f)))`|Z).x =exp_R.x/a #R x
  proof
    assume that A1:Z c= dom ((1/(1-log(number_e,a)))(#)(exp_R/(exp_R*f))) and
A2: (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 and
A3: a<>number_e;
    Z c= dom (exp_R/(exp_R*f)) by SEQ_1:def 6,A1;
    then Z c= dom exp_R /\ (dom (exp_R*f) \ (exp_R*f)"{0}) by RFUNCT_1:def 4;
    then A4:Z c= dom (exp_R*f) \ (exp_R*f)"{0} by XBOOLE_1:18;
    A5:Z c= dom (exp_R*f) by XBOOLE_1:1,A4;
A6: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A7: exp_R*f is_differentiable_on Z & for x st x in Z holds
    ((exp_R*f)`|Z).x =a #R x*log(number_e,a) by A2,A5,Th11;
A8: for x st x in Z holds (exp_R*f).x<>0
    proof
      let x;
      assume x in Z;
      then (exp_R*f).x=exp_R.(f.x) by FUNCT_1:22,A5;
      hence thesis by SIN_COS:59;
    end;
    then A9:exp_R/(exp_R*f) is_differentiable_on Z by A7,A6,FDIFF_2:21;
A10: 1-log(number_e,a)<>0
     proof
       assume A11:1-log(number_e,a)=0;
A12:    number_e>0 & number_e<>1 by TAYLOR_1:11;
A13:    log(number_e,a)=log(number_e,number_e) by A11,A12,POWER:60;
       a=(number_e) to_power log(number_e,number_e) by A13,POWER:def 3,A12,A2
       .=number_e by POWER:def 3,A12;
       hence contradiction by A3;
     end;
     for x st x in Z holds
     (((1/(1-log(number_e,a)))(#)(exp_R/(exp_R*f)))`|Z).x =exp_R.x/a #R x
     proof
       let x;
       assume A14:x in Z;
       then A15:(exp_R*f).x=exp_R.(f.x) by FUNCT_1:22,A5
       .=exp_R.(x*log(number_e,a)) by A14,A2
       .=a #R x by A2,Th1;
A16:   a #R x >0 by PREPOWER:95,A2;
A17:   exp_R is_differentiable_in x by SIN_COS:70;
A18:   exp_R*f is_differentiable_in x by A14,A7,FDIFF_1:16;
A19:   (exp_R*f).x <>0 by A8,A14;
       (((1/(1-log(number_e,a)))(#)(exp_R/(exp_R*f)))`|Z).x
       =(1/(1-log(number_e,a)))*diff((exp_R/(exp_R*f)),x)
         by A1,A9,FDIFF_1:28,A14
       .=(1/(1-log(number_e,a)))*
       ((diff(exp_R,x) * (exp_R*f).x - diff((exp_R*f),x) *
         exp_R.x)/((exp_R*f).x)^2)
       by FDIFF_2:14,A17,A18,A19
       .=(1/(1-log(number_e,a)))*
       ((exp_R.x * a #R x - diff((exp_R*f),x)*exp_R.x)/(a #R x)^2)
         by SIN_COS:70,A15
       .=(1/(1-log(number_e,a)))*((exp_R.x*(a #R x
          -diff((exp_R*f),x)))/(a #R x)^2)
       .=(1/(1-log(number_e,a)))*((exp_R.x*(a #R x -
          ((exp_R*f)`|Z).x))/(a #R x)^2) by A7,A14,FDIFF_1:def 8
       .=(1/(1-log(number_e,a)))*
       ((exp_R.x*(a #R x -a #R x*log(number_e,a)))/(a #R x)^2) by A2,A5,Th11,
A14
       .=(1/(1-log(number_e,a)))*((1-log(number_e,a))*exp_R.x*a #R x)/
        (a #R x)^2 by XCMPLX_1:75
       .=(1/(1-log(number_e,a)))*(1-log(number_e,a))*exp_R.x*a #R x/(a #R x)^2
       .=1*exp_R.x*a #R x/(a #R x)^2 by A10,XCMPLX_1:107
       .=exp_R.x/a #R x by A16,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A9,FDIFF_1:28,A1;
   end;

:: 20 f.x=ln(exp_R.x+1)
theorem
  Z c= dom (ln*(exp_R+f)) & (for x st x in Z holds f.x=1 )
  implies ln*(exp_R+f) is_differentiable_on Z &
  for x st x in Z holds
   ((ln*(exp_R+f))`|Z).x = exp_R.x/(exp_R.x+1)
  proof
    assume that A1:Z c= dom (ln*(exp_R+f)) and
A2: for x st x in Z holds f.x=1;
    for y st y in Z holds y in dom (exp_R+f) by FUNCT_1:21,A1;
    then A3:Z c= dom (exp_R+f) by TARSKI:def 3;
    then Z c= dom exp_R /\ dom f by SEQ_1:def 3;
    then A4:Z c= dom f by XBOOLE_1:18;
A5: for x st x in Z holds f.x=0*x+1 by A2;
     then A6:f is_differentiable_on Z &
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A7:  exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
     then A8:exp_R+f is_differentiable_on Z by A6,A3,FDIFF_1:26;
A9: for x st x in Z holds ((exp_R+f)`|Z).x =exp_R.x
     proof
       let x;
       assume A10:x in Z;
       hence ((exp_R+f)`|Z).x = diff(exp_R,x) + diff(f,x)
         by A7,A6,A3,FDIFF_1:26
       .=exp_R.x+ diff(f,x) by SIN_COS:70
       .=exp_R.x+(f`|Z).x by A10,FDIFF_1:def 8,A6
       .=exp_R.x+0 by A10,A5,FDIFF_1:31,A4
       .=exp_R.x;
     end;
A11:  for x st x in Z holds (exp_R+f).x>0
     proof
       let x;
       assume A12:x in Z;
       then A13:(exp_R+f).x=exp_R.x + f.x by SEQ_1:def 3,A3
       .=exp_R.x +1 by A2,A12;
       exp_R.x>0 by SIN_COS:59;
       hence (exp_R+f).x>0 by A13,XREAL_1:36;
     end;
A14: for x st x in Z holds ln*(exp_R+f) is_differentiable_in x
     proof
       let x;
       assume A15:x in Z;
       then A16:exp_R+f is_differentiable_in x by FDIFF_1:16,A8;
       (exp_R+f).x >0 by A11,A15;
       hence thesis by TAYLOR_1:20,A16;
     end;
     then A17:ln*(exp_R+f) is_differentiable_on Z by A1,FDIFF_1:16
;
     for x st x in Z holds
       ((ln*(exp_R+f))`|Z).x = exp_R.x/(exp_R.x+1)
     proof
       let x;
       assume A18:x in Z;
       then A19:(exp_R+f).x=exp_R.x + f.x by SEQ_1:def 3,A3
       .=exp_R.x +1 by A2,A18;
A20:    exp_R+f is_differentiable_in x by FDIFF_1:16,A8,A18;
A21:   (exp_R+f).x>0 by A11,A18;
       diff(ln*(exp_R+f),x)
       =diff((exp_R+f),x)/((exp_R+f).x) by TAYLOR_1:20,A20,A21
       .=((exp_R+f)`|Z).x/((exp_R+f).x) by A8,A18,FDIFF_1:def 8
       .=exp_R.x/(exp_R.x +1) by A9,A18,A19;
       hence thesis by A18,A17,FDIFF_1:def 8;
     end;
     hence thesis by A14,A1,FDIFF_1:16;
   end;

:: 21 f.x=ln(exp_R.x-1)
theorem
  Z c= dom (ln*(exp_R-f)) & (for x st x in Z holds f.x=1
  & (exp_R-f).x>0) implies ln*(exp_R-f) is_differentiable_on Z &
  for x st x in Z holds
   ((ln*(exp_R-f))`|Z).x = exp_R.x/(exp_R.x-1)
  proof
    assume that A1:Z c= dom (ln*(exp_R-f)) and
A2: for x st x in Z holds f.x=1 & (exp_R-f).x>0;
    for y st y in Z holds y in dom (exp_R-f) by FUNCT_1:21,A1;
    then A3:Z c= dom (exp_R-f) by TARSKI:def 3;
    then Z c= dom exp_R /\ dom f by SEQ_1:def 4;
    then A4:Z c= dom f by XBOOLE_1:18;
A5: for x st x in Z holds f.x=0*x+1 by A2;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A7: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A8:exp_R-f is_differentiable_on Z by A6,A3,FDIFF_1:27;
A9: for x st x in Z holds ((exp_R-f)`|Z).x =exp_R.x
     proof
       let x;
       assume A10:x in Z;
       hence ((exp_R-f)`|Z).x = diff(exp_R,x) - diff(f,x)
         by A7,A6,A3,FDIFF_1:27
       .= exp_R.x- diff(f,x) by SIN_COS:70
       .= exp_R.x-(f`|Z).x by A10,FDIFF_1:def 8,A6
       .= exp_R.x-0 by A10,FDIFF_1:31,A4,A5
       .= exp_R.x;
     end;
A11: for x st x in Z holds ln*(exp_R-f) is_differentiable_in x
     proof
       let x;
       assume A12:x in Z;
       then A13:exp_R-f is_differentiable_in x by FDIFF_1:16,A8;
       (exp_R-f).x >0 by A2,A12;
       hence thesis by TAYLOR_1:20,A13;
     end;
     then A14:ln*(exp_R-f) is_differentiable_on Z by A1,FDIFF_1:16
;
     for x st x in Z holds
      ((ln*(exp_R-f))`|Z).x = exp_R.x/(exp_R.x-1)
     proof
       let x;
       assume A15:x in Z;
       then A16:(exp_R-f).x=exp_R.x - f.x by SEQ_1:def 4,A3
       .=exp_R.x -1 by A2,A15;
A17:    (exp_R-f) is_differentiable_in x by FDIFF_1:16,A8,A15;
A18:   (exp_R-f).x >0 by A15,A2;
       diff(ln*(exp_R-f),x)
       =diff((exp_R-f),x)/((exp_R-f).x) by TAYLOR_1:20,A17,A18
       .=((exp_R-f)`|Z).x/((exp_R-f).x) by A8,A15,FDIFF_1:def 8
       .=exp_R.x/(exp_R.x -1) by A9,A15,A16;
       hence thesis by A15,A14,FDIFF_1:def 8;
     end;
     hence thesis by A11,A1,FDIFF_1:16;
   end;

:: 22 f.x=-ln(1-exp_R.x)
theorem
  Z c= dom -(ln*(f-exp_R)) & (for x st x in Z holds f.x=1
  & (f-exp_R).x>0) implies
      -(ln*(f-exp_R)) is_differentiable_on Z &
  for x st x in Z holds (((-ln*(f-exp_R)))`|Z).x =
      exp_R.x/(1-exp_R.x)
  proof
    assume that A1:Z c= dom -(ln*(f-exp_R)) and
A2: for x st x in Z holds f.x=1 & (f-exp_R).x>0;
A3: Z c= dom ((-1)(#)(ln*(f-exp_R))) by A1,RFUNCT_1:38;
A4: Z c= dom (ln*(f-exp_R)) by SEQ_1:def 7,A1;
    for y st y in Z holds y in dom (f-exp_R) by FUNCT_1:21,A4;
    then A5:Z c= dom (f-exp_R) by TARSKI:def 3;
    then Z c= dom exp_R /\ dom f by SEQ_1:def 4;
    then A6:Z c= dom f by XBOOLE_1:18;
A7: for x st x in Z holds f.x=0*x+1 by A2;
    then A8:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A6;
A9: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A10:f-exp_R is_differentiable_on Z by A8,A5,FDIFF_1:27;
A11: for x st x in Z holds ((f-exp_R)`|Z).x =-exp_R.x
     proof
       let x;
       assume A12:x in Z;
       hence ((f-exp_R)`|Z).x =diff(f,x)-diff(exp_R,x) by A9,A8,A5,FDIFF_1:27
       .= diff(f,x)-exp_R.x by SIN_COS:70
       .= (f`|Z).x-exp_R.x by A12,FDIFF_1:def 8,A8
       .=0-exp_R.x by A12,FDIFF_1:31,A6,A7
       .=-exp_R.x;
     end;
     for x st x in Z holds ln*(f-exp_R) is_differentiable_in x
     proof
       let x;
       assume A13:x in Z;
       then A14:f-exp_R is_differentiable_in x by FDIFF_1:16,A10;
       (f-exp_R).x >0 by A2,A13;
       hence thesis by TAYLOR_1:20,A14;
     end;
     then A15:ln*(f-exp_R) is_differentiable_on Z by A4,FDIFF_1:16
;
     then A16:(-1)(#)(ln*(f-exp_R)) is_differentiable_on Z
     by FDIFF_1:28,A3;
     for x st x in Z holds (((-ln*(f-exp_R)))`|Z).x =
       exp_R.x/(1-exp_R.x)
     proof
       let x;
       assume A17:x in Z;
       then A18:(f-exp_R).x=f.x-exp_R.x by SEQ_1:def 4,A5
       .=1-exp_R.x by A2,A17;
A19:    (f-exp_R) is_differentiable_in x by FDIFF_1:16,A10,A17;
A20:   (f-exp_R).x >0 by A17,A2;
       (((-1)(#)(ln*(f-exp_R)))`|Z).x
       =(-1)*diff((ln*(f-exp_R)),x) by FDIFF_1:28,A15,A17,A3
       .=(-1)*(diff((f-exp_R),x)/((f-exp_R).x)) by TAYLOR_1:20,A19,A20
       .=(-1)*(((f-exp_R)`|Z).x/(f-exp_R).x) by A10,A17,FDIFF_1:def 8
       .=(-1)*((-exp_R.x)/(1-exp_R.x)) by A11,A17,A18
       .=((-1)*(-exp_R.x))/(1-exp_R.x) by XCMPLX_1:75
       .=exp_R.x/(1-exp_R.x);
       hence thesis by RFUNCT_1:38;
     end;
     hence thesis by A16,RFUNCT_1:38;
   end;

:: 23 f.x=exp_R(2*x)+1
theorem Th23:
  Z c= dom ((( #Z 2)*exp_R)+f) & (for x st x in Z holds f.x=1)
  implies (( #Z 2)*exp_R)+f is_differentiable_on Z & for x st x in Z holds
  (((( #Z 2)*exp_R)+f)`|Z).x =2*exp_R(2*x)
  proof
    assume that A1:Z c= dom ((( #Z 2)*exp_R)+f) and
A2: (for x st x in Z holds f.x=1);
    Z c= dom (( #Z 2)*exp_R) /\ dom f by SEQ_1:def 3,A1;
    then A3:Z c= dom (( #Z 2)*exp_R) & Z c= dom f by XBOOLE_1:18;
A4: for x st x in Z holds f.x=0*x+1 by A2;
    then A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A3;
    now let x;
      assume x in Z;
      exp_R is_differentiable_in x by SIN_COS:70;
      hence ( #Z 2)*exp_R is_differentiable_in x by TAYLOR_1:3;
    end;
    then A6:( #Z 2)*exp_R is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds (((( #Z 2)*exp_R)+f)`|Z).x =2*exp_R(2*x)
    proof
      let x;
      assume A7:x in Z;
A8:   exp_R is_differentiable_in x by SIN_COS:70;
      (((( #Z 2)*exp_R)+f)`|Z).x
      =diff(( #Z 2)*exp_R,x)+diff(f,x) by A5,A6,FDIFF_1:26,A1,A7
      .=(2*( (exp_R.x) #Z (2-1)) * diff(exp_R,x))+diff(f,x) by A8,TAYLOR_1:3
      .=2*( (exp_R.x) #Z (2-1)) * exp_R.x+diff(f,x) by SIN_COS:70
      .=2* exp_R.x * exp_R.x+diff(f,x) by PREPOWER:45
      .=2*( exp_R.x * exp_R.x)+diff(f,x)
      .=2*( exp_R( x )* exp_R.x)+diff(f,x) by SIN_COS:def 27
      .=2*( exp_R( x )* exp_R(x))+diff(f,x) by SIN_COS:def 27
      .=2*(exp_R(x+x))+diff(f,x) by SIN_COS:55
      .=2* exp_R(2*x)+(f`|Z).x by FDIFF_1:def 8,A7,A5
      .= 2* exp_R(2*x)+0 by A4,FDIFF_1:31,A3,A7
      .=2* exp_R(2*x);
      hence thesis;
    end;
    hence thesis by A6,A5,FDIFF_1:26,A1;
  end;

:: 24 f.x=(1/2)*ln(exp_R(2*x)+1)
theorem
  Z c= dom ((1/2)(#)(ln*f)) & f=(( #Z 2)*exp_R)+f1 &
  (for x st x in Z holds f1.x=1) implies (1/2)(#)(ln*f)
  is_differentiable_on Z & for x st x in Z holds
  (((1/2)(#)(ln*f))`|Z).x =exp_R(x)/(exp_R(x)+exp_R(-x))
  proof
    assume that A1:Z c= dom ((1/2)(#)(ln*f)) and
A2: f=(( #Z 2)*exp_R)+f1 & (for x st x in Z holds f1.x=1);
A3: Z c= dom (ln*f) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom f by FUNCT_1:21,A3;
    then A4:Z c= dom ((( #Z 2)*exp_R)+f1) by TARSKI:def 3,A2;
    then Z c= dom (( #Z 2)*exp_R) /\ dom f1 by SEQ_1:def 3;
    then A5:Z c= dom (( #Z 2)*exp_R) by XBOOLE_1:18;
A6: (( #Z 2)*exp_R)+f1 is_differentiable_on Z & for x st x in Z holds
    (((( #Z 2)*exp_R)+f1)`|Z).x =2*exp_R(2*x) by A4,A2,Th23;
A7: for x st x in Z holds f.x >0
    proof
      let x;
      assume A8:x in Z;
A9:   ((( #Z 2)*exp_R)+f1).x
      =(( #Z 2)*exp_R).x+f1.x by SEQ_1:def 3,A4,A8
      .=( #Z 2).(exp_R.x)+f1.x by A8,A5,FUNCT_1:22
      .=(exp_R.x) #Z 2+f1.x by TAYLOR_1:def 1
      .=(exp_R.x) #Z 2+1 by A2,A8;
      exp_R.x>0 by SIN_COS:59;
      then (exp_R.x) #Z 2>0 by PREPOWER:49;
      hence thesis by A9,A2,XREAL_1:36;
    end;
    for x st x in Z holds ln*f is_differentiable_in x
    proof
      let x;
      assume A10:x in Z;
      then A11:f is_differentiable_in x by FDIFF_1:16,A6,A2;
      f.x >0 by A7,A10;
      hence ln*f is_differentiable_in x by TAYLOR_1:20,A11;
    end;
    then A12:ln*f is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds
    (((1/2)(#)(ln*f))`|Z).x =exp_R(x)/(exp_R(x)+exp_R(-x))
    proof
      let x;
      assume A13:x in Z;
A14:  exp_R(x)>0 by SIN_COS:60;
A15:   ((( #Z 2)*exp_R)+f1).x
      =(( #Z 2)*exp_R).x+f1.x by SEQ_1:def 3,A4,A13
      .=( #Z 2).(exp_R.x)+f1.x by A13,A5,FUNCT_1:22
      .=(exp_R.x) #Z 2+f1.x by TAYLOR_1:def 1
      .=(exp_R.x) #Z 2+1 by A2,A13
      .=(exp_R(x)) #Z (1+1)+1 by SIN_COS:def 27
      .=(exp_R(x)) #Z 1 * (exp_R(x)) #Z 1 +1 by PREPOWER:54,A14
      .=exp_R(x) * (exp_R(x)) #Z 1 +1 by PREPOWER:45
      .=exp_R(x) * exp_R(x) +1 by PREPOWER:45;
A16:  f is_differentiable_in x by FDIFF_1:16,A6,A2,A13;
A17:  f.x >0 by A7,A13;
      (((1/2)(#)(ln*f))`|Z).x
      =(1/2)*diff((ln*f),x) by A12,A1,FDIFF_1:28,A13
      .=(1/2)*(diff(f,x)/f.x) by TAYLOR_1:20,A16,A17
      .=(1/2)*((((( #Z 2)*exp_R)+f1)`|Z).x/((( #Z 2)*exp_R)+f1).x)
      by A13,FDIFF_1:def 8,A6,A2
      .=(1/2)*((2*exp_R(2*x))/(exp_R(x) * exp_R(x) +1)) by A4,A2,Th23,A13,A15
      .=(1/2)*(2*exp_R(2*x))/(exp_R(x) * exp_R(x) +1) by XCMPLX_1:75
      .=(exp_R(x+x)/exp_R(x))/((exp_R(x) * exp_R(x)+1)/exp_R(x))
       by A14,XCMPLX_1:55
      .=((exp_R(x)*exp_R(x))/exp_R(x))/((exp_R(x) * exp_R(x)+1)/exp_R(x))
        by SIN_COS:55
      .=(exp_R(x)*exp_R(x)/exp_R(x))/(exp_R(x) * exp_R(x)/exp_R(x)+1/exp_R(x))
         by XCMPLX_1:63
      .=exp_R(x)/(exp_R(x) * exp_R(x)/exp_R(x)+1/exp_R(x)) by A14,XCMPLX_1:90
      .=exp_R(x)/(exp_R(x)+1/exp_R(x)) by A14,XCMPLX_1:90
      .=exp_R(x)/(exp_R(x)+exp_R(-x)) by TAYLOR_1:4;
      hence thesis;
    end;
    hence thesis by A12,A1,FDIFF_1:28;
  end;

:: 25 f.x=exp_R(2*x)-1
theorem Th25:
  Z c= dom ((( #Z 2)*exp_R)-f) & (for x st x in Z holds f.x=1)
  implies (( #Z 2)*exp_R)-f is_differentiable_on Z & for x st x in Z holds
  (((( #Z 2)*exp_R)-f)`|Z).x =2*exp_R(2*x)
  proof
    assume that A1:Z c= dom ((( #Z 2)*exp_R)-f) and
A2: (for x st x in Z holds f.x=1);
    Z c= dom (( #Z 2)*exp_R) /\ dom f by SEQ_1:def 4,A1;
    then A3:Z c= dom (( #Z 2)*exp_R) & Z c= dom f by XBOOLE_1:18;
A4: for x st x in Z holds f.x=0*x+1 by A2;
    then A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A3;
    now let x;
      assume x in Z;
      exp_R is_differentiable_in x by SIN_COS:70;
      hence ( #Z 2)*exp_R is_differentiable_in x by TAYLOR_1:3;
    end;
    then A6:( #Z 2)*exp_R is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds (((( #Z 2)*exp_R)-f)`|Z).x =2*exp_R(2*x)
    proof
      let x;
      assume A7:x in Z;
A8:   exp_R is_differentiable_in x by SIN_COS:70;
      (((( #Z 2)*exp_R)-f)`|Z).x
      =diff(( #Z 2)*exp_R,x)-diff(f,x) by A5,A6,FDIFF_1:27,A1,A7
      .=(2*((exp_R.x) #Z (2-1)) * diff(exp_R,x))-diff(f,x) by A8,TAYLOR_1:3
      .=2*((exp_R.x) #Z 1) * exp_R.x-diff(f,x) by SIN_COS:70
      .=2* exp_R.x * exp_R.x-diff(f,x) by PREPOWER:45
      .=2*(exp_R.x * exp_R.x)-diff(f,x)
      .=2*(exp_R(x)* exp_R.x)-diff(f,x) by SIN_COS:def 27
      .=2*(exp_R(x)* exp_R(x))-diff(f,x) by SIN_COS:def 27
      .=2*(exp_R(x+x))-diff(f,x) by SIN_COS:55
      .=2* exp_R(2*x)-(f`|Z).x by FDIFF_1:def 8,A7,A5
      .= 2* exp_R(2*x)-0 by A4,FDIFF_1:31,A3,A7
      .=2* exp_R(2*x);
      hence thesis;
    end;
    hence thesis by A6,A5,FDIFF_1:27,A1;
  end;

:: 26 f.x=(1/2)*ln(exp_R(2*x)-1)
theorem
  Z c= dom ((1/2)(#)(ln*f)) & f=(( #Z 2)*exp_R)-f1 &
  (for x st x in Z holds f1.x=1 & f.x>0) implies (1/2)(#)(ln*f)
  is_differentiable_on Z & for x st x in Z holds
  (((1/2)(#)(ln*f))`|Z).x =exp_R(x)/(exp_R(x)-exp_R(-x))
  proof
    assume that A1:Z c= dom ((1/2)(#)(ln*f)) and
A2: f=(( #Z 2)*exp_R)-f1 & (for x st x in Z holds f1.x=1 & f.x>0);
A3: f=(( #Z 2)*exp_R)-f1 & (for x st x in Z holds f1.x=1) by A2;
A4: Z c= dom (ln*f) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom f by FUNCT_1:21,A4;
    then A5:Z c= dom ((( #Z 2)*exp_R)-f1) by TARSKI:def 3,A2;
    then Z c= dom (( #Z 2)*exp_R) /\ dom f1 by SEQ_1:def 4;
    then A6:Z c= dom (( #Z 2)*exp_R) by XBOOLE_1:18;
A7: (( #Z 2)*exp_R)-f1 is_differentiable_on Z & for x st x in Z holds
    (((( #Z 2)*exp_R)-f1)`|Z).x =2*exp_R(2*x) by A5,A3,Th25;
    for x st x in Z holds ln*f is_differentiable_in x
    proof
      let x;
      assume A8:x in Z;
      then A9:f is_differentiable_in x by FDIFF_1:16,A7,A2;
      f.x >0 by A2,A8;
      hence ln*f is_differentiable_in x by TAYLOR_1:20,A9;
    end;
    then A10:ln*f is_differentiable_on Z by A4,FDIFF_1:16;
    for x st x in Z holds
    (((1/2)(#)(ln*f))`|Z).x =exp_R(x)/(exp_R(x)-exp_R(-x))
    proof
      let x;
      assume A11:x in Z;
A12:  exp_R(x)>0 by SIN_COS:60;
A13:   ((( #Z 2)*exp_R)-f1).x
      =(( #Z 2)*exp_R).x-f1.x by SEQ_1:def 4,A5,A11
      .=( #Z 2).(exp_R.x)-f1.x by A11,A6,FUNCT_1:22
      .=(exp_R.x) #Z 2-f1.x by TAYLOR_1:def 1
      .=(exp_R.x) #Z 2-1 by A2,A11
      .=(exp_R(x)) #Z (1+1)-1 by SIN_COS:def 27
      .=(exp_R(x)) #Z 1 * (exp_R(x)) #Z 1 -1 by PREPOWER:54,A12
      .=exp_R(x) * (exp_R(x)) #Z 1 -1 by PREPOWER:45
      .=exp_R(x) * exp_R(x) -1 by PREPOWER:45;
A14:  f is_differentiable_in x by FDIFF_1:16,A7,A2,A11;
A15:  f.x >0 by A2,A11;
      (((1/2)(#)(ln*f))`|Z).x
      =(1/2)*diff((ln*f),x) by A10,A1,FDIFF_1:28,A11
      .=(1/2)*(diff(f,x)/f.x) by TAYLOR_1:20,A14,A15
      .=(1/2)*((((( #Z 2)*exp_R)-f1)`|Z).x/((( #Z 2)*exp_R)-f1).x)
      by A11,FDIFF_1:def 8,A7,A2
      .=(1/2)*((2*exp_R(2*x))/(exp_R(x) * exp_R(x) -1)) by A5,A3,Th25,A11,A13
      .=(1/2)*(2*exp_R(2*x))/(exp_R(x) * exp_R(x) -1) by XCMPLX_1:75
      .=(exp_R(x+x)/exp_R(x))/((exp_R(x) * exp_R(x)-1)/exp_R(x))
        by A12,XCMPLX_1:55
      .=((exp_R(x)*exp_R(x))/exp_R(x))/((exp_R(x) * exp_R(x)-1)/exp_R(x))
        by SIN_COS:55
      .=((exp_R(x)*exp_R(x))/exp_R(x))/((exp_R(x)*exp_R(x))/exp_R(x)-
         1/exp_R(x)) by XCMPLX_1:121
      .=exp_R(x)/(exp_R(x) * exp_R(x)/exp_R(x)-1/exp_R(x)) by A12,XCMPLX_1:90
      .=exp_R(x)/(exp_R(x)-1/exp_R(x)) by A12,XCMPLX_1:90
      .=exp_R(x)/(exp_R(x)-exp_R(-x)) by TAYLOR_1:4;
      hence thesis;
    end;
    hence thesis by A10,A1,FDIFF_1:28;
  end;

:: 27 f.x=(exp_R.x-1)^2
theorem Th27:
  Z c= dom (( #Z 2)*(exp_R-f)) & (for x st x in Z holds f.x=1)
  implies ( #Z 2)*(exp_R-f) is_differentiable_on Z & for x st x in Z holds
  ((( #Z 2)*(exp_R-f))`|Z).x =2*exp_R.x*(exp_R.x-1)
  proof
    assume that A1:Z c= dom (( #Z 2)*(exp_R-f)) and
A2: (for x st x in Z holds f.x=1);
    for y st y in Z holds y in dom (exp_R-f) by FUNCT_1:21,A1;
    then A3:Z c= dom (exp_R-f) by TARSKI:def 3;
    then Z c= dom exp_R /\ dom f by SEQ_1:def 4;
    then A4:Z c= dom f by XBOOLE_1:18;
A5: for x st x in Z holds f.x=0*x+1 by A2;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A7: for x st x in Z holds ( #Z 2)*(exp_R-f) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A8:f is_differentiable_in x by A6,FDIFF_1:16;
      exp_R is_differentiable_in x by SIN_COS:70;
      then exp_R-f is_differentiable_in x by FDIFF_1:22,A8;
      hence thesis by TAYLOR_1:3;
    end;
    then A9:( #Z 2)*(exp_R-f) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    ((( #Z 2)*(exp_R-f))`|Z).x =2*exp_R.x*(exp_R.x-1)
    proof
      let x;
      assume A10:x in Z;
      then A11:f is_differentiable_in x by A6,FDIFF_1:16;
A12:   exp_R is_differentiable_in x by SIN_COS:70;
      then A13:exp_R-f is_differentiable_in x by FDIFF_1:22,A11;
A14:   diff((exp_R-f),x)=diff(exp_R,x)-diff(f,x) by FDIFF_1:22,A11,A12
      .=diff(exp_R,x)-(f`|Z).x by FDIFF_1:def 8,A10,A6
      .=exp_R.x-(f`|Z).x by SIN_COS:70
      .=exp_R.x-0 by FDIFF_1:31,A4,A5,A10
      .=exp_R.x;
A15:   (exp_R-f).x=exp_R.x-f.x by SEQ_1:def 4,A3,A10
      .= exp_R.x-1 by A10,A2;
      ((( #Z 2)*(exp_R-f))`|Z).x
      =diff((( #Z 2)*(exp_R-f)),x) by A9,FDIFF_1:def 8,A10
      .=2* ((exp_R-f).x) #Z (2-1) * diff((exp_R-f),x) by A13,TAYLOR_1:3
      .=2*(exp_R.x-1)*exp_R.x by A15,A14,PREPOWER:45
      .=2*exp_R.x*(exp_R.x-1);
      hence thesis;
    end;
    hence thesis by A7,A1,FDIFF_1:16;
  end;

:: 28 f.x=ln((exp_R.x-1)^2/exp_R.x)
theorem
  Z c= dom f & f=ln*((( #Z 2)*(exp_R-f1))/exp_R) &
  (for x st x in Z holds f1.x=1 & (exp_R-f1).x>0) implies
    f is_differentiable_on Z &
      for x st x in Z holds (f`|Z).x =(exp_R.x+1)/(exp_R.x-1)
  proof
    assume that A1: Z c= dom f & f=ln*((( #Z 2)*(exp_R-f1))/exp_R)
    and A2:(for x st x in Z holds f1.x=1 & (exp_R-f1).x>0);
A3: for x st x in Z holds f1.x=1 by A2;
    for y st y in Z holds y in dom ((( #Z 2)*(exp_R-f1))/exp_R)
       by FUNCT_1:21,A1;
    then A4:Z c= dom ((( #Z 2)*(exp_R-f1))/exp_R) by TARSKI:def 3;
    then Z c=dom (( #Z 2)*(exp_R-f1))/\(dom (exp_R)\(exp_R)"{0})
       by RFUNCT_1:def 4;
    then A5:Z c= dom (( #Z 2)*(exp_R-f1)) by XBOOLE_1:18;
    then A6:( #Z 2)*(exp_R-f1) is_differentiable_on Z & for x st x in Z holds
    ((( #Z 2)*(exp_R-f1))`|Z).x =2*exp_R.x*(exp_R.x-1) by A3,Th27;
A7: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for x st x in Z holds exp_R.x<>0 by SIN_COS:59;
    then A8:(( #Z 2)*(exp_R-f1))/exp_R is_differentiable_on Z
      by A7,A6,FDIFF_2:21;
    for y st y in Z holds y in dom (exp_R-f1) by FUNCT_1:21,A5;
    then A9:Z c= dom (exp_R-f1) by TARSKI:def 3;
A10: for x st x in Z holds ((( #Z 2)*(exp_R-f1))/exp_R).x >0
    proof
      let x;
      assume A11:x in Z;
      then A12:((( #Z 2)*(exp_R-f1))/exp_R).x
      =(( #Z 2)*(exp_R-f1)).x*(exp_R.x)" by RFUNCT_1:def 4,A4
      .=(( #Z 2)*(exp_R-f1)).x*(1/exp_R.x) by XCMPLX_1:217
      .=(( #Z 2)*(exp_R-f1)).x/exp_R.x by XCMPLX_1:100
      .=( #Z 2).((exp_R-f1).x)/exp_R.x by A5,A11,FUNCT_1:22
      .=((exp_R-f1).x) #Z 2/exp_R.x by TAYLOR_1:def 1;
      (exp_R-f1).x>0 by A2,A11;
      then A13:((exp_R-f1).x) #Z 2>0 by PREPOWER:49;
      exp_R.x>0 by SIN_COS:59;
      hence ((( #Z 2)*(exp_R-f1))/exp_R).x >0 by A12,XREAL_1:141,A13;
    end;
A14: for x st x in Z holds ln*((( #Z 2)*(exp_R-f1))/exp_R)
     is_differentiable_in x
     proof
       let x;
       assume A15:x in Z;
       then A16:(( #Z 2)*(exp_R-f1))/exp_R is_differentiable_in x
         by FDIFF_1:16,A8;
       ((( #Z 2)*(exp_R-f1))/exp_R).x >0 by A10,A15;
       hence ln*((( #Z 2)*(exp_R-f1))/exp_R)
         is_differentiable_in x by TAYLOR_1:20,A16;
     end;
     then A17:f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x =(exp_R.x+1)/(exp_R.x-1)
     proof
       let x;
       assume A18:x in Z;
       then A19:(( #Z 2)*(exp_R-f1))/exp_R is_differentiable_in x
          by FDIFF_1:16,A8;
A20:    ((( #Z 2)*(exp_R-f1))/exp_R).x >0 by A10,A18;
A21:    exp_R.x>0 by SIN_COS:59;
A22:   (exp_R-f1).x=exp_R.x - f1.x by SEQ_1:def 4,A18,A9
       .=exp_R.x -1 by A2,A18;
       then A23:exp_R.x-1>0 by A18,A2;
A24:    ( #Z 2)*(exp_R-f1) is_differentiable_in x by A6,A18,FDIFF_1:16;
       exp_R is_differentiable_in x by SIN_COS:70;
       then A25:diff(((( #Z 2)*(exp_R-f1))/exp_R),x)
       =(diff((( #Z 2)*(exp_R-f1)),x)*exp_R.x-diff(exp_R,x)*
       (( #Z 2)*(exp_R-f1)).x)/(exp_R.x)^2 by FDIFF_2:14,A24,A21
       .=(((( #Z 2)*(exp_R-f1))`|Z).x*exp_R.x-diff(exp_R,x)*
       (( #Z 2)*(exp_R-f1)).x)/(exp_R.x)^2 by FDIFF_1:def 8,A18,A6
       .=(2*exp_R.x*(exp_R.x-1)*exp_R.x-
          diff(exp_R,x)*(( #Z 2)*(exp_R-f1)).x)/(exp_R.x)^2
       by A5,A3,Th27,A18
       .=(2*exp_R.x*(exp_R.x-1)*exp_R.x-exp_R.x*
        (( #Z 2)*(exp_R-f1)).x)/(exp_R.x)^2 by SIN_COS:70
       .=((2*exp_R.x*(exp_R.x-1)-(( #Z 2)*(exp_R-f1)).x)*exp_R.x)/
          (exp_R.x*exp_R.x)
       .=(2*exp_R.x*(exp_R.x-1)-(( #Z 2)*(exp_R-f1)).x)/exp_R.x
          by A21,XCMPLX_1:92
       .=(2*exp_R.x*(exp_R.x-1)-( #Z 2).((exp_R-f1).x))/exp_R.x
          by A5,A18,FUNCT_1:22
       .=(2*exp_R.x*(exp_R.x-1)-((exp_R-f1).x) #Z 2)/exp_R.x by TAYLOR_1:def 1
       .=(2*exp_R.x*(exp_R.x-1)-(exp_R.x-f1.x) #Z 2)/exp_R.x
          by SEQ_1:def 4,A18,A9
       .=(2*exp_R.x*(exp_R.x-1)-(exp_R.x-1) #Z (1+1))/exp_R.x by A18,A2
       .=(2*exp_R.x*(exp_R.x-1)-(exp_R.x-1) #Z 1*(exp_R.x-1) #Z 1)/exp_R.x
          by PREPOWER:54,A23
       .=(2*exp_R.x*(exp_R.x-1)-(exp_R.x-1) *(exp_R.x-1) #Z 1)/exp_R.x
          by PREPOWER:45
       .=(2*exp_R.x*(exp_R.x-1)-(exp_R.x-1) *(exp_R.x-1))/exp_R.x
          by PREPOWER:45
       .=((exp_R.x+1)*(exp_R.x-1))/exp_R.x;
A26:    ((( #Z 2)*(exp_R-f1))/exp_R).x
       =(( #Z 2)*(exp_R-f1)).x*(exp_R.x)" by RFUNCT_1:def 4,A4,A18
       .=(( #Z 2)*(exp_R-f1)).x*(1/exp_R.x) by XCMPLX_1:217
       .=(( #Z 2)*(exp_R-f1)).x/exp_R.x by XCMPLX_1:100
       .=( #Z 2).((exp_R-f1).x)/exp_R.x by A5,A18,FUNCT_1:22
       .=((exp_R.x-1) #Z (1+1))/exp_R.x by A22,TAYLOR_1:def 1
       .=((exp_R.x-1) #Z 1*(exp_R.x-1) #Z 1)/exp_R.x by PREPOWER:54,A23
       .=((exp_R.x-1) *(exp_R.x-1) #Z 1)/exp_R.x by PREPOWER:45
       .=((exp_R.x-1) *(exp_R.x-1))/exp_R.x by PREPOWER:45;
       (f`|Z).x =diff(ln*((( #Z 2)*(exp_R-f1))/exp_R),x)
       by A17,A18,FDIFF_1:def 8,A1
       .=(((exp_R.x+1)*(exp_R.x-1))/exp_R.x)/(((exp_R.x-1)*
        (exp_R.x-1))/exp_R.x) by A25,A26,TAYLOR_1:20,A19,A20
       .=((exp_R.x+1)*(exp_R.x-1))/((exp_R.x-1) *(exp_R.x-1)) by XCMPLX_1:55,
A21
       .=(exp_R.x+1)/(exp_R.x-1) by A23,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A14,A1,FDIFF_1:16;
   end;

:: 29 f.x=(exp_R.x+1)^2
theorem Th29:
  Z c= dom (( #Z 2)*(exp_R+f)) & (for x st x in Z holds f.x=1)
  implies ( #Z 2)*(exp_R+f) is_differentiable_on Z & for x st x in Z holds
  ((( #Z 2)*(exp_R+f))`|Z).x =2*exp_R.x*(exp_R.x+1)
  proof
    assume that A1:Z c= dom (( #Z 2)*(exp_R+f)) and
A2: (for x st x in Z holds f.x=1);
    for y st y in Z holds y in dom (exp_R+f) by FUNCT_1:21,A1;
    then A3:Z c= dom (exp_R+f) by TARSKI:def 3;
    then Z c= dom exp_R /\ dom f by SEQ_1:def 3;
    then A4:Z c= dom f by XBOOLE_1:18;
A5: for x st x in Z holds f.x=0*x+1 by A2;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A7: for x st x in Z holds ( #Z 2)*(exp_R+f) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A8:f is_differentiable_in x by A6,FDIFF_1:16;
      exp_R is_differentiable_in x by SIN_COS:70;
      then exp_R+f is_differentiable_in x by FDIFF_1:21,A8;
      hence thesis by TAYLOR_1:3;
    end;
    then A9:( #Z 2)*(exp_R+f) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    ((( #Z 2)*(exp_R+f))`|Z).x =2*exp_R.x*(exp_R.x+1)
    proof
      let x;
      assume A10:x in Z;
      then A11:f is_differentiable_in x by A6,FDIFF_1:16;
A12:   exp_R is_differentiable_in x by SIN_COS:70;
      then A13:exp_R+f is_differentiable_in x by FDIFF_1:21,A11;
A14:   diff((exp_R+f),x)=diff(exp_R,x)+diff(f,x) by FDIFF_1:21,A11,A12
      .=diff(exp_R,x)+(f`|Z).x by FDIFF_1:def 8,A10,A6
      .=exp_R.x+(f`|Z).x by SIN_COS:70
      .=exp_R.x+0 by A5,FDIFF_1:31,A4,A10
      .=exp_R.x;
A15:   (exp_R+f).x=exp_R.x+f.x by SEQ_1:def 3,A3,A10
      .= exp_R.x+1 by A10,A2;
      ((( #Z 2)*(exp_R+f))`|Z).x
      =diff((( #Z 2)*(exp_R+f)),x) by A9,FDIFF_1:def 8,A10
      .=2* ((exp_R+f).x) #Z (2-1) * diff((exp_R+f),x) by A13,TAYLOR_1:3
      .=2*(exp_R.x+1)*exp_R.x by A15,A14,PREPOWER:45
      .=2*exp_R.x*(exp_R.x+1);
      hence thesis;
    end;
    hence thesis by A7,A1,FDIFF_1:16;
  end;

:: 30 f.x=ln((exp_R.x+1)^2/exp_R.x)
theorem
  Z c= dom f & f=ln*((( #Z 2)*(exp_R+f1))/exp_R) &
  (for x st x in Z holds f1.x=1) implies f is_differentiable_on Z
  & for x st x in Z holds (f`|Z).x =(exp_R.x-1)/(exp_R.x+1)
  proof
    assume that A1: Z c= dom f & f=ln*((( #Z 2)*(exp_R+f1))/exp_R)
    and A2:for x st x in Z holds f1.x=1;
    for y st y in Z holds y in dom ((( #Z 2)*(exp_R+f1))/exp_R)
        by FUNCT_1:21,A1;
    then A3:Z c= dom ((( #Z 2)*(exp_R+f1))/exp_R) by TARSKI:def 3;
    then Z c=dom (( #Z 2)*(exp_R+f1))/\(dom (exp_R)\(exp_R)"{0})
        by RFUNCT_1:def 4;
    then A4:Z c= dom (( #Z 2)*(exp_R+f1)) by XBOOLE_1:18;
    then A5:( #Z 2)*(exp_R+f1) is_differentiable_on Z & for x st x in Z holds
    ((( #Z 2)*(exp_R+f1))`|Z).x =2*exp_R.x*(exp_R.x+1) by A2,Th29;
A6: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for x st x in Z holds exp_R.x<>0 by SIN_COS:59;
    then A7:(( #Z 2)*(exp_R+f1))/exp_R is_differentiable_on Z
       by A6,A5,FDIFF_2:21;
    for y st y in Z holds y in dom (exp_R+f1) by FUNCT_1:21,A4;
    then A8:Z c= dom (exp_R+f1) by TARSKI:def 3;
A9: for x st x in Z holds ((( #Z 2)*(exp_R+f1))/exp_R).x >0
    proof
      let x;
      assume A10:x in Z;
      then A11:((( #Z 2)*(exp_R+f1))/exp_R).x
      =(( #Z 2)*(exp_R+f1)).x*(exp_R.x)" by RFUNCT_1:def 4,A3
      .=(( #Z 2)*(exp_R+f1)).x*(1/exp_R.x) by XCMPLX_1:217
      .=(( #Z 2)*(exp_R+f1)).x/exp_R.x by XCMPLX_1:100
      .=( #Z 2).((exp_R+f1).x)/exp_R.x by A4,A10,FUNCT_1:22
      .=((exp_R+f1).x) #Z 2/exp_R.x by TAYLOR_1:def 1;
A12:   (exp_R+f1).x=exp_R.x + f1.x by SEQ_1:def 3,A8,A10
      .=exp_R.x +1 by A2,A10;
      exp_R.x>0 by SIN_COS:59;
      then (exp_R+f1).x>0 by XREAL_1:36,A12;
      then A13:((exp_R+f1).x) #Z 2>0 by PREPOWER:49;
      exp_R.x>0 by SIN_COS:59;
      hence ((( #Z 2)*(exp_R+f1))/exp_R).x >0 by A11,XREAL_1:141,A13;
    end;
A14: for x st x in Z holds ln*((( #Z 2)*(exp_R+f1))/exp_R)
     is_differentiable_in x
     proof
       let x;
       assume A15:x in Z;
       then A16:(( #Z 2)*(exp_R+f1))/exp_R is_differentiable_in x
           by FDIFF_1:16,A7;
       ((( #Z 2)*(exp_R+f1))/exp_R).x >0 by A9,A15;
       hence ln*((( #Z 2)*(exp_R+f1))/exp_R)
         is_differentiable_in x
       by TAYLOR_1:20,A16;
     end;
     then A17:f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x =(exp_R.x-1)/(exp_R.x+1)
     proof
       let x;
       assume A18:x in Z;
       then A19:(( #Z 2)*(exp_R+f1))/exp_R is_differentiable_in x
          by FDIFF_1:16,A7;
A20:    ((( #Z 2)*(exp_R+f1))/exp_R).x >0 by A9,A18;
A21:    exp_R.x>0 by SIN_COS:59;
       then A22:exp_R.x +1>0 by XREAL_1:36;
A23:    (exp_R+f1).x=exp_R.x + f1.x by SEQ_1:def 3,A8,A18
       .=exp_R.x +1 by A2,A18;
A24:   (( #Z 2)*(exp_R+f1)).x
       =( #Z 2).((exp_R+f1).x) by A4,A18,FUNCT_1:22
       .=(exp_R.x+1) #Z (1+1) by A23,TAYLOR_1:def 1
       .=(exp_R.x+1) #Z 1*(exp_R.x+1) #Z 1 by PREPOWER:54,A22
       .=(exp_R.x+1) *(exp_R.x+1) #Z 1 by PREPOWER:45
       .=(exp_R.x+1) *(exp_R.x+1) by PREPOWER:45;
A25:    ( #Z 2)*(exp_R+f1) is_differentiable_in x by A5,A18,FDIFF_1:16;
       exp_R is_differentiable_in x by SIN_COS:70;
       then A26:diff(((( #Z 2)*(exp_R+f1))/exp_R),x)
       =(diff((( #Z 2)*(exp_R+f1)),x)*exp_R.x-diff(exp_R,x)*
       (( #Z 2)*(exp_R+f1)).x)/(exp_R.x)^2 by FDIFF_2:14,A25,A21
       .=(((( #Z 2)*(exp_R+f1))`|Z).x*exp_R.x-diff(exp_R,x)*(( #Z 2)*
       (exp_R+f1)).x)/(exp_R.x)^2 by FDIFF_1:def 8,A18,A5
       .=(2*exp_R.x*(exp_R.x+1)*exp_R.x-diff(exp_R,x)*(( #Z 2)*
        (exp_R+f1)).x)/(exp_R.x)^2 by A4,A2,Th29,A18
       .=(2*exp_R.x*(exp_R.x+1)*exp_R.x-exp_R.x*(( #Z 2)*
         (exp_R+f1)).x)/(exp_R.x)^2 by SIN_COS:70
       .=((2*exp_R.x*(exp_R.x+1)-(( #Z 2)*(exp_R+f1)).x)*exp_R.x)/
         (exp_R.x*exp_R.x)
       .=(exp_R.x-1)*(exp_R.x+1)/exp_R.x by A24,A21,XCMPLX_1:92;
A27:    ((( #Z 2)*(exp_R+f1))/exp_R).x
       =(( #Z 2)*(exp_R+f1)).x*(exp_R.x)" by RFUNCT_1:def 4,A3,A18
       .=(( #Z 2)*(exp_R+f1)).x*(1/exp_R.x) by XCMPLX_1:217
       .=((exp_R.x+1) *(exp_R.x+1))/exp_R.x by A24,XCMPLX_1:100;
       (f`|Z).x =diff(ln*((( #Z 2)*(exp_R+f1))/exp_R),x)
       by A17,A18,FDIFF_1:def 8,A1
       .=(((exp_R.x+1)*(exp_R.x-1))/exp_R.x)/(((exp_R.x+1) *
        (exp_R.x+1))/exp_R.x) by A26,A27,TAYLOR_1:20,A19,A20
       .=((exp_R.x+1)*(exp_R.x-1))/((exp_R.x+1) *(exp_R.x+1)) by XCMPLX_1:55,
A21
       .=(exp_R.x-1)/(exp_R.x+1) by A22,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A14,A1,FDIFF_1:16;
   end;

:: 31 f.x=(1-exp_R.x)^2
theorem Th31:
  Z c= dom (( #Z 2)*(f-exp_R)) & (for x st x in Z holds f.x=1)
  implies ( #Z 2)*(f-exp_R) is_differentiable_on Z & for x st x in Z holds
  ((( #Z 2)*(f-exp_R))`|Z).x =-2*exp_R.x*(1-exp_R.x)
  proof
    assume that A1:Z c= dom (( #Z 2)*(f-exp_R)) and
A2: (for x st x in Z holds f.x=1);
    for y st y in Z holds y in dom (f-exp_R) by FUNCT_1:21,A1;
    then A3:Z c= dom (f-exp_R) by TARSKI:def 3;
    then Z c= dom exp_R /\ dom f by SEQ_1:def 4;
    then A4:Z c= dom f by XBOOLE_1:18;
A5: for x st x in Z holds f.x=0*x+1 by A2;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A7: for x st x in Z holds ( #Z 2)*(f-exp_R) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A8:f is_differentiable_in x by A6,FDIFF_1:16;
      exp_R is_differentiable_in x by SIN_COS:70;
      then f-exp_R is_differentiable_in x by FDIFF_1:22,A8;
      hence thesis by TAYLOR_1:3;
    end;
    then A9:( #Z 2)*(f-exp_R) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    ((( #Z 2)*(f-exp_R))`|Z).x =-2*exp_R.x*(1-exp_R.x)
    proof
      let x;
      assume A10:x in Z;
      then A11:f is_differentiable_in x by A6,FDIFF_1:16;
A12:   exp_R is_differentiable_in x by SIN_COS:70;
      then A13:f-exp_R is_differentiable_in x by FDIFF_1:22,A11;
A14:   diff((f-exp_R),x)=diff(f,x)-diff(exp_R,x) by FDIFF_1:22,A11,A12
      .=(f`|Z).x-diff(exp_R,x) by FDIFF_1:def 8,A10,A6
      .=(f`|Z).x-exp_R.x by SIN_COS:70
      .=0-exp_R.x by A5,FDIFF_1:31,A4,A10
      .=-exp_R.x;
A15:   (f-exp_R).x=f.x-exp_R.x by SEQ_1:def 4,A3,A10
      .=1- exp_R.x by A10,A2;
      ((( #Z 2)*(f-exp_R))`|Z).x
      =diff((( #Z 2)*(f-exp_R)),x) by A9,FDIFF_1:def 8,A10
      .=2* ((f-exp_R).x) #Z (2-1) * diff((f-exp_R),x) by A13,TAYLOR_1:3
      .=2*(1-exp_R.x)*(-exp_R.x) by A15,A14,PREPOWER:45
      .=-2*exp_R.x*(1-exp_R.x);
      hence thesis;
    end;
    hence thesis by A7,A1,FDIFF_1:16;
  end;

:: 32 f.x=ln(exp_R.x/(1-exp_R.x)^2)
theorem
  Z c= dom f & f=ln*(exp_R/(( #Z 2)*(f1-exp_R))) &
  (for x st x in Z holds f1.x=1 & (f1-exp_R).x>0) implies
  f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x =(1+exp_R.x)/(1-exp_R.x)
  proof
    assume that A1: Z c= dom f & f=ln*(exp_R/(( #Z 2)*(f1-exp_R)))
    and A2:(for x st x in Z holds f1.x=1 & (f1-exp_R).x>0);
A3: for x st x in Z holds f1.x=1 by A2;
    for y st y in Z holds y in dom (exp_R/(( #Z 2)*(f1-exp_R)))
     by FUNCT_1:21,A1;
    then A4:Z c= dom (exp_R/(( #Z 2)*(f1-exp_R))) by TARSKI:def 3;
    then Z c=dom exp_R/\(dom ((( #Z 2)*(f1-exp_R)))\((( #Z 2)*(f1-exp_R)))"{0})
    by RFUNCT_1:def 4;
    then A5:Z c=dom ((( #Z 2)*(f1-exp_R)))\((( #Z 2)*(f1-exp_R)))"{0}
      by XBOOLE_1:18;
    A6:Z c= dom (( #Z 2)*(f1-exp_R)) by XBOOLE_1:1,A5;
    then A7:( #Z 2)*(f1-exp_R) is_differentiable_on Z & for x st x in Z holds
    ((( #Z 2)*(f1-exp_R))`|Z).x =-2*exp_R.x*(1-exp_R.x) by A3,Th31;
A8: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A9: for x st x in Z holds (( #Z 2)*(f1-exp_R)).x>0
    proof
      let x;
      assume A10:x in Z;
A11:   ((( #Z 2)*(f1-exp_R))).x
      =( #Z 2).((f1-exp_R).x) by A6,A10,FUNCT_1:22
      .=((f1-exp_R).x) #Z 2 by TAYLOR_1:def 1;
      (f1-exp_R).x>0 by A2,A10;
      hence thesis by A11,PREPOWER:49;
    end;
    for x st x in Z holds (( #Z 2)*(f1-exp_R)).x<>0 by A9;
    then A12:exp_R/(( #Z 2)*(f1-exp_R)) is_differentiable_on Z
       by A8,A7,FDIFF_2:21;
    for y st y in Z holds y in dom (f1-exp_R) by FUNCT_1:21,A6;
    then A13:Z c= dom (f1-exp_R) by TARSKI:def 3;
A14: for x st x in Z holds (exp_R/(( #Z 2)*(f1-exp_R))).x >0
     proof
       let x;
       assume A15:x in Z;
       then A16:(exp_R/(( #Z 2)*(f1-exp_R))).x
       =exp_R.x*((( #Z 2)*(f1-exp_R)).x)" by RFUNCT_1:def 4,A4
       .=exp_R.x*(1/(( #Z 2)*(f1-exp_R)).x) by XCMPLX_1:217
       .=exp_R.x/(( #Z 2)*(f1-exp_R)).x by XCMPLX_1:100;
A17:    (( #Z 2)*(f1-exp_R)).x>0 by A9,A15;
       exp_R.x>0 by SIN_COS:59;
       hence (exp_R/(( #Z 2)*(f1-exp_R))).x >0 by A16,XREAL_1:141,A17;
     end;
A18: for x st x in Z holds ln*(exp_R/(( #Z 2)*(f1-exp_R)))
     is_differentiable_in x
     proof
       let x;
       assume A19:x in Z;
       then A20:exp_R/(( #Z 2)*(f1-exp_R)) is_differentiable_in x
          by FDIFF_1:16,A12;
       (exp_R/(( #Z 2)*(f1-exp_R))).x >0 by A14,A19;
       hence ln*(exp_R/(( #Z 2)*(f1-exp_R)))
         is_differentiable_in x by TAYLOR_1:20,A20;
     end;
     then A21:f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x =(1+exp_R.x)/(1-exp_R.x)
     proof
       let x;
       assume A22:x in Z;
       then A23:(( #Z 2)*(f1-exp_R)).x<>0 by A9;
A24:    ( #Z 2)*(f1-exp_R) is_differentiable_in x by A7,A22,FDIFF_1:16;
A25:    exp_R is_differentiable_in x by SIN_COS:70;
A26:    exp_R/(( #Z 2)*(f1-exp_R)) is_differentiable_in x by FDIFF_1:16,A12,A22
;
A27:   (exp_R/(( #Z 2)*(f1-exp_R))).x >0 by A14,A22;
A28:   (f1-exp_R).x>0 by A22,A2;
A29:   (f1-exp_R).x=f1.x-exp_R.x by SEQ_1:def 4,A22,A13
       .=1-exp_R.x by A2,A22;
       then 1-exp_R.x>0 by A22,A2;
       then A30:(1-exp_R.x) #Z 2>0 by PREPOWER:49;
       exp_R.x>0 by SIN_COS:59;
       then A31:exp_R.x/(1-exp_R.x) #Z 2<>0 by XREAL_1:141,A30;
A32:    ((( #Z 2)*(f1-exp_R))).x
       =( #Z 2).((f1-exp_R).x) by A6,A22,FUNCT_1:22
       .=((f1-exp_R).x) #Z 2 by TAYLOR_1:def 1
       .=(f1.x-exp_R.x) #Z 2 by SEQ_1:def 4,A22,A13
       .=(1-exp_R.x) #Z 2 by A22,A2;
A33:    diff((exp_R/(( #Z 2)*(f1-exp_R))),x)
       =(diff(exp_R,x)*(( #Z 2)*(f1-exp_R)).x-
          diff((( #Z 2)*(f1-exp_R)),x)*exp_R.x)
       /((( #Z 2)*(f1-exp_R)).x)^2 by FDIFF_2:14,A24,A25,A23
       .=(exp_R.x*(( #Z 2)*(f1-exp_R)).x-diff((( #Z 2)*(f1-exp_R)),x)*exp_R.x)
       /((( #Z 2)*(f1-exp_R)).x)^2 by SIN_COS:70
       .=(exp_R.x*(( #Z 2)*(f1-exp_R)).x-((( #Z 2)*(f1-exp_R))`|Z).x*exp_R.x)
       /((( #Z 2)*(f1-exp_R)).x)^2 by FDIFF_1:def 8,A22,A7
       .=(exp_R.x*((1-exp_R.x) #Z 2)-(-2*exp_R.x*(1-exp_R.x))*exp_R.x)/
       ((1-exp_R.x) #Z 2)^2 by A32,A6,A3,Th31,A22
       .=(exp_R.x*((1-exp_R.x) #Z 2+2*(1-exp_R.x)*exp_R.x))
       /((1-exp_R.x) #Z 2*(1-exp_R.x) #Z 2)
       .=((exp_R.x/(1-exp_R.x) #Z 2)*((1-exp_R.x) #Z 2+2*(1-exp_R.x)*exp_R.x))
       /(1-exp_R.x) #Z 2 by XCMPLX_1:84;
A34:    (exp_R/(( #Z 2)*(f1-exp_R))).x
       =exp_R.x*((( #Z 2)*(f1-exp_R)).x)" by RFUNCT_1:def 4,A4,A22
       .=exp_R.x*(1/(( #Z 2)*(f1-exp_R)).x) by XCMPLX_1:217
       .=exp_R.x/(1-exp_R.x) #Z 2 by A32,XCMPLX_1:100;
A35:   (1-exp_R.x) #Z 2=(1-exp_R.x) #Z (1+1)
       .=(1-exp_R.x) #Z 1 * (1-exp_R.x) #Z 1 by PREPOWER:54,A29,A28
       .=(1-exp_R.x) * (1-exp_R.x) #Z 1 by PREPOWER:45
       .=(1-exp_R.x) * (1-exp_R.x) by PREPOWER:45;
       (f`|Z).x =diff(ln*(exp_R/(( #Z 2)*(f1-exp_R))),x)
       by A21,A22,FDIFF_1:def 8,A1
       .=((exp_R.x/(1-exp_R.x) #Z 2)*((1-exp_R.x) #Z 2+2*(1-exp_R.x)*exp_R.x))
       /(1-exp_R.x) #Z 2/(exp_R.x/(1-exp_R.x) #Z 2) by A33,A34,TAYLOR_1:20,A26,
A27
       .=((exp_R.x/(1-exp_R.x) #Z 2)*((1-exp_R.x) #Z 2+2*(1-exp_R.x)*exp_R.x))
       /((exp_R.x/(1-exp_R.x) #Z 2)*(1-exp_R.x) #Z 2) by XCMPLX_1:79
       .=((1-exp_R.x) * (1+exp_R.x))/((1-exp_R.x) * (1-exp_R.x))
         by A35,A31,XCMPLX_1:92
       .=(1+exp_R.x)/ (1-exp_R.x) by A29,A28,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A18,A1,FDIFF_1:16;
   end;

:: 33 f.x=ln(exp_R.x/(1+exp_R.x)^2)
theorem
  Z c= dom f & f=ln*(exp_R/(( #Z 2)*(f1+exp_R))) &
  (for x st x in Z holds f1.x=1) implies f is_differentiable_on Z
  & for x st x in Z holds (f`|Z).x =(1-exp_R.x)/(1+exp_R.x)
  proof
    assume that A1: Z c= dom f & f=ln*(exp_R/(( #Z 2)*(f1+exp_R)))
    and A2:for x st x in Z holds f1.x=1;
    for y st y in Z holds y in dom (exp_R/(( #Z 2)*(f1+exp_R)))
      by FUNCT_1:21,A1;
    then A3:Z c= dom (exp_R/(( #Z 2)*(f1+exp_R))) by TARSKI:def 3;
    then Z c=dom exp_R/\(dom ((( #Z 2)*(f1+exp_R)))\((( #Z 2)*(f1+exp_R)))"{0})
    by RFUNCT_1:def 4;then
A4: Z c=dom ((( #Z 2)*(f1+exp_R)))\((( #Z 2)*(f1+exp_R)))"{0} by XBOOLE_1:18;
    A5:Z c= dom (( #Z 2)*(f1+exp_R)) by XBOOLE_1:1,A4;
    then A6:( #Z 2)*(f1+exp_R) is_differentiable_on Z & for x st x in Z holds
    ((( #Z 2)*(f1+exp_R))`|Z).x =2*exp_R.x*(1+exp_R.x) by A2,Th29;
A7: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for y st y in Z holds y in dom (f1+exp_R) by FUNCT_1:21,A5;
    then A8:Z c= dom (f1+exp_R) by TARSKI:def 3;
A9: for x st x in Z holds (( #Z 2)*(f1+exp_R)).x>0
    proof
      let x;
      assume A10:x in Z;
      then A11:((( #Z 2)*(f1+exp_R))).x
      =( #Z 2).((f1+exp_R).x) by A5,FUNCT_1:22
      .=((f1+exp_R).x) #Z 2 by TAYLOR_1:def 1;
A12:   (f1+exp_R).x=f1.x+exp_R.x by SEQ_1:def 3,A8,A10
      .=1+exp_R.x by A2,A10;
      exp_R.x>0 by SIN_COS:59;
      then (f1+exp_R).x>0 by XREAL_1:36,A12;
      hence thesis by A11,PREPOWER:49;
    end;
    for x st x in Z holds (( #Z 2)*(f1+exp_R)).x<>0 by A9;
    then A13:exp_R/(( #Z 2)*(f1+exp_R)) is_differentiable_on Z
     by A7,A6,FDIFF_2:21;
A14: for x st x in Z holds (exp_R/(( #Z 2)*(f1+exp_R))).x >0
     proof
       let x;
       assume A15:x in Z;
       then A16:(exp_R/(( #Z 2)*(f1+exp_R))).x
       =exp_R.x*((( #Z 2)*(f1+exp_R)).x)" by RFUNCT_1:def 4,A3
       .=exp_R.x*(1/(( #Z 2)*(f1+exp_R)).x) by XCMPLX_1:217
       .=exp_R.x/(( #Z 2)*(f1+exp_R)).x by XCMPLX_1:100;
A17:    (( #Z 2)*(f1+exp_R)).x>0 by A9,A15;
       exp_R.x>0 by SIN_COS:59;
       hence (exp_R/(( #Z 2)*(f1+exp_R))).x >0 by A16,XREAL_1:141,A17;
     end;
A18: for x st x in Z holds ln*(exp_R/(( #Z 2)*(f1+exp_R)))
     is_differentiable_in x
     proof
       let x;
       assume A19:x in Z;
       then A20:exp_R/(( #Z 2)*(f1+exp_R)) is_differentiable_in x
         by FDIFF_1:16,A13;
       (exp_R/(( #Z 2)*(f1+exp_R))).x >0 by A14,A19;
       hence ln*(exp_R/(( #Z 2)*(f1+exp_R)))
       is_differentiable_in x by TAYLOR_1:20,A20;
     end;
     then A21:f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x =(1-exp_R.x)/(1+exp_R.x)
     proof
       let x;
       assume A22:x in Z;
       then A23:(( #Z 2)*(f1+exp_R)).x<>0 by A9;
A24:    ( #Z 2)*(f1+exp_R) is_differentiable_in x by A6,A22,FDIFF_1:16;
A25:    exp_R is_differentiable_in x by SIN_COS:70;
A26:    exp_R/(( #Z 2)*(f1+exp_R)) is_differentiable_in x by FDIFF_1:16,A13,A22
;
A27:   (exp_R/(( #Z 2)*(f1+exp_R))).x >0 by A14,A22;
A28:   exp_R.x>0 by SIN_COS:59;
       then A29:1+exp_R.x>0 by XREAL_1:36;
       then (1+exp_R.x) #Z 2>0 by PREPOWER:49;
       then A30:exp_R.x/(1+exp_R.x) #Z 2<>0 by XREAL_1:141,A28;
A31:    ((( #Z 2)*(f1+exp_R))).x
       =( #Z 2).((f1+exp_R).x) by A5,A22,FUNCT_1:22
       .=((f1+exp_R).x) #Z 2 by TAYLOR_1:def 1
       .=(f1.x+exp_R.x) #Z 2 by SEQ_1:def 3,A22,A8
       .=(1+exp_R.x) #Z 2 by A22,A2;
A32:    diff((exp_R/(( #Z 2)*(f1+exp_R))),x)
       =(diff(exp_R,x)*(( #Z 2)*(f1+exp_R)).x-diff((( #Z 2)*(f1+exp_R)),x)*
         exp_R.x)
       /((( #Z 2)*(f1+exp_R)).x)^2 by FDIFF_2:14,A24,A25,A23
       .=(exp_R.x*(( #Z 2)*(f1+exp_R)).x-diff((( #Z 2)*(f1+exp_R)),x)*exp_R.x)
       /((( #Z 2)*(f1+exp_R)).x)^2 by SIN_COS:70
       .=(exp_R.x*(( #Z 2)*(f1+exp_R)).x-((( #Z 2)*(f1+exp_R))`|Z).x*exp_R.x)
       /((( #Z 2)*(f1+exp_R)).x)^2 by FDIFF_1:def 8,A22,A6
       .=(exp_R.x*(1+exp_R.x) #Z 2-2*exp_R.x*(1+exp_R.x)*exp_R.x)/((1+exp_R.x)
        #Z 2)^2 by A31,A5,A2,Th29,A22
       .=(exp_R.x*((1+exp_R.x) #Z 2-2*(1+exp_R.x)*exp_R.x))
       /((1+exp_R.x) #Z 2*(1+exp_R.x) #Z 2)
       .=((exp_R.x/(1+exp_R.x) #Z 2)*((1+exp_R.x) #Z 2-2*(1+exp_R.x)*exp_R.x))
       /(1+exp_R.x) #Z 2 by XCMPLX_1:84;
A33:    (exp_R/(( #Z 2)*(f1+exp_R))).x
       =exp_R.x*((( #Z 2)*(f1+exp_R)).x)" by RFUNCT_1:def 4,A3,A22
       .=exp_R.x*(1/(( #Z 2)*(f1+exp_R)).x) by XCMPLX_1:217
       .=exp_R.x/(1+exp_R.x) #Z 2 by A31, XCMPLX_1:100;
A34:   (1+exp_R.x) #Z 2=(1+exp_R.x) #Z (1+1)
       .=(1+exp_R.x) #Z 1 * (1+exp_R.x) #Z 1 by PREPOWER:54,A29
       .=(1+exp_R.x) * (1+exp_R.x) #Z 1 by PREPOWER:45
       .=(1+exp_R.x) * (1+exp_R.x) by PREPOWER:45;
       (f`|Z).x =diff(ln*(exp_R/(( #Z 2)*(f1+exp_R))),x)
       by A21,A22,FDIFF_1:def 8,A1
       .=((exp_R.x/(1+exp_R.x) #Z 2)*((1+exp_R.x) #Z 2-2*(1+exp_R.x)*exp_R.x))
       /(1+exp_R.x) #Z 2/(exp_R.x/(1+exp_R.x) #Z 2) by A32,A33,TAYLOR_1:20,A26,
A27
       .=((exp_R.x/(1+exp_R.x) #Z 2)*((1+exp_R.x) #Z 2-2*(1+exp_R.x)*exp_R.x))
       /((exp_R.x/(1+exp_R.x) #Z 2)*(1+exp_R.x) #Z 2) by XCMPLX_1:79
       .=((1+exp_R.x) * (1-exp_R.x))/((1+exp_R.x) * (1+exp_R.x))
          by A34,A30,XCMPLX_1:92
       .=(1-exp_R.x)/ (1+exp_R.x) by A29,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A18,A1,FDIFF_1:16;
   end;

:: 34 f.x=ln(exp_R(x)+exp_R(-x))
theorem
  Z c= dom (ln*f) & f=exp_R+(exp_R*f1) & (for x st x in Z holds
  f1.x=-x) implies ln*f is_differentiable_on Z & for x st x in Z
  holds ((ln*f)`|Z).x =(exp_R(x)-exp_R(-x))/(exp_R(x)+exp_R(-x))
  proof
    assume that A1:Z c= dom (ln*f) & f=exp_R+(exp_R*f1) and
A2: (for x st x in Z holds f1.x=-x);
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A3:Z c= dom (exp_R+(exp_R*f1)) by A1,TARSKI:def 3;
    then Z c= dom exp_R /\ dom (exp_R*f1) by SEQ_1:def 3;
    then A4:Z c= dom (exp_R*f1) by XBOOLE_1:18;
    then A5:exp_R*f1 is_differentiable_on Z &
    for x st x in Z holds ((exp_R*f1)`|Z).x = -exp_R(-x) by A2,Th14;
A6: exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A7: for x st x in Z holds ((exp_R+(exp_R*f1))`|Z).x =exp_R(x)-exp_R(-x)
     proof
       let x;
       assume A8:x in Z;
       hence ((exp_R+(exp_R*f1))`|Z).x = diff(exp_R,x) + diff((exp_R*f1),x)
       by A6,A3,A5,FDIFF_1:26
       .=exp_R.x+ diff((exp_R*f1),x) by SIN_COS:70
       .=exp_R.x+((exp_R*f1)`|Z).x by A8,FDIFF_1:def 8,A5
       .=exp_R.x+(-exp_R(-x)) by A8,A4,A2,Th14
       .=exp_R(x)+(-exp_R(-x)) by SIN_COS:def 27
       .=exp_R(x)-exp_R(-x);
     end;
     then A9:f is_differentiable_on Z & for x st x in Z holds
     (f`|Z).x =exp_R(x)-exp_R(-x) by A6,A5,A3,FDIFF_1:26,A1;
A10: for x st x in Z holds (exp_R+(exp_R*f1)).x>0
     proof
       let x;
       assume A11:x in Z;
       then A12:(exp_R+(exp_R*f1)).x=exp_R.x + (exp_R*f1).x by SEQ_1:def 3,A3
       .=exp_R.x +exp_R.(f1.x) by FUNCT_1:22,A4,A11
       .=exp_R.x +exp_R.(-x) by A2,A11
       .=exp_R(x) +exp_R.(-x) by SIN_COS:def 27
       .=exp_R(x) +exp_R(-x) by SIN_COS:def 27;
       exp_R(x)>0 & exp_R(-x)>0 by SIN_COS:60;
       hence thesis by A12,XREAL_1:36;
     end;
A13: for x st x in Z holds ln*f is_differentiable_in x
     proof
       let x;
       assume A14:x in Z;
       then A15:f is_differentiable_in x by FDIFF_1:16,A9;
       f.x >0 by A14,A10,A1;
       hence thesis by TAYLOR_1:20,A15;
     end;
     then A16:ln*f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds
     ((ln*f)`|Z).x =(exp_R(x)-exp_R(-x))/(exp_R(x)+exp_R(-x))
     proof
       let x;
       assume A17:x in Z;
A18:    f.x=exp_R.x + (exp_R*f1).x by SEQ_1:def 3,A3,A17,A1
       .=exp_R.x +exp_R.(f1.x) by FUNCT_1:22,A4,A17
       .=exp_R.x +exp_R.(-x) by A2,A17
       .=exp_R(x) +exp_R.(-x) by SIN_COS:def 27
       .=exp_R(x) +exp_R(-x) by SIN_COS:def 27;
A19:   f is_differentiable_in x by FDIFF_1:16,A9,A17;
A20:   f.x>0 by A17,A10,A1;
       diff((ln*f),x)
       =diff(f,x)/f.x by TAYLOR_1:20,A19,A20
       .=(f`|Z).x/f.x by A9,A17,FDIFF_1:def 8
       .=(exp_R(x)-exp_R(-x))/(exp_R(x) +exp_R(-x)) by A7,A1,A17,A18;
       hence thesis by A17,A16,FDIFF_1:def 8;
     end;
     hence thesis by A13,A1,FDIFF_1:16;
   end;

:: 35 f.x=ln(exp_R(x)-exp_R(-x))
theorem
  Z c= dom (ln*f) & f=exp_R-(exp_R*f1) & (for x st x in Z holds
  f1.x=-x & f.x>0) implies ln*f is_differentiable_on Z & for x
  st x in Z holds ((ln*f)`|Z).x=(exp_R(x)+exp_R(-x))/
     (exp_R(x)-exp_R(-x))
  proof
    assume that A1:Z c= dom (ln*f) & f=exp_R-(exp_R*f1) and
A2: (for x st x in Z holds f1.x=-x & f.x>0);
A3:  for x st x in Z holds f1.x=-x by A2;
     for y st y in Z holds y in dom f by FUNCT_1:21,A1;
     then A4:Z c= dom (exp_R-(exp_R*f1)) by A1,TARSKI:def 3;
     then Z c= dom exp_R /\ dom (exp_R*f1) by SEQ_1:def 4;
     then A5:Z c= dom (exp_R*f1) by XBOOLE_1:18;
     then A6:exp_R*f1 is_differentiable_on Z &
     for x st x in Z holds ((exp_R*f1)`|Z).x = -exp_R(-x) by A3,Th14;
A7:  exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A8: for x st x in Z holds ((exp_R-(exp_R*f1))`|Z).x =exp_R(x)+exp_R(-x)
     proof
       let x;
       assume A9:x in Z;
       hence ((exp_R-(exp_R*f1))`|Z).x = diff(exp_R,x) - diff((exp_R*f1),x)
       by A7,A4,A6,FDIFF_1:27
       .=exp_R.x- diff((exp_R*f1),x) by SIN_COS:70
       .=exp_R.x-((exp_R*f1)`|Z).x by A9,FDIFF_1:def 8,A6
       .=exp_R.x-(-exp_R(-x)) by A9,A5,A3,Th14
       .=exp_R.x+exp_R(-x)
       .=exp_R(x)+exp_R(-x) by SIN_COS:def 27;
     end;
     then A10:f is_differentiable_on Z & for x st x in Z holds
     (f`|Z).x =exp_R(x)+exp_R(-x) by A7,A6,A4,FDIFF_1:27,A1;
A11: for x st x in Z holds ln*f is_differentiable_in x
     proof
       let x;
       assume A12:x in Z;
       then A13:f is_differentiable_in x by FDIFF_1:16,A10;
       f.x >0 by A12,A2;
       hence thesis by TAYLOR_1:20,A13;
     end;
     then A14:ln*f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds
     ((ln*f)`|Z).x =(exp_R(x)+exp_R(-x))/(exp_R(x)-exp_R(-x))
     proof
       let x;
       assume A15:x in Z;
A16:    f.x=exp_R.x - (exp_R*f1).x by SEQ_1:def 4,A4,A15,A1
       .=exp_R.x -exp_R.(f1.x) by FUNCT_1:22,A5,A15
       .=exp_R.x -exp_R.(-x) by A2,A15
       .=exp_R(x) -exp_R.(-x) by SIN_COS:def 27
       .=exp_R(x) -exp_R(-x) by SIN_COS:def 27;
A17:   f is_differentiable_in x by FDIFF_1:16,A10,A15;
A18:   f.x>0 by A15,A2;
       diff((ln*f),x)
       =diff(f,x)/f.x by TAYLOR_1:20,A17,A18
       .=(f`|Z).x/f.x by A10,A15,FDIFF_1:def 8
       .=(exp_R(x)+exp_R(-x))/(exp_R(x) -exp_R(-x)) by A8,A1,A15,A16;
       hence thesis by A15,A14,FDIFF_1:def 8;
     end;
     hence thesis by A11,A1,FDIFF_1:16;
   end;

:: 36 f.x=(2/3)*((1+exp_R.x) #R (3/2))
theorem
  Z c= dom ((2/3)(#)(( #R (3/2))*(f+exp_R))) & (for x st x in Z holds f.x=1)
  implies (2/3)(#)(( #R (3/2))*(f+exp_R)) is_differentiable_on Z &
  for x st x in Z
  holds (((2/3)(#)(( #R (3/2))*(f+exp_R)))`|Z).x =exp_R.x*(1+exp_R.x) #R (1/2)
  proof
    assume that A1:Z c= dom ((2/3)(#)(( #R (3/2))*(f+exp_R))) and
A2: (for x st x in Z holds f.x=1);
A3: Z c= dom (( #R (3/2))*(f+exp_R)) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom (f+exp_R) by FUNCT_1:21,A3;
    then A4:Z c= dom (f+exp_R) by TARSKI:def 3;
    then Z c= dom exp_R /\ dom f by SEQ_1:def 3;
    then A5:Z c= dom f by XBOOLE_1:18;
A6: for x st x in Z holds f.x=0*x+1 by A2;
     then A7:f is_differentiable_on Z &
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A5;
A8:  exp_R is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
     then A9:f+exp_R is_differentiable_on Z by FDIFF_1:26,A7,A4;
A10:  for x st x in Z holds (f+exp_R).x>0
     proof
       let x;
       assume A11:x in Z;
       then A12:(f+exp_R).x=f.x+exp_R.x by A4,SEQ_1:def 3
       .=1+exp_R.x by A2,A11;
       exp_R.x>0 by SIN_COS:59;
       hence thesis by A12,XREAL_1:36;
     end;
     now let x;
       assume A13:x in Z;
       then A14:f+exp_R is_differentiable_in x by FDIFF_1:16,A9;
       (f+exp_R).x>0 by A10,A13;
       hence ( #R (3/2))*(f+exp_R) is_differentiable_in x by TAYLOR_1:22,A14;
     end;
     then A15:( #R (3/2))*(f+exp_R) is_differentiable_on Z by A3,FDIFF_1:16;
     for x st x in Z
     holds (((2/3)(#)(( #R (3/2))*(f+exp_R)))`|Z).x =exp_R.x*(1+exp_R.x) #R
      (1/2)
     proof
       let x;
       assume A16:x in Z;
       then A17:f+exp_R is_differentiable_in x by FDIFF_1:16,A9;
A18:    (f+exp_R).x>0 by A10,A16;
A19:    (f+exp_R).x=f.x+exp_R.x by A4,A16,SEQ_1:def 3
       .=1+exp_R.x by A2,A16;
A20:    ((f+exp_R)`|Z).x = diff(f,x)+diff(exp_R,x) by A8,A4,A7,FDIFF_1:26,A16
       .=diff(f,x)+exp_R.x by SIN_COS:70
       .=(f`|Z).x+exp_R.x by A16,FDIFF_1:def 8,A7
       .=0+exp_R.x by A16,A6,FDIFF_1:31,A5
       .=exp_R.x;
       (((2/3)(#)(( #R (3/2))*(f+exp_R)))`|Z).x
       =(2/3)*diff((( #R (3/2))*(f+exp_R)),x) by A1,FDIFF_1:28,A16,A15
       .=(2/3)*((3/2)*(((f+exp_R).x) #R (3/2-1)) * diff((f+exp_R),x))
       by TAYLOR_1:22,A17,A18
       .=(2/3)*((3/2)*(((f+exp_R).x) #R (3/2-1)) *((f+exp_R)`|Z).x )
       by FDIFF_1:def 8,A9,A16
       .=exp_R.x*(1+exp_R.x) #R (1/2) by A20,A19;
       hence thesis;
     end;
     hence thesis by A1,FDIFF_1:28,A15;
   end;

:: 37 f.x=(2/(3*lna))*((1+a |^x) #R (3/2))
theorem
  Z c= dom ((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp_R*f1)))) &
  (for x st x in Z holds f.x=1 & f1.x=x*log(number_e,a)) & a>0 & a<>1
  implies (2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp_R*f1)))
  is_differentiable_on Z & for x st x in Z holds
  (((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp_R*f1))))`|Z).x
  =a #R x * (1+a #R x) #R (1/2)
  proof
    assume that
A1: Z c= dom ((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp_R*f1)))) and
A2:(for x st x in Z holds f.x=1 & f1.x=x*log(number_e,a)) and
A3: a>0 and
A4: a<>1;
A5: for x st x in Z holds f1.x=x*log(number_e,a) by A2;
A6:  Z c= dom (( #R (3/2))*(f+(exp_R*f1))) by SEQ_1:def 6,A1;
     for y st y in Z holds y in dom (f+(exp_R*f1)) by FUNCT_1:21,A6;
     then A7:Z c= dom (f+(exp_R*f1)) by TARSKI:def 3;
     then Z c= dom (exp_R*f1) /\ dom f by SEQ_1:def 3;
     then A8:Z c= dom f & Z c= dom (exp_R*f1) by XBOOLE_1:18;
A9: for x st x in Z holds f.x=0*x+1 by A2;
     then A10:f is_differentiable_on Z &
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A8;
A11:  exp_R*f1 is_differentiable_on Z & for x st x in Z holds
     ((exp_R*f1)`|Z).x =a #R x*log(number_e,a) by Th11,A8,A3,A5;
     then A12:f+(exp_R*f1) is_differentiable_on Z by FDIFF_1:26,A10,A7;
A13:  for x st x in Z holds (f+(exp_R*f1)).x>0
     proof
       let x;
       assume A14:x in Z;
       then A15:(f+(exp_R*f1)).x=f.x+(exp_R*f1).x by A7,SEQ_1:def 3
       .=f.x+exp_R.(f1.x) by A14,FUNCT_1:22,A8
       .=1+exp_R.(f1.x) by A2,A14
       .=1+exp_R.(x*log(number_e,a)) by A2,A14;
       exp_R.(x*log(number_e,a))>0 by SIN_COS:59;
       hence thesis by A15,XREAL_1:36;
     end;
     now let x;
       assume A16:x in Z;
       then A17:f+(exp_R*f1) is_differentiable_in x by FDIFF_1:16,A12;
       (f+(exp_R*f1)).x>0 by A13,A16;
       hence ( #R (3/2))*(f+(exp_R*f1)) is_differentiable_in x
         by TAYLOR_1:22,A17;
     end;
     then A18:( #R (3/2))*(f+(exp_R*f1)) is_differentiable_on Z
       by A6,FDIFF_1:16;
A19: log(number_e,a)<>0
     proof
       assume A20:log(number_e,a)=0;
A21:    log(number_e,a)=log(number_e,1) by SIN_COS2:13,A20,TAYLOR_1:13;
A22:    number_e>0 & number_e<>1 by TAYLOR_1:11;
       then a=(number_e) to_power log(number_e,1) by A21,POWER:def 3,A3
       .=1 by POWER:def 3,A22;
       hence contradiction by A4;
     end;
     for x st x in Z holds
     (((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp_R*f1))))`|Z).x
     =a #R x * (1+a #R x) #R (1/2)
     proof
       let x;
       assume A23:x in Z;
       then A24:f+(exp_R*f1) is_differentiable_in x by FDIFF_1:16,A12;
A25:    (f+(exp_R*f1)).x>0 by A13,A23;
A26:    (f+(exp_R*f1)).x=f.x+(exp_R*f1).x by A7,A23,SEQ_1:def 3
       .=f.x+exp_R.(f1.x) by A23,FUNCT_1:22,A8
       .=1+exp_R.(f1.x) by A2,A23
       .=1+exp_R.(x*log(number_e,a)) by A2,A23
       .=1+a #R x by Th1,A3;
A27:    ((f+(exp_R*f1))`|Z).x=diff(f,x)+diff((exp_R*f1),x)
         by A11,A7,A10,FDIFF_1:26,A23
       .=diff(f,x)+((exp_R*f1)`|Z).x by A23,FDIFF_1:def 8,A11
       .=(f`|Z).x+((exp_R*f1)`|Z).x by A23,FDIFF_1:def 8,A10
       .=0+((exp_R*f1)`|Z).x by A23,A9,FDIFF_1:31,A8
       .=a #R x*log(number_e,a) by A23,Th11,A8,A3,A5;
A28:    diff((( #R (3/2))*(f+(exp_R*f1))),x)
       =(3/2)*(((f+(exp_R*f1)).x) #R (3/2-1)) * diff((f+(exp_R*f1)),x)
       by TAYLOR_1:22,A24,A25
       .=(3/2)*((1+a #R x) #R (1/2)) *(a #R x*log(number_e,a))
       by A27,A26,FDIFF_1:def 8,A12,A23
       .=(3*log(number_e,a))/2*a #R x*(1+a #R x) #R (1/2);
       log(number_e,a)>0 or log(number_e,a)<0 by A19;
       then A29:3*log(number_e,a) <> 0;
       (((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp_R*f1))))`|Z).x
       =(2/(3*log(number_e,a)))*((3*log(number_e,a))/2*a #R
       x*(1+a #R x) #R (1/2)) by A28,A1,FDIFF_1:28,A23,A18
       .=(2/(3*log(number_e,a)))*((3*log(number_e,a))/2)
       *a #R x*(1+a #R x) #R (1/2)
       .=1*a #R x*(1+a #R x) #R (1/2) by A29,XCMPLX_1:113
       .=a #R x*(1+a #R x) #R (1/2);
       hence thesis;
     end;
     hence thesis by A1,A18,FDIFF_1:28;
   end;

:: 38 f.x=(-1/2)*cos(2*x)
theorem
  Z c= dom ((-1/2)(#)(cos*f)) & (for x st x in Z holds f.x=2*x)
  implies (-1/2)(#)(cos*f) is_differentiable_on Z &
  for x st x in Z holds (((-1/2)(#)(cos*f))`|Z).x =sin(2*x)
  proof
    assume that A1:Z c= dom ((-1/2)(#)(cos*f)) and
A2: (for x st x in Z holds f.x=2*x);
A3: Z c= dom (cos*f) by SEQ_1:def 6,A1;
A4: for x st x in Z holds f.x=2*x+0 by A2;
    then A5:cos*f is_differentiable_on Z &
    for x st x in Z holds ((cos*f)`|Z).x = -2* sin.(2*x+0) by A3,FDIFF_4:38;
    for x st x in Z holds (((-1/2)(#)(cos*f))`|Z).x =sin(2*x)
    proof
      let x;
      assume A6:x in Z;
      then (((-1/2)(#)(cos*f))`|Z).x =(-1/2)*diff((cos*f),x)
        by A5,A1,FDIFF_1:28
      .=(-1/2)*((cos*f)`|Z).x by A5,A6,FDIFF_1:def 8
      .=(-1/2)*(-2* sin.(2*x+0)) by A4,A3,FDIFF_4:38,A6
      .=sin(2*x) by SIN_COS:def 21;
      hence thesis;
    end;
    hence thesis by A5,A1,FDIFF_1:28;
  end;

theorem
  Z c= dom (2(#)(( #R (1/2))*(f-cos))) & (for x st x in Z holds f.x=1
  & sin.x>0 & cos.x<1 & cos.x>-1)
  implies 2(#)(( #R (1/2))*(f-cos)) is_differentiable_on Z &
  for x st x in Z holds
  ((2(#)(( #R (1/2))*(f-cos)))`|Z).x =(1+cos.x) #R (1/2)
  proof
    assume that A1:Z c= dom (2(#)(( #R (1/2))*(f-cos))) and
A2: (for x st x in Z holds f.x=1 & sin.x>0 & cos.x<1 & cos.x>-1);
A3:  Z c= dom (( #R (1/2))*(f-cos)) by SEQ_1:def 6,A1;
     for y st y in Z holds y in dom (f-cos) by FUNCT_1:21,A3;
     then A4:Z c= dom (f-cos) by TARSKI:def 3;
     then Z c= dom cos /\ dom f by SEQ_1:def 4;
     then A5:Z c= dom f by XBOOLE_1:18;
A6: for x st x in Z holds f.x=0*x+1 by A2;
     then A7:f is_differentiable_on Z &
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A5;
A8:  cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
     then A9:f-cos is_differentiable_on Z by A7,A4,FDIFF_1:27;
A10: for x st x in Z holds (f-cos).x>0
     proof
       let x;
       assume A11:x in Z;
       then A12:(f-cos).x=f.x-cos.x by A4,SEQ_1:def 4
       .=1-cos.x by A2,A11;
       cos.x<1 by A11,A2;
       then 1-cos.x>1-1 by XREAL_1:17;
       hence thesis by A12;
     end;
     now let x;
       assume A13:x in Z;
       then A14: f-cos is_differentiable_in x by A9,FDIFF_1:16;
       (f-cos).x >0 by A10,A13;
       hence ( #R (1/2))*(f-cos) is_differentiable_in x by TAYLOR_1:22,A14;
     end;
     then A15:( #R (1/2))*(f-cos) is_differentiable_on Z by A3,FDIFF_1:16;
     for x st x in Z holds
     ((2(#)(( #R (1/2))*(f-cos)))`|Z).x =(1+cos.x) #R (1/2)
     proof
       let x;
       assume A16:x in Z;
       then A17: f-cos is_differentiable_in x by A9,FDIFF_1:16;
A18:    (f-cos).x >0 by A10,A16;
A19:    (f-cos).x=f.x-cos.x by A4,A16,SEQ_1:def 4
       .=1-cos.x by A2,A16;
       then A20:1-cos.x>0 by A10,A16;
A21:    diff((f-cos),x)=((f-cos)`|Z).x by A9,A16,FDIFF_1:def 8
       .=diff(f,x)-diff(cos,x) by A16,A7,A8,A4,FDIFF_1:27
       .=(f`|Z).x-diff(cos,x) by A7,A16,FDIFF_1:def 8
       .=0- diff(cos,x) by A6,FDIFF_1:31,A5,A16
       .=0-(-sin.x) by SIN_COS:68
       .=sin.x;
A22:    sin.x>0 & cos.x<1 & cos.x>-1 by A2,A16;
       ((2(#)(( #R (1/2))*(f-cos)))`|Z).x
       =2*diff((( #R (1/2))*(f-cos)),x) by FDIFF_1:28,A1,A15,A16
       .=2*((1/2)*( ((f-cos).x) #R (1/2-1)) * diff((f-cos),x))
       by TAYLOR_1:22,A17,A18
       .=sin.x*((1-cos.x) #R (-1/2)) by  A19,A21
       .=sin.x*(1 /  (1-cos.x) #R (1/2)) by PREPOWER:90,A20
       .=sin.x/(1-cos.x) #R (1/2) by XCMPLX_1:100
       .=(1+cos.x) #R (1/2) by Lm4,A22;
       hence thesis;
     end;
     hence thesis by A15,FDIFF_1:28,A1;
   end;

theorem
  Z c= dom ((-2)(#)(( #R (1/2))*(f+cos))) & (for x st x in Z holds f.x=1
  & sin.x>0 & cos.x<1 & cos.x>-1)
  implies (-2)(#)(( #R (1/2))*(f+cos)) is_differentiable_on Z &
  for x st x in Z holds
  (((-2)(#)(( #R (1/2))*(f+cos)))`|Z).x =(1-cos.x) #R (1/2)
  proof
    assume that A1:Z c= dom ((-2)(#)(( #R (1/2))*(f+cos))) and
A2: (for x st x in Z holds f.x=1 & sin.x>0 & cos.x<1 & cos.x>-1);
A3:  Z c= dom (( #R (1/2))*(f+cos)) by SEQ_1:def 6,A1;
     for y st y in Z holds y in dom (f+cos) by FUNCT_1:21,A3;
     then A4:Z c= dom (f+cos) by TARSKI:def 3;
     then Z c= dom cos /\ dom f by SEQ_1:def 3;
     then A5:Z c= dom f by XBOOLE_1:18;
A6: for x st x in Z holds f.x=0*x+1 by A2;
     then A7:f is_differentiable_on Z &
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A5;
A8:  cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
     then A9:f+cos is_differentiable_on Z by A7,A4,FDIFF_1:26;
A10: for x st x in Z holds (f+cos).x>0
     proof
       let x;
       assume A11:x in Z;
       then A12:(f+cos).x=f.x+cos.x by A4,SEQ_1:def 3
       .=1+cos.x by A2,A11;
       cos.x>-1 by A11,A2;
       then 1+cos.x>1+-1 by XREAL_1:10;
       hence thesis by A12;
     end;
     now let x;
       assume A13:x in Z;
       then A14: f+cos is_differentiable_in x by A9,FDIFF_1:16;
       (f+cos).x >0 by A10,A13;
       hence ( #R (1/2))*(f+cos) is_differentiable_in x by TAYLOR_1:22,A14;
     end;
     then A15:( #R (1/2))*(f+cos) is_differentiable_on Z by A3,FDIFF_1:16;
     for x st x in Z holds
     (((-2)(#)(( #R (1/2))*(f+cos)))`|Z).x =(1-cos.x) #R (1/2)
     proof
       let x;
       assume A16:x in Z;
       then A17: f+cos is_differentiable_in x by A9,FDIFF_1:16;
A18:    (f+cos).x >0 by A10,A16;
A19:    (f+cos).x=f.x+cos.x by A4,A16,SEQ_1:def 3
       .=1+cos.x by A2,A16;
       then A20:1+cos.x>0 by A10,A16;
A21:    diff((f+cos),x)=((f+cos)`|Z).x by A9,A16,FDIFF_1:def 8
       .=diff(f,x)+diff(cos,x) by A16,A7,A8,A4,FDIFF_1:26
       .=(f`|Z).x+diff(cos,x) by A7,A16,FDIFF_1:def 8
       .=0+diff(cos,x) by A6,FDIFF_1:31,A5,A16
       .=-sin.x by SIN_COS:68;
A22:    sin.x>0 & cos.x<1 & cos.x>-1 by A2,A16;
       (((-2)(#)(( #R (1/2))*(f+cos)))`|Z).x
       =(-2)*diff((( #R (1/2))*(f+cos)),x) by FDIFF_1:28,A1,A15,A16
       .=(-2)*((1/2)*( ((f+cos).x) #R (1/2-1)) * diff((f+cos),x))
       by TAYLOR_1:22,A17,A18
       .=--sin.x*((1+cos.x) #R (-1/2)) by  A19,A21
       .=sin.x*(1/(1+cos.x) #R (1/2)) by PREPOWER:90,A20
       .=sin.x/(1+cos.x) #R (1/2) by XCMPLX_1:100
       .=(1-cos.x) #R (1/2) by Lm5,A22;
       hence thesis;
     end;
     hence thesis by A15,FDIFF_1:28,A1;
   end;

:: 41 f.x=1/2*ln(1+2*sin.x)
Lm6: Z c= dom (f1+2(#)sin) & (for x st x in Z holds f1.x=1)
     implies f1+2(#)sin is_differentiable_on Z & for x st x in Z holds
     ((f1+2(#)sin)`|Z).x =2*cos.x
     proof
       assume that A1:Z c= dom (f1+2(#)sin) and
A2:    for x st x in Z holds f1.x=1;
       Z c= dom f1 /\ dom (2(#)sin) by SEQ_1:def 3,A1;
       then A3:Z c= dom f1 & Z c= dom (2(#)sin) by XBOOLE_1:18;
A4:    for x st x in Z holds f1.x=0*x+1 by A2;
       then A5:f1 is_differentiable_on Z & for x st x in Z
       holds (f1`|Z).x = 0 by FDIFF_1:31,A3;
       sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
       then A6:2(#)sin is_differentiable_on Z by FDIFF_1:28,A3;
       for x st x in Z holds ((f1+2(#)sin)`|Z).x = 2*cos.x
       proof
         let x;
         assume A7:x in Z;
A8:      sin is_differentiable_in x by SIN_COS:69;
         ((f1+2(#)sin)`|Z).x =diff(f1,x) + diff(2(#)sin,x)
         by FDIFF_1:26,A1,A6,A5,A7
         .=(f1`|Z).x+ diff(2(#)sin,x) by A7,A5,FDIFF_1:def 8
         .=(f1`|Z).x+2*diff(sin,x) by A8,FDIFF_1:23
         .=0+2*diff(sin,x) by A4,FDIFF_1:31,A3,A7
         .=2*cos.x by SIN_COS:69;
         hence thesis;
       end;
       hence thesis by A5,A6,A1,FDIFF_1:26;
     end;

theorem
  Z c= dom ((1/2)(#)(ln*f)) & f=f1+2(#)sin &
  (for x st x in Z holds f1.x=1 & f.x >0) implies (1/2)(#)(ln*f)
  is_differentiable_on Z & for x st x in Z holds
  (((1/2)(#)(ln*f))`|Z).x =cos.x/(1+2*sin.x)
  proof
    assume that A1:Z c= dom ((1/2)(#)(ln*f)) and
A2: f=f1+2(#)sin & (for x st x in Z holds f1.x=1 & f.x >0);
A3: f=f1+2(#)sin & (for x st x in Z holds f1.x=1) by A2;
A4: Z c= dom (ln*f) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom f by FUNCT_1:21,A4;
    then A5:Z c= dom (f1+2(#)sin) by A2,TARSKI:def 3;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x =2*cos.x by A3,Lm6;
    Z c= dom f1 /\ dom (2(#)sin) by SEQ_1:def 3,A5;
    then A7:Z c= dom f1 & Z c= dom (2(#)sin) by XBOOLE_1:18;
    for x st x in Z holds ln*f is_differentiable_in x
    proof
      let x;
      assume A8:x in Z;
      then A9:f is_differentiable_in x by FDIFF_1:16,A6;
      f.x >0 by A2,A8;
      hence ln*f is_differentiable_in x by TAYLOR_1:20,A9;
    end;
    then A10:ln*f is_differentiable_on Z by A4,FDIFF_1:16;
    for x st x in Z holds
    (((1/2)(#)(ln*f))`|Z).x =cos.x/(1+2*sin.x)
    proof
      let x;
      assume A11:x in Z;
      then A12:f is_differentiable_in x by FDIFF_1:16,A6;
A13:   f.x>0 by A2,A11;
A14:   f.x=f1.x+(2(#)sin).x by SEQ_1:def 3,A5,A11,A2
      .=1+(2(#)sin).x by A11,A2
      .=1+2*sin.x by SEQ_1:def 6,A7,A11;
      (((1/2)(#)(ln*f))`|Z).x
      =(1/2)*diff((ln*f),x) by A10,A1,FDIFF_1:28,A11
      .=(1/2)*(diff(f,x)/(f.x)) by TAYLOR_1:20,A12,A13
      .=(1/2)*((f`|Z).x/(f.x)) by FDIFF_1:def 8,A6,A11
      .=(1/2)*((2*cos.x)/(f.x)) by A5,A3,Lm6,A11
      .=(1/2)*(2*cos.x)/(f.x) by XCMPLX_1:75
      .=cos.x/(1+2*sin.x) by A14;
      hence thesis;
    end;
    hence thesis by A10,FDIFF_1:28,A1;
  end;

:: 42 f.x=(-1/2)*ln(1+2*cos.x)
Lm7: Z c= dom (f1+2(#)cos) & (for x st x in Z holds f1.x=1)
     implies f1+2(#)cos is_differentiable_on Z & for x st x in Z holds
     ((f1+2(#)cos)`|Z).x =-2*sin.x
     proof
       assume that A1:Z c= dom (f1+2(#)cos) and
A2:    for x st x in Z holds f1.x=1;
       Z c= dom f1 /\ dom (2(#)cos) by SEQ_1:def 3,A1;
       then A3:Z c= dom f1 & Z c= dom (2(#)cos) by XBOOLE_1:18;
A4:    for x st x in Z holds f1.x=0*x+1 by A2;
       then A5:f1 is_differentiable_on Z & for x st x in Z
       holds (f1`|Z).x = 0 by FDIFF_1:31,A3;
       cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
       then A6:2(#)cos is_differentiable_on Z by FDIFF_1:28,A3;
       for x st x in Z holds ((f1+2(#)cos)`|Z).x = -2*sin.x
       proof
         let x;
         assume A7:x in Z;
A8:      cos is_differentiable_in x by SIN_COS:68;
         ((f1+2(#)cos)`|Z).x =diff(f1,x) + diff(2(#)cos,x)
         by FDIFF_1:26,A1,A6,A5,A7
         .=(f1`|Z).x+ diff(2(#)cos,x) by A7,A5,FDIFF_1:def 8
         .=(f1`|Z).x+2*diff(cos,x) by A8,FDIFF_1:23
         .=0+2*diff(cos,x) by A4,FDIFF_1:31,A3,A7
         .=0+2*(-sin.x) by SIN_COS:68
         .=-2*sin.x;
         hence thesis;
       end;
       hence thesis by A5,A6,A1,FDIFF_1:26;
     end;

theorem
  Z c= dom ((-1/2)(#)(ln*f)) & f=f1+2(#)cos & (for x st x in Z
  holds f1.x=1 & f.x >0) implies (-1/2)(#)(ln*f)
  is_differentiable_on Z & for x st x in Z holds
  (((-1/2)(#)(ln*f))`|Z).x =sin.x/(1+2*cos.x)
  proof
    assume that A1:Z c= dom ((-1/2)(#)(ln*f)) and
A2: f=f1+2(#)cos & (for x st x in Z holds f1.x=1 & f.x >0);
A3: f=f1+2(#)cos & (for x st x in Z holds f1.x=1) by A2;
A4: Z c= dom (ln*f) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom f by FUNCT_1:21,A4;
    then A5:Z c= dom (f1+2(#)cos) by A2,TARSKI:def 3;
    then A6:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x =-2*sin.x by A3,Lm7;
    Z c= dom f1 /\ dom (2(#)cos) by SEQ_1:def 3,A5;
    then A7:Z c= dom f1 & Z c= dom (2(#)cos) by XBOOLE_1:18;
    for x st x in Z holds ln*f is_differentiable_in x
    proof
      let x;
      assume A8:x in Z;
      then A9:f is_differentiable_in x by FDIFF_1:16,A6;
      f.x >0 by A2,A8;
      hence ln*f is_differentiable_in x by TAYLOR_1:20,A9;
    end;
    then A10:ln*f is_differentiable_on Z by A4,FDIFF_1:16;
    for x st x in Z holds
    (((-1/2)(#)(ln*f))`|Z).x =sin.x/(1+2*cos.x)
    proof
      let x;
      assume A11:x in Z;
      then A12:f is_differentiable_in x by FDIFF_1:16,A6;
A13:   f.x>0 by A2,A11;
A14:   f.x=f1.x+(2(#)cos).x by SEQ_1:def 3,A5,A11,A2
      .=1+(2(#)cos).x by A11,A2
      .=1+2*cos.x by SEQ_1:def 6,A7,A11;
      (((-1/2)(#)(ln*f))`|Z).x
      =(-1/2)*diff((ln*f),x) by A10,A1,FDIFF_1:28,A11
      .=(-1/2)*(diff(f,x)/(f.x)) by TAYLOR_1:20,A12,A13
      .=(-1/2)*((f`|Z).x/(f.x)) by FDIFF_1:def 8,A6,A11
      .=(-1/2)*((-2*sin.x)/(f.x)) by A5,A3,Lm7,A11
      .=(-1/2)*(-2*sin.x)/(f.x) by XCMPLX_1:75
      .=sin.x/(1+2*cos.x) by A14;
      hence thesis;
    end;
    hence thesis by A10,FDIFF_1:28,A1;
  end;

:: 43 f.x=1/(4*a)*sin(2*a*x)
theorem Th43:
  Z c= dom ((1/(4*a))(#)(sin*f)) & (for x st x in Z holds f.x=2*a*x)
  & a<>0 implies (1/(4*a))(#)(sin*f) is_differentiable_on Z &
  for x st x in Z holds (((1/(4*a))(#)(sin*f))`|Z).x =1/2 * cos(2*a*x)
  proof
    assume that A1:Z c= dom ((1/(4*a))(#)(sin*f)) and
A2: (for x st x in Z holds f.x=2*a*x) & a<>0;
A3: Z c= dom (sin*f) by SEQ_1:def 6,A1;
A4: for x st x in Z holds f.x=(2*a)*x+0 by A2;
    then A5:sin*f is_differentiable_on Z & for x st x in Z holds
    ((sin*f)`|Z).x =(2*a)* cos.((2*a)*x+0) by A3,FDIFF_4:37;
    for x st x in Z holds (((1/(4*a))(#)(sin*f))`|Z).x =1/2 * cos(2*a*x)
    proof
      let x;
      assume A6:x in Z;
      (((1/(4*a))(#)(sin*f))`|Z).x
      =(1/(4*a))*diff((sin*f),x) by A1,FDIFF_1:28,A6,A5
      .=(1/(4*a))*((sin*f)`|Z).x by FDIFF_1:def 8,A5,A6
      .=(1/(4*a))*((2*a)* cos.((2*a)*x+0)) by A4,A3,FDIFF_4:37,A6
      .=1/(4*a)*(2*a)* cos.((2*a)*x+0)
      .=(1/4)*(1/a)*(2*a)* cos.((2*a)*x) by XCMPLX_1:103
      .=(1/4)*2*(1/a*a)* cos.(2*a*x)
      .=(1/2)*1*cos.(2*a*x) by A2,XCMPLX_1:107
      .=1/2*cos(2*a*x) by SIN_COS:def 23;
      hence thesis;
    end;
    hence thesis by A5,A1,FDIFF_1:28;
  end;

:: 44 f.x=x/2-1/(4*a)*sin(2*a*x)
theorem
  Z c= dom (f1-((1/(4*a))(#)(sin*f))) & (for x st x in Z holds f1.x=x/2
  & f.x=2*a*x) & a<>0 implies f1-((1/(4*a))(#)(sin*f)) is_differentiable_on Z
  & for x st x in Z holds ((f1-((1/(4*a))(#)(sin*f)))`|Z).x =(sin(a*x))^2
  proof
    assume that A1:Z c= dom (f1-((1/(4*a))(#)(sin*f))) and
A2: (for x st x in Z holds f1.x=x/2 & f.x=2*a*x) & a<>0;
A3: (for x st x in Z holds f.x=2*a*x) & a<>0 by A2;
    Z c= dom f1 /\ dom ((1/(4*a))(#)(sin*f)) by SEQ_1:def 4,A1;
    then A4:Z c= dom ((1/(4*a))(#)(sin*f)) & Z c= dom f1 by XBOOLE_1:18;
    then A5:(1/(4*a))(#)(sin*f) is_differentiable_on Z & for x st x in Z
    holds (((1/(4*a))(#)(sin*f))`|Z).x =1/2 * cos(2*a*x) by A3,Th43;
A6: for x st x in Z holds f1.x=(1/2)*x+0
    proof
      let x;
      assume x in Z;
      then f1.x=x/2 by A2
      .= (1/2)*x+0;
      hence thesis;
    end;
    then A7:f1 is_differentiable_on Z & for x st x in Z
    holds (f1`|Z).x =1/2 by FDIFF_1:31,A4;
    for x st x in Z holds ((f1-((1/(4*a))(#)(sin*f)))`|Z).x =(sin(a*x))^2
    proof
      let x;
      assume A8:x in Z;
      then ((f1-((1/(4*a))(#)(sin*f)))`|Z).x
      =diff(f1,x)-diff(((1/(4*a))(#)(sin*f)),x) by A5,A7,FDIFF_1:27,A1
      .=(f1`|Z).x-diff(((1/(4*a))(#)(sin*f)),x) by FDIFF_1:def 8,A8,A7
      .=(f1`|Z).x-(((1/(4*a))(#)(sin*f))`|Z).x by FDIFF_1:def 8,A8,A5
      .=(f1`|Z).x-1/2 * cos(2*a*x) by A4,A3,Th43,A8
      .=1/2-1/2 * cos(2*a*x) by A6,FDIFF_1:31,A4,A8
      .=(1/2)*(1-cos(2*(a*x)))
      .=(1/2)*(2*(sin(a*x))^2) by Lm1
      .=(sin(a*x))^2;
      hence thesis;
    end;
    hence thesis by A5,A7,FDIFF_1:27,A1;
  end;

:: 45 f.x=x/2+1/(4*a)*sin(2*a*x)
theorem
  Z c= dom (f1+((1/(4*a))(#)(sin*f))) & (for x st x in Z holds f1.x=x/2
  & f.x=2*a*x) & a<>0 implies f1+((1/(4*a))(#)(sin*f)) is_differentiable_on Z
  & for x st x in Z holds ((f1+((1/(4*a))(#)(sin*f)))`|Z).x =(cos(a*x))^2
  proof
    assume that A1:Z c= dom (f1+((1/(4*a))(#)(sin*f))) and
A2: (for x st x in Z holds f1.x=x/2 & f.x=2*a*x) & a<>0;
A3: (for x st x in Z holds f.x=2*a*x) & a<>0 by A2;
    Z c= dom f1 /\ dom ((1/(4*a))(#)(sin*f)) by SEQ_1:def 3,A1;
    then A4:Z c= dom ((1/(4*a))(#)(sin*f)) & Z c= dom f1 by XBOOLE_1:18;
    then A5:(1/(4*a))(#)(sin*f) is_differentiable_on Z & for x st x in Z
    holds (((1/(4*a))(#)(sin*f))`|Z).x =1/2 * cos(2*a*x) by A3,Th43;
A6: for x st x in Z holds f1.x=(1/2)*x+0
    proof
      let x;
      assume x in Z;
      then f1.x=x/2 by A2
      .= (1/2)*x+0;
      hence thesis;
    end;
    then A7:f1 is_differentiable_on Z & for x st x in Z
    holds (f1`|Z).x =1/2 by FDIFF_1:31,A4;
    for x st x in Z holds ((f1+((1/(4*a))(#)(sin*f)))`|Z).x =(cos(a*x))^2
    proof
      let x;
      assume A8:x in Z;
      then ((f1+((1/(4*a))(#)(sin*f)))`|Z).x
      =diff(f1,x)+diff(((1/(4*a))(#)(sin*f)),x) by A5,A7,FDIFF_1:26,A1
      .=(f1`|Z).x+diff(((1/(4*a))(#)(sin*f)),x) by FDIFF_1:def 8,A8,A7
      .=(f1`|Z).x+(((1/(4*a))(#)(sin*f))`|Z).x by FDIFF_1:def 8,A8,A5
      .=(f1`|Z).x+1/2 * cos(2*a*x) by A4,A3,Th43,A8
      .=1/2+1/2 * cos(2*a*x) by A6,FDIFF_1:31,A4,A8
      .=(1/2)*(1+cos(2*(a*x)))
      .=(1/2)*(2*(cos(a*x))^2) by Lm2
      .=(cos(a*x))^2;
      hence thesis;
    end;
    hence thesis by A5,A7,FDIFF_1:26,A1;
  end;

:: 46 f.x=(1/n)*(cos.x #Z n)
theorem Th46:
  Z c= dom ((1/n)(#)(( #Z n)*cos)) & n>0 implies
  (1/n)(#)(( #Z n)*cos) is_differentiable_on Z & for x st x in Z holds
  (((1/n)(#)(( #Z n)*cos))`|Z).x =-(cos.x) #Z (n-1)*sin.x
  proof
    assume that A1:Z c= dom ((1/n)(#)(( #Z n)*cos)) and A2:n>0;
A3: Z c= dom (( #Z n)*cos) by SEQ_1:def 6,A1;
    now let x;
      assume x in Z;
      cos is_differentiable_in x by SIN_COS:68;
      hence ( #Z n)*cos is_differentiable_in x by TAYLOR_1:3;
    end;
    then A4:( #Z n)*cos is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds
    (((1/n)(#)(( #Z n)*cos))`|Z).x =-(cos.x) #Z (n-1) *sin.x
    proof
      let x;
      assume A5:x in Z;
A6:   cos is_differentiable_in x by SIN_COS:68;
      (((1/n)(#)(( #Z n)*cos))`|Z).x =(1/n)*diff((( #Z n)*cos),x)
      by A4,A1,FDIFF_1:28,A5
      .=(1/n)*(n*( (cos.x) #Z (n-1)) * diff(cos,x)) by TAYLOR_1:3,A6
      .=(1/n)*(n*( (cos.x) #Z (n-1)) *(-sin.x)) by SIN_COS:68
      .=((1/n)*n)*( (cos.x) #Z (n-1)) *(-sin.x)
      .=((n")*n)*( (cos.x) #Z (n-1)) *(-sin.x) by XCMPLX_1:217
      .=1*( (cos.x) #Z (n-1)) *(-sin.x) by A2,XCMPLX_0:def 7
      .=-(cos.x) #Z (n-1) *sin.x;
      hence thesis;
    end;
    hence thesis by A4,A1,FDIFF_1:28;
  end;

:: 47 f.x=(1/3)*(cos.x #Z 3)-cos.x
theorem
  Z c= dom (((1/3)(#)(( #Z 3)*cos))-cos) & n>0 implies
  (1/3)(#)(( #Z 3)*cos)-cos is_differentiable_on Z & for x st x in Z holds
  ((((1/3)(#)(( #Z 3)*cos))-cos)`|Z).x =(sin.x)|^3
  proof
    assume that A1:Z c= dom (((1/3)(#)(( #Z 3)*cos))-cos) and  n>0;
    Z c= dom ((1/3)(#)(( #Z 3)*cos)) /\ dom cos by SEQ_1:def 4,A1;
    then A2:Z c= dom ((1/3)(#)(( #Z 3)*cos)) & Z c= dom cos by XBOOLE_1:18;
    then A3:((1/3)(#)(( #Z 3)*cos)) is_differentiable_on Z &
    for x st x in Z holds
    (((1/3)(#)(( #Z 3)*cos))`|Z).x =-(cos.x) #Z (3-1)*sin.x by Th46;
A4: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume A5:x in Z;
      ((((1/3)(#)(( #Z 3)*cos))-cos)`|Z).x
      = diff(((1/3)(#)(( #Z 3)*cos)),x) - diff(cos,x) by A3,A1,FDIFF_1:27,A4,A5
      .=diff(((1/3)(#)(( #Z 3)*cos)),x)-(-sin.x) by SIN_COS:68
      .=(((1/3)(#)(( #Z 3)*cos))`|Z).x-(-sin.x) by FDIFF_1:def 8,A3,A5
      .=-(cos.x) #Z (3-1)*sin.x--sin.x by A2,Th46,A5
      .=sin.x*(1-(cos.x) #Z 2 )
      .=sin.x*(1-(cos.x) |^ abs(2) ) by PREPOWER:def 4
      .=sin.x*(1-(cos.x) |^ 2 ) by ABSVALUE:def 1
      .=sin.x*(1-(cos.x)*(cos.x)) by WSIERP_1:2
      .=sin.x*((cos.x)*(cos.x)+(sin.x)*(sin.x)-(cos.x)*(cos.x)) by SIN_COS:31
      .=sin.x*((sin.x)|^2) by WSIERP_1:2
      .=((sin.x)|^(2+1)) by NEWTON:11
      .=(sin.x)|^3;
      hence ((((1/3)(#)(( #Z 3)*cos))-cos)`|Z).x =(sin.x)|^3;
    end;
    hence thesis by A3,A1,FDIFF_1:27,A4;
  end;

:: 48 f.x=sin.x-(1/3)*(sin.x #Z 3)
theorem
  Z c= dom (sin-((1/3)(#)(( #Z 3)*sin))) & n>0 implies
  sin-((1/3)(#)(( #Z 3)*sin)) is_differentiable_on Z & for x st x in Z holds
  ((sin-((1/3)(#)(( #Z 3)*sin)))`|Z).x =(cos.x)|^3
  proof
    assume that A1:Z c= dom (sin-((1/3)(#)(( #Z 3)*sin))) and  n>0;
    Z c= dom ((1/3)(#)(( #Z 3)*sin)) /\ dom sin by SEQ_1:def 4,A1;
    then A2:Z c= dom ((1/3)(#)(( #Z 3)*sin)) by XBOOLE_1:18;
    then A3:((1/3)(#)(( #Z 3)*sin)) is_differentiable_on Z &
    for x st x in Z holds
    (((1/3)(#)(( #Z 3)*sin))`|Z).x =(sin.x) #Z (3-1) *cos.x by FDIFF_4:54;
A4: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A5:x in Z;
      ((sin-((1/3)(#)(( #Z 3)*sin)))`|Z).x
      = diff(sin,x) - diff(((1/3)(#)(( #Z 3)*sin)),x) by A3,A1,FDIFF_1:27,A4,A5
      .=cos.x - diff(((1/3)(#)(( #Z 3)*sin)),x) by SIN_COS:69
      .=cos.x -(((1/3)(#)(( #Z 3)*sin))`|Z).x by FDIFF_1:def 8,A3,A5
      .=cos.x -((sin.x) #Z (3-1) *cos.x) by A2,FDIFF_4:54,A5
      .=cos.x*(1-(sin.x) #Z 2)
      .=cos.x*(1-(sin.x) |^ abs(2) ) by PREPOWER:def 4
      .=cos.x*(1-(sin.x) |^ 2 ) by ABSVALUE:def 1
      .=cos.x*(1-(sin.x)*(sin.x)) by WSIERP_1:2
      .=cos.x*((cos.x)*(cos.x)+(sin.x)*(sin.x)-(sin.x)*(sin.x)) by SIN_COS:31
      .=cos.x*((cos.x)|^2) by WSIERP_1:2
      .=((cos.x)|^(2+1)) by NEWTON:11
      .=(cos.x)|^3;
      hence ((sin-((1/3)(#)(( #Z 3)*sin)))`|Z).x =(cos.x)|^3;
    end;
    hence thesis by A3,A1,FDIFF_1:27,A4;
  end;

:: 49 f.x=sin(ln x)
theorem
  Z c= dom (sin*ln) implies sin*ln
  is_differentiable_on Z & for x st x in Z holds
  ((sin*ln)`|Z).x =cos.(log(number_e,x))/x
  proof
    assume A1: Z c= dom (sin*ln);
    for y st y in Z holds y in dom ln by FUNCT_1:21,A1;
    then A2:Z c= dom ln by TARSKI:def 3;
    then A3:ln is_differentiable_on Z by TAYLOR_1:18,FDIFF_1:34;
A4: for x st x in Z holds sin*ln is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A5: ln is_differentiable_in x by FDIFF_1:16,A3;
      sin is_differentiable_in ln.x by SIN_COS:69;
      hence sin*ln is_differentiable_in x by A5,FDIFF_2:13;
    end;
    then A6:sin*ln is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds ((sin*ln)`|Z).x =cos.(log(number_e,x))/x
    proof
      let x;
      assume A7:x in Z;
      then A8:x in right_open_halfline(0) by TAYLOR_1:18,FUNCT_1:21,A1;
A9:   ln is_differentiable_in x by FDIFF_1:16,A3,A7;
      sin is_differentiable_in ln.x by SIN_COS:69;
      then diff(sin*ln,x)
      = diff(sin,ln.x)*diff(ln,x) by A9,FDIFF_2:13
      .=cos.(ln.x)*diff(ln,x) by SIN_COS:69
      .=cos.(log(number_e,x))*diff(ln,x) by TAYLOR_1:def 2,A8
      .=cos.(log(number_e,x))*(1/x) by A7,A2,TAYLOR_1:18
      .=cos.(log(number_e,x))/x by XCMPLX_1:100;
      hence thesis by A7,A6,FDIFF_1:def 8;
    end;
    hence thesis by A4,FDIFF_1:16,A1;
  end;

:: 50 f.x=-cos(ln x)
theorem
  Z c= dom (-(cos*ln)) implies -(cos*ln)
  is_differentiable_on Z & for x st x in Z holds
  ((-(cos*ln))`|Z).x =sin.(log(number_e,x))/x
  proof
    assume Z c= dom (-(cos*ln));
    then A1:Z c= dom (cos*ln) by SEQ_1:def 7;
    then A2:Z c= dom ((-1)(#)(cos*ln)) by SEQ_1:def 6;
    for y st y in Z holds y in dom ln by FUNCT_1:21,A1;
    then A3:Z c= dom ln by TARSKI:def 3;
    then A4:ln is_differentiable_on Z by TAYLOR_1:18,FDIFF_1:34;
    for x st x in Z holds cos*ln is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A5:ln is_differentiable_in x by FDIFF_1:16,A4;
      cos is_differentiable_in ln.x by SIN_COS:68;
      hence thesis by A5,FDIFF_2:13;
    end;
    then A6:cos*ln is_differentiable_on Z by FDIFF_1:16,A1;
    then A7:(-1)(#)(cos*ln) is_differentiable_on Z &
    for x st x in Z holds (((-1)(#)(cos*ln))`|Z).x
    =(-1)*diff(cos*ln,x) by FDIFF_1:28,A2;
    for x st x in Z holds
    ((-(cos*ln))`|Z).x =sin.(log(number_e,x))/x
    proof
      let x;
      assume A8:x in Z;
      then A9:x in right_open_halfline(0) by TAYLOR_1:18,FUNCT_1:21,A1;
A10:   ln is_differentiable_in x by FDIFF_1:16,A4,A8;
A11:   cos is_differentiable_in ln.x by SIN_COS:68;
      ((-(cos*ln))`|Z).x
      =(((-1)(#)(cos*ln))`|Z).x by RFUNCT_1:38
      .=(-1)*diff((cos*ln),x) by A6,FDIFF_1:28,A2,A8
      .=(-1)*(diff(cos,ln.x)*diff(ln,x))
      by A11,A10,FDIFF_2:13
      .=(-1)*((-sin.(ln.x))*diff(ln,x)) by SIN_COS:68
      .=(-1)*(-sin.(ln.x))*diff(ln,x)
      .=(-1)*(-sin.(log(number_e,x)))*diff(ln,x)
      by TAYLOR_1:def 2,A9
      .=(-1)*(-sin.(log(number_e,x)))*(1/x) by A8,A3,TAYLOR_1:18
      .=sin.(log(number_e,x))/x by XCMPLX_1:100;
      hence thesis;
    end;
    hence thesis by A7,RFUNCT_1:38;
  end;
