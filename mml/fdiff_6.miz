:: Several Differentiable Formulas of Special Functions -- Part {II}
::  by Yan Zhang , Bo Li and Xiquan Liang
:: 
:: Received November 23, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, BOOLE, FUNCT_1, ARYTM_1, ARYTM_3, RELAT_1, ORDINAL2,
      PARTFUN1, SEQ_1, SIN_COS, FDIFF_1, PREPOWER, POWER, GROUP_1, SQUARE_1,
      TAYLOR_1, FINSEQ_1, ABSVALUE, LIMFUNC1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, PARTFUN1, LIMFUNC1, RCOMP_1,
      XREAL_0, NUMBERS, REAL_1, NAT_1, SEQ_1, FDIFF_1, PREPOWER, POWER,
      TAYLOR_1, SIN_COS, RFUNCT_1, SQUARE_1;
 constructors REAL_1, NAT_1, PARTFUN1, PARTFUN2, LIMFUNC1, RCOMP_1, TAYLOR_1,
      SERIES_1, PREPOWER, FDIFF_2, SIN_COS, RFUNCT_1;
 registrations RELSET_1, XREAL_0, MEMBERED, RCOMP_1, INT_1, SEQ_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions SQUARE_1;
 theorems FDIFF_2, TAYLOR_1, XBOOLE_1, XCMPLX_1, SEQ_1, FDIFF_1, FUNCT_1,
      TARSKI, PREPOWER, RFUNCT_1, SQUARE_1, XREAL_1, XCMPLX_0, NEWTON, SIN_COS,
      SIN_COS2, WSIERP_1, FDIFF_4, POWER, HEINE, ABSVALUE;

begin

 reserve c,y,X for set,
         x,r,a,b,c for Real,
         n for Nat,
         Z for open Subset of REAL,
         f,f1,f2 for PartFunc of REAL,REAL;

Lm6: 1-cos(2*x)=2*(sin x)^2
proof
  1-cos(2*x)=1-cos(x+x)
    .=1-((cos x) *(cos x)-(sin x) * (sin x)) by SIN_COS:80
    .=(cos x)^2+(sin x)^2-((cos x)^2-(sin x)^2) by SIN_COS:32
    .=2*(sin x)^2;
  hence thesis;
end;

Lm7: 1+cos(2*x)=2*(cos x)^2
proof
  1+cos(2*x)=1+cos(x+x)
    .=1+((cos x)^2-(sin x)^2) by SIN_COS:80
    .=(cos x)^2+(sin x)^2+((cos x)^2-(sin x)^2) by SIN_COS:32
    .=2*(cos x)^2;
  hence thesis;
end;

:: 39 f.x=2*(1-cos.x) #R (1/2)
Lm1: sin.x>0 implies sin.x=((1-cos.x)*(1+cos.x)) #R (1/2)
proof
  assume A1:sin.x>0;
  then sin.x=(sin.x) #R (2*(1/2)) by PREPOWER:86
  .=(sin.x) #R (1+1) #R (1/2) by A1,PREPOWER:105
  .=((sin.x) #R 1 * (sin.x) #R 1) #R (1/2) by PREPOWER:89,A1
  .=(sin.x * (sin.x) #R 1) #R (1/2) by A1,PREPOWER:86
  .=((sin.x)^2+(cos.x)^2-(cos.x)^2) #R (1/2) by A1,PREPOWER:86
  .=(1^2-(cos.x)^2) #R (1/2) by SIN_COS:31
  .=((1-cos.x)*(1+cos.x)) #R (1/2);
  hence thesis;
end;

Lm2: sin.x>0 & cos.x<1 & cos.x>-1 implies
   sin.x/(1-cos.x) #R (1/2) =(1+cos.x) #R (1/2)
proof
   assume that A1:sin.x>0 and A2:cos.x<1 and A3:cos.x>-1;
A6:1-cos.x>1-1 by A2,XREAL_1:17;
   1+cos.x>1+-1 by A3,XREAL_1:10;
   then A5:(1-cos.x)*(1+cos.x)>0 by A6,XREAL_1:131;
   sin.x/(1-cos.x) #R (1/2)
    =((1-cos.x)*(1+cos.x)) #R (1/2)/(1-cos.x) #R (1/2) by Lm1,A1
   .=(((1-cos.x)*(1+cos.x))/(1-cos.x)) #R (1/2) by PREPOWER:94,A6,A5
   .=(1+cos.x) #R (1/2) by A6,XCMPLX_1:90;
   hence thesis;
end;

:: a #R x=exp.(x*ln a)
theorem th9:
  a>0 implies exp.(x*log(number_e,a))=a #R x
  proof
    assume A1:a>0;
    then A2:a #R x >0 by PREPOWER:95;
    number_e>0 & number_e<>1 by TAYLOR_1:11;
    then exp.(x*log(number_e,a))
    =exp.(log(number_e,a to_power x)) by POWER:63,A1
    .=exp.(log(number_e,a #R x)) by POWER:def 2,A1
    .=a #R x by TAYLOR_1:15,A2;
    hence thesis;
  end;

:: 15 a #R (-x)=exp.(-x*ln a)
theorem th15:
  a>0 implies exp.(-x*log(number_e,a))=a #R (-x)
  proof
    assume A1:a>0;
    then A2:a #R (-x) >0 by PREPOWER:95;
A3: number_e>0 & number_e<>1 by TAYLOR_1:11;
    exp.(-x*log(number_e,a))
    =exp.((-x)*log(number_e,a))
    .=exp.(log(number_e,a to_power (-x))) by POWER:63,A1,A3
    .=exp.(log(number_e,a #R (-x))) by POWER:def 2,A1
    .=a #R (-x) by TAYLOR_1:15,A2;
    hence thesis;
  end;

:: 40 f.x=-2*(1+cos.x) #R (1/2)
Lm3: sin.x>0 & cos.x<1 & cos.x>-1
     implies sin.x/(1+cos.x) #R (1/2) =(1-cos.x) #R (1/2)
     proof
       assume that A1:sin.x>0 and A2:cos.x<1 and A3:cos.x>-1;
A7:    1-cos.x>1-1 by A2,XREAL_1:17;
A6:    1+cos.x>1+-1 by A3,XREAL_1:10;
       then A5:(1-cos.x)*(1+cos.x)>0 by A7,XREAL_1:131;
       sin.x/(1+cos.x) #R (1/2) 
       =((1-cos.x)*(1+cos.x)) #R (1/2)/(1+cos.x) #R (1/2) by Lm1,A1
       .=(((1-cos.x)*(1+cos.x))/(1+cos.x)) #R (1/2) by PREPOWER:94,A6,A5
       .=(1-cos.x) #R (1/2) by A6,XCMPLX_1:90;
       hence thesis;
     end;

:: 1 f.x=a^2-x^2
theorem th1:
  Z c= dom (f1-f2) & (for x st x in Z holds f1.x=a^2) & f2=#Z 2 implies
  f1-f2 is_differentiable_on Z &
  for x st x in Z holds ((f1-f2)`|Z).x = -2*x
  proof
    assume that A1:Z c= dom (f1-f2) and
A2: (for x st x in Z holds f1.x=a^2) and
A3: f2=#Z 2;
A4: f1+(-1)(#)f2=f1+-f2 by RFUNCT_1:38
    .=f1-f2 by RFUNCT_1:40;
A8: for x st x in Z holds f1.x=a^2+0*x by A2;
    for x st x in Z holds ((f1-f2)`|Z).x = -2*x 
    proof
      let x;
      assume x in Z;
      hence ((f1-f2)`|Z).x =0+2*(-1)*x by A4,A1,A3,FDIFF_4:12,A8
      .=-2*x;
    end;
    hence thesis by A4,A1,A3,FDIFF_4:12,A8;
  end;

:: 2 f.x=(a^2+x^2)/(a^2-x^2)
theorem th2:
  Z c= dom ((f1+f2)/(f1-f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x=a^2 & (f1-f2).x<>0) implies
  (f1+f2)/(f1-f2) is_differentiable_on Z & 
  for x st x in Z holds (((f1+f2)/(f1-f2))`|Z).x = 4*a^2*x/(a^2-x|^2)^2
  proof
    assume that A1:Z c= dom ((f1+f2)/(f1-f2)) and A2:f2=#Z 2 and 
A3: (for x st x in Z holds f1.x=a^2 & (f1-f2).x<>0);
    Z c=dom (f1+f2) /\ (dom (f1-f2) \ (f1-f2)"{0}) by A1,RFUNCT_1:def 4;
    then A4:Z c= dom (f1+f2) & Z c= dom (f1-f2) \ (f1-f2)"{0} by XBOOLE_1:18;
    dom ((f1-f2)^) c= dom (f1-f2) by RFUNCT_1:11;
    then dom (f1-f2) \ (f1-f2)"{0} c= dom (f1-f2) by RFUNCT_1:def 8; 
    then A5:Z c= dom (f1-f2) by XBOOLE_1:1,A4;
A6: for x st x in Z holds f1.x=a^2 by A3;
    then A7:f1+f2 is_differentiable_on Z &
    for x st x in Z holds ((f1+f2)`|Z).x = 2*x by A2,A4,FDIFF_4:17;
A8: f1-f2 is_differentiable_on Z &
    for x st x in Z holds ((f1-f2)`|Z).x = -2*x by th1,A2,A5,A6;
A10: for x st x in Z holds (f1-f2).x <> 0 by A3;
     then A9:(f1+f2)/(f1-f2) is_differentiable_on Z by A7,A8,FDIFF_2:21;
     for x st x in Z holds (((f1+f2)/(f1-f2))`|Z).x=4*a^2*x/(a^2-x|^2)^2
     proof
       let x;
       assume B1:x in Z;
       then A11:f1+f2 is_differentiable_in x by A7,FDIFF_1:16;
A12:   f1-f2 is_differentiable_in x by A8,FDIFF_1:16,B1;
A13:   f2.x=x #Z 2 by A2,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
B2:    (f1+f2).x=f1.x+f2.x by SEQ_1:def 3,A4,B1
       .=a^2+x |^2 by B1,A3,A13;
B3:    (f1-f2).x=f1.x-f2.x by A5,B1,SEQ_1:def 4
       .=a^2-x |^2 by A3,B1,A13;
       (f1-f2).x<>0 by A3,B1;
       then diff((f1+f2)/(f1-f2),x)
       =(diff(f1+f2,x)*(f1-f2).x - diff(f1-f2,x)*(f1+f2).x)/((f1-f2).x)^2 
       by FDIFF_2:14,A11,A12 
       .=(((f1+f2)`|Z).x * (f1-f2).x-diff(f1-f2,x) * (f1+f2).x)/((f1-f2).x)^2 
       by A7,B1,FDIFF_1:def 8
       .= (((f1+f2)`|Z).x * (f1-f2).x-((f1-f2)`|Z).x * (f1+f2).x)/((f1-f2).x)^2 
       by A8,B1,FDIFF_1:def 8
       .=((2*x)*(f1-f2).x-((f1-f2)`|Z).x * (f1+f2).x)/((f1-f2).x)^2 
       by B1,A2,A4,FDIFF_4:17,A6
       .=((2*x)*(f1-f2).x-(-2*x) * (f1+f2).x)/((f1-f2).x)^2 
       by B1,th1,A2,A5,A6
       .=4*a^2*x/(a^2-x|^2)^2 by B2,B3;
       hence thesis by B1,A9,FDIFF_1:def 8;
     end;
     hence thesis by A10,A7,A8,FDIFF_2:21;
   end;

:: 3 f.x=ln((a^2+x^2)/(a^2-x^2))
theorem th3:
  Z c= dom f & f=(log_ number_e)*((f1+f2)/(f1-f2)) & f2=#Z 2 & 
  (for x st x in Z holds f1.x=a^2 & (f1-f2).x>0 & a<>0) 
  implies f is_differentiable_on Z & for x 
  st x in Z holds (f`|Z).x =4*a^2*x/(a|^4-x|^4)
  proof
    assume that A1:Z c= dom f and A2:f=(log_ number_e)*((f1+f2)/(f1-f2)) and 
A3: f2=#Z 2 and A4:for x st x in Z holds f1.x=a^2 & (f1-f2).x>0 & a<>0; 
    for y st y in Z holds y in dom ((f1+f2)/(f1-f2)) by FUNCT_1:21,A1,A2;
    then A5:Z c= dom ((f1+f2)/(f1-f2)) by TARSKI:def 3;
    then Z c=dom (f1+f2) /\ (dom (f1-f2) \ (f1-f2)"{0}) by RFUNCT_1:def 4;
    then A9:Z c= dom (f1+f2) & Z c= dom (f1-f2) \ (f1-f2)"{0} by XBOOLE_1:18;
    dom ((f1-f2)^) c= dom (f1-f2) by RFUNCT_1:11;
    then dom (f1-f2) \ (f1-f2)"{0} c= dom (f1-f2) by RFUNCT_1:def 8; 
    then A15:Z c= dom (f1-f2) by XBOOLE_1:1,A9;
A11: for x st x in Z holds f1.x=a^2 & (f1-f2).x<>0 by A4;
     then A7:(f1+f2)/(f1-f2) is_differentiable_on Z & for x st x in Z holds 
     (((f1+f2)/(f1-f2))`|Z).x = 4*a^2*x/(a^2-x|^2)^2 by th2,A5,A3;
A8:  for x st x in Z holds ((f1+f2)/(f1-f2)).x >0
     proof
       let x;
       assume B1:x in Z;
       then B5:(f1-f2).x>0 by A4;
A10:   f2.x=x #Z 2 by A3,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
B2:    (f1+f2).x=f1.x+f2.x by A9,B1,SEQ_1:def 3
       .=a^2+x |^2 by B1,A4,A10;
       a<>0 by B1,A4;
       then B3:a^2>0 by SQUARE_1:74;
B4:    x |^2 = x^2 by NEWTON:100;
       x |^2>=0 by SQUARE_1:72,B4;
       then B7:a^2+x |^2>0+0 by B3,XREAL_1:10;
       ((f1+f2)/(f1-f2)).x =(f1+f2).x * ((f1-f2).x)" by A5,B1,RFUNCT_1:def 4
       .=(f1+f2).x / (f1-f2).x by XCMPLX_0:def 9;
       hence thesis by B5,B7,B2,XREAL_1:141;
     end;
A12: for x st x in Z holds (log_ number_e)*((f1+f2)/(f1-f2)) 
     is_differentiable_in x
     proof
       let x;
       assume B1:x in Z;
       then A14:(f1+f2)/(f1-f2) is_differentiable_in x by FDIFF_1:16,A7;
       ((f1+f2)/(f1-f2)).x >0 by A8,B1;
       hence (log_ number_e)*((f1+f2)/(f1-f2)) is_differentiable_in x 
       by TAYLOR_1:20,A14;
     end;
     then A:f is_differentiable_on Z by A1,A2,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x = 4*a^2*x/(a|^4-x|^4) 
     proof
       let x;
       assume B1:x in Z;
       then A14:(f1+f2)/(f1-f2) is_differentiable_in x by FDIFF_1:16,A7;
A10:   f2.x=x #Z 2 by A3,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46;
B2:    (f1+f2).x=f1.x+f2.x by A9,B1,SEQ_1:def 3
       .=a^2+x |^2 by B1,A4,A10;
B3:    (f1-f2).x=f1.x-f2.x by SEQ_1:def 4,A15,B1
       .=a^2-x |^2 by A4,B1,A10;
A16:   ((f1+f2)/(f1-f2)).x =(f1+f2).x * ((f1-f2).x)" by RFUNCT_1:def 4,A5,B1
       .=(a^2+x |^2)/(a^2-x |^2) by B2,B3,XCMPLX_0:def 9;
B4:    (a^2)^2=(a|^2)*(a^2) by NEWTON:100
       .=(a|^2)*(a|^2) by NEWTON:100
       .=(a|^2)|^2 by WSIERP_1:2
       .=a|^(2*2) by NEWTON:14
       .=a|^4;
B5:    (x |^2)^2=(x |^2)|^2 by WSIERP_1:2
       .=x|^(2*2) by NEWTON:14
       .=x|^4;
A17:   a^2-x |^2>0 by B1,A4,B3;
       ((f1+f2)/(f1-f2)).x >0 by A8,B1;
       then diff((log_ number_e)*((f1+f2)/(f1-f2)),x) 
       =diff(((f1+f2)/(f1-f2)),x)/(((f1+f2)/(f1-f2)).x) by TAYLOR_1:20,A14
       .=(((f1+f2)/(f1-f2))`|Z).x/(((f1+f2)/(f1-f2)).x) by A7,B1,FDIFF_1:def 8
       .= (4*a^2*x/(a^2-x|^2)^2)/((a^2+x |^2)/(a^2-x |^2))
         by A16,A11,th2,A5,A3,B1
       .=(4*a^2*x/(a^2-x|^2)/(a^2-x|^2))/((a^2+x |^2)/(a^2-x |^2))
       by XCMPLX_1:79
       .=(4*a^2*x/(a^2-x|^2))/((a^2+x |^2)/(a^2-x |^2))/(a^2-x|^2)
       by XCMPLX_1:48
       .=(4*a^2*x)/(a^2+x |^2)/(a^2-x|^2) by A17,XCMPLX_1:55
       .=(4*a^2*x)/((a^2+x |^2)*(a^2-x|^2)) by XCMPLX_1:79
       .=4*a^2*x/(a|^4-x|^4) by B4,B5;
       hence thesis by B1,A,A2,FDIFF_1:def 8;
     end;
     hence thesis by A12,A1,A2,FDIFF_1:16;
   end;

:: 4 f.x=(1/(4*a^2))*ln((a^2+x^2)/(a^2-x^2))
theorem 
  Z c= dom ((1/(4*a^2))(#)f) & f=(log_ number_e)*((f1+f2)/(f1-f2)) & f2=#Z 2 & 
  (for x st x in Z holds f1.x=a^2 & (f1-f2).x>0 & a<>0) 
  implies (1/(4*a^2))(#)f is_differentiable_on Z & for x 
  st x in Z holds (((1/(4*a^2))(#)f)`|Z).x =x/(a|^4-x|^4)
  proof
    assume that A1:Z c= dom ((1/(4*a^2))(#)f) and 
A2: f=(log_ number_e)*((f1+f2)/(f1-f2)) and 
A3: f2=#Z 2 and 
A4: (for x st x in Z holds f1.x=a^2 & (f1-f2).x>0 & a<>0);
A5: Z c= dom f by SEQ_1:def 6,A1;
    then A7:f is_differentiable_on Z & for x 
    st x in Z holds (f`|Z).x =4*a^2*x/(a|^4-x|^4) by th3,A2,A3,A4;
    for x st x in Z holds (((1/(4*a^2))(#)f)`|Z).x =x/(a|^4-x|^4)
    proof
      let x;
      assume B1:x in Z;
      then a<>0 by A4;
      then a^2>0 by SQUARE_1:74;
      then B2:4*a^2>4*0 by XREAL_1:70;
      (((1/(4*a^2))(#)f)`|Z).x 
      =(1/(4*a^2))*diff(f,x) by A7,A1,FDIFF_1:28,B1
      .=(1/(4*a^2))*(f`|Z).x by B1,A7,FDIFF_1:def 8
      .=(1/(4*a^2))*(4*a^2*x/(a|^4-x|^4)) by th3,A2,A3,A4,A5,B1
      .=(1/(4*a^2))*((4*a^2)*(x/(a|^4-x|^4))) by XCMPLX_1:75
      .=(x/(a|^4-x|^4))*(1/(4*a^2)*(4*a^2))
      .=x/(a|^4-x|^4) by XCMPLX_1:109,B2;
      hence thesis;
    end;
    hence thesis by A1,FDIFF_1:28,A7;
  end;

:: 5 f.x=x^2/(1+x^2)
theorem th5:
  Z c= dom (f1/(f2+f1)) & f1=#Z 2 & (for x st x in Z holds f2.x=1 & x<>0)
  implies f1/(f2+f1) is_differentiable_on Z & for x
  st x in Z holds ((f1/(f2+f1))`|Z).x =2*x/(1+x^2)^2
  proof
    assume that A1:Z c= dom (f1/(f2+f1)) and A2:f1=#Z 2 and
A3: for x st x in Z holds f2.x=1 & x<>0;
A6: for x st x in Z holds f2.x=1^2 by A3;
    Z c= dom f1 /\ (dom (f2+f1) \ (f2+f1)"{0}) by RFUNCT_1:def 4,A1;
    then A4:Z c= dom f1 & Z c= dom (f2+f1) \ (f2+f1)"{0} by XBOOLE_1:18;
    dom ((f2+f1)^) c= dom (f2+f1) by RFUNCT_1:11;
    then dom (f2+f1) \ (f2+f1)"{0} c= dom (f2+f1) by RFUNCT_1:def 8; 
    then A5:Z c= dom (f2+f1) by XBOOLE_1:1,A4;
    then A7:f2+f1 is_differentiable_on Z &
    for x st x in Z holds ((f2+f1)`|Z).x = 2*x by A2,A6,FDIFF_4:17;
    for x st x in Z holds f1 is_differentiable_in x by A2,TAYLOR_1:2;
    then A8:f1 is_differentiable_on Z by A4,FDIFF_1:16;
A10: for x st x in Z holds (f1`|Z).x=2*x
     proof 
       let x;
       assume B1:x in Z;
       2 * (x #Z (2-1))=2 * x by PREPOWER:45;
       then diff(f1,x) =2 * x by TAYLOR_1:2,A2;
       hence thesis by A8,B1,FDIFF_1:def 8;
     end;
A15: for x st x in Z holds (f2+f1).x <> 0
     proof
       let x;
       assume B1:x in Z;
       then B3:(f2+f1).x=f2.x+f1.x by A5,SEQ_1:def 3
       .=1+f1.x by B1,A3
       .=1+x #Z 2 by A2,TAYLOR_1:def 1
       .=1+x |^2 by PREPOWER:46
       .=1+x^2 by NEWTON:100;
       x^2 >=0 by SQUARE_1:72;
       then 1+x^2>0+0 by XREAL_1:10;
       hence thesis by B3;
     end;
A16: f1/(f2+f1) is_differentiable_on Z by A7,A8,A15,FDIFF_2:21;
     for x st x in Z holds ((f1/(f2+f1))`|Z).x =2*x/(1+x^2)^2
     proof
       let x;
       assume B1:x in Z;
A11:   f1 is_differentiable_in x by A2,TAYLOR_1:2;
A12:   f2+f1 is_differentiable_in x by A7,FDIFF_1:16,B1;
A13:   f1.x=x #Z 2 by A2,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46
       .=x^2 by NEWTON:100;
B2:    (f2+f1).x=f2.x+f1.x by A5,B1,SEQ_1:def 3
       .=1+f1.x by B1,A3
       .=1+x #Z 2 by A2,TAYLOR_1:def 1
       .=1+x |^2 by PREPOWER:46
       .=1+x^2 by NEWTON:100;
       (f2+f1).x<>0 by A15,B1;
       then diff(f1/(f2+f1),x)
       =(diff(f1,x)*(f2+f1).x - diff(f2+f1,x)*f1.x)/((f2+f1).x)^2 
       by FDIFF_2:14,A11,A12 
       .=((f1`|Z).x * (f2+f1).x-diff(f2+f1,x) * f1.x)/((f2+f1).x)^2 
       by A8,B1,FDIFF_1:def 8
       .= ((f1`|Z).x * (f2+f1).x-((f2+f1)`|Z).x * f1.x)/((f2+f1).x)^2 
       by A7,B1,FDIFF_1:def 8
       .=((2*x)*(f2+f1).x-((f2+f1)`|Z).x * f1.x)/((f2+f1).x)^2 
       by B1,A10
       .=((2*x)*(1+x^2)-(2*x) * x^2)/(1+x^2)^2 by B1,A5,A2,A6,FDIFF_4:17,B2,A13
       .=2*x/(1+x^2)^2;
       hence thesis by B1,A16,FDIFF_1:def 8;
     end;
     hence thesis by A7,A8,A15,FDIFF_2:21;
   end;

:: 6 f.x=(1/2)*ln(x^2/(1+x^2))
theorem 
  Z c= dom ((1/2)(#)f) & f=(log_ number_e)*(f1/(f2+f1)) & f1=#Z 2 & 
  (for x st x in Z holds f2.x=1 & x<>0)
  implies (1/2)(#)f is_differentiable_on Z & for x st x in Z holds
  (((1/2)(#)f)`|Z).x =1/(x*(1+x^2))
  proof
    assume that A1:Z c= dom ((1/2)(#)f) and
A2: f=(log_ number_e)*(f1/(f2+f1)) and
A3: f1=#Z 2 and
A4: (for x st x in Z holds f2.x=1 & x<>0);
A5: Z c= dom f by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom (f1/(f2+f1)) by FUNCT_1:21,A2,A5;
    then A6:Z c= dom (f1/(f2+f1)) by TARSKI:def 3;
    then A7:f1/(f2+f1) is_differentiable_on Z & for x 
    st x in Z holds ((f1/(f2+f1))`|Z).x =2*x/(1+x^2)^2 by A3,A4,th5;
    Z c= dom f1 /\ (dom (f2+f1) \ (f2+f1)"{0}) by RFUNCT_1:def 4,A6;
    then A8:Z c= dom f1 & Z c= dom (f2+f1) \ (f2+f1)"{0} by XBOOLE_1:18;
    dom ((f2+f1)^) c= dom (f2+f1) by RFUNCT_1:11;
    then dom (f2+f1) \ (f2+f1)"{0} c= dom (f2+f1) by RFUNCT_1:def 8; 
    then A9:Z c= dom (f2+f1) by XBOOLE_1:1,A8;
A10: for x st x in Z holds (f1/(f1+f2)).x >0
     proof
       let x;
       assume B1:x in Z;
B2:    f1.x=x #Z 2 by A3,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46
       .=x^2 by NEWTON:100;
B3:    (f2+f1).x=f2.x+f1.x by A9,B1,SEQ_1:def 3
       .=1+x^2 by B2,B1,A4;
       x<>0 by B1,A4;
       then B5:f1.x>0 by B2,SQUARE_1:74;
       then B8:1+x^2>0+0 by B2,XREAL_1:10;
       (f1/(f2+f1)).x=f1.x * ((f2+f1).x)" by RFUNCT_1:def 4,A6,B1
       .=f1.x/(f2+f1).x by XCMPLX_0:def 9;
       hence thesis by B5,B8,B3,XREAL_1:141;
     end;
     for x st x in Z holds (log_ number_e)*(f1/(f2+f1)) 
     is_differentiable_in x
     proof
       let x;
       assume B1:x in Z;
       then A13:f1/(f2+f1) is_differentiable_in x by FDIFF_1:16,A7;
       (f1/(f1+f2)).x >0 by A10,B1;
       hence (log_ number_e)*(f1/(f2+f1)) is_differentiable_in x
       by TAYLOR_1:20,A13;
     end;
     then A:f is_differentiable_on Z by A5,A2,FDIFF_1:16;
     for x st x in Z holds (((1/2)(#)f)`|Z).x =1/(x*(1+x^2))
     proof
       let x;
       assume B1:x in Z;
B2:    f1.x=x #Z 2 by A3,TAYLOR_1:def 1
       .=x |^2 by PREPOWER:46
       .=x^2 by NEWTON:100;
B3:    (f2+f1).x=f2.x+f1.x by A9,B1,SEQ_1:def 3
       .=1+x^2 by B2,B1,A4;
B4:    (f1/(f2+f1)).x=f1.x * ((f2+f1).x)" by RFUNCT_1:def 4,A6,B1
       .=x^2/(1+x^2) by B2,B3,XCMPLX_0:def 9;
A13:   f1/(f2+f1) is_differentiable_in x by FDIFF_1:16,A7,B1;
A14:   (f1/(f1+f2)).x >0 by A10,B1;
A16:   x<>0 by B1,A4;
       then x^2>0 by SQUARE_1:74;
       then A15:1+x^2>0+0 by XREAL_1:10;
       (((1/2)(#)f)`|Z).x 
       =(1/2)*diff((log_ number_e)*(f1/(f2+f1)),x) by A2,A,A1,FDIFF_1:28,B1
       .=(1/2)*(diff((f1/(f2+f1)),x)/(f1/(f2+f1)).x) by TAYLOR_1:20,A13,A14
       .=(1/2)*(((f1/(f2+f1))`|Z).x/(f1/(f2+f1)).x) by B1,FDIFF_1:def 8,A7
       .=(1/2)*((2*x/(1+x^2)^2)/(x^2/(1+x^2))) by B4,A6,A3,A4,th5,B1
       .=(1/2)*((2*x)/(1+x^2)^2)/(x^2/(1+x^2)) by XCMPLX_1:75
       .=((1/2)*(2*x))/(1+x^2)^2/(x^2/(1+x^2)) by XCMPLX_1:75
       .=x/(1+x^2)/(1+x^2)/(x^2/(1+x^2)) by XCMPLX_1:79
       .=x/(1+x^2)/(x^2/(1+x^2))/(1+x^2) by XCMPLX_1:48
       .=x/x^2/(1+x^2) by A15,XCMPLX_1:55
       .=x/x/x/(1+x^2) by XCMPLX_1:79
       .=1/x/(1+x^2) by XCMPLX_1:60,A16
       .=1/(x*(1+x^2)) by XCMPLX_1:79;
       hence thesis;
     end;
     hence thesis by A,A1,FDIFF_1:28;
   end;

:: 7 f.x=ln(x|^n)
theorem 
  Z c= dom ((log_ number_e)*( #Z n)) & (for x st x in Z holds x>0)
  implies (log_ number_e)*( #Z n) is_differentiable_on Z & for x 
  st x in Z holds (((log_ number_e)*( #Z n))`|Z).x =n/x
  proof
    assume that A1:Z c= dom ((log_ number_e)*( #Z n)) and 
A2: for x st x in Z holds x > 0;
A3: for x st x in Z holds x #Z n > 0 
    proof
      let x;
      assume x in Z;
      then x>0 by A2;
      hence x #Z n > 0 by PREPOWER:49;
    end;
A8: for x st x in Z holds (log_ number_e)*( #Z n) is_differentiable_in x
    proof
      let x;
      assume B1:x in Z;
A4:   #Z n is_differentiable_in x by TAYLOR_1:2;
      #Z n.x=x #Z n by TAYLOR_1:def 1;
      then #Z n.x>0 by A3,B1;
      hence (log_ number_e)*( #Z n) is_differentiable_in x 
      by TAYLOR_1:20,A4;
    end;
    then A:(log_ number_e)*( #Z n) is_differentiable_on Z 
    by A1,FDIFF_1:16;
    for x st x in Z holds (((log_ number_e)*( #Z n))`|Z).x =n/x
    proof
      let x;
      assume B1:x in Z;
      then A6:x>0 by A2;
      then A7:x |^n >0 by HEINE:5;
A4:   #Z n is_differentiable_in x &
      diff(( #Z n),x) = n * x #Z (n-1) by TAYLOR_1:2;
A5:   #Z n.x=x #Z n by TAYLOR_1:def 1;
      then #Z n.x>0 by A3,B1;
      then diff((log_ number_e)*( #Z n),x) 
      = n * x #Z (n-1)/x #Z n by A5,TAYLOR_1:20,A4
      .=n * x #Z (n-1)/x |^ n by PREPOWER:46
      .=n * (x |^ n/x |^ 1)/x |^ n by PREPOWER:53,A6
      .=n * (x |^ n/x |^ 1/x |^ n) by XCMPLX_1:75
      .=n * (x |^ n/x |^ n/x |^ 1) by XCMPLX_1:48
      .=n * (1/(x |^ 1)) by A7,XCMPLX_1:60
      .=n * (1/x) by NEWTON:10
      .=n*1/x by XCMPLX_1:75
      .=n/x;
      hence thesis by A,B1,FDIFF_1:def 8;
    end;
    hence thesis by A8,A1,FDIFF_1:16;
  end;

:: 8 f.x=1/x+ln((x-1)/x)
theorem 
  Z c= dom (f2^+(log_ number_e)*(f1/f2)) & (for x st x in Z holds f2.x = x 
  & f2.x>0 & f1.x=x-1 & f1.x >0 ) implies f2^+(log_ number_e)*(f1/f2) 
  is_differentiable_on Z & for x st x in Z holds
  ( (f2^+(log_ number_e)*(f1/f2))`|Z).x= 1/(x^2*(x-1))
  proof
    assume that A1:Z c= dom (f2^+(log_ number_e)*(f1/f2)) and 
A2: for x st x in Z holds f2.x = x & f2.x>0 & f1.x=x-1 & f1.x >0;
A4: Z c= dom (f2^) /\ dom ((log_ number_e)*(f1/f2)) by SEQ_1:def 3,A1;
    then A0:Z c= dom (f2^) by XBOOLE_1:18;
    dom (f2^) c= dom f2 by RFUNCT_1:11;
    then A3:Z c= dom f2 by A0,XBOOLE_1:1;
A9: for x st x in Z holds f2.x = 0+x & f2.x<>0 by A2;
    then A6:f2^ is_differentiable_on Z & for x st x in Z holds
    ((f2^)`|Z).x= -1/(0+x)^2 by A3,FDIFF_4:14;
A5: Z c=dom ((log_ number_e)*(f1/f2)) by XBOOLE_1:18,A4;
A10: for x st x in Z holds f1.x=x-1 & f1.x >0 & f2.x = x-0 & f2.x>0 by A2;
     then A7:(log_ number_e)*(f1/f2) is_differentiable_on Z & for x st x in Z
     holds (((log_ number_e)*(f1/f2))`|Z).x=(1-0)/((x-1)*(x-0))
       by A5,FDIFF_4:24;
     for x st x in Z holds
     ((f2^+(log_ number_e)*(f1/f2))`|Z).x= 1/(x^2*(x-1))
     proof
       let x;
       assume B1:x in Z;
       then B3:f2.x = x & f2.x>0 & f1.x=x-1 & f1.x >0 by A2;
       ((f2^+(log_ number_e)*(f1/f2))`|Z).x
       =diff(f2^,x) + diff((log_ number_e)*(f1/f2),x) by FDIFF_1:26,A6,A7,A1,B1
       .=((f2^)`|Z).x+ diff((log_ number_e)*(f1/f2),x) by A6,B1,FDIFF_1:def 8
       .=((f2^)`|Z).x+(((log_ number_e)*(f1/f2))`|Z).x by A7,B1,FDIFF_1:def 8
       .= -1/(0+x)^2+(((log_ number_e)*(f1/f2))`|Z).x by B1,A9,A3,FDIFF_4:14
       .=-1/(0+x)^2+(1-0)/((x-1)*(x-0)) by B1,A10,A5,FDIFF_4:24
       .=-(1*(x-1))/(x^2*(x-1))+1/((x-1)*x) by B3,XCMPLX_1:92
       .=-(1*(x-1))/(x^2*(x-1))+(1*x)/((x-1)*x*x) by B3,XCMPLX_1:92
       .=(-(x-1))/(x^2*(x-1))+x/(x^2*(x-1)) by XCMPLX_1:188
       .=((-x+1)+x)/(x^2*(x-1)) by XCMPLX_1:63
       .=1/(x^2*(x-1));
       hence thesis;
     end;
     hence thesis by FDIFF_1:26,A6,A7,A1;
   end;

:: 10 f.x=a #R x=exp.(x*ln a)
theorem th10:
  Z c= dom (exp*f) & (for x st x in Z holds f.x=x*log(number_e,a)) &
  a>0 implies
  exp*f is_differentiable_on Z &
  for x st x in Z holds ((exp*f)`|Z).x =a #R x*log(number_e,a)
  proof 
    assume that A1:Z c= dom (exp*f) and
A2: for x st x in Z holds f.x=x*log(number_e,a) and
A5: a>0;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then B1:Z c= dom f by TARSKI:def 3;
A6: for x st x in Z holds f.x = log(number_e,a)*x+0 by A2;
    then A3:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x =log(number_e,a) by FDIFF_1:31,B1;
A7: for x st x in Z holds exp*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A3;
      hence exp*f is_differentiable_in x by TAYLOR_1:19;
    end;
    then A:exp*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((exp*f)`|Z).x = a #R x*log(number_e,a)
    proof
      let x;
      assume A4:x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A3;
      then diff(exp*f,x) = exp.(f.x)*diff(f,x) by TAYLOR_1:19
      .=exp.(f.x)*(f`|Z).x by FDIFF_1:def 8,A4,A3
      .=exp.(f.x)*(log(number_e,a)) by A6,FDIFF_1:31,B1,A4
      .=exp.(x*log(number_e,a))*(log(number_e,a)) by A4,A2
      .=a #R x*log(number_e,a) by A5,th9;
      hence thesis by A4,A,FDIFF_1:def 8;    
    end;
    hence thesis by A7,A1,FDIFF_1:16;
  end;

:: 11 f.x=(a #R x)*(x-1/ln a)/ln a
theorem
  Z c= dom ((1/log(number_e,a))(#)((exp*f1)(#)f2)) &
  (for x st x in Z holds f1.x=x*log(number_e,a) & f2.x=x-1/log(number_e,a)) &
  a>0 & a<>1 implies
  (1/log(number_e,a))(#)((exp*f1)(#)f2) is_differentiable_on Z &
  for x st x in Z holds
  (((1/log(number_e,a))(#)((exp*f1)(#)f2))`|Z).x =x * a #R x
  proof
    assume that A1:Z c= dom ((1/log(number_e,a))(#)((exp*f1)(#)f2)) and
A2: (for x st x in Z holds f1.x=x*log(number_e,a) & f2.x=x-1/log(number_e,a)) 
    & a>0 & a<>1;
A3: (for x st x in Z holds f1.x=x*log(number_e,a)) & a>0 by A2;
A4: Z c= dom ((exp*f1)(#)f2) by SEQ_1:def 6,A1;
    then Z c= dom (exp*f1) /\ dom f2 by SEQ_1:def 5;
    then A5:Z c= dom (exp*f1) & Z c= dom f2 by XBOOLE_1:18;
    then A6:exp*f1 is_differentiable_on Z & for x st x in Z holds 
    ((exp*f1)`|Z).x =a #R x*log(number_e,a) by A3,th10;
A10: for x st x in Z holds f2.x = 1*x+-1/log(number_e,a)
     proof let x;
       assume B1:x in Z;
       1*x+-1/log(number_e,a)=1*x-1/log(number_e,a);
       hence thesis by A2,B1;
     end;
     then A7:f2 is_differentiable_on Z & for x st x in Z holds 
     (f2`|Z).x=1 by FDIFF_1:31,A5;
     then A8:((exp*f1)(#)f2) is_differentiable_on Z by A4,A6,FDIFF_1:29;
A9:  log(number_e,a)<>0
     proof
       assume log(number_e,a)=0;
       then B2:log(number_e,a)=log(number_e,1) by SIN_COS2:13,TAYLOR_1:13;
B3:    number_e>0 & number_e<>1 by TAYLOR_1:11;
       then a=(number_e) to_power log(number_e,1) by B2,POWER:def 3,A2
       .=1 by POWER:def 3,B3;
       hence contradiction by A2;
     end;
     for x st x in Z holds
     (((1/log(number_e,a))(#)((exp*f1)(#)f2))`|Z).x =x * a #R x
     proof
       let x;
       assume B1:x in Z;
       then B2:(exp*f1).x=exp.(f1.x) by A5,FUNCT_1:22
       .=exp.(x*log(number_e,a)) by B1,A2
       .=a #R x by A2,th9;
       (((1/log(number_e,a))(#)((exp*f1)(#)f2))`|Z).x 
       =(1/log(number_e,a))*diff((exp*f1)(#)f2,x) by A1,A8,FDIFF_1:28,B1
       .=(1/log(number_e,a))*(((exp*f1)(#)f2)`|Z).x by FDIFF_1:def 8,B1,A8
       .=(1/log(number_e,a))*(f2.x*diff(exp*f1,x)+(exp*f1).x*diff(f2,x))
       by B1,A4,A6,A7,FDIFF_1:29
       .=(1/log(number_e,a))*(f2.x*((exp*f1)`|Z).x+(exp*f1).x*diff(f2,x))
       by A6,B1,FDIFF_1:def 8
       .=(1/log(number_e,a))*(f2.x*((exp*f1)`|Z).x+(exp*f1).x*(f2`|Z).x)
       by A7,B1,FDIFF_1:def 8
       .=(1/log(number_e,a))*(f2.x*(a #R x*log(number_e,a))
       +(exp*f1).x*(f2`|Z).x) by A5,A3,th10,B1
       .=(1/log(number_e,a))*(f2.x*(a #R x*log(number_e,a))+(exp*f1).x*1) 
       by A10,FDIFF_1:31,A5,B1
       .=(1/log(number_e,a))*((f2.x*log(number_e,a)+1)*a #R x) by B2
       .=(1/log(number_e,a))*(((x-1/log(number_e,a))*log(number_e,a)+1)*a #R x)
       by A2,B1
       .=(1/log(number_e,a))*(x*log(number_e,a)
       -1/log(number_e,a)*log(number_e,a)+1)*a #R x 
       .=(1/log(number_e,a))*(x*log(number_e,a)-1+1)*a #R x by A9,XCMPLX_1:107
       .=1/log(number_e,a)*log(number_e,a)*x*a #R x
       .=1*x*a #R x by A9,XCMPLX_1:107
       .=x*a #R x;
       hence thesis;
     end;
     hence thesis by A1,FDIFF_1:28,A8;
   end;

:: 12 f.x=a #R x*exp(x)/(1+ln a)
theorem 
  Z c= dom ((1/(1+log(number_e,a)))(#)((exp*f)(#)exp)) & 
  (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 & a<>1/number_e
  implies 
  (1/(1+log(number_e,a)))(#)((exp*f)(#)exp) is_differentiable_on Z &
  for x st x in Z holds 
  (((1/(1+log(number_e,a)))(#)((exp*f)(#)exp))`|Z).x =a #R x*exp.x
  proof
    assume that A1:Z c= dom ((1/(1+log(number_e,a)))(#)((exp*f)(#)exp)) and
A2: (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 and
A8: a<>1/number_e;
A3: Z c= dom ((exp*f)(#)exp) by SEQ_1:def 6,A1;
    then Z c= dom (exp*f) /\ dom exp by SEQ_1:def 5;
    then A5:Z c= dom (exp*f) & Z c= dom exp by XBOOLE_1:18;
    then A6:exp*f is_differentiable_on Z & for x st x in Z holds 
    ((exp*f)`|Z).x =a #R x*log(number_e,a) by A2,th10;
A4: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A7:(exp*f)(#)exp is_differentiable_on Z by A3,A6,FDIFF_1:29;
A9: 1+log(number_e,a)<>0
    proof
      assume B1:1+log(number_e,a)=0;
B2:   number_e>0 & number_e<>1 by TAYLOR_1:11;
B6:   number_e*a>0*a by TAYLOR_1:11,XREAL_1:70,A2;
B4:   log(number_e,1)=0 by TAYLOR_1:13,SIN_COS2:13
      .=log(number_e,number_e)+log(number_e,a) by B1,POWER:60,B2
      .=log(number_e,number_e*a) by POWER:61,B2,A2;
      number_e*a=(number_e) to_power log(number_e,1) by B4,POWER:def 3,B2,B6
      .=1 by POWER:def 3,B2;
      hence contradiction by A8,XCMPLX_1:74;
    end;
    for x st x in Z holds
    (((1/(1+log(number_e,a)))(#)((exp*f)(#)exp))`|Z).x =a #R x*exp.x
    proof
      let x;
      assume B1:x in Z;
      then B2:(exp*f).x=exp.(f.x) by A5,FUNCT_1:22
      .=exp.(x*log(number_e,a)) by B1,A2
      .=a #R x by A2,th9;
      (((1/(1+log(number_e,a)))(#)((exp*f)(#)exp))`|Z).x 
      =(1/(1+log(number_e,a)))*diff((exp*f)(#)exp,x) by A1,A7,FDIFF_1:28,B1
      .=(1/(1+log(number_e,a)))*(((exp*f)(#)exp)`|Z).x by FDIFF_1:def 8,B1,A7
      .=(1/(1+log(number_e,a)))*(exp.x*diff(exp*f,x)+(exp*f).x*diff(exp,x))
      by B1,A4,A6,A3,FDIFF_1:29
      .=(1/(1+log(number_e,a)))*(exp.x*((exp*f)`|Z).x+(exp*f).x*diff(exp,x))
      by A6,B1,FDIFF_1:def 8
      .=(1/(1+log(number_e,a)))*(exp.x*((exp*f)`|Z).x+(exp*f).x*exp.x) 
      by TAYLOR_1:16
      .=(1/(1+log(number_e,a)))*((((exp*f)`|Z).x+(exp*f).x)*exp.x) 
      .=(1/(1+log(number_e,a)))*((a #R x*log(number_e,a)+(exp*f).x)*exp.x)
      by A5,A2,th10,B1
      .=(1/(1+log(number_e,a)))*(log(number_e,a)+1)*a #R x*exp.x by B2
      .=1*a #R x*exp.x by A9,XCMPLX_1:107
      .=a #R x*exp.x;
      hence thesis;
    end;
    hence thesis by A7,A1,FDIFF_1:28;
  end;

:: 13 f.x=exp(-x)
theorem th13:
  Z c= dom (exp*f) & (for x st x in Z holds f.x=-x) 
  implies exp*f is_differentiable_on Z &
  for x st x in Z holds ((exp*f)`|Z).x = -exp(-x)
  proof
    assume that A1:Z c= dom (exp*f) and
A2: for x st x in Z holds f.x=-x;
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A3:Z c= dom f by TARSKI:def 3;
A5: for x st x in Z holds f.x=(-1)*x+0 
    proof 
      let x;
      assume x in Z;
      then f.x=-x by A2
      .=(-1)*x+0;
      hence thesis;
    end;
    then A4:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = -1 by FDIFF_1:31,A3; 
A6: for x st x in Z holds exp*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A4;
      hence exp*f is_differentiable_in x by TAYLOR_1:19;
    end;
    then A:exp*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((exp*f)`|Z).x = -exp(-x)
    proof
      let x;
      assume B1:x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A4;
      then diff(exp*f,x) = exp.(f.x)*diff(f,x) by TAYLOR_1:19
      .=exp.(f.x)*(f`|Z).x by FDIFF_1:def 8,B1,A4
      .=exp.(f.x)*(-1) by A5,FDIFF_1:31,A3,B1
      .=exp.(-x)*(-1) by B1,A2
      .=-exp.(-x)
      .=-exp(-x) by SIN_COS:def 27;
      hence thesis by B1,A,FDIFF_1:def 8;    
    end;
    hence thesis by A6,A1,FDIFF_1:16;
  end;

:: 14 f.x=-(x+1)*exp(-x)
theorem 
  Z c= dom (f1(#)(exp*f2)) & (for x st x in Z holds f1.x=-x-1 & f2.x=-x) 
  implies f1(#)(exp*f2) is_differentiable_on Z &
  for x st x in Z holds ((f1(#)(exp*f2))`|Z).x = x/exp(x)
  proof
    assume that A1:Z c= dom (f1(#)(exp*f2)) and 
A2: (for x st x in Z holds f1.x=-x-1 & f2.x=-x);
A6: for x st x in Z holds f2.x=-x by A2;
    Z c= dom f1 /\ dom (exp*f2) by SEQ_1:def 5,A1;
    then A3:Z c= dom f1 & Z c= dom (exp*f2) by XBOOLE_1:18;
A7: for x st x in Z holds f1.x=(-1)*x+-1
    proof 
      let x;
      assume x in Z;
      then f1.x=-x-1 by A2
      .=(-1)*x+-1;
      hence thesis;
    end;
    then A4:f1 is_differentiable_on Z & 
    for x st x in Z holds (f1`|Z).x = -1 by FDIFF_1:31,A3; 
A5: exp*f2 is_differentiable_on Z &
    for x st x in Z holds ((exp*f2)`|Z).x = -exp(-x) by A6,A3,th13;
    for x st x in Z holds ((f1(#)(exp*f2))`|Z).x = x/exp(x)
    proof
      let x;
      assume B1:x in Z;
      then B2:(exp*f2).x=exp.(f2.x) by A3,FUNCT_1:22
      .=exp.(-x) by B1,A2
      .=exp(-x) by SIN_COS:def 27;
      ((f1(#)(exp*f2))`|Z).x 
      =((exp*f2).x)*diff(f1,x) + (f1.x)*diff(exp*f2,x)
        by A1,A5,A4,FDIFF_1:29,B1
      .=exp(-x)*diff(f1,x) + (-x-1)*diff(exp*f2,x) by B2,B1,A2
      .=exp(-x)*(f1`|Z).x + (-x-1)*diff(exp*f2,x) by B1,A4,FDIFF_1:def 8
      .=exp(-x)*(f1`|Z).x + (-x-1)*((exp*f2)`|Z).x by B1,A5,FDIFF_1:def 8
      .=exp(-x)*(-1) + (-x-1)*((exp*f2)`|Z).x by A7,FDIFF_1:31,A3,B1
      .=exp(-x)*(-1) + (-x-1)*(-exp(-x)) by A6,A3,th13,B1
      .=x*exp(-x) 
      .=x*(1/exp(x)) by TAYLOR_1:4
      .=x/exp(x) by XCMPLX_1:100;
      hence thesis;
    end;
    hence thesis by A1,A4,FDIFF_1:29,A5;
  end;

:: 16 f.x=a #R (-x)=exp.(-x*ln a)
theorem th16:
  Z c= dom -(exp*f) & (for x st x in Z holds f.x=-x*log(number_e,a)) 
  & a>0 implies
  -(exp*f) is_differentiable_on Z &
  for x st x in Z holds ((-(exp*f))`|Z).x =a #R (-x)*log(number_e,a)
  proof 
    assume that A1:Z c= dom -(exp*f) and 
A2: (for x st x in Z holds f.x=-x*log(number_e,a)) and 
A3: a>0;
A4: Z c= dom (exp*f) by A1,SEQ_1:def 7;
    for y st y in Z holds y in dom f by FUNCT_1:21,A4;
    then A5:Z c= dom f by TARSKI:def 3;
A7: for x st x in Z holds f.x = (-log(number_e,a))*x+0 
    proof
      let x;
      assume x in Z;
      then f.x=-log(number_e,a)*x by A2
      .=(-log(number_e,a))*x+0;
      hence thesis;
    end;
    then A6:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x =-log(number_e,a) by FDIFF_1:31,A5;
    for x st x in Z holds exp*f is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then f is_differentiable_in x by FDIFF_1:16,A6;
      hence exp*f is_differentiable_in x by TAYLOR_1:19;
    end;
    then A:exp*f is_differentiable_on Z by A4,FDIFF_1:16;
B2: Z c= dom ((-1)(#)(exp*f)) by A1,RFUNCT_1:38;
    then A8:(-1)(#)(exp*f) is_differentiable_on Z & for x st x in Z holds 
    (((-1)(#)(exp*f))`|Z).x=(-1)*diff((exp*f),x) by FDIFF_1:28,A;
    for x st x in Z holds ((-(exp*f))`|Z).x = a #R (-x)*log(number_e,a)
    proof
      let x;
      assume B1:x in Z;
      then B3:f is_differentiable_in x by FDIFF_1:16,A6;
      ((-(exp*f))`|Z).x=(((-1)(#)(exp*f))`|Z).x by RFUNCT_1:38
      .=(-1)*diff(exp*f,x) by B1,FDIFF_1:28,B2,A
      .=(-1)*(exp.(f.x)*diff(f,x)) by TAYLOR_1:19,B3
      .=(-1)*(exp.(f.x)*(f`|Z).x ) by FDIFF_1:def 8,B1,A6
      .=(-1)*(exp.(f.x)*(-log(number_e,a))) by A7,FDIFF_1:31,A5,B1
      .=(-1)*(exp.(-x*log(number_e,a))*(-log(number_e,a))) by B1,A2
      .=(-1)*(a #R (-x)*(-log(number_e,a))) by A3,th15
      .=a #R (-x)*log(number_e,a);
      hence thesis;
    end;
    hence thesis by RFUNCT_1:38,A8;
  end;

:: 17 f.x=-(a #R -x)*(x+1/ln a)/ln a
theorem 
  Z c= dom ((1/log(number_e,a))(#)((-(exp*f1))(#)f2)) & 
  (for x st x in Z holds f1.x=-x*log(number_e,a) & f2.x=x+1/log(number_e,a)) & 
  a>0 & a<>1 implies 
  (1/log(number_e,a))(#)((-(exp*f1))(#)f2) is_differentiable_on Z &
  for x st x in Z holds 
  (((1/log(number_e,a))(#)((-(exp*f1))(#)f2))`|Z).x =x /a #R x
  proof
    assume that A1:
    Z c= dom ((1/log(number_e,a))(#)((-(exp*f1))(#)f2)) and 
A2: (for x st x in Z holds f1.x=-x*log(number_e,a) & f2.x=x+1/log(number_e,a))
    & a>0 & a<>1;
A3: (for x st x in Z holds f1.x=-x*log(number_e,a)) & a>0 by A2;
A4: Z c= dom ((-(exp*f1))(#)f2) by SEQ_1:def 6,A1;
    then Z c= dom (-(exp*f1)) /\ dom f2 by SEQ_1:def 5;
    then A5:Z c= dom (-(exp*f1)) & Z c= dom f2 by XBOOLE_1:18;
    then A6: -(exp*f1) is_differentiable_on Z &
    for x st x in Z holds ((-(exp*f1))`|Z).x =a #R (-x)*log(number_e,a) 
    by A3,th16;
A10: for x st x in Z holds f2.x = 1*x+1/log(number_e,a) by A2;
     then A7:f2 is_differentiable_on Z & for x st x in Z holds 
     (f2`|Z).x=1 by FDIFF_1:31,A5;
     then A8:(-(exp*f1))(#)f2 is_differentiable_on Z by A4,A6,FDIFF_1:29;
A9:  log(number_e,a)<>0
     proof
       assume B1:log(number_e,a)=0;
B2:    log(number_e,a)=log(number_e,1) by SIN_COS2:13,B1,TAYLOR_1:13;
B3:    number_e>0 & number_e<>1 by TAYLOR_1:11;
       then a=(number_e) to_power log(number_e,1) by B2,POWER:def 3,A2
       .=1 by POWER:def 3,B3;
       hence contradiction by A2;
     end;
     for x st x in Z holds
     (((1/log(number_e,a))(#)((-(exp*f1))(#)f2))`|Z).x =x /a #R x
     proof
       let x;
       assume B1:x in Z;
       then x in dom -(exp*f1) by A5;
       then B4:x in dom (exp*f1) by SEQ_1:def 7;
B2:    (-(exp*f1)).x=-((exp*f1).x) by SEQ_1:def 7,B1,A5
       .=-(exp.(f1.x)) by FUNCT_1:22,B4
       .=-(exp.(-x*log(number_e,a))) by B1,A2
       .=-a #R (-x) by A2,th15;
       (((1/log(number_e,a))(#)((-(exp*f1))(#)f2))`|Z).x 
       =(1/log(number_e,a))*diff((-(exp*f1))(#)f2,x) by A1,A8,FDIFF_1:28,B1
       .=(1/log(number_e,a))*(((-(exp*f1))(#)f2)`|Z).x by FDIFF_1:def 8,B1,A8
       .=(1/log(number_e,a))*(f2.x*diff(-(exp*f1),x)+(-(exp*f1)).x*diff(f2,x))
       by B1,A4,A6,A7,FDIFF_1:29
       .=(1/log(number_e,a))*(f2.x*((-(exp*f1))`|Z).x+(-(exp*f1)).x*diff(f2,x))
       by A6,B1,FDIFF_1:def 8
       .=(1/log(number_e,a))*(f2.x*((-(exp*f1))`|Z).x+(-(exp*f1)).x*(f2`|Z).x)
       by A7,B1,FDIFF_1:def 8
       .=(1/log(number_e,a))*(f2.x*(a #R (-x)*log(number_e,a))
       +(-(exp*f1)).x*(f2`|Z).x) by A5,A3,th16,B1
       .=(1/log(number_e,a))*(f2.x*(a #R (-x)*log(number_e,a))
       +(-(exp*f1)).x*1) by A10,FDIFF_1:31,A5,B1
       .=(1/log(number_e,a))*((f2.x*log(number_e,a)-1)*a #R (-x)) by B2
       .=(1/log(number_e,a))*(((x+1/log(number_e,a))*
         log(number_e,a)-1)*a #R (-x)) by A2,B1
       .=(1/log(number_e,a))*
       (x*log(number_e,a)+1/log(number_e,a)*log(number_e,a)-1)*a #R (-x) 
       .=(1/log(number_e,a))*(x*log(number_e,a)+1-1)*a #R (-x)
         by A9,XCMPLX_1:107
       .=(1/log(number_e,a))*log(number_e,a)*x*a #R (-x) 
       .=1*x*a #R (-x) by A9,XCMPLX_1:107
       .=x*(1/a #R x) by PREPOWER:90,A2
       .=x/a #R x by XCMPLX_1:100;
       hence thesis;
     end;
     hence thesis by A8,A1,FDIFF_1:28;
   end;

:: 18 f.x=(1/(ln a-1))*a #R x/exp(x)
theorem 
  Z c= dom ((1/(log(number_e,a)-1))(#)((exp*f)/exp)) & 
  (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 & a<>number_e
  implies 
  (1/(log(number_e,a)-1))(#)((exp*f)/exp) is_differentiable_on Z &
  for x st x in Z holds 
  (((1/(log(number_e,a)-1))(#)((exp*f)/exp))`|Z).x =a #R x/exp.x
  proof
    assume that A1:Z c= dom ((1/(log(number_e,a)-1))(#)((exp*f)/exp)) and
A2: (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 and 
A3: a<>number_e;
    Z c= dom ((exp*f)/exp) by SEQ_1:def 6,A1;
    then Z c= dom (exp*f) /\ (dom exp \ (exp)"{0}) by RFUNCT_1:def 4;
    then A5:Z c= dom (exp*f) by XBOOLE_1:18;
A6: exp*f is_differentiable_on Z & for x st x in Z holds 
    ((exp*f)`|Z).x =a #R x*log(number_e,a) by A2,A5,th10;
A7: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for x st x in Z holds exp.x<>0 by SIN_COS:59;
    then A8:(exp*f)/exp is_differentiable_on Z by A6,A7,FDIFF_2:21;
A12: log(number_e,a)-1<>0
     proof
       assume B1:log(number_e,a)-1=0;
B2:    number_e>0 & number_e<>1 by TAYLOR_1:11;
B3:    log(number_e,a)=log(number_e,number_e) by B1,B2,POWER:60;
       a=(number_e) to_power log(number_e,number_e) by B3,POWER:def 3,B2,A2
       .=number_e by POWER:def 3,B2;
       hence contradiction by A3;
     end;
     for x st x in Z holds
     (((1/(log(number_e,a)-1))(#)((exp*f)/exp))`|Z).x =a #R x/exp.x
     proof
       let x;
       assume B1:x in Z;
       then B2:(exp*f).x=exp.(f.x) by FUNCT_1:22,A5
       .=exp.(x*log(number_e,a)) by B1,A2
       .=a #R x by A2,th9;
A9:    exp is_differentiable_in x by SIN_COS:70;
A10:   exp*f is_differentiable_in x by B1,A6,FDIFF_1:16;
A11:   exp.x <>0 by SIN_COS:59;
       (((1/(log(number_e,a)-1))(#)((exp*f)/exp))`|Z).x 
       =(1/(log(number_e,a)-1))*diff(((exp*f)/exp),x) by A1,A8,FDIFF_1:28,B1
       .=(1/(log(number_e,a)-1))*
       ((diff(exp*f,x) * exp.x - diff(exp,x) *(exp*f).x)/(exp.x)^2)
       by FDIFF_2:14,A9,A10,A11
       .=(1/(log(number_e,a)-1))*((diff(exp*f,x)*exp.x-exp.x*a #R x)/(exp.x)^2)
       by SIN_COS:70,B2
       .=(1/(log(number_e,a)-1))*(((diff(exp*f,x)-a #R x)*exp.x)/
         ((exp.x)*(exp.x)))
       .=(1/(log(number_e,a)-1))*((diff(exp*f,x)-a #R x)/(exp.x))
         by XCMPLX_1:92,A11
       .=(1/(log(number_e,a)-1))*(diff(exp*f,x)-a #R x)/(exp.x) by XCMPLX_1:75
       .=(1/(log(number_e,a)-1))*(((exp*f)`|Z).x-a #R x)/(exp.x) 
       by A6,B1,FDIFF_1:def 8
       .=(1/(log(number_e,a)-1))*(a #R x*log(number_e,a)-a #R x)/(exp.x) 
       by A2,A5,th10,B1
       .=(1/(log(number_e,a)-1))*(log(number_e,a)-1)*a #R x/exp.x
       .=1* a #R x/exp.x by A12,XCMPLX_1:107
       .=a #R x/exp.x;
       hence thesis;
     end;
     hence thesis by A8,FDIFF_1:28,A1;
   end;

:: 19 f.x=(1/(1-ln a))*exp(x)/a #R x
theorem 
  Z c= dom ((1/(1-log(number_e,a)))(#)(exp/(exp*f))) & 
  (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 & a<>number_e
  implies 
  (1/(1-log(number_e,a)))(#)(exp/(exp*f)) is_differentiable_on Z &
  for x st x in Z holds 
  (((1/(1-log(number_e,a)))(#)(exp/(exp*f)))`|Z).x =exp.x/a #R x
  proof
    assume that A1:Z c= dom ((1/(1-log(number_e,a)))(#)(exp/(exp*f))) and 
A2: (for x st x in Z holds f.x=x*log(number_e,a)) & a>0 and 
A3: a<>number_e;
    Z c= dom (exp/(exp*f)) by SEQ_1:def 6,A1;
    then Z c= dom exp /\ (dom (exp*f) \ (exp*f)"{0}) by RFUNCT_1:def 4;
    then A5:Z c= dom (exp*f) \ (exp*f)"{0} by XBOOLE_1:18;
    dom ((exp*f)^) c= dom (exp*f) by RFUNCT_1:11;
    then dom (exp*f) \ (exp*f)"{0} c= dom (exp*f) by RFUNCT_1:def 8; 
    then A6:Z c= dom (exp*f) by XBOOLE_1:1,A5;
A7: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A8: exp*f is_differentiable_on Z & for x st x in Z holds 
    ((exp*f)`|Z).x =a #R x*log(number_e,a) by A2,A6,th10;
A9: for x st x in Z holds (exp*f).x<>0 
    proof
      let x;
      assume x in Z;
      then (exp*f).x=exp.(f.x) by FUNCT_1:22,A6;
      hence thesis by SIN_COS:59;
    end;
    then A10:exp/(exp*f) is_differentiable_on Z by A8,A7,FDIFF_2:21;
A11: 1-log(number_e,a)<>0
     proof
       assume B1:1-log(number_e,a)=0;
B2:    number_e>0 & number_e<>1 by TAYLOR_1:11;
B3:    log(number_e,a)=log(number_e,number_e) by B1,B2,POWER:60;
       a=(number_e) to_power log(number_e,number_e) by B3,POWER:def 3,B2,A2
       .=number_e by POWER:def 3,B2;
       hence contradiction by A3;
     end;
     for x st x in Z holds 
     (((1/(1-log(number_e,a)))(#)(exp/(exp*f)))`|Z).x =exp.x/a #R x
     proof
       let x;
       assume B1:x in Z;
       then B2:(exp*f).x=exp.(f.x) by FUNCT_1:22,A6
       .=exp.(x*log(number_e,a)) by B1,A2
       .=a #R x by A2,th9;
A15:   a #R x >0 by PREPOWER:95,A2;
A12:   exp is_differentiable_in x by SIN_COS:70;
A13:   exp*f is_differentiable_in x by B1,A8,FDIFF_1:16;
A14:   (exp*f).x <>0 by A9,B1;
       (((1/(1-log(number_e,a)))(#)(exp/(exp*f)))`|Z).x 
       =(1/(1-log(number_e,a)))*diff((exp/(exp*f)),x) by A1,A10,FDIFF_1:28,B1
       .=(1/(1-log(number_e,a)))*
       ((diff(exp,x) * (exp*f).x - diff((exp*f),x) *exp.x)/((exp*f).x)^2)
       by FDIFF_2:14,A12,A13,A14
       .=(1/(1-log(number_e,a)))*
       ((exp.x * a #R x - diff((exp*f),x)*exp.x)/(a #R x)^2) by SIN_COS:70,B2
       .=(1/(1-log(number_e,a)))*((exp.x*(a #R x -diff((exp*f),x)))/(a #R x)^2) 
       .=(1/(1-log(number_e,a)))*((exp.x*(a #R x -((exp*f)`|Z).x))/(a #R x)^2)
       by A8,B1,FDIFF_1:def 8
       .=(1/(1-log(number_e,a)))*
       ((exp.x*(a #R x -a #R x*log(number_e,a)))/(a #R x)^2) by A2,A6,th10,B1
       .=(1/(1-log(number_e,a)))*((1-log(number_e,a))*exp.x*a #R x)/(a #R x)^2
       by XCMPLX_1:75
       .=(1/(1-log(number_e,a)))*(1-log(number_e,a))*exp.x*a #R x/(a #R x)^2
       .=1*exp.x*a #R x/(a #R x)^2 by A11,XCMPLX_1:107
       .=exp.x/a #R x by A15,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A10,FDIFF_1:28,A1;
   end;

:: 20 f.x=ln(exp.x+1)
theorem 
  Z c= dom ((log_ number_e)*(exp+f)) & (for x st x in Z holds f.x=1 ) 
  implies (log_ number_e)*(exp+f) is_differentiable_on Z &
  for x st x in Z holds (((log_ number_e)*(exp+f))`|Z).x = exp.x/(exp.x+1)
  proof
    assume that A1:Z c= dom ((log_ number_e)*(exp+f)) and
A2: for x st x in Z holds f.x=1;
    for y st y in Z holds y in dom (exp+f) by FUNCT_1:21,A1;
    then A5:Z c= dom (exp+f) by TARSKI:def 3; 
    then Z c= dom exp /\ dom f by SEQ_1:def 3;
    then A6:Z c= dom f by XBOOLE_1:18;
A15: for x st x in Z holds f.x=0*x+1 by A2;
     then A7:f is_differentiable_on Z & 
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A6;
A8:  exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
     then A9:exp+f is_differentiable_on Z by A7,A5,FDIFF_1:26;
A10: for x st x in Z holds ((exp+f)`|Z).x =exp.x
     proof
       let x;
       assume A4:x in Z;
       hence ((exp+f)`|Z).x = diff(exp,x) + diff(f,x) by A8,A7,A5,FDIFF_1:26
       .=exp.x+ diff(f,x) by SIN_COS:70
       .=exp.x+(f`|Z).x by A4,FDIFF_1:def 8,A7
       .=exp.x+0 by A4,A15,FDIFF_1:31,A6
       .=exp.x;
     end;
B1:  for x st x in Z holds (exp+f).x>0
     proof
       let x;
       assume A4:x in Z;
       then A11:(exp+f).x=exp.x + f.x by SEQ_1:def 3,A5
       .=exp.x +1 by A2,A4;
       exp.x>0 by SIN_COS:59;
       hence (exp+f).x>0 by A11,XREAL_1:36;
     end;
A16: for x st x in Z holds (log_ number_e)*(exp+f) is_differentiable_in x
     proof
       let x;
       assume A4:x in Z;
       then A5:exp+f is_differentiable_in x by FDIFF_1:16,A9;
       (exp+f).x >0 by B1,A4;
       hence thesis by TAYLOR_1:20,A5;
     end;
     then A:(log_ number_e)*(exp+f) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (((log_ number_e)*(exp+f))`|Z).x = exp.x/(exp.x+1)
     proof
       let x;
       assume A4:x in Z;
       then B5:(exp+f).x=exp.x + f.x by SEQ_1:def 3,A5
       .=exp.x +1 by A2,A4;
A6:    exp+f is_differentiable_in x by FDIFF_1:16,A9,A4;
A14:   (exp+f).x>0 by B1,A4;
       diff((log_ number_e)*(exp+f),x)
       =diff((exp+f),x)/((exp+f).x) by TAYLOR_1:20,A6,A14
       .=((exp+f)`|Z).x/((exp+f).x) by A9,A4,FDIFF_1:def 8 
       .=exp.x/(exp.x +1) by A10,A4,B5;
       hence thesis by A4,A,FDIFF_1:def 8;
     end;
     hence thesis by A16,A1,FDIFF_1:16;
   end;

:: 21 f.x=ln(exp.x-1)
theorem 
  Z c= dom ((log_ number_e)*(exp-f)) & (for x st x in Z holds f.x=1 
  & (exp-f).x>0) implies (log_ number_e)*(exp-f) is_differentiable_on Z &
  for x st x in Z holds (((log_ number_e)*(exp-f))`|Z).x = exp.x/(exp.x-1)
  proof
    assume that A1:Z c= dom ((log_ number_e)*(exp-f)) and
A2: for x st x in Z holds f.x=1 & (exp-f).x>0;
    for y st y in Z holds y in dom (exp-f) by FUNCT_1:21,A1;
    then A4:Z c= dom (exp-f) by TARSKI:def 3; 
    then Z c= dom exp /\ dom f by SEQ_1:def 4;
    then A6:Z c= dom f by XBOOLE_1:18;
B2: for x st x in Z holds f.x=0*x+1 by A2;
    then A7:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A6;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A9:exp-f is_differentiable_on Z by A7,A4,FDIFF_1:27;
A10: for x st x in Z holds ((exp-f)`|Z).x =exp.x
     proof
       let x;
       assume B4:x in Z;
       hence ((exp-f)`|Z).x = diff(exp,x) - diff(f,x) by A8,A7,A4,FDIFF_1:27
       .= exp.x- diff(f,x) by SIN_COS:70
       .= exp.x-(f`|Z).x by B4,FDIFF_1:def 8,A7
       .= exp.x-0 by B4,FDIFF_1:31,A6,B2
       .= exp.x;
     end;
A11: for x st x in Z holds (log_ number_e)*(exp-f) is_differentiable_in x
     proof
       let x;
       assume A4:x in Z;
       then A5:exp-f is_differentiable_in x by FDIFF_1:16,A9;
       (exp-f).x >0 by A2,A4;
       hence thesis by TAYLOR_1:20,A5;
     end;
     then A:(log_ number_e)*(exp-f) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (((log_ number_e)*(exp-f))`|Z).x = exp.x/(exp.x-1)
     proof
       let x;
       assume B4:x in Z;
       then B3:(exp-f).x=exp.x - f.x by SEQ_1:def 4,A4
       .=exp.x -1 by A2,B4;
A6:    (exp-f) is_differentiable_in x by FDIFF_1:16,A9,B4;
A14:   (exp-f).x >0 by B4,A2;
       diff((log_ number_e)*(exp-f),x)
       =diff((exp-f),x)/((exp-f).x) by TAYLOR_1:20,A6,A14
       .=((exp-f)`|Z).x/((exp-f).x) by A9,B4,FDIFF_1:def 8
       .=exp.x/(exp.x -1) by A10,B4,B3;
       hence thesis by B4,A,FDIFF_1:def 8;
     end;
     hence thesis by A11,A1,FDIFF_1:16;
   end;

:: 22 f.x=-ln(1-exp.x)
theorem 
  Z c= dom -((log_ number_e)*(f-exp)) & (for x st x in Z holds f.x=1 
  & (f-exp).x>0) implies -((log_ number_e)*(f-exp)) is_differentiable_on Z &
  for x st x in Z holds (((-(log_ number_e)*(f-exp)))`|Z).x = exp.x/(1-exp.x)
  proof
    assume that A1:Z c= dom -((log_ number_e)*(f-exp)) and
A2: for x st x in Z holds f.x=1 & (f-exp).x>0;
A4: Z c= dom ((-1)(#)((log_ number_e)*(f-exp))) by A1,RFUNCT_1:38;
A5: Z c= dom ((log_ number_e)*(f-exp)) by SEQ_1:def 7,A1;
    for y st y in Z holds y in dom (f-exp) by FUNCT_1:21,A5;
    then B1:Z c= dom (f-exp) by TARSKI:def 3;
    then Z c= dom exp /\ dom f by SEQ_1:def 4;
    then A6:Z c= dom f by XBOOLE_1:18;
B2: for x st x in Z holds f.x=0*x+1 by A2;
    then A7:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A6;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    then A9:f-exp is_differentiable_on Z by A7,B1,FDIFF_1:27;
A10: for x st x in Z holds ((f-exp)`|Z).x =-exp.x
     proof
       let x;
       assume B4:x in Z;
       hence ((f-exp)`|Z).x =diff(f,x)-diff(exp,x) by A8,A7,B1,FDIFF_1:27
       .= diff(f,x)-exp.x by SIN_COS:70
       .= (f`|Z).x-exp.x by B4,FDIFF_1:def 8,A7
       .=0-exp.x by B4,FDIFF_1:31,A6,B2
       .=-exp.x;
     end;
     for x st x in Z holds (log_ number_e)*(f-exp) is_differentiable_in x
     proof
       let x;
       assume A4:x in Z;
       then A5:f-exp is_differentiable_in x by FDIFF_1:16,A9;
       (f-exp).x >0 by A2,A4;
       hence thesis by TAYLOR_1:20,A5;
     end;
     then A:(log_ number_e)*(f-exp) is_differentiable_on Z by A5,FDIFF_1:16;
     then B3:(-1)(#)((log_ number_e)*(f-exp)) is_differentiable_on Z 
     by FDIFF_1:28,A4;
     for x st x in Z holds (((-(log_ number_e)*(f-exp)))`|Z).x =
       exp.x/(1-exp.x)
     proof
       let x;
       assume B4:x in Z;
       then B3:(f-exp).x=f.x-exp.x by SEQ_1:def 4,B1
       .=1-exp.x by A2,B4;
A6:    (f-exp) is_differentiable_in x by FDIFF_1:16,A9,B4;
A14:   (f-exp).x >0 by B4,A2;
       (((-1)(#)((log_ number_e)*(f-exp)))`|Z).x 
       =(-1)*diff(((log_ number_e)*(f-exp)),x) by FDIFF_1:28,A,B4,A4
       .=(-1)*(diff((f-exp),x)/((f-exp).x)) by TAYLOR_1:20,A6,A14
       .=(-1)*(((f-exp)`|Z).x/(f-exp).x) by A9,B4,FDIFF_1:def 8
       .=(-1)*((-exp.x)/(1-exp.x)) by A10,B4,B3
       .=((-1)*(-exp.x))/(1-exp.x) by XCMPLX_1:75
       .=exp.x/(1-exp.x);
       hence thesis by RFUNCT_1:38;
     end;
     hence thesis by B3,RFUNCT_1:38;
   end;

:: 23 f.x=exp(2*x)+1
theorem th23: 
  Z c= dom ((( #Z 2)*exp)+f) & (for x st x in Z holds f.x=1)
  implies (( #Z 2)*exp)+f is_differentiable_on Z & for x st x in Z holds
  (((( #Z 2)*exp)+f)`|Z).x =2*exp(2*x)
  proof
    assume that A1:Z c= dom ((( #Z 2)*exp)+f) and
A2: (for x st x in Z holds f.x=1);
    Z c= dom (( #Z 2)*exp) /\ dom f by SEQ_1:def 3,A1;
    then A4:Z c= dom (( #Z 2)*exp) & Z c= dom f by XBOOLE_1:18;
A7: for x st x in Z holds f.x=0*x+1 by A2;
    then A5:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
    now let x;
      assume x in Z;
      exp is_differentiable_in x by SIN_COS:70;
      hence ( #Z 2)*exp is_differentiable_in x by TAYLOR_1:3;
    end;
    then A6:( #Z 2)*exp is_differentiable_on Z by A4,FDIFF_1:16;
    for x st x in Z holds (((( #Z 2)*exp)+f)`|Z).x =2*exp(2*x)
    proof
      let x;
      assume B1:x in Z;
B2:   exp is_differentiable_in x by SIN_COS:70;
      (((( #Z 2)*exp)+f)`|Z).x 
      =diff(( #Z 2)*exp,x)+diff(f,x) by A5,A6,FDIFF_1:26,A1,B1
      .=(2*( (exp.x) #Z (2-1)) * diff(exp,x))+diff(f,x) by B2,TAYLOR_1:3
      .=2*( (exp.x) #Z (2-1)) * exp.x+diff(f,x) by SIN_COS:70
      .=2* exp.x * exp.x+diff(f,x) by PREPOWER:45
      .=2*( exp.x * exp.x)+diff(f,x)
      .=2*( exp( x )* exp.x)+diff(f,x) by SIN_COS:def 27
      .=2*( exp( x )* exp(x))+diff(f,x) by SIN_COS:def 27
      .=2*(exp(x+x))+diff(f,x) by SIN_COS:55
      .=2* exp(2*x)+(f`|Z).x by FDIFF_1:def 8,B1,A5
      .= 2* exp(2*x)+0 by A7,FDIFF_1:31,A4,B1
      .=2* exp(2*x);
      hence thesis;
    end;
    hence thesis by A6,A5,FDIFF_1:26,A1;
  end;

:: 24 f.x=(1/2)*ln(exp(2*x)+1)
theorem 
  Z c= dom ((1/2)(#)((log_ number_e)*f)) & f=(( #Z 2)*exp)+f1 & 
  (for x st x in Z holds f1.x=1) implies (1/2)(#)((log_ number_e)*f) 
  is_differentiable_on Z & for x st x in Z holds 
  (((1/2)(#)((log_ number_e)*f))`|Z).x =exp(x)/(exp(x)+exp(-x))
  proof
    assume that A1:Z c= dom ((1/2)(#)((log_ number_e)*f)) and
A2: f=(( #Z 2)*exp)+f1 & (for x st x in Z holds f1.x=1);
A3: Z c= dom ((log_ number_e)*f) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom f by FUNCT_1:21,A3;
    then A4:Z c= dom ((( #Z 2)*exp)+f1) by TARSKI:def 3,A2;
    then Z c= dom (( #Z 2)*exp) /\ dom f1 by SEQ_1:def 3;
    then A6:Z c= dom (( #Z 2)*exp) by XBOOLE_1:18;
A7: (( #Z 2)*exp)+f1 is_differentiable_on Z & for x st x in Z holds
    (((( #Z 2)*exp)+f1)`|Z).x =2*exp(2*x) by A4,A2,th23;
A8: for x st x in Z holds f.x >0
    proof
      let x;
      assume B1:x in Z;
B2:   ((( #Z 2)*exp)+f1).x
      =(( #Z 2)*exp).x+f1.x by SEQ_1:def 3,A4,B1
      .=( #Z 2).(exp.x)+f1.x by B1,A6,FUNCT_1:22
      .=(exp.x) #Z 2+f1.x by TAYLOR_1:def 1
      .=(exp.x) #Z 2+1 by A2,B1;
      exp.x>0 by SIN_COS:59;
      then (exp.x) #Z 2>0 by PREPOWER:49;
      hence thesis by B2,A2,XREAL_1:36;
    end;
    for x st x in Z holds (log_ number_e)*f is_differentiable_in x
    proof
      let x;
      assume B1:x in Z;
      then B2:f is_differentiable_in x by FDIFF_1:16,A7,A2;
      f.x >0 by A8,B1;
      hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,B2;
    end;
    then A:(log_ number_e)*f is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds 
    (((1/2)(#)((log_ number_e)*f))`|Z).x =exp(x)/(exp(x)+exp(-x))
    proof
      let x;
      assume B1:x in Z;
A12:  exp(x)>0 by SIN_COS:60;
B2:   ((( #Z 2)*exp)+f1).x
      =(( #Z 2)*exp).x+f1.x by SEQ_1:def 3,A4,B1
      .=( #Z 2).(exp.x)+f1.x by B1,A6,FUNCT_1:22
      .=(exp.x) #Z 2+f1.x by TAYLOR_1:def 1
      .=(exp.x) #Z 2+1 by A2,B1
      .=(exp(x)) #Z (1+1)+1 by SIN_COS:def 27
      .=(exp(x)) #Z 1 * (exp(x)) #Z 1 +1 by PREPOWER:54,A12
      .=exp(x) * (exp(x)) #Z 1 +1 by PREPOWER:45
      .=exp(x) * exp(x) +1 by PREPOWER:45;
A13:  f is_differentiable_in x by FDIFF_1:16,A7,A2,B1;
A14:  f.x >0 by A8,B1;
      (((1/2)(#)((log_ number_e)*f))`|Z).x 
      =(1/2)*diff(((log_ number_e)*f),x) by A,A1,FDIFF_1:28,B1
      .=(1/2)*(diff(f,x)/f.x) by TAYLOR_1:20,A13,A14
      .=(1/2)*((((( #Z 2)*exp)+f1)`|Z).x/((( #Z 2)*exp)+f1).x) 
      by B1,FDIFF_1:def 8,A7,A2
      .=(1/2)*((2*exp(2*x))/(exp(x) * exp(x) +1)) by A4,A2,th23,B1,B2
      .=(1/2)*(2*exp(2*x))/(exp(x) * exp(x) +1) by XCMPLX_1:75 
      .=(exp(x+x)/exp(x))/((exp(x) * exp(x)+1)/exp(x)) by A12,XCMPLX_1:55 
      .=((exp(x)*exp(x))/exp(x))/((exp(x) * exp(x)+1)/exp(x)) by SIN_COS:55
      .=(exp(x)*exp(x)/exp(x))/(exp(x) * exp(x)/exp(x)+1/exp(x)) by XCMPLX_1:63 
      .=exp(x)/(exp(x) * exp(x)/exp(x)+1/exp(x)) by A12,XCMPLX_1:90 
      .=exp(x)/(exp(x)+1/exp(x)) by A12,XCMPLX_1:90 
      .=exp(x)/(exp(x)+exp(-x)) by TAYLOR_1:4;
      hence thesis;
    end;
    hence thesis by A,A1,FDIFF_1:28;
  end;

:: 25 f.x=exp(2*x)-1
theorem th25: 
  Z c= dom ((( #Z 2)*exp)-f) & (for x st x in Z holds f.x=1)
  implies (( #Z 2)*exp)-f is_differentiable_on Z & for x st x in Z holds
  (((( #Z 2)*exp)-f)`|Z).x =2*exp(2*x)
  proof
    assume that A1:Z c= dom ((( #Z 2)*exp)-f) and
A2: (for x st x in Z holds f.x=1);
    Z c= dom (( #Z 2)*exp) /\ dom f by SEQ_1:def 4,A1;
    then A4:Z c= dom (( #Z 2)*exp) & Z c= dom f by XBOOLE_1:18;
A7: for x st x in Z holds f.x=0*x+1 by A2;
    then A5:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
    now let x;
      assume x in Z;
      exp is_differentiable_in x by SIN_COS:70;
      hence ( #Z 2)*exp is_differentiable_in x by TAYLOR_1:3;
    end;
    then A6:( #Z 2)*exp is_differentiable_on Z by A4,FDIFF_1:16;
    for x st x in Z holds (((( #Z 2)*exp)-f)`|Z).x =2*exp(2*x)
    proof
      let x;
      assume B1:x in Z;
B2:   exp is_differentiable_in x by SIN_COS:70;
      (((( #Z 2)*exp)-f)`|Z).x 
      =diff(( #Z 2)*exp,x)-diff(f,x) by A5,A6,FDIFF_1:27,A1,B1
      .=(2*((exp.x) #Z (2-1)) * diff(exp,x))-diff(f,x) by B2,TAYLOR_1:3
      .=2*((exp.x) #Z 1) * exp.x-diff(f,x) by SIN_COS:70
      .=2* exp.x * exp.x-diff(f,x) by PREPOWER:45
      .=2*(exp.x * exp.x)-diff(f,x)
      .=2*(exp(x)* exp.x)-diff(f,x) by SIN_COS:def 27
      .=2*(exp(x)* exp(x))-diff(f,x) by SIN_COS:def 27
      .=2*(exp(x+x))-diff(f,x) by SIN_COS:55
      .=2* exp(2*x)-(f`|Z).x by FDIFF_1:def 8,B1,A5
      .= 2* exp(2*x)-0 by A7,FDIFF_1:31,A4,B1
      .=2* exp(2*x);
      hence thesis;
    end;
    hence thesis by A6,A5,FDIFF_1:27,A1;
  end;

:: 26 f.x=(1/2)*ln(exp(2*x)-1)
theorem 
  Z c= dom ((1/2)(#)((log_ number_e)*f)) & f=(( #Z 2)*exp)-f1 & 
  (for x st x in Z holds f1.x=1 & f.x>0) implies (1/2)(#)((log_ number_e)*f) 
  is_differentiable_on Z & for x st x in Z holds 
  (((1/2)(#)((log_ number_e)*f))`|Z).x =exp(x)/(exp(x)-exp(-x))
  proof
    assume that A1:Z c= dom ((1/2)(#)((log_ number_e)*f)) and
A8: f=(( #Z 2)*exp)-f1 & (for x st x in Z holds f1.x=1 & f.x>0);
A2: f=(( #Z 2)*exp)-f1 & (for x st x in Z holds f1.x=1) by A8;
A3: Z c= dom ((log_ number_e)*f) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom f by FUNCT_1:21,A3;
    then A4:Z c= dom ((( #Z 2)*exp)-f1) by TARSKI:def 3,A8;
    then Z c= dom (( #Z 2)*exp) /\ dom f1 by SEQ_1:def 4;
    then A6:Z c= dom (( #Z 2)*exp) by XBOOLE_1:18;
A7: (( #Z 2)*exp)-f1 is_differentiable_on Z & for x st x in Z holds
    (((( #Z 2)*exp)-f1)`|Z).x =2*exp(2*x) by A4,A2,th25;
    for x st x in Z holds (log_ number_e)*f is_differentiable_in x
    proof
      let x;
      assume B1:x in Z;
      then B2:f is_differentiable_in x by FDIFF_1:16,A7,A8;
      f.x >0 by A8,B1;
      hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,B2;
    end;
    then A:(log_ number_e)*f is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds 
    (((1/2)(#)((log_ number_e)*f))`|Z).x =exp(x)/(exp(x)-exp(-x))
    proof
      let x;
      assume B1:x in Z;
A12:  exp(x)>0 by SIN_COS:60;
B2:   ((( #Z 2)*exp)-f1).x
      =(( #Z 2)*exp).x-f1.x by SEQ_1:def 4,A4,B1
      .=( #Z 2).(exp.x)-f1.x by B1,A6,FUNCT_1:22
      .=(exp.x) #Z 2-f1.x by TAYLOR_1:def 1
      .=(exp.x) #Z 2-1 by A8,B1
      .=(exp(x)) #Z (1+1)-1 by SIN_COS:def 27
      .=(exp(x)) #Z 1 * (exp(x)) #Z 1 -1 by PREPOWER:54,A12
      .=exp(x) * (exp(x)) #Z 1 -1 by PREPOWER:45
      .=exp(x) * exp(x) -1 by PREPOWER:45;
A13:  f is_differentiable_in x by FDIFF_1:16,A7,A8,B1;
A14:  f.x >0 by A8,B1;
      (((1/2)(#)((log_ number_e)*f))`|Z).x 
      =(1/2)*diff(((log_ number_e)*f),x) by A,A1,FDIFF_1:28,B1
      .=(1/2)*(diff(f,x)/f.x) by TAYLOR_1:20,A13,A14
      .=(1/2)*((((( #Z 2)*exp)-f1)`|Z).x/((( #Z 2)*exp)-f1).x) 
      by B1,FDIFF_1:def 8,A7,A8
      .=(1/2)*((2*exp(2*x))/(exp(x) * exp(x) -1)) by A4,A2,th25,B1,B2
      .=(1/2)*(2*exp(2*x))/(exp(x) * exp(x) -1) by XCMPLX_1:75 
      .=(exp(x+x)/exp(x))/((exp(x) * exp(x)-1)/exp(x)) by A12,XCMPLX_1:55 
      .=((exp(x)*exp(x))/exp(x))/((exp(x) * exp(x)-1)/exp(x)) by SIN_COS:55
      .=((exp(x)*exp(x))/exp(x))/((exp(x) * exp(x))/exp(x)-1/exp(x))
      by XCMPLX_1:121 
      .=exp(x)/(exp(x) * exp(x)/exp(x)-1/exp(x)) by A12,XCMPLX_1:90 
      .=exp(x)/(exp(x)-1/exp(x)) by A12,XCMPLX_1:90 
      .=exp(x)/(exp(x)-exp(-x)) by TAYLOR_1:4;
      hence thesis;
    end;
    hence thesis by A,A1,FDIFF_1:28;
  end;

:: 27 f.x=(exp.x-1)^2
theorem th27: 
  Z c= dom (( #Z 2)*(exp-f)) & (for x st x in Z holds f.x=1)
  implies ( #Z 2)*(exp-f) is_differentiable_on Z & for x st x in Z holds
  ((( #Z 2)*(exp-f))`|Z).x =2*exp.x*(exp.x-1)
  proof
    assume that A1:Z c= dom (( #Z 2)*(exp-f)) and
A2: (for x st x in Z holds f.x=1);
    for y st y in Z holds y in dom (exp-f) by FUNCT_1:21,A1;
    then A3:Z c= dom (exp-f) by TARSKI:def 3;
    then Z c= dom exp /\ dom f by SEQ_1:def 4;
    then A4:Z c= dom f by XBOOLE_1:18;
A9: for x st x in Z holds f.x=0*x+1 by A2;
    then A5:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A8: for x st x in Z holds ( #Z 2)*(exp-f) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then B1:f is_differentiable_in x by A5,FDIFF_1:16;
      exp is_differentiable_in x by SIN_COS:70;
      then exp-f is_differentiable_in x by FDIFF_1:22,B1;
      hence thesis by TAYLOR_1:3;
    end;
    then A6:( #Z 2)*(exp-f) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    ((( #Z 2)*(exp-f))`|Z).x =2*exp.x*(exp.x-1)
    proof
      let x;
      assume B1:x in Z;
      then B3:f is_differentiable_in x by A5,FDIFF_1:16;
B4:   exp is_differentiable_in x by SIN_COS:70;
      then B2:exp-f is_differentiable_in x by FDIFF_1:22,B3; 
B5:   diff((exp-f),x)=diff(exp,x)-diff(f,x) by FDIFF_1:22,B3,B4
      .=diff(exp,x)-(f`|Z).x by FDIFF_1:def 8,B1,A5
      .=exp.x-(f`|Z).x by SIN_COS:70
      .=exp.x-0 by FDIFF_1:31,A4,A9,B1
      .=exp.x;
B6:   (exp-f).x=exp.x-f.x by SEQ_1:def 4,A3,B1
      .= exp.x-1 by B1,A2;
      ((( #Z 2)*(exp-f))`|Z).x
      =diff((( #Z 2)*(exp-f)),x) by A6,FDIFF_1:def 8,B1
      .=2* ((exp-f).x) #Z (2-1) * diff((exp-f),x) by B2,TAYLOR_1:3
      .=2*(exp.x-1)*exp.x by B6,B5,PREPOWER:45
      .=2*exp.x*(exp.x-1);
      hence thesis;
    end;
    hence thesis by A8,A1,FDIFF_1:16;
  end;

:: 28 f.x=ln((exp.x-1)^2/exp.x)
theorem 
  Z c= dom f & f=(log_ number_e)*((( #Z 2)*(exp-f1))/exp) &
  (for x st x in Z holds f1.x=1 & (exp-f1).x>0) implies
    f is_differentiable_on Z &
      for x st x in Z holds (f`|Z).x =(exp.x+1)/(exp.x-1)
  proof
    assume that A1: Z c= dom f & f=(log_ number_e)*((( #Z 2)*(exp-f1))/exp) 
    and A6:(for x st x in Z holds f1.x=1 & (exp-f1).x>0);
A2: for x st x in Z holds f1.x=1 by A6;
    for y st y in Z holds y in dom ((( #Z 2)*(exp-f1))/exp) by FUNCT_1:21,A1;
    then A3:Z c= dom ((( #Z 2)*(exp-f1))/exp) by TARSKI:def 3;
    then Z c=dom (( #Z 2)*(exp-f1))/\(dom (exp)\(exp)"{0}) by RFUNCT_1:def 4;
    then A4:Z c= dom (( #Z 2)*(exp-f1)) by XBOOLE_1:18;
    then A5:( #Z 2)*(exp-f1) is_differentiable_on Z & for x st x in Z holds
    ((( #Z 2)*(exp-f1))`|Z).x =2*exp.x*(exp.x-1) by A2,th27;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for x st x in Z holds exp.x<>0 by SIN_COS:59;
    then A7:(( #Z 2)*(exp-f1))/exp is_differentiable_on Z by A8,A5,FDIFF_2:21;
    for y st y in Z holds y in dom (exp-f1) by FUNCT_1:21,A4;
    then A10:Z c= dom (exp-f1) by TARSKI:def 3;
A9: for x st x in Z holds ((( #Z 2)*(exp-f1))/exp).x >0
    proof
      let x;
      assume B1:x in Z;
      then B2:((( #Z 2)*(exp-f1))/exp).x
      =(( #Z 2)*(exp-f1)).x*(exp.x)" by RFUNCT_1:def 4,A3
      .=(( #Z 2)*(exp-f1)).x*(1/exp.x) by XCMPLX_1:217
      .=(( #Z 2)*(exp-f1)).x/exp.x by XCMPLX_1:100
      .=( #Z 2).((exp-f1).x)/exp.x by A4,B1,FUNCT_1:22
      .=((exp-f1).x) #Z 2/exp.x by TAYLOR_1:def 1;
      (exp-f1).x>0 by A6,B1;
      then B4:((exp-f1).x) #Z 2>0 by PREPOWER:49;
      exp.x>0 by SIN_COS:59;
      hence ((( #Z 2)*(exp-f1))/exp).x >0 by B2,XREAL_1:141,B4;
    end;
A11: for x st x in Z holds (log_ number_e)*((( #Z 2)*(exp-f1))/exp)
     is_differentiable_in x
     proof
       let x;
       assume B1:x in Z;
       then B2:(( #Z 2)*(exp-f1))/exp is_differentiable_in x by FDIFF_1:16,A7;
       ((( #Z 2)*(exp-f1))/exp).x >0 by A9,B1;
       hence (log_ number_e)*((( #Z 2)*(exp-f1))/exp) is_differentiable_in x
       by TAYLOR_1:20,B2;
     end;
     then A:f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x =(exp.x+1)/(exp.x-1)
     proof
       let x;
       assume B1:x in Z;
       then B2:(( #Z 2)*(exp-f1))/exp is_differentiable_in x by FDIFF_1:16,A7;
B3:    ((( #Z 2)*(exp-f1))/exp).x >0 by A9,B1;
B7:    exp.x>0 by SIN_COS:59;
B11:   (exp-f1).x=exp.x - f1.x by SEQ_1:def 4,B1,A10
       .=exp.x -1 by A6,B1;
       then B9:exp.x-1>0 by B1,A6;
B5:    ( #Z 2)*(exp-f1) is_differentiable_in x by A5,B1,FDIFF_1:16;
       exp is_differentiable_in x by SIN_COS:70;
       then B4:diff(((( #Z 2)*(exp-f1))/exp),x)
       =(diff((( #Z 2)*(exp-f1)),x)*exp.x-diff(exp,x)*
       (( #Z 2)*(exp-f1)).x)/(exp.x)^2 by FDIFF_2:14,B5,B7
       .=(((( #Z 2)*(exp-f1))`|Z).x*exp.x-diff(exp,x)*
       (( #Z 2)*(exp-f1)).x)/(exp.x)^2 by FDIFF_1:def 8,B1,A5
       .=(2*exp.x*(exp.x-1)*exp.x-diff(exp,x)*(( #Z 2)*(exp-f1)).x)/(exp.x)^2 
       by A4,A2,th27,B1
       .=(2*exp.x*(exp.x-1)*exp.x-exp.x*(( #Z 2)*(exp-f1)).x)/(exp.x)^2
       by SIN_COS:70
       .=((2*exp.x*(exp.x-1)-(( #Z 2)*(exp-f1)).x)*exp.x)/(exp.x*exp.x) 
       .=(2*exp.x*(exp.x-1)-(( #Z 2)*(exp-f1)).x)/exp.x by B7,XCMPLX_1:92
       .=(2*exp.x*(exp.x-1)-( #Z 2).((exp-f1).x))/exp.x by A4,B1,FUNCT_1:22
       .=(2*exp.x*(exp.x-1)-((exp-f1).x) #Z 2)/exp.x by TAYLOR_1:def 1
       .=(2*exp.x*(exp.x-1)-(exp.x-f1.x) #Z 2)/exp.x by SEQ_1:def 4,B1,A10
       .=(2*exp.x*(exp.x-1)-(exp.x-1) #Z (1+1))/exp.x by B1,A6
       .=(2*exp.x*(exp.x-1)-(exp.x-1) #Z 1*(exp.x-1) #Z 1)/exp.x
       by PREPOWER:54,B9
       .=(2*exp.x*(exp.x-1)-(exp.x-1) *(exp.x-1) #Z 1)/exp.x by PREPOWER:45
       .=(2*exp.x*(exp.x-1)-(exp.x-1) *(exp.x-1))/exp.x by PREPOWER:45
       .=((exp.x+1)*(exp.x-1))/exp.x;
B8:    ((( #Z 2)*(exp-f1))/exp).x
       =(( #Z 2)*(exp-f1)).x*(exp.x)" by RFUNCT_1:def 4,A3,B1
       .=(( #Z 2)*(exp-f1)).x*(1/exp.x) by XCMPLX_1:217
       .=(( #Z 2)*(exp-f1)).x/exp.x by XCMPLX_1:100
       .=( #Z 2).((exp-f1).x)/exp.x by A4,B1,FUNCT_1:22
       .=((exp.x-1) #Z (1+1))/exp.x by B11,TAYLOR_1:def 1
       .=((exp.x-1) #Z 1*(exp.x-1) #Z 1)/exp.x by PREPOWER:54,B9
       .=((exp.x-1) *(exp.x-1) #Z 1)/exp.x by PREPOWER:45
       .=((exp.x-1) *(exp.x-1))/exp.x by PREPOWER:45;
       (f`|Z).x =diff((log_ number_e)*((( #Z 2)*(exp-f1))/exp),x)
       by A,B1,FDIFF_1:def 8,A1
       .=(((exp.x+1)*(exp.x-1))/exp.x)/(((exp.x-1) *(exp.x-1))/exp.x) 
       by B4,B8,TAYLOR_1:20,B2,B3
       .=((exp.x+1)*(exp.x-1))/((exp.x-1) *(exp.x-1)) by XCMPLX_1:55,B7
       .=(exp.x+1)/(exp.x-1) by B9,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A11,A1,FDIFF_1:16;
   end;

:: 29 f.x=(exp.x+1)^2
theorem th29: 
  Z c= dom (( #Z 2)*(exp+f)) & (for x st x in Z holds f.x=1)
  implies ( #Z 2)*(exp+f) is_differentiable_on Z & for x st x in Z holds
  ((( #Z 2)*(exp+f))`|Z).x =2*exp.x*(exp.x+1)
  proof
    assume that A1:Z c= dom (( #Z 2)*(exp+f)) and
A2: (for x st x in Z holds f.x=1);
    for y st y in Z holds y in dom (exp+f) by FUNCT_1:21,A1;
    then A3:Z c= dom (exp+f) by TARSKI:def 3;
    then Z c= dom exp /\ dom f by SEQ_1:def 3;
    then A4:Z c= dom f by XBOOLE_1:18;
A8: for x st x in Z holds f.x=0*x+1 by A2;
    then A5:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A7: for x st x in Z holds ( #Z 2)*(exp+f) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then B1:f is_differentiable_in x by A5,FDIFF_1:16;
      exp is_differentiable_in x by SIN_COS:70;
      then exp+f is_differentiable_in x by FDIFF_1:21,B1;
      hence thesis by TAYLOR_1:3;
    end;
    then A6:( #Z 2)*(exp+f) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    ((( #Z 2)*(exp+f))`|Z).x =2*exp.x*(exp.x+1)
    proof
      let x;
      assume B1:x in Z;
      then B3:f is_differentiable_in x by A5,FDIFF_1:16;
B4:   exp is_differentiable_in x by SIN_COS:70;
      then B2:exp+f is_differentiable_in x by FDIFF_1:21,B3; 
B5:   diff((exp+f),x)=diff(exp,x)+diff(f,x) by FDIFF_1:21,B3,B4
      .=diff(exp,x)+(f`|Z).x by FDIFF_1:def 8,B1,A5
      .=exp.x+(f`|Z).x by SIN_COS:70
      .=exp.x+0 by A8,FDIFF_1:31,A4,B1
      .=exp.x;
B6:   (exp+f).x=exp.x+f.x by SEQ_1:def 3,A3,B1
      .= exp.x+1 by B1,A2;
      ((( #Z 2)*(exp+f))`|Z).x
      =diff((( #Z 2)*(exp+f)),x) by A6,FDIFF_1:def 8,B1
      .=2* ((exp+f).x) #Z (2-1) * diff((exp+f),x) by B2,TAYLOR_1:3
      .=2*(exp.x+1)*exp.x by B6,B5,PREPOWER:45
      .=2*exp.x*(exp.x+1);
      hence thesis;
    end;
    hence thesis by A7,A1,FDIFF_1:16;
  end;

:: 30 f.x=ln((exp.x+1)^2/exp.x)
theorem
  Z c= dom f & f=(log_ number_e)*((( #Z 2)*(exp+f1))/exp) &
  (for x st x in Z holds f1.x=1) implies f is_differentiable_on Z
  & for x st x in Z holds (f`|Z).x =(exp.x-1)/(exp.x+1)
  proof
    assume that A1: Z c= dom f & f=(log_ number_e)*((( #Z 2)*(exp+f1))/exp)
    and A2:for x st x in Z holds f1.x=1;
    for y st y in Z holds y in dom ((( #Z 2)*(exp+f1))/exp) by FUNCT_1:21,A1;
    then A3:Z c= dom ((( #Z 2)*(exp+f1))/exp) by TARSKI:def 3;
    then Z c=dom (( #Z 2)*(exp+f1))/\(dom (exp)\(exp)"{0}) by RFUNCT_1:def 4;
    then A4:Z c= dom (( #Z 2)*(exp+f1)) by XBOOLE_1:18;
    then A5:( #Z 2)*(exp+f1) is_differentiable_on Z & for x st x in Z holds
    ((( #Z 2)*(exp+f1))`|Z).x =2*exp.x*(exp.x+1) by A2,th29;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for x st x in Z holds exp.x<>0 by SIN_COS:59;
    then A7:(( #Z 2)*(exp+f1))/exp is_differentiable_on Z by A8,A5,FDIFF_2:21;
    for y st y in Z holds y in dom (exp+f1) by FUNCT_1:21,A4;
    then A10:Z c= dom (exp+f1) by TARSKI:def 3;
A9: for x st x in Z holds ((( #Z 2)*(exp+f1))/exp).x >0
    proof
      let x;
      assume B1:x in Z;
      then B2:((( #Z 2)*(exp+f1))/exp).x
      =(( #Z 2)*(exp+f1)).x*(exp.x)" by RFUNCT_1:def 4,A3
      .=(( #Z 2)*(exp+f1)).x*(1/exp.x) by XCMPLX_1:217
      .=(( #Z 2)*(exp+f1)).x/exp.x by XCMPLX_1:100
      .=( #Z 2).((exp+f1).x)/exp.x by A4,B1,FUNCT_1:22
      .=((exp+f1).x) #Z 2/exp.x by TAYLOR_1:def 1;
B3:   (exp+f1).x=exp.x + f1.x by SEQ_1:def 3,A10,B1
      .=exp.x +1 by A2,B1;
      exp.x>0 by SIN_COS:59;
      then (exp+f1).x>0 by XREAL_1:36,B3;
      then B4:((exp+f1).x) #Z 2>0 by PREPOWER:49;
      exp.x>0 by SIN_COS:59;
      hence ((( #Z 2)*(exp+f1))/exp).x >0 by B2,XREAL_1:141,B4;
    end;
A11: for x st x in Z holds (log_ number_e)*((( #Z 2)*(exp+f1))/exp)
     is_differentiable_in x
     proof
       let x;
       assume B1:x in Z;
       then B2:(( #Z 2)*(exp+f1))/exp is_differentiable_in x by FDIFF_1:16,A7;
       ((( #Z 2)*(exp+f1))/exp).x >0 by A9,B1;
       hence (log_ number_e)*((( #Z 2)*(exp+f1))/exp) is_differentiable_in x
       by TAYLOR_1:20,B2;
     end;
     then A:f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x =(exp.x-1)/(exp.x+1)
     proof
       let x;
       assume B1:x in Z;
       then B2:(( #Z 2)*(exp+f1))/exp is_differentiable_in x by FDIFF_1:16,A7;
B3:    ((( #Z 2)*(exp+f1))/exp).x >0 by A9,B1;
B7:    exp.x>0 by SIN_COS:59;
       then B12:exp.x +1>0 by XREAL_1:36;
B9:    (exp+f1).x=exp.x + f1.x by SEQ_1:def 3,A10,B1
       .=exp.x +1 by A2,B1;
B11:   (( #Z 2)*(exp+f1)).x
       =( #Z 2).((exp+f1).x) by A4,B1,FUNCT_1:22
       .=(exp.x+1) #Z (1+1) by B9,TAYLOR_1:def 1
       .=(exp.x+1) #Z 1*(exp.x+1) #Z 1 by PREPOWER:54,B12
       .=(exp.x+1) *(exp.x+1) #Z 1 by PREPOWER:45
       .=(exp.x+1) *(exp.x+1) by PREPOWER:45;
B5:    ( #Z 2)*(exp+f1) is_differentiable_in x by A5,B1,FDIFF_1:16;
       exp is_differentiable_in x by SIN_COS:70;
       then B4:diff(((( #Z 2)*(exp+f1))/exp),x)
       =(diff((( #Z 2)*(exp+f1)),x)*exp.x-diff(exp,x)*
       (( #Z 2)*(exp+f1)).x)/(exp.x)^2 by FDIFF_2:14,B5,B7
       .=(((( #Z 2)*(exp+f1))`|Z).x*exp.x-diff(exp,x)*(( #Z 2)*
       (exp+f1)).x)/(exp.x)^2 by FDIFF_1:def 8,B1,A5
       .=(2*exp.x*(exp.x+1)*exp.x-diff(exp,x)*(( #Z 2)*(exp+f1)).x)/(exp.x)^2 
       by A4,A2,th29,B1
       .=(2*exp.x*(exp.x+1)*exp.x-exp.x*(( #Z 2)*(exp+f1)).x)/(exp.x)^2
       by SIN_COS:70
       .=((2*exp.x*(exp.x+1)-(( #Z 2)*(exp+f1)).x)*exp.x)/(exp.x*exp.x) 
       .=(exp.x-1)*(exp.x+1)/exp.x by B11,B7,XCMPLX_1:92;
B8:    ((( #Z 2)*(exp+f1))/exp).x
       =(( #Z 2)*(exp+f1)).x*(exp.x)" by RFUNCT_1:def 4,A3,B1
       .=(( #Z 2)*(exp+f1)).x*(1/exp.x) by XCMPLX_1:217
       .=((exp.x+1) *(exp.x+1))/exp.x by B11,XCMPLX_1:100;
       (f`|Z).x =diff((log_ number_e)*((( #Z 2)*(exp+f1))/exp),x)
       by A,B1,FDIFF_1:def 8,A1
       .=(((exp.x+1)*(exp.x-1))/exp.x)/(((exp.x+1) *(exp.x+1))/exp.x)
       by B4,B8,TAYLOR_1:20,B2,B3
       .=((exp.x+1)*(exp.x-1))/((exp.x+1) *(exp.x+1)) by XCMPLX_1:55,B7
       .=(exp.x-1)/(exp.x+1) by B12,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A11,A1,FDIFF_1:16;
   end;

:: 31 f.x=(1-exp.x)^2
theorem th31: 
  Z c= dom (( #Z 2)*(f-exp)) & (for x st x in Z holds f.x=1)
  implies ( #Z 2)*(f-exp) is_differentiable_on Z & for x st x in Z holds
  ((( #Z 2)*(f-exp))`|Z).x =-2*exp.x*(1-exp.x)
  proof
    assume that A1:Z c= dom (( #Z 2)*(f-exp)) and
A2: (for x st x in Z holds f.x=1);
    for y st y in Z holds y in dom (f-exp) by FUNCT_1:21,A1;
    then A3:Z c= dom (f-exp) by TARSKI:def 3;
    then Z c= dom exp /\ dom f by SEQ_1:def 4;
    then A4:Z c= dom f by XBOOLE_1:18;
A7: for x st x in Z holds f.x=0*x+1 by A2;
    then A5:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A4;
A8: for x st x in Z holds ( #Z 2)*(f-exp) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then B1:f is_differentiable_in x by A5,FDIFF_1:16;
      exp is_differentiable_in x by SIN_COS:70;
      then f-exp is_differentiable_in x by FDIFF_1:22,B1;
      hence thesis by TAYLOR_1:3;
    end;
    then A6:( #Z 2)*(f-exp) is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds
    ((( #Z 2)*(f-exp))`|Z).x =-2*exp.x*(1-exp.x)
    proof
      let x;
      assume B1:x in Z;
      then B3:f is_differentiable_in x by A5,FDIFF_1:16;
B4:   exp is_differentiable_in x by SIN_COS:70;
      then B2:f-exp is_differentiable_in x by FDIFF_1:22,B3; 
B5:   diff((f-exp),x)=diff(f,x)-diff(exp,x) by FDIFF_1:22,B3,B4
      .=(f`|Z).x-diff(exp,x) by FDIFF_1:def 8,B1,A5
      .=(f`|Z).x-exp.x by SIN_COS:70
      .=0-exp.x by A7,FDIFF_1:31,A4,B1
      .=-exp.x;
B6:   (f-exp).x=f.x-exp.x by SEQ_1:def 4,A3,B1
      .=1- exp.x by B1,A2;
      ((( #Z 2)*(f-exp))`|Z).x
      =diff((( #Z 2)*(f-exp)),x) by A6,FDIFF_1:def 8,B1
      .=2* ((f-exp).x) #Z (2-1) * diff((f-exp),x) by B2,TAYLOR_1:3
      .=2*(1-exp.x)*(-exp.x) by B6,B5,PREPOWER:45
      .=-2*exp.x*(1-exp.x);
      hence thesis;
    end;
    hence thesis by A8,A1,FDIFF_1:16;
  end;

:: 32 f.x=ln(exp.x/(1-exp.x)^2)
theorem
  Z c= dom f & f=(log_ number_e)*(exp/(( #Z 2)*(f1-exp))) &
  (for x st x in Z holds f1.x=1 & (f1-exp).x>0) implies
  f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x =(1+exp.x)/(1-exp.x)
  proof
    assume that A1: Z c= dom f & f=(log_ number_e)*(exp/(( #Z 2)*(f1-exp)))
    and A6:(for x st x in Z holds f1.x=1 & (f1-exp).x>0);
A2: for x st x in Z holds f1.x=1 by A6;
    for y st y in Z holds y in dom (exp/(( #Z 2)*(f1-exp))) by FUNCT_1:21,A1;
    then A3:Z c= dom (exp/(( #Z 2)*(f1-exp))) by TARSKI:def 3;
    then Z c=dom exp/\(dom ((( #Z 2)*(f1-exp)))\((( #Z 2)*(f1-exp)))"{0}) 
    by RFUNCT_1:def 4;
    then B1:Z c=dom ((( #Z 2)*(f1-exp)))\((( #Z 2)*(f1-exp)))"{0}
      by XBOOLE_1:18;
    dom ((( #Z 2)*(f1-exp))^) c= dom (( #Z 2)*(f1-exp)) by RFUNCT_1:11;
    then dom (( #Z 2)*(f1-exp)) \ (( #Z 2)*(f1-exp))"{0} c=
      dom (( #Z 2)*(f1-exp)) by RFUNCT_1:def 8;
    then A4:Z c= dom (( #Z 2)*(f1-exp)) by XBOOLE_1:1,B1;
    then A5:( #Z 2)*(f1-exp) is_differentiable_on Z & for x st x in Z holds
    ((( #Z 2)*(f1-exp))`|Z).x =-2*exp.x*(1-exp.x) by A2,th31;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A9: for x st x in Z holds (( #Z 2)*(f1-exp)).x>0
    proof 
      let x;
      assume B1:x in Z;
B2:   ((( #Z 2)*(f1-exp))).x
      =( #Z 2).((f1-exp).x) by A4,B1,FUNCT_1:22
      .=((f1-exp).x) #Z 2 by TAYLOR_1:def 1;
      (f1-exp).x>0 by A6,B1;
      hence thesis by B2,PREPOWER:49;
    end;
    for x st x in Z holds (( #Z 2)*(f1-exp)).x<>0 by A9;
    then A7:exp/(( #Z 2)*(f1-exp)) is_differentiable_on Z by A8,A5,FDIFF_2:21;
    for y st y in Z holds y in dom (f1-exp) by FUNCT_1:21,A4;
    then A10:Z c= dom (f1-exp) by TARSKI:def 3;
A11: for x st x in Z holds (exp/(( #Z 2)*(f1-exp))).x >0
     proof
       let x;
       assume B1:x in Z;
       then B2:(exp/(( #Z 2)*(f1-exp))).x
       =exp.x*((( #Z 2)*(f1-exp)).x)" by RFUNCT_1:def 4,A3
       .=exp.x*(1/(( #Z 2)*(f1-exp)).x) by XCMPLX_1:217
       .=exp.x/(( #Z 2)*(f1-exp)).x by XCMPLX_1:100;
B4:    (( #Z 2)*(f1-exp)).x>0 by A9,B1;
       exp.x>0 by SIN_COS:59;
       hence (exp/(( #Z 2)*(f1-exp))).x >0 by B2,XREAL_1:141,B4;
     end;
A12: for x st x in Z holds (log_ number_e)*(exp/(( #Z 2)*(f1-exp)))
     is_differentiable_in x
     proof
       let x;
       assume B1:x in Z;
       then B2:exp/(( #Z 2)*(f1-exp)) is_differentiable_in x by FDIFF_1:16,A7;
       (exp/(( #Z 2)*(f1-exp))).x >0 by A11,B1;
       hence (log_ number_e)*(exp/(( #Z 2)*(f1-exp))) is_differentiable_in x
       by TAYLOR_1:20,B2;
     end;
     then A:f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x =(1+exp.x)/(1-exp.x)
     proof
       let x;
       assume B1:x in Z;
       then B2:(( #Z 2)*(f1-exp)).x<>0 by A9;
B5:    ( #Z 2)*(f1-exp) is_differentiable_in x by A5,B1,FDIFF_1:16;
B6:    exp is_differentiable_in x by SIN_COS:70;
B9:    exp/(( #Z 2)*(f1-exp)) is_differentiable_in x by FDIFF_1:16,A7,B1;
B10:   (exp/(( #Z 2)*(f1-exp))).x >0 by A11,B1;
B12:   (f1-exp).x>0 by B1,A6;
B13:   (f1-exp).x=f1.x-exp.x by SEQ_1:def 4,B1,A10
       .=1-exp.x by A6,B1;
       then 1-exp.x>0 by B1,A6;
       then B11:(1-exp.x) #Z 2>0 by PREPOWER:49;
       exp.x>0 by SIN_COS:59;
       then B16:exp.x/(1-exp.x) #Z 2<>0 by XREAL_1:141,B11;
B3:    ((( #Z 2)*(f1-exp))).x
       =( #Z 2).((f1-exp).x) by A4,B1,FUNCT_1:22
       .=((f1-exp).x) #Z 2 by TAYLOR_1:def 1
       .=(f1.x-exp.x) #Z 2 by SEQ_1:def 4,B1,A10
       .=(1-exp.x) #Z 2 by B1,A6;
B4:    diff((exp/(( #Z 2)*(f1-exp))),x)
       =(diff(exp,x)*(( #Z 2)*(f1-exp)).x-diff((( #Z 2)*(f1-exp)),x)*exp.x)
       /((( #Z 2)*(f1-exp)).x)^2 by FDIFF_2:14,B5,B6,B2
       .=(exp.x*(( #Z 2)*(f1-exp)).x-diff((( #Z 2)*(f1-exp)),x)*exp.x)
       /((( #Z 2)*(f1-exp)).x)^2 by SIN_COS:70
       .=(exp.x*(( #Z 2)*(f1-exp)).x-((( #Z 2)*(f1-exp))`|Z).x*exp.x)
       /((( #Z 2)*(f1-exp)).x)^2 by FDIFF_1:def 8,B1,A5
       .=(exp.x*((1-exp.x) #Z 2)-(-2*exp.x*(1-exp.x))*exp.x)/((1-exp.x) #Z 2)^2 
       by B3,A4,A2,th31,B1
       .=(exp.x*((1-exp.x) #Z 2+2*(1-exp.x)*exp.x))
       /((1-exp.x) #Z 2*(1-exp.x) #Z 2)
       .=((exp.x/(1-exp.x) #Z 2)*((1-exp.x) #Z 2+2*(1-exp.x)*exp.x))
       /(1-exp.x) #Z 2 by XCMPLX_1:84;
B8:    (exp/(( #Z 2)*(f1-exp))).x
       =exp.x*((( #Z 2)*(f1-exp)).x)" by RFUNCT_1:def 4,A3,B1
       .=exp.x*(1/(( #Z 2)*(f1-exp)).x) by XCMPLX_1:217
       .=exp.x/(1-exp.x) #Z 2 by B3,XCMPLX_1:100;
B15:   (1-exp.x) #Z 2=(1-exp.x) #Z (1+1)
       .=(1-exp.x) #Z 1 * (1-exp.x) #Z 1 by PREPOWER:54,B13,B12
       .=(1-exp.x) * (1-exp.x) #Z 1 by PREPOWER:45
       .=(1-exp.x) * (1-exp.x) by PREPOWER:45;
       (f`|Z).x =diff((log_ number_e)*(exp/(( #Z 2)*(f1-exp))),x)
       by A,B1,FDIFF_1:def 8,A1
       .=((exp.x/(1-exp.x) #Z 2)*((1-exp.x) #Z 2+2*(1-exp.x)*exp.x))
       /(1-exp.x) #Z 2/(exp.x/(1-exp.x) #Z 2) by B4,B8,TAYLOR_1:20,B9,B10
       .=((exp.x/(1-exp.x) #Z 2)*((1-exp.x) #Z 2+2*(1-exp.x)*exp.x))
       /((exp.x/(1-exp.x) #Z 2)*(1-exp.x) #Z 2) by XCMPLX_1:79
       .=((1-exp.x) * (1+exp.x))/((1-exp.x) * (1-exp.x)) by B15,B16,XCMPLX_1:92
       .=(1+exp.x)/ (1-exp.x) by B13,B12,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A12,A1,FDIFF_1:16;
   end;

:: 33 f.x=ln(exp.x/(1+exp.x)^2)
theorem
  Z c= dom f & f=(log_ number_e)*(exp/(( #Z 2)*(f1+exp))) &
  (for x st x in Z holds f1.x=1) implies f is_differentiable_on Z
  & for x st x in Z holds (f`|Z).x =(1-exp.x)/(1+exp.x)
  proof
    assume that A1: Z c= dom f & f=(log_ number_e)*(exp/(( #Z 2)*(f1+exp))) 
    and A2:for x st x in Z holds f1.x=1;
    for y st y in Z holds y in dom (exp/(( #Z 2)*(f1+exp))) by FUNCT_1:21,A1;
    then A3:Z c= dom (exp/(( #Z 2)*(f1+exp))) by TARSKI:def 3;
    then Z c=dom exp/\(dom ((( #Z 2)*(f1+exp)))\((( #Z 2)*(f1+exp)))"{0}) 
    by RFUNCT_1:def 4;then
B1: Z c=dom ((( #Z 2)*(f1+exp)))\((( #Z 2)*(f1+exp)))"{0} by XBOOLE_1:18;
    dom ((( #Z 2)*(f1+exp))^) c= dom (( #Z 2)*(f1+exp)) by RFUNCT_1:11;
    then dom (( #Z 2)*(f1+exp)) \ (( #Z 2)*(f1+exp))"{0} c=
    dom (( #Z 2)*(f1+exp)) by RFUNCT_1:def 8;
    then A4:Z c= dom (( #Z 2)*(f1+exp)) by XBOOLE_1:1,B1;
    then A5:( #Z 2)*(f1+exp) is_differentiable_on Z & for x st x in Z holds
    ((( #Z 2)*(f1+exp))`|Z).x =2*exp.x*(1+exp.x) by A2,th29;
A8: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
    for y st y in Z holds y in dom (f1+exp) by FUNCT_1:21,A4;
    then A10:Z c= dom (f1+exp) by TARSKI:def 3;
A9: for x st x in Z holds (( #Z 2)*(f1+exp)).x>0
    proof 
      let x;
      assume B1:x in Z;
      then B2:((( #Z 2)*(f1+exp))).x
      =( #Z 2).((f1+exp).x) by A4,FUNCT_1:22
      .=((f1+exp).x) #Z 2 by TAYLOR_1:def 1;
B3:   (f1+exp).x=f1.x+exp.x by SEQ_1:def 3,A10,B1
      .=1+exp.x by A2,B1;
      exp.x>0 by SIN_COS:59;
      then (f1+exp).x>0 by XREAL_1:36,B3;
      hence thesis by B2,PREPOWER:49;
    end;
    for x st x in Z holds (( #Z 2)*(f1+exp)).x<>0 by A9;
    then A7:exp/(( #Z 2)*(f1+exp)) is_differentiable_on Z by A8,A5,FDIFF_2:21;
A11: for x st x in Z holds (exp/(( #Z 2)*(f1+exp))).x >0
     proof
       let x;
       assume B1:x in Z;
       then B2:(exp/(( #Z 2)*(f1+exp))).x
       =exp.x*((( #Z 2)*(f1+exp)).x)" by RFUNCT_1:def 4,A3
       .=exp.x*(1/(( #Z 2)*(f1+exp)).x) by XCMPLX_1:217
       .=exp.x/(( #Z 2)*(f1+exp)).x by XCMPLX_1:100;
B4:    (( #Z 2)*(f1+exp)).x>0 by A9,B1;
       exp.x>0 by SIN_COS:59;
       hence (exp/(( #Z 2)*(f1+exp))).x >0 by B2,XREAL_1:141,B4;
     end;
A12: for x st x in Z holds (log_ number_e)*(exp/(( #Z 2)*(f1+exp)))
     is_differentiable_in x
     proof
       let x;
       assume B1:x in Z;
       then B2:exp/(( #Z 2)*(f1+exp)) is_differentiable_in x by FDIFF_1:16,A7;
       (exp/(( #Z 2)*(f1+exp))).x >0 by A11,B1;
       hence (log_ number_e)*(exp/(( #Z 2)*(f1+exp))) is_differentiable_in x
       by TAYLOR_1:20,B2;
     end;
     then A:f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds (f`|Z).x =(1-exp.x)/(1+exp.x)
     proof
       let x;
       assume B1:x in Z;
       then B2:(( #Z 2)*(f1+exp)).x<>0 by A9;
B5:    ( #Z 2)*(f1+exp) is_differentiable_in x by A5,B1,FDIFF_1:16;
B6:    exp is_differentiable_in x by SIN_COS:70;
B9:    exp/(( #Z 2)*(f1+exp)) is_differentiable_in x by FDIFF_1:16,A7,B1;
B10:   (exp/(( #Z 2)*(f1+exp))).x >0 by A11,B1;
B11:   exp.x>0 by SIN_COS:59;
       then B14:1+exp.x>0 by XREAL_1:36;
       then (1+exp.x) #Z 2>0 by PREPOWER:49;
       then B16:exp.x/(1+exp.x) #Z 2<>0 by XREAL_1:141,B11;
B3:    ((( #Z 2)*(f1+exp))).x
       =( #Z 2).((f1+exp).x) by A4,B1,FUNCT_1:22
       .=((f1+exp).x) #Z 2 by TAYLOR_1:def 1
       .=(f1.x+exp.x) #Z 2 by SEQ_1:def 3,B1,A10
       .=(1+exp.x) #Z 2 by B1,A2;
B4:    diff((exp/(( #Z 2)*(f1+exp))),x)
       =(diff(exp,x)*(( #Z 2)*(f1+exp)).x-diff((( #Z 2)*(f1+exp)),x)*exp.x)
       /((( #Z 2)*(f1+exp)).x)^2 by FDIFF_2:14,B5,B6,B2
       .=(exp.x*(( #Z 2)*(f1+exp)).x-diff((( #Z 2)*(f1+exp)),x)*exp.x)
       /((( #Z 2)*(f1+exp)).x)^2 by SIN_COS:70
       .=(exp.x*(( #Z 2)*(f1+exp)).x-((( #Z 2)*(f1+exp))`|Z).x*exp.x)
       /((( #Z 2)*(f1+exp)).x)^2 by FDIFF_1:def 8,B1,A5
       .=(exp.x*(1+exp.x) #Z 2-2*exp.x*(1+exp.x)*exp.x)/((1+exp.x) #Z 2)^2
       by B3,A4,A2,th29,B1
       .=(exp.x*((1+exp.x) #Z 2-2*(1+exp.x)*exp.x))
       /((1+exp.x) #Z 2*(1+exp.x) #Z 2)
       .=((exp.x/(1+exp.x) #Z 2)*((1+exp.x) #Z 2-2*(1+exp.x)*exp.x))
       /(1+exp.x) #Z 2 by XCMPLX_1:84;
B8:    (exp/(( #Z 2)*(f1+exp))).x
       =exp.x*((( #Z 2)*(f1+exp)).x)" by RFUNCT_1:def 4,A3,B1
       .=exp.x*(1/(( #Z 2)*(f1+exp)).x) by XCMPLX_1:217
       .=exp.x/(1+exp.x) #Z 2 by B3, XCMPLX_1:100;
B15:   (1+exp.x) #Z 2=(1+exp.x) #Z (1+1)
       .=(1+exp.x) #Z 1 * (1+exp.x) #Z 1 by PREPOWER:54,B14
       .=(1+exp.x) * (1+exp.x) #Z 1 by PREPOWER:45
       .=(1+exp.x) * (1+exp.x) by PREPOWER:45;
       (f`|Z).x =diff((log_ number_e)*(exp/(( #Z 2)*(f1+exp))),x)
       by A,B1,FDIFF_1:def 8,A1
       .=((exp.x/(1+exp.x) #Z 2)*((1+exp.x) #Z 2-2*(1+exp.x)*exp.x))
       /(1+exp.x) #Z 2/(exp.x/(1+exp.x) #Z 2) by B4,B8,TAYLOR_1:20,B9,B10
       .=((exp.x/(1+exp.x) #Z 2)*((1+exp.x) #Z 2-2*(1+exp.x)*exp.x))
       /((exp.x/(1+exp.x) #Z 2)*(1+exp.x) #Z 2) by XCMPLX_1:79
       .=((1+exp.x) * (1-exp.x))/((1+exp.x) * (1+exp.x)) by B15,B16,XCMPLX_1:92
       .=(1-exp.x)/ (1+exp.x) by B14,XCMPLX_1:92;
       hence thesis;
     end;
     hence thesis by A12,A1,FDIFF_1:16;
   end;

:: 34 f.x=ln(exp(x)+exp(-x))
theorem  
  Z c= dom ((log_ number_e)*f) & f=exp+(exp*f1) & (for x st x in Z holds 
  f1.x=-x) implies (log_ number_e)*f is_differentiable_on Z & for x st x in Z 
  holds (((log_ number_e)*f)`|Z).x =(exp(x)-exp(-x))/(exp(x)+exp(-x))
  proof
    assume that A1:Z c= dom ((log_ number_e)*f) & f=exp+(exp*f1) and
A2: (for x st x in Z holds f1.x=-x);
    for y st y in Z holds y in dom f by FUNCT_1:21,A1;
    then A3:Z c= dom (exp+(exp*f1)) by A1,TARSKI:def 3;
    then Z c= dom exp /\ dom (exp*f1) by SEQ_1:def 3;
    then A4:Z c= dom (exp*f1) by XBOOLE_1:18;
    then A5:exp*f1 is_differentiable_on Z &
    for x st x in Z holds ((exp*f1)`|Z).x = -exp(-x) by A2,th13;
A6: exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A16: for x st x in Z holds ((exp+(exp*f1))`|Z).x =exp(x)-exp(-x)
     proof
       let x;
       assume B1:x in Z;
       hence ((exp+(exp*f1))`|Z).x = diff(exp,x) + diff((exp*f1),x) 
       by A6,A3,A5,FDIFF_1:26
       .=exp.x+ diff((exp*f1),x) by SIN_COS:70
       .=exp.x+((exp*f1)`|Z).x by B1,FDIFF_1:def 8,A5
       .=exp.x+(-exp(-x)) by B1,A4,A2,th13
       .=exp(x)+(-exp(-x)) by SIN_COS:def 27
       .=exp(x)-exp(-x);
     end;
     then A8:f is_differentiable_on Z & for x st x in Z holds 
     (f`|Z).x =exp(x)-exp(-x) by A6,A5,A3,FDIFF_1:26,A1;
A15: for x st x in Z holds (exp+(exp*f1)).x>0
     proof
       let x;
       assume B1:x in Z;
       then B2:(exp+(exp*f1)).x=exp.x + (exp*f1).x by SEQ_1:def 3,A3
       .=exp.x +exp.(f1.x) by FUNCT_1:22,A4,B1
       .=exp.x +exp.(-x) by A2,B1
       .=exp(x) +exp.(-x) by SIN_COS:def 27
       .=exp(x) +exp(-x) by SIN_COS:def 27;
       exp(x)>0 & exp(-x)>0 by SIN_COS:60;
       hence thesis by B2,XREAL_1:36;
     end;
A17: for x st x in Z holds (log_ number_e)*f is_differentiable_in x
     proof
       let x;
       assume B1:x in Z;
       then B2:f is_differentiable_in x by FDIFF_1:16,A8;
       f.x >0 by B1,A15,A1;
       hence thesis by TAYLOR_1:20,B2;
     end;
     then A:(log_ number_e)*f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds 
     (((log_ number_e)*f)`|Z).x =(exp(x)-exp(-x))/(exp(x)+exp(-x))
     proof
       let x;
       assume B1:x in Z;
B2:    f.x=exp.x + (exp*f1).x by SEQ_1:def 3,A3,B1,A1
       .=exp.x +exp.(f1.x) by FUNCT_1:22,A4,B1
       .=exp.x +exp.(-x) by A2,B1
       .=exp(x) +exp.(-x) by SIN_COS:def 27
       .=exp(x) +exp(-x) by SIN_COS:def 27;
A10:   f is_differentiable_in x by FDIFF_1:16,A8,B1;
A14:   f.x>0 by B1,A15,A1;
       diff(((log_ number_e)*f),x)
       =diff(f,x)/f.x by TAYLOR_1:20,A10,A14
       .=(f`|Z).x/f.x by A8,B1,FDIFF_1:def 8 
       .=(exp(x)-exp(-x))/(exp(x) +exp(-x)) by A16,A1,B1,B2;
       hence thesis by B1,A,FDIFF_1:def 8;
     end;
     hence thesis by A17,A1,FDIFF_1:16;
   end;

:: 35 f.x=ln(exp(x)-exp(-x))
theorem
  Z c= dom ((log_ number_e)*f) & f=exp-(exp*f1) & (for x st x in Z holds
  f1.x=-x & f.x>0) implies (log_ number_e)*f is_differentiable_on Z & for x
  st x in Z holds (((log_ number_e)*f)`|Z).x=(exp(x)+exp(-x))/(exp(x)-exp(-x))
  proof
    assume that A1:Z c= dom ((log_ number_e)*f) & f=exp-(exp*f1) and
A11: (for x st x in Z holds f1.x=-x & f.x>0);
A2:  for x st x in Z holds f1.x=-x by A11;
     for y st y in Z holds y in dom f by FUNCT_1:21,A1;
     then A3:Z c= dom (exp-(exp*f1)) by A1,TARSKI:def 3;
     then Z c= dom exp /\ dom (exp*f1) by SEQ_1:def 4;
     then A4:Z c= dom (exp*f1) by XBOOLE_1:18;
     then A5:exp*f1 is_differentiable_on Z &
     for x st x in Z holds ((exp*f1)`|Z).x = -exp(-x) by A2,th13;
A6:  exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
A15: for x st x in Z holds ((exp-(exp*f1))`|Z).x =exp(x)+exp(-x)
     proof
       let x;
       assume B1:x in Z;
       hence ((exp-(exp*f1))`|Z).x = diff(exp,x) - diff((exp*f1),x)
       by A6,A3,A5,FDIFF_1:27
       .=exp.x- diff((exp*f1),x) by SIN_COS:70
       .=exp.x-((exp*f1)`|Z).x by B1,FDIFF_1:def 8,A5
       .=exp.x-(-exp(-x)) by B1,A4,A2,th13
       .=exp.x+exp(-x)
       .=exp(x)+exp(-x) by SIN_COS:def 27;
     end;
     then A8:f is_differentiable_on Z & for x st x in Z holds 
     (f`|Z).x =exp(x)+exp(-x) by A6,A5,A3,FDIFF_1:27,A1;
A16: for x st x in Z holds (log_ number_e)*f is_differentiable_in x
     proof
       let x;
       assume B1:x in Z;
       then B2:f is_differentiable_in x by FDIFF_1:16,A8;
       f.x >0 by B1,A11;
       hence thesis by TAYLOR_1:20,B2;
     end;
     then A:(log_ number_e)*f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds 
     (((log_ number_e)*f)`|Z).x =(exp(x)+exp(-x))/(exp(x)-exp(-x))
     proof
       let x;
       assume B1:x in Z;
B2:    f.x=exp.x - (exp*f1).x by SEQ_1:def 4,A3,B1,A1
       .=exp.x -exp.(f1.x) by FUNCT_1:22,A4,B1
       .=exp.x -exp.(-x) by A11,B1
       .=exp(x) -exp.(-x) by SIN_COS:def 27
       .=exp(x) -exp(-x) by SIN_COS:def 27;
A10:   f is_differentiable_in x by FDIFF_1:16,A8,B1;
A14:   f.x>0 by B1,A11;
       diff(((log_ number_e)*f),x)
       =diff(f,x)/f.x by TAYLOR_1:20,A10,A14
       .=(f`|Z).x/f.x by A8,B1,FDIFF_1:def 8 
       .=(exp(x)+exp(-x))/(exp(x) -exp(-x)) by A15,A1,B1,B2;
       hence thesis by B1,A,FDIFF_1:def 8;
     end;
     hence thesis by A16,A1,FDIFF_1:16;
   end;

:: 36 f.x=(2/3)*((1+exp.x) #R (3/2))
theorem 
  Z c= dom ((2/3)(#)(( #R (3/2))*(f+exp))) & (for x st x in Z holds f.x=1) 
  implies (2/3)(#)(( #R (3/2))*(f+exp)) is_differentiable_on Z &
  for x st x in Z
  holds (((2/3)(#)(( #R (3/2))*(f+exp)))`|Z).x =exp.x*(1+exp.x) #R (1/2)
  proof
    assume that A1:Z c= dom ((2/3)(#)(( #R (3/2))*(f+exp))) and
A2: (for x st x in Z holds f.x=1);
A3: Z c= dom (( #R (3/2))*(f+exp)) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom (f+exp) by FUNCT_1:21,A3;
    then A4:Z c= dom (f+exp) by TARSKI:def 3;
    then Z c= dom exp /\ dom f by SEQ_1:def 3;
    then A11:Z c= dom f by XBOOLE_1:18;
A12: for x st x in Z holds f.x=0*x+1 by A2;
     then A5:f is_differentiable_on Z & 
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A11;
A6:  exp is_differentiable_on Z by TAYLOR_1:16,FDIFF_1:34;
     then A7:f+exp is_differentiable_on Z by FDIFF_1:26,A5,A4;
A8:  for x st x in Z holds (f+exp).x>0
     proof
       let x;
       assume B1:x in Z;
       then B2:(f+exp).x=f.x+exp.x by A4,SEQ_1:def 3
       .=1+exp.x by A2,B1;
       exp.x>0 by SIN_COS:59;
       hence thesis by B2,XREAL_1:36;
     end;
     now let x;
       assume B1:x in Z;
       then B2:f+exp is_differentiable_in x by FDIFF_1:16,A7;
       (f+exp).x>0 by A8,B1;
       hence ( #R (3/2))*(f+exp) is_differentiable_in x by TAYLOR_1:22,B2;
     end;
     then A9:( #R (3/2))*(f+exp) is_differentiable_on Z by A3,FDIFF_1:16;
     for x st x in Z
     holds (((2/3)(#)(( #R (3/2))*(f+exp)))`|Z).x =exp.x*(1+exp.x) #R (1/2)
     proof
       let x;
       assume B1:x in Z;
       then B2:f+exp is_differentiable_in x by FDIFF_1:16,A7;
B3:    (f+exp).x>0 by A8,B1;
B4:    (f+exp).x=f.x+exp.x by A4,B1,SEQ_1:def 3
       .=1+exp.x by A2,B1;
B5:    ((f+exp)`|Z).x = diff(f,x)+diff(exp,x) by A6,A4,A5,FDIFF_1:26,B1
       .=diff(f,x)+exp.x by SIN_COS:70
       .=(f`|Z).x+exp.x by B1,FDIFF_1:def 8,A5
       .=0+exp.x by B1,A12,FDIFF_1:31,A11
       .=exp.x;
       (((2/3)(#)(( #R (3/2))*(f+exp)))`|Z).x 
       =(2/3)*diff((( #R (3/2))*(f+exp)),x) by A1,FDIFF_1:28,B1,A9
       .=(2/3)*((3/2)*(((f+exp).x) #R (3/2-1)) * diff((f+exp),x)) 
       by TAYLOR_1:22,B2,B3
       .=(2/3)*((3/2)*(((f+exp).x) #R (3/2-1)) *((f+exp)`|Z).x )
       by FDIFF_1:def 8,A7,B1
       .=(2/3)*(3/2)*(((f+exp).x) #R (3/2-1)) *((f+exp)`|Z).x 
       .=exp.x*(1+exp.x) #R (1/2) by B5,B4;
       hence thesis;
     end;
     hence thesis by A1,FDIFF_1:28,A9;
   end;

:: 37 f.x=(2/(3*lna))*((1+a |^x) #R (3/2))
theorem 
  Z c= dom ((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp*f1)))) & 
  (for x st x in Z holds f.x=1 & f1.x=x*log(number_e,a)) & a>0 & a<>1 
  implies (2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp*f1))) 
  is_differentiable_on Z & for x st x in Z holds 
  (((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp*f1))))`|Z).x
  =a #R x * (1+a #R x) #R (1/2)
  proof
    assume that
A1: Z c= dom ((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp*f1)))) and
A12:(for x st x in Z holds f.x=1 & f1.x=x*log(number_e,a)) and
A13: a>0 and
A15: a<>1;
A14: for x st x in Z holds f1.x=x*log(number_e,a) by A12; 
A3:  Z c= dom (( #R (3/2))*(f+(exp*f1))) by SEQ_1:def 6,A1;
     for y st y in Z holds y in dom (f+(exp*f1)) by FUNCT_1:21,A3;
     then A4:Z c= dom (f+(exp*f1)) by TARSKI:def 3;
     then Z c= dom (exp*f1) /\ dom f by SEQ_1:def 3;
     then A11:Z c= dom f & Z c= dom (exp*f1) by XBOOLE_1:18;
A16: for x st x in Z holds f.x=0*x+1 by A12;
     then A5:f is_differentiable_on Z & 
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A11;
A6:  exp*f1 is_differentiable_on Z & for x st x in Z holds 
     ((exp*f1)`|Z).x =a #R x*log(number_e,a) by th10,A11,A13,A14;
     then A7:f+(exp*f1) is_differentiable_on Z by FDIFF_1:26,A5,A4;
A8:  for x st x in Z holds (f+(exp*f1)).x>0
     proof
       let x;
       assume B1:x in Z;
       then B2:(f+(exp*f1)).x=f.x+(exp*f1).x by A4,SEQ_1:def 3
       .=f.x+exp.(f1.x) by B1,FUNCT_1:22,A11
       .=1+exp.(f1.x) by A12,B1
       .=1+exp.(x*log(number_e,a)) by A12,B1;
       exp.(x*log(number_e,a))>0 by SIN_COS:59;
       hence thesis by B2,XREAL_1:36;
     end;
     now let x;
       assume B1:x in Z;
       then B2:f+(exp*f1) is_differentiable_in x by FDIFF_1:16,A7;
       (f+(exp*f1)).x>0 by A8,B1;
       hence ( #R (3/2))*(f+(exp*f1)) is_differentiable_in x by TAYLOR_1:22,B2;
     end;
     then A9:( #R (3/2))*(f+(exp*f1)) is_differentiable_on Z by A3,FDIFF_1:16;
A10: log(number_e,a)<>0
     proof
       assume B1:log(number_e,a)=0;
B2:    log(number_e,a)=log(number_e,1) by SIN_COS2:13,B1,TAYLOR_1:13;
B3:    number_e>0 & number_e<>1 by TAYLOR_1:11;
       then a=(number_e) to_power log(number_e,1) by B2,POWER:def 3,A13
       .=1 by POWER:def 3,B3;
       hence contradiction by A15;
     end;
     for x st x in Z holds
     (((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp*f1))))`|Z).x
     =a #R x * (1+a #R x) #R (1/2)
     proof
       let x;
       assume B1:x in Z;
       then B2:f+(exp*f1) is_differentiable_in x by FDIFF_1:16,A7;
B3:    (f+(exp*f1)).x>0 by A8,B1;
B4:    (f+(exp*f1)).x=f.x+(exp*f1).x by A4,B1,SEQ_1:def 3
       .=f.x+exp.(f1.x) by B1,FUNCT_1:22,A11
       .=1+exp.(f1.x) by A12,B1
       .=1+exp.(x*log(number_e,a)) by A12,B1
       .=1+a #R x by th9,A13;
B5:    ((f+(exp*f1))`|Z).x=diff(f,x)+diff((exp*f1),x) by A6,A4,A5,FDIFF_1:26,B1
       .=diff(f,x)+((exp*f1)`|Z).x by B1,FDIFF_1:def 8,A6
       .=(f`|Z).x+((exp*f1)`|Z).x by B1,FDIFF_1:def 8,A5
       .=0+((exp*f1)`|Z).x by B1,A16,FDIFF_1:31,A11
       .=a #R x*log(number_e,a) by B1,th10,A11,A13,A14;
B6:    diff((( #R (3/2))*(f+(exp*f1))),x)
       =(3/2)*(((f+(exp*f1)).x) #R (3/2-1)) * diff((f+(exp*f1)),x)
       by TAYLOR_1:22,B2,B3
       .=(3/2)*((1+a #R x) #R (1/2)) *(a #R x*log(number_e,a)) 
       by B5,B4,FDIFF_1:def 8,A7,B1
       .=(3*log(number_e,a))/2*a #R x*(1+a #R x) #R (1/2);
       log(number_e,a)>0 or log(number_e,a)<0 by A10;
       then B8:3*log(number_e,a) <> 0;
       (((2/(3*log(number_e,a)))(#)(( #R (3/2))*(f+(exp*f1))))`|Z).x
       =(2/(3*log(number_e,a)))*((3*log(number_e,a))/2*a #R
       x*(1+a #R x) #R (1/2)) by B6,A1,FDIFF_1:28,B1,A9
       .=(2/(3*log(number_e,a)))*((3*log(number_e,a))/2)
       *a #R x*(1+a #R x) #R (1/2)
       .=1*a #R x*(1+a #R x) #R (1/2) by B8,XCMPLX_1:113
       .=a #R x*(1+a #R x) #R (1/2);
       hence thesis;
     end;
     hence thesis by A1,A9,FDIFF_1:28;
   end;

:: 38 f.x=(-1/2)*cos(2*x)
theorem 
  Z c= dom ((-1/2)(#)(cos*f)) & (for x st x in Z holds f.x=2*x)
  implies (-1/2)(#)(cos*f) is_differentiable_on Z &
  for x st x in Z holds (((-1/2)(#)(cos*f))`|Z).x =sin(2*x)
  proof
    assume that A1:Z c= dom ((-1/2)(#)(cos*f)) and
A2: (for x st x in Z holds f.x=2*x);
A3: Z c= dom (cos*f) by SEQ_1:def 6,A1;
A4: for x st x in Z holds f.x=2*x+0 by A2;
    then A5:cos*f is_differentiable_on Z &
    for x st x in Z holds ((cos*f)`|Z).x = -2* sin.(2*x+0) by A3,FDIFF_4:38;
    for x st x in Z holds (((-1/2)(#)(cos*f))`|Z).x =sin(2*x)
    proof
      let x;
      assume B1:x in Z;
      then (((-1/2)(#)(cos*f))`|Z).x =(-1/2)*diff((cos*f),x)
        by A5,A1,FDIFF_1:28
      .=(-1/2)*((cos*f)`|Z).x by A5,B1,FDIFF_1:def 8
      .=(-1/2)*(-2* sin.(2*x+0)) by A4,A3,FDIFF_4:38,B1
      .=sin(2*x) by SIN_COS:def 21;
      hence thesis;
    end;
    hence thesis by A5,A1,FDIFF_1:28;
  end;

theorem 
  Z c= dom (2(#)(( #R (1/2))*(f-cos))) & (for x st x in Z holds f.x=1 
  & sin.x>0 & cos.x<1 & cos.x>-1) 
  implies 2(#)(( #R (1/2))*(f-cos)) is_differentiable_on Z &
  for x st x in Z holds
  ((2(#)(( #R (1/2))*(f-cos)))`|Z).x =(1+cos.x) #R (1/2)
  proof
    assume that A1:Z c= dom (2(#)(( #R (1/2))*(f-cos))) and
A11: (for x st x in Z holds f.x=1 & sin.x>0 & cos.x<1 & cos.x>-1);
A3:  Z c= dom (( #R (1/2))*(f-cos)) by SEQ_1:def 6,A1;
     for y st y in Z holds y in dom (f-cos) by FUNCT_1:21,A3;
     then A4:Z c= dom (f-cos) by TARSKI:def 3;
     then Z c= dom cos /\ dom f by SEQ_1:def 4;
     then A5:Z c= dom f by XBOOLE_1:18;
A14: for x st x in Z holds f.x=0*x+1 by A11;
     then A7:f is_differentiable_on Z & 
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A5;
A8:  cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
     then A9:f-cos is_differentiable_on Z by A7,A4,FDIFF_1:27;
A10: for x st x in Z holds (f-cos).x>0
     proof
       let x;
       assume B1:x in Z;
       then B2:(f-cos).x=f.x-cos.x by A4,SEQ_1:def 4
       .=1-cos.x by A11,B1;
       cos.x<1 by B1,A11;
       then 1-cos.x>1-1 by XREAL_1:17;
       hence thesis by B2;
     end;
     now let x;
       assume B1:x in Z;
       then B2: f-cos is_differentiable_in x by A9,FDIFF_1:16;
       (f-cos).x >0 by A10,B1;
       hence ( #R (1/2))*(f-cos) is_differentiable_in x by TAYLOR_1:22,B2;
     end;
     then A12:( #R (1/2))*(f-cos) is_differentiable_on Z by A3,FDIFF_1:16;
     for x st x in Z holds
     ((2(#)(( #R (1/2))*(f-cos)))`|Z).x =(1+cos.x) #R (1/2)
     proof
       let x;
       assume B1:x in Z;
       then B2: f-cos is_differentiable_in x by A9,FDIFF_1:16;
B3:    (f-cos).x >0 by A10,B1;
B4:    (f-cos).x=f.x-cos.x by A4,B1,SEQ_1:def 4
       .=1-cos.x by A11,B1;
       then B6:1-cos.x>0 by A10,B1;
B5:    diff((f-cos),x)=((f-cos)`|Z).x by A9,B1,FDIFF_1:def 8
       .=diff(f,x)-diff(cos,x) by B1,A7,A8,A4,FDIFF_1:27
       .=(f`|Z).x-diff(cos,x) by A7,B1,FDIFF_1:def 8
       .=0- diff(cos,x) by A14,FDIFF_1:31,A5,B1
       .=0-(-sin.x) by SIN_COS:68
       .=sin.x;
B8:    sin.x>0 & cos.x<1 & cos.x>-1 by A11,B1;
       ((2(#)(( #R (1/2))*(f-cos)))`|Z).x
       =2*diff((( #R (1/2))*(f-cos)),x) by FDIFF_1:28,A1,A12,B1
       .=2*((1/2)*( ((f-cos).x) #R (1/2-1)) * diff((f-cos),x))
       by TAYLOR_1:22,B2,B3
       .=2*((1/2)*((1-cos.x) #R (-1/2)) * diff((f-cos),x)) by B4
       .=sin.x*((1-cos.x) #R (-1/2)) by B5
       .=sin.x*(1 /  (1-cos.x) #R (1/2)) by PREPOWER:90,B6
       .=sin.x/(1-cos.x) #R (1/2) by XCMPLX_1:100
       .=(1+cos.x) #R (1/2) by Lm2,B8;
       hence thesis;
     end;
     hence thesis by A12,FDIFF_1:28,A1;
   end;

theorem 
  Z c= dom ((-2)(#)(( #R (1/2))*(f+cos))) & (for x st x in Z holds f.x=1 
  & sin.x>0 & cos.x<1 & cos.x>-1) 
  implies (-2)(#)(( #R (1/2))*(f+cos)) is_differentiable_on Z &
  for x st x in Z holds
  (((-2)(#)(( #R (1/2))*(f+cos)))`|Z).x =(1-cos.x) #R (1/2)
  proof
    assume that A1:Z c= dom ((-2)(#)(( #R (1/2))*(f+cos))) and
A11: (for x st x in Z holds f.x=1 & sin.x>0 & cos.x<1 & cos.x>-1);
A3:  Z c= dom (( #R (1/2))*(f+cos)) by SEQ_1:def 6,A1;
     for y st y in Z holds y in dom (f+cos) by FUNCT_1:21,A3;
     then A4:Z c= dom (f+cos) by TARSKI:def 3;
     then Z c= dom cos /\ dom f by SEQ_1:def 3;
     then A5:Z c= dom f by XBOOLE_1:18;
A14: for x st x in Z holds f.x=0*x+1 by A11;
     then A7:f is_differentiable_on Z & 
     for x st x in Z holds (f`|Z).x = 0 by FDIFF_1:31,A5;
A8:  cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
     then A9:f+cos is_differentiable_on Z by A7,A4,FDIFF_1:26;
A10: for x st x in Z holds (f+cos).x>0
     proof
       let x;
       assume B1:x in Z;
       then B2:(f+cos).x=f.x+cos.x by A4,SEQ_1:def 3
       .=1+cos.x by A11,B1;
       cos.x>-1 by B1,A11;
       then 1+cos.x>1+-1 by XREAL_1:10;
       hence thesis by B2;
     end;
     now let x;
       assume B1:x in Z;
       then B2: f+cos is_differentiable_in x by A9,FDIFF_1:16;
       (f+cos).x >0 by A10,B1;
       hence ( #R (1/2))*(f+cos) is_differentiable_in x by TAYLOR_1:22,B2;
     end;
     then A12:( #R (1/2))*(f+cos) is_differentiable_on Z by A3,FDIFF_1:16;
     for x st x in Z holds
     (((-2)(#)(( #R (1/2))*(f+cos)))`|Z).x =(1-cos.x) #R (1/2)
     proof
       let x;
       assume B1:x in Z;
       then B2: f+cos is_differentiable_in x by A9,FDIFF_1:16;
B3:    (f+cos).x >0 by A10,B1;
B4:    (f+cos).x=f.x+cos.x by A4,B1,SEQ_1:def 3
       .=1+cos.x by A11,B1;
       then B6:1+cos.x>0 by A10,B1;
B5:    diff((f+cos),x)=((f+cos)`|Z).x by A9,B1,FDIFF_1:def 8
       .=diff(f,x)+diff(cos,x) by B1,A7,A8,A4,FDIFF_1:26
       .=(f`|Z).x+diff(cos,x) by A7,B1,FDIFF_1:def 8
       .=0+diff(cos,x) by A14,FDIFF_1:31,A5,B1
       .=-sin.x by SIN_COS:68;
B8:    sin.x>0 & cos.x<1 & cos.x>-1 by A11,B1;
       (((-2)(#)(( #R (1/2))*(f+cos)))`|Z).x
       =(-2)*diff((( #R (1/2))*(f+cos)),x) by FDIFF_1:28,A1,A12,B1
       .=(-2)*((1/2)*( ((f+cos).x) #R (1/2-1)) * diff((f+cos),x))
       by TAYLOR_1:22,B2,B3
       .=(-2)*((1/2)*((1+cos.x) #R (-1/2)) * diff((f+cos),x)) by B4
       .=--sin.x*((1+cos.x) #R (-1/2)) by B5
       .=sin.x*(1/(1+cos.x) #R (1/2)) by PREPOWER:90,B6
       .=sin.x/(1+cos.x) #R (1/2) by XCMPLX_1:100
       .=(1-cos.x) #R (1/2) by Lm3,B8;
       hence thesis;
     end;
     hence thesis by A12,FDIFF_1:28,A1;
   end;

:: 41 f.x=1/2*ln(1+2*sin.x)
Lm4: Z c= dom (f1+2(#)sin) & (for x st x in Z holds f1.x=1)
     implies f1+2(#)sin is_differentiable_on Z & for x st x in Z holds 
     ((f1+2(#)sin)`|Z).x =2*cos.x
     proof
       assume that A1:Z c= dom (f1+2(#)sin) and
A2:    for x st x in Z holds f1.x=1;
       Z c= dom f1 /\ dom (2(#)sin) by SEQ_1:def 3,A1;
       then A3:Z c= dom f1 & Z c= dom (2(#)sin) by XBOOLE_1:18;
A4:    for x st x in Z holds f1.x=0*x+1 by A2;
       then A5:f1 is_differentiable_on Z & for x st x in Z 
       holds (f1`|Z).x = 0 by FDIFF_1:31,A3;
       sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
       then A7:2(#)sin is_differentiable_on Z by FDIFF_1:28,A3;
       for x st x in Z holds ((f1+2(#)sin)`|Z).x = 2*cos.x
       proof
         let x;
         assume B1:x in Z;
B2:      sin is_differentiable_in x by SIN_COS:69;
         ((f1+2(#)sin)`|Z).x =diff(f1,x) + diff(2(#)sin,x) 
         by FDIFF_1:26,A1,A7,A5,B1
         .=(f1`|Z).x+ diff(2(#)sin,x) by B1,A5,FDIFF_1:def 8
         .=(f1`|Z).x+2*diff(sin,x) by B2,FDIFF_1:23
         .=0+2*diff(sin,x) by A4,FDIFF_1:31,A3,B1
         .=2*cos.x by SIN_COS:69;
         hence thesis;
       end;
       hence thesis by A5,A7,A1,FDIFF_1:26;
     end;

theorem 
  Z c= dom ((1/2)(#)((log_ number_e)*f)) & f=f1+2(#)sin & 
  (for x st x in Z holds f1.x=1 & f.x >0) implies (1/2)(#)((log_ number_e)*f)
  is_differentiable_on Z & for x st x in Z holds 
  (((1/2)(#)((log_ number_e)*f))`|Z).x =cos.x/(1+2*sin.x)
  proof
    assume that A1:Z c= dom ((1/2)(#)((log_ number_e)*f)) and
A2: f=f1+2(#)sin & (for x st x in Z holds f1.x=1 & f.x >0);
A3: f=f1+2(#)sin & (for x st x in Z holds f1.x=1) by A2;
A4: Z c= dom ((log_ number_e)*f) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom f by FUNCT_1:21,A4;
    then A9:Z c= dom (f1+2(#)sin) by A2,TARSKI:def 3;
    then A6:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x =2*cos.x by A3,Lm4;
    Z c= dom f1 /\ dom (2(#)sin) by SEQ_1:def 3,A9;
    then A10:Z c= dom f1 & Z c= dom (2(#)sin) by XBOOLE_1:18;
    for x st x in Z holds (log_ number_e)*f is_differentiable_in x
    proof
      let x;
      assume B1:x in Z;
      then B2:f is_differentiable_in x by FDIFF_1:16,A6;
      f.x >0 by A2,B1;
      hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,B2;
    end;
    then A8:(log_ number_e)*f is_differentiable_on Z by A4,FDIFF_1:16;
    for x st x in Z holds
    (((1/2)(#)((log_ number_e)*f))`|Z).x =cos.x/(1+2*sin.x)
    proof
      let x;
      assume B1:x in Z;
      then B2:f is_differentiable_in x by FDIFF_1:16,A6;
B3:   f.x>0 by A2,B1;
B4:   f.x=f1.x+(2(#)sin).x by SEQ_1:def 3,A9,B1,A2
      .=1+(2(#)sin).x by B1,A2
      .=1+2*sin.x by SEQ_1:def 6,A10,B1;
      (((1/2)(#)((log_ number_e)*f))`|Z).x
      =(1/2)*diff(((log_ number_e)*f),x) by A8,A1,FDIFF_1:28,B1
      .=(1/2)*(diff(f,x)/(f.x)) by TAYLOR_1:20,B2,B3
      .=(1/2)*((f`|Z).x/(f.x)) by FDIFF_1:def 8,A6,B1
      .=(1/2)*((2*cos.x)/(f.x)) by A9,A3,Lm4,B1
      .=(1/2)*(2*cos.x)/(f.x) by XCMPLX_1:75 
      .=cos.x/(1+2*sin.x) by B4;
      hence thesis;
    end;
    hence thesis by A8,FDIFF_1:28,A1;
  end;

:: 42 f.x=(-1/2)*ln(1+2*cos.x)
Lm5: Z c= dom (f1+2(#)cos) & (for x st x in Z holds f1.x=1)
     implies f1+2(#)cos is_differentiable_on Z & for x st x in Z holds 
     ((f1+2(#)cos)`|Z).x =-2*sin.x
     proof
       assume that A1:Z c= dom (f1+2(#)cos) and
A2:    for x st x in Z holds f1.x=1;
       Z c= dom f1 /\ dom (2(#)cos) by SEQ_1:def 3,A1;
       then A3:Z c= dom f1 & Z c= dom (2(#)cos) by XBOOLE_1:18;
A4:    for x st x in Z holds f1.x=0*x+1 by A2;
       then A5:f1 is_differentiable_on Z & for x st x in Z 
       holds (f1`|Z).x = 0 by FDIFF_1:31,A3;
       cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
       then A7:2(#)cos is_differentiable_on Z by FDIFF_1:28,A3;
       for x st x in Z holds ((f1+2(#)cos)`|Z).x = -2*sin.x
       proof 
         let x;
         assume B1:x in Z;
B2:      cos is_differentiable_in x by SIN_COS:68;
         ((f1+2(#)cos)`|Z).x =diff(f1,x) + diff(2(#)cos,x) 
         by FDIFF_1:26,A1,A7,A5,B1
         .=(f1`|Z).x+ diff(2(#)cos,x) by B1,A5,FDIFF_1:def 8
         .=(f1`|Z).x+2*diff(cos,x) by B2,FDIFF_1:23
         .=0+2*diff(cos,x) by A4,FDIFF_1:31,A3,B1
         .=0+2*(-sin.x) by SIN_COS:68
         .=-2*sin.x;
         hence thesis;
       end;
       hence thesis by A5,A7,A1,FDIFF_1:26;
     end;

theorem 
  Z c= dom ((-1/2)(#)((log_ number_e)*f)) & f=f1+2(#)cos & (for x st x in Z 
  holds f1.x=1 & f.x >0) implies (-1/2)(#)((log_ number_e)*f)
  is_differentiable_on Z & for x st x in Z holds 
  (((-1/2)(#)((log_ number_e)*f))`|Z).x =sin.x/(1+2*cos.x)
  proof
    assume that A1:Z c= dom ((-1/2)(#)((log_ number_e)*f)) and
A2: f=f1+2(#)cos & (for x st x in Z holds f1.x=1 & f.x >0);
A3: f=f1+2(#)cos & (for x st x in Z holds f1.x=1) by A2;
A4: Z c= dom ((log_ number_e)*f) by SEQ_1:def 6,A1;
    for y st y in Z holds y in dom f by FUNCT_1:21,A4;
    then A9:Z c= dom (f1+2(#)cos) by A2,TARSKI:def 3;
    then A6:f is_differentiable_on Z & 
    for x st x in Z holds (f`|Z).x =-2*sin.x by A3,Lm5;
    Z c= dom f1 /\ dom (2(#)cos) by SEQ_1:def 3,A9;
    then A10:Z c= dom f1 & Z c= dom (2(#)cos) by XBOOLE_1:18;
    for x st x in Z holds (log_ number_e)*f is_differentiable_in x
    proof
      let x;
      assume B1:x in Z;
      then B2:f is_differentiable_in x by FDIFF_1:16,A6;
      f.x >0 by A2,B1;
      hence (log_ number_e)*f is_differentiable_in x by TAYLOR_1:20,B2;
    end;
    then A8:(log_ number_e)*f is_differentiable_on Z by A4,FDIFF_1:16;
    for x st x in Z holds
    (((-1/2)(#)((log_ number_e)*f))`|Z).x =sin.x/(1+2*cos.x)
    proof
      let x;
      assume B1:x in Z;
      then B2:f is_differentiable_in x by FDIFF_1:16,A6;
B3:   f.x>0 by A2,B1;
B4:   f.x=f1.x+(2(#)cos).x by SEQ_1:def 3,A9,B1,A2
      .=1+(2(#)cos).x by B1,A2
      .=1+2*cos.x by SEQ_1:def 6,A10,B1;
      (((-1/2)(#)((log_ number_e)*f))`|Z).x
      =(-1/2)*diff(((log_ number_e)*f),x) by A8,A1,FDIFF_1:28,B1
      .=(-1/2)*(diff(f,x)/(f.x)) by TAYLOR_1:20,B2,B3
      .=(-1/2)*((f`|Z).x/(f.x)) by FDIFF_1:def 8,A6,B1
      .=(-1/2)*((-2*sin.x)/(f.x)) by A9,A3,Lm5,B1
      .=(-1/2)*(-2*sin.x)/(f.x) by XCMPLX_1:75 
      .=sin.x/(1+2*cos.x) by B4;
      hence thesis;
    end;
    hence thesis by A8,FDIFF_1:28,A1;
  end;

:: 43 f.x=1/(4*a)*sin(2*a*x)
theorem th43:
  Z c= dom ((1/(4*a))(#)(sin*f)) & (for x st x in Z holds f.x=2*a*x)
  & a<>0 implies (1/(4*a))(#)(sin*f) is_differentiable_on Z & 
  for x st x in Z holds (((1/(4*a))(#)(sin*f))`|Z).x =1/2 * cos(2*a*x)
  proof
    assume that A1:Z c= dom ((1/(4*a))(#)(sin*f)) and
A2: (for x st x in Z holds f.x=2*a*x) & a<>0;
A3: Z c= dom (sin*f) by SEQ_1:def 6,A1;
A4: for x st x in Z holds f.x=(2*a)*x+0 by A2;
    then A5:sin*f is_differentiable_on Z & for x st x in Z holds 
    ((sin*f)`|Z).x =(2*a)* cos.((2*a)*x+0) by A3,FDIFF_4:37; 
    for x st x in Z holds (((1/(4*a))(#)(sin*f))`|Z).x =1/2 * cos(2*a*x)
    proof
      let x;
      assume B1:x in Z;
      (((1/(4*a))(#)(sin*f))`|Z).x
      =(1/(4*a))*diff((sin*f),x) by A1,FDIFF_1:28,B1,A5
      .=(1/(4*a))*((sin*f)`|Z).x by FDIFF_1:def 8,A5,B1
      .=(1/(4*a))*((2*a)* cos.((2*a)*x+0)) by A4,A3,FDIFF_4:37,B1
      .=1/(4*a)*(2*a)* cos.((2*a)*x+0) 
      .=(1/4)*(1/a)*(2*a)* cos.((2*a)*x) by XCMPLX_1:103
      .=(1/4)*2*(1/a*a)* cos.(2*a*x) 
      .=(1/2)*1*cos.(2*a*x) by A2,XCMPLX_1:107
      .=1/2*cos(2*a*x) by SIN_COS:def 23;
      hence thesis;
    end;
    hence thesis by A5,A1,FDIFF_1:28;
  end;

:: 44 f.x=x/2-1/(4*a)*sin(2*a*x)
theorem 
  Z c= dom (f1-((1/(4*a))(#)(sin*f))) & (for x st x in Z holds f1.x=x/2 
  & f.x=2*a*x) & a<>0 implies f1-((1/(4*a))(#)(sin*f)) is_differentiable_on Z 
  & for x st x in Z holds ((f1-((1/(4*a))(#)(sin*f)))`|Z).x =(sin(a*x))^2
  proof
    assume that A1:Z c= dom (f1-((1/(4*a))(#)(sin*f))) and
A2: (for x st x in Z holds f1.x=x/2 & f.x=2*a*x) & a<>0;
A3: (for x st x in Z holds f.x=2*a*x) & a<>0 by A2;
    Z c= dom f1 /\ dom ((1/(4*a))(#)(sin*f)) by SEQ_1:def 4,A1;
    then A4:Z c= dom ((1/(4*a))(#)(sin*f)) & Z c= dom f1 by XBOOLE_1:18;
    then A5:(1/(4*a))(#)(sin*f) is_differentiable_on Z & for x st x in Z 
    holds (((1/(4*a))(#)(sin*f))`|Z).x =1/2 * cos(2*a*x) by A3,th43;
A7: for x st x in Z holds f1.x=(1/2)*x+0
    proof
      let x;
      assume x in Z;
      then f1.x=x/2 by A2
      .= (1/2)*x+0;
      hence thesis;
    end;
    then A6:f1 is_differentiable_on Z & for x st x in Z 
    holds (f1`|Z).x =1/2 by FDIFF_1:31,A4;
    for x st x in Z holds ((f1-((1/(4*a))(#)(sin*f)))`|Z).x =(sin(a*x))^2
    proof
      let x;
      assume B1:x in Z;
      then ((f1-((1/(4*a))(#)(sin*f)))`|Z).x 
      =diff(f1,x)-diff(((1/(4*a))(#)(sin*f)),x) by A5,A6,FDIFF_1:27,A1
      .=(f1`|Z).x-diff(((1/(4*a))(#)(sin*f)),x) by FDIFF_1:def 8,B1,A6
      .=(f1`|Z).x-(((1/(4*a))(#)(sin*f))`|Z).x by FDIFF_1:def 8,B1,A5
      .=(f1`|Z).x-1/2 * cos(2*a*x) by A4,A3,th43,B1
      .=1/2-1/2 * cos(2*a*x) by A7,FDIFF_1:31,A4,B1
      .=(1/2)*(1-cos(2*a*x))
      .=(1/2)*(1-cos(2*(a*x)))
      .=(1/2)*(2*(sin(a*x))^2) by Lm6
      .=(sin(a*x))^2;
      hence thesis;
    end;
    hence thesis by A5,A6,FDIFF_1:27,A1;
  end;

:: 45 f.x=x/2+1/(4*a)*sin(2*a*x)
theorem 
  Z c= dom (f1+((1/(4*a))(#)(sin*f))) & (for x st x in Z holds f1.x=x/2 
  & f.x=2*a*x) & a<>0 implies f1+((1/(4*a))(#)(sin*f)) is_differentiable_on Z 
  & for x st x in Z holds ((f1+((1/(4*a))(#)(sin*f)))`|Z).x =(cos(a*x))^2
  proof
    assume that A1:Z c= dom (f1+((1/(4*a))(#)(sin*f))) and
A2: (for x st x in Z holds f1.x=x/2 & f.x=2*a*x) & a<>0;
A3: (for x st x in Z holds f.x=2*a*x) & a<>0 by A2;
    Z c= dom f1 /\ dom ((1/(4*a))(#)(sin*f)) by SEQ_1:def 3,A1;
    then A4:Z c= dom ((1/(4*a))(#)(sin*f)) & Z c= dom f1 by XBOOLE_1:18;
    then A5:(1/(4*a))(#)(sin*f) is_differentiable_on Z & for x st x in Z 
    holds (((1/(4*a))(#)(sin*f))`|Z).x =1/2 * cos(2*a*x) by A3,th43;
A7: for x st x in Z holds f1.x=(1/2)*x+0
    proof
      let x;
      assume x in Z;
      then f1.x=x/2 by A2
      .= (1/2)*x+0;
      hence thesis;
    end;
    then A6:f1 is_differentiable_on Z & for x st x in Z 
    holds (f1`|Z).x =1/2 by FDIFF_1:31,A4;
    for x st x in Z holds ((f1+((1/(4*a))(#)(sin*f)))`|Z).x =(cos(a*x))^2
    proof
      let x;
      assume B1:x in Z;
      then ((f1+((1/(4*a))(#)(sin*f)))`|Z).x 
      =diff(f1,x)+diff(((1/(4*a))(#)(sin*f)),x) by A5,A6,FDIFF_1:26,A1
      .=(f1`|Z).x+diff(((1/(4*a))(#)(sin*f)),x) by FDIFF_1:def 8,B1,A6
      .=(f1`|Z).x+(((1/(4*a))(#)(sin*f))`|Z).x by FDIFF_1:def 8,B1,A5
      .=(f1`|Z).x+1/2 * cos(2*a*x) by A4,A3,th43,B1
      .=1/2+1/2 * cos(2*a*x) by A7,FDIFF_1:31,A4,B1
      .=(1/2)*(1+cos(2*a*x)) 
      .=(1/2)*(1+cos(2*(a*x)))
      .=(1/2)*(2*(cos(a*x))^2) by Lm7
      .=(cos(a*x))^2;
      hence thesis;
    end;
    hence thesis by A5,A6,FDIFF_1:26,A1;
  end;

:: 46 f.x=(1/n)*(cos.x #Z n)
theorem th46:
  Z c= dom ((1/n)(#)(( #Z n)*cos)) & n>0 implies 
  (1/n)(#)(( #Z n)*cos) is_differentiable_on Z & for x st x in Z holds
  (((1/n)(#)(( #Z n)*cos))`|Z).x =-(cos.x) #Z (n-1)*sin.x
  proof
    assume that A1:Z c= dom ((1/n)(#)(( #Z n)*cos)) and A2:n>0;
A3: Z c= dom (( #Z n)*cos) by SEQ_1:def 6,A1;
    now let x;
      assume x in Z;
      cos is_differentiable_in x by SIN_COS:68;
      hence ( #Z n)*cos is_differentiable_in x by TAYLOR_1:3;
    end;
    then B2:( #Z n)*cos is_differentiable_on Z by A3,FDIFF_1:16;
    for x st x in Z holds
    (((1/n)(#)(( #Z n)*cos))`|Z).x =-(cos.x) #Z (n-1) *sin.x
    proof
      let x;
      assume A4:x in Z;
A5:   cos is_differentiable_in x by SIN_COS:68;
      (((1/n)(#)(( #Z n)*cos))`|Z).x =(1/n)*diff((( #Z n)*cos),x) 
      by B2,A1,FDIFF_1:28,A4
      .=(1/n)*(n*( (cos.x) #Z (n-1)) * diff(cos,x)) by TAYLOR_1:3,A5
      .=(1/n)*(n*( (cos.x) #Z (n-1)) *(-sin.x)) by SIN_COS:68
      .=((1/n)*n)*( (cos.x) #Z (n-1)) *(-sin.x)
      .=((n")*n)*( (cos.x) #Z (n-1)) *(-sin.x) by XCMPLX_1:217
      .=1*( (cos.x) #Z (n-1)) *(-sin.x) by A2,XCMPLX_0:def 7
      .=-(cos.x) #Z (n-1) *sin.x;
      hence thesis;
    end;
    hence thesis by B2,A1,FDIFF_1:28;
  end;

:: 47 f.x=(1/3)*(cos.x #Z 3)-cos.x
theorem 
  Z c= dom (((1/3)(#)(( #Z 3)*cos))-cos) & n>0 implies 
  (1/3)(#)(( #Z 3)*cos)-cos is_differentiable_on Z & for x st x in Z holds 
  ((((1/3)(#)(( #Z 3)*cos))-cos)`|Z).x =(sin.x)|^3
  proof
    assume that A1:Z c= dom (((1/3)(#)(( #Z 3)*cos))-cos) and  n>0;
    Z c= dom ((1/3)(#)(( #Z 3)*cos)) /\ dom cos by SEQ_1:def 4,A1;
    then A4:Z c= dom ((1/3)(#)(( #Z 3)*cos)) & Z c= dom cos by XBOOLE_1:18;
    then A3:((1/3)(#)(( #Z 3)*cos)) is_differentiable_on Z & 
    for x st x in Z holds
    (((1/3)(#)(( #Z 3)*cos))`|Z).x =-(cos.x) #Z (3-1)*sin.x by th46;
B2: cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      ((((1/3)(#)(( #Z 3)*cos))-cos)`|Z).x
      = diff(((1/3)(#)(( #Z 3)*cos)),x) - diff(cos,x) by A3,A1,FDIFF_1:27,B2,A6
      .=diff(((1/3)(#)(( #Z 3)*cos)),x)-(-sin.x) by SIN_COS:68
      .=(((1/3)(#)(( #Z 3)*cos))`|Z).x-(-sin.x) by FDIFF_1:def 8,A3,A6
      .=-(cos.x) #Z (3-1)*sin.x--sin.x by A4,th46,A6
      .=-(cos.x) #Z 2 *sin.x--sin.x
      .=sin.x*(1-(cos.x) #Z 2 ) 
      .=sin.x*(1-(cos.x) |^ abs(2) ) by PREPOWER:def 4
      .=sin.x*(1-(cos.x) |^ 2 ) by ABSVALUE:def 1
      .=sin.x*(1-(cos.x)*(cos.x)) by WSIERP_1:2
      .=sin.x*((cos.x)*(cos.x)+(sin.x)*(sin.x)-(cos.x)*(cos.x)) by SIN_COS:31
      .=sin.x*((sin.x)|^2) by WSIERP_1:2
      .=((sin.x)|^(2+1)) by NEWTON:11
      .=(sin.x)|^3;
      hence ((((1/3)(#)(( #Z 3)*cos))-cos)`|Z).x =(sin.x)|^3;
    end;
    hence thesis by A3,A1,FDIFF_1:27,B2;
  end;

:: 48 f.x=sin.x-(1/3)*(sin.x #Z 3)
theorem 
  Z c= dom (sin-((1/3)(#)(( #Z 3)*sin))) & n>0 implies 
  sin-((1/3)(#)(( #Z 3)*sin)) is_differentiable_on Z & for x st x in Z holds 
  ((sin-((1/3)(#)(( #Z 3)*sin)))`|Z).x =(cos.x)|^3
  proof
    assume that A1:Z c= dom (sin-((1/3)(#)(( #Z 3)*sin))) and  n>0;
    Z c= dom ((1/3)(#)(( #Z 3)*sin)) /\ dom sin by SEQ_1:def 4,A1;
    then A4:Z c= dom ((1/3)(#)(( #Z 3)*sin)) by XBOOLE_1:18;
    then A3:((1/3)(#)(( #Z 3)*sin)) is_differentiable_on Z & 
    for x st x in Z holds
    (((1/3)(#)(( #Z 3)*sin))`|Z).x =(sin.x) #Z (3-1) *cos.x by FDIFF_4:54;
B2: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A6:x in Z; 
      ((sin-((1/3)(#)(( #Z 3)*sin)))`|Z).x 
      = diff(sin,x) - diff(((1/3)(#)(( #Z 3)*sin)),x) by A3,A1,FDIFF_1:27,B2,A6
      .=cos.x - diff(((1/3)(#)(( #Z 3)*sin)),x) by SIN_COS:69
      .=cos.x -(((1/3)(#)(( #Z 3)*sin))`|Z).x by FDIFF_1:def 8,A3,A6
      .=cos.x -((sin.x) #Z (3-1) *cos.x) by A4,FDIFF_4:54,A6
      .=cos.x -((sin.x) #Z 2 *cos.x)
      .=cos.x*(1-(sin.x) #Z 2) 
      .=cos.x*(1-(sin.x) |^ abs(2) ) by PREPOWER:def 4
      .=cos.x*(1-(sin.x) |^ 2 ) by ABSVALUE:def 1
      .=cos.x*(1-(sin.x)*(sin.x)) by WSIERP_1:2
      .=cos.x*((cos.x)*(cos.x)+(sin.x)*(sin.x)-(sin.x)*(sin.x)) by SIN_COS:31
      .=cos.x*((cos.x)|^2) by WSIERP_1:2
      .=((cos.x)|^(2+1)) by NEWTON:11
      .=(cos.x)|^3;
      hence ((sin-((1/3)(#)(( #Z 3)*sin)))`|Z).x =(cos.x)|^3;
    end;
    hence thesis by A3,A1,FDIFF_1:27,B2;
  end;

:: 49 f.x=sin(ln x)
theorem 
  Z c= dom (sin*(log_ number_e)) implies sin*(log_ number_e) 
  is_differentiable_on Z & for x st x in Z holds 
  ((sin*(log_ number_e))`|Z).x =cos.(log(number_e,x))/x
  proof
    assume A1: Z c= dom (sin*(log_ number_e));
    for y st y in Z holds y in dom (log_ number_e) by FUNCT_1:21,A1;
    then A2:Z c= dom (log_ number_e) by TARSKI:def 3;
    then A3:log_ number_e is_differentiable_on Z by TAYLOR_1:18,FDIFF_1:34; 
A8: for x st x in Z holds sin*(log_ number_e) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A6: log_ number_e is_differentiable_in x by FDIFF_1:16,A3;  
      sin is_differentiable_in (log_ number_e).x by SIN_COS:69;
      hence sin*(log_ number_e) is_differentiable_in x by A6,FDIFF_2:13;
    end;
    then A7:sin*(log_ number_e) is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds ((sin*(log_ number_e))`|Z).x =cos.(log(number_e,x))/x 
    proof
      let x;
      assume B1:x in Z;
      then B2:x in right_open_halfline(0) by TAYLOR_1:18,FUNCT_1:21,A1;
A6:   log_ number_e is_differentiable_in x by FDIFF_1:16,A3,B1;
      sin is_differentiable_in (log_ number_e).x by SIN_COS:69;
      then diff(sin*(log_ number_e),x) 
      = diff(sin,(log_ number_e).x)*diff((log_ number_e),x) by A6,FDIFF_2:13
      .=cos.((log_ number_e).x)*diff((log_ number_e),x) by SIN_COS:69
      .=cos.(log(number_e,x))*diff((log_ number_e),x) by TAYLOR_1:def 2,B2
      .=cos.(log(number_e,x))*(1/x) by B1,A2,TAYLOR_1:18
      .=cos.(log(number_e,x))/x by XCMPLX_1:100;
      hence thesis by B1,A7,FDIFF_1:def 8;
    end;
    hence thesis by A8,FDIFF_1:16,A1;
  end;

:: 50 f.x=-cos(ln x)
theorem 
  Z c= dom (-(cos*(log_ number_e))) implies -(cos*(log_ number_e)) 
  is_differentiable_on Z & for x st x in Z holds 
  ((-(cos*(log_ number_e)))`|Z).x =sin.(log(number_e,x))/x
  proof
    assume Z c= dom (-(cos*(log_ number_e)));
    then A5:Z c= dom (cos*(log_ number_e)) by SEQ_1:def 7;
    then A6:Z c= dom ((-1)(#)(cos*(log_ number_e))) by SEQ_1:def 6;
    for y st y in Z holds y in dom (log_ number_e) by FUNCT_1:21,A5;
    then A2:Z c= dom (log_ number_e) by TARSKI:def 3;
    then A3:log_ number_e is_differentiable_on Z by TAYLOR_1:18,FDIFF_1:34; 
    for x st x in Z holds cos*(log_ number_e) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then B3:log_ number_e is_differentiable_in x by FDIFF_1:16,A3;  
      cos is_differentiable_in (log_ number_e).x by SIN_COS:68;
      hence thesis by B3,FDIFF_2:13;
    end;
    then A9:cos*(log_ number_e) is_differentiable_on Z by FDIFF_1:16,A5;
    then A7:(-1)(#)(cos*(log_ number_e)) is_differentiable_on Z &
    for x st x in Z holds (((-1)(#)(cos*(log_ number_e)))`|Z).x
    =(-1)*diff(cos*(log_ number_e),x) by FDIFF_1:28,A6;
    for x st x in Z holds 
    ((-(cos*(log_ number_e)))`|Z).x =sin.(log(number_e,x))/x
    proof
      let x;
      assume B1:x in Z;
      then B2:x in right_open_halfline(0) by TAYLOR_1:18,FUNCT_1:21,A5;
B3:   log_ number_e is_differentiable_in x by FDIFF_1:16,A3,B1;
B4:   cos is_differentiable_in (log_ number_e).x by SIN_COS:68;
      ((-(cos*(log_ number_e)))`|Z).x 
      =(((-1)(#)(cos*(log_ number_e)))`|Z).x by RFUNCT_1:38
      .=(-1)*diff((cos*(log_ number_e)),x) by A9,FDIFF_1:28,A6,B1
      .=(-1)*(diff(cos,(log_ number_e).x)*diff((log_ number_e),x)) 
      by B4,B3,FDIFF_2:13
      .=(-1)*((-sin.((log_ number_e).x))*diff((log_ number_e),x)) by SIN_COS:68
      .=(-1)*(-sin.((log_ number_e).x))*diff((log_ number_e),x)
      .=(-1)*(-sin.(log(number_e,x)))*diff((log_ number_e),x)
      by TAYLOR_1:def 2,B2
      .=(-1)*(-sin.(log(number_e,x)))*(1/x) by B1,A2,TAYLOR_1:18
      .=sin.(log(number_e,x))/x by XCMPLX_1:100;
      hence thesis;
    end;
    hence thesis by A7,RFUNCT_1:38;
  end;
