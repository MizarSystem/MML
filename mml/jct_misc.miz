:: Miscellaneous { I }
::  by Andrzej Trybulec
::
:: Received August 28, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies NUMBERS, XBOOLE_0, SUBSET_1, XREAL_0, ORDINAL1, XXREAL_1,
      ARYTM_3, REAL_1, XXREAL_0, COMPLEX1, ARYTM_1, CARD_1, FUNCT_1, MCART_1,
      ZFMISC_1, PRE_TOPC, RCOMP_1, TARSKI, STRUCT_0, SETFAM_1, SEQ_1, RELAT_1,
      VALUED_0, SEQ_2, ORDINAL2, RELAT_2, PSCOMP_1, LIMFUNC1, CONNSP_1,
      METRIC_1, SEQ_4, XXREAL_2, TOPMETR, INTEGRA1, NAT_1, VALUED_1, EUCLID,
      JORDAN2C, MEASURE5;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, MCART_1, FUNCT_1, COMPLEX1,
      RELSET_1, FUNCT_2, INTEGRA1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      XREAL_0, REAL_1, XXREAL_1, XXREAL_2, NAT_1, VALUED_1, SEQ_1, SEQ_2,
      SEQ_4, STRUCT_0, PRE_TOPC, COMPTS_1, BORSUK_1, MEASURE6, PSCOMP_1,
      VALUED_0, METRIC_1, TBSP_1, TOPMETR, LIMFUNC1, CONNSP_1, JORDAN2C,
      EUCLID, RECDEF_1, RCOMP_1;
 constructors SETFAM_1, REAL_1, COMPLEX1, NAT_1, SEQM_3, PROB_1, LIMFUNC1,
      BINARITH, CONNSP_1, COMPTS_1, TBSP_1, TSP_1, TOPMETR, MEASURE6, JORDAN2C,
      INTEGRA1, SEQ_1, RECDEF_1, XXREAL_2, SEQ_4, SEQ_2, PSCOMP_1;
 registrations SUBSET_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0,
      MEMBERED, STRUCT_0, PRE_TOPC, COMPTS_1, BORSUK_1, EUCLID, TOPMETR,
      TOPREAL1, MEASURE6, JORDAN2C, BORSUK_3, TOPREAL6, INTEGRA1, VALUED_1,
      FUNCT_2, XXREAL_2, VALUED_0, FCONT_3, PSCOMP_1;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, RCOMP_1, SEQ_4, XBOOLE_0, SUBSET_1, PROB_1, LIMFUNC1,
      XXREAL_2;
 theorems ZFMISC_1, TARSKI, SUBSET_1, MCART_1, FUNCT_2, FUNCT_1, RCOMP_1,
      SEQ_4, SEQM_3, ABSVALUE, PSCOMP_1, GOBOARD7, BORSUK_1, TOPMETR, JORDAN6,
      UNIFORM1, RELAT_1, SEQ_2, TOPREAL6, TSP_1, SEQ_1, INTEGRA1, CONNSP_1,
      XREAL_0, TSEP_1, XBOOLE_0, XBOOLE_1, XREAL_1, COMPLEX1, JORDAN2C,
      PRE_TOPC, TOPREAL3, METRIC_6, XXREAL_0, XXREAL_1, XXREAL_2, VALUED_0,
      MEASURE6;
 schemes FUNCT_2;

begin :: Preliminaries

scheme
  NonEmpty{ A()-> non empty set, F(set)-> set}: { F(a) where a is Element of A
  (): not contradiction } is non empty proof
  consider a0 being set such that
A1: a0 in A() by XBOOLE_0:def 1;
  F(a0) in { F(a) where a is Element of A(): not contradiction } by A1;
  hence thesis;
end;

reserve
  r,s,r0,s0,t for real number;

canceled 8;

theorem Th9:
  for a,b being real number st r in [.a,b.] & s in [.a,b.] holds (r
  + s)/2 in [.a,b.]
proof
  let a,b be real number such that
A1: r in [.a,b.] and
A2: s in [.a,b.];
  reconsider a,b,r,s as Real by XREAL_0:def 1;
A3: s <= b by A2,XXREAL_1:1;
  r <= b by A1,XXREAL_1:1;
  then r+s <= b+b by A3,XREAL_1:9;
  then
A4: (r+s)/2 <= (b+b)/2 by XREAL_1:74;
A5: a <= s by A2,XXREAL_1:1;
  a <= r by A1,XXREAL_1:1;
  then a+a <= r+s by A5,XREAL_1:9;
  then (a+a)/2 <= (r+s)/2 by XREAL_1:74;
  hence thesis by A4;
end;

canceled;

theorem Th11:
  abs(abs(r0-s0)-abs(r-s)) <= abs(r0-r) + abs(s0-s)
proof
  r0-s0 - (r-s) = r0-r - (s0-s);
  then
A1: abs(r0-s0 - (r-s)) <= abs(r0-r) + abs(s0-s) by COMPLEX1:143;
  abs(abs(r0-s0) - abs(r-s)) <= abs(r0-s0 - (r-s)) by COMPLEX1:150;
  hence thesis by A1,XXREAL_0:2;
end;

theorem Th12:
  t in ].r,s.[ implies abs(t) < max(abs(r),abs(s))
proof
  assume
A1: t in ].r,s.[;
  reconsider r,t,s as Real by XREAL_0:def 1;
A2: r < t by A1,XXREAL_1:4;
A3: t < s by A1,XXREAL_1:4;
  per cases;
  suppose
A4: t >= 0;
    then t = abs(t) by ABSVALUE:def 1;
    then abs(t) < abs(s) by A3,A4,ABSVALUE:def 1;
    hence thesis by XXREAL_0:30;
  end;
  suppose
A5: t < 0;
    then
A6: -t = abs(t) by ABSVALUE:def 1;
    -r =abs(r) by A2,A5,ABSVALUE:def 1;
    then abs(t) < abs(r) by A2,A6,XREAL_1:26;
    hence thesis by XXREAL_0:30;
  end;
end;

scheme
  DoubleChoice{ A,B,C() -> non empty set, P[set,set,set]}: ex a being Function
of A(), B(), b being Function of A(), C() st for i being Element of A() holds P
  [i,a.i,b.i]
provided
A1: for i being Element of A() ex ai being Element of B(), bi being
Element of C() st P[i,ai,bi]
proof
  defpred P1[set,set] means P[$1,($2)`1,($2)`2];
A2: for e being Element of A() ex u being Element of [:B(),C():] st P1[e,u]
  proof
    let e be Element of A();
    consider ai being Element of B(), bi being Element of C() such that
A3: P[e,ai,bi] by A1;
    take [ai,bi];
    thus [ai,bi] is Element of [:B(),C():] by ZFMISC_1:106;
    [ai,bi]`1 = ai by MCART_1:7;
    hence thesis by A3,MCART_1:7;
  end;
  consider f being Function of A(), [:B(),C():] such that
A4: for e being Element of A() holds P1[e,f.e] from FUNCT_2:sch 3(A2);
  take pr1 f, pr2 f;
  let i be Element of A();
A5: (f.i)`2 = (pr2 f).i by FUNCT_2:def 7;
  (f.i)`1 = (pr1 f).i by FUNCT_2:def 6;
  hence thesis by A4,A5;
end;

theorem Th13:
  for S,T being non empty TopSpace, G being Subset of [:S,T:] st
for x being Point of [:S,T:] st x in G ex GS being Subset of S, GT being Subset
  of T st GS is open & GT is open & x in [:GS,GT:] & [:GS,GT:] c= G holds G is
  open
proof
  let S,T be non empty TopSpace, G being Subset of [:S,T:] such that
A1: for x being Point of [:S,T:] st x in G ex GS being Subset of S, GT
being Subset of T st GS is open & GT is open & x in [:GS,GT:] & [:GS,GT:] c= G;
  set A = {[:GS,GT:] where GS is Subset of S, GT is Subset of T : GS is open &
  GT is open & [:GS,GT:] c= G };
  A c= bool the carrier of [:S,T:]
  proof
    let e be set;
    assume e in A;
    then
    ex GS being Subset of S, GT being Subset of T st e = [:GS, GT:] & GS
    is open & GT is open & [:GS,GT:] c= G;
    hence thesis;
  end;
  then reconsider A as Subset-Family of [:S,T:];
  reconsider A as Subset-Family of [:S,T:];
  for x being set holds x in G iff ex Y being set st x in Y & Y in A
  proof
    let x be set;
    thus x in G implies ex Y being set st x in Y & Y in A
    proof
      assume x in G;
      then consider GS being Subset of S, GT being Subset of T such that
A2:   GS is open and
A3:   GT is open and
A4:   x in [:GS,GT:] and
A5:   [:GS,GT:] c= G by A1;
      take [:GS,GT:];
      thus thesis by A2,A3,A4,A5;
    end;
    given Y being set such that
A6: x in Y and
A7: Y in A;
    ex GS being Subset of S, GT being Subset of T st Y = [:GS,GT:] & GS
    is open & GT is open & [:GS,GT:] c= G by A7;
    hence thesis by A6;
  end;
  then
A8: G = union A by TARSKI:def 4;
  for e being set st e in A ex X1 being Subset of S, Y1 being Subset of T
  st e = [:X1,Y1:] & X1 is open & Y1 is open
  proof
    let e be set;
    assume e in A;
    then ex GS being Subset of S, GT being Subset of T st e = [:GS,GT:] & GS
    is open & GT is open & [:GS,GT:] c= G;
    hence thesis;
  end;
  hence thesis by A8,BORSUK_1:45;
end;

begin :: topological properties of sets of real numbers

theorem Th14:
  for A,B being compact Subset of REAL holds A /\ B is compact
proof
  let A,B be compact Subset of REAL;
  let s1 be Real_Sequence such that
A1: rng s1 c= A /\ B;
A2: A /\ B c= B by XBOOLE_1:17;
  A /\ B c= A by XBOOLE_1:17;
  then rng s1 c= A by A1,XBOOLE_1:1;
  then consider s2 being Real_Sequence such that
A3: s2 is subsequence of s1 and
A4: s2 is convergent and
A5: lim s2 in A by RCOMP_1:def 3;
  rng s2 c= rng s1 by A3,VALUED_0:21;
  then rng s2 c= A /\ B by A1,XBOOLE_1:1;
  then rng s2 c= B by A2,XBOOLE_1:1;
  then consider s3 being Real_Sequence such that
A6: s3 is subsequence of s2 and
A7: s3 is convergent and
A8: lim s3 in B by RCOMP_1:def 3;
  take s3;
  thus s3 is subsequence of s1 by A3,A6,VALUED_0:20;
  thus s3 is convergent by A7;
  lim s3 = lim s2 by A4,A6,SEQ_4:30;
  hence thesis by A5,A8,XBOOLE_0:def 4;
end;

theorem
  for T being non empty TopSpace for f being continuous RealMap of T for
  A being Subset of T st A is connected holds f.:A is interval
proof
  let T be non empty TopSpace;
  let f be continuous RealMap of T;
  let A be Subset of T;
  assume
A1: A is connected;
  let r,s be ext-real number;
A2: A c= f"(f.:A) by FUNCT_2:50;
  assume
A3: r in f.:A;
  then consider p being Point of T such that
A4: p in A and
A5: r = f.p by FUNCT_2:116;
  assume
A6: s in f.:A;
  then consider q being Point of T such that
A7: q in A and
A8: s = f.q by FUNCT_2:116;
  assume
Z: not [.r,s.] c= f.:A;
  reconsider r,s as Real by A3,A6;
  consider t being Real such that
A9: t in [.r,s.] and
A10: not t in f.:A by Z,SUBSET_1:7;
  reconsider r,s,t as Real;
  set P1 = f"left_open_halfline t, Q1 = f"right_open_halfline t, P = P1 /\ A,
  Q = Q1 /\ A, X = left_open_halfline t \/ right_open_halfline t;
A11: Q1 is open by PSCOMP_1:54;
  t <= s by A9,XXREAL_1:1;
  then
A12: t < s by A6,A10,XXREAL_0:1;
  right_open_halfline t = {r1 where r1 is Real: t < r1} by XXREAL_1:230;
  then s in right_open_halfline t by A12;
  then q in Q1 by A8,FUNCT_2:46;
  then
A13: Q <> {}T by A7,XBOOLE_0:def 4;
  left_open_halfline t /\ right_open_halfline t = ].t,t.[ by XXREAL_1:269
    .= {} by XXREAL_1:28;
  then left_open_halfline t misses right_open_halfline t by XBOOLE_0:def 7;
  then P1 misses Q1 by FUNCT_1:141;
  then P1 /\ Q1 = {} by XBOOLE_0:def 7;
  then
A14: P1 /\ Q1 misses P \/ Q by XBOOLE_1:65;
  reconsider Y = {t} as Subset of REAL;
  Y` = REAL \ [.t,t.] by XXREAL_1:17
    .= X by XXREAL_1:385;
  then
A15: (f"Y)` = f"X by FUNCT_2:177
    .= P1 \/ Q1 by RELAT_1:175;
  f"{t} misses f"(f.:A) by A10,FUNCT_1:141,ZFMISC_1:56;
  then f"{t} misses A by A2,XBOOLE_1:63;
  then A c= P1 \/ Q1 by A15,SUBSET_1:43;
  then
A16: A = A /\ (P1 \/ Q1) by XBOOLE_1:28
    .= P \/ Q by XBOOLE_1:23;
A17: P c= P1 by XBOOLE_1:17;
  r <= t by A9,XXREAL_1:1;
  then
A18: r < t by A3,A10,XXREAL_0:1;
  left_open_halfline t = {r1 where r1 is Real: r1 < t} by XXREAL_1:229;
  then r in left_open_halfline t by A18;
  then p in P1 by A5,FUNCT_2:46;
  then
A19: P <> {}T by A4,XBOOLE_0:def 4;
A20: Q c= Q1 by XBOOLE_1:17;
  P1 is open by PSCOMP_1:54;
  then P,Q are_separated by A11,A17,A20,A14,TSEP_1:49;
  hence contradiction by A1,A16,A19,A13,CONNSP_1:16;
end;

definition
 canceled;
  let A,B be Subset of REAL;
  func dist(A,B) -> real number means
  :Def2:
  ex X being Subset of REAL st X =
{abs(r - s) where r is Element of REAL, s is Element of REAL : r in A & s in B}
  & it = lower_bound X;
  existence
  proof
    set Y = {abs(r - s) where r is Element of REAL, s is Element of REAL : r
    in A & s in B};
    Y c= REAL
    proof
      let e be set;
      assume e in Y;
      then ex r,s being Element of REAL st e = abs(r - s) & r in A & s in B;
      hence thesis;
    end;
    then reconsider Y0 = Y as Subset of REAL;
    take lower_bound Y0;
    thus thesis;
  end;
  uniqueness;
  commutativity
  proof
    let IT be real number, A,B be Subset of REAL;
    given X0 being Subset of REAL such that
A1: X0 = {abs(r - s) where r is Element of REAL, s is Element of REAL
    : r in A & s in B} and
A2: IT = lower_bound X0;
    set Y = {abs(r - s) where r is Element of REAL, s is Element of REAL : r
    in B & s in A};
    Y c= REAL
    proof
      let e be set;
      assume e in Y;
      then ex r,s being Element of REAL st e = abs(r - s) & r in B & s in A;
      hence thesis;
    end;
    then reconsider Y0 = Y as Subset of REAL;
    take Y0;
    thus Y0 = {abs(r - s) where r is Element of REAL, s is Element of REAL : r
    in B & s in A};
    X0 = Y0
    proof
      thus X0 c= Y0
      proof
        let x be set;
        assume x in X0;
        then consider r,s being Element of REAL such that
A3:     x = abs(r - s) and
A4:     r in A and
A5:     s in B by A1;
        x = abs(s - r) by A3,UNIFORM1:13;
        hence thesis by A4,A5;
      end;
      let x be set;
      assume x in Y0;
      then consider r,s being Element of REAL such that
A6:   x = abs(r - s) and
A7:   r in B and
A8:   s in A;
      x = abs(s - r) by A6,UNIFORM1:13;
      hence thesis by A1,A7,A8;
    end;
    hence thesis by A2;
  end;
end;

theorem Th16:
  for A,B being Subset of REAL, r, s st r in A & s in B holds abs(
  r-s) >= dist(A,B)
proof
  let A,B be Subset of REAL;
  set Y = {abs(r - s) where r is Element of REAL, s is Element of REAL : r in
  A & s in B};
  let r,s;
  assume that
A1: r in A and
A2: s in B;
  Y c= REAL
  proof
    let e be set;
    assume e in Y;
    then ex r,s being Real st e = abs(r - s) & r in A & s in B;
    hence thesis;
  end;
  then reconsider Y0 = Y as Subset of REAL;
A3: Y0 is bounded_below
  proof
    take 0;
    let r0 be ext-real number;
    assume r0 in Y0;
    then ex r,s being Real st r0 = abs(r - s) & r in A & s in B;
    hence thesis by COMPLEX1:132;
  end;
A4: dist(A,B) = lower_bound Y0 by Def2;
  abs(r-s) in Y0 by A1,A2;
  hence thesis by A4,A3,SEQ_4:def 5;
end;

theorem Th17:
  for A,B being Subset of REAL, C,D being non empty Subset of REAL
  st C c= A & D c= B holds dist(A,B) <= dist(C,D)
proof
  let A,B be Subset of REAL, C,D be non empty Subset of REAL such that
A1: C c= A and
A2: D c= B;
  consider s0 being set such that
A3: s0 in D by XBOOLE_0:def 1;
  set Y = {abs(r - s) where r is Element of REAL, s is Element of REAL : r in
  C & s in D};
  consider r0 being set such that
A4: r0 in C by XBOOLE_0:def 1;
A5: Y c= REAL
  proof
    let e be set;
    assume e in Y;
    then ex r,s being Real st e = abs(r - s) & r in C & s in D;
    hence thesis;
  end;
  reconsider r0,s0 as real number by A4,A3;
  abs(r0 - s0) in Y by A4,A3;
  then reconsider Y as non empty Subset of REAL by A5;
  set X = {abs(r - s) where r is Element of REAL, s is Element of REAL : r in
  A & s in B};
  X c= REAL
  proof
    let e be set;
    assume e in X;
    then ex r,s being Real st e = abs(r - s) & r in A & s in B;
    hence thesis;
  end;
  then reconsider X as Subset of REAL;
A6: Y c= X
  proof
    let x be set;
    assume x in Y;
    then ex r,s being Real st x = abs(r - s) & r in C & s in D;
    hence thesis by A1,A2;
  end;
A7: X is bounded_below
  proof
    take 0;
    let r0 be ext-real number;
    assume r0 in X;
    then ex r,s being Real st r0 = abs(r - s) & r in A & s in B;
    hence thesis by COMPLEX1:132;
  end;
A8: dist(C,D) = lower_bound Y by Def2;
  dist(A,B) = lower_bound X by Def2;
  hence thesis by A7,A8,A6,SEQ_4:64;
end;

theorem Th18:
  for A, B being non empty compact Subset of REAL ex r,s being
  real number st r in A & s in B & dist(A,B) = abs(r - s)
proof
  defpred P[set,set] means ex r,s being Real st $1 = [r,s] & $2 = abs(r-s);
  let A, B be non empty compact Subset of REAL;
  reconsider At = A, Bt = B as non empty compact Subset of R^1 by TOPMETR:24
,TOPREAL6:81;
A1: the carrier of R^1|Bt = Bt by PRE_TOPC:29;
  reconsider AB = [:R^1|At, R^1|Bt:] as compact (non empty TopSpace);
A2: the carrier of R^1|At = At by PRE_TOPC:29;
A3: now
    let x be Element of AB;
    x in the carrier of AB;
    then x in [:A,B:] by A2,A1,BORSUK_1:def 5;
    then consider r,s being set such that
A4: r in REAL and
A5: s in REAL and
A6: x = [r,s] by ZFMISC_1:103;
    reconsider r,s as Real by A4,A5;
    take t = abs(r-s);
    thus P[x,t] by A6;
  end;
  consider f being RealMap of AB such that
A7: for x being Element of AB holds P[x,f.x] from FUNCT_2:sch 3(A3);
  for Y being Subset of REAL st Y is open holds f"Y is open
  proof
    let Y be Subset of REAL such that
A8: Y is open;
    for x being Point of AB st x in f"Y ex YS being Subset of R^1|At, YT
being Subset of R^1|Bt st YS is open & YT is open & x in [:YS,YT:] & [:YS,YT:]
    c= f"Y
    proof
      let x be Point of AB;
      consider r,s being Real such that
A9:   x = [r,s] and
A10:  f.x = abs(r-s) by A7;
      assume x in f"Y;
      then f.x in Y by FUNCT_1:def 13;
      then consider N being Neighbourhood of f.x such that
A11:  N c= Y by A8,RCOMP_1:39;
      consider g being real number such that
A12:  0 < g and
A13:  N = ].f.x-g,f.x+g.[ by RCOMP_1:def 7;
      reconsider a=r-g/2, b=r+g/2, c =s-g/2, d=s+g/2 as Real;
      reconsider S = ].a,b.[, T = ].c,d.[ as Subset of R^1 by TOPMETR:24;
      reconsider YT = T /\ B as Subset of R^1|Bt by A1,XBOOLE_1:17;
      reconsider YS = S /\ A as Subset of R^1|At by A2,XBOOLE_1:17;
A14:  s in T by A12,TOPREAL6:20,XREAL_1:217;
      take YS, YT;
A15:  T is open by JORDAN6:46;
      S is open by JORDAN6:46;
      hence YS is open & YT is open by A2,A1,A15,TSP_1:def 1;
A16:  r in S by A12,TOPREAL6:20,XREAL_1:217;
      x in the carrier of AB;
      then
A17:  x in [:A,B:] by A2,A1,BORSUK_1:def 5;
      then s in B by A9,ZFMISC_1:106;
      then
A18:  s in YT by A14,XBOOLE_0:def 4;
      f.:[:YS,YT:] c= N
      proof
        let e be set;
        assume e in f.:[:YS,YT:];
        then consider y being Element of AB such that
A19:    y in [:YS,YT:] and
A20:    e = f.y by FUNCT_2:116;
        consider r1,s1 being Real such that
A21:    y = [r1,s1] and
A22:    f.y = abs(r1-s1) by A7;
A23:    abs(abs(r1-s1)-abs(r-s)) <= abs(r1-r) + abs(s1-s) by Th11;
        s1 in YT by A19,A21,ZFMISC_1:106;
        then s1 in ].s-g/2,s+g/2.[ by XBOOLE_0:def 4;
        then
A24:    abs(s1-s) < g/2 by RCOMP_1:8;
        r1 in YS by A19,A21,ZFMISC_1:106;
        then r1 in ].r-g/2,r+g/2.[ by XBOOLE_0:def 4;
        then
A25:    abs(r1-r) < g/2 by RCOMP_1:8;
        g = g/2 + g/2;
        then abs(r1-r) + abs(s1-s) < g by A25,A24,XREAL_1:10;
        then abs(abs(r1-s1)-abs(r-s)) < g by A23,XXREAL_0:2;
        hence thesis by A13,A10,A20,A22,RCOMP_1:8;
      end;
      then
A26:  f.:[:YS,YT:] c= Y by A11,XBOOLE_1:1;
      r in A by A9,A17,ZFMISC_1:106;
      then r in YS by A16,XBOOLE_0:def 4;
      hence x in [:YS,YT:] by A9,A18,ZFMISC_1:106;
      dom f = the carrier of AB by FUNCT_2:def 1;
      hence thesis by A26,FUNCT_1:163;
    end;
    hence thesis by Th13;
  end;
  then reconsider f as continuous RealMap of AB by PSCOMP_1:54;
  f.:the carrier of AB is with_min by MEASURE6:def 17;
  then lower_bound(f.:the carrier of AB) in f.:the carrier of AB
   by MEASURE6:def 9;
  then consider x being Element of AB such that
A27: x in the carrier of AB and
A28: lower_bound(f.:the carrier of AB) = f.x by FUNCT_2:116;
A29: x in [:A,B:] by A2,A1,A27,BORSUK_1:def 5;
  then consider r1,s1 being set such that
A30: r1 in REAL and
A31: s1 in REAL and
A32: x = [r1,s1] by ZFMISC_1:103;
A33: f.:the carrier of AB = {abs(r - s) where r is Element of REAL, s is
  Element of REAL : r in A & s in B}
  proof
    hereby
      let x be set;
      assume x in f.:the carrier of AB;
      then consider y being Element of AB such that
A34:  y in the carrier of AB and
A35:  x = f.y by FUNCT_2:116;
      consider r1,s1 being Real such that
A36:  y = [r1,s1] and
A37:  f.y = abs(r1-s1) by A7;
A38:  [r1,s1] in [:A,B:] by A2,A1,A34,A36,BORSUK_1:def 5;
      then
A39:  s1 in B by ZFMISC_1:106;
      r1 in A by A38,ZFMISC_1:106;
      hence
      x in {abs(r - s) where r is Element of REAL, s is Element of REAL :
      r in A & s in B} by A35,A37,A39;
    end;
    let x be set;
    assume x in {abs(r - s) where r is Element of REAL, s is Element of REAL
    : r in A & s in B};
    then consider r,s being Real such that
A40: x = abs(r - s) and
A41: r in A and
A42: s in B;
    [r,s] in [:A,B:] by A41,A42,ZFMISC_1:106;
    then reconsider y = [r,s] as Element of AB by A2,A1,BORSUK_1:def 5;
    consider r1,s1 being Real such that
A43: y = [r1,s1] and
A44: f.y = abs(r1-s1) by A7;
A45: s1 = s by A43,ZFMISC_1:33;
    r1 = r by A43,ZFMISC_1:33;
    hence thesis by A40,A44,A45,FUNCT_2:43;
  end;
  reconsider r1,s1 as real number by A30,A31;
  take r1,s1;
  thus r1 in A & s1 in B by A29,A32,ZFMISC_1:106;
  consider r,s being Real such that
A46: x = [r,s] and
A47: f.x = abs(r-s) by A7;
A48: s1 = s by A32,A46,ZFMISC_1:33;
  r1 = r by A32,A46,ZFMISC_1:33;
  hence thesis by A28,A33,A47,A48,Def2;
end;

theorem Th19:
  for A, B being non empty compact Subset of REAL holds dist(A,B) >= 0
proof
  let A, B be non empty compact Subset of REAL;
  set X = {abs(r - s) where r is Element of REAL, s is Element of REAL : r in
  A & s in B};
  consider r0 being set such that
A1: r0 in A by XBOOLE_0:def 1;
A2: X c= REAL
  proof
    let e be set;
    assume e in X;
    then ex r,s being Real st e = abs(r - s) & r in A & s in B;
    hence thesis;
  end;
  consider s0 being set such that
A3: s0 in B by XBOOLE_0:def 1;
  reconsider r0,s0 as real number by A1,A3;
  abs(r0 - s0) in X by A1,A3;
  then reconsider X as non empty Subset of REAL by A2;
A4: for t being real number st t in X holds t >= 0
  proof
    let t be real number;
    assume t in X;
    then ex r,s being Real st t = abs(r - s) & r in A & s in B;
    hence thesis by COMPLEX1:132;
  end;
  dist(A,B) = lower_bound X by Def2;
  hence thesis by A4,SEQ_4:60;
end;

theorem Th20:
  for A,B being non empty compact Subset of REAL st A misses B
  holds dist(A,B) > 0
proof
  let A,B being non empty compact Subset of REAL such that
A1: A misses B;
  consider r0,s0 such that
A2: r0 in A and
A3: s0 in B and
A4: dist(A,B) = abs(r0 - s0) by Th18;
  reconsider r0,s0 as Real by XREAL_0:def 1;
  assume dist(A,B) <= 0;
  then abs(r0 - s0) = 0 by A4,Th19;
  then r0 = s0 by GOBOARD7:2;
  hence contradiction by A1,A2,A3,XBOOLE_0:3;
end;

theorem
  for e,f being real number, A,B being compact Subset of REAL st A
  misses B & A c= [.e,f.] & B c= [.e,f.] for S being Function of NAT, bool REAL
st for i being Element of NAT holds S.i is interval & S.i meets A & S.i meets
  B ex r being real number st r in [.e,f.] & not r in A \/ B & for i being
  Element of NAT ex k being Element of NAT st i <= k & r in S.k
proof
  let e,f be real number, A,B be compact Subset of REAL such that
A1: A misses B and
A2: A c= [.e,f.] and
A3: B c= [.e,f.];
  let S be Function of NAT, bool REAL such that
A4: for i being Element of NAT holds S.i is interval & S.i meets A & S.
  i meets B;
  defpred P[set,Subset of REAL] means $2 is closed-interval & $2 meets A & $2
  meets B & $2 c= S.$1;
A5: for i being Element of NAT ex u being Subset of REAL st P[i,u]
  proof
    let i be Element of NAT;
A6: S.i is interval by A4;
    S.i meets B by A4;
    then consider y being set such that
A7: y in S.i and
A8: y in B by XBOOLE_0:3;
    S.i meets A by A4;
    then consider x being set such that
A9: x in S.i and
A10: x in A by XBOOLE_0:3;
    reconsider y as Real by A8;
    reconsider x as Real by A10;
    per cases;
    suppose
A11:  x <= y;
      take [.x,y.];
      thus [.x,y.] is closed-interval by A11,INTEGRA1:def 1;
      x in [.x,y.] by A11;
      hence [.x,y.] meets A by A10,XBOOLE_0:3;
      y in [.x,y.] by A11;
      hence [.x,y.] meets B by A8,XBOOLE_0:3;
      thus [.x,y.] c= S.i by A9,A7,A6,XXREAL_2:def 12;
    end;
    suppose
A12:  y <= x;
      take [.y,x.];
      thus [.y,x.] is closed-interval by A12,INTEGRA1:def 1;
      x in [.y,x.] by A12;
      hence [.y,x.] meets A by A10,XBOOLE_0:3;
      y in [.y,x.] by A12;
      hence [.y,x.] meets B by A8,XBOOLE_0:3;
      thus [.y,x.] c= S.i by A9,A7,A6,XXREAL_2:def 12;
    end;
  end;
  consider T be Function of NAT, bool REAL such that
A13: for i being Element of NAT holds P[i,T.i] from FUNCT_2:sch 3(A5);
  T.0 meets B by A13;
  then
A14: B is non empty by XBOOLE_1:65;
  deffunc G(Element of NAT)=T.$1 /\ B;
  deffunc F(Element of NAT)=T.$1 /\ A;
  consider SA being Function of NAT, bool REAL such that
A15: for i being Element of NAT holds SA.i = F(i) from FUNCT_2:sch 4;
  consider SB being Function of NAT, bool REAL such that
A16: for i being Element of NAT holds SB.i = G(i) from FUNCT_2:sch 4;
  defpred P[Element of NAT,Real,Real] means $2 in SA.$1 & $3 in SB.$1 & dist(
  SA.$1,SB.$1) = abs($2 - $3);
A17: for i being Element of NAT ex ai,bi being Real st P[i,ai,bi]
  proof
    let i be Element of NAT;
    reconsider Si = T.i as closed-interval Subset of REAL by A13;
A18: T.i meets B by A13;
A19: SA.i = Si /\ A by A15;
A20: SB.i = Si /\ B by A16;
    T.i meets A by A13;
    then reconsider
    SAi = SA.i, SBi = SB.i as non empty compact Subset of REAL by A18,A19,A20
,Th14,XBOOLE_0:def 7;
    consider ai,bi being real number such that
A21: ai in SAi and
A22: bi in SBi and
A23: dist(SAi,SBi) = abs(ai - bi) by Th18;
    reconsider ai,bi as Real by XREAL_0:def 1;
    take ai,bi;
    thus thesis by A21,A22,A23;
  end;
  consider sa,sb being Real_Sequence such that
A24: for i being Element of NAT holds P[i,sa.i,sb.i] from DoubleChoice(
  A17);
  rng sa c= [.e,f.]
  proof
    let u be set;
    assume u in rng sa;
    then consider x being set such that
A25: x in dom sa and
A26: u = sa.x by FUNCT_1:def 5;
    reconsider n = x as Element of NAT by A25;
    sa.n in SA.n by A24;
    then u in T.n /\ A by A15,A26;
    then u in A by XBOOLE_0:def 4;
    hence thesis by A2;
  end;
  then consider ga being Real_Sequence such that
A27: ga is subsequence of sa and
A28: ga is convergent and
A29: lim ga in [.e,f.] by RCOMP_1:def 3;
  consider Nseq being increasing sequence of NAT such that
A30: ga = sa*Nseq by A27,VALUED_0:def 17;
  set gb = sb*Nseq;
  rng gb c= [.e,f.]
  proof
    let u be set;
    assume u in rng gb;
    then consider x being set such that
A31: x in dom gb and
A32: u = gb.x by FUNCT_1:def 5;
    reconsider n = x as Element of NAT by A31;
    gb.n = sb.(Nseq.n) by FUNCT_2:21;
    then gb.n in SB.(Nseq.n) by A24;
    then u in T.(Nseq.n) /\ B by A16,A32;
    then u in B by XBOOLE_0:def 4;
    hence thesis by A3;
  end;
  then consider fb being Real_Sequence such that
A33: fb is subsequence of gb and
A34: fb is convergent and
A35: lim fb in [.e,f.] by RCOMP_1:def 3;
  consider Nseq1 being increasing sequence of NAT such that
A36: fb = gb*Nseq1 by A33,VALUED_0:def 17;
  set fa = ga*Nseq1, r = (lim fa + lim fb)/2;
  set d0 = dist(A,B), ff = (1/2)(#)(fa+fb);
A37: fa is convergent by A28,SEQ_4:29;
  then
A38: fa+fb is convergent by A34,SEQ_2:19;
  then
A39: ff is convergent by SEQ_2:21;
  T.0 meets A by A13;
  then A is non empty by XBOOLE_1:65;
  then d0 > 0 by A1,A14,Th20;
  then
A40: d0/2 > 0 by XREAL_1:141;
  r = (1/2)*(lim fa + lim fb) .= (1/2)*lim(fa+fb) by A34,A37,SEQ_2:20
    .= lim ff by A38,SEQ_2:22;
  then consider k0 being Element of NAT such that
A41: for i being Element of NAT st k0 <= i holds abs(ff.i - r) < d0/2 by A39
,A40,SEQ_2:def 7;
  take r;
  lim fa = lim ga by A28,SEQ_4:30;
  hence r in [.e,f.] by A29,A35,Th9;
  now
    set i = Nseq.(Nseq1.k0), di = dist(SA.i,SB.i);
A42: 2*abs((sa.i+sb.i)/2 - r) = abs(2)*abs((sa.i+sb.i)/2 - r) by ABSVALUE:def 1
      .= abs(2*((sa.i+sb.i)/2 - r)) by COMPLEX1:151
      .= abs(sa.i+sb.i-2*r);
A43: fa.k0 = ga.(Nseq1.k0) by FUNCT_2:21
      .= sa.i by A30,FUNCT_2:21;
    T.i meets B by A13;
    then T.i /\ B <> {} by XBOOLE_0:def 7;
    then
A44: SB.i is non empty by A16;
A45: SB.i = T.i /\ B by A16;
    then
A46: SB.i c= B by XBOOLE_1:17;
A47: SB.i c= T.i by A45,XBOOLE_1:17;
A48: SA.i = T.i /\ A by A15;
    then
A49: SA.i c= A by XBOOLE_1:17;
    T.i meets A by A13;
    then T.i /\ A <> {} by XBOOLE_0:def 7;
    then
A50: SA.i is non empty by A15;
    then
A51: d0 <= di by A44,A49,A46,Th17;
    d0/2 <= di/2 by A50,A44,A49,A46,Th17,XREAL_1:74;
    then
A52: di/2 + d0/2 <= di/2 + di/2 by XREAL_1:8;
    ff.k0 = (1/2)*((fa+fb).k0) by SEQ_1:13
      .= ((fa+fb).k0)/2
      .= (fa.k0+fb.k0)/2 by SEQ_1:11;
    then
A53: abs((fa.k0+fb.k0)/2 - r) < d0/2 by A41;
    T.i is closed-interval by A13;
    then
A54: ex a,b being Real st a <= b & T.i = [.a,b.] by INTEGRA1:def 1;
A55: sb.i in SB.i by A24;
A56: SA.i c= T.i by A48,XBOOLE_1:17;
A57: fb.k0 = gb.(Nseq1.k0) by A36,FUNCT_2:21
      .= sb.i by FUNCT_2:21;
    2*(d0/2) = d0;
    then
A58: 2*abs((sa.i+sb.i)/2 - r) < d0 by A53,A43,A57,XREAL_1:70;
A59: sa.i in SA.i by A24;
    then
A60: di <= abs(sb.i-sa.i) by A55,Th16;
A61: now
      per cases;
      suppose
        sa.i <= sb.i;
        then sb.i - sa.i >= 0 by XREAL_1:50;
        then di <= sb.i-sa.i by A60,ABSVALUE:def 1;
        then d0 <= sb.i-sa.i by A51,XXREAL_0:2;
        then abs(sa.i+sb.i-2*r) <= sb.i-sa.i by A58,A42,XXREAL_0:2;
        then
A62:    r in [.sa.i,sb.i.] by RCOMP_1:9;
        [.sa.i,sb.i.] c= T.i by A54,A59,A55,A56,A47,XXREAL_2:def 12;
        hence r in T.i by A62;
      end;
      suppose
A63:    sb.i <= sa.i;
A64:    abs(sb.i-sa.i) = abs(sa.i-sb.i) by UNIFORM1:13;
        sa.i - sb.i >= 0 by A63,XREAL_1:50;
        then di <= sa.i-sb.i by A60,A64,ABSVALUE:def 1;
        then d0 <= sa.i-sb.i by A51,XXREAL_0:2;
        then abs(sb.i+sa.i-2*r) <= sa.i-sb.i by A58,A42,XXREAL_0:2;
        then
A65:    r in [.sb.i,sa.i.] by RCOMP_1:9;
        [.sb.i,sa.i.] c= T.i by A54,A59,A55,A56,A47,XXREAL_2:def 12;
        hence r in T.i by A65;
      end;
    end;
    assume
A66: r in A \/ B;
    per cases by A66,XBOOLE_0:def 3;
    suppose
A67:  r in B;
      SB.i = T.i /\ B by A16;
      then
A68:  r in SB.i by A61,A67,XBOOLE_0:def 4;
A69:  abs((fa.k0-fb.k0)/2) = abs((fa.k0-fb.k0))/abs(2) by COMPLEX1:153
        .= abs((fa.k0-fb.k0))/2 by ABSVALUE:def 1;
      fa.k0 - r = (fa.k0-fb.k0)/2 + ((fa.k0+fb.k0)/2 - r);
      then
A70:  abs(fa.k0 - r) <= abs((fa.k0-fb.k0)/2) + abs((fa.k0+fb.k0)/2 - r)
      by COMPLEX1:142;
      abs((fa.k0-fb.k0)/2) + abs((fa.k0+fb.k0)/2 - r) < abs((fa.k0-fb.k0
      )/2) + d0/2 by A53,XREAL_1:8;
      then abs(fa.k0 - r) < abs((fa.k0-fb.k0))/2 + d0/2 by A70,A69,XXREAL_0:2;
      then abs(fa.k0 - r) < di/2 + d0/2 by A24,A43,A57;
      then
A71:  abs(fa.k0 - r) < di by A52,XXREAL_0:2;
      fa.k0 in SA.i by A24,A43;
      hence contradiction by A68,A71,Th16;
    end;
    suppose
A72:  r in A;
      SA.i = T.i /\ A by A15;
      then
A73:  r in SA.i by A61,A72,XBOOLE_0:def 4;
A74:  abs((fb.k0-fa.k0)/2) = abs((fb.k0-fa.k0))/abs(2) by COMPLEX1:153
        .= abs((fb.k0-fa.k0))/2 by ABSVALUE:def 1;
      fb.k0 - r = (fb.k0-fa.k0)/2 + ((fb.k0+fa.k0)/2 - r);
      then
A75:  abs(fb.k0 - r) <= abs((fb.k0-fa.k0)/2) + abs((fb.k0+fa.k0)/2 - r)
      by COMPLEX1:142;
A76:  abs(fb.k0-fa.k0) = abs(fa.k0-fb.k0) by UNIFORM1:13
        .= di by A24,A43,A57;
      abs((fb.k0-fa.k0)/2) + abs((fb.k0+fa.k0)/2 - r) < abs((fb.k0-fa.k0
      )/2) + d0/2 by A53,XREAL_1:8;
      then abs(fb.k0 - r) < abs((fb.k0-fa.k0))/2 + d0/2 by A75,A74,XXREAL_0:2;
      then
A77:  abs(fb.k0 - r) < di by A52,A76,XXREAL_0:2;
      fb.k0 in SB.i by A24,A57;
      hence contradiction by A73,A77,Th16;
    end;
  end;
  hence not r in A \/ B;
  let i being Element of NAT;
  set k = max(k0,i);
  take j = Nseq.(Nseq1.k);
A78: fb.k = gb.(Nseq1.k) by A36,FUNCT_2:21
    .= sb.j by FUNCT_2:21;
A79: i <= k by XXREAL_0:25;
A80: sb.j in SB.j by A24;
  T.j meets B by A13;
  then T.j /\ B <> {} by XBOOLE_0:def 7;
  then
A81: SB.j is non empty by A16;
A82: dom Nseq = NAT by FUNCT_2:def 1;
  T.j meets A by A13;
  then T.j /\ A <> {} by XBOOLE_0:def 7;
  then
A83: SA.j is non empty by A15;
A84: i <= Nseq.i by SEQM_3:33;
A85: SA.j = T.j /\ A by A15;
  then
A86: SA.j c= T.j by XBOOLE_1:17;
  ff.k = (1/2)*((fa+fb).k) by SEQ_1:13
    .= ((fa+fb).k)/2
    .= (fa.k+fb.k)/2 by SEQ_1:11;
  then
A87: abs((fa.k+fb.k)/2 - r) < d0/2 by A41,XXREAL_0:25;
A88: 2*(d0/2) = d0;
  fa.k = ga.(Nseq1.k) by FUNCT_2:21
    .= sa.j by A30,FUNCT_2:21;
  then
A89: 2*abs((sa.j+sb.j)/2 - r) < d0 by A87,A78,A88,XREAL_1:70;
  T.j is closed-interval by A13;
  then
A90: ex a,b be Real st a <= b & T.j = [.a,b.] by INTEGRA1:def 1;
A91: SB.j = T.j /\ B by A16;
  then
A92: SB.j c= B by XBOOLE_1:17;
A93: SB.j c= T.j by A91,XBOOLE_1:17;
  dom Nseq1 = NAT by FUNCT_2:def 1;
  then Nseq1.i <= Nseq1.k by A79,VALUED_0:def 15;
  then
A94: Nseq.(Nseq1.i) <= j by A82,VALUED_0:def 15;
  i <= Nseq1.i by SEQM_3:33;
  then Nseq.i <= Nseq.(Nseq1.i) by A82,VALUED_0:def 15;
  then i <= Nseq.(Nseq1.i) by A84,XXREAL_0:2;
  hence i <= j by A94,XXREAL_0:2;
  set di = dist(SA.j,SB.j);
A95: 2*abs((sa.j+sb.j)/2 - r) = abs(2)*abs((sa.j+sb.j)/2 - r) by ABSVALUE:def 1
    .= abs(2*((sa.j+sb.j)/2 - r)) by COMPLEX1:151
    .= abs(sa.j+sb.j-2*r);
  SA.j c= A by A85,XBOOLE_1:17;
  then
A96: d0 <= di by A83,A81,A92,Th17;
A97: sa.j in SA.j by A24;
  then
A98: di <= abs(sb.j-sa.j) by A80,Th16;
A99: now
    per cases;
    suppose
      sa.j <= sb.j;
      then sb.j - sa.j >= 0 by XREAL_1:50;
      then di <= sb.j-sa.j by A98,ABSVALUE:def 1;
      then d0 <= sb.j-sa.j by A96,XXREAL_0:2;
      then abs(sa.j+sb.j-2*r) <= sb.j-sa.j by A89,A95,XXREAL_0:2;
      then
A100: r in [.sa.j,sb.j.] by RCOMP_1:9;
      [.sa.j,sb.j.] c= T.j by A90,A97,A80,A86,A93,XXREAL_2:def 12;
      hence r in T.j by A100;
    end;
    suppose
A101: sb.j <= sa.j;
A102: abs(sb.j-sa.j) = abs(sa.j-sb.j) by UNIFORM1:13;
      sa.j - sb.j >= 0 by A101,XREAL_1:50;
      then di <= sa.j-sb.j by A98,A102,ABSVALUE:def 1;
      then d0 <= sa.j-sb.j by A96,XXREAL_0:2;
      then abs(sb.j+sa.j-2*r) <= sa.j-sb.j by A89,A95,XXREAL_0:2;
      then
A103: r in [.sb.j,sa.j.] by RCOMP_1:9;
      [.sb.j,sa.j.] c= T.j by A90,A97,A80,A86,A93,XXREAL_2:def 12;
      hence r in T.j by A103;
    end;
  end;
  T.j c= S.j by A13;
  hence thesis by A99;
end;
 
:: Moved from JORDAN1A, AK, 23.02.2006

theorem Th22:
  for S being closed Subset of TOP-REAL 2 st S is Bounded holds
  proj2.:S is closed
proof
  let S be closed Subset of TOP-REAL 2;
  assume S is Bounded;
  then Cl(proj2.:S) = proj2.:Cl S by TOPREAL6:93
    .= proj2.:S by PRE_TOPC:52;
  hence thesis;
end;

theorem Th23:
  for S being Subset of TOP-REAL 2 st S is Bounded holds proj2.:S is bounded
proof
  let S be Subset of TOP-REAL 2;
  assume S is Bounded;
  then reconsider C = S as bounded Subset of Euclid 2 by JORDAN2C:def 2;
  consider r being Real, x being Point of Euclid 2 such that
A1: 0 < r and
A2: C c= Ball(x,r) by METRIC_6:def 10;
  reconsider P = Ball(x,r) as Subset of TOP-REAL 2 by TOPREAL3:13;
  reconsider p = x as Point of TOP-REAL 2 by TOPREAL3:13;
  set t = max(abs(p`2-r),abs(p`2+r));
  now
    assume that
A3: abs(p`2-r) <= 0 and
A4: abs(p`2+r) <= 0;
    abs(p`2-r) = 0 by A3,COMPLEX1:132;
    then abs(r-p`2) = 0 by UNIFORM1:13;
    then
A5: r-p`2 = 0 by ABSVALUE:7;
    abs(p`2+r) = 0 by A4,COMPLEX1:132;
    hence contradiction by A1,A5,ABSVALUE:7;
  end;
  then
A6: t > 0 by XXREAL_0:30;
A7: proj2.:P = ].p`2-r,p`2+r.[ by TOPREAL6:52;
  for s st s in proj2.:S holds abs(s) < t
  proof
    let s;
    proj2.:S c= proj2.:P by A2,RELAT_1:156;
    hence thesis by A7,Th12;
  end;
  hence thesis by A6,SEQ_4:14;
end;

theorem
  for S being compact Subset of TOP-REAL 2 holds proj2.:S is compact
proof
  let S being compact Subset of TOP-REAL 2;
  proj2.:S is closed by Th22;
  hence thesis by Th23,RCOMP_1:29;
end;
