:: Solving Roots of Polynomial Equations of Degree 2 and 3 with
:: Real Coefficients
::  by Liang Xiquan
::
:: Received May 18, 2000
:: Copyright (c) 2000 Association of Mizar Users
 
environ

 vocabularies NUMBERS, XREAL_0, ORDINAL1, XCMPLX_0, RELAT_1, ARYTM_3, REAL_1,
      CARD_1, ARYTM_1, SQUARE_1, FUNCT_3, XXREAL_0, NEWTON, POWER, PREPOWER,
      POLYEQ_1, ABIAN;
 notations ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0, REAL_1, NAT_1, SQUARE_1,
      NEWTON, PREPOWER, POWER, QUIN_1, XXREAL_0, ABIAN;
 constructors REAL_1, SQUARE_1, NAT_1, MEMBERED, QUIN_1, NEWTON, PREPOWER,
      POWER, ABIAN;
 registrations XCMPLX_0, XREAL_0, SQUARE_1, MEMBERED, QUIN_1, NEWTON;
 requirements NUMERALS, SUBSET, REAL, ARITHM, BOOLE;
 definitions SQUARE_1;
 theorems SQUARE_1, PREPOWER, POWER, QUIN_1, NEWTON, XREAL_0, XCMPLX_0,
      XCMPLX_1, XREAL_1, XXREAL_0, ABIAN;

begin :: Polynomial of Degree 1 and 2

reserve a, a9, a1, a2, a3, b, b9, c, c9, d, d9, h, p, q, x, x1, x2, x3, u, v,
  y, z for real number;

definition
  let a, b, x be complex number;
  func Polynom (a,b,x) equals
  a*x+b;
  coherence;
end;

registration
  let a, b, x be complex number;
  cluster Polynom(a,b,x) -> complex;
  coherence;
end;

registration
  let a, b, x be real number;
  cluster Polynom(a,b,x) -> real;
  coherence;
end;

definition
  let a, b, x be Real;
  redefine func Polynom(a,b,x) -> Real;
  coherence by XREAL_0:def 1;
end;

theorem
  for a, b, x being complex number holds a <> 0 & Polynom(a,b,x) = 0
  implies x = -(b/a)
proof
  let a, b, x be complex number;
  assume that
A1: a <> 0 and
A2: Polynom(a,b,x) = 0;
  a"*(-b) = a"*(a*x) by A2
    .= (a"*a)*x;
  then 1*x = a"*(-b) by A1,XCMPLX_0:def 7;
  then x = -(a"*b);
  hence thesis by XCMPLX_0:def 9;
end;

theorem
  for x being complex number holds Polynom(0,0,x) = 0;

theorem
  for b being complex number holds b <> 0 implies not ex x being complex
  number st Polynom(0,b,x) = 0;

definition
  let a,b,c,x be complex number;
  func Polynom(a,b,c,x) equals
  a*x^2+b*x+c;
  coherence;
end;

registration
  let a,b,c,x be real number;
  cluster Polynom(a,b,c,x) -> real;
  coherence;
end;

registration
  let a,b,c,x be complex number;
  cluster Polynom(a,b,c,x) -> complex;
  coherence;
end;

definition
  let a,b,c,x be Real;
  redefine func Polynom(a,b,c,x) -> Real;
  coherence by XREAL_0:def 1;
end;

theorem Th4:
  for a, b, c, a9, b9, c9 being complex number holds (for x being
  real number holds Polynom(a,b,c,x) = Polynom(a9,b9,c9,x)) implies a = a9& b =
  b9& c = c9
proof
  let a, b, c, a9, b9, c9 be complex number;
  assume
A1: for x being real number holds Polynom(a,b,c,x) = Polynom(a9,b9,c9,x);
  then
A2: Polynom(a,b,c,-1) = Polynom(a9,b9,c9,-1);
  Polynom(a,b,c,0) = Polynom(a9,b9,c9,0) & Polynom(a,b,c,1) = Polynom(a9,
  b9,c9,1) by A1;
  hence thesis by A2;
end;

theorem Th5:
  a <> 0 & delta(a,b,c) >= 0 implies for x holds Polynom(a,b,c,x) =
0 implies x = (-b+sqrt delta(a,b,c))/(2*a) or x = (-b-sqrt delta(a,b,c))/(2*a)
proof
  assume that
A1: a <> 0 and
A2: delta(a,b,c)>=0;
  now
    set e = a*c;
    let y;
    set t = a^2*y^2+(a*b)*y;
    assume Polynom(a,b,c,y) = 0;
    then a*(a*y^2+b*y+c) = a*0;
    then a*(a*y^2)+a*(b*y)+a*c = 0;
    then
A3: t +b^2/4-b^2*4" = -(4*e)*4";
A4: delta(a,b,c) = b^2-4*a*c by QUIN_1:def 1;
A5: now
      assume ((a*y+b/2) -sqrt((b^2-4*(a*c))/4)) =0;
      then (a*y+b/2) = sqrt(b^2-4*(a*c))/2 by A2,A4,SQUARE_1:85,99;
      then a*y = -(b*2" - sqrt(b^2-4*(a*c))*2" )
        .= ((-b)*2" +(sqrt delta(a,b,c)*2")) by A4;
      then y = ((-b)*2" +(sqrt delta(a,b,c)*2")) /a by A1,XCMPLX_1:90
        .= ((-b)*2" +(sqrt delta(a,b,c)*2"))*a" by XCMPLX_0:def 9
        .= (-b +sqrt delta(a,b,c))*(2"*a")
        .= (-b +sqrt delta(a,b,c))*(2*a)" by XCMPLX_1:205;
      hence y = (-b +sqrt delta(a,b,c))/(2*a) by XCMPLX_0:def 9;
    end;
A6: now
      assume (a*y+b/2) +sqrt((b^2-4*(a*c))/4) = 0;
      then (a*y+b/2) = - sqrt((b^2-4*(a*c))/4);
      then a*y+b/2 = -sqrt(b^2-4*(a*c))/2 by A2,A4,SQUARE_1:85,99;
      then a*y = -(b*2" + sqrt(b^2-4*(a*c))*2" )
        .= ((-b)*2" -(sqrt delta(a,b,c)*2")) by A4;
      then y = ((-b)*2" -(sqrt delta(a,b,c)*2")) /a by A1,XCMPLX_1:90
        .= ((-b)*2" -(sqrt delta(a,b,c)*2"))*a" by XCMPLX_0:def 9
        .= (-b -sqrt delta(a,b,c))*(2"*a")
        .= (-b -sqrt delta(a,b,c))*(2*a)" by XCMPLX_1:205;
      hence y = (-b -sqrt delta(a,b,c))/(2*a) by XCMPLX_0:def 9;
    end;
    (b^2-4*(a*c))/4 >= 0/4 by A2,A4,XREAL_1:74;
    then (a*y+b/2)^2 = (sqrt((b^2-4*(a*c))/4))^2 by A3,SQUARE_1:def 4;
    then
    ((a*y+b/2) - sqrt((b^2-4*(a*c))/4))* ((a*y+b/2) + sqrt((b^2-4*(a*c))/
    4)) = 0;
    hence y = (-b+sqrt delta(a,b,c))/(2*a) or y = (-b-sqrt delta(a,b,c))/(2*a)
    by A5,A6,XCMPLX_1:6;
  end;
  hence thesis;
end;

theorem
  for a, b, c, x being complex number holds a <> 0 & delta(a,b,c) = 0 &
  Polynom(a,b,c,x) = 0 implies x = -(b/(2*a))
proof
  let a, b, c, x be complex number;
  assume that
A1: a <> 0 and
A2: delta(a,b,c) = 0;
  set y = x;
  set t = a^2*y^2+(a*b)*y;
A3: b^2-4*a*c = delta(a,b,c) by QUIN_1:def 1;
  assume Polynom(a,b,c,y) = 0;
  then a*(a*y^2+b*y+c) = 0;
  then t +a*c = 0;
  then (a*y)^2+ 2*((a*y)*b*2") + (b/2)^2 = 0 by A2,A3;
  then (a*y+b/2)^2 = 0;
  then a*y+b/2 = 0 by XCMPLX_1:6;
  then y = (- b*2")/a by A1,XCMPLX_1:90
    .= (-1)*(b*2")*a" by XCMPLX_0:def 9
    .= -(b*(2"*a"))*1
    .= -(b*(2*a)") by XCMPLX_1:205;
  hence thesis by XCMPLX_0:def 9;
end;

theorem
  delta(a,b,c) < 0 implies not ex x st Polynom(a,b,c,x) = 0
proof
  set e = a*c;
  assume delta(a,b,c) < 0;
  then (b^2-4*a*c) < 0 by QUIN_1:def 1;
  then
A1: (b^2-4*(a*c))*4" < 0 by XREAL_1:134;
  given y such that
A2: Polynom(a,b,c,y) = 0;
  set t = a^2*y^2+(a*b)*y;
  a*(a*y^2+b*y+c) = a*0 by A2;
  then t +b^2/4-(b^2*4"-(4*e)*4") = 0;
  then
A3: (a*y+b/2)^2 = (b^2-4*(a*c))*4";
  then (a*y+b/2) > 0 by A1,XREAL_1:135;
  hence contradiction by A3,A1,XREAL_1:135;
end;

theorem
  for b, c being complex number holds b <> 0 & (for x being real number
  holds Polynom(0,b,c,x) = 0) implies x = -(c/b)
proof
  let b, c be complex number;
  assume
A1: b <> 0;
  set y = x;
  assume for x being real number holds Polynom(0,b,c,x) = 0;
  then Polynom(0,b,c,y) = 0;
  then y = (-c)/b by A1,XCMPLX_1:90
    .= ((-1)*c)*b" by XCMPLX_0:def 9
    .= (-1)*(c*b")
    .= (-1)*(c/b) by XCMPLX_0:def 9;
  hence thesis;
end;

theorem
  for x being complex number holds Polynom(0,0,0,x) = 0;

theorem
  for c being complex number holds c <> 0 implies not ex x being complex
  number st Polynom(0,0,c,x) = 0;

definition
  let a,x,x1,x2 be complex number;
  func Quard(a,x1,x2,x) equals
  a*((x-x1)*(x-x2));
  coherence;
end;

registration
  let a,x,x1,x2 be real number;
  cluster Quard(a,x1,x2,x) -> real;
  coherence;
end;

definition
  let a,x,x1,x2 be Real;
  redefine func Quard(a,x1,x2,x) -> Real;
  coherence by XREAL_0:def 1;
end;

theorem
  for a, b, c being complex number holds a <> 0 & (for x being real
number holds Polynom(a,b,c,x) = Quard(a,x1,x2,x)) implies b/a = -(x1+x2) & c/a
  = x1*x2
proof
  let a, b, c be complex number;
  assume
A1: a <> 0;
  assume
A2: for x being real number holds Polynom(a,b,c,x) = Quard(a,x1,x2,x);
  now
    let z be real number;
    set h = z-x1, t = z-x2;
    set e = h*t-z^2;
    Polynom(a,b,c,z) = Quard(a,x1,x2,z) by A2;
    then a*(h*t-z^2) = (b*z+c);
    then e = (b*z+c)/a by A1,XCMPLX_1:90
      .= a"*(b*z+c) by XCMPLX_0:def 9
      .= a"*(b*z)+a"*c;
    then z*z-z*x2-x1*z+x1*x2 = z^2+(a"*b)*z+a"*c;
    then z^2-(x1+x2)*z+x1*x2 = z^2+(b/a)*z+a"*c by XCMPLX_0:def 9
      .= z^2+(b/a)*z+(c/a) by XCMPLX_0:def 9;
    hence Polynom(1,-(x1+x2),x1*x2,z) = Polynom(1,b/a,c/a,z);
  end;
  hence thesis by Th4;
end;

begin :: Polynomial of Degree 3

definition
  let a,b,c,d,x be complex number;
  func Polynom(a,b,c,d,x) equals
  a*(x |^ 3)+ b*x^2 +c*x +d;
  coherence;
end;

registration
  let a,b,c,d,x be complex number;
  cluster Polynom(a,b,c,d,x) -> complex;
  coherence;
end;

registration
  let a,b,c,d,x be real number;
  cluster Polynom(a,b,c,d,x) -> real;
  coherence;
end;

definition
  let a,b,c,d,x be Real;
  redefine func Polynom(a,b,c,d,x) -> Real;
  coherence by XREAL_0:def 1;
end;

3 = 2*1 + 1;
then

Lm1: 3 is odd by ABIAN:1;

theorem Th12:
  (for x holds Polynom(a,b,c,d,x) = Polynom(a9,b9,c9,d9,x))
  implies a = a9 & b = b9 & c = c9 & d = d9
proof
  (-1) |^ 3 = - (1 |^ (2+1)) by Lm1,POWER:2
    .= - ((1 |^ 2)*1) by NEWTON:11;
  then
A1: (0 |^ 3) = 0 & (-1) |^ 3 = - 1 by NEWTON:15,16;
A2: 2 |^ 1 = 2 to_power 1 by POWER:46
    .= 2 by POWER:30;
A3: 2 |^ 3 = 2 |^ (2+1) .= (2 |^ (1+1))*2 by NEWTON:11
    .= ((2 |^ 1)*2)*2 by NEWTON:11
    .= (2 |^ 1)*(2*2);
  assume
A4: for x holds Polynom(a,b,c,d,x) = Polynom(a9,b9,c9,d9,x);
  then
A5: Polynom(a,b,c,d,2) = Polynom(a9,b9,c9,d9,2);
  Polynom(a,b,c,d,1) = Polynom(a9,b9,c9,d9,1) by A4;
  then a*1 + b*1 +c*1 +d = Polynom(a9,b9,c9,d9,1) by NEWTON:15;
  then
A6: a + b +c +d = a9*1+ b9*1 +c9*1 +d9 by NEWTON:15
    .= a9+ b9 +c9 +d9;
  Polynom(a,b,c,d,0) = Polynom(a9,b9,c9,d9,0) & Polynom(a,b,c,d,-1) =
  Polynom( a9,b9,c9,d9,-1) by A4;
  hence thesis by A6,A1,A5,A3,A2;
end;

definition
  let a,x,x1,x2,x3 be real number;
  func Tri(a,x1,x2,x3,x) equals
  a*((x-x1)*(x-x2)*(x-x3));
  coherence;
end;

registration
  let a,x,x1,x2,x3 be real number;
  cluster Tri(a,x1,x2,x3,x) -> real;
  coherence;
end;

definition
  let a,x,x1,x2,x3 be Real;
  redefine func Tri(a,x1,x2,x3,x) -> Real;
  coherence by XREAL_0:def 1;
end;

theorem
  a <> 0 & (for x holds Polynom(a,b,c,d,x) = Tri(a,x1,x2,x3,x)) implies
  b/a = -(x1+x2+x3) & c/a = x1*x2 +x2*x3 +x1*x3 & d/a = -x1*x2*x3
proof
  assume
A1: a <> 0;
  set t3 = d/a;
  set t2 = c/a;
  set t1 = b/a;
  set d9 = -x1*x2*x3;
  set c9 = x1*x2+x2*x3+x1*x3;
  set b9 = -(x1+x2+x3);
  assume
A2: for x holds Polynom(a,b,c,d,x) = Tri(a,x1,x2,x3,x);
  now
    let x;
    set t = a*(x |^ 3)+ b*x^2 +c*x +d;
    set r8 = ((x-x1)*(x-x2)*(x-x3));
A3: x |^ 1 = x to_power 1 by POWER:46;
    x |^ 3 = x |^ (2+1) .= (x |^ (1+1))*x by NEWTON:11
      .= ((x |^ 1)*x)*x by NEWTON:11;
    then
A4: x |^ 3 = (x*x)*x by A3,POWER:30;
    Polynom(a,b,c,d,x) = Tri(a,x1,x2,x3,x)by A2;
    then
A5: t/a = r8 by A1,XCMPLX_1:90;
    a"*t = (a"*a)*(x |^ 3)+ (a"*b)*x^2 +a"*(c*x +d)
      .= 1*(x |^ 3)+ (a"*b)*x^2 +((a"*c)*x +a"*d) by A1,XCMPLX_0:def 7
      .= 1*(x |^ 3)+ t1*x^2 +((a"*c)*x +a"*d) by XCMPLX_0:def 9
      .= 1*(x |^ 3)+ t1*x^2 + (t2*x +a"*d) by XCMPLX_0:def 9
      .= 1*(x |^ 3)+ t1*x^2 + (t2*x + t3) by XCMPLX_0:def 9
      .= Polynom(1,t1,t2,t3,x);
    hence Polynom(1,t1,t2,t3,x) = Polynom(1,b9, c9,d9,x) by A5,A4,XCMPLX_0:
    def 9;
  end;
  hence thesis by Th12;
end;

theorem Th14:
  (y+h) |^ 3 = y |^ 3+((3*h)*y^2+(3*h^2)*y)+h |^ 3
proof
  (y+h) |^ 3 = (y+h) |^ (2+1);
  then
A1: (y+h) |^ 3 = ((y+h) |^ (1+1))*(y+h) by NEWTON:11
    .= ((y+h) |^ 1*(y+h))*(y+h) by NEWTON:11
    .= ((y+h) |^ 1)*(y+h)^2
    .= ((y+h) to_power 1)*(y^2+2*y*h+h^2) by POWER:46
    .= (y+h)*(y^2+2*y*h+h^2) by POWER:30
    .= y*y^2+((3*h)*y^2+((2+1)*h^2)*y)+h*h^2;
  y |^ 3 = y |^ (2+1) .= (y |^ (1+1))*y by NEWTON:11
    .= (((y |^ 1)*y))*y by NEWTON:11
    .= (y |^ 1)*y^2;
  then
A2: y |^ 3 = (y to_power 1)*y^2by POWER:46;
  h |^ 3 = h |^ (2+1) .= (h |^ (1+1))*h by NEWTON:11
    .= (((h |^ 1)*h))*h by NEWTON:11
    .= (h |^ 1)*h^2
    .= (h to_power 1)*h^2 by POWER:46
    .= h*h^2 by POWER:30;
  hence thesis by A1,A2,POWER:30;
end;

theorem Th15:
  a <> 0 & Polynom(a,b,c,d,x) = 0 implies for a1,a2,a3,h,y st y =
(x+b/(3*a)) & h = -b/(3*a) & a1 = b/a & a2 = c/a & a3 = d/a holds y |^ 3 + ((3*
  h+a1)*y^2+(3*h^2+2*(a1*h)+a2)*y) + ((h |^ 3+a1*h^2)+(a2*h+a3)) = 0
proof
  assume
A1: a <> 0;
  assume
A2: Polynom(a,b,c,d,x) = 0;
  let a1,a2,a3,h,y;
  assume that
A3: y = x+ b/(3*a) & h = -b/(3*a) and
A4: a1 = b/a & a2 = c/a & a3 = d/a;
  0 = a"*(a*(x |^ 3)+ b*x^2 +(c*x +d)) by A2
    .= (a"*a)*(x |^ 3)+ a"*(b*x^2) +a"*(c*x +d)
    .= 1*(x |^ 3)+ a"*(b*x^2) +a"*(c*x +d) by A1,XCMPLX_0:def 7
    .= (x |^ 3)+ (a"*b)*x^2 +(a"*c)*x +a"*d
    .= (x |^ 3)+ (b/a)*x^2 +(a"*c)*x +a"*d by XCMPLX_0:def 9
    .= (x |^ 3)+ (b/a)*x^2 +(c/a)*x +a"*d by XCMPLX_0:def 9
    .= (x |^ 3)+ a1*x^2 + a2*x + a3 by A4,XCMPLX_0:def 9;
  then
  0 = y |^ 3 +((3*h)*y^2+(3*h^2)*y)+h |^ 3 + (a1*y^2+2*(a1*h)*y+a1*h^2) +
  a2*(y+h) + a3 by A3,Th14
    .= y |^ 3 +((3*h)*y^2+(3*h^2)*y)+(2*(a1*h)*y+a1*y^2) +(a2*y + ((h |^ 3 +
  a1*h^2)+(a2*h + a3)));
  hence thesis;
end;

theorem
  a <> 0 & Polynom(a,b,c,d,x) = 0 implies for a1,a2,a3,h,y st y = (x+b/(
3*a)) & h = -b/(3*a) & a1 = b/a & a2 = c/a & a3 = d/a holds y |^ 3 + 0*y^2 + ((
  3*a*c-b^2)/(3*a^2))*y + (2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2)) = 0
proof
  assume
A1: a <> 0;
  then
A2: 3*a <> 0;
  assume
A3: Polynom(a,b,c,d,x) = 0;
  let a1,a2,a3,h,y;
  assume that
A4: y = (x + b/(3*a)) and
A5: h = -b/(3*a) and
A6: a1 = b/a and
A7: a2 = c/a and
A8: a3 = d/a;
  set p0 = 3*h+a1;
A9: p0 = -(3*(b/(3*a)))+a1 by A5
    .= -(3*((3*a)"*b))+a1 by XCMPLX_0:def 9
    .= -(3*((3"*a")*b))+a1 by XCMPLX_1:205
    .= -(((3*3")*a")*b)+a1
    .= -(b/a)+a1 by XCMPLX_0:def 9;
  set q2 = (h |^ 3 +a1*h^2)+(a2*h + a3);
A10: q2 = 2*((b/(3*a)) |^ 3)+(3*a*d-b*c)/(3*a^2)
  proof
    set t = 3*a;
    set a6 = b/t;
A11: b/a = (3*b)/t by XCMPLX_1:92;
A12: a6 |^ 3 = a6 |^ (2+1) .= (a6 |^ (1+1))*a6 by NEWTON:11
      .= (a6 |^ 1)*a6*a6 by NEWTON:11
      .= (a6 |^ 1)*a6^2
      .= (a6 to_power 1)*a6^2 by POWER:46;
    q2 = ((-b/t) |^ 3 +b/a*(-b/t)^2) +(-(c/a*(b/t)) + d/a) by A5,A6,A7,A8
      .= ((-b/t) |^ 3 +b/a*(-b/t)^2) +(-(b*c)/(t*a) + d/a) by XCMPLX_1:77
      .= ((-b/t) |^ 3 +b/a*(-b/t)^2) +(-(b*c)/(3*a^2) + 1*(d/a))
      .= ((-b/t) |^ 3 +b/a*(-b/t)^2) +(-(b*c)/(3*a^2) + (t/t)*(d/a)) by A2,
    XCMPLX_1:60
      .= ((-b/t) |^ 3 +b/a*(-b/t)^2) + ((t/t)*(d/a))- (b*c)/(3*a^2)
      .= ((-b/t) |^ 3 +b/a*(-b/t)^2) + ((t*d)/(t*a))- (b*c)/(3*a^2) by
    XCMPLX_1:77
      .= ((-b/t) |^ 3 +b/a*(-b/t)^2) +(t*d)*(3*a^2)"-(b*c)/(3*a^2) by
    XCMPLX_0:def 9
      .= ((-b/t) |^ 3 +b/a*(-b/t)^2) +(t*d)*(3*a^2)"-(b*c)*(3*a^2)" by
    XCMPLX_0:def 9
      .= ((-b/t) |^ 3 +b/a*(-b/t)^2) +(t*d-b*c)*(3*a^2)"
      .= (-b/t) |^ (2+1) +b/a*(b/t)^2 +(t*d-b*c)/(3*a^2) by XCMPLX_0:def 9
      .= ((-b/t) |^ (1+1))*(-b/t) +b/a*(b/t)^2 +( t*d-b*c)/(3*a^2) by NEWTON
    :11
      .= ((-b/t) |^ 1)*(-b/t)*(-b/t) +b/a*(b/t)^2+( t*d-b*c)/(3*a^2) by
    NEWTON:11
      .= ((-b/t) |^ 1)*(-b/t)^2 +b/a*(b/t)^2 +( t*d-b*c)/(3*a^2)
      .= ((-b/t) to_power 1)*(-b/t)^2 +b/a*(b/t)^2 +( t*d-b*c)/(3*a^2) by
    POWER:46
      .= (-b/t)*(b/t)^2 +b/a*(b/t)^2 +(t*d-b*c)/(3*a^2) by POWER:30
      .= (-b/t)*(b^2/t^2) +b/a*(b/t)^2 +(t*d-b*c)/(3*a^2) by XCMPLX_1:77
      .= ((-b/t)*(b^2/t^2) +b/a*(b^2/t^2)) +(t*d-b*c)/(3*a^2) by XCMPLX_1:77
      .= (( b/a -b/t)*(b^2/t^2)) +(t*d-b*c)/(3*a^2)
      .= (((3*b)*t" -1*b/t)*(b^2/t^2)) +( t*d-b*c)/(3*a^2) by A11,XCMPLX_0:
    def 9
      .= (((3*b)*t" -1*b*t")*(b^2/t^2)) +( t*d-b*c)/(3*a^2) by XCMPLX_0:def
    9
      .= 2*((b*t")*(b^2/t^2)) +(t*d-b*c)/(3*a^2)
      .= 2*((b/t)*(b^2/t^2)) +(t*d-b*c)/(3*a^2) by XCMPLX_0:def 9
      .= 2*((b/t)*(b/t)^2) +(t*d-b*c)/(3*a^2) by XCMPLX_1:77;
    hence thesis by A12,POWER:30;
  end;
  set p2 = (3*h^2+2*(a1*h)+a2);
A13: p2 = (3*a*c-b^2)/(3*a^2)
  proof
    set t = ((3*a)"*b);
A14: (3*a)/(3*a) = 1 by A2,XCMPLX_1:60;
    p2 = (3*(b/(3*a))^2+2*(a1*-b/(3*a))+a2) by A5
      .= (3*((3*a)"*b)^2+2*(a1*-b/(3*a))+a2) by XCMPLX_0:def 9
      .= ((3*(3*a)")*b)*t+2*(a1*-b/(3*a))+a2
      .= (3*(3"*a")*b)*t+2*(a1*-b/(3*a))+a2 by XCMPLX_1:205
      .= (b/a)*((3*a)"*b)+2*(a1*-b/(3*a))+a2 by XCMPLX_0:def 9
      .= (b/a)*(b/(3*a))+2*(a1*-b/(3*a))+a2 by XCMPLX_0:def 9
      .= (b*b)/(a*(3*a))+2*(a1*-b/(3*a))+a2 by XCMPLX_1:77
      .= b^2/(3*a^2)-2*(b/a*(b/(3*a))) +c/a by A6,A7
      .= b^2/(3*a^2)-2*((b*b)/(a*(3*a))) +c/a by XCMPLX_1:77
      .= -b^2/(3*a^2) +((3*a)/(3*a))*(c/a) by A14
      .= -b^2/(3*a^2) +(3*a*c)/(3*a*a) by XCMPLX_1:77
      .= (3*a*c)/(3*a^2)-b^2/(3*a^2)
      .= (3*a*c)*(3*a^2)"-b^2/(3*a^2) by XCMPLX_0:def 9
      .= (3*a*c)*(3*a^2)"-b^2*(3*a^2)" by XCMPLX_0:def 9
      .= ((3*a*c)-b^2)*(3*a^2)";
    hence thesis by XCMPLX_0:def 9;
  end;
  y |^ 3 +(p0*y^2+p2*y) + q2 = 0 by A1,A3,A4,A5,A6,A7,A8,Th15;
  hence thesis by A6,A9,A13,A10;
end;

theorem
  (y |^ 3)+0*y^2+((3*a*c-b^2)/(3*a^2))*y + (2*((b/(3*a)) |^ 3)+(3*a*d-b*
  c)/(3*a^2)) = 0 implies for p,q st p = (3*a*c-b^2)/(3*a^2) & q = 2*((b/(3*a))
  |^ 3)+(3*a*d-b*c)/(3*a^2) holds Polynom(1,0,p,q,y) = 0;

theorem Th18:
  Polynom(1,0,p,q,y) = 0 implies for u,v st y = u+v & 3*v*u+p = 0
  holds u |^ 3 + v |^ 3 = -q & (u |^ 3)*(v |^ 3) = (-p/3) |^ 3
proof
  assume
A1: Polynom(1,0,p,q,y) = 0;
  let u,v;
  assume that
A2: y = u+v and
A3: 3*v*u+p = 0;
  (u+v) |^ 3 = u |^ 3+((3*v)*u^2+(3*v^2)*u)+v |^ 3 by Th14
    .= u |^ 3+(3*v*u)*(u+v)+v |^ 3;
  then 0 = (u |^ 3+v |^ 3)+((3*v*u + p )*(u+v)) + q by A1,A2;
  hence u |^ 3+v |^ 3 = - q by A3;
  3*(v*u) = - p by A3;
  hence thesis by NEWTON:12;
end;

theorem Th19:
  Polynom(1,0,p,q,y) = 0 implies for u,v st y = u+v & 3*v*u+p = 0
holds y = 3-root(-q/2+sqrt(q^2/4+(p/3) |^ 3)) + 3-root(-q/2-sqrt(q^2/4+(p/3) |^
  3)) or y = 3-root(-q/2+sqrt(q^2/4+(p/3) |^ 3)) + 3-root(-q/2+sqrt(q^2/4+(p/3)
|^ 3)) or y = 3-root(-q/2-sqrt(q^2/4+(p/3) |^ 3)) + 3-root(-q/2-sqrt(q^2/4+(p/3
  ) |^ 3))
proof
  set e1 = 1;
  assume
A1: Polynom(1,0,p,q,y) = 0;
  set e3 = (-p/3) |^ 3;
  set e2 = q;
  let u,v;
  assume that
A2: y = u+v and
A3: 3*v*u+p = 0;
  set z2 = v |^ 3;
  set z1 = u |^ 3;
A4: now
    let z;
    thus (z-z1)*(z-z2)= z^2-(z1+z2)*z+z1*z2
      .= z^2-(-q)*z+z1*z2 by A1,A2,A3,Th18
      .= z^2+q*z+(-p/3) |^ 3 by A1,A2,A3,Th18;
  end;
A5: z1+ z2 = -q by A1,A2,A3,Th18;
  then e2^2 = (z1+z2)^2 by SQUARE_1:61;
  then
A6: e2^2-(4*e1*e3) = -(-(z1^2+2*z1*z2+z2^2))-4*(z1*z2) by A1,A2,A3,
  Th18
    .= (z1-z2)^2;
  then
A7: (e2^2-4*e1*e3)>= 0 by XREAL_1:65;
  then
A8: delta(e1,e2,e3) >= 0 by QUIN_1:def 1;
  (z1-z1)*(z1-z2)= 0*(z1-z2);
  then
A9: Polynom(e1,e2,e3,z1) = 0 by A4;
  (z2-z1)*(z2-z2) = (z2-z1)*0;
  then
A10: Polynom(e1,e2,e3,z2) = 0 by A4;
A11: z2*z1 = (-p/3) |^ 3 by A1,A2,A3,Th18;
  now
    per cases by A9,A8,Th5;
    case
A12:  z1 = (-e2+sqrt delta(e1,e2,e3))/(2*e1);
A13:  (p/3) |^ 3 = (p/3) |^ (2+1) .= ((p/3) |^ (1+1))*(p/3) by NEWTON:11
        .= ((((p/3) |^ 1)*(p/3)))*(p/3) by NEWTON:11
        .= ((p/3) |^ 1)*(p/3)^2
        .= ((p/3) to_power 1)*(p/3)^2by POWER:46
        .= (p/3)*(p/3)^2 by POWER:30;
A14:  (q^2-4*(-p/3) |^ 3)>= 0 by A6,XREAL_1:65;
A15:  (-p/3) |^ 3 = (-p/3) |^ (2+1) .= ((-p/3) |^ (1+1))*(-p/3) by NEWTON:11
        .= ((((-p/3) |^ 1)*(-p/3)))*(-p/3) by NEWTON:11
        .= ((-p/3) |^ 1)*(-p/3)^2;
      then
A16:  (-p/3) |^ 3 = ((-p/3) to_power 1)*(-p/3)^2 by POWER:46
        .= (-p/3)*(p/3)^2 by POWER:30
        .= -((p/3)*(p/3)^2);
A17:  z1 = (-e2+sqrt(e2^2-4*e1*e3))/(2*e1) by A12,QUIN_1:def 1
        .= (-q)/2 +sqrt(q^2-4*(-p/3) |^ 3)/sqrt 4 by SQUARE_1:85,XCMPLX_1:63
        .= (-q)/2 +sqrt((q^2-4*(-p/3) |^ 3)/4) by A14,SQUARE_1:99
        .= (-q)/2 +sqrt(q^2/4-1*(-p/3) |^ 3);
A18:  now
        per cases by XXREAL_0:1;
        case
A19:      u >0;
          then
A20:      (-q/2 +sqrt(q^2/4+(p/3) |^ 3))> 0 by A17,A16,A13,PREPOWER:
          13;
          then u = 3 -Root (-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A17,A16,A13,A19,
          PREPOWER:def 3;
          hence u = 3-root(-q/2 +sqrt(q^2 /4+(p/3) |^ 3)) by A20, POWER:def
          1;
        end;
        case
A21:      u =0;
          then
A22:      -q/2 +sqrt(q^2/4+(p/3) |^ 3) = 0 by A17,A16,A13,
          NEWTON:16;
          then 3 -Root (-q/2 +sqrt(q^2/4+(p/3) |^ 3)) = 0 by PREPOWER:def
          3;
          hence u = 3-root(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A21,A22,POWER:
          def 1;
        end;
        case
          u <0;
          then
A23:      -u > 0 by XREAL_1:60;
          set r = (-q/2 +sqrt(q^2/4+(p/3) |^ 3));
A25:      (3-root (-1)) = -1 by Lm1,POWER:9;
          (-u) |^ 3 = (-u) |^ (2+1) .= ((-u) |^ (1+1))*(-u) by NEWTON:11
            .= ((((-u) |^ 1)*(-u)))*(-u) by NEWTON:11
            .= ((-u) |^ 1)*(-u)^2;
          then (-u) |^ 3 = ((-u) to_power 1)*(-u)^2by POWER:46;
          then
A26:      (-u) |^ 3 = (-u)*u^2 by POWER:30
            .= -((u)*u^2);
          u |^ 3 = u |^ (2+1) .= (u |^ (1+1))*u by NEWTON:11
            .= ((u |^ 1)*u)*u by NEWTON:11
            .= (u |^ 1)*(u*u);
          then
A27:      u |^ 3 = (u to_power 1)*u^2 by POWER:46;
          then
A28:      (-u) |^ 3 = -(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A17,A16,A13,A26,
          POWER:30;
A29:      (-u) |^ 3 = -(u |^ 3) by A26,A27,POWER:30;
          then
A30:      -(-q/2 +sqrt(q^2/4+(p/3) |^ 3))> 0 by A17,A16,A13,A23,
          PREPOWER:13;
          -(u |^ 3)> 0 by A23,A29,PREPOWER:13;
          then
          (-u) = 3 -Root(-(-q/2 +sqrt(q^2/4+(p/3) |^ 3))) by A17,A16,A13,
          A23,A28,PREPOWER:def 3;
          then (-u) = 3-root((-1)*(r)) by A30,POWER:def 1;
          then (-u) = (3-root (-1))*(3-root r) by Lm1,POWER:12;
          hence u = 3-root(-q/2 +sqrt ( q^2/4+(p/3) |^ 3)) by A25;
        end;
      end;
      now
        per cases by A8,A10,Th5;
        case
          z2 = (-e2+sqrt delta(e1,e2,e3))/(2*e1);
          then z2 = (-e2+sqrt(e2^2-4*e1*e3))/(2*e1) by QUIN_1:def 1;
          then z2 = (-q)/2 +sqrt(q^2-4*(-p/3) |^ 3)/sqrt 4 by
          SQUARE_1:85,
          XCMPLX_1:63;
          then
A31:      z2 = (-q)/2 +sqrt((q^2-4*(-p/3) |^ 3)/4) by A7, SQUARE_1:
          99
            .= (-q)/2 +sqrt(q^2/4-1*(-p/3) |^ 3);
A32:      (-p/3) |^ 3 = ((-p/3) to_power 1)*(-p/3)^2by A15,POWER:46
            .= (-p/3)*(p/3)^2 by POWER:30;
          now
            per cases by XXREAL_0:1;
            case
A33:          v >0;
              then
A34:          (-q/2 +sqrt(q^2/4+(p/3) |^ 3))> 0 by A13,A31,A32,
              PREPOWER:13;
              then v = 3 -Root (-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A13,A31,A32,
              A33,PREPOWER:def 3;
              hence v = 3-root(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A34,POWER:
              def 1;
            end;
            case
A35:          v=0;
              then (-q/2 +sqrt(q^2/4+(p/3) |^ 3)) = 0 by A13,A31,A32,
              NEWTON:16;
              hence v = 3-root(-q/2 +sqrt( q^2/4+(p/3) |^ 3)) by A35, POWER
              :6;
            end;
            case
              v<0;
              then
A36:          -v > 0 by XREAL_1:60;
              then
A37:          (-v) |^ 3 > 0 by PREPOWER:13;
              (-v) |^ 3 = (-v) |^ (2+1);
              then (-v) |^ 3 = ((-v) |^ (1+1))*(-v) by NEWTON:11;
              then (-v) |^ 3 = ((((-v) |^ 1)*(-v)))*(-v) by NEWTON:11;
              then (-v) |^ 3 = ((-v) |^ 1)*((-v)*(-v));
              then (-v) |^ 3 = ((-v) to_power 1)*(-v)^2by POWER:46;
              then (-v) |^ 3 = (-v)*(-v)^2 by POWER:30;
              then
A38:          (-v) |^ 3 = -(v*v^2);
              v |^ 3 = v |^ (2+1);
              then v |^ 3 = (v |^ (1+1))*v by NEWTON:11;
              then v |^ 3 = ((v |^ 1)*v)*v by NEWTON:11;
              then v |^ 3 = (v |^ 1)*(v*v);
              then
A39:          v |^ 3 = (v to_power 1)*v^2 by POWER:46;
              then
              (-v) |^ 3 = -(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A13,A31,A32,
              A38,POWER:30;
              then
A40:          (-v) = 3 -Root(-(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) ) by A36,A37
              ,PREPOWER:def 3;
              set r = (-q/2 +sqrt(q^2/4+(p/3) |^ 3));
A42:          (3-root (-1)) = -1 by Lm1,POWER:9;
              -(-q/2 +sqrt(q^2/4+(p/3) |^ 3))> 0 by A13,A31,A32,A37,A38,
              A39,POWER:30;
              then (-v) = 3-root((-1)*(r)) by A40,POWER:def 1;
              then (-v) = (3-root(-1))*(3-root r) by Lm1,POWER:12;
              hence v = 3-root(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A42;
            end;
          end;
          hence thesis by A2,A18;
        end;
        case
          z2 = (-e2-sqrt delta(e1,e2,e3))/(2*e1);
          then z2 = (-e2-sqrt(e2^2-4*e1*e3))/(2*e1) by QUIN_1:def 1;
          then z2 = (-q)/2 -(sqrt(q^2-4*(-p/3) |^ 3))/2;
          then
A43:      z2 = -q/2 -sqrt((q^2-4*(-p/3) |^ 3)/4) by A7,
          SQUARE_1:85,
          99
            .= -q/2 -sqrt(q^2/4-1*(-p/3) |^ 3);
          now
            per cases by XXREAL_0:1;
            case
A44:          v>0;
              then (-q/2 -sqrt(q^2/4+(p/3) |^ 3))> 0 by A16,A13,A43,
              PREPOWER:13;
              then
A45:          v = 3 -Root (-q/2 -sqrt(q^2/4+(p/3) |^ 3)) by A16,A13,A43,
              A44,PREPOWER:def 3;
              (-q/2 -sqrt(q^2/4+(p/3) |^ 3))> 0 by A16,A13,A43,A44,
              PREPOWER:13;
              hence v = 3-root (-q/2 -sqrt(q^2/4+(p/3) |^ 3)) by A45,POWER:
              def 1;
            end;
            case
A46:          v=0;
              then
A47:          (-q/2 -sqrt(q^2/4+(p/3) |^ 3)) = 0 by A16,A13,A43,
              NEWTON:16;
              hence v = 3 -Root (-q/2 -sqrt (q^2/4+(p/3) |^ 3)) by A46,
              PREPOWER:def 3;
              hence v = 3-root (-q/2 -sqrt(q ^2 /4+(p/3) |^ 3))by A47,
              POWER:def 1;
            end;
            case
              v<0;
              then
A48:          -v > 0 by XREAL_1:60;
              set r = (-q/2 -sqrt(q^2/4+(p/3) |^ 3));
A50:          (3-root (-1)) = -1 by Lm1,POWER:9;
              v |^ 3 = v |^ (2+1);
              then v |^ 3 = (v |^ (1+1))*v by NEWTON:11;
              then
A51:          v |^ 3 = ((v |^ 1)*v)*v by NEWTON:11;
              (-v) |^ 3 = (-v) |^ (2+1);
              then (-v) |^ 3 = ((-v) |^ (1+1))*(-v) by NEWTON:11;
              then (-v) |^ 3 = ((((-v) |^ 1)*(-v)))*(-v) by NEWTON:11;
              then (-v) |^ 3 = ((-v) |^ 1)*((-v)*(-v));
              then
A52:          (-v) |^ 3 = ((-v) to_power 1)*(-v)^2 by POWER:46
                .= (-v)*v^2 by POWER:30
                .= -(v*v^2);
A53:          v to_power 1 = v by POWER:30;
              then (-v) |^ 3 = -(v |^ 3) by A52,A51,POWER:46;
              then
A54:          -(-q/2 -sqrt(q^2/4+(p/3) |^ 3))> 0 by A16,A13,A43,A48,
              PREPOWER:13;
              (-v) |^ 3 = -(-q/2 -sqrt(q^2/4+(p/3) |^ 3)) by A16,A13,A43
              ,A52,A51,A53,POWER:46;
              then (-v) = 3 -Root(-(-q/2 -sqrt(q^2/4+(p/3) |^ 3)) ) by A48,
              A54,PREPOWER:def 3;
              then (-v) = 3-root((-1)*(r)) by A54,POWER:def 1;
              then (-v) = (3-root (-1))*(3-root r) by Lm1,POWER:12;
              hence v = 3-root(-q/2 -sqrt(q^2/4+(p/3) |^ 3)) by A50;
            end;
          end;
          hence thesis by A2,A18;
        end;
      end;
      hence thesis;
    end;
    case
A55:  z1 = (-e2-sqrt delta(e1,e2,e3))/(2*e1);
      (-p/3) |^ 3 = (-p/3) |^ (2+1);
      then (-p/3) |^ 3 = ((-p/3) |^ (1+1))*(-p/3) by NEWTON:11;
      then (-p/3) |^ 3 = ((((-p/3) |^ 1)*(-p/3)))*(-p/3) by NEWTON:11;
      then (-p/3) |^ 3 = ((-p/3) |^ 1)*((-p/3)*(-p/3));
      then (-p/3) |^ 3 = ((-p/3) to_power 1)*(-p/3)^2 by POWER:46;
      then (-p/3) |^ 3 = (-p/3)*(-p/3)^2 by POWER:30;
      then
A56:  (-p/3) |^ 3 = -((p/3)*(p/3)^2);
      z1 = (-e2-sqrt(e2^2-4*e1*e3))/(2*e1) by A55,QUIN_1:def 1;
      then
A57:  z1= ((-q)*2") -(sqrt(q^2-4*(-p/3) |^ 3))/2
        .= -q/2 -sqrt((q^2-4*(-p/3) |^ 3)/4) by A7,SQUARE_1:85,99
        .= -q/2 -sqrt(q^2/4-1*(-p/3) |^ 3);
      hence z1 = -q/2 -sqrt(q^2/4-(-p/3) |^ 3);
      (p/3) |^ 3 = (p/3) |^ (2+1);
      then (p/3) |^ 3 = ((p/3) |^ (1+1))*(p/3) by NEWTON:11;
      then (p/3) |^ 3 = ((((p/3) |^ 1)*(p/3)))*(p/3) by NEWTON:11;
      then (p/3) |^ 3 = ((p/3) |^ 1)*((p/3)*(p/3));
      then (p/3) |^ 3 = ((p/3) to_power 1)*(p/3)^2by POWER:46;
      then
A58:  (-p/3) |^ 3 = -((p/3) |^ 3) by A56,POWER:30;
A59:  now
        per cases by XXREAL_0:1;
        case
A60:      u >0;
          then (-q/2 -sqrt(q^2/4+(p/3) |^ 3))> 0 by A57,A58,PREPOWER:
          13;
          then
A61:      u = 3 -Root (-q/2 -sqrt(q^2/4+(p/3) |^ 3)) by A57,A58,A60,
          PREPOWER:def 3;
          (-q/2 -sqrt(q^2 /4+(p/3) |^ 3))> 0 by A57,A58,A60, PREPOWER
          :13;
          hence u = 3-root (-q/2 - sqrt(q^2/4+(p/3) |^ 3)) by A61, POWER:
          def 1;
        end;
        case
A62:      u =0;
          then (-q/2 -sqrt(q^2/4+(p/3) |^ 3)) = 0 by A57,A58,NEWTON:16;
          hence u = 3-root(-q/2 -sqrt(q^2/4+(p/3) |^ 3)) by A62,POWER:6;
        end;
        case
          u <0;
          then
A63:      -u > 0 by XREAL_1:60;
          then
A64:      (-u) |^ 3 > 0 by PREPOWER:13;
          (-u) |^ 3 = (-u) |^ (2+1);
          then (-u) |^ 3 = ((-u) |^ (1+1))*(-u) by NEWTON:11;
          then (-u) |^ 3 = ((((-u) |^ 1)*(-u)))*(-u) by NEWTON:11;
          then (-u) |^ 3 = ((-u) |^ 1)*((-u)*(-u));
          then (-u) |^ 3 = ((-u) to_power 1)*(-u)^2 by POWER:46;
          then (-u) |^ 3 = (-u)*(-u)^2 by POWER:30;
          then
A65:      (-u) |^ 3 = -(u*u^2);
          u |^ 3 = u |^ (2+1);
          then u |^ 3 = (u |^ (1+1))*u by NEWTON:11;
          then u |^ 3 = ((u |^ 1)*u)*u by NEWTON:11;
          then u |^ 3 = (u |^ 1)*(u*u);
          then
A66:      u |^ 3 = (u to_power 1)*u^2 by POWER:46;
          then -(-q/2 -sqrt(q^2/4+(p/3) |^ 3))> 0 by A57,A58,A64,A65,
          POWER:30;
          then
A67:      3 -Root(-(-q/2 -sqrt(q^2/4+(p/3) |^ 3))) = 3-root(-(-q/2 -sqrt
          (q^2/4+(p/3) |^ 3))) by POWER:def 1;
          set r = (-q/2 -sqrt(q^2/4+(p/3) |^ 3));
          (-u) |^ 3 = -(-q/2 -sqrt(q^2/4+(p/3) |^ 3)) by A57,A58,A65,
          A66,POWER:30;
          then (-u) = 3-root((-1)*(r)) by A63,A64,A67,PREPOWER:def 3;
          then
A69:      (-u) = (3-root (-1))*(3-root r) by Lm1,POWER:12;
          (3-root (-1)) = -1 by Lm1,POWER:9;
          hence u = 3-root(-q / 2 -sqrt(q^2/4+(p/3) |^ 3)) by A69;
        end;
      end;
      now
        per cases by A8,A10,Th5;
        case
A70:      z2 = (-e2+sqrt delta(e1,e2,e3))/(2*e1);
          (-p/3) |^ 3 = (-p/3) |^ (2+1);
          then (-p/3) |^ 3 = ((-p/3) |^ (1+1))*(-p/3) by NEWTON:11;
          then (-p/3) |^ 3 = ((((-p/3) |^ 1)*(-p/3)))*(-p/3) by NEWTON:
          11;
          then (-p/3) |^ 3 = ((-p/3) |^ 1)*((-p/3)*(-p/3));
          then (-p/3) |^ 3 = ((-p/3) to_power 1)*(-p/3)^2by POWER:46;
          then (-p/3) |^ 3 = (-p/3)*(-p/3)^2 by POWER:30;
          then
A71:      (-p/3) |^ 3 = -((p/3)*(p/3)^2);
          (p/3) |^ 3 = (p/3) |^ (2+1);
          then (p/3) |^ 3 = ((p/3) |^ (1+1))*(p/3) by NEWTON:11;
          then (p/3) |^ 3 = ((((p/3) |^ 1)*(p/3)))*(p/3) by NEWTON:11;
          then (p/3) |^ 3 = ((p/3) |^ 1)*((p/3)*(p/3));
          then (p/3) |^ 3 = ((p/3) to_power 1)*(p/3)^2by POWER:46;
          then
A72:      (-p/3) |^ 3 = -((p/3) |^ 3) by A71,POWER:30;
          z2 = (-e2+sqrt(e2^2-4*e1*e3))/(2*e1) by A70,QUIN_1:def 1;
          then z2 = (-q)/2 +sqrt(q^2-4*(-p/3) |^ 3)/sqrt 4 by SQUARE_1:
          85,XCMPLX_1:63;
          then
A73:      z2 = (-q)/2 +sqrt((q^2-4*(-p/3) |^ 3)/4) by A7, SQUARE_1
          :99
            .= (-q)/2 +sqrt(q^2/4-1*(-p/3) |^ 3);
          now
            per cases by XXREAL_0:1;
            case
A74:          v >0;
              then
A75:          (-q/2 +sqrt(q^2/4+(p/3) |^ 3))> 0 by A73,A72,
              PREPOWER:13;
              then v = 3 -Root (-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A73,A72,
              A74,PREPOWER:def 3;
              hence v = 3-root(-q/2 + sqrt(q^2/4+(p/3) |^ 3)) by A75,
              POWER:def 1;
            end;
            case
A76:          v=0;
              then (-q/2 +sqrt(q^2/4+(p/3) |^ 3)) = 0 by A73,A72,
              NEWTON:16;
              hence v = 3-root(-q/2 +sqrt(q^2/ 4+(p/3) |^ 3)) by A76,
              POWER:6;
            end;
            case
              v<0;
              then
A77:          -v > 0 by XREAL_1:60;
              then
A78:          (-v) |^ 3 > 0 by PREPOWER:13;
              (-v) |^ 3 = (-v) |^ (2+1);
              then (-v) |^ 3 = ((-v) |^ (1+1))*(-v) by NEWTON:11;
              then (-v) |^ 3 = ((((-v) |^ 1)*(-v)))*(-v) by NEWTON:11;
              then (-v) |^ 3 = ((-v) |^ 1)*((-v)*(-v));
              then (-v) |^ 3 = ((-v) to_power 1)*(-v)^2by POWER:46;
              then (-v) |^ 3 = (-v)*(-v)^2 by POWER:30;
              then
A79:          (-v) |^ 3 = -(v*v^2);
              v |^ 3 = v |^ (2+1);
              then v |^ 3 = (v |^ (1+1))*v by NEWTON:11;
              then v |^ 3 = ((v |^ 1)*v)*v by NEWTON:11;
              then v |^ 3 = (v |^ 1)*(v*v);
              then
A80:          v |^ 3 = (v to_power 1)*v^2 by POWER:46;
              then
A81:          -(-q/2 +sqrt(q^2/4+(p/3) |^ 3))> 0 by A73,A72,A78,A79,
              POWER:30;
              set r = (-q/2 +sqrt(q^2/4+(p/3) |^ 3));
A83:          (3-root (-1)) = -1 by Lm1,POWER:9;
              v |^ 3 = v*v^2 by A80,POWER:30;
              then (-v) = 3 -Root(-(-q/2 +sqrt(q^2/4+(p/3) |^ 3))) by A73,
              A72,A77,A78,A79,PREPOWER:def 3;
              then (-v) = 3-root((-1)*(r)) by A81,POWER:def 1;
              then (-v) = (3-root(-1))*(3-root r) by Lm1,POWER:12;
              hence v = 3-root(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A83;
            end;
          end;
          hence thesis by A2,A59;
        end;
        case
A84:      z2 = (-e2-sqrt delta(e1,e2,e3))/(2*e1);
          e2^2 = (z1+z2)^2 by A5,SQUARE_1:61;
          then
A85:      e2^2-(4*e1*e3) = (z1-z2)^2 by A11;
          (-p/3) |^ 3 = (-p/3) |^ (2+1)
            .= ((-p/3) |^ (1+1))*(-p/3) by NEWTON:11
            .= ((((-p/3) |^ 1)*(-p/3)))*(-p/3) by NEWTON:11
            .= ((-p/3) |^ 1)*((-p/3)*(-p/3));
          then (-p/3) |^ 3 = ((-p/3) to_power 1)*(-p/3)^2by POWER:46
            .= (-p/3)*(p/3)^2 by POWER:30;
          then
A86:      (-p/3) |^ 3 = -((p/3)*(p/3)^2);
          (p/3) |^ 3 = (p/3) |^ (2+1) .= ((p/3) |^ (1+1))*(p/3) by NEWTON:11
            .= ((((p/3) |^ 1)*(p/3)))*(p/3) by NEWTON:11
            .= ((p/3) |^ 1)*(p/3)^2;
          then (p/3) |^ 3 = ((p/3) to_power 1)*(p/3)^2by POWER:46;
          then
A87:      (-p/3) |^ 3 = -((p/3) |^ 3) by A86,POWER:30;
          z2 = (-e2-sqrt(e2^2-4*e1*e3))/(2*e1) by A84,QUIN_1:def 1;
          then
A88:      z2 = (-q)/2 +sqrt((q^2-4*(-p/3) |^ 3)/4) by A55,A84,
          A85,SQUARE_1:82
            .= (-q)/2 +sqrt(q^2/4-1*(-p/3) |^ 3);
          now
            per cases by XXREAL_0:1;
            case
A89:          v >0;
              then
A90:          (-q/2 +sqrt(q^2/4+(p/3) |^ 3))> 0 by A88,A87,
              PREPOWER:13;
              then v = 3 -Root (-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A88,A87,
              A89,PREPOWER:def 3;
              hence v = 3-root(-q/2 +sqrt (q^2/4+(p/3) |^ 3)) by A90,
              POWER:def 1;
            end;
            case
A91:          v=0;
              then v |^ 3 = 0 by NEWTON:16;
              hence
              v = 3-root(-q/2 +sqrt(q ^2/4+(p/3) |^ 3)) by A88,A87,A91
              ,POWER:6;
            end;
            case
              v<0;
              then
A92:          -v > 0 by XREAL_1:60;
              then
A93:          (-v) |^ 3 > 0 by PREPOWER:13;
              set r = (-q/2 +sqrt(q^2/4+(p/3) |^ 3));
              v |^ 3 = v |^ (2+1) .= (v |^ (1+1))*v by NEWTON:11
                .= ((v |^ 1)*v)*v by NEWTON:11
                .= (v |^ 1)*v^2;
              then
A94:          v |^ 3 = (v to_power 1)*v^2 by POWER:46;
              (-v) |^ 3 = (-v) |^ (2+1) .= ((-v) |^ (1+1))*(-v) by NEWTON:11
                .= ((((-v) |^ 1)*(-v)))*(-v) by NEWTON:11
                .= ((-v) |^ 1)*(-v)^2
                .= ((-v) to_power 1)*(-v)^2by POWER:46
                .= (-v)*v^2 by POWER:30;
              then
A95:          (-v) |^ 3 = -(v*v^2);
              then
A96:          -(v |^ 3)> 0 by A93,A94,POWER:30;
A98:          (3-root (-1)) = -1 by Lm1,POWER:9;
              (-v) |^ 3 = -(v |^ 3) by A95,A94,POWER:30;
              then (-v) = 3 -Root(-(-q/2 +sqrt(q^2/4+(p/3) |^ 3))) by A88,
              A87,A92,A93,PREPOWER:def 3;
              then (-v) = 3-root((-1)*(r)) by A88,A87,A96,POWER:def 1;
              then (-v) = (3-root(-1))*(3-root r) by Lm1,POWER:12;
              hence v = 3-root(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) by A98;
            end;
          end;
          hence
          y = 3-root(-q/2 +sqrt(q^2/4+(p/3) |^ 3)) +3-root(-q/2 -sqrt(q^2
          /4+(p/3) |^ 3)) by A2,A59;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem
  b <> 0 & delta(b,c,d) > 0 & Polynom(0,b,c,d,x) = 0 implies x = (-c+
  sqrt delta(b,c,d))/(2*b) or x = (-c-sqrt delta(b,c,d))/(2*b)
proof
  assume
A1: b <> 0 & delta(b,c,d)>0;
  assume Polynom(0,b,c,d,x) = 0;
  then Polynom(b,c,d,x) = 0;
  hence thesis by A1,Th5;
end;

theorem
  a <> 0 & p = c/a & q = d/a & Polynom(a,0,c,d,x) = 0 implies for u,v st
x = u+v & 3*v*u+p = 0 holds x = 3-root(-d/(2*a)+sqrt((d^2/(4*a^2))+(c/(3*a)) |^
3)) +3-root(-d/(2*a)-sqrt((d^2/(4*a^2))+(c/(3*a)) |^ 3)) or x = 3-root(-d/(2*a)
+sqrt((d^2/(4*a^2))+(c/(3*a)) |^ 3)) +3-root(-d/(2*a)+sqrt((d^2/(4*a^2))+(c/(3*
a)) |^ 3)) or x = 3-root(-d/(2*a)-sqrt((d^2/(4*a^2))+(c/(3*a)) |^ 3)) +3-root(-
  d/(2*a)-sqrt((d^2/(4*a^2))+(c/(3*a)) |^ 3))
proof
  assume that
A1: a <> 0 and
A2: p = c/a and
A3: q = d/a;
A4: p/3 = c/(3*a) & -q/2 = -d/(2*a) by A2,A3,XCMPLX_1:79;
  assume Polynom(a,0,c,d,x) = 0;
  then a"*(a*(x |^ 3)+c*x +d) = 0;
  then (a"*a)*(x |^ 3)+a"*(c*x +d)= 0;
  then 1*(x |^ 3)+a"*(c*x +d)= 0 by A1,XCMPLX_0:def 7;
  then (x |^ 3)+((a"*c)*x +a"*d) = 0;
  then (x |^ 3)+((c/a)*x +a"*d) = 0 by XCMPLX_0:def 9;
  then (x |^ 3)+((c/a)*x +d/a) = 0 by XCMPLX_0:def 9;
  then
A5: Polynom(1,0,p,q,x) = 0 by A2,A3;
  q^2/4 = d^2/a^2/4 by A3,XCMPLX_1:77;
  then
A6: q^2/4 = d^2/(4*a^2) by XCMPLX_1:79;
  let u,v;
  assume x = u+v & 3*v*u+p = 0;
  hence thesis by A5,A4,A6,Th19;
end;

theorem
  a <> 0 & delta(a,b,c) >= 0 & Polynom(a,b,c,0,x) = 0 implies x = 0 or x
  = (-b+sqrt delta(a,b,c))/(2*a) or x = (-b-sqrt delta(a,b,c))/(2*a)
proof
  assume
A1: a <> 0 & delta(a,b,c)>= 0;
  x |^ 3 = x |^ (2+1);
  then x |^ 3 = (x |^ (1+1))*x by NEWTON:11;
  then
A2: x |^ 3 = ((x |^ 1)*x)*x by NEWTON:11;
  x |^ 1 = x to_power 1 by POWER:46;
  then
A3: x |^ 3 = x^2*x by A2,POWER:30;
  assume Polynom(a,b,c,0,x) = 0;
  then (a*x^2 +b*x+c)*x = 0 by A3;
  then x = 0 or Polynom(a,b,c,x) = 0 by XCMPLX_1:6;
  hence thesis by A1,Th5;
end;

theorem
  a <> 0 & c/a < 0 & Polynom(a,0,c,0,x) = 0 implies x = 0 or x = sqrt -c
  /a or x = -sqrt(-c/a)
proof
  assume that
A1: a <> 0 and
A2: c/a < 0;
  x |^ 3 = x |^ (2+1);
  then x |^ 3 = (x |^ (1+1))*x by NEWTON:11;
  then
A3: x |^ 3 = ((x |^ 1)*x)*x by NEWTON:11;
  x |^ 1 = x to_power 1 by POWER:46;
  then
A4: x |^ 3 = x^2*x by A3,POWER:30;
  assume Polynom(a,0,c,0,x) = 0;
  then (a*x^2+c)*x = 0 by A4;
  then
A5: x = 0 or (a*x^2+c) = 0 by XCMPLX_1:6;
  now
    per cases by XXREAL_0:1;
    case
A6:   x > 0;
      x |^ 2 = (x |^ (1+1));
      then x |^ 2 = ((x |^ 1)*x) by NEWTON:11;
      then x |^ 2 = ((x to_power 1)*x) by POWER:46;
      then
A7:   x |^ 2 = x*x by POWER:30;
A8:   (-c/a) > 0 by A2,XREAL_1:60;
      x^2 = (-c)/a by A1,A5,A6,XCMPLX_1:90;
      then x^2 = ((-c)*a") by XCMPLX_0:def 9;
      then x^2 = -(c*a");
      then x^2 = -c/a by XCMPLX_0:def 9;
      then x = 2 -Root (-c/a) by A6,A8,A7,PREPOWER:def 3;
      hence thesis by A8,PREPOWER:41;
    end;
    case
A9:   x < 0;
      (-x) |^ 2 = ((-x) |^ (1+1));
      then (-x) |^ 2 = (((-x) |^ 1)*(-x)) by NEWTON:11;
      then (-x) |^ 2 = (((-x) to_power 1)*(-x)) by POWER:46;
      then
A10:  (-x) |^ 2 = (-x)*(-x) by POWER:30;
      x^2 = (-c)/a by A1,A5,A9,XCMPLX_1:90;
      then x^2 = ((-c)*a") by XCMPLX_0:def 9;
      then x^2 = -(c*a");
      then
A11:  (-x)^2=-c/a by XCMPLX_0:def 9;
A12:  (-c/a) > 0 by A2,XREAL_1:60;
      -x>0 by A9,XREAL_1:60;
      then -x = (2 -Root (-c/a)) by A11,A12,A10,PREPOWER:def 3;
      then -x = sqrt(-c/a) by A12,PREPOWER:41;
      hence thesis;
    end;
    case
      x=0;
      hence thesis;
    end;
  end;
  hence thesis;
end;
