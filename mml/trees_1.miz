:: Introduction to Trees
::  by Grzegorz Bancerek
::
:: Received October 25, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies FUNCT_1, FINSEQ_1, RELAT_1, BOOLE, ARYTM_1, FINSET_1, CARD_1,
      ZFMISC_1, TARSKI, ORDINAL2, TREES_1, ARYTM;
 notations TARSKI, XBOOLE_0, ENUMSET1, SUBSET_1, CARD_1, XCMPLX_0, NAT_1,
      NUMBERS, ORDINAL1, RELAT_1, FUNCT_1, FINSEQ_1, FINSET_1, XXREAL_0;
 constructors ENUMSET1, WELLORD2, XXREAL_0, XREAL_0, NAT_1, FINSEQ_1;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, ORDINAL1, FINSET_1, XXREAL_0,
      XREAL_0, NAT_1, FINSEQ_1, CARD_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FINSET_1, FUNCT_1, WELLORD2, XBOOLE_0;
 theorems TARSKI, NAT_1, FINSEQ_1, FINSET_1, FUNCT_1, CARD_1, RELAT_1,
      GRFUNC_1, CARD_2, XBOOLE_0, XBOOLE_1, XREAL_1, XXREAL_0, ORDINAL1,
      ENUMSET1, FINSEQ_2;
 schemes NAT_1, CLASSES1, XBOOLE_0;

begin

reserve D for non empty set,
  X,x,y,z for set,
  k,n,m for Element of NAT,
  f for Function,
  p,q,r for FinSequence of NAT;

::
:: Relations "is a prefix of", "is a proper prefix of" and
:: "are comparable" of finite sequences
::

notation
  let p,q be FinSequence;
  synonym p is_a_prefix_of q for p c= q;
end;

definition
  let p,q be FinSequence;
  redefine pred p is_a_prefix_of q means
  :Def1:
  ex n st p = q|Seg n;
  compatibility
  proof
    thus p c= q implies ex n st p = q|Seg n
    proof
      assume
A1:   p c= q;
      consider n be Nat such that
A2:   dom p = Seg n by FINSEQ_1:def 2;
      reconsider n as Element of NAT by ORDINAL1:def 13;
      take n;
      thus thesis by A1,A2,GRFUNC_1:64;
    end;
    thus thesis by RELAT_1:88;
  end;
end;

canceled 7;

theorem Th8:
  for p,q being FinSequence holds p is_a_prefix_of q iff
  ex r being FinSequence st q = p^r
proof
  let p,q be FinSequence;
  thus p is_a_prefix_of q implies ex r being FinSequence st q = p^r
  proof
    given n such that
A1: p = q|Seg n;
    thus thesis by A1,FINSEQ_1:101;
  end;
  given r being FinSequence such that
A2: q = p^r;
A3: dom p = Seg len p & p = q|dom p by A2,FINSEQ_1:33,def 3;
  thus thesis by A3,Def1;
end;

canceled 6;

theorem Th15:
  for p,q being FinSequence st p is_a_prefix_of q & len p = len q holds p = q
proof
  let p,q be FinSequence;
  assume that
A1: p is_a_prefix_of q and
A2: len p = len q;
  consider r being FinSequence such that
A3: q = p^r by A1,Th8;
A4: len p = len p + len r by A2,A3,FINSEQ_1:35;
A5: r = {} by A4;
  thus thesis by A3,A5,FINSEQ_1:47;
end;

theorem Th16:
  <*x*> is_a_prefix_of <*y*> iff x = y
proof
  thus <*x*> is_a_prefix_of <*y*> implies x = y
  proof
    assume
A1: <*x*> is_a_prefix_of <*y*>;
A2: len <*x*> = 1 & len <*y*> = 1 by FINSEQ_1:57;
A3: <*x*> = <*y*> by A1,A2,Th15;
A4: <*x*>.1 = x by FINSEQ_1:57;
    thus thesis by A3,A4,FINSEQ_1:57;
  end;
  thus thesis;
end;

notation
  let p,q be FinSequence;
  synonym p is_a_proper_prefix_of q for p c< q;
end;

Lm1: for A,B being finite set st A c= B & card A = card B holds A = B
proof
  let A,B be finite set such that
A1: A c= B and
A2: card A = card B;
A3: card(B \ A) = card B - card A by A1,CARD_2:63
    .= 0 by A2;
A4: B \ A = {} by A3;
A5: B c= A by A4,XBOOLE_1:37;
  thus thesis by A1,A5,XBOOLE_0:def 10;
end;

canceled 2;

theorem Th19:
  for p,q being finite set st p,q are_c=-comparable & card p = card q holds
  p = q
proof
  let p,q be finite set such that
A1: p c= q or q c= p and
A2: card p = card q;
  per cases by A1;
  suppose
A3: p c= q;
    thus thesis by A2,A3,Lm1;
  end;
  suppose
A4: q c= p;
    thus thesis by A2,A4,Lm1;
  end;
end;

reserve p1,p2,p3 for FinSequence;

canceled 3;

theorem Th23:
  <*x*>,<*y*> are_c=-comparable iff x = y
proof
  thus <*x*>,<*y*> are_c=-comparable implies x = y
  proof
    assume
A1: <*x*>,<*y*> are_c=-comparable;
A2: len <*x*> = 1 & len <*y*> = 1 by FINSEQ_1:57;
A3: <*x*> = <*y*> by A1,A2,Th19;
A4: <*x*>.1 = x by FINSEQ_1:57;
    thus thesis by A3,A4,FINSEQ_1:57;
  end;
  thus thesis;
end;

theorem Th24:
  for p,q being finite set st p c< q holds card p < card q
proof
  let p,q be finite set;
  assume that
A1: p c= q and
A2: p <> q;
A3: card p <= card q by A1,NAT_1:44;
A4: now
    assume
A5: card p = card q;
A6: p,q are_c=-comparable by A1,XBOOLE_0:def 9;
    thus contradiction by A2,A5,A6,Th19;
  end;
  thus thesis by A3,A4,XXREAL_0:1;
end;

canceled 5;

theorem Th30:
  p1^<*x*> is_a_prefix_of p2 implies p1 is_a_proper_prefix_of p2
proof
  assume
A1: p1^<*x*> is_a_prefix_of p2;
  consider p3 such that
A2: p2 = p1^<*x*>^p3 by A1,Th8;
A3: p2 = p1^(<*x*>^p3) by A2,FINSEQ_1:45;
  thus p1 is_a_prefix_of p2 by A3,Th8;
  assume
A4: p1 = p2;
A5: len p1 = len(p1^<*x*>) + len p3 by A2,A4,FINSEQ_1:35
    .= len p1 + len <*x*> + len p3 by FINSEQ_1:35
    .= len p1 + 1 + len p3 by FINSEQ_1:56;
A6: len p1 + 1 <= len p1 by A5,NAT_1:11;
  thus contradiction by A6,NAT_1:13;
end;

theorem Th31:
  p1 is_a_prefix_of p2 implies p1 is_a_proper_prefix_of p2^<*x*>
proof
  assume
A1: p1 is_a_prefix_of p2;
  consider p3 such that
A2: p2 = p1^p3 by A1,Th8;
A3: p2^<*x*> = p1^(p3^<*x*>) by A2,FINSEQ_1:45;
  thus p1 is_a_prefix_of p2^<*x*> by A3,Th8;
  assume
A4: p1 = p2^<*x*>;
A5: len p2 = len(p2^<*x*>) + len p3 by A2,A4,FINSEQ_1:35
    .= len p2 + len <*x*> + len p3 by FINSEQ_1:35
    .= len p2 + 1 + len p3 by FINSEQ_1:56;
A6: len p2 + 1 <= len p2 by A5,NAT_1:11;
  thus contradiction by A6,NAT_1:13;
end;

theorem Th32:
  p1 is_a_proper_prefix_of p2^<*x*> implies p1 is_a_prefix_of p2
proof
  assume that
A1: p1 is_a_prefix_of p2^<*x*> and
A2: p1 <> p2^<*x*>;
A3: ex p3 st p2^<*x*> = p1^p3 by A1,Th8;
A4: len p1 <= len(p2^<*x*>) by A1,NAT_1:44;
  A5: len(p2^<*x*>) = len p2 + len <*x*> & len <*x*> = 1 by FINSEQ_1:35,56;
A6: len p1 < len p2 + 1 by A1,A2,A4,A5,Th15,XXREAL_0:1;
A7: len p1 <= len p2 by A6,NAT_1:13;
A8: ex p3 st p1^p3 = p2 by A3,A7,FINSEQ_1:64;
  thus thesis by A8,Th8;
end;

theorem
  {} is_a_proper_prefix_of p2 or {} <> p2 implies
  p1 is_a_proper_prefix_of p1^p2
proof
  assume
A1: {} is_a_proper_prefix_of p2 or {} <> p2;
  thus p1 is_a_prefix_of p1^p2 by Th8;
  assume
A2: p1 = p1^p2;
A3: len p1 = len p1 + len p2 by A2,FINSEQ_1:35;
A4: p2 = {} by A3;
  thus contradiction by A1,A4;
end;

::
:: The set of proper prefixes of a finite sequence
::

definition
  let p be FinSequence;
  canceled 2;
  func ProperPrefixes p -> set means
  :Def4:
  x in
  it iff ex q being FinSequence st x = q & q is_a_proper_prefix_of p;
  existence
  proof
    consider D;
    reconsider E = D \/ rng p as non empty set;
    defpred X[set] means ex q being FinSequence st $1 = q &
    q is_a_proper_prefix_of p;
    consider X such that
A1: x in X iff x in E* & X[x] from XBOOLE_0:sch 1;
    take X;
    let x;
    thus x in X implies ex q being FinSequence st x = q &
    q is_a_proper_prefix_of p by A1;
    given q be FinSequence such that
A2: x = q and
A3: q is_a_proper_prefix_of p;
A4: q is_a_prefix_of p by A3,XBOOLE_0:def 8;
A5: ex n st q = p|Seg n by A4,Def1;
A6: rng q c= rng p by A5,RELAT_1:99;
A7: rng p c= E by XBOOLE_1:7;
A8: rng q c= E by A6,A7,XBOOLE_1:1;
    reconsider q as FinSequence of E by A8,FINSEQ_1:def 4;
A9: q in E* by FINSEQ_1:def 11;
    thus thesis by A1,A2,A3,A9;
  end;
  uniqueness
  proof
    let S1,S2 be set such that
A10: x in
    S1 iff ex q being FinSequence st x = q & q is_a_proper_prefix_of p and
A11: x in S2 iff ex q being FinSequence st x = q & q is_a_proper_prefix_of p;
    defpred X[set] means
    ex q being FinSequence st $1 = q & q is_a_proper_prefix_of p;
A12: x in S1 iff X[x] by A10;
A13: x in S2 iff X[x] by A11;
    thus thesis from XBOOLE_0:sch 2(A12,A13);
  end;
end;

canceled;

theorem Th35:
  for p being FinSequence st x in ProperPrefixes p holds x is FinSequence
proof
  let p be FinSequence;
  assume
A1: x in ProperPrefixes p;
A2: ex q being FinSequence st x = q & q is_a_proper_prefix_of p by A1,Def4;
  thus thesis by A2;
end;

theorem Th36:
  for p,q being FinSequence holds
  p in ProperPrefixes q iff p is_a_proper_prefix_of q
proof
  let p,q be FinSequence;
  thus p in ProperPrefixes q implies p is_a_proper_prefix_of q
  proof
    assume
A1: p in ProperPrefixes q;
A2: ex r being FinSequence st p = r & r is_a_proper_prefix_of q by A1,Def4;
    thus thesis by A2;
  end;
  thus thesis by Def4;
end;

theorem Th37:
  for p,q being FinSequence st p in ProperPrefixes q holds len p < len q
proof
  let p,q be FinSequence;
  assume
A1: p in ProperPrefixes q;
A2: p is_a_proper_prefix_of q by A1,Th36;
  thus thesis by A2,Th24;
end;

theorem
  for p,q,r being FinSequence st q^r in ProperPrefixes p holds
  q in ProperPrefixes p
proof
  let p,q,r be FinSequence;
  assume
A1: q^r in ProperPrefixes p;
A2: q is_a_prefix_of q^r by Th8;
A3: q^r is_a_proper_prefix_of p by A1,Th36;
A4: q is_a_proper_prefix_of p by A2,A3,XBOOLE_1:59;
  thus thesis by A4,Th36;
end;

theorem Th39:
  ProperPrefixes {} = {}
proof consider x being Element of ProperPrefixes {};
  assume
A1: not thesis;
  reconsider x as FinSequence by A1,Th35;
A2: x is_a_proper_prefix_of {} by A1,Th36;
  thus contradiction by A2,XBOOLE_1:115;
end;

theorem Th40:
  ProperPrefixes <*x*> = { {} }
proof
  thus ProperPrefixes <*x*> c= { {} }
  proof
    let y;
    assume
A1: y in ProperPrefixes <*x*>;
    consider p being FinSequence such that
A2: y = p and
A3: p is_a_proper_prefix_of <*x*> by A1,Def4;
A4: len p < len <*x*> by A3,Th24;
A5: len <*x*> = 1 & 1 = 0+1 by FINSEQ_1:56;
A6: p = {} by A4,A5,NAT_1:13;
    thus thesis by A2,A6,TARSKI:def 1;
  end;
  let y;
  assume
A7: y in { {} };
A8: y = {} by A7,TARSKI:def 1;
A9: {} is_a_prefix_of <*x*> by XBOOLE_1:2;
A10: {} is_a_proper_prefix_of <*x*> by A9,XBOOLE_0:def 8;
  thus thesis by A8,A10,Def4;
end;

theorem
  for p,q being FinSequence st p is_a_prefix_of q holds
  ProperPrefixes p c= ProperPrefixes q
proof
  let p,q be FinSequence such that
A1: p is_a_prefix_of q;
  let x;
  assume
A2: x in ProperPrefixes p;
  reconsider r = x as FinSequence by A2,Th35;
A3: r is_a_proper_prefix_of p by A2,Th36;
A4: r is_a_proper_prefix_of q by A1,A3,XBOOLE_1:58;
  thus thesis by A4,Th36;
end;

theorem Th42:
  for p,q,r being FinSequence st
  q in ProperPrefixes p & r in ProperPrefixes p holds q,r are_c=-comparable
proof
  let p,q,r be FinSequence;
  assume
A1: q in ProperPrefixes p;
A2: ex q1 being FinSequence st q = q1 & q1 is_a_proper_prefix_of p by A1,Def4;
  assume
A3: r in ProperPrefixes p;
A4: ex r1 being FinSequence st r = r1 & r1 is_a_proper_prefix_of p by A3,Def4;
A5: q is_a_prefix_of p by A2,XBOOLE_0:def 8;
  consider n such that
A6: q = p|Seg n by A5,Def1;
A7: r is_a_prefix_of p by A4,XBOOLE_0:def 8;
  consider k such that
A8: r = p|Seg k by A7,Def1;
A9: n <= k implies thesis
  proof
    assume
A10: n <= k;
A11: Seg n c= Seg k by A10,FINSEQ_1:7;
A12: q = r|Seg n by A6,A8,A11,FUNCT_1:82;
A13: q is_a_prefix_of r by A12,Def1;
    thus thesis by A13,XBOOLE_0:def 9;
  end;
A14: k <= n implies thesis
  proof
    assume
A15: k <= n;
A16: Seg k c= Seg n by A15,FINSEQ_1:7;
A17: r = q|Seg k by A6,A8,A16,FUNCT_1:82;
A18: r is_a_prefix_of q by A17,Def1;
    thus thesis by A18,XBOOLE_0:def 9;
  end;
  thus thesis by A9,A14;
end;

::
::  Trees and their properties
::

definition
  let X;
  attr X is Tree-like means
  :Def5:
  X c= NAT* & (for p st p in X holds ProperPrefixes p c= X) &
  for p,k,n st p^<*k*> in X & n <= k holds p^<*n*> in X;
end;

registration
  cluster non empty Tree-like set;
  existence
  proof
    take D = { <*>(NAT) };
    thus D is non empty;
    thus D c= NAT*
    proof
      let x;
      assume
A1:   x in D;
A2:   x = <*>(NAT) by A1,TARSKI:def 1;
      thus thesis by A2,FINSEQ_1:def 11;
    end;
    thus for p st p in D holds ProperPrefixes p c= D
    proof
      let p;
      assume
A3:   p in D;
A4:   p = <*>(NAT) by A3,TARSKI:def 1;
      thus thesis by A4,Th39,XBOOLE_1:2;
    end;
    let p,k,n;
    assume
A5: p^<*k*> in D;
    thus thesis by A5,TARSKI:def 1;
  end;
end;

definition
  mode Tree is Tree-like non empty set;
end;

reserve T,T1 for Tree;

canceled;

theorem Th44:
  x in T implies x is FinSequence of NAT
proof
  assume
A1: x in T;
A2: T c= NAT* by Def5;
  thus thesis by A1,A2,FINSEQ_1:def 11;
end;

definition
  let T;
  redefine mode Element of T -> FinSequence of NAT;
  coherence by Th44;
end;

theorem Th45:
  for p,q being FinSequence st p in T & q is_a_prefix_of p holds q in T
proof
  let p,q be FinSequence;
  assume that
A1: p in T and
A2: q is_a_prefix_of p;
  reconsider r = p as Element of T by A1;
A3: ProperPrefixes r c= T by Def5;
A4: q is_a_proper_prefix_of p or q = p by A2,XBOOLE_0:def 8;
A5: q in ProperPrefixes p or p = q by A4,Th36;
  thus thesis by A3,A5;
end;

theorem Th46:
  for r being FinSequence st q^r in T holds q in T
proof
  let r be FinSequence;
  assume
A1: q^r in T;
  reconsider p = q^r as FinSequence of NAT by A1,Th44;
  reconsider s = p|Seg len q as FinSequence of NAT by FINSEQ_1:23;
A2: len p = len q + len r by FINSEQ_1:35;
A3: len q <= len p by A2,NAT_1:11;
A4: len s = len q by A3,FINSEQ_1:21;
A5: now
    let n be Nat;
    assume
A6: 1 <= n & n <= len q;
A7: n in Seg len q by A6,FINSEQ_1:3;
A8: Seg len q = dom q by FINSEQ_1:def 3;
A9: p.n = q.n by A7,A8,FINSEQ_1:def 7;
    thus q.n = s.n by A7,A9,FUNCT_1:72;
  end;
A10: q = s by A4,A5,FINSEQ_1:18;
A11: q is_a_prefix_of p by A10,Def1;
A12: now
    assume
A13: q <> p;
A14: q is_a_proper_prefix_of p by A11,A13,XBOOLE_0:def 8;
A15: q in ProperPrefixes p by A14,Def4;
A16: ProperPrefixes p c= T by A1,Def5;
    thus thesis by A15,A16;
  end;
  thus thesis by A1,A12;
end;

theorem Th47:
  {} in T & <*> NAT in T
proof consider x being Element of T;
  reconsider x as FinSequence of NAT;
A1: x in T;
A2: {}^x in T by A1,FINSEQ_1:47;
A3: {} = <*> NAT;
  thus thesis by A2,A3,Th46;
end;

theorem Th48:
  { {} } is Tree
proof
  set D = { {} };
A1: D is Tree-like
  proof
    thus D c= NAT*
    proof
      let x;
      assume
A2:   x in D;
A3:   x = {} by A2,TARSKI:def 1;
      thus thesis by A3,FINSEQ_1:66;
    end;
    thus for p st p in D holds ProperPrefixes p c= D
    proof
      let p;
      assume
A4:   p in D;
A5:   p = {} by A4,TARSKI:def 1;
      thus thesis by A5,Th39,XBOOLE_1:2;
    end;
    let p,k,n;
    assume
A6: p^<*k*> in D;
    thus thesis by A6,TARSKI:def 1;
  end;
  thus thesis by A1;
end;

theorem Th49:
  T \/ T1 is Tree
proof
  reconsider D = T \/ T1 as non empty set;
A1: D is Tree-like
  proof
A2: T c= NAT* & T1 c= NAT* by Def5;
    thus D c= NAT* by A2,XBOOLE_1:8;
    thus for p st p in D holds ProperPrefixes p c= D
    proof
      let p;
      assume
A3:   p in D;
A4:   p in T or p in T1 by A3,XBOOLE_0:def 3;
A5:   ProperPrefixes p c= T or ProperPrefixes p c= T1 by A4,Def5;
A6:   T c= D & T1 c= D by XBOOLE_1:7;
      thus thesis by A5,A6,XBOOLE_1:1;
    end;
    let p,k,n;
    assume that
A7: p^<*k*> in D and
A8: n <= k;
A9: p^<*k*> in T or p^<*k*> in T1 by A7,XBOOLE_0:def 3;
A10: p^<*n*> in T or p^<*n*> in T1 by A8,A9,Def5;
    thus thesis by A10,XBOOLE_0:def 3;
  end;
  thus thesis by A1;
end;

theorem Th50:
  T /\ T1 is Tree
proof
A1: {} in T & {} in T1 by Th47;
  reconsider D = T /\ T1 as non empty set by A1,XBOOLE_0:def 4;
A2: D is Tree-like
  proof
A3: T c= NAT* & T /\ T1 c= T by Def5,XBOOLE_1:17;
    thus D c= NAT* by A3,XBOOLE_1:1;
    thus for p st p in D holds ProperPrefixes p c= D
    proof
      let p;
      assume
A4:   p in D;
A5:   p in T by A4,XBOOLE_0:def 4;
A6:   p in T1 by A4,XBOOLE_0:def 4;
A7:   ProperPrefixes p c= T by A5,Def5;
A8:   ProperPrefixes p c= T1 by A6,Def5;
      thus thesis by A7,A8,XBOOLE_1:19;
    end;
    let p,k,n;
    assume that
A9: p^<*k*> in D and
A10: n <= k;
A11: p^<*k*> in T by A9,XBOOLE_0:def 4;
A12: p^<*k*> in T1 by A9,XBOOLE_0:def 4;
A13: p^<*n*> in T by A10,A11,Def5;
A14: p^<*n*> in T1 by A10,A12,Def5;
    thus thesis by A13,A14,XBOOLE_0:def 4;
  end;
  thus thesis by A2;
end;

::
::  Finite trees and their properties
::

registration
  cluster finite Tree;
  existence by Th48;
end;

reserve fT,fT1 for finite Tree;

canceled;

theorem
  fT \/ fT1 is finite Tree by Th49;

theorem
  fT /\ T is finite Tree & T /\ fT is finite Tree by Th50;

::
::  Elementary trees
::

definition
  let n;
  canceled;
  func elementary_tree n -> finite Tree equals
  { <*k*> : k < n } \/ { {} };
  correctness
  proof
    reconsider IT = { <*k*> : k < n } \/ { {} } as non empty set;
A1: IT is Tree-like
    proof
      thus IT c= NAT*
      proof
        let x;
        assume
A2:     x in IT;
A3:     x in { <*k*> : k < n } or x in { {} } by A2,XBOOLE_0:def 3;
A4:     {} in NAT* by FINSEQ_1:66;
A5:     now
          assume
A6:       x in { <*k*> : k < n };
A7:       ex k st x = <*k*> & k < n by A6;
          thus thesis by A7,FINSEQ_1:def 11;
        end;
        thus thesis by A3,A4,A5,TARSKI:def 1;
      end;
      thus for p st p in IT holds ProperPrefixes p c= IT
      proof
        let p;
        assume
A8:     p in IT;
A9:     p in { <*k*> : k < n } or p in { {} } by A8,XBOOLE_0:def 3;
A10:    {} c= IT by XBOOLE_1:2;
A11:    now
          assume
A12:      p in { <*k*> : k < n };
A13:      ex k st p = <*k*> & k < n by A12;
A14:      ProperPrefixes p = { {} } by A13,Th40;
          thus thesis by A14,XBOOLE_1:7;
        end;
        thus thesis by A9,A10,A11,Th39,TARSKI:def 1;
      end;
      let p,k,m;
      assume
A15:  p^<*k*> in IT;
A16:  p^<*k*> in
      { <*j*> where j is Element of NAT : j < n } or p^<*k*> in { {} }
      by A15,XBOOLE_0:def 3;
      consider l being Element of NAT such that
A17:  p^<*k*> = <*l*> and
A18:  l < n by A16,TARSKI:def 1;
A19:  len p + len <*k*> = len <*l*> by A17,FINSEQ_1:35
        .= 1 by FINSEQ_1:56;
A20:  len p + 1 = 0 + 1 by A19,FINSEQ_1:56;
A21:  p = {} by A20;
A22:  <*k*> = <*l*> by A17,A21,FINSEQ_1:47;
A23:  <*k*>.1 = k by FINSEQ_1:def 8;
A24:  k = l by A22,A23,FINSEQ_1:def 8;
      assume
A25:  m <= k;
A26:  p^<*m*> = <*m*> by A21,FINSEQ_1:47;
A27:  m < n by A18,A24,A25,XXREAL_0:2;
A28:  p^<*m*> in { <*j*> where j is Element of NAT : j < n } by A26,A27;
      thus thesis by A28,XBOOLE_0:def 3;
    end;
    reconsider IT as Tree by A1;
A29: IT,Seg(n+1) are_equipotent
    proof
      defpred F[set,set] means
      $1 = {} & $2 = 1 or ex n st $1 = <*n*> & $2 = n+2;
A30:  x in IT & F[x,y] & F[x,z] implies y = z
      proof
        assume that x in IT and
        A31:    (
 x = {} & y = 1 or ex n st x = <*n*> & y = n+2)&( x = {} & z = 1 or ex n st x
        = <*n*> & z = n+2);
A32:    now
          given n1 being Element of NAT such that
A33:      x = <*n1*> & y = n1+2;
          given n2 being Element of NAT such that
A34:      x = <*n2*> & z = n2+2;
A35:      <*n1*>.1 = n1 by FINSEQ_1:def 8;
          thus thesis by A33,A34,A35,FINSEQ_1:def 8;
        end;
        thus thesis by A31,A32;
      end;
A36:  x in IT implies ex y st F[x,y]
      proof
        assume
A37:    x in IT;
A38:    now
          assume
A39:      x in { <*k*> : k < n };
          consider k such that
A40:      x = <*k*>
          and k < n by A39;
          reconsider y = k+2 as set;
          take y;
          thus F[x,y] by A40;
        end;
A41:    now
          assume
A42:      x in { {} };
          reconsider y = 1 as set;
          take y;
          thus F[x,y] by A42,TARSKI:def 1;
        end;
        thus thesis by A37,A38,A41,XBOOLE_0:def 3;
      end;
      consider f such that
A43:  dom f = IT & for x st x in IT holds F[x,f.x] from CLASSES1:sch 1(A36);
      take f;
      thus f is one-to-one
      proof
        let x,y;
        assume that
A44:    x in dom f and
A45:    y in dom f and
A46:    f.x = f.y;
A47:    x = {} & f.x = 1 or ex n st x = <*n*> & f.x = n+2 by A43,A44;
A48:    y = {} & f.y = 1 or ex n st y = <*n*> & f.y = n+2 by A43,A45;
A49:    now
          assume that
          x = {} and
A50:      f.x = 1;
          given n such that
          y = <*n*> and
A51:      f.y = n+2;
A52:      0+1 = n+1+1 by A46,A50,A51;
          thus contradiction by A52;
        end;
A53:    now
          assume that
          y = {} and
A54:      f.y = 1;
          given n such that
          x = <*n*> and
A55:      f.x = n+2;
A56:      0+1 = n+1+1 by A46,A54,A55;
          thus contradiction by A56;
        end;
        thus thesis by A46,A47,A48,A49,A53;
      end;
      thus dom f = IT by A43;
      thus rng f c= Seg(n+1)
      proof
        let x;
        assume
A57:    x in rng f;
        consider y such that
A58:    y in dom f and
A59:    x = f.y by A57,FUNCT_1:def 5;
A60:    1 <= 1+n by NAT_1:11;
A61:    1 in Seg(n+1) by A60,FINSEQ_1:3;
A62:    now
          given k such that
A63:      y = <*k*> and
A64:      x = k+2;
A65:      y in { <*j*> where j is Element of NAT : j < n } or y in { {} }
          by A43,A58,XBOOLE_0:def 3;
          consider l being Element of NAT such that
A66:      y = <*l*> & l < n by A63,A65,TARSKI:def 1;
A67:      <*k*>.1 = k & <*l*>.1 = l by FINSEQ_1:def 8;
A68:      k+1 <= n by A63,A66,A67,NAT_1:13;
A69:      1+0 <= k+1+1 & k+1+1 <= n+1 by A68,XREAL_1:9;
          thus thesis by A64,A69,FINSEQ_1:3;
        end;
        thus thesis by A43,A58,A59,A61,A62;
      end;
      let x;
      assume
A70:  x in Seg(n+1);
      reconsider k = x as Element of NAT by A70;
A71:  1 <= k by A70,FINSEQ_1:3;
A72:  k <= n+1 by A70,FINSEQ_1:3;
A73:  {} in { {} } by TARSKI:def 1;
A74:  {} in IT by A73,XBOOLE_0:def 3;
A75:  F[{},f.{}] by A43,A74;
A76:  1 in rng f by A43,A74,A75,FUNCT_1:def 5;
A77:  now
        assume
A78:    1 < k;
A79:    1+1 <= k by A78,NAT_1:13;
        consider m be Nat such that
A80:    k = 2+m by A79,NAT_1:10;
        reconsider m as Element of NAT by ORDINAL1:def 13;
A81:    1+1+m = 1+(1+m);
A82:    1+m <= n by A72,A80,A81,XREAL_1:8;
A83:    m < n by A82,NAT_1:13;
A84:    <*m*> in { <*j*> where j is Element of NAT : j < n } by A83;
A85:    <*m*> in IT by A84,XBOOLE_0:def 3;
A86:    F[<*m*>,f.<*m*>] by A43,A85;
A87:    k = f.<*m*> by A30,A80,A85,A86;
        thus k in rng f by A43,A85,A87,FUNCT_1:def 5;
      end;
      thus thesis by A71,A76,A77,XXREAL_0:1;
    end;
    thus thesis by A29,CARD_1:68;
  end;
end;

canceled;

theorem Th55:
  k < n implies <*k*> in elementary_tree n
proof
  assume
A1: k < n;
A2: <*k*> in { <*j*> where j is Element of NAT : j < n } by A1;
  thus thesis by A2,XBOOLE_0:def 3;
end;

theorem Th56:
  elementary_tree 0 = { {} }
proof
A1: now
    consider x being Element of { <*j*> where j is Element of NAT : j < 0 };
    assume
A2: { <*j*> where j is Element of NAT : j < 0 } <> {};
A3: x in { <*j*> where j is Element of NAT : j < 0 } by A2;
A4: ex k st x = <*k*> & k < 0 by A3;
    thus contradiction by A4;
  end;
  thus thesis by A1;
end;

theorem
  p in elementary_tree n implies p = {} or ex k st k < n & p = <*k*>
proof
  assume
A1: p in elementary_tree n;
A2: p in { {} } or p in { <*k*> : k < n } by A1,XBOOLE_0:def 3;
A3: p in { <*k*> : k < n } implies ex k st p = <*k*> & k < n;
  thus thesis by A2,A3,TARSKI:def 1;
end;

::
::  Leaves and subtrees
::

definition
  let T;
  func Leaves T -> Subset of T means
  :Def8:
  p in it iff p in T & not ex q st q in T & p is_a_proper_prefix_of q;
  existence
  proof
    defpred X[set] means
    for p st $1 = p for q st q in T holds not p is_a_proper_prefix_of q;
    consider X such that
A1: x in X iff x in T & X[x] from XBOOLE_0:sch 1;
A2: X c= T
    proof
      let x;
      thus thesis by A1;
    end;
    reconsider X as Subset of T by A2;
    take X;
    let p;
    thus
    p in X implies p in T & not ex q st q in T & p is_a_proper_prefix_of
    q by A1;
    assume that
A3: p in T and
A4: not ex q st q in T & p is_a_proper_prefix_of q;
A5: for r being FinSequence of NAT st p = r
    for q st q in T holds not r is_a_proper_prefix_of q by A4;
    thus thesis by A1,A3,A5;
  end;
  uniqueness
  proof
    let L1,L2 be Subset of T such that
A6: p in L1 iff p in T & not ex q st q in T & p is_a_proper_prefix_of q and
A7: p in L2 iff p in T & not ex q st q in T & p is_a_proper_prefix_of q;
A8: T c= NAT* by Def5;
A9: L1 c= NAT* by A8,XBOOLE_1:1;
A10: L2 c= NAT* by A8,XBOOLE_1:1;
A11: now
      let x;
      thus x in L1 implies x in L2
      proof
        assume
A12:    x in L1;
        reconsider p = x as FinSequence of NAT by A9,A12,FINSEQ_1:def 11;
A13:    not ex q st q in T & p is_a_proper_prefix_of q by A6,A12;
        thus thesis by A7,A12,A13;
      end;
      assume
A14:  x in L2;
      reconsider p = x as FinSequence of NAT by A10,A14,FINSEQ_1:def 11;
A15:  not ex q st q in T & p is_a_proper_prefix_of q by A7,A14;
      thus x in L1 by A6,A14,A15;
    end;
    thus thesis by A11,TARSKI:2;
  end;
  let p such that
A16: p in T;
  func T|p -> Tree means  :: subtree of T, which root is in p
  :Def9:
  q in it iff p^q in T;
  existence
  proof
    defpred X[set] means for q st $1 = q holds p^q in T;
    consider X such that
A17: x in X iff x in NAT* & X[x] from XBOOLE_0:sch 1;
    A18: <*>
 NAT in NAT* & for q st <*> NAT = q holds p^q in T by A16,FINSEQ_1:47
    ,def 11;
    reconsider X as non empty set by A17,A18;
A19: X c= NAT*
    proof
      let x;
      thus thesis by A17;
    end;
A20: now
      let q such that
A21:  q in X;
      thus ProperPrefixes q c= X
      proof
        let x;
        assume
A22:    x in ProperPrefixes q;
        consider r being FinSequence such that
A23:    x = r and
A24:    r is_a_proper_prefix_of q by A22,Def4;
A25:    r is_a_prefix_of q by A24,XBOOLE_0:def 8;
A26:    ex n st r = q|Seg n by A25,Def1;
        reconsider r as FinSequence of NAT by A26,FINSEQ_1:23;
        consider s being FinSequence such that
A27:    q = r^s by A26,FINSEQ_1:101;
A28:    p^q in T by A17,A21;
A29:    p^q = p^r^s by A27,FINSEQ_1:45;
        A30:    r
 in NAT* & for q st r = q holds p^q in T by A28,A29,Th46,FINSEQ_1:def 11;
        thus thesis by A17,A23,A30;
      end;
    end;
A31: now
      let q,k,n;
      assume that
A32:  q^<*k*> in X and
A33:  n <= k;
A34:  p^(q^<*k*>) in T by A17,A32;
A35:  p^q^<*k*> in T by A34,FINSEQ_1:45;
A36:  p^q^<*n*> in T by A33,A35,Def5;
      A37:  q
^<*n*> in NAT* & for r st r = q^<*n*> holds p^r in T by A36,FINSEQ_1:45
      ,def 11;
      thus q^<*n*> in X by A17,A37;
    end;
    reconsider X as Tree by A19,A20,A31,Def5;
    take X;
    let q;
    thus q in X implies p^q in T by A17;
    assume
A38: p^q in T;
    A39: q
 in NAT* & for r be FinSequence of NAT st q = r holds p^r in T by A38,
    FINSEQ_1:def 11;
    thus thesis by A17,A39;
  end;
  uniqueness
  proof
    let T1,T2 be Tree such that
A40: q in T1 iff p^q in T and
A41: q in T2 iff p^q in T;
A42: now
      let x;
      thus x in T1 implies x in T2
      proof
        assume
A43:    x in T1;
        reconsider q = x as FinSequence of NAT by A43,Th44;
A44:    p^q in T by A40,A43;
        thus thesis by A41,A44;
      end;
      assume
A45:  x in T2;
      reconsider q = x as FinSequence of NAT by A45,Th44;
A46:  p^q in T by A41,A45;
      thus x in T1 by A40,A46;
    end;
    thus thesis by A42,TARSKI:2;
  end;
end;

canceled 2;

theorem
  T|(<*> NAT) = T
proof
A1: <*> NAT in T by Th47;
  thus T|(<*> NAT) c= T
  proof
    let x;
    assume
A2: x in T|(<*> NAT);
    reconsider p = x as Element of T|(<*> NAT) by A2;
A3: {}^p = p by FINSEQ_1:47;
    thus thesis by A1,A3,Def9;
  end;
  let x;
  assume
A4: x in T;
  reconsider p = x as Element of T by A4;
A5: {}^p = p by FINSEQ_1:47;
  thus thesis by A1,A5,Def9;
end;

registration
  let T be finite Tree;
  let p be Element of T;
  cluster T|p -> finite;
  coherence
  proof
    consider t being Function such that
A1: rng t = T and
A2: dom t in omega by FINSET_1:def 1;
    defpred P[set,set] means ex q st t.$1 = q & ((ex r st $2 = r & q = p^r) or
    (for r holds q <> p^r) & $2 = <*> NAT);
A3: for x st x in dom t ex y st P[x,y]
    proof
      let x;
      assume
A4:   x in dom t;
A5:   t.x in T by A1,A4,FUNCT_1:def 5;
      reconsider q = t.x as FinSequence of NAT by A5,Th44;
A6:   (ex r st q = p^r) implies thesis;
      thus thesis by A6;
    end;
    consider f being Function such that
A7: dom f = dom t & for x st x in dom t holds P[x,f.x] from CLASSES1:sch 1(
    A3);
A8: T|p is finite
    proof
      take f;
      thus rng f c= T|p
      proof
        let x;
        assume
A9:     x in rng f;
        consider y such that
A10:    y in dom f and
A11:    x = f.y by A9,FUNCT_1:def 5;
        consider q such that
A12:    t.y = q and
A13:    (ex r st x = r & q = p^r) or (for r holds q <> p^r) & x = <*> NAT
        by A7,A10,A11;
        assume
A14:    not x in T|p;
        A15:    p
^(<*> NAT) = p & q in T by A1,A7,A10,A12,FINSEQ_1:47,FUNCT_1:def 5;
        thus contradiction by A13,A14,A15,Def9;
      end;
      thus T|p c= rng f
      proof
        let x;
        assume
A16:    x in T|p;
        reconsider q = x as FinSequence of NAT by A16,Th44;
A17:    p^q in T by A16,Def9;
        consider y such that
A18:    y in dom t and
A19:    p^q = t.y by A1,A17,FUNCT_1:def 5;
A20:    P[y,f.y] by A7,A18;
A21:    x = f.y by A19,A20,FINSEQ_1:46;
        thus thesis by A7,A18,A21,FUNCT_1:def 5;
      end;
      thus thesis by A2,A7;
    end;
    thus thesis by A8;
  end;
end;

definition
  let T;
  assume
A1: Leaves T <> {};
  mode Leaf of T -> Element of T means
    it in Leaves T;
  existence
  proof consider x being Element of Leaves T;
    reconsider x as Element of T by A1,TARSKI:def 3;
    take x;
    thus thesis by A1;
  end;
end;

definition
  let T;
  mode Subtree of T -> Tree means
    ex p being Element of T st it = T|p;
  existence
  proof consider p being Element of T;
    take T|p, p;
    thus thesis;
  end;
end;

reserve t for Element of T;

definition
  let T,p,T1;
  assume
A1: p in T;
  func T with-replacement (p,T1) -> Tree means
  :Def12:
  q in it iff q in T & not p is_a_proper_prefix_of q or
  ex r st r in T1 & q = p^r;
  existence
  proof
    reconsider p' = p as Element of T by A1;
A2: not p is_a_proper_prefix_of p';
A3: p in { t : not p is_a_proper_prefix_of t } by A2;
    reconsider X = { t : not p is_a_proper_prefix_of t } \/
    { p^s where s is Element of T1 : s = s } as non empty set by A3;
A4: x in { t : not p is_a_proper_prefix_of t } implies
    x is FinSequence of NAT & x in NAT* & x in T
    proof
      assume
A5:   x in { t : not p is_a_proper_prefix_of t };
A6:   ex t st x = t & not p is_a_proper_prefix_of t by A5;
      thus x is FinSequence of NAT by A6;
      thus thesis by A6,FINSEQ_1:def 11;
    end;
A7: X is Tree-like
    proof
      thus X c= NAT*
      proof
        let x;
        assume
A8:     x in X;
A9:     x in { t : not p is_a_proper_prefix_of t } or
        x in { p^s where s is Element of T1 : s = s } by A8,XBOOLE_0:def 3;
A10:    now
          assume
A11:      x in { p^s where s is Element of T1 : s = s };
A12:      ex s being Element of T1 st x = p^s & s = s by A11;
          thus x is FinSequence of NAT by A12;
        end;
        thus thesis by A4,A9,A10,FINSEQ_1:def 11;
      end;
      thus for q st q in X holds ProperPrefixes q c= X
      proof
        let q such that
A13:    q in X;
A14:    now
          assume
A15:      q in { t : not p is_a_proper_prefix_of t };
A16:      ex t st q = t & not p is_a_proper_prefix_of t by A15;
A17:      ProperPrefixes q c= T by A16,Def5;
          thus ProperPrefixes q c= X
          proof
            let x;
            assume
A18:        x in ProperPrefixes q;
            consider p1 such that
A19:        x = p1 and
A20:        p1 is_a_proper_prefix_of q by A18,Def4;
            reconsider p1 as Element of T by A17,A18,A19;
A21:        not p is_a_proper_prefix_of p1 by A16,A20,XBOOLE_1:56;
A22:        x in { s1 where s1 is Element of T :
            not p is_a_proper_prefix_of s1 } by A19,A21;
            thus thesis by A22,XBOOLE_0:def 3;
          end;
        end;
A23:    now
          assume
A24:      q in { p^s where s is Element of T1 : s = s };
          consider s being Element of T1 such that
A25:      q = p^s
          and s = s by A24;
          thus ProperPrefixes q c= X
          proof
            let x;
            assume
A26:        x in ProperPrefixes q;
            reconsider r = x as FinSequence by A26,Th35;
A27:        r is_a_proper_prefix_of p^s by A25,A26,Th36;
A28:        r is_a_prefix_of p^s by A27,XBOOLE_0:def 8;
            consider r1 being FinSequence such that
A29:        p^s = r^r1 by A28,Th8;
A30:        now
              assume
A31:          len p <= len r;
              consider r2 being FinSequence such that
A32:          p^r2 = r by A29,A31,FINSEQ_1:64;
A33:          p^s = p^(r2^r1) by A29,A32,FINSEQ_1:45;
A34:          s = r2^r1 by A33,FINSEQ_1:46;
              A35:          dom
 r2 = Seg len r2 & s|dom r2 = r2 by A34,FINSEQ_1:33,def 3;
              reconsider r2 as FinSequence of NAT by A35,FINSEQ_1:23;
A36:          r2 is_a_prefix_of s by A35,Def1;
              reconsider r2 as Element of T1 by A36,Th45;
A37:          r = p^r2 by A32;
A38:          r in { p^v where v is Element of T1 : v = v } by A37;
              thus r in X by A38,XBOOLE_0:def 3;
            end;
A39:        now
              assume
A40:          len r <= len p;
A41:          ex r2 being FinSequence st r^r2 = p by A29,A40,FINSEQ_1:64;
A42:          p|dom r = r by A41,FINSEQ_1:33;
A43:          dom r = Seg len r by FINSEQ_1:def 3;
              reconsider r3 = r as FinSequence of NAT by A42,A43,FINSEQ_1:23;
A44:          r3 is_a_prefix_of p by A42,A43,Def1;
A45:          not p is_a_proper_prefix_of r3 by A44,XBOOLE_0:def 8;
              reconsider r3 as Element of T by A1,A44,Th45;
A46:          r3 in { t : not p is_a_proper_prefix_of t } by A45;
              thus r in X by A46,XBOOLE_0:def 3;
            end;
            thus thesis by A30,A39;
          end;
        end;
        thus thesis by A13,A14,A23,XBOOLE_0:def 3;
      end;
      let q,k,n such that
A47:  q^<*k*> in X and
A48:  n <= k;
A49:  now
        assume
A50:    q^<*k*> in { t : not p is_a_proper_prefix_of t };
A51:    ex s being Element of T st
        q^<*k*> = s & not p is_a_proper_prefix_of s by A50;
        reconsider u = q^<*n*> as Element of T by A48,A51,Def5;
A52:    now
          assume
A53:      p is_a_proper_prefix_of u;
A54:      p is_a_prefix_of q by A53,Th32;
          thus contradiction by A51,A54,Th31;
        end;
A55:    q^<*n*> in { t : not p is_a_proper_prefix_of t } by A52;
        thus thesis by A55,XBOOLE_0:def 3;
      end;
A56:  now
        assume
A57:    q^<*k*> in { p^s where s is Element of T1 : s = s };
        consider s being Element of T1 such that
A58:    q^<*k*> = p^s
        and s = s by A57;
A59:    now
          assume
A60:      len q <= len p;
          consider r being FinSequence such that
A61:      q^r = p by A58,A60,FINSEQ_1:64;
A62:      q^<*k*> = q^(r^s) by A58,A61,FINSEQ_1:45;
A63:      <*k*> = r^s by A62,FINSEQ_1:46;
A64:      now
            assume
A65:        r = <*k*>;
            reconsider s = q^<*n*> as Element of T by A1,A48,A61,A65,Def5;
A66:        now
              assume
A67:          p is_a_proper_prefix_of s;
A68:          p is_a_prefix_of s by A67,XBOOLE_0:def 8;
A69:          len p = len q + len <*k*> by A61,A65,FINSEQ_1:35
                .= len q + 1 by FINSEQ_1:57
                .= len q + len <*n*> by FINSEQ_1:57
                .= len s by FINSEQ_1:35;
              thus contradiction by A67,A68,A69,Th15;
            end;
            thus q^<*n*> in { t : not p is_a_proper_prefix_of t } by A66;
          end;
A70:      now
            assume that
A71:        s = <*k*> and
A72:        r = {};
A73:        s = {}^s & {} = <*> NAT by FINSEQ_1:47;
A74:        {}^<*n*> in T1 by A48,A71,A73,Def5;
            reconsider t = <*n*> as Element of T1 by A74,FINSEQ_1:47;
A75:        q^<*n*> = p^t by A61,A72,FINSEQ_1:47;
            thus q^<*n*> in { p^v where v is Element of T1 : v = v }
            by A75;
          end;
          thus thesis by A63,A64,A70,FINSEQ_1:109,XBOOLE_0:def 3;
        end;
A76:    now
          assume
A77:      len p <= len q;
          consider r being FinSequence such that
A78:      p^r = q by A58,A77,FINSEQ_1:64;
A79:      p^(r^<*k*>) = p^s by A58,A78,FINSEQ_1:45;
A80:      r^<*k*> = s by A79,FINSEQ_1:46;
          A81:      dom r = Seg len r & s|dom r = r by A80,FINSEQ_1:33,def 3;
          reconsider r as FinSequence of NAT by A81,FINSEQ_1:23;
          reconsider t = r^<*n*> as Element of T1 by A48,A80,Def5;
A82:      q^<*n*> = p^t by A78,FINSEQ_1:45;
A83:      q^<*n*> in { p^v where v is Element of T1 : v = v } by A82;
          thus thesis by A83,XBOOLE_0:def 3;
        end;
        thus thesis by A59,A76;
      end;
      thus thesis by A47,A49,A56,XBOOLE_0:def 3;
    end;
    reconsider X as Tree by A7;
    take X;
    let q;
    thus q in X implies q in T & not p is_a_proper_prefix_of q or
    ex r st r in T1 & q = p^r
    proof
      assume
A84:  q in X;
A85:  now
        assume
A86:    q in { t : not p is_a_proper_prefix_of t };
A87:    ex s being Element of T st q = s & not p is_a_proper_prefix_of s
        by A86;
        thus thesis by A87;
      end;
A88:  now
        assume
A89:    q in { p^s where s is Element of T1 : s = s };
A90:    ex s being Element of T1 st q = p^s & s = s by A89;
        thus ex r st r in T1 & q = p^r by A90;
      end;
      thus thesis by A84,A85,A88,XBOOLE_0:def 3;
    end;
    assume
A91: q in T & not p is_a_proper_prefix_of q or ex r st r in T1 & q = p^r;
A92: q in T & not p is_a_proper_prefix_of q implies
    q in { t : not p is_a_proper_prefix_of t };
A93: (ex r st r in T1 & q = p^r)
    implies q in { p^v where v is Element of T1 : v = v };
    thus thesis by A91,A92,A93,XBOOLE_0:def 3;
  end;
  uniqueness
  proof
    let S1,S2 be Tree such that
A94: q in S1 iff q in T & not p is_a_proper_prefix_of q or
    ex r st r in T1 & q = p^r and
A95: q in S2 iff q in T & not p is_a_proper_prefix_of q or
    ex r st r in T1 & q = p^r;
    thus S1 c= S2
    proof
      let x;
      assume
A96:  x in S1;
      reconsider q = x as FinSequence of NAT by A96,Th44;
      A97:  q
 in T & not p is_a_proper_prefix_of q or ex r st r in T1 & q = p^ r
      by A94,A96;
      thus thesis by A95,A97;
    end;
    let x;
    assume
A98: x in S2;
    reconsider q = x as FinSequence of NAT by A98,Th44;
A99: q in T & not p is_a_proper_prefix_of q or ex r st r in T1 & q = p^r
    by A95,A98;
    thus thesis by A94,A99;
  end;
end;

canceled 3;

theorem Th64:
  p in T implies T with-replacement (p,T1) =
  { t1 where t1 is Element of T : not p is_a_proper_prefix_of t1 } \/
  { p^s where s is Element of T1 : s = s }
proof
  assume
A1: p in T;
  thus
  T with-replacement (p,T1) c= { t : not p is_a_proper_prefix_of t } \/
  { p^s where s is Element of T1 : s = s }
  proof
    let x;
    assume
A2: x in T with-replacement (p,T1);
    reconsider q = x as FinSequence of NAT by A2,Th44;
A3: (ex r st r in T1 & q = p^r) implies
    x in { p^s where s is Element of T1 : s = s };
A4: q in T & not p is_a_proper_prefix_of q implies
    x in { t : not p is_a_proper_prefix_of t };
    thus thesis by A1,A2,A3,A4,Def12,XBOOLE_0:def 3;
  end;
  let x such that
A5: x in { t : not p is_a_proper_prefix_of t } \/
  { p^s where s is Element of T1 : s = s };
A6: now
    assume
A7: x in { p^s where s is Element of T1 : s = s };
A8: ex s being Element of T1 st x = p^s & s = s by A7;
    thus thesis by A1,A8,Def12;
  end;
A9: now
    assume
A10: x in { t : not p is_a_proper_prefix_of t };
A11: ex t st x = t & not p is_a_proper_prefix_of t by A10;
    thus thesis by A1,A11,Def12;
  end;
  thus thesis by A5,A6,A9,XBOOLE_0:def 3;
end;

canceled;

theorem
  p in T implies T1 = T with-replacement (p,T1)|p
proof
  assume
A1: p in T;
A2: p in T with-replacement (p,T1) by A1,Def12;
  thus T1 c= T with-replacement (p,T1)|p
  proof
    let x;
    assume
A3: x in T1;
    reconsider q = x as FinSequence of NAT by A3,Th44;
A4: p^q in T with-replacement (p,T1) by A1,A3,Def12;
    thus thesis by A2,A4,Def9;
  end;
  let x;
  assume
A5: x in T with-replacement (p,T1)|p;
  reconsider q = x as FinSequence of NAT by A5,Th44;
A6: p^q in T with-replacement (p,T1) by A2,A5,Def9;
A7: now
    assume that
    p^q in T and
A8: not p is_a_proper_prefix_of p^q;
A9: p is_a_prefix_of p^q by Th8;
A10: p^q = p by A8,A9,XBOOLE_0:def 8
      .= p^{} by FINSEQ_1:47;
A11: q = {} by A10,FINSEQ_1:46;
    thus q in T1 by A11,Th47;
  end;
A12: (ex r st r in T1 & p^q = p^r) implies q in T1 by FINSEQ_1:46;
  thus thesis by A1,A6,A7,A12,Def12;
end;

registration
  let T be finite Tree, t be Element of T;
  let T1 be finite Tree;
  cluster T with-replacement (t,T1) -> finite;
  coherence
  proof
A1: { s where s is Element of T : not t is_a_proper_prefix_of s } c= T
    proof
      let x;
      assume
A2:   x in
      { s where s is Element of T : not t is_a_proper_prefix_of s };
A3:   ex s being Element of T st x = s & not t is_a_proper_prefix_of s
      by A2;
      thus thesis by A3;
    end;
A4: T1,{ t^s where s is Element of T1 : s = s } are_equipotent
    proof
      defpred P[set,set] means ex q st $1 = q & $2 = t^q;
A5:   x in T1 implies ex y st P[x,y]
      proof
        assume
A6:     x in T1;
        reconsider q = x as FinSequence of NAT by A6,Th44;
A7:     t^q = t^q;
        thus thesis by A7;
      end;
      consider f such that
A8:   dom f = T1 & for x st x in T1 holds P[x,f.x] from CLASSES1:sch 1(A5);
      take f;
      thus f is one-to-one
      proof
        let x,y;
        assume that
A9:     x in dom f & y in dom f and
A10:    f.x = f.y;
        A11:    (
 ex q st x = q & f.x = t^q)& ex r st y = r & f.y = t^r by A8,A9;
        thus thesis by A10,A11,FINSEQ_1:46;
      end;
      thus dom f = T1 by A8;
      thus rng f c= { t^s where s is Element of T1 : s = s }
      proof
        let x;
        assume
A12:    x in rng f;
        consider y such that
A13:    y in dom f and
A14:    x = f.y by A12,FUNCT_1:def 5;
        consider q such that
A15:    y = q and
A16:    f.y = t^q by A8,A13;
        reconsider q as Element of T1 by A8,A13,A15;
A17:    x = t^q by A14,A16;
        thus thesis by A17;
      end;
      let x;
      assume
A18:  x in { t^s where s is Element of T1 : s = s };
      consider s being Element of T1 such that
A19:  x = t^s
      and s = s by A18;
A20:  P[s,f.s] by A8;
      thus thesis by A8,A19,A20,FUNCT_1:def 5;
    end;
A21: { t^s where s is Element of T1 : s = s } is finite by A4,CARD_1:68;
A22: { v where v is Element of T : not t is_a_proper_prefix_of v } \/ { t^s
    where s is Element of T1 : s = s } is finite by A1,A21;
    thus thesis by A22,Th64;
  end;
end;

reserve w for FinSequence;

theorem Th67:
  for p being FinSequence holds ProperPrefixes p,dom p are_equipotent
proof
  let p be FinSequence;
  defpred P[set,set] means ex w st $1 = w & $2 = (len w)+1;
A1: for x st x in ProperPrefixes p ex y st P[x,y]
  proof
    let x;
    assume
A2: x in ProperPrefixes p;
    consider q being FinSequence such that
A3: x = q
    and q is_a_proper_prefix_of p by A2,Def4;
    reconsider y = (len q)+1 as set;
    take y,q;
    thus thesis by A3;
  end;
  consider f such that
A4: dom f = ProperPrefixes p and
A5: for x st x in ProperPrefixes p holds P[x,f.x] from CLASSES1:sch 1(A1);
  take f;
  thus f is one-to-one
  proof
    let x,y;
    assume that
A6: x in dom f & y in dom f and
A7: f.x = f.y;
    A8: (
ex q being FinSequence st x = q & f.x = (len q)+1 )& ex r being FinSequence
    st y = r & f.x = (len r)+1 by A4,A5,A6,A7;
    thus thesis by A4,A6,A8,Th19,Th42;
  end;
  thus dom f = ProperPrefixes p by A4;
  thus rng f c= dom p
  proof
    let x;
    assume
A9: x in rng f;
    consider y such that
A10: y in dom f and
A11: x = f.y by A9,FUNCT_1:def 5;
    consider q being FinSequence such that
A12: y = q and
A13: x = (len q)+1 by A4,A5,A10,A11;
A14: len q < len p by A4,A10,A12,Th37;
A15: 0+1 <= (len q)+1 & (len q)+1 <= len p by A14,NAT_1:13;
A16: x in Seg len p by A13,A15,FINSEQ_1:3;
    thus thesis by A16,FINSEQ_1:def 3;
  end;
  let x;
  assume
A17: x in dom p;
A18: x in Seg len p by A17,FINSEQ_1:def 3;
  reconsider n = x as Element of NAT by A17;
A19: 1 <= n by A18,FINSEQ_1:3;
A20: n <= len p by A18,FINSEQ_1:3;
  consider m be Nat such that
A21: n = 1+m by A19,NAT_1:10;
  reconsider m as Element of NAT by ORDINAL1:def 13;
  reconsider q = p|Seg m as FinSequence by FINSEQ_1:19;
A22: m <= len p by A20,A21,NAT_1:13;
A23: m <> len p by A20,A21,NAT_1:13;
A24: len q = m by A22,FINSEQ_1:21;
A25: q is_a_prefix_of p by Def1;
  A26: len q = m by A22,FINSEQ_1:21;
A27: q is_a_proper_prefix_of p by A23,A25,A26,XBOOLE_0:def 8;
A28: q in ProperPrefixes p by A27,Th36;
A29: ex r being FinSequence st q = r & f.q = (len r)+1 by A5,A28;
  thus thesis by A4,A21,A24,A28,A29,FUNCT_1:def 5;
end;

registration
  let p be FinSequence;
  cluster ProperPrefixes p -> finite;
  coherence
  proof
A1: ProperPrefixes p,dom p are_equipotent by Th67;
    thus thesis by A1,CARD_1:68;
  end;
end;

theorem
  for p being FinSequence holds card ProperPrefixes p = len p
proof
  let p be FinSequence;
A1: dom p = Seg len p by FINSEQ_1:def 3;
A2: ProperPrefixes p,dom p are_equipotent by Th67;
A3: card dom p = card len p by A1,FINSEQ_1:76;
  thus thesis by A2,A3,CARD_1:21;
end;

::
::  Height and width of finite trees
::

definition
  let IT be set;
  attr IT is AntiChain_of_Prefixes-like means
  :Def13:
  (for x st x in IT holds x is FinSequence) &
  for p1,p2 st p1 in IT & p2 in IT & p1 <> p2 holds
  not p1,p2 are_c=-comparable;
end;

registration
  cluster AntiChain_of_Prefixes-like set;
  existence
  proof
    take {};
    thus for x st x in {} holds x is FinSequence;
    let p1,p2;
    thus thesis;
  end;
end;

definition
  mode AntiChain_of_Prefixes is AntiChain_of_Prefixes-like set;
end;

canceled;

theorem Th70:
  { w } is AntiChain_of_Prefixes-like
proof
  thus for x st x in { w } holds x is FinSequence by TARSKI:def 1;
  let p1,p2;
  assume that
A1: p1 in { w } and
A2: p2 in { w };
A3: p1 = w by A1,TARSKI:def 1;
  thus thesis by A2,A3,TARSKI:def 1;
end;

theorem Th71:
  not p1,p2 are_c=-comparable implies { p1,p2 } is AntiChain_of_Prefixes-like
proof
  assume
A1: not p1,p2 are_c=-comparable;
  thus for x st x in { p1,p2 } holds x is FinSequence by TARSKI:def 2;
  let q1,q2 be FinSequence;
  assume that
A2: q1 in { p1,p2 } and
A3: q2 in { p1,p2 };
A4: q1 = p1 or q1 = p2 by A2,TARSKI:def 2;
A5: q2 = p1 or q2 = p2 by A3,TARSKI:def 2;
  thus thesis by A1,A4,A5;
end;

definition
  let T;
  mode AntiChain_of_Prefixes of T -> AntiChain_of_Prefixes means
    :Def14:
    it c= T;
  existence
  proof consider t;
    reconsider S = { t } as AntiChain_of_Prefixes by Th70;
    take S;
    let x;
    assume
A1: x in S;
A2: x = t by A1,TARSKI:def 1;
    thus thesis by A2;
  end;
end;

reserve t1,t2 for Element of T;

canceled;

theorem Th73:
  {} is AntiChain_of_Prefixes of T & { {} } is AntiChain_of_Prefixes of T
proof
A1: {} is AntiChain_of_Prefixes-like
  proof
    thus for x st x in {} holds x is FinSequence;
    let p1,p2;
    thus thesis;
  end;
  reconsider S = {} as AntiChain_of_Prefixes by A1;
A2: S c= T by XBOOLE_1:2;
  thus {} is AntiChain_of_Prefixes of T by A2,Def14;
  reconsider S = { {} } as AntiChain_of_Prefixes by Th70;
A3: S is AntiChain_of_Prefixes of T
  proof
    let x;
    assume
A4: x in S;
A5: x = {} by A4,TARSKI:def 1;
    thus thesis by A5,Th47;
  end;
  thus thesis by A3;
end;

theorem
  { t } is AntiChain_of_Prefixes of T
proof reconsider S = { t } as AntiChain_of_Prefixes by Th70;
A1: S is AntiChain_of_Prefixes of T
  proof
    let x;
    assume
A2: x in S;
A3: x = t by A2,TARSKI:def 1;
    thus thesis by A3;
  end;
  thus thesis by A1;
end;

theorem
  not t1,t2 are_c=-comparable implies { t1,t2 } is AntiChain_of_Prefixes of T
proof
  assume
A1: not t1,t2 are_c=-comparable;
  reconsider A = { t1,t2 } as AntiChain_of_Prefixes by A1,Th71;
A2: A is AntiChain_of_Prefixes of T
  proof
    let x;
    assume
A3: x in A;
A4: x = t1 or x = t2 by A3,TARSKI:def 2;
    thus thesis by A4;
  end;
  thus thesis by A2;
end;

registration
  let T be finite Tree;
  cluster -> finite AntiChain_of_Prefixes of T;
  coherence
  proof
    let X be AntiChain_of_Prefixes of T;
A1: X c= T by Def14;
    thus thesis by A1;
  end;
end;

definition
  let T be finite Tree;
  func height T -> Element of NAT means
  :Def15:
  (ex p st p in T & len p = it) & for p st p in T holds len p <= it;
  existence
  proof
    consider n be Nat such that
A1: T,Seg n are_equipotent by FINSEQ_1:77;
    defpred X[Nat] means for p st p in T holds len p <= $1;
A2: ex n be Nat st X[n]
    proof
A3:   now
        given p such that
A4:     p in T and
A5:     not len p <= n;
A6:     ProperPrefixes p c= T by A4,Def5;
A7:     ProperPrefixes p,dom p are_equipotent by Th67;
A8:     card ProperPrefixes p c= card T by A6,CARD_1:27;
A9:     card ProperPrefixes p = card dom p by A7,CARD_1:21;
        A10:    card
 T = card Seg n & dom p = Seg len p by A1,CARD_1:21,FINSEQ_1:def 3;
A11:    Seg n c= Seg len p by A5,FINSEQ_1:7;
A12:    card Seg n c= card Seg len p by A11,CARD_1:27;
        A13:    card
 Seg n = card n & card Seg len p = card len p by FINSEQ_1:76;
A14:    card n = card len p by A8,A9,A10,A12,A13,XBOOLE_0:def 10;
        thus contradiction by A5,A14,CARD_1:71;
      end;
      consider n be Nat such that
A15:  X[n] by A3;
      reconsider n as Element of NAT by ORDINAL1:def 13;
      take n;
      thus thesis by A15;
    end;
    consider n being Nat such that
A16: X[n] and
A17: for m be Nat st X[m] holds n <= m from NAT_1:sch 5(A2);
    consider x being Element of T;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    take n;
    thus ex p st p in T & len p = n
    proof
      assume
A18:  for p st p in T holds len p <> n;
      reconsider x as FinSequence of NAT;
A19:  len x <= n by A16;
A20:  len x < n by A18,A19,XXREAL_0:1;
      consider k be Nat such that
A21:  n = k+1 by A20,NAT_1:6;
      reconsider k as Element of NAT by ORDINAL1:def 13;
A22:  for p st p in T holds len p <= k
      proof
        let p;
        assume
A23:    p in T;
A24:    len p <= n by A16,A23;
A25:    len p < k+1 by A18,A21,A23,A24,XXREAL_0:1;
        thus thesis by A25,NAT_1:13;
      end;
A26:  n <= k by A17,A22;
      thus contradiction by A21,A26,NAT_1:13;
    end;
    let p;
    assume
A27: p in T;
    thus thesis by A16,A27;
  end;
  uniqueness
  proof
    let l1,l2 be Element of NAT;
    given p1 being FinSequence of NAT such that
A28: p1 in T & len p1 = l1;
    assume
A29: for p st p in T holds len p <= l1;
    given p2 being FinSequence of NAT such that
A30: p2 in T & len p2 = l2;
    assume
A31: for p st p in T holds len p <= l2;
A32: l1 <= l2 by A28,A31;
A33: l2 <= l1 by A29,A30;
    thus thesis by A32,A33,XXREAL_0:1;
  end;
  func width T -> Element of NAT means
  :Def16:
  ex X being AntiChain_of_Prefixes of T st it = card X &
  for Y being AntiChain_of_Prefixes of T holds card Y <= card X;
  existence
  proof
    defpred X[Nat] means ex X being finite set st $1 = card X & X c= T &
    (for p,q st p in X & q in X & p <> q holds not p,q are_c=-comparable);
    A34: 0 = card {} & for p,q st p in {} & q in {} & p <> q holds not p,q
    are_c=-comparable;
A35: ex n be Nat st X[n] by A34,XBOOLE_1:2;
A36: for n be Nat st X[n] holds n <= card T
    proof
      let n be Nat;
      given X being finite set such that
A37:  n = card X & X c= T and
      for p,q st p in X & q in X & p <> q holds not p,q are_c=-comparable;
A38:  card X c= card T & card X = card n by A37,CARD_1:27;
A39:  card T = card card T;
      thus thesis by A38,A39,NAT_1:41;
    end;
    consider n being Nat such that
A40: X[n] and
A41: for m be Nat st X[m] holds m <= n from NAT_1:sch 6(A36,A35);
    consider X being finite set such that
A42: n = card X and
A43: X c= T and
A44: for p,q st p in X & q in X & p <> q holds not p,q are_c=-comparable
    by A40;
A45: X is AntiChain_of_Prefixes-like
    proof
      thus for x st x in X holds x is FinSequence
      proof
        let x;
        assume
A46:    x in X;
A47:    x in T by A43,A46;
A48:    T c= NAT* by Def5;
        thus thesis by A47,A48,FINSEQ_1:def 11;
      end;
      let p1,p2;
      assume
A49:  p1 in X & p2 in X;
      reconsider q1 = p1, q2 = p2 as Element of T by A43,A49;
A50:  p1 = q1 & p2 = q2;
      thus thesis by A44,A49,A50;
    end;
    reconsider X as AntiChain_of_Prefixes by A45;
    reconsider X as AntiChain_of_Prefixes of T by A43,Def14;
    reconsider n as Element of NAT by ORDINAL1:def 13;
    take n,X;
    thus n = card X by A42;
    let Y be AntiChain_of_Prefixes of T;
    A51: Y
 c= T & for p,q st p in Y & q in Y & p <> q holds not p,q are_c=-comparable
    by Def13,Def14;
    thus thesis by A41,A42,A51;
  end;
  uniqueness
  proof
    let n1,n2 be Element of NAT;
    given X1 being AntiChain_of_Prefixes of T such that
A52: n1 = card X1 and
A53: for Y being AntiChain_of_Prefixes of T holds card Y <= card X1;
    given X2 being AntiChain_of_Prefixes of T such that
A54: n2 = card X2 and
A55: for Y being AntiChain_of_Prefixes of T holds card Y <= card X2;
A56: card X1 <= card X2 by A55;
A57: card X2 <= card X1 by A53;
    thus thesis by A52,A54,A56,A57,XXREAL_0:1;
  end;
end;

canceled 2;

theorem
  1 <= width fT
proof
  A1: (
 ex X being AntiChain_of_Prefixes of fT st width fT = card X & for Y being
  AntiChain_of_Prefixes of fT holds card Y <= card X)& { {} } is
  AntiChain_of_Prefixes of fT by Def16,Th73;
A2: card { {} } <= width fT by A1;
  thus thesis by A2,CARD_1:50;
end;

theorem
  height elementary_tree 0 = 0
proof
A1: now
    thus ex p st p in elementary_tree 0 & len p = 0
    proof
      take <*> NAT;
      thus thesis by Th56,TARSKI:def 1;
    end;
    let p;
    assume
A2: p in elementary_tree 0;
A3: p = {} by A2,Th56,TARSKI:def 1;
    thus len p <= 0 by A3;
  end;
  thus thesis by A1,Def15;
end;

theorem
  height fT = 0 implies fT = elementary_tree 0
proof
  assume
A1: height fT = 0;
  thus fT c= elementary_tree 0
  proof
    let x;
    assume
A2: x in fT;
    reconsider t = x as Element of fT by A2;
A3: len t = 0 by A1,Def15;
A4: x = {} by A3;
    thus thesis by A4,Th47;
  end;
  let x;
  assume
A5: x in elementary_tree 0;
A6: x = {} by A5,Th56,TARSKI:def 1;
  thus thesis by A6,Th47;
end;

theorem
  height elementary_tree(n+1) = 1
proof
  set T = elementary_tree(n+1);
A1: now
    thus ex p st p in T & len p = 1
    proof
      take p = <*0*>;
      thus p in T by Th55;
      thus thesis by FINSEQ_1:57;
    end;
    let p such that
A2: p in T;
A3: p in { {} } implies p = {} by TARSKI:def 1;
A4: now
      assume
A5:   p in { <*k*> : k < n+1 };
A6:   ex k st p = <*k*> & k < n+1 by A5;
      thus len p = 1 by A6,FINSEQ_1:57;
    end;
    thus len p <= 1 by A2,A3,A4,XBOOLE_0:def 3;
  end;
  thus thesis by A1,Def15;
end;

theorem
  width elementary_tree 0 = 1
proof
  set T = elementary_tree 0;
A1: now reconsider X = { {} } as AntiChain_of_Prefixes of T by Th73;
    take X;
    thus 1 = card X by CARD_1:50;
    let Y be AntiChain_of_Prefixes of T;
A2: Y c= X by Def14,Th56;
    thus card Y <= card X by A2,NAT_1:44;
  end;
  thus thesis by A1,Def16;
end;

theorem
  width elementary_tree(n+1) = n+1
proof
  set T = elementary_tree(n+1);
A1: now
A2: { <*k*> : k < n+1 } is AntiChain_of_Prefixes-like
    proof
      thus x in { <*k*> : k < n+1 } implies x is FinSequence
      proof
        assume
A3:     x in { <*k*> : k < n+1 };
A4:     ex k st x = <*k*> & k < n+1 by A3;
        thus thesis by A4;
      end;
      let p1,p2;
      assume
A5:   p1 in { <*k*> : k < n+1 } & p2 in { <*m*> : m < n+1 };
      A6:   (
 ex k st p1 = <*k*> & k < n+1)& ex m st p2 = <*m*> & m < n+1 by A5;
      thus thesis by A6,Th23;
    end;
    reconsider X = { <*k*> : k < n+1 } as AntiChain_of_Prefixes by A2;
A7: X c= T by XBOOLE_1:7;
    reconsider X as AntiChain_of_Prefixes of T by A7,Def14;
    take X;
A8: X,Seg(n+1) are_equipotent
    proof
      defpred P[set,set] means ex n st $1 = <*n*> & $2 = n+1;
A9:   x in X & P[x,y] & P[x,z] implies y = z
      proof
        assume x in X;
        given n1 being Element of NAT such that
A10:    x = <*n1*> & y = n1+1;
        given n2 being Element of NAT such that
A11:    x = <*n2*> & z = n2+1;
A12:    <*n1*>.1 = n1 by FINSEQ_1:def 8;
        thus thesis by A10,A11,A12,FINSEQ_1:def 8;
      end;
A13:  x in X implies ex y st P[x,y]
      proof
        assume
A14:    x in X;
        consider k such that
A15:    x = <*k*>
        and k < n+1 by A14;
        reconsider y = k+1 as set;
        take y;
        thus thesis by A15;
      end;
      consider f such that
A16:  dom f = X & for x st x in X holds P[x,f.x] from CLASSES1:sch 1(A13);
      take f;
      thus f is one-to-one
      proof
        let x,y;
        assume that
A17:    x in dom f & y in dom f and
A18:    f.x = f.y;
        A19:    (
 ex k1 being Element of NAT st x = <*k1*> & f.x = k1+1)& ex k2 being Element
        of NAT st y = <*k2*> & f.y = k2+1 by A16,A17;
        thus thesis by A18,A19;
      end;
      thus dom f = X by A16;
      thus rng f c= Seg(n+1)
      proof
        let x;
        assume
A20:    x in rng f;
        consider y such that
A21:    y in dom f and
A22:    x = f.y by A20,FUNCT_1:def 5;
        consider k such that
A23:    y = <*k*> and
A24:    x = k+1 by A16,A21,A22;
        consider m such that
A25:    y = <*m*> & m < n+1 by A16,A21;
A26:    <*k*>.1 = k & <*m*>.1 = m by FINSEQ_1:def 8;
A27:    0+1 <= k+1 & k+1 <= n+1 by A23,A25,A26,NAT_1:13;
        thus thesis by A24,A27,FINSEQ_1:3;
      end;
      let x;
      assume
A28:  x in Seg(n+1);
      reconsider k = x as Element of NAT by A28;
A29:  1 <= k by A28,FINSEQ_1:3;
A30:  k <= n+1 by A28,FINSEQ_1:3;
      consider m be Nat such that
A31:  k = 1+m by A29,NAT_1:10;
      reconsider m as Element of NAT by ORDINAL1:def 13;
A32:  m < n+1 by A30,A31,NAT_1:13;
A33:  <*m*> in X by A32;
A34:  P[<*m*>,f.<*m*>] by A16,A33;
A35:  x = f.<*m*> by A9,A31,A33,A34;
      thus thesis by A16,A33,A35,FUNCT_1:def 5;
    end;
A36: card Seg(n+1) = card X by A8,CARD_1:21;
    thus n+1 = card X by A36,FINSEQ_1:78;
    let Y be AntiChain_of_Prefixes of T;
A37: Y c= T by Def14;
A38: {} in Y implies Y = { {} }
    proof
      assume that
A39:  {} in Y and
A40:  Y <> { {} };
      consider x such that
A41:  not (x in Y iff x in { {} }) by A40,TARSKI:2;
A42:  {} <> x by A39,A41,TARSKI:def 1;
      reconsider x as FinSequence of NAT by A37,A39,A41,Th44,TARSKI:def 1;
A43:  {} is_a_prefix_of x by XBOOLE_1:2;
A44:  {},x are_c=-comparable by A43,XBOOLE_0:def 9;
      thus contradiction by A39,A41,A42,A44,Def13,TARSKI:def 1;
    end;
A45: card { {} } = 1 & 1 <= 1+n by CARD_1:50,NAT_1:11;
A46: now
      assume
A47:  not {} in Y;
A48:  Y c= X
      proof
        let x;
        assume
A49:    x in Y;
A50:    x in { <*k*> : k < n+1 } or x in { {} } by A37,A49,XBOOLE_0:def 3;
        thus thesis by A47,A49,A50,TARSKI:def 1;
      end;
      thus card Y <= card X by A48,NAT_1:44;
    end;
    thus card Y <= card X by A36,A38,A45,A46,FINSEQ_1:78;
  end;
  thus thesis by A1,Def16;
end;

theorem
  for t being Element of fT holds height(fT|t) <= height fT
proof
  let t be Element of fT;
  consider p such that
A1: p in fT|t and
A2: len p = height(fT|t) by Def15;
A3: t^p in fT by A1,Def9;
A4: len(t^p) <= height fT by A3,Def15;
  A5: len(t^p) = len t + len p & len p <= len p + len t by FINSEQ_1:35,NAT_1:11
  ;
  thus thesis by A2,A4,A5,XXREAL_0:2;
end;

theorem Th85:
  for t being Element of fT st t <> {} holds height(fT|t) < height fT
proof
  let t be Element of fT;
  assume
A1: t <> {};
A2: 0+1 <= len t by A1,NAT_1:13;
  consider p such that
A3: p in fT|t and
A4: len p = height(fT|t) by Def15;
A5: t^p in fT by A3,Def9;
A6: len(t^p) <= height fT by A5,Def15;
  A7: len(t^p) = len t + len p & len p + 1 <= len t + len p by A2,FINSEQ_1:35
  ,XREAL_1:9;
A8: height(fT|t)+1 <= height fT by A4,A6,A7,XXREAL_0:2;
  thus thesis by A8,NAT_1:13;
end;

scheme TreeInd { P[Tree] }: for fT holds P[fT]
provided
A1: for fT st for n st <*n*> in fT holds P[fT|<*n*>] holds P[fT]
proof
  defpred X[set] means for fT holds height fT = $1 implies P[fT];
A2: for n being Nat st for k being Nat st k < n holds X[k] holds X[n]
  proof
    let n be Nat such that
A3: for k being Nat st k < n for fT st height fT = k holds P[fT];
    let fT such that
A4: height fT = n;
A5: now
      let k;
      assume
A6:   <*k*> in fT;
      reconsider k' = <*k*> as Element of fT by A6;
A7:   height(fT|k') < height fT by Th85;
      thus P[fT|<*k*>] by A3,A4,A7;
    end;
    thus thesis by A1,A5;
  end;
A8: for n be Nat holds X[n] from NAT_1:sch 4(A2);
  let fT;
A9: height fT = height fT;
  thus thesis by A8,A9;
end;

begin :: Addenda

:: from MODAL_1, 2007.03.14, A.T.

reserve s,t for FinSequence;

theorem
  w^t is_a_proper_prefix_of w^s implies t is_a_proper_prefix_of s
proof
  assume
A1: w^t is_a_proper_prefix_of w^s;
A2: w^t is_a_prefix_of w^s by A1,XBOOLE_0:def 8;
  consider a be FinSequence such that
A3: w^s = w^t^a by A2,Th8;
A4: w^t^a = w^(t^a) by FINSEQ_1:45;
A5: s= t^a by A3,A4,FINSEQ_1:46;
A6: t is_a_prefix_of s by A5,Th8;
  thus thesis by A1,A6,XBOOLE_0:def 8;
end;

theorem
  n <> m implies not <*n*> is_a_prefix_of <*m*>^s
proof
  assume
A1: n <> m;
  assume
A2: <*n*> is_a_prefix_of <*m*>^s;
A3: ex a be FinSequence st <*m*>^s = <*n*>^a by A2,Th8;
A4: m = (<*m*>^s).1 by FINSEQ_1:58
    .= n by A3,FINSEQ_1:58;
  thus contradiction by A1,A4;
end;

theorem
  elementary_tree 1 = {{},<*0*>}
proof
A1: now
    let x;
    thus x in {{},<*0*>} implies x in { <*n*> : n < 1 } or x in {{}}
    proof
      assume
A2:   x in {{},<*0*>};
A3:   x = {} or x = <*0*> by A2,TARSKI:def 2;
      thus thesis by A3,TARSKI:def 1;
    end;
    assume
A4: x in { <*n*> : n < 1 } or x in {{}};
A5: now per cases by A4;
      suppose
A6:     x in { <*n*> : n < 1 };
        consider n such that
A7:     x = <*n*> and
A8:     n < 1 by A6;
A9:     n = 0 by A8,NAT_1:26;
        thus x in {{},<*0*>} by A7,A9,TARSKI:def 2;
      end;
      suppose
A10:    x in {{}};
A11:    x = {} by A10,TARSKI:def 1;
        thus x in {{},<*0*>} by A11,TARSKI:def 2;
      end;
    end;
    thus x in {{},<*0*>} by A5;
  end;
  thus thesis by A1,XBOOLE_0:def 3;
end;

theorem
  not <*n*> is_a_proper_prefix_of <*m*>
proof
  assume
A1: not thesis;
A2: <*n*> is_a_prefix_of <*m*> by A1,XBOOLE_0:def 8;
  thus contradiction by A1,A2,Th16;
end;

theorem
  elementary_tree 2 = {{},<*0*>,<*1*>}
proof
A1: now
    let x;
    thus
    x in {{},<*0*>,<*1*>} implies x in { <*n*> : n < 2 } or x in {{}}
    proof
      assume
A2:   x in {{},<*0*>,<*1*>};
A3:   x = {} or x = <*0*> or x = <*1*> by A2,ENUMSET1:def 1;
      thus thesis by A3,TARSKI:def 1;
    end;
    assume
A4: x in { <*n*> : n < 2 } or x in {{}};
A5: now per cases by A4;
      suppose
A6:     x in { <*n*> : n < 2 };
        consider n such that
A7:     x = <*n*> and
A8:     n < 2 by A6;
A9:     n = 0 or n = 1 by A8,NAT_1:27;
        thus x in {{},<*0*>,<*1*>} by A7,A9,ENUMSET1:def 1;
      end;
      suppose
A10:    x in {{}};
A11:    x = {} by A10,TARSKI:def 1;
        thus x in {{},<*0*>,<*1*>} by A11,ENUMSET1:def 1;
      end;
    end;
    thus x in {{},<*0*>,<*1*>} by A5;
  end;
  thus thesis by A1,XBOOLE_0:def 3;
end;

:: from BINTREE1

theorem
  for T being Tree, t being Element of T holds
  t in Leaves T iff not t^<*0*> in T
proof
  let T be Tree, t be Element of T;
  hereby
    assume
A1: t in Leaves T;
A2: t is_a_proper_prefix_of t^<*0*> by Th30;
    thus not t^<*0*> in T by A1,A2,Def8;
  end;
  assume that
A3: not t^<*0*> in T and
A4: not t in Leaves T;
  consider q being FinSequence of NAT such that
A5: q in T and
A6: t is_a_proper_prefix_of q by A4,Def8;
A7: t is_a_prefix_of q by A6,XBOOLE_0:def 8;
  consider r being FinSequence such that
A8: q = t^r by A7,Th8;
  reconsider r as FinSequence of NAT by A8,FINSEQ_1:50;
A9: len q = len t+len r by A8,FINSEQ_1:35;
A10: len r <> 0 by A6,A9,Th24;
A11: r <> {} by A10;
  consider p being FinSequence of NAT, x being Nat such that
A12: r = <*x*>^p by A11,FINSEQ_2:150;
  reconsider x as Element of NAT by ORDINAL1:def 13;
A13: q = t^<*x*>^p by A8,A12,FINSEQ_1:45;
A14: t^<*x*> in T by A5,A13,Th46;
  thus contradiction by A3,A14,Def5;
end;

theorem
  for T being Tree, t being Element of T holds
  t in Leaves T iff not ex n being Element of NAT st t^<*n*> in T
proof
  let T be Tree, t be Element of T;
  hereby
    assume
A1: t in Leaves T;
    given n being Element of NAT such that
A2: t^<*n*> in T;
A3: not t is_a_proper_prefix_of t^<*n*> by A1,A2,Def8;
A4: t is_a_prefix_of t^<*n*> by Th8;
A5: t = t^<*n*> by A3,A4,XBOOLE_0:def 8;
A6: t = t^{} by FINSEQ_1:47;
    thus contradiction by A5,A6,FINSEQ_1:46;
  end;
  assume that
A7: not(ex n being Element of NAT st t^<*n*> in T) and
A8: not t in Leaves T;
  consider q being FinSequence of NAT such that
A9: q in T and
A10: t is_a_proper_prefix_of q by A8,Def8;
A11: t is_a_prefix_of q by A10,XBOOLE_0:def 8;
  consider r being FinSequence such that
A12: q = t^r by A11,Th8;
  reconsider r as FinSequence of NAT by A12,FINSEQ_1:50;
A13: len q = len t+len r by A12,FINSEQ_1:35;
A14: len r <> 0 by A10,A13,Th24;
A15: r <> {} by A14;
  consider p being FinSequence of NAT, x being Nat such that
A16: r = <*x*>^p by A15,FINSEQ_2:150;
  reconsider x as Element of NAT by ORDINAL1:def 13;
A17: q = t^<*x*>^p by A12,A16,FINSEQ_1:45;
  thus contradiction by A7,A9,A17,Th46;
end;

