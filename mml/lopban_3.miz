:: The Series on {B}anach Algebra
::  by Yasunari Shidama
::
:: Received February 3, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, NORMSP_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, VECTSP_1,
      ARYTM_3, RELAT_1, ABSVALUE, ORDINAL2, SEQ_1, PROB_1, SEQ_2, SEQM_3,
      CAT_1, PREPOWER, POWER, SERIES_1, BHSP_3, RSSPACE3, LOPBAN_1, LOPBAN_2,
      LOPBAN_3, SUPINF_2, GROUP_1, FUNCOP_1, FRECHET2;
 notations TARSKI, SUBSET_1, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2, PRE_TOPC,
      DOMAIN_1, BINOP_1, FUNCOP_1,
      STRUCT_0, XREAL_0, ORDINAL1, NUMBERS, REAL_1, NAT_1,
      COMPLEX1, RLVECT_1, GROUP_1, VECTSP_1, NORMSP_1, RSSPACE3, SEQ_1, SEQ_2,
      SEQM_3, SERIES_1, PREPOWER, POWER, BHSP_3, LOPBAN_1, LOPBAN_2;
 constructors REAL_1, NAT_1, FUNCT_7, DOMAIN_1, SEQ_2, SERIES_1, SEQ_4,
      PREPOWER, RSSPACE3, LOPBAN_2, COMPLEX1, PARTFUN1, FUNCOP_1, BHSP_3;
 registrations SUBSET_1, RELSET_1, STRUCT_0, ORDINAL2, VECTSP_1, NORMSP_1,
      XREAL_0, MEMBERED, SEQ_1, SEQM_3, LOPBAN_2, FUNCOP_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions SEQ_2, SERIES_1, NORMSP_1, BHSP_3;
 theorems AXIOMS, ABSVALUE, REAL_1, RLVECT_1, VECTSP_1, BINOP_1, FUNCSDOM,
      RELAT_1, SEQ_1, SEQ_2, SEQM_3, SERIES_1, FUNCT_1, NAT_1, INT_1, SQUARE_1,
      FUNCT_2, NORMSP_1, SEQ_4, RSSPACE3, LOPBAN_1, PREPOWER, LOPBAN_2,
      GROUP_1, FUNCOP_1, BHSP_3;
 schemes SEQ_1, RECDEF_1, NAT_1, FUNCT_2;

begin :: Basic Properties of Sequences of Norm Space

definition
  let X be non empty NORMSTR;
  let seq be sequence of X;
   func Partial_Sums seq -> sequence of X means :Def1:
  it.0 = seq.0 & for n be Nat
  holds it.(n+1) = it.n + seq.(n+1);
existence
proof
      deffunc G(Nat,set)= (the add of X).($2,seq.($1+1));
      consider g be Function such that
      A1:   dom g = NAT & g.0 = seq.0
      & for n being Element of NAT holds g.(n+1) = G(n,g.n)
      from RECDEF_1:sch 3;
      defpred P[Nat] means g.$1 in the carrier of X;
       A2: P[0] by A1;
       A3:  for k be Nat st P[k] holds P[k+1]
       proof
       let k be Nat such that
         A4: P[k];
         reconsider gk=g.k as Element of the carrier of X by A4;
          g.(k+1)=(the add of X).(gk,seq.(k+1)) by A1;
         hence thesis;
       end;
         for n be Nat holds P[n] from NAT_1:sch 1(A2,A3);
       then for n be set st n in NAT
          holds g.n in the carrier of X;
         then reconsider g0=g as sequence of X by A1,FUNCT_2:5;
           take g0;
          now let n be Nat;
          thus g0.(n+1) = (the add of X).(g0.n,seq.(n+1)) by A1
            .=g0.n+seq.(n+1) by RLVECT_1:5;
        end;
        hence thesis by A1;
end;
uniqueness
proof
 let seq1,seq2 be sequence of X;
 assume that A5: seq1.0=seq.0 & for n be Nat holds
                seq1.(n+1)=seq1.n + seq.(n+1) and
             A6: seq2.0=seq.0 & for n be Nat holds
                seq2.(n+1)=seq2.n + seq.(n+1);
   defpred P[Nat] means seq1.$1 = seq2.$1;
 A7: P[0] by A5,A6;
 A8: for k be Nat st P[k] holds P[k+1]
     proof let k be Nat;
      assume P[k];
      hence seq1.(k+1) = seq2.k + seq.(k+1) by A5
                      .= seq2.(k+1) by A6;
      end;
    for n be Nat holds P[n] from NAT_1:sch 1(A7,A8);
 hence seq1 = seq2 by FUNCT_2:113;
end;
end;

theorem Th1:
for X be add-associative right_zeroed right_complementable (non empty NORMSTR)
 for seq be sequence of X st
   ( for n be Nat holds seq.n = 0.X)
     holds for m be Nat holds (Partial_Sums seq).m = 0.X
 proof
let X be add-associative right_zeroed right_complementable (non empty NORMSTR);
   let seq be sequence of X such that
    A1:   for n be Nat holds seq.n = 0.X;
    let m be Nat;
    defpred P[Nat] means seq.$1 = (Partial_Sums seq).$1;
A2: P[0] by Def1;
A3: for k be Nat st P[k] holds P[k+1]
    proof let k be Nat such that
A4:   P[k];
      thus seq.(k+1) = 0.X + seq.(k+1) by RLVECT_1:10
                    .= (Partial_Sums seq).k + seq.(k+1) by A1,A4
                    .= (Partial_Sums seq).(k+1) by Def1;
    end;
       for n be Nat holds P[n] from NAT_1:sch 1(A2,A3);
    then seq = Partial_Sums seq by FUNCT_2:113;
    hence (Partial_Sums seq).m = 0.X by A1;
  end;

definition
let X be RealNormSpace;
let seq be sequence of X;
 attr seq is summable means :Def2:
  Partial_Sums seq is convergent;
end;

registration
let X be RealNormSpace;
cluster summable sequence of X;
existence
  proof
    reconsider C = NAT --> 0.X as sequence of X by FUNCOP_1:57;
A1:   for n be Nat holds C.n = 0.X by FUNCOP_1:13;
    take C, 0.X;
    let p be Real such that
A2:   0<p;
    take 0;
    let m be Nat such that 0<=m;
      ||.Partial_Sums C.m-0.X.|| = ||.0.X-0.X.|| by A1,Th1
    .=0 by NORMSP_1:10;
    hence ||.Partial_Sums C.m-0.X.||<p by A2;
  end;
end;

definition
  let X be RealNormSpace;
  let seq be sequence of X;
 func Sum seq -> Element of X equals :Def3:
  lim Partial_Sums seq;
correctness;
end;

definition
  let X be RealNormSpace;
  let seq be sequence of X;
attr seq is norm_summable means :Def4:
 ||.seq.|| is summable;
end;

theorem Th2:
for X be RealNormSpace
  for seq be sequence of X
      for m be Nat holds  0 <= ||.seq.||.m
  proof
  let X be RealNormSpace;
  let seq be sequence of X;
  let m be Nat;
      ||.seq.||.m = ||.seq.m.|| by NORMSP_1:def 10;
  hence thesis by NORMSP_1:8;
  end;

theorem Th3:
for X be RealNormSpace
  for x,y,z be Element of X
     holds ||.x-y.|| = ||.(x-z)+(z-y).||
 proof
    let X be RealNormSpace;
    let x,y,z be Element of X;
 thus ||.x-y.|| = ||.x-0.X-y.|| by RLVECT_1:26
     .= ||.x-(z+-z)-y.|| by RLVECT_1:16
     .= ||.x-(z-z)-y.|| by RLVECT_1:def 11
     .= ||.(x-z)+z-y.|| by RLVECT_1:43
     .= ||.(x-z)+(z-y).|| by RLVECT_1:42;
end;

theorem Th4:
for X be RealNormSpace
  for seq be sequence of X holds
        seq is convergent implies
         for s be Real st 0<s ex n be Nat
         st for m be Nat st n<=m holds ||.seq.m -seq.n.||<s
    proof
    let X be RealNormSpace;
    let seq be sequence of X;
     assume seq is convergent;
     then consider g1 be Element of X such that
  A1: for s be Real st 0<s
     ex n be Nat st for m be Nat st n<=m holds ||.seq.m -g1.||<s
     by NORMSP_1:def 9;
     let s be Real; assume 0<s;
     then 0<s/2 by SEQ_2:3;
     then consider n be Nat such that
  A2: for m be Nat st n<=m holds ||.seq.m -g1.||<s/2 by A1;
        now
        let m be Nat such that A3: n<=m;
           ||.seq.n -g1.||<s/2 by A2;
     then A4: ||.g1-seq.n.||<s/2 by NORMSP_1:11;
        ||.seq.m -g1.||<s/2 by A2,A3;
     then A5: ||.seq.m -g1.||+||.g1-seq.n.||<s/2+s/2 by A4,REAL_1:67;
  A6: ||.seq.m -g1+(g1-seq.n).||<=||.seq.m -g1.||+||.g1-seq.n.||
            by NORMSP_1:def 2;
         ||.seq.m -g1+(g1-seq.n).|| = ||.seq.m -seq.n.|| by Th3;
        hence ||.seq.m -seq.n.|| < s by A5,A6,AXIOMS:22;
   end;
   hence thesis;
   end;

theorem Th5:
for X be RealNormSpace
  for seq be sequence of X holds
        seq is Cauchy_sequence_by_Norm
        iff
       for p be Real st p > 0 holds
           ex n be Nat st for m be Nat st n <= m
            holds ||.seq.m-seq.n.||< p
    proof
    let X be RealNormSpace;
    let seq be sequence of X;
   A1: now assume
      A2:      seq is Cauchy_sequence_by_Norm;
      thus for p be Real st p > 0 holds
           ex n be Nat st for m be Nat st n <= m
            holds ||.seq.m-seq.n.||< p
        proof
        let p be Real such that
           A3: p >0;
           consider n be Nat such that
           A4: for m,k be Nat st n <= m & n <=k
            holds ||.seq.m-seq.k.||< p by A2,A3,RSSPACE3:10;
             for m be Nat st n <= m holds ||.seq.m-seq.n.||<p by A4;
         hence thesis;
      end;
     end;
    now assume
   A5:     for p be Real st p > 0 holds
               ex n be Nat st for m be Nat st n <= m
             holds ||.seq.m-seq.n.||< p;
      now let s be Real;
      assume 0<s;
     then 0<s/2 by SEQ_2:3;
          then consider k be Nat such that
  A6: for m be Nat st k<=m holds ||.seq.m -seq.k.||<s/2 by A5;
        now
         let m,n be Nat such
          that A7: k<=m & k<=n;
          A8:   ||.seq.m -seq.k.||<s/2 by A6,A7;
             ||.seq.n -seq.k.||<s/2 by A6,A7;
          then ||.seq.k-seq.n.||<s/2 by NORMSP_1:11;
          then A9: ||.seq.m -seq.k.||+||.seq.k-seq.n.||<s/2+s/2
           by A8,REAL_1:67;
          A10: ||.seq.m -seq.k+(seq.k-seq.n).||
          <=||.seq.m -seq.k.||+||.seq.k-seq.n.|| by NORMSP_1:def 2;
               ||.seq.m -seq.k+(seq.k-seq.n).||
              =||.seq.m -seq.n.|| by Th3;
          hence ||.seq.m -seq.n.|| < s  by A9,A10,AXIOMS:22;
       end;
       hence ex k be Nat st
        for m,n be Nat st k <= m & k <=n holds ||.seq.m-seq.n.||< s;
           end;
       hence seq is Cauchy_sequence_by_Norm by RSSPACE3:10;
    end;
hence thesis by A1;
end;

theorem Th6:
for X be RealNormSpace
  for seq be sequence of X
  st (for n be Nat holds seq.n = 0.X)
    holds for m be Nat holds (Partial_Sums ||.seq.||).m = 0
  proof
  let X be RealNormSpace;
  let seq be sequence of X such that
A1:   for n be Nat holds seq.n = 0.X;
    let m be Nat;
    defpred P[Nat] means ||.seq.||.$1 = (Partial_Sums ||.seq.||).$1;
A2: P[0] by SERIES_1:def 1;
A3: for k be Nat st P[k] holds P[k+1]
    proof let k be Nat such that
A4:   P[k];
      thus ||.seq.||.(k+1) =0 +  ||.seq.||.(k+1)
                               .=  ||.0.X.|| + ||.seq.||.(k+1) by NORMSP_1:5
                        .= ||.seq.k.|| + ||.seq.||.(k+1) by A1
                        .= (Partial_Sums ||.seq.||).k + ||.seq.||.(k+1) by A4,
NORMSP_1:def 10
                        .= (Partial_Sums ||.seq.||).(k+1) by SERIES_1:def 1;
    end;
       for n be Nat  holds  P[n] from NAT_1:sch 1(A2,A3);
    hence (Partial_Sums ||.seq.||).m = ||.seq.||.m
                                  .= ||.seq.m.|| by NORMSP_1:def 10
                                  .= ||.0.X.|| by A1
                                  .= 0 by NORMSP_1:5;
  end;

definition
let X be RealNormSpace;
let seq be sequence of X;
  redefine attr seq is constant means   :Def5:
  ex r be Element of X st for n be Nat holds seq.n=r;
 compatibility
  proof
A1:dom seq = NAT by FUNCT_2:def 1;
   hereby assume
A2:   seq is constant;
    consider n0 being Nat;
    reconsider r = seq.n0 as Element of X;
    take r;
    let n be Nat;
    thus seq.n = r by A1,A2,SEQM_3:def 5;
   end;
   given r be Element of X such that
A3:  for n be Nat holds seq.n=r;
 for n1,n2 being set st n1 in dom seq & n2 in dom seq
   holds seq.n1=seq.n2
   proof
     let n1,n2 be set;
     assume
A4:  n1 in dom seq & n2 in dom seq;
      hence seq.n1 = r by A1,A3 .= seq.n2 by A1,A3,A4;
  end;
  hence seq is constant by SEQM_3:def 5;
 end;
end;

definition
let X be RealNormSpace;
let seq be sequence of X;
let k be Nat;
func seq ^\k -> sequence of X means :Def6:
  for n be Nat holds it.n=seq.(n+k);
existence
 proof deffunc U(Nat) = seq.($1+k);
  thus ex f being sequence of X st
   for n be Nat holds f.n=U(n) from FUNCT_2:sch 4;
 end;
uniqueness
 proof
  let seq1,seq2 be sequence of X;
  assume that A1:for n be Nat holds seq1.n=seq.(n+k) and
              A2:for n be Nat holds seq2.n=seq.(n+k);
      now let n be Nat;
       seq1.n=seq.(n+k) & seq2.n=seq.(n+k) by A1,A2;
    hence seq1.n=seq2.n;
   end;
  hence seq1=seq2 by FUNCT_2:113;
 end;
end;

definition
let X be non empty 1-sorted;
let Nseq be increasing Seq_of_Nat;
let seq be sequence of X;
redefine func seq*Nseq -> Function of NAT, the carrier of X;
 coherence
  proof
     A1:dom Nseq=NAT by FUNCT_2:def 1;
      rng Nseq c= NAT by SEQM_3:def 8;
     then rng Nseq c= dom seq by FUNCT_2:def 1;
     then dom (seq*Nseq)=NAT by A1,RELAT_1:46;
     hence thesis by FUNCT_2:def 1;
end;
end;

definition
let X be non empty 1-sorted;
let Nseq be increasing Seq_of_Nat;
let seq be sequence of X;
redefine func seq*Nseq -> sequence of X;
 coherence
proof
thus seq*Nseq is Function of NAT, the carrier of X;
end;
end;

definition
 canceled;
end;

theorem Th7:
for X be non empty 1-sorted
  for seq be sequence of X
   for Nseq be increasing Seq_of_Nat holds
      for n be Nat holds (seq*Nseq).n=seq.(Nseq.n)
  proof
 let X be non empty 1-sorted;
 let seq be sequence of X;
 let Nseq be increasing Seq_of_Nat;
 let n be Nat;
     dom (seq*Nseq)=NAT by FUNCT_2:def 1;
    hence (seq*Nseq).n=seq.(Nseq.n) by FUNCT_1:22;
  end;

theorem Th8:
for X be RealNormSpace
  for seq be sequence of X holds
   seq ^\0 =seq
  proof
  let X be RealNormSpace;
  let seq be sequence of X;
     now let n be Nat;
    thus (seq ^\0).n=seq.(n+0) by Def6
                  .=seq.n;
   end;
  hence thesis by FUNCT_2:113;
 end;

theorem
 for X be RealNormSpace
  for seq be sequence of X
    for k, m be Nat holds
   (seq ^\k)^\m=(seq ^\m)^\k
  proof
     let X be RealNormSpace;
     let seq be sequence of X;
     let k,m be Nat;
       now let n be Nat;
     thus ((seq ^\k)^\m).n=(seq ^\k).(n+m) by Def6
      .=seq.(n+m+k) by Def6
      .=seq.(n+k+m)
      .=(seq ^\m).(n+k) by Def6
      .=((seq ^\m)^\k).n by Def6;
    end;
   hence thesis by FUNCT_2:113;
  end;

theorem Th10:
for X be RealNormSpace
  for seq be sequence of X
   for k, m be Nat holds
   (seq ^\k)^\m=seq ^\(k+m)
  proof
     let X be RealNormSpace;
     let seq be sequence of X;
     let k,m be Nat;
       now let n be Nat;
     thus ((seq ^\k)^\m).n=(seq ^\k).(n+m) by Def6
      .=seq.(n+m+k) by Def6
      .=seq.(n+(k+m))
      .=(seq ^\(k+m)).n by Def6;
    end;
   hence thesis by FUNCT_2:113;
  end;

theorem Th11:
for X be RealNormSpace
  for seq,seq1 be sequence of X
  holds
    seq1 is subsequence of seq & seq is convergent implies
        seq1 is convergent
  proof
  let X be RealNormSpace;
  let seq,seq1 be sequence of X;
   assume that A1:seq1 is subsequence of seq and
               A2:seq is convergent;
   consider g1 be Element of X such that
   A3:for p be Real st 0<p ex n be Nat st for m be Nat st n<=m holds
     ||.(seq.m)-g1.||< p by A2,NORMSP_1:def 9;
   consider Nseq be increasing Seq_of_Nat such that
A4:seq1=seq*Nseq by A1,BHSP_3:def 4;
   take g=g1;
   let p be Real; assume 0<p;
   then consider n1 be Nat such that
     A5:for m be Nat st n1<=m holds ||.(seq.m)-g1.||<p by A3;
   take n=n1;
   let m be Nat such that A6:n<=m;
      m<=Nseq.m by SEQM_3:33;
   then A7:n<=Nseq.m by A6,AXIOMS:22;
      seq1.m=seq.(Nseq.m) by A4,Th7;
   hence ||.(seq1.m)-g.||<p by A5,A7;
  end;

theorem Th12:
for X be RealNormSpace
  for seq,seq1 be sequence of X holds
  seq1 is subsequence of seq & seq is convergent implies
        lim seq1=lim seq
  proof
  let X be RealNormSpace;
  let seq,seq1 be sequence of X;
   assume that A1:seq1 is subsequence of seq and
               A2:seq is convergent;
   A3:seq1 is convergent by A1,A2,Th11;
   consider Nseq be increasing Seq_of_Nat such that
A4:  seq1=seq*Nseq by A1,BHSP_3:def 4;
       now let p be Real; assume 0<p;
     then consider n1 be Nat such that
      A5:for m be Nat st n1<=m
      holds ||.(seq.m)-(lim seq).||<p by A2,NORMSP_1:def 11;
     take n=n1;
     let m be Nat such
     that A6:n<=m;
        m<=Nseq.m by SEQM_3:33;
     then A7:n<=Nseq.m by A6,AXIOMS:22;
        seq1.m=seq.(Nseq.m) by A4,Th7;
     hence ||.(seq1.m)-(lim seq).||<p by A5,A7;
    end;
   hence thesis by A3,NORMSP_1:def 11;
  end;

Lm1:
for n,m be Nat holds
n<m implies ex k be Nat st m=n+1+k
 proof
 let n,m be Nat;
  assume A1: n<m;
  then consider k1 be Nat such that A2:m=n+k1 by NAT_1:28;
   k1 <> 0 by A1,A2;
  then consider n1 be Nat such that A3:k1=n1+1 by NAT_1:22;
  take k=n1;
  thus m=n+1+k by A2,A3;
 end;

Lm2:
  for seq be Real_Sequence holds
     ((for n be Nat holds seq.n<seq.(n+1)) implies
              for n,k be Nat holds seq.n<seq.(n+1+k)) &
      ((for n,k be Nat holds seq.n<seq.(n+1+k)) implies
              for n,m be Nat st n<m holds seq.n<seq.m) &
      ((for n,m be Nat st n<m holds seq.n<seq.m) implies
              for n be Nat holds seq.n<seq.(n+1))
 proof
  let seq be Real_Sequence;
  thus (for n be Nat holds seq.n<seq.(n+1)) implies
                    for n,k be Nat holds seq.n<seq.(n+1+k)
   proof
    assume A1:for n be Nat holds seq.n<seq.(n+1);
    let n be Nat;
     defpred X[Nat] means seq.n<seq.(n+1+$1);
    A2: X[0] by A1;
     A3:now let k be Nat such that A4: X[k];
         seq.(n+1+k)<seq.(n+1+k+1) by A1;
      hence X[k+1] by A4,AXIOMS:22;
     end;
    thus for k be Nat holds X[k] from NAT_1:sch 1(A2,A3);
   end;
  thus (for n,k be Nat holds seq.n<seq.(n+1+k)) implies
                     for n,m be Nat st n<m holds seq.n<seq.m
   proof
    assume A5:for n,k be Nat holds seq.n<seq.(n+1+k);
    let n,m be Nat; assume n<m;
    then ex k be Nat st m=n+1+k by Lm1;
    hence seq.n<seq.m by A5;
   end;
  assume A6:for n,m be Nat st n<m holds seq.n<seq.m;
  let n be Nat;
     n<n+1 by NAT_1:38;
  hence seq.n<seq.(n+1) by A6;
 end;

Lm3:
 for f be Real_Sequence
   holds
( f is increasing iff (for n being Nat holds f.n < f.(n+1)))
  proof
   let f be Real_Sequence;
    thus f is increasing implies for n being Nat holds f.n < f.(n+1)
    proof
      assume A1: f is increasing;
      let n be Nat;
A2:   dom f = NAT by FUNCT_2:def 1;
         n < n+1 by NAT_1:38;
      hence thesis by A1,A2,SEQM_3:def 1;
    end;
    assume A3:for n being Nat holds f.n < f.(n+1);
  for m,n be Nat st m in dom f & n in dom f & m < n holds f.m < f.n
        proof
        let m,nn be Nat such that
        A4:   m in dom f & nn in dom f & m < nn;
        consider k be Nat such that
        A5: nn = m+1+k by A4,Lm1;
    thus thesis by A3,A5,Lm2;
    end;
 hence thesis by SEQM_3:def 1;
  end;

theorem Th13:
for X be RealNormSpace
  for seq be sequence of X
   for k be Nat holds
   seq ^\k is subsequence of seq
  proof
   let X be RealNormSpace;
   let seq be sequence of X;
   let k be Nat;
  deffunc U(Nat) = $1+k;
   consider seq1 be Real_Sequence such that
   A1:for n be Nat holds seq1.n=U(n) from SEQ_1:sch 1;
       now let n be Nat;
     A2:seq1.n=n+k & seq1.(n+1)=n+1+k by A1;
        n+k<n+k+1 by NAT_1:38;
     hence seq1.n<seq1.(n+1) by A2;
    end;
   then A3:seq1 is increasing by Lm3;
       now let n be Nat;
        n+k is Nat;
     hence seq1.n is Nat by A1;
    end;
   then reconsider seq1 as increasing Seq_of_Nat by A3,SEQM_3:29;
   take Nseq=seq1;
       now let n be Nat;
     thus (seq*Nseq).n=seq.(Nseq.n) by Th7
       .=seq.(n+k) by A1
       .=(seq ^\k).n by Def6;
    end;
   hence thesis by FUNCT_2:113;
  end;

:::::::::: Norm Space versions of SEQ_4_33 - SEQ_4_39

theorem Th14:
for X be RealNormSpace
  for seq,seq1 be sequence of X
    for k be Nat holds
  seq is convergent implies ((seq ^\k) is convergent & lim (seq ^\k)=lim seq)
  proof
   let X be RealNormSpace;
   let seq,seq1 be sequence of X;
   let k be Nat;
   assume A1:seq is convergent;
   A2:seq^\k is subsequence of seq by Th13;
   hence seq^\k is convergent by A1,Th11;
   thus lim (seq ^\k)=lim seq by A1,A2,Th12;
  end;

theorem Th15:
for X be RealNormSpace
  for seq,seq1 be sequence of X
  holds
  seq is convergent & (ex k be Nat st seq=seq1 ^\k)
     implies seq1 is convergent
  proof
  let X be RealNormSpace;
  let seq,seq1 be sequence of X;
  assume that
 A1: seq is convergent and
 A2: ex k be Nat st seq=seq1 ^\k;
     consider k be Nat such that
 A3: seq=seq1 ^\k by A2;
     consider g1 be Element of X  such that
 A4: for p be Real st 0<p ex n be Nat
       st for m be Nat st n<=m holds ||.seq.m-g1.||<p
     by A1,NORMSP_1:def 9;
     take g=g1;
     let p be Real; assume 0<p;
     then consider n1 be Nat such that
 A5: for m be Nat st n1<=m holds ||.seq.m-g1.||<p by A4;
     take n=n1+k;
     let m be Nat; assume
 A6: n<=m;
     then consider l be Nat such that
 A7: m=n1+k+l by NAT_1:28;
      m-k=n1+l+k+-k by A7;
     then consider m1 be Nat such that
 A8: m1=m-k;
 A9:  now assume not n1<=m1;
        then m1+k<n1+k by REAL_1:53;
        hence contradiction by A6,A8;
      end;
 A10:  m1+k=m by A8;
       ||.seq.m1-g1.||<p by A5,A9;
   hence ||.seq1.m-g.||<p by A3,A10,Def6;
 end;

theorem Th16:
for X be RealNormSpace
  for seq,seq1 be sequence of X
  holds
   seq is convergent & (ex k be Nat st seq=seq1 ^\k)
       implies lim seq1 =lim seq
  proof
  let X be RealNormSpace;
  let seq,seq1 be sequence of X;
     assume that
 A1: seq is convergent and
 A2: ex k be Nat st seq=seq1 ^\k;
 A3: seq1 is convergent by A1,A2,Th15;
     consider k be Nat such that
 A4: seq=seq1 ^\k by A2;
         now let p be Real; assume 0<p;
       then consider n1 be Nat such that
 A5:   for m be Nat st n1<=m holds ||.seq.m-(lim seq).||<p
 by A1,NORMSP_1:def 11;
       take n=n1+k;
       let m be Nat; assume
 A6:   n<=m;
       then consider l be Nat such that
 A7:    m=n1+k+l by NAT_1:28;
        m-k=n1+l+k+-k by A7;
       then consider m1 be Nat such that
 A8:     m1=m-k;
 A9:    now assume not n1<=m1;
         then m1+k<n1+k by REAL_1:53;
         hence contradiction by A6,A8;
        end;
 A10:   m1+k=m by A8;
         ||.seq.m1-(lim seq) .||<p by A5,A9;
      hence ||.seq1.m-(lim seq).||<p by A4,A10,Def6;
     end;
    hence thesis by A3,NORMSP_1:def 11;
  end;

theorem Th17:
for X be RealNormSpace
  for seq be sequence of X
    holds
  seq is constant implies seq is convergent
  proof
  let X be RealNormSpace;
  let seq be sequence of X;
   assume seq is constant;
   then consider r be Element of X such that
   A1:for n be Nat holds seq.n=r by Def5;
   take g=r;
   let p be Real such that A2:0<p;
   take n=0;
   let m be Nat such that n<=m;
      ||.(seq.m)-g.||=||.r-g.|| by A1
    .=||.0.X.|| by RLVECT_1:28
    .=0 by NORMSP_1:5;
   hence ||.(seq.m)-g.||<p by A2;
  end;

theorem Th18:
for X be RealNormSpace
  for seq be sequence of X
  st (for n be Nat holds seq.n = 0.X)
  holds seq is norm_summable
  proof
  let X be RealNormSpace;
  let seq be sequence of X
  such that
A1:   for n be Nat holds seq.n = 0.X;
       take 0;
    let p be real number such that
A2:   0<p;
    take 0;
    let m be Nat such that 0<=m;
       abs((Partial_Sums ||.seq.||).m-0) = abs(0-0) by A1,Th6
       .= 0 by ABSVALUE:def 1;
    hence abs((Partial_Sums ||.seq.||).m-0)<p by A2;
  end;

registration
let X be RealNormSpace;
 cluster norm_summable sequence of X;
 existence
  proof
    reconsider C = NAT --> 0.X as sequence of X by FUNCOP_1:57;
A1:   for n be Nat holds C.n = 0.X by FUNCOP_1:13;
    take C;
    thus thesis by A1,Th18;
  end;
end;

:::::::::: Norm Space versions of SERIES_1_7 - SERIES_1_16

theorem Th19:
for X be RealNormSpace
  for s be sequence of X holds
s is summable implies s is convergent & lim s = 0.X
proof
  let X be RealNormSpace;
  let s be sequence of X;
 assume s is summable;
 then A1: Partial_Sums(s) is convergent
 by Def2;
 then A2: Partial_Sums(s) ^\1 is convergent &
    lim(Partial_Sums(s) ^\1) = lim(Partial_Sums(s)) by Th14;
 then A3: lim(Partial_Sums(s) ^\1 - Partial_Sums(s))
     = lim(Partial_Sums(s)) - lim(Partial_Sums(s)) by A1,NORMSP_1:43
    .= 0.X by RLVECT_1:28;
 A4: s ^\1 = Partial_Sums(s) ^\1 - Partial_Sums(s)
    proof
     now let n be Nat;
         (Partial_Sums(s)).(n+1) = (Partial_Sums(s)).n
        + s.(n+1) by Def1;
      then (Partial_Sums(s)).(n+1) = (Partial_Sums(s)).n + (s ^\1).n
        by Def6;
      then (Partial_Sums(s) ^\1).n = (s ^\1).n+  (Partial_Sums(s)).n
        by Def6;
      then (Partial_Sums(s) ^\1).n- (Partial_Sums(s)).n
         = (s ^\1).n+ ( (Partial_Sums(s)).n- (Partial_Sums(s)).n )
                 by RLVECT_1:42;
      then (Partial_Sums(s) ^\1).n- (Partial_Sums(s)).n
         = (s ^\1).n+ ( 0.X) by RLVECT_1:28;
      hence
         (Partial_Sums(s) ^\1).n- (Partial_Sums(s)).n
         = (s ^\1).n by RLVECT_1:10;
    end;
      hence thesis by NORMSP_1:def 6;
    end;
 then s ^\1 is convergent by A1,A2,NORMSP_1:35;
 hence thesis by A3,A4,Th15,Th16;
end;

theorem Th20:
for X be RealNormSpace
  for s1,s2 be sequence of X holds
Partial_Sums(s1)+Partial_Sums(s2) = Partial_Sums(s1+s2)
proof
  let X be RealNormSpace;
  let s1,s2 be sequence of X;
  A1: (Partial_Sums(s1) + Partial_Sums(s2)).0
  = Partial_Sums(s1).0 + Partial_Sums(s2).0 by NORMSP_1:def 5
 .= s1.0 + Partial_Sums(s2).0 by Def1
 .= s1.0 + s2.0 by Def1
 .= (s1+s2).0 by NORMSP_1:def 5;
     now let n be Nat;
    thus (Partial_Sums(s1) + Partial_Sums(s2)).(n+1)
   = Partial_Sums(s1).(n+1) + Partial_Sums(s2).(n+1) by NORMSP_1:def 5
  .= Partial_Sums(s1).n + s1.(n+1) + Partial_Sums(s2).(n+1) by Def1
  .= Partial_Sums(s1).n+s1.(n+1)+(s2.(n+1)
  +Partial_Sums(s2).n) by Def1
  .= Partial_Sums(s1).n+s1.(n+1)+s2.(n+1)+Partial_Sums(s2).n
  by RLVECT_1:def 6
  .= Partial_Sums(s1).n+(s1.(n+1)+s2.(n+1))
  +Partial_Sums(s2).n by RLVECT_1:def 6
  .= Partial_Sums(s1).n+(s1+s2).(n+1)+Partial_Sums(s2).n by NORMSP_1:def 5
  .= Partial_Sums(s1).n+Partial_Sums(s2).n+(s1+s2).(n+1) by RLVECT_1:def 6
  .= (Partial_Sums(s1)+Partial_Sums(s2)).n+(s1+s2).(n+1) by NORMSP_1:def 5;
  end;
  hence thesis by A1,Def1;
end;

theorem Th21:
for X be RealNormSpace
  for s1,s2 be sequence of X holds
Partial_Sums(s1)-Partial_Sums(s2) = Partial_Sums(s1-s2)
 proof
 let X be RealNormSpace;
   let s1,s2 be sequence of X;
   A1: (Partial_Sums(s1) - Partial_Sums(s2)).0
  = Partial_Sums(s1).0 - Partial_Sums(s2).0 by NORMSP_1:def 6
 .= s1.0 - Partial_Sums(s2).0 by Def1
 .= s1.0 - s2.0 by Def1
 .= (s1-s2).0 by NORMSP_1:def 6;
     now let n be Nat; thus
      (Partial_Sums(s1) - Partial_Sums(s2)).(n+1)
   = Partial_Sums(s1).(n+1) - Partial_Sums(s2).(n+1)
     by NORMSP_1:def 6
  .= (Partial_Sums(s1).n + s1.(n+1)) - Partial_Sums(s2).(n+1) by Def1
  .= (Partial_Sums(s1).n+s1.(n+1))-(s2.(n+1)
  +Partial_Sums(s2).n) by Def1
  .= (Partial_Sums(s1).n+s1.(n+1))-s2.(n+1)-Partial_Sums(s2).n
      by RLVECT_1:41
  .= Partial_Sums(s1).n+(s1.(n+1)-s2.(n+1))-Partial_Sums(s2).n
     by RLVECT_1:42
  .= (s1-s2).(n+1)+Partial_Sums(s1).n-Partial_Sums(s2).n by NORMSP_1:def 6
  .= (s1-s2).(n+1)+(Partial_Sums(s1).n-Partial_Sums(s2).n)
      by RLVECT_1:42
  .= (Partial_Sums(s1)-Partial_Sums(s2)).n+(s1-s2).(n+1) by NORMSP_1:def 6;
  end;
  hence thesis by A1,Def1;
end;

registration
  let X be RealNormSpace;
  let seq be norm_summable sequence of X;
  cluster ||.seq.|| -> summable;
  coherence by Def4;
end;

registration
  let X be RealNormSpace;
  cluster summable -> convergent sequence of X;
coherence by Th19;
end;

theorem Th22:
for X be RealNormSpace
  for seq1,seq2 be sequence of X
  st seq1 is summable & seq2 is summable holds
     seq1+seq2 is summable & Sum(seq1+seq2) = Sum(seq1)+Sum(seq2)
proof
let X be RealNormSpace;
let seq1,seq2 be sequence of X such that
A1:    seq1 is summable and
A2:    seq2 is summable;
A3:Partial_Sums(seq1) is convergent &
   Partial_Sums(seq2) is convergent by A1,A2,Def2;
then A4:Partial_Sums(seq1)+Partial_Sums(seq2) is
convergent by NORMSP_1:34;
A5:Partial_Sums(seq1)+Partial_Sums(seq2)
   =Partial_Sums(seq1+seq2) by Th20;
      Sum(seq1+seq2)=lim(Partial_Sums(seq1+seq2)) by Def3
              .=lim(Partial_Sums(seq1)+Partial_Sums(seq2)) by Th20
              .=lim(Partial_Sums(seq1))+lim(Partial_Sums(seq2))
                 by A3,NORMSP_1:42;
    then Sum(seq1+seq2)=Sum(seq1)+lim(Partial_Sums(seq2)) by Def3
                .= Sum(seq1)+Sum(seq2) by Def3;
hence thesis by A4,A5,Def2;
end;

theorem Th23:
for X be RealNormSpace
  for seq1,seq2 be sequence of X
    st seq1 is summable & seq2 is summable holds
seq1-seq2 is summable & Sum(seq1-seq2)= Sum(seq1)-Sum(seq2)
proof
  let X be RealNormSpace;
  let seq1,seq2 be sequence of X such that
  A1:      seq1 is summable & seq2 is summable;
 A2:Partial_Sums(seq1) is convergent &
   Partial_Sums(seq2) is convergent by A1,Def2;
then A3:Partial_Sums(seq1)-Partial_Sums(seq2) is
convergent by NORMSP_1:35;
A4:Partial_Sums(seq1)-Partial_Sums(seq2)
   =Partial_Sums(seq1-seq2) by Th21;
    Sum(seq1-seq2)=lim(Partial_Sums(seq1-seq2)) by Def3
              .=lim(Partial_Sums(seq1)-Partial_Sums(seq2)) by Th21
              .=lim(Partial_Sums(seq1))-lim(Partial_Sums(seq2))
                 by A2,NORMSP_1:43;
    then Sum(seq1-seq2)=Sum(seq1)-lim(Partial_Sums(seq2)) by Def3
                .= Sum(seq1)-Sum(seq2) by Def3;
hence thesis by A3,A4,Def2;
end;

registration
  let X be RealNormSpace;
  let seq1, seq2 be summable sequence of X;
  cluster seq1 + seq2 -> summable;
coherence by Th22;
  cluster seq1 - seq2 -> summable;
coherence by Th23;
end;

theorem Th24:
for X be RealNormSpace
  for seq be sequence of X
       for z be Real holds
Partial_Sums(z * seq) = z * Partial_Sums(seq)
proof
let X be RealNormSpace;
let seq be sequence of X;
let z be Real;
defpred P[Nat] means Partial_Sums(z * seq).$1 = (z * Partial_Sums(seq)).$1;
A1:P[0]
proof
 thus Partial_Sums(z * seq).0 = (z * seq).0 by Def1
.=z * seq.0 by NORMSP_1:def 8
.=z * Partial_Sums(seq).0 by Def1
.=(z * Partial_Sums(seq)).0 by NORMSP_1:def 8;
end;
A2:now let n be Nat;
assume
A3: P[n];
thus P[n+1]
proof
thus Partial_Sums(z * seq).(n+1)
=Partial_Sums(z * seq).n + (z * seq).(n+1) by Def1
.=(z * Partial_Sums(seq).n )+ (z * seq).(n+1) by A3,NORMSP_1:def 8
.=(z * Partial_Sums(seq).n )+ (z * seq.(n+1)) by NORMSP_1:def 8
.= z * ( Partial_Sums(seq).n + seq.(n+1)) by RLVECT_1:def 9
.= z * ( Partial_Sums(seq).(n+1)) by Def1
.= (z * Partial_Sums(seq)).(n+1) by NORMSP_1:def 8;
end;
end;
     for n be Nat holds P[n] from NAT_1:sch 1(A1,A2);
hence thesis by FUNCT_2:113;
end;

theorem Th25:
  for X be RealNormSpace
  for seq be summable sequence of X
    for z be Real
       holds z *seq is summable & Sum(z *seq)= z * Sum(seq)
proof
  let X be RealNormSpace;
  let seq be summable sequence of X;
  let z be Real;
A1: Partial_Sums(seq) is convergent by Def2;
then A2:(z *Partial_Sums(seq)) is convergent by NORMSP_1:37;
A3: Partial_Sums(z *seq)=(z *Partial_Sums(seq)) by Th24;
    Sum(z *seq)=lim(Partial_Sums(z *seq)) by Def3
            .=lim((z *Partial_Sums(seq))) by Th24
            .=z * lim(Partial_Sums(seq)) by A1,NORMSP_1:45
            .=z * Sum(seq) by Def3;
hence z *seq is summable
& Sum(z *seq)= z * Sum(seq) by A2,A3,Def2;
end;

registration
  let X be RealNormSpace;
  let z be Real, seq be summable sequence of X;
  cluster z *seq -> summable;
  coherence by Th25;
end;

theorem Th26:
  for X be RealNormSpace
  for s,s1 be sequence of X
    st for n be Nat holds s1.n=s.0 holds
Partial_Sums(s^\1) = (Partial_Sums(s)^\1) - s1
proof
  let X be RealNormSpace;
  let s,s1 be sequence of X;
 assume A1: for n be Nat holds s1.n=s.0;
 A2: ((Partial_Sums(s)^\1) - s1).0 = (Partial_Sums(s)^\1).0 - s1.0
    by NORMSP_1:def 6
 .= (Partial_Sums(s)^\1).0 - s.0 by A1
 .= Partial_Sums(s).(0+1) - s.0 by Def6
 .= Partial_Sums(s).0 + s.(0+1) - s.0 by Def1
 .= s.(0+1) + s.0 - s.0 by Def1
 .= s.(0+1) + (s.0 - s.0) by RLVECT_1:42
 .= s.(0+1) + 0.X  by RLVECT_1:28
 .=s.(0+1) by RLVECT_1:10
 .= (s^\1).0 by Def6;
    now let k be Nat;
   thus ((Partial_Sums(s)^\1) - s1).(k+1)
  = (Partial_Sums(s)^\1).(k+1) - s1.(k+1) by NORMSP_1:def 6
  .= (Partial_Sums(s)^\1).(k+1) - s.0 by A1
  .= Partial_Sums(s).(k+1+1) - s.0 by Def6
  .= s.(k+1+1) + Partial_Sums(s).(k+1) - s.0 by Def1
  .= s.(k+1+1) + Partial_Sums(s).(k+1) - s1.k by A1
  .= s.(k+1+1) + (Partial_Sums(s).(k+1) - s1.k) by RLVECT_1:42
  .= s.(k+1+1) + ((Partial_Sums(s)^\1).k - s1.k) by Def6
  .= s.(k+1+1) + ((Partial_Sums(s)^\1) - s1).k by NORMSP_1:def 6
  .= ((Partial_Sums(s)^\1) - s1).k + (s^\1).(k+1) by Def6;
 end;
 hence thesis by A2,Def1;
end;

theorem Th27:
  for X be RealNormSpace
  for s be sequence of X holds
    s is summable implies for n be Nat holds s^\n is summable
proof
  let X be RealNormSpace;
  let s be sequence of X;
defpred X[Nat] means s^\$1 is summable;
 assume s is summable;
 then A1: X[0] by Th8;
 A2: for n be Nat st X[n] holds X[n+1]
 proof
   let n be Nat;
  assume s^\n is summable; then Partial_Sums(s^\n) is convergent
  by Def2;
  then A3: Partial_Sums(s^\n)^\1 is convergent by Th14;
  A4: s^\(n+1)=(s^\n)^\1 by Th10;
  reconsider s1 = NAT --> (s^\n).0 as sequence of X by FUNCOP_1:57;
A5: for k be Nat holds s1.k = (s^\n).0 by FUNCOP_1:13;
A6: s1 is convergent by Th17;
     Partial_Sums(s^\n^\1) = (Partial_Sums(s^\n)^\1) - s1 by A5,Th26;
  then Partial_Sums(s^\n^\1) is convergent by A3,A6,NORMSP_1:35;
  hence thesis by A4,Def2;
 end;
 thus for n be Nat holds X[n] from NAT_1:sch 1(A1,A2);
end;

registration
  let X be RealNormSpace;
  let seq be summable sequence of X, n be Nat;
  cluster seq^\n -> summable;
  coherence by Th27;
end;

theorem Th28:
for X be RealNormSpace
for seq be sequence of X holds
Partial_Sums ||.seq.|| is bounded_above iff seq is norm_summable
proof
  let X be RealNormSpace;
  let seq be sequence of X;
   for n be Nat holds 0 <=||.seq.||.n by Th2;
   then Partial_Sums(||.seq.||) is bounded_above
   iff ||.seq.|| is summable by SERIES_1:20;
  hence thesis by Def4;
end;

registration
  let X be RealNormSpace;
  let seq be norm_summable sequence of X;
  cluster Partial_Sums ||.seq.|| -> bounded_above;
  coherence by Th28;
end;

theorem Th29:
for X be RealBanachSpace
for seq be sequence of X holds
seq is summable iff (for p be Real st 0 <p holds ex n be Nat st
 for m be Nat st n <= m holds ||.Partial_Sums(seq).m-Partial_Sums(seq).n.||<p)
proof
  let X be RealBanachSpace;
  let seq be sequence of X;
A1:now assume seq is summable;
    then Partial_Sums(seq) is convergent by Def2;
    hence for p be Real st 0<p holds ex n be Nat st for m be Nat st n <= m
    holds ||.Partial_Sums(seq).m-Partial_Sums(seq).n.||<p by Th4;
    end;
   now assume for p be Real st 0<p
     holds ex n be Nat st for m be Nat st n <= m
    holds ||.Partial_Sums(seq).m-Partial_Sums(seq).n.||< p;
    then Partial_Sums(seq) is Cauchy_sequence_by_Norm by Th5;
    then Partial_Sums(seq) is convergent by LOPBAN_1:def 16;
    hence seq is summable by Def2;
    end;
hence thesis by A1;
end;

theorem Th30:
  for X be RealNormSpace
  for s be sequence of X
   for n,m be Nat st n<=m
holds ||.Partial_Sums(s).m - Partial_Sums(s).n.||
      <= abs(Partial_Sums(||.s.||).m - Partial_Sums(||.s.||).n)
proof
  let X be RealNormSpace;
  let s be sequence of X;
  let n,m be Nat such that
  A1: n<=m;
 set s1=Partial_Sums(s);
 set s2=Partial_Sums(||.s.||);
  defpred X[Nat] means ||.s1.(n+$1) - s1.n.|| <= abs(s2.(n+$1) - s2.n);
    ||.s1.(n+0) - s1.n.|| = ||.0.X.|| by RLVECT_1:28
.= 0 by NORMSP_1:5;
 then A2: X[0] by ABSVALUE:5;
    now let k be Nat;
    ||.s.k.|| >=0 by NORMSP_1:8;
  hence ||.s.||.k >= 0 by NORMSP_1:def 10;
 end;
 then A3: s2 is non-decreasing by SERIES_1:19;
 A4: for k be Nat st X[k] holds X[k+1]
  proof let k be Nat;
  assume A5: ||. s1.(n+k) - s1.n.|| <= abs(s2.(n+k) - s2.n);
       ||. s1.(n+(k+1)) - s1.n.|| = ||. s.(n+k+1) + s1.(n+k) - s1.n .|| by Def1
    .= ||. s.(n+k+1) + (s1.(n+k) - s1.n).|| by RLVECT_1:42;
    then A6: ||. s1.(n+(k+1))-s1.n .|| <= ||.s.(n+k+1).||+||.s1.(n+k) - s1.n.||
      by NORMSP_1:def 2;
       ||.s.(n+k+1).|| + ||. s1.(n+k) - s1.n.|| <=
    ||.s.(n+k+1).|| + abs(s2.(n+k) - s2.n) by A5,REAL_1:55;
    then A7: ||. s1.(n+(k+1))-s1.n.||
  <= ||. s.(n+k+1).||+ abs(s2.(n+k)-s2.n) by A6,AXIOMS:22;
       s2.(n+k)>=s2.n by A3,SEQM_3:11;
    then A8: s2.(n+k) - s2.n >= 0 by SQUARE_1:12;
    then A9: ||. s1.(n+(k+1))-s1.n .||
       <= ||. s.(n+k+1).||+(s2.(n+k)-s2.n) by A7,ABSVALUE:def 1;
       ||. s .(n+k+1) .|| >= 0 by NORMSP_1:8;
    then A10: ||. s .(n+k+1).|| +(s2.(n+k)-s2.n) >= 0+0 by A8,REAL_1:55;
      abs(s2.(n+(k+1)) - s2.n) = abs(s2.(n+k) + ( ||.s.||).(n+k+1) - s2.n)
                       by SERIES_1:def 1
                       .= abs(s2.(n+k) +  ||.s.(n+k+1).||-s2.n)
                       by NORMSP_1:def 10
                   .= abs(||.s.(n+k+1).|| + (s2.(n+k) - s2.n));
  hence ||.s1.(n+(k+1))-s1.n.|| <= abs(s2.(n+(k+1))-s2.n)
  by A9,A10,ABSVALUE:def 1;
  end;
  A11: for k be Nat holds X[k] from NAT_1:sch 1(A2,A4);
     reconsider k=m-n as Nat by A1,INT_1:18;
      n+k = m;
   hence thesis by A11;
  end;

theorem Th31:
  for X be RealBanachSpace
  for seq be sequence of X holds
  seq is norm_summable implies seq is summable
proof
  let X be RealBanachSpace;
  let seq be sequence of X;
  assume seq is norm_summable;
  then ||.seq.|| is summable by Def4;
  then A1:  Partial_Sums(||.seq.||) is convergent by SERIES_1:def 2;
   now let p be Real;
    assume 0<p;
    then consider n be Nat such that
    A2: for m be Nat st n <= m holds
    abs(Partial_Sums(||.seq.||).m-Partial_Sums(||.seq.||).n)< p
    by A1,SEQ_4:58;
   take n;
let m be Nat; assume A3:  n <= m;
    then A4: abs(Partial_Sums(||.seq.||).m-Partial_Sums(||.seq.||).n)< p by A2;
       ||.Partial_Sums(seq).m-Partial_Sums(seq).n.||
    <= abs(Partial_Sums(||.seq.||).m-Partial_Sums(||.seq.||).n)
    by A3,Th30;
   hence ||.Partial_Sums(seq).m-Partial_Sums(seq).n.|| <p by A4,AXIOMS:22;
   end;
hence seq is summable by Th29;
end;

theorem
  for X be RealNormSpace
   for rseq1 be Real_Sequence
     for seq2 be sequence of X holds
       rseq1 is summable &
       (ex m be Nat st for n be Nat st m<=n holds ||.seq2.n.|| <= rseq1.n )
       implies seq2 is norm_summable
proof
 let X be RealNormSpace;
 let rseq1 be Real_Sequence;
 let seq2 be sequence of X;
assume
A1:rseq1 is summable
& (ex m be Nat st for n be Nat st  m<=n holds ||.seq2.n.|| <= rseq1.n );
then consider m be Nat such that
A2:  for n be Nat st m<=n holds ||.seq2.n.|| <= rseq1.n;
A3:now let n be Nat; assume m <= n;
      then ||.seq2.n.|| <= rseq1.n by A2;
   hence ||.seq2.||.n <= rseq1.n by NORMSP_1:def 10;
end;
   now
   let n be Nat;
    ||.seq2.n.||=||.seq2.||.n by NORMSP_1:def 10;
   hence 0 <= ||.seq2.||.n by NORMSP_1:8;
end;
hence ||.seq2.|| is summable by A1,A3,SERIES_1:22;
end;

theorem
  for X be RealNormSpace
     for seq1,seq2 be sequence of X
holds
  (for n be Nat holds 0 <= ||.seq1.||.n & ||.seq1.||.n <= ||.seq2.||.n)
 & seq2 is norm_summable implies
  seq1 is norm_summable & Sum ||.seq1.|| <= Sum ||.seq2.||
proof
let X be RealNormSpace;
let seq1,seq2 be sequence of X;
assume A1:
 (for n be Nat holds 0 <= ||.seq1.||.n & ||.seq1.||.n <= ||.seq2.||.n)
& seq2 is norm_summable;
then ||.seq2.|| is summable by Def4;
hence ||.seq1.|| is summable & Sum(||.seq1.||)
<=Sum(||.seq2.||) by A1,SERIES_1:24;
end;

theorem
  for X be RealNormSpace
     for seq be sequence of X
holds
  (for n be Nat  holds ||.seq.||.n>0) &
 (ex m be Nat st for n be Nat st n >=m
 holds ||.seq.||.(n+1)/||.seq.||.n >= 1)
  implies not seq is norm_summable
proof
 let X be RealNormSpace;
 let seq be sequence of X;
assume (for n be Nat holds ||.seq.||.n>0)
  & (ex m be Nat st for n be Nat st n >=m holds ||.seq.||.(n+1)/
||.seq.||.n >= 1);
hence ||.seq.|| is not summable by SERIES_1:31;
end;

theorem
   for X be RealNormSpace
       for seq be sequence of X
        for rseq1 be Real_Sequence holds
  (for n be Nat holds rseq1.n = n-root (||.seq.||.n)) & rseq1 is convergent &
  lim rseq1 < 1
implies seq is norm_summable
proof
let X be RealNormSpace;
let seq be sequence of X;
let rseq1 be Real_Sequence;
assume
A1:(for n be Nat holds rseq1.n = n-root (||.seq.||.n)) &
rseq1 is convergent & lim rseq1 < 1;
   for n be Nat holds ||.seq.||.n >=0 by Th2;
hence ||.seq.|| is summable by A1,SERIES_1:32;
end;

theorem
   for X be RealNormSpace
  for seq be sequence of X
  for rseq1 be Real_Sequence
holds
 (for n be Nat holds rseq1.n = n-root (||.seq.||.n))
 & (ex m be Nat st
 for n be Nat st m<=n holds rseq1.n>= 1)
  implies ||.seq.|| is not summable
proof
let X be RealNormSpace;
let seq be sequence of X;
let rseq1 be Real_Sequence;
assume
A1:(for n be Nat holds rseq1.n = n-root (||.seq.||.n))
& (ex m be Nat st for n be Nat st m<=n holds rseq1.n>= 1);
   for n be Nat holds ||.seq.||.n >=0 by Th2;
hence ||.seq.|| is not summable by A1,SERIES_1:33;
end;

theorem
   for X be RealNormSpace
  for seq be sequence of X
  for rseq1 be Real_Sequence
holds
  (for n be Nat holds rseq1.n = n-root (||.seq.||.n))
 & rseq1 is convergent & lim rseq1 > 1
implies seq is not norm_summable
proof
let X be RealNormSpace;
let seq be sequence of X;
let rseq1 be Real_Sequence;
assume
A1:(for n be Nat holds rseq1.n = n-root (||.seq.||.n))
& rseq1 is convergent & lim rseq1 > 1;
   for n be Nat holds ||.seq.||.n >=0 by Th2;
hence ||.seq.|| is not summable by A1,SERIES_1:34;
end;

theorem
 for X be RealNormSpace
for seq be sequence of X
for rseq1 be Real_Sequence
   st ( ||.seq.|| is non-increasing )
            & (for n be Nat holds rseq1.n = 2
            to_power n *||.seq.||.(2 to_power n))
 holds
      ( seq is norm_summable iff rseq1 is summable)
proof
let X be RealNormSpace;
let seq be sequence of X;
let rseq1 be Real_Sequence;
assume ||.seq.|| is non-increasing & (for n be Nat holds rseq1.n
 = 2 to_power n *||.seq.||.(2 to_power n));
then for n be Nat holds ( ||.seq.|| is non-increasing & ||.seq.||.n >= 0
& rseq1.n = 2 to_power n *||.seq.||.(2 to_power n) ) by Th2;
then ||.seq.|| is summable iff rseq1 is summable by SERIES_1:35;
hence seq is norm_summable iff rseq1 is summable by Def4;
end;

theorem
 for X be RealNormSpace
for seq be sequence of X
for p be Real st
        (p>1 & (for n be Nat st n >=1 holds
        ||.seq.||.n = 1/ (n to_power p) ))
holds seq is norm_summable
proof
let X be RealNormSpace;
let seq be sequence of X;
let p be Real;
assume p>1 & (for n be Nat st n >=1 holds ||. seq .|| .n = 1/n to_power p);
then ||.seq.|| is summable by SERIES_1:36;
hence thesis by Def4;
end;

theorem
 for X be RealNormSpace
for seq be sequence of X
for p be Real holds
  p<=1 & (for n be Nat st n >=1 holds ||.seq.||.n=1/n to_power p)
 implies not seq is norm_summable
proof
let X be RealNormSpace;
let seq be sequence of X;
let p be Real;
assume p<=1 & (for n be Nat  st n  >=1 holds ||.seq.||.n=1/n to_power p);
then not ||.seq.|| is summable by SERIES_1:37;
hence thesis by Def4;
end;

theorem
 for X be RealNormSpace
for seq be sequence of X
for rseq1 be Real_Sequence
holds
  (for n be Nat holds seq.n<>0.X & rseq1.n=||.seq.||.(n+1)/||.seq.||.n) &
 rseq1 is convergent & lim rseq1 < 1 implies
  seq is norm_summable
proof
let X be RealNormSpace;
let seq be sequence of X;
let rseq1 be Real_Sequence;
 assume
A1:(for n be Nat  holds seq.n<>0.X & rseq1.n=||.seq.||.(n+1)/||.seq.||.n)
& rseq1 is convergent & lim rseq1 < 1;
   now let n be Nat;
  A2:seq.n<>0.X & rseq1.n=||.seq.||.(n+1)/||.seq.||.n by A1;
  A3:0 <=||.seq.||.n  & 0 <=||.seq.||.(n+1) by Th2;
 A4:  abs((||.seq.||)).n =abs((||.seq.||).n) by SEQ_1:16
                      .=||.seq.||.n by A3,ABSVALUE:def 1;
 A5:  abs((||.seq.||)).(n+1) =abs((||.seq.||).(n+1)) by SEQ_1:16
                      .=||.seq.||.(n+1) by A3,ABSVALUE:def 1;
  ||.seq.||.n =||.seq.n.|| by NORMSP_1:def 10;
 hence (||.seq.||).n <>0 &
 rseq1.n=abs((||.seq.||)).(n+1)/abs((||.seq.||)).n
 by A2,A4,A5,NORMSP_1:def 2;
end;
then ||.seq.|| is absolutely_summable  by A1,SERIES_1:42;
then A6:abs(||.seq.||) is summable by SERIES_1:def 5;
     now let n be Nat;
 A7:0 <=||.seq.||.n by Th2;
 thus
      abs((||.seq.||)).n =abs((||.seq.||).n) by SEQ_1:16
                      .=||.seq.||.n by A7,ABSVALUE:def 1;
end;
then abs((||.seq.||)) =||.seq.|| by FUNCT_2:113;
hence seq is norm_summable by A6,Def4;
end;

theorem
 for X be RealNormSpace
for seq be sequence of X
holds
  (for n be Nat  holds seq.n<>0.X)
 & (ex m be Nat st for n be Nat
 st n >=m holds ||.seq.||.(n+1)/||.seq.||.n >= 1)
  implies seq is not norm_summable
proof
let X be RealNormSpace;
let seq be sequence of X;
 assume
A1: (for n be Nat holds seq.n <> 0.X )
& (ex m be Nat st for n be Nat
st n  >=m holds ||.seq.||.(n+1)/||.seq.||.n >= 1);
A2:now let n be Nat;
      seq.n <> 0.X  by A1;
   then ||.seq.n.||<>0 by NORMSP_1:def 2;
   hence ||.seq.||.n <>0 by NORMSP_1:def 10;
end;
 consider m be Nat such that
A3:for n be Nat st n >=m holds ||.seq.||.(n+1)/||.seq.||.n >= 1 by A1;
   now let n be Nat such that
 A4:n>=m;
 A5:0 <=||.seq.||.n
   & 0 <=||.seq.||.(n+1) by Th2;
 A6:  abs((||.seq.||)).n =abs((||.seq.||).n) by SEQ_1:16
                      .=||.seq.||.n by A5,ABSVALUE:def 1;
         abs((||.seq.||)).(n+1) =abs((||.seq.||).(n+1)) by SEQ_1:16
                      .=||.seq.||.(n+1) by A5,ABSVALUE:def 1;
 hence abs((||.seq.||)).(n+1)/abs((||.seq.||)).n >= 1 by A3,A4,A6;
end;
hence ||.seq.|| is not summable by A2,SERIES_1:44;
end;

registration
  let X be RealBanachSpace;
  cluster norm_summable -> summable sequence of X;
  coherence by Th31;
end;

begin :: Basic Properties of Sequence of Banach_Algebra

theorem Th43:
 for X be Banach_Algebra holds
     for x,y,z being Element of X
        for a,b be Real holds
         x+y = y+x &
         (x+y)+z = x+(y+z) &
         x+(0.X) = x &
         (ex t being Element of
          X st  x+t=
          0.X) &
         (x*y)*z = x*(y*z) &
         1*x=x     &
         0*x=0.X  &
         a*0.X =0.X&
         (-1)*x=-x &
         x*(1.X) = x &
         (1.X)*x = x &
         x*(y+z) = x*y + x*z &
         (y+z)*x = y*x + z*x &
         a*(x*y) = (a*x)*y &
         a*(x+y) = a*x + a*y &
         (a+b)*x = a*x + b*x &
         (a*b)*x = a*(b*x)  &
         (a*b)*(x*y)=(a*x)*(b*y) &
         a*(x*y)=x*(a*y) &
         0.X * x  = 0.X   &
         x*0.X =0.X          &
         x*(y-z) = x*y-x*z   &
          (y-z)*x = y*x-z*x   &
          x+y-z = x+(y-z)      &
          x-y+z = x-(y-z)      &
          x-y-z = x-(y+z)      &
          x+y=(x-z)+(z+y)      &
          x-y=(x-z)+(z-y)      &
          x=(x-y)+y               &
          x=y-(y-x)               &
          ( ||.x.|| = 0 iff x = 0.X ) &
           ||.a * x.|| = abs(a) * ||.x.|| &
           ||.x + y.|| <= ||.x.|| + ||.y.|| &
          ||. x*y .|| <= ||.x.|| * ||.y.|| &
            ||. 1.X .|| = 1  &
          X is complete
 proof
   let X be Banach_Algebra;
   let x,y,z being Element of X;
   let a,b be Real;
   thus x+y = y+x;
   thus (x+y)+z = x+(y+z) by RLVECT_1:def 6;
   thus x+(0.X) = x by RLVECT_1:def 7;
   thus ex t being Element of X st x+t=0.X by RLVECT_1:def 8;
   thus (x*y)*z = x*(y*z) by GROUP_1:def 4;
   thus 1*x = x by RLVECT_1:def 9;
   thus 0*x = 0.X by RLVECT_1:23;
   thus a*0.X =0.X by RLVECT_1:23;
   thus (-1)*x = -x by RLVECT_1:29;
   thus x*(1.X) = x &
          (1.X)*x= x &
         x*(y+z) = x*y + x*z &
         (y+z)*x = y*x + z*x &
         a*(x*y) = (a*x)*y &
         a*(x+y) = a*x + a*y &
         (a+b)*x = a*x + b*x &
         (a*b)*x = a*(b*x)
 by FUNCSDOM:def 20,VECTSP_1:def 12,def 19;
 thus (a*b)*(x*y) =a*(b*(x*y))
                 by FUNCSDOM:def 20
                        .=a*(x*(b*y) )
                 by LOPBAN_2:def 11
                        .=(a*x)*(b*y)
                 by FUNCSDOM:def 20;
 thus a*(x*y)=x*(a*y) by LOPBAN_2:def 11;
 thus 0.X * x  = 0.X & x*0.X =0.X
  proof
   0.X*x =(0.X+0.X)*x  by RLVECT_1:def 7
          .=0.X*x + 0.X*x by VECTSP_1:def 12;
  then 0.X = 0.X*x + 0.X*x-0.X * x by RLVECT_1:28;
  then 0.X = 0.X*x + (0.X*x-0.X * x)  by RLVECT_1:42;
  then 0.X = 0.X*x + 0.X by RLVECT_1:28;
  hence 0.X = 0.X*x by RLVECT_1:def 7;
   x*0.X =x*(0.X+0.X) by RLVECT_1:def 7
          .=x*0.X + x*0.X by FUNCSDOM:def 20;
  then 0.X = x*0.X + x*0.X-x* 0.X by RLVECT_1:28;
  then 0.X = x*0.X + (x*0.X-x* 0.X)  by RLVECT_1:42;
  then 0.X = x*0.X + 0.X by RLVECT_1:28;
  hence 0.X = x*0.X by RLVECT_1:def 7;
  thus thesis;
  end;
 A1:    x*(y-z)+x*z
          =x*((y-z)+z) by FUNCSDOM:def 20
          .=x*(y-(z-z)) by RLVECT_1:43
          .=x*(y-0.X) by RLVECT_1:28
          .=x*y by RLVECT_1:26;
 thus x*(y-z)
         =x*(y-z)+0.X by RLVECT_1:10
         .=x*(y-z)+(x*z-x*z) by RLVECT_1:28
         .=x*y-x*z by A1,RLVECT_1:42;
 A2:    (y-z)*x+z*x
          =((y-z)+z)*x  by VECTSP_1:def 12
          .=(y-(z-z))*x by RLVECT_1:43
          .=(y-0.X)*x    by RLVECT_1:28
          .=y*x            by RLVECT_1:26;
 thus (y-z)*x
         =(y-z)*x+0.X by RLVECT_1:10
         .=(y-z)*x+(z*x-z*x) by RLVECT_1:28
         .=y*x-z*x by A2,RLVECT_1:42;
 thus x+y-z = x+(y-z) by RLVECT_1:42;
 thus x-y+z = x-(y-z) by RLVECT_1:43;
 thus x-y-z = x-(y+z) by RLVECT_1:41;
 thus (x-z)+(z+y)=(x-z)+z+y by RLVECT_1:def 6
                        .=x-(z-z)+y by RLVECT_1:43
                        .=x-0.X + y  by RLVECT_1:28
                        .=x+y  by RLVECT_1:26;
 thus (x-z)+(z-y) =x-z+z-y  by RLVECT_1:42
                         .=x-(z-z)-y by RLVECT_1:43
                         .=x-0.X -y by RLVECT_1:28
                         .=x-y by RLVECT_1:26;
 thus (x-y)+y=x-(y-y) by RLVECT_1:43
                   .=x-0.X by RLVECT_1:28
                   .=x by RLVECT_1:26;
 thus y-(y-x)=y-y + x by RLVECT_1:43
                     .=0.X + x by RLVECT_1:28
                     .=x by RLVECT_1:10;
 thus ( ||.x.|| = 0 iff x = 0.X ) &
          ||.a * x.|| = abs(a) * ||.x.|| &
          ||.x + y.|| <= ||.x.|| + ||.y.|| &
          ||. x*y .|| <= ||.x.|| * ||.y.|| by LOPBAN_2:def 9,NORMSP_1:def 2;
 thus thesis by LOPBAN_2:def 10;
 end;

definition
  let X be non empty multLoopStr;
  let v be Element of X;
  attr v is invertible means :Def8:
   ex w be Element of X st v*w = 1.X & w*v=1.X;
end;

definition let X be non empty Normed_AlgebraStr;
  let S be sequence of X;
  let a be Element of X;
  func a * S -> sequence of X means
   for n be Nat holds it.n = a * S.n;
existence
  proof
  deffunc F(Nat) = a * S.$1;
   ex S being sequence of X st
    for n be Nat holds S.n = F(n) from FUNCT_2:sch 4;
  hence thesis;
end;
uniqueness
  proof
       let S1, S2 be sequence of X;
       assume that
  A1:  ( for n be Nat holds S1.n = a * S.n ) and
  A2:  ( for n be Nat holds S2.n = a * S.n );
          for n be Nat holds S1.n = S2.n
       proof
           let n be Nat;
            S1.n = a * S.n by A1;
           hence thesis by A2;
       end;
      hence thesis by FUNCT_2:113;
  end;
end;

definition let X be non empty Normed_AlgebraStr;
          let S be sequence of X;
          let a be Element of X;
          func S*a -> sequence of X means
 for n be Nat holds it.n = S.n*a;
existence
  proof
  deffunc F(Nat) = S.$1 *a;
   ex S being sequence of X st
    for n be Nat holds S.n = F(n) from FUNCT_2:sch 4;
  hence thesis;
end;
uniqueness
  proof
       let S1, S2 be sequence of X;
       assume that
  A1:  ( for n be Nat holds S1.n = S.n *a) and
  A2:  ( for n be Nat holds S2.n = S.n *a);
          for n be Nat holds S1.n = S2.n
       proof
            let n be Nat;
            S1.n =  S.n *a by A1;
            hence thesis by A2;
       end;
      hence thesis by FUNCT_2:113;
  end;
end;

definition let X be non empty Normed_AlgebraStr;
          let seq1,seq2 be sequence of X;
          func seq1 * seq2 -> sequence of X means
 for n be Nat holds it.n = seq1.n * seq2.n;
existence
  proof
  deffunc F(Nat) = seq1.$1 * seq2.$1;
   ex S being sequence of X st
    for n be Nat holds S.n = F(n) from FUNCT_2:sch 4;
  hence thesis;
end;
uniqueness
          proof
               let S1, S2 be sequence of X;
               assume that
          A1:  ( for n be Nat holds S1.n = seq1.n * seq2.n ) and
          A2:  ( for n be Nat holds S2.n = seq1.n * seq2.n );
                  for n be Nat holds S1.n = S2.n
               proof
                  let n be Nat;
                   S1.n = seq1.n * seq2.n by A1;
                  hence thesis by A2;
               end;
              hence thesis by FUNCT_2:113;
          end;
end;

definition
  let X be Banach_Algebra;
  let x be Element of X such that
   A1:   x is invertible;
  func x" -> Element of X means :Def12:
    x*it= 1. X & it*x= 1. X;
  existence by A1,Def8;
  uniqueness
  proof
  let z,y be Element of X such that
     A2: x*z=1.X & z*x=1.X and
     A3: x*y=(1.X)  & y*x= (1.X);
        y= y*(1.X) by Th43
         .=(y*x)*z by A2,Th43
         .=z by A3,Th43;
    hence thesis;
  end;
end;

definition
  let X be Banach_Algebra;
  let z be Element of X;
  func z GeoSeq -> sequence of X means :Def13:
  it.0 = 1.X & for n be Nat  holds it.(n+1) = it.n * z;
  existence
   proof
      deffunc G(set,set)=(the mult of X).[$2,z];
      consider g be Function such that
      A1:   dom g = NAT & g.0 = 1.X
      & for n being Element of NAT holds g.(n+1) = G(n,g.n)
      from RECDEF_1:sch 3;
      defpred P[Nat] means g.$1 in the carrier of X;
       A2: P[0] by A1;
       A3:  for k be Nat st P[k] holds P[k+1]
       proof
       let k be Nat such that
         A4: P[k];
         reconsider gk=g.k as Element of the carrier of X by A4;
          g.(k+1)=(the mult of X).[gk,z] by A1;
         hence thesis;
       end;
         for n be Nat holds P[n]  from NAT_1:sch 1(A2,A3);
       then for n be set st n in NAT
          holds g.n in the carrier of X;
         then reconsider g0=g as sequence of X by A1,FUNCT_2:5;
           take g0;
          now let n be Nat;
          thus g0.(n+1) = (the mult of X).[g0.n,z] by A1
            .=(the mult of X).(g0.n,z) by BINOP_1:def 1
            .=g0.n * z by GROUP_1:def 1;
        end;
        hence thesis by A1;
end;
uniqueness
proof
 let seq1,seq2 be sequence of X;
 assume that A5: seq1.0=1.X & for n be Nat holds
                seq1.(n+1)=seq1.n * z and
                    A6: seq2.0=1.X  & for n be Nat  holds
                seq2.(n+1)=seq2.n * z;
defpred P[Nat] means seq1.$1 = seq2.$1;
 A7: P[0] by A5,A6;
 A8: for k be Nat st P[k] holds P[k+1]
     proof let k be Nat;
      assume P[k];
      hence seq1.(k+1) = seq2.k * z by A5
                    .= seq2.(k+1) by A6;
      end;
    for n be Nat holds P[n] from NAT_1:sch 1(A7,A8);
 hence seq1 = seq2 by FUNCT_2:113;
end;
end;

definition
  let X be Banach_Algebra;
  let z be Element of X, n be Nat;
  func z #N n -> Element of X equals:Def14:
  z GeoSeq.n;
  correctness;
end;

theorem
   for X be Banach_Algebra
    for z be Element of X holds
  z #N 0 = 1.X
proof
  let X be Banach_Algebra;
  let z be Element of X;
 thus z #N 0 = (z GeoSeq).0 by Def14
                   .= 1.X  by Def13;
end;

theorem Th45:
for X be Banach_Algebra
 for z be Element of X holds
||.z.|| < 1 implies z GeoSeq is summable norm_summable
proof
let X be Banach_Algebra;
let z be Element of X;
assume
A1:  ||.z.|| < 1;
 0<=  ||.z.|| by NORMSP_1:8;
then A2:  abs( ||.z.||)  < 1 by A1,ABSVALUE:def 1;
A3:  for n be Nat holds (   0 <= ||.z GeoSeq.||.n
                                  &   ||.z GeoSeq.||.n  <=(||.z.||  GeoSeq).n )
  proof
  defpred P[Nat] means
     0 <= ||.z GeoSeq.||.$1 & ||.z GeoSeq.||.$1 <= (  ||.z.||  GeoSeq ).$1;
   A4:  ||.(z GeoSeq).||.0 = ||.(z GeoSeq).0 .|| by NORMSP_1:def 10;
      ||.(z GeoSeq).0 .|| = ||.1.X.|| by Def13
                        .= 1 by LOPBAN_2:def 10
                        .= ( ||.z.||  GeoSeq ).0 by PREPOWER:4;
    then A5: P[0] by A4,NORMSP_1:8;
    A6: for k be Nat st P[k] holds P[k+1]
       proof
        let k be Nat;
        assume
        A7: P[k];
        A8: 0 <=  ||.z.|| by NORMSP_1:8;
        A9:      ||.z GeoSeq.||.(k+1) = ||. (z GeoSeq).(k+1) .||
                                           by NORMSP_1:def 10
               .= ||. (z GeoSeq).k * z .|| by Def13;
           ||. (z GeoSeq.k)*z .|| <= ||. (z GeoSeq.k) .|| *  ||.z.||
                                                by LOPBAN_2:def 9;
        then A10:   ||. (z GeoSeq.k)*z .|| <= ||. z GeoSeq .||.k * ||.z.||
                                                by NORMSP_1:def 10;
            ||. z GeoSeq.||.k  *  ||.z.||
                 <=  ( ||.z.|| GeoSeq ).k * ||.z.|| by A7,A8,AXIOMS:25;
         then ||. (z GeoSeq.k)*z .|| <= ( ||.z.||  GeoSeq ).k *  ||.z.||
                   by A10,AXIOMS:22;
         hence 0<= ||.z GeoSeq.||.(k+1) &
             ||.z GeoSeq.||.(k+1)  <=  ( ||.z.||  GeoSeq ).(k+1)
              by A9,NORMSP_1:8,PREPOWER:4;
        end;
      for n be Nat holds P[n] from NAT_1:sch 1(A5,A6);
     hence thesis;
   end;
   ||.z.|| GeoSeq is summable by A2,SERIES_1:28;
then ||.z GeoSeq.|| is summable by A3,SERIES_1:24;
then z GeoSeq is norm_summable by Def4;
hence thesis by Th31;
end;

theorem
   for X be Banach_Algebra
  for x be Point of X
      st ||.1.X - x .|| < 1
  holds ( (1.X - x) GeoSeq is summable
  & (1.X - x) GeoSeq is norm_summable) by Th45;

Lm4:
 for X be RealNormSpace
   for x be Point of X
     st (for e be Real st e>0 holds  ||.x.|| <e)
   holds x=0.X
  proof
    let X be RealNormSpace;
    let x be Point of X such that
    A1:   for e be Real st e > 0 holds ||.x.|| <e;
     now assume
       x<>0.X;
       then 0<> ||.x.|| by NORMSP_1:def 2;
     then 0< ||.x.|| by NORMSP_1:8;
     hence contradiction by A1;
    end;
    hence thesis;
  end;

  Lm5:
  for X be RealNormSpace
   for x,y be Point of X
     st (for e be Real st e>0 holds ||.x-y.|| <e)
   holds x=y
  proof
    let X be RealNormSpace;
    let x,y be Point of X such that
    A1:   for e be Real st e > 0 holds  ||.x-y.|| <e;
     x-y = 0.X by A1,Lm4;
    hence thesis by RLVECT_1:35;
  end;

Lm6:
 for X be RealNormSpace
   for x,y be Point of X
     for seq be Real_Sequence st
    ( seq is convergent
      & lim seq=0
      & for n be Nat holds ||.x-y.|| <=seq.n )
    holds x=y
proof
    let X be RealNormSpace;
    let x,y be Point of X;
    let seq be Real_Sequence such that
     A1: seq is convergent and
     A2:lim seq=0 and
    A3:  for n be Nat holds ||.x-y.|| <=seq.n;

 now
let e be Real
  such that
  A4: 0 < e;
  consider n be Nat such that
  A5:for m be Nat st n<=m holds
     abs( seq.m - 0  ) < e by A1,A2,A4,SEQ_2:def 7;
A6:  abs( seq.n -0 ) < e by A5;
    seq.n <=abs( seq.n ) by ABSVALUE:11;
  then A7: seq.n < e by A6,AXIOMS:22;
       ||.x-y.|| <= seq.n by A3;
  hence ||.x-y.|| < e by A7,AXIOMS:22;
end;
hence x=y by Lm5;
thus thesis;
end;

theorem
   for X be Banach_Algebra
  for x be Point of X
      st  ||.1.X - x .|| < 1
  holds x is invertible & x" = Sum ((1.X - x) GeoSeq )
  proof
    let X be Banach_Algebra;
    let x be Point of X such that
    A1:    ||.1.X - x .|| < 1;
    set seq = (1.X - x)GeoSeq;
    A2: seq is summable by A1,Th45;
    reconsider y=Sum(seq) as Element of X;
    A3:for n be Nat holds
         (Partial_Sums(seq).n)*(1.X -x)=(Partial_Sums(seq)^\1).n-seq.0
         proof
   defpred P[Nat] means
   (Partial_Sums(seq).$1)*(1.X -x)=(Partial_Sums(seq)^\1).$1-seq.0;
  A4:  (Partial_Sums(seq).0)*(1.X -x)
    =(seq.0)*(1.X -x) by Def1
   .=1.X *(1.X -x) by Def13
   .=1.X -x by Th43;
    (Partial_Sums(seq)^\1).0-seq.0
  =(Partial_Sums(seq)).(0+1)-seq.0 by Def6
  .=(Partial_Sums(seq)).(0)+seq.1-seq.0 by Def1
  .=seq.0 + seq.1-seq.0 by Def1
  .=seq.1 +(seq.0-seq.0) by Th43
  .=seq.1 + 0.X by RLVECT_1:28
  .=seq.(0+1) by RLVECT_1:10
  .=seq.0*(1.X -x) by Def13
  .=1.X * (1.X -x) by Def13
  .=1.X -x by Th43;
 then A5: P[0] by A4;
 A6: for k be Nat st P[k] holds P[k+1]
     proof let k be Nat;
      assume
    A7:  P[k];
  A8:  (Partial_Sums(seq).(k+1))*(1.X -x)
         = ( Partial_Sums(seq).k+seq.(k+1) )*(1.X -x) by Def1
         .=(Partial_Sums(seq).k)*(1.X -x)+seq.(k+1)*(1.X -x) by Th43
         .=(Partial_Sums(seq)^\1).k-seq.0+seq.((k+1)+1) by A7,Def13;
   (Partial_Sums(seq)^\1).(k+1)-seq.0
  =(Partial_Sums(seq)).((k+1)+1)-seq.0 by Def6
  .=(Partial_Sums(seq)).(k+1)+seq.((k+1)+1)-seq.0 by Def1
  .=(Partial_Sums(seq)^\1).(k)+seq.((k+1)+1)-seq.0 by Def6
  .=(Partial_Sums(seq)^\1).(k)+seq.((k+1)+1)+(-seq.0) by RLVECT_1:def 11
  .=(Partial_Sums(seq)^\1).(k)+(-seq.0) +seq.((k+1)+1) by RLVECT_1:def 6
  .=(Partial_Sums(seq)^\1).(k)-seq.0 +seq.((k+1)+1) by RLVECT_1:def 11;
      hence (Partial_Sums(seq).(k+1))*(1.X -x)
      =(Partial_Sums(seq)^\1).(k+1)-seq.0 by A8;
      end;
    for n be Nat holds P[n] from NAT_1:sch 1(A5,A6);
    hence thesis;
         end;

A9:
for n be Nat holds (1.X -x)*seq.n=seq.(n+1)
proof
 defpred P[Nat] means (1.X -x)*seq.$1=seq.($1+1);
       A10:  (1.X -x)*seq.0
             =(1.X -x)*1.X by Def13
            .=(1.X -x) by Th43;
        seq.(0+1)
             =seq.0 * (1.X -x) by Def13
             .=1.X * (1.X -x) by Def13
             .=(1.X -x) by Th43;
       then A11: P[0] by A10;
       A12:  for k be Nat st P[k] holds P[k+1]
       proof
       let k be Nat such that
         A13: P[k];
        thus (1.X -x)*seq.(k+1)
        = (1.X -x)*(seq.(k)*(1.X -x)) by Def13
        .=(1.X -x)*seq.(k)*(1.X -x) by Th43
        .=seq.((k+1)+1) by A13,Def13;
       end;
         for n be Nat holds P[n] from NAT_1:sch 1(A11,A12);
       hence thesis;
end;
  A14:for n be Nat holds
        (1.X -x)* (Partial_Sums(seq).n)=(Partial_Sums(seq)^\1).n-seq.0
         proof
   defpred P[Nat] means
  (1.X -x)* (Partial_Sums(seq).$1)=(Partial_Sums(seq)^\1).$1-seq.0;
  A15:  (1.X -x)*(Partial_Sums(seq).0)
    =(1.X -x)*(seq.0) by Def1
   .=(1.X -x)*1.X  by Def13
   .=1.X -x by Th43;
    (Partial_Sums(seq)^\1).0-seq.0
  =(Partial_Sums(seq)).(0+1)-seq.0 by Def6
  .=(Partial_Sums(seq)).(0)+seq.1-seq.0 by Def1
  .=seq.0 + seq.1-seq.0 by Def1
  .=seq.1 +(seq.0-seq.0) by Th43
  .=seq.1 + 0.X by RLVECT_1:28
  .=seq.(0+1) by RLVECT_1:10
  .=seq.0*(1.X -x) by Def13
  .=1.X * (1.X -x) by Def13
  .=1.X -x by Th43;
 then A16: P[0] by A15;
 A17: for k be Nat st P[k] holds P[k+1]
     proof let k be Nat;
      assume
    A18:  P[k];
  A19:  (1.X -x)*(Partial_Sums(seq).(k+1))
         = (1.X -x)*( Partial_Sums(seq).k+seq.(k+1) ) by Def1
         .=(1.X -x)*(Partial_Sums(seq).k)+(1.X -x)*seq.(k+1) by Th43
         .=(Partial_Sums(seq)^\1).k-seq.0+seq.((k+1)+1) by A9,A18;
    (Partial_Sums(seq)^\1).(k+1)-seq.0
  =(Partial_Sums(seq)).((k+1)+1)-seq.0 by Def6
  .=(Partial_Sums(seq)).(k+1)+seq.((k+1)+1)-seq.0 by Def1
  .=(Partial_Sums(seq)^\1).(k)+seq.((k+1)+1)-seq.0 by Def6
  .=(Partial_Sums(seq)^\1).(k)+seq.((k+1)+1)+(-seq.0) by RLVECT_1:def 11
  .=(Partial_Sums(seq)^\1).(k)+(-seq.0) +seq.((k+1)+1) by RLVECT_1:def 6
  .=(Partial_Sums(seq)^\1).(k)-seq.0 +seq.((k+1)+1) by RLVECT_1:def 11;
      hence (1.X -x)*(Partial_Sums(seq).(k+1))
      =(Partial_Sums(seq)^\1).(k+1)-seq.0 by A19;
      end;
    for n be Nat holds P[n] from NAT_1:sch 1(A16,A17);
    hence thesis;
         end;
    A20:    now let n be Nat;
           reconsider yn=Partial_Sums(seq).n  as Element of X;
         A21:  1.X -y*x =1.X -  0.X -y*x by RLVECT_1:26
          .=1.X - (yn*x-yn*x) -y*x by RLVECT_1:28
          .=1.X-yn*x+yn*x-y*x by RLVECT_1:43
          .=(1.X-yn*x)+(yn*x-y*x) by RLVECT_1:42
           .=(1.X-yn*x)+(yn-y)*x by Th43;
       A22: yn*(1.X - (1.X-x))
            =  yn*1.X - yn*(1.X -x) by Th43
           .=  yn - yn*(1.X -x) by Th43
           .=Partial_Sums(seq).n-((Partial_Sums(seq)^\1).n-seq.0) by A3
           .=Partial_Sums(seq).n-(Partial_Sums(seq)^\1).n +seq.0 by Th43;
        (Partial_Sums(seq)).(n+1) = (Partial_Sums(seq)).n
        + seq.(n+1) by Def1;
       then (Partial_Sums(seq)).(n+1) = (Partial_Sums(seq)).n + (seq ^\1).n
        by Def6;
       then  yn*(1.X - (1.X-x))
             =Partial_Sums(seq).n
              -( (Partial_Sums(seq)).n + (seq ^\1).n)+seq.0 by A22,Def6
             .=Partial_Sums(seq).n-(Partial_Sums(seq)).n - (seq ^\1).n
              +seq.0 by Th43
            .=0.X - (seq ^\1).n +seq.0 by RLVECT_1:28
            .=0.X-((seq ^\1).n-seq.0) by RLVECT_1:43
            .=-((seq ^\1).n-seq.0) by RLVECT_1:27
            .=seq.0+(- (seq ^\1).n) by RLVECT_1:47
            .= (seq.0- (seq ^\1).n) by RLVECT_1:def 11
            .=seq.0-seq.(n+1) by Def6
            .=1.X - seq.(n+1) by Def13;
then A23:  (1.X-yn*x) =1.X -(1.X - seq.(n+1) ) by Th43
           .=seq.(n+1) by Th43
           .=(seq^\1).n  by Def6;
      A24: ||. (seq^\1).n+(yn-y)*x .||
          <= ||. (seq^\1).n .|| + ||. (yn-y)*x .|| by NORMSP_1:def 2;
      A25:  ||. (seq^\1).n .|| + ||. (yn-y)*x .||
         =  ||. (seq^\1).||.n  + ||. (yn-y)*x .|| by NORMSP_1:def 10;
        ||. (yn-y)*x .|| <= ||. (yn-y).|| * ||. x .|| by Th43;
      then  ||. (seq^\1).||.n  + ||. (yn-y)*x .||
              <= ||. (seq^\1).||.n + ||. (yn-y).|| * ||. x .||
              by REAL_1:55;
      then ||. (seq^\1).n+(yn-y)*x .||
             <= ||. (seq^\1).||.n + ||. (yn-y).|| * ||. x .||
             by A24,A25,AXIOMS:22;
      then ||. 1.X -y*x.|| <= ||. (seq^\1).||.n
             +  ||.(Partial_Sums(seq)-y).n.|| * ||.x.||
               by A21,A23,NORMSP_1:def 7;
      then ||. 1.X -y*x.||
             <= ||. (seq^\1).||.n
             +  ||. Partial_Sums(seq)-y.||.n * ||. x .|| by NORMSP_1:def 10;
      then ||. 1.X -y*x.||
               <= ||. (seq^\1).||.n
               + ( ||. x .|| (#) ||. Partial_Sums(seq)-y.||).n  by SEQ_1:13;
     hence ||. 1.X -y*x.||
               <=( ||. (seq^\1).||
               +  ||. x .|| (#) ||. Partial_Sums(seq)-y.||).n  by SEQ_1:11;
    end;
 A26:    now let n be Nat;
           reconsider yn=Partial_Sums(seq).n  as Element of X;
         A27:  1.X -x*y
          =1.X -  0.X -x*y by RLVECT_1:26
          .=1.X - (x*yn-x*yn) -x*y by RLVECT_1:28
          .=1.X-x*yn+x*yn-x*y  by RLVECT_1:43
          .=(1.X-x*yn)+(x*yn-x*y) by RLVECT_1:42
           .=(1.X-x*yn)+x*(yn-y) by Th43;
       A28: (1.X - (1.X-x))*yn
            =  1.X*yn - (1.X -x)*yn by Th43
           .=  yn - (1.X -x)*yn by Th43
           .=Partial_Sums(seq).n-((Partial_Sums(seq)^\1).n-seq.0) by A14
           .=Partial_Sums(seq).n-(Partial_Sums(seq)^\1).n +seq.0 by Th43;
        (Partial_Sums(seq)).(n+1) = (Partial_Sums(seq)).n
        + seq.(n+1) by Def1;
       then (Partial_Sums(seq)).(n+1) = (Partial_Sums(seq)).n + (seq ^\1).n
        by Def6;
       then  (1.X - (1.X-x))*yn
             =Partial_Sums(seq).n
              -( (Partial_Sums(seq)).n + (seq ^\1).n)+seq.0 by A28,Def6
             .=Partial_Sums(seq).n-(Partial_Sums(seq)).n - (seq ^\1).n
              +seq.0 by Th43
            .=0.X - (seq ^\1).n +seq.0 by RLVECT_1:28
            .=0.X-((seq ^\1).n-seq.0) by RLVECT_1:43
            .=-((seq ^\1).n-seq.0) by RLVECT_1:27
            .=seq.0+(- (seq ^\1).n) by RLVECT_1:47
            .= (seq.0- (seq ^\1).n) by RLVECT_1:def 11
            .=seq.0-seq.(n+1) by Def6
            .=1.X - seq.(n+1) by Def13;
then A29:   (1.X-x*yn)
            =1.X -(1.X - seq.(n+1) ) by Th43
           .=seq.(n+1) by Th43
           .=(seq^\1).n  by Def6;
      A30: ||. (seq^\1).n+x*(yn-y) .||
          <= ||. (seq^\1).n .|| + ||. x*(yn-y) .|| by NORMSP_1:def 2;
      A31:  ||. (seq^\1).n .|| + ||. x*(yn-y) .||
         =  ||. (seq^\1).||.n  + ||. x*(yn-y) .|| by NORMSP_1:def 10;
         ||. x*(yn-y) .|| <= ||. x .||*||. (yn-y).||  by Th43;
        then ||. (seq^\1).||.n  + ||. x*(yn-y) .||
              <= ||. (seq^\1).||.n + ||. x .|| * ||. (yn-y).||
              by REAL_1:55;
      then ||. (seq^\1).n+x*(yn-y) .||
             <= ||. (seq^\1).||.n +  ||. x .|| * ||. (yn-y).||
             by A30,A31,AXIOMS:22;
      then ||. 1.X -x*y.|| <= ||. (seq^\1).||.n
         + ||. x .|| * ||. (Partial_Sums(seq)-y).n.|| by A27,A29,NORMSP_1:def 7
;
      then ||. 1.X -x*y.|| <= ||. (seq^\1).||.n
             + ||. x .|| * ||. Partial_Sums(seq)-y.||.n by NORMSP_1:def 10;
      then ||. 1.X -x*y.|| <= ||. (seq^\1).||.n
                + ( ||. x .|| (#) ||. Partial_Sums(seq)-y.||).n by SEQ_1:13;
      hence ||. 1.X -x*y.|| <=( ||. (seq^\1).||
                +  ||. x .|| (#) ||. Partial_Sums(seq)-y.||).n  by SEQ_1:11;
  end;
  A32:   Partial_Sums(seq) is convergent by A2,Def2;
   lim Partial_Sums(seq) =y by Def3;
  then A33:   ||. Partial_Sums(seq)-y.|| is convergent
      & lim ||. Partial_Sums(seq)-y.|| =0
   by A32,NORMSP_1:41;
  then A34:  ||. x .|| (#)  ||. Partial_Sums(seq)-y.|| is convergent
       by SEQ_2:21;
  A35:  lim ( ||. x .|| (#)  ||. Partial_Sums(seq)-y.|| )
            = ||. x .|| *0 by A33,SEQ_2:22
            .=0;
   seq is convergent & lim seq =0.X  by A2,Th19;
  then seq^\1 is convergent & lim (seq^\1)=0.X by Th14;
  then A36: ||. seq^\1 .|| is convergent & lim (  ||. seq^\1 .|| )= ||. 0.X.||
        by LOPBAN_1:47;
  then A37:   ||. seq^\1 .|| +  ||. x .|| (#)  ||. Partial_Sums(seq)-y.||
       is convergent by A34,SEQ_2:19;
  A38: lim( ||. seq^\1 .|| +  ||. x .|| (#)  ||. Partial_Sums(seq)-y.||)
         = lim( ||. seq^\1 .||)
         + lim (  ||. x .|| (#)  ||. Partial_Sums(seq)-y.||)
          by A34,A36,SEQ_2:20
         .=0 by A35,A36,NORMSP_1:5;
  then A39:  1.X = y*x by A20,A37,Lm6;
  A40:  1.X = x*y by A26,A37,A38,Lm6;
 hence x is invertible by A39,Def8;
 hence thesis by A39,A40,Def12;
end;

