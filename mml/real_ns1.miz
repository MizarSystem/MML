:: Completeness of the Real {E}uclidean Space
::  by Noboru Endou and Yasunari Shidama
:: 
:: Received December 28, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, RLVECT_1, VECTSP_1, FINSEQ_1, FINSEQ_2, RVSUM_1,
      EUCLID, BINOP_1, FUNCT_1, ARYTM, ARYTM_1, RELAT_1, COMPLEX1, ABSVALUE,
      SQUARE_1, ORDINAL2, SEQ_1, SEQ_2, BOOLE, REALSET1, NORMSP_1, RSSPACE3,
      LOPBAN_1, REAL_NS1, BHSP_1, METRIC_1, BHSP_3, PROB_2, EUCLID_2;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, RELSET_1,
      FUNCT_2, BINOP_1, PRE_TOPC, FINSEQ_1, FINSEQ_2, STRUCT_0, FUNCOP_1,
      ORDINAL1, NUMBERS,
      XXREAL_0, XREAL_0, SQUARE_1, COMPLEX1, REAL_1, NAT_1, RLVECT_1,
      BHSP_1, BHSP_2, BINOP_2, SEQ_1, SEQ_2, RVSUM_1, REALSET2, NORMSP_1,
      EUCLID, EUCLID_2, RSSPACE3, LOPBAN_1, BHSP_3;
 constructors REAL_1, BINARITH, DOMAIN_1, SEQ_2, SQUARE_1, REALSET2, RSSPACE3,
      LOPBAN_1, COMPLEX1, NAT_1, FINSEQOP, SUPINF_1, BHSP_2, BHSP_3, EUCLID_2,
      SETWOP_2;
 registrations RELSET_1, ORDINAL2, XREAL_0, FINSEQ_2, FUNCOP_1, MEMBERED,
      NORMSP_1, FUNCT_1, NAT_1, RSSPACE3, REVROT_1, BHSP_1, XXREAL_0;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 theorems TARSKI, XREAL_0, NORMSP_1, EUCLID, RLVECT_1, REALSET2, SEQ_2, SEQ_4,
      BINOP_1, COMPLEX1, ABSVALUE, RSSPACE3, FINSEQ_1, FINSEQ_2, RVSUM_1,
      FUNCT_2, LOPBAN_1, REAL_1, ZFMISC_1, FUNCOP_1, SEQ_1, NAT_1, SQUARE_1,
      FUNCT_1, FINSEQ_5, XREAL_1, RFUNCT_4, JORDAN2C, BHSP_2, BHSP_1, EUCLID_2,
      EUCLID_4, EUCLIDLP, BHSP_3;
 schemes NAT_1, SEQ_1, BINOP_1, BINOP_2, FUNCT_2, FINSEQ_1;
 definitions RLVECT_1, BINOP_1, BHSP_1, RSSPACE3, BHSP_3;

begin :: R^n space as RealLinearSpace

 reserve n for Nat;

TexADD:
  ex ADD be BinOp of REAL n st
    (for a,b being Element of REAL n holds ADD.(a,b) = a+b) &
      ADD is commutative associative
proof
   deffunc P(Element of REAL n,Element of REAL n) = $1+$2;
   consider ADD be BinOp of REAL n such that
A2: for a,b being Element of REAL n
     holds ADD.(a,b)=P(a,b) from BINOP_1:sch 4;
A3:ADD is commutative
   proof
    now let a,b be Element of REAL n;
     thus ADD.(a,b) = a+b by A2
                   .= ADD.(b,a) by A2;
    end;
    hence thesis by BINOP_1:def 2;
   end;
   ADD is associative
   proof
    now let a,b,c be Element of REAL n;
     reconsider a1=a,b1=b,c1=c as Element of n-tuples_on REAL by EUCLID:def 1;
     thus ADD.(a,ADD.(b,c)) = a+ADD.(b,c) by A2
                      .= a+(b+c) by A2
                      .= (a1+b1)+c1 by RVSUM_1:32
                      .= ADD.(a,b)+c by A2
                      .= ADD.(ADD.(a,b),c) by A2;
    end;
    hence thesis by BINOP_1:def 3;
   end;
   hence thesis by A2,A3;
end;

TexMULT:
  ex f be Function of [: REAL, REAL n :], REAL n st
    for r be Element of REAL, x be Element of REAL n holds
      f.(r,x) = r*x
proof
  deffunc F(real number,Element of REAL n) = $1 * $2;
  ex f be Function of [:REAL, REAL n:], REAL n st
   for r be Element of REAL, x be Element of REAL n
      holds f.(r,x) = F(r,x) from BINOP_1:sch 4;
  hence thesis;
end;

definition let n be Nat;
  func Euclid_add n -> BinOp of REAL n means :DefADD:
    for a,b being Element of REAL n holds
      it.(a,b) = a+b;
  existence
  proof
  consider ADD be BinOp of REAL n such that
AA: (for a,b being Element of REAL n holds ADD.(a,b) = a+b) &
     ADD is commutative associative by TexADD;
  take ADD;
  thus thesis by AA;
  end;
  uniqueness
  proof
    deffunc O(Element of REAL n, Element of REAL n)=$1+$2;
    for o1,o2 being BinOp of REAL n st
      (for a,b being Element of REAL n holds o1.(a,b) = O(a,b)) &
      (for a,b being Element of REAL n holds o2.(a,b) = O(a,b))
      holds o1 = o2 from BINOP_2:sch 2;
    hence thesis;
  end;
end;

registration let n be Nat;
  cluster Euclid_add n -> commutative associative;
  coherence
  proof
    consider ADD be BinOp of REAL n such that
A1: (for a,b being Element of REAL n holds ADD.(a,b) = a+b) &
      ADD is commutative associative by TexADD;
    thus thesis by A1,DefADD;
  end;
end;

definition let n be Nat;
  func Euclid_mult n -> Function of [: REAL, REAL n :], REAL n means :DefMULT:
    for r be Element of REAL, x be Element of REAL n
      holds it.(r,x) = r*x;
existence by TexMULT;
uniqueness
proof
  let mult1,mult2 be Function of [: REAL, REAL n :], REAL n such that
A1:for r be Element of REAL, x be Element of REAL n
      holds mult1.(r,x) = r*x and
A2: for r be Element of REAL, x be Element of REAL n
      holds mult2.(r,x) = r*x;
    for r being Element of REAL, x being Element of REAL n holds
      mult1.(r,x) = mult2.(r,x)
   proof
    let r be Element of REAL;
    let x be Element of REAL n;
    thus mult1.(r,x) = r*x by A1
                    .= mult2.(r,x) by A2;
  end;
  hence thesis by BINOP_1:2;
end;
end;

definition let n be Nat;
  func Euclid_norm n -> Function of REAL n,REAL means :DefNORM:
  for x being Element of REAL n holds
   it.x = |.x.|;
  existence
   proof
    deffunc F(Element of REAL n) = |.$1.|;
    consider f being Function of REAL n, REAL such that
A1: for x being Element of REAL n holds f.x = F(x) from FUNCT_2:sch 4;
    take f;
     let x be Element of REAL n;
     thus f.x = |.x.| by A1;
   end;
  uniqueness
   proof
    let f,g be Function of REAL n, REAL;
    assume that
A2: for x being Element of REAL n holds f.x = |.x.| and
A3: for x being Element of REAL n holds g.x = |.x.|;
    now
      let x be Element of REAL n;
      thus f.x = |.x.| by A2
        .= g.x by A3;
    end;
    hence thesis by FUNCT_2:113;
   end;
end;

definition let n be Nat;
  func REAL-NS n -> strict non empty NORMSTR means :DefRNS:
  the carrier of it = REAL n &
  the Zero of it = 0*n &
  the add of it = Euclid_add n &
  the Mult of it = Euclid_mult n &
  the norm of it = Euclid_norm n;
existence
proof
  take NORMSTR(#REAL n, 0*n, Euclid_add n, Euclid_mult n, Euclid_norm n #);
  thus thesis;
end;
uniqueness;
end;

registration let n be non empty Nat;
  cluster REAL-NS n -> non trivial;
coherence
proof
  the carrier of REAL-NS n = REAL n by DefRNS
   .= the carrier of TOP-REAL n by EUCLID:25;
  hence thesis by REALSET2:def 5;
end;
end;

theorem T1:
  for x be VECTOR of REAL-NS n,
      y be Element of REAL n st x=y holds ||.x.|| = |.y.|
proof
  let x be VECTOR of REAL-NS n;
  let y be Element of REAL n;
  assume A1: x=y;
  thus ||.x.|| = (the norm of REAL-NS n).x by NORMSP_1:def 1
               .= (Euclid_norm n).y by A1,DefRNS
               .= |.y.| by DefNORM;
end;

theorem T4:
  for n be Nat
  for x,y be VECTOR of REAL-NS n, a,b be Element of REAL n
  st x=a & y=b holds x+y = a + b
proof
  let n be Nat;
  let x,y be VECTOR of REAL-NS n;
  let a,b be Element of REAL n;
  assume
A1: x=a & y=b;
  thus x+y = (Euclid_add n).(a,b) by A1,DefRNS
          .= a+b by DefADD;
end;

theorem T6:
  for x be VECTOR of REAL-NS n, y be Element of REAL n,
      a be Real st x=y holds a * x = a * y
proof
  let x be Point of REAL-NS n,
      y be Element of REAL n,
      a be Real;
  assume A1: x = y;
  thus a * x =(Euclid_mult n).(a,x) by DefRNS
            .=a*y by A1,DefMULT;
end;

registration let n be Nat;
  cluster REAL-NS n -> RealNormSpace-like RealLinearSpace-like
      Abelian add-associative right_zeroed right_complementable;
coherence
proof
   thus REAL-NS n is RealNormSpace-like
   proof
    for x,y being Point of REAL-NS n, a be Real holds
     ( ||.x.|| = 0 iff x = 0.(REAL-NS n) ) &
     ||.a * x.|| = abs(a) * ||.x.|| &
     ||.x + y.|| <= ||.x.|| + ||.y.||
    proof
     let x,y be Point of REAL-NS n;
     let a be Real;
     thus ||.x.|| = 0 iff x = 0.(REAL-NS n)
     proof
      reconsider x1=x as Element of REAL n by DefRNS;
      |.x1.| = 0 iff x1 = 0*n by EUCLID:10,EUCLID:11;
      hence ||.x.|| = 0 iff x= 0.(REAL-NS n) by T1,DefRNS;
     end;
     thus ||.a * x.|| = abs(a) * ||.x.||
     proof
      reconsider x1=x as Element of REAL n by DefRNS;
A1:   a*x = a* x1 by T6;
      thus ||.a * x.|| = |.a*x1.| by T1,A1
                      .= abs(a)*|.x1.| by EUCLID:14
                      .= abs(a)*||.x.|| by T1;
     end;
     thus ||.x + y.|| <= ||.x.|| + ||.y.||
     proof
      reconsider x1=x,y1=y as Element of REAL n by DefRNS;
Q1:   x+y = x1+y1 by T4;
Q2:   ||.x + y.|| = |. x1+ y1 .| by Q1,T1;
      |. x1+ y1 .| <= |. x1 .| + |. y1 .| by EUCLID:15; then
      |. x1+ y1 .| <= ||.x.|| +|. y1 .| by T1;
      hence thesis by T1,Q2;
     end;
    end;
    hence thesis by NORMSP_1:def 2;
   end;
   thus REAL-NS n is RealLinearSpace-like
   proof
Q1: for a be Real, v,w being VECTOR of REAL-NS n holds
     a * (v + w) = a * v + a * w
    proof
     let a be Real;
     let v,w be VECTOR of REAL-NS n;
     thus a * (v + w) = a * v + a * w
     proof
      reconsider v1 = v, w1 = w as Element of REAL n by DefRNS;
P1:   a * v =a * v1 by T6;
P2:   a * w =a * w1 by T6;
      reconsider v2=v1,w2=w1 as Element of n-tuples_on REAL by EUCLID:def 1;
      thus a * (v + w) =(Euclid_mult n).(a,v+w) by DefRNS
                .=(Euclid_mult n).(a,v1+w1) by T4
                .= a*(v2+w2) by DefMULT
                .=a*v2 + a*w2 by RVSUM_1:73
                .=a * v+ a * w by P1,P2,T4;
     end;
    end;
Q2: for a,b be Real, v being VECTOR of REAL-NS n holds
     (a + b) * v = a * v + b * v
    proof
     let a,b be Real;
     let v be VECTOR of REAL-NS n;
     thus (a + b) * v = a * v + b * v
     proof
      reconsider v1 = v as Element of REAL n by DefRNS;
P1:   a * v = a * v1 by T6;
P2:   b * v = b * v1 by T6;
      reconsider v2=v1 as Element of n-tuples_on REAL by EUCLID:def 1;
      thus (a + b) * v =(Euclid_mult n).(a + b,v) by DefRNS
            .=(a + b) * v2 by DefMULT
            .=a*v2 + b*v2 by RVSUM_1:72
            .=a * v + b * v by P1,P2,T4;
     end;
    end;
Q3: for a,b be Real, v being VECTOR of REAL-NS n holds
     (a * b) * v = a * (b * v)
    proof
     let a,b be Real;
     let v be VECTOR of REAL-NS n;
     thus (a * b) * v = a * (b * v)
     proof
      reconsider v1 = v as Element of REAL n by DefRNS;
P2:   b * v = b * v1 by T6;
      reconsider v2=v1 as Element of n-tuples_on REAL by EUCLID:def 1;
      (a * b) * v=(Euclid_mult n).(a * b,v) by DefRNS
            .=(a * b) * v1 by DefMULT
            .=a * (b * v2) by RVSUM_1:71;
      hence thesis by P2,T6;
     end;
    end;
    for v being VECTOR of REAL-NS n holds 1 * v = v
    proof
     let v be VECTOR of REAL-NS n;
     thus 1 * v = v
     proof
      reconsider v1 = v as Element of REAL n by DefRNS;
      reconsider v2=v1 as Element of n-tuples_on REAL by EUCLID:def 1;
      thus 1 * v =(Euclid_mult n).(1,v) by DefRNS
          .=1 * v2 by DefMULT
          .=v by RVSUM_1:74;
     end;
    end;
    hence thesis by Q1,Q2,Q3,RLVECT_1:def 9;
   end;
   thus REAL-NS n is Abelian
   proof
    for v,w being VECTOR of REAL-NS n holds v + w = w + v
    proof
     let v, w be VECTOR of REAL-NS n;
     thus v + w = w + v
     proof
      reconsider v1 = v, w1 = w as Element of REAL n by DefRNS;
      thus v+w = (Euclid_add n).(v,w) by DefRNS
              .= (Euclid_add n).(w1,v1) by BINOP_1:def 2
              .= w+v by DefRNS;
     end;
    end;
    hence thesis by RLVECT_1:def 5;
   end;
   thus REAL-NS n is add-associative
   proof
    for u,v,w being VECTOR of REAL-NS n holds (u + v) + w = u + (v + w)
    proof
     let u, v, w be VECTOR of REAL-NS n;
     thus (u + v) + w = u + (v + w)
     proof
      reconsider u1 = u, v1 = v, w1 = w as Element of REAL n by DefRNS;
P2:   v + w =v1+w1 by T4;
      thus (u + v) + w
              =(Euclid_add n).((the add of REAL-NS n).(u,v),w) by DefRNS
              .=(Euclid_add n).((Euclid_add n).(u1,v1), w1) by DefRNS
              .=(Euclid_add n).(u1,(Euclid_add n).(v1,w1)) by BINOP_1:def 3
              .=(Euclid_add n).(u1,v1+w1) by DefADD
              .= u1 + (v1 + w1) by DefADD
              .= u + (v + w) by P2,T4;
     end;
    end;
    hence thesis by RLVECT_1:def 6;
   end;
   thus REAL-NS n is right_zeroed
   proof
    for v being VECTOR of REAL-NS n holds v + 0.(REAL-NS n) = v
    proof
     let v be VECTOR of REAL-NS n;
     thus v + 0.(REAL-NS n) = v
     proof
      reconsider v1 = v as Element of REAL n by DefRNS;
P1:   0.(REAL-NS n) = 0*n by DefRNS;
      reconsider zero = n|->0 as Element of n-tuples_on REAL by FINSEQ_2:132;
      reconsider v2=v1 as Element of n-tuples_on REAL by EUCLID:def 1;
      thus v + 0.(REAL-NS n) =v1 + 0*n by T4,P1
              .= v2 + zero by EUCLID:def 4
              .=v by RVSUM_1:33;
     end;
    end;
    hence thesis by RLVECT_1:def 7;
   end;
   for v being VECTOR of REAL-NS n
    ex w being VECTOR of REAL-NS n st v + w = 0.(REAL-NS n)
   proof
    let v be VECTOR of REAL-NS n;
    thus ex w being VECTOR of REAL-NS n st v + w = 0.(REAL-NS n)
    proof
     reconsider v1 = v as Element of REAL n by DefRNS;
     reconsider v2 = v1 as Element of n-tuples_on REAL by EUCLID:def 1;
P1:   0.(REAL-NS n) = 0*n by DefRNS .= n|->0 by EUCLID:def 4;
     reconsider w=-v1 as VECTOR of REAL-NS n by DefRNS;
     take w;
     thus v + w =v2+(-v2) by T4
           .=0.(REAL-NS n) by P1,RVSUM_1:40;
    end;
   end;
   hence thesis by RLVECT_1:def 8;
  end;
end;

theorem T7:
  for x be VECTOR of REAL-NS n, a be Element of REAL n st x=a holds -x = -a
proof
   let x be VECTOR of REAL-NS n;
   let a be Element of REAL n;
   assume
A1:x=a;
   reconsider a1=a as Element of n-tuples_on REAL by EUCLID:def 1;
   -x = (-1)*x by RLVECT_1:29
        .= (-1)*a by T6,A1
        .=-a1 by RVSUM_1:76;
   hence thesis;
end;

theorem T8:
  for x,y be VECTOR of REAL-NS n, a,b be Element of REAL n st x=a & y=b holds
    x-y = a - b
proof
   let x,y be VECTOR of REAL-NS n;
   let a,b be Element of REAL n;
   assume
A1: x=a & y=b;
A2:-y = -b by A1,T7;
   reconsider a1=a, b1=b as Element of n-tuples_on REAL by EUCLID:def 1;
   x-y =a+(-b) by A1,A2,T4
          .=a1-b1 by RVSUM_1:52;
   hence thesis;
end;

theorem Lm001:
  for f be FinSequence of REAL st dom f = Seg n holds
    f is Element of REAL n
proof
   let f be FinSequence of REAL;
   assume dom f= Seg n; then
   len f = n by FINSEQ_1:def 3; then
   f is Element of n-tuples_on REAL by FINSEQ_2:110;
   hence thesis by EUCLID:def 1;
end;

theorem Lm1:
  for n be Nat, x be Element of REAL n st
  (for i be Nat st i in Seg n holds 0 <= x.i) holds
    0 <= Sum x & (for i be Nat st i in Seg n holds x.i <= Sum x)
proof
   defpred P[Nat] means
    for x be Element of REAL $1
     st (for i be Nat st i in Seg $1 holds 0 <= x.i) holds
      0 <= Sum x
    & (for i be Nat st i in Seg $1 holds x.i <= Sum x);
P0:P[0]
   proof
    let x be Element of REAL 0;
    assume for i be Nat st i in Seg 0 holds 0 <= x.i;
    len x = 0 by EUCLID:2;
    hence thesis by FINSEQ_1:4,32,RVSUM_1:102;
   end;
PN:now let k be Nat such that
PN1: P[k];
    now let x be Element of REAL (k+1);
     assume
A1:   for i be Nat st i in Seg (k+1) holds 0 <= x.i;
     thus 0 <= Sum x
        & (for i be Nat st i in Seg (k+1) holds x.i <=Sum x)
     proof
      set xk= x|k;
      x is Element of (k+1)-tuples_on REAL by EUCLID:def 1; then
B1:   k+1 = len x by FINSEQ_2:109; then
      k <= len x by NAT_1:29; then
      len(x|k) = k by FINSEQ_1:80; then
      xk is Element of k-tuples_on REAL by FINSEQ_2:110; then
      reconsider xk as Element of REAL k by EUCLID:def 1;
B6:   now let i be Nat;
       assume
A2:    i in Seg k;
       k <= k+1 by NAT_1:29; then
       Seg k c= Seg(k+1) by FINSEQ_1:7; then
       0 <= x.i by A1,A2; then
       0 <= (x|Seg k).i by A2,FUNCT_1:72;
       hence 0 <= xk.i by FINSEQ_1:def 15;
      end; then
A2:   0 <=Sum xk by PN1;
      1 <= k+1 by NAT_1:29; then
      k+1 in Seg len x by B1,FINSEQ_1:3; then
B2:   k+1 in dom x by FINSEQ_1:def 3;
B3:   xk = x|Seg k by FINSEQ_1:def 15;
A3:   x = x| (k+1) by B1,FINSEQ_1:79
       .= x|Seg(k+1) by FINSEQ_1:def 15
       .= xk^<* x.(k+1) *> by B2,B3,FINSEQ_5:11;
B7:   k+1 in Seg (k+1) by FINSEQ_1:6; then
B4:   0 <= x.(k+1) by A1;
A4:   Sum x=Sum xk + x.(k+1) by A3,RVSUM_1:104;
      now let i be Nat;
       assume i in Seg (k+1); then
A7:    1 <= i & i <=k+1 by FINSEQ_1:3;
        per cases by A7,REAL_1:def 5;
        suppose i < k+1; then
         i <=k by NAT_1:38; then
B5:      i in Seg k by A7,FINSEQ_1:3; then
         xk.i <= Sum xk by PN1,B6; then
B8:      x.i <= Sum xk by B3,B5,FUNCT_1:72;
         0 <= x.(k+1) by B7,A1; then
         Sum xk <= Sum xk + x.(k+1) by XREAL_1:33;
         hence x.i <=Sum x by A4,B8,XREAL_1:2;
        end;
        suppose i=k+1;
         hence x.i <= Sum x by A4,A2,XREAL_1:33;
        end;
      end;
      hence thesis by A4,A2,B4,XREAL_1:35;
     end;
    end;
    hence P[k+1];
   end;
   thus for k be Nat holds P[k] from NAT_1:sch 1(P0,PN);
end;

theorem Lm2:
  for x be Element of REAL n holds
    for i be Nat st i in Seg n holds abs(x.i) <= |.x.|
proof
   let x be Element of REAL n;
    let i be Nat;
    assume
A1: i in Seg n;
    abs(x.i) * abs(x.i) = (x.i)^2
    proof
     per cases by ABSVALUE:1;
      suppose abs(x.i) = x.i;
       hence thesis by SQUARE_1:def 3;
      end;
      suppose abs(x.i) = -(x.i); then
       abs(x.i) * abs(x.i) = x.i * x.i;
       hence thesis by SQUARE_1:def 3;
      end;
    end; then
A2: (sqr x).i = abs(x.i)*abs(x.i) by RVSUM_1:78;
    reconsider sx = sqr x as Element of REAL n by EUCLID:def 1;
    now let k be Nat;
     assume k in Seg n;
     sx.k = (x.k)^2 by RVSUM_1:78;
     hence 0 <= sx.k by SQUARE_1:72;
    end; then
A3: abs(x.i)*abs(x.i) <= Sum sqr x by A1,A2,Lm1;
    0 <= abs(x.i)*abs(x.i) by XREAL_1:65; then
A4: sqrt (abs(x.i)*abs(x.i)) <= sqrt Sum sqr x by A3,SQUARE_1:94;
    0 <= abs(x.i) by COMPLEX1:132; then
    sqrt (abs(x.i))^2 = abs(x.i) by SQUARE_1:89; then
    abs(x.i) <= sqrt Sum sqr x by A4,SQUARE_1:def 3;
    hence abs(x.i) <= |.x.| by EUCLID:def 5;
end;

theorem Lm3:
  for x be Point of REAL-NS n, y be Element of REAL n st x=y holds
    for i be Nat st i in Seg n holds abs(y.i) <= ||.x.||
proof
   let x be Point of REAL-NS n,
       y be Element of REAL n;
   assume x=y; then
   ||.x.|| = |.y.| by T1;
   hence thesis by Lm2;
end;

theorem Lm4:
  for x be Element of REAL (n+1) holds |.x.|^2 = |.(x|n).|^2 + (x.(n+1))^2
proof
   let x be Element of REAL (n+1);
   reconsider x as Element of (n+1)-tuples_on REAL by EUCLID:def 1;
A1:n+1 = len x by FINSEQ_2:109; then
   n+1 in Seg len x by FINSEQ_1:6; then
A2:n+1 in dom x by FINSEQ_1:def 3;
A3:x|n = x|Seg n by FINSEQ_1:def 15;
   n <= len x by A1,NAT_1:29; then
   len (x|n) = n by FINSEQ_1:80; then
   reconsider xn = x|n as Element of n-tuples_on REAL by FINSEQ_2:110;
A4:x = x| (n+1) by A1,FINSEQ_1:79
    .= x|Seg(n+1) by FINSEQ_1:def 15
    .= (x|n)^<* x.(n+1) *> by A2,A3,FINSEQ_5:11;
A5:|.x.| = sqrt Sum sqr x by EUCLID:def 5;
   sqr x is Element of REAL (n+1) &
   for k be Nat st k in Seg (n+1) holds 0 <= (sqr x).k
   proof
    thus sqr x is Element of REAL (n+1) by EUCLID:def 1;
    let k be Nat;
     assume k in Seg(n+1);
     (sqr x).k = (x.k)^2 by RVSUM_1:78 .= x.k * x.k by SQUARE_1:def 3;
     hence 0 <= (sqr x).k by XREAL_1:65;
   end; then
   0 <= Sum sqr x by Lm1; then
A6:|.x.|^2 = Sum sqr x by A5,SQUARE_1:def 4;
A7:|.(x|n).| = sqrt Sum sqr (x|n) by EUCLID:def 5;
   sqr (x|n) is Element of REAL n &
   for k be Nat st k in Seg n holds 0 <= (sqr (x|n)).k
   proof
    sqr xn is Element of REAL n by EUCLID:def 1;
    hence sqr(x|n) is Element of REAL n;
    let k be Nat;
     assume k in Seg n;
     (sqr xn).k = (xn.k)^2 by RVSUM_1:78 .= xn.k * xn.k by SQUARE_1:def 3;
     hence 0 <= (sqr (x|n)).k by XREAL_1:65;
   end; then
   0 <= Sum sqr (x|n) by Lm1; then
A8:|.(x|n).|^2 + (x.(n+1))^2 = Sum sqr (x|n) + (x.(n+1))^2 
      by A7,SQUARE_1:def 4;
   (sqr(x|n))^<*(x.(n+1))^2*> = mlt(xn,xn)^<*(x.(n+1))^2*> by RVSUM_1:97
    .= mlt(xn,xn)^<*(x.(n+1))*(x.(n+1))*> by SQUARE_1:def 3
    .= mlt((xn)^<*x.(n+1)*>,(x|n)^<*x.(n+1)*>) by RFUNCT_4:2
    .= sqr x by A4,RVSUM_1:97;
   hence thesis by A6,A8,RVSUM_1:104;
end;

definition let n be Nat;
  let f be Function of NAT, REAL n, k be Nat;
  redefine func f.k -> Element of REAL n;
  coherence
  proof
    f.k in REAL n;
    hence thesis;
  end;
end;

theorem Lm11:
  for n be Nat
  for x be Point of REAL-NS n, xs be Element of REAL n,
    seq be sequence of REAL-NS n, xseq be Function of NAT, REAL n st
   xs = x & xseq = seq holds
    ((seq is convergent & lim seq = x)
  iff
  (for i be Nat st i in Seg n
    ex rseqi be Real_Sequence st
     for k be Nat holds rseqi.k = (xseq.k).i
   & rseqi is convergent & xs.i = lim rseqi))
proof
   defpred P[Nat] means
   for x be Point of REAL-NS $1,
       xs be Element of REAL $1,
       seq be sequence of REAL-NS $1,
       xseq be Function of NAT, REAL $1 st xs= x & xseq=seq holds
    ((seq is convergent & lim seq = x)
     iff
     (for i be Nat st i in Seg $1
       ex rseqi be Real_Sequence st
         for k be Nat holds rseqi.k = (xseq.k).i
       & rseqi is convergent & xs.i= lim rseqi));
P0:P[0]
   proof
    let x be Point of REAL-NS 0,
        xs be Element of REAL 0,
        seq be sequence of REAL-NS 0,
        xseq be Function of NAT,REAL 0;
    assume
A1:  xs= x & xseq=seq;
    now assume
     for i be Nat st i in Seg 0
      ex rseqi be Real_Sequence st for k be Nat holds rseqi.k = (xseq.k).i
      & rseqi is convergent & xs.i= lim rseqi;
     thus seq is convergent & lim seq = x
     proof
      xs = 0.REAL 0 by JORDAN2C:113,TARSKI:def 1 .= 0*0 by EUCLID:def 9; then
A8:   x=0.(REAL-NS 0) by DefRNS,A1;
A5:   for i be Nat holds seq.i=0.(REAL-NS 0)
      proof
       let i be Nat;
       xseq.i = 0.REAL 0 by JORDAN2C:113,TARSKI:def 1; then
       xseq.i = 0*0 by EUCLID:def 9;
       hence thesis by A1,DefRNS;
      end;
A6:   for r be Real st 0 < r
       ex m be Nat st
        for k be Nat st m <= k holds ||. seq.k - x .|| < r
      proof
       let r be Real;
       assume 
A7:    0 < r;
       take m = 1;
        let k be Nat;
        assume m <= k;
        ||. seq.k - x .|| = ||. 0.(REAL-NS 0) - 0.(REAL-NS 0) .|| by A8,A5
         .= ||. 0.(REAL-NS 0) .|| by RLVECT_1:28
         .= 0 by NORMSP_1:5;
        hence thesis by A7;
      end;
      hence seq is convergent by NORMSP_1:def 9;
      hence lim seq = x by A6,NORMSP_1:def 11;
     end;
    end;
    hence thesis by FINSEQ_1:4;
   end;
PN:now let n be Nat;
    assume 
P2: P[n];
    now let x be Point of REAL-NS (n+1),
            xs be Element of REAL (n+1),
            seq be sequence of REAL-NS (n+1),
            xseq be Function of NAT, REAL (n+1);
     assume 
A1:   xs= x & xseq=seq;
B1:   now 
       assume 
B2:    seq is convergent & lim seq = x;
       now 
        let i be Nat such that
A3:     i in Seg (n+1);
        deffunc F(Nat) = (xseq.$1).i;
        consider rseqi be Real_Sequence such that
A4:      for l be Nat holds rseqi.l = F(l) from SEQ_1:sch 1;
        rseqi is convergent & xs.i= lim rseqi
        proof
AA:      now let e be real number such that
A51:     e > 0;
          thus ex k be Nat st for m be Nat st
                k<=m holds abs(rseqi.m-xs.i) < e
          proof
           e is Real by XREAL_0:def 1; then 
           consider k be Nat such that
A52:       for m be Nat st m >= k holds
             ||.(seq.m) - x.|| < e by B2,A51,NORMSP_1:def 11;
           take k;
           let m be Nat;
           assume k<=m; then
A54:       ||.(seq.m) - x.|| < e by A52;
           len ((xseq.m) - xs) = (n+1) by EUCLID:2; then
           i in dom ((xseq.m) - xs) by FINSEQ_1:def 3,A3; then
A55:       (xseq.m).i-xs.i = ((xseq.m) - xs).i by RVSUM_1:47;
           (xseq.m)-xs = (seq.m) - x by A1,T8; then
A58:       abs((xseq.m).i-xs.i) <= ||.(seq.m) - x.|| by Lm3,A3,A55;
           rseqi.m-xs.i=(xseq.m).i-xs.i by A4;
           hence abs(rseqi.m-xs.i) < e by XREAL_1:2,A54,A58;
          end;
         end; then
         rseqi is convergent by SEQ_2:def 6;
         hence thesis by AA,SEQ_2:def 7; 
        end;
        hence ex rseqi be Real_Sequence st
           for k be Nat holds rseqi.k = (xseq.k).i
         & rseqi is convergent & xs.i= lim rseqi by A4;
       end; 
       hence for i be Nat st i in Seg (n+1)
         ex rseqi be Real_Sequence st
          for k be Nat holds rseqi.k = (xseq.k).i
        & rseqi is convergent & xs.i= lim rseqi;
      end;
      now assume 
B4:    (for i be Nat st i in Seg (n+1)
        ex rseqi be Real_Sequence st
          for k be Nat holds rseqi.k = (xseq.k).i
        & rseqi is convergent & xs.i= lim rseqi);
       thus seq is convergent & lim seq = x
       proof
        deffunc F1(Nat) = (xseq.$1) |n;
        defpred P1[Nat,Element of REAL n] means $2=(xseq.$1) |n;
PP0:    for i be Nat holds ex y be Element of REAL n st P1[i,y]
        proof
         let i be Nat;
         take y=(xseq.i) |n;
         len (xseq.i)= n+1 by EUCLID:2; then
         n <= len (xseq.i) by NAT_1:29; then
         len (xseq.i|n) = n by FINSEQ_1:80; then
         dom(xseq.i|n) = Seg n by FINSEQ_1:def 3;
         hence thesis by Lm001;
        end;
        consider xseqn be Function of NAT,REAL n such that
C01:     for i be Nat holds P1[i,xseqn.i] from FUNCT_2:sch 3(PP0);
        the carrier of REAL-NS n = REAL n by DefRNS; then
        reconsider seqn=xseqn as sequence of REAL-NS n;
        len xs= n+1 by EUCLID:2; then
        n <= len xs by NAT_1:29; then
        len (xs|n) = n by FINSEQ_1:80; then
        dom(xs|n) = Seg n by FINSEQ_1:def 3; then
        reconsider xsn=xs|n as Element of REAL n by Lm001;
        reconsider xn=xsn as Point of REAL-NS n by DefRNS;
An1:    xsn= xn & xseqn=seqn;
        for i be Nat st i in Seg n
         ex rseqi be Real_Sequence st
           for k be Nat holds rseqi.k = (xseqn.k).i
         & rseqi is convergent & xsn.i= lim rseqi
        proof     
         let i be Nat such that 
D2:      i in Seg n;
         n <= n+1 by NAT_1:29; then
         Seg n c= Seg (n+1) by FINSEQ_1:7; then
         consider rseqi be Real_Sequence such that
D3:      for k be Nat holds rseqi.k = (xseq.k).i
         & rseqi is convergent & xs.i= lim rseqi by B4,D2;
D4:      now let k be Nat;
          thus rseqi.k=(xseq.k).i by D3
                 .=((xseq.k) |Seg n).i by D2,FUNCT_1:72
                 .=((xseq.k) |n).i by FINSEQ_1:def 15
                 .=(xseqn.k).i by C01;
         end;
         xsn.i = (xs|Seg n).i by FINSEQ_1:def 15
              .=xs.i by D2,FUNCT_1:72;
         hence thesis by D3,D4;
        end; then
D6:     seqn is convergent & xn=lim seqn by An1,P2;
        n+1 in Seg (n+1) by FINSEQ_1:6; then
        consider rseqn1 be Real_Sequence such that
D7:     for k be Nat holds rseqn1.k = (xseq.k).(n+1)
        & rseqn1 is convergent & xs.(n+1)= lim rseqn1 by B4;
D8:     ||.seqn - xn.|| is convergent & lim ||.seqn - xn.|| = 0
          by D6,NORMSP_1:41;
        set seqn2 = ||.seqn - xn.|| (#) ||.seqn - xn.||;
D9:     seqn2 is convergent by SEQ_2:28,D8;
D10:    lim seqn2 = 0 * 0 by SEQ_2:29,D8 .= 0;
        deffunc F(Nat)=abs(rseqn1.$1 -xs.(n+1));
        consider absrseq be Real_Sequence such that
D11:    for i be Nat holds absrseq.i =F(i) from SEQ_1:sch 1;
D12:    absrseq is convergent & lim absrseq = 0
        proof
D121:    now let e be real number;
          assume
D122:     e > 0;
          consider m be Nat such that
D123:     for k be Nat st m <= k holds
          abs(rseqn1.k -xs.(n+1)) < e by D122,SEQ_2:def 7,D7;
          now let k be Nat;
          assume m <= k; then
          abs(abs(rseqn1.k -xs.(n+1))-0)  < e by D123; 
          hence abs(absrseq.k-0) < e by D11;
         end;
         hence ex m be Nat st
           for k be Nat st m <= k holds abs(absrseq.k-0) < e;
        end; then
        absrseq is convergent by SEQ_2:def 6;
        hence thesis by D121,SEQ_2:def 7;
       end;  
       set rseqn2 =absrseq (#) absrseq;
D13:   rseqn2 is convergent by SEQ_2:28,D12;
D14:   lim rseqn2 = 0 * 0 by SEQ_2:29,D12 .=0;
       set seq2 = ||.seq - x.|| (#) ||.seq - x.||;
D15:   seq2=seqn2+rseqn2
       proof
        now let k be Nat;
E7:     ||.seq - x.||.k = ||.(seq - x).k.|| by NORMSP_1:def 10
         .= ||.seq.k - x.|| by NORMSP_1:def 7;
E7N:    ||.seqn - xn.||.k = ||.(seqn - xn).k.|| by NORMSP_1:def 10
         .= ||.seqn.k - xn.|| by NORMSP_1:def 7;
        seq.k - x = xseq.k-xs by A1,T8; then
E8:     ||.seq.k - x.|| = |.xseq.k-xs .| by T1;
E8N:    seqn.k - xn = xseqn.k-xsn by T8;
        reconsider rxseqk= (xseq.k) as Element of (n+1)-tuples_on REAL
          by EUCLID:def 1;
        reconsider rxs= xs as Element of (n+1)-tuples_on REAL
          by EUCLID:def 1;
E10:    (xseq.k-xs).(n+1) = rxseqk.(n+1)-rxs.(n+1) by RVSUM_1:48
                         .= rseqn1.k - xs.(n+1) by D7;
E11:    (xseq.k-xs) |n=xseqn.k -xsn
        proof
         len (xseq.k-xs) = n+1 by EUCLID:2; then
         n <= len (xseq.k-xs) by NAT_1:29; then
P1:      len ((xseq.k-xs) |n) = n by FINSEQ_1:80; 
P2:      len (xseqn.k -xsn) = n by EUCLID:2; 
P3:      dom ((xseq.k-xs) |n) = Seg n by P1,FINSEQ_1:def 3;
P4:      dom(xseqn.k -xsn) = Seg n by P2,FINSEQ_1:def 3;
         now let i be Nat such that 
A1:       i in dom ((xseq.k-xs) |n);
A2:       i in Seg n by A1,P1,FINSEQ_1:def 3;
          reconsider xseq1 = xseq.k, xs1 = xs
            as Element of (n+1)-tuples_on REAL by EUCLID:def 1;
          reconsider xseq2 = xseqn.k, xs2 = xsn
            as Element of n-tuples_on REAL by EUCLID:def 1;
A3:       (xseq.k-xs).i = xseq1.i-xs1.i by RVSUM_1:48;
A4:       (xseqn.k-xsn).i = xseq2.i-xs2.i by RVSUM_1:48;
          thus ((xseq.k-xs) |n).i
            =((xseq.k-xs) |Seg n).i by FINSEQ_1:def 15
           .= (xseq.k-xs).i by A2,FUNCT_1:72
           .= ((xseq.k) |Seg n).i - xs.i by A2,A3,FUNCT_1:72
           .= ((xseq.k) |Seg n).i - (xs|Seg n).i by A2,FUNCT_1:72
           .= ((xseq.k) |n).i- (xs|Seg n).i by FINSEQ_1:def 15
           .= ((xseq.k) |n).i-(xs|n).i by FINSEQ_1:def 15
           .= (xseqn.k -xsn).i by A4,C01;
         end;
         hence thesis by P3,P4,FINSEQ_1:17;
        end; 
   E12: 0<= (rseqn1.k - xs.(n+1))^2 by SQUARE_1:72;
   E13: absrseq.k = abs(rseqn1.k - xs.(n+1)) by D11;
        thus seq2.k = ( ||.seq - x.||.k )*( ||.seq - x.||.k ) by SEQ_1:12
              .= |.xseq.k-xs .|^2 by SQUARE_1:def 3,E8,E7
              .= |.xseqn.k-xsn .|^2 +(rseqn1.k - xs.(n+1))^2 by Lm4,E10,E11
              .= ||. seqn.k-xn .||^2 +(rseqn1.k - xs.(n+1))^2 by E8N,T1
              .= ||. seqn.k-xn .||* ||. seqn.k-xn .|| +(rseqn1.k - xs.(n+1))^2
                      by SQUARE_1:def 3 
              .= ( ||.seqn - xn.|| (#) ||.seqn - xn.||).k
                + (rseqn1.k - xs.(n+1))^2 by SEQ_1:12,E7N
              .= seqn2.k + abs((rseqn1.k - xs.(n+1))^2) by E12,ABSVALUE:def 1
              .= seqn2.k + abs((rseqn1.k - xs.(n+1))*(rseqn1.k - xs.(n+1)))
                     by SQUARE_1:def 3
              .= seqn2.k + abs(rseqn1.k - xs.(n+1)) *abs(rseqn1.k - xs.(n+1))
                   by COMPLEX1:151
              .= seqn2.k + rseqn2.k by E13,SEQ_1:12;
       end;
       hence thesis by SEQ_1:11;
      end;
 D16: seq2 is convergent by SEQ_2:19,D9,D13,D15;
 D17: lim seq2 = 0+ 0 by SEQ_2:20,D9,D10,D13,D14,D15 .= 0;
 D18: for e be Real st e > 0 
       ex m be Nat st for k be Nat st k >= m holds ||.seq.k - x.|| < e 
      proof 
       let e be Real such that 
   E1: e > 0;
       e*0 < e*e by E1,XREAL_1:99; then
       consider m be Nat such that
  E5:  for k be Nat st m<=k holds abs(seq2.k-0) < e*e
         by D16,D17,SEQ_2:def 7;
       now let k be Nat such that
     E6:m<= k;
     E7:||.seq - x.||.k = ||.(seq - x).k.|| by NORMSP_1:def 10
                       .= ||.seq.k - x.|| by NORMSP_1:def 7;
     E8:seq2.k = ( ||.seq.k - x.|| )*( ||.seq.k - x.|| ) by E7,SEQ_1:12;
     E9:0 <= ||.seq.k - x.|| by NORMSP_1:8; then
    E10:0 * ( ||.seq.k - x.||) <= ( ||.seq.k - x.|| )*( ||.seq.k - x.||)
         by XREAL_1:66;
        abs(seq2.k-0 ) = ||.seq.k - x.|| * ||.seq.k - x.||
          by E10,E8,ABSVALUE:def 1; then
    E13:||.seq.k - x.|| * ||.seq.k - x.|| < e*e by E5,E6;
    E14:sqrt(e*e) = sqrt(e^2) by SQUARE_1:def 3 .= e by E1,SQUARE_1:89;
        sqrt( ||.seq.k - x.|| * ||.seq.k - x.|| )
                            = sqrt( ||.seq.k - x.||^2) by SQUARE_1:def 3
                           .= ||.seq.k - x.|| by E9,SQUARE_1:89;
        hence ||.seq.k - x.|| < e by SQUARE_1:95,E10,E13,E14;
       end;
       hence thesis;
      end; then 
      seq is convergent by NORMSP_1:def 9;
      hence thesis by D18,NORMSP_1:def 11;
     end;
    end;
    hence seq is convergent & lim seq = x
     iff (for i be Nat st i in Seg (n+1)
           ex rseqi be Real_Sequence st
            for k be Nat holds rseqi.k = (xseq.k).i
          & rseqi is convergent & xs.i= lim rseqi) by B1;
    end;
    hence P[n+1];
   end;
   thus for n be Nat holds P[n] from NAT_1:sch 1(P0,PN);
end;

theorem T10:
  for f be sequence of REAL-NS n st
    f is Cauchy_sequence_by_Norm holds f is convergent
proof
   let vseq be sequence of REAL-NS n such that
A1:vseq is Cauchy_sequence_by_Norm;
P0:the carrier of REAL-NS n = REAL n by DefRNS;
   reconsider xvseq=vseq as Function of NAT,REAL n by P0;
   defpred P[set, set] means
    ex rseqi be Real_Sequence st
     for l be Nat holds rseqi.l = (xvseq.l).$1
   & rseqi is convergent & $2 = lim rseqi;
A2:for i be Nat st i in Seg n ex y be Element of REAL st P[i,y]
   proof
    let i be Nat such that
A3: i in Seg n;
    deffunc F(Nat) = (xvseq.$1).i;
    consider rseqi be Real_Sequence such that
A4:  for l be Nat holds rseqi.l = F(l) from SEQ_1:sch 1;
A5: rseqi is convergent
    proof
     now let e be real number such that
A51:  e > 0;
      thus ex k be Nat st for m be Nat st
        k<=m holds abs(rseqi.m -rseqi.k) < e
      proof
       e is Real by XREAL_0:def 1; then
       consider k be Nat such that
A52:   for n, m be Nat st n >= k & m >= k holds
         ||.(vseq.n) - (vseq.m).|| < e by A1,A51,RSSPACE3:10;
       take k;
       let m be Nat such that
A53:   k<=m;
A54:   ||.(vseq.m) - (vseq.k).|| < e by A52,A53;
        len ((xvseq.m) - (xvseq.k)) =n by EUCLID:2; then
        i in dom ((xvseq.m) - (xvseq.k)) by FINSEQ_1:def 3,A3; then
A55:   (xvseq.m).i-(xvseq.k).i = ((xvseq.m) - (xvseq.k)).i by RVSUM_1:47;
        (xvseq.m)-(xvseq.k) = (vseq.m) - (vseq.k) by T8; then
A58:   abs((xvseq.m).i-(xvseq.k).i)
                   <= ||.(vseq.m) - (vseq.k).|| by A55,Lm3,A3;
       rseqi.m=(xvseq.m).i & rseqi.k=(xvseq.k).i by A4;
       hence abs(rseqi.m-rseqi.k) < e by XREAL_1:2,A54,A58;
      end;
     end;
     hence thesis by SEQ_4:58;
    end;
    take lim rseqi;
    thus thesis by A4,A5;
   end;
   consider f be FinSequence of REAL such that
A10:dom f = Seg n & for k be Nat st k in Seg n holds P[k,f.k]
     from FINSEQ_1:sch 5(A2);
   reconsider tseq=f as Element of REAL n by Lm001,A10;
   reconsider xseq=tseq as Point of REAL-NS n by DefRNS;
   xseq=tseq & xvseq=vseq;
   hence thesis by Lm11,A10;
end;

Cosik:
  REAL-NS n is RealBanachSpace
proof
  for seq be sequence of REAL-NS n st
    seq is Cauchy_sequence_by_Norm holds seq is convergent by T10;
  hence thesis by LOPBAN_1:def 16;
end;

registration let n;
  cluster REAL-NS n -> complete;
  coherence by Cosik;
end;

begin :: R^n space as RealNormSpace

definition let n be Nat;
  func Euclid_scalar n -> Function of [:REAL n,REAL n:],REAL means :DefSCALAR:
  for x,y being Element of REAL n holds
  it.(x,y) = Sum mlt(x,y);
existence
proof
  deffunc F(Element of REAL n,Element of REAL n) = Sum mlt($1,$2);
  ex f be Function of [:REAL n, REAL n:], REAL st
   for x be Element of REAL n,y be Element of REAL n
      holds f.(x,y) = F(x,y) from BINOP_1:sch 4;
  hence thesis;
end;
uniqueness
proof
   let scalar1,scalar2 be Function of [: REAL n, REAL n :], REAL such that
A1:for x,y be Element of REAL n holds scalar1.(x,y) = Sum mlt(x,y) and
A2:for x,y be Element of REAL n holds scalar2.(x,y) = Sum mlt(x,y);
   for x,y be Element of REAL n holds scalar1.(x,y) = scalar2.(x,y)
   proof
    let x,y be Element of REAL n;
    scalar1.(x,y) = Sum mlt(x,y) by A1;
    hence thesis by A2;
   end;
   hence thesis by BINOP_1:2;
end;
end;

definition let n be Nat;
  func REAL-US n -> strict non empty UNITSTR means :DefRUS:
  the carrier of it = REAL n &
  the Zero of it = 0*n &
  the add of it = Euclid_add n &
  the Mult of it = Euclid_mult n &
  the scalar of it = Euclid_scalar n;
existence
proof
  take UNITSTR(#REAL n, 0*n, Euclid_add n, Euclid_mult n, Euclid_scalar n#);
  thus thesis;
end;
uniqueness;
end;

registration let n be non empty Nat;
  cluster REAL-US n -> non trivial;
coherence
proof
  the carrier of REAL-US n = REAL n by DefRUS
   .= the carrier of TOP-REAL n by EUCLID:25;
  hence thesis by REALSET2:def 5;
end;
end;

registration let n be Nat;
  cluster REAL-US n -> RealUnitarySpace-like RealLinearSpace-like
      Abelian add-associative right_zeroed right_complementable;
coherence
proof
   thus REAL-US n is RealUnitarySpace-like
   proof
    now let x,y,z be Point of REAL-US n, a be Real;
    reconsider x1=x, y1=y, z1=z as Element of REAL n by DefRUS;
    reconsider x2=x1, y2=y1, z2=z1 as Element of n-tuples_on REAL
      by EUCLID:def 1;
C1:  len x2 = n & len y2 = n & len z2 = n by FINSEQ_2:109;
A1:  x .|. x = (Euclid_scalar n).(x,x) by DefRUS
            .= Sum mlt(x1,x1) by DefSCALAR;
C2:  x .|. z = (Euclid_scalar n).(x,z) by DefRUS
            .= Sum mlt(x1,z1) by DefSCALAR;
C3:  y .|. z = (Euclid_scalar n).(y,z) by DefRUS
            .= Sum mlt(y1,z1) by DefSCALAR;
A2:  for k be Nat st k in dom mlt(x1,x1) holds 0 <= mlt(x1,x1).k
     proof
      let k be Nat;
      assume k in dom mlt(x1,x1); then
      mlt(x1,x1).k = x1.k * x1.k by RVSUM_1:86;
      hence thesis by XREAL_1:65;
     end;
     hence 0 <= x .|. x by A1,RVSUM_1:114;
     thus x .|. x = 0 iff x = 0.(REAL-US n)
     proof
      now assume
B1:    x .|. x = 0 & x <> 0.(REAL-US n);
       for k be Nat st k in dom x2 holds x2.k = 0
       proof
        let k be Nat;
        assume 
B3:     k in dom x2;
B2:     dom multreal = [:REAL,REAL:] by FUNCT_2:def 1;
        [:rng x1, rng x1:] c= dom multreal by B2,ZFMISC_1:119; then
        dom(multreal.:(x1,x1)) = dom x1 /\ dom x1 by FUNCOP_1:84; then
A5:     k in dom mlt(x1,x1) by B3,RVSUM_1:def 9; then
        0 <= mlt(x1,x1).k & mlt(x1,x1).k <= 0 by B1,A1,A2,RVSUM_1:115; then
        mlt(x1,x1).k = 0; then
        x1.k * x1.k = 0 by A5,RVSUM_1:86; then
        (x1.k)^2 = 0 by SQUARE_1:def 3;
        hence thesis by SQUARE_1:74;
       end; then
       x1 = n |-> (0 qua Real) by RFUNCT_4:4; then
       x = 0*n by EUCLID:def 4;
       hence contradiction by B1,DefRUS;
      end;
      hence x .|. x = 0 implies x = 0.(REAL-US n);
      assume x = 0.(REAL-US n); then
      x = 0*n by DefRUS
       .= n |-> (0 qua Real) by EUCLID:def 4; then
      mlt(x2,x2) = 0 * x2 by RVSUM_1:92 .= n |-> 0 by RVSUM_1:75;
      hence x .|. x = 0 by A1,RVSUM_1:111;
     end;
     thus x .|. y = (Euclid_scalar n).(x,y) by DefRUS
                 .= Sum mlt(y1,x1) by DefSCALAR
                 .= (Euclid_scalar n).(y,x) by DefSCALAR
                 .= y .|. x by DefRUS;
     thus (x+y) .|. z = (Euclid_scalar n).(x+y,z) by DefRUS
             .= (Euclid_scalar n).((Euclid_add n).(x1,y1),z1) by DefRUS
             .= (Euclid_scalar n).(x1+y1,z1) by DefADD
             .= Sum mlt(x1+y1,z1) by DefSCALAR
             .= Sum ( mlt(x1,z1)+mlt(y1,z1) ) by C1,EUCLID_2:9
             .= Sum mlt(x2,z2) + Sum mlt(y2,z2) by RVSUM_1:119
             .= x .|. z + y .|. z by C2,C3;
     reconsider ax1=a*x1, ay1=a*y1 as Element of REAL n;
     reconsider ax2=a*x2, ay2=a*y2 as Element of n-tuples_on REAL;
     a*x is Element of REAL n by DefRUS; then
     reconsider ax = a*x as Element of n-tuples_on REAL by EUCLID:def 1;
V2:  a*x1 is Element of n-tuples_on REAL by EUCLID:def 1;
V3:  for k be Nat st k in Seg n holds (ax).k = (a*x1).k
     proof
      let k be Nat;
      assume k in Seg n;
      reconsider a as Element of REAL;
      reconsider a1 = n |-> a as Element of n-tuples_on REAL by FINSEQ_2:132;
      a*x = (Euclid_mult n).(a,x1) by DefRUS
         .= a*x1 by DefMULT;
      hence thesis;
     end; 
     thus (a*x) .|. y = (Euclid_scalar n).(a*x,y) by DefRUS
             .= (Euclid_scalar n).(a*x1,y1) by V2,V3,FINSEQ_2:139
             .= Sum mlt(a*x1,y1) by DefSCALAR
             .= Sum (a*mlt(x2,y2)) by RVSUM_1:94
             .= a * Sum mlt(x2,y2) by RVSUM_1:117
             .= a * (Euclid_scalar n).(x1,y1) by DefSCALAR
             .= a * ( x .|. y ) by DefRUS;
    end;
    hence thesis by BHSP_1:def 2;
   end;
   thus REAL-US n is RealLinearSpace-like
   proof
    thus for a be Real for v,w be VECTOR of REAL-US n holds
     a * (v + w) = a * v + a * w
    proof
     let a be Real;
     let v,w be VECTOR of REAL-US n;
     reconsider v1=v, w1=w as Element of REAL n by DefRUS;
     reconsider v2=v1, w2=w1 as Element of n-tuples_on REAL by EUCLID:def 1;
P1:  a * (v + w) = (Euclid_mult n).(a,v+w) by DefRUS
                .= (Euclid_mult n).(a,(Euclid_add n).(v1,w1)) by DefRUS
                .= (Euclid_mult n).(a,v1+w1) by DefADD
                .= a*(v1+w1) by DefMULT
                .= a*v2 + a*w2 by RVSUM_1:73;
     a*v is Element of REAL n by DefRUS; then
     reconsider av = a*v as Element of n-tuples_on REAL by EUCLID:def 1;
V2:  a*v1 is Element of n-tuples_on REAL by EUCLID:def 1;
V3:  for k be Nat st k in Seg n holds av.k = (a*v1).k
     proof
      let k be Nat;
      assume k in Seg n;
::      reconsider a as Element of REAL;
      reconsider a1 = n |-> a as Element of n-tuples_on REAL by FINSEQ_2:132;
      a*v = (Euclid_mult n).(a,v1) by DefRUS
         .= a*v1 by DefMULT;
      hence thesis;
     end; 
     a*w is Element of REAL n by DefRUS; then
     reconsider aw = a*w as Element of n-tuples_on REAL by EUCLID:def 1;
W2:  a*w1 is Element of n-tuples_on REAL by EUCLID:def 1;
     for k be Nat st k in Seg n holds aw.k = (a*w1).k
     proof
      let k be Nat;
      assume k in Seg n;
      reconsider a as Element of REAL;
      reconsider a1 = n |-> a as Element of n-tuples_on REAL by FINSEQ_2:132;
      a*w = (Euclid_mult n).(a,w1) by DefRUS
         .= a*w1 by DefMULT;
      hence thesis;
     end; then
W1:  a*w1 = a*w by W2,FINSEQ_2:139;
     a*v + a*w = (Euclid_add n).(a*v,a*w) by DefRUS
              .= (Euclid_add n).(a*v1,a*w1) by V2,V3,FINSEQ_2:139,W1
              .= a*v2 + a*w2 by DefADD;
     hence thesis by P1;
    end;
    thus for a,b be Real for v be VECTOR of REAL-US n holds
     (a + b) * v = a * v + b * v
    proof
     let a,b be Real;
     let v be VECTOR of REAL-US n;
     reconsider v1=v as Element of REAL n by DefRUS;
     reconsider v2=v1 as Element of n-tuples_on REAL by EUCLID:def 1;
P1:  (a+b)*v = (Euclid_mult n).(a+b,v1) by DefRUS
            .= (a+b)*v2 by DefMULT
            .= a*v1 + b*v1 by RVSUM_1:72; 
     a*v is Element of REAL n by DefRUS; then
     reconsider av = a*v as Element of n-tuples_on REAL by EUCLID:def 1;
V2:  a*v1 is Element of n-tuples_on REAL by EUCLID:def 1;
V3:  for k be Nat st k in Seg n holds av.k = (a*v1).k
     proof
      let k be Nat;
      assume k in Seg n;
      reconsider a as Element of REAL;
      reconsider a1 = n |-> a as Element of n-tuples_on REAL by FINSEQ_2:132;
      a*v = (Euclid_mult n).(a,v1) by DefRUS
         .= a*v1 by DefMULT;
      hence thesis;
     end; 
     b*v is Element of REAL n by DefRUS; then
     reconsider bv = b*v as Element of n-tuples_on REAL by EUCLID:def 1;
W2:  b*v1 is Element of n-tuples_on REAL by EUCLID:def 1;
     for k be Nat st k in Seg n holds bv.k = (b*v1).k
     proof
      let k be Nat;
      assume k in Seg n;
      reconsider b as Element of REAL;
      reconsider b1 = n |-> b as Element of n-tuples_on REAL by FINSEQ_2:132;
      b*v = (Euclid_mult n).(b,v1) by DefRUS
         .= b*v1 by DefMULT;
      hence thesis;
     end; then
W1:  b*v1 = b*v by W2,FINSEQ_2:139;
     a*v + b*v = (Euclid_add n).(a*v,b*v) by DefRUS
              .= (Euclid_add n).(a*v1,b*v1) by V2,V3,FINSEQ_2:139,W1
              .= a*v2 + b*v2 by DefADD;
     hence thesis by P1;
    end;
    thus for a,b be Real for v be VECTOR of REAL-US n holds
     (a * b) * v = a * (b * v)
    proof
     let a,b be Real;
     let v be VECTOR of REAL-US n;
     reconsider v1=v as Element of REAL n by DefRUS;
     reconsider v2=v1 as Element of n-tuples_on REAL by EUCLID:def 1;
     b*v is Element of REAL n by DefRUS; then
     reconsider bv = b*v as Element of n-tuples_on REAL by EUCLID:def 1;
W2:  b*v1 is Element of n-tuples_on REAL by EUCLID:def 1;
     for k be Nat st k in Seg n holds bv.k = (b*v1).k
     proof
      let k be Nat;
      assume k in Seg n;
      reconsider b as Element of REAL;
      reconsider b1 = n |-> b as Element of n-tuples_on REAL by FINSEQ_2:132;
      b*v = (Euclid_mult n).(b,v1) by DefRUS
         .= b*v1 by DefMULT;
      hence thesis;
     end; then
W1:  b*v1 = b*v by W2,FINSEQ_2:139;
P:   (a*b)*v = (Euclid_mult n).(a*b,v1) by DefRUS
            .= (a*b)*v2 by DefMULT
            .= a*(b*v2) by RVSUM_1:71;
     a*(b*v) = (Euclid_mult n).(a,b*v1) by W1,DefRUS
            .= a*(b*v2) by DefMULT;
     hence thesis by P;
    end;
    for v being VECTOR of REAL-US n holds 1 * v = v
    proof
     let v be VECTOR of REAL-US n;
     reconsider v1=v as Element of REAL n by DefRUS;
     reconsider v2=v1 as Element of n-tuples_on REAL by EUCLID:def 1;
     1 * v = (Euclid_mult n).(1,v1) by DefRUS
        .= 1 * v2 by DefMULT
        .= v2 by RVSUM_1:74;
     hence thesis;
    end;
    hence thesis;
   end;
   thus REAL-US n is Abelian
   proof
    let v,w be VECTOR of REAL-US n;
    reconsider v1 = v, w1 = w as Element of REAL n by DefRUS;
    thus v + w = (Euclid_add n).(v,w) by DefRUS
         .= (Euclid_add n).(w1,v1) by BINOP_1:def 2
         .= w + v by DefRUS;
   end;
   for u,v,w being Element of REAL-US n holds (u + v) + w = u + (v + w)
   proof
    let u,v,w be Element of REAL-US n;
    reconsider u1=u, v1=v, w1=w as Element of REAL n by DefRUS;
    reconsider u2=u1, v2=v1, w2=w1 as Element of n-tuples_on REAL 
      by EUCLID:def 1;
P:  (u + v) + w = (Euclid_add n).(u+v,w1) by DefRUS
               .= (Euclid_add n).((Euclid_add n).(u1,v1),w1) by DefRUS
               .= (Euclid_add n).(u1+v1,w1) by DefADD
               .= (u1+v1)+w2 by DefADD
               .= u2+(v2+w2) by RVSUM_1:32;
    u + (v + w) = (Euclid_add n).(u1,v+w) by DefRUS
               .= (Euclid_add n).(u2,(Euclid_add n).(v2,w2)) by DefRUS
               .= (Euclid_add n).(u2,v1+w1) by DefADD;
    hence thesis by P,DefADD;
   end;
   hence REAL-US n is add-associative by RLVECT_1:def 6;
   for v being Element of REAL-US n holds v + 0.(REAL-US n) = v
   proof
    let v be Element of REAL-US n;
    reconsider v1=v as Element of REAL n by DefRUS;
    v + 0.(REAL-US n) = (Euclid_add n).(v,0.(REAL-US n)) by DefRUS
     .= (Euclid_add n).(v1,0*n) by DefRUS
     .= v1 + 0*n by DefADD;
    hence thesis by EUCLID_4:1;
   end;
   hence REAL-US n is right_zeroed by RLVECT_1:def 7;
   for v being Element of REAL-US n
     ex w being Element of REAL-US n st v + w = 0.(REAL-US n)
   proof
    let v be Element of REAL-US n;
    reconsider v1=v as Element of REAL n by DefRUS;
    reconsider w = -v1 as Element of REAL-US n by DefRUS;
    v + w = (Euclid_add n).(v1,-v1) by DefRUS
         .= v1 + -v1 by DefADD
         .= 0*n by EUCLIDLP:7
         .= 0.(REAL-US n) by DefRUS;
    hence thesis;
   end;
   hence REAL-US n is right_complementable by RLVECT_1:def 8;
end;
end;

theorem T2:
  for n be Nat, a be Real,
   x1,y1 be Point of REAL-NS n, x2,y2 be Point of REAL-US n st
    x1 = x2 & y1 = y2 holds
     x1 + y1 = x2 + y2 & -x1 = -x2 & a * x1 = a * x2
proof
   let n be Nat, a be Real;
   let x1,y1 be Point of REAL-NS n;
   let x2,y2 be Point of REAL-US n;
   assume 
A1:x1 = x2 & y1 = y2;
   reconsider x=x1, y=y1 as Element of REAL n by DefRNS;
   thus x1 + y1 = (Euclid_add n).(x,y) by DefRNS
          .= x2 + y2 by A1,DefRUS;
   thus -x1 = (-1)*x1 by RLVECT_1:29
           .= (Euclid_mult n).(-1,x) by DefRNS
           .= (-1)*x2 by A1,DefRUS
           .= -x2 by RLVECT_1:29;
   thus a * x1 = (Euclid_mult n).(a,x) by DefRNS
              .= a*x2 by A1,DefRUS;
end;

theorem
  for n be Nat, x1 be Point of REAL-NS n, x2 be Point of REAL-US n st
    x1 = x2 holds ||.x1.||^2 = x2 .|. x2
proof
   let n be Nat,
       x1 be Point of REAL-NS n,
       x2 be Point of REAL-US n;
   assume
A1:x1 = x2;
   reconsider x=x1 as Element of REAL n by DefRNS;
   thus ||.x1.||^2 = |.x.|^2 by T1
             .= |(x,x)| by EUCLID_2:12
             .= Sum mlt(x,x) by EUCLID_2:def 1
             .= (Euclid_scalar n).(x,x) by DefSCALAR
             .= x2 .|. x2 by A1,DefRUS;
end;
  
theorem T5:
  for n be Nat, f be set holds
    f is sequence of REAL-NS n iff
      f is sequence of REAL-US n
proof
  let n be Nat, f be set;
  the carrier of REAL-NS n = REAL n by DefRNS
    .= the carrier of REAL-US n by DefRUS;
  hence thesis;
end;

theorem T3:
  for n be Nat,
      seq1 be sequence of REAL-NS n, seq2 be sequence of REAL-US n st
    seq1 = seq2 holds
    (seq1 is convergent implies seq2 is convergent & lim seq1 = lim seq2) &
    (seq2 is convergent implies seq1 is convergent & lim seq1 = lim seq2)
proof
   let n be Nat;
   let seq1 be sequence of REAL-NS n;
   let seq2 be sequence of REAL-US n;
   assume
A0:seq1 = seq2;
B0:the carrier of REAL-NS n = REAL n by DefRNS
    .= the carrier of REAL-US n by DefRUS;
   now assume
B1: seq1 is convergent; then
    consider RNg be Point of REAL-NS n such that
A1:  for r be Real st 0 < r
      ex m be Nat st for k be Nat st m <= k holds
       ||.(seq1.k) - RNg.|| < r by NORMSP_1:def 9;
    reconsider RUg = RNg as Point of REAL-US n by B0;
    for r be Real st 0 < r
     ex m be Nat st for k be Nat st m <= k holds
      dist(seq2.k, RUg) < r
    proof
     let r be Real;
     assume 0 < r; then
     consider m be Nat such that
A12:  for k be Nat st m <= k holds
       ||.(seq1.k) - RNg.|| < r by A1;
A13: for k be Nat st m <= k holds dist(seq2.k, RUg) < r
     proof
      let k be Nat;
      assume 
A14:  m <= k;
      reconsider p = seq1.k - RNg as Element of REAL n by DefRNS;
      seq1.k = seq2.k & -RNg = -RUg by A0,T2; then
A16:  p = seq2.k - RUg by T2;
      ||.(seq1.k) - RNg.|| = |.p.| by T1
        .= sqrt |(p,p)| by EUCLID_2:13
        .= sqrt Sum mlt(p,p) by EUCLID_2:def 1
        .= sqrt ((Euclid_scalar n).(p,p)) by DefSCALAR
        .= ||. seq2.k - RUg .|| by A16,DefRUS;
      hence thesis by A12,A14;
     end;
     take m;
     thus thesis by A13;
    end;
    hence
A2: seq2 is convergent by BHSP_2:def 1;
    reconsider LIMIT = lim seq1 as Point of REAL-US n by B0;
    for r be Real st r > 0
     ex m be Nat st for k be Nat st k >= m holds 
      dist((seq2.k), LIMIT) < r
    proof
     let r be Real;
     assume r > 0; then
     consider m be Nat such that
A22: for k be Nat st m <= k holds
       ||.(seq1.k) - lim seq1.|| < r by B1,NORMSP_1:def 11;
A23: for k be Nat st m <= k holds dist((seq2.k), LIMIT) < r
     proof
      let k be Nat;
      assume AB: m <= k;
      reconsider p = seq1.k - lim seq1 as Element of REAL n by DefRNS;
      seq1.k = seq2.k & -lim seq1 = -LIMIT by A0,T2; then
A25:  p = seq2.k - LIMIT by T2;
      ||.(seq1.k) - lim seq1.|| = |.p.| by T1
       .= sqrt |(p,p)| by EUCLID_2:13
       .= sqrt Sum mlt(p,p) by EUCLID_2:def 1
       .= sqrt ((Euclid_scalar n).(p,p)) by DefSCALAR
       .= ||. seq2.k-LIMIT .|| by A25,DefRUS;
      hence thesis by AB,A22;
     end;
     take m;
     thus thesis by A23;
    end;
    hence lim seq2 = lim seq1 by A2,BHSP_2:def 2;
   end;
   hence seq1 is convergent implies seq2 is convergent & lim seq1 = lim seq2;
   now assume
A30:seq2 is convergent; then
    consider RUg be Point of REAL-US n such that
A3:  for r be Real st 0 < r
      ex m be Nat st for k be Nat st m <= k holds
       dist(seq2.k, RUg) < r by BHSP_2:def 1;
    reconsider RNg = RUg as Point of REAL-NS n by B0;
    for r be Real st 0 < r
     ex m be Nat st for k be Nat st m <= k holds
      ||.(seq1.k)-RNg.|| < r
    proof
     let r be Real;
     assume 0 < r; then
     consider m be Nat such that
A31: for k be Nat st m <= k holds dist(seq2.k, RUg) < r by A3;
     take m;
     for k be Nat st m <= k holds ||.(seq1.k)-RNg.|| < r
     proof
      let k be Nat;
      assume m <= k; then
A32:  dist(seq2.k,RUg) < r by A31;
      reconsider p = seq2.k - RUg as Element of REAL n by DefRUS;
      seq1.k = seq2.k & -RNg = -RUg by A0,T2; then
A33:  p = seq1.k - RNg by T2;
      dist(seq2.k, RUg) = sqrt ((Euclid_scalar n).(p,p)) by DefRUS
       .= sqrt Sum mlt(p,p) by DefSCALAR
       .= sqrt |(p,p)| by EUCLID_2:def 1
       .= |.p.| by EUCLID_2:13;
      hence thesis by A32,A33,T1;
     end;
     hence thesis;
    end;
    hence
A4:  seq1 is convergent by NORMSP_1:def 9;
    reconsider LIMIT = lim seq2 as Point of REAL-NS n by B0;
    for r be Real st r > 0
     ex m be Nat st for k be Nat st k >= m holds
      ||.(seq1.k)-LIMIT.|| < r
    proof
     let r be Real;
     assume r > 0; then
     consider m be Nat such that
A42:  for k be Nat st k >= m holds
       dist((seq2.k), lim seq2) < r by A30,BHSP_2:def 2;
     take m;
      let k be Nat;
      assume k >= m; then
A43:  dist(seq2.k,lim seq2) < r by A42;
      reconsider p = seq2.k - lim seq2 as Element of REAL n by DefRUS;
      seq1.k = seq2.k & -lim seq2 = -LIMIT by A0,T2; then
A44:  p = seq1.k - LIMIT by T2;
      dist(seq2.k, lim seq2) = sqrt ((Euclid_scalar n).(p,p)) by DefRUS
       .= sqrt Sum mlt(p,p) by DefSCALAR
       .= sqrt |(p,p)| by EUCLID_2:def 1
       .= |.p.| by EUCLID_2:13;
      hence thesis by A43,A44,T1;
    end;
    hence lim seq1 = lim seq2 by A4,NORMSP_1:def 11;
   end;
   hence thesis;
end;

theorem
  for n be Nat,
   seq1 be sequence of REAL-NS n, seq2 be sequence of REAL-US n st
   seq1 = seq2 & seq1 is Cauchy_sequence_by_Norm holds
   seq2 is Cauchy
proof
   let n be Nat,
       seq1 be sequence of REAL-NS n,
       seq2 be sequence of REAL-US n;
   assume that
A1:seq1 = seq2 and
A2:seq1 is Cauchy_sequence_by_Norm;
    let r be Real;
    assume r > 0; then
    consider k be Nat such that
A4: for n,m be Nat st n >= k & m >= k holds
      dist(seq1.n, seq1.m) < r by A2,RSSPACE3:def 5;
    take k;
    let m1,m2 be Nat;
    assume m1 >= k & m2 >= k; then
A5: dist(seq1.m1, seq1.m2) < r by A4;
    reconsider p = seq2.m1 - seq2.m2 as Element of REAL n by DefRUS;
    -seq1.m2 = -seq2.m2 by A1,T2; then
    seq1.m1 + (-seq1.m2) = seq2.m1 + (-seq2.m2) by A1,T2; then
    ||. seq1.m1 - seq1.m2 .|| = |.p.| by T1
       .= sqrt |(p,p)| by EUCLID_2:13
       .= sqrt Sum mlt(p,p) by EUCLID_2:def 1
       .= sqrt ((Euclid_scalar n).(p,p)) by DefSCALAR
       .= ||. seq2.m1-seq2.m2 .|| by DefRUS;
    hence thesis by A5;
end;

theorem T4:
  for n be Nat,
   seq1 be sequence of REAL-NS n, seq2 be sequence of REAL-US n st
    seq1 = seq2 & seq2 is Cauchy holds
    seq1 is Cauchy_sequence_by_Norm
proof
   let n be Nat,
       seq1 be sequence of REAL-NS n,
       seq2 be sequence of REAL-US n;
   assume that
A1:seq1 = seq2 and
A2:seq2 is Cauchy;
   let r be Real;
   assume r > 0; then
   consider k be Nat such that
A4:for m1,m2 be Nat st m1 >= k & m2 >= k holds
     dist(seq2.m1,seq2.m2) < r by A2,BHSP_3:def 1;
   take k;
   let m1,m2 be Nat;
   assume m1 >= k & m2 >= k; then
A5:dist(seq2.m1, seq2.m2) < r by A4;
   reconsider p = seq2.m1 - seq2.m2 as Element of REAL n by DefRUS;
   -seq1.m2 = -seq2.m2 by A1,T2;then
A7:p = seq1.m1 - seq1.m2 by A1,T2;
   ||. seq2.m1 - seq2.m2 .|| = sqrt ((Euclid_scalar n). (p,p)) by DefRUS
     .= sqrt Sum mlt(p,p) by DefSCALAR
     .= sqrt |(p,p)| by EUCLID_2:def 1
     .= |.p.| by EUCLID_2:13
     .= ||. seq1.m1 - seq1.m2 .|| by A7,T1;
   hence thesis by A5;
end;

Cosik:
  for n be Nat holds REAL-US n is Hilbert
proof
   let n be Nat;
   for seq be sequence of REAL-US n holds
    seq is_Cauchy_sequence implies seq is convergent
   proof
    let seq be sequence of REAL-US n;
    assume
A1: seq is_Cauchy_sequence;
    reconsider seq' = seq as sequence of REAL-NS n by T5;
    the carrier of REAL-US n = REAL n by DefRUS; then
    reconsider s = seq as Function of NAT,REAL n;
    the carrier of REAL-NS n = REAL n by DefRNS; then
    reconsider s' = seq' as Function of NAT,REAL n;
    seq' is Cauchy_sequence_by_Norm by A1,T4; then
    seq' is convergent by T10;
    hence thesis by T3;
   end; then
   REAL-US n is_complete_space by BHSP_3:def 6;
   hence thesis by BHSP_3:def 7;
end;

registration let n;
  cluster REAL-US n -> Hilbert;
  coherence by Cosik;
end;
