:: Concept of Fuzzy Relation and Basic Properties of Its Operation
::  by Takashi Mitsuishi , Katsumi Wasaki and Yasunari Shidama
::
:: Received September 15, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies RELAT_1, FUNCT_3, FUNCT_1, SQUARE_1, FUZZY_1, BOOLE, FUZZY_3,
      FUNCT_2;
 notations XBOOLE_0, ZFMISC_1, SUBSET_1, RELSET_1, FUNCT_2, RFUNCT_1, FUZZY_1,
      FUZZY_2, XXREAL_0;
 constructors RFUNCT_1, FUZZY_2, RCOMP_1, XCMPLX_0, XXREAL_0;
 registrations SUBSET_1, MEMBERED;
 theorems FUZZY_1, FUNCT_2, FUZZY_2;

begin

 reserve C1,C2 for non empty set;

registration let C be non empty set;
 cluster -> quasi_total Membership_Func of C;
 coherence
  proof let f be Membership_Func of C;
       dom f = C by FUZZY_1:def 1;
   hence thesis by FUNCT_2:def 1;
  end;
end;

definition let C1,C2 be non empty set;
  mode RMembership_Func of C1,C2 is Membership_Func of [:C1,C2:];
end;

 reserve f,g for RMembership_Func of C1,C2;

begin :: Empty Fuzzy Set and Universal Fuzzy Set

definition
  let C1,C2 be non empty set;
  func Zmf(C1,C2) -> RMembership_Func of C1,C2 equals
  chi({},[:C1,C2:]);
  correctness by FUZZY_1:13;

  func Umf(C1,C2) -> RMembership_Func of C1,C2 equals
  chi([:C1,C2:],[:C1,C2:]);
  correctness by FUZZY_1:2;
end;

theorem Th1: Zmf(C1,C2) = EMF [:C1,C2:] by FUZZY_1:def 7;

theorem Th2: Umf(C1,C2) = UMF [:C1,C2:] by FUZZY_1:def 8;

theorem
  for x be Element of [:C1,C2:],h be RMembership_Func of C1,C2 holds
  (Zmf(C1,C2)).x <= h.x & h.x <= (Umf(C1,C2)).x
  proof
    Zmf(C1,C2) = EMF [:C1,C2:] & Umf(C1,C2) = UMF [:C1,C2:] by Th1,Th2;
    hence thesis by FUZZY_1:17;
  end;

theorem
  max(f,Umf(C1,C2)) = Umf(C1,C2) & min(f,Umf(C1,C2)) = f &
  max(f,Zmf(C1,C2)) = f & min(f,Zmf(C1,C2)) = Zmf(C1,C2)
  proof
    Zmf(C1,C2) = EMF [:C1,C2:] & Umf(C1,C2) = UMF [:C1,C2:] by Th1,Th2;
    hence thesis by FUZZY_1:19;
  end;

theorem
  1_minus(Zmf(C1,C2)) = Umf(C1,C2) & 1_minus(Umf(C1,C2)) = Zmf(C1,C2)
  proof
    Zmf(C1,C2) = EMF [:C1,C2:] & Umf(C1,C2) = UMF [:C1,C2:] by Th1,Th2;
    hence thesis by FUZZY_1:44;
  end;

theorem
  f\g = Zmf(C1,C2) implies f c= g
  proof Zmf(C1,C2) = EMF [:C1,C2:] by Th1;
    hence thesis by FUZZY_2:31;
  end;

theorem
  min(f,g) = Zmf(C1,C2) implies f\g = f
  proof Zmf(C1,C2) = EMF [:C1,C2:] by Th1;
    hence thesis by FUZZY_2:32;
  end;

