:: Solving the Roots of the Special Polynomial Equation with Real 
:: Coefficients
::  by Yuzhong Ding and Xiquan Liang
:: 
:: Received March 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary SQUARE_1, ARYTM_3, ARYTM_1, GROUP_1, FUNCT_3, POWER, POLYEQ_1,
      POLYEQ_4, POLYEQ_2, RELAT_1;
 notation XREAL_0, REAL_1, SQUARE_1, POWER, PREPOWER, NAT_1, POLYEQ_1, QUIN_1,
      POLYEQ_2, XCMPLX_0, RAT_1, NEWTON;
 constructors TARSKI, REAL_1, NAT_1, SQUARE_1, POWER, PREPOWER, QUIN_1,
      POLYEQ_1, POLYEQ_2, NEWTON;
 clusters QUIN_1, NAT_1, REAL_1, NEWTON, SQUARE_1, XREAL_0;
 requirements SUBSET, NUMERALS, REAL, ARITHM;
 theorems AXIOMS, REAL_1, REAL_2, SQUARE_1, PREPOWER, QUIN_1, POLYEQ_1, POWER,
      NEWTON, POLYEQ_2, NAT_1, XCMPLX_1, XCMPLX_0;

begin

reserve x,y,x1,x2,y1,a, b, c,d,e,f,p,q for Real;
reserve  m,n for Nat;

theorem T1:
    a<>0 & b/a<0 & c/a>0 & delta(a,b,c) >=0 implies
    (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)>0  

proof
        assume that
 A100:   a <> 0 & b/a < 0 & c/a>0 &  delta(a,b,c) >=0;
 A102:   b^2-4*a*c >= 0 by A100,QUIN_1:def 1;         
 A104:   (b<0 & a>0) or (b>0 & a<0) by A100,REAL_2:135;
         now per cases by A104;
         case
 A108:   b<0 & a>0; then
 A110:   2*a>0 by REAL_2:122; 
 A120:   -b>0 by A108,REAL_1:66;
 A130:   c>0  by A108,A100,REAL_2:136;
        0 <= sqrt( b^2-4*a*c) by SQUARE_1:82,A102,SQUARE_1:94;then
        -b+sqrt(b^2-4*a*c)>0 +0 by REAL_1:67,A120;then
 A142:   (-b+sqrt(b^2-4*a*c))/(2*a)>0 by A110,REAL_2:127;
         4*a>0  by REAL_2:122,A108;then
         --4*a*c>0  by REAL_2:122,A130;then
         -4*a*c<0  by REAL_1:66;then
         b^2+(-4*a*c)<b^2+0  by REAL_1:67;then
         sqrt(b^2-4*a*c)<sqrt(b^2)  
                by SQUARE_1:95,A102;then
         sqrt(b^2-4*a*c)<-b  by SQUARE_1:90,A108;then
         -sqrt(b^2-4*a*c)>-(-b)  by REAL_1:50;then
         -sqrt(b^2-4*a*c)+(-b)>-(-b)+(-b)  by REAL_1:67;then
         (-b-sqrt(b^2-4*a*c))/(2*a)>0  by REAL_2:127,A110;
          hence
            (-b+sqrt delta(a,b,c))/(2*a)>0  
           & (-b-sqrt delta(a,b,c))/(2*a)>0  by A142,QUIN_1:def 1;      
          end;
         case
 A200:   b>0 & a<0;then
 A210:   a*2<0*2 by REAL_1:70; 
         --b>0 by A200;then
 A220:   -b<0 by  REAL_1:66;
 A230:   c<0  by A200,A100,REAL_2:136;
         0 <= sqrt( b^2-4*a*c) 
                   by A102,SQUARE_1:94,SQUARE_1:82;then
         -b+0<0+sqrt( b^2-4*a*c) by REAL_1:67,A220;then
         --(sqrt( b^2-4*a*c)+b)>0 by REAL_2:112;then
         -b-sqrt( b^2-4*a*c)<0 by REAL_1:66;then
 A232:   (-b-sqrt( b^2-4*a*c))/(2*a)>0 by REAL_2:127,A210;
         a*c>0 by A200,A230,REAL_2:122;then
         4*(a*c)>0 by REAL_2:122;then
         --4*a*c>0 ;then
         -4*a*c<0  by REAL_1:66;then
         b^2+(-4*a*c)<b^2+0  by REAL_1:67;then
         sqrt(b^2-4*a*c)<sqrt(b^2)
                    by SQUARE_1:95,A102;then
         sqrt(b^2-4*a*c)< b  by SQUARE_1:89,A200;then
         -b+sqrt(b^2-4*a*c)<0+b+-b  by REAL_1:67;then
         (-b+sqrt(b^2-4*a*c))/(2*a)>0  by REAL_2:127,A210;
          hence
            (-b+sqrt delta(a,b,c))/(2*a)>0  
           & (-b-sqrt delta(a,b,c))/(2*a)>0  by A232,QUIN_1:def 1;      
          end;
         end;
       hence thesis;
end;

theorem 
    a<>0 & b/a>0 & c/a>0 & delta(a,b,c) >=0 implies
    (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)<0  
proof
        assume that
 A100:   a <> 0 & b/a>0 & c/a>0 & delta(a,b,c) >=0;
 A102:   b^2-4*a*c >=0 by QUIN_1:def 1,A100;         
 A104:   (b>0 & a>0) or (b<0 & a<0) by A100,REAL_2:136;
         now per cases by A104;
         case
 A108:   b>0 & a>0; then
 A110:   2*a>0 by REAL_2:122; 
 A130:   c>0  by A108,A100,REAL_2:136;
         4*a>0  by REAL_2:122,A108;then
         --4*a*c>0  by REAL_2:122,A130;then
         -4*a*c<0  by REAL_1:66;then
         b^2+(-4*a*c)<b^2+0  by REAL_1:67;then
         sqrt(b^2-4*a*c)<sqrt(b^2)  
              by SQUARE_1:95,A102;then
         sqrt(b^2-4*a*c)<b  by SQUARE_1:89,A108;then
         -b+sqrt(b^2-4*a*c)<0+(b+-b)  by REAL_1:67;then
 A134:   (-b+sqrt(b^2-4*a*c))/(2*a)<0  by REAL_2:128,A110;
         0 <= sqrt( b^2-4*a*c)
                      by A102,SQUARE_1:94,SQUARE_1:82;then
         0+0<b+sqrt( b^2-4*a*c) by REAL_1:67,A108;then
         --(b+sqrt( b^2-4*a*c))>0 ;then
         -b-sqrt( b^2-4*a*c)<0 by REAL_1:66;then
         (-b-sqrt( b^2-4*a*c))/(2*a)<0 by REAL_2:128,A110;
          hence
            (-b+sqrt delta(a,b,c))/(2*a)<0  
           & (-b-sqrt delta(a,b,c))/(2*a)<0  by A134,QUIN_1:def 1;      
          end;
         case
 A200:   b<0 & a<0;then
 A210:   -b>0 & a*2<0*2 by REAL_1:70,REAL_1:66; 
 A230:   c<0  by A200,A100,REAL_2:136;
         0 <= sqrt( b^2-4*a*c)
                       by A102,SQUARE_1:94,SQUARE_1:82;then
         0+0<-b+sqrt( b^2-4*a*c) by REAL_1:67,A210;then
 A232:   (-b+sqrt( b^2-4*a*c))/(2*a)<0  by REAL_2:128,A210;
         a*c>0 by A200,A230,REAL_2:122;then
         4*(a*c)>0 by REAL_2:122;then
         --4*a*c>0  ;then
         -4*a*c<0  by REAL_1:66;then
         b^2+(-4*a*c)<b^2+0  by REAL_1:67;then
         sqrt(b^2-4*a*c)<sqrt(b^2)
                    by SQUARE_1:95,A102;then
         sqrt(b^2-4*a*c)< -b  by SQUARE_1:90,A200;then
         b+sqrt(b^2-4*a*c)<0+b+-b  by REAL_1:67;then
         -(b+sqrt(b^2-4*a*c))>0  by REAL_1:66;then
         (-b-sqrt(b^2-4*a*c))/(2*a)<0  by REAL_2:128,A210;
         hence
            (-b+sqrt delta(a,b,c))/(2*a)<0  
           & (-b-sqrt delta(a,b,c))/(2*a)<0  by A232,QUIN_1:def 1;      
         end;
         end;
       hence thesis;
end;
  
theorem 
    a<>0 &  c/a<0  implies
    (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0  
     or
    (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0  

proof
        assume that
 A100:   a <> 0  & c/a<0 ;         
 A104:   (c>0 & a<0) or (c<0 & a>0) by A100,REAL_2:135;
        now per cases by A104;
         case
 A108:   c>0 & a<0; then
 A110:   2*a<2*0 by REAL_1:70; 
         4*a<4*0  by REAL_1:70,A108;then
         4*a*c<0*c  by REAL_1:70,A108;then
 A112:   -4*a*c>0  by REAL_1:66;then
         b^2+(-4*a*c)>b^2+0  by REAL_1:67;then
 A120:   b^2-4*a*c>b^2  ;
 A123:   b^2>=0 by SQUARE_1:72;then 
 A122:   sqrt(b^2-4*a*c)>sqrt(b^2)  by A120,SQUARE_1:95;
         -4*a*c+b^2>0+0 by A112,A123,REAL_1:67;then
 A126:   --sqrt(b^2-4*a*c)>0
            by SQUARE_1:95,SQUARE_1:82;then
 A128:   -sqrt(b^2-4*a*c)<0 by REAL_1:66;

          now per cases;
           suppose
           B100:b>=0;then
                --b>=0 ;then
           B104:-b<=0 by REAL_2:109,REAL_1:26; 
                sqrt(b^2-4*a*c)>b  by A122,B100,SQUARE_1:89;then
                 -b+sqrt(b^2-4*a*c)>0+b+-b  by REAL_1:67;then
           B110:(-b+sqrt(b^2-4*a*c))/(2*a)<0  by A110,REAL_2:128;
                -sqrt(b^2-4*a*c)+-b<0+0 by A128,B104,REAL_1:67;then           
                -b-sqrt(b^2-4*a*c)<0 ;then
                -b-sqrt delta(a,b,c)<0 by QUIN_1:def 1;
           hence 
           (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0 
                                         by QUIN_1:def 1,REAL_2:127,A110,B110;
            end;
           suppose 
           C100:b<0;then
           C104:-b>0  by REAL_1:66;
                sqrt(b^2-4*a*c)>-b  by A122,C100,SQUARE_1:90;then
                --(b+sqrt(b^2-4*a*c))>0 by REAL_2:112;then
                -b-sqrt(b^2-4*a*c)<0 by REAL_1:66;then
           C110:(-b-sqrt(b^2-4*a*c))/(2*a)>0  by A110,REAL_2:127;
                sqrt(b^2-4*a*c)+(-b)>0+0  by C104,A126,REAL_1:67;then
                sqrt delta(a,b,c)+(-b)>0+0  by QUIN_1:def 1;

           hence 
           (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0 
                                         by QUIN_1:def 1,C110,A110,REAL_2:128;
           end;
          end;::now     
           hence 
           (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0; 
           end;
         case
 A200:   c<0 & a>0;then
 A210:   2*a>0 by REAL_2:122; 
         4*a>0  by REAL_2:122,A200;then
         4*a*c<4*a*0  by REAL_1:70,A200;then
 A212:   -4*a*c>0  by REAL_1:66;then
         b^2+(-4*a*c)>b^2+0  by REAL_1:67;then
 A220:   b^2-4*a*c>b^2 ;
 A223:   b^2>=0 by SQUARE_1:72;then 
 A222:   sqrt(b^2-4*a*c)>sqrt(b^2)  by A220,SQUARE_1:95;
         -4*a*c+b^2>0+0 by A212,A223,REAL_1:67;then
 A226:   --sqrt(b^2-4*a*c)>0 
          by SQUARE_1:82,SQUARE_1:95;then
 A228:   -sqrt(b^2-4*a*c)<0 by REAL_1:66;

          now per cases;
           suppose
           D100:b>=0;then
                --b>=0;then
           D104:-b<=0 by REAL_2:109,REAL_1:26; 
                sqrt(b^2-4*a*c)>b  by A222,D100,SQUARE_1:89;then
                 -b+sqrt(b^2-4*a*c)>0+b+-b  by REAL_1:67;then
           D110:(-b+sqrt(b^2-4*a*c))/(2*a)>0  by A210,REAL_2:127;
                -sqrt(b^2-4*a*c)+-b<0+0 by A228,D104,REAL_1:67;then           
                -b-sqrt(b^2-4*a*c)<0;then
                -b-sqrt delta(a,b,c)<0 by QUIN_1:def 1;

           hence 
           (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0 
                                         by REAL_2:128,A210,D110,QUIN_1:def 1;
           end;
           suppose 
           C100:b<0;then
           C104:-b>0  by REAL_1:66;
                sqrt(b^2-4*a*c)>-b  by A222,C100,SQUARE_1:90;then
                --(b+sqrt(b^2-4*a*c))>0 by REAL_2:112;then
                -b-sqrt(b^2-4*a*c)<0 by REAL_1:66;then
           C110:(-b-sqrt(b^2-4*a*c))/(2*a)<0  by A210,REAL_2:128;
                sqrt(b^2-4*a*c)+(-b)>0+0  by C104,A226,REAL_1:67;then
                sqrt delta(a,b,c)+(-b)>0  by QUIN_1:def 1;

           hence 
           (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0 
                                          by C110,A210,REAL_2:127,QUIN_1:def 1;
           end;
          end; 
           hence 
           (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0 ;
          end;
         end;::case
 hence thesis;
end;::proof

theorem T6:
     a>0 & (ex m st n=2*m & m>=1) & x |^
  n = a implies    
     x=n-root a or x=-n-root a
proof
        now  assume 
 A100:  a>0 & (ex m st n=2*m & m>=1) ;
        assume 
 A102:   x |^ n=a ;
        consider m such that
 A106:  n=2*m & m>=1 by A100;
 A109:  n>=1 by A106,REAL_2:137;then
        x<>0 by A102,A100,NEWTON:16;then
 A114:  x>0 or x<0 by AXIOMS:21;
        now per cases by A114;
        case  x>0 ;
           hence
        x=n-root a or x=-n-root a by A109,POWER:5,A102;
        end;
        case   x<0;then
 A134:  -x>0 by REAL_1:66;
        n-root a=n-root((-x)|^ n) by A100,A102,POWER:1;then
        (-1)*n-root a=(-1)*(-x) by A134,A109,POWER:5;
       hence
        x=n-root a or x=-n-root a  by XCMPLX_1:180;
       end;
       end;
    hence thesis;
  end;
    hence thesis;
 end;
      
theorem T8:
  a <> 0 &  Poly2(a,b,0,x) = 0 implies
       x=0 or  x = -(b/a)
proof
        assume that
 A100:  a<>0 &  Poly2(a,b,0,x) = 0;
        a*x^2+b*x+0=0 by POLYEQ_1:def 2,A100;then
        a*(x*x)+b*x+0*x=0 by SQUARE_1:def 3;then
        a*x*x+b*x+0*x=0;then
        (a*x+b+0)*x=0 by XCMPLX_1:9;then
        a*x+b+-b=0+-b or x=0 by XCMPLX_1:6;then
        a*x+(b+-b)=0+-b or x=0 by XCMPLX_1:1;then
        a*x=0+-b or x=0 ;then
        x=(-b)/a or x=0  by  A100,XCMPLX_1:90;
      hence
       x=0 or x=-b/a   by XCMPLX_1:188;
end;

theorem 
  a <> 0 &  Poly2(a,0,0,x) = 0 implies
       x=0 
proof
        assume that
 A100:  a<>0 &  Poly2(a,0,0,x) = 0;
        a*x^2+0*x+0=0 by POLYEQ_1:def 2,A100;then
        x^2=0  by XCMPLX_1:6,A100;
      hence
        x=0  by SQUARE_1:73;
end;

theorem 
  a <> 0  & (ex m st n=2*m+1) & delta(a,b,c) >= 0 
  & Poly2(a,b,c,x|^ n) = 0  implies 
       x = n-root((-b+sqrt delta(a,b,c))/(2*a))
       or x = n-root((-b-sqrt delta(a,b,c))/(2*a)) 
proof
       assume that
 A100:  a <>0 &  (ex m st n=2*m+1) & delta(a,b,c)>=0 & Poly2(a,b,c,x|^ n)=0;
        set y = x|^ n;
        x|^ n = (-b+sqrt delta(a,b,c))/(2*a) 
        or x|^ n = (-b-sqrt delta(a,b,c))/(2*a) by A100,POLYEQ_1:5;
      hence
        x = n-root((-b+sqrt delta(a,b,c))/(2*a))
        or x = n-root((-b-sqrt delta(a,b,c))/(2*a))  by POWER:5,A100;
end;

theorem 
  a <> 0  & b/a<0 & c/a>0 & (ex m st n=2*m & m>=1) & delta(a,b,c) >= 0 
  & Poly2(a,b,c,x|^ n) = 0  implies 
       x = n-root((-b+sqrt delta(a,b,c))/(2*a))
       or x = -n-root((-b+sqrt delta(a,b,c))/(2*a))
       or x = n-root((-b-sqrt delta(a,b,c))/(2*a)) 
       or x = -n-root((-b-sqrt delta(a,b,c))/(2*a)) 

proof
       assume that
 A100:  a <>0 & b/a<0 & c/a>0 & (ex m st n=2*m & m>=1) & delta(a,b,c)>=0 
        & Poly2(a,b,c,x|^ n)=0;
        set y = x|^ n;
 A120:  x|^ n = (-b+sqrt delta(a,b,c))/(2*a) 
        or x|^ n = (-b-sqrt delta(a,b,c))/(2*a)     by A100,POLYEQ_1:5;
 A130:  (-b+sqrt delta(a,b,c))/(2*a)>0 &(-b-sqrt delta(a,b,c))/(2*a)>0
                                                            by  A100,T1;
        now per cases by A120;
        suppose  x|^ n = (-b+sqrt delta(a,b,c))/(2*a) ;
         hence
           x = n-root((-b+sqrt delta(a,b,c))/(2*a))
           or x = -n-root((-b+sqrt delta(a,b,c))/(2*a))
           or x = n-root((-b-sqrt delta(a,b,c))/(2*a)) 
           or x = -n-root((-b-sqrt delta(a,b,c))/(2*a))
                                                  by A100,A130,T6;
          end;
         suppose x|^ n = (-b-sqrt delta(a,b,c))/(2*a);
         hence  
          x = n-root((-b+sqrt delta(a,b,c))/(2*a))
          or x = -n-root((-b+sqrt delta(a,b,c))/(2*a))
          or x = n-root((-b-sqrt delta(a,b,c))/(2*a)) 
          or x = -n-root((-b-sqrt delta(a,b,c))/(2*a)) 
                                                   by A100,A130,T6;
         end;
        end;
   hence thesis;  
end;

theorem 
  a <> 0 & (ex m st n=2*m+1) & Poly2(a,b,0,x|^ n) = 0 implies
       x=0 or  x = n-root -(b/a)
proof
        assume that
 A100:  a<>0 & (ex m st n=2*m+1) & Poly2(a,b,0,x|^ n) = 0;
        set y = x|^ n;
 A110:  x|^ n = 0 or x|^ n = -(b/a) by T8,A100;
        now per cases by A110;
        suppose 
         x|^ n = 0;
          hence x =0  or x=n-root -(b/a) by PREPOWER:12;   
        end;
         suppose x|^ n = -(b/a); 
          hence x =0  or x=n-root -(b/a) by A100,POWER:5;
       end;
       end;
  hence thesis;   
end;

theorem 
  a <> 0 & b/a<0 & (ex m st n=2*m & m>=1) & Poly2(a,b,0,x|^ n) = 0 implies
       x=0 or  x = n-root -(b/a) or x = -n-root -(b/a) 
proof
        assume that
 A100:  a<>0 & b/a<0 & (ex m st n=2*m & m>=1) & Poly2(a,b,0,x|^ n) = 0;
 A111:  -(b/a)>0 by REAL_1:66,A100;       
        set y = x|^ n;
 A110:  x|^ n = 0 or x|^ n = -(b/a) by T8,A100;
        now per cases by A110;
        suppose  x|^ n = 0;
         hence x =0  or x=n-root -(b/a) or x=-n-root -(b/a)  by PREPOWER:12;   
         end;
        suppose x|^ n = -(b/a); 
          hence x =0  or x=n-root -(b/a) or x=-n-root -(b/a) 
                                          by A100,A111,T6;
       end;
       end;
  hence thesis;   
end;

theorem T14:
  a|^3+b|^3 = (a+b)*(a^2-a*b+b^2) &
  a|^5+b|^5=  (a+b)*(a|^4-a|^3*b+a|^2*b|^2-a*b|^3+b|^4)
proof 
 A100: (a^2-a*b+b^2)*(a+b)
       =(a+b+0)*a^2-(a*b)*(a+b)+b^2*(a+b) 
       .=a*a^2+b*a^2+0*a^2-(a+b+0)*(a*b)+b^2*(a+b)
       .=a*a^2+b*a^2-(a*(a*b)+b*(a*b)+0*(a*b))+(a+b+0)*b^2
       .=a^2*a+b*a^2-(a*(a*b)+b*(a*b))+(a*b^2+b*b^2+0*b^2)
       .=a|^ 3+b*a^2-(a*(a*b)+b*(a*b))+(a*b^2+b*b^2) by POLYEQ_2:4
       .=a|^ 3+b*a^2-(a*(a*b)+b*(b*a))+(a*b^2+b|^ 3) by POLYEQ_2:4
       .=a|^ 3+b*a^2-(a*a*b+b*(b*a))+(a*b^2+b|^ 3) 
       .=a|^ 3+b*a^2-(a*a*b+b*b*a)+(a*b^2+b|^ 3) 
       .=a|^ 3+b*a^2-(a^2*b+b*b*a)+(a*b^2+b|^ 3) by SQUARE_1:def 3
       .=a|^ 3+a^2*b-(a^2*b+a*b^2)+(a*b^2+b|^ 3) by SQUARE_1:def 3
       .=a|^ 3+a^2*b-a^2*b-a*b^2+(a*b^2+b|^ 3) 
       .=a|^ 3-a*b^2+(a*b^2+b|^ 3) 
       .=a|^ 3-a*b^2+a*b^2+b|^ 3 
       .=a|^ 3-(a*b^2-a*b^2)+b|^ 3 
       .=a|^ 3-0+b|^ 3 
       .=a|^3+b|^3  ;
       (a+b)*(a|^4-a|^3*b+a|^2*b|^2-a*b|^3+b|^4)
       =(a|^4-a|^3*b+a|^2*b|^2)*(a+b)-(a*b|^3)*(a+b)+b|^4*(a+b) 
                                                    
      .=a|^4*(a+b+0)-(a|^3*b)*(a+b)+(a|^2*b|^2)*(a+b)
                      -(a*b|^3)*(a+b)+b|^4*(a+b)  
      .=a|^4*a+b*a|^4+0*a|^4-(a|^3*b)*(a+b)+(a|^2*b|^2)*(a+b)
                      -(a*b|^3)*(a+b)+b|^4*(a+b)      
      .=a|^4*a|^1+b*a|^4+0*a|^4-(a|^3*b)*(a+b)+(a|^2*b|^2)*(a+b)
                      -(a*b|^3)*(a+b)+b|^4*(a+b)      by NEWTON:10
      .=a|^(4+1)+b*a|^4-(a|^3*b)*(a+b+0)+(a|^2*b|^2)*(a+b)
                      -(a*b|^3)*(a+b)+b|^4*(a+b)      by NEWTON:13
      .=a|^5+b*a|^4-(a*(a|^3*b)+b*(a|^3*b)+0*(a|^3*b))
       +(a|^2*b|^2)*(a+b+0)-(a*b|^3)*(a+b)+b|^4*(a+b)   
      .=a|^5+b*a|^4-(a*(a|^3*b)+b*(a|^3*b))+(a*(a|^2*b|^2)
       +b*(a|^2*b|^2)+0*(a|^2*b|^2))-(a*b|^3)*(a+b+0)+b|^4*(a+b)  
                                                     
      .=a|^5+b*a|^4-(a*(a|^3*b)+b*(a|^3*b))+(a*(a|^2*b|^2)
       +b*(a|^2*b|^2))-(a*(a*b|^3)+b*(a*b|^3)+0*(a*b|^3))+b|^4*(a+b+0)  
                                                       
      .=a|^5+b*a|^4-(a*(a|^3*b)+b*(a|^3*b))+(a*(a|^2*b|^2)
       +b*(a|^2*b|^2))-(a*(a*b|^3)+b*(a*b|^3))+(a*b|^4+b*b|^4+0*b|^4)  
                                                      
      .=(a|^5+b*a|^4)-(a*a|^3*b+b*(a|^3*b))+(a*(a|^2*b|^2)
       +b*(a|^2*b|^2))-(a*(a*b|^3)+b*(a*b|^3))+(a*b|^4+b*b|^4)  
                                                       
      .=(a|^5+b*a|^4)-(a|^4*b+b*(a|^3*b))+(a*(a|^2*b|^2)
       +b*(a|^2*b|^2))-(a*(a*b|^3)+b*(a*b|^3))+(a*b|^4+b*b|^4)  
                                                       by POLYEQ_2:4
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a*(a|^2*b|^2)
       +b*(a|^2*b|^2))-(a*(a*b|^3)+b*(a*b|^3))+(a*b|^4+b*b|^4)  
                                                       
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a*a|^2*b|^2
       +b*(a|^2*b|^2))-(a*(a*b|^3)+b*(a*b|^3))+(a*b|^4+b*b|^4)  
                                                      
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a*a|^2*b|^2
       +b*b|^2*a|^2)-(a*(a*b|^3)+b*(a*b|^3))+(a*b|^4+b*b|^4)  
                                                      
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a*a|^2*b|^2
       +b*b|^2*a|^2)-(a*a*b|^3+b*(a*b|^3))+(a*b|^4+b*b|^4)  
                                                      
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+((a|^2*a)*b|^2
        +b*b|^2*a|^2)-(a*a*b|^3+b*b|^3*a)+(a*b|^4+b*b|^4) 
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a|^2*a*b|^2
        +b*b|^2*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b*b|^4) by POLYEQ_2:4
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a|^(2+1)*b|^2
        +b|^2*b*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b*b|^4) by NEWTON:11
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a|^3*b|^2
        +b|^(2+1)*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b|^4*b) by NEWTON:11
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a|^3*b|^2
        +b|^3*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b|^(4+1)) by NEWTON:11
      .=(a|^5+b*a|^4)-(a|^4*b+b|^1*b*a|^3)+(a|^3*b|^2
        +b|^3*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b|^(4+1)) by NEWTON:10
      .=(a|^5+b*a|^4)-(a|^4*b+b|^1*b*a|^3)+(a|^3*b|^2
       +b|^3*a|^2)-(a|^1*a*b|^3+b|^4*a)+(a*b|^4+b|^(4+1)) by NEWTON:10
      .=(a|^5+b*a|^4)-(a|^4*b+b|^(1+1)*a|^3)+(a|^3*b|^2
        +b|^3*a|^2)-(a|^1*a*b|^3+b|^4*a)+(a*b|^4+b|^5) by NEWTON:11
      .=(a|^5+b*a|^4)-(a|^4*b+b|^2*a|^3)+(a|^3*b|^2
        +b|^3*a|^2)-(a|^(1+1)*b|^3+b|^4*a)+(a*b|^4+b|^5) by NEWTON:11
      .=a|^5+b*a|^4-b*a|^4-b|^2*a|^3+(a|^3*b|^2
        +b|^3*a|^2)-(a|^(1+1)*b|^3+b|^4*a)+(a*b|^4+b|^5) 
      .=(a|^5-b|^2*a|^3)+(a|^3*b|^2+b|^3*a|^2)-(a|^(1+1)*b|^3+b|^4*a)
        +(a*b|^4+b|^5) 
      .=a|^5-b|^2*a|^3+b|^2*a|^3+b|^3*a|^2-(a|^(1+1)*b|^3+b|^4*a)
        +(a*b|^4+b|^5) 
      .=a|^5-(b|^2*a|^3-b|^2*a|^3)+b|^3*a|^2-(a|^(1+1)*b|^3+b|^4*a)
        +(a*b|^4+b|^5) 
      .=(a|^5+a|^2*b|^3)-(a|^2*b|^3+a*b|^4)+(a*b|^4+b|^5) 
      .=a|^5+a|^2*b|^3-a|^2*b|^3-a*b|^4+(a*b|^4+b|^5) 
      .=a|^5-a*b|^4+(a*b|^4+b|^5) 
      .=a|^5-a*b|^4+a*b|^4+b|^5 
      .=a|^5-(a*b|^4-a*b|^4)+b|^5
      .=a|^5+b|^5 ;
hence thesis by A100;

end;

theorem 
  a<>0 & b^2-2*a*b-3*a^2>=0 & Poly3(a,b,b,a,x)=0 implies
   x=-1 or x= (a-b+sqrt(b^2-2*a*b-3*a^2))/(2*a)
       or x= (a-b-sqrt(b^2-2*a*b-3*a^2))/(2*a)
proof
     assume that
 A100: a<>0 & b^2-2*a*b-3*a^2>=0 & Poly3(a,b,b,a,x)=0; 
       a*(x|^ 3)+b*x^2+b*x+a=0 by POLYEQ_1:def 4,A100;then
       a*(x|^ 3)+(b*x^2+b*x)+a=0 ;then
       (x|^ 3)*a+1*a+0*a+(b*x^2+b*x)=0 by XCMPLX_1:1;then
       ((x|^ 3)+1+0)*a+(x^2*b+x*b+0*b)=0 by XCMPLX_1:9;then
       ((x|^ 3)+1)*a+(x^2+x+0)*b=0 by XCMPLX_1:9;then
       ((x|^ 3)+1)*a+(x*x+1*x+0*x)*b=0 by SQUARE_1:def 3;then
       ((x|^ 3)+1 to_power 3)*a+(x+1)*x*b=0 by POWER:31;then
       (x|^ 3+1|^3)*a+(x+1)*x*b=0 by POWER:48;then
       ((x+1)*(x^2-x*1+1^2))*a+(x+1)*x*b=0 by T14;then
       (x+1)*(x^2-x+1*1)*a+(x+1)*x*b=0 by SQUARE_1:def 3;then
       (x^2*a-x*a+x*b+a)*(x+1)=0 ;then
 A200: a*x^2-(a-b)*x+a=0 or x+1=0 
                       by XCMPLX_1:6;
       now per cases by A200;
       case
       x+1=0;then
        x=0-1;
       hence
           x=-1
        or x= (a-b+sqrt(b^2-2*a*b-3*a^2))/(2*a)
        or x= (a-b-sqrt(b^2-2*a*b-3*a^2))/(2*a);
       end;
       case
       a*x^2-(a-b)*x+a=0;then
       a*x^2+(-a+b)*x+a=0 ;then
 B202: Poly2(a,-a+b,a,x)=0 by POLYEQ_1:def 2;
       delta(a,-a+b,a)
        =(-a+b)^2-4*a*a by QUIN_1:def 1
       .=(-a+b)^2-4*(a*a) 
       .=(-(a-b))^2-4*(a*a) 
       .=(b-a)^2-4*(a*a) 
       .=b^2-2*b*a+a^2-4*(a*a) by SQUARE_1:64
       .=b^2-2*(b*a)+a^2-4*(a*a) 
       .=b^2-2*a*b+a^2-4*(a*a) 
       .=(b^2-2*a*b)+a^2-4*a^2 by SQUARE_1:def 3
       .=(b^2-2*a*b)-4*a^2+a^2 
       .=(b^2-2*a*b)-(4*a^2-a^2)
       .=(b^2-2*a*b)+(1*a^2-4*a^2+0*a^2) 
       .=(b^2-2*a*b)+(1-4+0)*a^2 
       .=(b^2-2*a*b)+(-(4-1))*a^2;then
 B205: delta(a,-a+b,a)
       =(b^2-2*a*b)+(-3)*a^2
       .=(b^2-2*a*b)+-(3*a^2) 
       .=-(-(b^2-2*a*b)+3*a^2)
       .=b^2-2*a*b-3*a^2 ;then
        x= (-(-a+b)+sqrt delta(a,-a+b,a))/(2*a)
        or x= (-(-a+b)-sqrt delta(a,-a+b,a))/(2*a)
                        by A100,B202,POLYEQ_1:5;
       hence 
           x=-1
        or x= (a-b+sqrt(b^2-2*a*b-3*a^2))/(2*a)
        or x= (a-b-sqrt(b^2-2*a*b-3*a^2))/(2*a) by B205;        
       end;
      end;
  hence thesis;
end;

definition  let a, b,c,d,e,f, x be Real;
func Poly5(a,b,c,d,e,f,x) equals
   :Def1: a*(x|^5)+b*(x|^4)+c*(x|^3)+d*(x^2)+e*x+f;
   coherence;
end;

theorem 
  a<>0 & b^2+2*a*b+5*a^2-4*a*c>0 & Poly5(a,b,c,c,b,a,x)=0 implies
       (for y1,y2 being Real st
        y1 = (a-b+sqrt(b^2+2*a*b+5*a^2-4*a*c))/(2*a) &
        y2 = (a-b-sqrt(b^2+2*a*b+5*a^2-4*a*c))/(2*a) holds
        x=-1 or
        x = (y1 + sqrt delta(1,(-y1),1))/2 or
        x = (y2 + sqrt delta(1,(-y2),1))/2 or
        x = (y1 - sqrt delta(1,(-y1),1))/2 or
        x = (y2 - sqrt delta(1,(-y2),1))/2)
proof
       assume that
 A100: a<>0 & b^2+2*a*b+5*a^2-4*a*c>0 & Poly5(a,b,c,c,b,a,x)=0; 
       let y1,y2 be Real;
       assume
 A104: y1 = (a-b+sqrt(b^2+2*a*b+5*a^2-4*a*c))/(2*a)
       & y2 = (a-b-sqrt(b^2+2*a*b+5*a^2-4*a*c))/(2*a);
       a*(x|^5)+b*(x|^4)+c*(x|^3)+c*(x^2)+b*x+a=0 by Def1,A100;then
       0=a*(x|^5)+b*(x|^4)+(c*(x|^3)+c*(x^2))+b*x+a 
       .=a*(x|^5)+b*(x|^4)+((c*(x|^3)+c*(x^2))+b*x)+a 
       .=a*(x|^5)+b*(x|^4)+b*x+(c*(x|^3)+c*(x^2))+a 
       .=a*(x|^5)+(b*(x|^4)+b*x)+(c*(x|^3)+c*(x^2))+a 
       .=a*(x|^5)+((b*(x|^4)+b*x)+(c*(x|^3)+c*(x^2)))+a 
       .=a*(x|^5)+(((b*(x|^4)+b*x)+(c*(x|^3)+c*(x^2)))+a) 
       .=a*(x|^5)+a+((b*(x|^4)+b*x)+(c*(x|^3)+c*(x^2))) 
       .=(a*(x|^5)+1*a+0*a)+(b*(x|^4)+b*x)+(c*(x|^3)+c*(x^2)) 
       .=(x|^5+1+0)*a+(b*(x|^4)+b*x+0*b)+(c*(x|^3)+c*(x^2)+0*c) 
       .=(x|^5+1)*a+(x|^4+x+0)*b+(c*(x|^3)+c*(x^2)+0*c) 
       .=(x|^5+1)*a+(x|^4+x)*b+(x|^3+x^2+0)*c 
       .=(x|^5+1|^5)*a+(x|^(3+1)+x)*b+(x|^3+x^2)*c by NEWTON:15
       .=(x|^5+1|^5)*a+(x|^3*x+x)*b+(x|^(2+1)+x^2)*c by NEWTON:11
       .=(x|^5+1|^5)*a+(x|^3*x+1*x+0*x)*b+(x|^2*x+x^2)*c by NEWTON:11
       .=(x|^5+1|^5)*a+(x|^3+1+0)*x*b+(x*x|^(1+1)+1*x^2+0*x^2)*c 
       .=(x|^5+1|^5)*a+(x|^3+1+0)*x*b+(x*(x|^1*x)+1*x^2+0*x^2)*c by NEWTON:11
       .=(x|^5+1|^5)*a+(x|^3+1+0)*x*b+(x*(x*x)+1*x^2+0*x^2)*c by NEWTON:10
       .=(x|^5+1|^5)*a+(x|^3+1+0)*x*b+(x*x^2+1*x^2+0*x^2)*c by SQUARE_1:def 3
       .=(x|^5+1|^5)*a+(x|^3+1)*x*b+(x+1+0)*x^2*c 
       .=((x+1)*(x|^4-x|^3*1+x|^2*1|^2-x*1|^3+1|^4))*a
                            +(x|^3+1)*x*b+(x+1+0)*x^2*c by T14
       .=((x+1)*(x|^4-x|^3+x|^2*1-x*1|^3+1|^4))*a
                            +(x|^3+1)*x*b+(x+1+0)*x^2*c by NEWTON:15
       .=((x+1)*(x|^4-x|^3+x|^2-x*1+1|^4))*a+(x|^3+1)*x*b+(x+1+0)*x^2*c 
                                                           by NEWTON:15
       .=((x+1)*(x|^4-x|^3+x|^2-x+1))*a+(x|^3+1)*x*b+(x+1+0)*x^2*c
                                                          by NEWTON:15
       .=((x+1)*(x|^4-x|^3+x|^2-x+1))*a+(x|^3+1|^3)*x*b+(x+1)*x^2*c
                                                          by NEWTON:15
       .=((x+1)*(x|^4-x|^3+x|^2-x+1))*a+((x+1)*(x^2-x*1+1^2))*x*b
              +(x+1)*x^2*c                                by T14
       .=((x+1)*(x|^4-x|^3+x|^2-x+1))*a+(x+1)*(x^2-x+1)*x*b
              +(x+1)*x^2*c                                by SQUARE_1:59
       .=(x+1)*((x|^4-x|^3+x|^2-x+1)*a)+(x+1)*(x^2-x+1)*x*b
              +(x+1)*x^2*c     
       .=(x+1)*((x|^4-x|^3+x|^2-x+1)*a)+(x+1)*((x^2-x+1)*x)*b
              +(x+1)*x^2*c                             
       .=(x+1)*((x|^4-x|^3+x|^2-x+1)*a)+(x+1)*(((x^2-x+1)*x)*b)
              +(x+1)*x^2*c                              
       .=(x+1)*((x|^4-x|^3+x|^2-x+1)*a)+(x+1)*(((x^2-x+1)*x)*b)
              +(x+1)*(x^2*c)                          
       .=((x|^4-x|^3+x|^2-x+1)*a+(x^2-x+1)*x*b+(x^2*c))*(x+1)
                                                       
       .=((x|^4-x|^3+x|^2-x+1)*a+(x^2-x+1)*(b*x)+(x^2*c))*(x+1)
                                                        
       .=((x|^4-x|^3+x|^2-x+1)*a+(x^2*(b*x)-x*(b*x)+1*(b*x))
             +(x^2*c))*(x+1)                           
       .=((x|^4-x|^3+x|^2)*a-x*a+1*a+(x^2*(b*x)-x*(b*x)+1*(b*x))
             +(x^2*c))*(x+1)                           
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x^2*(x*b)-x*(x*b)+(b*x))
             +(x^2*c))*(x+1)                           
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x^2*x*b-x*(x*b)+(b*x))
             +(x^2*c))*(x+1)                         
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x^2*x*b-x*x*b+(b*x))
             +(x^2*c))*(x+1)                          
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x*x*x*b-x*x*b+(b*x))
             +(x^2*c))*(x+1)                         by SQUARE_1:def 3
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x*x*x*b-x*x*b+(b*x))
             +(x*x*c))*(x+1)                         by SQUARE_1:def 3
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^1*x*x*b-x*x*b+(b*x))
             +(x*x*c))*(x+1)                         by NEWTON:10
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^1*x*x*b-x*x*b+(b*x))
             +(x|^1*x*c))*(x+1)                         by NEWTON:10
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^1*x*x*b-x*x*b+(b*x))
             +(x|^(1+1)*c))*(x+1)                         by NEWTON:11
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^(1+1)*x*b-x*x*b+(b*x))
             +(x|^2*c))*(x+1)                         by NEWTON:11
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^(2+1)*b-x*x*b+b*x)
             +(x|^2*c))*(x+1)                         by NEWTON:11
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^3*b-x|^1*x*b+b*x)
             +(x|^2*c))*(x+1)                         by NEWTON:10
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^3*b-x|^(1+1)*b+b*x)
             +(x|^2*c))*(x+1)                         by NEWTON:11
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(b*x|^3-b*x|^2+b*x)
             +c*x|^2)*(x+1)                          
       .=((((a*x|^4-a*x|^3)+a*x|^2)-a*x)+a+(b*x|^3-b*x|^2+b*x)
              +c*x|^2)*(x+1)                      
       .=((((a*x|^4-a*x|^3)+a*x|^2)-a*x)+(a+(b*x|^3-b*x|^2+b*x))
              +c*x|^2)*(x+1)                       
       .=((((a*x|^4-a*x|^3)+a*x|^2)-a*x)+((a+(b*x|^3-b*x|^2+b*x))
              +c*x|^2))*(x+1)                       
       .=((((a*x|^4-a*x|^3)+a*x|^2)-a*x)+c*x|^2+(a+(b*x|^3-b*x|^2+b*x))
              )*(x+1)                    
       .=((((a*x|^4-a*x|^3)+a*x|^2)-a*x)+c*x|^2+((b*x|^3-b*x|^2)+b*x)+a)
               *(x+1)                     
       .=((((a*x|^4-a*x|^3)+a*x|^2)-a*x)+c*x|^2+(b*x|^3-b*x|^2)+b*x+a)
               *(x+1)                    
       .=((((a*x|^4-a*x|^3)+a*x|^2)-a*x)+(c*x|^2+(b*x|^3-b*x|^2))+b*x+a)
               *(x+1)                       
       .=(((a*x|^4-a*x|^3)+a*x|^2)-a*x+(b*x|^3-b*x|^2)+c*x|^2+b*x+a)
               *(x+1)                     
       .=(((a*x|^4-a*x|^3)+a*x|^2)-(a*x-(b*x|^3-b*x|^2))+c*x|^2+b*x+a)
               *(x+1)                      
       .=(((a*x|^4-a*x|^3)+a*x|^2)+((b*x|^3-b*x|^2)-a*x)+c*x|^2+b*x+a)
               *(x+1)                     
       .=(((a*x|^4-a*x|^3)+a*x|^2)+(((b*x|^3-b*x|^2)-a*x)+c*x|^2)+b*x+a)
               *(x+1)                   
       .=((((a*x|^4-a*x|^3)+a*x|^2)+c*x|^2)+((b*x|^3-b*x|^2)-a*x)+b*x+a)
               *(x+1)                   
       .=((((a*x|^4-a*x|^3)+a*x|^2)+c*x|^2)+(b*x|^3-b*x|^2)-a*x+b*x+a)
               *(x+1)                     
       .=((a*x|^4-a*x|^3)+a*x|^2+c*x|^2+b*x|^3-b*x|^2-a*x+b*x+a)
               *(x+1)                    
       .=((a*x|^4-a*x|^3)+(a*x|^2+c*x|^2)+b*x|^3-b*x|^2-a*x+b*x+a)
               *(x+1)                     
       .=((a*x|^4-a*x|^3)+((a*x|^2+c*x|^2)+b*x|^3)-b*x|^2-a*x+b*x+a)
               *(x+1)                      
       .=(((a*x|^4-a*x|^3)+b*x|^3)+(a*x|^2+c*x|^2)-b*x|^2-a*x+b*x+a)
               *(x+1)                      
       .=(((a*x|^4-a*x|^3)+b*x|^3)+(a*x|^2+c*x|^2-b*x|^2)-a*x+b*x+a)
               *(x+1)                      
       .=(a*x|^4-a*x|^3+b*x|^3+(a+c-b)*x|^2-a*x+b*x+a)*(x+1) 
       .=(a*x|^4-(a*x|^3-b*x|^3)+(a+c-b)*x|^2-a*x+b*x+a)*(x+1) 
       .=((a*x|^4-(a-b)*x|^3+(a+c-b)*x|^2)-a*x+b*x+a)*(x+1) 
       .=((a*x|^4-(a-b)*x|^3+(a+c-b)*x|^2)-(a*x-b*x)+a)*(x+1) 
       .=((a*x|^4-(a-b)*x|^3+(a+c-b)*x|^2)-(a-b)*x+a)*(x+1) 
       .=(a*x|^4-(a-b)*x|^3+(a+c-b)*x|^2-(a-b)*x+a)*(x+1) ;then

 A200: a*x|^4-(a-b)*x|^3+(a+c-b)*x|^2-(a-b)*x+a=0 or x+1=0 by XCMPLX_1:6;
       now per cases by A200;
       case
       x+1=0;then
       x=0-1 ;
       hence
           x=-1 or
        x = (y1 + sqrt delta(1,(-y1),1))/2 or
        x = (y2 + sqrt delta(1,(-y2),1))/2 or
        x = (y1 - sqrt delta(1,(-y1),1))/2 or
        x = (y2 - sqrt delta(1,(-y2),1))/2 ;
       end;
       case
       a*x|^4-(a-b)*x|^3+(a+c-b)*x|^2-(a-b)*x+a=0;then
       0=-(-a*x|^4+(a-b)*x|^3)+(a+c-b)*x|^2-(a-b)*x+a 
       .=a*x|^4+-(a-b)*x|^3+(a+c-b)*x|^2-(a-b)*x+a 
       .=a*x|^4+(-(a-b))*x|^3+(a+c-b)*x|^2-(a-b)*x+a 
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*x|^2-(a-b)*x+a
       .=-(-(a*x|^4+(-a+b)*x|^3+(a+c-b)*x|^2)+(a-b)*x)+a 
       .=(a*x|^4+(-a+b)*x|^3+(a+c-b)*x|^2)+-(a-b)*x+a 
       .=(a*x|^4+(-a+b)*x|^3+(a+c-b)*x|^2)+(-(a-b))*x+a 
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*x|^(1+1)+(-a+b)*x+a
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*(x|^1*x)+(-a+b)*x+a by NEWTON:11
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*(x*x)+(-a+b)*x+a by NEWTON:10
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*x^2+(-a+b)*x+a 
                                            by SQUARE_1:def 3;then
 B202: Four(a,-a+b,a+c-b,-a+b,a,x)=0 by POLYEQ_2:def 1;
 B210: (-a+b)^2-4*a*(a+c-b)+8*a^2
       =(-(a-b))^2-4*a*(a+c-b)+8*a^2 
       .=(a-b)^2-(a+c-b)*(4*a)+8*a^2 by SQUARE_1:61
       .=(a^2-2*a*b+b^2)-(a+c-b)*(4*a)+8*a^2 by SQUARE_1:64
       .=(a^2-2*a*b+b^2)-(a*(a*4)+c*(a*4)-b*(a*4))+8*a^2
       .=(a^2-2*a*b+b^2)-(a*a*4+c*(a*4)-b*(a*4))+8*a^2
       .=(a^2-2*a*b+b^2)-(a*a*4+c*a*4-b*(a*4))+8*a^2 
       .=(a^2-2*a*b+b^2)-(a*a*4+c*a*4-b*a*4)+8*a^2 
       .=(a^2-2*a*b+b^2)-(4*a^2+(c*a)*4-b*a*4)+8*a^2 by SQUARE_1:def 3
       .=(a^2-2*a*b+b^2)-(4*a^2+4*a*c-(b*a)*4)+8*a^2 
       .=(a^2-2*a*b+b^2)-(4*a^2+4*a*c-4*a*b)+8*a^2 
       .=(a^2-2*a*b+b^2)-(4*a^2+4*a*c)+4*a*b+8*a^2 
       .=a^2-2*a*b+b^2-4*a^2-4*a*c+4*a*b+8*a^2 
       .=8*a^2+(a^2-(2*a*b-b^2)-4*a^2-4*a*c+4*a*b) 
       .=8*a^2+((a^2-4*a^2)-(2*a*b-b^2)-4*a*c+4*a*b) 
       .=8*a^2+(((1*a^2-4*a^2)-4*a*c)-(2*a*b-b^2)+4*a*b) 
       .=8*a^2+(((1*a^2-4*a^2)-4*a*c)-(2*a*b-b^2-4*a*b)) 
       .=8*a^2+((1*a^2-4*a^2)-4*a*c)-(2*a*b-b^2-4*a*b) 
       .=8*a^2+(1*a^2-4*a^2)-4*a*c-(2*a*b-b^2-4*a*b) 
       .=8*a^2+1*a^2-4*a^2-4*a*c-(2*a*b-b^2-4*a*b)
       .=(8+1-4)*a^2-4*a*c-(2*a*b-b^2-4*a*b);
       set y=x+1/x;
 B211:  y=x+1/x;
 B212: (-a+b)^2-4*a*(a+c-b)+8*a^2
        =5*a^2-4*a*c-(2*a*b-b^2-4*a*b) by B210
       .=(5*a^2-4*a*c)-((2*a*b-4*a*b)-b^2)
       .=(5*a^2-4*a*c)-(2*a*b-4*a*b)+b^2 
       .=(5*a^2-4*a*c)-2*a*b+4*a*b+b^2 
       .=(5*a^2-4*a*c)-2*a*b+(a*b)*(2+2)+b^2 
       .=(5*a^2-4*a*c)-2*a*b+(2*(a*b)+2*(a*b))+b^2 
       .=(5*a^2-4*a*c)-2*a*b+2*(a*b)+2*(a*b)+b^2 
       .=5*a^2-4*a*c-2*a*b+2*a*b+2*(a*b)+b^2 
       .=(5*a^2-4*a*c)-2*a*b+2*a*b+2*a*b+b^2 
       .=(5*a^2-4*a*c)-(2*a*b-2*a*b)+2*a*b+b^2 
       .=5*a^2-4*a*c+2*a*b+b^2
       .=b^2+2*a*b+(5*a^2-4*a*c) 
       .=b^2+2*a*b+5*a^2-4*a*c ;then
 B214: (-a+b)^2-4*a*(a+c-b)+8*a^2>0 by A100;
       y1 = (-(-a+b)+sqrt((-a+b)^2-4*a*(a+c-b)+8*a^2))/(2*a) &
       y2 = (-(-a+b)-sqrt((-a+b)^2-4*a*(a+c-b)+8*a^2))/(2*a) 
                                          by B212,A104;
       hence 
        x=-1 or
        x = (y1 + sqrt delta(1,(-y1),1))/2 or
        x = (y2 + sqrt delta(1,(-y2),1))/2 or
        x = (y1 - sqrt delta(1,(-y1),1))/2 or
        x = (y2 - sqrt delta(1,(-y2),1))/2       
                   by  A100,B202,B214,B211,POLYEQ_2:3;
       end;
       end;
  hence thesis;
end;

theorem T17: 
   x+y=p & x*y=q & p^2-4*q>=0 implies
      x=(p+sqrt(p^2-4*q))/2 & y=(p-sqrt(p^2-4*q))/2 or
      x=(p-sqrt(p^2-4*q))/2 & y=(p+sqrt(p^2-4*q))/2 
proof
        assume that
 A100: x+y=p & x*y=q & p^2-4*q>=0 ; 
       p*y-y*y+0*y=q by A100;then
       p*y-y^2=q by SQUARE_1:def 3;then
       1*y^2+(-p)*y+q=0 ;then
 A104: Poly2(1,-p,q,y)=0 by POLYEQ_1:def 2;
 A105: delta(1,-p,q)=(-p)^2-4*1*q by QUIN_1:def 1
       .=p^2-4*q by SQUARE_1:61;then
       y=(-(-p)+sqrt delta(1,-p,q))/(2*1) 
       or y=(-(-p)-sqrt delta(1,-p,q))/(2*1) by A100,POLYEQ_1:5,A104;then
 A106: y=(p+sqrt delta(1,-p,q))/2 
       or y=(0+p-sqrt delta(1,-p,q))/2;
       now per cases by A106;
       suppose
 A108: y=(p+sqrt delta(1,-p,q))/2 ;then
         x=p*2/2-(p/2+(sqrt delta(1,-p,q))/2) 
            by A100,XCMPLX_1:26;then
         x=2*p/2-1*p/2-(sqrt delta(1,-p,q))/2;then
 A110:   x=(2-1+0)*(p*2")-(sqrt delta(1,-p,q))/2 
           by XCMPLX_0:def 9;
         x=p/2-(sqrt delta(1,-p,q))/2+0/2 by A110,XCMPLX_0:def 9;
         hence 
          x=(p+sqrt(p^2-4*q))/2 & y=(p-sqrt(p^2-4*q))/2 or
          x=(p-sqrt(p^2-4*q))/2 & y=(p+sqrt(p^2-4*q))/2 
                                              by A108,A105;
       end;
       suppose
 A112:   y=(p-sqrt delta(1,-p,q))/2;then
         x=p-(p-sqrt delta(1,-p,q)+0)/2 by A100,XCMPLX_1:26;then
         x=2*p/2-1*p/2+(sqrt delta(1,-p,q))/2 ;then
 A210:   x=(2-1+0)*(p*2")+(sqrt delta(1,-p,q))/2 
                     by XCMPLX_0:def 9;
         x=p/2+(sqrt delta(1,-p,q))/2+0/2 by A210,XCMPLX_0:def 9;
         hence 
          x=(p+sqrt(p^2-4*q))/2 & y=(p-sqrt(p^2-4*q))/2 or
          x=(p-sqrt(p^2-4*q))/2 & y=(p+sqrt(p^2-4*q))/2 
                                                by A112,A105;
       end;
       end;
  hence thesis;
end;

theorem 
   (x|^ n)+(y|^ n)=p & (x|^ n)*(y|^ n)=q & p^2-4*q>=0 
     & (ex m st n=2*m+1) implies
      x=n-root((p+sqrt(p^2-4*q))/2) 
      & y=n-root((p-sqrt(p^2-4*q))/2) or
      x=n-root((p-sqrt(p^2-4*q))/2) 
      & y=n-root((p+sqrt(p^2-4*q))/2) 
proof
        assume that
 A100: (x|^ n)+(y|^ n)=p & (x|^ n)*(y|^ n)=q & p^2-4*q>=0 
       & (ex m st n=2*m+1); 
       set x1=x|^ n;
       set y1=y|^ n;
          x|^ n=(p+sqrt(p^2-4*q))/2 & y|^ n=(p-sqrt(p^2-4*q))/2 or
          x|^ n=(p-sqrt(p^2-4*q))/2 & y|^ n=(p+sqrt(p^2-4*q))/2
                                        by A100,T17;
       hence 
          x=n-root((p+sqrt(p^2-4*q))/2) & y=n-root((p-sqrt(p^2-4*q))/2) or
          x=n-root((p-sqrt(p^2-4*q))/2) & y=n-root((p+sqrt(p^2-4*q))/2)
                                        by A100,POWER:5;
end;

theorem 
   (x|^ n)+(y|^ n)=p & (x|^ n)*(y|^ n)=q & p^2-4*q>=0 & p>0 & q>0 
   &(ex m st n=2*m & m>=1) implies
      x=n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2) 
      or x=n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2) 
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
proof
        now assume that
 A100: (x|^ n)+(y|^ n)=p & (x|^ n)*(y|^ n)=q ;
        assume 
 A102: p^2-4*q>=0 & p>0 & q>0 & (ex m st n=2*m & m>=1); 
 A109: delta(1,(-p),q)=(-p)^2-4*1*q by QUIN_1:def 1
         .=p^2-4*q by SQUARE_1:61;then
       0 <= sqrt delta(1,(-p),q) by A102,SQUARE_1:94,SQUARE_1:82;then
       -(-p)+sqrt delta(1,-p,q)>0 +0 by REAL_1:67,A102;then
 AA1:  (-(-p)+sqrt delta(1,-p,q))/(2*1)>0  by REAL_2:127;
         --4*q>0 by REAL_2:122,A102 ;then
         -4*q<0  by REAL_1:66;then
         p^2+(-4*q)<p^2+0  by REAL_1:67;then
         sqrt(p^2-4*q)<sqrt(p^2)
               by SQUARE_1:95,A102;then
         sqrt(p^2-4*q)<p  by SQUARE_1:89,A102;then
         -sqrt(p^2-4*q)>-p  by REAL_1:50;then
         -sqrt(p^2-4*q)+p>-p+0+p  by REAL_1:67;then
 A110: (0+p+sqrt(p^2-4*q))/2>0  & (0+p-sqrt(p^2-4*q))/2>0  
         by AA1,REAL_2:127,A109;
       set x1=x|^ n;
       set y1=y|^ n;
 A116: x|^ n=(p+sqrt(p^2-4*q))/2 & y|^ n=(p-sqrt(p^2-4*q))/2 or
       x|^ n=(p-sqrt(p^2-4*q))/2 & y|^ n=(p+sqrt(p^2-4*q))/2
                                           by A102,T17,A100;
       now per cases by A116;
       suppose 
       x|^ n=(p+sqrt(p^2-4*q))/2 & y|^ n=(p-sqrt(p^2-4*q))/2;
      hence  
      x=n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2) 
      or x=n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2) 
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
                                          by A102,T6,A110;
       end;
       suppose 
      x|^ n=(p-sqrt(p^2-4*q))/2 & y|^ n=(p+sqrt(p^2-4*q))/2;
      hence  
      x=n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2) 
      or x=n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2) 
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
                                               by A102,T6,A110;      
       end;
     end;
    hence thesis;
   end;
  hence thesis;
end;

theorem 
   x|^ n+y|^ n=a & x|^ n-y|^ n=b & (ex m st n=2*m+1) implies
    x=n-root((a+b)/2) & y=n-root((a-b)/2) by POWER:5;

theorem 
   x|^ n+y|^ n=a & x|^ n-y|^ n=b & (ex m st n=2*m & m>=1) & a>0 
    & a+b>0 & a-b>0 implies
        x=n-root((a+b)/2) & y=n-root((a-b)/2)  or
        x=n-root((a+b)/2) & y=-n-root((a-b)/2) or
        x=-n-root((a+b)/2)& y=n-root((a-b)/2)  or
        x=-n-root((a+b)/2) & y=-n-root((a-b)/2)
::        by REAL_2:127,XCMPLX_1:90,XCMPLX_1:8;
proof
       assume that
 A100: x|^ n+y|^ n=a & x|^ n-y|^ n=b;
       assume 
 A102: (ex m st n=2*m & m>=1) & a>0 & a+b>0 & a-b>0;then
       (a+b)/2>0 & (a-b)/2>0 by REAL_2:127;
      hence
        x=n-root((a+b)/2) & y=n-root((a-b)/2)  or
        x=n-root((a+b)/2) & y=-n-root((a-b)/2) or
        x=-n-root((a+b)/2)& y=n-root((a-b)/2)  or
        x=-n-root((a+b)/2) & y=-n-root((a-b)/2)
        by A100,A102,T6;
end;

theorem 
   a*x|^ n+b*y|^ n=p & x*y=0 & (ex m st n=2*m+1) & a*b<>0  implies
    x=0 & y=n-root(p/b)  or x=n-root(p/a) & y=0
proof
       assume that
 A100: a*x|^ n+b*y|^ n=p & x*y=0 & (ex m st n=2*m+1) & a*b<>0;
       consider m such that
 A106: n=2*m+1 by A100;
       2*m>=0 by NAT_1:18;then
       2*m+1>=0+1 by REAL_1:55;then
 A109: n>0 & n>=1 by AXIOMS:22,A106;

 A110: (a<>0 & b<>0) & (x=0 or y=0) by A100,XCMPLX_1:6;
       now per cases by A110;
       suppose
 B100: x=0;then
       a*0 to_power n+b*y|^ n=p by POWER:47,A109,A100;then
       a*0 +b*y|^ n=p by POWER:def 2,A109;then
       y|^ n=p/b  by XCMPLX_1:90,A110;
     hence
       x=0 & y=n-root(p/b) or x=n-root(p/a) & y=0 by POWER:5,A100,B100;
       end;
       suppose 
 C100: y=0;then
       a*x|^ n+b*0 to_power n=p by A100,POWER:47,A109;then
       a*x|^ n+b*0=p by POWER:def 2,A109;then
       x|^ n=p/a  by XCMPLX_1:90,A110;
     hence
       x=0 & y=n-root(p/b) or x=n-root(p/a) & y=0 by POWER:5,A100,C100;
       end;
       end;
 hence thesis;
end;

theorem 
 a*x|^ n+b*y|^ n=p & x*y=0 & (ex m st n=2*m &m>=1) & p/b>0 &p/a>0 &a*b<>0
   implies
       x=0 & y=n-root(p/b) 
       or x=0 & y=-n-root(p/b)
       or x=n-root(p/a) & y=0      
       or x=-n-root(p/a) & y=0
proof
       assume that
 A100: a*x|^ n+b*y|^ n=p & x*y=0 & (ex m st n=2*m & m>=1) 
        & p/b>0 &p/a>0 &a*b<>0;
        consider m such that
 A106:  n=2*m & m>=1 by A100;
 A108:  n>=1 by A106,REAL_2:137;then
 A109: n>0 by AXIOMS:22;

 A110: (a<>0 & b<>0)&(x=0 or y=0) by A100,XCMPLX_1:6;
       now per cases by A110;
       suppose
 B100: x=0;then
       a*0 to_power n+b*y|^ n=p by POWER:47,A108,A100;then
       a*0 +b*y|^ n=p by POWER:def 2,A109;then
       y|^ n=p/b  by XCMPLX_1:90,A110;
     hence
       x=0 & y=n-root(p/b) 
       or x=0 & y=-n-root(p/b)
       or x=n-root(p/a) & y=0      
       or x=-n-root(p/a) & y=0 by T6,A100,B100;
       end;
       suppose 
 C100: y=0;then
       a*x|^ n+b*0 to_power n=p by A100,POWER:47,A108;then
       a*x|^ n+b*0=p by POWER:def 2,A109;then
       x|^ n=p/a  by XCMPLX_1:90,A110;
     hence
       x=0 & y=n-root(p/b) 
       or x=0 & y=-n-root(p/b)
       or x=n-root(p/a) & y=0      
       or x=-n-root(p/a) & y=0 by T6,A100,C100;
       end;
       end;
 hence thesis;
end;

theorem 
 a*x|^ n=p & x*y=q & (ex m st n=2*m+1) & p*a<>0 
   implies
       x=n-root(p/a) & y=q*n-root(a/p)      
proof
       assume that
 A100: a*x|^ n=p & x*y=q & (ex m st n=2*m+1) & p*a<>0 ; 
       consider m such that
 A101: n=2*m+1 by A100;
       2*m>=0 by NAT_1:18;then
       2*m+1>=0+1 by REAL_1:55;then
 A103: n>=1 by A101;
 A102: p<>0 & a<>0 by A100;then
       x|^ n=p/a  by A100,XCMPLX_1:90;then
 A104: x=n-root(p/a) by POWER:5,A100;then
       y*(n-root(p/a)*n-root(a/p))=q*n-root(a/p) by A100;then
       y*n-root((p/a)*(a/p))=q*n-root(a/p) by  POWER:12,A100;then
       y*n-root((p/a)*(a*p"))=q*n-root(a/p) by XCMPLX_0:def 9;then
       y*n-root(p/a*a*p")=q*n-root(a/p) ;then
       y*n-root(p*p")=q*n-root(a/p) by XCMPLX_1:88,A102;then
       y*n-root(p/p)=q*n-root(a/p) by XCMPLX_0:def 9;then
       y*n-root(1)=q*n-root(a/p) by XCMPLX_1:60,A102;then
       y*1=q*n-root(a/p) by POWER:7,A103;
     hence 
       x=n-root(p/a) & y=q*n-root(a/p) by A104;
end;
 
theorem 
  a*x|^ n=p & x*y=q & (ex m st n=2*m & m>=1) &p/a>0 &a<>0
   implies
       x=n-root(p/a) & y=q*n-root(a/p)  
       or x=-n-root(p/a) & y=-q*n-root(a/p)  
proof
       assume that
 A100: a*x|^ n=p & x*y=q & (ex m st n=2*m &m>=1) & p/a>0 &a<>0; 
       consider m such that
 A101: n=2*m & m>=1 by A100;
 A103: n>=1 by A101,REAL_2:137;
 A102: p<>0 by A100;
       (p/a)">0 by A100,REAL_1:72;then
       1/(p/a)>0 by XCMPLX_1:217;then
 A105: (1*a)/p>0 by XCMPLX_1:78;
       x|^ n=p/a  by XCMPLX_1:90,A100;then
 A104: x=n-root(p/a) or x=-n-root(p/a) by T6,A100;
       now per cases by A104;
       suppose 
 B100: x=n-root(p/a);then
       y*(n-root(p/a)*n-root(a/p))=q*n-root(a/p) by A100;then
       y*n-root((p/a)*(a/p))=q*n-root(a/p) by  POWER:12,A100,A103,A105;then
       y*n-root((p/a)*(a*p"))=q*n-root(a/p) by XCMPLX_0:def 9;then
       y*n-root(p/a*a*p")=q*n-root(a/p);then
       y*n-root(p*p")=q*n-root(a/p) by XCMPLX_1:88,A100;then
       y*n-root(p/p)=q*n-root(a/p) by XCMPLX_0:def 9;then
       y*n-root(1)=q*n-root(a/p) by XCMPLX_1:60,A102;then
       y*1=q*n-root(a/p) by POWER:7,A103;
     hence 
       x=n-root(p/a) & y=q*n-root(a/p)  
       or x=-n-root(p/a) & y=-q*n-root(a/p)  by B100;
       end;
       suppose
 B200: x=-n-root(p/a);then
       y*(n-root(p/a)*n-root(a/p))=-q*n-root(a/p)
          by A100;then
       y*n-root((p/a)*(a/p))=-q*n-root(a/p) by POWER:12,A100,A103,A105;then
       y*n-root((p/a)*(a*p"))=-q*n-root(a/p) by XCMPLX_0:def 9;then
       y*n-root(p/a*a*p")=-q*n-root(a/p) ;then
       y*n-root(p*p")=-q*n-root(a/p) by XCMPLX_1:88,A100;then
       y*n-root(p/p)=-q*n-root(a/p) by XCMPLX_0:def 9;then
       y*n-root(1)=-q*n-root(a/p) by XCMPLX_1:60,A102;then
       y*1=-q*n-root(a/p) by POWER:7,A103;
      hence 
       x=n-root(p/a) & y=q*n-root(a/p)  
       or x=-n-root(p/a) & y=-q*n-root(a/p)  by  B200; 
       end;
       end;
  hence thesis;
end;

theorem Th23:
for a,b,x being Real st a>0 & a<>1 & b>0 & a to_power x = b holds
    x = log(a,b) by POWER:def 3;

theorem 
 for a,x being Real st a>0 & a<>1 & a to_power x = 1 holds
    x=0
proof
     let a,x be Real;
     assume
 A1: a>0 & a<>1 & a to_power x = 1;then
     x=log(a,1) by Th23;
     hence thesis by POWER:59,A1;
end;

theorem 
 for a,x being Real st a>0 & a<>1 & a to_power x = a holds
    x=1 
proof
     let a,x be Real;
     assume
 A1: a>0 & a<>1 & a to_power x = a;then
     x=log(a,a) by Th23;
     hence thesis by POWER:60,A1;
end;

theorem Th26:
 for a,b,x being Real st a>0 & a<>1 & x>0 & log(a,x) = b holds
    x = a to_power b by POWER:def 3;

theorem 
 for a,b,x being Real st a>0 & a<>1 & x>0 & log(a,x) = 0 holds
    x = 1
 proof
    let a,b,x be Real;
    assume
    a>0 & a<>1 & x>0 & log(a,x) = 0;then
    a to_power 0 = x by Th26; 
    hence thesis by POWER:29;
end;

theorem 
 for a,b,x being Real st a>0 & a<>1 & x>0 & log(a,x) = 1 holds
    x = a
 proof
    let a,b,x be Real;
    assume
    a>0 & a<>1 & x>0 & log(a,x) = 1;then
    a to_power 1 = x by Th26; 
    hence thesis by POWER:30;
end;
