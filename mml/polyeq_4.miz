:: Solving the Roots of the Special Polynomial Equation with Real
:: Coefficients
:: by Yuzhong Ding and Xiquan Liang
::
:: Received March 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies SQUARE_1, ARYTM_3, ARYTM_1, GROUP_1, FUNCT_3, POWER, POLYEQ_1,
      POLYEQ_4, POLYEQ_2, RELAT_1;
 notations REAL_1, SQUARE_1, POWER, NAT_1, POLYEQ_1, QUIN_1, POLYEQ_2,
      NEWTON, XXREAL_0;
 constructors REAL_1, NAT_1, SQUARE_1, POWER, PREPOWER, QUIN_1, POLYEQ_1,
      POLYEQ_2, XXREAL_0;
 registrations NAT_1, REAL_1, XREAL_0;
 definitions SQUARE_1;
 requirements SUBSET, NUMERALS, REAL, ARITHM;
 theorems REAL_2, SQUARE_1, PREPOWER, QUIN_1, POLYEQ_1, POWER, NEWTON,
      POLYEQ_2, NAT_1, XCMPLX_1, XCMPLX_0, XREAL_1;

begin

 reserve x,y,x1,x2,y1,a,b,c,d,e,f,p,q for Real;
 reserve m,n for Nat;

theorem Th1:
  a<>0 & b/a<0 & c/a>0 & delta(a,b,c) >=0 implies
    (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)>0
proof
     assume
A1:  a <> 0 & b/a < 0 & c/a>0 & delta(a,b,c) >=0;
A2:  b^2-4*a*c >= 0 by A1,QUIN_1:def 1;
     now per cases by A1,XREAL_1:145;
     case
A3:   b<0 & a>0;
then A4:   2*a>0 by REAL_2:122;
A5:   -b>0 by A3,XREAL_1:60;
A6:   c>0 by A1,A3,XREAL_1:146;
         0 <= sqrt( b^2-4*a*c) by A2,SQUARE_1:82,94;
        then -b+sqrt(b^2-4*a*c)>0 +0 by A5,XREAL_1:10;
 then A7:   (-b+sqrt(b^2-4*a*c))/(2*a)>0 by A4,REAL_2:127;
          4*a>0 by A3,REAL_2:122;
         then --4*a*c>0 by A6,REAL_2:122;
         then -4*a*c<0 by XREAL_1:60;
         then b^2+(-4*a*c)<b^2+0 by XREAL_1:10;
         then sqrt(b^2-4*a*c)<sqrt(b^2) by A2,SQUARE_1:95;
         then sqrt(b^2-4*a*c)<-b by A3,SQUARE_1:90;
         then -sqrt(b^2-4*a*c)>-(-b) by XREAL_1:26;
         then -sqrt(b^2-4*a*c)+(-b)>-(-b)+(-b) by XREAL_1:10;
         then (-b-sqrt(b^2-4*a*c))/(2*a)>0 by A4,REAL_2:127;
          hence (-b+sqrt delta(a,b,c))/(2*a)>0
           & (-b-sqrt delta(a,b,c))/(2*a)>0 by A7,QUIN_1:def 1;
          end;
         case
 A8:   b>0 & a<0;
 then A9:   a*2<0*2 by XREAL_1:70;
          --b>0 by A8;
 then A10:   -b<0 by XREAL_1:60;
 A11:   c<0 by A1,A8,XREAL_1:146;
          0 <= sqrt( b^2-4*a*c) by A2,SQUARE_1:82,94;
         then -b+0<0+sqrt( b^2-4*a*c) by A10,XREAL_1:10;
         then --(sqrt( b^2-4*a*c)+b)>0 by XREAL_1:64;
         then -b-sqrt( b^2-4*a*c)<0 by XREAL_1:60;
 then A12:   (-b-sqrt( b^2-4*a*c))/(2*a)>0 by A9,REAL_2:127;
          a*c>0 by A8,A11,REAL_2:122;
         then 4*(a*c)>0 by REAL_2:122;
         then --4*a*c>0;
         then -4*a*c<0 by XREAL_1:60;
         then b^2+(-4*a*c)<b^2+0 by XREAL_1:10;
         then sqrt(b^2-4*a*c)<sqrt(b^2) by A2,SQUARE_1:95;
         then sqrt(b^2-4*a*c)< b by A8,SQUARE_1:89;
         then -b+sqrt(b^2-4*a*c)<0+b+-b by XREAL_1:10;
         then (-b+sqrt(b^2-4*a*c))/(2*a)>0 by A9,REAL_2:127;
         hence (-b+sqrt delta(a,b,c))/(2*a)>0
           & (-b-sqrt delta(a,b,c))/(2*a)>0 by A12,QUIN_1:def 1;
        end;
       end;
    hence thesis;
end;

theorem
  a<>0 & b/a>0 & c/a>0 & delta(a,b,c) >=0 implies
    (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)<0
proof
  assume
A1:   a <> 0 & b/a>0 & c/a>0 & delta(a,b,c) >=0;
A2:   b^2-4*a*c >=0 by A1,QUIN_1:def 1;
      now per cases by A1,XREAL_1:146;
      case
 A3:   b>0 & a>0;
 then A4:   2*a>0 by REAL_2:122;
 A5:   c>0 by A1,A3,XREAL_1:146;
          4*a>0 by A3,REAL_2:122;
         then --4*a*c>0 by A5,REAL_2:122;
         then -4*a*c<0 by XREAL_1:60;
         then b^2+(-4*a*c)<b^2+0 by XREAL_1:10;
         then sqrt(b^2-4*a*c)<sqrt(b^2) by A2,SQUARE_1:95;
         then sqrt(b^2-4*a*c)<b by A3,SQUARE_1:89;
         then -b+sqrt(b^2-4*a*c)<0+(b+-b) by XREAL_1:10;
 then A6:   (-b+sqrt(b^2-4*a*c))/(2*a)<0 by A4,REAL_2:128;
          0 <= sqrt(b^2-4*a*c) by A2,SQUARE_1:82,94;
         then 0+0<b+sqrt( b^2-4*a*c) by A3,XREAL_1:10;
         then --(b+sqrt( b^2-4*a*c))>0;
         then -b-sqrt( b^2-4*a*c)<0 by XREAL_1:60;
         then (-b-sqrt( b^2-4*a*c))/(2*a)<0 by A4,REAL_2:128;
          hence (-b+sqrt delta(a,b,c))/(2*a)<0
           & (-b-sqrt delta(a,b,c))/(2*a)<0 by A6,QUIN_1:def 1;
          end;
         case
 A7:   b<0 & a<0;
 then A8:   -b>0 & a*2<0*2 by XREAL_1:60,XREAL_1:70;
 A9:   c<0 by A1,A7,XREAL_1:146;
          0 <= sqrt( b^2-4*a*c) by A2,SQUARE_1:82,94;
         then 0+0<-b+sqrt( b^2-4*a*c) by A8,XREAL_1:10;
 then A10:   (-b+sqrt( b^2-4*a*c))/(2*a)<0 by A8,REAL_2:128;
          a*c>0 by A7,A9,REAL_2:122;
         then 4*(a*c)>0 by REAL_2:122;
         then --4*a*c>0;
         then -4*a*c<0 by XREAL_1:60;
         then b^2+(-4*a*c)<b^2+0 by XREAL_1:10;
         then sqrt(b^2-4*a*c)<sqrt(b^2) by A2,SQUARE_1:95;
         then sqrt(b^2-4*a*c)< -b by A7,SQUARE_1:90;
         then b+sqrt(b^2-4*a*c)<0+b+-b by XREAL_1:10;
         then -(b+sqrt(b^2-4*a*c))>0 by XREAL_1:60;
         then (-b-sqrt(b^2-4*a*c))/(2*a)<0 by A8,REAL_2:128;
         hence (-b+sqrt delta(a,b,c))/(2*a)<0
           & (-b-sqrt delta(a,b,c))/(2*a)<0 by A10,QUIN_1:def 1;
         end;
         end;
       hence thesis;
end;

theorem
  a<>0 & c/a<0 implies
    (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
    (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0
proof
     assume
A1:   a <> 0 & c/a<0;
    now per cases by A1,XREAL_1:145;
     case
 A2:   c>0 & a<0;
 then A3:   2*a<2*0 by XREAL_1:70;
          4*a<4*0 by A2,XREAL_1:70;
         then 4*a*c<0*c by A2,XREAL_1:70;
 then A4:   -4*a*c>0 by XREAL_1:60;
         then A5: b^2+(-4*a*c)>b^2+0 by XREAL_1:10;
 A6:   b^2>=0 by SQUARE_1:72;
 then A7:   sqrt(b^2-4*a*c)>sqrt(b^2) by A5,SQUARE_1:95;
          -4*a*c+b^2>0+0 by A4,A6,XREAL_1:10;
 then A8:   --sqrt(b^2-4*a*c)>0 by SQUARE_1:82,95;
 then A9:   -sqrt(b^2-4*a*c)<0 by XREAL_1:60;
           now per cases;
           suppose
           A10:b>=0;
           then --b>=0;
           then A11:-b<=-0 by REAL_2:109;
           sqrt(b^2-4*a*c)>b by A7,A10,SQUARE_1:89;
           then -b+sqrt(b^2-4*a*c)>0+b+-b by XREAL_1:10;
           then A12:(-b+sqrt(b^2-4*a*c))/(2*a)<0 by A3,REAL_2:128;
           -sqrt(b^2-4*a*c)+-b<0+0 by A9,A11,XREAL_1:10;
           then -b-sqrt(b^2-4*a*c)<0;
           then -b-sqrt delta(a,b,c)<0 by QUIN_1:def 1;
           hence
            (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0
                                        by A3,A12,QUIN_1:def 1,REAL_2:127;
            end;
           suppose
           A13:b<0;
           then A14:-b>0 by XREAL_1:60;
                 sqrt(b^2-4*a*c)>-b by A7,A13,SQUARE_1:90;
                then --(b+sqrt(b^2-4*a*c))>0 by XREAL_1:64;
                then -b-sqrt(b^2-4*a*c)<0 by XREAL_1:60;
           then A15:(-b-sqrt(b^2-4*a*c))/(2*a)>0 by A3,REAL_2:127;
                 sqrt(b^2-4*a*c)+(-b)>0+0 by A8,A14,XREAL_1:10;
                then sqrt delta(a,b,c)+(-b)>0+0 by QUIN_1:def 1;
           hence
            (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0
                                        by A3,A15,QUIN_1:def 1,REAL_2:128;
           end;
          end;
           hence
            (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0;
           end;
         case
 A16:   c<0 & a>0;
 then A17:   2*a>0 by REAL_2:122;
         4*a>0 by A16,REAL_2:122;
         then 4*a*c<4*a*0 by A16,XREAL_1:70;
 then A18:   -4*a*c>0 by XREAL_1:60;
         then A19: b^2+(-4*a*c)>b^2+0 by XREAL_1:10;
 A20:   b^2>=0 by SQUARE_1:72;
 then A21:   sqrt(b^2-4*a*c)>sqrt(b^2) by A19,SQUARE_1:95;
          -4*a*c+b^2>0+0 by A18,A20,XREAL_1:10;
 then A22:   --sqrt(b^2-4*a*c)>0 by SQUARE_1:82,95;
 then A23:   -sqrt(b^2-4*a*c)<0 by XREAL_1:60;
    now per cases;
      suppose
      A24:b>=0;
      then --b>=0;
      then A25:-b<=-0 by REAL_2:109;
      sqrt(b^2-4*a*c)>b by A21,A24,SQUARE_1:89;
      then -b+sqrt(b^2-4*a*c)>0+b+-b by XREAL_1:10;
      then A26:(-b+sqrt(b^2-4*a*c))/(2*a)>0 by A17,REAL_2:127;
      -sqrt(b^2-4*a*c)+-b<0+0 by A23,A25,XREAL_1:10;
      then -b-sqrt(b^2-4*a*c)<0;
      then -b-sqrt delta(a,b,c)<0 by QUIN_1:def 1;
      hence (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0
                                        by A17,A26,QUIN_1:def 1,REAL_2:128;
           end;
           suppose
           A27:b<0;
           then A28:-b>0 by XREAL_1:60;
           sqrt(b^2-4*a*c)>-b by A21,A27,SQUARE_1:90;
           then --(b+sqrt(b^2-4*a*c))>0 by XREAL_1:64;
           then -b-sqrt(b^2-4*a*c)<0 by XREAL_1:60;
           then A29:(-b-sqrt(b^2-4*a*c))/(2*a)<0 by A17,REAL_2:128;
           sqrt(b^2-4*a*c)+(-b)>0+0 by A22,A28,XREAL_1:10;
           then sqrt delta(a,b,c)+(-b)>0 by QUIN_1:def 1;
           hence
            (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0
                                         by A17,A29,QUIN_1:def 1,REAL_2:127;
           end;
          end;
           hence
            (-b+sqrt delta(a,b,c))/(2*a)>0 & (-b-sqrt delta(a,b,c))/(2*a)<0 or
           (-b+sqrt delta(a,b,c))/(2*a)<0 & (-b-sqrt delta(a,b,c))/(2*a)>0;
          end;
         end;
 hence thesis;
end;

theorem Th4:
  a>0 & (ex m st n=2*m & m>=1) & x |^ n = a implies
     x=n-root a or x=-n-root a
proof
   assume
A1:  a>0 & (ex m st n=2*m & m>=1);
   assume
A2:  x |^ n=a;
     consider m such that
A3:  n=2*m & m>=1 by A1;
A4:  n>=1 by A3,REAL_2:137;
     then A5: x<>0 by A1,A2,NEWTON:16;
     now per cases by A5,XREAL_1:1;
     case x>0;
     hence x=n-root a or x=-n-root a by A2,A4,POWER:5;
    end;
    case x<0;
 then A6:  -x>0 by XREAL_1:60;
      n-root a=n-root((-x)|^ n) by A1,A2,POWER:1;
      then (-1)*n-root a=(-1)*(-x) by A4,A6,POWER:5;
      hence x=n-root a or x=-n-root a;
     end;
    end;
    hence thesis;
 end;

theorem Th5:
  a <> 0 & Poly2(a,b,0,x) = 0 implies x=0 or x = -(b/a)
proof
     assume
A1:  a<>0 & Poly2(a,b,0,x) = 0;
     a*x^2+b*x+0=0 by A1,POLYEQ_1:def 2;
     then a*(x*x)+b*x+0*x=0;
     then a*x*x+b*x+0*x=0;
     then (a*x+b+0)*x=0;
     then a*x+b+-b=0+-b or x=0 by XCMPLX_1:6;
     then a*x+(b+-b)=0+-b or x=0;
     then a*x=0+-b or x=0;
     then x=(-b)/a or x=0 by A1,XCMPLX_1:90;
     hence x=0 or x=-b/a by XCMPLX_1:188;
end;

theorem
  a <> 0 & Poly2(a,0,0,x) = 0 implies x=0
proof
   assume
A1:a<>0 & Poly2(a,0,0,x) = 0; then
   a*x^2+0*x+0=0 by POLYEQ_1:def 2;
   then x^2=0 by A1,XCMPLX_1:6;
   hence x=0 by SQUARE_1:73;
end;

theorem
  a <> 0 & (ex m st n=2*m+1) & delta(a,b,c) >= 0 &
    Poly2(a,b,c,x|^ n) = 0 implies
      x = n-root((-b+sqrt delta(a,b,c))/(2*a))
      or x = n-root((-b-sqrt delta(a,b,c))/(2*a))
proof
     assume
A1:  a <>0 & (ex m st n=2*m+1) & delta(a,b,c)>=0 & Poly2(a,b,c,x|^ n)=0;
     set y = x|^ n;
     x|^ n = (-b+sqrt delta(a,b,c))/(2*a)
     or x|^ n = (-b-sqrt delta(a,b,c))/(2*a) by A1,POLYEQ_1:5;
     hence x = n-root((-b+sqrt delta(a,b,c))/(2*a))
      or x = n-root((-b-sqrt delta(a,b,c))/(2*a)) by A1,POWER:5;
end;

theorem
  a <> 0 & b/a<0 & c/a>0 & (ex m st n=2*m & m>=1) & delta(a,b,c) >= 0
  & Poly2(a,b,c,x|^ n) = 0 implies
       x = n-root((-b+sqrt delta(a,b,c))/(2*a))
       or x = -n-root((-b+sqrt delta(a,b,c))/(2*a))
       or x = n-root((-b-sqrt delta(a,b,c))/(2*a))
       or x = -n-root((-b-sqrt delta(a,b,c))/(2*a))
proof
     assume
A1:  a <>0 & b/a<0 & c/a>0 & (ex m st n=2*m & m>=1) & delta(a,b,c)>=0
     & Poly2(a,b,c,x|^ n)=0;
        set y = x|^ n;
 A2:  (-b+sqrt delta(a,b,c))/(2*a)>0 &(-b-sqrt delta(a,b,c))/(2*a)>0
                                                           by A1,Th1;
         now per cases by A1,POLYEQ_1:5;
        suppose x|^ n = (-b+sqrt delta(a,b,c))/(2*a);
         hence x = n-root((-b+sqrt delta(a,b,c))/(2*a))
           or x = -n-root((-b+sqrt delta(a,b,c))/(2*a))
           or x = n-root((-b-sqrt delta(a,b,c))/(2*a))
           or x = -n-root((-b-sqrt delta(a,b,c))/(2*a)) by A1,A2,Th4;
          end;
         suppose x|^ n = (-b-sqrt delta(a,b,c))/(2*a);
         hence x = n-root((-b+sqrt delta(a,b,c))/(2*a))
          or x = -n-root((-b+sqrt delta(a,b,c))/(2*a))
          or x = n-root((-b-sqrt delta(a,b,c))/(2*a))
          or x = -n-root((-b-sqrt delta(a,b,c))/(2*a)) by A1,A2,Th4;
         end;
        end;
   hence thesis;
end;

theorem
  a <> 0 & (ex m st n=2*m+1) & Poly2(a,b,0,x|^ n) = 0 implies
    x=0 or x = n-root -(b/a)
proof
     assume
A1:  a<>0 & (ex m st n=2*m+1) & Poly2(a,b,0,x|^ n) = 0;
     set y = x|^ n;
     now per cases by A1,Th5;
     suppose x|^ n = 0;
       hence x = 0 or x=n-root -(b/a) by PREPOWER:12;
     end;
     suppose x|^ n = -(b/a);
       hence x = 0 or x=n-root -(b/a) by A1,POWER:5;
     end;
   end;
   hence thesis;
end;

theorem
  a <> 0 & b/a<0 & (ex m st n=2*m & m>=1) & Poly2(a,b,0,x|^ n) = 0 implies
    x=0 or x = n-root -(b/a) or x = -n-root -(b/a)
proof
    assume
A1:  a<>0 & b/a<0 & (ex m st n=2*m & m>=1) & Poly2(a,b,0,x|^ n) = 0;
A2:  -(b/a)>0 by A1,XREAL_1:60;
    set y = x|^ n;
    now per cases by A1,Th5;
    suppose x|^ n = 0;
      hence x =0 or x=n-root -(b/a) or x=-n-root -(b/a) by PREPOWER:12;
    end;
    suppose x|^ n = -(b/a);
      hence x =0 or x=n-root -(b/a) or x=-n-root -(b/a) by A1,A2,Th4;
    end;
  end;
  hence thesis;
end;

theorem Th11:
  a|^3+b|^3 = (a+b)*(a^2-a*b+b^2) &
  a|^5+b|^5 = (a+b)*(a|^4-a|^3*b+a|^2*b|^2-a*b|^3+b|^4)
proof
 A1: (a^2-a*b+b^2)*(a+b)
       =a^2*a+b*a^2-(a*(a*b)+b*(a*b))+(a*b^2+b*b^2+0*b^2)
       .=a|^ 3+b*a^2-(a*(a*b)+b*(a*b))+(a*b^2+b*b^2) by POLYEQ_2:4
       .=a|^ 3+b*a^2-(a*a*b+b*(b*a))+(a*b^2+b|^ 3) by POLYEQ_2:4
       .=a|^ 3+b*a^2-(a^2*b+b*b*a)+(a*b^2+b|^ 3)
       .=a|^ 3+a^2*b-(a^2*b+a*b^2)+(a*b^2+b|^ 3)
       .=a|^3+b|^3;
        (a+b)*(a|^4-a|^3*b+a|^2*b|^2-a*b|^3+b|^4)
       =a|^4*a+b*a|^4+0*a|^4-(a|^3*b)*(a+b)+(a|^2*b|^2)*(a+b)
                      -(a*b|^3)*(a+b)+b|^4*(a+b)
      .=a|^4*a|^1+b*a|^4+0*a|^4-(a|^3*b)*(a+b)+(a|^2*b|^2)*(a+b)
                      -(a*b|^3)*(a+b)+b|^4*(a+b) by NEWTON:10
      .=a|^(4+1)+b*a|^4-(a|^3*b)*(a+b+0)+(a|^2*b|^2)*(a+b)
                      -(a*b|^3)*(a+b)+b|^4*(a+b) by NEWTON:13
      .=(a|^5+b*a|^4)-(a*a|^3*b+b*(a|^3*b))+(a*(a|^2*b|^2)
       +b*(a|^2*b|^2))-(a*(a*b|^3)+b*(a*b|^3))+(a*b|^4+b*b|^4)
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+((a|^2*a)*b|^2
        +b*b|^2*a|^2)-(a*a*b|^3+b*b|^3*a)+(a*b|^4+b*b|^4) by POLYEQ_2:4
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a|^2*a*b|^2
        +b*b|^2*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b*b|^4) by POLYEQ_2:4
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a|^(2+1)*b|^2
        +b|^2*b*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b*b|^4) by NEWTON:11
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a|^3*b|^2
        +b|^(2+1)*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b|^4*b) by NEWTON:11
      .=(a|^5+b*a|^4)-(a|^4*b+b*b*a|^3)+(a|^3*b|^2
        +b|^3*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b|^(4+1)) by NEWTON:11
      .=(a|^5+b*a|^4)-(a|^4*b+b|^1*b*a|^3)+(a|^3*b|^2
        +b|^3*a|^2)-(a*a*b|^3+b|^4*a)+(a*b|^4+b|^(4+1)) by NEWTON:10
      .=(a|^5+b*a|^4)-(a|^4*b+b|^1*b*a|^3)+(a|^3*b|^2
       +b|^3*a|^2)-(a|^1*a*b|^3+b|^4*a)+(a*b|^4+b|^(4+1)) by NEWTON:10
      .=(a|^5+b*a|^4)-(a|^4*b+b|^(1+1)*a|^3)+(a|^3*b|^2
        +b|^3*a|^2)-(a|^1*a*b|^3+b|^4*a)+(a*b|^4+b|^5) by NEWTON:11
      .=(a|^5+b*a|^4)-(a|^4*b+b|^2*a|^3)+(a|^3*b|^2
        +b|^3*a|^2)-(a|^(1+1)*b|^3+b|^4*a)+(a*b|^4+b|^5) by NEWTON:11
      .=(a|^5+a|^2*b|^3)-(a|^2*b|^3+a*b|^4)+(a*b|^4+b|^5)
      .=a|^5+b|^5;
hence thesis by A1;

end;

theorem
   a<>0 & b^2-2*a*b-3*a^2>=0 & Poly3(a,b,b,a,x)=0 implies
   x=-1 or x= (a-b+sqrt(b^2-2*a*b-3*a^2))/(2*a)
       or x= (a-b-sqrt(b^2-2*a*b-3*a^2))/(2*a)
proof
     assume
 A1: a<>0 & b^2-2*a*b-3*a^2>=0 & Poly3(a,b,b,a,x)=0;
        a*(x|^ 3)+b*x^2+b*x+a=0 by A1,POLYEQ_1:def 4;
       then a*(x|^ 3)+(b*x^2+b*x)+a=0;
       then (x|^ 3)*a+1*a+0*a+(b*x^2+b*x)=0;
       then ((x|^ 3)+1+0)*a+(x^2*b+x*b+0*b)=0;
       then ((x|^ 3)+1)*a+(x^2+x+0)*b=0;
       then ((x|^ 3)+1)*a+(x*x+1*x+0*x)*b=0;
       then ((x|^ 3)+1 to_power 3)*a+(x+1)*x*b=0 by POWER:31;
       then (x|^ 3+1|^3)*a+(x+1)*x*b=0 by POWER:48;
       then ((x+1)*(x^2-x*1+1^2))*a+(x+1)*x*b=0 by Th11;
       then (x+1)*(x^2-x+1*1)*a+(x+1)*x*b=0;
       then A2: (x^2*a-x*a+x*b+a)*(x+1)=0;
        now per cases by A2,XCMPLX_1:6;
       case x+1=0;
        then x=0-1;
       hence x=-1
        or x= (a-b+sqrt(b^2-2*a*b-3*a^2))/(2*a)
        or x= (a-b-sqrt(b^2-2*a*b-3*a^2))/(2*a);
       end;
       case a*x^2-(a-b)*x+a=0;
       then a*x^2+(-a+b)*x+a=0;
 then A3: Poly2(a,-a+b,a,x)=0 by POLYEQ_1:def 2;
       A4: delta(a,-a+b,a)
        =(-a+b)^2-4*a*a by QUIN_1:def 1
       .=(b-a)^2-4*(a*a)
       .=b^2-2*b*a+a^2-4*(a*a)
       .=(b^2-2*a*b)+a^2-4*a^2
       .=(b^2-2*a*b)+(-(4-1))*a^2;
        then x= (-(-a+b)+sqrt delta(a,-a+b,a))/(2*a)
        or x= (-(-a+b)-sqrt delta(a,-a+b,a))/(2*a) by A1,A3,POLYEQ_1:5;
       hence x=-1 or x= (a-b+sqrt(b^2-2*a*b-3*a^2))/(2*a)
        or x= (a-b-sqrt(b^2-2*a*b-3*a^2))/(2*a) by A4;
       end;
      end;
  hence thesis;
end;

definition let a,b,c,d,e,f,x be Real;
  func Poly5(a,b,c,d,e,f,x) equals
    a*(x|^5)+b*(x|^4)+c*(x|^3)+d*(x^2)+e*x+f;
  coherence;
end;

theorem
   a<>0 & b^2+2*a*b+5*a^2-4*a*c>0 & Poly5(a,b,c,c,b,a,x)=0 implies
       (for y1,y2 being Real st
        y1 = (a-b+sqrt(b^2+2*a*b+5*a^2-4*a*c))/(2*a) &
        y2 = (a-b-sqrt(b^2+2*a*b+5*a^2-4*a*c))/(2*a) holds
        x=-1 or
        x = (y1 + sqrt delta(1,(-y1),1))/2 or
        x = (y2 + sqrt delta(1,(-y2),1))/2 or
        x = (y1 - sqrt delta(1,(-y1),1))/2 or
        x = (y2 - sqrt delta(1,(-y2),1))/2)
proof
    assume
A1: a<>0 & b^2+2*a*b+5*a^2-4*a*c>0 & Poly5(a,b,c,c,b,a,x)=0;
    let y1,y2 be Real;
    assume
A2: y1 = (a-b+sqrt(b^2+2*a*b+5*a^2-4*a*c))/(2*a)
       & y2 = (a-b-sqrt(b^2+2*a*b+5*a^2-4*a*c))/(2*a);
        a*(x|^5)+b*(x|^4)+c*(x|^3)+c*(x^2)+b*x+a=0 by A1;
       then A3: 0=a*(x|^5)+(b*(x|^4)+b*x)+(c*(x|^3)+c*(x^2))+a
       .=(x|^5+1)*a+(x|^4+x+0)*b+(c*(x|^3)+c*(x^2)+0*c)
       .=(x|^5+1|^5)*a+(x|^(3+1)+x)*b+(x|^3+x^2)*c by NEWTON:15
       .=(x|^5+1|^5)*a+(x|^3*x+x)*b+(x|^(2+1)+x^2)*c by NEWTON:11
       .=(x|^5+1|^5)*a+(x|^3+1+0)*x*b+(x*x|^(1+1)+1*x^2+0*x^2)*c by NEWTON:11
       .=(x|^5+1|^5)*a+(x|^3+1+0)*x*b+(x*(x|^1*x)+1*x^2+0*x^2)*c by NEWTON:11
       .=(x|^5+1|^5)*a+(x|^3+1+0)*x*b+(x*(x*x)+1*x^2+0*x^2)*c by NEWTON:10
       .=(x|^5+1|^5)*a+(x|^3+1+0)*x*b+(x*x^2+1*x^2+0*x^2)*c
       .=((x+1)*(x|^4-x|^3*1+x|^2*1|^2-x*1|^3+1|^4))*a
                            +(x|^3+1)*x*b+(x+1+0)*x^2*c by Th11
       .=((x+1)*(x|^4-x|^3+x|^2*1-x*1|^3+1|^4))*a
                            +(x|^3+1)*x*b+(x+1+0)*x^2*c by NEWTON:15
       .=((x+1)*(x|^4-x|^3+x|^2-x*1+1|^4))*a+(x|^3+1)*x*b+(x+1+0)*x^2*c
                                                          by NEWTON:15
       .=((x+1)*(x|^4-x|^3+x|^2-x+1))*a+(x|^3+1)*x*b+(x+1+0)*x^2*c
                                                         by NEWTON:15
       .=((x+1)*(x|^4-x|^3+x|^2-x+1))*a+(x|^3+1|^3)*x*b+(x+1)*x^2*c
                                                         by NEWTON:15
       .=((x+1)*(x|^4-x|^3+x|^2-x+1))*a+((x+1)*(x^2-x*1+1^2))*x*b
              +(x+1)*x^2*c by Th11
       .=(x+1)*((x|^4-x|^3+x|^2-x+1)*a)+(x+1)*(x^2-x+1)*x*b
              +(x+1)*x^2*c
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x^2*x*b-x*x*b+(b*x))
             +(x^2*c))*(x+1)
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x*x*x*b-x*x*b+(b*x))
             +(x^2*c))*(x+1)
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x*x*x*b-x*x*b+(b*x))
             +(x*x*c))*(x+1)
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^1*x*x*b-x*x*b+(b*x))
             +(x*x*c))*(x+1) by NEWTON:10
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^1*x*x*b-x*x*b+(b*x))
             +(x|^1*x*c))*(x+1) by NEWTON:10
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^1*x*x*b-x*x*b+(b*x))
             +(x|^(1+1)*c))*(x+1) by NEWTON:11
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^(1+1)*x*b-x*x*b+(b*x))
             +(x|^2*c))*(x+1) by NEWTON:11
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^(2+1)*b-x*x*b+b*x)
             +(x|^2*c))*(x+1) by NEWTON:11
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^3*b-x|^1*x*b+b*x)
             +(x|^2*c))*(x+1) by NEWTON:10
       .=(a*x|^4-a*x|^3+a*x|^2-a*x+a+(x|^3*b-x|^(1+1)*b+b*x)
             +(x|^2*c))*(x+1) by NEWTON:11
       .=((((a*x|^4-a*x|^3)+a*x|^2)-a*x)+a+(b*x|^3-b*x|^2+b*x)
              +c*x|^2)*(x+1)
       .=(a*x|^4-(a-b)*x|^3+(a+c-b)*x|^2-(a-b)*x+a)*(x+1);
        now per cases by A3,XCMPLX_1:6;
       case x+1=0;
       then x=0-1;
       hence x=-1 or
        x = (y1 + sqrt delta(1,(-y1),1))/2 or
        x = (y2 + sqrt delta(1,(-y2),1))/2 or
        x = (y1 - sqrt delta(1,(-y1),1))/2 or
        x = (y2 - sqrt delta(1,(-y2),1))/2;
       end;
       case a*x|^4-(a-b)*x|^3+(a+c-b)*x|^2-(a-b)*x+a=0;
       then 0=(a*x|^4+(-a+b)*x|^3+(a+c-b)*x|^2)+-(a-b)*x+a
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*x|^(1+1)+(-a+b)*x+a
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*(x|^1*x)+(-a+b)*x+a by NEWTON:11
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*(x*x)+(-a+b)*x+a by NEWTON:10
       .=a*x|^4+(-a+b)*x|^3+(a+c-b)*x^2+(-a+b)*x+a;
 then A4: Four(a,-a+b,a+c-b,-a+b,a,x)=0 by POLYEQ_2:def 1;
       set y=x+1/x;
 A5:  y=x+1/x;
        y1 = (-(-a+b)+sqrt((-a+b)^2-4*a*(a+c-b)+8*a^2))/(2*a) &
       y2 = (-(-a+b)-sqrt((-a+b)^2-4*a*(a+c-b)+8*a^2))/(2*a) by A2;
       hence x=-1 or
        x = (y1 + sqrt delta(1,(-y1),1))/2 or
        x = (y2 + sqrt delta(1,(-y2),1))/2 or
        x = (y1 - sqrt delta(1,(-y1),1))/2 or
        x = (y2 - sqrt delta(1,(-y2),1))/2 by A1,A4,A5,POLYEQ_2:3;
       end;
       end;
  hence thesis;
end;

theorem Th14:
   x+y=p & x*y=q & p^2-4*q>=0 implies
      x=(p+sqrt(p^2-4*q))/2 & y=(p-sqrt(p^2-4*q))/2 or
      x=(p-sqrt(p^2-4*q))/2 & y=(p+sqrt(p^2-4*q))/2
proof
     assume
A1: x+y=p & x*y=q & p^2-4*q>=0;
    p*y-y*y+0*y=q by A1;
    then p*y-y^2=q;
    then 1*y^2+(-p)*y+q=0;
 then A2: Poly2(1,-p,q,y)=0 by POLYEQ_1:def 2;
 A3: delta(1,-p,q)=(-p)^2-4*1*q by QUIN_1:def 1
       .=p^2-4*q;
       then A4: y=(-(-p)+sqrt delta(1,-p,q))/(2*1)
       or y=(-(-p)-sqrt delta(1,-p,q))/(2*1) by A1,A2,POLYEQ_1:5;
        now per cases by A4;
       suppose
 A5: y=(p+sqrt delta(1,-p,q))/2;
         then x=p*2/2-(p/2+(sqrt delta(1,-p,q))/2) by A1
         .=p*2/2-(p/2+(sqrt (p^2-4*q))/2) by A3;
         hence x=(p+sqrt(p^2-4*q))/2 & y=(p-sqrt(p^2-4*q))/2 or
          x=(p-sqrt(p^2-4*q))/2 & y=(p+sqrt(p^2-4*q))/2 by A3,A5;
       end;
       suppose
 A6:   y=(p-sqrt delta(1,-p,q))/2;
         then x=p-(p-sqrt delta(1,-p,q)+0)/2 by A1
         .=p-(p-sqrt (p^2-4*q)+0)/2 by A3;
         hence x=(p+sqrt(p^2-4*q))/2 & y=(p-sqrt(p^2-4*q))/2 or
          x=(p-sqrt(p^2-4*q))/2 & y=(p+sqrt(p^2-4*q))/2 by A3,A6;
       end;
       end;
  hence thesis;
end;

theorem
  (x|^ n)+(y|^ n)=p & (x|^ n)*(y|^ n)=q & p^2-4*q>=0 &
    (ex m st n=2*m+1) implies
      x=n-root((p+sqrt(p^2-4*q))/2)
      & y=n-root((p-sqrt(p^2-4*q))/2) or
      x=n-root((p-sqrt(p^2-4*q))/2)
      & y=n-root((p+sqrt(p^2-4*q))/2)
proof
    assume
A1: (x|^ n)+(y|^ n)=p & (x|^ n)*(y|^ n)=q & p^2-4*q>=0 & (ex m st n=2*m+1);
    set x1=x|^ n;
    set y1=y|^ n;
    x|^ n=(p+sqrt(p^2-4*q))/2 & y|^ n=(p-sqrt(p^2-4*q))/2 or
    x|^ n=(p-sqrt(p^2-4*q))/2 & y|^ n=(p+sqrt(p^2-4*q))/2 by A1,Th14;
    hence x=n-root((p+sqrt(p^2-4*q))/2) & y=n-root((p-sqrt(p^2-4*q))/2) or
        x=n-root((p-sqrt(p^2-4*q))/2) & y=n-root((p+sqrt(p^2-4*q))/2)
                                       by A1,POWER:5;
end;

theorem
  (x|^ n)+(y|^ n)=p & (x|^ n)*(y|^ n)=q & p^2-4*q>=0 & p>0 & q>0 &
    (ex m st n=2*m & m>=1) implies
      x=n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
proof
    assume
A1: (x|^ n)+(y|^ n)=p & (x|^ n)*(y|^ n)=q;
    assume
A2: p^2-4*q>=0 & p>0 & q>0 & (ex m st n=2*m & m>=1);
A3: delta(1,(-p),q)=(-p)^2-4*1*q by QUIN_1:def 1
        .=p^2-4*q;
    then 0 <= sqrt delta(1,(-p),q) by A2,SQUARE_1:82,94;
    then A4: -(-p)+sqrt delta(1,-p,q)>0 +0 by A2,XREAL_1:10;
    --4*q>0 by A2,REAL_2:122;
    then -4*q<0 by XREAL_1:60;
    then p^2+(-4*q)<p^2+0 by XREAL_1:10;
    then sqrt(p^2-4*q)<sqrt(p^2) by A2,SQUARE_1:95;
    then sqrt(p^2-4*q)<p by A2,SQUARE_1:89;
    then -sqrt(p^2-4*q)>-p by XREAL_1:26;
    then -sqrt(p^2-4*q)+p>-p+0+p by XREAL_1:10;
     then A5: (0+p+sqrt(p^2-4*q))/2>0 & (0+p-sqrt(p^2-4*q))/2>0
        by A3,A4,REAL_2:127;
    set x1=x|^ n;
    set y1=y|^ n;
    now per cases by A1,A2,Th14;
       suppose x|^ n=(p+sqrt(p^2-4*q))/2 & y|^ n=(p-sqrt(p^2-4*q))/2;
      hence
       x=n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
                                         by A2,A5,Th4;
       end;
       suppose
       x|^ n=(p-sqrt(p^2-4*q))/2 & y|^ n=(p+sqrt(p^2-4*q))/2;
      hence
       x=n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=-n-root((p+sqrt(p^2-4*q))/2)&y=-n-root((p-sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=n-root((p+sqrt(p^2-4*q))/2)
      or x=n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
      or x=-n-root((p-sqrt(p^2-4*q))/2)&y=-n-root((p+sqrt(p^2-4*q))/2)
                                              by A2,A5,Th4;
       end;
     end;
    hence thesis;
end;

canceled;

theorem
    x|^ n+y|^ n=a & x|^ n-y|^ n=b & (ex m st n=2*m & m>=1) & a>0
    & a+b>0 & a-b>0 implies
        x=n-root((a+b)/2) & y=n-root((a-b)/2) or
        x=n-root((a+b)/2) & y=-n-root((a-b)/2) or
        x=-n-root((a+b)/2)& y=n-root((a-b)/2) or
        x=-n-root((a+b)/2) & y=-n-root((a-b)/2)
proof
    assume
A1: x|^ n+y|^ n=a & x|^ n-y|^ n=b;
    assume
A2: (ex m st n=2*m & m>=1) & a>0 & a+b>0 & a-b>0;
    then (a+b)/2>0 & (a-b)/2>0 by REAL_2:127;
    hence x=n-root((a+b)/2) & y=n-root((a-b)/2) or
        x=n-root((a+b)/2) & y=-n-root((a-b)/2) or
        x=-n-root((a+b)/2)& y=n-root((a-b)/2) or
        x=-n-root((a+b)/2) & y=-n-root((a-b)/2) by A1,A2,Th4;
end;

theorem
    a*x|^ n+b*y|^ n=p & x*y=0 & (ex m st n=2*m+1) & a*b<>0 implies
    x=0 & y=n-root(p/b) or x=n-root(p/a) & y=0
proof
    assume
A1: a*x|^ n+b*y|^ n=p & x*y=0 & (ex m st n=2*m+1) & a*b<>0;
    consider m such that
A2: n=2*m+1 by A1;
    2*m>=0 by NAT_1:18;
    then A3: 2*m+1>=0+1 by XREAL_1:9;
then A4: n>0 & n>=1 by A2,XREAL_1:2;
    now per cases by A1,XCMPLX_1:6;
       suppose
 A5: x=0;
       then a*0 to_power n+b*y|^ n=p by A1,A2,A3,POWER:47;
       then a*0 +b*y|^ n=p by A4,POWER:def 2;
       then y|^ n=p/b by A1,XCMPLX_1:90;
     hence x=0 & y=n-root(p/b) or x=n-root(p/a) & y=0 by A1,A5,POWER:5;
       end;
       suppose
 A6: y=0;
       then a*x|^ n+b*0 to_power n=p by A1,A2,A3,POWER:47;
       then a*x|^ n+b*0=p by A4,POWER:def 2;
       then x|^ n=p/a by A1,XCMPLX_1:90;
     hence x=0 & y=n-root(p/b) or x=n-root(p/a) & y=0 by A1,A6,POWER:5;
    end;
  end;
 hence thesis;
end;

theorem
  a*x|^ n+b*y|^ n=p & x*y=0 & (ex m st n=2*m &m>=1) & p/b>0 &p/a>0 &a*b<>0
   implies
       x=0 & y=n-root(p/b)
       or x=0 & y=-n-root(p/b)
       or x=n-root(p/a) & y=0
       or x=-n-root(p/a) & y=0
proof
    assume
A1: a*x|^ n+b*y|^ n=p & x*y=0 & (ex m st n=2*m & m>=1) & p/b>0 &p/a>0 &a*b<>0;
    consider m such that
A2:  n=2*m & m>=1 by A1;
A3:  n>=1 by A2,REAL_2:137;
 then A4: n>0 by XREAL_1:2;
    per cases by A1,XCMPLX_1:6;
    suppose
 A5: x=0;
       then a*0 to_power n+b*y|^ n=p by A1,A3,POWER:47;
       then a*0 +b*y|^ n=p by A4,POWER:def 2;
       then y|^ n=p/b by A1,XCMPLX_1:90;
     hence x=0 & y=n-root(p/b)
       or x=0 & y=-n-root(p/b)
       or x=n-root(p/a) & y=0
       or x=-n-root(p/a) & y=0 by A1,A5,Th4;
       end;
       suppose
 A6: y=0;
       then a*x|^ n+b*0 to_power n=p by A1,A3,POWER:47;
       then a*x|^ n+b*0=p by A4,POWER:def 2;
       then x|^ n=p/a by A1,XCMPLX_1:90;
     hence x=0 & y=n-root(p/b) or x=0 & y=-n-root(p/b)
       or x=n-root(p/a) & y=0 or x=-n-root(p/a) & y=0 by A1,A6,Th4;
       end;
end;

theorem
  a*x|^ n=p & x*y=q & (ex m st n=2*m+1) & p*a<>0 implies
       x=n-root(p/a) & y=q*n-root(a/p)
proof
    assume
A1: a*x|^ n=p & x*y=q & (ex m st n=2*m+1) & p*a<>0;
    consider m such that
A2: n=2*m+1 by A1;
    2*m>=0 by NAT_1:18;
    then A3: 2*m+1>=0+1 by XREAL_1:9;
A4: p<>0 & a<>0 by A1;
    then A5: x|^ n=p/a by A1,XCMPLX_1:90;
    then x=n-root(p/a) by A1,POWER:5;
    then y*(n-root(p/a)*n-root(a/p))=q*n-root(a/p) by A1;
    then y*n-root((p/a)*(a/p))=q*n-root(a/p) by A1,POWER:12;
    then y*n-root((p/a)*(a*p"))=q*n-root(a/p) by XCMPLX_0:def 9;
    then y*n-root(p/a*a*p")=q*n-root(a/p);
    then y*n-root(p*p")=q*n-root(a/p) by A4,XCMPLX_1:88;
    then y*n-root(p/p)=q*n-root(a/p) by XCMPLX_0:def 9;
    then y*n-root(1)=q*n-root(a/p) by A4,XCMPLX_1:60;
    then y*1=q*n-root(a/p) by A2,A3,POWER:7;
    hence x=n-root(p/a) & y=q*n-root(a/p) by A1,A5,POWER:5;
end;

theorem
  a*x|^ n=p & x*y=q & (ex m st n=2*m & m>=1) & p/a>0 &a<>0 implies
    x=n-root(p/a) & y=q*n-root(a/p)
    or x=-n-root(p/a) & y=-q*n-root(a/p)
proof
    assume
A1: a*x|^ n=p & x*y=q & (ex m st n=2*m &m>=1) & p/a>0 &a<>0;
    consider m such that
A2: n=2*m & m>=1 by A1;
A3: n>=1 by A2,REAL_2:137;
A4: p<>0 by A1;
    (p/a)">0 by A1,XREAL_1:124;
    then 1/(p/a)>0 by XCMPLX_1:217;
then A5: (1*a)/p>0 by XCMPLX_1:78;
A6: x|^ n=p/a by A1,XCMPLX_1:90;
    per cases by A1,A6,Th4;
    suppose
 A7: x=n-root(p/a);
       then y*(n-root(p/a)*n-root(a/p))=q*n-root(a/p) by A1;
       then y*n-root((p/a)*(a/p))=q*n-root(a/p) by A1,A3,A5,POWER:12;
       then y*n-root((p/a)*(a*p"))=q*n-root(a/p) by XCMPLX_0:def 9;
       then y*n-root(p/a*a*p")=q*n-root(a/p);
       then y*n-root(p*p")=q*n-root(a/p) by A1,XCMPLX_1:88;
       then y*n-root(p/p)=q*n-root(a/p) by XCMPLX_0:def 9;
       then y*n-root(1)=q*n-root(a/p) by A4,XCMPLX_1:60;
       then y*1=q*n-root(a/p) by A3,POWER:7;
       hence x=n-root(p/a) & y=q*n-root(a/p)
       or x=-n-root(p/a) & y=-q*n-root(a/p) by A7;
     end;
     suppose
 A8: x=-n-root(p/a);
       then y*(n-root(p/a)*n-root(a/p))=-q*n-root(a/p) by A1;
       then y*n-root((p/a)*(a/p))=-q*n-root(a/p) by A1,A3,A5,POWER:12;
       then y*n-root((p/a)*(a*p"))=-q*n-root(a/p) by XCMPLX_0:def 9;
       then y*n-root(p/a*a*p")=-q*n-root(a/p);
       then y*n-root(p*p")=-q*n-root(a/p) by A1,XCMPLX_1:88;
       then y*n-root(p/p)=-q*n-root(a/p) by XCMPLX_0:def 9;
       then y*n-root(1)=-q*n-root(a/p) by A4,XCMPLX_1:60;
       then y*1=-q*n-root(a/p) by A3,POWER:7;
      hence x=n-root(p/a) & y=q*n-root(a/p)
       or x=-n-root(p/a) & y=-q*n-root(a/p) by A8;
     end;
end;

canceled;

theorem
  for a,x being Real st a>0 & a<>1 & a to_power x = 1 holds
    x=0
proof
    let a,x be Real;
    assume
A1: a>0 & a<>1 & a to_power x = 1;
    then x=log(a,1) by POWER:def 3;
    hence thesis by A1,POWER:59;
end;

theorem
  for a,x being Real st a>0 & a<>1 & a to_power x = a holds
    x=1
proof
    let a,x be Real;
    assume
A1: a>0 & a<>1 & a to_power x = a;
    then x=log(a,a) by POWER:def 3;
    hence thesis by A1,POWER:60;
end;

canceled;

theorem
  for a,b,x being Real st a>0 & a<>1 & x>0 & log(a,x) = 0 holds
    x = 1
proof
  let a,b,x be Real;
  assume a>0 & a<>1 & x>0 & log(a,x) = 0;
  then a to_power 0 = x by POWER:def 3;
  hence thesis by POWER:29;
end;

theorem
  for a,b,x being Real st a>0 & a<>1 & x>0 & log(a,x) = 1 holds
    x = a
proof
  let a,b,x be Real;
  assume a>0 & a<>1 & x>0 & log(a,x) = 1;
  then a to_power 1 = x by POWER:def 3;
  hence thesis by POWER:30;
end;
