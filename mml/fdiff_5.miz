:: Some Differentiable Formulas of Special Functions
::  by Jianbing Cao , Fahui Zhai and Xiquan Liang
::
:: Received November 7, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies PRE_TOPC, BOOLE, FUNCT_1, ARYTM_1, ARYTM_3, RELAT_1, PARTFUN1,
      SEQ_1, SIN_COS, FDIFF_1, PREPOWER, GROUP_1, SQUARE_1, TAYLOR_1, FINSEQ_1,
      NAT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, RELAT_1, PARTFUN1, PARTFUN2, NUMBERS,
      RCOMP_1, REAL_1, NAT_1, NAT_D, SEQ_1, FDIFF_1, PREPOWER, TAYLOR_1,
      SIN_COS, RFUNCT_1, SQUARE_1, XXREAL_0;
 constructors PARTFUN1, REAL_1, SQUARE_1, NAT_1, RCOMP_1, PARTFUN2, RFUNCT_1,
      FDIFF_1, PREPOWER, POWER, SIN_COS, TAYLOR_1, ARYTM_3, NAT_D;
 registrations RELSET_1, MEMBERED, RCOMP_1, INT_1, SEQ_1, XXREAL_0, XREAL_0,
      ARYTM_3, NAT_1, ORDINAL1;
 requirements SUBSET, NUMERALS, ARITHM;
 definitions SQUARE_1;
 theorems FDIFF_2, TAYLOR_1, XBOOLE_1, XCMPLX_1, NAT_1, SEQ_1, FDIFF_1,
      FUNCT_1, TARSKI, PREPOWER, RFUNCT_1, XREAL_1, XCMPLX_0, NEWTON, SIN_COS;

begin

 reserve y for set,
         x,a,b for Real,
         n for Element of NAT,
         Z for open Subset of REAL,
         f,f1,f2,g for PartFunc of REAL,REAL;

Lm1:
  Z c= dom (f1+f2) & (for x st x in Z holds f1.x=a) & f2=#Z 2 implies
    f1+f2 is_differentiable_on Z &
  for x st x in Z holds ((f1+f2)`|Z).x = 2*x
  proof
    assume A1:Z c= dom (f1+f2) & (for x st x in Z holds f1.x=a) & f2=#Z 2;
    then Z c= dom f1 /\ dom f2 by SEQ_1:def 3;
    then A2:Z c= dom f1 & Z c= dom f2 by XBOOLE_1:18;
A3: for x st x in Z holds f1.x = 0*x+a by A1;
    then A4:f1 is_differentiable_on Z &
    for x st x in Z holds (f1`|Z).x = 0 by FDIFF_1:31,A2;
    for x st x in Z holds f2 is_differentiable_in x by A1,TAYLOR_1:2;
    then A5:f2 is_differentiable_on Z by FDIFF_1:16,A2;
A6: for x st x in Z holds (f2`|Z).x = 2 * x #Z (2-1)
    proof
      let x;
      assume A7:x in Z;
      diff(f2,x) = 2 * x #Z (2-1) by A1,TAYLOR_1:2;
      hence (f2`|Z).x =2 * x #Z (2-1) by A7,A5,FDIFF_1:def 8;
    end;
    for x st x in Z holds ((f1+f2)`|Z).x = 2*x
    proof
      let x;
      assume A8:x in Z;
      then ((f1+f2)`|Z).x=diff(f1,x)+diff(f2,x) by A4,A5,FDIFF_1:26,A1;
      hence ((f1+f2)`|Z).x=(f1`|Z).x +diff(f2,x) by A4,A8,FDIFF_1:def 8
       .=(f1`|Z).x +(f2`|Z).x by A5,A8,FDIFF_1:def 8
       .=0+(f2`|Z).x by FDIFF_1:31,A2,A3,A8
       .=2 * x #Z (2-1) by A6,A8
       .=2*x|^1 by PREPOWER:46
       .=2*x by NEWTON:10;
    end;
    hence thesis by A4,A5,FDIFF_1:26,A1;
  end;

Lm2:
  Z c= dom (f1-f2) & (for x st x in Z holds f1.x=a) & f2=#Z 2
  implies f1-f2 is_differentiable_on Z &
  for x st x in Z holds ((f1-f2)`|Z).x = -2*x
  proof
    assume
A1: Z c= dom (f1-f2) & (for x st x in Z holds f1.x=a) & f2=#Z 2;
    then Z c= dom f1 /\ dom f2 by SEQ_1:def 4; then
A2: Z c= dom f1 & Z c= dom f2 by XBOOLE_1:18;
A3: for x st x in Z holds f1.x = 0*x+a by A1; then
A4: f1 is_differentiable_on Z &
    for x st x in Z holds (f1`|Z).x = 0 by FDIFF_1:31,A2;
    for x st x in Z holds f2 is_differentiable_in x by A1,TAYLOR_1:2; then
A5: f2 is_differentiable_on Z by FDIFF_1:16,A2;
A6: for x st x in Z holds (f2`|Z).x = 2 * x #Z (2-1)
    proof
      let x;
      assume
A7:   x in Z;
      diff(f2,x) = 2 * x #Z (2-1) by A1,TAYLOR_1:2;
      hence (f2`|Z).x =2 * x #Z (2-1) by A7,A5,FDIFF_1:def 8;
    end;
    for x st x in Z holds ((f1-f2)`|Z).x = -2*x
    proof
      let x;
      assume
A8:   x in Z;
      then ((f1-f2)`|Z).x=diff(f1,x)-diff(f2,x) by A4,A5,FDIFF_1:27,A1;
      hence ((f1-f2)`|Z).x=(f1`|Z).x -diff(f2,x) by A4,A8,FDIFF_1:def 8
      .=(f1`|Z).x -(f2`|Z).x by A5,A8,FDIFF_1:def 8
      .=0-((f2`|Z).x) by FDIFF_1:31,A2,A3,A8
      .=0-(2 * x #Z (2-1)) by A6,A8
      .=0-(2*x|^1) by PREPOWER:46
      .=-(2*x|^1)
      .=-2*x by NEWTON:10;
    end;
    hence thesis by A4,A5,FDIFF_1:27,A1;
  end;

Lm3:
  Z c= dom (( #R (1/2))*f) & (for x st x in Z holds f.x=x & f.x>0)
  implies ( #R (1/2))*f is_differentiable_on Z &
  for x st x in Z holds ((( #R (1/2))*f)`|Z).x =(1/2)*x #R (-1/2)
  proof
    assume A1:Z c= dom (( #R (1/2))*f) &
    (for x st x in Z holds f.x=x & f.x>0); then
    for y st y in Z holds y in dom f by FUNCT_1:21;
    then A2:Z c= dom f by TARSKI:def 3;
A3: for x st x in Z holds f.x = 1*x+0 by A1;
    then A4:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
A5: for x st x in Z holds ( #R (1/2))*f is_differentiable_in x
    proof
      let x;
      assume A6:x in Z;
      then A7:f is_differentiable_in x by FDIFF_1:16,A4;
      f.x >0 by A1,A6;
      hence ( #R (1/2))*f is_differentiable_in x by TAYLOR_1:22,A7;
    end;then
A8:  ( #R (1/2))*f is_differentiable_on Z by A1,FDIFF_1:16;
    for x st x in Z holds ((( #R (1/2))*f)`|Z).x =(1/2)*x #R (-1/2)
    proof
      let x;
      assume A9:x in Z;
      then A10:f is_differentiable_in x by FDIFF_1:16,A4;
A11:   f.x = x & f.x >0 by A1,A9;
      diff(( #R (1/2))*f,x) = (1/2)*((f.x) #R (1/2-1)) * diff(f,x)
        by TAYLOR_1:22,A11,A10
      .= (1/2)*((f.x) #R (1/2-1))*(f`|Z).x by A4,A9,FDIFF_1:def 8
      .= (1/2)*(x #R (1/2-1))*1 by FDIFF_1:31,A2,A3,A9,A11
      .=(1/2)*x #R (-1/2);
      hence ((( #R (1/2))*f)`|Z).x =(1/2)*x #R (-1/2)
        by A9,A8,FDIFF_1:def 8;
    end;
    hence thesis by A1,FDIFF_1:16,A5;
  end;

:: 1 f.x=(a+x)/(a-x)
theorem
  Z c= dom (f1/f2) &
  (for x st x in Z holds f1.x=a+x & f2.x=a-x & f2.x<>0) implies
  (f1/f2) is_differentiable_on Z & for x st x in Z holds
  ((f1/f2)`|Z).x = (2*a)/(a-x)^2
  proof
    assume A1:Z c= dom (f1/f2) &
    (for x st x in Z holds f1.x=a+x & f2.x=a-x & f2.x<>0); then
    Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A2:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    A3:Z c= dom f2 by XBOOLE_1:1,A2;
A4: for x st x in Z holds f1.x=1*x+a by A1;
    then A5:f1 is_differentiable_on Z & for x st x in Z holds (f1`|Z).x = 1
      by FDIFF_1:31,A2;
A6: for x st x in Z holds f2.x =(-1)*x+a
    proof
      let x;
      assume x in Z;
      then f2.x =a-x by A1;
      hence thesis;
    end; then
A7: f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = -1 by FDIFF_1:31,A3;
A8:  for x st x in Z holds f2.x <> 0 by A1;then
A9: f1/f2 is_differentiable_on Z by A5,A7,FDIFF_2:21;
    for x st x in Z holds ((f1/f2)`|Z).x = (2*a)/(a-x)^2
    proof
      let x;
      assume A10:x in Z;
      then A11:f1 is_differentiable_in x by A5,FDIFF_1:16;
A12:  f2 is_differentiable_in x by A7,A10,FDIFF_1:16;
A13:  f2.x <>0 & f1.x=a+x & f2.x=a-x by A1,A10;
      then diff(f1/f2,x)
      =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A11,A12
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A5,A10,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A7,A10,FDIFF_1:def 8
      .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A10,FDIFF_1:31,A2,A4
      .=(1* f2.x-(-1)* f1.x)/(f2.x)^2 by A10,FDIFF_1:31,A3,A6
      .=(2*a)/(a-x)^2 by A13;
      hence ((f1/f2)`|Z).x = (2*a)/(a-x)^2 by FDIFF_1:def 8,A9,A10;
    end;
    hence thesis by A8,A5,A7,FDIFF_2:21;
  end;

:: 2 f.x=(x-a)/(x+a)
theorem
  Z c= dom (f1/f2) &
  (for x st x in Z holds f1.x=x-a & f2.x=x+a & f2.x<>0) implies
  (f1/f2) is_differentiable_on Z & for x st x in Z holds
  ((f1/f2)`|Z).x = (2*a)/(x+a)^2
  proof
    assume
A1: Z c= dom (f1/f2) &
    (for x st x in Z holds f1.x=x-a & f2.x=x+a & f2.x<>0); then
    Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4; then
A2: Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
A3: Z c= dom f2 by XBOOLE_1:1,A2;
A4: for x st x in Z holds f1.x = 1*x+-a
    proof
      let x;
      assume A5:x in Z;
      1*x+-a=1*x-a;
      hence thesis by A1,A5;
    end; then
A6: f1 is_differentiable_on Z &
    for x st x in Z holds (f1`|Z).x = 1 by FDIFF_1:31,A2;
A7: for x st x in Z holds f2.x = 1*x+a by A1;
    then A8:f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = 1 by FDIFF_1:31,A3;
A9:  for x st x in Z holds f2.x <> 0 by A1; then
A10: f1/f2 is_differentiable_on Z by A6,A8,FDIFF_2:21;
    for x st x in Z holds ((f1/f2)`|Z).x = (2*a)/(x+a)^2
    proof
      let x;
      assume
A11:   x in Z; then
A12:   f1 is_differentiable_in x by A6,FDIFF_1:16;
A13:  f2 is_differentiable_in x by A8,A11,FDIFF_1:16;
A14:  f2.x <>0 & f1.x=x-a & f2.x=x+a by A1,A11;
      then diff(f1/f2,x)
       =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A12,A13
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A6,A11,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A8,A11,FDIFF_1:def 8
      .=(1*f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A11,FDIFF_1:31,A2,A4
      .=(1*f2.x-1* f1.x)/(f2.x)^2 by A11,FDIFF_1:31,A3,A7
      .=(2*a)/(x+a)^2 by A14;
      hence ((f1/f2)`|Z).x = (2*a)/(x+a)^2 by FDIFF_1:def 8,A10,A11;
    end;
    hence thesis by A9,A6,A8,FDIFF_2:21;
  end;

:: 3 f.x=(x-a)/(x-b)
theorem
  Z c= dom (f1/f2) &
  (for x st x in Z holds f1.x=x-a & f2.x=x-b & f2.x<>0) implies
  (f1/f2) is_differentiable_on Z & for x st x in Z holds
  ((f1/f2)`|Z).x = (a-b)/(x-b)^2
  proof
    assume A1:Z c= dom (f1/f2) &
    (for x st x in Z holds f1.x=x-a & f2.x=x-b & f2.x<>0); then
    Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
    then A2:Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
    A3:Z c= dom f2 by XBOOLE_1:1,A2;
A4: for x st x in Z holds f1.x = 1*x+-a & f2.x = 1*x+-b
    proof
      let x;
      assume A5:x in Z;
      1*x+-a=1*x-a & 1*x+-b=1*x-b;
      hence thesis by A1,A5;
    end; then
A6: for x st x in Z holds f1.x = 1*x+-a; then
A7: f1 is_differentiable_on Z & for x st x in Z holds
    (f1`|Z).x = 1 by FDIFF_1:31,A2;
A8: for x st x in Z holds f2.x = 1*x+-b by A4; then
A9: f2 is_differentiable_on Z &
    for x st x in Z holds (f2`|Z).x = 1 by FDIFF_1:31,A3;
A10:  for x st x in Z holds f2.x <> 0 by A1; then
A11: f1/f2 is_differentiable_on Z by A7,A9,FDIFF_2:21;
    for x st x in Z holds ((f1/f2)`|Z).x = (a-b)/(x-b)^2
    proof
      let x;
      assume
A12:   x in Z;then
A13:   f1 is_differentiable_in x by A7,FDIFF_1:16;
A14:  f2 is_differentiable_in x by A9,A12,FDIFF_1:16;
A15:  f2.x <>0 & f1.x=x-a & f2.x=x-b by A1,A12;
      then diff(f1/f2,x)
      =(diff(f1,x) * f2.x - diff(f2,x) * f1.x)/(f2.x)^2 by FDIFF_2:14,A13,A14
      .=((f1`|Z).x * f2.x-diff(f2,x) * f1.x)/(f2.x)^2 by A7,A12,FDIFF_1:def 8
      .= ((f1`|Z).x * f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A9,A12,FDIFF_1:def 8
      .=(1* f2.x-(f2`|Z).x * f1.x)/(f2.x)^2 by A12,FDIFF_1:31,A2,A6
      .=(1* f2.x-1* f1.x)/(f2.x)^2 by A12,FDIFF_1:31,A3,A8
      .=(a-b)/(x-b)^2 by A15;
      hence ((f1/f2)`|Z).x = (a-b)/(x-b)^2 by FDIFF_1:def 8,A11,A12;
    end;
    hence thesis by A10,A7,A9,FDIFF_2:21;
  end;

:: 4 f.x=1/x
theorem Th4:
  Z c= dom f & (for x st x in Z holds f.x = x & f.x<>0) implies
  f^ is_differentiable_on Z & for x st x in Z holds
  ((f^)`|Z).x= -1/x^2
  proof
    assume that A1: Z c= dom f and
A2: for x st x in Z holds f.x = x & f.x<>0;
A3: for x st x in Z holds f.x = 1*x+0 by A2;then
A4: f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A1;
A5: for x st x in Z holds f.x<>0 by A2;
    then A6:f^ is_differentiable_on Z by A4,FDIFF_2:22;
    now let x;
      assume A7:x in Z;
A8:   f.x<>0 by A2,A7;
A9:   f is_differentiable_in x by A7,A4,FDIFF_1:16;
      ((f^)`|Z).x= diff(f^,x) by A7,A6,FDIFF_1:def 8
        .= - diff(f,x)/( f.x)^2 by A8,FDIFF_2:15,A9
        .=- (f`|Z).x/( f.x)^2 by A7,A4,FDIFF_1:def 8
        .=-1/( f.x)^2 by FDIFF_1:31,A1,A3,A7
        .=-1/x^2 by A2,A7;
      hence ((f^)`|Z).x=-1/x^2;
    end;
    hence thesis by A4,FDIFF_2:22,A5;
  end;

:: 5 f.x=sin(1/x)
theorem Th5:
  Z c= dom (sin*(f^)) &
  (for x st x in Z holds f.x = x & f.x<>0) implies
  sin*(f^) is_differentiable_on Z &
  for x st x in Z holds ((sin*(f^))`|Z).x = -(1/x^2)*cos.(1/x)
proof
    assume that
A1: Z c= dom (sin*(f^)) and
A2: for x st x in Z holds f.x = x & f.x<>0;
    for y st y in Z holds y in dom (f^) by FUNCT_1:21,A1;then
A3: Z c= dom (f^) by TARSKI:def 3;
    dom (f^) c= dom f by RFUNCT_1:11; then
A4: Z c= dom f by XBOOLE_1:1,A3;
A5: f^ is_differentiable_on Z & for x st x in Z holds
    ((f^)`|Z).x= -1/x^2 by A2,Th4,A4;
A6: for x st x in Z holds sin*(f^) is_differentiable_in x
    proof
      let x; assume x in Z; then
A7:   f^ is_differentiable_in x by FDIFF_1:16,A5;
      sin is_differentiable_in (f^).x by SIN_COS:69;
      hence sin*(f^) is_differentiable_in x by A7,FDIFF_2:13;
    end;then
A8: sin*(f^) is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds ((sin*(f^))`|Z).x = -(1/x^2)*cos.(1/x)
    proof
      let x;
      assume
A9:   x in Z;then
A10:   f^ is_differentiable_in x by FDIFF_1:16,A5;
      sin is_differentiable_in (f^).x by SIN_COS:69;
      then diff(sin*(f^),x) = diff(sin, (f^).x)*diff(f^,x) by A10,FDIFF_2:13
        .=cos.((f^).x)*diff(f^,x) by SIN_COS:69
        .=cos.((f.x)")*diff(f^,x) by A3,A9,RFUNCT_1:def 8
        .=cos.((f.x)")*((f^)`|Z).x by A5,FDIFF_1:def 8,A9
        .=cos.((f.x)")*(-1/x^2) by A9,A2,Th4,A4
        .=cos.(1*x")*(-1/x^2) by A2,A9
        .=cos.(1/x)*(-1/x^2) by XCMPLX_0:def 9;
      hence thesis by FDIFF_1:def 8,A8,A9;
    end;
    hence thesis by FDIFF_1:16,A1,A6;
  end;

:: 6 f.x=cos(1/x)
theorem Th6:
  Z c= dom (cos*(f^)) & (for x st x in Z holds f.x = x & f.x<>0) implies
  cos*(f^) is_differentiable_on Z &
  for x st x in Z holds ((cos*(f^))`|Z).x = 1/x^2*sin.(1/x)
proof
    assume that
A1: Z c= dom (cos*(f^)) and
A2: for x st x in Z holds f.x = x & f.x<>0;
    for y st y in Z holds y in dom (f^) by FUNCT_1:21,A1; then
A3: Z c= dom (f^) by TARSKI:def 3;
    dom (f^) c= dom f by RFUNCT_1:11; then
A4: Z c= dom f by XBOOLE_1:1,A3;
A5: f^ is_differentiable_on Z & for x st x in Z holds
    ((f^)`|Z).x= -1/x^2 by A2,Th4,A4;
A6: for x st x in Z holds cos*(f^) is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then A7: f^ is_differentiable_in x by FDIFF_1:16,A5;
      cos is_differentiable_in (f^).x by SIN_COS:68;
      hence cos*(f^) is_differentiable_in x by A7,FDIFF_2:13;
    end; then
A8: cos*(f^) is_differentiable_on Z by FDIFF_1:16,A1;
    for x st x in Z holds ((cos*(f^))`|Z).x = 1/x^2*sin.(1/x)
    proof
      let x;
      assume A9:x in Z;
      then A10: f^ is_differentiable_in x by FDIFF_1:16,A5;
A11:   diff(cos, (f^).x)=-sin.((f^).x) by SIN_COS:68;
      cos is_differentiable_in (f^).x by SIN_COS:68;
      then diff(cos*(f^),x) = diff(cos, (f^).x)*diff(f^,x) by A10,FDIFF_2:13
        .=-sin.((f^).x)*diff(f^,x) by A11
        .=-sin.((f.x)")*diff(f^,x) by A3,A9,RFUNCT_1:def 8
        .=-sin.((f.x)")*((f^)`|Z).x by A5,FDIFF_1:def 8,A9
        .=-sin.((f.x)")*(-1/x^2) by A9,A2,Th4,A4
        .=-sin.(1*x")*(-1/x^2) by A2,A9
        .=-sin.(1/x)*(-1/x^2) by XCMPLX_0:def 9
        .=sin.(1/x)*(1/x^2);
      hence thesis by FDIFF_1:def 8,A8,A9;
    end;
    hence thesis by FDIFF_1:16,A1,A6;
  end;

:: 7 f.x=x*sin(1/x)
theorem
  Z c= dom ((id Z)(#)(sin*(f^))) &
  (for x st x in Z holds f.x = x & f.x<>0) implies
  (id Z)(#)(sin*(f^)) is_differentiable_on Z &
  for x st x in Z holds
    (((id Z)(#)(sin*(f^)))`|Z).x = sin.(1/x)-(1/x)*cos.(1/x)
proof
   assume
A1:Z c= dom ((id Z)(#)(sin*(f^))); then
   Z c= dom (id Z) /\ dom (sin*(f^)) by SEQ_1:def 5; then
A2: Z c= dom id Z & Z c= dom (sin*(f^)) by XBOOLE_1:18;
   assume
A3: for x st x in Z holds f.x = x & f.x<>0;
   for y st y in Z holds y in dom (f^) by FUNCT_1:21,A2; then
A4:Z c= dom (f^) by TARSKI:def 3;
A5:sin*(f^) is_differentiable_on Z &
   for x st x in Z holds ((sin*(f^))`|Z).x
     = -(1/x^2)*cos.(1/x) by A2,A3,Th5;
A6:for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35; then
A7:id Z is_differentiable_on Z & for x st x in Z holds
     ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
    now let x;
      assume
A8:   x in Z;then f.x = x by A3; then
A9:    x<>0 by A3,A8;
      (((id Z)(#)(sin*(f^)))`|Z).x
         = ((sin*(f^)).x)*diff((id Z),x) + ((id Z).x)*diff(sin*(f^),x)
          by A5,A1,FDIFF_1:29,A7,A8
        .=((sin*(f^)).x)*((id Z)`|Z).x+ ((id Z).x)*diff(sin*(f^),x)
          by A8,FDIFF_1:def 8,A7
        .=((sin*(f^)).x)*1+ ((id Z).x)*diff(sin*(f^),x)
          by A6,A8,A2,FDIFF_1:31
        .=((sin*(f^)).x)+ x*diff(sin*(f^),x) by FUNCT_1:35,A8
        .=((sin*(f^)).x)+ x*(((sin*(f^))`|Z).x) by A5,A8,FDIFF_1:def 8
        .=((sin*(f^)).x)+ x*(-(1/x^2)*cos.(1/x)) by A3,A2,A8,Th5
        .=((sin*(f^)).x) -(x*(1/(x*x))*cos.(1/x))
        .=((sin*(f^)).x) -x*((1/x)*(1/x))*cos.(1/x) by XCMPLX_1:103
        .=((sin*(f^)).x)-(x*(1/x))*(1/x)*cos.(1/x)
        .=((sin*(f^)).x)-(1)*(1/x)*cos.(1/x) by A9,XCMPLX_1:107
        .=(sin.((f^).x))-(1/x)*cos.(1/x) by A2,A8,FUNCT_1:22
        .=(sin.((f.x)"))-(1/x)*cos.(1/x) by A8,A4,RFUNCT_1:def 8
        .=sin.(1*x")-(1/x)*cos.(1/x) by A8,A3
        .=sin.(1/x)-(1/x)*cos.(1/x) by XCMPLX_0:def 9;
      hence (((id Z)(#)(sin*(f^)))`|Z).x = sin.(1/x)-(1/x)*cos.(1/x);
    end;
    hence thesis by A1,FDIFF_1:29,A5,A7;
  end;

:: 8 f.x=x*cos(1/x)
theorem
  Z c= dom ((id Z)(#)(cos*(f^))) &
  (for x st x in Z holds f.x = x & f.x<>0) implies
  (id Z)(#)(cos*(f^)) is_differentiable_on Z &
  for x st x in Z holds
    (((id Z)(#)(cos*(f^)))`|Z).x = cos.(1/x)+(1/x)*sin.(1/x)
proof
   assume
A1:Z c= dom ((id Z)(#)(cos*(f^))); then
   Z c= dom (id Z) /\ dom (cos*(f^)) by SEQ_1:def 5; then
A2: Z c= dom id Z & Z c= dom (cos*(f^)) by XBOOLE_1:18;
   assume
A3: for x st x in Z holds f.x = x & f.x<>0;
   for y st y in Z holds y in dom (f^) by FUNCT_1:21,A2;then
A4:Z c= dom (f^) by TARSKI:def 3;
A5:cos*(f^) is_differentiable_on Z &
   for x st x in Z holds ((cos*(f^))`|Z).x
     = (1/x^2)*sin.(1/x) by A2,A3,Th6;
A6:for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;then
A7:id Z is_differentiable_on Z & for x st x in Z
     holds ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
   now let x;
     assume
A8:  x in Z; then f.x = x by A3; then
A9:   x<>0 by A3,A8;
     (((id Z)(#)(cos*(f^)))`|Z).x
      = ((cos*(f^)).x)*diff((id Z),x) + ((id Z).x)*diff(cos*(f^),x)
        by A5,A1,FDIFF_1:29,A7,A8
     .=((cos*(f^)).x)*((id Z)`|Z).x+ ((id Z).x)*diff(cos*(f^),x)
        by A8,FDIFF_1:def 8,A7
     .=((cos*(f^)).x)*1+ ((id Z).x)*diff(cos*(f^),x) by A6,A8,A2,FDIFF_1:31
     .=((cos*(f^)).x)+ x*diff(cos*(f^),x) by FUNCT_1:35,A8
     .=((cos*(f^)).x)+ x*(((cos*(f^))`|Z).x) by A5,A8,FDIFF_1:def 8
     .=((cos*(f^)).x)+ x*((1/x^2)*sin.(1/x)) by A3,A2,A8,Th6
     .=((cos*(f^)).x) +(x*(1/(x*x))*sin.(1/x))
     .=((cos*(f^)).x) +x*((1/x)*(1/x))*sin.(1/x) by XCMPLX_1:103
     .=((cos*(f^)).x)+(x*(1/x))*(1/x)*sin.(1/x)
     .=((cos*(f^)).x)+(1)*(1/x)*sin.(1/x) by A9,XCMPLX_1:107
     .=(cos.((f^).x))+(1/x)*sin.(1/x) by A2,A8,FUNCT_1:22
     .=(cos.((f.x)"))+(1/x)*sin.(1/x) by A8,A4,RFUNCT_1:def 8
     .=cos.(1*x")+(1/x)*sin.(1/x) by A8,A3
     .=cos.(1/x)+(1/x)*sin.(1/x) by XCMPLX_0:def 9;
      hence (((id Z)(#)(cos*(f^)))`|Z).x = cos.(1/x)+(1/x)*sin.(1/x);
    end;
    hence thesis by A1,FDIFF_1:29,A5,A7;
  end;

:: 9 f.x=sin(1/x)*cos(1/x)
theorem
  Z c= dom ((sin*(f^))(#)(cos*(f^))) &
   (for x st x in Z holds f.x = x & f.x<>0) implies
   (sin*(f^))(#)(cos*(f^)) is_differentiable_on Z &
   for x st x in Z holds
     (((sin*(f^))(#)(cos*(f^)))`|Z).x =(1/x^2)*((sin.(1/x))^2-(cos.(1/x))^2)
proof
    assume
A1: Z c= dom ((sin*(f^))(#)(cos*(f^))) &
    (for x st x in Z holds f.x = x & f.x<>0); then
    Z c= dom (sin*(f^)) /\ dom (cos*(f^)) by SEQ_1:def 5; then
A2: Z c= dom (sin*(f^)) & Z c= dom (cos*(f^)) by XBOOLE_1:18;
    for y st y in Z holds y in dom (f^) by FUNCT_1:21,A2; then
A3: Z c= dom (f^) by TARSKI:def 3;
A4: cos*(f^) is_differentiable_on Z &
    for x st x in Z holds ((cos*(f^))`|Z).x
      = (1/x^2)*sin.(1/x) by A1,A2,Th6;
A5: sin*(f^) is_differentiable_on Z &
    for x st x in Z holds ((sin*(f^))`|Z).x
      = -(1/x^2)*cos.(1/x) by A1,A2,Th5;
    now let x;
      assume
A6:   x in Z;
      (((sin*(f^))(#)(cos*(f^)))`|Z).x
       = ((cos*(f^)).x)*diff((sin*(f^)),x) + ((sin*(f^)).x)*diff(cos*(f^),x)
         by A4,A1,FDIFF_1:29,A6,A5
      .=((cos*(f^)).x)*((sin*(f^))`|Z).x+ ((sin*(f^)).x)*diff(cos*(f^),x)
         by A5,FDIFF_1:def 8,A6
      .=((cos*(f^)).x)*(-(1/x^2)*cos.(1/x))+((sin*(f^)).x)*diff(cos*(f^),x)
         by A6,A1,A2,Th5
      .=((cos*(f^)).x)*(-(1/x^2)*cos.(1/x))+((sin*(f^)).x)*(((cos*(f^))`|Z).x)
         by A4,FDIFF_1:def 8,A6
      .=((cos*(f^)).x)*(-(1/x^2)*cos.(1/x))+((sin*(f^)).x)*((1/x^2)*sin.(1/x))
         by A1,A2,A6,Th6
      .=(cos.((f^).x))*(-(1/x^2)*cos.(1/x))+((sin*(f^)).x)*((1/x^2)*sin.(1/x))
         by A2,A6,FUNCT_1:22
      .=(cos.((f.x)"))*(-(1/x^2)*cos.(1/x))+((sin*(f^)).x)*((1/x^2)*sin.(1/x))
         by A6,RFUNCT_1:def 8,A3
      .=(cos.(1*x"))*(-(1/x^2)*cos.(1/x))+((sin*(f^)).x)*((1/x^2)*sin.(1/x))
         by A1,A6
      .=cos.(1/x)*(-(1/x^2)*cos.(1/x))+((sin*(f^)).x)*((1/x^2)*sin.(1/x))
         by XCMPLX_0:def 9
      .=cos.(1/x)*(-(1/x^2)*cos.(1/x))+(sin.((f^).x))*((1/x^2)*sin.(1/x))
         by A2,A6,FUNCT_1:22
      .=cos.(1/x)*(-(1/x^2)*cos.(1/x))+(sin.((f.x)"))*((1/x^2)*sin.(1/x))
         by A6,RFUNCT_1:def 8,A3
      .=cos.(1/x)*(-(1/x^2)*cos.(1/x))+(sin.(1*x"))*((1/x^2)*sin.(1/x))
         by A1,A6
      .=-cos.(1/x)*(1/x^2)*cos.(1/x)+sin.(1/x)*((1/x^2)*sin.(1/x))
         by XCMPLX_0:def 9
      .=(1/x^2)*((sin.(1/x))^2-(cos.(1/x))^2);
      hence (((sin*(f^))(#)(cos*(f^)))`|Z).x =
        (1/x^2)*((sin.(1/x))^2-(cos.(1/x))^2);
    end;
    hence thesis by A1,FDIFF_1:29,A4,A5;
  end;

:: 10 f.x=sin(n*x)*(sin x #Z n)
theorem
  Z c= dom ((sin*f)(#)(( #Z n)*sin)) & n>=1 &
  (for x st x in Z holds f.x=n*x) implies
  (sin*f)(#)(( #Z n)*sin) is_differentiable_on Z &
  for x st x in Z holds
    (((sin*f)(#)(( #Z n)*sin))`|Z).x =n*(sin.x) #Z (n-1)*sin.((n+1)*x)
proof
   assume
A1:Z c= dom ((sin*f)(#)(( #Z n)*sin)) & n>=1 &
   (for x st x in Z holds f.x=n*x); then
   Z c= dom (sin*f) /\ dom (( #Z n)*sin) by SEQ_1:def 5; then
A2:Z c= dom (sin*f) & Z c= dom (( #Z n)*sin) by XBOOLE_1:18;
   for y st y in Z holds y in dom f by FUNCT_1:21,A2; then
A3:Z c= dom f by TARSKI:def 3;
A4: for x st x in Z holds f.x=n*x+0 by A1;
A5:f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = n by A4,A3,FDIFF_1:31;
   for x st x in Z holds sin*f is_differentiable_in x
   proof
     let x;
     assume x in Z; then
A6:  f is_differentiable_in x by FDIFF_1:16,A5;
     sin is_differentiable_in f.x by SIN_COS:69;
     hence sin*f is_differentiable_in x by A6,FDIFF_2:13;
   end; then
A7:sin*f is_differentiable_on Z by FDIFF_1:16,A2;
A8:for x st x in Z holds ((sin*f)`|Z).x = n*cos.(n*x)
   proof
     let x;
     assume
A9:  x in Z; then
A10:  f is_differentiable_in x by FDIFF_1:16,A5;
     sin is_differentiable_in f.x by SIN_COS:69;
     then diff(sin*f,x) = diff(sin, f.x)*diff(f,x) by FDIFF_2:13,A10
      .=cos.( f.x)*diff(f,x) by SIN_COS:69
      .=cos.(n*x)*diff(f,x) by A1,A9
      .=cos.(n*x)*(f`|Z).x by FDIFF_1:def 8,A9,A5
      .=n* cos.(n*x) by A9,A4,A3,FDIFF_1:31;
     hence ((sin*f)`|Z).x = n* cos.(n*x) by FDIFF_1:def 8,A7,A9;
   end;
   now let x;
     assume x in Z;
     sin is_differentiable_in x by SIN_COS:69;
     hence ( #Z n)*sin is_differentiable_in x by TAYLOR_1:3;
   end; then
A11:( #Z n)*sin is_differentiable_on Z by A2,FDIFF_1:16;
A12:for x st x in Z holds
     ((( #Z n)*sin)`|Z).x =n*((sin.x) #Z (n-1))*cos.x
   proof
     let x;
     assume
A13: x in Z;
A14: sin is_differentiable_in x by SIN_COS:69;
     diff((( #Z n)*sin),x) =(n*((sin.x) #Z (n-1)) * diff(sin,x))
       by TAYLOR_1:3,A14
      .=n*((sin.x) #Z (n-1)) *cos.x by SIN_COS:69;
     hence ((( #Z n)*sin)`|Z).x =n*((sin.x) #Z (n-1)) *cos.x
       by FDIFF_1:def 8,A13,A11;
   end;
   now let x;
     assume
A15:  x in Z;
A16:  n-1 is Element of NAT by A1,NAT_1:21;
     (((sin*f)(#)(( #Z n)*sin))`|Z).x
       =(( #Z n)*sin).x*diff(sin*f,x)+(sin*f).x*diff((( #Z n)*sin),x)
        by A11,A1,FDIFF_1:29,A7,A15
      .=(( #Z n).(sin.x))*diff(sin*f,x)+(sin*f).x*diff((( #Z n)*sin),x)
        by FUNCT_1:22,A15,A2
      .=((sin.x) #Z n)*diff(sin*f,x)+(sin*f).x*diff((( #Z n)*sin),x)
        by TAYLOR_1:def 1
      .=((sin.x) #Z n)*((sin*f)`|Z).x+(sin*f).x*diff((( #Z n)*sin),x)
        by A7,A15,FDIFF_1:def 8
      .=((sin.x) #Z n)*(n* cos.(n*x))+(sin*f).x*diff((( #Z n)*sin),x) by A8,A15
      .=((sin.x) #Z n)*(n* cos.(n*x))+(sin.(f.x))*diff((( #Z n)*sin),x)
         by A15,FUNCT_1:22, A2
      .=((sin.x) #Z n)*(n* cos.(n*x))+(sin.(n*x))*diff((( #Z n)*sin),x)
         by A1,A15
      .=((sin.x) #Z n)*(n* cos.(n*x))+(sin.(n*x))*((( #Z n)*sin)`|Z).x
         by A11,A15,FDIFF_1:def 8
      .=((sin.x) #Z n)*(n* cos.(n*x))+(sin.(n*x))*(n*((sin.x) #Z (n-1))*cos.x)
         by A15,A12
      .=((sin.x) #Z ((n-1)+1))*(n* cos.(n*x))+
         sin.(n*x)*n*((sin.x) #Z (n-1)) *cos.x
      .=(((sin.x) #Z (n-1))*((sin.x) #Z 1))*(n* cos.(n*x))+
         (sin.(n*x)*n*((sin.x) #Z (n-1)) *cos.x) by TAYLOR_1:1,A16
      .=(((sin.x) #Z (n-1))*(sin.x))*(n* cos.(n*x))+
         (sin.(n*x)*n*((sin.x) #Z (n-1)) *cos.x) by PREPOWER:45
      .= n*((sin.x) #Z (n-1))*(sin.x*cos.(n*x)+cos.x*sin.(n*x))
      .= n*((sin.x) #Z (n-1))*(sin.(x+n*x)) by SIN_COS:79
      .= n*((sin.x) #Z (n-1))*sin.((n+1)*x);
     hence (((sin*f)(#)(( #Z n)*sin))`|Z).x =n*(sin.x) #Z (n-1) *sin.((n+1)*x);
   end;
   hence thesis by A11,A7,A1,FDIFF_1:29;
end;

:: 11 f.x=cos(n*x)*(sin x #Z n)
theorem
  Z c= dom ((cos*f)(#)(( #Z n)*sin)) & n>=1 &
  (for x st x in Z holds f.x=n*x) implies
    (cos*f)(#)(( #Z n)*sin) is_differentiable_on Z &
  for x st x in Z holds
    (((cos*f)(#)(( #Z n)*sin))`|Z).x =n*(sin.x) #Z (n-1) * cos.((n+1)*x)
  proof
    assume
A1: Z c= dom ((cos*f)(#)(( #Z n)*sin)) & n>=1 &
    (for x st x in Z holds f.x=n*x); then
    Z c= dom (cos*f) /\ dom (( #Z n)*sin) by SEQ_1:def 5; then
A2: Z c= dom (cos*f) & Z c= dom (( #Z n)*sin) by XBOOLE_1:18;
    for y st y in Z holds y in dom f by FUNCT_1:21,A2; then
A3: Z c= dom f by TARSKI:def 3;
A4:  for x st x in Z holds f.x=n*x+0 by A1;
A5: f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = n by A4,A3,FDIFF_1:31;
    for x st x in Z holds cos*f is_differentiable_in x
    proof
      let x;
      assume x in Z; then
A6:   f is_differentiable_in x by FDIFF_1:16,A5;
      cos is_differentiable_in f.x by SIN_COS:68;
      hence cos*f is_differentiable_in x by A6,FDIFF_2:13;
    end; then
A7: cos*f is_differentiable_on Z by FDIFF_1:16,A2;
A8: for x st x in Z holds ((cos*f)`|Z).x = -n* sin.(n*x)
    proof
      let x;
      assume
A9:   x in Z; then
A10:   f is_differentiable_in x by FDIFF_1:16,A5;
      cos is_differentiable_in f.x by SIN_COS:68;
      then diff(cos*f,x) = diff(cos, f.x)*diff(f,x) by FDIFF_2:13,A10
      .=(-sin.( f.x))*diff(f,x) by SIN_COS:68
      .=(-sin.(n*x))*diff(f,x) by A1,A9
      .=(-sin.(n*x))*(f`|Z).x by FDIFF_1:def 8,A9,A5
      .=(-sin.(n*x))*n by A9,A4,A3,FDIFF_1:31
      .=-n* sin.(n*x);
      hence ((cos*f)`|Z).x = -n* sin.(n*x) by FDIFF_1:def 8,A7,A9;
    end;
    now let x;
      assume x in Z;
      sin is_differentiable_in x by SIN_COS:69;
      hence ( #Z n)*sin is_differentiable_in x by TAYLOR_1:3;
    end;then
A11: ( #Z n)*sin is_differentiable_on Z by A2,FDIFF_1:16;
A12:for x st x in Z holds
      ((( #Z n)*sin)`|Z).x =n*((sin.x) #Z (n-1)) *cos.x
    proof
      let x;
      assume
A13:  x in Z;
A14:  sin is_differentiable_in x by SIN_COS:69;
      diff((( #Z n)*sin),x) =(n*((sin.x) #Z (n-1)) * diff(sin,x))
        by TAYLOR_1:3,A14
       .=n*((sin.x) #Z (n-1)) *cos.x by SIN_COS:69;
      hence ((( #Z n)*sin)`|Z).x =n*((sin.x) #Z (n-1)) *cos.x
        by FDIFF_1:def 8,A13,A11;
    end;
    now let x;
      assume
A15:   x in Z;
A16:   n-1 is Element of NAT by A1,NAT_1:21;
      (((cos*f)(#)(( #Z n)*sin))`|Z).x
         =(( #Z n)*sin).x*diff(cos*f,x)+(cos*f).x*diff((( #Z n)*sin),x)
        by A11,A1,FDIFF_1:29,A7,A15
        .=(( #Z n).(sin.x))*diff(cos*f,x)+(cos*f).x*diff((( #Z n)*sin),x)
        by FUNCT_1:22,A15,A2
        .=((sin.x) #Z n)*diff(cos*f,x)+(cos*f).x*diff((( #Z n)*sin),x)
        by TAYLOR_1:def 1
        .=((sin.x) #Z n)*((cos*f)`|Z).x+(cos*f).x*diff((( #Z n)*sin),x)
        by A7,A15,FDIFF_1:def 8
        .=((sin.x) #Z n)*(-n* sin.(n*x))+(cos*f).x*diff((( #Z n)*sin),x)
        by A8,A15
        .=((sin.x) #Z n)*(-n* sin.(n*x))+(cos.(f.x))*diff((( #Z n)*sin),x)
        by A15,FUNCT_1:22,A2
        .=((sin.x) #Z n)*(-n* sin.(n*x))+(cos.(n*x))*diff((( #Z n)*sin),x)
        by A1,A15
        .=((sin.x) #Z n)*(-n* sin.(n*x))+(cos.(n*x))*((( #Z n)*sin)`|Z).x
        by A11,A15,FDIFF_1:def 8
        .=((sin.x) #Z n)*(-n* sin.(n*x))+
         (cos.(n*x))*(n*((sin.x) #Z (n-1)) *cos.x) by A15,A12
        .=((sin.x) #Z ((n-1)+1))*(-n* sin.(n*x))+
         cos.(n*x)*n*((sin.x) #Z (n-1)) *cos.x
        .=(((sin.x) #Z (n-1))*((sin.x) #Z 1))*(-n* sin.(n*x))+
         (cos.(n*x)*n*((sin.x) #Z (n-1)) *cos.x) by TAYLOR_1:1,A16
        .=(((sin.x) #Z (n-1))*(sin.x))*(-n* sin.(n*x))+
         (cos.(n*x)*n*((sin.x) #Z (n-1)) *cos.x) by PREPOWER:45
        .= n*((sin.x) #Z (n-1))*(cos.(n*x)*cos.x-sin.x*sin.(n*x))
        .= n*((sin.x) #Z (n-1))*(cos.(x+n*x)) by SIN_COS:79
        .= n*((sin.x) #Z (n-1))*cos.((n+1)*x);
      hence (((cos*f)(#)(( #Z n)*sin))`|Z).x =
        n*(sin.x) #Z (n-1) *cos.((n+1)*x);
    end;
    hence thesis by A11,A7,A1,FDIFF_1:29;
  end;

:: 12 f.x=cos(n*x)*(cos x #Z n)
theorem
  Z c= dom ((cos*f)(#)(( #Z n)*cos)) & n>=1 &
  (for x st x in Z holds f.x=n*x) implies
    (cos*f)(#)(( #Z n)*cos) is_differentiable_on Z &
  for x st x in Z holds
    (((cos*f)(#)(( #Z n)*cos))`|Z).x =-n*(cos.x) #Z (n-1) *sin.((n+1)*x)
  proof
    assume
A1: Z c= dom ((cos*f)(#)(( #Z n)*cos)) & n>=1 &
    (for x st x in Z holds f.x=n*x);then
    Z c= dom (cos*f) /\ dom (( #Z n)*cos) by SEQ_1:def 5;then
A2: Z c= dom (cos*f) & Z c= dom (( #Z n)*cos) by XBOOLE_1:18;
    for y st y in Z holds y in dom f by FUNCT_1:21,A2;then
A3: Z c= dom f by TARSKI:def 3;
A4:  for x st x in Z holds f.x=n*x+0 by A1;
A5: f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = n by A4,A3,FDIFF_1:31;
    for x st x in Z holds cos*f is_differentiable_in x
    proof
      let x;
      assume x in Z; then
A6:   f is_differentiable_in x by FDIFF_1:16,A5;
      cos is_differentiable_in f.x by SIN_COS:68;
      hence cos*f is_differentiable_in x by A6,FDIFF_2:13;
    end;then
A7: cos*f is_differentiable_on Z by FDIFF_1:16,A2;
A8: for x st x in Z holds ((cos*f)`|Z).x = -n* sin.(n*x)
    proof
      let x;
      assume
A9:   x in Z; then
A10:   f is_differentiable_in x by FDIFF_1:16,A5;
      cos is_differentiable_in f.x by SIN_COS:68;
      then diff(cos*f,x) = diff(cos, f.x)*diff(f,x) by FDIFF_2:13,A10
      .=(-sin.( f.x))*diff(f,x) by SIN_COS:68
      .=-sin.( f.x)*diff(f,x)
      .=-sin.(n*x)*diff(f,x) by A1,A9
      .=-sin.(n*x)*(f`|Z).x by FDIFF_1:def 8,A9,A5
      .=-n* sin.(n*x) by A9,A4,A3,FDIFF_1:31;
      hence ((cos*f)`|Z).x = -n* sin.(n*x) by FDIFF_1:def 8,A7,A9;
    end;
    now let x;
      assume x in Z;
      cos is_differentiable_in x by SIN_COS:68;
      hence ( #Z n)*cos is_differentiable_in x by TAYLOR_1:3;
    end;then
A11: ( #Z n)*cos is_differentiable_on Z by A2,FDIFF_1:16;
A12:for x st x in Z holds
      ((( #Z n)*cos)`|Z).x =-n*((cos.x) #Z (n-1)) *sin.x
    proof
      let x;
      assume
A13:  x in Z;
A14:  cos is_differentiable_in x by SIN_COS:68;
      diff((( #Z n)*cos),x) =(n*((cos.x) #Z (n-1)) * diff(cos,x))
        by TAYLOR_1:3,A14
      .=n*((cos.x) #Z (n-1))*(-sin.x) by SIN_COS:68
      .=-n*((cos.x) #Z (n-1))*sin.x;
      hence ((( #Z n)*cos)`|Z).x =-n*((cos.x) #Z (n-1)) *sin.x
        by FDIFF_1:def 8,A13,A11;
    end;
    now let x;
      assume
A15:   x in Z;
A16:   n-1 is Element of NAT by A1,NAT_1:21;
      (((cos*f)(#)(( #Z n)*cos))`|Z).x
        =(( #Z n)*cos).x*diff(cos*f,x)+(cos*f).x*diff((( #Z n)*cos),x)
          by A11,A1,FDIFF_1:29,A7,A15
       .=(( #Z n).(cos.x))*diff(cos*f,x)+(cos*f).x*diff((( #Z n)*cos),x)
          by FUNCT_1:22,A15,A2
       .=((cos.x) #Z n)*diff(cos*f,x)+(cos*f).x*diff((( #Z n)*cos),x)
          by TAYLOR_1:def 1
       .=((cos.x) #Z n)*((cos*f)`|Z).x+(cos*f).x*diff((( #Z n)*cos),x)
          by A7,A15,FDIFF_1:def 8
       .=((cos.x) #Z n)*(-n* sin.(n*x))+(cos*f).x*diff((( #Z n)*cos),x)
          by A8,A15
       .=((cos.x) #Z n)*(-n* sin.(n*x))+(cos.(f.x))*diff((( #Z n)*cos),x)
          by A15,FUNCT_1:22, A2
       .=((cos.x) #Z n)*(-n* sin.(n*x))+(cos.(n*x))*diff((( #Z n)*cos),x)
          by A1,A15
       .=((cos.x) #Z n)*(-n* sin.(n*x))+(cos.(n*x))*((( #Z n)*cos)`|Z).x
          by A11,A15,FDIFF_1:def 8
       .=((cos.x) #Z ((n-1)+1))*(-n* sin.(n*x))+
          (cos.(n*x))*(-n*((cos.x) #Z (n-1)) *sin.x) by A15,A12
       .=(((cos.x) #Z (n-1))*((cos.x) #Z 1))*(-n* sin.(n*x))+
          (cos.(n*x))*(-n*((cos.x) #Z (n-1)) *sin.x) by TAYLOR_1:1,A16
       .=(((cos.x) #Z (n-1))*(cos.x))*(-n* sin.(n*x))+
          (cos.(n*x))*(-n*((cos.x) #Z (n-1)) *sin.x) by PREPOWER:45
       .= -n*((cos.x) #Z (n-1))*(sin.(n*x)*cos.x+cos.(n*x)*sin.x)
       .= -n*((cos.x) #Z (n-1))*(sin.(x+n*x)) by SIN_COS:79
       .= -n*((cos.x) #Z (n-1))*sin.((n+1)*x);
      hence (((cos*f)(#)(( #Z n)*cos))`|Z).x =
        -n*(cos.x) #Z (n-1) *sin.((n+1)*x);
    end;
    hence thesis by A11,A7,A1,FDIFF_1:29;
  end;

:: 13 f.x=sin(n*x)*(cos x |^ n)
theorem
  Z c= dom ((sin*f)(#)(( #Z n)*cos)) & n>=1 &
  (for x st x in Z holds f.x=n*x) implies
  (sin*f)(#)(( #Z n)*cos) is_differentiable_on Z &
  for x st x in Z holds
    (((sin*f)(#)(( #Z n)*cos))`|Z).x =n*(cos.x) #Z (n-1) *cos.((n+1)*x)
  proof
    assume
A1: Z c= dom ((sin*f)(#)(( #Z n)*cos)) & n>=1 &
    (for x st x in Z holds f.x=n*x); then
    Z c= dom (sin*f) /\ dom (( #Z n)*cos) by SEQ_1:def 5; then
A2: Z c= dom (sin*f) & Z c= dom (( #Z n)*cos) by XBOOLE_1:18;
    for y st y in Z holds y in dom f by FUNCT_1:21,A2;then
A3: Z c= dom f by TARSKI:def 3;
A4:  for x st x in Z holds f.x=n*x+0 by A1;
A5: f is_differentiable_on Z &
      for x st x in Z holds (f`|Z).x = n by A4,A3,FDIFF_1:31;
    for x st x in Z holds sin*f is_differentiable_in x
    proof
      let x;
      assume x in Z;then
A6:   f is_differentiable_in x by FDIFF_1:16,A5;
      sin is_differentiable_in f.x by SIN_COS:69;
      hence sin*f is_differentiable_in x by A6,FDIFF_2:13;
    end;then
A7: sin*f is_differentiable_on Z by FDIFF_1:16,A2;
A8: for x st x in Z holds ((sin*f)`|Z).x = n* cos.(n*x)
    proof
      let x;
      assume
A9:   x in Z; then
A10:   f is_differentiable_in x by FDIFF_1:16,A5;
      sin is_differentiable_in f.x by SIN_COS:69;
      then diff(sin*f,x) = diff(sin, f.x)*diff(f,x) by FDIFF_2:13,A10
      .=cos.(f.x)*diff(f,x) by SIN_COS:69
      .=cos.(n*x)*diff(f,x) by A1,A9
      .=cos.(n*x)*(f`|Z).x by FDIFF_1:def 8,A9,A5
      .=n* cos.(n*x) by A9,A4,A3,FDIFF_1:31;
      hence ((sin*f)`|Z).x = n* cos.(n*x) by FDIFF_1:def 8,A7,A9;
    end;
    now let x;
      assume x in Z;
      cos is_differentiable_in x by SIN_COS:68;
      hence ( #Z n)*cos is_differentiable_in x by TAYLOR_1:3;
    end;then
A11: ( #Z n)*cos is_differentiable_on Z by A2,FDIFF_1:16;
A12:for x st x in Z holds
     ((( #Z n)*cos)`|Z).x =-n*((cos.x) #Z (n-1)) *sin.x
    proof
      let x;
      assume
A13:  x in Z;
A14:  cos is_differentiable_in x by SIN_COS:68;
      diff((( #Z n)*cos),x) =(n*((cos.x) #Z (n-1)) * diff(cos,x))
        by TAYLOR_1:3,A14
      .=(n*((cos.x) #Z (n-1))) *(-sin.x) by SIN_COS:68
      .=-n*((cos.x) #Z (n-1)) *sin.x;
      hence ((( #Z n)*cos)`|Z).x =-n*((cos.x) #Z (n-1)) *sin.x
        by FDIFF_1:def 8,A13,A11;
    end;
    now let x;
      assume
A15:   x in Z;
A16:   n-1 is Element of NAT by A1,NAT_1:21;
      (((sin*f)(#)(( #Z n)*cos))`|Z).x
        =(( #Z n)*cos).x*diff(sin*f,x)+(sin*f).x*diff((( #Z n)*cos),x)
       by A11,A1,FDIFF_1:29,A7,A15
       .=(( #Z n).(cos.x))*diff(sin*f,x)+(sin*f).x*diff((( #Z n)*cos),x)
           by FUNCT_1:22,A15,A2
       .=((cos.x) #Z n)*diff(sin*f,x)+(sin*f).x*diff((( #Z n)*cos),x)
           by TAYLOR_1:def 1
       .=((cos.x) #Z n)*((sin*f)`|Z).x+(sin*f).x*diff((( #Z n)*cos),x)
          by A7,A15,FDIFF_1:def 8
       .=((cos.x) #Z n)*(n* cos.(n*x))+(sin*f).x*diff((( #Z n)*cos),x)
          by A8,A15
       .=((cos.x) #Z n)*(n* cos.(n*x))+(sin.(f.x))*diff((( #Z n)*cos),x)
          by A15,FUNCT_1:22,A2
       .=((cos.x) #Z n)*(n* cos.(n*x))+(sin.(n*x))*diff((( #Z n)*cos),x)
          by A1,A15
       .=((cos.x) #Z n)*(n* cos.(n*x))+(sin.(n*x))*((( #Z n)*cos)`|Z).x
          by A11,A15,FDIFF_1:def 8
       .=((cos.x) #Z ((n-1)+1))*(n*cos.(n*x))+
          sin.(n*x)*(-n*((cos.x) #Z (n-1)) *sin.x) by A15,A12
       .=(((cos.x) #Z (n-1))*((cos.x) #Z 1))*(n* cos.(n*x))+
          sin.(n*x)*(-n*((cos.x) #Z (n-1)) *sin.x) by TAYLOR_1:1,A16
       .=(((cos.x) #Z (n-1))*((cos.x) #Z 1))*(n* cos.(n*x))-
          sin.(n*x)*n*((cos.x) #Z (n-1)) *sin.x
       .=(((cos.x) #Z (n-1))*(cos.x))*(n* cos.(n*x))-
          (sin.(n*x)*n*((cos.x) #Z (n-1)) *sin.x) by PREPOWER:45
       .= n*((cos.x) #Z (n-1))*(cos.(n*x)*cos.x-sin.x*sin.(n*x))
       .= n*((cos.x) #Z (n-1))*(cos.(x+n*x)) by SIN_COS:79
       .= n*((cos.x) #Z (n-1))*cos.((n+1)*x);
         hence (((sin*f)(#)(( #Z n)*cos))`|Z).x =
          n*(cos.x) #Z (n-1) *cos.((n+1)*x);
    end;
    hence thesis by A11,A7,A1,FDIFF_1:29;
  end;

:: 14 f.x=(1/x)*sin x
theorem
  Z c= dom ((f^)(#)sin) & (for x st x in Z holds f.x = x & f.x<>0)
  implies (f^)(#)sin is_differentiable_on Z &
    for x st x in Z holds (((f^)(#)sin)`|Z).x =(1/x)*cos.x-(1/x^2)*sin.x
proof
   assume
A1:Z c= dom ((f^)(#)sin) &
   (for x st x in Z holds f.x = x & f.x<>0);then
   Z c= dom (f^) /\ dom (sin) by SEQ_1:def 5;then
A2:Z c= dom (f^) & Z c= dom (sin) by XBOOLE_1:18;
   dom (f^) c= dom f by RFUNCT_1:11;then
A3:Z c= dom f by XBOOLE_1:1,A2;
A4:f^ is_differentiable_on Z & for x st x in Z holds
    ((f^)`|Z).x= -1/x^2 by Th4,A3,A1;
A5:sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
   now let x;
     assume
A6:  x in Z;
     hence (((f^)(#)sin)`|Z).x
       = (sin.x)*diff((f^),x)+((f^).x)*diff(sin,x) by A4,A1,FDIFF_1:29,A5
      .=(sin.x)*((f^)`|Z).x+ ((f^).x)*diff(sin,x) by A4,A6,FDIFF_1:def 8
      .=(sin.x)*(-1/x^2)+ ((f^).x)*diff(sin,x) by A6,Th4,A3,A1
      .=(sin.x)*(-1/x^2)+((f^).x)*(cos.x) by SIN_COS:69
      .=(sin.x)*(-1/x^2)+((f.x)")*(cos.x) by A2,A6,RFUNCT_1:def 8
      .=(sin.x)*(-1/x^2)+(1*x")*(cos.x) by A1,A6
      .=-(1/x^2)*(sin.x)+(1/x)*(cos.x) by XCMPLX_0:def 9
      .=(1/x)*cos.x-(1/x^2)*sin.x;
   end;
   hence thesis by A4,A1,FDIFF_1:29,A5;
end;

:: 15 f.x=(1/x)*cos x
theorem
  Z c= dom ((f^)(#)cos) & (for x st x in Z holds f.x = x & f.x<>0)
  implies (f^)(#)cos is_differentiable_on Z &
  for x st x in Z holds (((f^)(#)cos)`|Z).x =-(1/x)*sin.x-(1/x^2)*cos.x
proof
   assume
A1:Z c= dom ((f^)(#)cos) &
   (for x st x in Z holds f.x = x & f.x<>0);then
   Z c= dom (f^) /\ dom (cos) by SEQ_1:def 5;then
A2:Z c= dom (f^) & Z c= dom (cos) by XBOOLE_1:18;
   dom (f^) c= dom f by RFUNCT_1:11;then
A3:Z c= dom f by XBOOLE_1:1,A2;
A4:f^ is_differentiable_on Z & for x st x in Z holds
   ((f^)`|Z).x= -1/x^2 by Th4,A3,A1;
A5:cos is_differentiable_on Z by SIN_COS:72,FDIFF_1:34;
   now let x;
     assume
A6:  x in Z;
     hence (((f^)(#)cos)`|Z).x
       = (cos.x)*diff((f^),x)+((f^).x)*diff(cos,x) by A4,A1,FDIFF_1:29,A5
      .=(cos.x)*((f^)`|Z).x+ ((f^).x)*diff(cos,x) by A4,A6,FDIFF_1:def 8
      .=(cos.x)*(-1/x^2)+ ((f^).x)*diff(cos,x) by A6,Th4,A3,A1
      .=(cos.x)*(-1/x^2)+((f^).x)*(-sin.x) by SIN_COS:68
      .=(cos.x)*(-1/x^2)+((f.x)")*(-sin.x) by A2,A6,RFUNCT_1:def 8
      .=(cos.x)*(-1/x^2)+(1*x")*(-sin.x) by A6,A1
      .=-(1/x^2)*(cos.x)+(1/x)*(-sin.x) by XCMPLX_0:def 9
      .=-(1/x)*sin.x-(1/x^2)*cos.x;
    end;
    hence thesis by A4,A1,FDIFF_1:29,A5;
  end;

:: 16 f.x=sin.x+x^(1/2)
theorem
  Z c= dom (sin+( #R (1/2))*f) &
  (for x st x in Z holds f.x=x & f.x>0) implies
  sin+( #R (1/2))*f is_differentiable_on Z &
  for x st x in Z holds
    ((sin+( #R (1/2))*f)`|Z).x =cos.x+(1/2)* x #R (-1/2)
proof
    assume that
A1: Z c= dom (sin+( #R (1/2))*f) and
A2: for x st x in Z holds f.x=x & f.x>0;
    Z c= dom (( #R (1/2))*f) /\ dom sin by SEQ_1:def 3,A1;then
A3: Z c= dom (( #R (1/2))*f) & Z c= dom sin by XBOOLE_1:18;
A4: ( #R (1/2))*f is_differentiable_on Z & for x st x in Z holds
      ((( #R (1/2))*f)`|Z).x =(1/2)*x #R (-1/2) by Lm3,A3,A2;
A5: sin is_differentiable_on Z by SIN_COS:73,FDIFF_1:34;
    now let x;
      assume A6:x in Z;
      ((sin+( #R (1/2))*f)`|Z).x
       = diff(sin,x) + diff(( #R (1/2))*f,x) by A4,A1,FDIFF_1:26,A5,A6
      .=cos.x + diff(( #R (1/2))*f,x) by SIN_COS:69
      .=cos.x + ((( #R (1/2))*f)`|Z).x by FDIFF_1:def 8,A4,A6
      .=cos.x +(1/2)*x #R (-1/2) by Lm3,A3,A2,A6;
      hence ((sin+( #R (1/2))*f)`|Z).x =cos.x+(1/2)*x #R (-1/2);
    end;
    hence thesis by A4,A1,FDIFF_1:26,A5;
  end;

:: 17 f.x=x^2*sin(1/x)
theorem
  Z c= dom (g(#)(sin*(f^))) & g=#Z 2 &
  (for x st x in Z holds f.x = x & f.x<>0) implies
  g(#)(sin*(f^)) is_differentiable_on Z &
  for x st x in Z holds ((g(#)(sin*(f^)))`|Z).x = 2*x*sin.(1/x)-cos.(1/x)
proof
   assume
A1:Z c= dom (g(#)(sin*(f^))) & g=#Z 2 &
   (for x st x in Z holds f.x = x & f.x<>0); then
   Z c= dom g /\ dom (sin*(f^)) by SEQ_1:def 5;then
A2: Z c= dom g & Z c= dom (sin*(f^)) by XBOOLE_1:18;
   for y st y in Z holds y in dom (f^) by FUNCT_1:21,A2;then
A3:Z c= dom (f^) by TARSKI:def 3;
A4:sin*(f^) is_differentiable_on Z &
   for x st x in Z holds ((sin*(f^))`|Z).x
     = -(1/x^2)*cos.(1/x) by A1,A2,Th5;
   for x st x in Z holds g is_differentiable_in x by A1,TAYLOR_1:2;then
A5:g is_differentiable_on Z by FDIFF_1:16,A2;
A6:for x st x in Z holds (g`|Z).x = 2*x
   proof
     let x;
     assume
A7:  x in Z;
     diff(g,x) = 2 * (x #Z (2-1)) by A1,TAYLOR_1:2
              .=2*x by PREPOWER:45;
     hence (g`|Z).x =2*x by A5,A7,FDIFF_1:def 8;
   end;
   now let x;
     assume
A8:  x in Z;then
     f.x=x by A1;then
A9:   f.x<>0 & x<>0 by A1,A8;
     ((g(#)(sin*(f^)))`|Z).x
      = ((sin*(f^)).x)*diff(g,x) + (g.x)*diff(sin*(f^),x)
        by A4,A1,FDIFF_1:29,A8,A5
     .=((sin*(f^)).x)*(g`|Z).x+ (g.x)*diff(sin*(f^),x) by A8,A5,FDIFF_1:def 8
     .=((sin*(f^)).x)*(2*x)+(g.x)*diff(sin*(f^),x) by A8,A6
     .=((sin*(f^)).x)*(2*x)+(x #Z 2)*diff(sin*(f^),x) by A1,TAYLOR_1:def 1
     .=((sin*(f^)).x)*(2*x)+(x #Z 2)*(((sin*(f^))`|Z).x )
        by A4,A8,FDIFF_1:def 8
     .=((sin*(f^)).x)*(2*x)+(x #Z 2)*(-(1/x^2)*cos.(1/x)) by A1,A2,A8,Th5
     .=((sin*(f^)).x)*(2*x)-(x #Z (1+1))*((1/x^2)*cos.(1/x))
     .=((sin*(f^)).x)*(2*x) -((x #Z 1)*(x #Z 1))*((1/x^2)*cos.(1/x))
        by TAYLOR_1:1
     .=((sin*(f^)).x)*(2*x) -(x*(x #Z 1))*((1/x^2)*cos.(1/x)) by PREPOWER:45
     .=((sin*(f^)).x)*(2*x) -(x*x)*(((1*1)/(x*x))*cos.(1/x)) by PREPOWER:45
     .=((sin*(f^)).x)*(2*x) -(x*x)*(((1/x)*(1/x))*cos.(1/x)) by XCMPLX_1:103
     .=((sin*(f^)).x)*(2*x) -(x*(1/x))*(x*(1/x))*cos.(1/x)
     .=((sin*(f^)).x)*(2*x) -(x*(1/x))*(1)*cos.(1/x) by A9,XCMPLX_1:107
     .=((sin*(f^)).x)*(2*x) -(1)*(1)*cos.(1/x) by A9,XCMPLX_1:107
     .=(sin.((f^).x))*(2*x) -cos.(1/x) by A2,A8,FUNCT_1:22
     .=(sin.((f.x)"))*(2*x) -cos.(1/x) by A8,RFUNCT_1:def 8,A3
     .=(sin.(1*x"))*(2*x) -cos.(1/x) by A1,A8
     .=2*x*sin.(1/x)-cos.(1/x) by XCMPLX_0:def 9;
     hence ((g(#)(sin*(f^)))`|Z).x = 2*x*sin.(1/x)-cos.(1/x);
   end;
   hence thesis by A1,A4,A5,FDIFF_1:29;
end;

:: 18 f.x=x^2*cos(1/x)
theorem
  Z c= dom (g(#)(cos*(f^))) & g=#Z 2 &
  (for x st x in Z holds f.x = x & f.x<>0) implies
  g(#)(cos*(f^)) is_differentiable_on Z &
  for x st x in Z holds
    ((g(#)(cos*(f^)))`|Z).x = 2*x*cos.(1/x)+sin.(1/x)
proof
   assume
A1:Z c= dom (g(#)(cos*(f^))) & g=#Z 2 &
   (for x st x in Z holds f.x = x & f.x<>0); then
   Z c= dom g /\ dom (cos*(f^)) by SEQ_1:def 5;then
A2: Z c= dom g & Z c= dom (cos*(f^)) by XBOOLE_1:18;
   for y st y in Z holds y in dom (f^) by FUNCT_1:21,A2;then
A3:Z c= dom (f^) by TARSKI:def 3;
A4:cos*(f^) is_differentiable_on Z &
     for x st x in Z holds ((cos*(f^))`|Z).x
      = (1/x^2)*sin.(1/x) by A1,A2,Th6;
   for x st x in Z holds g is_differentiable_in x by A1,TAYLOR_1:2;then
A5:g is_differentiable_on Z by FDIFF_1:16,A2;
A6:for x st x in Z holds (g`|Z).x = 2*x
   proof
     let x;
     assume
A7:  x in Z;
     diff(g,x) = 2 * (x #Z (2-1)) by A1,TAYLOR_1:2
               .=2*x by PREPOWER:45;
     hence (g`|Z).x =2*x by A5,A7,FDIFF_1:def 8;
   end;
   now let x;
     assume
A8:  x in Z;then f.x=x by A1; then
A9:   f.x<>0 & x<>0 by A1,A8;
     ((g(#)(cos*(f^)))`|Z).x
       = ((cos*(f^)).x)*diff(g,x) + (g.x)*diff(cos*(f^),x)
         by A4,A1,FDIFF_1:29,A8,A5
      .=((cos*(f^)).x)*(g`|Z).x+ (g.x)*diff(cos*(f^),x)
         by A8,A5,FDIFF_1:def 8
      .=((cos*(f^)).x)*(2*x)+(g.x)*diff(cos*(f^),x) by A8,A6
      .=((cos*(f^)).x)*(2*x)+(x #Z 2)*diff(cos*(f^),x) by A1,TAYLOR_1:def 1
      .=((cos*(f^)).x)*(2*x)+(x #Z 2)*(((cos*(f^))`|Z).x )
         by A4,A8,FDIFF_1:def 8
      .=((cos*(f^)).x)*(2*x)+(x #Z (1+1))*((1/x^2)*sin.(1/x)) by A1,A2,A8,Th6
      .=((cos*(f^)).x)*(2*x) +((x #Z 1)*(x #Z 1))*((1/x^2)*sin.(1/x))
         by TAYLOR_1:1
      .=((cos*(f^)).x)*(2*x) +(x*(x #Z 1))*((1/x^2)*sin.(1/x)) by PREPOWER:45
      .=((cos*(f^)).x)*(2*x) +(x*x)*(((1*1)/(x*x))*sin.(1/x)) by PREPOWER:45
      .=((cos*(f^)).x)*(2*x) +(x*x)*(((1/x)*(1/x))*sin.(1/x)) by XCMPLX_1:103
      .=((cos*(f^)).x)*(2*x) +(x*(1/x))*(x*(1/x))*sin.(1/x)
      .=((cos*(f^)).x)*(2*x) +(x*(1/x))*(1)*sin.(1/x)by A9,XCMPLX_1:107
      .=((cos*(f^)).x)*(2*x) +(1)*(1)*sin.(1/x) by A9,XCMPLX_1:107
      .=(cos.((f^).x))*(2*x) +sin.(1/x) by A2,A8,FUNCT_1:22
      .=(cos.((f.x)"))*(2*x) +sin.(1/x) by A8,RFUNCT_1:def 8,A3
      .=(cos.(1*x"))*(2*x) +sin.(1/x) by A1,A8
      .=2*x*cos.(1/x)+sin.(1/x) by XCMPLX_0:def 9;
     hence ((g(#)(cos*(f^)))`|Z).x = 2*x*cos.(1/x)+sin.(1/x);
   end;
   hence thesis by A1,A4,A5,FDIFF_1:29;
end;

:: 19 f.x=ln(x)
theorem Th19:
  Z c= dom (ln*f) &
  (for x st x in Z holds f.x = x & f.x > 0) implies
    ln*f is_differentiable_on Z &
    for x st x in Z holds ((ln*f)`|Z).x = 1/x
proof
  assume A1: Z c= dom (ln*f) &
  for x st x in Z holds f.x = x & f.x > 0;
  for y st y in Z holds y in dom f by FUNCT_1:21,A1;
  then A2:Z c= dom f by TARSKI:def 3;
A3:for x st x in Z holds f.x = 1*x+0 by A1;
  then A4:f is_differentiable_on Z &
  for x st x in Z holds (f`|Z).x = 1 by FDIFF_1:31,A2;
A5:for x st x in Z holds ln*f is_differentiable_in x
  proof
    let x;
    assume A6:x in Z;
    then A7:f is_differentiable_in x by FDIFF_1:16,A4;
    f.x >0 by A1,A6;
    hence ln*f is_differentiable_in x by TAYLOR_1:20,A7;
  end;
  then A8:ln*f is_differentiable_on Z by A1,FDIFF_1:16;
  for x st x in Z holds ((ln*f)`|Z).x = 1/x
  proof
    let x;
    assume A9:x in Z;
    then A10:f is_differentiable_in x by FDIFF_1:16,A4;
A11: f.x = x & f.x >0 by A1,A9;
    diff(ln*f,x) =diff(f,x)/( f.x) by TAYLOR_1:20,A11,A10
      .=(f`|Z).x/( f.x) by A4,A9,FDIFF_1:def 8
      .=1/x by FDIFF_1:31,A2,A3,A9,A11;
    hence thesis by A9,A8,FDIFF_1:def 8;
  end;
  hence thesis by A1,FDIFF_1:16,A5;
end;

:: 20 f.x=x*ln(x)
theorem
  Z c= dom ((id Z)(#)f) & f = ln*f1 &
  (for x st x in Z holds f1.x=x & f1.x >0) implies
  (id Z)(#)f is_differentiable_on Z &
  for x st x in Z holds (((id Z)(#)f)`|Z).x = 1+ln.x
proof
   assume
A1:Z c= dom ((id Z)(#)f) & f = ln*f1 &
   (for x st x in Z holds f1.x=x & f1.x >0); then
   Z c= dom id Z /\ dom f by SEQ_1:def 5;then
A2:Z c= dom id Z & Z c= dom f by XBOOLE_1:18;
A3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;then
A4:id Z is_differentiable_on Z & for x st x in Z holds
     ((id Z)`|Z).x = 1 by FDIFF_1:31,A2;
A5:f is_differentiable_on Z & for x st x in Z holds
     (f`|Z).x = 1/x by A1,A2,Th19;
   for x st x in Z holds (((id Z)(#)f)`|Z).x =1+ln.x
   proof
     let x;
     assume
A6:  x in Z;then f1.x=x by A1;then
A7:  x<>0 by A1,A6;
     (((id Z)(#)f)`|Z).x =((id Z).x)*diff(f,x)+(f.x)*diff((id Z),x)
       by A5,A1,A4,FDIFF_1:29,A6
      .=((id Z).x)*((f`|Z).x)+(f.x)*diff((id Z),x) by A5,A6,FDIFF_1:def 8
      .=((id Z).x)*(1/x) + (f.x)*diff((id Z),x) by A6,A1,A2,Th19
      .=x*(1/x) + (f.x)*diff((id Z),x) by FUNCT_1:35,A6
      .=x*(1/x) + (f.x)*(((id Z)`|Z).x) by A4,A6,FDIFF_1:def 8
      .=x*(1/x) + (f.x)*1 by A3,A6,FDIFF_1:31,A2
      .=x*(1/x) + ln.(f1.x) by A1,A6,FUNCT_1:22,A2
      .=x*(1/x) + ln.x by A1,A6
      .= 1 + ln.x by A7,XCMPLX_1:107;
     hence thesis;
   end;
   hence thesis by A5,A1,A4,FDIFF_1:29;
end;

:: 21 f.x=x^2*ln(x)
theorem
  Z c= dom (g(#)f) & g=#Z 2 & f = ln*f1 &
  (for x st x in Z holds f1.x=x & f1.x>0) implies
  g(#)f is_differentiable_on Z &
  for x st x in Z holds ((g(#)f)`|Z).x = x+2*x*ln.x
proof
   assume
A1:Z c= dom (g(#)f) & g=#Z 2 & f = ln*f1 &
   (for x st x in Z holds f1.x=x & f1.x>0);
   then Z c= dom (g) /\ dom (f) by SEQ_1:def 5; then
A2:Z c= dom g & Z c= dom (f) by XBOOLE_1:18;
   for x st x in Z holds g is_differentiable_in x by A1,TAYLOR_1:2; then
A3:g is_differentiable_on Z by FDIFF_1:16,A2;
A4:for x st x in Z holds (g`|Z).x = 2*x
   proof
     let x;
     assume
A5:  x in Z;
     diff(g,x) = 2 * (x #Z (2-1)) by A1,TAYLOR_1:2
              .=2*x by PREPOWER:45;
     hence (g`|Z).x =2*x by A5,A3,FDIFF_1:def 8;
   end;
A6:f is_differentiable_on Z & for x st x in Z holds
     (f`|Z).x = 1/x by A1,A2,Th19;
   now let x;
     assume
A7:  x in Z; then f1.x=x by A1; then
A8:  x<>0 by A1,A7;
     ((g(#)f)`|Z).x =(g.x)*diff(f,x) + (f.x)*diff(g,x)
       by A3,A1,A6,A7,FDIFF_1:29
     .=(g.x)*((f`|Z).x)+(f.x)*diff(g,x) by A6,A7,FDIFF_1:def 8
     .=(g.x)*(1/x) + (f.x)*diff(g,x) by A7,A1,A2,Th19
     .=(x #Z 2)*(1/x) + (f.x)*diff(g,x) by A1,TAYLOR_1:def 1
     .=(x #Z 2)*(1/x) + (f.x)*((g`|Z).x) by A3,A7,FDIFF_1:def 8
     .=(x #Z 2)*(1/x) + ((ln*f1).x)*(2*x) by A1,A7,A4
     .=(x #Z 2)*(1/x) + (ln.(f1.x))*(2*x) by A1,A2,A7,FUNCT_1:22
     .=(x #Z (1+1))*(1/x) + 2*x*(ln.x) by A1,A7
     .=((x #Z 1)*(x #Z 1))*(1/x) + 2*x*(ln.x) by TAYLOR_1:1
     .=((x #Z 1)*x)*(1/x) + 2*x*(ln.x) by PREPOWER:45
     .=(x*x)*(1/x) + 2*x*(ln.x) by PREPOWER:45
     .=x*(x*(1/x)) + 2*x*(ln.x)
     .=x*1 + 2*x*(ln.x) by XCMPLX_1:107,A8
     .=x+2*x*(ln.x);
     hence ((g(#)f)`|Z).x = x+2*x*ln.x;
   end;
   hence thesis by A3,A1,A6,FDIFF_1:29;
end;

:: 22 f.x=(a+x^2)/(a-x^2)
theorem Th22:
  Z c= dom ((f1+f2)/(f1-f2)) &
  (for x st x in Z holds f1.x=a) & f2=#Z 2 &
  (for x st x in Z holds (f1-f2).x>0) implies
  (f1+f2)/(f1-f2) is_differentiable_on Z &
  for x st x in Z holds (((f1+f2)/(f1-f2))`|Z).x = (4*a*x)/(a-x|^2)|^2
  proof
    assume
A1: Z c= dom ((f1+f2)/(f1-f2)) &
    (for x st x in Z holds f1.x=a) & f2=#Z 2 &
    (for x st x in Z holds (f1-f2).x>0);
    Z c= dom (f1+f2) /\ (dom (f1-f2)\(f1-f2)"{0}) by A1,RFUNCT_1:def 4; then
A2: Z c= dom (f1+f2) & Z c= dom (f1-f2) \ (f1-f2)"{0} by XBOOLE_1:18;
A3: Z c= dom (f1-f2) by XBOOLE_1:1,A2;
A4: f1+f2 is_differentiable_on Z &
    for x st x in Z holds ((f1+f2)`|Z).x = 2*x by A2,A1,Lm1;
A5: f1-f2 is_differentiable_on Z &
    for x st x in Z holds ((f1-f2)`|Z).x = -2*x by A3,A1,Lm2;
A6:  for x st x in Z holds (f1-f2).x <> 0 by A1;then
A7:(f1+f2)/(f1-f2) is_differentiable_on Z by A4,A5,FDIFF_2:21;
    for x st x in Z holds (((f1+f2)/(f1-f2))`|Z).x = (4*a*x)/(a-x|^2)|^2
    proof
      let x;
      assume
A8:   x in Z;then
A9:  f1+f2 is_differentiable_in x by A4,FDIFF_1:16;
A10:  f1-f2 is_differentiable_in x by A5,A8,FDIFF_1:16;
A11:  f1.x=a & f2=#Z 2 & (f1-f2).x <>0 by A1,A8;
      then diff((f1+f2)/(f1-f2),x)
        =(diff((f1+f2),x)*(f1-f2).x -
          diff((f1-f2),x)*(f1+f2).x)/((f1-f2).x)^2 by FDIFF_2:14,A10,A9
       .=(diff((f1+f2),x)*(f1.x-f2.x)-
          diff((f1-f2),x)*(f1+f2).x)/((f1-f2).x)^2 by A8,SEQ_1:def 4,A3
       .=(diff((f1+f2),x)*(f1.x-f2.x)- diff((f1-f2),x)*
         (f1.x+f2.x))/((f1-f2).x)^2 by A8,SEQ_1:def 3,A2
       .=(diff((f1+f2),x)*(f1.x-f2.x)- diff((f1-f2),x)*
         (f1.x+f2.x))/(f1.x-f2.x)^2 by A8,SEQ_1:def 4,A3
       .=((((f1+f2)`|Z).x)*(f1.x-f2.x)- diff((f1-f2),x)*
         (f1.x+f2.x))/(f1.x-f2.x)^2 by A8,A4,FDIFF_1:def 8
       .=((2*x)*(f1.x-f2.x)-diff((f1-f2),x)*(f1.x+f2.x))/(f1.x-f2.x)^2
         by A8,A2,A1,Lm1
       .=((2*x)*(f1.x-f2.x)-(((f1-f2)`|Z).x)*(f1.x+f2.x))/(f1.x-f2.x)^2
         by A8,A5,FDIFF_1:def 8
       .=((2*x)*(f1.x-f2.x)-(-2*x)*(f1.x+f2.x))/(f1.x-f2.x)^2 by A8,A3,A1,Lm2
       .=(4*x*a)/(a-(x #Z 2))^2 by A11,TAYLOR_1:def 1
       .=(4*x*a)/(a-x|^2)^2 by PREPOWER:46
       .=(4*x*a)/((a-x|^2)|^1*(a- x|^2)) by NEWTON:10
       .=(4*x*a)/(a-x|^2)|^(1+1)by NEWTON:11
       .= (4*a*x)/(a-x|^2)|^2;
      hence (((f1+f2)/(f1-f2))`|Z).x = (4*a*x)/(a-x|^2)|^2
        by FDIFF_1:def 8,A8,A7;
    end;
    hence thesis by A6,A4,A5,FDIFF_2:21;
  end;

:: 23 f.x=ln((a+x^2)/(a-x^2))
theorem
  Z c= dom (ln*((f1+f2)/(f1-f2))) &
    (for x st x in Z holds f1.x=a) & f2=#Z 2 &
    (for x st x in Z holds (f1-f2).x>0) &
    (for x st x in Z holds (f1+f2).x>0) implies
  ln*((f1+f2)/(f1-f2)) is_differentiable_on Z &
  for x st x in Z holds
    ((ln*((f1+f2)/(f1-f2)))`|Z).x = (4*a*x)/(a|^2-x|^4)
  proof
    assume
A1: Z c= dom (ln*((f1+f2)/(f1-f2))) &
    (for x st x in Z holds f1.x=a) & f2=#Z 2 &
    (for x st x in Z holds (f1-f2).x>0) &
    (for x st x in Z holds (f1+f2).x >0);
    for y st y in Z holds y in dom ((f1+f2)/(f1-f2)) by FUNCT_1:21,A1; then
A2: Z c= dom ((f1+f2)/(f1-f2)) by TARSKI:def 3;
    then Z c= dom (f1+f2) /\ (dom (f1-f2) \ (f1-f2)"{0}) by RFUNCT_1:def 4;
    then
A3: Z c= dom (f1+f2) & Z c= dom (f1-f2) \ (f1-f2)"{0} by XBOOLE_1:18;
A4: Z c= dom (f1-f2) by XBOOLE_1:1,A3;
A5: (f1+f2)/(f1-f2) is_differentiable_on Z &
    for x st x in Z holds (((f1+f2)/(f1-f2))`|Z).x = (4*a*x)/(a-x|^2)|^2
      by Th22,A1,A2;
A6: for x st x in Z holds ((f1+f2)/(f1-f2)).x >0
    proof
      let x;
      assume
A7:   x in Z;then
A8:   x in dom ((f1+f2)/(f1-f2)) by FUNCT_1:21,A1;
A9:   (f1+f2).x >0 & (f1-f2).x >0 by A1,A7;
      ((f1+f2)/(f1-f2)).x=(f1+f2).x * ((f1-f2).x)" by A8,RFUNCT_1:def 4
      .=(f1+f2).x / (f1-f2).x by XCMPLX_0:def 9;
      hence thesis by XREAL_1:141,A9;
    end;
A10: for x st x in Z holds
      ln*((f1+f2)/(f1-f2)) is_differentiable_in x
    proof
      let x;
      assume
A11:  x in Z;then
A12:  (f1+f2)/(f1-f2) is_differentiable_in x by FDIFF_1:16,A5;
      ((f1+f2)/(f1-f2)).x >0 by A11,A6;
      hence ln*((f1+f2)/(f1-f2))
        is_differentiable_in x by TAYLOR_1:20,A12;
    end;then
A13:  ln*((f1+f2)/(f1-f2)) is_differentiable_on Z
      by A1,FDIFF_1:16;
    for x st x in Z holds
      ((ln*((f1+f2)/(f1-f2)))`|Z).x = (4*a*x)/(a|^2-x|^4)
    proof
      let x;
      assume
A14:  x in Z;then
A15:  ((f1+f2)/(f1-f2)) is_differentiable_in x by FDIFF_1:16,A5;
A16:  x in dom ((f1+f2)/(f1-f2)) by FUNCT_1:21,A1,A14;
A17:  f1.x=a & f2=#Z 2 & ((f1+f2)/(f1-f2)).x > 0 by A1,A14,A6;
A18:  (f1-f2).x <>0 & (f1+f2).x <>0 by A1,A14;
      diff(ln*((f1+f2)/(f1-f2)),x) =
        diff(((f1+f2)/(f1-f2)),x)/(((f1+f2)/(f1-f2)).x) by TAYLOR_1:20,A15,A17
    .=(((f1+f2)/(f1-f2))`|Z).x/(((f1+f2)/(f1-f2)).x) by A5,A14,FDIFF_1:def 8
    .=(((f1+f2)/(f1-f2))`|Z).x/((f1+f2).x *((f1-f2).x)") by A16,RFUNCT_1:def 4
    .= ((4*a*x)/(f1.x-x|^2)|^2)/(((f1+f2).x) *((f1-f2).x)")
        by A17,Th22,A1,A2,A14
    .= ((4*a*x)/(f1.x-(x #Z 2))|^2)/(((f1+f2).x) *((f1-f2).x)") by PREPOWER:46
    .= ((4*a*x)/(f1.x-f2.x)|^2)/(((f1+f2).x) *((f1-f2).x)")
        by A1,TAYLOR_1:def 1
    .= ((4*a*x)/((f1-f2).x)|^2)/(((f1+f2).x) *((f1-f2).x)")
        by A4,A14,SEQ_1:def 4
    .= ((4*a*x)/(((f1-f2).x)|^(1+1)))/((f1+f2).x/(f1-f2).x) by XCMPLX_0:def 9
    .= ((4*a*x)/(((f1-f2).x)|^1*(f1-f2).x))/((f1+f2).x/(f1-f2).x) by NEWTON:11
    .= ((4*a*x)/((f1-f2).x*(f1-f2).x))/((f1+f2).x/(f1-f2).x) by NEWTON:10
    .= ((4*a*x)/(f1-f2).x/(f1-f2).x)/((f1+f2).x/(f1-f2).x) by XCMPLX_1:79
    .= ((4*a*x)/(f1-f2).x)/((f1+f2).x/(f1-f2).x)/(f1-f2).x by XCMPLX_1:48
    .= (4*a*x)/(f1+f2).x/(f1-f2).x by A18,XCMPLX_1:55
    .= (4*a*x)/((f1+f2).x*(f1-f2).x) by XCMPLX_1:79
    .= (4*a*x)/((f1.x+f2.x)*(f1-f2).x) by A14,SEQ_1:def 3,A3
    .= (4*a*x)/((f1.x+f2.x)*(f1.x-f2.x)) by A4,A14,SEQ_1:def 4
    .= (4*a*x)/(a*a-f2.x*f2.x) by A17
    .= (4*a*x)/(a*a-(x #Z 2)*f2.x) by A1,TAYLOR_1:def 1
    .= (4*a*x)/(a*a-(x #Z 2)*(x #Z 2)) by A1,TAYLOR_1:def 1
    .= (4*a*x)/(a|^1*a- (x #Z 2)*(x #Z 2)) by NEWTON:10
    .= (4*a*x)/(a|^(1+1)- (x #Z 2)*(x #Z 2)) by NEWTON:11
    .= (4*a*x)/(a|^(1+1)- (x|^2)*(x #Z 2)) by PREPOWER:46
    .= (4*a*x)/(a|^2- (x|^2)*(x|^2)) by PREPOWER:46
    .= (4*a*x)/(a|^2- x|^(2+2)) by NEWTON:13
    .= (4*a*x)/(a|^2- x|^4);
       hence thesis by A14,A13,FDIFF_1:def 8;
     end;
     hence thesis by A1,FDIFF_1:16,A10;
   end;

:: 24 f.x=(1/x)*ln(x)
theorem
  Z c= dom ((f^)(#)g) & (for x st x in Z holds f.x = x) &
    g = ln*f1 & (for x st x in Z holds f1.x=x & f1.x >0)
      implies (f^)(#)g is_differentiable_on Z &
  for x st x in Z holds (((f^)(#)g)`|Z).x =(1/x^2)*(1-ln.x)
proof
    assume
A1: Z c= dom ((f^)(#)g) & (for x st x in Z holds f.x = x) &
      g = ln*f1 & (for x st x in Z holds f1.x=x & f1.x >0); then
    Z c= dom (f^) /\ dom g by SEQ_1:def 5;then
A2: Z c= dom (f^) & Z c= dom g by XBOOLE_1:18;
    dom (f^) c= dom f by RFUNCT_1:11;then
A3: Z c= dom f by XBOOLE_1:1,A2;
A4:  for x st x in Z holds f.x = x & f.x<>0
    proof
      let x; assume A5:x in Z; then
A6:   f.x = x by A1;
      f1.x = x by A5,A1;
      hence thesis by A6,A5,A1;
    end;
A7: f^ is_differentiable_on Z & for x st x in Z holds
    ((f^)`|Z).x= -1/x^2 by Th4,A3,A4;
A8: g is_differentiable_on Z & for x st x in Z holds
     (g`|Z).x = 1/x by A1,A2,Th19;
    now let x;
      assume
A9:   x in Z;
      (((f^)(#)g)`|Z).x
      = (g.x)*diff((f^),x)+((f^).x)*diff(g,x) by A7,A1,FDIFF_1:29,A8,A9
     .=(g.x)*((f^)`|Z).x+ ((f^).x)*diff(g,x) by A7,A9,FDIFF_1:def 8
     .=(g.x)*(-1/x^2)+ ((f^).x)*diff(g,x) by A9,Th4,A3,A4
     .=(g.x)*(-1/x^2)+ ((f^).x)*((g`|Z).x) by A9,FDIFF_1:def 8,A8
     .=(g.x)*(-1/x^2)+((f^).x)*(1/x) by A9,A1,A2,Th19
     .=(g.x)*(-1/x^2)+((f.x)")*(1/x) by A2,A9,RFUNCT_1:def 8
     .=(g.x)*(-1/x^2)+(1*x")*(1/x) by A9,A1
     .=-(1/x^2)*(g.x)+(1/x)*(1/x) by XCMPLX_0:def 9
     .=-(1/x^2)*((ln*f1).x)+1/(x^2) by  XCMPLX_1:103,A1
     .=-(1/x^2)*(ln.(f1.x))+1/(x^2) by A1,A2,A9,FUNCT_1:22
     .=-(1/x^2)*ln.x+1/(x^2) by A1,A9
     .=(1/x^2)*(1-ln.x);
      hence (((f^)(#)g)`|Z).x =(1/x^2)*(1-ln.x);
    end;
    hence thesis by A7,A1,FDIFF_1:29,A8;
  end;

:: 25 f.x=1/ln(x)
theorem
  Z c= dom (f^) & f = ln*f1 &
  (for x st x in Z holds f1.x=x & f1.x >0) &
  (for x st x in Z holds f.x<>0) implies
  (f^) is_differentiable_on Z &
  for x st x in Z holds ((f^)`|Z).x =-1/(x*(ln.x)^2)
proof
    assume
A1: Z c= dom (f^) & f = ln*f1 &
    (for x st x in Z holds f1.x=x & f1.x >0) &
    (for x st x in Z holds f.x<>0);
    dom (f^) c= dom f by RFUNCT_1:11;then
A2: Z c= dom f by XBOOLE_1:1,A1;
A3: f is_differentiable_on Z & for x st x in Z holds
      (f`|Z).x = 1/x by A1,A2,Th19;
A4: f^ is_differentiable_on Z by A3,FDIFF_2:22,A1;
    for x st x in Z holds ((f^)`|Z).x= - 1/(x*(ln.x)^2)
    proof
      let x;
      assume
A5:   x in Z;
A6:   f1.x= x & f1.x>0 & f.x<>0 by A1,A5;
A7:   f is_differentiable_in x by A5,A3,FDIFF_1:16;
      ((f^)`|Z).x= diff(f^,x) by A4,A5,FDIFF_1:def 8
      .= -diff(f,x)/(f.x)^2 by A6,FDIFF_2:15,A7
      .= -((f`|Z).x)/(f.x)^2 by A3,FDIFF_1:def 8,A5
      .= -(1/x)/((ln*f1).x)^2 by A1,A2,Th19,A5
      .= -((1/x)/(ln.x)^2) by A5,A6,A1,A2,FUNCT_1:22
      .= -1/(x*(ln.x)^2) by XCMPLX_1:79;
      hence thesis;
    end;
    hence thesis by A3,FDIFF_2:22,A1;
  end;
