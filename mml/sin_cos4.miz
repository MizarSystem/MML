:: Formulas And Identities of Trigonometric Functions
::  by Pacharapokin Chanapat, Kanchun and Hiroshi Yamazaki
::
:: Received February 3, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary ARYTM_3, ARYTM_1, SIN_COS, SIN_COS4, ARYTM;
 notations ORDINAL1, XCMPLX_0, XREAL_0, REAL_1, SIN_COS;
 constructors REAL_1, SQUARE_1, PREPOWER, SIN_COS;
 registrations INT_1, XCMPLX_0, SIN_COS, XREAL_0;
 requirements NUMERALS, SUBSET, ARITHM;
 theorems XCMPLX_1, SIN_COS, XREAL_0, COMPLEX2;

begin

reserve th, th1, th2, th3 for real number;

definition let th;
  func tan(th) -> Real equals :Def1: sin(th)/cos(th);
  correctness by XREAL_0:def 1;
end;

definition let th;
  func cot(th) -> Real equals :Def2: cos(th)/sin(th);
  correctness by XREAL_0:def 1;
end;

definition let th;
  func cosec(th) -> Real equals :Def3: 1/sin(th);
  correctness by XREAL_0:def 1;
end;

definition let th;
  func sec(th) -> Real equals :Def4: 1/cos(th);
  correctness by XREAL_0:def 1;
end;

theorem
   tan(th) = 1/cot(th)
proof
   tan(th) = sin(th)/cos(th) by Def1
  .=1/(cos(th)/sin(th)) by XCMPLX_1:57
  .=1/cot(th) by Def2;
  hence thesis;
end;

theorem
   tan(-th) = -tan(th)
proof
   tan(-th) = sin(-th)/cos(-th) by Def1
  .= sin(-th)/cos(th) by SIN_COS:34
  .= (-sin(th))/cos(th) by SIN_COS:34
  .= -sin(th)/cos(th) by XCMPLX_1:188
  .= -tan(th) by Def1;
  hence thesis;
end;

theorem
   cosec(-th) = -1/sin(th)
proof
   cosec(-th) = 1/sin(-th) by Def3
  .= 1/-sin(th) by SIN_COS:34
  .= -1/sin(th) by XCMPLX_1:189;
  hence thesis;
end;

theorem
   cot(-th) = -cot(th)
proof
   cot(-th) = cos(-th)/sin(-th) by Def2
  .=cos(th)/sin(-th) by SIN_COS:34
  .=cos(th)/(-sin(th)) by SIN_COS:34
  .=-cos(th)/sin(th) by XCMPLX_1:189
  .=-cot(th) by Def2;
  hence thesis;
end;

theorem
   cos(th1) <> 0 implies cos(th1)*sec(th1) = 1
proof
  assume
  A1: cos(th1) <> 0;
   cos(th1)*sec(th1) = cos(th1)*(1/cos(th1)) by Def4
  .= 1 by A1,XCMPLX_1:107;
  hence thesis;
end;

theorem Th6:
  sin(th)*sin(th) = 1-cos(th)*cos(th)
proof
   sin(th)*sin(th) = sin(th)*sin(th)+(1-1)
  .= sin(th)*sin(th)+(1---(sin(th)*sin(th)+cos(th)*cos(th)))
  by SIN_COS:32
  .= 1-cos(th)*cos(th);
  hence thesis;
end;

theorem Th7:
  cos(th)*cos(th) = 1-sin(th)*sin(th)
proof
   cos(th)*cos(th) = cos(th)*cos(th)+(1-1)
  .= cos(th)*cos(th)+(1---(sin(th)*sin(th)+cos(th)*cos(th)))
  by SIN_COS:32
  .= 1-sin(th)*sin(th);
  hence thesis;
end;

theorem Th8:
  cos(th)<>0 implies sin(th) = cos(th)*tan(th)
proof
  assume cos(th)<>0;
  then sin(th)= (cos(th)/cos(th))*sin(th) by XCMPLX_1:89
  .= cos(th)*(sin(th)/cos(th)) by XCMPLX_1:76
  .= cos(th)*tan(th) by Def1;
  hence thesis;
end;

theorem Th9:
  sin(th1-th2) = sin(th1)*cos(th2)-cos(th1)*sin(th2) by COMPLEX2:4;

theorem Th10:
  cos(th1-th2) = cos(th1)*cos(th2)+sin(th1)*sin(th2) by COMPLEX2:4;

theorem
   cos(th1)<>0 & cos(th2)<>0 implies
  tan(th1+th2)=(tan(th1)+tan(th2))/(1-tan(th1)*tan(th2))
proof
  assume A1: cos(th1)<>0 & cos(th2)<>0;
  then A2: cos(th1)*cos(th2)<>0 by XCMPLX_1:6;
   tan(th1+th2)=(sin(th1+th2))/(cos(th1+th2)) by Def1
  .=(sin(th1+th2)/(cos(th1)*cos(th2)))
  /(cos(th1+th2)/(cos(th1)*cos(th2))) by A2,XCMPLX_1:55
  .= ((sin(th1)*cos(th2)+cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /((cos(th1+th2))/(cos(th1)*cos(th2))) by SIN_COS:80
  .= ((sin(th1)*cos(th2)+cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /((cos(th1)*cos(th2)-sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by SIN_COS:80
  .= (sin(th1)*cos(th2)/(cos(th1)*cos(th2))
  +cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /((cos(th1)*cos(th2)-sin(th1)*sin(th2))/(cos(th1)*cos(th2)))
  by XCMPLX_1:63
  .= (sin(th1)*cos(th2)/(cos(th1)*cos(th2))
  +cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /(cos(th1)*cos(th2)/(cos(th1)*cos(th2))
  -(sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by XCMPLX_1:121
  .= (sin(th1)/cos(th1)*(cos(th2)/cos(th2))
  +cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /(cos(th1)*cos(th2)/(cos(th1)*cos(th2))
  -(sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by XCMPLX_1:77
  .= (sin(th1)/cos(th1)*(cos(th2)/cos(th2))
  +sin(th2)/cos(th2)*(cos(th1)/cos(th1)))
  /(cos(th1)*cos(th2)/(cos(th1)*cos(th2))
  -(sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by XCMPLX_1:77
  .= (sin(th1)/cos(th1)*(cos(th2)/cos(th2))
  +sin(th2)/cos(th2)*(cos(th1)/cos(th1)))
  /((cos(th1)/cos(th1))*(cos(th2)/cos(th2))
  -(sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by XCMPLX_1:77
  .= (sin(th1)/cos(th1)*(cos(th2)/cos(th2))
  +sin(th2)/cos(th2)*(cos(th1)/cos(th1)))
  /((cos(th1)/cos(th1))*(cos(th2)/cos(th2))
  -(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by XCMPLX_1:77
  .= (sin(th1)/cos(th1)+sin(th2)/cos(th2)*(cos(th1)/cos(th1)))
  /((cos(th1)/cos(th1))*(cos(th2)/cos(th2))
  -(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)+sin(th2)/cos(th2))
  /((cos(th1)/cos(th1))*(cos(th2)/cos(th2))
  -(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)+sin(th2)/cos(th2))
  /((cos(th1)/cos(th1))-(sin(th1)/cos(th1))*(sin(th2)/cos(th2)))
  by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)+sin(th2)/cos(th2))
  /(1-(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by A1,XCMPLX_1:60
  .= (tan(th1)+sin(th2)/cos(th2))
  /(1-(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by Def1
  .= (tan(th1)+tan(th2))/(1-(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by Def1
  .= (tan(th1)+tan(th2))/(1-tan(th1)*(sin(th2)/cos(th2))) by Def1
  .= (tan(th1)+tan(th2))/(1-tan(th1)*tan(th2)) by Def1;
  hence thesis;
end;

theorem
   cos(th1)<>0 & cos(th2)<>0 implies
  tan(th1-th2)=(tan(th1)-tan(th2))/(1+tan(th1)*tan(th2))
proof
  assume
  A1: cos(th1)<>0 & cos(th2)<>0;
  then A2: cos(th1)*cos(th2)<>0 by XCMPLX_1:6;
   tan(th1-th2)= sin(th1+(-th2))/cos(th1-th2) by Def1
  .= (sin(th1+(-th2))/(cos(th1)*cos(th2)))
  /(cos(th1+(-th2))/(cos(th1)*cos(th2))) by A2,XCMPLX_1:55
  .= ((sin(th1)*cos(-th2)+cos(th1)*sin(-th2))/(cos(th1)*cos(th2)))
  /((cos(th1+(-th2)))/(cos(th1)*cos(th2))) by SIN_COS:80
  .= ((sin(th1)*cos(th2)+cos(th1)*sin(-th2))/(cos(th1)*cos(th2)))
  /((cos(th1+(-th2)))/(cos(th1)*cos(th2))) by SIN_COS:34
  .= ((sin(th1)*cos(th2)+cos(th1)*(-sin(th2)))/(cos(th1)*cos(th2)))
  /((cos(th1+(-th2)))/(cos(th1)*cos(th2))) by SIN_COS:34
  .= ((sin(th1)*cos(th2)-cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /((cos(th1)*cos(-th2)-sin(th1)*sin(-th2))/(cos(th1)*cos(th2))) by SIN_COS:80
  .= ((sin(th1)*cos(th2)-cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /((cos(th1)*cos(th2)-sin(th1)*sin(-th2))/(cos(th1)*cos(th2))) by SIN_COS:34
  .= ((sin(th1)*cos(th2)-cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /((cos(th1)*cos(th2)-sin(th1)*(-sin(th2)))/(cos(th1)*cos(th2))) by SIN_COS:34
  .= (sin(th1)*cos(th2)/(cos(th1)*cos(th2))
  - cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /((cos(th1)*cos(th2)+sin(th1)*sin(th2))/(cos(th1)*cos(th2)))
  by XCMPLX_1:121
  .= (sin(th1)*cos(th2)/(cos(th1)*cos(th2))
  -cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /(cos(th1)*cos(th2)/(cos(th1)*cos(th2))
  +sin(th1)*sin(th2)/(cos(th1)*cos(th2))) by XCMPLX_1:63
  .= (sin(th1)/cos(th1)*(cos(th2)/cos(th2))
  -cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /(cos(th1)*cos(th2)/(cos(th1)*cos(th2))
  +(sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by XCMPLX_1:77
  .= (sin(th1)/cos(th1)*(cos(th2)/cos(th2))
  -sin(th2)/cos(th2)*(cos(th1)/cos(th1)))
  /(cos(th1)*cos(th2)/(cos(th1)*cos(th2))
  +(sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by XCMPLX_1:77
  .= (sin(th1)/cos(th1)*(cos(th2)/cos(th2))
  -sin(th2)/cos(th2)*(cos(th1)/cos(th1)))
  /((cos(th1)/cos(th1))*(cos(th2)/cos(th2))
  +(sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by XCMPLX_1:77
  .= (sin(th1)/cos(th1)*(cos(th2)/cos(th2))
  -sin(th2)/cos(th2)*(cos(th1)/cos(th1)))
  /((cos(th1)/cos(th1))*(cos(th2)/cos(th2))
  +(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by XCMPLX_1:77
  .= (sin(th1)/cos(th1)-sin(th2)/cos(th2)*(cos(th1)/cos(th1)))
  /((cos(th1)/cos(th1))*(cos(th2)/cos(th2))
  +(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)-sin(th2)/cos(th2))
  /((cos(th1)/cos(th1))*(cos(th2)/cos(th2))
  +(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)-sin(th2)/cos(th2))
  /((cos(th1)/cos(th1))+(sin(th1)/cos(th1))*(sin(th2)/cos(th2)))
  by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)-sin(th2)/cos(th2))
  /(1+(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by A1,XCMPLX_1:60
  .= (tan(th1)-sin(th2)/cos(th2))
  /(1+(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by Def1
  .= (tan(th1)-tan(th2))
  /(1+(sin(th1)/cos(th1))*(sin(th2)/cos(th2))) by Def1
  .= (tan(th1)-tan(th2)) / (1+tan(th1)*(sin(th2)/cos(th2))) by Def1
  .= (tan(th1)-tan(th2))/(1+tan(th1)*tan(th2)) by Def1;
  hence thesis;
end;

theorem
   sin(th1)<>0 & sin(th2)<>0 implies
  cot(th1+th2) = (cot(th1)*cot(th2)-1)/(cot(th2)+cot(th1))
proof
  assume A1: sin(th1)<>0 & sin(th2)<>0;
  then A2: sin(th1)*sin(th2)<>0 by XCMPLX_1:6;
   cot(th1+th2)=(cos(th1+th2))/(sin(th1+th2)) by Def2
  .=(cos(th1+th2)/(sin(th1)*sin(th2)))
  /(sin(th1+th2)/(sin(th1)*sin(th2))) by A2,XCMPLX_1:55
  .=((cos(th1)*cos(th2)-sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  /(sin(th1+th2)/(sin(th1)*sin(th2))) by SIN_COS:80
  .=((cos(th1)*cos(th2)-sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  /((sin(th1)*cos(th2)+cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by SIN_COS:80
  .=(cos(th1)*cos(th2)/(sin(th1)*sin(th2))
  -sin(th1)*sin(th2)/(sin(th1)*sin(th2)))
  /((sin(th1)*cos(th2)+cos(th1)*sin(th2))/(sin(th1)*sin(th2)))
  by XCMPLX_1:121
  .=(cos(th1)*cos(th2)/(sin(th1)*sin(th2))
  -sin(th1)*sin(th2)/(sin(th1)*sin(th2)))
  /(sin(th1)*cos(th2)/(sin(th1)*sin(th2))
  +(cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by XCMPLX_1:63
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))
  -sin(th1)*sin(th2)/(sin(th1)*sin(th2)))
  /(sin(th1)*cos(th2)/(sin(th1)*sin(th2))
  +(cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by XCMPLX_1:77
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))
  -sin(th1)/sin(th1)*(sin(th2)/sin(th2)))
  /(sin(th1)*cos(th2)/(sin(th1)*sin(th2))
  +(cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by XCMPLX_1:77
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))
  -sin(th1)/sin(th1)*(sin(th2)/sin(th2)))
  /((sin(th1)/sin(th1))*(cos(th2)/sin(th2))
  +(cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by XCMPLX_1:77
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))
  -sin(th1)/sin(th1)*(sin(th2)/sin(th2)))
  /((sin(th1)/sin(th1))*(cos(th2)/sin(th2))
  +(cos(th1)/sin(th1))*(sin(th2)/sin(th2))) by XCMPLX_1:77
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))-sin(th1)/sin(th1))
  /((sin(th1)/sin(th1))*(cos(th2)/sin(th2))
  +(cos(th1)/sin(th1))*(sin(th2)/sin(th2))) by A1,XCMPLX_1:89
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))-1)
  /((sin(th1)/sin(th1))*(cos(th2)/sin(th2))
  +(cos(th1)/sin(th1))*(sin(th2)/sin(th2))) by A1,XCMPLX_1:60
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))-1)
  /((cos(th2)/sin(th2))+(cos(th1)/sin(th1))*(sin(th2)/sin(th2)))
  by A1,XCMPLX_1:89
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))-1)
  /((cos(th2)/sin(th2))+(cos(th1)/sin(th1))) by A1,XCMPLX_1:89
  .=(cot(th1)*(cos(th2)/sin(th2))-1)
  /((cos(th2)/sin(th2))+(cos(th1)/sin(th1))) by Def2
  .=(cot(th1)*cot(th2)-1)/((cos(th2)/sin(th2))+(cos(th1)/sin(th1))) by Def2
  .=(cot(th1)*cot(th2)-1)/(cot(th2)+(cos(th1)/sin(th1))) by Def2
  .=(cot(th1)*cot(th2)-1)/(cot(th2)+cot(th1)) by Def2;
  hence thesis;
end;

theorem
   sin(th1)<>0 & sin(th2)<>0 implies
  cot(th1-th2)=(cot(th1)*cot(th2)+1)/(cot(th2)-cot(th1))
proof
  assume A1: sin(th1)<>0 & sin(th2)<>0;
  then A2: sin(th1)*sin(th2)<>0 by XCMPLX_1:6;
   cot(th1-th2)=(cos(th1-th2))/(sin(th1-th2)) by Def2
  .=(cos(th1-th2)/(sin(th1)*sin(th2)))
  /(sin(th1-th2)/(sin(th1)*sin(th2))) by A2,XCMPLX_1:55
  .=((cos(th1)*cos(th2)+sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  /(sin(th1-th2)/(sin(th1)*sin(th2))) by Th10
  .=((cos(th1)*cos(th2)+sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  /((sin(th1)*cos(th2)-cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by Th9
  .=(cos(th1)*cos(th2)/(sin(th1)*sin(th2))
  +sin(th1)*sin(th2)/(sin(th1)*sin(th2)))
  /((sin(th1)*cos(th2)-cos(th1)*sin(th2))/(sin(th1)*sin(th2)))
  by XCMPLX_1:63
  .=(cos(th1)*cos(th2)/(sin(th1)*sin(th2))
  +sin(th1)*sin(th2)/(sin(th1)*sin(th2)))
  /(sin(th1)*cos(th2)/(sin(th1)*sin(th2))
  -(cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by XCMPLX_1:121
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))
  +sin(th1)*sin(th2)/(sin(th1)*sin(th2)))
  /(sin(th1)*cos(th2)/(sin(th1)*sin(th2))
  -(cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by XCMPLX_1:77
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))
  +sin(th1)/sin(th1)*(sin(th2)/sin(th2)))
  /(sin(th1)*cos(th2)/(sin(th1)*sin(th2))
  -(cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by XCMPLX_1:77
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))
  +sin(th1)/sin(th1)*(sin(th2)/sin(th2)))
  /((sin(th1)/sin(th1))*(cos(th2)/sin(th2))
  -(cos(th1)*sin(th2))/(sin(th1)*sin(th2))) by XCMPLX_1:77
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))
  +sin(th1)/sin(th1)*(sin(th2)/sin(th2)))
  /((sin(th1)/sin(th1))*(cos(th2)/sin(th2))
  -(cos(th1)/sin(th1))*(sin(th2)/sin(th2))) by XCMPLX_1:77
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))+sin(th1)/sin(th1))
  /((sin(th1)/sin(th1))*(cos(th2)/sin(th2))
  -(cos(th1)/sin(th1))*(sin(th2)/sin(th2))) by A1,XCMPLX_1:89
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))+1)
  /((sin(th1)/sin(th1))*(cos(th2)/sin(th2))
  -(cos(th1)/sin(th1))*(sin(th2)/sin(th2))) by A1,XCMPLX_1:60
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))+1)
  /((cos(th2)/sin(th2))-(cos(th1)/sin(th1))*(sin(th2)/sin(th2)))
  by A1,XCMPLX_1:89
  .=(cos(th1)/sin(th1)*(cos(th2)/sin(th2))+1)
  /((cos(th2)/sin(th2))-(cos(th1)/sin(th1))) by A1,XCMPLX_1:89
  .=(cot(th1)*(cos(th2)/sin(th2))+1)
  /((cos(th2)/sin(th2))-(cos(th1)/sin(th1))) by Def2
  .=(cot(th1)*cot(th2)+1)/((cos(th2)/sin(th2))-(cos(th1)/sin(th1))) by Def2
  .=(cot(th1)*cot(th2)+1)/(cot(th2)-(cos(th1)/sin(th1))) by Def2
  .=(cot(th1)*cot(th2)+1)/(cot(th2)-cot(th1)) by Def2;
  hence thesis;
end;

theorem  Th15:
  cos(th1)<>0 & cos(th2)<>0 & cos(th3)<>0 implies
  sin(th1+th2+th3)
  = cos(th1)*cos(th2)*cos(th3)
  *(tan(th1)+tan(th2)+tan(th3)-tan(th1)*tan(th2)*tan(th3))
proof
  assume A1: cos(th1)<>0 & cos(th2)<>0 & cos(th3)<>0;
   sin(th1+th2+th3) = sin(th1+(th2+th3))
  .=sin(th1)*cos(th2+th3)+cos(th1)*sin(th2+th3) by SIN_COS:80
  .=sin(th1)*(cos(th2)*cos(th3)-sin(th2)*sin(th3)) + cos(th1)*sin(th2+th3)
  by SIN_COS:80
  .=sin(th1)*(cos(th2)*cos(th3))-sin(th1)*(sin(th2)*sin(th3))
  +cos(th1)*(sin(th2)*cos(th3)+cos(th2)*sin(th3)) by SIN_COS:80
  .=(cos(th1)*tan(th1))*(cos(th2)*cos(th3))-sin(th1)*(sin(th2)*sin(th3))
  +(cos(th1)*(sin(th2)*cos(th3))+cos(th1)*(cos(th2)*sin(th3))) by A1,Th8
  .=(cos(th1)*tan(th1))*(cos(th2)*cos(th3))
  -(cos(th1)*tan(th1))*(sin(th2)*sin(th3))
  +(cos(th1)*(sin(th2)*cos(th3))+cos(th1)*(cos(th2)*sin(th3))) by A1,Th8
  .=(cos(th1)*tan(th1))*(cos(th2)*cos(th3))
   -(cos(th1)*tan(th1))*((cos(th2)*tan(th2))*sin(th3))
  +(cos(th1)*(sin(th2)*cos(th3))+cos(th1)*(cos(th2)*sin(th3))) by A1,Th8
  .=(cos(th1)*tan(th1))*(cos(th2)*cos(th3))
  -(cos(th1)*tan(th1))*((cos(th2)*tan(th2))*(cos(th3)*tan(th3)))
  +(cos(th1)*(sin(th2)*cos(th3))+cos(th1)*(cos(th2)*sin(th3))) by A1,Th8
  .=(cos(th1)*tan(th1))*(cos(th2)*cos(th3))
  -(cos(th1)*tan(th1))*((cos(th2)*tan(th2))*(cos(th3)*tan(th3)))
  +(cos(th1)*((cos(th2)*tan(th2))*cos(th3))+cos(th1)*(cos(th2)*sin(th3)))
  by A1,Th8
  .=(cos(th1)*cos(th2))*(cos(th3)*tan(th1))
  -(cos(th1)*cos(th2)*(tan(th1)*tan(th2)))*(cos(th3)*tan(th3))
  +(cos(th1)*((cos(th2)*tan(th2))*cos(th3))
  +cos(th1)*(cos(th2)*(cos(th3)*tan(th3)))) by A1,Th8
  .=cos(th1)*cos(th2)*cos(th3)
  *(tan(th1)+tan(th2)+tan(th3)-tan(th1)*tan(th2)*tan(th3));
  hence thesis;
end;

theorem Th16:
  cos(th1)<>0 & cos(th2)<>0 & cos(th3)<>0 implies
  cos(th1+th2+th3)= cos(th1)*cos(th2)*cos(th3)
  *(1-tan(th2)*tan(th3)-tan(th3)*tan(th1)-tan(th1)*tan(th2))
proof
  assume
  A1: cos(th1)<>0 & cos(th2)<>0 & cos(th3)<>0;
   cos(th1+th2+th3)=cos(th1+(th2+th3))
  .=cos(th1)*cos(th2+th3)-sin(th1)*sin(th2+th3) by SIN_COS:80
  .=cos(th1)*(cos(th2)*cos(th3)-sin(th2)*sin(th3))
  -sin(th1)*sin(th2+th3) by SIN_COS:80
  .=cos(th1)*(cos(th2)*cos(th3))-cos(th1)*(sin(th2)*sin(th3))
  -(sin(th1))*(sin(th2)*cos(th3)+cos(th2)*sin(th3)) by SIN_COS:80
  .=cos(th1)*(cos(th2)*cos(th3))-cos(th1)*((cos(th2)*tan(th2))*sin(th3))
  -(sin(th1)*(sin(th2)*cos(th3)+cos(th2)*sin(th3))) by A1,Th8
  .=cos(th1)*(cos(th2)*cos(th3))
  -cos(th1)*((cos(th2)*tan(th2))*(cos(th3)*tan(th3)))
  -(sin(th1)*(sin(th2)*cos(th3)+cos(th2)*sin(th3))) by A1,Th8
  .=cos(th1)*(cos(th2)*cos(th3))
  -cos(th1)*((cos(th2)*tan(th2))*(cos(th3)*tan(th3)))
  -((cos(th1)*tan(th1))*(sin(th2)*cos(th3) + cos(th2)*sin(th3))) by A1,Th8
  .=cos(th1)*(cos(th2)*cos(th3))
  -cos(th1)*((cos(th2)*tan(th2))*(cos(th3)*tan(th3)))
  -((cos(th1)*tan(th1))*((cos(th2)*tan(th2))*cos(th3) + cos(th2)*sin(th3)))
  by A1,Th8
  .=(cos(th1)*cos(th2))*cos(th3)
  -((cos(th1)*cos(th2))*cos(th3))*(tan(th2)*tan(th3))
  -((cos(th1)*tan(th1))*(((cos(th2)*cos(th3))*tan(th2))
  +cos(th2)*(cos(th3)*tan(th3)))) by A1,Th8
  .=cos(th1)*cos(th2)*cos(th3)
  *(1-tan(th2)*tan(th3)-tan(th3)*tan(th1)-tan(th1)*tan(th2));
  hence thesis;
end;

theorem
   cos(th1)<>0 & cos(th2)<>0 & cos(th3)<>0 implies
  tan(th1+th2+th3)
  = (tan(th1)+tan(th2)+tan(th3)-tan(th1)*tan(th2)*tan(th3))
  /(1-tan(th2)*tan(th3)-tan(th3)*tan(th1)-tan(th1)*tan(th2))
proof
  assume
  A1: cos(th1)<>0 & cos(th2)<>0 & cos(th3)<>0;
  then cos(th1)*cos(th2) <> 0 by XCMPLX_1:6;
  then A2: cos(th1)*cos(th2)*cos(th3) <> 0 by A1,XCMPLX_1:6;
   tan(th1+th2+th3) = sin(th1+th2+th3)/cos(th1+th2+th3) by Def1
  .=cos(th1)*cos(th2)*cos(th3)
  *(tan(th1)+tan(th2)+tan(th3)-tan(th1)*tan(th2)*tan(th3))/cos(th1+th2+th3)
  by A1,Th15
  .=cos(th1)*cos(th2)*cos(th3)
  *(tan(th1)+tan(th2)+tan(th3)-tan(th1)*tan(th2)*tan(th3))
  /(cos(th1)*cos(th2)*cos(th3)
  *(1-tan(th2)*tan(th3)-tan(th3)*tan(th1)-tan(th1)*tan(th2))) by A1,Th16
  .=(cos(th1)*cos(th2)*cos(th3)/(cos(th1)*cos(th2)*cos(th3)))
  /((1-tan(th2)*tan(th3)-tan(th3)*tan(th1)-tan(th1)*tan(th2))
  /(tan(th1)+tan(th2)+tan(th3)-tan(th1)*tan(th2)*tan(th3))) by XCMPLX_1:85
  .=1/((1-tan(th2)*tan(th3)-tan(th3)*tan(th1)-tan(th1)*tan(th2))
  /(tan(th1)+tan(th2)+tan(th3)-tan(th1)*tan(th2)*tan(th3))) by A2,XCMPLX_1:60
  .=(tan(th1)+tan(th2)+tan(th3)-(tan(th1)*tan(th2)*tan(th3)))
  /(1-tan(th2)*tan(th3)-tan(th3)*tan(th1)-tan(th1)*tan(th2)) by XCMPLX_1:57;
  hence thesis;
end;

theorem
   sin(th1) <> 0 & sin(th2) <> 0 & sin(th3) <> 0 implies
  cot(th1+th2+th3)
  = (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th2)*cot(th3)+cot(th3)*cot(th1)+cot(th1)*cot(th2)-1)
proof
  assume
  A1: sin(th1) <> 0 & sin(th2) <> 0 & sin(th3) <> 0;
  then A2: sin(th1)*sin(th2) <> 0 by XCMPLX_1:6;
  then A3: sin(th1)*sin(th2)*sin(th3) <> 0 by A1,XCMPLX_1:6;
   cot(th1+th2+th3)
  = cos(th1+th2+th3)/sin(th1+th2+th3) by Def2
  .= (cos(th1+th2)*cos(th3)-sin(th1+th2)*sin(th3))
  /sin(th1+th2+th3) by SIN_COS:80
  .= ((cos(th1)*cos(th2)-sin(th1)*sin(th2))*cos(th3)-sin(th1+th2)*sin(th3))
  /sin(th1+th2+th3) by SIN_COS:80
  .= (cos(th3)*(cos(th1)*cos(th2)-sin(th1)*sin(th2))
  -(sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2))))
  /sin(th1+th2+th3) by SIN_COS:80
  .= ((cos(th3)*(cos(th1)*cos(th2)-sin(th1)*sin(th2))
  -(sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2))))
  /(sin(th1)*sin(th2)*sin(th3)))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by A3,XCMPLX_1:55
  .= ((cos(th3)*(cos(th1)*cos(th2)-sin(th1)*sin(th2))
  /(sin(th3)*(sin(th1)*sin(th2))))
  -((sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2)))
  /(sin(th3)*(sin(th1)*sin(th2)))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by XCMPLX_1:121
  .= (((cos(th3)/sin(th3))*((cos(th1)*cos(th2)-sin(th1)*sin(th2))
  /(sin(th1)*sin(th2)))) -((sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2)))
  /(sin(th3)*(sin(th1)*sin(th2)))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by XCMPLX_1:77
  .= (cot(th3)*((cos(th1)*cos(th2)-sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  -((sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2)))
  /(sin(th3)*(sin(th1)*sin(th2)))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by Def2
  .= (cot(th3)*((cos(th1)*cos(th2)/(sin(th1)*sin(th2)))
  -(sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  -((sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2)))
  /(sin(th3)*(sin(th1)*sin(th2)))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by XCMPLX_1:121
  .= (cot(th3)*((cos(th1)/sin(th1))*(cos(th2)/(sin(th2)))
  -(sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  -((sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2)))
  /(sin(th3)*(sin(th1)*sin(th2)))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by XCMPLX_1:77
  .= (cot(th3)*(cot(th1)*(cos(th2)/(sin(th2)))
  -(sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  -((sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2)))
  /(sin(th3)*(sin(th1)*sin(th2)))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by Def2
  .= (cot(th3)*(cot(th1)*cot(th2)
  -(sin(th1)*sin(th2))/(sin(th1)*sin(th2)))
  -((sin(th3)*(sin(th1)*cos(th2)+cos(th1)*sin(th2)))
  /(sin(th3)*(sin(th1)*sin(th2)))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by Def2
  .= (cot(th3)*(cot(th1)*cot(th2)-1)
  -(((sin(th1)*cos(th2)+cos(th1)*sin(th2))*sin(th3))
  /(sin(th1)*sin(th2)*sin(th3))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by A2,XCMPLX_1:60
  .= (cot(th3)*(cot(th1)*cot(th2)-1)
  -((sin(th1)*cos(th2)+cos(th1)*sin(th2))/(sin(th1)*sin(th2))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by A1,XCMPLX_1:92
  .= (cot(th3)*(cot(th1)*cot(th2)-1)
  -((sin(th1)*cos(th2)/(sin(th1)*sin(th2)))
  +(cos(th1)*sin(th2))/(sin(th1)*sin(th2))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by XCMPLX_1:63
  .= (cot(th3)*(cot(th1)*cot(th2)-1)
  -(cos(th2)/sin(th2)+(cos(th1)*sin(th2))/(sin(th1)*sin(th2))))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by A1,XCMPLX_1:92
  .= (cot(th3)*(cot(th1)*cot(th2)-1)
  -(cos(th2)/sin(th2)+cos(th1)/sin(th1)))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by A1,XCMPLX_1:92
  .= (cot(th3)*(cot(th1)*cot(th2)-1)-(cot(th2)+cos(th1)/sin(th1)))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by Def2
  .= ((cot(th1)*cot(th2)*cot(th3)-cot(th3)*1)-(cot(th2)+cot(th1)))
  /(sin(th1+th2+th3)/(sin(th1)*sin(th2)*sin(th3))) by Def2
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /((sin(th1+th2)*cos(th3)+cos(th1+th2)*sin(th3))
  /(sin(th1)*sin(th2)*sin(th3))) by SIN_COS:80
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(sin(th1+th2)*cos(th3)/(sin(th1)*sin(th2)*sin(th3))
  +cos(th1+th2)*sin(th3)/(sin(th1)*sin(th2)*sin(th3))) by XCMPLX_1:63
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(sin(th1+th2)*cos(th3)/(sin(th1)*sin(th2)*sin(th3))
  +cos(th1+th2)/(sin(th1)*sin(th2))) by A1,XCMPLX_1:92
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /((cos(th3)/sin(th3))*(sin(th1+th2)/(sin(th1)*sin(th2)))
  +cos(th1+th2)/(sin(th1)*sin(th2))) by XCMPLX_1:77
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(sin(th1+th2)/(sin(th1)*sin(th2)))
  +cos(th1+th2)/(sin(th1)*sin(th2))) by Def2
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*((sin(th1)*cos(th2)+cos(th1)*sin(th2))/(sin(th1)*sin(th2)))
  +cos(th1+th2)/(sin(th1)*sin(th2))) by SIN_COS:80
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*((sin(th1)*cos(th2)/(sin(th1)*sin(th2))
  +cos(th1)*sin(th2)/(sin(th1)*sin(th2))))
  +cos(th1+th2)/(sin(th1)*sin(th2))) by XCMPLX_1:63
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cos(th2)/sin(th2)+cos(th1)*sin(th2)/(sin(th1)*sin(th2)))
  +cos(th1+th2)/(sin(th1)*sin(th2))) by A1,XCMPLX_1:92
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cos(th2)/sin(th2)+cos(th1)/sin(th1))
  +cos(th1+th2)/(sin(th1)*sin(th2))) by A1,XCMPLX_1:92
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cot(th2)+cos(th1)/sin(th1))
  +cos(th1+th2)/(sin(th1)*sin(th2))) by Def2
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cot(th2)+cot(th1))+cos(th1+th2)/(sin(th1)*sin(th2))) by Def2
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cot(th2)+cot(th1))
  +(cos(th1)*cos(th2)-sin(th1)*sin(th2))/(sin(th1)*sin(th2))) by SIN_COS:80
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cot(th2)+cot(th1))
  +(cos(th1)*cos(th2)/(sin(th1)*sin(th2))
  -sin(th1)*sin(th2)/(sin(th1)*sin(th2)))) by XCMPLX_1:121
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cot(th2)+cot(th1))
  +(cos(th1)*cos(th2)/(sin(th1)*sin(th2))-1)) by A2,XCMPLX_1:60
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cot(th2)+cot(th1))
  +((cos(th1)/sin(th1))*(cos(th2)/sin(th2))-1)) by XCMPLX_1:77
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th3)*(cot(th2)+cot(th1))
  +(cot(th1)*(cos(th2)/sin(th2))-1)) by Def2
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th2)*cot(th3)+cot(th3)*cot(th1)+(cot(th1)*cot(th2)-1)) by Def2
  .= (cot(th1)*cot(th2)*cot(th3)-cot(th1)-cot(th2)-cot(th3))
  /(cot(th2)*cot(th3)+cot(th3)*cot(th1)+cot(th1)*cot(th2)-1);
  hence thesis;
end;

theorem Th19:
  sin(th1)+sin(th2)= 2*(cos((th1-th2)/2)*sin((th1+th2)/2))
proof
   sin(th1)+sin(th2)= sin(th1/2+th1/2)+sin(th2/2+th2/2)
  .= sin(th1/2)*cos(th1/2)+cos(th1/2)*sin(th1/2)+sin(th2/2+th2/2) by SIN_COS:80
  .= 2*(sin(th1/2)*cos(th1/2))
  +(sin(th2/2)*cos(th2/2)+sin(th2/2)*cos(th2/2)) by SIN_COS:80
  .= 2*(sin(th1/2)*cos(th1/2)*1+sin(th2/2)*cos(th2/2))
  .= 2*(sin(th1/2)*cos(th1/2) *(cos(th2/2)*cos(th2/2)+sin(th2/2)*sin(th2/2))
  +sin(th2/2)*cos(th2/2)*1) by SIN_COS:32
  .= 2*(sin(th1/2)*cos(th1/2)*(cos(th2/2)*cos(th2/2))
  +sin(th1/2)*cos(th1/2)*(sin(th2/2)*sin(th2/2))
  +sin(th2/2)*cos(th2/2)*(cos(th1/2)*cos(th1/2)+sin(th1/2)*sin(th1/2))) by
SIN_COS:32
  .= 2*((sin(th1/2)*cos(th2/2)+cos(th1/2)*sin(th2/2))
  *(cos(th1/2)*cos(th2/2)+sin(th1/2)*sin(th2/2)))
  .= 2*((sin(th1/2+th2/2))
  *(cos(th1/2)*cos(th2/2)+sin(th1/2)*sin(th2/2))) by SIN_COS:80
  .= 2*(cos(th1/2-th2/2)*sin((th1+th2)/2)) by Th10
  .= 2*(cos((th1-th2)/2)*sin((th1+th2)/2));
  hence thesis;
end;

theorem Th20:
  sin(th1)-sin(th2)= 2*(cos((th1+th2)/2)*sin((th1-th2)/2))
proof
   sin(th1)-sin(th2)= sin(th1/2+th1/2)-sin(th2/2+th2/2)
  .= sin(th1/2)*cos(th1/2)+cos(th1/2)*sin(th1/2)-sin(th2/2+th2/2) by SIN_COS:80
  .= 2*(sin(th1/2)*cos(th1/2))
  -(sin(th2/2)*cos(th2/2)+sin(th2/2)*cos(th2/2)) by SIN_COS:80
  .= 2*(sin(th1/2)*cos(th1/2)*1-sin(th2/2)*cos(th2/2))
  .= 2*(sin(th1/2)*cos(th1/2)*(cos(th2/2)*cos(th2/2)+sin(th2/2)*sin(th2/2))
  -sin(th2/2)*cos(th2/2)*1)
  by SIN_COS:32
  .= 2*(sin(th1/2)*cos(th1/2)*(cos(th2/2)*cos(th2/2))
  +sin(th1/2)*cos(th1/2)*(sin(th2/2)*sin(th2/2))
  -(sin(th2/2)*cos(th2/2)*(cos(th1/2)*cos(th1/2)+sin(th1/2)*sin(th1/2)))) by
SIN_COS:32
  .= 2*((sin(th1/2)*cos(th2/2)-cos(th1/2)*sin(th2/2))
  *(cos(th1/2)*cos(th2/2)+-sin(th1/2)*sin(th2/2)))
  .= 2*(sin(th1/2-th2/2)*(cos(th1/2)*cos(th2/2)-sin(th1/2)*sin(th2/2)))
  by Th9
  .= 2*(sin((th1-th2)/2)*(cos(th1/2+th2/2))) by SIN_COS:80
  .= 2*(cos((th1+th2)/2)*sin((th1-th2)/2));
  hence thesis;
end;

theorem Th21:
  cos(th1)+cos(th2)= 2*(cos((th1+th2)/2)*cos((th1-th2)/2))
proof
   cos(th1)+cos(th2)= cos(th1/2+th1/2)+cos(th2/2+th2/2)
  .= cos(th1/2)*cos(th1/2)-sin(th1/2)*sin(th1/2)+cos(th2/2+th2/2)
  by SIN_COS:80
  .= ((cos(th1/2)*cos(th1/2)+(-sin(th1/2)*sin(th1/2)))+(1+(-1)))
  +(cos(th2/2)*cos(th2/2)-sin(th2/2)*sin(th2/2)) by SIN_COS:80
  .= ((cos(th1/2)*cos(th1/2)+(-sin(th1/2)*sin(th1/2)))+1)
  +((-1)+(cos(th2/2)*cos(th2/2)-sin(th2/2)*sin(th2/2)))
  .= ((cos(th1/2)*cos(th1/2)+(-sin(th1/2)*sin(th1/2)))
  +(sin(th1/2)*sin(th1/2)+cos(th1/2)*cos(th1/2)))
  +((-1)+(cos(th2/2)*cos(th2/2)-sin(th2/2)*sin(th2/2))) by SIN_COS:32
  .= (cos(th1/2)*cos(th1/2)+((sin(th1/2)*sin(th1/2)
  --(-sin(th1/2)*sin(th1/2)))+cos(th1/2)*cos(th1/2)))
  +((-(sin(th2/2)*sin(th2/2)+cos(th2/2)*cos(th2/2)))
  +(cos(th2/2)*cos(th2/2)-sin(th2/2)*sin(th2/2))) by SIN_COS:32
  .= 2*(cos(th1/2)*cos(th1/2)-(sin(th2/2)*sin(th2/2)*(1)))
  .= 2*(cos(th1/2)*cos(th1/2)-(sin(th2/2)*sin(th2/2)
  *(cos(th1/2)*cos(th1/2)+sin(th1/2)*sin(th1/2))))
  by SIN_COS:32
  .= 2*(cos(th1/2)*cos(th1/2)*(1---sin(th2/2)*sin(th2/2))
  +-sin(th2/2)*sin(th2/2)*(sin(th1/2)*sin(th1/2)))
  .= 2*(cos(th1/2)*cos(th1/2)*((cos(th2/2)*cos(th2/2)+sin(th2/2)*sin(th2/2))
  ---sin(th2/2)*sin(th2/2))
  +-sin(th2/2)*sin(th2/2)*(sin(th1/2)*sin(th1/2)))
  by SIN_COS:32
  .= 2*((cos(th1/2)*cos(th2/2)+sin(th1/2)*sin(th2/2))
  *(cos(th1/2)*cos(th2/2)+-sin(th1/2)*sin(th2/2)))
  .= 2*((cos(th1/2-th2/2))
  *(cos(th1/2)*cos(th2/2)-sin(th1/2)*sin(th2/2))) by Th10
  .= 2*((cos((th1-th2)/2)*(cos(th1/2+th2/2)))) by SIN_COS:80
  .= 2*(cos((th1+th2)/2)*cos((th1-th2)/2));
  hence thesis;
end;

theorem Th22:
  cos(th1)-cos(th2)= -2*(sin((th1+th2)/2)*sin((th1-th2)/2))
proof
   cos(th1)-cos(th2)= cos(th1/2+th1/2)-cos(th2/2+th2/2)
  .= cos(th1/2)*cos(th1/2)-sin(th1/2)*sin(th1/2)-cos(th2/2+th2/2)
  by SIN_COS:80
  .= ((cos(th1/2)*cos(th1/2)+(-sin(th1/2)*sin(th1/2)))+(1+(-1)))
  -(cos(th2/2)*cos(th2/2)-sin(th2/2)*sin(th2/2)) by SIN_COS:80
  .= ((cos(th1/2)*cos(th1/2)+(-sin(th1/2)*sin(th1/2)))+1)
  +((-1)+(-cos(th2/2)*cos(th2/2)+sin(th2/2)*sin(th2/2)))
  .= ((cos(th1/2)*cos(th1/2)+(-sin(th1/2)*sin(th1/2)))
  +(sin(th1/2)*sin(th1/2)+cos(th1/2)*cos(th1/2)))
  +((-1)+(-cos(th2/2)*cos(th2/2)+sin(th2/2)*sin(th2/2))) by SIN_COS:32
  .= (cos(th1/2)*cos(th1/2)+((sin(th1/2)*sin(th1/2)
  --(-sin(th1/2)*sin(th1/2)))+cos(th1/2)*cos(th1/2)))
  +((-(sin(th2/2)*sin(th2/2)+cos(th2/2)*cos(th2/2)))
  +(-cos(th2/2)*cos(th2/2)+sin(th2/2)*sin(th2/2))) by SIN_COS:32
  .= 2*((cos(th1/2)*cos(th1/2)*(1))-cos(th2/2)*cos(th2/2))
  .= 2*((cos(th1/2)*cos(th1/2)*(sin(th2/2)*sin(th2/2)+cos(th2/2)*cos(th2/2)))
  -cos(th2/2)*cos(th2/2)) by SIN_COS:32
  .= 2*(cos(th1/2)*cos(th1/2)*(sin(th2/2)*sin(th2/2))
  +(cos(th2/2)*cos(th2/2)*(cos(th1/2)*cos(th1/2)---1)))
  .= 2*(cos(th1/2)*cos(th1/2)*(sin(th2/2)*sin(th2/2))
  +(cos(th2/2)*cos(th2/2)*(cos(th1/2)*cos(th1/2)
  ---(cos(th1/2)*cos(th1/2)+sin(th1/2)*sin(th1/2)))))
  by SIN_COS:32
  .= -2*((sin(th1/2)*cos(th2/2)--(-cos(th1/2)*sin(th2/2)))
  *(sin(th1/2)*cos(th2/2)+cos(th1/2)*sin(th2/2)))
  .= -2*(sin(th1/2+th2/2)*(sin(th1/2)*cos(th2/2)-cos(th1/2)*sin(th2/2)))
  by SIN_COS:80
  .= -2*(sin((th1+th2)/2)*sin(th1/2-th2/2)) by Th9
  .= -2*(sin((th1+th2)/2)*sin((th1-th2)/2));
  hence thesis;
end;

theorem
   cos(th1)<>0 & cos(th2)<>0 implies
  tan(th1)+tan(th2)= sin(th1+th2)/(cos(th1)*cos(th2))
proof
  assume
  A1: cos(th1)<>0 & cos(th2)<>0;
   tan(th1)+tan(th2)= sin(th1)/cos(th1)+tan(th2) by Def1
  .= sin(th1)/cos(th1)+sin(th2)/cos(th2) by Def1
  .= (sin(th1)*cos(th2)+sin(th2)*cos(th1))/(cos(th1)*cos(th2))
  by A1,XCMPLX_1:117
  .= sin(th1+th2)/(cos(th1)*cos(th2)) by SIN_COS:80;
  hence thesis;
end;

theorem
   cos(th1)<>0 & cos(th2)<>0 implies
  tan(th1)-tan(th2)= sin(th1-th2)/(cos(th1)*cos(th2))
proof
  assume
  A1: cos(th1)<>0 & cos(th2)<>0;
   tan(th1)-tan(th2)= sin(th1)/cos(th1)-tan(th2) by Def1
  .= sin(th1)/cos(th1)-sin(th2)/cos(th2) by Def1
  .= (sin(th1)*cos(th2)-sin(th2)*cos(th1))/(cos(th1)*cos(th2))
  by A1,XCMPLX_1:131
  .= sin(th1-th2)/(cos(th1)*cos(th2)) by Th9;
  hence thesis;
end;

theorem
   cos(th1)<>0 & sin(th2)<>0 implies
  tan(th1)+cot(th2)= cos(th1-th2)/(cos(th1)*sin(th2))
proof
  assume A1: cos(th1)<>0 & sin(th2)<>0;
   tan(th1)+cot(th2)= sin(th1)/cos(th1)+cot(th2) by Def1
  .= sin(th1)/cos(th1)+(cos(th2)/sin(th2)) by Def2
  .= (cos(th1)*cos(th2)+sin(th1)*sin(th2))/(cos(th1)*sin(th2))
  by A1,XCMPLX_1:117
  .= cos(th1-th2)/(cos(th1)*sin(th2)) by Th10;
  hence thesis;
end;

theorem
   cos(th1)<>0 & sin(th2)<>0 implies
  tan(th1)-cot(th2)= -cos(th1+th2)/(cos(th1)*sin(th2))
proof
  assume A1: cos(th1)<>0 & sin(th2)<>0;
   tan(th1)-cot(th2)= sin(th1)/cos(th1)-cot(th2) by Def1
  .= sin(th1)/cos(th1)-cos(th2)/sin(th2) by Def2
  .= (sin(th1)*sin(th2)---cos(th1)*cos(th2))/(cos(th1)*sin(th2))
  by A1,XCMPLX_1:131
  .= (-(cos(th1)*cos(th2)-sin(th1)*sin(th2)))/(cos(th1)*sin(th2))
  .= -(cos(th1)*cos(th2)-sin(th1)*sin(th2))/(cos(th1)*sin(th2))
  by XCMPLX_1:188
  .= -cos(th1+th2)/(cos(th1)*sin(th2)) by SIN_COS:80;
  hence thesis;
end;

theorem
   sin(th1)<>0 & sin(th2)<>0 implies
  cot(th1)+cot(th2) = sin(th1+th2)/(sin(th1)*sin(th2))
proof
  assume A1: sin(th1)<>0 & sin(th2)<>0;
   cot(th1)+cot(th2)= cos(th1)/sin(th1)+cot(th2) by Def2
  .= cos(th1)/sin(th1)+cos(th2)/sin(th2) by Def2
  .= (cos(th1)*sin(th2)+cos(th2)*sin(th1))/(sin(th1)*sin(th2))
  by A1,XCMPLX_1:117
  .= sin(th1+th2)/(sin(th1)*sin(th2)) by SIN_COS:80;
  hence thesis;
end;

theorem
   sin(th1)<>0 & sin(th2)<>0 implies
  cot(th1)-cot(th2)= -sin(th1-th2)/(sin(th1)*sin(th2))
proof
  assume A1: sin(th1)<>0 & sin(th2)<>0;
   cot(th1)-cot(th2)= cos(th1)/sin(th1)-cot(th2) by Def2
  .= cos(th1)/sin(th1)-cos(th2)/sin(th2) by Def2
  .= (cos(th1)*sin(th2)---cos(th2)*sin(th1))/(sin(th1)*sin(th2))
  by A1,XCMPLX_1:131
  .= (-(sin(th1)*cos(th2)-cos(th1)*sin(th2)))/(sin(th1)*sin(th2))
  .= (-(sin(th1-th2)))/(sin(th1)*sin(th2)) by Th9
  .= -sin(th1-th2)/(sin(th1)*sin(th2)) by XCMPLX_1:188;
  hence thesis;
end;

theorem
   sin(th1+th2)+sin(th1-th2) = 2*(sin(th1)*cos(th2))
proof
   sin(th1+th2)+sin(th1-th2)
  = (sin(th1)*cos(th2)+cos(th1)*sin(th2))+sin(th1-th2) by SIN_COS:80
  .= (sin(th1)*cos(th2)+cos(th1)*sin(th2))
  +(sin(th1)*cos(th2)---cos(th1)*sin(th2)) by Th9
  .= 2*(sin(th1)*cos(th2));
  hence thesis;
end;

theorem
   sin(th1+th2)-sin(th1-th2) = 2*(cos(th1)*sin(th2))
proof
   sin(th1+th2)-sin(th1-th2)
  = (sin(th1)*cos(th2)+cos(th1)*sin(th2))-sin(th1-th2) by SIN_COS:80
  .= (sin(th1)*cos(th2)+cos(th1)*sin(th2))
  ---(sin(th1)*cos(th2)-cos(th1)*sin(th2)) by Th9
  .= 2*(cos(th1)*sin(th2));
  hence thesis;
end;

theorem
   cos(th1+th2)+cos(th1-th2) = 2*(cos(th1)*cos(th2))
proof
   cos(th1+th2)+cos(th1-th2)
  = (cos(th1)*cos(th2)-sin(th1)*sin(th2))+cos(th1-th2) by SIN_COS:80
  .= (cos(th1)*cos(th2)+-sin(th1)*sin(th2))
  +(sin(th1)*sin(th2)+cos(th1)*cos(th2)) by Th10
  .= 2*(cos(th1)*cos(th2));
  hence thesis;
end;

theorem
   cos(th1+th2)-cos(th1-th2) = -2*(sin(th1)*sin(th2))
proof
   cos(th1+th2)-cos(th1-th2)
  = (cos(th1)*cos(th2)-sin(th1)*sin(th2))-cos(th1-th2) by SIN_COS:80
  .= (cos(th1)*cos(th2)+-sin(th1)*sin(th2))
  -(cos(th1)*cos(th2)+sin(th1)*sin(th2)) by Th10
  .= -2*(sin(th1)*sin(th2));
  hence thesis;
end;

theorem Th33:
  sin(th1)*sin(th2) = -(1/2)*(cos(th1+th2)-cos(th1-th2))
proof
   sin(th1)*sin(th2)
  = (cos(th1)*cos(th2)+sin(th1)*sin(th2)
  -cos(th1)*cos(th2)+sin(th1)*sin(th2))/2
  .= (cos(th1)*cos(-th2)+sin(th1)*sin(th2)
  -cos(th1)*cos(th2)+sin(th1)*sin(th2))/2 by SIN_COS:34
  .= (cos(th1)*cos(-th2)-sin(th1)*(-sin(th2))
  -cos(th1)*cos(th2)+sin(th1)*sin(th2))/2
  .= (cos(th1)*cos(-th2)-sin(th1)*sin(-th2)
  -cos(th1)*cos(th2)+sin(th1)*sin(th2))/2 by SIN_COS:34
  .= (cos(th1+(-th2)) - cos(th1)*cos(th2)+sin(th1)*sin(th2))/2 by SIN_COS:80
  .= (cos(th1-th2)-cos(th1)*cos(th2)+sin(th1)*sin(th2))/2
  .= (cos(th1-th2)-(cos(th1)*cos(th2)-sin(th1)*sin(th2)))/2
  .= (cos(th1-th2)-cos(th1+th2))/(2/1)
  by SIN_COS:80
  .= 1/2*-(cos(th1+th2)-cos(th1-th2)) by XCMPLX_1:81
  .= -(1/2)*(cos(th1+th2)-cos(th1-th2));
  hence thesis;
end;

theorem Th34:
  sin(th1)*cos(th2)= (1/2)*(sin(th1+th2)+sin(th1-th2))
proof
   sin(th1)*cos(th2)
  = (1/2)*((sin(th1)*cos(th2)+cos(th1)*sin(th2))
  +(sin(th1)*cos(th2)-cos(th1)*sin(th2)))
  .= (1/2)*(sin(th1+th2)+(sin(th1)*cos(th2)+cos(th1)*(-sin(th2)))) by
SIN_COS:80
  .= (1/2)*(sin(th1+th2)+(sin(th1)*cos(th2)+cos(th1)*sin(-th2)))
  by SIN_COS:34
  .= (1/2)*(sin(th1+th2)+(sin(th1)*cos(-th2)+cos(th1)*sin(-th2)))
  by SIN_COS:34
  .= (1/2)*(sin(th1+th2)+sin(th1+(-th2))) by SIN_COS:80
  .= (1/2)*(sin(th1+th2)+sin(th1-th2));
  hence thesis;
end;

theorem Th35:
  cos(th1)*sin(th2)= (1/2)*(sin(th1+th2)-sin(th1-th2))
proof
   cos(th1)*sin(th2)
  = (1/2)*(sin(th1)*cos(th2)+cos(th1)*sin(th2)
  -(sin(th1)*cos(th2)-cos(th1)*sin(th2)))
  .= (1/2)*(sin(th1+th2)-(sin(th1)*cos(th2)+cos(th1)*(-sin(th2)))) by
SIN_COS:80
  .= (1/2)*(sin(th1+th2)-(sin(th1)*cos(th2)+cos(th1)*sin(-th2)))
  by SIN_COS:34
  .= (1/2)*(sin(th1+th2)-(sin(th1)*cos(-th2)+cos(th1)*sin(-th2)))
  by SIN_COS:34
  .= (1/2)*(sin(th1+th2)-sin(th1+(-th2))) by SIN_COS:80
  .= (1/2)*(sin(th1+th2)-sin(th1-th2));
  hence thesis;
end;

theorem Th36:
  cos(th1)*cos(th2)= (1/2)*(cos(th1+th2)+cos(th1-th2))
proof
   cos(th1)*cos(th2)
  = (1/2)*((cos(th1)*cos(th2)-sin(th1)*sin(th2))
  +(cos(th1)*cos(th2)-(-sin(th1)*sin(th2))))
  .= (1/2)*(cos(th1+th2)+(cos(th1)*cos(th2)-sin(th1)*(-sin(th2)))) by
SIN_COS:80
  .= (1/2)*(cos(th1+th2)+(cos(th1)*cos(th2)-sin(th1)*sin(-th2)))
  by SIN_COS:34
  .= (1/2)*(cos(th1+th2)+(cos(th1)*cos(-th2)-sin(th1)*sin(-th2)))
  by SIN_COS:34
  .= (1/2)*(cos(th1+th2)+(cos(th1+(-th2)))) by SIN_COS:80
  .= (1/2)*(cos(th1+th2)+cos(th1-th2));
  hence thesis;
end;

theorem
   sin(th1)*sin(th2)*sin(th3)
  = (1/4)
  *(sin(th1+th2-th3)+sin(th2+th3-th1)+sin(th3+th1-th2)-sin(th1+th2+th3))
proof
   sin(th1)*sin(th2)*sin(th3)
  =(-((1/2)*(cos(th1+th2)-cos(th1-th2))))*sin(th3) by Th33
  .=(1/2)*(cos(th1-th2)*sin(th3)-cos(th1+th2)*sin(th3))
  .=(1/2)*((1/2)*(sin((th1-th2)+th3)-sin((th1-th2)-th3))
  -cos(th1+th2)*sin(th3)) by Th35
  .=(1/2)*((1/2)*(sin((th1-th2)+th3)-sin((th1-th2)-th3))
  -(1/2)*(sin((th1+th2)+th3)-sin((th1+th2)-th3))) by Th35
  .=((1/2)*(1/2))*((sin((th1-th2)+th3)-sin((th1-th2)-th3))
  -(sin((th1+th2)+th3)-sin((th1+th2)-th3)))
  .=(1/(2*2))*((sin((th1-th2)+th3)+-sin((th1-th2)-th3))
  +(sin((th1+th2)-th3)-sin((th1+th2)+th3)))
  .=(1/(2*2))*((sin((th1-th2)+th3)+sin(-((th1-th2)-th3)))
  +(sin((th1+th2)-th3)-sin((th1+th2)+th3))) by SIN_COS:34
  .=(1/(2*2))*((sin((th3+th1)-th2)+sin((th3+th2)-th1))
  +(sin((th1+th2)-th3)+-sin((th1+th2)+th3)))
  .=(1/(2*2))*(((sin(th3+th1-th2)+sin(th2+th3-th1))
  +sin(th1+th2-th3))+-sin(th1+th2+th3)) by XCMPLX_1:1
  .=(1/(2*2))*(sin(th1+th2-th3)+sin(th2+th3-th1)
  +sin(th3+th1-th2)-sin(th1+th2+th3));
  hence thesis;
end;

theorem
   sin(th1)*sin(th2)*cos(th3)
  = (1/4)
  *(-cos(th1+th2-th3)+cos(th2+th3-th1)+cos(th3+th1-th2)-cos(th1+th2+th3))
proof
   sin(th1)*sin(th2)*cos(th3)
  =(-((1/2)*(cos(th1+th2)-cos(th1-th2))))*cos(th3) by Th33
  .=(1/2)*(cos(th1-th2)*cos(th3)-cos(th1+th2)*cos(th3))
  .=(1/2)*((1/2)*(cos((th1-th2)+th3)+cos((th1-th2)-th3))
  -cos(th1+th2)*cos(th3)) by Th36
  .=(1/2)*((1/2)*(cos((th1-th2)+th3)+cos((th1-th2)-th3))
  -(1/2)*(cos((th1+th2)+th3)+cos((th1+th2)-th3))) by Th36
  .=((1/2)*(1/2))*((cos((th1-th2)+th3)+cos((th1-th2)-th3))
  -(cos((th1+th2)+th3)+cos((th1+th2)-th3)))
  .=(1/(2*2))*((cos(th3+(th1+-th2))+cos((th1-th2)-th3))
  ---(cos((th1+th2)+th3)+cos((th1+th2)-th3)))
  .=(1/(2*2))*((cos(th3+(th1+-th2))+cos((th1-th2)-th3))
  +-(cos((th1+th2)+th3)+cos((th1+th2)-th3)))
  .=(1/(2*2))*((cos((th3+th1)+-th2)+cos((th1-th2)-th3))
  +(-cos((th1+th2)+th3)+-cos((th1+th2)-th3)))
  .=(1/(2*2))*((-cos((th1+th2)-th3)+cos((th1-th2)-th3))
  +(cos((th3+th1)+-th2)+-cos((th1+th2)+th3)))
  .=(1/(2*2))*((-cos((th1+th2)-th3)+cos(-((th2-th1)+th3)))
  +(cos((th3+th1)+-th2)+-cos((th1+th2)+th3)))
  .=(1/(2*2))*((-cos((th1+th2)-th3)+cos((th2-th1)+th3))
  +(cos((th3+th1)+-th2)+-cos((th1+th2)+th3))) by SIN_COS:34
  .=(1/(2*2))*(((-cos((th1+th2)-th3)+cos((th2-th1)+th3))
  +cos((th3+th1)+-th2))---cos((th1+th2)+th3))
  .=(1/(2*2))*(-cos(th1+th2-th3)+cos(th2+th3-th1)
  +cos(th3+th1-th2)-cos(th1+th2+th3));
  hence thesis;
end;

theorem
   sin(th1)*cos(th2)*cos(th3)
  = (1/4)
  *(sin(th1+th2-th3)-sin(th2+th3-th1)+sin(th3+th1-th2)+sin(th1+th2+th3))
proof
   sin(th1)*cos(th2)*cos(th3)
  =((1/2)*(sin(th1+th2)+sin(th1-th2)))*cos(th3) by Th34
  .=(1/2)*(sin(th1+th2)*cos(th3)+sin(th1-th2)*cos(th3))
  .=(1/2)*(((1/2)*(sin((th1+th2)+th3)+sin((th1+th2)-th3)))
  +sin(th1-th2)*cos(th3)) by Th34
  .=(1/2)*(((1/2)*(sin((th1+th2)+th3)+sin((th1+th2)-th3)))
  +((1/2)*(sin((th1-th2)+th3)+sin((th1-th2)-th3)))) by Th34
  .=((1/2)*(1/2))*((sin((th1+th2)+th3)+sin((th1+th2)-th3))
  +(sin((th1-th2)+th3)+sin((th1-th2)-th3)))
  .=(1/(2*2))*((sin((th1+th2)+th3)+sin((th1+th2)-th3))
  +(sin((th1+-th2)+th3)+sin(-((th2-th1)+th3))))
  .=(1/(2*2))*((sin((th1+th2)+th3)+sin((th1+th2)-th3))
  +(-sin((th2-th1)+th3)+sin((th3+th1)+-th2))) by SIN_COS:34
  .=(1/(2*2))*(((sin((th1+th2)-th3)---sin((th2-th1)+th3)))
  +sin((th3+th1)+-th2)+sin((th1+th2)+th3))
  .=(1/(2*2))*(sin(th1+th2-th3)-sin(th2+th3-th1)
  +sin(th3+th1-th2)+sin(th1+th2+th3));
  hence thesis;
end;

theorem
   cos(th1)*cos(th2)*cos(th3)
  = (1/4)
  *(cos(th1+th2-th3)+cos(th2+th3-th1)+cos(th3+th1-th2)+cos(th1+th2+th3))
proof
   cos(th1)*cos(th2)*cos(th3)
  =((1/2)*(cos(th1+th2)+cos(th1-th2)))*cos(th3) by Th36
  .=(1/2)*(cos(th1+th2)*cos(th3)+cos(th1-th2)*cos(th3))
  .=(1/2)*(((1/2)*(cos((th1+th2)+th3)+cos((th1+th2)-th3)))
  +cos(th1-th2)*cos(th3)) by Th36
  .=(1/2)*(((1/2)*(cos((th1+th2)+th3)+cos((th1+th2)-th3)))
  +((1/2)*(cos((th1-th2)+th3)+cos((th1-th2)-th3)))) by Th36
  .=((1/2)*(1/2))*((cos((th1+th2)+th3)+cos((th1+th2)-th3))
  +(cos((th1-th2)+th3)+cos((th1-th2)-th3)))
  .=(1/(2*2))*((cos((th1+th2)+th3)+cos((th1+th2)-th3))
  +(cos((th3+th1)+-th2)+cos(-((th2-th1)+th3))))
  .=(1/(2*2))*((cos((th1+th2)+th3)+cos((th1+th2)-th3))
  +(cos((th3+th1)-th2)+cos((th2+th3)+-th1))) by SIN_COS:34
  .=(1/(2*2))*((cos((th1+th2)-th3)+(cos((th2+th3)-th1)
  +cos((th3+th1)-th2)))+cos((th1+th2)+th3))
  by XCMPLX_1:1
  .=(1/(2*2))*(cos(th1+th2-th3)+cos(th2+th3-th1)
  +cos(th3+th1-th2)+cos(th1+th2+th3));
  hence thesis;
end;

theorem Th41:
  sin(th1+th2)*sin(th1-th2) = sin(th1)*sin(th1)-sin(th2)*sin(th2)
proof
   sin(th1+th2)*sin(th1-th2)
  = (sin(th1)*cos(th2)+cos(th1)*sin(th2))*sin(th1-th2) by SIN_COS:80
  .= (sin(th1)*cos(th2)+cos(th1)*sin(th2))
  *(sin(th1)*cos(th2)---cos(th1)*sin(th2)) by Th9
  .= ((sin(th1)*sin(th1)*(cos(th2)*cos(th2)))
  -(cos(th1)*sin(th2)*cos(th1))*sin(th2))
  .= (sin(th1)*sin(th1)*(1-sin(th2)*sin(th2))
  -((cos(th1)*cos(th1))*sin(th2))*sin(th2)) by Th7
  .= (sin(th1)*sin(th1)*(1+-sin(th2)*sin(th2))
  -((1---sin(th1)*sin(th1))*sin(th2))*sin(th2)) by Th7
  .= (sin(th1)*sin(th1)-sin(th2)*sin(th2));
  hence thesis;
end;

theorem
   sin(th1+th2)*sin(th1-th2)= cos(th2)*cos(th2)-cos(th1)*cos(th1)
proof
   sin(th1+th2)*sin(th1-th2)
  = (sin(th1)*sin(th1)-sin(th2)*sin(th2)) by Th41
  .= ((1-cos(th1)*cos(th1))-sin(th2)*sin(th2)) by Th6
  .= ((1---cos(th1)*cos(th1))---(1-cos(th2)*cos(th2)))
  by Th6
  .= cos(th2)*cos(th2)-cos(th1)*cos(th1);
  hence thesis;
end;

theorem Th43:
  sin(th1+th2)*cos(th1-th2) = sin(th1)*cos(th1)+sin(th2)*cos(th2)
proof
   sin(th1+th2)*cos(th1-th2)
  = (sin(th1)*cos(th2)+cos(th1)*sin(th2))*cos(th1-th2) by SIN_COS:80
  .= (sin(th1)*cos(th2)+cos(th1)*sin(th2))
  *(cos(th1)*cos(th2)+sin(th1)*sin(th2)) by Th10
  .= ((sin(th1)*cos(th1)*(sin(th2)*sin(th2)+cos(th2)*cos(th2)))
  +(sin(th2)*cos(th2)*(sin(th1)*sin(th1))
  +sin(th2)*cos(th2)*(cos(th1)*cos(th1))))
  .= ((sin(th1)*cos(th1)*(1))
  +(sin(th2)*cos(th2)*(sin(th1)*sin(th1)+cos(th1)*cos(th1))))
  by SIN_COS:32
  .= ((sin(th1)*cos(th1))+(sin(th2)*cos(th2)*(1/1)))
  by SIN_COS:32
  .= sin(th1)*cos(th1)+sin(th2)*cos(th2);
  hence thesis;
end;

theorem
   cos(th1+th2)*sin(th1-th2) = sin(th1)*cos(th1)-sin(th2)*cos(th2)
proof
   cos(th1+th2)*sin(th1-th2)
  = sin(th1+(-th2))*cos(th1-(-th2))
  .= sin(th1)*cos(th1)+sin(-th2)*cos(-th2) by Th43
  .= sin(th1)*cos(th1)+sin(-th2)*cos(th2) by SIN_COS:34
  .= sin(th1)*cos(th1)+(-sin(th2))*cos(th2) by SIN_COS:34
  .= sin(th1)*cos(th1)-(sin(th2)*cos(th2));
  hence thesis;
end;

theorem Th45:
  cos(th1+th2)*cos(th1-th2)= cos(th1)*cos(th1)-sin(th2)*sin(th2)
proof
   cos(th1+th2)*cos(th1-th2)
  = (cos(th1)*cos(th2)-sin(th1)*sin(th2))*cos(th1-th2) by SIN_COS:80
  .= (cos(th1)*cos(th2)+-sin(th1)*sin(th2))
  *(cos(th1)*cos(th2)+sin(th1)*sin(th2)) by Th10
  .= (cos(th1)*cos(th1)*(cos(th2)*cos(th2))
  +cos(th1)*cos(th2)*(sin(th1)*sin(th2))
  +-((sin(th1)*sin(th2))*(cos(th1)*cos(th2)))
  +-(((sin(th1)*sin(th1))*sin(th2))*sin(th2)))
  .= (cos(th1)*cos(th1)*(1---sin(th2)*sin(th2))
  +-((sin(th1)*sin(th1))*(sin(th2)*sin(th2))))
  by Th7
  .= (cos(th1)*cos(th1)*(1)
  +((sin(th2)*sin(th2))*(-((cos(th1)*cos(th1)+sin(th1)*sin(th1))))))
  .= (cos(th1)*cos(th1)*(1)+(sin(th2)*sin(th2)*(-1)))
  by SIN_COS:32
  .= cos(th1)*cos(th1)-sin(th2)*sin(th2);
  hence thesis;
end;

theorem
   cos(th1+th2)*cos(th1-th2) = cos(th2)*cos(th2)-sin(th1)*sin(th1)
proof
   cos(th1+th2)*cos(th1-th2)
  = cos(th1)*cos(th1)-sin(th2)*sin(th2)
  by Th45
  .= (1-sin(th1)*sin(th1))-sin(th2)*sin(th2)
  by Th7
  .= (1-sin(th1)*sin(th1))-(1-cos(th2)*cos(th2))
  by Th6
  .= cos(th2)*cos(th2)-sin(th1)*sin(th1);
  hence thesis;
end;

theorem
   cos(th1)<>0 & cos(th2)<>0 implies
  sin(th1+th2)/sin(th1-th2)= (tan(th1)+tan(th2))/(tan(th1)-tan(th2))
proof
  assume A1: cos(th1)<>0 & cos(th2)<>0;
  then A2: cos(th1)*cos(th2)<>0 by XCMPLX_1:6;
   sin(th1+th2)/sin(th1-th2)
  = (sin(th1)*cos(th2)+cos(th1)*sin(th2))/sin(th1-th2)
  by SIN_COS:80
  .= (sin(th1)*cos(th2)+cos(th1)*sin(th2))
  /(sin(th1)*cos(th2)-cos(th1)*sin(th2))
  by Th9
  .= ((sin(th1)*cos(th2)+cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /((sin(th1)*cos(th2)-cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  by A2,XCMPLX_1:55
  .= ((sin(th1)*cos(th2)/(cos(th1)*cos(th2)))
  +cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /((sin(th1)*cos(th2)+(-cos(th1)*sin(th2)))/(cos(th1)*cos(th2))) by
XCMPLX_1:63
  .= ((sin(th1)*cos(th2)/(cos(th1)*cos(th2)))
  +cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /((sin(th1)*cos(th2)/(cos(th1)*cos(th2)))
  +(-cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  by XCMPLX_1:63
  .= (((sin(th1)/cos(th1))*(cos(th2)/cos(th2)))
  +cos(th1)*sin(th2)/(cos(th1)*cos(th2)))
  /((sin(th1)*cos(th2)/(cos(th1)*cos(th2)))
  +(-cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  by XCMPLX_1:77
  .= (((sin(th1)/cos(th1))*(cos(th2)/cos(th2)))
  +(cos(th1)/cos(th1))*(sin(th2)/cos(th2)))
  /((sin(th1)*cos(th2)/(cos(th1)*cos(th2)))
  +(-cos(th1)*sin(th2))/(cos(th1)*cos(th2)))
  by XCMPLX_1:77
  .= (((sin(th1)/cos(th1))*(cos(th2)/cos(th2)))
  +(cos(th1)/cos(th1))*(sin(th2)/cos(th2)))
  /((sin(th1)/cos(th1)*(cos(th2)/cos(th2)))
  +(cos(th1))*(-sin(th2))/(cos(th1)*cos(th2))) by XCMPLX_1:77
  .= (((sin(th1)/cos(th1))*(cos(th2)/cos(th2)))
  +(cos(th1)/cos(th1))*(sin(th2)/cos(th2)))
  /((sin(th1)/cos(th1)*(cos(th2)/cos(th2)))
  +(cos(th1)/cos(th1)*((-sin(th2))/cos(th2))))
  by XCMPLX_1:77
  .= (sin(th1)/cos(th1)+(cos(th1)/cos(th1))*(sin(th2)/cos(th2)))
  /((sin(th1)/cos(th1)*(cos(th2)/cos(th2)))
  +(cos(th1)/cos(th1)*((-sin(th2))/cos(th2))))
  by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)+(sin(th2)/cos(th2)))
  /((sin(th1)/cos(th1)*(cos(th2)/cos(th2)))
  +(cos(th1)/cos(th1)*((-sin(th2))/cos(th2))))
  by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)+(sin(th2)/cos(th2)))
  /(sin(th1)/cos(th1)+(cos(th1)/cos(th1)*((-sin(th2))/cos(th2))))
  by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)+(sin(th2)/cos(th2)))
  /(sin(th1)/cos(th1)+(-sin(th2))/cos(th2))
  by A1,XCMPLX_1:89
  .= (sin(th1)/cos(th1)+(sin(th2)/cos(th2)))
  /(sin(th1)/cos(th1)+-(sin(th2))/cos(th2))
  by XCMPLX_1:188
  .= (tan(th1)+(sin(th2)/cos(th2)))
  /(sin(th1)/cos(th1)+-sin(th2)/cos(th2))
  by Def1
  .= (tan(th1)+tan(th2))/(sin(th1)/cos(th1)+-sin(th2)/cos(th2))
  by Def1
  .= (tan(th1)+tan(th2))/(tan(th1)+-sin(th2)/cos(th2))
  by Def1
  .= (tan(th1)+tan(th2))/(tan(th1)+-tan(th2))
  by Def1
  .= (tan(th1)+tan(th2))/(tan(th1)-tan(th2));
  hence thesis;
 end;

theorem
   cos(th1) <> 0 & cos(th2) <> 0 implies
  cos(th1+th2)/cos(th1-th2) = (1-tan(th1)*tan(th2))/(1+tan(th1)*tan(th2))
proof
  assume cos(th1) <>0 & cos(th2) <> 0;
  then A1: cos(th1)*cos(th2) <> 0 by XCMPLX_1:6;
   cos(th1+th2)/cos(th1-th2)
  =(cos(th1)*cos(th2)-sin(th1)*sin(th2))/cos(th1-th2) by SIN_COS:80
  .=((cos(th1)*cos(th2)-sin(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /(cos(th1-th2)/(cos(th1)*cos(th2))) by A1,XCMPLX_1:55
  .=(cos(th1)*cos(th2)/(cos(th1)*cos(th2))
  -(sin(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /(cos(th1-th2)/(cos(th1)*cos(th2))) by XCMPLX_1:121
  .=(1-(sin(th1)*sin(th2))/(cos(th1)*cos(th2)))
  /(cos(th1-th2)/(cos(th1)*cos(th2))) by A1,XCMPLX_1:60
  .=(1-(sin(th1)/cos(th1))*(sin(th2)/cos(th2)))
  /(cos(th1-th2)/(cos(th1)*cos(th2))) by XCMPLX_1:77
  .=(1-tan(th1)*(sin(th2)/cos(th2)))
  /(cos(th1-th2)/(cos(th1)*cos(th2))) by Def1
  .=(1-tan(th1)*tan(th2))
  /(cos(th1-th2)/(cos(th1)*cos(th2))) by Def1
  .=(1-tan(th1)*tan(th2))
  /((cos(th1)*cos(th2)+sin(th1)*sin(th2))/(cos(th1)*cos(th2))) by Th10
  .=(1-tan(th1)*tan(th2))
  /(cos(th1)*cos(th2)/(cos(th1)*cos(th2))
  +sin(th1)*sin(th2)/(cos(th1)*cos(th2))) by XCMPLX_1:63
  .=(1-tan(th1)*tan(th2))
  /(1+sin(th1)*sin(th2)/(cos(th1)*cos(th2))) by A1,XCMPLX_1:60
  .=(1-tan(th1)*tan(th2))
  /(1+sin(th1)/cos(th1)*(sin(th2)/cos(th2))) by XCMPLX_1:77
  .=(1-tan(th1)*tan(th2))
  /(1+tan(th1)*(sin(th2)/cos(th2))) by Def1
  .=(1-tan(th1)*tan(th2))/(1+tan(th1)*tan(th2)) by Def1;
  hence thesis;
end;

theorem
   (sin(th1)+sin(th2))/(sin(th1)-sin(th2)) = tan((th1+th2)/2)*cot((th1-th2)/2)
proof
   (sin(th1)+sin(th2))/(sin(th1)-sin(th2))
  = 2*(cos((th1-th2)/2)*sin((th1+th2)/2))/(sin(th1)-sin(th2))
  by Th19
  .= 2*(cos((th1-th2)/2)*sin((th1+th2)/2))
  /(2*(cos((th1+th2)/2)*sin((th1-th2)/2)))
  by Th20
  .= (2/2)*((cos((th1-th2)/2)*sin((th1+th2)/2))
  /(cos((th1+th2)/2)*sin((th1-th2)/2)))
  by XCMPLX_1:77
  .= (sin((th1+th2)/2)/cos((th1+th2)/2))
  *(cos((th1-th2)/2)/sin((th1-th2)/2))
  by XCMPLX_1:77
  .= tan((th1+th2)/2)*(cos((th1-th2)/2)/sin((th1-th2)/2))
  by Def1
  .= tan((th1+th2)/2)*cot((th1-th2)/2)
  by Def2;
  hence thesis;
end;

theorem
   cos((th1-th2)/2)<>0 implies
  (sin(th1)+sin(th2))/(cos(th1)+cos(th2)) = tan((th1+th2)/2)
proof
  assume A1:cos((th1-th2)/2)<>0;
   (sin(th1)+sin(th2))/(cos(th1)+cos(th2))
  = 2*(cos((th1-th2)/2)*sin((th1+th2)/2))/(cos(th1)+cos(th2))
  by Th19
  .= 2*(cos((th1-th2)/2)*sin((th1+th2)/2))
  /(2*(cos((th1+th2)/2)*cos((th1-th2)/2)))
  by Th21
  .= (2/2)*((cos((th1-th2)/2)*sin((th1+th2)/2))
  /(cos((th1-th2)/2)*cos((th1+th2)/2)))
  by XCMPLX_1:77
  .= (cos((th1-th2)/2)/cos((th1-th2)/2))
  *(sin((th1+th2)/2)/cos((th1+th2)/2))
  by XCMPLX_1:77
  .= sin((th1+th2)/2)/cos((th1+th2)/2)
  by A1,XCMPLX_1:89
  .= tan((th1+th2)/2)
  by Def1;
  hence thesis;
end;

theorem
   cos((th1+th2)/2)<>0 implies
  (sin(th1)-sin(th2))/(cos(th1)+cos(th2)) = tan((th1-th2)/2)
proof
  assume A1:cos((th1+th2)/2)<>0;
   (sin(th1)-sin(th2))/(cos(th1)+cos(th2))
  = 2*(cos((th1+th2)/2)*sin((th1-th2)/2))/(cos(th1)+cos(th2))
  by Th20
  .= 2*(cos((th1+th2)/2)*sin((th1-th2)/2))
  /(2*(cos((th1+th2)/2)*cos((th1-th2)/2)))
  by Th21
  .= (2/2)*((cos((th1+th2)/2)*sin((th1-th2)/2))
  /(cos((th1+th2)/2)*cos((th1-th2)/2)))
  by XCMPLX_1:77
  .= (cos((th1+th2)/2)/cos((th1+th2)/2))*(sin((th1-th2)/2)/cos((th1-th2)/2))
  by XCMPLX_1:77
  .= sin((th1-th2)/2)/cos((th1-th2)/2)
  by A1,XCMPLX_1:89
  .= tan((th1-th2)/2)
  by Def1;
  hence thesis;
end;

theorem
   sin((th1+th2)/2)<>0 implies
  (sin(th1)+sin(th2))/(cos(th2)-cos(th1)) = cot((th1-th2)/2)
proof
  assume A1:sin((th1+th2)/2)<>0;
   (sin(th1)+sin(th2))/(cos(th2)-cos(th1))
  = 2*(cos((th1-th2)/2)*sin((th1+th2)/2))/(cos(th2)-cos(th1))
  by Th19
  .= 2*(cos((th1-th2)/2)*sin((th1+th2)/2))
  /(-2*(sin((th2+th1)/2)*sin((th2-th1)/2)))
  by Th22
  .= 2*(cos((th1-th2)/2)*sin((th1+th2)/2))
  /(2*((sin((th2+th1)/2)*(-sin((th2-th1)/2)))))
  .= 2*(cos((th1-th2)/2)*sin((th1+th2)/2))
  /(2*((sin((th2+th1)/2)*sin(-((th2-th1)/2)))))
  by SIN_COS:34
  .= (2/2)*(cos((th1-th2)/2)*sin((th1+th2)/2)
  /(sin((th2+th1)/2)*sin(((th1-th2))/2)))
  by XCMPLX_1:77
  .= (cos((th1-th2)/2)/(sin((th1-th2)/2))
  *(sin((th1+th2)/2)/sin((th2+th1)/2)))
  by XCMPLX_1:77
  .= cos((th1-th2)/2)/(sin((th1-th2)/2))
  by A1,XCMPLX_1:89
  .= cot((th1-th2)/2)
  by Def2;
  hence thesis;
end;

theorem
   sin((th1-th2)/2)<>0 implies
  (sin(th1)-sin(th2))/(cos(th2)-cos(th1)) = cot((th1+th2)/2)
proof
  assume A1:sin((th1-th2)/2)<>0;
   (sin(th1)-sin(th2))/(cos(th2)-cos(th1))
  = 2*(cos((th1+th2)/2)*sin((th1-th2)/2))/(cos(th2)-cos(th1))
  by Th20
  .= 2*(cos((th1+th2)/2)*sin((th1-th2)/2))
  /(-2*(sin((th2+th1)/2)*sin((th2-th1)/2)))
  by Th22
  .= 2*(cos((th1+th2)/2)*sin((th1-th2)/2))
  /(2*((sin((th2+th1)/2)*(-sin((th2-th1)/2)))))
  .= 2*(cos((th1+th2)/2)*sin((th1-th2)/2))
  /(2*((sin((th2+th1)/2)*sin(-((th2-th1)/2)))))
  by SIN_COS:34
  .= (2/2)*(cos((th1+th2)/2)*sin((th1-th2)/2)
  /(sin((th2+th1)/2)*sin(((th1-th2))/2)))
  by XCMPLX_1:77
  .= (cos((th1+th2)/2)/(sin((th2+th1)/2))
  *(sin((th1-th2)/2)/sin((th1-th2)/2)))
  by XCMPLX_1:77
  .= cos((th1+th2)/2)/sin((th2+th1)/2) by A1,XCMPLX_1:89
  .= cot((th1+th2)/2) by Def2;
  hence thesis;
end;

theorem
   (cos(th1)+cos(th2))/(cos(th1)-cos(th2))
  = cot((th1+th2)/2)*cot((th2-th1)/2)
proof
   (cos(th1)+cos(th2))/(cos(th1)-cos(th2))
  = 2*(cos((th1+th2)/2)*cos((th1-th2)/2))/(cos(th1)-cos(th2))
  by Th21
  .= 2*(cos((th1+th2)/2)*cos((th1-th2)/2))
  /(-2*(sin((th1+th2)/2)*sin((th1-th2)/2)))
  by Th22
  .= 2*(cos((th1+th2)/2)*cos((th1-th2)/2))
  /(2*((sin((th1+th2)/2)*(-sin((th1-th2)/2)))))
  .= 2*(cos((th1+th2)/2)*cos((th1-th2)/2))
  /(2*((sin((th1+th2)/2)*sin(-((th1-th2)/2)))))
  by SIN_COS:34
  .= (2/2)*(cos((th1+th2)/2)*cos((th1-th2)/2)
  /(sin((th1+th2)/2)*sin(((th2-th1))/2)))
  by XCMPLX_1:77
  .= (cos((th1+th2)/2)/(sin((th1+th2)/2))
   *(cos(-((th2-th1))/2)/sin((th2-th1)/2))) by XCMPLX_1:77
   .= (cos((th1+th2)/2)/(sin((th1+th2)/2))
   *(cos((th2-th1)/2)/sin((th2-th1)/2)))
   by SIN_COS:34
   .= cot((th1+th2)/2)*(cos((th2-th1)/2)/sin((th2-th1)/2))
   by Def2
   .= cot((th1+th2)/2)*cot((th2-th1)/2)
   by Def2;
   hence thesis;
 end;
