:: Matrices. Abelian Group of Matrices
::  by Katarzyna Jankowska
::
:: Received June 8, 1991
:: Copyright (c) 1991 Association of Mizar Users

environ

 vocabularies NUMBERS, NAT_1, XBOOLE_0, ALGSTR_0, FINSEQ_1, SUBSET_1, RLVECT_1,
      RELAT_1, CARD_1, FINSEQ_2, TARSKI, STRUCT_0, TREES_1, XXREAL_0, ZFMISC_1,
      FUNCT_1, QC_LANG1, INCSP_1, SUPINF_2, MESFUNC1, ARYTM_1, ARYTM_3,
      VECTSP_1, BINOP_1, MATRIX_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, NAT_1, FUNCT_1,
      FUNCT_2, FINSEQ_1, FINSEQ_2, STRUCT_0, ALGSTR_0, BINOP_1, FINSEQOP,
      RLVECT_1, VECTSP_1, XXREAL_0;
 constructors BINOP_1, XXREAL_0, FINSEQOP, VECTSP_1, RLVECT_1;
 registrations XBOOLE_0, RELAT_1, FUNCT_1, XREAL_0, FINSEQ_1, FINSEQ_2,
      STRUCT_0, VECTSP_1, ORDINAL1, CARD_1;
 requirements NUMERALS, SUBSET, BOOLE, REAL;
 definitions TARSKI, RLVECT_1, BINOP_1, FINSEQ_2, STRUCT_0, ALGSTR_0;
 theorems FINSEQ_1, FINSEQ_2, FINSEQ_3, TARSKI, FUNCOP_1, FUNCT_1, FUNCT_2,
      ZFMISC_1, NAT_1, BINOP_1, RLVECT_1, RELAT_1, ORDINAL1, CARD_1, CARD_2,
      XXREAL_0;
 schemes FINSEQ_1, BINOP_1, FINSEQ_2;

begin

reserve x,y,z for set,
  i,j,n,m for Nat,
  D for non empty set,
  K for non empty doubleLoopStr,
  s,t for FinSequence,
  a,a1,a2,b1,b2,d for Element of D,
  p, p1,p2,q,r for FinSequence of D,
  F for add-associative right_zeroed
  right_complementable Abelian (non empty doubleLoopStr);

definition
  let f be FinSequence;
  attr f is tabular means
  :Def1:
  ex n being Nat st for x st x in rng f ex s
  being FinSequence st s=x & len s = n;
end;

registration
  cluster tabular FinSequence;
  existence
  proof
    take M={},0;
    let x;
    assume x in rng M;
    hence thesis;
  end;
end;

theorem Th1:
  <*<*d*>*> is tabular
proof
  take n = 1;
  let x;
  assume
A1: x in rng <*<*d*>*>;
A2: len <*d*> = n by FINSEQ_1:56;
  rng <*<*d*>*> = {<*d*>} by FINSEQ_1:55;
  then x = <*d*> by A1,TARSKI:def 1;
  hence thesis by A2;
end;

theorem Th2:
  m |-> (n |-> x) is tabular
proof
  set s=m |-> (n |-> x);
  reconsider n1=n as Element of NAT by ORDINAL1:def 13;
  take n1;
  let z;
  assume
A1: z in rng s;
  take n|->x;
  rng s c= {n |-> x} by FUNCOP_1:19;
  hence thesis by A1,FINSEQ_1:def 18,TARSKI:def 1;
end;

theorem Th3:
  <*s*> is tabular
proof
  take len s;
  let x;
  assume x in rng <*s*>;
  then
A1: x in {s} by FINSEQ_1:55;
  then reconsider t=x as FinSequence by TARSKI:def 1;
  take t;
  thus thesis by A1,TARSKI:def 1;
end;

theorem Th4:
  for s1,s2 be FinSequence st len s1 =n & len s2 = n holds <*s1,s2
  *> is tabular
proof
  let s1,s2 be FinSequence;
  assume
A1: len s1 =n & len s2 =n;
  now
    take n;
    let x;
    assume x in rng (<*s1,s2*>);
    then
A2: x in { s1,s2} by FINSEQ_2:147;
    then reconsider r=x as FinSequence by TARSKI:def 2;
    take r;
    thus x= r & len r=n by A1,A2,TARSKI:def 2;
  end;
  hence thesis by Def1;
end;

theorem Th5:
  {} is tabular
proof
  now
    take n=0;
    let x;
    assume x in rng ({});
    hence ex t st t=x & len t=n;
  end;
  hence thesis by Def1;
end;

theorem
  <*{},{}*> is tabular by Th4,CARD_1:47;

theorem
  <*<*a1*>,<*a2*>*> is tabular
proof
  len <*a1*>=1 & len <*a2*>=1 by FINSEQ_1:56;
  hence thesis by Th4;
end;

theorem
  <*<*a1,a2*>,<*b1,b2*>*> is tabular
proof
  len <*a1,a2*>=2 & len <*b1,b2*>=2 by FINSEQ_1:61;
  hence thesis by Th4;
end;

registration
  let D be set;
  cluster tabular FinSequence of D*;
  existence
  proof
    take <*>(D*),0;
    thus thesis;
  end;
end;

definition
  let D be set;
  mode Matrix of D is tabular FinSequence of D*;
end;

registration
  let D be non empty set;
  cluster non empty-yielding Matrix of D;
  existence
  proof
    consider d be Element of D;
A1: rng <*<*d*>*>= {<*d*>} by FINSEQ_1:55;
    <*d*> in D* by FINSEQ_1:def 11;
    then rng <*<*d*>*> c= D* by A1,ZFMISC_1:37;
    then reconsider p = <*<*d*>*> as FinSequence of D* by FINSEQ_1:def 4;
    reconsider s = <*d*> as FinSequence;
    reconsider p as tabular FinSequence of D* by Th1;
    take p;
    now
      take s;
      thus s in rng p by A1,TARSKI:def 1;
      thus s <> {};
    end;
    hence thesis by RELAT_1:184;
  end;
end;

theorem Th9:
  s is Matrix of D iff ex n st for x st x in rng s ex p st x = p & len p = n
proof
  thus s is Matrix of D implies ex n st for x st x in rng s ex p st x=p & len
  p = n
  proof
    assume s is Matrix of D;
    then reconsider s as tabular FinSequence of D*;
    consider n such that
A1: for x st x in rng s ex t st t=x & len t = n by Def1;
    take n;
    for x st x in rng s ex p st x=p & len p = n
    proof
      let x;
      assume
A2:   x in rng s;
      then consider v being FinSequence such that
A3:   v=x and
A4:   len v=n by A1;
      rng s c= D* by FINSEQ_1:def 4;
      then reconsider p=v as FinSequence of D by A2,A3,FINSEQ_1:def 11;
      take p;
      thus thesis by A3,A4;
    end;
    hence thesis;
  end;
  given n such that
A5: for x st x in rng s ex p st x = p & len p = n;
A6: s is tabular
  proof
    consider n such that
A7: for x st x in rng s ex p st x = p & len p = n by A5;
    take n;
    for x st x in rng s ex t st t= x & len t= n
    proof
      let x;
      assume x in rng s;
      then consider p such that
A8:   x = p & len p = n by A7;
      reconsider t=p as FinSequence;
      take t;
      thus thesis by A8;
    end;
    hence thesis;
  end;
  rng s c= D*
  proof
    let x;
    assume x in rng s;
    then ex p st x=p & len p = n by A5;
    then reconsider q=x as FinSequence of D;
    q in D* by FINSEQ_1:def 11;
    hence thesis;
  end;
  hence thesis by A6,FINSEQ_1:def 4;
end;

definition
  let D;
  let m,n;
  canceled;
  mode Matrix of m,n,D -> Matrix of D means
    :Def3:
    len it = m & for p st p in rng it holds len p = n;
  existence
  proof
    reconsider m1=m,n1=n as Element of NAT by ORDINAL1:def 13;
    consider a;
    reconsider d= n1 |-> a as Element of D* by FINSEQ_1:def 11;
    reconsider M= m1 |-> d as FinSequence of D*;
    ex n be Element of NAT st for x st x in rng M ex p st x =p & len p = n
    proof
      reconsider p=n1 |-> a as FinSequence of D;
      take n1;
      let x;
      assume
A1:   x in rng M;
      take p;
      rng M c= {n |->a} by FUNCOP_1:19;
      hence thesis by A1,FINSEQ_1:def 18,TARSKI:def 1;
    end;
    then reconsider M as Matrix of D by Th9;
    take M;
    thus len M = m by FINSEQ_1:def 18;
    let p;
A2: rng M c= {n |-> a} by FUNCOP_1:19;
    assume p in rng M;
    then p = n |-> a by A2,TARSKI:def 1;
    hence thesis by FINSEQ_1:def 18;
  end;
end;

definition
  let D,n;
  mode Matrix of n,D is Matrix of n,n,D;
end;

definition
  let K be non empty 1-sorted;
  mode Matrix of K is Matrix of the carrier of K;
  let n;
  mode Matrix of n,K is Matrix of n,n,the carrier of K;
  let m;
  mode Matrix of n,m,K is Matrix of n,m,the carrier of K;
end;

theorem Th10:
  m |-> (n |-> a) is Matrix of m,n,D
proof
  reconsider n1=n,m1=m as Element of NAT by ORDINAL1:def 13;
  reconsider d= n1 |-> a as Element of D* by FINSEQ_1:def 11;
  reconsider M= m1 |-> d as FinSequence of D*;
  reconsider M as Matrix of D by Th2;
  M is Matrix of m,n,D
  proof
    thus len M = m by FINSEQ_1:def 18;
    let p;
A1: rng M c= {n |-> a} by FUNCOP_1:19;
    assume p in rng M;
    then p = n |-> a by A1,TARSKI:def 1;
    hence thesis by FINSEQ_1:def 18;
  end;
  hence thesis;
end;

theorem Th11:
  for p being FinSequence of D holds <*p*> is Matrix of 1,len p,D
proof
  let p be FinSequence of D;
  reconsider p9 = p as Element of (D)* by FINSEQ_1:def 11;
  <*p9*> is tabular by Th3;
  then reconsider M = <*p*> as Matrix of D;
  M is Matrix of 1,len p,D
  proof
    thus len M = 1 by FINSEQ_1:56;
    let q;
    assume q in rng M;
    then q in { p } by FINSEQ_1:55;
    hence thesis by TARSKI:def 1;
  end;
  hence thesis;
end;

theorem Th12:
  for p1,p2 st len p1 =n & len p2 = n holds <*p1,p2*> is Matrix of 2,n,D
proof
  let p1,p2;
  reconsider q1 = p1,q2 = p2 as Element of D* by FINSEQ_1:def 11;
  reconsider M = <*q1,q2*> as FinSequence of D* by FINSEQ_2:15;
  assume
A1: len p1 =n & len p2 =n;
  then reconsider M as Matrix of D by Th4;
  M is Matrix of 2,n,D
  proof
    thus len M = 2 by FINSEQ_1:61;
    let r;
    assume r in rng M;
    then r in { p1,p2 } by FINSEQ_2:147;
    hence thesis by A1,TARSKI:def 2;
  end;
  hence thesis;
end;

theorem Th13:
  {} is Matrix of 0,m,D
proof
  reconsider M =<*>(D*) as FinSequence of D*;
  reconsider M as Matrix of D by Th5;
  M is Matrix of 0,m,D
  proof
    thus len M =0;
    let p;
    assume p in rng M;
    hence thesis;
  end;
  hence thesis;
end;

theorem
  <*{}*> is Matrix of 1,0,D
proof
  len <*>D =0;
  hence thesis by Th11;
end;

theorem
  <*<*a*>*> is Matrix of 1,D
proof
  len <*a*> =1 by FINSEQ_1:56;
  hence thesis by Th11;
end;

theorem
  <*{},{}*> is Matrix of 2,0,D
proof
  len <*>D=0;
  hence thesis by Th12;
end;

theorem
  <*<*a1,a2*>*> is Matrix of 1,2,D
proof
  <*a1,a2*> is FinSequence of D & len <*a1,a2*> =2 by FINSEQ_1:61,FINSEQ_2:15;
  hence thesis by Th11;
end;

theorem
  <*<*a1*>,<*a2*>*> is Matrix of 2,1,D
proof
  len <*a1*>=1 & len <*a2*>=1 by FINSEQ_1:56;
  hence thesis by Th12;
end;

theorem
  <*<*a1,a2*>,<*b1,b2*>*> is Matrix of 2,D
proof
A1: len <*a1,a2*>=2 & len <*b1,b2*>=2 by FINSEQ_1:61;
  <*a1,a2*> is FinSequence of D & <*b1,b2*> is FinSequence of D by FINSEQ_2:15;
  hence thesis by A1,Th12;
end;

reserve M,M1,M2 for Matrix of D;

definition
  let M be tabular FinSequence;
  func width M -> Element of NAT means
  :Def4:
  ex s st s in rng M & len s = it if len M > 0 otherwise it = 0;
  existence
  proof
    thus len M > 0 implies ex n be Element of NAT,s st s in rng M & len s = n
    proof
      consider x being Element of rng M;
      consider n such that
A1:   for x st x in rng M ex s st s=x & len s = n by Def1;
      reconsider n as Element of NAT by ORDINAL1:def 13;
      assume len M > 0;
      then
A2:   rng M <>{} by CARD_1:47,RELAT_1:64;
      then consider s such that
A3:   s=x & len s = n by A1;
      take n;
      take s;
      thus thesis by A2,A3;
    end;
    thus thesis;
  end;
  uniqueness
  proof
    let n1,n2 be Element of NAT;
    thus len M > 0 implies ((ex s st s in rng M & len s = n1) & (ex s st s in
    rng M & len s = n2) implies n1 = n2)
    proof
      assume len M > 0;
      given s such that
A4:   s in rng M and
A5:   len s = n1;
      consider n such that
A6:   for x st x in rng M ex s st s=x & len s=n by Def1;
A7:   ex s1 be FinSequence st s1=s & len s1 =n by A6,A4;
      given p be FinSequence such that
A8:   p in rng M and
A9:   len p=n2;
      ex p1 be FinSequence st p1=p & len p1 = n by A6,A8;
      hence thesis by A5,A9,A7;
    end;
    thus thesis;
  end;
  consistency;
end;

theorem Th20:
  len M > 0 implies for n holds M is Matrix of len M, n, D iff n = width M
proof
  assume
A1: len M > 0;
  let n;
  thus M is Matrix of len M, n, D implies n = width M
  proof
    consider s such that
A2: s in rng M and
A3: len s=width M by A1,Def4;
    rng M c= D* by FINSEQ_1:def 4;
    then reconsider q=s as FinSequence of D by A2,FINSEQ_1:def 11;
    assume M is Matrix of len M, n, D;
    then len q= n by A2,Def3;
    hence thesis by A3;
  end;
  assume n=width M;
  then for p st p in rng M holds len p=n by A1,Def4;
  hence thesis by Def3;
end;

definition
  let M be tabular FinSequence;
  func Indices M -> set equals
  [:dom M,Seg width M:];
  coherence;
end;

definition
  let D be set;
  let M be Matrix of D;
  let i,j;
  assume
A1: [i,j] in Indices M;
  func M*(i,j) -> Element of D means
  :Def6:
  ex p being FinSequence of D st p = M.i & it = p.j;
  existence
  proof
    i in dom M by A1,ZFMISC_1:106;
    then
A2: M.i in rng M by FUNCT_1:def 5;
    rng M c= D* by FINSEQ_1:def 4;
    then reconsider p=M.i as FinSequence of D by A2,FINSEQ_1:def 11;
A3: j in Seg width M by A1,ZFMISC_1:106;
    M<> {} & len M >= 0 by A1,NAT_1:2,ZFMISC_1:106;
    then len M > 0 by XXREAL_0:1;
    then consider s such that
A4: s in rng M and
A5: len s = width M by Def4;
    consider n such that
A6: for x st x in rng M ex t st t=x & len t = n by Def1;
A7: ex v being FinSequence st p = v & len v = n by A2,A6;
    ex t st s = t & len t = n by A4,A6;
    then j in dom p by A3,A5,A7,FINSEQ_1:def 3;
    then
A8: p.j in rng p by FUNCT_1:def 5;
    rng p c=D by FINSEQ_1:def 4;
    then reconsider a=p.j as Element of D by A8;
    take a,p;
    thus thesis;
  end;
  uniqueness;
end;

theorem Th21:
  len M1 = len M2 & width M1 = width M2 & (for i,j st [i,j] in
  Indices M1 holds M1*(i,j) = M2*(i,j)) implies M1 = M2
proof
  assume that
A1: len M1 =len M2 and
A2: width M1= width M2 and
A3: for i,j st [i,j] in Indices M1 holds M1*(i,j) = M2*(i,j);
A4: dom M1= dom M2 by A1,FINSEQ_3:31;
  for k be Nat st k in dom M1 holds M1.k = M2.k
  proof
    let k be Nat;
A5: len M1 >= 0 by NAT_1:2;
    assume
A6: k in dom M1;
    then
A7: M1.k in rng M1 by FUNCT_1:def 5;
    rng M1 c= D* by FINSEQ_1:def 4;
    then reconsider p = M1.k as FinSequence of D by A7,FINSEQ_1:def 11;
    M1 <> {} by A6;
    then
A8: len M1 > 0 by A5,XXREAL_0:1;
    then M1 is Matrix of len M1,width M1,D by Th20;
    then
A9: len p = width M1 by A7,Def3;
A10: M2.k in rng M2 by A4,A6,FUNCT_1:def 5;
    rng M2 c= D* by FINSEQ_1:def 4;
    then reconsider q = M2.k as FinSequence of D by A10,FINSEQ_1:def 11;
    M2 is Matrix of len M1,width M1,D by A1,A2,A8,Th20;
    then
A11: len q = width M1 by A10,Def3;
    for l be Nat st 1 <= l & l <= len p holds p.l = q.l
    proof
      let l be Nat;
A12:  rng p c= D by FINSEQ_1:def 4;
      assume 1 <= l & l <= len p;
      then
A13:  l in Seg width M1 by A9,FINSEQ_1:3;
      then l in dom p by A9,FINSEQ_1:def 3;
      then p.l in rng p by FUNCT_1:def 5;
      then reconsider a = p.l as Element of D by A12;
A14:  rng q c= D by FINSEQ_1:def 4;
      l in dom q by A11,A13,FINSEQ_1:def 3;
      then q.l in rng q by FUNCT_1:def 5;
      then reconsider b = q.l as Element of D by A14;
      [k,l] in Indices M2 by A2,A4,A6,A13,ZFMISC_1:106;
      then
A15:  M2*(k,l)= b by Def6;
      [k,l] in Indices M1 by A6,A13,ZFMISC_1:106;
      then
A16:  M1*(k,l)=a by Def6;
      [k,l] in [:dom M1,Seg width M1:] by A6,A13,ZFMISC_1:106;
      hence thesis by A3,A16,A15;
    end;
    hence thesis by A9,A11,FINSEQ_1:18;
  end;
  hence thesis by A4,FINSEQ_1:17;
end;

scheme
  MatrixLambda { D() -> non empty set, n, m() -> Element of NAT, f(set,set) ->
  Element of D()} : ex M being Matrix of n(),m(),D() st for i,j st [i,j] in
  Indices M holds M*(i,j) = f(i,j) proof
  defpred P[set,set] means ex p being FinSequence st $2=p & len p = m() & for
  i st i in Seg m() holds p.i = f($1,i);
A1: n() >= 0 by NAT_1:2;
A2: for k be Nat st k in Seg n() ex y st P[k,y]
  proof
    let k be Nat;
    assume k in Seg n();
    deffunc F(Nat) = f(k,$1);
    consider p being FinSequence such that
A3: len p =m() & for i be Nat st i in dom p holds p.i = F(i) from
    FINSEQ_1:sch 2;
    take p,p;
    dom p = Seg m() by A3,FINSEQ_1:def 3;
    hence thesis by A3;
  end;
  consider M being FinSequence such that
A4: dom M = Seg n() and
A5: for k be Nat st k in Seg n() holds P[k,M.k] from FINSEQ_1:sch 1(A2);
A6: len M = n() by A4,FINSEQ_1:def 3;
  rng M c=D()*
  proof
    let x;
    assume x in rng M;
    then consider i be Nat such that
A7: i in dom M and
A8: M.i=x by FINSEQ_2:11;
    consider p being FinSequence such that
A9: M.i=p and
A10: len p = m() & for j st j in Seg m() holds p.j = f(i,j) by A4,A5,A7;
    rng p c= D()
    proof
      let z;
      assume z in rng p;
      then consider k be Nat such that
A11:  k in dom p and
A12:  p.k =z by FINSEQ_2:11;
      k in Seg len p by A11,FINSEQ_1:def 3;
      then z = f(i,k) by A10,A12;
      hence thesis;
    end;
    then p is FinSequence of D() by FINSEQ_1:def 4;
    hence thesis by A8,A9,FINSEQ_1:def 11;
  end;
  then reconsider M as FinSequence of D()* by FINSEQ_1:def 4;
  ex n st for x st x in rng M ex p being FinSequence of D() st x=p & len p = n
  proof
    take m();
    let x;
    assume x in rng M;
    then consider i be Nat such that
A13: i in dom M & M.i =x by FINSEQ_2:11;
    consider p being FinSequence such that
A14: x=p and
A15: len p = m() and
A16: for j st j in Seg m() holds p.j = f(i,j) by A4,A5,A13;
    rng p c= D()
    proof
      let z;
      assume z in rng p;
      then consider k be Nat such that
A17:  k in dom p and
A18:  p.k =z by FINSEQ_2:11;
      k in Seg len p by A17,FINSEQ_1:def 3;
      then z = f(i,k) by A15,A16,A18;
      hence thesis;
    end;
    then reconsider p as FinSequence of D() by FINSEQ_1:def 4;
    take p;
    thus thesis by A14,A15;
  end;
  then reconsider M as Matrix of D() by Th9;
  for p being FinSequence of D() st p in rng M holds len p= m()
  proof
    let p be FinSequence of D();
    assume p in rng M;
    then consider i be Nat such that
A19: i in dom M & M.i =p by FINSEQ_2:11;
    P[i,p] by A4,A5,A19;
    hence thesis;
  end;
  then reconsider M as Matrix of n(),m(),D() by A6,Def3;
  take M;
  let i,j;
  assume
A20: [i,j] in Indices M;
  then n() <> 0 by A4,ZFMISC_1:106;
  then
A21: n() > 0 by A1,XXREAL_0:1;
  i in Seg n() by A4,A20,ZFMISC_1:106;
  then
A22: ex q being FinSequence st M.i=q & len q = m() & for j st j in Seg m()
  holds q.j = f(i,j) by A5;
  j in Seg width M by A20,ZFMISC_1:106;
  then
A23: j in Seg m() by A6,A21,Th20;
  ex p being FinSequence of D() st p=M.i & M*(i,j)=p.j by A20,Def6;
  hence thesis by A22,A23;
end;

scheme
  MatrixEx { D() -> non empty set, n, m() -> Element of NAT, P[set,set,set] }
: ex M being Matrix of n(),m(),D() st for i,j st [i,j] in Indices M holds P[i,j
  ,M*(i,j)]
provided
for i,j st [i,j] in [:Seg n(), Seg m():] for x1,x2 being Element of D()
st P[i,j,x1] & P[i,j,x2] holds x1 = x2 and
A1: for i,j st [i,j] in [:Seg n(), Seg m():] ex x being Element of D()
st P[i,j,x]
proof
  defpred Q[set,set,set] means P[$1,$2,$3] & $3 in D();
A2: for x,y st x in Seg n() & y in Seg m() ex z st z in D() & Q[x,y,z]
  proof
    let x,y;
    assume that
A3: x in Seg n() and
A4: y in Seg m();
    reconsider y9=y as Element of NAT by A4;
    reconsider x9=x as Element of NAT by A3;
    [x9,y9] in [:Seg n(),Seg m():] by A3,A4,ZFMISC_1:106;
    then consider z9 being Element of D() such that
A5: P[x9,y9,z9] by A1;
    take z9;
    thus thesis by A5;
  end;
  consider f being Function of [:Seg n(),Seg m():],D() such that
A6: for x,y st x in Seg n() & y in Seg m() holds Q[x,y,f.(x,y)] from
  BINOP_1:sch 1(A2);
A7: n() >= 0 by NAT_1:2;
  defpred R[set,set] means ex p being FinSequence st $2=p & len p = m() & for
  i st i in Seg m() holds p.i = f.($1,i);
A8: for k be Nat st k in Seg n() ex y st R[k,y]
  proof
    let k be Nat;
    assume k in Seg n();
    deffunc F(Nat) = f.[k,$1];
    consider p being FinSequence such that
A9: len p =m() & for i be Nat st i in dom p holds p.i = F(i) from
    FINSEQ_1:sch 2;
    take p,p;
    dom p = Seg m() by A9,FINSEQ_1:def 3;
    hence thesis by A9;
  end;
  consider M being FinSequence such that
A10: dom M = Seg n() and
A11: for k be Nat st k in Seg n() holds R[k,M.k] from FINSEQ_1:sch 1(A8);
A12: len M = n() by A10,FINSEQ_1:def 3;
  rng M c=D()*
  proof
    let x;
    assume x in rng M;
    then consider i be Nat such that
A13: i in Seg n() and
A14: M.i=x by A10,FINSEQ_2:11;
    consider p being FinSequence such that
A15: M.i=p and
A16: len p = m() and
A17: for j st j in Seg m() holds p.j = f.(i,j) by A11,A13;
    rng p c= D()
    proof
      let z;
      assume z in rng p;
      then consider k be Nat such that
A18:  k in dom p and
A19:  p.k =z by FINSEQ_2:11;
A20:  k in Seg len p by A18,FINSEQ_1:def 3;
      then
A21:  [i,k] in [:Seg n(),Seg m():] by A13,A16,ZFMISC_1:106;
      z = f.(i,k) by A16,A17,A19,A20;
      hence thesis by A21,FUNCT_2:7;
    end;
    then p is FinSequence of D() by FINSEQ_1:def 4;
    hence thesis by A14,A15,FINSEQ_1:def 11;
  end;
  then reconsider M as FinSequence of (D())* by FINSEQ_1:def 4;
  ex n st for x st x in rng M ex p being FinSequence of D() st x=p & len p = n
  proof
    take m();
    let x;
    assume x in rng M;
    then consider i be Nat such that
A22: i in dom M and
A23: M.i = x by FINSEQ_2:11;
    consider p being FinSequence such that
A24: x=p and
A25: len p = m() and
A26: for j st j in Seg m() holds p.j =f.(i,j) by A10,A11,A22,A23;
    rng p c= D()
    proof
      let z;
      assume z in rng p;
      then consider k be Nat such that
A27:  k in dom p and
A28:  p.k =z by FINSEQ_2:11;
A29:  k in Seg len p by A27,FINSEQ_1:def 3;
      then
A30:  [i,k] in [:Seg n(),Seg m():] by A10,A22,A25,ZFMISC_1:106;
      z = f.(i,k) by A25,A26,A28,A29;
      hence thesis by A30,FUNCT_2:7;
    end;
    then reconsider p as FinSequence of D() by FINSEQ_1:def 4;
    take p;
    thus thesis by A24,A25;
  end;
  then reconsider M as Matrix of D() by Th9;
  for p being FinSequence of D() st p in rng M holds len p= m()
  proof
    let p be FinSequence of D();
    assume p in rng M;
    then consider i be Nat such that
A31: i in dom M & M.i = p by FINSEQ_2:11;
    R[i,p] by A10,A11,A31;
    hence thesis;
  end;
  then reconsider M as Matrix of n(),m(),D() by A12,Def3;
  take M;
  let i,j;
  assume
A32: [i,j] in Indices M;
  then n() <> 0 by A10,ZFMISC_1:106;
  then
A33: n() > 0 by A7,XXREAL_0:1;
  j in Seg width M by A32,ZFMISC_1:106;
  then
A34: j in Seg m() by A12,A33,Th20;
A35: ex p being FinSequence of D() st p=M.i & M*(i,j)=p.j by A32,Def6;
A36: i in Seg n() by A10,A32,ZFMISC_1:106;
  then ex q being FinSequence st M.i=q & len q = m() & for j st j in Seg m()
  holds q.j = f.(i,j) by A11;
  then f.(i,j)=M*(i,j) by A35,A34;
  hence thesis by A6,A36,A34;
end;

canceled;

theorem
  for M being Matrix of 0,m,D holds len M = 0 & width M = 0 & Indices M = {}
proof
  let M be Matrix of 0,m,D;
  len M = 0 by Def3;
  then width M = 0 by Def4;
  then Seg width M = 0;
  hence thesis by Def3,ZFMISC_1:113;
end;

theorem Th24:
  n > 0 implies for M being Matrix of n,m,D holds len M = n &
  width M = m & Indices M = [:Seg n, Seg m:]
proof
  assume
A1: n > 0;
  let M be Matrix of n,m,D;
  Seg len M = dom M & len M = n by Def3,FINSEQ_1:def 3;
  hence thesis by A1,Th20;
end;

theorem Th25:
  for M being Matrix of n,D holds len M = n & width M = n &
  Indices M = [:Seg n, Seg n:]
proof
  let M be Matrix of n,D;
A1: len M = n by Def3;
A2: 0 <= n by NAT_1:2;
A3: now
    per cases by A2,XXREAL_0:1;
    case
      n =0;
      hence width M = 0 by A1,Def4;
    end;
    case
      n > 0;
      hence width M= n by A1,Th20;
    end;
  end;
  Seg len M = dom M by FINSEQ_1:def 3;
  hence thesis by A3,Def3;
end;

theorem Th26:
  for M being Matrix of n,m,D holds len M = n & Indices M = [:Seg
  n,Seg width M:]
proof
  let M be Matrix of n,m,D;
  len M = n by Def3;
  hence thesis by FINSEQ_1:def 3;
end;

theorem Th27:
  for M1,M2 being Matrix of n,m,D holds Indices M1 = Indices M2
proof
  let M1,M2 be Matrix of n,m,D;
A1: len M1 = n by Def3;
A2: len M2 = n by Def3;
A3: 0 <= n by NAT_1:2;
A4: now
    per cases by A3,XXREAL_0:1;
    suppose
A5:   n = 0;
      then width M1 = 0 by A1,Def4;
      hence width M1= width M2 by A2,A5,Def4;
    end;
    suppose
A6:   n > 0;
      then width M1= m by A1,Th20;
      hence width M1 = width M2 by A2,A6,Th20;
    end;
  end;
  dom M1 = Seg n by A1,FINSEQ_1:def 3;
  hence thesis by A2,A4,FINSEQ_1:def 3;
end;

theorem Th28:
  for M1,M2 being Matrix of n,m,D st (for i,j st [i,j] in Indices
  M1 holds M1*(i,j) = M2*(i,j)) holds M1 = M2
proof
  let M1,M2 be Matrix of n,m,D;
A1: len M1=n by Th26;
A2: len M2=n by Th26;
A3: 0 <= n by NAT_1:2;
A4: now
    per cases by A3,XXREAL_0:1;
    suppose
A5:   n =0;
      then width M1 = 0 by A1,Def4;
      hence width M1= width M2 by A2,A5,Def4;
    end;
    suppose
A6:   n > 0;
      then width M1= m by A1,Th20;
      hence width M1 = width M2 by A2,A6,Th20;
    end;
  end;
  assume for i,j st [i,j] in Indices M1 holds M1*(i,j) = M2*(i,j);
  hence thesis by A1,A2,A4,Th21;
end;

theorem
  for M1 being Matrix of n,D holds for i,j st [i,j] in Indices M1 holds
  [j,i] in Indices M1
proof
  let M1 be Matrix of n,D;
  let i,j;
  assume [i,j] in Indices M1;
  then [i,j] in [:Seg n,Seg n:] by Th25;
  then
A1: j in Seg n & i in Seg n by ZFMISC_1:106;
  Indices M1 =[:Seg n,Seg n:] by Th25;
  hence thesis by A1,ZFMISC_1:106;
end;

definition
  let D;
  let M be Matrix of D;
  func M@ -> Matrix of D means
  :Def7:
  len it = width M & (for i,j holds [i,j]
in Indices it iff [j,i] in Indices M) & for i,j st [j,i] in Indices M holds it*
  (i,j) = M*(j,i);
  existence
  proof
    now
      per cases by NAT_1:3;
      suppose
A1:     width M > 0;
        deffunc F(Nat,Nat) = M*($2,$1);
        consider M1 being Matrix of width M,len M,D such that
A2:     for i,j st [i,j] in Indices M1 holds M1*(i,j) = F(i,j) from
        MatrixLambda;
        reconsider M9 = M1 as Matrix of D;
        Seg len M = dom M by FINSEQ_1:def 3;
        then
A3:     Indices M1=[:Seg width M,dom M:] by A1,Th24;
        thus thesis
        proof
          take M9;
          thus len M9 = width M by A1,Th24;
          thus for i,j holds [i,j] in Indices M9 iff [j,i] in Indices M by A3,
ZFMISC_1:107;
          let i,j;
          assume [j,i] in Indices M;
          then [i,j] in Indices M9 by A3,ZFMISC_1:107;
          hence thesis by A2;
        end;
      end;
      suppose
A4:     width M =0;
        reconsider M1 = {} as Matrix of D by Th13;
        thus thesis
        proof
          take M1;
X:        Indices M1=[:{},Seg width M1:];
          Seg width M = {} by A4;
          hence thesis by A4,X,ZFMISC_1:113;
        end;
      end;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let M1,M2 be Matrix of D;
    assume that
A5: len M1 = width M and
A6: for i,j holds [i,j] in Indices M1 iff [j,i] in Indices M and
A7: for i,j st [j,i] in Indices M holds M1*(i,j)=M*(j,i) and
A8: len M2 = width M and
A9: for i,j holds [i,j] in Indices M2 iff [j,i] in Indices M and
A10: for i,j st [j,i] in Indices M holds M2*(i,j)=M*(j,i);
A11: now
      let i,j;
      assume [i,j] in Indices M1;
      then
A12:  [j,i] in Indices M by A6;
      then M1*(i,j)=M*(j,i) by A7;
      hence M1*(i,j)=M2*(i,j) by A10,A12;
    end;
    now
      let x,y;
      thus [x,y] in [:dom M1,Seg width M1:] implies [x,y] in [:dom M2,Seg
      width M2:]
      proof
        assume
A13:    [x,y] in [:dom M1,Seg width M1:];
        then x in dom M1 by ZFMISC_1:106;
        then reconsider i=x as Element of NAT;
        y in Seg width M1 by A13,ZFMISC_1:106;
        then reconsider j=y as Element of NAT;
        [j,i] in Indices M by A6,A13;
        hence thesis by A9;
      end;
      thus [x,y] in [:dom M2,Seg width M2:] implies [x,y] in [:dom M1,Seg
      width M1:]
      proof
        assume
A14:    [x,y] in [:dom M2,Seg width M2:];
        then x in dom M2 by ZFMISC_1:106;
        then reconsider i=x as Element of NAT;
        y in Seg width M2 by A14,ZFMISC_1:106;
        then reconsider j=y as Element of NAT;
        [j,i] in Indices M by A9,A14;
        hence thesis by A6;
      end;
    end;
    then
A15: [:dom M1,Seg width M1:] = [:dom M2,Seg width M2:] by ZFMISC_1:108;
A16: now
      assume
A17:  len M1 <> 0;
      then Seg len M2 <> {} by A5,A8;
      then
A18:  dom M2 <> {} by FINSEQ_1:def 3;
      now
        per cases;
        suppose
A19:      Seg width M1 <> {};
          Seg len M1 <> {} by A17;
          then dom M1 <> {} by FINSEQ_1:def 3;
          then Seg width M1 = Seg width M2 by A15,A19,ZFMISC_1:134;
          hence width M1 = width M2 by FINSEQ_1:8;
        end;
        suppose
A20:      Seg width M1 = {};
          then [:dom M2,Seg width M2:]= {} by A15,ZFMISC_1:113;
          then Seg width M2 = {} by A18,ZFMISC_1:113;
          hence width M1=width M2 by A20,FINSEQ_1:8;
        end;
      end;
      hence len M1=len M2 & width M1 =width M2 by A5,A8;
    end;
    len M1 = 0 implies len M2= 0 & width M1 = 0 & width M2 = 0 by A5,A8,Def4;
    hence thesis by A16,A11,Th21;
  end;
end;

Lm1: for M being Matrix of D st width M > 0 holds len (M@)=width M & width (M@
)=len M
proof
  let M be Matrix of D;
  assume
A1: width M>0;
  thus len (M@)=width M by Def7;
A2: len M >= 0 by NAT_1:2;
  per cases by A2,XXREAL_0:1;
  suppose
    len M=0;
    hence thesis by A1,Def4;
  end;
  suppose
A3: len M>0;
    for i,j being set holds [i,j]in [:dom (M@),Seg width (M@):] iff [i,j]
    in [:Seg width M,dom M:]
    proof
      let i,j be set;
      thus [i,j] in [:dom (M@),Seg width (M@):] implies [i,j] in [:Seg width M
      ,dom M:]
      proof
        assume
A4:     [i,j]in [:dom (M@),Seg width (M@):];
        then i in dom (M@) & j in Seg width (M@) by ZFMISC_1:106;
        then reconsider i,j as Element of NAT;
        [i,j] in Indices (M@) by A4;
        then [j,i] in Indices M by Def7;
        hence thesis by ZFMISC_1:107;
      end;
      assume
A5:   [i,j] in [:Seg width M,dom M:];
      then i in Seg width M & j in dom M by ZFMISC_1:106;
      then reconsider i,j as Element of NAT;
      [j,i] in Indices M by A5,ZFMISC_1:107;
      then [i,j] in Indices (M@) by Def7;
      hence thesis;
    end;
    then
    dom M = Seg len M & [:Seg width M,dom M:]=[:dom (M@),Seg width (M@):]
    by FINSEQ_1:def 3,ZFMISC_1:108;
    then dom M=Seg width (M@) by A1,A3,ZFMISC_1:134;
    hence thesis by FINSEQ_1:def 3;
  end;
end;

Lm2: for n be Element of NAT, K being non empty set, A being Matrix of n,K
holds A@ is Matrix of n,K
proof
  let n be Element of NAT,K be non empty set, A be Matrix of n,K;
A1: len A = n by Def3;
A2: for p being FinSequence of K st p in rng (A@) holds len p = n
  proof
    let p be FinSequence of K;
    consider n2 being Nat such that
A3: for x being set st x in rng (A@) ex s being FinSequence st s=x &
    len s = n2 by Def1;
A4: for p2 being FinSequence of K st p2 in rng (A@) holds len p2 = n2
    proof
      let p2 be FinSequence of K;
      assume p2 in rng (A@);
      then ex s being FinSequence st s=p2 & len s = n2 by A3;
      hence thesis;
    end;
    assume
A5: p in rng (A@);
    then
A6: ex s being FinSequence st s=p & len s = n2 by A3;
    now
      per cases;
      case
A7:     n>0;
        then
A8:     width A=n by A1,Th20;
        then
A9:     width (A@)=len A by A7,Lm1;
A10:    len (A@)=width A by A7,A8,Lm1;
        then A@ is Matrix of n,n2,K by A4,A8,Def3;
        hence thesis by A1,A6,A7,A8,A10,A9,Th20;
      end;
      case
A11:    n<=0;
A12:    len (A@) = width A & ex x0 being set st x0 in dom (A@) & p=(A@).
        x0 by A5,Def7,FUNCT_1:def 5;
        width A=0 by A1,A11,Def4;
        then Seg width A = {};
        hence thesis by A12,FINSEQ_1:def 3;
      end;
    end;
    hence thesis;
  end;
A13: len (A@)=width A by Def7;
  now
A14: n >= 0 by NAT_1:2;
    per cases by A14,XXREAL_0:1;
    case
      n>0;
      hence len (A@)=n by A13,A1,Th20;
    end;
    case
      n=0;
      hence len (A@)=n by A13,A1,Def4;
    end;
  end;
  hence thesis by A2,Def3;
end;

definition
  let n,D;
  let M be Matrix of n,D;
  redefine func M@ -> Matrix of n,D;
  coherence
  proof
    n in NAT by ORDINAL1:def 13;
    hence thesis by Lm2;
  end;
end;

definition
  let D,M,i;
  func Line(M,i) -> FinSequence of D means
  :Def8:
  len it = width M & for j st j in Seg width M holds it.j = M*(i,j);
  existence
  proof
    deffunc F(Nat) = M*(i,$1);
    consider f being FinSequence of D such that
A1: len f = width M and
A2: for j be Nat st j in dom f holds f.j = F(j) from FINSEQ_2:sch 1;
    take f;
    thus len f = width M by A1;
    let j;
    dom f = Seg width M by A1,FINSEQ_1:def 3;
    hence thesis by A2;
  end;
  uniqueness
  proof
    let p1,p2 be FinSequence of D;
    assume that
A3: len p1=width M and
A4: for j st j in Seg width M holds p1.j = M*(i,j) and
A5: len p2=width M and
A6: for j st j in Seg width M holds p2.j = M*(i,j);
A7: dom p1 = Seg width M by A3,FINSEQ_1:def 3;
    for j be Nat st j in dom p1 holds p1.j=p2.j
    proof
      let j be Nat;
      assume
A8:   j in dom p1;
      then p1.j = M*(i,j) by A4,A7;
      hence thesis by A6,A7,A8;
    end;
    hence thesis by A3,A5,FINSEQ_2:10;
  end;
  func Col(M,i) -> FinSequence of D means
  :Def9:
  len it = len M & for j st j in dom M holds it.j = M*(j,i);
  existence
  proof
    deffunc F(Nat) = M*($1,i);
    consider z being FinSequence of D such that
A9: len z = len M and
A10: for j be Nat st j in dom z holds z.j = F(j) from FINSEQ_2:sch 1;
    take z;
    thus len z = len M by A9;
    let j;
A11: Seg len M = dom M by FINSEQ_1:def 3;
    dom z = Seg len M by A9,FINSEQ_1:def 3;
    hence thesis by A10,A11;
  end;
  uniqueness
  proof
    let p1,p2 be FinSequence of D;
    assume that
A12: len p1 = len M and
A13: for j st j in dom M holds p1.j = M*(j,i) and
A14: len p2= len M and
A15: for j st j in dom M holds p2.j = M*(j,i);
A16: dom p1 = Seg len M by A12,FINSEQ_1:def 3;
    for j be Nat st j in dom p1 holds p1.j=p2.j
    proof
      let j be Nat;
      assume j in dom p1;
      then
A17:  j in dom M by A16,FINSEQ_1:def 3;
      then p1.j = M*(j,i) by A13;
      hence thesis by A15,A17;
    end;
    hence thesis by A12,A14,FINSEQ_2:10;
  end;
end;

definition
  let D;
  let M be Matrix of D;
  let i;
  redefine func Line(M,i) -> Element of (width M)-tuples_on D;
  coherence
  proof
    len Line(M,i) = width M & Line(M,i) is Element of D* by Def8,
FINSEQ_1:def 11;
    then Line(M,i) in { p where p is Element of D*: len p = width M };
    hence thesis;
  end;
  redefine func Col(M,i) -> Element of (len M)-tuples_on D;
  coherence
  proof
    len Col(M,i) = len M & Col(M,i) is Element of D* by Def9,FINSEQ_1:def 11;
    then Col(M,i) in { p where p is Element of D*: len p = len M };
    hence thesis;
  end;
end;

reserve A,B for Matrix of n,K;

definition
  let K,n;
  func n-Matrices_over K -> set equals
  n-tuples_on (n-tuples_on the carrier of
  K);
  coherence;
  func 0.(K,n) -> Matrix of n,K equals
  n |-> (n |-> 0.K);
  coherence by Th10;
  func 1.(K,n) -> Matrix of n,K means
  :Def12:
  (for i st [i,i] in Indices it
holds it*(i,i) = 1.K) & for i,j st [i,j] in Indices it & i <> j holds it*(i,j)
  = 0.K;
  existence
  proof
    defpred P[set,set,set] means ($1 = $2 implies $3 = 1.K) & ($1 <> $2
    implies $3 = 0.K);
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
A1: for i,j st [i,j] in [:Seg n1, Seg n1:] ex x being Element of K st P[i, j,x]
    proof
      let i,j;
      assume [i,j] in [:Seg n1, Seg n1:];
      i = j implies P[i,j,1.K];
      hence thesis;
    end;
A2: for i,j st [i,j] in [:Seg n1, Seg n1:] for x1,x2 being Element of K st
    P[i,j,x1] & P[i,j,x2] holds x1 = x2;
    consider M being Matrix of n1,K such that
A3: for i,j st [i,j] in Indices M holds P[i,j,M*(i,j)] from MatrixEx(
    A2, A1);
    reconsider M as Matrix of n,K;
    take M;
    thus thesis by A3;
  end;
  uniqueness
  proof
    let M1,M2 be Matrix of n,K;
    assume that
A4: for i st [i,i] in Indices M1 holds M1*(i,i) = 1.K and
A5: for i,j st [i,j] in Indices M1 & i <> j holds M1*(i,j) = 0.K and
A6: for i st [i,i] in Indices M2 holds M2*(i,i) = 1.K and
A7: for i,j st [i,j] in Indices M2 & i <> j holds M2*(i,j) = 0.K;
A8: Indices M1 =Indices M2 by Th27;
A9: now
      let i,j;
      assume
A10:  [i,j] in Indices M1;
A11:  now
        assume
A12:    i<>j;
        then M1*(i,j)=0.K by A5,A10;
        hence M1*(i,j)=M2*(i,j) by A8,A7,A10,A12;
      end;
      now
        assume
A13:    i=j;
        then M1*(i,j)=1.K by A4,A10;
        hence M1*(i,j)=M2*(i,j) by A8,A6,A10,A13;
      end;
      hence M1*(i,j)=M2*(i,j) by A11;
    end;
A14: len M2=n & width M2=n by Th25;
    len M1=n & width M1=n by Th25;
    hence thesis by A9,A14,Th21;
  end;
  let A;
  func -A -> Matrix of n,K means
  :Def13:
  for i,j holds [i,j] in Indices A implies it*(i,j) = -(A*(i,j));
  existence
  proof
    deffunc F(Nat,Nat) = -(A*($1,$2));
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
    consider M being Matrix of n1,K such that
A15: for i,j st [i,j] in Indices M holds M*(i,j) = F(i,j) from
    MatrixLambda;
    Indices A=[:Seg n,Seg n:] by Th25;
    then
A16: Indices A = Indices M by Th25;
    reconsider M as Matrix of n,K;
    take M;
    thus thesis by A15,A16;
  end;
  uniqueness
  proof
    let M1,M2 be Matrix of n,K;
    assume that
A17: for i,j st [i,j] in Indices A holds M1*(i,j) =-(A*(i,j)) and
A18: for i,j st [i,j] in Indices A holds M2*(i,j) =-(A*(i,j));
A19: now
      let i,j;
      assume
A20:  [i,j] in Indices A;
      then M1*(i,j) = -(A*(i,j)) by A17;
      hence M1*(i,j)=M2*(i,j) by A18,A20;
    end;
    Indices M1=[:Seg n,Seg n:] by Th25;
    then Indices A = Indices M1 by Th25;
    hence thesis by A19,Th28;
  end;
  let B;
  func A+B -> Matrix of n,K means
  :Def14:
  for i,j holds [i,j] in Indices A implies it*(i,j) = A*(i,j) + B*(i,j);
  existence
  proof
    deffunc F(Nat,Nat) = A*($1,$2) + B*($1,$2);
    reconsider n1=n as Element of NAT by ORDINAL1:def 13;
    consider M being Matrix of n1,K such that
A21: for i,j st [i,j] in Indices M holds M*(i,j)= F(i,j) from
    MatrixLambda;
    Indices A=[:Seg n,Seg n:] by Th25;
    then
A22: Indices A = Indices M by Th25;
    reconsider M as Matrix of n,K;
    take M;
    thus thesis by A21,A22;
  end;
  uniqueness
  proof
    let M1,M2 be Matrix of n,K;
    assume that
A23: for i,j st [i,j] in Indices A holds M1*(i,j)=A*(i,j) + B*(i,j) and
A24: for i,j st [i,j] in Indices A holds M2*(i,j) = A*(i,j) + B*(i,j);
A25: now
      let i,j;
      assume
A26:  [i,j] in Indices A;
      then M1*(i,j)=(A*(i,j) + B*(i,j)) by A23;
      hence M1*(i,j)=M2*(i,j) by A24,A26;
    end;
    Indices M1=[:Seg n,Seg n:] by Th25;
    then Indices A = Indices M1 by Th25;
    hence thesis by A25,Th28;
  end;
end;

registration
  let K,n;
  cluster n-Matrices_over K -> non empty;
  coherence;
end;

theorem Th30:
  [i,j] in Indices (0.(K,n)) implies (0.(K,n))*(i,j)= 0.K
proof
  reconsider n1=n as Element of NAT by ORDINAL1:def 13;
  set M = 0.(K,n);
  assume
A1: [i,j] in Indices M;
  then
A2: [i,j] in [:Seg n,Seg n:] by Th25;
  then j in Seg n by ZFMISC_1:106;
  then
A3: (n1 |-> 0.K).j= 0.K by FUNCOP_1:13;
  i in Seg n by A2,ZFMISC_1:106;
  then M.i= n1 |-> 0.K by FUNCOP_1:13;
  hence thesis by A1,A3,Def6;
end;

theorem Th31:
  x is Element of n-Matrices_over K iff x is Matrix of n,K
proof
  thus x is Element of (n-Matrices_over K) implies x is Matrix of n,K
  proof
    assume x is Element of (n-Matrices_over K);
    then reconsider
    x as Element of n-tuples_on (n-tuples_on the carrier of K);
A1: len x=n by FINSEQ_1:def 18;
    ex m st for y st y in rng x ex q being FinSequence of K st y=q & len q =m
    proof
      take n;
      let y;
A2:   rng x c= (n-tuples_on the carrier of K) by FINSEQ_1:def 4;
      assume y in rng x;
      then reconsider q=y as Element of n-tuples_on the carrier of K by A2;
      reconsider q as FinSequence of K;
      take q;
      thus thesis by FINSEQ_1:def 18;
    end;
    then reconsider x as Matrix of the carrier of K by Th9;
    for q be FinSequence of K st q in rng x holds len q = n
    proof
      let q be FinSequence of K;
A3:   rng x c= n-tuples_on the carrier of K by FINSEQ_1:def 4;
      assume q in rng x;
      then reconsider q as Element of n-tuples_on the carrier of K by A3;
      len q=n by FINSEQ_1:def 18;
      hence thesis;
    end;
    hence thesis by A1,Def3;
  end;
  assume x is Matrix of n,K;
  then reconsider x as Matrix of n,K;
A4: len x = n by Def3;
  then reconsider x as Element of n-tuples_on ((the carrier of K)*) by
FINSEQ_2:110;
  rng x c= n-tuples_on the carrier of K
  proof
    let y;
    assume
A5: y in rng x;
    rng x c= (the carrier of K)* by FINSEQ_1:def 4;
    then reconsider p=y as FinSequence of K by A5,FINSEQ_1:def 11;
    len p =n by A5,Def3;
    then p is Element of n-tuples_on the carrier of K by FINSEQ_2:110;
    hence thesis;
  end;
  then x is FinSequence of n-tuples_on the carrier of K by FINSEQ_1:def 4;
  hence thesis by A4,FINSEQ_2:110;
end;

definition
  let K be non empty ZeroStr;
  let M be Matrix of K;
  attr M is diagonal means
  for i,j st [i,j] in Indices M & M*(i,j) <> 0.K holds i=j;
end;

registration
  let n, K;
  cluster diagonal Matrix of n,K;
  existence
  proof
    take 1.(K,n);
    let i,j;
    thus thesis by Def12;
  end;
end;

definition
  let K,n;
  mode Diagonal of n,K is diagonal Matrix of n,K;
end;

reserve A,A9,B,B9,C for Matrix of n,F;

theorem Th32:
  A + B = B + A
proof
A1: Indices A = Indices (A + B) by Th27;
A2: Indices A= Indices B by Th27;
  now
    let i,j;
    assume
A3: [i,j] in Indices (A + B);
    hence (A + B)*(i,j)=A*(i,j) + B*(i,j) by A1,Def14
      .=(B + A)*(i,j) by A2,A1,A3,Def14;
  end;
  hence thesis by Th28;
end;

theorem Th33:
  (A + B) + C = A + (B + C)
proof
A1: Indices A= Indices ((A + B) +C) by Th27;
A2: Indices A= Indices (A + B) by Th27;
A3: Indices A= Indices B by Th27;
  now
    let i,j;
    assume
A4: [i,j] in Indices ((A + B) + C);
    hence ((A + B)+C)*(i,j)=(A+B)*(i,j) + C*(i,j) by A1,A2,Def14
      .=(A*(i,j) + B*(i,j)) + C*(i,j) by A1,A4,Def14
      .=A*(i,j) + (B*(i,j) + C*(i,j)) by RLVECT_1:def 6
      .=A*(i,j) + ( B + C)*(i,j) by A3,A1,A4,Def14
      .=(A + ( B + C))*(i,j) by A1,A4,Def14;
  end;
  hence thesis by Th28;
end;

theorem Th34:
  A + 0.(F,n)= A
proof
A1: Indices A= Indices (A+0.(F,n)) by Th27;
A2: Indices A= Indices (0.(F,n)) by Th27;
  now
    let i,j;
    assume
A3: [i,j] in Indices (A+ 0.(F,n));
    hence (A+ 0.(F,n)) *(i,j)=A*(i,j) + 0.(F,n)*(i,j) by A1,Def14
      .=A*(i,j) +0.F by A1,A2,A3,Th30
      .=A*(i,j) by RLVECT_1:10;
  end;
  hence thesis by Th28;
end;

theorem Th35:
  A + (-A) = 0.(F,n)
proof
A1: Indices A= Indices (A + (-A)) by Th27;
A2: Indices A= Indices 0.(F,n) by Th27;
  now
    let i,j;
    assume
A3: [i,j] in Indices (A + (-A));
    hence (A + (-A))*(i,j)=A*(i,j)+ (-A)*(i,j) by A1,Def14
      .=A*(i,j)+ (-A*(i,j)) by A1,A3,Def13
      .=0.F by RLVECT_1:def 13
      .=(0.(F,n))*(i,j) by A2,A1,A3,Th30;
  end;
  hence thesis by Th28;
end;

definition
  let F,n;
  func n-G_Matrix_over F -> strict AbGroup means
  the carrier of it = n
-Matrices_over F & (for A,B holds (the addF of it).(A,B) = A+B) & 0.it = 0.(F,n
  );
  existence
  proof
    reconsider A0=0.(F,n) as Element of n-Matrices_over F;
    defpred P[Element of n-Matrices_over F, Element of n-Matrices_over F,
    Element of n-Matrices_over F] means ex A,B st $1=A & $2=B & $3 = A + B;
A1: for a,b being Element of n-Matrices_over F ex c being Element of n
    -Matrices_over F st P[a,b,c]
    proof
      let a,b be Element of (n-Matrices_over F);
      reconsider B=b as Matrix of n,F by Th31;
      reconsider A=a as Matrix of n,F by Th31;
      reconsider c = A+B as Element of n-Matrices_over F by Th31;
      take c;
      thus thesis;
    end;
    consider h being BinOp of n-Matrices_over F such that
A2: for a,b being Element of n-Matrices_over F holds P[a,b,h.(a,b)]
    from BINOP_1:sch 3(A1);
    set G=addLoopStr (# n-Matrices_over F,h,A0 #);
A3: h.(A,B)=A+B
    proof
      A is Element of (n-Matrices_over F) & B is Element of (n
      -Matrices_over F) by Th31;
      then ex A9,B9 st A=A9 & B=B9 & h.(A,B)= A9 + B9 by A2;
      hence thesis;
    end;
A4: for a being Element of n-Matrices_over F ex b being Element of n
    -Matrices_over F st ex A st a=A & b= -A
    proof
      let a be Element of n-Matrices_over F;
      reconsider A=a as Matrix of n,F by Th31;
      reconsider b=-A as Element of n-Matrices_over F by Th31;
      take b;
      thus thesis;
    end;
    G is Abelian add-associative right_zeroed right_complementable
    proof
      thus G is Abelian
      proof
        let a,b be Element of G;
        reconsider A=a,B=b as Matrix of n,F by Th31;
        thus a+b=A+B by A3
          .=B+A by Th32
          .=b+a by A3;
      end;
      hereby
        let a,b,c be Element of G;
        reconsider A=a,B=b,C=c as Matrix of n,F by Th31;
        thus (a+b)+c =h.(A+B,C) by A3
          .=(A+B)+C by A3
          .=A+(B+C) by Th33
          .=h.(A,B+C) by A3
          .=a+(b+c) by A3;
      end;
      hereby
        let a be Element of G;
        reconsider A=a as Matrix of n,F by Th31;
        thus a+0.G=A + 0.(F,n) by A3
          .=a by Th34;
      end;
      let a be Element of G;
      consider b being Element of n-Matrices_over F such that
A5:   ex A st a=A & b= -A by A4;
      consider A such that
A6:   a=A and
A7:   b= -A by A5;
      reconsider b = -A as Element of G by A7;
      take b;
      thus a+b=A+-A by A3,A6
        .=0.G by Th35;
    end;
    then reconsider G as strict AbGroup;
    take G;
    thus thesis by A3;
  end;
  uniqueness
  proof
    thus for G1,G2 being strict AbGroup st the carrier of G1 = n-Matrices_over
F & (for A,B holds (the addF of G1).(A,B) = A+B) & 0.G1 = 0.(F,n) & the carrier
of G2 = n-Matrices_over F & (for A,B holds (the addF of G2).(A,B) = A+B) & 0.G2
    = 0.(F,n) holds G1=G2
    proof
      let G1,G2 be strict AbGroup;
      assume that
A8:   the carrier of G1 = n-Matrices_over F and
A9:   for A,B holds (the addF of G1).(A,B) = A+B and
A10:  0.G1 = 0.(F,n) & the carrier of G2 = n-Matrices_over F and
A11:  for A,B holds (the addF of G2).(A,B) = A+B and
A12:  0.G2 = 0.(F,n);
      now
        let a,b be Element of G1;
        reconsider A = a, B = b as Matrix of n,F by A8,Th31;
        thus (the addF of G1).(a,b)= A+B by A9
          .= (the addF of G2).(a,b) by A11;
      end;
      hence thesis by A8,A10,A12,BINOP_1:2;
    end;
  end;
end;

theorem
  for M1 being Matrix of 0, n, K, M2 being Matrix of 0, m, K holds M1 = M2
proof
  let M1 be Matrix of 0, n, K, M2 be Matrix of 0, m, K;
  len M1 = 0 & len M2 = 0 by Def3;
  hence thesis by CARD_2:83;
end;

begin :: Addenda
:: from GOBOARD7, 2008.02.21, A.T.

theorem Th37:
  for D being set for i,j for M being Matrix of D st 1 <= i & i <=
  len M & 1 <= j & j <= width M holds [i,j] in Indices M
proof
  let D be set;
  let i,j;
  let M be Matrix of D;
  assume 1 <= i & i <= len M & 1 <= j & j <= width M;
  then i in dom M & j in Seg width M by FINSEQ_1:3,FINSEQ_3:27;
  hence thesis by ZFMISC_1:106;
end;

theorem
  for M being Matrix of n,m,D st 1 <= i & i <= n & 1 <= j & j <= m holds
  [i,j] in Indices M
proof
  let M be Matrix of n,m,D such that
A1: 1 <= i & i <= n and
A2: 1 <= j & j <= m;
A3: len M = n by Def3;
  per cases by NAT_1:3;
  suppose
    n = 0;
    hence thesis by A1,XXREAL_0:2;
  end;
  suppose
    n > 0;
    then m = width M by Th24;
    hence thesis by A1,A2,A3,Th37;
  end;
end;
 
:: from GOBOARD5, 2008.03.08, A.T.

theorem Th39:
  for M being tabular FinSequence, i,j st [i,j] in Indices M holds
  1 <= i & i <= len M & 1 <= j & j <= width M
proof
  let M be tabular FinSequence, i,j;
  assume [i,j] in Indices M;
  then i in dom M & j in Seg width M by ZFMISC_1:106;
  hence thesis by FINSEQ_1:3,FINSEQ_3:27;
end;

theorem
  for M being Matrix of n,m,D st [i,j] in Indices M holds 1 <= i & i <=
  n & 1 <= j & j <= m
proof
  let M be Matrix of n,m,D such that
A1: [i,j] in Indices M;
A2: len M = n by Def3;
  per cases by NAT_1:3;
  suppose
A3: n = 0;
A4: i in dom M by A1,ZFMISC_1:106;
    then
A5: 1 <= i by FINSEQ_3:27;
    i <= 0 by A2,A3,A4,FINSEQ_3:27;
    hence thesis by A5,XXREAL_0:2;
  end;
  suppose
    n > 0;
    then m = width M by Th24;
    hence thesis by A1,A2,Th39;
  end;
end;
