:: Intersections of Intervals and Balls in TOP-REAL n
::  by Artur Korni{\l}owicz and Yasunari Shidama
::
:: Received May 10, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies FUNCT_1, PRE_TOPC, EUCLID, ORDINAL2, RELAT_1, ARYTM, JGRAPH_6,
      COMPLEX1, ARYTM_1, ZF_LANG, PROB_1, BOOLE, FUNCOP_1, JORDAN1, TOPREAL1,
      ABSVALUE, UNIALG_1, GRCAT_1, JGRAPH_2, MCART_1, POLYEQ_1, FUNCT_3,
      SQUARE_1, ARYTM_3, METRIC_1, RLVECT_1, RVSUM_1, FINSEQ_1, EUCLID_2,
      JORDAN2C, LATTICES, ASYMPT_0, FINSEQ_2;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NAT_1, FUNCT_2, NUMBERS,
      XCMPLX_0, XREAL_0, REAL_1, FINSEQ_1, FINSEQ_2, RVSUM_1, SQUARE_1, QUIN_1,
      POLYEQ_1, STRUCT_0, PRE_TOPC, BORSUK_1, TOPREAL1, COMPLEX1, METRIC_1,
      TBSP_1, EUCLID, TOPRNS_1, JORDAN1, JORDAN2C, JGRAPH_2, JGRAPH_6,
      EUCLID_2;
 constructors JGRAPH_6, TOPRNS_1, JORDAN2C, TOPALG_1, REALSET1, JORDAN1,
      TOPREAL1, REAL_1, JGRAPH_2, QUIN_1, POLYEQ_1, SQUARE_1, EUCLID_2, TBSP_1,
      FINSEQOP, ABSVALUE, COMPLEX1, BINOP_2;
 registrations STRUCT_0, EUCLID, ARYTM_3, XREAL_0, REAL_1, NUMBERS, XBOOLE_0,
      TEX_2, QUIN_1, SQUARE_1, METRIC_1, BINARITH, TOPALG_2;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, JORDAN1;
 theorems JGRAPH_6, TOPRNS_1, XREAL_0, TARSKI, SUBSET_1, FUNCT_2, EUCLID,
      JGRAPH_2, XBOOLE_1, XBOOLE_0, BORSUK_1, FUNCOP_1, JORDAN2C, JORDAN1,
      TOPREAL1, SQUARE_1, REAL_1, XCMPLX_1, ABSVALUE, AXIOMS, REAL_2, JGRAPH_1,
      QUIN_1, POLYEQ_1, JGRAPH_3, TOPREAL3, METRIC_1, TOPREAL6, RVSUM_1,
      EUCLID_2, TBSP_1, GOBOARD6, REVROT_1, ENUMSET1, COMPLEX1;

begin :: Preliminaries

reserve
  n for Nat,
  a, b, r, w for real number,
  x, y, z for Point of TOP-REAL n,
  e for Point of Euclid n;

theorem Th1:
  x - y - z = x - z - y
proof
  thus x - y - z = x +- y - z by EUCLID:45
    .= x - z +- y by JORDAN2C:9
    .= x - z - y by EUCLID:45;
end;

theorem Th2:
  x + y = x + z implies y = z
proof
A1: x - x = 0.REAL n by EUCLID:46;
  assume x + y = x + z;
  then x - x + y = x + z - x by JORDAN2C:9;
  then x - x + y = x - x + z by JORDAN2C:9;
  hence y = 0.REAL n + z by A1,EUCLID:31
     .= z by EUCLID:31;
end;

theorem Th3:
  n is non empty implies x <> x + 1.REAL n
proof
  assume that
A1: n is non empty and
A2: x = x + 1.REAL n;
  x = x + 0.REAL n by EUCLID:31;
  then 0.REAL n = 1.REAL n by A2,Th2;
  hence thesis by A1,REVROT_1:19;
end;

theorem Th4:
  for x being set holds x = (1-r)*y + r*z implies
  (x = y iff r = 0 or y = z) & (x = z iff r = 1 or y = z)
proof
  let x be set;
  assume
A1:  x = (1-r)*y + r*z;
  hereby
    assume x = y;
    then 0.REAL n = (1-r)*y + r*z - y by A1,EUCLID:46
      .= (1-r)*y - y + r*z by JORDAN2C:9
      .= (1-r)*y - 1 * y + r*z by EUCLID:33
      .= (1-r-1)*y + r*z by EUCLID:54
      .= (-r)*y + r*z
      .= -r*y + r*z by EUCLID:44
      .= r*z - r*y by EUCLID:45
      .= r*(z-y) by EUCLID:53;
    then r = 0 or z-y = 0.REAL n by EUCLID:35;
    hence r = 0 or y = z by EUCLID:47;
  end;
  hereby
    assume
A2:   r = 0 or y = z;
    per cases by A2;
    suppose r = 0;
    hence x = y + 0 * z by A1,EUCLID:33
    .= y + 0.REAL n by EUCLID:33
    .= y by EUCLID:31;
    end;
    suppose z = y;
    hence x = (1-r+r)*y by A1,EUCLID:37
       .= y by EUCLID:33;
    end;
  end;
  hereby
    assume x = z;
    then 0.REAL n = (1-r)*y + r*z - z by A1,EUCLID:46
      .= (1-r)*y + (r*z - z) by EUCLID:49
      .= (1-r)*y + (r*z - 1 * z) by EUCLID:33
      .= (1-r)*y + (r*z +- 1 * z) by EUCLID:45
      .= (1-r)*y + ((-1)*z + r*z) by EUCLID:44
      .= (1-r)*y + (-1+r)*z by EUCLID:37
      .= (1-r)*y + (-(1-r))*z
      .= (1-r)*y + -(1-r)*z by EUCLID:44
      .= (1-r)*y - (1-r)*z by EUCLID:45
      .= (1-r)*(y-z) by EUCLID:53;
    then 1-r+r = 0+r or y-z = 0.REAL n by EUCLID:35;
    hence r = 1 or y = z by EUCLID:47,XCMPLX_1:27;
  end;
  assume
A3: r = 1 or y = z;
  per cases by A3;
  suppose r = 1;
  hence x = 0.REAL n + 1 * z by A1,EUCLID:33
     .= 1 * z by EUCLID:31
     .= z by EUCLID:33;
  end;
  suppose y = z;
  hence x = (1-r+r)*z by A1,EUCLID:37
     .= z by EUCLID:33;
  end;
end;

theorem Th5:
  for f being FinSequence of REAL holds |.f.|^2 = Sum sqr f
proof
  let f be FinSequence of REAL;
A1: Sum sqr f >= 0 by RVSUM_1:116;
  thus |.f.|^2 = (sqrt Sum sqr f)^2 by EUCLID:def 5
    .= Sum sqr f by A1,SQUARE_1:def 4;
end;

theorem Th6:
  for M being non empty MetrSpace,
      z1, z2, z3 being Point of M st
   z1 <> z2 & z1 in cl_Ball(z3,r) & z2 in cl_Ball(z3,r) holds
  r > 0
proof
  let M be non empty MetrSpace,
      z1, z2, z3 be Point of M such that
A1: z1 <> z2 and
A2: z1 in cl_Ball(z3,r) and
A3: z2 in cl_Ball(z3,r);
  now
    assume r = 0;
    then cl_Ball(z3,r) = {z3} by TOPREAL6:64;
    then z1 = z3 & z2 = z3 by A2,A3,TARSKI:def 1;
    hence contradiction by A1;
  end;
  hence r > 0 by A2,TOPREAL6:63;
end;

begin :: Subsets of TOP-REAL n

definition
  let n be Nat, x be Point of TOP-REAL n, r be real number;
  func Ball(x,r) -> Subset of TOP-REAL n equals :Def1:
  {p where p is Point of TOP-REAL n: |. p-x .| < r};
coherence
proof
  {p where p is Point of TOP-REAL n: |. p-x .| < r} c=
  the carrier of TOP-REAL n
  proof
    let q be set;
    assume q in {p where p is Point of TOP-REAL n: |. p-x .| < r};
    then ex p being Point of TOP-REAL n st q = p & |. p-x .| < r;
    hence thesis;
  end;
  hence thesis;
end;
  func cl_Ball(x,r) -> Subset of TOP-REAL n equals :Def2:
  {p where p is Point of TOP-REAL n: |. p-x .| <= r};
coherence
proof
  {p where p is Point of TOP-REAL n: |. p-x .| <= r} c=
  the carrier of TOP-REAL n
  proof
    let q be set;
    assume q in {p where p is Point of TOP-REAL n: |. p-x .| <= r};
    then ex p being Point of TOP-REAL n st q = p & |. p-x .| <= r;
    hence thesis;
  end;
  hence thesis;
end;
  func Sphere(x,r) -> Subset of TOP-REAL n equals :Def3:
  {p where p is Point of TOP-REAL n: |. p-x .| = r};
coherence
proof
  {p where p is Point of TOP-REAL n: |. p-x .| = r} c=
  the carrier of TOP-REAL n
  proof
    let q be set;
    assume q in {p where p is Point of TOP-REAL n: |. p-x .| = r};
    then ex p being Point of TOP-REAL n st q = p & |. p-x .| = r;
    hence thesis;
  end;
  hence thesis;
end;
end;

theorem Th7:
  y in Ball(x,r) iff |. y-x .| < r
proof
A1: Ball(x,r) = {p where p is Point of TOP-REAL n: |. p-x .| < r} by Def1;
  hereby
    assume y in Ball(x,r);
    then ex p being Point of TOP-REAL n st y = p & |. p-x .| < r by A1;
    hence |. y-x .| < r;
  end;
  thus thesis by A1;
end;

theorem Th8:
  y in cl_Ball(x,r) iff |. y-x .| <= r
proof
A1: cl_Ball(x,r) = {p where p is Point of TOP-REAL n: |. p-x .| <= r} by Def2;
  hereby
    assume y in cl_Ball(x,r);
    then ex p being Point of TOP-REAL n st y = p & |. p-x .| <= r by A1;
    hence |. y-x .| <= r;
  end;
  thus thesis by A1;
end;

theorem Th9:
  y in Sphere(x,r) iff |. y-x .| = r
proof
A1: Sphere(x,r) = {p where p is Point of TOP-REAL n: |. p-x .| = r} by Def3;
  hereby
    assume y in Sphere(x,r);
    then ex p being Point of TOP-REAL n st y = p & |. p-x .| = r by A1;
    hence |. y-x .| = r;
  end;
  thus thesis by A1;
end;

theorem
  y in Ball(0.REAL n,r) implies |.y.| < r
proof
  assume y in Ball(0.REAL n,r);
  then |. y-0.REAL n .| < r by Th7;
  hence |.y.| < r by JORDAN2C:13;
end;

theorem
  y in cl_Ball(0.REAL n,r) implies |.y.| <= r
proof
  assume y in cl_Ball(0.REAL n,r);
  then |. y-0.REAL n .| <= r by Th8;
  hence |.y.| <= r by JORDAN2C:13;
end;

theorem
  y in Sphere(0.REAL n,r) implies |.y.| = r
proof
  assume y in Sphere(0.REAL n,r);
  then |. y-0.REAL n .| = r by Th9;
  hence |.y.| = r by JORDAN2C:13;
end;

theorem Th13:
  x = e implies Ball(e,r) = Ball(x,r)
proof
  assume
A1: x = e;
  hereby
    let q be set;
    assume
A2:   q in Ball(e,r);
    then reconsider f = q as Point of Euclid n;
    reconsider p = f as Point of TOP-REAL n by TOPREAL3:13;
    dist(f,e) < r by A2,METRIC_1:12;
    then |. p-x .| < r by A1,JGRAPH_1:45;
    hence q in Ball(x,r) by Th7;
  end;
  let q be set;
  assume
A3: q in Ball(x,r);
  then reconsider q as Point of TOP-REAL n;
  reconsider f = q as Point of Euclid n by TOPREAL3:13;
  |. q-x .| < r by A3,Th7;
  then dist(f,e) < r by A1,JGRAPH_1:45;
  hence thesis by METRIC_1:12;
end;

theorem Th14:
  x = e implies cl_Ball(e,r) = cl_Ball(x,r)
proof
  assume
A1: x = e;
  hereby
    let q be set;
    assume
A2:   q in cl_Ball(e,r);
    then reconsider f = q as Point of Euclid n;
    reconsider p = f as Point of TOP-REAL n by TOPREAL3:13;
    dist(f,e) <= r by A2,METRIC_1:13;
    then |. p-x .| <= r by A1,JGRAPH_1:45;
    hence q in cl_Ball(x,r) by Th8;
  end;
  let q be set;
  assume
A3: q in cl_Ball(x,r);
  then reconsider q as Point of TOP-REAL n;
  reconsider f = q as Point of Euclid n by TOPREAL3:13;
  |. q-x .| <= r by A3,Th8;
  then dist(f,e) <= r by A1,JGRAPH_1:45;
  hence thesis by METRIC_1:13;
end;

theorem Th15:
  x = e implies Sphere(e,r) = Sphere(x,r)
proof
  assume
A1: x = e;
  hereby
    let q be set;
    assume
A2:   q in Sphere(e,r);
    then reconsider f = q as Point of Euclid n;
    reconsider p = f as Point of TOP-REAL n by TOPREAL3:13;
    dist(f,e) = r by A2,METRIC_1:14;
    then |. p-x .| = r by A1,JGRAPH_1:45;
    hence q in Sphere(x,r) by Th9;
  end;
  let q be set;
  assume
A3: q in Sphere(x,r);
  then reconsider q as Point of TOP-REAL n;
  reconsider f = q as Point of Euclid n by TOPREAL3:13;
  |. q-x .| = r by A3,Th9;
  then dist(f,e) = r by A1,JGRAPH_1:45;
  hence thesis by METRIC_1:14;
end;

theorem Th16:
  Ball(x,r) c= cl_Ball(x,r)
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
A1: Ball(x,r) = Ball(e,r) by Th13;
  cl_Ball(x,r) = cl_Ball(e,r) by Th14;
  hence thesis by A1,METRIC_1:15;
end;

theorem Th17:
  Sphere(x,r) c= cl_Ball(x,r)
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
A1: Sphere(x,r) = Sphere(e,r) by Th15;
  cl_Ball(x,r) = cl_Ball(e,r) by Th14;
  hence thesis by A1,METRIC_1:16;
end;

theorem Th18:
  Ball(x,r) \/ Sphere(x,r) = cl_Ball(x,r)
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
A1: Sphere(x,r) = Sphere(e,r) by Th15;
A2: Ball(x,r) = Ball(e,r) by Th13;
  cl_Ball(x,r) = cl_Ball(e,r) by Th14;
  hence thesis by A1,A2,METRIC_1:17;
end;

theorem Th19:
  Ball(x,r) misses Sphere(x,r)
proof
  assume not thesis;
  then consider q being set such that
A1: q in Ball(x,r) and
A2: q in Sphere(x,r) by XBOOLE_0:3;
  reconsider q as Point of TOP-REAL n by A1;
  |. q - x .| = r by A2,Th9;
  hence thesis by A1,Th7;
end;

registration
  let n, x; let r be non positive (real number);
  cluster Ball(x,r) -> empty;
coherence
proof
  assume not thesis;
  then consider y being Point of TOP-REAL n such that
A1: y in Ball(x,r) by SUBSET_1:10;
  |. y - x .| < r by A1,Th7;
  hence contradiction by TOPRNS_1:26;
end;
end;

registration
  let n, x; let r be positive (real number);
  cluster Ball(x,r) -> non empty;
coherence
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
  Ball(x,r) = Ball(e,r) by Th13;
  hence thesis by GOBOARD6:4;
end;
end;

theorem
  Ball(x,r) is non empty implies r > 0
proof
  assume
A1:  Ball(x,r) is non empty;
  assume r <= 0;
  then reconsider r as non positive (real number);
  Ball(x,r) is empty;
  hence thesis by A1;
end;

theorem
  Ball(x,r) is empty implies r <= 0
proof
  assume
A1:  Ball(x,r) is empty;
  assume r > 0;
  then reconsider r as positive (real number);
  Ball(x,r) is non empty;
  hence thesis by A1;
end;

registration
  let n, x; let r be negative (real number);
  cluster cl_Ball(x,r) -> empty;
coherence
proof
  assume not thesis;
  then consider y being Point of TOP-REAL n such that
A1: y in cl_Ball(x,r) by SUBSET_1:10;
  |. y - x .| <= r by A1,Th8;
  hence contradiction by TOPRNS_1:26;
end;
end;

registration
  let n, x; let r be non negative (real number);
  cluster cl_Ball(x,r) -> non empty;
coherence
proof
  |. x-x .| = 0 by TOPRNS_1:29;
  hence thesis by Th8;
end;
end;

theorem
  cl_Ball(x,r) is non empty implies r >= 0
proof
  assume
A1:  cl_Ball(x,r) is non empty;
  assume r < 0;
  then reconsider r as negative (real number);
  cl_Ball(x,r) is empty;
  hence thesis by A1;
end;

theorem
  cl_Ball(x,r) is empty implies r < 0
proof
  assume
A1:  cl_Ball(x,r) is empty;
  assume r >= 0;
  then reconsider r as non negative (real number);
  cl_Ball(x,r) is non empty;
  hence thesis by A1;
end;

theorem Th24:
  a + b = 1 & abs(a) + abs(b) = 1 & b <> 0 &
  x in cl_Ball(z,r) & y in Ball(z,r) implies
  a * x + b * y in Ball(z,r)
proof
  assume that
A1: a + b = 1 and
A2: abs(a) + abs(b) = 1 and
A3: b <> 0 and
A4: x in cl_Ball(z,r) and
A5: y in Ball(z,r);
  reconsider w = a * x + b * y as Point of Euclid n by TOPREAL3:13;
A6: a*z + b*z = (a+b)*z by EUCLID:37
     .= z by A1,EUCLID:33;
A7:  |. a*z + b*z - (a*x + b*y) .|
   = |. a*z - (a*x + b*y) + b*z .| by JORDAN2C:9
  .= |. a*z - a*x - b*y + b*z .| by EUCLID:50
  .= |. a*z - a*x + b*z - b*y .| by JORDAN2C:9
  .= |. a*z - a*x + (b*z - b*y) .| by EUCLID:49
  .= |. a*(z-x) + (b*z - b*y) .| by EUCLID:53
  .= |. a*(z-x) + b*(z-y) .| by EUCLID:53;
A8: a is Real by XREAL_0:def 1;
A9: b is Real by XREAL_0:def 1;
A10: 0 <= abs(a) by ABSVALUE:5;
A11: 0 < abs(b) by A3,ABSVALUE:6;
  |. x-z .| <= r & |. y-z .| < r by A4,A5,Th7,Th8;
  then |. z-x .| <= r & |. z-y .| < r by TOPRNS_1:28;
  then abs(a)*|. z-x .| <= abs(a)*r & abs(b)*|. z-y .| < abs(b)*r
    by A10,A11,AXIOMS:25,REAL_1:70;
  then abs(a)*|. z-x .| + abs(b)*|. z-y .| < abs(a)*r + abs(b)*r
    by REAL_1:67;
  then |. a*(z-x) .| + abs(b)*|. z-y .| < 1 * r by A2,A8,TOPRNS_1:8;
  then
A12: |. a*(z-x) .| + |. b*(z-y) .| < r by A9,TOPRNS_1:8;
  |. a*z + b*z - (a*x + b*y) .| <= |. a*(z-x) .| + |. b*(z-y) .|
      by A7,TOPRNS_1:30;
  then |. a*z + b*z - (a*x + b*y) .| < r by A12,AXIOMS:22;
  then |. a*x + b*y - (a*z + b*z) .| < r by TOPRNS_1:28;
  hence a * x + b * y in Ball(z,r) by A6,Th7;
end;

registration
  let n, x, r;
  cluster Ball(x,r) -> open Bounded;
coherence
proof
A1: r is Real by XREAL_0:def 1;
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
A2: Ball(e,r) is bounded by TBSP_1:19;
  Ball(e,r) = Ball(x,r) by Th13;
  hence thesis by A1,A2,GOBOARD6:6,JORDAN2C:def 2;
end;
  cluster cl_Ball(x,r) -> closed Bounded;
coherence
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
A3: cl_Ball(e,r) is bounded by TOPREAL6:67;
  cl_Ball(e,r) = cl_Ball(x,r) by Th14;
  hence thesis by A3,JORDAN2C:def 2,TOPREAL6:66;
end;
  cluster Sphere(x,r) -> closed Bounded;
coherence
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
A4: Sphere(e,r) is bounded by TOPREAL6:70;
  Sphere(e,r) = Sphere(x,r) by Th15;
  hence thesis by A4,JORDAN2C:def 2,TOPREAL6:69;
end;
end;

Lm1:
  for a being real number, P being Subset of TOP-REAL n,
      Q being Point of TOP-REAL n st
  P = {q where q is Point of TOP-REAL n: |.q - Q.| <= a} holds
  P is convex
proof
  let a be real number,
      P be Subset of TOP-REAL n,
      Q be Point of TOP-REAL n;
  assume
A1: P = {q where q is Point of TOP-REAL n: |.q - Q.| <= a};
  let p1,p2 be Point of TOP-REAL n;
  assume p1 in P;
  then consider q1 being Point of TOP-REAL n such that
A2: q1=p1 & |.q1-Q.| <= a by A1;
  assume p2 in P;
  then consider q2 being Point of TOP-REAL n such that
A3: q2=p2 & |.q2-Q.| <= a by A1;
  let x be set;
  assume
A4: x in LSeg(p1,p2);
  then x in {(1-r)*p1+r*p2 where r is Real: 0<=r & r<=1} by TOPREAL1:def 3;
  then consider r being Real such that
A5: x = (1-r)*p1+r*p2 and
A6: 0 <= r and
A7: r <= 1;
A8: r = abs(r) by A6,ABSVALUE:def 1;
   reconsider q = x as Point of TOP-REAL n by A4;
A9: |.(1-r)*(p1-Q).| = abs(1-r)*|.p1-Q.| by TOPRNS_1:8;
A10: 1-r >= 0 by A7,SQUARE_1:12;
   then
A11:abs(1-r) = 1-r by ABSVALUE:def 1;
   per cases;
   suppose 1-r > 0;
   then
A12: abs(1-r)*|.p1-Q.| <= abs(1-r)*a by A2,A11,AXIOMS:25;
A13: |.r*(p2-Q).| = abs(r)*|.p2-Q.| by TOPRNS_1:8;
   0 <= abs(r) by ABSVALUE:5;
   then
A14:abs(r)*|.p2-Q.| <= abs(r)*a by A3,AXIOMS:25;
A15: (1-r)*Q + r*Q = 1 * Q - r*Q + r*Q by EUCLID:54
    .= 1 * Q by EUCLID:52
    .= Q by EUCLID:33;
  q-Q = (1-r)*p1+r*p2-(1-r)*Q-r*Q by A5,A15,EUCLID:50
     .= (1-r)*p1+r*p2+-(1-r)*Q-r*Q by EUCLID:45
     .= (1-r)*p1+-(1-r)*Q+r*p2-r*Q by EUCLID:30
     .= (1-r)*p1-(1-r)*Q+r*p2-r*Q by EUCLID:45
     .= (1-r)*(p1-Q)+r*p2-r*Q by EUCLID:53
     .= (1-r)*(p1-Q)+(r*p2-r*Q) by EUCLID:49
     .= (1-r)*(p1-Q)+r*(p2-Q) by EUCLID:53;
   then
A16: |.q-Q.| <= |.(1-r)*(p1-Q).| + |.r*(p2-Q).| by TOPRNS_1:30;
  |.(1-r)*(p1-Q).|+|.r*(p2-Q).| <= (1-r)*a+r*a
    by A8,A9,A11,A12,A13,A14,REAL_1:55;
  then |.q-Q.| <= a by A16,AXIOMS:22;
  hence x in P by A1;
  end;
  suppose 1-r <= 0;
  then 1-r+r = 0+r by A10,AXIOMS:21;
  then 1 = r by XCMPLX_1:27;
  then x = p2 by A5,Th4;
  hence x in P by A1,A3;
  end;
end;

Lm2:
  for a being real number, P being Subset of TOP-REAL n,
      Q being Point of TOP-REAL n st
  P = {q where q is Point of TOP-REAL n: |.q - Q.| < a} holds
  P is convex
proof
  let a be real number,
      P be Subset of TOP-REAL n,
      Q be Point of TOP-REAL n;
  assume
A1: P = {q where q is Point of TOP-REAL n: |.q - Q.| < a};
  let p1,p2 be Point of TOP-REAL n;
  assume
A2: p1 in P & p2 in P;
  reconsider e = Q as Point of Euclid n by TOPREAL3:13;
  Ball(e,a) = Ball(Q,a) by Th13
   .= P by A1,Def1;
  hence LSeg(p1,p2) c= P by A2,TOPREAL3:28;
end;

:: Convex subsets of TOP-REAL n

registration
  let n, x, r;
  cluster Ball(x,r) -> convex;
coherence
proof
  Ball(x,r) = {p where p is Point of TOP-REAL n: |. p-x .| < r} by Def1;
  hence thesis by Lm2;
end;
  cluster cl_Ball(x,r) -> convex;
coherence
proof
  cl_Ball(x,r) = {p where p is Point of TOP-REAL n: |. p-x .| <= r} by Def2;
  hence thesis by Lm1;
end;
end;

definition
  let n be Nat; let f be map of TOP-REAL n, TOP-REAL n;
  attr f is homogeneous means :Def4:
  for r being real number, x being Point of TOP-REAL n holds f.(r*x) = r * f.x;
  attr f is additive means :Def5:
  for x, y being Point of TOP-REAL n holds f.(x+y) = f.x + f.y;
end;

registration
  let n;
  cluster TOP-REAL n --> 0.REAL n -> homogeneous additive;
coherence
proof
  set f = TOP-REAL n --> 0.REAL n;
  thus f is homogeneous
  proof
  let r be real number, x be Point of TOP-REAL n;
A1: f = (the carrier of TOP-REAL n) --> 0.REAL n by BORSUK_1:def 3;
  hence f.(r*x) = 0.REAL n by FUNCOP_1:13
     .= r * 0.REAL n by EUCLID:32
     .= r * f.x by A1,FUNCOP_1:13;
 end;
  let x, y be Point of TOP-REAL n;
A2: f = (the carrier of TOP-REAL n) --> 0.REAL n by BORSUK_1:def 3;
  hence f.(x+y) = 0.REAL n by FUNCOP_1:13
     .= 0.REAL n + 0.REAL n by EUCLID:31
     .= f.x + 0.REAL n by A2,FUNCOP_1:13
     .= f.x + f.y by A2,FUNCOP_1:13;
end;
end;

registration
  let n;
  cluster homogeneous additive continuous map of TOP-REAL n, TOP-REAL n;
existence
proof
  take TOP-REAL n --> 0.REAL n;
  thus thesis by BORSUK_1:36;
end;
end;

registration
  let a, c be real number;
  cluster AffineMap(a,0,c,0) -> homogeneous additive;
coherence
proof
  set f = AffineMap(a,0,c,0);
  hereby
  let r be real number, x be Point of TOP-REAL 2;
A1: (r*x)`1 = r*x`1 by TOPREAL3:9;
A2: (r*x)`2 = r*x`2 by TOPREAL3:9;
  thus f.(r*x) = |[a*((r*x)`1)+0,c*((r*x)`2)+0]| by JGRAPH_2:def 2
    .= |[r*(a*(x`1)),r*(c*(x`2))]| by A1,A2
    .= r * |[a*(x`1)+0,c*(x`2)+0]| by EUCLID:62
    .= r * f.x by JGRAPH_2:def 2;
  end;
  let x, y be Point of TOP-REAL 2;
A3: (x+y)`1 = x`1 + y`1 by TOPREAL3:7;
A4: (x+y)`2 = x`2 + y`2 by TOPREAL3:7;
  thus f.(x+y) = |[a*((x+y)`1)+0,c*((x+y)`2)+0]| by JGRAPH_2:def 2
    .= |[a*(x`1)+a*(y`1),c*(x`2)+c*(y`2)]| by A3,A4
    .= |[a*(x`1)+0,c*(x`2)+0]| + |[a*(y`1),c*(y`2)]| by EUCLID:60
    .= f.x + |[a*(y`1)+0,c*(y`2)+0]| by JGRAPH_2:def 2
    .= f.x + f.y by JGRAPH_2:def 2;
end;
end;

theorem
  for f being homogeneous additive map of TOP-REAL n, TOP-REAL n,
      X being convex Subset of TOP-REAL n holds
  f.:X is convex
proof
  let f be homogeneous additive map of TOP-REAL n, TOP-REAL n,
      X be convex Subset of TOP-REAL n;
  let p, q be Point of TOP-REAL n;
  assume p in f.:X;
  then consider p0 being Point of TOP-REAL n such that
A1: p0 in X and
A2: p = f.p0 by FUNCT_2:116;
  assume q in f.:X;
  then consider q0 being Point of TOP-REAL n such that
A3: q0 in X and
A4: q = f.q0 by FUNCT_2:116;
A5:LSeg(p,q) = { (1-l)*p + l*q where l is Real: 0 <= l & l <= 1 }
    by TOPREAL1:def 3;
A6:LSeg(p0,q0) = { (1-l)*p0 + l*q0 where l is Real: 0 <= l & l <= 1 }
    by TOPREAL1:def 3;
  let x be set;
  assume x in LSeg(p,q);
  then consider l being Real such that
A7: x = (1-l)*p + l*q and
A8: 0 <= l & l <= 1 by A5;
  set a = (1-l)*p0 + l*q0;
A9: a in LSeg(p0,q0) by A6,A8;
A10: LSeg(p0,q0) c= X by A1,A3,JORDAN1:def 1;
  f.a = f.((1-l)*p0) + f.(l*q0) by Def5
     .= f.((1-l)*p0) + l*f.q0 by Def4
     .= x by A2,A4,A7,Def4;
  hence x in f.:X by A9,A10,FUNCT_2:43;
end;

:: Halflines

reserve
  p, q for Point of TOP-REAL n;

definition
  let n be Nat; let p, q be Point of TOP-REAL n;
  func halfline(p,q) -> Subset of TOP-REAL n equals :Def6:
  { (1-l)*p + l*q where l is Real: 0 <= l };
coherence
proof
  set X = { (1-l)*p + l*q where l is Real: 0 <= l };
  X c= the carrier of TOP-REAL n
  proof
    let x be set;
    assume x in X;
    then ex l being Real st x = (1-l)*p + l*q & 0 <= l;
    hence thesis;
  end;
  hence thesis;
end;
end;

theorem Th26:
  for x being set holds x in halfline(p,q) iff
  ex l being real number st x = (1-l)*p + l*q & 0 <= l
proof
  let x be set;
A1: halfline(p,q) = { (1-l)*p + l*q where l is Real: 0 <= l } by Def6;
  hereby
    assume x in halfline(p,q);
    then ex l being Real st x = (1-l)*p + l*q & 0 <= l by A1;
    hence ex l being real number st x = (1-l)*p + l*q & 0 <= l;
  end;
  given l being real number such that
A2: x = (1-l)*p + l*q & 0 <= l;
  l is Real by XREAL_0:def 1;
  hence thesis by A1,A2;
end;

registration
  let n, p, q;
  cluster halfline(p,q) -> non empty;
coherence
proof
  (1-0)*p + 0 * q in halfline(p,q) by Th26;
  hence thesis;
end;
end;

theorem Th27:
  p in halfline(p,q)
proof
  (1-0)*p + 0 * q = p + 0 * q by EUCLID:33
    .= p + 0.REAL n by EUCLID:33
    .= p by EUCLID:31;
  hence thesis by Th26;
end;

theorem Th28:
  q in halfline(p,q)
proof
  (1-1)*p + 1 * q = 0 * p + q by EUCLID:33
    .= 0.REAL n + q by EUCLID:33
    .= q by EUCLID:31;
  hence thesis by Th26;
end;

theorem Th29:
  halfline(p,p) = {p}
proof
  hereby
    let d be set;
    assume d in halfline(p,p);
    then consider r being real number such that
A1:   d = (1-r)*p+r*p and 0 <= r by Th26;
    d = p by A1,Th4;
    hence d in {p} by TARSKI:def 1;
  end;
  let d be set;
  assume d in {p};
  then d = p by TARSKI:def 1;
  hence thesis by Th27;
end;

theorem Th30:
  x in halfline(p,q) implies halfline(p,x) c= halfline(p,q)
proof
  assume x in halfline(p,q);
  then consider R being real number such that
A1: x = (1-R)*p + R*q and
A2: 0 <= R by Th26;
  let d be set;
  assume
A3: d in halfline(p,x);
  then reconsider d as Point of TOP-REAL n;
  consider r being real number such that
A4: d = (1-r)*p + r*x and
A5: 0 <= r by A3,Th26;
  set o = r*R;
A6: 0 <= o by A2,A5,SQUARE_1:19;
  d = (1-r)*p + (r*((1-R)*p) + r*(R*q)) by A1,A4,EUCLID:36
   .= (1-r)*p + r*((1-R)*p) + r*(R*q) by EUCLID:30
   .= (1-r)*p + r*(1-R)*p + r*(R*q) by EUCLID:34
   .= ((1-r) + r*(1-R))*p + r*(R*q) by EUCLID:37
   .= (1-o)*p + o*q by EUCLID:34;
  hence thesis by A6,Th26;
end;

theorem
  x in halfline(p,q) & x <> p implies halfline(p,q) = halfline(p,x)
proof
  assume
A1: x in halfline(p,q);
  then consider R being real number such that
A2: x = (1-R)*p + R*q and
A3: 0 <= R by Th26;
  assume
A4: x <> p;
  thus halfline(p,q) c= halfline(p,x)
  proof
    let d be set;
    assume
A5:   d in halfline(p,q);
    then reconsider d as Point of TOP-REAL n;
    consider r being real number such that
A6:   d = (1-r)*p + r*q and
A7:   0 <= r by A5,Th26;
    set o = r/R;
A8: 0 <= o by A3,A7,SQUARE_1:27;
    R <> 0 by A2,A4,Th4;
    then o*R = r by XCMPLX_1:88;
    then d = (1-o + o*(1-R))*p + o*(R*q) by A6,EUCLID:34
     .= (1-o)*p + o*(1-R)*p + o*(R*q) by EUCLID:37
     .= (1-o)*p + o*((1-R)*p) + o*(R*q) by EUCLID:34
     .= (1-o)*p + (o*((1-R)*p) + o*(R*q)) by EUCLID:30
     .= (1-o)*p + o*((1-R)*p + R*q) by EUCLID:36;
    hence thesis by A2,A8,Th26;
  end;
  thus thesis by A1,Th30;
end;

theorem
  LSeg(p,q) c= halfline(p,q)
proof
  let a be set;
  assume a in LSeg(p,q);
  then ex r being Real st 0<=r & r<=1 & a = (1-r)*p+r*q by JGRAPH_1:52;
  hence thesis by Th26;
end;

registration
  let n, p, q;
  cluster halfline(p,q) -> convex;
coherence
proof
  set P = halfline(p,q);
  let u, v be Point of TOP-REAL n;
  assume u in P;
  then consider a being real number such that
A1: u = (1-a)*p+a*q and
A2: 0 <= a by Th26;
  assume v in P;
  then consider b being real number such that
A3: v = (1-b)*p+b*q and
A4: 0 <= b by Th26;
  let x be set;
  assume x in LSeg(u,v);
  then consider r being Real such that
A5: 0 <= r and
A6: r <= 1 and
A7: x = (1-r)*u+r*v by JGRAPH_1:52;
  set o = (1-r)*a+r*b;
  1-r >= r-r by A6,REAL_1:92;
  then
A8: (1-r)*a >= 0 * a by A2,AXIOMS:25;
  r*b >= 0 * 0 by A4,A5,SQUARE_1:19;
  then
A9: 0+0 <= o by A8,REAL_1:55;
  x = (1-r)*((1-a)*p)+(1-r)*(a*q) + r*((1-b)*p+b*q) by A1,A3,A7,EUCLID:36
   .= (1-r)*((1-a)*p) + (1-r)*(a*q) + (r*((1-b)*p) + r*(b*q)) by EUCLID:36
   .= (1-r)*((1-a)*p) + (r*((1-b)*p) + r*(b*q)) + (1-r)*(a*q) by EUCLID:30
   .= (1-r)*((1-a)*p) + r*((1-b)*p) + r*(b*q) + (1-r)*(a*q) by EUCLID:30
   .= (1-r)*(1-a)*p + r*((1-b)*p) + r*(b*q) + (1-r)*(a*q) by EUCLID:34
   .= (1-r)*(1-a)*p + r*(1-b)*p + r*(b*q) + (1-r)*(a*q) by EUCLID:34
   .= (1-r)*(1-a)*p + r*(1-b)*p + r*b*q + (1-r)*(a*q) by EUCLID:34
   .= (1-r)*(1-a)*p + r*(1-b)*p + r*b*q + (1-r)*a*q by EUCLID:34
   .= ((1-r)*(1-a) + r*(1-b)) * p + r*b*q + (1-r)*a*q by EUCLID:37
   .= ((1-r)*(1-a) + r*(1-b)) * p + (r*b*q + (1-r)*a*q) by EUCLID:30
   .= (1-o)*p + o*q by EUCLID:37;
  hence x in P by A9,Th26;
end;
end;

theorem Th33:
  y in Sphere(x,r) & z in Ball(x,r) implies LSeg(y,z) /\ Sphere(x,r) = {y}
proof
  set s = y, t = z;
  assume that
A1: s in Sphere(x,r) and
A2: t in Ball(x,r);
  hereby
    let m be set;
    assume
A3:   m in LSeg(s,t) /\ Sphere(x,r);
    then reconsider w = m as Point of TOP-REAL n;
    w in LSeg(s,t) by A3,XBOOLE_0:def 3;
    then consider d being Real such that
A4:   0 <= d and
A5:   d <= 1 and
A6:   w = (1-d)*s + d*t by JGRAPH_1:52;
A7: m in Sphere(x,r) by A3,XBOOLE_0:def 3;
  reconsider G = x as Point of Euclid n by TOPREAL3:13;
A8: r = |. w - x .| by A7,Th9
     .= |. (1-d)*s + d*t - (1-d+d)*x .| by A6,EUCLID:33
     .= |. (1-d)*s + d*t - ((1-d)*x + d*x) .| by EUCLID:37
     .= |. (1-d)*s + d*t - (1-d)*x - d*x .| by EUCLID:50
     .= |. (1-d)*s - (1-d)*x + d*t - d*x .| by JORDAN2C:9
     .= |. (1-d)*(s-x) + d*t - d*x .| by EUCLID:53
     .= |. (1-d)*(s-x) + (d*t - d*x) .| by EUCLID:49
     .= |. (1-d)*(s-x) + d*(t-x) .| by EUCLID:53;
    d-1 <= 1-1 by A5,REAL_1:49;
    then
A9: -0 <= -(d-1) by REAL_1:50;
A10: |. (1-d)*(s-x) .| = abs(1-d)*|. s-x .| by TOPRNS_1:8
      .= (1-d)*|. s-x .| by A9,ABSVALUE:def 1
      .= (1-d)*r by A1,Th9;
A11: |. d*(t-x) .| = abs(d)*|. t-x .| by TOPRNS_1:8
      .= d*|. t-x .| by A4,ABSVALUE:def 1;
    per cases by A4;
    suppose
A12:   d > 0;
    |. t-x .| < r by A2,Th7;
    then d*|. t-x .| < d*r by A12,REAL_1:70;
    then (1-d)*r + d*|. t-x .| < (1-d)*r + d*r by REAL_1:67;
    hence m in {s} by A8,A10,A11,TOPRNS_1:30;
    end;
    suppose d = 0;
    then m = 1 * s + 0.REAL n by A6,EUCLID:33
      .= 1 * s by EUCLID:31
      .= s by EUCLID:33;
    hence m in {s} by TARSKI:def 1;
    end;
  end;
  let m be set;
  assume m in {s};
  then
A13: m = s by TARSKI:def 1;
  s in LSeg(s,t) by TOPREAL1:6;
  hence thesis by A1,A13,XBOOLE_0:def 3;
end;

theorem Th34:
  y in Sphere(x,r) & z in Sphere(x,r) implies LSeg(y,z) \ {y,z} c= Ball(x,r)
proof
  assume that
A1: y in Sphere(x,r) and
A2: z in Sphere(x,r);
  per cases;
  suppose y = z;
  then LSeg(y,z) = {y} & {y,z} = {y} by ENUMSET1:69,TOPREAL1:7;
  then LSeg(y,z) \ {y,z} = {} by XBOOLE_1:37;
  hence thesis by XBOOLE_1:2;
  end;
  suppose
A3: y <> z;
  let a be set;
  assume
A4: a in LSeg(y,z) \ {y,z};
  then reconsider R = a as Point of TOP-REAL n;
A5: R in LSeg(y,z) by A4,XBOOLE_0:def 4;
  then consider l being Real such that
A6: 0 <= l and
A7: l <= 1 and
A8: R = (1-l)*y + l*z by JGRAPH_1:52;
  set l1 = 1-l;
  set X = |(y-x,z-x)|;
A9: the carrier of TOP-REAL n = REAL n by EUCLID:25;
  then
  reconsider xf = x, yf = y, zf = z as Element of n-tuples_on REAL
    by EUCLID:def 1;
  reconsider y1 = y-x, z1 = z-x as FinSequence of REAL by EUCLID:27;
  consider Rf being FinSequence of REAL such that
A10: R-x = Rf and
A11: |. R-x .| = |.Rf.| by TOPRNS_1:def 6;
A12: Sum sqr Rf >= 0 by RVSUM_1:116;
A13: |. y-x .|^2 = r^2 by A1,Th9;
A14: |. z-x .|^2 = r^2 by A2,Th9;
  reconsider W1 = l1*(y-x), W2 = l*(z-x) as Element of n-tuples_on REAL
    by A9,EUCLID:def 1;
A15: y-x = yf-xf by A9,EUCLID:def 13;
  then
A16: l1*(y-x) = l1*(yf-xf) by A9,EUCLID:def 11;
A17: z-x = zf-xf by A9,EUCLID:def 13;
  then
A18: l*(z-x) = l*(zf-xf) by A9,EUCLID:def 11;
A19: W1 + W2 = l1*(y-x) + l*(z-x) by A9,EUCLID:def 10
      .= l1*y-l1*x+l*(z-x) by EUCLID:53
      .= l1*y-l1*x+(l*z-l*x) by EUCLID:53
      .= l1*y-l1*x+l*z-l*x by EUCLID:49
      .= l1*y+l*z-l1*x-l*x by JORDAN2C:9
      .= l1*y+l*z-(l1*x+l*x) by EUCLID:50
      .= l1*y+l*z-(1 *x-l*x+l*x) by EUCLID:54
      .= l1*y+l*z-1 *x by EUCLID:52
      .= Rf by A8,A10,EUCLID:33;
  |. R-x .|^2 = (sqrt Sum sqr Rf)^2 by A11,EUCLID:def 5
    .= Sum sqr Rf by A12,SQUARE_1:def 4
    .= Sum (sqr W1 + 2*mlt(W1,W2) + sqr W2) by A19,RVSUM_1:98
    .= Sum (sqr W1 + 2*mlt(W1,W2)) + Sum sqr W2 by RVSUM_1:119
    .= Sum sqr W1 + Sum (2*mlt(W1,W2)) + Sum sqr W2 by RVSUM_1:119
    .= Sum (l1^2*sqr (yf-xf)) + Sum (2*mlt(W1,W2)) + Sum sqr (l*(zf-xf))
       by A16,A18,RVSUM_1:84
    .= l1^2*Sum sqr (yf-xf) + Sum (2*mlt(W1,W2)) + Sum sqr (l*(zf-xf))
       by RVSUM_1:117
    .= l1^2*Sum sqr (yf-xf) + Sum (2*mlt(W1,W2)) + Sum (l^2*sqr (zf-xf))
       by RVSUM_1:84
    .= l1^2*Sum sqr (yf-xf) + Sum (2*mlt(W1,W2)) + l^2*Sum sqr (zf-xf)
       by RVSUM_1:117
    .= l1^2*|.y1.|^2 + Sum (2*mlt(W1,W2)) + l^2*Sum sqr (zf-xf) by A15,Th5
    .= l1^2*|.y1.|^2 + Sum (2*mlt(W1,W2)) + l^2*|.z1.|^2 by A17,Th5
    .= l1^2*r^2 + Sum (2*mlt(W1,W2)) + l^2*|.z1.|^2 by A13,TOPRNS_1:def 6
    .= l1^2*r^2 + Sum (2*mlt(W1,W2)) + l^2*r^2 by A14,TOPRNS_1:def 6
    .= l1^2*r^2 + 2*Sum mlt(W1,W2) + l^2*r^2 by RVSUM_1:117
    .= l1^2*r^2 + 2*Sum (l1*mlt(yf-xf,l*(zf-xf))) + l^2*r^2 by A16,A18,
RVSUM_1:94
    .= l1^2*r^2 + 2*Sum (l1*(l*mlt(yf-xf,zf-xf))) + l^2*r^2 by RVSUM_1:94
    .= l1^2*r^2 + 2*Sum (l1*l*mlt(yf-xf,zf-xf)) + l^2*r^2 by RVSUM_1:71
    .= l1^2*r^2 + 2*(l1*l*Sum mlt(yf-xf,zf-xf)) + l^2*r^2 by RVSUM_1:117
    .= l1^2*r^2 + 2*l1*l*Sum mlt(yf-xf,zf-xf) + l^2*r^2
    .= l1^2*r^2 + 2*l1*l*|(y1,z1)| + l^2*r^2 by A15,A17,EUCLID_2:def 1
    .= l1^2*r^2 + 2*l1*l*X + l^2*r^2 by EUCLID_2:def 2
    .= (l1^2+l^2)*r^2 + 2*l*l1*X
    .= (1^2-2*1 *l+l^2+l^2)*r^2 + 2*l*l1*X by SQUARE_1:64
    .= (1-2*l+l^2+l^2)*r^2 + 2*l*l1*X by SQUARE_1:59;
  then
A20: |. R-x .|^2 - r^2 = (-2*l+2*l^2)*r^2+2*l*l1*X
    .= (-2*1 *l+2*(l*l))*r^2+2*l*l1*X by SQUARE_1:def 3
    .= 2*l*l1*(-r^2+X);
  Sphere(x,r) c= cl_Ball(x,r) by Th17;
  then LSeg(y,z) c= cl_Ball(x,r) by A1,A2,JORDAN1:def 1;
  then
A21: |. R-x .| <= r by A5,Th8;
  now
    assume l = 0;
    then R = y by A8,Th4;
    then R in {y,z} by TARSKI:def 2;
    hence contradiction by A4,XBOOLE_0:def 4;
  end;
  then
A22: 2*l > 0 by A6,SQUARE_1:21;
A23: 1-1 <= l1 by A7,REAL_1:92;
  now
    assume l1 = 0;
    then 1-l+l = 0+l;
    then l = 1 by XCMPLX_1:27;
    then R = z by A8,Th4;
    then R in {y,z} by TARSKI:def 2;
    hence contradiction by A4,XBOOLE_0:def 4;
  end;
  then
A24: 2*l*l1 > 0 by A22,A23,SQUARE_1:21;
  now
A25: Sum sqr (yf-zf) >= 0 by RVSUM_1:116;
A26: y-z = yf-zf by A9,EUCLID:def 13;
    assume |. R-x .| = r;
    then 2*l*l1*(-r^2+X) = 0 by A20,XCMPLX_1:14;
    then X-r^2 = 0 by A24,XCMPLX_1:6;
    then X = r^2 by XCMPLX_1:15;
    then 0 = |. y-x .|^2 - 2*X + |. z-x .|^2 by A13,A14
      .= |. y-x - (z-x) .|^2 by EUCLID_2:68
      .= |. y-x-z+x .|^2 by EUCLID:51
      .= |. y-x+x-z .|^2 by JORDAN2C:9
      .= |.y-z.|^2 by EUCLID:52
      .= |.yf-zf.|^2 by A26,TOPRNS_1:def 6
      .= (sqrt Sum sqr (yf-zf))^2 by EUCLID:def 5
      .= Sum sqr (yf-zf) by A25,SQUARE_1:def 4;
    then yf-zf = n |-> 0 by RVSUM_1:121;
    hence contradiction by A3,RVSUM_1:59;
  end;
  then |. R-x .| < r by A21,REAL_1:def 5;
  hence thesis by Th7;
  end;
end;

theorem Th35:
  y in Sphere(x,r) & z in Sphere(x,r) implies LSeg(y,z) /\ Sphere(x,r) = {y,z}
proof
  assume that
A1: y in Sphere(x,r) and
A2: z in Sphere(x,r);
A3: LSeg(y,z) \ {y,z} c= Ball(x,r) by A1,A2,Th34;
  hereby
    let a be set;
    assume
A4:   a in LSeg(y,z) /\ Sphere(x,r);
    then
A5: a in LSeg(y,z) by XBOOLE_0:def 3;
    assume not a in {y,z};
    then
A6: a in LSeg(y,z) \ {y,z} by A5,XBOOLE_0:def 4;
A7: a in Sphere(x,r) by A4,XBOOLE_0:def 3;
    Ball(x,r) misses Sphere(x,r) by Th19;
    hence contradiction by A3,A6,A7,XBOOLE_0:3;
  end;
  let a be set;
  assume a in {y,z};
  then
A8: a = y or a = z by TARSKI:def 2;
  y in LSeg(y,z) & z in LSeg(y,z) by TOPREAL1:6;
  hence thesis by A1,A2,A8,XBOOLE_0:def 3;
end;

theorem Th36:
  y in Sphere(x,r) & z in Sphere(x,r) implies
  halfline(y,z) /\ Sphere(x,r) = {y,z}
proof
  assume that
A1: y in Sphere(x,r) and
A2: z in Sphere(x,r);
  per cases;
  suppose
A3: y = z;
  then
A4: halfline(y,z) = {y} by Th29;
A5: {y,z} = {y} by A3,ENUMSET1:69;
  hence for m being set st m in halfline(y,z) /\ Sphere(x,r) holds
    m in {y,z} by A4,XBOOLE_0:def 3;
  let m be set;
  assume
A6: m in {y,z};
  then m = y by A5,TARSKI:def 1;
  hence thesis by A1,A4,A5,A6,XBOOLE_0:def 3;
  end;
  suppose
A7: y <> z;
  hereby
    let m be set;
    assume
A8:   m in halfline(y,z) /\ Sphere(x,r);
    then
A9: m in Sphere(x,r) by XBOOLE_0:def 3;
    reconsider w = m as Point of TOP-REAL n by A8;
    m in halfline(y,z) by A8,XBOOLE_0:def 3;
    then consider R being real number such that
A10:   m = (1-R)*y + R*z and
A11:   0 <= R by Th26;
    reconsider R as Real by XREAL_0:def 1;
A12: LSeg(y,z) = {(1-l)*y + l*z where l is Real: 0 <= l & l <= 1}
      by TOPREAL1:def 3;
A13: LSeg(y,w) = {(1-l)*y + l*w where l is Real: 0 <= l & l <= 1}
      by TOPREAL1:def 3;
    per cases by A11,REAL_1:def 5;
    suppose R = 0;
    then m = y by A10,Th4;
    hence m in {y,z} by TARSKI:def 2;
    end;
    suppose R = 1;
    then m = z by A10,Th4;
    hence m in {y,z} by TARSKI:def 2;
    end;
    suppose
A14:   R > 0 & R < 1;
A15: LSeg(y,z) \ {y,z} c= Ball(x,r) by A1,A2,Th34;
A16: w in LSeg(y,z) by A10,A12,A14;
    now
      assume m in {y,z};
      then m = y or m = z by TARSKI:def 2;
      hence contradiction by A7,A10,A14,Th4;
    end;
    then m in LSeg(y,z) \ {y,z} by A16,XBOOLE_0:def 4;
    then |. w-x .| < r by A15,Th7;
    hence m in {y,z} by A9,Th9;
    end;
    suppose
A17:   R > 1;
    then
A18: 1/R < 1 by SQUARE_1:2;
A20: 1/R > 0 by A17,REAL_2:127;
A21: (1-1/R)*y + 1/R*w = (1-1/R)*y + (1/R*((1-R)*y) + 1/R*(R*z)) by A10,
EUCLID:36
      .= (1-1/R)*y + (1/R*((1-R)*y) + 1/R*R*z) by EUCLID:34
      .= (1-1/R)*y + (1/R*((1-R)*y) + 1 * z) by A17,XCMPLX_1:88
      .= (1-1/R)*y + (1/R*((1-R)*y) + z) by EUCLID:33
      .= (1-1/R)*y + 1/R*((1-R)*y) + z by EUCLID:30
      .= (1-1/R)*y + 1/R*(1-R)*y + z by EUCLID:34
      .= (1-1/R+1/R*(1-R))*y + z by EUCLID:37
      .= (1-1/R+1/R*1-1/R*R)*y + z
      .= (1-1/R+1/R*1-1)*y + z by A17,XCMPLX_1:88
      .= 0.REAL n + z by EUCLID:33
      .= z by EUCLID:31;
    then
A22: z in LSeg(y,w) by A13,A18,A20;
    now
      assume z in {y,w};
      then z = y or z = w by TARSKI:def 2;
      hence contradiction by A7,A18,A21,Th4;
    end;
    then
A23: z in LSeg(y,w) \ {y,w} by A22,XBOOLE_0:def 4;
    LSeg(y,w) \ {y,w} c= Ball(x,r) by A1,A9,Th34;
    then |. z-x .| < r by A23,Th7;
    hence m in {y,z} by A2,Th9;
    end;
  end;
  let m be set;
  assume m in {y,z};
  then
A24: m = y or m = z by TARSKI:def 2;
  y in halfline(y,z) & z in halfline(y,z) by Th27,Th28;
  hence thesis by A1,A2,A24,XBOOLE_0:def 3;
  end;
end;

theorem Th37:
  for S, T, X being Element of REAL n st S = y & T = z & X = x holds
  y <> z & y in Ball(x,r) &
  a = (-(2*|(z-y,y-x)|) + sqrt delta
  (Sum sqr (T-S), 2 * |(z-y,y-x)|, Sum sqr (S-X) - r^2)) / (2 * Sum sqr (T-S))
  implies
  ex e being Point of TOP-REAL n st {e} = halfline(y,z) /\ Sphere(x,r) &
  e = (1-a)*y + a*z
proof
  let S, T, X be Element of REAL n such that
T1: S = y & T = z & X = x;
  set s = y, t = z;
  assume that
A1: s <> t and
A2: s in Ball(x,r) and
T2: a = (-(2*|(z-y,y-x)|) + sqrt delta
     (Sum sqr (T-S), 2 * |(z-y,y-x)|, Sum sqr (S-X) - r^2)) /
     (2 * Sum sqr (T-S));
A3: Ball(x,r) c= cl_Ball(x,r) by Th16;
  reconsider G = x as Point of Euclid n by TOPREAL3:13;
  cl_Ball(G,r) = cl_Ball(x,r) by Th14;
  then
A4: 0 <= --r by A2,A3,TOPREAL6:63;
  set A = Sum sqr (T-S);
  set B = 2 * |(t-s,s-x)|;
  set C = Sum sqr (S-X) - r^2;
  set l1 = (- B - sqrt delta(A,B,C)) / (2 * A);
  set l2 = (- B + sqrt delta(A,B,C)) / (2 * A);
A5: |. T-S .| <> 0 by T1,A1,EUCLID:19;
A6:now
    assume A <= 0;
    then A = 0 by RVSUM_1:116;
    hence contradiction by A5,EUCLID:def 5,SQUARE_1:82;
  end;
A7: |. s-x .| < r by A2,Th7;
A8: |. S-X .| = sqrt Sum sqr (S-X) by EUCLID:def 5;
A9: |. S-X .| >= 0 by EUCLID:12;
  t-s = T-S by T1,EUCLID:def 13;
  then
A10: |. t-s .| = |. T-S .| by TOPRNS_1:def 6;
A11: |. T-S .| = sqrt Sum sqr (T-S) by EUCLID:def 5;
  Sum sqr (T-S) >= 0 by RVSUM_1:116;
  then
A12: |. T-S .|^2 = Sum sqr (T-S) by A11,SQUARE_1:def 4;
  Sum sqr (S-X) >= 0 by RVSUM_1:116;
  then
A13: |. S-X .|^2 = Sum sqr (S-X) by A8,SQUARE_1:def 4;
  s-x = S-X by T1,EUCLID:def 13;
  then
A14: |. s-x .| = |. S-X .| by TOPRNS_1:def 6;
  then (sqrt Sum sqr (S-X))^2 < r^2 by A7,A8,A9,SQUARE_1:78;
  then
A15: C < 0 by A8,A13,REAL_2:105;
  then
A16: C/A < 0 by A6,REAL_2:128;
A17: delta(A,B,C) = B^2 - 4*A*C by QUIN_1:def 1;
  4*A > 0 by A6,SQUARE_1:21;
  then
A18: 4*A*C < 0 by A15,SQUARE_1:24;
  B^2 >= 0 by SQUARE_1:72;
  then
A19: B^2 - 4*A*C > 0 by A18,REAL_2:173;
A21: for x being real number holds Poly2(A,B,C,x) = Quard(A,l1,l2,x)
  proof
    let x be real number;
    thus Poly2(A,B,C,x) = A*x^2+B*x+C by POLYEQ_1:def 2
      .= A*(x-l1)*(x-l2) by A6,A17,A19,QUIN_1:16
      .= A*((x-l1)*(x-l2))
      .= Quard(A,l1,l2,x) by POLYEQ_1:def 3;
  end;
  then C/A = l1*l2 by A6,POLYEQ_1:11;
  then
A22: l1 < 0 & l2 > 0 or l1 > 0 & l2 < 0 by A16,REAL_2:132;
  take e1 = (1-l2)*s+l2*t;
  A*l2^2+B*l2--C = A*l2^2+B*l2+C
    .= Poly2(A,B,C,l2) by POLYEQ_1:def 2
    .= Quard(A,l1,l2,l2) by A21
    .= A*((l2-l1)*(l2-l2)) by POLYEQ_1:def 3
    .= 0;
  then -C = A*l2^2+B*l2 by XCMPLX_1:15;
  then
A23: C = -(A*l2^2+B*l2);
A24: e1 in halfline(s,t) by A19,A22,Th26,A6,A17,QUIN_1:27;
A25: |. e1 - x .| >= 0 by TOPRNS_1:26;
A26: l2 is Real by XREAL_0:def 1;
  |. e1 - x .|^2 = |. 1 * s - l2*s + l2*t - x .|^2 by EUCLID:54
   .= |. s - l2*s + l2*t - x .|^2 by EUCLID:33
   .= |. s + l2*t - l2*s - x .|^2 by JORDAN2C:9
   .= |. s + (l2*t - l2*s) - x .|^2 by EUCLID:49
   .= |. s - x + (l2*t - l2*s) .|^2 by JORDAN2C:9
   .= |. s-x + l2*(t-s) .|^2 by EUCLID:53
   .= |. s-x .|^2 + 2*|(l2*(t-s),s-x)| + |. l2*(t-s) .|^2 by EUCLID_2:67
   .= Sum sqr (S-X) + 2*(l2*|(t-s,s-x)|) + |. l2*(t-s) .|^2
      by A13,A14,EUCLID_2:41
   .= Sum sqr (S-X) + 2*l2*|(t-s,s-x)| + (abs(l2)*|. t-s .|)^2
      by A26,TOPRNS_1:8
   .= Sum sqr (S-X) + 2*l2*|(t-s,s-x)| + (abs(l2))^2*|. t-s .|^2
      by SQUARE_1:68
   .= C + r^2 + l2*B + l2^2*A by A10,A12,COMPLEX1:161
   .= r^2 by A23;
  then
A27:  |. e1 - x .| = r or |. e1 - x .| = -r by JGRAPH_3:1;
  r = 0 or r <> 0;
  then
A28: e1 in Sphere(x,r) by A4,A25,A27,Th9,REAL_1:66;
  hereby
    let d be set;
    assume d in {e1};
    then d = e1 by TARSKI:def 1;
    hence d in halfline(s,t) /\ Sphere(x,r) by A24,A28,XBOOLE_0:def 3;
  end;
  hereby
  let d be set;
  assume
A29: d in halfline(s,t) /\ Sphere(x,r);
  then d in halfline(s,t) by XBOOLE_0:def 3;
  then consider l being real number such that
A30:  d = (1-l)*s+l*t and
A31:  0 <= l by Th26;
   l is Real by XREAL_0:def 1;
   then
A32: |. l*(t-s) .|^2 = (abs(l)*|. t-s .|)^2 by TOPRNS_1:8
   .= (abs(l))^2 * |. t-s .|^2 by SQUARE_1:68
   .= l^2 * |. t-s .|^2 by COMPLEX1:161;
  d in Sphere(x,r) by A29,XBOOLE_0:def 3;
  then r = |. (1-l)*s+l*t - x .| by A30,Th9
    .= |. 1 * s - l*s + l*t - x .| by EUCLID:54
    .= |. s - l*s + l*t - x .| by EUCLID:33
    .= |. s - (l*s - l*t) - x .| by EUCLID:51
    .= |. s-x - (l*s - l*t) .| by Th1
    .= |. s-x - l*(s-t) .| by EUCLID:53
    .= |. s-x +- l*(s-t) .| by EUCLID:45
    .= |. s-x + l*(-(s-t)) .| by EUCLID:44
    .= |. s-x + l*(t-s) .| by EUCLID:48;
  then r^2 = |. s-x .|^2 + 2*|(l*(t-s),s-x)| + |. l*(t-s) .|^2 by EUCLID_2:67
    .= |. s-x .|^2 + 2*(l*|(t-s,s-x)|) + |. l*(t-s) .|^2 by EUCLID_2:41;
  then A*l^2+B*l+C = 0 by A10,A12,A13,A14,A32;
  then Poly2(A,B,C,l) = 0 by POLYEQ_1:def 2;
  then l = l1 or l = l2 by A6,A17,A19,POLYEQ_1:5;
  hence d in {e1} by A22,A30,A31,TARSKI:def 1,A19,A6,A17,QUIN_1:27;
  end;
  thus thesis by T2;
end;

theorem Th38:
  for S, T, Y being Element of REAL n st S = 1/2*y + 1/2*z & T = z & Y = x &
  y <> z & y in Sphere(x,r) & z in cl_Ball(x,r)
  ex e being Point of TOP-REAL n st e <> y &
     {y,e} = halfline(y,z) /\ Sphere(x,r) &
     (z in Sphere(x,r) implies e = z) &
     (not z in Sphere(x,r) &
      a = (-(2*|(z-(1/2*y + 1/2*z),1/2*y + 1/2*z-x)|) + sqrt delta
      (Sum sqr (T-S), 2 * |(z-(1/2*y + 1/2*z),1/2*y + 1/2*z-x)|,
      Sum sqr (S-Y) - r^2)) / (2 * Sum sqr (T-S))
      implies e = (1-a)*(1/2*y + 1/2*z) + a*z)
proof
  let S, T, Y be Element of REAL n such that
T1: S = 1/2*y + 1/2*z & T = z & Y = x;
  set s = y, t = z;
  set X = 1/2 * s + 1/2 * t;
  assume that
A1: s <> t and
A2: s in Sphere(x,r) and
A3: t in cl_Ball(x,r);
  reconsider G = x as Point of Euclid n by TOPREAL3:13;
A4: Ball(G,r) = Ball(x,r) by Th13;
A5: Sphere(x,r) c= cl_Ball(x,r) by Th17;
  per cases;
  suppose
A6:  t in Sphere(x,r);
  take e1 = t;
  thus thesis by A1,A2,A6,Th36;
  end;
  suppose
A7:  not t in Sphere(x,r);
A8: 1/2 + 1/2 = 1;
A9: abs(1/2) = 1/2 by ABSVALUE:def 1;
A10: Ball(G,r) = Ball(x,r) by Th13;
  Ball(x,r) \/ Sphere(x,r) = cl_Ball(x,r) by Th18;
  then t in Ball(G,r) by A3,A4,A7,XBOOLE_0:def 2;
  then
A11: X in Ball(G,r) by A2,A5,A8,A9,A10,Th24;
A12: now
     assume X = t;
     then t - 1/2 * s - 1/2 * t = t - t by EUCLID:50
       .= 0.REAL n by EUCLID:46;
     then 0.REAL n = t - 1/2 * t - 1/2 * s by Th1
       .= 1 * t - 1/2 * t - 1/2 * s by EUCLID:33
       .= (1-1/2) * t - 1/2 * s by EUCLID:54
       .= 1/2 * (t-s) by EUCLID:53;
     then t-s = 0.REAL n by EUCLID:35;
     hence contradiction by A1,EUCLID:47;
    end;
  set a = (-(2*|(t-X,X-x)|) + sqrt delta
     (Sum sqr (T-S), 2 * |(t-X,X-x)|, Sum sqr (S-Y) - r^2)) /
     (2 * Sum sqr (T-S));
  consider e1 being Point of TOP-REAL n such that
A13: {e1} = halfline(X,t) /\ Sphere(x,r) and
T3:  e1 = (1-a)*X + a*t by A4,A11,A12,T1,Th37;
  take e1;
A14: e1 in {e1} by TARSKI:def 1;
   then e1 in halfline(X,t) by A13,XBOOLE_0:def 3;
   then consider l being real number such that
A15:  e1 = (1-l)*X + l*t and
A16:  0 <= l by Th26;
  hereby
    assume e1 = s;
    then 0.REAL n = (1-l)*X + l*t - s by A15,EUCLID:46
      .= (1-l)*(1/2*s) + (1-l)*(1/2*t) + l*t - s by EUCLID:36
      .= (1-l)*(1/2*s) + ((1-l)*(1/2*t) + l*t) - s by EUCLID:30
      .= (1-l)*(1/2*s) - s + ((1-l)*(1/2*t) + l*t) by JORDAN2C:9
      .= (1-l)*(1/2*s) - 1 * s + ((1-l)*(1/2*t) + l*t) by EUCLID:33
      .= (1-l)*(1/2)*s - 1 * s + ((1-l)*(1/2*t) + l*t) by EUCLID:34
      .= ((1-l)*(1/2) - 1) * s + ((1-l)*(1/2*t) + l*t) by EUCLID:54
      .= (-1/2 - l*(1/2)) * s + ((1-l)*(1/2)*t + l*t) by EUCLID:34
      .= (-1/2 - l*(1/2)) * s + ((1-l)*(1/2) + l) * t by EUCLID:37
      .= ((-1)*(1/2 + l*(1/2))) * s + (1/2 + l*(1/2)) * t
      .= (-1)*((1/2 + l*(1/2)) * s) + (1/2 + l*(1/2)) * t by EUCLID:34
      .= - (1/2 + l*(1/2)) * s + (1/2 + l*(1/2)) * t by EUCLID:43
      .= (1/2 + l*(1/2)) * t - (1/2 + l*(1/2)) * s by EUCLID:45
      .= (1/2 + l*(1/2)) * (t - s) by EUCLID:53;
    then 1/2 + l*(1/2) = 0 or t - s = 0.REAL n by EUCLID:35;
    then (1+l)*(1/2) = 0 by A1,EUCLID:47;
    then 1+l-1 = 0-1 by XCMPLX_1:6;
    hence contradiction by A16,XCMPLX_1:26;
  end;
A17: s in halfline(s,t) by Th27;
  hereby
    let m be set;
    assume m in {s,e1};
    then
A18: m = s or m = e1 by TARSKI:def 2;
    set o = (1+l)/2;
A19: e1 = (1-l)*(1/2*s) + (1-l)*(1/2*t) + l*t by A15,EUCLID:36
      .= (1-l)*(1/2)*s + (1-l)*(1/2*t) + l*t by EUCLID:34
      .= (1-l)*(1/2)*s + (1-l)*(1/2)*t + l*t by EUCLID:34
      .= (1-l)*(1/2)*s + ((1-l)*(1/2)*t + l*t) by EUCLID:30
      .= (1-l)*(1/2)*s + ((1-l)*(1/2) + l)*t by EUCLID:37
      .= (1-o)*s + o*t;
    1+0 <= 1+l by A16,AXIOMS:24;
    then 0 <= o by SQUARE_1:27;
    then
A20: e1 in halfline(s,t) by A19,Th26;
    e1 in Sphere(x,r) by A13,A14,XBOOLE_0:def 3;
    hence m in halfline(s,t) /\ Sphere(x,r) by A2,A17,A18,A20,XBOOLE_0:def 3;
  end;
  hereby
  let m be set;
  assume
A21: m in halfline(s,t) /\ Sphere(x,r);
  then
A22: m in halfline(s,t) & m in Sphere(x,r) by XBOOLE_0:def 3;
  reconsider m1 = m as Point of TOP-REAL n by A21;
  per cases;
  suppose m in halfline(X,t);
  then m in halfline(X,t) /\ Sphere(x,r) by A22,XBOOLE_0:def 3;
  then m = e1 by A13,TARSKI:def 1;
  hence m in {s,e1} by TARSKI:def 2;
  end;
  suppose
A23: not m in halfline(X,t);
  consider M being real number such that
A24: m = (1-M)*s + M*t and
A25: 0 <= M by A22,Th26;
A26:now
    set o = 2*M-1;
    assume M > 1;
    then 2*M > 2*1 by REAL_1:70;
    then
A27: 2*M-1 > 2*1-1 by REAL_1:92;
    (1-o)*X + o*t = (1-o)*(1/2 * s) + (1-o)*(1/2 * t) + o*t by EUCLID:36
      .= (1-o)*(1/2) * s + (1-o)*(1/2 * t) + o*t by EUCLID:34
      .= (1-o)*(1/2) * s + (1-o)*(1/2) * t + o*t by EUCLID:34
      .= (1-o)*(1/2) * s + ((1-o)*(1/2) * t + o*t) by EUCLID:30
      .= (1-o)*(1/2) * s + ((1-o)*(1/2)+o) * t by EUCLID:37
      .= m by A24;
    hence contradiction by A23,A27,Th26;
  end;
A28: |. t-x .| <= r by A3,Th8;
  |. t-x .| <> r by A7,Th9;
  then |. t-x .| < r by A28,REAL_1:def 5;
  then t in Ball(x,r) by Th7;
  then
A29: LSeg(s,t) /\ Sphere(x,r) = {s} by A2,Th33;
A30: M is Real by XREAL_0:def 1;
  LSeg(s,t) = { (1-d)*s + d*t where d is Real: 0 <= d & d <= 1 }
    by TOPREAL1:def 3;
  then m in LSeg(s,t) by A24,A25,A26,A30;
  then m in {s} by A22,A29,XBOOLE_0:def 3;
  then m = s by TARSKI:def 1;
  hence m in {s,e1} by TARSKI:def 2;
  end;
  end;
  thus thesis by A7,T3;
  end;
end;

registration
  let n, x; let r be negative (real number);
  cluster Sphere(x,r) -> empty;
coherence
proof
  assume not thesis;
  then consider y being Point of TOP-REAL n such that
A1: y in Sphere(x,r) by SUBSET_1:10;
  |. y - x .| = r by A1,Th9;
  hence contradiction by TOPRNS_1:26;
end;
end;

registration
  let n be non empty Nat, x be Point of TOP-REAL n;
  let r be non negative (real number);
  cluster Sphere(x,r) -> non empty;
coherence
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
  per cases;
  suppose r = 0;
  then Sphere(e,r) = {e} by TOPREAL6:62;
  hence thesis by Th15;
  end;
  suppose r > 0;
  then reconsider r as positive (real number);
  consider s being Point of TOP-REAL n such that
A1: s in Ball(x,r) by SUBSET_1:10;
  reconsider S = s, T = s + 1.REAL n, XX = x as Element of REAL n by EUCLID:25;
  set a = (-(2*|(s + 1.REAL n-s,s-x)|) + sqrt delta
     (Sum sqr (T-S), 2 * |(s + 1.REAL n-s,s-x)|, Sum sqr (S-XX) - r^2)) /
     (2 * Sum sqr (T-S));
  s <> s + 1.REAL n by Th3;
  then ex e being Point of TOP-REAL n st
    {e} = halfline(s,s + 1.REAL n) /\ Sphere(x,r) &
    e = (1-a)*s + a*(s+1.REAL n) by A1,Th37;
  hence thesis;
  end;
end;
end;

theorem
  Sphere(x,r) is non empty implies r >= 0
proof
  assume
A1:  Sphere(x,r) is non empty;
  assume r < 0;
  then reconsider r as negative (real number);
  Sphere(x,r) is empty;
  hence thesis by A1;
end;

theorem
  n is non empty & Sphere(x,r) is empty implies r < 0
proof
  assume that
A1: n is non empty and
A2: Sphere(x,r) is empty;
  assume r >= 0;
  then reconsider r as non negative (real number);
  reconsider n as non empty Nat by A1;
  reconsider x as Point of TOP-REAL n;
  Sphere(x,r) is non empty;
  hence thesis by A2;
end;

begin :: Subsets of TOP-REAL 2

reserve
  s, t for Point of TOP-REAL 2;

theorem
  (a*s + b*t)`1 = a * s`1 + b * t`1
proof
  thus (a*s+b*t)`1 = (a*s)`1 + (b*t)`1 by TOPREAL3:7
    .= a*s`1 + (b*t)`1 by TOPREAL3:9
    .= a*s`1+b*t`1 by TOPREAL3:9;
end;

theorem
  (a*s + b*t)`2 = a * s`2 + b * t`2
proof
  thus (a*s+b*t)`2 = (a*s)`2 + (b*t)`2 by TOPREAL3:7
    .= a*s`2 + (b*t)`2 by TOPREAL3:9
    .= a*s`2+b*t`2 by TOPREAL3:9;
end;

theorem Th43:
  t in circle(a,b,r) iff |. t - |[a,b]| .| = r
proof
A1: circle(a,b,r) =
  {x where x is Point of TOP-REAL 2: |. x - |[a,b]| .| = r} by JGRAPH_6:def 5;
  hereby
    assume t in circle(a,b,r);
    then ex x being Point of TOP-REAL 2 st t = x & |. x - |[a,b]| .| = r
      by A1;
    hence |. t - |[a,b]| .| = r;
  end;
  thus thesis by A1;
end;

theorem Th44:
  t in closed_inside_of_circle(a,b,r) iff |. t - |[a,b]| .| <= r
proof
A1: closed_inside_of_circle(a,b,r) =
  {x where x is Point of TOP-REAL 2: |. x - |[a,b]| .| <= r} by JGRAPH_6:def 7;
  hereby
    assume t in closed_inside_of_circle(a,b,r);
    then ex x being Point of TOP-REAL 2 st t = x & |. x - |[a,b]| .| <= r
      by A1;
    hence |. t - |[a,b]| .| <= r;
  end;
  thus thesis by A1;
end;

theorem Th45:
  t in inside_of_circle(a,b,r) iff |. t - |[a,b]| .| < r
proof
A1: inside_of_circle(a,b,r) =
  {x where x is Point of TOP-REAL 2: |. x - |[a,b]| .| < r} by JGRAPH_6:def 6;
  hereby
    assume t in inside_of_circle(a,b,r);
    then ex x being Point of TOP-REAL 2 st t = x & |. x - |[a,b]| .| < r
      by A1;
      hence |. t - |[a,b]| .| < r;
  end;
  thus thesis by A1;
end;

registration
  let a, b be real number;
  let r be positive (real number);
  cluster inside_of_circle(a,b,r) -> non empty;
coherence
proof
  |. |[a,b]| - |[a,b]| .| = 0 by TOPRNS_1:29;
  hence thesis by Th45;
end;
end;

registration
  let a, b be real number;
  let r be non negative (real number);
  cluster closed_inside_of_circle(a,b,r) -> non empty;
coherence
proof
  |. |[a,b]| - |[a,b]| .| = 0 by TOPRNS_1:29;
  hence thesis by Th44;
end;
end;

theorem
  circle(a,b,r) c= closed_inside_of_circle(a,b,r)
proof
  let x be set;
  assume
A1:  x in circle(a,b,r);
  then reconsider x as Point of TOP-REAL 2;
  |. x - |[a,b]| .| = r by A1,Th43;
  hence thesis by Th44;
end;

theorem Th47:
  for x being Point of Euclid 2 st x = |[a,b]| holds
  cl_Ball(x,r) = closed_inside_of_circle(a,b,r)
proof
  let x be Point of Euclid 2 such that
A1: x = |[a,b]|;
  hereby
    let w be set;
    assume
A2:   w in cl_Ball(x,r);
    then reconsider u = w as Point of TOP-REAL 2 by TOPREAL3:13;
    reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
    dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
    then |. u - |[a,b]| .| <= r by A2,METRIC_1:13;
    hence w in closed_inside_of_circle(a,b,r) by Th44;
  end;
  let w be set;
  assume
A3: w in closed_inside_of_circle(a,b,r);
  then reconsider u = w as Point of TOP-REAL 2;
  reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
  dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
  then dist(e,x) <= r by A3,Th44;
  hence w in cl_Ball(x,r) by METRIC_1:13;
end;

theorem Th48:
  for x being Point of Euclid 2 st x = |[a,b]| holds
  Ball(x,r) = inside_of_circle(a,b,r)
proof
  let x be Point of Euclid 2 such that
A1: x = |[a,b]|;
  hereby
    let w be set;
    assume
A2:   w in Ball(x,r);
    then reconsider u = w as Point of TOP-REAL 2 by TOPREAL3:13;
    reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
    dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
    then |. u - |[a,b]| .| < r by A2,METRIC_1:12;
    hence w in inside_of_circle(a,b,r) by Th45;
  end;
  let w be set;
  assume
A3: w in inside_of_circle(a,b,r);
  then reconsider u = w as Point of TOP-REAL 2;
  reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
  dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
  then dist(e,x) < r by A3,Th45;
  hence w in Ball(x,r) by METRIC_1:12;
end;

theorem Th49:
  for x being Point of Euclid 2 st x = |[a,b]| holds
  Sphere(x,r) = circle(a,b,r)
proof
  let x be Point of Euclid 2 such that
A1: x = |[a,b]|;
  hereby
    let w be set;
    assume
A2:   w in Sphere(x,r);
    then reconsider u = w as Point of TOP-REAL 2 by TOPREAL3:13;
    reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
    dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
    then |. u - |[a,b]| .| = r by A2,METRIC_1:14;
    hence w in circle(a,b,r) by Th43;
  end;
  let w be set;
  assume
A3: w in circle(a,b,r);
  then reconsider u = w as Point of TOP-REAL 2;
  reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
  dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
  then dist(e,x) = r by A3,Th43;
  hence w in Sphere(x,r) by METRIC_1:14;
end;

theorem Th50:
  Ball(|[a,b]|,r) = inside_of_circle(a,b,r)
proof
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  thus Ball(|[a,b]|,r) = Ball(e,r) by Th13
    .= inside_of_circle(a,b,r) by Th48;
end;

theorem
  cl_Ball(|[a,b]|,r) = closed_inside_of_circle(a,b,r)
proof
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  thus cl_Ball(|[a,b]|,r) = cl_Ball(e,r) by Th14
    .= closed_inside_of_circle(a,b,r) by Th47;
end;

theorem Th52:
  Sphere(|[a,b]|,r) = circle(a,b,r)
proof
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  thus Sphere(|[a,b]|,r) = Sphere(e,r) by Th15
    .= circle(a,b,r) by Th49;
end;

theorem
  inside_of_circle(a,b,r) c= closed_inside_of_circle(a,b,r)
proof
  let x be set;
  assume
A1: x in inside_of_circle(a,b,r);
  then reconsider x as Point of TOP-REAL 2;
  |. x - |[a,b]| .| < r by A1,Th45;
  hence thesis by Th44;
end;

theorem
  inside_of_circle(a,b,r) misses circle(a,b,r)
proof
  assume not thesis;
  then consider x being set such that
A1: x in inside_of_circle(a,b,r) and
A2: x in circle(a,b,r) by XBOOLE_0:3;
  reconsider x as Point of TOP-REAL 2 by A1;
  |. x - |[a,b]| .| = r by A2,Th43;
  hence thesis by A1,Th45;
end;

theorem
  inside_of_circle(a,b,r) \/ circle(a,b,r) = closed_inside_of_circle(a,b,r)
proof
  reconsider p = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
A1: Sphere(p,r) = circle(a,b,r) by Th49;
A2: Ball(p,r) = inside_of_circle(a,b,r) by Th48;
  cl_Ball(p,r) = closed_inside_of_circle(a,b,r) by Th47;
  hence thesis by A1,A2,METRIC_1:17;
end;

theorem
  s in Sphere(0.REAL 2,r) implies s`1^2 + s`2^2 = r^2
proof
  assume s in Sphere(0.REAL 2,r);
  then |. s-0.REAL 2 .| = r by Th9;
  then |. s .| = r by JORDAN2C:13;
  hence s`1^2 + s`2^2 = r^2 by JGRAPH_1:46;
end;

theorem
  s <> t &
  s in closed_inside_of_circle(a,b,r) & t in closed_inside_of_circle(a,b,r)
  implies r > 0
proof
  reconsider x = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  cl_Ball(x,r) = closed_inside_of_circle(a,b,r) by Th47;
  hence thesis by Th6;
end;

theorem
  for S, T, X being Element of REAL 2 st S = s & T = t & X = |[a,b]| &
  w = (-(2*|(t-s,s-|[a,b]|)|) + sqrt delta
     (Sum sqr (T-S), 2 * |(t-s,s-|[a,b]|)|, Sum sqr (S-X) - r^2)) /
     (2 * Sum sqr (T-S)) &
  s <> t & s in inside_of_circle(a,b,r)
   ex e being Point of TOP-REAL 2 st {e} = halfline(s,t) /\ circle(a,b,r) &
   e = (1-w)*s + w*t
proof
  let S, T, X be Element of REAL 2 such that
T1: S = s & T = t & X = |[a,b]| and
T2: w = (-(2*|(t-s,s-|[a,b]|)|) + sqrt delta
     (Sum sqr (T-S), 2 * |(t-s,s-|[a,b]|)|, Sum sqr (S-X) - r^2)) /
     (2 * Sum sqr (T-S)) and
A1: s <> t and
A2: s in inside_of_circle(a,b,r);
A3: Ball(|[a,b]|,r) = inside_of_circle(a,b,r) by Th50;
  Sphere(|[a,b]|,r) = circle(a,b,r) by Th52;
  hence thesis by A1,A2,A3,T1,T2,Th37;
end;

theorem
  s in circle(a,b,r) & t in inside_of_circle(a,b,r) implies
  LSeg(s,t) /\ circle(a,b,r) = {s}
proof
  assume that
A1: s in circle(a,b,r) and
A2: t in inside_of_circle(a,b,r);
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
A3: circle(a,b,r) = Sphere(e,r) by Th49
     .= Sphere(|[a,b]|,r) by Th15;
  inside_of_circle(a,b,r) = Ball(e,r) by Th48
    .= Ball(|[a,b]|,r) by Th13;
  hence thesis by A1,A2,A3,Th33;
end;

theorem
  s in circle(a,b,r) & t in circle(a,b,r) implies
  LSeg(s,t) \ {s,t} c= inside_of_circle(a,b,r)
proof
  assume that
A1: s in circle(a,b,r) and
A2: t in circle(a,b,r);
  reconsider G = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
A3: Ball(|[a,b]|,r) = inside_of_circle(a,b,r) by Th50;
  Sphere(G,r) = circle(a,b,r) by Th49;
  then Sphere(|[a,b]|,r) = circle(a,b,r) by Th15;
  hence thesis by A1,A2,A3,Th34;
end;

theorem
  s in circle(a,b,r) & t in circle(a,b,r) implies
  LSeg(s,t) /\ circle(a,b,r) = {s,t}
proof
  assume that
A1: s in circle(a,b,r) and
A2: t in circle(a,b,r);
  reconsider G = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  Sphere(G,r) = circle(a,b,r) by Th49;
  then Sphere(|[a,b]|,r) = circle(a,b,r) by Th15;
  hence thesis by A1,A2,Th35;
end;

theorem
  s in circle(a,b,r) & t in circle(a,b,r) implies
  halfline(s,t) /\ circle(a,b,r) = {s,t}
proof
  assume that
A1: s in circle(a,b,r) and
A2: t in circle(a,b,r);
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  circle(a,b,r) = Sphere(e,r) by Th49
    .= Sphere(|[a,b]|,r) by Th15;
  hence thesis by A1,A2,Th36;
end;

theorem
  for S, T, Y being Element of REAL 2 st
    S = 1/2*s + 1/2*t & T = t & Y = |[a,b]| &
    s <> t & s in circle(a,b,r) & t in closed_inside_of_circle(a,b,r)
  ex e being Point of TOP-REAL 2 st e <> s &
   {s,e} = halfline(s,t) /\ circle(a,b,r) &
   (t in Sphere(|[a,b]|,r) implies e = t) &
   (not t in Sphere(|[a,b]|,r) &
     w = (-(2*|(t-(1/2*s + 1/2*t),1/2*s + 1/2*t-|[a,b]|)|) + sqrt delta
     (Sum sqr (T-S), 2 * |(t-(1/2*s + 1/2*t),1/2*s + 1/2*t-|[a,b]|)|,
     Sum sqr (S-Y) - r^2)) / (2 * Sum sqr (T-S))
     implies e = (1-w)*(1/2*s + 1/2*t) + w*t)
proof
  let S, T, Y be Element of REAL 2 such that
T1: S = 1/2*s + 1/2*t & T = t & Y = |[a,b]| and
A1: s <> t and
A2: s in circle(a,b,r) and
A3: t in closed_inside_of_circle(a,b,r);
  reconsider G = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
A4: cl_Ball(G,r) = closed_inside_of_circle(a,b,r) by Th47;
A5: cl_Ball(G,r) = cl_Ball(|[a,b]|,r) by Th14;
  Sphere(G,r) = circle(a,b,r) by Th49;
  then Sphere(|[a,b]|,r) = circle(a,b,r) by Th15;
  hence thesis by A1,A2,A3,A4,A5,T1,Th38;
end;

registration
  let a, b, r be real number;
  cluster inside_of_circle(a,b,r) -> convex;
coherence
proof
  inside_of_circle(a,b,r) =
   {q where q is Point of TOP-REAL 2: |.q - |[a,b]|.| < r } by JGRAPH_6:def 6;
  hence thesis by Lm2;
end;
  cluster closed_inside_of_circle(a,b,r) -> convex;
coherence
proof
  closed_inside_of_circle(a,b,r) =
   {q where q is Point of TOP-REAL 2: |.q - |[a,b]|.| <= r } by JGRAPH_6:def 7;
  hence thesis by Lm1;
end;
end;

