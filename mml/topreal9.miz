:: Intersections of Intervals and Balls in TOP-REAL n
::  by Artur Korni{\l}owicz and Yasunari Shidama
::
:: Received May 10, 2004
:: Copyright (c) 2004 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, XREAL_0, ORDINAL1, PRE_TOPC, EUCLID, ARYTM_1,
      ARYTM_3, SUPINF_2, XBOOLE_0, JORDAN2C, RELAT_1, CARD_1, VALUED_0,
      FINSEQ_1, COMPLEX1, SQUARE_1, CARD_3, RVSUM_1, XXREAL_0, METRIC_1, NAT_1,
      TARSKI, STRUCT_0, REAL_1, RCOMP_1, XXREAL_2, CONVEX1, RLTOPSP1, FUNCT_1,
      UNIALG_1, MSSUBFAM, FUNCOP_1, ORDINAL2, JGRAPH_2, MCART_1, PROB_1,
      FINSEQ_2, FUNCT_3, POLYEQ_1, JGRAPH_6;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, RELSET_1, FUNCT_2, NUMBERS,
      XCMPLX_0, XXREAL_0, XREAL_0, COMPLEX1, REAL_1, NAT_1, FINSEQ_1, FINSEQ_2,
      RVSUM_1, VALUED_0, SQUARE_1, QUIN_1, POLYEQ_1, STRUCT_0, PRE_TOPC,
      METRIC_1, TBSP_1, RLVECT_1, RLTOPSP1, EUCLID, JORDAN2C, JGRAPH_2,
      JGRAPH_6, GRCAT_1;
 constructors REAL_1, SQUARE_1, BINOP_2, COMPLEX1, QUIN_1, FINSEQOP, POLYEQ_1,
      BORSUK_1, TBSP_1, MONOID_0, TOPREAL1, JORDAN1, JORDAN2C, JGRAPH_2,
      JGRAPH_6, VALUED_0, CONVEX1, GRCAT_1;
 registrations XBOOLE_0, XXREAL_0, XREAL_0, SQUARE_1, NAT_1, MEMBERED,
      STRUCT_0, METRIC_1, MONOID_0, EUCLID, TOPALG_2, VALUED_0, FINSEQ_1,
      FUNCT_1, RELAT_1, PRE_TOPC, TBSP_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, RLTOPSP1, JORDAN1, SQUARE_1, EUCLID, VALUED_1,
      XCMPLX_0, ALGSTR_0, STRUCT_0, GRCAT_1;
 theorems JGRAPH_6, TOPRNS_1, XREAL_0, TARSKI, SUBSET_1, FUNCT_2, EUCLID,
      JGRAPH_2, XBOOLE_1, XBOOLE_0, FUNCOP_1, JORDAN2C, JORDAN1, SQUARE_1,
      XCMPLX_1, ABSVALUE, JGRAPH_1, QUIN_1, POLYEQ_1, TOPREAL3, METRIC_1,
      TOPREAL6, RVSUM_1, EUCLID_2, TBSP_1, GOBOARD6, REVROT_1, ENUMSET1,
      COMPLEX1, XREAL_1, XXREAL_0, RLTOPSP1, RLVECT_1, ORDINAL1, GRCAT_1;

begin :: Preliminaries

reserve n for Element of NAT,
  a, b, r, w for real number,
  x, y, z for Point of TOP-REAL n,
  e for Point of Euclid n;

theorem
  x - y - z = x - z - y by JORDAN2C:9;

theorem Th2:
  x + y = x + z implies y = z
proof
  assume x + y = x + z;
  then x - x + y = x + z - x by JORDAN2C:9;
  then x - x = 0.TOP-REAL n & x - x + y = x - x + z by EUCLID:46,JORDAN2C:9;
  hence y = 0.TOP-REAL n + z by EUCLID:31
    .= z by EUCLID:31;
end;

theorem Th3:
  n is non empty implies x <> x + 1.REAL n
proof
A1: 0.REAL n = 0.TOP-REAL n & x = x + 0.TOP-REAL n by EUCLID:31,70;
  assume n is non empty & x = x + 1.REAL n;
  hence thesis by A1,Th2,REVROT_1:19;
end;

theorem Th4:
  for x being set holds x = (1-r)*y + r*z implies (x = y iff r = 0
  or y = z) & (x = z iff r = 1 or y = z)
proof
  let x be set;
  assume
A1: x = (1-r)*y + r*z;
  hereby
    assume x = y;
    then 0.TOP-REAL n = (1-r)*y + r*z - y by A1,EUCLID:46
      .= (1-r)*y - y + r*z by JORDAN2C:9
      .= (1-r)*y - 1 * y + r*z by EUCLID:33
      .= (1-r-1)*y + r*z by EUCLID:54
      .= (-r)*y + r*z
      .= r*z - r*y by EUCLID:44
      .= r*(z-y) by EUCLID:53;
    then r = 0 or z-y = 0.TOP-REAL n by EUCLID:35;
    hence r = 0 or y = z by EUCLID:47;
  end;
  hereby
    assume
A2: r = 0 or y = z;
    per cases by A2;
    suppose
      r = 0;
      hence x = y + 0 * z by A1,EUCLID:33
        .= y + 0.TOP-REAL n by EUCLID:33
        .= y by EUCLID:31;
    end;
    suppose
      z = y;
      hence x = (1-r+r)*y by A1,EUCLID:37
        .= y by EUCLID:33;
    end;
  end;
  hereby
    assume x = z;
    then 0.TOP-REAL n = (1-r)*y + r*z - z by A1,EUCLID:46
      .= (1-r)*y + (r*z - z) by EUCLID:49
      .= (1-r)*y + (r*z + (-1)*z) by EUCLID:43
      .= (1-r)*y + (-1+r)*z by EUCLID:37
      .= (1-r)*y + (-(1-r))*z
      .= (1-r)*y - (1-r)*z by EUCLID:44
      .= (1-r)*(y-z) by EUCLID:53;
    then 1-r+r = 0+r or y-z = 0.TOP-REAL n by EUCLID:35;
    hence r = 1 or y = z by EUCLID:47;
  end;
  assume
A3: r = 1 or y = z;
  per cases by A3;
  suppose
    r = 1;
    hence x = 0.TOP-REAL n + 1 * z by A1,EUCLID:33
      .= 1 * z by EUCLID:31
      .= z by EUCLID:33;
  end;
  suppose
    y = z;
    hence x = (1-r+r)*z by A1,EUCLID:37
      .= z by EUCLID:33;
  end;
end;

theorem Th5:
  for f being real-valued FinSequence holds |.f.|^2 = Sum sqr f
proof
  let f be real-valued FinSequence;
  Sum sqr f >= 0 by RVSUM_1:116;
  hence thesis by SQUARE_1:def 4;
end;

theorem Th6:
  for M being non empty MetrSpace, z1, z2, z3 being Point of M st
  z1 <> z2 & z1 in cl_Ball(z3,r) & z2 in cl_Ball(z3,r) holds r > 0
proof
  let M be non empty MetrSpace, z1, z2, z3 be Point of M such that
A1: z1 <> z2 and
A2: z1 in cl_Ball(z3,r) and
A3: z2 in cl_Ball(z3,r);
  now
    assume r = 0;
    then
A4: cl_Ball(z3,r) = {z3} by TOPREAL6:64;
    then z1 = z3 by A2,TARSKI:def 1;
    hence contradiction by A1,A3,A4,TARSKI:def 1;
  end;
  hence thesis by A2,TOPREAL6:63;
end;

begin :: Subsets of TOP-REAL n

definition
  let n be Nat, x be Point of TOP-REAL n, r be real number;
  func Ball(x,r) -> Subset of TOP-REAL n equals
  {p where p is Point of
  TOP-REAL n: |. p-x .| < r};
  coherence
  proof
    {p where p is Point of TOP-REAL n: |. p-x .| < r} c= the carrier of
    TOP-REAL n
    proof
      let q be set;
      assume q in {p where p is Point of TOP-REAL n: |. p-x .| < r};
      then ex p being Point of TOP-REAL n st q = p & |. p-x .| < r;
      hence thesis;
    end;
    hence thesis;
  end;
  func cl_Ball(x,r) -> Subset of TOP-REAL n equals
  {p where p is Point of
  TOP-REAL n: |. p-x .| <= r};
  coherence
  proof
    {p where p is Point of TOP-REAL n: |. p-x .| <= r} c= the carrier of
    TOP-REAL n
    proof
      let q be set;
      assume q in {p where p is Point of TOP-REAL n: |. p-x .| <= r};
      then ex p being Point of TOP-REAL n st q = p & |. p-x .| <= r;
      hence thesis;
    end;
    hence thesis;
  end;
  func Sphere(x,r) -> Subset of TOP-REAL n equals
  {p where p is Point of
  TOP-REAL n: |. p-x .| = r};
  coherence
  proof
    {p where p is Point of TOP-REAL n: |. p-x .| = r} c= the carrier of
    TOP-REAL n
    proof
      let q be set;
      assume q in {p where p is Point of TOP-REAL n: |. p-x .| = r};
      then ex p being Point of TOP-REAL n st q = p & |. p-x .| = r;
      hence thesis;
    end;
    hence thesis;
  end;
end;

theorem Th7:
  y in Ball(x,r) iff |. y-x .| < r
proof
  hereby
    assume y in Ball(x,r);
    then ex p being Point of TOP-REAL n st y = p & |. p-x .| < r;
    hence |. y-x .| < r;
  end;
  thus thesis;
end;

theorem Th8:
  y in cl_Ball(x,r) iff |. y-x .| <= r
proof
  hereby
    assume y in cl_Ball(x,r);
    then ex p being Point of TOP-REAL n st y = p & |. p-x .| <= r;
    hence |. y-x .| <= r;
  end;
  thus thesis;
end;

theorem Th9:
  y in Sphere(x,r) iff |. y-x .| = r
proof
  hereby
    assume y in Sphere(x,r);
    then ex p being Point of TOP-REAL n st y = p & |. p-x .| = r;
    hence |. y-x .| = r;
  end;
  thus thesis;
end;

theorem
  y in Ball(0.TOP-REAL n,r) implies |.y.| < r
proof
  assume y in Ball(0.TOP-REAL n,r);
  then |. y-0.TOP-REAL n .| < r by Th7;
  hence thesis by RLVECT_1:26;
end;

theorem
  y in cl_Ball(0.TOP-REAL n,r) implies |.y.| <= r
proof
  assume y in cl_Ball(0.TOP-REAL n,r);
  then |. y-0.TOP-REAL n .| <= r by Th8;
  hence thesis by RLVECT_1:26;
end;

theorem
  y in Sphere(0.TOP-REAL n,r) implies |.y.| = r
proof
  assume y in Sphere(0.TOP-REAL n,r);
  then |. y-0.TOP-REAL n .| = r by Th9;
  hence thesis by RLVECT_1:26;
end;

theorem Th13:
  x = e implies Ball(e,r) = Ball(x,r)
proof
  assume
A1: x = e;
  hereby
    let q be set;
    assume
A2: q in Ball(e,r);
    then reconsider f = q as Point of Euclid n;
    reconsider p = f as Point of TOP-REAL n by TOPREAL3:13;
    dist(f,e) < r by A2,METRIC_1:12;
    then |. p-x .| < r by A1,JGRAPH_1:45;
    hence q in Ball(x,r);
  end;
  let q be set;
  assume
A3: q in Ball(x,r);
  then reconsider q as Point of TOP-REAL n;
  reconsider f = q as Point of Euclid n by TOPREAL3:13;
  |. q-x .| < r by A3,Th7;
  then dist(f,e) < r by A1,JGRAPH_1:45;
  hence thesis by METRIC_1:12;
end;

theorem Th14:
  x = e implies cl_Ball(e,r) = cl_Ball(x,r)
proof
  assume
A1: x = e;
  hereby
    let q be set;
    assume
A2: q in cl_Ball(e,r);
    then reconsider f = q as Point of Euclid n;
    reconsider p = f as Point of TOP-REAL n by TOPREAL3:13;
    dist(f,e) <= r by A2,METRIC_1:13;
    then |. p-x .| <= r by A1,JGRAPH_1:45;
    hence q in cl_Ball(x,r);
  end;
  let q be set;
  assume
A3: q in cl_Ball(x,r);
  then reconsider q as Point of TOP-REAL n;
  reconsider f = q as Point of Euclid n by TOPREAL3:13;
  |. q-x .| <= r by A3,Th8;
  then dist(f,e) <= r by A1,JGRAPH_1:45;
  hence thesis by METRIC_1:13;
end;

theorem Th15:
  x = e implies Sphere(e,r) = Sphere(x,r)
proof
  assume
A1: x = e;
  hereby
    let q be set;
    assume
A2: q in Sphere(e,r);
    then reconsider f = q as Point of Euclid n;
    reconsider p = f as Point of TOP-REAL n by TOPREAL3:13;
    dist(f,e) = r by A2,METRIC_1:14;
    then |. p-x .| = r by A1,JGRAPH_1:45;
    hence q in Sphere(x,r);
  end;
  let q be set;
  assume
A3: q in Sphere(x,r);
  then reconsider q as Point of TOP-REAL n;
  reconsider f = q as Point of Euclid n by TOPREAL3:13;
  |. q-x .| = r by A3,Th9;
  then dist(f,e) = r by A1,JGRAPH_1:45;
  hence thesis by METRIC_1:14;
end;

theorem
  Ball(x,r) c= cl_Ball(x,r)
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
  Ball(x,r) = Ball(e,r) & cl_Ball(x,r) = cl_Ball(e,r) by Th13,Th14;
  hence thesis by METRIC_1:15;
end;

theorem Th17:
  Sphere(x,r) c= cl_Ball(x,r)
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
  Sphere(x,r) = Sphere(e,r) & cl_Ball(x,r) = cl_Ball(e,r) by Th14,Th15;
  hence thesis by METRIC_1:16;
end;

theorem Th18:
  Ball(x,r) \/ Sphere(x,r) = cl_Ball(x,r)
proof
  reconsider e = x as Point of Euclid n by TOPREAL3:13;
A1: cl_Ball(x,r) = cl_Ball(e,r) by Th14;
  Sphere(x,r) = Sphere(e,r) & Ball(x,r) = Ball(e,r) by Th13,Th15;
  hence thesis by A1,METRIC_1:17;
end;

theorem Th19:
  Ball(x,r) misses Sphere(x,r)
proof
  assume not thesis;
  then consider q being set such that
A1: q in Ball(x,r) and
A2: q in Sphere(x,r) by XBOOLE_0:3;
  reconsider q as Point of TOP-REAL n by A1;
  |. q - x .| = r by A2,Th9;
  hence thesis by A1,Th7;
end;

registration
  let n be Nat, x be Point of TOP-REAL n;
  let r be non positive (real number);
  cluster Ball(x,r) -> empty;
  coherence
  proof
    assume not thesis;
    then consider y being Point of TOP-REAL n such that
A1: y in Ball(x,r) by SUBSET_1:10;
    n in NAT by ORDINAL1:def 13;
    then |. y - x .| < r by A1,Th7;
    hence contradiction;
  end;
end;

registration
  let n be Nat, x be Point of TOP-REAL n;
  let r be positive (real number);
  cluster Ball(x,r) -> non empty;
  coherence
  proof
    reconsider e = x as Point of Euclid n by TOPREAL3:13;
    n in NAT by ORDINAL1:def 13;
    then Ball(x,r) = Ball(e,r) by Th13;
    hence thesis by GOBOARD6:4;
  end;
end;

theorem
  Ball(x,r) is non empty implies r > 0;

theorem
  Ball(x,r) is empty implies r <= 0;

registration
  let n be Nat, x be Point of TOP-REAL n;
  let r be negative (real number);
  cluster cl_Ball(x,r) -> empty;
  coherence
  proof
    assume not thesis;
    then consider y being Point of TOP-REAL n such that
A1: y in cl_Ball(x,r) by SUBSET_1:10;
    n in NAT by ORDINAL1:def 13;
    then |. y - x .| <= r by A1,Th8;
    hence contradiction;
  end;
end;

registration
  let n be Nat, x be Point of TOP-REAL n;
  let r be non negative (real number);
  cluster cl_Ball(x,r) -> non empty;
  coherence
  proof
A1: n in NAT by ORDINAL1:def 13;
    then |. x-x .| = 0 by TOPRNS_1:29;
    hence thesis by A1,Th8;
  end;
end;

theorem
  cl_Ball(x,r) is non empty implies r >= 0;

theorem
  cl_Ball(x,r) is empty implies r < 0;

theorem Th24:
  a + b = 1 & abs(a) + abs(b) = 1 & b <> 0 & x in cl_Ball(z,r) & y
  in Ball(z,r) implies a * x + b * y in Ball(z,r)
proof
  assume that
A1: a + b = 1 and
A2: abs(a) + abs(b) = 1 and
A3: b <> 0 and
A4: x in cl_Ball(z,r) and
A5: y in Ball(z,r);
  |. y-z .| < r by A5,Th7;
  then
A6: |. z-y .| < r by TOPRNS_1:28;
  |. x-z .| <= r by A4,Th8;
  then 0 <= abs(a) & |. z-x .| <= r by COMPLEX1:132,TOPRNS_1:28;
  then
A7: abs(a)*|. z-x .| <= abs(a)*r by XREAL_1:66;
  0 < abs(b) by A3,COMPLEX1:133;
  then abs(b)*|. z-y .| < abs(b)*r by A6,XREAL_1:70;
  then abs(a)*|. z-x .| + abs(b)*|. z-y .| < abs(a)*r + abs(b)*r by A7,
XREAL_1:10;
  then a is Real & abs(a)*|. z-x .| + abs(b)*|. z-y .| < (abs(a)+abs(b))*r by
XREAL_0:def 1;
  then b is Real & |. a*(z-x) .| + abs(b)*|. z-y .| < 1 * r by A2,TOPRNS_1:8
,XREAL_0:def 1;
  then
A8: |. a*(z-x) .| + |. b*(z-y) .| < r by TOPRNS_1:8;
  |. a*z + b*z - (a*x + b*y) .| = |. a*z - (a*x + b*y) + b*z .| by JORDAN2C:9
    .= |. a*z - a*x - b*y + b*z .| by EUCLID:50
    .= |. a*z - a*x + b*z - b*y .| by JORDAN2C:9
    .= |. a*z - a*x + (b*z - b*y) .| by EUCLID:49
    .= |. a*(z-x) + (b*z - b*y) .| by EUCLID:53
    .= |. a*(z-x) + b*(z-y) .| by EUCLID:53;
  then |. a*z + b*z - (a*x + b*y) .| <= |. a*(z-x) .| + |. b*(z-y) .| by
TOPRNS_1:30;
  then |. a*z + b*z - (a*x + b*y) .| < r by A8,XXREAL_0:2;
  then
A9: |. a*x + b*y - (a*z + b*z) .| < r by TOPRNS_1:28;
  a*z + b*z = (a+b)*z by EUCLID:37
    .= z by A1,EUCLID:33;
  hence thesis by A9;
end;

registration
  let n be Nat, x be Point of TOP-REAL n;
  let r;
  cluster Ball(x,r) -> open;
  coherence
  proof
    reconsider e = x as Point of Euclid n by TOPREAL3:13;
A1: r is Real & Ball(e,r) is bounded by XREAL_0:def 1;
A2: n in NAT by ORDINAL1:def 13;
    then Ball(e,r) = Ball(x,r) by Th13;
    hence thesis by A1,A2,GOBOARD6:6;
  end;
  cluster cl_Ball(x,r) -> closed;
  coherence
  proof
    reconsider e = x as Point of Euclid n by TOPREAL3:13;
A1: n in NAT by ORDINAL1:def 13;
    then cl_Ball(e,r) is bounded & cl_Ball(e,r) = cl_Ball(x,r)
    by Th14,TOPREAL6:67;
    hence thesis by A1,TOPREAL6:66;
  end;
  cluster Sphere(x,r) -> closed;
  coherence
  proof
    reconsider e = x as Point of Euclid n by TOPREAL3:13;
A1: n in NAT by ORDINAL1:def 13;
    then Sphere(e,r) is bounded & Sphere(e,r) = Sphere(x,r) by Th15,TOPREAL6:70
    ;
    hence thesis by A1,TOPREAL6:69;
  end;
end;

registration
  let n,x,r;
  cluster Ball(x,r) -> Bounded;
  coherence
  proof
    reconsider e = x as Point of Euclid n by TOPREAL3:13;
A1: r is Real & Ball(e,r) is bounded by XREAL_0:def 1;
    Ball(e,r) = Ball(x,r) by Th13;
    hence thesis by JORDAN2C:def 2;
  end;
  cluster cl_Ball(x,r) -> Bounded;
  coherence
  proof
    reconsider e = x as Point of Euclid n by TOPREAL3:13;
    cl_Ball(e,r) is bounded & cl_Ball(e,r) = cl_Ball(x,r) by Th14,TOPREAL6:67;
    hence thesis by JORDAN2C:def 2;
  end;
  cluster Sphere(x,r) -> Bounded;
  coherence
  proof
    reconsider e = x as Point of Euclid n by TOPREAL3:13;
    Sphere(e,r) is bounded & Sphere(e,r) = Sphere(x,r) by Th15,TOPREAL6:70;
    hence thesis by JORDAN2C:def 2;
  end;
end;

Lm1: for a being real number, P being Subset of TOP-REAL n, Q being Point of
TOP-REAL n st P = {q where q is Point of TOP-REAL n: |.q - Q.| <= a} holds P is
convex
proof
  let a be real number, P be Subset of TOP-REAL n, Q be Point of TOP-REAL n;
  assume
A1: P = {q where q is Point of TOP-REAL n: |.q - Q.| <= a};
  let p1,p2 be Point of TOP-REAL n;
  assume p1 in P;
  then
A2: ex q1 being Point of TOP-REAL n st q1=p1 & |.q1-Q.| <= a by A1;
  assume
A3: p2 in P;
  then
A4: ex q2 being Point of TOP-REAL n st q2=p2 & |.q2-Q.| <= a by A1;
  let x be set;
  assume
A5: x in LSeg(p1,p2);
  then consider r being Real such that
A6: x = (1-r)*p1+r*p2 and
A7: 0 <= r and
A8: r <= 1;
A9: r = abs(r) by A7,ABSVALUE:def 1;
  reconsider q = x as Point of TOP-REAL n by A5;
A10: |.(1-r)*(p1-Q).| = abs(1-r)*|.p1-Q.| by TOPRNS_1:8;
A11: 1-r >= 0 by A8,XREAL_1:50;
  then
A12: abs(1-r) = 1-r by ABSVALUE:def 1;
  per cases;
  suppose
A13: 1-r > 0;
    0 <= abs(r) by COMPLEX1:132;
    then
A14: |.r*(p2-Q).| = abs(r)*|.p2-Q.| & abs(r)*|.p2-Q.| <= abs(r)*a by A4,
TOPRNS_1:8,XREAL_1:66;
    (1-r)*Q + r*Q = 1 * Q - r*Q + r*Q by EUCLID:54
      .= 1 * Q by EUCLID:52
      .= Q by EUCLID:33;
    then q-Q = (1-r)*p1+r*p2-(1-r)*Q-r*Q by A6,EUCLID:50
      .= (1-r)*p1+-(1-r)*Q+r*p2-r*Q by EUCLID:30
      .= (1-r)*p1-(1-r)*Q+r*p2-r*Q
      .= (1-r)*(p1-Q)+r*p2-r*Q by EUCLID:53
      .= (1-r)*(p1-Q)+(r*p2-r*Q) by EUCLID:49
      .= (1-r)*(p1-Q)+r*(p2-Q) by EUCLID:53;
    then
A15: |.q-Q.| <= |.(1-r)*(p1-Q).| + |.r*(p2-Q).| by TOPRNS_1:30;
    abs(1-r)*|.p1-Q.| <= abs(1-r)*a by A2,A12,A13,XREAL_1:66;
    then |.(1-r)*(p1-Q).|+|.r*(p2-Q).| <= (1-r)*a+r*a by A9,A10,A12,A14,
XREAL_1:9;
    then |.q-Q.| <= a by A15,XXREAL_0:2;
    hence thesis by A1;
  end;
  suppose
    1-r <= 0;
    then 1-r+r = 0+r by A11;
    hence thesis by A3,A6,Th4;
  end;
end;

Lm2: for a being real number, P being Subset of TOP-REAL n, Q being Point of
TOP-REAL n st P = {q where q is Point of TOP-REAL n: |.q - Q.| < a} holds P is
convex
proof
  let a be real number, P be Subset of TOP-REAL n, Q be Point of TOP-REAL n;
  assume
A1: P = {q where q is Point of TOP-REAL n: |.q - Q.| < a};
  reconsider e = Q as Point of Euclid n by TOPREAL3:13;
  let p1,p2 be Point of TOP-REAL n;
  assume
A2: p1 in P & p2 in P;
  Ball(e,a) = Ball(Q,a) by Th13
    .= P by A1;
  hence thesis by A2,TOPREAL3:28;
end;
 
:: Convex subsets of TOP-REAL n

registration
  let n be Nat, x be Point of TOP-REAL n;
  let r;
  cluster Ball(x,r) -> convex;
  coherence
  proof
    n in NAT by ORDINAL1:def 13;
    hence thesis by Lm2;
  end;
  cluster cl_Ball(x,r) -> convex;
  coherence
  proof
    n in NAT by ORDINAL1:def 13;
    hence thesis by Lm1;
  end;
end;

definition
  let n be Nat;
  let f be Function of TOP-REAL n, TOP-REAL n;
  attr f is homogeneous means
  :Def4:
  for r being real number, x being Point of TOP-REAL n holds f.(r*x) = r * f.x;
 canceled;
end;

registration
  let n;
  cluster TOP-REAL n --> 0.TOP-REAL n -> homogeneous additive;
  coherence
  proof
    set f = TOP-REAL n --> 0.TOP-REAL n;
    thus f is homogeneous
    proof
      let r be real number, x be Point of TOP-REAL n;
      thus f.(r*x) = 0.TOP-REAL n by FUNCOP_1:13
        .= r * 0.TOP-REAL n by EUCLID:32
        .= r * f.x by FUNCOP_1:13;
    end;
    let x, y be Point of TOP-REAL n;
    thus f.(x+y) = 0.TOP-REAL n by FUNCOP_1:13
      .= 0.TOP-REAL n + 0.TOP-REAL n by EUCLID:31
      .= f.x + 0.TOP-REAL n by FUNCOP_1:13
      .= f.x + f.y by FUNCOP_1:13;
  end;
end;

registration
  let n;
  cluster homogeneous additive continuous Function of TOP-REAL n, TOP-REAL n;
  existence
  proof
    take TOP-REAL n --> 0.TOP-REAL n;
    thus thesis;
  end;
end;

registration
  let a, c be real number;
  cluster AffineMap(a,0,c,0) -> homogeneous additive;
  coherence
  proof
    set f = AffineMap(a,0,c,0);
    hereby
      let r be real number, x be Point of TOP-REAL 2;
A1:   (r*x)`1 = r*x`1 & (r*x)`2 = r*x`2 by TOPREAL3:9;
      thus f.(r*x) = |[a*((r*x)`1)+0,c*((r*x)`2)+0]| by JGRAPH_2:def 2
        .= |[r*(a*(x`1)),r*(c*(x`2))]| by A1
        .= r * |[a*(x`1)+0,c*(x`2)+0]| by EUCLID:62
        .= r * f.x by JGRAPH_2:def 2;
    end;
    let x, y be Point of TOP-REAL 2;
A2: (x+y)`1 = x`1 + y`1 & (x+y)`2 = x`2 + y`2 by TOPREAL3:7;
    thus f.(x+y) = |[a*((x+y)`1)+0,c*((x+y)`2)+0]| by JGRAPH_2:def 2
      .= |[a*(x`1)+a*(y`1),c*(x`2)+c*(y`2)]| by A2
      .= |[a*(x`1)+0,c*(x`2)+0]| + |[a*(y`1),c*(y`2)]| by EUCLID:60
      .= f.x + |[a*(y`1)+0,c*(y`2)+0]| by JGRAPH_2:def 2
      .= f.x + f.y by JGRAPH_2:def 2;
  end;
end;

theorem
  for f being homogeneous additive Function of TOP-REAL n, TOP-REAL n, X
  being convex Subset of TOP-REAL n holds f.:X is convex
proof
  let f be homogeneous additive Function of TOP-REAL n, TOP-REAL n, X be
  convex Subset of TOP-REAL n;
  let p, q be Point of TOP-REAL n;
  assume p in f.:X;
  then consider p0 being Point of TOP-REAL n such that
A1: p0 in X and
A2: p = f.p0 by FUNCT_2:116;
  assume q in f.:X;
  then consider q0 being Point of TOP-REAL n such that
A3: q0 in X and
A4: q = f.q0 by FUNCT_2:116;
A5: LSeg(p0,q0) c= X by A1,A3,JORDAN1:def 1;
  let x be set;
  assume x in LSeg(p,q);
  then consider l being Real such that
A6: x = (1-l)*p + l*q and
A7: 0 <= l & l <= 1;
  set a = (1-l)*p0 + l*q0;
A8: a in LSeg(p0,q0) by A7;
  f.a = f.((1-l)*p0) + f.(l*q0) by GRCAT_1:def 13
    .= f.((1-l)*p0) + l*f.q0 by Def4
    .= x by A2,A4,A6,Def4;
  hence thesis by A8,A5,FUNCT_2:43;
end;
 
:: Halflines

reserve p, q for Point of TOP-REAL n;

definition
  let n be Nat;
  let p, q be Point of TOP-REAL n;
  func halfline(p,q) -> Subset of TOP-REAL n equals
  { (1-l)*p + l*q where l is Real: 0 <= l };
  coherence
  proof
    set X = { (1-l)*p + l*q where l is Real: 0 <= l };
    X c= the carrier of TOP-REAL n
    proof
      let x be set;
      assume x in X;
      then ex l being Real st x = (1-l)*p + l*q & 0 <= l;
      hence thesis;
    end;
    hence thesis;
  end;
end;

theorem Th26:
  for x being set holds x in halfline(p,q) iff ex l being real
  number st x = (1-l)*p + l*q & 0 <= l
proof
  let x be set;
  hereby
    assume x in halfline(p,q);
    then ex l being Real st x = (1-l)*p + l*q & 0 <= l;
    hence ex l being real number st x = (1-l)*p + l*q & 0 <= l;
  end;
  given l being real number such that
A1: x = (1-l)*p + l*q & 0 <= l;
  l is Real by XREAL_0:def 1;
  hence thesis by A1;
end;

registration
  let n, p, q;
  cluster halfline(p,q) -> non empty;
  coherence
  proof
    (1-0)*p + 0 * q in halfline(p,q);
    hence thesis;
  end;
end;

theorem Th27:
  p in halfline(p,q)
proof
  (1-0)*p + 0 * q = p + 0 * q by EUCLID:33
    .= p + 0.TOP-REAL n by EUCLID:33
    .= p by EUCLID:31;
  hence thesis;
end;

theorem Th28:
  q in halfline(p,q)
proof
  (1-1)*p + 1 * q = 0 * p + q by EUCLID:33
    .= 0.TOP-REAL n + q by EUCLID:33
    .= q by EUCLID:31;
  hence thesis;
end;

theorem Th29:
  halfline(p,p) = {p}
proof
  hereby
    let d be set;
    assume d in halfline(p,p);
    then ex r being real number st d = (1-r)*p+r*p & 0 <= r by Th26;
    then d = p by Th4;
    hence d in {p} by TARSKI:def 1;
  end;
  let d be set;
  assume d in {p};
  then d = p by TARSKI:def 1;
  hence thesis by Th27;
end;

theorem Th30:
  x in halfline(p,q) implies halfline(p,x) c= halfline(p,q)
proof
  assume x in halfline(p,q);
  then consider R being real number such that
A1: x = (1-R)*p + R*q and
A2: 0 <= R by Th26;
  let d be set;
  assume
A3: d in halfline(p,x);
  then reconsider d as Point of TOP-REAL n;
  consider r being real number such that
A4: d = (1-r)*p + r*x and
A5: 0 <= r by A3,Th26;
  set o = r*R;
  d = (1-r)*p + (r*((1-R)*p) + r*(R*q)) by A1,A4,EUCLID:36
    .= (1-r)*p + r*((1-R)*p) + r*(R*q) by EUCLID:30
    .= (1-r)*p + r*(1-R)*p + r*(R*q) by EUCLID:34
    .= ((1-r) + r*(1-R))*p + r*(R*q) by EUCLID:37
    .= (1-o)*p + o*q by EUCLID:34;
  hence thesis by A2,A5,Th26;
end;

theorem
  x in halfline(p,q) & x <> p implies halfline(p,q) = halfline(p,x)
proof
  assume
A1: x in halfline(p,q);
  then consider R being real number such that
A2: x = (1-R)*p + R*q and
A3: 0 <= R by Th26;
  assume
A4: x <> p;
  thus halfline(p,q) c= halfline(p,x)
  proof
    let d be set;
    assume
A5: d in halfline(p,q);
    then reconsider d as Point of TOP-REAL n;
    consider r being real number such that
A6: d = (1-r)*p + r*q and
A7: 0 <= r by A5,Th26;
    set o = r/R;
    R <> 0 by A2,A4,Th4;
    then o*R = r by XCMPLX_1:88;
    then d = (1-o + o*(1-R))*p + o*(R*q) by A6,EUCLID:34
      .= (1-o)*p + o*(1-R)*p + o*(R*q) by EUCLID:37
      .= (1-o)*p + o*((1-R)*p) + o*(R*q) by EUCLID:34
      .= (1-o)*p + (o*((1-R)*p) + o*(R*q)) by EUCLID:30
      .= (1-o)*p + o*((1-R)*p + R*q) by EUCLID:36;
    hence thesis by A2,A3,A7,Th26;
  end;
  thus thesis by A1,Th30;
end;

theorem
  LSeg(p,q) c= halfline(p,q)
proof
  let a be set;
  assume a in LSeg(p,q);
  then ex r being Real st 0<=r & r<=1 & a = (1-r)*p+r*q by JGRAPH_1:52;
  hence thesis;
end;

registration
  let n, p, q;
  cluster halfline(p,q) -> convex;
  coherence
  proof
    let u, v be Point of TOP-REAL n;
    set P = halfline(p,q);
    assume u in P;
    then consider a being real number such that
A1: u = (1-a)*p+a*q and
A2: 0 <= a by Th26;
    assume v in P;
    then consider b being real number such that
A3: v = (1-b)*p+b*q and
A4: 0 <= b by Th26;
    let x be set;
    assume x in LSeg(u,v);
    then consider r being Real such that
A5: 0 <= r and
A6: r <= 1 and
A7: x = (1-r)*u+r*v by JGRAPH_1:52;
    set o = (1-r)*a+r*b;
A8: 1-r >= r-r by A6,XREAL_1:15;
    x = (1-r)*((1-a)*p)+(1-r)*(a*q) + r*((1-b)*p+b*q) by A1,A3,A7,EUCLID:36
      .= (1-r)*((1-a)*p) + (1-r)*(a*q) + (r*((1-b)*p) + r*(b*q)) by EUCLID:36
      .= (1-r)*((1-a)*p) + (r*((1-b)*p) + r*(b*q)) + (1-r)*(a*q) by EUCLID:30
      .= (1-r)*((1-a)*p) + r*((1-b)*p) + r*(b*q) + (1-r)*(a*q) by EUCLID:30
      .= (1-r)*(1-a)*p + r*((1-b)*p) + r*(b*q) + (1-r)*(a*q) by EUCLID:34
      .= (1-r)*(1-a)*p + r*(1-b)*p + r*(b*q) + (1-r)*(a*q) by EUCLID:34
      .= (1-r)*(1-a)*p + r*(1-b)*p + r*b*q + (1-r)*(a*q) by EUCLID:34
      .= (1-r)*(1-a)*p + r*(1-b)*p + r*b*q + (1-r)*a*q by EUCLID:34
      .= ((1-r)*(1-a) + r*(1-b)) * p + r*b*q + (1-r)*a*q by EUCLID:37
      .= ((1-r)*(1-a) + r*(1-b)) * p + (r*b*q + (1-r)*a*q) by EUCLID:30
      .= (1-o)*p + o*q by EUCLID:37;
    hence thesis by A2,A4,A5,A8;
  end;
end;

theorem Th33:
  y in Sphere(x,r) & z in Ball(x,r) implies LSeg(y,z) /\ Sphere(x, r) = {y}
proof
  set s = y, t = z;
  assume that
A1: s in Sphere(x,r) and
A2: t in Ball(x,r);
  hereby
    let m be set;
    assume
A3: m in LSeg(s,t) /\ Sphere(x,r);
    then reconsider w = m as Point of TOP-REAL n;
    w in LSeg(s,t) by A3,XBOOLE_0:def 4;
    then consider d being Real such that
A4: 0 <= d and
A5: d <= 1 and
A6: w = (1-d)*s + d*t by JGRAPH_1:52;
A7: |. d*(t-x) .| = abs(d)*|. t-x .| by TOPRNS_1:8
      .= d*|. t-x .| by A4,ABSVALUE:def 1;
    d-1 <= 1-1 by A5,XREAL_1:11;
    then
A8: -(0 qua Element of NAT) <= -(d-1);
A9: |. (1-d)*(s-x) .| = abs(1-d)*|. s-x .| by TOPRNS_1:8
      .= (1-d)*|. s-x .| by A8,ABSVALUE:def 1
      .= (1-d)*r by A1,Th9;
    m in Sphere(x,r) by A3,XBOOLE_0:def 4;
    then
A10: r = |. w - x .| by Th9
      .= |. (1-d)*s + d*t - (1-d+d)*x .| by A6,EUCLID:33
      .= |. (1-d)*s + d*t - ((1-d)*x + d*x) .| by EUCLID:37
      .= |. (1-d)*s + d*t - (1-d)*x - d*x .| by EUCLID:50
      .= |. (1-d)*s - (1-d)*x + d*t - d*x .| by JORDAN2C:9
      .= |. (1-d)*(s-x) + d*t - d*x .| by EUCLID:53
      .= |. (1-d)*(s-x) + (d*t - d*x) .| by EUCLID:49
      .= |. (1-d)*(s-x) + d*(t-x) .| by EUCLID:53;
    per cases by A4;
    suppose
A11:  d > 0;
      |. t-x .| < r by A2,Th7;
      then d*|. t-x .| < d*r by A11,XREAL_1:70;
      then (1-d)*r + d*|. t-x .| < (1-d)*r + d*r by XREAL_1:10;
      hence m in {s} by A10,A9,A7,TOPRNS_1:30;
    end;
    suppose
      d = 0;
      then m = 1 * s + 0.TOP-REAL n by A6,EUCLID:33
        .= 1 * s by EUCLID:31
        .= s by EUCLID:33;
      hence m in {s} by TARSKI:def 1;
    end;
  end;
  let m be set;
A12: s in LSeg(s,t) by RLTOPSP1:69;
  assume m in {s};
  then m = s by TARSKI:def 1;
  hence thesis by A1,A12,XBOOLE_0:def 4;
end;

theorem Th34:
  y in Sphere(x,r) & z in Sphere(x,r) implies LSeg(y,z) \ {y,z} c= Ball(x,r)
proof
  assume that
A1: y in Sphere(x,r) and
A2: z in Sphere(x,r);
  per cases;
  suppose
    y = z;
    then LSeg(y,z) = {y} & {y,z} = {y} by ENUMSET1:69,RLTOPSP1:71;
    then LSeg(y,z) \ {y,z} = {} by XBOOLE_1:37;
    hence thesis by XBOOLE_1:2;
  end;
  suppose
A3: y <> z;
    the carrier of TOP-REAL n = REAL n by EUCLID:25
      .= n-tuples_on REAL;
    then reconsider xf = x, yf = y, zf = z as Element of n-tuples_on REAL;
    reconsider yyf = yf, zzf = zf, xxf = xf as Element of REAL n;
    reconsider y1 = y-x, z1 = z-x as FinSequence of REAL by EUCLID:27;
    set X = |(y-x,z-x)|;
    let a be set;
A4: z-x = zzf-xxf by EUCLID:73;
A5: Sum sqr (zf-xf) = Sum sqr (zzf-xxf) .= |.z1.|^2 by A4,Th5;
A6: |. z-x .|^2 = r^2 by A2,Th9;
    assume
A7: a in LSeg(y,z) \ {y,z};
    then reconsider R = a as Point of TOP-REAL n;
A8: R in LSeg(y,z) by A7,XBOOLE_0:def 5;
    then consider l being Real such that
A9: 0 <= l and
A10: l <= 1 and
A11: R = (1-l)*y + l*z by JGRAPH_1:52;
    set l1 = 1-l;
    reconsider W1 = l1*(y-x), W2 = l*(z-x) as Element of REAL n by EUCLID:25;
A12: Sum sqr (yf-zf) >= 0 by RVSUM_1:116;
    reconsider Rf=R-x as FinSequence of REAL by EUCLID:27;
A13: y-x = yyf-xxf by EUCLID:73;
A14: W1 + W2 = l1*(y-x)+l*(z-x) by EUCLID:68
      .= l1*y-l1*x+l*(z-x) by EUCLID:53
      .= l1*y-l1*x+(l*z-l*x) by EUCLID:53
      .= l1*y-l1*x+l*z-l*x by EUCLID:49
      .= l1*y+l*z-l1*x-l*x by JORDAN2C:9
      .= l1*y+l*z-(l1*x+l*x) by EUCLID:50
      .= l1*y+l*z-(1 *x-l*x+l*x) by EUCLID:54
      .= l1*y+l*z-1 *x by EUCLID:52
      .= Rf by A11,EUCLID:33;
    reconsider W1, W2 as Element of n-tuples_on REAL;
A15: mlt(W1,W2) = (mlt(l1*(y-x),l*(z-x)))
      .= (mlt(l1*(y-x),l*(zzf-xxf))) by A4,EUCLID:69
      .= (mlt(l1*(yyf-xxf),l*(zzf-xxf))) by A13,EUCLID:69
      .= (l1*mlt(yf-xf,l*(zf-xf))) by RVSUM_1:94;
A16: sqr W2 = sqr(l*(z-x)) .= sqr(l*(zf-xf)) by A4,EUCLID:69;
A17: sqr W1 = sqr(l1*(y-x)) .= sqr(l1*(yf-xf)) by A13,EUCLID:69
      .= (l1^2*sqr (yf-xf)) by RVSUM_1:84;
    Sum sqr Rf >= 0 by RVSUM_1:116;
    then |. R-x .|^2 = Sum sqr Rf by SQUARE_1:def 4
      .= Sum (sqr W1 + 2*mlt(W1,W2) + sqr W2) by A14,RVSUM_1:98
      .= Sum (sqr W1 + 2*mlt(W1,W2)) + Sum sqr W2 by RVSUM_1:119
      .= Sum sqr W1 + Sum (2*mlt(W1,W2)) + Sum sqr W2 by RVSUM_1:119
      .= Sum (l1^2*sqr (yf-xf)) + Sum (2*mlt(W1,W2)) + Sum sqr (l*(zf-xf))
    by A17,A16
      .= l1^2*Sum sqr (yf-xf) + Sum (2*mlt(W1,W2)) + Sum sqr (l*(zf-xf)) by
RVSUM_1:117
      .= l1^2*Sum sqr (yf-xf) + Sum (2*mlt(W1,W2)) + Sum (l^2*sqr (zf-xf))
    by RVSUM_1:84
      .= l1^2*Sum sqr (yf-xf) + Sum (2*mlt(W1,W2)) + l^2*Sum sqr (zf-xf) by
RVSUM_1:117
      .= l1^2*|.y1.|^2 + Sum (2*mlt(W1,W2)) + l^2*Sum sqr (zf-xf) by A13,Th5
      .= l1^2*r^2 + Sum (2*mlt(W1,W2)) + l^2*|.z1.|^2 by A1,A5,Th9
      .= l1^2*r^2 + 2*Sum mlt(W1,W2) + l^2*r^2 by A6,RVSUM_1:117
      .= l1^2*r^2 + 2*Sum (l1*mlt(yf-xf,l*(zf-xf))) + l^2*r^2 by A15
      .= l1^2*r^2 + 2*Sum (l1*(l*mlt(yf-xf,zf-xf))) + l^2*r^2 by RVSUM_1:94
      .= l1^2*r^2 + 2*Sum (l1*l*mlt(yf-xf,zf-xf)) + l^2*r^2 by RVSUM_1:71
      .= l1^2*r^2 + 2*(l1*l*Sum mlt(yf-xf,zf-xf)) + l^2*r^2 by RVSUM_1:117
      .= l1^2*r^2 + 2*l1*l*Sum mlt(yf-xf,zf-xf) + l^2*r^2
      .= l1^2*r^2 + 2*l1*l*X + l^2*r^2 by A13,A4,RVSUM_1:def 17
      .= (1-2*l+l^2+l^2)*r^2 + 2*l*l1*X;
    then
A18: |. R-x .|^2 - r^2 = 2*l*l1*(-r^2+X);
A19: yyf-zzf =y-z by EUCLID:73;
    now
      assume l = 0;
      then R = y by A11,Th4;
      then R in {y,z} by TARSKI:def 2;
      hence contradiction by A7,XBOOLE_0:def 5;
    end;
    then
A20: 2*l > 0 by A9,XREAL_1:131;
A21: now
      assume l1 = 0;
      then R = z by A11,Th4;
      then R in {y,z} by TARSKI:def 2;
      hence contradiction by A7,XBOOLE_0:def 5;
    end;
    1-1 <= l1 by A10,XREAL_1:15;
    then
A22: 2*l*l1 > 0 by A20,A21,XREAL_1:131;
A23: |. y-x .|^2 = r^2 by A1,Th9;
A24: now
      assume |. R-x .| = r;
      then X-r^2 = 0 by A18,A22,XCMPLX_1:6;
      then 0 = |. y-x .|^2 - 2*X + |. z-x .|^2 by A2,A23,Th9
        .= |. y-x - (z-x) .|^2 by EUCLID_2:68
        .= |. y-x-z+x .|^2 by EUCLID:51
        .= |. y-x+x-z .|^2 by JORDAN2C:9
        .= |.yf-zf.|^2 by A19,EUCLID:52
        .= Sum sqr (yf-zf) by A12,SQUARE_1:def 4;
      then yf-zf = n |-> 0 by RVSUM_1:121;
      hence contradiction by A3,RVSUM_1:59;
    end;
    Sphere(x,r) c= cl_Ball(x,r) by Th17;
    then LSeg(y,z) c= cl_Ball(x,r) by A1,A2,JORDAN1:def 1;
    then |. R-x .| <= r by A8,Th8;
    then |. R-x .| < r by A24,XXREAL_0:1;
    hence thesis;
  end;
end;

theorem Th35:
  y in Sphere(x,r) & z in Sphere(x,r) implies LSeg(y,z) /\ Sphere( x,r) = {y,z}
proof
A1: y in LSeg(y,z) & z in LSeg(y,z) by RLTOPSP1:69;
  assume
A2: y in Sphere(x,r) & z in Sphere(x,r);
  then
A3: LSeg(y,z) \ {y,z} c= Ball(x,r) by Th34;
  hereby
    let a be set;
    assume
A4: a in LSeg(y,z) /\ Sphere(x,r);
    assume
A5: not a in {y,z};
    a in LSeg(y,z) by A4,XBOOLE_0:def 4;
    then
A6: a in LSeg(y,z) \ {y,z} by A5,XBOOLE_0:def 5;
A7: Ball(x,r) misses Sphere(x,r) by Th19;
    a in Sphere(x,r) by A4,XBOOLE_0:def 4;
    hence contradiction by A3,A6,A7,XBOOLE_0:3;
  end;
  let a be set;
  assume a in {y,z};
  then a = y or a = z by TARSKI:def 2;
  hence thesis by A2,A1,XBOOLE_0:def 4;
end;

theorem Th36:
  y in Sphere(x,r) & z in Sphere(x,r) implies halfline(y,z) /\
  Sphere(x,r) = {y,z}
proof
  assume that
A1: y in Sphere(x,r) and
A2: z in Sphere(x,r);
  per cases;
  suppose
A3: y = z;
    then
A4: {y,z} = {y} by ENUMSET1:69;
A5: halfline(y,z) = {y} by A3,Th29;
    hence
    for m being set st m in halfline(y,z) /\ Sphere(x,r) holds m in {y,z}
    by A4,XBOOLE_0:def 4;
    let m be set;
    assume
A6: m in {y,z};
    then m = y by A4,TARSKI:def 1;
    hence thesis by A1,A5,A4,A6,XBOOLE_0:def 4;
  end;
  suppose
A7: y <> z;
    hereby
      let m be set;
      assume
A8:   m in halfline(y,z) /\ Sphere(x,r);
      then
A9:   m in Sphere(x,r) by XBOOLE_0:def 4;
      reconsider w = m as Point of TOP-REAL n by A8;
      m in halfline(y,z) by A8,XBOOLE_0:def 4;
      then consider R being real number such that
A10:  m = (1-R)*y + R*z and
A11:  0 <= R by Th26;
      reconsider R as Real by XREAL_0:def 1;
      per cases by A11,XXREAL_0:1;
      suppose
        R = 0;
        then m = y by A10,Th4;
        hence m in {y,z} by TARSKI:def 2;
      end;
      suppose
        R = 1;
        then m = z by A10,Th4;
        hence m in {y,z} by TARSKI:def 2;
      end;
      suppose
A12:    R > 0 & R < 1;
A13:    now
          assume m in {y,z};
          then m = y or m = z by TARSKI:def 2;
          hence contradiction by A7,A10,A12,Th4;
        end;
        w in LSeg(y,z) by A10,A12;
        then
A14:    m in LSeg(y,z) \ {y,z} by A13,XBOOLE_0:def 5;
        LSeg(y,z) \ {y,z} c= Ball(x,r) by A1,A2,Th34;
        then |. w-x .| < r by A14,Th7;
        hence m in {y,z} by A9,Th9;
      end;
      suppose
A15:    R > 1;
        then
A16:    1/R < 1 by XREAL_1:214;
A17:    (1-1/R)*y + 1/R*w = (1-1/R)*y + (1/R*((1-R)*y) + 1/R*(R*z)) by A10,
EUCLID:36
          .= (1-1/R)*y + (1/R*((1-R)*y) + 1/R*R*z) by EUCLID:34
          .= (1-1/R)*y + (1/R*((1-R)*y) + 1 * z) by A15,XCMPLX_1:88
          .= (1-1/R)*y + (1/R*((1-R)*y) + z) by EUCLID:33
          .= (1-1/R)*y + 1/R*((1-R)*y) + z by EUCLID:30
          .= (1-1/R)*y + 1/R*(1-R)*y + z by EUCLID:34
          .= (1-1/R+1/R*(1-R))*y + z by EUCLID:37
          .= (1-1/R+1/R*1-1/R*R)*y + z
          .= (1-1/R+1/R*1-1)*y + z by A15,XCMPLX_1:88
          .= 0.TOP-REAL n + z by EUCLID:33
          .= z by EUCLID:31;
A18:    now
          assume z in {y,w};
          then z = y or z = w by TARSKI:def 2;
          hence contradiction by A7,A16,A17,Th4;
        end;
        z in LSeg(y,w) by A15,A16,A17;
        then
A19:    z in LSeg(y,w) \ {y,w} by A18,XBOOLE_0:def 5;
        LSeg(y,w) \ {y,w} c= Ball(x,r) by A1,A9,Th34;
        then |. z-x .| < r by A19,Th7;
        hence m in {y,z} by A2,Th9;
      end;
    end;
    let m be set;
    assume m in {y,z};
    then
A20: m = y or m = z by TARSKI:def 2;
    y in halfline(y,z) & z in halfline(y,z) by Th27,Th28;
    hence thesis by A1,A2,A20,XBOOLE_0:def 4;
  end;
end;

theorem Th37:
  for S, T, X being Element of REAL n st S = y & T = z & X = x
holds y <> z & y in Ball(x,r) & a = (-(2*|(z-y,y-x)|) + sqrt delta (Sum sqr (T-
  S), 2 * |(z-y,y-x)|, Sum sqr (S-X) - r^2)) / (2 * Sum sqr (T-S)) implies ex e
being Point of TOP-REAL n st {e} = halfline(y,z) /\ Sphere(x,r) & e = (1-a)*y +
  a*z
proof
  let S, T, X be Element of REAL n such that
A1: S = y and
A2: T = z and
A3: X = x;
  set s = y, t = z;
A4: |. s-x .| = sqrt Sum sqr (S-X) by A1,A3,EUCLID:73;
A5: s-x = S-X by A1,A3,EUCLID:73;
A6: Sum sqr (T-S) >= 0 by RVSUM_1:116;
  then
A7: |. T-S .|^2 = Sum sqr (T-S) by SQUARE_1:def 4;
  set A = Sum sqr (T-S);
  assume that
A8: s <> t and
A9: s in Ball(x,r) and
A10: a = (-(2*|(z-y,y-x)|) + sqrt delta (Sum sqr (T-S), 2 * |(z-y,y-x)|,
  Sum sqr (S-X) - r^2)) / (2 * Sum sqr (T-S));
A11: |. T-S .| <> 0 by A1,A2,A8,EUCLID:19;
A12: now
    assume A <= 0;
    then A = 0 by RVSUM_1:116;
    hence contradiction by A11,SQUARE_1:82;
  end;
  set C = Sum sqr (S-X) - r^2;
  set B = 2 * |(t-s,s-x)|;
A13: r = 0 or r <> 0;
  Sum sqr (S-X) >= 0 by RVSUM_1:116;
  then
A14: |. S-X .|^2 = Sum sqr (S-X) by SQUARE_1:def 4;
  |. s-x .| < r by A9,Th7;
  then (sqrt Sum sqr (S-X))^2 < r^2 by A4,SQUARE_1:78;
  then
A15: C < 0 by A14,XREAL_1:51;
  then
A16: C/A < 0 by A12,XREAL_1:143;
  set l2 = (- B + sqrt delta(A,B,C)) / (2 * A);
  set l1 = (- B - sqrt delta(A,B,C)) / (2 * A);
  take e1 = (1-l2)*s+l2*t;
A17: 0 <= --r by A9;
A18: delta(A,B,C) = B^2 - 4*A*C & B^2 >= 0 by QUIN_1:def 1,XREAL_1:65;
A19: for x being real number holds Polynom(A,B,C,x) = Quard(A,l1,l2,x)
  proof
    let x be real number;
    thus Polynom(A,B,C,x) = A*x^2+B*x+C by POLYEQ_1:def 2
      .= A*(x-l1)*(x-l2) by A12,A15,A18,QUIN_1:16
      .= A*((x-l1)*(x-l2))
      .= Quard(A,l1,l2,x) by POLYEQ_1:def 3;
  end;
  then C/A = l1*l2 by A12,POLYEQ_1:11;
  then
A20: l1 < 0 & l2 > 0 or l1 > 0 & l2 < 0 by A16,XREAL_1:135;
A21: A*l2^2+B*l2--C = A*l2^2+B*l2+C .= Polynom(A,B,C,l2) by POLYEQ_1:def 2
    .= Quard(A,l1,l2,l2) by A19
    .= A*((l2-l1)*(l2-l2)) by POLYEQ_1:def 3
    .= 0;
  |. e1 - x .|^2 = |. 1 * s - l2*s + l2*t - x .|^2 by EUCLID:54
    .= |. s - l2*s + l2*t - x .|^2 by EUCLID:33
    .= |. s + l2*t - l2*s - x .|^2 by JORDAN2C:9
    .= |. s + (l2*t - l2*s) - x .|^2 by EUCLID:49
    .= |. s - x + (l2*t - l2*s) .|^2 by JORDAN2C:9
    .= |. s-x + l2*(t-s) .|^2 by EUCLID:53
    .= |. s-x .|^2 + 2*|(l2*(t-s),s-x)| + |. l2*(t-s) .|^2 by EUCLID_2:67
    .= Sum sqr (S-X) + 2*(l2*|(t-s,s-x)|) + |. l2*(t-s) .|^2 by A14,A5,
EUCLID_2:41
    .= Sum sqr (S-X) + 2*l2*|(t-s,s-x)| + (abs(l2)*|. t-s .|)^2 by TOPRNS_1:8
    .= Sum sqr (S-X) + 2*l2*|(t-s,s-x)| + (abs(l2))^2*|. t-s .|^2
    .= Sum sqr (S-X) + l2*(2 * |(t-s,s-x)|) + l2^2*(|. t-s .|^2) by
COMPLEX1:161
    .= Sum sqr (S-X) + l2*(2 * |(t-s,s-x)|) + l2^2*(|. T-S .|^2) by A1,A2,
EUCLID:73
    .= Sum sqr (S-X) + l2*(2 * |(t-s,s-x)|) + l2^2*Sum sqr (T-S) by A6,
SQUARE_1:def 4
    .= Sum sqr (S-X) + l2*(2 * |(t-s,s-x)|) + l2^2*A
    .= Sum sqr (S-X) + l2*B + l2^2*A
    .= Sum sqr (S-X) - r^2 + r^2 + l2*B + l2^2*A
    .= C + r^2 + l2*B + l2^2*A
    .= r^2 by A21;
  then |. e1 - x .| = r or |. e1 - x .| = -r by SQUARE_1:109;
  then
A22: e1 in Sphere(x,r) by A17,A13;
A23: 4*A*C < 0 by A12,A15,XREAL_1:131,134;
  then
A24: e1 in halfline(s,t) by A12,A18,A20,QUIN_1:27;
  hereby
    let d be set;
    assume d in {e1};
    then d = e1 by TARSKI:def 1;
    hence d in halfline(s,t) /\ Sphere(x,r) by A24,A22,XBOOLE_0:def 4;
  end;
A25: t-s = T-S by A1,A2,EUCLID:73;
  hereby
    let d be set;
    assume
A26: d in halfline(s,t) /\ Sphere(x,r);
    then d in halfline(s,t) by XBOOLE_0:def 4;
    then consider l being real number such that
A27: d = (1-l)*s+l*t and
A28: 0 <= l by Th26;
    l is Real by XREAL_0:def 1;
    then
A29: |. l*(t-s) .|^2 = (abs(l)*|. t-s .|)^2 by TOPRNS_1:8
      .= (abs l)^2 * |. t-s .|^2
      .= l^2 * |. t-s .|^2 by COMPLEX1:161;
    d in Sphere(x,r) by A26,XBOOLE_0:def 4;
    then r = |. (1-l)*s+l*t - x .| by A27,Th9
      .= |. 1 * s - l*s + l*t - x .| by EUCLID:54
      .= |. s - l*s + l*t - x .| by EUCLID:33
      .= |. s - (l*s - l*t) - x .| by EUCLID:51
      .= |. s-x - (l*s - l*t) .| by JORDAN2C:9
      .= |. s-x +- l*(s-t) .| by EUCLID:53
      .= |. s-x + l*(-(s-t)) .| by EUCLID:44
      .= |. s-x + l*(t-s) .| by EUCLID:48;
    then r^2 = |. s-x .|^2 + 2*|(l*(t-s),s-x)| + |. l*(t-s) .|^2 by EUCLID_2:67
      .= |. s-x .|^2 + 2*(l*|(t-s,s-x)|) + |. l*(t-s) .|^2 by EUCLID_2:41;
    then A*l^2+B*l+C = 0 by A7,A14,A5,A25,A29;
    then Polynom(A,B,C,l) = 0 by POLYEQ_1:def 2;
    then l = l1 or l = l2 by A12,A15,A18,POLYEQ_1:5;
    hence d in {e1} by A12,A23,A18,A20,A27,A28,QUIN_1:27,TARSKI:def 1;
  end;
  thus thesis by A10;
end;

theorem Th38:
  for S, T, Y being Element of REAL n st S = 1/2*y + 1/2*z & T = z
  & Y = x & y <> z & y in Sphere(x,r) & z in cl_Ball(x,r) ex e being Point of
TOP-REAL n st e <> y & {y,e} = halfline(y,z) /\ Sphere(x,r) & (z in Sphere(x,r)
implies e = z) & (not z in Sphere(x,r) & a = (-(2*|(z-(1/2*y + 1/2*z),1/2*y + 1
/2*z-x)|) + sqrt delta (Sum sqr (T-S), 2 * |(z-(1/2*y + 1/2*z),1/2*y + 1/2*z-x
)|, Sum sqr (S-Y) - r^2)) / (2 * Sum sqr (T-S)) implies e = (1-a)*(1/2*y + 1/2*
  z) + a*z)
proof
  let S, T, Y be Element of REAL n such that
A1: S = 1/2*y + 1/2*z & T = z & Y = x;
  reconsider G = x as Point of Euclid n by TOPREAL3:13;
  set s = y, t = z;
  set X = 1/2 * s + 1/2 * t;
  assume that
A2: s <> t and
A3: s in Sphere(x,r) and
A4: t in cl_Ball(x,r);
A5: Ball(G,r) = Ball(x,r) by Th13;
A6: Sphere(x,r) c= cl_Ball(x,r) by Th17;
  per cases;
  suppose
A7: t in Sphere(x,r);
    take t;
    thus thesis by A2,A3,A7,Th36;
  end;
  suppose
A8: not t in Sphere(x,r);
A9: now
      assume X = t;
      then t - 1/2 * s - 1/2 * t = t - t by EUCLID:50
        .= 0.TOP-REAL n by EUCLID:46;
      then 0.TOP-REAL n = t - 1/2 * t - 1/2 * s by JORDAN2C:9
        .= 1 * t - 1/2 * t - 1/2 * s by EUCLID:33
        .= (1-1/2) * t - 1/2 * s by EUCLID:54
        .= 1/2 * (t-s) by EUCLID:53;
      then t-s = 0.TOP-REAL n by EUCLID:35;
      hence contradiction by A2,EUCLID:47;
    end;
    Ball(x,r) \/ Sphere(x,r) = cl_Ball(x,r) by Th18;
    then
A10: t in Ball(G,r) by A4,A5,A8,XBOOLE_0:def 3;
    set a = (-(2*|(t-X,X-x)|) + sqrt delta (Sum sqr (T-S), 2 * |(t-X,X-x)|,
    Sum sqr (S-Y) - r^2)) / (2 * Sum sqr (T-S));
A11: 1/2 + 1/2 = 1 & abs(1/2) = 1/2 by ABSVALUE:def 1;
    Ball(G,r) = Ball(x,r) by Th13;
    then X in Ball(G,r) by A3,A6,A11,A10,Th24;
    then consider e1 being Point of TOP-REAL n such that
A12: {e1} = halfline(X,t) /\ Sphere(x,r) and
A13: e1 = (1-a)*X + a*t by A1,A5,A9,Th37;
    take e1;
A14: e1 in {e1} by TARSKI:def 1;
    then e1 in halfline(X,t) by A12,XBOOLE_0:def 4;
    then consider l being real number such that
A15: e1 = (1-l)*X + l*t and
A16: 0 <= l by Th26;
    hereby
      assume e1 = s;
      then 0.TOP-REAL n = (1-l)*X + l*t - s by A15,EUCLID:46
        .= (1-l)*(1/2*s) + (1-l)*(1/2*t) + l*t - s by EUCLID:36
        .= (1-l)*(1/2*s) + ((1-l)*(1/2*t) + l*t) - s by EUCLID:30
        .= (1-l)*(1/2*s) - s + ((1-l)*(1/2*t) + l*t) by JORDAN2C:9
        .= (1-l)*(1/2*s) - 1 * s + ((1-l)*(1/2*t) + l*t) by EUCLID:33
        .= (1-l)*(1/2)*s - 1 * s + ((1-l)*(1/2*t) + l*t) by EUCLID:34
        .= ((1-l)*(1/2) - 1) * s + ((1-l)*(1/2*t) + l*t) by EUCLID:54
        .= (-1/2 - l*(1/2)) * s + ((1-l)*(1/2)*t + l*t) by EUCLID:34
        .= (-1/2 - l*(1/2)) * s + ((1-l)*(1/2) + l) * t by EUCLID:37
        .= ((-1)*(1/2 + l*(1/2))) * s + (1/2 + l*(1/2)) * t
        .= (-(1/2 + l*(1/2))) * s + (1/2 + l*(1/2)) * t
        .= (1/2 + l*(1/2)) * t + -(1/2 + l*(1/2)) * s by EUCLID:44
        .= (1/2 + l*(1/2)) * t - (1/2 + l*(1/2)) * s
        .= (1/2 + l*(1/2)) * (t - s) by EUCLID:53;
      then 1/2 + l*(1/2) = 0 or t - s = 0.TOP-REAL n by EUCLID:35;
      hence contradiction by A2,A16,EUCLID:47;
    end;
A17: s in halfline(s,t) by Th27;
    hereby
      set o = (1+l)/2;
      let m be set;
      assume m in {s,e1};
      then
A18:  m = s or m = e1 by TARSKI:def 2;
      e1 = (1-l)*(1/2*s) + (1-l)*(1/2*t) + l*t by A15,EUCLID:36
        .= (1-l)*(1/2)*s + (1-l)*(1/2*t) + l*t by EUCLID:34
        .= (1-l)*(1/2)*s + (1-l)*(1/2)*t + l*t by EUCLID:34
        .= (1-l)*(1/2)*s + ((1-l)*(1/2)*t + l*t) by EUCLID:30
        .= (1-l)*(1/2)*s + ((1-l)*(1/2) + l)*t by EUCLID:37
        .= (1-o)*s + o*t;
      then
A19:  e1 in halfline(s,t) by A16;
      e1 in Sphere(x,r) by A12,A14,XBOOLE_0:def 4;
      hence m in halfline(s,t) /\ Sphere(x,r) by A3,A17,A18,A19,XBOOLE_0:def 4;
    end;
    hereby
      let m be set;
      assume
A20:  m in halfline(s,t) /\ Sphere(x,r);
      then
A21:  m in halfline(s,t) by XBOOLE_0:def 4;
A22:  m in Sphere(x,r) by A20,XBOOLE_0:def 4;
      per cases;
      suppose
        m in halfline(X,t);
        then m in halfline(X,t) /\ Sphere(x,r) by A22,XBOOLE_0:def 4;
        then m = e1 by A12,TARSKI:def 1;
        hence m in {s,e1} by TARSKI:def 2;
      end;
      suppose
A23:    not m in halfline(X,t);
        consider M being real number such that
A24:    m = (1-M)*s + M*t and
A25:    0 <= M by A21,Th26;
A26:    now
          set o = 2*M-1;
          assume M > 1;
          then 2*M > 2*1 by XREAL_1:70;
          then
A27:      2*M-1 > 2*1-1 by XREAL_1:16;
          (1-o)*X + o*t = (1-o)*(1/2 * s) + (1-o)*(1/2 * t) + o*t by EUCLID:36
            .= (1-o)*(1/2) * s + (1-o)*(1/2 * t) + o*t by EUCLID:34
            .= (1-o)*(1/2) * s + (1-o)*(1/2) * t + o*t by EUCLID:34
            .= (1-o)*(1/2) * s + ((1-o)*(1/2) * t + o*t) by EUCLID:30
            .= (1-o)*(1/2) * s + ((1-o)*(1/2)+o) * t by EUCLID:37
            .= m by A24;
          hence contradiction by A23,A27;
        end;
        |. t-x .| <= r & |. t-x .| <> r by A4,A8,Th8;
        then |. t-x .| < r by XXREAL_0:1;
        then t in Ball(x,r);
        then
A28:    LSeg(s,t) /\ Sphere(x,r) = {s} by A3,Th33;
        M is Real by XREAL_0:def 1;
        then m in LSeg(s,t) by A24,A25,A26;
        then m in {s} by A22,A28,XBOOLE_0:def 4;
        then m = s by TARSKI:def 1;
        hence m in {s,e1} by TARSKI:def 2;
      end;
    end;
    thus thesis by A8,A13;
  end;
end;

registration
  let n be Nat, x be Point of TOP-REAL n;
  let r be negative (real number);
  cluster Sphere(x,r) -> empty;
  coherence
  proof
    assume not thesis;
    then consider y being Point of TOP-REAL n such that
A1: y in Sphere(x,r) by SUBSET_1:10;
    n in NAT by ORDINAL1:def 13;
    then |. y - x .| = r by A1,Th9;
    hence contradiction;
  end;
end;

registration
  let n be non empty Nat, x be Point of TOP-REAL n;
  let r be non negative (real number);
  cluster Sphere(x,r) -> non empty;
  coherence
  proof
A1: n in NAT by ORDINAL1:def 13;
    reconsider e = x as Point of Euclid n by TOPREAL3:13;
    per cases;
    suppose
      r = 0;
      then Sphere(e,r) = {e} by TOPREAL6:62;
      hence thesis by A1,Th15;
    end;
    suppose
      r > 0;
      then reconsider r as positive (real number);
      consider s being Point of TOP-REAL n such that
A2:   s in Ball(x,r) by SUBSET_1:10;
      reconsider S = s, T = s + 1.REAL n, XX = x as Element of REAL n by
EUCLID:25;
      set a = (-(2*|(s + 1.REAL n-s,s-x)|) + sqrt delta (Sum sqr (T-S), 2 * |(
      s + 1.REAL n-s,s-x)|, Sum sqr (S-XX) - r^2)) / (2 * Sum sqr (T-S));
      s <> s + 1.REAL n by A1,Th3;
      then
      ex e being Point of TOP-REAL n st {e} = halfline(s,s + 1.REAL n) /\
      Sphere(x,r) & e = (1-a)*s + a*(s+1.REAL n) by A1,A2,Th37;
      hence thesis;
    end;
  end;
end;

theorem
  Sphere(x,r) is non empty implies r >= 0;

theorem
  n is non empty & Sphere(x,r) is empty implies r < 0;

begin :: Subsets of TOP-REAL 2

reserve s, t for Point of TOP-REAL 2;

theorem
  (a*s + b*t)`1 = a * s`1 + b * t`1
proof
  thus (a*s+b*t)`1 = (a*s)`1 + (b*t)`1 by TOPREAL3:7
    .= a*s`1 + (b*t)`1 by TOPREAL3:9
    .= a*s`1+b*t`1 by TOPREAL3:9;
end;

theorem
  (a*s + b*t)`2 = a * s`2 + b * t`2
proof
  thus (a*s+b*t)`2 = (a*s)`2 + (b*t)`2 by TOPREAL3:7
    .= a*s`2 + (b*t)`2 by TOPREAL3:9
    .= a*s`2+b*t`2 by TOPREAL3:9;
end;

theorem Th43:
  t in circle(a,b,r) iff |. t - |[a,b]| .| = r
proof
A1: circle(a,b,r) = {x where x is Point of TOP-REAL 2: |. x - |[a,b]| .| = r
  } by JGRAPH_6:def 5;
  hereby
    assume t in circle(a,b,r);
    then
    ex x being Point of TOP-REAL 2 st t = x & |. x - |[a,b]| .| = r by A1;
    hence |. t - |[a,b]| .| = r;
  end;
  thus thesis by A1;
end;

theorem Th44:
  t in closed_inside_of_circle(a,b,r) iff |. t - |[a,b]| .| <= r
proof
A1: closed_inside_of_circle(a,b,r) = {x where x is Point of TOP-REAL 2: |. x
  - |[a,b]| .| <= r} by JGRAPH_6:def 7;
  hereby
    assume t in closed_inside_of_circle(a,b,r);
    then
    ex x being Point of TOP-REAL 2 st t = x & |. x - |[a,b]| .| <= r by A1;
    hence |. t - |[a,b]| .| <= r;
  end;
  thus thesis by A1;
end;

theorem Th45:
  t in inside_of_circle(a,b,r) iff |. t - |[a,b]| .| < r
proof
A1: inside_of_circle(a,b,r) = {x where x is Point of TOP-REAL 2: |. x - |[a,
  b]| .| < r} by JGRAPH_6:def 6;
  hereby
    assume t in inside_of_circle(a,b,r);
    then
    ex x being Point of TOP-REAL 2 st t = x & |. x - |[a,b]| .| < r by A1;
    hence |. t - |[a,b]| .| < r;
  end;
  thus thesis by A1;
end;

registration
  let a, b be real number;
  let r be positive (real number);
  cluster inside_of_circle(a,b,r) -> non empty;
  coherence
  proof
    |. |[a,b]| - |[a,b]| .| = 0 by TOPRNS_1:29;
    hence thesis by Th45;
  end;
end;

registration
  let a, b be real number;
  let r be non negative (real number);
  cluster closed_inside_of_circle(a,b,r) -> non empty;
  coherence
  proof
    |. |[a,b]| - |[a,b]| .| = 0 by TOPRNS_1:29;
    hence thesis by Th44;
  end;
end;

theorem
  circle(a,b,r) c= closed_inside_of_circle(a,b,r)
proof
  let x be set;
  assume
A1: x in circle(a,b,r);
  then reconsider x as Point of TOP-REAL 2;
  |. x - |[a,b]| .| = r by A1,Th43;
  hence thesis by Th44;
end;

theorem Th47:
  for x being Point of Euclid 2 st x = |[a,b]| holds cl_Ball(x,r)
  = closed_inside_of_circle(a,b,r)
proof
  let x be Point of Euclid 2 such that
A1: x = |[a,b]|;
  hereby
    let w be set;
    assume
A2: w in cl_Ball(x,r);
    then reconsider u = w as Point of TOP-REAL 2 by TOPREAL3:13;
    reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
    dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
    then |. u - |[a,b]| .| <= r by A2,METRIC_1:13;
    hence w in closed_inside_of_circle(a,b,r) by Th44;
  end;
  let w be set;
  assume
A3: w in closed_inside_of_circle(a,b,r);
  then reconsider u = w as Point of TOP-REAL 2;
  reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
  dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
  then dist(e,x) <= r by A3,Th44;
  hence thesis by METRIC_1:13;
end;

theorem Th48:
  for x being Point of Euclid 2 st x = |[a,b]| holds Ball(x,r) =
  inside_of_circle(a,b,r)
proof
  let x be Point of Euclid 2 such that
A1: x = |[a,b]|;
  hereby
    let w be set;
    assume
A2: w in Ball(x,r);
    then reconsider u = w as Point of TOP-REAL 2 by TOPREAL3:13;
    reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
    dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
    then |. u - |[a,b]| .| < r by A2,METRIC_1:12;
    hence w in inside_of_circle(a,b,r) by Th45;
  end;
  let w be set;
  assume
A3: w in inside_of_circle(a,b,r);
  then reconsider u = w as Point of TOP-REAL 2;
  reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
  dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
  then dist(e,x) < r by A3,Th45;
  hence thesis by METRIC_1:12;
end;

theorem Th49:
  for x being Point of Euclid 2 st x = |[a,b]| holds Sphere(x,r) =
  circle(a,b,r)
proof
  let x be Point of Euclid 2 such that
A1: x = |[a,b]|;
  hereby
    let w be set;
    assume
A2: w in Sphere(x,r);
    then reconsider u = w as Point of TOP-REAL 2 by TOPREAL3:13;
    reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
    dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
    then |. u - |[a,b]| .| = r by A2,METRIC_1:14;
    hence w in circle(a,b,r) by Th43;
  end;
  let w be set;
  assume
A3: w in circle(a,b,r);
  then reconsider u = w as Point of TOP-REAL 2;
  reconsider e = u as Point of Euclid 2 by TOPREAL3:13;
  dist(e,x) = |. u - |[a,b]| .| by A1,JGRAPH_1:45;
  then dist(e,x) = r by A3,Th43;
  hence thesis by METRIC_1:14;
end;

theorem Th50:
  Ball(|[a,b]|,r) = inside_of_circle(a,b,r)
proof
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  thus Ball(|[a,b]|,r) = Ball(e,r) by Th13
    .= inside_of_circle(a,b,r) by Th48;
end;

theorem
  cl_Ball(|[a,b]|,r) = closed_inside_of_circle(a,b,r)
proof
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  thus cl_Ball(|[a,b]|,r) = cl_Ball(e,r) by Th14
    .= closed_inside_of_circle(a,b,r) by Th47;
end;

theorem Th52:
  Sphere(|[a,b]|,r) = circle(a,b,r)
proof
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  thus Sphere(|[a,b]|,r) = Sphere(e,r) by Th15
    .= circle(a,b,r) by Th49;
end;

theorem
  inside_of_circle(a,b,r) c= closed_inside_of_circle(a,b,r)
proof
  let x be set;
  assume
A1: x in inside_of_circle(a,b,r);
  then reconsider x as Point of TOP-REAL 2;
  |. x - |[a,b]| .| < r by A1,Th45;
  hence thesis by Th44;
end;

theorem
  inside_of_circle(a,b,r) misses circle(a,b,r)
proof
  assume not thesis;
  then consider x being set such that
A1: x in inside_of_circle(a,b,r) and
A2: x in circle(a,b,r) by XBOOLE_0:3;
  reconsider x as Point of TOP-REAL 2 by A1;
  |. x - |[a,b]| .| = r by A2,Th43;
  hence thesis by A1,Th45;
end;

theorem
  inside_of_circle(a,b,r) \/ circle(a,b,r) = closed_inside_of_circle(a,b ,r)
proof
  reconsider p = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
A1: cl_Ball(p,r) = closed_inside_of_circle(a,b,r) by Th47;
  Sphere(p,r) = circle(a,b,r) & Ball(p,r) = inside_of_circle(a,b,r) by Th48
,Th49;
  hence thesis by A1,METRIC_1:17;
end;

theorem
  s in Sphere(0.TOP-REAL 2,r) implies s`1^2 + s`2^2 = r^2
proof
  assume s in Sphere(0.TOP-REAL 2,r);
  then |. s-0.TOP-REAL 2 .| = r by Th9;
  then |. s .| = r by RLVECT_1:26;
  hence thesis by JGRAPH_1:46;
end;

theorem
  s <> t & s in closed_inside_of_circle(a,b,r) & t in
  closed_inside_of_circle(a,b,r) implies r > 0
proof
  reconsider x = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  cl_Ball(x,r) = closed_inside_of_circle(a,b,r) by Th47;
  hence thesis by Th6;
end;

theorem
  for S, T, X being Element of REAL 2 st S = s & T = t & X = |[a,b]| & w
  = (-(2*|(t-s,s-|[a,b]|)|) + sqrt delta (Sum sqr (T-S), 2 * |(t-s,s-|[a,b]|)|,
Sum sqr (S-X) - r^2)) / (2 * Sum sqr (T-S)) & s <> t & s in inside_of_circle(a,
b,r) ex e being Point of TOP-REAL 2 st {e} = halfline(s,t) /\ circle(a,b,r) & e
  = (1-w)*s + w*t
proof
A1: Ball(|[a,b]|,r) = inside_of_circle(a,b,r) & Sphere(|[a,b]|,r) = circle(a
  ,b,r ) by Th50,Th52;
  let S, T, X be Element of REAL 2;
  assume S = s & T = t & X = |[a,b]| & w = (-(2*|(t-s,s-|[a,b]|)|) + sqrt
delta ( Sum sqr (T-S), 2 * |(t-s,s-|[a,b]|)|, Sum sqr (S-X) - r^2)) / (2 * Sum
  sqr (T-S )) & s <> t & s in inside_of_circle(a,b,r);
  hence thesis by A1,Th37;
end;

theorem
  s in circle(a,b,r) & t in inside_of_circle(a,b,r) implies LSeg(s,t) /\
  circle(a,b,r) = {s}
proof
  assume
A1: s in circle(a,b,r) & t in inside_of_circle(a,b,r);
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
A2: inside_of_circle(a,b,r) = Ball(e,r) by Th48
    .= Ball(|[a,b]|,r) by Th13;
  circle(a,b,r) = Sphere(e,r) by Th49
    .= Sphere(|[a,b]|,r) by Th15;
  hence thesis by A1,A2,Th33;
end;

theorem
  s in circle(a,b,r) & t in circle(a,b,r) implies LSeg(s,t) \ {s,t} c=
  inside_of_circle(a,b,r)
proof
  assume
A1: s in circle(a,b,r) & t in circle(a,b,r);
  reconsider G = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  Sphere(G,r) = circle(a,b,r) by Th49;
  then
A2: Sphere(|[a,b]|,r) = circle(a,b,r) by Th15;
  Ball(|[a,b]|,r) = inside_of_circle(a,b,r) by Th50;
  hence thesis by A1,A2,Th34;
end;

theorem
  s in circle(a,b,r) & t in circle(a,b,r) implies LSeg(s,t) /\ circle(a,
  b,r) = {s,t}
proof
  reconsider G = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  Sphere(G,r) = circle(a,b,r) by Th49;
  then
A1: Sphere(|[a,b]|,r) = circle(a,b,r) by Th15;
  assume s in circle(a,b,r) & t in circle(a,b,r);
  hence thesis by A1,Th35;
end;

theorem
  s in circle(a,b,r) & t in circle(a,b,r) implies halfline(s,t) /\
  circle(a,b,r) = {s,t}
proof
  assume
A1: s in circle(a,b,r) & t in circle(a,b,r);
  reconsider e = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
  circle(a,b,r) = Sphere(e,r) by Th49
    .= Sphere(|[a,b]|,r) by Th15;
  hence thesis by A1,Th36;
end;

theorem
  for S, T, Y being Element of REAL 2 st S = 1/2*s + 1/2*t & T = t & Y =
|[a,b]| & s <> t & s in circle(a,b,r) & t in closed_inside_of_circle(a,b,r) ex
e being Point of TOP-REAL 2 st e <> s & {s,e} = halfline(s,t) /\ circle(a,b,r)
& (t in Sphere(|[a,b]|,r) implies e = t) & (not t in Sphere(|[a,b]|,r) & w = (-
(2*|(t-(1/2*s + 1/2*t),1/2*s + 1/2*t-|[a,b]|)|) + sqrt delta (Sum sqr (T-S), 2
* |(t-(1/2*s + 1/2*t),1/2*s + 1/2*t-|[a,b]|)|, Sum sqr (S-Y) - r^2)) / (2 * Sum
  sqr (T-S)) implies e = (1-w)*(1/2*s + 1/2*t) + w*t)
proof
  reconsider G = |[a,b]| as Point of Euclid 2 by TOPREAL3:13;
A1: cl_Ball(G,r) = closed_inside_of_circle(a,b,r) & cl_Ball(G,r) = cl_Ball(
  |[a,b ]|,r) by Th14,Th47;
  Sphere(G,r) = circle(a,b,r) by Th49;
  then
A2: Sphere(|[a,b]|,r) = circle(a,b,r) by Th15;
  let S, T, Y be Element of REAL 2;
  assume S = 1/2*s + 1/2*t & T = t & Y = |[a,b]| & s <> t & s in circle(a,b,r
  ) & t in closed_inside_of_circle(a,b,r);
  hence thesis by A1,A2,Th38;
end;

registration
  let a, b, r be real number;
  cluster inside_of_circle(a,b,r) -> convex;
  coherence
  proof
    inside_of_circle(a,b,r) = {q where q is Point of TOP-REAL 2: |.q - |[a
    ,b]|.| < r } by JGRAPH_6:def 6;
    hence thesis by Lm2;
  end;
  cluster closed_inside_of_circle(a,b,r) -> convex;
  coherence
  proof
    closed_inside_of_circle(a,b,r) = {q where q is Point of TOP-REAL 2: |.
    q - |[a,b]|.| <= r } by JGRAPH_6:def 7;
    hence thesis by Lm1;
  end;
end;
