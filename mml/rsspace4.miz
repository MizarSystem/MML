:: Banach Space of Bounded Real Sequences
::  by Yasumasa Suzuki
::
:: Received January 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, NORMSP_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, ORDINAL2, LATTICES, RLSUB_1, SEQ_1, FUNCT_2, FUNCOP_1,
      FUNCSDOM, SEQ_2, SEQM_3, BHSP_3, RSSPACE, RSSPACE3, LOPBAN_1, RSSPACE4;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, MCART_1, RELAT_1, PRE_TOPC,
      DOMAIN_1, FUNCOP_1, STRUCT_0, XREAL_0, ORDINAL1, NUMBERS, REAL_1, NAT_1,
      COMPLEX1, FUNCT_1, FUNCT_2, INTEGRA2, PSCOMP_1, RLVECT_1, NORMSP_1,
      SEQ_1, SEQ_2, SEQM_3, SEQ_4, RLSUB_1, PARTFUN1, RSSPACE, RSSPACE3,
      LOPBAN_1;
 constructors FUNCT_3, BINOP_1, ARYTM_0, REAL_1, INTEGRA1, INTEGRA2, XREAL_0,
      NAT_1, ABSVALUE, SQUARE_1, FUNCT_2, XCMPLX_0, SUBSET_1, MEMBERED,
      RFUNCT_2, PSCOMP_1, DOMAIN_1, SEQ_1, SEQ_2, SEQM_3, SERIES_1, SEQ_4,
      SUPINF_2, METRIC_1, FUNCOP_1, FUNCSDOM, RAT_1, INT_1, PARTFUN1, RLVECT_1,
      VECTSP_1, FRAENKEL, RLSUB_1, NORMSP_1, BHSP_2, RSSPACE, RSSPACE3,
      COMPLEX1, LOPBAN_1;
 registrations SUBSET_1, RELSET_1, STRUCT_0, RELAT_1, ORDINAL2, FUNCT_2, SEQ_1,
      RLVECT_1, NORMSP_1, XREAL_0, MEMBERED, RSSPACE3, SEQM_3;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI, XBOOLE_0, SEQ_4, NORMSP_1;
 theorems RELAT_1, AXIOMS, TARSKI, ABSVALUE, ZFMISC_1, REAL_1, XBOOLE_0, NAT_1,
      REAL_2, FUNCOP_1, SEQ_1, SEQ_2, SEQM_3, SEQ_4, FUNCT_1, FUNCT_2,
      RLVECT_1, RLSUB_1, NORMSP_1, XREAL_0, XCMPLX_0, PSCOMP_1, INTEGRA2,
      RSSPACE, RSSPACE3, RFUNCT_2, RSSPACE2, LOPBAN_1;
 schemes SEQ_1, FUNCT_2, XBOOLE_0;

begin :: The Banach Space of Bounded Real Sequences

Lm1:
 for rseq be Real_Sequence
 for K be real number st (for n be Nat holds rseq.n <= K)
 holds sup rng rseq <= K
 proof
   let rseq be Real_Sequence;
   let K be real number such that
   A1:  for n be Nat holds rseq.n <= K;
      now let s be real number such that
    A2: s in rng rseq;
       NAT= dom rseq by FUNCT_2:def 1;
    then rseq is Function of NAT, rng rseq by FUNCT_2:3;
    then consider n be set such that
    A3: n in NAT & rseq.n=s by A2,FUNCT_2:17;
    thus s <=K by A1,A3;
   end;
   hence thesis by SEQ_4:62;
end;

Lm2:
for rseq be Real_Sequence st rseq is bounded holds
   for n be Nat holds rseq.n <= sup rng rseq
proof
   let rseq be Real_Sequence such that
   A1:rseq is bounded;
   let n be Nat;
      rng rseq is bounded by A1,PSCOMP_1:7;
   then A2: rng rseq is bounded_above by SEQ_4:def 3;
      NAT = dom rseq by FUNCT_2:def 1;
   then rseq.n in rng rseq by FUNCT_1:12;
   hence thesis by A2,SEQ_4:def 4;
end;

definition
  func the_set_of_BoundedRealSequences -> Subset of
     Linear_Space_of_RealSequences means :Def1:
   for x being set holds x in it iff
    (x in the_set_of_RealSequences & seq_id x is bounded);
existence
proof
  defpred P[set] means seq_id $1 is bounded;
   consider IT being set such that
A1:for x being set holds x in IT
    iff x in the_set_of_RealSequences & P[x] from XBOOLE_0:sch 1;
     IT is Subset of the_set_of_RealSequences
   proof
        for x be set st x in IT holds x in the_set_of_RealSequences by A1;
     hence thesis by TARSKI:def 3;
   end;
   hence thesis by A1,RSSPACE:def 7;
end;
uniqueness
proof
   let X1,X2 be Subset of Linear_Space_of_RealSequences;
   assume that
A2: for x being set holds x in X1 iff
     (x in the_set_of_RealSequences & seq_id(x) is bounded) and
A3: for x being set holds x in X2 iff
    (x in the_set_of_RealSequences & seq_id(x) is bounded);
    thus X1 c= X2
    proof
       let x be set;
      assume x in X1;
      then x in the_set_of_RealSequences & seq_id(x) is bounded by A2;
      hence thesis by A3;
    end;
    let x be set;
    assume x in X2;
    then x in the_set_of_RealSequences & seq_id(x) is bounded by A3;
    hence thesis by A2;
  end;
end;

registration
  cluster the_set_of_BoundedRealSequences -> non empty;
  coherence
   proof
        seq_id Zeroseq is bounded
     proof
       reconsider rseq=seq_id Zeroseq as Real_Sequence;
          for n being Nat holds rseq.n = 0 by RSSPACE:def 6;
       then rseq is constant by SEQM_3:def 6;
       hence thesis by SEQM_3:73;
     end;
     hence thesis by Def1;
  end;
  cluster the_set_of_BoundedRealSequences -> lineary-closed;
  coherence
proof
  set W = the_set_of_BoundedRealSequences;
A1:for v,u be VECTOR of Linear_Space_of_RealSequences st
    v in the_set_of_BoundedRealSequences &
    u in the_set_of_BoundedRealSequences
        holds v + u in the_set_of_BoundedRealSequences
   proof
     let v,u be VECTOR of Linear_Space_of_RealSequences such that
    A2: v in W & u in W;
        seq_id(v+u) is bounded
        proof
          A3:  seq_id v is bounded by A2,Def1;
          A4:  seq_id u is bounded by A2,Def1;
             seq_id(v+u)=seq_id(seq_id v+seq_id u) by RSSPACE:4,def 7
                                 .=seq_id v+seq_id u by RSSPACE:3;
          hence thesis by A3,A4,SEQM_3:71;
         end;
      hence v+u in W by Def1,RSSPACE:def 7;
    end;
       for a be Real for v be VECTOR of Linear_Space_of_RealSequences
    st v in W holds a * v in W
    proof
     let a be Real;
     let v be VECTOR of Linear_Space_of_RealSequences such that
     A5: v in W;
        seq_id(a*v) is bounded
     proof
       A6: seq_id v is bounded by A5,Def1;
          seq_id(a*v) =seq_id(a(#)seq_id(v)) by RSSPACE:5,def 7
                  .=a(#)seq_id(v) by RSSPACE:3;
       hence thesis by A6,SEQM_3:70;
     end;
     hence a*v in W by Def1,RSSPACE:def 7;
   end;
   hence thesis by A1,RLSUB_1:def 1;
end;
end;

canceled;
 Lm3:
RLSStruct (# the_set_of_BoundedRealSequences,
        Zero_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        Add_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        Mult_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences) #)
is Subspace of Linear_Space_of_RealSequences by RSSPACE:13;

registration
  cluster RLSStruct (# the_set_of_BoundedRealSequences,
        Zero_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        Add_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        Mult_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences) #)
   -> Abelian add-associative right_zeroed right_complementable
        RealLinearSpace-like;
  coherence by RSSPACE:13;
end;

Lm4: RLSStruct (# the_set_of_BoundedRealSequences,
        Zero_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        Add_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        Mult_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences) #)
  is Abelian add-associative right_zeroed
    right_complementable RealLinearSpace-like;

Lm5:
  ex NORM be Function of the_set_of_BoundedRealSequences,REAL st
   for x be set st x in the_set_of_BoundedRealSequences holds
    NORM.x = sup rng abs seq_id(x)
proof
  deffunc F(set) = sup rng abs seq_id($1);
A1:for z be set st z in the_set_of_BoundedRealSequences
   holds F(z) in REAL;
      ex f being Function of the_set_of_BoundedRealSequences,REAL st
     for x being set st x in the_set_of_BoundedRealSequences holds
       f.x = F(x) from FUNCT_2:sch 2(A1);
   hence thesis;
end;

definition
  func linfty_norm ->
  Function of the_set_of_BoundedRealSequences, REAL means :Def2:
  for x be set st x in the_set_of_BoundedRealSequences holds
    it.x = sup rng abs seq_id x;
existence by Lm5;
uniqueness
proof
   let NORM1,NORM2
   be Function of the_set_of_BoundedRealSequences, REAL such that
A1:for x be set st x in the_set_of_BoundedRealSequences holds
     NORM1.x = sup rng abs seq_id x and
A2:for x be set st x in the_set_of_BoundedRealSequences holds
     NORM2.x = sup rng abs seq_id x;
A3:dom NORM1 = the_set_of_BoundedRealSequences &
     dom NORM2 = the_set_of_BoundedRealSequences by FUNCT_2:def 1;
      for z be set st z in the_set_of_BoundedRealSequences holds NORM1.z =
NORM2
.
z
   proof
     let z be set such that
A4:  z in the_set_of_BoundedRealSequences;
        NORM1.z = sup rng abs seq_id(z) by A1,A4;
     hence thesis by A2,A4;
  end;
  hence thesis by A3,FUNCT_1:9;
end;
end;

Lm6:
for rseq be Real_Sequence st (for n be Nat holds rseq.n=0) holds
      rseq is bounded & sup rng abs rseq = 0
proof
  let rseq be Real_Sequence such that
A1: for n be Nat holds rseq.n=0;
A2: for n be Nat holds (abs rseq).n=0
    proof
      let n be Nat;
A3:   rseq.n=0 by A1;
         (abs rseq).n = abs(rseq.n) by SEQ_1:16;
      hence thesis by A3,ABSVALUE:7;
    end;
    A4: rseq is constant by A1,SEQM_3:def 6;
       NAT = dom abs rseq by FUNCT_2:def 1;
then A5:abs rseq is Function of NAT, rng abs rseq by FUNCT_2:3;
       rng abs rseq = {0}
    proof
         rng abs rseq c= {0}
      proof let y be set;
        assume
      y in rng abs rseq;
        then consider n be set such that
   A6: n in NAT & abs(rseq).n=y by A5,FUNCT_2:17;
           y=0 by A2,A6;
        hence y in {0} by TARSKI:def 1;
      end;
      hence thesis by ZFMISC_1:39;
    end;
    hence thesis by A4,SEQM_3:73,SEQ_4:22;
end;

Lm7:
for rseq be Real_Sequence st
 ( rseq is bounded & sup rng abs rseq = 0 ) holds
  for n be Nat holds rseq.n = 0
proof
   let rseq be Real_Sequence such that
A1:rseq is bounded and
A2:sup rng abs rseq = 0;
   let n be Nat;
   A3: abs rseq is bounded by A1,SEQM_3:70;
      0 <= abs(rseq).n
   proof
        0 <= abs(rseq.n) by ABSVALUE:5;
     hence thesis by SEQ_1:16;
   end;
   then (abs rseq).n =0 by A2,A3,Lm2;
   then abs (rseq.n) = 0 by SEQ_1:16;
   hence thesis by ABSVALUE:7;
end;

theorem
    for rseq be Real_Sequence holds
     ( rseq is bounded & sup rng abs rseq = 0 ) iff
    for n be Nat holds rseq.n = 0 by Lm6,Lm7;

registration
  cluster NORMSTR (# the_set_of_BoundedRealSequences,
        Zero_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
         Add_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        Mult_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        linfty_norm #) -> Abelian add-associative right_zeroed
    right_complementable RealLinearSpace-like;
  coherence by Lm4,RSSPACE3:4;
end;

definition
  func linfty_Space -> non empty NORMSTR equals :Def3:
   NORMSTR (# the_set_of_BoundedRealSequences,
        Zero_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
         Add_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        Mult_(the_set_of_BoundedRealSequences,Linear_Space_of_RealSequences),
        linfty_norm #);
  coherence;
end;

theorem Th3:
  the carrier of linfty_Space = the_set_of_BoundedRealSequences &
( for x be set holds
   x is VECTOR of linfty_Space
   iff x is Real_Sequence & seq_id x is bounded ) &
0.linfty_Space = Zeroseq &
( for u be VECTOR of linfty_Space holds u = seq_id u ) &
( for u,v be VECTOR of linfty_Space holds u+v =seq_id(u)+seq_id(v) ) &
( for r be Real for u be VECTOR of linfty_Space holds r*u =r(#)seq_id(u) ) &
( for u be VECTOR of linfty_Space holds
   -u = -seq_id u & seq_id(-u) = -seq_id u ) &
( for u,v be VECTOR of linfty_Space holds u-v =seq_id(u)-seq_id(v) ) &
( for v be VECTOR of linfty_Space holds seq_id v is bounded ) &
( for v be VECTOR of linfty_Space holds ||.v.|| = sup rng abs seq_id v )
proof
   set l1 =linfty_Space;
A1:for x be set holds x is Element of l1
     iff x is Real_Sequence & seq_id x is bounded
   proof
     let x be set;
        x in the_set_of_RealSequences iff x is Real_Sequence by RSSPACE:def 1;
     hence thesis by Def1,Def3;
   end;
A2:for u be VECTOR of l1 holds u = seq_id u
   proof
     let u be VECTOR of l1;
        u is VECTOR of Linear_Space_of_RealSequences
     by Def3,Lm3,RLSUB_1:18;
     hence thesis by RSSPACE:def 2,def 7;
   end;
A3:for u,v be VECTOR of l1 holds u+v =seq_id u+seq_id v
   proof
     let u,v be VECTOR of l1;
     reconsider u1=u, v1 = v as VECTOR of Linear_Space_of_RealSequences
      by Def3,Lm3,RLSUB_1:18;
     set L1=Linear_Space_of_RealSequences;
     set W = the_set_of_BoundedRealSequences;
        dom (the add of L1)
       = [:the carrier of L1,the carrier of L1:] by FUNCT_2:def 1;
     then A4: dom ((the add of Linear_Space_of_RealSequences) | [:W,W:])
       =[:W,W:] by RELAT_1:91;
       u+v =(the add of l1).[u,v] by RLVECT_1:def 3
        .=((the add of Linear_Space_of_RealSequences)|[:W,W:]).[u,v]
         by Def3,RSSPACE:def 8
        .=(the add of Linear_Space_of_RealSequences).[u,v]
         by A4,Def3,FUNCT_1:70
        .=u1+v1 by RLVECT_1:def 3;
     hence thesis by RSSPACE:4,def 7;
   end;

A5:for r be Real for u be VECTOR of l1 holds r*u =r(#)seq_id u
   proof
     let r be Real;
     let u be VECTOR of l1;
     reconsider u1=u as VECTOR of Linear_Space_of_RealSequences
      by Def3,Lm3,RLSUB_1:18;
     set L1=Linear_Space_of_RealSequences;
     set W = the_set_of_BoundedRealSequences;
A6: [:REAL,W:] c= [:REAL,the carrier of L1:] by ZFMISC_1:119;
        dom (the Mult of L1) = [:REAL,the carrier of L1:] by FUNCT_2:def 1;
     then A7: dom ((the Mult of Linear_Space_of_RealSequences) | [:REAL,W:])
       =[:REAL,W:] by A6,RELAT_1:91;
        r*u =(the Mult of l1).[r,u] by RLVECT_1:def 4
        .=((the Mult of Linear_Space_of_RealSequences)|[:REAL,W:]).[r,u]
         by Def3,RSSPACE:def 9
        .=(the Mult of Linear_Space_of_RealSequences).[r,u]
         by A7,Def3,FUNCT_1:70
        .=r*u1 by RLVECT_1:def 4;
     hence thesis by RSSPACE:5,def 7;
   end;

A8:for u be VECTOR of l1 holds -u =-seq_id u & seq_id(-u)=-seq_id u
   proof
     let u be VECTOR of l1;
         -u = (-1)*u by Def3,RLVECT_1:29
        .= (-1)(#)seq_id u by A5
        .= -seq_id u by SEQ_1:25;
     hence thesis by A2;
   end;
A9:for u,v be VECTOR of l1 holds u-v =seq_id u-seq_id v
   proof
     let u,v be VECTOR of l1;
     thus u-v = u+ -v by RLVECT_1:def 11
             .= seq_id u+seq_id(-v) by A3
             .= seq_id u+(-seq_id v) by A8
             .= seq_id u-seq_id v by SEQ_1:15;
   end;

A10:0.l1 = Zeroseq
   proof
     set W = the_set_of_BoundedRealSequences;
     thus 0.l1 = Zero_(W,Linear_Space_of_RealSequences)
                  by Def3,RLVECT_1:def 2
              .= 0.Linear_Space_of_RealSequences by RSSPACE:def 10
              .= Zeroseq by RLVECT_1:def 2,RSSPACE:def 7;
   end;
      for v be VECTOR of l1 holds ||.v.|| = sup rng abs seq_id v
   proof
     let v be VECTOR of l1;
     thus ||.v.|| = (the norm of l1).v by NORMSP_1:def 1
                 .= sup rng abs seq_id v by Def2,Def3;
   end;
   hence thesis by A1,A2,A3,A5,A8,A9,A10,Def3;
end;

theorem Th4:
for x, y being Point of linfty_Space, a be Real holds
  ( ||.x.|| = 0 iff x = 0.linfty_Space ) &
  0 <= ||.x.|| &
  ||.x+y.|| <= ||.x.|| + ||.y.|| &
  ||. a*x .|| = abs(a) * ||.x.||
proof
   let x, y be Point of linfty_Space;
   let a be Real;
A1:now assume
     A2: ||.x.|| = 0;
     A3: ||.x.|| = sup rng abs seq_id x by Th3;
        seq_id x is bounded by Th3;
     then A4: for n be Nat holds 0 = (seq_id x).n by A2,A3,Lm7;
        x in the_set_of_RealSequences by Def1,Def3;
     hence x=0.linfty_Space by A4,Th3,RSSPACE:def 6;
 end;

A5:now assume
      x=0.linfty_Space;
    then A6: for n be Nat holds (seq_id x).n=0 by Th3,RSSPACE:def 6;
    thus ||.x.|| = sup rng abs seq_id x by Th3
       .= 0 by A6,Lm6;
end;

A7:seq_id x is bounded by Def1,Def3;
A8:0 <= sup rng abs seq_id x
proof
 A9: abs seq_id x is bounded by A7,SEQM_3:70;
     abs(seq_id x).1 =abs((seq_id x).1) by SEQ_1:16;
 then 0<= abs(seq_id x).1 by ABSVALUE:5;
 hence 0 <= sup rng abs seq_id x by A9,Lm2;
end;
A10:||.y.|| = sup rng abs seq_id y by Th3;
A11:sup rng abs seq_id(x+y) = ||.x + y.|| by Th3;
A12:for n be Nat holds (abs seq_id(x+y)).n
       = abs(((seq_id x).n) + ((seq_id y).n))
   proof
     let n be Nat;
        (abs seq_id(x+y)).n = (abs(seq_id(seq_id x+seq_id y))).n by Th3
        .= (abs(seq_id x+seq_id y)).n by RSSPACE:3
        .= abs((seq_id x+seq_id y).n) by SEQ_1:16
        .= abs(((seq_id x).n)+((seq_id y).n)) by SEQ_1:11;
     hence thesis;
   end;
A13:for n be Nat holds (abs seq_id(x+y)).n
        <= (abs seq_id x).n + (abs seq_id y).n
   proof
     let n be Nat;
        abs(((seq_id x).n)+ ((seq_id y).n))
       <= abs((seq_id x).n) + abs((seq_id y).n) by ABSVALUE:13;
     then (abs(seq_id(x+y))) .n
       <= abs((seq_id x).n) + abs((seq_id y).n) by A12;
     then (abs seq_id(x+y)) .n
        <= (abs seq_id x).n + abs((seq_id y).n) by SEQ_1:16;
     hence thesis by SEQ_1:16;
   end;
A14:for n being Nat holds
     (abs(seq_id(x+y))).n <= ((abs seq_id x) + (abs seq_id y)).n
   proof
     let n be Nat;
        (abs seq_id x).n + (abs seq_id y).n
        =((abs seq_id x) + (abs seq_id y)).n by SEQ_1:11;
     hence thesis by A13;
    end;
       seq_id x is bounded by Def1,Def3;
then A15:abs seq_id x is bounded by SEQM_3:70;
       seq_id y is bounded by Def1,Def3;
then A16:abs seq_id y is bounded by SEQM_3:70;
A17:sup rng abs seq_id(x+y)
   <= sup rng abs seq_id x + sup rng abs seq_id y
proof
      now let n be Nat;
A18: (abs seq_id(x+y)).n
        <= (abs seq_id x + abs seq_id y).n by A14;
A19:  (abs seq_id x + abs seq_id y).n
        = (abs seq_id x).n + (abs seq_id y).n by SEQ_1:11;
A20:   (abs seq_id x).n <=sup rng abs seq_id x by A15,Lm2;
     (abs seq_id y).n <=sup rng abs seq_id y by A16,Lm2;
then ((abs seq_id x) + (abs seq_id y)).n <=
          sup(rng abs seq_id x) + sup rng abs seq_id y
         by A19,A20,REAL_1:55;
   hence (abs seq_id(x+y)).n
     <= sup rng abs seq_id x + sup rng abs seq_id y
   by A18,AXIOMS:22;
 end;
  hence thesis by Lm1;
end;

A21:for n be Nat holds
     (abs(a(#)seq_id x)).n =(abs a)*(abs(seq_id x).n)
   proof
     let n be Nat;
        (abs(a(#)seq_id x)).n
               =abs((a(#)seq_id x).n) by SEQ_1:16
              .=abs(a*((seq_id x).n)) by SEQ_1:13
              .=(abs a)*(abs((seq_id x).n)) by ABSVALUE:10
              .=(abs a)*(abs(seq_id x).n) by SEQ_1:16;
     hence thesis;
   end;
      seq_id x is bounded by Def1,Def3;
   then abs seq_id x is bounded by SEQM_3:70;
   then rng abs seq_id x is bounded by PSCOMP_1:7;
then A22: rng abs seq_id x is bounded_above by SEQ_4:def 3;
A23: 0 <= abs a by ABSVALUE:5;
      ||. a*x .|| = sup rng abs seq_id(a*x) by Th3
      .=sup rng abs(seq_id(a(#)seq_id x)) by Th3
      .=sup(rng abs(a(#)seq_id x)) by RSSPACE:3
      .=sup(rng ((abs a) (#) (abs seq_id x))) by A21,SEQ_1:13
      .=sup(abs(a)*rng abs seq_id x) by INTEGRA2:17
      .=abs(a)*sup rng abs seq_id x by A22,A23,INTEGRA2:13
      .=abs(a)*||.x.|| by Th3;
   hence thesis by A1,A5,A8,A10,A11,A17,Th3;
end;

registration
  cluster linfty_Space -> RealNormSpace-like RealLinearSpace-like
       Abelian add-associative right_zeroed right_complementable;
  coherence by Def3,Th4,NORMSP_1:def 2;
end;

 reserve NRM for non empty RealNormSpace;
 reserve seq for sequence of NRM;

theorem
   for vseq be sequence of linfty_Space st vseq is Cauchy_sequence_by_Norm
 holds vseq is convergent
proof
  let vseq be sequence of linfty_Space such that
A1:vseq is Cauchy_sequence_by_Norm;
  defpred P[set,set] means ex i be Nat st $1=i &
    ex rseqi be Real_Sequence st
      (for n be Nat holds rseqi.n=(seq_id(vseq.n)).i) &
        rseqi is convergent & $2 = lim rseqi;
A2:for x be set st x in NAT ex y be set st y in REAL & P[x,y]
   proof
    let x be set; assume
       x in NAT;
    then reconsider i=x as Nat;
    deffunc F(Nat) = (seq_id(vseq.$1)).i;
    consider rseqi be Real_Sequence such that
A3:  for n be Nat holds rseqi.n = F(n) from SEQ_1:sch 1;
A4:  rseqi is convergent
     proof
          now
         let e be real number such that
A5:     e > 0;
         thus ex k be Nat st
          for m be Nat st k <= m holds abs(rseqi.m -rseqi.k) < e
         proof
         e is Real by XREAL_0:def 1;
           then consider k be Nat such that
A6:       for n, m be Nat st n >= k & m >= k holds
           ||.(vseq.n) - (vseq.m).|| < e by A1,A5,RSSPACE3:10;
            take k;
             let m be Nat such that
A7:         k<=m;
               ||.(vseq.m) - (vseq.k).||
             =sup rng abs(seq_id((vseq.m) - (vseq.k))) by Th3;
then A8:        sup rng abs(seq_id((vseq.m) - (vseq.k))) < e by A6,A7;
          seq_id((vseq.m)-(vseq.k)) is bounded by Def1,Def3;
then abs(seq_id((vseq.m)-(vseq.k))) is bounded by SEQM_3:70;
then A9:        abs(seq_id((vseq.m) - (vseq.k))).i
              <= sup rng abs(seq_id((vseq.m) - (vseq.k))) by Lm2;
          seq_id((vseq.m) - (vseq.k))
              =seq_id(seq_id(vseq.m)-seq_id(vseq.k)) by Th3
             .= seq_id(vseq.m)-seq_id(vseq.k) by RSSPACE:3
             .= seq_id(vseq.m)+-seq_id(vseq.k) by SEQ_1:15;
             then (seq_id((vseq.m) - (vseq.k))).i
              =(seq_id(vseq.m)).i+(-seq_id(vseq.k)).i by SEQ_1:11
             .=(seq_id(vseq.m)).i+(-(seq_id(vseq.k)).i) by SEQ_1:14
             .=(seq_id(vseq.m)).i-(seq_id(vseq.k)).i
             .=rseqi.m -(seq_id(vseq.k)).i by A3
             .=rseqi.m - rseqi.k by A3;
            then abs(rseqi.m-rseqi.k) = abs(seq_id((vseq.m) - (vseq.k))).i
             by SEQ_1:16;
            hence thesis by A8,A9,AXIOMS:22;
         end;
       end;
       hence thesis by SEQ_4:58;
     end;
     take y = lim rseqi;
     thus thesis by A3,A4;
   end;
   consider f be Function of NAT,REAL such that
A10:for x be set st x in NAT holds P[x,f.x] from FUNCT_2:sch 1(A2);
   reconsider tseq=f as Real_Sequence;
A11:now
     let i be Nat;
     reconsider x=i as set;
        ex i0 be Nat st x=i0 & ex rseqi be Real_Sequence st
       ( for n be Nat holds rseqi.n=(seq_id(vseq.n)).i0 ) &
      rseqi is convergent & f.x=lim rseqi by A10;
      hence ex rseqi be Real_Sequence st
       ( for n be Nat holds rseqi.n=(seq_id(vseq.n)).i ) &
       rseqi is convergent & tseq.i=lim rseqi;
   end;
A12:for e be Real st e >0
   ex k be Nat st
    for n be Nat st n >= k holds
     abs(seq_id tseq-seq_id(vseq.n)) is bounded &
       sup rng abs(seq_id tseq-seq_id(vseq.n)) <= e
proof
 let e be Real such that
A13:e > 0;
  consider k be Nat such that
A14: for n, m be Nat st n >= k & m >= k holds
      ||.(vseq.n) - (vseq.m).|| < e by A1,A13,RSSPACE3:10;
A15: for m,n be Nat st n >= k & m >= k holds
      abs seq_id((vseq.n) - (vseq.m)) is bounded &
        sup rng abs seq_id((vseq.n) - (vseq.m)) < e
     proof
       let m,n be Nat such that
       A16:  n >= k & m >= k;
          ||.(vseq.n) - (vseq.m).|| < e by A14,A16;
       then A17:   (the norm of linfty_Space).((vseq.n) - (vseq.m))
       < e by NORMSP_1:def 1;
           seq_id((vseq.n) - (vseq.m)) is bounded by Def1,Def3;
       hence thesis by A17,Def2,Def3,SEQM_3:70;
     end;
A18:  for n be Nat for i be Nat holds
         for rseq be Real_Sequence st
           ( for m be Nat holds
               rseq.m=abs(seq_id(vseq.m-vseq.n)).i ) holds
         ( rseq is convergent &
             lim rseq = abs((seq_id tseq -seq_id(vseq.n))).i )
     proof
       let n be Nat;
A19:   for m,k be Nat holds
       seq_id((vseq.m) - (vseq.k)) = seq_id(vseq.m)-seq_id(vseq.k)
       proof
         let m,k be Nat;
            seq_id((vseq.m) - (vseq.k))
            = seq_id(seq_id(vseq.m)-seq_id(vseq.k)) by Th3;
         hence thesis by RSSPACE:3;
       end;
          now let i be Nat;
         consider rseqi be Real_Sequence such that
    A20: ( for n be Nat holds rseqi.n=(seq_id(vseq.n)).i ) &
             rseqi is convergent & tseq.i=lim rseqi by A11;
            now let rseq be Real_Sequence such that
           A21: for m be Nat holds
           rseq.m=abs(seq_id(vseq.m-vseq.n)).i;
           A22: now let m be Nat;
           A23:    seq_id(vseq.m - vseq.n)
                   = seq_id(vseq.m)-seq_id(vseq.n) by A19;
                   thus rseq.m=abs(seq_id(vseq.m-vseq.n)).i by A21
                     .=abs((seq_id(vseq.m-vseq.n)).i) by SEQ_1:16
                     .=abs((seq_id(vseq.m)).i
                       -(seq_id(vseq.n)).i) by A23,RFUNCT_2:6
                     .=abs((rseqi.m) -(seq_id(vseq.n)).i) by A20;
                end;
              abs(tseq.i-(seq_id(vseq.n)).i)
                   = abs((tseq-(seq_id(vseq.n))).i) by RFUNCT_2:6
                  .= abs((seq_id tseq-(seq_id(vseq.n))).i) by RSSPACE:3
                  .= abs((seq_id tseq -seq_id(vseq.n))).i by SEQ_1:16;
                hence rseq is convergent &
                   lim rseq = abs(seq_id tseq -seq_id(vseq.n)).i
                 by A20,A22,RSSPACE3:1;
              end;
           hence for rseq be Real_Sequence st
            ( for m be Nat holds
               rseq.m=abs(seq_id(vseq.m-vseq.n)).i ) holds
            rseq is convergent
            & lim rseq = abs(seq_id tseq -seq_id(vseq.n)).i;
             end;
       hence thesis;
     end;
        for n be Nat st n >= k holds
      abs(seq_id tseq-seq_id(vseq.n)) is bounded &
        sup rng abs(seq_id tseq-seq_id(vseq.n)) <= e
     proof
       let n be Nat such that
A24:   n >= k;
A25:  for i be Nat holds abs((seq_id tseq -seq_id(vseq.n))).i <= e
       proof
         let i be Nat;
         deffunc F(Nat)= abs(seq_id((vseq.$1) - (vseq.n))).i;
         consider rseq be Real_Sequence such that
A26:     for m be Nat holds rseq.m = F(m) from SEQ_1:sch 1;
A27:     rseq is convergent by A18,A26;
A28:     lim rseq = abs(seq_id tseq-seq_id(vseq.n)).i by A18,A26;
            for m be Nat st m >= k holds rseq.m <= e
         proof
           let m be Nat;
           assume m >= k;
then A29:       abs(seq_id((vseq.m) - (vseq.n))) is bounded &
             sup rng abs seq_id((vseq.m) - (vseq.n)) <= e
               by A15,A24;
then A30:       abs(seq_id((vseq.m) - (vseq.n))).i
             <=sup rng abs seq_id((vseq.m) - (vseq.n)) by Lm2;
              rseq.m = abs(seq_id((vseq.m) - (vseq.n))).i by A26;
           hence thesis by A29,A30,AXIOMS:22;
         end;
        hence thesis by A27,A28,RSSPACE2:6;
  end;
     abs(seq_id tseq-seq_id(vseq.n)) is bounded
  proof
A31:  0 + 0 < 1 + e by A13,REAL_1:67;
A32:  0 + e < 1 + e by REAL_1:67;
            now let i be Nat;
            A33: abs((seq_id tseq -seq_id(vseq.n))).i <= e by A25;
               abs ((seq_id tseq -seq_id(vseq.n))).i
                  =abs(((seq_id tseq -seq_id(vseq.n))).i) by SEQ_1:16;
            hence abs(((seq_id tseq -seq_id(vseq.n))).i)
                <e+1 by A32,A33,AXIOMS:22;
         end;
        then seq_id tseq -seq_id(vseq.n) is bounded by A31,SEQ_2:15;
        hence thesis by SEQM_3:70;
     end;
   hence thesis by A25,Lm1;
 end;
hence thesis;
end;
A34:seq_id tseq is bounded
   proof
     consider m be Nat such that
A35: for n be Nat st n >= m holds
     abs (seq_id tseq -seq_id(vseq.n)) is bounded &
     sup rng abs (seq_id tseq -seq_id(vseq.n)) <= 1 by A12;
A36: abs (seq_id tseq -seq_id(vseq.m)) is bounded by A35;
        seq_id(vseq.m) is bounded by Def1,Def3;
then A37: abs seq_id (vseq.m) is bounded by SEQM_3:70;
     set a=abs(seq_id tseq -seq_id(vseq.m));
     set b=abs seq_id(vseq.m);
     set d=abs seq_id tseq;
A38: a + b is bounded by A36,A37,SEQM_3:71;
A39: for i be Nat holds d.i <= (a+b).i
     proof
       let i be Nat;
A40:   a.i = abs((seq_id tseq -seq_id(vseq.m)).i) by SEQ_1:16
          .= abs((seq_id tseq+-seq_id(vseq.m)).i) by SEQ_1:15
          .= abs((seq_id tseq).i+(-seq_id(vseq.m)).i) by SEQ_1:11
          .= abs((seq_id tseq).i+(-(seq_id(vseq.m)).i)) by SEQ_1:14
          .=abs((seq_id tseq).i-(seq_id(vseq.m)).i);
A41:   b.i=abs((seq_id(vseq.m)).i) by SEQ_1:16;
          d.i=abs((seq_id tseq).i) by SEQ_1:16;
       then d.i-b.i <= a.i by A40,A41,ABSVALUE:18;
       then d.i-b.i+b.i<= a.i + b.i by AXIOMS:24;
       hence thesis by SEQ_1:11;
     end;
        d is bounded
     proof
        reconsider r=sup rng (a+b)+1 as real number;
             0 <= sup rng(a+b)
         proof
            A42: (a+b).1 =a.1 + b.1 by SEQ_1:11;
               a.1=abs((seq_id tseq -seq_id(vseq.m)).1) by SEQ_1:16;
            then A43:    0<= a.1 by ABSVALUE:5;
              b.1=abs( (seq_id(vseq.m)).1 ) by SEQ_1:16;
            then 0<= b.1 by ABSVALUE:5;
           then 0 + 0 <= a.1+ b.1 by A43,REAL_1:55;
            hence 0<= sup rng(a+b) by A38,A42,Lm2;
         end;
then A44:      0+0 < sup( rng(a+b) )+1 by REAL_1:67;
A45:     sup( rng(a+b) ) +0 < sup( rng(a+b) )+1 by REAL_1:67;
           now let i be Nat;
           A46: d.i <= (a+b).i by A39;
              (a+b).i <= sup rng (a+b) by A38,Lm2;
           then A47:  d.i <= sup rng (a+b) by A46,AXIOMS:22;
              d.i=abs((seq_id tseq).i) by SEQ_1:16;
           hence abs((seq_id tseq).i) <r by A45,A47,AXIOMS:22;
        end;
       then seq_id tseq is bounded by A44,SEQ_2:15;
       hence thesis by SEQM_3:70;
     end;
     hence thesis by SEQM_3:70;
   end;
A48:tseq in the_set_of_RealSequences by RSSPACE:def 1;
   then reconsider tv=tseq as Point of linfty_Space by A34,Def1,Def3;
   take tv;
     let e1 be Real such that
A49: e1 > 0;
         set e=e1/2;
A50:   e > 0 by A49,SEQ_2:3;
A51:  e < e1 by A49,SEQ_2:4;
     consider m be Nat such that
A52: for n be Nat st n >= m holds
         abs(seq_id tseq-seq_id(vseq.n)) is bounded &
     sup rng abs(seq_id tseq-seq_id(vseq.n)) <= e by A12,A50;
        now let n be Nat such that
A53:   n >= m;
       reconsider u=tseq as VECTOR of linfty_Space
       by A34,A48,Def1,Def3;
A54:   sup rng( abs(seq_id tseq-seq_id(vseq.n))) <= e by A52,A53;
       reconsider v=vseq.n as VECTOR of linfty_Space;
          seq_id(u-v) = u-v by Th3;
       then sup rng abs seq_id(u-v)
       = sup rng abs(seq_id tseq-seq_id(vseq.n)) by Th3;
then A55:   (the norm of linfty_Space).(u-v) <= e by A54,Def2,Def3;
          ||.(vseq.n) - tv.|| =||.(vseq.n) +-tv.|| by RLVECT_1:def 11
          .=||.-(tv-(vseq.n)).|| by RLVECT_1:47
          .=||.tv-(vseq.n).|| by NORMSP_1:6;
       then ||.(vseq.n) - tv.|| <= e by A55,NORMSP_1:def 1;
       hence ||.(vseq.n) - tv.|| < e1 by A51,AXIOMS:22;
     end;
     hence thesis;
end;

begin

definition
  let X be non empty set;
  let Y be RealNormSpace;
  let IT be Function of X, the carrier of Y;
  attr IT is bounded means :Def4:
  ex K being Real st 0 <= K &
   for x being Element of X holds
   ||. IT.x .|| <= K;
end;

theorem Th6:
  for X be non empty set
    for Y be RealNormSpace holds
      for f be Function of X,the carrier of Y
         st (for x be Element of X holds f.x=0.Y)
        holds f is bounded
 proof
    let X be non empty set; let Y be RealNormSpace;
    let f be Function of X,the carrier of Y such that
        A1: for x be Element of X holds f.x=0.Y;
    A2:
      now
        let x be Element of X;
        thus ||. f.x .|| = ||. 0.Y .|| by A1
         .=0 by NORMSP_1:5;
       end;
     take 0;
     thus thesis by A2;
  end;

registration
  let X be non empty set;
  let Y be RealNormSpace;
  cluster bounded Function of X,the carrier of Y;
existence
proof
  set f=X --> 0.Y;
  reconsider f as Function of X,the carrier of Y by FUNCOP_1:58;
  take f;
     for x be Element of X holds f.x =0.Y by FUNCOP_1:13;
  hence thesis by Th6;
 end;
end;

definition
  let X be non empty set;
  let Y be RealNormSpace;
  func BoundedFunctions(X,Y) ->
    Subset of RealVectSpace(X,Y) means :Def5:
   for x being set holds x in it
     iff
   x is bounded Function of X,the carrier of Y;
existence
proof
  defpred P[set] means $1 is bounded Function of X,the carrier of Y;
  consider IT being set such that
  A1:for x being set holds x in IT
    iff x in Funcs(X,the carrier of Y)  & P[x] from XBOOLE_0:sch 1;
A2:  IT is Subset of Funcs(X,the carrier of Y)
proof
        for x be set st x in IT holds x
     in Funcs(X,the carrier of Y) by A1;
     hence thesis by TARSKI:def 3;
   end;
  A3: the carrier of RealVectSpace(X,Y) = Funcs(X,the carrier of Y)
  proof
     RealVectSpace(X,Y) = RLSStruct(#Funcs(X,the carrier of Y),
              (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#)
              by LOPBAN_1:def 4;
  hence thesis;
  end;
  take IT;
  thus IT is Subset of RealVectSpace(X,Y) by A2,A3;
  let x be set;
  thus x in IT implies x is bounded Function of X,the carrier of Y by A1;
 assume
A4:  x is bounded Function of X,the carrier of Y;
  then x in Funcs(X,the carrier of Y) by FUNCT_2:11;
  hence thesis by A1,A4;
end;
uniqueness
proof
   let X1,X2 be Subset of RealVectSpace(X,Y);
 assume that
A5: for x being set holds x in X1 iff
     x is bounded Function of X,the carrier of Y and
A6: for x being set holds x in X2 iff
     x is bounded Function of X,the carrier of Y;
       for x being set st x in X1 holds x in X2
    proof
      let x be set;
    assume x in X1;
      then x is bounded Function of X,the carrier of Y by A5;
      hence thesis by A6;
    end;
then A7: X1 c= X2 by TARSKI:def 3;
       for x being set st x in X2 holds x in X1
    proof
      let x be set;
      assume x in X2;
      then x is bounded Function of X,the carrier of Y by A6;
      hence thesis by A5;
    end;
    then X2 c= X1 by TARSKI:def 3;
    hence thesis by A7,XBOOLE_0:def 10;
  end;
end;

registration
  let X be non empty set;
  let Y be RealNormSpace;
  cluster BoundedFunctions(X,Y) -> non empty;
  coherence
   proof
     set f=X --> 0.Y;
     reconsider f as  Function of X,the carrier of Y
     by FUNCOP_1:58;
        f is bounded
     proof
          for x be Element of X holds f.x =0.Y by FUNCOP_1:13;
       hence thesis by Th6;
     end;
     hence thesis by Def5;
   end;
end;

theorem Th7:
  for X be non empty set for Y be RealNormSpace holds
  BoundedFunctions(X,Y) is lineary-closed
proof
  let X be non empty set; let Y be RealNormSpace;
  set W = BoundedFunctions(X,Y);
A1:  RealVectSpace(X,Y) =
  RLSStruct(#Funcs(X,the carrier of Y),
              (FuncZero(X,Y)),FuncAdd(X,Y),FuncExtMult(X,Y)#)
              by LOPBAN_1:def 4;
A2:for v,u be VECTOR of RealVectSpace(X,Y)  st
    v in W & u in W
    holds v + u in W
   proof
     let v,u be VECTOR of RealVectSpace(X,Y)  such that
     A3: v in W & u in W;
     reconsider f=v+u as Function of X,the carrier of Y
     by A1,FUNCT_2:121;
         f is bounded
     proof
     reconsider u1=u as bounded Function of X,
     the carrier of Y by A3,Def5;
     reconsider v1=v as bounded Function of X,
     the carrier of Y by A3,Def5;
     consider K1 be Real such that
       A4: 0 <= K1 &
     for x be Element of X holds ||. u1.x .||   <= K1 by Def4;
     consider K2 be Real such that
       A5: 0 <= K2 &
     for x be Element of X holds ||. v1.x .|| <= K2 by Def4;
     take K3=K1+K2;
         A6: 0 + 0 <= K1+K2 by A4,A5,REAL_1:55;
          now let x be Element of X;
        A7: ||. f.x .|| =||. v1.x+u1.x .|| by LOPBAN_1:14;
        A8: ||. u1.x+v1.x .|| <= ||. u1.x .||+ ||. v1.x .|| by NORMSP_1:def 2;
        A9:||. u1.x .|| <= K1 by A4;
          ||. v1.x .|| <= K2 by A5;
        then ||. u1.x .|| + ||. v1.x .|| <= K1 +K2 by A9,REAL_1:55;
       hence ||. f.x .|| <= K3 by A7,A8,AXIOMS:22;
        end;
     hence thesis by A6;
     end;
     hence thesis by Def5;
  end;

   for a be Real for v be VECTOR of RealVectSpace(X,Y)
    st v in W holds a * v in W
    proof
     let a be Real;
     let v be VECTOR of RealVectSpace(X,Y) such that
           A10: v in W;
          reconsider f=a*v as Function of X,the carrier of Y by A1,FUNCT_2:121;
             f is bounded
          proof
     reconsider v1=v as bounded Function of X,
     the carrier of Y by A10,Def5;
     consider K be Real such that
       A11: 0 <= K &
        for x be Element of X holds ||. v1.x .|| <= K by Def4;
      take K1=abs(a)*K;
      A12: 0 <= K & 0 <=abs(a) by A11,ABSVALUE:5;
         now let x be Element of X;
        A13: ||. f.x .|| =||. a*v1.x .|| by LOPBAN_1:15;
        A14: ||. a*v1.x .|| = abs(a)* ||. v1.x .|| by NORMSP_1:def 2;
        A15:||. v1.x .|| <= K by A11;
           0 <=abs(a) by ABSVALUE:5;
       hence ||. f.x .|| <= abs(a)* K by A13,A14,A15,AXIOMS:25;
     end;
     hence thesis by A12,REAL_2:121;
     end;
     hence thesis by Def5;
     end;
 hence thesis by A2,RLSUB_1:def 1;
end;

theorem Th8:
  for X be non empty set for Y be RealNormSpace holds
RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
        Mult_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)) #)
is Subspace of RealVectSpace(X,Y)
proof
  let X be non empty set;
  let Y be RealNormSpace;
     BoundedFunctions(X,Y) is lineary-closed by Th7;
  hence thesis by RSSPACE:13;
end;

registration
  let X be non empty set;
  let Y be RealNormSpace;
  cluster RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
        Mult_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)) #)
 -> Abelian add-associative right_zeroed right_complementable
        RealLinearSpace-like;
  coherence by Th8;
end;

canceled;

definition
  let X be non empty set;
  let Y be RealNormSpace;
  func R_VectorSpace_of_BoundedFunctions(X,Y) -> RealLinearSpace equals
  :Def6:
RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
        Mult_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)) #);
  coherence;
end;

registration
  let X be non empty set; let Y be RealNormSpace;
  cluster R_VectorSpace_of_BoundedFunctions(X,Y) -> strict;
  coherence
proof
     R_VectorSpace_of_BoundedFunctions(X,Y)=
  RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
        Mult_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)) #) by Def6;
   hence thesis;
 end;
end;

theorem Th10:
     for X be non empty set for  Y be RealNormSpace
     for f,g,h be VECTOR of R_VectorSpace_of_BoundedFunctions(X,Y)
       for f',g',h' be bounded Function of X,the carrier of Y
        st f'=f & g'=g & h'=h holds
      (h = f+g iff (for x be Element of X holds (h'.x = f'.x + g'.x)) )
proof
     let X be non empty set;
     let Y be RealNormSpace;
     let f,g,h be VECTOR of R_VectorSpace_of_BoundedFunctions(X,Y);
     let f',g',h' be bounded Function of X,the carrier of Y such that
       A1: f'=f and
       A2: g'=g and
       A3: h'=h;
     R_VectorSpace_of_BoundedFunctions(X,Y)=
  RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
        Mult_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)) #) by Def6;
      then A4:R_VectorSpace_of_BoundedFunctions(X,Y) is
           Subspace of RealVectSpace(X,Y) by Th8;
        then reconsider f1=f as VECTOR
           of RealVectSpace(X,Y) by RLSUB_1:18;
       reconsider g1=g as VECTOR
          of RealVectSpace(X,Y) by A4,RLSUB_1:18;
       reconsider h1=h as VECTOR
         of RealVectSpace(X,Y) by A4,RLSUB_1:18;
       reconsider f1'=f', g1'=g', h1'=h' as Element
            of Funcs(X, the carrier of Y) by FUNCT_2:11;
      A5: now assume
            A6: h = f+g;
             let x be Element of X;
                h1=f1+g1 by A4,A6,RLSUB_1:21;
             hence h'.x=f'.x+g'.x by A1,A2,A3,LOPBAN_1:14;
    end;
       now assume
               for x be Element of X holds h'.x=f'.x+g'.x;
           then h1=f1+g1 by A1,A2,A3,LOPBAN_1:14;
           hence h =f+g by A4,RLSUB_1:21;
     end;
     hence thesis by A5;
 end;

theorem Th11:
     for X be non empty set for  Y be RealNormSpace
     for f,h be VECTOR of R_VectorSpace_of_BoundedFunctions(X,Y)
     for f',h' be bounded Function of X,the carrier of Y
         st f'=f & h'=h
     for a be Real holds
      h = a*f iff for x be Element of X holds h'.x = a*f'.x
  proof
     let X be non empty set;
     let Y be RealNormSpace;
     let f,h be VECTOR of R_VectorSpace_of_BoundedFunctions(X,Y);
     let f',h' be bounded Function of X,the carrier of Y such that
       A1: f'=f and
       A2: h'=h;
     let a be Real;
     R_VectorSpace_of_BoundedFunctions(X,Y) =
  RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Mult_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)) #) by Def6;
      then A3:R_VectorSpace_of_BoundedFunctions(X,Y) is
           Subspace of RealVectSpace(X,Y) by Th8;
        then reconsider f1=f, h1=h as VECTOR
                   of RealVectSpace(X,Y) by RLSUB_1:18;
       reconsider f1'=f', h1'=h' as Element
            of Funcs(X, the carrier of Y) by FUNCT_2:11;
      A4: now assume
            A5: h = a*f;
             let x be Element of X;
                h1=a*f1 by A3,A5,RLSUB_1:22;
             hence h'.x=a*f'.x by A1,A2,LOPBAN_1:15;
    end;
        now assume
               for x be Element of X holds h'.x=a*f'.x;
           then h1=a*f1 by A1,A2,LOPBAN_1:15;
           hence h =a*f by A3,RLSUB_1:22;
     end;
     hence thesis by A4;
 end;

theorem Th12:
     for X be non empty set for Y be RealNormSpace holds
          0.R_VectorSpace_of_BoundedFunctions(X,Y)
          =(X -->0.Y)
     proof
     let X be non empty set; let Y be RealNormSpace;
        R_VectorSpace_of_BoundedFunctions(X,Y) =
    RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Mult_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)) #) by Def6;
      then A1:R_VectorSpace_of_BoundedFunctions(X,Y) is
           Subspace of RealVectSpace(X,Y) by Th8;
        0.RealVectSpace(X,Y)
          =(X -->0.Y) by LOPBAN_1:16;
     hence thesis by A1,RLSUB_1:19;
    end;

definition
  let X be non empty set; let Y be RealNormSpace;
  let f be set such that
  A1: f in BoundedFunctions(X,Y);
  func modetrans(f,X,Y)
  -> bounded Function of X,the carrier of Y equals:Def7:
    f;
  coherence by A1,Def5;
end;

definition
   let X be non empty set; let Y be RealNormSpace;
   let u be Function of X,the carrier of Y;
   func PreNorms(u) -> non empty Subset of REAL equals :Def8:
  {||.u.t.|| where t is Element of X : not contradiction  };
 coherence
 proof
   A1: now let x be set;
            now assume
              x in {||.u.t.|| where t is Element of X
           : not contradiction };
           then consider t be Element of X such that
            A2:   x=||.u.t.||;
          thus x in REAL by A2;
         end;
         hence x in {||.u.t.|| where t is Element of X
         : not contradiction } implies x in REAL;
       end;
     consider x be Element of X;
       ||.u.x.|| in {||.u.t.|| where t is Element of X
    : not contradiction };
    hence thesis by A1,TARSKI:def 3;
  end;
end;

theorem Th13:
    for X be non empty set for Y be RealNormSpace
    for g be bounded Function of X,the carrier of Y
      holds PreNorms(g) is non empty bounded_above
   proof
       let X be non empty set; let Y be RealNormSpace;
       let g be bounded Function of X,the carrier of Y;
      PreNorms(g) is bounded_above
   proof
  consider K be Real such that
        0 <= K and
    A1: for x be Element of X holds
       ||. g.x .|| <= K by Def4;
  A2:now let r be Real such that
        A3:  r in PreNorms(g);
            r in {||.g.t.|| where t is Element of X: not contradiction }
    by A3,Def8;
       then consider t be Element of X such that
          A4:   r=||.g.t.||;
        thus r <=K by A1,A4;
      end;
   take K;
   thus thesis by A2;
   end;
 hence thesis;
 end;

theorem
       for X be non empty set for Y be RealNormSpace
    for g be Function of X,the carrier of Y
      holds g is bounded iff PreNorms(g) is bounded_above
   proof
       let X be non empty set; let Y be RealNormSpace;
       let g be Function of X,the carrier of Y;

    now assume
    A1: PreNorms(g) is bounded_above;
         reconsider K=sup PreNorms(g) as Real;
    A2:  0 <= K
    proof
    A3:now let r be Real such that
        A4: r in PreNorms(g);
           r in {||.g.t.|| where t is Element of X : not contradiction  }
    by A4,Def8;
        then consider t be Element of X such that
          A5: r=||.g.t.||;
          thus 0 <= r by A5,NORMSP_1:8;
    end;
  consider r0 be set such that
  A6: r0 in PreNorms(g) by XBOOLE_0:def 1;
  reconsider r0 as Real by A6;
     0 <= r0 by A3,A6;
    hence thesis by A1,A6,SEQ_4:def 4;
    end;
    A7: now let t be Element of X;
           ||.g.t.|| in
              {||.g.s.|| where s is Element of X : not contradiction };
          then ||.g.t.|| in PreNorms(g) by Def8;
          hence ||.g.t.|| <= K by A1,SEQ_4:def 4;
        end;
 take K;
 thus g is bounded by A2,A7,Def4;
 end;
hence thesis by Th13;
end;

theorem Th15:
   for X be non empty set for Y be RealNormSpace
    ex NORM be Function of BoundedFunctions(X,Y),REAL st
          for f be set st f in BoundedFunctions(X,Y) holds
        NORM.f = sup PreNorms(modetrans(f,X,Y))
proof
   let X be non empty set; let Y be RealNormSpace;
  deffunc F(set) = sup PreNorms(modetrans($1,X,Y));
A1:for z be set st z in BoundedFunctions(X,Y) holds F(z) in REAL;
      ex f being Function of BoundedFunctions(X,Y),REAL st
     for x being set st x in BoundedFunctions(X,Y) holds
       f.x = F(x) from FUNCT_2:sch 2(A1);
   hence thesis;
end;

definition
   let X be non empty set; let Y be RealNormSpace;
   func BoundedFunctionsNorm(X,Y)
     -> Function of BoundedFunctions(X,Y), REAL means :Def9:
  for x be set st x in BoundedFunctions(X,Y) holds
    it.x = sup PreNorms(modetrans(x,X,Y));
existence by Th15;
uniqueness
proof
   let NORM1,NORM2 be Function of BoundedFunctions(X,Y), REAL
   such that
A1: (for x be set st x in BoundedFunctions(X,Y) holds
        NORM1.x = sup PreNorms(modetrans(x,X,Y))) and
A2:(for x be set st x in BoundedFunctions(X,Y) holds
     NORM2.x = sup PreNorms(modetrans(x,X,Y)));
A3:dom NORM1 = BoundedFunctions(X,Y) &
     dom NORM2 = BoundedFunctions(X,Y) by FUNCT_2:def 1;
      for z be set st z in BoundedFunctions(X,Y) holds NORM1.z = NORM2.z
   proof
     let z be set such that
A4:  z in BoundedFunctions(X,Y);
        NORM1.z = sup PreNorms(modetrans(z,X,Y)) by A1,A4;
     hence thesis by A2,A4;
  end;
  hence thesis by A3,FUNCT_1:9;
end;
end;

theorem Th16:
   for X be non empty set for Y be RealNormSpace
   for f be bounded Function of X,the carrier of Y holds modetrans(f,X,Y)=f
  proof
     let X be non empty set; let Y be RealNormSpace;
     let f be bounded Function of X,the carrier of Y;
        f in BoundedFunctions(X,Y) by Def5;
     hence thesis by Def7;
  end;

theorem Th17:
   for X be non empty set for Y be RealNormSpace
   for f be bounded Function of X,the carrier of Y holds
   BoundedFunctionsNorm(X,Y).f = sup PreNorms(f)
  proof
       let X be non empty set;let Y be RealNormSpace;
       let f be bounded Function of X,the carrier of Y;
       A1: f in BoundedFunctions(X,Y) by Def5;
      reconsider f'=f as set;
      thus BoundedFunctionsNorm(X,Y).f
        = sup PreNorms(modetrans(f',X,Y)) by A1,Def9
       .= sup PreNorms(f) by Th16;
  end;

definition let X be non empty set;let Y be RealNormSpace;
 func R_NormSpace_of_BoundedFunctions(X,Y) -> non empty NORMSTR equals
 :Def10: NORMSTR (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Mult_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
          BoundedFunctionsNorm(X,Y) #);
  coherence;
end;

theorem Th18:
    for X be non empty set for Y be RealNormSpace
       holds (X --> 0.Y) =
        0.R_NormSpace_of_BoundedFunctions(X,Y)
 proof
     let X be non empty set; let Y be RealNormSpace;
    A1: R_NormSpace_of_BoundedFunctions(X,Y)
     = NORMSTR (# BoundedFunctions(X,Y),
         Zero_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Add_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
           Mult_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
           BoundedFunctionsNorm(X,Y) #) by Def10;
A2: R_VectorSpace_of_BoundedFunctions(X,Y) =
    RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Mult_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)) #) by Def6;
             (X --> 0.Y)
            =0.R_VectorSpace_of_BoundedFunctions(X,Y) by Th12
            .=Zero_(BoundedFunctions(X,Y),
            RealVectSpace(X,Y)) by A2,RLVECT_1:def 2
            .=0.R_NormSpace_of_BoundedFunctions(X,Y) by A1,RLVECT_1:def 2;
  hence thesis;
end;

theorem Th19:
    for X be non empty set for Y be RealNormSpace
    for f being Point of
       R_NormSpace_of_BoundedFunctions(X,Y)
    for g be bounded Function of X,the carrier of Y st g=f holds
      for t be Element of X holds ||.g.t.|| <= ||.f.||
   proof
       let X be non empty set; let Y be RealNormSpace;
       let f being Point of
          R_NormSpace_of_BoundedFunctions(X,Y);
      let g be bounded Function of X,the carrier of Y such that
     A1:g=f;
A2: R_NormSpace_of_BoundedFunctions(X,Y)
     = NORMSTR (# BoundedFunctions(X,Y),
         Zero_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Add_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
           Mult_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
           BoundedFunctionsNorm(X,Y) #) by Def10;
 A3: PreNorms(g) is non empty bounded_above by Th13;
       now let t be Element of X;
           ||.g.t.|| in
              {||.g.s.|| where s is Element of X: not contradiction};
          then ||.g.t.|| in PreNorms(g) by Def8;
          then ||.g.t.|| <= sup PreNorms(g) by A3,SEQ_4:def 4;
          then ||.g.t.|| <= BoundedFunctionsNorm(X,Y).g by Th17;
           hence ||.g.t.|| <= ||.f.|| by A1,A2,NORMSP_1:def 1;
    end;
    hence thesis;
end;

theorem
       for X be non empty set for Y be RealNormSpace
    for f being Point of
       R_NormSpace_of_BoundedFunctions(X,Y)
    holds 0 <= ||.f.||
proof
       let X be non empty set; let Y be RealNormSpace;
       let f being Point of
          R_NormSpace_of_BoundedFunctions(X,Y);
A1: R_NormSpace_of_BoundedFunctions(X,Y)
  = NORMSTR (# BoundedFunctions(X,Y),
         Zero_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Add_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
           Mult_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
           BoundedFunctionsNorm(X,Y)
      #) by Def10;
    then reconsider g=f as bounded Function of X,the carrier of Y by Def5;
 A2: now let r be Real such that
        A3:  r in PreNorms(g);
            r in {||.g.t.|| where t is Element of X: not contradiction }
    by A3,Def8;
        then consider t be Element of X such that
          A4: r=||.g.t.||;
          thus 0 <= r by A4,NORMSP_1:8;
 end;
A5:PreNorms(g) is non empty bounded_above by Th13;
 consider r0 be set such that
  A6: r0 in PreNorms(g) by XBOOLE_0:def 1;
  reconsider r0 as Real by A6;
     0 <= r0 by A2,A6;
  then A7:  0 <=sup PreNorms(g) by A5,A6,SEQ_4:def 4;
     BoundedFunctionsNorm(X,Y).f = sup PreNorms(g) by Th17;
    hence thesis by A1,A7,NORMSP_1:def 1;
  end;

theorem Th21:
    for X be non empty set for Y be RealNormSpace
    for f being Point of
       R_NormSpace_of_BoundedFunctions(X,Y)
       st f = 0.R_NormSpace_of_BoundedFunctions(X,Y)
    holds 0 = ||.f.||
proof
    let X be non empty set; let Y be RealNormSpace;
    let f being Point of
       R_NormSpace_of_BoundedFunctions(X,Y) such that
A1:       f = 0.R_NormSpace_of_BoundedFunctions(X,Y);
A2: R_NormSpace_of_BoundedFunctions(X,Y)
  = NORMSTR (# BoundedFunctions(X,Y),
         Zero_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Add_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
           Mult_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
           BoundedFunctionsNorm(X,Y) #) by Def10;
  thus ||.f.|| = 0
    proof
        set z = X --> 0.Y;
        reconsider z as Function of X, the carrier of Y by FUNCOP_1:58;
        reconsider g=f as bounded Function of X,
        the carrier of Y by A2,Def5;
       A3: z=g by A1,Th18;
       A4:
      now let r be Real such that
        A5:  r in PreNorms(g);
            r in {||.g.t.|| where t is Element of X: not contradiction}
    by A5,Def8;
        then consider t be Element of X such that
          A6:   r=||.g.t.||;
             ||.g.t.|| = ||.0.Y.|| by A3,FUNCOP_1:13
                  .= 0 by NORMSP_1:def 2;
          hence 0 <= r & r <=0 by A6;
      end;
     A7:PreNorms(g) is non empty bounded_above by Th13;
     consider r0 be set such that
      A8: r0 in PreNorms(g) by XBOOLE_0:def 1;
     reconsider r0 as Real by A8;
     A9: 0 <= r0 & r0<=0 by A4,A8;
        (for s be real number st s in PreNorms(g) holds s <= 0)
         implies sup PreNorms(g) <= 0 by SEQ_4:62;
       then sup PreNorms(g) = 0 by A4,A7,A8,A9,SEQ_4:def 4;
       then BoundedFunctionsNorm(X,Y).f=0 by Th17;
      hence thesis by A2,NORMSP_1:def 1;
      end;
end;

theorem Th22:
     for X be non empty set for Y be RealNormSpace
     for f,g,h be Point of R_NormSpace_of_BoundedFunctions(X,Y)
       for f',g',h' be bounded Function of X,the carrier of Y
        st f'=f & g'=g & h'=h holds
      (h = f+g iff
          (for x be Element of X holds (h'.x = f'.x + g'.x)) )
proof
     let X be non empty set;let Y be RealNormSpace;
     let f,g,h be Point of R_NormSpace_of_BoundedFunctions(X,Y);
     let f',g',h' be bounded Function of X,the carrier of Y such that
       A1: f'=f and
       A2: g'=g and
       A3: h'=h;
A4: R_NormSpace_of_BoundedFunctions(X,Y)
  = NORMSTR (# BoundedFunctions(X,Y),
         Zero_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Add_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
           Mult_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
           BoundedFunctionsNorm(X,Y) #) by Def10;
A5: R_VectorSpace_of_BoundedFunctions(X,Y) =
    RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Mult_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)) #) by Def6;
 then reconsider f1=f, g1=g, h1=h as VECTOR of
 R_VectorSpace_of_BoundedFunctions(X,Y) by A4;
    h=f+g iff h1=f1+g1
proof
A6: now
assume h=f+g;
hence h1=Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)).[f,g] by A4,RLVECT_1:def 3
          .=f1+g1 by A5,RLVECT_1:def 3;
end;
   now
assume h1=f1+g1;
hence h=Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y))
         .[f1,g1] by A5,RLVECT_1:def 3
      .=f+g by A4,RLVECT_1:def 3;
end;
hence thesis by A6;
end;
hence thesis by A1,A2,A3,Th10;
end;

theorem Th23:
     for X be non empty set for Y be RealNormSpace
     for f,h be Point of R_NormSpace_of_BoundedFunctions(X,Y)
     for f',h' be bounded Function of X,the carrier of Y st f'=f & h'=h
     for a be Real holds
      h = a*f iff for x be Element of X holds h'.x = a*f'.x
proof
     let X be non empty set; let Y be RealNormSpace;
     let f,h be Point of
     R_NormSpace_of_BoundedFunctions(X,Y);
     let f',h' be bounded Function of X,the carrier of Y such that
       A1: f'=f and
       A2: h'=h;
     let a be Real;
A3: R_NormSpace_of_BoundedFunctions(X,Y)
  = NORMSTR (# BoundedFunctions(X,Y),
         Zero_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Add_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
           Mult_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
           BoundedFunctionsNorm(X,Y) #) by Def10;
A4: R_VectorSpace_of_BoundedFunctions(X,Y) =
    RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Mult_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)) #) by Def6;
 then reconsider f1=f as VECTOR of
 R_VectorSpace_of_BoundedFunctions(X,Y) by A3;
 reconsider h1=h as VECTOR of
 R_VectorSpace_of_BoundedFunctions(X,Y) by A3,A4;
   h=a*f iff h1=a*f1
proof

A5: now
assume h=a*f;
hence h1=Mult_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)).[a,f] by A3,RLVECT_1:def 4
          .=a*f1 by A4,RLVECT_1:def 4;
end;
   now
assume h1=a*f1;
hence
        h=Mult_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y))
         .[a,f1] by A4,RLVECT_1:def 4
      .=a*f by A3,RLVECT_1:def 4;
end;
hence thesis by A5;
end;
hence thesis by A1,A2,Th11;
end;

theorem Th24:
    for X be non empty set
    for Y be RealNormSpace
    for f, g being Point of
       R_NormSpace_of_BoundedFunctions(X,Y)
       for a be Real holds
  ( ||.f.|| = 0 iff
  f = 0.R_NormSpace_of_BoundedFunctions(X,Y) ) &
  ||.a*f.|| = abs(a) * ||.f.|| &
  ||.f+g.|| <= ||.f.|| + ||.g.||
proof
    let X be non empty set;
    let Y be RealNormSpace;
    let f, g being Point of R_NormSpace_of_BoundedFunctions(X,Y);
    let a be Real;
A1: R_NormSpace_of_BoundedFunctions(X,Y)
  = NORMSTR (# BoundedFunctions(X,Y),
         Zero_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Add_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
           Mult_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
           BoundedFunctionsNorm(X,Y) #) by Def10;
A2: R_VectorSpace_of_BoundedFunctions(X,Y) =
    RLSStruct (# BoundedFunctions(X,Y),
        Zero_(BoundedFunctions(X,Y),
        RealVectSpace(X,Y)),
         Add_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Mult_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)) #) by Def6;
A3:
now assume
   A4: ||.f.|| = 0;
   reconsider g=f as bounded Function of X,the carrier of Y by A1,Def5;
   set z = X --> 0.Y;
   reconsider z as Function of X, the carrier of Y by FUNCOP_1:58;
     now let t be Element of X;
                ||.g.t.|| <= ||.f.||  by Th19;
              then ||.g.t.|| = 0 by A4,NORMSP_1:8;
           hence g.t =0.Y by NORMSP_1:def 2
                 .=z.t by FUNCOP_1:13;
       end;
     then g=z by FUNCT_2:113;
   hence f=0.R_NormSpace_of_BoundedFunctions(X,Y) by Th18;
end;

A5:
now assume
   A6: f = 0.R_NormSpace_of_BoundedFunctions(X,Y);
  thus ||.f.|| = 0
  proof
     set z = X --> 0.Y;
     reconsider z as Function of X, the carrier of Y
    by FUNCOP_1:58;
    reconsider g=f as bounded Function of X,the carrier of Y by A1,Def5;
      A7: z=g by A6,Th18;
      A8:
      now let r be Real such that
        A9:  r in PreNorms(g);
            r in {||.g.t.|| where t is Element of X:not contradiction }
    by A9,Def8;
        then consider t be Element of X such that
          A10:   r=||.g.t.||;
             ||.g.t.|| = ||.0.Y.|| by A7,FUNCOP_1:13
                  .= 0 by NORMSP_1:def 2;
          hence 0 <= r & r <=0 by A10;
    end;
A11:PreNorms(g) is non empty bounded_above by Th13;
 consider r0 be set such that
  A12: r0 in PreNorms(g) by XBOOLE_0:def 1;
  reconsider r0 as Real by A12;
  A13: 0<=r0 by A8,A12;
   (for s be real number st s in PreNorms(g) holds s <= 0)
implies sup PreNorms(g) <= 0 by SEQ_4:62;
     then sup PreNorms(g) = 0 by A8,A11,A12,A13,SEQ_4:def 4;
    then BoundedFunctionsNorm(X,Y).f =0 by Th17;
    hence thesis by A1,NORMSP_1:def 1;
  end;
end;

A14:  ||.a*f.|| = abs(a) * ||.f.||
proof
reconsider f1=f, h1=a*f as bounded Function of X,
the carrier of Y by A1,Def5;
A15:
now let t be Element of  X;
 A16:  ||.h1.t.||= ||.a*f1.t.|| by Th23;
 A17:  ||.a*f1.t.|| =abs(a)*||.f1.t.|| by NORMSP_1:def 2;
 A18:   ||.f1.t.||<= ||.f.|| by Th19;
    0<= abs(a) by ABSVALUE:5;
    hence ||.h1.t.|| <= abs(a)*||.f.|| by A16,A17,A18,AXIOMS:25;
end;
A19:
     now let r be Real such that
        A20:  r in PreNorms(h1);
            r in {||.h1.t.|| where t is Element of  X: not contradiction  }
    by A20,Def8;
        then consider t be Element of X such that
          A21: r=||.h1.t.||;
          thus r <= abs(a)*||.f.|| by A15,A21;
  end;

A22:
(for s be real number st s in PreNorms(h1) holds s <= abs(a)*||.f.|| )
implies sup PreNorms(h1) <=  abs(a)*||.f.|| by SEQ_4:62;
   BoundedFunctionsNorm(X,Y).(a*f)
    = sup PreNorms(h1) by Th17;
then A23:     ||.a*f.|| <= abs(a)*||.f.|| by A1,A19,A22,NORMSP_1:def 1;
   now per cases;
case A24: a <> 0;
 A25: now let t be Element of X;
     h1.t=a*f1.t by Th23;
  then A26: a"*h1.t =( a"* a)*f1.t by RLVECT_1:def 9
             .=1*f1.t by A24,XCMPLX_0:def 7
             .=f1.t by RLVECT_1:def 9;
 A27:  ||.a"*h1.t.|| =abs(a")*||.h1.t.|| by NORMSP_1:def 2;
 A28:   ||.h1.t.||<= ||.a*f.|| by Th19;
 A29: 0<= abs(a") by ABSVALUE:5;
      abs(a") =abs(1*a") .=abs( 1/a) by XCMPLX_0:def 9
                 .=1/abs(a) by ABSVALUE:15
                 .=1*abs(a)" by XCMPLX_0:def 9
                 .=abs(a)";
  hence ||.f1.t.|| <= abs(a)"*||.a*f.|| by A26,A27,A28,A29,AXIOMS:25;
end;
A30:
     now let r be Real such that
        A31:  r in PreNorms(f1);
            r in {||.f1.t.|| where t is Element of  X: not contradiction }
    by A31,Def8;
        then consider t be Element of X such that
          A32:  r=||.f1.t.||;
          thus r <= abs(a)"*||.a*f.|| by A25,A32;
  end;
A33: abs(a) <>0 by A24,ABSVALUE:6;
 A34:
(for s be real number st s in PreNorms(f1) holds s <= abs(a)"*||.a*f.|| )
implies sup PreNorms(f1) <= abs(a)"*||.a*f.|| by SEQ_4:62;
    BoundedFunctionsNorm(X,Y).(f)
    = sup PreNorms(f1) by Th17;
 then A35: ||.f.|| <=abs(a)"*||.a*f.|| by A1,A30,A34,NORMSP_1:def 1;
    0 <= abs(a) by ABSVALUE:5;
 then abs(a)*||.f.|| <=abs(a)*(abs(a)"*||.a*f.||) by A35,AXIOMS:25;
 then abs(a)*||.f.|| <=(abs(a)*abs(a)")*||.a*f.||;
 then abs(a)*||.f.|| <=1*||.a*f.|| by A33,XCMPLX_0:def 7;
 hence abs(a)* ||.f.|| <=||.a*f.||;
 end;
 case A36: a=0;
  reconsider fz=f as VECTOR
     of R_VectorSpace_of_BoundedFunctions(X,Y)
 by A1,A2;
     A37: a*f =Mult_(BoundedFunctions(X,Y),
            RealVectSpace(X,Y)).[a,f] by A1,RLVECT_1:def 4
        .=a*fz by A2,RLVECT_1:def 4
        .=0.R_VectorSpace_of_BoundedFunctions(X,Y)
 by A36,RLVECT_1:23
        .=Zero_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)) by A2,RLVECT_1:def 2
        .=0.R_NormSpace_of_BoundedFunctions(X,Y) by A1,RLVECT_1:def 2;
  thus abs(a)* ||.f.|| =0 * ||.f.|| by A36,ABSVALUE:7
                      .=||.a*f.|| by A37,Th21;
  end;
end;
hence thesis by A23,AXIOMS:21;
end;
     ||.f+g.|| <= ||.f.|| + ||.g.||
proof
reconsider f1=f, g1=g, h1=f+g
as bounded Function of X,the carrier of Y by A1,Def5;
A38:
now let t be Element of X;
A39:  ||.h1.t.||= ||.f1.t+g1.t.|| by Th22;
A40:  ||.f1.t+g1.t.|| <=||.f1.t.||+||.g1.t.|| by NORMSP_1:def 2;
A41:   ||.f1.t.||<= ||.f.|| by Th19;
     ||.g1.t.||<= ||.g.|| by Th19;
   then ||.f1.t.||+||.g1.t.|| <= ||.f.|| + ||.g.|| by A41,REAL_1:55;
    hence ||.h1.t.|| <= ||.f.|| + ||.g.|| by A39,A40,AXIOMS:22;
end;
A42:
     now let r be Real such that
        A43:  r in PreNorms(h1);
            r in {||.h1.t.|| where t is Element of X: not contradiction}
    by A43,Def8;
        then consider t be Element of X such that
          A44:   r=||.h1.t.||;
          thus r <= ||.f.|| + ||.g.|| by A38,A44;
  end;
A45:
(for s be real number st s in PreNorms(h1) holds s <= ||.f.|| + ||.g.||)
implies sup PreNorms(h1) <= ||.f.|| + ||.g.|| by SEQ_4:62;
   BoundedFunctionsNorm(X,Y).(f+g) = sup PreNorms(h1) by Th17;
hence ||.f+g.|| <=||.f.|| + ||.g.|| by A1,A42,A45,NORMSP_1:def 1;
end;
hence thesis by A3,A5,A14;
end;

theorem Th25:
  for X be non empty set for Y be RealNormSpace holds
  R_NormSpace_of_BoundedFunctions(X,Y) is RealNormSpace-like
      proof
       let X be non empty set; let Y be RealNormSpace;
       for x, y being Point of R_NormSpace_of_BoundedFunctions(X,Y)
       for a be Real holds
  ( ||.x.|| = 0 iff
  x = 0.R_NormSpace_of_BoundedFunctions(X,Y) ) &
  ||.a*x.|| = abs(a) * ||.x.|| &
  ||.x+y.|| <= ||.x.|| +  ||.y.|| by Th24;
   hence thesis by NORMSP_1:def 2;
  end;

theorem Th26:
  for X be non empty set for Y be RealNormSpace holds
    R_NormSpace_of_BoundedFunctions(X,Y) is RealNormSpace
     proof
    let X be non empty set; let Y be RealNormSpace;
A1: R_NormSpace_of_BoundedFunctions(X,Y)
  = NORMSTR (# BoundedFunctions(X,Y),
         Zero_(BoundedFunctions(X,Y),
         RealVectSpace(X,Y)),
          Add_(BoundedFunctions(X,Y),
          RealVectSpace(X,Y)),
           Mult_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
           BoundedFunctionsNorm(X,Y) #) by Def10;
       RLSStruct (# BoundedFunctions(X,Y),
            Zero_(BoundedFunctions(X,Y),
            RealVectSpace(X,Y)),
             Add_(BoundedFunctions(X,Y),
             RealVectSpace(X,Y)),
            Mult_(BoundedFunctions(X,Y),
            RealVectSpace(X,Y)) #) is RealLinearSpace;
      hence thesis by A1,Th25,RSSPACE3:4;
     end;

registration
   let X be non empty set; let Y be RealNormSpace;
   cluster R_NormSpace_of_BoundedFunctions(X,Y) ->
               RealNormSpace-like RealLinearSpace-like
               Abelian add-associative right_zeroed right_complementable;
   coherence by Th26;
end;

theorem Th27:
     for X be non empty set for Y be RealNormSpace
     for f,g,h be Point of R_NormSpace_of_BoundedFunctions(X,Y)
       for f',g',h' be bounded Function of X,the carrier of Y
        st f'=f & g'=g & h'=h holds
      (h = f-g iff
          (for x be Element of X holds (h'.x = f'.x - g'.x)) )
proof
     let X be non empty set; let Y be RealNormSpace;
     let f,g,h be Point of R_NormSpace_of_BoundedFunctions(X,Y);
     let f',g',h' be bounded Function of X,the carrier of Y such that
       A1: f'=f and
       A2: g'=g and
       A3: h'=h;
A4: now assume h=f-g;
     then h+g=f-(g-g) by RLVECT_1:43;
     then h+g=f-0.R_NormSpace_of_BoundedFunctions(X,Y) by RLVECT_1:28;
     then A5: h+g=f by RLVECT_1:26;
        now let x be Element of  X;
          f'.x=h'.x +  g'.x by A1,A2,A3,A5,Th22;
       then f'.x-g'.x=h'.x + (g'.x-g'.x) by RLVECT_1:42;
       then f'.x-g'.x=h'.x + 0.Y by RLVECT_1:28;
       hence f'.x-g'.x=h'.x by RLVECT_1:10;
     end;
     hence for x be Element of X holds (h'.x = f'.x - g'.x);
end;
    now assume
     A6: for x be Element of X holds (h'.x = f'.x - g'.x);
        now let x be Element of  X;
          h'.x = f'.x - g'.x by A6;
       then h'.x + g'.x= f'.x - (g'.x- g'.x) by RLVECT_1:43;
       then h'.x + g'.x= f'.x - 0.Y by RLVECT_1:28;
       hence h'.x + g'.x= f'.x by RLVECT_1:26;
     end;
     then f=h+g by A1,A2,A3,Th22;
     then f-g=h+(g-g) by RLVECT_1:42;
     then f-g=h+0.R_NormSpace_of_BoundedFunctions(X,Y) by RLVECT_1:28;
     hence f-g=h by RLVECT_1:10;
end;
hence thesis by A4;
end;

Lm8:
for e be Real, seq be Real_Sequence st ( seq is convergent &
 ex k be Nat st (for i be Nat st k <= i holds seq.i <=e ) ) holds
  lim seq <=e
proof
   let e be Real;
   let seq be Real_Sequence such that
A1:seq is convergent and
A2:ex k be Nat st (for i be Nat st k <= i holds seq.i <=e );
   deffunc F(set) = e;
   reconsider cseq = NAT --> e as Real_Sequence by FUNCOP_1:57;
A3:for n be Nat holds cseq.n=e by FUNCOP_1:13;
   reconsider e1=e as real number;
A5:cseq is convergent by SEQ_4:39;
   consider k be Nat such that
A6:for i be Nat st k <= i holds seq.i <=e by A2;
A7:(seq ^\k) is convergent by A1,SEQ_4:33;
A8:lim (seq ^\k)=lim seq by A1,SEQ_4:33;
A9:now let i be Nat;
A10: (seq ^\k).i = seq.(k+i) by SEQM_3:def 9;
        k <= k+i by NAT_1:29;
     then seq.(k+i) <=e by A6;
     hence (seq ^\k) .i <= cseq.i by A3,A10;
  end;
     lim cseq = cseq.0 by SEQ_4:41 .= e by A3;
  hence thesis by A5,A7,A8,A9,SEQ_2:32;
end;

theorem Th28:
    for X be non empty set for Y be RealNormSpace st Y is complete
      for seq be sequence of R_NormSpace_of_BoundedFunctions(X,Y)
               st seq is Cauchy_sequence_by_Norm
 holds seq is convergent
proof
    let X be non empty set; let Y be RealNormSpace such that
 A1:   Y is complete;
    let vseq be sequence of R_NormSpace_of_BoundedFunctions(X,Y)
     such that
A2: vseq is Cauchy_sequence_by_Norm;
A3:  ||.vseq.|| is convergent
proof
    now let e1 be real number such that
   A4: e1 >0;
   reconsider e =e1 as Real by XREAL_0:def 1;
          consider k be Nat such that
   A5:      for n, m be Nat st n >= k & m >= k holds
                ||.(vseq.n) - (vseq.m).|| < e by A2,A4,RSSPACE3:10;
A6: now
      let m be Nat such that
      A7:   m >= k;
      A8:  abs( ||.vseq.m.||- ||.vseq.k.|| )
         <= ||.(vseq.m) - (vseq.k).|| by NORMSP_1:13;
      A9:   ||.vseq.k.||= ||.vseq.||.k by NORMSP_1:def 10;
      A10:   ||.vseq.m.||= ||.vseq.||.m by NORMSP_1:def 10;
            ||.(vseq.m) - (vseq.k).|| <e by A5,A7;
      hence abs( ||.vseq.||.m - ||.vseq.||.k ) <e1
      by A8,A9,A10,AXIOMS:22;
   end;
   take k;
   thus for m be Nat st m >= k holds
             abs(||.vseq.||.m - ||.vseq.||.k ) < e1 by A6;
 end;
 hence ||.vseq.|| is convergent by SEQ_4:58;
end;
    defpred P[set, set] means ex xseq be sequence of Y st
    (for n be Nat holds xseq.n=modetrans((vseq.n),X,Y).$1)
    & xseq is convergent & $2= lim xseq;
A11: R_NormSpace_of_BoundedFunctions(X,Y)
    = NORMSTR (# BoundedFunctions(X,Y),
           Zero_(BoundedFunctions(X,Y),
           RealVectSpace(X,Y)),
            Add_(BoundedFunctions(X,Y),
            RealVectSpace(X,Y)),
             Mult_(BoundedFunctions(X,Y),
             RealVectSpace(X,Y)),
             BoundedFunctionsNorm(X,Y) #) by Def10;
A12: for x be Element of X ex y be Element of Y st  P[x,y]
   proof
       let x be Element of X;
       deffunc F(Nat) = modetrans((vseq.$1),X,Y).x;
       consider xseq be sequence of Y such that
A13:  for n be Nat holds xseq.n = F(n) from FUNCT_2:sch 4;
A14: for m,k be Nat holds
             ||.xseq.m-xseq.k.|| <= ||.vseq.m - vseq.k.||
      proof
      let m,k be Nat;
            vseq.m is bounded Function of X,the carrier of Y
         by A11,Def5;
then A15:         modetrans((vseq.m),X,Y)=vseq.m by Th16;
             vseq.k is bounded Function of X,the carrier of Y
          by A11,Def5;
then A16:         modetrans((vseq.k),X,Y)=vseq.k by Th16;
         reconsider h1=vseq.m-vseq.k as bounded Function of X,
           the carrier of Y by A11,Def5;
           A17:  xseq.m =modetrans((vseq.m),X,Y).x by A13;
               xseq.k =modetrans((vseq.k),X,Y).x by A13;
           then xseq.m - xseq.k = h1.x by A15,A16,A17,Th27;
          hence thesis by Th19;
         end;
A18: xseq is convergent
     proof
           now
            let e be Real such that
A19:          e > 0;
               thus ex k be Nat st
                for n, m be Nat st n >= k & m >= k
                  holds ||.xseq.n -xseq.m.|| < e
                proof
                   consider k be Nat such that
A20:                for n, m be Nat st n >= k & m >= k holds
                     ||.(vseq.n) - (vseq.m).|| < e
                        by A2,A19,RSSPACE3:10;
                   take k;
                   thus for n, m be Nat st ( n >= k & m >= k )
                     holds ||.xseq.n-xseq.m.|| < e
                   proof
                     let n,m be Nat such that
A21:                  n >=k & m >= k;
A22:                 ||.xseq.n-xseq.m.||  <=
                     ||.(vseq.n) - (vseq.m).||  by A14;
                        ||.(vseq.n) - (vseq.m).|| < e by A20,A21;
                     hence ||.xseq.n-xseq.m.|| < e by A22,AXIOMS:22;
                 end;
               end;
            end;
            then xseq is Cauchy_sequence_by_Norm by RSSPACE3:10;
         hence thesis by A1,LOPBAN_1:def 16;
         end;
  take y = lim xseq;
  thus thesis by A13,A18;
  end;
   consider f be Function of  X,the carrier of Y such that
A23:for x be Element of X holds P[x,f.x] from FUNCT_2:sch 3(A12);
   reconsider tseq=f as Function of X,the carrier of Y;

A24: tseq is bounded
proof
A25: now
       let x be Element of X;
       consider xseq be sequence of Y such that
  A26: (for n be Nat holds xseq.n=modetrans((vseq.n),X,Y).x)
       & xseq is convergent
       & tseq.x = lim xseq by A23;
A27: for m be Nat holds ||.xseq.m.|| <= ||.vseq.m.||
      proof
      let m be Nat;
       vseq.m is bounded Function of X,the carrier of Y
by A11,Def5;
then A28:         modetrans((vseq.m),X,Y)=vseq.m by Th16;
         reconsider h1=vseq.m as bounded Function of X,the carrier of Y by A11,
Def5;
             xseq.m =modetrans((vseq.m),X,Y).x by A26;
          hence thesis by A28,Th19;
         end;
  A29:     ||.xseq.|| is convergent by A26,LOPBAN_1:47;
  A30:     ||.tseq.x.|| = lim ||.xseq.|| by A26,LOPBAN_1:47;
             for n be Nat holds ||.xseq.||.n  <=(||.vseq.||).n
            proof
            let n be Nat;
             A31: ||.xseq.||.n = ||.(xseq.n).|| by NORMSP_1:def 10;
                ||.(xseq.n).|| <= ||.vseq.n.|| by A27;
             hence thesis by A31,NORMSP_1:def 10;
            end;
   hence ||.tseq.x.|| <= lim (||.vseq.|| ) by A3,A29,A30,SEQ_2:32;
    end;
   take K= lim (||.vseq.|| );
       K >=0
    proof
        now let n be Nat;
            ||.vseq.n.|| >=0 by NORMSP_1:8;
         hence ||.vseq.||.n >=0 by NORMSP_1:def 10;
      end;
     hence thesis by A3,SEQ_2:31;
    end;
    hence thesis by A25;
end;

A32:
   for e be Real st
    e > 0 ex k be Nat st for n be Nat st
     n >= k holds
        for x be Element of X holds
            ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e
   proof
     let e be Real such that
      A33:e > 0;
      consider k be Nat such that
      A34:  for n, m be Nat st ( n >= k & m >= k ) holds
             ||.(vseq.n) - (vseq.m).|| < e by A2,A33,RSSPACE3:10;
      A35: now let n be Nat such that
         A36: n >= k;
              now let x be Element of X;
              consider xseq be sequence of Y such that
              A37:
              (for n be Nat holds  xseq.n=modetrans((vseq.n),X,Y).x)
                & xseq is convergent
                & tseq.x = lim xseq by A23;
               A38:
                for m,k be Nat holds
                 ||.xseq.m-xseq.k.|| <= ||.vseq.m - vseq.k.||
                proof
                let m,k be Nat;
                   vseq.m is bounded Function of X,the carrier of Y
                by A11,Def5;
                 then A39:           modetrans((vseq.m),X,Y)=vseq.m by Th16;
                   vseq.k is bounded Function of X,the carrier of Y
                by A11,Def5;
                then A40:            modetrans((vseq.k),X,Y)=vseq.k by Th16;
                reconsider h1=vseq.m-vseq.k
              as bounded Function of X,the carrier of Y by A11,Def5;
                A41:  xseq.m =modetrans((vseq.m),X,Y).x by A37;
                    xseq.k =modetrans((vseq.k),X,Y).x by A37;
                 then xseq.m - xseq.k =h1.x by A39,A40,A41,Th27;
                 hence thesis by Th19;
                 end;
           A42:  for m be Nat st m >=k holds
                    ||.xseq.n-xseq.m.|| <= e
                    proof
                      let m be Nat such that
                      A43:  m >=k;
                      A44:  ||.xseq.n-xseq.m.||
                      <= ||.vseq.n - vseq.m.|| by A38;
                         ||.vseq.n - vseq.m.|| <e by A34,A36,A43;
                      hence thesis by A44,AXIOMS:22;
                    end;
                     ||.xseq.n-tseq.x.|| <= e
                  proof
                    deffunc F(Nat) = ||.xseq.$1 - xseq.n.||;
                    consider rseq be Real_Sequence such that
  A45:              for m be Nat holds rseq.m = F(m) from SEQ_1:sch 1;
  A46:            rseq is convergent & lim rseq = ||.tseq.x-xseq.n.||
                    proof
                        A47: xseq - xseq.n is convergent by A37,NORMSP_1:36;
                        A48: lim (xseq-xseq.n)= tseq.x - xseq.n
                    by A37,NORMSP_1:44;
                             rseq = ||.xseq - xseq.n.||
                         proof
                             now let x be set such that
                           A49:  x in NAT;
                           reconsider k=x as Nat by A49;
                           thus rseq.x = ||.xseq.k - xseq.n.|| by A45
                             .= ||.(xseq - xseq.n).k.|| by NORMSP_1:def 7
                             .= ||.(xseq - xseq.n).||.x by NORMSP_1:def 10;
                         end;
                         hence thesis by SEQ_1:8;
                         end;
                         hence thesis by A47,A48,LOPBAN_1:24;
                     end;
               for m be Nat st m >= k holds rseq.m <= e
                    proof
                      let m be Nat such that
                      A50:  m >=k;
                         rseq.m = ||.xseq.m-xseq.n.|| by A45
                                     .= ||.xseq.n-xseq.m.|| by NORMSP_1:11;
                      hence thesis by A42,A50;
                    end;
                   then lim rseq <= e by A46,Lm8;
                   hence thesis by A46,NORMSP_1:11;
                  end;
                 hence ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e
                 by A37;
           end;
         hence for x be Element of X holds
          ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e;
     end;
     take k;
     thus thesis by A35;
   end;
  reconsider tseq as bounded Function of X,the carrier of Y by A24;
  reconsider tv=tseq as Point of
   R_NormSpace_of_BoundedFunctions(X,Y) by A11,Def5;

A51:
   for e be Real st
    e > 0 ex k be Nat st for n be Nat st
     n >= k holds ||.vseq.n - tv.|| <= e
   proof
   let e be Real such that
      A52: e > 0;
      consider k be Nat such that
      A53: for n be Nat st n >= k holds
          for x be Element of X holds
  ||.modetrans((vseq.n),X,Y).x - tseq.x.|| <= e by A32,A52;
         now let n be Nat such that
        A54: n >= k;
        set f1=modetrans((vseq.n),X,Y);
        set g1=tseq;
        reconsider h1=vseq.n-tv
        as bounded Function of X,the carrier of Y by A11,Def5;
       A55: now
       let t be Element of  X;
            vseq.n is bounded Function of X,the carrier of Y by A11,Def5;
         then modetrans((vseq.n),X,Y)=vseq.n by Th16;
       then ||.h1.t.||= ||.f1.t-g1.t.|| by Th27;
       hence ||.h1.t.|| <=e by A53,A54;
      end;
     A56:
       now let r be Real such that
        A57:  r in PreNorms(h1);
            r in {||.h1.t.|| where t is Element of X:not contradiction }
    by A57,Def8;
        then consider t be Element of X such that
          A58: r=||.h1.t.||;
          thus r <=e by A55,A58;
      end;
     A59:
     (for s be real number st s in PreNorms(h1) holds s <= e)
      implies sup PreNorms(h1) <=e by SEQ_4:62;
        BoundedFunctionsNorm(X,Y).(vseq.n-tv)
      = sup PreNorms(h1) by Th17;
     hence ||.vseq.n-tv.|| <=e by A11,A56,A59,NORMSP_1:def 1;
      end;
     hence thesis;
end;
     for e be Real st
    e > 0 ex m be Nat st for n be Nat st
     n >= m holds ||.(vseq.n) - tv.|| < e
   proof
    let e be Real such that
      A60: e > 0;
      A61: e/2 > 0 by A60,SEQ_2:3;
      A62: e/2<e by A60,SEQ_2:4;
      consider m be Nat such that
          A63: for n be Nat st
          n >= m holds ||.(vseq.n) - tv.|| <= e/2 by A51,A61;
             now let n be Nat such that
        A64: n >= m;
               ||.(vseq.n) - tv.|| <= e/2 by A63,A64;
            hence ||.(vseq.n) - tv.|| < e by A62,AXIOMS:22;
          end;
    hence thesis;
   end;
   hence thesis by NORMSP_1:def 9;
end;

theorem Th29:
  for X be non empty set
  for Y be RealBanachSpace holds
     R_NormSpace_of_BoundedFunctions(X,Y) is RealBanachSpace
proof
    let X be non empty set;
    let Y be RealBanachSpace;
       for seq be sequence of R_NormSpace_of_BoundedFunctions(X,Y)
      st seq is Cauchy_sequence_by_Norm holds seq is convergent by Th28;
    hence thesis by LOPBAN_1:def 16;
end;

registration
  let X be non empty set;
  let Y be RealBanachSpace;
  cluster R_NormSpace_of_BoundedFunctions (X,Y) -> complete;
  coherence by Th29;
end;
