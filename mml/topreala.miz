:: Some Properties of Rectangles on the Plane
::  by Artur Korni{\l}owicz and Yasunari Shidama
::
:: Received October 18, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, EUCLID, RELAT_1, ARYTM, ZF_LANG, SUBSET_1, CARD_3,
      TOPS_1, INT_1, FUNCT_1, ORDINAL2, ARYTM_1, MCART_1, T_0TOPSP, TOPS_2,
      ARYTM_3, RCOMP_1, ASYMPT_0, FUNCOP_1, TOPMETR, SGRAPH1, BOOLE, TOPREALA,
      FCONT_1, FINSEQ_1, PARTFUN1, TMAP_1, CONNSP_2, JGRAPH_6, NATTRA_1,
      TDLAT_3, PARTFUN2, FINSEQ_4, MONOID_0;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, RELAT_1, FUNCT_1,
      RELSET_1, PARTFUN1, FUNCT_2, SEQ_1, ORDINAL2, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, NAT_1, FUNCOP_1, FINSEQ_4, PARTFUN2, INT_1, CARD_3, FINSEQ_1,
      FUNCT_4, MONOID_0, RCOMP_1, RCOMP_2, FCONT_1, STRUCT_0, PRE_TOPC, TOPS_1,
      BORSUK_1, TSEP_1, TOPS_2, TDLAT_3, CONNSP_2, T_0TOPSP, TMAP_1, TOPMETR,
      EUCLID, JGRAPH_6, TOPALG_2;
 constructors GRCAT_1, PARTFUN2, REAL_1, TOPS_2, TOPS_1, BORSUK_3, TOPALG_2,
      COMPTS_1, FCONT_1, REALSET2, CARD_3, TMAP_1, RCOMP_2, FINSEQ_4, JGRAPH_6,
      INTEGRA2, PSCOMP_1, TDLAT_3, MONOID_0, YELLOW_8;
 registrations STRUCT_0, RELSET_1, XREAL_0, REAL_1, BORSUK_1, PRE_TOPC, INT_1,
      TOPALG_2, TOPS_1, YELLOW13, TOPREAL6, BORSUK_6, FINSEQ_1, FUNCT_4,
      TOPGRP_1, TSEP_1, PSCOMP_1, YELLOW12, MONOID_0, NAT_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM, REAL;
 definitions TARSKI, XBOOLE_0, FUNCT_2, PRE_TOPC, T_0TOPSP, TMAP_1, STRUCT_0,
      RCOMP_1, FCONT_1, PARTFUN2, MONOID_0;
 theorems PRE_TOPC, XREAL_0, FUNCT_2, BORSUK_1, INT_1, EUCLID, TOPS_2,
      BORSUK_3, FUNCT_1, XCMPLX_1, TOPMETR, RELAT_1, FUNCOP_1, RCOMP_2,
      TOPREAL3, TOPREAL6, JORDAN16, BORSUK_6, TARSKI, ZFMISC_1, FUNCT_4,
      TSEP_1, HILBERT3, FINSEQ_1, TMAP_1, XBOOLE_1, JORDAN6, REAL_1, TOPS_3,
      FCONT_1, JORDAN1, CONNSP_2, FRECHET, XBOOLE_0, FINSEQ_3, ORDINAL2, NAT_1,
      RCOMP_1, TOPREAL5, AXIOMS, TEX_2, DOMAIN_1, TOPS_1, TOPGRP_1, T_0TOPSP,
      TREAL_1, CARD_3, SUBSET_1, MSAFREE3, JGRAPH_6, STRUCT_0, EXTENS_1,
      TDLAT_3, ENUMSET1, FINSEQ_4, TOPALG_3;

begin :: Real numbers

set R = the carrier of R^1;
Lm1: the carrier of [:R^1,R^1:] = [:R,R:] by BORSUK_1:def 5;
reconsider p1 = 1 as positive (real number);

reserve
  n for Nat,
  i for Integer,
  a, b, r, s for real number;

theorem
  frac(r+i) = frac r
proof
  thus frac(r+i) = r+i-[\r+i/] by INT_1:def 6
    .= r+i-([\r/]+i) by INT_1:51
    .= r-[\r/]+i-i
    .= frac r by INT_1:def 6;
end;

theorem Th2:
  r <= a & a < [\r/]+1 implies [\a/] = [\r/]
proof
  assume
A1: r <= a;
  [\r/] <= r by INT_1:def 4;
  then
A2: [\r/] <= a by A1,AXIOMS:22;
  assume a < [\r/]+1;
  then a-1 < [\r/]+1-1 by REAL_1:54;
  hence [\a/] = [\r/] by A2,INT_1:def 4;
end;

theorem Th3:
  r <= a & a < [\r/]+1 implies frac r <= frac a
proof
  assume
A1: r <= a;
  assume a < [\r/]+1;
  then
A2: [\a/] = [\r/] by A1,Th2;
  frac a = a-[\a/] & frac r = r-[\r/] by INT_1:def 6;
  hence frac r <= frac a by A1,A2,REAL_1:49;
end;

theorem
  r < a & a < [\r/]+1 implies frac r < frac a
proof
  assume
A1: r < a;
  assume a < [\r/]+1;
  then
A2: [\a/] = [\r/] by A1,Th2;
  frac a = a-[\a/] & frac r = r-[\r/] by INT_1:def 6;
  hence frac r < frac a by A1,A2,REAL_1:54;
end;

theorem Th5:
  a >= [\r/]+1 & a <= r+1 implies [\a/] = [\r/]+1
proof
  assume
A1: a >= [\r/]+1;
  assume a <= r+1;
  then
A2: a-1 <= r+1-1 by REAL_1:49;
  r < [\r/]+1 by INT_1:52;
  then a-1 < [\r/]+1 by A2,AXIOMS:22;
  hence [\a/] = [\r/]+1 by A1,INT_1:def 4;
end;

theorem Th6:
  a >= [\r/]+1 & a < r+1 implies frac a < frac r
proof
  assume
A1: a >= [\r/]+1;
  assume
A2: a < r+1;
  then
A3: [\a/] = [\r/]+1 by A1,Th5;
A4: frac a = a-[\a/] & frac r = r-[\r/] by INT_1:def 6;
  a-1 < r by A2,REAL_1:84;
  then a-1-[\r/] < r-[\r/] by REAL_1:92;
  hence frac a < frac r by A3,A4;
end;

theorem
  r <= a & a < r+1 & r <= b & b < r+1 & frac a = frac b implies a = b
proof
  assume that
A1: r <= a and
A2: a < r+1 and
A3: r <= b and
A4: b < r+1 and
A5: frac a = frac b;
A6: frac a = a-[\a/] by INT_1:def 6;
A7: frac b = b-[\b/] by INT_1:def 6;
  [\r/] <= r by INT_1:def 4;
  then
A8: [\r/] <= a & [\r/] <= b by A1,A3,AXIOMS:22;
  per cases;
  suppose a < [\r/]+1 & b >= [\r/]+1;
  then frac a >= frac r & frac b < frac r by A1,A4,Th3,Th6;
  hence thesis by A5;
  end;
  suppose a >= [\r/]+1 & b < [\r/]+1;
  then frac a < frac r & frac b >= frac r by A2,A3,Th3,Th6;
  hence thesis by A5;
  end;
  suppose a < [\r/]+1 & b < [\r/]+1;
  then a-1 < [\r/]+1-1 & b-1 < [\r/]+1-1 by REAL_1:54;
  then
A9: [\a/] = [\r/] & [\b/] = [\r/] by A8,INT_1:def 4;
  thus a = a-[\a/]+[\a/]
        .= b by A5,A6,A7,A9;
  end;
  suppose a >= [\r/]+1 & b >= [\r/]+1;
  then
A10: [\a/] = [\r/]+1 & [\b/] = [\r/]+1 by A2,A4,Th5;
  thus a = a-[\a/]+[\a/]
        .= b by A5,A6,A7,A10;
  end;
end;

begin :: Subsets of REAL

registration
  let r be real number,
      s be positive (real number);
  cluster ].r,r+s.[ -> non empty;
coherence
proof
  r+0 < r+s by REAL_1:53;
  then r < (r+(r+s))/2 & (r+(r+s))/2 < r+s by TOPREAL3:3;
  hence thesis by JORDAN6:45;
end;
  cluster [.r,r+s.[ -> non empty;
coherence
proof
  r+0 < r+s by REAL_1:53;
  hence thesis by RCOMP_2:3;
end;
  cluster ].r,r+s.] -> non empty;
coherence
proof
  r+0 < r+s by REAL_1:53;
  hence thesis by RCOMP_2:4;
end;
  cluster [.r,r+s.] -> non empty;
coherence
proof
  r+0 < r+s by REAL_1:53;
  hence thesis by TOPREAL5:1;
end;
  cluster ].r-s,r.[ -> non empty;
coherence
proof
  r-s < r-0 by REAL_1:92;
  then r-s < (r-s+r)/2 & (r-s+r)/2 < r by TOPREAL3:3;
  hence thesis by JORDAN6:45;
end;
  cluster [.r-s,r.[ -> non empty;
coherence
proof
  r-s < r-0 by REAL_1:92;
  hence thesis by RCOMP_2:3;
end;
  cluster ].r-s,r.] -> non empty;
coherence
proof
  r-s < r-0 by REAL_1:92;
  hence thesis by RCOMP_2:4;
end;
  cluster [.r-s,r.] -> non empty;
coherence
proof
  r-s < r-0 by REAL_1:92;
  hence thesis by TOPREAL5:1;
end;
end;

registration
  let r be non positive (real number),
      s be positive (real number);
  cluster ].r,s.[ -> non empty;
coherence
proof
  r < (r+s)/2 & (r+s)/2 < s by TOPREAL3:3;
  hence thesis by JORDAN6:45;
end;
  cluster [.r,s.[ -> non empty;
coherence by RCOMP_2:3;
  cluster ].r,s.] -> non empty;
coherence by RCOMP_2:4;
  cluster [.r,s.] -> non empty;
coherence by TOPREAL5:1;
end;

registration
  let r be negative (real number),
      s be non negative (real number);
  cluster ].r,s.[ -> non empty;
coherence
proof
  r < (r+s)/2 & (r+s)/2 < s by TOPREAL3:3;
  hence thesis by JORDAN6:45;
end;
  cluster [.r,s.[ -> non empty;
coherence by RCOMP_2:3;
  cluster ].r,s.] -> non empty;
coherence by RCOMP_2:4;
  cluster [.r,s.] -> non empty;
coherence by TOPREAL5:1;
end;

:: r <= a & b <= s implies [.a,b.] c= [.r,s.];  TREAL_1:1
:: r <= a & b <= s implies ].a,b.[ c= ].r,s.[;  JGRAPH_3:9

theorem
  r <= a & b < s implies [.a,b.] c= [.r,s.[
proof
  assume that
A1: r <= a and
A2: b < s;
  let x be set such that
A3: x in [.a,b.];
  reconsider x as Real by A3;
  a <= x & x <= b by A3,TOPREAL5:1;
  then r <= x & x < s by A1,A2,AXIOMS:22;
  hence thesis by RCOMP_2:3;
end;

theorem
  r < a & b <= s implies [.a,b.] c= ].r,s.]
proof
  assume that
A1: r < a and
A2: b <= s;
  let x be set such that
A3: x in [.a,b.];
  reconsider x as Real by A3;
  a <= x & x <= b by A3,TOPREAL5:1;
  then r < x & x <= s by A1,A2,AXIOMS:22;
  hence thesis by RCOMP_2:4;
end;

theorem
  r < a & b < s implies [.a,b.] c= ].r,s.[
proof
  assume that
A1: r < a and
A2: b < s;
  let x be set such that
A3: x in [.a,b.];
  reconsider x as Real by A3;
  a <= x & x <= b by A3,TOPREAL5:1;
  then r < x & x < s by A1,A2,AXIOMS:22;
  hence thesis by JORDAN6:45;
end;

theorem Th11:
  r <= a & b <= s implies [.a,b.[ c= [.r,s.]
proof
  assume that
A1: r <= a and
A2: b <= s;
  let x be set such that
A3: x in [.a,b.[;
  reconsider x as Real by A3;
  a <= x & x < b by A3,RCOMP_2:3;
  then r <= x & x <= s by A1,A2,AXIOMS:22;
  hence thesis by TOPREAL5:1;
end;

theorem
  r <= a & b <= s implies [.a,b.[ c= [.r,s.[
proof
  assume that
A1: r <= a and
A2: b <= s;
  let x be set such that
A3: x in [.a,b.[;
  reconsider x as Real by A3;
  a <= x & x < b by A3,RCOMP_2:3;
  then r <= x & x < s by A1,A2,AXIOMS:22;
  hence thesis by RCOMP_2:3;
end;

theorem
  r < a & b <= s implies [.a,b.[ c= ].r,s.]
proof
  assume that
A1: r < a and
A2: b <= s;
  let x be set such that
A3: x in [.a,b.[;
  reconsider x as Real by A3;
  a <= x & x < b by A3,RCOMP_2:3;
  then r < x & x <= s by A1,A2,AXIOMS:22;
  hence thesis by RCOMP_2:4;
end;

theorem Th14:
  r < a & b <= s implies [.a,b.[ c= ].r,s.[
proof
  assume that
A1: r < a and
A2: b <= s;
  let x be set such that
A3: x in [.a,b.[;
  reconsider x as Real by A3;
  a <= x & x < b by A3,RCOMP_2:3;
  then r < x & x < s by A1,A2,AXIOMS:22;
  hence thesis by JORDAN6:45;
end;

theorem Th15:
  r <= a & b <= s implies ].a,b.] c= [.r,s.]
proof
  assume that
A1: r <= a and
A2: b <= s;
  let x be set such that
A3: x in ].a,b.];
  reconsider x as Real by A3;
  a < x & x <= b by A3,RCOMP_2:4;
  then r <= x & x <= s by A1,A2,AXIOMS:22;
  hence thesis by TOPREAL5:1;
end;

theorem
  r <= a & b < s implies ].a,b.] c= [.r,s.[
proof
  assume that
A1: r <= a and
A2: b < s;
  let x be set such that
A3: x in ].a,b.];
  reconsider x as Real by A3;
  a < x & x <= b by A3,RCOMP_2:4;
  then r <= x & x < s by A1,A2,AXIOMS:22;
  hence thesis by RCOMP_2:3;
end;

theorem
  r <= a & b <= s implies ].a,b.] c= ].r,s.]
proof
  assume that
A1: r <= a and
A2: b <= s;
  let x be set such that
A3: x in ].a,b.];
  reconsider x as Real by A3;
  a < x & x <= b by A3,RCOMP_2:4;
  then r < x & x <= s by A1,A2,AXIOMS:22;
  hence thesis by RCOMP_2:4;
end;

theorem Th18:
  r <= a & b < s implies ].a,b.] c= ].r,s.[
proof
  assume that
A1: r <= a and
A2: b < s;
  let x be set such that
A3: x in ].a,b.];
  reconsider x as Real by A3;
  a < x & x <= b by A3,RCOMP_2:4;
  then r < x & x < s by A1,A2,AXIOMS:22;
  hence thesis by JORDAN6:45;
end;

theorem Th19:
  r <= a & b <= s implies ].a,b.[ c= [.r,s.]
proof
  assume that
A1: r <= a and
A2: b <= s;
  let c be set;
  assume
A3: c in ].a,b.[;
  then reconsider c as Real;
  a < c & c < b by A3,JORDAN6:45;
  then r <= c & c <= s by A1,A2,AXIOMS:22;
  hence thesis by TOPREAL5:1;
end;

theorem
  r <= a & b <= s implies ].a,b.[ c= [.r,s.[
proof
  assume that
A1: r <= a and
A2: b <= s;
  let c be set;
  assume
A3: c in ].a,b.[;
  then reconsider c as Real;
  a < c & c < b by A3,JORDAN6:45;
  then r <= c & c < s by A1,A2,AXIOMS:22;
  hence thesis by RCOMP_2:3;
end;

theorem
  r <= a & b <= s implies ].a,b.[ c= ].r,s.]
proof
  assume that
A1: r <= a and
A2: b <= s;
  let c be set;
  assume
A3: c in ].a,b.[;
  then reconsider c as Real;
  a < c & c < b by A3,JORDAN6:45;
  then r < c & c <= s by A1,A2,AXIOMS:22;
  hence thesis by RCOMP_2:4;
end;

begin :: Functions

theorem
  for f being Function, x, X being set st
    x in dom f & f.x in f.:X & f is one-to-one
  holds x in X
proof
  let f be Function,
      x, X be set;
  assume
A1: x in dom f;
  assume f.x in f.:X;
  then
A2: ex a being set st a in dom f & a in X & f.x = f.a by FUNCT_1:def 12;
  assume f is one-to-one;
  hence x in X by A1,A2,FUNCT_1:def 8;
end;

theorem
  for f being FinSequence, i being natural number st i+1 in dom f holds
  i in dom f or i = 0
proof
  let f be FinSequence;
  let i be natural number;
  assume
A1: i+1 in dom f;
  1 <= i+1 by A1,FINSEQ_3:27;
  then
A2: 1 < i+1 or 1+0 = i+1 by REAL_1:def 5;
  per cases by A2,NAT_1:38,XCMPLX_1:2;
  suppose i = 0;
  hence thesis;
  end;
  suppose
A3: 1 <= i;
  i+1 <= len f by A1,FINSEQ_3:27;
  then i <= len f by NAT_1:38;
  hence thesis by A3,FINSEQ_3:27;
  end;
end;

theorem Th24:
  for x, y, X, Y being set, f being Function
    st x <> y & f in product ((x,y) --> (X,Y)) holds
  f.x in X & f.y in Y
proof
  let x, y, X, Y be set,
      f be Function such that
A1: x <> y and
A2: f in product ((x,y) --> (X,Y));
  set g = (x,y) --> (X,Y);
  dom g = {x,y} by FUNCT_4:65;
  then x in dom g & y in dom g by TARSKI:def 2;
  then f.x in g.x & f.y in g.y by A2,CARD_3:18;
  hence f.x in X & f.y in Y by A1,FUNCT_4:66;
end;

theorem Th25:
  for a, b being set holds <*a,b*> = (1,2) --> (a,b)
proof
  let a, b be set;
  set f = (1,2) --> (a,b);
  set g = <*a,b*>;
A1: dom g = {1,2} by FINSEQ_1:4,FINSEQ_3:29;
A2: dom f = {1,2} by FUNCT_4:65;
  now
    let x be set;
    assume
A3:   x in dom f;
    per cases by A2,A3,TARSKI:def 2;
    suppose
A4:   x = 1;
    hence f.x = a by FUNCT_4:66
       .= g.x by A4,FINSEQ_1:61;
    end;
    suppose
A5:   x = 2;
    hence f.x = b by FUNCT_4:66
       .= g.x by A5,FINSEQ_1:61;
    end;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

begin :: General topology

registration
  cluster constituted-FinSeqs non empty strict TopSpace;
existence
proof
  take TOP-REAL 1;
  thus thesis;
end;
end;

registration
  let T be constituted-FinSeqs TopSpace;
  cluster -> constituted-FinSeqs SubSpace of T;
coherence
proof
  let X be SubSpace of T;
  let p be Element of X;
A1: the carrier of X is Subset of the carrier of T by TSEP_1:1;
  per cases;
  suppose the carrier of X is non empty;
  then p in the carrier of X;
  then reconsider p as Point of T by A1;
  p is FinSequence;
  hence thesis;
  end;
  suppose the carrier of X is empty;
  hence thesis by SUBSET_1:def 2;
  end;
end;
end;

theorem Th26:
  for T being non empty TopSpace, Z being non empty SubSpace of T,
      t being Point of T, z being Point of Z,
      N being open a_neighborhood of t,
      M being Subset of Z st t = z & M = N /\ [#]Z
   holds M is open a_neighborhood of z
proof
  let T be non empty TopSpace,
      Z be non empty SubSpace of T,
      t be Point of T,
      z be Point of Z,
      N be open a_neighborhood of t,
      M be Subset of Z such that
A1: t = z and
A2: M = N /\ [#]Z;
A3: [#]Z = the carrier of Z by PRE_TOPC:12;
A4: t in Int N by CONNSP_2:def 1;
A5: Int N c= N by TOPS_1:44;
  M is open by A2,TOPS_2:32;
  then Int M = M by TOPS_1:55;
  then z in Int M by A1,A2,A3,A4,A5,XBOOLE_0:def 3;
  hence thesis by A2,CONNSP_2:def 1,TOPS_2:32;
end;

registration
  cluster empty -> discrete anti-discrete TopSpace;
coherence
proof
  let T be TopSpace;
  assume T is empty;
  then the carrier of T = {} by STRUCT_0:def 1;
  then bool the carrier of T = {{}, the carrier of T}
    by ENUMSET1:69,ZFMISC_1:1;
  hence thesis by TDLAT_3:16;
end;
end;

registration
  let X be discrete TopSpace, Y be TopSpace;
  cluster -> continuous map of X,Y;
coherence by TEX_2:68;
end;

theorem Th27:
  for X being TopSpace, Y being TopStruct, f being map of X,Y st f is empty
  holds f is continuous
proof
  let X be TopSpace, Y be TopStruct,
      f be map of X,Y such that
A1: f is empty;
  let P being Subset of Y;
  assume P is closed;
  f"P = {}X by A1,RELAT_1:172;
  hence f"P is closed;
end;

registration
  let X be TopSpace, Y be TopStruct;
  cluster empty -> continuous map of X,Y;
coherence by Th27;
end;

theorem
  for X being TopStruct, Y being non empty TopStruct,
      Z being non empty SubSpace of Y,
      f being map of X,Z
  holds f is map of X,Y
proof
  let X be TopStruct, Y be non empty TopStruct, Z be non empty SubSpace of Y;
  let f be map of X,Z;
  the carrier of Z is Subset of the carrier of Y by TSEP_1:1;
  then
A1: rng f c= the carrier of Y by XBOOLE_1:1;
  dom f = the carrier of X by FUNCT_2:def 1;
  hence f is map of X,Y by A1,FUNCT_2:4;
end;

theorem
  for S, T being non empty TopSpace,
      X being Subset of S, Y being Subset of T,
      f being continuous map of S,T,
      g being map of S|X,T|Y st g = f|X
  holds g is continuous
proof
  let S, T be non empty TopSpace,
      X be Subset of S,
      Y be Subset of T,
      f be continuous map of S,T,
      g be map of S|X,T|Y such that
A1: g = f|X;
  set h = f|X;
  dom f = the carrier of S by FUNCT_2:def 1;
  then
A2: dom h = X by RELAT_1:91;
A3: the carrier of S|X = X by JORDAN1:1;
  rng h c= the carrier of T;
  then reconsider h as map of S|X,T by A2,A3,FUNCT_2:4;
  h is continuous by TOPMETR:10;
  hence g is continuous by A1,TOPMETR:9;
end;

theorem
  for S, T being non empty TopSpace, Z being non empty SubSpace of T,
      f being map of S,T,
      g being map of S,Z st f = g & f is open holds
   g is open
proof
  let S, T be non empty TopSpace,
      Z be non empty SubSpace of T,
      f be map of S,T,
      g be map of S,Z such that
A1: f = g and
A2: f is open;
  for p being Point of S, P being open a_neighborhood of p
    ex R being a_neighborhood of g.p st R c= g.:P
  proof
    let p be Point of S,
        P be open a_neighborhood of p;
    consider R being open a_neighborhood of f.p such that
A3:   R c= f.:P by A2,TOPGRP_1:22;
    [#]Z = the carrier of Z by PRE_TOPC:12;
    then reconsider R2 = R /\ [#]Z as Subset of Z by XBOOLE_1:17;
    reconsider R2 as a_neighborhood of g.p by A1,Th26;
    take R2;
    R2 c= R by XBOOLE_1:17;
    hence R2 c= g.:P by A1,A3,XBOOLE_1:1;
  end;
  hence g is open by TOPGRP_1:23;
end;

theorem
  for S, T being non empty TopSpace,
      S1 being Subset of S, T1 being Subset of T,
      f being map of S,T, g being map of S|S1,T|T1
  st g = f|S1 & g is onto & f is open one-to-one
  holds g is open
proof
  let S, T be non empty TopSpace,
      S1 be Subset of S,
      T1 be Subset of T,
      f be map of S,T,
      g be map of S|S1,T|T1 such that
A1: g = f|S1 and
A2: rng g = the carrier of T|T1 and
A3: f is open and
A4: f is one-to-one;
  let A be Subset of S|S1;
  assume A is open;
  then consider C being Subset of S such that
A5: C is open and
A6: C /\ [#](S|S1) = A by TOPS_2:32;
A7: f.:C is open by A3,A5,T_0TOPSP:def 2;
A8: [#](T|T1)= T1 by PRE_TOPC:def 10;
A9: [#](T|T1)= the carrier of T|T1 by PRE_TOPC:12;
A10: [#](S|S1)= the carrier of S|S1 by PRE_TOPC:12;
A11: [#](S|S1)= S1 by PRE_TOPC:def 10;
A12: g.:(C /\ S1) c= g.:C /\ g.:S1 by RELAT_1:154;
  g.:A = (f.:C) /\ T1
  proof
    g.:C c= f.:C by A1,RELAT_1:161;
    then g.:C /\ g.:S1 c= (f.:C) /\ T1 by A8,A9,XBOOLE_1:27;
    hence g.:A c= (f.:C) /\ T1 by A6,A11,A12,XBOOLE_1:1;
    let y be set;
    assume
A13:   y in (f.:C) /\ T1;
    then y in f.:C by XBOOLE_0:def 3;
    then consider x being Element of S such that
A14:   x in C and
A15:   y = f.x by FUNCT_2:116;
    y in T1 by A13,XBOOLE_0:def 3;
    then consider a being set such that
A16:   a in dom g and
A17:   g.a = y by A2,A8,FUNCT_1:def 5;
A18: dom g c= dom f by A1,RELAT_1:89;
A19: dom f = the carrier of S by FUNCT_2:def 1;
    f.a = g.a by A1,A16,FUNCT_1:70;
    then a = x by A4,A15,A16,A17,A18,A19,FUNCT_1:def 8;
    then a in A by A6,A10,A14,A16,XBOOLE_0:def 3;
    hence y in g.:A by A16,A17,FUNCT_1:def 12;
  end;
  hence g.:A is open by A7,A8,TOPS_2:32;
end;

theorem
  for X, Y, Z being non empty TopSpace,
      f being map of X,Y, g being map of Y,Z st f is open & g is open
   holds g*f is open
proof
  let X, Y, Z be non empty TopSpace,
      f be map of X,Y,
      g be map of Y,Z such that
A1: f is open and
A2: g is open;
  let A be Subset of X;
  assume A is open;
  then
A3: f.:A is open by A1,T_0TOPSP:def 2;
  (g*f).:A = g.:(f.:A) by RELAT_1:159;
  hence (g*f).:A is open by A2,A3,T_0TOPSP:def 2;
end;

theorem
  for X, Y being TopSpace, Z being open SubSpace of Y,
      f being map of X, Y,
      g being map of X, Z st f = g & g is open
  holds f is open
proof
  let X, Y be TopSpace,
      Z be open SubSpace of Y,
      f be map of X, Y,
      g be map of X, Z such that
A1: f = g and
A2: g is open;
  let A be Subset of X;
  assume A is open;
  then g.:A is open by A2,T_0TOPSP:def 2;
  hence f.:A is open by A1,TSEP_1:17;
end;

theorem Th34:
  for S, T being non empty TopSpace,
      f being map of S,T st f is one-to-one onto
  holds f is continuous iff f" is open
proof
  let S, T be non empty TopSpace,
      f be map of S,T such that
A1: f is one-to-one;
  assume f is onto;
  then rng f = the carrier of T by FUNCT_2:def 3
    .= [#]T by PRE_TOPC:12;
  then
A2: f qua Function" = f" by A1,TOPS_2:def 4;
  thus f is continuous implies f" is open
  proof
    assume
A3:   f is continuous;
    let A be Subset of T;
    assume A is open;
    then f"A is open by A3,TOPS_2:55;
    hence f".:A is open by A1,A2,FUNCT_1:155;
  end;
  assume
A4: f" is open;
  now
    let A be Subset of T;
    assume A is open;
    then f".:A is open by A4,T_0TOPSP:def 2;
    hence f"A is open by A1,A2,FUNCT_1:155;
  end;
  hence thesis by TOPS_2:55;
end;

theorem
  for S, T being non empty TopSpace,
      f being map of S,T st f is one-to-one onto
  holds f is open iff f" is continuous
proof
  let S, T be non empty TopSpace,
      f be map of S,T such that
A1: f is one-to-one;
  assume f is onto;
  then
A2: rng f = the carrier of T by FUNCT_2:def 3
    .= [#]T by PRE_TOPC:12;
A3: the carrier of S = [#]S by PRE_TOPC:12;
  rng (f") = [#]S by A1,A2,TOPS_2:62;
  then
A4: f" is onto by A3,FUNCT_2:def 3;
A5: f" is one-to-one by A1,A2,TOPS_2:63;
  f"" = f by A1,A2,TOPS_2:64;
  hence thesis by A4,A5,Th34;
end;

theorem
  for S being TopSpace, T being non empty TopSpace holds
  S,T are_homeomorphic iff
  the TopStruct of S, the TopStruct of T are_homeomorphic
proof
  let S be TopSpace, T be non empty TopSpace;
  set SS = the TopStruct of S;
  set TT = the TopStruct of T;
A1: [#]S = the carrier of S by PRE_TOPC:12
        .= [#]SS by PRE_TOPC:12;
A2: [#]T = the carrier of T by PRE_TOPC:12
        .= [#]TT by PRE_TOPC:12;
  thus S,T are_homeomorphic implies
    the TopStruct of S, the TopStruct of T are_homeomorphic
  proof
    given f being map of S,T such that
A3:   f is_homeomorphism;
    reconsider g = f as map of SS,TT;
    take g;
A4: dom f = [#]S & rng f = [#]T & f is one-to-one by A3,TOPS_2:74;
    now
      let P be Subset of SS;
      reconsider R = P as Subset of S;
      thus g.:(Cl P) = f.:(Cl R) by TOPS_3:80
        .= Cl(f.:R) by A3,TOPS_2:74
        .= Cl(g.:P) by TOPS_3:80;
    end;
    hence g is_homeomorphism by A1,A2,A4,TOPS_2:74;
  end;
  given f being map of SS,TT such that
A5:  f is_homeomorphism;
  reconsider g = f as map of S,T;
  take g;
A6: dom f = [#]SS & rng f = [#]TT & f is one-to-one by A5,TOPS_2:74;
  now
    let P be Subset of S;
    reconsider R = P as Subset of SS;
    thus g.:(Cl P) = f.:(Cl R) by TOPS_3:80
      .= Cl(f.:R) by A5,TOPS_2:74
      .= Cl(g.:P) by TOPS_3:80;
  end;
  hence g is_homeomorphism by A1,A2,A6,TOPS_2:74;
end;

theorem
  for S, T being non empty TopSpace,
      f being map of S,T st f is one-to-one onto continuous open
  holds f is_homeomorphism
proof
  let S, T be non empty TopSpace,
      f be map of S,T such that
A1: f is one-to-one and
A2: f is onto and
A3: f is continuous and
A4: f is open;
A5: dom f = the carrier of S by FUNCT_2:def 1;
  then
A6: dom f = [#]S by PRE_TOPC:12;
  rng f = the carrier of T by A2,FUNCT_2:def 3;
  then
A7: rng f = [#]T by PRE_TOPC:12;
  for P being Subset of S holds P is open iff f.:P is open
  proof
    let P be Subset of S;
    thus P is open implies f.:P is open by A4,T_0TOPSP:def 2;
    assume f.:P is open;
    then f"(f.:P) is open by A3,TOPS_2:55;
    hence P is open by A1,A5,MSAFREE3:1;
  end;
  hence thesis by A1,A6,A7,TOPGRP_1:25;
end;

begin :: R^1

theorem
  for f being PartFunc of REAL,REAL st f = REAL --> r holds
  f is_continuous_on REAL
proof
  let f be PartFunc of REAL,REAL such that
A1: f = REAL --> r;
A2: dom f = REAL by A1,FUNCOP_1:19;
  f is_constant_on REAL
  proof
    reconsider r as Real by XREAL_0:def 1;
    take r;
    let c be Element of REAL;
    assume c in REAL /\ dom f;
    then c in dom f by XBOOLE_0:def 3;
    hence f/.c = f.c by FINSEQ_4:def 4
       .= r by A1,FUNCOP_1:13;
  end;
  hence f is_continuous_on REAL by A2,FCONT_1:44;
end;

theorem
  for f, f1, f2 being PartFunc of REAL,REAL st
   dom f = dom f1 \/ dom f2 & dom f1 is open & dom f2 is open &
   f1 is_continuous_on dom f1 & f2 is_continuous_on dom f2 &
   (for z being set st z in dom f1 holds f.z = f1.z) &
   (for z being set st z in dom f2 holds f.z = f2.z) holds
  f is_continuous_on dom f
proof
  let f, f1, f2 be PartFunc of REAL,REAL;
  set X1 = dom f1, X2 = dom f2;
  assume that
A1: dom f = X1 \/ X2 and
A2: X1 is open and
A3: X2 is open and
A4: f1 is_continuous_on X1 and
A5: f2 is_continuous_on X2 and
A6: for z being set st z in X1 holds f.z = f1.z and
A7: for z being set st z in X2 holds f.z = f2.z;
A8: f|(X1 \/ X2) = (f|X1) \/ (f|X2) by RELAT_1:107;
A9: dom ((f|X1) \/ (f|X2)) = dom (f|X1) \/ dom (f|X2) by RELAT_1:13;
A10: X1 c= dom f by A1,XBOOLE_1:7;
A11: dom (f|X1) = dom f /\ X1 by RELAT_1:90;
A12: dom f /\ X1 = X1 by A10,XBOOLE_1:28;
A13: X2 c= dom f by A1,XBOOLE_1:7;
A14: dom (f|X2) = dom f /\ X2 by RELAT_1:90;
A15: dom f /\ X2 = X2 by A13,XBOOLE_1:28;
  thus dom f c= dom f;
  let x be real number such that
A16:  x in dom f;
A17: (f|(X1 \/ X2)).x = f.x by A1,A16,FUNCT_1:72;
  per cases by A1,A16,XBOOLE_0:def 2;
  suppose
A18:  x in X1;
A19: (f|(X1 \/ X2)).x = f1.x by A6,A17,A18
      .= (f1|X1).x by A18,FUNCT_1:72;
A20: x in dom (f|(X1 \/ X2)) by A8,A9,A11,A12,A18,XBOOLE_0:def 2;
  for N1 being Neighbourhood of (f|(X1 \/ X2)).x
    ex N being Neighbourhood of x st
    for x1 being real number st x1 in dom (f|(X1 \/ X2)) & x1 in N holds
    (f|(X1 \/ X2)).x1 in N1
  proof
    let N1 be Neighbourhood of (f|(X1 \/ X2)).x;
    f1|X1 is_continuous_in x by A4,A18,FCONT_1:def 2;
    then consider N being Neighbourhood of x such that
A21:   for x1 being real number st x1 in dom (f1|X1) & x1 in N holds
      (f1|X1).x1 in N1 by A19,FCONT_1:4;
    consider N2 being Neighbourhood of x such that
A22:   N2 c= X1 by A2,A18,RCOMP_1:39;
    consider N3 being Neighbourhood of x such that
A23:   N3 c= N and
A24:   N3 c= N2 by RCOMP_1:38;
    take N3;
    let x1 be real number such that
A25:   x1 in dom (f|(X1 \/ X2)) and
A26:   x1 in N3;
    per cases;
    suppose
A27:   x1 in dom (f|X1);
    then
A28: x1 in X1 by A11,XBOOLE_0:def 3;
A29: dom (f|X1) = X1 /\ X1 by A10,A11,XBOOLE_1:28
      .= dom (f1|X1) by RELAT_1:90;
    (f|(X1 \/ X2)).x1 = f.x1 by A25,FUNCT_1:70
      .= f1.x1 by A6,A28
      .= (f1|X1).x1 by A28,FUNCT_1:72;
    hence (f|(X1 \/ X2)).x1 in N1 by A21,A23,A26,A27,A29;
    end;
    suppose
A30:   not x1 in dom (f|X1);
    x1 in N2 by A24,A26;
    hence (f|(X1 \/ X2)).x1 in N1 by A11,A12,A22,A30;
    end;
  end;
  hence f|dom f is_continuous_in x by A1,A20,FCONT_1:4;
  end;
  suppose
A31:  x in X2;
A32: (f|(X1 \/ X2)).x = f2.x by A7,A17,A31
      .= (f2|X2).x by A31,FUNCT_1:72;
A33: x in dom (f|(X1 \/ X2)) by A8,A9,A14,A15,A31,XBOOLE_0:def 2;
  for N1 being Neighbourhood of (f|(X1 \/ X2)).x
    ex N being Neighbourhood of x st
    for x1 being real number st x1 in dom (f|(X1 \/ X2)) & x1 in N holds
    (f|(X1 \/ X2)).x1 in N1
  proof
    let N1 be Neighbourhood of (f|(X1 \/ X2)).x;
    f2|X2 is_continuous_in x by A5,A31,FCONT_1:def 2;
    then consider N being Neighbourhood of x such that
A34:   for x1 being real number st x1 in dom (f2|X2) & x1 in N holds
      (f2|X2).x1 in N1 by A32,FCONT_1:4;
    consider N2 being Neighbourhood of x such that
A35:   N2 c= X2 by A3,A31,RCOMP_1:39;
    consider N3 being Neighbourhood of x such that
A36:   N3 c= N and
A37:   N3 c= N2 by RCOMP_1:38;
    take N3;
    let x1 be real number such that
A38:   x1 in dom (f|(X1 \/ X2)) and
A39:   x1 in N3;
    per cases;
    suppose
A40:   x1 in dom (f|X2);
    then
A41: x1 in X2 by A14,XBOOLE_0:def 3;
A42: dom (f|X2) = X2 /\ X2 by A13,A14,XBOOLE_1:28
      .= dom (f2|X2) by RELAT_1:90;
    (f|(X1 \/ X2)).x1 = f.x1 by A38,FUNCT_1:70
      .= f2.x1 by A7,A41
      .= (f2|X2).x1 by A41,FUNCT_1:72;
    hence (f|(X1 \/ X2)).x1 in N1 by A34,A36,A39,A40,A42;
    end;
    suppose
A43:   not x1 in dom (f|X2);
    x1 in N2 by A37,A39;
    hence (f|(X1 \/ X2)).x1 in N1 by A14,A15,A35,A43;
    end;
  end;
  hence f|dom f is_continuous_in x by A1,A33,FCONT_1:4;
  end;
end;

theorem Th40:
  for x being Point of R^1,
      N being Subset of REAL, M being Subset of R^1 st M = N holds
   N is Neighbourhood of x implies M is a_neighborhood of x
proof
  let x be Point of R^1,
      N be Subset of REAL,
      M be Subset of R^1 such that
A1:  M = N;
  given r such that
A2: 0 < r and
A3: N = ].x-r,x+r.[;
A4: M is open by A1,A3,JORDAN6:46;
  x in M by A1,A2,A3,TOPREAL6:20;
  hence M is a_neighborhood of x by A4,CONNSP_2:5;
end;

theorem Th41:
  for x being Point of R^1, M being a_neighborhood of x
   ex N being Neighbourhood of x st N c= M
proof
  let x be Point of R^1,
      M be a_neighborhood of x;
  consider V being Subset of R^1 such that
A1:  V is open and
A2:  V c= M and
A3:  x in V by CONNSP_2:8;
  x is Real by XREAL_0:def 1;
  then consider r being Real such that
A4:  r > 0 and
A5:  ].x-r,x+r.[ c= V by A1,A3,FRECHET:8;
  reconsider N = ].x-r,x+r.[ as Neighbourhood of x by A4,RCOMP_1:def 7;
  take N;
  thus thesis by A2,A5,XBOOLE_1:1;
end;

theorem Th42:
  for f being map of R^1,R^1, g being PartFunc of REAL,REAL,
      x being Point of R^1 st f = g & g is_continuous_in x holds
   f is_continuous_at x
proof
  let f be map of R^1,R^1,
      g be PartFunc of REAL,REAL,
      x be Point of R^1 such that
A1: f = g and
A2: g is_continuous_in x;
  let G be a_neighborhood of f.x;
  consider Z being Neighbourhood of g.x such that
A3: Z c= G by A1,Th41;
  consider N being Neighbourhood of x such that
A4:  g.:N c= Z by A2,FCONT_1:5;
  reconsider H = N as a_neighborhood of x by Th40,TOPMETR:24;
  take H;
  thus f.:H c= G by A1,A3,A4,XBOOLE_1:1;
end;

theorem
  for f being map of R^1,R^1, g being Function of REAL,REAL st
      f = g & g is_continuous_on REAL holds
   f is continuous
proof
  let f be map of R^1,R^1,
      g be Function of REAL,REAL such that
A1: f = g and
A2: g is_continuous_on REAL;
  for x being Point of R^1 holds f is_continuous_at x
  proof
    let x be Point of R^1;
A3: dom g = REAL by FUNCT_2:def 1;
    x is Real by XREAL_0:def 1;
    then g|REAL is_continuous_in x by A2,FCONT_1:def 2;
    then g is_continuous_in x by A3,RELAT_1:98;
    hence f is_continuous_at x by A1,Th42;
  end;
  hence f is continuous by TMAP_1:49;
end;

theorem
  a <= r & s <= b implies
  [.r,s.] is closed Subset of Closed-Interval-TSpace(a,b)
proof
  set T = Closed-Interval-TSpace(a,b);
  set A = [.r,s.];
  assume that
A1: a <= r and
A2: s <= b;
  per cases;
  suppose r > s;
  then A = {}T by RCOMP_1:13;
  hence thesis;
  end;
  suppose r <= s;
  then a <= s by A1,AXIOMS:22;
  then a <= b by A2,AXIOMS:22;
  then the carrier of T = [.a,b.] by TOPMETR:25;
  then reconsider A as Subset of T by A1,A2,TREAL_1:1;
  reconsider C = A as Subset of R^1 by TOPMETR:24;
A3: C is closed by TREAL_1:4;
  [#]T = the carrier of T by PRE_TOPC:12;
  then C /\ [#]T = A by XBOOLE_1:28;
  hence thesis by A3,PRE_TOPC:43;
  end;
end;

theorem
  a <= r & s <= b implies
  ].r,s.[ is open Subset of Closed-Interval-TSpace(a,b)
proof
  set T = Closed-Interval-TSpace(a,b);
  set A = ].r,s.[;
  assume that
A1: a <= r and
A2: s <= b;
  per cases;
  suppose r >= s;
  then A = {}T by RCOMP_1:12;
  hence thesis;
  end;
  suppose r < s;
  then a < s by A1,AXIOMS:22;
  then a <= b by A2,AXIOMS:22;
  then the carrier of T = [.a,b.] by TOPMETR:25;
  then reconsider A as Subset of T by A1,A2,Th19;
  reconsider C = A as Subset of R^1 by TOPMETR:24;
A3: C is open by JORDAN6:46;
  [#]T = the carrier of T by PRE_TOPC:12;
  then C /\ [#]T = A by XBOOLE_1:28;
  hence thesis by A3,TOPS_2:32;
  end;
end;

theorem
  a <= b & a <= r implies
  ].r,b.] is open Subset of Closed-Interval-TSpace(a,b)
proof
  set T = Closed-Interval-TSpace(a,b);
  assume that
A1: a <= b and
A2: a <= r;
A3: the carrier of T = [.a,b.] by A1,TOPMETR:25;
  then reconsider A = ].r,b.] as Subset of T by A2,Th15;
  reconsider C = ].r,b+1.[ as Subset of R^1 by TOPMETR:24;
A4: C is open by JORDAN6:46;
A5: [#]T = the carrier of T by PRE_TOPC:12;
  C /\ [#]T = A
  proof
    thus C /\ [#]T c= A
    proof
      let x be set;
      assume
A6:     x in C /\ [#]T;
      then
A7:   x in C by XBOOLE_0:def 3;
      then reconsider x as Real;
      x in [#]T by A6,XBOOLE_0:def 3;
      then
A8:   x <= b by A3,TOPREAL5:1;
      r < x by A7,JORDAN6:45;
      hence thesis by A8,RCOMP_2:4;
    end;
    b+0 < b+1 by REAL_1:53;
    then A c= C by Th18;
    hence thesis by A5,XBOOLE_1:19;
  end;
  hence thesis by A4,TOPS_2:32;
end;

theorem
  a <= b & r <= b implies
  [.a,r.[ is open Subset of Closed-Interval-TSpace(a,b)
proof
  set T = Closed-Interval-TSpace(a,b);
  assume that
A1: a <= b and
A2: r <= b;
A3: the carrier of T = [.a,b.] by A1,TOPMETR:25;
  then reconsider A = [.a,r.[ as Subset of T by A2,Th11;
  reconsider C = ].a-1,r.[ as Subset of R^1 by TOPMETR:24;
A4: C is open by JORDAN6:46;
A5: [#]T = the carrier of T by PRE_TOPC:12;
  C /\ [#]T = A
  proof
    thus C /\ [#]T c= A
    proof
      let x be set;
      assume
A6:     x in C /\ [#]T;
      then
A7:   x in C by XBOOLE_0:def 3;
      then reconsider x as Real;
      x in [#]T by A6,XBOOLE_0:def 3;
      then
A8:   a <= x by A3,TOPREAL5:1;
      x < r by A7,JORDAN6:45;
      hence thesis by A8,RCOMP_2:3;
    end;
    a-1 < a-0 by REAL_1:92;
    then A c= C by Th14;
    hence thesis by A5,XBOOLE_1:19;
  end;
  hence thesis by A4,TOPS_2:32;
end;

theorem Th48:
  a <= b & r <= s implies
  the carrier of [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):]
    = [: [.a,b.], [.r,s.] :]
proof
  set C1 = Closed-Interval-TSpace(a,b);
  set C2 = Closed-Interval-TSpace(r,s);
  assume a <= b & r <= s;
  then the carrier of C1 = [.a,b.] & the carrier of C2 = [.r,s.] by TOPMETR:25;
  hence the carrier of [:C1,C2:] = [:[.a,b.], [.r,s.]:] by BORSUK_1:def 5;
end;

begin :: TOP-REAL 2

theorem Th49:
  |[a,b]| = (1,2) --> (a,b)
proof
  thus |[a,b]| = <*a,b*> by EUCLID:def 16
    .= (1,2) --> (a,b) by Th25;
end;

theorem
  |[a,b]|.1 = a & |[a,b]|.2 = b
proof
  thus |[a,b]|.1 = ((1,2) --> (a,b)).1 by Th49
    .= a by FUNCT_4:66;
  thus |[a,b]|.2 = ((1,2) --> (a,b)).2 by Th49
    .= b by FUNCT_4:66;
end;

theorem Th51:
  closed_inside_of_rectangle(a,b,r,s) = product ((1,2)-->([.a,b.],[.r,s.]))
proof
  set A = [.a,b.], B = [.r,s.];
  set f = (1,2)-->(A,B);
  set R = closed_inside_of_rectangle(a,b,r,s);
A1: R = {p where p is Point of TOP-REAL 2:
        a <= p`1 & p`1 <= b & r <= p`2 & p`2 <= s} by JGRAPH_6:def 2;
  thus R c= product f
  proof
    let x be set;
    assume x in R;
    then consider p being Point of TOP-REAL 2 such that
A2:   x = p and
A3:   a <= p`1 & p`1 <= b & r <= p`2 & p`2 <= s by A1;
A4: p`1 in A & p`2 in B by A3,TOPREAL5:1;
    |[p`1,p`2]| = (1,2) --> (p`1,p`2) by Th49;
    then p = (1,2) --> (p`1,p`2) by EUCLID:57;
    hence x in product f by A2,A4,HILBERT3:11;
  end;
  let x be set;
  assume
A5: x in product f;
  then consider g being Function such that
A6: x = g and
A7: dom g = dom f and
    for y being set st y in dom f holds g.y in f.y by CARD_3:def 5;
A8: g.1 in A & g.2 in B by A5,A6,Th24;
A9: dom f = {1,2} by FUNCT_4:65;
  reconsider g1 = g.1, g2 = g.2 as Real by A8;
A10: |[g1,g2]| = (1,2) --> (g1,g2) by Th49;
  reconsider g as FinSequence by A7,A9,FINSEQ_1:4,def 2;
  len g = 2 by A7,A9,FINSEQ_1:4,def 3;
  then g = <*g.1,g.2*> by FINSEQ_1:61;
  then reconsider g as Point of TOP-REAL 2 by A10,Th25;
  g.1 = g`1 & g.2 = g`2 by EUCLID:def 14,def 15;
  then a <= g`1 & g`1 <= b & r <= g`2 & g`2 <= s by A8,TOPREAL5:1;
  hence x in R by A1,A6;
end;

theorem Th52:
  a <= b & r <= s implies
  |[a,r]| in closed_inside_of_rectangle(a,b,r,s)
proof
  set o = |[a,r]|;
  assume
A1: a <= b & r <= s;
A2: closed_inside_of_rectangle(a,b,r,s) =
      {p where p is Point of TOP-REAL 2:
        a <= p`1 & p`1 <= b & r <= p`2 & p`2 <= s} by JGRAPH_6:def 2;
  o`1 = a & o`2 = r by EUCLID:56;
  hence thesis by A1,A2;
end;

definition
  let a, b, c, d be real number;
  func Trectangle(a,b,c,d) -> SubSpace of TOP-REAL 2 equals :Def1:
  (TOP-REAL 2) | closed_inside_of_rectangle(a,b,c,d);
coherence;
end;

theorem Th53:
  the carrier of Trectangle(a,b,r,s) = closed_inside_of_rectangle(a,b,r,s)
proof
  thus the carrier of Trectangle(a,b,r,s) =
  the carrier of (TOP-REAL 2) | closed_inside_of_rectangle(a,b,r,s) by Def1
  .= closed_inside_of_rectangle(a,b,r,s) by JORDAN1:1;
end;

theorem Th54:
  a <= b & r <= s implies Trectangle(a,b,r,s) is non empty
proof
  assume a <= b & r <= s;
  then |[a,r]| in closed_inside_of_rectangle(a,b,r,s) by Th52;
  hence the carrier of Trectangle(a,b,r,s) is non empty by Th53;
end;

registration
  let a, c be non positive (real number);
  let b, d be non negative (real number);
  cluster Trectangle(a,b,c,d) -> non empty;
coherence by Th54;
end;

definition
  func R2Homeomorphism -> map of [:R^1,R^1:], TOP-REAL 2 means :Def2:
  for x, y being real number holds it. [x,y] = <*x,y*>;
existence
proof
  consider f being map of [:R^1,R^1:], TOP-REAL 2 such that
A1: for x, y being Real holds f. [x,y] = <*x,y*> by BORSUK_6:24;
  take f;
  let x, y be real number;
  x is Real & y is Real by XREAL_0:def 1;
  hence thesis by A1;
end;
uniqueness
proof
  let f, g be map of [:R^1,R^1:], TOP-REAL 2 such that
A2: for x, y being real number holds f. [x,y] = <*x,y*> and
A3: for x, y being real number holds g. [x,y] = <*x,y*>;
  now
    let a be Point of [:R^1,R^1:];
    consider x, y being Element of R such that
A4:   a = [x,y] by Lm1,DOMAIN_1:9;
    thus f.a = <*x,y*> by A2,A4
      .= g.a by A3,A4;
  end;
  hence thesis by FUNCT_2:113;
end;
end;

theorem Th55:
  for A, B being Subset of REAL holds
  R2Homeomorphism.:[:A,B:] = product ((1,2) --> (A,B))
proof
  for x, y being Real holds R2Homeomorphism. [x,y] = <*x,y*> by Def2;
  hence thesis by TOPREAL6:84;
end;

theorem Th56:
  R2Homeomorphism is_homeomorphism
proof
  for x, y being Real holds R2Homeomorphism. [x,y] = <*x,y*> by Def2;
  hence thesis by TOPREAL6:85;
end;

theorem Th57:
  a <= b & r <= s implies
  R2Homeomorphism | the carrier of
      [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):]
  is map of [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):],
    Trectangle(a,b,r,s)
proof
  set C1 = Closed-Interval-TSpace(a,b);
  set C2 = Closed-Interval-TSpace(r,s);
  set TR = Trectangle(a,b,r,s);
  set h = R2Homeomorphism | the carrier of [:C1,C2:];
  assume a <= b & r <= s;
  then
A1: the carrier of [:C1,C2:] = [: [.a,b.], [.r,s.] :] by Th48;
  dom R2Homeomorphism = [:R,R:] by Lm1,FUNCT_2:def 1;
  then the carrier of [:C1,C2:] c= dom R2Homeomorphism
    by A1,TOPMETR:24,ZFMISC_1:119;
  then
A2: dom h = the carrier of [:C1,C2:] by RELAT_1:91;
  rng h c= the carrier of TR
  proof
    let y be set;
    assume y in rng h;
    then consider x being set such that
A3:   x in dom h and
A4:   h.x = y by FUNCT_1:def 5;
A5: the carrier of TR = closed_inside_of_rectangle(a,b,r,s) by Th53;
A6: closed_inside_of_rectangle(a,b,r,s) =
      {p where p is Point of TOP-REAL 2:
        a <= p`1 & p`1 <= b & r <= p`2 & p`2 <= s} by JGRAPH_6:def 2;
    reconsider x as Point of [:R^1,R^1:] by A3;
A7: h.x = R2Homeomorphism.x by A3,FUNCT_1:70;
    then
    reconsider p = h.x as Point of TOP-REAL 2;
    dom h c= [:R,R:] by A1,A2,TOPMETR:24,ZFMISC_1:119;
    then consider x1, x2 being Element of R such that
A8:   x = [x1,x2] by A3,DOMAIN_1:9;
    h.x = <*x1,x2*> by A7,A8,Def2;
    then p`1 = <*x1,x2*>.1 & p`2 = <*x1,x2*>.2 by EUCLID:def 14,def 15;
    then
A9: x1 = p`1 & x2 = p`2 by FINSEQ_1:61;
    x1 in [.a,b.] & x2 in [.r,s.] by A1,A2,A3,A8,ZFMISC_1:106;
    then a <= p`1 & p`1 <= b & r <= p`2 & p`2 <= s by A9,TOPREAL5:1;
    hence thesis by A4,A5,A6;
  end;
  hence thesis by A2,FUNCT_2:4;
end;

theorem Th58:
  a <= b & r <= s implies
  for h being map of
    [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):],
    Trectangle(a,b,r,s) st
  h = R2Homeomorphism | the carrier of
      [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):]
  holds h is_homeomorphism
proof
  assume
A1: a <= b & r <= s;
  set C1 = Closed-Interval-TSpace(a,b);
  set C2 = Closed-Interval-TSpace(r,s);
  set TR = Trectangle(a,b,r,s);
  let h be map of [:C1,C2:], TR such that
A2: h = R2Homeomorphism | the carrier of [:C1,C2:];
  reconsider S0 = [:C1,C2:] as non empty SubSpace of [:R^1,R^1:]
    by BORSUK_3:25;
A3: closed_inside_of_rectangle(a,b,r,s) =
      {p where p is Point of TOP-REAL 2:
        a <= p`1 & p`1 <= b & r <= p`2 & p`2 <= s} by JGRAPH_6:def 2;
A4: the carrier of TR = closed_inside_of_rectangle(a,b,r,s) by Th53;
  set p = |[a,r]|;
  p`1 = a & p`2 = r by EUCLID:56;
  then p in closed_inside_of_rectangle(a,b,r,s) by A1,A3;
  then reconsider T0 = TR as non empty SubSpace of TOP-REAL 2
    by A4,STRUCT_0:def 1;
  reconsider g = h as map of S0,T0;
A5: g = R2Homeomorphism|S0 by A2,TMAP_1:def 3;
  g is onto
  proof
    thus rng g c= the carrier of T0;
    let y be set;
    assume
A6:  y in the carrier of T0;
    consider p being Point of TOP-REAL 2 such that
A7:   y = p and
A8:   a <= p`1 & p`1 <= b & r <= p`2 & p`2 <= s by A3,A4,A6;
A9: the carrier of [:C1,C2:] = [:[.a,b.],[.r,s.]:] by A1,Th48;
A10: dom g = the carrier of S0 by FUNCT_2:def 1;
    p`1 in [.a,b.] & p`2 in [.r,s.] by A8,TOPREAL5:1;
    then
A11: [p`1,p`2] in dom g by A9,A10,ZFMISC_1:def 2;
    then g. [p`1,p`2] = R2Homeomorphism. [p`1,p`2] by A2,FUNCT_1:72
      .= <*p`1,p`2*> by Def2
      .= |[p`1,p`2]| by EUCLID:def 16
      .= y by A7,EUCLID:57;
    hence thesis by A11,FUNCT_1:def 5;
  end;
  hence thesis by A5,Th56,JORDAN16:13;
end;

theorem
  a <= b & r <= s implies
  [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):],
  Trectangle(a,b,r,s) are_homeomorphic
proof
  assume
A1: a <= b & r <= s;
  set C1 = Closed-Interval-TSpace(a,b);
  set C2 = Closed-Interval-TSpace(r,s);
  reconsider h = R2Homeomorphism | the carrier of [:C1,C2:]
    as map of [:C1,C2:], Trectangle(a,b,r,s) by A1,Th57;
  take h;
  thus thesis by A1,Th58;
end;

theorem Th60:
  a <= b & r <= s implies
  for A being Subset of Closed-Interval-TSpace(a,b),
      B being Subset of Closed-Interval-TSpace(r,s) holds
  product ((1,2)-->(A,B)) is Subset of Trectangle(a,b,r,s)
proof
  set T = Closed-Interval-TSpace(a,b);
  set S = Closed-Interval-TSpace(r,s);
  assume
A1: a <= b & r <= s;
  let A be Subset of T;
  let B be Subset of S;
A2: the carrier of T = [.a,b.] & the carrier of S = [.r,s.] by A1,TOPMETR:25;
  closed_inside_of_rectangle(a,b,r,s) =
    product ((1,2)-->([.a,b.],[.r,s.])) by Th51;
  then product ((1,2)-->(A,B)) c= closed_inside_of_rectangle(a,b,r,s)
    by A2,TOPREAL6:29;
  hence thesis by Th53;
end;

theorem
  a <= b & r <= s implies
  for A being open Subset of Closed-Interval-TSpace(a,b),
      B being open Subset of Closed-Interval-TSpace(r,s) holds
  product ((1,2)-->(A,B)) is open Subset of Trectangle(a,b,r,s)
proof
  set T = Closed-Interval-TSpace(a,b);
  set S = Closed-Interval-TSpace(r,s);
  assume that
A1: a <= b and
A2: r <= s;
  let A be open Subset of T,
      B be open Subset of S;
  reconsider P = product ((1,2)-->(A,B)) as Subset of Trectangle(a,b,r,s)
    by A1,A2,Th60;
  the carrier of T is Subset of R^1 &
    the carrier of S is Subset of R^1 by TSEP_1:1;
  then
A3: A is Subset of REAL & B is Subset of REAL by TOPMETR:24,XBOOLE_1:1;
  reconsider h = R2Homeomorphism | the carrier of
      [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):]
  as map of [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):],
    Trectangle(a,b,r,s) by A1,A2,Th57;
A4: h is_homeomorphism by A1,A2,Th58;
A5: Trectangle(a,b,r,s) is non empty by A1,A2,Th54;
A6: h.:[:A,B:] = R2Homeomorphism.:[:A,B:] by EXTENS_1:1
       .= P by A3,Th55;
  [:A,B:] is open by BORSUK_1:46;
  hence thesis by A4,A5,A6,TOPGRP_1:25;
end;

theorem
  a <= b & r <= s implies
  for A being closed Subset of Closed-Interval-TSpace(a,b),
      B being closed Subset of Closed-Interval-TSpace(r,s) holds
  product ((1,2)-->(A,B)) is closed Subset of Trectangle(a,b,r,s)
proof
  set T = Closed-Interval-TSpace(a,b);
  set S = Closed-Interval-TSpace(r,s);
  assume that
A1: a <= b and
A2: r <= s;
  let A be closed Subset of T,
      B be closed  Subset of S;
  reconsider P = product ((1,2)-->(A,B)) as Subset of Trectangle(a,b,r,s)
    by A1,A2,Th60;
  the carrier of T is Subset of R^1 &
    the carrier of S is Subset of R^1 by TSEP_1:1;
  then
A3: A is Subset of REAL & B is Subset of REAL by TOPMETR:24,XBOOLE_1:1;
  reconsider h = R2Homeomorphism | the carrier of
      [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):]
  as map of [:Closed-Interval-TSpace(a,b),Closed-Interval-TSpace(r,s):],
    Trectangle(a,b,r,s) by A1,A2,Th57;
A4: h is_homeomorphism by A1,A2,Th58;
A5: Trectangle(a,b,r,s) is non empty by A1,A2,Th54;
A6: h.:[:A,B:] = R2Homeomorphism.:[:A,B:] by EXTENS_1:1
       .= P by A3,Th55;
  [:A,B:] is closed by TOPALG_3:16;
  hence thesis by A4,A5,A6,TOPS_2:72;
end;

