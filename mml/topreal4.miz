:: Connectedness Conditions Using Polygonal Arcs
::  by Yatsuka Nakamura and Jaros{\l}aw Kotowicz
::
:: Received August 24, 1992
:: Copyright (c) 1992 Association of Mizar Users
 
environ

 vocabularies NUMBERS, SUBSET_1, EUCLID, PRE_TOPC, FINSEQ_1, REAL_1, TOPREAL1,
      PARTFUN1, XBOOLE_0, RCOMP_1, RELAT_2, XXREAL_0, RELAT_1, FUNCT_1,
      TOPREAL2, MCART_1, METRIC_1, ARYTM_3, TARSKI, RLTOPSP1, ORDINAL4, NAT_1,
      CARD_1, ARYTM_1, PCOMPS_1, XREAL_0, ORDINAL1, STRUCT_0, TOPREAL4;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XREAL_0, REAL_1,
      NAT_1, RELAT_1, FUNCT_1, PARTFUN1, FINSEQ_1, FINSEQ_4, STRUCT_0,
      PRE_TOPC, CONNSP_1, METRIC_1, PCOMPS_1, RLTOPSP1, EUCLID, TOPREAL1,
      TOPREAL2, XXREAL_0;
 constructors PARTFUN1, XXREAL_0, REAL_1, NAT_1, FINSEQ_4, CONNSP_1, PCOMPS_1,
      TOPREAL1, TOPREAL2, RELSET_1, FUNCSDOM, DOMAIN_1;
 registrations XBOOLE_0, RELSET_1, XXREAL_0, XREAL_0, NAT_1, MEMBERED,
      STRUCT_0, PRE_TOPC, EUCLID;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, TOPREAL1, XBOOLE_0, STRUCT_0;
 theorems TARSKI, NAT_1, ZFMISC_1, FUNCT_1, FINSEQ_1, PRE_TOPC, CONNSP_1,
      EUCLID, TOPMETR, TOPREAL1, TOPREAL2, TOPREAL3, FINSEQ_4, FINSEQ_3,
      TBSP_1, PARTFUN2, RELAT_1, INT_1, XBOOLE_0, XBOOLE_1, XREAL_0, XREAL_1,
      XXREAL_0, PARTFUN1, ORDINAL1, RLTOPSP1;
 schemes NAT_1;

begin

reserve P,P1,P2,R for Subset of TOP-REAL 2,
  p,p1,p2,p3,p11,p22,q,q1,q2,q3,q4 for Point of TOP-REAL 2,
  f,h for FinSequence of TOP-REAL 2,
  r for Real,
  u for Point of Euclid 2,
  n,m,i,j,k for Element of NAT,
  x,y for set;

definition
  let P,p,q;
  pred P is_S-P_arc_joining p,q means
  :Def1:
  ex f st f is being_S-Seq & P = L~ f & p=f/.1 & q=f/.len f;
end;

definition
  let P;
  attr P is being_special_polygon means
  ex p1,p2,P1,P2 st p1 <> p2 & p1 in P &
p2 in P & P1 is_S-P_arc_joining p1,p2 & P2 is_S-P_arc_joining p1,p2 & P1 /\ P2
  = {p1,p2} & P = P1 \/ P2;
end;

definition
  let T be TopStruct, P be Subset of T;
  attr P is being_Region means
  :Def3:
  P is open connected;
end;

canceled;

theorem Th2:
  P is_S-P_arc_joining p,q implies P is being_S-P_arc
proof
  assume P is_S-P_arc_joining p,q;
  then ex f st f is being_S-Seq & P = L~f & p=f/.1 & q=f/.len f by Def1;
  hence thesis by TOPREAL1:def 11;
end;

theorem Th3:
  P is_S-P_arc_joining p,q implies P is_an_arc_of p,q
proof
  assume P is_S-P_arc_joining p,q;
  then ex h st h is being_S-Seq & P= L~h & p=h/.1 & q=h/.len h by Def1;
  hence thesis by TOPREAL1:31;
end;

theorem Th4:
  P is_S-P_arc_joining p,q implies p in P & q in P
proof
  assume P is_S-P_arc_joining p,q;
  then P is_an_arc_of p,q by Th3;
  hence thesis by TOPREAL1:4;
end;

theorem
  P is_S-P_arc_joining p,q implies p<>q
proof
  assume that
A1: P is_S-P_arc_joining p,q and
A2: p=q;
  consider f such that
A3: f is being_S-Seq and
  P = L~f and
A4: p=f/.1 & q=f/.len f by A1,Def1;
  len f >= 2 by A3,TOPREAL1:def 10;
  then Seg len f = dom f & len f >= 1 by FINSEQ_1:def 3,XXREAL_0:2;
  then
A5: len f in dom f & 1 in dom f by FINSEQ_1:3;
  f is one-to-one & 1 <> len f by A3,TOPREAL1:def 10;
  hence contradiction by A2,A4,A5,PARTFUN2:17;
end;

theorem
  P is being_special_polygon implies P is being_simple_closed_curve
proof
  given p1,p2,P1,P2 such that
A1: p1 <> p2 & p1 in P & p2 in P and
A2: P1 is_S-P_arc_joining p1,p2 & P2 is_S-P_arc_joining p1,p2 and
A3: P1 /\ P2 = {p1,p2} and
A4: P = P1 \/ P2;
  reconsider P1, P2 as non empty Subset of TOP-REAL 2 by A3;
  P1 is_an_arc_of p1,p2 & P2 is_an_arc_of p1,p2 by A2,Th3;
  hence thesis by A1,A3,A4,TOPREAL2:6;
end;

theorem Th7:
  p`1 = q`1 & p`2 <> q`2 & p in Ball(u,r) & q in Ball(u,r) & f =<*
p,|[p`1,(p`2+q`2)/2]|,q *> implies f is being_S-Seq & f/.1 =p & f/.len f = q &
  L~f is_S-P_arc_joining p,q & L~f c= Ball(u,r)
proof
  assume that
A1: p`1 = q`1 and
A2: p`2 <> q`2 and
A3: p in Ball(u,r) & q in Ball(u,r) and
A4: f =<* p,|[p`1,(p`2+q`2)/2]|,q *>;
  thus
A5: f is being_S-Seq & f/.1 =p & f/.len f = q by A1,A2,A4,TOPREAL3:43;
  p=|[p`1,p`2]| & q=|[q`1,q`2]| by EUCLID:57;
  then |[p`1,(p`2+q`2)/2]| in Ball(u,r) by A1,A3,TOPREAL3:30;
  then
A6: LSeg(p,|[p`1,(p`2+q`2)/2]|) c= Ball(u,r) & LSeg(|[p`1,(p`2+q`2)/2]|,q)
  c= Ball(u,r) by A3,TOPREAL3:28;
  thus L~f is_S-P_arc_joining p,q by A5,Def1;
  L~f=LSeg(p,|[p`1,(p`2+q`2)/2]|) \/ LSeg(|[p`1,(p`2+q`2)/2]|,q) by A4,
  TOPREAL3:23;
  hence thesis by A6,XBOOLE_1:8;
end;

theorem Th8:
  p`1 <> q`1 & p`2 = q`2 & p in Ball(u,r) & q in Ball(u,r) & f = <*
p,|[(p`1+q`1)/2,p`2]|,q *> implies f is being_S-Seq & f/.1 =p & f/.len f = q &
  L~f is_S-P_arc_joining p,q & L~f c= Ball(u,r)
proof
  assume that
A1: p`1 <> q`1 and
A2: p`2 = q`2 and
A3: p in Ball(u,r) & q in Ball(u,r) and
A4: f = <* p,|[(p`1+q`1)/2,p`2]|,q *>;
  thus
A5: f is being_S-Seq & f/.1 =p & f/.len f = q by A1,A2,A4,TOPREAL3:44;
  p=|[p`1,p`2]| & q=|[q`1,q`2]| by EUCLID:57;
  then |[(p`1+q`1)/2,p`2]| in Ball(u,r) by A2,A3,TOPREAL3:31;
  then
A6: LSeg(p,|[(p`1+q`1)/2,p`2]|) c= Ball(u,r) & LSeg(|[(p`1+q`1)/2,p`2]|,q)
  c= Ball(u,r) by A3,TOPREAL3:28;
  thus L~f is_S-P_arc_joining p,q by A5,Def1;
  L~f=LSeg(p,|[(p`1+q`1)/2,p`2]|) \/ LSeg(|[(p`1+q`1)/2,p`2]|,q) by A4,
  TOPREAL3:23;
  hence thesis by A6,XBOOLE_1:8;
end;

theorem Th9:
  p`1 <> q`1 & p`2 <> q`2 & p in Ball(u,r) & q in Ball(u,r) & |[p`1
,q`2]| in Ball(u,r) & f =<* p,|[p`1,q`2]|,q *> implies f is being_S-Seq & f/.1
  =p & f/.len f = q & L~f is_S-P_arc_joining p,q & L~f c= Ball(u,r)
proof
  assume that
A1: p`1 <> q`1 & p`2 <> q`2 and
A2: p in Ball(u,r) and
A3: q in Ball(u,r) and
A4: |[p`1,q`2]| in Ball(u,r) and
A5: f =<* p,|[p`1,q`2]|,q *>;
  thus
A6: f is being_S-Seq & f/.1 =p & f/.len f = q by A1,A5,TOPREAL3:41;
A7: LSeg(|[p`1,q`2]|,q) c= Ball(u,r) by A3,A4,TOPREAL3:28;
  thus L~f is_S-P_arc_joining p,q by A6,Def1;
  L~f=LSeg(p,|[p`1,q`2]|) \/ LSeg(|[p`1,q`2]|,q) & LSeg(p,|[p`1,q`2]|) c=
  Ball (u,r) by A2,A4,A5,TOPREAL3:23,28;
  hence thesis by A7,XBOOLE_1:8;
end;

theorem Th10:
  p`1 <> q`1 & p`2 <> q`2 & p in Ball(u,r) & q in Ball(u,r) & |[q
`1,p`2]| in Ball(u,r) & f =<* p,|[q`1,p`2]|,q *> implies f is being_S-Seq & f/.
  1 =p & f/.len f = q & L~f is_S-P_arc_joining p,q & L~f c= Ball(u,r)
proof
  assume that
A1: p`1 <> q`1 & p`2 <> q`2 and
A2: p in Ball(u,r) and
A3: q in Ball(u,r) and
A4: |[q`1,p`2]| in Ball(u,r) and
A5: f =<* p,|[q`1,p`2]|,q *>;
  thus
A6: f is being_S-Seq & f/.1 =p & f/.len f = q by A1,A5,TOPREAL3:42;
A7: LSeg(|[q`1,p`2]|,q) c= Ball(u,r) by A3,A4,TOPREAL3:28;
  thus L~f is_S-P_arc_joining p,q by A6,Def1;
  L~f=LSeg(p,|[q`1,p`2]|) \/ LSeg(|[q`1,p`2]|,q) & LSeg(p,|[q`1,p`2]|) c=
  Ball (u,r) by A2,A4,A5,TOPREAL3:23,28;
  hence thesis by A7,XBOOLE_1:8;
end;

theorem Th11:
  p <> q & p in Ball(u,r) & q in Ball(u,r) implies ex P st P
  is_S-P_arc_joining p,q & P c= Ball(u,r)
proof
  assume that
A1: p<>q and
A2: p in Ball(u,r) & q in Ball(u,r);
  now
    per cases by A1,TOPREAL3:11;
    suppose
A3:   p`1 <> q`1;
      now
        per cases;
        suppose
A4:       p`2 = q`2;
          reconsider P = L~<* p,|[(p`1+q`1)/2,p`2]|,q *> as Subset of TOP-REAL
          2;
          take P;
          thus P is_S-P_arc_joining p,q & P c= Ball(u,r) by A2,A3,A4,Th8;
        end;
        suppose
A5:       p`2 <> q`2;
A6:       p = |[p`1,p`2]| & q=|[q`1,q`2]| by EUCLID:57;
          now
            per cases by A2,A6,TOPREAL3:32;
            suppose
A7:           |[p`1,q`2]| in Ball(u,r);
              reconsider P = L~<* p,|[p`1,q`2]|,q *> as Subset of TOP-REAL 2;
              take P;
              thus P is_S-P_arc_joining p,q & P c= Ball(u,r) by A2,A3,A5,A7,
              Th9;
            end;
            suppose
A8:           |[q`1,p`2]| in Ball(u,r);
              reconsider P = L~<* p,|[q`1,p`2]|,q *> as Subset of TOP-REAL 2;
              take P;
              thus P is_S-P_arc_joining p,q & P c= Ball(u,r) by A2,A3,A5,A8,
              Th10;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    suppose
A9:   p`2 <> q`2;
      now
        per cases;
        suppose
A10:      p`1 = q`1;
          reconsider P = L~<* p,|[p`1,(p`2+q`2)/2]|,q *> as Subset of TOP-REAL
          2;
          take P;
          thus P is_S-P_arc_joining p,q & P c= Ball(u,r) by A2,A9,A10,Th7;
        end;
        suppose
A11:      p`1 <> q`1;
A12:      p = |[p`1,p`2]| & q=|[q`1,q`2]| by EUCLID:57;
          now
            per cases by A2,A12,TOPREAL3:32;
            suppose
A13:          |[p`1,q`2]| in Ball(u,r);
              reconsider P = L~<* p,|[p`1,q`2]|,q *> as Subset of TOP-REAL 2;
              take P;
              thus P is_S-P_arc_joining p,q & P c= Ball(u,r) by A2,A9,A11,A13
              ,Th9;
            end;
            suppose
A14:          |[q`1,p`2]| in Ball(u,r);
              reconsider P = L~<* p,|[q`1,p`2]|,q *> as Subset of TOP-REAL 2;
              take P;
              thus P is_S-P_arc_joining p,q & P c= Ball(u,r) by A2,A9,A11,A14
              ,Th10;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th12:
  p<>f/.1 & (f/.1)`2 = p`2 & f is being_S-Seq & p in LSeg(f,1) & h
= <* f/.1,|[((f/.1)`1+p`1)/2,(f/.1)`2]|,p *> implies h is being_S-Seq & h/.1=f
  /.1 & h/.len h=p & L~h is_S-P_arc_joining f/.1,p & L~h c= L~f & L~h = L~(f|1)
  \/ LSeg(f/.1,p)
proof
  assume that
A1: p<>f/.1 and
A2: (f/.1)`2 = p`2 and
A3: f is being_S-Seq and
A4: p in LSeg(f,1) and
A5: h = <* f/.1,|[((f/.1)`1+p`1)/2, (f/.1)`2]|,p *>;
  set p1 = f/.1, q = f/.(1+1);
A6: L~h=LSeg(p1,|[(p1`1 + p`1)/2,p1`2]|) \/ LSeg(|[(p1`1 + p`1)/2,p1`2]|,p)
  by A5,TOPREAL3:23;
A7: len f >= 2 by A3,TOPREAL1:def 10;
  then
A8: LSeg(f,1) = LSeg(p1,q) by TOPREAL1:def 5;
A9: p1`1 <> p`1 by A1,A2,TOPREAL3:11;
  hence
A10: h is being_S-Seq & h/.1 = p1 & h/.len h = p by A2,A5, TOPREAL3
  :44;
  p1 in LSeg(p1,q) by RLTOPSP1:69;
  then
A11: LSeg(p1,p) c= LSeg(p1,q) by A4,A8,TOPREAL1:12;
A12: Seg len f = dom f by FINSEQ_1:def 3;
  thus L~h is_S-P_arc_joining p1,p by A10,Def1;
A13: LSeg(f,1) c= L~f by TOPREAL3:26;
  LSeg(p1,|[(p1`1 + p`1)/2,p1`2]|) \/ LSeg(|[(p1`1 + p`1)/2,p1`2]|,p) =
  LSeg(p1,p) by A2,A9,TOPREAL1:11,TOPREAL3:19;
  hence L~h c= L~f by A8,A11,A6,A13,XBOOLE_1:1;
  len f >= 1 by A7,XXREAL_0:2;
  then Seg 1 c= Seg len f by FINSEQ_1:7;
  then f|1 = f|Seg 1 & dom f /\ Seg 1 = Seg 1 by A12,FINSEQ_1:def 15 ,
  XBOOLE_1:28;
  then dom(f|1) = Seg 1 by RELAT_1:90;
  then len (f|1) = 1 by FINSEQ_1:def 3;
  then L~(f|1)={} by TOPREAL1:28;
  hence thesis by A2,A9,A6,TOPREAL1:11,TOPREAL3:19;
end;

theorem Th13:
  p<>f/.1 & (f/.1)`1 = p`1 & f is being_S-Seq & p in LSeg(f,1) & h
= <* f/.1,|[(f/.1)`1,((f/.1)`2+p`2)/2]|,p *> implies h is being_S-Seq & h/.1=f
  /.1 & h/.len h=p & L~h is_S-P_arc_joining f/.1,p & L~h c= L~f & L~h = L~(f|1)
  \/ LSeg(f/.1,p)
proof
  set p1 = f/.1;
  assume that
A1: p<>p1 and
A2: p1`1 = p`1 and
A3: f is being_S-Seq and
A4: p in LSeg(f,1) and
A5: h = <* p1,|[p1`1,(p1`2+p`2)/2]|,p *>;
A6: L~h = LSeg(p1,|[p1`1,(p1`2+p`2)/2]|) \/ LSeg(|[p1`1,(p1`2+p`2)/2]|,p)
  by A5,TOPREAL3:23;
  set q = f/.(1+1);
A7: len f >= 2 by A3,TOPREAL1:def 10;
  then
A8: LSeg(f,1) = LSeg(p1,q) by TOPREAL1:def 5;
A9: p1`2 <> p`2 by A1,A2,TOPREAL3:11;
  hence
A10: h is being_S-Seq & h/.1 = p1 & h/.len h = p by A2,A5, TOPREAL3
  :43;
  p1 in LSeg(p1,q) by RLTOPSP1:69;
  then
A11: LSeg(p1,p) c= LSeg(p1,q) by A4,A8,TOPREAL1:12;
A12: Seg len f = dom f by FINSEQ_1:def 3;
  thus L~h is_S-P_arc_joining p1,p by A10,Def1;
A13: LSeg(f,1) c= L~f by TOPREAL3:26;
  LSeg(p1,|[p1`1,(p1`2+p`2)/2]|) \/ LSeg(|[p1`1,(p1`2+p`2)/2]|,p) = LSeg(
  p1,p) by A2,A9,TOPREAL1:11,TOPREAL3:20;
  hence L~h c= L~f by A8,A11,A6,A13,XBOOLE_1:1;
  len f >= 1 by A7,XXREAL_0:2;
  then Seg 1 c= Seg len f by FINSEQ_1:7;
  then f|1 = f|Seg 1 & dom f /\ Seg 1 = Seg 1 by A12,FINSEQ_1:def 15 ,
  XBOOLE_1:28;
  then dom(f|1) = Seg 1 by RELAT_1:90;
  then len (f|1) = 1 by FINSEQ_1:def 3;
  then L~(f|1)={} by TOPREAL1:28;
  hence thesis by A2,A9,A6,TOPREAL1:11,TOPREAL3:20;
end;

theorem Th14:
  f is being_S-Seq & i in dom f & i+1 in dom f & i>1 & p in LSeg(f
,i) & p<>f/.i & h = (f|i)^<*p*> implies h is being_S-Seq & h/.1=f/.1 & h/.len h
=p & L~h is_S-P_arc_joining f/.1,p & L~h c= L~f & L~h = L~(f|i) \/ LSeg(f/.i,p)
proof
  set p1 = f/.1, q = f/.i;
  assume that
A1: f is being_S-Seq and
A2: i in dom f and
A3: i+1 in dom f and
A4: i>1 and
A5: p in LSeg(f,i) and
A6: p<>f/.i and
A7: h = (f|i)^<*p*>;
A8: f is one-to-one by A1,TOPREAL1:def 10;
  set v = f|i;
A9: v = f|Seg i by FINSEQ_1:def 15;
  then
A10: dom v=dom f /\ Seg i by RELAT_1:90;
A11: Seg len h = dom h by FINSEQ_1:def 3;
A12: f is unfolded by A1,TOPREAL1:def 10;
A13: f is special by A1,TOPREAL1:def 10;
A14: f is s.n.c. by A1,TOPREAL1:def 10;
  set q1 = f/.i, q2 = f/.(i+1);
A15: Seg len f = dom f by FINSEQ_1:def 3;
  then
A16: i+1<=len f by A3,FINSEQ_1:3;
  then
A17: p in LSeg(q1,q2) by A4,A5,TOPREAL1:def 5;
A18: LSeg(f,i) = LSeg(q1,q2) by A4,A16,TOPREAL1:def 5;
A19: LSeg(f,i) = LSeg(q2,q1) by A4,A16,TOPREAL1:def 5;
  i<>i+1;
  then
A20: q1 <> q2 by A2,A3,A8,PARTFUN2:17;
A21: q1 in LSeg(q1,q2) by RLTOPSP1:69;
A22: q1 = |[q1`1,q1`2]| & q2 = |[q2`1,q2`2]| by EUCLID:57;
A23: i<=len f by A2,A15,FINSEQ_1:3;
  then Seg i c= dom f by A15,FINSEQ_1:7;
  then
A24: dom v = Seg i by A10,XBOOLE_1:28;
  then
A25: len v = i by FINSEQ_1:def 3;
  then
A26: len h = i + len <*p*> by A7,FINSEQ_1:35
    .= i+1 by FINSEQ_1:56;
  then
A27: h/.len h = p by A7,A25,FINSEQ_4:82;
A28: i in dom v by A4,A24,FINSEQ_1:3;
  then
A29: h/.i=v/.i by A7,FINSEQ_4:83
    .= q1 by A28,FINSEQ_4:85;
  then
A30: LSeg(h,i) = LSeg(q1,p) by A4,A26,A27,TOPREAL1:def 5;
A31: 1+1<=i by A4,NAT_1:13;
  thus
A32: h is being_S-Seq
  proof
    now
      set Z = {m: 1<=m & m<=len h};
      given x,y such that
A33:  x in dom h and
A34:  y in dom h and
A35:  h.x = h.y and
A36:  x<>y;
      x in Z by A11,A33,FINSEQ_1:def 1;
      then consider k1 be Element of NAT such that
A37:  k1=x and
A38:  1<=k1 and
A39:  k1<=len h;
      y in Z by A11,A34,FINSEQ_1:def 1;
      then consider k2 be Element of NAT such that
A40:  k2=y and
A41:  1<=k2 and
A42:  k2<=len h;
A43:  h/.k1 = h.y by A33,A35,A37,PARTFUN1:def 8
        .= h/.k2 by A34,A40,PARTFUN1:def 8;
      k2<=len f by A26,A16,A42,XXREAL_0:2;
      then
A44:  k2 in dom f by A15,A41,FINSEQ_1:3;
      k1<=len f by A26,A16,A39,XXREAL_0:2;
      then
A45:  k1 in dom f by A15,A38,FINSEQ_1:3;
A46:  k1+(1+1) = k1+1+1;
A47:  k2+(1+1) = k2+1+1;
      now
        per cases by A26,A39,A42,XXREAL_0:1;
        suppose
          k1=i+1 & k2=i+1;
          hence contradiction by A36,A37,A40;
        end;
        suppose
A48:      k1=i+1 & k2<i+1;
          then
A49:      k2+1<=k1 by NAT_1:13;
          now
            per cases by A49,XXREAL_0:1;
            suppose
              k2 + 1 = k1;
              hence contradiction by A6,A7,A25,A29,A43,A48,FINSEQ_4:82;
            end;
            suppose
              k2 + 1 < k1;
              then
A50:          k2+1<=i by A48,NAT_1:13;
              now
                per cases by A50,XXREAL_0:1;
                suppose
A51:              k2+1=i;
                  then k2<=i by NAT_1:11;
                  then
A52:              k2 in dom v by A24,A41,FINSEQ_1:3;
                  then
A53:              h/.k2 = v/.k2 by A7,FINSEQ_4:83
                    .= f/.k2 by A52,FINSEQ_4:85;
                  k2+1 <= len f by A2,A15,A51,FINSEQ_1:3;
                  then
A54:              f/.k2 in LSeg(f,k2) by A41,TOPREAL1:27;
                  LSeg(f,k2) /\ LSeg(f,i) = {f/.i} by A12,A16,A41,A47,A51,
                  TOPREAL1:def 8;
                  then f/.k2 in {f/.i} by A5,A26,A27,A43,A48,A54,A53,
                  XBOOLE_0:def 4;
                  then
A55:              f/.k2 = f/.i by TARSKI:def 1;
                  k2<i by A51,NAT_1:13;
                  hence contradiction by A2,A8,A44,A55,PARTFUN2:17;
                end;
                suppose
A56:              k2+1<i;
                  then
A57:              k2+1<=len f by A23,XXREAL_0:2;
A58:              LSeg(f,k2) misses LSeg(f,i) by A14,A56,TOPREAL1:def 9;
                  k2<=k2+1 by NAT_1:11;
                  then k2<=i by A56,XXREAL_0:2;
                  then
A59:              k2 in dom v by A24,A41,FINSEQ_1:3;
                  then h/.k2 = v/.k2 by A7,FINSEQ_4:83
                    .= f/.k2 by A59,FINSEQ_4:85;
                  then p in LSeg(f,k2) by A26,A27,A41,A43,A48,A57,
                  TOPREAL1:27;
                  hence contradiction by A5,A58,XBOOLE_0:3;
                end;
              end;
              hence contradiction;
            end;
          end;
          hence contradiction;
        end;
        suppose
A60:      k1<i+1 & k2=i+1;
          then
A61:      k1+1<=k2 by NAT_1:13;
          now
            per cases by A61,XXREAL_0:1;
            suppose
              k1 + 1 = k2;
              hence contradiction by A6,A7,A25,A29,A43,A60,FINSEQ_4:82;
            end;
            suppose
              k1 + 1 < k2;
              then
A62:          k1+1<=i by A60,NAT_1:13;
              now
                per cases by A62,XXREAL_0:1;
                suppose
A63:              k1+1=i;
                  then k1<=i by NAT_1:11;
                  then
A64:              k1 in dom v by A24,A38,FINSEQ_1:3;
                  then
A65:              h/.k1 = v/.k1 by A7,FINSEQ_4:83
                    .= f/.k1 by A64,FINSEQ_4:85;
                  k1+1 <= len f by A2,A15,A63,FINSEQ_1:3;
                  then
A66:              f/.k1 in LSeg(f,k1) by A38,TOPREAL1:27;
                  LSeg(f,k1) /\ LSeg(f,i) = {f/.i} by A12,A16,A38,A46,A63,
                  TOPREAL1:def 8;
                  then f/.k1 in {f/.i} by A5,A26,A27,A43,A60,A66,A65,
                  XBOOLE_0:def 4;
                  then
A67:              f/.k1 = f/.i by TARSKI:def 1;
                  k1<i by A63,NAT_1:13;
                  hence contradiction by A2,A8,A45,A67,PARTFUN2:17;
                end;
                suppose
A68:              k1+1<i;
                  then
A69:              k1+1<=len f by A23,XXREAL_0:2;
A70:              LSeg(f,k1) misses LSeg(f,i) by A14,A68,TOPREAL1:def 9;
                  k1<=k1+1 by NAT_1:11;
                  then k1<=i by A68,XXREAL_0:2;
                  then
A71:              k1 in dom v by A24,A38,FINSEQ_1:3;
                  then h/.k1 = v/.k1 by A7,FINSEQ_4:83
                    .= f/.k1 by A71,FINSEQ_4:85;
                  then p in LSeg(f,k1) by A26,A27,A38,A43,A60,A69,
                  TOPREAL1:27;
                  hence contradiction by A5,A70,XBOOLE_0:3;
                end;
              end;
              hence contradiction;
            end;
          end;
          hence contradiction;
        end;
        suppose
A72:      k1<i+1 & k2<i+1;
          then k2<=i by NAT_1:13;
          then
A73:      k2 in dom v by A24,A41,FINSEQ_1:3;
          k1<=i by A72,NAT_1:13;
          then
A74:      k1 in dom v by A24,A38,FINSEQ_1:3;
          then f.k1 = v.k1 by A9,FUNCT_1:70
            .= h.k1 by A7,A74,FINSEQ_1:def 7
            .= v.k2 by A7,A35,A37,A40,A73,FINSEQ_1:def 7
            .= f.k2 by A9,A73,FUNCT_1:70;
          hence contradiction by A8,A36,A37,A40,A45,A44,FUNCT_1:def 8;
        end;
      end;
      hence contradiction;
    end;
    hence h is one-to-one by FUNCT_1:def 8;
    thus len h >= 2 by A4,A26,A31,XREAL_1:8;
    thus h is unfolded
    proof
      let j be Nat such that
A75:  1 <= j and
A76:  j + 2 <= len h;
A77:  1<=j+1 by NAT_1:11;
      j+1+1 = j+(1+1);
      then j+1<=i by A26,A76,XREAL_1:8;
      then
A78:  j+1 in dom v by A24,A77,FINSEQ_1:3;
      then
A79:  h/.(j+1) = v/.(j+1) by A7,FINSEQ_4:83
        .= f/.(j+1) by A78,FINSEQ_4:85;
      i+1+1 = i+(1+1);
      then len h <= i+2 by A26,NAT_1:11;
      then j+2 <= i+2 by A76,XXREAL_0:2;
      then j<=i by XREAL_1:8;
      then
A80:  j in dom v by A24,A75,FINSEQ_1:3;
      then
A81:  LSeg(h,j) = LSeg(v,j) by A7,A78,TOPREAL3:25
        .= LSeg(f,j) by A80,A78,TOPREAL3:24;
      j<=j+2 by NAT_1:11;
      then
A82:  1<=j+(1+1) by A75,XXREAL_0:2;
A83:  j+(1+1) = j+1+1;
      i+1 in Seg len f by A3,FINSEQ_1:def 3;
      then len h <= len f by A26,FINSEQ_1:3;
      then
A84:  j+2 <= len f by A76,XXREAL_0:2;
      then
A85:  LSeg(f,j) /\ LSeg(f,j+1) = {f/.(j+1)} by A12,A75, TOPREAL1:
      def 8;
      now
        per cases by A76,XXREAL_0:1;
        suppose
A86:      j+2 = len h;
          j +1 <= j+1+1 by NAT_1:11;
          then j+1 <= len h by A76,XXREAL_0:2;
          then
A87:      h/.(j+1) in LSeg(h,j) by A75,TOPREAL1:27;
          h/.(j+1) in LSeg(h,j+1) by A76,A77,A83,TOPREAL1:27;
          then h/.(j+1) in LSeg(h,j) /\ LSeg(h,j+1) by A87, XBOOLE_0:
          def 4;
          then
A88:      {h/.(j+1)} c= LSeg(h,j) /\ LSeg(h,j+1) by ZFMISC_1:37;
          LSeg(h,j) /\ LSeg(h,j+1) c= {h/.(j+1)} by A26,A18,A21,A17,A30,
          A85,A81,A79,A86,TOPREAL1:12,XBOOLE_1:26;
          hence thesis by A88,XBOOLE_0:def 10;
        end;
        suppose
          j+2 < len h;
          then j+(1+1)<=i by A26,NAT_1:13;
          then
A89:      j+1+1 in dom v by A24,A82,FINSEQ_1:3;
          then LSeg(h,j+1) = LSeg(v,j+1) by A7,A78,TOPREAL3:25
            .= LSeg(f,j+1) by A78,A89,TOPREAL3:24;
          hence thesis by A12,A75,A84,A81,A79,TOPREAL1:def 8;
        end;
      end;
      hence thesis;
    end;
    thus h is s.n.c.
    proof
      let n,m be Nat;
      assume
A90:  m>n+1;
      n<=n+1 by NAT_1:11;
      then
A91:  n<=m by A90,XXREAL_0:2;
A92:  1<=n+1 by NAT_1:11;
A93:  LSeg(f,n) misses LSeg(f,m) by A14,A90,TOPREAL1:def 9;
      now
        per cases by XXREAL_0:1;
        suppose
A94:      m+1<len h;
A95:      1<m by A90,A92,XXREAL_0:2;
          then
A96:      1<=m+1 by NAT_1:13;
          m+1<=i by A26,A94,NAT_1:13;
          then
A97:      m+1 in dom v by A24,A96,FINSEQ_1:3;
A98:      m<=i by A26,A94,XREAL_1:8;
          then
A99:      m in dom v by A24,A95,FINSEQ_1:3;
          then
A100:     LSeg(h,m)=LSeg(v,m) by A7,A97,TOPREAL3:25
            .= LSeg(f,m) by A99,A97,TOPREAL3:24;
          now
            per cases;
            suppose
              0<n;
              then
A101:         0+1<=n by NAT_1:13;
              n+1<=i by A90,A98,XXREAL_0:2;
              then
A102:         n+1 in dom v by A24,A92,FINSEQ_1:3;
              n<=i by A91,A98,XXREAL_0:2;
              then
A103:         n in dom v by A24,A101,FINSEQ_1:3;
              then LSeg(h,n)=LSeg(v,n) by A7,A102,TOPREAL3:25
                .= LSeg(f,n) by A103,A102,TOPREAL3:24;
              then LSeg(h,n) misses LSeg(h,m) by A14,A90,A100, TOPREAL1:
              def 9;
              hence LSeg(h,n) /\ LSeg(h,m) = {} by XBOOLE_0:def 7;
            end;
            suppose
              0=n;
              then LSeg(h,n)={} by TOPREAL1:def 5;
              hence LSeg(h,n) /\ LSeg(h,m) = {};
            end;
          end;
          hence LSeg(h,n) /\ LSeg(h,m) = {};
        end;
        suppose
A104:     m+1=len h;
A105:     LSeg(f,n) /\ LSeg(f,m) = {} by A93,XBOOLE_0:def 7;
          now
            per cases;
            suppose
              0<n;
              then 0+1<=n by NAT_1:13;
              then
A106:         n in dom v by A24,A26,A91,A104,FINSEQ_1:3;
A107:         n+1 in dom v by A24,A26,A90,A92,A104,FINSEQ_1:3;
              then LSeg(h,n)=LSeg(v,n) by A7,A106,TOPREAL3:25
                .= LSeg(f,n) by A106,A107,TOPREAL3:24;
              hence {} = LSeg(h,m) /\ (LSeg(f,m) /\ LSeg(h,n)) by A105
                .= LSeg(h,m) /\ LSeg(f,m) /\ LSeg(h,n) by XBOOLE_1:16
                .= LSeg(h,n) /\ LSeg(h,m) by A26,A18,A21,A17,A30,A104,
              TOPREAL1:12,XBOOLE_1:28;
            end;
            suppose
              0=n;
              then LSeg(h,n)={} by TOPREAL1:def 5;
              hence LSeg(h,n) /\ LSeg(h,m) = {};
            end;
          end;
          hence LSeg(h,n) /\ LSeg(h,m) = {};
        end;
        suppose
          m+1>len h;
          then LSeg(h,m) = {} by TOPREAL1:def 5;
          hence LSeg(h,n) /\ LSeg(h,m) = {};
        end;
      end;
      hence LSeg(h,n) /\ LSeg(h,m) = {};
    end;
    let n be Nat such that
A108: 1 <= n and
A109: n + 1 <= len h;
    set p3 = h/.n, p4 = h/.(n+1);
    now
      per cases by A109,XXREAL_0:1;
      suppose
A110:   n+1 = len h;
A111:   i in dom v by A4,A24,FINSEQ_1:3;
        then
A112:   p3 = v/.i by A7,A26,A110,FINSEQ_4:83
          .= q1 by A111,FINSEQ_4:85;
        now
          per cases by A4,A13,A16,TOPREAL1:def 7;
          suppose
A113:       q1`1 = q2`1;
            then
A114:       q1`2<> q2`2 by A20,TOPREAL3:11;
            now
              per cases by A114,XXREAL_0:1;
              suppose
                q1`2<q2`2;
                then p in {p11: p11`1 = q1`1 & q1`2 <= p11`2 & p11`2<=q2`2}
                by A5,A19,A22,A113,TOPREAL3:15;
                then ex p11 st p=p11 & p11`1 = q1`1 & q1`2 <= p11`2 & p11`2<=
                q2`2;
                hence thesis by A7,A25,A26,A110,A112,FINSEQ_4:82;
              end;
              suppose
                q2`2<q1`2;
                then p in {p22: p22`1 = q1`1 & q2`2 <= p22`2 & p22`2<=q1`2}
                by A5,A19,A22,A113,TOPREAL3:15;
                then ex p11 st p=p11 & p11`1 = q1`1 & q2`2 <= p11`2 & p11`2<=
                q1`2;
                hence thesis by A7,A25,A26,A110,A112,FINSEQ_4:82;
              end;
            end;
            hence thesis;
          end;
          suppose
A115:       q1`2 = q2`2;
            then
A116:       q1`1<> q2`1 by A20,TOPREAL3:11;
            now
              per cases by A116,XXREAL_0:1;
              suppose
                q1`1<q2`1;
                then p in {p11: p11`2 = q1`2 & q1`1 <= p11`1 & p11`1<=q2`1}
                by A5,A19,A22,A115,TOPREAL3:16;
                then ex p11 st p=p11 & p11`2 = q1`2 & q1`1 <= p11`1 & p11`1<=
                q2`1;
                hence thesis by A7,A25,A26,A110,A112,FINSEQ_4:82;
              end;
              suppose
                q2`1<q1`1;
                then p in {p22: p22`2 = q1`2 & q2`1 <= p22`1 & p22`1<=q1`1}
                by A5,A19,A22,A115,TOPREAL3:16;
                then ex p11 st p=p11 & p11`2 = q1`2 & q2`1 <= p11`1 & p11`1<=
                q1`1;
                hence thesis by A7,A25,A26,A110,A112,FINSEQ_4:82;
              end;
            end;
            hence thesis;
          end;
        end;
        hence thesis;
      end;
      suppose
A117:   n+1 < len h;
A118:   1<=n+1 by A108,NAT_1:13;
        n+1<=i by A26,A117,NAT_1:13;
        then
A119:   n+1 in dom v by A24,A118,FINSEQ_1:3;
        then h/.(n+1)=v/.(n+1) by A7,FINSEQ_4:83;
        then
A120:   p4=f/.(n+1) by A119,FINSEQ_4:85;
        n<=i by A26,A117,XREAL_1:8;
        then
A121:   n in dom v by A24,A108,FINSEQ_1:3;
        then h/.n=v/.n by A7,FINSEQ_4:83;
        then
A122:   p3=f/.n by A121,FINSEQ_4:85;
        n+1 <= len f by A26,A16,A109,XXREAL_0:2;
        hence thesis by A13,A108,A122,A120,TOPREAL1:def 7;
      end;
    end;
    hence thesis;
  end;
A123: 1 in dom v by A4,A24,FINSEQ_1:3;
  then h/.1 = v/.1 by A7,FINSEQ_4:83
    .= p1 by A123,FINSEQ_4:85;
  hence
A124: h/.1=p1 & h/.len h=p by A7,A25,A26,FINSEQ_4:82;
  set Mf = {LSeg(f,j): 1<=j & j+1<=len f}, Mv = {LSeg(v,n): 1<=n & n+1<=len v}
  , Mh = {LSeg(h,m): 1<=m & m+1<=len h};
A125: Seg len v = dom v by FINSEQ_1:def 3;
  thus L~h is_S-P_arc_joining p1,p by A32,A124,Def1;
A126: now
    let x;
    assume x in L~h;
    then consider X be set such that
A127: x in X and
A128: X in Mh by TARSKI:def 4;
    consider k such that
A129: X=LSeg(h,k) and
A130: 1<=k and
A131: k+1<=len h by A128;
A132: k+1<= len f by A26,A16,A131,XXREAL_0:2;
    now
      per cases by A131,XXREAL_0:1;
      suppose
A133:   k+1 = len h;
        then
A134:   LSeg(f,k) in Mf by A26,A16,A130;
        LSeg(h,i) c= LSeg(f,i) by A5,A18,A21,A30,TOPREAL1:12;
        hence x in L~f by A26,A127,A129,A133,A134,TARSKI:def 4;
        thus x in L~v \/ LSeg(q1,p) by A26,A30,A127,A129,A133,XBOOLE_0:def 3;
      end;
      suppose
A135:   k+1 < len h;
        then
A136:   k+1<=len v by A25,A26,NAT_1:13;
A137:   k+1<=i by A26,A135,NAT_1:13;
        k<=k+1 by NAT_1:11;
        then k<=i by A137,XXREAL_0:2;
        then
A138:   k in dom v by A24,A130,FINSEQ_1:3;
        1<=k+1 by A130,NAT_1:13;
        then
A139:   k+1 in dom v by A24,A137,FINSEQ_1:3;
        then
A140:   X=LSeg(v,k) by A7,A129,A138,TOPREAL3:25
          .= LSeg(f,k) by A139,A138,TOPREAL3:24;
        then X in Mf by A130,A132;
        hence x in L~f by A127,TARSKI:def 4;
        X=LSeg(v,k) by A139,A138,A140,TOPREAL3:24;
        then X in Mv by A130,A136;
        then x in union Mv by A127,TARSKI:def 4;
        hence x in L~v \/ LSeg(q1,p) by XBOOLE_0:def 3;
      end;
    end;
    hence x in L~f & x in L~v \/ LSeg(q1,p);
  end;
  thus L~h c= L~f
  proof
    let x;
    assume x in L~h;
    hence thesis by A126;
  end;
A141: i<=i+1 by NAT_1:11;
  thus L~h c= L~(f|i) \/ LSeg(q,p)
  proof
    let x;
    assume x in L~h;
    hence thesis by A126;
  end;
  let x such that
A142: x in L~v \/ LSeg(q,p);
  now
    per cases by A142,XBOOLE_0:def 3;
    suppose
      x in L~v;
      then consider X be set such that
A143: x in X and
A144: X in Mv by TARSKI:def 4;
      consider k such that
A145: X=LSeg(v,k) and
A146: 1<=k and
A147: k+1<=len v by A144;
A148: k+1<=len h by A25,A26,A141,A147,XXREAL_0:2;
      k<=k+1 by NAT_1:11;
      then k<=len v by A147,XXREAL_0:2;
      then
A149: k in Seg len v by A146,FINSEQ_1:3;
      1<=k+1 by NAT_1:11;
      then k+1 in Seg len v by A147,FINSEQ_1:3;
      then X=LSeg(h,k) by A7,A125,A145,A149,TOPREAL3:25;
      then X in Mh by A146,A148;
      hence thesis by A143,TARSKI:def 4;
    end;
    suppose
A150: x in LSeg(q,p);
      LSeg(h,i) in Mh by A4,A26;
      hence thesis by A30,A150,TARSKI:def 4;
    end;
  end;
  hence thesis;
end;

theorem Th15:
  f/.2<>f/.1 & f is being_S-Seq & (f/.2)`2 = (f/.1)`2 & h = <* f/.
1,|[((f/.1)`1+(f/.2)`1)/2,(f/.1)`2]|,f/.2 *> implies h is being_S-Seq & h/.1=f
/.1 & h/.len h=f/.2 & L~h is_S-P_arc_joining f/.1,f/.2 & L~h c= L~f & L~h = L~(
  f|1) \/ LSeg(f/.1,f/.2) & L~h = L~(f|2) \/ LSeg(f/.2,f/.2)
proof
  set p1 = f/.1, p = f/.2;
  assume that
A1: p<>p1 and
A2: f is being_S-Seq and
A3: p`2 = p1`2 and
A4: h = <* p1,|[(p1`1+p`1)/2,p1`2]|,p *>;
A5: p1`1 <> p`1 by A1,A3,TOPREAL3:11;
  hence
A6: h is being_S-Seq & h/.1 = p1 & h/.len h = p by A3,A4,TOPREAL3:
  44;
A7: LSeg(p1,|[(p1`1 + p`1)/2,p1`2]|) \/ LSeg(|[(p1`1 + p`1)/2,p1`2]|,p) =
  LSeg(p1,p) by A3,A5,TOPREAL1:11,TOPREAL3:19;
  set M = {LSeg(f|2,k): 1<=k & k+1<=len(f|2)};
A8: Seg len f = dom f by FINSEQ_1:def 3;
A9: L~h=LSeg(p1,|[(p1`1 + p`1)/2,p1`2]|) \/ LSeg(|[(p1`1 + p`1)/2,p1`2]|,p)
  by A4,TOPREAL3:23;
A10: len f >= 2 by A2,TOPREAL1:def 10;
  then
A11: 1+1 in Seg len f by FINSEQ_1:3;
  then
A12: LSeg(f,1) = LSeg(p1,p) by A10,TOPREAL1:def 5;
  Seg 2 c= Seg len f by A10,FINSEQ_1:7;
  then f|2 = f|Seg 2 & dom f /\ Seg 2 = Seg 2 by A8,FINSEQ_1:def 15 ,
  XBOOLE_1:28;
  then
A13: dom(f|2) = Seg 2 by RELAT_1:90;
  then
A14: 1 in dom(f|2) & 2 in dom(f|2) by FINSEQ_1:3;
  thus
A15: L~h is_S-P_arc_joining p1,p by A6,Def1;
A16: L~(f|2) \/ LSeg(p,p) c= L~h
  proof
    let x such that
A17: x in L~(f|2) \/ LSeg(p,p);
    now
      per cases by A17,XBOOLE_0:def 3;
      suppose
        x in L~(f|2);
        then consider X be set such that
A18:    x in X and
A19:    X in M by TARSKI:def 4;
        consider m such that
A20:    X=LSeg(f|2,m) and
A21:    1<=m and
A22:    m+1<=len(f|2) by A19;
        len(f|2) - 1 =1+1 - 1 by A13,FINSEQ_1:def 3
          .= 1;
        then m+1-1 <= 1 by A22,XREAL_1:11;
        then m=1 by A21,XXREAL_0:1;
        hence thesis by A11,A12,A9,A7,A14,A18,A20,TOPREAL3:24;
      end;
      suppose
        x in LSeg(p,p);
        then
A23:    x in {p} by RLTOPSP1:71;
        p in L~h by A15,Th4;
        hence thesis by A23,TARSKI:def 1;
      end;
    end;
    hence thesis;
  end;
  LSeg(f,1) c= L~f by TOPREAL3:26;
  hence L~h c= L~f by A4,A12,A7,TOPREAL3:23;
  len f >= 1 by A10,XXREAL_0:2;
  then Seg 1 c= Seg len f by FINSEQ_1:7;
  then f|1 = f|Seg 1 & dom f /\ Seg 1 = Seg 1 by A8,FINSEQ_1:def 15 ,
  XBOOLE_1:28;
  then dom(f|1) = Seg 1 by RELAT_1:90;
  then len (f|1) = 1 by FINSEQ_1:def 3;
  then L~(f|1)={} by TOPREAL1:28;
  hence L~h = L~(f|1) \/ LSeg(p1,p) by A3,A5,A9,TOPREAL1:11,TOPREAL3:19;
A24: L~(f|2) c=L~(f|2) \/ LSeg(p,p) by XBOOLE_1:7;
A25: 1+1<=len(f|2) by A13,FINSEQ_1:def 3;
  LSeg(f|2,1) = LSeg(p1,p) by A11,A12,A14,TOPREAL3:24;
  then LSeg(p1,p) in M by A25;
  then L~h c=L~(f|2) by A9,A7,ZFMISC_1:92;
  then L~h c= L~(f|2) \/ LSeg(p,p) by A24,XBOOLE_1:1;
  hence thesis by A16,XBOOLE_0:def 10;
end;

theorem Th16:
  f/.2<>f/.1 & f is being_S-Seq & (f/.2)`1 = (f/.1)`1 & h = <* f/.
1,|[(f/.1)`1,((f/.1)`2+(f/.2)`2)/2]|,f/.2 *> implies h is being_S-Seq & h/.1=f
/.1 & h/.len h=f/.2 & L~h is_S-P_arc_joining f/.1,f/.2 & L~h c= L~f & L~h = L~(
  f|1) \/ LSeg(f/.1,f/.2) & L~h = L~(f|2) \/ LSeg(f/.2,f/.2)
proof
  set p1 = f/.1, p = f/.2;
  assume that
A1: p<>p1 and
A2: f is being_S-Seq and
A3: p`1 = p1`1 and
A4: h = <* p1,|[p1`1,(p1`2+p`2)/2]|,p *>;
A5: p1`2 <> p`2 by A1,A3,TOPREAL3:11;
  hence
A6: h is being_S-Seq & h/.1 = p1 & h/.len h = p by A3,A4,TOPREAL3:43;
A7: LSeg(p1,|[p1`1,(p1`2+p`2)/2]|) \/ LSeg(|[p1`1,(p1`2+p`2)/2]|,p) = LSeg(
  p1,p) by A3,A5,TOPREAL1:11,TOPREAL3:20;
  set M = {LSeg(f|2,k): 1<=k & k+1<=len(f|2)};
A8: Seg len f = dom f by FINSEQ_1:def 3;
A9: L~h = LSeg(p1,|[p1`1,(p1`2+p`2)/2]|) \/ LSeg(|[p1`1,(p1`2+p`2)/2]|,p)
  by A4,TOPREAL3:23;
A10: len f >= 1+1 by A2,TOPREAL1:def 10;
  then
A11: LSeg(f,1) = LSeg(p1,p) by TOPREAL1:def 5;
  Seg 2 c= Seg len f by A10,FINSEQ_1:7;
  then f|2 = f|Seg 2 & dom f /\ Seg 2 = Seg 2 by A8,FINSEQ_1:def 15 ,
  XBOOLE_1:28;
  then
A12: dom(f|2) = Seg 2 by RELAT_1:90;
  then
A13: 1 in dom(f|2) & 2 in dom(f|2) by FINSEQ_1:3;
  thus
A14: L~h is_S-P_arc_joining p1,p by A6,Def1;
A15: L~(f|2) \/ LSeg(p,p) c= L~h
  proof
    let x such that
A16: x in L~(f|2) \/ LSeg(p,p);
    now
      per cases by A16,XBOOLE_0:def 3;
      suppose
        x in L~(f|2);
        then consider X be set such that
A17:    x in X and
A18:    X in M by TARSKI:def 4;
        consider m such that
A19:    X=LSeg(f|2,m) and
A20:    1<=m and
A21:    m+1<=len(f|2) by A18;
        len(f|2) - 1 =1+1 - 1 by A12,FINSEQ_1:def 3
          .= 1;
        then m+1-1 <= 1 by A21,XREAL_1:11;
        then m=1 by A20,XXREAL_0:1;
        hence thesis by A10,A11,A9,A7,A13,A17,A19,TOPREAL3:24;
      end;
      suppose
        x in LSeg(p,p);
        then
A22:    x in {p} by RLTOPSP1:71;
        p in L~h by A14,Th4;
        hence thesis by A22,TARSKI:def 1;
      end;
    end;
    hence thesis;
  end;
  LSeg(f,1) c= L~f by TOPREAL3:26;
  hence L~h c= L~f by A4,A11,A7,TOPREAL3:23;
  len f >= 1 by A10,XXREAL_0:2;
  then Seg 1 c= Seg len f by FINSEQ_1:7;
  then f|1 = f|Seg 1 & dom f /\ Seg 1 = Seg 1 by A8,FINSEQ_1:def 15 ,
  XBOOLE_1:28;
  then dom(f|1) = Seg 1 by RELAT_1:90;
  then len (f|1) = 1 by FINSEQ_1:def 3;
  then L~(f|1)={} by TOPREAL1:28;
  hence L~h = L~(f|1) \/ LSeg(p1,p) by A3,A5,A9,TOPREAL1:11,TOPREAL3:20;
A23: L~(f|2) c=L~(f|2) \/ LSeg(p,p) by XBOOLE_1:7;
A24: 1+1<=len(f|2) by A12,FINSEQ_1:def 3;
  LSeg(f|2,1) = LSeg(p1,p) by A10,A11,A13,TOPREAL3:24;
  then LSeg(p1,p) in M by A24;
  then L~h c=L~(f|2) by A9,A7,ZFMISC_1:92;
  then L~h c= L~(f|2) \/ LSeg(p,p) by A23,XBOOLE_1:1;
  hence thesis by A15,XBOOLE_0:def 10;
end;

theorem Th17:
  f is being_S-Seq & i>2 & i in dom f & h = f|i implies h is
being_S-Seq & h/.1=f/.1 & h/.len h=f/.i & L~h is_S-P_arc_joining f/.1,f/.i & L~
  h c= L~f & L~h = L~(f|i) \/ LSeg(f/.i,f/.i)
proof
  assume that
A1: f is being_S-Seq & i>2 and
A2: i in dom f and
A3: h = f|i;
A4: Seg len f = dom f by FINSEQ_1:def 3;
  then i<=len f by A2,FINSEQ_1:3;
  then
A5: Seg i c= Seg len f by FINSEQ_1:7;
  h = f|Seg i by A3,FINSEQ_1:def 15;
  then dom h=Seg(len f) /\ Seg i by A4,RELAT_1:90;
  then
A6: dom h = Seg i by A5,XBOOLE_1:28;
  1<=i by A2,A4,FINSEQ_1:3;
  then
A7: 1 in dom h & i in dom h by A6,FINSEQ_1:3;
  len h = i by A6,FINSEQ_1:def 3;
  hence h is being_S-Seq & h/.1=f/.1 & h/.len h = f/.i by A1,A2,A3,A7,
  FINSEQ_4:85,TOPREAL3:40;
  hence L~h is_S-P_arc_joining f/.1,f/.i & L~h c= L~f by A3,Def1,
  TOPREAL3:27;
  then f/.i in L~(f|i) by A3,Th4;
  then LSeg(f/.i,f/.i) = {f/.i} & {f/.i} c= L~(f|i) by RLTOPSP1:71,
  ZFMISC_1:37;
  hence thesis by A3,XBOOLE_1:12;
end;

theorem Th18:
  p<>f/.1 & f is being_S-Seq & p in LSeg(f,n) implies ex h st h is
being_S-Seq & h/.1=f/.1 & h/.len h = p & L~h is_S-P_arc_joining f/.1,p & L~h c=
  L~f & L~h = L~(f|n) \/ LSeg(f/.n,p)
proof
  set p1 = f/.1, q = f/.n;
  assume that
A1: p<>p1 and
A2: f is being_S-Seq and
A3: p in LSeg(f,n);
A4: f is special by A2,TOPREAL1:def 10;
A5: n <= n+1 by NAT_1:11;
A6: now
    assume
A7: not n in dom f or not n+1 in dom f;
    now
      per cases by A7;
      suppose
        not n in dom f;
        then not(1 <= n & n <= len f) by FINSEQ_3:27;
        then not(1 <= n & n+1 <= len f) by A5,XXREAL_0:2;
        hence contradiction by A3,TOPREAL1:def 5;
      end;
      suppose
        not n+1 in dom f;
        then not(1 <= n+1 & n+1<= len f) by FINSEQ_3:27;
        hence contradiction by A3,NAT_1:11,TOPREAL1:def 5;
      end;
    end;
    hence contradiction;
  end;
A8: f is one-to-one by A2,TOPREAL1:def 10;
A9: Seg len f=dom f by FINSEQ_1:def 3;
  then
A10: 1<=n by A6,FINSEQ_1:3;
A11: n+1<=len f by A6,A9,FINSEQ_1:3;
A12: n<=len f by A6,A9,FINSEQ_1:3;
  now
    per cases;
    case
      f/.n = p & f/.(n+1) = p;
      then n+1 = n by A6,A8,PARTFUN2:17;
      hence contradiction;
    end;
    case
A13:  f/.n = p & f/.(n+1) <> p;
      then 1<n by A1,A10,XXREAL_0:1;
      then
A14:  1+1<=n by NAT_1:13;
      now
        per cases by A14,XXREAL_0:1;
        suppose
A15:      n=1+1;
          now
            per cases by A4,A12,A13,A15,TOPREAL1:def 7;
            suppose
A16:          p1`1=p`1;
              take h = <* p1,|[p1`1,(p1`2+p`2)/2]|,p *>;
              thus h is being_S-Seq & h/.1=p1 & h/.len h = p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h = L~(f|n) \/ LSeg(q,p) by A1,A2,A13,
              A15,A16,Th16;
            end;
            suppose
A17:          p1`2=p`2;
              take h = <* p1,|[(p1`1+p`1)/2,p1`2]|,p *>;
              thus h is being_S-Seq & h/.1=p1 & h/.len h = p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h = L~(f|n) \/ LSeg(q,p) by A1,A2,A13,
              A15,A17,Th15;
            end;
          end;
          hence thesis;
        end;
        suppose
A18:      n>2;
          take h=f|n;
          thus h is being_S-Seq & h/.1=p1 & h/.len h = p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h = L~(f|n) \/ LSeg(q,p) by A2,A6,A13,
          A18,Th17;
        end;
      end;
      hence thesis;
    end;
    case
A19:  f/.n <> p & f/.(n+1) = p;
      now
        per cases by A10,XXREAL_0:1;
        suppose
A20:      n=1;
          now
            per cases by A4,A11,A19,A20,TOPREAL1:def 7;
            suppose
A21:          p1`1= p`1;
              take h=<* p1,|[p1`1,(p1`2+p`2)/2]|,p *>;
              thus h is being_S-Seq & h/.1=p1 & h/.len h = p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h = L~(f|n) \/ LSeg(q,p) by A2,A19,A20
              ,A21,Th16;
            end;
            suppose
A22:          p1`2=p`2;
              take h=<* p1,|[(p1`1+p`1)/2,p1`2]|,p *>;
              thus h is being_S-Seq & h/.1=p1 & h/.len h = p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h = L~(f|n) \/ LSeg(q,p) by A2,A19,A20
              ,A22,Th15;
            end;
          end;
          hence thesis;
        end;
        suppose
A23:      1<n;
          take h=f|(n+1);
          1+1<n+1 by A23,XREAL_1:8;
          hence h is being_S-Seq & h/.1=p1 & h/.len h=p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h = L~(f|n) \/ LSeg(q,p) by A2,A6,A19,
          Th17,TOPREAL3:45;
        end;
      end;
      hence thesis;
    end;
    case
A24:  f/.n <> p & f/.(n+1) <> p;
      now
        per cases by A10,XXREAL_0:1;
        suppose
A25:      n=1;
          set q1 = f/.(1+1);
A26:      len f >= 1+1 by A2,TOPREAL1:def 10;
          then
A27:      LSeg(f,n) = LSeg(p1,q1) by A25,TOPREAL1:def 5;
          now
            per cases by A4,A26,TOPREAL1:def 7;
            suppose
A28:          p1`1=q1`1;
              take h = <* p1,|[p1`1,(p1`2+p`2)/2]|,p *>;
              p1`1 <= p`1 & p`1 <= q1`1 by A3,A27,A28,TOPREAL1:9;
              then p1`1 = p`1 by A28,XXREAL_0:1;
              hence h is being_S-Seq & h/.1=p1 & h/.len h = p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h = L~(f|n) \/ LSeg(q,p) by A1,A2,A3,
              A25,Th13;
            end;
            suppose
A29:          p1`2=q1`2;
              take h = <* p1,|[(p1`1+p`1)/2,p1`2]|,p *>;
              p1`2 <= p`2 & p`2 <= q1`2 by A3,A27,A29,TOPREAL1:10;
              then p1`2 = p`2 by A29,XXREAL_0:1;
              hence h is being_S-Seq & h/.1=p1 & h/.len h = p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h = L~(f|n) \/ LSeg(q,p) by A1,A2,A3,
              A25,Th12;
            end;
          end;
          hence thesis;
        end;
        suppose
A30:      1<n;
          take h = (f|n)^<*p*>;
          thus h is being_S-Seq & h/.1 = p1 & h/.len h = p & L~h
is_S-P_arc_joining p1,p & L~h c= L~f & L~h =L~(f|n) \/ LSeg(q,p) by A2,A3,A6,
          A24,A30,Th14;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th19:
  p<>f/.1 & f is being_S-Seq & p in L~f implies ex h st h is
being_S-Seq & h/.1=f/.1 & h/.len h = p & L~h is_S-P_arc_joining f/.1,p & L~h c=
  L~f
proof
  set M = {LSeg(f,i): 1<=i & i+1<=len f}, p1 = f/.1;
  assume that
A1: p<>p1 & f is being_S-Seq and
A2: p in L~f;
  consider X be set such that
A3: p in X and
A4: X in M by A2,TARSKI:def 4;
  consider n such that
A5: X=LSeg(f,n) and
  1<=n and
  n+1<=len f by A4;
  consider h such that
A6: h is being_S-Seq & h/.1=p1 & h/.len h = p & L~h is_S-P_arc_joining
  p1,p & L~h c= L~f and
  L~h = L~(f|n) \/ LSeg(f/.n,p) by A1,A3,A5,Th18;
  take h;
  thus thesis by A6;
end;

theorem Th20:
  ( p`1=(f/.len f)`1 & p`2<>(f/.len f)`2 or p`1<>(f/.len f)`1 & p
`2=(f/.len f)`2 ) & f/.len f in Ball(u,r) & p in Ball(u,r) & f is being_S-Seq &
LSeg(f/.len f,p) /\ L~f = {f/.len f} & h=f^<*p*> implies h is being_S-Seq & L~h
  is_S-P_arc_joining f/.1,p & L~h c= L~f \/ Ball(u,r)
proof
  set p1 = f/.1, p2 = f/.len f;
  assume
A1: p`1=p2`1 & p`2<>p2`2 or p`1<>p2`1 & p`2=p2`2;
  assume that
A2: p2 in Ball(u,r) & p in Ball(u,r) and
A3: f is being_S-Seq and
A4: LSeg(p2,p) /\ L~f = {p2} and
A5: h=f^<*p*>;
A6: not p in L~f by A1,A4,TOPREAL3:47;
A7: f is one-to-one by A3,TOPREAL1:def 10;
A8: f is unfolded by A3,TOPREAL1:def 10;
A9: f is one-to-one by A3,TOPREAL1:def 10;
A10: f is s.n.c. by A3,TOPREAL1:def 10;
A11: Seg len h = dom h by FINSEQ_1:def 3;
  set Mf = {LSeg(f,i): 1<=i & i+1<=len f}, Mh = {LSeg(h,m): 1<=m & m+1<=len h};
A12: Seg len f = dom f by FINSEQ_1:def 3;
A13: 2<=len f by A3,TOPREAL1:def 10;
  then
A14: 1<=len f by XXREAL_0:2;
  then
A15: len f in dom f by A12,FINSEQ_1:3;
  then
A16: h/.len f = p2 by A5,FINSEQ_4:83;
A17: len h = len f + len <*p*> by A5,FINSEQ_1:35
    .= len f + 1 by FINSEQ_1:56;
  then
A18: h/.len h = p by A5,FINSEQ_4:82;
  then
A19: LSeg(h,len f) = LSeg(p2,p) by A17,A14,A16,TOPREAL1:def 5;
A20: f is special by A3,TOPREAL1:def 10;
  thus
A21: h is being_S-Seq
  proof
    now
      set Z = {m: 1<=m & m<=len h};
      given x,y such that
A22:  x in dom h and
A23:  y in dom h and
A24:  h.x = h.y and
A25:  x<>y;
      y in Z by A11,A23,FINSEQ_1:def 1;
      then consider k2 be Element of NAT such that
A26:  k2=y and
A27:  1<=k2 and
A28:  k2<=len h;
      x in Z by A11,A22,FINSEQ_1:def 1;
      then consider k1 be Element of NAT such that
A29:  k1=x and
A30:  1<=k1 and
A31:  k1<=len h;
A32:  h/.k1 = h.y by A22,A24,A29,PARTFUN1:def 8
        .= h/.k2 by A23,A26,PARTFUN1:def 8;
      now
        per cases by A17,A31,A28,XXREAL_0:1;
        suppose
          k1=len f+1 & k2=len f+1;
          hence contradiction by A25,A29,A26;
        end;
        suppose
A33:      k1=len f+1 & k2<len f+1;
          then
A34:      k2+1<=k1 by NAT_1:13;
          now
            per cases by A34,XXREAL_0:1;
            suppose
              k2 + 1 = k1;
              hence contradiction by A1,A5,A16,A32,A33,FINSEQ_4:82;
            end;
            suppose
              k2 + 1 < k1;
              then
A35:          k2<len f+1-1 by A33,XREAL_1:22;
              then k2 in dom f by A12,A27,FINSEQ_1:3;
              then h/.k2 = f/.k2 by A5,FINSEQ_4:83;
              hence contradiction by A5,A13,A6,A27,A32,A33,A35,FINSEQ_4:82
              ,TOPREAL3:46;
            end;
          end;
          hence contradiction;
        end;
        suppose
A36:      k1<len f+1 & k2=len f+1;
          then
A37:      k1+1<=k2 by NAT_1:13;
          now
            per cases by A37,XXREAL_0:1;
            suppose
              k1 + 1 = k2;
              hence contradiction by A1,A5,A16,A32,A36,FINSEQ_4:82;
            end;
            suppose
              k1 + 1 < k2;
              then
A38:          k1<len f+1-1 by A36,XREAL_1:22;
              then k1 in dom f by A12,A30,FINSEQ_1:3;
              then h/.k1 = f/.k1 by A5,FINSEQ_4:83;
              hence contradiction by A5,A13,A6,A30,A32,A36,A38,FINSEQ_4:82
              ,TOPREAL3:46;
            end;
          end;
          hence contradiction;
        end;
        suppose
A39:      k1<len f+1 & k2<len f+1;
          then k2<=len f by NAT_1:13;
          then
A40:      k2 in dom f by A12,A27,FINSEQ_1:3;
          k1<=len f by A39,NAT_1:13;
          then
A41:      k1 in dom f by A12,A30,FINSEQ_1:3;
          then f.k1 = h.k1 by A5,FINSEQ_1:def 7
            .= f.k2 by A5,A24,A29,A26,A40,FINSEQ_1:def 7;
          hence contradiction by A9,A25,A29,A26,A41,A40,FUNCT_1:def 8;
        end;
      end;
      hence contradiction;
    end;
    hence h is one-to-one by FUNCT_1:def 8;
    2+1<=len f +1 by A13,XREAL_1:8;
    hence len h >= 2 by A17,XXREAL_0:2;
    thus h is unfolded
    proof
      let j be Nat such that
A42:  1 <= j and
A43:  j + 2 <= len h;
A44:  j+(1+1) = j+1+1;
      j+1 <= j+2 by XREAL_1:8;
      then
A45:  j+1 <= len h by A43,XXREAL_0:2;
      now
        per cases by A43,XXREAL_0:1;
        suppose
A46:      j+2 = len h;
          then
A47:      j+(1+1 - 1)= len f by A17;
          then j<=len f by NAT_1:13;
          then j in dom f by A12,A42,FINSEQ_1:3;
          then
A48:      LSeg(h,j) = LSeg(f,j) by A5,A15,A47,TOPREAL3:25;
          j in NAT by ORDINAL1:def 13;
          then LSeg(h,j) in Mf by A42,A47,A48;
          then
A49:      LSeg(h,j) = LSeg(h,j) /\ L~f by XBOOLE_1:28,ZFMISC_1:92;
          h/.(j+1) in LSeg(h,j) by A42,A45,TOPREAL1:27;
          then
A50:      {h/.(j+1)} c= LSeg(h,j) by ZFMISC_1:37;
          LSeg(h,j+1) = LSeg(p2,p) by A17,A14,A18,A16,A46,TOPREAL1:def 5;
          hence
          LSeg(h,j) /\ LSeg(h,j+1) = LSeg(h,j) /\ {h/.(j+1)} by A4,A17,A16
          ,A46,A49,XBOOLE_1:16
            .= {h/.(j+1)} by A50,XBOOLE_1:28;
        end;
        suppose
          j+2 < len h;
          then
A51:      j+(2+1)-1<=len f by A17,NAT_1:13;
          then
A52:      LSeg(f,j) /\ LSeg(f,j+1) = {f/.(j+1)} by A8,A42,A43,
          TOPREAL1:def 8;
          1<=j+2 by A44,NAT_1:11;
          then
A53:      j+1+1 in dom f by A12,A51,FINSEQ_1:3;
          j+1<=j+2 by A44,NAT_1:11;
          then
A54:      j+1<=len f by A51,XXREAL_0:2;
          1<=j+1 by NAT_1:11;
          then
A55:      j+1 in dom f by A12,A54,FINSEQ_1:3;
          then
A56:      h/.(j+1) = f/.(j+1) by A5,FINSEQ_4:83;
          j<=j+1 by NAT_1:11;
          then j<=len f by A54,XXREAL_0:2;
          then j in dom f by A12,A42,FINSEQ_1:3;
          then LSeg(h,j) = LSeg(f,j) by A5,A55,TOPREAL3:25;
          hence thesis by A5,A52,A55,A53,A56,TOPREAL3:25;
        end;
      end;
      hence thesis;
    end;
    thus h is s.n.c.
    proof
      let n,m be Nat such that
A57:  m>n+1;
      n<=n+1 by NAT_1:11;
      then
A58:  n<=m by A57,XXREAL_0:2;
A59:  n+1+1=n+(1+1);
A60:  1<=n+1 by NAT_1:11;
      LSeg(f,n) misses LSeg(f,m) by A10,A57,TOPREAL1:def 9;
      then
A61:  LSeg(f,n) /\ LSeg(f,m) = {} by XBOOLE_0:def 7;
      now
        per cases by XXREAL_0:1;
        suppose
A62:      m+1<len h;
          then
A63:      m+1<=len f by A17,NAT_1:13;
A64:      1<m by A57,A60,XXREAL_0:2;
          then 1<=m+1 by NAT_1:13;
          then
A65:      m+1 in dom f by A12,A63,FINSEQ_1:3;
A66:      m<=len f by A17,A62,XREAL_1:8;
          then m in dom f by A12,A64,FINSEQ_1:3;
          then
A67:      LSeg(h,m)=LSeg(f,m) by A5,A65,TOPREAL3:25;
          now
            per cases;
            suppose
              0<n;
              then
A68:          0+1<=n by NAT_1:13;
              n+1<=len f by A57,A66,XXREAL_0:2;
              then
A69:          n+1 in dom f by A12,A60,FINSEQ_1:3;
              n<=len f by A58,A66,XXREAL_0:2;
              then n in dom f by A12,A68,FINSEQ_1:3;
              hence LSeg(h,n) /\ LSeg(h,m) = {} by A5,A61,A67,A69,
              TOPREAL3:25;
            end;
            suppose
              0=n;
              then LSeg(h,n)={} by TOPREAL1:def 5;
              hence LSeg(h,n) /\ LSeg(h,m) = {};
            end;
          end;
          hence LSeg(h,n) /\ LSeg(h,m) = {};
        end;
        suppose
A70:      m+1=len h;
          now
            per cases;
            suppose
A71:          0<n;
A72:          n+1 in dom f by A17,A12,A57,A60,A70,FINSEQ_1:3;
A73:          0+1<=n by A71,NAT_1:13;
              then n in dom f by A17,A12,A58,A70,FINSEQ_1:3;
              then
A74:          LSeg(h,n)=LSeg(f,n) by A5,A72,TOPREAL3:25;
              now
                set L = LSeg(f,n);
                consider x being Element of LSeg(h,n) /\ LSeg(h,m);
                assume
A75:            LSeg(h,n) /\ LSeg(h,m) <> {};
                then
A76:            x in L by A74,XBOOLE_0:def 4;
A77:            x in LSeg(p2,p) by A17,A19,A70,A75,XBOOLE_0:def 4;
                n in NAT by ORDINAL1:def 13;
                then L in Mf by A17,A57,A70,A73;
                then x in L~f by A76,TARSKI:def 4;
                then x in {p2} by A4,A77,XBOOLE_0:def 4;
                then
A78:            x=p2 by TARSKI:def 1;
A79:            n+1+1<=len f by A17,A57,A70,NAT_1:13;
                now
                  per cases by A79,XXREAL_0:1;
                  suppose
A80:                n+1+1 = len f;
                    1 <= n+1 by NAT_1:11;
                    then
A81:                p2 in LSeg(f,n+1) by A80,TOPREAL1:27;
                    LSeg(f,n) /\ LSeg(f,n+1) = {f/.(n+ 1)} by A8,A59,
                    A73,A80,TOPREAL1:def 8;
                    then p2 in {f/.(n+1)} by A76,A78,A81, XBOOLE_0:
                    def 4;
                    then f/.len f=f/.(n+1) by TARSKI:def 1;
                    then len f + 1 = len f by A15,A7,A72,A80,
                    PARTFUN2:17;
                    hence contradiction;
                  end;
                  suppose
A82:                n+1+1<len f;
                    reconsider j=len f - 1 as Element of NAT by A13,INT_1:18,
                    XXREAL_0:2;
                    n+2+1<=len f by A82,NAT_1:13;
                    then n+2<=len f - 1 by XREAL_1:21;
                    then n+1<j by A59,NAT_1:13;
                    then LSeg(f,n) misses LSeg(f,j) by A10,TOPREAL1:def 9;
                    then
A83:                LSeg(f,n) /\ LSeg(f,j) = {} by XBOOLE_0:def 7;
                    (1+1)-1 = 1;
                    then j+1 = len f & 1<=j by A13,XREAL_1:15;
                    then p2 in LSeg(f,j) by TOPREAL1:27;
                    hence contradiction by A76,A78,A83,XBOOLE_0:def 4;
                  end;
                end;
                hence contradiction;
              end;
              hence LSeg(h,n) /\ LSeg(h,m) = {};
            end;
            suppose
              0=n;
              then LSeg(h,n)={} by TOPREAL1:def 5;
              hence LSeg(h,n) /\ LSeg(h,m) = {};
            end;
          end;
          hence LSeg(h,n) /\ LSeg(h,m) = {};
        end;
        suppose
          m+1>len h;
          then LSeg(h,m) = {} by TOPREAL1:def 5;
          hence LSeg(h,n) /\ LSeg(h,m) = {};
        end;
      end;
      hence LSeg(h,n) /\ LSeg(h,m) = {};
    end;
    hereby
      let n be Nat such that
A84:  1 <= n and
A85:  n + 1 <= len h;
      set p3 = h/.n, p4 = h/.(n+1);
      now
        per cases by A85,XXREAL_0:1;
        suppose
          n+1 = len h;
          hence p3`1=p4`1 or p3`2=p4`2 by A1,A5,A17,A16,FINSEQ_4:82;
        end;
        suppose
A86:      n+1 < len h;
A87:      1<=n+1 by A84,NAT_1:13;
          n+1<=len f by A17,A86,NAT_1:13;
          then n+1 in dom f by A12,A87,FINSEQ_1:3;
          then
A88:      p4=f/.(n+1) by A5,FINSEQ_4:83;
          n<=len f by A17,A86,XREAL_1:8;
          then n in dom f by A12,A84,FINSEQ_1:3;
          then
A89:      p3=f/.n by A5,FINSEQ_4:83;
          n+1<= len f by A17,A86,NAT_1:13;
          hence p3`1 = p4`1 or p3`2 = p4`2 by A20,A84,A89,A88, TOPREAL1:
          def 7;
        end;
      end;
      hence p3`1 = p4`1 or p3`2 = p4`2;
    end;
  end;
  1 in dom f by A12,A14,FINSEQ_1:3;
  then h/.1 = p1 by A5,FINSEQ_4:83;
  hence L~h is_S-P_arc_joining p1,p by A18,A21,Def1;
  let x;
  assume x in L~h;
  then consider X be set such that
A90: x in X and
A91: X in Mh by TARSKI:def 4;
  consider k such that
A92: X=LSeg(h,k) and
A93: 1<=k and
A94: k+1<=len h by A91;
  per cases by A94,XXREAL_0:1;
  suppose
A95: k+1 = len h;
A96: Ball(u,r) c= L~f \/ Ball(u,r) by XBOOLE_1:7;
    X c= Ball(u,r) by A2,A17,A19,A92,A95,TOPREAL3:28;
    hence thesis by A90,A96,TARSKI:def 3;
  end;
  suppose
    k+1 < len h;
    then
A97: k+1<=len f by A17,NAT_1:13;
    k<=k+1 by NAT_1:11;
    then k<=len f by A97,XXREAL_0:2;
    then
A98: k in dom f by A12,A93,FINSEQ_1:3;
    1<=k+1 by A93,NAT_1:13;
    then k+1 in dom f by A12,A97,FINSEQ_1:3;
    then X=LSeg(f,k) by A5,A92,A98,TOPREAL3:25;
    then X in Mf by A93,A97;
    then x in union Mf by A90,TARSKI:def 4;
    hence thesis by XBOOLE_0:def 3;
  end;
end;

theorem Th21:
  f/.len f in Ball(u,r) & p in Ball(u,r) & |[p`1,(f/.len f)`2]| in
Ball(u,r) & f is being_S-Seq & p`1<>(f/.len f)`1 & p`2<>(f/.len f)`2 & h=f^<*|[
p`1,(f/.len f)`2]|,p*> & (LSeg(f/.len f,|[p`1,(f/.len f)`2]|) \/ LSeg(|[p`1,(f
/.len f)`2]|,p)) /\ L~f = {f/.len f} implies L~h is_S-P_arc_joining f/.1,p & L~
  h c= L~f \/ Ball(u,r)
proof
  set p1 = f/.1, p2 = f/.len f;
  assume that
A1: p2 in Ball(u,r) and
A2: p in Ball(u,r) and
A3: |[p`1,p2`2]| in Ball(u,r) and
A4: f is being_S-Seq and
A5: p`1<>p2`1 and
A6: p`2<>p2`2 and
A7: h=f^<*|[p`1,p2`2]|,p*> and
A8: (LSeg(p2,|[p`1,p2`2]|) \/ LSeg(|[p`1,p2`2]|,p)) /\ L~f = {p2};
  set p3 = |[p`1,p2`2]|, f1 = f^<* p3 *>, h1 = f1^<* p *>;
  reconsider Lf = L~f as non empty Subset of TOP-REAL 2 by A8;
A9: p2 in LSeg(p2,p3) by RLTOPSP1:69;
  L~f is_S-P_arc_joining p1,p2 by A4,Def1;
  then Lf is_an_arc_of p1,p2 by Th3;
  then p2 in L~f by TOPREAL1:4;
  then p2 in LSeg(p2,p3) /\ L~f by A9,XBOOLE_0:def 4;
  then
A10: LSeg(p2,p3) /\ L~f c= (LSeg(p2,p3) /\ L~f) \/ (LSeg(p3,p) /\ L~f) & {p2
  } c= LSeg(p2,p3) /\ L~f by XBOOLE_1:7,ZFMISC_1:37;
  {p2} = (LSeg(p2,p3) /\ L~f) \/ (LSeg(p3,p) /\ L~f) by A8,XBOOLE_1:23;
  then
A11: LSeg(p2,p3) /\ L~f = {p2} by A10,XBOOLE_0:def 10;
A12: len f1 = len f + len <*p3*> by FINSEQ_1:35
    .= len f + 1 by FINSEQ_1:56;
  then
A13: f1/.len f1 = p3 by FINSEQ_4:82;
A14: p=|[p`1,p`2]| by EUCLID:57;
A15: Seg len f = dom f by FINSEQ_1:def 3;
  len f>=2 by A4,TOPREAL1:def 10;
  then
A16: 1<=len f by XXREAL_0:2;
  then len f in dom f by A15,FINSEQ_1:3;
  then
A17: f1/.len f = p2 by FINSEQ_4:83;
A18: LSeg(p3,p) /\ L~f1 c= {p3}
  proof
    set M1 = {LSeg(f1,i): 1<=i & i+1<=len f1}, Mf = {LSeg(f,j): 1<=j & j+1<=
    len f};
    assume not thesis;
    then consider x such that
A19: x in LSeg(p3,p) /\ L~f1 and
A20: not x in {p3} by TARSKI:def 3;
    x in L~f1 by A19,XBOOLE_0:def 4;
    then consider X be set such that
A21: x in X and
A22: X in M1 by TARSKI:def 4;
    consider k such that
A23: X=LSeg(f1,k) and
A24: 1<=k and
A25: k+1<=len f1 by A22;
A26: x in LSeg(p3,p) by A19,XBOOLE_0:def 4;
    now
      per cases by A25,XXREAL_0:1;
      suppose
        k+1 = len f1;
        then LSeg(f1,k) = LSeg(p2,p3) by A12,A13,A17,A24,TOPREAL1:def 5;
        then x in LSeg(p2,p3) /\ LSeg(p3,p) by A26,A21,A23,
        XBOOLE_0:def 4;
        hence contradiction by A20,TOPREAL3:37;
      end;
      suppose
        k+1 < len f1;
        then
A27:    k+1<=len f by A12,NAT_1:13;
        k<=k+1 by NAT_1:11;
        then k<=len f by A27,XXREAL_0:2;
        then
A28:    k in dom f by A15,A24,FINSEQ_1:3;
        1<=k+1 by A24,NAT_1:13;
        then k+1 in dom f by A15,A27,FINSEQ_1:3;
        then X=LSeg(f,k) by A23,A28,TOPREAL3:25;
        then X in Mf by A24,A27;
        then
A29:    x in L~f by A21,TARSKI:def 4;
        x in LSeg(p2,p3) \/ LSeg(p3,p) by A26,XBOOLE_0:def 3;
        then x in {p2} by A8,A29,XBOOLE_0:def 4;
        then x = p2 by TARSKI:def 1;
        hence contradiction by A5,A14,A26,TOPREAL3:17;
      end;
    end;
    hence contradiction;
  end;
A30: h1 = f^(<*p3*>^<*p*>) by FINSEQ_1:45
    .= h by A7,FINSEQ_1:def 9;
A31: p3`2 = p2`2 & p3`1 = p`1 by EUCLID:56;
  then
A32: f1 is being_S-Seq by A1,A3,A4,A5,A11,Th20;
A33: L~f1 is_S-P_arc_joining p1,p3 by A1,A3,A4,A5,A31,A11,Th20;
  then reconsider Lf1 = L~f1 as non empty Subset of TOP-REAL 2 by
  Th2,TOPREAL1:
  32;
A34: p3 in LSeg(p3,p) by RLTOPSP1:69;
A35: f1/.len f1=p3 by A12,FINSEQ_4:82;
  L~f1 c= L~f \/ Ball(u,r) by A1,A3,A4,A5,A31,A11,Th20;
  then L~f1 \/ Ball(u,r) c= L~f \/ Ball(u,r) \/ Ball(u,r) by XBOOLE_1:9;
  then
A36: L~f1 \/ Ball(u,r) c= L~f \/ (Ball(u,r) \/ Ball(u,r)) by
  XBOOLE_1:4;
A37: p`1=p3`1 & p`2<>p3`2 or p`1<>p3`1 & p`2=p3`2 by A6,EUCLID:56;
  Lf1 is_an_arc_of p1,p3 by A33,Th3;
  then p3 in L~f1 by TOPREAL1:4;
  then p3 in LSeg(p3,p) /\ L~f1 by A34,XBOOLE_0:def 4;
  then {p3} c= LSeg(p3,p) /\ L~f1 by ZFMISC_1:37;
  then
A38: LSeg(p3,p) /\ L~f1 = {p3} by A18,XBOOLE_0:def 10;
  1 in dom f by A15,A16,FINSEQ_1:3;
  then f1/.1=p1 by FINSEQ_4:83;
  hence L~h is_S-P_arc_joining p1,p by A2,A3,A37,A32,A35,A38,A30,Th20;
  L~h1 c= L~f1 \/ Ball(u,r) by A2,A3,A37,A32,A35,A38,Th20;
  hence thesis by A30,A36,XBOOLE_1:1;
end;

theorem Th22:
  f/.len f in Ball(u,r) & p in Ball(u,r) & |[(f/.len f)`1,p`2]| in
Ball(u,r) & f is being_S-Seq & p`1<>(f/.len f)`1 & p`2<>(f/.len f)`2 & h=f^<*|[
(f/.len f)`1,p`2]|,p*> & (LSeg(f/.len f,|[(f/.len f)`1,p`2]|) \/ LSeg(|[(f/.len
f)`1,p`2]|,p)) /\ L~f = {f/.len f} implies L~h is_S-P_arc_joining f/.1,p & L~h
  c= L~f \/ Ball(u,r)
proof
  set p1 = f/.1, p2 = f/.len f;
  assume that
A1: p2 in Ball(u,r) and
A2: p in Ball(u,r) and
A3: |[p2`1,p`2]| in Ball(u,r) and
A4: f is being_S-Seq and
A5: p`1<>p2`1 and
A6: p`2<>p2`2 and
A7: h=f^<*|[p2`1,p`2]|,p*> and
A8: (LSeg(p2,|[p2`1,p`2]|) \/ LSeg(|[p2`1,p`2]|,p)) /\ L~f = {p2};
  set p3 = |[p2`1,p`2]|, f1 = f^<* p3 *>, h1 = f1^<* p *>;
  reconsider Lf = L~f as non empty Subset of TOP-REAL 2 by A8;
A9: p2 in LSeg(p2,p3) by RLTOPSP1:69;
  L~f is_S-P_arc_joining p1,p2 by A4,Def1;
  then Lf is_an_arc_of p1,p2 by Th3;
  then p2 in L~f by TOPREAL1:4;
  then p2 in LSeg(p2,p3) /\ L~f by A9,XBOOLE_0:def 4;
  then
A10: LSeg(p2,p3) /\ L~f c= (LSeg(p2,p3) /\ L~f) \/ (LSeg(p3,p) /\ L~f) & {p2
  } c= LSeg(p2,p3) /\ L~f by XBOOLE_1:7,ZFMISC_1:37;
  {p2} = (LSeg(p2,p3) /\ L~f) \/ (LSeg(p3,p) /\ L~f) by A8,XBOOLE_1:23;
  then
A11: LSeg(p2,p3) /\ L~f = {p2} by A10,XBOOLE_0:def 10;
A12: len f1 = len f + len <*p3*> by FINSEQ_1:35
    .= len f + 1 by FINSEQ_1:56;
  then
A13: f1/.len f1 = p3 by FINSEQ_4:82;
A14: p=|[p`1,p`2]| by EUCLID:57;
A15: Seg len f = dom f by FINSEQ_1:def 3;
  len f>=2 by A4,TOPREAL1:def 10;
  then
A16: 1<=len f by XXREAL_0:2;
  then len f in dom f by A15,FINSEQ_1:3;
  then
A17: f1/.len f = p2 by FINSEQ_4:83;
A18: LSeg(p3,p) /\ L~f1 c= {p3}
  proof
    set M1 = {LSeg(f1,i): 1<=i & i+1<=len f1}, Mf = {LSeg(f,j): 1<=j & j+1<=
    len f};
    assume not thesis;
    then consider x such that
A19: x in LSeg(p3,p) /\ L~f1 and
A20: not x in {p3} by TARSKI:def 3;
    x in L~f1 by A19,XBOOLE_0:def 4;
    then consider X be set such that
A21: x in X and
A22: X in M1 by TARSKI:def 4;
    consider k such that
A23: X=LSeg(f1,k) and
A24: 1<=k and
A25: k+1<=len f1 by A22;
A26: x in LSeg(p3,p) by A19,XBOOLE_0:def 4;
    now
      per cases by A25,XXREAL_0:1;
      suppose
        k+1 = len f1;
        then LSeg(f1,k) = LSeg(p2,p3) by A12,A13,A17,A24,TOPREAL1:def 5;
        then x in LSeg(p2,p3) /\ LSeg(p3,p) by A26,A21,A23,
        XBOOLE_0:def 4;
        hence contradiction by A20,TOPREAL3:36;
      end;
      suppose
        k+1 < len f1;
        then
A27:    k+1<=len f by A12,NAT_1:13;
        k<=k+1 by NAT_1:11;
        then k<=len f by A27,XXREAL_0:2;
        then
A28:    k in dom f by A15,A24,FINSEQ_1:3;
        1<=k+1 by A24,NAT_1:13;
        then k+1 in dom f by A15,A27,FINSEQ_1:3;
        then X=LSeg(f,k) by A23,A28,TOPREAL3:25;
        then X in Mf by A24,A27;
        then
A29:    x in L~f by A21,TARSKI:def 4;
        x in LSeg(p2,p3) \/ LSeg(p3,p) by A26,XBOOLE_0:def 3;
        then x in {p2} by A8,A29,XBOOLE_0:def 4;
        then x = p2 by TARSKI:def 1;
        hence contradiction by A6,A14,A26,TOPREAL3:18;
      end;
    end;
    hence contradiction;
  end;
A30: h1 = f^(<*p3*>^<*p*>) by FINSEQ_1:45
    .= h by A7,FINSEQ_1:def 9;
A31: p3`2 = p`2 & p3`1 = p2`1 by EUCLID:56;
  then
A32: f1 is being_S-Seq by A1,A3,A4,A6,A11,Th20;
A33: L~f1 is_S-P_arc_joining p1,p3 by A1,A3,A4,A6,A31,A11,Th20;
  then reconsider Lf1 = L~f1 as non empty Subset of TOP-REAL 2 by
  Th2,TOPREAL1:
  32;
A34: p3 in LSeg(p3,p) by RLTOPSP1:69;
A35: f1/.len f1=p3 by A12,FINSEQ_4:82;
  L~f1 c= L~f \/ Ball(u,r) by A1,A3,A4,A6,A31,A11,Th20;
  then L~f1 \/ Ball(u,r) c= L~f \/ Ball(u,r) \/ Ball(u,r) by XBOOLE_1:9;
  then
A36: L~f1 \/ Ball(u,r) c= L~f \/ (Ball(u,r) \/ Ball(u,r)) by
  XBOOLE_1:4;
A37: p`1=p3`1 & p`2<>p3`2 or p`1<>p3`1 & p`2=p3`2 by A5,EUCLID:56;
  Lf1 is_an_arc_of p1,p3 by A33,Th3;
  then p3 in L~f1 by TOPREAL1:4;
  then p3 in LSeg(p3,p) /\ L~f1 by A34,XBOOLE_0:def 4;
  then {p3} c= LSeg(p3,p) /\ L~f1 by ZFMISC_1:37;
  then
A38: LSeg(p3,p) /\ L~f1 = {p3} by A18,XBOOLE_0:def 10;
  1 in dom f by A15,A16,FINSEQ_1:3;
  then f1/.1=p1 by FINSEQ_4:83;
  hence L~h is_S-P_arc_joining p1,p by A2,A3,A37,A32,A35,A38,A30,Th20;
  L~h1 c= L~f1 \/ Ball(u,r) by A2,A3,A37,A32,A35,A38,Th20;
  hence thesis by A30,A36,XBOOLE_1:1;
end;

theorem Th23:
  not f/.1 in Ball(u,r) & f/.len f in Ball(u,r) & p in Ball(u,r) &
f is being_S-Seq & not p in L~f implies ex h st L~h is_S-P_arc_joining f/.1,p &
  L~h c= L~f \/ Ball(u,r)
proof
  set p1 = f/.1;
  set Mf= {LSeg(f,k):1<=k & k+1<=len f};
  assume that
A1: not p1 in Ball(u,r) and
A2: f/.len f in Ball(u,r) and
A3: p in Ball(u,r) and
A4: f is being_S-Seq and
A5: not p in L~f;
A6: f is special by A4,TOPREAL1:def 10;
  defpred X[Nat] means 1<=$1 & $1<=len f - 1 & LSeg(f,$1) meets Ball(u,r);
A7: len f>= 2 by A4,TOPREAL1:def 10;
  then reconsider n=len f - 1 as Element of NAT by INT_1:18,XXREAL_0:2;
  2=1+1;
  then
A8: 1<=len f - 1 by A7,XREAL_1:21;
  n+1=len f;
  then f/.len f in LSeg(f,n) by A8,TOPREAL1:27;
  then LSeg(f,n) meets Ball(u,r) by A2,XBOOLE_0:3;
  then
A9: ex n be Nat st X[n] by A8;
  consider m be Nat such that
A10: X[m] and
A11: for i be Nat st X[i] holds m<=i from NAT_1:sch 5(A9);
  reconsider m as Element of NAT by ORDINAL1:def 13;
  consider q1,q2 such that
A12: f/.m = q1 and
A13: f/.(m+1) = q2;
A14: LSeg(f,m) /\ Ball(u,r) <> {} by A10,XBOOLE_0:def 7;
A15: m+1<=len f by A10,XREAL_1:21;
  then
A16: LSeg(f,m) = LSeg(q1,q2) by A10,A12,A13,TOPREAL1:def 5;
  m<=m+1 by NAT_1:11;
  then
A17: m<=len f by A15,XXREAL_0:2;
A18: Seg len f=dom f by FINSEQ_1:def 3;
A19: now
    consider x being Element of Ball(u,r) /\ L~(f|m);
    set M = {LSeg(f|m,i):1<=i & i+1<=len(f|m)};
A20: Seg len(f|m) = dom(f |m ) by FINSEQ_1:def 3;
    assume
A21: Ball(u,r) /\ L~(f|m) <> {};
    then
A22: x in Ball(u,r) by XBOOLE_0:def 4;
    x in L~(f|m) by A21,XBOOLE_0:def 4;
    then consider X be set such that
A23: x in X and
A24: X in M by TARSKI:def 4;
    consider k such that
A25: X=LSeg(f|m,k) and
A26: 1<=k and
A27: k+1<=len(f|m) by A24;
    k<=k+1 by NAT_1:11;
    then k<=len(f|m) by A27,XXREAL_0:2;
    then
A28: k in Seg len(f|m) by A26,FINSEQ_1:3;
    1<=k+1 by NAT_1:11;
    then k+1 in Seg len(f|m) by A27,FINSEQ_1:3;
    then x in LSeg(f,k) by A23,A25,A28,A20,TOPREAL3:24;
    then
A29: LSeg(f,k) meets Ball(u,r) by A22,XBOOLE_0:3;
    Seg m c= Seg len f by A17,FINSEQ_1:7;
    then
A30: Seg m = dom f /\ (Seg m) by A18,XBOOLE_1:28
      .= dom(f|(Seg m)) by RELAT_1:90
      .= Seg len(f|m) by A20,FINSEQ_1:def 15;
A31: k+1-1 <= len(f|m)-1 by A27,XREAL_1:11;
    then
A32: k<=m-1 by A30,FINSEQ_1:8;
    m = len(f|m) by A30,FINSEQ_1:8;
    then len(f|m) -1<=len f - 1 by A17,XREAL_1:11;
    then k<=len f - 1 by A31,XXREAL_0:2;
    then m<=k by A11,A26,A29;
    then m<=m-1 by A32,XXREAL_0:2;
    then 0<=m+-1-m by XREAL_1:50;
    hence contradiction;
  end;
  now
    let i;
    assume that
A33: 1<=i & i<=len f - 1 and
A34: LSeg(f,i) /\ Ball(u,r) <> {};
    LSeg(f,i) meets Ball(u,r) by A34,XBOOLE_0:def 7;
    hence m<=i by A11,A33;
  end;
  then
A35: not q1 in Ball(u,r) by A1,A10,A12,TOPREAL3:33;
  set A=LSeg(f,m) /\ Ball(u,r);
A36: q1=|[q1`1,q1`2]| & q2=|[q2`1,q2`2]| by EUCLID:57;
  m+1 <= n+1 by A10,XREAL_1:8;
  then LSeg(f,m) in Mf by A10;
  then
A37: LSeg(f,m) c= union Mf by ZFMISC_1:92;
  now
    per cases;
    suppose
      ex q st q in A & (p`1=q`1 or p`2=q`2);
      then consider q such that
A38:  q in A and
A39:  p`1=q`1 or p`2=q`2;
A40:  q in Ball(u,r) by A38,XBOOLE_0:def 4;
A41:  q in LSeg(q,p) by RLTOPSP1:69;
A42:  q in LSeg(f,m) by A38,XBOOLE_0:def 4;
      then consider h such that
A43:  h is being_S-Seq and
A44:  h/.1=p1 and
A45:  h/.len h=q and
A46:  L~h is_S-P_arc_joining p1,q and
A47:  L~h c= L~f and
A48:  L~h = L~(f|m) \/ LSeg(q1,q) by A1,A4,A12,A40,Th18;
      take g = h^<* p *>;
A49:  L~h \/ Ball(u,r) c= L~f \/ Ball(u,r) by A47,XBOOLE_1:9;
A50:  q in L~f by A37,A42;
A51:  now
        per cases by A39;
        suppose
          p`1=q`1;
          hence p`1=q`1 & p`2<>q`2 or p`1<>q`1 & p`2=q`2 by A5,A50,TOPREAL3
          :11;
        end;
        suppose
          p`2=q`2;
          hence p`1=q`1 & p`2<>q`2 or p`1<>q`1 & p`2=q`2 by A5,A50,TOPREAL3
          :11;
        end;
      end;
A52:  LSeg(q,p) /\ L~h c= {q}
      proof
A53:    now
          per cases by A6,A10,A15,A12,A13,TOPREAL1:def 7;
          suppose
            q1`1=q2`1;
            hence q1`1 = q`1 or q1`2=q`2 by A36,A16,A42,TOPREAL3:17;
          end;
          suppose
            q1`2=q2`2;
            hence q1`1 = q`1 or q1`2=q`2 by A36,A16,A42,TOPREAL3:18;
          end;
        end;
        LSeg(q,p) = LSeg(q,p) /\ Ball(u,r) by A3,A40,TOPREAL3:28 ,
        XBOOLE_1:28;
        then
A54:    LSeg(q,p) /\ (L~(f|m) \/ LSeg(q1,q)) = LSeg(q,p) /\ Ball(u,r) /\
        L~(f|m) \/ LSeg(q,p) /\ LSeg(q1,q) by XBOOLE_1:23
          .= LSeg(q,p) /\(Ball(u,r) /\ L~(f|m))\/ LSeg(q,p) /\ LSeg(q1,q) by
        XBOOLE_1:16
          .= LSeg(q1,q) /\ LSeg(q,p) by A19;
A55:    now
          q1 in LSeg(q1,q2) by RLTOPSP1:69;
          then
A56:      LSeg(q1,q) c= LSeg(f,m) by A16,A42,TOPREAL1:12;
          assume p in LSeg(q1,q);
          hence contradiction by A5,A37,A56,TARSKI:def 3;
        end;
        let x;
        assume x in LSeg(q,p) /\ L~h;
        hence thesis by A3,A35,A40,A51,A48,A54,A55,A53,TOPREAL3:49;
      end;
      q in L~h by A46,Th4;
      then q in LSeg(q,p) /\ L~h by A41,XBOOLE_0:def 4;
      then {q} c= LSeg(q,p) /\ L~h by ZFMISC_1:37;
      then
A57:  LSeg(q,p) /\ L~h = {q} by A52,XBOOLE_0:def 10;
      then L~g c=L~h \/ Ball(u,r) by A3,A40,A51,A43,A45,Th20;
      hence L~g is_S-P_arc_joining p1,p & L~g c=L~f \/ Ball(u,r) by A3,A40,A51
      ,A43,A44,A45,A57,A49,Th20,XBOOLE_1:1;
    end;
    suppose
A58:  for q st q in A holds p`1<>q`1 & p`2<>q`2;
      consider x being Element of A;
A59:  x in LSeg(f,m) by A14,XBOOLE_0:def 4;
      then reconsider q=x as Point of TOP-REAL 2;
A60:  q`1<>p`1 & q`2<>p`2 by A14,A58;
      q <> p1 by A1,A14,XBOOLE_0:def 4;
      then consider h such that
A61:  h is being_S-Seq and
A62:  h/.1=p1 and
A63:  h/.len h=q and
A64:  L~h is_S-P_arc_joining p1,q and
A65:  L~h c= L~f and
A66:  L~h = L~(f|m) \/ LSeg(q1,q) by A4,A12,A59,Th18;
A67:  q in Ball(u,r) by A14,XBOOLE_0:def 4;
A68:  q=|[q`1,q`2]| & p=|[p`1,p`2]| by EUCLID:57;
      now
        per cases by A3,A67,A68,TOPREAL3:32;
        suppose
A69:      |[q`1,p`2]| in Ball(u,r);
          set v = |[q`1,p`2]|;
A70:      (LSeg(q,v) \/ LSeg(v,p)) /\ L~h c= {q}
          proof
            let x;
            set L = LSeg(q,v) \/ LSeg(v,p);
            assume
A71:        x in L /\ L~h;
            LSeg(q,v) c=Ball(u,r) & LSeg(v,p) c=Ball(u,r) by A3,A67,A69
            ,TOPREAL3:28;
            then L = L /\ Ball(u,r) by XBOOLE_1:8,28;
            then
A72:        L /\ (L~(f|m) \/ LSeg(q1,q)) = L /\ Ball(u,r) /\ L~(f|m) \/
            L /\ LSeg( q1,q) by XBOOLE_1:23
              .= L /\ (Ball(u,r) /\ L~(f|m)) \/ L /\ LSeg(q1,q) by XBOOLE_1:
            16
              .= {} \/ L /\ LSeg(q1,q) by A19;
A73:        now
              per cases by A6,A10,A15,A12,A13,TOPREAL1:def 7;
              suppose
                q1`1=q2`1;
                hence q1`1 = q`1 or q1`2=q`2 by A36,A16,A59,TOPREAL3:17;
              end;
              suppose
                q1`2=q2`2;
                hence q1`1 = q`1 or q1`2=q`2 by A36,A16,A59,TOPREAL3:18;
              end;
            end;
            now
              per cases by A73;
              suppose
A74:            q1`1 = q`1;
                now
                  q1 in LSeg(q1,q2) by RLTOPSP1:69;
                  then
A75:              LSeg(q1,q) c= LSeg(f,m) by A16,A59, TOPREAL1:12;
A76:              v`2=p`2 by EUCLID:56;
                  assume v in LSeg(q1,q);
                  then v in LSeg(f,m) /\ Ball(u,r) by A69,A75,XBOOLE_0
                  :def 4;
                  hence contradiction by A58,A76;
                end;
                hence thesis by A35,A67,A60,A66,A69,A72,A71,A74,
                TOPREAL3:50;
              end;
              suppose
                q1`2 = q`2;
                hence thesis by A14,A58,A66,A72,A71,TOPREAL3:34;
              end;
            end;
            hence thesis;
          end;
          take g = h^<* |[q`1,p`2]|,p *>;
          q in LSeg(q,v) by RLTOPSP1:69;
          then
A77:      q in (LSeg(q,v) \/ LSeg(v,p)) by XBOOLE_0:def 3;
A78:      L~h \/ Ball(u,r) c= L~f \/ Ball(u,r) by A65,XBOOLE_1:9;
          q in L~h by A64,Th4;
          then q in (LSeg(q,v) \/ LSeg(v,p)) /\ L~h by A77, XBOOLE_0:
          def 4;
          then {q} c= (LSeg(q,v) \/ LSeg(v,p)) /\ L~h by ZFMISC_1:37;
          then
A79:      (LSeg(q,|[q`1,p`2]|) \/ LSeg(|[q`1,p`2]|,p)) /\ L~h={q} by
          A70,XBOOLE_0:def 10;
          then L~g c= L~h \/ Ball(u,r) by A3,A67,A60,A61,A63,A69,Th22;
          hence
          L~g is_S-P_arc_joining p1,p & L~g c=L~f \/ Ball(u,r) by A3,A67,
          A60,A61,A62,A63,A69,A79,A78,Th22, XBOOLE_1:1;
        end;
        suppose
A80:      |[p`1,q`2]| in Ball(u,r);
          set v = |[p`1,q`2]|;
A81:      (LSeg(q,v) \/ LSeg(v,p)) /\ L~h c= {q}
          proof
            let x;
            set L = LSeg(q,v) \/ LSeg(v,p);
            assume
A82:        x in L /\ L~h;
            LSeg(q,v) c=Ball(u,r) & LSeg(v,p) c= Ball(u,r) by A3,A67,
            A80,TOPREAL3:28;
            then L = L /\ Ball(u,r) by XBOOLE_1:8,28;
            then
A83:        L /\ (L~(f|m) \/ LSeg(q1,q)) = L /\ Ball(u,r) /\ L~(f|m) \/
            L /\ LSeg(q1,q) by XBOOLE_1:23
              .= L /\ (Ball(u,r) /\ L~(f|m)) \/ L /\ LSeg(q1,q) by XBOOLE_1:
            16
              .= {} \/ L /\ LSeg(q1,q) by A19;
A84:        now
              per cases by A6,A10,A15,A12,A13,TOPREAL1:def 7;
              suppose
                q1`1=q2`1;
                hence q1`1 = q`1 or q1`2=q`2 by A36,A16,A59,TOPREAL3:17;
              end;
              suppose
                q1`2=q2`2;
                hence q1`1 = q`1 or q1`2=q`2 by A36,A16,A59,TOPREAL3:18;
              end;
            end;
            now
              per cases by A84;
              suppose
                q1`1 = q`1;
                hence thesis by A14,A58,A66,A83,A82,TOPREAL3:35;
              end;
              suppose
A85:            q1`2 = q`2;
                now
                  q1 in LSeg(q1,q2) by RLTOPSP1:69;
                  then
A86:              LSeg(q1,q) c= LSeg(f,m) by A16,A59, TOPREAL1:12;
A87:              v`1=p`1 by EUCLID:56;
                  assume v in LSeg(q1,q);
                  then v in LSeg(f,m) /\ Ball(u,r) by A80,A86,XBOOLE_0
                  :def 4;
                  hence contradiction by A58,A87;
                end;
                hence thesis by A35,A67,A60,A66,A80,A83,A82,A85,
                TOPREAL3:51;
              end;
            end;
            hence thesis;
          end;
          take g = h^<* |[p`1,q`2]|,p *>;
          q in LSeg(q,v) by RLTOPSP1:69;
          then
A88:      q in (LSeg(q,v) \/ LSeg(v,p)) by XBOOLE_0:def 3;
A89:      L~h \/ Ball(u,r) c= L~f \/ Ball(u,r) by A65,XBOOLE_1:9;
          q in L~h by A64,Th4;
          then q in (LSeg(q,v) \/ LSeg(v,p)) /\ L~h by A88, XBOOLE_0:
          def 4;
          then {q} c= (LSeg(q,v) \/ LSeg(v,p)) /\ L~h by ZFMISC_1:37;
          then
A90:      (LSeg(q,|[p`1,q`2]|) \/ LSeg(|[p`1,q`2]|,p)) /\ L~h={q} by
          A81,XBOOLE_0:def 10;
          then L~g c= L~h \/ Ball(u,r) by A3,A67,A60,A61,A63,A80,Th21;
          hence
          L~g is_S-P_arc_joining p1,p & L~g c=L~f \/ Ball(u,r) by A3,A67,
          A60,A61,A62,A63,A80,A90,A89,Th21, XBOOLE_1:1;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th24:
  for R,p,p1,p2,P,r,u st p<>p1 & P is_S-P_arc_joining p1,p2 & P c=
  R & p in Ball(u,r) & p2 in Ball(u,r) & Ball(u,r) c= R ex P1 being Subset of
  TOP-REAL 2 st P1 is_S-P_arc_joining p1,p & P1 c= R
proof
  let R,p,p1,p2,P,r,u;
  assume that
A1: p<>p1 and
A2: P is_S-P_arc_joining p1,p2 and
A3: P c= R and
A4: p in Ball(u,r) and
A5: p2 in Ball(u,r) and
A6: Ball(u,r) c= R;
  consider f such that
A7: f is being_S-Seq and
A8: P = L~f and
A9: p1=f/.1 and
A10: p2=f/.len f by A2,Def1;
  now
    per cases;
    suppose
      p1 in Ball(u,r);
      then consider P1 such that
A11:  P1 is_S-P_arc_joining p1,p & P1 c= Ball(u,r) by A1,A4,Th11;
      reconsider P1 as Subset of TOP-REAL 2;
      take P1;
      thus P1 is_S-P_arc_joining p1,p & P1 c= R by A6,A11,XBOOLE_1:1;
    end;
    suppose
A12:  not p1 in Ball(u,r);
      now
        per cases;
        suppose
          p in P;
          then consider h such that
          h is being_S-Seq and
          h/.1=p1 and
          h/.len h=p and
A13:      L~h is_S-P_arc_joining p1,p & L~h c= L~f by A1,A7,A8,A9,
          Th19;
          reconsider P1=L~h as Subset of TOP-REAL 2;
          take P1;
          thus P1 is_S-P_arc_joining p1,p & P1 c= R by A3,A8,A13,XBOOLE_1:1;
        end;
        suppose
          not p in P;
          then consider h such that
A14:      L~h is_S-P_arc_joining p1,p and
A15:      L~h c= L~f \/ Ball(u,r) by A4,A5,A7,A8,A9,A10,A12,Th23;
          reconsider P1=L~h as Subset of TOP-REAL 2;
          take P1;
          thus P1 is_S-P_arc_joining p1,p by A14;
          L~f \/ Ball(u,r) c= R by A3,A6,A8,XBOOLE_1:8;
          hence P1 c= R by A15,XBOOLE_1:1;
        end;
      end;
      hence thesis;
    end;
  end;
  hence thesis;
end;

Lm1: TopSpaceMetr(Euclid 2)=the TopStruct of TOP-REAL 2 by EUCLID:def 8;

reserve P, R for Subset of TOP-REAL 2;

theorem Th25:
  for p st R is being_Region & P = {q: q<>p & q in R & not ex P1
  being Subset of TOP-REAL 2 st P1 is_S-P_arc_joining p,q & P1 c=R} holds P is
  open
proof
  let p;
  assume that
A1: R is being_Region and
A2: P = {q: q<>p & q in R & not ex P1 being Subset of TOP-REAL 2 st P1
  is_S-P_arc_joining p,q & P1 c=R};
  reconsider RR = R, PP=P as Subset of the TopStruct of TOP-REAL 2;
  R is open by A1,Def3;
  then
A3: RR is open by PRE_TOPC:60;
  now
    let u;
    reconsider p2=u as Point of TOP-REAL 2 by TOPREAL3:13;
    assume
A4: u in P;
    then
    ex q1 st q1 = u & q1<>p & q1 in R & not ex P1 being Subset of TOP-REAL
    2 st P1 is_S-P_arc_joining p,q1 & P1 c=R by A2;
    then consider r being real number such that
A5: r>0 and
A6: Ball(u,r) c= RR by A3,Lm1,TOPMETR:22;
    take r;
    thus r>0 by A5;
    reconsider r9 = r as Real by XREAL_0:def 1;
A7: p2 in Ball(u,r9) by A5,TBSP_1:16;
    thus Ball(u,r) c= P
    proof
      assume not thesis;
      then consider x such that
A8:   x in Ball(u,r) and
A9:   not x in P by TARSKI:def 3;
      x in R by A6,A8;
      then reconsider q=x as Point of TOP-REAL 2;
      now
        per cases by A2,A6,A8,A9;
        suppose
A10:      q=p;
A11:      now
            assume
A12:        q=p2;
            ex p3 st p3=p2 & p3<>p & p3 in R & not ex P1 being Subset of
            TOP-REAL 2 st P1 is_S-P_arc_joining p,p3 & P1 c=R by A2,A4;
            hence contradiction by A10,A12;
          end;
          u in Ball(u,r9) by A5,TBSP_1:16;
          then
A13:      ex P2 being Subset of TOP-REAL 2 st P2 is_S-P_arc_joining q,p2 &
          P2 c= Ball(u,r9) by A8,A11,Th11;
          not p2 in P
          proof
            assume p2 in P;
            then ex q4 st q4=p2 & q4<>p & q4 in R & not ex P1 being Subset of
            TOP-REAL 2 st P1 is_S-P_arc_joining p,q4 & P1 c=R by A2;
            hence contradiction by A6,A10,A13,XBOOLE_1:1;
          end;
          hence contradiction by A4;
        end;
        suppose
A14:      ex P1 being Subset of TOP-REAL 2 st P1 is_S-P_arc_joining p
          ,q & P1 c=R;
          not p2 in P
          proof
            assume p2 in P;
            then ex q4 st q4=p2 & q4<>p & q4 in R & not ex P1 being Subset of
            TOP-REAL 2 st P1 is_S-P_arc_joining p,q4 & P1 c=R by A2;
            hence contradiction by A6,A7,A8,A14,Th24;
          end;
          hence contradiction by A4;
        end;
      end;
      hence contradiction;
    end;
  end;
  then PP is open by Lm1,TOPMETR:22;
  hence thesis by PRE_TOPC:60;
end;

theorem Th26:
  R is being_Region & p in R & P = {q: q=p or ex P1 being Subset
  of TOP-REAL 2 st P1 is_S-P_arc_joining p,q & P1 c=R} implies P is open
proof
  assume that
A1: R is being_Region and
A2: p in R and
A3: P = {q: q=p or ex P1 being Subset of TOP-REAL 2 st P1
  is_S-P_arc_joining p,q & P1 c=R};
  reconsider RR = R, PP=P as Subset of the TopStruct of TOP-REAL 2;
  R is open by A1,Def3;
  then
A4: RR is open by PRE_TOPC:60;
  now
    let u;
    reconsider p2=u as Point of TOP-REAL 2 by TOPREAL3:13;
    assume u in P;
    then consider q1 such that
A5: q1=u and
A6: q1=p or ex P1 being Subset of TOP-REAL 2 st P1 is_S-P_arc_joining
    p,q1 & P1 c=R by A3;
    now
      per cases by A6;
      suppose
        q1=p;
        hence p2 in R by A2,A5;
      end;
      suppose
        ex P1 being Subset of TOP-REAL 2 st P1 is_S-P_arc_joining p,
        q1 & P1 c=R;
        then consider P2 being Subset of TOP-REAL 2 such that
A7:     P2 is_S-P_arc_joining p,q1 and
A8:     P2 c=R;
        p2 in P2 by A5,A7,Th4;
        hence p2 in R by A8;
      end;
    end;
    then consider r being real number such that
A9: r>0 and
A10: Ball(u,r) c= RR by A4,Lm1,TOPMETR:22;
    take r;
    thus r>0 by A9;
    reconsider r9= r as Real by XREAL_0:def 1;
A11: p2 in Ball(u,r9) by A9,TBSP_1:16;
    thus Ball(u,r) c= P
    proof
      let x;
      assume
A12:  x in Ball(u,r);
      then reconsider q=x as Point of TOP-REAL 2 by A10,TARSKI:def 3;
      now
        per cases;
        suppose
          q=p;
          hence thesis by A3;
        end;
        suppose
A13:      q<>p;
A14:      now
            assume q1=p;
            then p in Ball(u,r9) by A5,A9,TBSP_1:16;
            then consider P2 being Subset of TOP-REAL 2 such that
A15:        P2 is_S-P_arc_joining p,q and
A16:        P2 c= Ball(u,r9) by A12,A13,Th11;
            reconsider P2 as Subset of TOP-REAL 2;
            P2 c= R by A10,A16,XBOOLE_1:1;
            hence thesis by A3,A15;
          end;
          now
            assume q1<>p;
            then
            ex P1 being Subset of TOP-REAL 2 st P1 is_S-P_arc_joining p,q
            & P1 c=R by A5,A6,A10,A11,A12,A13,Th24;
            hence thesis by A3;
          end;
          hence thesis by A14;
        end;
      end;
      hence thesis;
    end;
  end;
  then PP is open by Lm1,TOPMETR:22;
  hence thesis by PRE_TOPC:60;
end;

theorem Th27:
  p in R & P={q: q=p or ex P1 being Subset of TOP-REAL 2 st P1
  is_S-P_arc_joining p,q & P1 c=R} implies P c= R
proof
  assume that
A1: p in R and
A2: P = {q: q=p or ex P1 being Subset of TOP-REAL 2 st P1
  is_S-P_arc_joining p,q & P1 c=R};
  let x;
  assume x in P;
  then consider q such that
A3: q=x and
A4: q=p or ex P1 being Subset of TOP-REAL 2 st P1 is_S-P_arc_joining p,q
  & P1 c=R by A2;
  now
    per cases by A4;
    suppose
      q=p;
      hence thesis by A1,A3;
    end;
    suppose
      ex P1 being Subset of TOP-REAL 2 st P1 is_S-P_arc_joining p,q & P1 c=R;
      then consider P1 being Subset of TOP-REAL 2 such that
A5:   P1 is_S-P_arc_joining p,q and
A6:   P1 c=R;
      q in P1 by A5,Th4;
      hence thesis by A3,A6;
    end;
  end;
  hence thesis;
end;

theorem Th28:
  R is being_Region & p in R & P={q: q=p or ex P1 be Subset of
  TOP-REAL 2 st P1 is_S-P_arc_joining p,q & P1 c=R} implies R c= P
proof
  assume that
A1: R is being_Region and
A2: p in R and
A3: P = {q: q=p or ex P1 be Subset of TOP-REAL 2 st P1
  is_S-P_arc_joining p,q & P1 c=R};
A4: p in P by A3;
  set P2 = R \ P;
  reconsider P22=P2 as Subset of TOP-REAL 2;
A5: [#]((TOP-REAL 2)|R) = R by PRE_TOPC:def 10;
  then reconsider P11 = P, P12 = P22 as Subset of (TOP-REAL 2)|R by A2,A3,
  Th27,
  XBOOLE_1:36;
  P \/ (R \ P) = P \/ R by XBOOLE_1:39;
  then
A6: [#]((TOP-REAL 2)|R) = P11 \/ P12 by A5,XBOOLE_1:12;
  now
    let x;
A7: now
      assume
A8:   x in P2;
      then reconsider q2=x as Point of TOP-REAL 2;
      not x in P by A8,XBOOLE_0:def 5;
      then
A9:   q2<>p & not ex P1 being Subset of TOP-REAL 2 st P1
      is_S-P_arc_joining p,q2 & P1 c=R by A3;
      q2 in R by A8,XBOOLE_0:def 5;
      hence
      x in {q: q<>p & q in R & not ex P1 being Subset of TOP-REAL 2 st P1
      is_S-P_arc_joining p,q & P1 c= R} by A9;
    end;
    now
      assume x in {q: q<>p & q in R & not ex P1 being Subset of TOP-REAL 2
      st P1 is_S-P_arc_joining p,q & P1 c=R};
      then
A10:  ex q3 st q3=x & q3<>p & q3 in R & not ex P1 being Subset of
      TOP-REAL 2 st P1 is_S-P_arc_joining p,q3 & P1 c=R;
      then not ex q st q=x & (q=p or ex P1 being Subset of TOP-REAL 2 st P1
      is_S-P_arc_joining p,q & P1 c=R);
      then not x in P by A3;
      hence x in P2 by A10,XBOOLE_0:def 5;
    end;
    hence x in P2 iff x in {q: q<>p & q in R & not ex P1 being Subset of
    TOP-REAL 2 st P1 is_S-P_arc_joining p,q & P1 c=R} by A7;
  end;
  then P2={q: q<>p & q in R & not ex P1 being Subset of TOP-REAL 2 st P1
  is_S-P_arc_joining p,q & P1 c=R} by TARSKI:2;
  then P22 is open by A1,Th25;
  then
A11: P22 in the topology of TOP-REAL 2 by PRE_TOPC:def 5;
  reconsider R9 = R as non empty Subset of TOP-REAL 2 by A2;
  R is connected by A1,Def3;
  then
A12: (TOP-REAL 2)|R9 is connected by CONNSP_1:def 3;
  P is open by A1,A2,A3,Th26;
  then
A13: P in the topology of TOP-REAL 2 by PRE_TOPC:def 5;
  P11 = P /\ [#]((TOP-REAL 2)|R) by XBOOLE_1:28;
  then P11 in the topology of (TOP-REAL 2)|R by A13,PRE_TOPC:def 9;
  then
A14: P11 is open by PRE_TOPC:def 5;
  P12 = P22 /\ [#]((TOP-REAL 2)|R) by XBOOLE_1:28;
  then P12 in the topology of ( TOP-REAL 2)|R by A11,PRE_TOPC:def 9;
  then
A15: P12 is open by PRE_TOPC:def 5;
A16: P11 misses P12 by XBOOLE_1:79;
  then P11 /\ P12 = {}((TOP-REAL 2)|R) by XBOOLE_0:def 7;
  then P2 = {} by A4,A12,A16,A6,A14,A15,CONNSP_1:12;
  hence thesis by XBOOLE_1:37;
end;

theorem
  R is being_Region & p in R & P={q: q=p or ex P1 being Subset of
  TOP-REAL 2 st P1 is_S-P_arc_joining p,q & P1 c=R} implies R = P
proof
  assume
A1: R is being_Region & p in R & P={q: q=p or ex P1 being Subset of
  TOP-REAL 2 st P1 is_S-P_arc_joining p,q & P1 c=R};
  hence R c= P by Th28;
  thus thesis by A1,Th27;
end;

theorem
  R is being_Region & p in R & q in R & p<>q implies ex P st P
  is_S-P_arc_joining p,q & P c=R
proof
  set RR={q2: q2=p or ex P1 be Subset of TOP-REAL 2 st P1 is_S-P_arc_joining p
  ,q2 & P1 c=R};
  RR c= the carrier of TOP-REAL 2
  proof
    let x;
    assume x in RR;
    then ex q2 st q2=x & (q2=p or ex P1 be Subset of TOP-REAL 2 st P1
    is_S-P_arc_joining p,q2 & P1 c=R);
    hence thesis;
  end;
  then reconsider RR as Subset of TOP-REAL 2;
  assume that
A1: R is being_Region & p in R and
A2: q in R and
A3: p<>q;
  R c= RR by A1,Th28;
  then q in RR by A2;
  then ex q1 st q1=q & (q1=p or ex P1 be Subset of TOP-REAL 2 st P1
  is_S-P_arc_joining p,q1 & P1 c=R);
  hence thesis by A3;
end;
