:: Several Differentiation Formulas of Special Functions -- Part {IV}
::  by Bo Li and Peng Wang
::
:: Received September 29, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies FUNCT_1, RELAT_1, PRE_TOPC, BOOLE, ARYTM_1, ARYTM_3, SEQ_1,
      PARTFUN1, SIN_COS, FDIFF_1, LIMFUNC1, PREPOWER, SQUARE_1, TAYLOR_1,
      FINSEQ_1, GROUP_1, SIN_COS4, ORDINAL2, ARYTM;
 notations TARSKI, SUBSET_1, ORDINAL1, XCMPLX_0, XXREAL_0, RELSET_1, PARTFUN1,
      PARTFUN2, RCOMP_1, TAYLOR_1, SIN_COS, RFUNCT_1, SQUARE_1, NUMBERS,
      REAL_1, NAT_1, NEWTON, SEQ_1, FDIFF_1, PREPOWER, LIMFUNC1;
 constructors PARTFUN1, REAL_1, SQUARE_1, NAT_1, RCOMP_1, PARTFUN2, RFUNCT_1,
      LIMFUNC1, FDIFF_1, PREPOWER, SIN_COS, TAYLOR_1, SEQ_1;
 registrations ORDINAL1, RELSET_1, NUMBERS, XCMPLX_0, XXREAL_0, INT_1,
      MEMBERED, SEQ_1, RCOMP_1, XBOOLE_0, VALUED_0;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions SQUARE_1, LIMFUNC1, XCMPLX_0, SIN_COS;
 theorems FDIFF_2, TAYLOR_1, XBOOLE_1, XCMPLX_1, NAT_1, SEQ_1, FDIFF_1,
      SQUARE_1, FDIFF_7, FDIFF_4, RFUNCT_1, PREPOWER, FDIFF_5, FUNCT_1,
      XREAL_1, NEWTON, SIN_COS, RELAT_1, XBOOLE_0, LIMFUNC1;

begin

 reserve
         x,a,b,c for Real,
         n for natural number,
         Z for open Subset of REAL,
         f,f1,f2 for PartFunc of REAL,REAL;

theorem Th1:
  x in dom tan implies cos.x<>0
  proof
    assume x in dom tan;
    then x in dom sin /\ (dom cos \ cos"{0}) by RFUNCT_1:def 4;
    then x in dom cos \ cos"{0} by XBOOLE_0:def 3;
    then x in dom (cos^) by RFUNCT_1:def 8;
    hence thesis by RFUNCT_1:13;
  end;

theorem Th2:
  x in dom cot implies sin.x<>0
  proof
    assume x in dom cot;
    then x in dom cos /\ (dom sin \ sin"{0}) by RFUNCT_1:def 4;
    then x in dom sin \ sin"{0} by XBOOLE_0:def 3;
    then x in dom (sin^) by RFUNCT_1:def 8;
    hence thesis by RFUNCT_1:13;
  end;

:: ((f1/f2).x) #Z n = (f1.x) #Z n/(f2.x) #Z n
theorem Th3:
  Z c= dom (f1/f2)
  implies for x st x in Z holds ((f1/f2).x) #Z n = (f1.x) #Z n/(f2.x) #Z n
  proof
    assume
A1: Z c= dom (f1/f2);
    let x;
    assume x in Z;
    then ((f1/f2).x) #Z n = (f1.x/f2.x) #Z n by A1,RFUNCT_1:def 4
    .=(f1.x/f2.x) |^ n by PREPOWER:46
    .=(f1.x) |^ n/(f2.x) |^ n by PREPOWER:15
    .=(f1.x) #Z n/(f2.x) |^ n by PREPOWER:46
    .=(f1.x) #Z n/(f2.x) #Z n by PREPOWER:46;
    hence thesis;
  end;

::  f.x=(x+a)/(x-b)
theorem
   Z c= dom (f1/f2) &
  (for x st x in Z holds f1.x=x+a & f2.x=x-b) implies
  (f1/f2) is_differentiable_on Z & for x st x in Z holds
  ((f1/f2)`|Z).x = (-a-b)/(x-b)^2
  proof
    assume
A1: Z c= dom (f1/f2) &
    (for x st x in Z holds f1.x=x+a & f2.x=x-b);
    then Z c= dom f1 /\ (dom f2 \ f2"{0}) by RFUNCT_1:def 4;
then A2: Z c= dom f1 & Z c= dom f2 \ f2"{0} by XBOOLE_1:18;
     for x st x in Z holds f2.x<>0
    proof
      let x;
      assume x in Z;
      then x in dom f2 \ f2"{0} by A2;
      then x in dom (f2^) by RFUNCT_1:def 8;
      hence thesis by RFUNCT_1:13;
    end;
    then for x st x in Z holds f1.x=x-(-a) & f2.x=x-b & f2.x<>0 by A1;
    hence thesis by A1,FDIFF_5:3;
  end;

Lm1: right_open_halfline 0 = {g where g is Real : 0<g} by LIMFUNC1:152;

:: f.x=ln(1/x)
theorem
   Z c= dom (ln*(f^)) &
  (for x st x in Z holds f.x = x)
  implies (ln*(f^)) is_differentiable_on Z &
  for x st x in Z holds ((ln*(f^))`|Z).x = -1/x
  proof
    assume that
A1: Z c= dom (ln*(f^)) and
A2: for x st x in Z holds f.x = x;
     dom (ln*(f^)) c= dom(f^) by RELAT_1:44;
then A3: Z c= dom (f^) by A1,XBOOLE_1:1;
     dom (f^) c= dom f by RFUNCT_1:11;
then A4: Z c= dom f by A3,XBOOLE_1:1;
A5: for x st x in Z holds f^.x>0
    proof
      let x;
      assume A6:x in Z;
then A7:   f^.x in right_open_halfline(0) by A1,FUNCT_1:21,TAYLOR_1:18;
A8:   f^.x=(f.x)" by A3,A6,RFUNCT_1:def 8
         .=1/x by A2,A6;
      then ex g being Real st (1/x)=g & 0<g by A7,Lm1;
      hence thesis by A8;
    end;
A9: for x st x in Z holds f.x>0
    proof
      let x;
      assume A10:x in Z;
      then f^.x>0 by A5;
      then (f.x)">0 by A3,A10,RFUNCT_1:def 8;
      hence thesis by XREAL_1:124;
    end;
then A11:for x st x in Z holds f.x=x & f.x<>0 by A2;
then A12: f^ is_differentiable_on Z &
    for x st x in Z holds ((f^)`|Z).x = -1/x^2 by A4,FDIFF_5:4;
A13: for x st x in Z holds
    (ln *(f^)) is_differentiable_in x &
    diff((ln *(f^)),x) = diff(f^,x)/(f^.x)
    proof
      let x;
      assume A14:x in Z;
then A15:   f^ is_differentiable_in x by A12,FDIFF_1:16;
       f^.x>0 by A5,A14;
      hence thesis by A15,TAYLOR_1:20;
    end;
then A16: for x st x in Z holds
    (ln *(f^)) is_differentiable_in x;
then A17:(ln *(f^)) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((ln*(f^))`|Z).x = -1/x
    proof
      let x;
      assume A18:x in Z;
      then f.x<>0 by A9;
then A19:   x<>0 by A2,A18;
       diff((ln *(f^)),x) = diff(f^,x)/(f^.x) by A13,A18
      .= (((f^)`|Z).x)/(f^.x) by A12,A18,FDIFF_1:def 8
      .= (((f^)`|Z).x)/((f.x)") by A3,A18,RFUNCT_1:def 8
      .= (((f^)`|Z).x)/(1*x") by A2,A18
      .= (-1/x^2)/(1*x") by A4,A11,A18,FDIFF_5:4
      .= -(x/x^2)
      .= -(x/x/x) by XCMPLX_1:79
      .= -1/x by A19,XCMPLX_1:60;
      hence thesis by A17,A18,FDIFF_1:def 8;
    end;
    hence thesis by A1,A16,FDIFF_1:16;
  end;

:: f.x = tan.(a*x+b)
theorem Th6:
  Z c= dom (tan*f) &
  (for x st x in Z holds f.x = a*x+b)
  implies tan*f is_differentiable_on Z &
  for x st x in Z holds ((tan*f)`|Z).x = a/(cos.(a*x+b))^2
  proof
    assume that
A1: Z c= dom (tan*f) and
A2: (for x st x in Z holds f.x = a*x+b);
A3:  for x st x in Z holds cos.(f.x)<>0
    proof
      let x;
      assume x in Z;
      then f.x in dom (sin/cos) by A1,FUNCT_1:21;
      hence thesis by Th1;
    end;
     dom(tan*f) c= dom f by RELAT_1:44;
    then A4: Z c= dom f by A1,XBOOLE_1:1;
then A5: f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = a by A2,FDIFF_1:31;
A6: for x st x in Z holds tan*f is_differentiable_in x
    proof
      let x;
      assume A7:x in Z;
then A8:   f is_differentiable_in x by A5,FDIFF_1:16;
       cos.(f.x)<>0 by A3,A7;
      then tan is_differentiable_in f.x by FDIFF_7:46;
      hence tan*f is_differentiable_in x by A8,FDIFF_2:13;
    end;
then A9: tan*f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((tan*f)`|Z).x = a/(cos.(a*x+b))^2
    proof
      let x;
      assume A10:x in Z;
then A11:   f is_differentiable_in x by A5,FDIFF_1:16;
A12:   cos.(f.x)<>0 by A3,A10;
      then tan is_differentiable_in f.x by FDIFF_7:46;
      then diff(tan*f,x) = diff(tan, f.x)*diff(f,x) by A11,FDIFF_2:13
      .=(1/(cos.(f.x))^2) * diff(f,x) by A12,FDIFF_7:46
      .=diff(f,x)/(cos.(a*x+b))^2 by A2,A10
      .=(f`|Z).x/(cos.(a*x+b))^2 by A5,A10,FDIFF_1:def 8
      .=a/(cos.(a*x+b))^2 by A2,A4,A10,FDIFF_1:31;
      hence thesis by A9,A10,FDIFF_1:def 8;
    end;
    hence thesis by A1,A6,FDIFF_1:16;
  end;

:: f.x = cot.(a*x+b)
theorem Th7:
  Z c= dom (cot*f) &
  (for x st x in Z holds f.x = a*x+b)
  implies cot*f is_differentiable_on Z &
  for x st x in Z holds ((cot*f)`|Z).x = -a/(sin.(a*x+b))^2
  proof
    assume that
A1: Z c= dom (cot*f) and
A2: (for x st x in Z holds f.x = a*x+b);
A3:  for x st x in Z holds sin.(f.x)<>0
    proof
      let x;
      assume x in Z;
      then f.x in dom (cos/sin) by A1,FUNCT_1:21;
      hence thesis by Th2;
    end;
     dom(cot*f) c= dom f by RELAT_1:44;
then A4: Z c= dom f by A1,XBOOLE_1:1;
then A5: f is_differentiable_on Z &
    for x st x in Z holds (f`|Z).x = a by A2,FDIFF_1:31;
A6: for x st x in Z holds cot*f is_differentiable_in x
    proof
      let x;
      assume A7:x in Z;
then A8:   f is_differentiable_in x by A5,FDIFF_1:16;
       sin.(f.x)<>0 by A3,A7;
      then cot is_differentiable_in f.x by FDIFF_7:47;
      hence cot*f is_differentiable_in x by A8,FDIFF_2:13;
    end;
then A9: cot*f is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((cot*f)`|Z).x = -a/(sin.(a*x+b))^2
    proof
      let x;
      assume A10:x in Z;
then A11:   f is_differentiable_in x by A5,FDIFF_1:16;
A12:   sin.(f.x)<>0 by A3,A10;
      then cot is_differentiable_in f.x by FDIFF_7:47;
      then diff(cot*f,x) = diff(cot, f.x)*diff(f,x) by A11,FDIFF_2:13
      .=(-1/(sin.(f.x))^2) * diff(f,x) by A12,FDIFF_7:47
      .=-diff(f,x)/(sin.(f.x))^2
      .=-diff(f,x)/(sin.(a*x+b))^2 by A2,A10
      .=-(f`|Z).x/(sin.(a*x+b))^2 by A5,A10,FDIFF_1:def 8
      .=-a/(sin.(a*x+b))^2 by A2,A4,A10,FDIFF_1:31;
      hence thesis by A9,A10,FDIFF_1:def 8;
    end;
    hence thesis by A1,A6,FDIFF_1:16;
  end;

:: f.x = tan.(1/x)
theorem
   Z c= dom (tan*(f^)) &
  (for x st x in Z holds f.x = x)
  implies (tan*(f^)) is_differentiable_on Z &
  for x st x in Z holds ((tan*(f^))`|Z).x = -1/(x^2*(cos.(1/x))^2)
  proof
    assume that
A1: Z c= dom (tan*(f^)) and
A2: for x st x in Z holds f.x = x;
     dom (tan*(f^)) c= dom (f^) by RELAT_1:44;
then A3: Z c= dom (f^) by A1,XBOOLE_1:1;
     dom (f^) c= dom f by RFUNCT_1:11;
then A4: Z c= dom f by A3,XBOOLE_1:1;
A5: for x st x in Z holds f.x=x & f.x<>0 by A2,A3,RFUNCT_1:13;
then A6: f^ is_differentiable_on Z &
    for x st x in Z holds ((f^)`|Z).x = -1/x^2 by A4,FDIFF_5:4;
A7: for x st x in Z holds (cos.((f^).x))<>0
    proof
      let x;
      assume x in Z;
      then f^.x in dom (sin/cos) by A1,FUNCT_1:21;
      hence thesis by Th1;
    end;
A8: for x st x in Z holds tan*(f^) is_differentiable_in x
    proof
      let x;
      assume A9:x in Z;
then A10:   f^ is_differentiable_in x by A6,FDIFF_1:16;
       (cos.((f^).x))<>0 by A7,A9;
      then tan is_differentiable_in (f^).x by FDIFF_7:46;
      hence tan*(f^) is_differentiable_in x by A10,FDIFF_2:13;
    end;
then A11: tan*(f^) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((tan*(f^))`|Z).x = -1/(x^2*(cos.(1/x))^2)
    proof
      let x;
      assume A12:x in Z;
then A13:   f^ is_differentiable_in x by A6,FDIFF_1:16;
A14:   cos.((f^).x)<>0 by A7,A12;
      then tan is_differentiable_in (f^).x by FDIFF_7:46;
      then diff(tan*(f^),x) = diff(tan,(f^).x)*diff((f^),x) by A13,FDIFF_2:13
      .=(1/(cos.((f^).x))^2) * diff((f^),x) by A14,FDIFF_7:46
      .=diff((f^),x)/(cos.((f.x)"))^2 by A3,A12,RFUNCT_1:def 8
      .=diff((f^),x)/(cos.(1*x"))^2 by A2,A12
      .=((f^)`|Z).x/(cos.(1*x"))^2 by A6,A12,FDIFF_1:def 8
      .=(-1/x^2)/(cos.(1*x"))^2 by A4,A5,A12,FDIFF_5:4
      .=(-1)/x^2/(cos.(1/x))^2
      .=(-1)/(x^2*(cos.(1/x))^2) by XCMPLX_1:79
      .=-1/(x^2*(cos.(1/x))^2);
      hence thesis by A11,A12,FDIFF_1:def 8;
    end;
    hence thesis by A1,A8,FDIFF_1:16;
  end;

:: f.x = cot.(1/x)
theorem
   Z c= dom (cot*(f^)) &
  (for x st x in Z holds f.x = x)
  implies (cot*(f^)) is_differentiable_on Z &
  for x st x in Z holds ((cot*(f^))`|Z).x = 1/(x^2*(sin.(1/x))^2)
  proof
    assume that
A1: Z c= dom (cot*(f^)) and
A2: for x st x in Z holds f.x = x;
     dom (cot*(f^)) c= dom (f^) by RELAT_1:44;
then A3: Z c= dom (f^) by A1,XBOOLE_1:1;
     dom (f^) c= dom f by RFUNCT_1:11;
then A4: Z c= dom f by A3,XBOOLE_1:1;
A5: for x st x in Z holds f.x=x & f.x<>0 by A2,A3,RFUNCT_1:13;
then A6: f^ is_differentiable_on Z &
    for x st x in Z holds ((f^)`|Z).x = -1/x^2 by A4,FDIFF_5:4;
A7: for x st x in Z holds (sin.((f^).x))<>0
    proof
      let x;
      assume x in Z;
      then f^.x in dom (cos/sin) by A1,FUNCT_1:21;
      hence thesis by Th2;
    end;
A8: for x st x in Z holds cot*(f^) is_differentiable_in x
    proof
      let x;
      assume A9:x in Z;
then A10:   f^ is_differentiable_in x by A6,FDIFF_1:16;
       (sin.((f^).x))<>0 by A7,A9;
      then cot is_differentiable_in (f^).x by FDIFF_7:47;
      hence cot*(f^) is_differentiable_in x by A10,FDIFF_2:13;
    end;
then A11: cot*(f^) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((cot*(f^))`|Z).x = 1/(x^2*(sin.(1/x))^2)
    proof
      let x;
      assume A12:x in Z;
then A13:   f^ is_differentiable_in x by A6,FDIFF_1:16;
A14:   sin.((f^).x)<>0 by A7,A12;
      then cot is_differentiable_in (f^).x by FDIFF_7:47;
      then diff(cot*(f^),x) = diff(cot,(f^).x)*diff((f^),x) by A13,FDIFF_2:13
      .=(-1/(sin.((f^).x))^2) * diff((f^),x) by A14,FDIFF_7:47
      .=-diff((f^),x)/(sin.((f^).x))^2
      .=-diff((f^),x)/(sin.((f.x)"))^2 by A3,A12,RFUNCT_1:def 8
      .=-diff((f^),x)/(sin.(1*x"))^2 by A2,A12
      .=-((f^)`|Z).x/(sin.(1*x"))^2 by A6,A12,FDIFF_1:def 8
      .=-(-1/x^2)/(sin.(1*x"))^2 by A4,A5,A12,FDIFF_5:4
      .=-(-1)/x^2/(sin.(1/x))^2
      .=-(-1)/(x^2*(sin.(1/x))^2) by XCMPLX_1:79
      .=1/(x^2*(sin.(1/x))^2);
      hence thesis by A11,A12,FDIFF_1:def 8;
    end;
    hence thesis by A1,A8,FDIFF_1:16;
  end;

:: f.x = tan.(a+b*x+c*x^2)
theorem
   Z c= dom (tan*(f1+c(#)f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x = a+b*x)
  implies tan*(f1+c(#)f2) is_differentiable_on Z &
  for x st x in Z holds
  ((tan*(f1+c(#)f2))`|Z).x = (b+2*c*x)/(cos.(a+b*x+c*x^2))^2
  proof
    assume that
A1: Z c= dom (tan*(f1+c(#)f2)) & f2=#Z 2 and
A2: (for x st x in Z holds f1.x = a+b*x);
     dom (tan*(f1+c(#)f2)) c= dom (f1+c(#)f2) by RELAT_1:44;
then A3: Z c= dom (f1+c(#)f2) by A1,XBOOLE_1:1;
    then Z c= dom f1 /\ dom (c(#)f2) by SEQ_1:def 3;
then A4: Z c= dom f1 & Z c= dom (c(#)f2) by XBOOLE_1:18;
A5: (f1+c(#)f2) is_differentiable_on Z &
    for x st x in Z holds ((f1+c(#)f2)`|Z).x = b+2*c*x by A1,A2,A3,FDIFF_4:12;
A6: for x st x in Z holds cos.((f1+c(#)f2).x)<>0
    proof
      let x;
      assume x in Z;
      then (f1+c(#)f2).x in dom (sin/cos) by A1,FUNCT_1:21;
      hence thesis by Th1;
    end;
A7: for x st x in Z holds tan*(f1+c(#)f2) is_differentiable_in x
    proof
      let x;
      assume A8:x in Z;
then A9:   (f1+c(#)f2) is_differentiable_in x by A5,FDIFF_1:16;
       cos.((f1+c(#)f2).x)<>0 by A6,A8;
      then tan is_differentiable_in (f1+c(#)f2).x by FDIFF_7:46;
      hence tan*(f1+c(#)f2) is_differentiable_in x by A9,FDIFF_2:13;
    end;
then A10: tan*(f1+c(#)f2) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds
    ((tan*(f1+c(#)f2))`|Z).x = (b+2*c*x)/(cos.(a+b*x+c*x^2))^2
    proof
      let x;
      assume A11:x in Z;
then A12:   (f1+c(#)f2) is_differentiable_in x by A5,FDIFF_1:16;
A13:  ( f1+c(#)f2).x=f1.x+(c(#)f2).x by A3,A11,SEQ_1:def 3
      .=f1.x +c*f2.x by A4,A11,SEQ_1:def 6
      .=a+b*x +c*(f2.x) by A2,A11
      .=a+b*x +c*(x #Z 2) by A1,TAYLOR_1:def 1
      .=a+b*x +c*(x |^2) by PREPOWER:46
      .=a+b*x+c*x^2 by NEWTON:100;
A14:   cos.((f1+c(#)f2).x)<>0 by A6,A11;
      then tan is_differentiable_in (f1+c(#)f2).x by FDIFF_7:46;
      then diff(tan*(f1+c(#)f2),x)
       = diff(tan, (f1+c(#)f2).x)*diff((f1+c(#)f2),x) by A12,FDIFF_2:13
      .=(1/(cos.((f1+c(#)f2).x))^2)*diff((f1+c(#)f2),x) by A14,FDIFF_7:46
      .=((f1+c(#)f2)`|Z).x/(cos.(a+b*x+c*x^2))^2 by A5,A11,A13,FDIFF_1:def 8
      .=(b+2*c*x)/(cos.(a+b*x+c*x^2))^2 by A1,A2,A3,A11,FDIFF_4:12;
      hence thesis by A10,A11,FDIFF_1:def 8;
    end;
    hence thesis by A1,A7,FDIFF_1:16;
  end;

:: f.x = cot.(a+b*x+c*x^2)
theorem
   Z c= dom (cot*(f1+c(#)f2)) & f2=#Z 2 &
  (for x st x in Z holds f1.x = a+b*x)
  implies cot*(f1+c(#)f2) is_differentiable_on Z &
  for x st x in Z holds
  ((cot*(f1+c(#)f2))`|Z).x = -(b+2*c*x)/(sin.(a+b*x+c*x^2))^2
  proof
    assume that
A1: Z c= dom (cot*(f1+c(#)f2)) & f2=#Z 2 and
A2: (for x st x in Z holds f1.x = a+b*x);
     dom (cot*(f1+c(#)f2)) c= dom (f1+c(#)f2) by RELAT_1:44;
then A3: Z c= dom (f1+c(#)f2) by A1,XBOOLE_1:1;
    then Z c= dom f1 /\ dom (c(#)f2) by SEQ_1:def 3;
then A4: Z c= dom f1 & Z c= dom (c(#)f2) by XBOOLE_1:18;
A5: (f1+c(#)f2) is_differentiable_on Z &
    for x st x in Z holds ((f1+c(#)f2)`|Z).x = b+2*c*x by A1,A2,A3,FDIFF_4:12;
A6: for x st x in Z holds sin.((f1+c(#)f2).x)<>0
    proof
      let x;
      assume x in Z;
      then (f1+c(#)f2).x in dom (cos/sin) by A1,FUNCT_1:21;
      hence thesis by Th2;
    end;
A7: for x st x in Z holds cot*(f1+c(#)f2) is_differentiable_in x
    proof
      let x;
      assume A8:x in Z;
then A9:   (f1+c(#)f2) is_differentiable_in x by A5,FDIFF_1:16;
       sin.((f1+c(#)f2).x)<>0 by A6,A8;
      then cot is_differentiable_in (f1+c(#)f2).x by FDIFF_7:47;
      hence cot*(f1+c(#)f2) is_differentiable_in x by A9,FDIFF_2:13;
    end;
then A10: cot*(f1+c(#)f2) is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds
    ((cot*(f1+c(#)f2))`|Z).x = -(b+2*c*x)/(sin.(a+b*x+c*x^2))^2
    proof
      let x;
      assume A11:x in Z;
then A12:   (f1+c(#)f2) is_differentiable_in x by A5,FDIFF_1:16;
A13:  (f1+c(#)f2).x=f1.x+(c(#)f2).x by A3,A11,SEQ_1:def 3
      .=f1.x +c*f2.x by A4,A11,SEQ_1:def 6
      .=a+b*x +c*(f2.x) by A2,A11
      .=a+b*x +c*(x #Z 2) by A1,TAYLOR_1:def 1
      .=a+b*x +c*(x |^2) by PREPOWER:46
      .=a+b*x+c*x^2 by NEWTON:100;
A14:   sin.((f1+c(#)f2).x)<>0 by A6,A11;
      then cot is_differentiable_in (f1+c(#)f2).x by FDIFF_7:47;
      then diff(cot*(f1+c(#)f2),x)
       = diff(cot, (f1+c(#)f2).x)*diff((f1+c(#)f2),x) by A12,FDIFF_2:13
      .=(-1/(sin.((f1+c(#)f2).x))^2)*diff((f1+c(#)f2),x) by A14,FDIFF_7:47
      .=-diff((f1+c(#)f2),x)/(sin.(a+b*x+c*x^2))^2 by A13
      .=-((f1+c(#)f2)`|Z).x/(sin.(a+b*x+c*x^2))^2 by A5,A11,FDIFF_1:def 8
      .=-(b+2*c*x)/(sin.(a+b*x+c*x^2))^2 by A1,A2,A3,A11,FDIFF_4:12;
      hence thesis by A10,A11,FDIFF_1:def 8;
    end;
    hence thesis by A1,A7,FDIFF_1:16;
  end;

:: f.x = tan.(exp_R.x)
theorem
   Z c= dom (tan*exp_R)
  implies tan*exp_R is_differentiable_on Z &
  for x st x in Z holds ((tan*exp_R)`|Z).x = exp_R.x/(cos.(exp_R.x))^2
  proof
    assume
A1: Z c= dom (tan*exp_R);
A2: for x st x in Z holds cos.(exp_R.x)<>0
    proof
      let x;
      assume x in Z;
      then exp_R.x in dom (sin/cos) by A1,FUNCT_1:21;
      hence thesis by Th1;
    end;
A3: for x st x in Z holds tan*exp_R is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
A5:   exp_R is_differentiable_in x by SIN_COS:70;
       cos.(exp_R.x)<>0 by A2,A4;
      then tan is_differentiable_in exp_R.x by FDIFF_7:46;
      hence tan*exp_R is_differentiable_in x by A5,FDIFF_2:13;
    end;
then A6: tan*exp_R is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((tan*exp_R)`|Z).x = exp_R.x/(cos.(exp_R.x))^2
    proof
      let x;
      assume A7:x in Z;
A8:   exp_R is_differentiable_in x by SIN_COS:70;
A9:   cos.(exp_R.x)<>0 by A2,A7;
      then tan is_differentiable_in exp_R.x by FDIFF_7:46;
      then diff(tan*exp_R,x)
       = diff(tan, exp_R.x)*diff(exp_R,x) by A8,FDIFF_2:13
      .=(1/(cos.(exp_R.x))^2) * diff(exp_R,x) by A9,FDIFF_7:46
      .=exp_R.x/(cos.(exp_R.x))^2 by SIN_COS:70;
      hence thesis by A6,A7,FDIFF_1:def 8;
    end;
    hence thesis by A1,A3,FDIFF_1:16;
  end;

:: f.x = cot.(exp_R.x)
theorem
   Z c= dom (cot*exp_R)
  implies cot*exp_R is_differentiable_on Z &
  for x st x in Z holds ((cot*exp_R)`|Z).x = -exp_R.x/(sin.(exp_R.x))^2
  proof
    assume
A1: Z c= dom (cot*exp_R);
A2: for x st x in Z holds sin.(exp_R.x)<>0
    proof
      let x;
      assume x in Z;
      then exp_R.x in dom (cos/sin) by A1,FUNCT_1:21;
      hence thesis by Th2;
    end;
A3: for x st x in Z holds cot*exp_R is_differentiable_in x
    proof
      let x;
      assume A4:x in Z;
A5:   exp_R is_differentiable_in x by SIN_COS:70;
       sin.(exp_R.x)<>0 by A2,A4;
      then cot is_differentiable_in exp_R.x by FDIFF_7:47;
      hence cot*exp_R is_differentiable_in x by A5,FDIFF_2:13;
    end;
then A6: cot*exp_R is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((cot*exp_R)`|Z).x = -exp_R.x/(sin.(exp_R.x))^2
    proof
      let x;
      assume A7:x in Z;
A8:   exp_R is_differentiable_in x by SIN_COS:70;
A9:   sin.(exp_R.x)<>0 by A2,A7;
      then cot is_differentiable_in exp_R.x by FDIFF_7:47;
      then diff(cot*exp_R,x)
       = diff(cot, exp_R.x)*diff(exp_R,x) by A8,FDIFF_2:13
      .=(-1/(sin.(exp_R.x))^2) * diff(exp_R,x) by A9,FDIFF_7:47
      .=-diff(exp_R,x)/(sin.(exp_R.x))^2
      .=-exp_R.x/(sin.(exp_R.x))^2 by SIN_COS:70;
      hence thesis by A6,A7,FDIFF_1:def 8;
    end;
    hence thesis by A1,A3,FDIFF_1:16;
  end;

:: f.x = tan.(ln.x)
theorem
   Z c= dom (tan*ln)
  implies tan*ln is_differentiable_on Z &
  for x st x in Z holds
  ((tan*ln)`|Z).x = 1/(x*(cos.(ln.x))^2)
  proof
    assume
A1: Z c= dom (tan*ln);
     dom (tan*ln) c= dom ln by RELAT_1:44;
then A2: Z c= dom ln by A1,XBOOLE_1:1;
A3: for x st x in Z holds x>0
    proof
      let x;
      assume x in Z;
      then x in right_open_halfline(0) by A2,TAYLOR_1:18;
      then ex g being Real st x=g & 0<g by Lm1;
      hence thesis;
    end;
A4: for x st x in Z holds cos.(ln.x)<>0
    proof
      let x;
      assume x in Z;
      then ln.x in dom (sin/cos) by A1,FUNCT_1:21;
      hence thesis by Th1;
    end;
A5: for x st x in Z holds ln is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then x>0 by A3;
      hence thesis by TAYLOR_1:18;
    end;
A6: for x st x in Z holds tan*ln is_differentiable_in x
    proof
      let x;
      assume A7:x in Z;
then A8:   ln is_differentiable_in x by A5;
       x>0 & cos.(ln.x)<>0 by A3,A4,A7;
      then tan is_differentiable_in ln.x by FDIFF_7:46;
      hence tan*ln is_differentiable_in x by A8,FDIFF_2:13;
    end;
then A9: tan*ln is_differentiable_on Z by A1,FDIFF_1:16;
A10: for x st x in Z holds diff(ln,x) = 1/x
    proof
      let x;
      assume x in Z;
      then x>0 by A3;
      then x in right_open_halfline(0) by Lm1;
      hence thesis by TAYLOR_1:18;
    end;
     for x st x in Z holds
    ((tan*ln)`|Z).x = 1/(x*(cos.(ln.x))^2)
    proof
      let x;
      assume A11:x in Z;
then A12:   ln is_differentiable_in x by A5;
A13:   x>0 & cos.(ln.x)<>0 by A3,A4,A11;
      then tan is_differentiable_in ln.x by FDIFF_7:46;
      then diff(tan*ln,x) = diff(tan,ln.x)*diff(ln,x) by A12,FDIFF_2:13
      .=(1/(cos.(ln.x))^2) * diff(ln,x) by A13,FDIFF_7:46
      .=(1/x)/(cos.(ln.x))^2 by A10,A11
      .=1/(x*(cos.(ln.x))^2) by XCMPLX_1:79;
      hence thesis by A9,A11,FDIFF_1:def 8;
    end;
    hence thesis by A1,A6,FDIFF_1:16;
  end;

:: f.x = cot.(ln.x)
theorem
   Z c= dom (cot*ln)
  implies cot*ln is_differentiable_on Z &
  for x st x in Z holds
  ((cot*ln)`|Z).x = -1/(x*(sin.(ln.x))^2)
  proof
    assume
A1: Z c= dom (cot*ln);
     dom (cot*ln) c= dom ln by RELAT_1:44;
then A2: Z c= dom ln by A1,XBOOLE_1:1;
A3: for x st x in Z holds x>0
    proof
      let x;
      assume x in Z;
      then x in right_open_halfline(0) by A2,TAYLOR_1:18;
      then ex g being Real st x=g & 0<g by Lm1;
      hence thesis;
    end;
A4: for x st x in Z holds sin.(ln.x)<>0
    proof
      let x;
      assume x in Z;
      then ln.x in dom (cos/sin) by A1,FUNCT_1:21;
      hence thesis by Th2;
    end;
A5: for x st x in Z holds ln is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then x>0 by A3;
      hence thesis by TAYLOR_1:18;
    end;
A6: for x st x in Z holds cot*ln is_differentiable_in x
    proof
      let x;
      assume A7:x in Z;
then A8:   ln is_differentiable_in x by A5;
       x>0 & sin.(ln.x)<>0 by A3,A4,A7;
      then cot is_differentiable_in ln.x by FDIFF_7:47;
      hence cot*ln is_differentiable_in x by A8,FDIFF_2:13;
    end;
then A9: cot*ln is_differentiable_on Z by A1,FDIFF_1:16;
A10: for x st x in Z holds diff(ln,x) = 1/x
    proof
      let x;
      assume x in Z;
      then x>0 by A3;
      then x in right_open_halfline(0) by Lm1;
      hence thesis by TAYLOR_1:18;
    end;
     for x st x in Z holds
    ((cot*ln)`|Z).x = -1/(x*(sin.(ln.x))^2)
    proof
      let x;
      assume A11:x in Z;
then A12:   ln is_differentiable_in x by A5;
A13:   x>0 & sin.(ln.x)<>0 by A3,A4,A11;
      then cot is_differentiable_in ln.x by FDIFF_7:47;
      then diff(cot*ln,x) = diff(cot,ln.x)*diff(ln,x) by A12,FDIFF_2:13
      .=(-1/(sin.(ln.x))^2) * diff(ln,x) by A13,FDIFF_7:47
      .=-diff(ln,x)/(sin.(ln.x))^2
      .=-(1/x)/(sin.(ln.x))^2 by A10,A11
      .=-1/(x*(sin.(ln.x))^2) by XCMPLX_1:79;
      hence thesis by A9,A11,FDIFF_1:def 8;
    end;
    hence thesis by A1,A6,FDIFF_1:16;
  end;

:: f.x = exp_R.(tan.x)
theorem
   Z c= dom (exp_R*tan)
  implies exp_R*tan is_differentiable_on Z &
  for x st x in Z holds
  ((exp_R*tan)`|Z).x = exp_R.(tan.x)/(cos.x)^2
  proof
    assume
A1: Z c= dom (exp_R*tan);
A2: for x st x in Z holds cos.x<>0
    proof
      let x;
      assume x in Z;
      then x in dom (sin/cos) by A1,FUNCT_1:21;
      hence thesis by Th1;
    end;
A3: for x st x in Z holds exp_R*tan is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A2;
then A4:   tan is_differentiable_in x by FDIFF_7:46;
       exp_R is_differentiable_in tan.x by SIN_COS:70;
      hence exp_R*tan is_differentiable_in x by A4,FDIFF_2:13;
    end;
then A5: exp_R*tan is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds
    ((exp_R*tan)`|Z).x = exp_R.(tan.x)/(cos.x)^2
    proof
      let x;
      assume A6:x in Z;
then A7:   cos.x<>0 by A2;
then A8:   tan is_differentiable_in x by FDIFF_7:46;
       exp_R is_differentiable_in tan.x by SIN_COS:70;
      then diff(exp_R*tan,x) = diff(exp_R,tan.x)*diff(tan,x) by A8,FDIFF_2:13
      .=diff(exp_R,tan.x)*(1/(cos.x)^2) by A7,FDIFF_7:46
      .=exp_R.(tan.x)/(cos.x)^2 by SIN_COS:70;
      hence thesis by A5,A6,FDIFF_1:def 8;
    end;
    hence thesis by A1,A3,FDIFF_1:16;
  end;

:: f.x = exp_R.(cot.x)
theorem
   Z c= dom (exp_R*cot)
  implies exp_R*cot is_differentiable_on Z &
  for x st x in Z holds
  ((exp_R*cot)`|Z).x = -exp_R.(cot.x)/(sin.x)^2
  proof
    assume
A1: Z c= dom (exp_R*cot);
A2: for x st x in Z holds sin.x<>0
    proof
      let x;
      assume x in Z;
      then x in dom (cos/sin) by A1,FUNCT_1:21;
      hence thesis by Th2;
    end;
A3: for x st x in Z holds exp_R*cot is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A2;
then A4:   cot is_differentiable_in x by FDIFF_7:47;
       exp_R is_differentiable_in cot.x by SIN_COS:70;
      hence exp_R*cot is_differentiable_in x by A4,FDIFF_2:13;
    end;
then A5: exp_R*cot is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds
    ((exp_R*cot)`|Z).x = -exp_R.(cot.x)/(sin.x)^2
    proof
      let x;
      assume A6:x in Z;
then A7:   sin.x<>0 by A2;
then A8:   cot is_differentiable_in x by FDIFF_7:47;
       exp_R is_differentiable_in cot.x by SIN_COS:70;
      then diff(exp_R*cot,x) = diff(exp_R,cot.x)*diff(cot,x) by A8,FDIFF_2:13
      .=diff(exp_R,cot.x)*(-1/(sin.x)^2) by A7,FDIFF_7:47
      .=exp_R.(cot.x) * (-1/(sin.x)^2) by SIN_COS:70
      .=-exp_R.(cot.x)/(sin.x)^2;
      hence thesis by A5,A6,FDIFF_1:def 8;
    end;
    hence thesis by A1,A3,FDIFF_1:16;
  end;

:: f.x = ln.(tan.x)
theorem
   Z c= dom (ln*tan)
  implies ln*tan is_differentiable_on Z &
  for x st x in Z holds ((ln*tan)`|Z).x = 1/(cos.x*sin.x)
  proof
    assume
A1: Z c= dom (ln*tan);
A2: for x st x in Z holds cos.x<>0
    proof
      let x;
      assume x in Z;
      then x in dom (sin/cos) by A1,FUNCT_1:21;
      hence thesis by Th1;
    end;
A3: for x st x in Z holds tan.x>0
    proof
      let x;
      assume x in Z;
then   tan.x in right_open_halfline(0) by A1,FUNCT_1:21,TAYLOR_1:18;
      then ex g being Real st tan.x=g & 0<g by Lm1;
      hence thesis;
    end;
     dom (ln*tan) c= dom tan by RELAT_1:44;
then A4: Z c= dom tan by A1,XBOOLE_1:1;
A5: for x st x in Z holds tan is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A2;
      hence thesis by FDIFF_7:46;
    end;
A6: for x st x in Z holds ln*tan is_differentiable_in x
    proof
      let x;
      assume A7:x in Z;
then A8:   tan is_differentiable_in x by A5;
       cos.x<>0 & tan.x>0 by A2,A3,A7;
      hence ln*tan is_differentiable_in x by A8,TAYLOR_1:20;
    end;
then A9: ln*tan is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((ln*tan)`|Z).x =1/(cos.x*sin.x)
    proof
      let x;
      assume A10:x in Z;
then A11:   tan is_differentiable_in x by A5;
A12:  cos.x<>0 & tan.x>0 by A2,A3,A10;
      then diff(ln*tan,x) =diff(tan,x)/(tan.x) by A11,TAYLOR_1:20
      .=(1/(cos.x)^2)/(tan.x) by A12,FDIFF_7:46
      .=1/((cos.x)^2*tan.x) by XCMPLX_1:79
      .=1/((cos.x)^2*(sin.x/cos.x)) by A4,A10,RFUNCT_1:def 4
      .=1/(((cos.x)^2*sin.x)/cos.x)
      .=cos.x/((cos.x)^2*sin.x) by XCMPLX_1:57
      .=cos.x/(cos.x)^2/sin.x by XCMPLX_1:79
      .=cos.x/cos.x/cos.x/sin.x by XCMPLX_1:79
      .=1/cos.x/sin.x by A12,XCMPLX_1:60
      .=1/(cos.x*sin.x) by XCMPLX_1:79;
      hence thesis by A9,A10,FDIFF_1:def 8;
    end;
    hence thesis by A1,A6,FDIFF_1:16;
  end;

:: f.x = ln.(cot.x)
theorem
   Z c= dom (ln*cot)
  implies ln*cot is_differentiable_on Z &
  for x st x in Z holds ((ln*cot)`|Z).x = -1/(sin.x*cos.x)
  proof
    assume
A1: Z c= dom (ln*cot);
A2:  for x st x in Z holds sin.x<>0
    proof
      let x;
      assume x in Z;
      then x in dom (cos/sin) by A1,FUNCT_1:21;
      hence thesis by Th2;
    end;
A3: for x st x in Z holds cot.x>0
    proof
      let x;
      assume x in Z;
then   cot.x in right_open_halfline(0) by A1,FUNCT_1:21,TAYLOR_1:18;
      then ex g being Real st cot.x=g & 0<g by Lm1;
      hence thesis;
    end;
     dom (ln*cot) c= dom cot by RELAT_1:44;
then A4: Z c= dom cot by A1,XBOOLE_1:1;
A5: for x st x in Z holds cot is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A2;
      hence thesis by FDIFF_7:47;
    end;
A6: for x st x in Z holds ln*cot is_differentiable_in x
    proof
      let x;
      assume A7:x in Z;
then A8:   cot is_differentiable_in x by A5;
       sin.x<>0 & cot.x>0 by A2,A3,A7;
      hence ln*cot is_differentiable_in x by A8,TAYLOR_1:20;
    end;
then A9: ln*cot is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds ((ln*cot)`|Z).x =-1/(sin.x*cos.x)
    proof
      let x;
      assume A10:x in Z;
then A11:   cot is_differentiable_in x by A5;
A12:  sin.x<>0 & cot.x>0 by A2,A3,A10;
      then diff(ln*cot,x) =diff(cot,x)/(cot.x) by A11,TAYLOR_1:20
      .=(-1/(sin.x)^2)/(cot.x) by A12,FDIFF_7:47
      .=-1/(sin.x)^2/cot.x
      .=-1/((sin.x)^2*cot.x) by XCMPLX_1:79
      .=-1/((sin.x)^2*(cos.x/sin.x)) by A4,A10,RFUNCT_1:def 4
      .=-1/(((sin.x)^2*cos.x)/sin.x)
      .=-sin.x/((sin.x)^2*cos.x) by XCMPLX_1:57
      .=-sin.x/(sin.x)^2/cos.x by XCMPLX_1:79
      .=-sin.x/sin.x/sin.x/cos.x by XCMPLX_1:79
      .=-1/sin.x/cos.x by A12,XCMPLX_1:60
      .=-1/(sin.x*cos.x) by XCMPLX_1:79;
      hence thesis by A9,A10,FDIFF_1:def 8;
    end;
    hence thesis by A1,A6,FDIFF_1:16;
  end;

:: f.x=(tan.x) #Z n
theorem
   (Z c= dom (( #Z n)*tan) & 1<=n)
  implies ( #Z n)*tan is_differentiable_on Z &
  for x st x in Z holds
  ((( #Z n)*tan)`|Z).x =n*(sin.x) #Z (n-1)/(cos.x) #Z (n+1)
  proof
    assume
A1: Z c= dom (( #Z n)*tan) & 1<=n;
A2: for x st x in Z holds cos.x<>0
    proof
      let x;
      assume x in Z;
      then x in dom (sin/cos) by A1,FUNCT_1:21;
      hence thesis by Th1;
    end;
     dom (( #Z n)*tan) c= dom tan by RELAT_1:44;
then A3: Z c= dom tan by A1,XBOOLE_1:1;
A4: for x st x in Z holds ( #Z n)*tan is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A2;
      then tan is_differentiable_in x by FDIFF_7:46;
      hence ( #Z n)*tan is_differentiable_in x by TAYLOR_1:3;
    end;
then A5: ( #Z n)*tan is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds
    ((( #Z n)*tan)`|Z).x =n*((sin.x) #Z (n-1))/(cos.x) #Z (n+1)
    proof
      let x;
      assume A6:x in Z;
then A7:   cos.x<>0 by A2;
then A8:   tan is_differentiable_in x by FDIFF_7:46;
      consider m being Nat such that
A9:  n = m + 1 by A1,NAT_1:6;
      set m = n-1;
       ((( #Z n)*tan)`|Z).x=diff(( #Z n)*tan,x) by A5,A6,FDIFF_1:def 8
      .=n*(tan.x) #Z (n-1) * diff(tan,x) by A8,TAYLOR_1:3
      .=n*(tan.x) #Z (n-1) *(1/(cos.x)^2) by A7,FDIFF_7:46
      .=n*((sin.x) #Z m /(cos.x) #Z m)/(cos.x)^2 by A3,A6,A9,Th3
      .=n*(sin.x) #Z (n-1)/(cos.x) #Z (n-1)/(cos.x)^2
      .=n*(sin.x) #Z (n-1)/((cos.x) #Z (n-1)*(cos.x)^2) by XCMPLX_1:79
      .=n*((sin.x) #Z (n-1))/((cos.x) #Z (n-1)*(cos.x) #Z 2) by FDIFF_7:1
      .=n*((sin.x) #Z (n-1))/((cos.x) #Z (n-1+2)) by A7,PREPOWER:54
      .=n*((sin.x) #Z (n-1))/(cos.x) #Z (n+1);
      hence thesis;
    end;
    hence thesis by A1,A4,FDIFF_1:16;
  end;

:: f.x=(cot.x) #Z n
theorem
   Z c= dom (( #Z n)*cot) & 1<=n
  implies ( #Z n)*cot is_differentiable_on Z &
  for x st x in Z holds
  ((( #Z n)*cot)`|Z).x =-n*(cos.x) #Z (n-1)/(sin.x) #Z (n+1)
  proof
    assume
A1: Z c= dom (( #Z n)*cot) & 1<=n;
A2: for x st x in Z holds sin.x<>0
    proof
      let x;
      assume x in Z;
      then x in dom (cos/sin) by A1,FUNCT_1:21;
      hence thesis by Th2;
    end;
     dom (( #Z n)*cot) c= dom cot by RELAT_1:44;
then A3: Z c= dom cot by A1,XBOOLE_1:1;
A4: for x st x in Z holds ( #Z n)*cot is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A2;
      then cot is_differentiable_in x by FDIFF_7:47;
      hence ( #Z n)*cot is_differentiable_in x by TAYLOR_1:3;
    end;
then A5: ( #Z n)*cot is_differentiable_on Z by A1,FDIFF_1:16;
     for x st x in Z holds
    ((( #Z n)*cot)`|Z).x =-n*((cos.x) #Z (n-1))/(sin.x) #Z (n+1)
    proof
      let x;
      assume A6:x in Z;
then A7:   sin.x<>0 by A2;
then A8:   cot is_differentiable_in x by FDIFF_7:47;
      consider m being Nat such that
A9:  n = m + 1 by A1,NAT_1:6;
      set m = n-1;
       ((( #Z n)*cot)`|Z).x=diff(( #Z n)*cot,x) by A5,A6,FDIFF_1:def 8
      .=n*(cot.x) #Z (n-1) * diff(cot,x) by A8,TAYLOR_1:3
      .=n*(cot.x) #Z (n-1) *(-1/(sin.x)^2) by A7,FDIFF_7:47
      .=-(n*(cot.x) #Z (n-1))/(sin.x)^2
      .=-n*((cos.x) #Z m/(sin.x) #Z m)/(sin.x)^2 by A3,A6,A9,Th3
      .=-n*(cos.x) #Z (n-1)/(sin.x) #Z (n-1)/(sin.x)^2
      .=-n*(cos.x) #Z (n-1)/((sin.x) #Z (n-1)*(sin.x)^2) by XCMPLX_1:79
      .=-n*((cos.x) #Z (n-1))/((sin.x) #Z (n-1)*(sin.x) #Z 2) by FDIFF_7:1
      .=-n*((cos.x) #Z (n-1))/((sin.x) #Z (n-1+2)) by A7,PREPOWER:54
      .=-n*((cos.x) #Z (n-1))/(sin.x) #Z (n+1);
      hence thesis;
    end;
    hence thesis by A1,A4,FDIFF_1:16;
  end;

:: f.x = tan.x+1/cos.x
theorem
   (Z c= dom (tan+cos^) &
  for x st x in Z holds (1+sin.x)<>0 & (1-sin.x)<>0)
  implies tan+cos^ is_differentiable_on Z &
  for x st x in Z holds ((tan+cos^)`|Z).x = 1/(1-sin.x)
  proof
    assume that
A1: Z c= dom (tan+cos^) and
A2: for x st x in Z holds 1+sin.x<>0 & 1-sin.x<>0;
     Z c= dom tan /\ dom (cos^) by A1,SEQ_1:def 3;
then A3: Z c= dom tan & Z c= dom (cos^) by XBOOLE_1:18;
then A4: for x st x in Z holds cos.x<>0 by Th1;
then A5: cos^ is_differentiable_on Z by FDIFF_4:39;
     for x st x in Z holds tan is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A3,Th1;
      hence thesis by FDIFF_7:46;
    end;
then A6: tan is_differentiable_on Z by A3,FDIFF_1:16;
     for x st x in Z holds ((tan+cos^)`|Z).x = 1/(1-sin.x)
    proof
      let x;
      assume A7:x in Z;
then A8:   cos.x<>0 & 1+sin.x<>0 & 1-sin.x<>0 by A2,A3,Th1;
       ((tan+cos^)`|Z).x = diff(tan,x) + diff(cos^,x) by A1,A5,A6,A7,FDIFF_1:26
      .=1/(cos.x)^2 + diff(cos^,x) by A8,FDIFF_7:46
      .=1/(cos.x)^2 + ((cos^)`|Z).x by A5,A7,FDIFF_1:def 8
      .=1/(cos.x)^2 + sin.x/(cos.x)^2 by A4,A7,FDIFF_4:39
      .=(1+sin.x)/((cos.x)^2+(sin.x)^2-(sin.x)^2)
      .=(1+sin.x)/(1-(sin.x)^2) by SIN_COS:31
      .=(1+sin.x)/((1+sin.x)*(1-sin.x))
      .=(1+sin.x)/(1+sin.x)/(1-sin.x) by XCMPLX_1:79
      .=1/(1-sin.x) by A8,XCMPLX_1:60;
      hence thesis;
    end;
    hence thesis by A1,A5,A6,FDIFF_1:26;
  end;

:: f.x = tan.x-1/cos.x
theorem
   (Z c= dom (tan-cos^) &
  for x st x in Z holds (1-sin.x)<>0 & (1+sin.x)<>0)
  implies tan-cos^ is_differentiable_on Z &
  for x st x in Z holds ((tan-cos^)`|Z).x = 1/(1+sin.x)
  proof
    assume that
A1: Z c= dom (tan-cos^) and
A2: for x st x in Z holds (1-sin.x)<>0 & (1+sin.x)<>0;
     Z c= dom tan /\ dom (cos^) by A1,SEQ_1:def 4;
then A3: Z c= dom tan & Z c= dom (cos^) by XBOOLE_1:18;
then A4: for x st x in Z holds cos.x<>0 by Th1;
then A5: cos^ is_differentiable_on Z by FDIFF_4:39;
     for x st x in Z holds tan is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A3,Th1;
      hence thesis by FDIFF_7:46;
    end;
then A6: tan is_differentiable_on Z by A3,FDIFF_1:16;
     for x st x in Z holds ((tan-cos^)`|Z).x = 1/(1+sin.x)
    proof
      let x;
      assume A7:x in Z;
then A8:   cos.x<>0 & (1-sin.x)<>0 & (1+sin.x)<>0 by A2,A3,Th1;
       ((tan-cos^)`|Z).x =
      diff(tan,x) - diff(cos^,x) by A1,A5,A6,A7,FDIFF_1:27
      .=1/(cos.x)^2 - diff(cos^,x) by A8,FDIFF_7:46
      .=1/(cos.x)^2 - ((cos^)`|Z).x by A5,A7,FDIFF_1:def 8
      .=1/(cos.x)^2 - sin.x/(cos.x)^2 by A4,A7,FDIFF_4:39
      .=(1-sin.x)/((cos.x)^2+(sin.x)^2-(sin.x)^2)
      .=(1-sin.x)/(1-(sin.x)^2) by SIN_COS:31
      .=(1-sin.x)/((1-sin.x)*(1+sin.x))
      .=(1-sin.x)/(1-sin.x)/(1+sin.x) by XCMPLX_1:79
      .=1/(1+sin.x) by A8,XCMPLX_1:60;
      hence thesis;
    end;
    hence thesis by A1,A5,A6,FDIFF_1:27;
  end;

:: f.x = tan.x-x
theorem
   Z c= dom (tan-id Z)
  implies tan-id Z is_differentiable_on Z &
  for x st x in Z holds ((tan-id Z)`|Z).x=(sin.x)^2/(cos.x)^2
  proof
    assume
A1: Z c= dom (tan-id Z);
    then Z c= dom tan /\ dom (id Z) by SEQ_1:def 4;
then A2: Z c= dom tan & Z c= dom (id Z) by XBOOLE_1:18;
A3: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
then A4: id Z is_differentiable_on Z &  for x st x in Z holds
    ((id Z)`|Z).x = 1 by A2,FDIFF_1:31;
     for x st x in Z holds tan is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A2,Th1;
      hence thesis by FDIFF_7:46;
    end;
then A5: tan is_differentiable_on Z by A2,FDIFF_1:16;
     for x st x in Z holds ((tan-id Z)`|Z).x=(sin.x)^2/(cos.x)^2
    proof
      let x;
      assume A6:x in Z;
then A7:   cos.x<>0 by A2,Th1;
then A8:   (cos.x)^2 >0 by SQUARE_1:74;
       ((tan-id Z)`|Z).x=
      diff(tan,x) - diff(id Z,x) by A1,A4,A5,A6,FDIFF_1:27
      .=1/(cos.x)^2-diff(id Z,x) by A7,FDIFF_7:46
      .=1/(cos.x)^2-((id Z)`|Z).x by A4,A6,FDIFF_1:def 8
      .=1/(cos.x)^2-1 by A2,A3,A6,FDIFF_1:31
      .=1/(cos.x)^2-(cos.x)^2/(cos.x)^2 by A8,XCMPLX_1:60
      .=(1-(cos.x)^2)/(cos.x)^2
      .=((sin.x)^2+(cos.x)^2-(cos.x)^2)/(cos.x)^2 by SIN_COS:31
      .=(sin.x)^2/(cos.x)^2;
      hence thesis;
    end;
    hence thesis by A1,A4,A5,FDIFF_1:27;
  end;

:: f.x = -cot.x-x
theorem
   Z c= dom (-cot-id Z)
  implies -cot-id Z is_differentiable_on Z &
  for x st x in Z holds ((-cot-id Z)`|Z).x=(cos.x)^2/(sin.x)^2
  proof
    assume
A1: Z c= dom (-cot-id Z);
    then Z c= dom (-cot) /\ dom (id Z) by SEQ_1:def 4;
then A2: Z c= dom (-cot) & Z c= dom (id Z) by XBOOLE_1:18;
then A3: Z c= dom ((-1)(#)cot) by RFUNCT_1:38;
A4: Z c= dom cot by A2,SEQ_1:def 7;
A5: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
then A6: id Z is_differentiable_on Z &  for x st x in Z holds
    ((id Z)`|Z).x = 1 by A2,FDIFF_1:31;
     for x st x in Z holds cot is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A4,Th2;
      hence thesis by FDIFF_7:47;
    end;
then A7: cot is_differentiable_on Z by A4,FDIFF_1:16;
    then (-1)(#)cot is_differentiable_on Z &
    for x st x in Z holds
    (((-1)(#)cot)`|Z).x = (-1)*diff(cot,x) by A3,FDIFF_1:28;
then A8: -cot is_differentiable_on Z by RFUNCT_1:38;
    set f = -cot;
     for x st x in Z holds ((-cot-id Z)`|Z).x=(cos.x)^2/(sin.x)^2
    proof
      let x;
      assume A9:x in Z;
then A10:   sin.x<>0 by A4,Th2;
then A11:   (sin.x)^2 >0 by SQUARE_1:74;
       ((f-id Z)`|Z).x=diff(f,x) - diff(id Z,x) by A1,A6,A8,A9,FDIFF_1:27
      .=(f`|Z).x -diff(id Z,x) by A8,A9,FDIFF_1:def 8
      .=(((-1)(#)cot)`|Z).x-diff(id Z,x) by RFUNCT_1:38
      .=(-1)*diff(cot,x)-diff(id Z,x) by A3,A7,A9,FDIFF_1:28
      .=(-1)*(-1/(sin.x)^2)-diff(id Z,x) by A10,FDIFF_7:47
      .=1/(sin.x)^2-((id Z)`|Z).x by A6,A9,FDIFF_1:def 8
      .=1/(sin.x)^2-1 by A2,A5,A9,FDIFF_1:31
      .=1/(sin.x)^2-(sin.x)^2/(sin.x)^2 by A11,XCMPLX_1:60
      .=(1-(sin.x)^2)/(sin.x)^2
      .=((cos.x)^2+(sin.x)^2-(sin.x)^2)/(sin.x)^2 by SIN_COS:31
      .=(cos.x)^2/(sin.x)^2;
      hence thesis;
    end;
    hence thesis by A1,A6,A8,FDIFF_1:27;
  end;

:: f.x = (1/a)*tan.(a*x)-x
theorem
   (Z c= dom ((1/a)(#)(tan*f)-id Z) &
  for x st x in Z holds f.x=a*x & a<>0)
  implies (1/a)(#)(tan*f)-id Z is_differentiable_on Z &
  for x st x in Z holds
  (((1/a)(#)(tan*f)-id Z)`|Z).x=(sin.(a*x))^2/(cos.(a*x))^2
  proof
    assume that
A1: Z c= dom ((1/a)(#)(tan*f)-id Z) and
A2: for x st x in Z holds f.x=a*x & a<>0;
     Z c= dom ((1/a)(#)(tan*f)) /\ dom (id Z) by A1,SEQ_1:def 4;
then A3: Z c= dom ((1/a)(#)(tan*f)) & Z c= dom (id Z) by XBOOLE_1:18;
then A4: Z c= dom (tan*f) by SEQ_1:def 6;
A5:for x st x in Z holds f.x=a*x+0 by A2;
then A6: tan*f is_differentiable_on Z & for x st x in Z holds
    ((tan*f)`|Z).x = a/(cos.(a*x+0))^2 by A4,Th6;
then A7: (1/a)(#)(tan*f) is_differentiable_on Z &
    for x st x in Z holds ((1/a)(#)(tan*f)`|Z).x
    = (1/a)*diff(tan*f,x) by A3,FDIFF_1:28;
A8:for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
then A9: id Z is_differentiable_on Z & for x st x in Z holds
    ((id Z)`|Z).x = 1 by A3,FDIFF_1:31;
A10:  for x st x in Z holds cos.(f.x)<>0
    proof
      let x;
      assume x in Z;
      then f.x in dom (sin/cos) by A4,FUNCT_1:21;
      hence thesis by Th1;
    end;
    set g = (1/a)(#)(tan*f);
     for x st x in Z holds
    (((1/a)(#)(tan*f)-id Z)`|Z).x=(sin.(a*x))^2/(cos.(a*x))^2
    proof
      let x;
      assume A11:x in Z;
then A12:   f.x=a*x+0 & cos.(f.x)<>0 & a<>0 by A2,A10;
then A13:  (cos.(a*x))^2>0 by SQUARE_1:74;
       ((g-id Z)`|Z).x=diff(g,x)-diff(id Z,x) by A1,A7,A9,A11,FDIFF_1:27
      .=(g`|Z).x-diff(id Z,x) by A7,A11,FDIFF_1:def 8
      .=(1/a)*diff(tan*f,x)-diff(id Z,x) by A3,A6,A11,FDIFF_1:28
      .=(1/a)*((tan*f)`|Z).x-diff(id Z,x) by A6,A11,FDIFF_1:def 8
      .=(1/a)*((tan*f)`|Z).x-((id Z)`|Z).x by A9,A11,FDIFF_1:def 8
      .=(1/a)*(a/(cos.(a*x))^2)-((id Z)`|Z).x by A4,A5,A11,A12,Th6
      .=1/(cos.(a*x))^2*(a/a) -1 by A3,A8,A11,FDIFF_1:31
      .=1/(cos.(a*x))^2*1 -1 by A12,XCMPLX_1:60
      .=1/(cos.(a*x))^2-(cos.(a*x))^2/(cos.(a*x))^2 by A13,XCMPLX_1:60
      .=(1-(cos.(a*x))^2)/(cos.(a*x))^2
      .=((sin.(a*x))^2+(cos.(a*x))^2-(cos.(a*x))^2)/(cos.(a*x))^2 by SIN_COS:31
      .=(sin.(a*x))^2/(cos.(a*x))^2;
      hence thesis;
    end;
    hence thesis by A1,A7,A9,FDIFF_1:27;
  end;

:: f.x = (-1/a)*cot.(a*x)-x
theorem
   (Z c= dom ((-1/a)(#)(cot*f)-id Z) &
  for x st x in Z holds f.x=a*x & a<>0)
  implies (-1/a)(#)(cot*f)-id Z is_differentiable_on Z &
  for x st x in Z holds
  (((-1/a)(#)(cot*f)-id Z)`|Z).x=(cos.(a*x))^2/(sin.(a*x))^2
  proof
    assume that
A1: Z c= dom ((-1/a)(#)(cot*f)-id Z) and
A2: for x st x in Z holds f.x=a*x & a<>0;
     Z c= dom ((-1/a)(#)(cot*f)) /\ dom (id Z) by A1,SEQ_1:def 4;
then A3: Z c= dom ((-1/a)(#)(cot*f)) & Z c= dom (id Z) by XBOOLE_1:18;
then A4: Z c= dom (cot*f) by SEQ_1:def 6;
A5: for x st x in Z holds f.x=a*x+0 by A2;
then A6: cot*f is_differentiable_on Z & for x st x in Z holds
    ((cot*f)`|Z).x = -a/(sin.(a*x+0))^2 by A4,Th7;
then A7: (-1/a)(#)(cot*f) is_differentiable_on Z &
    for x st x in Z holds ((-1/a)(#)(cot*f)`|Z).x
    = (-1/a)*diff(cot*f,x) by A3,FDIFF_1:28;
A8: for x st x in Z holds (id Z).x = 1*x+0 by FUNCT_1:35;
then A9: id Z is_differentiable_on Z & for x st x in Z holds
    ((id Z)`|Z).x = 1 by A3,FDIFF_1:31;
A10:  for x st x in Z holds sin.(f.x)<>0
    proof
      let x;
      assume x in Z;
      then f.x in dom (cos/sin) by A4,FUNCT_1:21;
      hence thesis by Th2;
    end;
    set g = (-1/a)(#)(cot*f);
     for x st x in Z holds
    (((-1/a)(#)(cot*f)-id Z)`|Z).x=(cos.(a*x))^2/(sin.(a*x))^2
    proof
      let x;
      assume A11:x in Z;
then A12:   f.x=a*x+0 & sin.(f.x)<>0 & a<>0 by A2,A10;
then A13:  (sin.(a*x))^2>0 by SQUARE_1:74;
       ((g-id Z)`|Z).x=diff(g,x)-diff(id Z,x) by A1,A7,A9,A11,FDIFF_1:27
      .=(g`|Z).x-diff(id Z,x) by A7,A11,FDIFF_1:def 8
      .=(-1/a)*diff(cot*f,x)-diff(id Z,x) by A3,A6,A11,FDIFF_1:28
      .=(-1/a)*((cot*f)`|Z).x-diff(id Z,x) by A6,A11,FDIFF_1:def 8
      .=(-1/a)*((cot*f)`|Z).x-((id Z)`|Z).x by A9,A11,FDIFF_1:def 8
      .=(-1/a)*(-a/(sin.(a*x))^2)-((id Z)`|Z).x by A4,A5,A11,A12,Th7
      .=1/(sin.(a*x))^2*(a/a) -1 by A3,A8,A11,FDIFF_1:31
      .=1/(sin.(a*x))^2*1 -1 by A12,XCMPLX_1:60
      .=1/(sin.(a*x))^2-(sin.(a*x))^2/(sin.(a*x))^2 by A13,XCMPLX_1:60
      .=(1-(sin.(a*x))^2)/(sin.(a*x))^2
      .=((cos.(a*x))^2+(sin.(a*x))^2-(sin.(a*x))^2)/(sin.(a*x))^2 by SIN_COS:31
      .=(cos.(a*x))^2/(sin.(a*x))^2;
      hence thesis;
    end;
    hence thesis by A1,A7,A9,FDIFF_1:27;
  end;

:: f.x = (a*x+b)*tan.x
theorem
   (Z c= dom (f(#)tan) &
  for x st x in Z holds f.x=a*x+b)
  implies (f(#)tan) is_differentiable_on Z &
  for x st x in Z holds
  ((f(#)tan)`|Z).x = a*sin.x/cos.x+(a*x+b)/(cos.x)^2
  proof
    assume that
A1: Z c= dom (f(#)tan) and
A2: for x st x in Z holds f.x=a*x+b;
A3: Z c= dom f /\ dom tan by A1,SEQ_1:def 5;
then A4: Z c= dom f by XBOOLE_1:18;
A5: Z c= dom tan by A3,XBOOLE_1:18;
A6: f is_differentiable_on Z & for x st x in Z holds
    (f`|Z).x = a by A2,A4,FDIFF_1:31;
A7: for x st x in Z holds tan is_differentiable_in x &
    diff(tan, x)=1/(cos.x)^2
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A5,Th1;
      hence thesis by FDIFF_7:46;
    end;
    then for x st x in Z holds tan is_differentiable_in x;
then A8: tan is_differentiable_on Z by A5,FDIFF_1:16;
     for x st x in Z holds
    ((f(#)tan)`|Z).x = a*sin.x/cos.x+(a*x+b)/(cos.x)^2
    proof
      let x;
      assume A9:x in Z;
      then ((f(#)tan)`|Z).x=
      (tan.x)*diff(f,x)+(f.x)*diff(tan,x) by A1,A6,A8,FDIFF_1:29
      .=(tan.x)*((f`|Z).x)+(f.x)*diff(tan,x) by A6,A9,FDIFF_1:def 8
      .=(tan.x)*a+(f.x)*diff(tan,x) by A2,A4,A9,FDIFF_1:31
      .=(tan.x)*a+(a*x+b)*diff(tan,x) by A2,A9
      .=(tan.x)*a+(a*x+b)*(1/(cos.x)^2) by A7,A9
      .=(sin.x/cos.x)*(a/1)+(a*x+b)/(cos.x)^2 by A5,A9,RFUNCT_1:def 4
      .=a*sin.x/cos.x+(a*x+b)/(cos.x)^2;
      hence thesis;
    end;
    hence thesis by A1,A6,A8,FDIFF_1:29;
  end;

:: f.x = (a*x+b)*cot.x
theorem
   (Z c= dom (f(#)cot) &
  for x st x in Z holds f.x=a*x+b)
  implies (f(#)cot) is_differentiable_on Z &
  for x st x in Z holds
  ((f(#)cot)`|Z).x = a*cos.x/sin.x-(a*x+b)/(sin.x)^2
  proof
    assume that
A1: Z c= dom (f(#)cot) and
A2: for x st x in Z holds f.x=a*x+b;
A3: Z c= dom f /\ dom cot by A1,SEQ_1:def 5;
then A4: Z c= dom f by XBOOLE_1:18;
A5: Z c= dom cot by A3,XBOOLE_1:18;
A6: f is_differentiable_on Z & for x st x in Z holds
    (f`|Z).x = a by A2,A4,FDIFF_1:31;
A7: for x st x in Z holds cot is_differentiable_in x &
    diff(cot, x)=-1/(sin.x)^2
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A5,Th2;
      hence thesis by FDIFF_7:47;
    end;
    then for x st x in Z holds cot is_differentiable_in x;
then A8: cot is_differentiable_on Z by A5,FDIFF_1:16;
     for x st x in Z holds
    ((f(#)cot)`|Z).x = a*cos.x/sin.x-(a*x+b)/(sin.x)^2
    proof
      let x;
      assume A9:x in Z;
      then ((f(#)cot)`|Z).x=
      (cot.x)*diff(f,x)+(f.x)*diff(cot,x) by A1,A6,A8,FDIFF_1:29
      .=(cot.x)*((f`|Z).x)+(f.x)*diff(cot,x) by A6,A9,FDIFF_1:def 8
      .=(cot.x)*a+(f.x)*diff(cot,x) by A2,A4,A9,FDIFF_1:31
      .=(cot.x)*a+(a*x+b)*diff(cot,x) by A2,A9
      .=(cot.x)*a+(a*x+b)*(-1/(sin.x)^2) by A7,A9
      .=(cos.x/sin.x)*(a/1)-(a*x+b)/(sin.x)^2 by A5,A9,RFUNCT_1:def 4
      .=a*cos.x/sin.x-(a*x+b)/(sin.x)^2;
      hence thesis;
    end;
    hence thesis by A1,A6,A8,FDIFF_1:29;
  end;

:: f.x = exp_R.x*tan.x
theorem
   Z c= dom (exp_R(#)tan)
  implies (exp_R(#)tan) is_differentiable_on Z &
  for x st x in Z holds
  ((exp_R(#)tan)`|Z).x = exp_R.x*sin.x/cos.x+exp_R.x/(cos.x)^2
  proof
    assume
A1: Z c= dom (exp_R(#)tan);
then A2: Z c= dom (exp_R) /\ dom tan by SEQ_1:def 5;
then A3: Z c= dom (exp_R) by XBOOLE_1:18;
A4: Z c= dom tan by A2,XBOOLE_1:18;
     for x st x in Z holds exp_R is_differentiable_in x by SIN_COS:70;
then A5: exp_R is_differentiable_on Z by A3,FDIFF_1:16;
A6: for x st x in Z holds tan is_differentiable_in x &
    diff(tan, x)=1/(cos.x)^2
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A4,Th1;
      hence thesis by FDIFF_7:46;
    end;
    then for x st x in Z holds tan is_differentiable_in x;
then A7: tan is_differentiable_on Z by A4,FDIFF_1:16;
     for x st x in Z holds
    ((exp_R(#)tan)`|Z).x = exp_R.x*sin.x/cos.x+exp_R.x/(cos.x)^2
    proof
      let x;
      assume A8:x in Z;
      then ((exp_R(#)tan)`|Z).x = (tan.x)*diff(exp_R,x)+(exp_R.x)*diff(tan,x)
        by A1,A5,A7,FDIFF_1:29
      .=(tan.x)*exp_R.x+(exp_R.x)*diff(tan,x) by SIN_COS:70
      .=(tan.x)*exp_R.x+(exp_R.x)*(1/(cos.x)^2) by A6,A8
      .=(sin.x/cos.x)*(exp_R.x/1)+(exp_R.x)/(cos.x)^2 by A4,A8,RFUNCT_1:def 4
      .=exp_R.x*sin.x/cos.x+exp_R.x/(cos.x)^2;
      hence thesis;
    end;
    hence thesis by A1,A5,A7,FDIFF_1:29;
  end;

:: f.x = exp_R.x*cot.x
theorem
   Z c= dom (exp_R(#)cot)
  implies (exp_R(#)cot) is_differentiable_on Z &
  for x st x in Z holds
  ((exp_R(#)cot)`|Z).x = exp_R.x*cos.x/sin.x-exp_R.x/(sin.x)^2
  proof
    assume
A1: Z c= dom (exp_R(#)cot);
then A2: Z c= dom (exp_R) /\ dom cot by SEQ_1:def 5;
then A3: Z c= dom (exp_R) by XBOOLE_1:18;
A4: Z c= dom cot by A2,XBOOLE_1:18;
     for x st x in Z holds exp_R is_differentiable_in x by SIN_COS:70;
then A5: exp_R is_differentiable_on Z by A3,FDIFF_1:16;
A6: for x st x in Z holds cot is_differentiable_in x &
    diff(cot, x)=-1/(sin.x)^2
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A4,Th2;
      hence thesis by FDIFF_7:47;
    end;
    then for x st x in Z holds cot is_differentiable_in x;
then A7: cot is_differentiable_on Z by A4,FDIFF_1:16;
     for x st x in Z holds
    ((exp_R(#)cot)`|Z).x = exp_R.x*cos.x/sin.x-exp_R.x/(sin.x)^2
    proof
      let x;
      assume A8:x in Z;
      then ((exp_R(#)cot)`|Z).x= (cot.x)*diff(exp_R,x)+(exp_R.x)*diff(cot,x)
        by A1,A5,A7,FDIFF_1:29
      .=(cot.x)*exp_R.x+(exp_R.x)*diff(cot,x) by SIN_COS:70
      .=(cot.x)*exp_R.x+(exp_R.x)*(-1/(sin.x)^2) by A6,A8
      .=(cos.x/sin.x)*(exp_R.x/1)-(exp_R.x)/(sin.x)^2 by A4,A8,RFUNCT_1:def 4
      .=exp_R.x*cos.x/sin.x-exp_R.x/(sin.x)^2;
      hence thesis;
    end;
    hence thesis by A1,A5,A7,FDIFF_1:29;
  end;

:: f.x = ln.x*tan.x
theorem
   Z c= dom (ln(#)tan)
  implies (ln(#)tan) is_differentiable_on Z &
  for x st x in Z holds
  ((ln(#)tan)`|Z).x = sin.x/cos.x/x+ln.x/(cos.x)^2
  proof
    assume
A1: Z c= dom (ln(#)tan);
then A2: Z c= dom ln /\ dom tan by SEQ_1:def 5;
then A3: Z c= dom ln by XBOOLE_1:18;
A4: Z c= dom tan by A2,XBOOLE_1:18;
A5: for x st x in Z holds x>0
     proof
      let x;
      assume x in Z;
      then x in right_open_halfline(0) by A3,TAYLOR_1:18;
      then ex g being Real st x=g & 0<g by Lm1;
      hence thesis;
    end;
     for x st x in Z holds ln is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then x>0 by A5;
      hence thesis by TAYLOR_1:18;
    end;
then A6: ln is_differentiable_on Z by A3,FDIFF_1:16;
A7: for x st x in Z holds diff(ln,x) = 1/x
    proof
      let x;
      assume x in Z;
      then x>0 by A5;
      then x in right_open_halfline(0) by Lm1;
      hence thesis by TAYLOR_1:18;
    end;
A8: for x st x in Z holds tan is_differentiable_in x &
    diff(tan, x)=1/(cos.x)^2
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A4,Th1;
      hence thesis by FDIFF_7:46;
    end;
    then for x st x in Z holds tan is_differentiable_in x;
then A9: tan is_differentiable_on Z by A4,FDIFF_1:16;
     for x st x in Z holds
    ((ln(#)tan)`|Z).x = sin.x/cos.x/x+ln.x/(cos.x)^2
    proof
      let x;
      assume A10:x in Z;
      then ((ln(#)tan)`|Z).x=
      (tan.x)*diff(ln,x)+(ln.x)*diff(tan,x) by A1,A6,A9,FDIFF_1:29
      .=(tan.x)*(1/x)+(ln.x)*diff(tan,x) by A7,A10
      .=(tan.x)*(1/x)+(ln.x)*(1/(cos.x)^2) by A8,A10
      .=sin.x/cos.x/x+ln.x/(cos.x)^2 by A4,A10,RFUNCT_1:def 4;
      hence thesis;
    end;
    hence thesis by A1,A6,A9,FDIFF_1:29;
  end;

:: f.x = ln.x*cot.x
theorem
   Z c= dom (ln(#)cot)
  implies ln(#)cot is_differentiable_on Z &
  for x st x in Z holds
  ((ln(#)cot)`|Z).x = cos.x/sin.x/x-ln.x/(sin.x)^2
  proof
    assume
A1: Z c= dom (ln(#)cot);
then A2: Z c= dom ln /\ dom cot by SEQ_1:def 5;
then A3: Z c= dom ln by XBOOLE_1:18;
A4: Z c= dom cot by A2,XBOOLE_1:18;
A5: for x st x in Z holds x>0
    proof
      let x;
      assume x in Z;
      then x in right_open_halfline(0) by A3,TAYLOR_1:18;
      then ex g being Real st x=g & 0<g by Lm1;
      hence thesis;
    end;
     for x st x in Z holds ln is_differentiable_in x
    proof
      let x;
      assume x in Z;
      then x>0 by A5;
      hence thesis by TAYLOR_1:18;
    end;
then A6: ln is_differentiable_on Z by A3,FDIFF_1:16;
A7: for x st x in Z holds diff(ln,x) = 1/x
    proof
      let x;
      assume x in Z;
      then x>0 by A5;
      then x in right_open_halfline(0) by Lm1;
      hence thesis by TAYLOR_1:18;
    end;
A8: for x st x in Z holds cot is_differentiable_in x &
    diff(cot, x)=-1/(sin.x)^2
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A4,Th2;
      hence thesis by FDIFF_7:47;
    end;
    then for x st x in Z holds cot is_differentiable_in x;
then A9: cot is_differentiable_on Z by A4,FDIFF_1:16;
     for x st x in Z holds
    ((ln(#)cot)`|Z).x = cos.x/sin.x/x-ln.x/(sin.x)^2
    proof
      let x;
      assume A10:x in Z;
      then ((ln(#)cot)`|Z).x = (cot.x)*diff(ln,x)+(ln.x)*diff(cot,x)
      by A1,A6,A9,FDIFF_1:29
      .=(cot.x)*(1/x)+(ln.x)*diff(cot,x) by A7,A10
      .=(cot.x)*(1/x)+(ln.x)*(-1/(sin.x)^2) by A8,A10
      .=cos.x/sin.x/x-ln.x/(sin.x)^2 by A4,A10,RFUNCT_1:def 4;
      hence thesis;
    end;
    hence thesis by A1,A6,A9,FDIFF_1:29;
  end;

:: f.x = 1/x*tan.x
theorem
   (Z c= dom (f^(#)tan) &
  for x st x in Z holds f.x=x)
  implies (f^(#)tan) is_differentiable_on Z &
  for x st x in Z holds
  ((f^(#)tan)`|Z).x = -sin.x/cos.x/x^2+1/x/(cos.x)^2
  proof
    assume that
A1: Z c= dom (f^(#)tan) and
A2: for x st x in Z holds f.x=x;
A3: Z c= dom (f^) /\ dom tan by A1,SEQ_1:def 5;
then A4: Z c= dom (f^) by XBOOLE_1:18;
A5: Z c= dom tan by A3,XBOOLE_1:18;
     dom (f^) c= dom f by RFUNCT_1:11;
then A6: Z c= dom f by A4,XBOOLE_1:1;
A7: for x st x in Z holds f.x = x & f.x<>0 by A2,A4,RFUNCT_1:13;
then A8: f^ is_differentiable_on Z &
    for x st x in Z holds ((f^)`|Z).x = -1/x^2 by A6,FDIFF_5:4;
A9: for x st x in Z holds tan is_differentiable_in x &
    diff(tan, x)=1/(cos.x)^2
    proof
      let x;
      assume x in Z;
      then cos.x<>0 by A5,Th1;
      hence thesis by FDIFF_7:46;
    end;
    then for x st x in Z holds tan is_differentiable_in x;
then A10: tan is_differentiable_on Z by A5,FDIFF_1:16;
     for x st x in Z holds ((f^(#)tan)`|Z).x
    =-sin.x/cos.x/x^2+1/x/(cos.x)^2
    proof
      let x;
      assume A11:x in Z;
      then ((f^(#)tan)`|Z).x=
      (tan.x)*diff(f^,x)+((f^).x)*diff(tan,x) by A1,A8,A10,FDIFF_1:29
      .=(tan.x)*((f^)`|Z).x+((f^).x)*diff(tan,x) by A8,A11,FDIFF_1:def 8
      .=(tan.x)*(-1/x^2)+((f^).x)*diff(tan,x) by A6,A7,A11,FDIFF_5:4
      .=-(tan.x)*(1/x^2)+((f^).x)*(1/(cos.x)^2) by A9,A11
      .=-(sin.x/cos.x)*(1/x^2)+((f^).x)/(cos.x)^2 by A5,A11,RFUNCT_1:def 4
      .=-sin.x/cos.x/x^2+(f.x)"/(cos.x)^2 by A4,A11,RFUNCT_1:def 8
      .=-sin.x/cos.x/x^2+1/x/(cos.x)^2 by A2,A11;
      hence thesis;
    end;
    hence thesis by A1,A8,A10,FDIFF_1:29;
  end;

:: f.x = 1/x*cot.x
theorem
   (Z c= dom (f^(#)cot) &
  for x st x in Z holds f.x=x)
  implies (f^(#)cot) is_differentiable_on Z &
  for x st x in Z holds
  ((f^(#)cot)`|Z).x = -cos.x/sin.x/x^2-1/x/(sin.x)^2
  proof
    assume that
A1: Z c= dom (f^(#)cot) and
A2: for x st x in Z holds f.x=x;
A3: Z c= dom (f^) /\ dom cot by A1,SEQ_1:def 5;
then A4: Z c= dom (f^) by XBOOLE_1:18;
A5: Z c= dom cot by A3,XBOOLE_1:18;
     dom (f^) c= dom f by RFUNCT_1:11;
then A6: Z c= dom f by A4,XBOOLE_1:1;
A7: for x st x in Z holds f.x = x & f.x<>0 by A2,A4,RFUNCT_1:13;
then A8: f^ is_differentiable_on Z &
    for x st x in Z holds ((f^)`|Z).x = -1/x^2 by A6,FDIFF_5:4;
A9: for x st x in Z holds cot is_differentiable_in x &
    diff(cot, x)=-1/(sin.x)^2
    proof
      let x;
      assume x in Z;
      then sin.x<>0 by A5,Th2;
      hence thesis by FDIFF_7:47;
    end;
    then for x st x in Z holds cot is_differentiable_in x;
then A10: cot is_differentiable_on Z by A5,FDIFF_1:16;
     for x st x in Z holds ((f^(#)cot)`|Z).x
    =-cos.x/sin.x/x^2-1/x/(sin.x)^2
    proof
      let x;
      assume A11:x in Z;
      then ((f^(#)cot)`|Z).x=
      (cot.x)*diff(f^,x)+((f^).x)*diff(cot,x) by A1,A8,A10,FDIFF_1:29
      .=(cot.x)*((f^)`|Z).x+((f^).x)*diff(cot,x) by A8,A11,FDIFF_1:def 8
      .=(cot.x)*(-1/x^2)+((f^).x)*diff(cot,x) by A6,A7,A11,FDIFF_5:4
      .=-(cot.x)*(1/x^2)+((f^).x)*(-1/(sin.x)^2) by A9,A11
      .=-(cos.x/sin.x)*(1/x^2)-((f^).x)/(sin.x)^2 by A5,A11,RFUNCT_1:def 4
      .=-cos.x/sin.x/x^2-(f.x)"/(sin.x)^2 by A4,A11,RFUNCT_1:def 8
      .=-cos.x/sin.x/x^2-1/x/(sin.x)^2 by A2,A11;
      hence thesis;
    end;
    hence thesis by A1,A8,A10,FDIFF_1:29;
  end;
