:: Integer and Rational Exponents
::  by Konrad Raczkowski
::
:: Received September 21, 1990
:: Copyright (c) 1990 Association of Mizar Users
 
environ

 vocabularies NUMBERS, INT_1, COMPLEX1, ORDINAL1, XREAL_0, SUBSET_1, RAT_1,
      SEQ_1, SEQ_2, FUNCT_1, XXREAL_0, ORDINAL2, FUNCOP_1, CARD_1, NEWTON,
      REAL_1, ARYTM_3, RELAT_1, NAT_1, ARYTM_1, VALUED_1, XXREAL_2, SEQ_4,
      SQUARE_1, VALUED_0, INT_2, PREPOWER;
 notations SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0, XXREAL_2, XREAL_0,
      COMPLEX1, REAL_1, INT_2, INT_1, NAT_1, NAT_D, NEWTON, RELAT_1, FUNCT_1,
      FUNCOP_1, VALUED_0, VALUED_1, SEQ_1, SEQ_2, SQUARE_1, SEQ_4, RAT_1;
 constructors FUNCOP_1, REAL_1, SQUARE_1, NAT_1, NAT_D, SEQ_2, SEQM_3,
      PARTFUN1, VALUED_1, SEQ_4, NEWTON, SEQ_1, XXREAL_2, RELSET_1, BINOP_2;
 registrations ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1, INT_1,
      RAT_1, MEMBERED, NEWTON, XBOOLE_0, VALUED_0, VALUED_1, FUNCT_2, FUNCOP_1,
      SEQ_4;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions VALUED_1, XCMPLX_0, XXREAL_2;
 theorems ABSVALUE, NAT_1, SEQ_1, SEQ_2, INT_1, SQUARE_1, SEQ_4, RAT_1,
      RFUNCT_2, TARSKI, FUNCT_2, NEWTON, XREAL_0, XCMPLX_0, XCMPLX_1, FUNCOP_1,
      COMPLEX1, XREAL_1, XXREAL_0, INT_2, ORDINAL1, NAT_D, VALUED_1, VALUED_0,
      FUNCT_1, XXREAL_2;
 schemes NAT_1, SEQ_1, FUNCT_2;

begin

registration
  let i be Integer;
  cluster abs i -> natural;
  coherence;
end;

reserve x for set;
reserve a, b, c for real number;
reserve m, n, m1, m2 for Element of NAT;
reserve k, l for Integer;
reserve p, q for Rational;
reserve s1, s2 for Real_Sequence;

canceled;

theorem Th2:
  s1 is convergent & (for n holds s1.n>=a) implies lim s1 >= a
proof
  assume that
A1: s1 is convergent and
A2: for n holds s1.n>=a;
  a in REAL by XREAL_0:def 1;
  then reconsider s = NAT --> a as Real_Sequence by FUNCOP_1:57;
A3: now
    let n;
    s1.n >= a by A2;
    hence s1.n >= s.n by FUNCOP_1:13;
  end;
  lim s = s.0 by SEQ_4:41
    .= a by FUNCOP_1:13;
  hence thesis by A1,A3,SEQ_2:32;
end;

theorem Th3:
  s1 is convergent & (for n holds s1.n<=a) implies lim s1 <= a
proof
  assume that
A1: s1 is convergent and
A2: for n holds s1.n<=a;
  a in REAL by XREAL_0:def 1;
  then reconsider s = NAT --> a as Real_Sequence by FUNCOP_1:57;
A3: now
    let n;
    s1.n <= a by A2;
    hence s1.n <= s.n by FUNCOP_1:13;
  end;
  lim s = s.0 by SEQ_4:41
    .= a by FUNCOP_1:13;
  hence thesis by A1,A3,SEQ_2:32;
end;

definition
  let a be real number;
  func a GeoSeq -> Real_Sequence means
  :Def1:
  for m holds it.m = a|^m;
  existence
  proof
    reconsider a as Real by XREAL_0:def 1;
    deffunc F(Element of NAT) = a|^$1;
    ex f be Real_Sequence st for m holds f.m = F(m) from FUNCT_2:sch 4;
    hence thesis;
  end;
  uniqueness
  proof
    let s1,s2 such that
A1: for n holds s1.n= a|^n and
A2: for n holds s2.n= a|^n;
    for n holds s1.n = s2.n
    proof
      let n;
      thus s1.n = a|^n by A1
        .= s2.n by A2;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

theorem Th4:
  s1 = a GeoSeq iff s1.0 = 1 & for m holds s1.(m+1) = s1.m * a
proof
  defpred P[Element of NAT] means s1.$1 = a GeoSeq.$1;
  hereby
    assume
A1: s1 = a GeoSeq;
    hence s1.0 = a|^0 by Def1
      .= 1 by NEWTON:9;
    let m;
    thus s1.(m+1) = a|^(m+1) by A1,Def1
      .= a|^m * a by NEWTON:11
      .= s1.m * a by A1,Def1;
  end;
  assume that
A2: s1.0 = 1 and
A3: for m holds s1.(m+1) = s1.m * a;
A4: for m st P[m] holds P[m+1]
  proof
    let m such that
A5: s1.m = a GeoSeq.m;
    thus s1.(m+1) = s1.m * a by A3
      .= a|^m * a by A5,Def1
      .= a|^(m+1) by NEWTON:11
      .= a GeoSeq.(m+1) by Def1;
  end;
  s1.0 = a|^0 by A2,NEWTON:9
    .= a GeoSeq.0 by Def1;
  then
A6: P[0];
  for m holds P[m] from NAT_1:sch 1(A6,A4);
  hence thesis by FUNCT_2:113;
end;

theorem
  for a st a <> 0 holds for m holds a GeoSeq.m <> 0
proof
  let a such that
A1: a <> 0;
  defpred P[Element of NAT] means a GeoSeq.$1 <> 0;
A2: for n holds P[n] implies P[n+1]
  proof
    let n such that
A3: a GeoSeq.n <> 0;
    a GeoSeq.(n+1) = a GeoSeq.n * a by Th4;
    hence thesis by A1,A3,XCMPLX_1:6;
  end;
A4: P[0] by Th4;
  thus for n holds P[n] from NAT_1:sch 1(A4,A2);
end;

canceled 6;

theorem Th12:
  for n being natural number holds 0 <> a implies 0 <> a |^ n
proof
  let n be natural number;
  defpred P[natural number] means a |^ $1 <> 0;
  assume
A1: 0<>a;
A2: for m be natural number st P[m] holds P[m+1]
  proof
    let m be natural number;
    assume a |^ m <> 0;
    then (a |^ m)*a <> 0 by A1,XCMPLX_1:6;
    hence thesis by NEWTON:11;
  end;
A3: P[0] by NEWTON:9;
  for m be natural number holds P[m] from NAT_1:sch 2(A3,A2);
  hence thesis;
end;

theorem Th13:
  for n being natural number holds 0 < a implies 0 < a |^ n
proof
  let n be natural number;
  defpred P[natural number] means 0 < a |^ $1;
  assume
A1: 0<a;
A2: for m be natural number st P[m] holds P[m+1]
  proof
    let m be natural number;
    assume a |^ m > 0;
    then (a |^ m)*a > 0*a by A1,XREAL_1:70;
    hence thesis by NEWTON:11;
  end;
A3: P[0] by NEWTON:9;
  for m be natural number holds P[m] from NAT_1:sch 2(A3,A2);
  hence thesis;
end;

theorem Th14:
  for n being natural number holds (1/a) |^ n = 1 / a |^ n
proof
  let n be natural number;
  defpred P[natural number] means (1/a) |^ $1 = 1/ a |^ $1;
A1: for m be natural number st P[m] holds P[m+1]
  proof
    let m be natural number;
    assume (1/a) |^ m = 1 / a |^ m;
    hence (1/a) |^ (m+1) = 1 / a |^ m * (1/a) by NEWTON:11
      .= 1*1 / (a |^ m * a) by XCMPLX_1:77
      .= 1 / a |^ (m+1) by NEWTON:11;
  end;
  (1/a) |^ 0 = (1/a) GeoSeq.0 by Def1
    .= 1/1 by Th4
    .= 1/(a GeoSeq.0) by Th4
    .= 1/ a |^ 0 by Def1;
  then
A2: P[0];
  for m be natural number holds P[m] from NAT_1:sch 2(A2,A1);
  hence thesis;
end;

theorem
  for n being natural number holds (b/a) |^ n = b |^ n / a |^ n
proof
  let n be natural number;
  thus (b/a) |^ n = (b*a") |^ n .= b |^ n * (a") |^ n by NEWTON:12
    .= b |^ n * (1/a) |^ n
    .= b |^ n * (1/a |^ n) by Th14
    .= b |^ n*1 / a |^ n
    .= b |^ n / a |^ n;
end;

canceled;

theorem Th17:
  for n being natural number st 0 < a & a <= b holds a |^ n <= b |^ n
proof
  let n be natural number;
  assume that
A1: 0 < a and
A2: a <= b;
  defpred P[natural number] means a |^ $1 <= b |^ $1;
A3: for m1 be natural number st P[m1] holds P[m1+1]
  proof
    let m1 be natural number such that
A4: a |^ m1 <= b |^ m1;
    a |^ m1 > 0 by A1,Th13;
    then a |^ m1 * a <= b |^ m1 * b by A1,A2,A4,XREAL_1:68;
    then a |^ (m1+1) <= b |^ m1 * b by NEWTON:11;
    hence thesis by NEWTON:11;
  end;
A5: b |^ 0 = b GeoSeq.0 by Def1
    .= 1 by Th4;
  a |^ 0 = a GeoSeq.0 by Def1
    .= 1 by Th4;
  then
A6: P[0] by A5;
  for m1 be natural number holds P[m1] from NAT_1:sch 2(A6,A3);
  hence thesis;
end;

Lm1: for n being natural number holds 0 < a & a < b & 1 <= n implies a |^ n <
b |^ n
proof
  let n be natural number;
  assume that
A1: 0 < a and
A2: a < b and
A3: 1 <= n;
  consider m being Nat such that
A4: n = 1+m by A3,NAT_1:10;
  defpred P[Element of NAT] means a |^ (1+$1) < b |^ (1+$1);
A5: for m1 st P[m1] holds P[m1+1]
  proof
    let m1;
    assume
A6: a |^ (1+m1) < b |^ (1+m1);
    a |^ (1+m1) > 0 by A1,Th13;
    then a |^ (1+m1) * a < b |^ (1+m1) * b by A1,A2,A6,XREAL_1:99;
    then a |^ (1+m1+1) < b |^ (1+m1) * b by NEWTON:11;
    hence thesis by NEWTON:11;
  end;
A7: b |^ (1+0) = b GeoSeq.(0+1) by Def1
    .= b GeoSeq.0 * b by Th4
    .= 1*b by Th4
    .= b;
  a |^ (1+0) = a GeoSeq.(0+1) by Def1
    .= a GeoSeq.0 * a by Th4
    .= 1*a by Th4
    .= a;
  then
A8: P[0] by A2,A7;
A9: for m1 holds P[m1] from NAT_1:sch 1(A8,A5);
  m in NAT by ORDINAL1:def 13;
  hence thesis by A4,A9;
end;

theorem Th18:
  for n being natural number st 0 <= a & a < b & 1 <= n holds a |^ n < b |^ n
proof
  let n be natural number;
  assume that
A1: 0 <= a and
A2: a < b and
A3: 1 <= n;
  per cases by A1;
  suppose
    a>0;
    hence thesis by A2,A3,Lm1;
  end;
  suppose
A4: a=0;
    reconsider k=n, k1=1 as Integer;
    reconsider m=k-k1 as Element of NAT by A3,INT_1:18;
    a |^ n = a |^ (m+1) .= a |^ m * a |^ 1 by NEWTON:13
      .= a |^ m * a GeoSeq.(0+1) by Def1
      .= a |^ m * (a GeoSeq.0 * 0) by A4,Th4
      .= 0;
    hence thesis by A1,A2,Th13;
  end;
end;

theorem Th19:
  for n being natural number holds a>=1 implies a |^ n >= 1
proof
  let n be natural number;
  assume a>=1;
  then a |^ n >= 1 |^ n by Th17;
  hence thesis by NEWTON:15;
end;

theorem Th20:
  for n being natural number st 1 <= a & 1 <= n holds a <= a |^ n
proof
  let n be natural number;
  assume that
A1: 1 <= a and
A2: 1 <= n;
  consider m being Nat such that
A3: n=m+1 by A2,NAT_1:6;
  defpred P[Element of NAT] means a <= a |^ ($1+1);
A4: for m1 st P[m1] holds P[m1+1]
  proof
    let m1;
    assume a <= a |^ (m1+1);
    then a*1 <= a |^ (m1+1) * a by A1,XREAL_1:68;
    hence thesis by NEWTON:11;
  end;
  a<=1*a;
  then a<=a GeoSeq.0 * a by Th4;
  then a<=a |^ 0 * a by Def1;
  then
A5: P[0] by NEWTON:11;
A6: for m1 holds P[m1] from NAT_1:sch 1(A5,A4);
  m in NAT by ORDINAL1:def 13;
  hence thesis by A3,A6;
end;

theorem
  for n being natural number st 1 < a & 2 <= n holds a < a |^ n
proof
  let n be natural number;
  assume that
A1: 1 < a and
A2: 2 <= n;
  consider m being Nat such that
A3: n = 2+m by A2,NAT_1:10;
  defpred P[Element of NAT] means a < a |^ (2+$1);
A4: a*a > a*1 by A1,XREAL_1:70;
A5: for m1 st P[m1] holds P[m1+1]
  proof
    let m1;
    assume a < a |^ (2+m1);
    then a |^ (2+m1) * a > a*a by A1,XREAL_1:70;
    then a |^ (2+m1+1) > a*a by NEWTON:11;
    hence thesis by A4,XXREAL_0:2;
  end;
  a |^ (2+0) = a GeoSeq.(1+1) by Def1
    .= a GeoSeq.(0+1) * a by Th4
    .= a GeoSeq.0 * a * a by Th4
    .= 1*a*a by Th4
    .= a*a;
  then
A6: P[0] by A4;
A7: for m1 holds P[m1] from NAT_1:sch 1(A6,A5);
  m in NAT by ORDINAL1:def 13;
  hence thesis by A3,A7;
end;

theorem Th22:
  for n being natural number st 0 < a & a <= 1 & 1 <= n holds a |^ n <= a
proof
  let n be natural number;
  assume that
A1: 0 < a and
A2: a <= 1 and
A3: 1 <= n;
  consider m being Nat such that
A4: n = 1+m by A3,NAT_1:10;
  defpred P[Element of NAT] means a |^ (1+$1) <= a;
A5: a*a <= a*1 by A1,A2,XREAL_1:66;
A6: for m1 st P[m1] holds P[m1+1]
  proof
    let m1;
    assume a |^ (1+m1) <= a;
    then a |^ (1+m1) * a <= a*a by A1,XREAL_1:66;
    then a |^ (1+(m1+1)) <= a*a by NEWTON:11;
    hence thesis by A5,XXREAL_0:2;
  end;
  a |^ (1+0) = a GeoSeq.(0+1) by Def1
    .= a GeoSeq.0 * a by Th4
    .= 1*a by Th4
    .= a;
  then
A7: P[0];
A8: for m1 holds P[m1] from NAT_1:sch 1(A7,A6);
  m in NAT by ORDINAL1:def 13;
  hence thesis by A4,A8;
end;

theorem
  for n being natural number st 0 < a & a < 1 & 2 <= n holds a |^ n < a
proof
  let n be natural number;
  assume that
A1: 0 < a and
A2: a < 1 and
A3: 2 <= n;
  consider m being Nat such that
A4: n = 2+m by A3,NAT_1:10;
  defpred P[Element of NAT] means a |^ (2+$1) < a;
A5: a*a < a*1 by A1,A2,XREAL_1:70;
A6: for m1 st P[m1] holds P[m1+1]
  proof
    let m1;
    assume a |^ (2+m1) < a;
    then a |^ (2+m1) * a < a*a by A1,XREAL_1:70;
    then a |^ (2+m1+1) < a*a by NEWTON:11;
    hence thesis by A5,XXREAL_0:2;
  end;
  a |^ (2+0) = a GeoSeq.(1+1) by Def1
    .= a GeoSeq.(0+1) * a by Th4
    .= a GeoSeq.0 * a * a by Th4
    .= 1*a*a by Th4
    .= a*a;
  then
A7: P[0] by A5;
A8: for m1 holds P[m1] from NAT_1:sch 1(A7,A6);
  m in NAT by ORDINAL1:def 13;
  hence thesis by A4,A8;
end;

theorem Th24:
  for n being natural number holds -1 < a implies (1 + a) |^ n >= 1 + n * a
proof
  let n be natural number;
  defpred P[natural number] means (1 + a) |^ $1 >= 1 + $1 * a;
  assume
A1: -1 < a;
A2: for m be natural number st P[m] holds P[m+1]
  proof
A3: -1+1<1+a by A1,XREAL_1:8;
    let m be natural number;
    assume (1+a) |^ m >= 1 + m * a;
    then (1+a) |^ m * (1+a) >= (1 + m*a)*(1+a) by A3,XREAL_1:66;
    then
A4: (1+a) |^ (m+1) >= 1 + 1*a + m*a + (m*a)*a by NEWTON:11;
    0<=a*a by XREAL_1:65;
    then 1 + (m+1)*a + 0 <= 1 + (m+1)*a + m*(a*a) by XREAL_1:9;
    hence thesis by A4,XXREAL_0:2;
  end;
  (1 + a) |^ 0 = (1+a) GeoSeq.0 by Def1
    .= 1 by Th4;
  then
A5: P[0];
  for m being natural number holds P[m] from NAT_1:sch 2(A5,A2);
  hence thesis;
end;

theorem Th25:
  for n being natural number st 0 < a & a < 1 holds (1 + a) |^ n
  <= 1 + 3 |^ n * a
proof
  let n be natural number;
  assume that
A1: 0 < a and
A2: a < 1;
A3: 1 + 0 < 1 + a by A1,XREAL_1:8;
  defpred P[natural number] means (1 + a) |^ $1 <= 1 + 3 |^ $1 * a;
A4: for n be natural number holds P[n] implies P[n+1]
  proof
    let n be natural number;
    assume (1 + a) |^ n <= 1 + 3 |^ n * a;
    then
A5: (1 + a) |^ n * (1 + a) <= (1 + 3 |^ n * a) * (1+a) by A1,XREAL_1:66;
A6: 1 + 3 |^ n * a + (3 |^ n + 3 |^ n) * a = 1 + (3 |^ n * (1 + 2)) * a
      .= 1 + 3 |^ (n+1) * a by NEWTON:11;
A7: 1 <= 3 |^ n
    proof
      per cases;
      suppose
A8:     0 = n;
        n is Element of NAT by ORDINAL1:def 13;
        then 3 |^ n = 3 GeoSeq.n by Def1
          .= 1 by A8,Th4;
        hence thesis;
      end;
      suppose
        0 <> n;
        then 0 + 1 <= n by NAT_1:13;
        then 1 |^ n < 3 |^ n by Lm1;
        hence thesis by NEWTON:15;
      end;
    end;
    then 1 * a <= 3 |^ n * a by A1,XREAL_1:66;
    then
A9: 1 + a <= 1 + 3 |^ n * a by XREAL_1:9;
    a * a < 1 * a by A1,A2,XREAL_1:70;
    then 3 |^ n * (a * a) < 3 |^ n * a by A7,XREAL_1:70;
    then
A10: 3 |^ n * a + 3 |^ n * (a*a) < 3 |^ n * a + 3 |^ n * a by XREAL_1:8;
    (1 + 3 |^ n * a) * (1+a) = 1 + a + (3 |^ n * a + 3 |^ n * (a * a));
    then (1 + 3 |^ n * a) * (1+a) < 1 + 3 |^ (n+1) * a by A9,A6,A10,XREAL_1:10;
    then (1 + a) |^ n * (1 + a) <= 1 + 3 |^ (n + 1) * a by A5,XXREAL_0:2;
    hence thesis by NEWTON:11;
  end;
A11: 1 + 3 |^ 0 * a = 1 + 3 GeoSeq.0 * a by Def1
    .= 1 + 1 * a by Th4
    .= 1 + a;
  (1 + a) |^ 0 = (1 + a) GeoSeq.0 by Def1
    .= 1 by Th4;
  then
A12: P[0] by A11,A3;
  for n be natural number holds P[n] from NAT_1:sch 2(A12,A4);
  hence thesis;
end;

theorem Th26:
  s1 is convergent & (for n holds s2.n = (s1.n) |^ m) implies s2
  is convergent & lim s2 = (lim s1) |^ m
proof
  assume that
A1: s1 is convergent and
A2: for n holds s2.n = (s1.n) |^ m;
  defpred P[Element of NAT] means for s being Real_Sequence st (for n holds s.
  n=(s1.n) |^ $1) holds s is convergent & lim s=(lim s1) |^ $1;
A3: P[0]
  proof
    let s be Real_Sequence;
    assume
A4: for n holds s.n = (s1.n) |^ 0;
A5: now
      let n be Nat;
      n in NAT by ORDINAL1:def 13;
      hence s.n = (s1.n) |^ 0 by A4
        .= (s1.n) GeoSeq.0 by Def1
        .= 1 by Th4;
    end;
    then
A6: s is constant by VALUED_0:def 18;
    hence s is convergent;
    thus lim s = s.0 by A6,SEQ_4:41
      .= 1 by A5
      .= (lim s1) GeoSeq.0 by Th4
      .= (lim s1) |^ 0 by Def1;
  end;
A7: for m1 holds P[m1] implies P[m1+1]
  proof
    let m1;
    assume
A8: for s being Real_Sequence st (for n holds s.n = (s1.n) |^ m1)
    holds s is convergent & lim s = (lim s1) |^ m1;
    deffunc O(Element of NAT) = (s1.$1) |^ m1;
    let s be Real_Sequence;
    consider s3 being Real_Sequence such that
A9: for n holds s3.n = O(n) from SEQ_1:sch 1;
    assume
A10: for n holds s.n = (s1.n) |^ (m1+1);
    now
      let n;
      thus s.n = (s1.n) |^ (m1+1) by A10
        .= (s1.n) |^ m1 * (s1.n) by NEWTON:11
        .= (s3.n) * (s1.n) by A9;
    end;
    then
A11: s = s3(#)s1 by SEQ_1:12;
A12: s3 is convergent by A8,A9;
    hence s is convergent by A1,A11,SEQ_2:28;
    lim s3 = (lim s1) |^ m1 by A8,A9;
    hence lim s = (lim s1) |^ m1 * (lim s1) by A1,A12,A11,SEQ_2:29
      .= (lim s1) |^ (m1+1) by NEWTON:11;
  end;
  for m1 holds P[m1] from NAT_1:sch 1(A3,A7);
  hence thesis by A2;
end;

definition
  let n be natural number;
  let a be real number;
  assume
A1: 1 <= n;
  canceled;
  func n -Root a -> real number means
  :Def3:
  it |^ n = a & it > 0 if a>0, it = 0 if a=0;
  consistency;
  existence
  proof
    now
      set X = {b where b is Real: 0 < b & b |^ n <= a};
      for x holds x in X implies x in REAL
      proof
        let x;
        assume x in X;
        then ex b be Real st x = b & 0 < b & b |^ n <= a;
        hence thesis;
      end;
      then reconsider X as Subset of REAL by TARSKI:def 3;
      reconsider a1 = a as Real by XREAL_0:def 1;
A2:   1 <= a implies ex c st c in X
      proof
        assume
A3:     1 <= a;
        consider c such that
A4:     c = 1;
        take c;
        c |^ n <= a by A3,A4,NEWTON:15;
        hence thesis by A4;
      end;
A5:   a < 1 implies ex c be real number st c is UpperBound of X
      proof
        consider c such that
A6:     c = 1;
        assume
A7:     a < 1;
        take c;
          let b be ext-real number;
          assume b in X;
          then consider d being Real such that
A8:         d = b & 0 < d & d |^ n <= a;
          assume not b <= c;
          then 1 |^ n < d |^ n by A1,A6,Lm1,A8;
          then 1 < d |^ n by NEWTON:15;
          hence contradiction by A7,A8,XXREAL_0:2;
      end;
      assume
A9:   a>0;
      1 <= a implies ex c be real number st c is UpperBound of X
      proof
        assume
A10:    1 <= a;
        take a;
          let b be ext-real number;
          assume b in X;
          then consider d being Real such that
A11:          b = d & 0 < d & d |^ n <= a;
          assume a < b;
          then a |^ n < d |^ n by A1,A9,Lm1,A11;
          then a |^ n < a by A11,XXREAL_0:2;
          hence contradiction by A1,A10,Th20;
      end;
      then
A12:  X is bounded_above by A5,XXREAL_2:def 10;
      take d = upper_bound X;
A13:  a < 1 implies ex c st c in X
      proof
        assume
A14:    a < 1;
        reconsider a as Real by XREAL_0:def 1;
        take a;
        a |^ n <= a by A1,A9,A14,Th22;
        hence thesis by A9;
      end;
A15:  0 < d
      proof
        consider c such that
A16:    c in X by A2,A13;
        ex e be Real st e = c & 0 < e & e |^ n <= a by A16;
        hence thesis by A12,A16,SEQ_4:def 4;
      end;
A17:  not a < d |^ n
      proof
        set b = d * (1 - a / d |^ n) / (n + 1);
        assume
A18:    a < d |^ n;
        then
A19:    1/(n+1) * d |^ n > 1/(n+1) * a by XREAL_1:70;
        a * (d |^ n)" > 0*a by A9,A18,XREAL_1:70;
        then -(- a / d |^ n) > 0;
        then - a / d |^ n < 0;
        then 1 + - a / d |^ n < 1+0 by XREAL_1:8;
        then
A20:    (1 - a / d |^ n) * (n+1)" < 1*(n+1)" by XREAL_1:70;
        1 < n+1 by A1,NAT_1:13;
        then (n+1)" < 1 by XREAL_1:214;
        then (1 - a / d |^ n) / (n + 1) < 1 by A20,XXREAL_0:2;
        then
A21:    d * ((1 - a / d |^ n) / (n + 1)) < d*1 by A15,XREAL_1:70;
        then b/d < d/d by A15,XREAL_1:76;
        then b/d < 1 by A15,XCMPLX_1:60;
        then - 1 < - b/d by XREAL_1:26;
        then
A22:    (1 + -b/d) |^ n >= 1 + n * (-b/d) by Th24;
        a / d |^ n < d |^ n / d |^ n by A9,A18,XREAL_1:76;
        then 0 + a / d |^ n < 1 by A9,A18,XCMPLX_1:60;
        then 0 < 1 - a / d |^ n by XREAL_1:22;
        then d * 0 < d * (1 - a / d |^ n) by A15,XREAL_1:70;
        then 0 / (n + 1) < d * (1 - a / d |^ n) / (n + 1) by XREAL_1:76;
        then consider c be real number such that
A23:    c in X and
A24:    d - b < c by A2,A13,A12,SEQ_4:def 4;
        0 < d - b by A21,XREAL_1:52;
        then
A25:    (d - b) |^ n < c |^ n by A1,A24,Lm1;
        (1 + n * (-b/d)) * d |^ n = (1 - n * (d * (1 - a / d |^ n) / (n +
        1)/d)) * d |^ n
          .= (1 - n*(d * ((1 - a / d |^ n) / (n+1))/d)) * d |^ n
          .= (1 - n * ((((1 - a / d |^ n) / (n + 1))/d) * d)) * d |^ n
          .= (1 - n * ((1 - a / d |^ n) / (n + 1))) * d |^ n by A15,XCMPLX_1:88
          .= (1 - (1 - a / d |^ n) * n / (n + 1)) * d |^ n
          .= (1 - (1 - a / d |^ n) * (n / (n + 1))) * d |^ n
          .= (1 - n/(n+1) + a/d |^ n * (n/(n+1))) * d |^ n
          .= ((n+1)/(n+1)-n/(n+1) + a/d |^ n *(n/(n+1))) * d |^ n by
XCMPLX_1:60
          .= ((n+1-n)/(n+1) + a/d |^ n *(n/(n+1))) * d |^ n
          .= (1/(n+1) + (n/(n+1) * a)/d |^ n) * d |^ n
          .= 1/(n+1) * d |^ n + (n/(n+1) * a)/d |^ n * d |^ n
          .= 1/(n+1) * d |^ n + n/(n+1) * a by A9,A18,XCMPLX_1:88;
        then
A26:    (1 + n * (-b/d)) * d |^ n > 1/(n+1) * a + n/(n+1) * a by A19,XREAL_1:8;
        (d - b) |^ n = ((d - b) * 1) |^ n
          .= ((d - b) * (d" * d)) |^ n by A15,XCMPLX_0:def 7
          .= ((d - b) * d" * d) |^ n
          .= ((d - b) / d * d) |^ n
          .= ((d/d - b/d) * d) |^ n
          .= ((1 - b/d) * d) |^ n by A15,XCMPLX_1:60
          .= (1 + -b/d) |^ n * d |^ n by NEWTON:12;
        then (d - b) |^ n >= (1 + n * (-b/d)) * d |^ n by A9,A18,A22,XREAL_1:66
;
        then (d - b) |^ n > ((n + 1)/(n+1)) * a by A26,XXREAL_0:2;
        then
A27:    (d - b) |^ n > 1 * a by XCMPLX_1:60;
        ex e be Real st e = c & 0 < e & e |^ n <= a by A23;
        hence contradiction by A25,A27,XXREAL_0:2;
      end;
      not d |^ n < a
      proof
        set b = min(1/2 , (a1 / d |^ n - 1)/3 |^ n );
        set c = (1 + b) * d;
A28:    c |^ n = (1 + b) |^ n * d |^ n by NEWTON:12;
A29:    0 < 3 |^ n by Th13;
        3 |^ n * b <= (a / d |^ n - 1)/3 |^ n * 3 |^ n by XREAL_1:66
,XXREAL_0:17;
        then 3 |^ n * b <= a / d |^ n - 1 by A29,XCMPLX_1:88;
        then
A30:    1 + 3 |^ n * b <= a / d |^ n by XREAL_1:21;
A31:    d |^ n > 0 by A15,Th13;
A32:    d |^ n <> 0 by A15,Th13;
        assume d |^ n < a;
        then d |^ n / d |^ n < a / d |^ n by A31,XREAL_1:76;
        then 1 < a / d |^ n by A32,XCMPLX_1:60;
        then 1 - 1 < a / d |^ n - 1 by XREAL_1:11;
        then (a / d |^ n - 1) / 3 |^ n > 0/3 |^ n by A29,XREAL_1:76;
        then
A33:    b > 0 by XXREAL_0:15;
        then 1 + b > 1 + 0 by XREAL_1:8;
        then
A34:    (1 + b) * d > 1 * d by A15,XREAL_1:70;
        b <= 1/2 by XXREAL_0:17;
        then b < 1 by XXREAL_0:2;
        then (1 + b) |^ n <= 1 + 3 |^ n * b by A33,Th25;
        then (1 + b) |^ n <= a / d |^ n by A30,XXREAL_0:2;
        then c |^ n <= a / d |^ n * d |^ n by A31,A28,XREAL_1:66;
        then
A35:    c |^ n <= a by A32,XCMPLX_1:88;
        (1 + b) * d > 0 * d by A15,A33,XREAL_1:70;
        then c in X by A35;
        hence contradiction by A12,A34,SEQ_4:def 4;
      end;
      hence a = d |^ n & d>0 by A15,A17,XXREAL_0:1;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    now
      let b,c be real number;
      assume that
A36:  b |^ n = a and
A37:  b>0 and
A38:  c |^ n = a and
A39:  c>0 and
A40:  b <> c;
      per cases by A40,XXREAL_0:1;
      suppose
        b > c;
        hence contradiction by A1,A36,A38,A39,Lm1;
      end;
      suppose
        c > b;
        hence contradiction by A1,A36,A37,A38,Lm1;
      end;
    end;
    hence thesis;
  end;
end;

definition
  let n;
  let a be Real;
  redefine func n -Root a -> Real;
  coherence by XREAL_0:def 1;
end;

Lm2: for n being Nat st a>0 & n>=1 holds (n -Root a) |^ n = a & n -Root (a |^
n) = a
proof
  let n be Nat;
  assume that
A1: a>0 and
A2: n>=1;
  thus (n -Root a) |^ n = a by A1,A2,Def3;
  assume
A3: n -Root (a |^ n) <> a;
  per cases by A3,XXREAL_0:1;
  suppose
A4: n -Root (a |^ n) > a;
    a |^ n > 0 by A1,Th13;
    hence contradiction by A1,A2,A4,Def3;
  end;
  suppose
A5: n -Root (a |^ n) < a;
    a |^ n > 0 by A1,Th13;
    hence contradiction by A1,A2,A5,Def3;
  end;
end;

canceled;

theorem Th28:
  for n being Nat st a>=0 & n>=1 holds (n -Root a) |^ n = a & n
  -Root (a |^ n) = a
proof
  let n be Nat;
  assume that
A1: a>=0 and
A2: n>=1;
  per cases by A1;
  suppose
    a>0;
    hence thesis by A2,Lm2;
  end;
  suppose
A3: a=0;
    reconsider k=n, k1=1 as Integer;
    reconsider m=k-k1 as Element of NAT by A2,INT_1:18;
A4: 0 |^ n = 0 |^ (m+1) .= 0 |^ m * 0 |^ 1 by NEWTON:13
      .= 0 |^ m * 0 GeoSeq.(0+1) by Def1
      .= 0 |^ m * (0 GeoSeq.0 * 0) by Th4
      .= 0;
    hence (n -Root a) |^ n = a by A2,A3,Def3;
    thus thesis by A2,A3,A4,Def3;
  end;
end;

theorem Th29:
  n>=1 implies n -Root 1 = 1
proof
A1: 1 |^ n = 1 by NEWTON:15;
  assume n>=1;
  hence thesis by A1,Def3;
end;

theorem Th30:
  a>=0 implies 1 -Root a = a
proof
  assume
A1: a>=0;
  a |^ 1 = a GeoSeq.(0+1) by Def1
    .= a GeoSeq.0 * a by Th4
    .= 1*a by Th4
    .= a;
  hence thesis by A1,Th28;
end;

theorem Th31:
  a>=0 & b>=0 & n>=1 implies n -Root (a*b) = n -Root a * n -Root b
proof
  assume that
A1: a>=0 and
A2: b>=0 and
A3: n>=1 and
A4: n -Root (a*b) <> n -Root a * n -Root b;
A5: (n -Root a * n -Root b) |^ n = (n -Root a) |^ n * (n -Root b) |^ n by
NEWTON:12
    .= a * (n -Root b) |^ n by A1,A3,Th28
    .= a * b by A2,A3,Th28;
  per cases by A1,A2;
  suppose
A6: a>0 & b>0;
    then
A7: n -Root b > 0 by A3,Def3;
A8: n -Root a > 0 by A3,A6,Def3;
    a*b>0 by A6,XREAL_1:131;
    then
A9: n -Root (a*b) > 0 by A3,Def3;
    per cases by A4,XXREAL_0:1;
    suppose
      n -Root (a*b) < n -Root a * n -Root b;
      hence contradiction by A3,A5,A9,Th28;
    end;
    suppose
      n -Root (a*b) > n -Root a * n -Root b;
      hence contradiction by A3,A5,A8,A7,Th28;
    end;
  end;
  suppose
A10: a=0 & b>0;
    then n -Root (a*b) = 0 by A3,Def3;
    hence contradiction by A4,A10;
  end;
  suppose
A11: a>0 & b=0;
    then n -Root (a*b) = 0 by A3,Def3;
    hence contradiction by A4,A11;
  end;
  suppose
A12: a=0 & b=0;
    then n -Root (a*b) = 0 by A3,Def3;
    hence contradiction by A4,A12;
  end;
end;

theorem Th32:
  a>0 & n>=1 implies n -Root (1/a) = 1/(n -Root a)
proof
  assume that
A1: a>0 and
A2: n>=1 and
A3: n -Root (1/a) <> 1/(n -Root a);
A4: n -Root a > 0 by A1,A2,Def3;
A5: (1/(n -Root a)) |^ n = 1 / (n -Root a) |^ n by Th14
    .= 1/a by A1,A2,Lm2;
A6: n -Root (1/a) > 0 by A1,A2,Def3;
  per cases by A3,XXREAL_0:1;
  suppose
    n -Root (1/a) > 1/(n -Root a);
    hence contradiction by A2,A4,A5,Lm2;
  end;
  suppose
    n -Root (1/a) < 1/(n -Root a);
    hence contradiction by A2,A5,A6,Lm2;
  end;
end;

theorem
  a>=0 & b>0 & n>=1 implies n -Root (a/b) = n -Root a / n -Root b
proof
  assume that
A1: a>=0 and
A2: b>0 and
A3: n>=1;
  thus n -Root (a/b) = n -Root (a*b")
    .= n -Root a * n -Root (b") by A1,A2,A3,Th31
    .= n -Root a * n -Root (1/b)
    .= n -Root a * (1/n -Root b) by A2,A3,Th32
    .= n -Root a * 1 / n -Root b
    .= n -Root a / n -Root b;
end;

theorem Th34:
  a>=0 & n>=1 & m>=1 implies n -Root (m -Root a) = (n*m) -Root a
proof
  assume that
A1: a>=0 and
A2: n>=1 and
A3: m>=1 and
A4: n -Root (m -Root a) <> (n*m) -Root a;
  per cases by A1;
  suppose
A5: a>0;
    then
A6: m -Root a > 0 by A3,Def3;
    then
A7: n -Root (m -Root a) > 0 by A2,Def3;
A8: (n -Root (m -Root a)) |^ (n*m) = ((n -Root (m -Root a)) |^ n) |^ m by
NEWTON:14
      .= (m -Root a) |^ m by A2,A6,Lm2
      .= a by A1,A3,Th28;
A9: n*m>=1 by A2,A3,XREAL_1:161;
    then
A10: (n*m) -Root a > 0 by A5,Def3;
    per cases by A4,XXREAL_0:1;
    suppose
      n -Root (m -Root a) < (n*m) -Root a;
      hence contradiction by A5,A8,A9,A7,Def3;
    end;
    suppose
      (n*m) -Root a < n -Root (m -Root a);
      hence contradiction by A5,A8,A9,A10,Def3;
    end;
  end;
  suppose
A11: a=0;
    n*m>=1 by A2,A3,XREAL_1:161;
    then
A12: (n*m) -Root a = 0 by A11,Def3;
    m -Root a = 0 by A3,A11,Def3;
    hence contradiction by A2,A4,A12,Def3;
  end;
end;

theorem Th35:
  a>=0 & n>=1 & m>=1 implies n -Root a * m -Root a = (n*m) -Root ( a |^ (n+m))
proof
  assume that
A1: a>=0 and
A2: n>=1 and
A3: m>=1 and
A4: n -Root a * m -Root a <> (n*m) -Root (a |^ (n+m));
A5: (n -Root a * m -Root a) |^ (n*m) = (n -Root a) |^ (n*m) * (m -Root a) |^
  (n*m) by NEWTON:12
    .= ((n -Root a) |^ n) |^ m * (m -Root a) |^ (n*m) by NEWTON:14
    .= a |^ m * (m -Root a) |^ (m*n) by A1,A2,Th28
    .= a |^ m * ((m -Root a) |^ m) |^ n by NEWTON:14
    .= a |^ m * a |^ n by A1,A3,Th28
    .= a |^ (n+m) by NEWTON:13;
A6: n*m>=1 by A2,A3,XREAL_1:161;
  per cases by A1;
  suppose
A7: a>0;
    then
A8: m -Root a > 0 by A3,Def3;
    n -Root a > 0 by A2,A7,Def3;
    then
A9: n -Root a * m -Root a > 0 by A8,XREAL_1:131;
A10: a |^ (n+m) > 0 by A7,Th13;
    then
A11: (n*m) -Root (a |^ (n+m)) > 0 by A6,Def3;
    per cases by A4,XXREAL_0:1;
    suppose
      n -Root a * m -Root a < (n*m) -Root (a |^ (n+m));
      hence contradiction by A5,A6,A10,A9,Def3;
    end;
    suppose
      n -Root a * m -Root a > (n*m) -Root (a |^ (n+m));
      hence contradiction by A5,A6,A10,A11,Def3;
    end;
  end;
  suppose
A12: a=0;
    reconsider k=n, k1=1 as Integer;
    reconsider m1=k-k1 as Element of NAT by A2,INT_1:18;
A13: a |^ (n+m) = a |^ (m1+m+1) .= a |^ (m1+m) * a by NEWTON:11
      .= 0 by A12;
    n -Root a = 0 by A2,A12,Def3;
    hence contradiction by A4,A6,A13,Def3;
  end;
end;

theorem Th36:
  0<=a & a<=b & n>=1 implies n -Root a <= n -Root b
proof
  assume that
A1: a>=0 and
A2: a<=b and
A3: n>=1 and
A4: n -Root a > n -Root b;
A5: now
    per cases by A1,A2;
    suppose
      b > 0;
      hence n -Root b >= 0 by A3,Def3;
    end;
    suppose
      b=0;
      hence n -Root b >= 0 by A3,Def3;
    end;
  end;
  (n -Root a) |^ n = a by A1,A3,Th28;
  then (n -Root a) |^ n <= (n -Root b) |^ n by A1,A2,A3,Th28;
  hence contradiction by A3,A4,A5,Th18;
end;

theorem Th37:
  a>=0 & a<b & n>=1 implies n -Root a < n -Root b
proof
  assume that
A1: a>=0 and
A2: a<b and
A3: n>=1 and
A4: n -Root a >= n -Root b;
  (n -Root a) |^ n = a by A1,A3,Th28;
  then
A5: (n -Root a) |^ n < (n -Root b) |^ n by A1,A2,A3,Lm2;
  n -Root b > 0 by A1,A2,A3,Def3;
  hence contradiction by A4,A5,Th17;
end;

theorem Th38:
  a>=1 & n>=1 implies n -Root a >= 1 & a >= n -Root a
proof
  assume that
A1: a>=1 and
A2: n>=1;
  n -Root a >= n -Root 1 by A1,A2,Th36;
  hence n -Root a >= 1 by A2,Th29;
  n -Root a <= n -Root (a |^ n) by A1,A2,Th20,Th36;
  hence thesis by A1,A2,Lm2;
end;

theorem Th39:
  0<=a & a<1 & n>=1 implies a <= n -Root a & n -Root a < 1
proof
  assume that
A1: 0<=a and
A2: a<1 and
A3: n>=1;
A4: now
    per cases by A1;
    suppose
      a>0;
      hence a |^ n >= 0 by Th13;
    end;
    suppose
      a=0;
      hence a |^ n >=0;
    end;
  end;
  now
    per cases by A1;
    suppose
      a>0;
      hence a |^ n <= a by A2,A3,Th22;
    end;
    suppose
      a=0;
      hence a |^ n <= a by A3,NEWTON:16;
    end;
  end;
  then n -Root (a |^ n) <= n -Root a by A3,A4,Th36;
  hence a <= n -Root a by A1,A3,Th28;
  n -Root a < n -Root 1 by A1,A2,A3,Th37;
  hence thesis by A3,Th29;
end;

theorem Th40:
  a>0 & n>=1 implies n -Root a - 1 <= (a-1)/n
proof
  assume that
A1: a>0 and
A2: n>=1;
  n -Root a > 0 by A1,A2,Def3;
  then n -Root a - 1 > 0 - 1 by XREAL_1:11;
  then (1 + (n -Root a - 1)) |^ n >= 1 + n*(n -Root a - 1) by Th24;
  then a >= 1 + n*(n -Root a - 1) by A1,A2,Lm2;
  then a - 1 >= n*(n -Root a - 1) by XREAL_1:21;
  then (a - 1)/n >= n*(n -Root a - 1)/n by XREAL_1:74;
  hence thesis by A2,XCMPLX_1:90;
end;

theorem
  a>=0 implies 2-Root a = sqrt a
proof
  assume that
A1: a>=0 and
A2: 2-Root a <> sqrt a;
A3: sqrt a >= 0 by A1,SQUARE_1:def 4;
  sqrt a |^ 2 = (sqrt a)^2 by NEWTON:100
    .= a by A1,SQUARE_1:def 4;
  hence contradiction by A2,A3,Th28;
end;

Lm3: for s being Real_Sequence, a st a >= 1 & (for n st n>=1 holds s.n = n
-Root a) holds s is convergent & lim s = 1
proof
  reconsider s2 = NAT --> 1 as Real_Sequence by FUNCOP_1:57;
  let s be Real_Sequence;
  let a such that
A1: a>=1;
  deffunc O(Element of NAT) = (a-1)/($1+1);
  consider s1 being Real_Sequence such that
A2: for n holds s1.n = O(n) from SEQ_1:sch 1;
  set s3 = s2 + s1;
A3: s1 is convergent by A2,SEQ_4:46;
  then
A4: s3 is convergent by SEQ_2:19;
  assume
A5: for n st n>=1 holds s.n = n -Root a;
A6: now
    let n;
A7: n+1>=0+1 by XREAL_1:8;
    set b = (s^\1).n - 1;
A8: (s^\1).n = s.(n+1) by NAT_1:def 3
      .= (n+1) -Root a by A5,A7;
    then
A9: (s^\1).n >= 1 by A1,A7,Th38;
    then (s^\1).n-1>=0 by XREAL_1:50;
    then
A10: -1 < b by XXREAL_0:2;
    a = (1+b) |^ (n+1) by A1,A7,A8,Lm2;
    then a >= 1 + (n+1)*b by A10,Th24;
    then a - 1 >= 1 + (n+1)*b - 1 by XREAL_1:11;
    then (a - 1)*(n+1)" >= (n+1)"*((n+1)*b) by XREAL_1:66;
    then (a - 1)*(n+1)" >= (n+1)"*(n+1)*b;
    then (a - 1)*(n+1)" >= 1*b by XCMPLX_0:def 7;
    then (a-1)/(n+1) + 1 >= (s^\1).n - 1 + 1 by XREAL_1:8;
    then s2.n + (a-1)/(n+1) >= (s^\1).n by FUNCOP_1:13;
    then s2.n + s1.n >= (s^\1).n by A2;
    hence s2.n<=(s^\1).n & (s^\1).n<=s3.n by A9,FUNCOP_1:13,SEQ_1:11;
  end;
  lim s1 = 0 by A2,SEQ_4:46;
  then
A11: lim s3 = s2.0 + 0 by A3,SEQ_4:59
    .= 1 by FUNCOP_1:13;
A12: lim s2 = s2.0 by SEQ_4:41
    .= 1 by FUNCOP_1:13;
  then
A13: s^\1 is convergent by A4,A11,A6,SEQ_2:33;
  hence s is convergent by SEQ_4:35;
  lim (s^\1) = 1 by A4,A11,A12,A6,SEQ_2:34;
  hence thesis by A13,SEQ_4:36;
end;

theorem
  for s being Real_Sequence st a > 0 & (for n st n>=1 holds s.n = n
  -Root a) holds s is convergent & lim s = 1
proof
  let s be Real_Sequence;
  assume
A1: a>0;
  assume
A2: for n st n>=1 holds s.n = n -Root a;
  per cases;
  suppose
    a>=1;
    hence thesis by A2,Lm3;
  end;
  suppose
A3: a<1;
    then a/a<1/a by A1,XREAL_1:76;
    then
A4: 1<=1/a by A1,XCMPLX_1:60;
    deffunc O(Element of NAT) = $1 -Root (1/a);
    consider s1 being Real_Sequence such that
A5: for n holds s1.n = O(n) from SEQ_1:sch 1;
A6: for n holds n>=1 implies s1.n=n -Root (1/a) by A5;
    then
A7: lim s1 = 1 by A4,Lm3;
A8: s1 is convergent by A4,A6,Lm3;
A9: now
      let b be real number;
      assume b>0;
      then consider m1 such that
A10:  for m st m1<=m holds abs(s1.m-1)<b by A8,A7,SEQ_2:def 7;
      take n=m1+1;
      let m;
      assume
A11:  n<=m;
A12:  n>=0+1 by XREAL_1:8;
      then
A13:  1<=m by A11,XXREAL_0:2;
      then
A14:  m -Root a<1 by A1,A3,Th39;
A15:  (m -Root a) <> 0 by A1,A13,Def3;
      then
A16:  abs(1/(m -Root a) - 1) = abs(1/(m -Root a) - (m -Root a)/(m -Root a
      )) by XCMPLX_1:60
        .= abs((1 - m -Root a)/(m -Root a))
        .= abs((1 - m -Root a)*(m -Root a)")
        .= abs(1 - m -Root a)*abs((m -Root a)") by COMPLEX1:151;
      0<m -Root a by A1,A3,A13,Th39;
      then (m -Root a)*(m -Root a)"<1*(m -Root a)" by A14,XREAL_1:70;
      then 1<(m -Root a)" by A15,XCMPLX_0:def 7;
      then
A17:  1<abs((m -Root a)") by ABSVALUE:def 1;
      0<>1 - m -Root a by A1,A3,A13,Th39;
      then abs(1 - m -Root a) > 0 by COMPLEX1:133;
      then
A18:  1*abs(1 - m -Root a) < abs(1 - m -Root a)*abs((m -Root a)") by A17,
XREAL_1:70;
      m1<=n by XREAL_1:31;
      then m1<=m by A11,XXREAL_0:2;
      then abs(s1.m-1)<b by A10;
      then abs(m -Root (1/a) - 1)<b by A5;
      then abs(1/(m -Root a) - 1)<b by A1,A13,Th32;
      then abs(-(m -Root a - 1)) < b by A16,A18,XXREAL_0:2;
      then abs(m -Root a - 1) < b by COMPLEX1:138;
      hence abs(s.m - 1) < b by A2,A12,A11,XXREAL_0:2;
    end;
    hence s is convergent by SEQ_2:def 6;
    hence thesis by A9,SEQ_2:def 7;
  end;
end;

definition
  let a be real number;
  let k;
  func a #Z k equals
  :Def4:
  a |^ abs k if k >= 0, (a |^ abs k)" if k < 0;
  consistency;
  coherence;
end;

registration
  let a be real number;
  let k;
  cluster a #Z k -> real;
  coherence
  proof
    a #Z k = a |^ abs(k) or a #Z k = (a |^ abs(k))" by Def4;
    hence thesis;
  end;
end;

definition
  let a be Real;
  let k;
  redefine func a #Z k -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem Th44:
  a #Z 0 = 1
proof
  thus a #Z 0 = a |^ abs(0) by Def4
    .= a |^ 0 by ABSVALUE:7
    .= a GeoSeq.0 by Def1
    .= 1 by Th4;
end;

theorem Th45:
  a #Z 1 = a
proof
  thus a #Z 1 = a |^ abs(1) by Def4
    .= a |^ (0+1) by ABSVALUE:def 1
    .= a GeoSeq.(0+1) by Def1
    .= a GeoSeq.0 * a by Th4
    .= 1*a by Th4
    .= a;
end;

theorem Th46:
  for n being natural number holds a #Z n = a |^ n
proof
  let n be natural number;
  thus a #Z n = a |^ abs(n) by Def4
    .= a |^ n by ABSVALUE:def 1;
end;

Lm4: 1" = 1;

theorem Th47:
  1 #Z k = 1
proof
  per cases;
  suppose
    k>=0;
    hence 1 #Z k = 1 |^ abs(k) by Def4
      .= 1 by NEWTON:15;
  end;
  suppose
    k<0;
    then 1 #Z k = (1 |^ abs(k))" by Def4;
    hence thesis by Lm4,NEWTON:15;
  end;
end;

theorem
  a<>0 implies a #Z k <> 0
proof
  assume
A1: a<>0;
  per cases;
  suppose
    k>=0;
    then a #Z k = a |^ abs(k) by Def4;
    hence thesis by A1,Th12;
  end;
  suppose
    k<0;
    then a #Z k = (a |^ abs(k))" by Def4;
    hence thesis by A1,Th12,XCMPLX_1:203;
  end;
end;

theorem Th49:
  a>0 implies a #Z k > 0
proof
  assume
A1: a>0;
  per cases;
  suppose
    k>=0;
    then a #Z k = a |^ abs(k) by Def4;
    hence thesis by A1,Th13;
  end;
  suppose
A2: k<0;
A3: a |^ abs(k) > 0 by A1,Th13;
    a #Z k = (a |^ abs(k))" by A2,Def4;
    hence thesis by A3;
  end;
end;

theorem Th50:
  (a*b) #Z k = a #Z k * b #Z k
proof
  per cases;
  suppose
A1: k>=0;
    hence (a*b) #Z k = (a*b) |^ abs(k) by Def4
      .= a |^ abs(k) * b |^ abs(k) by NEWTON:12
      .= a #Z k * b |^ abs(k) by A1,Def4
      .= a #Z k * b #Z k by A1,Def4;
  end;
  suppose
A2: k<0;
    hence (a*b) #Z k = ((a*b) |^ abs(k))" by Def4
      .= (a |^ abs(k) * b |^ abs(k))" by NEWTON:12
      .= (a |^ abs(k))" * (b |^ abs(k))" by XCMPLX_1:205
      .= a #Z k * (b |^ abs(k))" by A2,Def4
      .= a #Z k * b #Z k by A2,Def4;
  end;
end;

theorem Th51:
  a #Z (-k) = 1/a #Z k
proof
  per cases;
  suppose
A1: k>0;
    then -k<-0 by XREAL_1:26;
    hence a #Z (-k) = (a |^ abs(-k))" by Def4
      .= (a |^ abs(k))" by COMPLEX1:138
      .= 1 / a |^ abs(k)
      .= 1 / a #Z k by A1,Def4;
  end;
  suppose
A2: k=0;
    hence a #Z (-k) = 1/1 by Th44
      .= 1/a GeoSeq.0 by Th4
      .= 1/a |^ 0 by Def1
      .= 1/a #Z k by A2,Th46;
  end;
  suppose
A3: k<0;
    then a #Z k = (a |^ abs(k))" by Def4
      .= 1 / a |^ abs(k)
      .= 1 / a |^ abs(-k) by COMPLEX1:138
      .= 1 / a #Z (-k) by A3,Def4;
    hence thesis;
  end;
end;

theorem Th52:
  (1/a) #Z k = 1/a #Z k
proof
  per cases;
  suppose
A1: k>=0;
    hence (1/a) #Z k = (1/a) |^ abs(k) by Def4
      .= 1 / a |^ abs(k) by Th14
      .= 1 / a #Z k by A1,Def4;
  end;
  suppose
A2: k<0;
    hence (1/a) #Z k = ((1/a) |^ abs(k))" by Def4
      .= (1 / a |^ abs(k))" by Th14
      .= (a |^ abs(k))""
      .= (a #Z k)" by A2,Def4
      .= 1/a #Z k;
  end;
end;

theorem Th53:
  for m,n being natural number holds a<>0 implies a #Z (m-n) = a |^ m / a |^ n
proof
  let m,n be natural number;
  assume
A1: a<>0;
  per cases;
  suppose
    m-n>=0;
    then reconsider m1 = m-n as Element of NAT by INT_1:16;
A2: a #Z (m-n) * a |^ n = a |^ m1 * a |^ n by Th46
      .= a |^ (m1+n) by NEWTON:13
      .= a |^ m;
    a |^ n <> 0 by A1,Th12;
    hence thesis by A2,XCMPLX_1:90;
  end;
  suppose
    m-n<0;
    then -(m-n)>0 by XREAL_1:60;
    then reconsider m1 = n-m as Element of NAT by INT_1:16;
A3: a #Z (n-m) = a #Z (-(m-n)) .= 1/a #Z (m-n) by Th51;
    a #Z (n-m) * a |^ m = a |^ m1 * a |^ m by Th46
      .= a |^ (m1+m) by NEWTON:13
      .= a |^ n;
    then
A4: a |^ m / a #Z (m-n) = a |^ n by A3;
    a |^ n <> 0 by A1,Th12;
    hence thesis by A4,XCMPLX_1:54;
  end;
end;

theorem Th54:
  a<>0 implies a #Z (k+l) = a #Z k * a #Z l
proof
  assume
A1: a<>0;
  per cases;
  suppose
A2: k>=0 & l>=0;
    then
A3: k*l>=0;
    thus a #Z (k+l) = a |^ abs(k+l) by A2,Def4
      .= a |^ (abs(k)+abs(l)) by A3,ABSVALUE:24
      .= a |^ abs(k) * a |^ abs(l) by NEWTON:13
      .= a #Z k * a |^ abs(l) by A2,Def4
      .= a #Z k * a #Z l by A2,Def4;
  end;
  suppose
A4: k>=0 & l<0;
    then reconsider m = k as Element of NAT by INT_1:16;
    reconsider n = -l as Element of NAT by A4,INT_1:16;
    k+l = m - n;
    hence a #Z (k+l) = a |^ m / a |^ n by A1,Th53
      .= a |^ m * (a |^ n)"
      .= a |^ abs(k) * (a |^ n)" by ABSVALUE:def 1
      .= a |^ abs(k) * (a |^ abs(-l))" by ABSVALUE:def 1
      .= a |^ abs(k) * (a |^ abs(l))" by COMPLEX1:138
      .= a #Z k * (a |^ abs(l))" by A4,Def4
      .= a #Z k * a #Z l by A4,Def4;
  end;
  suppose
A5: k<0 & l>=0;
    then reconsider m = l as Element of NAT by INT_1:16;
    reconsider n = -k as Element of NAT by A5,INT_1:16;
    k+l = m - n;
    hence a #Z (k+l) = a |^ m / a |^ n by A1,Th53
      .= a |^ m * (a |^ n)"
      .= a |^ abs(l) * (a |^ n)" by ABSVALUE:def 1
      .= a |^ abs(l) * (a |^ abs(-k))" by ABSVALUE:def 1
      .= a |^ abs(l) * (a |^ abs(k))" by COMPLEX1:138
      .= a #Z l * (a |^ abs(k))" by A5,Def4
      .= a #Z k * a #Z l by A5,Def4;
  end;
  suppose
A6: k<0 & l<0;
    then
A7: k*l>=0;
    thus a #Z (k+l) = (a |^ abs(k+l))" by A6,Def4
      .= (a |^ (abs(k)+abs(l)))" by A7,ABSVALUE:24
      .= (a |^ abs(k) * a |^ abs(l))" by NEWTON:13
      .= (a |^ abs(k))" * (a |^ abs(l))" by XCMPLX_1:205
      .= a #Z k * (a |^ abs(l))" by A6,Def4
      .= a #Z k * a #Z l by A6,Def4;
  end;
end;

theorem Th55:
  a #Z k #Z l = a #Z (k*l)
proof
  per cases;
  suppose
A1: k*l > 0;
    per cases by A1,XREAL_1:136;
    suppose
A2:   k>0 & l>0;
      hence a #Z k #Z l = a #Z k |^ abs(l) by Def4
        .=a |^ abs(k) |^ abs(l) by A2,Def4
        .=a |^ (abs(k)*abs(l)) by NEWTON:14
        .=a |^ abs(k*l) by COMPLEX1:151
        .=a #Z (k*l) by A1,Def4;
    end;
    suppose
A3:   k<0 & l<0;
      hence a #Z k #Z l = (a #Z k |^ abs(l))" by Def4
        .=(((a |^ abs(k))") |^ abs(l))" by A3,Def4
        .=((1 / a |^ abs(k)) |^ abs(l))"
        .=(1 / a |^ abs(k) |^ abs(l))" by Th14
        .=(a |^ abs(k) |^ abs(l))""
        .=a |^ (abs(k)*abs(l)) by NEWTON:14
        .=a |^ abs(k*l) by COMPLEX1:151
        .=a #Z (k*l) by A1,Def4;
    end;
  end;
  suppose
A4: k*l < 0;
    per cases by A4,XREAL_1:135;
    suppose
A5:   k<0 & l>0;
      hence a #Z k #Z l = a #Z k |^ abs(l) by Def4
        .=((a |^ abs(k))") |^ abs(l) by A5,Def4
        .=(1 / a |^ abs(k)) |^ abs(l)
        .=1 / a |^ abs(k) |^ abs(l) by Th14
        .=1 / a |^ (abs(k)*abs(l)) by NEWTON:14
        .=1 / a |^ abs(k*l) by COMPLEX1:151
        .=(a |^ abs(k*l))"
        .=a #Z (k*l) by A4,Def4;
    end;
    suppose
A6:   k>0 & l<0;
      hence a #Z k #Z l = (a #Z k |^ abs(l))" by Def4
        .=(a |^ abs(k) |^ abs(l))" by A6,Def4
        .=(a |^ (abs(k)*abs(l)))" by NEWTON:14
        .=(a |^ abs(k*l))" by COMPLEX1:151
        .=a #Z (k*l) by A4,Def4;
    end;
  end;
  suppose
A7: k*l = 0;
    per cases by A7,XCMPLX_1:6;
    suppose
      k=0;
      hence a #Z k #Z l = 1 #Z l by Th44
        .= 1 by Th47
        .= a #Z (k*l) by A7,Th44;
    end;
    suppose
      l=0;
      hence a #Z k #Z l = 1 by Th44
        .= a #Z (k*l) by A7,Th44;
    end;
  end;
end;

theorem Th56:
  a>0 & n>=1 implies (n -Root a) #Z k = n -Root (a #Z k)
proof
  assume that
A1: a>0 and
A2: n>=1;
A3: n -Root a > 0 by A1,A2,Def3;
A4: m>=1 implies (n -Root a) |^ m = n -Root (a |^ m)
  proof
    assume that
A5: m>=1 and
A6: (n -Root a) |^ m <> n -Root (a |^ m);
A7: a |^ m > 0 by A1,Th13;
    then
A8: n -Root (a |^ m) >= 0 by A2,Def3;
A9: m -Root (n -Root (a |^ m)) = (n*m) -Root (a |^ m) by A2,A5,A7,Th34
      .= n -Root (m -Root (a |^ m)) by A2,A5,A7,Th34
      .= n -Root a by A1,A5,Lm2;
A10: m -Root ((n -Root a) |^ m) = n -Root a by A3,A5,Lm2;
A11: (n -Root a) |^ m >= 0 by A3,Th13;
    per cases by A6,XXREAL_0:1;
    suppose
      (n -Root a) |^ m < n -Root (a |^ m);
      hence contradiction by A5,A9,A11,Lm2;
    end;
    suppose
      (n -Root a) |^ m > n -Root (a |^ m);
      hence contradiction by A5,A9,A10,A8,Th37;
    end;
  end;
  per cases;
  suppose
A12: k>0;
    then abs(k)>0 by ABSVALUE:def 1;
    then
A13: abs(k)>=0+1 by NAT_1:13;
    thus (n -Root a) #Z k = (n -Root a) |^ abs(k) by A12,Def4
      .= n -Root (a |^ abs(k)) by A4,A13
      .= n -Root (a #Z k) by A12,Def4;
  end;
  suppose
A14: k<0;
    then abs(k)>0 by COMPLEX1:133;
    then
A15: abs(k)>=0+1 by NAT_1:13;
A16: a |^ abs(k) > 0 by A1,Th13;
    thus (n -Root a) #Z k = ((n -Root a) |^ abs(k))" by A14,Def4
      .= (n -Root (a |^ abs(k)))" by A4,A15
      .= 1/n -Root (a |^ abs(k))
      .= n -Root (1/a |^ abs(k)) by A2,A16,Th32
      .= n -Root ((a |^ abs(k))")
      .= n -Root (a #Z k) by A14,Def4;
  end;
  suppose
A17: k=0;
    then n -Root (a #Z k) = n -Root 1 by Th44
      .= 1 by A2,Th29;
    hence thesis by A17,Th44;
  end;
end;

definition
  let a be real number;
  let p;
  func a #Q p equals
  (denominator p) -Root (a #Z numerator p);
  coherence;
end;

registration
  let a be real number;
  let p;
  cluster a #Q p -> real;
  coherence;
end;

definition
  let a be Real;
  let p;
  redefine func a #Q p -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem Th58:
  p = 0 implies a #Q p = 1
proof
  reconsider i = 0 as Integer;
  assume that
A1: p=0;
  numerator(p)=0 by A1,RAT_1:36;
  hence a #Q p = 1 -Root (a #Z i) by A1,RAT_1:42
    .= 1 -Root 1 by Th44
    .= 1 by Th30;
end;

theorem Th59:
  a > 0 & p = 1 implies a #Q p = a
proof
  assume that
A1: a>0 and
A2: p=1;
A3: denominator(p)=1 by A2,RAT_1:40;
  numerator(p)=p by A2,RAT_1:40;
  hence a #Q p = 1 -Root a by A2,A3,Th45
    .= a by A1,Th30;
end;

Lm5: a>=0 implies a #Z k >= 0
proof
  assume
A1: a>=0;
  per cases by A1;
  suppose
A2: a = 0;
    abs k = 0 or abs k >= 1 by NAT_1:14;
    then (0 |^ abs k)" = 0" or (0 |^ abs k)" = 1" by NEWTON:9,16;
    hence thesis by A2,Def4;
  end;
  suppose
    a > 0;
    hence thesis by Th49;
  end;
end;

theorem Th60:
  for n be Nat st 0 <= a holds a #Q n = a |^ n
proof
  let n be Nat;
A1: denominator n=1 by RAT_1:40;
A2: numerator n=n by RAT_1:40;
  assume 0 <= a;
  hence a #Q n = a #Z n by A1,A2,Lm5,Th30
    .= a |^ n by Th46;
end;

theorem Th61:
  for n be Nat holds n>=1 & p = n" implies a #Q p = n -Root a
proof
  let n be Nat;
  assume that
A1: n>=1 and
A2: p = n";
  reconsider q=n as Rational;
A3: p=1/n by A2;
  then denominator(p)=numerator(q) by A1,RAT_1:88;
  then
A4: denominator(p) = n by RAT_1:40;
  numerator(p)=denominator(q) by A1,A3,RAT_1:88;
  then numerator(p) = 1 by RAT_1:40;
  hence thesis by A4,Th45;
end;

theorem Th62:
  1 #Q p = 1
proof
  thus 1 #Q p = (denominator(p)) -Root 1 by Th47
    .= 1 by Th29,RAT_1:29;
end;

theorem Th63:
  a>0 implies a #Q p > 0
proof
  assume a>0;
  then
A1: a #Z numerator(p) > 0 by Th49;
  denominator(p) >= 1 by RAT_1:29;
  hence thesis by A1,Def3;
end;

theorem Th64:
  a>0 implies a #Q p * a #Q q = a #Q (p+q)
proof
  set dp = denominator(p);
  set dq = denominator(q);
  set np = numerator(p);
  set nq = numerator(q);
A1: dp >= 1 by RAT_1:29;
  reconsider ddq=dq as Integer;
  reconsider ddp=dp as Integer;
A2: denominator(p+q) >= 1 by RAT_1:29;
A3: dp<>0 by RAT_1:def 3;
A4: dq >= 1 by RAT_1:29;
  then
A5: dp*dq >= 1 by A1,XREAL_1:161;
A6: dq<>0 by RAT_1:def 3;
  p+q = np/dp + q by RAT_1:37
    .= np/dp + nq/dq by RAT_1:37
    .= (np*dq+nq*dp)/(dp*dq) by A3,A6,XCMPLX_1:117;
  then consider n such that
A7: np*dq+nq*dp = numerator(p+q)*n and
A8: dp*dq = denominator(p+q)*n by A3,A6,RAT_1:60,XCMPLX_1:6;
  reconsider k=n as Integer;
  assume
A9: a>0;
  then
A10: a #Z (nq-np) >= 0 by Th49;
  n>0 by A4,A1,A8,XREAL_1:161;
  then
A11: n>=0+1 by NAT_1:13;
A12: a #Z np > 0 by A9,Th49;
  then
A13: (a #Z np) |^ (dp+dq)>= 0 by Th13;
A14: a #Z (nq-np) > 0 by A9,Th49;
  then
A15: (a #Z (nq-np)) |^ dp >= 0 by Th13;
A16: a #Q (p+q) > 0 by A9,Th63;
A17: a #Z numerator(p+q) > 0 by A9,Th49;
  thus a #Q p * a #Q q = dp -Root (a #Z np) * dq -Root (a #Z (np+(nq-np)))
    .= dp -Root (a #Z np) * dq -Root (a #Z np * a #Z (nq-np)) by A9,Th54
    .= dp -Root (a #Z np)*(dq -Root (a #Z np)*dq -Root (a #Z (nq-np))) by A12
,A10,Th31,RAT_1:29
    .= dp -Root (a #Z np) * dq -Root (a #Z np) * dq -Root (a #Z (nq-np))
    .= (dp*dq) -Root ((a #Z np) |^ (dp+dq))*dq -Root (a #Z (nq-np)) by A12,A4
,A1,Th35
    .= (dp*dq) -Root ((a #Z np) |^ (dp+dq)) * dq -Root (dp -Root ((a #Z (nq-
  np)) |^ dp)) by A14,A1,Lm2
    .= (dp*dq) -Root ((a #Z np) |^ (dp+dq)) * (dp*dq) -Root ((a #Z (nq-np))
  |^ dp) by A4,A1,A15,Th34
    .= (dp*dq) -Root (((a #Z np) |^ (dp+dq)) * ((a #Z (nq-np)) |^ dp)) by A4,A1
,A15,A13,Th31,XREAL_1:161
    .= (dp*dq) -Root (((a #Z np) #Z (ddp+ddq)) * ((a #Z (nq-np)) |^ dp)) by
Th46
    .= (dp*dq) -Root (((a #Z np) #Z (ddp+ddq)) * ((a #Z (nq-np)) #Z ddp)) by
Th46
    .= (dp*dq) -Root ( a #Z (np*(ddp+ddq)) * ((a #Z (nq-np)) #Z ddp) ) by Th55
    .= (dp*dq) -Root ( a #Z (np*(ddp+ddq)) * a #Z ((nq-np)*ddp) ) by Th55
    .= (dp*dq) -Root (a #Z (np*ddp+np*ddq+(nq-np)*ddp) ) by A9,Th54
    .= (denominator(p+q)*n) -Root (a #Z numerator(p+q) #Z k) by A7,A8,Th55
    .= ((denominator(p+q)*n) -Root (a #Z numerator(p+q))) #Z k by A5,A8,A17
,Th56
    .= ((n*denominator(p+q)) -Root (a #Z numerator(p+q))) |^ n by Th46
    .= (n -Root (a #Q (p+q))) |^ n by A11,A2,A17,Th34
    .= a #Q (p+q) by A11,A16,Lm2;
end;

theorem Th65:
  a>0 implies 1 / a #Q p = a #Q (-p)
proof
  assume a>0;
  then
A1: a #Z numerator(p) > 0 by Th49;
  thus a #Q (-p) = (denominator(-p)) -Root (a #Z (-numerator(p))) by RAT_1:87
    .= (denominator(p)) -Root (a #Z (-numerator(p))) by RAT_1:87
    .= (denominator(p)) -Root (1/a #Z numerator(p)) by Th51
    .= 1 / a #Q p by A1,Th32,RAT_1:29;
end;

theorem Th66:
  a>0 implies a #Q p / a #Q q = a #Q (p-q)
proof
  assume
A1: a>0;
  thus a #Q (p-q) = a #Q (p+-q) .= a #Q p * a #Q (-q) by A1,Th64
    .= a #Q p * (1/a #Q q) by A1,Th65
    .= a #Q p * 1 / a #Q q
    .= a #Q p / a #Q q;
end;

theorem Th67:
  a>0 & b>0 implies (a*b) #Q p = a #Q p * b #Q p
proof
  assume that
A1: a>0 and
A2: b>0;
A3: a #Z numerator(p) >= 0 by A1,Th49;
A4: b #Z numerator(p) >= 0 by A2,Th49;
  thus (a*b) #Q p = (denominator(p)) -Root (a #Z numerator(p) * b #Z numerator
  (p)) by Th50
    .= a #Q p * b #Q p by A3,A4,Th31,RAT_1:29;
end;

theorem Th68:
  a>0 implies (1/a) #Q p = 1/a #Q p
proof
  assume
A1: a>0;
  thus (1/a) #Q p = (denominator(p)) -Root (1/a #Z numerator(p)) by Th52
    .= (denominator(p)) -Root (a #Z (-numerator(p))) by Th51
    .= (denominator(p)) -Root (a #Z numerator(-p)) by RAT_1:87
    .= a #Q (-p) by RAT_1:87
    .= 1/a #Q p by A1,Th65;
end;

theorem Th69:
  a>0 & b>0 implies (a/b) #Q p = a #Q p / b #Q p
proof
  assume that
A1: a>0 and
A2: b>0;
  thus (a/b) #Q p = (a*(1/b)) #Q p .= a #Q p * (1/b) #Q p by A1,A2,Th67
    .= a #Q p * (1/b #Q p) by A2,Th68
    .= a #Q p / b #Q p;
end;

theorem Th70:
  a > 0 implies a #Q p #Q q = a #Q (p*q)
proof
A1: denominator(p)>=1 by RAT_1:29;
A2: denominator(q)<>0 by RAT_1:def 3;
A3: denominator(p)<>0 by RAT_1:def 3;
  then
A4: denominator(p)*denominator(q) <> 0 by A2,XCMPLX_1:6;
  p = numerator(p)/denominator(p) by RAT_1:37;
  then p*q = (numerator(p)/denominator(p))*(numerator(q)/denominator(q)) by
RAT_1:37
    .= (numerator(p)/denominator(p))*numerator(q)/denominator(q)
    .= (numerator(p)*numerator(q))/(denominator(p)*denominator(q)) by
XCMPLX_1:84;
  then consider n such that
A5: numerator(p)*numerator(q) = numerator(p*q)*n and
A6: denominator(p)*denominator(q) = denominator(p*q)*n by A3,A2,RAT_1:60
,XCMPLX_1:6;
A7: denominator(p*q)*n >= 0+1 by A4,A6,NAT_1:13;
  n>0 by A4,A6;
  then
A8: n>=0+1 by NAT_1:13;
  reconsider k=n as Integer;
A9: denominator(q)>=1 by RAT_1:29;
A10: denominator(p*q) >= 1 by RAT_1:29;
  assume
A11: a>0;
  then
A12: a #Z (numerator(p)*numerator(q)) >= 0 by Th49;
A13: a #Q (p*q) > 0 by A11,Th63;
A14: a #Z numerator(p*q) > 0 by A11,Th49;
  a #Z numerator(p) > 0 by A11,Th49;
  hence a #Q p #Q q = (denominator(q)) -Root ((denominator(p)) -Root (a #Z
  numerator(p) #Z numerator(q))) by Th56,RAT_1:29
    .= (denominator(q)) -Root ((denominator(p)) -Root (a #Z (numerator(p)*
  numerator(q)))) by Th55
    .= (denominator(p*q)*n) -Root (a #Z (numerator(p*q)*k)) by A1,A9,A12,A5,A6
,Th34
    .= (denominator(p*q)*n) -Root (a #Z numerator(p*q) #Z k) by Th55
    .= ((denominator(p*q)*n) -Root (a #Z numerator(p*q))) #Z k by A14,A7,Th56
    .= ((n*denominator(p*q)) -Root (a #Z numerator(p*q))) |^ n by Th46
    .= (n -Root (a #Q (p*q))) |^ n by A8,A10,A14,Th34
    .= a #Q (p*q) by A8,A13,Lm2;
end;

theorem Th71:
  a>=1 & p >= 0 implies a #Q p >= 1
proof
  assume that
A1: a>=1 and
A2: p>=0;
  numerator(p)>=0 by A2,RAT_1:76;
  then reconsider n = numerator(p) as Element of NAT by INT_1:16;
A3: a #Z numerator(p) = a |^ n by Th46;
  a |^ n >= 1 |^ n by A1,Th17;
  then
A4: a #Z numerator(p) >= 1 by A3,NEWTON:15;
  denominator(p) >= 1 by RAT_1:29;
  hence thesis by A4,Th38;
end;

theorem Th72:
  a>=1 & p<=0 implies a #Q p <= 1
proof
  assume that
A1: a>=1 and
A2: p<=0;
  a #Q (-p) >= 1 by A1,A2,Th71;
  then 1 / a #Q p >= 1 by A1,Th65;
  hence thesis by XREAL_1:193;
end;

theorem Th73:
  a>1 & p>0 implies a #Q p > 1
proof
  assume that
A1: a>1 and
A2: p>0;
A3: numerator(p)>0 by A2,RAT_1:77;
  then reconsider n = numerator(p) as Element of NAT by INT_1:16;
  n>=0+1 by A3,NAT_1:13;
  then
A4: a |^ n > 1 |^ n by A1,Lm1;
  a #Z numerator(p) = a |^ n by Th46;
  then a #Z numerator(p) > 1 by A4,NEWTON:15;
  then (denominator(p)) -Root (a #Z numerator(p))>(denominator(p)) -Root 1 by
Th37,RAT_1:29;
  hence thesis by Th29,RAT_1:29;
end;

theorem Th74:
  a>=1 & p>=q implies a #Q p >= a #Q q
proof
  assume that
A1: a>=1 and
A2: p>=q;
  per cases by A2,XXREAL_0:1;
  suppose
    p=q;
    hence thesis;
  end;
  suppose
    p>q;
    then
A3: p-q>=0 by XREAL_1:52;
A4: a #Q q <> 0 by A1,Th63;
A5: a #Q p / a #Q q = a #Q (p-q) by A1,Th66;
    a #Q q >= 0 by A1,Th63;
    then a #Q p / a #Q q * a #Q q >= 1 * a #Q q by A1,A3,A5,Th71,XREAL_1:66;
    hence thesis by A4,XCMPLX_1:88;
  end;
end;

theorem Th75:
  a>1 & p>q implies a #Q p > a #Q q
proof
  assume that
A1: a>1 and
A2: p>q;
A3: p-q>0 by A2,XREAL_1:52;
A4: a #Q p / a #Q q = a #Q (p-q) by A1,Th66;
A5: a #Q q <> 0 by A1,Th63;
  a #Q q > 0 by A1,Th63;
  then a #Q p / a #Q q * a #Q q > 1 * a #Q q by A1,A3,A4,Th73,XREAL_1:70;
  hence thesis by A5,XCMPLX_1:88;
end;

theorem Th76:
  a>0 & a<1 & p>0 implies a #Q p < 1
proof
  reconsider q=0 as Rational;
  assume that
A1: a>0 and
A2: a<1 and
A3: p>0;
  1/a>1 by A1,A2,Lm4,XREAL_1:90;
  then (1/a) #Q p > (1/a) #Q q by A3,Th75;
  then (1/a) #Q p > 1 by Th58;
  then 1/a #Q p > 1 by A1,Th68;
  hence thesis by XREAL_1:187;
end;

theorem
  a>0 & a<=1 & p<=0 implies a #Q p >= 1
proof
  assume that
A1: a>0 and
A2: a<=1 and
A3: p<=0;
  1/a >= 1 by A1,A2,Lm4,XREAL_1:87;
  then (1/a) #Q p <= 1 by A3,Th72;
  then
A4: 1/a #Q p <= 1 by A1,Th68;
  a #Q p > 0 by A1,Th63;
  hence thesis by A4,XREAL_1:189;
end;

definition
  let IT be Real_Sequence;
  redefine attr IT is rational-valued means
  :Def6:
  for n holds IT.n is Rational;
  compatibility
  proof
    thus IT is rational-valued implies for n holds IT.n is Rational;
    assume
A1: for n holds IT.n is Rational;
    now
      let x;
      per cases;
      suppose x in dom IT;
        then x in NAT by FUNCT_2:def 1;
        hence IT.x is rational by A1;
      end;
      suppose not x in dom IT;
        hence IT.x is rational by FUNCT_1:def 4;
      end;
    end;
    hence thesis by VALUED_0:10;
  end;
end;

registration
  cluster rational-valued Real_Sequence;
  existence
  proof
    reconsider s = NAT --> 0 as Real_Sequence by FUNCOP_1:57;
    take s;
    let n;
    thus thesis;
  end;
end;

definition
  mode Rational_Sequence is rational-valued Real_Sequence;
end;

registration
  let s be Rational_Sequence, n;
  cluster s.n -> rational;
  coherence;
end;

canceled;

theorem Th79:
  for a be real number ex s being Rational_Sequence st
  s is convergent & lim s = a & for n holds s.n<=a
proof
  let a be real number;
  deffunc O(Element of NAT) = [\($1+1)*a/]/($1+1);
  consider s being Real_Sequence such that
A1: for n holds s.n = O(n) from SEQ_1:sch 1;
  now
    let n;
    s.n = [\(n+1)*a/]/(n+1) by A1;
    hence s.n is Rational;
  end;
  then reconsider s as Rational_Sequence by Def6;
  deffunc O(Element of NAT) = 1/($1+1);
  consider s2 being Real_Sequence such that
A2: for n holds s2.n = O(n) from SEQ_1:sch 1;
  reconsider a1 = a as Real by XREAL_0:def 1;
  reconsider s1 = NAT --> a1 as Real_Sequence;
  take s;
  set s3 = s1 - s2;
A3: s2 is convergent by A2,SEQ_4:45;
  then
A4: s3 is convergent by SEQ_2:25;
A5: now
    let n;
    (n+1)*a <= [\(n+1)*a/] + 1 by INT_1:52;
    then (n+1)*a - 1 <= [\(n+1)*a/] + 1 - 1 by XREAL_1:11;
    then ((n+1)*a - 1)*(n+1)" <= [\(n+1)*a/]/(n+1) by XREAL_1:66;
    then (a/(n+1))*(n+1) - 1/(n+1) <= s.n by A1;
    then a - 1/(n+1) <= s.n by XCMPLX_1:88;
    then s1.n - 1/(n+1) <= s.n by FUNCOP_1:13;
    then
A6: s1.n - s2.n <= s.n by A2;
    [\(n+1)*a/] <= (n+1)*a by INT_1:def 4;
    then [\(n+1)*a/]*(n+1)" <= a*(n+1)*(n+1)" by XREAL_1:66;
    then [\(n+1)*a/]*(n+1)" <= a*((n+1)*(n+1)");
    then [\(n+1)*a/]*(n+1)" <= a*1 by XCMPLX_0:def 7;
    then [\(n+1)*a/]/(n+1) <= s1.n by FUNCOP_1:13;
    hence s3.n <= s.n & s.n <= s1.n by A1,A6,RFUNCT_2:6;
  end;
  lim s2 = 0 by A2,SEQ_4:45;
  then
A7: lim s3 = s1.0 - 0 by A3,SEQ_4:59
    .= a by FUNCOP_1:13;
A8: lim s1 = s1.0 by SEQ_4:41
    .= a by FUNCOP_1:13;
  hence s is convergent by A4,A7,A5,SEQ_2:33;
  thus lim s = a by A4,A7,A8,A5,SEQ_2:34;
  let n;
  s.n<=s1.n by A5;
  hence thesis by FUNCOP_1:13;
end;

theorem Th80:
  ex s being Rational_Sequence st s is convergent & lim s = a &
  for n holds s.n>=a
proof
  deffunc O(Element of NAT) = [/($1+1)*a\]/($1+1);
  consider s being Real_Sequence such that
A1: for n holds s.n = O(n) from SEQ_1:sch 1;
  now
    let n;
    [/(n+1)*a\]/(n+1) = s.n by A1;
    hence s.n is Rational;
  end;
  then reconsider s as Rational_Sequence by Def6;
  deffunc O(Element of NAT) = 1/($1+1);
  consider s2 being Real_Sequence such that
A2: for n holds s2.n = O(n) from SEQ_1:sch 1;
  take s;
  a in REAL by XREAL_0:def 1;
  then reconsider s1 = NAT --> a as Real_Sequence by FUNCOP_1:57;
  set s3 = s1 + s2;
A3: s2 is convergent by A2,SEQ_4:45;
  then
A4: s3 is convergent by SEQ_2:19;
A5: now
    let n;
    (n+1)*a + 1 >= [/(n+1)*a\] by INT_1:def 5;
    then ((n+1)*a + 1)*(n+1)" >= [/(n+1)*a\]/(n+1) by XREAL_1:66;
    then (a/(n+1))*(n+1) + 1/(n+1) >= s.n by A1;
    then a + 1/(n+1) >= s.n by XCMPLX_1:88;
    then s1.n + 1/(n+1) >= s.n by FUNCOP_1:13;
    then
A6: s1.n + s2.n >= s.n by A2;
    [/(n+1)*a\] >= (n+1)*a by INT_1:def 5;
    then [/(n+1)*a\]*(n+1)" >= a*(n+1)*(n+1)" by XREAL_1:66;
    then [/(n+1)*a\]*(n+1)" >= a*((n+1)*(n+1)");
    then [/(n+1)*a\]*(n+1)" >= a*1 by XCMPLX_0:def 7;
    then [/(n+1)*a\]/(n+1) >= s1.n by FUNCOP_1:13;
    hence s1.n <= s.n & s.n <= s3.n by A1,A6,SEQ_1:11;
  end;
  lim s2 = 0 by A2,SEQ_4:45;
  then
A7: lim s3 = s1.0 + 0 by A3,SEQ_4:59
    .= a by FUNCOP_1:13;
A8: lim s1 = s1.0 by SEQ_4:41
    .= a by FUNCOP_1:13;
  hence s is convergent by A4,A7,A5,SEQ_2:33;
  thus lim s = a by A4,A7,A8,A5,SEQ_2:34;
  let n;
  s.n>=s1.n by A5;
  hence thesis by FUNCOP_1:13;
end;

definition
  let a be real number;
  let s be Rational_Sequence;
  func a #Q s -> Real_Sequence means
  :Def7:
  for n holds it.n = a #Q (s.n);
  existence
  proof
    deffunc O(Element of NAT) = a #Q (s.$1);
    consider s1 being Real_Sequence such that
A1: for n holds s1.n = O(n) from SEQ_1:sch 1;
    take s1;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let s1,s2 be Real_Sequence;
    assume that
A2: for n holds s1.n = a #Q (s.n) and
A3: for n holds s2.n = a #Q (s.n);
    now
      let n;
      thus s1.n = a #Q (s.n) by A2
        .= s2.n by A3;
    end;
    hence thesis by FUNCT_2:113;
  end;
end;

Lm6: for s being Rational_Sequence, a st s is convergent & a>=1 holds a #Q s
is convergent
proof
  let s be Rational_Sequence;
  let a;
  assume that
A1: s is convergent and
A2: a>=1;
  s is bounded by A1,SEQ_2:27;
  then consider d be real number such that
  0<d and
A3: for n holds abs(s.n)<d by SEQ_2:15;
  consider m2 such that
A4: d<m2 by SEQ_4:10;
  reconsider m2 as Rational;
  now
A5: a #Q m2 >= 0 by A2,Th63;
    let c be real number;
    assume
A6: c>0;
    consider m1 such that
A7: a #Q m2*(a-1)/c < m1 by SEQ_4:10;
    m1+1>=m1 by XREAL_1:31;
    then a #Q m2 * (a-1)/c < m1+1 by A7,XXREAL_0:2;
    then a #Q m2 * (a-1)/c*c < c*(m1+1) by A6,XREAL_1:70;
    then a #Q m2 * (a-1) < c*(m1+1) by A6,XCMPLX_1:88;
    then a #Q m2 * (a-1)/(m1+1) < (m1+1)*c/(m1+1) by XREAL_1:76;
    then a #Q m2 * (a-1)/(m1+1) < c/(m1+1)*(m1+1);
    then
A8: a #Q m2 * (a-1)/(m1+1) < c by XCMPLX_1:88;
    consider n such that
A9: for m st n<=m holds abs(s.m -s.n)<(m1+1)" by A1,SEQ_4:58;
    take n;
    let m;
    assume m>=n;
    then
A10: abs(s.m -s.n)<=(m1+1)" by A9;
A11: m1+1>=0+1 by NAT_1:13;
    then (m1+1) -Root a - 1 <= (a-1)/(m1+1) by A2,Th40;
    then
A12: (a #Q m2)*((m1+1) -Root a - 1) <= (a #Q m2)*((a-1)/(m1+1)) by A5,
XREAL_1:66;
A13: a #Q (s.n) <> 0 by A2,Th63;
A14: abs(a #Q (s.m) - a #Q (s.n)) = abs((a #Q (s.m) - a #Q (s.n))*1)
      .=abs((a #Q (s.m) - a #Q (s.n))*(a #Q (s.n)/a #Q (s.n))) by A13,
XCMPLX_1:60
      .=abs(a #Q (s.n)*(a #Q (s.m) - a #Q (s.n))/a #Q (s.n))
      .=abs(a #Q (s.n)*((a #Q (s.m) - a #Q (s.n))/a #Q (s.n)))
      .=abs(a #Q (s.n))*abs((a #Q (s.m) - a #Q (s.n))/a #Q (s.n)) by
COMPLEX1:151
      .=abs(a #Q (s.n))*abs(a #Q (s.m)/a #Q (s.n) - a #Q (s.n)/a #Q (s.n))
      .=abs(a #Q (s.n))*abs(a #Q (s.m)/a #Q (s.n) - 1) by A13,XCMPLX_1:60
      .=abs(a #Q (s.n))*abs(a #Q (s.m-s.n) - 1) by A2,Th66;
A15: s.n<=abs(s.n) by ABSVALUE:11;
    reconsider m3 = (m1+1)" as Rational;
A16: abs(a #Q (s.m-s.n) - 1) >= 0 by COMPLEX1:132;
A17: a #Q (s.m-s.n) <> 0 by A2,Th63;
    s.m -s.n<=abs(s.m -s.n) by ABSVALUE:11;
    then s.m -s.n <= (m1+1)" by A10,XXREAL_0:2;
    then a #Q (s.m-s.n) <= a #Q m3 by A2,Th74;
    then a #Q (s.m-s.n) <= (m1+1) -Root a by A11,Th61;
    then
A18: a #Q (s.m-s.n) - 1 <= (m1+1) -Root a - 1 by XREAL_1:11;
A19: a #Q (s.m-s.n) > 0 by A2,Th63;
A20: now
      per cases;
      suppose
        s.m-s.n>=0;
        then a #Q (s.m-s.n) >= 1 by A2,Th71;
        then a #Q (s.m-s.n) - 1 >= 0 by XREAL_1:50;
        hence abs(a #Q (s.m-s.n) - 1) <= (m1+1) -Root a - 1 by A18,
ABSVALUE:def 1;
      end;
      suppose
A21:    s.m-s.n<0;
A22:    -(s.m -s.n)<=abs(-(s.m -s.n)) by ABSVALUE:11;
        abs(s.m -s.n) = abs(-(s.m -s.n)) by COMPLEX1:138;
        then -(s.m -s.n) <= m3 by A10,A22,XXREAL_0:2;
        then a #Q (-(s.m-s.n)) <= a #Q m3 by A2,Th74;
        then a #Q (-(s.m-s.n)) <= (m1+1) -Root a by A11,Th61;
        then
A23:    a #Q (-(s.m-s.n)) - 1 <= (m1+1) -Root a - 1 by XREAL_1:11;
        a #Q (-(s.m-s.n)) >= 1 by A2,A21,Th71;
        then a #Q (-(s.m-s.n)) - 1 >= 0 by XREAL_1:50;
        then
A24:    abs(a #Q (-(s.m-s.n)) - 1) <= (m1+1) -Root a - 1 by A23,ABSVALUE:def 1;
        a #Q (s.m-s.n) <= 1 by A2,A21,Th72;
        then
A25:    abs(a #Q (s.m-s.n)) <= 1 by A19,ABSVALUE:def 1;
        abs(a #Q (-(s.m-s.n)) - 1) >= 0 by COMPLEX1:132;
        then
A26:    abs(a #Q (s.m-s.n))*abs(a #Q (-(s.m-s.n)) - 1) <= 1*abs(a #Q (-(s
        .m-s.n)) - 1) by A25,XREAL_1:66;
        abs(a #Q (s.m-s.n) - 1) = abs((a #Q (s.m-s.n) - 1)*1)
          .= abs((a #Q (s.m-s.n) - 1)*(a #Q (s.m-s.n)/a #Q (s.m-s.n))) by A17,
XCMPLX_1:60
          .= abs(a #Q (s.m-s.n)*(a #Q (s.m-s.n) - 1)/a #Q (s.m-s.n))
          .= abs(a #Q (s.m-s.n)*((a #Q (s.m-s.n) - 1)/a #Q (s.m-s.n)))
          .= abs(a #Q (s.m-s.n))*abs((a #Q (s.m-s.n) - 1)/a #Q (s.m-s.n)) by
COMPLEX1:151
          .= abs(a #Q (s.m-s.n))*abs(a #Q (s.m-s.n)/a #Q (s.m-s.n) - 1/a #Q
        (s.m-s.n))
          .= abs(a #Q (s.m-s.n))*abs(1 - 1/a #Q (s.m-s.n)) by A17,XCMPLX_1:60
          .= abs(a #Q (s.m-s.n))*abs(1 - a #Q (-(s.m-s.n))) by A2,Th65
          .= abs(a #Q (s.m-s.n))*abs(-(1 - a #Q (-(s.m-s.n)))) by COMPLEX1:138
          .= abs(a #Q (s.m-s.n))*abs(a #Q (-(s.m-s.n)) - 1);
        hence abs(a #Q (s.m-s.n) - 1) <= (m1+1) -Root a - 1 by A24,A26,
XXREAL_0:2;
      end;
    end;
A27: a #Q (s.n) > 0 by A2,Th63;
    abs(s.n)<=m2 by A3,A4,XXREAL_0:2;
    then s.n<=m2 by A15,XXREAL_0:2;
    then a #Q (s.n) <= a #Q m2 by A2,Th74;
    then
A28: abs(a #Q (s.n)) <= a #Q m2 by A27,ABSVALUE:def 1;
    abs(a #Q (s.n)) >= 0 by A27,ABSVALUE:def 1;
    then abs(a #Q (s.n))*abs(a #Q (s.m-s.n) - 1) <= (a #Q m2)*((m1+1) -Root a
    - 1) by A20,A16,A28,XREAL_1:68;
    then abs(a #Q (s.m) - a #Q (s.n)) <= (a #Q m2)*(a-1)/(m1+1) by A14,A12,
XXREAL_0:2;
    then abs(a #Q (s.m) - a #Q (s.n)) < c by A8,XXREAL_0:2;
    then abs((a #Q s).m - a #Q (s.n)) < c by Def7;
    hence abs((a #Q s).m - (a #Q s).n) < c by Def7;
  end;
  hence thesis by SEQ_4:58;
end;

canceled;

theorem Th82:
  for s being Rational_Sequence st s is convergent & a>0 holds a
  #Q s is convergent
proof
  let s be Rational_Sequence;
  assume that
A1: s is convergent and
A2: a>0;
  per cases;
  suppose
    a>=1;
    hence thesis by A1,Lm6;
  end;
  suppose
A3: a<1;
    then a/a<1/a by A2,XREAL_1:76;
    then 1<1/a by A2,XCMPLX_1:60;
    then
A4: (1/a) #Q s is convergent by A1,Lm6;
    s is bounded by A1,SEQ_2:27;
    then consider d be real number such that
    0<d and
A5: for n holds abs(s.n)<d by SEQ_2:15;
    reconsider d as Real by XREAL_0:def 1;
    consider m1 such that
A6: 2*d < m1 by SEQ_4:10;
    reconsider m1 as Rational;
A7: a #Q m1 > 0 by A2,Th63;
    now
      let c be real number;
      assume
A8:   c>0;
      then c* a #Q m1 > 0 by A7,XREAL_1:131;
      then consider n such that
A9:   for m st n<=m holds abs(((1/a) #Q s).m -((1/a) #Q s).n)<c* a #Q
      m1 by A4,SEQ_4:58;
      take n;
      let m;
      assume m>=n;
      then
A10:  abs(((1/a) #Q s).m -((1/a) #Q s).n)<c* a #Q m1 by A9;
A11:  a #Q (s.m) <> 0 by A2,Th63;
A12:  a #Q (s.m+s.n) > 0 by A2,Th63;
      abs(s.m)<d by A5;
      then
A13:  abs(s.m)+abs(s.n)<d+d by A5,XREAL_1:10;
      abs(s.m+s.n)<=abs(s.m)+abs(s.n) by COMPLEX1:142;
      then abs(s.m+s.n)<d+d by A13,XXREAL_0:2;
      then abs(s.m+s.n)<m1 by A6,XXREAL_0:2;
      then
A14:  abs(-(s.m+s.n))<m1 by COMPLEX1:138;
      -(s.m+s.n)<=abs(-(s.m+s.n)) by ABSVALUE:11;
      then -(s.m+s.n)<m1 by A14,XXREAL_0:2;
      then
A15:  m1-(-(s.m+s.n))>0 by XREAL_1:52;
A16:  a #Q (s.n) <> 0 by A2,Th63;
      abs(((1/a) #Q s).m -((1/a) #Q s).n) = abs((1/a) #Q (s.m) -((1/a) #Q
      s) . n ) by Def7
        .= abs((1/a) #Q (s.m) -(1/a) #Q (s.n)) by Def7
        .= abs(1/a #Q (s.m) -(1/a) #Q (s.n)) by A2,Th68
        .= abs(1/a #Q (s.m) -1/a #Q (s.n)) by A2,Th68
        .= abs((a #Q (s.m))" -1/a #Q (s.n))
        .= abs((a #Q (s.m))" -(a #Q (s.n))")
        .= abs(a #Q (s.m) - a #Q (s.n))/(abs(a #Q (s.m))*abs(a #Q (s.n))) by
A11,A16,SEQ_2:11
        .= abs(a #Q (s.m) - a #Q (s.n))/(abs(a #Q (s.m) * a #Q (s.n))) by
COMPLEX1:151
        .= abs(a #Q (s.m) - a #Q (s.n))/abs(a #Q (s.m+s.n)) by A2,Th64
        .= abs(a #Q (s.m) - a #Q (s.n))/(a #Q (s.m+s.n)) by A12,ABSVALUE:def 1;
      then
A17:  (abs(a #Q (s.m) - a #Q (s.n))/a #Q (s.m+s.n))*a #Q (s.m+s.n) < c*a
      #Q m1 * a #Q (s.m+s.n) by A10,A12,XREAL_1:70;
      a #Q (s.m+s.n) <> 0 by A2,Th63;
      then
A18:  abs(a #Q (s.m) - a #Q (s.n))<c * a #Q m1 * a #Q (s.m+s.n) by A17,
XCMPLX_1:88;
      a #Q m1 * a #Q (s.m+s.n) = a #Q (m1+(s.m+s.n)) by A2,Th64;
      then c*(a #Q m1 * a #Q (s.m+s.n)) < 1*c by A2,A3,A8,A15,Th76,XREAL_1:70;
      then abs(a #Q (s.m) - a #Q (s.n))<c by A18,XXREAL_0:2;
      then abs((a #Q s).m - a #Q (s.n)) < c by Def7;
      hence abs((a #Q s).m - (a #Q s).n) < c by Def7;
    end;
    hence thesis by SEQ_4:58;
  end;
end;

Lm7: for s1,s2 being Rational_Sequence, a st s1 is convergent & s2 is
convergent & lim s1 = lim s2 & a>=1 holds lim a #Q s1 = lim a #Q s2
proof
  let s1,s2 be Rational_Sequence;
  let a;
  assume that
A1: s1 is convergent and
A2: s2 is convergent and
A3: lim s1 = lim s2 and
A4: a>=1;
A5: s1-s2 is convergent by A1,A2,SEQ_2:25;
A6: a #Q s2 is convergent by A2,A4,Th82;
A7: now
    let n;
    thus (((a #Q s1) - (a #Q s2)) + a #Q s2).n = ((a #Q s1) - (a #Q s2)).n + (
    a #Q s2).n by SEQ_1:11
      .= (a #Q s1).n - (a #Q s2).n + (a #Q s2).n by RFUNCT_2:6
      .= (a #Q s1).n;
  end;
  s2 is bounded by A2,SEQ_2:27;
  then consider d be real number such that
  0<d and
A8: for n holds abs(s2.n)<d by SEQ_2:15;
  consider m2 such that
A9: d<m2 by SEQ_4:10;
  reconsider m2 as Rational;
A10: lim(s1-s2) = lim s1 - lim s2 by A1,A2,SEQ_2:26
    .= 0 by A3;
A11: now
A12: a #Q m2 >= 0 by A4,Th63;
    let c be real number;
    assume
A13: c>0;
    consider m1 such that
A14: a #Q m2*(a-1)/c < m1 by SEQ_4:10;
    m1+1>=m1 by XREAL_1:31;
    then a #Q m2 * (a-1)/c < m1+1 by A14,XXREAL_0:2;
    then a #Q m2 * (a-1)/c*c < c*(m1+1) by A13,XREAL_1:70;
    then a #Q m2 * (a-1) < c*(m1+1) by A13,XCMPLX_1:88;
    then a #Q m2 * (a-1)/(m1+1) < (m1+1)*c/(m1+1) by XREAL_1:76;
    then a #Q m2 * (a-1)/(m1+1) < c/(m1+1)*(m1+1);
    then
A15: a #Q m2 * (a-1)/(m1+1) < c by XCMPLX_1:88;
    consider n such that
A16: for m st n<=m holds abs((s1-s2).m -0)<(m1+1)" by A5,A10,SEQ_2:def 7;
    take n;
    let m;
    assume m>=n;
    then abs((s1-s2).m -0)<(m1+1)" by A16;
    then
A17: abs(s1.m -s2.m)<=(m1+1)" by RFUNCT_2:6;
A18: m1+1>=0+1 by NAT_1:13;
    then (m1+1) -Root a - 1 <= (a-1)/(m1+1) by A4,Th40;
    then
A19: (a #Q m2)*((m1+1) -Root a - 1) <= (a #Q m2)*((a-1)/(m1+1)) by A12,
XREAL_1:66;
A20: a #Q (s2.m) <> 0 by A4,Th63;
A21: abs(a #Q (s1.m) - a #Q (s2.m)) = abs((a #Q (s1.m) - a #Q (s2.m))*1)
      .=abs((a #Q (s1.m) - a #Q (s2.m))*(a #Q (s2.m)/a #Q (s2.m))) by A20,
XCMPLX_1:60
      .=abs(a #Q (s2.m)*(a #Q (s1.m) - a #Q (s2.m))/a #Q (s2.m))
      .=abs(a #Q (s2.m)*((a #Q (s1.m) - a #Q (s2.m))/a #Q (s2.m)))
      .=abs(a #Q (s2.m))*abs((a #Q (s1.m) - a #Q (s2.m))/a #Q (s2.m)) by
COMPLEX1:151
      .=abs(a #Q (s2.m))*abs(a #Q (s1.m)/a #Q (s2.m) - a #Q (s2.m)/a #Q (s2.
    m))
      .=abs(a #Q (s2.m))*abs(a #Q (s1.m)/a #Q (s2.m) - 1) by A20,XCMPLX_1:60
      .=abs(a #Q (s2.m))*abs(a #Q (s1.m-s2.m) - 1) by A4,Th66;
A22: s2.m<=abs(s2.m) by ABSVALUE:11;
    reconsider m3 = (m1+1)" as Rational;
A23: abs(a #Q (s1.m-s2.m) - 1) >= 0 by COMPLEX1:132;
A24: a #Q (s1.m-s2.m) <> 0 by A4,Th63;
    s1.m -s2.m<=abs(s1.m -s2.m) by ABSVALUE:11;
    then s1.m -s2.m <= (m1+1)" by A17,XXREAL_0:2;
    then a #Q (s1.m-s2.m) <= a #Q m3 by A4,Th74;
    then a #Q (s1.m-s2.m) <= (m1+1) -Root a by A18,Th61;
    then
A25: a #Q (s1.m-s2.m) - 1 <= (m1+1) -Root a - 1 by XREAL_1:11;
A26: a #Q (s1.m-s2.m) > 0 by A4,Th63;
A27: now
      per cases;
      suppose
        s1.m-s2.m>=0;
        then a #Q (s1.m-s2.m) >= 1 by A4,Th71;
        then a #Q (s1.m-s2.m) - 1 >= 0 by XREAL_1:50;
        hence abs(a #Q (s1.m-s2.m) - 1) <= (m1+1) -Root a - 1 by A25,
ABSVALUE:def 1;
      end;
      suppose
A28:    s1.m-s2.m<0;
A29:    -(s1.m -s2.m)<=abs(-(s1.m -s2.m)) by ABSVALUE:11;
        abs(s1.m -s2.m) = abs(-(s1.m -s2.m)) by COMPLEX1:138;
        then -(s1.m -s2.m) <= m3 by A17,A29,XXREAL_0:2;
        then a #Q (-(s1.m-s2.m)) <= a #Q m3 by A4,Th74;
        then a #Q (-(s1.m-s2.m)) <= (m1+1) -Root a by A18,Th61;
        then
A30:    a #Q (-(s1.m-s2.m)) - 1 <= (m1+1) -Root a - 1 by XREAL_1:11;
        a #Q (-(s1.m-s2.m)) >= 1 by A4,A28,Th71;
        then a #Q (-(s1.m-s2.m)) - 1 >= 0 by XREAL_1:50;
        then
A31:    abs(a #Q (-(s1.m-s2.m)) - 1) <= (m1+1) -Root a - 1 by A30,
ABSVALUE:def 1;
        a #Q (s1.m-s2.m) <= 1 by A4,A28,Th72;
        then
A32:    abs(a #Q (s1.m-s2.m)) <= 1 by A26,ABSVALUE:def 1;
        abs(a #Q (-(s1.m-s2.m)) - 1) >= 0 by COMPLEX1:132;
        then
A33:    abs(a #Q (s1.m-s2.m))*abs(a #Q (-(s1.m-s2.m)) - 1) <= 1*abs(a #Q
        (-(s1.m-s2.m)) - 1) by A32,XREAL_1:66;
        abs(a #Q (s1.m-s2.m) - 1) = abs((a #Q (s1.m-s2.m) - 1)*1)
          .= abs((a #Q (s1.m-s2.m) - 1)*(a #Q (s1.m-s2.m)/a #Q (s1.m-s2.m)))
        by A24,XCMPLX_1:60
          .= abs(a #Q (s1.m-s2.m)*(a #Q (s1.m-s2.m) - 1)/a #Q (s1.m-s2.m))
          .= abs(a #Q (s1.m-s2.m)*((a #Q (s1.m-s2.m) - 1)/a #Q (s1.m-s2.m)))
          .= abs(a #Q (s1.m-s2.m))*abs((a #Q (s1.m-s2.m) - 1)/a #Q (s1.m-s2.
        m)) by COMPLEX1:151
          .= abs(a #Q (s1.m-s2.m))* abs(a #Q (s1.m-s2.m)/a #Q (s1.m-s2.m) -
        1/a #Q (s1.m-s2.m))
          .= abs(a #Q (s1.m-s2.m))*abs(1 - 1/a #Q (s1.m-s2.m)) by A24,
XCMPLX_1:60
          .= abs(a #Q (s1.m-s2.m))*abs(1 - a #Q (-(s1.m-s2.m))) by A4,Th65
          .= abs(a #Q (s1.m-s2.m))*abs(-(1 - a #Q (-(s1.m-s2.m)))) by
COMPLEX1:138
          .= abs(a #Q (s1.m-s2.m))*abs(a #Q (-(s1.m-s2.m)) - 1);
        hence abs(a #Q (s1.m-s2.m) - 1) <= (m1+1) -Root a - 1 by A31,A33,
XXREAL_0:2;
      end;
    end;
A34: a #Q (s2.m) > 0 by A4,Th63;
    abs(s2.m)<=m2 by A8,A9,XXREAL_0:2;
    then s2.m<=m2 by A22,XXREAL_0:2;
    then a #Q (s2.m) <= a #Q m2 by A4,Th74;
    then
A35: abs(a #Q (s2.m)) <= a #Q m2 by A34,ABSVALUE:def 1;
    abs(a #Q (s2.m)) >= 0 by A34,ABSVALUE:def 1;
    then
    abs(a #Q (s2.m))*abs(a #Q (s1.m-s2.m) - 1) <= (a #Q m2)*((m1+1) -Root
    a - 1) by A27,A23,A35,XREAL_1:68;
    then abs(a #Q (s1.m) - a #Q (s2.m))<= (a #Q m2)*(a-1)/(m1+1) by A21,A19,
XXREAL_0:2;
    then abs(a #Q (s1.m) - a #Q (s2.m)) < c by A15,XXREAL_0:2;
    then abs((a #Q s1).m - a #Q (s2.m)) < c by Def7;
    then abs((a #Q s1).m - (a #Q s2).m) < c by Def7;
    hence abs(((a #Q s1) - (a #Q s2)).m - 0) < c by RFUNCT_2:6;
  end;
  then
A36: ((a #Q s1) - (a #Q s2)) is convergent by SEQ_2:def 6;
  then lim((a #Q s1) - (a #Q s2)) = 0 by A11,SEQ_2:def 7;
  then lim(((a #Q s1) - (a #Q s2)) + a #Q s2) = 0 + lim a #Q s2 by A36,A6,
SEQ_2:20
    .= lim a #Q s2;
  hence thesis by A7,FUNCT_2:113;
end;

theorem Th83:
  for s1,s2 being Rational_Sequence, a st s1 is convergent & s2 is
  convergent & lim s1 = lim s2 & a>0 holds a #Q s1 is convergent & a #Q s2 is
  convergent & lim a #Q s1 = lim a #Q s2
proof
  let s1,s2 be Rational_Sequence;
  let a;
  assume that
A1: s1 is convergent and
A2: s2 is convergent and
A3: lim s1 = lim s2 and
A4: a>0;
  thus
A5: a #Q s1 is convergent by A1,A4,Th82;
  s2 is bounded by A2,SEQ_2:27;
  then consider e be real number such that
  0<e and
A6: for n holds abs(s2.n)<e by SEQ_2:15;
  s1 is bounded by A1,SEQ_2:27;
  then consider d be real number such that
  0<d and
A7: for n holds abs(s1.n)<d by SEQ_2:15;
  consider m1 such that
A8: d+e < m1 by SEQ_4:10;
  thus
A9: a #Q s2 is convergent by A2,A4,Th82;
  reconsider m1 as Rational;
A10: a #Q m1 > 0 by A4,Th63;
  per cases;
  suppose
    a>=1;
    hence thesis by A1,A2,A3,Lm7;
  end;
  suppose
A11: a<1;
    then a/a<1/a by A4,XREAL_1:76;
    then 1<1/a by A4,XCMPLX_1:60;
    then
A12: lim (1/a) #Q s1 = lim (1/a) #Q s2 by A1,A2,A3,Lm7;
A13: (1/a) #Q s2 is convergent by A2,A4,Th82;
A14: (1/a) #Q s1 is convergent by A1,A4,Th82;
    then
A15: (1/a) #Q s1 - (1/a) #Q s2 is convergent by A13,SEQ_2:25;
A16: lim ((1/a) #Q s1 - (1/a) #Q s2) = lim (1/a) #Q s1 - lim (1/a) #Q s2
    by A14,A13,SEQ_2:26
      .= 0 by A12;
A17: now
      let c be real number;
      assume
A18:  c>0;
      then c* a #Q m1 > 0 by A10,XREAL_1:131;
      then consider n such that
A19:  for m st n<=m holds abs(((1/a) #Q s1 - (1/a) #Q s2).m-0)<c*a #Q
      m1 by A15,A16,SEQ_2:def 7;
      take n;
      let m;
      assume m>=n;
      then abs(((1/a) #Q s1 - (1/a) #Q s2).m-0)<c*a #Q m1 by A19;
      then
A20:  abs(((1/a) #Q s1).m - ((1/a) #Q s2).m)<c*a #Q m1 by RFUNCT_2:6;
A21:  a #Q (s1.m) <> 0 by A4,Th63;
      abs(s1.m)<d by A7;
      then
A22:  abs(s1.m)+abs(s2.m)<d+e by A6,XREAL_1:10;
      abs(s1.m+s2.m)<=abs(s1.m)+abs(s2.m) by COMPLEX1:142;
      then abs(s1.m+s2.m)<d+e by A22,XXREAL_0:2;
      then abs(s1.m+s2.m)<m1 by A8,XXREAL_0:2;
      then
A23:  abs(-(s1.m+s2.m))<m1 by COMPLEX1:138;
      -(s1.m+s2.m)<=abs(-(s1.m+s2.m)) by ABSVALUE:11;
      then -(s1.m+s2.m)<m1 by A23,XXREAL_0:2;
      then
A24:  m1-(-(s1.m+s2.m))>0 by XREAL_1:52;
A25:  a #Q (s2.m) <> 0 by A4,Th63;
A26:  a #Q (s1.m+s2.m) > 0 by A4,Th63;
      abs(((1/a) #Q s1).m -((1/a) #Q s2).m) = abs((1/a) #Q (s1.m) -((1/a)
      #Q s2).m) by Def7
        .= abs((1/a) #Q (s1.m) -(1/a) #Q (s2.m)) by Def7
        .= abs(1/a #Q (s1.m) -(1/a) #Q (s2.m)) by A4,Th68
        .= abs(1/a #Q (s1.m) -1/a #Q (s2.m)) by A4,Th68
        .= abs((a #Q (s1.m))" -1/a #Q (s2.m))
        .= abs((a #Q (s1.m))" -(a #Q (s2.m))")
        .= abs(a #Q (s1.m) - a #Q (s2.m))/(abs(a #Q (s1.m))*abs(a #Q (s2.m))
      ) by A21,A25,SEQ_2:11
        .= abs(a #Q (s1.m) - a #Q (s2.m))/(abs(a #Q (s1.m) * a #Q (s2.m)))
      by COMPLEX1:151
        .= abs(a #Q (s1.m) - a #Q (s2.m))/abs(a #Q (s1.m+s2.m)) by A4,Th64
        .= abs(a #Q (s1.m) - a #Q (s2.m))/(a #Q (s1.m+s2.m)) by A26,
ABSVALUE:def 1;
      then
A27:  (abs(a #Q (s1.m) - a #Q (s2.m))/a #Q (s1.m+s2.m))*a #Q (s1.m+s2.m)
      < c*a #Q m1 * a #Q (s1.m+s2.m) by A20,A26,XREAL_1:70;
      a #Q (s1.m+s2.m) <> 0 by A4,Th63;
      then
A28:  abs(a #Q (s1.m) - a #Q (s2.m))<c * a #Q m1 * a #Q (s1.m+s2.m) by A27,
XCMPLX_1:88;
      a #Q m1 * a #Q (s1.m+s2.m) = a #Q (m1+(s1.m+s2.m)) by A4,Th64;
      then c*(a #Q m1 * a #Q (s1.m+s2.m)) < 1*c by A4,A11,A18,A24,Th76,
XREAL_1:70;
      then abs(a #Q (s1.m) - a #Q (s2.m))<c by A28,XXREAL_0:2;
      then abs((a #Q s1).m - a #Q (s2.m)) < c by Def7;
      then abs((a #Q s1).m - (a #Q s2).m) < c by Def7;
      hence abs(((a #Q s1) - (a #Q s2)).m - 0) < c by RFUNCT_2:6;
    end;
    then ((a #Q s1) - (a #Q s2)) is convergent by SEQ_2:def 6;
    then lim((a #Q s1) - (a #Q s2)) = 0 by A17,SEQ_2:def 7;
    then 0 = lim (a #Q s1)-lim (a #Q s2) by A5,A9,SEQ_2:26;
    hence thesis;
  end;
end;

definition
  let a,b be real number;
  assume
A1: a > 0;
  func a #R b -> real number means
  :Def8:
  ex s being Rational_Sequence st s is
  convergent & lim s = b & a #Q s is convergent & lim a #Q s = it;
  existence
  proof
    consider s being Rational_Sequence such that
A2: s is convergent and
A3: lim s = b and
    for n holds s.n<=b by Th79;
    take lim a #Q s;
    thus thesis by A1,A2,A3,Th82;
  end;
  uniqueness by A1,Th83;
end;

definition
  let a,b be Real;
  redefine func a #R b -> Real;
  coherence by XREAL_0:def 1;
end;

canceled;

theorem Th85:
  a > 0 implies a #R 0 = 1
proof
  reconsider s = NAT --> 0 as Real_Sequence by FUNCOP_1:57;
  reconsider s as Rational_Sequence;
  assume
A1: a > 0;
  s.0 = 0 by FUNCOP_1:13;
  then
A2: lim s = 0 by SEQ_4:41;
A3: now
    let n be Nat;
    reconsider nn=n as Element of NAT by ORDINAL1:def 13;
    thus a #Q s .n = a #Q (s.nn) by Def7
      .= 1 by Th58,FUNCOP_1:13;
  end;
  then
A4: a #Q s is constant by VALUED_0:def 18;
  a #Q s .0 = 1 by A3;
  then lim (a #Q s) = 1 by A4,SEQ_4:41;
  hence thesis by A1,A2,A4,Def8;
end;

theorem
  a > 0 implies a #R 1 = a
proof
  reconsider s = NAT --> 1 as Real_Sequence by FUNCOP_1:57;
  reconsider s as Rational_Sequence;
  s.0 = 1 by FUNCOP_1:13;
  then
A1: lim s = 1 by SEQ_4:41;
  assume
A2: a > 0;
A3: now
    let n be Nat;
    reconsider nn=n as Element of NAT by ORDINAL1:def 13;
    thus a #Q s .n = a #Q (s.nn) by Def7
      .= a by A2,Th59,FUNCOP_1:13;
  end;
  a in REAL by XREAL_0:def 1;
  then
A4: a #Q s is constant by A3,VALUED_0:def 18;
  a #Q s .0 = a by A3;
  then lim (a #Q s) = a by A4,SEQ_4:41;
  hence thesis by A2,A1,A4,Def8;
end;

theorem Th87:
  1 #R a = 1
proof
  consider s being Rational_Sequence such that
A1: s is convergent and
A2: a = lim s and
  for n holds s.n<=a by Th79;
A3: now
    let n be Nat;
    reconsider nn=n as Element of NAT by ORDINAL1:def 13;
    thus 1 #Q s .n = 1 #Q (s.nn) by Def7
      .= 1 by Th62;
  end;
  then 1 #Q s is constant by VALUED_0:def 18;
  then
A4: lim (1 #Q s) = 1 #Q s .0 by SEQ_4:41
    .= 1 by A3;
  1 #Q s is convergent by A1,Th82;
  hence thesis by A1,A2,A4,Def8;
end;

theorem Th88:
  a>0 implies a #R p = a #Q p
proof
  assume
A1: a>0;
  p in REAL by XREAL_0:def 1;
  then reconsider s = NAT --> p as Real_Sequence by FUNCOP_1:57;
A2: lim s = s.0 by SEQ_4:41
    .= p by FUNCOP_1:13;
  reconsider s as Rational_Sequence;
A3: now
    let n be Nat;
    reconsider nn=n as Element of NAT by ORDINAL1:def 13;
    thus (a #Q s).n = a #Q (s.nn) by Def7
      .= a #Q p by FUNCOP_1:13;
  end;
  a #Q p in REAL by XREAL_0:def 1;
  then
A4: a #Q s is constant by A3,VALUED_0:def 18;
  then lim (a #Q s) = (a #Q s).0 by SEQ_4:41
    .= a #Q p by A3;
  hence thesis by A1,A2,A4,Def8;
end;

theorem Th89:
  a > 0 implies a #R (b+c) = a #R b * a #R c
proof
  consider sb being Rational_Sequence such that
A1: sb is convergent and
A2: b = lim sb and
  for n holds sb.n<=b by Th79;
  consider sc being Rational_Sequence such that
A3: sc is convergent and
A4: c = lim sc and
  for n holds sc.n<=c by Th79;
  assume
A5: a>0;
  then
A6: a #Q sb is convergent by A1,Th82;
A7: a #Q sc is convergent by A5,A3,Th82;
  reconsider s = sb + sc as Rational_Sequence;
A8: lim s = b + c by A1,A2,A3,A4,SEQ_2:20;
A9: now
    let n;
    thus a #Q s .n = a #Q (s.n) by Def7
      .= a #Q (sb.n + sc.n) by SEQ_1:11
      .= a #Q (sb.n) * a #Q (sc.n) by A5,Th64
      .= a #Q (sb.n) * (a #Q sc .n) by Def7
      .= (a #Q sb .n) * (a #Q sc .n) by Def7;
  end;
A10: s is convergent by A1,A3,SEQ_2:19;
  then a #Q s is convergent by A5,Th82;
  hence a #R (b+c) = lim (a #Q s) by A5,A10,A8,Def8
    .= lim ((a #Q sb) (#) (a #Q sc)) by A9,SEQ_1:12
    .= (lim (a #Q sb)) * (lim (a #Q sc)) by A6,A7,SEQ_2:29
    .= a #R b * (lim (a #Q sc)) by A5,A1,A2,A6,Def8
    .= a #R b * a #R c by A5,A3,A4,A7,Def8;
end;

Lm8: a > 0 implies a #R b >= 0
proof
  consider s being Rational_Sequence such that
A1: s is convergent and
A2: b = lim s and
  for n holds s.n<=b by Th79;
  assume
A3: a > 0;
A4: now
    let n;
    a #Q s .n = a #Q (s.n) by Def7;
    hence a #Q s .n >= 0 by A3,Th63;
  end;
  a #Q s is convergent by A3,A1,Th82;
  then a #R b = lim (a #Q s) by A3,A1,A2,Def8;
  hence thesis by A3,A1,A4,Th82,SEQ_2:31;
end;

Lm9: a > 0 implies a #R b <> 0
proof
  assume
A1: a > 0;
  assume a #R b = 0;
  then 0 = a #R b * a #R (-b) .= a #R (b + -b) by A1,Th89
    .= 1 by A1,Th85;
  hence contradiction;
end;

theorem Th90:
  a > 0 implies a #R (-c) = 1 / a #R c
proof
  assume
A1: a>0;
  then 1 = a #R (c+-c) by Th85
    .= a #R (-c) * a #R c by A1,Th89;
  then 1 / a #R c = a #R (-c) * (a #R c / a #R c) by XCMPLX_1:75
    .= a #R (-c) * 1 by A1,Lm9,XCMPLX_1:60;
  hence thesis;
end;

theorem Th91:
  a > 0 implies a #R (b-c) = a #R b / a #R c
proof
  assume
A1: a > 0;
  thus a #R (b-c) = a #R (b + -c) .= a #R b * a #R (-c) by A1,Th89
    .= a #R b * (1 / a #R c) by A1,Th90
    .= a #R b * (1 * (a #R c)")
    .= a #R b / a #R c;
end;

theorem Th92:
  a > 0 & b > 0 implies (a * b) #R c = a #R c * b #R c
proof
  assume that
A1: a>0 and
A2: b>0;
A3: a*b > 0 by A1,A2,XREAL_1:131;
  consider s1 being Rational_Sequence such that
A4: s1 is convergent and
A5: c = lim s1 and
  for n holds s1.n>=c by Th80;
A6: a #Q s1 is convergent by A1,A4,Th82;
A7: b #Q s1 is convergent by A2,A4,Th82;
  now
    let n;
    thus (a*b) #Q s1 .n = (a*b) #Q (s1.n) by Def7
      .= a #Q (s1.n) * b #Q (s1.n) by A1,A2,Th67
      .= (a #Q s1 .n) * b #Q (s1.n) by Def7
      .= (a #Q s1 .n) * (b #Q s1 .n) by Def7;
  end;
  then
A8: (a*b) #Q s1 = a #Q s1 (#) b #Q s1 by SEQ_1:12;
  then (a*b) #Q s1 is convergent by A6,A7,SEQ_2:28;
  hence (a*b) #R c = lim ((a *b) #Q s1) by A3,A4,A5,Def8
    .= lim (a #Q s1) * lim (b #Q s1) by A6,A7,A8,SEQ_2:29
    .= a #R c * lim (b #Q s1) by A1,A4,A5,A6,Def8
    .= a #R c * b #R c by A2,A4,A5,A7,Def8;
end;

theorem Th93:
  a>0 implies (1/a) #R c = 1 / a #R c
proof
  assume
A1: a>0;
  1 = 1 #R c by Th87
    .= ((1/a)*a) #R c by A1,XCMPLX_1:107
    .= (1/a) #R c * a #R c by A1,Th92;
  then 1 / a #R c = (1/a) #R c * (a #R c / a #R c) by XCMPLX_1:75
    .= (1/a) #R c * 1 by A1,Lm9,XCMPLX_1:60;
  hence thesis;
end;

theorem
  a > 0 & b > 0 implies (a/b) #R c = a #R c / b #R c
proof
  assume that
A1: a>0 and
A2: b>0;
  thus (a/b) #R c = (a*b") #R c .= a #R c * (b") #R c by A1,A2,Th92
    .= a #R c * (1/b) #R c
    .= a #R c * (1 / b #R c) by A2,Th93
    .= a #R c * 1 / b #R c
    .= a #R c / b #R c;
end;

theorem Th95:
  a > 0 implies a #R b > 0
proof
  assume
A1: a > 0;
  then a #R b <> 0 by Lm9;
  hence thesis by A1,Lm8;
end;

theorem Th96:
  a>=1 & c>=b implies a #R c >= a #R b
proof
  assume that
A1: a>=1 and
A2: c>=b;
  consider s1 being Rational_Sequence such that
A3: s1 is convergent and
A4: c = lim s1 and
A5: for n holds s1.n>=c by Th80;
A6: a #Q s1 is convergent by A1,A3,Th82;
  consider s2 being Rational_Sequence such that
A7: s2 is convergent and
A8: b = lim s2 and
A9: for n holds s2.n<=b by Th79;
A10: a #Q s2 is convergent by A1,A7,Th82;
  now
    let n;
    s1.n>=c by A5;
    then
A11: s1.n>=b by A2,XXREAL_0:2;
    s2.n<=b by A9;
    then s1.n >= s2.n by A11,XXREAL_0:2;
    then a #Q (s1.n) >= a #Q (s2.n) by A1,Th74;
    then a #Q (s1.n) >= a #Q s2 .n by Def7;
    hence a #Q s1 .n >= a #Q s2 .n by Def7;
  end;
  then lim (a #Q s1) >= lim (a #Q s2) by A6,A10,SEQ_2:32;
  then a #R c >= lim (a #Q s2) by A1,A3,A4,A6,Def8;
  hence thesis by A1,A7,A8,A10,Def8;
end;

theorem Th97:
  a>1 & c>b implies a #R c > a #R b
proof
  assume that
A1: a>1 and
A2: c>b;
  consider p such that
A3: b<p and
A4: p<c by A2,RAT_1:22;
  consider q such that
A5: b<q and
A6: q<p by A3,RAT_1:22;
  consider s2 being Rational_Sequence such that
A7: s2 is convergent and
A8: b = lim s2 and
A9: for n holds s2.n<=b by Th79;
A10: a #Q q < a #Q p by A1,A6,Th75;
  now
    let n;
    s2.n<=b by A9;
    then s2.n<=q by A5,XXREAL_0:2;
    then a #Q (s2.n) <= a #Q q by A1,Th74;
    hence a #Q s2 .n <= a #Q q by Def7;
  end;
  then
A11: lim (a #Q s2) <= a #Q q by A1,A7,Th3,Th82;
  a #Q s2 is convergent by A1,A7,Th82;
  then a #R b <= a #Q q by A1,A7,A8,A11,Def8;
  then
A12: a #R b < a #Q p by A10,XXREAL_0:2;
  consider s1 being Rational_Sequence such that
A13: s1 is convergent and
A14: c = lim s1 and
A15: for n holds s1.n>=c by Th80;
  now
    let n;
    s1.n>=c by A15;
    then s1.n>=p by A4,XXREAL_0:2;
    then a #Q (s1.n) >= a #Q p by A1,Th74;
    hence a #Q s1 .n >= a #Q p by Def7;
  end;
  then
A16: lim (a #Q s1) >= a #Q p by A1,A13,Th2,Th82;
  a #Q s1 is convergent by A1,A13,Th82;
  then a #R c >= a #Q p by A1,A13,A14,A16,Def8;
  hence thesis by A12,XXREAL_0:2;
end;

theorem Th98:
  a>0 & a<=1 & c>=b implies a #R c <= a #R b
proof
  assume that
A1: a>0 and
A2: a<=1 and
A3: c>=b;
  1/a>=1 by A1,A2,Lm4,XREAL_1:87;
  then (1/a) #R c >= (1/a) #R b by A3,Th96;
  then 1/a #R c >= (1/a) #R b by A1,Th93;
  then
A4: 1/a #R c >= 1/a #R b by A1,Th93;
  a #R b > 0 by A1,Th95;
  hence thesis by A4,XREAL_1:91;
end;

theorem Th99:
  a >= 1 & b >= 0 implies a #R b >= 1
proof
  assume that
A1: a >= 1 and
A2: b >= 0;
  consider s being Rational_Sequence such that
A3: s is convergent and
A4: b = lim s and
A5: for n holds s.n>=b by Th80;
A6: now
    let n;
A7: a #Q s .n = a #Q (s.n) by Def7;
    s.n>=b by A5;
    hence a #Q s .n >= 1 by A1,A2,A7,Th71;
  end;
  a #Q s is convergent by A1,A3,Th82;
  then a #R b = lim (a #Q s) by A1,A3,A4,Def8;
  hence thesis by A1,A3,A6,Th2,Th82;
end;

theorem Th100:
  a > 1 & b > 0 implies a #R b > 1
proof
  assume that
A1: a>1 and
A2: b>0;
  a #R b > a #R 0 by A1,A2,Th97;
  hence thesis by A1,Th85;
end;

theorem Th101:
  a >= 1 & b <= 0 implies a #R b <= 1
proof
  assume that
A1: a>=1 and
A2: b<=0;
  a #R (-b) >= 1 by A1,A2,Th99;
  then 1 / a #R b >= 1 by A1,Th90;
  then 1 / (1 / a #R b) <= 1 by XREAL_1:213;
  hence thesis;
end;

theorem
  a > 1 & b < 0 implies a #R b < 1
proof
  assume that
A1: a>1 and
A2: b<0;
  -b>0 by A2,XREAL_1:60;
  then a #R (-b) > 1 by A1,Th100;
  then 1 / a #R b > 1 by A1,Th90;
  then 1 / (1 / a #R b) < 1 by XREAL_1:214;
  hence thesis;
end;

Lm10: s1 is convergent & s2 is convergent & lim s1 > 0 & p>=0 & (for n holds
s1.n>0 & s2.n = (s1.n) #Q p) implies lim s2 = (lim s1) #Q p
proof
  assume that
A1: s1 is convergent and
A2: s2 is convergent and
A3: lim s1 > 0 and
A4: p>=0 and
A5: for n holds s1.n>0 & s2.n = (s1.n) #Q p;
  reconsider s = NAT --> lim s1 as Real_Sequence;
  consider m0 being Element of NAT such that
A6: p<m0 by SEQ_4:10;
A7: lim s = s.0 by SEQ_4:41
    .= lim s1 by FUNCOP_1:13;
  then
A8: s1/"s is convergent by A1,A3,SEQ_2:37;
  deffunc O(Element of NAT) = ((s/"s1).$1) |^ m0;
  consider s3 being Real_Sequence such that
A9: for n holds s3.n = O(n) from SEQ_1:sch 1;
  for n holds s1.n<>0 by A5;
  then
A10: s1 is non-zero by SEQ_1:7;
  then
A11: s/"s1 is convergent by A1,A3,SEQ_2:37;
  then
A12: s3 is convergent by A9,Th26;
  reconsider q=m0 as Rational;
  deffunc O(Element of NAT) = ((s1/"s).$1) |^ m0;
  consider s4 being Real_Sequence such that
A13: for n holds s4.n = O(n) from SEQ_1:sch 1;
  lim (s/"s1) = lim s/lim s1 by A1,A3,A10,SEQ_2:38
    .= 1 by A3,A7,XCMPLX_1:60;
  then lim s3 = 1 |^ m0 by A11,A9,Th26;
  then
A14: lim s3 = 1 by NEWTON:15;
  lim (s1/"s) = lim s1/lim s by A1,A3,A7,SEQ_2:38
    .= 1 by A3,A7,XCMPLX_1:60;
  then lim s4 = 1 |^ m0 by A8,A13,Th26;
  then
A15: lim s4 = 1 by NEWTON:15;
  s2 is bounded by A2,SEQ_2:27;
  then consider d be real number such that
A16: d>0 and
A17: for n holds abs(s2.n)<d by SEQ_2:15;
A18: s4 is convergent by A8,A13,Th26;
  now
    let c be real number;
    assume
A19: c>0;
    then c/d>0 by A16,XREAL_1:141;
    then consider m1 such that
A20: for m st m>=m1 holds abs(s3.m-1)<c/d by A12,A14,SEQ_2:def 7;
A21: (lim s1) #Q p > 0 by A3,Th63;
    then
A22: abs((lim s1) #Q p) > 0 by ABSVALUE:def 1;
    then c/abs((lim s1) #Q p)>0 by A19,XREAL_1:141;
    then consider m2 such that
A23: for m st m>=m2 holds abs(s4.m-1)<c/abs((lim s1) #Q p) by A18,A15,
SEQ_2:def 7;
    take n = m1 + m2;
    let m such that
A24: m>=n;
    m2<=n by NAT_1:11;
    then
A25: m>=m2 by A24,XXREAL_0:2;
    m1<=n by NAT_1:11;
    then
A26: m>=m1 by A24,XXREAL_0:2;
    now
      per cases;
      suppose
A27:    s1.m >= lim s1;
A28:    s1.m/lim s1 = s1.m/s.m by FUNCOP_1:13
          .= s1.m*(s.m)"
          .= s1.m*s".m by VALUED_1:10
          .= (s1/"s).m by SEQ_1:12;
        s1.m*(lim s1)">=(lim s1)*(lim s1)" by A3,A27,XREAL_1:66;
        then
A29:    (s1/"s).m >= 1 by A3,A28,XCMPLX_0:def 7;
        then ((s1/"s).m) #Q p <= ((s1/"s).m) #Q q by A6,Th74;
        then ((s1/"s).m) #Q p <= ((s1/"s).m) |^ m0 by A29,Th60;
        then
A30:    ((s1/"s).m) #Q p - 1 <= ((s1/"s).m) |^ m0 - 1 by XREAL_1:11;
        ((s1/"s).m) #Q p >= 1 by A4,A29,Th71;
        then ((s1/"s).m) #Q p - 1 >= 1-1 by XREAL_1:11;
        then
A31:    ((s1/"s).m) #Q p - 1 = abs(((s1/"s).m) #Q p - 1) by ABSVALUE:def 1;
A32:    (lim s1) #Q p <> 0 by A3,Th63;
A33:    abs(s4.m-1)<c/abs((lim s1) #Q p) by A23,A25;
A34:    s1.m>0 by A5;
        ((s1/"s).m) |^ m0 >= 1 by A29,Th19;
        then ((s1/"s).m) |^ m0 - 1 >= 1-1 by XREAL_1:11;
        then abs(((s1/"s).m) #Q p - 1) <= abs(((s1/"s).m) |^ m0 - 1) by A30,A31
,ABSVALUE:def 1;
        then abs(((s1/"s).m) #Q p - 1) <= abs(s4.m - 1) by A13;
        then
A35:    abs(((s1/"s).m) #Q p - 1)<c/abs((lim s1) #Q p) by A33,XXREAL_0:2;
A36:    abs((lim s1) #Q p) <> 0 by A21,ABSVALUE:def 1;
        abs(s2.m - (lim s1) #Q p) = abs(((s1.m) #Q p - (lim s1) #Q p)*1) by A5
          .= abs(((s1.m) #Q p - (lim s1) #Q p)*((lim s1) #Q p/(lim s1) #Q p)
        ) by A32,XCMPLX_1:60
          .= abs((lim s1) #Q p * ((s1.m) #Q p - (lim s1) #Q p) / (lim s1) #Q
        p)
          .= abs((lim s1) #Q p * (((s1.m) #Q p - (lim s1) #Q p) / (lim s1)
        #Q p))
          .= abs((lim s1) #Q p)*abs(((s1.m) #Q p - (lim s1) #Q p) / (lim s1)
        #Q p) by COMPLEX1:151
          .= abs((lim s1) #Q p)*abs((s1.m) #Q p/(lim s1) #Q p - (lim s1) #Q
        p / (lim s1) #Q p)
          .= abs((lim s1) #Q p)*abs((s1.m) #Q p/(lim s1) #Q p - 1) by A32,
XCMPLX_1:60
          .= abs((lim s1) #Q p)*abs((s1.m/lim s1) #Q p - 1) by A3,A34,Th69;
        then abs(s2.m - (lim s1) #Q p)< abs((lim s1) #Q p)*(c/abs((lim s1) #Q
        p)) by A22,A28,A35,XREAL_1:70;
        hence abs(s2.m - (lim s1) #Q p)<c by A36,XCMPLX_1:88;
      end;
      suppose
A37:    s1.m <= lim s1;
A38:    lim s1/s1.m = s.m/s1.m by FUNCOP_1:13
          .= s.m*(s1.m)"
          .= s.m*s1".m by VALUED_1:10
          .= (s/"s1).m by SEQ_1:12;
A39:    s1.m <> 0 by A5;
A40:    s1.m>0 by A5;
        then s1.m*(s1.m)"<=(lim s1)*(s1.m)" by A37,XREAL_1:66;
        then
A41:    (s/"s1).m >= 1 by A39,A38,XCMPLX_0:def 7;
        then ((s/"s1).m) #Q p <= ((s/"s1).m) #Q q by A6,Th74;
        then ((s/"s1).m) #Q p <= ((s/"s1).m) |^ m0 by A41,Th60;
        then
A42:    ((s/"s1).m) #Q p - 1 <= ((s/"s1).m) |^ m0 - 1 by XREAL_1:11;
        (s1.m) #Q p > 0 by A5,Th63;
        then
A43:    abs((s1.m) #Q p) > 0 by ABSVALUE:def 1;
A44:    abs(s3.m-1)<c/d by A20,A26;
        ((s/"s1).m) #Q p >= 1 by A4,A41,Th71;
        then ((s/"s1).m) #Q p - 1 >= 1-1 by XREAL_1:11;
        then
A45:    ((s/"s1).m) #Q p - 1 = abs(((s/"s1).m) #Q p - 1) by ABSVALUE:def 1;
        ((s/"s1).m) |^ m0 >= 1 by A41,Th19;
        then ((s/"s1).m) |^ m0 - 1 >= 1-1 by XREAL_1:11;
        then abs(((s/"s1).m) #Q p - 1) <= abs(((s/"s1).m) |^ m0 - 1) by A42,A45
,ABSVALUE:def 1;
        then abs(((s/"s1).m) #Q p - 1) <= abs(s3.m - 1) by A9;
        then
A46:    abs(((s/"s1).m) #Q p - 1)<c/d by A44,XXREAL_0:2;
        abs(s2.m)<d by A17;
        then abs((s1.m) #Q p)<d by A5;
        then abs((s1.m) #Q p)/d<d/d by A16,XREAL_1:76;
        then abs((s1.m) #Q p)/d<1 by A16,XCMPLX_1:60;
        then
A47:    c*(abs((s1.m) #Q p)/d)<c*1 by A19,XREAL_1:70;
A48:    (s1.m) #Q p <> 0 by A5,Th63;
        abs(s2.m - (lim s1) #Q p) = abs(((s1.m) #Q p - (lim s1) #Q p)*1) by A5
          .= abs(((s1.m) #Q p - (lim s1) #Q p)*((s1.m) #Q p/(s1.m) #Q p)) by
A48,XCMPLX_1:60
          .= abs((s1.m) #Q p * ((s1.m) #Q p - (lim s1) #Q p) / (s1.m) #Q p)
          .= abs((s1.m) #Q p * (((s1.m) #Q p - (lim s1) #Q p) / (s1.m) #Q p)
        )
          .= abs((s1.m) #Q p)*abs(((s1.m) #Q p - (lim s1) #Q p) / (s1.m) #Q
        p) by COMPLEX1:151
          .= abs((s1.m) #Q p)*abs((s1.m) #Q p/(s1.m) #Q p - (lim s1) #Q p /
        (s1.m) #Q p)
          .= abs((s1.m) #Q p)*abs(1 - (lim s1) #Q p/(s1.m) #Q p) by A48,
XCMPLX_1:60
          .= abs((s1.m) #Q p)*abs(-(1 - (lim s1) #Q p/(s1.m) #Q p)) by
COMPLEX1:138
          .= abs((s1.m) #Q p)*abs((lim s1) #Q p/(s1.m) #Q p - 1)
          .= abs((s1.m) #Q p)*abs((lim s1/s1.m) #Q p - 1) by A3,A40,Th69;
        then abs(s2.m - (lim s1) #Q p)<abs((s1.m) #Q p)*(c/d) by A43,A38,A46,
XREAL_1:70;
        hence abs(s2.m - (lim s1 ) #Q p) < c by A47,XXREAL_0:2;
      end;
    end;
    hence abs(s2.m - (lim s1) #Q p) < c;
  end;
  hence thesis by A2,SEQ_2:def 7;
end;

theorem Th103:
  s1 is convergent & s2 is convergent & lim s1 > 0 & (for n holds
  s1.n>0 & s2.n = (s1.n) #Q p) implies lim s2 = (lim s1) #Q p
proof
  assume that
A1: s1 is convergent and
A2: s2 is convergent and
A3: lim s1 > 0 and
A4: for n holds s1.n>0 & s2.n = (s1.n) #Q p;
  per cases;
  suppose
    p>=0;
    hence thesis by A1,A2,A3,A4,Lm10;
  end;
  suppose
A5: p<=0;
    s1 is bounded by A1,SEQ_2:27;
    then consider d be real number such that
A6: d>0 and
A7: for n holds abs(s1.n)<d by SEQ_2:15;
    reconsider d as Real by XREAL_0:def 1;
A8: d #Q p > 0 by A6,Th63;
A9: now
      assume lim s2 = 0;
      then consider n such that
A10:  for m st m>=n holds abs(s2.m - 0)<d #Q p by A2,A8,SEQ_2:def 7;
      now
        let m;
A11:    s1.m>0 by A4;
A12:    s1.m<>0 by A4;
A13:    (s1.m) #Q p > 0 by A4,Th63;
        abs(s1.m)<d by A7;
        then s1.m<d by A11,ABSVALUE:def 1;
        then s1.m/s1.m<d/s1.m by A11,XREAL_1:76;
        then 1<=d/s1.m by A12,XCMPLX_1:60;
        then (d/s1.m) #Q p <= 1 by A5,Th72;
        then d #Q p/(s1.m) #Q p <= 1 by A6,A11,Th69;
        then
A14:    d #Q p/(s1.m) #Q p * (s1.m) #Q p<= 1*(s1.m) #Q p by A13,XREAL_1:66;
A15:    (s1.m) #Q p <> 0 by A4,Th63;
        assume m>=n;
        then abs(s2.m - 0)<d #Q p by A10;
        then abs((s1.m) #Q p)<d #Q p by A4;
        then (s1.m) #Q p<d #Q p by A13,ABSVALUE:def 1;
        hence contradiction by A15,A14,XCMPLX_1:88;
      end;
      hence contradiction;
    end;
    now
      let n;
      (s1.n) #Q p <> 0 by A4,Th63;
      hence s2.n <> 0 by A4;
    end;
    then
A16: s2 is non-zero by SEQ_1:7;
    then
A17: lim s2" = (lim s2)" by A2,A9,SEQ_2:36;
    deffunc O(Element of NAT) = (s2.$1)";
    consider s being Real_Sequence such that
A18: for n holds s.n = O(n) from SEQ_1:sch 1;
A19: now
      let n;
      s.n = (s2.n)" by A18
        .= ((s1.n) #Q p)" by A4
        .= 1 / (s1.n) #Q p
        .= (s1.n) #Q (-p) by A4,Th65;
      hence s1.n>0 & s.n = (s1.n) #Q (-p) by A4;
    end;
A20: dom s2 = NAT by FUNCT_2:def 1;
A21: dom s = NAT by FUNCT_2:def 1;
    then for n being set st n in dom s holds s.n = (s2.n)" by A18;
    then
A22: s = s2" by A21,A20,VALUED_1:def 7;
    s2" is convergent by A2,A16,A9,SEQ_2:35;
    then (lim s2)" = (lim s1) #Q (-p) by A1,A3,A5,A17,A22,A19,Lm10;
    then 1/lim s2 = 1/(lim s1) #Q p by A3,Th65;
    hence thesis by XCMPLX_1:59;
  end;
end;

Lm11: a > 0 implies a #R b #Q p = a #R (b * p)
proof
  consider s1 being Rational_Sequence such that
A1: s1 is convergent and
A2: b = lim s1 and
  for n holds s1.n>=b by Th80;
  reconsider s2 = p(#)s1 as Rational_Sequence;
  assume
A3: a>0;
A4: now
    let n;
A5: a #Q (s1.n) > 0 by A3,Th63;
    (a #Q s1 .n) #Q p = (a #Q (s1.n)) #Q p by Def7
      .= a #Q (p*s1.n) by A3,Th70
      .= a #Q (s2.n) by SEQ_1:13
      .= a #Q s2 .n by Def7;
    hence a #Q s1 .n > 0 & (a #Q s1 .n) #Q p = a #Q s2 .n by A5,Def7;
  end;
A6: s2 is convergent by A1,SEQ_2:21;
  then
A7: a #Q s2 is convergent by A3,Th82;
  lim s2 = b*p by A1,A2,SEQ_2:22;
  then
A8: a #R (b*p) = lim (a #Q s2) by A3,A6,A7,Def8;
A9: a #Q s1 is convergent by A3,A1,Th82;
  then a #R b = lim (a #Q s1) by A3,A1,A2,Def8;
  hence thesis by A3,A9,A7,A8,A4,Th95,Th103;
end;

Lm12: a>=1 & s1 is convergent & s2 is convergent & (for n holds s2.n = a #R (
s1.n)) implies lim s2 = a #R (lim s1)
proof
  assume that
A1: a>=1 and
A2: s1 is convergent and
A3: s2 is convergent and
A4: for n holds s2.n = a #R (s1.n);
  set d = abs(lim s1)+1;
A5: abs(lim s1)<abs(lim s1) + 1 by XREAL_1:31;
  now
    lim s1<=abs lim s1 by ABSVALUE:11;
    then lim s1<=d by A5,XXREAL_0:2;
    then
A6: a #R (lim s1) <= a #R d by A1,Th96;
    a #R (lim s1) > 0 by A1,Th95;
    then
A7: abs(a #R (lim s1)) <= a #R d by A6,ABSVALUE:def 1;
A8: a #R d >= 0 by A1,Th95;
    let c be real number;
    assume
A9: c>0;
    consider m1 such that
A10: a #R d*(a-1)/c < m1 by SEQ_4:10;
    m1+1>=m1 by XREAL_1:31;
    then a #R d * (a-1)/c < m1+1 by A10,XXREAL_0:2;
    then a #R d * (a-1)/c*c < c*(m1+1) by A9,XREAL_1:70;
    then a #R d * (a-1) < c*(m1+1) by A9,XCMPLX_1:88;
    then a #R d * (a-1)/(m1+1) < (m1+1)*c/(m1+1) by XREAL_1:76;
    then a #R d * (a-1)/(m1+1) < c/(m1+1)*(m1+1);
    then
A11: a #R d * (a-1)/(m1+1) < c by XCMPLX_1:88;
    reconsider m3 = (m1+1)" as Rational;
A12: abs(a #R (lim s1)) >= 0 by COMPLEX1:132;
    consider n such that
A13: for m st n<=m holds abs(s1.m -(lim s1))<(m1+1) " by A2,SEQ_2:def 7;
    take n;
    let m;
    assume m>=n;
    then
A14: abs(s1.m -(lim s1))<=(m1+1)" by A13;
A15: m1+1>=0+1 by NAT_1:13;
    then (m1+1) -Root a - 1 <= (a-1)/(m1+1) by A1,Th40;
    then
A16: (a #R d)*((m1+1) -Root a - 1) <= (a #R d)*((a-1)/(m1+1)) by A8,XREAL_1:66;
    s1.m -(lim s1)<=abs(s1.m -(lim s1)) by ABSVALUE:11;
    then s1.m -(lim s1) <= (m1+1)" by A14,XXREAL_0:2;
    then a #R (s1.m-lim s1) <= a #R m3 by A1,Th96;
    then a #R (s1.m-lim s1) <= a #Q m3 by A1,Th88;
    then a #R (s1.m-lim s1) <= (m1+1) -Root a by A15,Th61;
    then
A17: a #R (s1.m-lim s1) - 1 <= (m1+1) -Root a - 1 by XREAL_1:11;
A18: a #R (s1.m-lim s1) <> 0 by A1,Th95;
A19: a #R (s1.m-lim s1) > 0 by A1,Th95;
A20: now
      per cases;
      suppose
        s1.m-lim s1>=0;
        then a #R (s1.m-lim s1) >= 1 by A1,Th99;
        then a #R (s1.m-lim s1) - 1 >= 0 by XREAL_1:50;
        hence abs(a #R (s1.m-lim s1) - 1) <= (m1+1) -Root a - 1 by A17,
ABSVALUE:def 1;
      end;
      suppose
A21:    s1.m-lim s1<0;
A22:    -(s1.m -lim s1)<=abs(-(s1.m -lim s1)) by ABSVALUE:11;
        abs(s1.m -lim s1) = abs(-(s1.m -lim s1)) by COMPLEX1:138;
        then -(s1.m -lim s1) <= m3 by A14,A22,XXREAL_0:2;
        then a #R (-(s1.m-lim s1)) <= a #R m3 by A1,Th96;
        then a #R (-(s1.m-lim s1)) <= a #Q m3 by A1,Th88;
        then a #R (-(s1.m-lim s1)) <= (m1+1) -Root a by A15,Th61;
        then
A23:    a #R (-(s1.m-lim s1)) - 1 <= (m1+1) -Root a - 1 by XREAL_1:11;
        a #R (-(s1.m-lim s1)) >= 1 by A1,A21,Th99;
        then a #R (-(s1.m-lim s1)) - 1 >= 0 by XREAL_1:50;
        then
A24:    abs(a #R (-(s1.m-lim s1)) - 1) <= (m1+1) -Root a - 1 by A23,
ABSVALUE:def 1;
        a #R (s1.m-lim s1) <= 1 by A1,A21,Th101;
        then
A25:    abs(a #R (s1.m-lim s1)) <= 1 by A19,ABSVALUE:def 1;
        abs(a #R (-(s1.m-lim s1)) - 1) >= 0 by COMPLEX1:132;
        then
A26:    abs(a #R (s1.m-lim s1))*abs(a #R (-(s1.m-lim s1)) - 1) <= 1*abs(a
        #R (-(s1.m-lim s1)) - 1) by A25,XREAL_1:66;
        abs(a #R (s1.m-lim s1) - 1) = abs((a #R (s1.m-lim s1) - 1)*1)
          .= abs((a #R (s1.m-lim s1) - 1)*(a #R (s1.m-lim s1)/a #R (s1.m-lim
        s1))) by A18,XCMPLX_1:60
          .= abs(a #R (s1.m-lim s1)*(a #R (s1.m-lim s1) - 1)/a #R (s1.m-lim
        s1))
          .= abs(a #R (s1.m-lim s1)*((a #R (s1.m-lim s1) - 1)/a #R (s1.m-lim
        s1)))
          .= abs(a #R (s1.m-lim s1))*abs((a #R (s1.m-lim s1) - 1)/a #R (s1.m
        -lim s1)) by COMPLEX1:151
          .= abs(a #R (s1.m-lim s1))* abs(a #R (s1.m-lim s1)/a #R (s1.m-lim
        s1) - 1/a #R (s1.m-lim s1))
          .= abs(a #R (s1.m-lim s1))*abs(1 - 1/a #R (s1.m-lim s1)) by A18,
XCMPLX_1:60
          .= abs(a #R (s1.m-lim s1))*abs(1 - a #R (-(s1.m-lim s1))) by A1,Th90
          .= abs(a #R (s1.m-lim s1))*abs(-(1 - a #R (-(s1.m-lim s1)))) by
COMPLEX1:138
          .= abs(a #R (s1.m-lim s1))*abs(a #R (-(s1.m-lim s1)) - 1);
        hence abs(a #R (s1.m-lim s1) - 1) <= (m1+1) -Root a - 1 by A24,A26,
XXREAL_0:2;
      end;
    end;
    abs(a #R (s1.m-lim s1) - 1) >= 0 by COMPLEX1:132;
    then
A27: abs(a #R (lim s1))*abs(a #R (s1.m-lim s1) - 1) <= (a #R d)*((m1+1)
    -Root a - 1) by A20,A12,A7,XREAL_1:68;
    abs(a #R (s1.m) - a #R (lim s1)) = abs((a #R (s1.m) - a #R (lim s1))* 1)
      .=abs((a #R (s1.m) - a #R (lim s1))*(a #R (lim s1)/a #R (lim s1))) by A1
,Lm9,XCMPLX_1:60
      .=abs(a #R (lim s1)*(a #R (s1.m) - a #R (lim s1))/a #R (lim s1))
      .=abs(a #R (lim s1)*((a #R (s1.m) - a #R (lim s1))/a #R (lim s1)))
      .=abs(a #R (lim s1))*abs((a #R (s1.m) - a #R (lim s1))/a #R (lim s1))
    by COMPLEX1:151
      .=abs(a #R(lim s1))*abs(a #R (s1.m)/a #R (lim s1)-a #R (lim s1)/a #R (
    lim s1))
      .=abs(a #R (lim s1))*abs(a #R (s1.m)/a #R (lim s1) - 1) by A1,Lm9,
XCMPLX_1:60
      .=abs(a #R (lim s1))*abs(a #R (s1.m-lim s1) - 1) by A1,Th91;
    then abs(a #R (s1.m) - a #R (lim s1))<= (a #R d)*(a-1)/(m1+1) by A27,A16,
XXREAL_0:2;
    then abs(a #R (s1.m) - a #R (lim s1)) < c by A11,XXREAL_0:2;
    hence abs(s2.m - a #R (lim s1)) < c by A4;
  end;
  hence thesis by A3,SEQ_2:def 7;
end;

theorem Th104:
  a>0 & s1 is convergent & s2 is convergent & (for n holds s2.n =
  a #R (s1.n)) implies lim s2 = a #R (lim s1)
proof
  assume that
A1: a>0 and
A2: s1 is convergent and
A3: s2 is convergent and
A4: for n holds s2.n = a #R (s1.n);
  per cases;
  suppose
    a>=1;
    hence thesis by A2,A3,A4,Lm12;
  end;
  suppose
A5: a<1;
A6: now
      assume
A7:   lim s2 = 0;
      a #R ((lim s1) + 1) > 0 by A1,Th95;
      then consider n such that
A8:   for m st m>=n holds abs(s2.m-0)<a #R ((lim s1) +1) by A3,A7,SEQ_2:def 7;
      consider n1 being Element of NAT such that
A9:   for m st m>=n1 holds abs(s1.m-lim s1)<1 by A2,SEQ_2:def 7;
      now
        let m such that
A10:    m>=n+n1;
        n+n1>=n1 by NAT_1:12;
        then m>=n1 by A10,XXREAL_0:2;
        then
A11:    abs(s1.m-lim s1)<1 by A9;
        s1.m-lim s1<=abs(s1.m-lim s1) by ABSVALUE:11;
        then s1.m-lim s1< 1 by A11,XXREAL_0:2;
        then
A12:    s1.m-lim s1 + lim s1 < lim s1 + 1 by XREAL_1:8;
        n+n1>=n by NAT_1:12;
        then m>=n by A10,XXREAL_0:2;
        then abs(s2.m-0)<a #R ((lim s1)+1) by A8;
        then
A13:    abs(a #R (s1.m))<a #R ((lim s1)+1) by A4;
        a #R (s1.m) > 0 by A1,Th95;
        then a #R (s1.m) < a #R ((lim s1)+1) by A13,ABSVALUE:def 1;
        hence contradiction by A1,A5,A12,Th98;
      end;
      hence contradiction;
    end;
A14: now
      let n;
      thus s2".n = (s2.n)" by VALUED_1:10
        .= (a #R (s1.n))" by A4
        .= 1/(a #R (s1.n))
        .= (1/a) #R (s1.n) by A1,Th93;
    end;
    a*(1/a)<=1*(1/a) by A1,A5,XREAL_1:66;
    then
A15: 1<=1/a by A1,XCMPLX_1:107;
A16: a #R (lim s1) <> 0 by A1,Th95;
    now
      let n;
      s2.n = a #R (s1.n) by A4;
      hence s2.n <> 0 by A1,Th95;
    end;
    then
A17: s2 is non-zero by SEQ_1:7;
    then
A18: lim s2" = (lim s2)" by A3,A6,SEQ_2:36;
    s2" is convergent by A3,A6,A17,SEQ_2:35;
    then (lim s2)" = (1/a) #R (lim s1) by A2,A15,A18,A14,Lm12
      .= 1/a #R (lim s1) by A1,Th93;
    then 1 = 1/a #R (lim s1) * lim s2 by A6,XCMPLX_0:def 7;
    then a #R (lim s1)= a #R (lim s1)*(1/a #R (lim s1) * lim s2)
      .= a #R (lim s1)*(1/a #R (lim s1)) * lim s2
      .= 1 * lim s2 by A16,XCMPLX_1:107;
    hence thesis;
  end;
end;

theorem
  a > 0 implies a #R b #R c = a #R (b * c)
proof
  consider s being Rational_Sequence such that
A1: s is convergent and
A2: c = lim s and
  for n holds s.n<=c by Th79;
A3: lim (b(#)s) = b*c by A1,A2,SEQ_2:22;
A4: b(#)s is convergent by A1,SEQ_2:21;
  assume
A5: a>0;
  then
A6: (a #R b) #Q s is convergent by A1,Th82,Th95;
A7: now
    let n;
    thus (a #R b) #Q s .n = (a #R b) #Q (s.n) by Def7
      .= a #R (b*s.n) by A5,Lm11
      .= a #R ((b(#)s).n) by SEQ_1:13;
  end;
  a #R b > 0 by A5,Th95;
  then (a #R b) #R c = lim ((a #R b) #Q s) by A1,A2,A6,Def8;
  hence thesis by A5,A6,A4,A3,A7,Th104;
end;

begin :: Addenda
:: from PCOMPS_2, 2006.02.10, A.T.

reserve r, u for real number,
  k for natural number;

theorem
  r>0 & u > 0 implies ex k be Element of NAT st u/(2 |^ k) <= r
proof
  defpred P[Element of NAT] means $1 <= 2 |^ $1;
  assume that
A1: r>0 and
A2: u>0;
  set t = 1/r;
  reconsider t as Real;
  consider n such that
A3: (u * t) < n by SEQ_4:10;
A4: 0 < u * t by A1,A2,XREAL_1:131;
  then
A5: u/(u * t) > u/n by A2,A3,XREAL_1:78;
A6: for k be Element of NAT st P[k] holds P[k + 1]
  proof
    let k be Element of NAT;
    assume k <= 2 |^ k;
    then
A7: (2 |^ k) + 1 >= k + 1 by XREAL_1:9;
    2 |^ k >= 1 by Th19;
    then
A8: 2 |^ k + 2 |^ k >= 2 |^ k + 1 by XREAL_1:9;
    2 |^ (k + 1) = (2 |^ k) * (2 |^ 1) by NEWTON:13
      .= (2 |^ k) * 2 by NEWTON:10
      .= 2 |^ k + 2 |^ k;
    hence thesis by A8,A7,XXREAL_0:2;
  end;
  take n;
A9: r = 1/t .= (u * 1) / (u * t) by A2,XCMPLX_1:92
    .= u/(u * t);
A10: P[0];
  for k be Element of NAT holds P[k] from NAT_1:sch 1(A10,A6);
  then u/n >= u/(2 |^ n) by A2,A3,A4,XREAL_1:120;
  hence thesis by A9,A5,XXREAL_0:2;
end;

theorem
  k>=n & r >= 1 implies r |^ k >= r |^ n
proof
  assume that
A1: k>=n and
A2: r >= 1;
  consider m be Nat such that
A3: k = n + m by A1,NAT_1:10;
A4: r |^ k = r |^ n * r |^ m by A3,NEWTON:13;
  r |^ n >= 1 by A2,Th19;
  hence thesis by A2,A4,Th19,XREAL_1:153;
end;
 
:: from SCPINVAR, 2006.03.14, A.T.

theorem Th108:
  for n,m,l be Element of NAT st n divides m & n divides l holds n divides m-l
proof
  let n,m,l be Element of NAT;
  assume that
A1: n divides m and
A2: n divides l;
A3: -l =-n*(l div n) by A2,NAT_D:3
    .=n*(-(l div n));
  m = n * (m div n) by A1,NAT_D:3;
  then m - l =n * ((m div n) + -(l div n)) by A3;
  hence thesis by INT_1:def 9;
end;

theorem
  m divides n iff m divides (n qua Integer);

theorem Th110:
  m gcd n= m gcd abs(n-m)
proof
  set x=m gcd n, y=m gcd abs( n - m );
A1: x divides m by NAT_D:def 5;
A2: x divides n by NAT_D:def 5;
A3: y divides m by NAT_D:def 5;
  per cases;
  suppose
    n-m >= 0;
    then reconsider nm=n-m as Element of NAT by INT_1:16;
A4: abs( n - m )=nm by ABSVALUE:def 1;
    then y divides nm by NAT_D:def 5;
    then y divides nm+m by A3,NAT_D:8;
    then
A5: y divides x by A3,NAT_D:def 5;
    x divides n-m by A1,A2,Th108;
    then x divides y by A1,A4,NAT_D:def 5;
    hence thesis by A5,NAT_D:5;
  end;
  suppose
A6: n-m < 0;
    reconsider nn=n as Integer;
A7: abs( n - m )=-(n-m) by A6,ABSVALUE:def 1
      .=m-n;
    then reconsider mn=m-n as Element of NAT;
    y divides mn by A7,NAT_D:def 5;
    then y divides mn-m by A3,Th108;
    then y divides nn by INT_2:14;
    then
A8: y divides x by A3,NAT_D:def 5;
    x divides m-n by A1,A2,Th108;
    then x divides y by A1,A7,NAT_D:def 5;
    hence thesis by A8,NAT_D:5;
  end;
end;

theorem
  for a,b be Integer st a>=0 & b>=0 holds a gcd b = a gcd (b-a)
proof
  let a,b be Integer;
  assume that
A1: a>=0 and
A2: b>=0;
  thus a gcd b=abs(a) gcd abs(b) by INT_2:51
    .=abs(a) gcd abs(abs(b)-abs(a)) by Th110
    .=abs(a) gcd abs(b-abs(a)) by A2,ABSVALUE:def 1
    .=abs(a) gcd abs(b-a) by A1,ABSVALUE:def 1
    .=a gcd (b-a) by INT_2:51;
end;
 
:: missing, 2008.03.05, A.T.

theorem
  a>=0 implies a #Z l >= 0 by Lm5;

theorem
  a > 0 implies a #Q l = a #Z l
proof
  assume a > 0;
  then a #Z l > 0 by Th49;
  then
A1: a #Z l = 1-Root (a #Z l) |^ 1 by Def3;
  denominator l=1 by RAT_1:40;
  hence a #Q l = 1-Root (a #Z l) by RAT_1:40
    .= a #Z l by A1,NEWTON:10;
end;
 
:: missing, 2010.02.13, A.T.

theorem
 l <> 0 implies 0 #Z l = 0 proof assume
Z: l <> 0;
  per cases by Z;
  suppose
S:  l > 0;
    then reconsider l as Element of NAT by INT_1:16;
    l >= 0+1 by S,NAT_1:13;
    then
A:   0 |^ l = 0 by NEWTON:16;
    abs l = l by ABSVALUE:def 1;
   hence thesis by A,Def4;
  end;
  suppose
S:  l < 0;
    then reconsider k = -l as Element of NAT by INT_1:16;
    k > -0 by S,XREAL_1:60;
    then k >= 0+1 by NAT_1:13;
    then
A:   0 |^ k = 0 by NEWTON:16;
    abs l = k by S,ABSVALUE:def 1;
    then (0 |^ abs l)" = 0 by A;
   hence thesis by S,Def4;
  end;
 end;
