:: Gauges and Cages. { P } art { II }
::  by Artur Korni{\l}owicz and Robert Milewski
::
:: Received November 6, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies ARYTM, EUCLID, COMPTS_1, RELAT_2, SPPOL_1, NAT_1, BOOLE, TARSKI,
      MATRIX_2, GROUP_1, ARYTM_3, ARYTM_1, FINSEQ_1, JORDAN8, MCART_1,
      PSCOMP_1, TREES_1, MATRIX_1, GOBOARD5, SETFAM_1, JORDAN9, PRE_TOPC,
      JORDAN1A, TOPREAL1, GOBOARD1, FINSEQ_4, RELAT_1, FUNCT_1;
 notations TARSKI, XBOOLE_0, ENUMSET1, SETFAM_1, SUBSET_1, ORDINAL1, NUMBERS,
      XCMPLX_0, XXREAL_0, XREAL_0, REAL_1, NAT_1, NAT_D, FUNCT_1, PARTFUN1,
      STRUCT_0, FINSEQ_1, BINARITH, NEWTON, PRE_TOPC, COMPTS_1, CONNSP_1,
      MATRIX_1, EUCLID, GOBOARD1, TOPREAL1, GOBOARD5, PSCOMP_1, SPPOL_1, ABIAN,
      GOBRD13, JORDAN8, JORDAN9;
 constructors SETFAM_1, XXREAL_0, REAL_1, NAT_1, NAT_D, NEWTON, REALSET1,
      BINARITH, ABIAN, CONNSP_1, PSCOMP_1, JORDAN8, GOBRD13, JORDAN9;
 registrations RELSET_1, XXREAL_0, XREAL_0, NAT_1, INT_1, MEMBERED, NEWTON,
      ABIAN, GRAPH_3, SPRECT_1, TOPREAL6, JORDAN8, JORDAN10, XBOOLE_0;
 requirements NUMERALS, SUBSET, REAL, BOOLE, ARITHM;
 definitions TARSKI, SETFAM_1, XBOOLE_0, PSCOMP_1;
 theorems BINARITH, EUCLID, GOBRD11, JORDAN8, PSCOMP_1, JORDAN1A, NAT_1,
      REAL_1, GOBOARD5, FINSEQ_2, SPRECT_2, SPPOL_2, TOPREAL1, SPRECT_3,
      GOBRD13, JORDAN9, GOBOARD1, TARSKI, JORDAN10, ENUMSET1, NEWTON, NAT_2,
      INT_1, REAL_2, SPRECT_1, SPPOL_1, ZFMISC_1, ABIAN, SETFAM_1, XBOOLE_0,
      XBOOLE_1, XCMPLX_1, XREAL_1, XXREAL_0, NAT_D, PARTFUN1, MATRIX_1;
 schemes NAT_1;

begin  :: Preliminaries

reserve a, b, i, k, m, n for Element of NAT,
  r, s for real number,
  D for non empty Subset of TOP-REAL 2,
  C for compact connected non vertical non horizontal Subset of TOP-REAL 2;
1 = 2 * 0 + 1;
then

Lm1: 1 div 2 = 0 by NAT_D:def 1;
2 = 2 * 1 + 0;
then

Lm2: 2 div 2 = 1 by NAT_D:def 1;

Lm3: for x, A, B, C, D being set holds
x in A \/ B \/ C \/ D iff x in A or x in B or x in C or x in D
proof
  let x, A, B, C, D be set;
  hereby
    assume x in A \/ B \/ C \/ D;
    then x in A \/ B \/ C or x in D by XBOOLE_0:def 2;
    then x in A \/ B or x in C or x in D by XBOOLE_0:def 2;
    hence x in A or x in B or x in C or x in D by XBOOLE_0:def 2;
  end;
  assume x in A or x in B or x in C or x in D;
  then x in A \/ B or x in C or x in D by XBOOLE_0:def 2;
  then x in A \/ B \/ C or x in D by XBOOLE_0:def 2;
  hence thesis by XBOOLE_0:def 2;
end;

Lm4: for A, B, C, D being set holds union {A,B,C,D} = A \/ B \/ C \/ D
proof
  let A, B, C, D be set;
  hereby
    let x be set;
    assume x in union {A,B,C,D};
    then consider Z being set such that
A1: x in Z and
A2: Z in {A,B,C,D} by TARSKI:def 4;
    Z = A or Z = B or Z = C or Z = D by A2,ENUMSET1:def 2;
    hence x in A \/ B \/ C \/ D by A1,Lm3;
  end;
  let x be set;
  assume x in A \/ B \/ C \/ D;
  then
A3: x in A or x in B or x in C or x in D by Lm3;
  A in {A,B,C,D} & B in {A,B,C,D} & C in {A,B,C,D} & D in {A,B,C,D}
  by ENUMSET1:def 2;
  hence thesis by A3,TARSKI:def 4;
end;

theorem Th1:
  for A, B being set st
  for x being set st x in A ex K being set st K c= B & x c= union K
  holds union A c= union B
proof
  let A, B be set such that
A1: for x being set st x in A ex K being set st K c= B & x c= union K;
  let a be set;
  assume a in union A;
  then consider Z being set such that
A2: a in Z and
A3: Z in A by TARSKI:def 4;
  consider K being set such that
A4: K c= B and
A5: Z c= union K by A1,A3;
  ex S being set st a in S & S in K by A2,A5,TARSKI:def 4;
  hence a in union B by A4,TARSKI:def 4;
end;

registration
  let m be non empty Element of NAT;
  cluster 2|^m -> even;
  coherence
  proof
    defpred P[Nat] means $1 is non empty implies 2|^$1 is even;
A1: P[0];
A2: for k be Nat holds P[k] implies P[k+1]
    proof
      let k be Nat;
      assume P[k] & k+1 is non empty;
      2|^(k+1) = 2*2|^k by NEWTON:11;
      hence thesis;
    end;
    for k be Nat holds P[k] from NAT_1:sch 2(A1,A2);
    hence thesis;
  end;
end;

registration
  let n be even Element of NAT, m be non empty Element of NAT;
  cluster n|^m -> even;
  coherence
  proof
    defpred P[Element of NAT] means $1 is non empty implies n|^$1 is even;
A1: P[0];
A2: P[k] implies P[k+1]
    proof
      assume P[k] & k+1 is non empty;
      n|^(k+1) = n*n|^k by NEWTON:11;
      hence thesis;
    end;
    P[k] from NAT_1:sch 1(A1,A2);
    hence thesis;
  end;
end;

theorem Th2:
  r <> 0 implies 1/r * r|^(i+1) = r|^i
proof
  assume
A1: r <> 0;
  thus 1/r * r|^(i+1) = 1/r * (r|^i * r) by NEWTON:11
    .= 1/r * r * r|^i
    .= 1*r|^i by A1,XCMPLX_1:107
    .= r|^i;
end;

canceled 4;

Lm5: now
  let m be real number;
  assume 2 <= m;
  then 2*m >= 2*2 by XREAL_1:68;
  then 2*m - 2 >= 4 - 2 by XREAL_1:11;
  hence 2*m - 2 >= 0;
end;

Lm6: now
  let m be real number;
  assume 1 <= m;
  then 2*m >= 2*1 by XREAL_1:68;
  then 2*m - 1 >= 2 - 1 by XREAL_1:11;
  hence 2*m - 1 >= 0;
end;

Lm7: now
  let m;
  assume 2 <= m;
  then 2*m - 2 >= 0 by Lm5;
  hence 2*m-2 = 2*m-'2 by BINARITH:def 3;
end;

Lm8: now
  let m;
  assume 1 <= m;
  then 2*m - 1 >= 0 by Lm6;
  hence 2*m-1 = 2*m-'1 by BINARITH:def 3;
end;

Lm9: now
  let m;
  assume
A1: m >= 1;
  then 2*m >= 2*1 by XREAL_1:66;
  then 2*m-1 >= 2-1 by XREAL_1:11;
  then
A2: 2*m-'1 >= 1 by A1,Lm8;
  thus 2*m-'2+1 = 2*m-'1-'1+1 by BINARITH:63
    .= 2*m-'1 by A2,BINARITH:53;
end;

Lm10: for x being real number st 2 <= m holds
x/(2|^i)*(m-2) = x/(2|^(i+1))*(2*m-'2-2)
proof
  let x be real number;
  assume 2 <= m;
  then
A1: 2*m - 2 >= 0 by Lm5;
  thus x/(2|^i)*(m-2) = x/((2|^i)/(m-2)) by XCMPLX_1:82
    .= x/((2|^i)*2/((m-2)*2)) by XCMPLX_1:92
    .= x/((2|^i)*2)*((m-2)*2) by XCMPLX_1:82
    .= x/(2|^(i+1))*(2*m-2-2) by NEWTON:11
    .= x/(2|^(i+1))*(2*m-'2-2) by A1,BINARITH:def 3;
end;

Lm11: 2 <= m implies 1 <= 2*m-'2
proof
  assume
A1: 2 <= m;
  then 2*2 <= 2*m by XREAL_1:68;
  then
A2: 4-2 <= 2*m-2 by XREAL_1:11;
  2*m-'2 = 2*m-2 by A1,Lm7;
  hence thesis by A2,XXREAL_0:2;
end;

Lm12: 1 <= m implies 1 <= 2*m-'1
proof
  assume
A1: 1 <= m;
  then 2*1 <= 2*m by XREAL_1:68;
  then 2-1 <= 2*m-1 by XREAL_1:11;
  hence thesis by A1,Lm8;
end;

Lm13: m < 2|^i+3 implies 2*m-'2 < 2|^(i+1) + 3
proof
  per cases by NAT_1:27;
  suppose m = 0 or m = 1;
    hence thesis by NAT_2:10;
  end;
  suppose
A1: 2 <= m;
    assume m < 2|^i+3;
    then m+1 <= 2|^i + 3 by NAT_1:13;
    then 2*(m+1) <= 2*(2|^i + 3) by XREAL_1:66;
    then 2*m+2*1 <= 2*(2|^i) + 2*3;
    then 2*m+2*1 <= 2|^(i+1) + 6 by NEWTON:11;
    then 2*m+2-4 <= 2|^(i+1) + 6 - 4 by XREAL_1:11;
    then 2*m-2 <= 2|^(i+1) + 2;
    then
A2: 2*m-'2 <= 2|^(i+1) + 2 by A1,Lm7;
    2|^(i+1) + 2 < 2|^(i+1) + 3 by XREAL_1:8;
    hence 2*m-'2 < 2|^(i+1) + 3 by A2,XXREAL_0:2;
  end;
end;

Lm14: now
  let m;
  assume 2 <= m;
  hence 2*m-'2+1-2 = 2*m-2+1-2 by Lm7
    .= 2*m-3;
end;

begin  :: Gauges and Cages

theorem Th7:
  2 <= m & m < len Gauge(D,i) &
  1 <= a & a <= len Gauge(D,i) & 1 <= b & b <= len Gauge(D,i+1) implies
  Gauge(D,i)*(m,a)`1 = Gauge(D,i+1)*(2*m-'2,b)`1
proof
  set I = Gauge(D,i), J = Gauge(D,i+1),
  z = N-bound D, e = E-bound D, s = S-bound D, w = W-bound D;
  assume that
A1: 2 <= m and
A2: m < len I and
A3: 1 <= a & a <= len I and
A4: 1 <= b & b <= len J;
A5: len I = width I by JORDAN8:def 1;
A6: len J = width J by JORDAN8:def 1;
A7: 1 <= 2*m-'2 by A1,Lm11;
  m < 2|^i + 3 by A2,JORDAN8:def 1;
  then 2*m-'2 <= 2|^(i+1) + 3 by Lm13;
  then 2*m-'2 <= len J by JORDAN8:def 1;
  then
A8: [2*m-'2,b] in Indices J by A4,A6,A7,MATRIX_1:37;
  1 <= m by A1,XXREAL_0:2;
  then [m,a] in Indices I by A2,A3,A5,MATRIX_1:37;
  hence I*(m,a)`1 = |[w+((e-w)/(2|^i))*(m-2),s+((z-s)/(2|^i))*(a-2)]|`1
  by JORDAN8:def 1
    .= w+((e-w)/(2|^i))*(m-2) by EUCLID:56
    .= w+((e-w)/(2|^(i+1)))*(2*m-'2-2) by A1,Lm10
    .= |[w+((e-w)/(2|^(i+1)))*(2*m-'2-2),s+((z-s)/(2|^(i+1)))*(b-2)]|`1
  by EUCLID:56
    .= J*(2*m-'2,b)`1 by A8,JORDAN8:def 1;
end;

theorem Th8:
  2 <= n & n < len Gauge(D,i) &
  1 <= a & a <= len Gauge(D,i) & 1 <= b & b <= len Gauge(D,i+1) implies
  Gauge(D,i)*(a,n)`2 = Gauge(D,i+1)*(b,2*n-'2)`2
proof
  set I = Gauge(D,i), J = Gauge(D,i+1),
  z = N-bound D, e = E-bound D, s = S-bound D, w = W-bound D;
  assume that
A1: 2 <= n and
A2: n < len I and
A3: 1 <= a & a <= len I and
A4: 1 <= b & b <= len J;
A5: len I = width I by JORDAN8:def 1;
A6: len J = width J by JORDAN8:def 1;
A7: 1 <= 2*n-'2 by A1,Lm11;
  n < 2|^i + 3 by A2,JORDAN8:def 1;
  then 2*n-'2 <= 2|^(i+1) + 3 by Lm13;
  then 2*n-'2 <= len J by JORDAN8:def 1;
  then
A8: [b,2*n-'2] in Indices J by A4,A6,A7,MATRIX_1:37;
  1 <= n by A1,XXREAL_0:2;
  then [a,n] in Indices I by A2,A3,A5,MATRIX_1:37;
  hence I*(a,n)`2 = |[w+((e-w)/(2|^i))*(a-2),s+((z-s)/(2|^i))*(n-2)]|`2
  by JORDAN8:def 1
    .= s+((z-s)/(2|^i))*(n-2) by EUCLID:56
    .= s+((z-s)/(2|^(i+1)))*(2*n-'2-2) by A1,Lm10
    .= |[w+((e-w)/(2|^(i+1)))*(b-2),s+((z-s)/(2|^(i+1)))*(2*n-'2-2)]|`2
  by EUCLID:56
    .= J*(b,2*n-'2)`2 by A8,JORDAN8:def 1;
end;

Lm15: m+1 < len Gauge(D,i) implies 2*m-'1 < len Gauge(D,i+1)
proof
  assume m+1 < len Gauge(D,i);
  then m+1 < 2|^i + 3 by JORDAN8:def 1;
  then 2*(m+1)-'2 < 2|^(i+1) + 3 by Lm13;
  then 2*m+2*1-'2 < 2|^(i+1) + 3;
  then
A1: 2*m < 2|^(i+1) + 3 by BINARITH:39;
  2*m-'1 <= 2*m by BINARITH:62;
  then 2*m-'1 < 2|^(i+1) + 3 by A1,XXREAL_0:2;
  hence thesis by JORDAN8:def 1;
end;

theorem Th9:
  for D being compact non vertical non horizontal Subset of TOP-REAL 2 holds
  2 <= m & m+1 < len Gauge(D,i) & 2 <= n & n+1 < len Gauge(D,i) implies
  cell(Gauge(D,i),m,n) =
  cell(Gauge(D,i+1),2*m-'2,2*n-'2) \/ cell(Gauge(D,i+1),2*m-'1,2*n-'2) \/
  cell(Gauge(D,i+1),2*m-'2,2*n-'1) \/ cell(Gauge(D,i+1),2*m-'1,2*n-'1)
proof
  let D be compact non vertical non horizontal Subset of TOP-REAL 2;
  set I = Gauge(D,i), J = Gauge(D,i+1),
  z = N-bound D, e = E-bound D, s = S-bound D, w = W-bound D;
  assume that
A1: 2 <= m and
A2: m+1 < len I and
A3: 2 <= n and
A4: n+1 < len I;
A5: len I = width I by JORDAN8:def 1;
A6: len J = width J by JORDAN8:def 1;
A7: 1 <= m & 1 <= n by A1,A3,XXREAL_0:2;
A8: 2*m-'2+1 = 2*m-'1 by A1,Lm9,XXREAL_0:2;
A9: 2*n-'2+1 = 2*n-'1 by A3,Lm9,XXREAL_0:2;
A10: 2*m-'2 = 2*m-2 by A1,Lm7;
A11: 2*m-'1 = 2*m-1 by A1,Lm8,XXREAL_0:2;
A12: 2*n-'2 = 2*n-2 by A3,Lm7;
A13: 2*n-'1 = 2*n-1 by A3,Lm8,XXREAL_0:2;
A14: 2*m-'2+1-2 = 2*m-3 by A1,Lm14;
A15: 2*n-'2+1-2 = 2*n-3 by A3,Lm14;
A16: 2*(m+1)-'2-2 = 2*m+2*1-'2-2
    .= 2*m-2 by BINARITH:39;
A17: 2*(n+1)-'2-2 = 2*n+2*1-'2-2
    .= 2*n-2 by BINARITH:39;
A18: 1 <= 2*n-'1 by A7,Lm12;
A19: 1 <= 2*m-'1 by A7,Lm12;
  then
A20: 2*m-'1+1 = 2*m by BINARITH:61;
A21: 2*n-'1+1 = 2*n by A18,BINARITH:61;
A22: m < len I & n < width I by A2,A4,A5,NAT_1:13;
  then
A23: cell(I,m,n) =
  { |[r,q]| where r, q is Real: I*(m,1)`1 <= r & r <= I*(m+1,1)`1 &
  I*(1,n)`2 <= q & q <= I*(1,n+1)`2 } by A7,GOBRD11:32;
A24: 1 < len I by A7,A22,XXREAL_0:2;
A25: 1 <= len J by GOBRD11:34;
A26: 1 <= 2*m-'2 & 1 <= 2*n-'2 by A1,A3,Lm11;
A27: 2*m-'2 < 2*m-'1 & 2*n-'2 < 2*n-'1 by A10,A11,A12,A13,REAL_1:92;
  m < 2|^i + 3 by A22,JORDAN8:def 1;
  then 2*m-'2 < 2|^(i+1) + 3 by Lm13;
  then
A28: 2*m-'2 < len J by JORDAN8:def 1;
  n < 2|^i + 3 by A5,A22,JORDAN8:def 1;
  then 2*n-'2 < 2|^(i+1) + 3 by Lm13;
  then
A29: 2*n-'2 < width J by A6,JORDAN8:def 1;
  then
A30: cell(J,2*m-'2,2*n-'2) =
  { |[r,q]| where r, q is Real: J*(2*m-'2,1)`1 <= r & r <= J* (2*m-'2+1,1)`1 &
  J*(1,2*n-'2)`2 <= q & q <= J*(1,2*n-'2+1)`2 } by A26,A28,GOBRD11:32;
A31: 2*n-'1 < len J by A4,Lm15;
A32: 2*m-'1 < len J by A2,Lm15;
  then
A33: cell(J,2*m-'1,2*n-'2) =
  { |[r,q]| where r, q is Real: J*(2*m-'1,1)`1 <= r & r <= J* (2*m-'1+1,1)`1 &
  J*(1,2*n-'2)`2 <= q & q <= J* (1,2*n-'2+1)`2 } by A19,A26,A29,GOBRD11:32;
A34: cell(J,2*m-'2,2*n-'1) =
  { |[r,q]| where r, q is Real: J*(2*m-'2,1)`1 <= r & r <= J* (2*m-'2+1,1)`1 &
  J*(1,2*n-'1)`2 <= q & q <= J*(1,2*n-'1+1)`2 }
  by A6,A18,A26,A28,A31,GOBRD11:32;
A35: cell(J,2*m-'1,2*n-'1) =
  { |[r,q]| where r, q is Real: J*(2*m-'1,1)`1 <= r & r <= J* (2*m-'1+1,1)`1 &
  J*(1,2*n-'1)`2 <= q & q <= J*(1,2*n-'1+1)`2 }
  by A6,A18,A19,A31,A32,GOBRD11:32;
  e-w >= 0 by SPRECT_1:23,XREAL_1:50;
  then
A36: (e-w)/(2|^(i+1)) >= 0;
  2*m-3 < 2*m-2 by REAL_1:92;
  then (e-w)/(2|^(i+1))*(2*m-3) <= (e-w)/(2|^(i+1))*(2*m-2) by A36,XREAL_1:66;
  then
A37: w+(e-w)/(2|^(i+1))*(2*m-3) <= w+(e-w)/(2|^(i+1))*(2*m-2) by XREAL_1:8;
  z-s >= 0 by SPRECT_1:24,XREAL_1:50;
  then
A38: (z-s)/(2|^(i+1)) >= 0;
  2*n-3 < 2*n-2 by REAL_1:92;
  then (z-s)/(2|^(i+1))*(2*n-3) <= (z-s)/(2|^(i+1))*(2*n-2) by A38,XREAL_1:66;
  then
A39: s+(z-s)/(2|^(i+1))*(2*n-3) <= s+(z-s)/(2|^(i+1))*(2*n-2) by XREAL_1:8;
A40: 2*m-'2+1 <= len J by A28,NAT_1:13;
  1 <= 2*m-'2+1 by NAT_1:11;
  then [2*m-'2+1,1] in Indices J by A6,A25,A40,MATRIX_1:37;
  then
A41: J*(2*m-'2+1,1)`1
  = |[w+(e-w)/(2|^(i+1))*(2*m-'2+1-2),s+(z-s)/(2|^(i+1))*(1-2)]|`1
  by JORDAN8:def 1
    .= w+(e-w)/(2|^(i+1))*(2*m-'2+1-2) by EUCLID:56;
  2*m-'1 <= 2*m by BINARITH:52;
  then
A42: 1 <= 2*m by A19,XXREAL_0:2;
  2*n-'1 <= 2*n by BINARITH:52;
  then
A43: 1 <= 2*n by A18,XXREAL_0:2;
  2*m-'1+1 <= len J by A32,NAT_1:13;
  then [2*m,1] in Indices J by A6,A20,A25,A42,MATRIX_1:37;
  then
A44: J*(2*m,1)`1 = |[w+(e-w)/(2|^(i+1))*(2*m-2),s+(z-s)/(2|^(i+1))*(1-2) ]|`1
  by JORDAN8:def 1
    .= w+(e-w)/(2|^(i+1))*(2*m-2) by EUCLID:56;
  2*n-'1+1 <= len J by A31,NAT_1:13;
  then [1,2*n] in Indices J by A6,A21,A25,A43,MATRIX_1:37;
  then
A45: J*(1,2*n)`2 = |[w+(e-w)/(2|^(i+1))*(1-2),s+(z-s)/(2|^(i+1))*(2*n-2) ]|`2
  by JORDAN8:def 1
    .= s+(z-s)/(2|^(i+1))*(2*n-2) by EUCLID:56;
A46: 2*n-'2+1 <= len J by A6,A29,NAT_1:13;
  1 <= 2*n-'2+1 by NAT_1:11;
  then [1,2*n-'2+1] in Indices J by A6,A25,A46,MATRIX_1:37;
  then
A47: J*(1,2*n-'2+1)`2
  = |[w+(e-w)/(2|^(i+1))*(1-2),s+(z-s)/(2|^(i+1))*(2*n-'2+1-2)]|`2
  by JORDAN8:def 1
    .= s+(z-s)/(2|^(i+1))*(2*n-'2+1-2) by EUCLID:56;
A48: m <= m+1 by NAT_1:11;
A49: n <= n+1 by NAT_1:11;
  1 <= m+1 by NAT_1:11;
  then [m+1,1] in Indices I by A2,A5,A24,MATRIX_1:37;
  then
A50: I*(m+1,1)`1 = |[w+(e-w)/(2|^i)*(m+1-2),s+(z-s)/(2|^i)*(1-2)]|`1
  by JORDAN8:def 1
    .= w+(e-w)/(2|^i)*(m+1-2) by EUCLID:56
    .= w+(e-w)/(2|^(i+1))*(2*(m+1)-'2-2) by A1,A48,Lm10,XXREAL_0:2;
  1 <= n+1 by NAT_1:11;
  then [1,n+1] in Indices I by A4,A5,A24,MATRIX_1:37;
  then
A51: I*(1,n+1)`2 = |[w+(e-w)/(2|^i)*(1-2),s+(z-s)/(2|^i)*(n+1-2)]|`2
  by JORDAN8:def 1
    .= s+(z-s)/(2|^i)*(n+1-2) by EUCLID:56
    .= s+(z-s)/(2|^(i+1))*(2*(n+1)-'2-2) by A3,A49,Lm10,XXREAL_0:2;
A52: I*(m,1)`1 = J*(2*m-'2,1)`1 by A1,A22,A24,A25,Th7;
A53: I*(1,n)`2 = J*(1,2*n-'2)`2 by A3,A5,A22,A24,A25,Th8;
A54: J*(2*m-'2,1)`1 < J*(2*m-'1,1)`1 by A6,A25,A26,A27,A32,GOBOARD5:4;
A55: J*(1,2*n-'2)`2 < J*(1,2*n-'1)`2 by A6,A25,A26,A27,A31,GOBOARD5:5;
  thus cell(Gauge(D,i),m,n) c=
  cell(Gauge(D,i+1),2*m-'2,2*n-'2) \/ cell(Gauge(D,i+1),2*m-'1,2*n-'2) \/
  cell(Gauge(D,i+1),2*m-'2,2*n-'1) \/ cell(Gauge(D,i+1),2*m-'1,2*n-'1)
  proof
    let x be set;
    assume x in cell(I,m,n);
    then consider r, q being Real such that
A56: x = |[r,q]| and
A57: I*(m,1)`1 <= r & r <= I*(m+1,1)`1 & I*(1,n)`2 <= q & q <= I* (1,n+1)`2
    by A23;
    r <= J*(2*m-'1,1)`1 & q <= J*(1,2*n-'1)`2 or
    r >= J*(2*m-'1,1)`1 & q <= J*(1,2*n-'1)`2 or
    r <= J*(2*m-'1,1)`1 & q >= J*(1,2*n-'1)`2 or
    r >= J*(2*m-'1,1)`1 & q >= J*(1,2*n-'1)`2;
    then |[r,q]| in cell(J,2*m-'2,2*n-'2) or
    |[r,q]| in cell(J,2*m-'1,2*n-'2) or |[r,q]| in cell(J,2*m-'2,2*n-'1) or
    |[r,q]| in cell(J,2*m-'1,2*n-'1)
    by A8,A9,A16,A17,A20,A21,A30,A33,A34,A35,A44,A45,A50,A51,A52,A53,A57;
    hence thesis by A56,Lm3;
  end;
  let x be set;
  assume
A58: x in cell(Gauge(D,i+1),2*m-'2,2*n-'2) \/
  cell(Gauge(D,i+1),2*m-'1,2*n-'2) \/ cell(Gauge(D,i+1),2*m-'2,2*n-'1) \/
  cell(Gauge(D,i+1),2*m-'1,2*n-'1);
  per cases by A58,Lm3;
  suppose x in cell(Gauge(D,i+1),2*m-'2,2*n-'2);
    then consider r, q being Real such that
A59: x = |[r,q]| and
A60: J*(2*m-'2,1)`1 <= r and
A61: r <= J*(2*m-'2+1,1)`1 and
A62: J*(1,2*n-'2)`2 <= q and
A63: q <= J*(1,2*n-'2+1)`2 by A30;
A64: r <= I*(m+1,1)`1 by A14,A16,A37,A41,A50,A61,XXREAL_0:2;
    q <= I*(1,n+1)`2 by A15,A17,A39,A47,A51,A63,XXREAL_0:2;
    hence thesis by A23,A52,A53,A59,A60,A62,A64;
  end;
  suppose x in cell(Gauge(D,i+1),2*m-'1,2*n-'2);
    then consider r, q being Real such that
A65: x = |[r,q]| and
A66: J*(2*m-'1,1)`1 <= r and
A67: r <= J*(2*m-'1+1,1)`1 and
A68: J*(1,2*n-'2)`2 <= q and
A69: q <= J*(1,2*n-'2+1)`2 by A33;
A70: I*(m,1)`1 <= r by A52,A54,A66,XXREAL_0:2;
    q <= I*(1,n+1)`2 by A15,A17,A39,A47,A51,A69,XXREAL_0:2;
    hence thesis by A16,A20,A23,A44,A50,A53,A65,A67,A68,A70;
  end;
  suppose x in cell(Gauge(D,i+1),2*m-'2,2*n-'1);
    then consider r, q being Real such that
A71: x = |[r,q]| and
A72: J*(2*m-'2,1)`1 <= r and
A73: r <= J*(2*m-'2+1,1)`1 and
A74: J*(1,2*n-'1)`2 <= q and
A75: q <= J*(1,2*n-'1+1)`2 by A34;
A76: r <= I*(m+1,1)`1 by A14,A16,A37,A41,A50,A73,XXREAL_0:2;
    I*(1,n)`2 <= q by A53,A55,A74,XXREAL_0:2;
    hence thesis by A17,A21,A23,A45,A51,A52,A71,A72,A75,A76;
  end;
  suppose x in cell(Gauge(D,i+1),2*m-'1,2*n-'1);
    then consider r, q being Real such that
A77: x = |[r,q]| and
A78: J*(2*m-'1,1)`1 <= r and
A79: r <= J*(2*m-'1+1,1)`1 and
A80: J*(1,2*n-'1)`2 <= q and
A81: q <= J*(1,2*n-'1+1)`2 by A35;
A82: I*(m,1)`1 <= r by A52,A54,A78,XXREAL_0:2;
    I*(1,n)`2 <= q by A53,A55,A80,XXREAL_0:2;
    hence thesis by A16,A17,A20,A21,A23,A44,A45,A50,A51,A77,A79,A81,A82;
  end;
end;

theorem
  for D being compact non vertical non horizontal Subset of TOP-REAL 2,
  k being Element of NAT holds
  2 <= m & m+1 < len Gauge(D,i) & 2 <= n & n+1 < len Gauge(D,i) implies
  cell(Gauge(D,i),m,n) =
  union { cell(Gauge(D,i+k),a,b) where a, b is Element of NAT:
  2|^k*m - 2|^(k+1) + 2 <= a & a <= 2|^k*m - 2|^k + 1 &
  2|^k*n - 2|^(k+1) + 2 <= b & b <= 2|^k*n - 2|^k + 1 }
proof
  let D be compact non vertical non horizontal Subset of TOP-REAL 2;
  let k be Element of NAT;
  assume that
A1: 2 <= m and
A2: m+1 < len Gauge(D,i) and
A3: 2 <= n and
A4: n+1 < len Gauge(D,i);
  deffunc F(Element of NAT) =
  { cell(Gauge(D,i+$1),a,b) where a, b is Element of NAT:
  2|^$1*m - 2|^($1+1) + 2 <= a & a <= 2|^$1*m - 2|^$1 + 1 &
  2|^$1*n - 2|^($1+1) + 2 <= b & b <= 2|^$1*n - 2|^$1 + 1 };
  defpred P[Element of NAT] means cell(Gauge(D,i),m,n) = union F($1);
A5: P[0]
  proof
A6: now
      let m;
A7:   2|^0 * m = 1*m by NEWTON:9;
      hence 2|^0 * m - 2|^(0+1) + 2 = m - 2 + 2 by NEWTON:10
        .= m;
      thus 2|^0 * m - 2|^0 + 1 = m - 1 + 1 by A7,NEWTON:9
        .= m;
    end;
    F(0) = { cell(Gauge(D,i),m,n) }
    proof
      hereby
        let x be set;
        assume x in F(0);
        then consider a, b such that
A8:     x = cell(Gauge(D,i+0),a,b) and
A9:     2|^0 * m - 2|^(0+1) + 2 <= a & a <= 2|^0 * m - 2|^0 + 1 &
        2|^0 * n - 2|^(0+1) + 2 <= b & b <= 2|^0 * n - 2|^0 + 1;
        now
          let a, m;
          assume
A10:      2|^0 * m - 2|^(0+1) + 2 <= a & a <= 2|^0 * m - 2|^0 + 1;
          2|^0 * m - 2|^(0+1) + 2= m & 2|^0 * m - 2|^0 + 1 = m by A6;
          hence a = m by A10,XXREAL_0:1;
        end;
        then a = m & b = n by A9;
        hence x in { cell(Gauge(D,i),m,n) } by A8,TARSKI:def 1;
      end;
      let x be set;
      assume x in { cell(Gauge(D,i),m,n) };
      then
A11:  x = cell(Gauge(D,i+0),m,n) by TARSKI:def 1;
      2|^0 * m - 2|^(0+1) + 2 <= m & m <= 2|^0 * m - 2|^0 + 1 &
      2|^0 * n - 2|^(0+1) + 2 <= n & n <= 2|^0 * n - 2|^0 + 1 by A6;
      hence x in F(0) by A11;
    end;
    hence thesis by ZFMISC_1:31;
  end;
A12: for w being Element of NAT st P[w] holds P[w+1]
  proof
    let w be Element of NAT such that
A13: P[w];
A14: i+w+1 = i+(w+1);
A15: len Gauge(D,i+w) = 2|^(i+w) + 3 by JORDAN8:def 1;
A16: len Gauge(D,i) = 2|^i + 3 by JORDAN8:def 1;
    for x being set st x in F(w) ex K being set st K c= F(w+1) & x c= union K
    proof
      let x be set;
      assume x in F(w);
      then consider a, b such that
A17:  x = cell(Gauge(D,i+w),a,b) and
A18:  2|^w*m - 2|^(w+1) + 2 <= a and
A19:  a <= 2|^w*m - 2|^w + 1 and
A20:  2|^w*n - 2|^(w+1) + 2 <= b and
A21:  b <= 2|^w*n - 2|^w + 1;
      now
        let m;
        assume 2 <= m;
        then 2|^w*m >= 2|^w*2 by XREAL_1:66;
        then 2|^w*m >= 2|^(w+1) by NEWTON:11;
        then 0 <= 2|^w*m - 2|^(w+1) by XREAL_1:50;
        hence 0 + 2 <= 2|^w*m - 2|^(w+1) + 2 by XREAL_1:8;
      end;
      then
A22:  2 <= 2|^w*m - 2|^(w+1) + 2 & 2 <= 2|^w*n - 2|^(w+1) + 2 by A1,A3;
      then
A23:  2 <= a by A18,XXREAL_0:2;
A24:  2 <= b by A20,A22,XXREAL_0:2;
A25:  2*a-'1 = 2*a-1 by A23,Lm8,XXREAL_0:2;
A26:  2*b-'1 = 2*b-1 by A24,Lm8,XXREAL_0:2;
A27:  2*a-'2 = 2*a-2 by A18,A22,Lm7,XXREAL_0:2;
A28:  2*b-'2 = 2*b-2 by A20,A22,Lm7,XXREAL_0:2;
A29:  2*a-'2 < 2*a-'1 by A25,A27,REAL_1:92;
A30:  2*b-'2 < 2*b-'1 by A26,A28,REAL_1:92;
      take K = { cell(Gauge(D,i+w+1),2*a-'2,2*b-'2),
      cell(Gauge(D,i+w+1),2*a-'1,2*b-'2), cell(Gauge(D,i+w+1),2*a-'2,2*b-'1),
      cell(Gauge(D,i+w+1),2*a-'1,2*b-'1) };
      hereby
        let q be set;
        assume
A31:    q in K;
A32:    now
          let a,m;
          assume
A33:      2 <= a;
          assume a <= 2|^w*m - 2|^w + 1;
          then 2*a <= 2*(2|^w*m - 2|^w + 1) by XREAL_1:66;
          then 2*a <= 2*2|^w*m - 2*(2|^w) + 2;
          then 2*a <= 2|^(w+1)*m - 2*(2|^w) + 2 by NEWTON:11;
          then 2*a <= 2|^(w+1)*m - 2|^(w+1) + 2 by NEWTON:11;
          then 2*a-2 <= 2|^(w+1)*m - 2|^(w+1) + 2 - 2 by XREAL_1:11;
          then
A34:      2*a-'2 <= 2|^(w+1)*m - 2|^(w+1) + 2 - 2 by A33,Lm7;
          2|^(w+1)*m - 2|^(w+1) + 0 < 2|^(w+1)*m - 2|^(w+1) + 1 by XREAL_1:8;
          hence 2*a-'2 < 2|^(w+1)*m - 2|^(w+1) + 1 by A34,XXREAL_0:2;
        end;
A35:    now
          let a,m;
          assume
A36:      2 <= a;
          assume 2|^w*m - 2|^(w+1) + 2 <= a;
          then 2 * (2|^w*m - 2|^(w+1) + 2) <= 2 * a by XREAL_1:66;
          then 2*2|^w*m - 2 * (2|^(w+1)) + 4 <= 2 * a;
          then 2|^(w+1)*m - 2 * (2|^(w+1)) + 4 <= 2 * a by NEWTON:11;
          then 2|^(w+1)*m - 2|^(w+1+1) + 4 <= 2 * a by NEWTON:11;
          then 2|^(w+1)*m - 2|^(w+1+1) + 4 - 2 <= 2 * a - 2 by XREAL_1:11;
          hence 2|^(w+1)*m - 2|^(w+1+1) + (4 - 2) <= 2 * a -' 2 by A36,Lm7;
        end;
        then
A37:    2|^(w+1)*m - 2|^(w+1+1) + 2 <= 2*a-'2 by A18,A23;
A38:    2*a-'2 < 2|^(w+1)*m - 2|^(w+1) + 1 by A19,A23,A32;
        then 2*a-'2+1 <= 2|^(w+1)*m - 2|^(w+1) + 1 by INT_1:20;
        then
A39:    2*a-'1 <= 2|^(w+1)*m - 2|^(w+1) + 1 by A23,Lm9,XXREAL_0:2;
A40:    2|^(w+1)*n - 2|^(w+1+1) + 2 <= 2*b-'2 by A20,A24,A35;
A41:    2*b-'2 < 2|^(w+1)*n - 2|^(w+1) + 1 by A21,A24,A32;
        then 2*b-'2+1 <= 2|^(w+1)*n - 2|^(w+1) + 1 by INT_1:20;
        then
A42:    2*b-'1 <= 2|^(w+1)*n - 2|^(w+1) + 1 by A24,Lm9,XXREAL_0:2;
A43:    2|^(w+1)*m - 2|^(w+1+1) + 2 <= 2*a-'1 by A29,A37,XXREAL_0:2;
A44:    2|^(w+1)*n - 2|^(w+1+1) + 2 <= 2*b-'1 by A30,A40,XXREAL_0:2;
        q = cell(Gauge(D,i+(w+1)),2*a-'2,2*b-'2) or
        q = cell(Gauge(D,i+(w+1)),2*a-'1,2*b-'2) or
        q = cell(Gauge(D,i+(w+1)),2*a-'2,2*b-'1) or
        q = cell(Gauge(D,i+(w+1)),2*a-'1,2*b-'1) by A31,ENUMSET1:def 2;
        hence q in F(w+1) by A37,A38,A39,A40,A41,A42,A43,A44;
      end;
      now
        let a, m;
        assume m+1 < len Gauge(D,i);
        then m+1-2 < 2|^i + 3 - 2 by A16,XREAL_1:11;
        then m-1 < 2|^i + (3 - 2);
        then m-1 <= 2|^i + 0 by INT_1:20;
        then 2|^w*(m-1) <= 2|^w*2|^i by XREAL_1:66;
        then 2|^w*(m-1) <= 2|^(w+i) by NEWTON:13;
        then
A45:    2|^w*(m-1)+3 <= 2|^(w+i)+3 by XREAL_1:8;
        assume a <= 2|^w*m - 2|^w + 1;
        then a+1 <= 2|^w*m - 2|^w + 1 + 1 by XREAL_1:8;
        then a+1 < 2|^w*m - 2|^w + 1 + 1 + 1 by SPPOL_1:5;
        hence a+1 < len Gauge(D,i+w) by A15,A45,XXREAL_0:2;
      end;
      then a+1 < len Gauge(D,i+w) & b+1 < len Gauge(D,i+w) by A2,A4,A19,A21;
      then cell(Gauge(D,i+w),a,b) = cell(Gauge(D,i+w+1),2*a-'2,2*b-'2) \/
      cell(Gauge(D,i+w+1),2*a-'1,2*b-'2) \/
      cell(Gauge(D,i+w+1),2*a-'2,2*b-'1) \/
      cell(Gauge(D,i+w+1),2*a-'1,2*b-'1) by A23,A24,Th9;
      hence x c= union K by A17,Lm4;
    end;
    hence cell(Gauge(D,i),m,n) c= union F(w+1) by A13,Th1;
    F(w+1) is_finer_than F(w)
    proof
      let X be set;
      assume X in F(w+1);
      then consider a, b being Element of NAT such that
A46:  X = cell(Gauge(D,i+(w+1)),a,b) and
A47:  2|^(w+1)*m - 2|^(w+1+1) + 2 <= a and
A48:  a <= 2|^(w+1)*m - 2|^(w+1) + 1 and
A49:  2|^(w+1)*n - 2|^(w+1+1) + 2 <= b and
A50:  b <= 2|^(w+1)*n - 2|^(w+1) + 1;
A51:  now
        let a be even Element of NAT;
A52:    ex e being Element of NAT st a = 2*e by ABIAN:def 2;
        thus 2*(a div 2 + 1) = 2*(a div 2) + 2*1
          .= a + 2 by A52,NAT_D:20;
      end;
A53:  now
        let a be odd Element of NAT;
        consider e being Element of NAT such that
A54:    a = 2*e+1 by ABIAN:9;
A55:    2*e mod 2 = 0 by NAT_D:13;
        thus 2*(a div 2 + 1) = 2*(a div 2) + 2*1
          .= 2*(2*e div 2 + (1 div 2)) + 2 by A54,A55,NAT_D:19
          .= 2*(e + 0) + (1+1) by Lm1,NAT_D:20
          .= a + 1 by A54;
      end;
      deffunc G(Element of NAT,Element of NAT)=
      cell(Gauge(D,i+w+1),2*(a div 2 + 1)-'$1,2*(b div 2 + 1)-'$2);
A56:  now
        let a, m;
        assume
A57:    2 <= m;
        2|^(w+1+1) = 2|^(w+1) * 2|^1 by NEWTON:13
          .= 2|^(w+1) * 2 by NEWTON:10;
        then 2|^(w+1)*m >= 2|^(w+1+1) by A57,XREAL_1:66;
        then 0 <= 2|^(w+1)*m - 2|^(w+1+1) by XREAL_1:50;
        hence 0 + 2 <= 2|^(w+1)*m - 2|^(w+1+1) + 2 by XREAL_1:8;
      end;
      then 2 <= 2|^(w+1)*m - 2|^(w+1+1) + 2 by A1;
      then
A58:  2 <= a by A47,XXREAL_0:2;
      2 <= 2|^(w+1)*n - 2|^(w+1+1) + 2 by A3,A56;
      then
A59:  2 <= b by A49,XXREAL_0:2;
      take Y = cell(Gauge(D,i+w),a div 2 + 1,b div 2 + 1);
      2 div 2 <= a div 2 by A58,NAT_2:26;
      then
A60:  1 + 1 <= a div 2 + 1 by Lm2,XREAL_1:8;
A61:  now
        let m;
        thus 2 * (2|^w*m - 2|^(w+1) + 2) = 2*(2|^w*m) - 2*2|^(w+1) + (2+2)
          .= 2*2|^w*m - 2|^(w+1+1) + (2+2) by NEWTON:11
          .= 2|^(w+1)*m - 2|^(w+1+1) + (2+2) by NEWTON:11
          .= 2|^(w+1)*m - 2|^(w+1+1) + 2+2;
      end;
A62:  now
        let m;
        thus 2 * (2|^w*m - 2|^w + 1) = 2*(2|^w*m) - 2*2|^w + (1+1)
          .= 2*2|^w*m - 2|^(w+1) + (1+1) by NEWTON:11
          .= 2|^(w+1)*m - 2|^(w+1) + (1+1) by NEWTON:11
          .= 2|^(w+1)*m - 2|^(w+1) + 1+1;
      end;
A63:  now
        let a, m;
        assume
A64:    m+1 < len Gauge(D,i);
        assume a <= 2|^(w+1)*m - 2|^(w+1) + 1;
        then a+3 <= 2|^(w+1)*m - 2|^(w+1) + 1 + 3 by XREAL_1:8;
        then
A65:    a+3+0 < 2|^(w+1)*m - 2|^(w+1) + 1 + 3 + 1 by XREAL_1:10;
        then
A66:    a+3+1 <= 2|^(w+1)*m - 2|^(w+1) + 1 + 3 + 1 by INT_1:20;
        m+1 < 2|^i + 3 by A64,JORDAN8:def 1;
        then 2*(m+1)-'2 < 2|^(i+1) + 3 by Lm13;
        then 2*m+2*1-'2 < 2|^(i+1) + 3;
        then 2*m < 2|^(i+1) + 3 by BINARITH:39;
        then 1/2*(2*m) < 1/2*(2|^(i+1) + 3) by XREAL_1:70;
        then m < 1/2*2|^(i+1) + 1/2*3;
        then
A67:    m < 2|^i + 1/2*3 by Th2;
        2|^i + 3/2 < 2|^i + 2 by XREAL_1:8;
        then m < 2|^i + 2 by A67,XXREAL_0:2;
        then m+1 <= 2|^i + 2 by NAT_1:13;
        then m+1-2 <= 2|^i + 2 - 2 by XREAL_1:11;
        then 2|^(w+1)*(m-1) <= 2|^(w+1)*2|^i by XREAL_1:66;
        then 2|^(w+1)*(m - 1) + 5 < 2|^(w+1) * 2|^i + 6 by XREAL_1:10;
        then
A68:    2|^(w+1)*(m - 1) + 5 < 2|^(w+1+i) + 6 by NEWTON:13;
        then
A69:    2|^(w+1)*(m - 1) + 1 + (3 + 1) < 2*2|^(i+w) + 6 by A14,NEWTON:11;
A70:    2|^(w+1)*(m - 1) + 1 + 3 + 1 < 2*2|^(i+w) + 2*3 by A14,A68,NEWTON:11;
        now per cases;
          suppose
A71:        a is odd;
            2 * (a div 2 + 1 + 1) = 2*(a div 2 + 1) + 2*1
              .= a+1+2 by A53,A71
              .= a+(1+2);
            hence 2 * (a div 2 + 1 + 1) < 2 * (2|^(i+w) + 3)
            by A65,A69,XXREAL_0:2;
          end;
          suppose
A72:        a is even;
            2 * (a div 2 + 1 + 1) = 2*(a div 2 + 1) + 2*1
              .= a+2+2 by A51,A72
              .= a+(2+2);
            hence 2 * (a div 2 + 1 + 1) < 2 * (2|^(i+w) + 3)
            by A66,A70,XXREAL_0:2;
          end;
        end;
        hence a div 2 + 1 + 1 < len Gauge(D,i+w) by A15,XREAL_1:66;
      end;
      then
A73:  a div 2 + 1+1 < len Gauge(D,i+w) by A2,A48;
      2 div 2 <= b div 2 by A59,NAT_2:26;
      then
A74:  1 + 1 <= b div 2 + 1 by Lm2,XREAL_1:8;
      b div 2 + 1+1 < len Gauge(D,i+w) by A4,A50,A63;
      then
A75:  Y = G(2,2) \/ G(1,2) \/ G(2,1) \/ G(1,1) by A60,A73,A74,Th9;
A76:  now
        let m;
        let a be even Element of NAT;
        assume
A77:    2|^(w+1)*m - 2|^(w+1+1) + 2 <= a;
A78:    2 * (2|^w*m - 2|^(w+1) + 2) = 2|^(w+1)*m - 2|^(w+1+1) + 2+2 by A61;
        2|^(w+1)*m - 2|^(w+1+1) + 2 + 2 <= a + 2 by A77,XREAL_1:8;
        then 2 * (2|^w*m - 2|^(w+1) + 2) <= 2 * (a div 2 + 1) by A51,A78;
        hence 2|^w*m - 2|^(w+1) + 2 <= a div 2 + 1 by XREAL_1:70;
      end;
A79:  now
        let m;
        let a be even Element of NAT;
        assume a <= 2|^(w+1)*m - 2|^(w+1) + 1;
        then
A80:    a < 2|^(w+1)*m - 2|^(w+1) + 1 by REAL_1:def 5;
A81:    2 * (2|^w*m - 2|^w + 1) = 2|^(w+1)*m - 2|^(w+1) + 1+1 by A62;
        a + 1 <= 2|^(w+1)*m - 2|^(w+1) + 1 by A80,INT_1:20;
        then a + 1 + 1 <= 2|^(w+1)*m - 2|^(w+1) + 1 + 1 by XREAL_1:8;
        then a + (1+1) <= 2|^(w+1)*m - 2|^(w+1) + 1 + 1;
        then 2*(a div 2 + 1) <= 2*(2|^w*m - 2|^w + 1) by A51,A81;
        hence a div 2 + 1 <= 2|^w*m - 2|^w + 1 by XREAL_1:70;
      end;
A82:  now
        let m;
        let a be odd Element of NAT;
        assume 2|^(w+1)*m - 2|^(w+1+1) + 2 <= a;
        then
A83:    2|^(w+1)*m - 2|^(w+1+1) + 2 < a by REAL_1:def 5;
A84:    2 * (2|^w*m - 2|^(w+1) + 2) = 2|^(w+1)*m - 2|^(w+1+1) + 2+2 by A61;
        2|^(w+1)*m - 2|^(w+1+1) + 2 + 1 < a + 1 by A83,XREAL_1:8;
        then 2|^(w+1)*m - 2|^(w+1+1) + 2 + 1 + 1 <= a + 1 by INT_1:20;
        then 2 * (2|^w*m - 2|^(w+1) + 2) <= 2 * (a div 2 + 1) by A53,A84;
        hence 2|^w*m - 2|^(w+1) + 2 <= a div 2 + 1 by XREAL_1:70;
      end;
A85:  now
        let m;
        let a be odd Element of NAT;
        assume
A86:    a <= 2|^(w+1)*m - 2|^(w+1) + 1;
A87:    2 * (2|^w*m - 2|^w + 1) = 2|^(w+1)*m - 2|^(w+1) + 1+1 by A62;
        a + 1 <= 2|^(w+1)*m - 2|^(w+1) + 1 + 1 by A86,XREAL_1:8;
        then 2*(a div 2 + 1) <= 2*(2|^w*m - 2|^w + 1) by A53,A87;
        hence a div 2 + 1 <= 2|^w*m - 2|^w + 1 by XREAL_1:70;
      end;
      per cases;
      suppose
A88:    a is odd & b is odd;
        then
A89:    2*(a div 2 + 1)-'1 = a+1-'1 by A53
          .= a by BINARITH:39;
A90:    2|^w*m - 2|^(w+1) + 2 <= a div 2 + 1 by A47,A82,A88;
A91:    a div 2 + 1 <= 2|^w*m - 2|^w + 1 by A48,A85,A88;
A92:    2|^w*n - 2|^(w+1) + 2 <= b div 2 + 1 by A49,A82,A88;
        b div 2 + 1 <= 2|^w*n - 2|^w + 1 by A50,A85,A88;
        hence Y in F(w) by A90,A91,A92;
        2*(b div 2 + 1)-'1 = b+1-'1 by A53,A88
          .= b by BINARITH:39;
        hence X c= Y by A46,A75,A89,XBOOLE_1:7;
      end;
      suppose
A93:    a is odd & b is even;
        then
A94:    2*(a div 2 + 1)-'1 = a+1-'1 by A53
          .= a by BINARITH:39;
A95:    2*(b div 2 + 1)-'2 = b+2-'2 by A51,A93
          .= b by BINARITH:39;
A96:    2|^w*m - 2|^(w+1) + 2 <= a div 2 + 1 by A47,A82,A93;
A97:    a div 2 + 1 <= 2|^w*m - 2|^w + 1 by A48,A85,A93;
A98:    2|^w*n - 2|^(w+1) + 2 <= b div 2 + 1 by A49,A76,A93;
        b div 2 + 1 <= 2|^w*n - 2|^w + 1 by A50,A79,A93;
        hence Y in F(w) by A96,A97,A98;
A99:    G(1,2) c= G(2,2) \/ G(1,2) by XBOOLE_1:7;
        G(2,2) \/ G(1,2) c= G(2,2) \/ G(1,2) \/ G(2,1) by XBOOLE_1:7;
        then
A100:   G(1,2) c= G(2,2) \/ G(1,2) \/ G(2,1) by A99,XBOOLE_1:1;
        G(2,2) \/ G(1,2) \/ G(2,1) c= Y by A75,XBOOLE_1:7;
        hence X c= Y by A46,A94,A95,A100,XBOOLE_1:1;
      end;
      suppose
A101:   a is even & b is odd;
        then
A102:   2*(a div 2 + 1)-'2 = a+2-'2 by A51
          .= a by BINARITH:39;
A103:   2*(b div 2 + 1)-'1 = b+1-'1 by A53,A101
          .= b by BINARITH:39;
A104:   2|^w*m - 2|^(w+1) + 2 <= a div 2 + 1 by A47,A76,A101;
A105:   a div 2 + 1 <= 2|^w*m - 2|^w + 1 by A48,A79,A101;
A106:   2|^w*n - 2|^(w+1) + 2 <= b div 2 + 1 by A49,A82,A101;
        b div 2 + 1 <= 2|^w*n - 2|^w + 1 by A50,A85,A101;
        hence Y in F(w) by A104,A105,A106;
A107:   G(2,1) c= G(2,2) \/ G(1,2) \/ G(2,1) by XBOOLE_1:7;
        G(2,2) \/ G(1,2) \/ G(2,1) c= Y by A75,XBOOLE_1:7;
        hence X c= Y by A46,A102,A103,A107,XBOOLE_1:1;
      end;
      suppose
A108:   a is even & b is even;
        then
A109:   2*(a div 2 + 1)-'2 = a+2-'2 by A51
          .= a by BINARITH:39;
A110:   2|^w*m - 2|^(w+1) + 2 <= a div 2 + 1 by A47,A76,A108;
A111:   a div 2 + 1 <= 2|^w*m - 2|^w + 1 by A48,A79,A108;
A112:   2|^w*n - 2|^(w+1) + 2 <= b div 2 + 1 by A49,A76,A108;
        b div 2 + 1 <= 2|^w*n - 2|^w + 1 by A50,A79,A108;
        hence Y in F(w) by A110,A111,A112;
        2*(b div 2 + 1)-'2 = b+2-'2 by A51,A108
          .= b by BINARITH:39;
        then X c= G(2,2) \/ (G(1,2) \/ G(2,1) \/
        G(1,1)) by A46,A109,XBOOLE_1:7;
        then X c= G(2,2) \/ (G(1,2) \/ G(2,1)) \/ G(1,1) by XBOOLE_1:4;
        hence X c= Y by A75,XBOOLE_1:4;
      end;
    end;
    then
A113: union F(w+1) c= union F(w) by SETFAM_1:18;
    let d be set;
    assume d in union F(w+1);
    hence thesis by A13,A113;
  end;
  for w being Element of NAT holds P[w] from NAT_1:sch 1(A5,A12);
  hence thesis;
end;

theorem Th11:
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  N-max C in right_cell(Cage(C,n),i,Gauge(C,n))
proof
  N-max C in N-most C by PSCOMP_1:101;
  then consider p be Point of TOP-REAL 2 such that
A1: north_halfline N-max C /\ L~Cage(C,n) = {p} by JORDAN1A:107;
A2: p in north_halfline N-max C /\ L~Cage(C,n) by A1,TARSKI:def 1;
  then
A3: p in north_halfline N-max C & p in L~Cage(C,n) by XBOOLE_0:def 3;
  then consider i be Element of NAT such that
A4: 1 <= i & i+1 <= len Cage(C,n) and
A5: p in LSeg(Cage(C,n),i) by SPPOL_2:13;
  take i; thus
A6: 1 <= i & i < len Cage(C,n) by A4,NAT_1:13;
A7: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i1,j1,i2,j2 be Element of NAT such that
A8: [i1,j1] in Indices Gauge(C,n) and
A9: Cage(C,n)/.i = Gauge(C,n)*(i1,j1) and
A10: [i2,j2] in Indices Gauge(C,n) and
A11: Cage(C,n)/.(i+1) = Gauge(C,n)*(i2,j2) and
A12: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A4,JORDAN8:6;
A13: N-max C in N-most C by PSCOMP_1:101;
  then
A14: p`2 = N-bound L~Cage(C,n) by A2,JORDAN1A:103;
A15: LSeg(Cage(C,n),i) = LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1))
  by A4,TOPREAL1:def 5;
  then
A16: (Cage(C,n)/.i)`2 = p`2 by A3,A5,A6,A13,JORDAN1A:99,SPPOL_1:63;
A17: (Cage(C,n)/.(i+1))`2 = p`2 by A3,A5,A6,A13,A15,JORDAN1A:99,SPPOL_1:63;
A18: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A19: 1 <= i1 & i1 <= len Gauge(C,n) by A8,MATRIX_1:39;
A20: 1 <= i2 & i2 <= len Gauge(C,n) by A10,MATRIX_1:39;
A21: 1 <= j1 & j2 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A22: 1 <= j2 & j1 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A23: j1 = j2
  proof
    assume j1 <> j2;
    then j1 < j2 or j2 < j1 by REAL_1:def 5;
    hence contradiction by A9,A11,A12,A16,A17,A19,A21,A22,GOBOARD5:5;
  end;
  (Gauge(C,n)*(i1,j1))`2 = Gauge(C,n)*(i1,len Gauge(C,n))`2
  by A9,A14,A16,A19,JORDAN1A:91;
  then
A24: len Gauge(C,n) <= j1 by A18,A19,A21,GOBOARD5:5;
  then
A25: j1 = len Gauge(C,n) by A18,A22,XXREAL_0:1;
A26: 1 <= i1 & i1 < len Gauge(C,n)
  by A4,A8,A9,A10,A11,A12,A18,A20,A23,A24,MATRIX_1:39,JORDAN10:4,NAT_1:13;
A27: len Gauge(C,n) >= 4 by JORDAN8:13;
  then
A28: 1 < len Gauge(C,n) by XXREAL_0:2;
A29: (N-max C)`2 = N-bound C by EUCLID:56
    .= Gauge(C,n)*(1,len Gauge(C,n)-'1)`2 by A28,JORDAN8:17;
A30: len Gauge(C,n)-'1 <= len Gauge(C,n) by BINARITH:62;
  len Gauge(C,n) >= 1+1 by A27,XXREAL_0:2;
  then
A31: len Gauge(C,n)-1 >= 1 by XREAL_1:21;
  then len Gauge(C,n)-'1 >= 1 by BINARITH:def 3;
  then
A32: Gauge(C,n)*(1,j1)`2 >= (N-max C)`2 by A18,A25,A28,A29,A30,SPRECT_3:24;
  i1 <= i1+1 by NAT_1:11;
  then (Cage(C,n)/.i)`1 <= (Cage(C,n)/.(i+1))`1 by A4,A8,A9,A10,A11,A12,A18,A19
  ,A20,A21,A23,A24,JORDAN10:4,JORDAN1A:39;
  then (Cage(C,n)/.i)`1 <= p`1 & p`1 <= (Cage(C,n)/.(i+1))`1
  by A5,A15,TOPREAL1:9;
  then Gauge(C,n)*(i1,len Gauge(C,n))`1 <= (N-max C)`1 &
  (N-max C)`1 <= Gauge(C,n)*(i1+1,len Gauge(C,n))`1 by A3,A4,A8,A9,A10,A11,A12
  ,A18,A23,A25,JORDAN10:4,TOPREAL1:def 12;
  then
A33: Gauge(C,n)*(i1,1)`1 <= (N-max C)`1 &
  (N-max C)`1 <= Gauge(C,n)*(i1+1,1)`1 by A4,A8,A9,A10,A11,A12,A18,A19,A20,A23
  ,A24,A28,GOBOARD5:3,JORDAN10:4;
A34: len Gauge(C,n)-'1+1 = len Gauge(C,n) by A27,BINARITH:53,XXREAL_0:2;
  then
A35: len Gauge(C,n)-'1 < len Gauge(C,n) by NAT_1:13;
  N-max C = |[(N-max C)`1,(N-max C)`2]| by EUCLID:57;
  then N-max C in { |[r,s]| where r,s is Real :
  Gauge(C,n)*(i1,1)`1 <= r & r <= Gauge(C,n)*(i1+1,1)`1 &
  Gauge(C,n)*(1,j1-'1)`2 <= s & s <= Gauge(C,n)*(1,j1)`2 } by A25,A29,A32,A33;
  then N-max C in cell(Gauge(C,n),i1,j1-'1)
  by A18,A25,A26,A31,A34,A35,GOBRD11:32;
  hence N-max C in right_cell(Cage(C,n),i,Gauge(C,n))
  by A4,A7,A8,A9,A10,A11,A12,A18,A23,A24,GOBRD13:25,JORDAN10:4;
end;

theorem
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  N-max C in right_cell(Cage(C,n),i)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i < len Cage(C,n) and
A2: N-max C in right_cell(Cage(C,n),i,Gauge(C,n)) by Th11;
  take i;
  thus 1 <= i & i < len Cage(C,n) by A1;
A3: i+1 <= len Cage(C,n) by A1,NAT_1:13;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then right_cell(Cage(C,n),i,Gauge(C,n)) c= right_cell(Cage(C,n),i)
  by A1,A3,GOBRD13:34;
  hence N-max C in right_cell(Cage(C,n),i) by A2;
end;

theorem Th13:
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  E-min C in right_cell(Cage(C,n),i,Gauge(C,n))
proof
  E-min C in E-most C by PSCOMP_1:111;
  then consider p be Point of TOP-REAL 2 such that
A1: east_halfline E-min C /\ L~Cage(C,n) = {p} by JORDAN1A:108;
A2: p in east_halfline E-min C /\ L~Cage(C,n) by A1,TARSKI:def 1;
  then
A3: p in east_halfline E-min C & p in L~Cage(C,n) by XBOOLE_0:def 3;
  then consider i be Element of NAT such that
A4: 1 <= i & i+1 <= len Cage(C,n) and
A5: p in LSeg(Cage(C,n),i) by SPPOL_2:13;
  take i; thus
A6: 1 <= i & i < len Cage(C,n) by A4,NAT_1:13;
A7: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i1,j1,i2,j2 be Element of NAT such that
A8: [i1,j1] in Indices Gauge(C,n) and
A9: Cage(C,n)/.i = Gauge(C,n)*(i1,j1) and
A10: [i2,j2] in Indices Gauge(C,n) and
A11: Cage(C,n)/.(i+1) = Gauge(C,n)*(i2,j2) and
A12: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A4,JORDAN8:6;
A13: E-min C in E-most C by PSCOMP_1:111;
  then
A14: p`1 = E-bound L~Cage(C,n) by A2,JORDAN1A:104;
A15: LSeg(Cage(C,n),i) = LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1))
  by A4,TOPREAL1:def 5;
  then
A16: (Cage(C,n)/.i)`1 = p`1 by A3,A5,A6,A13,JORDAN1A:100,SPPOL_1:64;
A17: (Cage(C,n)/.(i+1))`1 = p`1 by A3,A5,A6,A13,A15,JORDAN1A:100,SPPOL_1:64;
A18: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A19: 1 <= i1 & i1 <= len Gauge(C,n) by A8,MATRIX_1:39;
A20: 1 <= i2 & i2 <= len Gauge(C,n) by A10,MATRIX_1:39;
A21: 1 <= j1 & j2 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A22: 1 <= j2 & j1 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A23: i1 = i2
  proof
    assume i1 <> i2;
    then i1 < i2 or i2 < i1 by REAL_1:def 5;
    hence contradiction by A9,A11,A12,A16,A17,A19,A20,A21,GOBOARD5:4;
  end;
  (Gauge(C,n)*(i1,j1))`1 = Gauge(C,n)*(len Gauge(C,n),j1)`1
  by A9,A14,A16,A18,A21,A22,JORDAN1A:92;
  then
A24: len Gauge(C,n) <= i1 by A19,A21,A22,GOBOARD5:4;
  then
A25: i1 = len Gauge(C,n) by A19,XXREAL_0:1;
A26: 1 <= j2 & j2 < width Gauge(C,n) by A4,A8,A9,A10,A11,A12,A22,A23,A24,
  JORDAN10:1,NAT_1:13;
A27: len Gauge(C,n) >= 4 by JORDAN8:13;
  then
A28: 1 < len Gauge(C,n) by XXREAL_0:2;
A29: (E-min C)`1 = E-bound C by EUCLID:56
    .= Gauge(C,n)*(len Gauge(C,n)-'1,1)`1 by A28,JORDAN8:15;
A30: len Gauge(C,n)-'1 <= len Gauge(C,n) by BINARITH:62;
  len Gauge(C,n) >= 1+1 by A27,XXREAL_0:2;
  then
A31: len Gauge(C,n)-1 >= 1 by XREAL_1:21;
  then len Gauge(C,n)-'1 >= 1 by BINARITH:def 3;
  then
A32: Gauge(C,n)*(i1,1)`1 >= (E-min C)`1 by A18,A25,A28,A29,A30,SPRECT_3:25;
  j2 <= j2+1 by NAT_1:11;
  then (Cage(C,n)/.i)`2 >= (Cage(C,n)/.(i+1))`2
  by A4,A8,A9,A10,A11,A12,A19,A22,A23,A24,JORDAN10:1,JORDAN1A:40;
  then (Cage(C,n)/.(i+1))`2 <= p`2 & p`2 <= (Cage(C,n)/.i)`2
  by A5,A15,TOPREAL1:10;
  then Gauge(C,n)*(len Gauge(C,n),j2)`2 <= (E-min C)`2 &
  (E-min C)`2 <= Gauge(C,n)*(len Gauge(C,n),j2+1)`2 by A3,A4,A8,A9,A10,A11,A12
  ,A23,A25,JORDAN10:1,TOPREAL1:def 13;
  then
A33: Gauge(C,n)*(1,j2)`2 <= (E-min C)`2 & (E-min C)`2 <= Gauge(C,n)*
  (1,j2+1)`2 by A4,A8,A9,A10,A11,A12,A20,A21,A22,A23,A25,GOBOARD5:2,JORDAN10:1;
  len Gauge(C,n) < len Gauge(C,n)+1 by NAT_1:13;
  then
A34: len Gauge(C,n)-1 < len Gauge(C,n) by XREAL_1:21;
A35: len Gauge(C,n)-'1+1 = len Gauge(C,n) by A27,BINARITH:53,XXREAL_0:2;
  E-min C = |[(E-min C)`1,(E-min C)`2]| by EUCLID:57;
  then E-min C in { |[r,s]| where r,s is Real :
  Gauge(C,n)*(len Gauge(C,n)-'1,1)`1 <= r &
  r <= Gauge(C,n)*(len Gauge(C,n),1)`1 & Gauge(C,n)*(1,j2)`2 <= s &
  s <= Gauge(C,n)*(1,j2+1)`2 } by A25,A29,A32,A33;
  then E-min C in cell(Gauge(C,n),i2-'1,j2)
  by A23,A25,A26,A31,A34,A35,GOBRD11:32;
  hence E-min C in right_cell(Cage(C,n),i,Gauge(C,n)) by A4,A7,A8,A9,A10,A11
  ,A12,A23,A24,GOBRD13:29,JORDAN10:1;
end;

theorem
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  E-min C in right_cell(Cage(C,n),i)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i < len Cage(C,n) and
A2: E-min C in right_cell(Cage(C,n),i,Gauge(C,n)) by Th13;
  take i;
  thus 1 <= i & i < len Cage(C,n) by A1;
A3: i+1 <= len Cage(C,n) by A1,NAT_1:13;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then right_cell(Cage(C,n),i,Gauge(C,n)) c= right_cell(Cage(C,n),i)
  by A1,A3,GOBRD13:34;
  hence E-min C in right_cell(Cage(C,n),i) by A2;
end;

theorem Th15:
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  E-max C in right_cell(Cage(C,n),i,Gauge(C,n))
proof
  E-max C in E-most C by PSCOMP_1:111;
  then consider p be Point of TOP-REAL 2 such that
A1: east_halfline E-max C /\ L~Cage(C,n) = {p} by JORDAN1A:108;
A2: p in east_halfline E-max C /\ L~Cage(C,n) by A1,TARSKI:def 1;
  then
A3: p in east_halfline E-max C & p in L~Cage(C,n) by XBOOLE_0:def 3;
  then consider i be Element of NAT such that
A4: 1 <= i & i+1 <= len Cage(C,n) and
A5: p in LSeg(Cage(C,n),i) by SPPOL_2:13;
  take i; thus
A6: 1 <= i & i < len Cage(C,n) by A4,NAT_1:13;
A7: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i1,j1,i2,j2 be Element of NAT such that
A8: [i1,j1] in Indices Gauge(C,n) and
A9: Cage(C,n)/.i = Gauge(C,n)*(i1,j1) and
A10: [i2,j2] in Indices Gauge(C,n) and
A11: Cage(C,n)/.(i+1) = Gauge(C,n)*(i2,j2) and
A12: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A4,JORDAN8:6;
A13: E-max C in E-most C by PSCOMP_1:111;
  then
A14: p`1 = E-bound L~Cage(C,n) by A2,JORDAN1A:104;
A15: LSeg(Cage(C,n),i) = LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1))
  by A4,TOPREAL1:def 5;
  then
A16: (Cage(C,n)/.i)`1 = p`1 by A3,A5,A6,A13,JORDAN1A:100,SPPOL_1:64;
A17: (Cage(C,n)/.(i+1))`1 = p`1 by A3,A5,A6,A13,A15,JORDAN1A:100,SPPOL_1:64;
A18: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A19: 1 <= i1 & i1 <= len Gauge(C,n) by A8,MATRIX_1:39;
A20: 1 <= i2 & i2 <= len Gauge(C,n) by A10,MATRIX_1:39;
A21: 1 <= j1 & j2 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A22: 1 <= j2 & j1 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A23: i1 = i2
  proof
    assume i1 <> i2;
    then i1 < i2 or i2 < i1 by REAL_1:def 5;
    hence contradiction by A9,A11,A12,A16,A17,A19,A20,A21,GOBOARD5:4;
  end;
  (Gauge(C,n)*(i1,j1))`1 = Gauge(C,n)*(len Gauge(C,n),j1)`1
  by A9,A14,A16,A18,A21,A22,JORDAN1A:92;
  then
A24: len Gauge(C,n) <= i1 by A19,A21,A22,GOBOARD5:4;
  then
A25: i1 = len Gauge(C,n) by A19,XXREAL_0:1;
A26: 1 <= j2 & j2 < width Gauge(C,n)
  by A4,A8,A9,A10,A11,A12,A22,A23,A24,JORDAN10:1,NAT_1:13;
A27: len Gauge(C,n) >= 4 by JORDAN8:13;
  then
A28: 1 < len Gauge(C,n) by XXREAL_0:2;
A29: (E-max C)`1 = E-bound C by EUCLID:56
    .= Gauge(C,n)*(len Gauge(C,n)-'1,1)`1 by A28,JORDAN8:15;
A30: len Gauge(C,n)-'1 <= len Gauge(C,n) by BINARITH:62;
  len Gauge(C,n) >= 1+1 by A27,XXREAL_0:2;
  then
A31: len Gauge(C,n)-1 >= 1 by XREAL_1:21;
  then len Gauge(C,n)-'1 >= 1 by BINARITH:def 3;
  then
A32: Gauge(C,n)*(i1,1)`1 >= (E-max C)`1 by A18,A25,A28,A29,A30,SPRECT_3:25;
  j2 <= j2+1 by NAT_1:11;
  then (Cage(C,n)/.i)`2 >= (Cage(C,n)/.(i+1))`2 by A4,A8,A9,A10,A11,A12,A19,A22
  ,A23,A24,JORDAN10:1,JORDAN1A:40;
  then (Cage(C,n)/.(i+1))`2 <= p`2 & p`2 <= (Cage(C,n)/.i)`2
  by A5,A15,TOPREAL1:10;
  then Gauge(C,n)*(len Gauge(C,n),j2)`2 <= (E-max C)`2 &
  (E-max C)`2 <= Gauge(C,n)*(len Gauge(C,n),j2+1)`2 by A3,A4,A8,A9,A10,A11,A12
  ,A23,A25,JORDAN10:1,TOPREAL1:def 13;
  then
A33: Gauge(C,n)*(1,j2)`2 <= (E-max C)`2 & (E-max C)`2 <= Gauge(C,n)*
  (1,j2+1)`2 by A4,A8,A9,A10,A11,A12,A21,A22,A23,A24,A28,GOBOARD5:2,JORDAN10:1;
  len Gauge(C,n) < len Gauge(C,n)+1 by NAT_1:13;
  then
A34: len Gauge(C,n)-1 < len Gauge(C,n) by XREAL_1:21;
A35: len Gauge(C,n)-'1+1 = len Gauge(C,n) by A27,BINARITH:53,XXREAL_0:2;
  E-max C = |[(E-max C)`1,(E-max C)`2]| by EUCLID:57;
  then E-max C in { |[r,s]| where r,s is Real :
  Gauge(C,n)*(len Gauge(C,n)-'1,1)`1 <= r &
  r <= Gauge(C,n)*(len Gauge(C,n),1)`1 & Gauge(C,n)*(1,j2)`2 <= s &
  s <= Gauge(C,n)*(1,j2+1)`2 } by A25,A29,A32,A33;
  then E-max C in cell(Gauge(C,n),i2-'1,j2)
  by A23,A25,A26,A31,A34,A35,GOBRD11:32;
  hence E-max C in right_cell(Cage(C,n),i,Gauge(C,n)) by A4,A7,A8,A9,A10,A11
  ,A12,A23,A24,GOBRD13:29,JORDAN10:1;
end;

theorem
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  E-max C in right_cell(Cage(C,n),i)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i < len Cage(C,n) and
A2: E-max C in right_cell(Cage(C,n),i,Gauge(C,n)) by Th15;
  take i;
  thus 1 <= i & i < len Cage(C,n) by A1;
A3: i+1 <= len Cage(C,n) by A1,NAT_1:13;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then right_cell(Cage(C,n),i,Gauge(C,n)) c= right_cell(Cage(C,n),i)
  by A1,A3,GOBRD13:34;
  hence E-max C in right_cell(Cage(C,n),i) by A2;
end;

theorem Th17:
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  S-min C in right_cell(Cage(C,n),i,Gauge(C,n))
proof
  S-min C in S-most C by PSCOMP_1:121;
  then consider p be Point of TOP-REAL 2 such that
A1: south_halfline S-min C /\ L~Cage(C,n) = {p} by JORDAN1A:109;
A2: p in south_halfline S-min C /\ L~Cage(C,n) by A1,TARSKI:def 1;
  then
A3: p in south_halfline S-min C & p in L~Cage(C,n) by XBOOLE_0:def 3;
  then consider i be Element of NAT such that
A4: 1 <= i & i+1 <= len Cage(C,n) and
A5: p in LSeg(Cage(C,n),i) by SPPOL_2:13;
  take i; thus
A6: 1 <= i & i < len Cage(C,n) by A4,NAT_1:13;
A7: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i1,j1,i2,j2 be Element of NAT such that
A8: [i1,j1] in Indices Gauge(C,n) and
A9: Cage(C,n)/.i = Gauge(C,n)*(i1,j1) and
A10: [i2,j2] in Indices Gauge(C,n) and
A11: Cage(C,n)/.(i+1) = Gauge(C,n)*(i2,j2) and
A12: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A4,JORDAN8:6;
A13: S-min C in S-most C by PSCOMP_1:121;
  then
A14: p`2 = S-bound L~Cage(C,n) by A2,JORDAN1A:105;
A15: LSeg(Cage(C,n),i) = LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1))
  by A4,TOPREAL1:def 5;
  then
A16: (Cage(C,n)/.i)`2 = p`2 by A3,A5,A6,A13,JORDAN1A:101,SPPOL_1:63;
A17: (Cage(C,n)/.(i+1))`2 = p`2 by A3,A5,A6,A13,A15,JORDAN1A:101,SPPOL_1:63;
A18: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A19: 1 <= i1 & i1 <= len Gauge(C,n) by A8,MATRIX_1:39;
A20: 1 <= i2 & i2 <= len Gauge(C,n) by A10,MATRIX_1:39;
A21: 1 <= j1 & j2 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A22: 1 <= j2 & j1 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A23: j1 = j2
  proof
    assume j1 <> j2;
    then j1 < j2 or j2 < j1 by REAL_1:def 5;
    hence contradiction by A9,A11,A12,A16,A17,A19,A21,A22,GOBOARD5:5;
  end;
  (Gauge(C,n)*(i1,j1))`2 = Gauge(C,n)*(i1,1)`2 by A9,A14,A16,A19,JORDAN1A:93;
  then
A24: 1 >= j1 by A19,A22,GOBOARD5:5;
  then
A25: j1 = 1 by A21,XXREAL_0:1;
A26: 1 <= i2 & i2 < len Gauge(C,n) by A4,A8,A9,A10,A11,A12,A19,A23,A24,
  MATRIX_1:39,JORDAN10:3,NAT_1:13;
A27: len Gauge(C,n) >= 4 by JORDAN8:13;
  then
A28: 1 < len Gauge(C,n) by XXREAL_0:2;
A29: 1+1 <= len Gauge(C,n) by A27,XXREAL_0:2;
A30: (S-min C)`2 = S-bound C by EUCLID:56
    .= Gauge(C,n)*(1,2)`2 by A28,JORDAN8:16;
  then
A31: Gauge(C,n)* (1,j1)`2 <= (S-min C)`2 by A18,A25,A28,A29,SPRECT_3:24;
  i2 <= i2+1 by NAT_1:11;
  then (Cage(C,n)/.i)`1 >= (Cage(C,n)/.(i+1))`1 by A4,A8,A9,A10,A11,A12,A19,A20
  ,A21,A22,A24,JORDAN10:3,JORDAN1A:39;
  then (Cage(C,n)/.(i+1))`1 <= p`1 & p`1 <= (Cage(C,n)/.i)`1
  by A5,A15,TOPREAL1:9;
  then
A32: Gauge(C,n)*(i2,1)`1 <= (S-min C)`1 & (S-min C)`1 <= Gauge(C,n)*(i2+
  1,1)`1 by A3,A4,A8,A9,A10,A11,A12,A23,A25,JORDAN10:3,TOPREAL1:def 14;
  S-min C = |[(S-min C)`1,(S-min C)`2]| by EUCLID:57;
  then S-min C in { |[r,s]| where r,s is Real :
  Gauge(C,n)*(i2,1)`1 <= r & r <= Gauge(C,n)*(i2+1,1)`1 &
  Gauge(C,n)*(1,j1)`2 <= s & s <= Gauge(C,n)*(1,j1+1)`2 } by A25,A30,A31,A32;
  then S-min C in cell(Gauge(C,n),i2,j1) by A18,A25,A26,A28,GOBRD11:32;
  hence S-min C in right_cell(Cage(C,n),i,Gauge(C,n))
  by A4,A7,A8,A9,A10,A11,A12,A23,A24,GOBRD13:27,JORDAN10:3;
end;

theorem
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  S-min C in right_cell(Cage(C,n),i)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i < len Cage(C,n) and
A2: S-min C in right_cell(Cage(C,n),i,Gauge(C,n)) by Th17;
  take i;
  thus 1 <= i & i < len Cage(C,n) by A1;
A3: i+1 <= len Cage(C,n) by A1,NAT_1:13;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then right_cell(Cage(C,n),i,Gauge(C,n)) c= right_cell(Cage(C,n),i)
  by A1,A3,GOBRD13:34;
  hence S-min C in right_cell(Cage(C,n),i) by A2;
end;

theorem Th19:
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  S-max C in right_cell(Cage(C,n),i,Gauge(C,n))
proof
  S-max C in S-most C by PSCOMP_1:121;
  then consider p be Point of TOP-REAL 2 such that
A1: south_halfline S-max C /\ L~Cage(C,n) = {p} by JORDAN1A:109;
A2: p in south_halfline S-max C /\ L~Cage(C,n) by A1,TARSKI:def 1;
  then
A3: p in south_halfline S-max C & p in L~Cage(C,n) by XBOOLE_0:def 3;
  then consider i be Element of NAT such that
A4: 1 <= i & i+1 <= len Cage(C,n) and
A5: p in LSeg(Cage(C,n),i) by SPPOL_2:13;
  take i; thus
A6: 1 <= i & i < len Cage(C,n) by A4,NAT_1:13;
A7: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i1,j1,i2,j2 be Element of NAT such that
A8: [i1,j1] in Indices Gauge(C,n) and
A9: Cage(C,n)/.i = Gauge(C,n)*(i1,j1) and
A10: [i2,j2] in Indices Gauge(C,n) and
A11: Cage(C,n)/.(i+1) = Gauge(C,n)*(i2,j2) and
A12: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A4,JORDAN8:6;
A13: S-max C in S-most C by PSCOMP_1:121;
  then
A14: p`2 = S-bound L~Cage(C,n) by A2,JORDAN1A:105;
A15: LSeg(Cage(C,n),i) = LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1))
  by A4,TOPREAL1:def 5;
  then
A16: (Cage(C,n)/.i)`2 = p`2 by A3,A5,A6,A13,JORDAN1A:101,SPPOL_1:63;
A17: (Cage(C,n)/.(i+1))`2 = p`2 by A3,A5,A6,A13,A15,JORDAN1A:101,SPPOL_1:63;
A18: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A19: 1 <= i1 & i1 <= len Gauge(C,n) by A8,MATRIX_1:39;
A20: 1 <= i2 & i2 <= len Gauge(C,n) by A10,MATRIX_1:39;
A21: 1 <= j1 & j2 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A22: 1 <= j2 & j1 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A23: j1 = j2
  proof
    assume j1 <> j2;
    then j1 < j2 or j2 < j1 by REAL_1:def 5;
    hence contradiction by A9,A11,A12,A16,A17,A19,A21,A22,GOBOARD5:5;
  end;
  (Gauge(C,n)*(i1,j1))`2 = Gauge(C,n)*(i1,1)`2 by A9,A14,A16,A19,JORDAN1A:93;
  then
A24: 1 >= j1 by A19,A22,GOBOARD5:5;
  then
A25: j1 = 1 by A21,XXREAL_0:1;
A26: 1 <= i2 & i2 < len Gauge(C,n) by A4,A8,A9,A10,A11,A12,A19,A23,A24,
  MATRIX_1:39,JORDAN10:3,NAT_1:13;
A27: len Gauge(C,n) >= 4 by JORDAN8:13;
  then
A28: 1 < len Gauge(C,n) by XXREAL_0:2;
A29: 1+1 <= len Gauge(C,n) by A27,XXREAL_0:2;
A30: (S-max C)`2 = S-bound C by EUCLID:56
    .= Gauge(C,n)*(1,2)`2 by A28,JORDAN8:16;
  then
A31: Gauge(C,n)* (1,j1)`2 <= (S-max C)`2 by A18,A25,A28,A29,SPRECT_3:24;
  i2 <= i2+1 by NAT_1:11;
  then (Cage(C,n)/.i)`1 >= (Cage(C,n)/.(i+1))`1 by A4,A8,A9,A10,A11,A12,A19,A20
  ,A21,A22,A24,JORDAN10:3,JORDAN1A:39;
  then (Cage(C,n)/.(i+1))`1 <= p`1 & p`1 <= (Cage(C,n)/.i)`1
  by A5,A15,TOPREAL1:9;
  then
A32: Gauge(C,n)*(i2,1)`1 <= (S-max C)`1 & (S-max C)`1 <= Gauge(C,n)*(i2+
  1,1)`1 by A3,A4,A8,A9,A10,A11,A12,A23,A25,JORDAN10:3,TOPREAL1:def 14;
  S-max C = |[(S-max C)`1,(S-max C)`2]| by EUCLID:57;
  then S-max C in { |[r,s]| where r,s is Real :
  Gauge(C,n)*(i2,1)`1 <= r & r <= Gauge(C,n)*(i2+1,1)`1 &
  Gauge(C,n)*(1,j1)`2 <= s & s <= Gauge(C,n)*(1,j1+1)`2 } by A25,A30,A31,A32;
  then S-max C in cell(Gauge(C,n),i2,j1) by A18,A25,A26,A28,GOBRD11:32;
  hence S-max C in right_cell(Cage(C,n),i,Gauge(C,n))
  by A4,A7,A8,A9,A10,A11,A12,A23,A24,GOBRD13:27,JORDAN10:3;
end;

theorem
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  S-max C in right_cell(Cage(C,n),i)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i < len Cage(C,n) and
A2: S-max C in right_cell(Cage(C,n),i,Gauge(C,n)) by Th19;
  take i;
  thus 1 <= i & i < len Cage(C,n) by A1;
A3: i+1 <= len Cage(C,n) by A1,NAT_1:13;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then right_cell(Cage(C,n),i,Gauge(C,n)) c= right_cell(Cage(C,n),i)
  by A1,A3,GOBRD13:34;
  hence S-max C in right_cell(Cage(C,n),i) by A2;
end;

theorem Th21:
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  W-min C in right_cell(Cage(C,n),i,Gauge(C,n))
proof
  W-min C in W-most C by PSCOMP_1:91;
  then consider p be Point of TOP-REAL 2 such that
A1: west_halfline W-min C /\ L~Cage(C,n) = {p} by JORDAN1A:110;
A2: p in west_halfline W-min C /\ L~Cage(C,n) by A1,TARSKI:def 1;
  then
A3: p in west_halfline W-min C & p in L~Cage(C,n) by XBOOLE_0:def 3;
  then consider i be Element of NAT such that
A4: 1 <= i & i+1 <= len Cage(C,n) and
A5: p in LSeg(Cage(C,n),i) by SPPOL_2:13;
  take i; thus
A6: 1 <= i & i < len Cage(C,n) by A4,NAT_1:13;
A7: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i1,j1,i2,j2 be Element of NAT such that
A8: [i1,j1] in Indices Gauge(C,n) and
A9: Cage(C,n)/.i = Gauge(C,n)*(i1,j1) and
A10: [i2,j2] in Indices Gauge(C,n) and
A11: Cage(C,n)/.(i+1) = Gauge(C,n)*(i2,j2) and
A12: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A4,JORDAN8:6;
A13: W-min C in W-most C by PSCOMP_1:91;
  then
A14: p`1 = W-bound L~Cage(C,n) by A2,JORDAN1A:106;
A15: LSeg(Cage(C,n),i) = LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1))
  by A4,TOPREAL1:def 5;
  then
A16: (Cage(C,n)/.i)`1 = p`1 by A3,A5,A6,A13,JORDAN1A:102,SPPOL_1:64;
A17: (Cage(C,n)/.(i+1))`1 = p`1 by A3,A5,A6,A13,A15,JORDAN1A:102,SPPOL_1:64;
A18: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A19: 1 <= i1 & i1 <= len Gauge(C,n) by A8,MATRIX_1:39;
A20: 1 <= i2 & i2 <= len Gauge(C,n) by A10,MATRIX_1:39;
A21: 1 <= j1 & j2 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A22: 1 <= j2 & j1 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A23: i1 = i2
  proof
    assume i1 <> i2;
    then i1 < i2 or i2 < i1 by REAL_1:def 5;
    hence contradiction by A9,A11,A12,A16,A17,A19,A20,A21,GOBOARD5:4;
  end;
  (Gauge(C,n)*(i1,j1))`1 = Gauge(C,n)*(1,j1)`1
  by A9,A14,A16,A18,A21,A22,JORDAN1A:94;
  then
A24: 1 >= i1 by A19,A21,A22,GOBOARD5:4;
  then
A25: i1 = 1 by A19,XXREAL_0:1;
A26: 1 <= j1 & j1 < width Gauge(C,n) by A4,A8,A9,A10,A11,A12,A21,A23,A24,
  JORDAN10:2,NAT_1:13;
A27: len Gauge(C,n) >= 4 by JORDAN8:13;
  then
A28: 1 < len Gauge(C,n) by XXREAL_0:2;
A29: 1+1 <= len Gauge(C,n) by A27,XXREAL_0:2;
A30: (W-min C)`1 = W-bound C by EUCLID:56
    .= Gauge(C,n)*(2,1)`1 by A28,JORDAN8:14;
  then
A31: Gauge(C,n)* (i1,1)`1 <= (W-min C)`1 by A18,A25,A28,A29,SPRECT_3:25;
  j1 <= j1+1 by NAT_1:11;
  then (Cage(C,n)/.i)`2 <= (Cage(C,n)/.(i+1))`2 by A4,A8,A9,A10,A11,A12,A19,A21
  ,A23,A24,JORDAN10:2,JORDAN1A:40;
  then (Cage(C,n)/.i)`2 <= p`2 & p`2 <= (Cage(C,n)/.(i+1))`2
  by A5,A15,TOPREAL1:10;
  then
A32: Gauge(C,n)*(1,j1)`2 <= (W-min C)`2 & (W-min C)`2 <= Gauge(C,n)*(1,
  j1+1)`2 by A3,A4,A8,A9,A10,A11,A12,A23,A25,JORDAN10:2,TOPREAL1:def 15;
  W-min C = |[(W-min C)`1,(W-min C)`2]| by EUCLID:57;
  then W-min C in { |[r,s]| where r,s is Real :
  Gauge(C,n)*(i1,1)`1 <= r & r <= Gauge(C,n)*(i1+1,1)`1 &
  Gauge(C,n)*(1,j1)`2 <= s & s <= Gauge(C,n)*(1,j1+1)`2 } by A25,A30,A31,A32;
  then W-min C in cell(Gauge(C,n),i1,j1) by A25,A26,A28,GOBRD11:32;
  hence W-min C in right_cell(Cage(C,n),i,Gauge(C,n))
  by A4,A7,A8,A9,A10,A11,A12,A23,A24,GOBRD13:23,JORDAN10:2;
end;

theorem
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  W-min C in right_cell(Cage(C,n),i)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i < len Cage(C,n) and
A2: W-min C in right_cell(Cage(C,n),i,Gauge(C,n)) by Th21;
  take i;
  thus 1 <= i & i < len Cage(C,n) by A1;
A3: i+1 <= len Cage(C,n) by A1,NAT_1:13;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then right_cell(Cage(C,n),i,Gauge(C,n)) c= right_cell(Cage(C,n),i)
  by A1,A3,GOBRD13:34;
  hence W-min C in right_cell(Cage(C,n),i) by A2;
end;

theorem Th23:
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  W-max C in right_cell(Cage(C,n),i,Gauge(C,n))
proof
  W-max C in W-most C by PSCOMP_1:91;
  then consider p be Point of TOP-REAL 2 such that
A1: west_halfline W-max C /\ L~Cage(C,n) = {p} by JORDAN1A:110;
A2: p in west_halfline W-max C /\ L~Cage(C,n) by A1,TARSKI:def 1;
  then
A3: p in west_halfline W-max C & p in L~Cage(C,n) by XBOOLE_0:def 3;
  then consider i be Element of NAT such that
A4: 1 <= i & i+1 <= len Cage(C,n) and
A5: p in LSeg(Cage(C,n),i) by SPPOL_2:13;
  take i; thus
A6: 1 <= i & i < len Cage(C,n) by A4,NAT_1:13;
A7: Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i1,j1,i2,j2 be Element of NAT such that
A8: [i1,j1] in Indices Gauge(C,n) and
A9: Cage(C,n)/.i = Gauge(C,n)*(i1,j1) and
A10: [i2,j2] in Indices Gauge(C,n) and
A11: Cage(C,n)/.(i+1) = Gauge(C,n)*(i2,j2) and
A12: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
  i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A4,JORDAN8:6;
A13: W-max C in W-most C by PSCOMP_1:91;
  then
A14: p`1 = W-bound L~Cage(C,n) by A2,JORDAN1A:106;
A15: LSeg(Cage(C,n),i) = LSeg(Cage(C,n)/.i,Cage(C,n)/.(i+1))
  by A4,TOPREAL1:def 5;
  then
A16: (Cage(C,n)/.i)`1 = p`1 by A3,A5,A6,A13,JORDAN1A:102,SPPOL_1:64;
A17: (Cage(C,n)/.(i+1))`1 = p`1 by A3,A5,A6,A13,A15,JORDAN1A:102,SPPOL_1:64;
A18: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
A19: 1 <= i1 & i1 <= len Gauge(C,n) by A8,MATRIX_1:39;
A20: 1 <= i2 & i2 <= len Gauge(C,n) by A10,MATRIX_1:39;
A21: 1 <= j1 & j2 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A22: 1 <= j2 & j1 <= width Gauge(C,n) by A8,A10,MATRIX_1:39;
A23: i1 = i2
  proof
    assume i1 <> i2;
    then i1 < i2 or i2 < i1 by REAL_1:def 5;
    hence contradiction by A9,A11,A12,A16,A17,A19,A20,A21,GOBOARD5:4;
  end;
  (Gauge(C,n)*(i1,j1))`1 = Gauge(C,n)*(1,j1)`1
  by A9,A14,A16,A18,A21,A22,JORDAN1A:94;
  then
A24: 1 >= i1 by A19,A21,A22,GOBOARD5:4;
  then
A25: i1 = 1 by A19,XXREAL_0:1;
A26: 1 <= j1 & j1 < width Gauge(C,n) by A4,A8,A9,A10,A11,A12,A21,A23,A24,
  JORDAN10:2,NAT_1:13;
A27: len Gauge(C,n) >= 4 by JORDAN8:13;
  then
A28: 1 < len Gauge(C,n) by XXREAL_0:2;
A29: 1+1 <= len Gauge(C,n) by A27,XXREAL_0:2;
A30: (W-max C)`1 = W-bound C by EUCLID:56
    .= Gauge(C,n)*(2,1)`1 by A28,JORDAN8:14;
  then
A31: Gauge(C,n)* (i1,1)`1 <= (W-max C)`1 by A18,A25,A28,A29,SPRECT_3:25;
  j1 <= j1+1 by NAT_1:11;
  then (Cage(C,n)/.i)`2 <= (Cage(C,n)/.(i+1))`2 by A4,A8,A9,A10,A11,A12,A19,A21
  ,A23,A24,JORDAN10:2,JORDAN1A:40;
  then (Cage(C,n)/.i)`2 <= p`2 & p`2 <= (Cage(C,n)/.(i+1))`2
  by A5,A15,TOPREAL1:10;
  then
A32: Gauge(C,n)*(1,j1)`2 <= (W-max C)`2 & (W-max C)`2 <= Gauge(C,n)*(1,
  j1+1)`2 by A3,A4,A8,A9,A10,A11,A12,A23,A25,JORDAN10:2,TOPREAL1:def 15;
  W-max C = |[(W-max C)`1,(W-max C)`2]| by EUCLID:57;
  then W-max C in { |[r,s]| where r,s is Real :
  Gauge(C,n)*(i1,1)`1 <= r & r <= Gauge(C,n)*(i1+1,1)`1 &
  Gauge(C,n)*(1,j1)`2 <= s & s <= Gauge(C,n)*(1,j1+1)`2 } by A25,A30,A31,A32;
  then W-max C in cell(Gauge(C,n),i1,j1) by A25,A26,A28,GOBRD11:32;
  hence W-max C in right_cell(Cage(C,n),i,Gauge(C,n))
  by A4,A7,A8,A9,A10,A11,A12,A23,A24,GOBRD13:23,JORDAN10:2;
end;

theorem
  ex i being Element of NAT st 1 <= i & i < len Cage(C,n) &
  W-max C in right_cell(Cage(C,n),i)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i < len Cage(C,n) and
A2: W-max C in right_cell(Cage(C,n),i,Gauge(C,n)) by Th23;
  take i;
  thus 1 <= i & i < len Cage(C,n) by A1;
A3: i+1 <= len Cage(C,n) by A1,NAT_1:13;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then right_cell(Cage(C,n),i,Gauge(C,n)) c= right_cell(Cage(C,n),i)
  by A1,A3,GOBRD13:34;
  hence W-max C in right_cell(Cage(C,n),i) by A2;
end;

theorem Th25:
  ex i being Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  N-min L~Cage(C,n) = Gauge(C,n)*(i,width Gauge(C,n))
proof
  N-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:43;
  then consider m be Nat such that
A1: m in dom Cage(C,n) and
A2: Cage(C,n).m = N-min L~Cage(C,n) by FINSEQ_2:11;
A3: Cage(C,n)/.m = N-min L~Cage(C,n) by A1,A2,PARTFUN1:def 8;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i,j be Element of NAT such that
A4: [i,j] in Indices Gauge(C,n) and
A5: Cage(C,n)/.m = Gauge(C,n)*(i,j) by A1,GOBOARD1:def 11;
  take i; thus
A6: 1 <= i & i <= len Gauge(C,n) by A4,MATRIX_1:39;
A7: 1 <= j & j <= width Gauge(C,n) by A4,MATRIX_1:39;
A8: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  now
    assume j < width Gauge(C,n);
    then (N-min L~Cage(C,n))`2 < Gauge(C,n)*(i,width Gauge(C,n))`2
    by A3,A5,A6,A7,GOBOARD5:5;
    then N-bound L~Cage(C,n) < Gauge(C,n)*(i,width Gauge(C,n))`2 by EUCLID:56;
    hence contradiction by A6,A8,JORDAN1A:91;
  end;
  hence N-min L~Cage(C,n) = Gauge(C,n)*(i,width Gauge(C,n))
  by A3,A5,A7,XXREAL_0:1;
end;

theorem
  ex i being Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  N-max L~Cage(C,n) = Gauge(C,n)*(i,width Gauge(C,n))
proof
  N-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:44;
  then consider m be Nat such that
A1: m in dom Cage(C,n) and
A2: Cage(C,n).m = N-max L~Cage(C,n) by FINSEQ_2:11;
A3: Cage(C,n)/.m = N-max L~Cage(C,n) by A1,A2,PARTFUN1:def 8;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i,j be Element of NAT such that
A4: [i,j] in Indices Gauge(C,n) and
A5: Cage(C,n)/.m = Gauge(C,n)*(i,j) by A1,GOBOARD1:def 11;
  take i; thus
A6: 1 <= i & i <= len Gauge(C,n) by A4,MATRIX_1:39;
A7: 1 <= j & j <= width Gauge(C,n) by A4,MATRIX_1:39;
A8: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  now
    assume j < width Gauge(C,n);
    then (N-max L~Cage(C,n))`2 < Gauge(C,n)*(i,width Gauge(C,n))`2
    by A3,A5,A6,A7,GOBOARD5:5;
    then N-bound L~Cage(C,n) < Gauge(C,n)*(i,width Gauge(C,n))`2 by EUCLID:56;
    hence contradiction by A6,A8,JORDAN1A:91;
  end;
  hence N-max L~Cage(C,n) = Gauge(C,n)*(i,width Gauge(C,n))
  by A3,A5,A7,XXREAL_0:1;
end;

theorem
  ex i being Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  Gauge(C,n)*(i,width Gauge(C,n)) in rng Cage(C,n)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i <= len Gauge(C,n) &
  N-min L~Cage(C,n) = Gauge(C,n)*(i,width Gauge(C,n)) by Th25;
  take i;
  thus thesis by A1,SPRECT_2:43;
end;

theorem Th28:
  ex j being Element of NAT st 1 <= j & j <= width Gauge(C,n) &
  E-min L~Cage(C,n) = Gauge(C,n)*(len Gauge(C,n),j)
proof
  E-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:49;
  then consider m be Nat such that
A1: m in dom Cage(C,n) and
A2: Cage(C,n).m = E-min L~Cage(C,n) by FINSEQ_2:11;
A3: Cage(C,n)/.m = E-min L~Cage(C,n) by A1,A2,PARTFUN1:def 8;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i,j be Element of NAT such that
A4: [i,j] in Indices Gauge(C,n) and
A5: Cage(C,n)/.m = Gauge(C,n)*(i,j) by A1,GOBOARD1:def 11;
  take j; thus
A6: 1 <= j & j <= width Gauge(C,n) by A4,MATRIX_1:39;
A7: 1 <= i & i <= len Gauge(C,n) by A4,MATRIX_1:39;
A8: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  now
    assume i < len Gauge(C,n);
    then (E-min L~Cage(C,n))`1 < Gauge(C,n)*(len Gauge(C,n),j)`1
    by A3,A5,A6,A7,GOBOARD5:4;
    then E-bound L~Cage(C,n) < Gauge(C,n)* (len Gauge(C,n),j)`1 by EUCLID:56;
    hence contradiction by A6,A8,JORDAN1A:92;
  end;
  hence E-min L~Cage(C,n) = Gauge(C,n)*(len Gauge(C,n),j)
  by A3,A5,A7,XXREAL_0:1;
end;

theorem
  ex j being Element of NAT st 1 <= j & j <= width Gauge(C,n) &
  E-max L~Cage(C,n) = Gauge(C,n)*(len Gauge(C,n),j)
proof
  E-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:50;
  then consider m be Nat such that
A1: m in dom Cage(C,n) and
A2: Cage(C,n).m = E-max L~Cage(C,n) by FINSEQ_2:11;
A3: Cage(C,n)/.m = E-max L~Cage(C,n) by A1,A2,PARTFUN1:def 8;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i,j be Element of NAT such that
A4: [i,j] in Indices Gauge(C,n) and
A5: Cage(C,n)/.m = Gauge(C,n)*(i,j) by A1,GOBOARD1:def 11;
  take j; thus
A6: 1 <= j & j <= width Gauge(C,n) by A4,MATRIX_1:39;
A7: 1 <= i & i <= len Gauge(C,n) by A4,MATRIX_1:39;
A8: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  now
    assume i < len Gauge(C,n);
    then (E-max L~Cage(C,n))`1 < Gauge(C,n)*(len Gauge(C,n),j)`1
    by A3,A5,A6,A7,GOBOARD5:4;
    then E-bound L~Cage(C,n) < Gauge(C,n)* (len Gauge(C,n),j)`1 by EUCLID:56;
    hence contradiction by A6,A8,JORDAN1A:92;
  end;
  hence E-max L~Cage(C,n) = Gauge(C,n)*(len Gauge(C,n),j)
  by A3,A5,A7,XXREAL_0:1;
end;

theorem
  ex j being Element of NAT st 1 <= j & j <= width Gauge(C,n) &
  Gauge(C,n)*(len Gauge(C,n),j) in rng Cage(C,n)
proof
  consider j be Element of NAT such that
A1: 1 <= j & j <= width Gauge(C,n) &
  E-min L~Cage(C,n) = Gauge(C,n)*(len Gauge(C,n),j) by Th28;
  take j;
  thus thesis by A1,SPRECT_2:49;
end;

theorem Th31:
  ex i being Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  S-min L~Cage(C,n) = Gauge(C,n)*(i,1)
proof
  S-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:45;
  then consider m be Nat such that
A1: m in dom Cage(C,n) and
A2: Cage(C,n).m = S-min L~Cage(C,n) by FINSEQ_2:11;
A3: Cage(C,n)/.m = S-min L~Cage(C,n) by A1,A2,PARTFUN1:def 8;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i,j be Element of NAT such that
A4: [i,j] in Indices Gauge(C,n) and
A5: Cage(C,n)/.m = Gauge(C,n)*(i,j) by A1,GOBOARD1:def 11;
  take i; thus
A6: 1 <= i & i <= len Gauge(C,n) by A4,MATRIX_1:39;
A7: 1 <= j & j <= width Gauge(C,n) by A4,MATRIX_1:39;
  now
    assume j > 1;
    then (S-min L~Cage(C,n))`2 > Gauge(C,n)*(i,1)`2 by A3,A5,A6,A7,GOBOARD5:5;
    then S-bound L~Cage(C,n) > Gauge(C,n)*(i,1)`2 by EUCLID:56;
    hence contradiction by A6,JORDAN1A:93;
  end;
  hence S-min L~Cage(C,n) = Gauge(C,n)*(i,1) by A3,A5,A7,XXREAL_0:1;
end;

theorem
  ex i being Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  S-max L~Cage(C,n) = Gauge(C,n)*(i,1)
proof
  S-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:46;
  then consider m be Nat such that
A1: m in dom Cage(C,n) and
A2: Cage(C,n).m = S-max L~Cage(C,n) by FINSEQ_2:11;
A3: Cage(C,n)/.m = S-max L~Cage(C,n) by A1,A2,PARTFUN1:def 8;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i,j be Element of NAT such that
A4: [i,j] in Indices Gauge(C,n) and
A5: Cage(C,n)/.m = Gauge(C,n)*(i,j) by A1,GOBOARD1:def 11;
  take i; thus
A6: 1 <= i & i <= len Gauge(C,n) by A4,MATRIX_1:39;
A7: 1 <= j & j <= width Gauge(C,n) by A4,MATRIX_1:39;
  now
    assume j > 1;
    then (S-max L~Cage(C,n))`2 > Gauge(C,n)*(i,1)`2 by A3,A5,A6,A7,GOBOARD5:5;
    then S-bound L~Cage(C,n) > Gauge(C,n)*(i,1)`2 by EUCLID:56;
    hence contradiction by A6,JORDAN1A:93;
  end;
  hence S-max L~Cage(C,n) = Gauge(C,n)*(i,1) by A3,A5,A7,XXREAL_0:1;
end;

theorem
  ex i being Element of NAT st 1 <= i & i <= len Gauge(C,n) &
  Gauge(C,n)*(i,1) in rng Cage(C,n)
proof
  consider i be Element of NAT such that
A1: 1 <= i & i <= len Gauge(C,n) &
  S-min L~Cage(C,n) = Gauge(C,n)*(i,1) by Th31;
  take i;
  thus thesis by A1,SPRECT_2:45;
end;

theorem Th34:
  ex j being Element of NAT st 1 <= j & j <= width Gauge(C,n) &
  W-min L~Cage(C,n) = Gauge(C,n)*(1,j)
proof
  W-min L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:47;
  then consider m be Nat such that
A1: m in dom Cage(C,n) and
A2: Cage(C,n).m = W-min L~Cage(C,n) by FINSEQ_2:11;
A3: Cage(C,n)/.m = W-min L~Cage(C,n) by A1,A2,PARTFUN1:def 8;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i,j be Element of NAT such that
A4: [i,j] in Indices Gauge(C,n) and
A5: Cage(C,n)/.m = Gauge(C,n)*(i,j) by A1,GOBOARD1:def 11;
  take j; thus
A6: 1 <= j & j <= width Gauge(C,n) by A4,MATRIX_1:39;
A7: 1 <= i & i <= len Gauge(C,n) by A4,MATRIX_1:39;
A8: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  now
    assume i > 1;
    then (W-min L~Cage(C,n))`1 > Gauge(C,n)*(1,j)`1 by A3,A5,A6,A7,GOBOARD5:4;
    then W-bound L~Cage(C,n) > Gauge(C,n)*(1,j)`1 by EUCLID:56;
    hence contradiction by A6,A8,JORDAN1A:94;
  end;
  hence W-min L~Cage(C,n) = Gauge(C,n)*(1,j) by A3,A5,A7,XXREAL_0:1;
end;

theorem
  ex j being Element of NAT st 1 <= j & j <= width Gauge(C,n) &
  W-max L~Cage(C,n) = Gauge(C,n)*(1,j)
proof
  W-max L~Cage(C,n) in rng Cage(C,n) by SPRECT_2:48;
  then consider m be Nat such that
A1: m in dom Cage(C,n) and
A2: Cage(C,n).m = W-max L~Cage(C,n) by FINSEQ_2:11;
A3: Cage(C,n)/.m = W-max L~Cage(C,n) by A1,A2,PARTFUN1:def 8;
  Cage(C,n) is_sequence_on Gauge(C,n) by JORDAN9:def 1;
  then consider i,j be Element of NAT such that
A4: [i,j] in Indices Gauge(C,n) and
A5: Cage(C,n)/.m = Gauge(C,n)*(i,j) by A1,GOBOARD1:def 11;
  take j; thus
A6: 1 <= j & j <= width Gauge(C,n) by A4,MATRIX_1:39;
A7: 1 <= i & i <= len Gauge(C,n) by A4,MATRIX_1:39;
A8: len Gauge(C,n) = width Gauge(C,n) by JORDAN8:def 1;
  now
    assume i > 1;
    then (W-max L~Cage(C,n))`1 > Gauge(C,n)*(1,j)`1 by A3,A5,A6,A7,GOBOARD5:4;
    then W-bound L~Cage(C,n) > Gauge(C,n)*(1,j)`1 by EUCLID:56;
    hence contradiction by A6,A8,JORDAN1A:94;
  end;
  hence W-max L~Cage(C,n) = Gauge(C,n)*(1,j) by A3,A5,A7,XXREAL_0:1;
end;

theorem
  ex j being Element of NAT st 1 <= j & j <= width Gauge(C,n) &
  Gauge(C,n)*(1,j) in rng Cage(C,n)
proof
  consider j be Element of NAT such that
A1: 1 <= j & j <= width Gauge(C,n) &
  W-min L~Cage(C,n) = Gauge(C,n)*(1,j) by Th34;
  take j;
  thus thesis by A1,SPRECT_2:47;
end;

