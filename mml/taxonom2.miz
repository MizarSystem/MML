:: Hierarchies and Classifications of Sets
::  by Mariusz Giero
::
:: Received December 28, 2001
:: Copyright (c) 2001 Association of Mizar Users
 
environ

 vocabularies ORDERS_2, XBOOLE_0, SUBSET_1, ORDERS_1, XXREAL_0, YELLOW_1,
      RELAT_2, WELLORD2, RELAT_1, TARSKI, TREES_1, EQREL_1, TAXONOM1, SETFAM_1,
      ZFMISC_1, ABIAN, PRE_TOPC, LATTICES, ORDINAL1, PARTIT1, TAXONOM2;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, RELAT_1, ORDINAL1,
      RELSET_1, STRUCT_0, ORDERS_2, EQREL_1, ABIAN, PARTIT1, ORDERS_1,
      YELLOW_1, TAXONOM1;
 constructors SETFAM_1, REALSET1, ABIAN, YELLOW_1, TAXONOM1, RELSET_1;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, EQREL_1, YELLOW_1;
 requirements BOOLE, SUBSET;
 definitions TARSKI, EQREL_1, TAXONOM1, SETFAM_1, XBOOLE_0;
 theorems TARSKI, TAXONOM1, PARTIT1, EQREL_1, ZFMISC_1, ORDERS_2, ORDERS_1,
      RELAT_1, RELSET_1, SETFAM_1, REALSET1, ABIAN, YELLOW_1, WELLORD2,
      ORDINAL1, XBOOLE_0, XBOOLE_1;
 schemes XBOOLE_0;

begin

reserve A for RelStr;
reserve X for non empty set;
reserve PX,PY,PZ,Y,a,b,c,x,y for set;
reserve S1,S2 for Subset of Y;

definition
  let A;
  attr A is with_superior means
  ex x be Element of A st x is_superior_of the InternalRel of A;
end;

definition
  let A;
  attr A is with_comparable_down means
  :Def2:
  for x,y be Element of A holds (
  ex z be Element of A st z <= x & z <= y) implies (x <= y or y <= x);
end;

theorem Th1:
  for a being set holds InclPoset {{a}} is non empty reflexive
  transitive antisymmetric with_superior with_comparable_down
proof
  let a be set;
  set A = {{a}};
  set R9 = RelIncl A;
  reconsider R = R9 as Relation of A;
  set L = RelStr(#A,R#);
A1: L is with_superior
  proof
    set max1 = {a};
    reconsider max9 = max1 as Element of L by TARSKI:def 1;
    take max9;
A2: for y be set st y in field R & y <> max9 holds [y,max9] in R
    proof
      let y be set such that
A3:   y in field R and
A4:   y <> max9;
      field R c= A \/ A by RELSET_1:19;
      hence thesis by A3,A4,TARSKI:def 1;
    end;
    [max9,max9] in R by WELLORD2:def 1;
    then max9 in field R by RELAT_1:30;
    hence thesis by A2,ORDERS_1:def 13;
  end;
A5: for x,y be Element of L holds (ex z be Element of L st z <= x & z <= y)
  implies x <= y or y <= x
  proof
    let x,y be Element of L;
    assume ex z be Element of L st z <= x & z <= y;
A6: y = {a} by TARSKI:def 1;
    x = {a} by TARSKI:def 1;
    then [x,y] in R by A6,WELLORD2:def 1;
    hence thesis by ORDERS_2:def 9;
  end;
  RelStr(#A,R#) = InclPoset A by YELLOW_1:def 1;
  hence thesis by A1,A5,Def2;
end;

registration
  cluster non empty reflexive transitive antisymmetric with_superior
    with_comparable_down strict RelStr;
  existence
  proof
    take InclPoset {{{}}};
    thus thesis by Th1;
  end;
end;

definition
  mode Tree is with_superior with_comparable_down Poset;
end;

theorem Th2:
  for EqR being Equivalence_Relation of X, x,y,z be set holds z in
  Class(EqR,x) & z in Class(EqR,y) implies Class(EqR,x) = Class(EqR,y)
proof
  let EqR being Equivalence_Relation of X, x,y,z be set;
  assume that
A1: z in Class(EqR,x) and
A2: z in Class(EqR,y);
A3: [z,y] in EqR by A2,EQREL_1:27;
  [z,x] in EqR by A1,EQREL_1:27;
  hence Class(EqR,x) = Class(EqR,z) by A1,EQREL_1:44
    .= Class(EqR,y) by A1,A3,EQREL_1:44;
end;

theorem Th3:
  for P being a_partition of X, x,y,z being set st x in P & y in P
  & z in x & z in y holds x = y
proof
  let P be a_partition of X, x,y,z being set such that
A1: x in P and
A2: y in P and
A3: z in x and
A4: z in y;
  consider EqR being Equivalence_Relation of X such that
A5: P = Class EqR by EQREL_1:43;
A6: ex y9 being Element of X st y = Class(EqR,y9) by A2,A5,EQREL_1:45;
  ex x9 being Element of X st x = Class(EqR,x9) by A1,A5,EQREL_1:45;
  hence thesis by A3,A4,A6,Th2;
end;

theorem Th4:
  for C,x be set st C is Classification of X & x in union C holds x c= X
proof
  let C,x be set such that
A1: C is Classification of X and
A2: x in union C;
  consider Y be set such that
A3: x in Y and
A4: Y in C by A2,TARSKI:def 4;
  reconsider Y9 = Y as a_partition of X by A1,A4,PARTIT1:def 3;
  let a be set;
  assume a in x;
  then a in union Y9 by A3,TARSKI:def 4;
  hence thesis;
end;

theorem
  for C being set st C is Strong_Classification of X holds InclPoset
  union C is Tree
proof
A1: X in {X} by TARSKI:def 1;
A2: X in {X} by TARSKI:def 1;
  let C being set such that
A3: C is Strong_Classification of X;
A4: C is Classification of X by A3,TAXONOM1:def 2;
  set B = union C;
A5: {X} in C by A3,TAXONOM1:def 2;
  then reconsider B9 = B as non empty set by A2,TARSKI:def 4;
  set R9 = RelIncl B;
  reconsider R = R9 as Relation of B;
  set D = RelStr(#B,R#);
  {X} in C by A3,TAXONOM1:def 2;
  then
A6: B <> {} by A1,TARSKI:def 4;
A7: now
    let x,y be Element of D;
    given z be Element of D such that
A8: z <= x and
A9: z <= y;
    reconsider z9 = z as Element of B9;
    reconsider z99 = z9 as Subset of X by A4,Th4;
    consider Z be set such that
A10: z9 in Z and
A11: Z in C by TARSKI:def 4;
    reconsider Z9 = Z as a_partition of X by A3,A11,PARTIT1:def 3;
    z99 in Z9 by A10;
    then z99 <> {} by EQREL_1:def 6;
    then consider a be set such that
A12: a in z by XBOOLE_0:def 1;
    [z,y] in R by A9,ORDERS_2:def 9;
    then
A13: z c= y by A6,WELLORD2:def 1;
    then
A14: a in y by A12;
A15: C is Classification of X by A3,TAXONOM1:def 2;
    reconsider x9 = x, y9 = y as Element of B9;
    consider S be set such that
A16: x9 in S and
A17: S in C by TARSKI:def 4;
    reconsider S9 = S as a_partition of X by A3,A17,PARTIT1:def 3;
    consider T be set such that
A18: y9 in T and
A19: T in C by TARSKI:def 4;
    reconsider T9 = T as a_partition of X by A3,A19,PARTIT1:def 3;
    [z,x] in R by A8,ORDERS_2:def 9;
    then
A20: z c= x by A6,WELLORD2:def 1;
    then
A21: a in x by A12;
    now
      per cases by A17,A19,A15,TAXONOM1:def 1;
      suppose
        S9 is_finer_than T9;
        then ex Y be set st Y in T9 & x9 c= Y by A16,SETFAM_1:def 2;
        hence x9 c= y9 or y9 c= x9 by A12,A21,A13,A18,Th3;
      end;
      suppose
        T9 is_finer_than S9;
        then ex Y be set st Y in S9 & y9 c= Y by A18,SETFAM_1:def 2;
        hence x9 c= y9 or y9 c= x9 by A12,A20,A14,A16,Th3;
      end;
    end;
    then [x9,y9] in R or [y9,x9] in R by WELLORD2:def 1;
    hence x <= y or y <= x by ORDERS_2:def 9;
  end;
A22: D is with_superior
  proof
    reconsider C9 = C as Strong_Classification of X by A3;
    reconsider s = X as Element of D by A5,A2,TARSKI:def 4;
    consider x be set such that
A23: x in SmallestPartition X by XBOOLE_0:def 1;
    SmallestPartition X in C9 by TAXONOM1:def 2;
    then reconsider x9 = x as Element of D by A23,TARSKI:def 4;
    take s;
A24: now
      let y be set such that
A25:  y in field R and
      y <> s;
A26:  y in dom R \/ rng R by A25,RELAT_1:def 6;
      per cases by A26,XBOOLE_0:def 3;
      suppose
        y in dom R;
        then reconsider y9 = y as Element of B9;
        y9 c= s by A4,Th4;
        hence [y,s] in R by WELLORD2:def 1;
      end;
      suppose
        y in rng R;
        then reconsider y9 = y as Element of B9;
        y9 c= s by A4,Th4;
        hence [y,s] in R by WELLORD2:def 1;
      end;
    end;
    [x9,s] in R by A6,A23,WELLORD2:def 1;
    then s in field R by RELAT_1:30;
    hence thesis by A24,ORDERS_1:def 13;
  end;
  RelStr(#B,R#) = InclPoset B by YELLOW_1:def 1;
  hence thesis by A22,A7,Def2;
end;

begin

definition
  let Y;
  attr Y is hierarchic means
  :Def3:
  for x,y be set st x in Y & y in Y holds (x c= y or y c= x or x misses y);
end;

registration
  cluster trivial -> hierarchic set;
  coherence
  proof
    let X be set such that
A1: X is trivial;
    per cases by A1,REALSET1:def 4;
    suppose
A2:   X is empty;
      for x,y be set st x in {} & y in {} holds x c= y or y c= x or x misses y;
      hence thesis by A2,Def3;
    end;
    suppose
      ex a being set st X = {a};
      then consider a be set such that
A3:   X = {a};
      X is hierarchic
      proof
        let x,y be set such that
A4:     x in X and
A5:     y in X;
        x = a by A3,A4,TARSKI:def 1;
        hence thesis by A3,A5,TARSKI:def 1;
      end;
      hence thesis;
    end;
  end;
end;

registration
  cluster non trivial hierarchic set;
  existence
  proof
    consider B be empty set;
    consider A be non empty set;
    consider C be set such that
A1: C = {A,B};
A2: C is hierarchic
    proof
      let x,y be set such that
A3:   x in C and
A4:   y in C;
      per cases by A1,A3,TARSKI:def 2;
      suppose
A5:     x = A;
        per cases by A1,A4,TARSKI:def 2;
        suppose
          y = A;
          hence thesis by A5;
        end;
        suppose
          y = B;
          hence thesis by XBOOLE_1:65;
        end;
      end;
      suppose
        x = B;
        hence thesis by XBOOLE_1:65;
      end;
    end;
    take C;
    now
      assume
A6:   C is trivial;
      per cases by A6,REALSET1:def 4;
      suppose
        C is empty;
        hence contradiction by A1;
      end;
      suppose
        ex x being set st C = {x};
        hence contradiction by A1,ZFMISC_1:9;
      end;
    end;
    hence thesis by A2;
  end;
end;

theorem Th6:
  {} is hierarchic
proof
  let x,y be set;
  assume that
A1: x in {} and
  y in {};
  thus thesis by A1;
end;

theorem
  {{}} is hierarchic
proof
  let x,y be set such that
A1: x in {{}} and
A2: y in {{}};
  x = {} by A1,TARSKI:def 1;
  hence thesis by A2,TARSKI:def 1;
end;

definition
  let Y;
  mode Hierarchy of Y -> Subset-Family of Y means
    :Def4:
    it is hierarchic;
  existence
  proof
    set H = {};
    reconsider H9 = H as Subset-Family of Y by XBOOLE_1:2;
    take H9;
    thus thesis by Th6;
  end;
end;

definition
  let Y;
  attr Y is mutually-disjoint means
  :Def5:
  for x,y be set st x in Y & y in Y & x <> y holds x misses y;
end;

Lm1: now
  let x,y be set such that
A1: x in {{}} and
A2: y in {{}} and
A3: x <> y;
  x = {} by A1,TARSKI:def 1;
  hence x misses y by A2,A3,TARSKI:def 1;
end;

registration
  let Y;
  cluster mutually-disjoint Subset-Family of Y;
  existence
  proof
    set e = {{}};
    now
      let x be set;
      assume x in {{}};
      then x = {} by TARSKI:def 1;
      then x c= Y by XBOOLE_1:2;
      hence x in bool Y;
    end;
    then reconsider e9 = e as Subset-Family of Y by TARSKI:def 3;
    take e9;
    thus thesis by Def5,Lm1;
  end;
end;

theorem
  {} is mutually-disjoint
proof
  let x,y be set;
  assume that
A1: x in {} and
  y in {};
  thus thesis by A1;
end;

theorem
  {{}} is mutually-disjoint by Def5,Lm1;

theorem Th10:
  {a} is mutually-disjoint
proof
  let x,y be set such that
A1: x in {a} and
A2: y in {a} and
A3: x <> y;
  x = a by A1,TARSKI:def 1;
  hence thesis by A2,A3,TARSKI:def 1;
end;

Lm2: now
  let Y;
  let H be Hierarchy of Y such that
A1: H is covering;
  let B be mutually-disjoint Subset-Family of Y such that
A2: B c= H and
A3: for C being mutually-disjoint Subset-Family of Y st C c= H & union B
  c= union C holds B = C;
  Y c= union B
  proof
    let y be set;
    assume y in Y;
    then y in union H by A1,ABIAN:4;
    then consider x be set such that
A4: y in x and
A5: x in H by TARSKI:def 4;
    now
      defpred X[set] means $1 c= x;
      consider D be set such that
A6:   for a be set holds a in D iff a in B & X[a] from XBOOLE_0:sch 1;
      set C = (B \ D) \/ {x};
A7:   (B \ D) c= C by XBOOLE_1:7;
A8:   {x} c= H
      proof
        let s be set;
        assume s in {x};
        hence thesis by A5,TARSKI:def 1;
      end;
      assume
A9:   not y in union B;
A10:  for p being set st p in B & not p in D & p <> x holds p misses x
      proof
A11:    H is hierarchic by Def4;
        let p be set such that
A12:    p in B and
A13:    not p in D and
        p <> x;
A14:    not x c= p by A4,A9,A12,TARSKI:def 4;
        not p c= x by A6,A12,A13;
        hence thesis by A2,A5,A12,A14,A11,Def3;
      end;
A15:  for p,q be set st p in C & q in C & p <> q holds p misses q
      proof
        let p,q be set such that
A16:    p in C and
A17:    q in C and
A18:    p <> q;
        per cases by A16,XBOOLE_0:def 3;
        suppose
A19:      p in (B \ D);
          then
A20:      not p in D by XBOOLE_0:def 5;
A21:      p in B by A19,XBOOLE_0:def 5;
          per cases by A17,XBOOLE_0:def 3;
          suppose
            q in (B \ D);
            then q in B by XBOOLE_0:def 5;
            hence thesis by A18,A21,Def5;
          end;
          suppose
            q in {x};
            then q = x by TARSKI:def 1;
            hence thesis by A10,A18,A21,A20;
          end;
        end;
        suppose
          p in {x};
          then
A22:      p = x by TARSKI:def 1;
          per cases by A17,XBOOLE_0:def 3;
          suppose
A23:        q in (B \ D);
            then
A24:        not q in D by XBOOLE_0:def 5;
            q in B by A23,XBOOLE_0:def 5;
            hence thesis by A10,A18,A22,A24;
          end;
          suppose
            q in {x};
            hence thesis by A18,A22,TARSKI:def 1;
          end;
        end;
      end;
      x in {x} by TARSKI:def 1;
      then
A25:  x in C by XBOOLE_0:def 3;
A26:  union B c= union C
      proof
        let s be set;
        assume s in union B;
        then consider t be set such that
A27:    s in t and
A28:    t in B by TARSKI:def 4;
        per cases;
        suppose
          t in D;
          then t c= x by A6;
          hence thesis by A25,A27,TARSKI:def 4;
        end;
        suppose
          not t in D;
          then t in (B \ D) by A28,XBOOLE_0:def 5;
          hence thesis by A7,A27,TARSKI:def 4;
        end;
      end;
A29:  x in {x} by TARSKI:def 1;
      (B \ D) c= B by XBOOLE_1:36;
      then
A30:  (B \ D) c= H by A2,XBOOLE_1:1;
      then C c= H by A8,XBOOLE_1:8;
      then C is mutually-disjoint Subset-Family of Y by
      A15,Def5, XBOOLE_1
      :1;
      then
A31:  B = C by A3,A30,A8,A26,XBOOLE_1:8;
      {x} c= (B \ D) \/ {x} by XBOOLE_1:7;
      hence contradiction by A4,A9,A31,A29,TARSKI:def 4;
    end;
    hence thesis;
  end;
  hence union B = Y by XBOOLE_0:def 10;
end;

Lm3: now
  let Y;
  let H be Hierarchy of Y;
  let B be mutually-disjoint Subset-Family of Y such that
A1: B c= H and
A2: for C being mutually-disjoint Subset-Family of Y st C c= H & union B
  c= union C holds B = C;
  let S1 such that
A3: S1 in B;
  now
    set C = B \ {{}};
    assume
A4: S1 = {};
A5: union B c= union C
    proof
      let x be set;
      assume x in union B;
      then consider y be set such that
A6:   x in y and
A7:   y in B by TARSKI:def 4;
      not y in {{}} by A6,TARSKI:def 1;
      then y in C by A7,XBOOLE_0:def 5;
      hence thesis by A6,TARSKI:def 4;
    end;
A8: C is mutually-disjoint
    proof
      let x,y be set such that
A9:   x in C and
A10:  y in C and
A11:  x <> y;
A12:  y in B by A10,XBOOLE_0:def 5;
      x in B by A9,XBOOLE_0:def 5;
      hence thesis by A11,A12,Def5;
    end;
    C c= B by XBOOLE_1:36;
    then C c= H by A1,XBOOLE_1:1;
    then B = C by A2,A5,A8;
    then not {} in {{}} by A3,A4,XBOOLE_0:def 5;
    hence contradiction by TARSKI:def 1;
  end;
  hence S1 <> {};
end;

definition
  let Y;
  let F be Subset-Family of Y;
  attr F is T_3 means
  :Def6:
  for A be Subset of Y st A in F for x be Element
  of Y st not x in A ex B be Subset of Y st x in B & B in F & A misses B;
end;

theorem Th11:
  for F be Subset-Family of Y st F = {} holds F is T_3
proof
  let F be Subset-Family of Y such that
A1: F = {};
  let A be Subset of Y such that
A2: A in F;
  let x be Element of Y such that
  not x in A;
  thus thesis by A1,A2;
end;

registration
  let Y;
  cluster covering T_3 Hierarchy of Y;
  existence
  proof
    per cases;
    suppose
A1:   Y <> {};
      set H9 = {Y};
      reconsider H = H9 as Subset-Family of Y by ZFMISC_1:80;
      H is hierarchic
      proof
        let x,y be set such that
A2:     x in H and
A3:     y in H;
        x = Y by A2,TARSKI:def 1;
        hence thesis by A3;
      end;
      then reconsider H as Hierarchy of Y by Def4;
      take H;
      union H = Y by ZFMISC_1:31;
      hence H is covering by ABIAN:4;
      thus H is T_3
      proof
        let A be Subset of Y;
        assume A in H;
        then
A4:     A = Y by TARSKI:def 1;
        let x be Element of Y;
        assume not x in A;
        hence thesis by A1,A4;
      end;
    end;
    suppose
A5:   Y = {};
      set H9 = {};
      reconsider H = H9 as Subset-Family of Y by XBOOLE_1:2;
      reconsider H1 = H as Hierarchy of Y by Def4,Th6;
      take H1;
      thus H1 is covering by A5,ABIAN:4,ZFMISC_1:2;
      thus thesis by Th11;
    end;
  end;
end;

definition
  let Y;
  let F be Subset-Family of Y;
  attr F is lower-bounded means
  :Def7:
  for B being set st B <> {} & B c= F & B
  is c=-linear ex c st c in F & c c= meet B;
end;

theorem Th12:
  for B being mutually-disjoint Subset-Family of Y st for b be set
  st b in B holds S1 misses b & Y <> {} holds B \/ {S1} is mutually-disjoint
  Subset-Family of Y & (S1 <> {} implies union (B \/ {S1}) <> union B)
proof
  let B being mutually-disjoint Subset-Family of Y such that
A1: for b be set st b in B holds S1 misses b & Y <> {};
  set C = B \/ {S1};
A2: now
    let c1,c2 be set such that
A3: c1 in C and
A4: c2 in C and
A5: c1 <> c2;
    per cases by A3,XBOOLE_0:def 3;
    suppose
A6:   c1 in B;
      per cases by A4,XBOOLE_0:def 3;
      suppose
        c2 in B;
        hence c1 misses c2 by A5,A6,Def5;
      end;
      suppose
        c2 in {S1};
        then c2 = S1 by TARSKI:def 1;
        hence c1 misses c2 by A1,A6;
      end;
    end;
    suppose
      c1 in {S1};
      then
A7:   c1 = S1 by TARSKI:def 1;
      then not c2 in {S1} by A5,TARSKI:def 1;
      then c2 in B by A4,XBOOLE_0:def 3;
      hence c1 misses c2 by A1,A7;
    end;
  end;
  {S1} c= bool Y
  proof
    let p be set;
    assume p in {S1};
    then p = S1 by TARSKI:def 1;
    hence thesis;
  end;
  hence C is mutually-disjoint Subset-Family of Y by A2,Def5,XBOOLE_1:8;
  thus S1 <> {} implies union C <> union B
  proof
    assume
A8: S1 <> {};
    not union C c= union B
    proof
A9:   {S1} c= C by XBOOLE_1:7;
      assume
A10:  union C c= union B;
      consider p be set such that
A11:  p in S1 by A8,XBOOLE_0:def 1;
      S1 in {S1} by TARSKI:def 1;
      then p in union C by A11,A9,TARSKI:def 4;
      then consider S2 be set such that
A12:  p in S2 and
A13:  S2 in B by A10,TARSKI:def 4;
      S1 misses S2 by A1,A13;
      hence contradiction by A11,A12,XBOOLE_0:3;
    end;
    hence thesis;
  end;
end;

definition
  let Y;
  let F be Subset-Family of Y;
  attr F is with_max's means
  :Def8:
  for S being Subset of Y st S in F ex T
being Subset of Y st S c= T & T in F & for V being Subset of Y st T c= V & V in
  F holds V = Y;
end;

begin

theorem
  for H being covering Hierarchy of Y st H is with_max's ex P being
  a_partition of Y st P c= H
proof
  let H be covering Hierarchy of Y such that
A1: H is with_max's;
  per cases;
  suppose
A2: Y = {};
    set P9 = {};
    reconsider P = P9 as Subset-Family of Y by XBOOLE_1:2;
    take P;
    thus thesis by A2,EQREL_1:54,XBOOLE_1:2;
  end;
  suppose
A3: Y <> {};
    now
      per cases;
      suppose
A4:     Y in H;
        set P = {Y};
A5:     P c= H
        proof
          let p be set;
          assume p in P;
          hence thesis by A4,TARSKI:def 1;
        end;
        P is a_partition of Y by A3,EQREL_1:48;
        hence thesis by A5;
      end;
      suppose
A6:     not Y in H;
        defpred X[set] means ex S be Subset of Y st (S in H & S c= $1 & for V
        being Subset of Y st $1 c= V & V in H holds V = Y);
        consider P9 be set such that
A7:     for T be set holds T in P9 iff T in H & X[T] from XBOOLE_0:
        sch 1;
A8:     P9 c= H
        proof
          let p be set;
          assume p in P9;
          hence thesis by A7;
        end;
        then reconsider P = P9 as Subset-Family of Y by XBOOLE_1:1;
A9:     now
          let S1 such that
A10:      S1 in P;
          thus S1 <> {}
          proof
            consider S be Subset of Y such that
A11:        S in H and
A12:        S c= S1 and
A13:        for V being Subset of Y st S1 c= V & V in H holds V = Y by A7,A10;
            assume
A14:        S1 = {};
            then S1 = S by A12
              .= Y by A14,A11,A13,XBOOLE_1:2;
            hence contradiction by A3,A14;
          end;
          thus for S2 st S2 in P holds S1 = S2 or S1 misses S2
          proof
            let S2 such that
A15:        S2 in P;
A16:        ex T be Subset of Y st T in H & T c= S2 & for V being
            Subset of Y st S2 c= V & V in H holds V = Y by A7,A15;
            S2 in H by A7,A15;
            hence thesis by A6,A16;
          end;
        end;
A17:    union H = Y by ABIAN:4;
        Y c= union P
        proof
          let p be set;
          assume p in Y;
          then consider h9 be set such that
A18:      p in h9 and
A19:      h9 in H by A17,TARSKI:def 4;
          reconsider h = h9 as Subset of Y by A19;
          consider T be Subset of Y such that
A20:      h c= T and
A21:      T in H and
A22:      for V being Subset of Y st T c= V & V in H holds V = Y by
          A1,A19,Def8;
          T in P by A7,A21,A22;
          hence thesis by A18,A20,TARSKI:def 4;
        end;
        then union P = Y by XBOOLE_0:def 10;
        then P is a_partition of Y by A9,EQREL_1:def 6;
        hence thesis by A8;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  for H being covering Hierarchy of Y for B being mutually-disjoint
Subset-Family of Y st B c= H & for C being mutually-disjoint Subset-Family of Y
  st C c= H & union B c= union C holds B = C holds B is a_partition of Y
proof
  let H be covering Hierarchy of Y;
  let B be mutually-disjoint Subset-Family of Y such that
A1: B c= H and
A2: for C being mutually-disjoint Subset-Family of Y st C c= H & union B
  c= union C holds B = C;
  thus union B = Y by A1,A2,Lm2;
  thus thesis by A1,A2,Def5,Lm3;
end;

theorem
  for H being covering T_3 Hierarchy of Y st H is lower-bounded & not {}
  in H for A being Subset of Y for B being mutually-disjoint Subset-Family of Y
st A in B & B c= H & for C being mutually-disjoint Subset-Family of Y st A in C
& C c= H & union B c= union C holds union B = union C holds B is a_partition of
  Y
proof
  let H be covering T_3 Hierarchy of Y such that
A1: H is lower-bounded and
A2: not {} in H;
  let A be Subset of Y;
  let B be mutually-disjoint Subset-Family of Y such that
A3: A in B and
A4: B c= H and
A5: for C being mutually-disjoint Subset-Family of Y st A in C & C c= H
  & union B c= union C holds union B = union C;
A6: union H = Y by ABIAN:4;
A7: H is hierarchic by Def4;
  per cases;
  suppose
A8: Y <> {};
    Y c= union B
    proof
      assume not Y c= union B;
      then consider x be set such that
A9:   x in Y and
A10:  not x in union B by TARSKI:def 3;
      consider xx be set such that
A11:  x in xx and
A12:  xx in H by A6,A9,TARSKI:def 4;
      defpred X[set] means x in $1;
      consider D be set such that
A13:  for h be set holds h in D iff h in H & X[h] from XBOOLE_0:sch 1;
A14:  D c= H
      proof
        let d be set;
        assume d in D;
        hence thesis by A13;
      end;
      now
        let h1,h2 be set such that
A15:    h1 in D and
A16:    h2 in D;
        now
A17:      x in h2 by A13,A16;
          assume
A18:      h1 misses h2;
          x in h1 by A13,A15;
          hence contradiction by A18,A17,XBOOLE_0:3;
        end;
        then h1 c= h2 or h2 c= h1 by A7,A14,A15,A16,Def3;
        hence h1,h2 are_c=-comparable by XBOOLE_0:def 9;
      end;
      then
A19:  D is c=-linear by ORDINAL1:def 9;
      xx in D by A13,A11,A12;
      then consider min1 be set such that
A20:  min1 in H and
A21:  min1 c= meet D by A1,A14,A19,Def7;
      reconsider min9 = min1 as Subset of Y by A20;
      set C = B \/ {min9};
A22:  B c= C by XBOOLE_1:7;
      now
        reconsider x9 = x as Element of Y by A9;
        let b1 be set such that
A23:    b1 in B;
        reconsider b19 = b1 as Subset of Y by A23;
A24:    not x9 in b19 by A10,A23,TARSKI:def 4;
A25:    not b1 c= min9
        proof
          reconsider b19 = b1 as Subset of Y by A23;
          consider k be Subset of Y such that
A26:      x9 in k and
A27:      k in H and
A28:      k misses b19 by A4,A23,A24,Def6;
          k in D by A13,A26,A27;
          then meet D c= k by SETFAM_1:4;
          then
A29:      min9 c= k by A21,XBOOLE_1:1;
          b1 <> {} by A2,A4,A23;
          then
A30:      ex y be set st y in b19 by XBOOLE_0:def 1;
          assume b1 c= min9;
          then b19 c= k by A29,XBOOLE_1:1;
          hence contradiction by A28,A30,XBOOLE_0:3;
        end;
        not min9 c= b1
        proof
          consider k be Subset of Y such that
A31:      x9 in k and
A32:      k in H and
A33:      k misses b19 by A4,A23,A24,Def6;
          k in D by A13,A31,A32;
          then meet D c= k by SETFAM_1:4;
          then
A34:      min9 c= k by A21,XBOOLE_1:1;
          assume
A35:      min9 c= b1;
          ex y be set st y in min9 by A2,A20,XBOOLE_0:def 1;
          hence contradiction by A35,A33,A34,XBOOLE_0:3;
        end;
        hence b1 misses min9 by A4,A7,A20,A23,A25,Def3;
      end;
      then
A36:  for b be set st b in B holds min9 misses b & Y <> {} by A8;
      then
A37:  C is mutually-disjoint Subset-Family of Y by Th12;
      {min9} c= H
      proof
        let s be set;
        assume s in {min9};
        hence thesis by A20,TARSKI:def 1;
      end;
      then
A38:  C c= H by A4,XBOOLE_1:8;
      union C <> union B by A2,A20,A36,Th12;
      hence contradiction by A3,A5,A37,A38,A22,ZFMISC_1:95;
    end;
    then
A39: union B = Y by XBOOLE_0:def 10;
    for A be Subset of Y st A in B holds (A <> {} & for E be Subset of Y
    st E in B holds A = E or A misses E) by A2,A4,Def5;
    hence thesis by A39,EQREL_1:def 6;
  end;
  suppose
A40: Y = {};
    now
      per cases by A40,ZFMISC_1:1,39;
      suppose
        H = {};
        hence B = {} by A4;
      end;
      suppose
A41:    H = {{}};
        B <> {{}}
        proof
          assume B = {{}};
          then {} in B by TARSKI:def 1;
          hence contradiction by A2,A4;
        end;
        hence B = {} by A4,A41,ZFMISC_1:39;
      end;
    end;
    hence thesis by A40,EQREL_1:54;
  end;
end;

theorem Th16:
  for H being covering T_3 Hierarchy of Y st H is lower-bounded &
not {} in H for A being Subset of Y for B being mutually-disjoint Subset-Family
of Y st A in B & B c= H & for C being mutually-disjoint Subset-Family of Y st A
  in C & C c= H & B c= C holds B = C holds B is a_partition of Y
proof
  let H be covering T_3 Hierarchy of Y such that
A1: H is lower-bounded and
A2: not {} in H;
  let A be Subset of Y;
  let B be mutually-disjoint Subset-Family of Y such that
A3: A in B and
A4: B c= H and
A5: for C being mutually-disjoint Subset-Family of Y st A in C & C c= H
  & B c= C holds B = C;
A6: union H = Y by ABIAN:4;
A7: H is hierarchic by Def4;
  per cases;
  suppose
A8: Y <> {};
    Y c= union B
    proof
      assume not Y c= union B;
      then consider x be set such that
A9:   x in Y and
A10:  not x in union B by TARSKI:def 3;
      consider xx be set such that
A11:  x in xx and
A12:  xx in H by A6,A9,TARSKI:def 4;
      defpred X[set] means x in $1;
      consider D be set such that
A13:  for h be set holds h in D iff h in H & X[h] from XBOOLE_0:sch 1;
A14:  D c= H
      proof
        let d be set;
        assume d in D;
        hence thesis by A13;
      end;
      now
        let h1,h2 be set such that
A15:    h1 in D and
A16:    h2 in D;
        now
A17:      x in h2 by A13,A16;
          assume
A18:      h1 misses h2;
          x in h1 by A13,A15;
          hence contradiction by A18,A17,XBOOLE_0:3;
        end;
        then h1 c= h2 or h2 c= h1 by A7,A14,A15,A16,Def3;
        hence h1,h2 are_c=-comparable by XBOOLE_0:def 9;
      end;
      then
A19:  D is c=-linear by ORDINAL1:def 9;
      xx in D by A13,A11,A12;
      then consider min1 be set such that
A20:  min1 in H and
A21:  min1 c= meet D by A1,A14,A19,Def7;
      reconsider min9 = min1 as Subset of Y by A20;
A22:  {min9} c= H
      proof
        let s be set;
        assume s in {min9};
        hence thesis by A20,TARSKI:def 1;
      end;
      set C = B \/ {min9};
      now
        reconsider x9 = x as Element of Y by A9;
        let b1 be set such that
A23:    b1 in B;
        reconsider b19 = b1 as Subset of Y by A23;
A24:    not x9 in b19 by A10,A23,TARSKI:def 4;
A25:    not b1 c= min9
        proof
          reconsider b19 = b1 as Subset of Y by A23;
          consider k be Subset of Y such that
A26:      x9 in k and
A27:      k in H and
A28:      k misses b19 by A4,A23,A24,Def6;
          k in D by A13,A26,A27;
          then meet D c= k by SETFAM_1:4;
          then
A29:      min9 c= k by A21,XBOOLE_1:1;
          b1 <> {} by A2,A4,A23;
          then
A30:      ex y be set st y in b19 by XBOOLE_0:def 1;
          assume b1 c= min9;
          then b19 c= k by A29,XBOOLE_1:1;
          hence contradiction by A28,A30,XBOOLE_0:3;
        end;
        not min9 c= b1
        proof
          consider k be Subset of Y such that
A31:      x9 in k and
A32:      k in H and
A33:      k misses b19 by A4,A23,A24,Def6;
          k in D by A13,A31,A32;
          then meet D c= k by SETFAM_1:4;
          then
A34:      min9 c= k by A21,XBOOLE_1:1;
          assume
A35:      min9 c= b1;
          ex y be set st y in min9 by A2,A20,XBOOLE_0:def 1;
          hence contradiction by A35,A33,A34,XBOOLE_0:3;
        end;
        hence b1 misses min9 by A4,A7,A20,A23,A25,Def3;
      end;
      then
A36:  for b be set st b in B holds min9 misses b & Y <> {} by A8;
      then
A37:  C is mutually-disjoint Subset-Family of Y by Th12;
A38:  B c= C by XBOOLE_1:7;
      union C <> union B by A2,A20,A36,Th12;
      hence contradiction by A3,A4,A5,A37,A22,A38,XBOOLE_1:8;
    end;
    then
A39: union B = Y by XBOOLE_0:def 10;
    for A be Subset of Y st A in B holds (A <> {} & for E be Subset of Y
    st E in B holds A = E or A misses E) by A2,A4,Def5;
    hence thesis by A39,EQREL_1:def 6;
  end;
  suppose
A40: Y = {};
    now
      per cases by A40,ZFMISC_1:1,39;
      suppose
        H = {};
        hence B = {} by A4;
      end;
      suppose
A41:    H = {{}};
        B <> {{}}
        proof
          assume B = {{}};
          then {} in B by TARSKI:def 1;
          hence contradiction by A2,A4;
        end;
        hence B = {} by A4,A41,ZFMISC_1:39;
      end;
    end;
    hence thesis by A40,EQREL_1:54;
  end;
end;

theorem Th17:
  for H being covering T_3 Hierarchy of Y st H is lower-bounded &
not {} in H for A being Subset of Y st A in H ex P being a_partition of Y st A
  in P & P c= H
proof
  let H be covering T_3 Hierarchy of Y such that
A1: H is lower-bounded and
A2: not {} in H;
  let A be Subset of Y such that
A3: A in H;
  set k1 = {A};
A4: k1 c= H by A3,ZFMISC_1:37;
A5: A in k1 by TARSKI:def 1;
A6: k1 c= bool Y by ZFMISC_1:37;
  defpred X[set] means A in $1 & $1 is mutually-disjoint & $1 c= H;
  consider K be set such that
A7: for S be set holds S in K iff S in bool bool Y & X[S] from XBOOLE_0:
  sch 1;
  k1 is mutually-disjoint by Th10;
  then
A8: k1 in K by A7,A5,A4,A6;
  for Z be set st Z c= K & Z is c=-linear ex X3 be set st X3 in K & for
  X1 be set st X1 in Z holds X1 c= X3
  proof
    let Z be set such that
A9: Z c= K and
A10: Z is c=-linear;
A11: now
      let X1,X2 be set;
      assume that
A12:  X1 in Z and
A13:  X2 in Z;
      X1,X2 are_c=-comparable by A10,A12,A13,ORDINAL1:def 9;
      hence X1 c= X2 or X2 c= X1 by XBOOLE_0:def 9;
    end;
    per cases;
    suppose
A14:  Z <> {};
      set X3 = union Z;
      take X3;
      now
        consider z be set such that
A15:    z in Z by A14,XBOOLE_0:def 1;
        A in z by A7,A9,A15;
        hence A in X3 by A15,TARSKI:def 4;
A16:    for a st a in Z holds a c= H by A7,A9;
        then X3 c= H by ZFMISC_1:94;
        then X3 c= bool Y by XBOOLE_1:1;
        hence X3 in bool bool Y;
        thus X3 is mutually-disjoint
        proof
          let t1,t2 be set such that
A17:      t1 in X3 and
A18:      t2 in X3 and
A19:      t1 <> t2;
          consider v1 be set such that
A20:      t1 in v1 and
A21:      v1 in Z by A17,TARSKI:def 4;
A22:      v1 is mutually-disjoint by A7,A9,A21;
          consider v2 be set such that
A23:      t2 in v2 and
A24:      v2 in Z by A18,TARSKI:def 4;
A25:      v2 is mutually-disjoint by A7,A9,A24;
          per cases by A11,A21,A24;
          suppose
            v1 c= v2;
            hence thesis by A19,A20,A23,A25,Def5;
          end;
          suppose
            v2 c= v1;
            hence thesis by A19,A20,A23,A22,Def5;
          end;
        end;
        thus X3 c= H by A16,ZFMISC_1:94;
      end;
      hence X3 in K by A7;
      thus thesis by ZFMISC_1:92;
    end;
    suppose
A26:  Z = {};
      consider a be set such that
A27:  a in K by A8;
      take a;
      thus a in K by A27;
      thus thesis by A26;
    end;
  end;
  then consider M be set such that
A28: M in K and
A29: for Z be set st Z in K & Z <> M holds not M c= Z by A8,ORDERS_1
  :175;
A30: M is mutually-disjoint Subset-Family of Y by A7,A28;
A31: now
    let C be mutually-disjoint Subset-Family of Y such that
A32: A in C and
A33: C c= H and
A34: M c= C;
    C in K by A7,A32,A33;
    hence M = C by A29,A34;
  end;
A35: A in M by A7,A28;
  take M;
  M c= H by A7,A28;
  hence thesis by A1,A2,A30,A35,A31,Th16;
end;

Lm4: now
  let x be non empty set;
A1: ex a be set st a in x by XBOOLE_0:def 1;
  let y;
  assume x c= y;
  hence x meets y by A1,XBOOLE_0:3;
end;

theorem Th18:
  for h being non empty set for Pmin being a_partition of X for hw
  being set st hw in Pmin & h c= hw for PS being a_partition of X st h in PS &
  for x st x in PS holds (x c= hw or hw c= x or hw misses x) for PT be set st (
  for a holds a in PT iff a in PS & a c= hw) holds PT \/ (Pmin \ {hw}) is
  a_partition of X & PT \/ (Pmin \ {hw}) is_finer_than Pmin
proof
  let h be non empty set;
  let Pmin be a_partition of X;
  let hw be set such that
A1: hw in Pmin and
A2: h c= hw;
  let PS being a_partition of X such that
A3: h in PS and
A4: for x st x in PS holds (x c= hw or hw c= x or hw misses x);
  let PT be set such that
A5: for a holds a in PT iff a in PS & a c= hw;
A6: PT c= PS
  proof
    let a;
    assume a in PT;
    hence thesis by A5;
  end;
A7: union PS = X by EQREL_1:def 6;
A8: union Pmin = X by EQREL_1:def 6;
  set P = PT \/ (Pmin \ {hw});
A9: PT c= P by XBOOLE_1:7;
A10: Pmin \ {hw} c= P by XBOOLE_1:7;
A11: h in PT by A2,A3,A5;
A12: X c= union P
  proof
    let a such that
A13: a in X;
    consider b such that
A14: a in b and
A15: b in Pmin by A8,A13,TARSKI:def 4;
    per cases;
    suppose
A16:  b = hw;
      consider c such that
A17:  a in c and
A18:  c in PS by A7,A13,TARSKI:def 4;
A19:  hw meets c by A14,A16,A17,XBOOLE_0:3;
      per cases by A4,A18,A19;
      suppose
        hw c= c;
        then
A20:    h c= c by A2,XBOOLE_1:1;
        h meets c
        proof
A21:      ex x st x in h by XBOOLE_0:def 1;
          assume h misses c;
          hence contradiction by A20,A21,XBOOLE_0:3;
        end;
        then h = c by A3,A18,EQREL_1:def 6;
        hence thesis by A9,A11,A17,TARSKI:def 4;
      end;
      suppose
        c c= hw;
        then c in PT by A5,A18;
        hence thesis by A9,A17,TARSKI:def 4;
      end;
    end;
    suppose
      b <> hw;
      then not b in {hw} by TARSKI:def 1;
      then b in Pmin \ {hw} by A15,XBOOLE_0:def 5;
      hence thesis by A10,A14,TARSKI:def 4;
    end;
  end;
A22: Pmin \ {hw} c= Pmin by XBOOLE_1:36;
A23: now
    let x,y such that
A24: x in PT and
A25: y in Pmin \ {hw};
A26: y in Pmin by A25,XBOOLE_0:def 5;
    not y in {hw} by A25,XBOOLE_0:def 5;
    then
A27: y <> hw by TARSKI:def 1;
A28: x c= hw by A5,A24;
    now
      assume x meets y;
      then ex a st a in x & a in y by XBOOLE_0:3;
      then hw meets y by A28,XBOOLE_0:3;
      hence contradiction by A1,A26,A27,EQREL_1:def 6;
    end;
    hence x misses y;
  end;
A29: now
    let A be Subset of X such that
A30: A in P;
    now
      per cases by A30,XBOOLE_0:def 3;
      suppose
        A in PT;
        hence A <> {} by A6,EQREL_1:def 6;
      end;
      suppose
        A in Pmin \ {hw};
        hence A <> {} by A22,EQREL_1:def 6;
      end;
    end;
    hence A <> {};
    thus for B be Subset of X st B in P holds A = B or A misses B
    proof
      let B be Subset of X such that
A31:  B in P;
      per cases by A30,XBOOLE_0:def 3;
      suppose
A32:    A in PT;
        per cases by A31,XBOOLE_0:def 3;
        suppose
          B in PT;
          hence thesis by A6,A32,EQREL_1:def 6;
        end;
        suppose
          B in Pmin \ {hw};
          hence thesis by A23,A32;
        end;
      end;
      suppose
A33:    A in Pmin \ {hw};
        per cases by A31,XBOOLE_0:def 3;
        suppose
          B in PT;
          hence thesis by A23,A33;
        end;
        suppose
          B in Pmin \ {hw};
          hence thesis by A22,A33,EQREL_1:def 6;
        end;
      end;
    end;
  end;
  PT c= bool X by A6,XBOOLE_1:1;
  then
A34: P c= bool X by XBOOLE_1:8;
  union P c= X
  proof
    let a;
    assume a in union P;
    then ex b st a in b & b in P by TARSKI:def 4;
    hence thesis by A34;
  end;
  then union P = X by A12,XBOOLE_0:def 10;
  hence PT \/ (Pmin \ {hw}) is a_partition of X by A34,A29,EQREL_1:def 6;
  let a such that
A35: a in P;
  per cases by A35,XBOOLE_0:def 3;
  suppose
    a in PT;
    then a c= hw by A5;
    hence thesis by A1;
  end;
  suppose
    a in Pmin \ {hw};
    hence thesis by A22;
  end;
end;

theorem Th19:
  for h being non empty set st h c= X for Pmax being a_partition
of X st ( (ex hy be set st hy in Pmax & hy c= h) & for x st x in Pmax holds (x
c= h or h c= x or h misses x)) for Pb be set st (for x holds x in Pb iff (x in
Pmax & x misses h)) holds Pb \/ {h} is a_partition of X & Pmax is_finer_than (
  Pb \/ {h})& for Pmin being a_partition of X st Pmax is_finer_than Pmin for hw
  being set st hw in Pmin & h c= hw holds (Pb \/ {h}) is_finer_than Pmin
proof
  let h be non empty set such that
A1: h c= X;
A2: {h} c= bool X
  proof
    let s be set;
    assume s in {h};
    then s = h by TARSKI:def 1;
    hence thesis by A1;
  end;
A3: h in {h} by TARSKI:def 1;
  let Pmax be a_partition of X such that
A4: ex hy be set st hy in Pmax & hy c= h and
A5: for x st x in Pmax holds (x c= h or h c= x or h misses x);
A6: now
    let s be set such that
A7: s in Pmax and
A8: h c= s;
    consider hy be set such that
A9: hy in Pmax and
A10: hy c= h by A4;
    hy <> {} by A9,EQREL_1:def 6;
    then hy meets s by A8,A10,Lm4,XBOOLE_1:1;
    then s = hy by A7,A9,EQREL_1:def 6;
    hence h = s by A8,A10,XBOOLE_0:def 10;
  end;
  let Pb be set such that
A11: for x holds x in Pb iff x in Pmax & x misses h;
  set P = Pb \/ {h};
A12: Pb c= P by XBOOLE_1:7;
A13: Pb c= Pmax
  proof
    let s be set;
    assume s in Pb;
    hence thesis by A11;
  end;
A14: now
    let A be Subset of X such that
A15: A in P;
    now
      per cases by A15,XBOOLE_0:def 3;
      suppose
        A in Pb;
        then A in Pmax by A11;
        hence A <> {} by EQREL_1:def 6;
      end;
      suppose
        A in {h};
        hence A <> {} by TARSKI:def 1;
      end;
    end;
    hence A<>{};
    thus for B be Subset of X st B in P holds A = B or A misses B
    proof
      let B be Subset of X such that
A16:  B in P;
      per cases by A15,XBOOLE_0:def 3;
      suppose
A17:    A in Pb;
        per cases by A16,XBOOLE_0:def 3;
        suppose
          B in Pb;
          hence thesis by A13,A17,EQREL_1:def 6;
        end;
        suppose
          B in {h};
          then B = h by TARSKI:def 1;
          hence thesis by A11,A17;
        end;
      end;
      suppose
A18:    A in {h};
        per cases by A16,XBOOLE_0:def 3;
        suppose
A19:      B in Pb;
          A = h by A18,TARSKI:def 1;
          hence thesis by A11,A19;
        end;
        suppose
          B in {h};
          then B = h by TARSKI:def 1;
          hence thesis by A18,TARSKI:def 1;
        end;
      end;
    end;
  end;
A20: {h} c= P by XBOOLE_1:7;
A21: union Pmax = X by EQREL_1:def 6;
A22: X c= union P
  proof
    let s be set;
    assume s in X;
    then consider t be set such that
A23: s in t and
A24: t in Pmax by A21,TARSKI:def 4;
    per cases;
    suppose
      t in Pb;
      hence thesis by A12,A23,TARSKI:def 4;
    end;
    suppose
      not t in Pb;
      then
A25:  t meets h by A11,A24;
      per cases by A5,A24,A25;
      suppose
        h c= t;
        then t = h by A6,A24;
        hence thesis by A3,A20,A23,TARSKI:def 4;
      end;
      suppose
A26:    t c= h;
        h in {h} by TARSKI:def 1;
        hence thesis by A20,A23,A26,TARSKI:def 4;
      end;
    end;
  end;
  Pb c= bool X by A13,XBOOLE_1:1;
  then
A27: P c= bool X by A2,XBOOLE_1:8;
  union P c= X
  proof
    let s be set;
    assume s in union P;
    then ex t be set st s in t & t in P by TARSKI:def 4;
    hence thesis by A27;
  end;
  then union P = X by A22,XBOOLE_0:def 10;
  hence Pb \/ {h} is a_partition of X by A27,A14,EQREL_1:def 6;
  thus Pmax is_finer_than (Pb \/ {h})
  proof
    let x be set such that
A28: x in Pmax;
    per cases;
    suppose
      x c= h;
      hence thesis by A3,A20;
    end;
    suppose
A29:  not x c= h;
      per cases by A5,A28,A29;
      suppose
        h c= x;
        then h = x by A6,A28;
        hence thesis by A3,A20;
      end;
      suppose
        h misses x;
        then x in Pb by A11,A28;
        hence thesis by A12;
      end;
    end;
  end;
  let Pmin be a_partition of X such that
A30: Pmax is_finer_than Pmin;
  let hw be set such that
A31: hw in Pmin and
A32: h c= hw;
  let s be set such that
A33: s in P;
  per cases by A33,XBOOLE_0:def 3;
  suppose
    s in Pb;
    then s in Pmax by A11;
    hence thesis by A30,SETFAM_1:def 2;
  end;
  suppose
    s in {h};
    then s = h by TARSKI:def 1;
    hence thesis by A31,A32;
  end;
end;

theorem
  for H being covering T_3 Hierarchy of X st H is lower-bounded & not {}
in H & (for C1 be set st (C1 <> {} & C1 c= PARTITIONS(X) & (for P1,P2 be set st
P1 in C1 & P2 in C1 holds P1 is_finer_than P2 or P2 is_finer_than P1)) holds (
ex PX,PY st (PX in C1 & PY in C1 & for PZ st PZ in C1 holds PZ is_finer_than PY
  & PX is_finer_than PZ))) ex C being Classification of X st union C = H
proof
  let H be covering T_3 Hierarchy of X such that
A1: H is lower-bounded and
A2: not {} in H and
A3: for C1 be set st C1 <> {} & C1 c= PARTITIONS(X) & (for P1,P2 be set
st P1 in C1 & P2 in C1 holds P1 is_finer_than P2 or P2 is_finer_than P1) holds
ex PX,PY st (PX in C1 & PY in C1 & for PZ st PZ in C1 holds PZ is_finer_than PY
  & PX is_finer_than PZ);
  union H = X by ABIAN:4;
  then consider h be set such that
A4: h in H by XBOOLE_0:def 1,ZFMISC_1:2;
  reconsider h as Subset of X by A4;
  consider PX being a_partition of X such that
  h in PX and
A5: PX c= H by A1,A2,A4,Th17;
  set L = {PX};
A6: L c= PARTITIONS(X)
  proof
    let l be set;
    assume l in L;
    then l = PX by TARSKI:def 1;
    hence thesis by PARTIT1:def 3;
  end;
A7: now
    let P1,P2 be set;
    assume that
A8: P1 in L and
A9: P2 in L;
    P1 = PX by A8,TARSKI:def 1;
    hence P1 is_finer_than P2 or P2 is_finer_than P1 by A9,TARSKI:def 1;
  end;
A10: H is hierarchic by Def4;
  defpred X[set] means (for P be set holds P in $1 implies P c= H) & (for P1,
  P2 be set holds P1 in $1 & P2 in $1 implies P1 is_finer_than P2 or P2
  is_finer_than P1);
  consider RL be set such that
A11: for L be set holds L in RL iff L in bool PARTITIONS X & X[L] from
  XBOOLE_0:sch 1;
  for a st a in L holds a c= H by A5,TARSKI:def 1;
  then
A12: L in RL by A11,A6,A7;
  now
    let Z be set such that
A13: Z c= RL and
A14: Z is c=-linear;
    set Y = union Z;
A15: now
      let X1,X2 be set;
      assume that
A16:  X1 in Z and
A17:  X2 in Z;
      X1,X2 are_c=-comparable by A14,A16,A17,ORDINAL1:def 9;
      hence X1 c= X2 or X2 c= X1 by XBOOLE_0:def 9;
    end;
A18: now
      let P1,P2 be set;
      assume that
A19:  P1 in Y and
A20:  P2 in Y;
      consider L1 be set such that
A21:  P1 in L1 and
A22:  L1 in Z by A19,TARSKI:def 4;
      consider L2 be set such that
A23:  P2 in L2 and
A24:  L2 in Z by A20,TARSKI:def 4;
      per cases by A15,A22,A24;
      suppose
        L1 c= L2;
        hence P1 is_finer_than P2 or P2 is_finer_than P1 by A11,A13,A21,A23,
        A24;
      end;
      suppose
        L2 c= L1;
        hence P1 is_finer_than P2 or P2 is_finer_than P1 by A11,A13,A21,A22,
        A23;
      end;
    end;
    take Y;
A25: now
      let P be set;
      assume P in Y;
      then ex L3 be set st P in L3 & L3 in Z by TARSKI:def 4;
      hence P c= H by A11,A13;
    end;
    Y c= PARTITIONS(X)
    proof
      let P be set;
      assume P in Y;
      then consider L4 be set such that
A26:  P in L4 and
A27:  L4 in Z by TARSKI:def 4;
      L4 in bool PARTITIONS(X) by A11,A13,A27;
      hence thesis by A26;
    end;
    hence Y in RL by A11,A25,A18;
    thus for X1 be set st X1 in Z holds X1 c= Y by ZFMISC_1:92;
  end;
  then consider C be set such that
A28: C in RL and
A29: for Z be set st Z in RL & Z <> C holds not C c= Z by A12,
  ORDERS_1:175;
  reconsider C as Subset of PARTITIONS(X) by A11,A28;
A30: C is Classification of X
  proof
    let P1,P2 be a_partition of X such that
A31: P1 in C and
A32: P2 in C;
    thus thesis by A11,A28,A31,A32;
  end;
A33: C <> {} by A12,A29,XBOOLE_1:2;
A34: H c= union C
  proof
    let h be set such that
A35: h in H;
    per cases;
    suppose
A36:  not h in union C;
      defpred X[set] means ex hx be set st (hx in $1 & h c= hx & h <> hx);
      consider Ca be set such that
A37:  for p be set holds p in Ca iff p in C & X[p] from XBOOLE_0:sch
      1;
A38:  Ca c= C
      proof
        let s be set;
        assume s in Ca;
        hence thesis by A37;
      end;
      defpred X[set] means ex hx be set st (hx in $1 & hx c= h & h <> hx);
      consider Cb be set such that
A39:  for p be set holds p in Cb iff p in C & X[p] from XBOOLE_0:sch
      1;
      consider PS be a_partition of X such that
A40:  h in PS and
A41:  PS c= H by A1,A2,A35,Th17;
A42:  h <> {} by A40,EQREL_1:def 6;
A43:  now
        consider t be set such that
A44:    t in h by A42,XBOOLE_0:def 1;
        let p be set;
        assume p in C;
        then p is a_partition of X by PARTIT1:def 3;
        then union p = X by EQREL_1:def 6;
        then consider v be set such that
A45:    t in v and
A46:    v in p by A35,A44,TARSKI:def 4;
        assume
A47:    for hv be set st hv in p holds hv misses h;
        not v misses h by A44,A45,XBOOLE_0:3;
        hence contradiction by A47,A46;
      end;
A48:  C c= Ca \/ Cb
      proof
        let p be set such that
A49:    p in C;
        consider hv be set such that
A50:    hv in p and
A51:    not hv misses h by A43,A49;
A52:    h <> hv by A36,A49,A50,TARSKI:def 4;
A53:    p c= H by A11,A28,A49;
        per cases by A10,A35,A50,A51,A53,Def3;
        suppose
          hv c= h;
          then p in Cb by A39,A49,A50,A52;
          hence thesis by XBOOLE_0:def 3;
        end;
        suppose
          h c= hv;
          then p in Ca by A37,A49,A50,A52;
          hence thesis by XBOOLE_0:def 3;
        end;
      end;
      Cb c= C
      proof
        let s be set;
        assume s in Cb;
        hence thesis by A39;
      end;
      then Ca \/ Cb c= C by A38,XBOOLE_1:8;
      then
A54:  C = Ca \/ Cb by A48,XBOOLE_0:def 10;
      then
A55:  Ca c= C by XBOOLE_1:7;
A56:  now
        let P1,P2 be set such that
A57:    P1 in Ca and
A58:    P2 in Ca;
        P2 in C by A55,A58;
        then
A59:    P2 is a_partition of X by PARTIT1:def 3;
        P1 in C by A55,A57;
        then P1 is a_partition of X by PARTIT1:def 3;
        hence P1 is_finer_than P2 or P2 is_finer_than P1 by A30,A55,A57,A58,
        A59,TAXONOM1:def 1;
      end;
A60:  Cb c= C by A54,XBOOLE_1:7;
A61:  now
        let P1,P2 be set such that
A62:    P1 in Cb and
A63:    P2 in Cb;
        P2 in C by A60,A63;
        then
A64:    P2 is a_partition of X by PARTIT1:def 3;
        P1 in C by A60,A62;
        then P1 is a_partition of X by PARTIT1:def 3;
        hence P1 is_finer_than P2 or P2 is_finer_than P1 by A30,A60,A62,A63,
        A64,TAXONOM1:def 1;
      end;
      now
        per cases;
        suppose
A65:      Cb <> {};
          defpred X[set] means $1 misses h;
A66:      {h} c= H
          proof
            let s be set;
            assume s in {h};
            hence thesis by A35,TARSKI:def 1;
          end;
          consider PX,Pmax be set such that
          PX in Cb and
A67:      Pmax in Cb and
A68:      for PZ st PZ in Cb holds PZ is_finer_than Pmax & PX
          is_finer_than PZ by A3,A60,A61,A65, XBOOLE_1:1;
          consider Pb be set such that
A69:      for x holds x in Pb iff x in Pmax & X[x] from XBOOLE_0:sch
          1;
          set PS1 = Pb \/ {h};
          set C1 = C \/ {PS1};
          Pmax in C by A60,A67;
          then
A70:      Pmax is a_partition of X by PARTIT1:def 3;
A71:      Pmax c= H by A11,A28,A60,A67;
          then
A72:      for a st a in Pmax holds a c= h or h c= a or h misses a by A10,
          A35,Def3;
          Pb c= Pmax
          proof
            let s be set;
            assume s in Pb;
            hence thesis by A69;
          end;
          then Pb c= H by A71,XBOOLE_1:1;
          then
A73:      PS1 c= H by A66,XBOOLE_1:8;
A74:      now
            let P3 be set;
            assume
A75:        P3 in C1;
            per cases by A75,XBOOLE_0:def 3;
            suppose
              P3 in C;
              hence P3 c= H by A11,A28;
            end;
            suppose
              P3 in {PS1};
              hence P3 c= H by A73,TARSKI:def 1;
            end;
          end;
          PS1 in {PS1} by TARSKI:def 1;
          then
A76:      PS1 in C1 by XBOOLE_0:def 3;
          h in {h} by TARSKI:def 1;
          then
A77:      h in PS1 by XBOOLE_0:def 3;
A78:      ex hx be set st hx in Pmax & hx c= h & h <> hx by A39,A67;
          then
A79:      Pmax is_finer_than PS1 by A35,A42,A70,A69,A72,Th19;
A80:      now
            let P3 be set such that
A81:        P3 in C;
            per cases;
            suppose
              Ca = {};
              then P3 is_finer_than Pmax by A48,A68,A81;
              hence PS1 is_finer_than P3 or P3 is_finer_than PS1 by A79,
              SETFAM_1:23;
            end;
            suppose
A82:          Ca <> {};
              now
                per cases by A48,A81,XBOOLE_0:def 3;
                suppose
A83:              P3 in Ca;
                  consider Pmin,PY be set such that
A84:              Pmin in Ca and
                  PY in Ca and
A85:              for PZ st PZ in Ca holds PZ is_finer_than PY &
                  Pmin is_finer_than PZ by A3,A55,A56,A82,XBOOLE_1:1;
A86:              Pmin is_finer_than P3 by A83,A85;
A87:              now
                    consider hx be set such that
A88:                hx in Pmin and
A89:                h c= hx and
A90:                h <> hx by A37,A84;
                    assume Pmin is_finer_than Pmax;
                    then consider hz be set such that
A91:                hz in Pmax and
A92:                hx c= hz by A88,SETFAM_1:def 2;
                    consider hy be set such that
A93:                hy in Pmax and
A94:                hy c= h and
                    h <> hy by A39,A67;
A95:                hy is non empty by A70,A93,EQREL_1:def 6;
                    hy c= hx by A89,A94,XBOOLE_1:1;
                    then hy meets hz by A92,A95,Lm4,XBOOLE_1:1;
                    then hy = hz by A70,A93,A91,EQREL_1:def 6;
                    then hx c= h by A94,A92,XBOOLE_1:1;
                    hence contradiction by A89,A90,XBOOLE_0:def 10;
                  end;
A96:              Pmax in C by A54,A67,XBOOLE_0:def 3;
                  then
A97:              Pmax is a_partition of X by PARTIT1:def 3;
                  Pmin in C by A55,A84;
                  then
A98:              Pmin is a_partition of X by PARTIT1:def 3;
A99:              ex hw be set st hw in Pmin & h c= hw & h <> hw by
                  A37,A84;
A100:             Pmin in C by A54,A84,XBOOLE_0:def 3;
                  then Pmin is a_partition of X by PARTIT1:def 3;
                  then Pmax is_finer_than Pmin by A30,A100,A96,A97,
                  A87,TAXONOM1:def 1;
                  then PS1 is_finer_than Pmin by A35,A42,A70,A69,A72,A78,
                  A98,A99,Th19;
                  hence PS1 is_finer_than P3 or P3 is_finer_than PS1 by
                  A86,SETFAM_1:23;
                end;
                suppose
                  P3 in Cb;
                  then P3 is_finer_than Pmax by A68;
                  hence PS1 is_finer_than P3 or P3 is_finer_than PS1 by A79,
                  SETFAM_1:23;
                end;
              end;
              hence PS1 is_finer_than P3 or P3 is_finer_than PS1;
            end;
          end;
A101:     now
            let P1,P2 be set;
            assume that
A102:       P1 in C1 and
A103:       P2 in C1;
            per cases by A102,XBOOLE_0:def 3;
            suppose
A104:         P1 in C;
              per cases by A103,XBOOLE_0:def 3;
              suppose
A105:           P2 in C;
                then
A106:           P2 is a_partition of X by PARTIT1:def 3;
                P1 is a_partition of X by A104,PARTIT1:def 3;
                hence P1 is_finer_than P2 or P2 is_finer_than P1 by A30,A104,
                A105,A106,TAXONOM1:def 1;
              end;
              suppose
                P2 in {PS1};
                then P2 = PS1 by TARSKI:def 1;
                hence P1 is_finer_than P2 or P2 is_finer_than P1 by A80,A104;
              end;
            end;
            suppose
              P1 in {PS1};
              then
A107:         P1 = PS1 by TARSKI:def 1;
              per cases by A103,XBOOLE_0:def 3;
              suppose
                P2 in C;
                hence P1 is_finer_than P2 or P2 is_finer_than P1 by A80,A107;
              end;
              suppose
                P2 in {PS1};
                hence P1 is_finer_than P2 or P2 is_finer_than P1 by A107,
                TARSKI:def 1;
              end;
            end;
          end;
A108:     PS1 is a_partition of X by A35,A42,A70,A69,A72,A78,Th19;
          {PS1} c= PARTITIONS(X)
          proof
            let s be set;
            assume s in {PS1};
            then s = PS1 by TARSKI:def 1;
            hence thesis by A108,PARTIT1:def 3;
          end;
          then C1 c= PARTITIONS(X) by XBOOLE_1:8;
          then C1 in RL by A11,A74,A101;
          then C = C1 by A29,XBOOLE_1:7;
          hence thesis by A77,A76,TARSKI:def 4;
        end;
        suppose
A109:     Cb = {};
          then consider Pmin,PY be set such that
A110:     Pmin in Ca and
          PY in Ca and
A111:     for PZ st PZ in Ca holds PZ is_finer_than PY & Pmin
          is_finer_than PZ by A3,A33,A54,A56;
          consider hw be set such that
A112:     hw in Pmin and
A113:     h c= hw and
          h <> hw by A37,A110;
          defpred X[set] means $1 c= hw;
          consider PT be set such that
A114:     for a holds a in PT iff a in PS & X[a] from XBOOLE_0:sch 1;
          set PS1 = PT \/ (Pmin \ {hw});
          set C1 = C \/ {PS1};
          PT c= PS
          proof
            let s be set;
            assume s in PT;
            hence thesis by A114;
          end;
          then
A115:     PT c= H by A41,XBOOLE_1:1;
A116:     Pmin c= H by A11,A28,A55,A110;
          then Pmin \ {hw} c= H by XBOOLE_1:1;
          then
A117:     PS1 c= H by A115,XBOOLE_1:8;
A118:     now
            let P3 be set;
            assume
A119:       P3 in C1;
            per cases by A119,XBOOLE_0:def 3;
            suppose
              P3 in C;
              hence P3 c= H by A11,A28;
            end;
            suppose
              P3 in {PS1};
              hence P3 c= H by A117,TARSKI:def 1;
            end;
          end;
          Pmin in C by A55,A110;
          then
A120:     Pmin is a_partition of X by PARTIT1:def 3;
A121:     for a st a in PS holds a c= hw or hw c= a or hw misses a by
          A10
          ,A41,A116,A112,Def3;
          then
A122:     PS1 is_finer_than Pmin by A40,A42,A120,A112,A113,A114,
          Th18;
A123:     now
            let P3 be set;
            assume P3 in C;
            then Pmin is_finer_than P3 by A48,A109,A111;
            hence PS1 is_finer_than P3 or P3 is_finer_than PS1 by A122,
            SETFAM_1:23;
          end;
A124:     now
            let P1,P2 be set;
            assume that
A125:       P1 in C1 and
A126:       P2 in C1;
            per cases by A125,XBOOLE_0:def 3;
            suppose
A127:         P1 in C;
              per cases by A126,XBOOLE_0:def 3;
              suppose
A128:           P2 in C;
                then
A129:           P2 is a_partition of X by PARTIT1:def 3;
                P1 is a_partition of X by A127,PARTIT1:def 3;
                hence P1 is_finer_than P2 or P2 is_finer_than P1 by A30,A127,
                A128,A129,TAXONOM1:def 1;
              end;
              suppose
                P2 in {PS1};
                then P2 = PS1 by TARSKI:def 1;
                hence P1 is_finer_than P2 or P2 is_finer_than P1 by A123,A127;
              end;
            end;
            suppose
              P1 in {PS1};
              then
A130:         P1 = PS1 by TARSKI:def 1;
              per cases by A126,XBOOLE_0:def 3;
              suppose
                P2 in C;
                hence P1 is_finer_than P2 or P2 is_finer_than P1 by A123,A130;
              end;
              suppose
                P2 in {PS1};
                hence P1 is_finer_than P2 or P2 is_finer_than P1 by A130,
                TARSKI:def 1;
              end;
            end;
          end;
A131:     PS1 is a_partition of X by A40,A42,A120,A112,A113,A114,A121,
          Th18;
          {PS1} c= PARTITIONS(X)
          proof
            let s be set;
            assume s in {PS1};
            then s = PS1 by TARSKI:def 1;
            hence thesis by A131,PARTIT1:def 3;
          end;
          then C1 c= PARTITIONS(X) by XBOOLE_1:8;
          then C1 in RL by A11,A118,A124;
          then
A132:     C = C1 by A29,XBOOLE_1:7;
A133:     PT c= PS1 by XBOOLE_1:7;
A134:     PS1 in {PS1} by TARSKI:def 1;
A135:     {PS1} c= C1 by XBOOLE_1:7;
          h in PT by A40,A113,A114;
          hence thesis by A133,A135,A134,A132,TARSKI:def 4;
        end;
      end;
      hence thesis;
    end;
    suppose
      h in union C;
      hence thesis;
    end;
  end;
  take C;
  union C c= H
  proof
    let h be set;
    assume h in union C;
    then consider P be set such that
A136: h in P and
A137: P in C by TARSKI:def 4;
    P c= H by A11,A28,A137;
    hence thesis by A136;
  end;
  hence thesis by A30,A34,XBOOLE_0:def 10;
end;
