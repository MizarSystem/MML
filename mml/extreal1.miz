:: Basic Properties of Extended Real Numbers
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received September 7, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies SUPINF_1, MEASURE6, ARYTM_3, ARYTM_1, RLVECT_1, ARYTM, COMPLEX1;
 notations TARSKI, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, REAL_1,
      SUPINF_1, SUPINF_2, MEASURE6;
 constructors REAL_1, SUPINF_2, MEASURE6, VALUED_1, PARTFUN1, SEQ_1;
 registrations NUMBERS, XXREAL_0, XREAL_0, MEMBERED, XBOOLE_0, VALUED_1,
      FUNCT_2;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions VALUED_1;
 theorems TARSKI, SUPINF_1, MEASURE5, MEASURE6, REAL_1, REAL_2, SUPINF_2,
      MEASURE3, XBOOLE_0, XCMPLX_1, XREAL_1, XXREAL_0;

begin

 reserve x,y,z for R_eal;
 reserve a for Real;

theorem
   x <> +infty & x <> -infty implies x is Real by XXREAL_0:14;

canceled 2;

theorem Th4:
  (x = +infty iff -x = -infty) & (x = -infty iff -x = +infty)
proof
    -x = +infty implies x = -infty
   proof
    assume -x = +infty;
    then --x = -infty by SUPINF_2:def 3;
    hence thesis;
   end;
   hence thesis by SUPINF_2:4;
end;

theorem Th5:
  x - -y = x + y
proof
  thus x - -y = x + - -y by SUPINF_2:def 4
             .= x + y;
end;

canceled;

theorem Th7:
  x <> -infty & y <> +infty & x <= y implies x <> +infty & y <> -infty
proof
   assume A1:x <> -infty & y <> +infty & x <= y;
   assume A2:not (x <> +infty & y <> -infty);
    now per cases by A2;
   suppose x = +infty;
    hence contradiction by A1,XXREAL_0:4;
    end;
    suppose y = -infty;
    hence contradiction by A1,XXREAL_0:6;
   end;
   end;
   hence thesis;
end;

Lm1:
  x in REAL & y in REAL implies x + y in REAL
  proof
    assume x in REAL & y in REAL;
    then consider a,b being Real such that
A1: x = a & y = b & x + y = a + b by SUPINF_2:def 2;
    thus thesis by A1;
  end;

theorem Th8:
  not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
  not ((y = +infty & z = -infty) or (y = -infty & z = +infty)) &
  not ((x = +infty & z = -infty) or (x = -infty & z = +infty)) implies
  x + y + z = x + (y + z)
proof
   assume that
A1:not (x = +infty & y = -infty or x = -infty & y = +infty) and
A2:not ((y = +infty & z = -infty) or (y = -infty & z = +infty)) and
A3:not ((x = +infty & z = -infty) or (x = -infty & z = +infty));
       now per cases by A1,A2,A3,SUPINF_2:2;
     suppose x in REAL & y in REAL & z in REAL;
     then reconsider A = x, B = y, C = z as Real;
      A + B = x + y & B + C = y + z by SUPINF_2:1;
     then A + B + C = x + y + z & A + (B + C) = x + (y + z) by SUPINF_2:1;
     hence thesis;
     end;
     suppose A4: x in REAL & y = +infty & z in REAL;
     then x + y = +infty & y + z = +infty by SUPINF_1:6,SUPINF_2:def 2;
     hence thesis by A4;
     end;
     suppose A5: x in REAL & y = -infty & z in REAL;
     then x + y = -infty & y + z = -infty by SUPINF_1:1,SUPINF_2:def 2;
     hence thesis by A5;
     end;
     suppose A6: x = -infty & y in REAL & z in REAL;
then x + y = -infty by SUPINF_1:1,SUPINF_2:def 2;
then A7:   x + y + z = -infty by A6,SUPINF_1:1,SUPINF_2:def 2;
         ex a,b being Real st y = a & z = b & y + z = a + b
       by A6,SUPINF_2:def 2;
     hence thesis by A6,A7,SUPINF_2:def 2;
     end;
     suppose A8: x = +infty & y in REAL & z in REAL;
then x + y = +infty by SUPINF_1:6,SUPINF_2:def 2;
then A9:   x + y + z = +infty by A8,SUPINF_1:6,SUPINF_2:def 2;
         ex a,b being Real st y = a & z = b & y + z = a + b
       by A8,SUPINF_2:def 2;
     hence thesis by A8,A9,SUPINF_2:def 2;
     end;
     suppose A10: x in REAL & y in REAL & z = +infty;
then y + z = +infty by SUPINF_1:6,SUPINF_2:def 2;
then A11:   x + (y + z) = +infty by A10,SUPINF_1:6,SUPINF_2:def 2;
         x + y in REAL by A10,Lm1;
     hence thesis by A10,A11,SUPINF_1:6,SUPINF_2:def 2;
     end;
     suppose A12: x in REAL & y in REAL & z = -infty;
then y + z = -infty by SUPINF_1:1,SUPINF_2:def 2;
then A13:   x + (y + z) = -infty by A12,SUPINF_1:1,SUPINF_2:def 2;
         x + y in REAL by A12,Lm1;
     hence thesis by A12,A13,SUPINF_1:1,SUPINF_2:def 2;
     end;
     suppose A14: x = +infty & y = +infty & z in REAL;
then x + y = +infty by SUPINF_2:def 2;
then A15:   x + y + z = +infty by A14,SUPINF_1:6,SUPINF_2:def 2;
         y + z <> -infty by A14,SUPINF_1:6,SUPINF_2:def 2;
     hence thesis by A14,A15,SUPINF_2:def 2;
     end;
     suppose A16: x in REAL & y = -infty & z = -infty;
then A17:   x + y = -infty by SUPINF_1:1,SUPINF_2:def 2;
     then x + y + z = -infty by A16,SUPINF_2:def 2;
     hence thesis by A16,A17;
     end;
     suppose A18: x = -infty & y = -infty & z in REAL;
then A19:   x + y = -infty by SUPINF_2:def 2;
     then x + y + z = -infty by A18,SUPINF_1:1,SUPINF_2:def 2;
     hence thesis by A18,A19;
     end;
     suppose x = -infty & y in REAL & z = -infty or
             x = -infty & y = -infty & z = -infty or
             x = +infty & y in REAL & z = +infty or
             x = +infty & y = +infty & z = +infty;
     hence thesis;
     end;
     suppose A20: x in REAL & y = +infty & z = +infty;
     then x + y = +infty & y + z = +infty by SUPINF_1:6,SUPINF_2:def 2;
     hence thesis by A20;
   end;
   end;
   hence thesis;
end;

theorem Th9:
  x + -x = 0.
proof
   per cases by SUPINF_2:2;
   suppose x in REAL;
   then consider a being Real such that
A1:x = a & -x = -a by SUPINF_2:def 3;
       x + -x = a + -a & -x + x = -a + a by A1,SUPINF_2:1;
   hence thesis;
   end;
   suppose x = -infty;
   hence thesis by SUPINF_1:6,SUPINF_2:4,def 2;
   end;
   suppose x = +infty;
   hence thesis by SUPINF_1:6,SUPINF_2:4,def 2;
end;
end;

canceled;

theorem
   not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
  not (y = +infty & z = +infty) & not (y = -infty & z = -infty) &
  not (x = +infty & z = +infty) & not (x = -infty & z = -infty) implies
  x + y - z = x + (y - z)
proof
  assume A1: not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
  not (y = +infty & z = +infty) & not (y = -infty & z = -infty) &
  not (x = +infty & z = +infty) & not (x = -infty & z = -infty);
then A2:not (y = +infty & -z = -infty) & not (y = -infty & -z = +infty) &
  not (x = +infty & -z = -infty) & not (x = -infty & -z = +infty) by Th4;
  thus x + y - z = x + y + -z by SUPINF_2:def 4
         .= x + (y + -z) by A1,A2,Th8
         .= x + (y - z) by SUPINF_2:def 4;
end;

begin  :: Operations "x * y","x / y", "|.x.|" in R_eal numbers

definition
  let x,y be R_eal;
func x * y -> R_eal means :Def1:
 (ex a,b being Real st (x = a & y = b & it = a * b)) or
 (((0. < x & y=+infty) or (0. < y & x=+infty) or (x < 0. & y=-infty) or
 (y < 0. & x = -infty)) & it = +infty) or
 (((x < 0. & y=+infty) or (y < 0. & x=+infty) or (0. < x & y=-infty) or
 (0. < y & x = -infty)) & it = -infty) or
 ((x = 0. or y = 0.) & it = 0.);
existence
proof
       (x in REAL or x in {-infty,+infty}) & (y in REAL or y in {-infty,+infty}
)
   by XBOOLE_0:def 2,XXREAL_0:def 4;
then A1:(x in REAL & y in REAL) or (0. < x & y = +infty) or
   (0. < y & x = +infty) or (x < 0. & y = +infty) or
   (y < 0. & x = +infty) or (0. < x & y = -infty) or
   (0. < y & x = -infty) or (x < 0. & y = -infty) or
   (y < 0. & x = -infty) or (x = 0.) or (y = 0.)
   by TARSKI:def 2;
       ex Z being R_eal st
   ((ex a,b being Real st (x = a & y = b & Z = a * b)) or
   (((0. < x & y = +infty) or (0. < y & x = +infty)) & Z = +infty) or
   (((x < 0. & y = +infty) or (y < 0. & x = +infty)) & Z = -infty) or
   (((0. < x & y = -infty) or (0. < y & x = -infty)) & Z = -infty) or
   (((x < 0. & y = -infty) or (y < 0. & x = -infty)) & Z = +infty) or
   ((x = 0. or y = 0.) & Z = 0.))
   proof
       x in REAL & y in REAL implies ex Z being R_eal st
    (ex a,b being Real st (x = a & y = b & Z = a * b))
    proof
     assume x in REAL & y in REAL;
     then reconsider x,y as Real;
     reconsider Z = x * y as R_eal by XXREAL_0:def 1;
     take Z;
     thus thesis;
    end;
    hence thesis by A1;
   end;
   hence thesis;
end;
uniqueness;
end;

canceled;

theorem Th13:
for x,y being R_eal holds
 for a,b being Real holds
 (x = a & y = b) implies x * y = a * b
proof
   let x,y be R_eal;
    let a,b be Real;
    assume A1:x = a & y = b;
        now per cases by A1,Def1;
     suppose (ex a,b being Real st (x = a & y = b & x * y = a * b));
     then consider a1,b1 being Real such that
A2:  x = a1 & y = b1 & x * y = a1 * b1;
     thus thesis by A1,A2;
     end;
     suppose ((x = 0. or y = 0.) & x * y = 0.);
     hence thesis by A1;
   end;
   end;
   hence thesis;
end;

theorem Th14:
  (0. <= x & 0. < y) or (0. < x & 0. <= y) implies 0. < x + y
proof
   assume A1:(0. <= x & 0. < y) or (0. < x & 0. <= y);
   per cases;
    suppose x = +infty;
    hence thesis by A1,SUPINF_2:def 2;
    end;
    suppose x <> +infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
        now per cases;
     suppose y = +infty;
     hence thesis by A1,SUPINF_2:def 2;
     end;
     suppose y <> +infty;
     then reconsider b = y as Real by A1,XXREAL_0:14;
         (0 <= a & 0 < b) or (0 < a & 0 <= b)
     proof
          now per cases by A1;
       suppose 0. <= x & 0. < y;
       hence thesis;
       end;
       suppose 0. < x & 0. <= y;
       hence thesis;
      end;
      end;
      hence thesis;
     end;
     then 0 + 0 < a + b by XREAL_1:10;
     hence thesis by SUPINF_2:1;
    end;
    end;
    hence thesis;
end;
end;

theorem Th15:
  (x <= 0. & y < 0.) or (x < 0. & y <= 0.) implies x + y < 0.
proof
   assume A1:(x <= 0. & y < 0.) or (x < 0. & y <= 0.);
   per cases;
    suppose x = -infty;
    hence thesis by A1,SUPINF_2:def 2;
    end;
    suppose x <> -infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
     now per cases;
     suppose y = -infty;
     hence thesis by A1,SUPINF_2:def 2;
     end;
     suppose y <> -infty;
     then reconsider b = y as Real by A1,XXREAL_0:14;
         (a <= 0 & b < 0) or (a < 0 & b <= 0)
     proof
          now per cases by A1;
       suppose x <= 0. & y < 0.;
       hence thesis;
       end;
       suppose x < 0. & y <= 0.;
       hence thesis;
      end;
      end;
      hence thesis;
     end;
     then a + b < 0 + 0 by XREAL_1:10;
     hence thesis by SUPINF_2:1;
    end;
    end;
    hence thesis;
end;
end;

theorem
  for x be R_eal st x in REAL holds
    (-infty < x & x < 0.) or x = 0. or (0. < x & x < +infty)
   by XXREAL_0:9,12;

theorem Th17:
  for x,y being R_eal holds x * y = y * x
proof
   let x,y be R_eal;
       now per cases by Def1;
    suppose
        ex a,b being Real st (x = a & y = b & x * y = a * b);
    then consider a1,b1 being Real such that
A1: x = a1 & y = b1 & x * y = a1 * b1;
    thus thesis by A1,Def1;
    end;
    suppose
        ((0. < x & y=+infty) or (0. < y & x=+infty) or (x < 0. & y=-infty) or
    (y < 0. & x = -infty)) & x * y = +infty;
    hence thesis by Def1;
    end;
    suppose
        ((x < 0. & y=+infty) or (y < 0. & x=+infty) or (0. < x & y=-infty) or
    (0. < y & x = -infty)) & x * y = -infty;
    hence thesis by Def1;
    end;
    suppose (x = 0. or y = 0.) & x * y = 0.;
    hence thesis by Def1;
   end;
   end;
   hence thesis;
end;

definition let x,y be R_eal;
  redefine func x*y;
  commutativity by Th17;
end;

canceled 2;

theorem Th20:
  (0. < x & 0. < y) or (x < 0. & y < 0.) implies 0. < x * y
proof
   assume A1:(0. < x & 0. < y) or (x < 0. & y < 0.);
       now per cases by A1;
    suppose A2:0. < x & 0. < y;
        now per cases by A2,Def1;
     suppose ex a,b being Real st (x = a & y = b & x * y = a * b);
     then consider a,b being Real such that
A3:  x = a & y = b & x * y = a * b;
     thus thesis by A2,A3,REAL_2:122;
     end;
     suppose ((0. < x & y=+infty) or (0. < y & x=+infty)) & x * y = +infty;
     hence thesis;
    end;
    end;
    hence thesis;
    end;
    suppose A4:x < 0. & y < 0.;
        now per cases by A4,Def1;
     suppose ex a,b being Real st (x = a & y = b & x * y = a * b);
     then consider a,b being Real such that
A5:  x = a & y = b & x * y = a * b;
     thus thesis by A4,A5,REAL_2:122;
     end;
     suppose ((x < 0. & y=-infty) or (y < 0. & x=-infty)) & x * y = +infty;
     hence thesis;
    end;
    end;
    hence thesis;
   end;
   end;
   hence thesis;
end;

theorem Th21:
  (0. < x & y < 0.) or (x < 0. & 0. < y) implies x * y < 0.
proof
   assume A1:(0. < x & y < 0.) or (x < 0. & 0. < y);
       now per cases by A1;
    suppose A2:0. < x & y < 0.;
        now per cases by A2,Def1;
     suppose ex a,b being Real st (x = a & y = b & x * y = a * b);
     then consider a,b being Real such that
A3:  x = a & y = b & x * y = a * b;
     thus thesis by A2,A3,XREAL_1:134;
     end;
     suppose ((0. < x & y=-infty) or (y < 0. & x=+infty)) & x * y = -infty;
     hence thesis;
    end;
    end;
    hence thesis;
    end;
    suppose A4:x < 0. & 0. < y;
        now per cases by A4,Def1;
     suppose ex a,b being Real st (x = a & y = b & x * y = a * b);
     then consider a,b being Real such that
A5:  x = a & y = b & x * y = a * b;
     thus thesis by A4,A5,XREAL_1:134;
     end;
     suppose ((x < 0. & y=+infty) or (0. < y & x=-infty)) & x * y = -infty;
     hence thesis;
    end;
    end;
    hence thesis;
   end;
   end;
   hence thesis;
end;

theorem
   x * y = 0. iff (x = 0. or y = 0.)
proof
    x * y = 0. implies (x = 0. or y = 0.)
   proof
    assume A1:x * y = 0.;
    assume A2:x <> 0. & y <> 0.;
        now per cases by A2;
     suppose (x < 0. & y < 0.) or (0. < x & 0. < y);
     hence contradiction by A1,Th20;
     end;
     suppose (x < 0. & 0. < y) or (0. < x & y < 0.);
     hence contradiction by A1,Th21;
    end;
    end;
    hence thesis;
   end;
   hence thesis by Def1;
end;

theorem
   x*y*z = x*(y*z)
proof
       now per cases by Def1;
    suppose ex a,b being Real st (x = a & y = b & x * y = a * b);
    then consider a,b being Real such that
A1: x = a & y = b & x * y = a * b;
        now per cases by A1,Def1;
     suppose ex b1,c being Real st (y = b1 & z = c & y * z = b1 * c);
     then consider b1,c being Real such that
A2:  y = b1 & z = c & y * z = b1 * c;
A3:  x * y * z = a * b * c by A1,A2,Th13;
         x * (y * z) = a * (b * c) by A1,A2,Th13;
     hence thesis by A3;
     end;
     suppose A4:0. < y & z=+infty & y * z = +infty;
         now per cases;
      suppose A5:a > 0;
      then a * b > 0 by A1,A4,REAL_2:122;
then  x * y * z = +infty by A1,A4,Def1;
      hence thesis by A1,A4,A5,Def1;
      end;
      suppose A6:a = 0;
      then x * y * z = 0. by A1,Def1;
      hence thesis by A1,A6,Def1;
      end;
      suppose A7:a < 0;
      then a * b < 0 by A1,A4,XREAL_1:134;
then  x * y * z = -infty by A1,A4,Def1;
      hence thesis by A1,A4,A7,Def1;
     end;
     end;
     hence thesis;
     end;
     suppose A8:y < 0. & z=-infty & y * z = +infty;
         now per cases;
      suppose A9:a > 0;
      then a * b < 0 by A1,A8,XREAL_1:134;
then  x * y * z = +infty by A1,A8,Def1;
      hence thesis by A1,A8,A9,Def1;
      end;
      suppose A10:a = 0;
      then x * y * z = 0. by A1,Def1;
      hence thesis by A1,A10,Def1;
      end;
      suppose A11:a < 0;
      then a * b > 0 by A1,A8,REAL_2:122;
then  x * y * z = -infty by A1,A8,Def1;
      hence thesis by A1,A8,A11,Def1;
     end;
     end;
     hence thesis;
     end;
     suppose A12:y < 0. & z=+infty & y * z = -infty;
         now per cases;
      suppose A13:a > 0;
      then a * b < 0 by A1,A12,XREAL_1:134;
then  x * y * z = -infty by A1,A12,Def1;
      hence thesis by A1,A12,A13,Def1;
      end;
      suppose A14:a = 0;
      then x * y * z = 0. by A1,Def1;
      hence thesis by A1,A14,Def1;
      end;
      suppose A15:a < 0;
      then a * b > 0 by A1,A12,REAL_2:122;
then  x * y * z = +infty by A1,A12,Def1;
      hence thesis by A1,A12,A15,Def1;
     end;
     end;
     hence thesis;
     end;
     suppose A16:0. < y & z=-infty & y * z = -infty;
         now per cases;
      suppose A17:a > 0;
      then a * b > 0 by A1,A16,REAL_2:122;
then  x * y * z = -infty by A1,A16,Def1;
      hence thesis by A1,A16,A17,Def1;
      end;
      suppose A18:a = 0;
      then x * y * z = 0. by A1,Def1;
      hence thesis by A1,A18,Def1;
      end;
      suppose A19:a < 0;
      then a * b < 0 by A1,A16,XREAL_1:134;
then  x * y * z = +infty by A1,A16,Def1;
      hence thesis by A1,A16,A19,Def1;
     end;
     end;
     hence thesis;
     end;
     suppose A20:y = 0. & y * z = 0.;
     then x * y * z = 0. by Def1;
     hence thesis by A20,Def1;
     end;
     suppose A21:z = 0. & y * z = 0.;
     then x * y * z = 0. by Def1;
     hence thesis by A21,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A22:0. < x & y=+infty & x * y = +infty;
        now per cases;
     suppose 0. < z;
     then y * z = +infty by A22,Def1;
     hence thesis by A22;
     end;
     suppose A23:0. = z;
then A24: x * y * z = 0. by Def1;
         y * z = 0. by A23,Def1;
     hence thesis by A24,Def1;
     end;
     suppose z < 0.;
     then y * z = -infty by A22,Def1;
     hence thesis by A22,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A25:0. < y & x=+infty & x * y = +infty;
        now per cases;
     suppose A26:0. < z;
then A27: x * y * z = +infty by A25,Def1;
         0. < y * z by A25,A26,Th20;
     hence thesis by A25,A27,Def1;
     end;
     suppose A28:0. = z;
then A29: x * y * z = 0. by Def1;
         y * z = 0. by A28,Def1;
     hence thesis by A29,Def1;
     end;
     suppose A30:z < 0.;
then A31: x * y * z = -infty by A25,Def1;
         y * z < 0. by A25,A30,Th21;
     hence thesis by A25,A31,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A32:x < 0. & y = -infty & x * y = +infty;
        now per cases;
     suppose A33:0. < z;
     then x * y * z = +infty by A32,Def1;
     hence thesis by A32,A33,Def1;
     end;
     suppose A34:0. = z;
then A35: x * y * z = 0. by Def1;
         y * z = 0. by A34,Def1;
     hence thesis by A35,Def1;
     end;
     suppose A36:z < 0.;
then A37: x * y * z = -infty by A32,Def1;
         y * z = +infty by A32,A36,Def1;
     hence thesis by A32,A37,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A38:y < 0. & x = -infty & x * y = +infty;
        now per cases;
     suppose A39:0. < z;
then A40: x * y * z = +infty by A38,Def1;
         y * z < 0. by A38,A39,Th21;
     hence thesis by A38,A40,Def1;
     end;
     suppose A41:0. = z;
then A42: x * y * z = 0. by Def1;
         y * z = 0. by A41,Def1;
     hence thesis by A42,Def1;
     end;
     suppose A43:z < 0.;
then A44: x * y * z = -infty by A38,Def1;
         0. < y * z by A38,A43,Th20;
     hence thesis by A38,A44,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A45:x < 0. & y=+infty & x * y = -infty;
        now per cases;
     suppose A46:0. < z;
     then y * z = +infty by A45,Def1;
     hence thesis by A45,A46,Def1;
     end;
     suppose A47:0. = z;
then A48: x * y * z = 0. by Def1;
         y * z = 0. by A47,Def1;
     hence thesis by A48,Def1;
     end;
     suppose A49:z < 0.;
then A50: x * y * z = +infty by A45,Def1;
         y * z = -infty by A45,A49,Def1;
     hence thesis by A45,A50,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A51:y < 0. & x=+infty & x * y = -infty;
        now per cases;
     suppose A52:0. < z;
then A53: x * y * z = -infty by A51,Def1;
         y * z < 0. by A51,A52,Th21;
     hence thesis by A51,A53,Def1;
     end;
     suppose A54:0. = z;
then A55: x * y * z = 0. by Def1;
         y * z = 0. by A54,Def1;
     hence thesis by A55,Def1;
     end;
     suppose A56:z < 0.;
then A57: x * y * z = +infty by A51,Def1;
         0. < y * z by A51,A56,Th20;
     hence thesis by A51,A57,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A58:0. < x & y = -infty & x * y = -infty;
        now per cases;
     suppose 0. < z;
     then y * z = -infty by A58,Def1;
     hence thesis by A58;
     end;
     suppose A59:0. = z;
then A60: x * y * z = 0. by Def1;
         y * z = 0. by A59,Def1;
     hence thesis by A60,Def1;
     end;
     suppose z < 0.;
     then y * z = +infty by A58,Def1;
     hence thesis by A58,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A61:0. < y & x = -infty & x * y = -infty;
        now per cases;
     suppose A62:0. < z;
then A63: x * y * z = -infty by A61,Def1;
         0. < y * z by A61,A62,Th20;
     hence thesis by A61,A63,Def1;
     end;
     suppose A64:0. = z;
then A65: x * y * z = 0. by Def1;
         y * z = 0. by A64,Def1;
     hence thesis by A65,Def1;
     end;
     suppose A66:z < 0.;
then A67: x * y * z = +infty by A61,Def1;
         y * z < 0. by A61,A66,Th21;
     hence thesis by A61,A67,Def1;
    end;
    end;
    hence thesis;
    end;
    suppose A68:x = 0. & x * y = 0.;
    then x * y * z = 0. by Def1;
    hence thesis by A68,Def1;
    end;
    suppose A69:y = 0. & x * y = 0.;
    then x * y * z = 0. by Def1;
    hence thesis by A69;
   end;
   end;
   hence thesis;
end;

theorem Th24:
-0. = 0.
proof 0 = -0;
 hence thesis by SUPINF_2:3;
end;

theorem
    (0. < x iff -x < 0.) & (x < 0. iff 0. < -x) by Th24,SUPINF_2:17;

theorem Th26:
  -(x * y) = x * (-y) & -(x * y) = (-x) * y
proof
       now per cases by Def1;
    suppose ex a,b being Real st (x = a & y = b & x * y = a * b);
    then consider a,b being Real such that
A1: x = a & y = b & x * y = a * b;
A2: -x = -a & -y = -b & -(x * y) = -(a * b) by A1,SUPINF_2:3;
    then x * (-y) = a * (-b) & (-x) * y = (-a) * b by A1,Th13;
    hence thesis by A2;
    end;
    suppose A3:((0. < x & y=+infty) or (0. < y & x=+infty) or
    (x < 0. & y=-infty) or (y < 0. & x = -infty)) & x * y = +infty;
then A4: -(x * y) = -infty by Th4;
A5: ((-x < 0. & y=+infty) or (0. < y & -x=-infty) or
    (0.<-x & y=-infty) or (y<0. & -x=+infty))
    by A3,Th4,Th24,SUPINF_2:17;
        ((0. < x & -y=-infty) or (-y < 0. & x=+infty) or
    (x<0. & -y=+infty) or (0.<-y & x=-infty))
    by A3,Th4,Th24,SUPINF_2:17;
    hence thesis by A4,A5,Def1;
    end;
    suppose A6:((x < 0. & y=+infty) or (y < 0. & x=+infty) or
    (0. < x & y=-infty) or (0. < y & x = -infty)) & x * y = -infty;
then A7: -(x * y) = +infty by Th4;
A8: ((0. < -x & y=+infty) or (y < 0. & -x=-infty) or
    (-x < 0. & y=-infty) or (0. < y & -x=+infty))
    by A6,Th4,Th24,SUPINF_2:17;
        ((x < 0. & -y=-infty) or (0. < -y & x=+infty) or
    (0. < x & -y=+infty) or (-y < 0. & x=-infty))
    by A6,Th4,Th24,SUPINF_2:17;
    hence thesis by A7,A8,Def1;
    end;
    suppose (x = 0. or y = 0.) & x * y = 0.;
    hence thesis by Def1,Th24;
   end;
   end;
   hence thesis;
end;

theorem
      x <> +infty & x <> -infty & x * y = +infty implies y = +infty or y =
-infty
proof
   assume A1:x <> +infty & x <> -infty & x * y = +infty;
   assume A2:y <> +infty & y <> -infty;
   reconsider a=x as Real by A1,XXREAL_0:14;
   reconsider b=y as Real by A2,XXREAL_0:14;
       x * y = a * b by Th13;
   hence contradiction by A1;
end;

theorem
   x <> +infty & x <> -infty & x * y = -infty implies y = +infty or y =
-infty
proof
   assume A1:x <> +infty & x <> -infty & x * y = -infty;
   assume A2:y <> +infty & y <> -infty;
   reconsider a=x as Real by A1,XXREAL_0:14;
   reconsider b=y as Real by A2,XXREAL_0:14;
       x * y = a * b by Th13;
   hence contradiction by A1;
end;

Lm2:
  x <> +infty & x <> -infty implies x * (y + z) = x * y + x * z
proof
   assume x <> +infty & x <> -infty;
   then reconsider a = x as Real by XXREAL_0:14;
   per cases by SUPINF_2:def 2;
    suppose A1: y + z = 0.;
     y = -z
    proof
      assume A2: y <> -z;
then A3:   -y <> z;
      per cases by SUPINF_2:2;
      suppose A4: y in REAL;
       z in REAL
      proof
        assume A5: not z in REAL;
        per cases by A5,SUPINF_2:2;
        suppose z = -infty;
        hence thesis by A1,A4,SUPINF_1:1,SUPINF_2:def 2;
        end;
        suppose z = +infty;
        hence thesis by A1,A4,SUPINF_1:6,SUPINF_2:def 2;
      end;
      end;
      then consider a,b being Real such that
A6:    y = a & z = b & y + z = a + b by A4,SUPINF_2:def 2;
      consider a' being Real such that
A7:   y = a' & -y = -a' by A4,SUPINF_2:def 3;
      thus thesis by A1,A2,A6,A7;
      end;
      suppose y = -infty;
      hence thesis by A1,A3,SUPINF_2:4,def 2;
      end;
      suppose A8: y = +infty;
      then z <> -infty by A3,SUPINF_2:def 3;
      hence thesis by A1,A8,SUPINF_2:def 2;
    end;
    end;
    then x * y + x * z = -(x * z) + x * z by Th26
                 .= 0. by Th9;
    hence thesis by A1,Def1;
    end;
    suppose y in REAL & z in REAL;
    then consider b,c being Real such that
A9: y = b & z = c & y + z = b + c by SUPINF_2:def 2;
A10: x * (y + z) = a * (b + c) by A9,Th13 .= a * b + a * c;
        x * y = a * b & x * z = a * c by A9,Th13;
    hence thesis by A10,SUPINF_2:1;
    end;
    suppose A11:y = +infty & z <> -infty;
        now per cases;
     suppose A12: a < 0;
then A13:  x * y = -infty by A11,Def1;
         now per cases;
      suppose z = +infty;
      then x * z = -infty by A12,Def1;
      then x * y + x * z = -infty by A13,SUPINF_2:def 2;
      hence thesis by A11,A13,SUPINF_2:def 2;
      end;
      suppose z <> +infty;
      then reconsider c = z as Real by A11,XXREAL_0:14;
         A14: x * z = a * c by Th13;
          y + z = +infty by A11,SUPINF_2:def 2;
      then x * (y + z) = -infty by A12,Def1;
      hence thesis by A13,A14,SUPINF_2:def 2;
     end;
     end;
     hence thesis;
     end;
     suppose a = 0;
     then x * (y + z) = 0. & x * y = 0. & x * z = 0. by Def1;
     hence thesis by SUPINF_2:18;
     end;
     suppose A15: a > 0;
then A16:  x * y = +infty by A11,Def1;
         now per cases;
      suppose z = +infty;
      then x * z = +infty by A15,Def1;
      then x * y + x * z = +infty by A16,SUPINF_2:def 2;
      hence thesis by A11,A16,SUPINF_2:def 2;
      end;
      suppose z <> +infty;
      then reconsider c = z as Real by A11,XXREAL_0:14;
         A17: x * z = a * c by Th13;
          y + z = +infty by A11,SUPINF_2:def 2;
      then x * (y + z) = +infty by A15,Def1;
      hence thesis by A16,A17,SUPINF_2:def 2;
     end;
     end;
     hence thesis;
    end;
    end;
    hence thesis;
    end;
    suppose A18:y = -infty & z <> +infty;
        now per cases;
     suppose A19: a < 0;
then A20:  x * y = +infty by A18,Def1;
         now per cases;
      suppose z = -infty;
      then x * z = +infty by A19,Def1;
      then x * y + x * z = +infty by A20,SUPINF_2:def 2;
      hence thesis by A18,A20,SUPINF_2:def 2;
      end;
      suppose z <> -infty;
      then reconsider c = z as Real by A18,XXREAL_0:14;
         A21: x * z = a * c by Th13;
          y + z = -infty by A18,SUPINF_2:def 2;
      then x * (y + z) = +infty by A19,Def1;
      hence thesis by A20,A21,SUPINF_2:def 2;
     end;
     end;
     hence thesis;
     end;
     suppose a = 0;
     then x * (y + z) = 0. & x * y = 0. & x * z = 0. by Def1;
     hence thesis by SUPINF_2:18;
     end;
     suppose A22: a > 0;
then A23: x * y = -infty by A18,Def1;
         now per cases;
      suppose z = -infty;
      then x * z = -infty by A22,Def1;
      then x * y + x * z = -infty by A23,SUPINF_2:def 2;
      hence thesis by A18,A23,SUPINF_2:def 2;
      end;
      suppose z <> -infty;
      then reconsider c = z as Real by A18,XXREAL_0:14;
         A24: x * z = a * c by Th13;
          y + z = -infty by A18,SUPINF_2:def 2;
then x * (y + z) = -infty by A22,Def1;
      hence thesis by A23,A24,SUPINF_2:def 2;
     end;
     end;
     hence thesis;
    end;
    end;
    hence thesis;
    end;
    suppose A25:z = +infty & y <> -infty;
        now per cases;
     suppose A26: a < 0;
then A27:  x * z = -infty by A25,Def1;
         now per cases;
      suppose y = +infty;
      then x * y = -infty by A26,Def1;
      then x * y + x * z = -infty by A27,SUPINF_2:def 2;
      hence thesis by A25,A27,SUPINF_2:def 2;
      end;
      suppose y <> +infty;
      then reconsider b = y as Real by A25,XXREAL_0:14;
A28:    x * y = a * b by Th13;
          y + z = +infty by A25,SUPINF_2:def 2;
then x * (y + z) = -infty by A26,Def1;
      hence thesis by A27,A28,SUPINF_2:def 2;
     end;
     end;
     hence thesis;
     end;
     suppose a = 0;
     then x * (y + z) = 0. & x * y = 0. & x * z = 0. by Def1;
     hence thesis by SUPINF_2:18;
     end;
     suppose A29: a > 0;
then A30: x * z = +infty by A25,Def1;
         now per cases;
      suppose y = +infty;
      then x * y = +infty by A29,Def1;
      then x * y + x * z = +infty by A30,SUPINF_2:def 2;
      hence thesis by A25,A30,SUPINF_2:def 2;
      end;
      suppose y <> +infty;
      then reconsider b = y as Real by A25,XXREAL_0:14;
A31:    x * y = a * b by Th13;
          y + z = +infty by A25,SUPINF_2:def 2;
then x * (y + z) = +infty by A29,Def1;
      hence thesis by A30,A31,SUPINF_2:def 2;
     end;
     end;
     hence thesis;
    end;
    end;
    hence thesis;
    end;
    suppose A32:z = -infty & y <> +infty;
        now per cases;
     suppose A33: a < 0;
then A34:  x * z = +infty by A32,Def1;
         now per cases;
      suppose y = -infty;
      then x * y = +infty by A33,Def1;
      then x * y + x * z = +infty by A34,SUPINF_2:def 2;
      hence thesis by A32,A34,SUPINF_2:def 2;
      end;
      suppose y <> -infty;
      then reconsider b = y as Real by A32,XXREAL_0:14;
A35:    x * y = a * b by Th13;
          y + z = -infty by A32,SUPINF_2:def 2;
      then x * (y + z) = +infty by A33,Def1;
      hence thesis by A34,A35,SUPINF_2:def 2;
     end;
     end;
     hence thesis;
     end;
     suppose a = 0;
     then x * (y + z) = 0. & x * y = 0. & x * z = 0. by Def1;
     hence thesis by SUPINF_2:18;
     end;
     suppose A36: a > 0;
then A37: x * z = -infty by A32,Def1;
         now per cases;
      suppose y = -infty;
      then x * y = -infty by A36,Def1;
      then x * y + x * z = -infty by A37,SUPINF_2:def 2;
      hence thesis by A32,A37,SUPINF_2:def 2;
      end;
      suppose y <> -infty;
      then reconsider b = y as Real by A32,XXREAL_0:14;
A38:    x * y = a * b by Th13;
          y + z = -infty by A32,SUPINF_2:def 2;
then x * (y + z) = -infty by A36,Def1;
      hence thesis by A37,A38,SUPINF_2:def 2;
     end;
     end;
     hence thesis;
    end;
    end;
    hence thesis;
end;
end;

theorem
 for x,y,z be R_eal st
  (x <> +infty & x <> -infty) or
  (y = -infty & z = +infty) or
  (y < 0. & z < 0.) or
  (y = 0. or z = 0.) or
  (0. < y & 0. < z) or
  (y = +infty & z = -infty) holds
    x * (y + z) = x * y + x * z
proof
   let x,y,z be R_eal;
   assume
A1: (x <> +infty & x <> -infty) or
    (y = -infty & z = +infty) or
    (y < 0. & z < 0.) or
    (y = 0. or z = 0.) or
    (0. < y & 0. < z) or
    (y = +infty & z = -infty);
    now per cases by A1;
    case x <> +infty & x <> -infty;
     hence thesis by Lm2;
    end;
    case
A2:  y = -infty & z = +infty;
     then y + z = 0. by Th9,SUPINF_2:4;
then A3:  x * (y + z) = 0. by Def1;
      now per cases by SUPINF_2:2;
      case x = +infty;
       then x*y = -infty & x*z = +infty by A2,Def1;
       hence thesis by A3,Th9,SUPINF_2:4;
      end;
      case x = -infty;
       then x*y = +infty & x*z = -infty by A2,Def1;
       hence thesis by A3,Th9,SUPINF_2:4;
      end;
      case
A4:    x in REAL;
        now per cases by A4,XXREAL_0:9,12;
        case -infty < x & x < 0.;
         then x*y = +infty & x*z = -infty by A2,Def1;
         hence thesis by A3,Th9,SUPINF_2:4;
        end;
        case x = 0.;
         then x*y = 0. & x*z = 0. by Def1;
         hence thesis by A3,SUPINF_2:18;
        end;
        case 0. < x & x < +infty;
         then x*y = -infty & x*z = +infty by A2,Def1;
         hence thesis by A3,Th9,SUPINF_2:4;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
    end;
    case
A5:  y < 0. & z < 0.;
then A6:  y + z < 0. by Th15;
      now per cases by SUPINF_2:2;
      case x = -infty;
       then x*y = +infty & x*z = +infty & x*(y+z)=+infty by A5,A6,Def1;
       hence thesis by SUPINF_2:def 2;
      end;
      case x = +infty;
       then x*y = -infty & x*z = -infty & x*(y+z)=-infty by A5,A6,Def1;
       hence thesis by SUPINF_2:def 2;
      end;
      case
A7:    x in REAL;
        now per cases by A7,XXREAL_0:9,12;
        case
A8:     -infty < x & x < 0.;
          now per cases by A5,SUPINF_2:2;
          case y = -infty & z = -infty;
then A9:       x*y = +infty & x*z = +infty & y+z = -infty
            by A8,Def1,SUPINF_2:def 2;
           then x*y + x*z = +infty by SUPINF_2:def 2;
           hence thesis by A8,A9,Def1;
          end;
          case
A10:       y = -infty & z in REAL;
           then y + z = -infty by SUPINF_1:1,SUPINF_2:def 2;
then A11:       x*(y+z) = +infty by A8,Def1;
A12:       x*y = +infty by A8,A10,Def1;
           consider a be Real such that
A13:        a = x by A7;
           consider c be Real such that
A14:        c = z by A10;
            x*z = a*c by A13,A14,Th13;
           hence thesis by A11,A12,SUPINF_2:def 2;
          end;
          case A15:y in REAL & z = -infty;
           then y + z = -infty by SUPINF_1:1,SUPINF_2:def 2;
then A16:       x*(y+z) = +infty by A8,Def1;
A17:       x*z = +infty by A8,A15,Def1;
           consider a be Real such that
A18:        a = x by A7;
           consider b be Real such that
A19:        b = y by A15;
            x*y = a*b by A18,A19,Th13;
           hence thesis by A16,A17,SUPINF_2:def 2;
          end;
          case A20:y in REAL & z in REAL;
           then consider b be Real such that
A21:        b = y;
           consider c be Real such that
A22:        c = z by A20;
           consider a be Real such that
A23:        a = x by A7;
            x*y = a*b & x*z = a*c & y+z = b+c
             by A21,A22,A23,Th13,SUPINF_2:1;
           then x*y + x*z = a*b + a*c &
           x*(y+z) = a*(b+c) by A23,Th13,SUPINF_2:1;
           hence thesis;
          end;
         end;
         hence thesis;
        end;
        case x = 0.;
         then x*y = 0. & x*z = 0. & x*(y+z)= 0. by Def1;
         hence thesis by SUPINF_2:18;
        end;
        case
A24:     0. < x & x < +infty;
          now per cases by A5,SUPINF_2:2;
          case y = -infty & z = -infty;
then A25:       x*y = -infty & x*z = -infty & y+z = -infty
            by A24,Def1,SUPINF_2:def 2;
           then x*y + x*z = -infty by SUPINF_2:def 2;
           hence thesis by A24,A25,Def1;
          end;
          case
A26:       y = -infty & z in REAL;
           then y + z = -infty by SUPINF_1:1,SUPINF_2:def 2;
then A27:       x*(y+z) = -infty by A24,Def1;
A28:       x*y = -infty by A24,A26,Def1;
           consider a be Real such that
A29:        a = x by A7;
           consider c be Real such that
A30:        c = z by A26;
            x*z = a*c by A29,A30,Th13;
           hence thesis by A27,A28,SUPINF_2:def 2;
          end;
          case A31:y in REAL & z = -infty;
           then y + z = -infty by SUPINF_1:1,SUPINF_2:def 2;
then A32:       x*(y+z) = -infty by A24,Def1;
A33:       x*z = -infty by A24,A31,Def1;
           consider a be Real such that
A34:        a = x by A7;
           consider b be Real such that
A35:        b = y by A31;
            x*y = a*b by A34,A35,Th13;
           hence thesis by A32,A33,SUPINF_2:def 2;
          end;
          case A36:y in REAL & z in REAL;
           then consider b be Real such that
A37:        b = y;
           consider c be Real such that
A38:        c = z by A36;
           consider a be Real such that
A39:        a = x by A7;
            x*y = a*b & x*z = a*c & y+z = b+c
             by A37,A38,A39,Th13,SUPINF_2:1;
           then x*y + x*z = a*b + a*c &
           x*(y+z) = a*(b+c) by A39,Th13,SUPINF_2:1;
           hence thesis;
          end;
         end;
         hence thesis;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
    end;
    case y = 0.;
     then x*y = 0. & y+z = z by Def1,SUPINF_2:18;
     hence thesis by SUPINF_2:18;
    end;
    case z = 0.;
     then x*z = 0. & y+z = y by Def1,SUPINF_2:18;
     hence thesis by SUPINF_2:18;
    end;
    case
A40:  0. < y & 0. < z;
then A41:  0. < y + z by Th14;
      now per cases by SUPINF_2:2;
      case x = -infty;
       then x*y = -infty & x*z = -infty & x*(y+z)=-infty by A40,A41,Def1;
       hence thesis by SUPINF_2:def 2;
      end;
      case x = +infty;
       then x*y = +infty & x*z = +infty & x*(y+z)=+infty by A40,A41,Def1;
       hence thesis by SUPINF_2:def 2;
      end;
      case
A42:    x in REAL;
        now per cases by A42,XXREAL_0:9,12;
        case
A43:     -infty < x & x < 0.;
          now per cases by A40,SUPINF_2:2;
          case y = +infty & z = +infty;
then A44:       x*y = -infty & x*z = -infty & y+z = +infty
            by A43,Def1,SUPINF_2:def 2;
           then x*y + x*z = -infty by SUPINF_2:def 2;
           hence thesis by A43,A44,Def1;
          end;
          case
A45:       y = +infty & z in REAL;
           then y + z = +infty by SUPINF_1:6,SUPINF_2:def 2;
then A46:       x*(y+z) = -infty by A43,Def1;
A47:       x*y = -infty by A43,A45,Def1;
           consider a be Real such that
A48:        a = x by A42;
           consider c be Real such that
A49:        c = z by A45;
            x*z = a*c by A48,A49,Th13;
           hence thesis by A46,A47,SUPINF_2:def 2;
          end;
          case A50:y in REAL & z = +infty;
           then y + z = +infty by SUPINF_1:6,SUPINF_2:def 2;
then A51:       x*(y+z) = -infty by A43,Def1;
A52:       x*z = -infty by A43,A50,Def1;
           consider a be Real such that
A53:        a = x by A42;
           consider b be Real such that
A54:        b = y by A50;
            x*y = a*b by A53,A54,Th13;
           hence thesis by A51,A52,SUPINF_2:def 2;
          end;
          case A55:y in REAL & z in REAL;
           then consider b be Real such that
A56:        b = y;
           consider c be Real such that
A57:        c = z by A55;
           consider a be Real such that
A58:        a = x by A42;
            x*y = a*b & x*z = a*c & y+z = b+c
             by A56,A57,A58,Th13,SUPINF_2:1;
           then x*y + x*z = a*b + a*c &
           x*(y+z) = a*(b+c) by A58,Th13,SUPINF_2:1;
           hence thesis;
          end;
         end;
         hence thesis;
        end;
        case x = 0.;
         then x*y = 0. & x*z = 0. & x*(y+z)= 0. by Def1;
         hence thesis by SUPINF_2:18;
        end;
        case
A59:     0. < x & x < +infty;
          now per cases by A40,SUPINF_2:2;
          case y = +infty & z = +infty;
then A60:       x*y = +infty & x*z = +infty & y+z = +infty
            by A59,Def1,SUPINF_2:def 2;
           then x*y + x*z = +infty by SUPINF_2:def 2;
           hence thesis by A59,A60,Def1;
          end;
          case
A61:       y = +infty & z in REAL;
           then y + z = +infty by SUPINF_1:6,SUPINF_2:def 2;
then A62:       x*(y+z) = +infty by A59,Def1;
A63:       x*y = +infty by A59,A61,Def1;
           consider a be Real such that
A64:        a = x by A42;
           consider c be Real such that
A65:        c = z by A61;
            x*z = a*c by A64,A65,Th13;
           hence thesis by A62,A63,SUPINF_2:def 2;
          end;
          case A66:y in REAL & z = +infty;
           then y + z = +infty by SUPINF_1:6,SUPINF_2:def 2;
then A67:       x*(y+z) = +infty by A59,Def1;
A68:       x*z = +infty by A59,A66,Def1;
           consider a be Real such that
A69:        a = x by A42;
           consider b be Real such that
A70:        b = y by A66;
            x*y = a*b by A69,A70,Th13;
           hence thesis by A67,A68,SUPINF_2:def 2;
          end;
          case A71:y in REAL & z in REAL;
           then consider b be Real such that
A72:        b = y;
           consider c be Real such that
A73:        c = z by A71;
           consider a be Real such that
A74:        a = x by A42;
            x*y = a*b & x*z = a*c & y+z = b+c
             by A72,A73,A74,Th13,SUPINF_2:1;
           then x*y + x*z = a*b + a*c &
           x*(y+z) = a*(b+c) by A74,Th13,SUPINF_2:1;
           hence thesis;
          end;
         end;
         hence thesis;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
    end;
    case
A75:  y = +infty & z = -infty;
     then y + z = 0. by Th9,SUPINF_2:4;
then A76:  x * (y + z) = 0. by Def1;
      now per cases by SUPINF_2:2;
      case x = +infty;
       then x*y = +infty & x*z = -infty by A75,Def1;
       hence thesis by A76,Th9,SUPINF_2:4;
      end;
      case x = -infty;
       then x*y = -infty & x*z = +infty by A75,Def1;
       hence thesis by A76,Th9,SUPINF_2:4;
      end;
      case
A77:    x in REAL;
        now per cases by A77,XXREAL_0:9,12;
        case -infty < x & x < 0.;
         then x*y = -infty & x*z = +infty by A75,Def1;
         hence thesis by A76,Th9,SUPINF_2:4;
        end;
        case x = 0.;
         then x*y = 0. & x*z = 0. by Def1;
         hence thesis by A76,SUPINF_2:18;
        end;
        case 0. < x & x < +infty;
         then x*y = +infty & x*z = -infty by A75,Def1;
         hence thesis by A76,Th9,SUPINF_2:4;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
    end;
   end;
   hence thesis;
end;

theorem
    not ((y = +infty & z = +infty) or (y = -infty & z = -infty)) &
x <> +infty & x <> -infty implies x * (y - z) = x * y - x * z
proof
   assume not ((y = +infty & z = +infty) or (y = -infty & z = -infty)) &
   x <> +infty & x <> -infty;
then A1:x * (y + -z) = x * y + x * (-z) by Lm2;
       y - (--z) = y + -z by Th5;
then x * (y - z) = x * y + -(x * z) by A1,Th26;
   hence thesis by SUPINF_2:def 4;
end;

definition
let x,y be R_eal;
assume A1:not((x=-infty or x=+infty) & (y=-infty or y=+infty)) & y <> 0.;
func x / y -> R_eal means :Def2:
(ex a,b being Real st (x = a & y = b & it = a / b)) or
(((x=+infty & 0. < y) or (x=-infty & y < 0.)) & it = +infty) or
(((x=-infty & 0. < y) or (x=+infty & y < 0.)) & it = -infty) or
((y = -infty or y = +infty) & it = 0.);
existence
proof
       now per cases by A1;
    suppose x<>-infty & x<>+infty & y <> 0.;
then A2: x is Real by MEASURE3:2;
        y in REAL or y in {-infty,+infty} by XBOOLE_0:def 2,XXREAL_0:def 4;
then A3: y in REAL or y = +infty or y = -infty by TARSKI:def 2;
        ex Z being R_eal st
    (ex a,b being Real st (x = a & y = b & Z = a / b)) or
    ((y = -infty or y = +infty) & Z = 0.)
    proof
         y in REAL implies ex Z being R_eal st
     ex a,b being Real st (x = a & y = b & Z = a / b)
     proof
      assume y in REAL;
      then consider a,b being Real such that
A4:   x = a & y = b by A2;
      reconsider Z = a / b as R_eal by XXREAL_0:def 1;
      take Z;
      thus thesis by A4;
     end;
     hence thesis by A3;
    end;
    hence thesis;
    end;
    suppose A5:y<>-infty & y<>+infty & y <> 0.;
then A6:y is Real by MEASURE3:2;
        x in REAL or x in {-infty,+infty} by XBOOLE_0:def 2,XXREAL_0:def 4;
then A7:x in REAL or x = +infty or x = -infty by TARSKI:def 2;
        ex Z being R_eal st
    (ex a,b being Real st (x = a & y = b & Z = a / b)) or
    (((x=+infty & 0. < y) or (x=-infty & y < 0.)) & Z = +infty) or
    (((x=-infty & 0. < y) or (x=+infty & y < 0.)) & Z = -infty)
    proof
A8: x in REAL implies ex Z being R_eal st
     ex a,b being Real st x = a & y = b & Z = a / b
     proof
      assume x in REAL;
      then consider a,b being Real such that
A9: x = a & y = b by A6;
      reconsider Z = a / b as R_eal by XXREAL_0:def 1;
      take Z;
      thus thesis by A9;
     end;
         0. < y or y < 0. by A5;
     hence thesis by A7,A8;
    end;
    hence thesis;
   end;
   end;
   hence thesis;
end;
uniqueness by A1;
end;

canceled;

theorem Th32: for x,y being R_eal st y <> 0. holds
for a,b being Real st x = a & y = b holds x / y = a / b
proof
   let x,y be R_eal;
   assume A1:y <> 0.;
   let a,b be Real;
   assume A2:x = a & y = b;
   then consider a1,b1 being Real such that
A3:x = a1 & y = b1 & x / y = a1 / b1 by A1,Def2;
   thus thesis by A2,A3;
end;

theorem   for x,y being R_eal st x <> -infty & x <> +infty &
(y = -infty or y = +infty) holds x / y = 0. by Def2;

theorem   for x being R_eal st x <> -infty & x <> +infty & x <> 0. holds
x / x = 1
proof
   let x be R_eal;
   assume A1:x <> -infty & x <> +infty & x <> 0.;
   then consider a,b being Real such that
A2:x = a & x = b & x / x = a / b by Def2;
   thus thesis by A1,A2,XCMPLX_1:60;
end;

definition let x be R_eal;
  func |. x .| -> R_eal equals :Def3:
  x if 0. <= x otherwise -x;
  coherence;
  consistency;
end;

canceled;

theorem
      for x being R_eal st 0. < x holds |.x.| = x by Def3;

theorem
      for x being R_eal st x < 0. holds |.x.| = -x by Def3;

theorem
      for a,b being Real holds a*b = R_EAL a * R_EAL b
proof
   let a,b be Real;
   reconsider c = a*b as real number;
   reconsider a,b as real number;
       R_EAL a = a & R_EAL b = b by MEASURE6:def 1;
   hence thesis by Th13;
end;

theorem
      for a,b being Real st b <> 0 holds a/b = R_EAL a / R_EAL b
proof
   let a,b be Real;
   assume A1:b <> 0;
   reconsider a,b as R_eal by XXREAL_0:def 1;
   reconsider a,b as real number;
   set c = a/b;
   reconsider a,b as Real;
    R_EAL a = a & R_EAL b = b by MEASURE6:def 1;
   hence thesis by A1,Th32;
end;

theorem
    for x,y being R_eal st x <= y & (x < +infty & -infty < y) holds 0. <= y-
x
proof
   let x,y being R_eal;
   assume that A1:x <= y and A2:(x < +infty & -infty < y);
   per cases;
     suppose x = -infty;
     hence thesis by A2,SUPINF_2:7;
     end;
     suppose x <> -infty;
     hence thesis by A1,A2,MEASURE5:1;
end;
end;

theorem
      for x,y being R_eal st x < y & (x < +infty & -infty < y) holds 0. < y-
x
proof
   let x,y be R_eal;
   assume that A1:x < y and A2:x < +infty & -infty < y;
   per cases;
    suppose y = +infty;
    hence thesis by A2,SUPINF_2:6;
    end;
    suppose A3:y <> +infty;
        now per cases;
     suppose x = -infty;
     hence thesis by A2,SUPINF_2:7;
     end;
     suppose A4:x <> -infty;
      y <= +infty by XXREAL_0:4;
     then reconsider a = x as Real by A1,A4,XXREAL_0:14;
     reconsider b = y as Real by A2,A3,XXREAL_0:14;
     consider p,q being Real;
      b - a > 0 by A1,XREAL_1:52;
     hence thesis by SUPINF_2:5;
    end;
    end;
    hence thesis;
end;
end;

theorem
      x <= y & 0. <= z implies x*z <= y*z
proof
   assume that A1:x <= y and A2:0. <= z;
   per cases by A2;
    suppose z = 0.;
    then x*z = 0. & y*z = 0. by Def1;
    hence thesis;
    end;
    suppose A3:0. < z;
        now per cases;
     suppose A4:x< +infty & -infty < y;
         now per cases;
      suppose A5:z = +infty;
          now per cases;
       suppose A6:x = 0.;
then A7:    x*z = 0. & 0. <= y by A1,Def1;
           now per cases by A1,A6;
        suppose 0.= y;
        hence thesis by A6;
        end;
        suppose 0. < y;
        hence thesis by A3,A7,Th20;
       end;
       end;
       hence thesis;
       end;
       suppose x < 0.;
       then x*z = -infty by A5,Def1;
       hence thesis by XXREAL_0:5;
       end;
       suppose A8:0. < x;
then A9:    x*z = +infty by A5,Def1;
           now per cases by A1,XXREAL_0:1;
        suppose y = x;
        hence thesis;
        end;
        suppose x < y;
        then 0. < y by A8; hence thesis by A5,A9,Def1;
       end;
       end;
       hence thesis;
      end;
      end;
      hence thesis;
      end;
      suppose z <>+infty;
      then reconsider c = z as Real by A3,XXREAL_0:14;
          now per cases;
       suppose x = -infty;
       then x*z = -infty by A3,Def1;
       hence thesis by XXREAL_0:5;
       end;
       suppose x <> -infty;
then A10:   x is Real by A4,XXREAL_0:14;
           now per cases;
        suppose y = +infty;
        then y*z = +infty by A3,Def1;
        hence thesis by XXREAL_0:4;
        end;
        suppose y <> +infty;
        then y is Real by A4,XXREAL_0:14;
        then consider a,b being Real such that
A11:    a = x & b = y & a <= b by A1,A10;
    x*z = a*c & y*z = b*c by A11,Th13;
        hence thesis by A3,A11,XREAL_1:66;
       end;
       end;
       hence thesis;
      end;
      end;
      hence thesis;
     end;
     end;
     hence thesis;
     end;
     suppose A12: not (x < +infty & -infty < y);
         now per cases by A12,XXREAL_0:4,6;
      suppose x = +infty; hence thesis by A1,XXREAL_0:4;
      end;
      suppose y = -infty; hence thesis by A1,XXREAL_0:6;
     end;
     end;
     hence thesis;
    end;
    end;
    hence thesis;
end;
end;

theorem
      x <= y & z <= 0. implies y*z <= x*z
proof
   assume that A1:x <= y and A2:z <= 0.;
   per cases by A2;
    suppose z = 0.;
    then x*z = 0. & y*z = 0. by Def1;
    hence thesis;
    end;
    suppose A3:z < 0.;
        now per cases;
     suppose A4:x< +infty & -infty < y;
         now per cases;
      suppose A5:z = -infty;
          now per cases;
       suppose A6:x = 0.;
then A7:    x*z = 0. & 0. <= y by A1,Def1;
           now per cases by A1,A6;
        suppose 0.= y;
        hence thesis by A6;
        end;
        suppose 0. < y;
        hence thesis by A3,A7,Th21;
       end;
       end;
       hence thesis;
       end;
       suppose x < 0.;
       then x*z = +infty by A5,Def1;
       hence thesis by XXREAL_0:4;
       end;
       suppose A8:0. < x;
then A9:    x*z = -infty by A5,Def1;
           now per cases by A1,XXREAL_0:1;
        suppose y = x;
        hence thesis;
        end;
        suppose x < y;
        then 0. < y by A8; hence thesis by A5,A9,Def1;
       end;
       end;
       hence thesis;
      end;
      end;
      hence thesis;
      end;
      suppose z <>-infty;
      then reconsider c = z as Real by A3,XXREAL_0:14;
          now per cases;
       suppose x = -infty;
       then x*z = +infty by A3,Def1;
       hence thesis by XXREAL_0:4;
       end;
       suppose x <> -infty;
then A10:   x is Real by A4,XXREAL_0:14;
           now per cases;
        suppose y = +infty;
        then y*z = -infty by A3,Def1;
        hence thesis by XXREAL_0:5;
        end;
        suppose y <> +infty;
        then y is Real by A4,XXREAL_0:14;
        then consider a,b being Real such that
A11:    a = x & b = y & a <= b by A1,A10;
    x*z = a*c & y*z = b*c by A11,Th13;
        hence thesis by A3,A11,XREAL_1:67;
       end;
       end;
       hence thesis;
      end;
      end;
      hence thesis;
     end;
     end;
     hence thesis;
     end;
     suppose A12: not (x < +infty & -infty < y);
         now per cases by A12,XXREAL_0:4,6;
      suppose x = +infty; hence thesis by A1,XXREAL_0:4;
      end;
      suppose y = -infty; hence thesis by A1,XXREAL_0:6;
     end;
     end;
     hence thesis;
    end;
    end;
    hence thesis;
end;
end;

theorem
      x < y & 0. < z & z <> +infty implies x*z < y*z
proof
   assume that A1:x < y and A2:0. < z and A3:z <> +infty;
A4:x < +infty & -infty < y
   proof
    assume A5: not (x < +infty & -infty < y);
        now per cases by A5,XXREAL_0:4,6;
     suppose x = +infty;
     hence contradiction by A1,XXREAL_0:4;
     end;
     suppose y = -infty;
     hence contradiction by A1,XXREAL_0:5;
    end;
    end;
    hence thesis;
   end;
    reconsider c = z as Real by A2,A3,XXREAL_0:14;
        now per cases;
     suppose x = -infty;
then A6:  x*z = -infty by A2,Def1;
         now per cases;
      suppose y = +infty;
      hence thesis by A2,A6,Def1;
      end;
      suppose y <> +infty;
      then reconsider b = y as Real by A4,XXREAL_0:14;
          y * z = b * c by Th13;
      hence thesis by A6,XXREAL_0:12;
     end;
     end;
     hence thesis;
     end;
     suppose A7:x <> -infty;
then A8: x is Real by A4,XXREAL_0:14;
     reconsider a = x as Real by A4,A7,XXREAL_0:14;
A9:  x * z = a * c by Th13;
         now per cases;
      suppose y = +infty;
      then y*z = +infty by A2,Def1;
      hence thesis by A9,XXREAL_0:9;
      end;
      suppose y <> +infty;
      then y is Real by A4,XXREAL_0:14;
      then consider a,b being Real such that
A10:  a = x & b = y & a <= b by A1,A8;
  x*z = a*c & y*z = b*c by A10,Th13;
      hence thesis by A1,A2,A10,XREAL_1:70;
     end;
     end;
     hence thesis;
   end;
   end;
   hence thesis;
end;

theorem
      x < y & z < 0. & z <> -infty implies y*z < x*z
proof
   assume that A1:x < y and A2:z < 0. and A3:z <> -infty;
A4:x < +infty & -infty < y
   proof
    assume A5: not (x < +infty & -infty < y);
        now per cases by A5,XXREAL_0:4,6;
     suppose x = +infty;
     hence contradiction by A1,XXREAL_0:4;
     end;
     suppose y = -infty;
     hence contradiction by A1,XXREAL_0:5;
    end;
    end;
    hence thesis;
   end;
    reconsider c = z as Real by A2,A3,XXREAL_0:14;
        now per cases;
     suppose x = -infty;
then A6:  x*z = +infty by A2,Def1;
         now per cases;
      suppose y = +infty;
      hence thesis by A2,A6,Def1;
      end;
      suppose y <> +infty;
      then reconsider b = y as Real by A4,XXREAL_0:14;
          y * z = b * c by Th13;
      hence thesis by A6,XXREAL_0:9;
     end;
     end;
     hence thesis;
     end;
     suppose A7:x <> -infty;
then A8: x is Real by A4,XXREAL_0:14;
     reconsider a = x as Real by A4,A7,XXREAL_0:14;
A9:  x * z = a * c by Th13;
         now per cases;
      suppose y = +infty;
      then y*z = -infty by A2,Def1;
      hence thesis by A9,XXREAL_0:12;
      end;
      suppose y <> +infty;
      then y is Real by A4,XXREAL_0:14;
      then consider a,b being Real such that
A10:  a = x & b = y & a <= b by A1,A8;
  x*z = a*c & y*z = b*c by A10,Th13;
      hence thesis by A1,A2,A10,XREAL_1:71;
     end;
     end;
     hence thesis;
   end;
   end;
   hence thesis;
end;

theorem
    (x is Real & y is Real) implies
  (x < y iff ex p,q being Real st (p = x & q = y & p < q));

theorem
      x <> -infty & y <> +infty & x <= y & 0. < z implies x/z <= y/z
proof
   assume that A1:x <> -infty & y <> +infty and A2:x <= y and
   A3:0. < z;
A4:x <> +infty & y <> -infty by A1,A2,Th7;
   then reconsider a = x as Real by A1,XXREAL_0:14;
   reconsider b = y as Real by A1,A4,XXREAL_0:14;
       now per cases;
    suppose A5:z = +infty;
then x/z = 0. by A1,A4,Def2;
    hence thesis by A1,A4,A5,Def2;
    end;
    suppose A6: z <> +infty;
     z <> -infty by A3;
    then reconsider c = z as Real by A6,XXREAL_0:14;
 x/z = a/c & y/z = b/c by A3,Th32;
    hence thesis by A2,A3,REAL_1:73;
   end;
   end;
   hence thesis;
end;

theorem
      x <= y & 0. < z & z <> +infty implies x/z <= y/z
proof
   assume that A1:x <= y and A2:0. < z and A3:z <> +infty;
       now per cases;
    suppose A4:x<>-infty;
        now per cases;
     suppose A5:y <> +infty;
         -infty <= x & y <= +infty by XXREAL_0:4,5;
     then A6: -infty < x & y < +infty by A4,A5,XXREAL_0:1;
     then reconsider a = x as Real by A1,XXREAL_0:14;
     reconsider b = y as Real by A1,A6,XXREAL_0:14;
         z <> -infty by A2;
     then reconsider c = z as Real by A3,XXREAL_0:14;
  x/z = a/c & y/z = b/c by A2,Th32;
     hence thesis by A1,A2,REAL_1:73;
     end;
     suppose y = +infty;
     then y/z = +infty by A2,A3,Def2;
     hence thesis by XXREAL_0:4;
    end;
    end;
    hence thesis;
    end;
    suppose x=-infty;
    then x/z = -infty by A2,A3,Def2;
    hence thesis by XXREAL_0:5;
   end;
   end;
   hence thesis;
end;

theorem
      x <> -infty & y <> +infty & x <= y & z < 0. implies y/z <= x/z
proof
   assume that A1:x <> -infty & y <> +infty and A2:x <= y and
   A3:z < 0.;
A4:x <> +infty & y <> -infty by A1,A2,Th7;
   then reconsider a = x as Real by A1,XXREAL_0:14;
   reconsider b = y as Real by A1,A4,XXREAL_0:14;
       now per cases;
    suppose A5:z = -infty;
then x/z = 0. by A1,A4,Def2;
    hence thesis by A1,A4,A5,Def2;
    end;
    suppose z <> -infty;
    then reconsider c = z as Real by A3,XXREAL_0:14;
 x/z = a/c & y/z = b/c by A3,Th32;
    hence thesis by A2,A3,REAL_1:74;
   end;
   end;
   hence thesis;
end;

theorem
      x <= y & z < 0. & z <> -infty implies y/z <= x/z
proof
   assume that A1:x <= y and A2:z < 0. and A3:z <> -infty;
       now per cases;
    suppose A4:x<>-infty;
        now per cases;
     suppose A5:y <> +infty;
         -infty <= x & y <= +infty by XXREAL_0:4,5;
     then A6: -infty < x & y < +infty by A4,A5,XXREAL_0:1;
     then reconsider a = x as Real by A1,XXREAL_0:14;
     reconsider b = y as Real by A1,A6,XXREAL_0:14;
     reconsider c = z as Real by A2,A3,XXREAL_0:14;
  x/z = a/c & y/z = b/c by A2,Th32;
     hence thesis by A1,A2,REAL_1:74;
     end;
     suppose y = +infty;
     then y/z = -infty by A2,A3,Def2;
     hence thesis by XXREAL_0:5;
    end;
    end;
    hence thesis;
    end;
    suppose x=-infty;
    then x/z = +infty by A2,A3,Def2;
    hence thesis by XXREAL_0:4;
   end;
   end;
   hence thesis;
end;

theorem
      x < y & 0. < z & z <> +infty implies x/z < y/z
proof
   assume that A1:x < y and A2:0. < z & z <> +infty;
A3:x <> +infty & y <> -infty by A1,XXREAL_0:4,5;
   reconsider c = z as Real by A2,XXREAL_0:14;
       now per cases;
    suppose y <> +infty;
    then reconsider b = y as Real by A3,XXREAL_0:14;
A4: y/z = b/c by A2,Th32;
        now per cases;
     suppose x <> -infty;
     then reconsider a = x as Real by A3,XXREAL_0:14;
  x/z = a/c by A2,Th32;
     hence thesis by A1,A2,A4,REAL_1:73;
     end;
     suppose x = -infty;
     then x/z = -infty by A2,Def2;
     hence thesis by A4,XXREAL_0:12;
    end;
    end;
    hence thesis;
    end;
    suppose y = +infty;
then A5: y/z = +infty by A2,Def2;
        now per cases;
     suppose x <> -infty;
     then reconsider a = x as Real by A3,XXREAL_0:14;
         x/z = a/c by A2,Th32;
     hence thesis by A5,XXREAL_0:9;
     end;
     suppose x = -infty;
     hence thesis by A2,A5,Def2;
    end;
    end;
    hence thesis;
   end;
   end;
   hence thesis;
end;

theorem
      x < y & z < 0. & z <> -infty implies y/z < x/z
proof
   assume that A1:x < y and A2:z < 0. & z <> -infty;
A3:x <> +infty & y <> -infty by A1,XXREAL_0:4,5;
   reconsider c = z as Real by A2,XXREAL_0:14;
   per cases;
    suppose y <> +infty;
    then reconsider b = y as Real by A3,XXREAL_0:14;
A4: y/z = b/c by A2,Th32;
        now per cases;
     suppose x <> -infty;
     then reconsider a = x as Real by A3,XXREAL_0:14;
  x/z = a/c by A2,Th32;
     hence thesis by A1,A2,A4,REAL_1:74;
     end;
     suppose x = -infty;
     then x/z = +infty by A2,Def2;
     hence thesis by A4,XXREAL_0:9;
    end;
    end;
    hence thesis;
    end;
    suppose y = +infty;
then A5: y/z = -infty by A2,Def2;
        now per cases;
     suppose x <> -infty;
     then reconsider a = x as Real by A3,XXREAL_0:14;
         x/z = a/c by A2,Th32;
     hence thesis by A5,XXREAL_0:12;
     end;
     suppose x = -infty;
     hence thesis by A2,A5,Def2;
    end;
    end;
    hence thesis;
end;
end;

