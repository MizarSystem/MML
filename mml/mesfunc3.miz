:: Lebesgue Integral of Simple Valued Function
::  by Yasunari Shidama and Noboru Endou
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PARTFUN1, SUPINF_1, MEASURE1, RELAT_1, FUNCT_1, COMPLEX1,
      SUPINF_2, BOOLE, ARYTM_1, TARSKI, FUNCOP_1, RVSUM_1, RLVECT_1, FUNCT_3,
      GROUP_1, PROB_2, FINSEQ_1, FINSEQ_2, INTEGRA1, MESFUNC2, MESFUNC3,
      VECTSP_1, SETWISEO, ORDINAL2, FINSOP_1, FINSET_1, SETFAM_1, CARD_4,
      CARD_1, PROB_1, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, ORDINAL1, CARD_1, NUMBERS,
      XCMPLX_0, NAT_1, REAL_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2,
      SETWOP_2, FINSEQ_1, SETFAM_1, FUNCOP_1, FINSOP_1, FUNCT_3, FINSET_1,
      CARD_4, PROB_1, PROB_2, XXREAL_0, SUPINF_1, SUPINF_2, MEASURE1, MEASURE3,
      FINSEQOP, FINSEQ_2, BINOP_1, VALUED_1, SEQ_1, BINOP_2, RVSUM_1, EXTREAL1,
      MESFUNC1, MESFUNC2, CONVFUN1;
 constructors PARTFUN1, BINOP_1, SETWISEO, REAL_1, NAT_1, FINSEQOP, PROB_2,
      FINSOP_1, RVSUM_1, MEASURE3, MEASURE6, EXTREAL1, MESFUNC1, BINARITH,
      MESFUNC2, CONVFUN1, VALUED_1, SEQ_1;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, ORDINAL1, FUNCT_2, NUMBERS,
      XXREAL_0, XREAL_0, NAT_1, BINOP_2, MEMBERED, FINSEQ_1, SEQ_1, SUPINF_2,
      MEASURE1, FINSET_1, VALUED_0, VALUED_1;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, BINOP_1, SUPINF_2, VALUED_1;
 theorems MEASURE1, TARSKI, SUPINF_1, FUNCT_1, FUNCT_2, NAT_1, REAL_1,
      SUPINF_2, RELSET_1, EXTREAL1, EXTREAL2, MESFUNC1, ZFMISC_1, FINSEQ_1,
      FINSEQ_2, FINSEQ_3, FUNCOP_1, FINSEQ_4, PROB_2, FUNCT_3, XBOOLE_0,
      XBOOLE_1, RELAT_1, RVSUM_1, BINOP_2, MESFUNC2, CONVFUN1, MEASURE2,
      FINSOP_1, FINSET_1, CARD_4, CARD_1, CARD_2, XREAL_1, PROB_1, XXREAL_0,
      XREAL_0, ORDINAL1, SEQ_1, NUMBERS, VALUED_1;
 schemes FUNCT_2, FINSEQ_1, FINSEQ_2, NAT_1, PARTFUN1, BINOP_1;

begin  :: Integral of Simple Valued Function ::

theorem Th1:
for n,m be Nat, a be Function of [:(Seg n),(Seg m):],REAL
for p,q be FinSequence of REAL st
( dom p=Seg n & for i be Nat st i in dom p holds
   ex r be FinSequence of REAL st
    (dom r = Seg m & p.i = Sum r
     & for j be Nat st j in dom r holds r.j=a.[i,j])) &
( dom q=Seg m & for j be Nat st j in dom q holds
   ex s be FinSequence of REAL st
    (dom s = Seg n & q.j = Sum s
     & for i be Nat st i in dom s holds s.i=a.[i,j]))
holds Sum p = Sum q
proof
   defpred P[Nat] means
   for m be Nat, a be Function of [: Seg $1, Seg m :],REAL
   for p,q be FinSequence of REAL
    st (dom p=Seg $1
        & for i be Nat st i in dom p
              holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j] )) &
        ( dom q=Seg m
        & for j be Nat st j in dom q
              holds ex s be FinSequence of REAL st
                     (dom(s) = Seg $1 & q.j=Sum(s)
                     & for i be Nat st i in dom(s) holds s.i=a.[i,j]))
   holds Sum(p)=Sum(q);
 A1: P[0]
 proof
  now
 let m be Nat, a be Function of [: Seg 0, Seg m :],REAL;
 let p,q be FinSequence of REAL such that
        A2:  dom p=Seg 0 and
            for i be Nat st i in dom p
                holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j]) and
        A3:  dom q=Seg m and
        A4:for j be Nat st j in dom q
              holds ex s be FinSequence of REAL st
                     (dom(s) = Seg 0 & q.j=Sum(s)
                     & for i be Nat st i in dom(s) holds s.i=a.[i,j] );
reconsider m as Element of NAT by ORDINAL1:def 13;
thus Sum(p)=Sum(q)
proof
  len p = 0 by A2,FINSEQ_1:def 3;
 then A5: Sum(p)=0 by FINSEQ_1:32,RVSUM_1:102;
  q = dom q --> 0
    proof
      now let z be set;
     assume
     A6: z in dom q;
        then z in { k where k is Element of NAT : 1 <= k & k <= m }
        by A3,FINSEQ_1:def 1;
        then consider k be Element of NAT such that
            A7:z=k & 1 <= k & k <= m;
          reconsider j=z as Nat by A7;
        consider s be FinSequence of REAL such that
           A8: dom(s) = Seg 0 and
           A9: q.j=Sum(s) and
             for i be Nat st i in dom(s) holds s.i=a.[i,j] by A4,A6;
           len s = 0 by A8,FINSEQ_1:def 3;
          hence q.z=0 by A9,FINSEQ_1:32,RVSUM_1:102;
         end;
    hence q = dom q --> 0 by FUNCOP_1:17;
    end;
    then q= m |-> 0 by A3,FINSEQ_2:def 2;
 hence Sum(p)=Sum(q) by A5,RVSUM_1:111;
 end;
end;
 hence thesis;
  end;
 A10: for n be Nat st P[n] holds P[n+1]
 proof
 let n be Nat such that
A11:P[n];
    reconsider n as Element of NAT by ORDINAL1:def 13;
   now
   let m be Nat, a be Function of [:Seg (n+1),Seg m :],REAL;
   let p,q be FinSequence of REAL such that
        A12: (dom p=Seg (n+1) & for i be Nat st i in dom p
                 holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j])) and
        A13: (dom q=Seg m
                 & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st
                     (dom s = Seg (n+1) & q.j=Sum s
                     & for i be Nat st i in dom s holds s.i=a.[i,j] ) );
       set a0=a| [:Seg n, Seg m :];
         n <= n+1 by NAT_1:11;
        then Seg n c= Seg (n+1) by FINSEQ_1:7;
        then [:Seg n,Seg m:] c= [:Seg (n+1),Seg m:] by ZFMISC_1:118;
    then reconsider a0 as Function of [:Seg n,Seg m :],REAL by FUNCT_2:38;
    reconsider p0= p | (Seg n) as FinSequence of REAL by FINSEQ_1:23;
      A14: ( dom p0=Seg n & for i be Nat st i in dom p0
                    holds ex r be FinSequence of REAL st
                        (dom r = Seg m & p0.i=Sum r
                        & for j be Nat st j in dom(r) holds r.j=a0.[i,j]))
      proof
        len p=n+1 by A12,FINSEQ_1:def 3;
       then n <= len p by NAT_1:11;
       hence A15: dom p0=Seg n by FINSEQ_1:21;
        now let i be Nat such that
         A16: i in dom p0;
          i in dom p by A12,A15,A16,FINSEQ_2:9;
         then consider r be FinSequence of REAL such that
               A17: dom(r) = Seg m and
               A18: p.i=Sum(r) and
               A19: for j be Nat st j in dom(r) holds r.j=a.[i,j] by A12;
            take r;
            thus dom(r) = Seg m by A17;
            thus p0.i = Sum(r) by A16,A18,FUNCT_1:70;
            thus for j be Nat st j in dom(r) holds r.j=a0.[i,j]
            proof
            let j be Nat such that
               A20: j in dom(r);
               A21: [i,j] in [:Seg n,Seg m:]
               by A15,A16,A17,A20,ZFMISC_1:106;
                n <= n+1 by NAT_1:11;
               then A22: Seg n c= Seg (n+1) by FINSEQ_1:7;
                dom a= [:Seg (n+1),Seg m:] by FUNCT_2:def 1;
               then A23: dom a0 = [:Seg (n+1),Seg m:]
                           /\ [:Seg n,Seg m:] by FUNCT_1:68
                          .= [:Seg n,Seg m:] by A22,ZFMISC_1:124;
               thus r.j=a.[i,j] by A19,A20
                     .=a0.[i,j] by A21,A23,FUNCT_1:70;
            end;
       end;
       hence thesis;
      end;
 deffunc F(Nat)=a.[n+1,$1];
reconsider m as Element of NAT by ORDINAL1:def 13;
 consider an be FinSequence such that
A24: len an=m &
     for j be Nat st j in Seg(m) holds an.j=F(j)
     from FINSEQ_1:sch 2;
  an is FinSequence of REAL
 proof
  now let i be Nat;
   assume i in dom an;
   then i in Seg m by A24,FINSEQ_1:def 3;
   then an.i = a.[n+1,i] by A24;
   hence an.i in REAL;
end;
hence thesis by FINSEQ_2:14;
 end;
 then reconsider an as FinSequence of REAL;
   A25:  dom an=Seg m by A24,FINSEQ_1:def 3;
   A26:  Sum(an) = p.(n+1)
   proof
     n+1 in dom p by A12,FINSEQ_1:6;
    then consider r be FinSequence of REAL such that
     A27: dom(r) = Seg m and
     A28: p.(n+1)=Sum(r) and
     A29: for j be Nat st j in dom(r) holds r.j=a.[n+1,j] by A12;
      now let j be Nat;
     assume A30:j in dom r;
     hence r.j=a.[n+1,j] by A29
        .=an.j by A24,A27,A30;
      end;
     hence thesis by A25,A27,A28,FINSEQ_1:17;
    end;
     reconsider q0=q-an as FinSequence of REAL;
     A31: dom q0=Seg m & for j be Nat st j in dom q0
                     holds ex s be FinSequence of REAL st
                         (dom s = Seg n
                         & q0.j=Sum(s)
                         & for i be Nat st i in dom(s) holds s.i=a0.[i,j] )
    proof
 dom diffreal = [:REAL,REAL:] by FUNCT_2:def 1;
then A32: [:rng q, rng an:] c= dom diffreal by ZFMISC_1:119;
     rng <:q,an:> c= [:rng q, rng an:] by FUNCT_3:71;
then A33: rng <:q,an:> c= dom diffreal by A32,XBOOLE_1:1;
 dom (diffreal.:(q,an)) = dom(diffreal*<:q,an:>) by FUNCOP_1:def 3
     .= dom <:q,an:> by A33,RELAT_1:46
     .= dom q /\ dom an by FUNCT_3:def 8;
    hence A34: dom q0= dom q /\ dom an by RVSUM_1:def 6
                      .= Seg m  /\ Seg m by A13,A24,FINSEQ_1:def 3
                      .= Seg m;
      now let j be Nat such that
            A35: j in dom q0;
            consider s be FinSequence of REAL such that
        A36: dom(s) = Seg (n+1) and
        A37: q.j=Sum(s) and
        A38: for i be Nat st i in dom(s) holds s.i=a.[i,j] by A13,A34,A35;
            reconsider sn=s|Seg (n) as FinSequence of REAL by FINSEQ_1:23;
            take sn;
            A39:len s = n+1 by A36,FINSEQ_1:def 3;
            then n <= len s by NAT_1:11;
            hence A40: dom sn =Seg n by FINSEQ_1:21;
              A41:  sn^<*(s.(n+1))*> = s by A39,FINSEQ_3:61;
                 n+1 in Seg (n+1) by FINSEQ_1:6;
                then s.(n+1)=a.[n+1,j] by A36,A38;
            then s.(n+1)=an.j by A24,A34,A35;
            then q.j-an.j=Sum(sn) +an.j-an.j by A37,A41,RVSUM_1:104
            .=Sum(sn);
            hence q0.j = Sum(sn) by A35,VALUED_1:12,13,14,15;
            thus for i be Nat st i in dom(sn) holds sn.i=a0.[i,j]
           proof
            let i be Nat such that
               A42: i in dom(sn);
               A43: [i,j] in [:Seg n,Seg m:]
               by A34,A35,A40,A42,ZFMISC_1:106;
                n <= n+1 by NAT_1:11;
               then A44: Seg n c= Seg (n+1) by FINSEQ_1:7;
                dom a= [:Seg (n+1),Seg m:] by FUNCT_2:def 1;
               then A45: dom a0 = [:Seg (n+1),Seg m:]
                           /\ [:Seg n,Seg m:] by FUNCT_1:68
                          .= [:Seg n,Seg m:] by A44,ZFMISC_1:124;
                thus sn.i=s.i by A42,FUNCT_1:70
                     .=a.[i,j] by A36,A38,A40,A42,A44
                     .=a0.[i,j] by A43,A45,FUNCT_1:70;
            end;
        end;
       hence thesis;
    end;
     len p=n+1 by A12,FINSEQ_1:def 3;
then A46: p0^<*Sum(an)*> = p by A26,FINSEQ_3:61;
     len q0=m by A31,FINSEQ_1:def 3;
then A47: q0 is Element of m-tuples_on REAL by FINSEQ_2:110;
A48: an is Element of m-tuples_on REAL by A24,FINSEQ_2:110;
    len q=m by A13,FINSEQ_1:def 3;
then q is Element of m-tuples_on REAL by FINSEQ_2:110;
then A49:  q0+an=q by A48,RVSUM_1:64;
   thus Sum(p) = Sum(p0)+Sum(an) by A46,RVSUM_1:104
   .=Sum(q0)+Sum(an) by A11,A14,A31
   .=Sum(q) by A47,A48,A49,RVSUM_1:119;
   end;
   hence thesis;
  end;
 thus for n be Nat holds P[n] from NAT_1:sch 2(A1,A10);
end;

theorem Th2:
  for F be FinSequence of ExtREAL, f be FinSequence of REAL st
    F=f holds Sum(F)=Sum(f)
proof
   let F be FinSequence of ExtREAL;
   let f be FinSequence of REAL;
   assume
A1: F = f;
   defpred P[Nat] means
   for G be FinSequence of ExtREAL, g be FinSequence of REAL st
    G=F|(Seg $1) & g = f|(Seg $1) & $1 <= len F holds Sum(G) = Sum(g);
A2:P[0]
   proof
     let G be FinSequence of ExtREAL, g be FinSequence of REAL;
     assume
A3: G=F|(Seg 0) & g = f|(Seg 0) & 0 <= len F;
      0 <= len F & 0 <= len f;
     then len G = 0 & len g = 0 by A3,FINSEQ_1:21;
     then G = <*>ExtREAL & g=<*>REAL by FINSEQ_1:32;
     hence thesis by CONVFUN1:4,RVSUM_1:102;
   end;
A4:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
A5:  P[k];
      for G be FinSequence of ExtREAL, g be FinSequence of REAL st
      G = F|(Seg (k+1)) & g = f|(Seg (k+1)) & k+1 <= len F holds
       Sum(G) = Sum(g)
     proof
       let G be FinSequence of ExtREAL, g be FinSequence of REAL;
       assume
A6:    G = F|(Seg (k+1)) & g = f|(Seg (k+1)) & k+1 <= len F;
then A7:   len G = k+1 & dom G = Seg (k+1) by FINSEQ_1:21;
       reconsider G1 = G|(Seg k) as FinSequence of ExtREAL by FINSEQ_1:23;
A8:   G = G1^<*G.(k+1)*> by A7,FINSEQ_3:61;
       reconsider G2=<*G.(k+1)*> as FinSequence of ExtREAL;
A9:   len g = k+1 & dom g = Seg (k+1) by A1,A6,FINSEQ_1:21;
       reconsider g1 = g|(Seg k) as FinSequence of REAL by FINSEQ_1:23;
A10:   g = g1^<*g.(k+1)*> by A9,FINSEQ_3:61;
       reconsider g2=<*g.(k+1)*> as FinSequence of REAL;
        k <= k+1 by NAT_1:11;
       then Seg k c= Seg (k+1) by FINSEQ_1:7;
then A11:   G1 = F|(Seg k) & g1 = f|(Seg k) by A6,FUNCT_1:82;
        k <= k+1 by NAT_1:11;
       then k <= len F by A6,XXREAL_0:2;
then A12:   Sum G1 = Sum g1 by A5,A11;
        not -infty in rng G1 & not -infty in rng <*G.(k+1)*>
       proof
          now assume A13: -infty in rng G1 or -infty in rng <*G.(k+1)*>;
            rng g1 c= REAL & rng g2 c= REAL;
           hence contradiction by A1,A6,A13,XREAL_0:def 1;
         end;
         hence thesis;
       end;
then A14:  Sum G = Sum G1 + Sum (<*G.(k+1)*>) by A8,CONVFUN1:7
            .= Sum G1 + G.(k+1) by CONVFUN1:5;
        Sum g = Sum g1 + g.(k+1) by A10,RVSUM_1:104;
       hence Sum G = Sum g by A1,A6,A12,A14,SUPINF_2:1;
     end;
     hence thesis;
   end;
A15:for k be Nat holds P[k] from NAT_1:sch 2(A2,A4);
    F|(Seg len F) = F|(len F) & f|(len f) = f|(Seg len f)
     by FINSEQ_1:def 15;
   then F|(Seg len F) = F & f|(Seg len f) = f by FINSEQ_1:79;
   hence thesis by A1,A15;
end;

theorem Th3:
for X be non empty set, S be SigmaField of X,
 f be PartFunc of X,ExtREAL st f is_simple_func_in S holds
  ex F be Finite_Sep_Sequence of S,
     a be FinSequence of ExtREAL st
        dom f = union rng F & dom F= dom a &
        (for n be Nat st n in dom F
          for x be set st x in F.n holds f.x = a.n) &
        for x be set st x in dom f holds
          ex ax be FinSequence of ExtREAL st
            dom ax = dom a &
            (for n be Nat st n in dom ax holds ax.n=a.n*(chi(F.n,X)).x)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   assume
 f is_simple_func_in S;
   then consider F be Finite_Sep_Sequence of S such that
A1: dom f = union rng F and
A2: for n being Nat,x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y by MESFUNC2:def 5;
   defpred P[Nat,Element of ExtREAL] means
   for x be set st x in F.$1 holds $2 = f.x;
A3: for k be Nat st k in Seg len F
    ex y be Element of ExtREAL st P[k,y]
   proof
     let k be Nat;
     assume k in Seg len F;
then A4: k in dom F by FINSEQ_1:def 3;
then A5: F.k in rng F by FUNCT_1:12;
      now per cases;
       case A6:F.k = {};
         take y = 0.;
          for x be set st x in F.k holds y = f.x by A6;
         hence thesis;
       end;
       case F.k <> {};
         then consider x1 be set such that
A7:      x1 in F.k by XBOOLE_0:def 1;
         take y = f.x1;
          for x be set st x in F.k holds y = f.x
         proof
           let x be set;
           assume A8:x in F.k;
A9:       rng F c= bool X by XBOOLE_1:1;
           then reconsider x1 as Element of X by A5,A7;
           reconsider x as Element of X by A5,A8,A9;
            f.x = f.x1 by A2,A4,A7,A8;
           hence thesis;
         end;
         hence thesis;
       end;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
A10: dom a = Seg len F &
    for k be Nat st k in Seg len F holds P[k,a.k]
    from FINSEQ_1:sch 5(A3);
A11:for n be Nat st n in dom F
    for x be set st x in F.n holds a.n = f.x
   proof
     let n be Nat;
     assume n in dom F;
     then n in Seg len F by FINSEQ_1:def 3;
     hence thesis by A10;
   end;
A12:for x be set st x in dom f holds ex ax be FinSequence of ExtREAL st
     dom ax = dom a &
     (for n be Nat st n in dom ax holds ax.n = a.n*(chi(F.n,X)).x)
   proof
     let x be set;
     assume x in dom f;
     deffunc Q(Nat) = a.$1 * (chi(F.$1,X)).x;
     consider ax be FinSequence of ExtREAL such that
A13:   len ax = len F &
      for k be Nat st k in Seg len F holds ax.k=Q(k)
      from FINSEQ_2:sch 1;
     take ax;
      for n be Nat st n in dom ax holds ax.n = a.n*(chi(F.n,X)).x
     proof
       let n be Nat;
       assume n in dom ax;
       then n in Seg len ax by FINSEQ_1:def 3;
       hence thesis by A13;
     end;
     hence thesis by A10,A13,FINSEQ_1:def 3;
   end;
   take F,a;
   thus thesis by A1,A10,A11,A12,FINSEQ_1:def 3;
end;

theorem Th4:
for X be set, F be FinSequence of X holds
   F is disjoint_valued iff
   (for i,j be Nat st i in dom F & j in dom F & i <> j holds F.i misses F.j)
proof
   let X be set;
   let F be FinSequence of X;
    now assume
A1:  for i,j be Nat st i in dom F & j in dom F & i <> j holds F.i misses F.j;
     thus F is disjoint_valued
     proof
        for x,y be set st x <> y holds F.x misses F.y
       proof
         let x,y be set;
         assume
A2:      x <> y;
         per cases;
           suppose A3:x in dom F & y in dom F;
              reconsider x,y as Element of NAT by A3;
              F.x misses F.y by A1,A2,A3;
             hence thesis;
           end;
           suppose not x in dom F;
             then F.x = {} by FUNCT_1:def 4;
             hence thesis by XBOOLE_1:65;
           end;
           suppose not y in dom F;
             then F.y = {} by FUNCT_1:def 4;
             hence thesis by XBOOLE_1:65;
           end;
       end;
       hence thesis by PROB_2:def 3;
     end;
   end;
   hence thesis by PROB_2:def 3;
end;

theorem Th5:
for X be non empty set, A be set, S be SigmaField of X,
    F be Finite_Sep_Sequence of S, G be FinSequence of S st
      dom G = dom F & for i be Nat st i in dom G holds
        G.i = A /\ F.i holds G is Finite_Sep_Sequence of S
proof
  let X be non empty set,
      A be set,
      S be SigmaField of X,
      F be Finite_Sep_Sequence of S,
      G be FinSequence of S such that
 A1: dom G = dom F and
 A2: for i be Nat st i in dom G holds G.i= A /\ F.i;
        now let i,j be Nat;
       assume A3: i in dom G & j in dom G & i <> j;
  then A4: F.i misses F.j by A1,Th4;
                    (A /\ F.i) /\ (A /\ F.j)
                  =A /\ (F.i /\ (A /\ F.j)) by XBOOLE_1:16
                 .=A /\ ((F.i /\ F.j) /\ A) by XBOOLE_1:16
                 .=A /\ ({} /\ A) by A4,XBOOLE_0:def 7
                 .={};
                 then A /\ F.i misses A /\ F.j by XBOOLE_0:def 7;
                 then G.i misses A /\ F.j by A2,A3;
                hence G.i misses G.j by A2,A3;
        end;
        hence thesis by Th4;
     end;

theorem Th6:
for X be non empty set, A be set, F,G be FinSequence of X st
  dom G = dom F & (for i be Nat st i in dom G holds G.i = A /\ F.i) holds
   union rng G = A /\ union rng F
proof
   let X be non empty set;
   let A be set;
   let F,G be FinSequence of X;
   assume that
A1:dom G = dom F and
A2:for i be Nat st i in dom G holds G.i = A /\ F.i;
   thus union rng G c= A /\ union rng F
   proof
      let r be set;
      assume r in union rng G;
      then consider E being set such that
A3:   r in E & E in rng G by TARSKI:def 4;
      consider s being set such that
A4:   s in dom G & E = G.s by A3,FUNCT_1:def 5;
       reconsider s as Element of NAT by A4;
       r in A /\ F.s by A2,A3,A4;
then A5:   r in A & r in F.s by XBOOLE_0:def 3;
       F.s in rng F by A1,A4,FUNCT_1:12;
      then r in union rng F by A5,TARSKI:def 4;
      hence thesis by A5,XBOOLE_0:def 3;
   end;
   let r be set;
   assume r in A /\ union rng F;
then A6:r in A & r in union rng F by XBOOLE_0:def 3;
   then consider E being set such that
A7: r in E & E in rng F by TARSKI:def 4;
   consider s being set such that
A8: s in dom F & E = F.s by A7,FUNCT_1:def 5;
    reconsider s as Element of NAT by A8;
    A /\ E = G.s by A1,A2,A8;
then A9:r in G.s by A6,A7,XBOOLE_0:def 3;
    G.s in rng G by A1,A8,FUNCT_1:12;
   hence thesis by A9,TARSKI:def 4;
end;

theorem Th7:
for X be set, F be FinSequence of X, i be Nat st i in dom F holds
  F.i c= union rng F & F.i /\ union rng F = F.i
proof
   let X be set, F be FinSequence of X, i be Nat;
   assume
 i in dom F;
   then F.i in rng F by FUNCT_1:12;
   hence F.i c= union rng F by ZFMISC_1:92;
   hence F.i /\ union rng F = F.i by XBOOLE_1:28;
 end;

theorem Th8:
for X be non empty set, S be SigmaField of X,
  M be sigma_Measure of S, F be Finite_Sep_Sequence of S holds
    dom F = dom (M*F)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let F be Finite_Sep_Sequence of S;
    dom M = S by FUNCT_2:def 1;
   then rng F c= dom M;
   hence thesis by RELAT_1:46;
end;

theorem Th9:
for X be non empty set, S be SigmaField of X,
  M be sigma_Measure of S, F be Finite_Sep_Sequence of S holds
    M.(union rng F) = Sum(M*F)
proof
   let X be non empty set;
   let S be SigmaField of X;
   let M be sigma_Measure of S;
   let F be Finite_Sep_Sequence of S;
   reconsider F1 = M*F as FinSequence of ExtREAL;
   consider f be Function of NAT,ExtREAL such that
A1: Sum(F1) = f.(len F1) &
    f.0 = 0. &
    for i be Element of NAT st i < len F1 holds f.(i+1) = f.i + F1.(i+1)
      by CONVFUN1:def 5;
   consider G being Sep_Sequence of S such that
A2: (union rng F = union rng G &
    (for n being Nat st n in dom F holds F.n = G.n) &
    (for m being Nat st not m in dom F holds G.m = {}))
    by MESFUNC2:33;
A3:M.(union rng F) = SUM(M*G) by A2,MEASURE1:def 11
                  .= sup(rng Ser(M*G));
   reconsider G1 = M*G as Num of (rng(M*G) qua
       denumerable non empty Subset of ExtREAL) by SUPINF_2:def 16;
A4:   Ser(M*G) = Ser(rng (M*G),G1) by SUPINF_2:def 21;
then A5:Ser(M*G).0 = G1.0 &
   for n be Element of NAT, y be R_eal st y = Ser(M*G).n holds
    Ser(M*G).(n+1) = y + G1.(n+1) by SUPINF_2:def 17;
    not 0 in Seg len F by FINSEQ_1:3;
   then not 0 in dom F by FINSEQ_1:def 3;
then A6:G.0 = {} by A2;
A7:dom G = NAT by FUNCT_2:def 1;
then A8:Ser(M*G).0 = M.(G.0) by A5,FUNCT_1:23 .= 0. by A6,MEASURE1:def 11;
A9:for i be Nat st i < len F1 holds Ser(M*G).(i+1) = Ser(M*G).i + F1.(i+1)
   proof
     let i be Nat;
     assume i < len F1;
     then 1 <= i+1 & i+1 <= len F1 by NAT_1:11,13;
     then i+1 in dom F1 by FINSEQ_3:27;
then A10:  i+1 in dom F by Th8;
      reconsider i as Element of NAT by ORDINAL1:def 13;
      Ser(M*G).(i+1) = Ser(M*G).i + G1.(i+1) by A4,SUPINF_2:def 17
     .= Ser(M*G).i + M.(G.(i+1)) by A7,FUNCT_1:23
     .= Ser(M*G).i + M.(F.(i+1)) by A2,A10;
     hence thesis by A10,FUNCT_1:23;
   end;
   defpred Q[Nat] means
    $1 <= len F1 implies Ser(M*G).$1 = f.$1;
A11:Q[0] by A1,A8;
A12:for k be Nat st Q[k] holds Q[k+1]
   proof
     let k be Nat;
     assume
A13:  Q[k];
A14:   for i be Nat st i < len F1 holds f.(i+1) = f.i + F1.(i+1)
      proof
        let i be Nat;
         i in NAT by ORDINAL1:def 13;
        hence thesis by A1;
      end;
     assume k+1 <= len F1;
then A15: k < len F1 by NAT_1:13;
     then Ser(M*G).(k+1) = f.k + F1.(k+1) by A9,A13;
     hence thesis by A14,A15;
   end;
A16:for k be Nat holds Q[k] from NAT_1:sch 2(A11,A12);
   defpred P[Nat] means
    $1 >= len F1 implies Ser(M*G).$1 = Ser(M*G).(len F1);
A17:P[0]
   proof
     assume
      0 >= len F1;
     then 0 = len F1;
     hence thesis;
   end;
A18:M*G is nonnegative by MEASURE2:1;
A19:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
A20:  P[k];
     assume
A21:  k+1 >= len F1;
      reconsider k as Element of NAT by ORDINAL1:def 13;
       now per cases;
       case
A22:    k >= len F1;
then A23:    Ser(M*G).(k+1) = Ser(M*G).(len F1) + G1.(k+1)
           by A4,A20,SUPINF_2:def 17;
         k+1 > len F1 by A22,NAT_1:13;
        then not k+1 in Seg len F1 by FINSEQ_1:3;
        then not k+1 in dom F1 by FINSEQ_1:def 3;
then A24:    not k+1 in dom F by Th8;
         G1.(k+1) = M.(G.(k+1)) by A7,FUNCT_1:23
         .= M.{} by A2,A24 .= 0. by MEASURE1:def 11;
        hence thesis by A23,SUPINF_2:18;
       end;
       case k < len F1;
        then k+1 <= len F1 by NAT_1:13;
        hence thesis by A21,XXREAL_0:1;
       end;
     end;
     hence thesis;
   end;
A25:for k be Nat holds P[k] from NAT_1:sch 2(A17,A19);
   defpred P1[Nat] means
   $1 < len F1 implies Ser(M*G).(len F1 - $1) <= Ser(M*G).(len F1);
A26:P1[0];
A27:for k be Nat st P1[k] holds P1[k+1]
   proof
     let k be Nat;
     assume
A28:   P1[k];
     assume
A29:   k+1 < len F1;
     then consider k' being Nat such that
     A30:   len F1 = k+1 + k' by NAT_1:10;
     reconsider k' as Element of NAT by ORDINAL1:def 13;
A31:  k <= k+1 by NAT_1:11;
      Ser(M*G).(k') <= Ser(M*G).(k' + 1) by A18,SUPINF_2:59;
     hence thesis by A28,A29,A30,A31,XXREAL_0:2;
   end;
A32:for i be Nat holds P1[i] from NAT_1:sch 2(A26,A27);
A33:for i be Nat st i < len F1 holds Ser(M*G).i <= Ser(M*G).(len F1)
   proof
     let i be Nat;
     assume i < len F1;
     then consider k be Nat such that
     A34:  len F1 = i + k by NAT_1:10;
     reconsider k as Element of NAT by ORDINAL1:def 13;
A35: k = len F1 - i & i = len F1 - k by A34;
      len F1 <= len F1 + i by NAT_1:11;
then A36: k <= len F1 by A35,XREAL_1:22;
      Ser(M*G).(len F1 - k) <= Ser(M*G).(len F1)
     proof
        now per cases by A36,REAL_1:def 5;
         case k = len F1;
           hence thesis by A8,A18,SUPINF_2:59;
         end;
         case k < len F1;
           hence thesis by A32;
         end;
       end;
       hence thesis;
     end;
     hence thesis by A34;
   end;
    for z be ext-real number st z in rng Ser(M*G) holds z <= Ser(M*G).(len F1)
   proof
     let z be ext-real number;
     assume z in rng Ser(M*G);
     then consider n be set such that
A37:   n in dom Ser(M*G) & z = Ser(M*G).n by FUNCT_1:def 5;
     reconsider n as Element of NAT by A37;
      now per cases;
       case n < len F1;
         hence thesis by A33,A37;
       end;
       case n >= len F1;
         hence thesis by A25,A37;
       end;
     end;
     hence thesis;
   end;
then A38:Ser(M*G).(len F1) is majorant of rng Ser(M*G) by SUPINF_1:def 9;
    Ser(M*G).(len F1) in rng Ser(M*G)
   proof
      dom (Ser(M*G)) = NAT by FUNCT_2:def 1;
     hence thesis by FUNCT_1:12;
   end;
   then Ser(M*G).(len F1) = sup(rng Ser(M*G)) by A38,SUPINF_1:80;
   hence thesis by A1,A3,A16;
end;

theorem Th10:
for F,G be FinSequence of ExtREAL, a be R_eal st
  ((a <> +infty & a <> -infty) or
   (for i be Nat st i in dom F holds F.i < 0.) or
   (for i be Nat st i in dom F holds 0. < F.i)) &
  dom F = dom G &
  for i be Nat st i in dom G holds G.i=a*F.i holds
    Sum(G)=a*Sum(F)
proof
   let F,G be FinSequence of ExtREAL;
   let a be R_eal;
   assume that
A1:(a <> +infty & a <> -infty) or
    (for i be Nat st i in dom F holds F.i < 0.) or
    (for i be Nat st i in dom F holds 0. < F.i) and
A2:dom F = dom G and
A3:for i be Nat st i in dom G holds G.i=a*F.i;
   consider f be Function of NAT,ExtREAL such that
A4: Sum(F) = f.(len F) & f.0 = 0. &
    for i being Element of NAT st i < len F holds f.(i+1)=f.i+F.(i+1)
    by CONVFUN1:def 5;
   consider g be Function of NAT,ExtREAL such that
A5: Sum(G) = g.(len G) & g.0 = 0. &
    for i being Element of NAT st i < len G holds g.(i+1)=g.i+G.(i+1)
    by CONVFUN1:def 5;
   defpred P[Nat] means
    $1 <= len G implies g.$1 = a*f.$1;
A6:  P[0] by A4,A5,EXTREAL1:def 1;
A7:  for i be Nat st P[i] holds P[i+1]
     proof
       let i be Nat;
       assume
A8:   P[i];
       assume A9:i+1 <= len G;
       reconsider i as Element of NAT by ORDINAL1:def 13;
A10:   i < len G by A9,NAT_1:13;
        1 <= i+1 by NAT_1:11;
then A11:   i+1 in dom G by A9,FINSEQ_3:27;
       then i+1 in Seg len F by A2,FINSEQ_1:def 3;
       then i+1 <= len F by FINSEQ_1:3;
then A12:   i < len F by NAT_1:13;
A13:   g.(i+1) = g.i + G.(i+1) by A5,A10 .= a*f.i + a*F.(i+1)
         by A3,A8,A9,A11,NAT_1:13;
        now per cases by A1;
        case a <> +infty & a <> -infty;
         then g.(i+1) = a*(f.i + F.(i+1)) by A13,EXTREAL1:29;
         hence thesis by A4,A12;
        end;
        case
A14:      for i be Nat st i in dom F holds F.i < 0.;
         defpred P1[Nat] means $1 < len F implies f.$1 <= 0.;
A15:      P1[0] by A4;
A16:      for i be Nat st P1[i] holds P1[i+1]
         proof
           let i be Nat;
           assume
A17:       P1[i];
           assume
A18:       i+1 < len F;
           reconsider i as Element of NAT by ORDINAL1:def 13;
            i < len F by A18,NAT_1:13;
then A19:       f.(i+1) = f.i + F.(i+1) by A4;
            1 <= i+1 & i+1 <= len F by A18,NAT_1:12;
           then i+1 in dom F by FINSEQ_3:27;
           then F.(i+1) < 0. by A14;
           hence thesis by A17,A18,A19,EXTREAL1:15,NAT_1:13;
         end;
          for i be Nat holds P1[i] from NAT_1:sch 2(A15,A16);
         then (f.i < 0. or f.i = 0.) & F.(i+1) < 0. by A2,A11,A12,A14;
         then g.(i+1) = a*(f.i + F.(i+1)) by A13,EXTREAL1:29;
         hence thesis by A4,A12;
        end;
        case
A20:      for i be Nat st i in dom F holds 0. < F.i;
         defpred P2[Nat] means
          $1 < len F implies 0. <= f.$1;
A21:      P2[0] by A4;
A22:      for i be Nat st P2[i] holds P2[i+1]
         proof
           let i be Nat;
           assume
A23:        P2[i];
           assume
A24:        i+1 < len F;
            reconsider i as Element of NAT by ORDINAL1:def 13;
             i < len F by A24,NAT_1:13;
then A25:       f.(i+1) = f.i + F.(i+1) by A4;
            1 <= i+1 & i+1 <= len F by A24,NAT_1:12;
           then i+1 in dom F by FINSEQ_3:27;
           then 0. < F.(i+1) by A20;
           hence thesis by A23,A24,A25,EXTREAL1:14,NAT_1:13;
         end;
          for i be Nat holds P2[i] from NAT_1:sch 2(A21,A22);
         then (0. < f.i or f.i = 0.) & 0. < F.(i+1) by A2,A11,A12,A20;
         then g.(i+1) = a*(f.i + F.(i+1)) by A13,EXTREAL1:29;
         hence thesis by A4,A12;
        end;
       end;
       hence thesis;
     end;
A26:  for i be Nat holds P[i] from NAT_1:sch 2(A6,A7);
      Seg len F = dom G by A2,FINSEQ_1:def 3
       .= Seg len G by FINSEQ_1:def 3;
     then a*Sum(F) = a*f.(len G) by A4,FINSEQ_1:8 .= g.(len G) by A26;
     hence thesis by A5;
end;

theorem Th11:
for F be FinSequence of REAL holds F is FinSequence of ExtREAL
 proof
   let F be FinSequence of REAL;
    rng F c= ExtREAL by NUMBERS:31,XBOOLE_1:1;
   hence thesis by FINSEQ_1:def 4;
 end;

definition
  let X be non empty set;
  let S be SigmaField of X;
  let f be PartFunc of X,ExtREAL;
  let F be Finite_Sep_Sequence of S;
  let a be FinSequence of ExtREAL;
  pred F,a are_Re-presentation_of f means :Def1:
    dom f = union rng F & dom F=dom a &
    for n be Nat st n in dom F
      for x be set st x in F.n holds f.x=a.n;
end;

theorem
   for X be non empty set,
      S be SigmaField of X,
      f be PartFunc of X,ExtREAL st
    f is_simple_func_in S holds
      ex F be Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL st
       F,a are_Re-presentation_of f
proof
let X be non empty set;
let S be SigmaField of X;
let f be PartFunc of X,ExtREAL such that
A1: f is_simple_func_in S;
consider F being Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL such that
     A2:  dom f = union rng F & dom F= dom a
          & (for n be Nat st n in dom F
              for x be set st x in F.n holds f.x=a.n )
          & for x be set st x in dom f
                 holds ex ax be FinSequence of ExtREAL
                           st dom ax= dom a &
                                (for n be Nat st n in dom ax
                                   holds ax.n=a.n*(chi(F.n,X)).x) by A1,Th3;
   take F,a;
   thus thesis by A2,Def1;
end;

theorem Th13:
for X be non empty set, S be SigmaField of X,
 F be Finite_Sep_Sequence of S holds
  ex G be Finite_Sep_Sequence of S st
    (union rng F = union rng G &
     (for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)))
proof
   let X be non empty set;
   let S be SigmaField of X;
   let F be Finite_Sep_Sequence of S;
   defpred P[Nat] means
   for F be Finite_Sep_Sequence of S st len F = $1 holds
    ex G be Finite_Sep_Sequence of S st
     (union rng F = union rng G &
     (for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)));
A1:P[0]
   proof
     let F be Finite_Sep_Sequence of S;
     assume
A2:   len F = 0;
     take G = F;
      for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)
     proof
       let n be Nat;
       assume A3:n in dom G;
        G = {} by A2,FINSEQ_1:25;
       hence thesis by A3,FINSEQ_1:26;
     end;
     hence thesis;
   end;
A4:for n be Nat st P[n] holds P[n+1]
   proof
     let n be Nat;
     assume A5:P[n];
     let F be Finite_Sep_Sequence of S;
     assume
A6:  len F = n+1;
     reconsider n as Element of NAT by ORDINAL1:def 13;
     reconsider F1 = F|(Seg n) as Finite_Sep_Sequence of S by MESFUNC2:36;
A7:     n <= len F by A6,NAT_1:11;
then A8:  len F1 = n by FINSEQ_1:21;
     then consider G1 be Finite_Sep_Sequence of S such that
A9:   (union rng F1 = union rng G1 &
       (for n be Nat st n in dom G1 holds
        (G1.n <> {} & ex m be Nat st m in dom F1 & F1.m = G1.n))) by A5;
A10: dom F = dom (F1^<*F.(n+1)*>) &
     for k be Nat st k in dom F holds F.k = (F1^<*F.(n+1)*>).k
     proof
       thus
     dom (F1^<*F.(n+1)*>) = Seg (len F1 + len <*F.(n+1)*>) by FINSEQ_1:def 7
        .= Seg (n + 1) by A8,FINSEQ_1:56
        .= dom F by A6,FINSEQ_1:def 3;
         let k be Nat;
         assume
A11:       k in dom F;
          now per cases;
           case
A12:          k in dom F1;
then A13:          (F1^<*F.(n+1)*>).k = F1.k by FINSEQ_1:def 7;
              k in Seg n by A7,A12,FINSEQ_1:21;
             then k <= n by FINSEQ_1:3;
             then (F|n).k = F.k by FINSEQ_3:121;
             hence thesis by A13,FINSEQ_1:def 15;
           end;
           case not k in dom F1;
             then not (k in Seg n) by A7,FINSEQ_1:21;
then A14:          not (1 <= k & k <= n) by FINSEQ_1:3;
              k in Seg (n+1) by A6,A11,FINSEQ_1:def 3;
then A15:          not (k < n + 1) & k <= n+1 by A14,FINSEQ_1:3,NAT_1:13;
              dom <*F.(n+1)*> = Seg 1 by FINSEQ_1:def 8;
then A16:          1 in dom <*F.(n+1)*> by FINSEQ_1:4,TARSKI:def 1;
              (F1^<*F.(n+1)*>).k = (F1^<*F.(n+1)*>).(len F1 + 1)
               by A8,A15,REAL_1:def 5
             .= <*F.(n+1)*>.1 by A16,FINSEQ_1:def 7
             .= F.(n+1) by FINSEQ_1:def 8;
             hence thesis by A15,REAL_1:def 5;
           end;
       end;
       hence thesis;
     end;
then A17:  F = F1^<*F.(n+1)*> by FINSEQ_1:17;
      ex G be Finite_Sep_Sequence of S st
      (union rng F = union rng G &
       (for k be Nat st k in dom G holds
        (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)))
     proof
        now per cases;
         case F.(n+1) = {};
then A18:        rng <*F.(n+1)*> = {{}} by FINSEQ_1:55;
           take G = G1;
            union rng F = union rng G &
           (for k be Nat st k in dom G holds
            (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k))
           proof
              rng F = rng F1 \/ rng <*F.(n+1)*> by A17,FINSEQ_1:44;
             hence union rng F
              = union rng F1 \/ union rng <*F.(n+1)*> by ZFMISC_1:96
             .= union rng F1 \/ {} by A18,ZFMISC_1:31
             .= union rng G by A9;
              for k be Nat st k in dom G holds
              (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)
             proof
               let k be Nat;
               assume
A19:             k in dom G;
               then consider m be Nat such that
A20:             m in dom F1 & F1.m = G.k by A9;
                 dom F1 c= dom F & F1.m = F.m by A17,A20,FINSEQ_1:39,def 7;
               hence thesis by A9,A19,A20;
             end;
             hence thesis;
           end;
           hence thesis;
         end;
         case
A21:        F.(n+1) <> {};
            1 <= n+1 by NAT_1:11;
           then n+1 in Seg (n+1) by FINSEQ_1:3;
           then n+1 in dom F by A6,FINSEQ_1:def 3;
           then F.(n+1) in rng F & rng F c= S by FUNCT_1:12;
then A22:           <*F.(n+1)*> is FinSequence of S by FINSEQ_1:95;
            for k,m be set st k <> m holds
            (G1^<*F.(n+1)*>).k misses (G1^<*F.(n+1)*>).m
           proof
             let k,m be set;
             assume
A23:           k <> m;
              now per cases;
               case
A24:              k in dom (G1^<*F.(n+1)*>) & m in dom (G1^<*F.(n+1)*>);
                 reconsider k,m as Element of NAT by A24;
                  now per cases;
                 case k = len (G1^<*F.(n+1)*>);
                   then k = len G1 + len <*F.(n+1)*> by FINSEQ_1:35;
then A25:                k = len G1 + 1 by FINSEQ_1:56;
                    1 <= len <*F.(n+1)*> by FINSEQ_1:56;
then A26:                (G1^<*F.(n+1)*>).k = <*F.(n+1)*>.1 by A25,FINSEQ_1:86
                    .= F.(n+1) by FINSEQ_1:def 8;
                    m in Seg (len (G1^<*F.(n+1)*>)) by A24,FINSEQ_1:def 3;
                   then m in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35;
                   then m in Seg (len G1 + 1) by FINSEQ_1:56;
                   then 1 <= m & m <= len G1 + 1 by FINSEQ_1:3;
                   then 1 <= m & m < len G1 + 1 by A23,A25,REAL_1:def 5;
                   then 1 <= m & m <= len G1 by NAT_1:13;
then A27:                m in dom G1 by FINSEQ_3:27;
then A28:               (G1^<*F.(n+1)*>).m = G1.m by FINSEQ_1:def 7;
                   consider m1 be Nat such that
A29:                m1 in dom F1 & F1.m1 = G1.m by A9,A27;
                    dom F1 c= dom F by A10,FINSEQ_1:39;
then A30:               m1 in dom F &
                   (G1^<*F.(n+1)*>).m = F.m1 by A17,A28,A29,FINSEQ_1:def 7;
                    m1 in Seg len F1 by A29,FINSEQ_1:def 3;
                   then m1 <= n by A8,FINSEQ_1:3;
                   then m1 <> n+1 by NAT_1:13;
                   hence thesis by A26,A30,PROB_2:def 3;
                   end;
                   case k <> len (G1^<*F.(n+1)*>);
                     then k <> len G1 + len <*F.(n+1)*> by FINSEQ_1:35;
then A31:                 k <> len G1 + 1 by FINSEQ_1:56;
                    k in Seg (len (G1^<*F.(n+1)*>)) by A24,FINSEQ_1:def 3;
                   then k in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35;
                   then k in Seg (len G1 + 1) by FINSEQ_1:56;
                   then 1 <= k & k <= len G1 + 1 by FINSEQ_1:3;
                   then 1 <= k & k < len G1 + 1 by A31,REAL_1:def 5;
                   then 1 <= k & k <= len G1 by NAT_1:13;
then A32:                k in dom G1 by FINSEQ_3:27;
then A33:               (G1^<*F.(n+1)*>).k = G1.k by FINSEQ_1:def 7;
                      now per cases;
                       case m = len (G1^<*F.(n+1)*>);
                   then m = len G1 + len <*F.(n+1)*> by FINSEQ_1:35;
then A34:                m = len G1 + 1 by FINSEQ_1:56;
                    1 <= len <*F.(n+1)*> by FINSEQ_1:56;
then A35:                (G1^<*F.(n+1)*>).m = <*F.(n+1)*>.1 by A34,FINSEQ_1:86
                    .= F.(n+1) by FINSEQ_1:def 8;
                   consider k1 be Nat such that
A36:                k1 in dom F1 & F1.k1 = G1.k by A9,A32;
                    dom F1 c= dom F by A10,FINSEQ_1:39;
then A37:               k1 in dom F &
                   (G1^<*F.(n+1)*>).k = F.k1 by A17,A33,A36,FINSEQ_1:def 7;
                    k1 in Seg len F1 by A36,FINSEQ_1:def 3;
                   then k1 <= n by A8,FINSEQ_1:3;
                   then k1 <> n+1 by NAT_1:13;
                   hence thesis by A35,A37,PROB_2:def 3;
                       end;
                       case m <> len (G1^<*F.(n+1)*>);
                        then m <> len G1 + len <*F.(n+1)*> by FINSEQ_1:35;
then A38:                     m <> len G1 + 1 by FINSEQ_1:56;
                    m in Seg (len (G1^<*F.(n+1)*>)) by A24,FINSEQ_1:def 3;
                   then m in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35;
                   then m in Seg (len G1 + 1) by FINSEQ_1:56;
                   then 1 <= m & m <= len G1 + 1 by FINSEQ_1:3;
                   then 1 <= m & m < len G1 + 1 by A38,REAL_1:def 5;
                   then 1 <= m & m <= len G1 by NAT_1:13;
                   then m in dom G1 by FINSEQ_3:27;
               then (G1^<*F.(n+1)*>).m = G1.m by FINSEQ_1:def 7;
                   hence thesis by A23,A33,PROB_2:def 3;
                       end;
                     end;
                     hence thesis;
                   end;
                 end;
                 hence thesis;
               end;
               case not(k in dom (G1^<*F.(n+1)*>) &
                        m in dom (G1^<*F.(n+1)*>));
                 then (G1^<*F.(n+1)*>).k = {} or (G1^<*F.(n+1)*>).m = {}
                   by FUNCT_1:def 4;
                 hence thesis by XBOOLE_1:65;
               end;
             end;
             hence thesis;
           end;
           then reconsider G = G1^<*F.(n+1)*> as Finite_Sep_Sequence of S
            by A22,FINSEQ_1:96,PROB_2:def 3;
           take G;
            union rng F = union rng G &
           for n be Nat st n in dom G holds
            (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)
           proof
              rng F = rng F1 \/ rng <*F.(n+1)*> by A17,FINSEQ_1:44;
             hence union rng F
              = union rng F1 \/ union rng <*F.(n+1)*> by ZFMISC_1:96
             .= union (rng G1 \/ rng <*F.(n+1)*>) by A9,ZFMISC_1:96
             .= union rng G by FINSEQ_1:44;
A39:          len G = len G1 + len <*F.(n+1)*> by FINSEQ_1:35
              .= len G1 + 1 by FINSEQ_1:56;
              for k be Nat st k in dom G holds
             (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)
             proof
               let k be Nat;
               assume
A40:             k in dom G;
                reconsider k as Element of NAT by ORDINAL1:def 13;
                now per cases;
                 case
A41:                k = len G;
                    dom <*F.(n+1)*> = Seg 1 by FINSEQ_1:55;
                   then 1 in dom <*F.(n+1)*> by FINSEQ_1:4,TARSKI:def 1;
then A42:                G.k = <*F.(n+1)*>.1 by A39,A41,FINSEQ_1:def 7
                      .= F.(n+1) by FINSEQ_1:def 8;
                    1 <= n+1 by NAT_1:11;
                   then n+1 in Seg (n+1) by FINSEQ_1:3;
                   then n+1 in dom F by A6,FINSEQ_1:def 3;
                   hence thesis by A21,A42;
                 end;
                 case
A43:                k <> len G;
                    1 <= k & k <= len G by A40,FINSEQ_3:27;
                   then 1 <= k & k < len G by A43,REAL_1:def 5;
                   then 1 <= k & k <= len G1 by A39,NAT_1:13;
then A44:                k in dom G1 by FINSEQ_3:27;
then A45:                G.k = G1.k by FINSEQ_1:def 7;
                    ex m be Nat st m in dom F & F.m = G.k
                   proof
                     consider m be Nat such that
A46:                  m in dom F1 & F1.m = G1.k by A9,A44;
A47:                 dom F1 c= dom F by A10,FINSEQ_1:39;
                     take m;
                     thus thesis by A17,A45,A46,A47,FINSEQ_1:def 7;
                   end;
                   hence thesis by A9,A44,A45;
                 end;
               end;
               hence thesis;
             end;
             hence thesis;
           end;
           hence thesis;
         end;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
A48:len F = len F;
    for n be Nat holds P[n] from NAT_1:sch 2(A1,A4);
   hence thesis by A48;
end;

Lm1:
for a be FinSequence of ExtREAL, p,N be Element of ExtREAL st
 N = len a &
 (for n be Nat st n in dom a holds a.n = p) holds
   Sum(a) = N * p
proof
   let a be FinSequence of ExtREAL;
   let p,N be Element of ExtREAL;
   assume that
A1: N = len a and
A2: for n be Nat st n in dom a holds a.n = p;
   defpred P[Nat] means
   for F be FinSequence of ExtREAL, p be Element of ExtREAL st
    $1 = len F &
    (for n be Nat st n in dom F holds F.n = p) holds
      ex N be Element of ExtREAL st N = $1 & Sum(F) = N * p;
A3:P[0]
   proof
      for F be FinSequence of ExtREAL, p be Element of ExtREAL st
      0 = len F &
      (for n be Nat st n in dom F holds F.n = p) holds
        ex N be Element of ExtREAL st N = 0 & Sum(F) = N * p
     proof
       let F be FinSequence of ExtREAL;
       let p be Element of ExtREAL;
       assume that
A4:     0 = len F and
         for n be Nat st n in dom F holds F.n = p;
       take N = 0.;
       consider f be Function of NAT,ExtREAL such that
A5:     Sum(F) = f.(len F) & f.0 = 0. &
        for i be Element of NAT st i < len F holds f.(i+1)=f.i+F.(i+1)
        by CONVFUN1:def 5;
       thus thesis by A4,A5,EXTREAL1:def 1;
     end;
     hence thesis;
   end;
A6:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
A7:   P[k];
      reconsider k as Element of NAT by ORDINAL1:def 13;
      for F be FinSequence of ExtREAL, p be Element of ExtREAL st
      k+1 = len F &
      (for n be Nat st n in dom F holds F.n = p) holds
       ex N be Element of ExtREAL st N = k+1 & Sum(F) = N * p
     proof
       let F be FinSequence of ExtREAL;
       let p be Element of ExtREAL;
       assume that
A8:     k+1 = len F and
A9:     (for n be Nat st n in dom F holds F.n = p);
        F <> {} by A8,FINSEQ_1:25;
       then consider G be FinSequence, v be set such that
A10:     F = G^<*v*> by FINSEQ_1:63;
       reconsider G as FinSequence of ExtREAL by A10,FINSEQ_1:50;
       reconsider H = <*v*> as FinSequence of ExtREAL by A10,FINSEQ_1:50;
A11:    k + 1 = len G + len <*v*> by A8,A10,FINSEQ_1:35
        .= len G + 1 by FINSEQ_1:56;
        dom <*v*> = Seg 1 by FINSEQ_1:55;
then A12:    1 in dom <*v*> by FINSEQ_1:4,TARSKI:def 1;
        1 <= k + 1 by NAT_1:11;
then A13:    k + 1 in dom F by A8,FINSEQ_3:27;
     v = <*v*>.1 by FINSEQ_1:57 .= F.(k + 1) by A10,A11,A12,FINSEQ_1:def 7
        .= p by A9,A13;
       then reconsider v as Element of ExtREAL;
        for n be Nat st n in dom G holds G.n = p
       proof
         let n be Nat;
         assume A14:n in dom G;
then A15:      1 <= n & n <= len G by FINSEQ_3:27;
          len G <= len G + 1 by NAT_1:11;
then A16:      1 <= n & n <= len G + 1 by A15,XXREAL_0:2;
          len F = len G + len <*v*> by A10,FINSEQ_1:35
          .= len G + 1 by FINSEQ_1:56;
         then n in dom F by A16,FINSEQ_3:27;
         then F.n = p by A9;
         hence thesis by A10,A14,FINSEQ_1:def 7;
       end;
       then consider N1 be Element of ExtREAL such that
A17:    N1 = k & Sum(G) = N1 * p by A7,A11;
       consider f be Function of NAT,ExtREAL such that
A18:    Sum(F) = f.(len F) & f.0 = 0. &
        for i being Element of NAT st i < len F holds f.(i+1) = f.i + F.(i+1)
         by CONVFUN1:def 5;
       consider g be Function of NAT,ExtREAL such that
A19:    Sum(G) = g.(len G) & g.0 = 0. &
        for i being Element of NAT st i < len G holds g.(i+1) = g.i + G.(i+1)
         by CONVFUN1:def 5;
A20:   for k1 be Nat st k1 <= len G holds f.k1 = g.k1
       proof
         defpred P2[Nat] means
         $1 <= len G implies f.$1 = g.$1;
A21:      P2[0] by A18,A19;
A22:      for k1 be Nat st P2[k1] holds P2[k1+1]
         proof
           let k1 be Nat;
           assume
A23:        P2[k1];
            reconsider k1 as Element of NAT by ORDINAL1:def 13;
            now assume
A24:          k1+1 <= len G;
A25:          k1 <= k1+1 by NAT_1:11;
A26:          k1 < len G by A24,NAT_1:13;
then A27:          g.(k1+1) = g.k1 + G.(k1+1) by A19;
              len F = len G + len <*v*> by A10,FINSEQ_1:35
             .= len G + 1 by FINSEQ_1:56;
             then k1 < len F by A26,NAT_1:13;
then A28:          f.(k1+1) = f.k1 + F.(k1+1) by A18;
              1 <= k1 + 1 by NAT_1:11;
             then k1 + 1 in dom G by A24,FINSEQ_3:27;
             hence thesis by A10,A23,A25,A27,A28,FINSEQ_1:def 7,XXREAL_0:2;
           end;
           hence thesis;
         end;
          for k1 be Nat holds P2[k1] from NAT_1:sch 2(A21,A22);
         hence thesis;
       end;
        k < len F by A8,NAT_1:13;
   then Sum(F) = f.k + F.(k+1) by A8,A18 .= g.k + F.(k+1) by A11,A20;
then A29:   Sum(F) = Sum(G) + p by A9,A11,A13,A19
        .= N1 * p + 1. * p by A17,EXTREAL2:4;
       take N = N1+1.;
        N = k+1 & Sum(F) = N * p
       proof
        thus N = k+1 by A17,MESFUNC1:def 8,SUPINF_2:1;
         now per cases;
         case k = 0;
          hence Sum F = N * p by A17,A29,EXTREAL1:29;
         end;
         case 0 < k;
          hence Sum(F) = N * p by A17,A29,EXTREAL1:29,MESFUNC1:def 8;
         end;
        end;
        hence Sum(F) = N * p;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
    for k be Nat holds P[k] from NAT_1:sch 2(A3,A6);
   then consider N' be Element of ExtREAL such that
A30: N' = len a & Sum(a) = N' * p by A2;
   thus thesis by A1,A30;
end;

Lm2:
for X be non empty set,
    S be SigmaField of X,
    f be PartFunc of X,ExtREAL st
    f is_simple_func_in S
   & (for x be set st x in dom f holds 0. <= f.x)
   & (for x be set st x in dom f holds 0. <> f.x) holds
        ex  F be Finite_Sep_Sequence of S,
              a be FinSequence of ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. < a.n &  a.n < +infty
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <= f.x and
A3:for x be set st x in dom f holds 0. <> f.x;
   consider F1 be Finite_Sep_Sequence of S such that
A4: dom f = union rng F1 and
A5: for n be Nat, x,y be Element of X st
    n in dom F1 & x in F1.n & y in F1.n
     holds f.x = f.y by A1,MESFUNC2:def 5;
   consider G be Finite_Sep_Sequence of S such that
A6: union rng F1 = union rng G and
A7: for n be Nat st n in dom G holds
     (G.n <> {} & ex m be Nat st m in dom F1 & F1.m = G.n) by Th13;
   set F = <*{}*>^G;
    rng <*{}*> = {{}} & {} in S by FINSEQ_1:55,MEASURE1:21;
   then rng <*{}*> c= S & rng G c= S by ZFMISC_1:37;
   then rng <*{}*> \/ rng G c= S by XBOOLE_1:8;
   then rng F c= S by FINSEQ_1:44;
   then reconsider F as FinSequence of S by FINSEQ_1:def 4;
    for x,y be set st x <> y holds F.x misses F.y
   proof
     let x,y be set;
     assume
A8:   x<>y;
      now per cases;
       case
A9:      x in dom F & y in dom F;
         reconsider x,y as Element of NAT by A9;
          x in Seg (len (<*{}*>^G)) & y in Seg (len (<*{}*>^G))
           by A9,FINSEQ_1:def 3;
then A10:      1 <= x & x <= len (<*{}*>^G) &
         1 <= y & y <= len (<*{}*>^G) by FINSEQ_1:3;
          now per cases;
           case
A11:          x = 1 or y = 1;
A12:          dom <*{}*> = Seg 1 by FINSEQ_1:55;
              now per cases by A11;
               case
A13:            x = 1;
               then x in dom <*{}*> by A12,FINSEQ_1:4,TARSKI:def 1;
               then F.x = <*{}*>.x by FINSEQ_1:def 7;
               then F.x = {} by A13,FINSEQ_1:def 8;
               hence thesis by XBOOLE_1:65;
               end;
               case
A14:            y = 1;
               then y in dom <*{}*> by A12,FINSEQ_1:4,TARSKI:def 1;
               then F.y = <*{}*>.y by FINSEQ_1:def 7;
               then F.y = {} by A14,FINSEQ_1:def 8;
               hence thesis by XBOOLE_1:65;
               end;
             end;
             hence thesis;
           end;
           case x <> 1 & y <> 1;
             then 1 < x & 1 < y by A10,REAL_1:def 5;
             then len <*{}*> < x & len <*{}*> < y by FINSEQ_1:57;
then A15:          F.x = G.(x - len <*{}*>) & F.y = G.(y - len <*{}*>)
               by A10,FINSEQ_1:37;
              x - (len <*{}*>) <> y - (len <*{}*>) by A8;
             hence thesis by A15,PROB_2:def 3;
           end;
         end;
         hence thesis;
       end;
       case not (x in dom F) or not (y in dom F);
         then F.x = {} or F.y = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
       end;
     end;
     hence thesis;
   end;
   then reconsider F as Finite_Sep_Sequence of S by PROB_2:def 3;
A16:union rng F = union (rng <*{}*> \/ rng G) by FINSEQ_1:44
   .= union ({{}} \/ rng G) by FINSEQ_1:55
   .= union {{}} \/ union rng G by ZFMISC_1:96
   .= {} \/ union rng G by ZFMISC_1:31
   .= dom f by A4,A6;
   defpred P[Nat,Element of ExtREAL] means
    (for x be Element of X st $1 in dom F & $1 = 1 holds $2 = 0.) &
    (for x be Element of X st $1 in dom F & $1 >= 2 & x in F.$1 holds
      $2 = f.x);
A17:for k be Nat st k in Seg len F
    ex y be Element of ExtREAL st P[k,y]
   proof
     let k be Nat;
     assume A18:k in Seg len F;
      ex y be Element of ExtREAL st P[k,y]
     proof
        now per cases;
         case A19:k = 1;
           take y = 0.;
A20:        for x be Element of X st k in dom F & k = 1 holds y = 0.;
            for x be Element of X st k in dom F & k >= 2 & x in F.k holds
            y = f.x by A19;
           hence thesis by A20;
         end;
         case A21:k <> 1;
            1 <= k & k <= len F by A18,FINSEQ_1:3;
then A22:       1 < k & k <= len <*{}*> + len G by A21,FINSEQ_1:35,REAL_1:def 5
;
        then 1+1 <= k & k <= len <*{}*> + len G by NAT_1:13;
then A23:        len <*{}*> + 1 <= k & k <= len <*{}*> + len G by FINSEQ_1:56;
           then consider k2 being Nat such that
           A24:         len <*{}*> + 1 + k2 = k by NAT_1:10;
           reconsider k2 as Element of NAT by ORDINAL1:def 13;
        1 + k2 = k - len <*{}*> by A24;
then A25:       1 <= 1 + k2 by A23,XREAL_1:21;
            k - len <*{}*> <= len G by A22,XREAL_1:22;
           then 1 + k2 in Seg len G by A24,A25,FINSEQ_1:3;
then A26:       1 + k2 in dom G by FINSEQ_1:def 3;
A27:       F.k = G.(k - len <*{}*>) by A23,FINSEQ_1:36 .= G.(1+k2) by A24;
           then F.k <> {} by A7,A26;
           then consider z be set such that
A28:        z in F.k by XBOOLE_0:def 1;
           take y = f.z;
           consider m1 be Nat such that
A29:        m1 in dom F1 & F1.m1 = G.(1+k2) by A7,A26;
            F.k in rng F1 & rng F1 c= S
            by A27,A29,FUNCT_1:12;
           then F.k in S & S c= bool X;
           then reconsider z as Element of X by A28;
A30:       for x be Element of X st k in dom F & k = 1 holds y = 0. by A21;
            for x be Element of X st k in dom F & k >= 2 & x in F.k holds
            y = f.x
           proof
             let x be Element of X;
             assume k in dom F & k >= 2 & x in F.k;
             then z in F1.m1 & x in F1.m1 by A27,A28,A29;
             hence thesis  by A5,A29;
           end;
           hence thesis by A30;
         end;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
A31: dom a = Seg len F &
    for n be Nat st n in Seg len F holds
       P[n,a.n] from FINSEQ_1:sch 5(A17);
A32:dom F = dom a &
   a.1 = 0. &
   (for n be Nat,x be Element of X st n in dom F & n >= 2 & x in F.n holds
    a.n = f.x)
   proof
     thus dom a = dom F by A31,FINSEQ_1:def 3;
      len F = len <*{}*> + len G by FINSEQ_1:35 .= 1 + len G by FINSEQ_1:56;
     then 1 <= len F by NAT_1:11;
then A33:  1 in Seg len F by FINSEQ_1:3;
  then 1 in dom F by FINSEQ_1:def 3;
     hence a.1 = 0. by A31,A33;
      for n be Nat, x be Element of X st n in dom F & n >= 2 & x in F.n holds
      a.n = f.x
     proof
       let n be Nat;
       let x be Element of X;
       assume
A34:     n in dom F & n >= 2 & x in F.n;
       then n in Seg len F by FINSEQ_1:def 3;
       hence thesis by A31,A34;
     end;
     hence thesis;
   end;
A35:for n' be Nat st n' in dom F
      for x be set st x in F.n' holds f.x=a.n'
    proof
      let n' be Nat;
      assume
A36:    n' in dom F;
       now per cases;
       case A37:n' = 1;
        thus for x be set st x in F.n' holds f.x = a.n'
        proof
          let x be set;
          assume A38:x in F.n';
           dom <*{}*> = {1} by FINSEQ_1:4,55;
          then 1 in dom <*{}*> by TARSKI:def 1;
          then F.1 = <*{}*>.1 by FINSEQ_1:def 7 .= {} by FINSEQ_1:57;
          hence thesis by A37,A38;
        end;
       end;
       case
A39:     n' <> 1;
         n' in Seg len F by A36,FINSEQ_1:def 3;
        then 1 <= n' & n' <= len F by FINSEQ_1:3;
        then 1 < n' by A39,REAL_1:def 5;
then A40:     1+1 <= n' by NAT_1:13;
        thus for x be set st x in F.n' holds f.x = a.n'
        proof
          let x be set;
          assume A41:x in F.n';
           F.n' in rng F & rng F c= S by A36,FUNCT_1:12;
          then F.n' in S & S c= bool X;
          then reconsider x as Element of X by A41;
           a.n' = f.x by A32,A36,A40,A41;
          hence thesis;
        end;
       end;
      end;
      hence thesis;
    end;
A42:for n be Nat st 2 <= n & n in dom a holds  0. < a.n &  a.n < +infty
   proof
     let n be Nat;
     assume that
A43:   2 <= n and
A44:   n in dom a;
      n in Seg len F by A32,A44,FINSEQ_1:def 3;
     then n <= len F by FINSEQ_1:3;
then A45:  n <= len <*{}*> + len G by FINSEQ_1:35;
      1+1 <= n by A43;
     then len <*{}*> + 1 <= n by FINSEQ_1:56;
then A46:  F.n = G.(n - len <*{}*>) by A45,FINSEQ_1:36 .= G.(n-1) by
FINSEQ_1:56;
A47:  dom <*{}*> = {1} by FINSEQ_1:4,55;
      1 <> n by A43;
     then not n in dom <*{}*> by A47,TARSKI:def 1;
     then consider k be Nat such that
A48:   k in dom G & n = len <*{}*> + k by A32,A44,FINSEQ_1:38;
      n = 1 + k by A48,FINSEQ_1:56;
     then F.n <> {} by A7,A46,A48;
     then consider x1 be set such that
A49:   x1 in F.n by XBOOLE_0:def 1;
      F.n in rng F by A32,A44,FUNCT_1:12;
then A50: F.n c= union rng F by ZFMISC_1:92;
 then x1 in dom f & dom f c= X by A16,A49;
     then reconsider x1 as Element of X;
A51: a.n = f.x1 by A32,A43,A44,A49;
      f is finite by A1,MESFUNC2:def 5;
then A52: |.f.x1.| < +infty by A16,A49,A50,MESFUNC2:def 1;
      0. <= f.x1 & 0. <> f.x1 by A2,A3,A16,A49,A50;
     hence thesis by A51,A52,EXTREAL2:58;
   end;
   take F,a;
   thus thesis by A16,A32,A35,A42,Def1;
end;

Lm3:
for X be non empty set,
    S be SigmaField of X,
    f be PartFunc of X,ExtREAL st
    f is_simple_func_in S
   & (for x be set st x in dom f holds 0. <= f.x)
   & (ex x be set st x in dom f & 0. = f.x) holds
        ex F be Finite_Sep_Sequence of S,
              a be FinSequence of  ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. < a.n &  a.n < +infty
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <= f.x and
A3:ex x be set st x in dom f & 0. = f.x;

   consider x0 be set such that
A4:x0 in dom f & 0.= f.x0 by A3;
   reconsider x0 as Element of X by A4;

   consider F1 be Finite_Sep_Sequence of S such that
A5:dom f = union rng F1 &
    (for n be Nat, x,y be Element of X st
     n in dom F1 & x in F1.n & y in F1.n holds f.x = f.y) by A1,MESFUNC2:def 5;

   consider V be set such that
A6:x0 in V & V in rng F1 by A4,A5,TARSKI:def 4;
   consider n0 be set such that
A7:n0 in dom F1 & V = F1.n0 by A6,FUNCT_1:def 5;
   reconsider n0 as Element of NAT by A7;
   set N={n where n is Element of NAT:
           n in dom F1 &
           for x be Element of X st x in F1.n holds f.x = 0.};
A8:for x be Element of X st x in F1.n0 holds f.x = 0. by A4,A5,A6,A7;
    for z be set st z in N holds z in dom F1
   proof
     let z be set;
     assume z in N;
     then consider n' be Element of NAT such that
A9:  z = n' & n' in dom F1 &
      for x be Element of X st x in F1.n' holds f.x = 0.;
     thus thesis by A9;
   end;
then A10:N c= dom F1 by TARSKI:def 3;
   then reconsider N as finite set by FINSET_1:13;
   consider P1 be FinSequence such that
A11: rng P1 = N & P1 is one-to-one by FINSEQ_4:73;
   set M = findom F1 \ N;
   consider P2 be FinSequence such that
A12: rng P2 = M & P2 is one-to-one by FINSEQ_4:73;
   set F0 = {Fn where Fn is Element of S :
                    Fn in rng F1 &
                    for x be Element of X st x in Fn holds f.x = 0.};
   set G1 = union F0;
    for F' be set st F' in F0 holds F' in bool X
   proof
     let F' be set;
     assume F' in F0;
     then consider Fn be Element of S such that
A13:  F'=Fn & Fn in rng F1 &
      for x be Element of X st x in Fn holds f.x = 0.;
      thus thesis by A13;
   end;
   then reconsider F0 as Subset-Family of X by TARSKI:def 3;
   defpred P[set,set] means
    ($1 in dom F1 implies $2 = F1.$1) &
    (not ($1 in dom F1) implies $2 = {});
A14:for x be set st x in NAT ex y be set st y in S & P[x,y]
   proof
     let x be set;
     assume x in NAT;
     then reconsider x as Element of NAT;
      now per cases;
      case A15:x in dom F1;
then A16:   F1.x in rng F1 & rng F1 c= S by FUNCT_1:12;
       reconsider y = F1.x as set;
       take y;
       thus thesis by A15,A16;
      end;
      case A17:not (x in dom F1);
       take y = {};
        y in S by PROB_1:10;
       hence thesis by A17;
      end;
     end;
     hence thesis;
   end;
   consider F2 be Function of NAT,S such that
A18:for x be set st x in NAT holds P[x,F2.x] from FUNCT_2:sch 1(A14);
    for z be set st z in rng F1 holds z in rng F2
   proof
     let z be set;
     assume z in rng F1;
     then consider n' be set such that
A19:  n' in dom F1 & z = F1.n' by FUNCT_1:def 5;
A20: F2.n' = F1.n' by A18,A19;
      dom F1 c= NAT & dom F2 = NAT by FUNCT_2:def 1;
     hence thesis by A19,A20,FUNCT_1:12;
   end;
   then rng F1 c= rng F2 by TARSKI:def 3;
then A21:rng F1 is countable by CARD_4:46;
    for z be set st z in F0 holds z in rng F1
   proof
     let z be set;
     assume z in F0;
     then consider Fn' be Element of S such that
A22:  z = Fn' & Fn' in rng F1 &
      for x be Element of X st x in Fn' holds f.x = 0.;
     thus thesis by A22;
   end;
then A23:F0 c= rng F1 by TARSKI:def 3;
    F1.n0 is Element of S & F1.n0 in rng F1 &
   for x be Element of X st x in F1.n0 holds f.x = 0.
   proof
    consider n0' be Nat such that
A24: n0 = n0' & n0' in dom F1 &
     for x be Element of X st x in F1.n0' holds f.x = 0. by A7,A8;
     F1.n0 in rng F1 & rng F1 c= S by A24,FUNCT_1:12;
    hence F1.n0 is Element of S;
    thus F1.n0 in rng F1 by A24,FUNCT_1:12;
    let x be Element of X;
    assume x in F1.n0;
    hence thesis by A24;
   end;
then A25:F1.n0 in F0;
   then reconsider F0 as N_Sub_set_fam of X by A21,A23,CARD_4:46;
    for F' be set st F' in F0 holds F' in S
   proof
     let F' be set;
     assume F' in F0;
     then consider Fn be Element of S such that
A26:  F'=Fn & Fn in rng F1 &
      for x be Element of X st x in Fn holds f.x = 0.;
     thus thesis by A26;
   end;
   then F0 c= S by TARSKI:def 3;
   then reconsider G1 as Element of S by MEASURE1:def 9;
A27:len P1 = card N & len P2 = card M by A11,A12,FINSEQ_4:77;
   reconsider L = Seg len F1 as finite set;
A28:N c= Seg len F1 by A10,FINSEQ_1:def 3;
   then card N <= card L by NAT_1:44;
   then len P1 <= len F1 & len F1 <= len F1 + 1 by A27,FINSEQ_1:78,NAT_1:11;
   then len P1 <= len F1 + 1 by XXREAL_0:2;
   then consider lenF being Nat such that
   A29:len F1 + 1 = len P1 + lenF by NAT_1:10;
   reconsider lenF as Element of NAT by ORDINAL1:def 13;
    M = (Seg len F1) \ N by FINSEQ_1:def 3;
   then card M = card (Seg len F1) - card N by A28,CARD_2:63;
then A30:len P2 = len F1 - len P1 + 1 - 1 by A27,FINSEQ_1:78;
   defpred P[Nat,set] means
    ($1 = 1 implies $2 = G1) &
    ($1 >=2 implies ex k' be Nat st k'=$1-1 & $2 = F1.(P2.k'));
A31:for k be Nat st k in Seg lenF ex U be Element of S st P[k,U]
   proof
     let k be Nat;
     assume A32:k in Seg lenF;
     per cases;
      suppose A33:k = 1;
       take U = G1;
       thus thesis by A33;
      end;
      suppose A34:k <> 1;
A35:   k >= 1 & k <= lenF by A32,FINSEQ_1:3;
       then k > 1 by A34,REAL_1:def 5;
then A36:   k >= 1+1 by NAT_1:13;
       consider k' being Nat such that
       A37:    k = 1 + k' by A35,NAT_1:10;
        k-1>=1 & k-1<=lenF -1 by A35,A36,XREAL_1:11,21;
       then k' in Seg len P2 by A29,A30,A37,FINSEQ_1:3;
       then k' in dom P2 by FINSEQ_1:def 3;
       then P2.k' in M by A12,FUNCT_1:12;
       then F1.(P2.k') in rng F1 & rng F1 c= S by FUNCT_1:12;
       then reconsider U = F1.(P2.k') as Element of S;
       take U;
       thus thesis by A34,A37;
      end;
   end;
   consider F be FinSequence of S such that
A38: dom F = Seg lenF &
    for k be Nat st k in Seg lenF holds
     P[k,F.k] from FINSEQ_1:sch 5(A31);
   defpred A[Nat,Element of ExtREAL] means
   for y be Element of X holds
    (y in F.$1 implies $2 = f.y) & (F.$1 = {} implies $2 = 1.);
A39:for k be Nat st k in Seg len F
    ex x be Element of ExtREAL st A[k,x]
   proof
     let k be Nat;
     assume k in Seg len F;
then A40: k in dom F by FINSEQ_1:def 3;
      now per cases;
      case k = 1;
then A41:   F.k = G1 by A38,A40;
A42:   for y be Element of X holds
        (y in F.k implies 0. = f.y) & (F.k = {} implies 0.=1.)
       proof
        let y be Element of X;
   y in F.k implies 0. = f.y
        proof
         assume y in F.k;
         then consider Y be set such that
A43:      y in Y & Y in F0 by A41,TARSKI:def 4;
         consider Fn be Element of S such that
A44:      Y = Fn & Fn in rng F1 &
          for x be Element of X st x in Fn holds f.x = 0. by A43;
         thus f.y = 0. by A43,A44;
        end;
        hence thesis by A6,A7,A25,A41,TARSKI:def 4;
       end;
       take x = 0.;
       thus thesis by A42;
      end;
      case A45:k <> 1;
A46:   1 <= k & k <= lenF by A38,A40,FINSEQ_1:3;
       then 1 < k by A45,REAL_1:def 5;
then A47:   1+1 <= k by NAT_1:13;
       then consider k1 be Nat such that
A48:    k1 = k-1 & F.k = F1.(P2.k1) by A38,A40;
        1 <= k1 & k1 <= len P2 by A29,A30,A46,A47,A48,XREAL_1:11,21;
       then k1 in Seg len P2 by FINSEQ_1:3;
       then k1 in dom P2 by FINSEQ_1:def 3;
       then P2.k1 in M by A12,FUNCT_1:12;
       then consider k' be set such that
A49:    k' in dom F1 & F.k = F1.k' by A48;
       reconsider k' as Element of NAT by A49;
        now per cases;
        case F.k = {};
then A50:     for y be Element of X holds
          (y in F.k implies 1. = f.y) & (F.k = {} implies 1.=1.);
         take x = 1.;
         thus thesis by A50;
        end;
        case F.k <> {};
         then consider y1 be set such that
A51:      y1 in F.k by XBOOLE_0:def 1;
          F.k in rng F & rng F c= S by A40,FUNCT_1:12;
         then F.k in S & S c= bool X;
         then reconsider y1 as Element of X by A51;
         take x = f.y1;
          for y be Element of X holds
          (y in F.k implies x = f.y) & (F.k = {} implies x=1.)
         proof
          let y be Element of X;
           y in F.k implies x = f.y
          proof
           assume y in F.k;
           then y in F1.k' & y1 in F1.k' by A49,A51;
           hence thesis by A5,A49;
          end;
          hence thesis by A51;
         end;
         hence thesis;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
A52: dom a = Seg len F &
    for n be Nat st n in Seg (len F) holds A[n,a.n]
    from FINSEQ_1:sch 5(A39);
A53:for n be Nat, x be Element of X st n in dom F & x in F.n holds a.n = f.x
   proof
    let n be Nat;
    let x be Element of X;
    assume A54:n in dom F & x in F.n;
    then n in Seg len F by FINSEQ_1:def 3;
    hence thesis by A52,A54;
   end;

    for z be set st z in union rng F holds z in union rng F1
   proof
    let z be set;
    assume z in union rng F;
    then consider Y be set such that
A55:  z in Y & Y in rng F by TARSKI:def 4;
    consider k be set such that
A56:  k in dom F & Y = F.k by A55,FUNCT_1:def 5;
    reconsider k as Element of NAT by A56;
     now per cases;
     case k = 1;
      then z in G1 by A38,A55,A56;
      then consider Y' be set such that
A57:    z in Y' & Y' in F0 by TARSKI:def 4;
      consider Fn' be Element of S such that
A58:    Y' = Fn' &
       Fn' in rng F1 &
       for x be Element of X st x in Fn' holds f.x = 0. by A57;
      thus thesis by A57,A58,TARSKI:def 4;
     end;
     case A59:k <> 1;
A60:   1 <= k & k <= lenF by A38,A56,FINSEQ_1:3;
      then 1 < k by A59,REAL_1:def 5;
then A61:   1 + 1 <= k by NAT_1:13;
      then consider k' be Nat such that
A62:    k' = k-1 & F.k = F1.(P2.k') by A38,A56;
       1 <= k' & k' <= len P2 by A29,A30,A60,A61,A62,XREAL_1:11,21;
      then k' in dom P2 by FINSEQ_3:27;
      then P2.k' in M by A12,FUNCT_1:12;
      then F1.(P2.k') in rng F1 by FUNCT_1:12;
      hence thesis by A55,A56,A62,TARSKI:def 4;
     end;
    end;
    hence thesis;
   end;
then A63:union rng F c= union rng F1 by TARSKI:def 3;
    for z be set st z in union rng F1 holds z in union rng F
   proof
    let z be set;
    assume z in union rng F1;
    then consider Y be set such that
A64:  z in Y & Y in rng F1 by TARSKI:def 4;
    consider m1 be set such that
A65:  m1 in dom F1 & Y = F1.m1 by A64,FUNCT_1:def 5;
    reconsider m1 as Element of NAT by A65;
     now per cases;
     case m1 in N;
      then consider m1' be Element of NAT such that
A66:    m1 = m1' & m1' in dom F1 &
       for x be Element of X st x in F1.m1' holds f.x = 0.;
    F1.m1' in rng F1 & rng F1 c= S by A66,FUNCT_1:12;
      then reconsider F1m1' = F1.m1' as Element of S;
A67:   z in Y & Y in F0 by A64,A65,A66;
       lenF >= 1+0 by A29,A30,XREAL_1:21;
      then 1 in Seg lenF by FINSEQ_1:3;
      then F.1 in rng F & F.1 = G1 by A38,FUNCT_1:12;
      then z in union F0 & union F0 in rng F by A67,TARSKI:def 4;
      hence thesis by TARSKI:def 4;
     end;
     case not (m1 in N);
      then m1 in M by A65,XBOOLE_0:def 4;
      then consider m1' be set such that
A68:    m1' in dom P2 & m1 = P2.m1' by A12,FUNCT_1:def 5;
       m1' in Seg (len P2) & Seg (len P2) c= NAT by A68,FINSEQ_1:def 3;
      then reconsider m1' as Element of NAT;
       m1' in Seg len P2 by A68,FINSEQ_1:def 3;
      then 1 <= m1' & m1' <= len P2 by FINSEQ_1:3;
then A69:   1+1 <= m1' + 1 &  m1'+1 <= lenF by A29,A30,XREAL_1:8;
      reconsider m2 = m1'+1 as Nat;
       1 <= m2 by A69,XXREAL_0:2;
then A70:  m2 in Seg lenF by A69,FINSEQ_1:3;
      then consider k' be Nat such that
A71:   k' = m2 -1 & F.m2 = F1.(P2.k') by A38,A69;
  F.m2 in rng F by A38,A70,FUNCT_1:12;
      hence thesis by A64,A65,A68,A71,TARSKI:def 4;
     end;
    end;
    hence thesis;
   end;
   then union rng F1 c= union rng F by TARSKI:def 3;
then A72:union rng F = dom f by A5,A63,XBOOLE_0:def 10;
A73:a.1 = 0. &
    dom F = dom a &
    (for n be Nat,x be Element of X st n in dom F & x in F.n holds
      a.n = f.x)
   proof
      0+1 <= lenF by A29,A30,XREAL_1:21;
then A74:1 in Seg lenF by FINSEQ_1:3;
then A75:1 in Seg len F by A38,FINSEQ_1:def 3;
A76:F.1 = G1 by A38,A74;
     reconsider F1n0 = F1.n0 as Element of S by A6,A7;
      F1n0 in F0 by A6,A7,A8;
     then x0 in F.1 by A6,A7,A76,TARSKI:def 4;
     hence a.1 = 0. by A4,A52,A75;
     thus dom F = dom a by A52,FINSEQ_1:def 3;
     thus thesis by A53;
   end;
A77:for n be Nat st n in dom F
     for x be set st x in F.n holds f.x=a.n
    proof
     let n be Nat;
     assume A78:n in dom F;
     then F.n in rng F & rng F c= S by FUNCT_1:12;
then A79:F.n in S & S c= bool X;
     let x be set;
     assume A80:x in F.n;
     then reconsider x as Element of X by A79;
      a.n = f.x by A53,A78,A80;
     hence thesis;
    end;
A81:for n,m be Nat st n in dom F & m in dom F & n = 1 & m <> 1 holds
     F.n misses F.m
    proof
     let n,m be Nat;
     assume
A82:   n in dom F & m in dom F & n = 1 & m <> 1;
then A83:  F.n = G1 by A38;
      1 <= m & m <= len F by A82,FINSEQ_3:27;
     then 1 < m by A82,REAL_1:def 5;
then A84:  1+1 <= m & m in Seg lenF by A38,A82,NAT_1:13;
     then consider k' be Nat such that
A85:   k' = m-1 & F.m = F1.(P2.k') by A38;
      m-1>=1 & m <= lenF by A84,FINSEQ_1:3,XREAL_1:21;
     then m-1>=1 & m-1<=lenF -1 by XREAL_1:11;
     then k' in Seg len P2 by A29,A30,A85,FINSEQ_1:3;
     then k' in dom P2 by FINSEQ_1:def 3;
     then P2.k' in M by A12,FUNCT_1:12;
then A86: P2.k' in dom F1 & not (P2.k' in N) by XBOOLE_0:def 4;
      F.n /\ F.m = {}
     proof
       assume F.n /\ F.m <> {};
       then consider v be set such that
A87:     v in F.n /\ F.m by XBOOLE_0:def 1;
A88:    v in F.n & v in F.m by A87,XBOOLE_0:def 3;
       then consider Y be set such that
A89:     v in Y & Y in F0 by A83,TARSKI:def 4;
       consider Fv be Element of S such that
A90:     Y = Fv & Fv in rng F1 &
        for x be Element of X st x in Fv holds f.x = 0. by A89;
       consider n' be set such that
A91:     n' in dom F1 & Fv = F1.n' by A90,FUNCT_1:def 5;
        reconsider n' as Element of NAT by A91;
   n' <> P2.k' by A86,A90,A91;
       then F1.(n') misses F1.(P2.k') by PROB_2:def 3;
   then F1.(n') /\ F1.(P2.k') = {} by XBOOLE_0:def 7;
       hence contradiction by A85,A88,A89,A90,A91,XBOOLE_0:def 3;
     end;
     hence thesis by XBOOLE_0:def 7;
    end;
A92:for n,m be Nat st n in dom F & m in dom F & n <> 1 & m <> 1 & n <> m holds
     F.n misses F.m
    proof
     let n,m be Nat;
     assume
A93:   n in dom F & m in dom F & n <> 1 & m <> 1 & n <> m;
then A94: 1 <= n & n <= lenF & 1 <= m & m <= lenF by A38,FINSEQ_1:3;
     then 1 < n & 1 < m by A93,REAL_1:def 5;
then A95:  1+1 <= n & 1+1 <= m by NAT_1:13;
     then consider k1 be Nat such that
A96:   k1 = n-1 & F.n = F1.(P2.k1) by A38,A93;
     consider k2 be Nat such that
A97:   k2 = m-1 & F.m = F1.(P2.k2) by A38,A93,A95;
      1 <= n-1 & n-1 <= lenF -1 & 1<=m-1 & m-1<=lenF -1
      by A94,A95,XREAL_1:11,21;
     then k1 in Seg len P2 & k2 in Seg len P2 by A29,A30,A96,A97,FINSEQ_1:3;
then A98:  k1 in dom P2 & k2 in dom P2 by FINSEQ_1:def 3;
      k1 <> k2 by A93,A96,A97;
     then P2.k1 <> P2.k2 by A12,A98,FUNCT_1:def 8;
     hence thesis by A96,A97,PROB_2:def 3;
    end;
     for x,y be set st x <> y holds F.x misses F.y
    proof
     let x,y be set;
     assume A99:x<>y;
      now per cases;
      case A100:x in dom F & y in dom F;
        reconsider x,y as Element of NAT by A100;
        now per cases;
        case A101:x = 1 or y = 1;
          now per cases by A101;
          case A102:x = 1;
           then y <> 1 by A99;
           hence thesis by A81,A100,A102;
          end;
          case A103:y = 1;
           then x <> 1 by A99;
           hence thesis by A81,A100,A103;
          end;
         end;
         hence thesis;
        end;
        case x <> 1 & y <> 1;
         hence thesis by A92,A99,A100;
        end;
       end;
       hence thesis;
      end;
      case A104:not (x in dom F) or not (y in dom F);
        now per cases by A104;
        case not (x in dom F);
         then F.x = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
        end;
        case not (y in dom F);
         then F.y = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
    end;
    then reconsider F as Finite_Sep_Sequence of S by PROB_2:def 3;
A105:for n be Nat st 2 <= n & n in dom a holds 0. < a.n & a.n < +infty
   proof
    let n be Nat;
    assume A106:2 <= n & n in dom a;
A107:f is finite by A1,MESFUNC2:def 5;
     0+1 <= lenF by A29,A30,XREAL_1:21;
then A108: 1 in dom F & n <> 1 by A38,A106,FINSEQ_1:3;
    then F.1 misses F.n by A73,A81,A106;
then A109: G1 misses F.n by A38,A108;
A110:1 <= n & n <= lenF by A38,A73,A106,FINSEQ_1:3;
    consider k1 be Nat such that
A111:  k1 = n-1 & F.n = F1.(P2.k1) by A38,A73,A106;
     1 + 1 <= n by A106;
    then 1 <= n-1 & n-1 <= lenF -1 by A110,XREAL_1:11,21;
    then k1 in Seg len P2 by A29,A30,A111,FINSEQ_1:3;
    then k1 in dom P2 by FINSEQ_1:def 3;
    then P2.k1 in M by A12,FUNCT_1:12;
then A112: F.n in rng F1 by A111,FUNCT_1:12;
     a.n <> 0. & 0. <= a.n & a.n < +infty
    proof
      now per cases;
      case F.n <> {};
       then consider y be set such that
A113:     y in F.n by XBOOLE_0:def 1;
A114:    F.n in rng F & rng F c= S by A73,A106,FUNCT_1:12;
       then F.n in S & S c= bool X;
       then reconsider y as Element of X by A113;
       reconsider Fn = F.n as Element of S by A114;
A115:    a.n = f.y by A52,A106,A113;
        G1 /\ F.n = {} by A109,XBOOLE_0:def 7;
then A116:    not y in G1 by A113,XBOOLE_0:def 3;
       thus a.n <> 0.
       proof
        assume a.n = 0.;
        then for x be Element of X st x in F.n holds f.x = 0. by A73,A106;
        then Fn in F0 by A112;
        hence contradiction by A113,A116,TARSKI:def 4;
       end;
        y in union rng F by A113,A114,TARSKI:def 4;
       then 0. <= f.y & |. f.y .| < +infty by A2,A5,A63,A107,MESFUNC2:def 1;
       hence 0. <= a.n & a.n < +infty by A115,EXTREAL2:58;
      end;
      case F.n = {};
       hence thesis by A52,A106,EXTREAL2:20,MESFUNC1:def 8;
      end;
     end;
     hence thesis;
    end;
    hence thesis;
   end;
   take F,a;
   thus thesis by A72,A73,A77,A105,Def1;
end;

theorem Th14:
for X be non empty set,
    S be SigmaField of X,
    f be PartFunc of X,ExtREAL st
      f is_simple_func_in S
      & for x be set st x in dom f holds 0. <= f.x holds
        ex F be Finite_Sep_Sequence of S,
              a be FinSequence of ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. < a.n & a.n < +infty
proof
   let X be non empty set;
   let S be SigmaField of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <= f.x;
   per cases;
     suppose ex x be set st x in dom f & 0. = f.x;
       hence thesis by A1,A2,Lm3;
     end;
     suppose for x be set st x in dom f holds 0. <> f.x;
       hence thesis by A1,A2,Lm2;
     end;
end;

theorem
   for X be non empty set,
      S be SigmaField of X,
      f be PartFunc of X,ExtREAL,
      F be Finite_Sep_Sequence of S,
      a be FinSequence of ExtREAL,
      x be Element of X
      st F,a are_Re-presentation_of f
          & x in dom f holds
   ex ax be FinSequence of ExtREAL st dom ax= dom a &
        (for n be Nat st n in dom ax
          holds ax.n=a.n*(chi(F.n,X)).x) & f.x=Sum(ax)
proof
let X be non empty set;
let S be SigmaField of X;
let f be PartFunc of X,ExtREAL;
let F be Finite_Sep_Sequence of S;
let a be FinSequence of ExtREAL;
let x be Element of X such that
A1: F,a are_Re-presentation_of f and
A2: x in dom f;
A3:dom f = union rng F & dom F = dom a &
  for n be Nat st n in dom F
   for x be set st x in F.n holds f.x = a.n by A1,Def1;
  deffunc F(Nat) = a.$1*(chi(F.$1,X)).x;
  consider ax be FinSequence of ExtREAL such that
A4:len ax = len a &
   for j be Nat st j in Seg len a holds
    ax.j = F(j) from FINSEQ_2:sch 1;
   take ax;
A5:dom ax = Seg len a by A4,FINSEQ_1:def 3;
    f.x = Sum(ax)
   proof
    consider Y be set such that
A6:  x in Y & Y in rng F by A2,A3,TARSKI:def 4;
    consider k be set such that
A7:  k in dom F & Y = F.k by A6,FUNCT_1:def 5;
    reconsider k as Element of NAT by A7;
A8: k in Seg len a by A3,A7,FINSEQ_1:def 3;
    consider SA be Function of NAT,ExtREAL such that
A9:  Sum(ax) = SA.(len ax) & SA.0 = 0. &
     for i be Element of NAT st i < len ax holds SA.(i+1)=SA.i + ax.(i+1)
      by CONVFUN1:def 5;
A10: for i be Nat st i in Seg len ax & i <> k holds ax.i = 0.
    proof
     let i be Nat;
     assume A11:i in Seg len ax & i <> k;
then A12:  ax.i = a.i * (chi(F.i,X)).x by A4;
      F.i misses F.k by A11,PROB_2:def 3;
     then F.i /\ F.k = {} by XBOOLE_0:def 7;
     then not (x in F.i) by A6,A7,XBOOLE_0:def 3;
     then chi(F.i,X).x = 0. by FUNCT_3:def 3;
     hence thesis by A12,EXTREAL1:def 1;
    end;
A13: for i be Nat st i in Seg len ax & i = k holds ax.i = f.x
    proof
     let i be Nat;
     assume A14:i in Seg len ax & i = k;
     then chi(F.i,X).x = 1. by A6,A7,FUNCT_3:def 3,MESFUNC1:def 8;
     then ax.i = a.i * 1. by A4,A14 .= a.i by EXTREAL2:4;
     hence thesis by A1,A6,A7,A14,Def1;
    end;
    defpred P[Nat] means
    $1 <= len ax implies
    ($1 < k implies SA.$1 = 0.) & ($1 >= k implies SA.$1 = f.x);
A15: P[0] by A8,A9,FINSEQ_1:3;
A16: for i be Nat st P[i] holds P[i+1]
    proof
     let i be Nat;
     assume A17:P[i];
     assume A18:i+1 <= len ax;
     reconsider i as Element of NAT by ORDINAL1:def 13;
      now per cases;
      case A19:i+1 < k;
A20:    i <= i+1 by NAT_1:11;
then A21:   i < k & i <= len ax by A18,A19,XXREAL_0:2;
        k <= len a by A8,FINSEQ_1:3;
then A22:    i < len ax by A4,A21,XXREAL_0:2;
       then 1 <= i+1 & i+1 <= len ax by NAT_1:11,13;
then A23:    i+1 in Seg len ax by FINSEQ_1:3;
        SA.(i+1) = 0. + ax.(i+1) by A9,A17,A19,A20,A22,XXREAL_0:2
               .= ax.(i+1) by SUPINF_2:18
               .= 0. by A10,A19,A23;
       hence thesis by A19;
      end;
      case A24:i+1 >= k;
        now per cases;
        case A25:k = i+1;
A26:     k <= len a by A8,FINSEQ_1:3;
         then i < len ax by A4,A25,NAT_1:13;
         hence SA.(i+1) = SA.i + ax.(i+1) by A9
                 .= ax.k by A4,A17,A25,A26,NAT_1:13,SUPINF_2:18
                 .= f.x by A4,A8,A13;
        end;
        case k <> i+1;
then A27:    k < i+1 by A24,REAL_1:def 5;
then A28:    k <= i & i < len ax by A18,NAT_1:13;
          1 <= i+1 by NAT_1:11;
then A29:     i+1 in Seg len ax by A18,FINSEQ_1:3;
         thus SA.(i+1) = SA.i + ax.(i+1) by A9,A28
                 .= f.x + 0. by A10,A17,A18,A27,A29,NAT_1:13
                 .= f.x by SUPINF_2:18;
        end;
       end;
       hence thesis by A24;
      end;
     end;
     hence thesis;
    end;
A30: for i be Nat holds P[i] from NAT_1:sch 2(A15,A16);
     len ax >= k by A4,A8,FINSEQ_1:3;
    hence thesis by A9,A30;
   end;
   hence thesis by A4,A5,FINSEQ_1:def 3;
end;

theorem
   for p be FinSequence of ExtREAL,
      q be FinSequence of REAL st p=q holds Sum(p)=Sum(q)
proof
   let p be FinSequence of ExtREAL;
   let q be FinSequence of REAL;
   assume
A1: p = q;
   consider P be Function of NAT,ExtREAL such that
A2: Sum(p) = P.(len p) &
    P.0 = 0. &
    for i be Element of NAT st
     i < len p holds P.(i+1) = P.i + p.(i+1) by CONVFUN1:def 5;
    now per cases;
     case
A3:   len q = 0;
       Sum(q) = addreal "**" q by RVSUM_1:def 13;
      hence thesis by A1,A2,A3,BINOP_2:2,FINSOP_1:def 1;
     end;
     case
A4:   len q <> 0;
       Sum(q) = addreal $$ q by RVSUM_1:def 13;
      then consider Q be Function of NAT,REAL such that
A5:    Q.1 = q.1 &
       (for n be Element of NAT st 0 <> n & n < len q holds
         Q.(n+1) = addreal.(Q.n,q.(n+1))) & Sum(q) = Q.(len q)
          by A4,FINSOP_1:def 1;
      defpred P1[Nat] means
      0 <> $1 & $1 <= len q implies P.$1 = Q.$1;
A6:   P1[0];
A7:   for k be Nat st P1[k] holds P1[k+1]
      proof
        let k be Nat;
        assume
A8:     P1[k];
        assume
A9:     0 <> k+1 & k+1 <= len q;
        reconsider k as Element of NAT by ORDINAL1:def 13;
        per cases;
          suppose
A10:        k = 0;
             k < len q by A9,NAT_1:13;
            then P.(k+1) = P.k + p.(k+1) by A1,A2
                   .= p.(k+1) by A2,A10,SUPINF_2:18;
            hence thesis by A1,A5,A10;
          end;
          suppose
A11:        k <> 0;
A12:        k < len q by A9,NAT_1:13;
then A13:        P.(k+1) = P.k + p.(k+1) by A1,A2;
             Q.(k+1) = addreal.(Q.k,q.(k+1)) by A5,A11,A12
                   .= Q.k + q.(k+1) by BINOP_2:def 9;
            hence thesis by A1,A8,A9,A11,A13,NAT_1:13,SUPINF_2:1;
          end;
      end;
       for n be Nat holds P1[n] from NAT_1:sch 2(A6,A7);
      hence thesis by A1,A2,A4,A5;
     end;
   end;
   hence thesis;
end;

theorem Th17:
for p be FinSequence of ExtREAL st
 (for n be Nat st n in dom p holds 0. <= p.n) &
 (ex n be Nat st n in dom p & p.n=+infty) holds
   Sum(p)=+infty
proof
   let p be FinSequence of ExtREAL;
   assume that
A1:for n be Nat st n in dom p holds 0. <= p.n and
A2:ex n be Nat st n in dom p & p.n=+infty;
   consider m be Nat such that
A3: m in dom p & p.m = +infty by A2;
   consider f be Function of NAT,ExtREAL such that
A4: Sum(p) = f.(len p) & f.0 = 0. &
    for i be Element of NAT st i < len p holds f.(i+1)=f.i + p.(i+1)
    by CONVFUN1:def 5;
   defpred P[Nat] means
   $1 <= len p implies
    ($1 < m implies 0. <= f.$1) & ($1 >= m implies f.$1 = +infty);
A5:P[0] by A3,A4,FINSEQ_3:27;
A6:for i be Nat st P[i] holds P[i+1]
   proof
    let i be Nat;
    assume A7:P[i];
    assume A8:i+1 <= len p;
    reconsider i as Element of NAT by ORDINAL1:def 13;
     now per cases;
     case A9:i+1 < m;
A10:   i <= i+1 by NAT_1:11;
       i < len p by A8,NAT_1:13;
then A11:   f.(i+1) = f.i + p.(i+1) by A4;
       1 <= i+1 by NAT_1:11;
      then i+1 in Seg len p by A8,FINSEQ_1:3;
      then i+1 in dom p by FINSEQ_1:def 3;
      then 0. <= p.(i+1) by A1;
      hence thesis by A7,A8,A9,A10,A11,EXTREAL2:7,XXREAL_0:2;
     end;
     case A12:i+1 >= m;
       now per cases;
       case A13:i+1 = m;
         i < len p by A8,NAT_1:13;
        then f.(i+1) = f.i + p.(i+1) by A4;
        hence f.(i+1) = +infty
         by A3,A7,A8,A13,NAT_1:13,SUPINF_2:def 2;
       end;
       case i+1 <> m;
then A14:    i+1 > m by A12,REAL_1:def 5;
         1 <= i+1 by NAT_1:11;
        then i+1 in Seg len p by A8,FINSEQ_1:3;
        then i+1 in dom p by FINSEQ_1:def 3;
then A15:    p.(i+1) <> -infty by A1;
         i < len p by A8,NAT_1:13;
        then f.(i+1) = f.i + p.(i+1) by A4;
        hence f.(i+1) = +infty by A7,A8,A14,A15,NAT_1:13,SUPINF_2:def 2;
       end;
      end;
      hence thesis;
     end;
    end;
    hence thesis;
   end;
A16:for i be Nat holds P[i] from NAT_1:sch 2(A5,A6);
    m in Seg len p by A3,FINSEQ_1:def 3;
   then len p >= m by FINSEQ_1:3;
   hence thesis by A4,A16;
end;

definition
let X be non empty set;
let S be SigmaField of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
  assume A1: f is_simple_func_in S
                & dom f <> {}
                & for x be set st x in dom f holds 0. <= f.x;
func integral(X,S,M,f) -> Element of ExtREAL means
   ex F be Finite_Sep_Sequence of S,
      a, x be FinSequence of ExtREAL st F,a are_Re-presentation_of f
         & a.1 =0.
         & (for n be Nat st 2 <= n & n in dom a holds
           0. < a.n & a.n < +infty )
         & dom x = dom F
         & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
         & it=Sum(x);
existence
proof
consider F be Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL such that
          A2: F,a are_Re-presentation_of f and
          A3: a.1 =0. and
          A4: for n be Nat st 2 <= n & n in dom a holds
              0. < a.n & a.n < +infty by A1,Th14;
deffunc G(set) = (a.$1)*((M*F).$1);
defpred P[set] means $1 in dom F;
A5:  for x be set st P[x] holds G(x) in ExtREAL;
consider p be PartFunc of NAT,ExtREAL such that
 A6: (for x be set holds x in dom p iff x in NAT & P[x]) &
       (for x be set st x in dom p holds p.x = G(x))
           from PARTFUN1:sch 3(A5);
   for x be set st x in dom p holds x in dom F by A6;
  then A7: dom p c= dom F by TARSKI:def 3;
       for x be set st x in dom F holds x in dom p by A6;
      then dom F c= dom p by TARSKI:def 3;
        then A8: dom p = dom F by A7,XBOOLE_0:def 10;
  then dom p = Seg len F by FINSEQ_1:def 3;
  then p is FinSequence & rng p c= ExtREAL by FINSEQ_1:def 2;
  then reconsider p as FinSequence of ExtREAL by FINSEQ_1:def 4;
  A9: for n be Nat st n in dom p holds p.n=a.n*(M*F).n by A6;
take Sum(p);
thus thesis by A2,A3,A4,A8,A9;
end;
uniqueness
proof
let s1,s2 be Element of ExtREAL such that
  A10: ex F1 be Finite_Sep_Sequence of S,
      a1, x1 be FinSequence of ExtREAL
      st F1,a1 are_Re-presentation_of f
         & a1.1 =0.
         & (for n be Nat st 2 <= n & n in dom a1
           holds  0. < a1.n & a1.n < +infty)
         & dom x1 = dom F1
         & (for n be Nat st n in dom x1 holds x1.n=a1.n*(M*F1).n)
         & s1=Sum(x1) and
  A11:
  ex  F2 be Finite_Sep_Sequence of S,
       a2 be FinSequence of ExtREAL,
       x2 be FinSequence of ExtREAL
       st F2,a2 are_Re-presentation_of f
         &  a2.1 =0.
         &  (for n be Nat st 2 <= n & n in dom a2
              holds  0. < a2.n & a2.n < +infty)
         &  dom x2 = dom F2
         &  (for n be Nat st n in dom x2 holds x2.n=a2.n*(M*F2).n)
         &  s2=Sum(x2);
   thus s1=s2
   proof
      consider F1 be Finite_Sep_Sequence of S,
      a1 be FinSequence of ExtREAL,
      x1 be FinSequence of ExtREAL such that
      A12:    F1,a1 are_Re-presentation_of f and
      A13:    a1.1 =0. and
      A14:  (for n be Nat st 2 <= n & n in dom a1
                         holds  0. < a1.n & a1.n < +infty) and
      A15: dom x1 = dom F1 and
      A16: (for n be Nat st n in dom x1 holds x1.n=a1.n*(M*F1).n) and
      A17: s1=Sum(x1) by A10;
      A18: dom F1 = dom a1 by A12,Def1;
      consider F2 be Finite_Sep_Sequence of S,
      a2 be FinSequence of ExtREAL,
      x2 be FinSequence of ExtREAL such that
      A19:    F2,a2 are_Re-presentation_of f and
      A20:    a2.1 =0. and
      A21:  (for n be Nat st 2 <= n & n in dom a2
                   holds  0. < a2.n & a2.n < +infty) and
      A22: dom x2 = dom F2 and
      A23: (for n be Nat st n in dom x2 holds x2.n=a2.n*(M*F2).n) and
      A24: s2=Sum(x2) by A11;
      A25: dom F2 = dom a2 by A19,Def1;
      A26:M is nonnegative & M.{} = 0. by MEASURE1:def 11;
      A27:for n be Nat st n in dom F1 holds 0. <= (M*F1).n
          proof
          let n be Nat;
            assume A28: n in dom F1;
            then A29: (M*F1).n=M.(F1.n) by FUNCT_1:23;
             F1.n in rng F1 by A28,FUNCT_1:12;
            hence thesis by A26,A29,SUPINF_2:58;
          end;
      A30:for n be Nat st n in dom F2 holds 0. <= (M*F2).n
          proof
          let n be Nat;
            assume A31: n in dom F2;
            then A32: (M*F2).n=M.(F2.n) by FUNCT_1:23;
             F2.n in rng F2 by A31,FUNCT_1:12;
            hence thesis by A26,A32,SUPINF_2:58;
          end;
        A33:for n be Nat st n in dom a1 holds 0. <= a1.n
          proof
          let n be Nat;
            assume A34: n in dom a1;
             now per cases;
             case n=1;
             thus a1.1=0. by A13;
             end;
             case A35:n<>1;
              n in Seg len a1 by A34,FINSEQ_1:def 3;
             then 1 <= n by FINSEQ_1:3;
             then 1 < n by A35,REAL_1:def 5;
             then 1+1 < n+1 by XREAL_1:10;
             then 2<=n by NAT_1:13;
             hence 0. <= a1.n by A14,A34;
             end;
            end;
            hence thesis;
            end;
      A36: for n be Nat st n in dom a2 holds 0. <= a2.n
          proof
          let n be Nat;
            assume A37: n in dom a2;
             now per cases;
             case n=1;
             thus a2.1=0. by A20;
             end;
             case A38:n<>1;
              n in Seg len a2 by A37,FINSEQ_1:def 3;
             then 1 <=n by FINSEQ_1:3;
             then 1 < n by A38,REAL_1:def 5;
             then 1+1 < n+1 by XREAL_1:10;
             then 2<=n by NAT_1:13;
              hence 0. <= a2.n by A21,A37;
             end;
            end;
            hence thesis;
            end;
        A39:for n be Nat st n in dom x1 holds 0. <= x1.n
          proof
          let n be Nat;
            assume A40: n in dom x1;
            then A41: 0. <= a1.n by A15,A18,A33;
             0. <= (M*F1).n by A15,A27,A40;
            then 0. <= (a1.n)*( (M*F1).n) by A41,EXTREAL2:45;
            hence thesis by A16,A40;
          end;
      A42:for n be Nat st n in dom x2 holds 0. <= x2.n
          proof
          let n be Nat;
            assume A43: n in dom x2;
            then A44: 0. <= a2.n by A22,A25,A36;
             0. <= (M*F2).n by A22,A30,A43;
            then 0. <= (a2.n)*( (M*F2).n) by A44,EXTREAL2:45;
            hence thesis by A23,A43;
          end;
    now per cases;
      case ex i,j be Nat st
                     i in dom F1 & j in dom F2
                     &2<= i & 2<=j  & M.(F1.i /\ F2.j)=+infty;
            then consider i,j be Nat such that
            A45: i in dom F1 & j in dom F2
                   &2<= i & 2<=j & M.(F1.i /\ F2.j)=+infty;
             A46:F1.i in rng F1 by A45,FUNCT_1:12;
              then A47:  F1.i is_measurable M by MEASURE1:def 12;
              A48:   F2.j in rng F2 by A45,FUNCT_1:12;
              then  F2.j is_measurable M by MEASURE1:def 12;
               then F1.i /\ F2.j is_measurable M by A47,MEASURE1:66;
              then A49:  F1.i /\ F2.j in S by MEASURE1:def 12;
              A50:  F1.i /\ F2.j c= F1.i by XBOOLE_1:17;
              A51:  F1.i /\ F2.j c= F2.j by XBOOLE_1:17;
                +infty <= M.(F1.i) by A45,A46,A49,A50,MEASURE1:62;
              then A52:M.(F1.i)= +infty by XXREAL_0:4;
                +infty <= M.(F2.j) by A45,A48,A49,A51,MEASURE1:62;
              then A53:M.(F2.j)= +infty by XXREAL_0:4;
              A54: (M*F1).i = +infty by A45,A52,FUNCT_1:23;
                 0. < a1.i by A14,A18,A45;
              then +infty = ( a1.i)*((M*F1).i) by A54,EXTREAL1:def 1;
               then x1.i=+infty by A15,A16,A45;
             then A55:  Sum(x1)=+infty by A15,A39,A45,Th17;
              A56: (M*F2).j = +infty by A45,A53,FUNCT_1:23;
                 0. < a2.j by A21,A25,A45;
              then +infty = (a2.j)*((M*F2).j) by A56,EXTREAL1:def 1;
               then x2.j=+infty by A22,A23,A45;
           hence s1=s2 by A17,A22,A24,A42,A45,A55,Th17;
      end;
     case A57: for i,j be Nat
      st  i in dom F1 & j in dom F2 & 2<= i & 2<=j  holds
      M.(F1.i /\ F2.j) <> +infty;
        set n= len x1;
        set m= len x2;
         ex a be Function of [:(Seg n),(Seg m) :],REAL
         st for i,j be Nat st i in Seg n & j in Seg m
         holds a.(i,j) = a1.i * (M.(F1.i /\ F2.j))
         proof
         deffunc F(set,set) = a1.$1 * (M.(F1.$1 /\ F2.$2));
         A58: for x,y be set st x in Seg n & y in Seg m
          holds F(x,y) in REAL
  proof
  let x,y be set such that
  A59: x in (Seg n)  & y in (Seg m);
      x in { k where k is Element of NAT : 1 <= k & k <= n }
     by A59,FINSEQ_1:def 1;
     then consider kx be Element of NAT such that
    A60: x=kx &  ( 1 <= kx & kx <= n );
     y in { k where k is Element of NAT : 1 <= k & k <= m }
    by A59,FINSEQ_1:def 1;
    then consider ky be Element of NAT such that
    A61: y=ky & ( 1 <= ky & ky <= m );
    A62: kx in dom F1 by A15,A59,A60,FINSEQ_1:def 3;
    A63: ky in dom F2 by A22,A59,A61,FINSEQ_1:def 3;
     F1.kx in rng F1 by A62,FUNCT_1:12;
    then A64:  F1.kx is_measurable M by MEASURE1:def 12;
     F2.ky in rng F2 by A63,FUNCT_1:12;
    then  F2.ky is_measurable M by MEASURE1:def 12;
      then F1.kx /\ F2.ky is_measurable M by A64,MEASURE1:66;
    then A65:  F1.kx /\ F2.ky in S by MEASURE1:def 12;
     now per cases;
        case A66:  not (2 <= kx & 2 <= ky);
             now per cases by A66;
              case A67: kx < 2;
               then kx <=1+1;
               then kx <= 1 by A67,NAT_1:8;
               then F(kx,ky) = 0. * (M.(F1.kx /\ F2.ky)) by A13,A60,XXREAL_0:1
               .=0 by EXTREAL1:def 1;
               hence F(kx,ky) in REAL;
               end;
              case A68: ky < 2;
               then ky <=1+1;
               then A69:ky <= 1 by A68,NAT_1:8;
                now per cases;
                 case F1.kx /\ F2.ky = {};
                    hence F(kx,ky) = a1.kx * 0. by MEASURE1:def 11
                                   .=0 by EXTREAL1:def 1;
                 end;
                 case  F1.kx /\ F2.ky <> {};
                   then consider x be set such that
                     A70: x in F1.kx /\ F2.ky by XBOOLE_0:def 1;
                     A71: x in F1.kx & x in F2.ky by A70,XBOOLE_0:def 3;
                     then a1.kx=f.x by A12,A62,Def1
                            .=a2.ky by A19,A63,A71,Def1
                            .=0. by A20,A61,A69,XXREAL_0:1;
                    hence F(kx,ky) = 0 by EXTREAL1:def 1;
                 end;
               end;
               hence F(kx,ky) in REAL;
               end;
            end;
           hence F(kx,ky) in REAL;
        end;
        case A72: 2 <= kx & 2 <= ky;
           then A73: M.(F1.kx /\ F2.ky) <> +infty by A57,A62,A63;
            M  is nonnegative by MEASURE1:def 11;
           then 0. <= M.(F1.kx /\ F2.ky)  by A65,SUPINF_2:58;
            then reconsider r3=M.(F1.kx /\ F2.ky) as Real
              by A73,XXREAL_0:14;
             a1.kx is Real
            proof
              A74: a1.kx <> +infty by A14,A18,A62,A72;
                0. <= a1.kx by A18,A33,A62;
           hence a1.kx is Real by A74,XXREAL_0:14;
            end;
            then reconsider r2=a1.kx as Real;
             a1.kx*M.(F1.kx /\ F2.ky) = r2*r3 by EXTREAL1:13;
           hence F(kx,ky) in REAL;
           end;
      end;
   hence thesis by A60,A61;
 end;
         consider f being Function of [:(Seg n),(Seg m) :],REAL such that
         A75: for x,y be set st x in Seg n & y in Seg m
           holds f.(x,y)=F(x,y) from BINOP_1:sch 2(A58);
           take f;
           thus thesis by A75;
         end;
    then consider a be Function of [:(Seg n),(Seg m) :],REAL such that
    A76: for i,j be Nat st i in Seg n & j in Seg m
         holds a.(i,j) = a1.i * (M.(F1.i /\ F2.j));
      ex p be FinSequence of REAL st dom p=Seg n
            & for i be Nat st i in dom p
                  holds ex r be FinSequence of REAL st
                        (dom r = Seg m
                         & p.i=Sum(r)
                         & for j be Nat st j in dom(r) holds r.j=a.[i,j] )
   proof
     defpred P[Nat,set] means
     ex r be FinSequence of REAL st
               dom(r) = Seg m & $2=Sum(r)
             & for j be Nat st j in dom(r) holds r.j=a.[$1,j];
     A77: for k be Nat,y1,y2 be set st
          k in Seg n & P[k,y1] & P[k,y2] holds y1=y2
          proof
          let k be Nat,y1,y2 be set such that k in Seg n and
             A78:   P[k,y1] and
             A79:   P[k,y2];
             consider r1 be FinSequence of REAL such that
                     A80: dom(r1) = Seg m and
                     A81: y1=Sum(r1) and
                     A82: for j be Nat st j in dom(r1)
                       holds r1.j=a.[k,j]  by A78;
             consider r2 be FinSequence of REAL such that
                     A83: dom(r2) = Seg m and
                     A84: y2=Sum(r2) and
                     A85: for j be Nat st j in dom(r2)
                       holds r2.j=a.[k,j] by A79;
              now let j be Nat;
                 assume A86: j in dom (r1);
                 hence r1.j = a.[k,j] by A82
                             .= r2.j by A80,A83,A85,A86;
                 end;
               hence thesis by A80,A81,A83,A84,FINSEQ_1:17;
          end;
     A87: for k be Nat st k in Seg n ex x be set st P[k,x]
     proof
     let k be Nat;
      assume k in Seg n;
       deffunc G(set)= a.[k,$1];
        consider r being FinSequence such that
          A88: len r = m and
          A89:  for i be Nat st i in Seg m holds r.i=G(i)
          from FINSEQ_1:sch 2;
          A90: dom(r) = Seg m by A88,FINSEQ_1:def 3;
           for i be Nat st i in dom r holds r.i in REAL
          proof
            let i be Nat;
            assume i in dom r;
            then A91: i in Seg m by A88,FINSEQ_1:def 3;
             a.[k,i] in REAL;
            hence r.i in REAL by A89,A91;
          end;
          then reconsider r as FinSequence of REAL by FINSEQ_2:14;
          take x=Sum(r);
           for i be Nat st i in Seg m holds r.i=G(i) by A89;
          hence thesis by A90;
     end;
     consider p be FinSequence such that
     A92: dom p = Seg n &
     for k be Nat st k in Seg n holds P[k,p.k]
     from FINSEQ_1:sch 1(A77,A87);
      for i be Nat st i in dom p holds p.i in REAL
     proof
     let i be Nat;
     assume i in dom p;
     then consider r be FinSequence of REAL such that
     A93:   dom(r) = Seg m & p.i=Sum(r)
                  & for j be Nat st j in dom(r) holds r.j=a.[i,j] by A92;
      thus p.i in REAL by A93;
     end;
     then reconsider p as FinSequence of REAL by FINSEQ_2:14;
     take p;
     thus thesis by A92;
   end;
    then consider p be FinSequence of REAL such that
      A94:    dom p=Seg n & for i be Nat st i in dom p
                  holds ex r be FinSequence of REAL st
                        (dom r = Seg m & p.i=Sum r
                         & for j be Nat st j in dom r holds r.j=a.[i,j]);
      ex   q be FinSequence of REAL st dom q=Seg m
           & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st
                       (dom s = Seg n & q.j=Sum s
                        & for i be Nat st i in dom s holds s.i=a.[i,j] )
  proof
   defpred P[Nat,set] means
                 ex s be FinSequence of REAL st
                       dom(s) = Seg n & $2=Sum(s)
                       & for i be Nat st i in dom(s) holds s.i=a.[i,$1];
     A95: for k be Nat,y1,y2 be set st
          k in Seg m & P[k,y1] & P[k,y2] holds y1=y2
               proof
          let k be Nat,y1,y2 be set such that k in Seg m and
             A96:   P[k,y1] and
             A97:   P[k,y2];
             consider s1 be FinSequence of REAL such that
                     A98: dom(s1) = Seg n and
                     A99: y1=Sum(s1) and
                     A100: for j be Nat st j in dom s1
                       holds s1.j=a.[j,k]  by A96;
             consider s2 be FinSequence of REAL such that
                     A101: dom(s2) = Seg n and
                     A102: y2=Sum(s2) and
                     A103: for j be Nat st j in dom(s2)
                       holds s2.j=a.[j,k] by A97;
              now let j be Nat;
                 assume A104: j in dom (s1);
                 hence s1.j = a.[j,k] by A100
                             .= s2.j by A98,A101,A103,A104;
                 end;
               hence thesis by A98,A99,A101,A102,FINSEQ_1:17;
          end;
     A105: for k be Nat st k in Seg m ex x be set st P[k,x]
      proof
          let k be Nat;
           assume  k in Seg m;
            deffunc G(set)= a.[$1,k];
             consider s being FinSequence such that
               A106: len s = n and
               A107:  for i be Nat st i in Seg n holds s.i=G(i)
                       from FINSEQ_1:sch 2;
               A108: dom(s) = Seg n by A106,FINSEQ_1:def 3;
                for i be Nat st i in dom s holds s.i in REAL
               proof
                 let i be Nat;
                 assume i in dom s;
                 then A109: i in Seg n by A106,FINSEQ_1:def 3;
                  a.[i,k] in REAL;
                 hence s.i in REAL by A107,A109;
               end;
               then reconsider s as FinSequence of REAL by FINSEQ_2:14;
               take x=Sum(s);
                for i be Nat st i in Seg n holds s.i=G(i) by A107;
               hence thesis by A108;
          end;
     consider p be FinSequence such that
     A110: dom p = Seg m &
     for k be Nat st k in Seg m holds P[k,p.k]
     from FINSEQ_1:sch 1(A95,A105);
      for i be Nat st i in dom p holds p.i in REAL
          proof
          let i be Nat;
          assume i in dom p;
          then consider s be FinSequence of REAL such that
          A111: dom(s) = Seg n & p.i=Sum(s)
                       & for j be Nat st j in dom(s)
                       holds s.j=a.[j,i] by A110;
           thus p.i in REAL by A111;
          end;
     then reconsider p as FinSequence of REAL by FINSEQ_2:14;
     take p;
     thus thesis by A110;
   end;
    then consider q be FinSequence of REAL such that
     A112:  dom q=Seg m & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st (dom(s) = Seg n
                        & q.j=Sum(s)
                        & for i be Nat st i in dom(s) holds s.i=a.[i,j]);
     A113: Sum(p)=Sum(q) by A94,A112,Th1;
      p=x1
     proof
       A114: dom p = dom x1 by A94,FINSEQ_1:def 3;
        for k be Nat st k in dom p holds p.k = x1.k
       proof
       let k be Nat;
         assume A115: k in dom p;
         then consider r be FinSequence of REAL such that
          A116:dom r = Seg m and
          A117:p.k=Sum r and
          A118:for j be Nat st j in dom r holds r.j=a.[k,j] by A94;
          A119: for j be Nat st j in dom r holds
               r.j=a1.k * (M.(F1.k /\ F2.j))
               proof
                 let j be Nat;
                  assume
                  A120: j in dom r;
                  hence r.j=a.(k,j) by A118
                     .=a1.k * (M.(F1.k /\ F2.j)) by A76,A94,A115,A116,A120;
               end;
            ex F11 be Finite_Sep_Sequence of S st union rng F11 = F1.k &
                  dom F11=dom r &
                  for j be Nat st j in dom r holds F11.j=F1.k /\ F2.j
           proof
           deffunc G(set)=F1.k /\ F2.$1;
           consider F being FinSequence such that
             A121: len F = m and
             A122: for i be Nat st i in Seg m holds F.i=G(i)
             from FINSEQ_1:sch 2;
A123:        for i being Nat st i in Seg m holds F.i=G(i) by A122;
             A124: dom F = Seg m by A121,FINSEQ_1:def 3;
              for i be Nat st i in dom F holds F.i in S
             proof
                let i be Nat;
                assume A125: i in dom F;
                then i in Seg m by A121,FINSEQ_1:def 3;
                 then A126: i in dom F2 by A22,FINSEQ_1:def 3;
                   F1.k in rng F1 by A15,A114,A115,FUNCT_1:12;
                  then A127: F1.k is_measurable M by MEASURE1:def 12;
                   F2.i in rng F2 by A126,FUNCT_1:12;
                  then F2.i is_measurable M by MEASURE1:def 12;
                    then F1.k /\ F2.i is_measurable M by A127,MEASURE1:66;
                  then F1.k /\ F2.i in S by MEASURE1:def 12;
                  hence F.i in S by A122,A124,A125;
             end;
             then reconsider F as FinSequence of S by FINSEQ_2:14;
             A128: dom F = dom F2 by A22,A124,FINSEQ_1:def 3;
             then reconsider F as Finite_Sep_Sequence of S by A123,A124,Th5;
             take F;
              union rng F = F1.k
             proof
                thus union rng F = F1.k /\ union rng F2
                   by A123,A124,A128,Th6
                                .= F1.k /\ dom f by A19,Def1
                                .= F1.k /\ union rng F1 by A12,Def1
                                .= F1.k by A15,A114,A115,Th7;
             end;
             hence thesis by A116,A121,A122,FINSEQ_1:def 3;
            end;
           then consider F11 be Finite_Sep_Sequence of S such that
           A129: union rng F11 = F1.k & dom F11=dom r &
               for j be Nat st j in dom r holds F11.j=F1.k /\ F2.j;
           A130: Sum(M*F11) = M.((F1.k)) by A129,Th9;
           reconsider rr=r as FinSequence of ExtREAL by Th11;
           A131: for j be Nat st j in dom rr holds rr.j=a1.k * (M*F11).j
           proof
             let j be Nat;
              assume
              A132:j in dom rr;
              hence rr.j = a1.k * (M.(F1.k /\ F2.j)) by A119
                   .=a1.k *(M.(F11.j)) by A129,A132
                   .=a1.k *(M*F11).j  by A129,A132,FUNCT_1:23;
          end;
           k in Seg len a1 by A15,A18,A114,A115,FINSEQ_1:def 3;
then A133:      1 <= k by FINSEQ_1:3;
A134:       a1.k <> -infty & a1.k <> +infty
          proof
            now per cases;
            case k = 1;
              hence thesis by A13;
            end;
            case k <> 1;
              then 1 < k by A133,REAL_1:def 5;
              then 1+1 <= k by NAT_1:13;
              hence thesis by A14,A15,A18,A114,A115;
            end;
           end;
           hence thesis;
          end;
           dom rr = dom (M*F11) by A129,Th8;
           then Sum rr=(a1.k)*Sum(M*F11) by A131,A134,Th10;
            then Sum r=(a1.k)*M.((F1.k)) by A130,Th2
                         .=a1.k*((M*F1).k) by A15,A114,A115,FUNCT_1:23;
           hence thesis by A16,A114,A115,A117;
       end;
       hence thesis by A114,FINSEQ_1:17;
     end;
     then A135: Sum p = Sum x1 by Th2;
      q=x2
     proof
       A136: dom q = dom x2 by A112,FINSEQ_1:def 3;
        for k be Nat st k in dom q holds q.k = x2.k
       proof
       let k be Nat;
         assume A137: k in dom q;
         then consider s be FinSequence of REAL such that
          A138:dom s = Seg n and
          A139:q.k=Sum s and
          A140:for i be Nat st i in dom s holds s.i=a.[i,k] by A112;
          A141: for i be Nat st i in dom s holds
               s.i=a1.i * (M.(F1.i /\ F2.k))
               proof
                 let i be Nat;
                  assume
                  A142: i in dom s;
                  hence s.i=a.(i,k) by A140
                     .=a1.i * (M.(F1.i /\ F2.k)) by A76,A112,A137,A138,A142;
               end;
          A143: for i be Nat st i in dom s holds
               s.i=a2.k * (M.(F1.i /\ F2.k))
               proof
                 let i be Nat;
                  assume
                  A144: i in dom s;
                    now per cases;
                      case A145: F1.i /\ F2.k = {};
                         then M.(F1.i /\ F2.k)=0. by MEASURE1:def 11;
                         hence s.i = a1.i * 0. by A141,A144
                                .=0. by EXTREAL1:def 1
                                .=a2.k *0. by EXTREAL1:def 1
                                .=a2.k *M.(F1.i /\ F2.k) by A145,
MEASURE1:def 11;
                      end;
                      case F1.i /\ F2.k <> {};
                        then consider x be set such that
                          A146: x in F1.i /\ F2.k by XBOOLE_0:def 1;
                          A147: x in F1.i & x in F2.k by A146,XBOOLE_0:def 3;
                           dom p = dom x1 by A94,FINSEQ_1:def 3;
                          then a1.i=f.x by A12,A15,A94,A138,A144,A147,Def1
                             .=a2.k by A19,A22,A136,A137,A147,Def1;
                         hence s.i = a2.k*M.(F1.i /\ F2.k) by A141,A144;
                      end;
                    end;
                 hence thesis;
               end;
            ex F21 be Finite_Sep_Sequence of S st union rng F21 = F2.k &
                  dom F21=dom(s) &
                  for j be Nat st j in dom(s) holds F21.j=F1.j /\ F2.k
           proof
           deffunc G(set)= F2.k /\ F1.$1;
           consider F being FinSequence such that
             A148: len F = n and
             A149: for i be Nat st i in Seg n holds F.i=G(i)
             from FINSEQ_1:sch 2;
             A150: dom F = Seg n by A148,FINSEQ_1:def 3;
              for i be Nat st i in dom F holds F.i in S
             proof
                let i be Nat;
                assume A151: i in dom F;
                then i in Seg n by A148,FINSEQ_1:def 3;
                 then i in dom F1 by A15,FINSEQ_1:def 3;
                  then F1.i in rng F1 by FUNCT_1:12;
                  then A152: F1.i is_measurable M by MEASURE1:def 12;
                   F2.k in rng F2 by A22,A136,A137,FUNCT_1:12;
                  then F2.k is_measurable M by MEASURE1:def 12;
                    then F1.i /\ F2.k is_measurable M by A152,MEASURE1:66;
                    then F1.i /\ F2.k in S by MEASURE1:def 12;
                  hence F.i in S by A149,A150,A151;
             end;
             then reconsider F as FinSequence of S by FINSEQ_2:14;
             A153: dom F = dom F1 by A15,A150,FINSEQ_1:def 3;
             then reconsider F as Finite_Sep_Sequence of S by A149,A150,Th5;
             take F;
              union rng F = F2.k
             proof
               thus union rng F = F2.k /\ union rng F1
                 by A149,A150,A153,Th6
                               .= F2.k /\ dom f by A12,Def1
                               .= F2.k /\ union rng F2 by A19,Def1
                               .= F2.k by A22,A136,A137,Th7;
             end;
             hence thesis by A138,A148,A149,FINSEQ_1:def 3;
            end;
           then consider F21 be Finite_Sep_Sequence of S such that
       A154: union rng F21 = F2.k & dom F21 = dom s &
                  for i be Nat st i in dom s holds F21.i=F1.i /\ F2.k;
            A155: Sum(M*F21) = M.((F2.k)) by A154,Th9;
            reconsider ss=s as FinSequence of ExtREAL by Th11;
            A156: for j be Nat st j in dom ss holds ss.j=a2.k * (M*F21).j
            proof
              let j be Nat;
              assume
          A157:j in dom ss;
              hence ss.j = a2.k * (M.(F1.j /\ F2.k)) by A143
                      .=a2.k *(M.(F21.j)) by A154,A157
                      .=a2.k *(M*F21).j by A154,A157,FUNCT_1:23;
           end;
            k in Seg len a2 by A22,A25,A136,A137,FINSEQ_1:def 3;
then A158:       1 <= k by FINSEQ_1:3;
A159:        a2.k <> -infty & a2.k <> +infty
           proof
            per cases;
             suppose k = 1;
               hence thesis by A20;
             end;
             suppose k <> 1;
               then 1 < k by A158,REAL_1:def 5;
               then 1+1 <= k by NAT_1:13;
               hence thesis by A21,A22,A25,A136,A137;
             end;
           end;
            dom ss=dom (M*F21) by A154,Th8;
           then Sum(ss)=(a2.k)*Sum(M*F21) by A156,A159,Th10;
           then Sum(s)=(a2.k)*M.((F2.k)) by A155,Th2
                         .=a2.k*((M*F2).k)
                         by A22,A136,A137,FUNCT_1:23;
           hence thesis by A23,A136,A137,A139;
       end;
       hence thesis by A136,FINSEQ_1:17;
      end;
     hence s1=s2 by A17,A24,A113,A135,Th2;
      end;
    end;
  hence thesis;
  end;
 end;
end;

begin :: Additional Lemmas

theorem :: RVSUM_1:110
 for a be FinSequence of ExtREAL, p,N be Element of ExtREAL st
 N = len a &
 (for n be Nat st n in dom a holds a.n = p) holds
   Sum(a) = N * p by Lm1;
