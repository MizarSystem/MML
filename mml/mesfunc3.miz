:: Lebesgue Integral of Simple Valued Function
::  by Yasunari Shidama and Noboru Endou
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary PARTFUN1, SUPINF_1, MEASURE1, RELAT_1, FUNCT_1, COMPLEX1, SUPINF_2,
      BOOLE, ARYTM_1, TARSKI, ARYTM_3, FUNCOP_1, RVSUM_1, RLVECT_1, FUNCT_3,
      GROUP_1, PROB_2, FINSEQ_1, FINSEQ_2, INTEGRA1, MESFUNC2, MESFUNC3,
      VECTSP_1, SETWISEO, ORDINAL2, FINSOP_1, FINSET_1, SETFAM_1, CARD_4,
      CARD_1;
 notation TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, XREAL_0, NAT_1,
      REAL_1, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2, SETWOP_2, FINSEQ_1,
      SETFAM_1, FUNCOP_1, RAT_1, FINSOP_1, FUNCT_3, FINSET_1, CARD_1, CARD_4,
      PROB_2, SUPINF_1, SUPINF_2, MEASURE1, MEASURE3, FINSEQOP, FINSEQ_2,
      BINOP_1, SEQ_1, BINOP_2, RVSUM_1, EXTREAL1, MESFUNC1, MESFUNC2, CONVFUN1;
 constructors REAL_1, NAT_1, BINOP_1, SETWISEO, FINSEQOP, FINSOP_1, EXTREAL1,
      SUPINF_2, PARTFUN1, PROB_2, MEMBERED, RAT_1, MESFUNC1, MEASURE3,
      MEASURE6, RVSUM_1, SEQ_1, MESFUNC2, CONVFUN1, BINARITH;
 clusters SUBSET_1, SUPINF_1, XREAL_0, RELSET_1, NAT_1, MEMBERED, FINSEQ_1,
      ORDINAL2, MEASURE1, BINOP_2;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0;
 theorems MEASURE1, TARSKI, SUPINF_1, FUNCT_1, FUNCT_2, NAT_1, REAL_1,
      SUPINF_2, AXIOMS, RELSET_1, EXTREAL1, EXTREAL2, MESFUNC1, ZFMISC_1,
      FINSEQ_1, FINSEQ_2, FINSEQ_3, FUNCOP_1, FINSEQ_4, PROB_2, FUNCT_3,
      XBOOLE_0, XBOOLE_1, XCMPLX_1, RELAT_1, RVSUM_1, BINOP_2, MESFUNC2,
      JORDAN3, SCMFSA_7, SETFAM_1, CONVFUN1, MEASURE2, FINSOP_1, FINSET_1,
      CARD_4, CARD_1, CARD_2;
 schemes FUNCT_2, FINSEQ_1, FINSEQ_2, NAT_1, PARTFUN1;

begin  :: Integral of Simple Valued Function ::

 reserve X for non empty set;
 reserve S for sigma_Field_Subset of X;
 reserve n,m for Nat;

theorem LM01:
for n,m be Nat, a be Function of [:(Seg n),(Seg m):],REAL
for p,q be FinSequence of REAL st
( dom p=Seg n & for i be Nat st i in dom p holds
   ex r be FinSequence of REAL st
    (dom r = Seg m & p.i = Sum r
     & for j be Nat st j in dom r holds r.j=a.[i,j])) &
( dom q=Seg m & for j be Nat st j in dom q holds
   ex s be FinSequence of REAL st
    (dom s = Seg n & q.j = Sum s
     & for i be Nat st i in dom s holds s.i=a.[i,j]))
holds Sum p = Sum q
proof
   defpred P[Nat] means
   for m be Nat, a be Function of [: Seg $1, Seg m :],REAL
   for p,q be FinSequence of REAL
    st (dom p=Seg $1
        & for i be Nat st i in dom p
              holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j] )) &
        ( dom q=Seg m
        & for j be Nat st j in dom q
              holds ex s be FinSequence of REAL st
                     (dom(s) = Seg $1 & q.j=Sum(s)
                     & for i be Nat st i in dom(s) holds s.i=a.[i,j]))
   holds Sum(p)=Sum(q);
 P0: P[0]
 proof
 now
 let m be Nat, a be Function of [: Seg 0, Seg m :],REAL;
 let p,q be FinSequence of REAL such that
        A1:  dom p=Seg 0 and
           for i be Nat st i in dom p
                holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j]) and
        B1:  dom q=Seg m and
        B2:for j be Nat st j in dom q
              holds ex s be FinSequence of REAL st
                     (dom(s) = Seg 0 & q.j=Sum(s)
                     & for i be Nat st i in dom(s) holds s.i=a.[i,j] );
thus Sum(p)=Sum(q)
proof
 len p = 0 by FINSEQ_1:def 3,A1; then
 C1: Sum(p)=0 by RVSUM_1:102,FINSEQ_1:32;
 C2: q = dom q --> 0
    proof
     now let z be set;
     assume
     C4: z in dom q;
        z in { k where k is Nat : 1 <= k & k <= m }
        by FINSEQ_1:def 1,B1,C4; then
        consider k be Nat such that
            V1:z=k & 1 <= k & k <= m;
          reconsider j=z as Nat by V1;
        consider s be FinSequence of REAL such that
           D1: dom(s) = Seg 0 and
           D2: q.j=Sum(s) and
            for i be Nat st i in dom(s) holds s.i=a.[i,j] by C4,B2;
          len s = 0 by FINSEQ_1:def 3,D1;
          hence q.z=0 by D2,FINSEQ_1:32,RVSUM_1:102;
         end;
    hence q = dom q --> 0 by FUNCOP_1:17;
    end;
    q= m |-> 0 by B1,C2,FINSEQ_2:def 2;
 hence Sum(p)=Sum(q) by C1,RVSUM_1:111;
 end;
end;
 hence thesis;
  end;
 PN: for n be Nat st P[n] holds P[n+1]
 proof
 let n be Nat such that
AS:P[n];
  now
   let m be Nat, a be Function of [:Seg (n+1),Seg m :],REAL;
   let p,q be FinSequence of REAL such that
        P1: (dom p=Seg (n+1) & for i be Nat st i in dom p
                 holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j])) and
        P2: (dom q=Seg m
                 & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st
                     (dom s = Seg (n+1) & q.j=Sum s
                     & for i be Nat st i in dom s holds s.i=a.[i,j] ) );
       set a0=a| [:Seg n, Seg m :];
        n <= n+1 by NAT_1:29; then
        Seg n c= Seg (n+1) by FINSEQ_1:7; then
        [:Seg n,Seg m:] c= [:Seg (n+1),Seg m:] by ZFMISC_1:118; then
    reconsider a0 as Function of [:Seg n,Seg m :],REAL by FUNCT_2:38;
    reconsider p0= p | (Seg n) as FinSequence of REAL by FINSEQ_1:23;
      P4: ( dom p0=Seg n & for i be Nat st i in dom p0
                    holds ex r be FinSequence of REAL st
                        (dom r = Seg m & p0.i=Sum r
                        & for j be Nat st j in dom(r) holds r.j=a0.[i,j]))
      proof
       len p=n+1 by FINSEQ_1:def 3,P1;
       then n <= len p by NAT_1:29;
       hence
       B1: dom p0=Seg n by FINSEQ_1:21;
       now let i be Nat such that
         AS1: i in dom p0;
         i in dom p by P1,AS1,B1,FINSEQ_2:9;
         then consider r be FinSequence of REAL such that
               B3: dom(r) = Seg m and
               B4: p.i=Sum(r) and
               B5: for j be Nat st j in dom(r) holds r.j=a.[i,j] by P1;
            take r;
            thus dom(r) = Seg m by B3;
            thus p0.i = Sum(r) by B4,AS1,FUNCT_1:70;
            thus for j be Nat st j in dom(r) holds r.j=a0.[i,j]
            proof
            let j be Nat such that
               AS3: j in dom(r);
               D1: [i,j] in [:Seg n,Seg m:]
               by AS1,B1,B3,AS3,ZFMISC_1:106;
               n <= n+1 by NAT_1:29; then
               D2: Seg n c= Seg (n+1) by FINSEQ_1:7;
               dom a= [:Seg (n+1),Seg m:] by FUNCT_2:def 1; then
               D3: dom a0 = [:Seg (n+1),Seg m:]
                           /\ [:Seg n,Seg m:] by FUNCT_1:68
                          .= [:Seg n,Seg m:] by D2,ZFMISC_1:124;
               thus r.j=a.[i,j] by AS3,B5
                     .=a0.[i,j] by D1,D3,FUNCT_1:70;
            end;
       end;
       hence thesis;
      end;
 deffunc F(Nat)=a.[n+1,$1];
 consider an be FinSequence such that
P60: len an=m &
             for j be Nat st j in Seg(m) holds an.j=F(j) from FINSEQ_1:sch 2;
 an is FinSequence of REAL
 proof
 now let i be Nat;
   assume i in dom an; then
   i in Seg m by P60,FINSEQ_1:def 3; then
   an.i = a.[n+1,i] by P60;
   hence an.i in REAL;
end;
hence thesis by FINSEQ_2:14;
 end;
 then reconsider an as FinSequence of REAL;
   P61:  dom an=Seg m by P60,FINSEQ_1:def 3;
   P62:  Sum(an) = p.(n+1)
   proof
    n+1 in dom p by P1,FINSEQ_1:6;
    then consider r be FinSequence of REAL such that
     A3: dom(r) = Seg m and
     A4: p.(n+1)=Sum(r) and
     A5: for j be Nat st j in dom(r) holds r.j=a.[n+1,j] by P1;
     now let j be Nat;
     assume A7:j in dom r;
     hence r.j=a.[n+1,j] by A5
        .=an.j by A7,A3,P60;
      end;
     hence thesis by A4,A3,P61,FINSEQ_1:17;
    end;
     reconsider q0=q-an as FinSequence of REAL;
     P5: dom q0=Seg m & for j be Nat st j in dom q0
                     holds ex s be FinSequence of REAL st
                         (dom s = Seg n
                         & q0.j=Sum(s)
                         & for i be Nat st i in dom(s) holds s.i=a0.[i,j] )
    proof
E1: dom diffreal = [:REAL,REAL:] by FUNCT_2:def 1;
    rng q c= REAL & rng an c= REAL by RELSET_1:12; then
E2: [:rng q, rng an:] c= dom diffreal by E1,ZFMISC_1:119;
    rng <:q,an:> c= [:rng q, rng an:] by FUNCT_3:71; then
E3: rng <:q,an:> c= dom diffreal by E2,XBOOLE_1:1;
E4: dom (diffreal.:(q,an)) = dom(diffreal*<:q,an:>) by FUNCOP_1:def 3
     .= dom <:q,an:> by E3,RELAT_1:46
     .= dom q /\ dom an by FUNCT_3:def 8;
    thus
      A1: dom q0= dom q /\ dom an by E4,RVSUM_1:def 6
                      .= Seg m  /\ Seg m by P60,FINSEQ_1:def 3,P2
                      .= Seg m;
     now let j be Nat such that
            A3: j in dom q0;
            consider s be FinSequence of REAL such that
        A4: dom(s) = Seg (n+1) and
        A5: q.j=Sum(s) and
        A6: for i be Nat st i in dom(s) holds s.i=a.[i,j] by A1,P2,A3;
            reconsider sn=s|Seg (n) as FinSequence of REAL by FINSEQ_1:23;
            take sn;
            B0:len s = n+1 by FINSEQ_1:def 3,A4; then
            n <= len s by NAT_1:29;
            hence
            B1: dom sn =Seg n by FINSEQ_1:21;
              B11:  sn^<*(s.(n+1))*> = s by FINSEQ_3:61,B0;
                n+1 in Seg (n+1) by FINSEQ_1:6; then
                s.(n+1)=a.[n+1,j] by A6,A4; then
            A7: s.(n+1)=an.j by P60,A3,A1;
            A8: q.j-an.j=Sum(sn) +an.j-an.j by A7,B11,RVSUM_1:104,A5
                           .=Sum(sn);
            thus q0.j = Sum(sn) by A8,RVSUM_1:47,A3;
            thus for i be Nat st i in dom(sn) holds sn.i=a0.[i,j]
           proof
            let i be Nat such that
               AS3: i in dom(sn);
               D1: [i,j] in [:Seg n,Seg m:]
               by B1,A1,A3,AS3,ZFMISC_1:106;
               n <= n+1 by NAT_1:29; then
               D2: Seg n c= Seg (n+1) by FINSEQ_1:7;
               dom a= [:Seg (n+1),Seg m:] by FUNCT_2:def 1; then
               D3: dom a0 = [:Seg (n+1),Seg m:]
                           /\ [:Seg n,Seg m:] by FUNCT_1:68
                          .= [:Seg n,Seg m:] by D2,ZFMISC_1:124;
                thus sn.i=s.i by AS3,FUNCT_1:70
                     .=a.[i,j] by A6,A4,D2,AS3,B1
                     .=a0.[i,j] by D1,D3,FUNCT_1:70;
            end;
        end;
       hence thesis;
    end;
    len p=n+1 by FINSEQ_1:def 3,P1; then
P8: p0^<*Sum(an)*> = p by P62,FINSEQ_3:61;
    len q0=m by FINSEQ_1:def 3,P5; then
P10: q0 is Element of m-tuples_on REAL by FINSEQ_2:110;
P11: an is Element of m-tuples_on REAL by P60,FINSEQ_2:110;
   len q=m by FINSEQ_1:def 3,P2; then
P12: q is Element of m-tuples_on REAL by FINSEQ_2:110;
P9:  q0+an=q by P11,P12,RVSUM_1:64;
   thus Sum(p) = Sum(p0)+Sum(an) by RVSUM_1:104,P8
   .=Sum(q0)+Sum(an) by P4,P5,AS
   .=Sum(q) by P9,RVSUM_1:119,P10,P11;
   end;
   hence P[n+1];
  end;
 thus for n be Nat holds P[n] from NAT_1:sch 1(P0,PN);
end;

theorem LM02:
  for F be FinSequence of ExtREAL, f be FinSequence of REAL st
    F=f holds Sum(F)=Sum(f)
proof
   let F be FinSequence of ExtREAL;
   let f be FinSequence of REAL;
   assume
A0: F = f;
   defpred P[Nat] means
   for G be FinSequence of ExtREAL, g be FinSequence of REAL st
    G=F|(Seg $1) & g = f|(Seg $1) & $1 <= len F holds Sum(G) = Sum(g);
P0:P[0]
   proof
     let G be FinSequence of ExtREAL, g be FinSequence of REAL;
     assume
P01: G=F|(Seg 0) & g = f|(Seg 0) & 0 <= len F;
     0 <= len F & 0 <= len f by NAT_1:18; then
     len G = 0 & len g = 0 by P01,FINSEQ_1:21; then
     G = <*>ExtREAL & g=<*>REAL by FINSEQ_1:32;
     hence thesis by SUPINF_2:def 1,CONVFUN1:4,RVSUM_1:102;
   end;
PK:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
PK1:  P[k];
     for G be FinSequence of ExtREAL, g be FinSequence of REAL st
      G = F|(Seg (k+1)) & g = f|(Seg (k+1)) & k+1 <= len F holds
       Sum(G) = Sum(g)
     proof
       let G be FinSequence of ExtREAL, g be FinSequence of REAL;
       assume
PK2:    G = F|(Seg (k+1)) & g = f|(Seg (k+1)) & k+1 <= len F; then
PK3:   len G = k+1 & dom G = Seg (k+1) by FINSEQ_1:21;
       reconsider G1 = G|(Seg k) as FinSequence of ExtREAL by FINSEQ_1:23;
PK4:   G = G1^<*G.(k+1)*> by PK3,FINSEQ_3:61;
       reconsider G2=<*G.(k+1)*> as FinSequence of ExtREAL;
PK5:   len g = k+1 & dom g = Seg (k+1) by A0,PK2,FINSEQ_1:21;
       reconsider g1 = g|(Seg k) as FinSequence of REAL by FINSEQ_1:23;
PK6:   g = g1^<*g.(k+1)*> by PK5,FINSEQ_3:61;
       reconsider g2=<*g.(k+1)*> as FinSequence of REAL;
       k <= k+1 by NAT_1:29; then
       Seg k c= Seg (k+1) by FINSEQ_1:7; then
PK7:   G1 = F|(Seg k) & g1 = f|(Seg k) by PK2,FUNCT_1:82;
       k <= k+1 by NAT_1:29; then
       k <= len F by PK2,AXIOMS:22; then
PK8:   Sum G1 = Sum g1 by PK1,PK7;
       not -infty in rng G1 & not -infty in rng <*G.(k+1)*>
       proof
         now assume PK9: -infty in rng G1 or -infty in rng <*G.(k+1)*>;
           rng g1 c= REAL & rng g2 c= REAL by FINSEQ_1:def 4;
           hence contradiction by PK9,A0,PK2,SUPINF_1:6;
         end;
         hence thesis;
       end; then
PK10:  Sum G = Sum G1 + Sum (<*G.(k+1)*>) by PK4,CONVFUN1:7
            .= Sum G1 + G.(k+1) by CONVFUN1:5;
       Sum g = Sum g1 + g.(k+1) by PK6,RVSUM_1:104;
       hence Sum G = Sum g by A0,PK2,PK8,PK10,SUPINF_2:1;
     end;
     hence thesis;
   end;
A1:for k be Nat holds P[k] from NAT_1:sch 1(P0,PK);
   F|(Seg len F) = F|(len F) & f|(len f) = f|(Seg len f)
     by FINSEQ_1:def 15; then
   F|(Seg len F) = F & f|(Seg len f) = f by FINSEQ_1:79;
   hence thesis by A1,A0;
end;

theorem LM03:
for X be non empty set, S be sigma_Field_Subset of X,
 f be PartFunc of X,ExtREAL st f is_simple_func_in S holds
  ex F be Finite_Sep_Sequence of S,
     a be FinSequence of ExtREAL st
        dom f = union rng F & dom F= dom a &
        (for n be Nat st n in dom F
          for x be set st x in F.n holds f.x = a.n) &
        for x be set st x in dom f holds
          ex ax be FinSequence of ExtREAL st
            dom ax = dom a &
            (for n be Nat st n in dom ax holds ax.n=a.n*(chi(F.n,X)).x)
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   assume
A1: f is_simple_func_in S;
   consider F be Finite_Sep_Sequence of S such that
A3: dom f = union rng F and
A4: for n being Nat,x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y by A1,MESFUNC2:def 5;
   defpred P[Nat,Element of ExtREAL] means
   for x be set st x in F.$1 holds $2 = f.x;
P: for k be Nat st k in Seg len F
    ex y be Element of ExtREAL st P[k,y]
   proof
     let k be Nat;
     assume k in Seg len F; then
P00: k in dom F by FINSEQ_1:def 3; then
P06: F.k in rng F by FUNCT_1:12;
     now per cases;
       case P02:F.k = {};
         take y = 0.;
         for x be set st x in F.k holds y = f.x by P02;
         hence thesis;
       end;
       case F.k <> {}; then
         consider x1 be set such that
P03:      x1 in F.k by XBOOLE_0:def 1;
         take y = f.x1;
         for x be set st x in F.k holds y = f.x
         proof
           let x be set;
           assume P04:x in F.k;
           rng F c= S & S c= bool X by FINSEQ_1:def 4; then
P07:       rng F c= bool X by XBOOLE_1:1; then
           reconsider x1 as Element of X by P03,P06;
           reconsider x as Element of X by P04,P07,P06;
           f.x = f.x1 by P00,P03,P04,A4;
           hence thesis;
         end;
         hence thesis;
       end;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
A5: dom a = Seg len F &
    for k be Nat st k in Seg len F holds P[k,a.k] from FINSEQ_1:sch 5(P);
A7:for n be Nat st n in dom F
    for x be set st x in F.n holds a.n = f.x
   proof
     let n be Nat;
     assume n in dom F; then
     n in Seg len F by FINSEQ_1:def 3;
     hence thesis by A5;
   end;
A8:for x be set st x in dom f holds ex ax be FinSequence of ExtREAL st
     dom ax = dom a &
     (for n be Nat st n in dom ax holds ax.n = a.n*(chi(F.n,X)).x)
   proof
     let x be set;
     assume x in dom f;
     deffunc Q(Nat) = a.$1 * (chi(F.$1,X)).x;
     consider ax be FinSequence of ExtREAL such that
Q1:   len ax = len F &
      for k be Nat st k in Seg len F holds ax.k=Q(k) from FINSEQ_2:sch 1;
     take ax;
     for n be Nat st n in dom ax holds ax.n = a.n*(chi(F.n,X)).x
     proof
       let n be Nat;
       assume n in dom ax; then
       n in Seg len ax by FINSEQ_1:def 3;
       hence thesis by Q1;
     end;
     hence thesis by A5,Q1,FINSEQ_1:def 3;
   end;
   take F,a;
   thus thesis by A3,A5,FINSEQ_1:def 3,A7,A8;
end;

theorem LM048:
for X be set, F be FinSequence of X holds
   F is disjoint_valued iff
   (for i,j be Nat st i in dom F & j in dom F & i <> j holds F.i misses F.j)
proof
   let X be set;
   let F be FinSequence of X;
   now assume
A5:  for i,j be Nat st i in dom F & j in dom F & i <> j holds F.i misses F.j;
     thus F is disjoint_valued
     proof
       for x,y be set st x <> y holds F.x misses F.y
       proof
         let x,y be set;
         assume
A3:      x <> y;
         per cases;
           suppose A4:x in dom F & y in dom F;
             dom F c= NAT by RELSET_1:12; then
             reconsider x,y as Nat by A4;
             F.x misses F.y by A3,A4,A5;
             hence thesis;
           end;
           suppose not x in dom F; then
             F.x = {} by FUNCT_1:def 4;
             hence thesis by XBOOLE_1:65;
           end;
           suppose not y in dom F; then
             F.y = {} by FUNCT_1:def 4;
             hence thesis by XBOOLE_1:65;
           end;
       end;
       hence thesis by PROB_2:def 3;
     end;
   end;
   hence thesis by PROB_2:def 3;
end;

theorem LM048a:
for X be non empty set, A be set, S be sigma_Field_Subset of X,
    F be Finite_Sep_Sequence of S, G be FinSequence of S st
      dom G = dom F & for i be Nat st i in dom G holds
        G.i = A /\ F.i holds G is Finite_Sep_Sequence of S
proof
  let X be non empty set,
      A be set,
      S be sigma_Field_Subset of X,
      F be Finite_Sep_Sequence of S,
      G be FinSequence of S such that
 AS0: dom G = dom F and
 AS1: for i be Nat st i in dom G holds G.i= A /\ F.i;
       now let i,j be Nat;
       assume AS2: i in dom G & j in dom G & i <> j;
  L10: F.i misses F.j by AS2,AS0,LM048;
                   (A /\ F.i) /\ (A /\ F.j)
                  =A /\ (F.i /\ (A /\ F.j)) by XBOOLE_1:16
                 .=A /\ ((F.i /\ F.j) /\ A) by XBOOLE_1:16
                 .=A /\ ({} /\ A) by XBOOLE_0:def 7,L10
                 .={}; then
                 A /\ F.i misses A /\ F.j by XBOOLE_0:def 7; then
                 G.i misses A /\ F.j by AS1,AS2;
                hence G.i misses G.j by AS1,AS2;
        end;
        hence thesis by LM048;
     end;

theorem LM048b:
for X be non empty set, A be set, F,G be FinSequence of X st
  dom G = dom F & (for i be Nat st i in dom G holds G.i = A /\ F.i) holds
   union rng G = A /\ union rng F
proof
   let X be non empty set;
   let A be set;
   let F,G be FinSequence of X;
   assume that
A0:dom G = dom F and
A1:for i be Nat st i in dom G holds G.i = A /\ F.i;
   thus union rng G c= A /\ union rng F
   proof
      let r be set;
      assume r in union rng G; then
      consider E being set such that
A2:   r in E & E in rng G by TARSKI:def 4;
      consider s being set such that
A3:   s in dom G & E = G.s by A2,FUNCT_1:def 5;
      dom G c= NAT by RELSET_1:12; then
      reconsider s as Element of NAT by A3;
      r in A /\ F.s by A1,A2,A3; then
A4:   r in A & r in F.s by XBOOLE_0:def 3;
      F.s in rng F by A0,A3,FUNCT_1:12; then
      r in union rng F by A4,TARSKI:def 4;
      hence thesis by A4,XBOOLE_0:def 3;
   end;
   let r be set;
   assume r in A /\ union rng F; then
A5:r in A & r in union rng F by XBOOLE_0:def 3; then
   consider E being set such that
A6: r in E & E in rng F by TARSKI:def 4;
   consider s being set such that
A7: s in dom F & E = F.s by A6,FUNCT_1:def 5;
   dom F c= NAT by RELSET_1:12; then
   reconsider s as Element of NAT by A7;
   A /\ E = G.s by A0,A1,A7; then
A8:r in G.s by A5,A6,XBOOLE_0:def 3;
   G.s in rng G by A0,A7,FUNCT_1:12;
   hence thesis by A8,TARSKI:def 4;
end;

theorem LM048c:
for X be set, F be FinSequence of X, i be Nat st i in dom F holds
  F.i c= union rng F & F.i /\ union rng F = F.i
proof
   let X be set, F be FinSequence of X, i be Nat;
   assume
AS: i in dom F;
   F.i in rng F by AS,FUNCT_1:12;
   hence F.i c= union rng F by ZFMISC_1:92;
   hence F.i /\ union rng F = F.i by XBOOLE_1:28;
 end;

theorem LM049:
for X be non empty set, S be sigma_Field_Subset of X,
  M be sigma_Measure of S, F be Finite_Sep_Sequence of S holds
    dom F = dom (M*F)
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let M be sigma_Measure of S;
   let F be Finite_Sep_Sequence of S;
   dom M = S by FUNCT_2:def 1; then
   rng F c= dom M by RELSET_1:12;
   hence thesis by RELAT_1:46;
end;

theorem LM050:
for X be non empty set, S be sigma_Field_Subset of X,
  M be sigma_Measure of S, F be Finite_Sep_Sequence of S holds
    M.(union rng F) = Sum(M*F)
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let M be sigma_Measure of S;
   let F be Finite_Sep_Sequence of S;
   reconsider F1 = M*F as FinSequence of ExtREAL;
   consider f be Function of NAT,ExtREAL such that
A1: Sum(F1) = f.(len F1) &
    f.0 = 0. &
    for i be Nat st i < len F1 holds f.(i+1) = f.i + F1.(i+1)
      by CONVFUN1:def 5;
   consider G being Sep_Sequence of S such that
A2: (union rng F = union rng G &
    (for n st n in dom F holds F.n = G.n) &
    (for m st not m in dom F holds G.m = {})) by MESFUNC2:33;
A4:M.(union rng F) = SUM(M*G) by MEASURE1:def 11,A2
                  .= sup(rng Ser(M*G)) by SUPINF_2:def 23;
   reconsider G1 = M*G as Num of rng(M*G) by SUPINF_2:def 16;
A5:   Ser(M*G) = Ser(rng (M*G),G1) by SUPINF_2:def 21; then
A6:Ser(M*G).0 = G1.0 &
   for n be Nat, y be R_eal st y = Ser(M*G).n holds
    Ser(M*G).(n+1) = y + G1.(n+1) by SUPINF_2:def 17;
   not 0 in Seg len F by FINSEQ_1:3; then
   not 0 in dom F by FINSEQ_1:def 3; then
A7:G.0 = {} by A2;
B1:dom G = NAT by FUNCT_2:def 1; then
A8:Ser(M*G).0 = M.(G.0) by A6,FUNCT_1:23 .= 0. by A7,MEASURE1:def 11;
C1:for i be Nat st i < len F1 holds Ser(M*G).(i+1) = Ser(M*G).i + F1.(i+1)
   proof
     let i be Nat;
     assume i < len F1; then
     1 <= i+1 & i+1 <= len F1 by NAT_1:29,38; then
     i+1 in dom F1 by FINSEQ_3:27; then
B2:  i+1 in dom F by LM049;
     Ser(M*G).(i+1) = Ser(M*G).i + G1.(i+1) by A5,SUPINF_2:def 17
     .= Ser(M*G).i + M.(G.(i+1)) by B1,FUNCT_1:23
     .= Ser(M*G).i + M.(F.(i+1)) by B2,A2;
     hence thesis by B2,FUNCT_1:23;
   end;
   defpred Q[Nat] means
    $1 <= len F1 implies Ser(M*G).$1 = f.$1;
Q0:Q[0] by A8,A1;
QK:for k be Nat st Q[k] holds Q[k+1]
   proof
     let k be Nat;
     assume
QK1:  Q[k];
     assume k+1 <= len F1; then
QK3: k < len F1 by NAT_1:38; then
     Ser(M*G).(k+1) = f.k + F1.(k+1) by QK1,C1;
     hence thesis by QK3,A1;
   end;
Q1:for k be Nat holds Q[k] from NAT_1:sch 1(Q0,QK);
   defpred P[Nat] means
    $1 >= len F1 implies Ser(M*G).$1 = Ser(M*G).(len F1);
P0:P[0]
   proof
     assume
     0 >= len F1; then
     0 = len F1 by NAT_1:18;
     hence thesis;
   end;
R1:M*G is nonnegative by MEASURE2:1;
PK:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
PK1:  P[k];
     assume
PK2:  k+1 >= len F1;
     now per cases;
       case
PK3:    k >= len F1; then
PK5:    Ser(M*G).(k+1) = Ser(M*G).(len F1) + G1.(k+1)
           by A5,SUPINF_2:def 17,PK1;
        k+1 > len F1 by PK3,NAT_1:38; then
        not k+1 in Seg len F1 by FINSEQ_1:3; then
        not k+1 in dom F1 by FINSEQ_1:def 3; then
PK6:    not k+1 in dom F by LM049;
        G1.(k+1) = M.(G.(k+1)) by B1,FUNCT_1:23
         .= M.{} by PK6,A2 .= 0. by MEASURE1:def 11;
        hence thesis by PK5,SUPINF_2:18;
       end;
       case k < len F1; then
        k+1 <= len F1 by NAT_1:38;
        hence thesis by PK2,AXIOMS:21;
       end;
     end;
     hence thesis;
   end;
F3:for k be Nat holds P[k] from NAT_1:sch 1(P0,PK);
   defpred P1[Nat] means
   $1 < len F1 implies Ser(M*G).(len F1 - $1) <=' Ser(M*G).(len F1);
V7:P1[0];
V8:for k be Nat st P1[k] holds P1[k+1]
   proof
     let k be Nat;
     assume
V0:   P1[k];
     assume
V2:   k+1 < len F1; then
     consider k' be Nat such that
V1:   len F1 = k+1 + k' by NAT_1:28;
V3:  k <= k+1 by NAT_1:29;
     Ser(M*G).(k') <=' Ser(M*G).(k' + 1) by R1,SUPINF_2:59;
     hence thesis by V0,V1,V3,V2,AXIOMS:22,SUPINF_1:29;
   end;
V9:for i be Nat holds P1[i] from NAT_1:sch 1(V7,V8);
V13:for i be Nat st i < len F1 holds Ser(M*G).i <=' Ser(M*G).(len F1)
   proof
     let i be Nat;
     assume i < len F1; then
     consider k be Nat such that
V10:  len F1 = i + k by NAT_1:28;
V11: k = len F1 - i & i = len F1 - k by V10;
     len F1 <= len F1 + i by NAT_1:29; then
V12: k <= len F1 by V11,REAL_1:86;
     Ser(M*G).(len F1 - k) <=' Ser(M*G).(len F1)
     proof
       now per cases by V12,REAL_1:def 5;
         case k = len F1;
           hence thesis by R1,A8,SUPINF_2:59;
         end;
         case k < len F1;
           hence thesis by V9;
         end;
       end;
       hence thesis;
     end;
     hence thesis by V10;
   end;
   for z be R_eal holds z in rng Ser(M*G) implies z <=' Ser(M*G).(len F1)
   proof
     let z be R_eal;
     assume z in rng Ser(M*G); then
     consider n be set such that
F1:   n in dom Ser(M*G) & z = Ser(M*G).n by FUNCT_1:def 5;
     reconsider n as Nat by F1,FUNCT_2:def 1;
     now per cases;
       case n < len F1;
         hence thesis by F1,V13;
       end;
       case n >= len F1;
         hence thesis by F3,F1;
       end;
     end;
     hence thesis;
   end; then
P1:Ser(M*G).(len F1) is majorant of rng Ser(M*G) by SUPINF_1:def 9;
   Ser(M*G).(len F1) in rng Ser(M*G)
   proof
     dom (Ser(M*G)) = NAT by FUNCT_2:def 1;
     hence thesis by FUNCT_1:12;
   end; then
   Ser(M*G).(len F1) = sup(rng Ser(M*G)) by P1,SUPINF_1:80;
   hence thesis by A4,A1,Q1;
end;

theorem LM051:
for F,G be FinSequence of ExtREAL, a be R_eal st
  ((a <> +infty & a <> -infty) or
   (for i be Nat st i in dom F holds F.i <' 0.) or
   (for i be Nat st i in dom F holds 0. <' F.i)) &
  dom F = dom G &
  for i be Nat st i in dom G holds G.i=a*F.i holds
    Sum(G)=a*Sum(F)
proof
   let F,G be FinSequence of ExtREAL;
   let a be R_eal;
   assume that
AS1:(a <> +infty & a <> -infty) or
    (for i be Nat st i in dom F holds F.i <' 0.) or
    (for i be Nat st i in dom F holds 0. <' F.i) and
AS2:dom F = dom G and
AS3:for i be Nat st i in dom G holds G.i=a*F.i;
   consider f be Function of NAT,ExtREAL such that
A1: Sum(F) = f.(len F) & f.0 = 0. &
    for i being Nat st i < len F holds f.(i+1)=f.i+F.(i+1) by CONVFUN1:def 5;
   consider g be Function of NAT,ExtREAL such that
A2: Sum(G) = g.(len G) & g.0 = 0. &
    for i being Nat st i < len G holds g.(i+1)=g.i+G.(i+1) by CONVFUN1:def 5;
   defpred P[Nat] means
    $1 <= len G implies g.$1 = a*f.$1;
P0:  P[0] by A1,A2,EXTREAL1:def 1;
PK:  for i be Nat st P[i] holds P[i+1]
     proof
       let i be Nat;
       assume
PK1:   P[i];
       assume PK4:i+1 <= len G; then
PK2:   i < len G by NAT_1:38;
       1 <= i+1 by NAT_1:29; then
PK7:   i+1 in dom G by PK4,FINSEQ_3:27; then
       i+1 in Seg len F by AS2,FINSEQ_1:def 3; then
       i+1 <= len F by FINSEQ_1:3; then
PK8:   i < len F by NAT_1:38;
PK6:   g.(i+1) = g.i + G.(i+1) by PK2,A2 .= a*f.i + a*F.(i+1)
         by PK1,PK4,PK7,AS3,NAT_1:38;
       now per cases by AS1;
        case a <> +infty & a <> -infty; then
         g.(i+1) = a*(f.i + F.(i+1)) by PK6,EXTREAL1:29;
         hence thesis by PK8,A1;
        end;
        case
C1:      for i be Nat st i in dom F holds F.i <' 0.;
         defpred P1[Nat] means $1 < len F implies f.$1 <=' 0.;
D1:      P1[0] by A1;
D2:      for i be Nat st P1[i] holds P1[i+1]
         proof
           let i be Nat;
           assume
D21:       P1[i];
           assume
D22:       i+1 < len F; then
           i < len F by NAT_1:38; then
D23:       f.(i+1) = f.i + F.(i+1) by A1;
           1 <= i+1 & i+1 <= len F by D22,NAT_1:37; then
           i+1 in dom F by FINSEQ_3:27; then
           F.(i+1) <' 0. by C1;
           hence thesis by D23,D21,D22,NAT_1:38,EXTREAL2:10;
         end;
         for i be Nat holds P1[i] from NAT_1:sch 1(D1,D2); then
         f.i <=' 0. & F.(i+1) <' 0. by PK8,PK7,AS2,C1; then
         (f.i <' 0. or f.i = 0.) & F.(i+1) <' 0. by SUPINF_1:22; then
         g.(i+1) = a*(f.i + F.(i+1)) by PK6,EXTREAL1:29;
         hence thesis by PK8,A1;
        end;
        case
E1:      for i be Nat st i in dom F holds 0. <' F.i;
         defpred P2[Nat] means
          $1 < len F implies 0. <=' f.$1;
F1:      P2[0] by A1;
F2:      for i be Nat st P2[i] holds P2[i+1]
         proof
           let i be Nat;
           assume
D21:        P2[i];
           assume
D22:        i+1 < len F; then
            i < len F by NAT_1:38; then
D23:       f.(i+1) = f.i + F.(i+1) by A1;
           1 <= i+1 & i+1 <= len F by D22,NAT_1:37; then
           i+1 in dom F by FINSEQ_3:27; then
           0. <' F.(i+1) by E1;
           hence thesis by D23,D21,D22,NAT_1:38,EXTREAL2:8;
         end;
         for i be Nat holds P2[i] from NAT_1:sch 1(F1,F2); then
         0. <=' f.i & 0. <' F.(i+1) by PK8,PK7,AS2,E1; then
         (0. <' f.i or f.i = 0.) & 0. <' F.(i+1) by SUPINF_1:22; then
         g.(i+1) = a*(f.i + F.(i+1)) by PK6,EXTREAL1:29;
         hence thesis by PK8,A1;
        end;
       end;
       hence thesis;
     end;
A3:  for i be Nat holds P[i] from NAT_1:sch 1(P0,PK);
     Seg len F = dom G by AS2,FINSEQ_1:def 3
       .= Seg len G by FINSEQ_1:def 3; then
     a*Sum(F) = a*f.(len G) by A1,FINSEQ_1:8 .= g.(len G) by A3;
     hence thesis by A2;
end;

theorem LM052:
for F be FinSequence of REAL holds F is FinSequence of ExtREAL
 proof
   let F be FinSequence of REAL;
   rng F c= REAL & REAL c= ExtREAL by FINSEQ_1:def 4; then
   rng F c= ExtREAL by XBOOLE_1:1;
   hence thesis by FINSEQ_1:def 4;
 end;

definition
  let X be non empty set;
  let S be sigma_Field_Subset of X;
  let f be PartFunc of X,ExtREAL;
  let F be Finite_Sep_Sequence of S;
  let a be FinSequence of ExtREAL;
  pred F,a are_Re-presentation_of f means :def_1:
    dom f = union rng F & dom F=dom a &
    for n be Nat st n in dom F
      for x be set st x in F.n holds f.x=a.n;
end;

theorem
  for X be non empty set,
      S be sigma_Field_Subset of X,
      f be PartFunc of X,ExtREAL st
    f is_simple_func_in S holds
      ex F be Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL st
       F,a are_Re-presentation_of f
proof
let X be non empty set;
let S be sigma_Field_Subset of X;
let f be PartFunc of X,ExtREAL such that
AS: f is_simple_func_in S;
consider F being Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL such that
     A:  dom f = union rng F & dom F= dom a
          & (for n be Nat st n in dom F
              for x be set st x in F.n holds f.x=a.n )
          & for x be set st x in dom f
                 holds ex ax be FinSequence of ExtREAL
                           st dom ax= dom a &
                                (for n be Nat st n in dom ax
                                   holds ax.n=a.n*(chi(F.n,X)).x) by LM03,AS;
   take F,a;
   thus thesis by A,def_1;
end;

theorem Cor1:
for X be non empty set, S be sigma_Field_Subset of X,
 F be Finite_Sep_Sequence of S holds
  ex G be Finite_Sep_Sequence of S st
    (union rng F = union rng G &
     (for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)))
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let F be Finite_Sep_Sequence of S;
   defpred P[Nat] means
   for F be Finite_Sep_Sequence of S st len F = $1 holds
    ex G be Finite_Sep_Sequence of S st
     (union rng F = union rng G &
     (for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)));
A1:P[0]
   proof
     let F be Finite_Sep_Sequence of S;
     assume
B1:   len F = 0;
     take G = F;
     for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)
     proof
       let n be Nat;
       assume B2:n in dom G;
       G = {} by B1,FINSEQ_1:25;
       hence thesis by B2,FINSEQ_1:26;
     end;
     hence thesis;
   end;
A2:for n be Nat st P[n] holds P[n+1]
   proof
     let n be Nat;
     assume C1:P[n];
     let F be Finite_Sep_Sequence of S;
     assume
C3:  len F = n+1;
     reconsider F1 = F|(Seg n) as Finite_Sep_Sequence of S by MESFUNC2:36;
C0':     n <= len F by C3,NAT_1:29; then
C0:  len F1 = n by FINSEQ_1:21; then
     consider G1 be Finite_Sep_Sequence of S such that
C2:   (union rng F1 = union rng G1 &
       (for n be Nat st n in dom G1 holds
        (G1.n <> {} & ex m be Nat st m in dom F1 & F1.m = G1.n))) by C1;
C4': dom F = dom (F1^<*F.(n+1)*>) &
     for k be Nat st k in dom F holds F.k = (F1^<*F.(n+1)*>).k
     proof
C5:    dom (F1^<*F.(n+1)*>) = Seg (len F1 + len <*F.(n+1)*>) by FINSEQ_1:def 7
        .= Seg (n + 1) by C0,FINSEQ_1:56
        .= dom F by C3,FINSEQ_1:def 3;
       for k be Nat st k in dom F holds F.k = (F1^<*F.(n+1)*>).k
       proof
         let k be Nat;
         assume
E0:       k in dom F;
         now per cases;
           case
E1:          k in dom F1; then
E2:          (F1^<*F.(n+1)*>).k = F1.k by FINSEQ_1:def 7;
             k in Seg n by C0',E1,FINSEQ_1:21; then
             k <= n by FINSEQ_1:3; then
             (F|n).k = F.k by JORDAN3:20;
             hence thesis by E2,FINSEQ_1:def 15;
           end;
           case not k in dom F1; then
             not (k in Seg n) by C0',FINSEQ_1:21; then
E3:          not (1 <= k & k <= n) by FINSEQ_1:3;
             k in Seg (n+1) by E0,C3,FINSEQ_1:def 3; then
E4:          not (k < n + 1) & k <= n+1 by E3,FINSEQ_1:3,NAT_1:38;
             dom <*F.(n+1)*> = Seg 1 by FINSEQ_1:def 8; then
E5:          1 in dom <*F.(n+1)*> by TARSKI:def 1,FINSEQ_1:4;
             (F1^<*F.(n+1)*>).k = (F1^<*F.(n+1)*>).(len F1 + 1)
               by E4,C0,REAL_1:def 5
             .= <*F.(n+1)*>.1 by E5,FINSEQ_1:def 7
             .= F.(n+1) by FINSEQ_1:def 8;
             hence thesis by E4,REAL_1:def 5;
           end;
         end;
         hence thesis;
       end;
       hence thesis by C5;
     end; then
C4:  F = F1^<*F.(n+1)*> by FINSEQ_1:17;
     ex G be Finite_Sep_Sequence of S st
      (union rng F = union rng G &
       (for k be Nat st k in dom G holds
        (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)))
     proof
       now per cases;
         case D1:F.(n+1) = {};
D2:        rng <*F.(n+1)*> = {{}} by D1,FINSEQ_1:55;
           take G = G1;
           union rng F = union rng G &
           (for k be Nat st k in dom G holds
            (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k))
           proof
             rng F = rng F1 \/ rng <*F.(n+1)*> by C4,FINSEQ_1:44;
             hence union rng F
              = union rng F1 \/ union rng <*F.(n+1)*> by ZFMISC_1:96
             .= union rng F1 \/ {} by D2,ZFMISC_1:31
             .= union rng G by C2;
             for k be Nat st k in dom G holds
              (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)
             proof
               let k be Nat;
               assume
E1:             k in dom G; then
               consider m be Nat such that
E2:             m in dom F1 & F1.m = G.k by C2;
               dom F1 c= dom F & F1.m = F.m by C4,E2,FINSEQ_1:39,def 7;
               hence thesis by E1,C2,E2;
             end;
             hence thesis;
           end;
           hence thesis;
         end;
         case
D0:        F.(n+1) <> {};
           1 <= n+1 by NAT_1:29; then
           n+1 in Seg (n+1) by FINSEQ_1:3; then
           n+1 in dom F by C3,FINSEQ_1:def 3; then
           F.(n+1) in rng F & rng F c= S by FUNCT_1:12,FINSEQ_1:def 4; then
D3':           <*F.(n+1)*> is FinSequence of S by SCMFSA_7:22; then
D3:        G1^<*F.(n+1)*> is FinSequence of S by SCMFSA_7:23;
           for k,m be set st k <> m holds
            (G1^<*F.(n+1)*>).k misses (G1^<*F.(n+1)*>).m
           proof
             let k,m be set;
             assume
D4:           k <> m;
             now per cases;
               case
D5:              k in dom (G1^<*F.(n+1)*>) & m in dom (G1^<*F.(n+1)*>);
                 dom (G1^<*F.(n+1)*>) c= NAT by D3,RELSET_1:12; then
                 reconsider k,m as Nat by D5;
                 now per cases;
                 case k = len (G1^<*F.(n+1)*>); then
                   k = len G1 + len <*F.(n+1)*> by FINSEQ_1:35; then
D6:                k = len G1 + 1 by FINSEQ_1:56;
                   1 <= len <*F.(n+1)*> by FINSEQ_1:56; then
D7:                (G1^<*F.(n+1)*>).k = <*F.(n+1)*>.1 by D6,SCMFSA_7:10
                    .= F.(n+1) by FINSEQ_1:def 8;
                   m in Seg (len (G1^<*F.(n+1)*>)) by D5,FINSEQ_1:def 3; then
                   m in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35; then
                   m in Seg (len G1 + 1) by FINSEQ_1:56; then
                   1 <= m & m <= len G1 + 1 by FINSEQ_1:3; then
                   1 <= m & m < len G1 + 1 by D4,D6,REAL_1:def 5; then
                   1 <= m & m <= len G1 by NAT_1:38; then
D9:                m in dom G1 by FINSEQ_3:27; then
D11:               (G1^<*F.(n+1)*>).m = G1.m by FINSEQ_1:def 7;
                   consider m1 be Nat such that
D10:                m1 in dom F1 & F1.m1 = G1.m by D9,C2;
                   dom F1 c= dom F by C4',FINSEQ_1:39; then
D12:               m1 in dom F &
                   (G1^<*F.(n+1)*>).m = F.m1 by D10,D11,C4,FINSEQ_1:def 7;
                   m1 in Seg len F1 by D10,FINSEQ_1:def 3; then
                   m1 <= n by C0,FINSEQ_1:3; then
                   m1 <> n+1 by NAT_1:38;
                   hence thesis by D12,D7,PROB_2:def 3;
                   end;
                   case k <> len (G1^<*F.(n+1)*>); then
                     k <> len G1 + len <*F.(n+1)*> by FINSEQ_1:35; then
E13:                 k <> len G1 + 1 by FINSEQ_1:56;
                   k in Seg (len (G1^<*F.(n+1)*>)) by D5,FINSEQ_1:def 3; then
                   k in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35; then
                   k in Seg (len G1 + 1) by FINSEQ_1:56; then
                   1 <= k & k <= len G1 + 1 by FINSEQ_1:3; then
                   1 <= k & k < len G1 + 1 by E13,REAL_1:def 5; then
                   1 <= k & k <= len G1 by NAT_1:38; then
E9:                k in dom G1 by FINSEQ_3:27; then
E11:               (G1^<*F.(n+1)*>).k = G1.k by FINSEQ_1:def 7;
                     now per cases;
                       case m = len (G1^<*F.(n+1)*>); then
                   m = len G1 + len <*F.(n+1)*> by FINSEQ_1:35; then
E6:                m = len G1 + 1 by FINSEQ_1:56;
                   1 <= len <*F.(n+1)*> by FINSEQ_1:56; then
E7:                (G1^<*F.(n+1)*>).m = <*F.(n+1)*>.1 by E6,SCMFSA_7:10
                    .= F.(n+1) by FINSEQ_1:def 8;
                   consider k1 be Nat such that
E10:                k1 in dom F1 & F1.k1 = G1.k by E9,C2;
                   dom F1 c= dom F by C4',FINSEQ_1:39; then
E12:               k1 in dom F &
                   (G1^<*F.(n+1)*>).k = F.k1 by E10,E11,C4,FINSEQ_1:def 7;
                   k1 in Seg len F1 by E10,FINSEQ_1:def 3; then
                   k1 <= n by C0,FINSEQ_1:3; then
                   k1 <> n+1 by NAT_1:38;
                   hence thesis by E12,E7,PROB_2:def 3;
                       end;
                       case m <> len (G1^<*F.(n+1)*>); then
                        m <> len G1 + len <*F.(n+1)*> by FINSEQ_1:35; then
F8:                     m <> len G1 + 1 by FINSEQ_1:56;
                   m in Seg (len (G1^<*F.(n+1)*>)) by D5,FINSEQ_1:def 3; then
                   m in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35; then
                   m in Seg (len G1 + 1) by FINSEQ_1:56; then
                   1 <= m & m <= len G1 + 1 by FINSEQ_1:3; then
                   1 <= m & m < len G1 + 1 by F8,REAL_1:def 5; then
                   1 <= m & m <= len G1 by NAT_1:38; then
                   m in dom G1 by FINSEQ_3:27; then
               (G1^<*F.(n+1)*>).m = G1.m by FINSEQ_1:def 7;
                   hence thesis by E11,D4,PROB_2:def 3;
                       end;
                     end;
                     hence thesis;
                   end;
                 end;
                 hence thesis;
               end;
               case not(k in dom (G1^<*F.(n+1)*>) &
                        m in dom (G1^<*F.(n+1)*>)); then
                 (G1^<*F.(n+1)*>).k = {} or (G1^<*F.(n+1)*>).m = {}
                   by FUNCT_1:def 4;
                 hence thesis by XBOOLE_1:65;
               end;
             end;
             hence thesis;
           end; then
           reconsider G = G1^<*F.(n+1)*> as Finite_Sep_Sequence of S
            by D3',SCMFSA_7:23,PROB_2:def 3;
           take G;
           union rng F = union rng G &
           for n be Nat st n in dom G holds
            (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)
           proof
             rng F = rng F1 \/ rng <*F.(n+1)*> by C4,FINSEQ_1:44;
             hence union rng F
              = union rng F1 \/ union rng <*F.(n+1)*> by ZFMISC_1:96
             .= union (rng G1 \/ rng <*F.(n+1)*>) by ZFMISC_1:96,C2
             .= union rng G by FINSEQ_1:44;
G1:          len G = len G1 + len <*F.(n+1)*> by FINSEQ_1:35
              .= len G1 + 1 by FINSEQ_1:56;
             for k be Nat st k in dom G holds
             (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)
             proof
               let k be Nat;
               assume
P1:             k in dom G;
               now per cases;
                 case
P2:                k = len G;
                   dom <*F.(n+1)*> = Seg 1 by FINSEQ_1:55; then
                   1 in dom <*F.(n+1)*> by FINSEQ_1:4,TARSKI:def 1; then
P3:                G.k = <*F.(n+1)*>.1 by G1,P2,FINSEQ_1:def 7
                      .= F.(n+1) by FINSEQ_1:def 8;
                   1 <= n+1 by NAT_1:29; then
                   n+1 in Seg (n+1) by FINSEQ_1:3; then
                   n+1 in dom F by C3,FINSEQ_1:def 3;
                   hence thesis by P3,D0;
                 end;
                 case
P5:                k <> len G;
                   1 <= k & k <= len G by P1,FINSEQ_3:27; then
                   1 <= k & k < len G by P5,REAL_1:def 5; then
                   1 <= k & k <= len G1 by G1,NAT_1:38; then
P6:                k in dom G1 by FINSEQ_3:27; then
P9:                G.k = G1.k by FINSEQ_1:def 7;
                   ex m be Nat st m in dom F & F.m = G.k
                   proof
                     consider m be Nat such that
P8:                  m in dom F1 & F1.m = G1.k by C2,P6;
P10:                 dom F1 c= dom F by C4',FINSEQ_1:39;
                     take m;
                     thus thesis by P10,P8,P9,C4,FINSEQ_1:def 7;
                   end;
                   hence thesis by P9,P6,C2;
                 end;
               end;
               hence thesis;
             end;
             hence thesis;
           end;
           hence thesis;
         end;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
E1:len F = len F;
   for n be Nat holds P[n] from NAT_1:sch 1(A1,A2);
   hence thesis by E1;
end;

RVSUM_1_110:
for a be FinSequence of ExtREAL, p,N be Element of ExtREAL st
 N = len a &
 (for n be Nat st n in dom a holds a.n = p) holds
   Sum(a) = N * p
proof
   let a be FinSequence of ExtREAL;
   let p,N be Element of ExtREAL;
   assume that
A1: N = len a and
A2: for n be Nat st n in dom a holds a.n = p;
   defpred P[Nat] means
   for F be FinSequence of ExtREAL, p be Element of ExtREAL st
    $1 = len F &
    (for n be Nat st n in dom F holds F.n = p) holds
      ex N be Element of ExtREAL st N = $1 & Sum(F) = N * p;
A3:P[0]
   proof
     for F be FinSequence of ExtREAL, p be Element of ExtREAL st
      0 = len F &
      (for n be Nat st n in dom F holds F.n = p) holds
        ex N be Element of ExtREAL st N = 0 & Sum(F) = N * p
     proof
       let F be FinSequence of ExtREAL;
       let p be Element of ExtREAL;
       assume that
B1:     0 = len F and
        for n be Nat st n in dom F holds F.n = p;
       take N = 0.;
       consider f be Function of NAT,ExtREAL such that
B4:     Sum(F) = f.(len F) & f.0 = 0. &
        for i be Nat st i < len F holds f.(i+1)=f.i+F.(i+1) by CONVFUN1:def 5;
       thus thesis by B1,B4,SUPINF_2:def 1,EXTREAL1:def 1;
     end;
     hence thesis;
   end;
A4:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
C1:   P[k];
     for F be FinSequence of ExtREAL, p be Element of ExtREAL st
      k+1 = len F &
      (for n be Nat st n in dom F holds F.n = p) holds
       ex N be Element of ExtREAL st N = k+1 & Sum(F) = N * p
     proof
       let F be FinSequence of ExtREAL;
       let p be Element of ExtREAL;
       assume that
C2:     k+1 = len F and
C3:     (for n be Nat st n in dom F holds F.n = p);
       F <> {} by C2,FINSEQ_1:25; then
       consider G be FinSequence, v be set such that
C4:     F = G^<*v*> by FINSEQ_1:63;
       reconsider G as FinSequence of ExtREAL by C4,FINSEQ_1:50;
       reconsider H = <*v*> as FinSequence of ExtREAL by C4,FINSEQ_1:50;
C8:    k + 1 = len G + len <*v*> by C4,FINSEQ_1:35,C2
        .= len G + 1 by FINSEQ_1:56; then
C5:    k = len G by XCMPLX_1:2;
       dom <*v*> = Seg 1 by FINSEQ_1:55; then
C6:    1 in dom <*v*> by FINSEQ_1:4,TARSKI:def 1;
       1 <= k + 1 by NAT_1:29; then
C7:    k + 1 in dom F by C2,FINSEQ_3:27;
    v = <*v*>.1 by FINSEQ_1:57 .= F.(k + 1) by C6,C4,C8,FINSEQ_1:def 7
        .= p by C3,C7; then
       reconsider v as Element of ExtREAL;
       for n be Nat st n in dom G holds G.n = p
       proof
         let n be Nat;
         assume P0:n in dom G; then
P1:      1 <= n & n <= len G by FINSEQ_3:27;
         len G <= len G + 1 by NAT_1:29; then
P2:      1 <= n & n <= len G + 1 by P1,AXIOMS:22;
         len F = len G + len <*v*> by C4,FINSEQ_1:35
          .= len G + 1 by FINSEQ_1:56; then
         n in dom F by P2,FINSEQ_3:27; then
         F.n = p by C3;
         hence thesis by C4,P0,FINSEQ_1:def 7;
       end;
       then consider N1 be Element of ExtREAL such that
C10:    N1 = k & Sum(G) = N1 * p by C1,C5;
       consider f be Function of NAT,ExtREAL such that
C11:    Sum(F) = f.(len F) & f.0 = 0. &
        for i being Nat st i < len F holds f.(i+1) = f.i + F.(i+1)
         by CONVFUN1:def 5;
       consider g be Function of NAT,ExtREAL such that
C12:    Sum(G) = g.(len G) & g.0 = 0. &
        for i being Nat st i < len G holds g.(i+1) = g.i + G.(i+1)
         by CONVFUN1:def 5;
D9':   for k1 be Nat st k1 <= len G holds f.k1 = g.k1
       proof
         defpred P2[Nat] means
         $1 <= len G implies f.$1 = g.$1;
D7:      P2[0] by C11,C12;
D8:      for k1 be Nat st P2[k1] holds P2[k1+1]
         proof
           let k1 be Nat;
           assume
D1:        P2[k1];
           now assume
D2:          k1+1 <= len G;
D3:          k1 <= k1+1 by NAT_1:29;
D5:          k1 < len G by D2,NAT_1:38; then
D4:          g.(k1+1) = g.k1 + G.(k1+1) by C12;
             len F = len G + len <*v*> by C4,FINSEQ_1:35
             .= len G + 1 by FINSEQ_1:56; then
             k1 < len F by D5,NAT_1:38; then
D6:          f.(k1+1) = f.k1 + F.(k1+1) by C11;
             1 <= k1 + 1 by NAT_1:29; then
             k1 + 1 in dom G by D2,FINSEQ_3:27;
             hence thesis by D1,AXIOMS:22,D3,D4,D6,C4,FINSEQ_1:def 7;
           end;
           hence thesis;
         end;
         for k1 be Nat holds P2[k1] from NAT_1:sch 1(D7,D8);
         hence thesis;
       end;
       k < len F by C2,NAT_1:38; then
   Sum(F) = f.k + F.(k+1) by C2,C11 .= g.k + F.(k+1) by D9',C5; then
D11:   Sum(F) = g.k + p by C7,C3 .= Sum(G) + p by C8,C12,XCMPLX_1:2
        .= N1 * p + 1. * p by C10,EXTREAL2:4;
       take N = N1+1.;
       N = k+1 & Sum(F) = N * p
       proof
        thus N = k+1 by C10,MESFUNC1:def 8,SUPINF_2:1;
        now per cases by NAT_1:19;
         case k = 0;
          hence Sum F = N * p by D11,C10,SUPINF_2:def 1,EXTREAL1:29;
         end;
         case 0 < k; then
Q2:       0. <' N1 by C10,EXTREAL1:18;
          0. <' 1. by MESFUNC1:def 8,EXTREAL1:18;
          hence Sum(F) = N * p by D11,Q2,EXTREAL1:29;
         end;
        end;
        hence Sum(F) = N * p;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
   for k be Nat holds P[k] from NAT_1:sch 1(A3,A4); then
   consider N' be Element of ExtREAL such that
A5: N' = len a & Sum(a) = N' * p by A2;
   thus thesis by A1,A5;
end;

Cor2:
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL st
    f is_simple_func_in S
   & (for x be set st x in dom f holds 0. <=' f.x)
   & (for x be set st x in dom f holds 0. <> f.x) holds
        ex  F be Finite_Sep_Sequence of S,
              a be FinSequence of ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. <' a.n &  a.n <' +infty
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <=' f.x and
A3:for x be set st x in dom f holds 0. <> f.x;
   consider F1 be Finite_Sep_Sequence of S such that
A4: dom f = union rng F1 and
A5: for n be Nat, x,y be Element of X st n in dom F1 & x in F1.n & y in F1.n
     holds f.x = f.y by A1,MESFUNC2:def 5;
   consider G be Finite_Sep_Sequence of S such that
A6: union rng F1 = union rng G and
A7: for n be Nat st n in dom G holds
     (G.n <> {} & ex m be Nat st m in dom F1 & F1.m = G.n) by Cor1;
   set F = <*{}*>^G;
   rng <*{}*> = {{}} & {} in S by FINSEQ_1:55,MEASURE1:21; then
   rng <*{}*> c= S & rng G c= S by ZFMISC_1:37,FINSEQ_1:def 4; then
   rng <*{}*> \/ rng G c= S by XBOOLE_1:8; then
   rng F c= S by FINSEQ_1:44; then
   reconsider F as FinSequence of S by FINSEQ_1:def 4;
   for x,y be set st x <> y holds F.x misses F.y
   proof
     let x,y be set;
     assume
B1:   x<>y;
     now per cases;
       case
B2:      x in dom F & y in dom F;
         dom F c= NAT by RELSET_1:12; then
         reconsider x,y as Nat by B2;
         x in Seg (len (<*{}*>^G)) & y in Seg (len (<*{}*>^G))
           by B2,FINSEQ_1:def 3; then
B7:      1 <= x & x <= len (<*{}*>^G) &
         1 <= y & y <= len (<*{}*>^G) by FINSEQ_1:3;
         now per cases;
           case
B3:          x = 1 or y = 1;
B4:          dom <*{}*> = Seg 1 by FINSEQ_1:55;
             now per cases by B3;
               case
B5:            x = 1; then
               x in dom <*{}*> by B4,FINSEQ_1:4,TARSKI:def 1; then
               F.x = <*{}*>.x by FINSEQ_1:def 7; then
               F.x = {} by B5,FINSEQ_1:def 8;
               hence thesis by XBOOLE_1:65;
               end;
               case
B6:            y = 1; then
               y in dom <*{}*> by B4,FINSEQ_1:4,TARSKI:def 1; then
               F.y = <*{}*>.y by FINSEQ_1:def 7; then
               F.y = {} by B6,FINSEQ_1:def 8;
               hence thesis by XBOOLE_1:65;
               end;
             end;
             hence thesis;
           end;
           case x <> 1 & y <> 1; then
             1 < x & 1 < y by B7,REAL_1:def 5; then
             len <*{}*> < x & len <*{}*> < y by FINSEQ_1:57; then
B8:          F.x = G.(x - len <*{}*>) & F.y = G.(y - len <*{}*>)
               by B7,FINSEQ_1:37;
             x - (len <*{}*>) <> y - (len <*{}*>) by B1,XCMPLX_1:19;
             hence thesis by B8,PROB_2:def 3;
           end;
         end;
         hence thesis;
       end;
       case not (x in dom F) or not (y in dom F); then
         F.x = {} or F.y = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
       end;
     end;
     hence thesis;
   end; then
   reconsider F as Finite_Sep_Sequence of S by PROB_2:def 3;
A8:union rng F = union (rng <*{}*> \/ rng G) by FINSEQ_1:44
   .= union ({{}} \/ rng G) by FINSEQ_1:55
   .= union {{}} \/ union rng G by ZFMISC_1:96
   .= {} \/ union rng G by ZFMISC_1:31
   .= dom f by A4,A6;
   defpred P[Nat,Element of ExtREAL] means
    (for x be Element of X st $1 in dom F & $1 = 1 holds $2 = 0.) &
    (for x be Element of X st $1 in dom F & $1 >= 2 & x in F.$1 holds
      $2 = f.x);
C1:for k be Nat st k in Seg len F
    ex y be Element of ExtREAL st P[k,y]
   proof
     let k be Nat;
     assume C2':k in Seg len F;
     ex y be Element of ExtREAL st P[k,y]
     proof
       now per cases;
         case C3:k = 1;
           take y = 0.;
C4:        for x be Element of X st k in dom F & k = 1 holds y = 0.;
           for x be Element of X st k in dom F & k >= 2 & x in F.k holds
            y = f.x by C3;
           hence thesis by C4;
         end;
         case C5:k <> 1;
           1 <= k & k <= len F by C2',FINSEQ_1:3; then
C6':       1 < k & k <= len <*{}*> + len G by C5,FINSEQ_1:35,REAL_1:def 5; then
        1+1 <= k & k <= len <*{}*> + len G by NAT_1:38; then
C7:        len <*{}*> + 1 <= k & k <= len <*{}*> + len G by FINSEQ_1:56; then
           consider k2 be Nat such that
C8:         len <*{}*> + 1 + k2 = k by NAT_1:28;
       1 + k2 = k - len <*{}*> by C8; then
C10:       1 <= 1 + k2 by C7,REAL_1:84;
           k - len <*{}*> <= len G by C6',REAL_1:86; then
           1 + k2 in Seg len G by C8,C10,FINSEQ_1:3; then
C12:       1 + k2 in dom G by FINSEQ_1:def 3;
C13:       F.k = G.(k - len <*{}*>) by C7,FINSEQ_1:36 .= G.(1+k2) by C8; then
           F.k <> {} by A7,C12; then
           consider z be set such that
C14:        z in F.k by XBOOLE_0:def 1;
           take y = f.z;
           consider m1 be Nat such that
C15:        m1 in dom F1 & F1.m1 = G.(1+k2) by A7,C12;
           F.k in rng F1 & rng F1 c= S
            by C15,C13,FUNCT_1:12,FINSEQ_1:def 4; then
           F.k in S & S c= bool X; then
           reconsider z as Element of X by C14;
C18:       for x be Element of X st k in dom F & k = 1 holds y = 0. by C5;
           for x be Element of X st k in dom F & k >= 2 & x in F.k holds
            y = f.x
           proof
             let x be Element of X;
             assume k in dom F & k >= 2 & x in F.k; then
             z in F1.m1 & x in F1.m1 by C14,C15,C13;
             hence thesis  by C15,A5;
           end;
           hence thesis by C18;
         end;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
D1: dom a = Seg len F &
    for n be Nat st n in Seg len F holds
       P[n,a.n] from FINSEQ_1:sch 5(C1);
A9:dom F = dom a &
   a.1 = 0. &
   (for n be Nat,x be Element of X st n in dom F & n >= 2 & x in F.n holds
    a.n = f.x)
   proof
     thus dom a = dom F by D1,FINSEQ_1:def 3;
     len F = len <*{}*> + len G by FINSEQ_1:35 .= 1 + len G by FINSEQ_1:56;then
     1 <= len F by NAT_1:29; then
D3:  1 in Seg len F by FINSEQ_1:3; then
  1 in dom F by FINSEQ_1:def 3;
     hence a.1 = 0. by D1,D3;
     for n be Nat, x be Element of X st n in dom F & n >= 2 & x in F.n holds
      a.n = f.x
     proof
       let n be Nat;
       let x be Element of X;
       assume
D5:     n in dom F & n >= 2 & x in F.n; then
       n in Seg len F by FINSEQ_1:def 3;
       hence thesis by D1,D5;
     end;
     hence thesis;
   end;
A10:for n' be Nat st n' in dom F
      for x be set st x in F.n' holds f.x=a.n'
    proof
      let n' be Nat;
      assume
E1:    n' in dom F;
      now per cases;
       case E2:n' = 1;
        thus for x be set st x in F.n' holds f.x = a.n'
        proof
          let x be set;
          assume E3:x in F.n';
          dom <*{}*> = {1} by FINSEQ_1:4,FINSEQ_1:55; then
          1 in dom <*{}*> by TARSKI:def 1; then
          F.1 = <*{}*>.1 by FINSEQ_1:def 7 .= {} by FINSEQ_1:57;
          hence thesis by E3,E2;
        end;
       end;
       case
E4:     n' <> 1;
        n' in Seg len F by E1,FINSEQ_1:def 3; then
        1 <= n' & n' <= len F by FINSEQ_1:3; then
        1 < n' by E4,REAL_1:def 5; then
E5:     1+1 <= n' by NAT_1:38;
        thus for x be set st x in F.n' holds f.x = a.n'
        proof
          let x be set;
          assume E6:x in F.n';
          F.n' in rng F & rng F c= S by E1,FUNCT_1:12,RELSET_1:12; then
          F.n' in S & S c= bool X; then
          reconsider x as Element of X by E6;
          a.n' = f.x by A9,E1,E5,E6;
          hence thesis;
        end;
       end;
      end;
      hence thesis;
    end;
A12:for n be Nat st 2 <= n & n in dom a holds  0. <' a.n &  a.n <' +infty
   proof
     let n be Nat;
     assume that
G1:   2 <= n and
G2:   n in dom a;
     n in Seg len F by A9,G2,FINSEQ_1:def 3; then
     n <= len F by FINSEQ_1:3; then
G3:  n <= len <*{}*> + len G by FINSEQ_1:35;
     1+1 <= n by G1; then
     len <*{}*> + 1 <= n by FINSEQ_1:56; then
G7:  F.n = G.(n - len <*{}*>) by G3,FINSEQ_1:36 .= G.(n-1) by FINSEQ_1:56;
G5:  dom <*{}*> = {1} by FINSEQ_1:4,FINSEQ_1:55;
     1 <> n by G1; then
     not n in dom <*{}*> by G5,TARSKI:def 1; then
     consider k be Nat such that
G6:   k in dom G & n = len <*{}*> + k by A9,G2,FINSEQ_1:38;
     n = 1 + k by G6,FINSEQ_1:56; then
     k = n - 1; then
     F.n <> {} by G6,G7,A7; then
     consider x1 be set such that
G8:   x1 in F.n by XBOOLE_0:def 1;
     F.n in rng F by A9,G2,FUNCT_1:12; then
G10: F.n c= union rng F by ZFMISC_1:92; then
 x1 in dom f & dom f c= X by A8,G8,RELSET_1:12; then
     reconsider x1 as Element of X;
G11: a.n = f.x1 by G8,G1,G2,A9;
     f is_finite by A1,MESFUNC2:def 5; then
G12: |.f.x1.| <' +infty by G10,A8,G8,MESFUNC2:def 1;
     0. <=' f.x1 & 0. <> f.x1 by G10,A8,G8,A2,A3;
     hence thesis by G11,G12,SUPINF_1:22,EXTREAL2:58;
   end;
   take F,a;
   thus thesis by A8,A9,A10,def_1,A12;
end;

Cor3:
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL st
    f is_simple_func_in S
   & (for x be set st x in dom f holds 0. <=' f.x)
   & (ex x be set st x in dom f & 0. = f.x) holds
        ex F be Finite_Sep_Sequence of S,
              a be FinSequence of  ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. <' a.n &  a.n <' +infty
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <=' f.x and
A3:ex x be set st x in dom f & 0. = f.x;

   consider x0 be set such that
A31:x0 in dom f & 0.= f.x0 by A3;
   dom f c= X by RELSET_1:12; then
   reconsider x0 as Element of X by A31;

   consider F1 be Finite_Sep_Sequence of S such that
A4:dom f = union rng F1 &
    (for n be Nat, x,y be Element of X st
     n in dom F1 & x in F1.n & y in F1.n holds f.x = f.y) by A1,MESFUNC2:def 5;

   consider V be set such that
A41:x0 in V & V in rng F1 by A31,A4,TARSKI:def 4;
   consider n0 be set such that
A42:n0 in dom F1 & V = F1.n0 by A41,FUNCT_1:def 5;
   dom F1 c= NAT by RELSET_1:12; then
   reconsider n0 as Nat by A42;
   set N={n where n is Nat:
           n in dom F1 &
           for x be Element of X st x in F1.n holds f.x = 0.};
A44:for x be Element of X st x in F1.n0 holds f.x = 0. by A4,A41,A42,A31;
A5:dom F1 = Seg len F1 by FINSEQ_1:def 3;
   for z be set st z in N holds z in dom F1
   proof
     let z be set;
     assume z in N; then
     consider n' be Nat such that
A51:  z = n' & n' in dom F1 &
      for x be Element of X st x in F1.n' holds f.x = 0.;
     thus thesis by A51;
   end; then
A52:N c= dom F1 by TARSKI:def 3; then
   reconsider N as finite set by A5,FINSET_1:13;
   consider P1 be FinSequence such that
A6: rng P1 = N & P1 is one-to-one by FINSEQ_4:73;
   set M = dom F1 \ N;
A61:M c= dom F1 by XBOOLE_1:36; then
   reconsider M as finite set by A5,FINSET_1:13;
   consider P2 be FinSequence such that
A7: rng P2 = M & P2 is one-to-one by FINSEQ_4:73;
   set F0 = {Fn where Fn is Element of S :
                    Fn in rng F1 &
                    for x be Element of X st x in Fn holds f.x = 0.};
   set G1 = union F0;
   for F' be set st F' in F0 holds F' in bool X
   proof
     let F' be set;
     assume F' in F0; then
     consider Fn be Element of S such that
A71:  F'=Fn & Fn in rng F1 &
      for x be Element of X st x in Fn holds f.x = 0.;
      thus thesis by A71;
   end; then
   F0 c= bool X by TARSKI:def 3; then
   reconsider F0 as Subset-Family of X by SETFAM_1:def 7;
   defpred P[set,set] means
    ($1 in dom F1 implies $2 = F1.$1) &
    (not ($1 in dom F1) implies $2 = {});
A72:for x be set st x in NAT ex y be set st y in S & P[x,y]
   proof
     let x be set;
     assume x in NAT; then
     reconsider x as Nat;
     now per cases;
      case A73:x in dom F1; then
A74:   F1.x in rng F1 & rng F1 c= S by FUNCT_1:12,FINSEQ_1:def 4;
       reconsider y = F1.x as set;
       take y;
       thus thesis by A73,A74;
      end;
      case A75:not (x in dom F1);
       take y = {};
       y in S by MEASURE1:45;
       hence thesis by A75;
      end;
     end;
     hence thesis;
   end;
   consider F2 be Function of NAT,S such that
A76:for x be set st x in NAT holds P[x,F2.x] from FUNCT_2:sch 1(A72);
   for z be set st z in rng F1 holds z in rng F2
   proof
     let z be set;
     assume z in rng F1; then
     consider n' be set such that
A78:  n' in dom F1 & z = F1.n' by FUNCT_1:def 5;
     dom F1 c= NAT by RELSET_1:12; then
A79: F2.n' = F1.n' by A76,A78;
     dom F1 c= NAT & dom F2 = NAT by RELSET_1:12,FUNCT_2:def 1;
     hence thesis by A79,A78,FUNCT_1:12;
   end; then
   rng F1 c= rng F2 by TARSKI:def 3; then
B71:rng F1 is countable by CARD_4:46;
   for z be set st z in F0 holds z in rng F1
   proof
     let z be set;
     assume z in F0; then
     consider Fn' be Element of S such that
B72:  z = Fn' & Fn' in rng F1 &
      for x be Element of X st x in Fn' holds f.x = 0.;
     thus thesis by B72;
   end; then
B74:F0 c= rng F1 by TARSKI:def 3;
   F1.n0 is Element of S & F1.n0 in rng F1 &
   for x be Element of X st x in F1.n0 holds f.x = 0.
   proof
    consider n0' be Nat such that
C72: n0 = n0' & n0' in dom F1 &
     for x be Element of X st x in F1.n0' holds f.x = 0. by A44,A42;
    F1.n0 in rng F1 & rng F1 c= S by C72,FUNCT_1:12,RELSET_1:12;
    hence F1.n0 is Element of S;
    thus F1.n0 in rng F1 by C72,FUNCT_1:12;
    let x be Element of X;
    assume x in F1.n0;
    hence thesis by C72;
   end; then
C73:F1.n0 in F0; then
   reconsider F0 as N_Sub_set_fam of X by B74,B71,CARD_4:46;
   for F' be set st F' in F0 holds F' in S
   proof
     let F' be set;
     assume F' in F0; then
     consider Fn be Element of S such that
A71:  F'=Fn & Fn in rng F1 &
      for x be Element of X st x in Fn holds f.x = 0.;
     thus thesis by A71;
   end; then
   F0 c= S by TARSKI:def 3; then
   reconsider G1 as Element of S by MEASURE1:def 9;
A72:len P1 = card N & len P2 = card M by A6,A7,FINSEQ_4:77;
   reconsider L = Seg len F1 as finite set;
A75:N c= Seg len F1 by A52,FINSEQ_1:def 3; then
   card N <= card L by CARD_1:80; then
   len P1 <= len F1 & len F1 <= len F1 + 1 by A72,NAT_1:29,FINSEQ_1:78; then
   len P1 <= len F1 + 1 by AXIOMS:22; then
   consider lenF be Nat such that
A73:len F1 + 1 = len P1 + lenF by NAT_1:28;
A74:lenF = len F1 + 1 - len P1 by A73;
   M = (Seg len F1) \ N by FINSEQ_1:def 3; then
   card M = card (Seg len F1) - card N by A75,CARD_2:63; then
A8':len P2 = len F1 - len P1 + 1 - 1 by A72,FINSEQ_1:78;
   defpred P[Nat,set] means
    ($1 = 1 implies $2 = G1) &
    ($1 >=2 implies ex k' be Nat st k'=$1-1 & $2 = F1.(P2.k'));
S1:for k be Nat st k in Seg lenF ex U be Element of S st P[k,U]
   proof
     let k be Nat;
     assume S01:k in Seg lenF;
     per cases;
      suppose S02:k = 1;
       take U = G1;
       thus thesis by S02;
      end;
      suppose S03:k <> 1;
S04:   k >= 1 & k <= lenF by S01,FINSEQ_1:3; then
       k > 1 by S03,REAL_1:def 5; then
S05:   k >= 1+1 by NAT_1:38;
       consider k' be Nat such that
S06:    k = 1 + k' by S04,NAT_1:28;
       k-1>=1 & k-1<=lenF -1 by S04,S05,REAL_1:49,84; then
       k' in Seg len P2 by A8',A74,S06,FINSEQ_1:3; then
       k' in dom P2 by FINSEQ_1:def 3; then
       P2.k' in M by A7,FUNCT_1:12; then
       F1.(P2.k') in rng F1 & rng F1 c= S by A61,FUNCT_1:12,RELSET_1:12; then
       reconsider U = F1.(P2.k') as Element of S;
       take U;
       thus thesis by S03,S06;
      end;
   end;
   consider F be FinSequence of S such that
S2: dom F = Seg lenF &
    for k be Nat st k in Seg lenF holds
     P[k,F.k] from FINSEQ_1:sch 5(S1);
   defpred A[Nat,Element of ExtREAL] means
   for y be Element of X holds
    (y in F.$1 implies $2 = f.y) & (F.$1 = {} implies $2 = 1.);
T1:for k be Nat st k in Seg len F
    ex x be Element of ExtREAL st A[k,x]
   proof
     let k be Nat;
     assume k in Seg len F; then
U15: k in dom F by FINSEQ_1:def 3;
     now per cases;
      case k = 1; then
T13:   F.k = G1 by U15,S2;
T12:   for y be Element of X holds
        (y in F.k implies 0. = f.y) & (F.k = {} implies 0.=1.)
       proof
        let y be Element of X;
T12':   y in F.k implies 0. = f.y
        proof
         assume y in F.k; then
         consider Y be set such that
T14:      y in Y & Y in F0 by T13,TARSKI:def 4;
         consider Fn be Element of S such that
T15:      Y = Fn & Fn in rng F1 &
          for x be Element of X st x in Fn holds f.x = 0. by T14;
         thus f.y = 0. by T14,T15;
        end;
        thus thesis by T12',A41,A42,C73,TARSKI:def 4,T13;
       end;
       take x = 0.;
       thus thesis by T12;
      end;
      case T16:k <> 1;
T19:   1 <= k & k <= lenF by U15,S2,FINSEQ_1:3; then
       1 < k by T16,REAL_1:def 5; then
T17:   1+1 <= k by NAT_1:38; then
       consider k1 be Nat such that
T18:    k1 = k-1 & F.k = F1.(P2.k1) by U15,S2;
       1 <= k1 & k1 <= len P2 by A8',A74,T17,T18,T19,REAL_1:49,84; then
       k1 in Seg len P2 by FINSEQ_1:3; then
       k1 in dom P2 by FINSEQ_1:def 3; then
       P2.k1 in M by A7,FUNCT_1:12; then
       consider k' be set such that
U11:    k' in dom F1 & F.k = F1.k' by T18,A61;
       dom F1 c= NAT by RELSET_1:12; then
       reconsider k' as Nat by U11;
       now per cases;
        case U12:F.k = {};
U13:     for y be Element of X holds
          (y in F.k implies 1. = f.y) & (F.k = {} implies 1.=1.) by U12;
         take x = 1.;
         thus thesis by U13;
        end;
        case F.k <> {}; then
         consider y1 be set such that
U14:      y1 in F.k by XBOOLE_0:def 1;
         F.k in rng F & rng F c= S by U15,FUNCT_1:12,FINSEQ_1:def 4; then
         F.k in S & S c= bool X; then
         reconsider y1 as Element of X by U14;
         take x = f.y1;
         for y be Element of X holds
          (y in F.k implies x = f.y) & (F.k = {} implies x=1.)
         proof
          let y be Element of X;
          y in F.k implies x = f.y
          proof
           assume y in F.k; then
           y in F1.k' & y1 in F1.k' by U11,U14;
           hence thesis by A4,U11;
          end;
          hence thesis by U14;
         end;
         hence thesis;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
T2: dom a = Seg len F &
    for n be Nat st n in Seg (len F) holds A[n,a.n] from FINSEQ_1:sch 5(T1);
T3:for n be Nat, x be Element of X st n in dom F & x in F.n holds a.n = f.x
   proof
    let n be Nat;
    let x be Element of X;
    assume T31:n in dom F & x in F.n; then
    n in Seg len F by FINSEQ_1:def 3;
    hence thesis by T31,T2;
   end;

   for z be set st z in union rng F holds z in union rng F1
   proof
    let z be set;
    assume z in union rng F; then
    consider Y be set such that
K0:  z in Y & Y in rng F by TARSKI:def 4;
    consider k be set such that
K1:  k in dom F & Y = F.k by K0,FUNCT_1:def 5;
    dom F c= NAT by RELSET_1:12; then
    reconsider k as Nat by K1;
    now per cases;
     case k = 1; then
      z in G1 by K0,K1,S2; then
      consider Y' be set such that
K3:    z in Y' & Y' in F0 by TARSKI:def 4;
      consider Fn' be Element of S such that
K4:    Y' = Fn' &
       Fn' in rng F1 &
       for x be Element of X st x in Fn' holds f.x = 0. by K3;
      thus thesis by K3,K4,TARSKI:def 4;
     end;
     case K5:k <> 1;
K6:   1 <= k & k <= lenF by S2,K1,FINSEQ_1:3; then
      1 < k by K5,REAL_1:def 5; then
K7:   1 + 1 <= k by NAT_1:38; then
      consider k' be Nat such that
K8:    k' = k-1 & F.k = F1.(P2.k') by S2,K1;
      1 <= k' & k' <= len P2 by A8',A74,K6,K7,K8,REAL_1:49,84; then
      k' in dom P2 by FINSEQ_3:27; then
      P2.k' in M by A7,FUNCT_1:12; then
      F1.(P2.k') in rng F1 by A61,FUNCT_1:12;
      hence thesis by K0,K1,K8,TARSKI:def 4;
     end;
    end;
    hence thesis;
   end; then
T4:union rng F c= union rng F1 by TARSKI:def 3;
   for z be set st z in union rng F1 holds z in union rng F
   proof
    let z be set;
    assume z in union rng F1; then
    consider Y be set such that
L1:  z in Y & Y in rng F1 by TARSKI:def 4;
    consider m1 be set such that
L2:  m1 in dom F1 & Y = F1.m1 by L1,FUNCT_1:def 5;
    dom F1 c= NAT by RELSET_1:12; then
    reconsider m1 as Nat by L2;
    now per cases;
     case m1 in N; then
      consider m1' be Nat such that
L3:    m1 = m1' & m1' in dom F1 &
       for x be Element of X st x in F1.m1' holds f.x = 0.;
L4:   F1.m1' in rng F1 & rng F1 c= S by L3,FUNCT_1:12,RELSET_1:12; then
      reconsider F1m1' = F1.m1' as Element of S;
L7:   z in Y & Y in F0 by L1,L3,L2,L4;
      lenF - 1 >= 0 by A8',A74,NAT_1:18; then
      lenF >= 1+0 by REAL_1:84; then
      1 in Seg lenF by FINSEQ_1:3; then
      F.1 in rng F & F.1 = G1 by S2,FUNCT_1:12; then
      z in union F0 & union F0 in rng F by L7,TARSKI:def 4;
      hence thesis by TARSKI:def 4;
     end;
     case not (m1 in N); then
      m1 in M by L2,XBOOLE_0:def 4; then
      consider m1' be set such that
L8:    m1' in dom P2 & m1 = P2.m1' by A7,FUNCT_1:def 5;
      m1' in Seg (len P2) & Seg (len P2) c= NAT by L8,FINSEQ_1:def 3; then
      reconsider m1' as Nat;
      m1' in Seg len P2 by L8,FINSEQ_1:def 3; then
      1 <= m1' & m1' <= len P2 by FINSEQ_1:3; then
L9:   1+1 <= m1' + 1 &  m1'+1 <= lenF by A8',A74,AXIOMS:24;
      reconsider m2 = m1'+1 as Nat;
      1 <= m2 by L9,AXIOMS:22; then
L11:  m2 in Seg lenF by L9,FINSEQ_1:3; then
      consider k' be Nat such that
L12:   k' = m2 -1 & F.m2 = F1.(P2.k') by L9,S2;
L13:  F.m2 in rng F by L11,S2,FUNCT_1:12;
      thus thesis by L1,L2,L12,L13,L8,TARSKI:def 4;
     end;
    end;
    hence thesis;
   end; then
   union rng F1 c= union rng F by TARSKI:def 3; then
A10:union rng F = dom f by A4,T4,XBOOLE_0:def 10;
A11:a.1 = 0. &
    dom F = dom a &
    (for n be Nat,x be Element of X st n in dom F & x in F.n holds
      a.n = f.x)
   proof
     0 <= lenF - 1 by A8',A74,NAT_1:18; then
     0+1 <= lenF by REAL_1:84; then
A111:1 in Seg lenF by FINSEQ_1:3; then
A112:1 in Seg len F by S2,FINSEQ_1:def 3;
A113:F.1 = G1 by A111,S2;
     rng F1 c= S by RELSET_1:12; then
     reconsider F1n0 = F1.n0 as Element of S by A41,A42;
     F1n0 in F0 by A44,A41,A42; then
     x0 in F.1 by A113,A41,A42,TARSKI:def 4;
     hence a.1 = 0. by A31,A112,T2;
     thus dom F = dom a by T2,FINSEQ_1:def 3;
     thus thesis by T3;
   end;
A13:for n be Nat st n in dom F
     for x be set st x in F.n holds f.x=a.n
    proof
     let n be Nat;
     assume A131:n in dom F; then
     F.n in rng F & rng F c= S by FUNCT_1:12,RELSET_1:12; then
A132:F.n in S & S c= bool X;
     let x be set;
     assume A133:x in F.n; then
     reconsider x as Element of X by A132;
     a.n = f.x by A131,A133,T3;
     hence thesis;
    end;
A134:for n,m be Nat st n in dom F & m in dom F & n = 1 & m <> 1 holds
     F.n misses F.m
    proof
     let n,m be Nat;
     assume
T1:   n in dom F & m in dom F & n = 1 & m <> 1;
T2:  F.n = G1 by T1,S2;
     1 <= m & m <= len F by T1,FINSEQ_3:27; then
     1 < m by T1,REAL_1:def 5; then
T3:  1+1 <= m & m in Seg lenF by T1,S2,NAT_1:38; then
     consider k' be Nat such that
T4:   k' = m-1 & F.m = F1.(P2.k') by S2;
     m-1>=1 & m <= lenF by T3,REAL_1:84,FINSEQ_1:3; then
     m-1>=1 & m-1<=lenF -1 by REAL_1:49; then
     k' in Seg len P2 by T4,A8',A74,FINSEQ_1:3; then
     k' in dom P2 by FINSEQ_1:def 3; then
     P2.k' in M by A7,FUNCT_1:12; then
T4': P2.k' in dom F1 & not (P2.k' in N) by XBOOLE_0:def 4;
     F.n /\ F.m = {}
     proof
       assume F.n /\ F.m <> {}; then
       consider v be set such that
T5:     v in F.n /\ F.m by XBOOLE_0:def 1;
T6:    v in F.n & v in F.m by T5,XBOOLE_0:def 3; then
       consider Y be set such that
T7:     v in Y & Y in F0 by T2,TARSKI:def 4;
       consider Fv be Element of S such that
T8:     Y = Fv & Fv in rng F1 &
        for x be Element of X st x in Fv holds f.x = 0. by T7;
       consider n' be set such that
T9:     n' in dom F1 & Fv = F1.n' by T8,FUNCT_1:def 5;
       dom F1 c= NAT by RELSET_1:12; then
       reconsider n' as Nat by T9;
T10:   n' <> P2.k' by T4',T8,T9;
       F1.(n') misses F1.(P2.k') by T10,PROB_2:def 3; then
   F1.(n') /\ F1.(P2.k') = {} by XBOOLE_0:def 7;
       hence contradiction by T4,T6,T7,T8,T9,XBOOLE_0:def 3;
     end;
     hence thesis by XBOOLE_0:def 7;
    end;
A135:for n,m be Nat st n in dom F & m in dom F & n <> 1 & m <> 1 & n <> m holds
     F.n misses F.m
    proof
     let n,m be Nat;
     assume
D1:   n in dom F & m in dom F & n <> 1 & m <> 1 & n <> m; then
D2': 1 <= n & n <= lenF & 1 <= m & m <= lenF by S2,FINSEQ_1:3; then
     1 < n & 1 < m by D1,REAL_1:def 5; then
D3:  1+1 <= n & 1+1 <= m by NAT_1:38; then
     consider k1 be Nat such that
D4:   k1 = n-1 & F.n = F1.(P2.k1) by S2,D1;
     consider k2 be Nat such that
D5:   k2 = m-1 & F.m = F1.(P2.k2) by S2,D1,D3;
     1 <= n-1 & n-1 <= lenF -1 & 1<=m-1 & m-1<=lenF -1
      by D2',D3,REAL_1:49,84; then
     k1 in Seg len P2 & k2 in Seg len P2 by D4,D5,A8',A74,FINSEQ_1:3; then
D6:  k1 in dom P2 & k2 in dom P2 by FINSEQ_1:def 3;
     k1 <> k2 by D1,D4,D5,XCMPLX_1:19; then
     P2.k1 <> P2.k2 by D6,A7,FUNCT_1:def 8;
     hence thesis by D4,D5,PROB_2:def 3;
    end;
    for x,y be set st x <> y holds F.x misses F.y
    proof
     let x,y be set;
     assume B1:x<>y;
     now per cases;
      case B2:x in dom F & y in dom F;
       dom F c= NAT by RELSET_1:12; then
       reconsider x,y as Nat by B2;
       now per cases;
        case B4:x = 1 or y = 1;
         now per cases by B4;
          case B5:x = 1; then
           y <> 1 by B1;
           hence thesis by B2,B5,A134;
          end;
          case B6:y = 1; then
           x <> 1 by B1;
           hence thesis by B2,B6,A134;
          end;
         end;
         hence thesis;
        end;
        case x <> 1 & y <> 1;
         hence thesis by B2,B1,A135;
        end;
       end;
       hence thesis;
      end;
      case C1:not (x in dom F) or not (y in dom F);
       now per cases by C1;
        case not (x in dom F); then
         F.x = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
        end;
        case not (y in dom F); then
         F.y = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
    end; then
    reconsider F as Finite_Sep_Sequence of S by PROB_2:def 3;
A12:for n be Nat st 2 <= n & n in dom a holds 0. <' a.n & a.n <' +infty
   proof
    let n be Nat;
    assume X1:2 <= n & n in dom a;
X4':f is_finite by A1,MESFUNC2:def 5;
    0 <= lenF - 1 by A8',A74,NAT_1:18; then
    0+1 <= lenF by REAL_1:84; then
X5: 1 in dom F & n <> 1 by S2,X1,FINSEQ_1:3; then
    F.1 misses F.n by X1,A11,A134; then
X6: G1 misses F.n by X5,S2;
D2':1 <= n & n <= lenF by X1,A11,S2,FINSEQ_1:3;
    consider k1 be Nat such that
D4:  k1 = n-1 & F.n = F1.(P2.k1) by S2,A11,X1;
    1 + 1 <= n by X1; then
    1 <= n-1 & n-1 <= lenF -1 by D2',REAL_1:49,84; then
    k1 in Seg len P2 by D4,A8',A74,FINSEQ_1:3; then
    k1 in dom P2 by FINSEQ_1:def 3; then
    P2.k1 in M by A7,FUNCT_1:12; then
D7: F.n in rng F1 by D4,A61,FUNCT_1:12;
    a.n <> 0. & 0. <=' a.n & a.n <' +infty
    proof
     now per cases;
      case F.n <> {}; then
       consider y be set such that
Y5:     y in F.n by XBOOLE_0:def 1;
Y6:    F.n in rng F & rng F c= S by X1,A11,FUNCT_1:12,FINSEQ_1:def 4; then
       F.n in S & S c= bool X; then
       reconsider y as Element of X by Y5;
       reconsider Fn = F.n as Element of S by Y6;
Y7:    a.n = f.y by X1,T2,Y5;
       G1 /\ F.n = {} by X6,XBOOLE_0:def 7; then
Y8:    not y in G1 by Y5,XBOOLE_0:def 3;
       thus a.n <> 0.
       proof
        assume a.n = 0.; then
        for x be Element of X st x in F.n holds f.x = 0. by X1,A11; then
        Fn in F0 by D7;
        hence contradiction by Y8,Y5,TARSKI:def 4;
       end;
       y in union rng F by Y5,Y6,TARSKI:def 4; then
       0. <=' f.y & |. f.y .| <' +infty by A10,X4',A2,MESFUNC2:def 1;
       hence 0. <=' a.n & a.n <' +infty by Y7,EXTREAL2:58;
      end;
      case F.n = {}; then
       a.n = 1. by X1,T2;
       hence thesis by SUPINF_1:16,SUPINF_2:def 1,MESFUNC1:def 8,EXTREAL2:20;
      end;
     end;
     hence thesis;
    end;
    hence thesis by SUPINF_1:22;
   end;
   take F,a;
   thus thesis by A10,A11,A13,def_1,A12;
end;

theorem LM061:
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL st
      f is_simple_func_in S
      & for x be set st x in dom f holds 0. <=' f.x holds
        ex F be Finite_Sep_Sequence of S,
              a be FinSequence of ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. <' a.n & a.n <' +infty
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <=' f.x;
   per cases;
     suppose ex x be set st x in dom f & 0. = f.x;
       hence thesis by A1,A2,Cor3;
     end;
     suppose for x be set st x in dom f holds 0. <> f.x;
       hence thesis by A1,A2,Cor2;
     end;
end;

theorem
  for X be non empty set,
      S be sigma_Field_Subset of X,
      f be PartFunc of X,ExtREAL,
      F be Finite_Sep_Sequence of S,
      a be FinSequence of ExtREAL,
      x be Element of X
      st F,a are_Re-presentation_of f
          & x in dom f holds
   ex ax be FinSequence of ExtREAL st dom ax= dom a &
        (for n be Nat st n in dom ax
          holds ax.n=a.n*(chi(F.n,X)).x) & f.x=Sum(ax)
proof
let X be non empty set;
let S be sigma_Field_Subset of X;
let f be PartFunc of X,ExtREAL;
let F be Finite_Sep_Sequence of S;
let a be FinSequence of ExtREAL;
let x be Element of X such that
AS2: F,a are_Re-presentation_of f and
AS3: x in dom f;
A1:dom f = union rng F & dom F = dom a &
  for n be Nat st n in dom F
   for x be set st x in F.n holds f.x = a.n by AS2,def_1;
  deffunc F(Nat) = a.$1*(chi(F.$1,X)).x;
  consider ax be FinSequence of ExtREAL such that
A2:len ax = len a &
   for j be Nat st j in Seg len a holds
    ax.j = F(j) from FINSEQ_2:sch 1;
   take ax;
A3:dom ax = Seg len a by A2,FINSEQ_1:def 3;
   f.x = Sum(ax)
   proof
    consider Y be set such that
A4:  x in Y & Y in rng F by AS3,A1,TARSKI:def 4;
    consider k be set such that
A5:  k in dom F & Y = F.k by A4,FUNCT_1:def 5;
    dom F c= NAT by RELSET_1:12; then
    reconsider k as Nat by A5;
A6: k in Seg len a by A5,A1,FINSEQ_1:def 3;
    consider SA be Function of NAT,ExtREAL such that
B1:  Sum(ax) = SA.(len ax) & SA.0 = 0. &
     for i be Nat st i < len ax holds SA.(i+1)=SA.i + ax.(i+1)
      by CONVFUN1:def 5;
C0: for i be Nat st i in Seg len ax & i <> k holds ax.i = 0.
    proof
     let i be Nat;
     assume C1:i in Seg len ax & i <> k; then
C2:  ax.i = a.i * (chi(F.i,X)).x by A2;
     F.i misses F.k by C1,PROB_2:def 3; then
     F.i /\ F.k = {} by XBOOLE_0:def 7; then
     not (x in F.i) by A4,A5,XBOOLE_0:def 3; then
     chi(F.i,X).x = 0. by SUPINF_2:def 1,FUNCT_3:def 3;
     hence thesis by C2,EXTREAL1:def 1;
    end;
D0: for i be Nat st i in Seg len ax & i = k holds ax.i = f.x
    proof
     let i be Nat;
     assume D1:i in Seg len ax & i = k;
     chi(F.i,X).x = 1. by MESFUNC1:def 8,A4,A5,D1,FUNCT_3:def 3;
     then ax.i = a.i * 1. by D1,A2 .= a.i by EXTREAL2:4;
     hence thesis by AS2,def_1,A4,A5,D1;
    end;
    defpred P[Nat] means
    $1 <= len ax implies
    ($1 < k implies SA.$1 = 0.) & ($1 >= k implies SA.$1 = f.x);
P0: P[0]
    proof
     1 <= k by A6,FINSEQ_1:3;
     hence thesis by B1;
    end;
Pi: for i be Nat st P[i] holds P[i+1]
    proof
     let i be Nat;
     assume L1:P[i];
     assume L0:i+1 <= len ax;
     now per cases;
      case L2:i+1 < k;
L3:    i <= i+1 by NAT_1:29; then
L3':   i < k & i <= len ax by L0,L2,AXIOMS:22;
       k <= len a by A6,FINSEQ_1:3; then
L5:    i < len ax by L3',A2,AXIOMS:22; then
       1 <= i+1 & i+1 <= len ax by NAT_1:29,38; then
L6:    i+1 in Seg len ax by FINSEQ_1:3;
       SA.(i+1) = 0. + ax.(i+1) by L3,L1,L2,AXIOMS:22,B1,L5
               .= ax.(i+1) by SUPINF_2:18
               .= 0. by L2,L6,C0;
       hence thesis by L2;
      end;
      case L7:i+1 >= k;
       now per cases;
        case L8:k = i+1;
L10:     k <= len a by A6,FINSEQ_1:3; then
         i < len ax by A2,L8,NAT_1:38;
         hence SA.(i+1) = SA.i + ax.(i+1) by B1
                 .= ax.k by L8,SUPINF_2:18,L1,NAT_1:38,L10,A2
                 .= f.x by D0,A6,A2;
        end;
        case k <> i+1; then
L12':    k < i+1 by L7,REAL_1:def 5; then
L13:    k <= i & i < len ax by L0,NAT_1:38;
         1 <= i+1 by NAT_1:29; then
L14:     i+1 in Seg len ax by L0,FINSEQ_1:3;
         thus SA.(i+1) = SA.i + ax.(i+1) by B1,L13
                 .= f.x + 0. by L14,C0,L12',L1,L0,NAT_1:38
                 .= f.x by SUPINF_2:18;
        end;
       end;
       hence thesis by L7;
      end;
     end;
     hence thesis;
    end;
PP: for i be Nat holds P[i] from NAT_1:sch 1(P0,Pi);
    len ax >= k by A2,A6,FINSEQ_1:3;
    hence thesis by B1,PP;
   end;
   hence thesis by A2,A3,FINSEQ_1:def 3;
end;

theorem
  for p be FinSequence of ExtREAL,
      q be FinSequence of REAL st p=q holds Sum(p)=Sum(q)
proof
   let p be FinSequence of ExtREAL;
   let q be FinSequence of REAL;
   assume
A1: p = q;
   consider P be Function of NAT,ExtREAL such that
A2: Sum(p) = P.(len p) &
    P.0 = 0. &
    for i be Nat st
     i < len p holds P.(i+1) = P.i + p.(i+1) by CONVFUN1:def 5;
   now per cases;
     case
A4:   len q = 0;
      Sum(q) = addreal "**" q by RVSUM_1:def 10;
      hence thesis by A2,A1,SUPINF_2:def 1,BINOP_2:2,A4,FINSOP_1:def 1;
     end;
     case
A6:   len q <> 0;
      Sum(q) = addreal $$ q by RVSUM_1:def 10; then
      consider Q be Function of NAT,REAL such that
A7:    Q.1 = q.1 &
       (for n be Nat st 0 <> n & n < len q holds
         Q.(n+1) = addreal.(Q.n,q.(n+1))) & Sum(q) = Q.(len q)
          by A6,FINSOP_1:def 1;
      defpred P1[Nat] means
      0 <> $1 & $1 <= len q implies P.$1 = Q.$1;
A8:   P1[0];
A9:   for k be Nat st P1[k] holds P1[k+1]
      proof
        let k be Nat;
        assume
A10:     P1[k];
        assume
A11:     0 <> k+1 & k+1 <= len q;
        per cases;
          suppose
A12:        k = 0;
            k < len q by A11,NAT_1:38; then
            P.(k+1) = P.k + p.(k+1) by A1,A2
                   .= p.(k+1) by SUPINF_2:18,A2,A12;
            hence thesis by A1,A12,A7;
          end;
          suppose
A14:        k <> 0;
A15:        k < len q by A11,NAT_1:38; then
A16:        P.(k+1) = P.k + p.(k+1) by A1,A2;
            Q.(k+1) = addreal.(Q.k,q.(k+1)) by A14,A15,A7
                   .= Q.k + q.(k+1) by BINOP_2:def 9;
            hence thesis by A1,A10,A11,NAT_1:38,A14,A16,SUPINF_2:1;
          end;
      end;
      for n be Nat holds P1[n] from NAT_1:sch 1(A8,A9);
      hence thesis by A2,A1,A6,A7;
     end;
   end;
   hence thesis;
end;

theorem LM042:
for p be FinSequence of ExtREAL st
 (for n be Nat st n in dom p holds 0. <=' p.n) &
 (ex n be Nat st n in dom p & p.n=+infty) holds
   Sum(p)=+infty
proof
   let p be FinSequence of ExtREAL;
   assume that
AS1:for n be Nat st n in dom p holds 0. <=' p.n and
AS2:ex n be Nat st n in dom p & p.n=+infty;
   consider m be Nat such that
A1: m in dom p & p.m = +infty by AS2;
   consider f be Function of NAT,ExtREAL such that
A2: Sum(p) = f.(len p) & f.0 = 0. &
    for i be Nat st i < len p holds f.(i+1)=f.i + p.(i+1) by CONVFUN1:def 5;
   defpred P[Nat] means
   $1 <= len p implies
    ($1 < m implies 0. <=' f.$1) & ($1 >= m implies f.$1 = +infty);
P0:P[0]
   proof
    assume 0 <= len p;
    1 <= m by A1,FINSEQ_3:27;
    hence thesis by A2;
   end;
Pi:for i be Nat st P[i] holds P[i+1]
   proof
    let i be Nat;
    assume L1:P[i];
    assume L2:i+1 <= len p;
    now per cases;
     case L3:i+1 < m; 
L4:   i <= i+1 by NAT_1:29;
      i < len p by L2,NAT_1:38; then
L5:   f.(i+1) = f.i + p.(i+1) by A2;
      1 <= i+1 by NAT_1:29; then
      i+1 in Seg len p by L2,FINSEQ_1:3; then
      i+1 in dom p by FINSEQ_1:def 3; then
      0. <=' p.(i+1) by AS1;
      hence thesis by L1,L2,L3,L4,AXIOMS:22,L5,EXTREAL2:7;
     end;
     case L6:i+1 >= m;
      now per cases;
       case L8:i+1 = m;
        i < len p by NAT_1:38,L2; then
        f.(i+1) = f.i + p.(i+1) by A2;
        hence f.(i+1) = +infty 
         by NAT_1:38,L2,L1,SUPINF_2:19,A1,L8,SUPINF_2:def 2;
       end;
       case i+1 <> m; then
L12:    i+1 > m by L6,REAL_1:def 5;
        1 <= i+1 by NAT_1:29; then
        i+1 in Seg len p by L2,FINSEQ_1:3; then
        i+1 in dom p by FINSEQ_1:def 3; then
L13:    p.(i+1) <> -infty by AS1,SUPINF_2:19;
        i < len p by NAT_1:38,L2; then
        f.(i+1) = f.i + p.(i+1) by A2;
        hence f.(i+1) = +infty by L12,L13,SUPINF_2:def 2,L2,L1,NAT_1:38;
       end;
      end;
      hence thesis by L6;
     end;
    end;
    hence thesis;
   end;
PP:for i be Nat holds P[i] from NAT_1:sch 1(P0,Pi);
   m in Seg len p by A1,FINSEQ_1:def 3; then
   len p >= m by FINSEQ_1:3;
   hence thesis by A2,PP;
end;

definition
let X be non empty set;
let S be sigma_Field_Subset of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
  assume AS: f is_simple_func_in S
                & dom f <> {}
                & for x be set st x in dom f holds 0. <=' f.x;
func integral(X,S,M,f) -> Element of ExtREAL means
  ex F be Finite_Sep_Sequence of S,
      a, x be FinSequence of ExtREAL st F,a are_Re-presentation_of f
         & a.1 =0.
         & (for n be Nat st 2 <= n & n in dom a holds
           0. <' a.n & a.n <' +infty )
         & dom x = dom F
         & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
         & it=Sum(x);
existence
proof
consider F be Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL such that
          AS0: F,a are_Re-presentation_of f and
          AS01: a.1 =0. and
          AS02: for n be Nat st 2 <= n & n in dom a holds
              0. <' a.n & a.n <' +infty by AS,LM061;
deffunc G(set) = (a.$1)*((M*F).$1);
defpred P[set] means $1 in dom F;
P1:  for x be set st P[x] holds G(x) in ExtREAL;
consider p be PartFunc of NAT,ExtREAL such that
 P2: (for x be set holds x in dom p iff x in NAT & P[x]) &
       (for x be set st x in dom p holds p.x = G(x))
           from PARTFUN1:sch 3(P1);
  for x be set st x in dom p holds x in dom F by P2; then
  Q1: dom p c= dom F by TARSKI:def 3;
      for x be set st x in dom F holds x in dom p
      proof
        let x be set;
        assume Q11:x in dom F;
        dom F c= NAT by RELSET_1:12;
        hence thesis by P2,Q11;
      end; then
      dom F c= dom p by TARSKI:def 3; then
        P3: dom p = dom F by Q1,XBOOLE_0:def 10;
  then dom p = Seg len F by FINSEQ_1:def 3;
  then p is FinSequence & rng p c= ExtREAL by FINSEQ_1:def 2,RELSET_1:12;
  then
  reconsider p as FinSequence of ExtREAL by FINSEQ_1:def 4;
  P4: for n be Nat st n in dom p holds p.n=a.n*(M*F).n by P2;
take Sum(p);
thus thesis by AS0,AS01,AS02,P3,P4;
end;
uniqueness
proof
let s1,s2 be Element of ExtREAL such that
  A1: ex F1 be Finite_Sep_Sequence of S,
      a1, x1 be FinSequence of ExtREAL
      st F1,a1 are_Re-presentation_of f
         & a1.1 =0.
         & (for n be Nat st 2 <= n & n in dom a1
           holds  0. <' a1.n & a1.n <' +infty)
         & dom x1 = dom F1
         & (for n be Nat st n in dom x1 holds x1.n=a1.n*(M*F1).n)
         & s1=Sum(x1) and
  A2:
  ex  F2 be Finite_Sep_Sequence of S, 
       a2 be FinSequence of ExtREAL, 
       x2 be FinSequence of ExtREAL
       st F2,a2 are_Re-presentation_of f
         &  a2.1 =0.
         &  (for n be Nat st 2 <= n & n in dom a2 
              holds  0. <' a2.n & a2.n <' +infty)
         &  dom x2 = dom F2
         &  (for n be Nat st n in dom x2 holds x2.n=a2.n*(M*F2).n) 
         &  s2=Sum(x2);
   thus s1=s2
   proof
      consider F1 be Finite_Sep_Sequence of S,
      a1 be FinSequence of ExtREAL,
      x1 be FinSequence of ExtREAL such that
      AS0:    F1,a1 are_Re-presentation_of f and
      AS1:    a1.1 =0. and
      AS2:  (for n be Nat st 2 <= n & n in dom a1
                         holds  0. <' a1.n & a1.n <' +infty) and
      AS3: dom x1 = dom F1 and
      AS4: (for n be Nat st n in dom x1 holds x1.n=a1.n*(M*F1).n) and
      AS5: s1=Sum(x1) by A1;
      AS8: dom F1 = dom a1 by AS0,def_1;
      consider F2 be Finite_Sep_Sequence of S,
      a2 be FinSequence of ExtREAL,
      x2 be FinSequence of ExtREAL such that
      BS0:    F2,a2 are_Re-presentation_of f and
      BS1:    a2.1 =0. and
      BS2:  (for n be Nat st 2 <= n & n in dom a2
                   holds  0. <' a2.n & a2.n <' +infty) and
      BS3: dom x2 = dom F2 and
      BS4: (for n be Nat st n in dom x2 holds x2.n=a2.n*(M*F2).n) and
      BS5: s2=Sum(x2) by A2;
      BS8: dom F2 = dom a2 by BS0,def_1;
      C1:M is nonnegative & M.{} = 0. by MEASURE1:def 11;
      C2:for n be Nat st n in dom F1 holds 0. <=' (M*F1).n
          proof
          let n be Nat;
            assume A: n in dom F1; then
            B: (M*F1).n=M.(F1.n) by FUNCT_1:23;
            P5: rng F1 c= S by FINSEQ_1:def 4;
            F1.n in rng F1 by A,FUNCT_1:12; 
            hence thesis by P5,B,SUPINF_2:58,C1;
          end;
      C3:for n be Nat st n in dom F2 holds 0. <=' (M*F2).n
          proof
          let n be Nat;
            assume A: n in dom F2; then
            B: (M*F2).n=M.(F2.n) by FUNCT_1:23;
            P5: rng F2 c= S by FINSEQ_1:def 4;
            F2.n in rng F2 by A,FUNCT_1:12;
            hence thesis by P5,B,SUPINF_2:58,C1;
          end;
        C6:for n be Nat st n in dom a1 holds 0. <=' a1.n
          proof
          let n be Nat;
            assume A: n in dom a1;
            now per cases;
             case n=1;
             thus a1.1=0. by AS1;
             end;
             case B2:n<>1;
             n in Seg len a1 by A,FINSEQ_1:def 3;
             then 1 <= n by FINSEQ_1:3;
             then 1 < n by B2,REAL_1:def 5;
             then 1+1 < n+1 by REAL_1:67;
             then 2<=n by NAT_1:38;
             hence 0. <=' a1.n by AS2,A;
             end;
            end;
            hence thesis;
            end;
      C7: for n be Nat st n in dom a2 holds 0. <=' a2.n
          proof
          let n be Nat;
            assume A: n in dom a2;
            now per cases;
             case n=1;
             thus a2.1=0. by BS1;
             end;
             case B2:n<>1;
             n in Seg len a2 by A,FINSEQ_1:def 3;
             then 1 <=n by FINSEQ_1:3;
             then 1 < n by B2,REAL_1:def 5;
             then 1+1 < n+1 by REAL_1:67;
             then 2<=n by NAT_1:38;
              hence 0. <=' a2.n by BS2,A;
             end;
            end;
            hence thesis;
            end;
        C8:for n be Nat st n in dom x1 holds 0. <=' x1.n
          proof
          let n be Nat;
            assume A: n in dom x1; then
            B2: 0. <=' a1.n by C6,AS3,AS8;
            B4: 0. <=' (M*F1).n by C2,AS3,A;
            0. <=' (a1.n)*( (M*F1).n) by B2,B4,EXTREAL2:45;
            hence thesis by A,AS4;
          end;
      C9:for n be Nat st n in dom x2 holds 0. <=' x2.n
          proof
          let n be Nat;
            assume A: n in dom x2; then
            B2: 0. <=' a2.n by C7,BS3,BS8;
            B4: 0. <=' (M*F2).n by C3,BS3,A;
            0. <=' (a2.n)*( (M*F2).n) by B2,B4,EXTREAL2:45;
            hence thesis by A,BS4;
          end;
   now per cases;
      case CA1: ex i,j be Nat st
                     i in dom F1 & j in dom F2
                     &2<= i & 2<=j  & M.(F1.i /\ F2.j)=+infty;
            consider i,j be Nat such that
            CA11: i in dom F1 & j in dom F2 
                   &2<= i & 2<=j & M.(F1.i /\ F2.j)=+infty by CA1;
                R5: rng F1 c= S by FINSEQ_1:def 4;
             Q1':F1.i in rng F1 by CA11,FUNCT_1:12; then
              Q2:  F1.i is_measurable M by R5,MEASURE1:def 12;
                 S5: rng F2 c= S by FINSEQ_1:def 4;
              Q3':   F2.j in rng F2 by CA11,FUNCT_1:12; then
              Q4:  F2.j is_measurable M by S5,MEASURE1:def 12;
               F1.i /\ F2.j is_measurable M by Q2,Q4,MEASURE1:66; then
              Q6:  F1.i /\ F2.j in S by MEASURE1:def 12;
              Q7:  F1.i /\ F2.j c= F1.i by XBOOLE_1:17;
              Q8:  F1.i /\ F2.j c= F2.j by XBOOLE_1:17;
               +infty <=' M.(F1.i) by CA11,Q1',R5,Q6,Q7,MEASURE1:62; then
              Q10:M.(F1.i)= +infty by SUPINF_1:24;
               +infty <=' M.(F2.j) by CA11,Q3',S5,Q6,Q8,MEASURE1:62; then
              Q12:M.(F2.j)= +infty by SUPINF_1:24;
              P1: (M*F1).i = +infty by FUNCT_1:23,CA11,Q10;
                0. <' a1.i by AS2,AS8,CA11; then
              +infty = ( a1.i)*((M*F1).i) by P1,EXTREAL1:def 1; then
               x1.i=+infty by AS3,AS4,CA11; then
             P4:  Sum(x1)=+infty by LM042,C8,AS3,CA11;
              U1: (M*F2).j = +infty by FUNCT_1:23,CA11,Q12;
                0. <' a2.j by BS2,BS8,CA11; then
              +infty = (a2.j)*((M*F2).j) by U1,EXTREAL1:def 1; then
               x2.j=+infty by BS3,BS4,CA11;
           hence s1=s2 by AS5,BS5,P4,LM042,C9,BS3,CA11;
      end;
     case CA2: for i,j be Nat
      st  i in dom F1 & j in dom F2 & 2<= i & 2<=j  holds
      M.(F1.i /\ F2.j) <> +infty;
        set n= len x1;
        set m= len x2;
        ex a be Function of [:(Seg n),(Seg m) :],REAL
         st for i,j be Nat st i in Seg n & j in Seg m
         holds a.[i,j] = a1.i * (M.(F1.i /\ F2.j))
         proof
         deffunc F(set,set) = a1.$1 * (M.(F1.$1 /\ F2.$2));
         L1: for x,y be set st x in Seg n & y in Seg m
          holds F(x,y) in REAL
  proof
  let x,y be set such that
  L4: x in (Seg n)  & y in (Seg m);
     x in { k where k is Nat : 1 <= k & k <= n } by L4,FINSEQ_1:def 1;
     then consider kx be Nat such that
    L5: x=kx &  ( 1 <= kx & kx <= n );
    y in { k where k is Nat : 1 <= k & k <= m } by L4,FINSEQ_1:def 1;
    then consider ky be Nat such that
    L7: y=ky & ( 1 <= ky & ky <= m );
    L9: kx in dom F1 by L5,L4,AS3,FINSEQ_1:def 3;
    L10: ky in dom F2 by L7,L4,BS3,FINSEQ_1:def 3;
    R5: rng F1 c= S by FINSEQ_1:def 4;
    F1.kx in rng F1 by L9,FUNCT_1:12; then
    Q2:  F1.kx is_measurable M by R5,MEASURE1:def 12;
    S5: rng F2 c= S by FINSEQ_1:def 4;
    F2.ky in rng F2 by L10,FUNCT_1:12; then
    Q4:  F2.ky is_measurable M by S5,MEASURE1:def 12;
      F1.kx /\ F2.ky is_measurable M by Q2,Q4,MEASURE1:66; then
    Q6:  F1.kx /\ F2.ky in S by MEASURE1:def 12;
    now per cases;
        case C1:  not (2 <= kx & 2 <= ky);
            now per cases by C1;
              case CL1: kx < 2;
               kx <=1+1 by CL1; then
               CL2':kx <= 1 by NAT_1:26,CL1;
               F(kx,ky) = 0. * (M.(F1.kx /\ F2.ky)) by CL2',L5,AXIOMS:21,AS1
               .=0. by EXTREAL1:def 1;
               hence F(kx,ky) in REAL by SUPINF_2:def 1;
               end;
              case CR1: ky < 2;
               ky <=1+1 by CR1; then
               CR2':ky <= 1 by NAT_1:26,CR1;
               now per cases;
                 case F1.kx /\ F2.ky = {};
                    hence F(kx,ky) = a1.kx * 0. by MEASURE1:def 11
                                   .=0. by EXTREAL1:def 1;
                 end;
                 case L2:  F1.kx /\ F2.ky <> {};
                   consider x be set such that
                     L3: x in F1.kx /\ F2.ky by L2,XBOOLE_0:def 1;
                     L4: x in F1.kx & x in F2.ky by L3,XBOOLE_0:def 3;
                     a1.kx=f.x by AS0,L9,L4,def_1
                            .=a2.ky by BS0,L10,L4,def_1
                            .=0. by BS1,CR2',L7,AXIOMS:21;
                    hence F(kx,ky) = 0. by EXTREAL1:def 1;
                 end;
               end;
               hence F(kx,ky) in REAL by SUPINF_2:def 1;
               end;
            end;
           hence F(kx,ky) in REAL;
        end;
        case C2: 2 <= kx & 2 <= ky;
           Q8: M.(F1.kx /\ F2.ky) <> +infty by C2,L9,L10,CA2;
           M  is nonnegative by MEASURE1:def 11;
           then 0. <=' M.(F1.kx /\ F2.ky)  by Q6,SUPINF_2:58; then
            reconsider r3=M.(F1.kx /\ F2.ky) as Real
              by SUPINF_2:19,EXTREAL1:1,Q8;
            a1.kx is Real
            proof
              Q15: a1.kx <> +infty by C2,L9,AS2,AS8;
               0. <=' a1.kx by C6,L9,AS8;
           hence a1.kx is Real by SUPINF_2:19,EXTREAL1:1,Q15;
            end;
            then reconsider r2=a1.kx as Real;
            a1.kx*M.(F1.kx /\ F2.ky) = r2*r3 by EXTREAL1:13;
           hence F(kx,ky) in REAL;
           end;
      end;
   hence thesis by L7,L5;
 end;
         consider f being Function of [:(Seg n),(Seg m) :],REAL such that
         L2: for x,y be set st x in Seg n & y in Seg m
           holds f.[x,y]=F(x,y) from FUNCT_2:sch 6(L1);
           take f;
           thus thesis by L2;
         end;
    then consider a be Function of [:(Seg n),(Seg m) :],REAL such that
    P1: for i,j be Nat st i in Seg n & j in Seg m
         holds a.[i,j] = a1.i * (M.(F1.i /\ F2.j));
     ex p be FinSequence of REAL st dom p=Seg n
            & for i be Nat st i in dom p
                  holds ex r be FinSequence of REAL st
                        (dom r = Seg m
                         & p.i=Sum(r)
                         & for j be Nat st j in dom(r) holds r.j=a.[i,j] )
   proof
     defpred P[Nat,set] means
     ex r be FinSequence of REAL st
               dom(r) = Seg m & $2=Sum(r)
             & for j be Nat st j in dom(r) holds r.j=a.[$1,j];
     K1: for k be Nat,y1,y2 be set st
          k in Seg n & P[k,y1] & P[k,y2] holds y1=y2 
          proof
          let k be Nat,y1,y2 be set such that k in Seg n and
             AS1:   P[k,y1] and
             AS2:   P[k,y2];
             consider r1 be FinSequence of REAL such that
                     AS11: dom(r1) = Seg m and
                     AS12: y1=Sum(r1) and
                     AS13: for j be Nat st j in dom(r1)
                       holds r1.j=a.[k,j]  by AS1;
             consider r2 be FinSequence of REAL such that
                     AS21: dom(r2) = Seg m and
                     AS22: y2=Sum(r2) and
                     AS23: for j be Nat st j in dom(r2)
                       holds r2.j=a.[k,j] by AS2;
             now let j be Nat;
                 assume AG: j in dom (r1);
                 hence r1.j = a.[k,j] by AS13
                             .= r2.j by AS23,AG,AS11,AS21;
                 end;
               hence thesis by AS12,AS22,AS11,AS21,FINSEQ_1:17; 
          end;
     K2: for k be Nat st k in Seg n ex x be set st P[k,x]
     proof
     let k be Nat;
      assume k in Seg n;
       deffunc G(set)= a.[k,$1];
        consider r being FinSequence such that
          U1: len r = m and
          U2:  for i be Nat st i in Seg m holds r.i=G(i) from FINSEQ_1:sch 2;
          P1: dom(r) = Seg m by U1,FINSEQ_1:def 3;
          for i be Nat st i in dom r holds r.i in REAL
          proof
            let i be Nat;
            assume i in dom r; then
            P3: i in Seg m by U1,FINSEQ_1:def 3;
            a.[k,i] in REAL;
            hence r.i in REAL by U2,P3; 
          end; 
          then 
          reconsider r as FinSequence of REAL 
          by FINSEQ_2:14;
          take x=Sum(r);
          thus thesis by P1,U2;
     end;
     consider p be FinSequence such that
     K3: dom p = Seg n & for k be Nat st k in Seg n holds P[k,p.k]
     from FINSEQ_1:sch 1(K1,K2);
     for i be Nat st i in dom p holds p.i in REAL
     proof
     let i be Nat;
     assume i in dom p; then
     consider r be FinSequence of REAL such that
     Q1:   dom(r) = Seg m & p.i=Sum(r)
                  & for j be Nat st j in dom(r) holds r.j=a.[i,j] by K3;
      thus p.i in REAL by Q1;
     end;
     then reconsider p as FinSequence of REAL by FINSEQ_2:14;
     take p;
     thus thesis by K3;
   end;
    then consider p be FinSequence of REAL such that
      P3:    dom p=Seg n & for i be Nat st i in dom p
                  holds ex r be FinSequence of REAL st
                        (dom r = Seg m & p.i=Sum r
                         & for j be Nat st j in dom r holds r.j=a.[i,j]);
     ex   q be FinSequence of REAL st dom q=Seg m
           & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st
                       (dom s = Seg n & q.j=Sum s
                        & for i be Nat st i in dom s holds s.i=a.[i,j] )
  proof
   defpred P[Nat,set] means
                 ex s be FinSequence of REAL st
                       dom(s) = Seg n & $2=Sum(s)
                       & for i be Nat st i in dom(s) holds s.i=a.[i,$1];
     K1: for k be Nat,y1,y2 be set st
          k in Seg m & P[k,y1] & P[k,y2] holds y1=y2
               proof
          let k be Nat,y1,y2 be set such that k in Seg m and
             AS1:   P[k,y1] and
             AS2:   P[k,y2];
             consider s1 be FinSequence of REAL such that
                     AS11: dom(s1) = Seg n and
                     AS12: y1=Sum(s1) and
                     AS13: for j be Nat st j in dom s1
                       holds s1.j=a.[j,k]  by AS1;
             consider s2 be FinSequence of REAL such that
                     AS21: dom(s2) = Seg n and
                     AS22: y2=Sum(s2) and
                     AS23: for j be Nat st j in dom(s2)
                       holds s2.j=a.[j,k] by AS2;
             now let j be Nat;
                 assume AG: j in dom (s1);
                 hence s1.j = a.[j,k] by AS13
                             .= s2.j by AS23,AG,AS11,AS21;
                 end;
               hence thesis by AS12,AS22,AS11,AS21,FINSEQ_1:17; 
          end;
     K2: for k be Nat st k in Seg m ex x be set st P[k,x]
      proof
          let k be Nat;
           assume  k in Seg m;
            deffunc G(set)= a.[$1,k];
             consider s being FinSequence such that
               U1: len s = n and
               U2:  for i be Nat st i in Seg n holds s.i=G(i)
                       from FINSEQ_1:sch 2;
               P1: dom(s) = Seg n by U1,FINSEQ_1:def 3;
               for i be Nat st i in dom s holds s.i in REAL
               proof
                 let i be Nat;
                 assume i in dom s; then
                 P3: i in Seg n by U1,FINSEQ_1:def 3;
                 a.[i,k] in REAL;
                 hence s.i in REAL by U2,P3;
               end;
               then reconsider s as FinSequence of REAL by FINSEQ_2:14;
               take x=Sum(s);
               thus thesis by P1,U2;
          end;
     consider p be FinSequence such that
     K3: dom p = Seg m & for k be Nat st k in Seg m holds P[k,p.k]
     from FINSEQ_1:sch 1(K1,K2);
     for i be Nat st i in dom p holds p.i in REAL
          proof
          let i be Nat;
          assume i in dom p; then
          consider s be FinSequence of REAL such that
          Q1: dom(s) = Seg n & p.i=Sum(s)
                       & for j be Nat st j in dom(s)
                       holds s.j=a.[j,i] by K3;
           thus p.i in REAL by Q1;
          end;
     then reconsider p as FinSequence of REAL by FINSEQ_2:14;
     take p;
     thus thesis by K3;
   end;
    then consider q be FinSequence of REAL such that
     P5:  dom q=Seg m & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st (dom(s) = Seg n
                        & q.j=Sum(s)
                        & for i be Nat st i in dom(s) holds s.i=a.[i,j]);
     P6: Sum(p)=Sum(q) by LM01,P3,P5;
     p=x1
     proof
       T1: dom p = dom x1 by P3,FINSEQ_1:def 3;
       T2: for k be Nat st k in dom p holds p.k = x1.k
       proof
       let k be Nat;
         assume T3: k in dom p;
         consider r be FinSequence of REAL such that
          T4:dom r = Seg m and
          T5:p.k=Sum r and
          T6:for j be Nat st j in dom r holds r.j=a.[k,j] by P3,T3;
          T7: for j be Nat st j in dom r holds
               r.j=a1.k * (M.(F1.k /\ F2.j))
               proof
                 let j be Nat;
                  assume
                  T71: j in dom r;
                  hence r.j=a.[k,j] by T6
                     .=a1.k * (M.(F1.k /\ F2.j)) by T71,T3,P3,T4,P1;
               end;
           ex F11 be Finite_Sep_Sequence of S st union rng F11 = F1.k &
                  dom F11=dom r &
                  for j be Nat st j in dom r holds F11.j=F1.k /\ F2.j
           proof
           deffunc G(set)=F1.k /\ F2.$1;
           consider F being FinSequence such that
             U1: len F = m and
             U2: for i be Nat st i in Seg m holds F.i=G(i)
             from FINSEQ_1:sch 2;
             K1: dom F = Seg m by FINSEQ_1:def 3,U1;
             for i be Nat st i in dom F holds F.i in S
             proof
                let i be Nat;
                assume L0: i in dom F;
                L8: i in Seg m by L0,FINSEQ_1:def 3,U1;
                 L10: i in dom F2 by L8,BS3,FINSEQ_1:def 3;
                 R5: rng F1 c= S by FINSEQ_1:def 4;
                  F1.k in rng F1 by T1,T3,AS3,FUNCT_1:12; then
                  Q2: F1.k is_measurable M by R5,MEASURE1:def 12;
                  S5: rng F2 c= S by FINSEQ_1:def 4;
                  F2.i in rng F2 by L10,FUNCT_1:12; then
                  Q4: F2.i is_measurable M by S5,MEASURE1:def 12;
                    F1.k /\ F2.i is_measurable M by Q2,Q4,MEASURE1:66;
                  then F1.k /\ F2.i in S by MEASURE1:def 12;
                  hence F.i in S by L0,K1,U2;
             end;
             then reconsider F as FinSequence of S by FINSEQ_2:14;
             K2: dom F = dom F2 by K1,BS3,FINSEQ_1:def 3;
             then reconsider F as Finite_Sep_Sequence of S by LM048a,U2,K1;
             take F;
             union rng F = F1.k
             proof
                thus union rng F = F1.k /\ union rng F2
                   by LM048b,U2,K2,K1
                                .= F1.k /\ dom f by BS0,def_1
                                .= F1.k /\ union rng F1 by AS0,def_1
                                .= F1.k by LM048c,T1,T3,AS3;
             end;
             hence thesis by FINSEQ_1:def 3,U1,T4,U2;
            end;
           then consider F11 be Finite_Sep_Sequence of S such that
           M1: union rng F11 = F1.k & dom F11=dom r &
               for j be Nat st j in dom r holds F11.j=F1.k /\ F2.j;
           M2: Sum(M*F11) = M.((F1.k)) by M1,LM050;
           reconsider rr=r as FinSequence of ExtREAL by LM052;
           MM: for j be Nat st j in dom rr holds rr.j=a1.k * (M*F11).j
           proof
             let j be Nat;
              assume
              T71:j in dom rr;
              thus rr.j = a1.k * (M.(F1.k /\ F2.j)) by T7,T71
                   .=a1.k *(M.(F11.j)) by M1,T71
                   .=a1.k *(M*F11).j  by FUNCT_1:23,T71,M1;
          end;
          k in Seg len a1 by T1,T3,AS3,AS8,FINSEQ_1:def 3; then
Z11:      1 <= k by FINSEQ_1:3;
Z1:       a1.k <> -infty & a1.k <> +infty
          proof
           now per cases;
            case k = 1;
              hence thesis by AS1,SUPINF_2:19;
            end;
            case k <> 1; then
              1 < k by Z11,REAL_1:def 5; then
              1+1 <= k by NAT_1:38;
              hence thesis by AS2,T1,T3,AS3,AS8,SUPINF_2:19;
            end;
           end;
           hence thesis;
          end;
          dom rr = dom (M*F11) by M1,LM049;
           then Sum rr=(a1.k)*Sum(M*F11) by MM,Z1,LM051; then
            Sum r=(a1.k)*M.((F1.k)) by M2,LM02
                         .=a1.k*((M*F1).k) by FUNCT_1:23,T1,T3,AS3;
           hence thesis by T1,T3,T5,AS4;
       end;
       thus thesis by T1,T2,FINSEQ_1:17;
     end; then
     P7: Sum p = Sum x1 by LM02;
     q=x2
     proof
       T1: dom q = dom x2 by P5,FINSEQ_1:def 3;
       T2: for k be Nat st k in dom q holds q.k = x2.k
       proof
       let k be Nat;
         assume T3: k in dom q;
         consider s be FinSequence of REAL such that
          T4:dom s = Seg n and
          T5:q.k=Sum s and
          T6:for i be Nat st i in dom s holds s.i=a.[i,k] by P5,T3;
          T7: for i be Nat st i in dom s holds
               s.i=a1.i * (M.(F1.i /\ F2.k))
               proof
                 let i be Nat;
                  assume
                  T71: i in dom s;
                  thus s.i=a.[i,k] by T6,T71
                     .=a1.i * (M.(F1.i /\ F2.k)) by T71,T3,P5,T4,P1;
               end;
          T8: for i be Nat st i in dom s holds
               s.i=a2.k * (M.(F1.i /\ F2.k))
               proof
                 let i be Nat;
                  assume
                  T81: i in dom s;
                   now per cases;
                      case L1: F1.i /\ F2.k = {};
                         M.(F1.i /\ F2.k)=0. by L1,MEASURE1:def 11;
                         hence s.i = a1.i * 0. by T7,T81
                                .=0. by EXTREAL1:def 1
                                .=a2.k *0. by EXTREAL1:def 1
                                .=a2.k *M.(F1.i /\ F2.k) by L1,MEASURE1:def 11;
                      end;
                      case L2: F1.i /\ F2.k <> {};
                        consider x be set such that
                          L3: x in F1.i /\ F2.k by L2,XBOOLE_0:def 1;
                          L4: x in F1.i & x in F2.k by L3,XBOOLE_0:def 3;
                          dom p = dom x1 by P3,FINSEQ_1:def 3; then
                          a1.i=f.x by AS0,L4,def_1,T81,T4,P3,AS3
                             .=a2.k by BS0,T1,T3,BS3,L4,def_1;
                         hence s.i = a2.k*M.(F1.i /\ F2.k) by T7,T81;
                      end;
                    end;
                 hence thesis;
               end;       
           ex F21 be Finite_Sep_Sequence of S st union rng F21 = F2.k &
                  dom F21=dom(s) &
                  for j be Nat st j in dom(s) holds F21.j=F1.j /\ F2.k
           proof
           deffunc G(set)= F2.k /\ F1.$1;
           consider F being FinSequence such that
             U1: len F = n and
             U2: for i be Nat st i in Seg n holds F.i=G(i)
             from FINSEQ_1:sch 2;
             K1: dom F = Seg n by FINSEQ_1:def 3,U1;
             for i be Nat st i in dom F holds F.i in S  
             proof
                let i be Nat;
                assume L0: i in dom F; then
                L8: i in Seg n by FINSEQ_1:def 3,U1;
                 L10: i in dom F1 by L8,AS3,FINSEQ_1:def 3;
                 R5: rng F1 c= S by FINSEQ_1:def 4;
                  F1.i in rng F1 by L10,FUNCT_1:12; then
                  Q2: F1.i is_measurable M by R5,MEASURE1:def 12;
                  S5: rng F2 c= S by FINSEQ_1:def 4;
                  F2.k in rng F2 by T1,T3,BS3,FUNCT_1:12; then
                  Q4: F2.k is_measurable M by S5,MEASURE1:def 12;
                    F1.i /\ F2.k is_measurable M by Q2,Q4,MEASURE1:66; then
                    F1.i /\ F2.k in S by MEASURE1:def 12;
                  hence F.i in S by L0,K1,U2;
             end;
             then reconsider F as FinSequence of S by FINSEQ_2:14;
             K2: dom F = dom F1 by K1,AS3,FINSEQ_1:def 3;
             then reconsider F as Finite_Sep_Sequence of S by LM048a,U2,K1;
             take F;
             union rng F = F2.k
             proof
               thus union rng F = F2.k /\ union rng F1
                 by LM048b,U2,K2,K1
                               .= F2.k /\ dom f by AS0,def_1
                               .= F2.k /\ union rng F2 by BS0,def_1
                               .= F2.k by LM048c,T1,T3,BS3;
             end;
             hence thesis by FINSEQ_1:def 3,U1,T4,U2;
            end;
           then
           consider F21 be Finite_Sep_Sequence of S such that
       M1: union rng F21 = F2.k & dom F21 = dom s &
                  for i be Nat st i in dom s holds F21.i=F1.i /\ F2.k;
            M2: Sum(M*F21) = M.((F2.k)) by M1,LM050;
            reconsider ss=s as FinSequence of ExtREAL by LM052;
            MM: for j be Nat st j in dom ss holds ss.j=a2.k * (M*F21).j
            proof
              let j be Nat;
              assume
          T71:j in dom ss;
              thus ss.j = a2.k * (M.(F1.j /\ F2.k)) by T71,T8
                      .=a2.k *(M.(F21.j)) by M1,T71
                      .=a2.k *(M*F21).j by FUNCT_1:23,T71,M1;
           end;
           k in Seg len a2 by T1,T3,BS3,BS8,FINSEQ_1:def 3; then
Z21:       1 <= k by FINSEQ_1:3;
Z2:        a2.k <> -infty & a2.k <> +infty
           proof
            per cases;
             suppose k = 1;
               hence thesis by BS1,SUPINF_2:19;
             end;
             suppose k <> 1; then
               1 < k by Z21,REAL_1:def 5; then
               1+1 <= k by NAT_1:38;
               hence thesis by BS2,T1,T3,BS3,BS8,SUPINF_2:19;
             end;
           end;
           dom ss=dom (M*F21) by M1,LM049;
           then Sum(ss)=(a2.k)*Sum(M*F21) by Z2,MM,LM051;
           then Sum(s)=(a2.k)*M.((F2.k)) by LM02,M2
                         .=a2.k*((M*F2).k)
                         by FUNCT_1:23,T1,T3,BS3;
           hence thesis by T1,T3,T5,BS4;
       end;
       thus thesis by T1,T2,FINSEQ_1:17;
      end;
     hence s1=s2 by AS5,BS5,P6,P7,LM02;
      end;
    end;
  hence thesis;
  end;
 end;
end;

begin :: Additional Lemmas

theorem :: RVSUM_1:110
for a be FinSequence of ExtREAL, p,N be Element of ExtREAL st
 N = len a &
 (for n be Nat st n in dom a holds a.n = p) holds
   Sum(a) = N * p by RVSUM_1_110;
