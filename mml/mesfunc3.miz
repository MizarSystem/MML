:: Lebesgue Integral of Simple Valued Function
::  by Yasunari Shidama and Noboru Endou
::
:: Received September 25, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PARTFUN1, SUPINF_1, MEASURE1, RELAT_1, FUNCT_1, COMPLEX1,
      SUPINF_2, BOOLE, ARYTM_1, TARSKI, ARYTM_3, FUNCOP_1, RVSUM_1, RLVECT_1,
      FUNCT_3, GROUP_1, PROB_2, FINSEQ_1, FINSEQ_2, INTEGRA1, MESFUNC2,
      MESFUNC3, VECTSP_1, SETWISEO, ORDINAL2, FINSOP_1, FINSET_1, SETFAM_1,
      CARD_4, CARD_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, XREAL_0, NAT_1,
      REAL_1, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2, SETWOP_2, FINSEQ_1,
      SETFAM_1, FUNCOP_1, FINSOP_1, FUNCT_3, FINSET_1, CARD_1, CARD_4, PROB_2,
      SUPINF_1, SUPINF_2, MEASURE1, MEASURE3, FINSEQOP, FINSEQ_2, BINOP_1,
      SEQ_1, BINOP_2, RVSUM_1, EXTREAL1, MESFUNC1, MESFUNC2, CONVFUN1;
 constructors REAL_1, NAT_1, BINOP_1, SETWISEO, FINSEQOP, FINSOP_1, EXTREAL1,
      SUPINF_2, PARTFUN1, PROB_2, MEMBERED, RAT_1, MESFUNC1, MEASURE3,
      MEASURE6, RVSUM_1, SEQ_1, MESFUNC2, CONVFUN1, BINARITH;
 registrations SUBSET_1, SUPINF_1, XREAL_0, RELSET_1, NAT_1, MEMBERED,
      FINSEQ_1, ORDINAL2, MEASURE1, BINOP_2;
 requirements NUMERALS, REAL, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0;
 theorems MEASURE1, TARSKI, SUPINF_1, FUNCT_1, FUNCT_2, NAT_1, REAL_1,
      SUPINF_2, AXIOMS, RELSET_1, EXTREAL1, EXTREAL2, MESFUNC1, ZFMISC_1,
      FINSEQ_1, FINSEQ_2, FINSEQ_3, FUNCOP_1, FINSEQ_4, PROB_2, FUNCT_3,
      XBOOLE_0, XBOOLE_1, XCMPLX_1, RELAT_1, RVSUM_1, BINOP_2, MESFUNC2,
      JORDAN3, SCMFSA_7, SETFAM_1, CONVFUN1, MEASURE2, FINSOP_1, FINSET_1,
      CARD_4, CARD_1, CARD_2;
 schemes FUNCT_2, FINSEQ_1, FINSEQ_2, NAT_1, PARTFUN1;

begin  :: Integral of Simple Valued Function ::

 reserve X for non empty set;
 reserve S for sigma_Field_Subset of X;
 reserve n,m for Nat;

theorem Th1:
for n,m be Nat, a be Function of [:(Seg n),(Seg m):],REAL
for p,q be FinSequence of REAL st
( dom p=Seg n & for i be Nat st i in dom p holds
   ex r be FinSequence of REAL st
    (dom r = Seg m & p.i = Sum r
     & for j be Nat st j in dom r holds r.j=a.[i,j])) &
( dom q=Seg m & for j be Nat st j in dom q holds
   ex s be FinSequence of REAL st
    (dom s = Seg n & q.j = Sum s
     & for i be Nat st i in dom s holds s.i=a.[i,j]))
holds Sum p = Sum q
proof
   defpred P[Nat] means
   for m be Nat, a be Function of [: Seg $1, Seg m :],REAL
   for p,q be FinSequence of REAL
    st (dom p=Seg $1
        & for i be Nat st i in dom p
              holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j] )) &
        ( dom q=Seg m
        & for j be Nat st j in dom q
              holds ex s be FinSequence of REAL st
                     (dom(s) = Seg $1 & q.j=Sum(s)
                     & for i be Nat st i in dom(s) holds s.i=a.[i,j]))
   holds Sum(p)=Sum(q);
 A1: P[0]
 proof
 now
 let m be Nat, a be Function of [: Seg 0, Seg m :],REAL;
 let p,q be FinSequence of REAL such that
        A2:  dom p=Seg 0 and
           for i be Nat st i in dom p
                holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j]) and
        A3:  dom q=Seg m and
        A4:for j be Nat st j in dom q
              holds ex s be FinSequence of REAL st
                     (dom(s) = Seg 0 & q.j=Sum(s)
                     & for i be Nat st i in dom(s) holds s.i=a.[i,j] );
thus Sum(p)=Sum(q)
proof
 len p = 0 by A2,FINSEQ_1:def 3; then
 A5: Sum(p)=0 by FINSEQ_1:32,RVSUM_1:102;
 A6: q = dom q --> 0
    proof
     now let z be set;
     assume
     A7: z in dom q;
        z in { k where k is Nat : 1 <= k & k <= m }
        by A3,A7,FINSEQ_1:def 1; then
        consider k be Nat such that
            A8:z=k & 1 <= k & k <= m;
          reconsider j=z as Nat by A8;
        consider s be FinSequence of REAL such that
           A9: dom(s) = Seg 0 and
           A10: q.j=Sum(s) and
            for i be Nat st i in dom(s) holds s.i=a.[i,j] by A4,A7;
          len s = 0 by A9,FINSEQ_1:def 3;
          hence q.z=0 by A10,FINSEQ_1:32,RVSUM_1:102;
         end;
    hence q = dom q --> 0 by FUNCOP_1:17;
    end;
    q= m |-> 0 by A3,A6,FINSEQ_2:def 2;
 hence Sum(p)=Sum(q) by A5,RVSUM_1:111;
 end;
end;
 hence thesis;
  end;
 A11: for n be Nat st P[n] holds P[n+1]
 proof
 let n be Nat such that
A12:P[n];
  now
   let m be Nat, a be Function of [:Seg (n+1),Seg m :],REAL;
   let p,q be FinSequence of REAL such that
        A13: (dom p=Seg (n+1) & for i be Nat st i in dom p
                 holds ex r be FinSequence of REAL st
                     (dom(r) = Seg m & p.i=Sum(r)
                     & for j be Nat st j in dom(r) holds r.j=a.[i,j])) and
        A14: (dom q=Seg m
                 & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st
                     (dom s = Seg (n+1) & q.j=Sum s
                     & for i be Nat st i in dom s holds s.i=a.[i,j] ) );
       set a0=a| [:Seg n, Seg m :];
        n <= n+1 by NAT_1:29; then
        Seg n c= Seg (n+1) by FINSEQ_1:7; then
        [:Seg n,Seg m:] c= [:Seg (n+1),Seg m:] by ZFMISC_1:118; then
    reconsider a0 as Function of [:Seg n,Seg m :],REAL by FUNCT_2:38;
    reconsider p0= p | (Seg n) as FinSequence of REAL by FINSEQ_1:23;
      A15: ( dom p0=Seg n & for i be Nat st i in dom p0
                    holds ex r be FinSequence of REAL st
                        (dom r = Seg m & p0.i=Sum r
                        & for j be Nat st j in dom(r) holds r.j=a0.[i,j]))
      proof
       len p=n+1 by A13,FINSEQ_1:def 3;
       then n <= len p by NAT_1:29;
       hence
       A16: dom p0=Seg n by FINSEQ_1:21;
       now let i be Nat such that
         A17: i in dom p0;
         i in dom p by A13,A16,A17,FINSEQ_2:9;
         then consider r be FinSequence of REAL such that
               A18: dom(r) = Seg m and
               A19: p.i=Sum(r) and
               A20: for j be Nat st j in dom(r) holds r.j=a.[i,j] by A13;
            take r;
            thus dom(r) = Seg m by A18;
            thus p0.i = Sum(r) by A17,A19,FUNCT_1:70;
            thus for j be Nat st j in dom(r) holds r.j=a0.[i,j]
            proof
            let j be Nat such that
               A21: j in dom(r);
               A22: [i,j] in [:Seg n,Seg m:]
               by A16,A17,A18,A21,ZFMISC_1:106;
               n <= n+1 by NAT_1:29; then
               A23: Seg n c= Seg (n+1) by FINSEQ_1:7;
               dom a= [:Seg (n+1),Seg m:] by FUNCT_2:def 1; then
               A24: dom a0 = [:Seg (n+1),Seg m:]
                           /\ [:Seg n,Seg m:] by FUNCT_1:68
                          .= [:Seg n,Seg m:] by A23,ZFMISC_1:124;
               thus r.j=a.[i,j] by A20,A21
                     .=a0.[i,j] by A22,A24,FUNCT_1:70;
            end;
       end;
       hence thesis;
      end;
 deffunc F(Nat)=a.[n+1,$1];
 consider an be FinSequence such that
A25: len an=m &
             for j be Nat st j in Seg(m) holds an.j=F(j) from FINSEQ_1:sch 2;
 an is FinSequence of REAL
 proof
 now let i be Nat;
   assume i in dom an; then
   i in Seg m by A25,FINSEQ_1:def 3; then
   an.i = a.[n+1,i] by A25;
   hence an.i in REAL;
end;
hence thesis by FINSEQ_2:14;
 end;
 then reconsider an as FinSequence of REAL;
   A26:  dom an=Seg m by A25,FINSEQ_1:def 3;
   A27:  Sum(an) = p.(n+1)
   proof
    n+1 in dom p by A13,FINSEQ_1:6;
    then consider r be FinSequence of REAL such that
     A28: dom(r) = Seg m and
     A29: p.(n+1)=Sum(r) and
     A30: for j be Nat st j in dom(r) holds r.j=a.[n+1,j] by A13;
     now let j be Nat;
     assume A31:j in dom r;
     hence r.j=a.[n+1,j] by A30
        .=an.j by A25,A28,A31;
      end;
     hence thesis by A26,A28,A29,FINSEQ_1:17;
    end;
     reconsider q0=q-an as FinSequence of REAL;
     A32: dom q0=Seg m & for j be Nat st j in dom q0
                     holds ex s be FinSequence of REAL st
                         (dom s = Seg n
                         & q0.j=Sum(s)
                         & for i be Nat st i in dom(s) holds s.i=a0.[i,j] )
    proof
A33: dom diffreal = [:REAL,REAL:] by FUNCT_2:def 1;
    rng q c= REAL & rng an c= REAL by RELSET_1:12; then
A34: [:rng q, rng an:] c= dom diffreal by A33,ZFMISC_1:119;
    rng <:q,an:> c= [:rng q, rng an:] by FUNCT_3:71; then
A35: rng <:q,an:> c= dom diffreal by A34,XBOOLE_1:1;
A36: dom (diffreal.:(q,an)) = dom(diffreal*<:q,an:>) by FUNCOP_1:def 3
     .= dom <:q,an:> by A35,RELAT_1:46
     .= dom q /\ dom an by FUNCT_3:def 8;
    thus
      A37: dom q0= dom q /\ dom an by A36,RVSUM_1:def 6
                      .= Seg m  /\ Seg m by A14,A25,FINSEQ_1:def 3
                      .= Seg m;
     now let j be Nat such that
            A38: j in dom q0;
            consider s be FinSequence of REAL such that
        A39: dom(s) = Seg (n+1) and
        A40: q.j=Sum(s) and
        A41: for i be Nat st i in dom(s) holds s.i=a.[i,j] by A14,A37,A38;
            reconsider sn=s|Seg (n) as FinSequence of REAL by FINSEQ_1:23;
            take sn;
            A42:len s = n+1 by A39,FINSEQ_1:def 3; then
            n <= len s by NAT_1:29;
            hence
            A43: dom sn =Seg n by FINSEQ_1:21;
              A44:  sn^<*(s.(n+1))*> = s by A42,FINSEQ_3:61;
                n+1 in Seg (n+1) by FINSEQ_1:6; then
                s.(n+1)=a.[n+1,j] by A39,A41; then
            A45: s.(n+1)=an.j by A25,A37,A38;
            A46: q.j-an.j=Sum(sn) +an.j-an.j by A40,A44,A45,RVSUM_1:104
                           .=Sum(sn);
            thus q0.j = Sum(sn) by A38,A46,RVSUM_1:47;
            thus for i be Nat st i in dom(sn) holds sn.i=a0.[i,j]
           proof
            let i be Nat such that
               A47: i in dom(sn);
               A48: [i,j] in [:Seg n,Seg m:]
               by A37,A38,A43,A47,ZFMISC_1:106;
               n <= n+1 by NAT_1:29; then
               A49: Seg n c= Seg (n+1) by FINSEQ_1:7;
               dom a= [:Seg (n+1),Seg m:] by FUNCT_2:def 1; then
               A50: dom a0 = [:Seg (n+1),Seg m:]
                           /\ [:Seg n,Seg m:] by FUNCT_1:68
                          .= [:Seg n,Seg m:] by A49,ZFMISC_1:124;
                thus sn.i=s.i by A47,FUNCT_1:70
                     .=a.[i,j] by A39,A41,A43,A47,A49
                     .=a0.[i,j] by A48,A50,FUNCT_1:70;
            end;
        end;
       hence thesis;
    end;
    len p=n+1 by A13,FINSEQ_1:def 3; then
A51: p0^<*Sum(an)*> = p by A27,FINSEQ_3:61;
    len q0=m by A32,FINSEQ_1:def 3; then
A52: q0 is Element of m-tuples_on REAL by FINSEQ_2:110;
A53: an is Element of m-tuples_on REAL by A25,FINSEQ_2:110;
   len q=m by A14,FINSEQ_1:def 3; then
A54: q is Element of m-tuples_on REAL by FINSEQ_2:110;
A55:  q0+an=q by A53,A54,RVSUM_1:64;
   thus Sum(p) = Sum(p0)+Sum(an) by A51,RVSUM_1:104
   .=Sum(q0)+Sum(an) by A12,A15,A32
   .=Sum(q) by A52,A53,A55,RVSUM_1:119;
   end;
   hence P[n+1];
  end;
 thus for n be Nat holds P[n] from NAT_1:sch 1(A1,A11);
end;

theorem Th2:
  for F be FinSequence of ExtREAL, f be FinSequence of REAL st
    F=f holds Sum(F)=Sum(f)
proof
   let F be FinSequence of ExtREAL;
   let f be FinSequence of REAL;
   assume
A1: F = f;
   defpred P[Nat] means
   for G be FinSequence of ExtREAL, g be FinSequence of REAL st
    G=F|(Seg $1) & g = f|(Seg $1) & $1 <= len F holds Sum(G) = Sum(g);
A2:P[0]
   proof
     let G be FinSequence of ExtREAL, g be FinSequence of REAL;
     assume
A3: G=F|(Seg 0) & g = f|(Seg 0) & 0 <= len F;
     0 <= len F & 0 <= len f by NAT_1:18; then
     len G = 0 & len g = 0 by A3,FINSEQ_1:21; then
     G = <*>ExtREAL & g=<*>REAL by FINSEQ_1:32;
     hence thesis by CONVFUN1:4,RVSUM_1:102,SUPINF_2:def 1;
   end;
A4:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
A5:  P[k];
     for G be FinSequence of ExtREAL, g be FinSequence of REAL st
      G = F|(Seg (k+1)) & g = f|(Seg (k+1)) & k+1 <= len F holds
       Sum(G) = Sum(g)
     proof
       let G be FinSequence of ExtREAL, g be FinSequence of REAL;
       assume
A6:    G = F|(Seg (k+1)) & g = f|(Seg (k+1)) & k+1 <= len F; then
A7:   len G = k+1 & dom G = Seg (k+1) by FINSEQ_1:21;
       reconsider G1 = G|(Seg k) as FinSequence of ExtREAL by FINSEQ_1:23;
A8:   G = G1^<*G.(k+1)*> by A7,FINSEQ_3:61;
       reconsider G2=<*G.(k+1)*> as FinSequence of ExtREAL;
A9:   len g = k+1 & dom g = Seg (k+1) by A1,A6,FINSEQ_1:21;
       reconsider g1 = g|(Seg k) as FinSequence of REAL by FINSEQ_1:23;
A10:   g = g1^<*g.(k+1)*> by A9,FINSEQ_3:61;
       reconsider g2=<*g.(k+1)*> as FinSequence of REAL;
       k <= k+1 by NAT_1:29; then
       Seg k c= Seg (k+1) by FINSEQ_1:7; then
A11:   G1 = F|(Seg k) & g1 = f|(Seg k) by A6,FUNCT_1:82;
       k <= k+1 by NAT_1:29; then
       k <= len F by A6,AXIOMS:22; then
A12:   Sum G1 = Sum g1 by A5,A11;
       not -infty in rng G1 & not -infty in rng <*G.(k+1)*>
       proof
         now assume A13: -infty in rng G1 or -infty in rng <*G.(k+1)*>;
           rng g1 c= REAL & rng g2 c= REAL by FINSEQ_1:def 4;
           hence contradiction by A1,A6,A13,SUPINF_1:6;
         end;
         hence thesis;
       end; then
A14:  Sum G = Sum G1 + Sum (<*G.(k+1)*>) by A8,CONVFUN1:7
            .= Sum G1 + G.(k+1) by CONVFUN1:5;
       Sum g = Sum g1 + g.(k+1) by A10,RVSUM_1:104;
       hence Sum G = Sum g by A1,A6,A12,A14,SUPINF_2:1;
     end;
     hence thesis;
   end;
A15:for k be Nat holds P[k] from NAT_1:sch 1(A2,A4);
   F|(Seg len F) = F|(len F) & f|(len f) = f|(Seg len f)
     by FINSEQ_1:def 15; then
   F|(Seg len F) = F & f|(Seg len f) = f by FINSEQ_1:79;
   hence thesis by A1,A15;
end;

theorem Th3:
for X be non empty set, S be sigma_Field_Subset of X,
 f be PartFunc of X,ExtREAL st f is_simple_func_in S holds
  ex F be Finite_Sep_Sequence of S,
     a be FinSequence of ExtREAL st
        dom f = union rng F & dom F= dom a &
        (for n be Nat st n in dom F
          for x be set st x in F.n holds f.x = a.n) &
        for x be set st x in dom f holds
          ex ax be FinSequence of ExtREAL st
            dom ax = dom a &
            (for n be Nat st n in dom ax holds ax.n=a.n*(chi(F.n,X)).x)
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   assume
A1: f is_simple_func_in S;
   consider F be Finite_Sep_Sequence of S such that
A2: dom f = union rng F and
A3: for n being Nat,x,y being Element of X st
     n in dom F & x in F.n & y in F.n holds f.x = f.y by A1,MESFUNC2:def 5;
   defpred P[Nat,Element of ExtREAL] means
   for x be set st x in F.$1 holds $2 = f.x;
A4: for k be Nat st k in Seg len F
    ex y be Element of ExtREAL st P[k,y]
   proof
     let k be Nat;
     assume k in Seg len F; then
A5: k in dom F by FINSEQ_1:def 3; then
A6: F.k in rng F by FUNCT_1:12;
     now per cases;
       case A7:F.k = {};
         take y = 0.;
         for x be set st x in F.k holds y = f.x by A7;
         hence thesis;
       end;
       case F.k <> {}; then
         consider x1 be set such that
A8:      x1 in F.k by XBOOLE_0:def 1;
         take y = f.x1;
         for x be set st x in F.k holds y = f.x
         proof
           let x be set;
           assume A9:x in F.k;
           rng F c= S & S c= bool X by FINSEQ_1:def 4; then
A10:       rng F c= bool X by XBOOLE_1:1; then
           reconsider x1 as Element of X by A6,A8;
           reconsider x as Element of X by A6,A9,A10;
           f.x = f.x1 by A3,A5,A8,A9;
           hence thesis;
         end;
         hence thesis;
       end;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
A11: dom a = Seg len F &
    for k be Nat st k in Seg len F holds P[k,a.k] from FINSEQ_1:sch 5(A4);
A12:for n be Nat st n in dom F
    for x be set st x in F.n holds a.n = f.x
   proof
     let n be Nat;
     assume n in dom F; then
     n in Seg len F by FINSEQ_1:def 3;
     hence thesis by A11;
   end;
A13:for x be set st x in dom f holds ex ax be FinSequence of ExtREAL st
     dom ax = dom a &
     (for n be Nat st n in dom ax holds ax.n = a.n*(chi(F.n,X)).x)
   proof
     let x be set;
     assume x in dom f;
     deffunc Q(Nat) = a.$1 * (chi(F.$1,X)).x;
     consider ax be FinSequence of ExtREAL such that
A14:   len ax = len F &
      for k be Nat st k in Seg len F holds ax.k=Q(k) from FINSEQ_2:sch 1;
     take ax;
     for n be Nat st n in dom ax holds ax.n = a.n*(chi(F.n,X)).x
     proof
       let n be Nat;
       assume n in dom ax; then
       n in Seg len ax by FINSEQ_1:def 3;
       hence thesis by A14;
     end;
     hence thesis by A11,A14,FINSEQ_1:def 3;
   end;
   take F,a;
   thus thesis by A2,A11,A12,A13,FINSEQ_1:def 3;
end;

theorem Th4:
for X be set, F be FinSequence of X holds
   F is disjoint_valued iff
   (for i,j be Nat st i in dom F & j in dom F & i <> j holds F.i misses F.j)
proof
   let X be set;
   let F be FinSequence of X;
   now assume
A1:  for i,j be Nat st i in dom F & j in dom F & i <> j holds F.i misses F.j;
     thus F is disjoint_valued
     proof
       for x,y be set st x <> y holds F.x misses F.y
       proof
         let x,y be set;
         assume
A2:      x <> y;
         per cases;
           suppose A3:x in dom F & y in dom F;
             dom F c= NAT by RELSET_1:12; then
             reconsider x,y as Nat by A3;
             F.x misses F.y by A1,A2,A3;
             hence thesis;
           end;
           suppose not x in dom F; then
             F.x = {} by FUNCT_1:def 4;
             hence thesis by XBOOLE_1:65;
           end;
           suppose not y in dom F; then
             F.y = {} by FUNCT_1:def 4;
             hence thesis by XBOOLE_1:65;
           end;
       end;
       hence thesis by PROB_2:def 3;
     end;
   end;
   hence thesis by PROB_2:def 3;
end;

theorem Th5:
for X be non empty set, A be set, S be sigma_Field_Subset of X,
    F be Finite_Sep_Sequence of S, G be FinSequence of S st
      dom G = dom F & for i be Nat st i in dom G holds
        G.i = A /\ F.i holds G is Finite_Sep_Sequence of S
proof
  let X be non empty set,
      A be set,
      S be sigma_Field_Subset of X,
      F be Finite_Sep_Sequence of S,
      G be FinSequence of S such that
 A1: dom G = dom F and
 A2: for i be Nat st i in dom G holds G.i= A /\ F.i;
       now let i,j be Nat;
       assume A3: i in dom G & j in dom G & i <> j;
  A4: F.i misses F.j by A1,A3,Th4;
                   (A /\ F.i) /\ (A /\ F.j)
                  =A /\ (F.i /\ (A /\ F.j)) by XBOOLE_1:16
                 .=A /\ ((F.i /\ F.j) /\ A) by XBOOLE_1:16
                 .=A /\ ({} /\ A) by A4,XBOOLE_0:def 7
                 .={}; then
                 A /\ F.i misses A /\ F.j by XBOOLE_0:def 7; then
                 G.i misses A /\ F.j by A2,A3;
                hence G.i misses G.j by A2,A3;
        end;
        hence thesis by Th4;
     end;

theorem Th6:
for X be non empty set, A be set, F,G be FinSequence of X st
  dom G = dom F & (for i be Nat st i in dom G holds G.i = A /\ F.i) holds
   union rng G = A /\ union rng F
proof
   let X be non empty set;
   let A be set;
   let F,G be FinSequence of X;
   assume that
A1:dom G = dom F and
A2:for i be Nat st i in dom G holds G.i = A /\ F.i;
   thus union rng G c= A /\ union rng F
   proof
      let r be set;
      assume r in union rng G; then
      consider E being set such that
A3:   r in E & E in rng G by TARSKI:def 4;
      consider s being set such that
A4:   s in dom G & E = G.s by A3,FUNCT_1:def 5;
      dom G c= NAT by RELSET_1:12; then
      reconsider s as Element of NAT by A4;
      r in A /\ F.s by A2,A3,A4; then
A5:   r in A & r in F.s by XBOOLE_0:def 3;
      F.s in rng F by A1,A4,FUNCT_1:12; then
      r in union rng F by A5,TARSKI:def 4;
      hence thesis by A5,XBOOLE_0:def 3;
   end;
   let r be set;
   assume r in A /\ union rng F; then
A6:r in A & r in union rng F by XBOOLE_0:def 3; then
   consider E being set such that
A7: r in E & E in rng F by TARSKI:def 4;
   consider s being set such that
A8: s in dom F & E = F.s by A7,FUNCT_1:def 5;
   dom F c= NAT by RELSET_1:12; then
   reconsider s as Element of NAT by A8;
   A /\ E = G.s by A1,A2,A8; then
A9:r in G.s by A6,A7,XBOOLE_0:def 3;
   G.s in rng G by A1,A8,FUNCT_1:12;
   hence thesis by A9,TARSKI:def 4;
end;

theorem Th7:
for X be set, F be FinSequence of X, i be Nat st i in dom F holds
  F.i c= union rng F & F.i /\ union rng F = F.i
proof
   let X be set, F be FinSequence of X, i be Nat;
   assume
A1: i in dom F;
   F.i in rng F by A1,FUNCT_1:12;
   hence F.i c= union rng F by ZFMISC_1:92;
   hence F.i /\ union rng F = F.i by XBOOLE_1:28;
 end;

theorem Th8:
for X be non empty set, S be sigma_Field_Subset of X,
  M be sigma_Measure of S, F be Finite_Sep_Sequence of S holds
    dom F = dom (M*F)
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let M be sigma_Measure of S;
   let F be Finite_Sep_Sequence of S;
   dom M = S by FUNCT_2:def 1; then
   rng F c= dom M by RELSET_1:12;
   hence thesis by RELAT_1:46;
end;

theorem Th9:
for X be non empty set, S be sigma_Field_Subset of X,
  M be sigma_Measure of S, F be Finite_Sep_Sequence of S holds
    M.(union rng F) = Sum(M*F)
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let M be sigma_Measure of S;
   let F be Finite_Sep_Sequence of S;
   reconsider F1 = M*F as FinSequence of ExtREAL;
   consider f be Function of NAT,ExtREAL such that
A1: Sum(F1) = f.(len F1) &
    f.0 = 0. &
    for i be Nat st i < len F1 holds f.(i+1) = f.i + F1.(i+1)
      by CONVFUN1:def 5;
   consider G being Sep_Sequence of S such that
A2: (union rng F = union rng G &
    (for n st n in dom F holds F.n = G.n) &
    (for m st not m in dom F holds G.m = {})) by MESFUNC2:33;
A3:M.(union rng F) = SUM(M*G) by A2,MEASURE1:def 11
                  .= sup(rng Ser(M*G)) by SUPINF_2:def 23;
   reconsider G1 = M*G as Num of rng(M*G) by SUPINF_2:def 16;
A4:   Ser(M*G) = Ser(rng (M*G),G1) by SUPINF_2:def 21; then
A5:Ser(M*G).0 = G1.0 &
   for n be Nat, y be R_eal st y = Ser(M*G).n holds
    Ser(M*G).(n+1) = y + G1.(n+1) by SUPINF_2:def 17;
   not 0 in Seg len F by FINSEQ_1:3; then
   not 0 in dom F by FINSEQ_1:def 3; then
A6:G.0 = {} by A2;
A7:dom G = NAT by FUNCT_2:def 1; then
A8:Ser(M*G).0 = M.(G.0) by A5,FUNCT_1:23 .= 0. by A6,MEASURE1:def 11;
A9:for i be Nat st i < len F1 holds Ser(M*G).(i+1) = Ser(M*G).i + F1.(i+1)
   proof
     let i be Nat;
     assume i < len F1; then
     1 <= i+1 & i+1 <= len F1 by NAT_1:29,38; then
     i+1 in dom F1 by FINSEQ_3:27; then
A10:  i+1 in dom F by Th8;
     Ser(M*G).(i+1) = Ser(M*G).i + G1.(i+1) by A4,SUPINF_2:def 17
     .= Ser(M*G).i + M.(G.(i+1)) by A7,FUNCT_1:23
     .= Ser(M*G).i + M.(F.(i+1)) by A2,A10;
     hence thesis by A10,FUNCT_1:23;
   end;
   defpred Q[Nat] means
    $1 <= len F1 implies Ser(M*G).$1 = f.$1;
A11:Q[0] by A1,A8;
A12:for k be Nat st Q[k] holds Q[k+1]
   proof
     let k be Nat;
     assume
A13:  Q[k];
     assume k+1 <= len F1; then
A14: k < len F1 by NAT_1:38; then
     Ser(M*G).(k+1) = f.k + F1.(k+1) by A9,A13;
     hence thesis by A1,A14;
   end;
A15:for k be Nat holds Q[k] from NAT_1:sch 1(A11,A12);
   defpred P[Nat] means
    $1 >= len F1 implies Ser(M*G).$1 = Ser(M*G).(len F1);
A16:P[0]
   proof
     assume
     0 >= len F1; then
     0 = len F1 by NAT_1:18;
     hence thesis;
   end;
A17:M*G is nonnegative by MEASURE2:1;
A18:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
A19:  P[k];
     assume
A20:  k+1 >= len F1;
     now per cases;
       case
A21:    k >= len F1; then
A22:    Ser(M*G).(k+1) = Ser(M*G).(len F1) + G1.(k+1)
           by A4,A19,SUPINF_2:def 17;
        k+1 > len F1 by A21,NAT_1:38; then
        not k+1 in Seg len F1 by FINSEQ_1:3; then
        not k+1 in dom F1 by FINSEQ_1:def 3; then
A23:    not k+1 in dom F by Th8;
        G1.(k+1) = M.(G.(k+1)) by A7,FUNCT_1:23
         .= M.{} by A2,A23 .= 0. by MEASURE1:def 11;
        hence thesis by A22,SUPINF_2:18;
       end;
       case k < len F1; then
        k+1 <= len F1 by NAT_1:38;
        hence thesis by A20,AXIOMS:21;
       end;
     end;
     hence thesis;
   end;
A24:for k be Nat holds P[k] from NAT_1:sch 1(A16,A18);
   defpred P1[Nat] means
   $1 < len F1 implies Ser(M*G).(len F1 - $1) <=' Ser(M*G).(len F1);
A25:P1[0];
A26:for k be Nat st P1[k] holds P1[k+1]
   proof
     let k be Nat;
     assume
A27:   P1[k];
     assume
A28:   k+1 < len F1; then
     consider k' be Nat such that
A29:   len F1 = k+1 + k' by NAT_1:28;
A30:  k <= k+1 by NAT_1:29;
     Ser(M*G).(k') <=' Ser(M*G).(k' + 1) by A17,SUPINF_2:59;
     hence thesis by A27,A28,A29,A30,AXIOMS:22,SUPINF_1:29;
   end;
A31:for i be Nat holds P1[i] from NAT_1:sch 1(A25,A26);
A32:for i be Nat st i < len F1 holds Ser(M*G).i <=' Ser(M*G).(len F1)
   proof
     let i be Nat;
     assume i < len F1; then
     consider k be Nat such that
A33:  len F1 = i + k by NAT_1:28;
A34: k = len F1 - i & i = len F1 - k by A33;
     len F1 <= len F1 + i by NAT_1:29; then
A35: k <= len F1 by A34,REAL_1:86;
     Ser(M*G).(len F1 - k) <=' Ser(M*G).(len F1)
     proof
       now per cases by A35,REAL_1:def 5;
         case k = len F1;
           hence thesis by A8,A17,SUPINF_2:59;
         end;
         case k < len F1;
           hence thesis by A31;
         end;
       end;
       hence thesis;
     end;
     hence thesis by A33;
   end;
   for z be R_eal holds z in rng Ser(M*G) implies z <=' Ser(M*G).(len F1)
   proof
     let z be R_eal;
     assume z in rng Ser(M*G); then
     consider n be set such that
A36:   n in dom Ser(M*G) & z = Ser(M*G).n by FUNCT_1:def 5;
     reconsider n as Nat by A36,FUNCT_2:def 1;
     now per cases;
       case n < len F1;
         hence thesis by A32,A36;
       end;
       case n >= len F1;
         hence thesis by A24,A36;
       end;
     end;
     hence thesis;
   end; then
A37:Ser(M*G).(len F1) is majorant of rng Ser(M*G) by SUPINF_1:def 9;
   Ser(M*G).(len F1) in rng Ser(M*G)
   proof
     dom (Ser(M*G)) = NAT by FUNCT_2:def 1;
     hence thesis by FUNCT_1:12;
   end; then
   Ser(M*G).(len F1) = sup(rng Ser(M*G)) by A37,SUPINF_1:80;
   hence thesis by A1,A3,A15;
end;

theorem Th10:
for F,G be FinSequence of ExtREAL, a be R_eal st
  ((a <> +infty & a <> -infty) or
   (for i be Nat st i in dom F holds F.i <' 0.) or
   (for i be Nat st i in dom F holds 0. <' F.i)) &
  dom F = dom G &
  for i be Nat st i in dom G holds G.i=a*F.i holds
    Sum(G)=a*Sum(F)
proof
   let F,G be FinSequence of ExtREAL;
   let a be R_eal;
   assume that
A1:(a <> +infty & a <> -infty) or
    (for i be Nat st i in dom F holds F.i <' 0.) or
    (for i be Nat st i in dom F holds 0. <' F.i) and
A2:dom F = dom G and
A3:for i be Nat st i in dom G holds G.i=a*F.i;
   consider f be Function of NAT,ExtREAL such that
A4: Sum(F) = f.(len F) & f.0 = 0. &
    for i being Nat st i < len F holds f.(i+1)=f.i+F.(i+1) by CONVFUN1:def 5;
   consider g be Function of NAT,ExtREAL such that
A5: Sum(G) = g.(len G) & g.0 = 0. &
    for i being Nat st i < len G holds g.(i+1)=g.i+G.(i+1) by CONVFUN1:def 5;
   defpred P[Nat] means
    $1 <= len G implies g.$1 = a*f.$1;
A6:  P[0] by A4,A5,EXTREAL1:def 1;
A7:  for i be Nat st P[i] holds P[i+1]
     proof
       let i be Nat;
       assume
A8:   P[i];
       assume A9:i+1 <= len G; then
A10:   i < len G by NAT_1:38;
       1 <= i+1 by NAT_1:29; then
A11:   i+1 in dom G by A9,FINSEQ_3:27; then
       i+1 in Seg len F by A2,FINSEQ_1:def 3; then
       i+1 <= len F by FINSEQ_1:3; then
A12:   i < len F by NAT_1:38;
A13:   g.(i+1) = g.i + G.(i+1) by A5,A10 .= a*f.i + a*F.(i+1)
         by A3,A8,A9,A11,NAT_1:38;
       now per cases by A1;
        case a <> +infty & a <> -infty; then
         g.(i+1) = a*(f.i + F.(i+1)) by A13,EXTREAL1:29;
         hence thesis by A4,A12;
        end;
        case
A14:      for i be Nat st i in dom F holds F.i <' 0.;
         defpred P1[Nat] means $1 < len F implies f.$1 <=' 0.;
A15:      P1[0] by A4;
A16:      for i be Nat st P1[i] holds P1[i+1]
         proof
           let i be Nat;
           assume
A17:       P1[i];
           assume
A18:       i+1 < len F; then
           i < len F by NAT_1:38; then
A19:       f.(i+1) = f.i + F.(i+1) by A4;
           1 <= i+1 & i+1 <= len F by A18,NAT_1:37; then
           i+1 in dom F by FINSEQ_3:27; then
           F.(i+1) <' 0. by A14;
           hence thesis by A17,A18,A19,EXTREAL2:10,NAT_1:38;
         end;
         for i be Nat holds P1[i] from NAT_1:sch 1(A15,A16); then
         f.i <=' 0. & F.(i+1) <' 0. by A2,A11,A12,A14; then
         (f.i <' 0. or f.i = 0.) & F.(i+1) <' 0. by SUPINF_1:22; then
         g.(i+1) = a*(f.i + F.(i+1)) by A13,EXTREAL1:29;
         hence thesis by A4,A12;
        end;
        case
A20:      for i be Nat st i in dom F holds 0. <' F.i;
         defpred P2[Nat] means
          $1 < len F implies 0. <=' f.$1;
A21:      P2[0] by A4;
A22:      for i be Nat st P2[i] holds P2[i+1]
         proof
           let i be Nat;
           assume
A23:        P2[i];
           assume
A24:        i+1 < len F; then
            i < len F by NAT_1:38; then
A25:       f.(i+1) = f.i + F.(i+1) by A4;
           1 <= i+1 & i+1 <= len F by A24,NAT_1:37; then
           i+1 in dom F by FINSEQ_3:27; then
           0. <' F.(i+1) by A20;
           hence thesis by A23,A24,A25,EXTREAL2:8,NAT_1:38;
         end;
         for i be Nat holds P2[i] from NAT_1:sch 1(A21,A22); then
         0. <=' f.i & 0. <' F.(i+1) by A2,A11,A12,A20; then
         (0. <' f.i or f.i = 0.) & 0. <' F.(i+1) by SUPINF_1:22; then
         g.(i+1) = a*(f.i + F.(i+1)) by A13,EXTREAL1:29;
         hence thesis by A4,A12;
        end;
       end;
       hence thesis;
     end;
A26:  for i be Nat holds P[i] from NAT_1:sch 1(A6,A7);
     Seg len F = dom G by A2,FINSEQ_1:def 3
       .= Seg len G by FINSEQ_1:def 3; then
     a*Sum(F) = a*f.(len G) by A4,FINSEQ_1:8 .= g.(len G) by A26;
     hence thesis by A5;
end;

theorem Th11:
for F be FinSequence of REAL holds F is FinSequence of ExtREAL
 proof
   let F be FinSequence of REAL;
   rng F c= REAL & REAL c= ExtREAL by FINSEQ_1:def 4; then
   rng F c= ExtREAL by XBOOLE_1:1;
   hence thesis by FINSEQ_1:def 4;
 end;

definition
  let X be non empty set;
  let S be sigma_Field_Subset of X;
  let f be PartFunc of X,ExtREAL;
  let F be Finite_Sep_Sequence of S;
  let a be FinSequence of ExtREAL;
  pred F,a are_Re-presentation_of f means :Def1:
    dom f = union rng F & dom F=dom a &
    for n be Nat st n in dom F
      for x be set st x in F.n holds f.x=a.n;
end;

theorem
  for X be non empty set,
      S be sigma_Field_Subset of X,
      f be PartFunc of X,ExtREAL st
    f is_simple_func_in S holds
      ex F be Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL st
       F,a are_Re-presentation_of f
proof
let X be non empty set;
let S be sigma_Field_Subset of X;
let f be PartFunc of X,ExtREAL such that
A1: f is_simple_func_in S;
consider F being Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL such that
     A2:  dom f = union rng F & dom F= dom a
          & (for n be Nat st n in dom F
              for x be set st x in F.n holds f.x=a.n )
          & for x be set st x in dom f
                 holds ex ax be FinSequence of ExtREAL
                           st dom ax= dom a &
                                (for n be Nat st n in dom ax
                                   holds ax.n=a.n*(chi(F.n,X)).x) by A1,Th3;
   take F,a;
   thus thesis by A2,Def1;
end;

theorem Th13:
for X be non empty set, S be sigma_Field_Subset of X,
 F be Finite_Sep_Sequence of S holds
  ex G be Finite_Sep_Sequence of S st
    (union rng F = union rng G &
     (for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)))
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let F be Finite_Sep_Sequence of S;
   defpred P[Nat] means
   for F be Finite_Sep_Sequence of S st len F = $1 holds
    ex G be Finite_Sep_Sequence of S st
     (union rng F = union rng G &
     (for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)));
A1:P[0]
   proof
     let F be Finite_Sep_Sequence of S;
     assume
A2:   len F = 0;
     take G = F;
     for n be Nat st n in dom G holds
      (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)
     proof
       let n be Nat;
       assume A3:n in dom G;
       G = {} by A2,FINSEQ_1:25;
       hence thesis by A3,FINSEQ_1:26;
     end;
     hence thesis;
   end;
A4:for n be Nat st P[n] holds P[n+1]
   proof
     let n be Nat;
     assume A5:P[n];
     let F be Finite_Sep_Sequence of S;
     assume
A6:  len F = n+1;
     reconsider F1 = F|(Seg n) as Finite_Sep_Sequence of S by MESFUNC2:36;
A7:     n <= len F by A6,NAT_1:29; then
A8:  len F1 = n by FINSEQ_1:21; then
     consider G1 be Finite_Sep_Sequence of S such that
A9:   (union rng F1 = union rng G1 &
       (for n be Nat st n in dom G1 holds
        (G1.n <> {} & ex m be Nat st m in dom F1 & F1.m = G1.n))) by A5;
A10: dom F = dom (F1^<*F.(n+1)*>) &
     for k be Nat st k in dom F holds F.k = (F1^<*F.(n+1)*>).k
     proof
A11:    dom (F1^<*F.(n+1)*>) = Seg (len F1 + len <*F.(n+1)*>) by FINSEQ_1:def 7
        .= Seg (n + 1) by A8,FINSEQ_1:56
        .= dom F by A6,FINSEQ_1:def 3;
       for k be Nat st k in dom F holds F.k = (F1^<*F.(n+1)*>).k
       proof
         let k be Nat;
         assume
A12:       k in dom F;
         now per cases;
           case
A13:          k in dom F1; then
A14:          (F1^<*F.(n+1)*>).k = F1.k by FINSEQ_1:def 7;
             k in Seg n by A7,A13,FINSEQ_1:21; then
             k <= n by FINSEQ_1:3; then
             (F|n).k = F.k by JORDAN3:20;
             hence thesis by A14,FINSEQ_1:def 15;
           end;
           case not k in dom F1; then
             not (k in Seg n) by A7,FINSEQ_1:21; then
A15:          not (1 <= k & k <= n) by FINSEQ_1:3;
             k in Seg (n+1) by A6,A12,FINSEQ_1:def 3; then
A16:          not (k < n + 1) & k <= n+1 by A15,FINSEQ_1:3,NAT_1:38;
             dom <*F.(n+1)*> = Seg 1 by FINSEQ_1:def 8; then
A17:          1 in dom <*F.(n+1)*> by FINSEQ_1:4,TARSKI:def 1;
             (F1^<*F.(n+1)*>).k = (F1^<*F.(n+1)*>).(len F1 + 1)
               by A8,A16,REAL_1:def 5
             .= <*F.(n+1)*>.1 by A17,FINSEQ_1:def 7
             .= F.(n+1) by FINSEQ_1:def 8;
             hence thesis by A16,REAL_1:def 5;
           end;
         end;
         hence thesis;
       end;
       hence thesis by A11;
     end; then
A18:  F = F1^<*F.(n+1)*> by FINSEQ_1:17;
     ex G be Finite_Sep_Sequence of S st
      (union rng F = union rng G &
       (for k be Nat st k in dom G holds
        (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)))
     proof
       now per cases;
         case A19:F.(n+1) = {};
A20:        rng <*F.(n+1)*> = {{}} by A19,FINSEQ_1:55;
           take G = G1;
           union rng F = union rng G &
           (for k be Nat st k in dom G holds
            (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k))
           proof
             rng F = rng F1 \/ rng <*F.(n+1)*> by A18,FINSEQ_1:44;
             hence union rng F
              = union rng F1 \/ union rng <*F.(n+1)*> by ZFMISC_1:96
             .= union rng F1 \/ {} by A20,ZFMISC_1:31
             .= union rng G by A9;
             for k be Nat st k in dom G holds
              (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)
             proof
               let k be Nat;
               assume
A21:             k in dom G; then
               consider m be Nat such that
A22:             m in dom F1 & F1.m = G.k by A9;
               dom F1 c= dom F & F1.m = F.m by A18,A22,FINSEQ_1:39,def 7;
               hence thesis by A9,A21,A22;
             end;
             hence thesis;
           end;
           hence thesis;
         end;
         case
A23:        F.(n+1) <> {};
           1 <= n+1 by NAT_1:29; then
           n+1 in Seg (n+1) by FINSEQ_1:3; then
           n+1 in dom F by A6,FINSEQ_1:def 3; then
           F.(n+1) in rng F & rng F c= S by FINSEQ_1:def 4,FUNCT_1:12; then
A24:           <*F.(n+1)*> is FinSequence of S by SCMFSA_7:22; then
A25:        G1^<*F.(n+1)*> is FinSequence of S by SCMFSA_7:23;
           for k,m be set st k <> m holds
            (G1^<*F.(n+1)*>).k misses (G1^<*F.(n+1)*>).m
           proof
             let k,m be set;
             assume
A26:           k <> m;
             now per cases;
               case
A27:              k in dom (G1^<*F.(n+1)*>) & m in dom (G1^<*F.(n+1)*>);
                 dom (G1^<*F.(n+1)*>) c= NAT by A25,RELSET_1:12; then
                 reconsider k,m as Nat by A27;
                 now per cases;
                 case k = len (G1^<*F.(n+1)*>); then
                   k = len G1 + len <*F.(n+1)*> by FINSEQ_1:35; then
A28:                k = len G1 + 1 by FINSEQ_1:56;
                   1 <= len <*F.(n+1)*> by FINSEQ_1:56; then
A29:                (G1^<*F.(n+1)*>).k = <*F.(n+1)*>.1 by A28,SCMFSA_7:10
                    .= F.(n+1) by FINSEQ_1:def 8;
                   m in Seg (len (G1^<*F.(n+1)*>)) by A27,FINSEQ_1:def 3; then
                   m in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35; then
                   m in Seg (len G1 + 1) by FINSEQ_1:56; then
                   1 <= m & m <= len G1 + 1 by FINSEQ_1:3; then
                   1 <= m & m < len G1 + 1 by A26,A28,REAL_1:def 5; then
                   1 <= m & m <= len G1 by NAT_1:38; then
A30:                m in dom G1 by FINSEQ_3:27; then
A31:               (G1^<*F.(n+1)*>).m = G1.m by FINSEQ_1:def 7;
                   consider m1 be Nat such that
A32:                m1 in dom F1 & F1.m1 = G1.m by A9,A30;
                   dom F1 c= dom F by A10,FINSEQ_1:39; then
A33:               m1 in dom F &
                   (G1^<*F.(n+1)*>).m = F.m1 by A18,A31,A32,FINSEQ_1:def 7;
                   m1 in Seg len F1 by A32,FINSEQ_1:def 3; then
                   m1 <= n by A8,FINSEQ_1:3; then
                   m1 <> n+1 by NAT_1:38;
                   hence thesis by A29,A33,PROB_2:def 3;
                   end;
                   case k <> len (G1^<*F.(n+1)*>); then
                     k <> len G1 + len <*F.(n+1)*> by FINSEQ_1:35; then
A34:                 k <> len G1 + 1 by FINSEQ_1:56;
                   k in Seg (len (G1^<*F.(n+1)*>)) by A27,FINSEQ_1:def 3; then
                   k in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35; then
                   k in Seg (len G1 + 1) by FINSEQ_1:56; then
                   1 <= k & k <= len G1 + 1 by FINSEQ_1:3; then
                   1 <= k & k < len G1 + 1 by A34,REAL_1:def 5; then
                   1 <= k & k <= len G1 by NAT_1:38; then
A35:                k in dom G1 by FINSEQ_3:27; then
A36:               (G1^<*F.(n+1)*>).k = G1.k by FINSEQ_1:def 7;
                     now per cases;
                       case m = len (G1^<*F.(n+1)*>); then
                   m = len G1 + len <*F.(n+1)*> by FINSEQ_1:35; then
A37:                m = len G1 + 1 by FINSEQ_1:56;
                   1 <= len <*F.(n+1)*> by FINSEQ_1:56; then
A38:                (G1^<*F.(n+1)*>).m = <*F.(n+1)*>.1 by A37,SCMFSA_7:10
                    .= F.(n+1) by FINSEQ_1:def 8;
                   consider k1 be Nat such that
A39:                k1 in dom F1 & F1.k1 = G1.k by A9,A35;
                   dom F1 c= dom F by A10,FINSEQ_1:39; then
A40:               k1 in dom F &
                   (G1^<*F.(n+1)*>).k = F.k1 by A18,A36,A39,FINSEQ_1:def 7;
                   k1 in Seg len F1 by A39,FINSEQ_1:def 3; then
                   k1 <= n by A8,FINSEQ_1:3; then
                   k1 <> n+1 by NAT_1:38;
                   hence thesis by A38,A40,PROB_2:def 3;
                       end;
                       case m <> len (G1^<*F.(n+1)*>); then
                        m <> len G1 + len <*F.(n+1)*> by FINSEQ_1:35; then
A41:                     m <> len G1 + 1 by FINSEQ_1:56;
                   m in Seg (len (G1^<*F.(n+1)*>)) by A27,FINSEQ_1:def 3; then
                   m in Seg (len G1 + len <*F.(n+1)*>) by FINSEQ_1:35; then
                   m in Seg (len G1 + 1) by FINSEQ_1:56; then
                   1 <= m & m <= len G1 + 1 by FINSEQ_1:3; then
                   1 <= m & m < len G1 + 1 by A41,REAL_1:def 5; then
                   1 <= m & m <= len G1 by NAT_1:38; then
                   m in dom G1 by FINSEQ_3:27; then
               (G1^<*F.(n+1)*>).m = G1.m by FINSEQ_1:def 7;
                   hence thesis by A26,A36,PROB_2:def 3;
                       end;
                     end;
                     hence thesis;
                   end;
                 end;
                 hence thesis;
               end;
               case not(k in dom (G1^<*F.(n+1)*>) &
                        m in dom (G1^<*F.(n+1)*>)); then
                 (G1^<*F.(n+1)*>).k = {} or (G1^<*F.(n+1)*>).m = {}
                   by FUNCT_1:def 4;
                 hence thesis by XBOOLE_1:65;
               end;
             end;
             hence thesis;
           end; then
           reconsider G = G1^<*F.(n+1)*> as Finite_Sep_Sequence of S
            by A24,PROB_2:def 3,SCMFSA_7:23;
           take G;
           union rng F = union rng G &
           for n be Nat st n in dom G holds
            (G.n <> {} & ex m be Nat st m in dom F & F.m = G.n)
           proof
             rng F = rng F1 \/ rng <*F.(n+1)*> by A18,FINSEQ_1:44;
             hence union rng F
              = union rng F1 \/ union rng <*F.(n+1)*> by ZFMISC_1:96
             .= union (rng G1 \/ rng <*F.(n+1)*>) by A9,ZFMISC_1:96
             .= union rng G by FINSEQ_1:44;
A42:          len G = len G1 + len <*F.(n+1)*> by FINSEQ_1:35
              .= len G1 + 1 by FINSEQ_1:56;
             for k be Nat st k in dom G holds
             (G.k <> {} & ex m be Nat st m in dom F & F.m = G.k)
             proof
               let k be Nat;
               assume
A43:             k in dom G;
               now per cases;
                 case
A44:                k = len G;
                   dom <*F.(n+1)*> = Seg 1 by FINSEQ_1:55; then
                   1 in dom <*F.(n+1)*> by FINSEQ_1:4,TARSKI:def 1; then
A45:                G.k = <*F.(n+1)*>.1 by A42,A44,FINSEQ_1:def 7
                      .= F.(n+1) by FINSEQ_1:def 8;
                   1 <= n+1 by NAT_1:29; then
                   n+1 in Seg (n+1) by FINSEQ_1:3; then
                   n+1 in dom F by A6,FINSEQ_1:def 3;
                   hence thesis by A23,A45;
                 end;
                 case
A46:                k <> len G;
                   1 <= k & k <= len G by A43,FINSEQ_3:27; then
                   1 <= k & k < len G by A46,REAL_1:def 5; then
                   1 <= k & k <= len G1 by A42,NAT_1:38; then
A47:                k in dom G1 by FINSEQ_3:27; then
A48:                G.k = G1.k by FINSEQ_1:def 7;
                   ex m be Nat st m in dom F & F.m = G.k
                   proof
                     consider m be Nat such that
A49:                  m in dom F1 & F1.m = G1.k by A9,A47;
A50:                 dom F1 c= dom F by A10,FINSEQ_1:39;
                     take m;
                     thus thesis by A18,A48,A49,A50,FINSEQ_1:def 7;
                   end;
                   hence thesis by A9,A47,A48;
                 end;
               end;
               hence thesis;
             end;
             hence thesis;
           end;
           hence thesis;
         end;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
A51:len F = len F;
   for n be Nat holds P[n] from NAT_1:sch 1(A1,A4);
   hence thesis by A51;
end;

Lm1:
for a be FinSequence of ExtREAL, p,N be Element of ExtREAL st
 N = len a &
 (for n be Nat st n in dom a holds a.n = p) holds
   Sum(a) = N * p
proof
   let a be FinSequence of ExtREAL;
   let p,N be Element of ExtREAL;
   assume that
A1: N = len a and
A2: for n be Nat st n in dom a holds a.n = p;
   defpred P[Nat] means
   for F be FinSequence of ExtREAL, p be Element of ExtREAL st
    $1 = len F &
    (for n be Nat st n in dom F holds F.n = p) holds
      ex N be Element of ExtREAL st N = $1 & Sum(F) = N * p;
A3:P[0]
   proof
     for F be FinSequence of ExtREAL, p be Element of ExtREAL st
      0 = len F &
      (for n be Nat st n in dom F holds F.n = p) holds
        ex N be Element of ExtREAL st N = 0 & Sum(F) = N * p
     proof
       let F be FinSequence of ExtREAL;
       let p be Element of ExtREAL;
       assume that
A4:     0 = len F and
        for n be Nat st n in dom F holds F.n = p;
       take N = 0.;
       consider f be Function of NAT,ExtREAL such that
A5:     Sum(F) = f.(len F) & f.0 = 0. &
        for i be Nat st i < len F holds f.(i+1)=f.i+F.(i+1) by CONVFUN1:def 5;
       thus thesis by A4,A5,EXTREAL1:def 1,SUPINF_2:def 1;
     end;
     hence thesis;
   end;
A6:for k be Nat st P[k] holds P[k+1]
   proof
     let k be Nat;
     assume
A7:   P[k];
     for F be FinSequence of ExtREAL, p be Element of ExtREAL st
      k+1 = len F &
      (for n be Nat st n in dom F holds F.n = p) holds
       ex N be Element of ExtREAL st N = k+1 & Sum(F) = N * p
     proof
       let F be FinSequence of ExtREAL;
       let p be Element of ExtREAL;
       assume that
A8:     k+1 = len F and
A9:     (for n be Nat st n in dom F holds F.n = p);
       F <> {} by A8,FINSEQ_1:25; then
       consider G be FinSequence, v be set such that
A10:     F = G^<*v*> by FINSEQ_1:63;
       reconsider G as FinSequence of ExtREAL by A10,FINSEQ_1:50;
       reconsider H = <*v*> as FinSequence of ExtREAL by A10,FINSEQ_1:50;
A11:    k + 1 = len G + len <*v*> by A8,A10,FINSEQ_1:35
        .= len G + 1 by FINSEQ_1:56; then
A12:    k = len G by XCMPLX_1:2;
       dom <*v*> = Seg 1 by FINSEQ_1:55; then
A13:    1 in dom <*v*> by FINSEQ_1:4,TARSKI:def 1;
       1 <= k + 1 by NAT_1:29; then
A14:    k + 1 in dom F by A8,FINSEQ_3:27;
    v = <*v*>.1 by FINSEQ_1:57 .= F.(k + 1) by A10,A11,A13,FINSEQ_1:def 7
        .= p by A9,A14; then
       reconsider v as Element of ExtREAL;
       for n be Nat st n in dom G holds G.n = p
       proof
         let n be Nat;
         assume A15:n in dom G; then
A16:      1 <= n & n <= len G by FINSEQ_3:27;
         len G <= len G + 1 by NAT_1:29; then
A17:      1 <= n & n <= len G + 1 by A16,AXIOMS:22;
         len F = len G + len <*v*> by A10,FINSEQ_1:35
          .= len G + 1 by FINSEQ_1:56; then
         n in dom F by A17,FINSEQ_3:27; then
         F.n = p by A9;
         hence thesis by A10,A15,FINSEQ_1:def 7;
       end;
       then consider N1 be Element of ExtREAL such that
A18:    N1 = k & Sum(G) = N1 * p by A7,A12;
       consider f be Function of NAT,ExtREAL such that
A19:    Sum(F) = f.(len F) & f.0 = 0. &
        for i being Nat st i < len F holds f.(i+1) = f.i + F.(i+1)
         by CONVFUN1:def 5;
       consider g be Function of NAT,ExtREAL such that
A20:    Sum(G) = g.(len G) & g.0 = 0. &
        for i being Nat st i < len G holds g.(i+1) = g.i + G.(i+1)
         by CONVFUN1:def 5;
A21:   for k1 be Nat st k1 <= len G holds f.k1 = g.k1
       proof
         defpred P2[Nat] means
         $1 <= len G implies f.$1 = g.$1;
A22:      P2[0] by A19,A20;
A23:      for k1 be Nat st P2[k1] holds P2[k1+1]
         proof
           let k1 be Nat;
           assume
A24:        P2[k1];
           now assume
A25:          k1+1 <= len G;
A26:          k1 <= k1+1 by NAT_1:29;
A27:          k1 < len G by A25,NAT_1:38; then
A28:          g.(k1+1) = g.k1 + G.(k1+1) by A20;
             len F = len G + len <*v*> by A10,FINSEQ_1:35
             .= len G + 1 by FINSEQ_1:56; then
             k1 < len F by A27,NAT_1:38; then
A29:          f.(k1+1) = f.k1 + F.(k1+1) by A19;
             1 <= k1 + 1 by NAT_1:29; then
             k1 + 1 in dom G by A25,FINSEQ_3:27;
             hence thesis by A10,A24,A26,A28,A29,AXIOMS:22,FINSEQ_1:def 7;
           end;
           hence thesis;
         end;
         for k1 be Nat holds P2[k1] from NAT_1:sch 1(A22,A23);
         hence thesis;
       end;
       k < len F by A8,NAT_1:38; then
   Sum(F) = f.k + F.(k+1) by A8,A19 .= g.k + F.(k+1) by A12,A21; then
A30:   Sum(F) = g.k + p by A9,A14 .= Sum(G) + p by A11,A20,XCMPLX_1:2
        .= N1 * p + 1. * p by A18,EXTREAL2:4;
       take N = N1+1.;
       N = k+1 & Sum(F) = N * p
       proof
        thus N = k+1 by A18,MESFUNC1:def 8,SUPINF_2:1;
        now per cases by NAT_1:19;
         case k = 0;
          hence Sum F = N * p by A18,A30,EXTREAL1:29,SUPINF_2:def 1;
         end;
         case 0 < k; then
A31:       0. <' N1 by A18,EXTREAL1:18;
          0. <' 1. by EXTREAL1:18,MESFUNC1:def 8;
          hence Sum(F) = N * p by A30,A31,EXTREAL1:29;
         end;
        end;
        hence Sum(F) = N * p;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
   for k be Nat holds P[k] from NAT_1:sch 1(A3,A6); then
   consider N' be Element of ExtREAL such that
A32: N' = len a & Sum(a) = N' * p by A2;
   thus thesis by A1,A32;
end;

Lm2:
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL st
    f is_simple_func_in S
   & (for x be set st x in dom f holds 0. <=' f.x)
   & (for x be set st x in dom f holds 0. <> f.x) holds
        ex  F be Finite_Sep_Sequence of S,
              a be FinSequence of ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. <' a.n &  a.n <' +infty
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <=' f.x and
A3:for x be set st x in dom f holds 0. <> f.x;
   consider F1 be Finite_Sep_Sequence of S such that
A4: dom f = union rng F1 and
A5: for n be Nat, x,y be Element of X st n in dom F1 & x in F1.n & y in F1.n
     holds f.x = f.y by A1,MESFUNC2:def 5;
   consider G be Finite_Sep_Sequence of S such that
A6: union rng F1 = union rng G and
A7: for n be Nat st n in dom G holds
     (G.n <> {} & ex m be Nat st m in dom F1 & F1.m = G.n) by Th13;
   set F = <*{}*>^G;
   rng <*{}*> = {{}} & {} in S by FINSEQ_1:55,MEASURE1:21; then
   rng <*{}*> c= S & rng G c= S by FINSEQ_1:def 4,ZFMISC_1:37; then
   rng <*{}*> \/ rng G c= S by XBOOLE_1:8; then
   rng F c= S by FINSEQ_1:44; then
   reconsider F as FinSequence of S by FINSEQ_1:def 4;
   for x,y be set st x <> y holds F.x misses F.y
   proof
     let x,y be set;
     assume
A8:   x<>y;
     now per cases;
       case
A9:      x in dom F & y in dom F;
         dom F c= NAT by RELSET_1:12; then
         reconsider x,y as Nat by A9;
         x in Seg (len (<*{}*>^G)) & y in Seg (len (<*{}*>^G))
           by A9,FINSEQ_1:def 3; then
A10:      1 <= x & x <= len (<*{}*>^G) &
         1 <= y & y <= len (<*{}*>^G) by FINSEQ_1:3;
         now per cases;
           case
A11:          x = 1 or y = 1;
A12:          dom <*{}*> = Seg 1 by FINSEQ_1:55;
             now per cases by A11;
               case
A13:            x = 1; then
               x in dom <*{}*> by A12,FINSEQ_1:4,TARSKI:def 1; then
               F.x = <*{}*>.x by FINSEQ_1:def 7; then
               F.x = {} by A13,FINSEQ_1:def 8;
               hence thesis by XBOOLE_1:65;
               end;
               case
A14:            y = 1; then
               y in dom <*{}*> by A12,FINSEQ_1:4,TARSKI:def 1; then
               F.y = <*{}*>.y by FINSEQ_1:def 7; then
               F.y = {} by A14,FINSEQ_1:def 8;
               hence thesis by XBOOLE_1:65;
               end;
             end;
             hence thesis;
           end;
           case x <> 1 & y <> 1; then
             1 < x & 1 < y by A10,REAL_1:def 5; then
             len <*{}*> < x & len <*{}*> < y by FINSEQ_1:57; then
A15:          F.x = G.(x - len <*{}*>) & F.y = G.(y - len <*{}*>)
               by A10,FINSEQ_1:37;
             x - (len <*{}*>) <> y - (len <*{}*>) by A8,XCMPLX_1:19;
             hence thesis by A15,PROB_2:def 3;
           end;
         end;
         hence thesis;
       end;
       case not (x in dom F) or not (y in dom F); then
         F.x = {} or F.y = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
       end;
     end;
     hence thesis;
   end; then
   reconsider F as Finite_Sep_Sequence of S by PROB_2:def 3;
A16:union rng F = union (rng <*{}*> \/ rng G) by FINSEQ_1:44
   .= union ({{}} \/ rng G) by FINSEQ_1:55
   .= union {{}} \/ union rng G by ZFMISC_1:96
   .= {} \/ union rng G by ZFMISC_1:31
   .= dom f by A4,A6;
   defpred P[Nat,Element of ExtREAL] means
    (for x be Element of X st $1 in dom F & $1 = 1 holds $2 = 0.) &
    (for x be Element of X st $1 in dom F & $1 >= 2 & x in F.$1 holds
      $2 = f.x);
A17:for k be Nat st k in Seg len F
    ex y be Element of ExtREAL st P[k,y]
   proof
     let k be Nat;
     assume A18:k in Seg len F;
     ex y be Element of ExtREAL st P[k,y]
     proof
       now per cases;
         case A19:k = 1;
           take y = 0.;
A20:        for x be Element of X st k in dom F & k = 1 holds y = 0.;
           for x be Element of X st k in dom F & k >= 2 & x in F.k holds
            y = f.x by A19;
           hence thesis by A20;
         end;
         case A21:k <> 1;
           1 <= k & k <= len F by A18,FINSEQ_1:3; then
A22:       1 < k & k <= len <*{}*> + len G by A21,FINSEQ_1:35,REAL_1:def 5
; then
        1+1 <= k & k <= len <*{}*> + len G by NAT_1:38; then
A23:        len <*{}*> + 1 <= k & k <= len <*{}*> + len G by FINSEQ_1:56; then
           consider k2 be Nat such that
A24:         len <*{}*> + 1 + k2 = k by NAT_1:28;
       1 + k2 = k - len <*{}*> by A24; then
A25:       1 <= 1 + k2 by A23,REAL_1:84;
           k - len <*{}*> <= len G by A22,REAL_1:86; then
           1 + k2 in Seg len G by A24,A25,FINSEQ_1:3; then
A26:       1 + k2 in dom G by FINSEQ_1:def 3;
A27:       F.k = G.(k - len <*{}*>) by A23,FINSEQ_1:36 .= G.(1+k2) by A24; then
           F.k <> {} by A7,A26; then
           consider z be set such that
A28:        z in F.k by XBOOLE_0:def 1;
           take y = f.z;
           consider m1 be Nat such that
A29:        m1 in dom F1 & F1.m1 = G.(1+k2) by A7,A26;
           F.k in rng F1 & rng F1 c= S
            by A27,A29,FINSEQ_1:def 4,FUNCT_1:12; then
           F.k in S & S c= bool X; then
           reconsider z as Element of X by A28;
A30:       for x be Element of X st k in dom F & k = 1 holds y = 0. by A21;
           for x be Element of X st k in dom F & k >= 2 & x in F.k holds
            y = f.x
           proof
             let x be Element of X;
             assume k in dom F & k >= 2 & x in F.k; then
             z in F1.m1 & x in F1.m1 by A27,A28,A29;
             hence thesis  by A5,A29;
           end;
           hence thesis by A30;
         end;
       end;
       hence thesis;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
A31: dom a = Seg len F &
    for n be Nat st n in Seg len F holds
       P[n,a.n] from FINSEQ_1:sch 5(A17);
A32:dom F = dom a &
   a.1 = 0. &
   (for n be Nat,x be Element of X st n in dom F & n >= 2 & x in F.n holds
    a.n = f.x)
   proof
     thus dom a = dom F by A31,FINSEQ_1:def 3;
     len F = len <*{}*> + len G by FINSEQ_1:35 .= 1 + len G by FINSEQ_1:56;then
     1 <= len F by NAT_1:29; then
A33:  1 in Seg len F by FINSEQ_1:3; then
  1 in dom F by FINSEQ_1:def 3;
     hence a.1 = 0. by A31,A33;
     for n be Nat, x be Element of X st n in dom F & n >= 2 & x in F.n holds
      a.n = f.x
     proof
       let n be Nat;
       let x be Element of X;
       assume
A34:     n in dom F & n >= 2 & x in F.n; then
       n in Seg len F by FINSEQ_1:def 3;
       hence thesis by A31,A34;
     end;
     hence thesis;
   end;
A35:for n' be Nat st n' in dom F
      for x be set st x in F.n' holds f.x=a.n'
    proof
      let n' be Nat;
      assume
A36:    n' in dom F;
      now per cases;
       case A37:n' = 1;
        thus for x be set st x in F.n' holds f.x = a.n'
        proof
          let x be set;
          assume A38:x in F.n';
          dom <*{}*> = {1} by FINSEQ_1:4,55; then
          1 in dom <*{}*> by TARSKI:def 1; then
          F.1 = <*{}*>.1 by FINSEQ_1:def 7 .= {} by FINSEQ_1:57;
          hence thesis by A37,A38;
        end;
       end;
       case
A39:     n' <> 1;
        n' in Seg len F by A36,FINSEQ_1:def 3; then
        1 <= n' & n' <= len F by FINSEQ_1:3; then
        1 < n' by A39,REAL_1:def 5; then
A40:     1+1 <= n' by NAT_1:38;
        thus for x be set st x in F.n' holds f.x = a.n'
        proof
          let x be set;
          assume A41:x in F.n';
          F.n' in rng F & rng F c= S by A36,FUNCT_1:12,RELSET_1:12; then
          F.n' in S & S c= bool X; then
          reconsider x as Element of X by A41;
          a.n' = f.x by A32,A36,A40,A41;
          hence thesis;
        end;
       end;
      end;
      hence thesis;
    end;
A42:for n be Nat st 2 <= n & n in dom a holds  0. <' a.n &  a.n <' +infty
   proof
     let n be Nat;
     assume that
A43:   2 <= n and
A44:   n in dom a;
     n in Seg len F by A32,A44,FINSEQ_1:def 3; then
     n <= len F by FINSEQ_1:3; then
A45:  n <= len <*{}*> + len G by FINSEQ_1:35;
     1+1 <= n by A43; then
     len <*{}*> + 1 <= n by FINSEQ_1:56; then
A46:  F.n = G.(n - len <*{}*>) by A45,FINSEQ_1:36 .= G.(n-1) by FINSEQ_1:56;
A47:  dom <*{}*> = {1} by FINSEQ_1:4,55;
     1 <> n by A43; then
     not n in dom <*{}*> by A47,TARSKI:def 1; then
     consider k be Nat such that
A48:   k in dom G & n = len <*{}*> + k by A32,A44,FINSEQ_1:38;
     n = 1 + k by A48,FINSEQ_1:56; then
     k = n - 1; then
     F.n <> {} by A7,A46,A48; then
     consider x1 be set such that
A49:   x1 in F.n by XBOOLE_0:def 1;
     F.n in rng F by A32,A44,FUNCT_1:12; then
A50: F.n c= union rng F by ZFMISC_1:92; then
 x1 in dom f & dom f c= X by A16,A49,RELSET_1:12; then
     reconsider x1 as Element of X;
A51: a.n = f.x1 by A32,A43,A44,A49;
     f is_finite by A1,MESFUNC2:def 5; then
A52: |.f.x1.| <' +infty by A16,A49,A50,MESFUNC2:def 1;
     0. <=' f.x1 & 0. <> f.x1 by A2,A3,A16,A49,A50;
     hence thesis by A51,A52,EXTREAL2:58,SUPINF_1:22;
   end;
   take F,a;
   thus thesis by A16,A32,A35,A42,Def1;
end;

Lm3:
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL st
    f is_simple_func_in S
   & (for x be set st x in dom f holds 0. <=' f.x)
   & (ex x be set st x in dom f & 0. = f.x) holds
        ex F be Finite_Sep_Sequence of S,
              a be FinSequence of  ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. <' a.n &  a.n <' +infty
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <=' f.x and
A3:ex x be set st x in dom f & 0. = f.x;

   consider x0 be set such that
A4:x0 in dom f & 0.= f.x0 by A3;
   dom f c= X by RELSET_1:12; then
   reconsider x0 as Element of X by A4;

   consider F1 be Finite_Sep_Sequence of S such that
A5:dom f = union rng F1 &
    (for n be Nat, x,y be Element of X st
     n in dom F1 & x in F1.n & y in F1.n holds f.x = f.y) by A1,MESFUNC2:def 5;

   consider V be set such that
A6:x0 in V & V in rng F1 by A4,A5,TARSKI:def 4;
   consider n0 be set such that
A7:n0 in dom F1 & V = F1.n0 by A6,FUNCT_1:def 5;
   dom F1 c= NAT by RELSET_1:12; then
   reconsider n0 as Nat by A7;
   set N={n where n is Nat:
           n in dom F1 &
           for x be Element of X st x in F1.n holds f.x = 0.};
A8:for x be Element of X st x in F1.n0 holds f.x = 0. by A4,A5,A6,A7;
   for z be set st z in N holds z in dom F1
   proof
     let z be set;
     assume z in N; then
     consider n' be Nat such that
A10:  z = n' & n' in dom F1 &
      for x be Element of X st x in F1.n' holds f.x = 0.;
     thus thesis by A10;
   end; then
A11:N c= dom F1 by TARSKI:def 3; then
   reconsider N as finite set by FINSET_1:13;
   consider P1 be FinSequence such that
A12: rng P1 = N & P1 is one-to-one by FINSEQ_4:73;
   set M = dom F1 \ N;
A13:M c= dom F1 by XBOOLE_1:36; then
   reconsider M as finite set by FINSET_1:13;
   consider P2 be FinSequence such that
A14: rng P2 = M & P2 is one-to-one by FINSEQ_4:73;
   set F0 = {Fn where Fn is Element of S :
                    Fn in rng F1 &
                    for x be Element of X st x in Fn holds f.x = 0.};
   set G1 = union F0;
   for F' be set st F' in F0 holds F' in bool X
   proof
     let F' be set;
     assume F' in F0; then
     consider Fn be Element of S such that
A15:  F'=Fn & Fn in rng F1 &
      for x be Element of X st x in Fn holds f.x = 0.;
      thus thesis by A15;
   end; then
   F0 c= bool X by TARSKI:def 3; then
   reconsider F0 as Subset-Family of X by SETFAM_1:def 7;
   defpred P[set,set] means
    ($1 in dom F1 implies $2 = F1.$1) &
    (not ($1 in dom F1) implies $2 = {});
A16:for x be set st x in NAT ex y be set st y in S & P[x,y]
   proof
     let x be set;
     assume x in NAT; then
     reconsider x as Nat;
     now per cases;
      case A17:x in dom F1; then
A18:   F1.x in rng F1 & rng F1 c= S by FINSEQ_1:def 4,FUNCT_1:12;
       reconsider y = F1.x as set;
       take y;
       thus thesis by A17,A18;
      end;
      case A19:not (x in dom F1);
       take y = {};
       y in S by MEASURE1:45;
       hence thesis by A19;
      end;
     end;
     hence thesis;
   end;
   consider F2 be Function of NAT,S such that
A20:for x be set st x in NAT holds P[x,F2.x] from FUNCT_2:sch 1(A16);
   for z be set st z in rng F1 holds z in rng F2
   proof
     let z be set;
     assume z in rng F1; then
     consider n' be set such that
A21:  n' in dom F1 & z = F1.n' by FUNCT_1:def 5;
     dom F1 c= NAT by RELSET_1:12; then
A22: F2.n' = F1.n' by A20,A21;
     dom F1 c= NAT & dom F2 = NAT by FUNCT_2:def 1,RELSET_1:12;
     hence thesis by A21,A22,FUNCT_1:12;
   end; then
   rng F1 c= rng F2 by TARSKI:def 3; then
A23:rng F1 is countable by CARD_4:46;
   for z be set st z in F0 holds z in rng F1
   proof
     let z be set;
     assume z in F0; then
     consider Fn' be Element of S such that
A24:  z = Fn' & Fn' in rng F1 &
      for x be Element of X st x in Fn' holds f.x = 0.;
     thus thesis by A24;
   end; then
A25:F0 c= rng F1 by TARSKI:def 3;
   F1.n0 is Element of S & F1.n0 in rng F1 &
   for x be Element of X st x in F1.n0 holds f.x = 0.
   proof
    consider n0' be Nat such that
A26: n0 = n0' & n0' in dom F1 &
     for x be Element of X st x in F1.n0' holds f.x = 0. by A7,A8;
    F1.n0 in rng F1 & rng F1 c= S by A26,FUNCT_1:12,RELSET_1:12;
    hence F1.n0 is Element of S;
    thus F1.n0 in rng F1 by A26,FUNCT_1:12;
    let x be Element of X;
    assume x in F1.n0;
    hence thesis by A26;
   end; then
A27:F1.n0 in F0; then
   reconsider F0 as N_Sub_set_fam of X by A23,A25,CARD_4:46;
   for F' be set st F' in F0 holds F' in S
   proof
     let F' be set;
     assume F' in F0; then
     consider Fn be Element of S such that
A28:  F'=Fn & Fn in rng F1 &
      for x be Element of X st x in Fn holds f.x = 0.;
     thus thesis by A28;
   end; then
   F0 c= S by TARSKI:def 3; then
   reconsider G1 as Element of S by MEASURE1:def 9;
A29:len P1 = card N & len P2 = card M by A12,A14,FINSEQ_4:77;
   reconsider L = Seg len F1 as finite set;
A30:N c= Seg len F1 by A11,FINSEQ_1:def 3; then
   card N <= card L by CARD_1:80; then
   len P1 <= len F1 & len F1 <= len F1 + 1 by A29,FINSEQ_1:78,NAT_1:29; then
   len P1 <= len F1 + 1 by AXIOMS:22; then
   consider lenF be Nat such that
A31:len F1 + 1 = len P1 + lenF by NAT_1:28;
A32:lenF = len F1 + 1 - len P1 by A31;
   M = (Seg len F1) \ N by FINSEQ_1:def 3; then
   card M = card (Seg len F1) - card N by A30,CARD_2:63; then
A33:len P2 = len F1 - len P1 + 1 - 1 by A29,FINSEQ_1:78;
   defpred P[Nat,set] means
    ($1 = 1 implies $2 = G1) &
    ($1 >=2 implies ex k' be Nat st k'=$1-1 & $2 = F1.(P2.k'));
A34:for k be Nat st k in Seg lenF ex U be Element of S st P[k,U]
   proof
     let k be Nat;
     assume A35:k in Seg lenF;
     per cases;
      suppose A36:k = 1;
       take U = G1;
       thus thesis by A36;
      end;
      suppose A37:k <> 1;
A38:   k >= 1 & k <= lenF by A35,FINSEQ_1:3; then
       k > 1 by A37,REAL_1:def 5; then
A39:   k >= 1+1 by NAT_1:38;
       consider k' be Nat such that
A40:    k = 1 + k' by A38,NAT_1:28;
       k-1>=1 & k-1<=lenF -1 by A38,A39,REAL_1:49,84; then
       k' in Seg len P2 by A32,A33,A40,FINSEQ_1:3; then
       k' in dom P2 by FINSEQ_1:def 3; then
       P2.k' in M by A14,FUNCT_1:12; then
       F1.(P2.k') in rng F1 & rng F1 c= S by A13,FUNCT_1:12,RELSET_1:12; then
       reconsider U = F1.(P2.k') as Element of S;
       take U;
       thus thesis by A37,A40;
      end;
   end;
   consider F be FinSequence of S such that
A41: dom F = Seg lenF &
    for k be Nat st k in Seg lenF holds
     P[k,F.k] from FINSEQ_1:sch 5(A34);
   defpred A[Nat,Element of ExtREAL] means
   for y be Element of X holds
    (y in F.$1 implies $2 = f.y) & (F.$1 = {} implies $2 = 1.);
A42:for k be Nat st k in Seg len F
    ex x be Element of ExtREAL st A[k,x]
   proof
     let k be Nat;
     assume k in Seg len F; then
A43: k in dom F by FINSEQ_1:def 3;
     now per cases;
      case k = 1; then
A44:   F.k = G1 by A41,A43;
A45:   for y be Element of X holds
        (y in F.k implies 0. = f.y) & (F.k = {} implies 0.=1.)
       proof
        let y be Element of X;
A46:   y in F.k implies 0. = f.y
        proof
         assume y in F.k; then
         consider Y be set such that
A47:      y in Y & Y in F0 by A44,TARSKI:def 4;
         consider Fn be Element of S such that
A48:      Y = Fn & Fn in rng F1 &
          for x be Element of X st x in Fn holds f.x = 0. by A47;
         thus f.y = 0. by A47,A48;
        end;
        thus thesis by A6,A7,A27,A44,A46,TARSKI:def 4;
       end;
       take x = 0.;
       thus thesis by A45;
      end;
      case A49:k <> 1;
A50:   1 <= k & k <= lenF by A41,A43,FINSEQ_1:3; then
       1 < k by A49,REAL_1:def 5; then
A51:   1+1 <= k by NAT_1:38; then
       consider k1 be Nat such that
A52:    k1 = k-1 & F.k = F1.(P2.k1) by A41,A43;
       1 <= k1 & k1 <= len P2 by A32,A33,A50,A51,A52,REAL_1:49,84; then
       k1 in Seg len P2 by FINSEQ_1:3; then
       k1 in dom P2 by FINSEQ_1:def 3; then
       P2.k1 in M by A14,FUNCT_1:12; then
       consider k' be set such that
A53:    k' in dom F1 & F.k = F1.k' by A13,A52;
       dom F1 c= NAT by RELSET_1:12; then
       reconsider k' as Nat by A53;
       now per cases;
        case A54:F.k = {};
A55:     for y be Element of X holds
          (y in F.k implies 1. = f.y) & (F.k = {} implies 1.=1.) by A54;
         take x = 1.;
         thus thesis by A55;
        end;
        case F.k <> {}; then
         consider y1 be set such that
A56:      y1 in F.k by XBOOLE_0:def 1;
         F.k in rng F & rng F c= S by A43,FINSEQ_1:def 4,FUNCT_1:12; then
         F.k in S & S c= bool X; then
         reconsider y1 as Element of X by A56;
         take x = f.y1;
         for y be Element of X holds
          (y in F.k implies x = f.y) & (F.k = {} implies x=1.)
         proof
          let y be Element of X;
          y in F.k implies x = f.y
          proof
           assume y in F.k; then
           y in F1.k' & y1 in F1.k' by A53,A56;
           hence thesis by A5,A53;
          end;
          hence thesis by A56;
         end;
         hence thesis;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
   end;
   consider a be FinSequence of ExtREAL such that
A57: dom a = Seg len F &
    for n be Nat st n in Seg (len F) holds A[n,a.n] from FINSEQ_1:sch 5(A42);
A58:for n be Nat, x be Element of X st n in dom F & x in F.n holds a.n = f.x
   proof
    let n be Nat;
    let x be Element of X;
    assume A59:n in dom F & x in F.n; then
    n in Seg len F by FINSEQ_1:def 3;
    hence thesis by A57,A59;
   end;

   for z be set st z in union rng F holds z in union rng F1
   proof
    let z be set;
    assume z in union rng F; then
    consider Y be set such that
A60:  z in Y & Y in rng F by TARSKI:def 4;
    consider k be set such that
A61:  k in dom F & Y = F.k by A60,FUNCT_1:def 5;
    dom F c= NAT by RELSET_1:12; then
    reconsider k as Nat by A61;
    now per cases;
     case k = 1; then
      z in G1 by A41,A60,A61; then
      consider Y' be set such that
A62:    z in Y' & Y' in F0 by TARSKI:def 4;
      consider Fn' be Element of S such that
A63:    Y' = Fn' &
       Fn' in rng F1 &
       for x be Element of X st x in Fn' holds f.x = 0. by A62;
      thus thesis by A62,A63,TARSKI:def 4;
     end;
     case A64:k <> 1;
A65:   1 <= k & k <= lenF by A41,A61,FINSEQ_1:3; then
      1 < k by A64,REAL_1:def 5; then
A66:   1 + 1 <= k by NAT_1:38; then
      consider k' be Nat such that
A67:    k' = k-1 & F.k = F1.(P2.k') by A41,A61;
      1 <= k' & k' <= len P2 by A32,A33,A65,A66,A67,REAL_1:49,84; then
      k' in dom P2 by FINSEQ_3:27; then
      P2.k' in M by A14,FUNCT_1:12; then
      F1.(P2.k') in rng F1 by A13,FUNCT_1:12;
      hence thesis by A60,A61,A67,TARSKI:def 4;
     end;
    end;
    hence thesis;
   end; then
A68:union rng F c= union rng F1 by TARSKI:def 3;
   for z be set st z in union rng F1 holds z in union rng F
   proof
    let z be set;
    assume z in union rng F1; then
    consider Y be set such that
A69:  z in Y & Y in rng F1 by TARSKI:def 4;
    consider m1 be set such that
A70:  m1 in dom F1 & Y = F1.m1 by A69,FUNCT_1:def 5;
    dom F1 c= NAT by RELSET_1:12; then
    reconsider m1 as Nat by A70;
    now per cases;
     case m1 in N; then
      consider m1' be Nat such that
A71:    m1 = m1' & m1' in dom F1 &
       for x be Element of X st x in F1.m1' holds f.x = 0.;
A72:   F1.m1' in rng F1 & rng F1 c= S by A71,FUNCT_1:12,RELSET_1:12; then
      reconsider F1m1' = F1.m1' as Element of S;
A73:   z in Y & Y in F0 by A69,A70,A71,A72;
      lenF - 1 >= 0 by A32,A33,NAT_1:18; then
      lenF >= 1+0 by REAL_1:84; then
      1 in Seg lenF by FINSEQ_1:3; then
      F.1 in rng F & F.1 = G1 by A41,FUNCT_1:12; then
      z in union F0 & union F0 in rng F by A73,TARSKI:def 4;
      hence thesis by TARSKI:def 4;
     end;
     case not (m1 in N); then
      m1 in M by A70,XBOOLE_0:def 4; then
      consider m1' be set such that
A74:    m1' in dom P2 & m1 = P2.m1' by A14,FUNCT_1:def 5;
      m1' in Seg (len P2) & Seg (len P2) c= NAT by A74,FINSEQ_1:def 3; then
      reconsider m1' as Nat;
      m1' in Seg len P2 by A74,FINSEQ_1:def 3; then
      1 <= m1' & m1' <= len P2 by FINSEQ_1:3; then
A75:   1+1 <= m1' + 1 &  m1'+1 <= lenF by A32,A33,AXIOMS:24;
      reconsider m2 = m1'+1 as Nat;
      1 <= m2 by A75,AXIOMS:22; then
A76:  m2 in Seg lenF by A75,FINSEQ_1:3; then
      consider k' be Nat such that
A77:   k' = m2 -1 & F.m2 = F1.(P2.k') by A41,A75;
A78:  F.m2 in rng F by A41,A76,FUNCT_1:12;
      thus thesis by A69,A70,A74,A77,A78,TARSKI:def 4;
     end;
    end;
    hence thesis;
   end; then
   union rng F1 c= union rng F by TARSKI:def 3; then
A79:union rng F = dom f by A5,A68,XBOOLE_0:def 10;
A80:a.1 = 0. &
    dom F = dom a &
    (for n be Nat,x be Element of X st n in dom F & x in F.n holds
      a.n = f.x)
   proof
     0 <= lenF - 1 by A32,A33,NAT_1:18; then
     0+1 <= lenF by REAL_1:84; then
A81:1 in Seg lenF by FINSEQ_1:3; then
A82:1 in Seg len F by A41,FINSEQ_1:def 3;
A83:F.1 = G1 by A41,A81;
     rng F1 c= S by RELSET_1:12; then
     reconsider F1n0 = F1.n0 as Element of S by A6,A7;
     F1n0 in F0 by A6,A7,A8; then
     x0 in F.1 by A6,A7,A83,TARSKI:def 4;
     hence a.1 = 0. by A4,A57,A82;
     thus dom F = dom a by A57,FINSEQ_1:def 3;
     thus thesis by A58;
   end;
A84:for n be Nat st n in dom F
     for x be set st x in F.n holds f.x=a.n
    proof
     let n be Nat;
     assume A85:n in dom F; then
     F.n in rng F & rng F c= S by FUNCT_1:12,RELSET_1:12; then
A86:F.n in S & S c= bool X;
     let x be set;
     assume A87:x in F.n; then
     reconsider x as Element of X by A86;
     a.n = f.x by A58,A85,A87;
     hence thesis;
    end;
A88:for n,m be Nat st n in dom F & m in dom F & n = 1 & m <> 1 holds
     F.n misses F.m
    proof
     let n,m be Nat;
     assume
A89:   n in dom F & m in dom F & n = 1 & m <> 1;
A90:  F.n = G1 by A41,A89;
     1 <= m & m <= len F by A89,FINSEQ_3:27; then
     1 < m by A89,REAL_1:def 5; then
A91:  1+1 <= m & m in Seg lenF by A41,A89,NAT_1:38; then
     consider k' be Nat such that
A92:   k' = m-1 & F.m = F1.(P2.k') by A41;
     m-1>=1 & m <= lenF by A91,FINSEQ_1:3,REAL_1:84; then
     m-1>=1 & m-1<=lenF -1 by REAL_1:49; then
     k' in Seg len P2 by A32,A33,A92,FINSEQ_1:3; then
     k' in dom P2 by FINSEQ_1:def 3; then
     P2.k' in M by A14,FUNCT_1:12; then
A93: P2.k' in dom F1 & not (P2.k' in N) by XBOOLE_0:def 4;
     F.n /\ F.m = {}
     proof
       assume F.n /\ F.m <> {}; then
       consider v be set such that
A94:     v in F.n /\ F.m by XBOOLE_0:def 1;
A95:    v in F.n & v in F.m by A94,XBOOLE_0:def 3; then
       consider Y be set such that
A96:     v in Y & Y in F0 by A90,TARSKI:def 4;
       consider Fv be Element of S such that
A97:     Y = Fv & Fv in rng F1 &
        for x be Element of X st x in Fv holds f.x = 0. by A96;
       consider n' be set such that
A98:     n' in dom F1 & Fv = F1.n' by A97,FUNCT_1:def 5;
       dom F1 c= NAT by RELSET_1:12; then
       reconsider n' as Nat by A98;
A99:   n' <> P2.k' by A93,A97,A98;
       F1.(n') misses F1.(P2.k') by A99,PROB_2:def 3; then
   F1.(n') /\ F1.(P2.k') = {} by XBOOLE_0:def 7;
       hence contradiction by A92,A95,A96,A97,A98,XBOOLE_0:def 3;
     end;
     hence thesis by XBOOLE_0:def 7;
    end;
A100:for n,m be Nat st n in dom F & m in dom F & n <> 1 & m <> 1 & n <> m holds
     F.n misses F.m
    proof
     let n,m be Nat;
     assume
A101:   n in dom F & m in dom F & n <> 1 & m <> 1 & n <> m; then
A102: 1 <= n & n <= lenF & 1 <= m & m <= lenF by A41,FINSEQ_1:3; then
     1 < n & 1 < m by A101,REAL_1:def 5; then
A103:  1+1 <= n & 1+1 <= m by NAT_1:38; then
     consider k1 be Nat such that
A104:   k1 = n-1 & F.n = F1.(P2.k1) by A41,A101;
     consider k2 be Nat such that
A105:   k2 = m-1 & F.m = F1.(P2.k2) by A41,A101,A103;
     1 <= n-1 & n-1 <= lenF -1 & 1<=m-1 & m-1<=lenF -1
      by A102,A103,REAL_1:49,84; then
     k1 in Seg len P2 & k2 in Seg len P2 by A32,A33,A104,A105,FINSEQ_1:3; then
A106:  k1 in dom P2 & k2 in dom P2 by FINSEQ_1:def 3;
     k1 <> k2 by A101,A104,A105,XCMPLX_1:19; then
     P2.k1 <> P2.k2 by A14,A106,FUNCT_1:def 8;
     hence thesis by A104,A105,PROB_2:def 3;
    end;
    for x,y be set st x <> y holds F.x misses F.y
    proof
     let x,y be set;
     assume A107:x<>y;
     now per cases;
      case A108:x in dom F & y in dom F;
       dom F c= NAT by RELSET_1:12; then
       reconsider x,y as Nat by A108;
       now per cases;
        case A109:x = 1 or y = 1;
         now per cases by A109;
          case A110:x = 1; then
           y <> 1 by A107;
           hence thesis by A88,A108,A110;
          end;
          case A111:y = 1; then
           x <> 1 by A107;
           hence thesis by A88,A108,A111;
          end;
         end;
         hence thesis;
        end;
        case x <> 1 & y <> 1;
         hence thesis by A100,A107,A108;
        end;
       end;
       hence thesis;
      end;
      case A112:not (x in dom F) or not (y in dom F);
       now per cases by A112;
        case not (x in dom F); then
         F.x = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
        end;
        case not (y in dom F); then
         F.y = {} by FUNCT_1:def 4;
         hence thesis by XBOOLE_1:65;
        end;
       end;
       hence thesis;
      end;
     end;
     hence thesis;
    end; then
    reconsider F as Finite_Sep_Sequence of S by PROB_2:def 3;
A113:for n be Nat st 2 <= n & n in dom a holds 0. <' a.n & a.n <' +infty
   proof
    let n be Nat;
    assume A114:2 <= n & n in dom a;
A115:f is_finite by A1,MESFUNC2:def 5;
    0 <= lenF - 1 by A32,A33,NAT_1:18; then
    0+1 <= lenF by REAL_1:84; then
A116: 1 in dom F & n <> 1 by A41,A114,FINSEQ_1:3; then
    F.1 misses F.n by A80,A88,A114; then
A117: G1 misses F.n by A41,A116;
A118:1 <= n & n <= lenF by A41,A80,A114,FINSEQ_1:3;
    consider k1 be Nat such that
A119:  k1 = n-1 & F.n = F1.(P2.k1) by A41,A80,A114;
    1 + 1 <= n by A114; then
    1 <= n-1 & n-1 <= lenF -1 by A118,REAL_1:49,84; then
    k1 in Seg len P2 by A32,A33,A119,FINSEQ_1:3; then
    k1 in dom P2 by FINSEQ_1:def 3; then
    P2.k1 in M by A14,FUNCT_1:12; then
A120: F.n in rng F1 by A13,A119,FUNCT_1:12;
    a.n <> 0. & 0. <=' a.n & a.n <' +infty
    proof
     now per cases;
      case F.n <> {}; then
       consider y be set such that
A121:     y in F.n by XBOOLE_0:def 1;
A122:    F.n in rng F & rng F c= S by A80,A114,FINSEQ_1:def 4,FUNCT_1:12; then
       F.n in S & S c= bool X; then
       reconsider y as Element of X by A121;
       reconsider Fn = F.n as Element of S by A122;
A123:    a.n = f.y by A57,A114,A121;
       G1 /\ F.n = {} by A117,XBOOLE_0:def 7; then
A124:    not y in G1 by A121,XBOOLE_0:def 3;
       thus a.n <> 0.
       proof
        assume a.n = 0.; then
        for x be Element of X st x in F.n holds f.x = 0. by A80,A114; then
        Fn in F0 by A120;
        hence contradiction by A121,A124,TARSKI:def 4;
       end;
       y in union rng F by A121,A122,TARSKI:def 4; then
       0. <=' f.y & |. f.y .| <' +infty by A2,A79,A115,MESFUNC2:def 1;
       hence 0. <=' a.n & a.n <' +infty by A123,EXTREAL2:58;
      end;
      case F.n = {}; then
       a.n = 1. by A57,A114;
       hence thesis by EXTREAL2:20,MESFUNC1:def 8,SUPINF_1:16,SUPINF_2:def 1;
      end;
     end;
     hence thesis;
    end;
    hence thesis by SUPINF_1:22;
   end;
   take F,a;
   thus thesis by A79,A80,A84,A113,Def1;
end;

theorem Th14:
for X be non empty set,
    S be sigma_Field_Subset of X,
    f be PartFunc of X,ExtREAL st
      f is_simple_func_in S
      & for x be set st x in dom f holds 0. <=' f.x holds
        ex F be Finite_Sep_Sequence of S,
              a be FinSequence of ExtREAL
              st F,a are_Re-presentation_of f
                 & a.1=0.
                 & for n be Nat st 2 <= n & n in dom a
                 holds 0. <' a.n & a.n <' +infty
proof
   let X be non empty set;
   let S be sigma_Field_Subset of X;
   let f be PartFunc of X,ExtREAL;
   assume that
A1:f is_simple_func_in S and
A2:for x be set st x in dom f holds 0. <=' f.x;
   per cases;
     suppose ex x be set st x in dom f & 0. = f.x;
       hence thesis by A1,A2,Lm3;
     end;
     suppose for x be set st x in dom f holds 0. <> f.x;
       hence thesis by A1,A2,Lm2;
     end;
end;

theorem
  for X be non empty set,
      S be sigma_Field_Subset of X,
      f be PartFunc of X,ExtREAL,
      F be Finite_Sep_Sequence of S,
      a be FinSequence of ExtREAL,
      x be Element of X
      st F,a are_Re-presentation_of f
          & x in dom f holds
   ex ax be FinSequence of ExtREAL st dom ax= dom a &
        (for n be Nat st n in dom ax
          holds ax.n=a.n*(chi(F.n,X)).x) & f.x=Sum(ax)
proof
let X be non empty set;
let S be sigma_Field_Subset of X;
let f be PartFunc of X,ExtREAL;
let F be Finite_Sep_Sequence of S;
let a be FinSequence of ExtREAL;
let x be Element of X such that
A1: F,a are_Re-presentation_of f and
A2: x in dom f;
A3:dom f = union rng F & dom F = dom a &
  for n be Nat st n in dom F
   for x be set st x in F.n holds f.x = a.n by A1,Def1;
  deffunc F(Nat) = a.$1*(chi(F.$1,X)).x;
  consider ax be FinSequence of ExtREAL such that
A4:len ax = len a &
   for j be Nat st j in Seg len a holds
    ax.j = F(j) from FINSEQ_2:sch 1;
   take ax;
A5:dom ax = Seg len a by A4,FINSEQ_1:def 3;
   f.x = Sum(ax)
   proof
    consider Y be set such that
A6:  x in Y & Y in rng F by A2,A3,TARSKI:def 4;
    consider k be set such that
A7:  k in dom F & Y = F.k by A6,FUNCT_1:def 5;
    dom F c= NAT by RELSET_1:12; then
    reconsider k as Nat by A7;
A8: k in Seg len a by A3,A7,FINSEQ_1:def 3;
    consider SA be Function of NAT,ExtREAL such that
A9:  Sum(ax) = SA.(len ax) & SA.0 = 0. &
     for i be Nat st i < len ax holds SA.(i+1)=SA.i + ax.(i+1)
      by CONVFUN1:def 5;
A10: for i be Nat st i in Seg len ax & i <> k holds ax.i = 0.
    proof
     let i be Nat;
     assume A11:i in Seg len ax & i <> k; then
A12:  ax.i = a.i * (chi(F.i,X)).x by A4;
     F.i misses F.k by A11,PROB_2:def 3; then
     F.i /\ F.k = {} by XBOOLE_0:def 7; then
     not (x in F.i) by A6,A7,XBOOLE_0:def 3; then
     chi(F.i,X).x = 0. by FUNCT_3:def 3,SUPINF_2:def 1;
     hence thesis by A12,EXTREAL1:def 1;
    end;
A13: for i be Nat st i in Seg len ax & i = k holds ax.i = f.x
    proof
     let i be Nat;
     assume A14:i in Seg len ax & i = k;
     chi(F.i,X).x = 1. by A6,A7,A14,FUNCT_3:def 3,MESFUNC1:def 8;
     then ax.i = a.i * 1. by A4,A14 .= a.i by EXTREAL2:4;
     hence thesis by A1,A6,A7,A14,Def1;
    end;
    defpred P[Nat] means
    $1 <= len ax implies
    ($1 < k implies SA.$1 = 0.) & ($1 >= k implies SA.$1 = f.x);
A15: P[0] by A8,A9,FINSEQ_1:3;
A16: for i be Nat st P[i] holds P[i+1]
    proof
     let i be Nat;
     assume A17:P[i];
     assume A18:i+1 <= len ax;
     now per cases;
      case A19:i+1 < k;
A20:    i <= i+1 by NAT_1:29; then
A21:   i < k & i <= len ax by A18,A19,AXIOMS:22;
       k <= len a by A8,FINSEQ_1:3; then
A22:    i < len ax by A4,A21,AXIOMS:22; then
       1 <= i+1 & i+1 <= len ax by NAT_1:29,38; then
A23:    i+1 in Seg len ax by FINSEQ_1:3;
       SA.(i+1) = 0. + ax.(i+1) by A9,A17,A19,A20,A22,AXIOMS:22
               .= ax.(i+1) by SUPINF_2:18
               .= 0. by A10,A19,A23;
       hence thesis by A19;
      end;
      case A24:i+1 >= k;
       now per cases;
        case A25:k = i+1;
A26:     k <= len a by A8,FINSEQ_1:3; then
         i < len ax by A4,A25,NAT_1:38;
         hence SA.(i+1) = SA.i + ax.(i+1) by A9
                 .= ax.k by A4,A17,A25,A26,NAT_1:38,SUPINF_2:18
                 .= f.x by A4,A8,A13;
        end;
        case k <> i+1; then
A27:    k < i+1 by A24,REAL_1:def 5; then
A28:    k <= i & i < len ax by A18,NAT_1:38;
         1 <= i+1 by NAT_1:29; then
A29:     i+1 in Seg len ax by A18,FINSEQ_1:3;
         thus SA.(i+1) = SA.i + ax.(i+1) by A9,A28
                 .= f.x + 0. by A10,A17,A18,A27,A29,NAT_1:38
                 .= f.x by SUPINF_2:18;
        end;
       end;
       hence thesis by A24;
      end;
     end;
     hence thesis;
    end;
A30: for i be Nat holds P[i] from NAT_1:sch 1(A15,A16);
    len ax >= k by A4,A8,FINSEQ_1:3;
    hence thesis by A9,A30;
   end;
   hence thesis by A4,A5,FINSEQ_1:def 3;
end;

theorem
  for p be FinSequence of ExtREAL,
      q be FinSequence of REAL st p=q holds Sum(p)=Sum(q)
proof
   let p be FinSequence of ExtREAL;
   let q be FinSequence of REAL;
   assume
A1: p = q;
   consider P be Function of NAT,ExtREAL such that
A2: Sum(p) = P.(len p) &
    P.0 = 0. &
    for i be Nat st
     i < len p holds P.(i+1) = P.i + p.(i+1) by CONVFUN1:def 5;
   now per cases;
     case
A3:   len q = 0;
      Sum(q) = addreal "**" q by RVSUM_1:def 10;
      hence thesis by A1,A2,A3,BINOP_2:2,FINSOP_1:def 1,SUPINF_2:def 1;
     end;
     case
A4:   len q <> 0;
      Sum(q) = addreal $$ q by RVSUM_1:def 10; then
      consider Q be Function of NAT,REAL such that
A5:    Q.1 = q.1 &
       (for n be Nat st 0 <> n & n < len q holds
         Q.(n+1) = addreal.(Q.n,q.(n+1))) & Sum(q) = Q.(len q)
          by A4,FINSOP_1:def 1;
      defpred P1[Nat] means
      0 <> $1 & $1 <= len q implies P.$1 = Q.$1;
A6:   P1[0];
A7:   for k be Nat st P1[k] holds P1[k+1]
      proof
        let k be Nat;
        assume
A8:     P1[k];
        assume
A9:     0 <> k+1 & k+1 <= len q;
        per cases;
          suppose
A10:        k = 0;
            k < len q by A9,NAT_1:38; then
            P.(k+1) = P.k + p.(k+1) by A1,A2
                   .= p.(k+1) by A2,A10,SUPINF_2:18;
            hence thesis by A1,A5,A10;
          end;
          suppose
A11:        k <> 0;
A12:        k < len q by A9,NAT_1:38; then
A13:        P.(k+1) = P.k + p.(k+1) by A1,A2;
            Q.(k+1) = addreal.(Q.k,q.(k+1)) by A5,A11,A12
                   .= Q.k + q.(k+1) by BINOP_2:def 9;
            hence thesis by A1,A8,A9,A11,A13,NAT_1:38,SUPINF_2:1;
          end;
      end;
      for n be Nat holds P1[n] from NAT_1:sch 1(A6,A7);
      hence thesis by A1,A2,A4,A5;
     end;
   end;
   hence thesis;
end;

theorem Th17:
for p be FinSequence of ExtREAL st
 (for n be Nat st n in dom p holds 0. <=' p.n) &
 (ex n be Nat st n in dom p & p.n=+infty) holds
   Sum(p)=+infty
proof
   let p be FinSequence of ExtREAL;
   assume that
A1:for n be Nat st n in dom p holds 0. <=' p.n and
A2:ex n be Nat st n in dom p & p.n=+infty;
   consider m be Nat such that
A3: m in dom p & p.m = +infty by A2;
   consider f be Function of NAT,ExtREAL such that
A4: Sum(p) = f.(len p) & f.0 = 0. &
    for i be Nat st i < len p holds f.(i+1)=f.i + p.(i+1) by CONVFUN1:def 5;
   defpred P[Nat] means
   $1 <= len p implies
    ($1 < m implies 0. <=' f.$1) & ($1 >= m implies f.$1 = +infty);
A5:P[0] by A3,A4,FINSEQ_3:27;
A6:for i be Nat st P[i] holds P[i+1]
   proof
    let i be Nat;
    assume A7:P[i];
    assume A8:i+1 <= len p;
    now per cases;
     case A9:i+1 < m;
A10:   i <= i+1 by NAT_1:29;
      i < len p by A8,NAT_1:38; then
A11:   f.(i+1) = f.i + p.(i+1) by A4;
      1 <= i+1 by NAT_1:29; then
      i+1 in Seg len p by A8,FINSEQ_1:3; then
      i+1 in dom p by FINSEQ_1:def 3; then
      0. <=' p.(i+1) by A1;
      hence thesis by A7,A8,A9,A10,A11,AXIOMS:22,EXTREAL2:7;
     end;
     case A12:i+1 >= m;
      now per cases;
       case A13:i+1 = m;
        i < len p by A8,NAT_1:38; then
        f.(i+1) = f.i + p.(i+1) by A4;
        hence f.(i+1) = +infty
         by A3,A7,A8,A13,NAT_1:38,SUPINF_2:19,def 2;
       end;
       case i+1 <> m; then
A14:    i+1 > m by A12,REAL_1:def 5;
        1 <= i+1 by NAT_1:29; then
        i+1 in Seg len p by A8,FINSEQ_1:3; then
        i+1 in dom p by FINSEQ_1:def 3; then
A15:    p.(i+1) <> -infty by A1,SUPINF_2:19;
        i < len p by A8,NAT_1:38; then
        f.(i+1) = f.i + p.(i+1) by A4;
        hence f.(i+1) = +infty by A7,A8,A14,A15,NAT_1:38,SUPINF_2:def 2;
       end;
      end;
      hence thesis by A12;
     end;
    end;
    hence thesis;
   end;
A16:for i be Nat holds P[i] from NAT_1:sch 1(A5,A6);
   m in Seg len p by A3,FINSEQ_1:def 3; then
   len p >= m by FINSEQ_1:3;
   hence thesis by A4,A16;
end;

definition
let X be non empty set;
let S be sigma_Field_Subset of X;
let M be sigma_Measure of S;
let f be PartFunc of X,ExtREAL;
  assume A1: f is_simple_func_in S
                & dom f <> {}
                & for x be set st x in dom f holds 0. <=' f.x;
func integral(X,S,M,f) -> Element of ExtREAL means
  ex F be Finite_Sep_Sequence of S,
      a, x be FinSequence of ExtREAL st F,a are_Re-presentation_of f
         & a.1 =0.
         & (for n be Nat st 2 <= n & n in dom a holds
           0. <' a.n & a.n <' +infty )
         & dom x = dom F
         & (for n be Nat st n in dom x holds x.n=a.n*(M*F).n)
         & it=Sum(x);
existence
proof
consider F be Finite_Sep_Sequence of S,
         a be FinSequence of ExtREAL such that
          A2: F,a are_Re-presentation_of f and
          A3: a.1 =0. and
          A4: for n be Nat st 2 <= n & n in dom a holds
              0. <' a.n & a.n <' +infty by A1,Th14;
deffunc G(set) = (a.$1)*((M*F).$1);
defpred P[set] means $1 in dom F;
A5:  for x be set st P[x] holds G(x) in ExtREAL;
consider p be PartFunc of NAT,ExtREAL such that
 A6: (for x be set holds x in dom p iff x in NAT & P[x]) &
       (for x be set st x in dom p holds p.x = G(x))
           from PARTFUN1:sch 3(A5);
  for x be set st x in dom p holds x in dom F by A6; then
  A7: dom p c= dom F by TARSKI:def 3;
      for x be set st x in dom F holds x in dom p
      proof
        let x be set;
        assume A8:x in dom F;
        dom F c= NAT by RELSET_1:12;
        hence thesis by A6,A8;
      end; then
      dom F c= dom p by TARSKI:def 3; then
        A9: dom p = dom F by A7,XBOOLE_0:def 10;
  then dom p = Seg len F by FINSEQ_1:def 3;
  then p is FinSequence & rng p c= ExtREAL by FINSEQ_1:def 2,RELSET_1:12;
  then
  reconsider p as FinSequence of ExtREAL by FINSEQ_1:def 4;
  A10: for n be Nat st n in dom p holds p.n=a.n*(M*F).n by A6;
take Sum(p);
thus thesis by A2,A3,A4,A9,A10;
end;
uniqueness
proof
let s1,s2 be Element of ExtREAL such that
  A11: ex F1 be Finite_Sep_Sequence of S,
      a1, x1 be FinSequence of ExtREAL
      st F1,a1 are_Re-presentation_of f
         & a1.1 =0.
         & (for n be Nat st 2 <= n & n in dom a1
           holds  0. <' a1.n & a1.n <' +infty)
         & dom x1 = dom F1
         & (for n be Nat st n in dom x1 holds x1.n=a1.n*(M*F1).n)
         & s1=Sum(x1) and
  A12:
  ex  F2 be Finite_Sep_Sequence of S,
       a2 be FinSequence of ExtREAL,
       x2 be FinSequence of ExtREAL
       st F2,a2 are_Re-presentation_of f
         &  a2.1 =0.
         &  (for n be Nat st 2 <= n & n in dom a2
              holds  0. <' a2.n & a2.n <' +infty)
         &  dom x2 = dom F2
         &  (for n be Nat st n in dom x2 holds x2.n=a2.n*(M*F2).n)
         &  s2=Sum(x2);
   thus s1=s2
   proof
      consider F1 be Finite_Sep_Sequence of S,
      a1 be FinSequence of ExtREAL,
      x1 be FinSequence of ExtREAL such that
      A13:    F1,a1 are_Re-presentation_of f and
      A14:    a1.1 =0. and
      A15:  (for n be Nat st 2 <= n & n in dom a1
                         holds  0. <' a1.n & a1.n <' +infty) and
      A16: dom x1 = dom F1 and
      A17: (for n be Nat st n in dom x1 holds x1.n=a1.n*(M*F1).n) and
      A18: s1=Sum(x1) by A11;
      A19: dom F1 = dom a1 by A13,Def1;
      consider F2 be Finite_Sep_Sequence of S,
      a2 be FinSequence of ExtREAL,
      x2 be FinSequence of ExtREAL such that
      A20:    F2,a2 are_Re-presentation_of f and
      A21:    a2.1 =0. and
      A22:  (for n be Nat st 2 <= n & n in dom a2
                   holds  0. <' a2.n & a2.n <' +infty) and
      A23: dom x2 = dom F2 and
      A24: (for n be Nat st n in dom x2 holds x2.n=a2.n*(M*F2).n) and
      A25: s2=Sum(x2) by A12;
      A26: dom F2 = dom a2 by A20,Def1;
      A27:M is nonnegative & M.{} = 0. by MEASURE1:def 11;
      A28:for n be Nat st n in dom F1 holds 0. <=' (M*F1).n
          proof
          let n be Nat;
            assume A29: n in dom F1; then
            A30: (M*F1).n=M.(F1.n) by FUNCT_1:23;
            A31: rng F1 c= S by FINSEQ_1:def 4;
            F1.n in rng F1 by A29,FUNCT_1:12;
            hence thesis by A27,A30,A31,SUPINF_2:58;
          end;
      A32:for n be Nat st n in dom F2 holds 0. <=' (M*F2).n
          proof
          let n be Nat;
            assume A33: n in dom F2; then
            A34: (M*F2).n=M.(F2.n) by FUNCT_1:23;
            A35: rng F2 c= S by FINSEQ_1:def 4;
            F2.n in rng F2 by A33,FUNCT_1:12;
            hence thesis by A27,A34,A35,SUPINF_2:58;
          end;
        A36:for n be Nat st n in dom a1 holds 0. <=' a1.n
          proof
          let n be Nat;
            assume A37: n in dom a1;
            now per cases;
             case n=1;
             thus a1.1=0. by A14;
             end;
             case A38:n<>1;
             n in Seg len a1 by A37,FINSEQ_1:def 3;
             then 1 <= n by FINSEQ_1:3;
             then 1 < n by A38,REAL_1:def 5;
             then 1+1 < n+1 by REAL_1:67;
             then 2<=n by NAT_1:38;
             hence 0. <=' a1.n by A15,A37;
             end;
            end;
            hence thesis;
            end;
      A39: for n be Nat st n in dom a2 holds 0. <=' a2.n
          proof
          let n be Nat;
            assume A40: n in dom a2;
            now per cases;
             case n=1;
             thus a2.1=0. by A21;
             end;
             case A41:n<>1;
             n in Seg len a2 by A40,FINSEQ_1:def 3;
             then 1 <=n by FINSEQ_1:3;
             then 1 < n by A41,REAL_1:def 5;
             then 1+1 < n+1 by REAL_1:67;
             then 2<=n by NAT_1:38;
              hence 0. <=' a2.n by A22,A40;
             end;
            end;
            hence thesis;
            end;
        A42:for n be Nat st n in dom x1 holds 0. <=' x1.n
          proof
          let n be Nat;
            assume A43: n in dom x1; then
            A44: 0. <=' a1.n by A16,A19,A36;
            A45: 0. <=' (M*F1).n by A16,A28,A43;
            0. <=' (a1.n)*( (M*F1).n) by A44,A45,EXTREAL2:45;
            hence thesis by A17,A43;
          end;
      A46:for n be Nat st n in dom x2 holds 0. <=' x2.n
          proof
          let n be Nat;
            assume A47: n in dom x2; then
            A48: 0. <=' a2.n by A23,A26,A39;
            A49: 0. <=' (M*F2).n by A23,A32,A47;
            0. <=' (a2.n)*( (M*F2).n) by A48,A49,EXTREAL2:45;
            hence thesis by A24,A47;
          end;
   now per cases;
      case A50: ex i,j be Nat st
                     i in dom F1 & j in dom F2
                     &2<= i & 2<=j  & M.(F1.i /\ F2.j)=+infty;
            consider i,j be Nat such that
            A51: i in dom F1 & j in dom F2
                   &2<= i & 2<=j & M.(F1.i /\ F2.j)=+infty by A50;
                A52: rng F1 c= S by FINSEQ_1:def 4;
             A53:F1.i in rng F1 by A51,FUNCT_1:12; then
              A54:  F1.i is_measurable M by A52,MEASURE1:def 12;
                 A55: rng F2 c= S by FINSEQ_1:def 4;
              A56:   F2.j in rng F2 by A51,FUNCT_1:12; then
              A57:  F2.j is_measurable M by A55,MEASURE1:def 12;
               F1.i /\ F2.j is_measurable M by A54,A57,MEASURE1:66; then
              A58:  F1.i /\ F2.j in S by MEASURE1:def 12;
              A59:  F1.i /\ F2.j c= F1.i by XBOOLE_1:17;
              A60:  F1.i /\ F2.j c= F2.j by XBOOLE_1:17;
               +infty <=' M.(F1.i) by A51,A52,A53,A58,A59,MEASURE1:62; then
              A61:M.(F1.i)= +infty by SUPINF_1:24;
               +infty <=' M.(F2.j) by A51,A55,A56,A58,A60,MEASURE1:62; then
              A62:M.(F2.j)= +infty by SUPINF_1:24;
              A63: (M*F1).i = +infty by A51,A61,FUNCT_1:23;
                0. <' a1.i by A15,A19,A51; then
              +infty = ( a1.i)*((M*F1).i) by A63,EXTREAL1:def 1; then
               x1.i=+infty by A16,A17,A51; then
             A64:  Sum(x1)=+infty by A16,A42,A51,Th17;
              A65: (M*F2).j = +infty by A51,A62,FUNCT_1:23;
                0. <' a2.j by A22,A26,A51; then
              +infty = (a2.j)*((M*F2).j) by A65,EXTREAL1:def 1; then
               x2.j=+infty by A23,A24,A51;
           hence s1=s2 by A18,A23,A25,A46,A51,A64,Th17;
      end;
     case A66: for i,j be Nat
      st  i in dom F1 & j in dom F2 & 2<= i & 2<=j  holds
      M.(F1.i /\ F2.j) <> +infty;
        set n= len x1;
        set m= len x2;
        ex a be Function of [:(Seg n),(Seg m) :],REAL
         st for i,j be Nat st i in Seg n & j in Seg m
         holds a.[i,j] = a1.i * (M.(F1.i /\ F2.j))
         proof
         deffunc F(set,set) = a1.$1 * (M.(F1.$1 /\ F2.$2));
         A67: for x,y be set st x in Seg n & y in Seg m
          holds F(x,y) in REAL
  proof
  let x,y be set such that
  A68: x in (Seg n)  & y in (Seg m);
     x in { k where k is Nat : 1 <= k & k <= n } by A68,FINSEQ_1:def 1;
     then consider kx be Nat such that
    A69: x=kx &  ( 1 <= kx & kx <= n );
    y in { k where k is Nat : 1 <= k & k <= m } by A68,FINSEQ_1:def 1;
    then consider ky be Nat such that
    A70: y=ky & ( 1 <= ky & ky <= m );
    A71: kx in dom F1 by A16,A68,A69,FINSEQ_1:def 3;
    A72: ky in dom F2 by A23,A68,A70,FINSEQ_1:def 3;
    A73: rng F1 c= S by FINSEQ_1:def 4;
    F1.kx in rng F1 by A71,FUNCT_1:12; then
    A74:  F1.kx is_measurable M by A73,MEASURE1:def 12;
    A75: rng F2 c= S by FINSEQ_1:def 4;
    F2.ky in rng F2 by A72,FUNCT_1:12; then
    A76:  F2.ky is_measurable M by A75,MEASURE1:def 12;
      F1.kx /\ F2.ky is_measurable M by A74,A76,MEASURE1:66; then
    A77:  F1.kx /\ F2.ky in S by MEASURE1:def 12;
    now per cases;
        case A78:  not (2 <= kx & 2 <= ky);
            now per cases by A78;
              case A79: kx < 2;
               kx <=1+1 by A79; then
               A80:kx <= 1 by A79,NAT_1:26;
               F(kx,ky) = 0. * (M.(F1.kx /\ F2.ky)) by A14,A69,A80,AXIOMS:21
               .=0. by EXTREAL1:def 1;
               hence F(kx,ky) in REAL by SUPINF_2:def 1;
               end;
              case A81: ky < 2;
               ky <=1+1 by A81; then
               A82:ky <= 1 by A81,NAT_1:26;
               now per cases;
                 case F1.kx /\ F2.ky = {};
                    hence F(kx,ky) = a1.kx * 0. by MEASURE1:def 11
                                   .=0. by EXTREAL1:def 1;
                 end;
                 case A83:  F1.kx /\ F2.ky <> {};
                   consider x be set such that
                     A84: x in F1.kx /\ F2.ky by A83,XBOOLE_0:def 1;
                     A85: x in F1.kx & x in F2.ky by A84,XBOOLE_0:def 3;
                     a1.kx=f.x by A13,A71,A85,Def1
                            .=a2.ky by A20,A72,A85,Def1
                            .=0. by A21,A70,A82,AXIOMS:21;
                    hence F(kx,ky) = 0. by EXTREAL1:def 1;
                 end;
               end;
               hence F(kx,ky) in REAL by SUPINF_2:def 1;
               end;
            end;
           hence F(kx,ky) in REAL;
        end;
        case A86: 2 <= kx & 2 <= ky;
           A87: M.(F1.kx /\ F2.ky) <> +infty by A66,A71,A72,A86;
           M  is nonnegative by MEASURE1:def 11;
           then 0. <=' M.(F1.kx /\ F2.ky)  by A77,SUPINF_2:58; then
            reconsider r3=M.(F1.kx /\ F2.ky) as Real
              by A87,EXTREAL1:1,SUPINF_2:19;
            a1.kx is Real
            proof
              A88: a1.kx <> +infty by A15,A19,A71,A86;
               0. <=' a1.kx by A19,A36,A71;
           hence a1.kx is Real by A88,EXTREAL1:1,SUPINF_2:19;
            end;
            then reconsider r2=a1.kx as Real;
            a1.kx*M.(F1.kx /\ F2.ky) = r2*r3 by EXTREAL1:13;
           hence F(kx,ky) in REAL;
           end;
      end;
   hence thesis by A69,A70;
 end;
         consider f being Function of [:(Seg n),(Seg m) :],REAL such that
         A89: for x,y be set st x in Seg n & y in Seg m
           holds f.[x,y]=F(x,y) from FUNCT_2:sch 6(A67);
           take f;
           thus thesis by A89;
         end;
    then consider a be Function of [:(Seg n),(Seg m) :],REAL such that
    A90: for i,j be Nat st i in Seg n & j in Seg m
         holds a.[i,j] = a1.i * (M.(F1.i /\ F2.j));
     ex p be FinSequence of REAL st dom p=Seg n
            & for i be Nat st i in dom p
                  holds ex r be FinSequence of REAL st
                        (dom r = Seg m
                         & p.i=Sum(r)
                         & for j be Nat st j in dom(r) holds r.j=a.[i,j] )
   proof
     defpred P[Nat,set] means
     ex r be FinSequence of REAL st
               dom(r) = Seg m & $2=Sum(r)
             & for j be Nat st j in dom(r) holds r.j=a.[$1,j];
     A91: for k be Nat,y1,y2 be set st
          k in Seg n & P[k,y1] & P[k,y2] holds y1=y2
          proof
          let k be Nat,y1,y2 be set such that k in Seg n and
             A92:   P[k,y1] and
             A93:   P[k,y2];
             consider r1 be FinSequence of REAL such that
                     A94: dom(r1) = Seg m and
                     A95: y1=Sum(r1) and
                     A96: for j be Nat st j in dom(r1)
                       holds r1.j=a.[k,j]  by A92;
             consider r2 be FinSequence of REAL such that
                     A97: dom(r2) = Seg m and
                     A98: y2=Sum(r2) and
                     A99: for j be Nat st j in dom(r2)
                       holds r2.j=a.[k,j] by A93;
             now let j be Nat;
                 assume A100: j in dom (r1);
                 hence r1.j = a.[k,j] by A96
                             .= r2.j by A94,A97,A99,A100;
                 end;
               hence thesis by A94,A95,A97,A98,FINSEQ_1:17;
          end;
     A101: for k be Nat st k in Seg n ex x be set st P[k,x]
     proof
     let k be Nat;
      assume k in Seg n;
       deffunc G(set)= a.[k,$1];
        consider r being FinSequence such that
          A102: len r = m and
          A103:  for i be Nat st i in Seg m holds r.i=G(i) from FINSEQ_1:sch 2;
          A104: dom(r) = Seg m by A102,FINSEQ_1:def 3;
          for i be Nat st i in dom r holds r.i in REAL
          proof
            let i be Nat;
            assume i in dom r; then
            A105: i in Seg m by A102,FINSEQ_1:def 3;
            a.[k,i] in REAL;
            hence r.i in REAL by A103,A105;
          end;
          then
          reconsider r as FinSequence of REAL
          by FINSEQ_2:14;
          take x=Sum(r);
          thus thesis by A103,A104;
     end;
     consider p be FinSequence such that
     A106: dom p = Seg n & for k be Nat st k in Seg n holds P[k,p.k]
     from FINSEQ_1:sch 1(A91,A101);
     for i be Nat st i in dom p holds p.i in REAL
     proof
     let i be Nat;
     assume i in dom p; then
     consider r be FinSequence of REAL such that
     A107:   dom(r) = Seg m & p.i=Sum(r)
                  & for j be Nat st j in dom(r) holds r.j=a.[i,j] by A106;
      thus p.i in REAL by A107;
     end;
     then reconsider p as FinSequence of REAL by FINSEQ_2:14;
     take p;
     thus thesis by A106;
   end;
    then consider p be FinSequence of REAL such that
      A108:    dom p=Seg n & for i be Nat st i in dom p
                  holds ex r be FinSequence of REAL st
                        (dom r = Seg m & p.i=Sum r
                         & for j be Nat st j in dom r holds r.j=a.[i,j]);
     ex   q be FinSequence of REAL st dom q=Seg m
           & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st
                       (dom s = Seg n & q.j=Sum s
                        & for i be Nat st i in dom s holds s.i=a.[i,j] )
  proof
   defpred P[Nat,set] means
                 ex s be FinSequence of REAL st
                       dom(s) = Seg n & $2=Sum(s)
                       & for i be Nat st i in dom(s) holds s.i=a.[i,$1];
     A109: for k be Nat,y1,y2 be set st
          k in Seg m & P[k,y1] & P[k,y2] holds y1=y2
               proof
          let k be Nat,y1,y2 be set such that k in Seg m and
             A110:   P[k,y1] and
             A111:   P[k,y2];
             consider s1 be FinSequence of REAL such that
                     A112: dom(s1) = Seg n and
                     A113: y1=Sum(s1) and
                     A114: for j be Nat st j in dom s1
                       holds s1.j=a.[j,k]  by A110;
             consider s2 be FinSequence of REAL such that
                     A115: dom(s2) = Seg n and
                     A116: y2=Sum(s2) and
                     A117: for j be Nat st j in dom(s2)
                       holds s2.j=a.[j,k] by A111;
             now let j be Nat;
                 assume A118: j in dom (s1);
                 hence s1.j = a.[j,k] by A114
                             .= s2.j by A112,A115,A117,A118;
                 end;
               hence thesis by A112,A113,A115,A116,FINSEQ_1:17;
          end;
     A119: for k be Nat st k in Seg m ex x be set st P[k,x]
      proof
          let k be Nat;
           assume  k in Seg m;
            deffunc G(set)= a.[$1,k];
             consider s being FinSequence such that
               A120: len s = n and
               A121:  for i be Nat st i in Seg n holds s.i=G(i)
                       from FINSEQ_1:sch 2;
               A122: dom(s) = Seg n by A120,FINSEQ_1:def 3;
               for i be Nat st i in dom s holds s.i in REAL
               proof
                 let i be Nat;
                 assume i in dom s; then
                 A123: i in Seg n by A120,FINSEQ_1:def 3;
                 a.[i,k] in REAL;
                 hence s.i in REAL by A121,A123;
               end;
               then reconsider s as FinSequence of REAL by FINSEQ_2:14;
               take x=Sum(s);
               thus thesis by A121,A122;
          end;
     consider p be FinSequence such that
     A124: dom p = Seg m & for k be Nat st k in Seg m holds P[k,p.k]
     from FINSEQ_1:sch 1(A109,A119);
     for i be Nat st i in dom p holds p.i in REAL
          proof
          let i be Nat;
          assume i in dom p; then
          consider s be FinSequence of REAL such that
          A125: dom(s) = Seg n & p.i=Sum(s)
                       & for j be Nat st j in dom(s)
                       holds s.j=a.[j,i] by A124;
           thus p.i in REAL by A125;
          end;
     then reconsider p as FinSequence of REAL by FINSEQ_2:14;
     take p;
     thus thesis by A124;
   end;
    then consider q be FinSequence of REAL such that
     A126:  dom q=Seg m & for j be Nat st j in dom q
                 holds ex s be FinSequence of REAL st (dom(s) = Seg n
                        & q.j=Sum(s)
                        & for i be Nat st i in dom(s) holds s.i=a.[i,j]);
     A127: Sum(p)=Sum(q) by A108,A126,Th1;
     p=x1
     proof
       A128: dom p = dom x1 by A108,FINSEQ_1:def 3;
       A129: for k be Nat st k in dom p holds p.k = x1.k
       proof
       let k be Nat;
         assume A130: k in dom p;
         consider r be FinSequence of REAL such that
          A131:dom r = Seg m and
          A132:p.k=Sum r and
          A133:for j be Nat st j in dom r holds r.j=a.[k,j] by A108,A130;
          A134: for j be Nat st j in dom r holds
               r.j=a1.k * (M.(F1.k /\ F2.j))
               proof
                 let j be Nat;
                  assume
                  A135: j in dom r;
                  hence r.j=a.[k,j] by A133
                     .=a1.k * (M.(F1.k /\ F2.j)) by A90,A108,A130,A131,A135;
               end;
           ex F11 be Finite_Sep_Sequence of S st union rng F11 = F1.k &
                  dom F11=dom r &
                  for j be Nat st j in dom r holds F11.j=F1.k /\ F2.j
           proof
           deffunc G(set)=F1.k /\ F2.$1;
           consider F being FinSequence such that
             A136: len F = m and
             A137: for i be Nat st i in Seg m holds F.i=G(i)
             from FINSEQ_1:sch 2;
             A138: dom F = Seg m by A136,FINSEQ_1:def 3;
             for i be Nat st i in dom F holds F.i in S
             proof
                let i be Nat;
                assume A139: i in dom F;
                A140: i in Seg m by A136,A139,FINSEQ_1:def 3;
                 A141: i in dom F2 by A23,A140,FINSEQ_1:def 3;
                 A142: rng F1 c= S by FINSEQ_1:def 4;
                  F1.k in rng F1 by A16,A128,A130,FUNCT_1:12; then
                  A143: F1.k is_measurable M by A142,MEASURE1:def 12;
                  A144: rng F2 c= S by FINSEQ_1:def 4;
                  F2.i in rng F2 by A141,FUNCT_1:12; then
                  A145: F2.i is_measurable M by A144,MEASURE1:def 12;
                    F1.k /\ F2.i is_measurable M by A143,A145,MEASURE1:66;
                  then F1.k /\ F2.i in S by MEASURE1:def 12;
                  hence F.i in S by A137,A138,A139;
             end;
             then reconsider F as FinSequence of S by FINSEQ_2:14;
             A146: dom F = dom F2 by A23,A138,FINSEQ_1:def 3;
             then reconsider F as Finite_Sep_Sequence of S by A137,A138,Th5;
             take F;
             union rng F = F1.k
             proof
                thus union rng F = F1.k /\ union rng F2
                   by A137,A138,A146,Th6
                                .= F1.k /\ dom f by A20,Def1
                                .= F1.k /\ union rng F1 by A13,Def1
                                .= F1.k by A16,A128,A130,Th7;
             end;
             hence thesis by A131,A136,A137,FINSEQ_1:def 3;
            end;
           then consider F11 be Finite_Sep_Sequence of S such that
           A147: union rng F11 = F1.k & dom F11=dom r &
               for j be Nat st j in dom r holds F11.j=F1.k /\ F2.j;
           A148: Sum(M*F11) = M.((F1.k)) by A147,Th9;
           reconsider rr=r as FinSequence of ExtREAL by Th11;
           A149: for j be Nat st j in dom rr holds rr.j=a1.k * (M*F11).j
           proof
             let j be Nat;
              assume
              A150:j in dom rr;
              thus rr.j = a1.k * (M.(F1.k /\ F2.j)) by A134,A150
                   .=a1.k *(M.(F11.j)) by A147,A150
                   .=a1.k *(M*F11).j  by A147,A150,FUNCT_1:23;
          end;
          k in Seg len a1 by A16,A19,A128,A130,FINSEQ_1:def 3; then
A151:      1 <= k by FINSEQ_1:3;
A152:       a1.k <> -infty & a1.k <> +infty
          proof
           now per cases;
            case k = 1;
              hence thesis by A14,SUPINF_2:19;
            end;
            case k <> 1; then
              1 < k by A151,REAL_1:def 5; then
              1+1 <= k by NAT_1:38;
              hence thesis by A15,A16,A19,A128,A130,SUPINF_2:19;
            end;
           end;
           hence thesis;
          end;
          dom rr = dom (M*F11) by A147,Th8;
           then Sum rr=(a1.k)*Sum(M*F11) by A149,A152,Th10; then
            Sum r=(a1.k)*M.((F1.k)) by A148,Th2
                         .=a1.k*((M*F1).k) by A16,A128,A130,FUNCT_1:23;
           hence thesis by A17,A128,A130,A132;
       end;
       thus thesis by A128,A129,FINSEQ_1:17;
     end; then
     A153: Sum p = Sum x1 by Th2;
     q=x2
     proof
       A154: dom q = dom x2 by A126,FINSEQ_1:def 3;
       A155: for k be Nat st k in dom q holds q.k = x2.k
       proof
       let k be Nat;
         assume A156: k in dom q;
         consider s be FinSequence of REAL such that
          A157:dom s = Seg n and
          A158:q.k=Sum s and
          A159:for i be Nat st i in dom s holds s.i=a.[i,k] by A126,A156;
          A160: for i be Nat st i in dom s holds
               s.i=a1.i * (M.(F1.i /\ F2.k))
               proof
                 let i be Nat;
                  assume
                  A161: i in dom s;
                  thus s.i=a.[i,k] by A159,A161
                     .=a1.i * (M.(F1.i /\ F2.k)) by A90,A126,A156,A157,A161;
               end;
          A162: for i be Nat st i in dom s holds
               s.i=a2.k * (M.(F1.i /\ F2.k))
               proof
                 let i be Nat;
                  assume
                  A163: i in dom s;
                   now per cases;
                      case A164: F1.i /\ F2.k = {};
                         M.(F1.i /\ F2.k)=0. by A164,MEASURE1:def 11;
                         hence s.i = a1.i * 0. by A160,A163
                                .=0. by EXTREAL1:def 1
                                .=a2.k *0. by EXTREAL1:def 1
                                .=a2.k *M.(F1.i /\ F2.k) by A164,
MEASURE1:def 11;
                      end;
                      case A165: F1.i /\ F2.k <> {};
                        consider x be set such that
                          A166: x in F1.i /\ F2.k by A165,XBOOLE_0:def 1;
                          A167: x in F1.i & x in F2.k by A166,XBOOLE_0:def 3;
                          dom p = dom x1 by A108,FINSEQ_1:def 3; then
                          a1.i=f.x by A13,A16,A108,A157,A163,A167,Def1
                             .=a2.k by A20,A23,A154,A156,A167,Def1;
                         hence s.i = a2.k*M.(F1.i /\ F2.k) by A160,A163;
                      end;
                    end;
                 hence thesis;
               end;
           ex F21 be Finite_Sep_Sequence of S st union rng F21 = F2.k &
                  dom F21=dom(s) &
                  for j be Nat st j in dom(s) holds F21.j=F1.j /\ F2.k
           proof
           deffunc G(set)= F2.k /\ F1.$1;
           consider F being FinSequence such that
             A168: len F = n and
             A169: for i be Nat st i in Seg n holds F.i=G(i)
             from FINSEQ_1:sch 2;
             A170: dom F = Seg n by A168,FINSEQ_1:def 3;
             for i be Nat st i in dom F holds F.i in S
             proof
                let i be Nat;
                assume A171: i in dom F; then
                A172: i in Seg n by A168,FINSEQ_1:def 3;
                 A173: i in dom F1 by A16,A172,FINSEQ_1:def 3;
                 A174: rng F1 c= S by FINSEQ_1:def 4;
                  F1.i in rng F1 by A173,FUNCT_1:12; then
                  A175: F1.i is_measurable M by A174,MEASURE1:def 12;
                  A176: rng F2 c= S by FINSEQ_1:def 4;
                  F2.k in rng F2 by A23,A154,A156,FUNCT_1:12; then
                  A177: F2.k is_measurable M by A176,MEASURE1:def 12;
                    F1.i /\ F2.k is_measurable M by A175,A177,MEASURE1:66; then
                    F1.i /\ F2.k in S by MEASURE1:def 12;
                  hence F.i in S by A169,A170,A171;
             end;
             then reconsider F as FinSequence of S by FINSEQ_2:14;
             A178: dom F = dom F1 by A16,A170,FINSEQ_1:def 3;
             then reconsider F as Finite_Sep_Sequence of S by A169,A170,Th5;
             take F;
             union rng F = F2.k
             proof
               thus union rng F = F2.k /\ union rng F1
                 by A169,A170,A178,Th6
                               .= F2.k /\ dom f by A13,Def1
                               .= F2.k /\ union rng F2 by A20,Def1
                               .= F2.k by A23,A154,A156,Th7;
             end;
             hence thesis by A157,A168,A169,FINSEQ_1:def 3;
            end;
           then
           consider F21 be Finite_Sep_Sequence of S such that
       A179: union rng F21 = F2.k & dom F21 = dom s &
                  for i be Nat st i in dom s holds F21.i=F1.i /\ F2.k;
            A180: Sum(M*F21) = M.((F2.k)) by A179,Th9;
            reconsider ss=s as FinSequence of ExtREAL by Th11;
            A181: for j be Nat st j in dom ss holds ss.j=a2.k * (M*F21).j
            proof
              let j be Nat;
              assume
          A182:j in dom ss;
              thus ss.j = a2.k * (M.(F1.j /\ F2.k)) by A162,A182
                      .=a2.k *(M.(F21.j)) by A179,A182
                      .=a2.k *(M*F21).j by A179,A182,FUNCT_1:23;
           end;
           k in Seg len a2 by A23,A26,A154,A156,FINSEQ_1:def 3; then
A183:       1 <= k by FINSEQ_1:3;
A184:        a2.k <> -infty & a2.k <> +infty
           proof
            per cases;
             suppose k = 1;
               hence thesis by A21,SUPINF_2:19;
             end;
             suppose k <> 1; then
               1 < k by A183,REAL_1:def 5; then
               1+1 <= k by NAT_1:38;
               hence thesis by A22,A23,A26,A154,A156,SUPINF_2:19;
             end;
           end;
           dom ss=dom (M*F21) by A179,Th8;
           then Sum(ss)=(a2.k)*Sum(M*F21) by A181,A184,Th10;
           then Sum(s)=(a2.k)*M.((F2.k)) by A180,Th2
                         .=a2.k*((M*F2).k)
                         by A23,A154,A156,FUNCT_1:23;
           hence thesis by A24,A154,A156,A158;
       end;
       thus thesis by A154,A155,FINSEQ_1:17;
      end;
     hence s1=s2 by A18,A25,A127,A153,Th2;
      end;
    end;
  hence thesis;
  end;
 end;
end;

begin :: Additional Lemmas

theorem :: RVSUM_1:110
for a be FinSequence of ExtREAL, p,N be Element of ExtREAL st
 N = len a &
 (for n be Nat st n in dom a holds a.n = p) holds
   Sum(a) = N * p by Lm1;
