:: More on the Continuity of Real Functions
::  by Keiko Narita , Artur Kornilowicz and Yasunari Shidama
:: 
:: Received February 22, 2011
:: Copyright (c) 2011 Association of Mizar Users
::           (Stowarzyszenie Uzytkownikow Mizara, Bialystok, Poland).
:: This code can be distributed under the GNU General Public Licence
:: version 3.0 or later, or the Creative Commons Attribution-ShareAlike
:: License version 3.0 or later, subject to the binding interpretation
:: detailed in file COPYING.interpretation.
:: See COPYING.GPL and COPYING.CC-BY-SA for the full text of these
:: licenses, or see http://www.gnu.org/licenses/gpl.html and
:: http://creativecommons.org/licenses/by-sa/3.0/.

environ

 vocabularies NUMBERS, SUBSET_1, XREAL_0, ORDINAL1, SEQ_1, PARTFUN1, RELAT_1,
      TARSKI, SEQ_2, ORDINAL2, FUNCT_2, FUNCT_1, XBOOLE_0, XXREAL_0, NAT_1,
      ARYTM_3, CARD_1, COMPLEX1, ARYTM_1, REAL_1, RCOMP_1, VALUED_1, ZFMISC_1,
      XXREAL_2, FCONT_1, NORMSP_1, STRUCT_0, PRE_TOPC, FINSEQ_1, CARD_3,
      FINSET_1, MEMBERED, BORSUK_1, REAL_NS1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1,
      RELSET_1, PARTFUN1, FUNCT_2, FINSET_1, NUMBERS, XXREAL_0, XREAL_0,
      VALUED_0, VALUED_1, REAL_1, NAT_1, MEMBERED, COMPLEX1, BINOP_2, XXREAL_2,
      FINSEQ_1, SEQ_1, SEQ_2, RCOMP_1, FCONT_1, STRUCT_0, PRE_TOPC, RLVECT_1,
      NORMSP_0, NORMSP_1, EUCLID, NFCONT_1, REAL_NS1, PDIFF_1, INTEGR15,
      VALUED_2, NFCONT_3, VFUNCT_1;
 constructors REAL_1, COMPLEX1, SEQ_2, SEQ_4, SEQ_1, RELSET_1, FCONT_1,
      NFCONT_1, VFUNCT_1, BINOP_2, PDIFF_1, INTEGR15, NFCONT_3, VALUED_2;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      MEMBERED, VALUED_0, FUNCT_2, XXREAL_2, FUNCT_1, ZFMISC_1, STRUCT_0,
      EUCLID, FINSEQ_1, REAL_NS1, NFCONT_3, VALUED_2;
 requirements REAL, NUMERALS, SUBSET, BOOLE;
 definitions VALUED_1, FCONT_1, NFCONT_3, INTEGR15, TARSKI;
 theorems TARSKI, NAT_1, FUNCT_1, FUNCT_2, PARTFUN1, XREAL_0, PARTFUN2,
      RELAT_1, RELSET_1, XBOOLE_0, XBOOLE_1, XCMPLX_1, XREAL_1, XXREAL_0,
      ORDINAL1, XXREAL_2, VALUED_0, FCONT_1, NFCONT_1, NORMSP_0, FINSEQ_1,
      FINSET_1, VFUNCT_1, REAL_NS1, SUBSET_1, PDIFF_8, NFCONT_3, VALUED_2;
 schemes FUNCT_1, FINSEQ_1, PARTFUN2;

begin :: Basic Properties of the continuous functions of PartFunc of REAL,REAL n

reserve n,m,i,k for Element of NAT;
reserve x,X,X1 for set;
reserve r,p for Real;
reserve s,x0,x1,x2 for real number;
reserve f,f1,f2 for PartFunc of REAL,REAL n;
reserve h for PartFunc of REAL, the carrier of REAL-NS n;

definition let n,f,x0;
  pred f is_continuous_in x0 means :Def1:
  ex g be PartFunc of REAL,the carrier of REAL-NS n st
    f=g & g is_continuous_in x0;
end;

theorem Def1Th:
  h = f implies (f is_continuous_in x0 iff h is_continuous_in x0)
proof
  assume AS: h = f;
  hereby assume f is_continuous_in x0;
    then ex g be PartFunc of REAL,the carrier of REAL-NS n st
    f=g & g is_continuous_in x0 by Def1;
    hence h is_continuous_in x0 by AS;
  end;
  thus thesis by AS,Def1;
end;

theorem Th1:
  x0 in X & f is_continuous_in x0 implies f|X is_continuous_in x0
  proof
    assume that
A1: x0 in X and
A2: f is_continuous_in x0;
    consider g be PartFunc of REAL,the carrier of REAL-NS n such that
A3: f=g & g is_continuous_in x0 by A2,Def1;
    g|X is_continuous_in x0 by A1,A3,NFCONT_3:6;
    hence thesis by A3,Def1;
  end;

theorem Th3:
  f is_continuous_in x0 iff x0 in dom f &
  for r st 0<r ex s st 0<s & for x1 st x1 in dom f & abs(x1-x0)<s
  holds |. f/.x1 - f/.x0 .|<r
proof
  hereby assume f is_continuous_in x0;
    then consider g be PartFunc of REAL,the carrier of REAL-NS n
     such that
A2: f=g & g is_continuous_in x0 by Def1;
    thus x0 in dom f by A2,NFCONT_3:8;
    thus
    for r st 0<r ex s st 0<s & for x1 st x1
    in dom f & abs(x1-x0)<s holds |. f/.x1 - f/.x0 .|<r
    proof
      let r;
      assume 0<r;
      then consider s such that
A5:   0<s & for x1 st x1
      in dom g & abs(x1-x0)<s holds ||. g/.x1 - g/.x0 .||<r by NFCONT_3:8,A2;
      take s;
      thus 0 < s by A5;
      let x1;
      assume x1 in dom f & abs(x1-x0)<s; then
A6:   ||. g/.x1 - g/.x0 .|| < r by A2,A5;
      g/.x1 = f/.x1 & g/.x0 = f/.x0 by A2,REAL_NS1:def 4;
      hence |. f/.x1 - f/.x0 .|<r by A6,REAL_NS1:1,5;
    end;
  end;
  assume
A1:x0 in dom f &
  for r st 0<r ex s st 0<s & for x1 st x1
    in dom f & abs(x1-x0)<s holds |. f/.x1 - f/.x0 .|<r;
  reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
  now let r;
    assume 0<r;
    then consider s such that
A2: 0<s and
A3: for x1 st x1 in dom f & abs(x1-x0)<s holds |. f/.x1 - f/.x0 .|<r by A1;
    take s;
    thus 0<s by A2;
    let x1;
    assume x1 in dom g & abs(x1-x0)<s; then
A4: |. f/.x1 - f/.x0 .|<r by A3;
    g/.x1 = f/.x1 & g/.x0 = f/.x0 by REAL_NS1:def 4;
    hence ||. g/.x1 - g/.x0 .||<r by A4,REAL_NS1:1,5;
  end;
  then g is_continuous_in x0 by A1,NFCONT_3:8;
  hence thesis by Def1;
end;

theorem LM002:
 for r be Real, z be Element of REAL n,w be Point of REAL-NS n
  st z=w holds
 {y where y is Element of REAL n: |.y-z.| < r} =
 {y where y is Point of REAL-NS n: ||.y-w.|| < r}
proof
  let r be Real, z be Element of REAL n,w be Point of REAL-NS n;
  assume AS: z=w;
  set N1={y where y is Element of REAL n: |.y-z.| < r};
  set N2={y where y is Point of REAL-NS n: ||.y-w.|| < r};
A3: N1 c= N2
  proof let x be set;
    assume x in N1;
    then consider y be Element of REAL n such that
A1:   x=y & |.y-z.| < r;
    reconsider x1=x as Element of REAL n by A1;
    reconsider x2=x1 as Point of REAL-NS n by REAL_NS1:def 4;
    ||.x2-w.|| < r by AS,A1,REAL_NS1:1,5;
    hence x in N2;
  end;
  N2 c= N1
  proof let x be set;
    assume x in N2;
    then consider y be Point of REAL-NS n such that
A4:   x=y & ||.y-w.|| < r;
    reconsider x3=x as Point of REAL-NS n by A4;
    reconsider x4=x3 as Element of REAL n by REAL_NS1:def 4;
    |.x4-z.| < r by AS,A4,REAL_NS1:1,5;
    hence x in N1;
  end;
  hence N1 = N2 by A3,XBOOLE_0:def 10;
end;

definition
  let n be Element of NAT;
  let Z be set;
  let f be PartFunc of Z,REAL n;
  deffunc F(set) = |. f/.$1 .|;
  func |.f.| -> PartFunc of Z,REAL means :Def4:
  dom it = dom f & for x be set st x in dom it holds it/.x = |. f/.x .|;
 existence
 proof
   consider g being Function such that
P3: dom g = dom f & for x be set st x in dom f holds g.x = F(x)
    from FUNCT_1:sch 3;
  now let y be set;
   assume y in rng g;
   then consider x be set such that
   P41: x in dom g & y=g.x by FUNCT_1:def 5;
   g.x = F(x) by P3,P41;
   hence y in REAL by P41;
  end;
  then rng g c= REAL by TARSKI:def 3;
  then g in PFuncs(Z,REAL) by P3,PARTFUN1:def 5;
  then reconsider g as PartFunc of Z,REAL by PARTFUN1:120;
  take g;
  thus dom g = dom f by P3;
  let x be set;
  assume P6: x in dom g;
  then g.x = F(x) by P3;
  hence thesis by PARTFUN1:def 8,P6;
end;
uniqueness
proof
  let g1,g2 be PartFunc of Z,REAL such that
A1:dom g1 = dom f & for x be set st x in dom g1 holds g1/.x = F(x) and
A2:dom g2 = dom f & for x be set st x in dom g2 holds g2/.x = F(x);
  now let x be Element of Z;
    assume P10:x in dom g1; then
P12: g1/.x = |. f/.x .| by A1;
    g1/.x= g2/.x by A2,P12,A1,P10;
    then g1.x= g2/.x by P10,PARTFUN1:def 8;
    hence g1.x= g2.x by A1,A2,P10,PARTFUN1:def 8;
  end;
  hence g1=g2 by PARTFUN1:34,A1,A2;
end;
end;

definition
  let n be Element of NAT;
  let Z be non empty set;
  let f be PartFunc of Z,REAL n;
  deffunc G(set) = - f/.$1;
  defpred P[set] means $1 in dom f;
  func -f -> PartFunc of Z, REAL n means  :Def5:
  dom it = dom f & for c be set st c in dom it holds it/.c = - (f/.c);
  existence
  proof
    consider F being PartFunc of Z,REAL n such that
A3: for c be Element of Z holds c in dom F iff P[c] and
A4: for c be Element of Z st c in dom F holds F/.c = G(c) from PARTFUN2:sch 2;
    take F;
    thus thesis by A3,A4,SUBSET_1:8;
  end;
  uniqueness
  proof
    let g1,g2 be PartFunc of Z,REAL n such that
A1: dom g1 = dom f & for x be set st x in dom g1 holds g1/.x = -f/.x and
A2: dom g2 = dom f & for x be set st x in dom g2 holds g2/.x = -f/.x;
    now let x be Element of Z;
     assume P10:x in dom g1; then
P12: g1/.x = - f/.x by A1;
     g1/.x= g2/.x by A2,P12,A1,P10;
     then g1.x= g2/.x by P10,PARTFUN1:def 8;
     hence g1.x= g2.x by A1,A2,P10,PARTFUN1:def 8;
   end;
   hence g1=g2 by PARTFUN1:34,A1,A2;
 end;
end;

theorem LM003A:
  for f1,f2 be PartFunc of REAL,the carrier of REAL-NS n,
      g1,g2 be PartFunc of REAL,REAL n st f1=g1 & f2=g2 holds
    f1+f2 = g1+g2
proof
  let f1,f2 be PartFunc of REAL,the carrier of REAL-NS n,
      g1,g2 be PartFunc of REAL,REAL n;
  assume AS: f1=g1 & f2=g2;
C1: dom(f1+f2) = dom f1 /\ dom f2 by VFUNCT_1:def 1; then
A2: dom(f1+f2) = dom(g1+g2) by AS,VALUED_2:def 45;
B1: now
    let x be Element of REAL;
    assume A3: x in dom(f1+f2); then
V:  x in dom g1 & x in dom g2 by C1,AS,XBOOLE_0:def 4;
W:  f1/.x=g1/.x & f2/.x=g2/.x by AS,REAL_NS1:def 4;
    g1/.x = g1.x & g2/.x = g2.x by V,PARTFUN1:def 8; then
R:  f1/.x + f2/.x = g1.x + g2.x by REAL_NS1:2,W;
    (f1+f2)/.x = f1/.x + f2/.x by A3,VFUNCT_1:def 1; then
    (f1+f2).x = f1/.x + f2/.x by PARTFUN1:def 8,A3;
    hence (f1+f2).x = (g1+g2).x by R,A2,A3,VALUED_2:def 45;
  end;
  f1+f2 is PartFunc of REAL,REAL n by REAL_NS1:def 4;
  hence thesis by A2,B1,PARTFUN1:34;
end;

theorem LM003B:
  for f1 be PartFunc of REAL,the carrier of REAL-NS n,
      g1 be PartFunc of REAL,REAL n,
      a be Real st f1=g1 holds
    a(#)f1 = a(#)g1
proof
  let f1 be PartFunc of REAL,the carrier of REAL-NS n,
      g1 be PartFunc of REAL,REAL n,
      a be Real;
  assume AS: f1=g1;
S:  dom(a(#)f1) = dom f1 by VFUNCT_1:def 4; then
A2: dom(a(#)f1) = dom(a(#)g1) by AS,VALUED_2:def 39;
B1: now
    let x be Element of REAL;
    assume A3: x in dom(a(#)f1); then
B:  g1.x = g1/.x by PARTFUN1:def 8,AS,S;
    f1/.x=g1/.x by AS,REAL_NS1:def 4; then
A5: a*(f1/.x)=a*(g1/.x) by REAL_NS1:3;
A6: (a(#)f1)/.x = a*(f1/.x) by A3,VFUNCT_1:def 4;
    (a(#)g1).x = a(#)(g1.x) by A2,A3,VALUED_2:def 39;
    hence (a(#)f1).x = (a(#)g1).x by A3,A5,A6,PARTFUN1:def 8,B;
  end;
  a(#)f1 is PartFunc of REAL,REAL n by REAL_NS1:def 4;
  hence thesis by A2,B1,PARTFUN1:34;
end;

theorem
  for f1 be PartFunc of REAL, REAL n holds (-1)(#)f1 = -f1
proof
  let f1 be PartFunc of REAL, REAL n;
F:  dom((-1)(#)f1) = dom f1 by VALUED_2:def 39; then
A1: dom((-1)(#)f1) = dom(-f1) by Def5;
  now let x be Element of REAL;
    assume A2: x in dom((-1)(#)f1);
G:  f1.x = f1/.x by F,PARTFUN1:def 8,A2;
A5: (-f1)/.x = -(f1/.x) by A1,A2,Def5;
    (f1[#](-1)).x = (-1)(#)(f1.x) by A2,VALUED_2:def 39;
    hence ((-1)(#)f1).x = (-f1).x by PARTFUN1:def 8,G,A1,A2,A5;
  end;
  hence thesis by A1,PARTFUN1:34;
end;

theorem LM003E:
  for f1 be PartFunc of REAL,the carrier of REAL-NS n,
      g1 be PartFunc of REAL,REAL n st f1=g1 holds
    -f1 = -g1
proof
  let f1 be PartFunc of REAL,the carrier of REAL-NS n,
      g1 be PartFunc of REAL,REAL n;
  assume AS: f1=g1;
  dom(-f1) = dom f1 by VFUNCT_1:def 6; then
A2: dom(-f1) = dom(-g1) by AS,Def5;
B1: now
    let x be Element of REAL;
    assume A3: x in dom(-f1);
    f1/.x=g1/.x by AS,REAL_NS1:def 4; then
A5: -(f1/.x)=-(g1/.x) by REAL_NS1:4;
A6: (-f1)/.x = -(f1/.x) by A3,VFUNCT_1:def 6;
    (-g1)/.x = -(g1/.x) by A2,A3,Def5;
    then (-f1).x = (-g1)/.x by A3,A5,A6,PARTFUN1:def 8;
    hence (-f1).x = (-g1).x by A2,A3,PARTFUN1:def 8;
  end;
  -f1 is PartFunc of REAL,REAL n by REAL_NS1:def 4;
  hence -f1 = -g1 by A2,B1,PARTFUN1:34;
end;

theorem LM003F:
 for f1 be PartFunc of REAL,the carrier of REAL-NS n,
     g1 be PartFunc of REAL,REAL n st f1=g1 holds
  ||. f1 .|| = |. g1 .|
proof
  let f1 be PartFunc of REAL,the carrier of REAL-NS n,
      g1 be PartFunc of REAL,REAL n;
  assume AS: f1=g1;
  dom (||. f1 .||) = dom f1 by NORMSP_0:def 3; then
A2: dom (||. f1 .||) = dom(|. g1 .|) by AS,Def4;
  now
    let x be Element of REAL;
    assume A3: x in dom ||. f1 .||;
A4: f1/.x=g1/.x by AS,REAL_NS1:def 4;
    set y1=g1/.x;
A5: ||. f1/.x .||=|. y1 .| by A4,REAL_NS1:1;
A6: (||. f1 .||).x = ||. f1/.x .|| by A3,NORMSP_0:def 3;
    |. g1 .| /.x = |. g1/.x .| by A2,A3,Def4;
    hence (||. f1 .||).x = (|. g1 .|).x by A2,A3,A5,A6,PARTFUN1:def 8;
  end;
  hence thesis by A2,PARTFUN1:34;
end;

theorem LM003D:
 for f1,f2 be PartFunc of REAL,the carrier of REAL-NS n,
     g1,g2 be PartFunc of REAL,REAL n st f1=g1 & f2=g2 holds
  f1-f2 = g1-g2
proof
  let f1,f2 be PartFunc of REAL,the carrier of REAL-NS n,
      g1,g2 be PartFunc of REAL,REAL n;
  assume AS: f1=g1 & f2=g2;
K:  dom(f1-f2) = dom f1 /\ dom f2 by VFUNCT_1:def 2; then
A2: dom(f1-f2) = dom(g1-g2) by AS,VALUED_2:def 46;
B1: now
    let x be Element of REAL;
    assume A3: x in dom(f1-f2);
a:  f1/.x=g1/.x & f2/.x=g2/.x by AS,REAL_NS1:def 4;
    x in dom f1 & x in dom f2 by A3,K,XBOOLE_0:def 4; then
    g1.x = g1/.x & g2/.x = g2.x by AS,PARTFUN1:def 8; then
A5: f1/.x - f2/.x = g1.x - g2.x by REAL_NS1:5,a;
A6: (f1-f2)/.x = f1/.x - f2/.x by A3,VFUNCT_1:def 2;
    (f1-f2)/.x = (f1-f2).x by A3,PARTFUN1:def 8;
    hence (f1-f2).x = (g1-g2).x by A5,A6,A2,A3,VALUED_2:def 46;
  end;
  f1-f2 is PartFunc of REAL,REAL n by REAL_NS1:def 4;
  hence f1-f2 = g1-g2 by A2,B1,PARTFUN1:34;
end;

theorem
  f is_continuous_in x0 iff
  x0 in dom f & for N1 be Subset of REAL n st ex r be Real st 0 < r &
  {y where y is Element of REAL  n: |.y-f/.x0.| < r} = N1
  ex N being Neighbourhood of x0 st
  for x1 st x1 in dom f & x1 in N holds f/.x1 in N1
proof
  thus f is_continuous_in x0 implies x0 in dom f &
    for N1 be Subset of REAL n st ex r be Real st 0 < r &
    {y where y is Element of REAL n: |.y-f/.x0.| < r} = N1
    ex N being Neighbourhood of x0
    st for x1 st x1 in dom f & x1 in N holds f/.x1 in N1
  proof
    assume f is_continuous_in x0;
    then consider g be PartFunc of REAL,the carrier of REAL-NS n such that
B2:  f=g & g is_continuous_in x0 by Def1;
     thus x0 in dom f by B2,NFCONT_3:9;
     let N01 be Subset of REAL n such that
X2:  ex r be Real st 0 < r &
     {y where y is Element of REAL n: |.y-f/.x0.| < r} = N01;
     consider r such that
A2:  0<r and
A3:  { p where p is Element of REAL n : |. p - f/.x0 .| < r } = N01 by X2;
     f/.x0 = g/.x0 by B2,REAL_NS1:def 4; then
A4:  { p where p is Point of REAL-NS n : ||. p - g/.x0 .|| < r } = N01
       by A3,LM002;
     { p where p is Point of REAL-NS n : ||. p - g/.x0 .|| < r }
     is Neighbourhood of g/.x0 by NFCONT_1:3,A2;
     then consider N being Neighbourhood of x0 such that
A5:  for x1 st x1 in dom g & x1 in N holds g/.x1 in N01 by A4,NFCONT_3:9,B2;
     take N;
     let x1;
     assume x1 in dom f & x1 in N;
     then g/.x1 in N01 by B2,A5;
     hence f/.x1 in N01 by B2,REAL_NS1:def 4;
   end;
   assume
A8: x0 in dom f & for N1 be Subset of REAL n
    st ex r be Real st 0 < r &
    {y where y is Element of REAL n: |.y-f/.x0.| < r} = N1
    ex N being Neighbourhood of x0
    st for x1 st x1 in dom f & x1 in N holds f/.x1 in N1;
    reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
    now let N1 being Neighbourhood of g/.x0;
      consider r be Real such that
A10:  0<r and
A11: { p where p is Point of REAL-NS n : ||. p - g/.x0 .|| < r } c= N1
       by NFCONT_1:def 3;
     set N01 = { p where p is Element of REAL n : |. p - f/.x0 .| < r };
     f/.x0 = g/.x0 by REAL_NS1:def 4; then
A12: { p where p is Element of REAL n : |. p - f/.x0 .| < r }
     = { p where p is Point of REAL-NS n : ||. p - g/.x0 .|| < r } by LM002;
     now let x be set;
       assume x in N01;
       then ex p be Element of REAL n
       st p=x & |. p - f/.x0 .| < r;
       hence x in REAL n;
     end;
     then N01 is Subset of REAL n by TARSKI:def 3;
     then consider N being Neighbourhood of x0 such that
A13: for x1 st x1 in dom f & x1 in N holds f/.x1 in N01 by A8,A10;
     take N;
     let x1;
     assume x1 in dom g & x1 in N;
     then f/.x1 in N01 by A13;
     then g/.x1 in N01 by REAL_NS1:def 4;
     hence g/.x1 in N1 by A11,A12;
   end;
   then g is_continuous_in x0 by A8,NFCONT_3:9;
   hence thesis by Def1;
end;

theorem
  f is_continuous_in x0 iff x0 in dom f &
  for N1 be Subset of REAL n
  st ex r be Real st 0 < r &
  {y where y is Element of REAL n: |.y-f/.x0.| < r} = N1
  ex N being Neighbourhood of x0 st f.:N c= N1
proof
  reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
  hereby assume f is_continuous_in x0; then
P0: g is_continuous_in x0 by Def1Th;
  hence x0 in dom f by NFCONT_3:10;
  thus for N1 be Subset of REAL n st ex r be Real st 0 < r &
    {y where y is Element of REAL n: |.y-f/.x0.| < r} = N1
    ex N being Neighbourhood of x0 st f.:N c= N1
  proof
    let N1 be Subset of REAL n;
    given r be Real such that
P4: 0 < r & {y where y is Element of REAL n: |.y-f/.x0.| < r} = N1;
    f/.x0 = g/.x0 by REAL_NS1:def 4; then
P5: { p where p is Element of REAL n : |. p - f/.x0 .| < r }
      = { p where p is Point of REAL-NS n : ||. p - g/.x0 .|| < r } by LM002;
    N1 is Neighbourhood of g/.x0 by P4,P5,NFCONT_1:3;
    then consider N being Neighbourhood of x0 such that
P6: g.:N c= N1 by NFCONT_3:10,P0;
    take N;
    thus f.:N c= N1 by P6;
  end;
end;
assume AS: x0 in dom f & for N1 be Subset of REAL n
 st ex r be Real st 0 < r &
  {y where y is Element of REAL n: |.y-f/.x0.| < r}
   = N1 ex N being Neighbourhood of x0 st f.:N c= N1;
   now let N1 being Neighbourhood of g/.x0;
consider r be Real such that
A10: 0<r and
A11: { p where p is Point of REAL-NS n : ||. p - g/.x0 .|| < r } c= N1
  by NFCONT_1:def 3;
  set N01={ p where p is Element of REAL n : |. p - f/.x0 .| < r };
f/.x0 = g/.x0 by REAL_NS1:def 4; then
A12: { p where p is Element of REAL n: |. p - f/.x0 .| < r }
= { p where p is Point of REAL-NS n : ||. p - g/.x0 .|| < r } by LM002;
now let x be set;
  assume x in N01;
  then ex p be Element of REAL n st p=x & |. p - f/.x0 .| < r;
  hence x in REAL n;
end;
then N01 is Subset of REAL n by TARSKI:def 3;
then consider N being Neighbourhood of x0 such that
A13: f.:N c= N01 by AS,A10;
take N;
thus g.:N c= N1 by XBOOLE_1:1,A11,A12,A13;
end;
then g is_continuous_in x0 by AS,NFCONT_3:10;
hence thesis by Def1Th;
end;

theorem
  (ex N be Neighbourhood of x0 st dom f /\ N = {x0}) implies
  f is_continuous_in x0
  proof
    given N be Neighbourhood of x0 such that
P1: dom f /\ N = {x0};
    reconsider g = f as PartFunc of REAL,the carrier of REAL-NS n
      by REAL_NS1:def 4;
    g is_continuous_in x0 by P1,NFCONT_3:11;
    hence thesis by Def1Th;
  end;

theorem
  x0 in dom f1 /\ dom f2 & f1 is_continuous_in x0 & f2 is_continuous_in x0
    implies f1+f2 is_continuous_in x0
proof
  assume AS: x0 in dom f1 /\ dom f2 & f1 is_continuous_in x0
     & f2 is_continuous_in x0;
  reconsider g1=f1, g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
P2: g1 is_continuous_in x0 by AS,Def1Th;
    g2 is_continuous_in x0 by AS,Def1Th; then
P3: (g1+g2) is_continuous_in x0 & (g1-g2) is_continuous_in x0
      by AS,P2,NFCONT_3:12;
  g1+g2 = f1+f2 by LM003A;
  hence thesis by P3,Def1Th;
end;

theorem
  x0 in dom f1 /\ dom f2 & f1 is_continuous_in x0 & f2 is_continuous_in x0
  implies f1-f2 is_continuous_in x0
proof
  assume AS: x0 in dom f1 /\ dom f2 & f1 is_continuous_in x0 &
    f2 is_continuous_in x0;
   reconsider g1=f1,g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
     by REAL_NS1:def 4;
P2: g1 is_continuous_in x0 by AS,Def1Th;
   g2 is_continuous_in x0 by AS,Def1Th; then
P3:(g1+g2) is_continuous_in x0 & (g1-g2) is_continuous_in x0
     by AS,P2,NFCONT_3:12;
   g1-g2 = f1-f2 by LM003D;
   hence thesis by P3,Def1Th;
end;

theorem
  f is_continuous_in x0 implies r(#)f is_continuous_in x0
  proof
    assume AS:f is_continuous_in x0;
    reconsider g = f as PartFunc of REAL,the carrier of REAL-NS n
      by REAL_NS1:def 4;
    g is_continuous_in x0 by AS,Def1Th; then
P1: r(#)g is_continuous_in x0 by NFCONT_3:13;
    r(#)g = r(#)f by LM003B;
    hence thesis by P1,Def1Th;
  end;

theorem
  x0 in dom f & f is_continuous_in x0 implies
    |. f .| is_continuous_in x0
  proof
    assume
AS: x0 in dom f & f is_continuous_in x0;
    reconsider g = f as PartFunc of REAL,the carrier of REAL-NS n
      by REAL_NS1:def 4;
    g is_continuous_in x0 by AS,Def1Th;
    then ||. g .|| is_continuous_in x0 by AS,NFCONT_3:14;
    hence thesis by LM003F;
  end;

theorem
  x0 in dom f & f is_continuous_in x0 implies
    -f is_continuous_in x0
  proof
    assume
AS: x0 in dom f & f is_continuous_in x0;
    reconsider g = f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
    g is_continuous_in x0 by AS,Def1Th; then
aa: -g is_continuous_in x0 by AS,NFCONT_3:14;
    -g = -f by LM003E;
    hence thesis by aa,Def1Th;
  end;

theorem
  for S be RealNormSpace,
      z be Point of REAL-NS n,
      f1 be PartFunc of REAL,REAL n,
      f2 be PartFunc of the carrier of REAL-NS n,the carrier of S
     st x0 in dom (f2*f1) & f1 is_continuous_in x0
        & z=f1/.x0 & f2 is_continuous_in z
    holds f2*f1 is_continuous_in x0
proof
let S be RealNormSpace,
    z be Point of REAL-NS n,
    f1 be PartFunc of REAL,REAL n,
    f2 be PartFunc of the carrier of REAL-NS n,the carrier of S;
  assume AS: x0 in dom (f2*f1) & f1 is_continuous_in x0
        & z=f1/.x0 & f2 is_continuous_in z;
   reconsider g1= f1 as PartFunc of REAL,the carrier of REAL-NS n
   by REAL_NS1:def 4;
P1:  g1 is_continuous_in x0 by AS,Def1Th;
  f1/.x0 = g1/.x0 by REAL_NS1:def 4;
  hence thesis by P1,NFCONT_3:15,AS;
end;

theorem Th12Y:
  for S be RealNormSpace,
      f1 be PartFunc of REAL,the carrier of S,
      f2 be PartFunc of the carrier of S,REAL
     st x0 in dom (f2*f1) & f1 is_continuous_in x0
      & f2 is_continuous_in f1/.x0
    holds f2*f1 is_continuous_in x0
proof
  let S be RealNormSpace,
      f1 be PartFunc of REAL,the carrier of S,
      f2 be PartFunc of the carrier of S,REAL;
  assume
A1: x0 in dom (f2*f1);
  assume that
A2: f1 is_continuous_in x0 and
A3: f2 is_continuous_in f1/.x0;
  let s1 be Real_Sequence such that
A4: rng s1 c= dom (f2*f1) and
A5: s1 is convergent & lim s1 = x0;
A6: dom (f2*f1) c= dom f1 by RELAT_1:44;
A8: rng (f1/*s1) c= dom f2
  proof
    let x;
    assume x in rng (f1/*s1);
    then consider n such that
A7: x=(f1/*s1).n by FUNCT_2:190;
    s1.n in rng s1 by VALUED_0:28;
    then f1.(s1.n) in dom f2 by A4,FUNCT_1:21;
    hence x in dom f2 by A4,A6,A7,FUNCT_2:185,XBOOLE_1:1;
  end;
aa: now
    let n;
    s1.n in rng s1 by VALUED_0:28; then
A9: s1.n in dom f1 by A4,FUNCT_1:21;
    thus ((f2*f1)/*s1).n = (f2*f1).(s1.n) by A4,FUNCT_2:185
      .= f2.(f1.(s1.n)) by A9,FUNCT_1:23
      .= f2.((f1/*s1).n) by A4,A6,FUNCT_2:185,XBOOLE_1:1
      .= (f2/*(f1/*s1)).n by A8,FUNCT_2:185;
  end; then
A10: f2/*(f1/*s1) = (f2*f1)/*s1 by FUNCT_2:113;
  rng s1 c= dom f1 by A4,A6,XBOOLE_1:1; then
A11: f1/*s1 is convergent & f1/.x0 = lim (f1/*s1) by A2,A5,NFCONT_3:def 1;
 hence (f2*f1)/*s1 is convergent by A3,A8,A10,NFCONT_1:def 10;
 thus (f2*f1).x0 = (f2*f1)/.x0 by PARTFUN1:def 8,A1
        .=f2/.(f1/.x0) by PARTFUN2:6,A1
        .= lim (f2/*(f1/*s1)) by A11,A3,A8,NFCONT_1:def 10
        .= lim ((f2*f1)/*s1) by aa,FUNCT_2:113;
end;

definition let n;
  let f be PartFunc of REAL n,REAL;
  let x0 be Element of REAL n;
  pred f is_continuous_in x0 means  :XDef1a:
  ex y0 be Point of REAL-NS n,
     g be PartFunc of the carrier of REAL-NS n,REAL st
    x0=y0 & f=g & g is_continuous_in y0;
end;

theorem Def1aTh:
 for f be PartFunc of REAL n,REAL,
     h be PartFunc of the carrier of REAL-NS n,REAL,
     x0 be Element of REAL n,
     y0 be Point of REAL-NS n
  st f=h & x0=y0 holds
  f is_continuous_in x0 iff h is_continuous_in y0
proof
  let f be PartFunc of REAL n,REAL,
      h be PartFunc of the carrier of REAL-NS n,REAL,
      x0 be Element of REAL n,
      y0 be Point of REAL-NS n;
  assume AS: f=h & x0=y0;
  hereby assume f is_continuous_in x0;
  then ex y1 be Point of REAL-NS n,
  g be PartFunc of the carrier of REAL-NS n,REAL st
  x0=y1 & f=g & g is_continuous_in y1 by XDef1a;
  hence h is_continuous_in y0 by AS;
  end;
  thus thesis by AS,XDef1a;
end;

theorem Th12X:
for f1 be PartFunc of REAL,REAL n,
    f2 be PartFunc of REAL n,REAL st x0 in dom (f2*f1) &
 f1 is_continuous_in x0 & f2 is_continuous_in f1/.x0
 holds f2*f1 is_continuous_in x0
proof
  let f1 be PartFunc of REAL,REAL n,
      f2 be PartFunc of REAL n,REAL;
  assume
AS: x0 in dom (f2*f1) & f1 is_continuous_in x0
     & f2 is_continuous_in f1/.x0;
  reconsider g1=f1 as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
  reconsider g2=f2 as PartFunc of the carrier of REAL-NS n,REAL
     by REAL_NS1:def 4;
A1: g1 is_continuous_in x0 by AS,Def1Th;
    f1/.x0 = g1/.x0 by REAL_NS1:def 4;
   then g2 is_continuous_in g1/.x0 by AS,Def1aTh;
   hence thesis by A1,AS,Th12Y;
end;

definition let n,f;
  attr f is continuous means :Def2:
  for x0 st x0 in dom f holds f is_continuous_in x0;
end;

theorem Def2Th:
 for g be PartFunc of REAL,the carrier of REAL-NS n,
     f be PartFunc of REAL,REAL n st g=f holds
  g is continuous iff f is continuous
proof
  let g be PartFunc of REAL,the carrier of REAL-NS n,
      f be PartFunc of REAL,REAL n;
  assume AS: g=f;
  hereby assume
A1: g is continuous;
  now
    let x0;
    assume x0 in dom f;
    then g is_continuous_in x0 by A1,AS,NFCONT_3:def 2;
    hence f is_continuous_in x0 by AS,Def1Th;
  end;
  hence f is continuous by Def2;
  end;
  assume
A2: f is continuous;
  now
    let x0;
    assume x0 in dom g;
    then f is_continuous_in x0 by A2,AS,Def2;
    hence g is_continuous_in x0 by AS,Def1Th;
  end;
  hence thesis by NFCONT_3:def 2;
end;

theorem Th15:
  X c= dom f implies
  (f|X is continuous iff for x0,r st x0 in X & 0<r
    ex s st 0<s & for x1 st x1 in X & abs(x1-x0) < s
    holds |. f/.x1 - f/.x0 .| < r)
proof
  assume
A1: X c= dom f;
  thus f|X is continuous implies for x0,r st x0 in X & 0<r ex s st 0<s & for
  x1 st x1 in X & abs(x1-x0) < s holds |. f/.x1 - f/.x0 .| < r
  proof
    assume
A2: f|X is continuous;
    let x0,r;
    assume that
A3: x0 in X and
A4: 0<r;
    x0 in dom(f|X) by A1,A3,RELAT_1:91;
    then f|X is_continuous_in x0 by A2,Def2;
    then consider s such that
A5: 0<s and
A6: for x1 st x1 in dom(f|X) & abs(x1-x0)<s
      holds |.(f|X)/.x1-(f|X)/.x0 .| <r by A4,Th3;
    take s;
    thus 0<s by A5;
    let x1;
    assume that
A7: x1 in X and
A8: abs(x1-x0)<s;
A9: dom (f|X) = dom f /\ X by RELAT_1:90
      .= X by A1,XBOOLE_1:28;
    then |. f/.x1 - f/.x0 .| = |.(f|X)/.x1 - f/.x0 .| by A7,PARTFUN2:32
      .= |.(f|X)/.x1 - (f|X)/.x0 .| by A3,A9,PARTFUN2:32;
    hence thesis by A6,A9,A7,A8;
  end;
  assume
A10: for x0,r st x0 in X & 0<r ex s st 0<s & for x1 st x1 in X &
  abs(x1-x0) < s holds |. f/.x1 - f/.x0 .| < r;
A11: dom (f|X) = dom f /\ X by RELAT_1:90
    .= X by A1,XBOOLE_1:28;
  now
    let x0 such that
A12: x0 in dom(f|X);
A13: x0 in X by A12,RELAT_1:86;
    for r st 0<r ex s st 0<s & for x1 st x1 in dom(f|X) & abs(x1-x0)<s
    holds |.(f|X)/.x1-(f|X)/.x0 .|<r
    proof
      let r;
      assume
      0<r;
      then consider s such that
A14:  0<s and
A15:  for x1 st x1 in X & abs(x1-x0) < s
        holds |. f/.x1 - f/.x0 .| < r by A10,A13;
      take s;
      thus 0<s by A14;
      let x1 such that
A16:  x1 in dom(f|X) and
A17:  abs(x1-x0)<s;
      |.(f|X)/.x1-(f|X)/.x0 .| = |.(f|X)/.x1 - f/.x0 .| by A12,PARTFUN2:32
        .= |. f/.x1 - f/.x0 .| by A16,PARTFUN2:32;
      hence thesis by A11,A15,A16,A17;
    end;
    hence f|X is_continuous_in x0 by Th3,A12;
  end;
  hence thesis by Def2;
end;

registration let n;
  cluster constant -> continuous PartFunc of REAL,REAL n;
  coherence
  proof
    let f be PartFunc of REAL,REAL n;
    assume
A1: f is constant;
    now
      reconsider s = 1 as real number;
      let x0,r;
      assume that
A2:   x0 in dom f and
A3:   0<r;
      take s;
      thus 0<s;
      let x1;
      assume
A4:   x1 in dom f;
      assume abs(x1-x0) < s;
      f/.x1 =f.x1 by PARTFUN1:def 8,A4
      .=f.x0 by A1,A2,A4,FUNCT_1:def 16
      .= f/.x0 by PARTFUN1:def 8,A2;
      hence |. f/.x1 - f/.x0 .| < r by A3;
    end;
    then f|dom f is continuous by Th15;
    hence thesis by RELAT_1:98;
  end;
end;

registration let n;
  cluster continuous PartFunc of REAL,REAL n;
  existence
  proof
    take f = the constant PartFunc of REAL,REAL n;
    thus thesis;
  end;
end;

registration let n;
  let f be continuous PartFunc of REAL,REAL n, X be set;
  cluster f|X -> continuous PartFunc of REAL,REAL n;
  coherence
  proof
    for x0 st x0 in dom(f|X) holds f|X is_continuous_in x0
    proof
      let x0;
      assume
A1:   x0 in dom(f|X);
      then x0 in dom f by RELAT_1:86; then
A2:   f is_continuous_in x0 by Def2;
      x0 in X by A1,RELAT_1:86;
      hence thesis by A2,Th1;
    end;
    hence thesis by Def2;
  end;
end;

theorem
  f|X is continuous & X1 c= X implies f|X1 is continuous
proof
  assume
A1: f|X is continuous;
  assume X1 c= X;
  then f|X1 = f|X|X1 by RELAT_1:103;
  hence thesis by A1;
end;

registration let n;
  cluster empty -> continuous PartFunc of REAL,REAL n;
  coherence;
end;

registration let n,f;
  let X be trivial set;
  cluster f|X -> continuous PartFunc of REAL,REAL n;
  coherence
   proof
   reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
     by REAL_NS1:def 4;
   g|X is continuous PartFunc of REAL,the carrier of REAL-NS n;
   hence thesis by Def2Th;
  end;
end;

registration let n;
  let f1,f2 be continuous PartFunc of REAL,REAL n;
  cluster f1+f2 -> continuous PartFunc of REAL,REAL n;
  coherence
  proof
   reconsider g1= f1,g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
   by REAL_NS1:def 4;
P1: g1 is continuous & g2 is continuous by Def2Th;
    g1+g2= f1+f2 by LM003A;
    hence thesis by P1,Def2Th;
  end;
end;

theorem
  X c= dom f1 /\ dom f2 & f1|X is continuous & f2|X is continuous
  implies (f1+f2) |X is continuous & (f1-f2) |X is continuous
proof
  assume AS: X c= dom f1 /\ dom f2 & f1|X is continuous & f2|X
is continuous;
   reconsider g1=f1,g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
   by REAL_NS1:def 4;
P2: g1|X is continuous by AS,Def2Th;
g2|X is continuous by AS,Def2Th; then
P3: (g1+g2) |X is continuous & (g1-g2) |X is continuous
    by AS,P2,NFCONT_3:19;
P4: g1+g2 = f1+f2 by LM003A;
  g1-g2 = f1-f2 by LM003D;
  hence thesis by P3,P4,Def2Th;
end;

theorem
  X c= dom f1 & X1 c= dom f2 & f1|X is continuous & f2|X1 is continuous
  implies (f1+f2) | (X /\ X1) is continuous & (f1-f2) | (X /\ X1) is continuous
proof
  assume AS: X c= dom f1 & X1 c= dom f2 & f1|X is continuous & f2|X1
  is continuous;
   reconsider g1=f1,g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
   by REAL_NS1:def 4;
P2: g1|X is continuous by AS,Def2Th;
    g2|X1 is continuous by AS,Def2Th; then
P3: (g1+g2) | (X /\ X1) is continuous & (g1-g2) | (X /\ X1) is continuous
    by AS,P2,NFCONT_3:20;
P4: g1+g2 = f1+f2 by LM003A;
    g1-g2 = f1-f2 by LM003D;
    hence thesis by P3,P4,Def2Th;
end;

registration let n;
  let f be continuous PartFunc of REAL,REAL n;
  let r;
  cluster r(#)f -> continuous PartFunc of REAL,REAL n;
  coherence
  proof
   reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
   by REAL_NS1:def 4;
P1: g is continuous by Def2Th;
  r(#)g = r(#)f by LM003B;
  hence thesis by P1,Def2Th;
  end;
end;

theorem
  X c= dom f & f|X is continuous implies (r(#)f) | X is continuous
  proof
  assume AS:X c= dom f & f|X is continuous;
  reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
   by REAL_NS1:def 4;
  g|X is continuous PartFunc of REAL,the carrier of REAL-NS n by AS,Def2Th;
  then
P1: (r(#)g) | X is continuous by AS,NFCONT_3:21;
  r(#)g = r(#)f by LM003B;
  hence thesis by P1,Def2Th;
  end;

theorem
  X c= dom f & f|X is continuous
  implies |. f .| |X is continuous & (-f) | X is continuous
proof
   assume AS: X c= dom f & f|X is continuous;
   reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
   by REAL_NS1:def 4;
   g|X is continuous by AS,Def2Th; then
P1: ||. g .|| |X is continuous & (-g) | X is continuous by AS,NFCONT_3:22;
   hence |. f .| |X is continuous by LM003F;
   -g = - f by LM003E;
   hence (-f) | X is continuous by P1,Def2Th;
end;

theorem
  f is total & (for x1,x2 holds f/.(x1+x2) = f/.x1 + f/.x2) &
  (ex x0 st f is_continuous_in x0) implies f|REAL is continuous
proof
  assume AS:
  f is total & (for x1,x2 holds f/.(x1+x2) = f/.x1 + f/.x2);
  given x0 such that
P3: f is_continuous_in x0;
  reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
  by REAL_NS1:def 4;
P2: now let x1,x2;
P21:  g/.x1 = f/.x1 & g/.x2 = f/.x2 by REAL_NS1:def 4;
      thus g/.(x1+x2) = f/.(x1+x2) by REAL_NS1:def 4
      .=f/.x1 + f/.x2 by AS
      .= g/.x1 + g/.x2 by P21,REAL_NS1:2;
    end;
    g is_continuous_in x0 by P3,Def1Th;
    then g|REAL is continuous by AS,P2,NFCONT_3:23;
    hence thesis by Def2Th;
  end;

theorem
  for Y be Subset of REAL-NS n st
  dom f is compact & f| (dom f) is continuous & Y = rng f
  holds Y is compact
proof
  let Y be Subset of REAL-NS n;
  assume AS:dom f is compact & f| (dom f) is continuous & Y = (rng f);
  reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
  g| (dom g) is continuous by AS,Def2Th;
  hence Y is compact by AS,NFCONT_3:24;
end;

theorem
  for Y be Subset of REAL, Z be Subset of REAL-NS n st
  Y c= dom f & Z = f.:Y & Y is compact & f|Y is continuous
  holds Z is compact
  proof
    let Y be Subset of REAL, Z be Subset of REAL-NS n;
    assume
AS: Y c= dom f & Z = (f.:Y) & Y is compact & f|Y is continuous;
    reconsider g = f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
    g|Y is continuous by AS,Def2Th;
    hence Z is compact by AS,NFCONT_3:25;
  end;

definition let n,f;
  attr f is Lipschitzian means  :Def6:
  ex g be PartFunc of REAL,the carrier of REAL-NS n st g=f &
  g is Lipschitzian;
end;

theorem Def6Th:
  f is Lipschitzian iff
  ex r be real number st 0<r & for x1,x2 st x1 in dom f &
  x2 in dom f holds |.f/.x1-f/.x2.| <= r*abs(x1-x2)
proof
  hereby assume f is Lipschitzian;
  then
  consider g be PartFunc of REAL,the carrier of REAL-NS n such that
A1:  g=f & g is Lipschitzian by Def6;
  consider r be real number such that
A2: 0<r & for x1,x2 st x1 in dom g &
    x2 in dom g holds ||.g/.x1-g/.x2.||<=r*abs(x1-x2) by A1,NFCONT_3:def 3;
  take r;
  thus 0<r by A2;
  thus for x1,x2 st x1 in dom f &
       x2 in dom f holds |.f/.x1-f/.x2.|<=r*abs(x1-x2)
  proof
    let x1,x2;
    assume x1 in dom f & x2 in dom f; then
A4: ||.g/.x1-g/.x2.||<=r*abs(x1-x2) by A1,A2;
    f/.x1 = g/.x1 &  f/.x2 = g/.x2 by A1,REAL_NS1:def 4;
    hence |.f/.x1-f/.x2.|<=r*abs(x1-x2) by A4,REAL_NS1:1,5;
  end;
end;
  given r be real number such that
A5: 0<r & for x1,x2 st x1 in dom f &
    x2 in dom f holds |.f/.x1-f/.x2.|<=r*abs(x1-x2);
  reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
  by REAL_NS1:def 4;
  now
    let x1,x2;
    assume x1 in dom g & x2 in dom g; then
A7: |.f/.x1-f/.x2.|<=r*abs(x1-x2) by A5;
    f/.x1 = g/.x1 &  f/.x2 = g/.x2 by REAL_NS1:def 4;
    hence ||.g/.x1-g/.x2.||<=r*abs(x1-x2) by A7,REAL_NS1:1,5;
  end;
  then g is Lipschitzian by A5,NFCONT_3:def 3;
  hence thesis by Def6;
end;

theorem Def6Th1:
  f=h implies (f is Lipschitzian iff h is Lipschitzian)
proof
  assume AS: f=h;
  hereby assume f is Lipschitzian;
  then consider r be real number such that
A1: 0<r & for x1,x2 st x1 in dom f &
    x2 in dom f holds |.f/.x1-f/.x2.|<=r*abs(x1-x2) by Def6Th;
  now
    let x1,x2;
    assume x1 in dom h & x2 in dom h; then
A3: |.f/.x1-f/.x2.|<=r*abs(x1-x2) by A1,AS;
    f/.x1 = h/.x1 &  f/.x2 = h/.x2 by AS,REAL_NS1:def 4;
    hence ||. h/.x1-h/.x2 .||<=r*abs(x1-x2) by A3,REAL_NS1:1,5;
  end;
  hence h is Lipschitzian by A1,NFCONT_3:def 3;
  end;
  assume h is Lipschitzian;
  then consider r be real number such that
A4: 0<r & for x1,x2 st x1 in dom h &
    x2 in dom h holds ||.h/.x1-h/.x2.||<=r*abs(x1-x2) by NFCONT_3:def 3;
  now
    let x1,x2;
    assume x1 in dom f & x2 in dom f; then
A6: ||.h/.x1-h/.x2.||<=r*abs(x1-x2) by A4,AS;
    f/.x1 = h/.x1 &  f/.x2 = h/.x2 by AS,REAL_NS1:def 4;
    hence |.f/.x1-f/.x2.|<=r*abs(x1-x2) by A6,REAL_NS1:1,5;
  end;
  hence thesis by A4,Def6Th;
end;

theorem
  f|X is Lipschitzian iff ex r be real number
     st 0<r & for x1,x2 st x1 in dom(f|X) &
   x2 in dom(f|X) holds |. f/.x1-f/.x2 .| <= r*abs(x1-x2)
proof
  hereby assume f|X is Lipschitzian; then
  consider r be real number such that
A1: 0<r & for x1,x2 st x1 in dom (f|X) &
    x2 in dom (f|X) holds |.(f|X)/.x1-(f|X)/.x2.|<=r*abs(x1-x2) by Def6Th;
  take r;
  thus 0<r by A1;
  thus for x1,x2 st x1 in dom(f|X) &
        x2 in dom(f|X) holds |. f/.x1-f/.x2 .|<=r*abs(x1-x2)
  proof
    let x1,x2;
    assume A2: x1 in dom(f|X) & x2 in dom(f|X); then
    |.(f|X)/.x1-(f|X)/.x2.|<=r*abs(x1-x2) by A1; then
    |.f/.x1-(f|X)/.x2.|<=r*abs(x1-x2) by A2,PARTFUN2:32;
    hence |.f/.x1-f/.x2.|<=r*abs(x1-x2) by A2,PARTFUN2:32;
  end;
  end;
  given r be real number such that
A3: 0<r & for x1,x2 st x1 in dom(f|X) &
    x2 in dom(f|X) holds |. f/.x1-f/.x2 .|<=r*abs(x1-x2);
  now
   let x1,x2;
   assume A5: x1 in dom(f|X) & x2 in dom(f|X);
   then |. f/.x1-f/.x2 .|<=r*abs(x1-x2) by A3;
   then |.(f|X)/.x1-f/.x2.|<=r*abs(x1-x2) by A5,PARTFUN2:32;
   hence |.(f|X)/.x1-(f|X)/.x2.|<=r*abs(x1-x2) by A5,PARTFUN2:32;
 end;
 hence f|X is Lipschitzian by A3,Def6Th;
end;

registration let n;
  cluster empty -> Lipschitzian PartFunc of REAL,REAL n;
  coherence
  proof
    let f be PartFunc of REAL,REAL n;
    reconsider g = f as PartFunc of REAL,the carrier of REAL-NS n
      by REAL_NS1:def 4;
    assume f is empty;
    then g is empty;
    hence thesis by Def6Th1;
  end;
end;

registration let n;
  cluster empty PartFunc of REAL,REAL n;
  existence
  proof
    reconsider e = {} as PartFunc of REAL,REAL n by RELSET_1:25;
    take e;
    thus thesis;
  end;
end;

registration let n;
  let f be Lipschitzian PartFunc of REAL,REAL n, X be set;
  cluster f|X -> Lipschitzian PartFunc of REAL,REAL n;
  coherence
  proof
    reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
      by REAL_NS1:def 4;
    g is Lipschitzian by Def6Th1;
    then g|X is Lipschitzian;
    hence thesis by Def6Th1;
  end;
end;

theorem
  f|X is Lipschitzian & X1 c= X implies f|X1 is Lipschitzian
proof
  assume that
A1: f|X is Lipschitzian and
A2: X1 c= X;
  f|X1 = f|X|X1 by A2,RELAT_1:103;
  hence thesis by A1;
end;

registration let n;
  let f1,f2 be Lipschitzian PartFunc of REAL,REAL n;
  cluster f1+f2 -> Lipschitzian PartFunc of REAL,REAL n;
  coherence
  proof
   reconsider g1=f1,g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
P1:g1 is Lipschitzian & g2 is Lipschitzian by Def6Th1;
   g1+g2 = f1+f2 by LM003A;
   hence thesis by P1,Def6Th1;
  end;
  cluster f1-f2 -> Lipschitzian PartFunc of REAL,REAL n;
  coherence
  proof
   reconsider g1=f1,g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
   P1:g1 is Lipschitzian & g2 is Lipschitzian by Def6Th1;
   g1-g2 = f1-f2 by LM003D;
   hence thesis by P1,Def6Th1;
  end;
end;

theorem
  f1|X is Lipschitzian & f2|X1 is Lipschitzian implies
    (f1+f2) | (X /\ X1) is Lipschitzian
proof
   assume
AS:f1|X is Lipschitzian & f2|X1 is Lipschitzian;
   reconsider g1=f1,g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
     by REAL_NS1:def 4;
   g1|X is Lipschitzian & g2|X1 is Lipschitzian by Def6Th1,AS; then
P1: (g1+g2) | (X /\ X1) is Lipschitzian by NFCONT_3:28;
  g1+g2 = f1+f2 by LM003A;
  hence thesis by P1,Def6Th1;
end;

theorem
  f1|X is Lipschitzian & f2|X1 is Lipschitzian implies
  (f1-f2) | (X /\ X1) is Lipschitzian
proof
  assume
  AS:f1|X is Lipschitzian & f2|X1 is Lipschitzian;
    reconsider g1=f1,g2=f2 as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
 g1|X is Lipschitzian & g2|X1 is Lipschitzian by Def6Th1,AS;
   then
 P1: (g1-g2) | (X /\ X1) is Lipschitzian by NFCONT_3:29;
 g1-g2 = f1-f2 by LM003D;
 hence thesis by P1,Def6Th1;
end;

registration let n;
  let f be Lipschitzian PartFunc of REAL, REAL n;
  let p;
  cluster p(#)f -> Lipschitzian PartFunc of REAL, REAL n;
  coherence
  proof
    reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
P1: g is Lipschitzian by Def6Th1;
    p(#)g = p(#)f by LM003B;
    hence thesis by P1,Def6Th1;
  end;
end;

theorem
  f|X is Lipschitzian & X c= dom f implies (p(#)f) | X is Lipschitzian
proof
   assume AS: f|X is Lipschitzian & X c= dom f;
   reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
     by REAL_NS1:def 4;
   g|X is Lipschitzian by Def6Th1,AS; then
P1: (p(#)g) | X is Lipschitzian by NFCONT_3:30,AS;
   p(#)g = p(#)f by LM003B;
   hence thesis by P1,Def6Th1;
end;

registration let n;
  let f be Lipschitzian PartFunc of REAL, REAL n;
  cluster |. f .| -> Lipschitzian PartFunc of REAL,REAL;
  coherence
  proof
    reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
    g is Lipschitzian by Def6Th1;
    then ||. g .|| is Lipschitzian;
    hence thesis by LM003F;
  end;
end;

theorem
  f|X is Lipschitzian implies -(f|X) is Lipschitzian
    & |. f .| |X is Lipschitzian & (-f) |X is Lipschitzian
proof
   assume AS: f|X is Lipschitzian;
   reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
     by REAL_NS1:def 4;
   g|X is Lipschitzian by AS,Def6Th1; then
P1: -(g|X) is Lipschitzian & (||. g .||) |X is Lipschitzian &
   (-g) | X is Lipschitzian by NFCONT_3:31;
  -(g|X) = -(f|X) by LM003E;
  hence -(f|X) is Lipschitzian by P1,Def6Th1;
  thus (|. f .|) |X is Lipschitzian by P1,LM003F;
  -g = - f by LM003E;
  hence thesis by P1,Def6Th1;
end;

registration let n;
  cluster constant -> Lipschitzian PartFunc of REAL, REAL n;
  coherence
  proof
    let f be PartFunc of REAL, REAL n;
    assume
A1: f is constant;
    reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
    g is constant by A1;
    hence thesis by Def6Th1;
  end;
end;

registration let n;
  cluster Lipschitzian -> continuous PartFunc of REAL, REAL n;
  coherence
  proof
    let f be PartFunc of REAL, REAL n;
    reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
    by REAL_NS1:def 4;
    assume f is Lipschitzian;
    then g is Lipschitzian by Def6Th1;
    hence thesis by Def2Th;
  end;
end;

theorem
  for r,p be Element of REAL n
    st (for x0 st x0 in X holds f/.x0 = x0*r+p)
  holds f|X is continuous
proof
  let r,p be Element of REAL n;
  assume AS: for x0 st x0 in X holds f/.x0 = x0*r+p;
  reconsider g= f as PartFunc of REAL,the carrier of REAL-NS n
   by REAL_NS1:def 4;
  reconsider r0=r, p0=p as Point of REAL-NS n by REAL_NS1:def 4;
  now let x0;
 assume A1: x0 in X;
A2: x0*r = x0*r0 by REAL_NS1:3;
   thus g/.x0 = f/.x0 by REAL_NS1:def 4
        .= x0*r+p by A1,AS
        .= x0*r0+p0 by A2,REAL_NS1:2;
  end;
  then g|X is continuous by NFCONT_3:33;
  hence thesis by Def2Th;
end;

theorem X0:
  for x0 be Element of REAL n st 1 <= i & i <= n
  holds proj(i,n) is_continuous_in x0
proof
let x0 be Element of REAL n;
assume AS0: 1 <=i & i <= n;
P2:dom proj(i,n) = REAL n by FUNCT_2:def 1;
reconsider prg=proj(i,n) as PartFunc of the carrier of REAL-NS n,REAL
  by REAL_NS1:def 4;
reconsider px0 = x0 as Element of the carrier of REAL-NS n
  by REAL_NS1:def 4;
now let r be Real;
   assume A3: 0<r;
   take s = r;
   thus 0<s by A3;
      now let px1 be Element of the carrier of REAL-NS n;
      assume A4: px1 in dom prg & ||.px1-px0.|| < r;
        reconsider x1=px1 as Element of REAL n by REAL_NS1:def 4;
A40:    ||.px1-px0.|| = |.x1-x0.| by REAL_NS1:1,REAL_NS1:5;
       proj(i,n)/.(x1-x0) =proj(i,n)/.x1-proj(i,n)/.x0 by AS0,PDIFF_8:12;
       then |. proj(i,n)/.x1-proj(i,n)/.x0.| <= |. x1-x0 .|
        by AS0,PDIFF_8:5;
     hence |. proj(i,n)/.px1-proj(i,n)/.px0.| < r by A4,A40,XXREAL_0:2;
    end;
  hence for px1 be Element of the carrier of REAL-NS n st px1 in dom prg
     & ||.px1-px0.|| < s holds |. proj(i,n)/.px1-proj(i,n)/.px0 .|<r;
 end;
 then prg is_continuous_in px0 by P2,NFCONT_1:15;
 hence thesis by XDef1a;
end;

theorem X1:
for n be non empty Element of NAT, h be PartFunc of REAL, REAL n holds
h is_continuous_in x0 iff
    (x0 in dom h & for i be Element of NAT st i in Seg n
    holds proj(i,n)*h is_continuous_in x0)
proof
let n be non empty Element of NAT, h be PartFunc of REAL,REAL n;
hereby
 assume AS: h is_continuous_in x0;
  hence
  X1: x0 in dom h by Th3;
  thus for i be Element of NAT st i in Seg n
    holds proj(i,n)*h is_continuous_in x0
  proof
   let i be Element of NAT;
   assume i in Seg n; then
   P1: 1<=i & i <= n by FINSEQ_1:3;
   P20:dom (proj(i,n)) = REAL n by FUNCT_2:def 1;
   rng h c= REAL n; then
P2: dom (proj(i,n)*h) = dom h by P20,RELAT_1:46;
   proj(i,n) is_continuous_in h/.x0 by P1,X0;
   hence thesis by P2,AS,X1,Th12X;
  end;
end;
assume
AS11: x0 in dom h &
   for i be Element of NAT st i in Seg n
      holds (proj(i,n)*h) is_continuous_in x0;
for r be Real st 0<r ex s be real number
st 0<s & for x1 be real number st x1
  in dom h & abs(x1-x0)<s holds |. h/.x1 - h/.x0 .|<r
proof
 let r0 be Real;
 assume AX1: 0 < r0;
 set r=r0/2;
 AX11:0 < r by XREAL_1:217,AX1;
  defpred P[Nat,real number] means 0 < $2 &
  for x1 be real number st x1 in dom h & abs(x1- x0) < $2 holds
  |. (proj($1,n)*h).x1 - (proj($1,n)*h).x0  .| < r/n;
  BB1: 0 < r/n by XREAL_1:141,AX11;
  BB2: for j be Nat st j in Seg n ex x be Element of REAL st P[j,x]
     proof
      let j be Nat;
      assume BX0: j in Seg n;
  U3: proj(j,n)*h is_continuous_in x0 by AS11,BX0;
     P20:dom (proj(j,n)) = REAL n by FUNCT_2:def 1;
     rng h c= REAL n; then
     P21:dom (proj(j,n)*h) = dom h by P20,RELAT_1:46;
    consider sj be real number such that
   BX3: 0 < sj &
      for x1 be real number st x1 in dom (proj(j,n)*h) & abs(x1- x0) < sj holds
       |.(proj(j,n)*h).x1 - (proj(j,n)*h).x0 .| < r/n by U3,FCONT_1:3,BB1;
    sj is Real by XREAL_0:def 1;
    hence thesis by BX3,P21;
  end;
consider s0 be FinSequence of REAL such that
BB3: dom s0 = Seg n &
     for k be Nat st k in Seg n holds P[k,s0.k] from FINSEQ_1:sch 5(BB2);
     n in Seg n by FINSEQ_1:5;
 then reconsider rs0= rng s0 as non empty ext-real-membered set
      by BB3,FUNCT_1:12;
 rng s0 is finite by BB3,FINSET_1:26;
 then reconsider rs0 as left_end right_end (non empty ext-real-membered set);
XA19:min rs0 in rng s0 by XXREAL_2:def 7;
     then reconsider s = min rs0 as Real;
     take s;
     ex i1 be set st i1 in dom s0 & s = s0.i1 by XA19,FUNCT_1:def 5;
     hence 0 < s by BB3;
now let x1;
  assume BX3: x1 in dom h & abs( x1- x0) < s;
 now let j be Element of NAT;
   assume 1<=j & j <= n; then
BX1: j in Seg n by FINSEQ_1:3;
    then s0.j in rng s0 by BB3,FUNCT_1:12;
    then s <= s0.j by XXREAL_2:def 7;
    then abs(x1- x0) < s0.j by BX3,XXREAL_0:2; then
X100: |. (proj(j,n)*h).x1 - (proj(j,n)*h).x0 .| < r/n by BX1,BX3,BB3;
    C1:(proj(j,n)*h).x1 =(proj(j,n)).(h.x1) by BX3,FUNCT_1:23
         .=(proj(j,n)).(h/.x1) by BX3,PARTFUN1:def 8;
    (proj(j,n)*h).x0 = (proj(j,n)).(h.x0) by AS11,FUNCT_1:23
    .=(proj(j,n)).(h/.x0) by AS11,PARTFUN1:def 8;
   hence |. proj(j,n).((h/.x1) - (h/.x0)) .| <= r/n by PDIFF_8:12,X100,C1;
 end;
 then |. h/.x1 - h/.x0 .| <= n*(r/n) by PDIFF_8:17; then
D1:  |. h/.x1 - h/.x0 .| <= r by XCMPLX_1:88;
  r < r0 by AX1,XREAL_1:218;
  hence|. h/.x1 - h/.x0 .| < r0 by XXREAL_0:2,D1;
  end;
  hence for x1 st x1 in dom h & abs(x1-x0) < s
    holds |. h/.x1 - h/.x0 .| < r0;
 end;
hence thesis by AS11,Th3;
end;

theorem
for n be non empty Element of NAT, h be PartFunc of REAL, REAL n holds
 h is continuous iff
  for i be Element of NAT st i in Seg n
    holds proj(i,n)*h is continuous
proof
let n be non empty Element of NAT,
    h be PartFunc of REAL, REAL n;
hereby assume
P1: h is continuous;
thus for i be Element of NAT st i in Seg n holds (proj(i,n)*h) is continuous
proof
let i be Element of NAT;
 assume P2: i in Seg n;
     P20:dom (proj(i,n)) = REAL n by FUNCT_2:def 1;
     rng h c= REAL n; then
     P21:dom (proj(i,n)*h) = dom h by P20,RELAT_1:46;
 now let x0;
  assume x0 in dom (proj(i,n)*h);
   then h is_continuous_in x0 by P1,P21,Def2;
   hence (proj(i,n)*h) is_continuous_in x0 by P2,X1;
  end;
hence proj(i,n)*h is continuous by FCONT_1:def 2;
end;
end;
assume
Q1:  for i be Element of NAT st i in Seg n holds (proj(i,n)*h) is continuous;
let x0;
  assume A2: x0 in dom h;
  now let i be Element of NAT;
     assume P2: i in Seg n;
     P20:dom (proj(i,n)) = REAL n by FUNCT_2:def 1;
     rng h c= REAL n; then
     P21:dom (proj(i,n)*h) = dom h by P20,RELAT_1:46;
     (proj(i,n)*h) is continuous by Q1,P2;
     hence (proj(i,n)*h) is_continuous_in x0 by A2,P21,FCONT_1:def 2;
 end;
 hence thesis by A2,X1;
end;

theorem Y0:
  for x0 be Point of REAL-NS n st 1 <= i & i <= n holds
  Proj(i,n) is_continuous_in x0
proof
let x0 be Point of REAL-NS n;
assume AS0: 1 <= i & i <= n;
P2: dom (Proj(i,n)) = the carrier of REAL-NS n by FUNCT_2:def 1;
now let r be Real;
   assume A3: 0<r;
   take s = r;
   thus 0< s by A3;
    now let x1 be Point of REAL-NS n;
      assume A4: x1 in dom (Proj(i,n)) & ||.x1-x0.|| < s;
      Proj(i,n)/.(x1-x0) = Proj(i,n)/.x1-Proj(i,n)/.x0 by AS0,PDIFF_8:11;
      then ||. Proj(i,n)/.x1-Proj(i,n)/.x0.|| <= ||. x1-x0 .||
        by AS0,PDIFF_8:3;
      hence ||. Proj(i,n)/.x1-Proj(i,n)/.x0.|| < r by A4,XXREAL_0:2;
    end;
    hence for x1 be Point of REAL-NS n st x1 in dom (Proj(i,n))
     & ||.x1-x0.|| < s holds ||. Proj(i,n)/.x1-Proj(i,n)/.x0 .||<r;
 end;
 hence thesis by P2,NFCONT_1:14;
end;

theorem Y1:
for n be non empty Element of NAT,
    h be PartFunc of REAL,the carrier of REAL-NS n holds
 h is_continuous_in x0 iff
  for i be Element of NAT st i in Seg n
    holds Proj(i,n)*h is_continuous_in x0
proof
let n be non empty Element of NAT,
  h be PartFunc of REAL, the carrier of REAL-NS n;
hereby assume AS: h is_continuous_in x0; then
  X1: x0 in dom h by NFCONT_3:7;
  thus for i be Element of NAT st i in Seg n
    holds Proj(i,n)*h is_continuous_in x0
  proof
   let i be Element of NAT;
   assume i in Seg n; then
   P1: 1<=i & i <= n by FINSEQ_1:3;
   P20:dom (Proj(i,n)) = the carrier of REAL-NS n by FUNCT_2:def 1;
   rng h c= the carrier of REAL-NS n; then
P2: dom (Proj(i,n)*h) = dom h by P20,RELAT_1:46;
   Proj(i,n) is_continuous_in h/.x0 by P1,Y0;
   hence (Proj(i,n)*h) is_continuous_in x0 by X1,P2,AS,NFCONT_3:15;
  end;
end;
assume AS1: for i be Element of NAT st i in Seg n
    holds (Proj(i,n)*h) is_continuous_in x0;
1<=n by NAT_1:14;
then 1 in Seg n by FINSEQ_1:3;
then (Proj(1,n)*h) is_continuous_in x0 by AS1; then
U1:x0 in dom(Proj(1,n)*h) by NFCONT_3:8;
U2:dom(Proj(1,n)*h) c= dom h by RELAT_1:44;
for r st 0<r ex s st 0<s & for x1 st x1
  in dom h & abs(x1-x0)<s holds ||. h/.x1 - h/.x0 .||<r
proof
 let r0 be Real;
 set r=r0/2;
 assume AX1: 0 < r0; then
 AX11:0 < r by XREAL_1:217;
  defpred P[set,real number] means
  ex j be Element of NAT st $1=j & 0 < $2 &
  for x1 st x1 in dom h & abs(x1- x0) < $2 holds
      ||. (Proj(j,n)*h)/.x1 - (Proj(j,n)*h)/.x0 .|| < r/n;
  BB1: 0 < r/n by XREAL_1:141,AX11;
  BB2: for j0 be Nat st j0 in Seg n holds
     ex x be Element of REAL st P[j0,x]
     proof
      let j0 be Nat;
      assume BX0: j0 in Seg n;
      reconsider j=j0 as Element of NAT by ORDINAL1:def 13;
     U3: Proj(j,n)*h is_continuous_in x0 by AS1,BX0;
     P20:dom (Proj(j,n)) = the carrier of REAL-NS n by FUNCT_2:def 1;
     rng h c= the carrier of REAL-NS n; then
     P21:dom (Proj(j,n)*h) = dom h by P20,RELAT_1:46;
    consider sj be real number such that
BX3: 0 < sj &
     for x1 st x1 in dom (Proj(j,n)*h) & abs(x1- x0) < sj holds
     ||. (Proj(j,n)*h)/.x1 - (Proj(j,n)*h)/.x0 .|| < r/n by U3,NFCONT_3:8,BB1;
     sj is Real by XREAL_0:def 1;
   hence thesis by BX3,P21;
  end;
consider s0 be FinSequence of REAL such that
BB3: dom s0 = Seg n &
     for k be Nat st k in Seg n holds P[k,s0.k] from FINSEQ_1:sch 5(BB2);
     n in Seg n by FINSEQ_1:5;
 then reconsider rs0= rng s0 as non empty ext-real-membered set
      by BB3,FUNCT_1:12;
 rng s0 is finite by BB3,FINSET_1:26;
 then reconsider rs0 as left_end right_end (non empty ext-real-membered set);
XA19:min rs0 in rng s0 by XXREAL_2:def 7;
    then reconsider s = min rs0 as Real;
   take s;
   consider i1 be set such that
XA20: i1 in dom s0 & s = s0.i1 by XA19,FUNCT_1:def 5;
   ex j be Element of NAT st i1=j & 0 < s0.i1 &
      for x1 st x1 in dom h & abs(x1- x0) < s0.i1 holds
       ||. (Proj(j,n)*h)/.x1 - (Proj(j,n)*h)/.x0 .|| < r/n by BB3,XA20;
  hence 0 < s by XA20;
  let x1;
  assume BX3: x1 in dom h & abs(x1- x0) < s;
 now let j be Element of NAT;
 assume 1<=j & j <= n; then
     BX1:j in Seg n by FINSEQ_1:3;
     then consider jj be Element of NAT such that
     BX2: jj = j & 0 < s0.j &
      for x1 st x1 in dom h & abs(x1- x0)< s0.j holds
    ||. (Proj(jj,n)*h)/.x1 - (Proj(jj,n)*h)/.x0 .|| < r/n by BB3;
    s0.j in rng s0 by BX1,BB3,FUNCT_1:12;
    then s <= s0.j by XXREAL_2:def 7;
    then abs(x1- x0) < s0.j by BX3,XXREAL_0:2; then
   X100: ||. (Proj(j,n)*h)/.x1 - (Proj(j,n)*h)/.x0 .|| < r/n by BX2,BX3;
    C0:dom (Proj(j,n)) = the carrier of REAL-NS n by FUNCT_2:def 1; then
    C1:(Proj(j,n)*h)/.x1 = (Proj(j,n))/.(h/.x1) by BX3,PARTFUN2:9;
   (Proj(j,n)*h)/.x0 = (Proj(j,n))/.(h/.x0) by C0,U2,U1,PARTFUN2:9;
   hence ||. Proj(j,n).((h/.x1) - (h/.x0)) .|| <= r/n
                                             by PDIFF_8:11,X100,C1;
 end;
 then ||. h/.x1 - h/.x0 .|| <= n*(r/n) by PDIFF_8:16; then
D1: ||. h/.x1 - h/.x0 .|| <= r by XCMPLX_1:88;
  r < r0 by AX1,XREAL_1:218;
  hence ||. h/.x1 - h/.x0 .|| < r0 by XXREAL_0:2,D1;
end;
hence thesis by U2,U1,NFCONT_3:8;
end;

theorem
for n be non empty Element of NAT,
    h be PartFunc of REAL, the carrier of REAL-NS n holds
 h is continuous iff
  for i be Element of NAT st i in Seg n
    holds Proj(i,n)*h is continuous
  proof
    let n be non empty Element of NAT,
    h be PartFunc of REAL, the carrier of REAL-NS n;
    hereby assume
P1:   h is continuous;
      thus for i be Element of NAT st i in Seg n
      holds Proj(i,n)*h is continuous
      proof
        let i be Element of NAT;
        assume
P2:     i in Seg n;
P20:    dom Proj(i,n) = the carrier of REAL-NS n by FUNCT_2:def 1;
        rng h c= the carrier of REAL-NS n; then
P21:    dom (Proj(i,n)*h) = dom h by P20,RELAT_1:46;
        now let x0;
          assume x0 in dom (Proj(i,n)*h);
          then h is_continuous_in x0 by P1,P21,NFCONT_3:def 2;
          hence Proj(i,n)*h is_continuous_in x0 by P2,Y1;
        end;
        hence Proj(i,n)*h is continuous by NFCONT_3:def 2;
      end;
    end;
    assume
Q1: for i be Element of NAT st i in Seg n holds Proj(i,n)*h is continuous;
    let x0;
    assume A2: x0 in dom h;
    now let i be Element of NAT;
      assume P2: i in Seg n;
P20:  dom (Proj(i,n)) = the carrier of REAL-NS n by FUNCT_2:def 1;
      rng h c= the carrier of REAL-NS n; then
P21:  dom (Proj(i,n)*h) = dom h by P20,RELAT_1:46;
      Proj(i,n)*h is continuous by Q1,P2;
      hence Proj(i,n)*h is_continuous_in x0 by A2,P21,NFCONT_3:def 2;
    end;
    hence h is_continuous_in x0 by Y1;
  end;
