:: The {C}atalan Numbers. {P}art {II}
::  by Karol P\c{a}k
::
:: Received October 31, 2006
:: Copyright (c) 2006 Association of Mizar Users

environ

 vocabularies TARSKI, ARYTM, ARYTM_1, ARYTM_3, RELAT_1, FUNCT_1, FUNCT_2,
      FUNCT_3, FUNCOP_1, BOOLE, FINSEQ_1, FINSET_1, FINSOP_1, SQUARE_1, CARD_1,
      ORDINAL2, CARD_FIN, CATALAN1, FRAENKEL, AFINSQ_1, HENMODEL, BINOP_1,
      RLVECT_1, MONOID_0, NEWTON, QC_LANG1, SEQ_1, SEQ_2, SEQM_3, SERIES_1,
      ALGSEQ_1, SGRAPH1, VECTSP_1, PREPOWER, ABSVALUE, GROUP_1, SUPINF_2,
      CATALAN2, NAT_1;
 notations ORDINAL1, REAL_1, TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS,
      RELAT_1, FUNCT_1, RELSET_1, NAT_1, FINSET_1, CARD_1, AFINSQ_1, DOMAIN_1,
      XCMPLX_0, FUNCT_2, BINOP_1, BINOP_2, NEWTON, STIRL2_1, BINARITH,
      FUNCOP_1, XXREAL_0, CARD_FIN, CATALAN1, FRAENKEL, XREAL_0, SEQ_1, SEQ_2,
      SEQM_3, SERIES_1, SQUARE_1, PREPOWER, COMPLEX1, QUIN_1;
 constructors WELLORD2, DOMAIN_1, SETWISEO, REAL_1, SQUARE_1, NAT_1, QUIN_1,
      SEQM_3, PREPOWER, SERIES_1, BINARITH, CATALAN1, STIRL2_1, PARTFUN3,
      BINOM, CARD_FIN;
 registrations XBOOLE_0, SUBSET_1, RELAT_1, FUNCT_1, ORDINAL1, FUNCOP_1,
      FINSET_1, FRAENKEL, NUMBERS, XXREAL_0, XREAL_0, SQUARE_1, NAT_1, INT_1,
      BINOP_2, MEMBERED, NEWTON, AFINSQ_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, FUNCT_1, FRAENKEL, QUIN_1, STIRL2_1;
 theorems CATALAN1, AFINSQ_1, TARSKI, XBOOLE_0, ZFMISC_1, RELAT_1, XBOOLE_1,
      NAT_1, FUNCT_2, FINSET_1, XCMPLX_1, REAL_1, CARD_2, INT_1, ORDINAL1,
      WELLORD2, EULER_1, CARD_1, BINOP_2, NEWTON, XREAL_1, BINARITH, FUNCOP_1,
      STIRL2_1, FUNCT_1, CARD_FIN, SERIES_1, SEQ_1, SEQ_2, COMPLEX1, SQUARE_1,
      ABSVALUE, SEQM_3, RSSPACE2, POWER, PREPOWER, WSIERP_1, FIB_NUM, TIETZE,
      XREAL_0, SERIES_2, XXREAL_0;
 schemes FUNCT_2, NAT_1, XBOOLE_0, STIRL2_1;

begin

 reserve x, x1, x2, y, X, D for set,
         i, j, k, l, m, n for Element of NAT,
         p, q for XFinSequence of NAT,
         p', q' for XFinSequence,
         pd, qd for XFinSequence of D;

theorem Th1:
  (p'^q')|dom p' = p'
proof
  A1:dom p' c= dom (p'^q') & len p'= dom p' by AFINSQ_1:24,def 1;
   dom p'=len p' by AFINSQ_1:def 1;
  then reconsider r=(p'^q')|(dom p') as XFinSequence by AFINSQ_1:12;
   dom r=len r by AFINSQ_1:def 1;
  then A2: len r= len p' by A1,RELAT_1:91;
   now let k such that A3:k < len p';
     k+0<len p'+len q' by A3,XREAL_1: 10;
    then k in len p'+len q' & k in len p' by A3,EULER_1:1;
    then k in dom (p'^q') & k in dom p' by AFINSQ_1:def 1,def 4;
    then k in dom p' & k in dom (p'^q')/\ dom p' by XBOOLE_0:def 3;
    then (p'^q').k=p'.k & r.k=(p'^q').k by AFINSQ_1:def 4,FUNCT_1:71;
    hence r.k=p'.k;
  end;
  hence thesis by A2,AFINSQ_1:11;
end;

theorem Th2:
  n <= dom p' implies (p'^q')|n = p'|n
proof
  assume A1:n <= dom p';
   dom p'=len p' by AFINSQ_1:def 1;
  then n c= dom p' by A1,CARD_1:56;
  then ((p'^q')|dom p')|n=(p'^q')|n by RELAT_1:103;
  hence thesis by Th1;
end;

theorem Th3:
  n = dom p' + k implies (p'^q')|n = p'^(q'|k)
proof
  assume A1: n = dom p' + k;
   now per cases;
    suppose A2:n>=len (p'^q');
      then n>=len p'+len q' & dom p'=len p' by AFINSQ_1:20,def 1;
      then k >= len q' by A1,XREAL_1:10;
      then len q' c= k & len (p'^q') c= n by A2,CARD_1:56;
      then dom q' c= k & dom (p'^q') c= n by AFINSQ_1:def 1;
      then q'|k = q' & (p'^q')|n = (p'^q') by RELAT_1:97;
      hence thesis;
    end;
    suppose A3:n<len (p'^q');
      then n<len p'+len q' & dom p'=len p' by AFINSQ_1:20,def 1;
      then k < len q' by A1,XREAL_1:8;
      then A4:len (q'|k)=k by AFINSQ_1:14;
      then A5:len (p'^(q'|k))=len p' + k & len ((p'^q')|n)=n & dom p'=len p'
        by A3,AFINSQ_1:def 1,14,20;
       now let m such that A6:m < len ((p'^q')|n);
         m <  n & m <len (p'^q') by A3,A5,A6,XXREAL_0:2;
        then m in n & m in len (p'^q') & len (p'^q')=dom (p'^q')
          by EULER_1:1,AFINSQ_1:def 1;
        then A7:m in dom (p'^q') /\ n by XBOOLE_0:def 3;
        then A8:((p'^q')|n).m=(p'^q').m by FUNCT_1:71;
         now per cases;
          suppose m<dom p';
            then m in dom p' by EULER_1:1;
            then (p'^(q'|k)).m=p'.m & (p'^q').m=p'.m by AFINSQ_1:def 4;
            hence ((p'^q')|n).m=(p'^(q'|k)).m by A7,FUNCT_1:71;
          end;
          suppose m>=dom p';
            then m>=len p' & m< len (p'^(q'|k)) & m <  len (p'^q')
              by A3,A1,A5,A6,XXREAL_0:2;
            then A9:(p'^(q'|k)).m=(q'|k).(m-len p')&m-len p'+len p'<len (p'^q')
              &q'.(m-len p')=(p'^q').m& len (p'^q')=len p'+len q' &m<len p'+k &
              m>=len p' by AFINSQ_1:22,A4,AFINSQ_1:20;
            then m-len p' < k & m-len p'<len q' & m-len p' is Nat
              by XREAL_1:8,NAT_1:21;
            then m-len p' in k & m-len p' in len q' & dom q'=len q'
              by EULER_1:1,AFINSQ_1:def 1;
            then m-len p' in k/\dom q' by XBOOLE_0:def 3;
            hence ((p'^q')|n).m=(p'^(q'|k)).m by A8,A9,FUNCT_1:71;
          end;
        end;
        hence ((p'^q')|n).m=(p'^(q'|k)).m;
      end;
      hence thesis by A1,A5,AFINSQ_1:11;
    end;
  end;
  hence thesis;
end;

theorem Th4:
  ex q' st p' = (p'|n)^q'
proof
   now per cases;
    suppose n > len p';
      then len p' c= n & dom p'=len p' by CARD_1:56,AFINSQ_1:def 1;
      then p'|n=p' & p'^{}=p' & {} is XFinSequence by RELAT_1:97,AFINSQ_1:32;
      hence thesis;
    end;
    suppose A1:n <= len p';
       defpred P[Nat] means for k st k= len p'-$1 holds ex q' st p'=(p'|k)^q';
    A2:P[0]
       proof
          len p'-0=dom p' by AFINSQ_1:def 1;
         then p'|(len p'-0)=p' & p'^{}=p' & {} is XFinSequence
           by RELAT_1:97,AFINSQ_1:32;
         hence thesis;
      end;
   A3:for m st P[m] holds P[m+1]
      proof
        let m such that A4:P[m];
        let k such that A5:k = len p'-(m+1);
        consider q' such that
     A6:p'=(p'|(k+1))^q' by A4,A5;
         len p'-m<=len p'-0 & p'=p'|(dom p') & len p'=dom p'
          by XREAL_1:12,RELAT_1:97,AFINSQ_1:def 1;
        then ((k+1=len p' & dom (p' | len p')=len p') or k+1<len p')
          by A5,REAL_1:def 5;
        then dom (p'|(k+1))=k+1 & dom (p'|(k+1))=len (p'|(k+1)) & k<=k+1
          by AFINSQ_1:def 1,14,NAT_1:11;
        then len (p'|(k+1))=k+1 & k c= k+1 & rng (p'|(k+1))<>{} &
          p'|(k+1) is XFinSequence of rng (p'|(k+1))
          by CARD_1:56,ORDINAL1:def 8,RELAT_1:65;
        then p'|(k+1)=(p'|(k+1))|k^<%(p'|(k+1)).k%> & (p'|(k+1))|k =p'|k
          by CARD_FIN:43,RELAT_1:103;
        then p'=(p'|k)^(<%(p'|(k+1)).k%>^q') &
         (<%(p'|(k+1)).k%>^q') is XFinSequence by A6,AFINSQ_1:30;
        hence thesis;
      end;
   A7:for m holds P[m] from NAT_1:sch 1(A2,A3);
      reconsider n1=len p'-n as Element of NAT by A1,NAT_1:21;
      reconsider n2=len p'-n1 as Element of NAT;
       ex q' st p'=(p'|n2)^q' by A7;
      hence thesis;
    end;
  end;
  hence thesis;
end;

theorem Th5:
  ex qd st pd = (pd|n)^qd
proof
  consider q' such that
A1:pd = (pd|n)^q' by Th4;
   rng q' c= rng pd & rng pd c= D by A1,AFINSQ_1:28,ORDINAL1:def 8;
  then rng q' c= D by XBOOLE_1:1;
  then q' is XFinSequence of D by ORDINAL1:def 8;
  hence thesis by A1;
end;

definition let p;
  attr p is dominated_by_0 means :Def1:
  rng p c= {0,1} & for k st k <= dom p holds 2*Sum (p|k) <= k;
end;

theorem Th6:
  p is dominated_by_0 implies 2*Sum (p|k) <= k
proof
  assume A1:p is dominated_by_0;
   now per cases;
    suppose k <= dom p;
      hence thesis by A1,Def1;
    end;
    suppose A2:k > dom p;
       dom p =len p by AFINSQ_1:def 1;
      then dom p c= k & dom p=len p by A2,CARD_1:56;
      then 2 * Sum (p|(len p)) <= dom p & p|(len p)=p & p|k=p
        by A1,Def1,RELAT_1:97;
      hence thesis by A2,XXREAL_0:2;
     end;
  end;
  hence thesis;
end;

theorem Th7:
  p is dominated_by_0 implies p.0 = 0
proof
  assume A1:p is dominated_by_0;
   now per cases;
    suppose not 0 in dom p;
      hence thesis by FUNCT_1:def 4;
    end;
    suppose 0 in dom p;
      then dom p<>0 & dom p=len p by AFINSQ_1:def 1;
      then len p >= 1 by NAT_1:14;
      then 1 c= len p & dom p=len p & 0 in 1
        by CARD_1:56,AFINSQ_1:def 1,EULER_1:1;
      then dom (p|1)=1 & 0 in dom p /\ 1 by RELAT_1:91,XBOOLE_0:def 3;
      then len (p|1)=1 & (p|1).0=p.0 by AFINSQ_1:def 1,FUNCT_1:71;
      then p|1=<%p.0%> & addnat "**" <%p.0%>= p.0 by AFINSQ_1:38,STIRL2_1:44;
      then 2*Sum <%p.0%> <=1 & Sum <%p.0%>= p.0 by A1,Th6;
      then 2*(p.0)<=1+0;
      then 2*(p.0)=1 or 2*(p.0)=0 by NAT_1:9;
      hence thesis by NAT_1:15;
    end;
  end;
  hence thesis;
end;

definition let k,m;
  redefine func k-->m -> XFinSequence of NAT;
  coherence
  proof
     dom (k-->m)=k by FUNCOP_1:19;
    then reconsider p=k-->m as XFinSequence by AFINSQ_1:7;
     rng p c={m} by FUNCOP_1:19;
    then rng p c= NAT by XBOOLE_1:1;
    hence thesis by ORDINAL1:def 8;
  end;
end;

Lm1:n <= m implies (m-->k)|n = (n-->k)
proof
  assume n <= m;
  then n c= m by CARD_1:56;
  then n/\m = n by XBOOLE_1:28;
  hence thesis by FUNCOP_1:18;
end;

Lm2:Sum (k-->m)=k*m
proof
   rng (k-->m)c={m} & {m}c={0,m} by FUNCOP_1:19,ZFMISC_1:12;
  then rng (k-->m) c= {0,m} & (k-->m)"{m}=k & card k=k
    by XBOOLE_1:1,FUNCOP_1:21,CARD_1:66;
  hence thesis by CARD_FIN:45;
end;

Lm3:(k-->0) is dominated_by_0
proof
   rng (k-->0) c={0} & {0} c= {0,1} by FUNCOP_1:19,ZFMISC_1:12;
  hence rng (k-->0) c={0,1} by XBOOLE_1:1;
  let n such that A1:n <= dom (k-->0);
   dom (k-->0)=k by FUNCOP_1:19;
  then (k-->0)|n =(n-->0) & Sum (n-->0) =0*n by A1,Lm1,Lm2;
  hence thesis;
end;

registration
  cluster empty dominated_by_0 XFinSequence of NAT;
  existence
  proof
     (0-->0) is dominated_by_0 by Lm3;
    hence thesis;
  end;
  cluster non empty dominated_by_0 XFinSequence of NAT;
  existence
  proof
     [:1,{0}:] <> {};
    then (1-->0)<>{} & (1-->0) is dominated_by_0 by Lm3,FUNCOP_1:def 2;
    hence thesis;
  end;
end;

theorem
   n-->0 is dominated_by_0 by Lm3;

Lm4:Sum(p^q)=Sum p + Sum q
proof
   addnat "**" (p ^ q) = addnat.(addnat "**" p,addnat "**" q) by STIRL2_1:47;
  hence thesis by BINOP_2:def 23;
end;

theorem Th9:
  n >= m implies (n-->0)^(m-->1) is dominated_by_0
proof
  assume A1:n>=m;
  set p=(n-->0)^(m-->1);
  thus rng p c= {0,1}
  proof
     rng (n-->0) c={0} & rng (m-->1) c= {1} & {0} c= {0,1} & {1} c= {0,1}
      by FUNCOP_1:19,ZFMISC_1:12;
    then rng (n-->0) c= {0,1} & rng (m-->1) c= {0,1} by XBOOLE_1:1;
    then rng (n-->0) \/ rng (m-->1) c= {0,1} by XBOOLE_1:8;
    hence thesis by AFINSQ_1:29;
  end;
  let k such that A2:k <= dom p;
   now per cases;
    suppose A3:k <= dom (n-->0);
       dom (n-->0)=n by FUNCOP_1:19;
      then p|k=(n-->0)|k & (n-->0)|k = (k-->0) & Sum (k-->0)=0*k
        by A3,Th2,Lm1,Lm2;
      hence 2*Sum(p|k)<=k;
    end;
    suppose k > dom (n-->0);
      then reconsider kd=k-dom (n-->0) as Element of NAT by NAT_1:21;
       dom p=len p by AFINSQ_1:def 1;
      then k<=len (n-->0)+len (m-->1) by A2,AFINSQ_1:20;
      then k-len (n-->0)<=len (m-->1)+len (n-->0)-len (n-->0) &
        dom (n-->0)= len (n-->0) by XREAL_1:11,AFINSQ_1:def 1;
      then kd <= len (m-->1)& len (m-->1) =dom (m-->1) by AFINSQ_1:def 1;
      then kd <= m & k=kd+dom (n-->0) by FUNCOP_1:19;
      then p|k = (n-->0)^((m-->1)|kd) & (m-->1)|kd = (kd-->1) by Th3, Lm1;
      then Sum(p|k)=Sum (n-->0) + Sum (kd-->1) & Sum (n-->0) =n*0 &
        Sum (kd-->1)=kd*1 by Lm2,Lm4;
      then A4:Sum(p|k)=kd & dom (n-->0)=n & dom p= len (n-->0) +len (m-->1) &
        len (n-->0)= dom (n-->0) & dom (n-->0)=n & len (m-->1)= dom (m-->1) &
        dom (m-->1)=m by FUNCOP_1:19,AFINSQ_1:def 1,def 4;
      then k-n <= m+n-n by A2,XREAL_1:11;
      then k-n <= n by A1,XXREAL_0:2;
      then k-n+(k-n)<=n+(k-n) by XREAL_1:8;
      hence 2 * Sum (p|k)<=k by A4;
    end;
  end;
  hence thesis;
end;

theorem Th10:
  p is dominated_by_0 implies p|n is dominated_by_0
proof
  assume A1:p is dominated_by_0;
   now per cases;
    suppose n<=len p;
       rng (p|n) c= rng p & rng p c= {0,1} by Def1,A1,RELAT_1:99;
      then A2:rng(p|n)c={0,1} by XBOOLE_1:1;
       for k st k<=dom (p|n) holds 2*Sum((p|n)|k)<=k
      proof
        let k such that A3:k <= dom (p|n);
         len (p|n) is Nat & len (p|n)=dom (p|n) by AFINSQ_1:def 1;
        then k c= dom (p|n) & dom (p|n) = dom p/\n by RELAT_1:90,A3,CARD_1:56;
        then k c= n & k c= dom p by XBOOLE_1:18;
        then (p|n)|k=p|k by RELAT_1:103;
        hence thesis by A1,Th6;
      end;
      hence thesis by A2,Def1;
    end;
    suppose n>len p;
      then dom p<=n & dom p in NAT by AFINSQ_1:def 1,ORDINAL1:def 13;
      then dom p c= n by CARD_1:56;
      hence thesis by A1,RELAT_1:97;
    end;
 end;
 hence thesis;
end;

theorem Th11:
  p is dominated_by_0 & q is dominated_by_0 implies p^q is dominated_by_0
proof
  assume A1:p is dominated_by_0 & q is dominated_by_0;
  then rng p c= {0,1} & rng q c= {0,1} by Def1;
  then rng p \/rng q c= {0,1} by XBOOLE_1:8;
  hence rng (p^q) c= {0,1} by AFINSQ_1:29;
  let k such that k <= dom (p^q);
   now per cases;
    suppose k <= dom p;
      then (p^q)|k=p|k & 2* Sum(p|k)<=k by A1,Def1,Th2;
      hence thesis;
    end;
    suppose k>dom p;
      then reconsider kd=k-dom p as Element of NAT by NAT_1:21;
       k=kd+dom p & q|kd is dominated_by_0 by A1,Th10;
      then (p^q)|k=p^(q|kd) & 2 * Sum (p|(len p)) <= len p & 2 * Sum (q|kd)<=
kd
         & len p=dom p & p|(dom p)=p by A1,Th3,Th6,AFINSQ_1:def 1,RELAT_1:98;
      then Sum((p^q)|k)=Sum p+Sum(q|kd) & 2* Sum p + 2 * Sum (q|kd)<=dom p+kd
        by Lm4,XREAL_1:9;
      hence thesis;
    end;
 end;
 hence thesis;
end;

theorem Th12:
  p is dominated_by_0 implies 2 * Sum (p|(2*n+1)) < 2*n+1
proof
  assume p is dominated_by_0;
  then A1:2 * Sum (p|(2*n+1)) <= 2 * n + 1 by Th6;
  assume 2 * Sum (p|(2*n+1))>= 2 * n + 1;
  then 2 * Sum (p|(2*n+1))= 2 * n + 1 by A1,XXREAL_0:1;
  then 2 * (Sum (p|(2*n+1))-n)=1;
  hence thesis by INT_1:22;
end;

theorem Th13:
  p is dominated_by_0 & n <= len p-2*Sum p implies
    p^(n-->1) is dominated_by_0
proof
  set q=(n-->1);
  assume A1:p is dominated_by_0 & n <= len p- 2 * Sum p;
   rng q c= {1} & {1} c= {0,1} by FUNCOP_1:19,ZFMISC_1:12;
  then rng p c= {0,1} & rng q c= {0,1} by A1,XBOOLE_1:1,Def1;
  then rng p \/ rng q c= {0,1} by XBOOLE_1:8;
  hence rng (p^q) c= {0,1} by AFINSQ_1:29;
  let m such that A2:m <= dom (p^q);
   now per cases;
    suppose m <= dom p;
      then (p^q)|m=p|m & 2*Sum (p|m) <= m by A1,Th2,Th6;
      hence thesis;
    end;
    suppose m > dom p;
      then reconsider md=m-dom p as Element of NAT by NAT_1:21;
       dom q = n & len q = dom q by FUNCOP_1:19,AFINSQ_1:def 1;
      then dom (p^q)= len p + n & dom p=len p by AFINSQ_1:def 4,def 1;
      then md+dom p<=n+dom p by A2;
      then A3:md <= n by XREAL_1:8;
      then q|md = md-->1 & Sum (md-->1)=md*1 & m=dom p+ md by Lm1,Lm2;
      then (p^q)|m=p^(md-->1) & Sum (p^(md-->1))=Sum p+ md by Th3,Lm4;
      then 2*Sum ((p^q)|m)= 2 * Sum p + m-dom p +md & n-n<=len p- 2 * Sum p-n
        by A1,XREAL_1:11;
      then 2*Sum ((p^q)|m) <=2 * Sum p+m-dom p+n & m-(len p-2 * Sum p-n)<=m-0
        & dom p=len p by A3,XREAL_1:8,12,AFINSQ_1:def 1;
      hence 2*Sum ((p^q)|m) <= m by XXREAL_0:2;
    end;
  end;
  hence thesis;
end;

theorem Th14:
  p is dominated_by_0 & n <= k+len p-2*Sum p implies
    (k-->0)^p^(n-->1) is dominated_by_0
proof
  assume A1:p is dominated_by_0 & n <= k + len p - 2 * Sum p;
  set q=(k-->0);  dom q=k & len q =dom q by FUNCOP_1:19,AFINSQ_1:def 1;
  then A2:len (q^p)=k+len p by AFINSQ_1:20;
   Sum q=k*0 & q is dominated_by_0 by Lm2,Lm3;
  then Sum (q^p)=0+ Sum p & q^p is dominated_by_0 by A1,Lm4,Th11;
  hence thesis by A1,A2,Th13;
end;

theorem Th15:
   p is dominated_by_0 & 2*Sum (p|k)=k implies k <= len p & len (p|k) = k
proof
  assume A1:p is dominated_by_0 & 2 * Sum (p|k)=k;
  A2:k <= len p
  proof
    assume A3:k > len p;
    then len p c= k & dom p=len p by CARD_1:56,AFINSQ_1:def 1;
    then p|k=p & p|len p=p by RELAT_1:97;
    hence thesis by A1,A3,Th6;
  end;
  then k c= len p & len p=dom p by CARD_1:56,AFINSQ_1:def 1;
  then dom p/\k=k by XBOOLE_1:28;
  then dom (p|k)=k by RELAT_1:90;
  hence thesis by A2,AFINSQ_1:def 1;
end;

theorem Th16:
  p is dominated_by_0 & 2*Sum (p|k) = k & p = (p|k)^q implies
    q is dominated_by_0
proof
  assume A1:p is dominated_by_0 & 2 * Sum (p|k)=k & p = (p|k)^q;
  then rng q c= rng p & rng p c= {0,1} by AFINSQ_1:28,Def1;
  hence rng q c= {0,1} by XBOOLE_1:1;
  let n such that n <= dom q;
   dom (p|k)= len (p|k) by AFINSQ_1:def 1;
  then p|(len (p|k)+n)=(p|k)^(q|n) by A1,Th3;
  then 2* Sum (p|(len (p|k)+n))<= len (p|k)+n &
    Sum (p|(len (p|k)+n))= Sum (p|k)+ Sum (q|n) by A1,Th6,Lm4;
  then k+ 2 * Sum (q|n) <= len (p|k)+n & len (p|k)=k by A1,Th15;
  hence 2 * Sum (q|n) <= n by XREAL_1:8;
end;

theorem Th17:
  p is dominated_by_0 & 2*Sum (p|k) = k & k = n+1 implies p|k = p|n^(1-->1)
proof
  assume A1:p is dominated_by_0 & 2 * Sum (p|k) = k & k = n+1;
  set q=p|k;
   n<=n+1 by NAT_1:11;
  then len q=n+1 & n c= k by A1,Th15,CARD_1:56;
  then A2:q=q|n^<%q.n%> & q|n = p|n by CARD_FIN:43,RELAT_1:103;
   q.n=1
  proof
    assume A3:q.n<>1;
     n<n+1 by NAT_1:13;
    then A4:q is dominated_by_0 & len q=n+1 & dom q=len q & n in n+1
      by A1,Th10,Th15,AFINSQ_1:def 1,EULER_1:1;
    then rng q c= {0,1} & q.n in rng q by Def1,FUNCT_1:12;
    then q.n=0 & q=(q|n)^<%q.n%> by A3,A4,TARSKI:def 2,CARD_FIN:43;
    then A5:Sum q= Sum(q|n) +Sum <%0%> & Sum <%0%>=0 by Lm4,STIRL2_1:44;
     Sum (p|k)<>0 by A1;
    then Sum (p|k) >=1 by NAT_1:14;
    then reconsider s=Sum (p|k)-1 as Element of NAT by NAT_1:21;
     2*s+1=n by A1;
    then 2*Sum(q|n)<n & n<k by A1,A4,NAT_1:13,Th12;
    hence thesis by A5,A1;
  end;
  then dom <%q.n%>=1 & rng <%q.n%>={1} by AFINSQ_1:36;
  hence thesis by A2,FUNCOP_1:15;
end;

theorem Th18:
  for m,p st m = min*({n:2*Sum(p|n) = n & n > 0}) & m > 0 & p is dominated_by_0
    ex q st p|m = (1-->0)^q^(1-->1) & q is dominated_by_0
proof
  let m,p such that A1:m = min*({n: 2*Sum(p|n)=n & n>0}) & m > 0 and
                    A2:p is dominated_by_0;
  reconsider M={n: 2*Sum(p|n)=n & n>0} as non empty Subset of NAT
    by A1,NAT_1:def 1;
   min* M in M by NAT_1:def 1;
  then consider n such that
A3:n=min*M & 2*Sum(p|n)=n & n>0;
  reconsider n1=n-1 as Element of NAT by A3,NAT_1:20;
   Sum(p|n)<>0 by A3;
  then Sum(p|n)>=1 by NAT_1:14;
  then n>=2*1 by A3,XREAL_1:66;
  then A4:n1>=2-1 by XREAL_1:11;
  consider q such that
A5:(p|n1) = (p|n1)|1 ^ q by Th5;
  take q;set q0=(1-->0);set q1=(1-->1);set qq=q0^q^q1;
   n <= len p & n1 < n1+1  by A2,A3,Th15,NAT_1:13;
  then A6:n1 < len p by XXREAL_0:2;
  then 1 < len p by A4,XXREAL_0:2;
  then len (p|1)=1 & p|1 is dominated_by_0 by A2,Th10,AFINSQ_1:14;
  then p|1 = <%(p|1).0%> & (p|1).0=0 & dom <%0%>=1 & rng <%0%>={0}
    & 1 c= n1 by A4,CARD_1:56,Th7,AFINSQ_1:36,38;
  then A7:p|1=1-->0 & (p|n1)|1=p|1 & p|(n1+1)=p|n1^(1-->1)
    by A2,A3,Th17,RELAT_1:103,FUNCOP_1:15;
  hence p|m =qq by A1,A3,A5;
   rng q c= rng (q0^q) & rng (q0^q) c= rng qq & p|m is dominated_by_0
    by A2,Th10,AFINSQ_1:27,28;
  then rng q c= rng qq & rng qq c= {0,1} by A1,A3,A5,XBOOLE_1:1,A7,Def1;
  hence rng q c= {0,1} by XBOOLE_1:1;
  let k such that A8:k <= dom q;
   len (p|n1)=n1 by A6,AFINSQ_1:14;
  then n1=len q0+ len q & len q=dom q by A5,A7,AFINSQ_1:20,def 1;
  then len q0 + k <= n1 by A8,XREAL_1:8;
  then dom q0=1 & len q0 +k c= n1 & len q0 = dom q0
    by FUNCOP_1:19,CARD_1:56,AFINSQ_1:def 1;
  then (p|n1)|(1+k)=q0^q|k & (p|n1)|(1+k) =p|(1+k) & 1+k <= n1 & n1<n
    by A5,A7,Th3,RELAT_1:103,CARD_1:56,NAT_1:13;
  then A9:Sum(p|(1+k))=Sum q0 + Sum (q|k) & 1+k <n & Sum (q0)=0*1
    by XXREAL_0:2,Lm2,Lm4;
   2*Sum(p|(1+k)) < 1+k
  proof
    assume A10:2*Sum(p|(1+k)) >= 1+k;
     2*Sum (p|(k+1))<=k+1 by A2,Th6;
    then 2*Sum(p|(1+k))=1+k & 1+k >0 by A10,XXREAL_0:1;
    then 1+k in M;
    hence thesis by A9,A3,NAT_1:def 1;
  end;
  hence thesis by A9,NAT_1:13;
end;

theorem Th19:
  for p st rng p c= {0,1} & p is not dominated_by_0
    ex k st 2*k+1 = min*{n : 2*Sum(p|n) > n}
          & 2*k+1 <= dom p & k = Sum (p|(2*k)) & p.(2*k) = 1
proof
  let p such that A1:rng p c= {0,1} and A2:p is not dominated_by_0;
  set M={n : 2*Sum(p|n) > n};
   M c= NAT
  proof
    let x such that A3:x in M;
     ex n st x=n & 2*Sum(p|n) > n by A3;
    hence thesis;
  end;
  then reconsider M as Subset of NAT;
  consider k such that
A4:k <= dom p & 2*Sum(p|k) > k by A1,A2,Def1;
   k in M by A4;
  then reconsider M as non empty Subset of NAT;
   min*M in M by NAT_1:def 1;
  then consider n such that
A5:min*M=n & 2*Sum(p|n) > n;
   n>0
  proof
    assume n<=0;
    then n=0;
    then p|n={} & dom {}={} & len {}=dom {} by RELAT_1:110,AFINSQ_1:def 1;
    then addnat "**" p|n = the_unity_wrt addnat by STIRL2_1:def 3;
    hence thesis by A5,BINOP_2:5;
  end;
  then reconsider n1=n-1 as Element of NAT by NAT_1:20;
  take Sum(p|n1);
   k in M by A4;
  then A6:k >= n by A5,NAT_1:def 1;
  then A7:dom p >= n by A4,XXREAL_0:2;
A8:2*Sum(p|n1) = n1
  proof
    assume A9:2*Sum(p|n1) <> n1;
     n1 < n1+1 by NAT_1:13;
    then not n1 in M & n1 < dom p by A5,A7,NAT_1:def 1,XXREAL_0:2;
    then A10:2*Sum(p|n1) <= n1 & n1 < len p by AFINSQ_1:def 1;
    then n1+1 <= len p by NAT_1:13;
    then ((n = len p & p|dom p = p) or n <len p) & dom p=len p & n1 < n1+1
      by REAL_1:def 5, RELAT_1:98,AFINSQ_1:def 1,NAT_1:13;
    then A11:len(p|n) = n1+1 & n1 c= n1+1 & n1 < n by AFINSQ_1:14,CARD_1:56;
    then A12:(p|n)=((p|n)|n1)^<%(p|n).n1%> & (p|n)|n1=p|n1
      by CARD_FIN:43,RELAT_1:103;
     n1 in len (p|n) & len (p|n)=dom (p|n) by A11,EULER_1:1,AFINSQ_1:def 1;
    then (p|n).n1 in rng(p|n) & rng (p|n)c= rng p by FUNCT_1:12,RELAT_1:99;
    then (p|n).n1 in {0,1} by A1,TARSKI:def 3;
    then ((p|n).n1 =0 or (p|n).n1 = 1)& Sum <%(p|n).n1%> = (p|n).n1
      &2*Sum(p|n1)<n1 by A9,A10,REAL_1:def 5,STIRL2_1:44,TARSKI:def 2;
    then A13:2*Sum(p|n1) + 2*Sum <%(p|n).n1%> < n1+2 by XREAL_1:10;
     Sum(p|n)=Sum(p|n1)+ Sum <%(p|n).n1%> by A12,Lm4;
    then 2*Sum(p|n1)+ 2* Sum <%(p|n).n1%> >= n+1 by A5,NAT_1:13;
    hence contradiction by A13;
  end;
   p.n1=1
  proof
    assume A14:p.n1<>1;
     n1 <n1+1 by NAT_1:13;
    then A15:n1 < dom p & n1 c= n & n1 in n & n <= len p
      by A7,XXREAL_0:2,AFINSQ_1:def 1,CARD_1:56,EULER_1:1;
    then n1 in dom p & n c= len p & len p=dom p
      by EULER_1:1,CARD_1:56,AFINSQ_1:def 1;
    then p.n1 in rng p & n1 in dom p/\n & dom(p|n)=n1+1 & dom(p|n)=len(p|n)
      by FUNCT_1:12,A15,XBOOLE_0:def 3,AFINSQ_1:def 1,RELAT_1:91;
    then p.n1=0 & Sum <%0%>=0 & (p|n)=((p|n)|n1)^<%(p|n).n1%> & (p|n).n1=p.n1
&
      (p|n)|n1=p|n1 by A1,A14,A15,CARD_FIN:43,FUNCT_1:71,RELAT_1:103,STIRL2_1:
44,TARSKI:def 2;
    then Sum (p|n)=Sum (p|n1) +0 & n1 <n1+1 by NAT_1:13,Lm4;
    hence thesis by A5,A8;
  end;
  hence thesis by A5,A6,A8,A4,XXREAL_0:2;
end;

theorem Th20:
  for p, q, k st p|(2*k+len q) = (k-->0)^q^(k-->1) &
    q is dominated_by_0 & 2*Sum q = len q & k > 0 holds
    min*{n:2*Sum(p|n) = n & n > 0} = 2*k+len q
proof
  let p,q,k such that A1:p|(2*k+len q)=(k-->0)^q^(k-->1) and
  A2:q is dominated_by_0 & 2*Sum q=len q and A3:k>0;
  set M={n:2*Sum(p|n)=n & n>0};
  A4:M c= NAT
    proof
      let y such that A5:y in M;
       ex i st i=y & 2*Sum(p|i)=i & i>0 by A5;
      hence thesis;
    end;
  set k0=k-->0;set k1=k-->1;set kqk=k0^q^k1;
   Sum kqk = Sum (k0^q) + Sum k1 by Lm4;
  then Sum kqk = Sum k0+Sum q+Sum k1 & Sum k0=k*0 & Sum k1=k*1 by Lm4,Lm2;
  then 2*Sum kqk=len q+2*k & len q >=0 & 2*k > 0 by A2,A3,XREAL_1:70;
  then 2*Sum (p|(2*k+len q))=len q+2*k & len q+2*k>0+0 by A1,XREAL_1:10;
  then A6:2*k+len q in M;
  then reconsider M as non empty Subset of NAT by A4;
   min*M=2*k+len q
  proof
    assume A7:min*M<>2*k+len q;
     kqk = k0^(q^k1) by AFINSQ_1:30;
    then len kqk = len k0+len (q^k1) & len k0 = dom k0 & dom k0 = k
      by AFINSQ_1:def 1,20,FUNCOP_1:19;
    then A8:len kqk = k+(len q+len k1) & len k1 = dom k1 & dom k1 = k
      & 2*k+len q >= min*M by A6,NAT_1:def 1,AFINSQ_1:def 1,
20,FUNCOP_1:19;
    then A9:min*M c= 2*k+len q  by CARD_1:56;
    then A10:p|min*M= kqk|min*M by A1,RELAT_1:103;
     min*M in M by NAT_1:def 1;
    then A11:ex i st i=min*M & 2*Sum(p|i)=i & i>0;
     now per cases;
      suppose A12:min*M <= k;
         k=dom k0 & kqk=k0^(q^k1) by FUNCOP_1:19,AFINSQ_1:30;
        then kqk|min*M=k0|min*M & k0|min*M =min*M-->0 by A12,Th2,Lm1;
        then Sum (p|min*M)=Sum (min*M-->0) & Sum (min*M-->0)=min*M * 0
          by A9,Lm2,A1,RELAT_1:103;
        hence contradiction by A11;
      end;
      suppose min*M > k;
        then reconsider mk=min*M-k as Element of NAT by NAT_1:21;
         now per cases;
          suppose A13:min*M <= k+len q;
             dom (k0^q)=len k0+len q & len k0 = dom k0 & dom k0 = k & min*M=mk+
k
              by AFINSQ_1:def 1,def 4,FUNCOP_1:19;
            then kqk|min*M=(k0^q)|min*M&(k0^q)|min*M=k0^(q|mk) by A13,Th2,Th3;
            then Sum(p|min*M)=Sum(k0)+Sum(q|mk) & Sum(k0)=k*0 by A10,Lm4,Lm2;
            then mk+k <= mk & 1 <= k by A3,A2,A11,Th6,NAT_1:14;
            hence contradiction by NAT_1:19;
          end;
          suppose min*M > k+len q;
            then reconsider mkL=min*M-(k+len q) as Element of NAT by NAT_1:21;
             dom (k0^q)=len k0 + len q & len k0=dom k0 & len kqk=2*k+len q &
              min*M < len kqk & dom k0=k
              by A8,AFINSQ_1:def 1,def 4,FUNCOP_1:19,A7,REAL_1:def 5;
            then min*M=dom(k0^q)+mkL & mkL < 2*k+len q-(k+len q) by XREAL_1:11
;
            then kqk|min*M=(k0^q)^(k1|mkL) & k1|mkL=mkL-->1 by Th3,Lm1;
            then Sum(p|min*M)=Sum(k0^q)+Sum (k1|mkL) & Sum(k1|mkL)=mkL*1 &
              Sum(k0^q)=Sum(k0)+Sum q & Sum k0=k*0 & 2*Sum(p|min*M)=min*M
              by A10,A11,Lm2,Lm4;
            hence contradiction by A2,A7;
          end;
        end;
        hence contradiction;
      end;
    end;
    hence contradiction;
  end;
  hence thesis;
end;

theorem Th21:
  for p st p is dominated_by_0 & {i: 2*Sum(p|i) = i & i > 0} = {} & len p > 0
    ex q st p = <%0%>^q & q is dominated_by_0
proof
  let p such that A1:p is dominated_by_0 and
                  A2:{i: 2*Sum(p|i)=i & i>0}={} & len p > 0;
  set M={i: 2*Sum(p|i)=i & i>0};
  consider q such that
A3:p=p|1^q by Th5;
  take q;
   len p >= 1 & dom p=len p by A2,NAT_1:14,AFINSQ_1:def 1;
  then 1 c= dom p by CARD_1:56;
  then A4:dom (p|1)=1 & 0 in 1 & len (p|1)=dom (p|1)
    by RELAT_1:91,EULER_1:1,AFINSQ_1:def 1;
  then A5:p|1=<%(p|1).0%> & (p|1).0=p.0 by AFINSQ_1:38,FUNCT_1:70;
  hence p=<%0%>^q by A3,A1,Th7;
   rng p c= {0,1} & rng q c= rng p by A1,A3,Def1,AFINSQ_1:28;
  then A6:rng q c= {0,1} by XBOOLE_1:1;
  assume q is not dominated_by_0;
  then consider i such that
A7:i <= dom q & 2*Sum(q|i) > i by A6,Def1;
   p|(1+i)=(p|1)^(q|i) by A3,Th3,A4;
  then Sum (p|(1+i))=Sum <%p.0%>+Sum (q|i) & Sum <%p.0%>=p.0 by A5,Lm4,
STIRL2_1:44;
  then A8:Sum (p|(1+i))=0+Sum (q|i) by A1,Th7;
  then 1+i >=2*Sum (q|i) & 2*Sum(q|i) >= i+1 by A7,A1,Th6,NAT_1:13;
  then 1+i =2*Sum (q|i) by XXREAL_0:1;
  then 1+i in M by A8;
  hence thesis by A2;
end;

theorem Th22:
  p is dominated_by_0 implies
    <%0%>^p is dominated_by_0 & {i: 2*Sum((<%0%>^p)|i) = i & i > 0} = {}
proof
  assume A1:p is dominated_by_0;
  set q=1-->0;
   dom q = 1 & len q = dom q & q is dominated_by_0
    by FUNCOP_1:19,AFINSQ_1:def 1,Lm3;
  then q = <%q.0%> & q.0 = 0 & q is dominated_by_0 by Th7,AFINSQ_1:38;
  hence <%0%>^p is dominated_by_0 by A1,Th11;
  set M={i: 2*Sum((<%0%>^p)|i)=i & i>0};
  assume M<>{};
  then consider x such that
A2:x in M by XBOOLE_0:def 1;
  consider i such that
A3:x=i & 2*Sum((<%0%>^p)|i)=i & i>0 by A2;
  reconsider i1=i-1 as Element of NAT by A3,NAT_1:20;
   dom <%0%>=1 by AFINSQ_1:36;
  then i=dom <%0%> +i1;
  then (<%0%>^p)|i=<%0%>^(p|i1) by Th3;
  then Sum ((<%0%>^p)|i)=Sum <%0%>+ Sum (p|i1) & Sum <%0%>=0 by Lm4,STIRL2_1:
44;
  then i=2*Sum (p|i1) & i1<i1+1 by A3,NAT_1:13;
  hence thesis by A1,Th6;
end;

theorem
   rng p c= {0,1} & p is not dominated_by_0 & 2*k+1 = min*{n:2*Sum(p|n) > n}
    implies p|(2*k) is dominated_by_0
proof
  set M={n : 2*Sum(p|n) > n};set q=p|(2*k);
  assume A1:rng p c= {0,1} & p is not dominated_by_0 & 2 * k + 1 = min*M;
  then reconsider M as non empty Subset of NAT by NAT_1:def 1;
   rng q c= rng p by RELAT_1:99;
  hence rng q c= {0,1} by A1,XBOOLE_1:1;
  let m such that A2:m <= dom q;
  assume A3:2*Sum(q|m) > m;
   dom q=len q by AFINSQ_1:def 1;
  then m c= dom q & dom q c= 2*k
    by A2,CARD_1:56,RELAT_1:87;
  then m c= 2*k by XBOOLE_1:1;
  then q|m=p|m & m<=2*k & 2*k<2*k+1 by RELAT_1:103,CARD_1:56,NAT_1:13;
  then m in M & m < 2*k+1 by A3,XXREAL_0:2;
  hence thesis by A1,NAT_1:def 1;
end;

begin

definition let n,m be Nat;
  func Domin_0(n,m) -> Subset of {0,1}^omega means :Def2:
    x in it iff ex p be XFinSequence of NAT st
      p = x & p is dominated_by_0 & dom p = n & Sum p = m;
existence
    proof
      defpred Q[set] means
         ex p st p=$1 & p is dominated_by_0 & dom p = n & Sum p=m;
      consider X such that
A1:   x in X iff x in bool [:NAT,NAT:] & Q[x] from XBOOLE_0:sch 1;
       X c= {0,1}^omega
      proof
        let x such that A2:x in X;
        consider p such that
    A3: p=x & p is dominated_by_0 & dom p = n & Sum p=m by A1,A2;
         rng p c= {0,1} by A3,Def1;
        then p is XFinSequence of {0,1} by ORDINAL1:def 8;
        hence thesis by A3,AFINSQ_1:46;
      end;
      then reconsider X as Subset of {0,1}^omega;
      take X;let x;
      thus x in X implies Q[x] by A1;
      given p such that
        A4:p=x & p is dominated_by_0 & dom p = n & Sum p=m;
       dom p c= NAT & rng p c= NAT by ORDINAL1:def 8;
      then p c= [:dom p,rng p:] & [:dom p,rng p:] c= [:NAT,NAT:]
        by RELAT_1:21,ZFMISC_1:119;
      then p c= [:NAT,NAT:] by XBOOLE_1:1;
      hence x in X by A1,A4;
   end;
uniqueness
   proof
     let X1,X2 be Subset of {0,1}^omega such that
  A5:x in X1 iff ex p st p=x & p is dominated_by_0 & dom p = n &Sum p=m and
  A6:x in X2 iff ex p st p=x & p is dominated_by_0 & dom p = n & Sum p=m;
      x in X1 iff x in X2
     proof
        x in X1 iff ex p st p=x & p is dominated_by_0&dom p=n&Sum p=m by A5;
       hence thesis by A6;
     end;
     hence thesis by TARSKI:2;
   end;
end;

theorem Th24:
  p in Domin_0(n,m) iff p is dominated_by_0 & dom p = n & Sum p = m
proof
  thus p in Domin_0(n,m) implies p is dominated_by_0 & dom p=n & Sum p=m
  proof
    assume p in Domin_0(n,m);
    then ex q st q=p & q is dominated_by_0 & dom q = n & Sum q = m by Def2;
    hence thesis;
  end;
  thus thesis by Def2;
end;

theorem Th25:
  Domin_0(n,m) c= Choose(n,m,1,0)
proof
  let x such that A1:x in Domin_0(n,m);
  consider p such that
A2:p = x & p is dominated_by_0 & dom p = n & Sum p = m by A1,Def2;
   rng p c= {0,1} by A2,Def1;
  hence thesis by A2,CARD_FIN:46;
end;

registration
  let n,m;
  cluster Domin_0(n,m) -> finite;
  coherence
  proof
     Choose(n,m,1,0) is finite & Domin_0(n,m) c= Choose(n,m,1,0) by Th25;
    hence thesis by FINSET_1:13;
  end;
end;

theorem Th26:
  Domin_0(n,m) is empty iff 2*m > n
proof
  thus Domin_0(n,m) is empty implies 2 * m > n
  proof
    assume A1:Domin_0(n,m) is empty;
    assume A2:2*m <= n;
     m<=m+m by NAT_1:12;
    then m <= n by A2,XXREAL_0:2;
    then reconsider nm=n-m as Element of NAT by NAT_1:21;
    set p=nm-->0; set q=m-->1;
     dom (p^q)=len p+len q & len p=dom p & len q=dom q & dom p=nm & dom q=m &
      Sum p=0*nm & Sum q =1*m & Sum (p^q)=Sum p+ Sum q & 2*m-m <= nm
      by A2,AFINSQ_1:def 1,def 4,FUNCOP_1:19,Lm2,Lm4,XREAL_1:11;
    then dom (p^q)=nm+m & Sum (p^q)=m & p^q is dominated_by_0 by Th9;
    hence thesis by A1,Def2;
  end;
  assume A3:2 * m > n;
  assume Domin_0(n,m) is non empty;
  then consider x such that
A4:x in Domin_0(n,m) by XBOOLE_0:def 1;
  consider p such that
A5:p = x & p is dominated_by_0 & dom p = n & Sum p = m by A4,Def2;
   p|n=p by A5,RELAT_1:98;
  hence thesis by A3,A5,Th6;
end;

theorem Th27:
  Domin_0(n,0) = {n-->0}
proof
   set p=n-->0;
A1:Domin_0(n,0) c= {p}
   proof
     let x such that A2:x in Domin_0(n,0);
     consider q such that
  A3:x=q & q is dominated_by_0 & dom q = n & Sum q =0 by A2,Def2;
      len q=n by A3,AFINSQ_1:def 1;
     then for x st x in dom q holds q.x=0 by A3,STIRL2_1:53;
     then q= n-->0 by A3,FUNCOP_1:17;
     hence thesis by A3,TARSKI:def 1;
   end;
    {p} c= Domin_0(n,0)
   proof
     let x such that A4:x in {p};
      dom p=n & Sum p=n*0 & p is dominated_by_0  by FUNCOP_1:19,Lm2,Lm3;
     then p in Domin_0(n,0) by Def2;
     hence thesis by A4,TARSKI:def 1;
   end;
   hence {n-->0} = Domin_0(n,0) by A1,XBOOLE_0:def 10;
end;

theorem Th28:
  card Domin_0(n,0) = 1
proof
   Domin_0(n,0)={n-->0} by Th27;
  hence thesis by CARD_1:50;
end;

theorem Th29:
  for p,n st rng p c= {0,n}
    ex q st len p = len q & rng q c= {0,n} &
      (for k st k <= len p holds Sum (p|k)+Sum (q|k) = n*k) &
      for k st k in len p holds q.k = n-p.k
proof
  let p,n such that A1:rng p c= {0,n};
  defpred P[set,set] means for k st k=$1 holds $2=n-p.k;
  A2:for k st k in len p ex x be Element of {0,n} st P[k,x]
  proof
    let k such that A3:k in len p;
     k in dom p by A3,AFINSQ_1:def 1;
    then p.k in rng p by FUNCT_1:12;
    then p.k=0 or p.k=n by A1,TARSKI:def 2;
    then n-p.k in {0,n} & P[k,n-p.k] by TARSKI:def 2;
    hence thesis;
  end;
  consider q be XFinSequence of {0,n} such that
A4:dom q=len p& for k st k in len p holds P[k,q.k] from STIRL2_1:sch 6(A2);
   rng q c= {0,n} & {0,n} is Subset of NAT by ORDINAL1:def 8;
  then rng q c= NAT by XBOOLE_1:1;
  then reconsider q as XFinSequence of NAT by ORDINAL1:def 8;
  take q;
  thus len p=len q by A4,AFINSQ_1:def 1;
  thus rng q c= {0,n} by ORDINAL1:def 8;
  defpred Q[Element of NAT] means
    $1 <= len p implies Sum (p|$1)+ Sum (q|$1)=n*$1;
  A5:Q[0]
  proof
    assume 0 <= len p;
     dom {}={};
    then len {}=0 & p|0={} & q|0={} & the_unity_wrt addnat=0
      by RELAT_1:110,AFINSQ_1:def 1, BINOP_2:5;
    then addnat "**" (p|0)= 0 & addnat "**" (q|0)= 0 by STIRL2_1:def 3;
    hence thesis;
  end;
  A6:for k st Q[k] holds Q[k+1]
  proof
    let k such that A7:Q[k];set k1=k+1;
    assume A8:k+1 <= len p;
    then k1 c= len p & len p=dom p by CARD_1:56,AFINSQ_1:def 1;
    then dom (p|k1)=k1 & dom (q|k1)=k1 & k<k+1 by A4,RELAT_1:91,NAT_1:13;
    then len (p|k1)=k1 & len (q|k1)=k1 & k in dom (p|k1) & k in dom (q|k1) &
      k c= k+1 & k< len p by A8,AFINSQ_1:def 1,EULER_1:1,CARD_1:56,XXREAL_0:2;
    then (p|k1)=((p|k1)|k)^<%(p|k1).k%> & (p|k1).k= p.k & (p|k1)|k=p|k &
     (q|k1)=((q|k1)|k)^<%(q|k1).k%> & (q|k1).k= q.k & (q|k1)|k=q|k & k in len p
      by CARD_FIN:43,FUNCT_1:70,RELAT_1:103,EULER_1:1;
    then Sum (p|k1)=Sum (p|k)+Sum <%p.k%> & q.k=n-p.k &
      Sum (q|k1)=Sum (q|k)+Sum <%q.k%> by Lm4,A4;
    then Sum (p|k1)=Sum (p|k)+p.k & Sum (q|k1)=Sum (q|k)+ (n-p.k) by STIRL2_1:
44;
    hence thesis by A7,A8,NAT_1:13;
  end;
   for k holds Q[k] from NAT_1:sch 1(A5,A6);
  hence thesis by A4;
end;

theorem Th30:
  m <= n implies n choose m > 0
proof
  assume A1:m<=n;
  then reconsider nm=n-m as Nat by NAT_1:21;
   m!>0 & nm!>0 by NEWTON:23;
  then (m!)*(nm!)>(m!)*0 & n!>0*((m!)*(nm!)) by XREAL_1:70,NEWTON:23;
  then (n!)/((m!)*(nm!))>0 by XREAL_1:83;
  hence thesis by A1,NEWTON:def 3;
end;

theorem Th31:
  2*(m+1) <= n implies
    card (Choose(n,m+1,1,0) \ Domin_0(n,m+1)) = card Choose(n,m,1,0)
proof
  assume A1:2 * (m+1) <= n;
  defpred P[set,set] means
    for p,k st $1 = p & 2 * k + 1= min*{i:2*Sum (p|i)>i}
      ex r1,r2 be XFinSequence of NAT st $2=r1^r2 &
        len r1=2*k+1 & len r1=len (p|(2*k+1)) & p=(p|(2*k+1))^r2 &
        (for o be Nat st o < 2*k+1 holds r1.o=1-p.o);

  set CH=Choose(n,m+1,1,0);
  set Z=Domin_0(n,m+1);
   m<=m+1 by NAT_1:13;
  then 2*m <= 2*(m+1) by XREAL_1:66;
  then 2*m <= n & m <= m+m by A1,XXREAL_0:2,XREAL_1:33;
  then m<= n & card n=n by CARD_1:66,XXREAL_0:2;
  then (card n) choose m>0 by Th30;
  then reconsider
    W=Choose(n,m,1,0) as non empty finite set by CARD_FIN:18,CARD_1:78;
  A2:for x st x in CH\Z ex y st y in W & P[x,y]
  proof
    let x such that A3: x in CH\Z;
     x in CH by A3,XBOOLE_0:def 4;
    then consider p be XFinSequence of NAT such that
 A4:p = x & dom p = n & rng p c= {0,1} & Sum p=m+1 by CARD_FIN:46;
     not p in Z by A3,A4,XBOOLE_0:def 4;
    then p is not dominated_by_0 by A4,Def2;
    then consider o be Element of NAT such that
 A5:2 * o + 1 = min*{i : 2*Sum(p|i) > i}
      & 2 * o + 1 <= dom p & o = Sum (p|(2*o)) & p.(2*o) = 1 by A4,Th19;
    set q=p|(2*o+1);
     rng q c= rng p by RELAT_1:99;
    then rng q c= {0,1} by A4,XBOOLE_1:1;
    then consider r1 be XFinSequence of NAT such that
 A6:len q=len r1 & rng r1 c= {0,1} &
         (for i st i <=len q holds Sum (q|i)+ Sum (r1|i)= 1*i) &
         for i st i in len q holds r1.i=1-q.i by Th29;
     q|dom q=q & r1|dom r1=r1 & dom q=len q & len q=dom r1
      by RELAT_1:98,AFINSQ_1:def 1,A6;
    then A7:Sum q+Sum r1=1*len q by A6;
    consider r2 be XFinSequence of NAT such that
      A8:p=q^r2 by Th5;
    A9:m+1= Sum q + Sum r2 by A4,A8,Lm4;
     dom p=len p by AFINSQ_1:def 1;
    then A10:2*o+1 c= dom p by A5,CARD_1:56;
    then A11:dom q=2*o+1 & 2*o <2*o+1 by RELAT_1:91,NAT_1:13;
    then len q=2*o+1 & 2*o c= 2*o+1 & 2*o in 2*o+1 & len q=dom q
      by AFINSQ_1:def 1,CARD_1:56,EULER_1:1;
    then q=(q|(2*o))^<%q.(2*o)%> & q|(2*o)=p|(2*o) & q.(2*o)=p.(2*o)
      by CARD_FIN:43,RELAT_1:103,FUNCT_1:70;
    then Sum q=Sum (p|(2*o))+Sum <%p.(2*o)%> & Sum <%p.(2*o)%>=1 by A5,Lm4,
STIRL2_1:44;
    then Sum q = o+1 & Sum q+ Sum r1= 2*o+1 by A5,A7,A11,AFINSQ_1:def 1;
    then Sum r1+Sum r2=m+1-(2*o+1)+2*o by A9;
    then A12:Sum (r1^r2)=m by Lm4;
take R=r1^r2;
     len p=len r1+len r2 & len p =n by A4,A6,A8,AFINSQ_1:20,def 1;
    then A13:dom R=n by AFINSQ_1:def 4;
     rng r2 c= rng p by A8,AFINSQ_1:28;
    then rng r2 c= {0,1} by A4,XBOOLE_1:1;
    then rng r1 \/ rng r2 c= {0,1} by A6,XBOOLE_1:8;
    then rng R c= {0,1} by AFINSQ_1:29;
hence R in W by A12,A13,CARD_FIN:46;
  thus P[x,R]
    proof
      let p' be XFinSequence of NAT, k such that
  A14:x=p' & 2 * k + 1= min*{i:2*Sum (p'|i)>i};
      take r1,r2;set q'=p'|(2*k+1);
      thus A15:R=r1^r2 &len r1=2*k+1 & len r1=len q' &p'=q'^r2
        by A6,A8,A4,A5,A14,A11,AFINSQ_1:def 1;
      thus for i be Nat st i < 2*k+1 holds r1.i=1-p'.i
      proof let i be Nat such that A16:i <2*k+1;
         i in len q & dom q=len q by A4,A5,A10,A16,A14,A15,EULER_1:1,RELAT_1:91
;
        then r1.i=1-q.i & p.i=q.i by A6,FUNCT_1:70;
        hence thesis by A4,A14;
      end;
    end;
  end;
  consider F be Function of CH\Z,W such that
A17: for x st x in CH\Z holds P[x,F.x] from FUNCT_2:sch 1(A2);

A18:F is one-to-one
  proof
    let x,y such that A19:x in dom F & y in dom F and A20:F.x=F.y;
     x in CH by A19,XBOOLE_0:def 4;
    then consider p such that
 A21:p = x & dom p = n & rng p c= {0,1} & Sum p=m+1 by CARD_FIN:46;
     not p in Z by A21,XBOOLE_0:def 4,A19;
    then p is not dominated_by_0 by A21,Def2;
    then consider k1 be Element of NAT such that
      A22:2 * k1 + 1 = min*{i : 2*Sum(p|i) > i}
         & 2*k1+1<=dom p & k1 = Sum (p|(2*k1)) & p.(2*k1)=1 by A21,Th19;
    reconsider K={i : 2*Sum(p|i) > i} as non empty Subset of NAT
      by A22,NAT_1:def 1;
    set pk=p|(2*k1+1);
    consider r1,r2 be XFinSequence of NAT such that
      A23:F.x=r1^r2 and
      A24:len r1=2*k1+1 & len r1=len (p|(2*k1+1)) & p=(p|(2*k1+1))^r2 and
      A25:for i be Nat st i < 2*k1+1 holds r1.i=1-p.i by A21,A22,A17,A19;
     y in CH by A19,XBOOLE_0:def 4;
    then consider q such that
A26:q = y & dom q = n & rng q c= {0,1} & Sum q=m+1 by CARD_FIN:46;
     not q in Z by A26,XBOOLE_0:def 4,A19;
    then q is not dominated_by_0 by A26,Def2;
    then consider k2 be Element of NAT such that
 A27:2 * k2 + 1 = min*{i : 2*Sum(q|i) > i}
      & 2*k2+1<=dom q & k2 = Sum (q|(2*k2)) & q.(2*k2)=1 by A26,Th19;
    reconsider M={i : 2*Sum(q|i) > i} as non empty Subset of NAT
      by A27,NAT_1:def 1;
    set qk=q|(2*k2+1);
    consider s1,s2 be XFinSequence of NAT such that
 A28:F.y=s1^s2 and
 A29:len s1=2*k2+1 & len s1=len qk & q=qk^s2 and
 A30:for i be Nat st i < 2*k2+1 holds s1.i=1-q.i by A26,A27,A17,A19;
    assume A31:x<>y;
     len q= len qk + len s2 & len p=len pk+ len r2 by A24,A29,AFINSQ_1:20;
    then A32:len q=len (s1^s2) & len p=len (r1^r2) & len p=n & len q=n
      by A21,A26,A24,A29,AFINSQ_1:def 1,20;
    then consider i such that
 A33:i < len p & p.i<>q.i by A21,A26,A31,AFINSQ_1:11;
     now per cases by REAL_1:def 5;
      suppose A34:k1=k2;
         now per cases;
          suppose A35:i< len pk;
            then i in len pk & len pk=len qk by A34,A24,A29,EULER_1:1;
            then i in dom r1 & i in dom s1 & r1.i=1-p.i &
              s1.i=1-q.i by A29,A24,A35,A25,A30,AFINSQ_1:def 1;
            then r1.i<> s1.i & r1.i=(r1^r2).i & s1.i=(s1^s2).i
              by A33,AFINSQ_1:def 4;
            hence contradiction by A20,A23,A28;
          end;
          suppose i>=len pk;
            then p.i=r2.(i-len pk) & q.i=s2.(i-len pk)&(r1^r2).i=r2.(i-len pk)
&
              (s1^s2).i=s2.(i-len pk) by A32,A33,A34,A24,A29,AFINSQ_1:22;
            hence contradiction by A20,A23,A28,A33;
          end;
        end;
        hence contradiction;
     end;
     suppose k1>k2;
       then 2*k2<2*k1 by XREAL_1:70;
       then A36:len s1 < len r1 by A24,A29,XREAL_1:8;
       then len s1 < dom r1 by AFINSQ_1:def 1;
       then (s1^s2)|(len s1)=r1|(len s1) & dom s1=len s1
         by A20,A23,A28,Th2,AFINSQ_1:def 1;
       then A37:s1=r1|(len s1) by Th1;
        len s1<=len s1+len s2&len (s1^s2)=len s1+len s2 by NAT_1:11,AFINSQ_1:20
;
       then len s1 <= len p & len p=dom p by A32,AFINSQ_1:def 1;
       then A38:len s1 c= dom p by CARD_1:56;
       then len qk=dom (p|len qk) by A29,RELAT_1:91;
       then A39:len qk= len (p|len qk) by AFINSQ_1:def 1;
        for k st k < len qk holds qk.k=(p|len qk).k
       proof
         let k such that A40:k<len qk;
         k<len s1&k<len r1&dom r1=len r1 by A36,A40,A29
,XXREAL_0:2,AFINSQ_1:def 1;
         then A41:s1.k=1-q.k & r1.k=1-p.k & k in len s1 & k in dom r1
           by A24,A29,A25,A30,EULER_1:1;
         then k in dom r1/\ len s1 & k in dom p & len q=dom q & len p=dom p
           by A38,XBOOLE_0:def 3,AFINSQ_1:def 1;
         then r1.k=(r1|len s1).k & k in dom p/\ len s1 & k in dom q/\len s1
           by A41,FUNCT_1:71,XBOOLE_0:def 3,A21,A26;
         then p.k=q.k & p.k=(p|len qk).k & q.k=qk.k by A29,A37,A41,FUNCT_1:71;
         hence thesis;
       end;
       then A42:qk=p|len qk by A39,AFINSQ_1:11;
        2*k2+1 in M by A27,NAT_1:def 1;
       then ex i st 2*k2+1=i & 2* Sum(q|i)>i;
       then len qk in K by A29,A42;
       hence contradiction by A29,A24,A22,A36,NAT_1:def 1;
     end;
     suppose k1<k2;
       then 2*k1<2*k2 by XREAL_1:70;
       then A43:len r1 < len s1  by A24,A29,XREAL_1:8;
       then len r1 < dom s1 by AFINSQ_1:def 1;
       then (r1^r2)|(len r1)=s1|(len r1) & dom r1=len r1
         by A20,A23,A28,Th2,AFINSQ_1:def 1;
       then A44:r1=s1|(len r1) by Th1;
        len r1<=len r1+len r2&len (r1^r2)=len r1+len r2 by NAT_1:11,AFINSQ_1:20
;
       then len r1 <= len q & len q=dom q by A32,AFINSQ_1:def 1;
       then A45:len r1 c= dom q by CARD_1:56;
       then len pk=dom (q|len pk) by A24,RELAT_1:91;
       then A46:len pk= len (q|len pk) by AFINSQ_1:def 1;
        for k st k < len pk holds pk.k=(q|len pk).k
       proof
         let k such that A47:k<len pk;
         k<len r1&k<len s1&dom s1=len s1 by A43,A47,A24
,XXREAL_0:2,AFINSQ_1:def 1;
        then A48:s1.k=1-q.k & r1.k=1-p.k & k in len r1 & k in dom s1
          by A24,A29,A25,A30,EULER_1:1;
        then k in dom s1/\ len r1 & k in dom q & len p=dom p & len q=dom q
          by A45,XBOOLE_0:def 3,AFINSQ_1:def 1;
        then s1.k=(s1|len r1).k & k in dom q/\ len r1 & k in dom p/\len r1
          by A48,FUNCT_1:71,XBOOLE_0:def 3,A21,A26;
        then p.k=q.k & q.k=(q|len pk).k & p.k=pk.k by A24,A44,A48,FUNCT_1:71;
        hence thesis;
      end;
      then A49:pk=q|len pk by A46,AFINSQ_1:11;
       2*k1+1 in K by A22,NAT_1:def 1;
      then ex i st 2*k1+1=i & 2* Sum(p|i)>i;
      then len pk in M by A24,A49;
      hence contradiction by A29,A24,A27,A43,NAT_1:def 1;
     end;
   end;
   hence contradiction;
 end;
A50: rng F = W
proof
  thus rng F c= W;
  thus W c= rng F
  proof
    let x such that A51:x in W;
    consider p such that
      A52:p = x & dom p = n & rng p c= {0,1} & Sum p=m by A51,CARD_FIN:46;
    set M={i:2*Sum (p|i)<i};
 A53:M c= NAT
    proof
      let y such that A54:y in M;
       ex i st i=y & 2*Sum(p|i)<i by A54;
      hence thesis;
    end;
     m<m+1 by NAT_1:13;
    then 2*m < 2*(m+1) by XREAL_1:70;
    then 2*m < n by A1,XXREAL_0:2;
    then 2*Sum (p|n)<n by A52,RELAT_1:98;
    then A55:n in M;
    then reconsider M as non empty Subset of NAT by A53;
     ex k st 2*k+1 = min*M & Sum (p|(2*k+1))=k & 2*k+1 <= dom p
    proof
      set mm=min*M;
       mm in M by NAT_1:def 1;
      then consider o be Element of NAT such that
   A56:mm=o & 2*Sum(p|o)<o;
      reconsider m1=mm-1 as Element of NAT by A56,NAT_1:20;
       mm <= dom p & dom p=len p by A55,A52,NAT_1:def 1,AFINSQ_1:def 1;
      then mm c= dom p by CARD_1:56;
      then dom(p|mm)=mm & m1<m1+1 by RELAT_1:91,NAT_1:13;
      then len(p|mm)=m1+1 & m1 in dom (p|mm) & m1 c= mm
        by AFINSQ_1:def 1,EULER_1:1,CARD_1:56;
      then p|mm=(p|mm)|m1 ^ <%(p|mm).m1%> & (p|mm).m1=p.m1 & (p|mm)|m1=p|m1
        by CARD_FIN:43,FUNCT_1:70,RELAT_1:103;
      then A57:Sum(p|mm)=Sum(p|m1)+Sum<%p.m1%> & Sum<%p.m1%>=p.m1 by Lm4,
STIRL2_1:44;
       m1 < m1+1 by NAT_1:13;
      then not m1 in M by NAT_1:def 1;
      then 2*Sum(p|m1)>=m1;
      then 2*Sum(p|m1)+2*p.m1 >= m1+0 by XREAL_1:9;
      then 2*Sum(p|mm)>=m1 & 2*Sum(p|mm)<m1+1 by A56,A57;
      then 2*Sum(p|mm)+1=mm & mm <= dom p by A55,A52,NAT_1:9,NAT_1:def 1;
      hence thesis;
    end;
    then consider k such that
 A58:2 * k + 1 = min*M & Sum (p|(2*k+1))=k & 2 * k + 1 <= dom p;
    set k1=2*k+1;
    consider q such that
A59:p=p|k1^q by Th5;
     rng (p|k1) c= rng p by RELAT_1:99;
    then rng (p|k1) c= {0,1} by A52,XBOOLE_1:1;
    then consider r be XFinSequence of NAT such that
A60:len r=len (p|k1) & rng r c= {0,1} and
A61:for i st i <= len (p|k1) holds Sum ((p|k1)|i)+Sum(r|i)=1*i and
A62:for i st i in len (p|k1) holds r.i=1-(p|k1).i by Th29;
     (p|k1)|dom (p|k1)=(p|k1) & r|dom r=r & dom(p|k1)=len(p|k1)&len (p|k1)=dom
r
      by RELAT_1:98,AFINSQ_1:def 1,A60;
    then A63:Sum (p|k1)+Sum r=1*len (p|k1) by A61;
    set rq=r^q;
     dom p=len p by AFINSQ_1:def 1;
    then k1 c= dom p by A58,CARD_1:56;
    then A64:dom (p|k1)=k1 by RELAT_1:91;
    then A65:len (p|k1)=k1 by AFINSQ_1:def 1;
     rng q c= rng p by A59,AFINSQ_1:28;
    then rng q c= {0,1} by A52,XBOOLE_1:1;
    then dom rq=len (p|k1)+len q & rng r\/rng q c= {0,1}
      by A60,AFINSQ_1:def 4,XBOOLE_1:8;
    then A66:dom rq=dom p & rng rq c= {0,1} by A59,AFINSQ_1:def 4,29;
    A67:k+Sum r=k1 & m=k+Sum q & Sum rq=Sum r+ Sum q & k1+1> k1
      & dom r=k1 by A52,A58,A59,Lm4,A60,A63,A65,NAT_1:13,AFINSQ_1:def 1;
    then Sum rq=m+1 & 2* Sum r > k1 & (r^q)|k1=r by Th1;
    then A68:rq in CH & rq is not dominated_by_0 & Sum rq=m+1 & len rq =n
      by Th6,A52,A66,CARD_FIN:46,AFINSQ_1:def 1;
    then rq in CH & not rq in Z by Th24;
    then A69:rq in CH\Z by XBOOLE_0:def 4;
    consider j be Element of NAT such that
A70:2 * j + 1 = min*{i : 2*Sum(rq|i) > i}
         & 2*j+1<=dom rq & j = Sum (rq|(2*j)) & rq.(2*j)=1
      by A66,A68,Th19;
    reconsider K={i : 2*Sum(rq|i) > i} as non empty Subset of NAT
      by A70,NAT_1:def 1;
    set rqj=rq|(2*j+1);set j1=2*j+1;
    consider r1,r2 be XFinSequence of NAT such that
A71:F.rq=r1^r2 and
A72:len r1=j1 & len r1=len rqj & rq=rqj^r2 and
A73:for i be Nat st i < j1 holds r1.i=1-rq.i by A69,A70,A17;
A74:j1=k1
      proof
         k1 < 2*Sum r & rq|k1=r by Th1,A67;
        then k1 in K;
        then A75:k1>=j1 by A70,NAT_1:def 1;
        then j1 c= k1 by CARD_1:56;
        then Sum ((p|k1)|j1)+Sum(r|j1)=j1*1 & (p|k1)|j1=p|j1
          by A75,A61,A65,RELAT_1:103;
        then A76:2*Sum(r|j1)=2*j1-2*Sum (p|j1);
         j1 in K by NAT_1:def 1,A70;
        then (ex i st i=j1 & 2*Sum(rq|i) > i);
        then j1+(j1-2*Sum (p|j1))>2*Sum (p|j1)+(j1-2*Sum (p|j1))
          by A76,A75,A67,Th2;
        then j1>2*Sum (p|j1) by XREAL_1:8;
        then j1 in M;
        then j1 >= k1 by A58,NAT_1:def 1;
        hence thesis by A75,XXREAL_0:1;
      end;
       r1^r2=p|k1^q
      proof
         dom rq=len r1+len r2 & dom rq=len(p|k1)+len q & dom rq=len(p|k1)+len q
&
          len (p|k1^q)=len (p|k1)+ len q & dom rq=len rq
          by A60,A72,AFINSQ_1:20,def 1,def 4;
        then A77:len (r1^r2)=len (p|k1^q) & len (p|k1^q)=len rq by AFINSQ_1:20;
         now let i such that A78:i < len (r1^r2);
           now per cases;
            suppose A79:i < len r1;
              then i in len r1 & len r1=len (p|k1)
                by A64,A72,A74,EULER_1:1,AFINSQ_1:def 1;
              then i in dom r1 & i in dom (p|k1) & i in dom r & i in len (p|k1
)
                by A60,AFINSQ_1:def 1;
              then (r1^r2).i=r1.i & (p|k1^q).i=(p|k1).i & rq.i=r.i &
                r.i=1-(p|k1).i by A62,AFINSQ_1:def 4;
              then (r1^r2).i=r1.i & (p|k1^q).i=-rq.i+1 & r1.i=1-rq.i
                by A79,A72,A73;
              hence (r1^r2).i=(p|k1^q).i;
            end;
            suppose i>= len r1;
              then (r1^r2).i=r2.(i-len r1) & rq.i=q.(i-len r) &
                rq.i=r2.(i-len r1) & (p|k1^q).i=q.(i-len r)
                by A72,A78,A77,AFINSQ_1:22,A60,A65,A74;
              hence (r1^r2).i=(p|k1^q).i;
            end;
          end;
          hence (r1^r2).i=(p|k1^q).i;
        end;
        hence thesis by A77,AFINSQ_1:11;
      end;
      then p=F.rq & rq in dom F by A59,A69,A71,FUNCT_2:def 1;
      hence thesis by A52,FUNCT_1:12;
    end;
  end;
   dom F=CH\Z by FUNCT_2:def 1;
  then (CH\Z),W are_equipotent by A18,A50,WELLORD2:def 4;
  hence thesis by CARD_1:21;
end;

theorem Th32:
  2*(m+1) <= n implies
    card Domin_0(n,m+1) = (n choose (m+1)) - (n choose m)
proof
  assume A1:2 * (m+1) <= n;
  set CH=Choose(n,m+1,1,0);set Z=Domin_0(n,m+1);set W=Choose(n,m,1,0);
   Z c= CH by Th25;
  then card (CH \ Z) = card CH - card Z by CARD_2:63;
  then card Z=card CH - card (CH \ Z) & 0<>1;
  then card Z=card CH-card W & card CH = (card n) choose (m+1) &
    card W = (card n) choose m & card n=n by A1,Th31,CARD_FIN:18,CARD_1:66;
  hence thesis;
end;

theorem Th33:
  2*m <= n implies
    card Domin_0(n,m) = (n+1-2*m) / (n+1-m) * (n choose m)
proof
  assume A1:2*m<=n;
   now per cases;
    suppose A2:m=0;
      then n+1-2*m=n+1 & n+1-m=n+1 & (n choose m)=1 by NEWTON:29;
      then (n+1-2*m)/(n+1-m)*(n choose m)=1 by XCMPLX_1:60;
      hence thesis by A2,Th28;
    end;
    suppose m>0;
      then reconsider m1=m-1 as Element of NAT by NAT_1:20;
       m<=m+m & m1<=m1+(1+m1+1) by NAT_1:11;
      then A3:m<=n & m1 <= n by A1,XXREAL_0:2;
      then reconsider nm=n-m as Element of NAT by NAT_1:21;
      set n'=n!;set m'=m!;set nm'=nm!;set nm1'=(nm+1)!;set m1'=m1!;
A4:   1/(m'*nm')=((nm+1)*1)/((m'*nm')*(nm+1)) by XCMPLX_1:92
               .=(nm+1)/(m'*(nm'*(nm+1)))
               .=(nm+1)/(m'*nm1') by NEWTON:21;
   1/(m1'*nm1')=((m1+1)*1)/((m1'*nm1')*(m1+1)) by XCMPLX_1:92
                 .=m/(nm1'*(m1'*(m1+1)))
                 .=m/(nm1'*((m1+1)!)) by NEWTON:21
                 .=-(-m)/(nm1'*m') by XCMPLX_1:191;
then A5:   1/(m'*nm')-1/(m1'*nm1')=(nm+1)/(m'*nm1')+(-m)/(m'*nm1')
                                by A4
                          .=((nm+1)+(-m))/(m'*nm1') by XCMPLX_1:63
                          .=(n+1-2*m)/(m'*(nm'*(nm+1))) by NEWTON:21
                          .=(1*(n+1-2*m))/((m'*nm')*(nm+1))
                          .=(1/(m'*nm'))*((n+1-2*m)/(nm+1)) by XCMPLX_1:77;
A6: n'/(m'*nm')-n'/(m1'*nm1')=n'*(1/(m'*nm'))-n'/(m1'*nm1') by XCMPLX_1:100
                         .=n'*(1/(m'*nm'))-n'*(1/(m1'*nm1')) by XCMPLX_1:100
                         .=n'*(1/(m'*nm')-1/(m1'*nm1'))
                         .=n'*((1/(m'*nm'))*((n+1-2*m)/(nm+1))) by A5
                         .=(n'*(1/(m'*nm')))*((n+1-2*m)/(nm+1))
                         .=((n'*1)/(m'*nm'))*((n+1-2*m)/(nm+1)) by XCMPLX_1:75;
       n-m1=nm+1;
      then n choose m=n'/(m'*nm') & n choose m1=n'/(m1'*nm1') & 2*(m1+1)<=n
        by A1,A3,NEWTON:def 3;
      hence thesis by A6,Th32;
    end;
  end;
  hence thesis;
end;

theorem Th34:
  card Domin_0(2+k,1) = k+1
proof
   2*1<=2+k by NAT_1:11;
  then card Domin_0(2+k,1)=(2+k+1-2*1)/(2+k+1-1)*((2+k) choose 1) by Th33
                         .=(k+1)/(2+k)*(2+k) by STIRL2_1:61
                         .=k+1 by XCMPLX_1:88;
  hence thesis;
end;

theorem
   card Domin_0(4+k,2) = (k+1)*(k+4)/2
proof
   2*2<=4+k by NAT_1:11;
  then card Domin_0(4+k,2)=(4+k+1-2*2)/(4+k+1-2)*((4+k) choose 2) by Th33
                         .=(k+1)/(k+3)*((4+k)*(4+k-1)/2) by STIRL2_1:61
                         .=((k+1)/(k+3)*(3+k))*(4+k)/2
                         .=(k+1)*(4+k)/2 by XCMPLX_1:88;
  hence thesis;
end;

theorem
   card Domin_0(6+k,3) = (k+1)*(k+5)*(k+6)/6
proof
   2*3<=6+k by NAT_1:11;
  then card Domin_0(6+k,3)=(6+k+1-2*3)/(6+k+1-3)*((6+k) choose 3) by Th33
                         .=(k+1)/(k+4)*((6+k)*(6+k-1)*(6+k-2)/6) by STIRL2_1:61
                         .=((k+1)/(k+4)*(4+k))*(5+k)*(6+k)/6
                         .=(k+1)*(5+k)*(6+k)/6 by XCMPLX_1:88;
  hence thesis;
end;

theorem Th37:
  card Domin_0(2*n,n) = ((2*n) choose n) / (n+1)
proof
   card Domin_0(2*n,n) = (2*n+1-2*n)/(2*n+1-n)*((2*n) choose n) by Th33
  .=(1*((2*n) choose n))/(n+1) by XCMPLX_1:75;
  hence thesis;
end;

theorem Th38:
  card Domin_0(2*n,n) = Catalan(n+1)
proof
   2*n+2 -' 2=2*n+2-2 & n+1 -'1 =n+1-1 by BINARITH:def 3;
  then 2*(n+1)-'2=2*n & n+1-'1=n &
    Catalan(n+1)=((2*(n+1) -' 2) choose ((n+1) -' 1))/(n+1) by CATALAN1:def 1;
  hence thesis by Th37;
end;

Lm5:D^omega is functional
  proof
    let x such that A1:x in D^omega;
    thus thesis by A1,AFINSQ_1:def 8;
  end;

definition let D;
  mode OMEGA of D -> functional non empty set means :Def3:
  for x st x in it holds x is XFinSequence of D;
existence
  proof
    reconsider D'OMEGA=D^omega as functional non empty set by Lm5;
    take D'OMEGA;
    thus thesis by AFINSQ_1:def 8;
  end;
end;

definition let D;
  redefine func D^omega -> OMEGA of D;
  coherence
  proof
    A1:D^omega is functional by Lm5;
     for x st x in D^omega holds x is XFinSequence of D by AFINSQ_1:def 8;
    hence thesis by A1,Def3;
  end;
  let F be OMEGA of D;
  mode Element of F ->  XFinSequence of D;
  coherence by Def3;
end;

reserve pN, qN for Element of NAT^omega;

theorem
   Card {pN:dom pN = 2*n & pN is dominated_by_0} = (2*n) choose n
proof
  set 2n=2*n;
  defpred P[set,set] means
    for i st i=$1 holds $2=Domin_0(2n,i);
  set D=bool ({0,1}^omega);
  A1:for k st k in n+1 ex x be Element of D st P[k,x]
  proof
    let k such that k in n+1;
    reconsider Z=Domin_0(2n,k) as Element of D;
    take Z;thus thesis;
  end;
  consider r be XFinSequence of D such that
    A2:dom r=n+1 & for k st k in n+1 holds P[k,r.k] from STIRL2_1:sch 6(A1);
  A3:for i st i in dom r holds r.i is finite
  proof
    let i such that A4:i in dom r;
     r.i=Domin_0(2n,i) by A2,A4;
    hence thesis;
  end;
  A5:for i,j be Element of NAT st
    i in dom r & j in dom r & i<>j holds r.i misses r.j
  proof
    let i,j be Element of NAT such that A6:i in dom r & j in dom r & i<>j;
    assume r.i meets r.j;
    then r.i/\r.j<>{} by XBOOLE_0:def 7;
    then consider x such that
      A7:x in r.i/\r.j by XBOOLE_0:def 1;
     r.i=Domin_0(2n,i) & x in r.i by A2,A6,A7,XBOOLE_0:def 3;
    then consider p such that
      A8:p = x & p is dominated_by_0 & dom p = 2n & Sum p = i by Def2;
     r.j=Domin_0(2n,j) & x in r.j by A2,A6,A7,XBOOLE_0:def 3;
    then ex q st q = x & q is dominated_by_0 & dom q = 2*n & Sum q = j by Def2
;
    hence thesis by A6,A8;
  end;
  consider Cardr be XFinSequence of NAT such that
    A9:dom Cardr = dom r and
    A10:for i st i in dom Cardr holds Cardr.i=Card (r.i) and
    A11:Card union rng r = Sum(Cardr) from STIRL2_1:sch 7(A3,A5);
  defpred Q[Element of NAT] means $1 <dom Cardr implies
                         Sum(Cardr|($1+1))=(2n choose $1);
  A12:Q[0]
  proof
    assume 0 <dom Cardr;
    then 0<len Cardr by AFINSQ_1:def 1;
    then A13:1<=len Cardr & len Cardr=dom Cardr & 0 in len Cardr
      by NAT_1:14,AFINSQ_1:def 1,EULER_1:1;
    then 1 c= dom Cardr by CARD_1:56;
    then dom (Cardr|1)=1 & 0 in 1 by RELAT_1:91,EULER_1:1;
    then len (Cardr|1)=1 & (Cardr|1).0=Cardr.0 by AFINSQ_1:def 1,FUNCT_1:70;
    then A14:Cardr|1=<%Cardr.0%> & Cardr.0 = Card (r.0) by A10,A13,AFINSQ_1:38;
     0 in n+1 by EULER_1:1;
    then r.0=Domin_0(2n,0) by A2;
    then Card (r.0)=1 by Th28;
    then Sum (Cardr|1)=1 & (2n choose 0)=1 by A14,NEWTON:29,STIRL2_1:44;
    hence thesis;
  end;
  A15:for i st Q[i] holds Q[i+1]
  proof
    let i such that A16:Q[i];set i1=i+1;
    assume A17:i+1< dom Cardr;
    then i1 in dom Cardr by EULER_1:1;
    then A18:Sum (Cardr|i1) + Cardr.i1 = Sum (Cardr|(i1+1)) &
      Cardr.i1=Card(r.i1)& r.i1=Domin_0(2n,i1) by A2,A9,A10,CARD_FIN:44;
     i1 <=n by NAT_1:13,A2,A9,A17;
    then 2*i1<=2n by XREAL_1:66;
    then Sum(Cardr|(i1+1))=(2n choose i)+((2n choose i1)-(2n choose i))
      by A18,A16,A17,NAT_1:13,Th32;
    hence thesis;
  end;
  A19:for i holds Q[i] from NAT_1:sch 1(A12,A15);
   n < dom Cardr & Cardr|(n+1)= Cardr by A2,A9,RELAT_1:98,NAT_1:13;
  then A20:Sum(Cardr)=(2n choose n) by A19;
  set Z={pN:dom pN = 2* n & pN is dominated_by_0};
  A21:Z c= union rng r
    proof
      let x such that A22:x in Z;
      consider pN such that
A23:   x=pN & dom pN = 2n & pN is dominated_by_0 by A22;
       pN in Domin_0(2*n,Sum pN) by A23,Th24;
      then 2*Sum pN <= 2n by Th26;
      then 1/2*(2*Sum pN)<= 1/2*(2 * n) by XREAL_1:66;
      then Sum pN < n+1 by NAT_1:13;
      then Sum pN in n+1 by EULER_1:1;
      then r.(Sum pN)=Domin_0(2n,Sum pN) & r.(Sum pN) in rng r
        by A2,FUNCT_1:12;
      then pN in r.(Sum pN) & r.(Sum pN) in rng r by A23,Th24;
      hence thesis by A23,TARSKI:def 4;
    end;
   union rng r c= Z
    proof
      let x such that A24:x in union rng r;
      consider y such that
        A25:x in y & y in rng r by A24,TARSKI:def 4;
      consider i be set such that
        A26:i in dom r & y = r.i by A25,FUNCT_1:def 5;
      reconsider i as Element of NAT by A26;
       y=Domin_0(2n,i) by A2,A26;
      then consider p such that
        A27:p=x & p is dominated_by_0 & dom p = 2n & Sum p = i by A25,Def2;
       p is Element of NAT^omega by AFINSQ_1:def 8;
      hence thesis by A27;
    end;
  hence thesis by A11,A20,A21,XBOOLE_0:def 10;
end;

theorem Th40:
  for n,m,k,j,l st j = n-2*(k+1) & l = m-(k+1)
    holds
  Card {pN : pN in Domin_0(n,m) & 2*(k+1) = min*{i:2*Sum(pN|i) = i & i > 0}}
      = card Domin_0(2*k,k) * card Domin_0(j,l)
proof
  let n,m,k,j,l such that A1:j=n-2*(k+1) & l=m-(k+1);
  set 2k1=2*(k+1);
  set F={pN:pN in Domin_0(n,m) & 2 * (k + 1) = min*{i: 2*Sum(pN|i)=i&i>0}};
  set Z1=Domin_0(2*k,k);
  set Z2=Domin_0(j,l);
  set q0=1-->0;set q1=1-->1;
  defpred P[set,set] means
    ex r1,r2 be XFinSequence of NAT st
      $1 = q0^r1^q1^r2 & len (q0^r1^q1) = 2*(k+1) & $2 = [r1,r2];
 A2:for x st x in F ex y st y in [:Z1,Z2:] & P[x,y]
 proof
   let x such that A3:x in F;
   consider pN such that
     A4:pN=x & pN in Domin_0(n,m) & 2k1=min*{i: 2*Sum(pN|i)=i & i>0} by A3;
   consider r2 be XFinSequence of NAT such that
     A5:pN = pN|2k1^r2 by Th5;
    2k1>2*0 & pN is dominated_by_0&{i:2*Sum(pN|i)=i&i>0}={i:2*Sum(pN|i)=i&i>0}
    by A4,Th24,XREAL_1:70;
   then consider r1 be XFinSequence of NAT such that
     A6:pN|2k1 = q0^r1^q1 & r1 is dominated_by_0 by A4,Th18;
   take [r1,r2];
    2k1>2*0 by XREAL_1:70;
   then reconsider M={i:2*Sum(pN|i)=i & i>0} as non empty Subset of NAT
     by A4,NAT_1:def 1;
    2k1 in M by A4,NAT_1:def 1;
   then A7:ex o be Element of NAT st o=2k1 & 2*Sum(pN|o)=o & o>0;
   then pN is dominated_by_0 & 2*Sum(pN|2k1)=2k1 by A4,Th24;
   then A8:len (pN|2k1)=2k1 & Sum(pN|2k1)=k+1 by Th15;
   then 2k1=len(q0^r1)+len q1 & len q1=dom q1 & dom q1 = 1
     by A6,AFINSQ_1:def 1,20,FUNCOP_1:19;
   then A9:2*k+1=len q0+ len r1 & len q0=dom q0 & dom q0=1
     by AFINSQ_1:def 1,20,FUNCOP_1:19;
    k+1=Sum (q0^r1) + Sum q1 & Sum q1=1*1 by A6,A7,Lm2,Lm4;
   then k=Sum q0+ Sum r1 & Sum q0=0*1 by Lm2,Lm4;
   then Sum r1=k & dom r1=2*k by A9,AFINSQ_1:def 1;
   then A10:r1 in Z1 by A6,Th24;
    dom pN=n & Sum pN=m & 2* Sum (pN|2k1)=2k1 & pN is dominated_by_0
     by A4,A7,Th24;
   then n=2k1+len r2 & m=k+1+Sum r2 & r2 is dominated_by_0 by Th16,
      A5,A8,AFINSQ_1:def 4,Lm4;
   then dom r2=j & Sum r2 =l & r2 is dominated_by_0 by A1,AFINSQ_1:def 1;
   then r2 in Z2 by Th24;
   hence thesis by A4,A5,A6,A8,A10,ZFMISC_1:def 2;
 end;
 consider f being Function of F,[:Z1,Z2:] such that
   A11:for x st x in F holds P[x,f.x] from FUNCT_2:sch 1(A2);
A12: [:Z1,Z2:]={} implies F={}
  proof
    assume [:Z1,Z2:]={};
    then (Z1={} or Z2={}) & Z1<>{} by Th26,ZFMISC_1:113;
    then 2*l > j by Th26;
    then 2*m-2*(k+1) > n-2*(k+1) by A1;
    then A13:2*m > n by XREAL_1:11;
    assume F<>{};
    then consider x such that
      A14:x in F by XBOOLE_0:def 1;
     ex pN st pN=x & pN in Domin_0(n,m) & 2k1=min*{i:2*Sum(pN|i)=i&i>0} by A14;
    hence thesis by A13,Th26;
  end;
  then A15:dom f=F by FUNCT_2:def 1;
   for x,y st x in F & y in F & f.x = f.y holds x = y
  proof
    let x,y such that A16:x in F & y in F and A17:f.x = f.y;
    consider x1,x2 be XFinSequence of NAT such that
       A18:x = q0^x1^q1^x2 and
       A19:len (q0^x1^q1) = 2k1 & f.x = [x1,x2] by A11,A16;
    consider y1,y2 be XFinSequence of NAT such that
       A20:y = q0^y1^q1^y2 and
       A21:len (q0^y1^q1) = 2k1 & f.y = [y1,y2] by A11,A16;
     x1=y1 & x2=y2 by A17,A19,A21,ZFMISC_1:33;
    hence thesis by A18,A20;
  end;
  then A22:f is one-to-one by A12,FUNCT_2:25;
   rng f=[:Z1,Z2:]
  proof
    thus rng f c= [:Z1,Z2:];
    let x such that A23:x in [:Z1,Z2:];
    consider x1,x2 such that
      A24:x1 in Z1 & x2 in Z2 & x=[x1,x2] by A23,ZFMISC_1:def 2;
    consider p such that
      A25:p=x1 & p is dominated_by_0 & dom p = 2*k & Sum p = k by A24,Def2;
    consider q such that
      A26:q=x2 & q is dominated_by_0 & dom q = j & Sum q = l by A24,Def2;
   set 0p1=q0^p^q1;
    len p=2*k by A25,AFINSQ_1:def 1;
   then 1<= 1+len p - 2 * Sum p by A25;
   then 0p1 is dominated_by_0 by A25,Th14;
   then A27:0p1^q is dominated_by_0 by A26,Th11;
    dom 0p1=len (q0^p)+ len q1 & len q1=dom q1 & dom q1=1
    by AFINSQ_1:def 1,def 4,FUNCOP_1:19;
   then A28:dom 0p1 = len q0+len p+1 & len q0 = dom q0 & dom q0 = 1
     by AFINSQ_1:20,def 1,FUNCOP_1:19;
   then A29:dom 0p1 = 1+2*k+1 by A25,AFINSQ_1:def 1;
    0p1=q0^(p^q1) by AFINSQ_1:30;
   then Sum 0p1=Sum q0+ Sum (p^q1) & Sum q0=1*0 & Sum q1 = 1*1 by Lm4,Lm2;
   then Sum 0p1=0+ (Sum p+ 1) by Lm4;
   then Sum (0p1^q)=k+1+l & dom (0p1^q)=len 0p1+len q & len 0p1=dom 0p1 &
     len q=dom q by A25,A26,Lm4,AFINSQ_1:def 1,def 4;
   then A30:0p1^q in Domin_0(n,m) by A1,A26,A29,A27,Th24;
    (0p1^q)|(2*1+len p)=0p1 & 2*Sum p=len p
     by A25,Th1,A28,AFINSQ_1:def 1;
   then min*{i:2*Sum((0p1^q)|i)=i & i>0}=2*1+len p & len p=2*k
     & 0p1^q is Element of NAT^omega by A25,AFINSQ_1:def 8,Th20;
   then A31:0p1^q in F by A30;
   then consider r1,r2 be XFinSequence of NAT such that
      A32:0p1^q = q0^r1^q1^r2 & len (q0^r1^q1) = 2k1
        & f.(0p1^q) = [r1,r2] by A11;
    dom (q0^r1^q1)=2k1 by A32,AFINSQ_1:def 1;
   then (0p1^q)|2k1=0p1 & (q0^r1^q1^r2)|2k1=(q0^r1^q1) by A29,Th1;
   then q0^p=q0^r1 & q=r2 by A32,AFINSQ_1:31;
   then p=r1 & q=r2 by AFINSQ_1:31;
   hence thesis by A24,A31,A15,FUNCT_1:12,A25,A26,A32;
  end;
  then F,[:Z1,Z2:] are_equipotent by A15,A22,WELLORD2:def 4;
  then Card F=Card [:Z1,Z2:] & Card [:Z1,Z2:] =card Z1*card Z2
    by CARD_1:21,CARD_2:65;
  hence thesis;
end;

:::::::::::::::::::::::::::::::::::::::::::::::
::        THE RECURRENCE RELATION FOR        ::
::           THE CATALAN NUMBERS             ::
:::::::::::::::::::::::::::::::::::::::::::::::

theorem Th41:
  for n,m st 2*m <= n
    ex CardF be XFinSequence of NAT st
      Card {pN: pN in Domin_0(n,m) & {i:2*Sum(pN|i)=i & i > 0}<>{}} = Sum CardF
      & dom CardF = m
      & for j st j < m holds
        CardF.j = card Domin_0(2*j,j) * card Domin_0(n-'2*(j+1),m -'(j+1))
proof
  let n,m such that A1:2*m <=n;
  set W={pN: pN in Domin_0(n,m) & {i: 2*Sum(pN|i)=i & i>0}<>{}};
  defpred P[set,set] means
    for j st j=$1 holds
    $2={pN : pN in Domin_0(n,m) & 2 * (j + 1) = min*{i: 2*Sum(pN|i) = i&i>0}};
  set Z=Domin_0(n,m);
  A2:for k st k in m ex x be Element of bool Z st P[k,x]
  proof
    let k such that k in m;
    set N={pN : pN in Domin_0(n,m) & 2*(k+1) = min*{i: 2*Sum(pN|i)=i & i>0}};
     N c= Z
    proof
      let x such that A3:x in N;
       ex pN st x=pN & pN in Z & 2*(k+1) = min*{i: 2*Sum(pN|i)=i&i>0} by A3;
      hence thesis;
    end;
    then reconsider N as Element of bool Z;
    take N;thus thesis;
  end;
  consider C be XFinSequence of bool Z such that
    A4:dom C=m & for k st k in m holds P[k,C.k] from STIRL2_1:sch 6(A2);
  A5:for k st k in dom C holds C.k is finite
  proof
    let k such that A6:k in dom C;
     C.k in rng C & rng C c= bool Z by A6,FUNCT_1:12,ORDINAL1:def 8;
    hence thesis by FINSET_1:13;
  end;
  A7:for i,j st i in dom C & j in dom C & i<>j holds C.i misses C.j
  proof
    let i,j such that A8:i in dom C & j in dom C & i<>j;
    assume C.i meets C.j;
    then C.i/\C.j<>{} by XBOOLE_0:def 7;
    then consider x such that
      A9:x in C.i/\C.j by XBOOLE_0:def 1;
     C.i={pN : pN in Domin_0(n,m) & 2 *(i+1) = min*{k: 2*Sum(pN|k) = k & k>0}}&
      x in C.i by A4,A8,A9,XBOOLE_0:def 3;
    then consider pN such that
      A10:x=pN & pN in Domin_0(n,m) & 2 *(i+1) = min*{k: 2*Sum(pN|k) = k & k>0}
;
     C.j={qN : qN in Domin_0(n,m) & 2 *(j+1) = min*{k: 2*Sum(qN|k) = k & k>0}}&
      x in C.j by A4,A8,A9,XBOOLE_0:def 3;
    then ex qN st x=qN & qN in Domin_0(n,m) &
      2*(j+1)=min*{k:2*Sum(qN|k)=k&k>0};
    hence thesis by A8,A10;
  end;
  consider CardC be XFinSequence of NAT such that
    A11:dom CardC = dom C and
    A12:for i st i in dom CardC holds CardC.i=Card (C.i) and
    A13:Card union rng C = Sum(CardC) from STIRL2_1:sch 7(A5,A7);
  take CardC;
  A14:W c= union rng C
  proof
    let x; assume x in W;
    then consider pN such that
      A15:x=pN & pN in Domin_0(n,m) & {i: 2*Sum(pN|i)=i & i>0}<>{};
    set I={i: 2*Sum(pN|i)=i & i>0};
     I c= NAT
    proof
      let y such that A16:y in I;
       ex i st i=y & 2*Sum(pN|i)=i & i>0 by A16;
      hence thesis;
    end;
    then reconsider I as non empty Subset of NAT by A15;
     min*I in I by NAT_1:def 1;
    then consider M be Element of NAT such that
      A17:min*I=M & 2*Sum (pN|M)=M & M>0;
     Sum (pN|M)<>0 by A17;
    then Sum (pN|M)>0;
    then reconsider Sum1=Sum (pN|M)-1 as Nat by NAT_1:20;
    consider q such that
      A18:pN=(pN|M)^q by Th5;
     Sum pN =Sum (pN|M)+Sum q by A18,Lm4;
    then m =Sum (pN|M)+Sum q by A15,Th24;
    then m >= Sum (pN|M) & Sum1+1 >Sum1 by NAT_1:11,13;
    then m > Sum1 by XXREAL_0:2;
    then Sum1 in m by EULER_1:1;
    then C.Sum1 in rng C &
      C.Sum1={qN:qN in Domin_0(n,m) & 2*(Sum1+1) = min*{i:2*Sum(qN|i)=i&i>0}}
      by A4,FUNCT_1:12;
    then pN in C.Sum1 & C.Sum1 in rng C by A15,A17;
    hence thesis by A15,TARSKI:def 4;
  end;
   union rng C c= W
  proof
    let x;assume x in union rng C;
    then consider y such that
      A19:x in y & y in rng C by TARSKI:def 4;
    consider j be set such that
      A20:j in dom C & C.j=y by A19,FUNCT_1:def 5;
    reconsider j as Element of NAT by A20;
     y={pN : pN in Domin_0(n,m) & 2 *(j+1)=min*{i:2*Sum(pN|i)=i&i>0}} by A4,A20
;
    then consider pN such that
      A21:x=pN & pN in Domin_0(n,m) & 2 *(j+1)=min*{i:2*Sum(pN|i)=i&i>0} by A19
;
     2*(j+1)<>0;
    then {i : 2 * Sum(pN | i)=i & i>0} <>{} & pN in NAT^omega
      by A21,NAT_1:def 1;
    hence thesis by A21;
  end;
  hence Card W = Sum CardC & dom CardC = m by A4,A11,A13,A14,XBOOLE_0:def 10;
  let j such that A22:j < m;
  set P={pN : pN in Domin_0(n,m) & 2 *(j+1)=min*{i:2*Sum(pN|i)=i&i>0}};
  A23:m >=j+1 by A22,NAT_1:13;
  then 2*(j+1)<= 2*m by XREAL_1:66;
  then 2*(j+1)<=n by A1,XXREAL_0:2;
  then A24:n-'2*(j+1)=n-2*(j+1) & m-'(j+1)=m-(j+1) by A23,BINARITH:50;
   j in dom C by A4,A22,EULER_1:1;
  then C.j=P & CardC.j =Card (C.j) by A4,A11,A12;
  hence thesis by A24,Th40;
end;

theorem Th42:
  for n st n > 0 holds
    Domin_0(2*n,n)={pN:pN in Domin_0(2*n,n) & {i:2*Sum(pN|i) = i & i > 0}<>{}}
proof
  let n such that A1:n>0;
  set P={pN: pN in Domin_0(2*n,n) & {i: 2*Sum(pN|i)=i & i>0}<>{}};
  thus Domin_0(2*n,n) c= P
  proof
    let x such that A2:x in Domin_0(2*n,n);
    consider p such that
      A3:x=p & p is dominated_by_0 & dom p = 2*n & Sum p = n by A2,Def2;
     2*Sum(p|(2*n))=2*n & n+n >0+0 by A1,A3,XREAL_1:10,RELAT_1:98;
    then 2*n in {i: 2*Sum(p|i)=i & i>0} & p in NAT^omega by AFINSQ_1:def 8;
    hence thesis by A2,A3;
  end;
  thus P c= Domin_0(2*n,n)
  proof
     let x such that A4:x in P;
      ex pN st x=pN & pN in Domin_0(2*n,n) & {i: 2*Sum(pN|i)=i & i>0}<>{} by A4
;
     hence thesis;
  end;
end;

theorem Th43:
  for n st n > 0 ex Catal be XFinSequence of NAT st
    Sum Catal = Catalan(n+1) &
    dom Catal = n &
    for j st j < n holds Catal.j = Catalan(j+1)* Catalan(n-'j)
proof
  let n such that A1:n>0;
  consider CardF be XFinSequence of NAT such that
    A2:Card {pN: pN in Domin_0(2*n,n) & {i:2*Sum(pN|i)=i&i>0}<>{}}=Sum CardF
    and
    A3:dom CardF = n and
    A4:for j st j < n holds
      CardF.j=card Domin_0(2*j,j)*card Domin_0(2*n-'2*(j+1),n-'(j+1)) by Th41;
  take CardF;
   Sum CardF= Card Domin_0(2*n,n) by A1,A2,Th42;
  hence Sum CardF=Catalan(n+1) & dom CardF =n by A3,Th38;
  let j such that A5:j < n;
   n-'j=n-j & n-j > j-j by A5,BINARITH:50,XREAL_1:11;
  then n-'j >0;
  then reconsider nj=(n-'j)-1 as Element of NAT by NAT_1:20;
  A6:j+1<=n by A5,NAT_1:13;
  then 2*(j+1)<=2*n by XREAL_1:66;
  then 2*n-'2*(j+1)=2*n-2*(j+1) & n-'(j+1)=n-(j+1) & n-j=n-'j
    by A5,A6,BINARITH:50;
  then A7:card Domin_0(2*n-'2*(j+1),n-'(j+1))=card Domin_0(2*nj,nj)
                                         .=Catalan(nj+1) by Th38;
   card Domin_0(2*j,j)=Catalan(j+1) by Th38;
  hence thesis by A4,A5,A7;
end;

theorem Th44:
  Card {pN:pN in Domin_0(n+1,m) & {i: 2*Sum(pN|i) = i & i > 0} = {}} =
    card Domin_0(n,m)
proof
  set F={pN: pN in Domin_0(n+1,m) & {i: 2*Sum(pN|i)=i & i>0}={}};
  set Z=Domin_0(n,m);
  defpred P[set,set] means
     ex p st $1=<%0%>^p & $2=p;
  A1:for x st x in F ex y st y in Z & P[x,y]
  proof
    let x;
    assume x in F;
    then consider pN such that
      A2:x=pN & pN in Domin_0(n+1,m) & {i: 2*Sum(pN|i)=i & i>0}={};
     pN is dominated_by_0 & dom pN=n+1 & len pN = dom pN
      by A2,Th24,AFINSQ_1:def 1;
    then consider q such that
      A3:pN=<%0%>^q & q is dominated_by_0 by A2,Th21;
    take q;
     Sum pN=Sum <%0%>+Sum q & Sum <%0%>=0 by A3,Lm4,STIRL2_1:44;
    then A4:Sum q=m by A2,Th24;
     dom pN=len <%0%>+ len q & len <%0%> =1 by A3,AFINSQ_1:def 4,37;
    then n+1=len q+1 by A2,Th24;
    then dom q=n by AFINSQ_1:def 1;
    hence thesis by A2,A3,A4,Th24;
  end;
  consider f being Function of F,Z such that
    A5:for x st x in F holds P[x,f.x] from FUNCT_2:sch 1(A1);
  A6: Z={} implies F={}
  proof
    assume A7:Z={};assume F<>{};
    then consider x such that
      A8:x in F by XBOOLE_0:def 1;
    consider pN such that
      A9:x=pN & pN in Domin_0(n+1,m) & {i: 2*Sum(pN|i)=i & i>0}={} by A8;
     dom pN=n+1 by A9,Th24;
    then pN|(n+1)=pN by RELAT_1:98;
    then A10:Sum(pN|(n+1))=m & pN is dominated_by_0 & 2*m >n by A7,A9,Th24,Th26
;
    then 2*m <= n +1 & 2*m >= n+1 by Th6,NAT_1:13;
    then 2*Sum(pN|(n+1)) =n+1 by A10,XXREAL_0:1;
    then n+1 in {i:2*Sum(pN|i)=i&i>0};
    hence thesis by A9;
  end;
  then A11:dom f=F by FUNCT_2:def 1;
   for x,y st x in F & y in F & f.x = f.y holds x = y
  proof
    let x,y such that A12:x in F & y in F & f.x = f.y;
     (ex p st x=<%0%>^p & f.x=p) & (ex q st y=<%0%>^q & f.y=q) by A5,A12;
    hence thesis by A12;
  end;
  then A13:f is one-to-one by A6,FUNCT_2:25;
   rng f=Z
  proof
    thus rng f c= Z;
    let x such that A14:x in Z;
    consider p such that
      A15:p = x & p is dominated_by_0 & dom p = n & Sum p = m by A14,Def2;
    set q=<%0%>^p;
     dom q=len <%0%>+len p & len p=dom p & Sum q=Sum <%0%>+Sum p
      by AFINSQ_1:def 1,def 4,Lm4;
    then dom q=1+n & Sum q=0+m & q is dominated_by_0
      by A15,Th22,AFINSQ_1:37,STIRL2_1:44;
    then q in Domin_0(n+1,m) & {i: 2*Sum(q|i)=i & i>0}={} & q in NAT^omega
      by Th22,Th24,A15,AFINSQ_1:def 8;
    then A16:q in F;
    then consider r be XFinSequence of NAT such that
      A17:q=<%0%>^r & f.q=r by A5;
     r=p by A17,AFINSQ_1:31;
    hence thesis by A11,A15,A16,A17, FUNCT_1:12;
  end;
  then F,Z are_equipotent by A11,A13,WELLORD2:def 4;
  hence thesis by CARD_1:21;
end;

theorem
   for n,m st 2*m <= n
    ex CardF be XFinSequence of NAT st
      card Domin_0(n,m) = Sum CardF + card Domin_0(n-'1,m) &
      dom CardF = m &
      for j st j < m holds
        CardF.j = card Domin_0(2*j,j) * card Domin_0(n-'2*(j+1),m-'(j+1))
proof
  let n,m such that A1:2*m <=n;
  set Zne={pN: pN in Domin_0(n,m) & {i: 2*Sum(pN|i)=i & i>0}<>{}};
  set Ze={pN: pN in Domin_0(n,m) & {i: 2*Sum(pN|i)=i & i>0}={}};
  set Z=Domin_0(n,m);
  A2:Ze c= Z
  proof
    let x;assume x in Ze;
    then ex pN st x=pN & pN in Domin_0(n,m) & {i: 2*Sum(pN|i)=i & i>0}={};
    hence thesis;
  end;
  then reconsider Ze as finite set by FINSET_1:13;
  A3:Zne c= Z
  proof
    let x;assume x in Zne;
    then ex pN st x=pN & pN in Domin_0(n,m) & {i: 2*Sum(pN|i)=i & i>0}<>{};
    hence thesis;
  end;
  then reconsider Zne as finite set by FINSET_1:13;
  A4:Z c= Ze \/ Zne
  proof
    let x such that A5:x in Z;
    consider p be XFinSequence of NAT such that
      A6:p = x & p is dominated_by_0 & dom p = n & Sum p = m by A5,Def2;
    reconsider p as Element of NAT^omega by AFINSQ_1:def 8;
    set I={i: 2*Sum(p|i)=i & i>0};
     now per cases;
      suppose I={};
        then p in Ze by A5,A6;
        hence x in Ze\/Zne by XBOOLE_0:def 2,A6;
      end;
      suppose I<>{};
        then p in Zne by A5,A6;
        hence x in Ze\/Zne by XBOOLE_0:def 2,A6;
      end;
    end;
    hence thesis;
  end;
  A7:Ze misses Zne
  proof
    assume Ze meets Zne;
    then consider x such that
      A8:x in Ze & x in Zne by XBOOLE_0:3;
    A9:(ex pN st pN=x & pN in Z & {i: 2*Sum(pN|i)=i & i>0}<>{}) by A8;
     (ex qN st qN=x & qN in Z & {i: 2*Sum(qN|i)=i & i>0}={}) by A8;
    hence thesis by A9;
  end;
   Ze\/ Zne c= Z by A2,A3,XBOOLE_1:8;
  then A10:Ze\/Zne=Z by A4,XBOOLE_0:def 10;
  consider C be XFinSequence of NAT such that
    A11:Card Zne = Sum C & dom C = m and
    A12:for j st j < m holds
        C.j=card Domin_0(2*j,j)*card Domin_0(n-'2*(j+1),m-'(j+1)) by A1,Th41;
  take C;
   now per cases;
    suppose A13:n=0;
      then 2*m=0 by A1;
      then len C=0 by A11,AFINSQ_1:def 1;
      then addnat"**"C = the_unity_wrt addnat & the_unity_wrt addnat = 0 &
        n-1<1-1 by A13,STIRL2_1:def 3,BINOP_2:5;
      hence card Z = Sum C + card Domin_0(n-'1,m) by A13,BINARITH:def 3;
    end;
    suppose A14:n>0;
      then reconsider n1=n-1 as Element of NAT by NAT_1:20;
       n=n1+1 & 1<=n by A14,NAT_1:14;
      then card Ze = card Domin_0(n1,m) & n1 = n-'1 by Th44,BINARITH:50;
      hence card Z = Sum C+card Domin_0(n-'1,m) by A7,A10,A11,CARD_2:53;
    end;
  end;
  hence thesis by A11,A12;
end;

theorem
   for n,k ex p st
    Sum p = card Domin_0(2*n+2+k,n+1) &
    dom p = k+1 &
    for i st i <= k holds p.i = card Domin_0(2*n+1+i,n)
proof
  let n,k;
  defpred P[set,set] means for j st $1=j holds $2=card Domin_0(2*n+1+j,n);
  A1:for i st i in k+1 ex x be Element of NAT st P[i,x]
  proof
    let i such that i in k+1;
     P[i,card Domin_0(2*n+1+i,n)];
    hence thesis;
  end;
  consider p such that
    A2:dom p=k+1 and
    A3:for i st i in k+1 holds P[i,p.i] from STIRL2_1:sch 6(A1);
    A4:for i st i <= k holds p.i = card Domin_0(2*n+1+i,n)
      proof
        let i such that A5:i <= k;
         i<k+1 by A5,NAT_1:13;
        then i in k+1 by EULER_1:1;
        hence thesis by A3;
      end;
  take p;
   now per cases;
    suppose A6:n=0;
       for x st x in dom p holds p.x = 1
      proof
        let x such that A7:x in dom p;
        reconsider i=x as Element of NAT by A7;
         p.i=card Domin_0(2*n+1+i,n) by A3,A2,A7;
        hence thesis by A6,Th28;
      end;
      then p=(k+1)-->1 by A2,FUNCOP_1:17;
      then Sum p=(k+1)*1 & card Domin_0(2*n+2+k,n+1)=k+1 by A6,Lm2,Th34;
      hence Sum p = card Domin_0(2*n+2+k,n+1);
    end;
    suppose n>0;
      then reconsider n1=n-1 as Element of NAT by NAT_1:20;
      defpred Q[Element of NAT] means for q st
        dom q = $1+1 & for i st i <= $1 holds q.i = card Domin_0(2*n+1+i,n)
        holds Sum q = card Domin_0(2*n+2+$1,n+1);
      A8:Q[0]
      proof
        let q such that
          A9:dom q= 0+1 & for i st i <= 0 holds q.i = card Domin_0(2*n+1+i,n);
        set CHn=(2*n+1) choose n;set CHn1=(2*n+1) choose n1;
        set CHn'1=(2*n+1) choose (n+1);set 2CHn'1=(2*n+2) choose (n+1);
        set 2CHn=(2*n+2) choose n;
         2*n+2=(2*n+1)+1 & n<=n+(n+1) & (2*n+1)-n=n+1 & n1+1=n & 2*(n+1)=2*n+2
         & 2*n <= 2*n+1 by NAT_1:11;
        then 2CHn'1 = CHn'1 + CHn & 2CHn = CHn + CHn1 & CHn = CHn'1 &
          card Domin_0(2*n+2,n+1)=2CHn'1-2CHn & q.0=card Domin_0(2*n+1+0,n) &
          card Domin_0(2*n+1,n1+1)=CHn-CHn1 & len q = 1
          by A9,NEWTON:30,32, Th32,AFINSQ_1:def 1;
        then card Domin_0(2*n+2,n+1)=q.0 & Sum <%q.0%>=q.0 & q=<%q.0%>
          by AFINSQ_1:38,STIRL2_1:44;
        hence thesis;
      end;
      A10:for j st Q[j] holds Q[j+1]
      proof
        let j such that A11:Q[j];set j1=j+1;
        let q such that
          A12:dom q=j1+1 & for i st i<=j1 holds q.i=card Domin_0(2*n+1+i,n);
        set CH1=(2*n+2+j) choose (n+1);set CH2=(2*n+2+j) choose (n1+1);
        A13:Sum(q|j1)=CH1-CH2
        proof
           j1 <=j1+1 by NAT_1:11;
          then j1 c= j1+1 by CARD_1:56;
          then A14:dom (q|j1)=j1 by A12,RELAT_1:91;
          A15:for i st i<=j holds (q|j1).i = card Domin_0(2*n+1+i,n)
          proof
            let i such that A16:i<=j;
             i<j1 & j<=j1 by A16,NAT_1:13;
            then i in dom (q|j1) & i<=j1 by A14,EULER_1:1;
            then (q|j1).i=q.i & q.i= card Domin_0(2*n+1+i,n) by A12,FUNCT_1:70
;
            hence thesis;
          end;
           2*(n+1)<=2*n+2+j by NAT_1:11;
          then card Domin_0(2*n+2+j,n+1)=CH1-CH2 by Th32;
          hence thesis by A11,A14,A15;
        end;
        set CH3=(2*n+1+j1) choose n;set CH4=(2*n+1+j1) choose n1;
        A17:2*(n+1)<=2*(n+1)+j1 by NAT_1:11;
         q.j1=card Domin_0(2*n+1+j1,n)&2*n<=2*n+(1+j1) & n1+1=n by A12,NAT_1:11
;
        then q.j1=CH3-CH4 by Th32;
        then A18:Sum(q|j1)+q.j1=CH1+CH2-(CH3+CH4) by A13
          .=((2*n+2+j+1) choose (n+1))-(CH3+CH4) by NEWTON:32
          .=((2*n+2+j1) choose (n+1))-((2*n+2+j1) choose (n1+1)) by NEWTON:32
          .=card Domin_0(2*n+2+j1,n+1) by A17,Th32;
         j1 <j1+1 by NAT_1:13;
        then j1 in dom q by EULER_1:1,A12;
        then Sum (q|(j1+1))=Sum (q|j1) + q.j1 & q|(j1+1)=q
          by CARD_FIN:44,A12,RELAT_1:98;
        hence thesis by A18;
      end;
       for j holds Q[j] from NAT_1:sch 1(A8,A10);
      hence Sum p = card Domin_0(2*n+2+k,n+1) by A2,A4;
    end;
  end;
  hence thesis by A2,A4;
end;

begin

reserve seq1,seq2,seq3,seq4 for Real_Sequence,
        r,s,e for real number,
        Fr,Fr1,Fr2 for XFinSequence of REAL;

definition let Fr;
  func Sum(Fr) -> Real equals
   addreal "**" Fr;
  coherence;
end;

definition let Fr,x;
  redefine func Fr.x -> Real;
  coherence
  proof
    per cases;
      suppose x in dom Fr;
        then Fr.x in rng Fr & rng Fr c= REAL by FUNCT_1:def 5,ORDINAL1:def 8;
        hence Fr.x is Real;
      end;
      suppose not x in dom Fr;
        then Fr.x=0 by FUNCT_1:def 4;
        hence thesis;
      end;
    end;
end;

Lm6:dom Fr=0 implies Sum Fr = 0
proof
  assume dom Fr=0;
  then len Fr=0 by AFINSQ_1:def 1;
  hence thesis by BINOP_2:2,STIRL2_1:def 3;
end;

Lm7:(dom Fr=1 or len Fr=1) implies Sum Fr=Fr.0
proof
  assume dom Fr=1 or len Fr=1;
  then len Fr=1 by AFINSQ_1:def 1;
  then Fr=<%Fr.0%> & addreal"**" <%Fr.0%>=Fr.0 by AFINSQ_1:38,STIRL2_1:44;
  hence thesis;
end;

Lm8:
  for Fr,n st n in dom Fr holds
    Sum (Fr|n) + Fr.n = Sum (Fr|(n+1))
proof
  let Fr,n such that A1:n in dom Fr;
   addreal.(addreal "**" Fr|n, Fr.n) = addreal "**" Fr|(n+1) &
  addreal.(addreal "**" Fr|n, Fr.n)=addreal "**" Fr|n + Fr.n &
  addreal "**" Fr|n = Sum (Fr|n) by A1,CARD_FIN:42,BINOP_2:def 9;
  hence thesis;
end;

Lm9:
  for Fr1,Fr2 st
      dom Fr1=dom Fr2 &
      for n st n in len Fr1 holds Fr1.n=Fr2.(len Fr1-'(1+n))
    holds Sum Fr1 = Sum Fr2
proof
  let Fr1,Fr2 such that
    A1:dom Fr1=dom Fr2 and
    A2:for n st n in len Fr1 holds Fr1.n=Fr2.(len Fr1-'(1+n));
  defpred P[set,set] means
    for i st i=$1 holds $2=len Fr1-'(1+i);
  A3:for x st x in len Fr1 ex y st y in len Fr1 & P[x,y]
  proof
    let x such that A4:x in len Fr1;
     len Fr1 is Subset of NAT by STIRL2_1:8;
    then reconsider k=x as Element of NAT by A4;
    take len Fr1-'(1+k);
     k< len Fr1 & 1+k > 0 by A4,EULER_1:1;
    then k+1 <= len Fr1 & len Fr1 +0< len Fr1 +(1+k)
      by NAT_1:13,XREAL_1:10;
    then len Fr1-'(1+k)=len Fr1-(1+k) & len Fr1-(1+k) < len Fr1+(1+k)-(1+k)
      by BINARITH:50,XREAL_1:11;
    hence thesis by EULER_1:1;
  end;
  consider P be Function of len Fr1,len Fr1 such that
    A5:for x st x in len Fr1 holds P[x,P.x] from FUNCT_2:sch 1(A3);
   for x1,x2 st x1 in len Fr1 & x2 in len Fr1 & P.x1 = P.x2 holds x1 = x2
  proof
    let x1,x2 such that A6:x1 in len Fr1 & x2 in len Fr1 & P.x1 = P.x2;
     len Fr1 is Subset of NAT by STIRL2_1:8;
    then reconsider i=x1,j=x2 as Element of NAT by A6;
     i< len Fr1 & j < len Fr1 by A6,EULER_1:1;
    then i+1 <= len Fr1 & j+1 <= len Fr1 by NAT_1:13;
    then len Fr1-'(1+i)=len Fr1-(1+i) & len Fr1-'(1+j)=len Fr1-(1+j) by
      BINARITH:50;
    then P.x1=len Fr1-(1+i) & P.x2=len Fr1-(1+j) by A5,A6;
    hence thesis by A6;
  end;
  then P is one-to-one & card len Fr1 =card len Fr1  by FUNCT_2:77;
  then P is one-to-one & P is onto by STIRL2_1:70;
  then P is bijective & len Fr1=dom Fr1
    by FUNCT_2:def 4,AFINSQ_1:def 1;
  then reconsider P as Permutation of dom Fr1;
  A7:now let x such that A8:x in dom Fr1;
     x in dom P by A8,FUNCT_2:67;
    then P.x in rng P by FUNCT_1:12;
    hence x in dom P & P.x in dom Fr2 by A1,A8,FUNCT_2:67;
  end;
  A9:for x st x in dom P & P.x in dom Fr2 holds x in dom Fr1;
   now let x such that A10:x in dom Fr1;
    reconsider k=x as Element of NAT by A10;
     k in dom Fr1 & dom Fr1=len Fr1 by A10,AFINSQ_1:def 1;
    then P.k=len Fr1-'(1+k) & Fr1.k=Fr2.(len Fr1-'(1+k)) by A2,A5;
    hence Fr1.x=Fr2.(P.x);
  end;
  then Fr1 = Fr2 * P by A7,A9,FUNCT_1:20;
  hence thesis by A1,CARD_FIN:48;
end;

:::::::::::::::::::::::::::::::::::::::::::::::
::              CAUCHY PRODUCT               ::
:::::::::::::::::::::::::::::::::::::::::::::::

definition
  let seq1, seq2 be Real_Sequence;
  func seq1 (##) seq2 -> Real_Sequence means :Def5:
  for k be Nat ex Fr be XFinSequence of REAL st
    dom Fr = k+1 &
    (for n st n in k+1 holds Fr.n = seq1.n * seq2.(k-'n)) &
    Sum Fr = it.k;
  existence
  proof
    defpred P[set,set] means
      for k be Nat st k=$1 ex Fr st
      dom Fr = k+1 &
      (for n st n in k+1 holds Fr.n = seq1.n * seq2.(k-'n)) &
      Sum Fr = $2;
    A1:for x st x in NAT ex y st y in REAL & P[x,y]
    proof
      let x;assume x in NAT;
      then reconsider k=x as Element of NAT;
      defpred Q[set,set] means for i st i=$1 holds $2=seq1.i * seq2.(k-'i);
      A2:for i st i in k+1 ex z be Element of REAL st Q[i,z]
      proof
        let i such that i in k+1;
        take S=seq1.i * seq2.(k-'i);
        thus thesis;
      end;
      consider Fr such that
        A3:dom Fr = k+1 and
        A4:for i st i in k+1 holds Q[i,Fr.i] from STIRL2_1:sch 6(A2);
      take Sum Fr;
       P[k,Sum Fr]
      proof
         for n st n in k+1 holds Fr.n = seq1.n * seq2.(k-'n) by A4;
        hence thesis by A3;
      end;
      hence thesis;
    end;
    consider seq3 such that
A5: for x st x in NAT holds P[x,seq3.x] from FUNCT_2:sch 1(A1);
     for k be Nat holds ex Fr st dom Fr = k+1 &
      (for n st n in k+1 holds Fr.n = seq1.n * seq2.(k-'n)) &
      Sum Fr = seq3.k
    proof
      let k be Nat;  k in NAT by ORDINAL1:def 13;
      hence thesis by A5;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let seq3,seq4 such that
    A6:for k be Nat ex Fr be XFinSequence of REAL st
      dom Fr = k+1 &
      (for n st n in k+1 holds Fr.n = seq1.n * seq2.(k-'n)) &
      Sum Fr = seq3.k and
    A7:for k be Nat ex Fr be XFinSequence of REAL st
      dom Fr = k+1 &
      (for n st n in k+1 holds Fr.n = seq1.n * seq2.(k-'n)) &
      Sum Fr = seq4.k;
     now let x such that A8:x in NAT;
      reconsider k=x as Element of NAT by A8;
      consider Fr1 be XFinSequence of REAL such that
        A9:dom Fr1 = k+1 and
        A10:for n st n in k+1 holds Fr1.n = seq1.n * seq2.(k-'n) and
        A11:Sum Fr1 = seq3.k by A6;
      consider Fr2 be XFinSequence of REAL such that
        A12:dom Fr2 = k+1 and
        A13:for n st n in k+1 holds Fr2.n = seq1.n * seq2.(k-'n) and
        A14:Sum Fr2 = seq4.k by A7;
       now let n such that A15:n in dom Fr1;
         Fr1.n=seq1.n * seq2.(k-'n) & Fr2.n=seq1.n * seq2.(k-'n)
          by A9,A10,A13,A15;
        hence Fr1.n=Fr2.n;
      end;
      hence seq3.x=seq4.x by A9,A11,A12,A14,AFINSQ_1:10;
    end;
    hence seq3=seq4 by SEQ_1:8;
  end;
  commutativity
  proof
  let seq3,seq1,seq2;
  assume A16:for k be Nat ex Fr be XFinSequence of REAL st
    dom Fr = k+1 &
    (for n st n in k+1 holds Fr.n = seq1.n * seq2.(k-'n)) &
    Sum Fr = seq3.k;
  let k be Nat;
  reconsider k'=k as Element of NAT by ORDINAL1:def 13;
  defpred Q[set,set] means for i st i=$1 holds $2=seq2.i * seq1.(k-'i);
  A17:for i be Element of NAT st i in k'+1 ex z be Element of REAL st Q[i,z]
    proof
      let i be Element of NAT such that i in k'+1;
      take S=seq2.i * seq1.(k-'i);
      thus thesis;
    end;
  consider Fr1 be XFinSequence of REAL such that
    A18:dom Fr1 = k+1 and
    A19:for n st n in k+1 holds Fr1.n = seq1.n * seq2.(k-'n) and
    A20:Sum Fr1 = seq3.k by A16;
  consider Fr2 such that
    A21:dom Fr2 = k'+1 and
    A22:for i be Element of NAT st i in k'+1 holds Q[i,Fr2.i]
      from STIRL2_1:sch 6(A17);
  take Fr2;
  thus dom Fr2 = k+1 & for n st n in k+1 holds Fr2.n = seq2.n * seq1.(k-'n)
    by A21,A22;
   now let n such that A23:n in len Fr1;
    A24:len Fr1=dom Fr1 by AFINSQ_1:def 1;
    then n < k+1 by A18,A23,EULER_1:1;
    then A25:n <= k & n+1 <= k+1  by NAT_1:13;
    then n+1 <= len Fr2 & len Fr2=dom Fr2 & k-n<=k & k<k+1
      by A21,AFINSQ_1:def 1,NAT_1:13, XREAL_1:45;
    then A26:len Fr2-'(n+1)=(k+1)-(n+1) & k-n < k+1
      by A21,BINARITH:50,XXREAL_0:2;
    then A27:len Fr2-'(n+1) in k+1 by EULER_1:1;
     k-'n <= k-'n + n & k-'n=k-n by A25,NAT_1:11,BINARITH:50;
    then k-'(k-'n)=k-(k-'n) & k-'n=k-n by BINARITH:50;
    then Fr2.(len Fr2-'(n+1))=seq2.(k-'n) * seq1.n
      & Fr1.n= seq1.n *seq2.(k-'n) & len Fr1=dom Fr1
      by A18,A19,A22,A23,A24,A26,A27;
    hence Fr1.n=Fr2.(len Fr1-'(1+n)) by A18,A21,AFINSQ_1:def 1;
  end;
  hence seq3.k=Sum Fr2 by A18,A20,A21,Lm9;
  end;
end;

theorem
   for Fr,n st n in dom Fr holds
    Sum (Fr|n) + Fr.n = Sum (Fr|(n+1)) by Lm8;

theorem
   for Fr1,Fr2 st
      dom Fr1=dom Fr2 &
      for n st n in len Fr1 holds Fr1.n=Fr2.(len Fr1-'(1+n))
    holds Sum Fr1 = Sum Fr2 by Lm9;

theorem Th49:
  for Fr1,Fr2 st
      dom Fr1=dom Fr2 &
      for n st n in len Fr1 holds Fr1.n=r*Fr2.n
    holds Sum Fr1 = r*Sum Fr2
proof
  let Fr1,Fr2 such that
    A1:dom Fr1=dom Fr2 and
    A2:for n st n in len Fr1 holds Fr1.n=r*Fr2.n;
  defpred P[Element of NAT] means
    $1 <= len Fr1 implies Sum(Fr1|$1)=r*Sum(Fr2|$1);
  A3:P[0]
  proof
     dom (Fr1|0)=0 & dom (Fr2|0)=0 by RELAT_1:60,110;
    then Sum (Fr1|0)=0 & Sum (Fr2|0)=0 by Lm6;
    hence thesis;
  end;
  A4:for i st P[i] holds P[i+1]
  proof
    let i such that A5:P[i];
    assume i+1 <= len Fr1;
    then i< len Fr1 by NAT_1:13;
    then i < len Fr1 & i in len Fr1 & len Fr1=dom Fr1
      by EULER_1:1,AFINSQ_1:def 1;
    then Sum(Fr1|(i+1))=Fr1.i+Sum(Fr1|i) &Sum(Fr2|(i+1))=Fr2.i+Sum(Fr2|i) &
      Sum(Fr1|i)=r*Sum(Fr2|i) & Fr1.i=r*Fr2.i by A1,A2,A5,Lm8;
    hence thesis;
  end;
  A6:for i holds P[i] from NAT_1:sch 1(A3,A4);
   len Fr1=dom Fr1 & Fr1|(dom Fr1)=Fr1 & Fr2|(dom Fr1)=Fr2
    by A1,AFINSQ_1:def 1,RELAT_1:98;
  hence thesis by A6;
end;

theorem
   seq1 (##) (r(#)seq2) = r(#)(seq1 (##) seq2)
proof
  set RS=r(#)seq2;set S=seq1 (##) seq2;
   now let x such that A1:x in NAT;
    reconsider k=x as Element of NAT by A1;
    consider Fr1 such that
      A2:dom Fr1 = k+1 and
      A3:for n st n in k+1 holds Fr1.n = seq1.n * RS.(k-'n) and
      A4:Sum Fr1 = (seq1(##)RS).k by Def5;
    consider Fr2 such that
      A5:dom Fr2 = k+1 and
      A6:for n st n in k+1 holds Fr2.n = seq1.n * seq2.(k-'n) and
      A7:Sum Fr2 = S.k by Def5;
    now let n such that A8:n in len Fr1;
      dom Fr1=len Fr1 by AFINSQ_1:def 1;
     then Fr1.n = seq1.n * RS.(k-'n) & Fr2.n = seq1.n * seq2.(k-'n)
       & RS.(k-'n)= r * seq2.(k-'n) by A2,A3,A6,A8,SEQ_1:13;
     hence Fr1.n=r*Fr2.n;
   end;
   then Sum Fr1 = r*Sum Fr2 by A2,A5,Th49;
   hence (seq1(##)RS).x=(r(#)S).x by A4,A7,SEQ_1:13;
  end;
  hence thesis by SEQ_1:8;
end;

theorem
   seq1 (##) (seq2 + seq3) = (seq1 (##) seq2) + (seq1 (##) seq3)
proof
  set S=seq2 + seq3;
  set S2=seq1 (##) seq2;
  set S3=seq1 (##) seq3;
   now let x such that A1:x in NAT;
    reconsider k=x as Element of NAT by A1;
    consider Fr be XFinSequence of REAL such that
      A2:dom Fr = k+1 and
      A3:for n st n in k+1 holds Fr.n = seq1.n * S.(k-'n) and
      A4:Sum Fr = (seq1(##)S).k by Def5;
    consider Fr1 be XFinSequence of REAL such that
      A5:dom Fr1 = k+1 and
      A6:for n st n in k+1 holds Fr1.n = seq1.n * seq2.(k-'n) and
      A7:Sum Fr1 = S2.k by Def5;
    consider Fr2 be XFinSequence of REAL such that
      A8:dom Fr2 = k+1 and
      A9:for n st n in k+1 holds Fr2.n = seq1.n * seq3.(k-'n) and
      A10:Sum Fr2 = S3.k by Def5;
    A11:for n st n in dom Fr holds Fr.n=addreal.(Fr1.n,Fr2.n)
    proof
      let n such that A12:n in dom Fr;
       Fr1.n = seq1.n * seq2.(k-'n) & Fr2.n = seq1.n * seq3.(k-'n) &
        Fr.n = seq1.n * S.(k-'n) & S.(k-'n)=seq2.(k-'n) + seq3.(k-'n)
        by A2,A3,A6,A9,A12,SEQ_1:11;
      then Fr.n=Fr1.n+Fr2.n;
      hence Fr.n=addreal.(Fr1.n,Fr2.n) by BINOP_2:def 9;
    end;
     len Fr1=dom Fr1 by AFINSQ_1:def 1;
    then len Fr1=len Fr2 & len Fr1=len Fr by A2,A5,A8,AFINSQ_1:def 1;
    then addreal "**" Fr1^Fr2 = addreal "**" Fr by A11,CARD_FIN:62;
    then Sum Fr=addreal.(Sum (Fr1),Sum(Fr2)) by STIRL2_1:47;
    then Sum Fr=Sum (Fr1)+Sum(Fr2) by BINOP_2:def 9;
    hence (seq1(##)S).x=(S2+S3).x by SEQ_1:11,A4,A7,A10;
  end;
  hence thesis by SEQ_1:8;
end;

theorem Th52:
  (seq1 (##) seq2).0=seq1.0*seq2.0
proof
  set S=seq1.0 * seq2.0;
  consider Fr such that
    A1:dom Fr = 0+1 and
    A2:for n st n in 0+1 holds Fr.n = seq1.n * seq2.(0-'n) and
    A3:Sum Fr = (seq1 (##) seq2).0 by Def5;
   0 in 1 by EULER_1:1;
  then Fr.0=seq1.0 * seq2.(0-'0) & 0-'0=0 & len Fr=1
    by A1,A2,BINARITH:51,AFINSQ_1:def 1;
  then Fr=<%S%> by AFINSQ_1:38;
  hence thesis by A3,STIRL2_1:44;
end;

theorem Th53:
  for seq1, seq2, n ex Fr st
    Partial_Sums(seq1 (##) seq2).n = Sum Fr &
    dom Fr=n+1 &
    for i st i in n+1 holds Fr.i=seq1.i * Partial_Sums(seq2).(n-'i)
proof
  let seq1,seq2,n;
  set S=seq1 (##) seq2;
  set P=Partial_Sums(seq2);
  defpred P[Nat] means ex Fr st
    Partial_Sums(S).$1 = Sum Fr &
    dom Fr=$1+1 &
    for i st i in $1+1 holds Fr.i=seq1.i * P.($1-'i);
  A1:P[0]
  proof
    set Fr=1--> (seq1.0*seq2.0);
     dom Fr=1 by FUNCOP_1:19;
    then reconsider Fr as XFinSequence by AFINSQ_1:7;
     rng Fr c={seq1.0*seq2.0} by FUNCOP_1:19;
    then rng Fr c= REAL by XBOOLE_1:1;
    then reconsider Fr as XFinSequence of REAL by ORDINAL1:def 8;
    take Fr;
     dom Fr=1 & 0 in 1 & Fr|0={} by FUNCOP_1:19,EULER_1:1,RELAT_1:110;
    then Sum(Fr|0)+Fr.0=Sum (Fr|(0+1)) & Fr.0=seq1.0*seq2.0 & dom (Fr|0)=0
      & Fr|1=Fr by Lm8,FUNCOP_1:13,RELAT_1:60,98;
    then Sum Fr=0+seq1.0*seq2.0 by Lm6;
    then Sum Fr=S.0 by Th52;
    hence Partial_Sums(S).0 = Sum Fr & dom Fr=0+1
      by SERIES_1:def 1,FUNCOP_1:19;
    let i such that A2:i in 0+1;
     i<1 by A2,EULER_1:1;
    then A3:i=0 by NAT_1:14;
    then (0-'i)=0 & dom Fr=1 by BINARITH:51,FUNCOP_1:19;
    then P.(0-'i)= seq2.0 & Fr.0=seq1.0*seq2.0
      by A2,A3,SERIES_1:def 1,FUNCOP_1:13;
    hence thesis by A3;
  end;
  A4:for n st P[n] holds P[n+1]
  proof
    let n;set n1=n+1;assume P[n];
    then consider Fr such that
      A5:Partial_Sums(S).n = Sum Fr and
      A6:dom Fr=n1 and
      A7:for i st i in n+1 holds Fr.i=seq1.i * P.(n-'i);
    consider Fr1 such that
      A8:dom Fr1 = n1+1 and
      A9:for i st i in n1+1 holds Fr1.i = seq1.i * seq2.(n1-'i) and
      A10:Sum Fr1 = S.n1 by Def5;
    defpred Q[set,set] means
      for i st i=$1 holds $2=seq1.i * P.(n1-'i);
    A11:for i st i in n1+1 ex x be Element of REAL st Q[i,x]
    proof
      let i such that i in n1+1;
      take seq1.i * Partial_Sums(seq2).(n1-'i);
      thus thesis;
    end;
    consider Fr2 such that
      A12:dom Fr2 = n1+1 and
      A13:for i st i in n1+1 holds Q[i,Fr2.i] from STIRL2_1:sch 6(A11);
    take Fr2;
    A14:for i st i in dom (Fr2|n1) holds (Fr2|n1).i=addreal.(Fr.i,(Fr1|n1).i)
    proof
      let i such that A15:i in dom (Fr2|n1);
       i in dom Fr2 /\ n1 by A15,RELAT_1:90;
      then A16:i in n1+1 & i in n1 & i in dom(Fr1|n1)
        by A8,A12,XBOOLE_0:def 3,RELAT_1:90;
      then A17:Fr1.i = seq1.i * seq2.(n1-'i) & Fr.i=seq1.i * P.(n-'i) &
        Fr2.i=seq1.i * P.(n1-'i) by A7,A9,A13;
       i < n1+1 & i < n1 by A16,EULER_1:1;
      then i <= n1 & i <=n by NAT_1:13;
      then n1-'i=n1-i & n-'i=n-i by BINARITH:50;
      then (n-'i)+1=n1-'i;
      then P.(n1-'i)=P.(n-'i)+seq2.(n1-'i) by SERIES_1:def 1;
      then Fr2.i=Fr.i+Fr1.i & Fr1.i=(Fr1|n1).i & Fr2.i=(Fr2|n1).i
        by A15,A16,A17,FUNCT_1:70;
      hence thesis by BINOP_2:def 9;
    end;
    set A=addreal;
     n1<=n1+1 by NAT_1:11;
    then n1 c= n1+1 by CARD_1:56;
    then dom (Fr2|n1)=n1 & dom (Fr1|n1)=n1 & dom Fr=len Fr
      by A8,A12,RELAT_1:91,AFINSQ_1:def 1;
    then len (Fr2|n1)=len Fr & len (Fr1|n1)=len Fr by A6,AFINSQ_1:def 1;
    then A"**"(Fr2|n1)=A"**"(Fr^(Fr1|n1)) by A14,CARD_FIN:62;
    then Sum(Fr2|n1)=A.(Sum Fr,Sum(Fr1|n1)) by STIRL2_1:47;
    then A18:Sum(Fr2|n1)=Sum Fr+Sum(Fr1|n1) by BINOP_2:def 9;
     n1 < n1+1 by NAT_1:13;
    then A19:n1 in n1+1 by EULER_1:1;
    then A20:Fr1.n1=seq1.n1 * seq2.(n1-'n1) & n1-'n1=0 & P.0=seq2.0
      & Fr2.n1=seq1.n1 * P.(n1-'n1) by A9,A13,BINARITH:51,SERIES_1:def 1;
     Sum(Fr2|(n1+1)) = Fr2.n1 + Sum(Fr2|n1) &
      Sum(Fr1|(n1+1)) = Fr1.n1 + Sum(Fr1|n1) &
      Fr2|(n1+1)=Fr2 & Fr1|(n1+1)=Fr1 by A8,A12,A19,Lm8,RELAT_1:98;
    then Sum Fr2=Partial_Sums(S).n + S.n1 by A5,A10,A18,A20;
    hence thesis by A12,A13,SERIES_1:def 1;
  end;
   for i holds P[i] from NAT_1:sch 1(A1,A4);
  hence thesis;
end;

theorem Th54:
  for seq1, seq2, n st seq2 is summable ex Fr st
     Partial_Sums(seq1 (##) seq2).n= Sum seq2 * Partial_Sums(seq1).n- Sum Fr &
     dom Fr=n+1 &
     for i st i in n+1 holds Fr.i=seq1.i * Sum (seq2^\(n-'i+1))
proof
  let seq1, seq2, n such that A1:seq2 is summable;
  set S=seq1 (##) seq2;set P1=Partial_Sums(seq1);set P2=Partial_Sums(seq2);
  defpred Q[set,set] means
     for i st i=$1 holds $2=seq1.i * Sum (seq2^\(n-'i+1));
  A2:for i st i in n+1 ex x be Element of REAL st Q[i,x]
  proof
    let i such that i in n+1;
    take seq1.i * Sum (seq2^\(n-'i+1));
    thus thesis;
  end;
  consider Fr such that
    A3:dom Fr = n+1 and
    A4:for i st i in n+1 holds Q[i,Fr.i]  from STIRL2_1:sch 6(A2);
  take Fr;
  consider Fr1 such that
    A5:Partial_Sums(S).n = Sum Fr1 and
    A6:dom Fr1=n+1 and
    A7:for i st i in n+1 holds Fr1.i=seq1.i * P2.(n-'i) by Th53;
  defpred P[Element of NAT] means $1+1 <= n+1 implies
    Sum (Fr1|($1+1)) + Sum (Fr|($1+1))= Sum seq2 * P1.$1;
  A8:P[0]
  proof
     0 in n+1 & dom (Fr|0)={} & dom (Fr1|0)={}
      by EULER_1:1,RELAT_1:60,110;
    then Sum (Fr|(0+1))=Fr.0 + Sum (Fr|0) & Sum (Fr|0)=0 &
       Fr.0=seq1.0 * Sum (seq2^\(n-'0+1)) & Fr1.0=seq1.0 * P2.(n-'0) &
       Sum (Fr1|(0+1))=Fr1.0 + Sum (Fr1|0) & Sum (Fr1|0)=0
       by A3,A4,A6,A7,Lm8,Lm6;
    then Sum (Fr|(0+1))+Sum (Fr1|(0+1))=seq1.0*(Sum(seq2^\(n-'0+1))+P2.(n-'0))
                                      .=seq1.0*Sum seq2 by A1,SERIES_1:18;
    hence thesis by SERIES_1:def 1;
  end;
  A9:for k st P[k] holds P[k+1]
  proof
    let k such that A10:P[k];set k1=k+1;
    assume k+1+1<=n+1;
    then k1<n+1 & k1<=k1+1 by NAT_1:13;
    then k1 in n+1 & k1<=n+1 by EULER_1:1;
    then Sum(Fr|(k1+1))=Fr.k1 + Sum (Fr|k1) & Fr1.k1=seq1.k1 * P2.(n-'k1) &
     Fr.k1=seq1.k1*Sum(seq2^\(n-'k1+1)) & Sum(Fr1|(k1+1))=Fr1.k1+Sum (Fr1|k1)&
      Sum (Fr1|k1) + Sum (Fr|k1)= Sum seq2 * P1.k by A3,A4,A6,A7,A10,Lm8;
    then Sum(Fr|(k1+1)) +  Sum(Fr1|(k1+1))=
      seq1.k1 * ( Sum(seq2^\(n-'k1+1)) + P2.(n-'k1) ) + Sum seq2 * P1.k
      .=seq1.k1*Sum seq2 +Sum seq2 * P1.k by A1,SERIES_1:18
      .=Sum seq2*(P1.k+seq1.k1)
      .=P1.k1*Sum seq2 by SERIES_1:def 1;
    hence thesis;
  end;
  A11:for k holds P[k] from NAT_1:sch 1(A8,A9);
   Fr|(n+1)=Fr & Fr1|(n+1)=Fr1 by A3,A6,RELAT_1:98;
  then Sum Fr1 + Sum Fr= Sum seq2 * P1.n by A11;
  hence thesis by A3,A4,A5;
end;

theorem Th55:
  for Fr ex absFr be XFinSequence of REAL st
    dom absFr=dom Fr &
    abs(Sum Fr) <= Sum absFr &
    for i st i in dom absFr holds absFr.i=abs(Fr.i)
proof
  let Fr;
  defpred P[set,set] means $2=abs(Fr.$1);
  A1:for i st i in len Fr ex x be Element of REAL st P[i,x];
  consider absFr be XFinSequence of REAL such that
    A2:dom absFr = len Fr and
    A3:for i st i in len Fr holds P[i,absFr.i]  from STIRL2_1:sch 6(A1);
  take absFr;
  defpred Q[Element of NAT] means $1 <= len Fr implies
    abs Sum (Fr|$1) <= Sum (absFr|$1);
  A4:Q[0]
  proof
     Fr|0={} & absFr|0={}  by RELAT_1:110;
    then Sum (Fr|0)=0 & Sum (absFr|0)=0 by Lm6,RELAT_1:60;
    hence thesis by COMPLEX1:130;
  end;
  A5:for i st Q[i] holds Q[i+1]
  proof
    let i such that A6:Q[i];set i1=i+1;
    assume i1<=len Fr;
    then i < len Fr & len Fr = dom Fr by NAT_1:13,AFINSQ_1:def 1;
    then i in dom Fr & i in len Fr & i <= len Fr by EULER_1:1;
    then Sum(Fr|i1)=Fr.i+Sum(Fr|i) & Sum(absFr|i1)=absFr.i+Sum (absFr|i)&
      abs Sum (Fr|i) <= Sum (absFr|i) & absFr.i=abs(Fr.i) by A2,A3,A6,Lm8;
    then abs(Sum(Fr|i1))<=absFr.i +abs(Sum (Fr|i)) &
      absFr.i + abs(Sum (Fr|i)) <= Sum(absFr|i1) by COMPLEX1:142,XREAL_1:9;
    hence thesis by XXREAL_0:2;
  end;
   for i holds Q[i] from NAT_1:sch 1(A4,A5);
  then abs Sum (Fr|(len Fr)) <= Sum (absFr|(len Fr)) & len Fr=dom Fr &
    Fr|(dom Fr)=Fr & absFr|(dom absFr)=absFr by RELAT_1:98,AFINSQ_1:def 1;
  hence thesis by A2,A3;
end;

theorem Th56:
  for seq1 st seq1 is summable ex r st
    0 < r & for k holds abs(Sum(seq1^\k)) < r
proof
  let seq1 such that A1:seq1 is summable;
  set P=Partial_Sums(seq1);
   P is convergent & lim P= Sum seq1 by A1,SERIES_1:def 2,def 3;
  then consider n be Element of NAT such that
    A2:for m st n<=m holds abs(P.m-Sum seq1)<1 by SEQ_2:def 7;
  defpred P[Nat] means
    ex r st r>=0 & for i st i <= $1 holds abs(Sum (seq1^\i) )<=r;
  A3:P[0]
  proof
    take abs(Sum(seq1));thus abs(Sum(seq1))>=0 by COMPLEX1:132;
    let i such that A4:i<=0;
     i=0 by A4;hence thesis by SEQM_3:34;
  end;
  A5:for k st P[k] holds P[k+1]
  proof
    let k; assume P[k];
    then consider r such that
      A6:r>=0 & for i st i <= k holds abs(Sum (seq1^\i) )<=r;
    take M=max(r,abs(Sum (seq1^\(k+1))));
    thus M >=0 by A6,XXREAL_0:25;
    let i such that A7:i <= k+1;
     now per cases by A7,NAT_1:8;
      suppose i=k+1;hence abs(Sum (seq1^\i))<=M by XXREAL_0:25;
      end;
      suppose i<=k;
        then abs(Sum (seq1^\i) )<=r & r<=M  by A6,XXREAL_0:25;
        hence abs(Sum (seq1^\i))<=M by XXREAL_0:2;
      end;
    end;hence thesis;
  end;
   for k holds P[k] from NAT_1:sch 1(A3,A5);
  then consider r such that
    A8:r>=0 & for i st i <= n holds abs(Sum (seq1^\i) )<=r;
  take r1=r+1;
   r1>0+0 by A8,XREAL_1:10;hence r1>0;
  let k;
   now per cases;
    suppose k <= n;
      then abs(Sum (seq1^\k) )<=r & 0+r< r1 by A8,XREAL_1:10;
      hence abs(Sum(seq1^\k))<r1 by XXREAL_0:2;
    end;
    suppose A9:k > n;
      then reconsider k1=k-1 as Element of NAT by NAT_1:20;
       k1+1>n by A9;
      then k1>=n by NAT_1:13;
      then abs(P.k1-Sum seq1)<1 & Sum seq1 = P.k1+Sum(seq1^\(k1+1))
        by A1,A2,SERIES_1:18;
      then abs(-Sum(seq1^\(k1+1)))<1;
      then abs( Sum(seq1^\(k1+1)) )<1 & 1+0<=r1
        by COMPLEX1:138,A8,XREAL_1:8;
     hence abs(Sum(seq1^\k))<r1 by XXREAL_0:2;
    end;
  end;
  hence thesis;
end;

theorem Th57:
  for seq1, n, m st n <= m & for i holds seq1.i>=0
    holds (Partial_Sums seq1).n <= (Partial_Sums seq1).m
proof
  let seq1, n, m such that A1:n <= m and A2:for i holds seq1.i>=0;
  set S=Partial_Sums seq1;
  defpred P[Nat] means S.n <= S.(n+$1);
  A3:P[0];
  A4:for i st P[i] holds P[i+1]
  proof
    let i such that A5:P[i];set ni=n+i;
     S.(ni+1)=S.ni+seq1.(ni+1) & seq1.(ni+1)>=0 by A2,SERIES_1:def 1;
    then S.(ni+1) >= S.ni + 0 by XREAL_1:8;
    hence thesis by A5,XXREAL_0:2;
  end;
  A6:for i holds P[i] from NAT_1:sch 1(A3,A4);
  reconsider m'=m, n'=n as Nat;
A7:m'-n' is Element of NAT by A1,NAT_1:21;
   n'+(m'-n')=m';
  hence thesis by A6,A7;
end;

:::::::::::::::::::::::::::::::::::::::::::::::
::          CAUCHY PRODUCT THEOREM           ::
:::::::::::::::::::::::::::::::::::::::::::::::

theorem Th58:
  for seq1, seq2 st seq1 is absolutely_summable & seq2 is summable holds
     seq1 (##) seq2 is summable &
     Sum(seq1 (##) seq2) = Sum seq1 * Sum seq2
proof
  let seq1, seq2 such that
    A1:seq1 is absolutely_summable and A2:seq2 is summable;
  set S=seq1 (##) seq2;set P1=Partial_Sums(seq1);set P2=Partial_Sums(seq2);
  set PA=Partial_Sums(abs(seq1));set P=Partial_Sums(S);
  set S1=Sum seq1;set S2=Sum seq2;
A3:for e st 0<e ex n st for m st n<=m holds abs(P.m-S1*S2)<e
  proof
    let e such that A4:0<e;
    consider r such that
      A5:0 < r & for k holds abs(Sum(seq2^\k)) < r by A2,Th56;
    A6:now let n be Element of NAT;
       abs(seq1.n)=abs(seq1).n & abs(seq1.n)>= 0 by SEQ_1:16,COMPLEX1:132;
      hence abs(seq1).n>=0;
    end;
    set e1=e/(3*(abs(S2)+1));set e2=e/(3*r);set e3=e/(3*(Sum abs(seq1)+1));
     abs(S2)>=0 by COMPLEX1:132;
    then (abs(S2)+1)> 0+0 by XREAL_1:10;
    then A7:3*(abs(S2)+1)>3*0 by XREAL_1:70;
    then A8:e1>0 by A4,XREAL_1:141;
     seq1 is summable by A1,SERIES_1:40;
    then P1 is convergent & lim P1= S1 & e1>0
      by A4,A7,SERIES_1:def 2,def 3,XREAL_1:141;
    then consider n0 be Element of NAT such that
      A9:for n st n0 <= n holds abs(P1.n-S1) < e1 by SEQ_2:def 7;
    A10:abs seq1 is summable by A1,SERIES_1:def 5;
     3*r>0*3 by A5,XREAL_1:70;
    then e2>0 by A4,XREAL_1:141;
    then consider n1 be Element of NAT such that
A11:for n st n1 <= n holds abs(PA.n-PA.n1) < e2 by A10,SERIES_1:25;
     Sum abs seq1>=0 by A10,A6,SERIES_1:21;
    then Sum abs(seq1)+1>0+0 by XREAL_1:10;
    then 3*(Sum abs(seq1)+1)>0*3 by XREAL_1:70;
    then A12:e3>0 & P2 is convergent & lim P2= S2
      by A2,SERIES_1:def 2,def 3,A4,XREAL_1:141;
    then consider n2 be Element of NAT such that
A13:for n st n2<=n holds abs(P2.n-S2)<e3 by SEQ_2:def 7;
A14: max(n1+1,n2) = n1+1 or max(n1+1,n2) = n2 by XXREAL_0:16;
     max(1,n0) = 1 or max(1,n0) = n0 by XXREAL_0:16;
    then reconsider M=max(max(1,n0),max(n1+1,n2)) as Element of NAT
      by A14,XXREAL_0:16;
    take 2M=M*2;
    let m such that A15:2M <= m;
    A16:1<=max(1,n0) & n0<= max(1,n0) & max(1,n0)<=M & n1+1 <= max(n1+1,n2) &
      n2 <= max(n1+1,n2) & max(n1+1,n2)<=M & M<=M+M by XXREAL_0:25,NAT_1:11;
    then A17:n0 <= M & n1+1 <= M & n2 <= M & 0<M & M<=m by A15,XXREAL_0:2;
    then reconsider M1=M-1 as Element of NAT by NAT_1:20;
     abs(S2)>=0 by COMPLEX1:132;
    then abs(S2)+1>0+0 & abs(S2)+1 > abs(S2)+0 by XREAL_1:10;
    then A18:e1*(abs(S2)+1)=e/3 & e1*(abs(S2)+1) > e1*abs(S2) by
         A8,XCMPLX_1:93,XREAL_1:70;
     n0 <= m by A17,XXREAL_0:2;
    then abs(S2*(P1.m-S1))=abs(S2)*abs(P1.m-S1) & abs(P1.m-S1)<e1 & abs(S2)>=0
      by A9,COMPLEX1:151,132;
    then abs(S2*(P1.m-S1))<=abs(S2)*e1 by XREAL_1:66;
then A19:abs(S2*(P1.m-S1))<e/3 by A18,XXREAL_0:2;
    consider Fr such that
      A20:P.m= S2 * P1.m- Sum Fr and
      A21:dom Fr=m+1 and
      A22:for i st i in m+1 holds Fr.i=seq1.i*Sum(seq2^\(m-'i+1))by A2,Th54;
    consider absFr be XFinSequence of REAL such that
      A23:dom absFr=dom Fr and
      A24:abs(Sum Fr) <= Sum absFr and
      A25:for i st i in dom absFr holds absFr.i=abs(Fr.i) by Th55;
    defpred Q[Element of NAT] means
      $1+1 <= M implies Sum(absFr|($1+1))<= e3 * PA.$1;
A26:Q[0]
    proof
       S2=P2.(m-'0)+Sum(seq2^\(m-'0+1)) & m-'0=m by A2,SERIES_1:18,BINARITH:58;
      then A27:Sum(seq2^\(m-'0+1))=S2-P2.m;
       0 in m+1 & dom(absFr|0)=0 by EULER_1:1,RELAT_1:110,60;
      then Sum(absFr|(0+1))=absFr.0 + Sum(absFr|0) & Sum(absFr|0)=0 &
        absFr.0=abs(Fr.0) & Fr.0=seq1.0*Sum(seq2^\(m-'0+1))
        by A22,A23,A25,A21,Lm6,Lm8;
      then A28:Sum(absFr|(0+1))=abs(seq1.0)*abs(Sum(seq2^\(m-'0+1)))
                                                           by COMPLEX1:151
          .=abs(seq1).0*abs(Sum(seq2^\(m-'0+1))) by SEQ_1:16
          .=abs(seq1).0*abs(P2.m-S2) by A27,COMPLEX1:146;
       n2 <= M & M <= M+M by A16,XXREAL_0:2;
      then n2 <=2M by XXREAL_0:2;
      then n2 <= m by A15,XXREAL_0:2;
      then abs(P2.m-S2)<e3 & abs(seq1).0>=0 by A13,A6;
      then abs(seq1).0*abs(P2.m-S2)<=e3*abs(seq1).0 by XREAL_1:66;
      hence thesis by A28,SERIES_1:def 1;
    end;
A29:for k st Q[k] holds Q[k+1]
    proof
      let k such that A30:Q[k];set k1=k+1;
      assume A31:k1+1<=M;
      then A32:k1<M by NAT_1:13;
      then A33:k1<m & m<m+1 by A17,XXREAL_0:2,NAT_1:13;
      then k1 < m+1 by XXREAL_0:2;
      then k1 in m+1 by EULER_1:1;
      then A34:Fr.k1=seq1.k1*Sum(seq2^\(m-'k1+1)) & abs(Fr.k1)=absFr.k1 &
        Sum(absFr|(k1+1))=absFr.k1 + Sum(absFr|k1) by A21,A22,A23,A25,Lm8;
      A35:S2=P2.(m-'k1)+Sum(seq2^\(m-'k1+1)) by A2,SERIES_1:18;
       e3*abs(seq1.k1)+Sum(absFr|k1) <= e3*abs(seq1.k1) + e3 * PA.k &
        abs(seq1.k1)=abs(seq1).k1 by XREAL_1:8,SEQ_1:16,A30,A31,NAT_1:13;
      then e3*abs(seq1.k1)+Sum(absFr|k1)<=e3*(abs(seq1).k1+PA.k);
      then A36:e3*abs(seq1.k1)+Sum(absFr|k1)<= e3* PA.k1 by SERIES_1:def 1;
       m-k1 >= m-M & m-M >= 2M-M by A15,A32,XREAL_1:11,12;
      then m-k1 >= M by XXREAL_0:2;
      then m-k1 >= n2 & m-k1=m-'k1 by A33,A17,XXREAL_0:2,BINARITH:50;
      then abs(P2.(m-'k1)-S2)<e3 by A13;
      then abs(S2-P2.(m-'k1))<e3 & abs(seq1.k1)>=0 by COMPLEX1:146,
132;
      then abs(S2-P2.(m-'k1))*abs(seq1.k1) <= e3*abs(seq1.k1) by XREAL_1:66;
      then abs(seq1.k1 * Sum(seq2^\(m-'k1+1))) <= e3 * abs(seq1.k1)
        by A35,COMPLEX1:151;
      then Sum(absFr|(k1+1))<=e3*abs(seq1.k1)+Sum(absFr|k1) by A34,XREAL_1:8;
      hence thesis by A36,XXREAL_0:2;
    end;
A37:for k holds Q[k] from NAT_1:sch 1(A26,A29);
     PA.M1 <= Sum abs(seq1) by A6,A10,RSSPACE2:4;
    then A38:e3* PA.M1 <= e3* Sum abs(seq1) by A12,XREAL_1:66;
     Sum abs(seq1)>=0 by A10,A6,SERIES_1:21;
    then Sum abs(seq1)+1>0+0 & Sum abs(seq1)+1>Sum abs(seq1)+0 by XREAL_1:10;
    then (Sum abs(seq1)+1)*e3=e/3 & e3*(Sum abs(seq1)+1) >= e3*Sum abs(seq1)
      & M=M1+1 by A12,XCMPLX_1:93,XREAL_1:66;
    then e3* PA.M1 <=e/3 & Sum(absFr|M)<= e3 * PA.M1 by A37,A38,XXREAL_0:2;
    then A39:Sum(absFr|M)<=e/3 by XXREAL_0:2;
    consider Fr1 such that
A40:absFr=absFr|M^Fr1 by Th5;
     M<m+1 by A17,NAT_1:13;
    then M < len absFr by A21,A23,AFINSQ_1:def 1;
    then A41:m+1=len (absFr|M)+ len Fr1 & len (absFr|M)=M
      by A21,A23,A40,AFINSQ_1:def 4,14;
    defpred S[Element of NAT] means M+$1+1<=m+1 implies
      Sum(Fr1|($1+1))<=r*(PA.(M+$1)-PA.M1);
A42:S[0]
    proof
      assume A43:M+0+1<=m+1;
      then M+1-M<=m+1-M & len Fr1=m+1-M by A41,XREAL_1:11;
      then 1 c= len Fr1 & len Fr1=dom Fr1 by CARD_1:56,AFINSQ_1:def 1;
      then dom (Fr1|1)=1 & 0 in 1 by RELAT_1:91,EULER_1:1;
      then A44:(Fr1|1).0=Fr1.0 & Sum (Fr1|1)=(Fr1|1).0 by Lm7,FUNCT_1:70;
       M <m+1 & m+1=len absFr by A21,A23,A43,NAT_1:13,AFINSQ_1:def 1;
      then absFr.M=Fr1.(M-M) & M in m+1 by A41,A40,AFINSQ_1:22,EULER_1:1;
      then Fr1.0=abs(Fr.M) & Fr.M=seq1.M*Sum(seq2^\(m-'M+1))by A21,A22,A23,A25
;
      then A45:Fr1.0=abs(seq1.M)*abs(Sum(seq2^\(m-'M+1))) by COMPLEX1:151;
       PA.M1+abs(seq1).(M1+1)=PA.(M1+1) by SERIES_1:def 1;
      then PA.M-PA.M1=abs(seq1.M) & abs(seq1.M)>=0 & r>abs(Sum(seq2^\(m-'M+1))
)
        by A5,COMPLEX1:132,SEQ_1:16;
      hence thesis by A44,A45,XREAL_1:66;
    end;
A46:for k st S[k] holds S[k+1]
    proof
      let k such that A47:S[k];set k1=k+1;set Mk1=M+k1;
      assume A48:Mk1+1<=m+1;
      then M<=Mk1 & Mk1 <m+1 & m+1=len absFr
        by A21,A23,NAT_1:13,11,AFINSQ_1:def 1;
      then absFr.Mk1=Fr1.(Mk1-M) & Mk1 in m+1 by A40,A41,AFINSQ_1:22,EULER_1:1
;
      then A49:Fr1.k1=abs(Fr.Mk1) & Fr.Mk1=seq1.Mk1*Sum(seq2^\(m-'Mk1+1))
        by A21,A23,A22,A25;
      then abs(Fr.Mk1)=abs(seq1.Mk1)*abs(Sum(seq2^\(m-'Mk1+1))) &
        abs(seq1.Mk1)>=0 &abs(Sum(seq2^\(m-'Mk1+1)))<r by A5,COMPLEX1:132,
151;
      then A50:Fr1.k1<=r*abs(seq1.Mk1) by A49,XREAL_1:66;
       Mk1<m+1 by A48,NAT_1:13;
      then k1 < len Fr1 by A41,XREAL_1:9;
      then k1 in len Fr1 & dom Fr1=len Fr1 by EULER_1:1,AFINSQ_1:def 1;
      then Sum(Fr1|(k1+1))=Fr1.k1+Sum(Fr1|k1) &Sum(Fr1|k1)<=r*(PA.(M+k)-PA.M1)
        & abs(seq1.Mk1)=abs(seq1).Mk1 by A47,Lm8,SEQ_1:16,A48,NAT_1:13;
      then Sum(Fr1|(k1+1))<=r*abs(seq1).Mk1+r*(PA.(M+k)-PA.M1)by A50,XREAL_1:9
;
      then Sum(Fr1|(k1+1))<=r*((abs(seq1).Mk1+PA.(M+k))-PA.M1);
      then Sum(Fr1|(k1+1))<=r*(PA.(M+k+1)-PA.M1) by SERIES_1:def 1;
      hence thesis;
    end;
A51:for k holds S[k] from NAT_1:sch 1(A42,A46);
     M1+1>=n1+1 by A16,XXREAL_0:2;
    then M1>= n1 by XREAL_1:10;
    then PA.M1>=PA.n1 & n1+1<=m by A17,A6,Th57,XXREAL_0:2;
    then PA.m-PA.M1<=PA.m-PA.n1 & n1 <= m by XREAL_1:12,NAT_1:13;
    then A52:r*(PA.m-PA.M1)<=r*(PA.m-PA.n1) & PA.m>=PA.n1
      & abs(PA.m - PA.n1) < e2 by A6,A5,A11,XREAL_1:66,Th57;
    then PA.m-PA.n1>=PA.n1-PA.n1 by XREAL_1:11;
    then abs(PA.m-PA.n1)=(PA.m-PA.n1) by ABSVALUE:def 1;
    then r*(PA.m-PA.n1)<=r*e2 by A5,A52,XREAL_1:66;
    then A53:r*(PA.m-PA.M1)<=r*e2 by A52,XXREAL_0:2;
     dom Fr1=m-M+1 by A41,AFINSQ_1:def 1;
    then Fr1|((m-M)+1)=Fr1 & m=M+(m-M) & M+(m-M)+1<=m+1 & m-M=m-'M
      by RELAT_1:98,BINARITH:50,A17;
    then Sum(Fr1)<=r*(PA.m-PA.M1) by A51;
    then Sum(Fr1)<=r*e2 by A53,XXREAL_0:2;
then A54:Sum(Fr1)<=e/3 by A5,XCMPLX_1:93;
     Sum absFr=addreal.(Sum(absFr|M),Sum Fr1) by A40,STIRL2_1:47;
    then Sum absFr=Sum(absFr|M)+Sum Fr1 & Sum(absFr|M)+Sum Fr1 <=e/3+e/3
      by A54,A39,BINOP_2:def 9,XREAL_1:9;
    then A55:abs(Sum Fr)<= e/3+e/3 by A24,XXREAL_0:2;
     P.m-S1*S2=S2*(P1.m-S1)-Sum Fr by A20;
    then abs(P.m-S1*S2)<=abs(S2*(P1.m-S1))+abs(Sum Fr) &
      abs(S2*(P1.m-S1))+abs(Sum Fr) <e/3+(e/3+e/3)
      by A19,A55,XREAL_1:10,COMPLEX1:143;
    hence abs(P.m-S1*S2)<e by XXREAL_0:2;
  end;
  then A56:P is convergent by SEQ_2:def 6;
  hence S is summable by SERIES_1:def 2;
   lim P = S1*S2 by A3,A56,SEQ_2:def 7;
  hence thesis by SERIES_1:def 3;
end;

theorem Th59:
  p=Fr implies Sum p=Sum Fr
proof
  assume A1:p=Fr;
   now per cases;
    suppose A2:dom p=0;
      then len p=0 by AFINSQ_1:def 1;
      then addnat "**" p=the_unity_wrt addnat & 0=the_unity_wrt addnat
        by STIRL2_1:def 3,BINOP_2:5;
      hence thesis by A1,A2,Lm6;
    end;
    suppose dom p>0;
      then reconsider d1=dom p-1 as Nat by NAT_1:20;
      defpred P[Element of NAT] means
        $1 in dom p implies Sum(p|($1+1))=Sum(Fr|($1+1));
      A3:P[0]
      proof
        assume 0 in dom p;
        then 0 < dom p;
        then 0+1<= dom p & dom p=len p by NAT_1:13,AFINSQ_1:def 1;
        then 1 c= dom p by CARD_1:56;
        then dom (p|1)=1 & dom (Fr|1)=1 by A1,RELAT_1:91;
        then len (p|1)=1 & Sum(Fr|1)=(Fr|1).0 by Lm7,AFINSQ_1:def 1;
        then p|1=<%(p|1).0%> & Sum(Fr|1)=(p|1).0 by A1,AFINSQ_1:38;
        hence thesis by STIRL2_1:44;
      end;
      A4:for n st P[n] holds P[n+1]
      proof
        let n such that A5:P[n];set n1=n+1;
        assume A6:n1 in dom p;
        then n<n1 & n1 < dom p by EULER_1:1,NAT_1:13;
        then n < dom p by XXREAL_0:2;
        then Sum(p|(n1+1))=p.n1+Sum(p|n1) & Sum(Fr|(n1+1))=Fr.n1+Sum (Fr|n1)&
          Sum (p|n1)=Sum (Fr|n1) by A1,A5,A6,Lm8,CARD_FIN:44,EULER_1:1;
        hence thesis by A1;
      end;
      A7:for n holds P[n] from NAT_1:sch 1(A3,A4);
       d1 <d1+1 by NAT_1:13;
      then d1 in dom p & p|dom p= p & d1+1=dom p by EULER_1:1,RELAT_1:98;
      hence thesis by A1,A7;
    end;
  end;
  hence thesis;
end;

begin

:::::::::::::::::::::::::::::::::::::::::::::::
::        THE GENERATING FUNCTION FOR        ::
::           THE CATALAN NUMBERS             ::
:::::::::::::::::::::::::::::::::::::::::::::::

theorem
   for r ex Catal be Real_Sequence st
    (for n holds Catal.n = Catalan(n+1) * r|^n) &
    (abs r < 1/4 implies
       Catal is absolutely_summable &
       Sum Catal = 1 + r * (Sum Catal) |^ 2 &
       Sum Catal = 2 / (1 + sqrt(1-4*r)) &
       (r<>0 implies Sum Catal = (1 - sqrt(1-4*r)) / (2*r)) )
proof
  defpred E[set,set] means
  for r st $1=r ex Catal be Real_Sequence st
     (for n holds Catal.n = Catalan(n+1) * r|^n) &
     (abs r < 1/4 implies
         Catal is absolutely_summable &
         Sum Catal = 1 + r * (Sum Catal) |^ 2 &
         $2=Sum Catal);
A1:for x st x in REAL ex y st y in REAL & E[x,y]
    proof
      let x; assume x in REAL;
      then reconsider r=x as Real;
      deffunc C(Element of NAT)=Catalan($1+1) * r|^$1;
      consider Cat be Real_Sequence such that
        A2:for n holds Cat.n = C(n) from FUNCT_2:sch 4;
      take Sum Cat;
      thus Sum Cat in REAL;
      let s such that A3:x=s;
      take Cat;
      thus for n holds Cat.n = Catalan(n+1) * s|^n by A2,A3;
      assume A4:abs s <1/4;
      set a=4*abs(r);set G=a GeoSeq;
      defpred P[Nat] means abs(Cat).$1 <= G.$1;
      A5:P[0]
        proof
           Cat.0=Catalan(0+1) * r|^0 by A2;
          then abs(Cat).0=abs(r|^0) & r|^0=1 & a|^0= 1 & abs(1)=1
            by SEQ_1:16,CATALAN1:11,NEWTON:9,ABSVALUE:def 1;
          hence thesis by PREPOWER:def 1;
        end;
      A6:for n st P[n] holds P[n+1]
        proof
          let n such that A7:P[n];set n1=n+1;
           Catalan(n1)>=0 & Catalan(n1+1)>=0 by CATALAN1:17;
          then abs(Catalan(n1+1))=Catalan(n1+1) & abs(Catalan(n1))=Catalan(n1)
            by ABSVALUE:def 1;
          then abs(Catalan(n1+1)) < 4*abs(Catalan(n1)) & abs(r|^n1) >=0&r|^n1=
            r*r|^n by CATALAN1:21,COMPLEX1:132,NEWTON:11;
          then abs(r|^n1)*abs(Catalan(n1+1))<=(4*abs(Catalan(n1)))*abs(r*r|^n)
            & abs(r*r|^n) =abs(r)*abs(r|^n) by XREAL_1:66,COMPLEX1:151;
          then abs(r|^n1*Catalan(n1+1))<=a*(abs(Catalan(n1))*abs(r|^n))
            by COMPLEX1:151;
          then abs(Cat.n1)<=a*(abs(Catalan(n1))*abs(r|^n)) by A2;
          then abs(Cat.n1)<=a*abs(Catalan(n1)*r|^n) & abs(r)>=0
            by COMPLEX1:132,151;
          then abs(Cat.n1)<=a*abs(Cat.n) & abs(Cat.n)=abs(Cat).n & a>=0*4
            by A2,SEQ_1:16,XREAL_1:66;
          then abs(Cat).n1<=a*abs(Cat).n & a*abs(Cat).n <= a*G.n
            by A7,SEQ_1:16,XREAL_1:66;
          then abs(Cat).n1<=a*G.n by XXREAL_0:2;
          hence thesis by PREPOWER:4;
        end;
      A8:for n holds P[n] from NAT_1:sch 1(A5,A6);
      A9:now let n;
           abs(Cat).n=abs(Cat.n) & abs(Cat.n)>=0 by SEQ_1:16,COMPLEX1:132;
          hence abs(Cat).n>=0;
      end;
       4 > 0 & abs(r)>=0 by COMPLEX1:132;
      then a<4*(1/4) & a>=4*0 by A3,A4,XREAL_1:66,70;
      then a<1 & abs(a)=a by ABSVALUE:def 1;
      then G is summable & Sum G = 1/(1-a) by SERIES_1:28;
      then abs(Cat) is summable by A8,A9,SERIES_1:24;
      hence A10:Cat is absolutely_summable by SERIES_1:def 5;
   for y st y in NAT holds (Cat^\1).y=(Cat (##) (r(#)Cat)).y
      proof let y;
        assume y in NAT; then reconsider n=y as Element of NAT;set n1=n+1;
        consider Fr1 such that
          A11:dom Fr1 = n1 and
          A12:for i st i in n+1 holds Fr1.i = Cat.i * (r(#)Cat).(n-'i) and
          A13:Sum Fr1 = (Cat (##) (r(#)Cat)).n by Def5;
        consider Catal be XFinSequence of NAT such that
          A14:Sum Catal = Catalan (n1+1) and
          A15:dom Catal = n1 and
          A16:for j st j<n1 holds Catal.j=Catalan(j+1)*Catalan(n1-'j) by Th43;
         rng Catal c= NAT by ORDINAL1:def 8;
        then rng Catal c= REAL by XBOOLE_1:1;
        then reconsider CatalR=Catal as XFinSequence of REAL by ORDINAL1:def 8;
        defpred Q[set,set] means for k st k=$1 holds $2 = r |^ n1 * Catal.k;
        A17:for k st k in n1 ex x be Element of REAL st Q[k,x]
        proof
          let k such that k in n1;
          take r |^ n1 * Catal.k;
          reconsider k'=k, n'=n1 as Element of NAT;
          thus thesis by XREAL_0:def 1;
        end;
        consider Fr2 such that
          A18:dom Fr2 = n1 and
          A19:for k st k in n1 holds Q[k,Fr2.k] from STIRL2_1:sch 6(A17);
         now let k such that A20:k in len Fr2;
            k in n1 by A20,A18,AFINSQ_1:def 1;
           hence Fr2.k= r|^n1 * CatalR.k by A19;
        end;
        then A21:Sum Fr2 = r|^n1 * (Sum CatalR) by A15,A18,Th49
                        .= r|^n1 *  Catalan (n1+1) by A14,Th59
                        .=Cat.n1 by A2
                        .=(Cat^\1).n by SEQM_3:def 9;
         now let k such that A22:k in dom Fr2;
          A23:k<n+1 by A18,A22,EULER_1:1;
          then k<=n & k<=n+1 by NAT_1:13;
          then A24:n-'k=n-k & n1-'k=n1-k & n=k+(n-k) by BINARITH:50;
          then Fr1.k = Cat.k * (r(#)Cat).(n-k) by A12,A18,A22
            .=Catalan(k+1) * r|^k * (r(#)Cat).(n-k) by A2
            .=Catalan(k+1) * r|^k * (r * Cat.(n-k)) by A24,SEQ_1:13
            .=Catalan(k+1) * r|^k*(r*(Catalan((n-'k)+1)*r|^(n-'k))) by A2,A24
            .=Catalan(k+1) * Catalan(n1-'k) * r *(r|^k*r|^(n-'k)) by A24
            .=Catalan(k+1) * Catalan(n1-'k) * r *(r|^n) by A24,NEWTON:13
            .=Catal.k * r * r|^n  by A16,A23
            .=Catal.k * (r * r|^n)
            .=Catal.k * r|^n1 by NEWTON:11
            .=Fr2.k by A18,A19,A22;
          hence Fr1.k=Fr2.k;
        end;
        hence thesis by A13,A21,A11,A18,AFINSQ_1:10;
      end;
      then A25:Cat^\1= Cat(##)(r(#)Cat) by SEQ_1:8;
      A26:Cat is summable by A10,SERIES_1:40;
      then r(#)Cat is summable & Sum(r(#)Cat)=r*Sum Cat &
        Cat.0=Catalan(0+1)*r|^0 & r|^0=1 by A2,SERIES_1:13,NEWTON:9;
      then Sum(Cat^\(0+1))=Sum Cat *(r*Sum Cat) & Partial_Sums(Cat).0=1
        by A10,A25,Th58,CATALAN1:11,SERIES_1:def 1;
      then Sum Cat= 1 + r*(Sum Cat*Sum Cat) by A26,SERIES_1:18;
      hence thesis by WSIERP_1:2,A3;
    end;
    consider SumC be Function of REAL,REAL such that
A27: for x st x in REAL holds E[x,SumC.x] from FUNCT_2:sch 1(A1);
A28:for r st r<>0 & abs r < 1/4 holds
      SumC.r=(1-sqrt(1-4*r))/(2*r) or SumC.r=(1+sqrt(1-4*r))/(2*r)
    proof
      let r such that A29:r<>0 & abs r < 1/4;
       r is Real by XREAL_0:def 1;
      then consider Catal be Real_Sequence such that
         for n holds Catal.n = Catalan(n+1) * r|^n and
        A30:abs r < 1/4 implies
           Catal is absolutely_summable &
           Sum Catal = 1 + r * (Sum Catal) |^ 2 & SumC.r=Sum Catal by A27;
      set S=Sum Catal;
       S=1+r*(S*S) by A29,A30,WSIERP_1:2
      .=1+r*S^2 by SQUARE_1:def 3;
      then A31:r*S^2+(-1)*S+1=0;
       r<=1/4 by A29,ABSVALUE:12;
      then 4*r <= (1/4)*4 by XREAL_1:66;
      then A32:4*r-4*r<=1-4*r by XREAL_1:11;
       delta(r,-1,1)= 1-4*r & -(-1)=1 by SQUARE_1:59,61;
      hence thesis by A29,A30,A31,A32,FIB_NUM:6;
    end;
A33:for r,s st 0<s & s <= r & r < 1/4 holds SumC.s <= SumC.r
    proof
      let r,s such that A34:0<s & s <= r & r < 1/4;
      A35:r >= 0 & s < 1/4 by A34,XXREAL_0:2;
       s is Real by XREAL_0:def 1;
      then consider Cs be Real_Sequence such that
        A36:for n holds Cs.n = Catalan(n+1) * s|^n and
        A37:abs s < 1/4 implies
           Cs is absolutely_summable &
           Sum Cs = 1 + s * (Sum Cs) |^ 2 & SumC.s=Sum Cs by A27;
       r is Real by XREAL_0:def 1;
      then consider Cr be Real_Sequence such that
        A38:for n holds Cr.n = Catalan(n+1) * r|^n and
        A39:abs r < 1/4 implies
           Cr is absolutely_summable &
           Sum Cr = 1 + r * (Sum Cr) |^ 2 & SumC.r=Sum Cr by A27;
      A40:now let n;
         s|^n <= r|^n & Catalan(n+1)>=0 by A34,PREPOWER:17,CATALAN1:17;
        then Catalan(n+1) * s|^n <= Catalan(n+1)*r|^n & Catalan(n+1)*r|^n=Cr.n
          by A38,XREAL_1:66;
        hence Cs.n <= Cr.n by A36;
      end;
       Cs is summable&Cr is summable by A34,A35,A37,A39
,SERIES_1:40,ABSVALUE:def 1;
      hence thesis by A34,A35,A37,A39,A40,TIETZE:7,ABSVALUE:def 1;
    end;
A41:for r,s st 0<r & r<s & s<1/4 holds
      (1+sqrt(1-4*r))/(2*r) > (1+sqrt(1-4*s))/(2*s)
    proof
      let r,s such that A42:0<r & r<s & s<1/4;
       4*r < 4*s & 4*s<4*(1/4) by A42,XREAL_1:70;
      then 1-4*r >= 1-4*s & 4*s-4*s < 1-4*s by XREAL_1:11,12;
      then sqrt(1-4*r)>= sqrt(1-4*s) & sqrt(1-4*s)>0 by SQUARE_1:93,
94;
      then 1+sqrt(1-4*r)>=1+sqrt(1-4*s) & 2*r>2*0 & 2*r<2*s &1+sqrt(1-4*s)>0+0
        by A42,XREAL_1:9,10,70;
      then (1+sqrt(1-4*r))/(2*r)>=(1+sqrt(1-4*s))/(2*r) &
        (1+sqrt(1-4*s))/(2*r)>(1+sqrt(1-4*s))/(2*s) by XREAL_1:74,78;
      hence thesis by XXREAL_0:2;
    end;
set R={r where r is Real:0<r & r<1/4 & SumC.r=(1+sqrt(1-4*r))/(2*r)};
A43:R={}
    proof
      assume R<>{};
      then consider x such that
   A44:x in R by XBOOLE_0:def 1;
      consider r be Real such that
   A45:x=r & 0<r & r<1/4 & SumC.r=(1+sqrt(1-4*r))/(2*r) by A44;
  A46:R c= {r}
      proof
        assume not R c= {r};
        then R\{r}<>{} by XBOOLE_1:37;
        then consider y such that
     A47:y in R\{r} by XBOOLE_0:def 1;
         y in R by A47;
        then consider s be Real such that
    A48:y=s & 0<s & s<1/4 & SumC.s=(1+sqrt(1-4*s))/(2*s);
    A49:r<>s by A47,A48,ZFMISC_1:64;
         now per cases by A49,REAL_1:def 5;
          suppose r>s;
            then SumC.s > SumC.r & SumC.s <= SumC.r by A33,A41,A45,A48;
            hence contradiction;
          end;
          suppose r<s;
            then SumC.r > SumC.s & SumC.r <= SumC.s by A33,A41,A45,A48;
            hence contradiction;
          end;
        end;
        hence contradiction;
      end;
      consider s be real number such that
        A50:r<s & s< 1/4 by A45,XREAL_1:7;
       s>0 & not s in R & s is Real
        by A45,A46,A50,TARSKI:def 1,XREAL_0:def 1;
      then SumC.s<>(1+sqrt(1-4*s))/(2*s) & abs(s)=s & s<>0
        by A50,ABSVALUE:def 1;
      then A51:SumC.s=(1-sqrt(1-4*s))/(2*s) by A28,A50;
       4*s<4*(1/4) & 4*r<4*(1/4)by A45,A50,XREAL_1:70;
      then 4*s-4*s < 1-4*s & 4*r-4*r < 1-4*r by XREAL_1:11;
      then sqrt(1-4*s)>0 & sqrt(1-4*r)>0 & s>0
        by SQUARE_1:93,A45,A50;
      then 1-sqrt(1-4*s) <= 1-0 & 1+0<1+sqrt(1-4*r) & 2*s>2*0 & 2*r>2*0&2*r<2*
s
        by A45,A50,XREAL_1:10,12,70;
      then (1-sqrt(1-4*s))/(2*s)<=1/(2*s) & 1/(2*r)<(1+sqrt(1-4*r))/(2*r)
        & 1/(2*s)<1/(2*r) by XREAL_1:74,76,78;
      then SumC.s<1/(2*r) & 1/(2*r) < SumC.r by A45,A51,XXREAL_0:2;
      then SumC.s<SumC.r & SumC.s>=SumC.r by A33,A45,A50,XXREAL_0:2;
      hence thesis;
    end;
A52:for r st 0<r & abs r <1/4 holds SumC.r=(1-sqrt(1-4*r))/(2*r)
    proof
      let r such that A53:0<r & abs(r)<1/4;
      assume SumC.r<>(1-sqrt(1-4*r))/(2*r);
      then SumC.r=(1+sqrt(1-4*r))/(2*r) & r is Real & abs(r)=r
        by A28,A53,ABSVALUE:def 1;
      then r in R by A53;
      hence thesis by A43;
    end;
A54:for r st r<0 & abs r<1/4 holds SumC.r=(1-sqrt(1-4*r))/(2*r)
    proof
      let r such that A55:r<0 & abs r<1/4;
       r is Real by XREAL_0:def 1;
      then consider Cr be Real_Sequence such that
        A56:for n holds Cr.n = Catalan(n+1) * (r|^n) and
        A57:abs r < 1/4 implies
           Cr is absolutely_summable &
           Sum Cr = 1 + r * (Sum Cr) |^ 2 & SumC.r=Sum Cr by A27;
       -r is Real by XREAL_0:def 1;
      then consider CR be Real_Sequence such that
        A58:for n holds CR.n = Catalan(n+1) * (-r)|^n and
        A59:abs (-r) < 1/4 implies
           CR is absolutely_summable &
           Sum CR = 1 + (-r) * (Sum CR) |^ 2 & SumC.(-r)=Sum CR by A27;
       now let x;assume x in NAT;
        then reconsider n=x as Element of NAT;
         (-r)|^n=((-1) *r)|^n
              .=(-1)|^n* r|^n by NEWTON:12;
        then A60:abs((-r)|^n)=abs((-1)|^n)*abs(r|^n) by COMPLEX1:151
                            .=1*abs(r|^n) by SERIES_2:1;
         0-r > 0-0 by A55,XREAL_1:12;
        then (-r)|^ n>=0 & Catalan(n+1)>=0 by POWER:3,CATALAN1:17;
        then abs((-r)|^ n)=(-r)|^ n & abs(Catalan(n+1))=Catalan(n+1)
          by ABSVALUE:def 1;
        then CR.n=abs(r|^n)*abs(Catalan(n+1)) by A58,A60
                .=abs(r|^n*Catalan(n+1)) by COMPLEX1:151
                .=abs(Cr.n) by A56
                .=abs(Cr).n by SEQ_1:16;
        hence abs(Cr).x=CR.x;
      end;
      then A61:abs(Cr)=CR & abs Sum Cr<=Sum abs Cr by A55,A57,SEQ_1:8,TIETZE:8;
    assume SumC.r<>(1-sqrt(1-4*r))/(2*r);
      then A62:SumC.r=(1+sqrt(1-4*r))/(2*r) & 0-r > 0-0 & abs(-r)<1/4
        by A55,XREAL_1:12,A28,COMPLEX1:138;
      then A63:abs((1+sqrt(1-4*r))/(2*r))<= Sum CR &
        Sum CR=(1-sqrt(1-4*(-r)))/(2*(-r)) by A52,A55,A57,A59,A61;
       1/4>= -r & r<1/4 by A55,A62,ABSVALUE:12;
      then 4*(1/4)>=4*(-r) & 4*(1/4)>4*r by XREAL_1:66,70;
      then 1-4*(-r)>=4*(-r)-4*(-r) & 1-4*r>4*r-4*r by XREAL_1:11;
      then sqrt(1-4*(-r))>=0 & sqrt(1-4*r)>0 by SQUARE_1:93,def 4;
      then 1-sqrt(1-4*(-r))<=1-0 & 1+sqrt(1-4*r)> 1+0 & 1+sqrt(1-4*r)>=0+0 &
        2*r<2*0 by A55,XREAL_1:10,12,70;
      then 1-sqrt(1-4*(-r)) < 1+sqrt(1-4*r) & 1+sqrt(1-4*r)=abs(1+sqrt(1-4*r))
&
        abs(2*r)=-(2*r) & 0-2*r > 0-0 by XXREAL_0:2,XREAL_1:12,ABSVALUE:def 1;
      then (1-sqrt(1-4*(-r)))/(2*(-r))<abs(1+sqrt(1-4*r))/abs(2*r)
        by XREAL_1:76;
      hence thesis by A63,COMPLEX1:153;
    end;
  let r;
   r is Real by XREAL_0:def 1;
  then consider Cat be Real_Sequence such that
     A64:for n holds Cat.n = Catalan(n+1) * r|^n and
     A65:abs r < 1/4 implies
         Cat is absolutely_summable &
         Sum Cat = 1 + r * (Sum Cat) |^ 2 &
         SumC.r=Sum Cat by A27;
  take Cat;
  thus for n holds Cat.n = Catalan(n+1) * r|^n by A64;
  assume A66:abs r<1/4;
  hence Cat is absolutely_summable & Sum Cat = 1 + r*(Sum Cat)|^ 2 by A65;
A67:r<>0 implies Sum Cat = (1 - sqrt(1-4*r)) / (2*r)
  proof
    assume r<>0;
    then r>0 or r<0;
    hence thesis by A52,A54,A65,A66;
  end;
  set s=sqrt(1-4*r);
   now per cases;
    suppose r=0;
      hence 2 / (1 + s)=Sum Cat by A65,A66,SQUARE_1:83;
    end;
    suppose A68:r<>0;
      then A69:2*r<>0;
       r<=1/4 by A66,ABSVALUE:12;
      then 4*r <=4*(1/4) by XREAL_1:66;
      then A70:1-4*r >= 4*r-4*r by XREAL_1:11;
      then s >=0 by SQUARE_1:def 4;
      then 1+s >= 1+0 by XREAL_1:9;
      then (1+s)/(1+s)=1 by XCMPLX_1:60;
      then (1-s) / (2*r)=((1-s) / (2*r)) * ((1+s)/(1+s))
          .=((1-s)*(1+s)) / ((2*r)*(1+s)) by XCMPLX_1:77
          .=(1^2-s^2)/ ((2*r)*(1+s)) by SQUARE_1:67
          .=(1-(1-4*r))/ ((2*r)*(1+s)) by A70,SQUARE_1:def 4,59
          .=((2*r)*2)/((2*r)*(1+s))
          .=((2*r)/(2*r))*(2/(1+s)) by XCMPLX_1:77
          .=(1)*(2/(1+s)) by A69,XCMPLX_1:60;
     hence Sum Cat=2/(1+s) by A67,A68;
    end;
  end;
  hence thesis by A67;
end;
