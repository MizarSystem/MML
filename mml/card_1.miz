:: Cardinal Numbers
::  by Grzegorz Bancerek
::
:: Received September 19, 1989
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies ORDINAL1, RELAT_1, FUNCT_1, BOOLE, WELLORD1, TARSKI, WELLORD2,
      ORDINAL2, FINSET_1, CARD_1, FUNCOP_1, COMPLEX1, GR_CY_1, ARYTM;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, ENUMSET1, FUNCT_1,
      FUNCOP_1, WELLORD1, ORDINAL1, ORDINAL2, WELLORD2, FINSET_1;
 constructors ENUMSET1, WELLORD1, WELLORD2, FUNCOP_1, ORDINAL2, FINSET_1;
 registrations XBOOLE_0, SUBSET_1, FUNCT_1, ORDINAL1, FUNCOP_1, FINSET_1;
 requirements NUMERALS, SUBSET, BOOLE;
 definitions ORDINAL1, TARSKI, FUNCT_1, FINSET_1, WELLORD2, XBOOLE_0, RELAT_1;
 theorems TARSKI, FUNCT_1, ZFMISC_1, ORDINAL1, ORDINAL2, WELLORD1, WELLORD2,
      RELAT_1, FINSET_1, XBOOLE_0, XBOOLE_1, FUNCOP_1, ENUMSET1, ORDINAL3;
 schemes ORDINAL1, ORDINAL2, FUNCT_1, FINSET_1, XBOOLE_0;

begin

reserve A,B,C for Ordinal,
  X,X1,Y,Y1,Z,a,b,b1,b2,x,y,z for set,
  R for Relation,
  f,g,h for Function,
  k,m,n for natural number;

notation
  synonym 0 for {};
end;

definition
  let IT be set;
  attr IT is cardinal means
  :Def1:
  ex B st IT = B & for A st A,B are_equipotent holds B c= A;
end;

registration
  cluster cardinal set;
  existence
  proof
    consider A;
    defpred P[Ordinal] means $1,A are_equipotent;
A1: ex A st P[A];
    consider B such that
A2: P[B] & for C st P[C] holds B c= C from ORDINAL1:sch 1(A1);
    reconsider IT = B as set;
    take IT,B;
    thus IT = B;
    let C;
    assume C,B are_equipotent;
    hence B c= C by A2,WELLORD2:22;
  end;
end;

definition
  mode Cardinal is cardinal set;
end;

registration
  cluster cardinal -> ordinal set;
  coherence
  proof
    let M be set;
    assume M is cardinal;
    then ex B st M = B & for A st A,B are_equipotent holds B c= A by Def1;
    hence thesis;
  end;
end;

reserve M,N for Cardinal;

canceled 3;

theorem Th4:
  for X ex A st X,A are_equipotent
proof
  let X;
  consider R such that
A1: R well_orders X by WELLORD2:26;
  set Q = R|_2 X;
A2: field Q = X & Q is well-ordering by A1,WELLORD2:25;
  take A = order_type_of Q;
  Q,RelIncl A are_isomorphic by A2,WELLORD2:def 2;
  then consider f such that
A3: f is_isomorphism_of Q,RelIncl A by WELLORD1:def 8;
A4: dom f = field Q & rng f = field RelIncl A & f is one-to-one &
  for a,b holds [a,b] in Q iff a in field Q & b in field Q & [f.a,f.b] in
  RelIncl A by A3,WELLORD1:def 7;
  take f;
  thus f is one-to-one & dom f = X & rng f = A by A1,A4,WELLORD2:25,def 1;
end;

notation
  let M,N;
  synonym M <=` N for M c= N;
  synonym M <` N for M in N;
end;

canceled 3;

theorem Th8:
  M = N iff M,N are_equipotent
proof
  thus M = N implies M,N are_equipotent;
  consider A such that
A1: M = A & for C st C,A are_equipotent holds A c= C by Def1;
  consider B such that
A2: N = B & for C st C,B are_equipotent holds B c= C by Def1;
  assume
A3: M,N are_equipotent;
  then
A4: B c= A & N,M are_equipotent by A1,A2;
  A c= B by A1,A2,A3;
  hence thesis by A1,A2,A4,XBOOLE_0:def 10;
end;

canceled 4;

theorem
  M <` N iff M <=` N & M <> N
proof
  M c< N iff M c= N & M <> N by XBOOLE_0:def 8;
  hence thesis by ORDINAL1:21,def 2;
end;

theorem
  M <` N iff not N <=` M by ORDINAL1:7,26;

definition
  let X;
  canceled 3;
  func Card X -> Cardinal means
  :Def5:
  X,it are_equipotent;
  existence
  proof
    defpred P[Ordinal] means $1,X are_equipotent;
A1: ex A st P[A] by Th4;
    consider A such that
A2: P[A] & for B st P[B] holds A c= B from ORDINAL1:sch 1(A1);
    A is cardinal
    proof
      take A; thus A = A;
      let B;
      assume B,A are_equipotent;
      hence A c= B by A2,WELLORD2:22;
    end;
    then reconsider M = A as Cardinal;
    take M;
    thus X,M are_equipotent by A2;
  end;
  uniqueness
  proof
    let M,N such that
A3: X,M are_equipotent and
A4: X,N are_equipotent;
    M,N are_equipotent by A3,A4,WELLORD2:22;
    hence thesis by Th8;
  end;
  projectivity;
end;

registration
 cluster {} -> cardinal;
 coherence
  proof
    take {}; thus {} = {};
    let A such that A,{} are_equipotent;
    thus thesis by XBOOLE_1:2;
  end;
end;

registration let X be empty set;
 cluster Card X -> empty;
 coherence by Def5;
end;

registration let X be non empty set;
 cluster Card X -> non empty;
 coherence
  proof assume Card X is empty;
    then X,{} are_equipotent by Def5;
    then ex f st f is one-to-one & dom f = X & rng f = {} by WELLORD2:def 4;
   hence contradiction by RELAT_1:65;
  end;
end;

canceled 6;

theorem Th21:
  X,Y are_equipotent iff Card X = Card Y
proof
A1: X,Card X are_equipotent & Y,Card Y are_equipotent by Def5;
  thus X,Y are_equipotent implies Card X = Card Y
  proof
    assume X,Y are_equipotent;
    then Card X,Y are_equipotent by A1,WELLORD2:22;
    hence thesis by Def5;
  end;
  assume Card X = Card Y;
  hence X,Y are_equipotent by A1,WELLORD2:22;
end;

theorem Th22:
  R is well-ordering implies field R,order_type_of R are_equipotent
proof
  assume R is well-ordering;
  then R,RelIncl order_type_of R are_isomorphic by WELLORD2:def 2;
  then consider f such that
A1: f is_isomorphism_of R,RelIncl order_type_of R by WELLORD1:def 8;
  take f;
  field RelIncl order_type_of R = order_type_of R by WELLORD2:def 1;
  hence thesis by A1,WELLORD1:def 7;
end;

theorem Th23:
  X c= M implies Card X <=` M
proof
  defpred P[Ordinal] means $1 c= M & X,$1 are_equipotent;
  reconsider m = M as Ordinal;
  assume X c= M;
  then order_type_of RelIncl X c= m & RelIncl X is well-ordering &
  field RelIncl X = X by WELLORD2:9,17,def 1;
  then
A1: ex A st P[A] by Th22;
  consider A such that
A2: P[A] & for B st P[B] holds A c= B from ORDINAL1:sch 1(A1);
  A is cardinal
  proof
    take A; thus A = A;
    let B;
    assume
A3: B,A are_equipotent;
    assume
A4: not A c= B;
    then m c= B by A2,A3,WELLORD2:22;
    hence contradiction by A2,A4,XBOOLE_1:1;
  end;
  then reconsider A as Cardinal;
  Card X = A by A2,Def5;
  hence Card X c= M by A2;
end;

theorem Th24:
  Card A c= A
proof
A1: ex B st Card A = B & for C st C,B are_equipotent holds B c= C by Def1;
  A,Card A are_equipotent by Def5;
  hence thesis by A1;
end;

theorem
  X in M implies Card X <` M
proof
  assume
A1: X in M;
  then reconsider A = X as Ordinal by ORDINAL1:23;
  Card A c= A & Card X = Card A by Th24;
  hence Card X in M by A1,ORDINAL1:22;
end;

theorem Th26:
  Card X <=` Card Y iff ex f st f is one-to-one & dom f = X & rng f c= Y
proof
  thus Card X <=` Card Y implies
  ex f st f is one-to-one & dom f = X & rng f c= Y
  proof
    assume
A1: Card X c= Card Y;
A2: X,Card X are_equipotent & Y,Card Y are_equipotent by Def5;
    then consider f such that
A3: f is one-to-one & dom f = X & rng f = Card X by WELLORD2:def 4;
    consider g such that
A4: g is one-to-one & dom g = Y & rng g = Card Y by A2,WELLORD2:def 4;
    take h = g"*f;
    g" is one-to-one by A4,FUNCT_1:62;
    hence h is one-to-one by A3,FUNCT_1:46;
    rng g = dom(g") & dom g = rng(g") by A4,FUNCT_1:55;
    hence dom h = X & rng h c= Y by A1,A3,A4,RELAT_1:45,46;
  end;
  given f such that
A5: f is one-to-one & dom f = X & rng f c= Y;
  Y,Card Y are_equipotent by Def5;
  then consider g such that
A6: g is one-to-one & dom g = Y & rng g = Card Y by WELLORD2:def 4;
A7: X,rng(g*f) are_equipotent
  proof
    take g*f;
    thus g*f is one-to-one by A5,A6,FUNCT_1:46;
    thus dom(g*f) = X by A5,A6,RELAT_1:46;
    thus thesis;
  end;
A8: Card rng(g*f) <=` Card Y by A6,Th23,RELAT_1:45;
  X,Card X are_equipotent & rng(g*f),Card rng(g*f) are_equipotent by Def5;
  then Card X,X are_equipotent &
  X,Card rng(g*f) are_equipotent by A7,WELLORD2:22;
  then Card X,Card rng(g*f) are_equipotent by WELLORD2:22;
  hence Card X <=` Card Y by A8,Th8;
end;

theorem Th27:
  X c= Y implies Card X <=` Card Y
proof
  assume
A1: X c= Y;
  ex f st f is one-to-one & dom f = X & rng f c= Y
  proof
    take id X;
    thus thesis by A1,RELAT_1:71;
  end;
  hence thesis by Th26;
end;

theorem
  Card X <=` Card Y iff ex f st dom f = Y & X c= rng f
proof
  thus Card X <=` Card Y implies ex f st dom f = Y & X c= rng f
  proof
    assume Card X <=` Card Y;
    then consider f such that
A1: f is one-to-one & dom f = X & rng f c= Y by Th26;
    defpred
    P[set,set] means $1 in rng f & $2 = f".$1 or not $1 in rng f & $2 = 0;
A2: for x st x in Y ex y st P[x,y]
    proof
      let x such that x in Y;
      not x in rng f implies thesis;
      hence thesis;
    end;
A3: for x,y,z st x in Y & P[x,y] & P[x,z] holds y = z;
    consider g such that
A4: dom g = Y & for y st y in Y holds P[y,g.y] from FUNCT_1:sch 2(A3,A2);
    take g; thus dom g = Y by A4;
    let x;
    assume x in X;
    then
A5: f".(f.x) = x & f.x in rng f by A1,FUNCT_1:56,def 5;
    then x = g.(f.x) by A1,A4;
    hence x in rng g by A1,A4,A5,FUNCT_1:def 5;
  end;
  given f such that
A6: dom f = Y & X c= rng f;
  deffunc f(set) = f"{$1};
  consider g such that
A7: dom g = X & for x st x in X holds g.x = f(x) from FUNCT_1:sch 3;
  X <> {} implies thesis
  proof
    assume X <> {};
    then reconsider M = rng g as non empty set by A7,RELAT_1:65;
    for Z st Z in M holds Z <> {}
    proof
      let Z;
      assume Z in M;
      then consider x such that
A8:   x in dom g & Z = g.x by FUNCT_1:def 5;
A9:   Z = f"{x} & x in rng f by A6,A7,A8;
      consider y such that
A10:  y in dom f & x = f.y by A6,A7,A8,FUNCT_1:def 5;
      x in {x} by TARSKI:def 1;
      hence thesis by A9,A10,FUNCT_1:def 13;
    end;
    then consider F being Function such that
A11: dom F = M & for Z st Z in M holds F.Z in Z by WELLORD2:28;
A12: dom(F*g) = X by A7,A11,RELAT_1:46;
A13: F*g is one-to-one
    proof
      let x,y;
      assume
A14:  x in dom(F*g) & y in dom(F*g) & (F*g).x = (F*g).y;
      then
A15:  g.x = f"{x} & g.y = f"{y} & (F*g).x = F.(g.x) & (F*g).y = F. ( g.y)
      by A7,A12,FUNCT_1:23;
      then F.(f"{x}) = F.(f"{y}) & f"{x} in M & f"{y} in M
      by A7,A12,A14,FUNCT_1:def 5;
      then F.(f"{x}) in f"{x} & F.(f"{y}) in f"{y} by A11;
      then f.(F.(f"{x})) in {x} & f.(F.(f"{y})) in {y} by FUNCT_1:def 13;
      then f.(F.(f"{x})) = x & f.(F.(f"{y})) = y by TARSKI:def 1;
      hence thesis by A14,A15;
    end;
    rng(F*g) c= Y
    proof
      let x;
      assume x in rng(F*g);
      then consider y such that
A16:  y in dom(F*g) & x = (F*g).y by FUNCT_1:def 5;
A17:  g.y = f"{y} & x = F.(g.y) by A7,A12,A16,FUNCT_1:23;
      then f"{y} in M by A7,A12,A16,FUNCT_1:def 5;
      then x in f"{y} by A11,A17;
      hence x in Y by A6,FUNCT_1:def 13;
    end;
    hence thesis by A12,A13,Th26;
  end;
  hence thesis by Th27,XBOOLE_1:2;
end;

theorem Th29:
  not X,bool X are_equipotent
proof
  given f such that
A1: f is one-to-one & dom f = X & rng f = bool X;
  defpred P[set] means for Y st Y = f.$1 holds not $1 in Y;
  consider Z such that
A2: a in Z iff a in X & P[a] from XBOOLE_0:sch 1;
  Z c= X
  proof
    let a; thus thesis by A2;
  end;
  then consider a such that
A3: a in X & Z = f.a by A1,FUNCT_1:def 5;
  not a in Z by A2,A3;
  then ex Y st Y = f.a & a in Y by A2,A3;
  hence contradiction by A2,A3;
end;

theorem Th30:
  Card X <` Card bool X
proof
  deffunc f(set) = {$1};
  consider f such that
A1: dom f = X & for x st x in X holds f.x = f(x) from FUNCT_1:sch 3;
A2: f is one-to-one
  proof
    let x,y;
    assume
A3: x in dom f & y in dom f & f.x = f.y;
    then f.x = { x } & f.y = { y } by A1;
    hence thesis by A3,ZFMISC_1:6;
  end;
  rng f c= bool X
  proof
    let x;
    assume x in rng f;
    then consider y such that
A4: y in dom f & x = f.y by FUNCT_1:def 5;
A5: f.y = { y } by A1,A4;
    { y } c= X
    proof
      let z;
      assume z in { y };
      hence thesis by A1,A4,TARSKI:def 1;
    end;
    hence thesis by A4,A5;
  end;
  then
A6: Card X <=` Card bool X by A1,A2,Th26;
  not X,bool X are_equipotent by Th29;
  then Card X <> Card bool X by Th21;
  then Card X c< Card bool X by A6,XBOOLE_0:def 8;
  hence thesis by ORDINAL1:21;
end;

definition
  let X;
  func nextcard X -> Cardinal means
  :Def6:
  Card X <` it & for M st Card X <` M holds it <=` M;
  existence
  proof
    defpred P[Ordinal] means ex M st $1 = M & Card X <` M;
    Card X <` Card bool X & Card bool X = Card bool X by Th30;
    then
A1: ex A st P[A];
    consider A such that
A2: P[A] and
A3: for B st P[B] holds A c= B from ORDINAL1:sch 1(A1);
    consider M such that
A4: A = M & Card X <` M by A2;
    take M; thus Card X <` M by A4;
    let N;
    assume Card X <` N;
    hence M c= N by A3,A4;
  end;
  uniqueness
  proof
    let M1,M2 be Cardinal such that
A5: Card X <` M1 & for M st Card X <` M holds M1 <=` M and
A6: Card X <` M2 & for M st Card X <` M holds M2 <=` M;
    M1 <=` M2 & M2 <=` M1 by A5,A6;
    hence thesis by XBOOLE_0:def 10;
  end;
end;

canceled;

theorem Th32:
  M <` nextcard M
proof Card M <` nextcard M & M = Card M by Def5,Def6;
  hence thesis;
end;

theorem
  Card {} <` nextcard X
proof
   Card {} <=` Card X & Card X <` nextcard X by Def6,XBOOLE_1:2;
  hence thesis by ORDINAL1:22;
end;

theorem Th34:
  Card X = Card Y implies nextcard X = nextcard Y
proof
  assume
A1: Card X = Card Y;
  Card X <` nextcard X & for N st Card X <` N holds nextcard X <=` N by Def6;
  hence thesis by A1,Def6;
end;

theorem Th35:
  X,Y are_equipotent implies nextcard X = nextcard Y
proof
  assume X,Y are_equipotent;
  then Card X = Card Y by Th21;
  hence thesis by Th34;
end;

theorem
  A in nextcard A
proof
  assume not A in nextcard A;
  then nextcard A c= A & A,Card A are_equipotent by Def5,ORDINAL1:26;
  then Card nextcard A <=` Card A & Card nextcard A = nextcard A &
  nextcard Card A = nextcard A & Card A <` nextcard Card A
  by Def5,Th27,Th32,Th35;
  hence contradiction by ORDINAL1:7;
end;

reserve S for T-Sequence;

definition
  let M;
  attr M is limit means
  :Def7:
  not ex N st M = nextcard N;
end;

notation
  let M;
  synonym M is_limit_cardinal for M is limit;
end;

definition
  let A;
  func alef A -> set means
  :Def8:
  ex S st it = last S & dom S = succ A & S.{} = Card omega &
  (for B st succ B in succ A holds S.succ B = nextcard union { S.B }) &
  for B st B in succ A & B <> {} & B is_limit_ordinal
  holds S.B = Card sup(S|B);
  correctness
  proof
    deffunc C(Ordinal,set) = nextcard union {$2};
    deffunc D(Ordinal,T-Sequence) = Card sup $2;
    set B = Card omega;
    thus (ex x,S st x = last S & dom S = succ A & S.{} = B &
    (for C st succ C in succ A holds S.succ C = C(C,S.C)) &
    for C st C in succ A & C <> {} & C is_limit_ordinal
    holds S.C = D(C,S|C) ) & for x1,x2 being set st
    (ex S st x1 = last S & dom S = succ A & S.{} = B &
    (for C st succ C in succ A holds S.succ C = C(C,S.C)) &
    for C st C in succ A & C <> {} & C is_limit_ordinal
    holds S.C = D(C,S|C) ) &
    (ex S st x2 = last S & dom S = succ A & S.{} = B &
    (for C st succ C in succ A holds S.succ C = C(C,S.C)) &
    for C st C in succ A & C <> {} & C is_limit_ordinal holds S.C = D(C,S|C) )
    holds x1 = x2 from ORDINAL2:sch 7;
  end;
end;

Lm1: now
  deffunc F(Ordinal) = alef $1;
  deffunc C(Ordinal,set) = nextcard union { $2 };
  deffunc D(Ordinal,T-Sequence) = Card sup $2;
A1: for A,x holds x = F(A) iff
  ex S st x = last S & dom S = succ A & S.{} = Card omega &
  (for C st succ C in succ A holds S.succ C = C(C,S.C)) &
  for C st C in succ A & C <> {} & C is_limit_ordinal
  holds S.C = D(C,S|C) by Def8;
  F({}) = Card omega from ORDINAL2:sch 8(A1);
  hence alef 0 = Card omega;
  thus for A holds F(succ A) = C(A,F(A)) from ORDINAL2:sch 9(A1);
  thus A <> {} & A is_limit_ordinal implies
  for S st dom S = A & for B st B in A holds S.B = alef B holds
  alef A = Card sup S
  proof
    assume
A2: A <> {} & A is_limit_ordinal;
    let S such that
A3: dom S = A and
A4: for B st B in A holds S.B = F(B);
    thus F(A) = D(A,S) from ORDINAL2:sch 10(A1,A2,A3,A4);
  end;
end;
deffunc f(Ordinal) = alef $1;

registration
  let A;
  cluster alef A -> cardinal;
  coherence
  proof
A1: now
      given B such that
A2:   A = succ B;
      alef A = nextcard union {alef B} by A2,Lm1;
      hence thesis;
    end;
    now
      assume
A3:   A <> {} & for B holds A <> succ B;
      then
A4:   A is_limit_ordinal by ORDINAL1:42;
      consider S such that
A5:   dom S = A & for B st B in A holds S.B = f(B) from ORDINAL2:sch 2;
      alef A = Card sup S by A3,A4,A5,Lm1;
      hence alef A is Cardinal;
    end;
    hence thesis by A1,Lm1;
  end;
end;

canceled;

theorem
  alef 0 = Card omega by Lm1;

theorem Th39:
  alef succ A = nextcard alef A
proof
  thus alef succ A = nextcard union { alef A } by Lm1
    .= nextcard alef A by ZFMISC_1:31;
end;

theorem
  A <> {} & A is_limit_ordinal implies
  for S st dom S = A & for B st B in A holds S.B = alef B holds
  alef A = Card sup S by Lm1;

theorem Th41:
  A in B iff alef A <` alef B
proof
  defpred P[Ordinal] means for A st A in $1 holds alef A <` alef $1;
A1: P[{}];
A2: for B st P[B] holds P[succ B]
  proof
    let B such that
A3: P[B];
    let A;
    assume
A4: A in succ B;
A5: alef succ B = nextcard alef B by Th39;
A6: A c< B iff A c= B & A <> B by XBOOLE_0:def 8;
    now
      assume A in B;
      then alef A <` alef B & alef B <` nextcard alef B by A3,Th32;
      hence thesis by A5,ORDINAL1:19;
    end;
    hence thesis by A4,A5,A6,Th32,ORDINAL1:21,34;
  end;
A7: for B st B <> {} & B is_limit_ordinal &
  for C st C in B holds P[C] holds P[B]
  proof
    let B such that
A8: B <> {} & B is_limit_ordinal and
    for C st C in B for A st A in C holds alef A <` alef C;
    consider S such that
A9: dom S = B and
A10: for C st C in B holds S.C = f(C) from ORDINAL2:sch 2;
A11: alef B = Card sup S by A8,A9,A10,Lm1;
    let A;
    assume A in B;
    then succ A in B by A8,ORDINAL1:41;
    then S.succ A in rng S & S.succ A = alef succ A &
    alef succ A = alef succ A by A9,A10,FUNCT_1:def 5;
    then alef succ A in sup rng S & sup rng S = sup S by ORDINAL2:27,35;
    then alef succ A c= sup S & Card alef succ A = alef succ A
    by Def5,ORDINAL1:def 2;
    then
A12: alef succ A <=` alef B by A11,Th27;
    alef succ A = nextcard alef A & alef A <` nextcard alef A by Th32,Th39;
    hence thesis by A12;
  end;
A13: for B holds P[B] from ORDINAL2:sch 1(A1,A2,A7);
  hence A in B implies alef A <` alef B;
  assume
A14: alef A <` alef B;
  then
A15: not B in A by A13;
  alef A <> alef B by A14;
  hence thesis by A15,ORDINAL1:24;
end;

theorem Th42:
  alef A = alef B implies A = B
proof
  assume
A1: alef A = alef B;
A2: now
    assume A in B;
    then alef A <` alef B by Th41;
    hence contradiction by A1;
  end;
  now
    assume B in A;
    then alef B <` alef A by Th41;
    hence contradiction by A1;
  end;
  hence thesis by A2,ORDINAL1:24;
end;

theorem
  A c= B iff alef A <=` alef B
proof
  (A c< B iff A <> B & A c= B) &
  (alef A c< alef B iff alef A <> alef B & alef A c= alef B)
  by XBOOLE_0:def 8;
  then (A in B iff alef A <` alef B) & (alef A = alef B implies A = B) &
  (alef A <=` alef B iff alef A = alef B or alef A <` alef B) &
  (A c= B iff A = B or A in B) by Th41,Th42,ORDINAL1:21,def 2;
  hence thesis;
end;

theorem
  X c= Y & Y c= Z & X,Z are_equipotent implies
  X,Y are_equipotent & Y,Z are_equipotent
proof
  assume X c= Y & Y c= Z & X,Z are_equipotent;
  then Card X <=` Card Y & Card Y <=` Card Z & Card X = Card Z by Th21,Th27;
  then Card X = Card Y & Card Y = Card Z by XBOOLE_0:def 10;
  hence X,Y are_equipotent & Y,Z are_equipotent by Th21;
end;

theorem
  bool Y c= X implies Card Y <` Card X & not Y,X are_equipotent
proof
  assume bool Y c= X;
  then Card Y <` Card bool Y & Card bool Y <=` Card X by Th27,Th30;
  hence Card Y <` Card X;
  then Card Y <> Card X;
  hence thesis by Th21;
end;

theorem Th46:
  X,{} are_equipotent implies X = {}
proof
    given f such that
A1: f is one-to-one & dom f = X & rng f = {};
    thus thesis by A1,RELAT_1:65;
end;

theorem
  Card {} = {};

theorem Th48:
  X,{x} are_equipotent iff ex x st X = { x }
proof
  thus X,{x} are_equipotent implies ex x st X = { x }
  proof
    assume X,{x} are_equipotent;
    then consider f such that
A1: f is one-to-one & dom f = { x } & rng f = X by WELLORD2:def 4;
    rng f = { f.x } by A1,FUNCT_1:14;
    hence thesis by A1;
  end;
  given y such that
A2: X = { y };
  take f = X --> x;
A3: dom f = X & for y st y in X holds f.y = x by FUNCOP_1:13,19;
  thus f is one-to-one
  proof
    let a,b;
    assume a in dom f & b in dom f & f.a = f.b;
    then a = y & b = y by A2,A3,TARSKI:def 1;
    hence thesis;
  end;
  thus dom f = X by FUNCOP_1:19;
  thus rng f c= { x } by FUNCOP_1:19;
  let a;
  assume a in { x };
  then
A4: a = x & y in { y } by TARSKI:def 1;
  then f.y = x by A2,FUNCOP_1:13;
  hence a in rng f by A2,A3,A4,FUNCT_1:def 5;
end;

theorem
  Card X = Card { x } iff ex x st X = { x }
proof
  (Card X = Card { x } iff X,{x} are_equipotent) &
  (X,{x} are_equipotent iff ex x st X = { x }) by Th21,Th48;
  hence thesis;
end;

theorem Th50:
  Card { x } = 1
proof
A1: 1 = succ 0;
  1 is cardinal
  proof
    take IT = 1; thus 1 = IT;
    let A;
    assume A,IT are_equipotent;
    then ex y st A = { y } by A1,Th48;
    hence IT c= A by A1,ZFMISC_1:39;
  end;
  then reconsider M = 1 as Cardinal;
  { x },M are_equipotent by A1,Th48;
  hence thesis by Def5;
end;

canceled 7;

theorem Th58:
  X misses X1 & Y misses Y1 & X,Y are_equipotent &
  X1,Y1 are_equipotent implies X \/ X1,Y \/ Y1 are_equipotent
proof
  assume
A1: X /\ X1 = {} & Y /\ Y1 = {};
  given f such that
A2: f is one-to-one & dom f = X & rng f = Y;
  given g such that
A3: g is one-to-one & dom g = X1 & rng g = Y1;
  defpred P[set,set] means $1 in X & $2 = f.$1 or $1 in X1 & $2 = g.$1;
A4: for x st x in X \/ X1 ex y st P[x,y]
  proof
    let x;
    assume x in X \/ X1;
    then x in X or x in X1 by XBOOLE_0:def 2;
    hence thesis;
  end;
A5: for x,y,z st x in X \/ X1 & P[x,y] & P[x,z] holds y = z by A1,
  XBOOLE_0:def 3;
  consider h such that
A6: dom h = X \/ X1 and
A7: for x st x in X \/ X1 holds P[x,h.x] from FUNCT_1:sch 2(A5,A4);
  take h;
  thus h is one-to-one
  proof
    let x,y;
    assume
A8: x in dom h & y in dom h & h.x = h.y;
    then
A9: (x in X & h.x = f.x or x in X1 & h.x = g.x) &
    (y in X & h.y = f.y or y in X1 & h.y = g.y) by A6,A7;
A10: now
      assume x in X & y in X1;
      then not x in X1 & not y in X & f.x in Y & g.y in Y1
      by A1,A2,A3,FUNCT_1:def 5,XBOOLE_0:def 3;
      hence contradiction by A1,A8,A9,XBOOLE_0:def 3;
    end;
    now
      assume y in X & x in X1;
      then not y in X1 & not x in X & f.y in Y & g.x in Y1
      by A1,A2,A3,FUNCT_1:def 5,XBOOLE_0:def 3;
      hence contradiction by A1,A8,A9,XBOOLE_0:def 3;
    end;
    hence thesis by A2,A3,A8,A9,A10,FUNCT_1:def 8;
  end;
  thus dom h = X \/ X1 by A6;
  thus rng h c= Y \/ Y1
  proof
    let x;
    assume x in rng h;
    then consider y such that
A11: y in dom h & x = h.y by FUNCT_1:def 5;
A12: y in X & x = f.y or y in X1 & x = g.y by A6,A7,A11;
A13: now
      assume y in X;
      then x in Y by A1,A2,A12,FUNCT_1:def 5,XBOOLE_0:def 3;
      hence x in Y \/ Y1 by XBOOLE_0:def 2;
    end;
    now
      assume y in X1;
      then x in Y1 by A1,A3,A12,FUNCT_1:def 5,XBOOLE_0:def 3;
      hence x in Y \/ Y1 by XBOOLE_0:def 2;
    end;
    hence thesis by A6,A11,A13,XBOOLE_0:def 2;
  end;
  let x such that
A14: x in Y \/ Y1;
A15: now
    assume x in Y;
    then consider y such that
A16: y in dom f & x = f.y by A2,FUNCT_1:def 5;
A17: y in X \/ X1 by A2,A16,XBOOLE_0:def 2;
    then P[y,h.y] by A7;
    hence x in rng h by A1,A2,A6,A16,A17,FUNCT_1:def 5,XBOOLE_0:def 3;
  end;
  now
    assume x in Y1;
    then consider y such that
A18: y in dom g & x = g.y by A3,FUNCT_1:def 5;
A19: y in X \/ X1 by A3,A18,XBOOLE_0:def 2;
    then P[y,h.y] by A7;
    hence x in rng h by A1,A3,A6,A18,A19,FUNCT_1:def 5,XBOOLE_0:def 3;
  end;
  hence thesis by A14,A15,XBOOLE_0:def 2;
end;

theorem Th59:
  x in X & y in X implies X \ { x },X \ { y } are_equipotent
proof
  assume
A1: x in X & y in X;
  defpred P[set,set] means $1 = y & $2 = x or $1 <> y & $1 = $2;
A2: for a st a in X \ { x } ex b st P[a,b]
  proof
    let a such that a in X \ { x };
    a = y implies thesis;
    hence thesis;
  end;
A3: for a,b1,b2 st a in X \ { x } & P[a,b1] & P[a,b2] holds b1 = b2;
  consider f such that
A4: dom f = X \ { x } & for a st a in X \ { x } holds P[a,f.a] from
  FUNCT_1:sch 2(A3, A2);
  take f;
  thus f is one-to-one
  proof
    let b1,b2;
    assume
A5: b1 in dom f & b2 in dom f & f.b1 = f.b2 & b1 <> b2;
    then P[b1,f.b1] & P[b2,f.b2] & not b1 in { x } & not b2 in { x }
    by A4,XBOOLE_0:def 4;
    hence thesis by A5,TARSKI:def 1;
  end;
  thus dom f = X \ { x } by A4;
  thus rng f c= X \ { y }
  proof
    let z;
    assume z in rng f;
    then consider a such that
A6: a in dom f & z = f.a by FUNCT_1:def 5;
A7: now
      assume a = y;
      then not y in { x } & z = x by A4,A6,XBOOLE_0:def 4;
      then y <> z & z in X by A1,TARSKI:def 1;
      then not z in { y } & z in X by TARSKI:def 1;
      hence z in X \ { y } by XBOOLE_0:def 4;
    end;
    now
      assume a <> y;
      then not a in { y } & a = z & a in X by A4,A6,TARSKI:def 1;
      hence z in X \ { y } by XBOOLE_0:def 4;
    end;
    hence thesis by A7;
  end;
  let z;
  assume
A8: z in X \ { y };
  then z in X & not z in { y } by XBOOLE_0:def 4;
  then
A9: z <> y by TARSKI:def 1;
A10: now
    assume
A11: z = x;
    then not y in { x } by A9,TARSKI:def 1;
    then
A12: y in dom f by A1,A4,XBOOLE_0:def 4;
    then z = f.y by A4,A11;
    hence z in rng f by A12,FUNCT_1:def 5;
  end;
  now
    assume z <> x;
    then not z in { x } by TARSKI:def 1;
    then
A13: z in X \ { x } by A8,XBOOLE_0:def 4;
    then z = f.z by A4,A9;
    hence z in rng f by A4,A13,FUNCT_1:def 5;
  end;
  hence z in rng f by A10;
end;

theorem Th60:
  X c= dom f & f is one-to-one implies X,f.:X are_equipotent
proof
  assume
A1: X c= dom f & f is one-to-one;
  take g = f|X;
  thus g is one-to-one by A1,FUNCT_1:84;
  thus dom g = X by A1,RELAT_1:91;
  thus rng g = f.:X by RELAT_1:148;
end;

theorem Th61:
  X,Y are_equipotent & x in X & y in Y implies
  X \ { x },Y \ { y } are_equipotent
proof
  given f such that
A1: f is one-to-one & dom f = X & rng f = Y;
  assume
A2: x in X & y in Y;
A3: X \ { x },f.:(X \ { x }) are_equipotent by A1,Th60;
A4: f.:(X \ { x }) = f.:X \ Im(f,x) by A1,FUNCT_1:123
    .= Y \ Im(f,x) by A1,RELAT_1:146
    .= Y \ { f.x } by A1,A2,FUNCT_1:117;
  f.x in Y by A1,A2,FUNCT_1:def 5;
  then Y \ { f.x },Y \ { y } are_equipotent by A2,Th59;
  hence thesis by A3,A4,WELLORD2:22;
end;

theorem Th62:
  succ X, succ Y are_equipotent implies X, Y are_equipotent
proof
A1: X = succ X \ {X} & Y = succ Y \ {Y} by ORDINAL1:52;
  X in succ X & Y in succ Y by ORDINAL1:10;
  hence thesis by A1,Th61;
end;

theorem Th63:
  n = {} or ex m st n = succ m
proof
  defpred P[natural number] means $1 = {} or ex m st $1 = succ m;
A1: P[{}];
A2: for a being natural number st P[a] holds P[succ a];
  thus P[n] from ORDINAL2:sch 17(A1,A2);
end;

theorem Th64:
  n,m are_equipotent implies n = m
proof
  defpred P[natural number] means for n st n,$1 are_equipotent holds n = $1;
A1: P[{}] by Th46;
A2: for a being natural number st P[a] holds P[succ a]
  proof
    let a be natural number such that
A3: P[a];
    let n;
    assume
A4: n,succ a are_equipotent;
    per cases;
    suppose n = {};
      hence n = succ a by A4,Th46;
    end;
    suppose n <> {};
      then ex m st n = succ m by Th63;
      hence n = succ a by A3,A4,Th62;
    end;
  end;
  P[m] from ORDINAL2:sch 17(A1,A2);
  hence thesis;
end;

theorem Th65:
  x in omega implies x is cardinal
proof
  assume that
A1: x in omega and
A2: for B st x = B ex C st C,B are_equipotent & not B c= C;
  reconsider A = x as Ordinal by A1,ORDINAL1:23;
  consider B such that
A3: B,A are_equipotent & not A c= B by A2;
  B in A by A3,ORDINAL1:26;
  then B in omega by A1,ORDINAL1:19;
  then reconsider n = A, m = B as natural number by A1,ORDINAL1:def 13;
  n,m are_equipotent by A3;
  hence contradiction by A3,Th64;
end;

registration
  cluster natural -> cardinal number;
  correctness
  proof
    let n be number;
    assume n is natural;
    then n in omega by ORDINAL1:def 13;
    hence thesis by Th65;
  end;
end;

theorem
  for n being natural number holds n = Card n by Def5;

canceled;

theorem Th68:
  X,Y are_equipotent & X is finite implies Y is finite
proof
  assume X,Y are_equipotent;
  then consider f such that
A1: f is one-to-one & dom f = X & rng f = Y by WELLORD2:def 4;
  given p being Function such that
A2: rng p = X and
A3: dom p in omega;
  take f*p; thus rng(f*p) = Y by A1,A2,RELAT_1:47;
  thus dom(f*p) in omega by A1,A2,A3,RELAT_1:46;
end;

theorem Th69:
  n is finite & Card n is finite
proof reconsider n as Element of omega by ORDINAL1:def 13;
  rng id n = n & dom id n = n by RELAT_1:71;
  then n is finite by FINSET_1:def 1;
  hence thesis by Def5;
end;

canceled;

theorem
  Card n = Card m implies n = m
proof
  assume
A1: Card n = Card m;
  reconsider n,m as Element of omega by ORDINAL1:def 13;
  Card n = n & Card m = m by Def5;
  hence thesis by A1;
end;

theorem
  Card n <=` Card m iff n c= m
proof
  Card n = n & Card m = m by Def5;
  hence thesis;
end;

theorem Th73:
  Card n <` Card m iff n in m
proof
  Card n = n & Card m = m by Def5;
  hence thesis;
end;

theorem Th74:
  X is finite implies ex n st X,n are_equipotent
proof
  defpred P[set] means ex n st $1,n are_equipotent;
  assume
A1: X is finite;
A2: P[{}];
A3: for Z,Y st Z in X & Y c= X & P[Y] holds P[Y \/ {Z}]
  proof
    let Z,Y such that Z in X & Y c= X;
    given n such that
A4: Y,n are_equipotent;
A5: Z in Y implies thesis
    proof
      assume Z in Y;
      then
A6:   { Z } c= Y by ZFMISC_1:37;
      take n; thus thesis by A4,A6,XBOOLE_1:12;
    end;
    not Z in Y implies thesis
    proof
      assume
A7:   not Z in Y;
      take m = succ n;
A8:   { Z },{ n } are_equipotent by Th48;
A9:   now
        assume n meets { n };
        then consider x being set such that
A10:    x in n & x in { n } by XBOOLE_0:3;
        x = n by A10,TARSKI:def 1;
        hence contradiction by A10;
      end;
      now consider x being Element of Y /\ { Z };
        assume Y /\ { Z } <> {};
        then x in Y & x in { Z } by XBOOLE_0:def 3;
        hence contradiction by A7,TARSKI:def 1;
      end;
      then Y misses { Z } by XBOOLE_0:def 7;
      hence thesis by A4,A8,A9,Th58;
    end;
    hence thesis by A5;
  end;
  thus P[X] from FINSET_1:sch 2(A1,A2,A3);
end;

canceled;

theorem Th76:
  nextcard Card n = Card succ n
proof reconsider sn = succ n as natural number;
  n in succ n by ORDINAL1:10;
  then
A1: Card n <` Card sn by Th73;
  for M st Card Card n <` M holds Card succ n <=` M
  proof
    let M such that
A2: Card Card n in M;
    Card n = n by Def5;
    then succ n c= M & Card succ n = succ n by A2,Def5,ORDINAL1:33;
    hence Card succ n c= M;
  end;
  hence thesis by A1,Def6;
end;

definition
  let n be natural number;
  redefine func succ n -> Element of omega;
  coherence by ORDINAL1:def 13;
end;

notation
  let X be finite set;
  synonym card X for Card X;
end;

definition
  let X be finite set;
  redefine
  canceled 3;
  func card X -> Element of omega;
  coherence
  proof consider n such that
A1: X,n are_equipotent by Th74;
    reconsider n as Element of omega by ORDINAL1:def 13;
    X,n are_equipotent by A1;
    hence thesis by Def5;
  end;
end;

canceled;

theorem
  card {} = 0;

theorem
  card { x } = 1 by Th50;

canceled;

theorem
  for X,Y being finite set holds
  X,Y are_equipotent implies card X = card Y by Th21;

theorem
  X is finite implies nextcard X is finite
proof
  assume X is finite;
  then reconsider X as finite set;
  Card X = Card card X & X,Card X are_equipotent by Def5;
  then Card(succ card X) = nextcard Card X & nextcard Card X = nextcard X
  by Th35,Th76;
  hence thesis by Th69;
end;

scheme CardinalInd { Sigma[set] }: for M holds Sigma[M]
provided
A1: Sigma[{}]
and
A2: for M st Sigma[M] holds Sigma[nextcard M]
and
A3: for M st M <> {} & M is_limit_cardinal & for N st N <` M holds Sigma[N]
holds Sigma[M]
proof
  defpred P[Ordinal] means $1 is Cardinal implies Sigma[$1];
A4: for A st for B st B in A holds P[B] holds P[A]
  proof
    let A such that
A5: for B st B in A holds P[B] and
A6: A is Cardinal;
    reconsider M = A as Cardinal by A6;
A7: now
      assume not M is_limit_cardinal;
      then consider N such that
A8:   M = nextcard N by Def7;
      N in M & N = N by A8,Th32;
      hence Sigma[M] by A2,A5,A8;
    end;
    now
      assume
A9:   M <> {} & M is_limit_cardinal;
      for N st N <` M holds Sigma[N] by A5;
      hence Sigma[M] by A3,A9;
    end;
    hence Sigma[A] by A1,A7;
  end;
A10: for A holds P[A] from ORDINAL1:sch 2(A4);
  let M;
  thus thesis by A10;
end;

scheme CardinalCompInd { Sigma[set] }: for M holds Sigma[M]
provided
A1: for M st for N st N <` M holds Sigma[N] holds Sigma[M]
proof
  defpred P[Ordinal] means $1 is Cardinal implies Sigma[$1];
A2: for A st for B st B in A holds P[B] holds P[A]
  proof
    let A such that
A3: for B st B in A holds B is Cardinal implies Sigma[B];
    assume A is Cardinal;
    then reconsider M = A as Cardinal;
    for N st N <` M holds Sigma[N] by A3;
    hence Sigma[A] by A1;
  end;
A4: for A holds P[A] from ORDINAL1:sch 2(A2);
  let M;
  thus thesis by A4;
end;

theorem Th83:
  alef 0 = omega
proof
  thus alef 0 c= omega by Lm1,Th24;
  thus omega c= alef 0
  proof
    let x;
    assume
A1: x in omega;
    then reconsider A = x as Ordinal by ORDINAL1:23;
    consider n being Element of omega such that
A2: A = n by A1;
    succ n c= omega by ORDINAL1:33;
    then Card succ n c= Card omega & n in succ n &
    Card succ n = succ n by Def5,Th27,ORDINAL1:10;
    hence x in alef 0 by A2,Lm1;
  end;
end;

theorem
  Card omega = omega by Lm1,Th83;

Lm2: omega is_limit_ordinal by ORDINAL1:def 12;

theorem
  Card omega is_limit_cardinal
proof
  given N such that
A1: Card omega = nextcard N;
  N in omega by A1,Lm1,Th32,Th83;
  then
A2: succ N in omega & N is natural by Lm2,ORDINAL1:41,def 13;
  reconsider n=N as Element of omega by A1,Lm1,Th32,Th83;
A3: nextcard Card n = Card succ n by Th76;
A4: Card n = n by Def5;
  Card succ n = succ n by Def5;
  hence contradiction by A1,A2,A3,A4,Lm1,Th83;
end;

registration
  cluster -> finite Element of omega;
  coherence by Th69;
end;

registration
  cluster finite Cardinal;
  existence
  proof consider n being Element of omega;
    take n;
    thus thesis;
  end;
end;

theorem
  for M being finite Cardinal ex n st M = Card n
proof
  let M be finite Cardinal;
  Card M = M & Card M = Card card M by Def5;
  hence thesis;
end;

registration
  let X be finite set;
  cluster card X -> finite;
  coherence;
end;

begin :: The meaning of numerals, 2006.08.25

theorem Th87:
  1 = { 0 }
proof
  thus 1 = succ 0
    .= { 0 };
end;

theorem Th88:
  2 = { 0,1 }
proof
  thus 2 = succ 1
    .= { 0,1 } by Th87,ENUMSET1:41;
end;

theorem Th89:
  3 = { 0,1,2 }
proof
  thus 3 = succ 2
    .= { 0,1,2 } by Th88,ENUMSET1:43;
end;

theorem Th90:
  4 = { 0,1,2,3 }
proof
  thus 4 = succ 3
    .= { 0,1,2,3 } by Th89,ENUMSET1:46;
end;

theorem Th91:
  5 = { 0,1,2,3,4 }
proof
  thus 5 = succ 4
    .= { 0,1,2,3,4 } by Th90,ENUMSET1:50;
end;

theorem Th92:
  6 = { 0,1,2,3,4,5 }
proof
  thus 6 = succ 5
    .= { 0,1,2,3,4,5 } by Th91,ENUMSET1:55;
end;

theorem Th93:
  7 = { 0,1,2,3,4,5,6 }
proof
  thus 7 = succ 6
    .= { 0,1,2,3,4,5,6 } by Th92,ENUMSET1:61;
end;

theorem Th94:
  8 = { 0,1,2,3,4,5,6,7 }
proof
  thus 8 = succ 7
    .= { 0,1,2,3,4,5,6,7 } by Th93,ENUMSET1:68;
end;

theorem Th95:
  9 = { 0,1,2,3,4,5,6,7,8 }
proof
  thus 9 = succ 8
    .= { 0,1,2,3,4,5,6,7,8 } by Th94,ENUMSET1:134;
end;

theorem
  10 = { 0,1,2,3,4,5,6,7,8,9 }
proof
  thus 10 = succ 9
    .= { 0,1,2,3,4,5,6,7,8,9 } by Th95,ENUMSET1:135;
end;

:: Moved from FRECHET2 by AK on 27.12.2006

theorem
  for f being Function st dom f is infinite & f is one-to-one holds
  rng f is infinite
proof
  let f be Function;
  assume
A1: dom f is infinite & f is one-to-one;
  then dom f,rng f are_equipotent by WELLORD2:def 4;
  hence rng f is infinite by A1,Th68;
end;

:: from ALGSEQ_1, 2007.03.18, A.T.

reserve k,n,m for natural number;

definition
  let n;
  func Segm n -> set equals

  n;
  coherence;
end;

reserve l for Element of omega;

definition
  let n;
  redefine func Segm n -> Subset of omega;
  coherence
  proof
    n in omega by ORDINAL1:def 13;
    hence thesis by ORDINAL1:26;
  end;
end;

canceled 4;

:: from CARD_4, 2007.08.16, A.T.

theorem Th102:
  A,n are_equipotent implies A = n
proof
  defpred P[natural number] means for A st A,$1 are_equipotent holds A = $1;
A1: P[{}] by Th46;
A2: for n st P[n] holds P[succ n]
  proof
    let n such that
A3: P[n];
    let A;
    assume
A4: A,succ n are_equipotent;
    then A <> {} by Th46;
    then
A5: {} in A by ORDINAL3:10;
    now
      assume A is_limit_ordinal;
      then
A6:   omega c= A by A5,ORDINAL1:def 12;
      Card succ n = succ n by Def5;
      then Card omega <=` Card A & Card A = Card (succ n) &
      Card (succ n) <` Card omega by A4,A6,Lm1,Th21,Th27,Th83;
      hence contradiction by ORDINAL1:7;
    end;
    then consider B such that
A7: A = succ B by ORDINAL1:42;
    B in A & n in (succ n) by A7,ORDINAL1:10;
    then A \ {B},(succ n) \ { n} are_equipotent & A\{B} = B &
    (succ n) \ { n} = n by A4,A7,Th61,ORDINAL1:52;
    hence thesis by A3,A7;
  end;
  P[n] from ORDINAL2:sch 17(A1,A2);
  hence thesis;
end;

theorem Th103:
  A is finite iff A in omega
proof
  thus A is finite implies A in omega
  proof
    assume A is finite;
    then consider n such that
A1: A,n are_equipotent by Th74;
    A = n by A1,Th102;
    hence thesis by ORDINAL1:def 13;
  end;
  assume A in omega;
  then A is Element of omega;
  hence thesis;
end;

registration
  cluster natural -> finite set;
  coherence
  proof
    let a be set;
    assume a in omega;
    then a is Element of omega;
    hence thesis;
  end;
end;

registration
  cluster natural -> cardinal set;
  coherence
  proof
    let X be set;
    assume X is natural;
    then X in omega by ORDINAL1:def 13;
    hence X is cardinal by Th65;
  end;
end;

:: from CARD_4, CARD_5 etc. 2008.02.21, A.T.

registration
 cluster omega -> infinite;
 coherence
  proof
    not omega in omega;
   hence thesis by Th103;
  end;
end;

registration
 cluster infinite set;
 existence
  proof
   take omega;
   thus thesis;
  end;
end;

registration let A be infinite set;
 cluster bool A -> infinite;
 coherence by FINSET_1:24;
 let B be non empty set;
 cluster [:A,B:] ->infinite;
 coherence by FINSET_1:22;
 cluster [:B,A:] ->infinite;
 coherence by FINSET_1:23;
end;

registration
  let X be infinite set;
  cluster infinite Subset of X;
  existence
  proof X c= X;
    hence thesis;
  end;
end;

