:: Classical Configurations in Affine Planes
::  by Henryk Oryszczyszyn and Krzysztof Pra\.zmowski
::
:: Received April 13, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies DIRAF, AFF_1, ANALOAF, INCSP_1, AFF_2;
 notations STRUCT_0, ANALOAF, DIRAF, AFF_1;
 constructors AFF_1;
 theorems AFF_1;

begin

reserve AP for AffinPlane,
  a,a',b,b',c,c',x,y,o,p,q,r,s for Element of AP,
  A,C
  ,C',D,K,M,N,P,T for Subset of AP;

definition
  let AP;
  attr AP is satisfying_PPAP means
  :Def1:
  for M,N,a,b,c,a',b',c' st M is
  being_line & N is being_line & a in M & b in M & c in M & a' in N & b' in N &
  c' in N & a,b' // b,a' & b,c' // c,b' holds a,c' // c,a';
end;

definition
  let AP be AffinSpace;
  attr AP is Pappian means
  :Def2:
  for M,N being Subset of AP, o,a,b,c,a',b',c'
being Element of AP st M is being_line & N is being_line & M<>N & o in M & o in
N & o<>a & o<>a' & o<>b & o<>b' & o<>c & o<>c' & a in M & b in M & c in M & a'
  in N & b' in N & c' in N & a,b' // b,a' & b,c' // c,b' holds a,c' // c,a';
end;

definition
  let AP;
  attr AP is satisfying_PAP_1 means
  :Def3:
  for M,N,o,a,b,c,a',b',c' st M is
being_line & N is being_line & M<>N & o in M & o in N & o<>a & o<>a' & o<>b & o
<>b' & o<>c & o<>c' & a in M & b in M & c in M & b' in N & c' in N & a,b' // b,
  a' & b,c' // c,b' & a,c' // c,a' & b<>c holds a' in N;
end;

definition
  let AP be AffinSpace;
  attr AP is Desarguesian means
  :Def4:
  for A,P,C being Subset of AP, o,a,b,c,
a',b',c' being Element of AP st o in A & o in P & o in C & o<>a & o<>b & o<>c &
a in A & a' in A & b in P & b' in P & c in C & c' in C & A is being_line & P is
being_line & C is being_line & A<>P & A<>C & a,b // a',b' & a,c // a',c' holds
  b,c // b',c';
end;

definition
  let AP;
  attr AP is satisfying_DES_1 means
  :Def5:
  for A,P,C,o,a,b,c,a',b',c' st o in
A & o in P & o<>a & o<>b & o<>c & a in A & a' in A & b in P & b' in P & c in C
& c' in C & A is being_line & P is being_line & C is being_line & A<>P & A<>C &
a,b // a',b' & a,c // a',c' & b,c // b',c' & not LIN a,b,c & c <>c' holds o in
  C;
end;

definition
  let AP;
  attr AP is satisfying_DES_2 means
  for A,P,C,o,a,b,c,a',b',c' st o in A & o
in P & o in C & o<>a & o<>b & o<>c & a in A & a' in A & b in P & b' in P & c in
C & A is being_line & P is being_line & C is being_line & A<>P & A<>C & a,b //
  a',b' & a,c // a',c' & b,c // b',c' holds c' in C;
end;

definition
  let AP be AffinSpace;
  attr AP is Moufangian means
  :Def7:
  for K being Subset of AP, o,a,b,c,a',b',
  c' being Element of AP st K is being_line & o in K & c in K & c' in K & not a
in K & o<>c & a<>b & LIN o,a,a' & LIN o,b,b' & a,b // a',b' & a,c // a',c' & a,
  b // K holds b,c // b',c';
end;

definition
  let AP;
  attr AP is satisfying_TDES_1 means
  :Def8:
  for K,o,a,b,c,a',b',c' st K is
being_line & o in K & c in K & c' in K & not a in K & o<>c & a<>b & LIN o,a,a'
  & a,b // a',b' & b,c // b',c' & a,c // a',c' & a,b // K holds LIN o,b,b';
end;

definition
  let AP;
  attr AP is satisfying_TDES_2 means
  :Def9:
  for K,o,a,b,c,a',b',c' st K is
being_line & o in K & c in K & c' in K & not a in K & o<>c & a<>b & LIN o,a,a'
  & LIN o,b,b' & b,c // b',c' & a,c // a',c' & a,b // K holds a,b // a',b';
end;

definition
  let AP;
  attr AP is satisfying_TDES_3 means
  :Def10:
  for K,o,a,b,c,a',b',c' st K is
being_line & o in K & c in K & not a in K & o<>c & a<>b & LIN o,a,a' & LIN o,b,
  b' & a,b // a',b' & a,c // a',c' & b,c // b',c' & a,b // K holds c' in K;
end;

definition
  let AP be AffinSpace;
  attr AP is translational means
  :Def11:
  for A,P,C being Subset of AP, a,b,c,
  a',b',c' being Element of AP st A // P & A // C & a in A & a' in A & b in P &
  b' in P & c in C & c' in C & A is being_line & P is being_line & C is
  being_line & A<>P & A<>C & a,b // a',b' & a,c // a',c' holds b,c // b',c';
end;

definition
  let AP;
  attr AP is satisfying_des_1 means
  :Def12:
  for A,P,C,a,b,c,a',b',c' st A // P
& a in A & a' in A & b in P & b' in P & c in C & c' in C & A is being_line & P
is being_line & C is being_line & A<>P & A<>C & a,b // a',b' & a,c // a',c' & b
  ,c // b',c' & not LIN a,b,c & c <>c' holds A // C;
end;

definition
  let AP be AffinSpace;
  attr AP is satisfying_pap means
  :Def13:
  for M,N being Subset of AP, a,b,c,a'
,b',c' being Element of AP st M is being_line & N is being_line & a in M & b in
M & c in M & M // N & M<>N & a' in N & b' in N & c' in N & a,b' // b,a' & b,c'
  // c,b' holds a,c' // c,a';
end;

definition
  let AP;
  attr AP is satisfying_pap_1 means
  :Def14:
  for M,N,a,b,c,a',b',c' st M is
being_line & N is being_line & a in M & b in M & c in M & M // N & M<>N & a' in
N & b' in N & a,b' // b,a' & b,c' // c,b' & a,c' // c,a' & a'<>b' holds c' in N
  ;
end;

canceled 14;

theorem
  AP is Pappian iff AP is satisfying_PAP_1
proof
A1: now
    assume
A2: AP is satisfying_PAP_1;
    thus AP is Pappian
    proof
      let M,N,o,a,b,c,a',b',c';
      assume that
A3:   M is being_line and
A4:   N is being_line and
A5:   M<>N & o in M & o in N and
A6:   o<>a and
A7:   o<>a' and
A8:   o<>b and
A9:   o<>b' and
A10:  o<>c & o<>c' and
A11:  a in M and
A12:  b in M and
A13:  c in M and
A14:  a' in N and
A15:  b' in N and
A16:  c' in N and
A17:  a,b' // b,a' and
A18:  b,c' // c,b';
      set A=Line(a,c'), P=Line(b,a');
A19:  b<>a' by A3,A4,A5,A7,A12,A14,AFF_1:30;
      then
A20:  b in P by AFF_1:38;
      assume
A21:  not a,c' // c,a';
      then
A22:  a<>c' by AFF_1:12;
      then
A23:  a in A & c' in A by AFF_1:38;
A24:  a' in P by A19,AFF_1:38;
      A is being_line by A22,AFF_1:38;
      then consider K such that
A25:  c in K and
A26:  A // K by AFF_1:63;
A27:  b<>c
      proof
        assume
A28:    b=c;
        then LIN b,c',b' by A18,AFF_1:def 1;
        then LIN b',c',b by AFF_1:15;
        then b'=c' or b in N by A4,A15,A16,AFF_1:39;
        hence contradiction by A3,A4,A5,A8,A12,A17,A21,A28,AFF_1:30;
      end;
A29:  b'<>c'
      proof
        assume
        b'=c';
        then b',b // b',c by A18,AFF_1:13;
        then LIN b',b,c by AFF_1:def 1;
        then LIN b,c,b' by AFF_1:15;
        then b' in M by A3,A12,A13,A27,AFF_1:39;
        hence contradiction by A3,A4,A5,A9,A15,AFF_1:30;
      end;
A30:  not P // K
      proof
        assume
        P // K;
        then P // A by A26,AFF_1:58;
        then b,a' // a,c' by A23,A20,A24,AFF_1:53;
        then a,b' // a,c' by A17,A19,AFF_1:14;
        then LIN a,b',c' by AFF_1:def 1;
        then LIN b',c',a by AFF_1:15;
        then a in N by A4,A15,A16,A29,AFF_1:39;
        hence contradiction by A3,A4,A5,A6,A11,AFF_1:30;
      end;
A31:  P is being_line by A19,AFF_1:38;
      K is being_line by A26,AFF_1:50;
      then consider x such that
A32:  x in P and
A33:  x in K by A31,A30,AFF_1:72;
A34:  a,c' // c,x by A23,A25,A26,A33,AFF_1:53;
      LIN b,x,a' by A31,A20,A24,A32,AFF_1:33;
      then b,x // b,a' by AFF_1:def 1;
      then a,b' // b,x by A17,A19,AFF_1:14;
      then x in N by A2,A3,A4,A5,A6,A8,A9,A10,A11,A12,A13,A15,A16,A18
      ,A27,A34,Def3;
      then N=P by A4,A14,A21,A31,A24,A32,A34,AFF_1:30;
      hence contradiction by A3,A4,A5,A8,A12,A20,AFF_1:30;
    end;
  end;
  now
    assume
A35: AP is Pappian;
    thus AP is satisfying_PAP_1
    proof
      let M,N,o,a,b,c,a',b',c';
      assume that
A36:  M is being_line and
A37:  N is being_line and
A38:  M<>N and
A39:  o in M and
A40:  o in N and
A41:  o<>a and
      o<>a' and
A42:  o<>b and
A43:  o<>b' and
A44:  o<>c and
A45:  o<>c' and
A46:  a in M and
A47:  b in M and
A48:  c in M and
A49:  b' in N and
A50:  c' in N and
A51:  a,b' // b,a' and
A52:  b,c' // c,b' and
A53:  a,c' // c,a' and
A54:  b<>c;
A55:  a<>c' by A36,A37,A38,A39,A40,A41,A46,A50,AFF_1:30;
A56:  b<>a'
      proof
        assume
        b=a';
        then c,b // a,c' by A53,AFF_1:13;
        then c' in M by A36,A46,A47,A48,A54,AFF_1:62;
        hence contradiction by A36,A37,A38,A39,A40,A45,A50,AFF_1:30;
      end;
      not b,a' // N
      proof
        assume
A57:    b,a' // N;
        b,a' // a,b' by A51,AFF_1:13;
        then a,b' // N by A56,A57,AFF_1:46;
        then b',a // N by AFF_1:48;
        then a in N by A37,A49,AFF_1:37;
        hence contradiction by A36,A37,A38,A39,A40,A41,A46,AFF_1:30;
      end;
      then consider x such that
A58:  x in N and
A59:  LIN b,a',x by A37,AFF_1:73;
A60:  b,a' // b,x by A59,AFF_1:def 1;
A61:  o<>x
      proof
        assume
        o=x;
        then b,o // a,b' by A51,A56,A60,AFF_1:14;
        then b' in M by A36,A39,A42,A46,A47,AFF_1:62;
        hence contradiction by A36,A37,A38,A39,A40,A43,A49,AFF_1:30;
      end;
      a,b' // b,x by A51,A56,A60,AFF_1:14;
      then a,c' // c,x by A35,A36,A37,A38,A39,A40,A41,A42,A43,A44,A45,A46,A47
      ,A48,A49
      ,A50,A52,A58,A61,Def2;
      then c,a' // c,x by A53,A55,AFF_1:14;
      then LIN c,a',x by AFF_1:def 1;
      then
A62:  LIN a',x,c by AFF_1:15;
A63:  LIN a',x,x by AFF_1:16;
      assume
A64:  not a' in N;
      LIN a',x,b by A59,AFF_1:15;
      then x in M by A36,A47,A48,A54,A64,A58,A62,A63,AFF_1:17,39;
      hence contradiction by A36,A37,A38,A39,A40,A58,A61,AFF_1:30;
    end;
  end;
  hence thesis by A1;
end;

theorem
  AP is Desarguesian iff AP is satisfying_DES_1
proof
A1: now
    assume
A2: AP is satisfying_DES_1;
    thus AP is Desarguesian
    proof
      let A,P,C,o,a,b,c,a',b',c';
      assume that
A3:   o in A and
A4:   o in P and
A5:   o in C and
A6:   o<>a and
A7:   o<>b and
A8:   o<>c and
A9:   a in A and
A10:  a' in A and
A11:  b in P and
A12:  b' in P and
A13:  c in C and
A14:  c' in C and
A15:  A is being_line and
A16:  P is being_line and
A17:  C is being_line and
A18:  A<>P and
A19:  A<>C and
A20:  a,b // a',b' and
A21:  a,c // a',c';
      assume
A22:  not b,c // b',c';
A23:  a'<>b'
      proof
A24:    a',c' // c,a by A21,AFF_1:13;
        assume
A25:    a'=b';
        then a' in C by A3,A4,A5,A10,A12,A15,A16,A18,AFF_1:30;
        then a in C or a'=c' by A13,A14,A17,A24,AFF_1:62;
        hence contradiction by A3,A5,A6,A9,A15,A17,A19,A22,A25, AFF_1
        :12,30;
      end;
A26:  a'<>c'
      proof
        assume
        a'=c';
        then
A27:    a' in P by A3,A4,A5,A10,A14,A15,A17,A19,AFF_1:30;
        a',b' // b,a by A20,AFF_1:13;
        then a in P by A11,A12,A16,A23,A27,AFF_1:62;
        hence contradiction by A3,A4,A6,A9,A15,A16,A18,AFF_1:30;
      end;
A28:  o<>c'
      proof
        assume
A29:    o=c';
        a',c' // a,c by A21,AFF_1:13;
        then c in A by A3,A9,A10,A15,A26,A29,AFF_1:62;
        hence contradiction by A3,A5,A8,A13,A15,A17,A19,AFF_1:30;
      end;
      set K=Line(a',c'), M=Line(a,c), N=Line(b',c');
A30:  a<>c by A3,A5,A6,A9,A13,A15,A17,A19,AFF_1:30;
      then
A31:  c in M by AFF_1:38;
A32:  a<>b by A3,A4,A6,A9,A11,A15,A16,A18,AFF_1:30;
A33:  not LIN a',b',c'
      proof
        assume
A34:    LIN a',b',c';
        then a',b' // a',c' by AFF_1:def 1;
        then a',b' // a,c by A21,A26,AFF_1:14;
        then a,b // a,c by A20,A23,AFF_1:14;
        then LIN a,b,c by AFF_1:def 1;
        then LIN b,c,a by AFF_1:15;
        then b,c // b,a by AFF_1:def 1;
        then b,c // a,b by AFF_1:13;
        then
A35:    b,c // a',b' by A20,A32,AFF_1:14;
        LIN b',c',a' by A34,AFF_1:15;
        then b',c' // b',a' by AFF_1:def 1;
        then b',c' // a',b' by AFF_1:13;
        hence contradiction by A22,A23,A35,AFF_1:14;
      end;
A36:  b'<>c' by A22,AFF_1:12;
      then
A37:  b' in N & c' in N by AFF_1:38;
      N is being_line by A36,AFF_1:38;
      then consider D such that
A38:  b in D and
A39:  N // D by AFF_1:63;
A40:  a in M by A30,AFF_1:38;
A41:  not M // D
      proof
        assume
        M // D;
        then M // N by A39,AFF_1:58;
        then a,c // b',c' by A40,A31,A37,AFF_1:53;
        then a',c' // b',c' by A21,A30,AFF_1:14;
        then c',a' // c',b' by AFF_1:13;
        then LIN c',a',b' by AFF_1:def 1;
        hence contradiction by A33,AFF_1:15;
      end;
A42:  M is being_line by A30,AFF_1:38;
      D is being_line by A39,AFF_1:50;
      then consider x such that
A43:  x in M and
A44:  x in D by A42,A41,AFF_1:72;
      LIN a,c,x by A42,A40,A31,A43,AFF_1:33;
      then a,c // a,x by AFF_1:def 1;
      then
A45:  a,x // a',c' by A21,A30,AFF_1:14;
      set T=Line(c',x);
A46:  a<>a'
      proof
        assume
A47:    a=a';
        then LIN a,c,c' by A21,AFF_1:def 1;
        then LIN c,c',a by AFF_1:15;
        then
A48:    c =c' or a in C by A13,A14,A17,AFF_1:39;
        LIN a,b,b' by A20,A47,AFF_1:def 1;
        then LIN b,b',a by AFF_1:15;
        then b=b' or a in P by A11,A12,A16,AFF_1:39;
        hence contradiction by A3,A4,A5,A6,A9,A15,A16,A17,A18,A19,A22,
        A48,AFF_1:11,30;
      end;
A49:  x<>c'
      proof
        assume
        x=c';
        then c',a // c',a' by A45,AFF_1:13;
        then LIN c',a,a' by AFF_1:def 1;
        then LIN a,a',c' by AFF_1:15;
        then c' in A by A9,A10,A15,A46,AFF_1:39;
        hence contradiction by A3,A5,A14,A15,A17,A19,A28,AFF_1:30;
      end;
      then
A50:  T is being_line & c' in T by AFF_1:38;
A51:  b,x // b',c' by A37,A38,A39,A44,AFF_1:53;
A52:  x in T by A49,AFF_1:38;
A53:  a<>x
      proof
        assume
        a=x;
        then a,b // b',c' by A37,A38,A39,A44,AFF_1:53;
        then a',b' // b',c' by A20,A32,AFF_1:14;
        then b',a' // b',c' by AFF_1:13;
        then LIN b',a',c' by AFF_1:def 1;
        hence contradiction by A33,AFF_1:15;
      end;
      not LIN a,b,x
      proof
        assume
        LIN a,b,x;
        then a,b // a,x by AFF_1:def 1;
        then a,b // a',c' by A45,A53,AFF_1:14;
        then a',b' // a',c' by A20,A32,AFF_1:14;
        hence contradiction by A33,AFF_1:def 1;
      end;
      then o in T by A2,A3,A4,A6,A7,A9,A10,A11,A12,A15,A16,A18,A20,A45
      ,A51,A49,A50,A52,Def5;
      then x in C by A5,A14,A17,A28,A50,A52,AFF_1:30;
      then C=M by A13,A17,A22,A42,A31,A43,A51,AFF_1:30;
      hence contradiction by A3,A5,A6,A9,A15,A17,A19,A40,AFF_1:30;
    end;
  end;
  now
    assume
A54: AP is Desarguesian;
    thus AP is satisfying_DES_1
    proof
      let A,P,C,o,a,b,c,a',b',c';
      assume that
A55:  o in A and
A56:  o in P and
A57:  o<>a and
A58:  o<>b and
      o<>c and
A59:  a in A and
A60:  a' in A and
A61:  b in P and
A62:  b' in P and
A63:  c in C and
A64:  c' in C and
A65:  A is being_line and
A66:  P is being_line and
A67:  C is being_line and
A68:  A<>P and
A69:  A<>C and
A70:  a,b // a',b' and
A71:  a,c // a',c' and
A72:  b,c // b',c' and
A73:  not LIN a,b,c and
A74:  c <>c';
      set K=Line(o,c');
      assume
A75:  not o in C;
      then
A76:  K is being_line by A64,AFF_1:38;
A77:  a'<>c'
      proof
        assume
A78:    a'=c';
        then b,c // a',b' by A72,AFF_1:13;
        then a,b // b,c or a'=b' by A70,AFF_1:14;
        then b,a // b,c or a'=b' by AFF_1:13;
        then LIN b,a,c or a'=b' by AFF_1:def 1;
        hence contradiction by A55,A56,A60,A62,A64,A65,A66,A68,A73,A75,A78,
        AFF_1:15,30;
      end;
A79:  A<>K
      proof
        assume
        A=K;
        then
A80:    c' in A by A55,AFF_1:38;
        a',c' // a,c by A71,AFF_1:13;
        then c in A by A59,A60,A65,A77,A80,AFF_1:62;
        hence contradiction by A63,A64,A65,A67,A69,A74,A80,AFF_1:30;
      end;
A81:  a<>c by A73,AFF_1:16;
A82:  o in K by A64,A75,AFF_1:38;
A83:  c' in K by A64,A75,AFF_1:38;
      not a,c // K
      proof
        assume
        a,c // K;
        then a',c' // K by A71,A81,AFF_1:46;
        then c',a' // K by AFF_1:48;
        then a' in K by A76,A83,AFF_1:37;
        then
A84:    a' in P by A55,A56,A60,A65,A76,A82,A79,AFF_1:30;
        a',b' // b,a by A70,AFF_1:13;
        then a'=b' or a in P by A61,A62,A66,A84,AFF_1:62;
        then a,c // b,c by A55,A56,A57,A59,A65,A66,A68,A71,A72,A77,
        AFF_1:14,30;
        then c,a // c,b by AFF_1:13;
        then LIN c,a,b by AFF_1:def 1;
        hence contradiction by A73,AFF_1:15;
      end;
      then consider x such that
A85:  x in K and
A86:  LIN a,c,x by A76,AFF_1:73;
A87:  o<>x
      proof
        assume
        o=x;
        then LIN a,o,c by A86,AFF_1:15;
        then
A88:    c in A by A55,A57,A59,A65,AFF_1:39;
        then c' in A by A59,A60,A65,A71,A81,AFF_1:62;
        hence contradiction by A63,A64,A65,A67,A69,A74,A88,AFF_1:30;
      end;
A89:  b'<>c'
      proof
        assume
        b'=c';
        then a'=b' or a,c // a,b by A70,A71,AFF_1:14;
        then a'=b' or LIN a,c,b by AFF_1:def 1;
        then b,c // a,c by A71,A72,A73,A77,AFF_1:14,15;
        then c,b // c,a by AFF_1:13;
        then LIN c,b,a by AFF_1:def 1;
        hence contradiction by A73,AFF_1:15;
      end;
A90:  a,c // a,x by A86,AFF_1:def 1;
      then a,x // a',c' by A71,A81,AFF_1:14;
      then
b,x // b',c' by A54,A55,A56,A57,A58,A59,A60,A61,A62,A65,A66,A68,A70,A76,A82
      ,A83,A79,A85,A87,Def4;
      then
A91:  b,x // b,c by A72,A89,AFF_1:14;
A92:  not LIN a,b,x
      proof
        assume
        LIN a,b,x;
        then a,b // a,x by AFF_1:def 1;
        then a,b // a,c or a=x by A90,AFF_1:14;
        hence contradiction by A55,A57,A59,A65,A73,A76,A82,A79,A85,AFF_1:30 ,
        def 1;
      end;
      LIN a,x,c by A86,AFF_1:15;
      then c in K by A85,A92,A91,AFF_1:23;
      hence contradiction by A63,A64,A67,A74,A75,A76,A82,A83,AFF_1:30;
    end;
  end;
  hence thesis by A1;
end;

theorem Th17:
  AP is Moufangian implies AP is satisfying_TDES_1
proof
  assume
A1: AP is Moufangian;
  thus AP is satisfying_TDES_1
  proof
    let K,o,a,b,c,a',b',c';
    assume that
A2: K is being_line and
A3: o in K and
A4: c in K and
A5: c' in K and
A6: not a in K and
A7: o<>c and
A8: a<>b and
A9: LIN o,a,a' and
A10: a,b // a',b' and
A11: b,c // b',c' and
A12: a,c // a',c' and
A13: a,b // K;
    consider P such that
A14: a' in P and
A15: K // P by A2,AFF_1:63;
A16: P is being_line by A15,AFF_1:50;
    set A=Line(o,b), C=Line(o,a);
A17: o in A & b in A by AFF_1:26;
    assume
A18: not LIN o,b,b';
    then o<>b by AFF_1:16;
    then
A19: A is being_line by AFF_1:def 3;
A20: not b in K by A6,A13,AFF_1:49;
    not A // P
    proof
      assume
      A // P;
      then A // K by A15,AFF_1:58;
      hence contradiction by A3,A20,A17,AFF_1:59;
    end;
    then consider x such that
A21: x in A and
A22: x in P by A19,A16,AFF_1:72;
A23: o in C & a in C by AFF_1:26;
A24: LIN o,b,x by A19,A17,A21,AFF_1:33;
    a,b // P by A13,A15,AFF_1:57;
    then a',b' // P by A8,A10,AFF_1:46;
    then
A25: b' in P by A14,A16,AFF_1:37;
    then
A26: LIN b',x,b' by A16,A22,AFF_1:33;
A27: C is being_line by A3,A6,AFF_1:def 3;
    then
A28: a' in C by A3,A6,A9,A23,AFF_1:39;
A29: b'<>c'
    proof
A30:  a',c' // c,a by A12,AFF_1:13;
      assume
A31:  b'=c';
      then P=K by A5,A15,A25,AFF_1:59;
      then a'=o by A2,A3,A6,A27,A23,A28,A14,AFF_1:30;
      then b'=o by A2,A3,A4,A5,A6,A31,A30,AFF_1:62;
      hence contradiction by A18,AFF_1:16;
    end;
A32: b<>c by A4,A6,A13,AFF_1:49;
    a',x // K by A14,A15,A22,AFF_1:54;
    then a,b // a',x by A2,A13,AFF_1:45;
    then b,c // x,c' by A1,A2,A3,A4,A5,A6,A7,A8,A9,A12,A13,A24,Def7;
    then b',c' // x,c' by A11,A32,AFF_1:14;
    then c',b' // c',x by AFF_1:13;
    then LIN c',b',x by AFF_1:def 1;
    then
A33: LIN b',x,c' by AFF_1:15;
A34: a'<>b'
    proof
      assume
A35:  a'=b';
A36:  now
        assume
        a'=c';
        then b'=o by A2,A3,A5,A6,A27,A23,A28,A35,AFF_1:30;
        hence contradiction by A18,AFF_1:16;
      end;
      a,c // b,c or a'=c' by A11,A12,A35,AFF_1:14;
      then c,a // c,b by A36,AFF_1:13;
      then LIN c,a,b by AFF_1:def 1;
      then LIN a,c,b by AFF_1:15;
      then a,c // a,b by AFF_1:def 1;
      then a,b // a,c by AFF_1:13;
      then a,c // K by A8,A13,AFF_1:46;
      then c,a // K by AFF_1:48;
      hence contradiction by A2,A4,A6,AFF_1:37;
    end;
    LIN b',x,a' by A14,A16,A22,A25,AFF_1:33;
    then LIN b',c',a' by A18,A24,A33,A26,AFF_1:17;
    then b',c' // b',a' by AFF_1:def 1;
    then b',c' // a',b' by AFF_1:13;
    then b,c // a',b' by A11,A29,AFF_1:14;
    then a,b // b,c by A10,A34,AFF_1:14;
    then b,c // K by A8,A13,AFF_1:46;
    then c,b // K by AFF_1:48;
    hence contradiction by A2,A4,A20,AFF_1:37;
  end;
end;

theorem
  AP is satisfying_TDES_1 implies AP is satisfying_TDES_2
proof
  assume
A1: AP is satisfying_TDES_1;
  thus AP is satisfying_TDES_2
  proof
    let K,o,a,b,c,a',b',c';
    assume that
A2: K is being_line and
A3: o in K and
A4: c in K and
A5: c' in K and
A6: not a in K and
A7: o<>c and
A8: a<>b and
A9: LIN o,a,a' and
A10: LIN o,b,b' and
A11: b,c // b',c' and
A12: a,c // a',c' and
A13: a,b // K;
    set A=Line(o,a), P=Line(o,b);
A14: A is being_line & a in A by A3,A6,AFF_1:38;
A15: o in A by A3,A6,AFF_1:38;
    then
A16: a' in A by A3,A6,A9,A14,AFF_1:39;
A17: o<>b by A3,A6,A13,AFF_1:49;
    then
A18: P is being_line by AFF_1:38;
    consider N such that
A19: a' in N and
A20: K // N by A2,AFF_1:63;
A21: N is being_line by A20,AFF_1:50;
    set T=Line(b',c');
A22: not b in K by A6,A13,AFF_1:49;
A23: b in P by A17,AFF_1:38;
A24: o in P by A17,AFF_1:38;
    then
A25: b' in P by A10,A17,A18,A23,AFF_1:39;
    assume
A26: not a,b // a',b';
    then
A27: a'<>b' by AFF_1:12;
A28: not b' in K
    proof
A29:  a',c' // a,c by A12,AFF_1:13;
A30:  b',c' // c,b by A11,AFF_1:13;
      assume
A31:  b' in K;
      then b'=o by A2,A3,A22,A18,A24,A23,A25,AFF_1:30;
      then c' in A by A2,A3,A4,A5,A22,A15,A30,AFF_1:62;
      then a'=c' or c in A by A14,A16,A29,AFF_1:62;
      hence contradiction by A2,A3,A4,A5,A6,A7,A27,A22,A15,A14,A31,A30,
      AFF_1:30,62;
    end;
    then
A32: T is being_line by A5,AFF_1:38;
A33: b' in T by A5,A28,AFF_1:38;
A34: c' in T by A5,A28,AFF_1:38;
    not N // T
    proof
      assume
      N // T;
      then K // T by A20,AFF_1:58;
      hence contradiction by A5,A28,A33,A34,AFF_1:59;
    end;
    then consider x such that
A35: x in N and
A36: x in T by A32,A21,AFF_1:72;
    a',x // K by A19,A20,A35,AFF_1:54;
    then
A37: a,b // a',x by A2,A13,AFF_1:45;
    LIN c',b',x by A32,A33,A34,A36,AFF_1:33;
    then c',b' // c',x by AFF_1:def 1;
    then b',c' // x,c' by AFF_1:13;
    then b,c // x,c' by A5,A11,A28,AFF_1:14;
    then LIN o,b,x by A1,A2,A3,A4,A5,A6,A7,A8,A9,A12,A13,A37,Def8;
    then x in P by A17,A18,A24,A23,AFF_1:39;
    then P=T by A26,A18,A25,A32,A33,A36,A37,AFF_1:30;
    then LIN c',b',b by A18,A23,A33,A34,AFF_1:33;
    then c',b' // c',b by AFF_1:def 1;
    then b',c' // b,c' by AFF_1:13;
    then b,c // b,c' by A5,A11,A28,AFF_1:14;
    then LIN b,c,c' by AFF_1:def 1;
    then
A38: LIN c,c',b by AFF_1:15;
    then a,c // a',c by A2,A4,A5,A12,A22,AFF_1:39;
    then c,a // c,a' by AFF_1:13;
    then LIN c,a,a' by AFF_1:def 1;
    then LIN a,a',c by AFF_1:15;
    then
A39: a=a' or c in A by A14,A16,AFF_1:39;
    b,c // b',c by A2,A4,A5,A11,A22,A38,AFF_1:39;
    then c,b // c,b' by AFF_1:13;
    then LIN c,b,b' by AFF_1:def 1;
    then LIN b,b',c by AFF_1:15;
    then b=b' or c in P by A18,A23,A25,AFF_1:39;
    hence contradiction by A2,A3,A4,A6,A7,A26,A22,A18,A15,A14,A24,A23,A39,
    AFF_1:11,30;
  end;
end;

theorem
  AP is satisfying_TDES_2 implies AP is satisfying_TDES_3
proof
  assume
A1: AP is satisfying_TDES_2;
  thus AP is satisfying_TDES_3
  proof
    let K,o,a,b,c,a',b',c';
    assume that
A2: K is being_line and
A3: o in K and
A4: c in K and
A5: not a in K and
A6: o<>c and
A7: a<>b and
A8: LIN o,a,a' and
A9: LIN o,b,b' and
A10: a,b // a',b' and
A11: a,c // a',c' and
A12: b,c // b',c' and
A13: a,b // K;
    set A=Line(o,a), P=Line(o,b), N=Line(b,c);
A14: o in A by A3,A5,AFF_1:38;
A15: not LIN a,b,c
    proof
      assume
      LIN a,b,c;
      then a,b // a,c by AFF_1:def 1;
      then a,c // K by A7,A13,AFF_1:46;
      then c,a // K by AFF_1:48;
      hence contradiction by A2,A4,A5,AFF_1:37;
    end;
A16: o<>b by A3,A5,A13,AFF_1:49;
    then
A17: b in P by AFF_1:38;
A18: a',b' // b,a by A10,AFF_1:13;
A19: b<>c by A4,A5,A13,AFF_1:49;
    then
A20: b in N & c in N by AFF_1:38;
A21: a in A by A3,A5,AFF_1:38;
A22: A is being_line by A3,A5,AFF_1:38;
A23: A<>P
    proof
      assume
      A=P;
      then a,b // A by A22,A21,A17,AFF_1:54,55;
      hence contradiction by A3,A5,A7,A13,A14,A21,AFF_1:59,67;
    end;
    assume
A24: not c' in K;
A25: P is being_line by A16,AFF_1:38;
A26: o in P by A16,AFF_1:38;
    then
A27: b' in P by A9,A16,A25,A17,AFF_1:39;
A28: a' in A by A3,A5,A8,A22,A14,A21,AFF_1:39;
A29: a'<>b'
    proof
      assume
A30:  a'=b';
      then a,c // b,c or a'=c' by A11,A12,AFF_1:14;
      then c,a // c,b or a'=c' by AFF_1:13;
      then LIN c,a,b or a'=c' by AFF_1:def 1;
      hence contradiction by A3,A24,A15,A22,A25,A14,A26,A28,A27,A23,A30,
      AFF_1:15,30;
    end;
A31: a'<>c'
    proof
      assume
      a'=c';
      then b,c // a',b' by A12,AFF_1:13;
      then a,b // b,c by A10,A29,AFF_1:14;
      then b,a // b,c by AFF_1:13;
      then LIN b,a,c by AFF_1:def 1;
      hence contradiction by A15,AFF_1:15;
    end;
    not a',c' // K
    proof
      assume
A32:  a',c' // K;
      a',c' // a,c by A11,AFF_1:13;
      then a,c // K by A31,A32,AFF_1:46;
      then c,a // K by AFF_1:48;
      hence contradiction by A2,A4,A5,AFF_1:37;
    end;
    then consider x such that
A33: x in K and
A34: LIN a',c',x by A2,AFF_1:73;
    a',c' // a',x by A34,AFF_1:def 1;
    then
A35: a,c // a',x by A11,A31,AFF_1:14;
    N is being_line by A19,AFF_1:38;
    then consider T such that
A36: x in T and
A37: N // T by AFF_1:63;
A38: not b in K by A5,A13,AFF_1:49;
A39: not T // P
    proof
      assume
      T // P;
      then N // P by A37,AFF_1:58;
      then c in P by A17,A20,AFF_1:59;
      hence contradiction by A2,A3,A4,A6,A38,A25,A26,A17,AFF_1:30;
    end;
    T is being_line by A37,AFF_1:50;
    then consider y such that
A40: y in T and
A41: y in P by A25,A39,AFF_1:72;
A42: b,c // y,x by A20,A36,A37,A40,AFF_1:53;
A43: now
      assume
      y=b';
      then b',c' // b',x by A12,A19,A42,AFF_1:14;
      then LIN b',c',x by AFF_1:def 1;
      then
A44:  LIN c',x,b' by AFF_1:15;
      LIN c',x,a' & LIN c',x,c' by A34,AFF_1:15,16;
      then LIN a',b',c' by A24,A33,A44,AFF_1:17;
      then a',b' // a',c' by AFF_1:def 1;
      then a',b' // a,c by A11,A31,AFF_1:14;
      then a,b // a,c by A10,A29,AFF_1:14;
      hence contradiction by A15,AFF_1:def 1;
    end;
    LIN o,b,y by A25,A26,A17,A41,AFF_1:33;
    then a,b // a',y by A1,A2,A3,A4,A5,A6,A7,A8,A13,A33,A42,A35,Def9;
    then a',b' // a',y by A7,A10,AFF_1:14;
    then LIN a',b',y by AFF_1:def 1;
    then LIN b',y,a' by AFF_1:15;
    then a' in P by A25,A27,A41,A43,AFF_1:39;
    then a in P by A25,A17,A27,A29,A18,AFF_1:62;
    hence contradiction by A3,A5,A25,A26,A23,AFF_1:38;
  end;
end;

theorem
  AP is satisfying_TDES_3 implies AP is Moufangian
proof
  assume
A1: AP is satisfying_TDES_3;
  thus AP is Moufangian
  proof
    let K,o,a,b,c,a',b',c';
    assume that
A2: K is being_line and
A3: o in K and
A4: c in K and
A5: c' in K and
A6: not a in K and
A7: o<>c and
A8: a<>b and
A9: LIN o,a,a' and
A10: LIN o,b,b' and
A11: a,b // a',b' and
A12: a,c // a',c' and
A13: a,b // K;
    set A=Line(o,a), P=Line(o,b), M=Line(b,c), T=Line(a',c');
A14: o in A by A3,A6,AFF_1:38;
    assume
A15: not b,c // b',c';
    then
A16: b<>c by AFF_1:12;
    then
A17: b in M by AFF_1:38;
A18: a',b' // b,a by A11,AFF_1:13;
A19: c in M by A16,AFF_1:38;
A20: a in A by A3,A6,AFF_1:38;
A21: A is being_line by A3,A6,AFF_1:38;
    then
A22: a' in A by A3,A6,A9,A14,A20,AFF_1:39;
A23: o<>b by A3,A6,A13,AFF_1:49;
    then
A24: P is being_line by AFF_1:38;
A25: b in P by A23,AFF_1:38;
A26: A<>P
    proof
      assume
      A=P;
      then a,b // A by A21,A20,A25,AFF_1:54,55;
      hence contradiction by A3,A6,A8,A13,A14,A20,AFF_1:59,67;
    end;
A27: o in P by A23,AFF_1:38;
    then
A28: b' in P by A10,A23,A24,A25,AFF_1:39;
A29: a'<>b'
    proof
A30:  a',c' // c,a by A12,AFF_1:13;
      assume
A31:  a'=b';
      then a' in K by A3,A21,A24,A14,A27,A22,A28,A26,AFF_1:30;
      then a'=c' by A2,A4,A5,A6,A30,AFF_1:62;
      hence contradiction by A15,A31,AFF_1:12;
    end;
A32: a'<>c'
    proof
      assume
      a'=c';
      then
A33:  a' in P by A2,A3,A5,A6,A21,A14,A20,A27,A22,AFF_1:30;
      a',b' // b,a by A11,AFF_1:13;
      then a in P by A24,A25,A28,A29,A33,AFF_1:62;
      hence contradiction by A3,A6,A24,A27,A26,AFF_1:38;
    end;
    then
A34: T is being_line & c' in T by AFF_1:38;
A35: M is being_line by A16,AFF_1:38;
    then consider N such that
A36: b' in N and
A37: M // N by AFF_1:63;
A38: N is being_line by A37,AFF_1:50;
A39: not LIN a,b,c
    proof
      assume
      LIN a,b,c;
      then a,b // a,c by AFF_1:def 1;
      then a,c // K by A8,A13,AFF_1:46;
      then c,a // K by AFF_1:48;
      hence contradiction by A2,A4,A6,AFF_1:37;
    end;
    not a',c' // N
    proof
      assume
A40:  a',c' // N;
      a',c' // a,c by A12,AFF_1:13;
      then a,c // N by A32,A40,AFF_1:46;
      then a,c // M by A37,AFF_1:57;
      then c,a // M by AFF_1:48;
      then a in M by A35,A19,AFF_1:37;
      hence contradiction by A39,A35,A17,A19,AFF_1:33;
    end;
    then consider x such that
A41: x in N and
A42: LIN a',c',x by A38,AFF_1:73;
A43: b,c // b',x by A17,A19,A36,A37,A41,AFF_1:53;
    a',c' // a',x by A42,AFF_1:def 1;
    then a,c // a',x by A12,A32,AFF_1:14;
    then
A44: x in K by A1,A2,A3,A4,A6,A7,A8,A9,A10,A11,A13,A43,Def10;
A45: a' in T by A32,AFF_1:38;
    then x in T by A32,A34,A42,AFF_1:39;
    then K=T by A2,A5,A15,A34,A43,A44,AFF_1:30;
    then a' in P by A2,A3,A6,A21,A14,A20,A27,A22,A45,AFF_1:30;
    then a in P by A24,A25,A28,A29,A18,AFF_1:62;
    hence contradiction by A3,A6,A24,A27,A26,AFF_1:38;
  end;
end;

theorem Th21:
  AP is translational iff AP is satisfying_des_1
proof
A1: now
    assume
A2: AP is satisfying_des_1;
    thus AP is translational
    proof
      let A,P,C,a,b,c,a',b',c';
      assume that
A3:   A // P and
A4:   A // C and
A5:   a in A and
A6:   a' in A and
A7:   b in P and
A8:   b' in P and
A9:   c in C and
A10:  c' in C and
A11:  A is being_line and
A12:  P is being_line and
A13:  C is being_line and
A14:  A<>P and
A15:  A<>C and
A16:  a,b // a',b' and
A17:  a,c // a',c';
A18:  a<>b by A3,A5,A7,A14,AFF_1:59;
A19:  a'<>b' by A3,A6,A8,A14,AFF_1:59;
      set K=Line(a,c), N=Line(b',c');
A20:  a<>c by A4,A5,A9,A15,AFF_1:59;
      then
A21:  a in K by AFF_1:38;
      assume
A22:  not b,c // b',c';
      then
A23:  b'<>c' by AFF_1:12;
      then
A24:  b' in N by AFF_1:38;
A25:  b<>c by A22,AFF_1:12;
A26:  not LIN a,b,c
      proof
        assume
A27:    LIN a,b,c;
        then LIN b,c,a by AFF_1:15;
        then b,c // b,a by AFF_1:def 1;
        then b,c // a,b by AFF_1:13;
        then
A28:    b,c // a',b' by A16,A18,AFF_1:14;
        LIN c,b,a by A27,AFF_1:15;
        then c,b // c,a by AFF_1:def 1;
        then b,c // a,c by AFF_1:13;
        then b,c // a',c' by A17,A20,AFF_1:14;
        then a',c' // a',b' by A25,A28,AFF_1:14;
        then LIN a',c',b' by AFF_1:def 1;
        then LIN b',c',a' by AFF_1:15;
        then b',c' // b',a' by AFF_1:def 1;
        then b',c' // a',b' by AFF_1:13;
        hence contradiction by A22,A19,A28,AFF_1:14;
      end;
A29:  c in K by A20,AFF_1:38;
A30:  N is being_line by A23,AFF_1:38;
      then consider M such that
A31:  b in M and
A32:  N // M by AFF_1:63;
A33:  c' in N by A23,AFF_1:38;
A34:  a'<>c' by A4,A6,A10,A15,AFF_1:59;
A35:  not LIN a',b',c'
      proof
        assume
        LIN a',b',c';
        then a',b' // a',c' by AFF_1:def 1;
        then a,b // a',c' by A16,A19,AFF_1:14;
        then a,b // a,c by A17,A34,AFF_1:14;
        hence contradiction by A26,AFF_1:def 1;
      end;
A36:  not K // M
      proof
        assume
        K // M;
        then K // N by A32,AFF_1:58;
        then a,c // b',c' by A21,A29,A24,A33,AFF_1:53;
        then a',c' // b',c' by A17,A20,AFF_1:14;
        then c',a' // c',b' by AFF_1:13;
        then LIN c',a',b' by AFF_1:def 1;
        hence contradiction by A35,AFF_1:15;
      end;
A37:  K is being_line by A20,AFF_1:38;
A38:  M is being_line by A32,AFF_1:50;
      then consider x such that
A39:  x in K and
A40:  x in M by A37,A36,AFF_1:72;
A41:  b,x // b',c' by A24,A33,A31,A32,A40,AFF_1:53;
      set D=Line(x,c');
A42:  A<>D
      proof
        assume
        A=D;
        then c' in A by AFF_1:26;
        hence contradiction by A4,A10,A15,AFF_1:59;
      end;
A43:  x in D by AFF_1:26;
      LIN a,c,x by A37,A21,A29,A39,AFF_1:33;
      then a,c // a,x by AFF_1:def 1;
      then
A44:  a,x // a',c' by A17,A20,AFF_1:14;
A45:  c' in D by AFF_1:26;
A46:  not LIN a,b,x
      proof
A47:    a<>x
        proof
          assume
          a=x;
          then a,b // b',c' by A24,A33,A31,A32,A40,AFF_1:53;
          then a',b' // b',c' by A16,A18,AFF_1:14;
          then b',a' // b',c' by AFF_1:13;
          then LIN b',a',c' by AFF_1:def 1;
          hence contradiction by A35,AFF_1:15;
        end;
        assume
        LIN a,b,x;
        then LIN x,b,a by AFF_1:15;
        then
A48:    x,b // x,a by AFF_1:def 1;
        x<>b by A37,A21,A29,A26,A39,AFF_1:33;
        hence contradiction by A37,A21,A31,A38,A36,A39,A40,A48,A47,
        AFF_1:52;
      end;
A49:  P // C by A3,A4,AFF_1:58;
A50:  x<>c'
      proof
A51:    now
A52:      P // P by A3,AFF_1:58;
          assume
A53:      P=N;
          then c in P by A9,A10,A49,A33,AFF_1:59;
          hence contradiction by A7,A8,A22,A33,A53,A52,AFF_1:53;
        end;
        assume
        x=c';
        then M=N by A33,A32,A40,AFF_1:59;
        then
A54:    P=N or b=b' by A7,A8,A12,A30,A24,A31,AFF_1:30;
        then b,a // b,a' by A16,A51,AFF_1:13;
        then LIN b,a,a' by AFF_1:def 1;
        then LIN a,a',b by AFF_1:15;
        then b in A or a=a' by A5,A6,A11,AFF_1:39;
        then LIN a',c,c' by A3,A7,A14,A17,AFF_1:59,def 1;
        then LIN c,c',a' by AFF_1:15;
        then c =c' or a' in C by A9,A10,A13,AFF_1:39;
        hence contradiction by A4,A6,A15,A22,A54,A51,AFF_1:11,59;
      end;
      then D is being_line by AFF_1:38;
      then A // D by A2,A3,A5,A6,A7,A8,A11,A12,A14,A16,A41,A44,A43,
      A45,A50,A46,A42,Def12;
      then D // C by A4,AFF_1:58;
      then C=D by A10,A45,AFF_1:59;
      then C=K by A9,A13,A22,A37,A29,A39,A41,A43,AFF_1:30;
      hence contradiction by A4,A5,A15,A21,AFF_1:59;
    end;
  end;
  now
    assume
A55: AP is translational;
    thus AP is satisfying_des_1
    proof
      let A,P,C,a,b,c,a',b',c';
      assume that
A56:  A // P and
A57:  a in A and
A58:  a' in A and
A59:  b in P and
A60:  b' in P and
A61:  c in C & c' in C and
A62:  A is being_line and
A63:  P is being_line and
A64:  C is being_line and
A65:  A<>P and
A66:  A<>C and
A67:  a,b // a',b' and
A68:  a,c // a',c' and
A69:  b,c // b',c' and
A70:  not LIN a,b,c and
A71:  c <>c';
      assume
A72:  not A // C;
      consider K such that
A73:  c' in K and
A74:  A // K by A62,AFF_1:63;
A75:  a<>c by A70,AFF_1:16;
A76:  not a,c // K
      proof
        assume
        a,c // K;
        then a,c // A by A74,AFF_1:57;
        then
A77:    c in A by A57,A62,AFF_1:37;
        a',c' // a,c by A68,AFF_1:13;
        then a',c' // A by A57,A62,A75,A77,AFF_1:41;
        then c' in A by A58,A62,AFF_1:37;
        hence contradiction by A61,A62,A64,A66,A71,A77,AFF_1:30;
      end;
A78:  A<>K
      proof
        assume
A79:    A=K;
        a',c' // a,c by A68,AFF_1:13;
        then a'=c' by A58,A73,A74,A76,A79,AFF_1:46,54;
        then a',b' // b,c by A69,AFF_1:13;
        then a'=b' or a,b // b,c by A67,AFF_1:14;
        then b' in A or b,a // b,c by A58,AFF_1:13;
        then LIN b,a,c by A56,A60,A65,AFF_1:59,def 1;
        hence contradiction by A70,AFF_1:15;
      end;
A80:  now
        assume
        b'=c';
        then a,b // a,c or a'=b' by A67,A68,AFF_1:14;
        hence contradiction by A56,A58,A60,A65,A70,AFF_1:59,def 1;
      end;
A81:  K is being_line by A74,AFF_1:50;
      then consider x such that
A82:  x in K and
A83:  LIN a,c,x by A76,AFF_1:73;
      a,c // a,x by A83,AFF_1:def 1;
      then a,x // a',c' by A68,A75,AFF_1:14;
      then b,x // b',c' by A55,A56,A57,A58,A59,A60,A62,A63,A65,A67,A73,A74,
      A81,A82,
      A78,Def11;
      then b,x // b,c or b'=c' by A69,AFF_1:14;
      then LIN b,x,c by A80,AFF_1:def 1;
      then
A84:  LIN x,c,b by AFF_1:15;
A85:  LIN x,c, c by AFF_1:16;
      LIN x,c,a by A83,AFF_1:15;
      then c in K by A70,A82,A84,A85,AFF_1:17;
      hence contradiction by A61,A64,A71,A72,A73,A74,A81,AFF_1:30;
    end;
  end;
  hence thesis by A1;
end;

theorem
  AP is satisfying_pap iff AP is satisfying_pap_1
proof
A1: now
    assume
A2: AP is satisfying_pap;
    thus AP is satisfying_pap_1
    proof
      let M,N,a,b,c,a',b',c';
      assume that
A3:   M is being_line and
A4:   N is being_line and
A5:   a in M and
A6:   b in M and
A7:   c in M and
A8:   M // N and
A9:   M<>N and
A10:  a' in N and
A11:  b' in N and
A12:  a,b' // b,a' and
A13:  b,c' // c,b' and
A14:  a,c' // c,a' and
A15:  a'<>b';
A16:  c <>a' by A7,A8,A9,A10,AFF_1:59;
      set C=Line(c,b');
A17:  c <>b' by A7,A8,A9,A11,AFF_1:59;
      then C is being_line by AFF_1:38;
      then consider K such that
A18:  b in K and
A19:  C // K by AFF_1:63;
A20:  c in C & b' in C by A17,AFF_1:38;
A21:  not K // N
      proof
        assume
        K // N;
        then C // N by A19,AFF_1:58;
        then C // M by A8,AFF_1:58;
        then b' in M by A7,A20,AFF_1:59;
        hence contradiction by A8,A9,A11,AFF_1:59;
      end;
      K is being_line by A19,AFF_1:50;
      then consider x such that
A22:  x in K and
A23:  x in N by A4,A21,AFF_1:72;
A24:  b,x // c,b' by A20,A18,A19,A22,AFF_1:53;
      then a,x // c,a' by A2,A3,A4,A5,A6,A7,A8,A9,A10,A11,A12,A23,Def13;
      then a,x // a,c' by A14,A16,AFF_1:14;
      then LIN a,x,c' by AFF_1:def 1;
      then
A25:  LIN c',x,a by AFF_1:15;
A26:  a<>b
      proof
        assume
        a=b;
        then LIN a,b',a' by A12,AFF_1:def 1;
        then LIN a',b',a by AFF_1:15;
        then a'=b' or a in N by A4,A10,A11,AFF_1:39;
        hence contradiction by A5,A8,A9,A15,AFF_1:59;
      end;
A27:  c'<>b
      proof
        assume
        c'=b;
        then a' in M by A3,A5,A6,A7,A14,A26,AFF_1:62;
        hence contradiction by A8,A9,A10,AFF_1:59;
      end;
      b,x // b,c' by A13,A17,A24,AFF_1:14;
      then LIN b,x,c' by AFF_1:def 1;
      then
A28:  LIN c',x,b by AFF_1:15;
      assume
A29:  not c' in N;
      LIN c',x,c' by AFF_1:16;
      then c' in M by A3,A5,A6,A29,A26,A23,A25,A28,AFF_1:17,39;
      then b' in M by A3,A6,A7,A13,A27,AFF_1:62;
      hence contradiction by A8,A9,A11,AFF_1:59;
    end;
  end;
  now
    assume
A30: AP is satisfying_pap_1;
    thus AP is satisfying_pap
    proof
      let M,N,a,b,c,a',b',c';
      assume that
A31:  M is being_line and
A32:  N is being_line and
A33:  a in M and
A34:  b in M and
A35:  c in M and
A36:  M // N & M<>N and
A37:  a' in N and
A38:  b' in N and
A39:  c' in N and
A40:  a,b' // b,a' and
A41:  b,c' // c,b';
      set A=Line(c,a'), C=Line(c,b'), D=Line(b,c');
A42:  b<>c' by A34,A36,A39,AFF_1:59;
      then
A43:  b in D & c' in D by AFF_1:38;
      assume
A44:  not a,c' // c,a';
      then
A45:  c <>a' by AFF_1:12;
      then
A46:  c in A by AFF_1:38;
A47:  a' in A by A45,AFF_1:38;
A48:  A is being_line by A45,AFF_1:38;
      then consider P such that
A49:  a in P and
A50:  A // P by AFF_1:63;
A51:  a'<>b'
      proof
        assume
A52:    a'=b';
        then a',a // a',b by A40,AFF_1:13;
        then LIN a',a,b by AFF_1:def 1;
        then LIN a,b,a' by AFF_1:15;
        then a=b or a' in M by A31,A33,A34,AFF_1:39;
        hence contradiction by A36,A37,A41,A44,A52,AFF_1:59;
      end;
A53:  not D // P
      proof
        assume
        D // P;
        then c,b' // P by A41,A42,A43,AFF_1:46,54;
        then c,b' // A by A50,AFF_1:57;
        then b' in A by A48,A46,AFF_1:37;
        then c in N by A32,A37,A38,A51,A48,A46,A47,AFF_1:30;
        hence contradiction by A35,A36,AFF_1:59;
      end;
A54:  D is being_line by A42,AFF_1:38;
      P is being_line by A50,AFF_1:50;
      then consider x such that
A55:  x in D and
A56:  x in P by A54,A53,AFF_1:72;
      LIN b,x,c' by A54,A43,A55,AFF_1:33;
      then b,x // b,c' by AFF_1:def 1;
      then
A57:  b,x // c,b' by A41,A42,AFF_1:14;
      a,x // c,a' by A46,A47,A49,A50,A56,AFF_1:53;
      then x in N by A30,A31,A32,A33,A34,A35,A36,A37,A38,A40,A51,
      A57,Def14;
      then x=c' or b in N by A32,A39,A54,A43,A55,AFF_1:30;
      hence contradiction by A34,A36,A44,A46,A47,A49,A50,A56,AFF_1:53,59;
    end;
  end;
  hence thesis by A1;
end;

theorem Th23:
  AP is Pappian implies AP is satisfying_pap
proof
  assume
A1: AP is Pappian;
  thus AP is satisfying_pap
  proof
    let M,N,a,b,c,a',b',c';
    assume that
A2: M is being_line and
A3: N is being_line and
A4: a in M and
A5: b in M and
A6: c in M and
A7: M // N and
A8: M<>N and
A9: a' in N and
A10: b' in N and
A11: c' in N and
A12: a,b' // b,a' and
A13: b,c' // c,b';
A14: b<>a' by A5,A7,A8,A9,AFF_1:59;
    set K=Line(a,c'), C=Line(c,b');
A15: c <>b' by A6,A7,A8,A10,AFF_1:59;
    then
A16: C is being_line by AFF_1:38;
    assume
A17: not a,c' // c,a';
A18: now
      assume
A19:  a=b;
      then LIN a,b',a' by A12,AFF_1:def 1;
      then LIN a',b',a by AFF_1:15;
      then a'=b' or a in N by A3,A9,A10,AFF_1:39;
      hence contradiction by A4,A7,A8,A13,A17,A19,AFF_1:59;
    end;
A20: now
      assume
      a'=b';
      then a',a // a',b by A12,AFF_1:13;
      then LIN a',a,b by AFF_1:def 1;
      then LIN a,b,a' by AFF_1:15;
      then a' in M by A2,A4,A5,A18,AFF_1:39;
      hence contradiction by A7,A8,A9,AFF_1:59;
    end;
A21: now
      assume
A22:  b=c;
      then LIN b,c',b' by A13,AFF_1:def 1;
      then LIN b',c',b by AFF_1:15;
      then b'=c' or b in N by A3,A10,A11,AFF_1:39;
      hence contradiction by A5,A7,A8,A12,A17,A22,AFF_1:59;
    end;
A23: now
      assume
      b'=c';
      then b',b // b',c by A13,AFF_1:13;
      then LIN b',b,c by AFF_1:def 1;
      then LIN b,c,b' by AFF_1:15;
      then b' in M by A2,A5,A6,A21,AFF_1:39;
      hence contradiction by A7,A8,A10,AFF_1:59;
    end;
A24: a<>c' by A4,A7,A8,A11,AFF_1:59;
    then
A25: a in K by AFF_1:38;
    K is being_line by A24,AFF_1:38;
    then consider T such that
A26: a' in T and
A27: K // T by AFF_1:63;
A28: T is being_line by A27,AFF_1:50;
A29: c' in K by A24,AFF_1:38;
A30: c in C & b' in C by A15,AFF_1:38;
    not C // T
    proof
      assume
      C // T;
      then C // K by A27,AFF_1:58;
      then c,b' // a,c' by A25,A29,A30,AFF_1:53;
      then b,c' // a,c' by A13,A15,AFF_1:14;
      then c',b // c',a by AFF_1:13;
      then LIN c',b,a by AFF_1:def 1;
      then LIN a,b,c' by AFF_1:15;
      then c' in M by A2,A4,A5,A18,AFF_1:39;
      hence contradiction by A7,A8,A11,AFF_1:59;
    end;
    then consider x such that
A31: x in C and
A32: x in T by A16,A28,AFF_1:72;
    set D=Line(x,b);
A33: x<>b
    proof
      assume
      x=b;
      then LIN b,c,b' by A16,A30,A31,AFF_1:33;
      then b' in M by A2,A5,A6,A21,AFF_1:39;
      hence contradiction by A7,A8,A10,AFF_1:59;
    end;
    then
A34: b in D by AFF_1:38;
    then
A35: D<>N by A5,A7,A8,AFF_1:59;
A36: D is being_line by A33,AFF_1:38;
A37: x in D by A33,AFF_1:38;
    not D // N
    proof
      assume
      D // N;
      then D // M by A7,AFF_1:58;
      then x in M by A5,A37,A34,AFF_1:59;
      then c in T or b' in M by A2,A6,A16,A30,A31,A32,AFF_1:30;
      hence contradiction by A7,A8,A10,A17,A25,A29,A26,A27,AFF_1:53,59;
    end;
    then consider o such that
A38: o in D and
A39: o in N by A3,A36,AFF_1:72;
    LIN b',c,x by A16,A30,A31,AFF_1:33;
    then
A40: b',c // b',x by AFF_1:def 1;
A41: a'<>x
    proof
      assume
      a'=x;
      then b',a' // b',c by A40,AFF_1:13;
      then LIN b',a',c by AFF_1:def 1;
      then c in N by A3,A9,A10,A20,AFF_1:39;
      hence contradiction by A6,A7,A8,AFF_1:59;
    end;
A42: now
      assume
      o=x;
      then N=T by A3,A9,A26,A28,A32,A39,A41,AFF_1:30;
      then N=K by A11,A29,A27,AFF_1:59;
      hence contradiction by A4,A7,A8,A25,AFF_1:59;
    end;
A43: a,c' // x,a' by A25,A29,A26,A27,A32,AFF_1:53;
A44: now
      assume
      o=a';
      then LIN a',b,x by A36,A37,A34,A38,AFF_1:33;
      then a',b // a',x by AFF_1:def 1;
      then b,a' // x,a' by AFF_1:13;
      then a,b' // x,a' by A12,A14,AFF_1:14;
      then a,b' // a,c' by A43,A41,AFF_1:14;
      then LIN a,b',c' by AFF_1:def 1;
      then LIN b',c',a by AFF_1:15;
      then a in N by A3,A10,A11,A23,AFF_1:39;
      hence contradiction by A4,A7,A8,AFF_1:59;
    end;
    c,b' // x,b' by A40,AFF_1:13;
    then
A45: b,c' // x,b' by A13,A15,AFF_1:14;
A46: a<>b' by A4,A7,A8,A10,AFF_1:59;
    not a,b' // D
    proof
      assume
      a,b' // D;
      then b,a' // D by A12,A46,AFF_1:46;
      then a' in D by A36,A34,AFF_1:37;
      then b in T by A26,A28,A32,A36,A37,A34,A41,AFF_1:30;
      then b,a' // a,c' by A25,A29,A26,A27,AFF_1:53;
      then a,b' // a,c' by A12,A14,AFF_1:14;
      then LIN a,b',c' by AFF_1:def 1;
      then LIN b',c',a by AFF_1:15;
      then a in N by A3,A10,A11,A23,AFF_1:39;
      hence contradiction by A4,A7,A8,AFF_1:59;
    end;
    then consider y such that
A47: y in D and
A48: LIN a,b',y by A36,AFF_1:73;
A49: LIN a,y,a by AFF_1:16;
A50: b'<>x
    proof
      assume
      b'=x;
      then a,c' // a',b' by A25,A29,A26,A27,A32,AFF_1:53;
      then a,c' // N by A3,A9,A10,A20,AFF_1:41;
      then c',a // N by AFF_1:48;
      then a in N by A3,A11,AFF_1:37;
      hence contradiction by A4,A7,A8,AFF_1:59;
    end;
A51: now
      assume
      o=b';
      then LIN b',x,b by A36,A37,A34,A38,AFF_1:33;
      then b',x // b',b by AFF_1:def 1;
      then x,b' // b,b' by AFF_1:13;
      then b,c' // b,b' by A45,A50,AFF_1:14;
      then LIN b,c',b' by AFF_1:def 1;
      then LIN b',c',b by AFF_1:15;
      then b in N by A3,A10,A11,A23,AFF_1:39;
      hence contradiction by A5,A7,A8,AFF_1:59;
    end;
A52: now
      assume
      o=y;
      then LIN b',o,a by A48,AFF_1:15;
      then a in N by A3,A10,A39,A51,AFF_1:39;
      hence contradiction by A4,A7,A8,AFF_1:59;
    end;
A53: b<>c' by A5,A7,A8,A11,AFF_1:59;
A54: now
      assume
      o=c';
      then D // C by A13,A15,A53,A16,A30,A36,A34,A38,AFF_1:52;
      then b in C by A31,A37,A34,AFF_1:59;
      then LIN c,b,b' by A16,A30,AFF_1:33;
      then b' in M by A2,A5,A6,A21,AFF_1:39;
      hence contradiction by A7,A8,A10,AFF_1:59;
    end;
    LIN b',a,y by A48,AFF_1:15;
    then b',a // b',y by AFF_1:def 1;
    then a,b' // y,b' by AFF_1:13;
    then
A55: y,b' // b,a' by A12,A46,AFF_1:14;
    o<>b by A5,A7,A8,A39,AFF_1:59;
    then y,c' // x,a' by A1,A3,A9,A10,A11,A36,A37,A34,A38,A39,A45,A47,A55,
    A35,A51,A44,A54,A42,A52,Def2;
    then y,c' // a,c' by A43,A41,AFF_1:14;
    then c',y // c',a by AFF_1:13;
    then LIN c',y,a by AFF_1:def 1;
    then
A56: LIN a,y,c' by AFF_1:15;
    LIN a,y,b' by A48,AFF_1:15;
    then a in D or a in N by A3,A10,A11,A23,A47,A56,A49,AFF_1:17,39;
    then D // N by A2,A4,A5,A7,A8,A18,A36,A34,AFF_1:30,59;
    hence contradiction by A38,A39,A35,AFF_1:59;
  end;
end;

theorem Th24:
  AP is satisfying_PPAP iff AP is Pappian & AP is satisfying_pap
proof
A1: AP is Pappian & AP is satisfying_pap implies AP is satisfying_PPAP
  proof
    assume that
A2: AP is Pappian and
A3: AP is satisfying_pap;
    thus AP is satisfying_PPAP
    proof
      let M,N,a,b,c,a',b',c';
      assume that
A4:   M is being_line and
A5:   N is being_line and
A6:   a in M and
A7:   b in M and
A8:   c in M and
A9:   a' in N and
A10:  b' in N and
A11:  c' in N and
A12:  a,b' // b,a' and
A13:  b,c' // c,b';
      now
        assume
A14:    M<>N;
        assume
A15:    not thesis;
        now
          assume
          not M // N;
          then consider o such that
A16:      o in M and
A17:      o in N by A4,A5,AFF_1:72;
A18:      o<>a
          proof
            assume
A19:        o=a;
            then o,b' // a',b by A12,AFF_1:13;
            then o=b' or b in N by A5,A9,A10,A17,AFF_1:62;
            then o=b' or o=b by A4,A5,A7,A14,A16,A17,AFF_1:30;
            then c,o // b,c' or o,c' // b',c by A13,AFF_1:13;
            then c' in M or c =o or c in N or o=c' by A4,A5,A7,A8,A10,A11
            ,A16,A17,AFF_1:62;
            then o=c or o=c' by A4,A5,A8,A11,A14,A16,A17,AFF_1:30;
            hence contradiction by A5,A9,A11,A15,A17,A19,AFF_1:12,65;
          end;
A20:      o<>b
          proof
            assume
A21:        o=b;
            then o,c' // b',c by A13,AFF_1:13;
            then o=c' or c in N by A5,A10,A11,A17,AFF_1:62;
            then
A22:        o=c' or o=c by A4,A5,A8,A14,A16,A17,AFF_1:30;
            o,a' // b',a by A12,A21,AFF_1:13;
            then o=a' or a in N by A5,A9,A10,A17,AFF_1:62;
            hence contradiction by A4,A5,A6,A8,A14,A15,A16,A17,A18,A22,
            AFF_1:12,30,65;
          end;
A23:      o<>c'
          proof
            assume
A24:        o=c';
            then b' in M by A4,A7,A8,A13,A16,A20,AFF_1:62;
            then a,o // b,a' by A4,A5,A10,A12,A14,A16,A17,AFF_1:30;
            then a' in M by A4,A6,A7,A16,A18,AFF_1:62;
            hence contradiction by A4,A6,A8,A15,A16,A24,AFF_1:65;
          end;
A25:      o<>c
          proof
            assume
A26:        o=c;
            then o,b' // c',b by A13,AFF_1:13;
            then o=b' or b in N by A5,A10,A11,A17,AFF_1:62;
            then a' in M by A4,A5,A6,A7,A9,A12,A16,A17,A18,A20,
            AFF_1
            :30,62;
            then a'=o by A4,A5,A9,A14,A16,A17,AFF_1:30;
            hence contradiction by A15,A26,AFF_1:12;
          end;
A27:      o<>a'
          proof
            assume
A28:        o=a';
            then o,b // a,b' by A12,AFF_1:13;
            then b' in M by A4,A6,A7,A16,A20,AFF_1:62;
            then o=b' by A4,A5,A10,A14,A16,A17,AFF_1:30;
            then c,o // b,c' by A13,AFF_1:13;
            then c' in M by A4,A7,A8,A16,A25,AFF_1:62;
            hence contradiction by A4,A6,A8,A15,A16,A28,AFF_1:65;
          end;
          o<>b'
          proof
            assume
A29:        o=b';
            then o,c // b,c' by A13,AFF_1:13;
            then
A30:        c' in M by A4,A7,A8,A16,A25,AFF_1:62;
            a' in M by A4,A6,A7,A12,A16,A18,A29,AFF_1:62;
            hence contradiction by A4,A6,A8,A15,A30,AFF_1:65;
          end;
          hence
          thesis by A2,A4,A5,A6,A7,A8,A9,A10,A11,A12,A13,A14,A16,
          A17,A18,A20,A25,A27,A23,Def2;
        end;
        hence thesis by A3,A4,A5,A6,A7,A8,A9,A10,A11,A12,A13,A14,
        Def13;
      end;
      hence thesis by A4,A6,A8,A9,A11,AFF_1:65;
    end;
  end;
  AP is satisfying_PPAP implies AP is Pappian & AP is satisfying_pap
  proof
    assume
A31: AP is satisfying_PPAP;
    thus AP is Pappian
    proof
      let M,N,o,a,b,c,a',b',c';
      assume that
A32:  M is being_line & N is being_line and
      M<>N and
      o in M and
      o in N and
      o<>a and
      o<>a' and
      o<>b and
      o<>b' and
      o<>c and
      o<>c' and
A33:  a in M & b in M & c in M & a' in N & b' in N & c' in N & a,b' //
      b,a' & b,c' // c,b';
      thus thesis by A31,A32,A33,Def1;
    end;
    thus AP is satisfying_pap
    proof
      let M,N,a,b,c,a',b',c';
      assume that
A34:  M is being_line & N is being_line & a in M & b in M & c in M and
      M // N and
      M<>N and
A35:  a' in N & b' in N & c' in N & a,b' // b,a' & b,c' // c,b';
      thus thesis by A31,A34,A35,Def1;
    end;
  end;
  hence thesis by A1;
end;

theorem
  AP is Pappian implies AP is Desarguesian
proof
  assume
A1: AP is Pappian;
  then AP is satisfying_pap by Th23;
  then
A2: AP is satisfying_PPAP by A1,Th24;
  thus AP is Desarguesian
  proof
    let A,P,C,o,a,b,c,a',b',c';
    assume that
A3: o in A and
A4: o in P and
A5: o in C and
A6: o<>a and
A7: o<>b and
    o<>c and
A8: a in A and
A9: a' in A and
A10: b in P and
A11: b' in P and
A12: c in C and
A13: c' in C and
A14: A is being_line and
A15: P is being_line and
A16: C is being_line and
A17: A<>P and
A18: A<>C and
A19: a,b // a',b' and
A20: a,c // a',c';
    assume
A21: not b,c // b',c';
    then
A22: b<>c by AFF_1:12;
A23: a<>c by A3,A5,A6,A8,A12,A14,A16,A18,AFF_1:30;
A24: not b in C
    proof
      assume
A25:  b in C;
      then b' in C by A4,A5,A7,A10,A11,A15,A16,AFF_1:30;
      hence contradiction by A12,A13,A16,A21,A25,AFF_1:65;
    end;
A26: a<>b by A3,A4,A6,A8,A10,A14,A15,A17,AFF_1:30;
A27: a<>a'
    proof
      assume
A28:  a=a';
      then LIN a,c,c' by A20,AFF_1:def 1;
      then LIN c,c',a by AFF_1:15;
      then
A29:  c =c' or a in C by A12,A13,A16,AFF_1:39;
      LIN a,b,b' by A19,A28,AFF_1:def 1;
      then LIN b,b',a by AFF_1:15;
      then b=b' or a in P by A10,A11,A15,AFF_1:39;
      hence contradiction by A3,A4,A5,A6,A8,A14,A15,A16,A17,A18,A21,A29,
      AFF_1:11,30;
    end;
    set M=Line(b',c'), N=Line(a',b'), D=Line(a',c');
A30: a'<>b'
    proof
A31:  a',c' // c,a by A20,AFF_1:13;
      assume
A32:  a'=b';
      then a' in C by A3,A4,A5,A9,A11,A14,A15,A17,AFF_1:30;
      then a in C or a'=c' by A12,A13,A16,A31,AFF_1:62;
      hence contradiction by A3,A5,A6,A8,A14,A16,A18,A21,A32,AFF_1:12,30;
    end;
    then
A33: N is being_line by AFF_1:38;
A34: a'<>c'
    proof
      assume
      a'=c';
      then
A35:  a' in P by A3,A4,A5,A9,A13,A14,A16,A18,AFF_1:30;
      a',b' // b,a by A19,AFF_1:13;
      then a in P by A10,A11,A15,A30,A35,AFF_1:62;
      hence contradiction by A3,A4,A6,A8,A14,A15,A17,AFF_1:30;
    end;
A36: not LIN a',b',c'
    proof
      assume
A37:  LIN a',b',c';
      then a',b' // a',c' by AFF_1:def 1;
      then a',b' // a,c by A20,A34,AFF_1:14;
      then a,b // a,c by A19,A30,AFF_1:14;
      then LIN a,b,c by AFF_1:def 1;
      then LIN b,c,a by AFF_1:15;
      then b,c // b,a by AFF_1:def 1;
      then b,c // a,b by AFF_1:13;
      then
A38:  b,c // a',b' by A19,A26,AFF_1:14;
      LIN b',c',a' by A37,AFF_1:15;
      then b',c' // b',a' by AFF_1:def 1;
      then b',c' // a',b' by AFF_1:13;
      hence contradiction by A21,A30,A38,AFF_1:14;
    end;
A39: not LIN a,b,c
    proof
      assume
      LIN a,b,c;
      then a,b // a,c by AFF_1:def 1;
      then a,b // a',c' by A20,A23,AFF_1:14;
      then a',b' // a',c' by A19,A26,AFF_1:14;
      hence contradiction by A36,AFF_1:def 1;
    end;
A40: now
      LIN o,a,a' by A3,A8,A9,A14,AFF_1:33;
      then o,a // o,a' by AFF_1:def 1;
      then
A41:  a',o // a,o by AFF_1:13;
      set M=Line(b,c), N=Line(a,b), D=Line(a,c);
A42:  N is being_line by A26,AFF_1:38;
      M is being_line by A22,AFF_1:38;
      then consider K such that
A43:  o in K and
A44:  M // K by AFF_1:63;
A45:  K is being_line by A44,AFF_1:50;
A46:  a in N by A26,AFF_1:38;
A47:  b in N by A26,AFF_1:38;
A48:  b in M & c in M by A22,AFF_1:38;
      not N // K
      proof
        assume
        N // K;
        then N // M by A44,AFF_1:58;
        then c in N by A48,A47,AFF_1:59;
        hence contradiction by A39,A42,A46,A47,AFF_1:33;
      end;
      then consider p such that
A49:  p in N and
A50:  p in K by A42,A45,AFF_1:72;
A51:  b,c // p,o by A48,A43,A44,A50,AFF_1:53;
A52:  o<>p
      proof
        assume
        o=p;
        then LIN o,a,b by A42,A46,A47,A49,AFF_1:33;
        then b in A by A3,A6,A8,A14,AFF_1:39;
        hence contradiction by A3,A4,A7,A10,A14,A15,A17,AFF_1:30;
      end;
      set R=Line(a',p);
A53:  p<>a'
      proof
        assume
        p=a';
        then b in A by A8,A9,A14,A27,A42,A46,A47,A49,AFF_1:30;
        hence contradiction by A3,A4,A7,A10,A14,A15,A17,AFF_1:30;
      end;
      then
A54:  R is being_line by AFF_1:38;
      D is being_line by A23,AFF_1:38;
      then consider T such that
A55:  p in T and
A56:  D // T by AFF_1:63;
A57:  a in D & c in D by A23,AFF_1:38;
A58:  not C // T
      proof
        assume
        C // T;
        then C // D by A56,AFF_1:58;
        then a in C by A12,A57,AFF_1:59;
        hence contradiction by A3,A5,A6,A8,A14,A16,A18,AFF_1:30;
      end;
      T is being_line by A56,AFF_1:50;
      then consider q such that
A59:  q in C and
A60:  q in T by A16,A58,AFF_1:72;
A61:  p,q // a,c by A57,A55,A56,A60,AFF_1:53;
      then
A62:  b,q // a,o by A2,A5,A12,A16,A42,A46,A47,A49,A59,A51,Def1;
A63:  a' in R & p in R by A53,AFF_1:38;
      assume
      not b,c // A;
      then
A64:  K<>A by A48,A44,AFF_1:54;
      not b,q // R
      proof
        assume
        b,q // R;
        then
A65:    a,o // R by A24,A59,A62,AFF_1:46;
        a,o // A by A3,A8,A14,AFF_1:54,55;
        then p in A by A6,A9,A63,A65,AFF_1:59,67;
        hence contradiction by A3,A14,A43,A45,A50,A52,A64,AFF_1:30;
      end;
      then consider r such that
A66:  r in R and
A67:  LIN b,q,r by A54,AFF_1:73;
A68:  now
        assume
        r=q;
        then b,r // a,o by A2,A5,A12,A16,A42,A46,A47,A49,A59,A51,A61,
        Def1;
        then
A69:    r,b // o,a by AFF_1:13;
        LIN o,a,a' by A3,A8,A9,A14,AFF_1:33;
        then o,a // o,a' by AFF_1:def 1;
        hence r,b // o,a' by A6,A69,AFF_1:14;
      end;
      LIN q,r,b by A67,AFF_1:15;
      then q,r // q,b by AFF_1:def 1;
      then r,q // b,q by AFF_1:13;
      then r,q // a,o by A24,A59,A62,AFF_1:14;
      then
A70:  a',o // r,q by A6,A41,AFF_1:14;
      LIN b,a,p by A42,A46,A47,A49,AFF_1:33;
      then b,a // b,p by AFF_1:def 1;
      then a,b // p,b by AFF_1:13;
      then
A71:  p,b // a',b' by A19,A26,AFF_1:14;
      LIN r,b,q by A67,AFF_1:15;
      then r,b // r,q by AFF_1:def 1;
      then a',o // r,b by A70,A68,AFF_1:13,14;
      then
A72:  p,o // r,b' by A2,A4,A10,A11,A15,A54,A63,A66,A71,Def1;
      p,q // a',c' by A20,A23,A61,AFF_1:14;
      then
A73:  p,o // r,c' by A2,A5,A13,A16,A59,A54,A63,A66,A70,Def1;
      then r,c' // r,b' by A52,A72,AFF_1:14;
      then LIN r,c',b' by AFF_1:def 1;
      then LIN c',b',r by AFF_1:15;
      then c',b' // c',r by AFF_1:def 1;
      then
A74:  r,c' // b',c' by AFF_1:13;
      b,c // r,c' by A52,A51,A73,AFF_1:14;
      then r=c' by A21,A74,AFF_1:14;
      then p,o // b',c' by A72,AFF_1:13;
      hence contradiction by A21,A52,A51,AFF_1:14;
    end;
A75: b' in N by A30,AFF_1:38;
A76: b'<>c' by A21,AFF_1:12;
    then
A77: b' in M & c' in M by AFF_1:38;
    M is being_line by A76,AFF_1:38;
    then consider K such that
A78: o in K and
A79: M // K by AFF_1:63;
A80: K is being_line by A79,AFF_1:50;
A81: a' in N by A30,AFF_1:38;
    not N // K
    proof
      assume
      N // K;
      then N // M by A79,AFF_1:58;
      then c' in N by A77,A75,AFF_1:59;
      hence contradiction by A36,A33,A81,A75,AFF_1:33;
    end;
    then consider p such that
A82: p in N and
A83: p in K by A33,A80,AFF_1:72;
A84: o<>a'
    proof
      assume
A85:  o=a';
      a',b' // b,a by A19,AFF_1:13;
      then a in P by A4,A10,A11,A15,A30,A85,AFF_1:62;
      hence contradiction by A3,A4,A6,A8,A14,A15,A17,AFF_1:30;
    end;
A86: o<>p
    proof
      assume
      o=p;
      then LIN o,a',b' by A33,A81,A75,A82,AFF_1:33;
      then
A87:  b' in A by A3,A9,A14,A84,AFF_1:39;
      a',b' // a,b by A19,AFF_1:13;
      then b in A by A8,A9,A14,A30,A87,AFF_1:62;
      hence contradiction by A3,A4,A7,A10,A14,A15,A17,AFF_1:30;
    end;
    D is being_line by A34,AFF_1:38;
    then consider T such that
A88: p in T and
A89: D // T by AFF_1:63;
A90: T is being_line by A89,AFF_1:50;
A91: a' in D & c' in D by A34,AFF_1:38;
    not C // T
    proof
      assume
      C // T;
      then C // D by A89,AFF_1:58;
      then a' in C by A13,A91,AFF_1:59;
      hence contradiction by A3,A5,A9,A14,A16,A18,A84,AFF_1:30;
    end;
    then consider q such that
A92: q in C and
A93: q in T by A16,A90,AFF_1:72;
A94: b',c' // p,o by A77,A78,A79,A83,AFF_1:53;
A95: o<>b'
    proof
      assume
A96:  o=b';
      b',a' // a,b by A19,AFF_1:13;
      then b in A by A3,A8,A9,A14,A30,A96,AFF_1:62;
      hence contradiction by A3,A4,A7,A10,A14,A15,A17,AFF_1:30;
    end;
A97: b'<>q
    proof
      assume
      b'=q;
      then P=C by A4,A5,A11,A15,A16,A95,A92,AFF_1:30;
      hence contradiction by A10,A11,A12,A13,A15,A21,AFF_1:65;
    end;
    set R=Line(a,p);
A98: p<>a
    proof
      assume
      p=a;
      then b' in A by A8,A9,A14,A27,A33,A81,A75,A82,AFF_1:30;
      hence contradiction by A3,A4,A11,A14,A15,A17,A95,AFF_1:30;
    end;
    then
A99: R is being_line by AFF_1:38;
A100: p,q // a',c' by A91,A88,A89,A93,AFF_1:53;
    then
A101: b',q // a',o by A2,A5,A13,A16,A33,A81,A75,A82,A92,A94,
    Def1;
A102: a in R & p in R by A98,AFF_1:38;
    not b',c' // A by A14,A21,A40,AFF_1:45;
    then
A103: K<>A by A77,A79,AFF_1:54;
    not b',q // R
    proof
      assume
      b',q // R;
      then
A104: a',o // R by A101,A97,AFF_1:46;
      a',o // A by A3,A9,A14,AFF_1:54,55;
      then p in A by A8,A84,A102,A104,AFF_1:59,67;
      hence contradiction by A3,A14,A78,A80,A83,A86,A103,AFF_1:30;
    end;
    then consider r such that
A105: r in R and
A106: LIN b',q,r by A99,AFF_1:73;
A107: now
      assume
      r=q;
      then b',r // a',o by A2,A5,A13,A16,A33,A81,A75,A82,A92,A94,A100,
      Def1;
      then
A108: r,b' // o,a' by AFF_1:13;
      LIN o,a',a by A3,A8,A9,A14,AFF_1:33;
      then o,a' // o,a by AFF_1:def 1;
      hence r,b' // o,a by A84,A108,AFF_1:14;
    end;
    LIN b',a',p by A33,A81,A75,A82,AFF_1:33;
    then b',a' // b',p by AFF_1:def 1;
    then p,b' // a',b' by AFF_1:13;
    then
A109: p,b' // a,b by A19,A30,AFF_1:14;
    LIN o,a,a' by A3,A8,A9,A14,AFF_1:33;
    then o,a // o,a' by AFF_1:def 1;
    then
A110: a,o // a',o by AFF_1:13;
    LIN q,r,b' by A106,AFF_1:15;
    then q,r // q,b' by AFF_1:def 1;
    then r,q // b',q by AFF_1:13;
    then r,q // a',o by A101,A97,AFF_1:14;
    then
A111: a,o // r,q by A84,A110,AFF_1:14;
    LIN r,b',q by A106,AFF_1:15;
    then r,b' // r,q by AFF_1:def 1;
    then a,o // r,b' by A111,A107,AFF_1:13,14;
    then
A112: p,o // r,b by A2,A4,A10,A11,A15,A99,A102,A105,A109,Def1;
    p,q // a,c by A20,A34,A100,AFF_1:14;
    then
A113: p,o // r,c by A2,A5,A12,A16,A92,A99,A102,A105,A111,Def1;
    then r,c // r,b by A86,A112,AFF_1:14;
    then LIN r,c,b by AFF_1:def 1;
    then LIN c,b,r by AFF_1:15;
    then c,b // c,r by AFF_1:def 1;
    then
A114: b,c // r,c by AFF_1:13;
    b',c' // r,c by A86,A94,A113,AFF_1:14;
    then r=c by A21,A114,AFF_1:14;
    then b,c // p,o by A112,AFF_1:13;
    hence contradiction by A21,A86,A94,AFF_1:14;
  end;
end;

theorem
  AP is Desarguesian implies AP is Moufangian
proof
  assume
A1: AP is Desarguesian;
  thus AP is Moufangian
  proof
    let K,o,a,b,c,a',b',c';
    assume that
A2: K is being_line and
A3: o in K and
A4: c in K & c' in K and
A5: not a in K and
A6: o<>c and
A7: a<>b and
A8: LIN o,a,a' and
A9: LIN o,b,b' and
A10: a,b // a',b' & a,c // a',c' and
A11: a,b // K;
    set A=Line(o,a), P=Line(o,b);
A12: o in A by A3,A5,AFF_1:38;
A13: now
      assume
A14:  o=b;
      b,a // K by A11,AFF_1:48;
      hence contradiction by A2,A3,A5,A14,AFF_1:37;
    end;
    then
A15: b in P by AFF_1:38;
A16: a in A by A3,A5,AFF_1:38;
A17: A is being_line by A3,A5,AFF_1:38;
A18: A<>P
    proof
      assume
      A=P;
      then a,b // A by A17,A16,A15,AFF_1:54,55;
      hence contradiction by A3,A5,A7,A11,A12,A16,AFF_1:59,67;
    end;
A19: P is being_line & o in P by A13,AFF_1:38;
    then
A20: b' in P by A9,A13,A15,AFF_1:39;
    a' in A by A3,A5,A8,A17,A12,A16,AFF_1:39;
    hence thesis by A1,A2,A3,A4,A5,A6,A10,A13,A17,A12,A16,A19,A15,A20,A18,
    Def4;
  end;
end;

theorem Th27:
  AP is satisfying_TDES_1 implies AP is satisfying_des_1
proof
  assume
A1: AP is satisfying_TDES_1;
  thus AP is satisfying_des_1
  proof
    let A,P,C,a,b,c,a',b',c';
    assume that
A2: A // P and
A3: a in A and
A4: a' in A and
A5: b in P and
A6: b' in P and
A7: c in C and
A8: c' in C and
A9: A is being_line and
A10: P is being_line and
A11: C is being_line and
A12: A<>P and
A13: A<>C and
A14: a,b // a',b' and
A15: a,c // a',c' and
A16: b,c // b',c' and
A17: not LIN a,b,c and
A18: c <>c';
    set P'=Line(a,b), C'=Line(a',b');
A19: a'<>b' by A2,A4,A6,A12,AFF_1:59;
    then
A20: a' in C' by AFF_1:38;
A21: a'<>c'
    proof
      assume
      a'=c';
      then b,c // a',b' by A16,AFF_1:13;
      then a,b // b,c by A14,A19,AFF_1:14;
      then b,a // b,c by AFF_1:13;
      then LIN b,a,c by AFF_1:def 1;
      hence contradiction by A17,AFF_1:15;
    end;
A22: not c' in A
    proof
      assume
A23:  c' in A;
      a',c' // a,c by A15,AFF_1:13;
      then c in A by A3,A4,A9,A21,A23,AFF_1:62;
      hence contradiction by A7,A8,A9,A11,A13,A18,A23,AFF_1:30;
    end;
A24: C' is being_line by A19,AFF_1:38;
    assume
A25: not A // C;
    then consider o such that
A26: o in A and
A27: o in C by A9,A11,AFF_1:72;
A28: LIN o,c',c by A7,A8,A11,A27,AFF_1:33;
A29: a<>a'
    proof
      assume
A30:  a=a';
      then LIN a,c,c' by A15,AFF_1:def 1;
      then
A31:  LIN c,c',a by AFF_1:15;
      LIN a,b,b' by A14,A30,AFF_1:def 1;
      then LIN b,b',a by AFF_1:15;
      then b=b' or a in P by A5,A6,A10,AFF_1:39;
      then LIN b,c,c' by A2,A3,A12,A16,AFF_1:59,def 1;
      then
A32:  LIN c,c',b by AFF_1:15;
      LIN c,c',c by AFF_1:16;
      hence contradiction by A17,A18,A31,A32,AFF_1:17;
    end;
A33: o<>a'
    proof
      assume
A34:  o=a';
      a',c' // c,a by A15,AFF_1:13;
      then a in C by A7,A8,A11,A27,A21,A34,AFF_1:62;
      hence contradiction by A3,A4,A9,A11,A13,A27,A29,A34,AFF_1:30;
    end;
A35: a<>b by A17,AFF_1:16;
    then
A36: P' is being_line by AFF_1:38;
    consider N such that
A37: c' in N and
A38: A // N by A9,AFF_1:63;
A39: b' in C' by A19,AFF_1:38;
A40: not N // C'
    proof
      assume
      N // C';
      then A // C' by A38,AFF_1:58;
      then b' in A by A4,A39,A20,AFF_1:59;
      hence contradiction by A2,A6,A12,AFF_1:59;
    end;
    N is being_line by A38,AFF_1:50;
    then consider q such that
A41: q in N and
A42: q in C' by A24,A40,AFF_1:72;
A43: c',q // A by A37,A38,A41,AFF_1:54;
A44: c'<>q
    proof
      assume
      c'=q;
      then LIN a',b',c' by A24,A39,A20,A42,AFF_1:33;
      then a',b' // a',c' by AFF_1:def 1;
      then a',b' // a,c by A15,A21,AFF_1:14;
      then a,b // a,c by A14,A19,AFF_1:14;
      hence contradiction by A17,AFF_1:def 1;
    end;
A45: c',a' // c,a by A15,AFF_1:13;
A46: c,b // c',b' by A16,AFF_1:13;
A47: a in P' by A35,AFF_1:38;
A48: b<>c by A17,AFF_1:16;
A49: not c in P
    proof
      assume
A50:  c in P;
      then c' in P by A5,A6,A10,A16,A48,AFF_1:62;
      hence contradiction by A2,A7,A8,A10,A11,A18,A25,A50,AFF_1:30;
    end;
    not P // C by A2,A25,AFF_1:58;
    then consider s such that
A51: s in P and
A52: s in C by A10,A11,AFF_1:72;
A53: LIN s,c,c' by A7,A8,A11,A52,AFF_1:33;
A54: b<>b'
    proof
      assume
      b=b';
      then b,a // b,a' by A14,AFF_1:13;
      then LIN b,a,a' by AFF_1:def 1;
      then LIN a,a',b by AFF_1:15;
      then b in A by A3,A4,A9,A29,AFF_1:39;
      hence contradiction by A2,A5,A12,AFF_1:59;
    end;
A55: s<>b
    proof
      assume
A56:  s=b;
      b,c // c',b' by A16,AFF_1:13;
      then b' in C by A7,A8,A11,A48,A52,A56,AFF_1:62;
      hence contradiction by A2,A5,A6,A10,A11,A25,A54,A52,A56,AFF_1:30;
    end;
    consider M such that
A57: c in M and
A58: A // M by A9,AFF_1:63;
A59: M is being_line by A58,AFF_1:50;
A60: b in P' by A35,AFF_1:38;
    not M // P'
    proof
      assume
      M // P';
      then A // P' by A58,AFF_1:58;
      then b in A by A3,A60,A47,AFF_1:59;
      hence contradiction by A2,A5,A12,AFF_1:59;
    end;
    then consider p such that
A61: p in M and
A62: p in P' by A59,A36,AFF_1:72;
    M // P by A2,A58,AFF_1:58;
    then
A63: c,p // P by A57,A61,AFF_1:54;
A64: M // N by A58,A38,AFF_1:58;
    then
A65: c,p // c',q by A57,A37,A61,A41,AFF_1:53;
A66: now
      assume
      p=q;
      then M=N by A64,A61,A41,AFF_1:59;
      hence contradiction by A7,A8,A11,A18,A25,A57,A58,A37,A59,AFF_1:30;
    end;
A67: P' // C' by A14,A35,A19,AFF_1:51;
    then
A68: p,b // q,b' by A60,A39,A62,A42,AFF_1:53;
A69: q,a' // p,a by A47,A20,A67,A62,A42,AFF_1:53;
    c',q // c,p by A57,A37,A64,A61,A41,AFF_1:53;
    then LIN o,q,p by A1,A3,A4,A9,A26,A28,A22,A45,A69,A43,A44,A33,
    Def8;
    then
A70: LIN p,q,o by AFF_1:15;
    c <>p by A17,A36,A60,A47,A62,AFF_1:33;
    then LIN s,p,q by A1,A5,A6,A10,A51,A53,A63,A68,A49,A55,A65,A46,Def8;
    then
A71: LIN p,q,s by AFF_1:15;
    LIN p,q,p by AFF_1:16;
    then o=s or p in C by A11,A27,A52,A70,A71,A66,AFF_1:17,39;
    then c in P' by A2,A7,A11,A12,A25,A26,A57,A58,A59,A61,A62,A51, AFF_1
    :30,59;
    hence contradiction by A17,A36,A60,A47,AFF_1:33;
  end;
end;

theorem
  AP is Moufangian implies AP is translational
proof
  assume
  AP is Moufangian;
  then AP is satisfying_des_1 by Th17,Th27;
  hence thesis by Th21;
end;

theorem
  AP is translational implies AP is satisfying_pap
proof
  assume
A1: AP is translational;
  thus AP is satisfying_pap
  proof
    let M,N,a,b,c,a',b',c';
    assume that
A2: M is being_line and
A3: N is being_line and
A4: a in M and
A5: b in M and
A6: c in M and
A7: M // N and
A8: M<>N and
A9: a' in N and
A10: b' in N and
A11: c' in N and
A12: a,b' // b,a' and
A13: b,c' // c,b';
    set A=Line(a,b'), A'=Line(b,a'), P=Line(b,c'), P'=Line(c,b');
A14: c <>b' by A6,A7,A8,A10,AFF_1:59;
    then
A15: c in P' & b' in P' by AFF_1:38;
A16: b<>c' by A5,A7,A8,A11,AFF_1:59;
    then
A17: P is being_line & b in P by AFF_1:38;
A18: P' is being_line by A14,AFF_1:38;
    then consider C' such that
A19: a in C' and
A20: P' // C' by AFF_1:63;
A21: C' is being_line by A20,AFF_1:50;
    assume
A22: not thesis;
A23: now
      assume
A24:  a=c;
      then b,c' // b,a' by A12,A13,A14,AFF_1:14;
      then LIN b,c',a' by AFF_1:def 1;
      then LIN a',c',b by AFF_1:15;
      then a'=c' or b in N by A3,A9,A11,AFF_1:39;
      hence contradiction by A5,A7,A8,A22,A24,AFF_1:11,59;
    end;
A25: P'<>C'
    proof
      assume
      P'=C';
      then LIN a,c,b' by A18,A15,A19,AFF_1:33;
      then b' in M by A2,A4,A6,A23,AFF_1:39;
      hence contradiction by A7,A8,A10,AFF_1:59;
    end;
A26: c' in P by A16,AFF_1:38;
    then
A27: P' // P by A13,A16,A14,A18,A17,A15,AFF_1:52;
A28: now
      assume
A29:  b=c;
      then LIN b,c',b' by A13,AFF_1:def 1;
      then LIN b',c',b by AFF_1:15;
      then b'=c' or b in N by A3,A10,A11,AFF_1:39;
      hence contradiction by A5,A7,A8,A12,A22,A29,AFF_1:59;
    end;
A30: P'<>P
    proof
      assume
      P'=P;
      then LIN b,c,b' by A17,A15,AFF_1:33;
      then b' in M by A2,A5,A6,A28,AFF_1:39;
      hence contradiction by A7,A8,A10,AFF_1:59;
    end;
A31: a<>b' by A4,A7,A8,A10,AFF_1:59;
    then
A32: A is being_line by AFF_1:38;
    then consider C such that
A33: c in C and
A34: A // C by AFF_1:63;
A35: C is being_line by A34,AFF_1:50;
A36: a,b // b',a' by A4,A5,A7,A9,A10,AFF_1:53;
A37: b',c' // c,b by A5,A6,A7,A10,A11,AFF_1:53;
A38: b<>a' by A5,A7,A8,A9,AFF_1:59;
    then
A39: a' in A' by AFF_1:38;
A40: a in A & b' in A by A31,AFF_1:38;
A41: A<>C
    proof
      assume
      A=C;
      then LIN a,c,b' by A32,A40,A33,AFF_1:33;
      then b' in M by A2,A4,A6,A23,AFF_1:39;
      hence contradiction by A7,A8,A10,AFF_1:59;
    end;
    not C // C'
    proof
      assume
      C // C';
      then A // C' by A34,AFF_1:58;
      then A // P' by A20,AFF_1:58;
      then b',a // b',c by A40,A15,AFF_1:53;
      then LIN b',a,c by AFF_1:def 1;
      then LIN a,c,b' by AFF_1:15;
      then b' in M by A2,A4,A6,A23,AFF_1:39;
      hence contradiction by A7,A8,A10,AFF_1:59;
    end;
    then consider s such that
A42: s in C and
A43: s in C' by A35,A21,AFF_1:72;
A44: A' is being_line & b in A' by A38,AFF_1:38;
A45: now
      assume
A46:  a=b;
      then LIN a,b',a' by A12,AFF_1:def 1;
      then LIN a',b',a by AFF_1:15;
      then a'=b' or a in N by A3,A9,A10,AFF_1:39;
      hence contradiction by A4,A7,A8,A13,A22,A46,AFF_1:59;
    end;
A47: now
      assume
      a'=b';
      then a',a // a',b by A12,AFF_1:13;
      then LIN a',a,b by AFF_1:def 1;
      then LIN a,b,a' by AFF_1:15;
      then a' in M by A2,A4,A5,A45,AFF_1:39;
      hence contradiction by A7,A8,A9,AFF_1:59;
    end;
A48: A<>A'
    proof
      assume
      A=A';
      then LIN a',b',a by A32,A40,A39,AFF_1:33;
      then a in N by A3,A9,A10,A47,AFF_1:39;
      hence contradiction by A4,A7,A8,AFF_1:59;
    end;
A49: b<>s
    proof
      assume
      b=s;
      then a,b' // b,c by A40,A33,A34,A42,AFF_1:53;
      then b,a' // b,c by A12,A31,AFF_1:14;
      then LIN b,a',c by AFF_1:def 1;
      then LIN b,c,a' by AFF_1:15;
      then a' in M by A2,A5,A6,A28,AFF_1:39;
      hence contradiction by A7,A8,A9,AFF_1:59;
    end;
A50: A // A' by A12,A31,A38,AFF_1:51;
    a,s // b',c by A15,A19,A20,A43,AFF_1:53;
    then
A51: b,s // a',c by A1,A32,A40,A44,A39,A33,A34,A35,A42,A36,A48,A41,A50, Def11;
    b',a // c,s by A40,A33,A34,A42,AFF_1:53;
    then c',a // b,s by A1,A18,A17,A26,A15,A19,A20,A21,A43,
    A37,A30,A25,A27
    , Def11;
    then c',a // a',c by A51,A49,AFF_1:14;
    hence contradiction by A22,AFF_1:13;
  end;
end;

