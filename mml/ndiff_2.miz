:: Differentiable Functions on Normed Linear Spaces. {P}art {II}
::  by Hiroshi Imura , Yuji Sakai and Yasunari Shidama
::
:: Received June 4, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabulary PRE_TOPC, FCONT_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, LATTICES, SQUARE_1, ORDINAL2, SEQ_1, SEQ_2, SEQM_3,
      RCOMP_1, BOOLE, REALSET1, FINSEQ_4, PARTFUN1, NORMSP_1, RSSPACE,
      LOPBAN_1, FDIFF_1, NDIFF_2;
 notation TARSKI, XBOOLE_0, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, PRE_TOPC,
      STRUCT_0, XCMPLX_0, XREAL_0, ORDINAL1, NUMBERS, REAL_1, NAT_1, FINSEQ_4,
      SQUARE_1, COMPLEX1,
      RLVECT_1, SEQ_1, SEQ_2, SEQM_3, REALSET2, VFUNCT_1,
      NORMSP_1, RSSPACE, LOPBAN_1, LOPBAN_3, NFCONT_1, FDIFF_1, NDIFF_1;
 constructors REAL_1, BINARITH, SQUARE_1, MEMBERED, SEQ_2, PREPOWER, PARTFUN1,
      FINSEQ_4, VFUNCT_1, RSSPACE3, LOPBAN_3, NFCONT_1, FDIFF_1,
      COMPLEX1, NDIFF_1;
 clusters RELSET_1, STRUCT_0, ORDINAL2, XREAL_0, MEMBERED, SEQ_1, LOPBAN_1,
      LOPBAN_2, FDIFF_1, COMPLEX1, NDIFF_1, XCMPLX_0, SQUARE_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI;
 theorems AXIOMS, TARSKI, ABSVALUE, XBOOLE_0, XBOOLE_1, REAL_1, REAL_2,
      RLVECT_1, XCMPLX_0, XCMPLX_1, ZFMISC_1, SEQ_1, SEQ_2, NAT_1, SEQM_3,
      SQUARE_1, RELAT_1, FUNCT_1, FINSEQ_4, VFUNCT_1, FUNCT_2, SEQ_4, NORMSP_1,
      LOPBAN_1, LOPBAN_2, LOPBAN_3, PARTFUN1, PARTFUN2, NFCONT_1, FDIFF_1,
      NDIFF_1;
 schemes RCOMP_1, NORMSP_1;

begin :: Differentiable Functions on Normed Linear Spaces
 
 reserve n,k for Nat;
 reserve x,y,X,X1,Z,Z1 for set;
 reserve s,g,r,p for Real;
 reserve S,T for non trivial RealNormSpace;
 reserve f,f1,f2 for PartFunc of S,T;
 reserve rseq,rseq1,rseq2 for Real_Sequence;
 reserve s1,s2,q1,seq,seq1,seq2 for sequence of S;
 reserve x0,x1,x2 for Point of S;
 reserve Y for Subset of  S;
 reserve f,f1,f2 for PartFunc of S,T;
 reserve s1,s2,q1,seq,seq1,seq2 for sequence of S;
 reserve h for convergent_to_0 sequence of S;
 reserve c for constant sequence of S;
 reserve R,R1,R2 for REST of S,T;
 reserve L,L1,L2 for Point of R_NormSpace_of_BoundedLinearOperators(S,T);

definition
  let X,Y,Z be RealNormSpace;
  let f be Element of BoundedLinearOperators(X,Y);
  let g be Element of BoundedLinearOperators(Y,Z);
  func g*f -> Element of BoundedLinearOperators(X,Z) equals
  modetrans(g,Y,Z)*modetrans(f,X,Y);
  correctness
  proof
  modetrans(g,Y,Z)*modetrans(f,X,Y)
    is bounded LinearOperator of X,Z by LOPBAN_2:2;
  hence thesis by LOPBAN_1:def 10;
  end;
end;

definition
  let X,Y,Z be RealNormSpace;
  let f be Point of R_NormSpace_of_BoundedLinearOperators(X,Y);
  let g be Point of R_NormSpace_of_BoundedLinearOperators(Y,Z);
  func g*f -> Point of R_NormSpace_of_BoundedLinearOperators(X,Z)
   equals :Def001:
    modetrans(g,Y,Z)*modetrans(f,X,Y);
  correctness 
  proof
  AS0: 
  R_NormSpace_of_BoundedLinearOperators(X,Z) =
  NORMSTR (# BoundedLinearOperators(X,Z),
          Zero_(BoundedLinearOperators(X,Z),
          R_VectorSpace_of_LinearOperators(X,Z)),
           Add_(BoundedLinearOperators(X,Z),
           R_VectorSpace_of_LinearOperators(X,Z)),
            Mult_(BoundedLinearOperators(X,Z),
            R_VectorSpace_of_LinearOperators(X,Z)),
          BoundedLinearOperatorsNorm(X,Z) #)
       by LOPBAN_1:def 15;
    modetrans(g,Y,Z)*modetrans(f,X,Y)
    is bounded LinearOperator of X,Z   
 by LOPBAN_2:2;
  hence thesis by AS0,LOPBAN_1:def 10;
  end;
end;

theorem LM01:
for x0 be Point of S st f is_differentiable_in x0
 ex N being Neighbourhood of x0 st
  (
    N c= dom f & 
    for z be Point of S holds 
     for h be convergent_to_0 Real_Sequence
     for c st rng c = {x0} & rng (h*z+c) c= N holds
      h"(#)(f*(h*z+c) - f*c) is convergent & 
      diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c))
  )
proof
   let x0 be Point of S such that 
ASM0:f is_differentiable_in x0;
   consider N be Neighbourhood of x0 such that 
P01:N c= dom f and
P02:ex R st
     R/.0.S=0.T & 
     R is_continuous_in 0.S &
     for x be Point of S st x in N holds
      f/.x-f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0) by ASM0,NDIFF_1:52;
   consider R such that
P03A:R/.0.S=0.T and
    R is_continuous_in 0.S and
P03:for x be Point of S st x in N holds 
     f/.x - f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0) by P02;
   now 
     let z be Point of S;
     let h be convergent_to_0 Real_Sequence;
     let c such that 
P04:  rng c = {x0} and
P05:  rng (h*z+c) c= N;
     x0 in N by NFCONT_1:4; then
P041:rng c c= dom f by P04,P01,ZFMISC_1:37; 
P051:rng (h*z+c) c= dom f by P01,P05,XBOOLE_1:1;
P06: for n be Nat holds 
      ||. (h"(#)(f*(h*z+c) - f*c)).n - diff(f,x0).z .||
        = ||. (h"(#) (R*(h*z))).n .||
     proof
       let n be Nat;
       R_NormSpace_of_BoundedLinearOperators(S,T)
        = NORMSTR (# BoundedLinearOperators(S,T),
                 Zero_(BoundedLinearOperators(S,T),
                 R_VectorSpace_of_LinearOperators(S,T)),
                  Add_(BoundedLinearOperators(S,T),
                  R_VectorSpace_of_LinearOperators(S,T)),
                   Mult_(BoundedLinearOperators(S,T),
                   R_VectorSpace_of_LinearOperators(S,T)),
                   BoundedLinearOperatorsNorm(S,T) #) 
                by LOPBAN_1:def 15; then
       reconsider L = diff(f,x0) as Element of BoundedLinearOperators(S,T); 
       R is total by NDIFF_1:def 5; then
       dom R = the carrier of S by PARTFUN1:def 4; then
P052:  rng (h*z) c= dom R;
       dom c = NAT by FUNCT_2:def 1; then
       c.n in rng c by FUNCT_1:12; then
P061:  c.n=x0 by P04,TARSKI:def 1;
       dom (h*z+c) = NAT by FUNCT_2:def 1; then
       (h*z+c).n in rng (h*z+c) by FUNCT_1:12; then
       (h*z+c).n in N by P05; then
       (h*z).n+c.n in N by NORMSP_1:def 5; then
P063:  h.n*z+x0  in N by P061,NDIFF_1:def 3;  
       h is being_not_0 by FDIFF_1:def 1; then
Q064:  h.n <> 0 by SEQ_1:7;
       (h"(#)(f*(h*z+c) - f*c)).n - diff(f,x0).z
         = (h".n) * (f*(h*z+c) - f*c).n - diff(f,x0).z by NDIFF_1:def 2
        .= (h.n)" * (f*(h*z+c) - f*c).n - diff(f,x0).z by SEQ_1:def 8
        .= (h.n)" * ((f*(h*z+c)).n - (f*c).n) - diff(f,x0).z by NORMSP_1:def 6
        .= (h.n)" * (f/.((h*z+c).n) - (f*c).n) - diff(f,x0).z 
      by P051,NFCONT_1:8
        .= (h.n)" * (f/.((h*z+c).n) - f/.(c.n) )- diff(f,x0).z 
      by P041,NFCONT_1:8
        .= (h.n)" * (f/.((h*z).n+c.n) - f/.(c.n) )- diff(f,x0).z 
      by NORMSP_1:def 5
        .= (h.n)" * (f/.(h.n*z+c.n) - f/.(c.n) )- diff(f,x0).z 
      by NDIFF_1:def 3
        .= (h.n)" * (diff(f,x0).(h.n*z+x0-x0) + R/.(h.n*z+x0-x0))
         - diff(f,x0).z by P03,P063,P061
        .= (h.n)" * (diff(f,x0).(h.n*z+(x0-x0)) + R/.(h.n*z+x0-x0))
         - diff(f,x0).z by RLVECT_1:42
        .= (h.n)" * (diff(f,x0).(h.n*z+(x0-x0)) + R/.(h.n*z+(x0-x0)))
         - diff(f,x0).z by RLVECT_1:42
        .= (h.n)" * (diff(f,x0).(h.n*z+0.S) + R/.(h.n*z+(x0-x0)))
         - diff(f,x0).z by RLVECT_1:28 
        .= (h.n)" * (diff(f,x0).(h.n*z+0.S) + R/.(h.n*z+0.S))
         - diff(f,x0).z by RLVECT_1:28 
        .= (h.n)" * (diff(f,x0).(h.n*z) + R/.(h.n*z+0.S))
         - diff(f,x0).z by RLVECT_1:10 
        .= (h.n)" * (diff(f,x0).(h.n*z) + R/.(h.n*z))
         - diff(f,x0).z by RLVECT_1:10 
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (diff(f,x0).(h.n*z))
         - diff(f,x0).z by RLVECT_1:def 9
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (modetrans(L,S,T).(h.n*z))
         - diff(f,x0).z by LOPBAN_1:def 12
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (h.n* (modetrans(L,S,T).z)) 
         - diff(f,x0).z by LOPBAN_1:def 6
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (h.n* (L.z)) 
         - diff(f,x0).z by LOPBAN_1:def 12
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (h.n)* (diff(f,x0).z)
         - diff(f,x0).z by RLVECT_1:def 9
        .= (h.n)" *  (R/.(h.n*z))+1* (diff(f,x0).z) 
         - diff(f,x0).z by Q064,XCMPLX_0:def 7
        .= (h.n)" *  (R/.(h.n*z))+(diff(f,x0).z) - diff(f,x0).z 
      by RLVECT_1:def 9
        .= (h.n)" *  (R/.(h.n*z))+(diff(f,x0).z - diff(f,x0).z)  
           by RLVECT_1:42
        .= (h.n)" * (R/.(h.n*z))+0.T by RLVECT_1:28 
        .= (h.n)" * (R/.(h.n*z)) by RLVECT_1:10 
        .= (h".n) * (R/.(h.n*z)) by SEQ_1:def 8
        .= (h".n) * (R/.((h*z).n)) by NDIFF_1:def 3
        .= (h".n) * ((R*(h*z)).n) by P052, NFCONT_1:8 
        .=(h"(#) (R*(h*z))).n by NDIFF_1:def 2;
       hence thesis;
     end;
P07A:abs(h)"(#)(R*(h*z)) is convergent & lim ( abs(h)"(#)(R*(h*z)) ) = 0.T
     proof
       now per cases;
         case CA0: z= 0.S;
P01:       for n be Nat holds  (abs(h)"(#) (R*(h*z))).n  = 0.T
           proof
             let n be Nat;
             R is total by NDIFF_1:def 5; then
             dom R = the carrier of S by PARTFUN1:def 4; then
P052:        rng (h*z) c= dom R;
             thus  (abs(h)"(#)(R*(h*z))).n
               =abs(h)".n*(R*(h*z)).n by NDIFF_1:def 2
              .=(abs(h).n)"*(R*(h*z)).n by SEQ_1:def 8
              .=(abs(h.n))"*(R*(h*z)).n by SEQ_1:16
              .= (abs(h.n))"*(R/.((h*z).n)) by P052, NFCONT_1:8
              .= (abs(h.n))"*(R/.(h.n*0.S)) by CA0,NDIFF_1:def 3
              .= (abs(h.n))"*(R/.(0.S)) by RLVECT_1:23
              .=0.T by RLVECT_1:23,P03A;
           end; then
P02:       abs(h)"(#) (R*(h*z)) is  constant by LOPBAN_3:def 5; 
           (abs(h)"(#) (R*(h*z))).0 = 0.T by P01;
           hence abs(h)"(#) (R*(h*z))  is convergent & 
            lim ( abs(h)"(#) (R*(h*z)) ) =0.T by P02,NDIFF_1:21;
         end;
         case CA1: z<> 0.S;
           reconsider s = h*z as convergent_to_0 sequence of S
         by NDIFF_1:24,CA1;
           now let n;
             R is total by NDIFF_1:def 5; then 
             dom R = the carrier of S by PARTFUN1:def 4; then 
A34:         rng s c= dom R; 
             h is being_not_0 by FDIFF_1:def 1; then
             h.n <> 0 by SEQ_1:7; then
Q1:          abs(h.n) <> 0 by ABSVALUE:6;
             s is_not_0 by NDIFF_1:def 4; then
             s.n <> 0.S by NDIFF_1:7; then
Q2:          ||.s.n.|| <> 0 by NORMSP_1:def 2;  
             ||.s.n.|| = ||.(h.n)*z.|| by NDIFF_1:def 3
             .= abs(h.n) * ||.z.|| by NORMSP_1:def 2; then
             (abs(h.n))"* ||.s.n.||
              = ((abs(h.n))"* (abs(h.n))) * ||.z.||
             .= 1 * ||.z.|| by Q1,XCMPLX_0:def 7
             .=  ||.z.||; then
             ||.z.|| *( ||.s.n.|| )" =  (abs(h.n))"* ||.s.n.|| *( ||.s.n.|| )"
             .= (abs(h.n))"* (||.s.n.|| *( ||.s.n.|| )")
             .= (abs(h.n))"* (||.s.n.|| /( ||.s.n.|| )) by XCMPLX_0:def 9
             .= (abs(h.n))"*1 by Q2,XCMPLX_1:60 
             .= (abs(h.n))"; then
 Q3:         ||.z.|| *( ||.s.||.n )" =  (abs(h.n))" by NORMSP_1:def 10;
             R is total by NDIFF_1:def 5; then
             dom R = the carrier of S by PARTFUN1:def 4; then
P052:        rng (h*z) c= dom R;
             thus 
             (abs(h)"(#)(R*(h*z))).n 
              = abs(h)".n*(R*(h*z)).n by NDIFF_1:def 2
             .=(abs(h).n)"*(R*(h*z)).n by SEQ_1:def 8
             .=(abs(h.n))"*(R*(h*z)).n by SEQ_1:16
             .= (abs(h.n))"*(R/.((h*z).n)) by P052, NFCONT_1:8
             .= ( ||.z.|| *||.s.||".n)*(R/.(s.n)) by Q3,SEQ_1:def 8
             .= ||.z.|| *(||.s.||".n *(R/.(s.n)) ) by RLVECT_1:def 9
             .= ||.z.|| *((||.s.||".n)*((R*s).n)) by A34,NFCONT_1:8
             .= ||.z.|| *((||.s.||")(#)(R*s)).n by NDIFF_1:def 2
             .= ( ||.z.|| *((||.s.||")(#)(R*s))).n by NORMSP_1:def 8;
           end; then 
A38:       (abs(h)"(#)(R*(h*z))) =  ||.z.|| *((||.s.||")(#)(R*s)) 
          by FUNCT_2:113;
A39:       (||.s.||")(#)(R*s) is convergent  & lim ((||.s.||")(#)(R*s)) = 0.T 
          by NDIFF_1:def 5;
           hence (abs(h)"(#)(R*(h*z))) is convergent by A38,NORMSP_1:37;
           thus lim (abs(h)"(#)(R*(h*z)))
            = ||.z.|| *  0.T by A39,A38,NORMSP_1:45
           .= 0.T by RLVECT_1:23;
         end;
       end;
       hence thesis;
     end;
P07: h"(#) (R*(h*z)) is convergent & lim ( h"(#) (R*(h*z)) )=0.T
     proof
P071:  now let e be Real such that  
P08:     0 < e;
         consider m be Nat such that
P09:      for n be Nat st m <= n holds   
           ||.(abs(h)"(#) (R*(h*z))).n  - 0.T  .|| < e
         by P07A,P08,NORMSP_1:def 11;
         now let n be Nat such that 
P10:       m <= n;
           ||.(h"(#) (R*(h*z))).n  -0.T .|| 
            = ||.(h"(#) (R*(h*z))).n  .|| by RLVECT_1:26
           .= ||.h".n*(R*(h*z)).n .|| by NDIFF_1:def 2
           .= ||.(h.n)"*(R*(h*z)).n .|| by SEQ_1:def 8
           .=abs(abs((h.n)")) * ||.(R*(h*z)).n .|| by NORMSP_1:def 2
           .= ||.abs((h.n)")*(R*(h*z)).n .|| by NORMSP_1:def 2
           .= ||.abs(h".n)*(R*(h*z)).n .|| by SEQ_1:def 8
           .= ||.abs(h").n*(R*(h*z)).n .|| by SEQ_1:16
           .= ||.(abs(h))".n*(R*(h*z)).n .|| by SEQ_1:62
           .= ||.(abs(h)"(#) (R*(h*z))).n .|| by NDIFF_1:def 2
           .= ||.(abs(h)"(#) (R*(h*z))).n -0.T .|| by RLVECT_1:26;
           hence ||.(h"(#) (R*(h*z))).n  -0.T .|| < e by P10,P09;
         end;
         hence 
         ex m be Nat st 
          for n be Nat st m <= n holds   
           ||.(h"(#) (R*(h*z))).n  -0.T .|| < e;
       end;
       hence h"(#) (R*(h*z)) is convergent by NORMSP_1:def 9;
       hence lim ( h"(#) (R*(h*z)) ) =0.T by NORMSP_1:def 11,P071;
     end;
Q01: now let e be Real such that  
P08:   0 < e;
       consider m be Nat such that
P09:    for n be Nat st m <= n holds
         ||.(h"(#) (R*(h*z))).n  - 0.T .|| < e by P07,P08,NORMSP_1:def 11;
       now let n be Nat such that 
P10:     m <= n;
         ||.(h"(#) (R*(h*z))).n  -0.T .|| < e by P09,P10; then
         ||.(h"(#) (R*(h*z))).n   .|| < e by RLVECT_1:26;
         hence ||. (h"(#)(f*(h*z+c) - f*c)).n - diff(f,x0).z .|| < e by P06;
       end;
       hence
       ex m be Nat st 
        for n be Nat st m <= n holds
         ||. (h"(#)(f*(h*z+c) - f*c)).n - diff(f,x0).z .|| < e;
     end;
     hence (h"(#)(f*(h*z+c) - f*c)) is convergent by NORMSP_1:def 9;
     hence lim (h"(#)(f*(h*z+c) - f*c)) = diff(f,x0).z by Q01,NORMSP_1:def 11;
   end;
   hence thesis by P01;
end;

theorem
for x0 be Point of S st f is_differentiable_in x0 
  for z be Point of S
  for h be convergent_to_0 Real_Sequence
  for c st (rng c = {x0}  & rng (h*z+c) c= dom f ) holds
    h"(#)(f*(h*z+c) - f*c) is convergent & 
    diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c)) 
proof
   let x0 be Point of S such that  
AS1:f is_differentiable_in x0;
   let z be Point of S;
   let h be convergent_to_0 Real_Sequence;
   let c such that 
AS2:rng c = {x0} and
AS22:rng (h*z+c) c= dom f;
   consider N being Neighbourhood of x0 such that
AS3:N c= dom f and
AS4:for z be Point of S holds 
     for h be convergent_to_0 Real_Sequence
     for c st rng c = {x0} & rng (h*z+c) c= N holds
      h"(#)(f*(h*z+c) - f*c) is convergent 
      & diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c)) by LM01,AS1;
   consider g be Real such that
A61:0<g and
A62:{y where y is Point of S: ||.y-x0.|| < g} c= N by NFCONT_1:def 3;
A9:for n be Nat holds c.n=x0
   proof
     let n be Nat;
     c.n in rng c by NFCONT_1:6;
     hence c.n=x0 by AS2,TARSKI:def 1;
   end;
   ex n st rng ((h^\n*z+c)) c= N
   proof
A10: c is convergent by NDIFF_1:21; 
     c.0 in rng c by NFCONT_1:6; then 
     c.0=x0 by AS2,TARSKI:def 1; then
A11: lim c = x0 by NDIFF_1:21;
A12: h*z is convergent & lim (h*z) = 0.S 
     proof
P2:    now let p; 
         assume 
AS0:     0<p;
         ex pp be Real st pp > 0 & pp*||.z.|| < p
         proof
           take pp=p/(||.z.||+1);
L3:        0 <= ||.z.|| by NORMSP_1:8;
L5:        ||.z.||+1 > 0+0 by L3,REAL_1:67; then
L4:        0  <  p/(  ||.z.||+1 ) by AS0,SEQ_2:6;
L6:        ||.z.||+0 <  ||.z.||+1 by REAL_1:67;
           0 <= ||.z.|| by NORMSP_1:8; then
           pp* ||.z.|| < pp*(  ||.z.||  + 1) by SEQ_4:6,L6,L4;
           hence pp > 0 & pp* ||.z.||  < p by AS0,SEQ_2:6,XCMPLX_1:88,L5;
         end; then 
         consider pp be Real such that   
AS1:      pp > 0  & pp*||.z.|| < p;
         h is convergent & lim h =0 by FDIFF_1:def 1; then
         consider n be Nat such that
AS2:      for m be Nat st n <= m  holds  abs(h.m- 0) < pp by AS1,SEQ_2:def 7;
         take n;
         let m be Nat such that 
A6:      n<=m;
A11:     abs(h.m-0) < pp by A6,AS2;
A12:     ||.(h*z).m-0.S.|| = ||.h.m*z  - 0.S.|| by NDIFF_1:def 3
         .= ||.h.m*z.|| by RLVECT_1:26
         .= abs(h.m)*||.z.|| by NORMSP_1:def 2;
         0 <= ||.z.|| by NORMSP_1:8; then
         abs(h.m)*||.z.|| <= pp* ||.z.|| by A11,AXIOMS:25;
         hence ||.(h*z).m-0.S.||<p by A12,AS1,AXIOMS:22;
       end;
       hence h*z is convergent by NORMSP_1:def 9;
       hence lim (h*z) = 0.S by P2,NORMSP_1:def 11;
     end; then 
A13: lim ((h*z) + c) = 0.S  + x0 by A10,A11,NORMSP_1:42
      .= x0 by RLVECT_1:10;
     h*z + c is convergent by A10,A12,NORMSP_1:34; then 
     consider n such that
A14:  for m being Nat st n<=m holds 
       ||.(h*z+c).m-x0.||<g by A61,A13,NORMSP_1:def 11;
     take n;
     let y be set; 
     assume y in rng ((h^\n*z+c)); then 
     consider m be Nat such that 
A16:  y =  ((h^\n*z+c)).m by NFCONT_1:6;
     n <= n+m by NAT_1:29; then
     ||.(h*z+c).(n+m)-x0.||<g by A14; then 
     ||.((h*z).(n+m)+c.(n+m)) -x0.||<g by NORMSP_1:def 5; then 
     ||.((h*z).(n+m)+x0) -x0.||<g by A9; then
     ||.((h*z).(n+m)+c.m) -x0.||<g by A9; then 
     ||.((h.(n+m)*z)+c.m) -x0.||<g by NDIFF_1:def 3; then
     ||.((h^\n).m*z+c.m) -x0.||<g by SEQM_3:def 9; then 
     ||.(((h^\n)*z).m+c.m) -x0.||<g by NDIFF_1:def 3; then
KK:  ||.((h^\n*z+c)).m - x0 .|| < g by NORMSP_1:def 5;
     reconsider y1=y as Point of S by A16;
     y1 in  {w where w is Point of S: ||.w-x0.|| < g} by A16,KK;
     hence y in N by A62;
   end; then 
   consider n be Nat such that 
A17:rng ((h^\n*z+c)) c= N;
A18:(h^\n)"(#)(f*(h^\n*z+c) - f*c) is convergent 
      & diff(f,x0).z = lim ((h^\n)"(#)(f*(h^\n*z+c) - f*c)) by AS4,A17,AS2;
Q01:now 
     let m be Nat;
     thus 
     ((h*z+c)^\n).m = (h*z+c).(n+m) by LOPBAN_3:def 6
      .= (h*z).(n+m)+c.(n+m) by NORMSP_1:def 5
      .=(h*z).(n+m)+x0 by A9
      .=(h*z).(n+m)+c.m by A9
      .=(h.(n+m)*z)+c.m by NDIFF_1:def 3
      .=(h^\n).m*z+c.m by SEQM_3:def 9
      .=((h^\n)*z).m+c.m by NDIFF_1:def 3
      .=(h^\n*z+c).m by NORMSP_1:def 5;
   end;
   x0 in N by NFCONT_1:4; then
Q02:rng c c= dom f by AS2,AS3,ZFMISC_1:37; 
Q03:rng (h^\n*z+c) c= dom f by A17,AS3,XBOOLE_1:1;
   (h^\n)"(#)(f*(h^\n*z+c) - f*c) = (h"(#)(f*(h*z+c) - f*c))^\n 
   proof
     now let m be Nat;
K1:    ((f*(h*z+c))^\n).m =(f*(h*z+c)).(n+m) by LOPBAN_3:def 6
         .=f/.((h*z+c).(n+m)) by AS22,NFCONT_1:8        
         .=f/.( ( (h*z+c)^\n).m) by LOPBAN_3:def 6
         .=f/.( (h^\n*z+c).m) by Q01;
       thus
           ( (h"(#)(f*(h*z+c) - f*c))^\n).m
            =(h"(#)(f*(h*z+c) - f*c)).(n+m) by LOPBAN_3:def 6
           .=h".(n+m)*(f*(h*z+c) - f*c).(n+m) by NDIFF_1:def 2
           .=(h.(n+m))"*(f*(h*z+c) - f*c).(n+m) by SEQ_1:def 8
           .=((h^\n).m)"*(f*(h*z+c) - f*c).(n+m) by SEQM_3:def 9
           .=((h^\n).m)"*((f*(h*z+c)).(n+m) - (f*c).(n+m)) 
                    by NORMSP_1:def 6
           .=((h^\n).m)"*(((f*(h*z+c))^\n).m - (f*c).(n+m))
                    by LOPBAN_3:def 6
           .=((h^\n).m)"*(f/.( (h^\n*z+c).m) - f/.(c.(n+m)))
          by K1,Q02,NFCONT_1:8
           .=((h^\n).m)"*(f/.( (h^\n*z+c).m) - f/.x0) by A9
           .=((h^\n).m)"*(f/.( (h^\n*z+c).m) - f/.(c.m)) by A9
           .=((h^\n).m)"*(f/.((h^\n*z+c).m) - (f*c).m) by Q02,NFCONT_1:8
           .=((h^\n).m)"*((f*(h^\n*z+c)).m - (f*c).m) by Q03,NFCONT_1:8
           .=((h^\n).m)"*((f*(h^\n*z+c) - f*c).m) by NORMSP_1:def 6
           .=((h^\n)".m)*(f*(h^\n*z+c) - f*c).m by SEQ_1:def 8
           .=((h^\n)"(#)(f*(h^\n*z+c) - f*c)).m by NDIFF_1:def 2;
     end;
     hence thesis by FUNCT_2:113; 
   end;
   hence thesis by A18,LOPBAN_3:15,16;
end;
   
theorem LM03:
for x0 be Point of S 
for N be Neighbourhood of x0 st N c= dom f 
 for z be Point of S 
 for dv be Point of T holds 
  (
    (for h be convergent_to_0 Real_Sequence
     for c st ( rng c = {x0} & rng (h*z+c) c= N ) holds
      h"(#)(f*(h*z+c) - f*c) is convergent & 
      dv = lim (h"(#)(f*(h*z+c) - f*c)))
    iff
    (for e be Real st e > 0 holds
      ex d be Real st d > 0 & 
       for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e )
  )          
proof
   let x0 be Point of S;
   let N be Neighbourhood of x0 such that
ASN:N c= dom f;
   let z be Point of S;
   let dv be Point of T;
L1:now assume   
AS1: ( for h be convergent_to_0 Real_Sequence
       for c st ( rng c = {x0}  & rng (h*z+c) c=  N ) holds
        h"(#)(f*(h*z+c) - f*c) is convergent & 
        dv = lim (h"(#)(f*(h*z+c) - f*c)));
     assume          
L2:  not ( for r be Real st r > 0 holds
          ex d be Real st d > 0 & 
            for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
              ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < r );
     consider r be Real such that
L3:   r > 0 and
L4:   for d be Real st d > 0 holds 
       ex h be Real st 
        abs(h) < d & h <> 0 &  h*z+x0 in N & 
        not ( ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < r ) by L2;
     defpred P[Nat,real number] means
     ex rr being Real st rr = $2 &
      abs(rr) < (1/($1+1)) &
      rr <> 0 & 
      rr*z+x0 in N &
      not (  ||. rr"*(f/.(rr*z+x0) -  f/.x0) - dv .|| < r );
L5:  for n be Nat ex h be real number st P[n,h] 
     proof
       let n be Nat;
       0 <= n by NAT_1:18; then
       0+0 < 1+ n by REAL_1:67; then
       0 < 1 * (n + 1)" by REAL_1:72; then 
       0 < 1/(n + 1) by XCMPLX_0:def 9; then
       consider h be Real such that
PP:     abs(h) < 1/(n + 1) & 
        h <> 0 & 
        h*z+x0 in N & 
        not ( ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < r  ) by L4;
       reconsider h as real number;
       thus thesis by PP;
     end;
     consider h be Real_Sequence such that 
L6:   for n be Nat holds P[n,h.n] from RCOMP_1:sch 1(L5);
     deffunc F(Nat) = x0;
     consider c be sequence of S such that
C0:   for n holds c.n=F(n) from NORMSP_1:sch 1;
     reconsider c as constant sequence of S by C0,LOPBAN_3:def 5;
C1:  rng c = {x0} 
     proof
       now let x be set;
       assume x in rng c; then
       consider n be Nat such that 
D1:     x=c.n by NFCONT_1:6;
       x=x0 by C0,D1;
       hence x in {x0} by TARSKI:def 1; 
     end; then
L:   rng c c= {x0} by TARSKI:def 3;
     now let x be set;
       assume x in {x0}; then
       x=x0 by TARSKI:def 1; then 
       x=c.1 by C0;
       hence x in rng c by NFCONT_1:6;
     end; then
     {x0} c= rng c by TARSKI:def 3;
     hence rng c = {x0} by XBOOLE_0:def 10,L;
   end;
   x0 in N by NFCONT_1:4; then
C2:rng c c= dom f by C1,ASN,ZFMISC_1:37; 
L7:now
     let p be real number;
     assume 0<p; then 
A12: 0<p" by REAL_1:72;
     consider n such that 
A13:  p"<n by SEQ_4:10;
     p" + 0 < n + 1 by A13,REAL_1:67; then 
     1/(n+1) < 1/p" by A12,SEQ_2:10; then
A14: 1/(n+1) < p by XCMPLX_1:218;
     take n;
     let m be Nat;
     assume n<=m; then 
A15: n + 1 <= m + 1 by AXIOMS:24;
     0  <=  n by NAT_1:18; then
     0+0 < 1+ n by REAL_1:67; then 
A16: 1/(m+1) <= 1/(n+1) by A15,SEQ_4:1;
     P[m,h.m] by L6;
     then abs(h.m-0) <  1/(n+1) by A16,AXIOMS:22;
     hence abs(h.m-0) <p by A14,AXIOMS:22;
   end; then 
L8:h is convergent by SEQ_2:def 6; then 
L9:lim h = 0 by L7,SEQ_2:def 7;
   for n holds h.n <> 0
   proof
     let n;
     P[n,h.n] by L6;
     hence thesis;
   end;
   then h is_not_0 by SEQ_1:7; then
   reconsider h as  convergent_to_0 Real_Sequence by L8,L9,FDIFF_1:def 1;
C3:rng (h*z+c) c= N  
   proof
     now let x be set;
       assume x in rng (h*z+c); then
       consider n be Nat such that 
D1:     x=(h*z+c).n by NFCONT_1:6;
ee:    P[n,h.n] by L6;
       x=(h*z).n+c.n by D1,NORMSP_1:def 5
       .=(h.n)*z+c.n by NDIFF_1:def 3
       .=(h.n)*z+x0 by C0;
       hence x in N by ee;  
     end;
     hence thesis by TARSKI:def 3;
   end; then
C31:rng (h*z+c) c= dom f by ASN,XBOOLE_1:1; 
L11:h"(#)(f*(h*z+c) - f*c) is convergent 
      &  dv = lim (h"(#)(f*(h*z+c) - f*c)) by AS1,C1,C3;
   consider n be Nat such that 
L12:for m be Nat st n <=m holds 
     ||. (h"(#)(f*(h*z+c) - f*c)).m- dv.|| < r by L11,L3,NORMSP_1:def 11;
L13:||. (h"(#)(f*(h*z+c) - f*c)).n- dv.|| < r by L12;
ee:    P[n,h.n] by L6;
   ||. (h"(#)(f*(h*z+c) - f*c)).n- dv.|| 
     = ||.(h".n)*((f*(h*z+c) - f*c).n)- dv.|| by NDIFF_1:def 2
    .= ||.(h.n)"*((f*(h*z+c) - f*c).n)- dv.|| by SEQ_1:def 8
    .= ||.(h.n)"*( (f*(h*z+c)).n - (f*c).n)- dv.|| by NORMSP_1:def 6
    .= ||.(h.n)"*( (f*(h*z+c)).n - f/.(c.n)) - dv.|| by C2,NFCONT_1:8
    .= ||.(h.n)"*( (f*(h*z+c)).n - f/.x0) - dv.|| by C0
    .= ||.(h.n)"*( f/.( (h*z+c).n) - f/.x0) - dv.|| by C31,NFCONT_1:8
    .= ||.(h.n)"*( f/.((h*z).n+c.n) - f/.x0) - dv.|| by NORMSP_1:def 5
    .= ||.(h.n)"*( f/.((h*z).n+x0) - f/.x0) - dv.|| by C0
    .=  ||.(h.n)"*(f/.((h.n)*z+x0) -  f/.x0)-dv .|| by NDIFF_1:def 3;
   hence 
      ( for e be Real st e > 0 holds
        ex d be Real st d > 0 
          & for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
              ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e ) by ee,L13;
 end;
   now assume   
AS1:( for e be Real st e > 0 holds
       ex d be Real st d > 0 
        & for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) 
           holds   ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e );
   now   
     let h be convergent_to_0 Real_Sequence;
     let c such that
A01:  rng c = {x0} and
A02:  rng (h*z+c) c= N;
A021:rng (h*z+c) c= dom  f by A02, ASN,XBOOLE_1:1;  
C0:  for n holds c.n=x0
     proof
       let n be Nat;
       c.n in rng c by NFCONT_1:6;
       hence c.n=x0 by A01,TARSKI:def 1;
     end;
     x0 in N by NFCONT_1:4; then
C2:  rng c c= dom f by A01,ASN,ZFMISC_1:37;
R2:  h is_not_0 by FDIFF_1:def 1;
R3:  h is convergent & lim h = 0 by FDIFF_1:def 1;
R5:  now let r be Real such that 
DS0:   r > 0;
       consider d be Real  such that
DS1:    d > 0 and
DS2:    (for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
         ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < r ) by AS1,DS0;
       consider n be Nat such that
DS3:   for m be Nat st n <=m  holds
         abs(h.m-0) < d by DS1,R3,SEQ_2:def 7;
       take n;
       thus  for m be Nat st n <=m holds
       ||. (h"(#)(f*(h*z+c) - f*c)).m- dv.|| < r
       proof
         let m be Nat;
         assume n <= m; then
DS42:    abs(h.m-0) < d by DS3;
DS5:     h.m <> 0 by R2,SEQ_1:7;
         (h*z+c).m in rng (h*z+c) by NFCONT_1:6; then
         (h*z+c).m in N by A02; then
         (h*z).m+c.m in N by NORMSP_1:def 5; then
         h.m*z + c.m in N by NDIFF_1:def 3; then
DS6:     h.m*z + x0 in N by C0;
         ||. (h"(#)(f*(h*z+c) - f*c)).m- dv.|| 
          = ||.(h".m)*((f*(h*z+c) - f*c).m)- dv.|| by NDIFF_1:def 2
         .= ||.(h.m)"*((f*(h*z+c) - f*c).m)- dv.|| by SEQ_1:def 8
         .= ||.(h.m)"*( (f*(h*z+c)).m - (f*c).m)- dv.|| by NORMSP_1:def 6
         .= ||.(h.m)"*( (f*(h*z+c)).m - f/.(c.m)) - dv.|| by C2,NFCONT_1:8
         .= ||.(h.m)"*( (f*(h*z+c)).m - f/.x0) - dv.|| by C0
         .= ||.(h.m)"*( f/.( (h*z+c).m) - f/.x0) - dv.|| by A021,NFCONT_1:8
         .= ||.(h.m)"*( f/.((h*z).m+c.m) - f/.x0) - dv.|| by NORMSP_1:def 5
         .= ||.(h.m)"*( f/.((h*z).m+x0) - f/.x0) - dv.|| by C0
         .=  ||.(h.m)"*(f/.((h.m)*z+x0) -  f/.x0)-dv .|| by NDIFF_1:def 3;
         hence thesis by DS2,DS42,DS5,DS6;
         end;
       end;
       hence (h"(#)(f*(h*z+c) - f*c)) is convergent by NORMSP_1:def 9;
       hence lim (h"(#)(f*(h*z+c) - f*c)) = dv by R5,NORMSP_1:def 11;
     end;
     hence 
     for h be convergent_to_0 Real_Sequence
     for c st ( rng c = {x0}  & rng (h*z+c) c= N ) holds
       h"(#)(f*(h*z+c) - f*c) is convergent & 
       dv = lim (h"(#)(f*(h*z+c) - f*c));
   end;
   hence thesis by L1;
end;

definition 
let S,T;
let f; 
let x0 be Point of S;
let z be Point of S;
pred f is_Gateaux_differentiable_in x0,z means :Def01:
ex N be Neighbourhood of x0 st N c= dom f &   
 ex dv be Point of T st 
  for e be Real st e > 0 holds
   ex d be Real st d > 0 &
    for h be Real st ( abs(h) < d & h <> 0 & h*z+x0 in N) 
             holds ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e;
end;

theorem LM_etc:
(for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| > 0 iff x <> y) &
(for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| = ||.y-x.||) &
(for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| = 0 iff x = y) &
(for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| <> 0 iff x <> y) &
(for X be RealNormSpace, x,y,z be Point of X, e be Real st e > 0 holds 
  ( (||.x-z.|| <e/2 & ||.z-y.|| <e/2)  implies ||.x-y.|| <e) ) &
(for X be RealNormSpace, x,y,z be Point of X, e be Real st e > 0 holds 
  ( (||.x-z.|| <e/2 & ||.y-z.|| <e/2)  implies ||.x-y.|| <e) ) &
(for X be RealNormSpace, x be Point of X st
  ( for e be Real st e>0 holds  ||.x.|| <e ) holds  x=0.X ) &
(for X be RealNormSpace, x,y be Point of X st
  ( for e be Real st e>0 holds  ||.x-y.|| <e ) holds x=y )  
proof
   thus 
   for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| > 0 iff x <> y
   proof
     let X be RealNormSpace; 
     let x, y be Point of X;
     0 < ||.x-y.|| implies x-y <> 0.X by NORMSP_1:def 2;
     hence 0 < ||.x-y.|| implies x <> y by RLVECT_1:28;
     now
       assume x <> y; then 
       0 <> ||.x-y.|| by NORMSP_1:15;
       hence 0 < ||.x-y.|| by NORMSP_1:8;
     end;
     hence x <> y implies 0 < ||.x-y.||;
   end;
   thus for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| = ||.y-x.||
   proof
     let X be RealNormSpace; 
     let x, y be Point of X;
     thus ||.x-y.|| = ||.-(x-y).|| by NORMSP_1:6
     .= ||.y+(-x).|| by RLVECT_1:47
     .= ||.y-x.|| by RLVECT_1:def 11;
   end;
  L5: for X be RealNormSpace
   for x, y, z be Point of X
   for e be Real st e > 0
   holds
   ((||.x-z.|| < e/2 & ||.z-y.|| < e/2)  implies ||.x-y.|| <e)
  proof
    let X be RealNormSpace;
    let x, y, z be Point of X;
    let e be Real such that e > 0;
    assume ||.x-z.|| < e/2 & ||.z-y.|| < e/2;
      then La: ||.x-z.|| + ||.z-y.|| < e/2 + e/2 by REAL_1:67;
    ||.x-y.|| <= ||.x-z.|| + ||.z-y.|| by NORMSP_1:14;
    then ||.x-y.|| + (||.x-z.|| + ||.z-y.||)
      < (||.x-z.|| + ||.z-y.||) + e by La, REAL_1:67;
    then ||.x-y.|| + (||.x-z.|| + ||.z-y.||) +- (||.x-z.|| + ||.z-y.||)
      < e + (||.x-z.|| + ||.z-y.||) +- (||.x-z.|| + ||.z-y.||)
   by REAL_1:67;
    hence thesis;
  end;
  L6: for X be RealNormSpace 
   for x, y, z be Point of X
   for e be Real st e > 0
   holds
   ((||.x-z.|| < e/2 & ||.y-z.|| < e/2)  implies ||.x-y.|| < e)
  proof
    let X be RealNormSpace; 
    let x, y, z be Point of X;
    let e be Real such that La: e > 0;
    assume Lb: ||.x-z.|| < e/2 & ||.y-z.|| < e/2;
    then ||.-(y-z).|| < e/2 by NORMSP_1:6;
    then ||.z+-y.|| < e/2 by RLVECT_1:47;
    then ||.z-y.|| < e/2 by RLVECT_1:def 11;
    hence thesis by La, Lb, L5;
  end;
  L7: for X be RealNormSpace 
   for x be Point of X
     st (for e be Real st e>0 holds  ||.x.|| <e)
   holds x=0.X
  proof
    let X be RealNormSpace; 
    let x be Point of X such that
    GS1:   for  e be Real st e > 0 holds  ||.x.|| <e;
    now 
      assume x<>0.X; then
      0 <> ||.x.|| by NORMSP_1:def 2; then 
      0 <  ||.x.|| by NORMSP_1:8;
      hence contradiction by GS1;
    end;
    hence thesis;
  end;
  for X be RealNormSpace 
   for x,y  be Point of X
     st   (for e be Real st e>0 holds  ||.x-y.|| <e) 
   holds  x=y 
  proof
    let X be RealNormSpace; 
    let x,y be Point of X;
    assume for e be Real st e > 0 holds  ||.x-y.|| <e; then
    x-y = 0.X by L7;
    hence thesis by RLVECT_1:35;
  end;
  hence  thesis by L5,L6,  L7,NORMSP_1:10;
end;

definition 
let S,T;
let f; 
let x0 be Point of S;
let z be Point of S;
assume 
    AS2: f is_Gateaux_differentiable_in x0,z;
func Gateaux_diff(f,x0,z) -> Point of T means :Def02:
  ex N be Neighbourhood of x0 st N c= dom f &
   for e be Real st e > 0 holds
    ex d be Real st d > 0 & 
     for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N ) holds
      ||. h"*(f/.(h*z+x0) -  f/.x0) - it .|| < e;
existence  
proof
   consider N be Neighbourhood of x0 such that 
A1: N c= dom f and   
A2: ex dv be Point of T st
     for e be Real st e > 0 holds
      ex d be Real st 
       d > 0 & 
       for h be Real st (abs(h)<d & h<>0 & h*z+x0 in N) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e by AS2,Def01;
   thus thesis by A1,A2;
end;
uniqueness
proof
   let dv1,dv2 be Point of T;
   assume that
A5: ex N1 be Neighbourhood of x0 st 
     N1 c= dom f &
     for e be Real st e > 0 holds
      ex d be Real st d > 0 &
       for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N1) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv1 .|| < e 
    and
A6: ex N2 be Neighbourhood of x0 st
     N2 c= dom f &
     for e be Real st e > 0 holds
      ex d be Real st d > 0 &
       for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N2) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv2 .|| < e;
   consider N1 be Neighbourhood of x0 such that  
    N1 c= dom f and
B02:for e be Real st e > 0 holds
     ex d be Real st d > 0 & 
      for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N1) holds
       ||. h"*(f/.(h*z+x0) -  f/.x0) - dv1 .|| < e by A5;
   consider N2 be Neighbourhood of x0 such that
    N2 c= dom f and
C02:for e be Real st e > 0 holds
     ex d be Real st d > 0 &
      for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N2) holds
       ||. h"*(f/.(h*z+x0) -  f/.x0) - dv2 .|| < e by A6;
   now 
     let e be Real such that 
D01:  e > 0;
D02: 0 < e/2 by D01,SEQ_2:3;
     consider d1 be Real such that 
E01:  d1 > 0 and
E02:  for h be Real st ( abs(h) < d1  & h <> 0 & h*z+x0 in N1) holds
       ||. h"*(f/.(h*z+x0) -  f/.x0) - dv1 .|| < e/2 by B02,D02;
     consider d2 be Real such that 
F01:  d2 > 0 and
F02:  for h be Real st ( abs(h) < d2  & h <> 0 & h*z+x0 in N2) holds
       ||. h"*(f/.(h*z+x0) -  f/.x0) - dv2 .|| < e/2 by C02,D02;

E03: ex h be Real st  
      h <> 0 & abs(h) < d1  &  abs(h) < d2  & h*z+x0 in N1 & h*z+x0 in N2
     proof
       consider N0 be Neighbourhood of x0 such that 
Q19:    N0 c= N1 & N0 c= N2 by NDIFF_1:1;
       consider g be Real such that
Q20:    0<g & {y where y is Point of S: ||.y - x0.|| < g} c= N0 
       by NFCONT_1:def 3;
       set d3=min(d1,d2);
Q22:   d3<=d1 & d3 <=d2 by SQUARE_1:35;                
       0 < d3 by E01,F01,SQUARE_1:38; then
Q23:   0 < d3/2 & d3/2 < d3 by SEQ_2:3,4; then
Q25:   d3/2 < d1 & d3/2 < d2 by Q22,AXIOMS:22;
       consider n0 be Nat such that 
AS0:    ||.z.|| /g < n0 by SEQ_4:10;
       0 <= n0 by NAT_1:18; then
P1:    0 < n0+1 by NAT_1:38;
       n0 <= n0+1 by NAT_1:29; then
       ||.z.|| /g < (n0+1) by AXIOMS:22,AS0; then
       ( ||.z.|| /g)*g < (n0+1)*g by Q20,REAL_1:70; then
       ||.z.|| < (n0+1)*g by Q20,XCMPLX_1:88; then
P6:    ||.z.|| /(n0+1) < g by P1,REAL_2:178;
       set n1=n0+1;
       set h = min(1/(n1+1), d3/2);
       take h;
       0+0 <n1+1 by P1,REAL_1:67; then
       0 < 1 * (1 + n1)" by REAL_1:72; then 
P3:    0 < 1/(1 + n1) by XCMPLX_0:def 9; 
       hence h <> 0 by Q23,SQUARE_1:38; 
Q27:   0 < h by P3,Q23,SQUARE_1:38;
       h<=1/(n1+1) & h <=d3/2 by SQUARE_1:35; then
       h < d1 & h < d2 by Q25,AXIOMS:22;
       hence abs(h) < d1 & abs(h) < d2 by ABSVALUE:def 1,Q27;
       0 <= ||.z.|| & n1 <=  1+n1 by NORMSP_1:8,NAT_1:37; then
P4:    ||.z.|| /(1+n1) <= ||.z.|| /n1 by P1,REAL_2:201;
PP4:   ||.h*z+x0-x0.|| = ||.h*z+(x0-x0).|| by RLVECT_1:42  
                      .= ||.h*z+0.S.|| by RLVECT_1:28
                      .= ||.h*z.|| by RLVECT_1:10 
                      .= abs(h)*||.z.|| by NORMSP_1:def 2 
                      .= h* ||.z.|| by Q27,ABSVALUE:def 1;
       ( 1 /(1+n1) )* ||.z.|| = ||.z.|| /(1+n1) by XCMPLX_1:100; then
P40:   ( 1 /(1+n1) )* ||.z.|| < g by AXIOMS:22,P6,P4;
       0 <= ||.z.|| & h <=1/(n1+1) by NORMSP_1:8,SQUARE_1:35; then
       h* ||.z.|| <=(1/(n1+1)) *  ||.z.|| by AXIOMS:25; then
       ||.h*z+x0-x0.|| < g by PP4,AXIOMS:22,P40; then
       (h*z+x0) in {y where y is Point of S: ||.y - x0.|| < g}; then 
       (h*z+x0) in N0 by Q20;
       hence (h*z+x0) in N1 & (h*z+x0) in N2 by Q19;
     end; 
     consider h be Real such that
H1:   abs(h) < d1 & abs(h) < d2 & h <> 0 &
      h*z+x0 in N1 &  h*z+x0 in N2 by E03;
E04: ||. h"*(f/.(h*z+x0) -  f/.x0) - dv1 .|| < e/2 by H1,E02;
E05: ||. h"*(f/.(h*z+x0) -  f/.x0) - dv2 .|| < e/2 by H1,F02;
     ||. dv1- h"*(f/.(h*z+x0) -  f/.x0) .|| < e/2 by E04,LM_etc;
     hence  ||. dv1- dv2 .|| < e by D01,LM_etc,E05;
   end; 
   hence dv1=dv2 by LM_etc;
end;
end;

theorem
for x0 be Point of S, z be Point of S holds
 ( f is_Gateaux_differentiable_in x0,z 
  iff 
   ex N being Neighbourhood of x0 st 
    ( N c= dom f &
      ex dv be Point of T st 
       for h be convergent_to_0 Real_Sequence
       for c st rng c = {x0} & rng (h*z+c) c= N holds
        h"(#)(f*(h*z+c) - f*c) is convergent & 
        dv= lim (h"(#)(f*(h*z+c) - f*c))
    )
 )  
proof
   let x0 be Point of S;
   let z be Point of S;
L: now assume 
     f is_Gateaux_differentiable_in x0,z; then
     consider N being Neighbourhood of x0 such that  
L11:  N c= dom f and
L12:  for e be Real st e > 0 holds
       ex d be Real st 
        d > 0 & 
        for h be Real st (abs(h) < d  & h <> 0 & h*z+x0 in N) holds   
         ||. h"*(f/.(h*z+x0) -  f/.x0) - Gateaux_diff(f,x0,z) .|| < e by Def02;
     for h be convergent_to_0 Real_Sequence, c st
      ( rng c = {x0}  & rng (h*z+c) c= N ) holds
       h"(#)(f*(h*z+c) - f*c) is convergent & 
       Gateaux_diff(f,x0,z) = lim (h"(#)(f*(h*z+c) - f*c)) by L11,L12,LM03;
     hence
     ex N being Neighbourhood of x0 st 
      ( N c= dom f &
        ex dv be Point of T st 
         for h be convergent_to_0 Real_Sequence, c st 
          rng c = {x0} & rng (h*z+c) c= N holds
           h"(#)(f*(h*z+c) - f*c) is convergent &
           dv= lim (h"(#)(f*(h*z+c) - f*c))
      ) by L11;
   end;       
R: now assume 
     ex N being Neighbourhood of x0 st 
      ( N c= dom f &
        ex dv be Point of  T st 
         for h be convergent_to_0 Real_Sequence, c st 
          rng c = {x0} & rng (h*z+c) c= N holds
           h"(#)(f*(h*z+c) - f*c) is convergent &
           dv= lim (h"(#)(f*(h*z+c) - f*c))
      ); then 
     consider N being Neighbourhood of x0 such that  
R11:  N c= dom f and
R12:  ex dv be Point of T st 
       for h be convergent_to_0 Real_Sequence, c st
        rng c = {x0} & rng (h*z+c) c= N holds
         h"(#)(f*(h*z+c) - f*c) is convergent &
         dv= lim (h"(#)(f*(h*z+c) - f*c));
     consider dv be Point of T such that 
R13:  for h be convergent_to_0 Real_Sequence, c st 
       rng c = {x0} & rng (h*z+c) c= N holds
        h"(#)(f*(h*z+c) - f*c) is convergent & 
        dv= lim (h"(#)(f*(h*z+c) - f*c)) by R12;
     for e be Real st e > 0 holds
      ex d be Real st 
       d > 0 & 
       for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds   
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e by LM03,R13,R11;          
     hence f is_Gateaux_differentiable_in x0,z by Def01,R11;
   end;
   thus thesis by L,R;
end;

theorem
for x0 be Point of S st f is_differentiable_in x0
for z be Point of S holds
 ( f is_Gateaux_differentiable_in x0,z &
   Gateaux_diff(f,x0,z)=diff(f,x0).z & 
   ex N being Neighbourhood of x0 st 
    ( N c= dom f &
      for h be convergent_to_0 Real_Sequence, c st 
       rng c = {x0} & rng (h*z+c) c= N holds
        h"(#)(f*(h*z+c) - f*c) is convergent &
        Gateaux_diff(f,x0,z)= lim (h"(#)(f*(h*z+c) - f*c))) 
 )
proof
   let x0 be Point of S such that
AS0:f is_differentiable_in x0;
   let z be Point of S;
   consider  N being Neighbourhood of x0 such that   
AS1:N c= dom f and
AS2:for z be Point of S holds 
     for h be convergent_to_0 Real_Sequence, c st 
      rng c = {x0} & rng (h*z+c) c= N holds
       h"(#)(f*(h*z+c) - f*c) is convergent &
       diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c)) by LM01,AS0;
AS3:
   for h be convergent_to_0 Real_Sequence, c st
    rng c = {x0} & rng (h*z+c) c= N holds
     h"(#)(f*(h*z+c) - f*c) is convergent &
     diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c)) by AS2; 
P1:for e be Real st e > 0 holds
    ex d be Real st
     d > 0 &
     for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
      ||. h"*(f/.(h*z+x0) -  f/.x0) - diff(f,x0).z .|| < e by AS1,AS3,LM03;
   hence f is_Gateaux_differentiable_in x0,z by AS1,Def01;
   hence Gateaux_diff(f,x0,z)=diff(f,x0).z by P1,AS1,Def02;
   hence thesis by AS1,AS3;
end;

reserve U for non trivial RealNormSpace;
reserve f1 for PartFunc of S,T;
reserve f2 for PartFunc of T,U;

theorem LM04: 
for R be REST of S,T st R/.0.S=0.T  
 for e be Real st e > 0 
  ex d be Real st   
   d > 0 & 
   for h be Point of S st ||.h.|| < d holds  ||.R/.h.|| <= e*||.h.||
proof
   let R be REST of S,T such that 
AS1:R/.0.S=0.T;
   let e be Real such that 
AS2:e > 0;
   R is total by NDIFF_1:def 5; then
   consider d be Real such that 
AS3:d > 0  and 
AS4:(for z be Point of S st z <> 0.S & ||.z.|| < d holds 
      ( ||.z.||"* ||. R/.z .||) < e ) by AS2,NDIFF_1:26;
   take d;
   now 
     let h be Point of S such that 
AS5:  ||.h.|| < d;
     now per cases;
       case  
CAS1:    h  <> 0.S;
R5:      ||.h.|| <> 0 by CAS1,NORMSP_1:def 2;  
R6:      0 <= ||.h.|| by NORMSP_1:8; 
         ||.h.||"*||. R/.h .|| <= e by CAS1,AS4,AS5; then
         ||.h.||*(||.h.||"*||. R/.h .||) <=  ||.h.||*e by R6,AXIOMS:25; then
         ||.h.||*||.h.||"*||. R/.h .|| <=   e* ||.h.||; then
         1*||. R/.h .|| <=   e* ||.h.|| by R5,XCMPLX_0:def 7;
         hence ||. R/.h .|| <= e* ||.h.||;
       end;
       case
CAS2:    h  = 0.S;
R6:      0 <= ||.h.|| by NORMSP_1:8; 
         0* ||.h.|| <= e* ||.h.|| by R6,AS2,AXIOMS:25;
         hence ||. R/.h .|| <= e* ||.h.|| by AS1,CAS2,NORMSP_1:def 2; 
       end;
     end;
     hence   ||. R/.h .|| <= e* ||.h.||;
   end;
   hence thesis by AS3; 
end;

theorem 
 for R be REST of T,U st R/.0.T=0.U 
 for L be bounded LinearOperator of S,T holds 
  R*L is REST of S,U
proof
   let R be REST of T,U such that 
AS1:R/.0.T=0.U;
   let L be bounded LinearOperator of S,T;
   consider K be Real such that
P1: 0 <= K and
P2: for h be Point of S holds
     ||.L.h.|| <= K * ||.h.|| by LOPBAN_1:def 9;
Q1:0 + K  < 1 + K by REAL_1:67;
ML1:dom L = the carrier of S by FUNCT_2:def 1;
   R is total by NDIFF_1:def 5; then
   dom R = the carrier of T by PARTFUN1:def 4; then
   MLR: rng L c= dom R; then
   dom(R*L) = dom L by RELAT_1:46
           .= the carrier of S by FUNCT_2:def 1; then
ML3:R*L is total by PARTFUN1:def 4;
   now 
     let e be Real such that 
AS3:  e > 0;
     set e1=e/(1 + K);
P3:  0/(1 + K) < e/(1 + K) by AS3,Q1,P1,REAL_1:73;
     consider d be Real such that 
P4:   0 < d and
P5:  for h be Point of  T st ||.h.|| < d holds
      ||.R/.h.|| <= e1*||.h.|| by LM04,P3,AS1;
     set d1=d/( 1 +  K );
P6:  0/(1 + K) < d/(1 + K) by P4,Q1,P1,REAL_1:73;
     now 
       let h be Point of S such that 
P7:     h <> 0.S  and
P8:    ||.h.|| < d1;
KKK:   R/.(L.h) = R/.(L/.h) by FINSEQ_4:def 4,ML1
               .=(R*L)/.h by PARTFUN2:10,MLR,ML1;
P10:   ||.h.||  <> 0 by P7,NORMSP_1:def 2; then
P12:   ||.h.|| > 0 by NORMSP_1:8; then
S9:    ||.h.||" > 0 by REAL_1:72;
S11:   ||.L.h.|| <= K * ||.h.|| by P2;
       K * ||.h.||  < ( K +1) * ||.h.|| by Q1,P12,REAL_1:70; then
P11:   ||.L.h.|| < ( K +1) * ||.h.|| by S11,AXIOMS:22; 
       ( K +1) * ||.h.|| <   ( K +1) *d1 by P8,Q1,P1,REAL_1:70; then
       ||.L.h.|| < ( K +1) * d1 by P11,AXIOMS:22; then
       ||.L.h.|| < d by Q1,P1,XCMPLX_1:88; then
S12:   ||.R/.(L.h).|| <= e1*||.L.h.|| by P5;
       e1*||.L.h.||  <  e1* ((K +1) * ||.h.||) by P11,REAL_1:70,P3; then
       ||.R/.(L.h).|| < e1* ((K +1) * ||.h.||) by S12,AXIOMS:22; then
       ||.h.||"* ||.(R*L)/.h.|| <  ||.h.||"* (e1* ( K +1) * ||.h.||) 
        by S9,KKK,REAL_1:70; then
       ||.h.||"* ||.(R*L)/.h.|| < ||.h.||*||.h.||"*e1* ( K +1);  then
       ||.h.||"* ||.(R*L)/.h.|| < 1*e1* ( K +1) by P10,XCMPLX_0:def 7;
       hence ||.h.||"* ||.(R*L)/.h.|| < e by Q1,P1,XCMPLX_1:88;
     end;      
     hence ex  d1 be Real st 
            d1 > 0 &  
            for h be Point of S st h <> 0.S  &  ||.h.|| < d1 holds
             ||.h.||"* ||.(R*L)/.h.|| < e by P6; 
   end;
   hence thesis by ML3,NDIFF_1:26;   
end;
 
theorem LM06:
 for  R be REST of S,T
 for  L be bounded LinearOperator of T,U holds 
  L*R is REST of S,U
proof
   let  R be REST of S,T;
   let   L be bounded LinearOperator of T,U;
   consider  K be Real such that 
P1: 0 <= K and
P2: for z be Point of T holds ||.L.z.|| <= K * ||.z.|| by LOPBAN_1:def 9;
Q1:0 + K  < 1 + K by REAL_1:67; then
Q11:0 <  1 +  K by P1;
ML1:dom L = the carrier of T by FUNCT_2:def 1;
ML2:R is total by NDIFF_1:def 5; then
ML2':dom R = the carrier of S by PARTFUN1:def 4; 
MLR:rng R c= dom L by ML1; then
   dom(L*R) = dom R by RELAT_1:46
           .= the carrier of S by ML2,PARTFUN1:def 4; then
ML3:L*R is total by PARTFUN1:def 4;
   now 
     let ee be Real such that 
AS30: ee > 0;
     set e=ee/2;
AS3: e > 0 by AS30,SEQ_2:3;
AS31:e < ee by AS30,SEQ_2:4;
     set e1=e/( 1 +  K );
P3:  0/(1 + K) < e/(1 + K) by AS3,Q1,P1,REAL_1:73;
     R is total by NDIFF_1:def 5; then
     consider d be Real such that 
P4:   0 <  d  and
P5:   for h be Point of  S st h <> 0.S &  ||.h.|| < d holds
        ||.h.||"* ||.R/.h.|| < e1 by NDIFF_1:26,P3;
     now 
       let h be Point of S such that 
P7:     h <> 0.S  and
P8:     ||.h.||   < d;
KKK:   L.(R/.h) = L/.(R/.h) by FINSEQ_4:def 4,ML1
               .=(L*R)/.h by PARTFUN2:10,MLR,ML2';
       ||.h.||  <> 0 by P7,NORMSP_1:def 2; then
       ||.h.|| > 0 by NORMSP_1:8; then
S9:    ||.h.||" > 0 by REAL_1:72;
S11:   ||.L.(R/.h).|| <= K * ||.R/.h.|| by P2;
       0 <=  ||.R/.h.|| by NORMSP_1:8; then
       K * ||.R/.h.||  <= ( K +1) * ||.R/.h.|| by Q1,AXIOMS:25; then
       ||.L.(R/.h).|| <= ( K +1) * ||.R/.h.|| by S11,AXIOMS:22; then   
P12:   ||.h.||"* ||.L.(R/.h).|| <=   ||.h.||"*(( K +1) * ||.R/.h.|| ) 
      by S9,AXIOMS:25;
       ||.h.||"* ||.(R/.h).||   <  e1 by P5,P7,P8; then
       ( K +1) *( ||.h.||"* ||.R/.h.||) <=( K +1) *e1 by Q11,AXIOMS:25; then
       ( K +1) *( ||.h.||"* ||.R/.h.||) <=e by Q1,P1,XCMPLX_1:88; then
       ||.h.||"* ||.L.(R/.h).|| <= e by P12,AXIOMS:22;
       hence ||.h.||"* ||.(L*R)/.h.|| < ee by AS31,KKK,AXIOMS:22;
     end;
     hence
         ex d be Real st 
          d > 0 &
          for h be Point of S st h <> 0.S  &  ||.h.|| < d holds
           ||.h.||"* ||.(L*R)/.h.|| < ee by P4; 
   end;
   hence thesis by ML3,NDIFF_1:26;   
end;
 
theorem
for R1 be REST of S,T st R1/.0.S=0.T
for R2 be REST of T,U st R2/.0.T=0.U holds
 R2*R1 is REST of S,U
proof
   let R1 be REST of S,T such that 
AS0:R1/.0.S=0.T;
   let R2 be REST of T,U such that 
AS1:R2/.0.T=0.U;
M1:R1 is total by NDIFF_1:def 5; then
ML1:dom R1 = the carrier of S by PARTFUN1:def 4;
   R2 is total by NDIFF_1:def 5; then
ML2:dom R2 = the carrier of T by PARTFUN1:def 4; 
MLR:rng R1 c= dom R2 by ML2; then
   dom(R2*R1) = dom R1 by RELAT_1:46
             .= the carrier of S by M1,PARTFUN1:def 4; then
ML3:R2*R1 is total by PARTFUN1:def 4;
   now 
     let e0 be Real such that 
AS20: e0 > 0;
     set e=e0/2;
AS2: e > 0 by AS20,SEQ_2:3;
AS3: e < e0 by AS20,SEQ_2:4;
     consider d2 be Real such that 
P1:   0 <  d2  and
P2:   for z be Point of  T st ||.z.|| < d2 holds
        ||.R2/.z.|| <= e*||.z.|| by LM04,AS1,AS2;
     consider d1 be Real such that 
P3:   0 <  d1  and
P4:   for h be Point of  S st ||.h.|| < d1 holds
       ||.R1/.h.|| <=1* ||.h.|| by LM04,AS0;
     set d=min(d1,d2);
P5:  d <=d1 & d <=d2 by SQUARE_1:35; 
P6:  0 < d by P1,P3,SQUARE_1:38;
     now 
       let h be Point of  S such that 
P7:     h <> 0.S  and
P8:     ||.h.|| < d;
KKK:   R2/.(R1/.h) =(R2*R1)/.h by PARTFUN2:10,MLR,ML1;
       ||.h.|| < d1 by P5,P8,AXIOMS:22; then
P10:   ||.R1/.h.|| <=1* ||.h.|| by P4; then
       ||.R1/.h.|| < d by P8,AXIOMS:22; then
       ||.R1/.h.|| < d2 by P5,AXIOMS:22; then
P13:   ||.R2/.(R1/.h).|| <= e*||.R1/.h.|| by P2;
       e*||.R1/.h.|| <= e* ||.h.|| by P10,AS2,AXIOMS:25; then
P14:   ||.R2/.(R1/.h).|| <= e*||.h.|| by AXIOMS:22,P13;
P15:   ||.h.||  <> 0 by P7,NORMSP_1:def 2; then
       ||.h.|| > 0 by NORMSP_1:8; then
       ||.h.||" > 0 by REAL_1:72; then
       ||.h.||"* ||.R2/.(R1/.h).|| <=  ||.h.||"*(e*||.h.||) 
      by AXIOMS:25,P14; then
       ||.h.||"* ||.R2/.(R1/.h).|| <=  ||.h.||"*||.h.||*e; then
       ||.h.||"* ||.R2/.(R1/.h).|| <= 1*e by P15,XCMPLX_0:def 7;
       hence 
       ||.h.||"* ||.(R2*R1)/.h.|| < e0 by KKK,AXIOMS:22,AS3;
     end;
     hence 
     ex d be Real st
      d > 0 & 
      for h be Point of S st h <> 0.S & ||.h.|| < d holds
       ||.h.||"* ||.(R2*R1)/.h.|| < e0 by P6;
   end;
   hence thesis by ML3,NDIFF_1:26;   
end;
  
theorem LM08: 
for R1 be REST of S,T st R1/.0.S=0.T 
for R2 be REST of T,U st R2/.0.T=0.U 
for L be bounded LinearOperator of S,T holds 
 R2*(L+R1) is REST of S,U
proof
   let R1 be REST of S,T such that 
AS0:R1/.0.S=0.T;
   let R2 be REST of T,U such that 
AS1:R2/.0.T=0.U;
   let L be bounded LinearOperator of S,T;
   consider  K be Real such that 
P1: 0 <= K and
P2: for h be Point of S holds
     ||.L.h.|| <= K * ||.h.|| by LOPBAN_1:def 9;
   consider d0 be Real such that 
PR3:0 <  d0  and
PR4:for h be Point of  S st ||.h.|| < d0 holds
     ||.R1/.h.|| <=1* ||.h.|| by LM04,AS0;
Q1:0 + K  < 1 + K by REAL_1:67;
W1:R1 is total by NDIFF_1:def 5;
   R2 is total by NDIFF_1:def 5; then
ML1:dom R2 = the carrier of T by PARTFUN1:def 4;
ML2:dom L  = the carrier of S by FUNCT_2:def 1; then
W3:L is total by PARTFUN1:def 4;
W4:L+R1 is total by W1,W3,VFUNCT_1:38; then
ML3:dom(L+R1)=the carrier of S by PARTFUN1:def 4;
ML4:rng (L+R1) c= dom R2 by ML1;
   dom (R2*(L+R1)) = dom(L+R1) by ML4,RELAT_1:46
                  .=the carrier of S by W4,PARTFUN1:def 4; then  
ML6:R2*(L+R1) is total by PARTFUN1:def 4;
   now 
     let ee be Real such that 
AS30: ee > 0;
     set e=ee/2;
AS3: e > 0 by AS30,SEQ_2:3;
AS31:e < ee by AS30,SEQ_2:4;
     set e1=e/( 1 +  K );
P3:  0/(1 + K) < e/(1 + K) by AS3,Q1,P1,REAL_1:73;
     consider d be Real such that 
P4:   0 <  d  and
P5:   for z be Point of  T st ||.z.|| < d holds
       ||.R2/.z.|| <= e1*||.z.|| by LM04,P3,AS1;
     set d1=d/( 1 +  K );
P60: 0/(1 + K) < d/(1 + K) by P4,Q1,P1,REAL_1:73;
     set dd1=min(d0,d1);
P61: dd1 <=d0 & dd1 <=d1 by SQUARE_1:35; 
P6:  0 < dd1 by PR3,P60,SQUARE_1:38;
     now 
       let h be Point of S such that 
P7:     h <> 0.S and
P80:    ||.h.|| < dd1;
P8:    ||.h.|| < d1 by P80,P61,AXIOMS:22;
KKK:   R2/.(L.h+R1/.h) = R2/.(L/.h+R1/.h) by FINSEQ_4:def 4,ML2
                       .=R2/.((L+R1)/.h) by ML3,VFUNCT_1:def 1
                       .=(R2*(L+R1))/.h by PARTFUN2:10,ML4,ML3;
P10:   ||.h.||  <> 0 by P7,NORMSP_1:def 2; then
       ||.h.|| > 0 by NORMSP_1:8; then
S9:    ||.h.||" > 0 by REAL_1:72;
S11:   ||.L.h.|| <= K * ||.h.|| by P2;
       ||.h.|| < d0 by P80,P61,AXIOMS:22; then  
S11R:  ||.R1/.h.|| <=1* ||.h.|| by PR4;
S12R:  ||.L.h+R1/.h.|| <=  ||.L.h.|| +  ||.R1/.h.|| by NORMSP_1:def 2;
       ||.L.h.|| +  ||.R1/.h.||  <= K * ||.h.||  + 1* ||.h.||  
        by REAL_1:55,S11,S11R; then
P11:   ||.L.h+R1/.h.|| <= ( K +1) * ||.h.|| by AXIOMS:22,S12R;
       ( K +1) * ||.h.|| <   ( K +1) *d1 by P8,Q1,P1,REAL_1:70; then
       ||.L.h+R1/.h.|| < ( K +1) * d1 by P11,AXIOMS:22; then
       ||.L.h+R1/.h.|| < d by Q1,P1,XCMPLX_1:88; then
S12:   ||.R2/.(L.h+R1/.h).|| <= e1*||.L.h+R1/.h.|| by P5;
       e1*||.L.h+R1/.h.|| <= e1* ((K +1) * ||.h.||) by P11,AXIOMS:25,P3; then
       ||.R2/.(L.h+R1/.h).|| <= e1* ((K +1) * ||.h.||) by S12,AXIOMS:22; then
       ||.h.||"* ||.(R2*(L+R1))/.h.|| <=  ||.h.||"* (e1* ( K +1) * ||.h.||) 
             by S9,KKK,AXIOMS:25; then
       ||.h.||"* ||.(R2*(L+R1))/.h.|| <=  ||.h.||*||.h.||"*e1* ( K +1); then
       ||.h.||"* ||.(R2*(L+R1))/.h.|| <=  1*e1* ( K +1) 
             by P10,XCMPLX_0:def 7; then
       ||.h.||"* ||.(R2*(L+R1))/.h.|| <= e by Q1,P1,XCMPLX_1:88;
       hence ||.h.||"* ||.(R2*(L+R1))/.h.|| < ee by AS31,AXIOMS:22;
     end;      
     hence 
     ex dd1 be Real st 
      dd1 > 0 &
      for h be Point of S st h <> 0.S & ||.h.|| < dd1 holds
       ||.h.||"* ||.(R2*(L+R1))/.h.|| < ee by P6; 
   end;
   hence thesis by ML6,NDIFF_1:26;   
end;

theorem LM09: 
for R1 be REST of S,T st R1/.0.S=0.T 
for R2 be REST of T,U st R2/.0.T=0.U 
for L1 be bounded LinearOperator of S,T
for L2 be bounded LinearOperator of T,U holds 
  L2*R1+ R2*(L1+R1) is REST of S,U
proof
   let R1 be REST of S,T such that 
AS0:R1/.0.S=0.T;
   let R2 be REST of T,U such that 
AS1:R2/.0.T=0.U;
   let L1 be bounded LinearOperator of S,T;
   let L2 be bounded LinearOperator of T,U;
P1:L2*R1 is REST of S,U by LM06;
   R2*(L1+R1)  is REST of S,U by AS0,AS1,LM08;
   hence thesis by P1,NDIFF_1:33;
end;  
 
theorem
for f1 be PartFunc of S,T st f1 is_differentiable_in x0
for f2 be PartFunc of T,U st f2 is_differentiable_in (f1/.x0) holds
 f2*f1 is_differentiable_in x0 &
 diff(f2*f1,x0) = diff(f2,f1/.x0)*diff(f1,x0)
proof 
   let  f1 be PartFunc of S,T such that
AS0:f1 is_differentiable_in x0;
   let  f2 be PartFunc of T,U such that
AS1:f2 is_differentiable_in f1/.x0;
   consider  N1 being Neighbourhood of x0 such that 
P11:N1 c= dom f1 and
P12:ex R1 be REST of S,T st
      R1/.0.S=0.T &
      R1 is_continuous_in 0.S &
      for x be Point of S st x in N1 holds 
        f1/.x-f1/.x0 = diff(f1,x0).(x-x0) + R1/.(x-x0) by NDIFF_1:52,AS0;
   consider  N2 being Neighbourhood of f1/.x0 such that 
P21:N2 c= dom f2 and
P22:ex R2 be REST of T,U st
      R2/.0.T=0.U &
      R2 is_continuous_in 0.T &
      for y be Point of T st y in N2 holds
        f2/.y-f2/.(f1/.x0) = diff(f2,f1/.x0).(y-f1/.x0) + R2/.(y-f1/.x0) 
       by NDIFF_1:52,AS1;
   consider R1 be REST of S,T such that 
P13:R1/.0.S=0.T &
    for x be Point of S st x in N1 holds
      f1/.x-f1/.x0 = diff(f1,x0).(x-x0) + R1/.(x-x0) by P12;
   consider R2 be REST of T,U such that
P23:R2/.0.T=0.U &
    for y be Point of T st y in N2 holds
      f2/.y-f2/.(f1/.x0) = diff(f2,f1/.x0).(y-f1/.x0) + R2/.(y-f1/.x0) by P22;
   f1 is_continuous_in x0 by AS0,NDIFF_1:49; then 
   consider N3 be Neighbourhood of x0 such that
A4: f1.:N3 c= N2 by NFCONT_1:17;
   consider N be Neighbourhood of x0 such that
A5: N c= N1 & N c= N3 by NDIFF_1:1;
A6:N c= dom (f2*f1)
   proof
     let x be set; 
     assume
A61:  x in N; then 
A7:  x in N1 by A5;
     reconsider x' = x as Point of S by A61;
     f1.x' in f1.:N3 by P11,A5,A61,A7,FUNCT_1:def 12; then
     f1.x' in N2 by A4; 
     hence thesis by P11,P21,A7,FUNCT_1:21;
   end;
R1:R_NormSpace_of_BoundedLinearOperators(S,T) 
      = NORMSTR (# BoundedLinearOperators(S,T),
         Zero_(BoundedLinearOperators(S,T),
         R_VectorSpace_of_LinearOperators(S,T)),
         Add_(BoundedLinearOperators(S,T),
         R_VectorSpace_of_LinearOperators(S,T)),
          Mult_(BoundedLinearOperators(S,T),
          R_VectorSpace_of_LinearOperators(S,T)),
          BoundedLinearOperatorsNorm(S,T) #) 
      by LOPBAN_1:def 15;
   reconsider 
   LB1 = diff(f1,x0) as Element of BoundedLinearOperators(S,T) by R1;
R3:LB1=modetrans(LB1,S,T) by LOPBAN_1:def 12;
Q1:R_NormSpace_of_BoundedLinearOperators(T,U) 
       = NORMSTR (# BoundedLinearOperators(T,U),
         Zero_(BoundedLinearOperators(T,U),
         R_VectorSpace_of_LinearOperators(T,U)),
         Add_(BoundedLinearOperators(T,U),
         R_VectorSpace_of_LinearOperators(T,U)),
          Mult_(BoundedLinearOperators(T,U),
          R_VectorSpace_of_LinearOperators(T,U)),
          BoundedLinearOperatorsNorm(T,U) #) 
      by LOPBAN_1:def 15;
   reconsider 
   LB2 = diff(f2,f1/.x0) as Element of BoundedLinearOperators(T,U) by Q1;
Q3:LB2=modetrans(LB2,T,U) by LOPBAN_1:def 12;
   set L1=modetrans(diff(f1,x0),S,T);
   set L2=modetrans(diff(f2,f1/.x0),T,U);
   reconsider R0=L2*R1+R2*(L1+R1) as REST of S,U by LM09,P13,P23;
G1:now 
     let x be Point of S such that 
B1:   x in N;
B2:  x in N1 by B1,A5;
B3:  f1/.x-f1/.x0 = diff(f1,x0).(x-x0) + R1/.(x-x0) by B1,A5,P13;
B4:  x0 in N by NFCONT_1:4;
     x in N1 by A5,B1; then
     f1.x in f1.:N3 by P11,A5,B1,FUNCT_1:def 12; then
     f1.x in N2 by A4; then
B5:  f1/.x in N2 by FINSEQ_4:def 4,B2,P11;
W1:  R1 is total by NDIFF_1:def 5; then
ML0: dom R1 = the carrier of S by PARTFUN1:def 4;
     R2 is total by NDIFF_1:def 5; then
ML1: dom R2 = the carrier of T by PARTFUN1:def 4;
ML2: dom L1  = the carrier of S by FUNCT_2:def 1; then
W3:  L1 is total by PARTFUN1:def 4;
W4:  L1+R1 is total by W1,W3,VFUNCT_1:38; then
ML3: dom(L1+R1)=the carrier of S by PARTFUN1:def 4;
ML4: rng (L1+R1) c= dom R2 by ML1;
ML5: dom (R2*(L1+R1)) = dom(L1+R1) by ML4,RELAT_1:46
                     .=the carrier of S by W4,PARTFUN1:def 4;
KK1: R2/.(L1.(x-x0)+R1/.(x-x0)) 
       = R2/.(L1/.(x-x0)+R1/.(x-x0)) by FINSEQ_4:def 4,ML2
      .=R2/.((L1+R1)/.(x-x0)) by ML3,VFUNCT_1:def 1
      .=(R2*(L1+R1))/.(x-x0) by PARTFUN2:10,ML4,ML3;
NL1: dom L2 = the carrier of T by FUNCT_2:def 1;
NL2: rng R1 c= dom L2 by NL1; then
NL3: dom(L2*R1) = dom R1 by RELAT_1:46
               .= the carrier of S by W1,PARTFUN1:def 4;
KK2: L2.(R1/.(x-x0)) = L2/.(R1/.(x-x0)) by FINSEQ_4:def 4,NL1
                    .=(L2*R1)/.(x-x0) by PARTFUN2:10,NL2,ML0;
KK3: dom (L2*R1+R2*(L1+R1)) 
        =  (the carrier of S) /\ (the carrier of S) by ML5,NL3,VFUNCT_1:def 1
        .= the carrier of S;
KK4: L2.(L1.(x-x0)) = (L2*L1).(x-x0) by ML2,FUNCT_1:23
                   .=(diff(f2,f1/.x0)*diff(f1,x0)).(x-x0) by Def001;
     thus      
     (f2*f1)/.x-(f2*f1)/.x0
       =f2/.(f1/.x) -(f2*f1)/.x0 by PARTFUN2:6,B1,A6
      .=f2/.(f1/.x) -f2/.(f1/.x0) by PARTFUN2:6,B4,A6
      .=diff(f2,f1/.x0).(f1/.x-f1/.x0)  + R2/.(f1/.x-f1/.x0) by P23,B5
      .=L2.(L1.(x-x0)) + L2.(R1/.(x-x0))
          + (R2*(L1+R1))/.(x-x0) by KK1,LOPBAN_1:def 5,R3,Q3,B3
      .=L2.(L1.(x-x0)) + ((L2*R1)/.(x-x0)
          + (R2*(L1+R1))/.(x-x0)) by RLVECT_1:def 6,KK2
      .=(diff(f2,f1/.x0)*diff(f1,x0)).(x-x0) + R0/.(x-x0)
        by KK4,KK3,VFUNCT_1:def 1;
   end;
   hence f2*f1 is_differentiable_in x0 by A6,NDIFF_1:def 6;
   hence
    diff(f2*f1,x0) = diff(f2,f1/.x0)*diff(f1,x0) by G1,A6,NDIFF_1:def 7;
end;
