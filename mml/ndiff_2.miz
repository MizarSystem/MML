:: Differentiable Functions on Normed Linear Spaces. {P}art {II}
::  by Hiroshi Imura , Yuji Sakai and Yasunari Shidama
::
:: Received June 4, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, FCONT_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, LATTICES, SQUARE_1, ORDINAL2, SEQ_1, SEQ_2, SEQM_3,
      RCOMP_1, BOOLE, REALSET1, FINSEQ_4, PARTFUN1, NORMSP_1, RSSPACE,
      LOPBAN_1, FDIFF_1, NDIFF_2, FUNCOP_1;
 notations TARSKI, XBOOLE_0, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, PRE_TOPC,
      STRUCT_0, XCMPLX_0, XREAL_0, ORDINAL1, NUMBERS, REAL_1, NAT_1, FINSEQ_4,
      SQUARE_1, COMPLEX1, FUNCOP_1,
      RLVECT_1, SEQ_1, SEQ_2, SEQM_3, REALSET2, VFUNCT_1,
      NORMSP_1, RSSPACE, LOPBAN_1, LOPBAN_3, NFCONT_1, FDIFF_1, NDIFF_1;
 constructors REAL_1, BINARITH, SQUARE_1, MEMBERED, SEQ_2, PREPOWER, PARTFUN1,
      FINSEQ_4, VFUNCT_1, RSSPACE3, LOPBAN_3, NFCONT_1, FDIFF_1, COMPLEX1,
      NDIFF_1, NAT_1, FUNCOP_1;
 registrations RELSET_1, STRUCT_0, ORDINAL2, XREAL_0, MEMBERED, SEQ_1,
      LOPBAN_1, LOPBAN_2, FDIFF_1, NDIFF_1, SEQM_3, FUNCOP_1;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions TARSKI;
 theorems AXIOMS, TARSKI, ABSVALUE, XBOOLE_0, XBOOLE_1, REAL_1, REAL_2,
      RLVECT_1, XCMPLX_0, XCMPLX_1, ZFMISC_1, SEQ_1, SEQ_2, NAT_1, SEQM_3,
      SQUARE_1, RELAT_1, FUNCT_1, FINSEQ_4, VFUNCT_1, FUNCT_2, SEQ_4, NORMSP_1,
      LOPBAN_1, LOPBAN_2, LOPBAN_3, PARTFUN1, PARTFUN2, NFCONT_1, FDIFF_1,
      NDIFF_1, FUNCOP_1;
 schemes RCOMP_1, FUNCT_2;

begin :: Differentiable Functions on Normed Linear Spaces

 reserve n,k for Nat;
 reserve x,y,X,X1,Z,Z1 for set;
 reserve s,g,r,p for Real;
 reserve S,T for non trivial RealNormSpace;
 reserve f,f1,f2 for PartFunc of S,T;
 reserve rseq,rseq1,rseq2 for Real_Sequence;
 reserve s1,s2,q1,seq,seq1,seq2 for sequence of S;
 reserve x0,x1,x2 for Point of S;
 reserve Y for Subset of  S;
 reserve f,f1,f2 for PartFunc of S,T;
 reserve s1,s2,q1,seq,seq1,seq2 for sequence of S;
 reserve h for convergent_to_0 sequence of S;
 reserve c for constant sequence of S;
 reserve R,R1,R2 for REST of S,T;
 reserve L,L1,L2 for Point of R_NormSpace_of_BoundedLinearOperators(S,T);

definition
  let X,Y,Z be RealNormSpace;
  let f be Element of BoundedLinearOperators(X,Y);
  let g be Element of BoundedLinearOperators(Y,Z);
  func g*f -> Element of BoundedLinearOperators(X,Z) equals
  modetrans(g,Y,Z)*modetrans(f,X,Y);
  correctness
  proof
  modetrans(g,Y,Z)*modetrans(f,X,Y)
    is bounded LinearOperator of X,Z by LOPBAN_2:2;
  hence thesis by LOPBAN_1:def 10;
  end;
end;

definition
  let X,Y,Z be RealNormSpace;
  let f be Point of R_NormSpace_of_BoundedLinearOperators(X,Y);
  let g be Point of R_NormSpace_of_BoundedLinearOperators(Y,Z);
  func g*f -> Point of R_NormSpace_of_BoundedLinearOperators(X,Z)
   equals :Def2:
    modetrans(g,Y,Z)*modetrans(f,X,Y);
  correctness
  proof
  A1:
  R_NormSpace_of_BoundedLinearOperators(X,Z) =
  NORMSTR (# BoundedLinearOperators(X,Z),
          Zero_(BoundedLinearOperators(X,Z),
          R_VectorSpace_of_LinearOperators(X,Z)),
           Add_(BoundedLinearOperators(X,Z),
           R_VectorSpace_of_LinearOperators(X,Z)),
            Mult_(BoundedLinearOperators(X,Z),
            R_VectorSpace_of_LinearOperators(X,Z)),
          BoundedLinearOperatorsNorm(X,Z) #)
       by LOPBAN_1:def 15;
    modetrans(g,Y,Z)*modetrans(f,X,Y)
    is bounded LinearOperator of X,Z
 by LOPBAN_2:2;
  hence thesis by A1,LOPBAN_1:def 10;
  end;
end;

theorem Th1:
for x0 be Point of S st f is_differentiable_in x0
 ex N being Neighbourhood of x0 st
  (
    N c= dom f &
    for z be Point of S holds
     for h be convergent_to_0 Real_Sequence
     for c st rng c = {x0} & rng (h*z+c) c= N holds
      h"(#)(f*(h*z+c) - f*c) is convergent &
      diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c))
  )
proof
   let x0 be Point of S such that
A1:f is_differentiable_in x0;
   consider N be Neighbourhood of x0 such that
A2:N c= dom f and
A3:ex R st
     R/.0.S=0.T &
     R is_continuous_in 0.S &
     for x be Point of S st x in N holds
      f/.x-f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0) by A1,NDIFF_1:52;
   consider R such that
A4:R/.0.S=0.T and
    R is_continuous_in 0.S and
A5:for x be Point of S st x in N holds
     f/.x - f/.x0 = diff(f,x0).(x-x0) + R/.(x-x0) by A3;
   now
     let z be Point of S;
     let h be convergent_to_0 Real_Sequence;
     let c such that
A6:  rng c = {x0} and
A7:  rng (h*z+c) c= N;
     x0 in N by NFCONT_1:4; then
A8:rng c c= dom f by A2,A6,ZFMISC_1:37;
A9:rng (h*z+c) c= dom f by A2,A7,XBOOLE_1:1;
A10: for n be Nat holds
      ||. (h"(#)(f*(h*z+c) - f*c)).n - diff(f,x0).z .||
        = ||. (h"(#) (R*(h*z))).n .||
     proof
       let n be Nat;
       R_NormSpace_of_BoundedLinearOperators(S,T)
        = NORMSTR (# BoundedLinearOperators(S,T),
                 Zero_(BoundedLinearOperators(S,T),
                 R_VectorSpace_of_LinearOperators(S,T)),
                  Add_(BoundedLinearOperators(S,T),
                  R_VectorSpace_of_LinearOperators(S,T)),
                   Mult_(BoundedLinearOperators(S,T),
                   R_VectorSpace_of_LinearOperators(S,T)),
                   BoundedLinearOperatorsNorm(S,T) #)
                by LOPBAN_1:def 15; then
       reconsider L = diff(f,x0) as Element of BoundedLinearOperators(S,T);
       R is total by NDIFF_1:def 5; then
       dom R = the carrier of S by PARTFUN1:def 4; then
A11:  rng (h*z) c= dom R;
       dom c = NAT by FUNCT_2:def 1; then
       c.n in rng c by FUNCT_1:12; then
A12:  c.n=x0 by A6,TARSKI:def 1;
       dom (h*z+c) = NAT by FUNCT_2:def 1; then
       (h*z+c).n in rng (h*z+c) by FUNCT_1:12; then
       (h*z+c).n in N by A7; then
       (h*z).n+c.n in N by NORMSP_1:def 5; then
A13:  h.n*z+x0  in N by A12,NDIFF_1:def 3;
       h is being_not_0 by FDIFF_1:def 1; then
A14:  h.n <> 0 by SEQ_1:7;
       (h"(#)(f*(h*z+c) - f*c)).n - diff(f,x0).z
         = (h".n) * (f*(h*z+c) - f*c).n - diff(f,x0).z by NDIFF_1:def 2
        .= (h.n)" * (f*(h*z+c) - f*c).n - diff(f,x0).z by SEQ_1:def 8
        .= (h.n)" * ((f*(h*z+c)).n - (f*c).n) - diff(f,x0).z by NORMSP_1:def 6
        .= (h.n)" * (f/.((h*z+c).n) - (f*c).n) - diff(f,x0).z
      by A9,NFCONT_1:8
        .= (h.n)" * (f/.((h*z+c).n) - f/.(c.n) )- diff(f,x0).z
      by A8,NFCONT_1:8
        .= (h.n)" * (f/.((h*z).n+c.n) - f/.(c.n) )- diff(f,x0).z
      by NORMSP_1:def 5
        .= (h.n)" * (f/.(h.n*z+c.n) - f/.(c.n) )- diff(f,x0).z
      by NDIFF_1:def 3
        .= (h.n)" * (diff(f,x0).(h.n*z+x0-x0) + R/.(h.n*z+x0-x0))
         - diff(f,x0).z by A5,A12,A13
        .= (h.n)" * (diff(f,x0).(h.n*z+(x0-x0)) + R/.(h.n*z+x0-x0))
         - diff(f,x0).z by RLVECT_1:42
        .= (h.n)" * (diff(f,x0).(h.n*z+(x0-x0)) + R/.(h.n*z+(x0-x0)))
         - diff(f,x0).z by RLVECT_1:42
        .= (h.n)" * (diff(f,x0).(h.n*z+0.S) + R/.(h.n*z+(x0-x0)))
         - diff(f,x0).z by RLVECT_1:28
        .= (h.n)" * (diff(f,x0).(h.n*z+0.S) + R/.(h.n*z+0.S))
         - diff(f,x0).z by RLVECT_1:28
        .= (h.n)" * (diff(f,x0).(h.n*z) + R/.(h.n*z+0.S))
         - diff(f,x0).z by RLVECT_1:10
        .= (h.n)" * (diff(f,x0).(h.n*z) + R/.(h.n*z))
         - diff(f,x0).z by RLVECT_1:10
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (diff(f,x0).(h.n*z))
         - diff(f,x0).z by RLVECT_1:def 9
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (modetrans(L,S,T).(h.n*z))
         - diff(f,x0).z by LOPBAN_1:def 12
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (h.n* (modetrans(L,S,T).z))
         - diff(f,x0).z by LOPBAN_1:def 6
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (h.n* (L.z))
         - diff(f,x0).z by LOPBAN_1:def 12
        .= (h.n)" *  (R/.(h.n*z))+(h.n)" * (h.n)* (diff(f,x0).z)
         - diff(f,x0).z by RLVECT_1:def 9
        .= (h.n)" *  (R/.(h.n*z))+1* (diff(f,x0).z)
         - diff(f,x0).z by A14,XCMPLX_0:def 7
        .= (h.n)" *  (R/.(h.n*z))+(diff(f,x0).z) - diff(f,x0).z
      by RLVECT_1:def 9
        .= (h.n)" *  (R/.(h.n*z))+(diff(f,x0).z - diff(f,x0).z)
           by RLVECT_1:42
        .= (h.n)" * (R/.(h.n*z))+0.T by RLVECT_1:28
        .= (h.n)" * (R/.(h.n*z)) by RLVECT_1:10
        .= (h".n) * (R/.(h.n*z)) by SEQ_1:def 8
        .= (h".n) * (R/.((h*z).n)) by NDIFF_1:def 3
        .= (h".n) * ((R*(h*z)).n) by A11,NFCONT_1:8
        .=(h"(#) (R*(h*z))).n by NDIFF_1:def 2;
       hence thesis;
     end;
A15:abs(h)"(#)(R*(h*z)) is convergent & lim ( abs(h)"(#)(R*(h*z)) ) = 0.T
     proof
       now per cases;
         case A16: z= 0.S;
A17:       for n be Nat holds  (abs(h)"(#) (R*(h*z))).n  = 0.T
           proof
             let n be Nat;
             R is total by NDIFF_1:def 5; then
             dom R = the carrier of S by PARTFUN1:def 4; then
A18:        rng (h*z) c= dom R;
             thus  (abs(h)"(#)(R*(h*z))).n
               =abs(h)".n*(R*(h*z)).n by NDIFF_1:def 2
              .=(abs(h).n)"*(R*(h*z)).n by SEQ_1:def 8
              .=(abs(h.n))"*(R*(h*z)).n by SEQ_1:16
              .= (abs(h.n))"*(R/.((h*z).n)) by A18,NFCONT_1:8
              .= (abs(h.n))"*(R/.(h.n*0.S)) by A16,NDIFF_1:def 3
              .= (abs(h.n))"*(R/.(0.S)) by RLVECT_1:23
              .=0.T by A4,RLVECT_1:23;
           end; then
A19:       abs(h)"(#) (R*(h*z)) is  constant by LOPBAN_3:def 5;
           (abs(h)"(#) (R*(h*z))).0 = 0.T by A17;
           hence abs(h)"(#) (R*(h*z))  is convergent &
            lim ( abs(h)"(#) (R*(h*z)) ) =0.T by A19,NDIFF_1:21;
         end;
         case A20: z<> 0.S;
           reconsider s = h*z as convergent_to_0 sequence of S
         by A20,NDIFF_1:24;
           now let n;
             R is total by NDIFF_1:def 5; then
             dom R = the carrier of S by PARTFUN1:def 4; then
A21:         rng s c= dom R;
             h is being_not_0 by FDIFF_1:def 1; then
             h.n <> 0 by SEQ_1:7; then
A22:          abs(h.n) <> 0 by ABSVALUE:6;
             s is_not_0 by NDIFF_1:def 4; then
             s.n <> 0.S by NDIFF_1:7; then
A23:          ||.s.n.|| <> 0 by NORMSP_1:def 2;
             ||.s.n.|| = ||.(h.n)*z.|| by NDIFF_1:def 3
             .= abs(h.n) * ||.z.|| by NORMSP_1:def 2; then
             (abs(h.n))"* ||.s.n.||
              = ((abs(h.n))"* (abs(h.n))) * ||.z.||
             .= 1 * ||.z.|| by A22,XCMPLX_0:def 7
             .=  ||.z.||; then
             ||.z.|| *( ||.s.n.|| )" =  (abs(h.n))"* ||.s.n.|| *( ||.s.n.|| )"
             .= (abs(h.n))"* (||.s.n.|| *( ||.s.n.|| )")
             .= (abs(h.n))"* (||.s.n.|| /( ||.s.n.|| )) by XCMPLX_0:def 9
             .= (abs(h.n))"*1 by A23,XCMPLX_1:60
             .= (abs(h.n))"; then
 A24:         ||.z.|| *( ||.s.||.n )" =  (abs(h.n))" by NORMSP_1:def 10;
             R is total by NDIFF_1:def 5; then
             dom R = the carrier of S by PARTFUN1:def 4; then
A25:        rng (h*z) c= dom R;
             thus
             (abs(h)"(#)(R*(h*z))).n
              = abs(h)".n*(R*(h*z)).n by NDIFF_1:def 2
             .=(abs(h).n)"*(R*(h*z)).n by SEQ_1:def 8
             .=(abs(h.n))"*(R*(h*z)).n by SEQ_1:16
             .= (abs(h.n))"*(R/.((h*z).n)) by A25,NFCONT_1:8
             .= ( ||.z.|| *||.s.||".n)*(R/.(s.n)) by A24,SEQ_1:def 8
             .= ||.z.|| *(||.s.||".n *(R/.(s.n)) ) by RLVECT_1:def 9
             .= ||.z.|| *((||.s.||".n)*((R*s).n)) by A21,NFCONT_1:8
             .= ||.z.|| *((||.s.||")(#)(R*s)).n by NDIFF_1:def 2
             .= ( ||.z.|| *((||.s.||")(#)(R*s))).n by NORMSP_1:def 8;
           end; then
A26:       (abs(h)"(#)(R*(h*z))) =  ||.z.|| *((||.s.||")(#)(R*s))
          by FUNCT_2:113;
A27:       (||.s.||")(#)(R*s) is convergent  & lim ((||.s.||")(#)(R*s)) = 0.T
          by NDIFF_1:def 5;
           hence (abs(h)"(#)(R*(h*z))) is convergent by A26,NORMSP_1:37;
           thus lim (abs(h)"(#)(R*(h*z)))
            = ||.z.|| *  0.T by A26,A27,NORMSP_1:45
           .= 0.T by RLVECT_1:23;
         end;
       end;
       hence thesis;
     end;
A28: h"(#) (R*(h*z)) is convergent & lim ( h"(#) (R*(h*z)) )=0.T
     proof
A29:  now let e be Real such that
A30:     0 < e;
         consider m be Nat such that
A31:      for n be Nat st m <= n holds
           ||.(abs(h)"(#) (R*(h*z))).n  - 0.T  .|| < e
         by A15,A30,NORMSP_1:def 11;
         now let n be Nat such that
A32:       m <= n;
           ||.(h"(#) (R*(h*z))).n  -0.T .||
            = ||.(h"(#) (R*(h*z))).n  .|| by RLVECT_1:26
           .= ||.h".n*(R*(h*z)).n .|| by NDIFF_1:def 2
           .= ||.(h.n)"*(R*(h*z)).n .|| by SEQ_1:def 8
           .=abs(abs((h.n)")) * ||.(R*(h*z)).n .|| by NORMSP_1:def 2
           .= ||.abs((h.n)")*(R*(h*z)).n .|| by NORMSP_1:def 2
           .= ||.abs(h".n)*(R*(h*z)).n .|| by SEQ_1:def 8
           .= ||.abs(h").n*(R*(h*z)).n .|| by SEQ_1:16
           .= ||.(abs(h))".n*(R*(h*z)).n .|| by SEQ_1:62
           .= ||.(abs(h)"(#) (R*(h*z))).n .|| by NDIFF_1:def 2
           .= ||.(abs(h)"(#) (R*(h*z))).n -0.T .|| by RLVECT_1:26;
           hence ||.(h"(#) (R*(h*z))).n  -0.T .|| < e by A31,A32;
         end;
         hence
         ex m be Nat st
          for n be Nat st m <= n holds
           ||.(h"(#) (R*(h*z))).n  -0.T .|| < e;
       end;
       hence h"(#) (R*(h*z)) is convergent by NORMSP_1:def 9;
       hence lim ( h"(#) (R*(h*z)) ) =0.T by A29,NORMSP_1:def 11;
     end;
A33: now let e be Real such that
A34:   0 < e;
       consider m be Nat such that
A35:    for n be Nat st m <= n holds
         ||.(h"(#) (R*(h*z))).n  - 0.T .|| < e by A28,A34,NORMSP_1:def 11;
       now let n be Nat such that
A36:     m <= n;
         ||.(h"(#) (R*(h*z))).n  -0.T .|| < e by A35,A36; then
         ||.(h"(#) (R*(h*z))).n   .|| < e by RLVECT_1:26;
         hence ||. (h"(#)(f*(h*z+c) - f*c)).n - diff(f,x0).z .|| < e by A10;
       end;
       hence
       ex m be Nat st
        for n be Nat st m <= n holds
         ||. (h"(#)(f*(h*z+c) - f*c)).n - diff(f,x0).z .|| < e;
     end;
     hence (h"(#)(f*(h*z+c) - f*c)) is convergent by NORMSP_1:def 9;
     hence lim (h"(#)(f*(h*z+c) - f*c)) = diff(f,x0).z by A33,NORMSP_1:def 11;
   end;
   hence thesis by A2;
end;

theorem
for x0 be Point of S st f is_differentiable_in x0
  for z be Point of S
  for h be convergent_to_0 Real_Sequence
  for c st (rng c = {x0}  & rng (h*z+c) c= dom f ) holds
    h"(#)(f*(h*z+c) - f*c) is convergent &
    diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c))
proof
   let x0 be Point of S such that
A1:f is_differentiable_in x0;
   let z be Point of S;
   let h be convergent_to_0 Real_Sequence;
   let c such that
A2:rng c = {x0} and
A3:rng (h*z+c) c= dom f;
   consider N being Neighbourhood of x0 such that
A4:N c= dom f and
A5:for z be Point of S holds
     for h be convergent_to_0 Real_Sequence
     for c st rng c = {x0} & rng (h*z+c) c= N holds
      h"(#)(f*(h*z+c) - f*c) is convergent
      & diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c)) by A1,Th1;
   consider g be Real such that
A6:0<g and
A7:{y where y is Point of S: ||.y-x0.|| < g} c= N by NFCONT_1:def 3;
A8:for n be Nat holds c.n=x0
   proof
     let n be Nat;
     c.n in rng c by NFCONT_1:6;
     hence c.n=x0 by A2,TARSKI:def 1;
   end;
   ex n st rng ((h^\n*z+c)) c= N
   proof
A9: c is convergent by NDIFF_1:21;
     c.0 in rng c by NFCONT_1:6; then
     c.0=x0 by A2,TARSKI:def 1; then
A10: lim c = x0 by NDIFF_1:21;
A11: h*z is convergent & lim (h*z) = 0.S
     proof
A12:    now let p;
         assume
A13:     0<p;
         ex pp be Real st pp > 0 & pp*||.z.|| < p
         proof
           take pp=p/(||.z.||+1);
A14:        0 <= ||.z.|| by NORMSP_1:8;
A15:        ||.z.||+1 > 0+0 by A14,REAL_1:67; then
A16:        0  <  p/(  ||.z.||+1 ) by A13,SEQ_2:6;
A17:        ||.z.||+0 <  ||.z.||+1 by REAL_1:67;
           0 <= ||.z.|| by NORMSP_1:8; then
           pp* ||.z.|| < pp*(  ||.z.||  + 1) by A16,A17,SEQ_4:6;
           hence pp > 0 & pp* ||.z.||  < p by A13,A15,SEQ_2:6,XCMPLX_1:88;
         end; then
         consider pp be Real such that
A18:      pp > 0  & pp*||.z.|| < p;
         h is convergent & lim h =0 by FDIFF_1:def 1; then
         consider n be Nat such that
A19:      for m be Nat st n <= m  holds  abs(h.m- 0) < pp by A18,SEQ_2:def 7;
         take n;
         let m be Nat such that
A20:      n<=m;
A21:     abs(h.m-0) < pp by A19,A20;
A22:     ||.(h*z).m-0.S.|| = ||.h.m*z  - 0.S.|| by NDIFF_1:def 3
         .= ||.h.m*z.|| by RLVECT_1:26
         .= abs(h.m)*||.z.|| by NORMSP_1:def 2;
         0 <= ||.z.|| by NORMSP_1:8; then
         abs(h.m)*||.z.|| <= pp* ||.z.|| by A21,AXIOMS:25;
         hence ||.(h*z).m-0.S.||<p by A18,A22,AXIOMS:22;
       end;
       hence h*z is convergent by NORMSP_1:def 9;
       hence lim (h*z) = 0.S by A12,NORMSP_1:def 11;
     end; then
A23: lim ((h*z) + c) = 0.S  + x0 by A9,A10,NORMSP_1:42
      .= x0 by RLVECT_1:10;
     h*z + c is convergent by A9,A11,NORMSP_1:34; then
     consider n such that
A24:  for m being Nat st n<=m holds
       ||.(h*z+c).m-x0.||<g by A6,A23,NORMSP_1:def 11;
     take n;
     let y be set;
     assume y in rng ((h^\n*z+c)); then
     consider m be Nat such that
A25:  y =  ((h^\n*z+c)).m by NFCONT_1:6;
     n <= n+m by NAT_1:29; then
     ||.(h*z+c).(n+m)-x0.||<g by A24; then
     ||.((h*z).(n+m)+c.(n+m)) -x0.||<g by NORMSP_1:def 5; then
     ||.((h*z).(n+m)+x0) -x0.||<g by A8; then
     ||.((h*z).(n+m)+c.m) -x0.||<g by A8; then
     ||.((h.(n+m)*z)+c.m) -x0.||<g by NDIFF_1:def 3; then
     ||.((h^\n).m*z+c.m) -x0.||<g by SEQM_3:def 9; then
     ||.(((h^\n)*z).m+c.m) -x0.||<g by NDIFF_1:def 3; then
A26:  ||.((h^\n*z+c)).m - x0 .|| < g by NORMSP_1:def 5;
     reconsider y1=y as Point of S by A25;
     y1 in  {w where w is Point of S: ||.w-x0.|| < g} by A25,A26;
     hence y in N by A7;
   end; then
   consider n be Nat such that
A27:rng ((h^\n*z+c)) c= N;
A28:(h^\n)"(#)(f*(h^\n*z+c) - f*c) is convergent
      & diff(f,x0).z = lim ((h^\n)"(#)(f*(h^\n*z+c) - f*c)) by A2,A5,A27;
A29:now
     let m be Nat;
     thus
     ((h*z+c)^\n).m = (h*z+c).(n+m) by LOPBAN_3:def 6
      .= (h*z).(n+m)+c.(n+m) by NORMSP_1:def 5
      .=(h*z).(n+m)+x0 by A8
      .=(h*z).(n+m)+c.m by A8
      .=(h.(n+m)*z)+c.m by NDIFF_1:def 3
      .=(h^\n).m*z+c.m by SEQM_3:def 9
      .=((h^\n)*z).m+c.m by NDIFF_1:def 3
      .=(h^\n*z+c).m by NORMSP_1:def 5;
   end;
   x0 in N by NFCONT_1:4; then
A30:rng c c= dom f by A2,A4,ZFMISC_1:37;
A31:rng (h^\n*z+c) c= dom f by A4,A27,XBOOLE_1:1;
   (h^\n)"(#)(f*(h^\n*z+c) - f*c) = (h"(#)(f*(h*z+c) - f*c))^\n
   proof
     now let m be Nat;
A32:    ((f*(h*z+c))^\n).m =(f*(h*z+c)).(n+m) by LOPBAN_3:def 6
         .=f/.((h*z+c).(n+m)) by A3,NFCONT_1:8
         .=f/.( ( (h*z+c)^\n).m) by LOPBAN_3:def 6
         .=f/.( (h^\n*z+c).m) by A29;
       thus
           ( (h"(#)(f*(h*z+c) - f*c))^\n).m
            =(h"(#)(f*(h*z+c) - f*c)).(n+m) by LOPBAN_3:def 6
           .=h".(n+m)*(f*(h*z+c) - f*c).(n+m) by NDIFF_1:def 2
           .=(h.(n+m))"*(f*(h*z+c) - f*c).(n+m) by SEQ_1:def 8
           .=((h^\n).m)"*(f*(h*z+c) - f*c).(n+m) by SEQM_3:def 9
           .=((h^\n).m)"*((f*(h*z+c)).(n+m) - (f*c).(n+m))
                    by NORMSP_1:def 6
           .=((h^\n).m)"*(((f*(h*z+c))^\n).m - (f*c).(n+m))
                    by LOPBAN_3:def 6
           .=((h^\n).m)"*(f/.( (h^\n*z+c).m) - f/.(c.(n+m)))
          by A30,A32,NFCONT_1:8
           .=((h^\n).m)"*(f/.( (h^\n*z+c).m) - f/.x0) by A8
           .=((h^\n).m)"*(f/.( (h^\n*z+c).m) - f/.(c.m)) by A8
           .=((h^\n).m)"*(f/.((h^\n*z+c).m) - (f*c).m) by A30,NFCONT_1:8
           .=((h^\n).m)"*((f*(h^\n*z+c)).m - (f*c).m) by A31,NFCONT_1:8
           .=((h^\n).m)"*((f*(h^\n*z+c) - f*c).m) by NORMSP_1:def 6
           .=((h^\n)".m)*(f*(h^\n*z+c) - f*c).m by SEQ_1:def 8
           .=((h^\n)"(#)(f*(h^\n*z+c) - f*c)).m by NDIFF_1:def 2;
     end;
     hence thesis by FUNCT_2:113;
   end;
   hence thesis by A28,LOPBAN_3:15,16;
end;

theorem Th3:
for x0 be Point of S
for N be Neighbourhood of x0 st N c= dom f
 for z be Point of S
 for dv be Point of T holds
  (
    (for h be convergent_to_0 Real_Sequence
     for c st ( rng c = {x0} & rng (h*z+c) c= N ) holds
      h"(#)(f*(h*z+c) - f*c) is convergent &
      dv = lim (h"(#)(f*(h*z+c) - f*c)))
    iff
    (for e be Real st e > 0 holds
      ex d be Real st d > 0 &
       for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e )
  )
proof
   let x0 be Point of S;
   let N be Neighbourhood of x0 such that
A1:N c= dom f;
   let z be Point of S;
   let dv be Point of T;
A2:now assume
A3: ( for h be convergent_to_0 Real_Sequence
       for c st ( rng c = {x0}  & rng (h*z+c) c=  N ) holds
        h"(#)(f*(h*z+c) - f*c) is convergent &
        dv = lim (h"(#)(f*(h*z+c) - f*c)));
     assume
A4:  not ( for r be Real st r > 0 holds
          ex d be Real st d > 0 &
            for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
              ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < r );
     consider r be Real such that
A5:   r > 0 and
A6:   for d be Real st d > 0 holds
       ex h be Real st
        abs(h) < d & h <> 0 &  h*z+x0 in N &
        not ( ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < r ) by A4;
     defpred P[Nat,real number] means
     ex rr being Real st rr = $2 &
      abs(rr) < (1/($1+1)) &
      rr <> 0 &
      rr*z+x0 in N &
      not (  ||. rr"*(f/.(rr*z+x0) -  f/.x0) - dv .|| < r );
A7:  for n be Nat ex h be real number st P[n,h]
     proof
       let n be Nat;
       0 <= n by NAT_1:18; then
       0+0 < 1+ n by REAL_1:67; then
       0 < 1 * (n + 1)" by REAL_1:72; then
       0 < 1/(n + 1) by XCMPLX_0:def 9; then
       consider h be Real such that
A8:     abs(h) < 1/(n + 1) &
        h <> 0 &
        h*z+x0 in N &
        not ( ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < r  ) by A6;
       reconsider h as real number;
       thus thesis by A8;
     end;
     consider h be Real_Sequence such that
A9:   for n be Nat holds P[n,h.n] from RCOMP_1:sch 1(A7);
     reconsider c = NAT --> x0 as sequence of S by FUNCOP_1:57;
A10:   for n holds c.n=x0 by FUNCOP_1:13;
A11:  rng c = {x0}
     proof
       now let x be set;
       assume x in rng c; then
       consider n be Nat such that
A12:     x=c.n by NFCONT_1:6;
       x=x0 by A10,A12;
       hence x in {x0} by TARSKI:def 1;
     end; then
A13:   rng c c= {x0} by TARSKI:def 3;
     now let x be set;
       assume x in {x0}; then
       x=x0 by TARSKI:def 1; then
       x=c.1 by A10;
       hence x in rng c by NFCONT_1:6;
     end; then
     {x0} c= rng c by TARSKI:def 3;
     hence rng c = {x0} by A13,XBOOLE_0:def 10;
   end;
   x0 in N by NFCONT_1:4; then
A14:rng c c= dom f by A1,A11,ZFMISC_1:37;
A15:now
     let p be real number;
     assume 0<p; then
A16: 0<p" by REAL_1:72;
     consider n such that
A17:  p"<n by SEQ_4:10;
     p" + 0 < n + 1 by A17,REAL_1:67; then
     1/(n+1) < 1/p" by A16,SEQ_2:10; then
A18: 1/(n+1) < p by XCMPLX_1:218;
     take n;
     let m be Nat;
     assume n<=m; then
A19: n + 1 <= m + 1 by AXIOMS:24;
     0  <=  n by NAT_1:18; then
     0+0 < 1+ n by REAL_1:67; then
A20: 1/(m+1) <= 1/(n+1) by A19,SEQ_4:1;
     P[m,h.m] by A9;
     then abs(h.m-0) <  1/(n+1) by A20,AXIOMS:22;
     hence abs(h.m-0) <p by A18,AXIOMS:22;
   end; then
A21:h is convergent by SEQ_2:def 6; then
A22:lim h = 0 by A15,SEQ_2:def 7;
   for n holds h.n <> 0
   proof
     let n;
     P[n,h.n] by A9;
     hence thesis;
   end;
   then h is_not_0 by SEQ_1:7; then
   reconsider h as  convergent_to_0 Real_Sequence by A21,A22,FDIFF_1:def 1;
A23:rng (h*z+c) c= N
   proof
     now let x be set;
       assume x in rng (h*z+c); then
       consider n be Nat such that
A24:     x=(h*z+c).n by NFCONT_1:6;
A25:    P[n,h.n] by A9;
       x=(h*z).n+c.n by A24,NORMSP_1:def 5
       .=(h.n)*z+c.n by NDIFF_1:def 3
       .=(h.n)*z+x0 by A10;
       hence x in N by A25;
     end;
     hence thesis by TARSKI:def 3;
   end; then
A26:rng (h*z+c) c= dom f by A1,XBOOLE_1:1;
A27:h"(#)(f*(h*z+c) - f*c) is convergent
      &  dv = lim (h"(#)(f*(h*z+c) - f*c)) by A3,A11,A23;
   consider n be Nat such that
A28:for m be Nat st n <=m holds
     ||. (h"(#)(f*(h*z+c) - f*c)).m- dv.|| < r by A5,A27,NORMSP_1:def 11;
A29:||. (h"(#)(f*(h*z+c) - f*c)).n- dv.|| < r by A28;
A30:    P[n,h.n] by A9;
   ||. (h"(#)(f*(h*z+c) - f*c)).n- dv.||
     = ||.(h".n)*((f*(h*z+c) - f*c).n)- dv.|| by NDIFF_1:def 2
    .= ||.(h.n)"*((f*(h*z+c) - f*c).n)- dv.|| by SEQ_1:def 8
    .= ||.(h.n)"*( (f*(h*z+c)).n - (f*c).n)- dv.|| by NORMSP_1:def 6
    .= ||.(h.n)"*( (f*(h*z+c)).n - f/.(c.n)) - dv.|| by A14,NFCONT_1:8
    .= ||.(h.n)"*( (f*(h*z+c)).n - f/.x0) - dv.|| by A10
    .= ||.(h.n)"*( f/.( (h*z+c).n) - f/.x0) - dv.|| by A26,NFCONT_1:8
    .= ||.(h.n)"*( f/.((h*z).n+c.n) - f/.x0) - dv.|| by NORMSP_1:def 5
    .= ||.(h.n)"*( f/.((h*z).n+x0) - f/.x0) - dv.|| by A10
    .=  ||.(h.n)"*(f/.((h.n)*z+x0) -  f/.x0)-dv .|| by NDIFF_1:def 3;
   hence
      ( for e be Real st e > 0 holds
        ex d be Real st d > 0
          & for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
              ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e ) by A29,A30;
 end;
   now assume
A31:( for e be Real st e > 0 holds
       ex d be Real st d > 0
        & for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N)
           holds   ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e );
   now
     let h be convergent_to_0 Real_Sequence;
     let c such that
A32:  rng c = {x0} and
A33:  rng (h*z+c) c= N;
A34:rng (h*z+c) c= dom  f by A1,A33,XBOOLE_1:1;
A35:  for n holds c.n=x0
     proof
       let n be Nat;
       c.n in rng c by NFCONT_1:6;
       hence c.n=x0 by A32,TARSKI:def 1;
     end;
     x0 in N by NFCONT_1:4; then
A36:  rng c c= dom f by A1,A32,ZFMISC_1:37;
A37:  h is_not_0 by FDIFF_1:def 1;
A38:  h is convergent & lim h = 0 by FDIFF_1:def 1;
A39:  now let r be Real such that
A40:   r > 0;
       consider d be Real  such that
A41:    d > 0 and
A42:    (for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
         ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < r ) by A31,A40;
       consider n be Nat such that
A43:   for m be Nat st n <=m  holds
         abs(h.m-0) < d by A38,A41,SEQ_2:def 7;
       take n;
       thus  for m be Nat st n <=m holds
       ||. (h"(#)(f*(h*z+c) - f*c)).m- dv.|| < r
       proof
         let m be Nat;
         assume n <= m; then
A44:    abs(h.m-0) < d by A43;
A45:     h.m <> 0 by A37,SEQ_1:7;
         (h*z+c).m in rng (h*z+c) by NFCONT_1:6; then
         (h*z+c).m in N by A33; then
         (h*z).m+c.m in N by NORMSP_1:def 5; then
         h.m*z + c.m in N by NDIFF_1:def 3; then
A46:     h.m*z + x0 in N by A35;
         ||. (h"(#)(f*(h*z+c) - f*c)).m- dv.||
          = ||.(h".m)*((f*(h*z+c) - f*c).m)- dv.|| by NDIFF_1:def 2
         .= ||.(h.m)"*((f*(h*z+c) - f*c).m)- dv.|| by SEQ_1:def 8
         .= ||.(h.m)"*( (f*(h*z+c)).m - (f*c).m)- dv.|| by NORMSP_1:def 6
         .= ||.(h.m)"*( (f*(h*z+c)).m - f/.(c.m)) - dv.|| by A36,NFCONT_1:8
         .= ||.(h.m)"*( (f*(h*z+c)).m - f/.x0) - dv.|| by A35
         .= ||.(h.m)"*( f/.( (h*z+c).m) - f/.x0) - dv.|| by A34,NFCONT_1:8
         .= ||.(h.m)"*( f/.((h*z).m+c.m) - f/.x0) - dv.|| by NORMSP_1:def 5
         .= ||.(h.m)"*( f/.((h*z).m+x0) - f/.x0) - dv.|| by A35
         .=  ||.(h.m)"*(f/.((h.m)*z+x0) -  f/.x0)-dv .|| by NDIFF_1:def 3;
         hence thesis by A42,A44,A45,A46;
         end;
       end;
       hence (h"(#)(f*(h*z+c) - f*c)) is convergent by NORMSP_1:def 9;
       hence lim (h"(#)(f*(h*z+c) - f*c)) = dv by A39,NORMSP_1:def 11;
     end;
     hence
     for h be convergent_to_0 Real_Sequence
     for c st ( rng c = {x0}  & rng (h*z+c) c= N ) holds
       h"(#)(f*(h*z+c) - f*c) is convergent &
       dv = lim (h"(#)(f*(h*z+c) - f*c));
   end;
   hence thesis by A2;
end;

definition
let S,T;
let f;
let x0 be Point of S;
let z be Point of S;
pred f is_Gateaux_differentiable_in x0,z means :Def3:
ex N be Neighbourhood of x0 st N c= dom f &
 ex dv be Point of T st
  for e be Real st e > 0 holds
   ex d be Real st d > 0 &
    for h be Real st ( abs(h) < d & h <> 0 & h*z+x0 in N)
             holds ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e;
end;

theorem Th4:
(for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| > 0 iff x <> y) &
(for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| = ||.y-x.||) &
(for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| = 0 iff x = y) &
(for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| <> 0 iff x <> y) &
(for X be RealNormSpace, x,y,z be Point of X, e be Real st e > 0 holds
  ( (||.x-z.|| <e/2 & ||.z-y.|| <e/2)  implies ||.x-y.|| <e) ) &
(for X be RealNormSpace, x,y,z be Point of X, e be Real st e > 0 holds
  ( (||.x-z.|| <e/2 & ||.y-z.|| <e/2)  implies ||.x-y.|| <e) ) &
(for X be RealNormSpace, x be Point of X st
  ( for e be Real st e>0 holds  ||.x.|| <e ) holds  x=0.X ) &
(for X be RealNormSpace, x,y be Point of X st
  ( for e be Real st e>0 holds  ||.x-y.|| <e ) holds x=y )
proof
   thus
   for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| > 0 iff x <> y
   proof
     let X be RealNormSpace;
     let x, y be Point of X;
     0 < ||.x-y.|| implies x-y <> 0.X by NORMSP_1:def 2;
     hence 0 < ||.x-y.|| implies x <> y by RLVECT_1:28;
     now
       assume x <> y; then
       0 <> ||.x-y.|| by NORMSP_1:15;
       hence 0 < ||.x-y.|| by NORMSP_1:8;
     end;
     hence x <> y implies 0 < ||.x-y.||;
   end;
   thus for X be RealNormSpace, x,y be Point of X holds ||.x-y.|| = ||.y-x.||
   proof
     let X be RealNormSpace;
     let x, y be Point of X;
     thus ||.x-y.|| = ||.-(x-y).|| by NORMSP_1:6
     .= ||.y+(-x).|| by RLVECT_1:47
     .= ||.y-x.|| by RLVECT_1:def 11;
   end;
  A1: for X be RealNormSpace
   for x, y, z be Point of X
   for e be Real st e > 0
   holds
   ((||.x-z.|| < e/2 & ||.z-y.|| < e/2)  implies ||.x-y.|| <e)
  proof
    let X be RealNormSpace;
    let x, y, z be Point of X;
    let e be Real such that e > 0;
    assume ||.x-z.|| < e/2 & ||.z-y.|| < e/2;
      then A2: ||.x-z.|| + ||.z-y.|| < e/2 + e/2 by REAL_1:67;
    ||.x-y.|| <= ||.x-z.|| + ||.z-y.|| by NORMSP_1:14;
    then ||.x-y.|| + (||.x-z.|| + ||.z-y.||)
      < (||.x-z.|| + ||.z-y.||) + e by A2,REAL_1:67;
    then ||.x-y.|| + (||.x-z.|| + ||.z-y.||) +- (||.x-z.|| + ||.z-y.||)
      < e + (||.x-z.|| + ||.z-y.||) +- (||.x-z.|| + ||.z-y.||)
   by REAL_1:67;
    hence thesis;
  end;
  A3: for X be RealNormSpace
   for x, y, z be Point of X
   for e be Real st e > 0
   holds
   ((||.x-z.|| < e/2 & ||.y-z.|| < e/2)  implies ||.x-y.|| < e)
  proof
    let X be RealNormSpace;
    let x, y, z be Point of X;
    let e be Real such that A4: e > 0;
    assume A5: ||.x-z.|| < e/2 & ||.y-z.|| < e/2;
    then ||.-(y-z).|| < e/2 by NORMSP_1:6;
    then ||.z+-y.|| < e/2 by RLVECT_1:47;
    then ||.z-y.|| < e/2 by RLVECT_1:def 11;
    hence thesis by A1,A4,A5;
  end;
  A6: for X be RealNormSpace
   for x be Point of X
     st (for e be Real st e>0 holds  ||.x.|| <e)
   holds x=0.X
  proof
    let X be RealNormSpace;
    let x be Point of X such that
    A7:   for  e be Real st e > 0 holds  ||.x.|| <e;
    now
      assume x<>0.X; then
      0 <> ||.x.|| by NORMSP_1:def 2; then
      0 <  ||.x.|| by NORMSP_1:8;
      hence contradiction by A7;
    end;
    hence thesis;
  end;
  for X be RealNormSpace
   for x,y  be Point of X
     st   (for e be Real st e>0 holds  ||.x-y.|| <e)
   holds  x=y
  proof
    let X be RealNormSpace;
    let x,y be Point of X;
    assume for e be Real st e > 0 holds  ||.x-y.|| <e; then
    x-y = 0.X by A6;
    hence thesis by RLVECT_1:35;
  end;
  hence  thesis by A1,A3,A6,NORMSP_1:10;
end;

definition
let S,T;
let f;
let x0 be Point of S;
let z be Point of S;
assume
    A1: f is_Gateaux_differentiable_in x0,z;
func Gateaux_diff(f,x0,z) -> Point of T means :Def4:
  ex N be Neighbourhood of x0 st N c= dom f &
   for e be Real st e > 0 holds
    ex d be Real st d > 0 &
     for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N ) holds
      ||. h"*(f/.(h*z+x0) -  f/.x0) - it .|| < e;
existence
proof
   consider N be Neighbourhood of x0 such that
A2: N c= dom f and
A3: ex dv be Point of T st
     for e be Real st e > 0 holds
      ex d be Real st
       d > 0 &
       for h be Real st (abs(h)<d & h<>0 & h*z+x0 in N) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e by A1,Def3;
   thus thesis by A2,A3;
end;
uniqueness
proof
   let dv1,dv2 be Point of T;
   assume that
A4: ex N1 be Neighbourhood of x0 st
     N1 c= dom f &
     for e be Real st e > 0 holds
      ex d be Real st d > 0 &
       for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N1) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv1 .|| < e
    and
A5: ex N2 be Neighbourhood of x0 st
     N2 c= dom f &
     for e be Real st e > 0 holds
      ex d be Real st d > 0 &
       for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N2) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv2 .|| < e;
   consider N1 be Neighbourhood of x0 such that
    N1 c= dom f and
A6:for e be Real st e > 0 holds
     ex d be Real st d > 0 &
      for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N1) holds
       ||. h"*(f/.(h*z+x0) -  f/.x0) - dv1 .|| < e by A4;
   consider N2 be Neighbourhood of x0 such that
    N2 c= dom f and
A7:for e be Real st e > 0 holds
     ex d be Real st d > 0 &
      for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N2) holds
       ||. h"*(f/.(h*z+x0) -  f/.x0) - dv2 .|| < e by A5;
   now
     let e be Real such that
A8:  e > 0;
A9: 0 < e/2 by A8,SEQ_2:3;
     consider d1 be Real such that
A10:  d1 > 0 and
A11:  for h be Real st ( abs(h) < d1  & h <> 0 & h*z+x0 in N1) holds
       ||. h"*(f/.(h*z+x0) -  f/.x0) - dv1 .|| < e/2 by A6,A9;
     consider d2 be Real such that
A12:  d2 > 0 and
A13:  for h be Real st ( abs(h) < d2  & h <> 0 & h*z+x0 in N2) holds
       ||. h"*(f/.(h*z+x0) -  f/.x0) - dv2 .|| < e/2 by A7,A9;

A14: ex h be Real st
      h <> 0 & abs(h) < d1  &  abs(h) < d2  & h*z+x0 in N1 & h*z+x0 in N2
     proof
       consider N0 be Neighbourhood of x0 such that
A15:    N0 c= N1 & N0 c= N2 by NDIFF_1:1;
       consider g be Real such that
A16:    0<g & {y where y is Point of S: ||.y - x0.|| < g} c= N0
       by NFCONT_1:def 3;
       set d3=min(d1,d2);
A17:   d3<=d1 & d3 <=d2 by SQUARE_1:35;
       0 < d3 by A10,A12,SQUARE_1:38; then
A18:   0 < d3/2 & d3/2 < d3 by SEQ_2:3,4; then
A19:   d3/2 < d1 & d3/2 < d2 by A17,AXIOMS:22;
       consider n0 be Nat such that
A20:    ||.z.|| /g < n0 by SEQ_4:10;
       0 <= n0 by NAT_1:18; then
A21:    0 < n0+1 by NAT_1:38;
       n0 <= n0+1 by NAT_1:29; then
       ||.z.|| /g < (n0+1) by A20,AXIOMS:22; then
       ( ||.z.|| /g)*g < (n0+1)*g by A16,REAL_1:70; then
       ||.z.|| < (n0+1)*g by A16,XCMPLX_1:88; then
A22:    ||.z.|| /(n0+1) < g by A21,REAL_2:178;
       set n1=n0+1;
       set h = min(1/(n1+1), d3/2);
       take h;
       0+0 <n1+1 by A21,REAL_1:67; then
       0 < 1 * (1 + n1)" by REAL_1:72; then
A23:    0 < 1/(1 + n1) by XCMPLX_0:def 9;
       hence h <> 0 by A18,SQUARE_1:38;
A24:   0 < h by A18,A23,SQUARE_1:38;
       h<=1/(n1+1) & h <=d3/2 by SQUARE_1:35; then
       h < d1 & h < d2 by A19,AXIOMS:22;
       hence abs(h) < d1 & abs(h) < d2 by A24,ABSVALUE:def 1;
       0 <= ||.z.|| & n1 <=  1+n1 by NAT_1:37,NORMSP_1:8; then
A25:    ||.z.|| /(1+n1) <= ||.z.|| /n1 by A21,REAL_2:201;
A26:   ||.h*z+x0-x0.|| = ||.h*z+(x0-x0).|| by RLVECT_1:42
                      .= ||.h*z+0.S.|| by RLVECT_1:28
                      .= ||.h*z.|| by RLVECT_1:10
                      .= abs(h)*||.z.|| by NORMSP_1:def 2
                      .= h* ||.z.|| by A24,ABSVALUE:def 1;
       ( 1 /(1+n1) )* ||.z.|| = ||.z.|| /(1+n1) by XCMPLX_1:100; then
A27:   ( 1 /(1+n1) )* ||.z.|| < g by A22,A25,AXIOMS:22;
       0 <= ||.z.|| & h <=1/(n1+1) by NORMSP_1:8,SQUARE_1:35; then
       h* ||.z.|| <=(1/(n1+1)) *  ||.z.|| by AXIOMS:25; then
       ||.h*z+x0-x0.|| < g by A26,A27,AXIOMS:22; then
       (h*z+x0) in {y where y is Point of S: ||.y - x0.|| < g}; then
       (h*z+x0) in N0 by A16;
       hence (h*z+x0) in N1 & (h*z+x0) in N2 by A15;
     end;
     consider h be Real such that
A28:   abs(h) < d1 & abs(h) < d2 & h <> 0 &
      h*z+x0 in N1 &  h*z+x0 in N2 by A14;
A29: ||. h"*(f/.(h*z+x0) -  f/.x0) - dv1 .|| < e/2 by A11,A28;
A30: ||. h"*(f/.(h*z+x0) -  f/.x0) - dv2 .|| < e/2 by A13,A28;
     ||. dv1- h"*(f/.(h*z+x0) -  f/.x0) .|| < e/2 by A29,Th4;
     hence  ||. dv1- dv2 .|| < e by A8,A30,Th4;
   end;
   hence dv1=dv2 by Th4;
end;
end;

theorem
for x0 be Point of S, z be Point of S holds
 ( f is_Gateaux_differentiable_in x0,z
  iff
   ex N being Neighbourhood of x0 st
    ( N c= dom f &
      ex dv be Point of T st
       for h be convergent_to_0 Real_Sequence
       for c st rng c = {x0} & rng (h*z+c) c= N holds
        h"(#)(f*(h*z+c) - f*c) is convergent &
        dv= lim (h"(#)(f*(h*z+c) - f*c))
    )
 )
proof
   let x0 be Point of S;
   let z be Point of S;
A1: now assume
     f is_Gateaux_differentiable_in x0,z; then
     consider N being Neighbourhood of x0 such that
A2:  N c= dom f and
A3:  for e be Real st e > 0 holds
       ex d be Real st
        d > 0 &
        for h be Real st (abs(h) < d  & h <> 0 & h*z+x0 in N) holds
         ||. h"*(f/.(h*z+x0) -  f/.x0) - Gateaux_diff(f,x0,z) .|| < e by Def4;
     for h be convergent_to_0 Real_Sequence, c st
      ( rng c = {x0}  & rng (h*z+c) c= N ) holds
       h"(#)(f*(h*z+c) - f*c) is convergent &
       Gateaux_diff(f,x0,z) = lim (h"(#)(f*(h*z+c) - f*c)) by A2,A3,Th3;
     hence
     ex N being Neighbourhood of x0 st
      ( N c= dom f &
        ex dv be Point of T st
         for h be convergent_to_0 Real_Sequence, c st
          rng c = {x0} & rng (h*z+c) c= N holds
           h"(#)(f*(h*z+c) - f*c) is convergent &
           dv= lim (h"(#)(f*(h*z+c) - f*c))
      ) by A2;
   end;
A4: now assume
     ex N being Neighbourhood of x0 st
      ( N c= dom f &
        ex dv be Point of  T st
         for h be convergent_to_0 Real_Sequence, c st
          rng c = {x0} & rng (h*z+c) c= N holds
           h"(#)(f*(h*z+c) - f*c) is convergent &
           dv= lim (h"(#)(f*(h*z+c) - f*c))
      ); then
     consider N being Neighbourhood of x0 such that
A5:  N c= dom f and
A6:  ex dv be Point of T st
       for h be convergent_to_0 Real_Sequence, c st
        rng c = {x0} & rng (h*z+c) c= N holds
         h"(#)(f*(h*z+c) - f*c) is convergent &
         dv= lim (h"(#)(f*(h*z+c) - f*c));
     consider dv be Point of T such that
A7:  for h be convergent_to_0 Real_Sequence, c st
       rng c = {x0} & rng (h*z+c) c= N holds
        h"(#)(f*(h*z+c) - f*c) is convergent &
        dv= lim (h"(#)(f*(h*z+c) - f*c)) by A6;
     for e be Real st e > 0 holds
      ex d be Real st
       d > 0 &
       for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
        ||. h"*(f/.(h*z+x0) -  f/.x0) - dv .|| < e by A5,A7,Th3;
     hence f is_Gateaux_differentiable_in x0,z by A5,Def3;
   end;
   thus thesis by A1,A4;
end;

theorem
for x0 be Point of S st f is_differentiable_in x0
for z be Point of S holds
 ( f is_Gateaux_differentiable_in x0,z &
   Gateaux_diff(f,x0,z)=diff(f,x0).z &
   ex N being Neighbourhood of x0 st
    ( N c= dom f &
      for h be convergent_to_0 Real_Sequence, c st
       rng c = {x0} & rng (h*z+c) c= N holds
        h"(#)(f*(h*z+c) - f*c) is convergent &
        Gateaux_diff(f,x0,z)= lim (h"(#)(f*(h*z+c) - f*c)))
 )
proof
   let x0 be Point of S such that
A1:f is_differentiable_in x0;
   let z be Point of S;
   consider  N being Neighbourhood of x0 such that
A2:N c= dom f and
A3:for z be Point of S holds
     for h be convergent_to_0 Real_Sequence, c st
      rng c = {x0} & rng (h*z+c) c= N holds
       h"(#)(f*(h*z+c) - f*c) is convergent &
       diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c)) by A1,Th1;
A4:
   for h be convergent_to_0 Real_Sequence, c st
    rng c = {x0} & rng (h*z+c) c= N holds
     h"(#)(f*(h*z+c) - f*c) is convergent &
     diff(f,x0).z = lim (h"(#)(f*(h*z+c) - f*c)) by A3;
A5:for e be Real st e > 0 holds
    ex d be Real st
     d > 0 &
     for h be Real st ( abs(h) < d  & h <> 0 & h*z+x0 in N) holds
      ||. h"*(f/.(h*z+x0) -  f/.x0) - diff(f,x0).z .|| < e by A2,A4,Th3;
   hence f is_Gateaux_differentiable_in x0,z by A2,Def3;
   hence Gateaux_diff(f,x0,z)=diff(f,x0).z by A2,A5,Def4;
   hence thesis by A2,A4;
end;

reserve U for non trivial RealNormSpace;
reserve f1 for PartFunc of S,T;
reserve f2 for PartFunc of T,U;

theorem Th7:
for R be REST of S,T st R/.0.S=0.T
 for e be Real st e > 0
  ex d be Real st
   d > 0 &
   for h be Point of S st ||.h.|| < d holds  ||.R/.h.|| <= e*||.h.||
proof
   let R be REST of S,T such that
A1:R/.0.S=0.T;
   let e be Real such that
A2:e > 0;
   R is total by NDIFF_1:def 5; then
   consider d be Real such that
A3:d > 0  and
A4:(for z be Point of S st z <> 0.S & ||.z.|| < d holds
      ( ||.z.||"* ||. R/.z .||) < e ) by A2,NDIFF_1:26;
   take d;
   now
     let h be Point of S such that
A5:  ||.h.|| < d;
     now per cases;
       case
A6:    h  <> 0.S;
A7:      ||.h.|| <> 0 by A6,NORMSP_1:def 2;
A8:      0 <= ||.h.|| by NORMSP_1:8;
         ||.h.||"*||. R/.h .|| <= e by A4,A5,A6; then
         ||.h.||*(||.h.||"*||. R/.h .||) <=  ||.h.||*e by A8,AXIOMS:25; then
         ||.h.||*||.h.||"*||. R/.h .|| <=   e* ||.h.||; then
         1*||. R/.h .|| <=   e* ||.h.|| by A7,XCMPLX_0:def 7;
         hence ||. R/.h .|| <= e* ||.h.||;
       end;
       case
A9:    h  = 0.S;
A10:      0 <= ||.h.|| by NORMSP_1:8;
         0* ||.h.|| <= e* ||.h.|| by A2,A10,AXIOMS:25;
         hence ||. R/.h .|| <= e* ||.h.|| by A1,A9,NORMSP_1:def 2;
       end;
     end;
     hence   ||. R/.h .|| <= e* ||.h.||;
   end;
   hence thesis by A3;
end;

theorem
 for R be REST of T,U st R/.0.T=0.U
 for L be bounded LinearOperator of S,T holds
  R*L is REST of S,U
proof
   let R be REST of T,U such that
A1:R/.0.T=0.U;
   let L be bounded LinearOperator of S,T;
   consider K be Real such that
A2: 0 <= K and
A3: for h be Point of S holds
     ||.L.h.|| <= K * ||.h.|| by LOPBAN_1:def 9;
A4:0 + K  < 1 + K by REAL_1:67;
A5:dom L = the carrier of S by FUNCT_2:def 1;
   R is total by NDIFF_1:def 5; then
   dom R = the carrier of T by PARTFUN1:def 4; then
   A6: rng L c= dom R; then
   dom(R*L) = dom L by RELAT_1:46
           .= the carrier of S by FUNCT_2:def 1; then
A7:R*L is total by PARTFUN1:def 4;
   now
     let e be Real such that
A8:  e > 0;
     set e1=e/(1 + K);
A9:  0/(1 + K) < e/(1 + K) by A2,A4,A8,REAL_1:73;
     consider d be Real such that
A10:   0 < d and
A11:  for h be Point of  T st ||.h.|| < d holds
      ||.R/.h.|| <= e1*||.h.|| by A1,A9,Th7;
     set d1=d/( 1 +  K );
A12:  0/(1 + K) < d/(1 + K) by A2,A4,A10,REAL_1:73;
     now
       let h be Point of S such that
A13:     h <> 0.S  and
A14:    ||.h.|| < d1;
A15:   R/.(L.h) = R/.(L/.h) by A5,FINSEQ_4:def 4
               .=(R*L)/.h by A5,A6,PARTFUN2:10;
A16:   ||.h.||  <> 0 by A13,NORMSP_1:def 2; then
A17:   ||.h.|| > 0 by NORMSP_1:8; then
A18:    ||.h.||" > 0 by REAL_1:72;
A19:   ||.L.h.|| <= K * ||.h.|| by A3;
       K * ||.h.||  < ( K +1) * ||.h.|| by A4,A17,REAL_1:70; then
A20:   ||.L.h.|| < ( K +1) * ||.h.|| by A19,AXIOMS:22;
       ( K +1) * ||.h.|| <   ( K +1) *d1 by A2,A4,A14,REAL_1:70; then
       ||.L.h.|| < ( K +1) * d1 by A20,AXIOMS:22; then
       ||.L.h.|| < d by A2,A4,XCMPLX_1:88; then
A21:   ||.R/.(L.h).|| <= e1*||.L.h.|| by A11;
       e1*||.L.h.||  <  e1* ((K +1) * ||.h.||) by A9,A20,REAL_1:70; then
       ||.R/.(L.h).|| < e1* ((K +1) * ||.h.||) by A21,AXIOMS:22; then
       ||.h.||"* ||.(R*L)/.h.|| <  ||.h.||"* (e1* ( K +1) * ||.h.||)
        by A15,A18,REAL_1:70; then
       ||.h.||"* ||.(R*L)/.h.|| < ||.h.||*||.h.||"*e1* ( K +1);  then
       ||.h.||"* ||.(R*L)/.h.|| < 1*e1* ( K +1) by A16,XCMPLX_0:def 7;
       hence ||.h.||"* ||.(R*L)/.h.|| < e by A2,A4,XCMPLX_1:88;
     end;
     hence ex  d1 be Real st
            d1 > 0 &
            for h be Point of S st h <> 0.S  &  ||.h.|| < d1 holds
             ||.h.||"* ||.(R*L)/.h.|| < e by A12;
   end;
   hence thesis by A7,NDIFF_1:26;
end;

theorem Th9:
 for  R be REST of S,T
 for  L be bounded LinearOperator of T,U holds
  L*R is REST of S,U
proof
   let  R be REST of S,T;
   let   L be bounded LinearOperator of T,U;
   consider  K be Real such that
A1: 0 <= K and
A2: for z be Point of T holds ||.L.z.|| <= K * ||.z.|| by LOPBAN_1:def 9;
A3:0 + K  < 1 + K by REAL_1:67; then
A4:0 <  1 +  K by A1;
A5:dom L = the carrier of T by FUNCT_2:def 1;
A6:R is total by NDIFF_1:def 5; then
A7:dom R = the carrier of S by PARTFUN1:def 4;
A8:rng R c= dom L by A5; then
   dom(L*R) = dom R by RELAT_1:46
           .= the carrier of S by A6,PARTFUN1:def 4; then
A9:L*R is total by PARTFUN1:def 4;
   now
     let ee be Real such that
A10: ee > 0;
     set e=ee/2;
A11: e > 0 by A10,SEQ_2:3;
A12:e < ee by A10,SEQ_2:4;
     set e1=e/( 1 +  K );
A13:  0/(1 + K) < e/(1 + K) by A1,A3,A11,REAL_1:73;
     R is total by NDIFF_1:def 5; then
     consider d be Real such that
A14:   0 <  d  and
A15:   for h be Point of  S st h <> 0.S &  ||.h.|| < d holds
        ||.h.||"* ||.R/.h.|| < e1 by A13,NDIFF_1:26;
     now
       let h be Point of S such that
A16:     h <> 0.S  and
A17:     ||.h.||   < d;
A18:   L.(R/.h) = L/.(R/.h) by A5,FINSEQ_4:def 4
               .=(L*R)/.h by A7,A8,PARTFUN2:10;
       ||.h.||  <> 0 by A16,NORMSP_1:def 2; then
       ||.h.|| > 0 by NORMSP_1:8; then
A19:    ||.h.||" > 0 by REAL_1:72;
A20:   ||.L.(R/.h).|| <= K * ||.R/.h.|| by A2;
       0 <=  ||.R/.h.|| by NORMSP_1:8; then
       K * ||.R/.h.||  <= ( K +1) * ||.R/.h.|| by A3,AXIOMS:25; then
       ||.L.(R/.h).|| <= ( K +1) * ||.R/.h.|| by A20,AXIOMS:22; then
A21:   ||.h.||"* ||.L.(R/.h).|| <=   ||.h.||"*(( K +1) * ||.R/.h.|| )
      by A19,AXIOMS:25;
       ||.h.||"* ||.(R/.h).||   <  e1 by A15,A16,A17; then
       ( K +1) *( ||.h.||"* ||.R/.h.||) <=( K +1) *e1 by A4,AXIOMS:25; then
       ( K +1) *( ||.h.||"* ||.R/.h.||) <=e by A1,A3,XCMPLX_1:88; then
       ||.h.||"* ||.L.(R/.h).|| <= e by A21,AXIOMS:22;
       hence ||.h.||"* ||.(L*R)/.h.|| < ee by A12,A18,AXIOMS:22;
     end;
     hence
         ex d be Real st
          d > 0 &
          for h be Point of S st h <> 0.S  &  ||.h.|| < d holds
           ||.h.||"* ||.(L*R)/.h.|| < ee by A14;
   end;
   hence thesis by A9,NDIFF_1:26;
end;

theorem
for R1 be REST of S,T st R1/.0.S=0.T
for R2 be REST of T,U st R2/.0.T=0.U holds
 R2*R1 is REST of S,U
proof
   let R1 be REST of S,T such that
A1:R1/.0.S=0.T;
   let R2 be REST of T,U such that
A2:R2/.0.T=0.U;
A3:R1 is total by NDIFF_1:def 5; then
A4:dom R1 = the carrier of S by PARTFUN1:def 4;
   R2 is total by NDIFF_1:def 5; then
A5:dom R2 = the carrier of T by PARTFUN1:def 4;
A6:rng R1 c= dom R2 by A5; then
   dom(R2*R1) = dom R1 by RELAT_1:46
             .= the carrier of S by A3,PARTFUN1:def 4; then
A7:R2*R1 is total by PARTFUN1:def 4;
   now
     let e0 be Real such that
A8: e0 > 0;
     set e=e0/2;
A9: e > 0 by A8,SEQ_2:3;
A10: e < e0 by A8,SEQ_2:4;
     consider d2 be Real such that
A11:   0 <  d2  and
A12:   for z be Point of  T st ||.z.|| < d2 holds
        ||.R2/.z.|| <= e*||.z.|| by A2,A9,Th7;
     consider d1 be Real such that
A13:   0 <  d1  and
A14:   for h be Point of  S st ||.h.|| < d1 holds
       ||.R1/.h.|| <=1* ||.h.|| by A1,Th7;
     set d=min(d1,d2);
A15:  d <=d1 & d <=d2 by SQUARE_1:35;
A16:  0 < d by A11,A13,SQUARE_1:38;
     now
       let h be Point of  S such that
A17:     h <> 0.S  and
A18:     ||.h.|| < d;
A19:   R2/.(R1/.h) =(R2*R1)/.h by A4,A6,PARTFUN2:10;
       ||.h.|| < d1 by A15,A18,AXIOMS:22; then
A20:   ||.R1/.h.|| <=1* ||.h.|| by A14; then
       ||.R1/.h.|| < d by A18,AXIOMS:22; then
       ||.R1/.h.|| < d2 by A15,AXIOMS:22; then
A21:   ||.R2/.(R1/.h).|| <= e*||.R1/.h.|| by A12;
       e*||.R1/.h.|| <= e* ||.h.|| by A9,A20,AXIOMS:25; then
A22:   ||.R2/.(R1/.h).|| <= e*||.h.|| by A21,AXIOMS:22;
A23:   ||.h.||  <> 0 by A17,NORMSP_1:def 2; then
       ||.h.|| > 0 by NORMSP_1:8; then
       ||.h.||" > 0 by REAL_1:72; then
       ||.h.||"* ||.R2/.(R1/.h).|| <=  ||.h.||"*(e*||.h.||)
      by A22,AXIOMS:25; then
       ||.h.||"* ||.R2/.(R1/.h).|| <=  ||.h.||"*||.h.||*e; then
       ||.h.||"* ||.R2/.(R1/.h).|| <= 1*e by A23,XCMPLX_0:def 7;
       hence
       ||.h.||"* ||.(R2*R1)/.h.|| < e0 by A10,A19,AXIOMS:22;
     end;
     hence
     ex d be Real st
      d > 0 &
      for h be Point of S st h <> 0.S & ||.h.|| < d holds
       ||.h.||"* ||.(R2*R1)/.h.|| < e0 by A16;
   end;
   hence thesis by A7,NDIFF_1:26;
end;

theorem Th11:
for R1 be REST of S,T st R1/.0.S=0.T
for R2 be REST of T,U st R2/.0.T=0.U
for L be bounded LinearOperator of S,T holds
 R2*(L+R1) is REST of S,U
proof
   let R1 be REST of S,T such that
A1:R1/.0.S=0.T;
   let R2 be REST of T,U such that
A2:R2/.0.T=0.U;
   let L be bounded LinearOperator of S,T;
   consider  K be Real such that
A3: 0 <= K and
A4: for h be Point of S holds
     ||.L.h.|| <= K * ||.h.|| by LOPBAN_1:def 9;
   consider d0 be Real such that
A5:0 <  d0  and
A6:for h be Point of  S st ||.h.|| < d0 holds
     ||.R1/.h.|| <=1* ||.h.|| by A1,Th7;
A7:0 + K  < 1 + K by REAL_1:67;
A8:R1 is total by NDIFF_1:def 5;
   R2 is total by NDIFF_1:def 5; then
A9:dom R2 = the carrier of T by PARTFUN1:def 4;
A10:dom L  = the carrier of S by FUNCT_2:def 1; then
A11:L is total by PARTFUN1:def 4;
A12:L+R1 is total by A8,A11,VFUNCT_1:38; then
A13:dom(L+R1)=the carrier of S by PARTFUN1:def 4;
A14:rng (L+R1) c= dom R2 by A9;
   dom (R2*(L+R1)) = dom(L+R1) by A14,RELAT_1:46
                  .=the carrier of S by A12,PARTFUN1:def 4; then
A15:R2*(L+R1) is total by PARTFUN1:def 4;
   now
     let ee be Real such that
A16: ee > 0;
     set e=ee/2;
A17: e > 0 by A16,SEQ_2:3;
A18:e < ee by A16,SEQ_2:4;
     set e1=e/( 1 +  K );
A19:  0/(1 + K) < e/(1 + K) by A3,A7,A17,REAL_1:73;
     consider d be Real such that
A20:   0 <  d  and
A21:   for z be Point of  T st ||.z.|| < d holds
       ||.R2/.z.|| <= e1*||.z.|| by A2,A19,Th7;
     set d1=d/( 1 +  K );
A22: 0/(1 + K) < d/(1 + K) by A3,A7,A20,REAL_1:73;
     set dd1=min(d0,d1);
A23: dd1 <=d0 & dd1 <=d1 by SQUARE_1:35;
A24:  0 < dd1 by A5,A22,SQUARE_1:38;
     now
       let h be Point of S such that
A25:     h <> 0.S and
A26:    ||.h.|| < dd1;
A27:    ||.h.|| < d1 by A23,A26,AXIOMS:22;
A28:   R2/.(L.h+R1/.h) = R2/.(L/.h+R1/.h) by A10,FINSEQ_4:def 4
                       .=R2/.((L+R1)/.h) by A13,VFUNCT_1:def 1
                       .=(R2*(L+R1))/.h by A13,A14,PARTFUN2:10;
A29:   ||.h.||  <> 0 by A25,NORMSP_1:def 2; then
       ||.h.|| > 0 by NORMSP_1:8; then
A30:    ||.h.||" > 0 by REAL_1:72;
A31:   ||.L.h.|| <= K * ||.h.|| by A4;
       ||.h.|| < d0 by A23,A26,AXIOMS:22; then
A32:  ||.R1/.h.|| <=1* ||.h.|| by A6;
A33:  ||.L.h+R1/.h.|| <=  ||.L.h.|| +  ||.R1/.h.|| by NORMSP_1:def 2;
       ||.L.h.|| +  ||.R1/.h.||  <= K * ||.h.||  + 1* ||.h.||
        by A31,A32,REAL_1:55; then
A34:   ||.L.h+R1/.h.|| <= ( K +1) * ||.h.|| by A33,AXIOMS:22;
       ( K +1) * ||.h.|| <   ( K +1) *d1 by A3,A7,A27,REAL_1:70; then
       ||.L.h+R1/.h.|| < ( K +1) * d1 by A34,AXIOMS:22; then
       ||.L.h+R1/.h.|| < d by A3,A7,XCMPLX_1:88; then
A35:   ||.R2/.(L.h+R1/.h).|| <= e1*||.L.h+R1/.h.|| by A21;
       e1*||.L.h+R1/.h.|| <= e1* ((K +1) * ||.h.||) by A19,A34,AXIOMS:25; then
       ||.R2/.(L.h+R1/.h).|| <= e1* ((K +1) * ||.h.||) by A35,AXIOMS:22; then
       ||.h.||"* ||.(R2*(L+R1))/.h.|| <=  ||.h.||"* (e1* ( K +1) * ||.h.||)
             by A28,A30,AXIOMS:25; then
       ||.h.||"* ||.(R2*(L+R1))/.h.|| <=  ||.h.||*||.h.||"*e1* ( K +1); then
       ||.h.||"* ||.(R2*(L+R1))/.h.|| <=  1*e1* ( K +1)
             by A29,XCMPLX_0:def 7; then
       ||.h.||"* ||.(R2*(L+R1))/.h.|| <= e by A3,A7,XCMPLX_1:88;
       hence ||.h.||"* ||.(R2*(L+R1))/.h.|| < ee by A18,AXIOMS:22;
     end;
     hence
     ex dd1 be Real st
      dd1 > 0 &
      for h be Point of S st h <> 0.S & ||.h.|| < dd1 holds
       ||.h.||"* ||.(R2*(L+R1))/.h.|| < ee by A24;
   end;
   hence thesis by A15,NDIFF_1:26;
end;

theorem Th12:
for R1 be REST of S,T st R1/.0.S=0.T
for R2 be REST of T,U st R2/.0.T=0.U
for L1 be bounded LinearOperator of S,T
for L2 be bounded LinearOperator of T,U holds
  L2*R1+ R2*(L1+R1) is REST of S,U
proof
   let R1 be REST of S,T such that
A1:R1/.0.S=0.T;
   let R2 be REST of T,U such that
A2:R2/.0.T=0.U;
   let L1 be bounded LinearOperator of S,T;
   let L2 be bounded LinearOperator of T,U;
A3:L2*R1 is REST of S,U by Th9;
   R2*(L1+R1)  is REST of S,U by A1,A2,Th11;
   hence thesis by A3,NDIFF_1:33;
end;

theorem
for f1 be PartFunc of S,T st f1 is_differentiable_in x0
for f2 be PartFunc of T,U st f2 is_differentiable_in (f1/.x0) holds
 f2*f1 is_differentiable_in x0 &
 diff(f2*f1,x0) = diff(f2,f1/.x0)*diff(f1,x0)
proof
   let  f1 be PartFunc of S,T such that
A1:f1 is_differentiable_in x0;
   let  f2 be PartFunc of T,U such that
A2:f2 is_differentiable_in f1/.x0;
   consider  N1 being Neighbourhood of x0 such that
A3:N1 c= dom f1 and
A4:ex R1 be REST of S,T st
      R1/.0.S=0.T &
      R1 is_continuous_in 0.S &
      for x be Point of S st x in N1 holds
        f1/.x-f1/.x0 = diff(f1,x0).(x-x0) + R1/.(x-x0) by A1,NDIFF_1:52;
   consider  N2 being Neighbourhood of f1/.x0 such that
A5:N2 c= dom f2 and
A6:ex R2 be REST of T,U st
      R2/.0.T=0.U &
      R2 is_continuous_in 0.T &
      for y be Point of T st y in N2 holds
        f2/.y-f2/.(f1/.x0) = diff(f2,f1/.x0).(y-f1/.x0) + R2/.(y-f1/.x0)
       by A2,NDIFF_1:52;
   consider R1 be REST of S,T such that
A7:R1/.0.S=0.T &
    for x be Point of S st x in N1 holds
      f1/.x-f1/.x0 = diff(f1,x0).(x-x0) + R1/.(x-x0) by A4;
   consider R2 be REST of T,U such that
A8:R2/.0.T=0.U &
    for y be Point of T st y in N2 holds
      f2/.y-f2/.(f1/.x0) = diff(f2,f1/.x0).(y-f1/.x0) + R2/.(y-f1/.x0) by A6;
   f1 is_continuous_in x0 by A1,NDIFF_1:49; then
   consider N3 be Neighbourhood of x0 such that
A9: f1.:N3 c= N2 by NFCONT_1:17;
   consider N be Neighbourhood of x0 such that
A10: N c= N1 & N c= N3 by NDIFF_1:1;
A11:N c= dom (f2*f1)
   proof
     let x be set;
     assume
A12:  x in N; then
A13:  x in N1 by A10;
     reconsider x' = x as Point of S by A12;
     f1.x' in f1.:N3 by A3,A10,A12,A13,FUNCT_1:def 12; then
     f1.x' in N2 by A9;
     hence thesis by A3,A5,A13,FUNCT_1:21;
   end;
A14:R_NormSpace_of_BoundedLinearOperators(S,T)
      = NORMSTR (# BoundedLinearOperators(S,T),
         Zero_(BoundedLinearOperators(S,T),
         R_VectorSpace_of_LinearOperators(S,T)),
         Add_(BoundedLinearOperators(S,T),
         R_VectorSpace_of_LinearOperators(S,T)),
          Mult_(BoundedLinearOperators(S,T),
          R_VectorSpace_of_LinearOperators(S,T)),
          BoundedLinearOperatorsNorm(S,T) #)
      by LOPBAN_1:def 15;
   reconsider
   LB1 = diff(f1,x0) as Element of BoundedLinearOperators(S,T) by A14;
A15:LB1=modetrans(LB1,S,T) by LOPBAN_1:def 12;
A16:R_NormSpace_of_BoundedLinearOperators(T,U)
       = NORMSTR (# BoundedLinearOperators(T,U),
         Zero_(BoundedLinearOperators(T,U),
         R_VectorSpace_of_LinearOperators(T,U)),
         Add_(BoundedLinearOperators(T,U),
         R_VectorSpace_of_LinearOperators(T,U)),
          Mult_(BoundedLinearOperators(T,U),
          R_VectorSpace_of_LinearOperators(T,U)),
          BoundedLinearOperatorsNorm(T,U) #)
      by LOPBAN_1:def 15;
   reconsider
   LB2 = diff(f2,f1/.x0) as Element of BoundedLinearOperators(T,U) by A16;
A17:LB2=modetrans(LB2,T,U) by LOPBAN_1:def 12;
   set L1=modetrans(diff(f1,x0),S,T);
   set L2=modetrans(diff(f2,f1/.x0),T,U);
   reconsider R0=L2*R1+R2*(L1+R1) as REST of S,U by A7,A8,Th12;
A18:now
     let x be Point of S such that
A19:   x in N;
A20:  x in N1 by A10,A19;
A21:  f1/.x-f1/.x0 = diff(f1,x0).(x-x0) + R1/.(x-x0) by A7,A10,A19;
A22:  x0 in N by NFCONT_1:4;
     x in N1 by A10,A19; then
     f1.x in f1.:N3 by A3,A10,A19,FUNCT_1:def 12; then
     f1.x in N2 by A9; then
A23:  f1/.x in N2 by A3,A20,FINSEQ_4:def 4;
A24:  R1 is total by NDIFF_1:def 5; then
A25: dom R1 = the carrier of S by PARTFUN1:def 4;
     R2 is total by NDIFF_1:def 5; then
A26: dom R2 = the carrier of T by PARTFUN1:def 4;
A27: dom L1  = the carrier of S by FUNCT_2:def 1; then
A28:  L1 is total by PARTFUN1:def 4;
A29:  L1+R1 is total by A24,A28,VFUNCT_1:38; then
A30: dom(L1+R1)=the carrier of S by PARTFUN1:def 4;
A31: rng (L1+R1) c= dom R2 by A26;
A32: dom (R2*(L1+R1)) = dom(L1+R1) by A31,RELAT_1:46
                     .=the carrier of S by A29,PARTFUN1:def 4;
A33: R2/.(L1.(x-x0)+R1/.(x-x0))
       = R2/.(L1/.(x-x0)+R1/.(x-x0)) by A27,FINSEQ_4:def 4
      .=R2/.((L1+R1)/.(x-x0)) by A30,VFUNCT_1:def 1
      .=(R2*(L1+R1))/.(x-x0) by A30,A31,PARTFUN2:10;
A34: dom L2 = the carrier of T by FUNCT_2:def 1;
A35: rng R1 c= dom L2 by A34; then
A36: dom(L2*R1) = dom R1 by RELAT_1:46
               .= the carrier of S by A24,PARTFUN1:def 4;
A37: L2.(R1/.(x-x0)) = L2/.(R1/.(x-x0)) by A34,FINSEQ_4:def 4
                    .=(L2*R1)/.(x-x0) by A25,A35,PARTFUN2:10;
A38: dom (L2*R1+R2*(L1+R1))
        =  (the carrier of S) /\ (the carrier of S) by A32,A36,VFUNCT_1:def 1
        .= the carrier of S;
A39: L2.(L1.(x-x0)) = (L2*L1).(x-x0) by A27,FUNCT_1:23
                   .=(diff(f2,f1/.x0)*diff(f1,x0)).(x-x0) by Def2;
     thus
     (f2*f1)/.x-(f2*f1)/.x0
       =f2/.(f1/.x) -(f2*f1)/.x0 by A11,A19,PARTFUN2:6
      .=f2/.(f1/.x) -f2/.(f1/.x0) by A11,A22,PARTFUN2:6
      .=diff(f2,f1/.x0).(f1/.x-f1/.x0)  + R2/.(f1/.x-f1/.x0) by A8,A23
      .=L2.(L1.(x-x0)) + L2.(R1/.(x-x0))
          + (R2*(L1+R1))/.(x-x0) by A15,A17,A21,A33,LOPBAN_1:def 5
      .=L2.(L1.(x-x0)) + ((L2*R1)/.(x-x0)
          + (R2*(L1+R1))/.(x-x0)) by A37,RLVECT_1:def 6
      .=(diff(f2,f1/.x0)*diff(f1,x0)).(x-x0) + R0/.(x-x0)
        by A38,A39,VFUNCT_1:def 1;
   end;
   hence f2*f1 is_differentiable_in x0 by A11,NDIFF_1:def 6;
   hence
    diff(f2*f1,x0) = diff(f2,f1/.x0)*diff(f1,x0) by A11,A18,NDIFF_1:def 7;
end;
