:: Riemann Indefinite Integral of Functions of Real Variable
::  by Yasunari Shidama , Noboru Endou , Katsumi Wasaki and Katuhiko Kanazashi
::
:: Received June 6, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies INTEGRA1, INTEGRA2, ARYTM_1, RELAT_1, FUNCT_1, SEQFUNC, PARTFUN1,
      REALSET1, SEQ_1, SEQ_2, FUNCT_3, RLVECT_1, BOOLE, RFUNCT_1, FCONT_1,
      ABSVALUE, ORDINAL2, ARYTM_3, SQUARE_1, FDIFF_1, RCOMP_1, PARTFUN2,
      PRE_TOPC, INTEGRA5, FINSEQ_1, FINSEQ_2, FINSEQ_4, ARYTM, SIN_COS,
      INTEGRA7, MEASURE5, PREPOWER;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      XXREAL_0, COMPLEX1, REAL_1, NAT_1, RELSET_1, FUNCT_2, RCOMP_1, PARTFUN1,
      PARTFUN2, PSCOMP_1, FINSEQ_1, SEQ_1, RFUNCT_1, SEQ_2, SEQ_4, SQUARE_1,
      PREPOWER, FCONT_1, FDIFF_1, FINSEQ_2, FINSEQ_4, RVSUM_1, INTEGRA1,
      INTEGRA2, SEQFUNC, INTEGRA5, SIN_COS, TAYLOR_1;
 constructors ARYTM_0, REAL_1, FDIFF_1, FINSEQ_4, PARTFUN1, PARTFUN2, RFUNCT_1,
      INTEGRA2, LIMFUNC1, FCONT_1, PSCOMP_1, NAT_1, BINOP_2, INTEGRA5,
      COMSEQ_3, SIN_COS, PREPOWER, TAYLOR_1, SQUARE_1;
 registrations XREAL_0, RELSET_1, FINSEQ_2, INTEGRA1, SEQ_1, RCOMP_1, INT_1,
      ORDINAL1, MEMBERED, PREPOWER, XBOOLE_0, NUMBERS, VALUED_0;
 requirements NUMERALS, BOOLE, SUBSET, REAL, ARITHM;
 definitions XCMPLX_0, SUBSET_1, INTEGRA1, INTEGRA5;
 theorems TARSKI, PARTFUN1, RVSUM_1, ZFMISC_1, PREPOWER, INTEGRA1, INTEGRA2,
      RFUNCT_1, FUNCT_1, SEQ_1, SEQ_2, FDIFF_1, SEQFUNC, ABSVALUE, XCMPLX_1,
      FINSEQ_1, FINSEQ_2, SQUARE_1, RCOMP_1, FCONT_1, FCONT_3, FDIFF_2,
      RELAT_1, XREAL_0, XXREAL_0, PARTFUN2, FUNCT_2, XBOOLE_0, XBOOLE_1, SEQ_4,
      XREAL_1, COMPLEX1, INTEGRA4, INTEGRA5, SIN_COS, INTEGRA6, TAYLOR_1;
 schemes FUNCT_2, XBOOLE_0;

begin :: Preliminaries

 reserve a,b,r for real number;
 reserve A for non empty set;
 reserve X,x for set;
 reserve f,g,F,G for PartFunc of REAL,REAL;
 reserve n for Element of NAT;

theorem Th1:
for f,g being Function of A,REAL st
 rng f is bounded_above & rng g is bounded_above &
 (for x be set st x in A holds abs(f.x-g.x)<=a) holds
  sup rng f - sup rng g <= a & sup rng g - sup rng f <= a
proof
   let f,g be Function of A,REAL;
   assume
A1: rng f is bounded_above & rng g is bounded_above &
    (for x be set st x in A holds abs(f.x-g.x)<=a);
A2:dom f = A & dom g = A by FUNCT_2:def 1;
then A3:rng f is non empty Subset of REAL &
   rng g is non empty Subset of REAL by RELAT_1:65;
    for b st b in rng f holds b <= sup rng g + a
   proof
    let b;
    assume b in rng f;
    then consider x being Element of A such that
A4:  x in dom f & b=f.x by PARTFUN1:26;
     abs(f.x-g.x)<=a by A1;
    then f.x-g.x <= a by ABSVALUE:12;
then A5: b <= a + g.x by A4,XREAL_1:22;
     g.x in rng g by A2,FUNCT_1:12;
    then g.x <= sup rng g by A1,SEQ_4:def 4;
    then a + g.x <= a + sup rng g by XREAL_1:8;
    hence b <= sup rng g + a by A5,XXREAL_0:2;
   end;
then A6:sup rng f <= sup rng g + a by A3,SEQ_4:62;
    for b st b in rng g holds b <= sup rng f + a
   proof
    let b;
    assume b in rng g;
    then consider x being Element of A such that
A7:  x in dom g & b=g.x by PARTFUN1:26;
     abs(f.x-g.x)<=a by A1;
    then abs(g.x-f.x)<=a by COMPLEX1:146;
    then g.x-f.x <= a by ABSVALUE:12;
then A8: b <= a + f.x by A7,XREAL_1:22;
     f.x in rng f by A2,FUNCT_1:12;
    then f.x <= sup rng f by A1,SEQ_4:def 4;
    then a + f.x <= a + sup rng f by XREAL_1:8;
    hence b <= sup rng f + a by A8,XXREAL_0:2;
   end;
   then sup rng g  <= sup rng f + a by A3,SEQ_4:62;
   hence thesis by A6,XREAL_1:22;
end;

theorem Th2:
for f,g being Function of A,REAL st
 rng f is bounded_below & rng g is bounded_below &
 (for x be set st x in A holds abs(f.x-g.x)<=a) holds
  inf rng f - inf rng g <= a & inf rng g - inf rng f <= a
proof
   let f,g be Function of A,REAL;
   assume
A1: rng f is bounded_below & rng g is bounded_below &
    (for x be set st x in A holds abs(f.x-g.x)<=a);
A2:dom f = A & dom g = A by FUNCT_2:def 1;
then A3:rng f is non empty Subset of REAL &
   rng g is non empty Subset of REAL by RELAT_1:65;
    for b st b in rng f holds inf rng g -a <=b
   proof
    let b;
    assume b in rng f;
    then consider x being Element of A such that
A4:  x in dom f & b=f.x by PARTFUN1:26;
     abs(f.x-g.x)<=a by A1;
    then abs(g.x-f.x)<=a by COMPLEX1:146;
    then g.x-f.x <= a by ABSVALUE:12;
then A5: g.x - a <=b  by A4,XREAL_1:14;
     g.x in rng g by A2,FUNCT_1:12;
    then inf rng g <=g.x by A1,SEQ_4:def 5;
    then inf rng g - a <= g.x-a by XREAL_1:11;
    hence inf rng g - a <= b by A5,XXREAL_0:2;
   end;
then A6:inf rng g-a <= inf rng f by A3,SEQ_4:60;
    for b st b in rng g holds  inf rng f -a <= b
   proof
    let b;
    assume b in rng g;
    then consider x being Element of A such that
A7:  x in dom g & b=g.x by PARTFUN1:26;
     abs(f.x-g.x)<=a by A1;
    then f.x-g.x <= a by ABSVALUE:12;
then A8: f.x - a <= b by A7,XREAL_1:14;
     f.x in rng f by A2,FUNCT_1:12;
    then inf rng f <= f.x by A1,SEQ_4:def 5;
    then inf rng f - a <= f.x -a by XREAL_1:11;
    hence inf rng f - a <= b by A8,XXREAL_0:2;
   end;
   then inf rng f -a <= inf rng g by A3,SEQ_4:60;
   hence thesis by A6,XREAL_1:14;
end;

theorem Th3:
 f|X is_bounded_on X implies f is_bounded_on X
proof
   assume f|X is_bounded_on X;
   then consider r be real number such that
A1: for z be set st z in X /\ dom(f|X) holds abs((f|X).z) <= r by RFUNCT_1:90;
    now let z be set;
    assume
A2:  z in X /\ dom f;
A3: dom(f|X) = dom f /\ X by FUNCT_1:68;
    then X /\ dom(f|X) = (X /\ X) /\ dom f by XBOOLE_1:16;
    then abs((f|X).z) <= r by A1,A2;
    hence abs(f.z) <= r by A2,A3,FUNCT_1:68;
   end;
   hence f is_bounded_on X by RFUNCT_1:90;
end;

theorem Th4:
for x be Real st
 x in X & f|X is_differentiable_in x holds f is_differentiable_in x
proof
   let x be Real;
   assume A1: x in X & f|X is_differentiable_in x;
   then consider N be Neighbourhood of x such that
A2: N c= dom(f|X) &
    ex L be LINEAR, R be REST st
     for x1 be Real st x1 in N holds (f|X).x1 - (f|X).x = L.(x1-x) + R.(x1-x)
       by FDIFF_1:def 5;
    N c= dom f /\ X by A2,FUNCT_1:68;
then A3:N c= dom f by XBOOLE_1:18;
   consider L be LINEAR, R be REST such that
A4: for x1 be Real st x1 in N holds (f|X).x1 - (f|X).x = L.(x1-x) + R.(x1-x)
     by A2;
    now let x1 be Real;
    assume A5: x1 in N;
    then (f|X).x1 = f.x1 & (f|X).x = f.x by A1,A2,FUNCT_1:70,72;
    hence f.x1 - f.x = L.(x1-x) + R.(x1-x) by A4,A5;
   end;
   hence thesis by A3,FDIFF_1:def 5;
end;

theorem
 f|X is_differentiable_on X implies f is_differentiable_on X
proof
   assume A1: f|X is_differentiable_on X;
   then X c= dom(f|X) & for x be Real st x in X holds (f|X)|X
is_differentiable_in x
     by FDIFF_1:def 7;
   then X c= dom f /\ X by FUNCT_1:68;
then A2:X c= dom f by XBOOLE_1:18;
    now let x be Real;
    assume A3:x in X;
    then (f|X)|X is_differentiable_in x by A1,FDIFF_1:def 7;
    hence f|X is_differentiable_in x by A3,Th4;
   end;
   hence thesis by A2,FDIFF_1:def 7;
end;

theorem Th6:
f is_differentiable_on X & g is_differentiable_on X implies
  f+g is_differentiable_on X & f-g is_differentiable_on X &
  f(#)g is_differentiable_on X
proof
   assume A1:f is_differentiable_on X & g is_differentiable_on X;
   then reconsider Z = X as Subset of REAL by FDIFF_1:15;
   reconsider Z as open Subset of REAL by A1,FDIFF_1:17;
    X c= dom f & (for x be Real st x in X holds f|X is_differentiable_in x) &
   X c= dom g & (for x be Real st x in X holds g|X is_differentiable_in x)
     by A1,FDIFF_1:def 7;
   then Z c= dom f /\ dom g by XBOOLE_1:19;
   then Z c= dom(f+g) & Z c= dom(f-g) & Z c= dom(f(#)g) by SEQ_1:def 3,def 4
,def 5;
   hence thesis by A1,FDIFF_1:26,27,29;
end;

theorem Th7:
f is_differentiable_on X implies r(#)f is_differentiable_on X
proof
   assume A1:f is_differentiable_on X;
   then reconsider Z = X as Subset of REAL by FDIFF_1:15;
   reconsider Z as open Subset of REAL by A1,FDIFF_1:17;
   reconsider r1 = r as Real by XREAL_0:def 1;
    X c= dom f & (for x be Real st x in X holds f|X is_differentiable_in x)
     by A1,FDIFF_1:def 7;
   then Z c= dom(r(#)f) by SEQ_1:def 6;
   then (r1(#)f) is_differentiable_on X by A1,FDIFF_1:28;
   hence thesis;
end;

theorem Th8:
(for x be set st x in X holds g.x <> 0) &
f is_differentiable_on X & g is_differentiable_on X
 implies f/g is_differentiable_on X
proof
   assume that
A1: for x be set st x in X holds g.x <> 0 and
A2: f is_differentiable_on X & g is_differentiable_on X;
   reconsider Z = X as Subset of REAL by A2,FDIFF_1:15;
   reconsider Z as open Subset of REAL by A2,FDIFF_1:17;
    for x be Real st x in Z holds g.x <> 0 by A1;
   hence thesis by A2,FDIFF_2:21;
end;

theorem
 (for x be set st x in X holds f.x <> 0) & f is_differentiable_on X
 implies f^ is_differentiable_on X
proof
   assume that
A1: for x be set st x in X holds f.x <> 0 and
A2: f is_differentiable_on X;
   reconsider Z = X as Subset of REAL by A2,FDIFF_1:15;
   reconsider Z as open Subset of REAL by A2,FDIFF_1:17;
    for x be Real st x in Z holds f.x <> 0 by A1;
   hence thesis by A2,FDIFF_2:22;
end;

theorem Th10:
  a <= b & [' a,b '] c= X & F is_differentiable_on X &
    F`|X is_integrable_on [' a,b '] & F`|X is_bounded_on [' a,b ']
implies
  F.b = integral(F`|X,a,b) + F.a
proof
   assume that
A1: a <= b and
A2: [' a,b '] c= X & F is_differentiable_on X &
    F`|X is_integrable_on [' a,b '] & F`|X is_bounded_on [' a,b '];
A3:[' a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
   then [.a,b.] = [.inf [.a,b.], sup [.a,b.].] by INTEGRA1:5;
then A4:a=inf [.a,b.] & b=sup [.a,b.] by A3,INTEGRA1:6;
    integral(F`|X,a,b) = integral(F`|X,[' a,b ']) by A1,INTEGRA5:def 5;
   then integral(F`|X,a,b) = F.(sup [' a,b ']) - F.(inf [' a,b '])
    by A2,INTEGRA5:13;
   hence thesis by A3,A4;
end;

begin :: The Definition of Indefinite Integral

definition let X be set, f be PartFunc of REAL,REAL;
  func IntegralFuncs(f,X) -> set means :Def1:
   x in it iff
   ex F be PartFunc of REAL,REAL st
    x = F & F is_differentiable_on X & F`|X = f|X;
existence
proof
   defpred P[set] means
    ex F be PartFunc of REAL,REAL st
     $1 = F & F is_differentiable_on X & F`|X = f|X;
   consider Z being set such that
A1: x in Z iff x in PFuncs(REAL,REAL) & P[x] from XBOOLE_0:sch 1;
   take Z;
   let x;
   thus x in Z implies
         ex F being PartFunc of REAL,REAL st
          x = F & F is_differentiable_on X & F`|X = f|X by A1;
   given F being PartFunc of REAL,REAL such that
A2: x = F and
A3: F is_differentiable_on X and
A4: F`|X = f|X;
    F in PFuncs(REAL,REAL) by PARTFUN1:119;
   hence x in Z by A1,A2,A3,A4;
end;
uniqueness
proof
   let Z1,Z2 be set such that
A5: x in Z1 iff ex F be PartFunc of REAL,REAL st
                 x = F & F is_differentiable_on X & F`|X = f|X and
A6: x in Z2 iff ex F be PartFunc of REAL,REAL st
                 x = F & F is_differentiable_on X & F`|X = f|X;
    x in Z1 iff x in Z2
   proof
     x in Z1 iff ex F be PartFunc of REAL,REAL st
       x = F & F is_differentiable_on X & F`|X = f|X by A5;
    hence thesis by A6;
   end;
   hence thesis by TARSKI:2;
end;
end;

definition let X be set, F,f be PartFunc of REAL,REAL;
  pred F is_integral_of f,X means :Def2:
    F in IntegralFuncs(f,X);
end;

Lm1:
F is_integral_of f,X iff F is_differentiable_on X & F`|X = f|X
proof
   hereby assume F is_integral_of f,X;
    then F in IntegralFuncs(f,X) by Def2;
    then ex F' be PartFunc of REAL,REAL st
     F = F' & F' is_differentiable_on X & F'`|X = f|X by Def1;
    hence F is_differentiable_on X & F`|X = f|X;
   end;
   assume F is_differentiable_on X & F`|X = f|X;
   then F in IntegralFuncs(f,X) by Def1;
   hence thesis by Def2;
end;

theorem Th11:
  F is_integral_of f,X implies X c= dom F
proof
   assume F is_integral_of f,X;
   then F in IntegralFuncs(f,X) by Def2;
   then ex G be PartFunc of REAL,REAL st
    F = G & G is_differentiable_on X & G`|X = f|X by Def1;
   hence X c= dom F by FDIFF_1:def 7;
end;

theorem
   F is_integral_of f,X & G is_integral_of g,X
    implies F+G is_integral_of f+g,X & F-G is_integral_of f-g,X
proof
   assume F is_integral_of f,X & G is_integral_of g,X;
then A1:F is_differentiable_on X & F`|X = f|X &
   G is_differentiable_on X & G`|X = g|X by Lm1;
then A2:F+G is_differentiable_on X & F-G is_differentiable_on X by Th6;
    dom(f|X) = X & dom(g|X) = X by A1,FDIFF_1:def 8;
   then dom f /\ X = X & dom g /\ X = X by FUNCT_1:68;
   then X c= dom f & X c= dom g by XBOOLE_1:18;
   then X c= dom f /\ dom g by XBOOLE_1:19;
then A3:X c= dom(f+g) & X c= dom(f-g) by SEQ_1:def 3,def 4;
   then X = dom((f+g)|X) & X = dom((f-g)|X) by RELAT_1:91;
then A4:dom((F+G)`|X) = dom((f+g)|X) & dom((F-G)`|X) = dom((f-g)|X)
     by A2,FDIFF_1:def 8;
    now let x be Element of REAL;
    assume x in dom((F+G)`|X);
then A5: x in X by A2,FDIFF_1:def 8;
then A6: ((F+G)`|X).x = diff(F+G,x) by A2,FDIFF_1:def 8;
     F|X is_differentiable_in x & G|X is_differentiable_in x
      by A1,A5,FDIFF_1:def 7;
    then F is_differentiable_in x & G is_differentiable_in x by A5,Th4;
then A7: ((F+G)`|X).x = diff(F,x) + diff(G,x) by A6,FDIFF_1:21;
     (F`|X).x = diff(F,x) & (G`|X).x = diff(G,x) by A1,A5,FDIFF_1:def 8;
    then f.x = diff(F,x) & g.x = diff(G,x) by A1,A5,FUNCT_1:72;
    then ((F+G)`|X).x = (f+g).x by A3,A5,A7,SEQ_1:def 3;
    hence ((F+G)`|X).x = ((f+g)|X).x by A5,FUNCT_1:72;
   end;
   then (F+G)`|X = (f+g)|X by A4,PARTFUN1:34;
   hence F+G is_integral_of f+g,X by A2,Lm1;
    now let x be Element of REAL;
    assume x in dom((F-G)`|X);
then A8: x in X by A2,FDIFF_1:def 8;
then A9: ((F-G)`|X).x = diff(F-G,x) by A2,FDIFF_1:def 8;
     F|X is_differentiable_in x & G|X is_differentiable_in x
      by A1,A8,FDIFF_1:def 7;
    then F is_differentiable_in x & G is_differentiable_in x by A8,Th4;
then A10: ((F-G)`|X).x = diff(F,x) - diff(G,x) by A9,FDIFF_1:22;
     (F`|X).x = diff(F,x) & (G`|X).x = diff(G,x) by A1,A8,FDIFF_1:def 8;
    then f.x = diff(F,x) & g.x = diff(G,x) by A1,A8,FUNCT_1:72;
    then ((F-G)`|X).x = (f-g).x by A3,A8,A10,SEQ_1:def 4;
    hence ((F-G)`|X).x = ((f-g)|X).x by A8,FUNCT_1:72;
   end;
   then (F-G)`|X = (f-g)|X by A4,PARTFUN1:34;
   hence F-G is_integral_of f-g,X by A2,Lm1;
end;

theorem
   F is_integral_of f,X implies r(#)F is_integral_of r(#)f,X
proof
   assume F is_integral_of f,X;
then A1:F is_differentiable_on X & F`|X = f|X by Lm1;
then A2:r(#)F is_differentiable_on X by Th7;
    dom(f|X) = X by A1,FDIFF_1:def 8;
   then dom f /\ X = X by FUNCT_1:68;
   then X c= dom f by XBOOLE_1:18;
then A3:X c= dom(r(#)f) by SEQ_1:def 6;
   then X = dom((r(#)f)|X) by RELAT_1:91;
then A4:dom((r(#)F)`|X) = dom((r(#)f)|X) by A2,FDIFF_1:def 8;
    now let x be Element of REAL;
    assume x in dom((r(#)F)`|X);
then A5: x in X by A2,FDIFF_1:def 8;
then A6: ((r(#)F)`|X).x = diff(r(#)F,x) by A2,FDIFF_1:def 8;
    reconsider r1 = r as Real by XREAL_0:def 1;
     F|X is_differentiable_in x by A1,A5,FDIFF_1:def 7;
    then F is_differentiable_in x by A5,Th4;
then A7: ((r1(#)F)`|X).x = r1*diff(F,x) by A6,FDIFF_1:23;
     (F`|X).x = diff(F,x) by A1,A5,FDIFF_1:def 8;
    then f.x = diff(F,x) by A1,A5,FUNCT_1:72;
    then ((r(#)F)`|X).x = (r(#)f).x by A3,A5,A7,SEQ_1:def 6;
    hence ((r(#)F)`|X).x = ((r(#)f)|X).x by A5,FUNCT_1:72;
   end;
   then (r(#)F)`|X = (r(#)f)|X by A4,PARTFUN1:34;
   hence r(#)F is_integral_of r(#)f,X by A2,Lm1;
end;

theorem
   F is_integral_of f,X & G is_integral_of g,X
    implies F(#)G is_integral_of f(#)G+F(#)g,X
proof
   assume A1:F is_integral_of f,X & G is_integral_of g,X;
then A2:F is_differentiable_on X & F`|X = f|X &
   G is_differentiable_on X & G`|X = g|X by Lm1;
then A3:F(#)G is_differentiable_on X by Th6;
    dom(f|X) = X & dom(g|X) = X by A2,FDIFF_1:def 8;
   then dom f /\ X = X & dom g /\ X = X by FUNCT_1:68;
   then X c= dom f & X c= dom F & X c= dom g & X c= dom G by A1,Th11,
XBOOLE_1:18;
   then X c= dom f /\ dom G & X c= dom F /\ dom g by XBOOLE_1:19;
then A4:X c= dom(f(#)G) & X c= dom(F(#)g) by SEQ_1:def 5;
   then X c= dom(f(#)G) /\ dom(F(#)g) by XBOOLE_1:19;
then A5:X c= dom(f(#)G+F(#)g) by SEQ_1:def 3;
   then X = dom((f(#)G+F(#)g)|X) by RELAT_1:91;
then A6:dom((F(#)G)`|X) = dom((f(#)G+F(#)g)|X) by A3,FDIFF_1:def 8;
    now let x be Element of REAL;
    assume x in dom((F(#)G)`|X);
then A7: x in X by A3,FDIFF_1:def 8;
then A8: ((F(#)G)`|X).x = diff(F(#)G,x) by A3,FDIFF_1:def 8;
     F|X is_differentiable_in x & G|X is_differentiable_in x
      by A2,A7,FDIFF_1:def 7;
    then F is_differentiable_in x & G is_differentiable_in x by A7,Th4;
then A9: ((F(#)G)`|X).x = (G.x)*diff(F,x) + (F.x)*diff(G,x) by A8,FDIFF_1:24;
     (F`|X).x = diff(F,x) & (G`|X).x = diff(G,x) by A2,A7,FDIFF_1:def 8;
    then f.x = diff(F,x) & g.x = diff(G,x) by A2,A7,FUNCT_1:72;
    then (F(#)g).x = (F.x)*diff(G,x) & (f(#)G).x = (G.x)*diff(F,x)
      by A4,A7,SEQ_1:def 5;
    then ((F(#)G)`|X).x = (F(#)g+f(#)G).x by A5,A7,A9,SEQ_1:def 3;
    hence ((F(#)G)`|X).x = ((F(#)g+f(#)G)|X).x by A7,FUNCT_1:72;
   end;
   then (F(#)G)`|X = (F(#)g+f(#)G)|X by A6,PARTFUN1:34;
   hence F(#)G is_integral_of f(#)G+F(#)g,X by A3,Lm1;
end;

theorem
   (for x be set st x in X holds G.x <> 0) &
    F is_integral_of f,X & G is_integral_of g,X implies
    F/G is_integral_of (f(#)G-F(#)g)/(G(#)G),X
proof
   assume that
A1: for x be set st x in X holds G.x <> 0 and
A2: F is_integral_of f,X & G is_integral_of g,X;
A3:F is_differentiable_on X & F`|X = f|X &
   G is_differentiable_on X & G`|X = g|X by A2,Lm1;
then A4:F/G is_differentiable_on X by A1,Th8;
    dom(f|X) = X & dom(g|X) = X by A3,FDIFF_1:def 8;
   then dom f /\ X = X & dom g /\ X = X by FUNCT_1:68;
then A5:X c= dom f & X c= dom F & X c= dom g & X c= dom G
     by A2,Th11,XBOOLE_1:18;
   then X c= dom f /\ dom G & X c= dom F /\ dom g by XBOOLE_1:19;
then A6:X c= dom(f(#)G) & X c= dom(F(#)g) by SEQ_1:def 5;
   then X c= dom(f(#)G) /\ dom(F(#)g) by XBOOLE_1:19;
then A7:X c= dom(f(#)G-F(#)g) by SEQ_1:def 4;
then A8:X = dom((f(#)G-F(#)g)|X) by RELAT_1:91;
    dom((G|X)(#)(G|X)) = dom(G|X) /\ dom(G|X) by SEQ_1:def 5;
   then dom((G|X)(#)(G|X)) = dom G /\ X by FUNCT_1:68;
then A9:dom((G|X)(#)(G|X)) = X by A5,XBOOLE_1:28;
    now assume (G|X)"{0} <> {};
    then consider y be set such that
A10:  y in (G|X)"{0} by XBOOLE_0:def 1;
A11: y in dom(G|X) & (G|X).y in {0} by A10,FUNCT_1:def 13;
    then y in dom G /\ X by FUNCT_1:68;
    then y in X by XBOOLE_0:def 3;
    then G.y <> 0 & (G|X).y = G.y by A1,A11,FUNCT_1:70;
    hence contradiction by A11,TARSKI:def 1;
   end;
   then (dom(G|X) \ (G|X)"{0}) /\ (dom(G|X) \ (G|X)"{0}) = dom(G|X);
   then dom((G|X)(#)(G|X)) \ ((G|X)(#)(G|X))"{0} = dom(G|X) by RFUNCT_1:12;
   then dom((G|X)(#)(G|X)) \ ((G|X)(#)(G|X))"{0} = dom G /\ X by FUNCT_1:68;
   then dom((G|X)(#)(G|X)) \ ((G|X)(#)(G|X))"{0} = X by A5,XBOOLE_1:28;
   then dom((f(#)G-F(#)g)|X) /\ (dom((G|X)(#)(G|X)) \ ((G|X)(#)(G|X))"{0}) = X
     by A8;
   then dom(((f(#)G-F(#)g)|X)/((G|X)(#)(G|X))) = X by RFUNCT_1:def 4;
   then dom(((f(#)G-F(#)g)|X)/((G(#)G)|X)) = X by RFUNCT_1:61;
   then dom(((f(#)G-F(#)g)/(G(#)G))|X) = X by RFUNCT_1:64;
then A12:dom((F/G)`|X) = dom(((f(#)G-F(#)g)/(G(#)G))|X) by A4,FDIFF_1:def 8;
    now let x be Element of REAL;
    assume x in dom((F/G)`|X);
then A13: x in X by A4,FDIFF_1:def 8;
then A14: ((F/G)`|X).x = diff(F/G,x) by A4,FDIFF_1:def 8;
A15:  now let y be set;
     assume A16:y in dom(G|X);
then A17:  (G|X).y = G.y by FUNCT_1:70;
      y in dom G /\ X by A16,FUNCT_1:68;
     then y in X by XBOOLE_0:def 3;
     then G.y <> 0 by A1;
     then not (G|X).y in {0} by A17,TARSKI:def 1;
     hence not y in (G|X)"{0} by FUNCT_1:def 13;
    end;
A18: G.x <> 0 by A1,A13;
     X = dom G /\ X by A5,XBOOLE_1:28;
    then X = dom(G|X) by FUNCT_1:68;
    then x in dom(G|X) & not x in (G|X)"{0} by A13,A15;
    then x in (dom(G|X)\(G|X)"{0}) /\ (dom(G|X)\(G|X)"{0}) by XBOOLE_0:def 4;
then A19: x in dom((G|X)(#)(G|X)) \ ((G|X)(#)(G|X))"{0} by RFUNCT_1:12;
     F|X is_differentiable_in x & G|X is_differentiable_in x
      by A3,A13,FDIFF_1:def 7;
    then F is_differentiable_in x & G is_differentiable_in x by A13,Th4;
then A20: ((F/G)`|X).x
     = (diff(F,x)*G.x - diff(G,x)*F.x)/((G.x)^2) by A14,A18,FDIFF_2:14;
     x in dom((f(#)G-F(#)g)) /\ (dom((G|X)(#)(G|X)) \ ((G|X)(#)(G|X))"{0})
      by A7,A13,A19,XBOOLE_0:def 3;
then A21: x in dom((f(#)G-F(#)g)/((G|X)(#)(G|X))) by RFUNCT_1:def 4;
     (F`|X).x = diff(F,x) & (G`|X).x = diff(G,x) by A3,A13,FDIFF_1:def 8;
    then f.x = diff(F,x) & g.x = diff(G,x) by A3,A13,FUNCT_1:72;
    then (F(#)g).x = (F.x)*diff(G,x) & (f(#)G).x = (G.x)*diff(F,x)
      by A6,A13,SEQ_1:def 5;
then A22: (f(#)G-F(#)g).x = diff(F,x)*G.x - diff(G,x)*F.x by A7,A13,SEQ_1:def 4
;
     G.x = (G|X).x by A13,FUNCT_1:72;
    then (G.x)^2 = (G|X).x * (G|X).x by SQUARE_1:def 3;
    then (G.x)^2 = ((G|X)(#)(G|X)).x by A9,A13,SEQ_1:def 5;
    then ((F/G)`|X).x = ((f(#)G-F(#)g)/((G|X)(#)(G|X))).x
      by A20,A21,A22,RFUNCT_1:def 4;
    then ((F/G)`|X).x = ((f(#)G-F(#)g)/(G(#)G)|X).x by RFUNCT_1:61;
    hence ((F/G)`|X).x = (((f(#)G-F(#)g)/(G(#)G))|X).x by RFUNCT_1:64;
   end;
   then (F/G)`|X = ((f(#)G-F(#)g)/(G(#)G))|X by A12,PARTFUN1:34;
   hence F/G is_integral_of (f(#)G-F(#)g)/(G(#)G),X by A4,Lm1;
end;

theorem
   a <= b & [' a,b '] c= dom f & f is_continuous_on [' a,b '] &
  ]. a,b .[ c= dom F &
  (for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x) + F.a)
  implies
    F is_integral_of f,]. a,b .[
proof
    set C = [' a,b '];
    set O = ].a,b.[;
    assume that
A1:  a <= b and
A2:  [' a,b '] c= dom f and
A3:  f is_continuous_on [' a,b '] and
A4:  ]. a,b .[ c= dom F and
A5:  for x be real number st x in ].a,b.[ holds F.x = integral(f,a,x) +F.a;
     [' a,b '] = [.a,b.] by A1,INTEGRA5:def 4;
then A6: O c= [' a,b '] by RCOMP_1:15;
then A7: O c= dom f by A2,XBOOLE_1:1;
    deffunc G0(Element of REAL) = integral(f,a,$1);
    consider G1 be Function of REAL,REAL such that
A8:  for h be Real holds G1.h=G0(h) from FUNCT_2:sch 4;
    reconsider G=G1|O as PartFunc of REAL,REAL;
     dom G = dom G1 /\ O by FUNCT_1:68;
then A9: dom G = REAL /\ O by FUNCT_2:def 1;
then A10:O = dom G by XBOOLE_1:28;
A11:now let x be real number;
     reconsider z = x as Real by XREAL_0:def 1;
     assume x in ].a,b.[;
     then G.x = G1.x by A10,FUNCT_1:70;
     then G.x = integral(f,a,z) by A8;
     hence G.x = integral(f,a,x);
    end;
A12:for x be Real st x in O holds G is_differentiable_in x & diff(G,x)=f.x
    proof
     let x be Real;
     assume A13:x in ]. a,b .[;
     reconsider z = x as real number;
      f is_continuous_on O by A3,A6,FCONT_1:17;
then A14: f|O is_continuous_in z by A13,FCONT_1:def 2;
A15: f is_bounded_on [' a,b '] & f is_integrable_on [' a,b ']
       by A3,INTEGRA5:10,11;
      for r be real number st 0 < r
      ex s be real number st
       0 < s & for x1 be real number st x1 in dom f & abs(x1-z) < s
        holds abs(f.x1 - f.z) < r
     proof
      let r be real number;
      assume 0 < r;
      then consider s be real number such that
A16:   0 < s &
       for x1 be real number st x1 in dom(f|O) & abs(x1-z)<s
        holds abs((f|O).x1 - (f|O).z) < r by A14,FCONT_1:3;
      consider ss1 be real number such that
A17:   0 < ss1 & ].z-ss1,z+ss1.[ c= O by A13,RCOMP_1:40;
      set s1=min(ss1,s);
      take s1;
       now let x1 be real number;
       assume A18:x1 in dom f & abs(x1-z)<s1;
        s1 <= s by XXREAL_0:17;
then A19:   abs(x1-z) < s by A18,XXREAL_0:2;
        s1 <= ss1 by XXREAL_0:17;
       then abs(x1-z) < ss1 by A18,XXREAL_0:2;
then A20:   x1 in ].z-ss1,z+ss1.[ by RCOMP_1:8;
        O = O /\ dom f by A7,XBOOLE_1:28;
then A21:   O = dom(f|O) by PARTFUN2:32;
       then abs(f.x1-f.z) = abs((f|O).x1 - f.z) by A17,A20,FUNCT_1:70;
       then abs(f.x1-f.z) = abs((f|O).x1 - (f|O).z) by A13,A21,FUNCT_1:70;
       hence abs(f.x1-f.z)<r by A16,A17,A19,A20,A21;
      end;
      hence thesis by A16,A17,XXREAL_0:15;
     end;
     then f is_continuous_in z by A7,A13,FCONT_1:3;
     hence thesis by A1,A2,A10,A11,A13,A15,INTEGRA6:28;
    end;
    then for x be Real st x in O holds G is_differentiable_in x;
then A22: G is_differentiable_on O by A10,FDIFF_1:16;
    deffunc H0(Element of REAL) = F.a;
    consider H1 be Function of REAL,REAL such that
A23:for h be Real holds H1.h = H0(h) from FUNCT_2:sch 4;
    reconsider H=H1|O as PartFunc of REAL,REAL;
     dom H = dom H1 /\ O by FUNCT_1:68;
then A24: dom H = REAL /\ O by FUNCT_2:def 1;
then A25: dom H = O by XBOOLE_1:28;
     now let x be Element of REAL;
     assume A26:x in O /\ dom H;
     reconsider z=x as real number;
      H.x = H1.z by A25,A26,FUNCT_1:70;
     then H.x = F.a by A23;
     hence H/.x = F.a by A25,A26,PARTFUN1:def 8;
    end;
then A27: H is_constant_on O by PARTFUN2:def 3;
then A28:H is_differentiable_on O &
    for x be Real st x in O holds (H`|O).x = 0 by A25,FDIFF_1:30;
     dom(F|O) = dom F /\ O by PARTFUN2:32;
then A29:dom(F|O) = O by A4,XBOOLE_1:28;
then A30: dom(F|O)= dom G /\ dom H by A9,A24,XBOOLE_1:28;
     now let x be set;
     assume
 A31:  x in dom(F|O);
     then reconsider z=x as real number by XREAL_0:def 1;
      (F|O).x = F.z  by A31,FUNCT_1:70;
then A32:  (F|O).x = integral(f,a,z) + F.a by A5,A29,A31;
     reconsider z1=z as Real by XREAL_0:def 1;
      H.x = H1.z1 by A25,A29,A31,FUNCT_1:70;
     then H.x = F.a by A23;
     hence (F|O).x = G.x + H.x by A11,A29,A31,A32;
    end;
then A33:F|O = G+H by A30,SEQ_1:def 3;
    then F|O is_differentiable_on O &
    for x be Real st x in O holds ((F|O)`|O).x = diff(G,x) + diff(H,x)
      by A22,A28,A29,FDIFF_1:26;
    then for x be Real st x in O holds F|O is_differentiable_in x by FDIFF_1:16
;
then A34: F is_differentiable_on O by A4,FDIFF_1:def 7;
    then dom(F`|O) = O by FDIFF_1:def 8;
    then dom(F`|O) = O /\ dom f by A7,XBOOLE_1:28;
then A35: dom(F`|O) = dom(f|O) by PARTFUN2:32;
     now let x be Real;
     assume x in dom (F`|O);
then A36:  x in O by A34,FDIFF_1:def 8;
     then x in dom f /\ O by A7,XBOOLE_0:def 3;
then A37:  x in dom (f|O) by PARTFUN2:32;
      (F`|O).x = ((F|O)`|O).x by A34,FDIFF_2:16;
     then (F`|O).x = diff(G,x) + diff(H,x) by A22,A28,A29,A33,A36,FDIFF_1:26;
then A38:  (F`|O).x = f.x + diff(H,x) by A12,A36;
      (H`|O).x = 0 by A25,A27,A36,FDIFF_1:30;
     then diff(H,x) = 0 by A28,A36,FDIFF_1:def 8;
     hence (F`|O).x = (f|O).x by A37,A38,FUNCT_1:70;
    end;
    then F`|O = f|O by A35,PARTFUN1:34;
    hence F is_integral_of f,O by A34,Lm1;
end;

theorem
   for x,x0 be real number st
  f is_continuous_on [.a,b.] & x in ].a,b.[ & x0 in ].a,b.[ &
  F is_integral_of f,].a,b.[
    holds F.x = integral(f,x0,x) + F.x0
proof
   let x,x0 be real number;
   set O = ].a,b.[;
   assume that
A1: f is_continuous_on [. a,b .] and
A2: x in ].a,b.[ & x0 in ].a,b.[ and
A3: F is_integral_of f,].a,b.[;
A4:F is_differentiable_on O & F`|O = f|O by A3,Lm1;
A5:O c= [.a,b.] by RCOMP_1:15;
A6:[.x0,x.] c=].a,b.[ & [.x,x0.] c=].a,b.[ by A2,RCOMP_1:17;
A7:now assume A8:x0 <= x;
then A9: [' x0,x '] c= ].a,b.[ by A6,INTEGRA5:def 4;
     (F`|].a,b.[)||[' x0,x '] = (f|].a,b.[)|[' x0,x '] by A3,Lm1;
 then A10:(F`|].a,b.[)||[' x0,x '] = f||[' x0,x '] by A9,FUNCT_1:82;
     [' x0,x '] c= [.a,b.] by A5,A9,XBOOLE_1:1;
 then A11:f is_continuous_on [' x0,x '] by A1,FCONT_1:17;
    then f is_integrable_on [' x0,x '] by INTEGRA5:17;
    then f||[' x0,x '] is_integrable_on [' x0,x '] by INTEGRA5:def 2;
 then A12:F`|].a,b.[ is_integrable_on [' x0,x '] by A10,INTEGRA5:def 2;
     f is_bounded_on [' x0,x '] by A11,INTEGRA5:10;
    then f||[' x0,x '] is_bounded_on [' x0,x '] by INTEGRA5:9;
    then F`|].a,b.[ is_bounded_on [' x0,x '] by A10,Th3;
    then F.x = integral(f|].a,b.[,x0,x) +F.x0 by A4,A8,A9,A12,Th10;
    then F.x = integral(f|].a,b.[,[' x0,x ']) + F.x0 by A8,INTEGRA5:def 5;
    then F.x = integral(f,[' x0,x ']) + F.x0 by A9,FUNCT_1:82;
    hence F.x = integral(f,x0,x) +F.x0 by A8,INTEGRA5:def 5;
   end;
    now assume A13: x < x0;
 then A14:[' x,x0 '] c= ].a,b.[ by A6,INTEGRA5:def 4;
     integral(f,x0,x) = -integral(f,[' x,x0 ']) by A13,INTEGRA5:def 5;
 then A15:integral(f,x0,x) = -integral(f,x,x0) by A13,INTEGRA5:def 5;
     (F`|]. a,b .[)|| [' x,x0 '] =(f|].a,b.[)|[' x,x0 '] by A3,Lm1;
 then A16:(F`|]. a,b .[)|| [' x,x0 '] = f|| [' x,x0 '] by A14,FUNCT_1:82;
     [' x,x0 '] c= [.a,b.] by A5,A14,XBOOLE_1:1;
 then A17:f is_continuous_on [' x,x0 '] by A1,FCONT_1:17;
    then f is_integrable_on [' x,x0 '] by INTEGRA5:17;
    then f||[' x,x0 '] is_integrable_on [' x,x0 '] by INTEGRA5:def 2;
 then A18:F`|].a,b.[ is_integrable_on [' x,x0 '] by A16,INTEGRA5:def 2;
     f is_bounded_on [' x,x0 '] by A17,INTEGRA5:10;
    then (F`|].a,b.[)|[' x,x0 '] is_bounded_on [' x,x0 '] by A16,INTEGRA5:9;
    then F`|].a,b.[ is_bounded_on [' x,x0 '] by Th3;
    then F.x0 = integral(f|]. a,b .[,x,x0) +F.x by A4,A13,A14,A18,Th10;
    then F.x0 = integral(f|]. a,b .[,[' x,x0 ']) +F.x by A13,INTEGRA5:def 5;
    then F.x0 = integral(f,[' x,x0 ']) +F.x by A14,FUNCT_1:82;
    then F.x0 = integral(f,x,x0) +F.x by A13,INTEGRA5:def 5;
    hence F.x = integral(f,x0,x) + F.x0 by A15;
   end;
   hence thesis by A7;
end;

theorem Th18:
  a <= b & [' a,b '] c= X & F is_integral_of f,X &
    f is_integrable_on [' a,b '] & f is_bounded_on [' a,b ']
   implies F.b = integral(f,a,b) + F.a
proof
   assume that
A1: a <= b and
A2: [' a,b '] c= X;
   assume
A3: F is_integral_of f,X;
then A4:F is_differentiable_on X & F`|X=f|X by Lm1;
    (F`|X)||[' a,b '] =(f|X )|[' a,b '] by A3,Lm1;
then A5:(F`|X)||[' a,b '] = f||[' a,b '] by A2,FUNCT_1:82;
   assume f is_integrable_on [' a,b '];
   then f||[' a,b '] is_integrable_on [' a,b '] by INTEGRA5:def 2;
then A6:F`|X is_integrable_on [' a,b '] by A5,INTEGRA5:def 2;
   assume f is_bounded_on [' a,b '];
   then (F`|X)|[' a,b '] is_bounded_on [' a,b '] by A5,INTEGRA5:9;
   then F`|X is_bounded_on [' a,b '] by Th3;
   then F.b = integral(f|X,a,b) +F.a by A1,A2,A4,A6,Th10;
   then F.b = integral(f|X,[' a,b ']) +F.a by A1,INTEGRA5:def 5;
   then F.b =integral(f,[' a,b ']) +F.a by A2,FUNCT_1:82;
   hence F.b = integral(f,a,b) + F.a by A1,INTEGRA5:def 5;
end;

theorem Th19:
  a <= b & [. a,b .] c= X & f is_continuous_on X implies
    f is_continuous_on [' a,b '] & f is_integrable_on [' a,b '] &
    f is_bounded_on [' a,b ']
proof
   assume a <= b;
then A1:[. a,b .] = [' a,b '] by INTEGRA5:def 4;
   assume [. a,b .] c= X & f is_continuous_on X;
   hence f is_continuous_on [' a,b '] by A1,FCONT_1:17;
   hence thesis by INTEGRA5:10,17;
end;

theorem Th20:
  a <= b & [. a,b .] c= X & f is_continuous_on X & F is_integral_of f,X
    implies F.b = integral(f,a,b) + F.a
proof
   assume that
A1: a <= b and
A2: [. a,b .] c= X;
A3:[. a,b .] =[' a,b '] by A1,INTEGRA5:def 4;
   assume f is_continuous_on X;
then A4:f is_continuous_on [' a,b '] & f is_integrable_on [' a,b '] &
   f is_bounded_on [' a,b '] by A1,A2,Th19;
   assume F is_integral_of f,X;
   hence F.b = integral(f,a,b) + F.a by A1,A2,A3,A4,Th18;
end;

theorem Th21:
  b <= a & [' b,a '] c= X &
  f is_integrable_on [' b,a '] & g is_integrable_on [' b,a '] &
  f is_bounded_on [' b,a '] & g is_bounded_on [' b,a '] &
  X c= dom f & X c= dom g & F is_integral_of f,X & G is_integral_of g,X
  implies
    (F.a)*(G.a) - (F.b)*(G.b) = integral(f(#)G,b,a) + integral(F(#)g,b,a)
proof
    assume that
A1:  b <= a and
A2:  [' b,a '] c= X and
A3:  f is_integrable_on [' b,a '] & g is_integrable_on [' b,a '] and
A4:  f is_bounded_on [' b,a '] & g is_bounded_on [' b,a '];
    assume that
A5:  X c= dom f & X c= dom g and
A6:  F is_integral_of f,X & G is_integral_of g,X;
    set I = [' b,a '];
A7: X c= dom F & X c= dom G by A6,Th11;
    then X c= dom f /\ dom G & X c= dom F /\ dom g by A5,XBOOLE_1:19;
    then X c=dom (f(#)G) & X c=dom (F(#)g) by SEQ_1:def 5;
then A8:I c= dom(f(#)G) & I c= dom(F(#)g) by A2,XBOOLE_1:1;
A9: F is_differentiable_on X & F`|X=f|X &
    G is_differentiable_on X & G`|X=g|X by A6,Lm1;
then A10: X is open Subset of REAL by FDIFF_1:15,17;
then A11:F(#)G is_differentiable_on X by A9,FDIFF_2:20;
     F is_continuous_on X & G is_continuous_on X by A9,FDIFF_1:33;
    then F is_continuous_on I & G is_continuous_on I by A2,FCONT_1:17;
then A12:F is_integrable_on I & G is_integrable_on I &
    F is_bounded_on I & G is_bounded_on I by INTEGRA5:10,17;
     I c= dom f & I c= dom g & I c= dom F & I c= dom G
      by A2,A5,A7,XBOOLE_1:1;
then A13:f(#)G is_integrable_on I & F(#)g is_integrable_on I &
    f(#)G is_bounded_on I /\ I & F(#)g is_bounded_on I /\ I
      by A3,A4,A12,INTEGRA6:14,RFUNCT_1:101;
then A14:f(#)G + F(#)g is_integrable_on I &
    f(#)G + F(#)g is_bounded_on I /\ I by A8,INTEGRA6:11,RFUNCT_1:100;
A15:min(b,a) <= b & b <= max(b,a) by XXREAL_0:17,25;
A16:[. b,a .] = I by A1,INTEGRA5:def 4;
     min(b,a) = b by A1,XXREAL_0:def 8;
    then max(b,a) = a by XXREAL_0:36;
then A17:b in I & a in I by A15,A16,RCOMP_1:15;
    then a in X & b in X by A2;
    then a in dom F /\ dom G & b in dom F /\ dom G by A7,XBOOLE_0:def 3;
then A18:a in dom (F(#)G) & b in dom (F(#)G) by SEQ_1:def 5;
then A19:(F(#)G).b=(F.b)*(G.b) by SEQ_1:def 5;
     (F(#)G)`|X = (f|X)(#)G +F(#)(g|X) by A9,A10,FDIFF_2:20;
    then (F(#)G)`|X =(f(#)G)|X +F(#)(g|X) by RFUNCT_1:61;
    then (F(#)G)`|X =(f(#)G)|X +(F(#)g)|X by RFUNCT_1:61;
    then (F(#)G)`|X =(f(#)G+F(#)g)|X by RFUNCT_1:60;
    then F(#)G is_integral_of (f(#)G+F(#)g),X by A11,Lm1;
    then (F(#)G).a = integral(f(#)G+F(#)g,b,a) +(F(#)G).b
      by A1,A2,A14,Th18;
    then (F(#)G).a-(F(#)G).b = integral(f(#)G,b,a) + integral(F(#)g,b,a)
      by A1,A8,A13,A17,INTEGRA6:24;
    hence thesis by A18,A19,SEQ_1:def 5;
end;

theorem
   b <= a & [. b,a .] c= X & X c= dom f & X c= dom g &
  f is_continuous_on X & g is_continuous_on X &
  F is_integral_of f,X & G is_integral_of g,X
  implies
    (F.a)*(G.a) - (F.b)*(G.b) = integral(f(#)G,b,a)+ integral(F(#)g,b,a)
proof
   assume that
A1: b <= a and
A2: [. b,a .] c= X;
   assume that
A3: X c= dom f & X c= dom g and
A4: f is_continuous_on X & g is_continuous_on X and
A5: F is_integral_of f,X & G is_integral_of g,X;
A6:[. b,a .] =[' b,a '] by A1,INTEGRA5:def 4;
   then f is_continuous_on [' b,a '] & g is_continuous_on [' b,a ']
     by A2,A4,FCONT_1:17;
   then f is_integrable_on [' b,a '] & f is_bounded_on [' b,a '] &
   g is_integrable_on [' b,a '] & g is_bounded_on [' b,a ']
     by INTEGRA5:10,17;
   hence thesis by A1,A2,A3,A5,A6,Th21;
end;

begin :: Examples of Indefinite Integral

theorem Th23:
  sin is_integral_of cos,REAL
proof
A1:dom(sin`|REAL) = REAL by FDIFF_1:def 8,SIN_COS:73;
    dom(cos|REAL) = (dom cos) /\ REAL by FUNCT_1:68;
then A2:dom(cos|REAL) = REAL /\ REAL by SIN_COS:def 22;
    now
    let x be set;
    assume A3: x in dom(sin`|REAL);
    then reconsider z=x as real number by XREAL_0:def 1;
     (sin`|REAL).x = diff(sin,z) by A3,FDIFF_1:def 8,SIN_COS:73;
    then (sin`|REAL).x = cos.z by SIN_COS:69;
    hence (sin`|REAL).x = (cos|REAL).x by A2,A3,FUNCT_1:68;
   end;
   then sin`|REAL=cos|REAL by A1,A2,FUNCT_1:9;
   hence sin is_integral_of cos,REAL by Lm1,SIN_COS:73;
end;

theorem
   sin.b-sin.a = integral(cos,a,b)
proof
A1:cos is_continuous_on REAL by FDIFF_1:33,SIN_COS:72;
A2:min(a,b) <= a & a <= max(a,b) by XXREAL_0:17,25;
then A3:min(a,b) <= max(a,b) by XXREAL_0:2;
    [. min(a,b),max(a,b) .] c= REAL;
then A4:sin.max(a,b) = integral(cos,min(a,b),max(a,b)) + sin.min(a,b)
     by A1,A3,Th20,Th23;
A5:now assume A6:min(a,b) = a;
    then max(a,b) = b by XXREAL_0:36;
    hence sin.b - sin.a= integral(cos,a,b) by A4,A6;
   end;
    now assume A7:min(a,b) = b;
then A8: max(a,b) = a by XXREAL_0:36;
     now assume b < a;
     then integral(cos,a,b) = -integral(cos,[' b,a ']) by INTEGRA5:def 5;
     then sin.a = -integral(cos,a,b) + sin.b by A2,A4,A7,A8,INTEGRA5:def 5;
     hence sin.b - sin.a= integral(cos,a,b);
    end;
    hence sin.b - sin.a= integral(cos,a,b) by A2,A5,A7,XXREAL_0:1;
   end;
   hence sin.b - sin.a= integral(cos,a,b) by A5,XXREAL_0:15;
end;

theorem Th25:
  (-1)(#)cos is_integral_of sin,REAL
proof
    dom cos = REAL by SIN_COS:def 22;
then A1:dom((-1)(#)cos) =REAL by SEQ_1:def 6;
then A2:(-1)(#)cos is_differentiable_on REAL
      by FCONT_3:4,FDIFF_1:28,SIN_COS:72;
then A3:dom (((-1)(#)cos)`|REAL) = REAL by FDIFF_1:def 8;
    dom(sin|REAL) = dom sin /\ REAL by FUNCT_1:68;
then A4:dom(sin|REAL) = REAL /\ REAL by SIN_COS:def 20;
    now let x be set;
    assume A5: x in dom(((-1)(#)cos)`|REAL);
    then reconsider z=x as real number by XREAL_0:def 1;
     (((-1)(#)cos)`|REAL).x=(-1)*diff(cos,z)
      by A1,A5,FCONT_3:4,FDIFF_1:28,SIN_COS:72;
    then (((-1)(#)cos)`|REAL).x=(-1)*(-sin.z) by SIN_COS:68;
    hence (((-1)(#)cos)`|REAL).x=(sin|REAL).x by A4,A5,FUNCT_1:68;
   end;
   then ((-1)(#)cos)`|REAL=sin|REAL by A3,A4,FUNCT_1:9;
   hence (-1)(#)cos is_integral_of sin,REAL by A2,Lm1;
end;

theorem
   cos.a - cos.b = integral(sin,a,b)
proof
A1:dom cos = REAL & REAL = dom sin by SIN_COS:def 20,def 22;
A2:sin is_continuous_on REAL by FDIFF_1:33,SIN_COS:73;
    min(a,b) in REAL & max(a,b) in REAL by XREAL_0:def 1;
then A3:min(a,b) in dom((-1)(#)cos) & max(a,b) in dom((-1)(#)cos) by A1,
SEQ_1:def 6;
A4:min(a,b) <= a & a <= max(a,b) by XXREAL_0:17,25;
then A5:min(a,b) <= max(a,b) by XXREAL_0:2;
    [. min(a,b),max(a,b) .] c= REAL;
   then ((-1)(#)cos).max(a,b)
    = integral(sin,min(a,b),max(a,b)) + ((-1)(#)cos).min(a,b)
      by A2,A5,Th20,Th25;
   then (-1)*cos.max(a,b) = integral(sin,min(a,b),max(a,b)) + ((-1)(#)cos).min
(a,b)
      by A3,SEQ_1:def 6;
   then (-1)*cos.max(a,b) = integral(sin,min(a,b),max(a,b)) + (-1)*cos.min(a,b
)
      by A3,SEQ_1:def 6;
then A6: cos.min(a,b) - cos.max(a,b) = integral(sin,min(a,b),max(a,b));
A7: now assume A8:min(a,b) = a;
    then max(a,b) = b by XXREAL_0:36;
    hence cos.a - cos.b = integral(sin,a,b) by A6,A8;
   end;
    now assume A9:min(a,b) = b;
then A10:max(a,b) = a by XXREAL_0:36;
     b < a implies cos.a - cos.b= integral(sin,a,b)
    proof
     assume A11:b < a;
     then integral(sin,a,b) = -integral(sin,[' b,a ']) by INTEGRA5:def 5;
     then integral(sin,a,b) = -integral(sin,b,a) by A11,INTEGRA5:def 5;
     hence cos.a - cos.b= integral(sin,a,b) by A6,A9,A10;
    end;
    hence cos.a - cos.b = integral(sin,a,b) by A4,A7,A9,XXREAL_0:1;
   end;
   hence cos.a - cos.b = integral(sin,a,b) by A7,XXREAL_0:15;
end;

theorem Th27:
  exp_R is_integral_of exp_R,REAL
proof
A1:dom(exp_R`|REAL) = REAL by FDIFF_1:def 8,TAYLOR_1:16;
A2:dom(exp_R|REAL) = REAL /\ REAL by FUNCT_1:68,TAYLOR_1:16;
    now
    let x be set;
    assume A3: x in dom(exp_R`|REAL);
    then reconsider z=x as real number by XREAL_0:def 1;
    reconsider z1=z as Real by XREAL_0:def 1;
     (exp_R`|REAL).x = diff(exp_R,z) by A3,FDIFF_1:def 8,TAYLOR_1:16;
    then (exp_R`|REAL).x = exp_R.z1 by TAYLOR_1:16;
    hence (exp_R`|REAL).x = (exp_R|REAL).x by A2,FUNCT_1:68;
   end;
   then exp_R`|REAL=exp_R|REAL by A1,A2,FUNCT_1:9;
   hence exp_R is_integral_of exp_R,REAL by Lm1,TAYLOR_1:16;
end;

theorem
   exp_R.b-exp_R.a = integral(exp_R,a,b)
proof
A1:exp_R is_continuous_on REAL by FDIFF_1:33,TAYLOR_1:16;
A2:min(a,b) <= a & a <= max(a,b) by XXREAL_0:17,25;
then A3:min(a,b) <= max(a,b) by XXREAL_0:2;
    [. min(a,b),max(a,b) .] c= REAL;
then A4:exp_R.max(a,b) = integral(exp_R,min(a,b),max(a,b)) + exp_R.min(a,b)
     by A1,A3,Th20,Th27;
A5:min(a,b) = a implies exp_R.b - exp_R.a= integral(exp_R,a,b)
   proof
    assume A6:min(a,b) = a;
    then max(a,b) = b by XXREAL_0:36;
    hence exp_R.b - exp_R.a= integral(exp_R,a,b) by A4,A6;
   end;
    min(a,b) = b implies exp_R.b - exp_R.a= integral(exp_R,a,b)
   proof
    assume A7:min(a,b) = b;
then A8: max(a,b) = a by XXREAL_0:36;
     b < a implies exp_R.b - exp_R.a= integral(exp_R,a,b)
    proof
     assume b < a;
     then integral(exp_R,a,b) = -integral(exp_R,[' b,a ']) by INTEGRA5:def 5;
     then exp_R.a = -integral(exp_R,a,b) + exp_R.b by A2,A4,A7,A8,
INTEGRA5:def 5;
     hence exp_R.b - exp_R.a= integral(exp_R,a,b);
    end;
    hence exp_R.b - exp_R.a = integral(exp_R,a,b) by A2,A5,A7,XXREAL_0:1;
   end;
   hence exp_R.b - exp_R.a = integral(exp_R,a,b) by A5,XXREAL_0:15;
end;

theorem Th29:
  #Z (n+1) is_integral_of (n+1)(#)( #Z n),REAL
proof
A1:dom( #Z (n+1)) = REAL & dom( #Z n) = REAL by FUNCT_2:def 1;
    for x be Real st x in REAL holds
    ( #Z (n+1))|REAL is_differentiable_in x
   proof
    let x be Real;
    assume x in REAL;
     #Z (n+1) is_differentiable_in x by TAYLOR_1:2;
    hence thesis by A1,RELAT_1:97;
   end;
then A2:#Z (n+1) is_differentiable_on REAL by A1,FDIFF_1:def 7;
then A3:dom(( #Z (n+1))`|REAL) = REAL by FDIFF_1:def 8;
    dom(((n+1)(#)( #Z n))|REAL) = (dom((n+1)(#)( #Z n))) /\ REAL
     by FUNCT_1:68;
   then dom(((n+1)(#)( #Z n))|REAL) = (dom( #Z n)) /\ REAL by SEQ_1:def 6;
then A4:dom(((n+1)(#)( #Z n))|REAL) = REAL /\ REAL by FUNCT_2:def 1;
   reconsider m = n+1 as natural number;
    now let x be set;
    assume A5: x in dom(( #Z (n+1))`|REAL);
    then reconsider z=x as real number by XREAL_0:def 1;
     (( #Z (n+1))`|REAL).x = diff(( #Z (n+1)),z) by A2,A5,FDIFF_1:def 8;
    then (( #Z (n+1))`|REAL).x = m * z #Z (m-1) by TAYLOR_1:2;
then A6: (( #Z (n+1))`|REAL).x = (n+1) * ( #Z n).x by TAYLOR_1:def 1;
     x in dom( #Z n) by A1,A5;
    then x in dom((n+1)(#)( #Z n)) by SEQ_1:def 6;
    then (( #Z (n+1))`|REAL).x = ((n+1)(#)( #Z n)).x by A6,SEQ_1:def 6;
    hence (( #Z (n+1))`|REAL).x = (((n+1)(#)( #Z n))|REAL).x
      by A4,A5,FUNCT_1:68;
   end;
   then (( #Z (n+1))`|REAL) = (((n+1)(#)( #Z n))|REAL) by A3,A4,FUNCT_1:9;
   hence thesis by A2,Lm1;
end;

theorem
   ( #Z (n+1)).b - ( #Z (n+1)).a = integral((n+1)(#)( #Z n),a,b)
proof
A1:dom( #Z (n+1)) = REAL & dom( #Z n) = REAL by FUNCT_2:def 1;
then A2:REAL c= dom((n+1)(#)( #Z n)) by SEQ_1:def 6;
    for x be Real st x in REAL holds
    ( #Z n)|REAL is_differentiable_in x
   proof
    let x be Real;
    assume x in REAL;
     #Z n is_differentiable_in x by TAYLOR_1:2;
    hence thesis by A1,RELAT_1:97;
   end;
   then #Z n is_differentiable_on REAL by A1,FDIFF_1:def 7;
   then (n+1)(#)( #Z n) is_differentiable_on REAL &
   for x be Real st x in REAL holds
    (((n+1)(#)( #Z n))`|REAL).x = (n+1)*diff( #Z n,x)
      by A2,FCONT_3:4,FDIFF_1:28;
then A3:(n+1)(#)( #Z n) is_continuous_on REAL by FDIFF_1:33;
A4:min(a,b) <= a & a <= max(a,b) by XXREAL_0:17,25;
then A5:min(a,b) <= max(a,b) by XXREAL_0:2;
A6:[. min(a,b),max(a,b) .] c= REAL;
    #Z (n+1) is_integral_of (n+1)(#)( #Z n),REAL by Th29;
then A7:( #Z (n+1)).max(a,b)
    = integral((n+1)(#)( #Z n),min(a,b),max(a,b)) + ( #Z (n+1)).min(a,b)
     by A3,A5,A6,Th20;
A8:min(a,b) = a implies
    ( #Z (n+1)).b - ( #Z (n+1)).a= integral((n+1)(#)( #Z n),a,b)
   proof
    assume A9:min(a,b) = a;
    then max(a,b) = b by XXREAL_0:36;
    hence ( #Z (n+1)).b - ( #Z (n+1)).a
             = integral((n+1)(#)( #Z n),a,b) by A7,A9;
   end;
    min(a,b) = b implies
    ( #Z (n+1)).b - ( #Z (n+1)).a= integral((n+1)(#)( #Z n),a,b)
   proof
    assume A10:min(a,b) = b;
then A11: max(a,b) = a by XXREAL_0:36;
     b < a implies
     ( #Z (n+1)).b - ( #Z (n+1)).a= integral((n+1)(#)( #Z n),a,b)
    proof
     assume b < a;
     then integral((n+1)(#)( #Z n),a,b) = -integral((n+1)(#)( #Z n),[' b,a '])
       by INTEGRA5:def 5;
     then ( #Z (n+1)).a = -integral((n+1)(#)( #Z n),a,b) + ( #Z (n+1)).b
       by A4,A7,A10,A11,INTEGRA5:def 5;
     hence ( #Z (n+1)).b - ( #Z (n+1)).a= integral((n+1)(#)( #Z n),a,b);
    end;
    hence ( #Z (n+1)).b - ( #Z (n+1)).a= integral((n+1)(#)( #Z n),a,b)
     by A4,A8,A10,XXREAL_0:1;
   end;
   hence thesis by A8,XXREAL_0:15;
end;

begin :: Uniform Convergent Functional Sequence

theorem
   for H be Functional_Sequence of REAL,REAL,
      rseq be Real_Sequence st
  a < b &
  (for n be Element of NAT holds
   H.n is_integrable_on [' a,b '] & H.n is_bounded_on [' a,b '] &
   rseq.n = integral(H.n,a,b) ) &
  H is_unif_conv_on [' a,b ']
    holds
  lim(H,[' a,b ']) is_bounded_on [' a,b '] &
  lim(H,[' a,b ']) is_integrable_on [' a,b '] &
  rseq is convergent &
  lim rseq = integral(lim(H,[' a,b ']),a,b)
proof
    let H be Functional_Sequence of REAL,REAL, rseq be Real_Sequence;
    set AB = [' a,b '];
    assume that
A1:  a < b and
A2:  (for n be Element of NAT holds
       H.n is_integrable_on AB & H.n is_bounded_on AB &
       rseq.n =integral(H.n,a,b) ) and
A3:  H is_unif_conv_on AB;
A4: AB common_on_dom H by A3,SEQFUNC:44;
     H is_point_conv_on AB by A3,SEQFUNC:23;
then A5: dom lim(H,AB) = AB by SEQFUNC:22;
    consider K be Element of NAT such that
A6: for n be Element of NAT,x be Element of REAL st n >= K & x in AB
      holds abs((H.n).x-(lim(H,AB)).x) < 1 by A3,SEQFUNC:44;
     H.K is_bounded_on AB by A2;
    then consider r be real number such that
A7: for c be set st c in AB /\ dom(H.K) holds
      abs((H.K).c) <= r by RFUNCT_1:90;
     dom lim(H,AB) c= dom(H.K) by A4,A5,SEQFUNC:def 10;
then A8: AB /\ dom lim(H,AB) c= AB /\ dom(H.K) by XBOOLE_1:26;
     now let c be set;
     assume
A9:  c in AB /\ dom lim(H,AB);
then A10:abs((H.K).c) <= r by A7,A8;
      c in AB by A9,XBOOLE_0:def 3;
     then abs((H.K).c - (lim(H,AB)).c) < 1 by A6;
then A11:abs((H.K).c) + abs((H.K).c-(lim(H,AB)).c) <= r+1 by A10,XREAL_1:9;
      (lim(H,AB)).c = (H.K).c - ((H.K).c - (lim(H,AB)).c);
     then abs((lim(H,AB)).c)
      <= abs((H.K).c) + abs((H.K).c-(lim(H,AB)).c) by COMPLEX1:143;
     hence abs((lim(H,AB)).c) <= r+1 by A11,XXREAL_0:2;
    end;
    hence A12: lim(H,AB) is_bounded_on AB by RFUNCT_1:90;
    consider T being DivSequence of AB such that
A13:delta T is convergent & lim delta T = 0 by INTEGRA4:11;
A14:for n be Element of NAT holds
     (H.n)||AB is Function of AB,REAL &
     (H.n)||AB is_bounded_on AB &
     lower_sum((H.n)||AB,T) is convergent &
     lim lower_sum((H.n)||AB,T) = lower_integral((H.n)||AB) &
     upper_sum((H.n)||AB,T) is convergent &
     lim upper_sum((H.n)||AB,T) = upper_integral((H.n)||AB) &
     lower_integral((H.n)||AB) = upper_integral((H.n)||AB) &
     rseq.n =integral((H.n)||AB)
    proof
     let n be Element of NAT;
      AB c= dom (H.n) by A4,SEQFUNC:def 10;
     then (H.n)||AB is total by INTEGRA5:6;
     hence A15:  (H.n)||AB is Function of AB,REAL by FUNCT_2:131;
      H.n is_bounded_on AB by A2;
     hence (H.n)||AB is_bounded_on AB by INTEGRA5:9;
     hence lower_sum((H.n)||AB,T) is convergent &
      lim lower_sum((H.n)||AB,T) = lower_integral((H.n)||AB) &
      upper_sum((H.n)||AB,T) is convergent &
      lim upper_sum((H.n)||AB,T) = upper_integral((H.n)||AB)
        by A13,A15,INTEGRA4:8,9;
      (H.n) is_integrable_on AB by A2;
     then (H.n)||AB is_integrable_on AB by INTEGRA5:def 2;
     hence lower_integral((H.n)||AB) = upper_integral((H.n)||AB) by
INTEGRA1:def 17;
      rseq.n = integral(H.n,a,b) by A2;
     then rseq.n = integral(H.n,AB) by A1,INTEGRA5:def 5;
     hence rseq.n = integral((H.n)||AB);
    end;
    set H0 = lim(H,AB)||AB;
     lim(H,AB) is_bounded_above_on AB &
    lim(H,AB) is_bounded_below_on AB by A12,RFUNCT_1:def 11;
then A16:H0 is_bounded_above_on AB & H0 is_bounded_below_on AB by INTEGRA5:7,8;
then A17:H0 is_bounded_on AB by RFUNCT_1:def 11;
     H0 is total by A5,INTEGRA5:6;
then A18:H0 is Function of AB,REAL by FUNCT_2:131;
then A19:H0 is_upper_integrable_on AB & H0 is_lower_integrable_on AB
     by A17,INTEGRA4:10;
A20:for e be Element of REAL st 0 < e
     ex N be Element of NAT st
      for n,k be Element of NAT st N<=n holds
       abs(upper_sum((H.n)||AB,T).k-upper_sum(H0,T).k) <= e*(b-a)
    proof
     let e be Element of REAL;
     assume 0 < e;
     then consider N be Element of NAT such that
A21: for n be Element of NAT,x be Element of REAL st n>=N & x in AB
       holds abs((H.n).x-(lim(H,AB)).x)<e by A3,SEQFUNC:44;
     take N;
      now let n,k be Element of NAT;
      assume A22:N<= n;
      set Hn = (H.n)||AB;
      reconsider Tk=T.k as DivisionPoint of AB by INTEGRA1:def 4;
      set l0 = len Tk;
       len upper_volume(Hn,T.k) = l0 by INTEGRA1:def 7;
      then reconsider R1=upper_volume(Hn,T.k) as Element of l0-tuples_on REAL
        by FINSEQ_2:110;
       len upper_volume(H0,T.k) = l0 by INTEGRA1:def 7;
      then reconsider R2=upper_volume(H0,T.k) as Element of l0-tuples_on REAL
        by FINSEQ_2:110;
      consider H1 be Function of AB,REAL such that
A23:  rng H1 = {e} & H1 is_bounded_on AB by INTEGRA4:5;
       len upper_volume(H1,T.k) = l0 by INTEGRA1:def 7;
      then reconsider R3=upper_volume(H1,T.k) as Element of l0-tuples_on REAL
        by FINSEQ_2:110;
       upper_sum(Hn,T).k-upper_sum(H0,T).k
       = upper_sum(Hn,T.k)-upper_sum(H0,T).k by INTEGRA2:def 4;
      then upper_sum(Hn,T).k-upper_sum(H0,T).k
       =upper_sum(Hn,T.k)-upper_sum(H0,T.k) by INTEGRA2:def 4;
then A24: upper_sum(Hn,T).k-upper_sum(H0,T).k = Sum(R1-R2) by RVSUM_1:120;
       upper_sum(H0,T).k-upper_sum(Hn,T).k
       = upper_sum(H0,T.k)-upper_sum(Hn,T).k by INTEGRA2:def 4;
      then upper_sum(H0,T).k-upper_sum(Hn,T).k
       = upper_sum(H0,T.k)-upper_sum(Hn,T.k) by INTEGRA2:def 4;
then A25:upper_sum(H0,T).k-upper_sum(Hn,T).k = Sum(R2-R1) by RVSUM_1:120;
A26: for i be Element of NAT st i in Seg l0 holds
       (R1-R2).i <= R3.i & (R2-R1).i <= R3.i
      proof
       let i be Element of NAT;
       assume A27: i in Seg l0;
        (R1-R2).i = R1.i-R2.i by RVSUM_1:48;
       then (R1-R2).i = sup rng(Hn|divset(T.k,i)) * vol divset(T.k,i)
                  - (upper_volume(H0,T.k)).i by A27,INTEGRA1:def 7;
       then (R1-R2).i = sup rng(Hn|divset(T.k,i)) * vol divset(T.k,i)
                  - sup rng(H0|divset(T.k,i)) * vol divset(T.k,i)
                     by A27,INTEGRA1:def 7;
then A28:    (R1-R2).i
        = (sup rng (Hn|divset(T.k,i)) - sup rng (H0|divset(T.k,i)))
           * vol divset(T.k,i);
        (R2-R1).i = R2.i-R1.i by RVSUM_1:48;
       then (R2-R1).i = sup rng(H0|divset(T.k,i)) * vol divset(T.k,i)
                  - (upper_volume(Hn,T.k)).i by A27,INTEGRA1:def 7;
       then (R2-R1).i = sup rng(H0|divset(T.k,i)) * vol divset(T.k,i)
                  - sup rng (Hn|divset(T.k,i)) * vol divset(T.k,i)
                     by A27,INTEGRA1:def 7;
then A29:   (R2-R1).i = (sup rng(H0|divset(T.k,i)) - sup rng(Hn|divset(T.k,i)))
                  * vol divset(T.k,i);
A30:    sup rng(Hn|divset(T.k,i)) - sup rng(H0|divset(T.k,i))
        <= sup rng(H1|divset(T.k,i)) &
       sup rng(H0|divset(T.k,i)) - sup rng(Hn|divset(T.k,i))
        <= sup rng(H1|divset(T.k,i))
       proof
         i in dom Tk by A27,FINSEQ_1:def 3;
then A31:     divset(T.k,i) c= AB by INTEGRA1:10;
then A32:    H0|divset(T.k,i) is_bounded_above_on divset(T.k,i) by A16,
INTEGRA2:5;
         Hn is_bounded_on AB by A14;
        then Hn is_bounded_above_on AB by RFUNCT_1:def 11;
then A33:    Hn|divset(T.k,i) is_bounded_above_on divset(T.k,i) by A31,
INTEGRA2:5;
         Hn is Function of AB, REAL by A14;
        then reconsider f = Hn|divset(T.k,i) as Function of divset(T.k,i),REAL
          by A31,FUNCT_2:38;
        reconsider g = H0|divset(T.k,i) as Function of divset(T.k,i),REAL
          by A18,A31,FUNCT_2:38;
A34:    rng f is bounded_above & rng g is bounded_above by A32,A33,INTEGRA1:15;
A35:   rng (H1|divset(T.k,i)) c= {e} by A23,RELAT_1:99;
        consider x0 be set such that
A36:    x0 in divset(T.k,i) by XBOOLE_0:def 1;
         H1.x0 in {e} by A23,A31,A36,FUNCT_2:6;
        then H1.x0 = e by TARSKI:def 1;
then A37:   (H1|divset(T.k,i)).x0 = e by A36,FUNCT_1:72;
         H1|divset(T.k,i) is Function of divset(T.k,i),REAL
          by A31,FUNCT_2:38;
        then e in rng(H1|divset(T.k,i)) by A36,A37,FUNCT_2:6;
        then {e} c= rng(H1|divset(T.k,i)) by ZFMISC_1:37;
        then rng(H1|divset(T.k,i)) = {e} by A35,XBOOLE_0:def 10;
then A38:    e=sup rng(H1|divset(T.k,i)) by SEQ_4:22;
A39:    for x be set st x in divset(T.k,i) holds abs(f.x-g.x) <=e
        proof
         let x be set;
         assume
A40:     x in divset(T.k,i);
         then abs(f.x-g.x) = abs(Hn.x-g.x) by FUNCT_1:72;
         then abs(f.x-g.x) = abs(Hn.x-H0.x) by A40,FUNCT_1:72;
         then abs(f.x-g.x) = abs((H.n).x-H0.x) by A31,A40,FUNCT_1:72;
         then abs(f.x-g.x) = abs((H.n).x-(lim(H,AB)).x)
           by A31,A40,FUNCT_1:72;
         hence thesis by A21,A22,A31,A40;
        end;
        hence sup rng(Hn|divset(T.k,i)) - sup rng(H0|divset(T.k,i))
              <= sup rng(H1|divset(T.k,i)) by A34,A38,Th1;
         for x be set st x in divset(T.k,i) holds
         abs(g.x-f.x) <= sup rng(H1|divset(T.k,i))
        proof
         let x be set;
         assume x in divset(T.k,i);
         then abs(f.x-g.x) <= sup rng(H1|divset(T.k,i)) by A38,A39;
         hence thesis by COMPLEX1:146;
        end;
        hence sup rng(H0|divset(T.k,i)) - sup rng(Hn|divset(T.k,i))
              <= sup rng(H1|divset(T.k,i)) by A34,Th1;
       end;
A41:    0 <= vol divset(T.k,i) by INTEGRA1:11;
       then (R1-R2).i <= sup rng(H1|divset(T.k,i)) * vol divset(T.k,i)
         by A28,A30,XREAL_1:66;
       hence (R1-R2).i <= R3.i by A27,INTEGRA1:def 7;
        (R2-R1).i <= sup rng(H1|divset(T.k,i)) * vol divset(T.k,i)
         by A29,A30,A41,XREAL_1:66;
       hence thesis by A27,INTEGRA1:def 7;
      end;
      then for i be Nat st i in Seg l0 holds (R1-R2).i <= R3.i;
then A42: upper_sum(Hn,T).k-upper_sum(H0,T).k <= Sum R3 by A24,RVSUM_1:112;
A43: sup rng H1 = e by A23,SEQ_4:22;
A44: vol AB = b-a by A1,INTEGRA6:5;
       H1 is_bounded_above_on AB by A23,RFUNCT_1:def 11;
then A45: upper_sum(H1,T.k) <= e*(b-a) by A43,A44,INTEGRA1:29;
then A46: upper_sum((H.n)||AB,T).k-upper_sum(H0,T).k <= e*(b-a)
       by A42,XXREAL_0:2;
       for i be Nat st i in Seg l0 holds (R2-R1).i <= R3.i by A26;
      then upper_sum(H0,T).k-upper_sum(Hn,T).k <= Sum R3 by A25,RVSUM_1:112;
      then upper_sum(H0,T).k-upper_sum(Hn,T).k <= e*(b-a) by A45,XXREAL_0:2;
      then -e*(b-a) <= -(upper_sum(H0,T).k - upper_sum((H.n)||AB,T).k)
         by XREAL_1:26;
      hence abs(upper_sum((H.n)||AB,T).k-upper_sum(H0,T).k)
                 <=e*(b-a) by A46,ABSVALUE:12;
     end;
     hence thesis;
    end;
A47:for e be Element of REAL st 0 < e
      ex N be Element of NAT st
       for n,k be Element of NAT st N<=n holds
        abs(lower_sum((H.n)||AB,T).k-lower_sum(H0,T).k) <= e*(b-a)
    proof
     let e be Element of REAL;
     assume 0 < e;
     then consider N be Element of NAT such that
A48: for n be Element of  NAT,x be Element of REAL st
       n>=N & x in AB holds
       abs((H.n).x-(lim(H,AB)).x)<e by A3,SEQFUNC:44;
     take N;
     hereby let n,k be Element of NAT;
      assume A49: N<= n;
      set Hn=(H.n)||AB;
      reconsider Tk=T.k as DivisionPoint of AB by INTEGRA1:def 4;
      set l0 = len Tk;
       len lower_volume(Hn,T.k) = l0 by INTEGRA1:def 8;
      then reconsider R1=lower_volume(Hn,T.k) as Element of l0-tuples_on REAL
        by FINSEQ_2:110;
       len lower_volume(H0,T.k) = l0 by INTEGRA1:def 8;
      then reconsider R2=lower_volume(H0,T.k) as Element of l0-tuples_on REAL
       by FINSEQ_2:110;
      consider H1 be Function of AB,REAL such that
A50:  rng H1 = {e} & H1 is_bounded_on AB by INTEGRA4:5;
       len lower_volume(H1,T.k) = l0 by INTEGRA1:def 8;
      then reconsider R3=lower_volume(H1,T.k) as Element of l0-tuples_on REAL
        by FINSEQ_2:110;
       lower_sum(Hn,T).k-lower_sum(H0,T).k
       = lower_sum(Hn,T.k)-lower_sum(H0,T).k by INTEGRA2:def 5;
      then lower_sum(Hn,T).k-lower_sum(H0,T).k
       =lower_sum(Hn,T.k)-lower_sum(H0,T.k) by INTEGRA2:def 5;
then A51: lower_sum(Hn,T).k-lower_sum(H0,T).k = Sum(R1-R2) by RVSUM_1:120;
       lower_sum(H0,T).k-lower_sum(Hn,T).k
       =lower_sum(H0,T.k)-lower_sum(Hn,T).k by INTEGRA2:def 5;
      then lower_sum(H0,T).k-lower_sum(Hn,T).k
       =lower_sum(H0,T.k)-lower_sum(Hn,T.k) by INTEGRA2:def 5;
then A52:lower_sum(H0,T).k-lower_sum(Hn,T).k
       =Sum(R2-R1) by RVSUM_1:120;
A53: for i be Element of NAT st i in Seg l0 holds
       (R1-R2).i <= R3.i & (R2-R1).i <= R3.i
      proof
       let i be Element of NAT;
       assume A54: i in Seg l0;
        (R1-R2).i = R1.i-R2.i by RVSUM_1:48;
       then (R1-R2).i = inf rng(Hn|divset(T.k,i)) * vol divset(T.k,i)
                    - (lower_volume(H0,T.k)).i by A54,INTEGRA1:def 8;
       then (R1-R2).i = inf rng(Hn|divset(T.k,i)) * vol divset(T.k,i)
                    - inf rng(H0|divset(T.k,i)) * vol divset(T.k,i)
                     by A54,INTEGRA1:def 8;
then A55:    (R1-R2).i = (inf rng(Hn|divset(T.k,i)) - inf rng(H0|divset(T.k,i))
)
                    * vol divset(T.k,i);
        (R2-R1).i = R2.i-R1.i by RVSUM_1:48;
       then (R2-R1).i = inf rng(H0|divset(T.k,i)) * vol divset(T.k,i)
                    - (lower_volume(Hn,T.k)).i by A54,INTEGRA1:def 8;
       then (R2-R1).i = inf rng(H0|divset(T.k,i)) * vol divset(T.k,i)
                    - inf rng(Hn|divset(T.k,i)) * vol divset(T.k,i)
                     by A54,INTEGRA1:def 8;
then A56:   (R2-R1).i = (inf rng(H0|divset(T.k,i)) - inf rng(Hn|divset(T.k,i)))
                    * vol divset(T.k,i);
        i in dom Tk by A54,FINSEQ_1:def 3;
then A57:    divset(T.k,i) c= AB by INTEGRA1:10;
then A58:   H0|divset(T.k,i) is_bounded_below_on divset(T.k,i) by A16,
INTEGRA2:6;
       reconsider g = H0|divset(T.k,i) as Function of divset(T.k,i),REAL
         by A18,A57,FUNCT_2:38;
        Hn is Function of AB,REAL by A14;
       then reconsider f = Hn|divset(T.k,i) as Function of divset(T.k,i),REAL
         by A57,FUNCT_2:38;
        Hn is_bounded_on AB by A14;
       then Hn is_bounded_below_on AB by RFUNCT_1:def 11;
       then Hn|divset(T.k,i) is_bounded_below_on divset(T.k,i)
          by A57,INTEGRA2:6;
then A59:   rng f is bounded_below & rng g is bounded_below by A58,INTEGRA1:13;
A60:  rng(H1|divset(T.k,i)) c= {e} by A50,RELAT_1:99;
       consider x0 be set such that
A61:   x0 in divset(T.k,i) by XBOOLE_0:def 1;
        H1.x0 in {e} by A50,A57,A61,FUNCT_2:6;
       then H1.x0 = e by TARSKI:def 1;
then A62:  (H1|divset(T.k,i)).x0 = e by A61,FUNCT_1:72;
        H1|divset(T.k,i) is Function of divset(T.k,i),REAL
         by A57,FUNCT_2:38;
       then e in rng(H1|divset(T.k,i)) by A61,A62,FUNCT_2:6;
       then {e} c= rng(H1|divset(T.k,i)) by ZFMISC_1:37;
       then rng(H1|divset(T.k,i)) = {e} by A60,XBOOLE_0:def 10;
then A63:   e=inf rng(H1|divset(T.k,i)) by SEQ_4:22;
A64:    0 <= vol divset(T.k,i) by INTEGRA1:11;
A65:   now let x be set;
        assume
A66:    x in divset(T.k,i);
        then abs(f.x-g.x) = abs(Hn.x-g.x) by FUNCT_1:72;
        then abs(f.x-g.x) = abs(Hn.x-H0.x) by A66,FUNCT_1:72;
        then abs(f.x-g.x) = abs((H.n).x-H0.x) by A57,A66,FUNCT_1:72;
        then abs(f.x-g.x) = abs((H.n).x-(lim(H,AB)).x) by A57,A66,FUNCT_1:72;
        hence abs(f.x - g.x) <= e by A48,A49,A57,A66;
       end;
       then inf rng(Hn|divset(T.k,i)) - inf rng(H0|divset(T.k,i))
                <= inf rng(H1|divset(T.k,i)) by A59,A63,Th2;
       then (R1-R2).i <= inf rng(H1|divset(T.k,i)) * vol divset(T.k,i)
         by A55,A64,XREAL_1:66;
       hence (R1-R2).i <= R3.i by A54,INTEGRA1:def 8;
        now let x be set;
        assume x in divset(T.k,i);
        then abs(f.x-g.x) <= inf (rng (H1|divset(T.k,i))) by A63,A65;
        hence abs(g.x-f.x) <= inf rng(H1|divset(T.k,i)) by COMPLEX1:146;
       end;
       then inf rng(H0|divset(T.k,i)) - inf rng(Hn|divset(T.k,i))
             <= inf rng(H1|divset(T.k,i)) by A59,Th2;
       then (R2-R1).i <= inf rng(H1|divset(T.k,i)) * vol divset(T.k,i)
         by A56,A64,XREAL_1:66;
       hence thesis by A54,INTEGRA1:def 8;
      end;
      then for i be Nat st i in Seg l0 holds (R1-R2).i <= R3.i;
then A67: lower_sum(Hn,T).k-lower_sum(H0,T).k <=lower_sum(H1,T.k)
        by A51,RVSUM_1:112;
A68: sup rng H1 = e by A50,SEQ_4:22;
A69: vol AB = b-a by A1,INTEGRA6:5;
       H1 is_bounded_above_on AB by A50,RFUNCT_1:def 11;
then A70: upper_sum(H1,T.k) <= e*(b-a) by A68,A69,INTEGRA1:29;
       lower_sum(H1,T.k) <= upper_sum(H1,T.k) by A50,INTEGRA1:30;
then A71: lower_sum(H1,T.k) <= e*(b-a) by A70,XXREAL_0:2;
then A72: lower_sum((H.n)||AB,T).k-lower_sum(H0,T).k <= e*(b-a) by A67,
XXREAL_0:2;
       for i be Nat st i in Seg l0 holds (R2-R1).i <= R3.i
        by A53;
      then lower_sum(H0,T).k-lower_sum(Hn,T).k <= Sum R3 by A52,RVSUM_1:112;
      then lower_sum(H0,T).k-lower_sum(Hn,T).k <= e*(b-a) by A71,XXREAL_0:2;
      then -e*(b-a) <= -(lower_sum(H0,T).k - lower_sum((H.n)||AB,T).k)
       by XREAL_1:26;
      hence abs(lower_sum((H.n)||AB,T).k-lower_sum(H0,T).k)
                 <=e*(b-a) by A72,ABSVALUE:12;
     end;
    end;
A73: 0 < b-a by A1,XREAL_1:52;
A74:
    for e be Element of REAL st 0 < e
     ex N be Element of NAT st
      for n,k be Element of NAT st N<=n holds
       abs(lower_sum((H.n)||AB,T).k-lower_sum(H0,T).k) <= e
    proof
     let e be Element of REAL;
     assume 0 < e;
then A75: 0 < e/(b-a) by A73,XREAL_1:141;
     consider N be Element of NAT such that
A76:  for n,k be Element of NAT st N <= n holds
       abs(lower_sum((H.n)||AB,T).k-lower_sum(H0,T).k)
        <=(e/(b-a)) * (b-a) by A47,A75;
     take N;
     hereby let n,k be Element of NAT;
      assume N<=n;
      then abs(lower_sum((H.n)||AB,T).k-lower_sum(H0,T).k)
        <=(e/(b-a)) * (b-a) by A76;
      hence abs(lower_sum((H.n)||AB,T).k-lower_sum(H0,T).k)
             <= e by A73,XCMPLX_1:88;
     end;
    end;
A77:
    lower_sum(H0,T) is convergent & lim lower_sum(H0,T) = lower_integral H0 &
    upper_sum(H0,T) is convergent & lim upper_sum(H0,T) = upper_integral H0
      by A13,A17,A18,INTEGRA4:8,9;
A78:for e be Element of REAL st 0 < e
     ex N be Element of NAT st
      for n be Element of NAT st N<=n holds
       abs(upper_integral((H.n)||AB)
           -upper_integral(lim(H,AB)||AB )) <=e
    proof
     let e be Element of REAL;
     assume 0 < e;
then A79:   0 < e/(b-a) by A73,XREAL_1:141;
     consider N be Element of NAT such that
A80:   for n,k be Element of NAT st N<=n holds
       abs(upper_sum((H.n)||AB,T).k-upper_sum(H0,T).k)
        <= (e/(b-a)) * (b-a) by A20,A79;
A81:  now let n,k be Element of NAT;
      assume N<=n;
      then abs(upper_sum((H.n)||AB,T).k-upper_sum(H0,T).k)
        <=(e/(b-a)) * (b-a) by A80;
      hence abs(upper_sum((H.n)||AB,T).k-upper_sum(H0,T).k)
             <= e by A73,XCMPLX_1:88;
     end;
     take N;
     hereby let n be Element of NAT;
      assume A82: N<=n;
  A83: now let k be Element of NAT;
        abs(upper_sum((H.n)||AB,T).k-upper_sum(H0,T).k) <=e by A81,A82;
       then abs(upper_sum((H.n)||AB,T).k+(-upper_sum(H0,T)).k) <= e
         by SEQ_1:14;
       then abs((upper_sum((H.n)||AB,T)+-upper_sum(H0,T)).k) <= e
         by SEQ_1:11;
       then abs((upper_sum((H.n)||AB,T)-upper_sum(H0,T)).k) <= e by SEQ_1:15;
       hence abs(upper_sum((H.n)||AB,T)-upper_sum(H0,T)).k <= e by SEQ_1:16;
      end;
A84:   upper_sum((H.n)||AB,T) is convergent by A14;
then A85:   upper_sum((H.n)||AB,T)-upper_sum(H0,T) is convergent by A77,
SEQ_2:25;
       lim(upper_sum((H.n)||AB,T)-upper_sum(H0,T))
       = lim upper_sum((H.n)||AB,T) -lim upper_sum(H0,T)
          by A77,A84,SEQ_2:26;
      then lim(upper_sum((H.n)||AB,T)-upper_sum(H0,T))
       = upper_integral((H.n)||AB) -lim upper_sum(H0,T) by A14;
then A86:   lim(upper_sum((H.n)||AB,T)-upper_sum(H0,T))
       = upper_integral((H.n)||AB) - upper_integral(lim(H,AB)||AB)
         by A13,A17,A18,INTEGRA4:9;
       abs(upper_sum((H.n)||AB,T)-upper_sum(H0,T)) is convergent
        by A85,SEQ_4:26;
      then lim abs(upper_sum((H.n)||AB,T)-upper_sum(H0,T)) <= e by A83,
PREPOWER:3;
      hence abs(upper_integral((H.n)||AB) - upper_integral(lim(H,AB)||AB))
        <=e by A85,A86,SEQ_4:27;
     end;
    end;

A87:
    for e be Element of REAL st 0 < e
     ex N be Element of NAT st
      for n be Element of NAT st N<=n holds
       abs(lower_integral((H.n)||AB)
           -lower_integral(lim(H,AB)||AB )) <=e
    proof
     let e be Element of REAL;
     assume 0 < e;
     then consider N be Element of NAT such that
A88:  for n,k be Element of NAT st N<=n holds
       abs(lower_sum((H.n)||AB,T).k-lower_sum(H0,T).k) <=e by A74;
     take N;
     hereby let n be Element of NAT;
      set LHT = lower_sum((H.n)||AB,T);
      assume A89: N<=n;
  A90: now let k be Element of NAT;
        abs(LHT.k - lower_sum(H0,T).k) <=e by A88,A89;
       then abs(LHT.k + (-lower_sum(H0,T)).k) <= e by SEQ_1:14;
       then abs((LHT + -lower_sum(H0,T)).k) <= e by SEQ_1:11;
       then abs((LHT - lower_sum(H0,T)).k) <= e by SEQ_1:15;
       hence abs(LHT - lower_sum(H0,T)).k <= e by SEQ_1:16;
      end;
A91:   LHT is convergent by A14;
      then lim(LHT - lower_sum(H0,T)) = lim LHT -lim lower_sum(H0,T)
        by A77,SEQ_2:26;
      then lim(LHT - lower_sum(H0,T))
        = lower_integral((H.n)||AB) -lim lower_sum(H0,T) by A14;
then A92:   lim(LHT - lower_sum(H0,T))
       = lower_integral((H.n)||AB) -lower_integral(lim(H,AB)||AB)
           by A13,A17,A18,INTEGRA4:8;
A93:   LHT - lower_sum(H0,T) is convergent by A77,A91,SEQ_2:25;
      then abs(LHT - lower_sum(H0,T)) is convergent by SEQ_4:26;
      then lim abs(LHT - lower_sum(H0,T)) <= e by A90,PREPOWER:3;
      hence abs(lower_integral((H.n)||AB) - lower_integral(lim(H,AB)||AB )) <=e
         by A92,A93,SEQ_4:27;
     end;
    end;
    set K1 = upper_integral(lim(H,AB)||AB);
    set L1 = lower_integral(lim(H,AB)||AB);
A94:
    now let e1 be Element of REAL;
     set e=e1/2;
     assume 0 < e1;
then A95:0 < e1/2 & e1/2 < e1 by XREAL_1:217,218;
 then A96:0 < e/2 by XREAL_1:217;
     then consider N1 be Element of NAT such that
A97:  for n be Element of NAT st N1<=n holds
       abs(upper_integral((H.n)||AB)
           -upper_integral(lim(H,AB)||AB )) <=e/2 by A78;
     consider N2 be Element of NAT such that
A98:  for n be Element of NAT st N2<=n holds
       abs(lower_integral((H.n)||AB)
           -lower_integral(lim(H,AB)||AB )) <=e/2 by A87,A96;
     reconsider n=max(N1,N2) as Element of NAT by XXREAL_0:16;
     set K = upper_integral((H.n)||AB);
     set L = lower_integral((H.n)||AB);
      N1<= n by XXREAL_0:25;
then A99:  abs(K - K1) <=e/2 by A97;
      N2<=n by XXREAL_0:25;
     then abs(L - L1) <=e/2 by A98;
then A100:  abs(K - L1) <= e/2 by A14;
      (K - K1) - (K - L1) = L1 - K1;
then A101:  abs(L1 - K1) <= abs(K - K1) + abs(K - L1) by COMPLEX1:143;
      abs(K - K1) + abs(K-L1) <= e/2+e/2 by A99,A100,XREAL_1:9;
     then abs(L1 - K1) <= e by A101,XXREAL_0:2;
     hence abs(L1 - K1) < e1 by A95,XXREAL_0:2;
    end;
     now assume K1 <> L1;
     then 0 < abs(L1 -K1) by COMPLEX1:148;
     hence contradiction by A94;
    end;
    then lim(H,AB)||AB is_integrable_on AB by A19,INTEGRA1:def 17;
    hence lim(H,AB) is_integrable_on AB by INTEGRA5:def 2;
A102: for p be real number st
     0<p ex n be Element of NAT st
      for m be Element of NAT st n<=m holds
       abs(rseq.m-integral(lim(H,AB),a,b))<p
    proof
     let p be real number;
     assume 0<p;
then A103:0 < p/2 & p/2 < p by XREAL_1:217,218;
     set e=p/2;
     consider N be Element of NAT such that
A104:   for n be Element of NAT st N<=n holds
       abs(upper_integral((H.n)||AB)
        -upper_integral(lim(H,AB)||AB )) <=e by A78,A103;
     take N;
     hereby let n be Element of NAT;
      assume N<=n;
then A105:   abs(upper_integral((H.n)||AB) - upper_integral(lim(H,AB)||AB)) <=e
by A104;
       upper_integral((H.n)||AB) = integral((H.n)||AB);
then A106:   upper_integral((H.n)||AB) = rseq.n by A14;
       upper_integral(lim(H,AB)||AB) = integral(lim(H,AB),AB);
      then upper_integral(lim(H,AB)||AB) = integral(lim(H,AB),a,b)
        by A1,INTEGRA5:def 5;
      hence abs(rseq.n - integral(lim(H,AB),a,b)) < p by A103,A105,A106,
XXREAL_0:2;
     end;
    end;
    hence rseq is convergent by SEQ_2:def 6;
    hence lim rseq = integral(lim(H,AB),a,b) by A102,SEQ_2:def 7;
end;
