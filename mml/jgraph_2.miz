:: On Outside Fashoda Meet Theorem
::  by Yatsuka Nakamura
::
:: Received July 16, 2001
:: Copyright (c) 2001 Association of Mizar Users

environ

 vocabularies EUCLID, ARYTM, ARYTM_3, RELAT_1, SQUARE_1, ARYTM_1, PRE_TOPC,
      SUBSET_1, BOOLE, ORDINAL2, FUNCT_1, FUNCT_4, METRIC_1, COMPLEX1, MCART_1,
      JORDAN2C, FINSEQ_1, FINSEQ_2, FUNCT_5, TOPMETR, RCOMP_1, PARTFUN1,
      BORSUK_1, JGRAPH_2, FUNCOP_1, RLVECT_1, PCOMPS_1, STRUCT_0;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, RELAT_1, FUNCT_1, RELSET_1, PARTFUN1, FUNCT_2, XXREAL_0,
      FUNCOP_1, FINSEQ_1, FINSEQ_2, NAT_1, DOMAIN_1, STRUCT_0, PRE_TOPC,
      PCOMPS_1, TOPMETR, METRIC_1, RCOMP_1, SQUARE_1, PSCOMP_1, RLVECT_1,
      EUCLID,
      JORDAN2C,
      FUNCT_4;
 constructors FUNCT_4, REAL_1, SQUARE_1, RCOMP_1, MONOID_0, TOPMETR, PSCOMP_1,
      FUNCOP_1, JORDAN2C, SEQ_1, FUNCSDOM;
 registrations XBOOLE_0, RELSET_1, FUNCT_2, NUMBERS, XXREAL_0, XREAL_0, REAL_1,
      SQUARE_1, STRUCT_0, PRE_TOPC, BORSUK_1, MONOID_0, EUCLID, TOPMETR,
      PSCOMP_1, ORDINAL1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, XBOOLE_0, SQUARE_1, EUCLID, STRUCT_0, XCMPLX_0;
 theorems TARSKI, RELAT_1, SUBSET_1, FUNCT_1, FUNCT_2, FUNCT_4, TOPS_1, TOPS_2,
      PARTFUN1, PRE_TOPC, REVROT_1, JORDAN2C, FINSEQ_2, FRECHET, TOPMETR,
      JORDAN6, EUCLID, JGRAPH_1, SQUARE_1, TOPREAL3, TOPREAL6, PSCOMP_1,
      METRIC_1, SPPOL_2, TSEP_1, XBOOLE_0, XBOOLE_1, XREAL_0, XCMPLX_0,
      XCMPLX_1, FUNCOP_1, XREAL_1, XXREAL_0, XXREAL_1;
 schemes FUNCT_2, CLASSES1;

begin

reserve T,T1,T2,S for non empty TopSpace;

canceled 8;

theorem Th9: :: BORSUK_2:1
  for f being Function of T1,S, g being Function
  of T2,S,F1,F2 being Subset of T st
  T1 is SubSpace of T & T2 is SubSpace of T & F1=[#] T1 & F2=[#] T2 &
  ([#] T1) \/ ([#] T2) = [#] T & F1 is closed & F2 is closed &
  f is continuous & g is continuous &
  ( for p be set st p in ([#] T1) /\ ([#] T2) holds f.p = g.p )
  ex h being Function of T,S st h = f+*g & h is continuous
proof
  let f be Function of T1,S, g be Function of T2,S,F1,F2 being Subset of T;
  assume that
A1: T1 is SubSpace of T & T2 is SubSpace of T and
A2: F1=[#] T1 & F2=[#] T2 and
A3: ([#] T1) \/ ([#] T2) = [#] T and
A4: F1 is closed and
A5: F2 is closed and
A6: f is continuous and
A7: g is continuous and
A8: for p be set st p in ([#] T1) /\ ([#] T2) holds f.p = g.p;
  set h = f+*g;
A9: dom f = the carrier of T1 by FUNCT_2:def 1
    .= [#] T1;
A10: dom g = the carrier of T2 by FUNCT_2:def 1
    .= [#] T2;
  then
A11: dom h = [#] T by A3,A9,FUNCT_4:def 1
    .= the carrier of T;
  rng f \/ rng g c= the carrier of S & rng h c= rng f \/ rng g by FUNCT_4:18;
  then reconsider h as Function of T,S by A11,FUNCT_2:4,XBOOLE_1:1;
  take h;
  thus h = f+*g;
  for P being Subset of S st P is closed holds h"P is closed
  proof
    let P be Subset of S;
    assume
A12: P is closed;
A13: h"P c= dom h & dom h = dom f \/ dom g by FUNCT_4:def 1,RELAT_1:167;
    then
A14: h"P = h"P /\ ([#] T1 \/ [#] T2) by A9,A10,XBOOLE_1:28
      .= (h"P /\ [#](T1)) \/ (h"P /\ [#](T2)) by XBOOLE_1:23;
A15: for x being set st x in [#] T1 holds h.x = f.x
    proof
      let x be set such that
A16:  x in [#] T1;
      now per cases;
        suppose
A17:      x in [#] T2;
          then x in [#] T1 /\ [#] T2 by A16,XBOOLE_0:def 4;
          then f.x = g.x by A8;
          hence thesis by A10,A17,FUNCT_4:14;
        end;
        suppose not x in [#] T2;
          hence thesis by A10,FUNCT_4:12;
        end;
      end;
      hence thesis;
    end;
    now
      let x be set;
      thus x in h"P /\ [#] T1 implies x in f"P
      proof
        assume x in h"P /\ [#] T1;
        then x in h"P & x in dom f & x in [#] T1 by A9,XBOOLE_0:def 4;
        then h.x in P & x in dom f & f.x = h.x by A15,FUNCT_1:def 13;
        hence thesis by FUNCT_1:def 13;
      end;
      assume x in f"P;
      then x in dom f & f.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#] T1 & h.x in P by A13,A15,XBOOLE_0:def 3;
      then x in h"P & x in [#] T1 by FUNCT_1:def 13;
      hence x in h"P /\ [#] T1 by XBOOLE_0:def 4;
    end;
    then
A18: h"P /\ [#] T1 = f"P by TARSKI:2;
    now
      let x be set;
      thus x in h"P /\ [#] T2 implies x in g"P
      proof
        assume x in h"P /\ [#] T2;
        then x in h"P & x in dom g & x in [#] T2 by A10,XBOOLE_0:def 4;
        then h.x in P & x in dom g & g.x = h.x by FUNCT_1:def 13,FUNCT_4:14;
        hence thesis by FUNCT_1:def 13;
      end;
      assume x in g"P;
      then x in dom g & g.x in P by FUNCT_1:def 13;
      then x in dom h & x in [#] T2 & h.x in P
      by A13,FUNCT_4:14,XBOOLE_0:def 3;
      then x in h"P & x in [#] T2 by FUNCT_1:def 13;
      hence x in h"P /\ [#] T2 by XBOOLE_0:def 4;
    end;
    then
A19: h"P = f"P \/ g"P by A14,A18,TARSKI:2;
    f"P c= [#] T1 & [#] T1 c= [#] T by A3,XBOOLE_1:7;
    then reconsider P1 = f"P as Subset of T by XBOOLE_1:1;
    g"P c= [#] T2 & [#] T2 c= [#] T by A3,XBOOLE_1:7;
    then reconsider P2 = g"P as Subset of T by XBOOLE_1:1;
    set P3 = f"P, P4 = g"P;
A20: P3 is closed & P4 is closed by A6,A7,A12,PRE_TOPC:def 12;
    then consider F01 being Subset of T such that
A21: F01 is closed & P3=F01 /\ [#]T1 by A1,PRE_TOPC:43;
A22: P1 is closed by A2,A4,A21,TOPS_1:35;
    consider F02 being Subset of T such that
A23: F02 is closed & P4=F02 /\ [#]T2 by A1,A20,PRE_TOPC:43;
    P2 is closed by A2,A5,A23,TOPS_1:35;
    hence thesis by A19,A22,TOPS_1:36;
  end;
  hence thesis by PRE_TOPC:def 12;
end;

theorem Th10:
  for n being Element of NAT,q2 being Point of Euclid n,
  q being Point of TOP-REAL n, r being real number st q=q2 holds
  Ball(q2,r) = {q3 where q3 is Point of TOP-REAL n: |.q-q3.|<r}
proof
  let n be Element of NAT,q2 be Point of (Euclid n),
  q be Point of TOP-REAL n,r be real number;
  assume
A1: q=q2;
A2: Ball(q2,r)= {q4 where q4 is Element of Euclid n: dist(q2,q4) < r}
  by METRIC_1:18;
A3: {q4 where q4 is Element of Euclid n: dist(q2,q4) < r}
  c= {q3 where q3 is Point of TOP-REAL n: |.q-q3.|<r}
  proof
    let x be set;
    assume x in {q4 where q4 is Element of Euclid n: dist(q2,q4) < r};
    then consider q4 being Element of Euclid n such that
A4: q4=x & dist(q2,q4) < r;
    reconsider q44=q4 as Point of TOP-REAL n by TOPREAL3:13;
    dist(q2,q4)=|.q-q44.| by A1,JGRAPH_1:45;
    hence thesis by A4;
  end;
  {q3 where q3 is Point of TOP-REAL n: |.q-q3.|<r}
  c={q4 where q4 is Element of Euclid n: dist(q2,q4) < r}
  proof
    let x be set;
    assume x in {q3 where q3 is Point of TOP-REAL n: |.q-q3.|<r};
    then consider q3 being Point of TOP-REAL n such that
A5: x=q3 & |.q-q3.|<r;
    reconsider q34=q3 as Point of Euclid n by TOPREAL3:13;
    dist(q2,q34)=|.q-q3.| by A1,JGRAPH_1:45;
    hence thesis by A5;
  end;
  hence thesis by A2,A3,XBOOLE_0:def 10;
end;

theorem Th11:
  (0.TOP-REAL 2)`1=0 & (0.TOP-REAL 2)`2=0 by EUCLID:56,58;

theorem Th12:
  1.REAL 2 = <* 1, 1 *>
proof
  reconsider f= (2 qua Nat |-> (1 qua Real))
  as FinSequence of REAL by FINSEQ_2:77;
  thus 1.REAL 2=1*2 by JORDAN2C:def 8
    .=f by JORDAN2C:def 7
    .=<* 1 qua Real,1 qua Real *> by FINSEQ_2:75;
end;

theorem Th13:
  (1.REAL 2)`1=1 & (1.REAL 2)`2=1 by Th12,EUCLID:56;

theorem Th14:
  dom proj1=the carrier of TOP-REAL 2 & dom proj1=REAL 2
proof
  thus dom proj1=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  hence thesis by EUCLID:25;
end;

theorem Th15:
  dom proj2=the carrier of TOP-REAL 2 & dom proj2=REAL 2
proof
  thus dom proj2=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  hence thesis by EUCLID:25;
end;

canceled 2;

theorem Th18:
  for p being Point of TOP-REAL 2 holds p=|[proj1.p,proj2.p]|
proof
  let p be Point of TOP-REAL 2;
A1: p=|[p`1,p`2]| by EUCLID:57;
  p`1=proj1.p by PSCOMP_1:def 28;
  hence thesis by A1,PSCOMP_1:def 29;
end;

theorem Th19:
  for B being Subset of TOP-REAL 2 st
  B={0.TOP-REAL 2} holds B`<>{} & (the carrier of TOP-REAL 2)\B<>{}
proof
  let B be Subset of TOP-REAL 2;
  assume
A1: B={0.TOP-REAL 2};
  now
    assume |[0,1]| in B;
    then |[0,1]|`2=0 by A1,Th11,TARSKI:def 1;
    hence contradiction by EUCLID:56;
  end;
  then |[0,1]| in (the carrier of TOP-REAL 2) \ B by XBOOLE_0:def 5;
  hence thesis by SUBSET_1:def 5;
end;

theorem Th20: :: BORSUK_1:def 2
  for X,Y being non empty TopSpace,f being Function of X,Y
  holds f is continuous iff for p being Point of X,V being Subset of Y
  st f.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & f.:W c= V
proof
  let X,Y be non empty TopSpace,f be Function of X,Y;
A1: dom f=the carrier of X by FUNCT_2:def 1;
A2: [#]Y <> {};
  hereby
    assume
A3: f is continuous;
    thus for p being Point of X,V being Subset of Y
    st f.p in V & V is open holds
    ex W being Subset of X st p in W & W is open & f.:W c= V
    proof
      let p be Point of X,V be Subset of Y;
      assume
A4:   f.p in V & V is open;
      then
A5:   f"V is open by A2,A3,TOPS_2:55;
A6:   p in f"V by A1,A4,FUNCT_1:def 13;
      f.:(f"V) c= V by FUNCT_1:145;
      hence thesis by A5,A6;
    end;
  end;
  assume
A7: for p being Point of X,V being Subset of Y st f.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & f.:W c= V;
  for G being Subset of Y st G is open holds f"G is open
  proof
    let G be Subset of Y;
    assume
A8: G is open;
    for z being set holds z in f"G iff
    ex Q being Subset of X st Q is open & Q c= f"G & z in Q
    proof
      let z be set;
      now
        assume
A9:     z in f"G;
        then
A10:    z in dom f & f.z in G by FUNCT_1:def 13;
        reconsider p=z as Point of X by A9;
        consider W being Subset of X such that
A11:    p in W & W is open & f.:W c= G by A7,A8,A10;
A12:    f"(f.:W) c= f"G by A11,RELAT_1:178;
        W c= f"(f.:W) by A1,FUNCT_1:146;
        hence ex Q being Subset of X st Q is open & Q c= f"G & z in Q
        by A11,A12,XBOOLE_1:1;
      end;
      hence thesis;
    end;
    hence thesis by TOPS_1:57;
  end;
  hence thesis by A2,TOPS_2:55;
end;

theorem Th21:
  for p being Point of TOP-REAL 2, G being Subset of TOP-REAL 2
  st G is open & p in G
  ex r being real number st r>0 & {q where q is Point of TOP-REAL 2:
  p`1-r<q`1 & q`1<p`1+r & p`2-r<q`2 & q`2<p`2+r} c= G
proof
  let p be Point of TOP-REAL 2,G being Subset of TOP-REAL 2;
  assume
A1: G is open & p in G;
  reconsider q2=p as Point of Euclid 2 by TOPREAL3:13;
X:TopSpaceMetr Euclid 2 = the TopStruct of TOP-REAL 2 by EUCLID:def 8;
  reconsider GG=G as Subset of the TopStruct of TOP-REAL 2;
  GG is open by A1,PRE_TOPC:60;
  then consider r being real number such that
A2: r>0 & Ball(q2,r) c= GG by A1,TOPMETR:22,X;
  set s=r/sqrt(2);
  sqrt 2>0 by SQUARE_1:93;
  then
A3: s>0 by A2,XREAL_1:141;
A4: Ball(q2,r)= {q3 where q3 is Point of TOP-REAL 2: |.p-q3.|<r} by Th10;
  {q where q is Point of TOP-REAL 2:
  p`1-s<q`1 & q`1<p`1+s & p`2-s<q`2 & q`2<p`2+s} c= Ball(q2,r)
  proof
    let x be set;
    assume x in {q where q is Point of TOP-REAL 2:
    p`1-s<q`1 & q`1<p`1+s & p`2-s<q`2 & q`2<p`2+s};
    then consider q being Point of TOP-REAL 2 such that
A5: q=x & ( p`1-s<q`1 & q`1<p`1+s & p`2-s<q`2 & q`2<p`2+s);
A6: (|.p-q.|)^2=((p-q)`1)^2+((p-q)`2)^2 by JGRAPH_1:46;
A7: (p-q)`1=p`1-q`1 & (p-q)`2=p`2-q`2 by TOPREAL3:8;
    p`1-s+s<q`1+s by A5,XREAL_1:10;
    then
A8: p`1-q`1<q`1+s-q`1 by XREAL_1:16;
    p`1+s-s>q`1-s by A5,XREAL_1:16;
    then p`1-q`1>q`1+-s-q`1 by XREAL_1:16;
    then
A9: (p`1-q`1)^2<s^2 by A8,SQUARE_1:120;
    p`2-s+s<q`2+s by A5,XREAL_1:10;
    then
A10: p`2-q`2<q`2+s-q`2 by XREAL_1:16;
    p`2+s-s>q`2-s by A5,XREAL_1:16;
    then p`2-q`2>q`2+-s-q`2 by XREAL_1:16;
    then
A11: (p`2-q`2)^2<s^2 by A10,SQUARE_1:120;
    s^2=r^2/(sqrt(2))^2 by XCMPLX_1:77
      .=r^2/2 by SQUARE_1:def 4;
    then s^2+s^2=r^2;
    then (|.p-q.|)^2<r^2 by A6,A7,A9,A11,XREAL_1:10;
    then |.p-q.|<r by A2,SQUARE_1:118;
    hence thesis by A4,A5;
  end;
  hence thesis by A2,A3,XBOOLE_1:1;
end;

theorem Th22:
  for X,Y,Z being non empty TopSpace, B being Subset of Y,
  C being Subset of Z, f being Function of X,Y, h being Function of Y|B,Z|C
  st f is continuous & h is continuous & rng f c= B & B<>{} & C<>{} holds
  ex g being Function of X,Z st g is continuous & g=h*f
proof
  let X,Y,Z be non empty TopSpace, B be Subset of Y, C be Subset of Z,
  f be Function of X,Y, h be Function of Y|B,Z|C;
  assume
A1: f is continuous & h is continuous & rng f c= B & B<>{} & C<>{};
  then reconsider V=B as non empty Subset of Y;
  reconsider F=C as non empty Subset of Z by A1;
A2: Z|F is non empty;
A3: Y|V is non empty;
A4: the carrier of Y|B=[#](Y|B)
    .=B by PRE_TOPC:def 10;
  the carrier of X=dom f by FUNCT_2:def 1;
  then reconsider u=f as Function of X,Y|B by A1,A4,FUNCT_2:4;
  reconsider G=Z|C as non empty TopSpace by A2;
  reconsider H=Y|B as non empty TopSpace by A3;
  reconsider k=u as Function of X,H;
A5: u is continuous by A1,TOPMETR:9;
  reconsider j=h as Function of H,G;
A6: j*k is Function of X,G;
  reconsider w=h*k as Function of X,G;
  the carrier of (Z|C)=[#](Z|C)
    .=C by PRE_TOPC:def 10;
  then reconsider v=h*u as Function of X,Z by A6,FUNCT_2:9;
  v is continuous by A1,A5,PRE_TOPC:56;
  hence thesis;
end;

reserve p,q for Point of TOP-REAL 2;

definition
  func Out_In_Sq -> Function of NonZero TOP-REAL 2, NonZero TOP-REAL 2
  means
  :Def1:
  for p being Point of TOP-REAL 2 st p<>0.TOP-REAL 2 holds
  ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
  it.p=|[1/p`1,p`2/p`1/p`1]|) &
  (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
  it.p=|[p`1/p`2/p`2,1/p`2]|);
  existence
  proof
    reconsider BP= NonZero TOP-REAL 2 as non empty set
    by Th19;
    defpred P[set,set] means (for p being Point of TOP-REAL 2 st p=$1 holds
    ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)implies
    $2=|[1/p`1,p`2/p`1/p`1]|) &
    (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
    $2=|[p`1/p`2/p`2,1/p`2]|));
A1: for x being Element of BP ex y being Element of BP st P[x,y]
    proof
      let x be Element of BP;
      reconsider q=x as Point of TOP-REAL 2 by TARSKI:def 3;
      now per cases;
        case
A2:       (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1);
          now
            assume |[1/q`1,q`2/q`1/q`1]| in {0.TOP-REAL 2};
            then 0.TOP-REAL 2= |[1/q`1,q`2/q`1/q`1]| by TARSKI:def 1;
            then 0=1/q`1 & 0=q`2/q`1/q`1 by Th11,EUCLID:56;
            then
A3:         0=1/q`1*q`1;
            now per cases;
              case
A4:             q`1=0;
                then q`2=0 by A2;
                then q=0.TOP-REAL 2 by A4,EUCLID:57,58;
                then q in {0.TOP-REAL 2} by TARSKI:def 1;
                hence contradiction by XBOOLE_0:def 5;
              end;
              case q`1<>0;
                hence contradiction by A3,XCMPLX_1:88;
              end;
            end;
            hence contradiction;
          end;

then reconsider r= |[1/q`1,q`2/q`1/q`1]| as Element of BP by XBOOLE_0:def 5;
          for p being Point of TOP-REAL 2 st p=x holds
          ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)implies
          r=|[1/p`1,p`2/p`1/p`1]|) &
          (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
          r=|[p`1/p`2/p`2,1/p`2]|) by A2;
          hence thesis;
        end;
        case
A5:       not (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1);
          now
            assume |[q`1/q`2/q`2,1/q`2]| in {0.TOP-REAL 2};
            then 0.TOP-REAL 2= |[q`1/q`2/q`2,1/q`2]| by TARSKI:def 1;
            then (0.TOP-REAL 2)`2=1/q`2 & (0.TOP-REAL 2)`1= q`1/q`2/q`2
             by EUCLID:56;
            then
A6:         0=1/q`2*q`2 by Th11;
            q`2 <> 0 by A5;
           hence contradiction by A6,XCMPLX_1:88;
          end;

then reconsider r= |[q`1/q`2/q`2,1/q`2]| as Element of BP by XBOOLE_0:def 5;
          for p being Point of TOP-REAL 2 st p=x holds
          ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)implies
          r=|[1/p`1,p`2/p`1/p`1]|) &
          (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
          r=|[p`1/p`2/p`2,1/p`2]|) by A5;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    ex h being Function of BP, BP st for x being Element of BP holds
    P[x,h.x] from FUNCT_2:sch 3(A1);
    then consider h being Function of BP, BP such that
A7: for x being Element of BP holds
    for p being Point of TOP-REAL 2 st p=x holds
    ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)implies
    h.x=|[1/p`1,p`2/p`1/p`1]|) &
    (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
    h.x=|[p`1/p`2/p`2,1/p`2]|);
    for p being Point of TOP-REAL 2 st p<>0.TOP-REAL 2 holds
    ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)implies
    h.p=|[1/p`1,p`2/p`1/p`1]|) &
    (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
    h.p=|[p`1/p`2/p`2,1/p`2]|)
    proof
      let p be Point of TOP-REAL 2;
      assume p<>0.TOP-REAL 2;
      then not p in {0.TOP-REAL 2} by TARSKI:def 1;
      then p in NonZero TOP-REAL 2 by XBOOLE_0:def 5;
      hence thesis by A7;
    end;
    hence thesis;
  end;
  uniqueness
  proof
    let h1,h2 be Function of NonZero TOP-REAL 2,
    NonZero TOP-REAL 2;
    assume
A8: ( for p being Point of TOP-REAL 2 st p<>0.TOP-REAL 2 holds
    ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)implies
    h1.p=|[1/p`1,p`2/p`1/p`1]|) &
    (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
    h1.p=|[p`1/p`2/p`2,1/p`2]|))&
    ( for p being Point of TOP-REAL 2 st p<>0.TOP-REAL 2 holds
    ((p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)implies
    h2.p=|[1/p`1,p`2/p`1/p`1]|) &
    (not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) implies
    h2.p=|[p`1/p`2/p`2,1/p`2]|));
    for x being set st x in NonZero TOP-REAL 2
    holds h1.x=h2.x
    proof
      let x be set;
      assume
A9:   x in NonZero TOP-REAL 2;
      then reconsider q=x as Point of TOP-REAL 2;
      not q in {0.TOP-REAL 2} by A9,XBOOLE_0:def 5;
      then
A10:  q<>0.TOP-REAL 2 by TARSKI:def 1;
      now per cases;
        case
A11:      (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1);
          then h1.q=|[1/q`1,q`2/q`1/q`1]| by A8,A10;
          hence thesis by A8,A10,A11;
        end;
        case
A12:      not (q`2<=q`1 & -q`1<=q`2 or q`2>=q`1 & q`2<=-q`1);
          then h1.q=|[q`1/q`2/q`2,1/q`2]| by A8,A10;
          hence thesis by A8,A10,A12;
        end;
      end;
      hence thesis;
    end;
    hence h1=h2 by FUNCT_2:18;
  end;
end;

theorem Th23:
  for p being Point of TOP-REAL 2 st
  not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) holds
  p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2
proof
  let p being Point of TOP-REAL 2;
  assume
A1: not(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1);
A2: -p`1<p`2 implies --p`1>-p`2 by XREAL_1:26;
  -p`1>p`2 implies --p`1<-p`2 by XREAL_1:26;
  hence thesis by A1,A2;
end;

theorem Th24:
  for p being Point of TOP-REAL 2 st p<>0.TOP-REAL 2 holds
  ((p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2)implies
  Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]|) &
  (not (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) implies
  Out_In_Sq.p=|[1/p`1,p`2/p`1/p`1]|)
proof
  let p be Point of TOP-REAL 2;
  assume
A1: p<>0.TOP-REAL 2;
  hereby
    assume
A2: (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2);
    now per cases by A2;
      case
A3:     p`1<=p`2 & -p`2<=p`1;
        now
          assume
A4:       p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1;
A5:       now per cases by A4;
            case p`2<=p`1 & -p`1<=p`2;
              hence p`1=p`2 or p`1=-p`2 by A3,XXREAL_0:1;
            end;
            case p`2>=p`1 & p`2<=-p`1;
              then -p`2>=--p`1 by XREAL_1:26;
              hence p`1=p`2 or p`1=-p`2 by A3,XXREAL_0:1;
            end;
          end;
          now per cases by A5;
            case
A6:           p`1=p`2;
              then p`1<>0 by A1,EUCLID:57,58;
              then p`1/p`2/p`2=1/p`1 by A6,XCMPLX_1:60;
              hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A1,A4,A6,Def1;
            end;
            case
A7:           p`1=-p`2;
              then
A8:           -p`1=p`2;
A9:           p`2<>0 by A1,A7,EUCLID:57,58;
A10:          p`1/p`2/p`2=(-(p`2/p`2))/p`2 by A7
                .=(-1)/p`2 by A9,XCMPLX_1:60
                .= 1/p`1 by A7,XCMPLX_1:193;
              1/p`2= -(1/p`1) by A8,XCMPLX_1:189
                .=-(p`2/p`1/(-p`1)) by A7,A10,XCMPLX_1:193
                .=--(p`2/p`1/p`1) by XCMPLX_1:189
                .=p`2/p`1/p`1;
              hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A1,A4,A10,Def1;
            end;
          end;
          hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]|;
        end;
        hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A1,Def1;
      end;
      case
A11:    p`1>=p`2 & p`1<=-p`2;
        now
          assume
A12:      p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1;
A13:      now per cases by A12;
            case p`2<=p`1 & -p`1<=p`2;
              then --p`1>=-p`2 by XREAL_1:26;
              hence p`1=p`2 or p`1=-p`2 by A11,XXREAL_0:1;
            end;
            case p`2>=p`1 & p`2<=-p`1;
              hence p`1=p`2 or p`1=-p`2 by A11,XXREAL_0:1;
            end;
          end;
          now per cases by A13;
            case
A14:          p`1=p`2;
              then p`1 <> 0 by A1,EUCLID:57,58;
              then p`1/p`2/p`2=1/p`1 by A14,XCMPLX_1:60;
              hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A1,A12,A14,Def1;
            end;
            case
A15:          p`1=-p`2;
              then
A16:          -p`1=p`2;
A17:          p`2<>0 by A1,A15,EUCLID:57,58;
A18:          p`1/p`2/p`2 =(-(p`2/p`2))/p`2 by A15
                .=(-1)/p`2 by A17,XCMPLX_1:60
                .= 1/p`1 by A15,XCMPLX_1:193;
              then 1/p`2=-(p`1/p`2/p`2) by A16,XCMPLX_1:189
                .=-(p`2/p`1/(-p`1)) by A15,XCMPLX_1:192
                .=--(p`2/p`1/p`1) by XCMPLX_1:189
                .=p`2/p`1/p`1;
              hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A1,A12,A18,Def1;
            end;
          end;
          hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]|;
        end;
        hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A1,Def1;
      end;
    end;
    hence Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]|;
  end;
  hereby
    assume
A19: not(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2);
A20: -p`2<p`1 implies --p`2>-p`1 by XREAL_1:26;
    -p`2>p`1 implies --p`2<-p`1 by XREAL_1:26;
    hence Out_In_Sq.p=|[1/p`1,p`2/p`1/p`1]| by A1,A19,A20,Def1;
  end;
end;

theorem Th25:
  for D being Subset of TOP-REAL 2, K0 being Subset of (TOP-REAL 2)|D st
  K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2} holds
  rng (Out_In_Sq|K0) c= the carrier of ((TOP-REAL 2)|D)|K0
proof
  let D be Subset of TOP-REAL 2, K0 be Subset of (TOP-REAL 2)|D;
  assume
A1: K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2};
A2: the carrier of ((TOP-REAL 2)|D) =[#]((TOP-REAL 2)|D)
    .=D by PRE_TOPC:def 10;
  let y be set;
  assume y in rng (Out_In_Sq|K0);
  then consider x being set such that
A3: x in dom (Out_In_Sq|K0) & y=(Out_In_Sq|K0).x by FUNCT_1:def 5;
A4: x in (dom Out_In_Sq) /\ K0 by A3,RELAT_1:90;
  then
A5: x in K0 by XBOOLE_0:def 4;
  K0 c= the carrier of TOP-REAL 2 by A2,XBOOLE_1:1;
  then reconsider p=x as Point of TOP-REAL 2 by A5;
A6: Out_In_Sq.p=y by A3,A5,FUNCT_1:72;
  consider px being Point of TOP-REAL 2 such that
A7: x=px & (px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1)
  & px<>0.TOP-REAL 2 by A1,A5;
  reconsider K00=K0 as Subset of TOP-REAL 2 by A2,XBOOLE_1:1;
  K00=[#]((TOP-REAL 2)|K00) by PRE_TOPC:def 10
    .=the carrier of ((TOP-REAL 2)|K00);
  then
A8: p in the carrier of ((TOP-REAL 2)|K00) by A4,XBOOLE_0:def 4;
A9: for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K00 holds q`1<>0
  proof
    let q be Point of TOP-REAL 2;
    assume
A10: q in the carrier of (TOP-REAL 2)|K00;
    the carrier of (TOP-REAL 2)|K00=[#]((TOP-REAL 2)|K00)
      .=K0 by PRE_TOPC:def 10;
    then consider p3 being Point of TOP-REAL 2 such that
A11: q=p3 & ((p3`2<=p3`1 & -p3`1<=p3`2 or p3`2>=p3`1 & p3`2<=-p3`1)
    & p3<>0.TOP-REAL 2) by A1,A10;
    now
      assume
A12:  q`1=0;
      then q`2=0 by A11;
      hence contradiction by A11,A12,EUCLID:57,58;
    end;
    hence thesis;
  end;
  then
A13: p`1<>0 by A8;
  set p9=|[1/p`1,p`2/p`1/p`1]|;
A14: p9`1=1/p`1 & p9`2=p`2/p`1/p`1 by EUCLID:56;
A15: now
    assume p9=0.TOP-REAL 2;
    then 0 *p`1=1/p`1*p`1 by A14,EUCLID:56,58;
    hence contradiction by A8,A9,XCMPLX_1:88;
  end;
A16: Out_In_Sq.p=|[1/p`1,p`2/p`1/p`1]| by A7,Def1;
  now per cases;
    case
A17:  p`1>=0;
      then p`2/p`1<=p`1/p`1 & (-1 *p`1)/p`1<=p`2/p`1 or p`2>=p`1 & p`2<=-1 *p
      `1 by A7,XREAL_1:74;
      then
B18:    p`2/p`1<=1 & (-1)*p`1/p`1<=p`2/p`1 or p`2>=p`1 & p`2<=-1 *p`1
      by A8,A9,XCMPLX_1:60;
      then
      p`2/p`1<=1 & -1<=p`2/p`1 or p`2/p`1>=1 & p`2/p`1<=(-1)*p`1/p`1
      by A13,A17,XCMPLX_1:90;
      then (-1)/p`1<= p`2/p`1/p`1 by A17,XREAL_1:74;
      then
A19:  p`2/p`1/p`1 <=1/p`1 & -(1/p`1)<= p`2/p`1/p`1 or
      p`2/p`1/p`1 >=1/p`1 & p`2/p`1/p`1<= -(1/p`1)
      by A17,B18,A13,XREAL_1:74;
      p9`1=1/p`1 & p9`2=p`2/p`1/p`1 by EUCLID:56;
      hence y in K0 by A1,A6,A15,A16,A19;
    end;
    case
A20:  p`1<0;
      then p`2<=p`1 & (-1 *p`1)<=p`2 or p`2/p`1<=p`1/p`1 & p`2/p`1>=(-1 *p`1)/
      p`1 by A7,XREAL_1:75;
      then
B21:    p`2<=p`1 & (-1 *p`1)<=p`2 or p`2/p`1<=1 & p`2/p`1>=(-1)*p`1/p`1
      by A20,XCMPLX_1:60;
      then
A21:  p`2/p`1>=p`1/p`1 & (-1 *p`1)<=p`2 or p`2/p`1<=1 & p`2/p`1>=-1
      by A20,XCMPLX_1:90;
      not (p`2/p`1>=1 & p`2/p`1<=-1);
      then (-1)/p`1>= p`2/p`1/p`1 by A20,A21,XCMPLX_1:60,XREAL_1:75;
      then
A23:  p`2/p`1/p`1 <=1/p`1 & -(1/p`1)<= p`2/p`1/p`1 or
      p`2/p`1/p`1 >=1/p`1 & p`2/p`1/p`1<= -(1/p`1)
      by A20,B21,XREAL_1:75;
      p9`1=1/p`1 & p9`2=p`2/p`1/p`1 by EUCLID:56;
      hence y in K0 by A1,A6,A15,A16,A23;
    end;
  end;
  then y in [#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 10;
  hence thesis;
end;

theorem Th26:
  for D being Subset of TOP-REAL 2, K0 being Subset of (TOP-REAL 2)|D st
  K0={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2} holds
  rng (Out_In_Sq|K0) c= the carrier of ((TOP-REAL 2)|D)|K0
proof
  let D be Subset of TOP-REAL 2, K0 be Subset of (TOP-REAL 2)|D;
  assume
A1: K0={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2};
A2: the carrier of ((TOP-REAL 2)|D) =[#]((TOP-REAL 2)|D)
    .=D by PRE_TOPC:def 10;
  let y be set;
  assume y in rng (Out_In_Sq|K0);
  then consider x being set such that
A3: x in dom (Out_In_Sq|K0) & y=(Out_In_Sq|K0).x by FUNCT_1:def 5;
  x in (dom Out_In_Sq) /\ K0 by A3,RELAT_1:90;
  then
A4: x in K0 by XBOOLE_0:def 4;
  K0 c= the carrier of TOP-REAL 2 by A2,XBOOLE_1:1;
  then reconsider p=x as Point of TOP-REAL 2 by A4;
A5: Out_In_Sq.p=y by A3,A4,FUNCT_1:72;
  consider px being Point of TOP-REAL 2 such that
A6: x=px & (px`1<=px`2 & -px`2<=px`1 or px`1>=px`2 & px`1<=-px`2)
  & px<>0.TOP-REAL 2 by A1,A4;
  reconsider K00=K0 as Subset of TOP-REAL 2 by A2,XBOOLE_1:1;
A7: K00=[#]((TOP-REAL 2)|K00) by PRE_TOPC:def 10
    .=the carrier of ((TOP-REAL 2)|K00);
A8: for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K00 holds q`2<>0
  proof
    let q be Point of TOP-REAL 2;
    assume
A9: q in the carrier of (TOP-REAL 2)|K00;
    the carrier of (TOP-REAL 2)|K00=[#]((TOP-REAL 2)|K00)
      .=K0 by PRE_TOPC:def 10;
    then consider p3 being Point of TOP-REAL 2 such that
A10: q=p3 & ((p3`1<=p3`2 & -p3`2<=p3`1 or p3`1>=p3`2 & p3`1<=-p3`2)
    & p3<>0.TOP-REAL 2) by A1,A9;
    now
      assume
A11:  q`2=0;
      then q`1=0 by A10;
      hence contradiction by A10,A11,EUCLID:57,58;
    end;
    hence thesis;
  end;
  then
A12: p`2<>0 by A4,A7;
  set p9=|[p`1/p`2/p`2,1/p`2]|;
A13: p9`2=1/p`2 & p9`1=p`1/p`2/p`2 by EUCLID:56;
A14: now
    assume p9=0.TOP-REAL 2;
    then 0 *p`2=1/p`2*p`2 by A13,EUCLID:56,58;
    hence contradiction by A4,A7,A8,XCMPLX_1:88;
  end;
A15: Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A6,Th24;
  now per cases;
    case
A16:  p`2>=0;
      then p`1/p`2<=p`2/p`2 & (-1 *p`2)/p`2<=p`1/p`2 or p`1>=p`2 & p`1<=-1 *p
      `2 by A6,XREAL_1:74;
      then
B17:   p`1/p`2<=1 & (-1)*p`2/p`2<=p`1/p`2 or p`1>=p`2 & p`1<=-1 *p`2
      by A4,A7,A8,XCMPLX_1:60;
      then
      p`1/p`2<=1 & -1<=p`1/p`2 or p`1/p`2>=1 & p`1/p`2<=(-1)*p`2/p`2
      by A12,A16,XCMPLX_1:90;
      then (-1)/p`2<= p`1/p`2/p`2 by A16,XREAL_1:74;
      then
A18:  p`1/p`2/p`2 <=1/p`2 & -(1/p`2)<= p`1/p`2/p`2 or
      p`1/p`2/p`2 >=1/p`2 & p`1/p`2/p`2<= -(1/p`2)
      by A16,B17,A12,XREAL_1:74;
      p9`2=1/p`2 & p9`1=p`1/p`2/p`2 by EUCLID:56;
      hence y in K0 by A1,A5,A14,A15,A18;
    end;
    case
A19:  p`2<0;
      then p`1<=p`2 & (-1 *p`2)<=p`1 or p`1/p`2<=p`2/p`2 & p`1/p`2>=(-1 *p`2)/
      p`2 by A6,XREAL_1:75;
      then
B20:    p`1<=p`2 & (-1 *p`2)<=p`1 or p`1/p`2<=1 & p`1/p`2>=(-1)*p`2/p`2
      by A19,XCMPLX_1:60;
      then
      p`1/p`2>=1 & (-1)*p`2/p`2>=p`1/p`2 or p`1/p`2<=1 & p`1/p`2>=-1
      by A19,XCMPLX_1:90;
      then (-1)/p`2>= p`1/p`2/p`2 by A19,XREAL_1:75;
      then
A21:  p`1/p`2/p`2 <=1/p`2 & -(1/p`2)<= p`1/p`2/p`2 or
      p`1/p`2/p`2 >=1/p`2 & p`1/p`2/p`2<= -(1/p`2)
      by A19,B20,XREAL_1:75;
      p9`2=1/p`2 & p9`1=p`1/p`2/p`2 by EUCLID:56;
      hence y in K0 by A1,A5,A14,A15,A21;
    end;
  end;
  then y in [#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 10;
  hence thesis;
end;

XX: 0.TOP-REAL 2 = 0.REAL 2 by EUCLID:70;

theorem Th27:
  for K0a being set,D being non empty Subset of TOP-REAL 2
  st K0a={p where p is Point of TOP-REAL 2:
  (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2}
  & D`={0.TOP-REAL 2} holds K0a is non empty Subset of (TOP-REAL 2)|D &
  K0a is non empty Subset of TOP-REAL 2
proof
  let K0a be set,D be non empty Subset of TOP-REAL 2;
  assume
A1: K0a={p where p is Point of TOP-REAL 2:
  (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2}
  & D`={0.TOP-REAL 2};
  ((1.REAL 2)`2<=(1.REAL 2)`1 & -(1.REAL 2)`1<=(1.REAL 2)`2 or
  (1.REAL 2)`2>=(1.REAL 2)`1 & (1.REAL 2)`2<=-(1.REAL 2)`1) &
  (1.REAL 2)<>0.TOP-REAL 2 by Th13,REVROT_1:19,XX;
  then
A2: 1.REAL 2 in K0a by A1;
A3: the carrier of (TOP-REAL 2)|D=[#]((TOP-REAL 2)|D)
    .=D by PRE_TOPC:def 10;
A4: K0a c= D
  proof
    let x be set;
    assume x in K0a;
    then consider p8 being Point of TOP-REAL 2 such that
A5: x=p8 & (
    (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1) & p8<>0.TOP-REAL 2)
    by A1;
A6: D=D``
      .=NonZero TOP-REAL 2 by A1,SUBSET_1:def 5;
    not x in {0.TOP-REAL 2} by A5,TARSKI:def 1;
    hence thesis by A5,A6,XBOOLE_0:def 5;
  end;
  hence K0a is non empty Subset of ((TOP-REAL 2)|D) by A2,A3;
  thus thesis by A2,A4,XBOOLE_1:1;
end;

theorem Th28:
  for K0a being set,D being non empty Subset of TOP-REAL 2
  st K0a={p where p is Point of TOP-REAL 2:
  (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2}
  & D`={0.TOP-REAL 2} holds K0a is non empty Subset of (TOP-REAL 2)|D &
  K0a is non empty Subset of TOP-REAL 2
proof
  let K0a be set,D be non empty Subset of TOP-REAL 2;
  assume
A1: K0a={p where p is Point of TOP-REAL 2:
  (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2}
  & D`={0.TOP-REAL 2};
  ((1.REAL 2)`1<=(1.REAL 2)`2 & -(1.REAL 2)`2<=(1.REAL 2)`1 or
  (1.REAL 2)`1>=(1.REAL 2)`2 & (1.REAL 2)`1<=-(1.REAL 2)`2) &
  (1.REAL 2)<>0.TOP-REAL 2 by Th13,REVROT_1:19,XX;
  then
A2: 1.REAL 2 in K0a by A1;
A3: the carrier of (TOP-REAL 2)|D=[#]((TOP-REAL 2)|D)
    .=D by PRE_TOPC:def 10;
A4: K0a c= D
  proof
    let x be set;
    assume x in K0a;
    then consider p8 being Point of TOP-REAL 2 such that
A5: x=p8 & (
    (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2) & p8<>0.TOP-REAL 2)
    by A1;
A6: D=D``
      .=NonZero TOP-REAL 2 by A1,SUBSET_1:def 5;
    not x in {0.TOP-REAL 2} by A5,TARSKI:def 1;
    hence thesis by A5,A6,XBOOLE_0:def 5;
  end;
  hence K0a is non empty Subset of ((TOP-REAL 2)|D) by A2,A3;
  thus thesis by A2,A4,XBOOLE_1:1;
end;

theorem Th29:
  for X being non empty TopSpace,
  f1,f2 being Function of X,R^1 st f1 is continuous &
  f2 is continuous holds ex g being Function of X,R^1
  st (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g.p=r1+r2) & g is continuous
proof
  let X being non empty TopSpace,f1,f2 be Function of X,R^1;
  assume
A1: f1 is continuous & f2 is continuous;
  defpred P[set,set] means
  (for r1,r2 being real number st f1.$1=r1 & f2.$1=r2 holds $2=r1+r2);
A2: for x being Element of X ex y being Element of REAL st P[x,y]
  proof
    let x be Element of X;
    reconsider r1=f1.x as Real by TOPMETR:24;
    reconsider r2=f2.x as Real by TOPMETR:24;
    set r3=r1+r2;
    for r1,r2 being real number st f1.x=r1 & f2.x=r2 holds r3=r1+r2;
    hence ex y being Element of REAL
    st (for r1,r2 being real number st f1.x=r1 & f2.x=r2 holds y=r1+r2);
  end;
  ex f being Function of the carrier of X,REAL
  st for x being Element of X holds P[x,f.x] from FUNCT_2:sch 3(A2);
  then consider f being Function of the carrier of X,REAL such that
A3: for x being Element of X holds
  (for r1,r2 being real number st f1.x=r1 & f2.x=r2 holds f.x=r1+r2);
  reconsider g0=f as Function of X,R^1 by TOPMETR:24;
A4: for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g0.p=r1+r2 by A3;
  for p being Point of X,V being Subset of R^1 st g0.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & g0.:W c= V
  proof
    let p be Point of X,V be Subset of R^1;
    assume
A5: g0.p in V & V is open;
    reconsider r=g0.p as Real by TOPMETR:24;
    consider r0 being Real such that
A6: r0>0 & ].r-r0,r+r0.[ c= V by A5,FRECHET:8;
    reconsider r1=f1.p as Real by TOPMETR:24;
    reconsider G1=].r1-r0/2,r1+r0/2.[ as Subset of R^1 by TOPMETR:24;
    r0/2 > 0 by A6,XREAL_1:217;
    then
A7: r1<r1+r0/2 by XREAL_1:31;
    then r1-r0/2<r1 by XREAL_1:21;
    then
A8: f1.p in G1 by A7,XXREAL_1:4;
    G1 is open by JORDAN6:46;
    then consider W1 being Subset of X such that
A9: p in W1 & W1 is open & f1.:W1 c= G1 by A1,A8,Th20;
    reconsider r2=f2.p as Real by TOPMETR:24;
    reconsider G2=].r2-r0/2,r2+r0/2.[ as Subset of R^1 by TOPMETR:24;
    r0/2 > 0 by A6,XREAL_1:217;
    then
A10: r2<r2+r0/2 by XREAL_1:31;
    then r2-r0/2<r2 by XREAL_1:21;
    then
A11: f2.p in G2 by A10,XXREAL_1:4;
    G2 is open by JORDAN6:46;
    then consider W2 being Subset of X such that
A12: p in W2 & W2 is open & f2.:W2 c= G2 by A1,A11,Th20;
    set W=W1 /\ W2;
A13: W is open by A9,A12,TOPS_1:38;
A14: p in W by A9,A12,XBOOLE_0:def 4;
    g0.:W c= ].r-r0,r+r0.[
    proof
      let x be set;
      assume x in g0.:W;
      then consider z being set such that
A15:  z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
A16:  z in W1 by A15,XBOOLE_0:def 4;
      reconsider pz=z as Point of X by A15;
A17:  pz in the carrier of X;
      then pz in dom f1 by FUNCT_2:def 1;
      then
A18:  f1.pz in f1.:W1 by A16,FUNCT_1:def 12;
      reconsider aa1=f1.pz as Real by TOPMETR:24;
A19:  z in W2 by A15,XBOOLE_0:def 4;
      pz in dom f2 by A17,FUNCT_2:def 1;
      then
A20:  f2.pz in f2.:W2 by A19,FUNCT_1:def 12;
      reconsider aa2=f2.pz as Real by TOPMETR:24;
A21:  x=aa1+aa2 by A3,A15;
      then reconsider rx=x as Real;
A22:  r1-r0/2<aa1 & aa1<r1+r0/2 by A9,A18,XXREAL_1:4;
A23:  r2-r0/2<aa2 & aa2<r2+r0/2 by A12,A20,XXREAL_1:4;
      then aa1+aa2<r1+r0/2+(r2+r0/2) by A22,XREAL_1:10;
      then aa1+aa2<r1+r2+(r0/2+r0/2);
      then
A24:  rx<r+r0 by A3,A21;
      r1-r0/2+(r2-r0/2)<aa1+aa2 by A22,A23,XREAL_1:10;
      then r1+r2-(r0/2+r0/2)<aa1+aa2;
      then r-r0<aa1+aa2 by A3;
      hence thesis by A21,A24,XXREAL_1:4;
    end;
    hence thesis
    by A6,A13,A14,XBOOLE_1:1;
  end;
  then g0 is continuous by Th20;
  hence thesis by A4;
end;

theorem
  for X being non empty TopSpace, a being real number holds
  ex g being Function of X,R^1 st (for p being Point of X holds g.p=a)
  & g is continuous
proof
  let X be non empty TopSpace,a be real number;
  reconsider a1 = a as Element of R^1 by TOPMETR:24,XREAL_0:def 1;
  set g1 = (the carrier of X) --> a1;
  reconsider g0=g1 as Function of X,R^1;
A3: for p being Point of X holds g1.p=a by FUNCOP_1:13;
  for p being Point of X,V being Subset of R^1 st g0.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & g0.:W c= V
  proof
    let p be Point of X,V be Subset of R^1;
    assume
A4: g0.p in V & V is open;
    set f1=g0;
    set G1=V;
A5: f1.: [#]X c= G1
    proof
      let y be set;
      assume y in f1.: [#]X;
      then consider x being set such that
A6:   x in dom f1 & x in [#]X & y=f1.x by FUNCT_1:def 12;
      y=a by A6,FUNCOP_1:13;
      hence thesis by A4,FUNCOP_1:13;
    end;
    thus thesis by A5,TOPS_1:53;
  end;
  then g0 is continuous by Th20;
  hence thesis by A3;
end;

theorem Th31:
  for X being non empty TopSpace,
  f1,f2 being Function of X,R^1 st f1 is continuous &
  f2 is continuous holds ex g being Function of X,R^1
  st (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g.p=r1-r2) & g is continuous
proof
  let X being non empty TopSpace,f1,f2 be Function of X,R^1;
  assume
A1: f1 is continuous & f2 is continuous;
  defpred P[set,set] means
  (for r1,r2 being real number st f1.$1=r1 & f2.$1=r2 holds $2=r1-r2);
A2: for x being Element of X ex y being Element of REAL st P[x,y]
  proof
    let x be Element of X;
    reconsider r1=f1.x as Real by TOPMETR:24;
    reconsider r2=f2.x as Real by TOPMETR:24;
    set r3=r1-r2;
    for r1,r2 being real number st f1.x=r1 & f2.x=r2 holds r3=r1-r2;
    hence ex y being Element of REAL
    st (for r1,r2 being real number st f1.x=r1 & f2.x=r2 holds y=r1-r2);
  end;
  ex f being Function of the carrier of X,REAL
  st for x being Element of X holds P[x,f.x] from FUNCT_2:sch 3(A2);
  then consider f being Function of the carrier of X,REAL such that
A3: for x being Element of X holds
  (for r1,r2 being real number st f1.x=r1 & f2.x=r2 holds f.x=r1-r2);
  reconsider g0=f as Function of X,R^1 by TOPMETR:24;
A4: for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g0.p=r1-r2 by A3;
  for p being Point of X,V being Subset of R^1 st g0.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & g0.:W c= V
  proof
    let p be Point of X,V be Subset of R^1;
    assume
A5: g0.p in V & V is open;
    reconsider r=g0.p as Real by TOPMETR:24;
    consider r0 being Real such that
A6: r0>0 & ].r-r0,r+r0.[ c= V by A5,FRECHET:8;
    reconsider r1=f1.p as Real by TOPMETR:24;
    reconsider G1=].r1-r0/2,r1+r0/2.[ as Subset of R^1 by TOPMETR:24;
    r0/2 > 0 by A6,XREAL_1:217;
    then
A7: r1<r1+r0/2 by XREAL_1:31;
    then r1-r0/2<r1 by XREAL_1:21;
    then
A8: f1.p in G1 by A7,XXREAL_1:4;
    G1 is open by JORDAN6:46;
    then consider W1 being Subset of X such that
A9: p in W1 & W1 is open & f1.:W1 c= G1 by A1,A8,Th20;
    reconsider r2=f2.p as Real by TOPMETR:24;
    reconsider G2=].r2-r0/2,r2+r0/2.[ as Subset of R^1 by TOPMETR:24;
    r0/2 > 0 by A6,XREAL_1:217;
    then
A10: r2<r2+r0/2 by XREAL_1:31;
    then r2-r0/2<r2 by XREAL_1:21;
    then
A11: f2.p in G2 by A10,XXREAL_1:4;
    G2 is open by JORDAN6:46;
    then consider W2 being Subset of X such that
A12: p in W2 & W2 is open & f2.:W2 c= G2 by A1,A11,Th20;
    set W=W1 /\ W2;
A13: W is open by A9,A12,TOPS_1:38;
A14: p in W by A9,A12,XBOOLE_0:def 4;
    g0.:W c= ].r-r0,r+r0.[
    proof
      let x be set;
      assume x in g0.:W;
      then consider z being set such that
A15:  z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
A16:  z in W1 by A15,XBOOLE_0:def 4;
      reconsider pz=z as Point of X by A15;
A17:  pz in the carrier of X;
      then pz in dom f1 by FUNCT_2:def 1;
      then
A18:  f1.pz in f1.:W1 by A16,FUNCT_1:def 12;
      reconsider aa1=f1.pz as Real by TOPMETR:24;
A19:  z in W2 by A15,XBOOLE_0:def 4;
      pz in dom f2 by A17,FUNCT_2:def 1;
      then
A20:  f2.pz in f2.:W2 by A19,FUNCT_1:def 12;
      reconsider aa2=f2.pz as Real by TOPMETR:24;
A21:  x=aa1-aa2 by A3,A15;
      then reconsider rx=x as Real;
A22:  r1-r0/2<aa1 & aa1<r1+r0/2 by A9,A18,XXREAL_1:4;
A23:  r2-r0/2<aa2 & aa2<r2+r0/2 by A12,A20,XXREAL_1:4;
      then aa1-aa2<r1+r0/2-(r2-r0/2) by A22,XREAL_1:16;
      then aa1-aa2<r1-r2+(r0/2+r0/2);
      then
A24:  rx<r+r0 by A3,A21;
      r1-r0/2-(r2+r0/2)<aa1-aa2 by A22,A23,XREAL_1:16;
      then r1-r2-(r0/2+r0/2)<aa1-aa2;
      then r-r0<aa1-aa2 by A3;
      hence thesis by A21,A24,XXREAL_1:4;
    end;
    hence thesis
    by A6,A13,A14,XBOOLE_1:1;
  end;
  then g0 is continuous by Th20;
  hence thesis by A4;
end;

theorem Th32:

for X being non empty TopSpace, f1 being Function of X,R^1 st f1 is continuous
  ex g being Function
  of X,R^1 st (for p being Point of X,r1 being real number st
  f1.p=r1 holds g.p=r1*r1) & g is continuous
proof
  let X being non empty TopSpace,f1 be Function of X,R^1;
  assume
A1: f1 is continuous;
  defpred P[set,set] means
  (for r1 being real number st f1.$1=r1 holds $2=r1*r1);
A2: for x being Element of X ex y being Element of REAL st P[x,y]
  proof
    let x be Element of X;
    reconsider r1=f1.x as Real by TOPMETR:24;
    set r3=r1*r1;
    for r1 being real number st f1.x=r1 holds r3=r1*r1;
    hence ex y being Element of REAL
    st (for r1 being real number st f1.x=r1 holds y=r1*r1);
  end;
  ex f being Function of the carrier of X,REAL
  st for x being Element of X holds P[x,f.x] from FUNCT_2:sch 3(A2);
  then consider f being Function of the carrier of X,REAL such that
A3: for x being Element of X holds
  (for r1 being real number st f1.x=r1 holds f.x=r1*r1);
  reconsider g0=f as Function of X,R^1 by TOPMETR:24;
A4: for p being Point of X,r1 being real number st
  f1.p=r1 holds g0.p=r1*r1 by A3;
  for p being Point of X,V being Subset of R^1 st g0.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & g0.:W c= V
  proof
    let p be Point of X,V be Subset of R^1;
    assume
A5: g0.p in V & V is open;
    reconsider r=g0.p as Real by TOPMETR:24;
    consider r0 being Real such that
A6: r0>0 & ].r-r0,r+r0.[ c= V by A5,FRECHET:8;
    reconsider r1=f1.p as Real by TOPMETR:24;
A7: r=r1*r1 by A3;
A8: r=r1^2 by A3;
A9: 0<=r by A7,XREAL_1:65;
A11: (sqrt(r+r0))^2=r+r0 by A9,A6,SQUARE_1:def 4;
    now per cases;
      case
A12:    r1>=0;
        then
A13:    r1=sqrt r by A8,SQUARE_1:def 4;
        set r4=sqrt(r+r0)-sqrt(r);
        r+r0>r by A6,XREAL_1:31;
        then sqrt(r+r0)>sqrt(r) by A7,SQUARE_1:95,XREAL_1:65;
        then
A14:    r4>0 by XREAL_1:52;
A15:    r4^2=(sqrt(r+r0))^2-2*sqrt(r+r0)*sqrt(r)+(sqrt(r))^2
          .=r+r0-2*sqrt(r+r0)*sqrt(r)+(sqrt(r))^2
           by A9,A6,SQUARE_1:def 4
          .=r+(r0-2*sqrt(r+r0)*sqrt(r))+r by A9,SQUARE_1:def 4
          .=2*r+r0-2*sqrt(r+r0)*sqrt(r);
        reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:24;
A16:    r1<r1+r4 by A14,XREAL_1:31;
        then r1-r4<r1 by XREAL_1:21;
        then
A17:    f1.p in G1 by A16,XXREAL_1:4;
        G1 is open by JORDAN6:46;
        then consider W1 being Subset of X such that
A18:    p in W1 & W1 is open & f1.:W1 c= G1 by A1,A17,Th20;
        set W=W1;
        g0.:W c= ].r-r0,r+r0.[
        proof
          let x be set;
          assume x in g0.:W;
          then consider z being set such that
A19:      z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
          reconsider pz=z as Point of X by A19;
          pz in the carrier of X;
          then pz in dom f1 by FUNCT_2:def 1;
          then
A20:      f1.pz in f1.:W1 by A19,FUNCT_1:def 12;
          reconsider aa1=f1.pz as Real by TOPMETR:24;
A21:      x=aa1*aa1 by A3,A19;
A22:      r1-r4<aa1 & aa1<r1+r4 by A18,A20,XXREAL_1:4;
          -r1-r4<=r1-r4 by A12,XREAL_1:11;
          then -(r1+r4)<aa1 by A22,XXREAL_0:2;
          then
A23:      aa1--(r1+r4)>0 by XREAL_1:52;
          r1+r4-aa1>0 by A22,XREAL_1:52;
          then (r1+r4-aa1)*(r1+r4+aa1)>0 by A23,XREAL_1:131;
          then (r1+r4)^2-aa1^2>0;
          then
A24:      aa1^2<(r1+r4)^2 by XREAL_1:49;
          now per cases;
            case 0<=r1-r4;
              then
A25:          (r1-r4)^2<aa1^2 by A22,SQUARE_1:78;
              (-2)*r4^2<=0 by XREAL_1:65,133;
              then (r1-r4)^2 -aa1^2+-2*r4^2<= (r1-r4)^2 -aa1^2+0 by XREAL_1:9;
              then (r1-r4)^2 -2*r4^2 -aa1^2<0 by A25,XREAL_1:51;
              hence r-r0< aa1*aa1 by A7,A13,A15,XREAL_1:50;
            end;
            case 0>r1-r4;
              then r1<r4 by XREAL_1:50;
              then r1^2<r4^2 by A12,SQUARE_1:78;
              then
A26:          r1^2-r4^2<0 by XREAL_1:51;
              r1^2-r4^2 -2*r1*r4<0-0 by A26,A14,A12;
              hence r-r0< aa1*aa1 by A7,A13,A15,XREAL_1:65;
            end;
          end;
          hence thesis by A7,A13,A15,A21,A24,XXREAL_1:4;
        end;

hence thesis by A6,A18,XBOOLE_1:1;
      end;
      case
A27:    r1<0;
A29:    (-r1)^2=r1^2;
        then
A30:    -r1=sqrt r by A7,A27,SQUARE_1:89;
        set r4=sqrt(r+r0)-sqrt(r);
        r+r0>r by A6,XREAL_1:31;
        then sqrt(r+r0)>sqrt(r) by A7,SQUARE_1:95,XREAL_1:65;
        then
A31:    r4>0 by XREAL_1:52;
A32:    r4^2=r+r0-2*sqrt(r+r0)*sqrt(r)+(sqrt(r))^2 by A11
          .=r+(r0-2*sqrt(r+r0)*sqrt(r))+r by A7,A27,A29,SQUARE_1:89
          .=2*r+r0-2*sqrt(r+r0)*sqrt(r);
        then
A33:    -2*r1*r4+r4^2=r0 by A7,A30;
        reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:24;
A34:    r1<r1+r4 by A31,XREAL_1:31;
        then r1-r4<r1 by XREAL_1:21;
        then
A35:    f1.p in G1 by A34,XXREAL_1:4;
        G1 is open by JORDAN6:46;
        then consider W1 being Subset of X such that
A36:    p in W1 & W1 is open & f1.:W1 c= G1 by A1,A35,Th20;
        set W=W1;
        g0.:W c= ].r-r0,r+r0.[
        proof
          let x be set;
          assume x in g0.:W;
          then consider z being set such that
A37:      z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
          reconsider pz=z as Point of X by A37;
          pz in the carrier of X;
          then pz in dom f1 by FUNCT_2:def 1;
          then
A38:      f1.pz in f1.:W1 by A37,FUNCT_1:def 12;
          reconsider aa1=f1.pz as Real by TOPMETR:24;
A39:      x=aa1*aa1 by A3,A37;
A40:      r1-r4<aa1 & aa1<r1+r4 by A36,A38,XXREAL_1:4;
          -r1-r4>=r1-r4 by A27,XREAL_1:11;
          then -(-r1-r4)<=-(r1-r4) by XREAL_1:26;
          then -(r1-r4)>aa1 by A40,XXREAL_0:2;
          then
A41:      -(r1-r4)+(r1-r4)>aa1+(r1-r4) by XREAL_1:10;
          aa1-(r1-r4)>0 by A40,XREAL_1:52;
          then -(-aa1+(r1-r4))>0;
          then ((r1-r4)+-aa1)<0;
          then (r1-r4-aa1)*(r1-r4+aa1)>0 by A41,XREAL_1:132;
          then (r1-r4)^2-aa1^2>0;
          then
A42:      aa1^2<(r1-r4)^2 by XREAL_1:49;
          now per cases;
            case
S:           0>=r1+r4;
              -aa1>-(r1+r4) by A40,XREAL_1:26;
              then
A44:          (-(r1+r4))^2<(-aa1)^2 by S,SQUARE_1:78;
              (-2)*r4^2<=0 by XREAL_1:65,133;
              then (r1+r4)^2 -aa1^2+-2*r4^2<= (r1+r4)^2 -aa1^2+0 by XREAL_1:9;
              then (r1+r4)^2 -2*r4^2 -aa1^2<0 by A44,XREAL_1:51;
              hence r-r0< aa1*aa1 by A7,A33,XREAL_1:50;
            end;
            case 0<r1+r4;
              then 0+-r1<(r1+r4)+-r1 by XREAL_1:10;
              then (-r1)^2<r4^2 by A27,SQUARE_1:78;
              then
A45:          r1^2 -r1^2>r1^2-r4^2 by XREAL_1:17;
              r1^2-r4^2 +2*r1*r4<0+0 by A45,A31,A27;
              hence r-r0< aa1*aa1 by A7,A30,A32,XREAL_1:65;
            end;
          end;
          hence thesis by A7,A33,A39,A42,XXREAL_1:4;
        end;

hence thesis by A6,A36,XBOOLE_1:1;
      end;
    end;
    hence thesis;
  end;
  then g0 is continuous by Th20;
  hence thesis by A4;
end;

theorem Th33:
  for X being non empty TopSpace,
  f1 being Function of X,R^1,a being real number st f1 is continuous
  holds ex g being Function of X,R^1
  st (for p being Point of X,r1 being real number st
  f1.p=r1 holds g.p=a*r1) & g is continuous
proof
  let X being non empty TopSpace,f1 be Function of X,R^1,a being real number;
  assume
A1: f1 is continuous;
  defpred P[set,set] means (for r1 being Real st f1.$1=r1 holds $2=a*r1);
A2: for x being Element of X ex y being Element of REAL st P[x,y]
  proof
    let x be Element of X;
    reconsider r1=f1.x as Real by TOPMETR:24;
    reconsider r3=a*r1 as Element of REAL;
    for r1 being Real st f1.x=r1 holds r3=a*r1;
    hence ex y being Element of REAL
    st (for r1 being Real st f1.x=r1 holds y=a*r1);
  end;
  ex f being Function of the carrier of X,REAL
  st for x being Element of X holds P[x,f.x] from FUNCT_2:sch 3(A2);
  then consider f being Function of the carrier of X,REAL such that
A3: for x being Element of X holds
  (for r1 being Real st f1.x=r1 holds f.x=a*r1);
  reconsider g0=f as Function of X,R^1 by TOPMETR:24;
A4: for p being Point of X,r1 being real number st f1.p=r1 holds g0.p=a*r1
  proof
    let p be Point of X, r1 be real number such that
A5: f1.p=r1;
    reconsider r1 as Element of REAL by XREAL_0:def 1;
    g0.p=a*r1 by A3,A5;
    hence thesis;
  end;
  for p being Point of X,V being Subset of R^1 st g0.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & g0.:W c= V
  proof
    let p be Point of X,V be Subset of R^1;
    assume
A6: g0.p in V & V is open;
    reconsider r=g0.p as Real by TOPMETR:24;
    consider r0 being Real such that
A7: r0>0 & ].r-r0,r+r0.[ c= V by A6,FRECHET:8;
    reconsider r1=f1.p as Real by TOPMETR:24;
A8: r=a*r1 by A3;
A9: r=a*r1 by A3;
    now per cases;
      case
A10:    a>=0;
        now per cases by A10;
          case
A11:        a>0;
            set r4=r0/a;
A12:        r4>0 by A7,A11,XREAL_1:141;
            reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:24;
A13:        r1<r1+r4 by A12,XREAL_1:31;
            then r1-r4<r1 by XREAL_1:21;
            then
A14:        f1.p in G1 by A13,XXREAL_1:4;
            G1 is open by JORDAN6:46;
            then consider W1 being Subset of X such that
A15:        p in W1 & W1 is open & f1.:W1 c= G1 by A1,A14,Th20;
            set W=W1;
            g0.:W c= ].r-r0,r+r0.[
            proof
              let x be set;
              assume x in g0.:W;
              then consider z being set such that
A16:          z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
              reconsider pz=z as Point of X by A16;
              pz in the carrier of X;
              then pz in dom f1 by FUNCT_2:def 1;
              then
A17:          f1.pz in f1.:W1 by A16,FUNCT_1:def 12;
              reconsider aa1=f1.pz as Real by TOPMETR:24;
A18:          x=a*aa1 by A3,A16;
              reconsider rx=x as Real by A16,XREAL_0:def 1;
A19:          r1-r4<aa1 & aa1<r1+r4 by A15,A17,XXREAL_1:4;
              a*(r1+r4) =a*r1+a*r4
                .=r+r0 by A8,A11,XCMPLX_1:88;
              then
A20:          rx<r+r0 by A11,A18,A19,XREAL_1:70;
A21:          a*(r1-r4)<a*aa1 by A11,A19,XREAL_1:70;
              a*(r1-r4) =a*r1-a*r4
                .=r-r0 by A8,A11,XCMPLX_1:88;
              hence thesis by A18,A20,A21,XXREAL_1:4;
            end;

hence thesis by A7,A15,XBOOLE_1:1;
          end;
          case
A22:        a=0;
            set r4=r0;
            reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:24;
A23:        r1<r1+r4 by A7,XREAL_1:31;
            then r1-r4<r1 by XREAL_1:21;
            then
A24:        f1.p in G1 by A23,XXREAL_1:4;
            G1 is open by JORDAN6:46;
            then consider W1 being Subset of X such that
A25:        p in W1 & W1 is open & f1.:W1 c= G1 by A1,A24,Th20;
            set W=W1;
            g0.:W c= ].r-r0,r+r0.[
            proof
              let x be set;
              assume x in g0.:W;
              then consider z being set such that
A26:          z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
              reconsider pz=z as Point of X by A26;
              reconsider aa1=f1.pz as Real by TOPMETR:24;
              x=a*aa1 by A3,A26
                .=0 by A22;
              hence thesis by A7,A9,A22,XXREAL_1:4;
            end;

hence thesis by A7,A25,XBOOLE_1:1;
          end;
        end;
        hence thesis;
      end;
      case
A28:    a<0;
        then
A29:    -a>0 by XREAL_1:60;
A30:    -a<>0 by A28;
        set r4=r0/(-a);
A31:    r4>0 by A7,A29,XREAL_1:141;
A32:    (-a)*r4=r0 by A30,XCMPLX_1:88;
        reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:24;
A33:    r1<r1+r4 by A31,XREAL_1:31;
        then r1-r4<r1 by XREAL_1:21;
        then
A34:    f1.p in G1 by A33,XXREAL_1:4;
        G1 is open by JORDAN6:46;
        then consider W1 being Subset of X such that
A35:    p in W1 & W1 is open & f1.:W1 c= G1 by A1,A34,Th20;
        set W=W1;
        g0.:W c= ].r-r0,r+r0.[
        proof
          let x be set;
          assume x in g0.:W;
          then consider z being set such that
A36:      z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
          reconsider pz=z as Point of X by A36;
          pz in the carrier of X;
          then pz in dom f1 by FUNCT_2:def 1;
          then
A37:      f1.pz in f1.:W1 by A36,FUNCT_1:def 12;
          reconsider aa1=f1.pz as Real by TOPMETR:24;
A38:      x=a*aa1 by A3,A36;
A39:      r1-r4<aa1 & aa1<r1+r4 by A35,A37,XXREAL_1:4;
          then
A40:      (a)*aa1<(a)*(r1-r4) by A28,XREAL_1:71;
A41:      (a)*(r1-r4) =a*r1+-(a*r4)
            .=r+r0 by A4,A32;
          (a)*(r1+r4) =a*r1--(a*r4)
            .=r-r0 by A4,A32;
          then r-r0< (a)*aa1 by A28,A39,XREAL_1:71;
          hence thesis by A38,A40,A41,XXREAL_1:4;
        end;

hence thesis by A7,A35,XBOOLE_1:1;
      end;
    end;
    hence thesis;
  end;
  then g0 is continuous by Th20;
  hence thesis by A4;
end;

theorem Th34:
  for X being non empty TopSpace,
  f1 being Function of X,R^1,a being real number st f1 is continuous
  holds ex g being Function of X,R^1
  st (for p being Point of X,r1 being real number st
  f1.p=r1 holds g.p=r1+a) & g is continuous
proof
  let X being non empty TopSpace,f1 be Function of X,R^1,a being real number;
  assume
A1: f1 is continuous;
  defpred P[set,set] means (for r1 being Real st f1.$1=r1 holds $2=r1+a);
A2: for x being Element of X ex y being Element of REAL st P[x,y]
  proof
    let x be Element of X;
    reconsider r1=f1.x as Real by TOPMETR:24;
    reconsider r2=a as Element of REAL by XREAL_0:def 1;
    set r3 =r1+r2;
    for r1 being Real st f1.x=r1 holds r3=r1+r2;
    hence ex y being Element of REAL
    st (for r1 being Real st f1.x=r1 holds y=r1+a);
  end;
  ex f being Function of the carrier of X,REAL
  st for x being Element of X holds P[x,f.x] from FUNCT_2:sch 3(A2);
  then consider f being Function of the carrier of X,REAL such that
A3: for x being Element of X holds
  (for r1 being Real st f1.x=r1 holds f.x=r1+a);
  reconsider g0=f as Function of X,R^1 by TOPMETR:24;
A4: for p being Point of X,r1 being real number st f1.p=r1 holds g0.p=r1+a
  proof
    let p be Point of X, r1 be real number such that
A5: f1.p=r1;
    reconsider r1 as Element of REAL by XREAL_0:def 1;
    g0.p=r1+a by A3,A5;
    hence thesis;
  end;
  for p being Point of X,V being Subset of R^1 st g0.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & g0.:W c= V
  proof
    let p be Point of X,V be Subset of R^1;
    assume
A6: g0.p in V & V is open;
    reconsider r=g0.p as Real by TOPMETR:24;
    consider r0 being Real such that
A7: r0>0 & ].r-r0,r+r0.[ c= V by A6,FRECHET:8;
    reconsider r1=f1.p as Real by TOPMETR:24;
    set r4=r0;
    reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:24;
A8: r1<r1+r4 by A7,XREAL_1:31;
    then r1-r4<r1 by XREAL_1:21;
    then
A9: f1.p in G1 by A8,XXREAL_1:4;
    G1 is open by JORDAN6:46;
    then consider W1 being Subset of X such that
A10: p in W1 & W1 is open & f1.:W1 c= G1 by A1,A9,Th20;
    set W=W1;
    g0.:W c= ].r-r0,r+r0.[
    proof
      let x be set;
      assume x in g0.:W;
      then consider z being set such that
A11:  z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
      reconsider pz=z as Point of X by A11;
      pz in the carrier of X;
      then pz in dom f1 by FUNCT_2:def 1;
      then
A12:  f1.pz in f1.:W1 by A11,FUNCT_1:def 12;
      reconsider aa1=f1.pz as Real by TOPMETR:24;
A13:  x=aa1+a by A3,A11;
A14:  r1-r4<aa1 & aa1<r1+r4 by A10,A12,XXREAL_1:4;
      then
A15:  (r1+r4)+a>aa1+a by XREAL_1:10;
A16:  (r1-r4)+a<aa1+a by A14,XREAL_1:10;
      (r1-r4)+a =r1+a-r4
        .=r-r0 by A3;
      hence thesis by A13,A15,A16,XXREAL_1:4;
    end;
    hence thesis by A7,A10,XBOOLE_1:1;
  end;
  then g0 is continuous by Th20;
  hence thesis by A4;
end;

theorem Th35:
  for X being non empty TopSpace,
  f1,f2 being Function of X,R^1 st f1 is continuous &
  f2 is continuous holds ex g being Function of X,R^1
  st (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g.p=r1*r2) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume
A1: f1 is continuous & f2 is continuous;
  then consider g1 being Function of X,R^1 such that
A2: (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g1.p=r1+r2) & g1 is continuous by Th29;
  consider g2 being Function of X,R^1 such that
A3: (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g2.p=r1-r2) & g2 is continuous by A1,Th31;
  consider g3 being Function of X,R^1 such that
A4: (for p being Point of X,r1 being real number st
  g1.p=r1 holds g3.p=r1*r1) & g3 is continuous by A2,Th32;
  consider g4 being Function of X,R^1 such that
A5: (for p being Point of X,r1 being real number st
  g2.p=r1 holds g4.p=r1*r1) & g4 is continuous by A3,Th32;
  consider g5 being Function of X,R^1 such that
A6: (for p being Point of X,r1,r2 being real number st
  g3.p=r1 & g4.p=r2 holds g5.p=r1-r2) & g5 is continuous by A4,A5,Th31;
  consider g6 being Function of X,R^1 such that
A7: (for p being Point of X,r1 being real number st
  g5.p=r1 holds g6.p=(1/4)*r1) & g6 is continuous by A6,Th33;
  for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g6.p=r1*r2
  proof
    let p be Point of X,r1,r2 be real number;
    assume
A8: f1.p=r1 & f2.p=r2;
    then
A9: g1.p=r1+r2 by A2;
A10: g2.p=r1-r2 by A3,A8;
A11: g3.p=(r1+r2)^2 by A4,A9;
    g4.p=(r1-r2)^2 by A5,A10;
    then g5.p= (r1+r2)^2 -(r1-r2)^2 by A6,A11;
    then g6.p=(1/4)*( r1^2+2*r1*r2+r2^2 -(r1-r2)^2) by A7
      .= r1*r2;
    hence thesis;
  end;
  hence thesis by A7;
end;

theorem Th36:
  for X being non empty TopSpace,
  f1 being Function of X,R^1 st f1 is continuous &
  (for q being Point of X holds f1.q<>0) holds ex g being Function of X,R^1
  st (for p being Point of X,r1 being real number st
  f1.p=r1 holds g.p=1/r1) & g is continuous
proof
  let X being non empty TopSpace,f1 be Function of X,R^1;
  assume
A1: f1 is continuous & (for q being Point of X holds f1.q<>0);
  defpred P[set,set] means (for r1 being Real st f1.$1=r1 holds $2=1/r1);
A2: for x being Element of X ex y being Element of REAL st P[x,y]
  proof
    let x be Element of X;
    reconsider r1=f1.x as Real by TOPMETR:24;
    set r3=1/r1;
    for r1 being Real st f1.x=r1 holds r3=1/r1;
    hence ex y being Element of REAL
    st (for r1 being Real st f1.x=r1 holds y=1/r1);
  end;
  ex f being Function of the carrier of X,REAL
  st for x being Element of X holds P[x,f.x] from FUNCT_2:sch 3(A2);
  then consider f being Function of the carrier of X,REAL such that
A3: for x being Element of X holds
  (for r1 being Real st f1.x=r1 holds f.x=1/r1);
  reconsider g0=f as Function of X,R^1 by TOPMETR:24;
A4: for p being Point of X,r1 being real number st f1.p=r1 holds g0.p=1/r1
  proof
    let p be Point of X,r1 be real number such that
A5: f1.p=r1;
    reconsider r1 as Element of REAL by XREAL_0:def 1;
    g0.p=1/r1 by A3,A5;
    hence thesis;
  end;
  for p being Point of X,V being Subset of R^1 st g0.p in V & V is open holds
  ex W being Subset of X st p in W & W is open & g0.:W c= V
  proof
    let p be Point of X,V be Subset of R^1;
    assume
A6: g0.p in V & V is open;
    reconsider r=g0.p as Real by TOPMETR:24;
    consider r0 being Real such that
A7: r0>0 & ].r-r0,r+r0.[ c= V by A6,FRECHET:8;
    reconsider r1=f1.p as Real by TOPMETR:24;
A8: r1<>0 by A1;
A9: r=1/r1 by A3;
    then
A10: r=r1";
    now per cases;
      case
A11:    r1>=0;
        then
A12:    0<r by A8,A10;
A16:    1 *(1/r)=r1 by A9;
        set r4=r0/r/(r+r0);
A17:    r0/r>0 by A7,A12,XREAL_1:141;
A19:    r4>0 by A11,A17,A7,A9,XREAL_1:141;
A20:    r1-r4=1/r-r0/(r+r0)/r by A16
          .=(1-r0/(r+r0))/r
          .=((r+r0)/(r+r0)-r0/(r+r0))/r
                  by A11,A7,A9,XCMPLX_1:60
          .=((r+r0-r0)/(r+r0))/r
          .=r/(r+r0)/r;
        r/(r+r0)>0 by A12,A7,XREAL_1:141;
        then
A21:    r1-r4>0 by A12,A20,XREAL_1:141;
        reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:24;
A22:    r1<r1+r4 by A19,XREAL_1:31;
        then r1-r4<r1 by XREAL_1:21;
        then
A23:    f1.p in G1 by A22,XXREAL_1:4;
        G1 is open by JORDAN6:46;
        then consider W1 being Subset of X such that
A24:    p in W1 & W1 is open & f1.:W1 c= G1 by A1,A23,Th20;
        set W=W1;
        g0.:W c= ].r-r0,r+r0.[
        proof
          let x be set;
          assume x in g0.:W;
          then consider z being set such that
A25:      z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
          reconsider pz=z as Point of X by A25;
          pz in the carrier of X;
          then pz in dom f1 by FUNCT_2:def 1;
          then
A26:      f1.pz in f1.:W1 by A25,FUNCT_1:def 12;
          reconsider aa1=f1.pz as Real by TOPMETR:24;
A27:      x=1/aa1 by A3,A25;
A28:      r1-r4<aa1 & aa1<r1+r4 by A24,A26,XXREAL_1:4;
          then
A29:      1/(1/r+r4)<1/aa1 by A16,A21,XREAL_1:78;
A30:      0<r0^2 by A7,SQUARE_1:74;
          r0 *r< r0 *r+(r0 *r0+r0 *r0) by A30,XREAL_1:31;
          then r0 *r-(r0 *r0+r0 *r0)< r0 *r by XREAL_1:21;
          then (r0 *r-(r0 *r0+r0 *r0))+ r*r<r*r+r0 *r by XREAL_1:10;
          then (r-r0)*(r+r0+r0)/(r+r0+r0)<r*(r+r0)/(r+r0+r0)
          by A11,A7,A9,XREAL_1:76;
          then r-r0<r*(r+r0)/(r+r0+r0) by A9,A11,A7,XCMPLX_1:90;
          then r-r0<r/((r+r0+r0)/(r+r0)) by XCMPLX_1:78;
          then r-r0<r/((r+r0)/(r+r0)+r0/(r+r0));
          then r-r0<r*1/(1+r0/(r+r0))
                 by A11,A7,A9,XCMPLX_1:60;
          then r-r0<1/((1+r0/(r+r0))/r) by XCMPLX_1:78;
          then r-r0<1/(1/r+r0/(r+r0)/r);
          then r-r0<1/(1/r+r0/r/(r+r0));
          then
A31:      r-r0<1/aa1 by A29,XXREAL_0:2;
A32:      1/aa1<1/(r1-r4) by A21,A28,XREAL_1:90;
          1/(r1-r4) =1/(r1-r0 *r"/(r+r0))
            .=1/(r1-r0 *(1/r)/(r+r0))
            .=1/(r1-r0/((r+r0)/r1)) by A16,XCMPLX_1:78
            .=1/(r1*1-r1*(r0/(r+r0))) by XCMPLX_1:82
            .=1/((1-(r0/(r+r0)))*r1)
            .=1/(((r+r0)/(r+r0)-(r0/(r+r0)))*r1)
             by A11,A7,A9,XCMPLX_1:60
            .=1/((r+r0-r0)/(r+r0)*r1)
            .=1/(r/((r+r0)/r1)) by XCMPLX_1:82
            .=1/(r*r1/(r+r0)) by XCMPLX_1:78
            .=(r+r0)/(r*r1)*1 by XCMPLX_1:81
            .=(r+r0)/1 by A8,A10,XCMPLX_0:def 7
            .=r+r0;
          hence thesis by A27,A31,A32,XXREAL_1:4;
        end;

hence thesis by A7,A24,XBOOLE_1:1;
      end;
      case
S:      r1<0;
        then
A33:    r1"<0;
        then
A34:    0<-r by A10,XREAL_1:60;
        r1*((-r)*(1/(-r)))=r1*1 by A34,XCMPLX_1:88;
        then (-(r*r1))*(1/(-r))=r1;
        then
A38:    (-1)*(1/(-r))=r1 by A1,A9,XCMPLX_1:88;
        set r4=r0/(-r)/(-r+r0);
A39:    r0/(-r)>0 by A7,A34,XREAL_1:141;
A41:    r4>0 by S,A39,A7,A9,XREAL_1:141;
A42:    r1+r4=-(1/(-r))+r0/(-r+r0)/(-r) by A38
          .=(-1)/(-r)+r0/(-r+r0)/(-r)
          .=(-1+r0/(-r+r0))/(-r)
          .=(-((-r+r0)/(-r+r0))+r0/(-r+r0))/(-r) by A9,A7,S,XCMPLX_1:60
          .=((-(-r+r0))/(-r+r0)+r0/(-r+r0))/(-r)
          .=((r-r0+r0)/(-r+r0))/(-r)
          .=r/(-r+r0)/(-r);
        (-r)/(-r+r0)>0 by A34,A7,XREAL_1:141;
        then -(r/(-r+r0))>0;
        then (r/(-r+r0))<0;
        then
A43:    (r1+r4)<0 by A34,A42,XREAL_1:143;
        reconsider G1=].r1-r4,r1+r4.[ as Subset of R^1 by TOPMETR:24;
A44:    r1<r1+r4 by A41,XREAL_1:31;
        then r1-r4<r1 by XREAL_1:21;
        then
A45:    f1.p in G1 by A44,XXREAL_1:4;
        G1 is open by JORDAN6:46;
        then consider W1 being Subset of X such that
A46:    p in W1 & W1 is open & f1.:W1 c= G1 by A1,A45,Th20;
        set W=W1;
        g0.:W c= ].r-r0,r+r0.[
        proof
          let x be set;
          assume x in g0.:W;
          then consider z being set such that
A47:      z in dom g0 & z in W & g0.z=x by FUNCT_1:def 12;
          reconsider pz=z as Point of X by A47;
          pz in the carrier of X;
          then pz in dom f1 by FUNCT_2:def 1;
          then
A48:      f1.pz in f1.:W1 by A47,FUNCT_1:def 12;
          reconsider aa1=f1.pz as Real by TOPMETR:24;
A49:      x=1/aa1 by A3,A47;
A50:      r1-r4<aa1 & aa1<r1+r4 by A46,A48,XXREAL_1:4;
          then
A51:      1/(-(1/(-r))-r4)>1/aa1 by A38,A43,XREAL_1:101;
A52:      0<r0^2 by A7,SQUARE_1:74;
          r0 *(-r)< r0 *(-r)+(r0 *r0+r0 *r0) by A52,XREAL_1:31;
          then r0 *(-r)-(r0 *r0+r0 *r0)< r0 *(-r) by XREAL_1:21;
          then (r0 *(-r)-(r0 *r0+r0 *r0))+ (-r)*(-r)<r0 *(-r)+(-r)*(-r)
          by XREAL_1:10;
          then ((-r)-r0)*((-r)+r0+r0)/((-r)+r0+r0)<(-r)*((-r)+r0)/((-r)+r0+r0)
          by S,A7,A10,XREAL_1:76;
          then (-r)-r0<(-r)*((-r)+r0)/((-r)+r0+r0)
           by A9,S,A7,XCMPLX_1:90;
          then (-r)-r0<(-r)/(((-r)+r0+r0)/((-r)+r0)) by XCMPLX_1:78;
          then (-r)-r0<(-r)/(((-r)+r0)/((-r)+r0)+r0/((-r)+r0));
          then (-r)-r0<(-r)*1/(1+r0/((-r)+r0)) by A33,A7,A10,XCMPLX_1:60;
          then (-r)-r0<1/((1+r0/((-r)+r0))/(-r)) by XCMPLX_1:78;
          then (-r)-r0<1/(1/(-r)+r0/((-r)+r0)/(-r));
          then -(r+r0)<1/(1/(-r)+r4);
          then (r+r0)>-(1/(1/(-r)+r4)) by XREAL_1:27;
          then (r+r0)>(1/-(1/(-r)+r4)) by XCMPLX_1:189;
          then
A53:      r+r0>1/aa1 by A51,XXREAL_0:2;
A54:      1/aa1>1/(r1+r4) by A43,A50,XREAL_1:89;
          1/(r1+r4) =1/(r1+r0 *(-r)"/(-r+r0))
            .=1/(r1+r0 *(1/(-r))/(-r+r0))
            .=1/(r1+(-(r1*r0))/(-r+r0)) by A38
            .=1/(r1+-((r1*r0)/(-r+r0)))
            .=1/(r1-((r1*r0))/(-r+r0))
            .=1/(r1-r0/((-r+r0)/r1)) by XCMPLX_1:78
            .=1/(r1*1-r1*(r0/(-r+r0))) by XCMPLX_1:82
            .=1/(r1*(1-r0/(-r+r0)))
            .=1/(((-r+r0)/(-r+r0)-(r0/(-r+r0)))*r1)
             by A33,A7,A10,XCMPLX_1:60
            .=1/((-r+r0-r0)/(-(r-r0))*(r1))
            .=1/((-(-r+r0-r0)/(r-r0))*(r1)) by XCMPLX_1:189
            .=1/((-r+r0-r0)/((r-r0))*(-r1))
            .=1/((-r)/((r-r0)/(-r1))) by XCMPLX_1:82
            .=1/((-r)*(-r1)/(r-r0)) by XCMPLX_1:78
            .=(r-r0)/((-r)*(-r1))*1 by XCMPLX_1:81
            .=(r-r0)/((-r)*(-r)") by A38
            .=(r-r0)/1 by A34,XCMPLX_0:def 7
            .=r-r0;
          hence thesis by A49,A53,A54,XXREAL_1:4;
        end;

hence thesis by A7,A46,XBOOLE_1:1;
      end;
    end;
    hence thesis;
  end;
  then g0 is continuous by Th20;
  hence thesis by A4;
end;

theorem Th37:
  for X being non empty TopSpace,
  f1,f2 being Function of X,R^1 st f1 is continuous & f2 is continuous &
  (for q being Point of X holds f2.q<>0) holds ex g being Function of X,R^1
  st (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g.p=r1/r2) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume
A1: f1 is continuous & f2 is continuous
  & (for q being Point of X holds f2.q<>0);
  then consider g1 being Function of X,R^1 such that
A2: (for p being Point of X,r2 being real number st
  f2.p=r2 holds g1.p=1/r2) & g1 is continuous by Th36;
  consider g2 being Function of X,R^1 such that
A3: (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & g1.p=r2 holds g2.p=r1*r2) & g2 is continuous by A1,A2,Th35;
  for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g2.p=r1/r2
  proof
    let p be Point of X,r1,r2 be real number;
    assume
A4: f1.p=r1 & f2.p=r2;
    then g1.p=1/r2 by A2;
    then g2.p=r1*(1/r2) by A3,A4
      .=r1/r2;
    hence thesis;
  end;
  hence thesis by A3;
end;

theorem Th38:
  for X being non empty TopSpace,
  f1,f2 being Function of X,R^1 st f1 is continuous & f2 is continuous &
  (for q being Point of X holds f2.q<>0) holds ex g being Function of X,R^1
  st (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g.p=r1/r2/r2) & g is continuous
proof
  let X be non empty TopSpace, f1,f2 be Function of X,R^1;
  assume
A1: f1 is continuous & f2 is continuous
  & (for q being Point of X holds f2.q<>0);
  then consider g2 being Function of X,R^1 such that
A2: (for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g2.p=r1/r2) & g2 is continuous by Th37;
  consider g3 being Function of X,R^1 such that
A3: (for p being Point of X,r1,r2 being real number st
  g2.p=r1 & f2.p=r2 holds g3.p=r1/r2) & g3 is continuous by A1,A2,Th37;
  for p being Point of X,r1,r2 being real number st
  f1.p=r1 & f2.p=r2 holds g3.p=r1/r2/r2
  proof
    let p be Point of X,r1,r2 be real number;
    assume
A4: f1.p=r1 & f2.p=r2;
    then g2.p=r1/r2 by A2;
    hence thesis by A3,A4;
  end;
  hence thesis by A3;
end;

theorem Th39:
  for K0 being Subset of TOP-REAL 2,
  f being Function of (TOP-REAL 2)|K0,R^1 st
  (for p being Point of (TOP-REAL 2)|K0 holds
  f.p=proj1.p) holds f is continuous
proof
  let K0 be Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K0,R^1;
  assume
A1: (for p being Point of (TOP-REAL 2)|K0 holds f.p=proj1.p);
A2: dom f= the carrier of (TOP-REAL 2)|K0 by FUNCT_2:def 1;
A3: the carrier of (TOP-REAL 2)|K0 =[#]((TOP-REAL 2)|K0)
    .=K0 by PRE_TOPC:def 10;
A4: (the carrier of TOP-REAL 2)/\K0=K0 by XBOOLE_1:28;
A5: for x being set st x in dom f holds f.x=proj1.x by A1;
  reconsider g=proj1 as Function of TOP-REAL 2,R^1 by TOPMETR:24;
A6: f=g|K0 by A2,A3,A4,A5,Th14,FUNCT_1:68;
  g is continuous by TOPREAL6:83;
  hence thesis by A6,TOPMETR:10;
end;

theorem Th40:
  for K0 being Subset of TOP-REAL 2,
  f being Function of (TOP-REAL 2)|K0,R^1 st
  (for p being Point of (TOP-REAL 2)|K0 holds
  f.p=proj2.p) holds f is continuous
proof
  let K0 be Subset of TOP-REAL 2, f be Function of (TOP-REAL 2)|K0,R^1;
  assume
A1: (for p being Point of (TOP-REAL 2)|K0 holds f.p=proj2.p);
A2: dom f= the carrier of (TOP-REAL 2)|K0 by FUNCT_2:def 1;
A3: the carrier of (TOP-REAL 2)|K0 =[#]((TOP-REAL 2)|K0)
    .=K0 by PRE_TOPC:def 10;
A4: (the carrier of TOP-REAL 2) /\ K0=K0 by XBOOLE_1:28;
  for x being set st x in dom f holds f.x=proj2.x by A1;
  then f=proj2|K0 by A2,A3,A4,Th15,FUNCT_1:68;
  hence thesis by TOPREAL6:83;
end;

theorem Th41:
  for K1 being non empty Subset of TOP-REAL 2,
  f being Function of (TOP-REAL 2)|K1,R^1 st
  (for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2)|K1
  holds f.p=1/p`1) & (for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`1<>0 ) holds f is continuous
proof
  let K1 be non empty Subset of TOP-REAL 2,
  f be Function of (TOP-REAL 2)|K1,R^1;
  assume
A1: (for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2)|K1
  holds f.p=1/p`1) & (for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`1<>0 );
A2: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
    .=K1 by PRE_TOPC:def 10;
  reconsider g1=proj1|K1 as Function of (TOP-REAL 2)|K1,R^1 by TOPMETR:24;
A3: for q being Point of (TOP-REAL 2)|K1 holds g1.q=proj1.q
  proof
    let q be Point of (TOP-REAL 2)|K1;
A4: q in the carrier of (TOP-REAL 2)|K1;
    dom proj1=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    then q in dom proj1 /\ K1 by A2,A4,XBOOLE_0:def 4;
    hence thesis by FUNCT_1:71;
  end;
  then
A5: g1 is continuous by Th39;
  for q being Point of (TOP-REAL 2)|K1 holds g1.q<>0
  proof
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A2;
    g1.q=proj1.q by A3
      .=q2`1 by PSCOMP_1:def 28;
    hence thesis by A1;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A6: (for q being Point of (TOP-REAL 2)|K1,r2 being real number st g1.q=r2
  holds g3.q=1/r2) & g3 is continuous by A5,Th36;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then
A7: dom f=dom g3 by FUNCT_2:def 1;
  for x being set st x in dom f holds f.x=g3.x
  proof
    let x be set;
    assume
A8: x in dom f;
    then x in [#]((TOP-REAL 2)|K1);
    then x in K1 by PRE_TOPC:def 10;
    then reconsider r=x as Point of (TOP-REAL 2);
    reconsider s=x as Point of (TOP-REAL 2)|K1 by A8;
A9: f.r=1/r`1 by A1,A8;
A10: g1.s=proj1.s by A3;
    proj1.r=r`1 by PSCOMP_1:def 28;
    hence thesis by A6,A9,A10;
  end;
  hence thesis by A6,A7,FUNCT_1:9;
end;

theorem Th42:
  for K1 being non empty Subset of TOP-REAL 2,
  f being Function of (TOP-REAL 2)|K1,R^1 st
  (for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2)|K1
  holds f.p=1/p`2) & (for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`2<>0 ) holds f is continuous
proof
  let K1 be non empty Subset of TOP-REAL 2,
  f be Function of (TOP-REAL 2)|K1,R^1;
  assume
A1: (for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2)|K1
  holds f.p=1/p`2) & (for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`2<>0 );
A2: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
    .=K1 by PRE_TOPC:def 10;
  reconsider g1=proj2|K1 as Function of (TOP-REAL 2)|K1,R^1 by TOPMETR:24;
A3: for q being Point of (TOP-REAL 2)|K1 holds g1.q=proj2.q
  proof
    let q be Point of (TOP-REAL 2)|K1;
A4: q in the carrier of (TOP-REAL 2)|K1;
    dom proj2=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    then q in dom proj2 /\ K1 by A2,A4,XBOOLE_0:def 4;
    hence thesis by FUNCT_1:71;
  end;
  then
A5: g1 is continuous by Th40;
  for q being Point of (TOP-REAL 2)|K1 holds g1.q<>0
  proof
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A2;
    g1.q=proj2.q by A3
      .=q2`2 by PSCOMP_1:def 29;
    hence thesis by A1;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A6: (for q being Point of (TOP-REAL 2)|K1,r2 being real number st g1.q=r2
  holds g3.q=1/r2) & g3 is continuous by A5,Th36;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then
A7: dom f=dom g3 by FUNCT_2:def 1;
  for x being set st x in dom f holds f.x=g3.x
  proof
    let x be set;
    assume
A8: x in dom f;
    then x in [#]((TOP-REAL 2)|K1);
    then x in K1 by PRE_TOPC:def 10;
    then reconsider r=x as Point of (TOP-REAL 2);
    reconsider s=x as Point of (TOP-REAL 2)|K1 by A8;
A9: f.r=1/r`2 by A1,A8;
A10: g1.s=proj2.s by A3;
    proj2.r=r`2 by PSCOMP_1:def 29;
    hence thesis by A6,A9,A10;
  end;
  hence thesis by A6,A7,FUNCT_1:9;
end;

theorem Th43:
  for K1 being non empty Subset of TOP-REAL 2,
  f being Function of (TOP-REAL 2)|K1,R^1 st
  (for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2)|K1
  holds f.p=p`2/p`1/p`1) & (for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`1<>0 ) holds f is continuous
proof
  let K1 be non empty Subset of TOP-REAL 2,
  f be Function of (TOP-REAL 2)|K1,R^1;
  assume
A1: (for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2)|K1
  holds f.p=p`2/p`1/p`1) & (for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`1<>0 );
A2: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
    .=K1 by PRE_TOPC:def 10;
  reconsider g2=proj2|K1 as Function of (TOP-REAL 2)|K1,R^1 by TOPMETR:24;
A3: for q being Point of (TOP-REAL 2)|K1 holds g2.q=proj2.q
  proof
    let q be Point of (TOP-REAL 2)|K1;
A4: q in the carrier of (TOP-REAL 2)|K1;
    dom proj2=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    then q in dom proj2 /\ K1 by A2,A4,XBOOLE_0:def 4;
    hence thesis by FUNCT_1:71;
  end;
  then
A5: g2 is continuous by Th40;
  reconsider g1=proj1|K1 as Function of (TOP-REAL 2)|K1,R^1 by TOPMETR:24;
A6: for q being Point of (TOP-REAL 2)|K1 holds g1.q=proj1.q
  proof
    let q be Point of (TOP-REAL 2)|K1;
A7: q in the carrier of (TOP-REAL 2)|K1;
    dom proj1=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    then q in dom proj1 /\ K1 by A2,A7,XBOOLE_0:def 4;
    hence thesis by FUNCT_1:71;
  end;
  then
A8: g1 is continuous by Th39;
  for q being Point of (TOP-REAL 2)|K1 holds g1.q<>0
  proof
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A2;
    g1.q=proj1.q by A6
      .=q2`1 by PSCOMP_1:def 28;
    hence thesis by A1;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A9: (for q being Point of
  (TOP-REAL 2)|K1,r1,r2 being real number st g2.q=r1 & g1.q=r2
  holds g3.q=r1/r2/r2) & g3 is continuous by A5,A8,Th38;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then
A10: dom f=dom g3 by FUNCT_2:def 1;
  for x being set st x in dom f holds f.x=g3.x
  proof
    let x be set;
    assume
A11: x in dom f;
    then x in [#]((TOP-REAL 2)|K1);
    then x in K1 by PRE_TOPC:def 10;
    then reconsider r=x as Point of (TOP-REAL 2);
    reconsider s=x as Point of (TOP-REAL 2)|K1 by A11;
A12: f.r=r`2/r`1/r`1 by A1,A11;
A13: g2.s=proj2.s by A3;
A14: g1.s=proj1.s by A6;
A15: proj2.r=r`2 by PSCOMP_1:def 29;
    proj1.r=r`1 by PSCOMP_1:def 28;
    hence thesis by A9,A12,A13,A14,A15;
  end;
  hence thesis by A9,A10,FUNCT_1:9;
end;

theorem Th44:
  for K1 being non empty Subset of TOP-REAL 2,
  f being Function of (TOP-REAL 2)|K1,R^1 st
  (for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2)|K1
  holds f.p=p`1/p`2/p`2) & (for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`2<>0 ) holds f is continuous
proof
  let K1 be non empty Subset of TOP-REAL 2,
  f be Function of (TOP-REAL 2)|K1,R^1;
  assume
A1: (for p being Point of (TOP-REAL 2) st p in the carrier of (TOP-REAL 2)|K1
  holds f.p=p`1/p`2/p`2) & (for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`2<>0 );
A2: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
    .=K1 by PRE_TOPC:def 10;
  reconsider g2=proj1|K1 as Function of (TOP-REAL 2)|K1,R^1 by TOPMETR:24;
A3: for q being Point of (TOP-REAL 2)|K1 holds g2.q=proj1.q
  proof
    let q be Point of (TOP-REAL 2)|K1;
A4: q in the carrier of (TOP-REAL 2)|K1;
    dom proj1=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    then q in dom proj1 /\ K1 by A2,A4,XBOOLE_0:def 4;
    hence thesis by FUNCT_1:71;
  end;
  then
A5: g2 is continuous by Th39;
  reconsider g1=proj2|K1 as Function of (TOP-REAL 2)|K1,R^1 by TOPMETR:24;
A6: for q being Point of (TOP-REAL 2)|K1 holds g1.q=proj2.q
  proof
    let q be Point of (TOP-REAL 2)|K1;
A7: q in the carrier of (TOP-REAL 2)|K1;
    dom proj2=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
    then q in dom proj2 /\ K1 by A2,A7,XBOOLE_0:def 4;
    hence thesis by FUNCT_1:71;
  end;
  then
A8: g1 is continuous by Th40;
  for q being Point of (TOP-REAL 2)|K1 holds g1.q<>0
  proof
    let q be Point of (TOP-REAL 2)|K1;
    q in the carrier of (TOP-REAL 2)|K1;
    then reconsider q2=q as Point of TOP-REAL 2 by A2;
    g1.q=proj2.q by A6
      .=q2`2 by PSCOMP_1:def 29;
    hence thesis by A1;
  end;
  then consider g3 being Function of (TOP-REAL 2)|K1,R^1 such that
A9: (for q being Point of
  (TOP-REAL 2)|K1,r1,r2 being real number st g2.q=r1 & g1.q=r2
  holds g3.q=r1/r2/r2) & g3 is continuous by A5,A8,Th38;
  dom g3=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1;
  then
A10: dom f=dom g3 by FUNCT_2:def 1;
  for x being set st x in dom f holds f.x=g3.x
  proof
    let x be set;
    assume
A11: x in dom f;
   then x in [#]((TOP-REAL 2)|K1);
    then x in K1 by PRE_TOPC:def 10;
    then reconsider r=x as Point of (TOP-REAL 2);
    reconsider s=x as Point of (TOP-REAL 2)|K1 by A11;
A12: f.r=r`1/r`2/r`2 by A1,A11;
A13: g2.s=proj1.s by A3;
A14: g1.s=proj2.s by A6;
A15: proj1.r=r`1 by PSCOMP_1:def 28;
    proj2.r=r`2 by PSCOMP_1:def 29;
    hence thesis by A9,A12,A13,A14,A15;
  end;
  hence thesis by A9,A10,FUNCT_1:9;
end;

theorem Th45:
  for K0,B0 being Subset of TOP-REAL 2, f being Function of
  (TOP-REAL 2)|K0,(TOP-REAL 2)|B0,
  f1,f2 being Function of (TOP-REAL 2)|K0,R^1 st f1 is continuous &
  f2 is continuous & K0<>{} & B0<>{} &
  (for x,y,r,s being real number st |[x,y]| in K0 &
  r=f1.(|[x,y]|) & s=f2.(|[x,y]|) holds f.(|[x,y]|)=|[r,s]|) holds
  f is continuous
proof
  let K0,B0 be Subset of TOP-REAL 2, f be Function of
  (TOP-REAL 2)|K0,(TOP-REAL 2)|B0, f1,f2 be Function of (TOP-REAL 2)|K0,R^1;
  assume
A1: f1 is continuous & f2 is continuous & K0<>{} & B0<>{} &
  (for x,y,r,s being real number st |[x,y]| in K0 &
  r=f1.(|[x,y]|) & s=f2.(|[x,y]|) holds f. |[x,y]|=|[r,s]|);
  then reconsider K1=K0 as non empty Subset of TOP-REAL 2;
  reconsider B1=B0 as non empty Subset of TOP-REAL 2 by A1;
  reconsider X=(TOP-REAL 2)|K1,Y=(TOP-REAL 2)|B1 as non empty TopSpace;
  reconsider f0=f as Function of X,Y;
  for r being Point of X,V being Subset of Y st f0.r in V & V is open holds
  ex W being Subset of X st r in W & W is open & f0.:W c= V
  proof
    let r be Point of X,V be Subset of Y;
    assume
A2: f0.r in V & V is open;
    then consider V2 being Subset of TOP-REAL 2 such that
A3: V2 is open & V=V2 /\ [#]Y by TOPS_2:32;
A4: V2 /\ [#]Y c= V2 by XBOOLE_1:17;
    then f0.r in V2 by A2,A3;
    then reconsider p=f0.r as Point of TOP-REAL 2;
    consider r2 being real number such that
A5: r2>0 & {q where q is Point of TOP-REAL 2:
    p`1-r2<q`1 & q`1<p`1+r2 & p`2-r2<q`2 & q`2<p`2+r2} c= V2 by A2,A3,A4,Th21;
A6: r in the carrier of X;
    then
A7: r in dom f1 by FUNCT_2:def 1;
A8: r in dom f2 by A6,FUNCT_2:def 1;
A9: f1.r in rng f1 by A7,FUNCT_1:12;
    f2.r in rng f2 by A8,FUNCT_1:12;
    then reconsider r3=f1.r,r4=f2.r as Real by A9,TOPMETR:24;
A10: the carrier of X=[#]X
      .=K0 by PRE_TOPC:def 10;
    then r in K0;
    then reconsider pr=r as Point of TOP-REAL 2;
A11: r= |[pr`1,pr`2]| by EUCLID:57;
    then
A12: f0. |[pr`1,pr`2]|=|[r3,r4]| by A1,A10;
    p`1 <p`1+r2 by A5,XREAL_1:31;
    then p`1-r2< p`1 & p`1<p`1+r2 by XREAL_1:21;
    then
A13: p`1 in ].p`1-r2,p`1+r2.[ by XXREAL_1:4;
    then
A14: f1.r in ].p`1-r2,p`1+r2.[ by A11,A12,EUCLID:56;
    p`2 <p`2+r2 by A5,XREAL_1:31;
    then p`2-r2< p`2 & p`2<p`2+r2 by XREAL_1:21;
    then
A15: p`2 in ].p`2-r2,p`2+r2.[ by XXREAL_1:4;
    reconsider G1= ].p`1-r2,p`1+r2.[,G2= ].p`2-r2,p`2+r2.[ as
    Subset of R^1 by TOPMETR:24;
A16: G1 is open & G2 is open by JORDAN6:46;
A17: f1.r in G1 & f2.r in G2 by A11,A12,A13,A15,EUCLID:56;
    consider W1 being Subset of X such that
A18: r in W1 & W1 is open & f1.:W1 c= G1 by A1,A14,A16,Th20;
    consider W2 being Subset of X such that
A19: r in W2 & W2 is open & f2.:W2 c= G2 by A1,A16,A17,Th20;
    reconsider W5=W1 /\ W2 as Subset of X;
A20: r in W5 by A18,A19,XBOOLE_0:def 4;
    f1.:W5 c= f1.:W1 by RELAT_1:156,XBOOLE_1:17;
    then
A21: f1.:W5 c= G1 by A18,XBOOLE_1:1;
    f2.:W5 c= f2.:W2 by RELAT_1:156,XBOOLE_1:17;
    then
A22: f2.:W5 c= G2 by A19,XBOOLE_1:1;
    f0.:W5 c= V
    proof
      let v be set;
      assume
A23:  v in f0.:W5;
      then reconsider q2=v as Point of Y;
      consider k being set such that
A24:  k in dom f0 & k in W5 & q2=f0.k by A23,FUNCT_1:def 12;
A25:  q2 in [#]Y;
      the carrier of X=[#]X
        .=K0 by PRE_TOPC:def 10;
      then k in K0 by A24;
      then reconsider r8=k as Point of TOP-REAL 2;
A26:  dom f0=the carrier of (TOP-REAL 2)|K1 by FUNCT_2:def 1
        .=[#]((TOP-REAL 2)|K1)
        .=K0 by PRE_TOPC:def 10;
A27:  k= |[r8`1,r8`2]| by EUCLID:57;
A28:  |[r8`1,r8`2]| in K0 by A24,A26,EUCLID:57;
A29:  dom f1=the carrier of (TOP-REAL 2)|K0 by FUNCT_2:def 1
        .=[#]((TOP-REAL 2)|K0)
        .=K0 by PRE_TOPC:def 10;
A30:  dom f2=the carrier of (TOP-REAL 2)|K0 by FUNCT_2:def 1
        .=[#]((TOP-REAL 2)|K0)
        .=K0 by PRE_TOPC:def 10;
A31:  f1.(|[r8`1,r8`2]|) in rng f1 by A28,A29,FUNCT_1:def 5;
      f2.(|[r8`1,r8`2]|) in rng f2 by A28,A30,FUNCT_1:def 5;
      then reconsider r7=f1.(|[r8`1,r8`2]|), s7=f2.(|[r8`1,r8`2]|) as Real
      by A31,TOPMETR:24;
A32:  v=|[r7,s7]| by A1,A24,A26,A27;
A33:  (|[r7,s7]|)`1 =r7 by EUCLID:56;
A34:  (|[r7,s7]|)`2 =s7 by EUCLID:56;
A35:  |[r8`1,r8`2]| in W5 by A24,EUCLID:57;
      then
A36:  f1.(|[r8`1,r8`2]|) in f1.:W5 by A28,A29,FUNCT_1:def 12;
      f2.(|[r8`1,r8`2]|) in f2.:W5 by A28,A30,A35,FUNCT_1:def 12;
      then p`1-r2< r7 & r7<p`1+r2 &
      p`2-r2< s7 & s7<p`2+r2 by A21,A22,A36,XXREAL_1:4;
      then v in {q3 where q3 is Point of TOP-REAL 2:
      p`1-r2<q3`1 & q3`1<p`1+r2 & p`2-r2<q3`2 & q3`2<p`2+r2} by A32,A33,A34;
      hence thesis by A3,A5,A25,XBOOLE_0:def 4;
    end;

hence thesis by A18,A19,A20,TOPS_1:38;
  end;
  hence thesis by Th20;
end;

theorem Th46:
  for K0,B0 being Subset of TOP-REAL 2,f being
  Function of (TOP-REAL 2)|K0,(TOP-REAL 2)|B0
  st f=Out_In_Sq|K0 & B0=NonZero TOP-REAL 2 &
  K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2}
  holds f is continuous
proof
  let K0,B0 be Subset of TOP-REAL 2,f be
  Function of (TOP-REAL 2)|K0,(TOP-REAL 2)|B0;
  assume
A1: f=Out_In_Sq|K0 & B0=NonZero TOP-REAL 2 &
  K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2};
  ((1.REAL 2)`2<=(1.REAL 2)`1 & -(1.REAL 2)`1<=(1.REAL 2)`2 or
  (1.REAL 2)`2>=(1.REAL 2)`1 & (1.REAL 2)`2<=-(1.REAL 2)`1) &
  (1.REAL 2)<>0.TOP-REAL 2 by Th13,REVROT_1:19,XX;
  then
A2: 1.REAL 2 in K0 by A1;
  then reconsider K1=K0 as non empty Subset of TOP-REAL 2;
A3: K0 c= B0
  proof
    let x be set;
    assume x in K0;
    then consider p8 being Point of TOP-REAL 2 such that
A4: x=p8 & (
    (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1) & p8<>0.TOP-REAL 2)
    by A1;
    not x in {0.TOP-REAL 2} by A4,TARSKI:def 1;
    hence thesis by A1,A4,XBOOLE_0:def 5;
  end;
A5: dom ((proj2)*(Out_In_Sq|K1)) c= dom (Out_In_Sq|K1) by RELAT_1:44;
A6: dom (Out_In_Sq|K1) c= dom ((proj2)*(Out_In_Sq|K1))
  proof
    let x be set;
    assume
A7: x in dom (Out_In_Sq|K1);
    then
A8: x in dom Out_In_Sq /\ K1 by RELAT_1:90;
A9: (Out_In_Sq|K1).x=Out_In_Sq.x by A7,FUNCT_1:70;
A10: dom proj2 = (the carrier of TOP-REAL 2) by FUNCT_2:def 1;
    x in dom Out_In_Sq by A8,XBOOLE_0:def 4;
    then Out_In_Sq.x in rng Out_In_Sq by FUNCT_1:12;
    then Out_In_Sq.x in the carrier of TOP-REAL 2 by XBOOLE_0:def 5;
    hence thesis by A7,A9,A10,FUNCT_1:21;
  end;
A11: K1 c= NonZero TOP-REAL 2
  proof
    let z be set;
    assume z in K1;
    then consider p8 being Point of TOP-REAL 2 such that
A12: p8=z &( (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1)
    & p8<>0.TOP-REAL 2) by A1;
    z in (the carrier of TOP-REAL 2) & not z in {0.TOP-REAL 2}
    by A12,TARSKI:def 1;
    hence thesis by XBOOLE_0:def 5;
  end;
A13: NonZero TOP-REAL 2<>{} by Th19;
A14: dom ((proj2)*(Out_In_Sq|K1)) =dom (Out_In_Sq|K1) by A5,A6,XBOOLE_0:def 10
    .=dom Out_In_Sq /\ K1 by RELAT_1:90
    .=(NonZero TOP-REAL 2)/\ K1 by A13,FUNCT_2:def 1
    .=K1 by A11,XBOOLE_1:28
    .=[#]((TOP-REAL 2)|K1) by PRE_TOPC:def 10
    .=the carrier of (TOP-REAL 2)|K1;
  rng ((proj2)*(Out_In_Sq|K1)) c= the carrier of R^1 by TOPMETR:24;
  then reconsider g2=(proj2)*(Out_In_Sq|K1) as Function
  of (TOP-REAL 2)|K1,R^1 by A14,FUNCT_2:4;
A15: dom ((proj1)*(Out_In_Sq|K1)) c= dom (Out_In_Sq|K1) by RELAT_1:44;
  dom (Out_In_Sq|K1) c= dom ((proj1)*(Out_In_Sq|K1))
  proof
    let x be set;
    assume
A16: x in dom (Out_In_Sq|K1);
    then
A17: x in dom Out_In_Sq /\ K1 by RELAT_1:90;
A18: (Out_In_Sq|K1).x=Out_In_Sq.x by A16,FUNCT_1:70;
A19: dom proj1 = (the carrier of TOP-REAL 2) by FUNCT_2:def 1;
    x in dom Out_In_Sq by A17,XBOOLE_0:def 4;
    then Out_In_Sq.x in rng Out_In_Sq by FUNCT_1:12;
    then Out_In_Sq.x in the carrier of TOP-REAL 2 by XBOOLE_0:def 5;
    hence thesis by A16,A18,A19,FUNCT_1:21;
  end;
  then
A20: dom ((proj1)*(Out_In_Sq|K1)) =dom (Out_In_Sq|K1) by A15,XBOOLE_0:def 10
    .=dom Out_In_Sq /\ K1 by RELAT_1:90
    .=(NonZero TOP-REAL 2)/\ K1 by A13,FUNCT_2:def 1
    .=K1 by A11,XBOOLE_1:28
    .=[#]((TOP-REAL 2)|K1) by PRE_TOPC:def 10
    .=the carrier of (TOP-REAL 2)|K1;
  rng ((proj1)*(Out_In_Sq|K1)) c= the carrier of R^1 by TOPMETR:24;
  then reconsider g1=(proj1)*(Out_In_Sq|K1) as Function
  of (TOP-REAL 2)|K1,R^1 by A20,FUNCT_2:4;
A21: for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`1<>0
  proof
    let q be Point of TOP-REAL 2;
    assume
A22: q in the carrier of (TOP-REAL 2)|K1;
    the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
      .=K1 by PRE_TOPC:def 10;
    then consider p3 being Point of TOP-REAL 2 such that
A23: q=p3 & ((p3`2<=p3`1 & -p3`1<=p3`2 or p3`2>=p3`1 & p3`2<=-p3`1)
    & p3<>0.TOP-REAL 2) by A1,A22;
    now
      assume
A24:  q`1=0;
      then q`2=0 by A23;
      hence contradiction by A23,A24,EUCLID:57,58;
    end;
    hence thesis;
  end;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g2.p=p`2/p`1/p`1
  proof
    let p be Point of TOP-REAL 2;
    assume
A25: p in the carrier of (TOP-REAL 2)|K1;
A26: NonZero TOP-REAL 2<>{} by Th19;
A27: dom (Out_In_Sq|K1)=dom Out_In_Sq /\ K1 by RELAT_1:90
      .=(NonZero TOP-REAL 2)/\ K1 by A26,FUNCT_2:def 1
      .=K1 by A11,XBOOLE_1:28;
A28: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
      .=K1 by PRE_TOPC:def 10;
    then consider p3 being Point of TOP-REAL 2 such that
A29: p=p3 & ((p3`2<=p3`1 & -p3`1<=p3`2 or p3`2>=p3`1 & p3`2<=-p3`1)
    & p3<>0.TOP-REAL 2) by A1,A25;
A30: Out_In_Sq.p=|[1/p`1,p`2/p`1/p`1]| by A29,Def1;
    (Out_In_Sq|K1).p=Out_In_Sq.p by A25,A28,FUNCT_1:72;
    then g2.p=(proj2).(|[1/p`1,p`2/p`1/p`1]|) by A25,A27,A28,A30,FUNCT_1:23
      .=(|[1/p`1,p`2/p`1/p`1]|)`2 by PSCOMP_1:def 29
      .=p`2/p`1/p`1 by EUCLID:56;
    hence thesis;
  end;
  then consider f2 being Function of (TOP-REAL 2)|K1,R^1 such that
A31: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds f2.p=p`2/p`1/p`1;
A32: f2 is continuous by A21,A31,Th43;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g1.p=1/p`1
  proof
    let p be Point of TOP-REAL 2;
    assume
A33: p in the carrier of (TOP-REAL 2)|K1;
A34: K1 c= NonZero TOP-REAL 2
    proof
      let z be set;
      assume z in K1;
      then consider p8 being Point of TOP-REAL 2 such that
A35:  p8=z &( (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1)
      & p8<>0.TOP-REAL 2) by A1;
      z in (the carrier of TOP-REAL 2) & not z in {0.TOP-REAL 2}
      by A35,TARSKI:def 1;
      hence thesis by XBOOLE_0:def 5;
    end;
A36: NonZero TOP-REAL 2<>{} by Th19;
A37: dom (Out_In_Sq|K1)=dom Out_In_Sq /\ K1 by RELAT_1:90
      .=(NonZero TOP-REAL 2)/\ K1 by A36,FUNCT_2:def 1
      .=K1 by A34,XBOOLE_1:28;
A38: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
      .=K1 by PRE_TOPC:def 10;
    then consider p3 being Point of TOP-REAL 2 such that
A39: p=p3 & ((p3`2<=p3`1 & -p3`1<=p3`2 or p3`2>=p3`1 & p3`2<=-p3`1)
    & p3<>0.TOP-REAL 2) by A1,A33;
A40: Out_In_Sq.p=|[1/p`1,p`2/p`1/p`1]| by A39,Def1;
    (Out_In_Sq|K1).p=Out_In_Sq.p by A33,A38,FUNCT_1:72;
    then g1.p=(proj1).(|[1/p`1,p`2/p`1/p`1]|) by A33,A37,A38,A40,FUNCT_1:23
      .=(|[1/p`1,p`2/p`1/p`1]|)`1 by PSCOMP_1:def 28
      .=1/p`1 by EUCLID:56;
    hence thesis;
  end;
  then consider f1 being Function of (TOP-REAL 2)|K1,R^1 such that
A41: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds f1.p=1/p`1;
A42: f1 is continuous by A21,A41,Th41;
  for x,y,r,s being real number st |[x,y]| in K1 &
  r=f1.(|[x,y]|) & s=f2.(|[x,y]|) holds f. |[x,y]|=|[r,s]|
  proof
    let x,y,r,s be real number;
    assume
A43: |[x,y]| in K1 & r=f1.(|[x,y]|) & s=f2.(|[x,y]|);
    set p99=|[x,y]|;
A44: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
      .=K1 by PRE_TOPC:def 10;
    consider p3 being Point of TOP-REAL 2 such that
A45: p99=p3 & ((p3`2<=p3`1 & -p3`1<=p3`2 or p3`2>=p3`1 & p3`2<=-p3`1)
    & p3<>0.TOP-REAL 2) by A1,A43;
A46: ((p99`2<=p99`1 & -p99`1<=p99`2 or p99`2>=p99`1 & p99`2<=-p99`1) implies
    Out_In_Sq.p99=|[1/p99`1,p99`2/p99`1/p99`1]|) &
    (not(p99`2<=p99`1 & -p99`1<=p99`2 or p99`2>=p99`1 & p99`2<=-p99`1) implies
    Out_In_Sq.p99=|[p99`1/p99`2/p99`2,1/p99`2]|) by A45,Def1;
A47: f1.p99=1/p99`1 by A41,A43,A44;
    (Out_In_Sq|K0). |[x,y]|= |[1/p99`1,p99`2/p99`1/p99`1]| by A43,A45,A46,
FUNCT_1:72
      .=|[r,s]| by A31,A43,A44,A47;
    hence thesis by A1;
  end;
  hence thesis by A2,A3,A32,A42,Th45;
end;

theorem Th47:
  for K0,B0 being Subset of TOP-REAL 2,f being
  Function of (TOP-REAL 2)|K0,(TOP-REAL 2)|B0
  st f=Out_In_Sq|K0 & B0=NonZero TOP-REAL 2 &
  K0={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2}
  holds f is continuous
proof
  let K0,B0 be Subset of TOP-REAL 2,f be
  Function of (TOP-REAL 2)|K0,(TOP-REAL 2)|B0;
  assume
A1: f=Out_In_Sq|K0 & B0=NonZero TOP-REAL 2 &
  K0={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2};
  ((1.REAL 2)`1<=(1.REAL 2)`2 & -(1.REAL 2)`2<=(1.REAL 2)`1 or
  (1.REAL 2)`1>=(1.REAL 2)`2 & (1.REAL 2)`1<=-(1.REAL 2)`2) &
  (1.REAL 2)<>0.TOP-REAL 2 by Th13,REVROT_1:19,XX;
  then
A2: 1.REAL 2 in K0 by A1;
  then reconsider K1=K0 as non empty Subset of TOP-REAL 2;
A3: K0 c= B0
  proof
    let x be set;
    assume x in K0;
    then consider p8 being Point of TOP-REAL 2 such that
A4: x=p8 & (
    (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2) & p8<>0.TOP-REAL 2)
    by A1;
    not x in {0.TOP-REAL 2} by A4,TARSKI:def 1;
    hence thesis by A1,A4,XBOOLE_0:def 5;
  end;
A5: dom ((proj1)*(Out_In_Sq|K1)) c= dom (Out_In_Sq|K1) by RELAT_1:44;
A6: dom (Out_In_Sq|K1) c= dom ((proj1)*(Out_In_Sq|K1))
  proof
    let x be set;
    assume
A7: x in dom (Out_In_Sq|K1);
    then
A8: x in dom Out_In_Sq /\ K1 by RELAT_1:90;
A9: (Out_In_Sq|K1).x=Out_In_Sq.x by A7,FUNCT_1:70;
A10: dom proj1 = (the carrier of TOP-REAL 2) by FUNCT_2:def 1;
    x in dom Out_In_Sq by A8,XBOOLE_0:def 4;
    then Out_In_Sq.x in rng Out_In_Sq by FUNCT_1:12;
    then Out_In_Sq.x in the carrier of TOP-REAL 2 by XBOOLE_0:def 5;
    hence thesis by A7,A9,A10,FUNCT_1:21;
  end;
A11: K1 c= NonZero TOP-REAL 2
  proof
    let z be set;
    assume z in K1;
    then consider p8 being Point of TOP-REAL 2 such that
A12: p8=z &( (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2)
    & p8<>0.TOP-REAL 2) by A1;
    z in (the carrier of TOP-REAL 2) & not z in {0.TOP-REAL 2}
    by A12,TARSKI:def 1;
    hence thesis by XBOOLE_0:def 5;
  end;
A13: NonZero TOP-REAL 2<>{} by Th19;
A14: dom ((proj1)*(Out_In_Sq|K1)) =dom (Out_In_Sq|K1) by A5,A6,XBOOLE_0:def 10
    .=dom Out_In_Sq /\ K1 by RELAT_1:90
    .=(NonZero TOP-REAL 2)/\ K1 by A13,FUNCT_2:def 1
    .=K1 by A11,XBOOLE_1:28
    .=[#]((TOP-REAL 2)|K1) by PRE_TOPC:def 10
    .=the carrier of (TOP-REAL 2)|K1;
  rng ((proj1)*(Out_In_Sq|K1)) c= the carrier of R^1 by TOPMETR:24;
  then reconsider g2=(proj1)*(Out_In_Sq|K1) as Function
  of (TOP-REAL 2)|K1,R^1 by A14,FUNCT_2:4;
A15: dom ((proj2)*(Out_In_Sq|K1)) c= dom (Out_In_Sq|K1) by RELAT_1:44;
  dom (Out_In_Sq|K1) c= dom ((proj2)*(Out_In_Sq|K1))
  proof
    let x be set;
    assume
A16: x in dom (Out_In_Sq|K1);
    then
A17: x in dom Out_In_Sq /\ K1 by RELAT_1:90;
A18: (Out_In_Sq|K1).x=Out_In_Sq.x by A16,FUNCT_1:70;
A19: dom proj2 = (the carrier of TOP-REAL 2) by FUNCT_2:def 1;
    x in dom Out_In_Sq by A17,XBOOLE_0:def 4;
    then Out_In_Sq.x in rng Out_In_Sq by FUNCT_1:12;
    then Out_In_Sq.x in the carrier of TOP-REAL 2 by XBOOLE_0:def 5;
    hence thesis by A16,A18,A19,FUNCT_1:21;
  end;
  then
A20: dom ((proj2)*(Out_In_Sq|K1)) =dom (Out_In_Sq|K1) by A15,XBOOLE_0:def 10
    .=dom Out_In_Sq /\ K1 by RELAT_1:90
    .=(NonZero TOP-REAL 2)/\ K1 by A13,FUNCT_2:def 1
    .=K1 by A11,XBOOLE_1:28
    .=[#]((TOP-REAL 2)|K1) by PRE_TOPC:def 10
    .=the carrier of (TOP-REAL 2)|K1;
  rng ((proj2)*(Out_In_Sq|K1)) c= the carrier of R^1 by TOPMETR:24;
  then reconsider g1=(proj2)*(Out_In_Sq|K1) as Function
  of (TOP-REAL 2)|K1,R^1 by A20,FUNCT_2:4;
A21: for q being Point of TOP-REAL 2 st q in
  the carrier of (TOP-REAL 2)|K1 holds q`2<>0
  proof
    let q be Point of TOP-REAL 2;
    assume
A22: q in the carrier of (TOP-REAL 2)|K1;
    the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
      .=K1 by PRE_TOPC:def 10;
    then consider p3 being Point of TOP-REAL 2 such that
A23: q=p3 & ((p3`1<=p3`2 & -p3`2<=p3`1 or p3`1>=p3`2 & p3`1<=-p3`2)
    & p3<>0.TOP-REAL 2) by A1,A22;
    now
      assume
A24:  q`2=0;
      then q`1=0 by A23;
      hence contradiction by A23,A24,EUCLID:57,58;
    end;
    hence thesis;
  end;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g2.p=p`1/p`2/p`2
  proof
    let p be Point of TOP-REAL 2;
    assume
A25: p in the carrier of (TOP-REAL 2)|K1;
A26: NonZero TOP-REAL 2<>{} by Th19;
A27: dom (Out_In_Sq|K1)=dom Out_In_Sq /\ K1 by RELAT_1:90
      .=(NonZero TOP-REAL 2)/\ K1 by A26,FUNCT_2:def 1
      .=K1 by A11,XBOOLE_1:28;
A28: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
      .=K1 by PRE_TOPC:def 10;
    then consider p3 being Point of TOP-REAL 2 such that
A29: p=p3 & ((p3`1<=p3`2 & -p3`2<=p3`1 or p3`1>=p3`2 & p3`1<=-p3`2)
    & p3<>0.TOP-REAL 2) by A1,A25;
A30: Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A29,Th24;
    (Out_In_Sq|K1).p=Out_In_Sq.p by A25,A28,FUNCT_1:72;
    then g2.p=(proj1).(|[p`1/p`2/p`2,1/p`2]|) by A25,A27,A28,A30,FUNCT_1:23
      .=(|[p`1/p`2/p`2,1/p`2]|)`1 by PSCOMP_1:def 28
      .=p`1/p`2/p`2 by EUCLID:56;
    hence thesis;
  end;
  then consider f2 being Function of (TOP-REAL 2)|K1,R^1 such that
A31: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds f2.p=p`1/p`2/p`2;
A32: f2 is continuous by A21,A31,Th44;
  for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds g1.p=1/p`2
  proof
    let p be Point of TOP-REAL 2;
    assume
A33: p in the carrier of (TOP-REAL 2)|K1;
A34: K1 c= NonZero TOP-REAL 2
    proof
      let z be set;
      assume z in K1;
      then consider p8 being Point of TOP-REAL 2 such that
A35:  p8=z &( (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2)
      & p8<>0.TOP-REAL 2) by A1;
      z in (the carrier of TOP-REAL 2) & not z in {0.TOP-REAL 2}
      by A35,TARSKI:def 1;
      hence thesis by XBOOLE_0:def 5;
    end;
A36: NonZero TOP-REAL 2<>{} by Th19;
A37: dom (Out_In_Sq|K1)=dom Out_In_Sq /\ K1 by RELAT_1:90
      .=(NonZero TOP-REAL 2)/\ K1 by A36,FUNCT_2:def 1
      .=K1 by A34,XBOOLE_1:28;
A38: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
      .=K1 by PRE_TOPC:def 10;
    then consider p3 being Point of TOP-REAL 2 such that
A39: p=p3 & ((p3`1<=p3`2 & -p3`2<=p3`1 or p3`1>=p3`2 & p3`1<=-p3`2)
    & p3<>0.TOP-REAL 2) by A1,A33;
A40: Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A39,Th24;
    (Out_In_Sq|K1).p=Out_In_Sq.p by A33,A38,FUNCT_1:72;
    then g1.p=(proj2).(|[p`1/p`2/p`2,1/p`2]|) by A33,A37,A38,A40,FUNCT_1:23
      .=(|[p`1/p`2/p`2,1/p`2]|)`2 by PSCOMP_1:def 29
      .=1/p`2 by EUCLID:56;
    hence thesis;
  end;
  then consider f1 being Function of (TOP-REAL 2)|K1,R^1 such that
A41: for p being Point of TOP-REAL 2 st p in the carrier of (TOP-REAL 2)|K1
  holds f1.p=1/p`2;
A42: f1 is continuous by A21,A41,Th42;
  for x,y,s,r being real number st |[x,y]| in K1 &
  s=f2.(|[x,y]|) & r=f1.(|[x,y]|) holds f. |[x,y]|=|[s,r]|
  proof
    let x,y,s,r be real number;
    assume
A43: |[x,y]| in K1 & s=f2.(|[x,y]|) & r=f1.(|[x,y]|);
    set p99=|[x,y]|;
A44: the carrier of (TOP-REAL 2)|K1=[#]((TOP-REAL 2)|K1)
      .=K1 by PRE_TOPC:def 10;
    consider p3 being Point of TOP-REAL 2 such that
A45: p99=p3 & ((p3`1<=p3`2 & -p3`2<=p3`1 or p3`1>=p3`2 & p3`1<=-p3`2)
    & p3<>0.TOP-REAL 2) by A1,A43;
A46: f1.p99=1/p99`2 by A41,A43,A44;
    (Out_In_Sq|K0). |[x,y]|=(Out_In_Sq). |[x,y]| by A43,FUNCT_1:72
      .= |[p99`1/p99`2/p99`2,1/p99`2]| by A45,Th24
      .=|[s,r]| by A31,A43,A44,A46;
    hence thesis by A1;
  end;
  hence thesis by A2,A3,A32,A42,Th45;
end;

scheme TopSubset { P[set] } :
  { p where p is Point of TOP-REAL 2 : P[p] } is Subset of TOP-REAL 2
proof
  { p where p is Point of TOP-REAL 2 : P[p] } c= the carrier of TOP-REAL 2
  proof
    let x be set;
    assume x in { p where p is Point of TOP-REAL 2 : P[p] };
    then consider p being Point of TOP-REAL 2 such that
A1: p = x & P[p];
    thus thesis by A1;
  end;
  hence thesis;
end;

scheme TopCompl { P[set], K() -> Subset of TOP-REAL 2 } :
  K()` = { p where p is Point of TOP-REAL 2 : not P[p] }
provided
A1: K() = { p where p is Point of TOP-REAL 2 : P[p] }
proof
  thus K()` c= { p where p is Point of TOP-REAL 2: not P[p] }
  proof
    let x be set;
    assume
A2: x in K()`;
    then x in (the carrier of TOP-REAL 2) \ K() by SUBSET_1:def 5;
    then
A3: x in (the carrier of TOP-REAL 2) & not x in K() by XBOOLE_0:def 5;
    reconsider qx=x as Point of TOP-REAL 2 by A2;
    not P[qx] by A1,A3;
    hence thesis;
  end;
  let x be set;
  assume x in {p7 where p7 is Point of TOP-REAL 2: not P[p7]};
  then consider p7 being Point of TOP-REAL 2 such that
A4: p7=x & not P[p7];
  not ex q7 being Point of TOP-REAL 2 st x=q7 & P[q7] by A4;
  then not x in K() by A1;
  then x in (the carrier of TOP-REAL 2) \ K() by A4,XBOOLE_0:def 5;
  hence thesis by SUBSET_1:def 5;
end;

Lm1: now
  let p01, p02,px1,px2 be real number;
  set r0 = (p01 -p02)/4;
  assume p01 - px1 - (p02 - px2)<=r0--r0;
  then p01 - p02 - (px1 - px2)<=r0+r0;
  then p01 - p02<= (px1 - px2)+(r0+r0) by XREAL_1:22;
  then p01 - p02 - (p01 -p02)/2<= (px1 - px2) by XREAL_1:22;
  hence (p01 - p02)/2<= px1 - px2;
end;

scheme ClosedSubset { F,G(Point of TOP-REAL 2) -> real number } :
  {p where p is Point of TOP-REAL 2 : F(p) <= G(p) }
  is closed Subset of TOP-REAL 2
provided
A1: for p,q being Point of TOP-REAL 2 holds
F(p-q) = F(p) - F(q) & G(p-q) = G(p) - G(q)
and
A2: for p,q being Point of TOP-REAL 2 holds
(|. (p-q).|)^2 = (F(p-q))^2+(G(p-q))^2
proof
  defpred P[Point of TOP-REAL 2] means F($1) <= G($1);
  reconsider K2 = {p7 where p7 is Point of TOP-REAL 2: P[p7] }
  as Subset of TOP-REAL 2 from TopSubset;
A3: K2 = {p7 where p7 is Point of TOP-REAL 2: P[p7] };
A4: K2`={p7 where p7 is Point of TOP-REAL 2:not P[p7]} from TopCompl(A3);
YY: the TopStruct of TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8;
  then reconsider K21 = K2` as Subset of TopSpaceMetr Euclid 2 by XX;
  for p being Point of Euclid 2 st p in K21
  ex r being real number st r>0 & Ball(p,r) c= K21
  proof
    let p be Point of Euclid 2;
    assume
A5: p in K21;
    then reconsider p0=p as Point of TOP-REAL 2;
    set r0=(F(p0) -G(p0))/4;
    consider p7 being Point of TOP-REAL 2 such that
A6: p0=p7 & F(p7)>G(p7) by A4,A5;
A7: F(p0)- G(p0)>0 by A6,XREAL_1:52;
    then
A8: r0>0 by XREAL_1:141;
A9: (F(p0) -G(p0))/2 >0 by A7,XREAL_1:141;
    Ball(p,r0) c= K2`
    proof
      let x be set;
      assume
A10:  x in Ball(p,r0);
      then reconsider px=x as Point of TOP-REAL 2 by TOPREAL3:13;
      Ball(p,r0)={q where q is Element of Euclid 2:
      dist(p,q) < r0} by METRIC_1:18;
      then consider q being Element of Euclid 2 such that
A11:  q=x & dist(p,q) < r0 by A10;
A12:  dist(p,q)= |. (p0-px).| by A11,JGRAPH_1:45;
A13:  (|. (p0-px).|)^2 =(F(p0-px))^2+(G(p0-px))^2 by A2;
A14:  (G(p0-px))^2 >= 0 by XREAL_1:65;
      (F(p0-px))^2 >= 0 by XREAL_1:65;
      then
A15:  (G(p0-px))^2+0 <= (G(p0-px))^2 + (F(p0-px))^2 by XREAL_1:9;
A16:  0+(F(p0-px))^2 <= (G(p0-px))^2 + (F(p0-px))^2 by A14,XREAL_1:9;
A17:  (|.(p0-px).|)^2 <= r0^2 by A11,A12,SQUARE_1:77;
      then
A18:  (G(p0-px))^2 <= r0^2 by A13,A15,XXREAL_0:2;
A19:  (F(p0-px))^2 <= r0^2 by A13,A16,A17,XXREAL_0:2;
A20:  G(p0-px)=G(p0) - G(px) & F(p0-px)=F(p0) - F(px) by A1;
      then
A21:  -r0 <=G(p0) - G(px) & G(p0) - G(px)<=r0
 by A7,A18,SQUARE_1:117;
      -r0 <=F(p0) - F(px) & F(p0) - F(px)<=r0
       by A7,A19,A20,SQUARE_1:117;
      then F(p0) - F(px) - (G(p0) - G(px))<=r0--r0 by A21,XREAL_1:15;
      then F(px)-G(px)>0 by A9,Lm1;
      then F(px)>G(px) by XREAL_1:49;
      hence thesis by A4;
    end;
    hence thesis by A8;
  end;
  then K21 is open by TOPMETR:22,XX;
  then K2` is open by PRE_TOPC:60,XX,YY;
  hence thesis by TOPS_1:29;
end;
deffunc F(Point of TOP-REAL 2)=$1`1;
deffunc G(Point of TOP-REAL 2)=$1`2;

Lm2: for p,q being Point of TOP-REAL 2 holds
F(p-q) = F(p) - F(q) & G(p-q) = G(p) - G(q) by TOPREAL3:8;

Lm3: for p,q being Point of TOP-REAL 2 holds
(|. (p-q).|)^2 = (F(p-q))^2+(G(p-q))^2 by JGRAPH_1:46;

Lm4: {p7 where p7 is Point of TOP-REAL 2:F(p7)<=G(p7) }
is closed Subset of TOP-REAL 2 from ClosedSubset(Lm2,Lm3);

Lm5: for p,q being Point of TOP-REAL 2 holds
G(p-q) = G(p) - G(q) & F(p-q) = F(p) - F(q) by TOPREAL3:8;

Lm6: for p,q being Point of TOP-REAL 2 holds
(|. (p-q).|)^2 = (G(p-q))^2+(F(p-q))^2 by JGRAPH_1:46;

Lm7: {p7 where p7 is Point of TOP-REAL 2:G(p7)<=F(p7) }
is closed Subset of TOP-REAL 2 from ClosedSubset(Lm5,Lm6);
deffunc H(Point of TOP-REAL 2)=-$1`1;
deffunc I(Point of TOP-REAL 2)=-$1`2;

Lm8: now
  let p,q be Point of TOP-REAL 2;
  thus H(p-q) = -(p`1 - q`1) by TOPREAL3:8
    .= H(p) - H(q);
  thus G(p-q) = G(p) - G(q) by TOPREAL3:8;
end;

Lm9: now
  let p,q be Point of TOP-REAL 2;
  (H(p-q))^2 = (F(p-q))^2;
  hence (|. (p-q).|)^2 = (H(p-q))^2+(G(p-q))^2 by JGRAPH_1:46;
end;

Lm10: {p7 where p7 is Point of TOP-REAL 2:H(p7)<=G(p7) }
is closed Subset of TOP-REAL 2 from ClosedSubset(Lm8,Lm9);

Lm11: now
  let p,q be Point of TOP-REAL 2;
  thus G(p-q) = G(p) - G(q) by TOPREAL3:8;
  thus H(p-q) = -(p`1 - q`1) by TOPREAL3:8
    .= H(p) - H(q);
end;

Lm12: now
  let p,q be Point of TOP-REAL 2;
  (-(p-q)`1)^2 = ((p-q)`1)^2;
  hence (|. (p-q).|)^2 = (G(p-q))^2+(H(p-q))^2 by JGRAPH_1:46;
end;

Lm13: {p7 where p7 is Point of TOP-REAL 2:G(p7)<=H(p7) }
is closed Subset of TOP-REAL 2 from ClosedSubset(Lm11,Lm12);

Lm14: now
  let p,q be Point of TOP-REAL 2;
  thus I(p-q) = -(p`2 - q`2) by TOPREAL3:8
    .= I(p) - I(q);
  thus F(p-q) = F(p) - F(q) by TOPREAL3:8;
end;

Lm15: now
  let p,q be Point of TOP-REAL 2;
  (-(p-q)`2)^2 = ((p-q)`2)^2;
  hence (|. (p-q).|)^2 = (I(p-q))^2+(F(p-q))^2 by JGRAPH_1:46;
end;

Lm16: {p7 where p7 is Point of TOP-REAL 2:I(p7)<=F(p7) }
is closed Subset of TOP-REAL 2 from ClosedSubset(Lm14,Lm15);

Lm17: now
  let p,q be Point of TOP-REAL 2;
  thus F(p-q) = F(p) - F(q) by TOPREAL3:8;
  thus I(p-q) = -(p`2 - q`2) by TOPREAL3:8
    .= I(p) - I(q);
end;

Lm18: now
  let p,q be Point of TOP-REAL 2;
  (I(p-q))^2 = (G(p-q))^2;
  hence (|. (p-q).|)^2 = (F(p-q))^2+(I(p-q))^2 by JGRAPH_1:46;
end;

Lm19: {p7 where p7 is Point of TOP-REAL 2: F(p7)<=I(p7) }
is closed Subset of TOP-REAL 2 from ClosedSubset(Lm17,Lm18);

theorem Th48:
  for B0 being Subset of TOP-REAL 2,K0 being Subset of
  (TOP-REAL 2)|B0,f being Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0)
  st f=Out_In_Sq|K0 & B0=NonZero TOP-REAL 2
  & K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2}
  holds f is continuous & K0 is closed
proof
  let B0 be Subset of TOP-REAL 2,K0 be Subset of
  (TOP-REAL 2)|B0,f being Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0);
  assume
A1: f=Out_In_Sq|K0 & B0=NonZero TOP-REAL 2
  & K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2};
  the carrier of (TOP-REAL 2)|B0=[#]((TOP-REAL 2)|B0)
    .= B0 by PRE_TOPC:def 10;
  then reconsider K1=K0 as Subset of TOP-REAL 2 by XBOOLE_1:1;
  K0 c= B0
  proof
    let x be set;
    assume x in K0;
    then consider p8 being Point of TOP-REAL 2 such that
A2: x=p8 & (
    (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1) & p8<>0.TOP-REAL 2)
    by A1;
    not x in {0.TOP-REAL 2} by A2,TARSKI:def 1;
    hence thesis by A1,A2,XBOOLE_0:def 5;
  end;
  then
A3: ((TOP-REAL 2)|B0)|K0=(TOP-REAL 2)|K1 by PRE_TOPC:28;
  defpred P[Point of TOP-REAL 2] means
  ($1`2<=$1`1 & -$1`1<=$1`2 or $1`2>=$1`1 & $1`2<=-$1`1);
  reconsider K1={p7 where p7 is Point of TOP-REAL 2: P[p7]}
  as Subset of TOP-REAL 2 from TopSubset;
  reconsider K2={p7 where p7 is Point of TOP-REAL 2: p7`2<=p7`1 }
  as closed Subset of TOP-REAL 2 by Lm7;
  reconsider K3={p7 where p7 is Point of TOP-REAL 2: -p7`1<=p7`2 }
  as closed Subset of TOP-REAL 2 by Lm10;
  reconsider K4={p7 where p7 is Point of TOP-REAL 2: p7`1<=p7`2 }
  as closed Subset of TOP-REAL 2 by Lm4;
  reconsider K5={p7 where p7 is Point of TOP-REAL 2: p7`2<=-p7`1 }
  as closed Subset of TOP-REAL 2 by Lm13;
A4: K2 /\ K3 is closed by TOPS_1:35;
A5: K4 /\ K5 is closed by TOPS_1:35;
A6: K2 /\ K3 \/ K4 /\ K5 c= K1
  proof
    let x be set;
    assume
A7: x in K2 /\ K3 \/ K4 /\ K5;
    now per cases by A7,XBOOLE_0:def 3;
      case x in K2 /\ K3;
        then
A8:     x in K2 & x in K3 by XBOOLE_0:def 4;
        then consider p7 being Point of TOP-REAL 2 such that
A9:     p7=x & p7`2<=(p7`1);
        consider p8 being Point of TOP-REAL 2 such that
A10:    p8=x & -p8`1<=p8`2 by A8;
        thus thesis by A9,A10;
      end;
      case x in K4 /\ K5;
        then
A11:    x in K4 & x in K5 by XBOOLE_0:def 4;
        then consider p7 being Point of TOP-REAL 2 such that
A12:    p7=x & p7`2>=(p7`1);
        consider p8 being Point of TOP-REAL 2 such that
A13:    p8=x & p8`2<= -p8`1 by A11;
        thus thesis by A12,A13;
      end;
    end;
    hence thesis;
  end;
  K1 c= K2 /\ K3 \/ K4 /\ K5
  proof
    let x be set;
    assume x in K1;
    then consider p being Point of TOP-REAL 2 such that
A14: p=x & (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1);
    x in K2 & x in K3 or x in K4 & x in K5 by A14;
    then x in K2 /\ K3 or x in K4 /\ K5 by XBOOLE_0:def 4;
    hence thesis by XBOOLE_0:def 3;
  end;
  then K1=K2 /\ K3 \/ K4 /\ K5 by A6,XBOOLE_0:def 10;
  then
A15: K1 is closed by A4,A5,TOPS_1:36;
A16: K1 /\ B0 c= K0
  proof
    let x be set;
    assume x in K1 /\ B0;
    then
A17: x in K1 & x in B0 by XBOOLE_0:def 4;
    then consider p7 being Point of TOP-REAL 2 such that
A18: p7=x & (p7`2<=(p7`1) & -(p7`1)<=p7`2 or p7`2>=(p7`1) & p7`2<=-(p7`1));
    x in the carrier of TOP-REAL 2 & not x in {0.TOP-REAL 2}
    by A1,A17,XBOOLE_0:def 5;
    then not x=0.TOP-REAL 2 by TARSKI:def 1;
    hence thesis by A1,A18;
  end;
  K0 c= K1 /\ B0
  proof
    let x be set;
    assume x in K0;
    then consider p being Point of TOP-REAL 2 such that
A19: x=p & (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2
 by A1;
    x in the carrier of TOP-REAL 2 & not x in {0.TOP-REAL 2}
     by A19,TARSKI:def 1;
    then x in K1 & x in B0 by A1,A19,XBOOLE_0:def 5;
    hence thesis by XBOOLE_0:def 4;
  end;
  then K0=K1 /\ B0 by A16,XBOOLE_0:def 10
    .=K1 /\ [#]((TOP-REAL 2)|B0) by PRE_TOPC:def 10;
  hence thesis by A1,A3,A15,Th46,PRE_TOPC:43;
end;

theorem Th49:
  for B0 being Subset of TOP-REAL 2,K0 being Subset of
  (TOP-REAL 2)|B0,f being Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0)
  st f=Out_In_Sq|K0 & B0=NonZero TOP-REAL 2
  & K0={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2}
  holds f is continuous & K0 is closed
proof
  let B0 be Subset of TOP-REAL 2,K0 be Subset of
  (TOP-REAL 2)|B0,f being Function of ((TOP-REAL 2)|B0)|K0,((TOP-REAL 2)|B0);
  assume
A1: f=Out_In_Sq|K0 & B0=NonZero TOP-REAL 2
  & K0={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2};
  the carrier of (TOP-REAL 2)|B0=[#]((TOP-REAL 2)|B0)
    .= B0 by PRE_TOPC:def 10;
  then reconsider K1=K0 as Subset of TOP-REAL 2 by XBOOLE_1:1;
  K0 c= B0
  proof
    let x be set;
    assume x in K0;
    then consider p8 being Point of TOP-REAL 2 such that
A2: x=p8 & (
    (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2) & p8<>0.TOP-REAL 2)
    by A1;
    x in the carrier of TOP-REAL 2 & not x in {0.TOP-REAL 2}
     by A2,TARSKI:def 1;
    hence thesis by A1,XBOOLE_0:def 5;
  end;
  then
A3: ((TOP-REAL 2)|B0)|K0=(TOP-REAL 2)|K1 by PRE_TOPC:28;
  defpred P[Point of TOP-REAL 2] means
  ($1`1<=$1`2 & -$1`2<=$1`1 or $1`1>=$1`2 & $1`1<=-$1`2);
  reconsider K1={p7 where p7 is Point of TOP-REAL 2: P[p7]}
  as Subset of TOP-REAL 2 from TopSubset;
  reconsider K2={p7 where p7 is Point of TOP-REAL 2: p7`1<=p7`2 }
  as closed Subset of TOP-REAL 2 by Lm4;
  reconsider K3={p7 where p7 is Point of TOP-REAL 2:
  -p7`2<=p7`1 } as closed Subset of TOP-REAL 2 by Lm16;
  reconsider K4={p7 where p7 is Point of TOP-REAL 2:
  p7`2<=p7`1 } as closed Subset of TOP-REAL 2 by Lm7;
  reconsider K5={p7 where p7 is Point of TOP-REAL 2:
  p7`1<=-p7`2 } as closed Subset of TOP-REAL 2 by Lm19;
A4: K2 /\ K3 is closed by TOPS_1:35;
A5: K4 /\ K5 is closed by TOPS_1:35;
A6: K2 /\ K3 \/ K4 /\ K5 c= K1
  proof
    let x be set;
    assume
A7: x in K2 /\ K3 \/ K4 /\ K5;
    now per cases by A7,XBOOLE_0:def 3;
      case x in K2 /\ K3;
        then
A8:     x in K2 & x in K3 by XBOOLE_0:def 4;
        then consider p7 being Point of TOP-REAL 2 such that
A9:     p7=x & p7`1<=(p7`2);
        consider p8 being Point of TOP-REAL 2 such that
A10:    p8=x & -p8`2<=p8`1 by A8;
        thus thesis by A9,A10;
      end;
      case x in K4 /\ K5;
        then
A11:    x in K4 & x in K5 by XBOOLE_0:def 4;
        then consider p7 being Point of TOP-REAL 2 such that
A12:    p7=x & p7`1>=(p7`2);
        consider p8 being Point of TOP-REAL 2 such that
A13:    p8=x & p8`1<= -p8`2 by A11;
        thus thesis by A12,A13;
      end;
    end;
    hence thesis;
  end;
  K1 c= K2 /\ K3 \/ K4 /\ K5
  proof
    let x be set;
    assume x in K1;
    then consider p being Point of TOP-REAL 2 such that
A14: p=x & (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2);
    x in K2 & x in K3 or x in K4 & x in K5 by A14;
    then x in K2 /\ K3 or x in K4 /\ K5 by XBOOLE_0:def 4;
    hence thesis by XBOOLE_0:def 3;
  end;
  then K1=K2 /\ K3 \/ K4 /\ K5 by A6,XBOOLE_0:def 10;
  then
A15: K1 is closed by A4,A5,TOPS_1:36;
A16: K1 /\ B0 c= K0
  proof
    let x be set;
    assume x in K1 /\ B0;
    then
A17: x in K1 & x in B0 by XBOOLE_0:def 4;
    then consider p7 being Point of TOP-REAL 2 such that
A18: p7=x & (p7`1<=(p7`2) & -(p7`2)<=p7`1 or p7`1>=(p7`2) & p7`1<=-(p7`2));
    x in the carrier of TOP-REAL 2 & not x in {0.TOP-REAL 2}
    by A1,A17,XBOOLE_0:def 5;
    then not x=0.TOP-REAL 2 by TARSKI:def 1;
    hence thesis by A1,A18;
  end;
  K0 c= K1 /\ B0
  proof
    let x be set;
    assume x in K0;
    then consider p being Point of TOP-REAL 2 such that
A19: x=p & (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2
 by A1;
    x in the carrier of TOP-REAL 2 & not x in {0.TOP-REAL 2}
     by A19,TARSKI:def 1;
    then x in K1 & x in B0 by A1,A19,XBOOLE_0:def 5;
    hence thesis by XBOOLE_0:def 4;
  end;
  then K0=K1 /\ B0 by A16,XBOOLE_0:def 10
    .=K1 /\ [#]((TOP-REAL 2)|B0) by PRE_TOPC:def 10;
  hence thesis by A1,A3,A15,Th47,PRE_TOPC:43;
end;

theorem Th50:
  for D being non empty Subset of TOP-REAL 2 st D`={0.TOP-REAL 2} holds
  ex h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D
  st h=Out_In_Sq & h is continuous
proof
  let D be non empty Subset of TOP-REAL 2;
  assume
A1: D`={0.TOP-REAL 2};
  reconsider B0= {0.TOP-REAL 2} as Subset of TOP-REAL 2;
A2: D=(B0)` by A1
    .=(NonZero TOP-REAL 2) by SUBSET_1:def 5;
A3: {p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)
  & p<>0.TOP-REAL 2} c= the carrier of (TOP-REAL 2)|D
  proof
    let x be set;
    assume x in {p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)
    & p<>0.TOP-REAL 2};
    then consider p such that
A4: x=p & (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)& p<>0.TOP-REAL 2;
    now
      assume not x in D;
      then x in (the carrier of TOP-REAL 2) \ D by A4,XBOOLE_0:def 5;
      then x in D` by SUBSET_1:def 5;
      hence contradiction by A1,A4,TARSKI:def 1;
    end;
    then x in [#]((TOP-REAL 2)|D) by PRE_TOPC:def 10;
    hence thesis;
  end;
  ((1.REAL 2)`2<=(1.REAL 2)`1 & -(1.REAL 2)`1<=(1.REAL 2)`2 or
  (1.REAL 2)`2>=(1.REAL 2)`1 & (1.REAL 2)`2<=-(1.REAL 2)`1) &
  (1.REAL 2)<>0.TOP-REAL 2 by Th13,REVROT_1:19,XX;
  then 1.REAL 2 in {p where p is Point of TOP-REAL 2:
  (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2};
  then reconsider K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1)
  & p<>0.TOP-REAL 2} as non empty Subset of (TOP-REAL 2)|D by A3;
A5: {p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2)
  & p<>0.TOP-REAL 2} c= the carrier of (TOP-REAL 2)|D
  proof
    let x be set;
    assume x in {p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2)
    & p<>0.TOP-REAL 2};
    then consider p such that
A6: x=p & (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2)& p<>0.TOP-REAL 2;
    now
      assume not x in D;
      then x in (the carrier of TOP-REAL 2) \ D by A6,XBOOLE_0:def 5;
      then x in D` by SUBSET_1:def 5;
      hence contradiction by A1,A6,TARSKI:def 1;
    end;
    then x in [#]((TOP-REAL 2)|D) by PRE_TOPC:def 10;
    hence thesis;
  end;
  set Y1=|[-1,1]|;
  Y1`1=-1 & Y1`2=1 by EUCLID:56;
  then Y1 in {p where p is Point of TOP-REAL 2:
  (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2} by Th11;
  then reconsider K1={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2)
  & p<>0.TOP-REAL 2} as non empty Subset of (TOP-REAL 2)|D by A5;
A7: the carrier of ((TOP-REAL 2)|D) =[#]((TOP-REAL 2)|D)
    .=D by PRE_TOPC:def 10;
A8: K0 c= NonZero TOP-REAL 2
  proof
    let z be set;
    assume z in K0;
    then consider p8 being Point of TOP-REAL 2 such that
A9: p8=z &( (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1)
    & p8<>0.TOP-REAL 2);
    z in (the carrier of TOP-REAL 2) & not z in {0.TOP-REAL 2}
     by A9,TARSKI:def 1;
    hence thesis by XBOOLE_0:def 5;
  end;
A10: NonZero TOP-REAL 2<> {} by Th19;
A11: dom (Out_In_Sq|K0)= dom (Out_In_Sq) /\ K0 by RELAT_1:90
    .=(NonZero TOP-REAL 2) /\ K0 by A10,FUNCT_2:def 1
    .=K0 by A8,XBOOLE_1:28;
A12: K0=[#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 10
    .=the carrier of ((TOP-REAL 2)|D)|K0;
  rng (Out_In_Sq|K0) c= the carrier of ((TOP-REAL 2)|D)|K0
  proof
    let y be set;
    assume y in rng (Out_In_Sq|K0);
    then consider x being set such that
A13: x in dom (Out_In_Sq|K0) & y=(Out_In_Sq|K0).x by FUNCT_1:def 5;
A14: x in (dom Out_In_Sq) /\ K0 by A13,RELAT_1:90;
    then
A15: x in K0 by XBOOLE_0:def 4;
    K0 c= the carrier of TOP-REAL 2 by A7,XBOOLE_1:1;
    then reconsider p=x as Point of TOP-REAL 2 by A15;
A16: Out_In_Sq.p=y by A13,A15,FUNCT_1:72;
    consider px being Point of TOP-REAL 2 such that
A17: x=px & (px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1)
    & px<>0.TOP-REAL 2 by A15;
    reconsider K00=K0 as Subset of TOP-REAL 2 by A7,XBOOLE_1:1;
    K00=[#]((TOP-REAL 2)|K00) by PRE_TOPC:def 10
      .=the carrier of ((TOP-REAL 2)|K00);
    then
A18: p in the carrier of ((TOP-REAL 2)|K00) by A14,XBOOLE_0:def 4;
A19: for q being Point of TOP-REAL 2 st q in
    the carrier of (TOP-REAL 2)|K00 holds q`1<>0
    proof
      let q be Point of TOP-REAL 2;
      assume
A20:  q in the carrier of (TOP-REAL 2)|K00;
      the carrier of (TOP-REAL 2)|K00=[#]((TOP-REAL 2)|K00)
        .=K0 by PRE_TOPC:def 10;
      then consider p3 being Point of TOP-REAL 2 such that
A21:  q=p3 & ((p3`2<=p3`1 & -p3`1<=p3`2 or p3`2>=p3`1 & p3`2<=-p3`1)
      & p3<>0.TOP-REAL 2) by A20;
      now
        assume
A22:    q`1=0;
        then q`2=0 by A21;
        hence contradiction by A21,A22,EUCLID:57,58;
      end;
      hence thesis;
    end;
    then
A23: p`1<>0 by A18;
    set p9=|[1/p`1,p`2/p`1/p`1]|;
A24: p9`1=1/p`1 & p9`2=p`2/p`1/p`1 by EUCLID:56;
A25: now
      assume p9=0.TOP-REAL 2;
      then 0 *p`1=1/p`1*p`1 by A24,EUCLID:56,58;
      hence contradiction by A18,A19,XCMPLX_1:88;
    end;
A26: Out_In_Sq.p=|[1/p`1,p`2/p`1/p`1]| by A17,Def1;
    now per cases;
      case
A27:    p`1>=0;
        then p`2/p`1<=p`1/p`1 & (-1 *p`1)/p`1<=p`2/p`1 or p`2>=p`1 & p`2<=-1 *
        p `1 by A17,XREAL_1:74;
        then
B28:      p`2/p`1<=1 & (-1)*p`1/p`1<=p`2/p`1 or p`2>=p`1 & p`2<=-1 *p`1
        by A18,A19,XCMPLX_1:60;
        then
A28:    p`2/p`1<=1 & -1<=p`2/p`1 or p`2/p`1>=p`1/p`1 & p`2<=-1 *p `1
        by A18,A19,A27,XCMPLX_1:90;
        not (p`2/p`1>=1 & p`2/p`1<=-1);
        then (-1)/p`1<= p`2/p`1/p`1 by A19,A18,A27,A28,XCMPLX_1:60,XREAL_1:74;
        then
A30:    p`2/p`1/p`1 <=1/p`1 & -(1/p`1)<= p`2/p`1/p`1 or
        p`2/p`1/p`1 >=1/p`1 & p`2/p`1/p`1<= -(1/p`1)
         by A23,A27,B28,XREAL_1:74;
        p9`1=1/p`1 & p9`2=p`2/p`1/p`1 by EUCLID:56;
        hence y in K0 by A16,A25,A26,A30;
      end;
      case
A31:    p`1<0;

then p`2<=p`1 & (-1 *p`1)<=p`2 or p`2/p`1<=p`1/p`1 & p`2/p`1>=(-1 *p`1)/ p`1
        by A17,XREAL_1:75;
        then
B32:      p`2<=p`1 & (-1 *p`1)<=p`2 or p`2/p`1<=1 & p`2/p`1>=(-1)*p`1/p`1
        by A31,XCMPLX_1:60;
        then
A32:    p`2/p`1>=p`1/p`1 & (-1 *p`1)<=p`2 or p`2/p`1<=1 & p`2/p`1 >=-1
        by A31,XCMPLX_1:90;
        not(p`2/p`1>=1 & p`2/p`1<=-1);
        then (-1)/p`1>= p`2/p`1/p`1 by A31,A32,XCMPLX_1:60,XREAL_1:75;
        then
A34:    p`2/p`1/p`1 <=1/p`1 & -(1/p`1)<= p`2/p`1/p`1 or
        p`2/p`1/p`1 >=1/p`1 & p`2/p`1/p`1<= -(1/p`1) by A31,B32,XREAL_1:75;
        p9`1=1/p`1 & p9`2=p`2/p`1/p`1 by EUCLID:56;
        hence y in K0 by A16,A25,A26,A34;
      end;
    end;
    then y in [#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 10;
    hence thesis;
  end;
  then reconsider f=Out_In_Sq|K0
  as Function of ((TOP-REAL 2)|D)|K0,((TOP-REAL 2)|D) by A11,A12,FUNCT_2:4
,XBOOLE_1:1;
A35: K1 c= NonZero TOP-REAL 2
  proof
    let z be set;
    assume z in K1;
    then consider p8 being Point of TOP-REAL 2 such that
A36: p8=z &( (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2)
    & p8<>0.TOP-REAL 2);
    z in (the carrier of TOP-REAL 2) & not z in {0.TOP-REAL 2}
    by A36,TARSKI:def 1;
    hence thesis by XBOOLE_0:def 5;
  end;
A37: NonZero TOP-REAL 2<> {} by Th19;
A38: dom (Out_In_Sq|K1)= dom (Out_In_Sq) /\ K1 by RELAT_1:90
    .=(NonZero TOP-REAL 2) /\ K1 by A37,FUNCT_2:def 1
    .=K1 by A35,XBOOLE_1:28;
A39: K1=[#](((TOP-REAL 2)|D)|K1) by PRE_TOPC:def 10
    .=the carrier of ((TOP-REAL 2)|D)|K1;
  rng (Out_In_Sq|K1) c= the carrier of ((TOP-REAL 2)|D)|K1
  proof
    let y be set;
    assume y in rng (Out_In_Sq|K1);
    then consider x being set such that
A40: x in dom (Out_In_Sq|K1) & y=(Out_In_Sq|K1).x by FUNCT_1:def 5;
A41: x in (dom Out_In_Sq) /\ K1 by A40,RELAT_1:90;
    then
A42: x in K1 by XBOOLE_0:def 4;
    K1 c= the carrier of TOP-REAL 2 by A7,XBOOLE_1:1;
    then reconsider p=x as Point of TOP-REAL 2 by A42;
A43: Out_In_Sq.p=y by A40,A42,FUNCT_1:72;
    consider px being Point of TOP-REAL 2 such that
A44: x=px & (px`1<=px`2 & -px`2<=px`1 or px`1>=px`2 & px`1<=-px`2)
    & px<>0.TOP-REAL 2 by A42;
    reconsider K10=K1 as Subset of TOP-REAL 2 by A7,XBOOLE_1:1;
    K10=[#]((TOP-REAL 2)|K10) by PRE_TOPC:def 10
      .=the carrier of ((TOP-REAL 2)|K10);
    then
A45: p in the carrier of ((TOP-REAL 2)|K10) by A41,XBOOLE_0:def 4;
A46: for q being Point of TOP-REAL 2 st q in
    the carrier of (TOP-REAL 2)|K10 holds q`2<>0
    proof
      let q be Point of TOP-REAL 2;
      assume
A47:  q in the carrier of (TOP-REAL 2)|K10;
      the carrier of (TOP-REAL 2)|K10=[#]((TOP-REAL 2)|K10)
        .=K1 by PRE_TOPC:def 10;
      then consider p3 being Point of TOP-REAL 2 such that
A48:  q=p3 & ((p3`1<=p3`2 & -p3`2<=p3`1 or p3`1>=p3`2 & p3`1<=-p3`2)
      & p3<>0.TOP-REAL 2) by A47;
      now
        assume
A49:    q`2=0;
        then q`1=0 by A48;
        hence contradiction by A48,A49,EUCLID:57,58;
      end;
      hence thesis;
    end;
    set p9=|[p`1/p`2/p`2,1/p`2]|;
A51: now
      assume p9=0.TOP-REAL 2;
      then p9`2=0 & p9`1=0 by EUCLID:56,58;
      then 0 *p`2=1/p`2*p`2 by EUCLID:56;
      hence contradiction by A45,A46,XCMPLX_1:88;
    end;
A52: Out_In_Sq.p=|[p`1/p`2/p`2,1/p`2]| by A44,Th24;
    now per cases;
      case
A53:    p`2>=0;
        then p`1/p`2<=p`2/p`2 & (-1 *p`2)/p`2<=p`1/p`2 or p`1>=p`2 & p`1<=-1 *
        p `2 by A44,XREAL_1:74;
        then
B54:     p`1/p`2<=1 & (-1)*p`2/p`2<=p`1/p`2 or p`1>=p`2 & p`1<=-1 *p`2
        by A45,A46,XCMPLX_1:60;
        then
A54:    p`1/p`2<=1 & -1<=p`1/p`2 or p`1/p`2>=p`2/p`2 & p`1<=-1 *p `2
        by A45,A46,A53,XCMPLX_1:90;
A55:    p`1/p`2<=1 & -1<=p`1/p`2 or p`1/p`2>=1 & p`1/p`2<=(-1)*p`2 /p`2
        by A46,A45,A53,B54,XCMPLX_1:90,XREAL_1:74;
        not(p`1/p`2>=1 & p`1/p`2<=-1);
        then (-1)/p`2<= p`1/p`2/p`2 by A45,A46,A53,A54,XCMPLX_1:60,XREAL_1:74;
        then
A56:    p`1/p`2/p`2 <=1/p`2 & -(1/p`2)<= p`1/p`2/p`2 or
        p`1/p`2/p`2 >=1/p`2 & p`1/p`2/p`2<= -(1/p`2)
         by A53,A55,XREAL_1:74;
        p9`2=1/p`2 & p9`1=p`1/p`2/p`2 by EUCLID:56;
        hence y in K1 by A43,A51,A52,A56;
      end;
      case
A57:    p`2<0;

then p`1<=p`2 & (-1 *p`2)<=p`1 or p`1/p`2<=p`2/p`2 & p`1/p`2>=(-1 *p`2)/ p`2
        by A44,XREAL_1:75;
        then
B58:     p`1<=p`2 & (-1 *p`2)<=p`1 or p`1/p`2<=1 & p`1/p`2>=(-1)*p`2/p`2
        by A57,XCMPLX_1:60;
        then
        p`1/p`2>=1 & (-1)*p`2/p`2>=p`1/p`2 or p`1/p`2<=1 & p`1/p`2 >=-1
        by A57,XCMPLX_1:90;
        then (-1)/p`2>= p`1/p`2/p`2 by A57,XREAL_1:75;
        then
A59:    p`1/p`2/p`2 <=1/p`2 & -(1/p`2)<= p`1/p`2/p`2 or
        p`1/p`2/p`2 >=1/p`2 & p`1/p`2/p`2<= -(1/p`2)
         by B58,A57,XREAL_1:75;
        p9`2=1/p`2 & p9`1=p`1/p`2/p`2 by EUCLID:56;
        hence y in K1 by A43,A51,A52,A59;
      end;
    end;
    then y in [#](((TOP-REAL 2)|D)|K1) by PRE_TOPC:def 10;
    hence thesis;
  end;
  then reconsider g=Out_In_Sq|K1 as Function of ((TOP-REAL 2)|D)|K1,
  ((TOP-REAL 2)|D) by A38,A39,FUNCT_2:4,XBOOLE_1:1;
A60: K0=[#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 10;
A61: K1=[#](((TOP-REAL 2)|D)|K1) by PRE_TOPC:def 10;
A62: D= [#]((TOP-REAL 2)|D) by PRE_TOPC:def 10;
  D c= K0 \/ K1
  proof
    let x be set;
    assume
A67: x in D;
    then
A68: x in (the carrier of TOP-REAL 2) & not x in {0.TOP-REAL 2}
    by A2,XBOOLE_0:def 5;
    reconsider px=x as Point of TOP-REAL 2 by A67;
    (px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1) & px<>0.TOP-REAL 2
     or
    (px`1<=px`2 & -px`2<=px`1 or px`1>=px`2 & px`1<=-px`2)
    & px<>0.TOP-REAL 2 by A68,TARSKI:def 1,XREAL_1:28;
    then x in K0 or x in K1;
    hence thesis by XBOOLE_0:def 3;
  end;
  then
A69: ([#](((TOP-REAL 2)|D)|K0)) \/ ([#](((TOP-REAL 2)|D)|K1))
  = [#]((TOP-REAL 2)|D) by A60,A61,A62,XBOOLE_0:def 10;
  f=Out_In_Sq|K0 & D=NonZero TOP-REAL 2
  & K0={p:(p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) & p<>0.TOP-REAL 2}
   by A2;
  then
A70: f is continuous & K0 is closed by Th48;
  g=Out_In_Sq|K1 & D=NonZero TOP-REAL 2
  & K1={p:(p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2) & p<>0.TOP-REAL 2}
   by A2;
  then
A71: g is continuous & K1 is closed by Th49;
A72: for x be set st x in ([#]((((TOP-REAL 2)|D)|K0)))
  /\ ([#] ((((TOP-REAL 2)|D)|K1))) holds f.x = g.x
  proof
    let x be set;
    assume
Z:   x in ([#]((((TOP-REAL 2)|D)|K0))) /\ ([#] ((((TOP-REAL 2)|D)|K1)));
    then x in K0 & x in K1 by A60,A61,XBOOLE_0:def 4;
    then f.x=Out_In_Sq.x by FUNCT_1:72;
    hence thesis by Z,A61,FUNCT_1:72;
  end;
  then consider h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D such that
A74: h= f+*g & h is continuous by A60,A61,A69,A70,A71,Th9;
A75: dom h=the carrier of ((TOP-REAL 2)|D) by FUNCT_2:def 1;
A76: the carrier of ((TOP-REAL 2)|D) =[#](((TOP-REAL 2)|D))
    .=(NonZero TOP-REAL 2) by A2,PRE_TOPC:def 10;
  then
A77: dom Out_In_Sq=the carrier of ((TOP-REAL 2)|D) by FUNCT_2:def 1;
A78: dom f=K0 by A12,FUNCT_2:def 1;
A79: K0=[#](((TOP-REAL 2)|D)|K0) by PRE_TOPC:def 10;
A80: dom g=K1 by A39,FUNCT_2:def 1;
  K1=[#](((TOP-REAL 2)|D)|K1) by PRE_TOPC:def 10;
  then
A81: f tolerates g by A72,A78,A79,A80,PARTFUN1:def 6;
  for x being set st x in dom h holds h.x=Out_In_Sq.x
  proof
    let x be set;
    assume
A82: x in dom h;
    then x in (the carrier of TOP-REAL 2) & not x in {0.TOP-REAL 2}
    by A76,XBOOLE_0:def 5;
    then
A83: x <>0.TOP-REAL 2 by TARSKI:def 1;
    reconsider p=x as Point of TOP-REAL 2 by A76,A82,XBOOLE_0:def 5;
    now per cases;
      case
A84:    x in K0;
        h.p=(g+*f).p by A74,A81,FUNCT_4:35
          .=f.p by A78,A84,FUNCT_4:14;
        hence thesis by A84,FUNCT_1:72;
      end;
      case not x in K0;
        then not (p`2<=p`1 & -p`1<=p`2 or p`2>=p`1 & p`2<=-p`1) by A83;
        then (p`1<=p`2 & -p`2<=p`1 or p`1>=p`2 & p`1<=-p`2)by XREAL_1:28;
        then
A85:    x in K1 by A83;
        then Out_In_Sq.p=g.p by FUNCT_1:72;
        hence thesis by A74,A80,A85,FUNCT_4:14;
      end;
    end;
    hence thesis;
  end;
  then f+*g=Out_In_Sq by A74,A75,A77,FUNCT_1:9;
  hence thesis by A60,A61,A69,A70,A71,A72,Th9;
end;

theorem Th51:
  for B,K0,Kb being Subset of TOP-REAL 2 st B={0.TOP-REAL 2}
  & K0={p: -1<p`1 & p`1<1 & -1<p`2 & p`2<1}
  & Kb={q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1
  or -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1}
  ex f being Function of (TOP-REAL 2)|B`,(TOP-REAL 2)|B` st
  f is continuous & f is one-to-one &
  (for t being Point of TOP-REAL 2 st t in K0 & t<>0.TOP-REAL 2 holds
  not f.t in K0 \/ Kb)
  &(for r being Point of TOP-REAL 2 st not r in K0 \/ Kb holds f.r in K0)
  &(for s being Point of TOP-REAL 2 st s in Kb holds f.s=s)
proof
  let B,K0,Kb be Subset of TOP-REAL 2;
  assume
A1: B={0.TOP-REAL 2} & K0={p: -1<p`1 & p`1<1 & -1<p`2 & p`2<1}
  & Kb={q: -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1
  or -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1};
  then
A2: B`=NonZero TOP-REAL 2 by SUBSET_1:def 5;
  reconsider D=B` as non empty Subset of TOP-REAL 2 by A1,Th19;
A3: D`={0.TOP-REAL 2} by A1;
  then consider h being Function of (TOP-REAL 2)|D,(TOP-REAL 2)|D such that
A4: h=Out_In_Sq & h is continuous by Th50;
A5: D =(NonZero TOP-REAL 2) by A1,SUBSET_1:def 5;
  set K0a={p8 where p8 is Point of TOP-REAL 2:
  (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1) & p8<>0.TOP-REAL 2};
  set K1a={p8 where p8 is Point of TOP-REAL 2:
  (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2) & p8<>0.TOP-REAL 2 };
  for x1,x2 being set st x1 in dom Out_In_Sq & x2 in dom Out_In_Sq &
  Out_In_Sq.x1=Out_In_Sq.x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume that
A6: x1 in dom Out_In_Sq & x2 in dom Out_In_Sq and
A7: Out_In_Sq.x1=Out_In_Sq.x2;
    NonZero TOP-REAL 2<>{} by Th19;
    then
A8: dom Out_In_Sq=NonZero TOP-REAL 2
by FUNCT_2:def 1;
    reconsider p1=x1,p2=x2 as Point of TOP-REAL 2 by A6,XBOOLE_0:def 5;
A9: the carrier of (TOP-REAL 2)|D=[#]((TOP-REAL 2)|D)
      .=D by PRE_TOPC:def 10;
    reconsider K01=K0a as non empty Subset of ((TOP-REAL 2)|D) by A3,Th27;
    ((1.REAL 2)`1<=(1.REAL 2)`2 & -(1.REAL 2)`2<=(1.REAL 2)`1 or
    (1.REAL 2)`1>=(1.REAL 2)`2 & (1.REAL 2)`1<=-(1.REAL 2)`2) &
    (1.REAL 2)<>0.TOP-REAL 2 by Th13,REVROT_1:19,XX;
    then
A10: 1.REAL 2 in K1a;
    K1a c= D
    proof
      let x be set;
      assume x in K1a;
      then consider p8 being Point of TOP-REAL 2 such that
A11:  x=p8 & (
      (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2) &
      p8<>0.TOP-REAL 2);
      not x in {0.TOP-REAL 2} by A11,TARSKI:def 1;
      hence thesis by A2,A11,XBOOLE_0:def 5;
    end;
    then reconsider K11=K1a as non empty Subset of ((TOP-REAL 2)|D) by A9,A10;
A12: D c= K01 \/ K11
    proof
      let x be set;
      assume
A13:  x in D;
      then
A14:  x in (the carrier of TOP-REAL 2) & not x in {0.TOP-REAL 2}
      by A5,XBOOLE_0:def 5;
      reconsider px=x as Point of TOP-REAL 2 by A13;
      (px`2<=px`1 & -px`1<=px`2 or px`2>=px`1 & px`2<=-px`1) &
      px<>0.TOP-REAL 2 or
      (px`1<=px`2 & -px`2<=px`1 or px`1>=px`2 & px`1<=-px`2)
      & px<>0.TOP-REAL 2 by A14,TARSKI:def 1,XREAL_1:27;
      then x in K01 or x in K11;
      hence thesis by XBOOLE_0:def 3;
    end;
A15: x1 in D & x2 in D by A1,A6,A8,SUBSET_1:def 5;
    now per cases by A12,A15,XBOOLE_0:def 3;
      case x1 in K01;
        then consider p7 being Point of TOP-REAL 2 such that
A16:    p1=p7 & (
        (p7`2<=p7`1 & -p7`1<=p7`2 or p7`2>=p7`1 & p7`2<=-p7`1) &
        p7<>0.TOP-REAL 2);
A17:    Out_In_Sq.p1=|[1/p1`1,p1`2/p1`1/p1`1]| by A16,Def1;
        now per cases by A12,A15,XBOOLE_0:def 3;
          case x2 in K0a;
            then consider p8 being Point of (TOP-REAL 2) such that
A18:        p2=p8 & ( (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1)
            & p8<>0.TOP-REAL 2);
A19:        |[1/p2`1,p2`2/p2`1/p2`1]|
            =|[1/p1`1,p1`2/p1`1/p1`1]| by A7,A17,A18,Def1;
            set qq=|[1/p2`1,p2`2/p2`1/p2`1]|;
            qq`1=1/p2`1 & qq`2=p2`2/p2`1/p2`1 by EUCLID:56;
            then
A20:        1/p1`1= 1/p2`1 & p1`2/p1`1/p1`1 = p2`2/p2`1/p2`1 by A19,EUCLID:56;
A21:        (1/p1`1)"=(p1`1)""
              .=p1`1;
A23:        now
              assume
A24:          p1`1=0;
              then p1`2=0 by A16;
              hence contradiction by A16,A24,EUCLID:57,58;
            end;
            then p1`2/p1`1= p2`2/p1`1 by A20,A21,XCMPLX_1:53;
            then
A25:        p1`2=p2`2 by A23,XCMPLX_1:53;
            p1=|[p1`1,p1`2]| by EUCLID:57;
            hence thesis by A20,A21,A25,EUCLID:57;
          end;
          case
A26:        x2 in K1a & not x2 in K0a;
            then consider p8 being Point of (TOP-REAL 2) such that
A27:        p2=p8 & ((p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2)
            & p8<>0.TOP-REAL 2);
            not((p2`2<=p2`1 & -p2`1<=p2`2 or p2`2>=p2`1 & p2`2<=-p2`1)
            & p2 <> 0.TOP-REAL 2) by A26;
            then Out_In_Sq.p2=|[p2`1/p2`2/p2`2,1/p2`2]| by A27,Def1;
            then
A28:        1/p1`1=p2`1/p2`2/p2`2 & p1`2/p1`1/p1`1=1/p2`2 by A7,A17,SPPOL_2:1;
A29:        now
              assume
A30:          p2`2=0;
              then p2`1=0 by A27;
              hence contradiction by A27,A30,EUCLID:57,58;
            end;
A31:        now
              assume
A32:          p1`1=0;
              then p1`2=0 by A16;
              hence contradiction by A16,A32,EUCLID:57,58;
            end;
A33:        p2`1/p2`2=1/p1`1*p2`2 by A28,A29,XCMPLX_1:88
              .= p2`2/p1`1;
A34:        p1`2/p1`1=1/p2`2*p1`1 by A28,A31,XCMPLX_1:88
              .= p1`1/p2`2;
            then
A35:        (p2`1/p2`2)* (p1`2/p1`1)=1 by A29,A31,A33,XCMPLX_1:113;
A36:        (p2`1/p2`2)* (p1`2/p1`1)*p1`1=1 *p1`1 by A29,A31,A33,A34,
XCMPLX_1:113;
            then (p2`1/p2`2)* ((p1`2/p1`1)*p1`1)=p1`1;
            then
A37:        (p2`1/p2`2)*p1`2=p1`1 by A31,XCMPLX_1:88;
A38:        p2`1<>0 & p1`2<>0 by A31,A36;
            then
A39:        (p2`1/p2`2)=p1`1/p1`2 by A37,XCMPLX_1:90;
            consider p9 being Point of (TOP-REAL 2) such that
A40:        p2=p9 & ( (p9`1<=p9`2 & -p9`2<=p9`1 or p9`1>=p9`2 & p9`1<=-p9`2)
            & p9<>0.TOP-REAL 2) by A26;
A41:        now per cases by A40;
              case
A42:            p2`1<=p2`2 & -p2`2<=p2`1;
                then p2`2>=0;
                then p2`1/p2`2<=p2`2/p2`2 by A42,XREAL_1:74;
                hence p2`1/p2`2<=1 by A29,XCMPLX_1:60;
              end;
              case
A43:            p2`1>=p2`2 & p2`1<=-p2`2;
                p2`2 <= 0 by A43;
                then p2`1/p2`2<=p2`2/p2`2 by A43,XREAL_1:75;
                hence p2`1/p2`2<=1 by A29,XCMPLX_1:60;
              end;
            end;
A45:        now per cases by A16;
              case
A46:            p1`2<=p1`1 & -p1`1<=p1`2;
                then p1`1>=0;
                then p1`2/p1`1<=p1`1/p1`1 by A46,XREAL_1:74;
                hence p1`2/p1`1<=1 by A31,XCMPLX_1:60;
              end;
              case
A47:            p1`2>=p1`1 & p1`2<=-p1`1;
                p1`1 <= 0 by A47;
                then p1`2/p1`1<=p1`1/p1`1 by A47,XREAL_1:75;
                hence p1`2/p1`1<=1 by A31,XCMPLX_1:60;
              end;
            end;
A49:        now per cases by A40;
              case
A50:            p2`1<=p2`2 & -p2`2<=p2`1;
                then p2`2>=0;
                then (-p2`2)/p2`2<=p2`1/p2`2 by A50,XREAL_1:74;
                hence -1<=p2`1/p2`2 by A29,XCMPLX_1:198;
              end;
              case
A51:            p2`1>=p2`2 & p2`1<=-p2`2;
                then
A52:            -p2`1>=--p2`2 by XREAL_1:26;
                p2`2 <= 0 by A51;
                then (-p2`1)/(-p2`2)>=p2`2/(-p2`2) by A52,XREAL_1:74;
                then (-p2`1)/(-p2`2)>= -1 by A29,XCMPLX_1:199;
                hence -1<=p2`1/p2`2 by XCMPLX_1:192;
              end;
            end;
A54:        now per cases by A16;
              case
A55:            p1`2<=p1`1 & -p1`1<=p1`2;
                then p1`1>=0;
                then (-p1`1)/p1`1<=p1`2/p1`1 by A55,XREAL_1:74;
                hence -1<=p1`2/p1`1 by A31,XCMPLX_1:198;
              end;
              case
A56:            p1`2>=p1`1 & p1`2<=-p1`1;
                then
A57:            -p1`2>=--p1`1 by XREAL_1:26;
                p1`1 <= 0 by A56;
                then (-p1`2)/(-p1`1)>=p1`1/(-p1`1) by A57,XREAL_1:74;
                then (-p1`2)/(-p1`1)>= -1 by A31,XCMPLX_1:199;
                hence -1<=p1`2/p1`1 by XCMPLX_1:192;
              end;
            end;
            now per cases;
              case
A59:            0<=p2`1/p2`2;

then
B60: p1`2>0 & p1`1>=0 or p1`2<0 & p1`1<=0 by A31,A37;
                now
                  assume p1`2/p1`1<>1;
                  then p1`2/p1`1<1 by A45,XXREAL_0:1;
                  hence contradiction by A35,A41,A59,B60,XREAL_1:164;
                end;
                then p1`2=(1)*p1`1 by A31,XCMPLX_1:88;
                then (p2`1/p2`2)*p2`2=(1)*p2`2 by A31,A39,XCMPLX_1:60
                  .=p2`2;
                then p2`1=p2`2 by A29,XCMPLX_1:88;
                hence contradiction by A26,A40;
              end;
              case
A61:            0>p2`1/p2`2;
                then
B62:              p1`2<0 & p1`1>0 or p1`2>0 & p1`1<0 by A39,XREAL_1:145;
                now
                  assume p1`2/p1`1<>-1;
                  then -1<p1`2/p1`1 by A54,XXREAL_0:1;
                  hence contradiction by A35,A49,A61,B62,XREAL_1:168;
                end;
                then p1`2=(-1)*p1`1 by A31,XCMPLX_1:88
                  .= -p1`1;
                then -p1`2 =p1`1;
                then p2`1/p2`2=-1 by A38,A39,XCMPLX_1:198;
                then p2`1=(-1)*p2`2 by A29,XCMPLX_1:88;
                then -p2`1=p2`2;
                hence contradiction by A26,A40;
              end;
            end;
            hence contradiction;
          end;
        end;
        hence thesis;
      end;
      case x1 in K1a;
        then consider p7 being Point of TOP-REAL 2 such that
A63:    p1=p7 & ( (p7`1<=p7`2 & -p7`2<=p7`1 or p7`1>=p7`2 & p7`1<=-p7`2)
        & p7<>0.TOP-REAL 2);
A64:    Out_In_Sq.p1=|[p1`1/p1`2/p1`2,1/p1`2]| by A63,Th24;
        now per cases by A12,A15,XBOOLE_0:def 3;
          case x2 in K1a;
            then consider p8 being Point of (TOP-REAL 2) such that
A65:        p2=p8 & ( (p8`1<=p8`2 & -p8`2<=p8`1 or p8`1>=p8`2 & p8`1<=-p8`2)
            & p8<>0.TOP-REAL 2);
A66:        |[p2`1/p2`2/p2`2,1/p2`2]|
            =|[p1`1/p1`2/p1`2,1/p1`2]| by A7,A64,A65,Th24;
            set qq=|[p2`1/p2`2/p2`2,1/p2`2]|;
            qq`2=1/p2`2 & qq`1=p2`1/p2`2/p2`2 by EUCLID:56;
            then
A67:        1/p1`2= 1/p2`2 & p1`1/p1`2/p1`2 = p2`1/p2`2/p2`2 by A66,EUCLID:56;
A68:        (1/p1`2)"=(p1`2)""
              .=p1`2;
A70:        now
              assume
A71:          p1`2=0;
              then p1`1=0 by A63;
              hence contradiction by A63,A71,EUCLID:57,58;
            end;
            then p1`1/p1`2= p2`1/p1`2 by A67,A68,XCMPLX_1:53;
            then
A72:        p1`1=p2`1 by A70,XCMPLX_1:53;
            p1=|[p1`1,p1`2]| by EUCLID:57;
            hence thesis by A67,A68,A72,EUCLID:57;
          end;
          case
A73:        x2 in K0a & not x2 in K1a;
            then consider p8 being Point of (TOP-REAL 2) such that
A74:        p2=p8 & ( (p8`2<=p8`1 & -p8`1<=p8`2 or p8`2>=p8`1 & p8`2<=-p8`1)
            & p8<>0.TOP-REAL 2);
            Out_In_Sq.p2=|[1/p2`1,p2`2/p2`1/p2`1]| by A74,Def1;
            then
A75:        1/p1`2=p2`2/p2`1/p2`1 & p1`1/p1`2/p1`2=1/p2`1 by A7,A64,SPPOL_2:1;
A76:        now
              assume
A77:          p2`1=0;
              then p2`2=0 by A74;
              hence contradiction by A74,A77,EUCLID:57,58;
            end;
A78:        now
              assume
A79:          p1`2=0;
              then p1`1=0 by A63;
              hence contradiction by A63,A79,EUCLID:57,58;
            end;
A80:        p2`2/p2`1=1/p1`2*p2`1 by A75,A76,XCMPLX_1:88
              .= p2`1/p1`2;
            p1`1/p1`2=1/p2`1*p1`2 by A75,A78,XCMPLX_1:88
              .= p1`2/p2`1;
            then
A81:        (p2`2/p2`1)* (p1`1/p1`2)=1 by A76,A78,A80,XCMPLX_1:113;
            then (p2`2/p2`1)* (p1`1/p1`2)*p1`2=p1`2;
            then (p2`2/p2`1)* ((p1`1/p1`2)*p1`2)=p1`2;
            then
A82:        (p2`2/p2`1)*p1`1=p1`2 by A78,XCMPLX_1:88;
A83:        p2`2<>0 & p1`1<>0 by A81;
            then
A84:        (p2`2/p2`1)=p1`2/p1`1 by A82,XCMPLX_1:90;
            consider p9 being Point of (TOP-REAL 2) such that
A85:        p2=p9 & ( (p9`2<=p9`1 & -p9`1<=p9`2 or p9`2>=p9`1 & p9`2<=-p9`1)
            & p9<>0.TOP-REAL 2) by A73;
A86:        now per cases by A85;
              case
A87:            p2`2<=p2`1 & -p2`1<=p2`2;
                then p2`1>=0;
                then p2`2/p2`1<=p2`1/p2`1 by A87,XREAL_1:74;
                hence p2`2/p2`1<=1 by A76,XCMPLX_1:60;
              end;
              case
A88:            p2`2>=p2`1 & p2`2<=-p2`1;
                p2`1 <= 0 by A88;
                then p2`2/p2`1<=p2`1/p2`1 by A88,XREAL_1:75;
                hence p2`2/p2`1<=1 by A76,XCMPLX_1:60;
              end;
            end;
A90:        now per cases by A63;
              case
A91:            p1`1<=p1`2 & -p1`2<=p1`1;
                then p1`2>=0;
                then p1`1/p1`2<=p1`2/p1`2 by A91,XREAL_1:74;
                hence p1`1/p1`2<=1 by A78,XCMPLX_1:60;
              end;
              case
A92:            p1`1>=p1`2 & p1`1<=-p1`2;
                p1`2 <= 0 by A92;
                then p1`1/p1`2<=p1`2/p1`2 by A92,XREAL_1:75;
                hence p1`1/p1`2<=1 by A78,XCMPLX_1:60;
              end;
            end;
A94:        now per cases by A85;
              case
A95:            p2`2<=p2`1 & -p2`1<=p2`2;
                then p2`1>=0;
                then (-p2`1)/p2`1<=p2`2/p2`1 by A95,XREAL_1:74;
                hence -1<=p2`2/p2`1 by A76,XCMPLX_1:198;
              end;
              case
A96:            p2`2>=p2`1 & p2`2<=-p2`1;
                then
A97:            -p2`2>=--p2`1 by XREAL_1:26;
                p2`1 <= 0 by A96;
                then (-p2`2)/(-p2`1)>=p2`1/(-p2`1)
                 by A97,XREAL_1:74;
                then (-p2`2)/(-p2`1)>= -1 by A76,XCMPLX_1:199;
                hence -1<=p2`2/p2`1 by XCMPLX_1:192;
              end;
            end;
A99:        now per cases by A63;
              case
A100:           p1`1<=p1`2 & -p1`2<=p1`1;
                then p1`2>=0;
                then (-p1`2)/p1`2<=p1`1/p1`2 by A100,XREAL_1:74;
                hence -1<=p1`1/p1`2 by A78,XCMPLX_1:198;
              end;
              case
A101:           p1`1>=p1`2 & p1`1<=-p1`2;
                then
A102:           -p1`1>=--p1`2 by XREAL_1:26;
                p1`2 <= 0 by A101;
                then (-p1`1)/(-p1`2)>=p1`2/(-p1`2) by A102,XREAL_1:74;
                then (-p1`1)/(-p1`2)>= -1 by A78,XCMPLX_1:199;
                hence -1<=p1`1/p1`2 by XCMPLX_1:192;
              end;
            end;
            now per cases;
              case
A104:           0<=p2`2/p2`1;

then
B105: p1`1>0 & p1`2>=0 or p1`1<0 & p1`2<=0 by A83,A81;
                now
                  assume p1`1/p1`2<>1;
                  then p1`1/p1`2<1 by A90,XXREAL_0:1;
                  hence contradiction by A81,A86,A104,B105,XREAL_1:164;
                end;
                then p1`1=(1)*p1`2 by A78,XCMPLX_1:88;
                then (p2`2/p2`1)*p2`1 =(1)*p2`1 by A78,A84,XCMPLX_1:60
                  .=p2`1;
                then p2`2=p2`1 by A76,XCMPLX_1:88;
                hence contradiction by A73,A85;
              end;
              case
A106:           0>p2`2/p2`1;
                then
B107:             p1`1<0 & p1`2>0 or p1`1>0 & p1`2<0 by A84,XREAL_1:145;
                now
                  assume p1`1/p1`2<>-1;
                  then -1<p1`1/p1`2 by A99,XXREAL_0:1;
                  hence contradiction by A81,A94,A106,B107,XREAL_1:168;
                end;
                then p1`1=(-1)*p1`2 by A78,XCMPLX_1:88
                  .= -p1`2;
                then -p1`1 =p1`2;
                then p2`2/p2`1=-1 by A83,A84,XCMPLX_1:198;
                then p2`2=(-1)*p2`1 by A76,XCMPLX_1:88;
                then -p2`2=p2`1;
                hence contradiction by A73,A85;
              end;
            end;
            hence contradiction;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  then
A108: Out_In_Sq is one-to-one by FUNCT_1:def 8;
A109: for t being Point of TOP-REAL 2 st t in K0 & t<>0.TOP-REAL 2 holds
  not Out_In_Sq.t in K0 \/ Kb
  proof
    let t be Point of TOP-REAL 2;
    assume
A110: t in K0 & t<>0.TOP-REAL 2;
    then consider p3 being Point of TOP-REAL 2 such that
A111: p3=t & (-1<p3`1 & p3`1<1 & -1<p3`2 & p3`2<1) by A1;
    now
      assume
A112: Out_In_Sq.t in K0 \/ Kb;
      now per cases by A112,XBOOLE_0:def 3;
        case Out_In_Sq.t in K0;
          then consider p4 being Point of TOP-REAL 2 such that
A113:     p4=Out_In_Sq.t & (-1<p4`1 & p4`1<1 & -1<p4`2 & p4`2<1) by A1;
          now per cases;
            case
A114:         (t`2<=t`1 & -t`1<=t`2 or t`2>=t`1 & t`2<=-t`1);
              then Out_In_Sq.t=|[1/t`1,t`2/t`1/t`1]| by A110,Def1;
              then
A115:         p4`1=1/t`1 & p4`2=t`2/t`1/t`1 by A113,EUCLID:56;
              now per cases;
                case
A116:             t`1>=0;
                  now per cases by A116;
                    case
A117:                 t`1>0;
                      then 1/t`1*t`1<1 *t`1 by A113,A115,XREAL_1:70;
                      hence contradiction by A111,A117,XCMPLX_1:88;
                    end;
                    case
A118:                 t`1=0;
                      then t`2=0 by A114;
                      hence contradiction by A110,A118,EUCLID:57,58;
                    end;
                  end;
                  hence contradiction;
                end;
                case
A119:             t`1<0;
                  then (-1)*t`1>1/t`1*t`1 by A113,A115,XREAL_1:71;
                  then (-1)*t`1>1 by A119,XCMPLX_1:88;
                  then --t`1<=-1 by XREAL_1:26;
                  hence contradiction by A111;
                end;
              end;
              hence contradiction;
            end;
            case
A120:         not(t`2<=t`1 & -t`1<=t`2 or t`2>=t`1 & t`2<=-t`1);
              then Out_In_Sq.t=|[t`1/t`2/t`2,1/t`2]| by A110,Def1;
              then
A121:         p4`2=1/t`2 & p4`1=t`1/t`2/t`2 by A113,EUCLID:56;
              now per cases;
                case
A124:             t`2>=0;
                  now per cases by A124;
                    case
A125:                 t`2>0;
                      then 1/t`2*t`2<1 *t`2 by A113,A121,XREAL_1:70;
                      hence contradiction by A111,A125,XCMPLX_1:88;
                    end;
                    case t`2=0;
                      hence contradiction by A120;
                    end;
                  end;
                  hence contradiction;
                end;
                case
A126:             t`2<0;
                  then (-1)*t`2>1/t`2*t`2 by A113,A121,XREAL_1:71;
                  then (-1)*t`2>1 by A126,XCMPLX_1:88;
                  then --t`2<=-1 by XREAL_1:26;
                  hence contradiction by A111;
                end;
              end;
              hence contradiction;
            end;
          end;
          hence contradiction;
        end;
        case Out_In_Sq.t in Kb;
          then consider p4 being Point of TOP-REAL 2 such that
A127:     p4=Out_In_Sq.t &
          (-1=p4`1 & -1<=p4`2 & p4`2<=1 or p4`1=1 & -1<=p4`2 & p4`2<=1
          or -1=p4`2 & -1<=p4`1 & p4`1<=1
          or 1=p4`2 & -1<=p4`1 & p4`1<=1) by A1;
          now per cases;
            case
A128:         (t`2<=t`1 & -t`1<=t`2 or t`2>=t`1 & t`2<=-t`1);
              then
A129:         Out_In_Sq.t=|[1/t`1,t`2/t`1/t`1]| by A110,Def1;
              then
A130:         p4`1=1/t`1 & p4`2=t`2/t`1/t`1 by A127,EUCLID:56;
              now per cases by A127;
                case -1=p4`1 & -1<=p4`2 & p4`2<=1;
                  then 1 *(t`1)"=-1 by A130;
                  then
A131:             (t`1)*(t`1)" =-t`1;
                  now per cases;
                    case t`1<>0;
                      then -t`1=1 by A131,XCMPLX_0:def 7;
                      hence contradiction by A111;
                    end;
                    case
A132:                 t`1=0;
                      then t`2=0 by A128;
                      hence contradiction by A110,A132,EUCLID:57,58;
                    end;
                  end;
                  hence contradiction;
                end;
                case p4`1=1 & -1<=p4`2 & p4`2<=1;
                  then 1 *(t`1)"=1 by A130;
                  then
A133:             (t`1)*(t`1)"=t`1;
                  now per cases;
                    case t`1<>0;
                      hence contradiction by A111,A133,XCMPLX_0:def 7;
                    end;
                    case
A134:                 t`1=0;
                      then t`2=0 by A128;
                      hence contradiction by A110,A134,EUCLID:57,58;
                    end;
                  end;
                  hence contradiction;
                end;
                case
A135:             -1=p4`2 & -1<=p4`1 & p4`1<=1;
                  reconsider K01=K0a as non empty Subset of (TOP-REAL 2)|D
                  by A3,Th27;
A136:             rng (Out_In_Sq|K01) c= the carrier of ((TOP-REAL 2)|D)|K01
                  by Th25;
A137:             dom (Out_In_Sq|K01)=(dom Out_In_Sq) /\ K01 by RELAT_1:90
                    .=D /\ K01 by A2,FUNCT_2:def 1
                    .=[#]((TOP-REAL 2)|D) /\ K01 by PRE_TOPC:def 10
                    .=(the carrier of ((TOP-REAL 2)|D)) /\ K01
                    .=K01 by XBOOLE_1:28;
                  t in K01 by A110,A128;

then (Out_In_Sq|K01).t in rng (Out_In_Sq|K01) by A137,FUNCT_1:12;
                  then
A138:             (Out_In_Sq|K01).t in the carrier of (( TOP-REAL 2)|D)|K01
                  by A136;
A139:           the carrier of ((TOP-REAL 2)|D)|K01=[#](((TOP-REAL 2)|D)|K01)
                    .=K01 by PRE_TOPC:def 10;
                  t in K01 by A110,A128;
                  then Out_In_Sq.t in K0a by A138,A139,FUNCT_1:72;
                  then consider p5 being Point of TOP-REAL 2 such that
A140:             p5=p4 &
                  (p5`2<=p5`1 & -p5`1<=p5`2 or p5`2>=p5`1 & p5`2<=-p5`1)
                  & p5<>0.TOP-REAL 2 by A127;
                  now per cases by A135,A140,XREAL_1:26;
                    case p4`1>=1;
                      then
A141:                 1/t`1=1 by A130,A135,XXREAL_0:1;
                      then t`2/t`1/t`1=(t`2/t`1)*1
                        .=t`2*1 by A141
                        .=t`2;
                      hence contradiction by A111,A127,A129,A135,EUCLID:56;
                    end;
                    case -1>=p4`1;
                      then
A142:                 1/t`1=-1 by A130,A135,XXREAL_0:1;
                      then t`2/t`1/t`1=(t`2/t`1)*(-1)
                        .=-(t`2/t`1)
                        .=-(t`2*(-1)) by A142
                        .=t`2;
                      hence contradiction by A111,A127,A129,A135,EUCLID:56;
                    end;
                  end;
                  hence contradiction;
                end;
                case
A143:             1=p4`2 & -1<=p4`1 & p4`1<=1;
                  reconsider K01=K0a as non empty Subset of (TOP-REAL 2)|D
                  by A3,Th27;
A144:             rng (Out_In_Sq|K01) c= the carrier of ((TOP-REAL 2)|D)|K01
                  by Th25;
                  dom (Out_In_Sq|K01)=(dom Out_In_Sq) /\ K01 by RELAT_1:90
                    .=D /\ K01 by A2,FUNCT_2:def 1
                    .=[#]((TOP-REAL 2)|D) /\ K01 by PRE_TOPC:def 10
                    .=(the carrier of ((TOP-REAL 2)|D)) /\ K01
                    .=K01 by XBOOLE_1:28;
                  then t in dom (Out_In_Sq|K01) by A110,A128;
                  then (Out_In_Sq|K01).t in rng (Out_In_Sq|K01) by FUNCT_1:12;
                  then
A145:             (Out_In_Sq|K01).t in the carrier of (( TOP-REAL 2)|D)|K01
                  by A144;
A146:             the carrier of ((TOP-REAL 2)|D)|K01=[#](((TOP-REAL 2)|D)|K01)
                    .=K01 by PRE_TOPC:def 10;
                  t in K01 by A110,A128;
                  then Out_In_Sq.t=(Out_In_Sq|K01).t by FUNCT_1:72;
                  then consider p5 being Point of TOP-REAL 2 such that
A147:             p5=p4 &
                  (p5`2<=p5`1 & -p5`1<=p5`2 or p5`2>=p5`1 & p5`2<=-p5`1)
                  & p5<>0.TOP-REAL 2 by A127,A145,A146;
                  now per cases by A143,A147,XREAL_1:27;
                    case p4`1>=1;
                      then
A148:                 1/t`1=1 by A130,A143,XXREAL_0:1;
                      then t`2/t`1/t`1=(t`2/t`1)*1
                        .=t`2*1 by A148
                        .=t`2;
                      hence contradiction by A111,A127,A129,A143,EUCLID:56;
                    end;
                    case -1>=p4`1;
                      then
A149:                 1/t`1=-1 by A130,A143,XXREAL_0:1;
                      then t`2/t`1/t`1=(t`2/t`1)*(-1)
                        .=-(t`2/t`1)
                        .=-(t`2*(-1)) by A149
                        .=t`2;
                      hence contradiction by A111,A127,A129,A143,EUCLID:56;
                    end;
                  end;
                  hence contradiction;
                end;
              end;
              hence contradiction;
            end;
            case
A150:         not(t`2<=t`1 & -t`1<=t`2 or t`2>=t`1 & t`2<=-t`1);
              then
A151:         Out_In_Sq.t=|[t`1/t`2/t`2,1/t`2]| by A110,Def1;
              then
A152:         p4`2=1/t`2 & p4`1=t`1/t`2/t`2 by A127,EUCLID:56;
              now per cases by A127;
                case -1=p4`2 & -1<=p4`1 & p4`1<=1;
                  then 1 *(t`2)"=-1 by A152;
                  then
A155:             (t`2)*(t`2)"=-t`2;
                  now per cases;
                    case t`2<>0;
                      then -t`2=1 by A155,XCMPLX_0:def 7;
                      hence contradiction by A111;
                    end;
                    case t`2=0;
                      hence contradiction by A150;
                    end;
                  end;
                  hence contradiction;
                end;
                case p4`2=1 & -1<=p4`1 & p4`1<=1;
                  then 1 *(t`2)"=1 by A152;
                  then
A156:             (t`2)*(t`2)" =t`2;
                  now per cases;
                    case t`2<>0;
                      hence contradiction by A111,A156,XCMPLX_0:def 7;
                    end;
                    case t`2=0;
                      hence contradiction by A150;
                    end;
                  end;
                  hence contradiction;
                end;
                case
A157:             -1=p4`1 & -1<=p4`2 & p4`2<=1;
A158:             (t`1<=t`2 & -t`2<=t`1 or t`1>=t`2 & t`1<=-t`2) by A150,Th23;
                  reconsider K11=K1a as non empty Subset of (TOP-REAL 2)|D
                  by A3,Th28;
A159:             rng (Out_In_Sq|K11) c= the carrier of ((TOP-REAL 2)|D)|K11
                  by Th26;
A160:             dom (Out_In_Sq|K11)=(dom Out_In_Sq) /\ K11 by RELAT_1:90
                    .=D /\ K11 by A2,FUNCT_2:def 1
                    .=[#]((TOP-REAL 2)|D) /\ K11 by PRE_TOPC:def 10
                    .=(the carrier of ((TOP-REAL 2)|D)) /\ K11
                    .=K11 by XBOOLE_1:28;
                  t in K11 by A110,A158;

then (Out_In_Sq|K11).t in rng (Out_In_Sq|K11) by A160,FUNCT_1:12;
                  then
A161:             (Out_In_Sq|K11).t in the carrier of (( TOP-REAL 2)|D)|K11
                  by A159;
A162:             the carrier of ((TOP-REAL 2)|D)|K11=[#](((TOP-REAL 2)|D)|K11)
                    .=K11 by PRE_TOPC:def 10;
                  t in K11 by A110,A158;
                  then Out_In_Sq.t=(Out_In_Sq|K11).t by FUNCT_1:72;
                  then consider p5 being Point of TOP-REAL 2 such that
A163:             p5=p4 &
                  (p5`1<=p5`2 & -p5`2<=p5`1 or p5`1>=p5`2 & p5`1<=-p5`2)
                  & p5<>0.TOP-REAL 2 by A127,A161,A162;
                  now per cases by A157,A163,XREAL_1:26;
                    case p4`2>=1;
                      then
A164:                 1/t`2=1 by A152,A157,XXREAL_0:1;
                      then t`1/t`2/t`2=(t`1/t`2)*1
                        .=t`1*1 by A164
                        .=t`1;
                      hence contradiction by A111,A127,A151,A157,EUCLID:56;
                    end;
                    case -1>=p4`2;
                      then
A165:                 1/t`2=-1 by A152,A157,XXREAL_0:1;
                      then t`1/t`2/t`2=(t`1/t`2)*(-1)
                        .=-(t`1/t`2)
                        .=-(t`1*(-1)) by A165
                        .=t`1;
                      hence contradiction by A111,A127,A151,A157,EUCLID:56;
                    end;
                  end;
                  hence contradiction;
                end;
                case
A166:             1=p4`1 & -1<=p4`2 & p4`2<=1;
A167:             (t`1<=t`2 & -t`2<=t`1 or t`1>=t`2 & t`1<=-t`2) by A150,Th23;
                  reconsider K11=K1a as non empty Subset of (TOP-REAL 2)|D
                  by A3,Th28;
A168:             rng (Out_In_Sq|K11) c= the carrier of ((TOP-REAL 2)|D)|K11
                  by Th26;
A169:             dom (Out_In_Sq|K11)=(dom Out_In_Sq) /\ K11 by RELAT_1:90
                    .=D /\ K11 by A2,FUNCT_2:def 1
                    .=[#]((TOP-REAL 2)|D) /\ K11 by PRE_TOPC:def 10
                    .=(the carrier of ((TOP-REAL 2)|D)) /\ K11
                    .=K11 by XBOOLE_1:28;
                  t in K11 by A110,A167;

then (Out_In_Sq|K11).t in rng (Out_In_Sq|K11) by A169,FUNCT_1:12;
                  then
A170:             (Out_In_Sq|K11).t in the carrier of (( TOP-REAL 2)|D)|K11
                  by A168;
A171:            the carrier of ((TOP-REAL 2)|D)|K11=[#](((TOP-REAL 2)|D)|K11)
                    .=K11 by PRE_TOPC:def 10;
                  t in K11 by A110,A167;
                  then Out_In_Sq.t in K1a by A170,A171,FUNCT_1:72;
                  then consider p5 being Point of TOP-REAL 2 such that
A172:             p5=p4 &
                  (p5`1<=p5`2 & -p5`2<=p5`1 or p5`1>=p5`2 & p5`1<=-p5`2)
                  & p5<>0.TOP-REAL 2 by A127;
                  now per cases by A166,A172,XREAL_1:27;
                    case p4`2>=1;
                      then
A173:                 1/t`2=1 by A152,A166,XXREAL_0:1;
                      then t`1/t`2/t`2=(t`1/t`2)*1
                        .=t`1*1 by A173
                        .=t`1;
                      hence contradiction by A111,A127,A151,A166,EUCLID:56;
                    end;
                    case -1>=p4`2;
                      then
A174:                 1/t`2=-1 by A152,A166,XXREAL_0:1;
                      then t`1/t`2/t`2=(t`1/t`2)*(-1)
                        .=-(t`1/t`2)
                        .=-(t`1*(-1)) by A174
                        .=t`1;
                      hence contradiction by A111,A127,A151,A166,EUCLID:56;
                    end;
                  end;
                  hence contradiction;
                end;
              end;
              hence contradiction;
            end;
          end;
          hence contradiction;
        end;
      end;
      hence contradiction;
    end;
    hence thesis;
  end;
A175: for t being Point of TOP-REAL 2 st not t in K0 \/ Kb holds
  Out_In_Sq.t in K0
  proof
    let t be Point of TOP-REAL 2;
    assume not t in K0 \/ Kb;
    then
A176: not t in K0 & not t in Kb by XBOOLE_0:def 3;
    then
A177: not t=0.TOP-REAL 2 by A1,Th11;
    then not t in {0.TOP-REAL 2} by TARSKI:def 1;
    then t in NonZero TOP-REAL 2 by XBOOLE_0:def 5;
    then Out_In_Sq.t in NonZero TOP-REAL 2
    by FUNCT_2:7;
    then reconsider p4=Out_In_Sq.t as Point of TOP-REAL 2;
    now per cases;
      case
A178:   (t`2<=t`1 & -t`1<=t`2 or t`2>=t`1 & t`2<=-t`1);
        then Out_In_Sq.t=|[1/t`1,t`2/t`1/t`1]| by A177,Def1;
        then
A179:   p4`1=1/t`1 & p4`2=t`2/t`1/t`1 by EUCLID:56;
        now per cases;
          case
A180:       t`1>0;
            now per cases;
              case
A182:           t`2>0;
                -1>=t`1 or t`1>=1 or -1>=t`2 or t`2>=1 by A1,A176;
                then
A184:           t`1>=1 by A178,A182,A180,XXREAL_0:2;
                not t`1=1 by A1,A176,A178;
                then
A185:           t`1>1 by A184,XXREAL_0:1;
                then
A186:           t`1/t`1>1/t`1 by XREAL_1:76;
A187:           0<t`2/t`1 by A180,A182,XREAL_1:141;
A188:           (-1)*t`1/t`1< t`2/t`1/t`1 by A180,A187,XREAL_1:76;
                t`1<(t`1)^2 by A185,SQUARE_1:76;
                then (t`2)<(t`1)^2 by A178,A180,XXREAL_0:2;
                then t`2/t`1<(t`1)^2/t`1 by A180,XREAL_1:76;
                then t`2/t`1<(t`1) by A180,XCMPLX_1:90;
                then t`2/t`1/t`1<(t`1)/t`1 by A180,XREAL_1:76;
                hence -1<1/t`1 & 1/t`1<1 & -1< t`2/t`1/t`1 & t`2/t`1/t`1<1
                by A180,A186,A188,XCMPLX_1:60,90;
              end;
              case
A189:           t`2<=0;
A190:           --t`1>=-t`2 by A178,A180,XREAL_1:26;
A191:           now
                  assume t`1<1;
                  then -1>=t`2 by A1,A176,A180,A189;
                  then -t`1<=-1 by A178,A180,XXREAL_0:2;
                  hence t`1>=1 by XREAL_1:26;
                end;
                not t`1=1 by A1,A176,A178;
                then
A192:           t`1>1 by A191,XXREAL_0:1;
                then
A193:           t`1/t`1>1/t`1 by XREAL_1:76;
                t`1<(t`1)^2 by A192,SQUARE_1:76;
                then (t`1)^2 >-t`2 by A190,XXREAL_0:2;
                then (t`1)^2/t`1 >(-t`2)/t`1 by A180,XREAL_1:76;
                then t`1> (-t`2)/t`1 by A180,XCMPLX_1:90;
                then t`1>-(t`2/t`1);
                then -t`1<--(t`2/t`1) by XREAL_1:26;
                then (-1)*t`1/t`1< t`2/t`1/t`1 by A180,XREAL_1:76;
                hence -1<1/t`1 & 1/t`1<1 & -1< t`2/t`1/t`1 & t`2/t`1/t`1<1
                by A193,A180,A189,XCMPLX_1:60,90;
              end;
            end;
            hence -1<1/t`1 & 1/t`1<1 & -1< t`2/t`1/t`1 & t`2/t`1/t`1<1;
          end;
          case
A195:       t`1<=0;
            now per cases by A195;
              case
A196:           t`1=0;
                then t`2=0 by A178;
                hence contradiction by A1,A176,A196;
              end;
              case
A197:           t`1<0;
                now per cases;
                  case
A199:               t`2>0;
                    -1>=t`1 or t`1>=1 or -1>=t`2 or t`2>=1 by A1,A176;
                    then t`1<=-1 or 1<=-t`1 by A178,A197,XXREAL_0:2;
                    then
A200:               t`1<=-1 or -1>=--t`1 by XREAL_1:26;
                    not t`1=-1 by A1,A176,A178;
                    then
A201:               t`1<-1 by A200,XXREAL_0:1;
                    then t`1/t`1>(-1)/t`1 by XREAL_1:77;
                    then -(t`1/t`1)<-((-1)/t`1) by XREAL_1:26;
                    then
A202:               -(t`1/t`1)<1/t`1;
A203:               0>t`2/t`1 by A197,A199,XREAL_1:144;
A204:               (-1)*t`1/t`1< t`2/t`1/t`1 by A197,A203,XREAL_1:77;
                    -t`1<(t`1)^2 by A201,SQUARE_1:116;
                    then (t`2)<(t`1)^2 by A178,A197,XXREAL_0:2;
                    then t`2/t`1>(t`1)^2/t`1 by A197,XREAL_1:77;
                    then t`2/t`1>(t`1) by A197,XCMPLX_1:90;
                    then t`2/t`1/t`1<(t`1)/t`1 by A197,XREAL_1:77;
                    hence -1<1/t`1 & 1/t`1<1 & -1< t`2/t`1/t`1 & t`2/t`1/t`1<1
                    by A197,A202,A204,XCMPLX_1:60;
                  end;
                  case
A205:               t`2<=0;
                    then -1>=t`1 or -1>=t`2 by A1,A176,A197;
                    then
A206:               t`1<=-1 by A178,A197,XXREAL_0:2;
                    not t`1=-1 by A1,A176,A178;
                    then
A207:               t`1< -1 by A206,XXREAL_0:1;
                    then t`1/t`1> (-1)/t`1 by XREAL_1:77;
                    then 1> (-1)/t`1 by A197,XCMPLX_1:60;
                    then
A208:               -1<-(-1)/t`1 by XREAL_1:26;
A209:               -t`1>=-t`2 by A178,A197,XREAL_1:26;
                    -t`1<(t`1)^2 by A207,SQUARE_1:116;
                    then (t`1)^2 >-t`2 by A209,XXREAL_0:2;
                    then (t`1)^2/t`1 <(-t`2)/t`1 by A197,XREAL_1:77;
                    then t`1< (-t`2)/t`1 by A197,XCMPLX_1:90;
                    then t`1<-(t`2/t`1);
                    then -t`1>--(t`2/t`1) by XREAL_1:26;
                    then
A210:               (-1)*t`1/t`1< t`2/t`1/t`1 by A197,XREAL_1:77;
                    t`2/t`1/t`1<(t`1)/t`1 by A197,A205,XREAL_1:77;
                    hence -1<1/t`1 & 1/t`1<1 & -1< t`2/t`1/t`1 & t`2/t`1/t`1<1
                    by A197,A208,A210,XCMPLX_1:60,90;
                  end;
                end;
                hence -1<1/t`1 & 1/t`1<1 & -1< t`2/t`1/t`1 & t`2/t`1/t`1<1;
              end;
            end;
            hence -1<1/t`1 & 1/t`1<1 & -1< t`2/t`1/t`1 & t`2/t`1/t`1<1;
          end;
        end;
        hence thesis by A1,A179;
      end;
      case
A211:   not(t`2<=t`1 & -t`1<=t`2 or t`2>=t`1 & t`2<=-t`1);
        then Out_In_Sq.t=|[t`1/t`2/t`2,1/t`2]| by A177,Def1;
        then
A212:   p4`2=1/t`2 & p4`1=t`1/t`2/t`2 by EUCLID:56;
A213:   t`1<=t`2 & -t`2<=t`1 or t`1>=t`2 & t`1<=-t`2 by A211,Th23;
        now per cases;
          case
A214:       t`2>0;
            now per cases;
              case
A216:           t`1>0;
A218:           -1>=t`2 or t`2>=1 or -1>=t`1 or t`1>=1 by A1,A176;
                not t`2=1 by A1,A176,A211,A216;
                then
A219:           t`2>1 by A211,A216,A214,A218,XXREAL_0:1,2;
                then
A220:           t`2/t`2>1/t`2 by XREAL_1:76;
A221:           0<t`1/t`2 by A214,A216,XREAL_1:141;
A222:           (-1)*t`2/t`2< t`1/t`2/t`2 by A214,A221,XREAL_1:76;
                t`2<(t`2)^2 by A219,SQUARE_1:76;
                then (t`1)<(t`2)^2 by A211,A214,XXREAL_0:2;
                then t`1/t`2<(t`2)^2/t`2 by A214,XREAL_1:76;
                then t`1/t`2<(t`2) by A214,XCMPLX_1:90;
                then t`1/t`2/t`2<(t`2)/t`2 by A214,XREAL_1:76;
                hence -1<1/t`2 & 1/t`2<1 & -1< t`1/t`2/t`2 & t`1/t`2/t`2<1
                by A214,A220,A222,XCMPLX_1:60,90;
              end;
              case
A223:           t`1<=0;
A224:           now
                  assume t`2<1;
                  then -1>=t`1 by A1,A176,A214,A223;
                  then -t`2<=-1 by A213,A214,XXREAL_0:2;
                  hence t`2>=1 by XREAL_1:26;
                end;
                not t`2=1 by A1,A176,A213;
                then
A225:           t`2>1 by A224,XXREAL_0:1;
                then
A226:           t`2/t`2>1/t`2 by XREAL_1:76;
                t`2<(t`2)^2 by A225,SQUARE_1:76;
                then (t`2)^2 >-t`1 by A211,A214,XXREAL_0:2;
                then (t`2)^2/t`2 >(-t`1)/t`2 by A214,XREAL_1:76;
                then t`2> (-t`1)/t`2 by A214,XCMPLX_1:90;
                then t`2>-(t`1/t`2);
                then -t`2<--(t`1/t`2) by XREAL_1:26;
                then
A227:           (-1)*t`2/t`2< t`1/t`2/t`2 by A214,XREAL_1:76;
                t`1/t`2/t`2<(t`2)/t`2 by A214,A223,XREAL_1:76;
                hence -1<1/t`2 & 1/t`2<1 & -1< t`1/t`2/t`2 & t`1/t`2/t`2<1
                by A214,A226,A227,XCMPLX_1:60,90;
              end;
            end;
            hence -1<1/t`2 & 1/t`2<1 & -1< t`1/t`2/t`2 & t`1/t`2/t`2<1;
          end;
          case
A228:       t`2<=0;
X:          t`2 <> 0 by A211;
            now
A230:           t`2<0 by A228,X;
A232:           t`1<=t`2 or t`1<=-t`2 by A211,Th23;
                now per cases;
                  case
A233:               t`1>0;
                    -1>=t`2 or t`2>=1 or -1>=t`1 or t`1>=1 by A1,A176;
                    then t`2<=-1 or 1<=-t`2 by A213,A230,XXREAL_0:2;
                    then
A234:               t`2<=-1 or -1>=--t`2 by XREAL_1:26;
                    not t`2=-1 by A1,A176,A213;
                    then
A235:               t`2<-1 by A234,XXREAL_0:1;
                    then t`2/t`2>(-1)/t`2 by XREAL_1:77;
                    then -(t`2/t`2)<-((-1)/t`2) by XREAL_1:26;
                    then
A236:               -(t`2/t`2)<1/t`2;
A237:               0>t`1/t`2 by A230,A233,XREAL_1:144;
A238:               (-1)*t`2/t`2< t`1/t`2/t`2 by A230,A237,XREAL_1:77;
                    -t`2<(t`2)^2 by A235,SQUARE_1:116;
                    then (t`1)<(t`2)^2 by A230,A232,XXREAL_0:2;
                    then t`1/t`2>(t`2)^2/t`2 by A230,XREAL_1:77;
                    then t`1/t`2>(t`2) by A230,XCMPLX_1:90;
                    then t`1/t`2/t`2<(t`2)/t`2 by A230,XREAL_1:77;
                    hence -1<1/t`2 & 1/t`2<1 & -1< t`1/t`2/t`2 & t`1/t`2/t`2<1
                    by A230,A236,A238,XCMPLX_1:60;
                  end;
                  case
A239:               t`1<=0;
                    then
A240:               -1>=t`2 or -1>=t`1 by A1,A176,A230;
                    not t`2=-1 by A1,A176,A213;
                    then
A241:               t`2< -1 by A211,A230,A240,XXREAL_0:1,2;
                    then t`2/t`2> (-1)/t`2 by XREAL_1:77;
                    then 1> (-1)/t`2 by A230,XCMPLX_1:60;
                    then
A242:               -1<-(-1)/t`2 by XREAL_1:26;
A243:               -t`2>=-t`1 by A211,A230,XREAL_1:26;
                    -t`2<(t`2)^2 by A241,SQUARE_1:116;
                    then (t`2)^2 >-t`1 by A243,XXREAL_0:2;
                    then (t`2)^2/t`2 <(-t`1)/t`2 by A230,XREAL_1:77;
                    then t`2< (-t`1)/t`2 by A230,XCMPLX_1:90;
                    then t`2<-(t`1/t`2);
                    then -t`2>--(t`1/t`2) by XREAL_1:26;
                    then
                    (-1)*t`2/t`2< t`1/t`2/t`2 by A230,XREAL_1:77;
                    hence -1<1/t`2 & 1/t`2<1 & -1< t`1/t`2/t`2 & t`1/t`2/t`2<1
                    by A242,A230,A239,XCMPLX_1:90;
                  end;
                end;
                hence -1<1/t`2 & 1/t`2<1 & -1< t`1/t`2/t`2 & t`1/t`2/t`2<1;
            end;
            hence -1<1/t`2 & 1/t`2<1 & -1< t`1/t`2/t`2 & t`1/t`2/t`2<1;
          end;
        end;
        hence thesis by A1,A212;
      end;
    end;
    hence thesis;
  end;
  for s being Point of TOP-REAL 2 st s in Kb holds Out_In_Sq.s=s
  proof
    let t be Point of TOP-REAL 2;
    assume t in Kb;
    then consider p4 being Point of TOP-REAL 2 such that
A245: p4=t & (-1=p4`1 & -1<=p4`2 & p4`2<=1 or p4`1=1 & -1<=p4`2 & p4`2<=1
    or -1=p4`2 & -1<=p4`1 & p4`1<=1 or 1=p4`2 & -1<=p4`1 & p4`1<=1) by A1;
A246: not t=0.TOP-REAL 2 by A245,EUCLID:56,58;
A247: t<>0.TOP-REAL 2 by A245,EUCLID:56,58;
    now per cases;
      case
A248:   (t`2<=t`1 & -t`1<=t`2 or t`2>=t`1 & t`2<=-t`1);
        then
A249:   Out_In_Sq.t=|[1/t`1,t`2/t`1/t`1]| by A246,Def1;
A250:   1<=t`1 & t`1>=-1 or 1>=t`1 & -1>=--t`1 by A245,A248,XREAL_1:26;
        now per cases by A245,A250,XXREAL_0:1;
          case t`1=1;
            hence thesis by A249,EUCLID:57;
          end;
          case
A251:       t`1=-1;
            thus thesis by A249,A251,EUCLID:57;
          end;
        end;
        hence thesis;
      end;
      case
A252:   not (t`2<=t`1 & -t`1<=t`2 or t`2>=t`1 & t`2<=-t`1);
        then
A253:   Out_In_Sq.t=|[t`1/t`2/t`2,1/t`2]| by A247,Def1;
        now per cases by A245,A252;
          case t`2=1;
            hence thesis by A253,EUCLID:57;
          end;
          case
A254:       t`2=-1;
            thus thesis by A253,A254,EUCLID:57;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A4,A108,A109,A175;
end;

theorem Th52:
  for f,g being Function of I[01],TOP-REAL 2, K0 being Subset of TOP-REAL 2,
  O,I being Point of I[01] st O=0 & I=1 & f is continuous one-to-one &
  g is continuous one-to-one & K0={p: -1<p`1 & p`1<1 & -1<p`2 & p`2<1}&
  (f.O)`1=-1 & (f.I)`1=1 &
  -1<=(f.O)`2 & (f.O)`2<=1 & -1<=(f.I)`2 & (f.I)`2<=1 &
  (g.O)`2=-1 & (g.I)`2=1 &
  -1<=(g.O)`1 & (g.O)`1<=1 & -1<=(g.I)`1 & (g.I)`1<=1 &
  rng f misses K0 & rng g misses K0 holds rng f meets rng g
proof
  let f,g be Function of I[01],TOP-REAL 2, K0 be Subset of TOP-REAL 2,
  O,I be Point of I[01];
  assume
A1: O=0 & I=1 & f is continuous & f is one-to-one &
  g is continuous & g is one-to-one & K0={p: -1<p`1 & p`1<1 & -1<p`2 & p`2<1}&
  (f.O)`1=-1 & (f.I)`1=1 &
  -1<=(f.O)`2 & (f.O)`2<=1 & -1<=(f.I)`2 & (f.I)`2<=1 &
  (g.O)`2=-1 & (g.I)`2=1 &
  -1<=(g.O)`1 & (g.O)`1<=1 & -1<=(g.I)`1 & (g.I)`1<=1 &
  rng f /\ K0={} & rng g /\ K0={};
  defpred P[Point of TOP-REAL 2] means
  -1=$1`1 & -1<=$1`2 & $1`2<=1 or $1`1=1 & -1<=$1`2 & $1`2<=1
  or -1=$1`2 & -1<=$1`1 & $1`1<=1 or 1=$1`2 & -1<=$1`1 & $1`1<=1;
  reconsider Kb={q: P[q]} as Subset of TOP-REAL 2 from TopSubset;
  reconsider B={0.TOP-REAL 2} as Subset of TOP-REAL 2;
  consider h being Function of (TOP-REAL 2)|B`,(TOP-REAL 2)|B` such that
A2: h is continuous & h is one-to-one & (for t being Point of TOP-REAL 2
  st t in K0 & t<>0.TOP-REAL 2 holds not h.t in K0 \/ Kb)
  &(for r being Point of TOP-REAL 2 st not r in K0 \/ Kb holds h.r in K0)
  &(for s being Point of TOP-REAL 2 st s in Kb holds h.s=s) by A1,Th51;
A3: dom f =the carrier of I[01] by FUNCT_2:def 1;
A4: dom g =the carrier of I[01] by FUNCT_2:def 1;
A5: B`<>{} by Th19;
  rng f c= B`
  proof
    let x be set;
    assume
A6: x in rng f;
    now
      assume x in B;
      then
A7:   x=0.TOP-REAL 2 by TARSKI:def 1;
      (0.TOP-REAL 2)`1=0 & (0.TOP-REAL 2)`2=0 by EUCLID:56,58;
      then 0.TOP-REAL 2 in K0 by A1;
      hence contradiction by A1,A6,A7,XBOOLE_0:def 4;
    end;
    then x in (the carrier of TOP-REAL 2)\ B by A6,XBOOLE_0:def 5;
    hence thesis by SUBSET_1:def 5;
  end;
  then consider w being Function of I[01],TOP-REAL 2 such that
A8: w is continuous & w=h*f by A1,A2,A5,Th22;
  reconsider d1=h*f as Function of I[01],TOP-REAL 2 by A8;
A9: the carrier of (TOP-REAL 2)|B` =[#]((TOP-REAL 2)|B`)
    .=B` by PRE_TOPC:def 10;
  rng f c=(the carrier of (TOP-REAL 2))\B
  proof
    let e be set;
    assume
A10: e in rng f;
    now
      assume e in B;
      then
A11:  e=0.TOP-REAL 2 by TARSKI:def 1;
      0.TOP-REAL 2 in {p: -1<p`1 & p`1<1 & -1<p`2 & p`2<1} by Th11;
      hence contradiction by A1,A10,A11,XBOOLE_0:def 4;
    end;
    hence thesis by A10,XBOOLE_0:def 5;
  end;
  then
A12: rng f c= the carrier of (TOP-REAL 2)|B` by A9,SUBSET_1:def 5;
A13: d1 is one-to-one by A1,A2,FUNCT_1:46;
  rng g c= B`
  proof
    let x be set;
    assume
A14: x in rng g;
    now
      assume x in B;
      then
A15:  x=0.TOP-REAL 2 by TARSKI:def 1;
      0.TOP-REAL 2 in K0 by A1,Th11;
      hence contradiction by A1,A14,A15,XBOOLE_0:def 4;
    end;
    then x in (the carrier of TOP-REAL 2)\ B by A14,XBOOLE_0:def 5;
    hence thesis by SUBSET_1:def 5;
  end;
  then consider w2 being Function of I[01],TOP-REAL 2 such that
A16: w2 is continuous & w2=h*g by A1,A2,A5,Th22;
  reconsider d2=h*g as Function of I[01],TOP-REAL 2 by A16;
  rng g c=(the carrier of (TOP-REAL 2))\B
  proof
    let e be set;
    assume
A17: e in rng g;
    now
      assume e in B;
      then
A18:  e=0.TOP-REAL 2 by TARSKI:def 1;
      0.TOP-REAL 2 in {p: -1<p`1 & p`1<1 & -1<p`2 & p`2<1} by Th11;
      hence contradiction by A1,A17,A18,XBOOLE_0:def 4;
    end;
    hence thesis by A17,XBOOLE_0:def 5;
  end;
  then
A19: rng g c= the carrier of (TOP-REAL 2)|B` by A9,SUBSET_1:def 5;
A20: d2 is one-to-one by A1,A2,FUNCT_1:46;
  f.O in Kb by A1;
  then
A21: h.(f.O)=f.O by A2;
  f.I in Kb by A1;
  then
A22: h.(f.I)=f.I by A2;
  g.O in Kb by A1;
  then
A23: h.(g.O)=g.O by A2;
  g.I in Kb by A1;
  then h.(g.I)=g.I by A2;
  then
A24: (d1.O)`1=-1 & (d1.I)`1=1 &
  (d2.O)`2=-1 & (d2.I)`2=1 by A1,A3,A4,A21,A22,A23,FUNCT_1:23;
  for r being Point of I[01] holds
  -1<=(d1.r)`1 & (d1.r)`1<=1 & -1<=(d2.r)`1 & (d2.r)`1<=1 &
  -1<=(d1.r)`2 & (d1.r)`2<=1 & -1<=(d2.r)`2 & (d2.r)`2<=1
  proof
    let r be Point of I[01];
    f.r in rng f by A3,FUNCT_1:12;
    then
A25: not f.r in K0 by A1,XBOOLE_0:def 4;
A26: not f.r in Kb implies d1.r in K0 \/ Kb
    proof
      assume not f.r in Kb;
      then not f.r in K0 \/ Kb by A25,XBOOLE_0:def 3;
      then
A27:  h.(f.r) in K0 by A2;
      d1.r=h.(f.r) by A3,FUNCT_1:23;
      hence thesis by A27,XBOOLE_0:def 3;
    end;
A28: f.r in Kb implies d1.r in K0 \/ Kb
    proof
      assume
A29:  f.r in Kb;
      then
A30:  h.(f.r)=f.r by A2;
      d1.r=h.(f.r) by A3,FUNCT_1:23;
      hence thesis by A29,A30,XBOOLE_0:def 3;
    end;
    g.r in rng g by A4,FUNCT_1:12;
    then
A31: not g.r in K0 by A1,XBOOLE_0:def 4;
A32: not g.r in Kb implies d2.r in K0 \/ Kb
    proof
      assume not g.r in Kb;
      then not g.r in K0 \/ Kb by A31,XBOOLE_0:def 3;
      then
A33:  h.(g.r) in K0 by A2;
      d2.r=h.(g.r) by A4,FUNCT_1:23;
      hence thesis by A33,XBOOLE_0:def 3;
    end;
A34: g.r in Kb implies d2.r in K0 \/ Kb
    proof
      assume
A35:  g.r in Kb;
      then
A36:  h.(g.r)=g.r by A2;
      d2.r=h.(g.r) by A4,FUNCT_1:23;
      hence thesis by A35,A36,XBOOLE_0:def 3;
    end;
    now per cases by A26,A28,A32,A34,XBOOLE_0:def 3;
      case
A37:    d1.r in K0 & d2.r in K0;
        then consider p being Point of TOP-REAL 2 such that
A38:    p=d1.r & ( -1<p`1 & p`1<1 & -1<p`2 & p`2<1) by A1;
        consider q being Point of TOP-REAL 2 such that
A39:    q=d2.r & ( -1<q`1 & q`1<1 & -1<q`2 & q`2<1) by A1,A37;
        thus thesis by A38,A39;
      end;
      case
A40:    d1.r in K0 & d2.r in Kb;
        then consider p being Point of TOP-REAL 2 such that
A41:    p=d1.r & ( -1<p`1 & p`1<1 & -1<p`2 & p`2<1) by A1;
        consider q being Point of TOP-REAL 2 such that
A42:    q=d2.r & ( -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1
        or -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1) by A40;
        thus thesis by A41,A42;
      end;
      case
A43:    d1.r in Kb & d2.r in K0;
        then consider p being Point of TOP-REAL 2 such that
A44:    p=d2.r & ( -1<p`1 & p`1<1 & -1<p`2 & p`2<1) by A1;
        consider q being Point of TOP-REAL 2 such that
A45:    q=d1.r & ( -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1
        or -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1) by A43; thus
        thesis by A44,A45;
      end;
      case
A46:    d1.r in Kb & d2.r in Kb;
        then consider p being Point of TOP-REAL 2 such that
A47:    p=d2.r & ( -1=p`1 & -1<=p`2 & p`2<=1 or p`1=1 & -1<=p`2 & p`2<=1
        or -1=p`2 & -1<=p`1 & p`1<=1 or 1=p`2 & -1<=p`1 & p`1<=1);
        consider q being Point of TOP-REAL 2 such that
A48:    q=d1.r & ( -1=q`1 & -1<=q`2 & q`2<=1 or q`1=1 & -1<=q`2 & q`2<=1
        or -1=q`2 & -1<=q`1 & q`1<=1 or 1=q`2 & -1<=q`1 & q`1<=1) by A46; thus
        thesis by A47,A48;
      end;
    end;
    hence thesis;
  end;
  then rng d1 meets rng d2 by A1,A8,A13,A16,A20,A24,JGRAPH_1:65;
  then
A49: rng d1 /\ rng d2<>{} by XBOOLE_0:def 7;
  consider s being Element of rng d1 /\ rng d2;
A50: s in rng d1 & s in rng d2 by A49,XBOOLE_0:def 4;
  then consider t1 being set such that
A51: t1 in dom d1 & s=d1.t1 by FUNCT_1:def 5;
  consider t2 being set such that
A52: t2 in dom d2 & s=d2.t2 by A50,FUNCT_1:def 5;
  reconsider W=B` as non empty Subset of TOP-REAL 2 by Th19;
A53: the carrier of (TOP-REAL 2)|W <>{};
  h.(f.t1)=d1.t1 by A51,FUNCT_1:22;
  then
A54: h.(f.t1)=h.(g.t2) by A51,A52,FUNCT_1:22;
A55: dom h=the carrier of (TOP-REAL 2)|B` by A53,FUNCT_2:def 1;
A56: f.t1 in rng f by A3,A51,FUNCT_1:12;
  dom g =the carrier of I[01] by FUNCT_2:def 1;
  then
A57: g.t2 in rng g by A52,FUNCT_1:12;
  then f.t1=g.t2 by A2,A12,A19,A54,A55,A56,FUNCT_1:def 8;
  then rng f /\ rng g <> {} by A56,A57,XBOOLE_0:def 4;
  hence thesis by XBOOLE_0:def 7;
end;

theorem Th53:
  for A,B,C,D being real number, f being Function of TOP-REAL 2,TOP-REAL 2
  st (for t being Point of TOP-REAL 2 holds
  f.t=|[A*(t`1)+B,C*(t`2)+D]|) holds f is continuous
proof
  let A,B,C,D be real number,f be Function of TOP-REAL 2,TOP-REAL 2;
  assume
A1: (for t being Point of TOP-REAL 2 holds f.t=|[A*(t`1)+B,C*(t`2)+D]|);
A2: (TOP-REAL 2)| [#](TOP-REAL 2)=the TopStruct of TOP-REAL 2 by TSEP_1:100;
  then reconsider f1=proj1*f as Function of (TOP-REAL 2)| [#](TOP-REAL 2),R^1
  by TOPMETR:24;
  reconsider f2=proj2*f as Function of (TOP-REAL 2)| [#](TOP-REAL 2),R^1
  by A2,TOPMETR:24;
  reconsider f0=f as Function of (TOP-REAL 2)| [#](TOP-REAL 2),
  (TOP-REAL 2)| [#](TOP-REAL 2) by A2;
  set K0=[#](TOP-REAL 2);
  reconsider h11=proj1 as Function of TOP-REAL 2,R^1 by TOPMETR:24;
  reconsider h1=h11 as Function of (TOP-REAL 2)| [#](TOP-REAL 2),R^1 by A2;
  h11 is continuous by TOPREAL6:83;
  then h1 is continuous by A2,PRE_TOPC:62;
  then consider g1 being Function
  of (TOP-REAL 2)| [#](TOP-REAL 2),R^1 such that
A3: (for p being Point of (TOP-REAL 2)| [#](TOP-REAL 2),r1 being real number
  st h1.p=r1 holds g1.p=A*r1) & g1 is continuous by A2,Th33;
  consider g11 being Function of (TOP-REAL 2)| [#](TOP-REAL 2),R^1 such that
A4: (for p being Point of (TOP-REAL 2)| [#](TOP-REAL 2),r1 being real number
  st g1.p=r1 holds g11.p=r1+B) & g11 is continuous by A3,Th34;
  dom f1=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  then
A5: dom f1=dom g11 by A2,FUNCT_2:def 1;
  for x being set st x in dom f1 holds f1.x=g11.x
  proof
    let x be set;
    assume
A6: x in dom f1;
    then
A7: f1.x=proj1.(f.x) by FUNCT_1:22;
    reconsider p=x as Point of TOP-REAL 2 by A6,FUNCT_2:def 1;
A8: f1.x=proj1.(|[A*(p`1)+B,C*(p`2)+D]|) by A1,A7
      .=A*(p`1)+B by PSCOMP_1:128
      .=A*(proj1.p)+B by PSCOMP_1:def 28;
    A*(proj1.p)=g1.p by A2,A3;
    hence thesis by A2,A4,A8;
  end;
  then
A9: f1 is continuous by A4,A5,FUNCT_1:9;
  reconsider h11=proj2 as Function of TOP-REAL 2,R^1 by TOPMETR:24;
  reconsider h1=h11 as Function of (TOP-REAL 2)| [#](TOP-REAL 2),R^1 by A2;
  h11 is continuous by A2,TOPREAL6:83;
  then h1 is continuous by A2,PRE_TOPC:62;
  then consider g1 being Function
  of (TOP-REAL 2)| [#](TOP-REAL 2),R^1 such that
A10: (for p being Point of (TOP-REAL 2)| [#](TOP-REAL 2),r1 being real number
  st h1.p=r1 holds g1.p=C*r1) & g1 is continuous by Th33;
  consider g11 being Function of (TOP-REAL 2)| [#](TOP-REAL 2),R^1 such that
A11: (for p being Point of (TOP-REAL 2)| [#](TOP-REAL 2),r1 being real number
  st g1.p=r1 holds g11.p=r1+D) & g11 is continuous by A10,Th34;
  dom f2=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
  then
A12: dom f2=dom g11 by A2,FUNCT_2:def 1;
  for x being set st x in dom f2 holds f2.x=g11.x
  proof
    let x be set;
    assume
A13: x in dom f2;
    then
A14: f2.x=proj2.(f.x) by FUNCT_1:22;
    reconsider p=x as Point of TOP-REAL 2 by A13,FUNCT_2:def 1;
A15: f2.x=proj2.(|[A*(p`1)+B,C*(p`2)+D]|) by A1,A14
      .=C*(p`2)+D by PSCOMP_1:128
      .=C*(proj2.p)+D by PSCOMP_1:def 29;
    C*(proj2.p)=g1.p by A2,A10;
    hence thesis by A2,A11,A15;
  end;
  then
A16: f2 is continuous by A11,A12,FUNCT_1:9;
  for x,y,r,s being real number st |[x,y]| in K0 &
  r=f1.(|[x,y]|) & s=f2.(|[x,y]|) holds f0. |[x,y]|=|[r,s]|
  proof
    let x,y,r,s be real number;
    assume
A17: |[x,y]| in K0 & r=f1.(|[x,y]|) & s=f2.(|[x,y]|);
A18: dom f =the carrier of TOP-REAL 2 by FUNCT_2:def 1;
A19: f. |[x,y]| is Point of TOP-REAL 2;
A20: proj1.(f0. |[x,y]|) =r by A17,A18,FUNCT_1:23;
    proj2.(f0. |[x,y]|) =s by A17,A18,FUNCT_1:23;
    hence thesis by A19,A20,Th18;
  end;
   then f0 is continuous by A2,A9,A16,Th45;
  hence f is continuous by A2,PRE_TOPC:64;
end;

definition
  let A,B,C,D be real number;
  func AffineMap(A,B,C,D) -> Function of TOP-REAL 2,TOP-REAL 2 means
  :Def2:
  for t being Point of TOP-REAL 2 holds it.t=|[A*(t`1)+B,C*(t`2)+D]|;
  existence
  proof
    defpred P[set,set] means for t being Point of TOP-REAL 2 st t=$1 holds
    $2=|[A*(t`1)+B,C*(t`2)+D]|;
A3: for x being set st x in the carrier of TOP-REAL 2 ex y being set st P[x,y]
    proof
      let x be set;
      assume x in the carrier of TOP-REAL 2;
      then reconsider t2=x as Point of TOP-REAL 2;
      reconsider y2=|[A*(t2`1)+B,C*(t2`2)+D]| as set;
      (for t being Point of TOP-REAL 2 st t=x holds
      y2 =|[A*(t`1)+B,C*(t`2)+D]|);
      hence thesis;
    end;
    ex ff being Function st dom ff=(the carrier of TOP-REAL 2) &
    for x being set st x in (the carrier of TOP-REAL 2) holds
    P[x,ff.x] from CLASSES1:sch 1(A3);
    then consider ff being Function such that
A4: dom ff=the carrier of TOP-REAL 2 &
    for x being set st x in the carrier of TOP-REAL 2 holds
    (for t being Point of TOP-REAL 2 st t=x holds
    ff.x=|[A*(t`1)+B,C*(t`2)+D]|);
    for x being set st x in the carrier of TOP-REAL 2 holds
    ff.x in the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in the carrier of TOP-REAL 2;
      then reconsider t=x as Point of TOP-REAL 2;
      ff.t=|[A*(t`1)+B,C*(t`2)+D]| by A4;
      hence thesis;
    end;
    then reconsider ff as Function of TOP-REAL 2,TOP-REAL 2 by A4,FUNCT_2:5;
    take ff;
    thus thesis by A4;
  end;
  uniqueness
  proof
    let m1,m2 be Function of TOP-REAL 2,TOP-REAL 2 such that
A5: for t being Point of TOP-REAL 2 holds m1.t=|[A*(t`1)+B,C*(t`2)+D]| and
A6: for t being Point of TOP-REAL 2 holds m2.t=|[A*(t`1)+B,C*(t`2)+D]|;
    for x being Point of TOP-REAL 2 holds m1.x = m2.x
    proof
      let t be Point of TOP-REAL 2;
      thus m1.t = |[A*(t`1)+B,C*(t`2)+D]| by A5
        .= m2.t by A6;
    end;
    hence m1 = m2 by FUNCT_2:113;
  end;
end;

registration
  let a,b,c,d be real number;
  cluster AffineMap(a,b,c,d) -> continuous;
  coherence
  proof
    for t being Point of TOP-REAL 2 holds
    AffineMap(a,b,c,d).t=|[a*(t`1)+b,c*(t`2)+d]| by Def2;
    hence thesis by Th53;
  end;
end;

theorem Th54:
  for A,B,C,D being real number st A>0 & C>0
  holds AffineMap(A,B,C,D) is one-to-one
proof
  let A,B,C,D be real number such that
A1: A>0 and
A2: C>0;
  set ff = AffineMap(A,B,C,D);
  for x1,x2 being set st x1 in dom ff & x2 in dom ff & ff.x1=ff.x2 holds x1=x2
  proof
    let x1,x2 be set;
    assume
A3: x1 in dom ff & x2 in dom ff & ff.x1=ff.x2;
    then reconsider p1=x1 as Point of TOP-REAL 2;
    reconsider p2=x2 as Point of TOP-REAL 2 by A3;
A4: ff.x1= |[A*(p1`1)+B,C*(p1`2)+D]| by Def2;
    ff.x2= |[A*(p2`1)+B,C*(p2`2)+D]| by Def2;
    then A*(p1`1)+B=A*(p2`1)+B & C*(p1`2)+D=C*(p2`2)+D by A3,A4,SPPOL_2:1;
    then (p1`1)=A*(p2`1)/A & C*(p1`2)/C=C*(p2`2)/C by A1,XCMPLX_1:90;
    then (p1`1)=(p2`1) & (p1`2)=C*(p2`2)/C by A1,A2,XCMPLX_1:90;
    hence thesis by A2,TOPREAL3:11,XCMPLX_1:90;
  end;
  hence thesis by FUNCT_1:def 8;
end;

theorem
  for f,g being Function of I[01],TOP-REAL 2,a,b,c,d being real number ,
  O,I being Point of I[01] st O=0 & I=1 & f is continuous one-to-one &
  g is continuous one-to-one & (f.O)`1=a & (f.I)`1=b &
  c <=(f.O)`2 & (f.O)`2<=d & c <=(f.I)`2 & (f.I)`2<=d &
  (g.O)`2=c & (g.I)`2=d & a<=(g.O)`1 & (g.O)`1<=b & a<=(g.I)`1 & (g.I)`1<=b &
  a < b & c < d & not (ex r being Point of I[01] st
  a<(f.r)`1 & (f.r)`1<b & c <(f.r)`2 & (f.r)`2<d)&
  not (ex r being Point of I[01] st
  a<(g.r)`1 & (g.r)`1<b & c <(g.r)`2 & (g.r)`2<d) holds rng f meets rng g
proof
  let f,g be Function of I[01],TOP-REAL 2,a,b,c,d be real number,
  O,I be Point of I[01];
  assume
A1: O=0 & I=1 & f is continuous one-to-one & g is continuous one-to-one &
  (f.O)`1=a & (f.I)`1=b &
  c <=(f.O)`2 & (f.O)`2<=d & c <=(f.I)`2 & (f.I)`2<=d &
  (g.O)`2=c & (g.I)`2=d & a<=(g.O)`1 & (g.O)`1<=b & a<=(g.I)`1 & (g.I)`1<=b &
  a < b & c < d & not (ex r being Point of I[01] st
  a<(f.r)`1 & (f.r)`1<b & c <(f.r)`2 & (f.r)`2<d)&
  not (ex r being Point of I[01] st
  a<(g.r)`1 & (g.r)`1<b & c <(g.r)`2 & (g.r)`2<d);
  then
A2: b-a>0 by XREAL_1:52;
A3: d-c>0 by A1,XREAL_1:52;
  set A=2/(b-a),B=1-2*b/(b-a),C=2/(d-c),D=1-2*d/(d-c);
A4: A>0 by A2,XREAL_1:141;
A5: C>0 by A3,XREAL_1:141;
  set ff =AffineMap(A,B,C,D);
A6: dom ff=the carrier of TOP-REAL 2 by FUNCT_2:def 1;
A7: dom f=the carrier of I[01] by FUNCT_2:def 1;
A8: dom g=the carrier of I[01] by FUNCT_2:def 1;
A9: ff is one-to-one by A4,A5,Th54;
  reconsider f2=ff*f,g2=ff*g as Function of I[01],TOP-REAL 2;
A12: f2 is one-to-one by A1,A9,FUNCT_1:46;
A13: g2 is one-to-one by A1,A9,FUNCT_1:46;
  defpred P[Point of TOP-REAL 2] means -1<$1`1 & $1`1<1 & -1<$1`2 & $1`2<1;
  reconsider K0={p: P[p]} as Subset of TOP-REAL 2 from TopSubset;
A14: f2.O=ff.(f.O) by A7,FUNCT_1:23
    .=|[A*a+B,C*((f.O)`2)+D]| by A1,Def2;
A15: f2.I=ff.(f.I) by A7,FUNCT_1:23
    .=|[A*b+B,C*((f.I)`2)+D]| by A1,Def2;
A16: g2.O=ff.(g.O) by A8,FUNCT_1:23
    .=|[A*((g.O)`1)+B,C*c+D]| by A1,Def2;
A17: g2.I=ff.(g.I) by A8,FUNCT_1:23
    .=|[A*((g.I)`1)+B,C*d+D]| by A1,Def2;
A18: (f2.O)`1= -1
  proof thus (f2.O)`1=A*a+B by A14,EUCLID:56
      .= a*2/(b-a)+(1-2*b/(b-a))
      .= a*2/(b-a)+((b-a)/(b-a)-2*b/(b-a)) by A2,XCMPLX_1:60
      .= a*2/(b-a)+((b-a)-2*b)/(b-a)
      .= (a*2+((b-a)-2*b))/(b-a)
      .= (-(b-a))/(b-a)
      .= -((b-a)/(b-a))
      .= -1 by A2,XCMPLX_1:60;
  end;
A19: (f2.I)`1=A*b+B by A15,EUCLID:56
    .= b*2/(b-a)+(1-2*b/(b-a))
    .= 1;
A20: (g2.O)`2=2/(d-c)*c+(1-2*d/(d-c)) by A16,EUCLID:56
    .= c*2/(d-c)+(1-2*d/(d-c))
    .= c*2/(d-c)+((d-c)/(d-c)-2*d/(d-c)) by A3,XCMPLX_1:60
    .= c*2/(d-c)+((d-c)-2*d)/(d-c)
    .= (c*2+((d-c)-2*d))/(d-c)
    .= (-(d-c))/(d-c)
    .= -((d-c)/(d-c))
    .= -1 by A3,XCMPLX_1:60;
A21: (g2.I)`2=C*d+D by A17,EUCLID:56
    .= d*2/(d-c)+(1-2*d/(d-c))
    .= 1;
A22: -1<=(f2.O)`2 & (f2.O)`2<=1 & -1<=(f2.I)`2 & (f2.I)`2<=1
  proof
    reconsider s0=(f.O)`2 as Real;
A23: (f2.O)`2=((s0-d)+(s0-d)-(c-d))/(d-c)
    proof thus
      (f2.O)`2=C*s0+D by A14,EUCLID:56
        .= s0 *2/(d-c)+(1-2*d/(d-c))
        .= s0 *2/(d-c)+((d-c)/(d-c)-2*d/(d-c)) by A3,XCMPLX_1:60
        .= s0 *2/(d-c)+((d-c)-2*d)/(d-c)
        .= (s0 *2+((d-c)-2*d))/(d-c)
        .= ((s0-d)+(s0-d)-(c-d))/(d-c);
    end;
    c-d<=s0-d by A1,XREAL_1:11;
    then c-d+(c-d)<=(s0-d)+(s0-d) by XREAL_1:9;
    then
A24: c-d+(c-d)-(c-d)<=(s0-d)+(s0-d)-(c-d) by XREAL_1:11;
A25: (c-d)/(d-c) = (-(d-c))/(d-c)
      .= -((d-c)/(d-c))
      .= -1 by A3,XCMPLX_1:60;
    d-d>=s0-d by A1,XREAL_1:11;
    then 0+(d-d)-(c-d)>=(s0-d)+(s0-d)-(c-d) by XREAL_1:11;
    then
A26: (d-c)/(d-c)>=((s0-d)+(s0-d)-(c-d))/(d-c) by A3,XREAL_1:74;
    reconsider s1=(f.I)`2 as Real;
A27: (f2.I)`2=((s1-d)+(s1-d)-(c-d))/(d-c)
    proof thus
      (f2.I)`2=C*s1+D by A15,EUCLID:56
        .= s1*2/(d-c)+(1-2*d/(d-c))
        .= s1*2/(d-c)+((d-c)/(d-c)-2*d/(d-c)) by A3,XCMPLX_1:60
        .= s1*2/(d-c)+((d-c)-2*d)/(d-c)
        .= (s1*2+((d-c)-2*d))/(d-c)
        .= ((s1-d)+(s1-d)-(c-d))/(d-c);
    end;
    c-d<=s1-d by A1,XREAL_1:11;
    then c-d+(c-d)<=(s1-d)+(s1-d) by XREAL_1:9;
    then
A28: c-d+(c-d)-(c-d)<=(s1-d)+(s1-d)-(c-d) by XREAL_1:11;
    d-d>=s1-d by A1,XREAL_1:11;
    then 0+(d-d)-(c-d)>=(s1-d)+(s1-d)-(c-d) by XREAL_1:11;
    then (d-c)/(d-c)>=((s1-d)+(s1-d)-(c-d))/(d-c) by A3,XREAL_1:74;
    hence thesis by A3,A23,A24,A25,A26,A27,A28,XREAL_1:74;
  end;
A29: -1<=(g2.O)`1 & (g2.O)`1<=1 & -1<=(g2.I)`1 & (g2.I)`1<=1
  proof
    reconsider s0=(g.O)`1 as Real;
A30: (g2.O)`1=((s0-b)+(s0-b)-(a-b))/(b-a)
    proof thus
      (g2.O)`1=A*s0+B by A16,EUCLID:56
        .= s0 *2/(b-a)+(1-2*b/(b-a))
        .= s0 *2/(b-a)+((b-a)/(b-a)-2*b/(b-a)) by A2,XCMPLX_1:60
        .= s0 *2/(b-a)+((b-a)-2*b)/(b-a)
        .= (s0 *2+((b-a)-2*b))/(b-a)
        .= ((s0-b)+(s0-b)-(a-b))/(b-a);
    end;
    a-b<=s0-b by A1,XREAL_1:11;
    then a-b+(a-b)<=(s0-b)+(s0-b) by XREAL_1:9;
    then
A31: a-b+(a-b)-(a-b)<=(s0-b)+(s0-b)-(a-b) by XREAL_1:11;
A32: (a-b)/(b-a) = (-(b-a))/(b-a)
      .= -((b-a)/(b-a))
      .= -1 by A2,XCMPLX_1:60;
    b-b>=s0-b by A1,XREAL_1:11;
    then 0+(b-b)-(a-b)>=(s0-b)+(s0-b)-(a-b) by XREAL_1:11;
    then
A33: (b-a)/(b-a)>=((s0-b)+(s0-b)-(a-b))/(b-a) by A2,XREAL_1:74;
    reconsider s1=(g.I)`1 as Real;
A34: (g2.I)`1=((s1-b)+(s1-b)-(a-b))/(b-a)
    proof thus
      (g2.I)`1=A*s1+B by A17,EUCLID:56
        .= s1*2/(b-a)+(1-2*b/(b-a))
        .= s1*2/(b-a)+((b-a)/(b-a)-2*b/(b-a)) by A2,XCMPLX_1:60
        .= s1*2/(b-a)+((b-a)-2*b)/(b-a)
        .= (s1*2+((b-a)-2*b))/(b-a)
        .= ((s1-b)+(s1-b)-(a-b))/(b-a);
    end;
    a-b<=s1-b by A1,XREAL_1:11;
    then a-b+(a-b)<=(s1-b)+(s1-b) by XREAL_1:9;
    then
A35: a-b+(a-b)-(a-b)<=(s1-b)+(s1-b)-(a-b) by XREAL_1:11;
    b-b>=s1-b by A1,XREAL_1:11;
    then 0+(b-b)-(a-b)>=(s1-b)+(s1-b)-(a-b) by XREAL_1:11;
    then (b-a)/(b-a)>=((s1-b)+(s1-b)-(a-b))/(b-a) by A2,XREAL_1:74;
    hence thesis by A2,A30,A31,A32,A33,A34,A35,XREAL_1:74;
  end;
A36: now
    assume rng f2 meets K0;
    then consider x being set such that
A37: x in rng f2 & x in K0 by XBOOLE_0:3;
    reconsider q=x as Point of TOP-REAL 2 by A37;
    consider p such that
A38: p=q &( -1<p`1 & p`1<1 & -1<p`2 & p`2<1) by A37;
    consider z being set such that
A39: z in dom f2 & x=f2.z by A37,FUNCT_1:def 5;
    reconsider u=z as Point of I[01] by A39;
    reconsider t=f.u as Point of TOP-REAL 2;
A40: ff.t=|[A*(t`1)+B,C*(t`2)+D]| by Def2;
    ff.t=p by A38,A39,FUNCT_1:22;
    then
A41: -1<A*(t`1)+B & A*(t`1)+B<1 & -1<C*(t`2)+D & C*(t`2)+D<1
    by A38,A40,EUCLID:56;
A42: A*(t`1)+B=(2*((t`1)-b)-(a-b))/(b-a)
    proof thus A*(t`1)+B= (t`1)*2/(b-a)+(1-2*b/(b-a))
        .= (t`1)*2/(b-a)+((b-a)/(b-a)-2*b/(b-a)) by A2,XCMPLX_1:60
        .= (t`1)*2/(b-a)+((b-a)-2*b)/(b-a)
        .= ((t`1)*2+((b-a)-2*b))/(b-a)
        .= (2*((t`1)-b)-(a-b))/(b-a);
    end;
    then (-1)*(b-a)< (2*((t`1)-b)-(a-b))/(b-a)*(b-a) by A2,A41,XREAL_1:70;
    then (-1)*(b-a)< 2*((t`1)-b)-(a-b) by A2,XCMPLX_1:88;
    then (-1)*(b-a)+(a-b)< 2*((t`1)-b)-(a-b)+(a-b) by XREAL_1:10;
    then 2*(a-b)/2< 2*((t`1)-b)/2 by XREAL_1:76;
    then
A43: a < (t`1) by XREAL_1:11;
    (1)*(b-a)> (2*((t`1)-b)-(a-b))/(b-a)*(b-a) by A2,A41,A42,XREAL_1:70;
    then (1)*(b-a)> 2*((t`1)-b)-(a-b) by A2,XCMPLX_1:88;
    then (1)*(b-a)+(a-b)> 2*((t`1)-b)-(a-b)+(a-b) by XREAL_1:10;
    then 0/2>((t`1)-b)*2/2;
    then
A44: 0+b>t`1 by XREAL_1:21;
A45: C*(t`2)+D= (t`2)*2/(d-c)+(1-2*d/(d-c))
      .= (t`2)*2/(d-c)+((d-c)/(d-c)-2*d/(d-c)) by A3,XCMPLX_1:60
      .= (t`2)*2/(d-c)+((d-c)-2*d)/(d-c)
      .= ((t`2)*2+((d-c)-2*d))/(d-c)
      .= (2*((t`2)-d)-(c-d))/(d-c);
    then (-1)*(d-c)< (2*((t`2)-d)-(c-d))/(d-c)*(d-c) by A3,A41,XREAL_1:70;
    then (-1)*(d-c)< 2*((t`2)-d)-(c-d) by A3,XCMPLX_1:88;
    then (-1)*(d-c)+(c-d)< 2*((t`2)-d)-(c-d)+(c-d) by XREAL_1:10;
    then 2*(c-d)/2< 2*((t`2)-d)/2 by XREAL_1:76;
    then
A46: c < (t`2) by XREAL_1:11;
    (1)*(d-c)> (2*((t`2)-d)-(c-d))/(d-c)*(d-c) by A3,A41,A45,XREAL_1:70;
    then (1)*(d-c)> 2*((t`2)-d)-(c-d) by A3,XCMPLX_1:88;
    then (1)*(d-c)+(c-d)> 2*((t`2)-d)-(c-d)+(c-d) by XREAL_1:10;
    then 0/2>((t`2)-d)*2/2;
    then 0+d>t`2 by XREAL_1:21;
    hence contradiction by A1,A43,A44,A46;
  end;
  now
    assume rng g2 meets K0;
    then consider x being set such that
A47: x in rng g2 & x in K0 by XBOOLE_0:3;
    reconsider q=x as Point of TOP-REAL 2 by A47;
    consider p such that
A48: p=q &( -1<p`1 & p`1<1 & -1<p`2 & p`2<1) by A47;
    consider z being set such that
A49: z in dom g2 & x=g2.z by A47,FUNCT_1:def 5;
    reconsider u=z as Point of I[01] by A49;
    reconsider t=g.u as Point of TOP-REAL 2;
A50: ff.t=|[A*(t`1)+B,C*(t`2)+D]| by Def2;
    ff.t=p by A48,A49,FUNCT_1:22;
    then
A51: -1<A*(t`1)+B & A*(t`1)+B<1 & -1<C*(t`2)+D & C*(t`2)+D<1
    by A48,A50,EUCLID:56;
A52: A*(t`1)+B= (t`1)*2/(b-a)+(1-2*b/(b-a))
      .= (t`1)*2/(b-a)+((b-a)/(b-a)-2*b/(b-a)) by A2,XCMPLX_1:60
      .= (t`1)*2/(b-a)+((b-a)-2*b)/(b-a)
      .= ((t`1)*2+((b-a)-2*b))/(b-a)
      .= (2*((t`1)-b)-(a-b))/(b-a);
    then (-1)*(b-a)< (2*((t`1)-b)-(a-b))/(b-a)*(b-a) by A2,A51,XREAL_1:70;
    then (-1)*(b-a)< 2*((t`1)-b)-(a-b) by A2,XCMPLX_1:88;
    then (-1)*(b-a)+(a-b)< 2*((t`1)-b)-(a-b)+(a-b) by XREAL_1:10;
    then 2*(a-b)/2< 2*((t`1)-b)/2 by XREAL_1:76;
    then
A53: a < (t`1) by XREAL_1:11;
    (1)*(b-a)> (2*((t`1)-b)-(a-b))/(b-a)*(b-a) by A2,A51,A52,XREAL_1:70;
    then (1)*(b-a)> 2*((t`1)-b)-(a-b) by A2,XCMPLX_1:88;
    then (1)*(b-a)+(a-b)> 2*((t`1)-b)-(a-b)+(a-b) by XREAL_1:10;
    then 0/2>((t`1)-b)*2/2;
    then
A54: 0+b>t`1 by XREAL_1:21;
A55: C*(t`2)+D= (t`2)*2/(d-c)+(1-2*d/(d-c))
      .= (t`2)*2/(d-c)+((d-c)/(d-c)-2*d/(d-c)) by A3,XCMPLX_1:60
      .= (t`2)*2/(d-c)+((d-c)-2*d)/(d-c)
      .= ((t`2)*2+((d-c)-2*d))/(d-c)
      .= (2*((t`2)-d)-(c-d))/(d-c);
    then (-1)*(d-c)< (2*((t`2)-d)-(c-d))/(d-c)*(d-c) by A3,A51,XREAL_1:70;
    then (-1)*(d-c)< 2*((t`2)-d)-(c-d) by A3,XCMPLX_1:88;
    then (-1)*(d-c)+(c-d)< 2*((t`2)-d)-(c-d)+(c-d) by XREAL_1:10;
    then 2*(c-d)/2< 2*((t`2)-d)/2 by XREAL_1:76;
    then
A56: c < (t`2) by XREAL_1:11;
    (1)*(d-c)> (2*((t`2)-d)-(c-d))/(d-c)*(d-c) by A3,A51,A55,XREAL_1:70;
    then (1)*(d-c)> 2*((t`2)-d)-(c-d) by A3,XCMPLX_1:88;
    then (1)*(d-c)+(c-d)> 2*((t`2)-d)-(c-d)+(c-d) by XREAL_1:10;
    then 0/2>((t`2)-d)*2/2;
    then 0+d>t`2 by XREAL_1:21;
    hence contradiction by A1,A53,A54,A56;
  end;
  then rng f2 meets rng g2 by A1,A12,A13,A18,A19,A20,A21,A22,A29,A36,Th52;
  then
A57: rng f2 /\ rng g2 <> {} by XBOOLE_0:def 7;
  consider y being Element of rng f2 /\ rng g2;
A58: y in rng f2 & y in rng g2 by A57,XBOOLE_0:def 4;
  then consider x being set such that
A59: x in dom f2 & y=f2.x by FUNCT_1:def 5;
A60: y=ff.(f.x) by A59,FUNCT_1:22;
  consider x2 being set such that
A61: x2 in dom g2 & y=g2.x2 by A58,FUNCT_1:def 5;
A62: y=ff.(g.x2) by A61,FUNCT_1:22;
  dom f2 c= dom f by RELAT_1:44;
  then
A63: f.x in rng f by A59,FUNCT_1:12;
  dom g2 c= dom g by RELAT_1:44;
  then
A64: g.x2 in rng g by A61,FUNCT_1:12;
  then f.x=g.x2 by A6,A9,A60,A62,A63,FUNCT_1:def 8;
  then rng f /\ rng g <> {} by A63,A64,XBOOLE_0:def 4;
  hence thesis by XBOOLE_0:def 7;
end;

theorem
  {p7 where p7 is Point of TOP-REAL 2: p7`2<=p7`1 }
  is closed Subset of TOP-REAL 2 &
  {p7 where p7 is Point of TOP-REAL 2: p7`1<=p7`2 }
  is closed Subset of TOP-REAL 2 by Lm4,Lm7;

theorem
  {p7 where p7 is Point of TOP-REAL 2: -p7`1<=p7`2 }
  is closed Subset of TOP-REAL 2 &
  {p7 where p7 is Point of TOP-REAL 2: p7`2<=-p7`1 }
  is closed Subset of TOP-REAL 2 by Lm10,Lm13;

theorem
  {p7 where p7 is Point of TOP-REAL 2: -p7`2<=p7`1 }
  is closed Subset of TOP-REAL 2 &
  {p7 where p7 is Point of TOP-REAL 2: p7`1<=-p7`2 }
  is closed Subset of TOP-REAL 2 by Lm16,Lm19;

