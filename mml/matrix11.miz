:: Basic Properties of Determinants of Square Matrices over a Field
::  by Karol P\c{a}k
:: 
:: Received March 21, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies VECTSP_1, RLVECT_1, MATRIX_1, FINSEQ_2, ARYTM_1, FINSEQ_1,
      TREES_1, FUNCT_1, RELAT_1, BOOLE, INCSP_1, CAT_3, RVSUM_1, BINOP_1,
      SETWISEO, MATRIX_2, FINSUB_1, FINSET_1, FINSEQOP, FUNCT_5, SUBSET_1,
      MATRIX_3, FUNCT_2, SGRAPH1, CARD_3, FINSEQ_4, NAT_1, ABSVALUE, FINSOP_1,
      CARD_1, FINSEQ_7, FUNCT_4, TARSKI, ORDINAL2, MATRIX11, COMPLEX1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0,
      FINSET_1, RELAT_1, FUNCT_1, PARTFUN1, FUNCT_2, FINSEQ_1, BINOP_1,
      STRUCT_0, NAT_D, FUNCT_3, FUNCOP_1, RLVECT_1, GROUP_1, VECTSP_1,
      SETWISEO, SETWOP_2, FINSEQ_2, FINSEQOP, MATRIX_1, FINSUB_1, MATRIX_2,
      NAT_1, FVSUM_1, CAT_2, FINSOP_1, BINARITH, GROUP_4, FINSEQ_4, MATRIX_3,
      CARD_1, FINSEQ_7, FUNCT_4, ORDINAL1, SGRAPH1, MATRIX_6;
 constructors RELAT_2, PARTFUN1, SETWISEO, NAT_1, FINSOP_1, SETWOP_2, CAT_2,
      ALGSTR_1, MATRIX_2, FVSUM_1, DOMAIN_1, BINOP_1, XXREAL_0, FINSEQ_4,
      BINARITH, FINSEQ_5, GROUP_4, MATRIX_7, VECTSP_1, FINSEQ_7, WELLORD2,
      STRUCT_0, ORDINAL2, SGRAPH1, NAT_D, MATRIX_6;
 registrations SUBSET_1, FUNCT_1, FINSUB_1, FINSET_1, RELSET_1, STRUCT_0,
      FVSUM_1, MATRIX_2, ARYTM_3, VECTSP_1, FUNCT_2, PARTFUN1, XBOOLE_0,
      ORDINAL1, XXREAL_0, NAT_1, FINSEQ_1, INT_1, RELAT_1;
 requirements ARITHM, NUMERALS, REAL, BOOLE, SUBSET;
 definitions BINOP_1, RLVECT_1, FINSEQ_2, FINSEQ_1, TARSKI, XBOOLE_0, MATRIX_1,
      MATRIX_3, FVSUM_1, RELAT_1;
 theorems FINSEQ_1, FINSEQ_2, TARSKI, FUNCT_2, NAT_1, BINOP_1, FUNCT_1,
      SETWISEO, VECTSP_1, ZFMISC_1, FVSUM_1, SETWOP_2, MATRIX_1, MATRIX_2,
      FUNCOP_1, FINSUB_1, FINSEQ_3, RLVECT_1, RELAT_1, FINSOP_1, RELSET_1,
      XBOOLE_0, XBOOLE_1, GROUP_1, MATRIX_3, FINSEQ_5, FINSEQ_4, FINSET_1,
      GROUP_4, XREAL_1, MATRIX_7, ORDINAL1, MATRIX_4, CARD_1, CARD_2, MATRIX_9,
      STIRL2_1, RADIX_1, REWRITE1, REAL_1, FINSEQ_7, SGRAPH1, WELLORD2,
      FRAENKEL, FUNCT_5, FUNCT_4, NAT_D, XXREAL_0;
 schemes FUNCT_2, NAT_1, FINSEQ_1, FINSEQ_2, MATRIX_1, BINOP_1, FUNCT_1,
      SETWISEO;

begin :: The Sign of a Permutation

  reserve x, y, X for set,
          i, j, k, l, n, m for Nat,
          D for non empty set,
          K for Field,
          a,b for Element of K,
          perm, p, q, pq for Element of Permutations(n),
          Perm,P, Q for Permutation of Seg n, 
          F for Function of Seg n,Seg n, 
          perm2, p2, q2, pq2 for Element of Permutations(n+2),
          Perm2 for Permutation of Seg (n+2);
         
notation let X be set;
  synonym 2Set(X) for TWOELEMENTSETS(X);
end;

theorem Th1:
  X in 2Set Seg n iff ex i,j st i in Seg n & j in Seg n & i < j & X = {i,j}
proof
  thus X in 2Set Seg n implies 
     ex i,j st i in Seg n & j in Seg n & i<j & X={i,j}
  proof
    assume X in 2Set Seg n;
    then consider x,y such that
      A1:x in Seg n & y in Seg n & x<>y & X={x,y} by SGRAPH1:10;
    reconsider x,y as Element of NAT by A1;
    x>y or y>x by A1,REAL_1:def 5;
    hence thesis by A1;
  end;
  assume ex i,j st i in Seg n & j in Seg n & i<j & X={i,j};
  then consider i,j such that 
    A1:i in Seg n & j in Seg n & i<j & X={i,j};
  {i,j} c= Seg n by A1,ZFMISC_1:38;
  hence thesis by A1,SGRAPH1:10;
end;

theorem Th2:
  2Set Seg 0 = {} & 2Set Seg 1 = {}
proof
  thus 2Set Seg 0 = {}
  proof
    assume 2Set Seg 0 <> {};
    then consider x such that A1:x in 2Set Seg 0 by XBOOLE_0:def 1;
    ex i,j st i in Seg 0 & j in Seg 0 & i < j & x={i,j} by A1,Th1;
    hence thesis by FINSEQ_1:4;
  end;
  thus 2Set Seg 1 = {}
  proof
    assume 2Set Seg 1 <> {};
    then consider x such that A1:x in 2Set Seg 1 by XBOOLE_0:def 1;
    consider i,j such that 
      A2:i in Seg 1 & j in Seg 1 & i < j & x={i,j} by A1,Th1;
    i=1 & j=1 by A2,TARSKI:def 1,FINSEQ_1:4;
    hence thesis by A2;
  end;
end;

theorem Th3:
  for n st n >= 2 holds {1,2} in 2Set Seg n
proof
  let n such that A1:n>=2;
  1<=n by A1,XXREAL_0:2;
  then 1 in Seg n & 2 in Seg n by A1;
  hence thesis by Th1;
end;

registration let n;
  cluster 2Set Seg(n+2) -> non empty finite;
  coherence
  proof
    n+2>=0+2 by XREAL_1:8;
    hence thesis by Th3,SGRAPH1:15;
  end;
end;

registration let n, x;
  let perm be Element of Permutations(n);
  cluster perm.x -> natural;
  coherence
  proof
    per cases;
      suppose A1:x in dom perm;
        perm is Permutation of Seg n by MATRIX_2:def 11;
        then perm.x in rng perm &rng perm=Seg n 
          by A1,FUNCT_2:def 3,FUNCT_1:def 5;
        then perm.x is Element of NAT;
        hence thesis;end;
      suppose not x in dom perm;
        hence thesis by FUNCT_1:def 4;end;
   end;
end;

registration let K;
  cluster the mult of K -> having_a_unity;
  coherence by FVSUM_1:11;
  cluster the mult of K -> associative;
  coherence by GROUP_1:31;
end;

     ::------------------------------------------::
     ::     The partial sign of a permutation    ::
     ::------------------------------------------::

definition let n,K;
  let perm2 be Element of Permutations(n+2);
  func Part_sgn(perm2,K) -> Function of 2Set Seg (n+2),the carrier of K means 
:Def1:  for i,j be Element of NAT st i in Seg (n+2)& j in Seg (n+2) & i < j 
      holds
        ( perm2.i < perm2.j implies it.{i,j}= 1_K ) &
        ( perm2.i > perm2.j implies it.{i,j}=-1_K );
  existence
  proof
    set n'=n+2;
    defpred P[set,set] means 
    for i,j be Element of NAT st i in Seg n' & j in Seg n' & i < j & $1={i,j} 
    holds
      ( perm2.i < perm2.j implies $2=1_K) &
      ( perm2.i > perm2.j implies $2=-1_K);
    A1:for x st x in 2Set Seg n' ex y st y in the carrier of K & P[x,y]
    proof
      let x such that A2:x in 2Set Seg n';
      consider i,j such that 
        A3:i in Seg n' & j in Seg n' & i<j & x={i,j} by Th1,A2;
      perm2 is Permutation of Seg n' by MATRIX_2:def 11;
      then A4:perm2.i <> perm2.j by A3,FUNCT_2:25;
      now per cases by A4,REAL_1:def 5;
        case A4:perm2.i < perm2.j;
          P[x,1_K]
            proof            
              let i',j' be Element of NAT such that
                A5:i' in Seg n' & j' in Seg n' & i' < j' & x={i',j'};
              (i=i' & j=j') or (i=j' & j=i') by A3,A5,ZFMISC_1:28;
              hence thesis by A4,A3,A5; 
            end;     
          hence thesis;end;
        case A4:perm2.i > perm2.j;
          P[x,-1_K]
            proof              
              let i',j' be Element of NAT such that
                A5:i' in Seg n' & j' in Seg n' & i' < j' & x={i',j'};
              (i=i' & j=j') or (i=j' & j=i') by A3,A5,ZFMISC_1:28;
              hence thesis by A4,A3,A5;
            end;
          hence thesis;end;
      end;
      hence thesis;
    end;
    consider Path being Function of 2Set Seg n',the carrier of K such that
      A2:for x st x in 2Set Seg n' holds P[x,Path.x] from FUNCT_2:sch 1(A1);
    take Path;    
    let i,j be Element of NAT such that A3:i in Seg n' & j in Seg n' & i < j;
    {i,j} in 2Set Seg n' by A3,Th1;
    hence thesis by A2,A3;   
  end;
  uniqueness
  proof set n'=n+2;
    let P1,P2 be Function of 2Set Seg n',the carrier of K such that  
    A1:for i,j be Element of NAT st i in Seg n' & j in Seg n' & i < j 
      holds ( perm2.i < perm2.j implies P1.{i,j}=1_K ) &
        ( perm2.i > perm2.j implies P1.{i,j}=-1_K ) and
    A2:for i,j be Element of NAT st i in Seg n' & j in Seg n' & i < j
      holds ( perm2.i < perm2.j implies P2.{i,j}=1_K ) &
        ( perm2.i > perm2.j implies P2.{i,j}=-1_K );
    for x st x in 2Set Seg n' holds P1.x = P2.x
    proof
      let x such that A3:x in 2Set Seg n';
      consider i,j such that 
        A4:i in Seg n' & j in Seg n' & i < j & x={i,j} by Th1,A3;
      perm2 is Permutation of Seg n' by MATRIX_2:def 11;
      then A5:perm2.i <> perm2.j by A4,FUNCT_2:25;
      now per cases by A5,REAL_1:def 5;
        case perm2.i < perm2.j;
          then P1.{i,j}=1_K & P2.{i,j}=1_K by A1,A2,A4;
          hence thesis by A4;end;
        case perm2.i > perm2.j;
          then P1.{i,j}=-1_K & P2.{i,j}=-1_K by A1,A2,A4;
          hence thesis by A4;end;
      end;
      hence thesis;
    end;
    hence thesis by FUNCT_2:18;
  end;
end;

theorem Th4:
  for X be Element of Fin 2Set Seg (n+2) st
    for x st x in X holds Part_sgn(p2,K).x = 1_K holds
      (the mult of K) $$ (X,Part_sgn(p2,K)) = 1_K
proof
  let X be Element of Fin 2Set Seg (n+2) such that
    A1:for x st x in X holds Part_sgn(p2,K).x = 1_K;
  set mm = the mult of K;
  set KK = the carrier of K;
  set 2S = 2Set Seg (n+2);
  set Path = Part_sgn(p2,K);
  consider G be Function of Fin 2S,KK such that
    A2:mm $$ (X,Path) = G.X and
    A3:for e being Element of KK st e is_a_unity_wrt mm holds G.{} = e and
    A4:for x being Element of 2S holds G.{x} = Path.x and
    A5:for B being Element of Fin 2S st B c= X & B <> {}
      for x being Element of 2S st x in X \ B holds G.(B \/ {x}) = 
      mm.(G.B,Path.x) by SETWISEO:def 3;
  defpred P[Nat] means 
     for B be Element of Fin 2S st card B=$1 & B c= X holds G.B=1_K;
  A6:P[0]
  proof
    let B be Element of Fin 2S such that A7:card B=0 & B c= X;
    B={} & 1_K is_a_unity_wrt mm by A7,CARD_2:59,FVSUM_1:6;
    hence thesis by A3;
  end;
  A7:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A8:P[k];set k1=k+1;
    let B be Element of Fin 2S such that A9:card B=k1 & B c= X;
    now per cases;
      case k=0;
        then consider x such that
          A10:B={x} by A9,CARD_2:60;
        A11:x in B & B c= 2S by A10,FINSUB_1:def 5,TARSKI:def 1;
        then reconsider x as Element of 2S;
        G.B=Path.x & Path.x=1_K by A1,A4,A9,A10,A11;
        hence thesis;end;
      case A10:k>0;
        consider x such that
          A11:x in B by XBOOLE_0:def 1,A9,CARD_1:78;
        B c= 2S by FINSUB_1:def 5;
        then reconsider x as Element of 2S by A11;
        B\{x} c= B & B c= 2S by FINSUB_1:def 5;
        then B\{x} c= 2S & B\{x} c= X by A9,XBOOLE_1:1;
        then reconsider B'=B\{x} as Element of Fin 2S by FINSUB_1:def 5;        
        A13:{x} \/ B'=B & not x in B' by A11,ZFMISC_1:140,64;
        then k+1=card B'+1 & x in X\B' & B' c= X & x in X
          by A9,A11,CARD_2:54,XBOOLE_0:def 4,XBOOLE_1:1;
        then G.B'=1_K & G.B=mm.(G.B',Path.x) & Path.x=1_K
          by A1,A5,A8,A10,A13,CARD_1:78;
        then G.B=1_K * 1_K by GROUP_1:def 1;
        hence thesis by VECTSP_1:def 13; end;
    end;
    hence thesis;
  end;
  for k be Element of NAT holds P[k] from NAT_1:sch 1(A6,A7);  
  then P[card X];
  hence thesis by A2;
end;

reserve s for Element of 2Set Seg (n+2);

theorem Th5:
  Part_sgn(p2,K).s = 1_K or Part_sgn(p2,K).s = -1_K
proof
  consider i,j such that
    A1:i in Seg (n+2) & j in Seg (n+2) & i < j & s={i,j} by Th1;
  p2 is Permutation of Seg (n+2) by MATRIX_2:def 11;
  then p2.i <> p2.j by A1,FUNCT_2:25;
  then p2.i > p2.j or p2.i < p2.j by REAL_1:def 5;
  hence thesis by A1,Def1;
end;

theorem Th6:
  for i,j st i in Seg (n+2) & j in Seg (n+2) 
       & i < j & p2.i = q2.i & p2.j = q2.j
    holds Part_sgn(p2,K).{i,j} = Part_sgn(q2,K).{i,j}
proof 
  set n2=n+2;
  let i,j such that 
    A1:i in Seg n2 & j in Seg n2 & i<j and 
    A2:p2.i=q2.i & p2.j=q2.j;
  reconsider q2'=q2,p2'=p2 as Permutation of Seg n2 by MATRIX_2:def 11;   
  A3:p2'.i<>p2'.j by A1,FUNCT_2:25;
  now per cases by A3,REAL_1:def 5;
    suppose p2.i < p2.j;
      then Part_sgn(p2,K).{i,j}=1_K & Part_sgn(q2,K).{i,j}=1_K
        by A1,A2,Def1;
      hence thesis;
    end;
    suppose p2.i > p2.j;
      then Part_sgn(p2,K).{i,j}=-1_K & Part_sgn(q2,K).{i,j}=-1_K
        by A1,A2,Def1;
      hence thesis;
    end;
  end;
  hence thesis;  
end;

theorem Th7:
  for X be Element of Fin 2Set Seg (n+2),p2,q2 
    for F be finite set st 
      F={s:s in X & Part_sgn(p2,K).s <> Part_sgn(q2,K).s}
    holds
      (card F mod 2 = 0 implies (the mult of K) $$ (X,Part_sgn(p2,K)) =
                                (the mult of K) $$ (X,Part_sgn(q2,K))  ) &
      (card F mod 2 = 1 implies (the mult of K) $$ (X,Part_sgn(p2,K)) =
                               -(the mult of K) $$ (X,Part_sgn(q2,K))  )
proof
  let X be Element of Fin 2Set Seg (n+2);
  let p2,q2;
  let F be finite set such that 
    A1:F={s:s in X & Part_sgn(p2,K).s<>Part_sgn(q2,K).s};
  set mm=the mult of K;
  set KK=the carrier of K;
  set 2S=2Set Seg (n+2);
  set Pp=Part_sgn(p2,K);
  set Pq=Part_sgn(q2,K);
  A:F c= X
  proof
    let x;
    assume x in F;
    then ex s st x=s & s in X & Pp.s<>Pq.s by A1;
    hence thesis;
  end;
  X c= 2S by FINSUB_1:def 5;
  then F c= 2S by A,XBOOLE_1:1;
  then reconsider F'=F as Element of Fin 2S by FINSUB_1:def 5;
  consider Gp be Function of Fin 2S,KK such that
    B1:mm $$ (F',Pp) = Gp.F and
    B2:for e be Element of KK st e is_a_unity_wrt mm holds Gp.{} = e and
    B3:for x be Element of 2S holds Gp.{x} = Pp.x and
    B4:for B be Element of Fin 2S st B c= F & B <> {}
      for x being Element of 2S st x in F' \ B holds Gp.(B\/{x})= 
      mm.(Gp.B,Pp.x) by SETWISEO:def 3;   
  consider Gq be Function of Fin 2S,KK such that
    C1:mm $$ (F',Pq) = Gq.F and
    C2:for e be Element of KK st e is_a_unity_wrt mm holds Gq.{} = e and
    C3:for x be Element of 2S holds Gq.{x} = Pq.x and
    C4:for B be Element of Fin 2S st B c= F & B <> {}
      for x be Element of 2S st x in F \ B holds Gq.(B\/{x})= 
      mm.(Gq.B,Pq.x) by SETWISEO:def 3;
  defpred P[Nat] means 
    for B be Element of Fin 2S st card B=$1 & B c= F holds 
       (card B mod 2=0 implies Gp.B=Gq.B) & 
       (card B mod 2=1 implies Gp.B=-Gq.B);
  A2:P[0]
    proof
      let B be Element of Fin 2S such that A3:card B=0 & B c= F;
      B={} & 1_K is_a_unity_wrt mm & 0=0 mod 2
        by A3,CARD_2:59,FVSUM_1:6,NAT_D:26;
      then Gp.B=1_K & Gq.B=1_K & 0=0 mod 2 by B2,C2;
      hence thesis by A3;
    end;
  A3:for s st s in F holds Pp.s = -Pq.s
  proof
    let s such that A4:s in F;
    consider s' be Element of 2S such that
      A5:s'=s & s' in X & Pp.s'<>Pq.s' by A1,A4;
    (Pp.s=1_K or Pp.s=-1_K) & (Pq.s=1_K or Pq.s=-1_K) by Th5;
    then Pp.s+Pq.s=0.K by A5,RLVECT_1:def 10;
    hence thesis by VECTSP_1:63;
  end;   
  A4:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A5:P[k];set k1=k+1;
    let B be Element of Fin 2S such that A6:card B=k1 & B c= F;
    now per cases;
      case A7:k=0;
         then consider x such that
           A8:B={x} by A6,CARD_2:60;
         A9:x in B & B c= 2S by A8,FINSUB_1:def 5,TARSKI:def 1;
         then reconsider x as Element of 2S;
         Gp.B=Pp.x & Gq.B=Pq.x & x in F
           by A6,A8,A9,B3,C3;
         hence thesis by A6,A3,A7,NAT_D:14;end;
      case A7:k>0;
         consider x such that
           A8:x in B by XBOOLE_0:def 1,A6,CARD_1:78;
         B c= 2S by FINSUB_1:def 5;
         then reconsider x as Element of 2S by A8;
         B\{x} c= B & B c= 2S by FINSUB_1:def 5;
         then B\{x} c= 2S & B\{x} c= F by A6,XBOOLE_1:1;
         then reconsider B'=B\{x} as Element of Fin 2S by FINSUB_1:def 5;   
         A11:{x} \/ B'=B & not x in B' & x in F
           by A6,A8,ZFMISC_1:140,64;
         then A12:k+1=card B'+1 & x in F\B' & B' c= F & x in F 
           by A6,CARD_2:54,XBOOLE_0:def 4,XBOOLE_1:1;
         then A13:Gp.B= mm.(Gp.B',Pp.x) & Gq.B= mm.(Gq.B',Pq.x) 
           by A7,A11,B4,C4,CARD_1:78;
         now per cases by NAT_D:12;
           case A14:k mod 2=0;
              then Gp.B'=Gq.B' & Pp.x=-Pq.x by A3,A5,A12;
              then Gp.B=Gp.B'*(-Pq.x) & Gq.B=Gp.B'*Pq.x & 0<2-1
                by A13,GROUP_1:def 1;
              then Gp.B=-Gq.B & k1 mod 2 =0 +1 by A14,VECTSP_1:40,RADIX_1:3;
              hence thesis by A6;end;
           case A14:k mod 2=1;
              then Gp.B'=-Gq.B' & Pp.x=-Pq.x by A3,A5,A12;
              then Gp.B=(-Gq.B')*(-Pq.x) & Gq.B=Gq.B'*Pq.x &2-1=1
                by A13,GROUP_1:def 1;
              hence thesis by A6,A14,VECTSP_1:42,RADIX_1:2;end;
         end;
         hence thesis;end;
    end;
    hence thesis;
  end;
  A5:for k be Element of NAT holds P[k] from NAT_1:sch 1(A2,A4);
  X c= 2S & X\F c= X by FINSUB_1:def 5;
  then X\F c= 2S by XBOOLE_1:1;
  then reconsider Y=X\F as Element of Fin 2S by FINSUB_1:def 5;
  dom Pp=2S & dom Pq=2S & Y c=2S 
    by FUNCT_2:def 1,FINSUB_1:def 5;
  then A6:dom (Pp|Y)=Y & dom (Pq|Y)=Y  by RELAT_1:91;
  for x st x in dom (Pp|Y) holds (Pp|Y).x=(Pq|Y).x
    proof
      let x such that A7:x in dom (Pp|Y); 
      assume A8:(Pp|Y).x<>(Pq|Y).x;
      Y c= 2S by FINSUB_1:def 5;
      then reconsider x'=x as Element of 2S by A6,A7;
      (Pp|Y).x'=Pp.x' & (Pq|Y).x'=Pq.x' & x' in X & not x' in F
        by A6,A7,FUNCT_1:70,XBOOLE_0:def 4;     
      hence contradiction by A1,A8;
    end;
  then Pp|Y=Pq|Y by A6,FUNCT_1:9;
  then A7:mm $$(Y,Pp) = mm $$(Y,Pq) by SETWOP_2:9;
  Y misses F & F\/Y=X by A,XBOOLE_1:79,45;
  then A8:mm $$(X, Pp) = mm.(mm $$(Y,Pp),mm $$(F',Pp)) & 
      mm $$(X, Pq) = mm.(mm $$(Y,Pq),mm $$(F',Pq)) by SETWOP_2:6;
  now per cases by NAT_D:12;
    case card F mod 2=0;
      hence thesis by A5,A7,A8,B1,C1;end;
    case A9:card F mod 2=1;
      then mm $$ (F',Pp)=-mm $$ (F',Pq) by A5,B1,C1;
      then mm $$(X, Pp) = (mm $$(Y,Pp))*(-mm $$(F',Pq)) &
        mm $$(X, Pq) = (mm $$(Y,Pp))*(mm $$(F',Pq)) by A7,A8,GROUP_1:def 1;
      hence thesis by A9,VECTSP_1:40;end;
  end;
  hence thesis;
end;

theorem Th8:
  for P be Permutation of Seg n st P is_transposition 
    for i,j st i < j holds P.i = j iff 
        i in dom P & j in dom P &
        P.i=j & P.j=i & for k st k <> i & k <> j & k in dom P holds P.k = k
proof
  let P such that A1:P is_transposition;
  let i,j such that A2:i < j;
  consider i',j' be Nat such that
    i' in dom P & j' in dom P and 
    A4:i'<>j' & P.i'=j' & P.j'=i' and
    A5:for k st k <>i' & k<>j' & k in dom P holds P.k=k by A1,MATRIX_2:def 14;
  thus P.i=j implies (i in dom P & j in dom P &
      P.i=j & P.j=i & for k st k<>i & k<>j & k in dom P holds P.k = k)
  proof
    assume A6:P.i=j;
    then A7:i in dom P by FUNCT_1:def 4,A2;
    then A8:P.i in rng P & rng P=Seg n & dom P=Seg n 
      by FUNCT_1:def 5,FUNCT_2:def 3,67;
    then P.j<>j by A2,A6,A7,FUNCT_1:def 8;
    then (j=j' or j=i') & (i=j' or i=i') by A5,A6,A7,A8;
    hence thesis by A4,A5,A6,A8,FUNCT_1:def 5;
  end;
  thus thesis;
end;

theorem Th9:
  for p2,q2,pq2,i,j st pq2 = p2*q2 & q2 is_transposition & q2.i = j & i < j
    for s st Part_sgn(p2,K).s <> Part_sgn(pq2,K).s
      holds i in s or j in s
proof
  set n2=n+2;
  let p, q, pq be Element of Permutations(n2), i, j such that
    A1:pq = p*q and
    A2:q is_transposition & q.i=j & i < j;
  let s be Element of 2Set Seg(n2) such that 
    A3:Part_sgn(p,K).s<>Part_sgn(pq,K).s;
  consider i',j' be Nat such that
    A4:i' in Seg n2 & j' in Seg n2 & i' < j' & s={i',j'} by Th1;
  reconsider q'=q,pq'=pq as Permutation of Seg n2 by MATRIX_2:def 11; 
  assume not i in s & not j in s;
  then i<>i' & i<>j' & j<>i' & j<>j' & dom q'=Seg n2
      by FUNCT_2:67, A4,TARSKI:def 2; 
  then q.i'=i' & q.j'=j' & dom pq'=Seg n2 
    by A2,A4,Th8,FUNCT_2:67;
  then pq.i'=p.i' & pq.j'=p.j' by A1,A4,FUNCT_1:22;
  hence contradiction by A3,A4,Th6;
end;
     
LM1:for i,j st i in Seg (n+2) & j in Seg (n+2) & i < j & 1_K<>-1_K holds
  (Part_sgn(p2,K).{i,j}=1_K implies p2.i<p2.j)&
  (Part_sgn(p2,K).{i,j}=-1_K implies p2.i>p2.j)
proof set n2=n+2;
  let i,j such that A1: i in Seg n2 & j in Seg n2 & i < j & 1_K<>-1_K;
  reconsider p'=p2 as Permutation of Seg n2 by MATRIX_2:def 11;
  thus Part_sgn(p2,K).{i,j}=1_K implies p2.i<p2.j
  proof
    assume A2:Part_sgn(p2,K).{i,j}=1_K;
    p'.i<>p'.j by A1,FUNCT_2:25;
    then (p2.i<p2.j or p2.i>p2.j) &{i,j} in 2Set Seg n2 by A1,REAL_1:def 5,Th1;
    hence thesis by A1,A2,Def1;
  end;
  assume A2:Part_sgn(p2,K).{i,j}=-1_K;
  p'.i<>p'.j by A1,FUNCT_2:25;
  then (p2.i<p2.j or p2.i>p2.j) & {i,j} in 2Set Seg n2 by A1,REAL_1:def 5,Th1;
  hence thesis by A1,A2,Def1;
end;

theorem Th10:
  for p2,q2,pq2,i,j,K st 
      pq2 = p2*q2 & q2 is_transposition & q2.i = j & i < j & 1_K <> -1_K
    holds 
      Part_sgn(p2,K).{i,j} <> Part_sgn(pq2,K).{i,j} &
      for k st k in Seg(n+2) & i <> k & j <> k holds
           Part_sgn(p2,K).{i,k} <> Part_sgn(pq2,K).{i,k} 
                           iff Part_sgn(p2,K).{j,k} <> Part_sgn(pq2,K).{j,k}
proof 
  set n2=n+2;
  let p, q, pq be Element of Permutations(n2), i, j, K such that
    A1:pq = p*q & q is_transposition and 
    A2:q.i=j & i < j & 1_K <> -1_K;
  set P1=Part_sgn(p,K);set P2=Part_sgn(pq,K);
  reconsider p'=p,q'=q,pq'=pq as Permutation of Seg n2 by MATRIX_2:def 11; 
  A3:q.i=j & q.j=i & i in dom q & j in dom q & 
       dom pq'=Seg n2 & dom q'=Seg n2 & dom p'=Seg n2
    by A1,A2,Th8,FUNCT_2:67;
  then A4:pq.i=p.j & pq.j=p.i & p'.i<>p'.j by A2,FUNCT_1:def 8,A1,FUNCT_1:22;
  now per cases by A4,REAL_1:def 5;
    suppose p.i < p.j;
      then P1.{i,j}=1_K & P2.{i,j}=-1_K by A2,A3,A4,Def1;
      hence P1.{i,j}<>P2.{i,j} by A2;end;
    suppose p.i > p.j;
      then P1.{i,j}=-1_K & P2.{i,j}=1_K by A2,A3,A4,Def1;
      hence P1.{i,j}<>P2.{i,j} by A2;end;
  end;
  hence P1.{i,j}<>P2.{i,j};
  let k such that
  A5:k in Seg n2 & i <> k & j <> k;
  A6:p.k<>p.i & p.k<>p.j & q.k=k & pq.k=p.(q.k)
         by A5,A3,A2,A1,FUNCT_1:22,def 8,Th8;
      (j<k or k<j) & (i<k or k<i) by A5,REAL_1:def 5;then
  A7:{i,k} in 2Set Seg n2 & {j,k} in 2Set Seg n2
        by A3,A5,Th1;
  A8:P1.{i,k} = P2.{i,k} implies P1.{j,k} = P2.{j,k}
     proof
       assume A9:P1.{i,k} = P2.{i,k};
       (P1.{k,i}=1_K or P1.{k,i}=-1_K) &(i<k or i>k) by A5,Th5,A7,REAL_1:def 5;
       then ((pq.j<pq.k & p.j<p.k) or (pq.j>pq.k & p.j>p.k)) & (j<k or k<j)
         by A2,A4,A5,A6,A9,LM1,REAL_1:def 5,A3;
       then (P2.{j,k}=1_K & P1.{j,k}=1_K) or (P2.{j,k}=-1_K & P1.{j,k}=-1_K)
         by A3,A5,Def1;hence thesis;
     end;  
   P1.{j,k} = P2.{j,k} implies P1.{i,k} = P2.{i,k}
     proof
       assume A10:P1.{j,k} = P2.{j,k};
       (P1.{k,j}=1_K or P1.{k,j}=-1_K) &(j<k or j>k) by A5,Th5,A7,REAL_1:def 5;
       then ((pq.i<pq.k & p.i<p.k) or (pq.i>pq.k & p.i>p.k)) & (i<k or k<i)
         by A2,A4,A5,A6,A10,LM1,REAL_1:def 5,A3;
       then (P2.{i,k}=1_K & P1.{i,k}=1_K) or (P2.{i,k}=-1_K & P1.{i,k}=-1_K)
         by A3,A5,Def1;hence thesis;
     end;
  hence thesis by A8;
end;

     ::------------------------------------------::
     ::        The sign of a permutation         ::
     ::------------------------------------------::

definition let n,K;
  let perm2 be Element of Permutations(n+2);
  func sgn(perm2,K) -> Element of K equals
    (the mult of K) $$ (FinOmega(2Set Seg (n+2)),Part_sgn(perm2,K));
  coherence;
end;

theorem Th11:
  sgn(p2,K) = 1_K or sgn(p2,K) = -1_K
proof
  set KK=the carrier of K;set n2=n+2;set 2S=2Set Seg n2;
  set mm=the mult of K;set Path=Part_sgn(p2,K);
  A1:FinOmega 2S=2S by MATRIX_2:def 17;
  then reconsider 2S'=2S as Element of Fin 2S;
  consider G be Function of Fin 2S, KK such that
    A2:mm $$(2S',Path) = G.2S' and
    A3:for e be Element of KK st e is_a_unity_wrt mm holds G.{} = e and
    A4:for s holds G.{s} = Path.s and
    A5:for B be Element of Fin 2S st B c= 2S' & B <> {} for s st s in 2S' \ B
         holds G.(B \/ {s}) = mm.(G.B,Path.s) by SETWISEO:def 3;
   defpred P[Nat] means 
     for B be Element of Fin 2S st card B=$1 & B c= 2S 
       holds (G.B=1_K or G.B=-1_K);
  A6:P[0]
  proof
    let B be Element of Fin 2S such that A7:card B=0 & B c= 2S;
    B={} & 1_K is_a_unity_wrt mm by A7,CARD_2:59,FVSUM_1:6;
    hence thesis by A3;
  end;
  A7:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A8:P[k];set k1=k+1;
    let B be Element of Fin 2S such that A9:card B=k1 & B c= 2S;
    now per cases;
      case k=0;
        then consider x such that
    A10:B={x} by A9,CARD_2:60;
        x in B by A10,TARSKI:def 1;
        then reconsider x as Element of 2S by A9;
        G.B=Path.x & (Path.x=1_K or Path.x=-1_K) by A4,A10,Th5;
        hence thesis;end;
      case A10:k>0;
        consider x such that
          A11:x in B by XBOOLE_0:def 1,A9,CARD_1:78;      
        reconsider x as Element of 2S by A9,A11;
        A12:B\{x} c= 2S by A9,XBOOLE_1:1;
        then reconsider B'=B\{x} as Element of Fin 2S by FINSUB_1:def 5;        
        A13:{x}\/ B'=B & not x in B' by A11,ZFMISC_1:140,64;
        then k+1=card B'+1 & x in 2S\B' & x in 2S
          by A9,CARD_2:54,XBOOLE_0:def 4;
        then (G.B'=1_K or G.B'=-1_K) &(Path.x=1_K or Path.x=-1_K) &
          G.B=mm.(G.B',Path.x) by A5,A8,A10,A12,A13,Th5,CARD_1:78;
        then G.B=1_K*1_K or G.B=1_K*(-1_K) or G.B=(-1_K)*1_K or
          G.B=(-1_K)*(-1_K) by GROUP_1:def 1;
        then G.B=1_K*1_K or G.B=1_K*(-1_K) by VECTSP_1:42;
        hence thesis by VECTSP_1:def 13;end;
    end;
    hence thesis;
  end;
  for k be Element of NAT holds P[k] from NAT_1:sch 1(A6,A7);  
  then P[card 2S'];
  hence thesis by A1,A2;
end;

theorem Th12:
  for Id be Element of Permutations(n+2) st Id = idseq (n+2) holds
    sgn(Id,K) = 1_K
proof 
  set n2=n+2;
  let Id be Element of Permutations n2 such that A1:Id=idseq n2;  
  set 2S=2Set Seg n2;
  set mm=the mult of K;
  set Path=Part_sgn(Id,K);
  A2:FinOmega 2S=2S by MATRIX_2:def 17;
  then reconsider 2S'=2S as Element of Fin 2S;
  now let x; 
    assume x in 2S';
    then consider i,j such that
      A3:i in Seg n2 & j in Seg n2 & i < j & x={i,j} by Th1;
    Id.i=i & Id.j=j by A1,A3,FUNCT_1:35;
    hence Path.x=1_K by A3,Def1;
  end;
  hence thesis by A2,Th4;
end;

LM2:X in 2Set Seg n & i in X implies 
   i in Seg n & ex j st j in Seg n & i <> j & X={i,j}
proof
  assume A1:X in 2Set Seg n & i in X;
  then consider i',j' be Nat such that
    A2:i' in Seg n & j' in Seg n & i' < j' & X={i',j'} by Th1;
  now per cases by A1,A2,TARSKI:def 2;
    case i=i';hence thesis by A2;end;
    case i=j';hence thesis by A2;end;
  end;
  hence thesis;
end;

theorem Th13:
  for p2,q2,pq2 st pq2 = p2*q2 & q2 is_transposition holds
    sgn(pq2,K) = -sgn(p2,K)
proof 
  set n2=n+2;
  set 2SS=2Set Seg n2;
  A0:FinOmega 2SS=2SS by MATRIX_2:def 17;
  then reconsider 2S=2SS as Element of Fin 2SS; 
  let p, q, pq be Element of Permutations n2 such that 
  A1:pq = p*q & q is_transposition;
  A2:for i,j st i < j & q.i=j holds sgn(pq,K)=-sgn(p,K)
  proof
    let i,j such that A3:i < j & q.i=j;
    now per cases;
      suppose 1_K=-1_K;
      then sgn(pq,K)=-1_K & sgn(p,K)=1_K by Th11;
      hence thesis;
     end;
      suppose A4:1_K<>-1_K;
        set P1=Part_sgn(pq,K);
        set P2=Part_sgn(p,K);
        set D={s:s in 2S & Part_sgn(pq,K).s<>Part_sgn(p,K).s};
        D c= 2S
          proof 
            let x;
            assume x in D;
            then ex s st x=s & s in 2S & P1.s<>P2.s;
            hence thesis;
          end;
        then reconsider D as finite set by FINSET_1:13;
        set D1={s:s in 2S & P1.s<>P2.s & i in s};
        set D2={s:s in 2S & P1.s<>P2.s & j in s};
        A5:D1 c= D
          proof 
            let x; 
            assume x in D1;
            then ex s st x=s & s in 2S & P1.s<>P2.s & i in s;
            hence thesis;
          end;
        A6:D2 c= D
          proof 
            let x; 
            assume x in D2;
            then ex s st x=s & s in 2S & P1.s<>P2.s & j in s;
            hence thesis;
          end;
        then reconsider D1,D2 as finite set by A5,FINSET_1:13;
        A7:D c= D1\/D2
          proof 
            let x; 
            assume x in D;
            then consider s such that 
              A8:x=s & s in 2S & P1.s<>P2.s;
            i in s or j in s by A1,A3,A8,Th9;
            then x in D1 or x in D2 by A8;
            hence thesis by XBOOLE_0:def 2;
          end;
        q is Permutation of Seg n2 by MATRIX_2:def 11; 
        then A8':i in dom q &j in dom q & dom q=Seg n2 by A1,A3,Th8,FUNCT_2:67;
        then B8:i in {i,j} & j in {i,j} & {i,j} in 2S & P1.{i,j}<>P2.{i,j} 
          by A1,A3,A4,Th1,TARSKI:def 2,Th10;
        then {i,j} in D1 & {i,j} in D2;
        then A8:{i,j} in D1/\D2 by XBOOLE_0:def 3;
        A9:D1/\D2 c= {{i,j}}
        proof
          let x; 
          assume x in D1/\D2;
          then A10:x in D1 & x in D2 by XBOOLE_0:def 3;
          then consider s1 be Element of 2SS such that 
            A11:x=s1 & s1 in 2S & P1.s1<>P2.s1 & i in s1; 
          consider s2 be Element of 2SS such that 
            A12:x=s2 & s2 in 2S & P1.s2<>P2.s2 & j in s2 by A10;
          consider i',j' be Nat such that
            A13:i' in Seg n2 & j' in Seg n2 & i'<j' & {i',j'}=x by A11,Th1;
          (i=i' or  i=j') & (j=i' or j=j') by A11,A12,A13,TARSKI:def 2;
          hence thesis by A3,A13,TARSKI:def 1;
        end;
        defpred P[set,set] means for k st k in $1 & k<>i holds 
          (k<>j implies $2={j,k}) &(k=j implies $2={i,j});
        A10:for x st x in D1 ex y be set st y in D2 & P[x,y]
        proof
          let x;
          assume x in D1;
          then consider s such that 
            A11:x=s & s in 2S & P1.s<>P2.s & i in s;
          consider j' be Nat such that
            A12:j' in Seg n2 & j'<>i & s={i,j'} by A11,LM2;
          now per cases;
            suppose A13:j'=j;
              take X={i,j};thus X in D2 by B8;
              let k such that A14:k in x & k<>i;
              thus (k<>j implies X={j,k}) &(k=j implies X={i,j}) 
                by A11,A12,A14,TARSKI:def 2,A13;end;
            suppose A13:j'<>j;
              take X={j,j'};
              (j<j' or j>j') by A13,REAL_1:def 5;
              then P1.X<>P2.X & j in X & X in 2SS 
                by A1,A3,A4,A8',A11,A12,Th1,Th10,TARSKI:def 2;
              hence X in D2;
              let k such that A14:k in x & k<>i;
              thus (k<>j implies X={j,k}) &(k=j implies X={i,j})      
                by A11,A12,A13,A14,TARSKI:def 2;end;
          end;
          hence thesis;
        end;
        consider f be Function of D1,D2 such that 
A11:      for x st x in D1 holds P[x,f.x] from FUNCT_2:sch 1(A10);
A12:    for x1,x2 be set st x1 in D1 & x2 in D1 & f.x1 = f.x2 holds x1 = x2
          proof
            let x1,x2 be set such that A13:x1 in D1 & x2 in D1 & f.x1 = f.x2;
            consider s1 be Element of 2SS such that 
              A14:x1=s1 & s1 in 2S & P1.s1<>P2.s1 & i in s1 by A13;
            consider j1 be Nat such that
              A15:j1 in Seg n2 & i<>j1 & {i,j1}=s1 by A14,LM2;
            consider s2 be Element of 2SS such that 
              A16:x2=s2 & s2 in 2S & P1.s2<>P2.s2 & i in s2 by A13;
            consider j2 be Nat such that
              A17:j2 in Seg n2 & i<>j2 & {i,j2}=s2 by A16,LM2;
            A18:j1 in s1 & j2 in s2 by A15,A17,TARSKI:def 2;
            now per cases;
              case j=j1 & j=j2;
                hence thesis by A14,A15,A16,A17;end;
              case j<>j1 & j=j2;      
                then f.x1={j,j1} & f.x2={i,j} & i in {i,j} 
                  by A11,A13,A14,A15,A16,A17,A18;
                hence thesis by A3,A13,A15,TARSKI:def 2;end;
              case j=j1 & j<>j2;
                then f.x1={i,j} & f.x2={j,j2} & i in {i,j} 
                  by A11,A13,A14,A15,A16,A17,A18;
                hence thesis by A3,A13,A17,TARSKI:def 2;end;
              case A19:j<>j1 & j<>j2;
                then f.x1={j,j1} & f.x2={j,j2} & j1 in {j,j1} 
                  by A11,A13,A14,A15,A16,A17,A18,TARSKI:def 2;
                hence thesis by A13,A14,A15,A16,A17,A19,TARSKI:def 2;end;
            end;hence thesis;
          end;
A13:    for y be set st y in D2 ex x st x in D1 & y = f.x
          proof
            let y be set; assume y in D2;
            then consider s such that
              A14:s=y & s in 2S & P1.s<>P2.s & j in s;
            consider i1 be Nat such that
              A15:i1 in Seg n2 & i1<>j & s={j,i1} by A14,LM2;
            now per cases;
              suppose i1=i;
                then s={i,j} & {i,j} in D1 & j in {i,j} by B8,A15;
                then f.s=y & s in D1 by A3,A11,A14;
                hence thesis;end;
              suppose A16:i1<>i;
                then i<i1 or i>i1 by REAL_1:def 5;
                then P1.{i,i1}<>P2.{i,i1} & {i,i1} in 2SS & i in {i,i1}
                  by A14,A15,A1,A3,A4,A8',Th1,Th10,TARSKI:def 2;
                then {i,i1} in D1 & i1 in {i,i1} by TARSKI:def 2;
                then f.{i,i1}={j,i1} & {i,i1} in D1 by A15,A16,A11;
                hence thesis by A14,A15;end;
            end;
            hence thesis;
          end;
       {i,j} in D1 by B8;
       then card D1<>0 by CARD_2:59;
       then card D1>0;
       then reconsider c1=card D1-1 as Nat by NAT_1:20;
       {i,j} in D2 by B8;
       then f is one-to-one & rng f=D2 & dom f=D1 
         by FUNCT_2:16,def 1,25,A12,A13;
       then D1,D2 are_equipotent & D1\/D2 c= D & { {i,j}} c= D1/\D2
         by A5,A6,WELLORD2:def 4,XBOOLE_1:8,A8,ZFMISC_1:37;
       then card D1=card D2 & D1\/D2=D & D1/\D2 ={{i,j}} 
         by A7,A9,XBOOLE_0:def 10,CARD_1:21;
       then card D=card D1+card D1-card{{i,j}} by CARD_2:64
                 .=(c1+1)+(c1+1)-1 by CARD_1:50
                 .=2*c1+1;
       then card D mod 2=1 mod 2 & 1 mod 2=1 by NAT_D:14,21;
       hence thesis by Th7,A0;
     end;
    end;
    hence thesis;
  end;:: end A2
  consider i,j such that 
    A3:i in dom q & j in dom q & i<>j & q.i=j & q.j=i and
    for k st k <>i & k<>j & k in dom q holds q.k=k by A1,MATRIX_2:def 14;
  i<j or j<i by A3,REAL_1:def 5;
  hence thesis by A2,A3;
end;

     ::------------------------------------------::
     ::       The sign of a transposition        ::
     ::------------------------------------------::

theorem Th14:
  for tr be Element of Permutations(n+2) st 
    tr is_transposition holds sgn(tr,K) = -1_K
proof 
  set n2=n+2;
  set S=Seg n2;
  let tr be Element of Permutations(n2) such that A1:tr is_transposition;
  reconsider Tr=tr as Permutation of S by MATRIX_2:def 11;
  reconsider Id=idseq n2,IdTr=(id S)*Tr as Element of Permutations(n2) 
    by MATRIX_2:def 11;
  rng Tr=S by FUNCT_2:def 3;
  then IdTr=Tr by RELAT_1:80;
  then sgn(tr,K) = -sgn(Id,K) by A1,Th13;
  hence thesis by Th12;
end;

theorem Th15:
  for P be FinSequence of Group_of_Perm(n+2),
         p2 be Element of Permutations(n+2) st p2 = Product P &
      (for i st i in dom P ex trans be Element of Permutations(n+2) 
         st P.i = trans & trans is_transposition)
    holds 
      (len P mod 2=0 implies sgn(p2,K) =  1_K) &
      (len P mod 2=1 implies sgn(p2,K) = -1_K)
proof
  set n2=n+2;
  set S=Seg n2;
  set G=Group_of_Perm(n2);
  defpred P[Nat] means
     for P be FinSequence of G,p2 st
       p2=Product P & len P=$1 &
       (for i st i in dom P ex trans be Element of Permutations(n2) 
         st P.i=trans & trans is_transposition)
   holds 
     (len P mod 2=0 implies sgn(p2,K)=1_K) &
     (len P mod 2=1 implies sgn(p2,K)=-1_K);
  A1:P[0]
    proof
      let P be FinSequence of G,p2 such that
         A1:p2=Product P & len P=0 and
         for i st i in dom P ex trans be Element of Permutations(n2) 
            st P.i=trans & trans is_transposition;
      dom P= Seg 0 & Seg 0=0 by A1,FINSEQ_1:4,def 3;
      then P=<*>the carrier of G by RELAT_1:64;
      then Product P=1_G by GROUP_4:11;
      then Product P=idseq n2 by MATRIX_2:28;
      hence thesis by A1,NAT_D:26,Th12;
    end;
  A2:for k be Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT such that A3:P[k];set k1=k+1;
      let P be FinSequence of G,p2 such that
        A4:p2=Product P & len P=k1 and
        A5:for i st i in dom P ex trans be Element of Permutations(n2) 
             st P.i=trans & trans is_transposition;
      len P in Seg len P & Seg len P=dom P by A4,FINSEQ_1:5,def 3;      
      then consider x be set, Q be FinSequence such that
        A6:P = <*x*>^Q & len P = len Q+1 by REWRITE1:5,RELAT_1:60;
      reconsider X=<*x*>,Q as FinSequence of G by A6,FINSEQ_1:50;    
      reconsider PQ=Product Q as Element of Permutations(n2) 
        by MATRIX_2:def 13;
      A7:for i st i in dom Q ex trans be Element of Permutations(n2) 
           st Q.i=trans & trans is_transposition
         proof
           let i such that A8:i in dom Q;
           i is Element of NAT by ORDINAL1:def 13;
           then Q.i=P.(len X+i) & len X+i in dom P by A6,A8,FINSEQ_1:def 7,41;
           hence thesis by A5; 
         end;
      1+0<=k1 by XREAL_1:9;
      then 1 in Seg k1;
      then 1 in dom P & P.1=x by A4,A6,FINSEQ_1:def 3,58;
      then consider tr be Element of Permutations(n2) such that 
        A8:x=tr & tr is_transposition by A5;
      reconsider Tr=tr as Element of G by MATRIX_2:def 13;       
      A9':p2=Tr*Product Q by A4,A6,A8,GROUP_4:10
         .=(the mult of G).(Tr,Product Q) by GROUP_1:def 1
         .=PQ*tr by MATRIX_2:def 13;
      then A9:sgn(p2,K)=-sgn(PQ,K) by A8,Th13;
      now per cases by NAT_D:12;
        suppose A10:len Q mod 2=0;
          then sgn(PQ,K)=1_K & 0<2-1 by A4,A3,A6,A7;
          then sgn(p2,K)=-1_K & len P mod 2 =0 +1
            by A6,A9',A8,Th13,A10,RADIX_1:3;
          hence thesis;end;
        suppose A10:len Q mod 2=1;
          then sgn(PQ,K)=-1_K & 2-1=1 by A4,A3,A6,A7;
          hence thesis by RLVECT_1:30,RADIX_1:2,A6,A9,A10;end;
      end;
      hence thesis;
    end;
  for k be Element of NAT holds P[k] from NAT_1:sch 1(A1,A2);
  hence thesis;
end;
 
theorem Th16:
  for i,j,n st i < j & i in Seg n & j in Seg n
     ex tr be Element of Permutations(n) st
       tr is_transposition & tr.i = j
proof
  let i,j,n such that A1:i<j & i in Seg n & j in Seg n;
  defpred P[set,set] means for k st k in Seg n & k=$1 holds 
    (k=i implies $2=j) & (k=j implies $2=i) & (k<>i & k<>j implies $2=k);
   A2:for x st x in Seg n ex y be set st y in Seg n & P[x,y]
   proof
     let x such that A3:x in Seg n;
     reconsider m=x as Nat by A3,ORDINAL1:def 13;    
     now per cases;
       suppose m=i;then P[x,j];
         hence thesis by A1;end;
       suppose m=j;
         then P[x,i];
         hence thesis by A1;end;
       suppose m<>i & m<>j;
         then P[x,x]; 
         hence thesis by A3;end;
     end;
     hence thesis;
   end;
   consider f be Function of Seg n,Seg n such that 
A3:  for x st x in Seg n holds P[x,f.x] from FUNCT_2:sch 1(A2);
A4:  for x1,x2 be set st x1 in Seg n & x2 in Seg n & f.x1 = f.x2 holds x1=x2
   proof
     let x1,x2 be set such that A5:x1 in Seg n & x2 in Seg n & f.x1=f.x2;
     reconsider k1=x1,k2=x2 as Nat by A5,ORDINAL1:def 13;    
     (x1=i or x1=j or (x1<>i & x1<>j))& (x2=i or x2=j or (x2<>i & x2<>j));
     then ((x1=i & f.x1=j) or (x1=j & f.x1=i) or (x1<>i & x1<>j & f.x1=k1))
       &((x2=i&f.x2=j) or (x2=j&f.x2=i) or (x2<>i & x2<>j & f.x2=k2)) by A3,A5;
     hence thesis by A5;  
   end;
   for y st y in Seg n ex x st x in Seg n & y = f.x
     proof
       let y such that A6:y in Seg n;
       reconsider k=y as Nat by A6,ORDINAL1:def 13;  
       (k=i & f.j=i) or (k=j & f.i=j) or (k<>i & k<>j& f.k=k) by A1,A3,A6;
       hence thesis by A1,A6;
     end;
   then f is one-to-one & rng f=Seg n by FUNCT_2:16,25,A1,A4;
   then f is one-to-one & f is onto by FUNCT_2:def 3;
   then f is Permutation of Seg n by FUNCT_2:def 4;
   then reconsider P=f as Element of Permutations(n) by MATRIX_2:def 11;
   take P;
   dom P=Seg n by A1,FUNCT_2:def 1;
   then i in dom P & j in dom P & i<>j & P.i=j & P.j=i &
      for k st k <>i & k<>j & k in dom P holds P.k=k by A1,A3;
   hence thesis by MATRIX_2:def 14; 
end;

theorem Th17:
  for p be Element of Permutations(k+1) st p.(k+1) <> k+1
    ex tr be Element of Permutations(k+1) st
      tr is_transposition & tr.(p.(k+1)) = k+1 & (tr*p).(k+1) = k+1
proof 
  set k1=k+1;
  let p be Element of Permutations(k1) such that A1:p.k1<>k1;
  reconsider p'=p as Permutation of Seg k1 by MATRIX_2:def 11;
  A2:dom p'=Seg k1 & rng p'=Seg k1 & k1 in Seg k1 
    by FUNCT_2:67,def 3,FINSEQ_1:5;
  then A3:p.k1 in Seg k1 by FUNCT_1:def 5;
  then p.k1<=k1 by FINSEQ_1:3;
  then p.k1 < k1 by A1,REAL_1:def 5;
  then consider tr be Element of Permutations(k1) such that
     A5:tr is_transposition & tr.(p.k1)=k1 by A2,A3,Th16;
  reconsider tr'=tr as Permutation of Seg k1 by MATRIX_2:def 11;
  dom tr'=Seg k1 by FUNCT_2:67;
  then dom (tr*p)= Seg k1 by A2,RELAT_1:46;
  then (tr*p).k1=tr.(p.k1) by A2,FUNCT_1:22;
  hence thesis by A5;
end;

theorem Th18:
  for X,x st not x in X for p1 be Permutation of X\/{x} st p1.x = x 
    ex p be Permutation of X st p1|X = p
proof
  let X,x such that A1:not x in X;
  let p1 be Permutation of X\/{x} such that A2:p1.x=x;
  set pX=p1|X;
  A3:X c= X\/{x} & dom p1=X\/{x} & rng p1=X\/{x} 
    by FUNCT_2:67,def 3,XBOOLE_1:7;
  then A4:dom pX=X & rng pX c= X\/{x} by RELAT_1:91,RELAT_1:99;
  A5:X c= rng pX
  proof
    let y be set such that A6:y in X;
    consider x' be set such that
      A7:x' in dom p1 & p1.x'=y by FUNCT_1:def 5,A3,A6; 
    A8:x' in X
    proof
      assume not x' in X;
      then x' in {x} by A3,A7,XBOOLE_0:def 2;
      hence thesis by A1,A2,A6,A7,TARSKI:def 1;
    end;
    then pX.x'=p1.x' by A4,FUNCT_1:70;
    hence thesis by A4,A7,A8,FUNCT_1:def 5;
  end;
  rng pX c= X
  proof
    let y be set such that A6:y in rng pX;
    consider x' be set such that 
      A7:x' in dom pX & pX.x'=y by A6,FUNCT_1:def 5;
    assume A8:not y in X; 
    y in rng pX by A7,FUNCT_1:def 5;
    then y in {x} by A4,A8,XBOOLE_0:def 2;
    then y=x & p1 is one-to-one & pX.x'=p1.x' by A7,TARSKI:def 1,FUNCT_1:70;
    hence thesis by A1,A2,A3,A4,A6,A7,FUNCT_1:def 8;
  end;
  then A6:rng pX=X & pX is one-to-one by A5,FUNCT_1:84,XBOOLE_0:def 10; 
  then reconsider pX as Function of X,X by A4,FUNCT_2:3;
  pX is onto by FUNCT_2:def 3,A6;
  then pX is Permutation of X by FUNCT_2:def 4,A6;
  hence thesis;
end;

theorem Th19:  
  for p,q be (Permutation of X),p1,q1 be Permutation of X\/{x} st 
      p1|X = p & q1|X = q & p1.x = x & q1.x = x
    holds (p1*q1)|X = p*q & (p1*q1).x = x
proof  
  let p,q be Permutation of X,p1,q1 be Permutation of X\/{x} such that
    A2:p1|X=p & q1|X=q & p1.x=x & q1.x=x;
  set X1=X\/{x};set pq1=p1*q1; set pq=p*q;
  A3:X c=X1 & dom p=X & dom q=X & dom p1=X1 & dom q1=X1 & rng q=X 
    & dom pq1=X1 & dom pq =X by FUNCT_2:67,def 3,XBOOLE_1:7;
  then A4: dom (pq1|X)=X & dom (q1|X)=X by RELAT_1:91;
  for y be set st y in dom pq holds pq.y=(pq1|X).y
  proof
    let y be set such that A7:y in dom pq;
    q.y in rng q by A3,A7,FUNCT_1:def 5;
    then pq1.y=(pq1|X).y & pq.y=p.(q.y) & pq1.y=p1.(q1.y) & q1.y=q.y 
     & p1.(q.y)=p.(q.y) by A2,A3,A4,A7,FUNCT_1:68,FUNCT_2:70;
    hence thesis;
  end;
  hence pq1|X = pq by A3,A4,FUNCT_1:9;
  x in {x} by TARSKI:def 1;
  then x in X1 by XBOOLE_0:def 2;
  hence thesis by A2,FUNCT_2:70;
end;

theorem Th20:
  for tr be Element of Permutations(k) st tr is_transposition
    holds tr*tr = idseq k & tr = tr"
proof
  let tr be Element of Permutations(k) such that A1:tr is_transposition;
  consider i,j such that
    A2:i in dom tr & j in dom tr & i<>j & tr.i=j & tr.j=i and
    A3:for m st m <>i & m<>j & m in dom tr holds tr.m=m by A1,MATRIX_2:def 14;
  reconsider TR=tr as Permutation of Seg k by MATRIX_2:def 11;
  set TT=TR*TR;set I=idseq k;
  A5:dom TT=Seg k & dom I=Seg k & dom TR=Seg k by FUNCT_2:67;
  for x st x in dom TT holds TT.x = I.x
  proof
    let x such that A6:x in dom TT;
    reconsider m=x as Nat by A5,A6,ORDINAL1:def 13;
    now per cases;
      suppose m=i or m=j;
        hence TT.m=m by A2,A6,FUNCT_1:22;end;
      suppose m<>i & m<>j;
        then tr.m<>i & tr.m<>j & tr.m=m by A3,A5,A6;
        hence TT.m=m by A6,FUNCT_1:22;end;
    end;
    hence thesis by A5,A6,FUNCT_1:35;
  end;
  hence A6:tr*tr = idseq k by A5,FUNCT_1:9;
  rng TR=Seg k by FUNCT_2:def 3;
  hence thesis by A5,A6,FUNCT_1:64;
end;

     ::------------------------------------------::
     ::          A general theorem about         ::
     :: the representation of a permutation as   ::
     ::       a  product of transpositions       ::
     ::------------------------------------------::

theorem Th21:
  for perm ex P be FinSequence of Group_of_Perm(n) st
      perm=Product P &
      for i st i in dom P ex trans be Element of Permutations(n) 
         st P.i = trans & trans is_transposition
proof
  defpred P[Nat] means
    for perm be Element of Permutations($1) 
    ex P be FinSequence of Group_of_Perm($1) st perm=Product P &
     for i st i in dom P ex trans be Element of Permutations($1) 
       st P.i=trans & trans is_transposition;
A1:P[0]
  proof
    let perm be Element of Permutations(0);
    take F=<*>the carrier of Group_of_Perm 0;
    perm is Permutation of Seg 0 & Seg 0={} by MATRIX_2:def 11,FINSEQ_1:4;
    then dom perm={} by FUNCT_2:67;
    then perm=idseq 0 by RELAT_1:64,81,FINSEQ_1:4;
    then perm=1_Group_of_Perm(0) by MATRIX_2:28;
    hence thesis by GROUP_4:11;
  end;
A2:for k be Element of NAT st P[k] holds P[k+1]
  proof 
    let k be Element of NAT such that A3':P[k];set k1=k+1;

A3:for p be Element of Permutations(k1) st p.k1=k1
        ex P be FinSequence of Group_of_Perm(k1) st
          p=Product P &
          for i st i in dom P ex trans be Element of Permutations(k1) 
            st P.i=trans & trans is_transposition
    proof 
      set Gk=Group_of_Perm(k);
      set Gk1=Group_of_Perm(k1);
      let p be Element of Permutations(k1) such that A4:p.k1=k1;
      reconsider k'=k as Nat;  
      reconsider p'=p as Permutation of Seg k1 by MATRIX_2:def 11;
      A5:Seg k1=Seg k\/{k1} & not k1 in Seg k by FINSEQ_1:11,FINSEQ_3:9;
      then consider pk be Permutation of Seg k such that 
        A6:p'|(Seg k)=pk by A4,Th18;     
      reconsider pk'=pk as Element of Permutations(k) by MATRIX_2:def 11;
      consider P be FinSequence of Gk such that
        A9:pk'=Product P and
        A10:for i st i in dom P ex trans be Element of Permutations(k) 
            st P.i=trans & trans is_transposition by A3';
      defpred PR[set,set] means
        for i be Nat, tr be Element of Permutations(k)            
             st i in dom P & P.i=tr & i=$1
          ex newtr be Element of Permutations(k1) st newtr=$2 &
                 newtr is_transposition & 
                 newtr.k1=k1 & tr=newtr|(Seg k);
  A11:for m be Element of NAT st m in Seg (len P) 
              ex x be Element of Gk1 st PR[m,x]
        proof
          let m be Element of NAT such that A12:m in Seg (len P);            
          m in dom P by A12,FINSEQ_1:def 3;
          then consider tr be Element of Permutations(k) such that
            A13:P.m=tr & tr is_transposition by A10;
          reconsider tr'=tr as Permutation of Seg k by MATRIX_2:def 11;
          A14:(Seg k is empty implies Seg k is empty);
          then consider newt be Function of Seg k1,Seg k1 such that
          A15:newt|(Seg k) = tr' & newt.k1=k1 by A5,STIRL2_1:67;
          newt is onto & newt is one-to-one by A5,A14,A15,STIRL2_1:68;
          then newt is Permutation of Seg k1 by FUNCT_2:def 4;
          then reconsider NT=newt as Element of Permutations(k1) 
            by MATRIX_2:def 11;
          reconsider NT'=NT as Element of Gk1 
            by MATRIX_2:def 13;
        take NT';
          let I be Nat, TR be Element of Permutations(k) such that     
          A18:I in dom P & P.I=TR & I=m;
        take NT; 
          consider i',j' be Nat such that
            A19:i' in dom tr & j' in dom tr and 
            A20:i'<>j' & tr.i'=j' & tr.j'=i' and
            A21:for k st k <>i' & k<>j' & k in dom tr holds tr.k=k 
               by A13,MATRIX_2:def 14;
          dom tr c= dom newt by A15,RELAT_1:89;
          then A22:i' in dom newt & j' in dom newt & newt.i'=tr.i' & 
          newt.j'=tr.j' by A15,A19,FUNCT_1:70;
          for m st m <>i' & m<>j' & m in dom newt holds newt.m=m
            proof
              let m such that A23:m <>i' & m<>j' & m in dom newt;
              dom newt=Seg k1 by FUNCT_2:67;
              then (m in Seg k or m in {k1}) & dom tr'=Seg k
                by A5,A23,XBOOLE_0:def 2,FUNCT_2:67;
              then m in dom tr or m=k1 by TARSKI:def 1;
              then(tr.m=newt.m&tr.m=m)or newt.m=m by A15,A21,A23,FUNCT_1:70;
              hence thesis;
            end;
          hence thesis by A13,A15,A18,A20,A22,MATRIX_2:def 14;
        end;
  consider Pr be FinSequence of Gk1 such that 
        A12:dom Pr = Seg (len P) and
        A13:for m be Element of NAT st m in Seg (len P) holds PR[m,Pr.m] 
          from FINSEQ_1:sch 5(A11);
  take Pr;
      set mG=the mult of Gk;set mG1=the mult of Gk1;
      set Ik1=idseq k1;set Ik=idseq k;
      A14:pk'=mG "**" P & Product Pr = mG1 "**" Pr by A9,GROUP_4:def 3;
  now per cases;
    suppose A15:len Pr=0;
        then A16:len P=0 by A12,FINSEQ_1:def 3;
        mG has_a_unity & mG1 has_a_unity by GROUP_1:34;
        then A17':pk'=the_unity_wrt mG & Product Pr=the_unity_wrt mG1 & 
          the_unity_wrt mG=1_Gk & the_unity_wrt mG1=1_Gk1
          by A14,A15,A16,FINSOP_1:def 1,GROUP_1:33;
        then A17:pk=Ik & Product Pr=Ik1 by MATRIX_2:28;
        A18:dom p'=Seg k1 & dom Ik1 = Seg k1 by FUNCT_2:67;
        for y st y in dom p holds p.y = Ik1.y
          proof
            let y such that A19:y in dom p;
            reconsider y'=y as Nat by ORDINAL1:def 13,A18,A19;
            (y in Seg k or y in {k1})& dom pk=Seg k & y in Seg k1
               by FUNCT_2:67,A5,A18,A19,XBOOLE_0:def 2;
            then ((pk.y=p.y & Ik.y'=y') or (p.k1 =k1 & y=k1)) & Ik1.y'=y' 
              by A4,A6,FUNCT_1:70,TARSKI:def 1,FUNCT_1:35;
            hence thesis by A17',MATRIX_2:28;
          end;
        hence p=Product Pr & for i st i in dom Pr 
        ex t be Element of Permutations(k1) st Pr.i=t & t is_transposition 
          by A15,FINSEQ_1:def 3,4, A17,A18,FUNCT_1:9;end;
    suppose A15:len Pr>0;
        then A16:len P>0 by A12,FINSEQ_1:def 3;
        mG has_a_unity by GROUP_1:34;
        then consider fP be Function of NAT,Gk such that
           B1:fP.1=P.1 and
           B2:for n be Element of NAT st 0 <> n & n < len P holds
             fP.(n + 1) = mG.(fP.n,P.(n + 1)) and
           B3:pk=fP.(len P) by A14,A16,FINSOP_1:def 1;
        mG1 has_a_unity by GROUP_1:34;
        then consider fPr be Function of NAT,Gk1 such that
          C1:fPr.1=Pr.1 and
          C2:for n be Element of NAT st 0 <> n & n < len Pr holds
            fPr.(n + 1) = mG1.(fPr.n,Pr.(n + 1)) and
          C3:Product Pr=fPr.(len Pr) by A14,A15,FINSOP_1:def 1;
        defpred N[Nat] means ($1>0 & $1<=len P) implies
          ex Prod1 be Element of Permutations(k1),
             Prod be Element of Permutations(k)
            st Prod1=fPr.$1 & fP.$1=Prod & Prod1|(Seg k)=Prod & Prod1.k1=k1;
    A17:N[0];
    A18:for m be Element of NAT st N[m] holds N[m+1]
        proof
          let m be Element of NAT such that A19:N[m];set m1=m+1;
          assume A20:m1>0 & m1<=len P;          
          m1+0>0; then m1 >=1 by NAT_1:19;
          then A21:m1 in Seg len P & len P=len Pr & dom P=Seg len P 
            by A20,A12,FINSEQ_1:def 3;
          then consider tr be Element of Permutations(k) such that 
            D1:P.m1=tr & tr is_transposition by A10;
          consider tr1 be Element of Permutations(k1) such that
            D2:tr1=Pr.m1 & tr1 is_transposition and
            D3:tr1.k1=k1 & tr=tr1|(Seg k) by D1,A13,A21;
          now per cases;
            suppose m=0;
              hence thesis by B1,C1,D1,D2,D3;end;
            suppose A22:m>0;
              A23:m+0<m+1 by XREAL_1:8;
              then consider Q1 be Element of Permutations(k1),
                          Q be Element of Permutations(k) such that
                A24:Q1=fPr.m & fP.m=Q & Q1|(Seg k)=Q & Q1.k1=k1 
                by A22,A19,A20,XXREAL_0:2;
              reconsider Q1,tr1 as Permutation of Seg k1 by MATRIX_2:def 11;
              reconsider Q,tr as Permutation of Seg k by MATRIX_2:def 11;
              reconsider trQ=tr*Q as Element of Permutations(k) 
                by MATRIX_2:def 11;
              reconsider trQ1=tr1*Q1 as Element of Permutations(k1) 
                by MATRIX_2:def 11;
              m<len P & len P=len Pr by A20,XXREAL_0:2,A12,A23,FINSEQ_1:def 3;
              then fP.m1 = mG.(Q,tr) & fPr.m1 = mG1.(Q1,tr1) 
                by B2,C2,D1,D2,A22,A24;
              then fP.m1=trQ & fPr.m1=trQ1 & trQ1|(Seg k)=trQ & trQ1.k1=k1 
                by A5,D3,A24,Th19,MATRIX_2:def 13;
              hence thesis;end;
          end;
          hence thesis;
        end;
    A19:for m be Element of NAT holds N[m] from NAT_1:sch 1(A17,A18);
        A20:len P=len Pr by A12,FINSEQ_1:def 3;
        then consider Prod1 be Element of Permutations(k1),
             Prod be Element of Permutations(k) such that
          A21:Prod1=fPr.(len P) & fP.(len P)=Prod and
          A22:Prod1|(Seg k)=Prod & Prod1.k1=k1 by A15,A19;
        reconsider Prod1 as Permutation of Seg k1 by MATRIX_2:def 11;
        A23:dom p'=Seg k1 & dom Prod1 = Seg k1 by FUNCT_2:67;
        for y st y in dom p holds p.y = Prod1.y
          proof
            let y such that A24:y in dom p;
            (y in Seg k or y in {k1})& dom pk=Seg k 
              by FUNCT_2:67,A5,A23,A24,XBOOLE_0:def 2;
            then (Prod.y=p.y & Prod.y=Prod1.y) or (y=k1 & p.k1=Prod1.k1)
              by A4,A6,FUNCT_1:70,TARSKI:def 1,A21,A22,B3;
            hence thesis;
          end;
        hence p=Product Pr by A20,A21,C3,A23,FUNCT_1:9;
        thus for i st i in dom Pr ex trans be Element of Permutations(k1) 
          st Pr.i=trans & trans is_transposition
          proof
            let m such that A24:m in dom Pr;
            reconsider m'=m as Element of NAT by ORDINAL1:def 13;
            A25:Seg (len P)=dom P by FINSEQ_1:def 3;
            then consider t be Element of Permutations(k) such that
              A26:P.m=t & t is_transposition by A10,A12,A24;
            ex T be Element of Permutations(k1) st T=Pr.m' & T is_transposition
              & T.k1=k1 & t=T|(Seg k) by A12,A13,A24,A25,A26;
            hence thesis;
          end;
        end;end;
      hence thesis;
    end; ::end A3
    let p be Element of Permutations(k1);
    reconsider p'=p as Permutation of Seg k1 by MATRIX_2:def 11;
    set Gk1=Group_of_Perm(k1);
    now per cases;
      suppose p.k1=k1;
        hence thesis by A3;end;
      suppose p.k1<>k1;
        then consider tr be Element of Permutations(k1) such that
          A4:tr is_transposition & tr.(p.k1)=k1 & (tr*p).k1=k1 by Th17;
        reconsider tr'=tr as Permutation of Seg k1 by MATRIX_2:def 11; 
        reconsider T=tr as Element of Gk1 by MATRIX_2:def 13; 
        reconsider trp=tr'*p' as Element of Permutations(k1) 
          by MATRIX_2:def 11; 
        reconsider TRP=trp as Element of Gk1 by MATRIX_2:def 13;
        consider P be FinSequence of Gk1 such that
          A5:trp=Product P and
          A6:for i st i in dom P ex trans be Element of Permutations(k1) 
            st P.i=trans & trans is_transposition by A3,A4;
      take PT=P^<*T*>;
        A7:rng p'=Seg k1 by FUNCT_2:def 3; 
        Product PT=TRP * T by A5,GROUP_4:9;
        hence Product PT=(the mult of Gk1).(TRP,T) by GROUP_1:def 1
                       .=tr*(tr*p) by MATRIX_2:def 13
                       .=(tr*tr)*p by RELAT_1:55
                       .=(idseq k1)*p by A4,Th20
                       .=p by A7,RELAT_1:80;
        thus for m st m in dom PT ex trans be Element of Permutations(k1) 
          st PT.m=trans & trans is_transposition
        proof
          let m such that A8:m in dom PT;set L=len P;set L1=L+1;
          len PT=len P + 1 by FINSEQ_2:19;
          then A9:dom PT=Seg L1 & Seg L1=Seg L\/{L1} by FINSEQ_1:11,def 3;
          now per cases by A9,A8,XBOOLE_0:def 2;
            suppose m in Seg L;
               then m in dom P & m is Element of NAT 
                 by FINSEQ_1:def 3,ORDINAL1:def 13;
               then PT.m=P.m & ex trans be Element of Permutations(k1) 
                 st P.m=trans & trans is_transposition by A6,FINSEQ_1:def 7;
               hence thesis;end;
            suppose m in {L1};
               then m=L1 by TARSKI:def 1;
               then PT.m=T by FINSEQ_1:59;
               hence thesis by A4;end;
          end;
          hence thesis;
        end;end;
    end;
    hence thesis;
  end;
A3: for n be Element of NAT holds P[n] from NAT_1:sch 1(A1,A2);
  let perm be Element of Permutations(n);
  n is Element of NAT by ORDINAL1:def 13;
  hence thesis by A3;
end;

theorem Th22:
  K is Fanoian iff 1_K <> -1_K
proof
  thus K is Fanoian implies 1_K <> -1_K
  proof
     assume A1:K is Fanoian;
     assume 1_K=-1_K;
     then 1_K+1_K=0.K by RLVECT_1:def 10;
     then 1_K=0.K by A1,VECTSP_1:def 28;
     hence thesis by VECTSP_1:def 21;
  end;
  assume A1:1_K <> -1_K;
  assume not K is Fanoian;
  then consider a such that
A2:a+a=0.K & a<>0.K by VECTSP_1:def 28;
  a=a*1_K by VECTSP_1:def 13;
  then 0.K=a*(1_K+1_K) by A2,VECTSP_1:def 18;
  then 0.K=1_K+1_K by A2,VECTSP_1:44;
  hence thesis by A1,VECTSP_1:63;
end;

     ::------------------------------------------::
     ::   The relation between sign and parity   ::
     ::            of permutations               ::
     ::------------------------------------------::

theorem Th23: 
  for K be Fanoian Field holds
     ( perm2 is even iff sgn(perm2,K) =  1_K )&
     ( perm2 is odd  iff sgn(perm2,K) = -1_K )
proof
  set n2=n+2;
  let K be Fanoian Field;
  A1:len Permutations n2 = n2 by MATRIX_2:20;
  thus B1:perm2 is even implies sgn(perm2,K) = 1_K
  proof
    assume perm2 is even;
    then ex L be FinSequence of Group_of_Perm(n2) st (len L) mod 2=0 & 
        perm2=Product L & for i st i in dom L ex q2 st L.i=q2 & 
        q2 is_transposition by A1,MATRIX_2:def 15;
    hence thesis by Th15;
  end;
  thus B2:sgn(perm2,K)= 1_K implies perm2 is even
    proof
      assume A2:sgn(perm2,K)= 1_K;
      consider P be FinSequence of Group_of_Perm(n2) such that
        A3:perm2=Product P and
        A4:for i st i in dom P ex trans be Element of Permutations(n2) 
          st P.i=trans & trans is_transposition by Th21;
      assume perm2 is odd;
      then len P mod 2 <> 0 by A1,A3,A4,MATRIX_2:def 15;
      then len P mod 2 = 1 by NAT_D:12;
      then sgn(perm2,K)=-1_K by Th15,A3,A4;
      hence thesis by Th22,A2;
    end;
  thus thesis by Th11,Th22,B1,B2;
end;

     ::------------------------------------------::
     ::     The sign of a composition of         ::
     ::               permutations               ::
     ::------------------------------------------::

theorem Th24:
  for p2,q2,pq2 st pq2 = p2*q2 holds sgn(pq2,K) = sgn(p2,K)*sgn(q2,K)
proof 
  set n2=n+2;
  let p, q, pq be Element of Permutations(n2) such that A1:pq=p*q;
  consider P1 be FinSequence of Group_of_Perm(n2) such that
     B1:p=Product P1 and
     B2:for i st i in dom P1 ex trans be Element of Permutations(n2)
        st P1.i=trans & trans is_transposition by Th21;
  consider P2 be FinSequence of Group_of_Perm(n2) such that
     C1:q=Product P2 and
     C2:for i st i in dom P2 ex trans be Element of Permutations(n2) 
        st P2.i=trans & trans is_transposition by Th21;
  set PP=P2^P1;
  A2:Product PP=(Product P2)*(Product P1) by GROUP_4:8
     .=(the mult of Group_of_Perm(n2)).(Product P2,Product P1) by GROUP_1:def 1
     .=pq by A1,B1,C1,MATRIX_2:def 13;
  A3:for i st i in dom PP ex trans be Element of Permutations(n2) 
      st PP.i=trans & trans is_transposition
    proof
      let i such that A4:i in dom PP;
      A5:i is Element of NAT by ORDINAL1:def 13;
      now per cases by A4,A5,FINSEQ_1:38;
        suppose i in dom P2;
          then P2.i=PP.i & ex trans be Element of Permutations(n2) 
            st P2.i=trans & trans is_transposition by C2,A5,FINSEQ_1:def 7;  
          hence thesis;end;
        suppose ex k be Element of NAT st k in dom P1 & i=len P2 + k;
          then consider k be Element of NAT such that
            A6:k in dom P1 & i=len P2 + k;
          P1.k=PP.i & ex trans be Element of Permutations(n2) 
            st P1.k=trans & trans is_transposition by B2,A6,FINSEQ_1:def 7;
          hence thesis;end;
      end;
      hence thesis;
    end;
  now per cases by NAT_D:12;
    suppose A4:len P1 mod 2=0 & len P2 mod 2=0;
       len PP mod 2=(len P2+len P1) mod 2 by FINSEQ_1:35
                  .=(0+len P1+0) mod 2 by A4,NAT_D:22
                  .=0 by A4;
       then sgn(pq,K)=1_K & sgn(p,K)=1_K & sgn(q,K)=1_K
         by B1,B2,C1,C2,A2,A3,A4,Th15;
       hence thesis by VECTSP_1:def 13;end;
    suppose A4:len P1 mod 2=1 & len P2 mod 2=0;
       len PP mod 2=(len P2+len P1) mod 2 by FINSEQ_1:35
                  .=(0+len P1+0) mod 2 by A4,NAT_D:22
                  .=1 by A4;
       then sgn(pq,K)=-1_K & sgn(p,K)=-1_K & sgn(q,K)=1_K
         by B1,B2,C1,C2,A2,A3,A4,Th15;
       hence thesis by VECTSP_1:def 13;end;
    suppose A4:len P1 mod 2=0 & len P2 mod 2=1;
       len PP mod 2=(len P2+len P1) mod 2 by FINSEQ_1:35
                  .=(1+len P1) mod 2 by A4,NAT_D:22
                  .=(1+0) mod 2 by A4,NAT_D:22
                  .=1 by NAT_D:14;
       then sgn(pq,K)=-1_K & sgn(p,K)=1_K & sgn(q,K)=-1_K
         by B1,B2,C1,C2,A2,A3,A4,Th15;
       hence thesis by VECTSP_1:def 13; end;
    suppose A4:len P1 mod 2=1 & len P2 mod 2=1;
       len PP mod 2=(len P2+len P1) mod 2 by FINSEQ_1:35
                  .=(1+len P1) mod 2 by A4,NAT_D:22
                  .=(1+1) mod 2 by A4,NAT_D:22
                  .=0 by NAT_D:25;
       then sgn(pq,K)=1_K & sgn(p,K)=-1_K & sgn(q,K)=-1_K & 1_K*1_K=1_K
         by B1,B2,C1,C2,A2,A3,A4,Th15,VECTSP_1:def 13;
       hence thesis by VECTSP_1:42;end;
  end;
  hence thesis;
end;

LM3:n<2 implies p is even & p=idseq n
proof
  assume A1:n<2;
  reconsider P=p as Permutation of Seg n by MATRIX_2:def 11;
  now per cases by A1,NAT_1:23;
    suppose A2:n=0;
       then dom P={} by FUNCT_2:67,FINSEQ_1:4;
       then P={} & len Permutations(n)=n by RELAT_1:64,MATRIX_2:20;
       hence thesis by MATRIX_2:29,A2,RELAT_1:81,FINSEQ_1:4;end;
    suppose n=1;
       then P=id Seg n & len Permutations(n)=n 
         by TARSKI:def 1,MATRIX_2:20,21;
       hence thesis by MATRIX_2:29;end;
  end;
  hence thesis;
end;

theorem Th25:
  (p is even & q is even) or (p is odd & q is odd) iff p*q is even
proof
  reconsider pq=p*q as Element of Permutations(n) by MATRIX_9:39;
  now per cases;
    suppose n<2;
      then p is even & q is even & pq is even by LM3;
      hence thesis;
    end;
    suppose n>=2;
      then reconsider n2=n-2 as Nat by NAT_1:21;
      consider K be Fanoian Field;
      reconsider p'=p,q'=q,pq as Element of Permutations(n2+2);
      thus (p is even&q is even) or (p is odd&q is odd) implies p*q is even
        proof
          assume (p is even & q is even) or (p is odd & q is odd);
          then (sgn(p',K)=1_K & sgn(q',K)=1_K)
            or (sgn(p',K)=-1_K & sgn(q',K)=-1_K) by Th23;
          then sgn(p',K)*sgn(q',K)=1_K *1_K & 1_K*1_K=1_K
            by VECTSP_1:42,VECTSP_1:def 13;
          then sgn(pq,K)=1_K by Th24;
          hence thesis by Th23;
        end;
      thus p*q is even implies ((p is even&q is even) or (p is odd&q is odd))
        proof
          assume p*q is even;
          then sgn(pq,K)=1_K by Th23;
          then A4:sgn(p',K)*sgn(q',K)=1_K by Th24;
          assume A5:not((p is even & q is even) or (p is odd & q is odd));
          now per cases by A5;
            suppose p is even & q is odd;
              then sgn(p',K)=1_K & sgn(q',K)=-1_K by Th23;
              then sgn(p',K)*sgn(q',K)=-1_K by VECTSP_1:def 13;
              hence thesis by A4,Th22;end;
            suppose p is odd & q is even;
              then sgn(p',K)=-1_K & sgn(q',K)=1_K by Th23;
              then sgn(p',K)*sgn(q',K)=-1_K by VECTSP_1:def 13;
              hence thesis by A4,Th22;end;
          end;
          hence thesis;
        end;end;
  end;
  hence thesis;
end;

theorem Th26:
  -(a,perm2) = sgn(perm2,K)*a
proof
  per cases;
    suppose perm2 is even & K is Fanoian;
    then -(a,perm2)=a & sgn(perm2,K)=1_K by MATRIX_2:def 16,Th23;
    hence thesis by VECTSP_1:def 13;
  end;
  suppose perm2 is odd & K is Fanoian;
    then -(a,perm2)=-a & sgn(perm2,K)=-1_K & (-1_K)*a=-1_K*a
      by MATRIX_2:def 16,Th23,VECTSP_1:40;
    hence thesis by VECTSP_1:def 13;
  end;
  suppose perm2 is even & not K is Fanoian;
     then -(a,perm2)=a & 1_K=-1_K & (sgn(perm2,K)=1_K or sgn(perm2,K)=-1_K)
       by MATRIX_2:def 16,Th11,Th22;
     hence thesis by VECTSP_1:def 13;
  end;
  suppose perm2 is odd & not K is Fanoian;
    then -(a,perm2)=-a & 1_K=-1_K & (-1_K)*a=-1_K*a &
      (sgn(perm2,K)=1_K or sgn(perm2,K)=-1_K)
        by MATRIX_2:def 16,VECTSP_1:40,Th11,Th22;
    hence thesis by VECTSP_1:def 13;
  end;
end;

theorem Th27:
  for tr be Element of Permutations(n+2) st
    tr is_transposition holds tr is odd
proof
  let tr be Element of Permutations(n+2) such that A1:tr is_transposition;
  consider K be Fanoian Field;
  sgn(tr,K)=-1_K by A1,Th14;
  hence thesis by Th23;
end;

registration let n;
  cluster odd Permutation of Seg (n+2);
  existence
  proof set n2=n+2;
    n2>=2+0 by XREAL_1:8;
    then {1,2} in 2Set Seg n2 by Th3;
    then consider i,j such that 
      A1:i in Seg n2 & j in Seg n2 & i < j & {1,2}={i,j} by Th1;
    consider tr be Element of Permutations(n2) such that
      A2:tr is_transposition & tr.i = j by A1,Th16;
    tr is odd & len Permutations n2=n2 by Th27,A2,MATRIX_2:20;
    hence thesis;
  end;
end;

begin :: The Determinant of a Linear Combination of Lines

  reserve pD,qD for FinSequence of D,
          M for Matrix of n,m,D,
          pK,qK for FinSequence of K,
          A,B for Matrix of n,K;

definition let l, n, m, D;
           let M be Matrix of n,m,D;
           let pD be FinSequence of D;
  func ReplaceLine(M,l,pD) -> Matrix of n,m,D means :Def2:
      len it = len M & width it = width M & 
      for i,j st [i,j] in Indices M holds 
        (i <> l implies it*(i,j) = M*(i,j)) & (i = l implies it*(l,j) = pD.j)
    if len pD = width M otherwise it = M;
  consistency; 
  existence
proof
  thus len pD=width M implies ex M1 be Matrix of n,m,D st
  len M1 = len M & width M1 = width M &
      for i,j st [i,j] in Indices M holds
        (i <> l implies M1*(i,j) = M*(i,j)) & (i = l implies M1*(l,j) = pD.j)
    proof
      assume A1:len pD=width M;
       defpred P[set,set,set] means for i,j st i=$1 & j=$2 holds
         (i <>l implies $3 = M*(i,j)) & (i = l implies $3 = pD.j);
     reconsider n1=n,m1=m as Element of NAT by ORDINAL1:def 13;
     A2:for i,j st [i,j] in [:Seg n1, Seg m1:]
       for x1,x2 being Element of D st P[i,j,x1] & P[i,j,x2] holds x1 = x2
       proof
         let i,j such that [i,j] in [:Seg n1, Seg m1:];
         let x1,x2 being Element of D such that A3:P[i,j,x1] & P[i,j,x2];
         (i <>l  implies x1 = M*(i,j)) & (i = l implies x1 = pD.j) &
         (i <>l  implies x2 = M*(i,j)) & (i = l implies x2 = pD.j) by A3;
         hence thesis;     
       end;
     A3:for i,j st [i,j] in [:Seg n1, Seg m1:] 
          ex x being Element of D st P[i,j,x]
      proof
        let i,j such that A4:[i,j] in [:Seg n1, Seg m1:];
        now per cases;
          case A5:i=l;
            n1<>0 by FINSEQ_1:4,A4,ZFMISC_1:106;then n1>0;
            then len pD=m by A1,MATRIX_1:24;
            then j in Seg len pD by A4,ZFMISC_1:106;
            then j in dom pD by FINSEQ_1:def 3;
            then pD.j in rng pD & rng pD c= D by FUNCT_1:def 5,FINSEQ_1:def 4;
            then pD.j in D & P[i,j,pD.j] by A5;
            hence thesis;end;
          case i<>l;
            then P[i,j,M*(i,j)];
            hence thesis;end;
        end;
        hence thesis;
      end;
      consider M1 be Matrix of n1,m1,D such that
        A4:for i,j st [i,j] in Indices M1 holds P[i,j,M1*(i,j)] 
                      from MATRIX_1:sch 2(A2,A3);
      reconsider M1 as Matrix of n,m,D;
      take M1;
      A5:now per cases;
        suppose n=0; 
          then A6:len M=0 & len M1=0 by MATRIX_1:def 3;
          then width M1=0 & 0=width M by MATRIX_1:def 4;
          hence len M = len M1 & width M1=width M by A6;end;
        suppose n > 0;
          then len M=n & len M1=n & width M=m & width M1=m by MATRIX_1:24;
          hence len M = len M1 & width M = width M1;end;
      end;
      reconsider M'=M as Matrix of len M,width M,D by MATRIX_2:7; 
      Indices M'=Indices M & Indices M'=Indices M1 by MATRIX_1:27; 
      hence thesis by A4,A5;
    end;
    thus thesis;  
  end;
uniqueness
  proof
  let M1,M2 be Matrix of n,m,D;
  thus len pD=width M & 
     (len M1 = len M & width M1 = width M & for i,j st [i,j] in Indices M holds 
        (i <> l implies M1*(i,j) = M*(i,j)) & (i = l implies M1*(l,j) = pD.j))&
     (len M2 = len M & width M2 = width M & for i,j st [i,j] in Indices M holds 
        (i <> l implies M2*(i,j) = M*(i,j)) & (i = l implies M2*(l,j) = pD.j))
     implies M1=M2
  proof
    assume len pD=width M;
    assume A2:len M1=len M&width M1=width M&for i,j st [i,j] in Indices M holds 
        (i <> l implies M1*(i,j) = M*(i,j))&(i = l implies M1*(l,j) = pD.j);
    assume A3:len M2=len M&width M2=width M&for i,j st [i,j] in Indices M holds 
        (i <> l implies M2*(i,j) = M*(i,j))&(i = l implies M2*(l,j) = pD.j);
    for i,j st [i,j] in Indices M1 holds M1*(i,j)=M2*(i,j)
    proof
      let i,j such that A4:[i,j] in Indices M1;
      [i,j] in Indices M by A2,A4,MATRIX_4:55;
      then (i<>l implies M1*(i,j)=M*(i,j)) & (i=l implies M1*(l,j)=pD.j)& 
        (i<>l implies M2*(i,j)=M*(i,j)) & (i=l implies M2*(l,j)=pD.j) by A2,A3;
      hence thesis;
    end;
    hence thesis by MATRIX_1:28;
  end;
  thus thesis;
  end;
end;

notation let l, n, m, D;
  let M be Matrix of n,m,D;
  let pD be FinSequence of D;
  synonym RLine(M,l,pD) for ReplaceLine(M,l,pD);
end;

LM4:for l,M,pD holds Indices M=Indices RLine(M,l,pD) & 
  len M=len RLine(M,l,pD) & width M=width RLine(M,l,pD)
proof
  let l,M,pD;
  now per cases;
    case len pD=width M;
      then len M=len ReplaceLine(M,l,pD) & width M=width RLine(M,l,pD) by Def2;
      hence thesis by MATRIX_4:55;end;
    case len pD<>width M;
      hence thesis by Def2;end;end;
  hence thesis;
end;

theorem Th28:
  for l,M,pD,i st i in Seg n holds 
     (i = l & len pD = width M implies Line(RLine(M,l,pD),i) = pD) &
     (i <> l implies Line(RLine(M,l,pD),i) = Line(M,i))
proof
  let l,M,pD,i such that A1:i in Seg n;
  set R=RLine(M,l,pD);
  set LR=Line(R,i);
  set LM=Line(M,i);
  thus i=l & len pD=width M implies LR=pD
    proof
      assume A2:i=l & len pD=width M;
      then A3:width R=len pD by Def2;
      then A4:len LR=len pD by MATRIX_1:def 8;
      now let j be Element of NAT such that A5:1 <=j & j <= len pD;
        n=len R by MATRIX_1:def 3;
        then j in Seg width R & i in dom R by A1,A3,A5,FINSEQ_1:def 3;
        then LR.j=R*(i,j) & [i,j] in Indices R & Indices R=Indices M
          by MATRIX_1:def 8,ZFMISC_1:106,LM4;
        hence LR.j=pD.j by A2,Def2;
      end;
      hence thesis by A4,FINSEQ_1:18;
    end;
  assume A2:i<>l;
  A3:len LR=width R & width M=width R & width M=len LM by MATRIX_1:def 8,LM4;
  now let j be Element of NAT such that A5:1 <=j & j <= len LM;
    i in Seg len M by A1,MATRIX_1:def 3;
    then j in Seg len LM & i in dom M by A5,FINSEQ_1:def 3;
    then A4:LM.j=M*(i,j) & LR.j=R*(i,j) & [i,j] in Indices M 
      by A3,MATRIX_1:def 8,ZFMISC_1:106;
    now per cases;
      case len pD=width M;
        hence LM.j=LR.j by A2,A4,Def2;end;
      case len pD<>width M;
        hence LM.j=LR.j by Def2;end;
    end;
    hence LM.j=LR.j;
  end;
  hence thesis by A3,FINSEQ_1:18;
end;

theorem
   for M,pD st len pD=width M
     for p' be Element of D* st pD = p' holds RLine(M,l,pD) = Replace(M,l,p')
proof
  let M,pD such that A1:len pD=width M;
  let p' be Element of D* such that A1':pD=p';
  set RL=RLine(M,l,pD); set R=Replace(M,l,p');
  A2:len R=len M & len M=len RL by LM4,FINSEQ_7:7;
  now let i be Element of NAT such that A3:1 <= i & i <=len R;
    i in Seg len R by A3;
    then A4:i in dom R & i in dom M & i in Seg n & n=len M
      by A2,FINSEQ_1:def 3,MATRIX_1:def 3;
    now per cases;
     case i=l;
       then R/.i=p' & R/.i=R.i & Line(RL,i)=pD & Line(RL,i)=RL.i
         by A1,A4,A2,A3,Th28,FINSEQ_7:10,FINSEQ_4:def 4,MATRIX_2:10;
       hence R.i=RL.i by A1';end;
     case i<>l;
       then R.i=R/.i & R/.i=M/.i & M/.i=M.i & M.i=Line(M,i) & 
         Line(M,i)=Line(RL,i) & Line(RL,i)=RL.i by Th28,A2,A3,A4,
         FINSEQ_4:def 4,MATRIX_2:10,FINSEQ_7:12;
       hence R.i=RL.i;end;
    end;
    hence R.i=RL.i;    
  end;
  hence thesis by A2,FINSEQ_1:18;
end;

theorem Th30:
  M = RLine(M,l,Line(M,l))
proof
  set L=Line(M,l);set RL=RLine(M,l,L);
  A1:width M=len L & len M=len RL by LM4,MATRIX_1:def 8;
  now let i be Element of NAT such that A2:1<= i & i <= len M;
    A3:i in Seg len M & n=len M by A2,MATRIX_1:def 3;
    then A4:Line(M,i)=M.i & RL.i=Line(RL,i) by MATRIX_2:10;
    now per cases;
      case i=l;
        hence RL.i=M.i  by A4,Th28,A1,A3;end;
      case i<>l;
        hence RL.i=M.i  by A4,Th28,A3;end;
    end;
    hence RL.i=M.i;
  end;
  hence thesis by A1,FINSEQ_1:18;
end;

LM5:for pK,a holds len pK = len (a*pK)
proof
  let pK,a;
  pK is Element of (len pK)-tuples_on the carrier of K by FINSEQ_2:110;
  then (a*pK) is Element of (len pK)-tuples_on the carrier of K 
    by FINSEQ_2:133;
  hence thesis by FINSEQ_2:109;
end;

LM6:for pK,qK st len pK=len qK holds len pK=len(pK+qK)
proof
  let pK,qK be FinSequence of K;
  assume len pK=len qK;
  then pK is Element of (len pK)-tuples_on the carrier of K &
  qK is Element of (len pK)-tuples_on the carrier of K by FINSEQ_2:110;
  then (pK+qK) is Element of (len pK)-tuples_on the carrier of K
    by FINSEQ_2:140;
  hence thesis by FINSEQ_2:109;
end;

theorem Th31:
  for l,pK,qK,perm st l in Seg n & len pK = n & len qK = n
    for M be Matrix of n,K holds 
  (the mult of K) $$ Path_matrix(perm,RLine(M,l,a * pK + b * qK)) =
             a * ((the mult of K) $$ Path_matrix(perm,RLine(M,l,pK))) +
             b * ((the mult of K) $$ Path_matrix(perm,RLine(M,l,qK)))
proof
  let l,pK,qK,perm such that A1:l in Seg n & len pK = n & len qK = n;
  let M be Matrix of n,K;
  set Rpq=RLine(M,l,a*pK+b*qK);
  set Rp=RLine(M,l,pK);set Rq=RLine(M,l,qK);
  set mm=the mult of K;
  set Ppq=Path_matrix(perm,Rpq);
  set Pp=Path_matrix(perm,Rp);
  set Pq=Path_matrix(perm,Rq);  
  A2:n > 0 & n>=1 & len Ppq=n & len Pp=n & len Pq=n
    by MATRIX_3:def 7,NAT_1:14,A1,FINSEQ_1:4;
  then consider fpq be Function of NAT,the carrier of K such that
    A3:fpq.1 = Ppq.1 and
    A4:for k be Element of NAT st 0<>k & k<len Ppq holds
       fpq.(k+1)=mm.(fpq.k,Ppq.(k+1)) and
    A5:mm $$ Ppq=fpq.(len Ppq) by FINSOP_1:def 1;
  consider fp be Function of NAT,the carrier of K such that
    A6:fp.1 = Pp.1 and
    A7:for k be Element of NAT st 0<>k & k<len Pp holds
       fp.(k+1)=mm.(fp.k,Pp.(k+1)) and
    A8:mm $$ Pp=fp.(len Pp) by A2,FINSOP_1:def 1;
  consider fq be Function of NAT,the carrier of K such that
    A9:fq.1 = Pq.1 and
    A10:for k be Element of NAT st 0<>k & k<len Pq holds
       fq.(k+1)=mm.(fq.k,Pq.(k+1)) and
    A11:mm $$ Pq=fq.(len Pq) by A2,FINSOP_1:def 1;  
    reconsider L=l as Element of NAT by ORDINAL1:def 13;
A12:for k be Element of NAT st k in Seg n holds
     (k<>L implies Ppq.k=Pp.k & Pp.k=Pq.k)&
     (k=L implies ex Ppk,Pqk be Element of K st Ppk=Pp.k & Pqk=Pq.k &
                                                             Ppq.k=a*Ppk+b*Pqk)
    proof
      let k be Element of NAT such that A13:k in Seg n;
      A14:k in dom Ppq & k in dom Pp & k in dom Pq & perm.k in Seg n 
        by A13,A2,FINSEQ_1:def 3,MATRIX_7:14;
      then reconsider pk=perm.k as Element of NAT;
      A15':len (a*pK)=n & len (b*qK)=n  by A1,LM5;
      then A15:len (a*pK+b*qK)=n by LM6;
      [k,pk] in [:Seg n,Seg n:] by A13,A14,ZFMISC_1:106; 
      then A16:[k,pk] in Indices M & width M=n by MATRIX_1:25;
      thus k<>L implies Ppq.k=Pp.k & Pp.k=Pq.k
      proof assume k<>L;
        then Rpq*(k,pk)=M*(k,pk) & Rp*(k,pk)=M*(k,pk) & Rq*(k,pk)=M*(k,pk) 
          by A1,Def2,A15,A16;
        then Ppq.k=M*(k,pk)&Pp.k=M*(k,pk)&Pq.k=M*(k,pk) by A14,MATRIX_3:def 7;
        hence thesis;
      end;
      A17:dom (a*pK+b*qK)=Seg n & dom (a*pK) =Seg n & dom (b*qK) =Seg n & 
      dom pK=Seg n & dom qK=Seg n by A1,A15,A15',FINSEQ_1:def 3;
      then pK/.pk=pK.pk & qK/.pk=qK.pk by A14,FINSEQ_4:def 4;
      then reconsider ppk=pK.pk, qpk=qK.pk as Element of K;
      assume A18:k=L; take ppk,qpk;
      Rpq*(k,pk) = (a*pK+b*qK).pk & (a*pK).pk = a*ppk & (b*qK).pk = b*qpk 
        & Rp*(k,pk)=pK.pk & Rq*(k,pk)=qK.pk
        by A1,Def2,A14,A15,A16,A17,A18,FVSUM_1:62;
      then Rpq*(k,pk)=(a*ppk)+(b*qpk) & Pp.k=pK.pk & Pq.k=qK.pk
        by A14,A17,FVSUM_1:21,MATRIX_3:def 7;
      hence thesis by A14,MATRIX_3:def 7;                  
    end;
  defpred P[Nat] means (1 <= $1 & $1 < L) implies (fp.$1=fq.$1 & fpq.$1=fp.$1);
  A13:P[0];
  A14:for k be Element of NAT st P[k] holds P[k+1]
    proof
      let k be Element of NAT such that A15:P[k];set k1=k+1;
      assume A16:1 <= k1 & k1 < L;
      L<=n by A1,FINSEQ_1:3;
      then k1 <= n by A16,XXREAL_0:2;
      then A17:k1 in Seg n & k < n by A16,NAT_1:13;
      now per cases;
        case k=0;
          hence thesis by A3,A6,A9,A17,A12,A16;end;
        case k>0;
          then k<=k1 & 0<k+0 by NAT_1:13;
          then fp.k=fq.k & fpq.k=fp.k & fpq.k1=mm.(fpq.k,Ppq.k1) & 
            fp.k1=mm.(fp.k,Pp.k1) & fq.k1=mm.(fq.k,Pq.k1)
            by A2,A4,A7,A10,A15,A16,A17,NAT_1:13,19;
          hence thesis by A17,A12,A16;end;
      end;
      hence thesis;
    end;   
  A15:fpq.L=a*fp.L+b*fq.L
  proof
    consider PpL,PqL be Element of K such that 
      A16:PpL=Pp.L & PqL=Pq.L & Ppq.L=a*PpL+b*PqL by A1,A12;
    A17:L>=1 by A1,FINSEQ_1:3;
    now per cases by A17,REAL_1:def 5;
      case A18:L>1;
        then reconsider L1=L-1 as Element of NAT by NAT_1:20;
        A19:for k be Element of NAT holds P[k] from NAT_1:sch 1(A13,A14);
        L1+1>1+0 by A18;
        then L1+0>0 & L1<L1+1 & L <= n by A1,NAT_1:19,FINSEQ_1:3;
        then 0<L1 & 1<=L1 & L1 < L & L1 < n & L=L1+1 by NAT_1:19,XXREAL_0:2;
        then fp.L1=fq.L1 & fpq.L1=fp.L1 & fpq.L=mm.(fpq.L1,a*PpL+b*PqL) &
          fp.L=mm.(fp.L1,PpL) & fq.L=mm.(fq.L1,PqL) by A2,A4,A7,A10,A16,A19;
        then A20:fq.L=fp.L1*PqL & fp.L=fp.L1*PpL & fpq.L=fp.L1*(a*PpL+b*PqL)
          by GROUP_1:def 1;
        then fp.L1*(a*PpL)=a*fp.L & fp.L1*(b*PqL)=b*fq.L by GROUP_1:def 4;
        hence thesis by A20,VECTSP_1:def 18;
end;
      case L=1;
        hence thesis by A3,A6,A9,A16;end;
    end;
    hence thesis;
  end;
  defpred Q[Nat] means (1<=$1 & L<=$1 & $1<=n) implies 
    for k be Element of NAT st $1=k holds fpq.k=a*fp.k+b*fq.k;
  A16:Q[0];
  A17:for k be Element of NAT st Q[k] holds Q[k+1]
  proof
    let k be Element of NAT such that A18:Q[k];set k1=k+1;
    assume A19:1<=k1 & L<=k1 & k1<=n;
    let k' be Element of NAT such that A20:k'=k1;
    now per cases by A19,REAL_1:def 5;
      case k1=L;
        hence thesis by A15,A20;end;
      case A21:k1>L;
        then A22:k>=L & k<n &1<=L & k1 in Seg n by A1,FINSEQ_1:3,A19,NAT_1:13;
        then k1 in dom Pp by A2,FINSEQ_1:def 3;
        then Pp/.k1=Pp.k1 by FINSEQ_4:def 4;
        then reconsider Ppk1=Pp.k1 as Element of K; 
        fpq.k=a*fp.k+b*fq.k & Ppq.k1=Pp.k1 & Pp.k1=Pq.k1 &
          fp.k1=mm.(fp.k,Pp.k1)&fq.k1=mm.(fq.k,Pq.k1)&fpq.k1=mm.(fpq.k,Ppq.k1)
          by A2,A4,A7,A10,A12,A21,A22,A18,XXREAL_0:2;
        then fpq.k1=(a*fp.k+b*fq.k)*Ppk1 & fq.k1=fq.k*Ppk1 & fp.k1=fp.k*Ppk1&
          (a*fp.k)*Ppk1=a*(fp.k*Ppk1) & (b*fq.k)*Ppk1=b*(fq.k*Ppk1)
          by GROUP_1:def 1,GROUP_1:def 4;
        hence thesis by A20,VECTSP_1:def 18;end;
    end;
    hence thesis;
  end;
  A18:for k be Element of NAT holds Q[k] from NAT_1:sch 1(A16,A17);
  1<=L & L<=n by A1,FINSEQ_1:3;
  hence thesis by A2,A5,A8,A11,A18;
end;

theorem Th32:
  for l,pK,qK,perm st l in Seg n & len pK = n & len qK = n
    for M be Matrix of n,K holds
       (Path_product(RLine(M,l,a*pK+b*qK))).perm =
  a*(Path_product(RLine(M,l,pK))).perm + b*(Path_product(RLine(M,l,qK))).perm
proof
  let l,pK,qK,perm such that A1:l in Seg n & len pK = n & len qK = n;
  let M be Matrix of n,K;
  set mm=the mult of K;
  set Rpq=RLine(M,l,a*pK+b*qK);
  set Rp=RLine(M,l,pK);
  set Rq=RLine(M,l,qK);
  set Ppq=Path_matrix(perm,Rpq);
  set Pathpq=Path_product(Rpq);
  set Pp=Path_matrix(perm,Rp);
  set Pathp=Path_product(Rp);
  set Pq=Path_matrix(perm,Rq);
  set Pathq=Path_product(Rq);
  now per cases;
    case perm is even;
      then mm $$ Pp=-(mm $$ Pp,perm) & mm $$ Pq=-(mm $$ Pq,perm) &
           mm $$ Ppq=-(mm $$ Ppq,perm) by MATRIX_2:def 16;
      then Pathp.perm=mm $$ Pp & Pathq.perm=mm $$ Pq &
         Pathpq.perm=mm $$ Ppq by MATRIX_3:def 8;
      hence thesis by Th31,A1;end;
    case perm is odd;
      then -mm $$ Pp=-(mm $$ Pp,perm) & -mm $$ Pq=-(mm $$ Pq,perm) &
           -mm $$ Ppq=-(mm $$ Ppq,perm) by MATRIX_2:def 16;
      then A3:Pathp.perm=-mm$$Pp & Pathq.perm=-mm$$Pq & Pathpq.perm=-mm$$Ppq
        & mm $$ Ppq=a*(mm $$ Pp)+b*(mm $$ Pq)
        by MATRIX_3:def 8,A1,Th31;
      -a*(mm $$ Pp)=a*(-mm $$ Pp) & -b*(mm $$ Pq)=b*(-mm $$ Pq)
        & -(a*(mm $$ Pp)+b*(mm $$ Pq))=-(a*(mm $$ Pp))-(b*(mm $$ Pq))
        by VECTSP_1:64,VECTSP_1:40;         
      hence thesis by A3;end;
  end;
  hence thesis;
end;

     ::------------------------------------------::
     ::       The determinant of a linear        ::
     ::          combination of lines            ::
     ::------------------------------------------::

theorem Th33:
  for l, pK, qK st l in Seg n & len pK = n & len qK = n  
    for M be Matrix of n,K holds
      Det(RLine(M,l,a*pK+b*qK)) = a*Det(RLine(M,l,pK)) + b*Det(RLine(M,l,qK))
proof
  let l, pK, qK such that A1:l in Seg n & len pK = n & len qK = n;
  let M be Matrix of n,K;   
  set aa=the add of K;
  set KK=the carrier of K;
  set P=Permutations(n);
  set Rpq=RLine(M,l,a*pK+b*qK);
  set Rp=RLine(M,l,pK); set Rq=RLine(M,l,qK);
  set Pathpq=Path_product(Rpq); set Pathp=Path_product(Rp);
  set Pathq=Path_product(Rq); set F=FinOmega(P);
  P is finite by MATRIX_2:30;
  then A3:F=P by MATRIX_2:def 17;
  consider Gpq be Function of Fin P,KK such that
    B1:Det Rpq = Gpq.F and
    B2:for e being Element of KK st e is_a_unity_wrt aa holds Gpq.{} = e and
    B3:for x being Element of P holds Gpq.{x} = Pathpq.x and
    B4:for B' being Element of Fin P st B' c= F & B' <> {}
      for x being Element of P st x in F \ B' holds Gpq.(B' \/ {x}) = 
      aa.(Gpq.B',Pathpq.x) by A3,SETWISEO:def 3;  
  consider Gp be Function of Fin P,KK such that
    C1:Det Rp = Gp.F and
    C2:for e being Element of KK st e is_a_unity_wrt aa holds Gp.{} = e and
    C3:for x being Element of P holds Gp.{x} = Pathp.x and
    C4:for B' being Element of Fin P st B' c= F & B' <> {}
      for x being Element of P st x in F \ B' holds Gp.(B' \/ {x}) =
      aa.(Gp.B',Pathp.x) by A3,SETWISEO:def 3;
  consider Gq be Function of Fin P,KK such that
    D1:Det Rq = Gq.F and
    D2:for e being Element of KK st e is_a_unity_wrt aa holds Gq.{} = e and
    D3:for x being Element of P holds Gq.{x} = Pathq.x and
    D4:for B' being Element of Fin P st B' c= F & B' <> {}
      for x being Element of P st x in F \ B' holds Gq.(B' \/ {x}) = 
      aa.(Gq.B',Pathq.x) by A3,SETWISEO:def 3;
  defpred P[Nat] means 
     for B be Element of Fin P st card B=$1 holds Gpq.B=a*Gp.B+b*Gq.B;
  A4:P[0]
  proof
    let B be Element of Fin P such that A4:card B=0;
    B={} & 0.K is_a_unity_wrt aa by A4,CARD_2:59,FVSUM_1:8;
    then Gpq.B=0.K & Gp.B=0.K & Gq.B=0.K & a*0.K =0.K & b*0.K =0.K & 
      0.K+0.K=0.K by VECTSP_1:36,B2,C2,D2,RLVECT_1:10;
    hence thesis;
  end;
  A5:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A6':P[k];
    let B be Element of Fin P such that A6:card B=k+1;
    now per cases;
      case k=0;
        then consider x such that
          A7:B={x} by A6,CARD_2:60;
        x in B & B c= P by A7,FINSUB_1:def 5,TARSKI:def 1;
        then reconsider x as Element of P;
        Gpq.B=Pathpq.x & Gp.B=Pathp.x & Gq.B=Pathq.x by A7,B3,C3,D3;
        hence thesis by A1,Th32;      
      end;
      case A7:k>0;
        consider x such that
          A8:x in B by XBOOLE_0:def 1,A6,CARD_1:78;
        B c= P by FINSUB_1:def 5;
        then reconsider x as Element of P by A8;
        B\{x} c= B & B c= P by FINSUB_1:def 5;
        then B\{x} c= P by XBOOLE_1:1;
        then reconsider B'=B\{x} as Element of Fin P by FINSUB_1:def 5;
        A10:{x} \/ B'=B & not x in B' by A8,ZFMISC_1:140,64;
        then k+1=card B'+1 & x in F\B' & B' c= F 
          by A3,A6,CARD_2:54,XBOOLE_0:def 4,FINSUB_1:def 5;
        then A12:Gpq.B'=a*Gp.B'+b*Gq.B' & Gpq.B =aa.(Gpq.B',Pathpq.x)& 
         Gp.B =aa.(Gp.B',Pathp.x) & Gq.B =aa.(Gq.B',Pathq.x) & Pathpq.x=
         a*Pathp.x+b*Pathq.x  by A1,A6',A10,B4,C4,D4,Th32,A7,CARD_1:78; 
       then Gpq.B =(a*Gp.B'+b*Gq.B')+(a*Pathp.x+b*Pathq.x)
                 .=a*Gp.B'+(b*Gq.B'+(a*Pathp.x+b*Pathq.x)) by
                   RLVECT_1:def 6
                 .=a*Gp.B'+(a*Pathp.x+(b*Gq.B'+b*Pathq.x)) by
                 RLVECT_1:def 6
                 .=(a*Gp.B'+a*Pathp.x)+(b*Gq.B'+b*Pathq.x)
                 by RLVECT_1:def 6
                 .=(a*(Gp.B'+Pathp.x))+(b*Gq.B'+b*Pathq.x) by
                 VECTSP_1:def 18
                 .=(a*(aa.(Gp.B',Pathp.x)))+(b*(Gq.B'+Pathq.x))
                 by VECTSP_1:def 18
                 .=(a*(aa.(Gp.B',Pathp.x)))+(b*(aa.(Gq.B',Pathq.x)));
        hence thesis by A12;end;
    end;
    hence thesis;
  end;
  for k be Element of NAT holds P[k] from NAT_1:sch 1(A4,A5);
  then F is Element of Fin P & P[card F];
  hence thesis by B1,C1,D1;
end;

theorem Th34:
  l in Seg n & len pK = n implies Det(RLine(A,l,a*pK)) = a*Det(RLine(A,l,pK))
proof
  assume A1:l in Seg n & len pK = n;
  pK is Element of (len pK)-tuples_on the carrier of K by FINSEQ_2:110;
  then a*pK+0.K*pK=(a+0.K)*pK & a+0.K=a by FVSUM_1:68,RLVECT_1:10;
  hence Det(RLine(A,l,a*pK))=a*Det(RLine(A,l,pK))+0.K*Det(RLine(A,l,pK))
    by A1,Th33
    .=a*Det(RLine(A,l,pK))+0.K by VECTSP_1:36
    .=a*Det(RLine(A,l,pK)) by RLVECT_1:10;
end;

theorem
  l in Seg n implies Det(RLine(A,l,a*Line(A,l))) = a*Det(A)
proof
  assume A1:l in Seg n;
  len Line(A,l)=width A & width A=n by MATRIX_1:25,def 8;
  then Det(RLine(A,l,a*Line(A,l)))=a*Det(RLine(A,l,Line(A,l))) by A1,Th34;
  hence thesis by Th30;
end;

theorem Th36:
  l in Seg n & len pK = n & len qK = n implies
     Det(RLine(A,l,pK+qK)) = Det(RLine(A,l,pK)) + Det(RLine(A,l,qK))
proof
  assume A1:l in Seg n & len pK=n & len qK=n;
  then pK is Element of (len pK)-tuples_on the carrier of K &
    qK is Element of (len pK)-tuples_on the carrier of K by FINSEQ_2:110;
  then 1_K*pK=pK & 1_K*qK=qK by FVSUM_1:70;
  hence Det(RLine(A,l,pK+qK))=1_K*Det(RLine(A,l,pK))+1_K*Det(RLine(A,l,qK))
    by A1,Th33
    .= Det(RLine(A,l,pK))+1_K*Det(RLine(A,l,qK)) by VECTSP_1:def 13
    .= Det(RLine(A,l,pK))+Det(RLine(A,l,qK)) by VECTSP_1:def 13;
end;

LM7:for F,M holds M*F is Matrix of n,m,D
proof
  let F,M;
  len M=n by MATRIX_1:def 3;
  then dom F=Seg n & rng F c=Seg n & dom M=Seg n
    by FUNCT_2:67,FINSEQ_1:def 3,RELSET_1:12;
  then A1:dom(M*F)=Seg n&n is Element of NAT by RELAT_1:46,ORDINAL1:def 13;
  then reconsider Mp=M*F as FinSequence by FINSEQ_1:def 2;
  A2:for x st x in rng Mp ex p be FinSequence of D st x = p & len p = m
  proof
    let x such that A3:x in rng Mp;
    rng Mp c= rng M & rng M c= D* by FINSEQ_1:def 4,RELAT_1:45;
    then x in D* by A3,TARSKI:def 3;
    then reconsider p=x as FinSequence of D by FINSEQ_1:def 11;
    take p;p in rng M by A3,FUNCT_1:25;
    hence thesis by MATRIX_1:def 3;
  end;
  then reconsider Mp as Matrix of D by MATRIX_1:9;
  len Mp = n & for p be FinSequence of D st p in rng Mp holds len p = m
  proof
    thus len Mp = n by A1,FINSEQ_1:def 3;
    let p be FinSequence of D;assume p in rng Mp;
    then ex q be FinSequence of D st p = q & len q = m by A2;
    hence thesis;
  end;
  hence thesis by MATRIX_1:def 3;
end;

begin :: The Determinant of a Matrix with Permutated Lines
      :: and with a Repeated Line

definition
  let n, m, D;
  let F be Function of Seg n,Seg n;
  let M be Matrix of n,m,D;
  redefine func M*F -> Matrix of n,m,D means :Def3:
    len it = len M & width it = width M &
    for i,j,k st [i,j] in Indices M & F.i = k holds it*(i,j)= M*(k,j);
  compatibility
  proof
    let Mp be Matrix of n,m,D;
    thus Mp=M*F implies len Mp = len M & width Mp = width M &
       for i,j,k st [i,j] in Indices M & F.i = k holds Mp*(i,j)=M*(k,j)
    proof
      assume A1:Mp=M*F;
      A2:len M=n & len Mp=n by MATRIX_1:def 3;
      A3:now per cases;
        case n=0;
          then width M=0 & width Mp=0 by A2,MATRIX_1:def 4;
          hence width M=width Mp;end;
        case n>0;
          then width M=m & width Mp=m by A2,MATRIX_1:20;
          hence width M=width Mp;end;
      end;
      hence len Mp = len M & width Mp = width M by A2;
      let i,j,k such that A4:[i,j] in Indices M & F.i = k;
      Indices M=[:Seg n,Seg width M:] & len Mp=n 
        by MATRIX_1:26;
      then A5:i in Seg n & j in Seg width M & dom F =Seg n 
        & dom Mp=Seg n by FUNCT_2:67,FINSEQ_1:def 3,A4,ZFMISC_1:106;
      then F.i in rng F & rng F c= Seg n by FUNCT_1:def 5,RELSET_1:12;
      then Mp.i=M.k & Line(Mp,i)=Mp.i & k in Seg n 
        by A1,A4,A5,FUNCT_1:22,MATRIX_2:10;
      then Line(Mp,i)=Line(M,k) & Line(M,k).j=M*(k,j) 
        by A5,MATRIX_2:10,MATRIX_1:def 8;
      hence thesis by A3,A5,MATRIX_1:def 8;
    end;
    assume A1:len Mp = len M & width Mp = width M;
    assume A2:for i,j,k st [i,j] in Indices M &F.i=k holds Mp*(i,j)=M*(k,j);
    reconsider Mf=M*F as Matrix of n,m,D by LM7;
    for i,j st [i,j] in Indices Mp holds Mp*(i,j) = Mf*(i,j)
    proof
      let i,j such that A3:[i,j] in Indices Mp;
      Indices Mp=[:Seg n,Seg width M:] &
        len Mf=n by A1,MATRIX_1:26;
      then A4:i in Seg n & j in Seg width M & dom F =Seg n & dom Mf=Seg n 
        by A3,FUNCT_2:67,FINSEQ_1:def 3,ZFMISC_1:106;
      then A5:F.i in rng F & rng F c= Seg n by FUNCT_1:def 5,RELSET_1:12;
      then F.i in Seg n;
      then reconsider k=F.i as Element of NAT;   
      Mf.i=M.k & Line(Mf,i)=Mf.i & k in Seg n
        by A4,A5,FUNCT_1:22,MATRIX_2:10;
      then Line(Mf,i)=Line(M,k) & Line(M,k).j=M*(k,j)& width M=len Line(M,k)
        & width Mf=len Line(Mf,i) by A4,MATRIX_2:10,MATRIX_1:def 8;
      then Mf*(i,j)=M*(k,j) & Indices Mp=Indices M
        by A1,A4,MATRIX_1:def 8,MATRIX_4:55;
      hence thesis by A2,A3;
    end;
    hence thesis by MATRIX_1:28;
  end;
  correctness by LM7;
end;

theorem Th37:
  Indices M = Indices (M*F) & 
  for i,j st [i,j] in Indices M 
     ex k st F.i = k & [k,j] in Indices M & (M*F)*(i,j) = M*(k,j)
proof set Mp=M*F;
  len M=len Mp & width M=width Mp by Def3;
  hence Indices M=Indices Mp by MATRIX_4:55;
  let i,j such that A1:[i,j] in Indices M;
  Indices M=[:Seg n,Seg width M:] by MATRIX_1:26;
  then A2:i in Seg n & j in Seg width M & dom F=Seg n 
    by A1,ZFMISC_1:106,FUNCT_2:67;
  then A3:F.i in rng F & rng F c= Seg n by FUNCT_1:def 5,RELSET_1:12;
  then (F.i) in Seg n;
  then reconsider k=F.i as Element of NAT;   
  [k,j] in [:Seg n,Seg width M:] & Indices M=[:Seg n,Seg width M:] 
    by A2,A3,ZFMISC_1:106,MATRIX_1:26;
  then [k,j] in Indices M & Mp*(i,j)=M*(k,j) by A1,Def3;
  hence thesis;
end;

theorem Th46:
  for M be Matrix of n,m,D,F
    for k st k in Seg n holds Line(M*F,k) = M.(F.k)
proof
  let M be Matrix of n,m,D,F;
  let k such that A1:k in Seg n;
  len (M*F)=n by MATRIX_1:def 3;
  then k in dom (M*F) by A1,FINSEQ_1:def 3;
  then (M*F).k=M.(F.k) by FUNCT_1:22;
  hence thesis by A1,MATRIX_2:10;
end;

theorem Th38:
  M*(idseq n) = M
proof
 reconsider I=idseq n as Permutation of Seg n;
 A1:len (M*I)=len M & width (M*I) = width M by Def3;
 for i,j st [i,j] in Indices M holds M*(i,j) = (M*I)*(i,j)
 proof
   let i,j such that A2:[i,j] in Indices M;
   consider k such that 
     A3:I.i = k & [k,j] in Indices M & (M*I)*(i,j)=M*(k,j) by A2,Th37;
   [i,j] in [:Seg n,Seg width M:] by A2,MATRIX_1:26;
   then i in Seg n by ZFMISC_1:106;
   hence thesis by A3,FUNCT_1:34;
 end;
 hence thesis by A1,MATRIX_1:21;
end;

theorem Th39:
  for p,Perm,q st q = p*Perm" 
    holds Path_matrix(p,A*Perm) = (Path_matrix(q,A))*Perm
proof
  let p,Perm,q such that A1:q=p*Perm";
  set P1=Path_matrix(p,A*Perm);
  set P2= Path_matrix(q,A);
  set Ap=A*Perm;
  reconsider perm=Perm as Element of Permutations(n) by MATRIX_2:def 11;
  perm" is Element of Permutations(n) by MATRIX_7:18;
  then perm" is Permutation of Seg n & perm is Permutation of Seg n &
    q is Permutation of Seg n & p is Permutation of Seg n by MATRIX_2:def 11;
  then A2:dom perm=Seg n & rng perm=Seg n & dom q=Seg n & rng q=Seg n &
    dom p=Seg n & rng p=Seg n & dom (perm")=Seg n & rng (perm")=Seg n 
    by FUNCT_2:def 3,67;
  len P2=n by MATRIX_3:def 7;
  then A3':dom P2=Seg n by FINSEQ_1:def 3;
  then A3:dom (P2*perm)=Seg n & n is Element of NAT 
    by A2,RELAT_1:46,ORDINAL1:def 13;
  then reconsider P2p=P2*perm as FinSequence by FINSEQ_1:def 2;
  A4:len P2p=n & len P1=n by A3,FINSEQ_1:def 3,MATRIX_3:def 7;
  now let k be Element of NAT;assume 1<=k & k<=len P1;
    then A5:k in Seg n by A4;
    then A6:p.k in Seg n by A2,FUNCT_1:12;
    then reconsider pk=p.k as Element of NAT;
    [k,pk] in [:Seg n,Seg n:] by A5,A6,ZFMISC_1:106;
    then [k,pk] in Indices A by MATRIX_1:25;
    then consider permk be Nat such that
      A7:perm.k = permk & [permk,pk] in Indices A and
      A8:(Ap)*(k,pk)=A*(permk,pk) by Th37;    
    Indices A=[:Seg n,Seg n:] by MATRIX_1:25;
    then A9:permk in Seg n by A7,ZFMISC_1:106;
    then q.permk in Seg n by A2,FUNCT_1:12;
    then reconsider qpermk=q.permk as Element of NAT;
    dom P2=Seg n & dom P2p=Seg n by A2,A3',RELAT_1:46;
    then P2.permk=A*(permk,qpermk) & P2p.k=P2.permk & k=(perm").(perm.k) 
      & q.permk=p.((perm").(perm.k)) & dom P1=Seg n
      by A1,A2,A4,A5,A7,A9,MATRIX_3:def 7,FUNCT_1:22,56,FINSEQ_1:def 3;
    hence P2p.k=P1.k by A5,A8,MATRIX_3:def 7;
  end;
  hence thesis by A4,FINSEQ_1:18;
end;

theorem Th40:
  for p,Perm,q st q=p*Perm" holds 
       (the mult of K) $$ (Path_matrix(p,A*Perm)) = 
       (the mult of K) $$ (Path_matrix(q,A))
proof
  let p,Perm,q such that A1:q=p*Perm";
  set P1=Path_matrix(p,A*Perm);
  set P2= Path_matrix(q,A);
  set Ap=A*Perm;
  set mm=the mult of K;
  now per cases;
    case n=0;
      then len P1=0 & len P2=0 & mm has_a_unity by MATRIX_3:def 7;
      then mm $$ P1=the_unity_wrt mm & mm $$ P2=the_unity_wrt mm
        by FINSOP_1:def 1;
      hence thesis;
    end;
    case n+0>0;
      then n>=1 & len P2=n & P1 = P2*Perm & Perm is Element of Permutations(n)
        by A1,Th39,NAT_1:19,MATRIX_3:def 7,MATRIX_2:def 11;
      hence thesis by MATRIX_7:33;
    end;
  end;
  hence thesis;
end;

theorem Th41:
  for p2,q2 st q2 = p2" holds sgn(p2,K) = sgn(q2,K)
proof
  let p2,q2 such that A1:q2=p2";
  n+1+1>=0+1 & n+2 is Element of NAT by XREAL_1:8,ORDINAL1:def 13;
  then -(1_K,p2)=-(1_K,q2) & -(1_K,p2)=sgn(p2,K)*1_K &
    -(1_K,q2)=sgn(q2,K)*1_K by Th26,A1,MATRIX_7:29;
  then sgn(p2,K)*1_K=sgn(q2,K) by VECTSP_1:def 13;
  hence thesis by VECTSP_1:def 13;
end;

theorem Th42:
 for M be (Matrix of n+2,K),perm2,Perm2 st perm2=Perm2
   for p2,q2 st q2 = p2*Perm2" holds
     (Path_product(M)).q2 = sgn(perm2,K)*(Path_product(M*Perm2)).p2
proof
  let M be (Matrix of n+2,K),perm2,Perm2 such that A0:perm2=Perm2;
  let p2,q2 such that A1:q2=p2*Perm2";
  set mm=the mult of K;set P=Permutations(n+2);
  set PMp=mm $$ (Path_matrix(p2,M*Perm2));set PM=mm $$ (Path_matrix(q2,M));
  reconsider perm2'=perm2" as Element of P by MATRIX_7:18;
  sgn(q2,K)=sgn(p2,K)*sgn(perm2',K) by A0,A1,Th24
           .=sgn(p2,K)*sgn(perm2,K) by Th41;
  then -(PM,q2)=(sgn(perm2,K)*sgn(p2,K))*PM by Th26
              .=sgn(perm2,K)*(sgn(p2,K)*PM) by GROUP_1:def 4
              .=sgn(perm2,K)*(sgn(p2,K)*PMp) by A1,Th40
              .=sgn(perm2,K)*-(PMp,p2) by Th26
              .=sgn(perm2,K)*((Path_product(M*Perm2)).p2) by MATRIX_3:def 8;
  hence thesis by MATRIX_3:def 8;
end;

theorem Th43:
  for perm ex P be Permutation of Permutations(n) st
    for p be Element of Permutations(n) holds P.p = p*perm
proof
  let perm;set P=Permutations(n);
  defpred g[set,set] means 
    for p be Element of Permutations(n) st $1=p holds $2=p*perm;
  A1:for x st x in P ex y be set st y in P & g[x,y]
  proof
    let x;assume x in P;
    then reconsider p=x as Element of P;
    reconsider pp=p*perm as Element of P by MATRIX_9:39;
    take pp;thus thesis;
  end;
  consider G be Function of P,P such that 
    A2:for x st x in P holds g[x,G.x] from FUNCT_2:sch 1(A1);
  for x1,x2 be set st x1 in P & x2 in P & G.x1 = G.x2 holds x1 = x2
  proof
    let x1,x2 be set such that A3:x1 in P & x2 in P & G.x1=G.x2;
    reconsider p1=x1,p2=x2 as Element of P by A3;
    perm is Permutation of Seg n & p1 is Permutation of Seg n & 
      p2 is Permutation of Seg n by MATRIX_2:def 11;
    then A4:rng perm=Seg n & dom p1=Seg n & dom p2=Seg n & G.p1=p1*perm & 
      G.p2=p2*perm by A2,FUNCT_2:def 3,67;
    then p1=p1*(id rng perm) by RELAT_1:78
          .= p1*(perm*perm") by FUNCT_1:61    
          .= p2*perm*perm" by A3,A4,RELAT_1:55
          .= p2*(perm*perm") by RELAT_1:55
          .=p2*(id rng perm) by FUNCT_1:61
          .=p2 by A4,RELAT_1:78;
    hence thesis;
  end;
  then A3:G is one-to-one & P is finite set & Card P=Card P
    by FUNCT_2:25,MATRIX_2:30;
  then G is onto by STIRL2_1:70;
  then reconsider G as Permutation of P by A3,FUNCT_2:def 4;
  take G;
  thus thesis by A2;
end;

theorem Th44:
  for M be Matrix of n+2,n+2,K, perm2,Perm2 st perm2=Perm2
    holds Det(M*Perm2) = sgn(perm2,K)*Det(M)
proof
  set n2=n+2;
  let M be Matrix of n2,n2,K, perm2,Perm2 such that A0:perm2=Perm2;
  set aa=the add of K;
  set KK=the carrier of K;
  set P=Permutations(n2);
  set Mperm=M*Perm2;
  set PathM=Path_product(M); set PathMp=Path_product(Mperm);
  set F=FinOmega(P);
  P is finite by MATRIX_2:30;
  then A1:F=P by MATRIX_2:def 17;
  consider GM be Function of Fin P,KK such that
    B1:Det M = GM.F and
    for e being Element of KK st e is_a_unity_wrt aa holds GM.{} = e and
    B3:for x being Element of P holds GM.{x} = PathM.x and
    B4:for B' being Element of Fin P st B' c= F & B' <> {}
      for x being Element of P st x in F \ B' holds GM.(B' \/ {x}) =
      aa.(GM.B',PathM.x) by A1,SETWISEO:def 3;
  consider GMp be Function of Fin P,KK such that
    C1:Det Mperm = GMp.F and
    for e being Element of KK st e is_a_unity_wrt aa holds GMp.{} = e and
    C3:for x being Element of P holds GMp.{x} = PathMp.x and
    C4:for B' being Element of Fin P st B' c= F & B' <> {}
      for x being Element of P st x in F \ B' holds GMp.(B' \/ {x}) =
      aa.(GMp.B',PathMp.x) by A1,SETWISEO:def 3;
  reconsider perm2'=perm2" as Element of P by MATRIX_7:18;
  consider PERM be Permutation of P such that
    A2:for p be Element of P holds PERM.p=p*perm2' by Th43;
  defpred P[Nat] means $1<>0 implies
    for B be Element of Fin P st card B=$1
      holds sgn(perm2,K)*GMp.B=GM.(PERM.:B);
A3:P[0];
A4:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A5:P[k];set k1=k+1;
    assume k1<>0;
    let B be Element of Fin P such that A6:card B=k1;
    per cases;
    suppose k=0;
      then consider x such that
   A7:B={x} by CARD_2:60,A6;
      x in B & B c= P by A7,FINSUB_1:def 5,TARSKI:def 1;
      then reconsider x as Element of P;
      GMp.{x}=PathMp.x&GM.{PERM.x}=PathM.(PERM.x)&PERM.x=x*perm2'
        by A2,B3,C3;
      then sgn(perm2,K)* GMp.B = GM.{PERM.x} & P=dom PERM
        by A0,A7,Th42,FUNCT_2:67; then
      sgn(perm2,K)* GMp.B = GM.(Im(PERM,x)) & P=dom PERM by FUNCT_1:117;
      hence thesis by A7;end;
    suppose A7:k>0;
      consider x such that
        A8:x in B by XBOOLE_0:def 1,A6,CARD_1:78;
      B c= P by FINSUB_1:def 5;
      then reconsider x as Element of P by A8;
      reconsider Px=PERM.x as Element of P;
      B\{x} c= B & B c= P & PERM.:(B\{x}) c= rng PERM & PERM.:B c= rng PERM
        by FINSUB_1:def 5,RELAT_1:144;
      then A10:B\{x} c= P & PERM.:(B\{x}) c= P & PERM.:B c= P 
        by XBOOLE_1:1,FUNCT_2:def 3;
      then reconsider B'=B\{x},PeBx=PERM.:(B\{x}),PeB=PERM.:B 
        as Element of Fin P by FINSUB_1:def 5;
      A11:{x} \/ B'=B & not x in B' & B' misses {x} & dom PERM=P
        by A8,ZFMISC_1:140,64,XBOOLE_1:79,FUNCT_2:67;
      then A:PERM.:B=Im(PERM,x)\/PeBx & B'/\{x}={} & Im(PERM,x)={Px}
        by RELAT_1:153,XBOOLE_0:def 7,FUNCT_1:117;
      PeB={Px}\/PeBx & PERM.:{}={Px}/\PeBx & Px in {Px} & PERM.:{}={}
        by A,FUNCT_1:121,TARSKI:def 1,RELAT_1:149;
      then not Px in PeBx by XBOOLE_0:def 3;
      then A13:k+1=card B'+1 & x in F\B' & Px in F\PeBx
        by A11,A1,A6,CARD_2:54,XBOOLE_0:def 4; then
      consider y such that
        A15:y in B' by XBOOLE_0:def 1,A7,CARD_1:78;
      B' c= P & dom PERM=P by FUNCT_2:67,FINSUB_1:def 5;
      then PERM.y in PeBx & PERM.x=x*perm2' by A2,A15,FUNCT_1:def 12;
      then GM.PeB = aa.(GM.PeBx,PathM.Px) & sgn(perm2,K)* PathMp.x = PathM.Px
        by A0,A1,A,A13,A10,B4,Th42;
      then GM.PeB=(sgn(perm2,K)*GMp.B')+(sgn(perm2,K)*PathMp.x)by A13,A5,A7
                .=sgn(perm2,K)*(GMp.B'+PathMp.x) by VECTSP_1:def 18
                .=sgn(perm2,K)*GMp.B by A13,A1,A11,A7,A10,C4,CARD_1:78;
      hence thesis;
    end;
  end;
A5: for k be Element of NAT holds P[k] from NAT_1:sch 1(A3,A4);
  A6:(sgn(perm2,K)=1_K or sgn(perm2,K)=-1_K) & (1_K)*(1_K)=1_K &
     (1_K)*(1_K)=(-1_K)*(-1_K) by VECTSP_1:42,Th11,VECTSP_1:def 13;
  card F<>0 & PERM.:dom PERM=rng PERM & dom PERM=P & rng PERM=P
    by A1,CARD_2:59,RELAT_1:146,FUNCT_2:67,def 3;
  then sgn(perm2,K)*Det Mperm=Det M by B1,C1,A1,A5;
  hence sgn(perm2,K)*Det M = 1_K*Det Mperm by A6,GROUP_1:def 4
                           .= Det Mperm by VECTSP_1:def 13;
end;

     ::------------------------------------------::
     ::    The determinant of a matrix with      ::
     ::            permutated lines              ::
     ::------------------------------------------::

theorem Th45:
  for M be (Matrix of n,K),perm,Perm st perm = Perm holds
    Det(M*Perm) = -(Det(M),perm)
proof
  let M be (Matrix of n,K),perm,Perm such that A0:Perm=perm;
  per cases;
    suppose n<2;
      then perm is even & perm=idseq n by LM3;
      then M*perm=M & -(Det(M),perm)=Det M by Th38,MATRIX_2:def 16;
      hence thesis by A0;end;
    suppose n>=2;
      then reconsider n2=n-2 as Nat by NAT_1:21;
      reconsider M'=M as Matrix of n2+2,K;
      reconsider Perm2=Perm as Permutation of Seg(n2+2);
      reconsider perm2=perm as Element of Permutations(n2+2);
      Det(M'*Perm2) = sgn(perm2,K)*Det(M') by A0,Th44;
      hence thesis by Th26;end;
end;

theorem Th47:
  for PERM be Permutation of Permutations(n), perm st
     perm is odd & for p holds PERM.p = p*perm holds
  PERM.:{p:p is even} = {q:q is odd}
proof
  set P=Permutations(n);
  let PERM be Permutation of P, perm such that
    A1:perm is odd and 
    A2:for p holds PERM.p=p*perm;
  set OD={q:q is odd};
  set E={p:p is even};
  for y holds y in OD iff ex x st x in dom PERM & x in E & y = PERM.x
  proof
    let y;
    thus y in OD implies ex x st x in dom PERM & x in E & y = PERM.x
    proof
      assume y in OD;
      then consider q such that
        A3:y=q & q is odd;
      reconsider perm'=perm" as Element of P by MATRIX_7:18;
      reconsider qp'=q*perm' as Element of P by MATRIX_9:39;
      take qp';
      n>=2 & 2>=1 by A1,LM3;
      then n>=1 & n is Element of NAT by ORDINAL1:def 13,XXREAL_0:2;
      then dom PERM=P & perm'*perm=idseq n & PERM.qp'=qp'*perm & q*(idseq n)=q
        & perm' is odd by A1,A2,FUNCT_2:67,MATRIX_2:24,25,MATRIX_7:28;
      then qp' in dom PERM & PERM.qp'=q & q*perm' is even & len P = n
        by A3,Th25,RELAT_1:55,MATRIX_2:20;
      hence thesis by A3;
    end;
    assume ex x st x in dom PERM & x in E & y = PERM.x;
    then consider x such that
      A3:x in dom PERM & x in E & y = PERM.x;
    consider p such that
      A4:p=x & p is even by A3;
    reconsider pp=p*perm as Element of P by MATRIX_9:39;
    PERM.x=p*perm & pp is odd by A2,A4,A1,Th25;
    hence thesis by A3;
  end;
  hence thesis by FUNCT_1:def 12;
end;

LM8:for n,i,j st i in Seg n & j in Seg n & i<j holds
  ex ODD,EVEN be finite set st
    EVEN ={p:p is even} &
    ODD = {q:q is odd} &
    EVEN /\ ODD = {} & EVEN \/ ODD = Permutations(n) &
    ex PERM be Function of EVEN,ODD, perm st
       perm is_transposition & perm.i=j & dom PERM=EVEN &
       PERM is bijective & for p st p in EVEN holds PERM.p=p*perm
proof
  let n,i,j such that A1:i in Seg n & j in Seg n & i<j;
  set P=Permutations(n);
  consider tr be Element of P such that
    A2:tr is_transposition & tr.i = j by A1,Th16;
  consider PE be Permutation of P such that
    A3:for p holds PE.p=p*tr by Th43;
  set EVEN={p:p is even};
  set ODD={q:q is odd};
  A4:EVEN c= P
  proof let x;assume x in EVEN;then
    ex p st p=x & p is even;
    hence thesis;
  end;
  A5:ODD c= P
  proof let x such that A6:x in ODD;
    ex q st q=x & q is odd by A6;
    hence thesis;
  end;
  P is finite by MATRIX_2:30;
  then reconsider O=ODD,E=EVEN as finite set by A4,A5,FINSET_1:13;
  take O,E;
  thus  E ={p:p is even} & O ={q:q is odd};
  thus E /\ O = {}
  proof
    assume E /\ O <>{};
    then consider x such that 
      A6:x in E/\O by XBOOLE_0:def 1;
    x in E & x in O by A6,XBOOLE_0:def 3;
    then (ex p st p=x & p is even) & (ex q st q=x & q is odd);
    hence thesis;
  end;
  thus E \/ O = P
  proof
    thus E \/ O c= P by A4,A5,XBOOLE_1:8;
    let x such that A6:x in P;
    reconsider p=x as Element of P by A6;
    p is even or p is odd;
    then p in E or p in O;
    hence thesis by XBOOLE_0:def 2;
  end;
  {i,j} in 2Set Seg n by A1,Th1;
  then n>=2 by Th2,NAT_1:23;
  then reconsider n2=n-2 as Nat by NAT_1:21;
  tr is Element of Permutations(n2+2);
  then tr is odd by Th27,A2; 
  then A6':PE.:E=O by A3,Th47;
  set PERM=PE|E;
  A7':dom PE=P by FUNCT_2:67;
  then A7:dom PERM=E & rng PERM=O by A4,A6',RELAT_1:148,91;
  then reconsider PERM as Function of E,O by FUNCT_2:3;
  take PERM,tr;
  PERM is one-to-one onto by A7,FUNCT_1:84,FUNCT_2:def 3;
  hence tr is_transposition & tr.i=j & dom PERM=E & PERM is bijective 
    by A2,A7',A4,RELAT_1:91,FUNCT_2:def 4;
  let p such that A8:p in E;
  PERM.p=PE.p & PE.p=p*tr by A3,A7,A8, FUNCT_1:70;
  hence thesis;
end;

theorem
  for n st n >= 2 ex ODD,EVEN be finite set st
    EVEN ={p:p is even} & ODD = {q:q is odd} &
    EVEN /\ ODD = {} & EVEN \/ ODD =Permutations(n) & card EVEN = card ODD
proof
  let n such that A1:n >= 2;
  1 <= n by A1,XXREAL_0:2;
  then 1 in Seg n & 2 in Seg n by A1;
  then consider O,E be finite set such that
    A2:E ={p:p is even} & O = {q:q is odd} and
    A3:E /\ O = {} & E \/ O = Permutations(n) and
    A4:ex P be Function of E,O, perm st
       perm is_transposition & perm.1=2 & dom P=E & 
       P is bijective & for p st p in E holds P.p=p*perm by LM8;
  consider P be Function of E,O, perm such that
    A5:perm is_transposition & perm.1=2 & dom P=E and
    A6:P is bijective & for p st p in E holds P.p=p*perm by A4;
  P is onto by A6,FUNCT_2:def 4;
  then rng P=O & P is one-to-one by A6,FUNCT_2:def 3,def 4;
  then E,O are_equipotent by A5,WELLORD2:def 4;
  then card E=card O by CARD_1:21;
  hence thesis by A2,A3;
end;

theorem Th49:
  for i,j st i in Seg n & j in Seg n & i < j
    for M be (Matrix of n,K) st Line(M,i) = Line(M,j) 
      for p,q,tr be Element of Permutations(n) 
          st q = p*tr & tr is_transposition & tr.i=j holds
     (Path_product M).q = - (Path_product M).p
proof
  let i,j such that A1:i in Seg n & j in Seg n & i < j;
  let M be (Matrix of n,K) such that A2:Line(M,i) = Line(M,j);
  let p,q,tr be Element of Permutations(n) such that
    A3:q = p*tr & tr is_transposition & tr.i=j;
  {i,j} in 2Set Seg n by A1,Th1;
  then n>=2 by Th2,NAT_1:23;
  then reconsider n2=n-2 as Nat by NAT_1:21;
  reconsider M'=M as Matrix of n2+2,K;
  reconsider TR=tr as Permutation of Seg (n2+2) by MATRIX_2:def 11; 
  reconsider Tr=tr,p2=p as Element of Permutations(n2+2); 
  set Mt=M'*TR;
  A4:len Mt=len M' by Def3;
  for k be Element of NAT st 1 <=k & k <= len M' holds M'.k=Mt.k
  proof
    let k be Element of NAT such that A5:1 <=k & k <= len M';
    A6:k in Seg (len M') & len M'=n & dom TR=Seg n 
      by A5,MATRIX_1:def 3,FUNCT_2:67;
    then A7:Line(Mt,k)=M.(tr.k) & Line(M,i)=M.i & Line(M,j)=M.j & Line(M,k)=M.k 
      by A1,Th46,MATRIX_2:10;
    per cases;
      suppose k=i;
         hence thesis by A1,A2,A3,A7,MATRIX_2:10;end;
      suppose k=j;
         then Line(Mt,k)=M.i & M.k=M.i & Line(Mt,k)=Mt.k
           by A1,A2,A3,A7,Th8,MATRIX_2:10;
          hence thesis;end;
      suppose k<>i & k<>j;
         then Line(Mt,k)=M.k & Line(Mt,k)=Mt.k
           by A1,A3,A6,A7,Th8,MATRIX_2:10;
         hence thesis;end;
  end;
  then Mt=M & tr=tr" & sgn(Tr,K)=-1_K by A3,A4,FINSEQ_1:18,Th20,Th14;
  hence (Path_product M).q = (-1_K)*((Path_product M').p2) by A3,Th42
                          .= -1_K*((Path_product M').p2) by VECTSP_1:41
                          .= -(Path_product M).p by VECTSP_1:def 13;
end;

     ::------------------------------------------::
     ::     The determinant of a matrix with     ::
     ::            a repeated line               ::
     ::------------------------------------------::

theorem Th50:
  for i,j st i in Seg n & j in Seg n & i < j
    for M be (Matrix of n,K) st Line(M,i) = Line(M,j) holds
      Det(M) = 0.K
proof
  let i,j such that A1:i in Seg n & j in Seg n & i < j;
  let M be (Matrix of n,K) such that A2:Line(M,i) = Line(M,j);
  set P=Permutations(n);
  set aa=the add of K;
  set KK=the carrier of K;
  set PathM=Path_product(M);
  set F=FinOmega(P);
  consider Q,E be finite set such that
    E ={p:p is even} & Q = {q:q is odd} and
    A4:E /\ Q = {} & E \/ Q = P and
    A5:ex P be Function of E,Q,tr be Element of Permutations n st
       tr is_transposition & tr.i=j & dom P=E &
       P is bijective & for p st p in E holds P.p=p*tr by A1,LM8;
  consider PERM be Function of E,Q, tr be Element of Permutations n such that      
    A6:tr is_transposition & tr.i=j & dom PERM = E and
    A7:PERM is bijective & for p st p in E holds PERM.p=p*tr by A5;
  E c= P & Q c= P by A4,XBOOLE_1:7;
  then reconsider E,Q as Element of Fin P by FINSUB_1:def 5;
  aa has_a_unity by FVSUM_1:10;
  then consider GE be Function of Fin P,KK such that
    B1:aa $$ (E,PathM) = GE.E and
    B2:for e being Element of KK st e is_a_unity_wrt aa holds GE.{} = e and
    B3:for x being Element of P holds GE.{x} = PathM.x and
    B4:for B' being Element of Fin P st B' c= E & B' <> {}
      for x being Element of P st x in E \ B' holds GE.(B' \/ {x}) = 
      aa.(GE.B',PathM.x) by SETWISEO:def 3;  
  aa has_a_unity by FVSUM_1:10;
  then consider GQ be Function of Fin P,KK such that
    C1:aa $$ (Q,PathM) = GQ.Q and
    C2:for e being Element of KK st e is_a_unity_wrt aa holds GQ.{} = e and
    C3:for x being Element of P holds GQ.{x} = PathM.x and
    C4:for B' being Element of Fin P st B' c= Q & B' <> {}
      for x being Element of P st x in Q \ B' holds GQ.(B' \/ {x}) = 
      aa.(GQ.B',PathM.x) by SETWISEO:def 3;    
  defpred P[Nat] means 
    for B,PB be Element of Fin P st card B=$1 & B c= E & PERM.:B=PB holds 
      GE.B + GQ.PB = 0.K;
A8:P[0]
  proof
     let B,PB be Element of Fin P such that A9:card B=0 & B c= E & PERM.:B=PB;
    B={} & 0.K is_a_unity_wrt aa & PERM.:{}={} 
      by A9,CARD_2:59,FVSUM_1:8,RELAT_1:149;
    then GE.B=0.K & GQ.PB=0.K by B2,C2,A9;
    hence thesis by RLVECT_1:def 7;
  end;
A9:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A9:P[k];
    let B,PB be Element of Fin P such that A10:card B=k+1 & B c=E&PERM.:B=PB;
    now per cases;
      case k=0;
        then consider x such that
          A11:B={x} by A10,CARD_2:60;
        A12:x in B & B c= P by A11,FINSUB_1:def 5,TARSKI:def 1;
        then reconsider x as Element of P;
        x*tr is Element of P & x in E by A10,MATRIX_9:39,A12;
        then reconsider Px=PERM.x as Element of P by A7;  
        GE.{x}=PathM.x & GQ.{PERM.x}=PathM.(Px) & Px = x*tr & Im(PERM,x)={Px}
          by A6,A7,A10,A12,B3,C3,FUNCT_1:117;
        then -GE.B=GQ.PB by A1,A2,A6,A10,A11,Th49;
        hence thesis by RLVECT_1:def 10;end;
      case A10':k>0;
        consider x such that
          A11:x in B by A10,XBOOLE_0:def 1,CARD_1:78;
        x in B & B c= P by A11,FINSUB_1:def 5;
        then reconsider x as Element of P;
        x*tr is Element of P & x in E by A10,MATRIX_9:39,A11;
        then reconsider Px=PERM.x as Element of P by A7;
        PERM is onto by A7,FUNCT_2:def 4;
        then A13':B\{x} c= B & B c= P & PERM.:(B\{x}) c= rng PERM &
          PERM.:B c= rng PERM & rng PERM =Q & Q c= P
          by FINSUB_1:def 5,RELAT_1:144,FUNCT_2:def 3;
        then A13:B\{x} c=P& PERM.:(B\{x})c=P & PERM.:B c=P& Px in Q &B\{x}c=E&
          PERM.:(B\{x}) c= Q by A10,A6,A11,XBOOLE_1:1,FUNCT_1:def 5;
        then reconsider B'=B\{x},PeBx=PERM.:(B\{x}),PeB=PERM.:B 
          as Element of Fin P by FINSUB_1:def 5;
        A14:{x} \/ B'=B & not x in B' & B' misses {x} 
          by A11,ZFMISC_1:140,64,XBOOLE_1:79;
        then A15:PERM.:B=Im(PERM,x)\/PeBx & B'/\{x}={} & Im(PERM,x)={Px} &
          PERM is one-to-one by A7,A6,A10,A11,RELAT_1:153,XBOOLE_0:def 7,
          FUNCT_1:117,FUNCT_2:def 4;
        then PeB={Px}\/PeBx & PERM.:{}={Px}/\PeBx & Px in {Px}&PERM.:{}={}
          by FUNCT_1:121,TARSKI:def 1,RELAT_1:149;
        then not Px in PeBx by XBOOLE_0:def 3;
        then A17:k+1=card B'+1 & x in E\B' & Px in Q\PeBx & B' c= E
          by A10,A11,A13,A14,CARD_2:54,XBOOLE_0:def 4; then
        consider y such that
          A19:y in B' by XBOOLE_0:def 1,A10',CARD_1:78;
        PERM.y in PeBx & Px=x*tr by A6,A7,A10,A11,A19,A13,FUNCT_1:def 12;
        then GQ.PB = aa.(GQ.PeBx,PathM.Px) & -PathM.x=PathM.Px 
          by A1,A2,A10,A13',A6,A15,A17,C4,Th49;
        hence GQ.PB+GE.B = (GQ.PeBx-PathM.x)+(GE.B'+PathM.x)
              by A17,A10',A14,B4,CARD_1:78
                        .= GQ.PeBx+(-PathM.x+(GE.B'+PathM.x)) by
                        RLVECT_1:def 6
                        .= GQ.PeBx+(GE.B'+(PathM.x-PathM.x)) by
                        RLVECT_1:def 6
                        .= GQ.PeBx+(GE.B'+0.K) by RLVECT_1:def 10
                        .= (GQ.PeBx+GE.B')+0.K by RLVECT_1:def 6
                        .= 0.K+0.K by A17,A9
                        .=0.K by RLVECT_1:10;end;
    end;
    hence thesis;
  end;
A10:for k be Element of NAT holds P[k] from NAT_1:sch 1(A8,A9);
  PERM is onto by A7,FUNCT_2:def 4;
  then rng PERM=Q by FUNCT_2:def 3;
  then PERM.:E=Q & P[card E] & aa has_a_unity & E misses Q & P is finite
    by A6,A10,RELAT_1:146,FVSUM_1:10,A4,XBOOLE_0:def 7;
  then aa $$ (E,PathM)+aa $$ (Q,PathM)=0.K & P=F &
       aa $$ (E\/Q,PathM)=aa.(aa $$ (E,PathM),aa $$ (Q,PathM))
    by B1,C1,SETWOP_2:6,MATRIX_2:def 17;
  hence thesis by A4;
end;

theorem Th51:
  for i, j st i in Seg n & j in Seg n & i <> j holds
    Det RLine(A,i,Line(A,j)) = 0.K
proof
  let i, j such that A1:i in Seg n & j in Seg n & i <> j;
  len Line(A,j) = width A by MATRIX_1:def 8;
  then Line(RLine(A,i,Line(A,j)),i)=Line(A,j) & (i<j or j<i) &
     Line(RLine(A,i,Line(A,j)),j)=Line(A,j) by A1,Th28,REAL_1:def 5;
  hence thesis by A1,Th50;
end;

theorem Th52:
  for i, j st i in Seg n & j in Seg n & i <> j holds
    Det RLine(A,i,a*Line(A,j)) = 0.K
proof
  let i, j such that A1:i in Seg n & j in Seg n & i <> j;
  width A = n by MATRIX_1:25;
  then len Line(A,j)=n by MATRIX_1:def 8;
  hence Det(RLine(A,i,a*Line(A,j))) = a*Det(RLine(A,i,Line(A,j))) by A1,Th34
                                   .= a*0.K by A1,Th51
                                   .= 0.K by VECTSP_1:36;
end;

theorem
  for i, j st i in Seg n & j in Seg n & i <> j holds
    Det A = Det RLine(A,i,Line(A,i) + a*Line(A,j))
proof
  let i, j such that A1:i in Seg n & j in Seg n & i <> j;
  width A = n by MATRIX_1:25;
  then len Line(A,i)=n & len Line(A,j)=n & len Line(A,j)=len (a*Line(A,j))
    by LM5,MATRIX_1:def 8;
  hence Det RLine(A,i,Line(A,i)+a*Line(A,j))=
  Det(RLine(A,i,Line(A,i))) + Det(RLine(A,i,a*Line(A,j))) by A1,Th36
  .=Det(A)+Det(RLine(A,i,a*Line(A,j))) by Th30
  .=Det(A)+0.K by A1,Th52
  .=Det(A) by RLVECT_1:10;
end;

theorem Th54:
  not F in Permutations(n) implies Det(A*F) = 0.K
proof
  assume not F in Permutations(n);
  then not F is Permutation of Seg n by MATRIX_2:def 11;
  then (F is not onto or F is not one-to-one) & card Seg n=card Seg n
    by FUNCT_2:def 4;
  then F is not one-to-one by STIRL2_1:70;
  then consider x,y such that
  A1:x in dom F & y in dom F & F.x = F.y & x<>y by FUNCT_1:def 8;
  A2:dom F=Seg n by FUNCT_2:67; 
  then reconsider x,y as Nat by A1,ORDINAL1:def 13;
  Line(A*F,x)=A.(F.x) & Line(A*F,y)=A.(F.y) by A1,A2,Th46;
  then Line(A*F,x)=Line(A*F,y) & (x>y or y>x) by A1,REAL_1:def 5;
  hence thesis by A1,A2,Th50;
end;

begin :: The Determinant of a Product of Two Square Matrices

definition
  let K be non empty LoopStr;
  func addFinS(K) -> BinOp of (the carrier of K)* means :Def4:
    for p1,p2 be Element of (the carrier of K)* holds
      it.(p1,p2)=p1+p2;
existence
  proof
    set KK= the carrier of K;
    defpred ADD[set,set,set] means
      for p1,p2 be Element of KK* st $1=p1 & $2=p2 holds $3=p1+p2;
    A1:for x be Element of KK* for y be Element of KK*
        ex z be Element of KK* st ADD[x,y,z] 
    proof
      let x be Element of KK*;let y be Element of KK*;
      reconsider p1=x,p2=y as FinSequence of KK;
      reconsider pp=p1+p2 as Element of KK* by FINSEQ_1:def 11;
      take pp;thus thesis;
    end;
    consider A be Function of [:KK*,KK*:],KK* such that
      A2:for x be Element of KK* for y be Element of KK* holds ADD[x,y,A.(x,y)]
      from BINOP_1:sch 3(A1);
    take A;
    thus thesis by A2;
  end;
uniqueness 
  proof
    set KK= the carrier of K;
    let f1,f2 be Function of [:KK*,KK*:],KK* such that
      A1:for p1,p2 be Element of KK* holds f1.(p1,p2)=p1+p2 and
      A2:for p1,p2 be Element of KK* holds f2.(p1,p2)=p1+p2;
    now let p1 be Element of KK*;
        let p2 be Element of KK*;
      f1.(p1,p2)=p1+p2 & f2.(p1,p2)=p1+p2 by A1,A2;
      hence f1.(p1,p2) = f2.(p1,p2);
    end;
    hence thesis by BINOP_1:2;
  end;
end;

LM9:for K be non empty LoopStr
    for p1,p2 be Element of (the carrier of K)* holds
      dom (p1+p2)=dom p1/\dom p2
proof
  let K be non empty LoopStr;
  let p1,p2 be Element of (the carrier of K)*;
  rng p1 c= the carrier of K & rng p2 c= the carrier of K by FINSEQ_1:def 4;
  then [:rng p1,rng p2:]c=[:the carrier of K,the carrier of K:]by ZFMISC_1:119;
  then [:rng p1, rng p2:] c= dom (the add of K) by FUNCT_2:def 1;
  hence thesis by FUNCOP_1:84;
end;

registration let K be Abelian (non empty LoopStr);
  cluster addFinS(K) -> commutative;
  coherence
  proof
    set KK= the carrier of K;set ADD=the add of K;
    let p1,p2 be Element of KK*;
    A0:rng p1 c= KK & rng p2 c= KK by FINSEQ_1:def 4;
    A1:dom(p1+p2)=dom p1/\dom p2 & 
         dom(p2+p1)=dom p2/\dom p1 by LM9;
    now let k be Element of NAT such that A2:k in dom (p1+p2);
      k in dom p1 & k in dom p2 by A1,A2,XBOOLE_0:def 3;
      then p1.k in rng p1 & p2.k in rng p2 by FUNCT_1:def 5;
      then reconsider p1k=p1.k,p2k=p2.k as Element of K by A0;
      (p1+p2).k=p1k+p2k & (p2+p1).k=p2k+p1k by A1,A2,FVSUM_1:21;
      hence (p1+p2).k=(p2+p1).k;
    end;
    then p1+p2=p2+p1 & (addFinS(K)).(p1,p2)=p1+p2 by A1,FINSEQ_1:17,Def4;
    hence thesis by Def4;
  end;
end;

registration let K be add-associative (non empty LoopStr);
  cluster addFinS(K) -> associative;
  coherence
  proof   
    set KK= the carrier of K;set ADD=the add of K;set aK=addFinS(K);
    set kk=[:KK,KK:];
    let p1,p2,p3 be Element of KK*;
    reconsider p12=p1+p2,p23=p2+p3 as Element of KK* by FINSEQ_1:def 11;
    A0:rng p1 c=KK & rng p2 c=KK & rng p3 c=KK & rng p12 c=KK & rng p23 c=KK 
      by FINSEQ_1:def 4;    
    A1:dom(p12)=dom p1/\dom p2 & dom (p12+p3)=dom p12/\dom p3 &
       dom(p23)=dom p2/\dom p3 & dom (p1+p23)=dom p1/\dom p23
        by LM9;
    then A1':dom (p12+p3)=dom (p1+p23) by XBOOLE_1:16;
    now let k be Element of NAT such that A2:k in dom (p12+p3);
      A3:k in dom p12 & k in dom p3 by A1,A2,XBOOLE_0:def 3;
      then A4:k in dom p1 & k in dom p2 by A1,XBOOLE_0:def 3;
      then A5:k in dom p23 by A3,A1,XBOOLE_0:def 3;
      then p1.k in rng p1 & p12.k in rng p12 & p3.k in rng p3
         & p23.k in rng p23 & p2.k in rng p2 by A3,A4,FUNCT_1:def 5;
      then reconsider p1k=p1.k, p12k=p12.k,p2k=p2.k,p23k=p23.k,p3k=p3.k
        as Element of K by A0;
      (p12).k=p1k+p2k & (p23).k=p2k+p3k & (p12+p3).k=p12k+p3k 
        & (p1+p23).k=p1k+p23k by A1',A2,A3,A5,FVSUM_1:21;
      hence (p1+p23).k=(p12+p3).k by RLVECT_1:def 6;
    end;
    then A3:p1+p23 = p12+p3 by A1',FINSEQ_1:17;
    thus aK.(p1,aK.(p2,p3)) = aK.(p1,p23) by Def4
                           .= p1+p23 by Def4
                           .= aK.(p12,p3) by A3,Def4
                           .= aK.(aK.(p1,p2),p3) by Def4;
  end;
end;

theorem Th55:
  for A,B be Matrix of K st width A = len B & len B > 0
    for i st i in Seg len A
  ex P be FinSequence of (the carrier of K)* st
      len P = len B &
      Line(A*B,i) = addFinS(K) "**" P &
      for j st j in Seg len B holds P.j = A*(i,j) * Line(B,j)
proof
  let A,B be Matrix of K such that A1:width A = len B & len B > 0;
  let i such that A2:i in Seg len A;
  set KK=the carrier of K;set a=addFinS(K);
  set mm=the mult of K; set aa=the add of K;
  deffunc F(Element of NAT)=A*(i,$1) * Line(B,$1);
  consider P be FinSequence such that
      A3:len P = len B and
      A4:for k be Element of NAT st k in Seg len B holds P.k = F(k) 
    from FINSEQ_1:sch 2;
  reconsider n=len P,m=len B,w=width B as Nat;
  rng P c= KK*
  proof
    let y such that A5:y in rng P;
    consider x such that
      A6:x in dom P & y=P.x by A5,FUNCT_1:def 5;
    A7:dom P=Seg m by A3,FINSEQ_1:def 3;
    then reconsider x as Element of NAT by A6;
    P.x=A*(i,x) * Line(B,x) by A4,A6,A7;
    hence thesis by A6,FINSEQ_1:def 11;
  end;
  then reconsider P as FinSequence of KK* by FINSEQ_1:def 4;
  take P;
  thus len P=len B by A3;
  A5:m>=1 by A1,NAT_1:14;
  then consider F be Function of NAT,KK* such that
    A6:F.1 = P.1 and
    A7:for n be Element of NAT st 0<>n & n<len P holds F.(n+1)=a.(F.n,P.(n+1))
    and A8:a "**" P = F.(len P) by FINSOP_1:def 1,A3;
  defpred P[Nat] means
    1 <= $1 & $1 <= m implies for F1 be FinSequence of KK st F.$1=F1 holds
      len F1=w & for j be Element of NAT st j in Seg w holds
       ex LC be FinSequence of KK st 
       LC = mlt(Line(A,i),Col(B,j))|(Seg $1) & aa"**"LC=F1.j;
  A9:P[0];
  A10:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A11:P[k];
    set k1=k+1;
    assume A12:1 <= k1 & k1 <= m;
    then A13:k1 in Seg m;
    let Fk1 be FinSequence of KK such that A14:F.k1=Fk1;
    per cases;
      suppose A14':k=0;
        then A15:Fk1=P.1 & P.1=A*(i,1) * Line(B,1) & len Line(B,1) =w 
          by A4,A6,A13,A14,MATRIX_1:def 8;
      hence len Fk1= w by LM5;
        let j be Element of NAT such that A16:j in Seg w;
        set L=Line(A,i);
        set C=Col(B,j);     
        A17:mm.:(L,C) is Element of m-tuples_on KK by A1,FINSEQ_2:140;
        reconsider LC1=(mlt(L,C))|(Seg k1),mLC=mlt(L,C) as  FinSequence of KK 
          by FINSEQ_1:23;
      take LC1;
        1 in Seg m & Seg m=dom B & len mLC=m 
          by A17,A5,FINSEQ_2:109,FINSEQ_1:def 3;
        then A18:L.1=A*(i,1) & C.1=B*(1,j) & Line(B,1).j=B*(1,j) &1 in dom mLC
          by A1,A16,MATRIX_1:def 8,def 9,FINSEQ_1:def 3;
        then A19:mLC.1=(A*(i,1)) * (B*(1,j)) by FVSUM_1:73;
        len Fk1 =w by A15,LM5;
        then j in dom Fk1 by A16,FINSEQ_1:def 3;
        then A20:Fk1.j=(A*(i,1)) * (B*(1,j)) by A15,A18,FVSUM_1:62;
        len mLC=m by A17,FINSEQ_2:109;
        then dom mLC=Seg m & Seg 1 = Seg m/\Seg 1  by A5,FINSEQ_1:def 3,9;
        then dom LC1=Seg 1 & 1 in Seg 1 by A14',RELAT_1:90;
        then len LC1=1 & LC1.1=mLC.1 by FINSEQ_1:def 3,FUNCT_1:70;
        then LC1=<*mLC.1*> by FINSEQ_1:57;
        hence thesis by A20,A19,FINSOP_1:12;end;
      suppose A15:k>0;
        dom P=Seg m by A3,FINSEQ_1:def 3;
        then P.k1 in rng P & rng P c= KK* by A13,FUNCT_1:def 5,FINSEQ_1:def 4;
        then reconsider Pk1=P.k1,Fk=F.k as FinSequence of KK 
          by FINSEQ_1:def 11;
       Pk1 = F(k1) & len Line(B,k1)=w by A4,A13,MATRIX_1:def 8;
       then len Pk1=w by LM5;
       then A16:dom Pk1=Seg w by FINSEQ_1:def 3;
       A16':k+0<k1 by XREAL_1:10;
       then B1:len Fk=w by A11,A12,A15,NAT_1:14,XXREAL_0:2;
       then dom Fk=Seg w & Fk is Element of KK* & Pk1 is Element of KK*
         by FINSEQ_1:def 3,FINSEQ_1:def 11;
       then A17:dom (Fk+Pk1)=Seg w /\Seg w & a.(Fk,Pk1)=Fk+Pk1 & k < m
         by A12,A16,A16',LM9,Def4,XXREAL_0:2;
       then A17':len (Fk+Pk1)=w & Fk1=Fk+Pk1 by A3,A7,A14,A15,FINSEQ_1:def 3;
     hence len Fk1=w; 
       let j be Element of NAT such that A18:j in Seg w;
       set L=Line(A,i);
       set C=Col(B,j);
       consider LC be FinSequence of KK such that 
         A19:LC = mlt(L,C)|(Seg k) & (the add of K)"**"LC=Fk.j
            by A11,A12,A15,A16',A18,NAT_1:14,XXREAL_0:2;
        A20:mm.:(L,C) is Element of m-tuples_on KK by A1,FINSEQ_2:140;
        reconsider LC1=mlt(L,C)|(Seg k1),mLC=mlt(L,C) as  FinSequence of KK
          by FINSEQ_1:23;
      take LC1;
        k1 in Seg m & Seg m=dom B by A12,FINSEQ_1:def 3;
        then A21:L.k1=A*(i,k1) & C.k1=B*(k1,j) & Line(B,k1).j=B*(k1,j)
          by A1,A18,MATRIX_1:def 8,def 9;
        j in dom Fk by B1,A18,FINSEQ_1:def 3;
        then Fk.j in rng Fk & rng Fk c= KK by FUNCT_1:def 5,FINSEQ_1:def 4;
        then reconsider Fkj=Fk.j as Element of KK;
        A22: Pk1 = F(k1) by A4,A13;
        then Pk1.j=(A*(i,k1)) * (B*(k1,j)) by A16,A18,A21,FVSUM_1:62;
        then reconsider Pk1j=Pk1.j as Element of KK;
        len mLC=m by A20,FINSEQ_2:109;
        then k1 in dom mLC by A13,FINSEQ_1:def 3;
        then LC1=LC^<*mLC.k1*> & mLC.k1=(A*(i,k1))*(B*(k1,j)) & aa has_a_unity
          by A19,A21,FINSEQ_5:11,FVSUM_1:73,FVSUM_1:10;
        then aa"**"LC1=aa.(Fk.j,(A*(i,k1)) * (B*(k1,j))) & j in dom Fk1 
          by A17, A3,A7,A14,A15,A18,A19,FINSOP_1:5;
        then aa"**"LC1=Fkj+Pk1j & Fk1.j=Fkj+Pk1j 
          by A17',A22,A16,A18,A21,FVSUM_1:62,21;
        hence thesis;end;
  end;
  A11:for k be Element of NAT holds P[k] from NAT_1:sch 1(A9,A10);
  a "**" P is Element of KK*;
  then reconsider Fm=F.m as FinSequence of KK by A3,A8;
  A12:len Fm=w by A5,A11;
  set L=Line(A*B,i);
  width (A*B)=w by A1,MATRIX_3:def 4;
  then A13:len L = w by MATRIX_1:def 8;
  for j be Element of NAT st 1 <=j & j <= len L holds L.j=Fm.j
    proof
      let j be Element of NAT such that A14:1 <=j & j <= len L;
      set LA=Line(A,i); set CB=Col(B,j);
      set AB=A*B;
      A15:j in Seg w by A13,A14;
      then consider LC be FinSequence of KK such that
        A16:LC = mlt(LA,CB)|(Seg m) & 
            aa"**"LC=Fm.j by A5,A11;
      mm.:(LA,CB) is Element of m-tuples_on KK by A1,FINSEQ_2:140;
      then len mlt(LA,CB)=m by FINSEQ_2:109;
      then dom mlt(LA,CB)=Seg m by FINSEQ_1:def 3;
      then A17:Fm.j=LA "*" CB by A16,RELAT_1:98;
      len A<>0 by A2,FINSEQ_1:4;
      then A18:len A>0 & len AB=len A & width AB=w by A1,MATRIX_3:def 4;
      then AB is Matrix of len A,w,K by MATRIX_1:20;
      then Indices AB=[:Seg len A,Seg w:] by A18,MATRIX_1:26;
      then [i,j] in Indices AB by A2,A15,ZFMISC_1:106;
      then AB*(i,j)=LA "*" CB by A1,MATRIX_3:def 4;
      hence thesis by A17,A15,A18,MATRIX_1:def 8;
    end;
  hence L=a "**" P by A3,A8,A12,A13,FINSEQ_1:18;
  let j such that A14:j in Seg len B;
  j is Element of NAT by ORDINAL1:def 13;
  hence thesis by A4,A14;
end;

theorem Th56:
  for A,B,C be (Matrix of n,K), i st i in Seg n
    ex P be FinSequence of K st
      len P = n & 
      Det RLine(C,i,Line(A*B,i)) = (the add of K)"**" P &
      for j st j in Seg n holds P.j = A*(i,j) * Det RLine(C,i,Line(B,j))
proof
  let A,B,C be (Matrix of n,K), i such that A1:i in Seg n;
  set KK=the carrier of K;
  set a=addFinS(K);
  set aa=the add of K;
  n<>0 by A1,FINSEQ_1:4;
  then A2:n>0 & len B=n & width B=n & width A=n & len A=n by MATRIX_1:25;
  then consider P be FinSequence of KK* such that
    A3:len P = n and
    A4:Line(A*B,i) = a "**" P and
    A5:for j st j in Seg len B holds P.j = A*(i,j) * Line(B,j) by A1,Th55;
  deffunc F(Element of NAT)=A*(i,$1) * Det RLine(C,i,Line(B,$1));
  consider D be FinSequence of KK such that 
    A6:len D = len A and
    A7:for j be Element of NAT st j in Seg len A holds D.j = F(j) 
         from FINSEQ_2:sch 1;
  take D;thus len D=n by A6,MATRIX_1:25;
  len P >= 1 by A1,FINSEQ_1:4,A3,NAT_1:14;
  then consider Fp be Function of NAT,KK* such that
    B1:Fp.1 = P.1 and
    B2:for k be Element of NAT st 0 <> k & k < n holds
       Fp.(k+1) = a.(Fp.k,P.(k+1)) and
    B3:Line(A*B,i) = Fp.n by A3,A4,FINSOP_1:def 1;
  len D >= 1 by A2,A6,NAT_1:14;
  then consider Fd be Function of NAT,KK such that
    C1:Fd.1 = D.1 and
    C2:for k be Element of NAT st 0 <> k & k < n holds
       Fd.(k+1) = aa.(Fd.k,D.(k+1)) and
    C3:aa"**"D = Fd.n by A2,A6,FINSOP_1:def 1;
  defpred P[Nat] means 1 <= $1 & $1<= n implies
    for pK st pK=Fp.$1 holds len pK=n & Fd.$1=Det RLine(C,i,pK);
  A8:P[0];
  A9:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A10:P[k];
    set k1=k+1;
    set A'=A*(i,k1);
    set L=Line(B,k1);
    assume A11:1 <= k1 & k1 <= n;
    then A12:k1 in Seg n;
    let  Fpk1 be FinSequence of KK such that A13:Fpk1=Fp.k1;
    per cases;
      suppose A14:k=0;
        then Fpk1=P.k1 & P.k1=A'*L &len L = n & Fd.k1=D.k1
          & D.k1=F(1) by B1,C1,A2,A5,A7,A12,A13,MATRIX_1:def 8;
        hence thesis by A1,A14,Th34,LM5;end;
      suppose A14:k>0;          
        k1 in dom P by A3,A12,FINSEQ_1:def 3;
        then P.k1 in rng P & rng P c= KK* by FUNCT_1:def 5,FINSEQ_1:def 4;
        then reconsider Pk1=P.k1,Fpk=Fp.k as Element of KK*;
        k+0<k1 by XREAL_1:10;
        then 1<=k & k<n by A11,A14,XXREAL_0:2,NAT_1:14;
        then A15:Fd.k=Det RLine(C,i,Fpk) & Fd.k1 = aa.(Fd.k,D.k1)&D.k1=F(k1)&
          Fpk1 = a.(Fpk,Pk1) & P.k1=A' * L & len Fpk=n & len L=n
          by B2,C2,A2,A5,A7,A10,A12,A13,MATRIX_1:def 8;      
        then Fpk1=Fpk+(A'*L) & Det RLine(C,i,A'*L)=A'* Det RLine(C,i,L) &
          len (A'*L)=n by A1,Def4,LM5,Th34;
        then Det RLine(C,i,Fpk1)=Det RLine(C,i,Fpk) + A'*Det RLine(C,i,L) & 
          len Fpk1=n by A1,A15,Th36,LM6;
        hence thesis by A15;end;
  end;
 for k be Element of NAT holds P[k] from NAT_1:sch 1(A8,A9);
 then P[len P] & n >= 1 by A1,FINSEQ_1:4,NAT_1:14;
 hence thesis by A2,A7,A3,B3,C3;
end;

theorem Th57:
  for X being set,Y being non empty set, x st not x in X
  ex BIJECT be Function of [:Funcs(X,Y),Y:],Funcs(X\/{x},Y) st 
    BIJECT is bijective & 
    for f be Function of X,Y,F be Function of X\/{x},Y st F|X=f 
      holds BIJECT.[f,F.x]=F
proof
  let X be set, Y be non empty set, x such that A1:not x in X;
  set FXY=Funcs(X,Y);
  set Xx=X\/{x};
  set FXxY=Funcs(Xx,Y);  
  defpred P[set,set] means
    for f be Function of X,Y,F be Function of Xx,Y, y be set 
      st [f,y]=$1 & F.x=y & F|X=f holds F=$2;
  A2:for x be Element of [:FXY,Y:] ex y be Element of FXxY st P[x,y]
  proof
    let x' be Element of [:FXY,Y:];
    consider f,y be set such that
      A3:f in FXY & y in Y & x'=[f,y] by ZFMISC_1:def 2;
    reconsider f as Function of X,Y by A3,FUNCT_2:121;
    Y\/{y}=Y by A3,ZFMISC_1:46;
    then consider F be Function of X\/{x},Y such that      
       A4:F|X = f & F.x=y by A1,STIRL2_1:67;
    reconsider F'=F as Element of FXxY by FUNCT_2:11;
    take F';
    let g be Function of X,Y,G be Function of Xx,Y, y' be set such that
      A5:[g,y']=x' & G.x=y' & G|X=g;   
    now let xx be set such that A6:xx in Xx;
      (xx in X or xx in {x}) & dom g= X & dom f=X 
        by FUNCT_2:def 1,A6,XBOOLE_0:def 2;
      then ((G.xx=g.xx & F.xx=f.xx) or xx = x) & f=g & y=y'
        by A3,A4,A5,FUNCT_1:70,TARSKI:def 1,ZFMISC_1:33;
      hence G.xx=F.xx by A4,A5; 
    end;
    hence thesis by FUNCT_2:18;
  end;
  consider H be Function of [:FXY,Y:],FXxY such that 
    A3:for x be Element of [:FXY,Y:] holds P[x,H.x] from FUNCT_2:sch 3(A2);
  take H;
  A4:FXxY c= rng H
  proof
    let f' be set such that A5:f' in FXxY;
    reconsider f=f' as Function of Xx,Y by A5,FUNCT_2:121;
    X c= Xx & dom f=Xx & rng f c= Y & rng (f|X) c= rng f 
      by XBOOLE_1:7,FUNCT_2:def 1,RELSET_1:12,RELAT_1:99;
    then dom (f|X)=X & rng (f|X) c= Y by RELAT_1:91,XBOOLE_1:1;
    then reconsider fX=f|X as Function of X,Y by FUNCT_2:4;
    x in {x} by TARSKI:def 1;
    then x in Xx & Xx=dom f by XBOOLE_0:def 2,FUNCT_2:def 1;
    then f.x in rng f & rng f c= Y & fX in FXY 
      by FUNCT_1:def 5,RELSET_1:12,FUNCT_2:11;
    then [fX,f.x] in [:FXY,Y:]&[:FXY,Y:]=dom H by ZFMISC_1:106,FUNCT_2:def 1;
    then H.[fX,f.x] in rng H & H.[fX,f.x]=f by A3,FUNCT_1:def 5;
    hence thesis;        
  end;
  rng H c= FXxY by RELSET_1:12;
  then A5:FXxY = rng H by A4,XBOOLE_0:def 10;    
  now let x1,x2 be set such that 
      A6:x1 in [:FXY,Y:] & x2 in [:FXY,Y:] and
      A7:H.x1 = H.x2;
    consider f1,y1 be set such that
      A8:f1 in FXY & y1 in Y & x1=[f1,y1] by A6,ZFMISC_1:def 2;
    consider f2,y2 be set such that
      A9:f2 in FXY & y2 in Y & x2=[f2,y2] by A6,ZFMISC_1:def 2;
    reconsider f1,f2 as Function of X,Y by A8,A9,FUNCT_2:121;
    Y\/{y1}=Y by A8,ZFMISC_1:46;
    then consider F1 be Function of X\/{x},Y such that      
       A10:F1|X = f1 & F1.x=y1 by A1,STIRL2_1:67;
    Y\/{y2}=Y by A9,ZFMISC_1:46;
    then consider F2 be Function of X\/{x},Y such that      
       A11:F2|X = f2 & F2.x=y2 by A1,STIRL2_1:67;  
    H.x1=F1 & H.x2=F2 by A3,A6,A8,A9,A10,A11;
    hence x1=x2 by A8,A9,A7,A10,A11;
  end;
  then H is one-to-one onto by A5,FUNCT_2:25,def 3;
  hence H is bijective by FUNCT_2:def 4;  
  let f be Function of X,Y,F be Function of X\/{x},Y such that 
    A6:F|X=f;
  x in {x} by TARSKI:def 1;
  then x in Xx & Xx=dom F by XBOOLE_0:def 2,FUNCT_2:def 1;
  then F.x in rng F & rng F c= Y & f in FXY 
    by FUNCT_1:def 5,RELSET_1:12,FUNCT_2:11;
  then [f,F.x] in [:FXY,Y:] by ZFMISC_1:106; 
  hence thesis by A3,A6;
end;

theorem Th58:
  for X being finite set, Y being non empty finite set, x st not x in X
  for F being BinOp of D st 
      F has_a_unity & F is commutative & F is associative & F has_an_inverseOp
  for f being Function of Funcs(X,Y),D
  for g being Function of Funcs(X\/{x},Y),D st 
      for H be Function of X,Y, SF be Element of Fin Funcs(X\/{x},Y) st
        SF={h where h is Function of X\/{x},Y: h|X = H} holds F $$ (SF,g)=f.H
  holds
    F $$ (FinOmega(Funcs(X,Y)),f) = F $$ (FinOmega(Funcs(X\/{x},Y)),g)
proof
  let X be finite set,Y be non empty finite set, x such that A1:not x in X;
  set FXY=Funcs(X,Y);set Xx=X\/{x};set FXxY=Funcs(Xx,Y);
  let F be BinOp of D such that 
    A2:F has_a_unity &F is commutative &F is associative &F has_an_inverseOp;
  let f be Function of FXY,D;
  let g be Function of FXxY,D such that
    A3:for H be Function of X,Y, SF be Element of Fin FXxY st
        SF={h where h is Function of Xx,Y: h|X = H} holds F $$ (SF,g)=f.H;  
  consider B be Function of [:FXY,Y:],FXxY such that 
    A4:B is bijective and
    A5:for f be Function of X,Y,F be Function of Xx,Y
      st F|X=f holds B.[f,F.x]=F by A1,Th57;
  FXY is finite by FRAENKEL:16;
  then dom B=[:FXY,Y:] & [:FXY,Y:] is finite by FUNCT_2:def 1,FINSET_1:19;
  then reconsider domB=dom B as Element of Fin [:FXY,Y:] by FINSUB_1:def 5;
  B is one-to-one onto & FXxY is finite by A4,FUNCT_2:def 4,FRAENKEL:16;
  then F $$(B.:domB,g)=F $$ (domB, g*B) & rng B=FXxY & FinOmega(FXxY)=FXxY
    by A2,SETWOP_2:8,FUNCT_2:def 3,MATRIX_2:def 17;
  then A6':F $$(FinOmega(FXxY),g)=F $$ (domB, g*B) by RELAT_1:146;
  reconsider Y'=Y as Element of Fin Y by FINSUB_1:def 5;
  FXY is finite by FRAENKEL:16;
  then reconsider FXY'=FXY as Element of Fin FXY by FINSUB_1:def 5;
  reconsider gB=g*B as Function of [:FXY,Y:],D; 
  for z be Element of FXY holds f.z=F$$(Y',(curry (gB)).z)
  proof
    let z be Element of FXY;
    reconsider Z=z as Function of X,Y by FUNCT_2:121;
    set SF={h where h is Function of Xx,Y: h|X = Z};
    B1:{z} c= FXY by ZFMISC_1:37;
    then [:{Z},Y:] c= [:FXY,Y:] & [:{Z},Y:] is finite 
      by ZFMISC_1:118;
    then reconsider ZY=[:{Z},Y:]as Element of Fin [:FXY,Y:] by FINSUB_1:def 5;
    A7:B.:ZY c= SF
    proof let b be set such that A8:b in B.:ZY;
      consider zy be set such that
        A9:zy in dom B & zy in ZY & b=B.zy by A8,FUNCT_1:def 12;
      consider z',y' be set such that 
        A10:z' in {Z} & y' in Y & zy = [z',y'] by A9,ZFMISC_1:def 2;
      Y\/{y'}=Y by A10,ZFMISC_1:46;
      then consider F1 be Function of Xx,Y such that      
        A11:F1|X = Z & F1.x=y' by A1,STIRL2_1:67; 
      z'=Z by A10,TARSKI:def 1;
      then B.zy=F1 & F1 in SF by A5,A10,A11;
      hence thesis by A9;
    end;
    A7':SF c= B.:ZY
    proof let sf be set such that A8:sf in SF;
      consider h be Function of Xx,Y such that
        A9:h=sf & h|X = Z by A8;
      x in {x} by TARSKI:def 1;
      then x in Xx & dom h =Xx by XBOOLE_0:def 2,FUNCT_2:def 1;
      then h.x in rng h & rng h c= Y & z in {Z} 
        by FUNCT_1:def 5,RELSET_1:12,TARSKI:def 1; 
      then [z,h.x] in [:FXY,Y:] & [:FXY,Y:]=dom B & [z,h.x] in ZY
        by ZFMISC_1:106,FUNCT_2:def 1;
      then B.[z,h.x] in B.:ZY by FUNCT_1:def 12;
      hence thesis by A9,A5;
    end;    
    then reconsider SF as Element of Fin FXxY by A7,XBOOLE_0:def 10;
    B is one-to-one & B.:ZY = SF by A4,A7,A7',FUNCT_2:def 4,XBOOLE_0:def 10;
    then A9:F $$(B.:ZY,g)=f.Z&F $$(B.:ZY,g)=F $$(ZY,gB) by A2,A3,SETWOP_2:8;
    deffunc Q(set) = [z,$1];
    consider q be Function such that 
      A10:dom q = Y & for x st x in Y holds q.x = Q(x) from FUNCT_1:sch 3;
    A11:q is one-to-one
    proof
      now let x1,x2 be set such that A12:x1 in dom q & x2 in dom q &q.x1=q.x2;
        [z,x1]=q.x1 & q.x2=[z,x2] by A10,A12;
        hence x1=x2 by A12,ZFMISC_1:33;
      end;
      hence thesis by FUNCT_1:def 8;
    end;
    A12:q.:Y=ZY
    proof
      for x' be set holds x' in ZY iff x' in q.:(Y)
      proof let x' be set;
        thus x' in ZY implies x' in q.:(Y)
        proof
          assume x' in ZY;
          then consider z',y' be set such that 
            A14:z' in {Z} & y' in Y & x'=[z',y'] by ZFMISC_1:def 2;       
          q.y'=[z,y'] & z=z' by A10,A14,TARSKI:def 1;
          hence thesis by A10,A14,FUNCT_1:def 12;
        end;
        assume x' in q.:Y;
        then consider y' be set such that 
          A13:y' in dom q & y' in Y & x' = q.y' by FUNCT_1:def 12;
        x'=[z,y'] by A10,A13;
        hence thesis by A13,ZFMISC_1:128;
      end;
      hence thesis by TARSKI:2;
    end;
    ZY c= [:FXY,Y:] & rng q=ZY by B1,A12,A10,RELAT_1:146,ZFMISC_1:118;
    then reconsider q as Function of Y,[:FXY,Y:] by A10,FUNCT_2:4;
    reconsider gBq=gB*q as Function of Y,D; 
    dom gB=[:FXY,Y:] by FUNCT_2:def 1;
    then consider C be Function such that
      A13:(curry (gB)).z =C & dom C=Y & rng C c= rng gB and
      A14:for y' be set st y' in Y holds C.y'=gB.(z,y') by FUNCT_5:36;
    reconsider C as Function of Y,D by A13;
    now let x' be set such that A15:x' in Y;
      C.x'=gB.(z,x') & q.x'=[z,x'] & (gB*q).x'=gB.(q.x') 
         by A10,A14,A15,FUNCT_2:21;
      hence C.x' = gBq.x';
    end;
    then C=gBq by FUNCT_2:18;
    hence thesis by A9,A13,A2,A11,A12,SETWOP_2:8;
  end;
  then F$$([:FXY',Y':],gB)=F$$(FXY',f) by A2,MATRIX_3:32;
  then F$$(domB,gB)=F$$(FXY',f) & FXY is finite by FUNCT_2:def 1;
  hence thesis by A6',MATRIX_2:def 17;
end;

theorem Th59:
  for A,B be Matrix of n,m,D, i st i <= n & 0 < n
    for F be Function of Seg i, Seg n 
      ex M be Matrix of n,m,D st
        M = A +* ((B*(idseq n+*F))|Seg i) & 
        for j holds
               (j in Seg i implies M.j = B.(F.j)) & 
               (not j in Seg i implies M.j = A.j)
proof
  let A,B be Matrix of n,m,D, i such that A1:i<= n & 0<n;
  let F be Function of Seg i,Seg n;
  set I=idseq n; 
  set IF=I+*F; 
  n in Seg n by A1,FINSEQ_1:5;
  then A3:i <=n & dom F=Seg i by A1,FUNCT_2:def 1;
  then rng F c= Seg n & dom F c= Seg n by RELSET_1:12,FINSEQ_1:7;
  then rng F\/Seg n=Seg n & dom F\/Seg n =Seg n & dom I=Seg n & rng I=Seg n 
    by XBOOLE_1:12,RELAT_1:71;
  then rng IF c=Seg n & dom IF=Seg n by FUNCT_4:18,def 1;
  then reconsider IF as Function of Seg n,Seg n by FUNCT_2:4;
  reconsider BIF=B*IF as Matrix of n,m,D;
  set BIFi=BIF|Seg i;  
  set M=A+* BIFi;
  len BIF=len B & len B=n by A1,Def3,MATRIX_1:24;
  then dom BIF=Seg n & Seg i c= Seg n & len A=n 
    by A1,FINSEQ_1:def 3,7,MATRIX_1:24;
  then A5:dom BIFi=Seg i & Seg i c= dom A & dom A=Seg n 
    by RELAT_1:91,FINSEQ_1:def 3;
  then A6:dom M=Seg i \/Seg n & Seg i\/Seg n=Seg n & n is Element of NAT
    by XBOOLE_1:12,FUNCT_4:def 1,ORDINAL1:def 13;
  then reconsider M as FinSequence by FINSEQ_1:def 2;
  A7:for j holds
      (j in Seg i implies M.j = B.(F.j) )&( not j in Seg i implies M.j = A.j )
  proof
    let j;
    thus j in Seg i implies M.j = B.(F.j)
    proof
      assume A8:j in Seg i;
      Seg i c= Seg n by A1,FINSEQ_1:7;
      then IF.j=F.j & j in Seg n by A3,A8,FUNCT_4:14;
      then Line(BIF,j)=B.(F.j) & Line(BIF,j)=BIF.j & BIFi.j=BIF.j
        by A5,Th46,A8,MATRIX_2:10,FUNCT_1:70;
      hence thesis by A5,A8,FUNCT_4:14;
    end;
    assume not j in Seg i;
    hence thesis by A5,FUNCT_4:12;
  end;
  A8:for x st x in rng M ex p be FinSequence of D st x = p & len p = m
  proof
    let x such that A9:x in rng M;
    consider k be set such that
      A10:k in dom M & M.k=x by A9,FUNCT_1:def 5; 
    reconsider k as Nat by A6,A10,ORDINAL1:def 13;
    per cases;
      suppose A11:k in Seg i;
        then A12:F.k in rng F & rng F c= Seg n by A3,FUNCT_1:def 5,RELSET_1:12;
        then reconsider Fk=F.k as Element of NAT by TARSKI:def 3;
        take L=Line(B,Fk);
        B.(F.k)=L & M.k=B.(F.k) & len L=width B & width B=m
          by A1,A7,A11,A12,MATRIX_2:10,MATRIX_1:def 8,24;
        hence thesis by A10;end;
      suppose A11:not k in Seg i;
        take L=Line(A,k);
         M.k=A.k & A.k=L & len L=width A & width A=m
           by A1,A6,A7,A10,A11,MATRIX_2:10,MATRIX_1:def 8,24;
        hence thesis by A10;end;    
  end; 
  then reconsider M as Matrix of D by MATRIX_1:9;
  A9:now let p be FinSequence of D;assume p in rng M;then 
       ex q be FinSequence of D st p = q & len q = m by A8;
       hence len p=m;
     end;
  len M=n by A6,FINSEQ_1:def 3;
  then reconsider M as Matrix of n,m,D by A9,MATRIX_1:def 3; 
  take M;thus thesis by A7;
end;

LM10:for A,B be Matrix of n,n,K, i st i <= n & 0 < n
  ex P be Function of Funcs(Seg i,Seg n),the carrier of K st
     for F be Function of Seg i,Seg n
       for M be Matrix of n,n,K st 
         M = A*B +* ((B*(idseq n +* F))| Seg i) & 
         for j holds (j in Seg i implies M.j=B.(F.j))&
                     (not j in Seg i implies M.j=(A*B).j)
     ex Path be FinSequence of K st
        len Path=i & 
        (for Fj,j be Nat st j in Seg i & Fj=F.j holds Path.j=A*(j,Fj))
     & P.F= (the mult of K) $$ Path * Det M
proof
  let A,B be Matrix of n,n,K, i such that A1:i <= n & 0 < n;
  set mm=the mult of K;
  set Si=Seg i;
  set Sn=Seg n;
  set I=idseq n;
  set AB=A*B;
  set FF=Funcs(Si,Sn);
  set KK=the carrier of K;
  reconsider Sn as non empty set by A1,FINSEQ_1:5;
  ex f be Function st dom f = Si & rng f c= Sn by FUNCT_1:18;
  then reconsider FF as non empty set by FUNCT_2:def 2;
  reconsider AB as Matrix of n,K;
  defpred PP[set,set] means
    for F be Function of Seg i,Seg n st F=$1
      for M be Matrix of n,n,K st 
        M = A*B +* ((B*(I +* F))| Si) & 
        for j holds(j in Si implies M.j=B.(F.j))&
                   (not j in Si implies M.j=(A*B).j)
      ex Path be FinSequence of K st
        len Path=i & 
        (for Fj,j be Nat st j in Si & Fj=F.j holds Path.j=A*(j,Fj))
          & $2= mm $$ Path * Det M;
  A2:for x be Element of FF ex y be Element of KK st PP[x,y]
  proof
    let x be Element of FF;
    reconsider F=x as Function of Si,Sn by FUNCT_2:121;
    consider M be Matrix of n,K such that
      M = AB +* ((B*(I +* F))| Si) and
      A4:for j holds (j in Si implies M.j=B.(F.j))&
                      (not j in Si implies M.j=(AB).j) by A1,Th59;
    defpred Path[set,set] means
      for Fj,j be Nat st j =$1 & Fj=F.j holds $2=A*(j,Fj);
    A5:for x' be set st x' in Si ex y st y in KK & Path[x',y]
    proof
      let x' be set such that A6:x' in Si;
      reconsider i=x' as Nat by A6,ORDINAL1:def 13;
      Si=dom F by FUNCT_2:def 1;
      then F.i in rng F & rng F c= Seg n by A6,FUNCT_1:def 5,RELSET_1:12;
      then F.i in Sn;
      then reconsider Fi=F.i as Nat by ORDINAL1:def 13;
      take Y=A*(i,Fi);thus thesis;
    end;
    consider path be Function of Si,KK such that 
      A6:for x st x in Si holds Path[x,path.x] from FUNCT_2:sch 1(A5);
    dom path=Si & i is Element of NAT by FUNCT_2:def 1,ORDINAL1:def 13;
    then reconsider p=path as FinSequence by FINSEQ_1:def 2;
    rng path c= KK by RELSET_1:12;
    then reconsider p as FinSequence of K by FINSEQ_1:def 4;
    take MM=(mm $$ p)*Det M;
    let F' be Function of Si,Seg n such that A7:F'=x;
    let M' be Matrix of n,n,K such that 
       M' = A*B +* ((B*(I +* F'))| Si) and
       A9:for j holds (j in Seg i implies M'.j=B.(F'.j))&
                    (not j in Seg i implies M'.j=(A*B).j);
    take p;
    dom path=Si & i is Element of NAT by FUNCT_2:def 1,ORDINAL1:def 13;
    hence len p=i & for Fj,j be Nat st j in Si & Fj=F'.j holds p.j=A*(j,Fj) 
      by A6,A7,FINSEQ_1:def 3;
    A10:len M=n & len M'=n by MATRIX_1:25;
    now let k be Element of NAT such that 1 <=k & k <= len M;
      per cases;
        suppose k in Si;
          then M.k=B.(F.k) & M'.k=B.(F'.k) by A4,A9;
          hence M.k=M'.k by A7;end;
        suppose not k in Si;
          then M.k=AB.k & M'.k=AB.k by A4,A9;
          hence M.k=M'.k;end;
    end;
    hence thesis by A10,FINSEQ_1:18;
  end;
  consider P be Function of FF,KK such that 
    A3:for x be Element of FF holds PP[x,P.x] from FUNCT_2:sch 3(A2);
  reconsider P as Function of Funcs(Si,Seg n),KK;
  take P;
  let F be Function of Si,Seg n;reconsider F'=F as Function of Si,Sn;
  let M be Matrix of n,K such that
    A4:M = A*B +* ((B*(I +* F))| Si) and
    A5:for j holds (j in Si implies M.j=B.(F.j))&
                   (not j in Si implies M.j=(A*B).j);
  F' in FF by FUNCT_2:11;
  hence thesis by A3,A4,A5;
end;

theorem Th60:
  for A,B be Matrix of n,K st 0 < n
    ex P be Function of Funcs(Seg n,Seg n),the carrier of K st
       ( for F be Function of Seg n,Seg n
         ex Path be FinSequence of K st
            len Path = n &
            (for Fj,j be Nat st j in Seg n & Fj = F.j holds Path.j = A*(j,Fj))&
            P.F= (the mult of K) $$ Path * Det (B*F) ) &
  Det(A*B) = (the add of K) $$ (FinOmega(Funcs(Seg n,Seg n)),P)
proof
  let A,B be Matrix of n,K such that A1:0 < n;
  set KK=the carrier of K;
  set mm= the mult of K;
  set I=idseq n;
  set aa=the add of K;
  set AB=A*B;
  defpred FF[Function,Nat] means
     for F be Function of Seg $2,Seg n
         for M be Matrix of n,n,K st 
              M = A*B +* ((B*(I +* F))| Seg $2) & 
              for j holds (j in Seg $2 implies M.j=B.(F.j))&
                        (not j in Seg $2 implies M.j=(A*B).j)
          ex Path be FinSequence of K st
            len Path=$2 & 
            (for Fj,j be Nat st j in Seg $2 & Fj=F.j holds Path.j=A*(j,Fj))
            & $1.F= mm $$ Path * Det M;
  defpred P[Nat] means $1<=n implies
    for FUNC be non empty set st FUNC=Funcs(Seg $1,Seg n)
        ex P be Function of FUNC,KK st
      FF[P,$1] & Det(AB) = aa $$ (FinOmega(FUNC),P);
  A2:P[0]
  proof assume 0<=n;
    let FUNC be non empty set such that A3:FUNC=Funcs(Seg 0,Seg n);
    consider P be Function of FUNC,KK such that
      A4:FF[P,0] by A1,A3,LM10;
    dom {}={} & rng {} c= Seg n & Seg 0={} by XBOOLE_1:2,FINSEQ_1:4;
    then reconsider E={} as Function of Seg 0,Seg n by FUNCT_2:4;
    consider M be Matrix of n,K such that
      A5:M = AB +* ((B*(I +* E))| Seg 0) and
      A6:for j holds (j in Seg 0 implies M.j=B.(E.j))&
                     (not j in Seg 0 implies M.j=AB.j)by A1,Th59;
    consider Path be FinSequence of K such that
      A7:len Path=0 and
          for Fj,j be Nat st j in Seg 0 & Fj=E.j holds Path.j=A*(j,Fj) and
      A8:P.E= mm $$ Path * Det M by A1,A4,A5,A6;
    (B*(I +* E))| Seg 0={} & Path=<*>KK & the_unity_wrt mm=1_K
      by A7,FINSEQ_1:32,4,RELAT_1:110,GROUP_1:33;
    then M=AB & mm "**" Path= 1_K by FINSOP_1:11,A5,FUNCT_4:22;
    then A9:P.E=Det AB by VECTSP_1:def 13,A8;
    FUNC={E} by FUNCT_5:64,A3,FINSEQ_1:4;
    then FinOmega(FUNC)={E} & E in FUNC by MATRIX_2:def 17,TARSKI:def 1;
    then aa $$ (FinOmega(FUNC),P)=P.E by SETWISEO:26;
    hence thesis by A4,A9;
  end;
  A3:for k be Element of NAT st P[k] holds P[k+1]
  proof
    let k be Element of NAT such that A4:P[k];set k1=k+1;
    assume A5:k1 <= n; 
    let FUNC1 be non empty set such that A6:FUNC1=Funcs(Seg k1,Seg n);
    consider P1 be Function of FUNC1,KK such that
      A7:FF[P1,k1] by A5,A6,LM10;
    n in Seg n by A1,FINSEQ_1:5;
    then ex f be Function st dom f=Seg k & rng f c= Seg n by FUNCT_1:18;
    then reconsider FUNC=Funcs(Seg k,Seg n) as non empty set by FUNCT_2:def 2;
    A5':k+0<=k1 by XREAL_1:10;
    then consider P be Function of FUNC,KK such that
      A8:FF[P,k] & Det(AB) = aa $$ (FinOmega(FUNC),P) by A4,A5,XXREAL_0:2;
    set X=Seg k;
    set Xx=Seg k\/{k1};
    set Y=Seg n;
    A9:FUNC1=Funcs(Xx,Y) by A6,FINSEQ_1:11;
    reconsider FUNC1'=Funcs(Xx,Y) as non empty set by A6,FINSEQ_1:11;
    reconsider P1'=P1 as Function of FUNC1',KK by A9;
    A10:n in Y & not k1 in X & aa has_a_unity & aa has_an_inverseOp
      by A1,FINSEQ_3:9,FINSEQ_1:5,FVSUM_1:10,17;
for H be Function of X,Y, SF be Element of Fin FUNC1' st
  SF={h where h is Function of Xx,Y: h|X=H} holds aa $$ (SF,P1')=P.H
    proof
      let H be Function of X,Y, SF be Element of Fin FUNC1' such that
        A11:SF={h where h is Function of Xx,Y: h|X=H};
      reconsider YY=Y as non empty set by A1,FINSEQ_1:5;
      reconsider Y'=Y as Element of Fin YY by FINSUB_1:def 5;
      defpred Q[set,set] means
        for h be Function of Xx,Y st h|X=H & h.k1=$1 holds h=$2;
      A12:for y st y in YY ex f' be set st f' in SF & Q[y,f']
      proof
        let y such that A13:y in YY;
        Y\/{y}=Y by A13,ZFMISC_1:46;
        then consider q be Function of Xx,Y such that
          A14:q|X=H & q.k1=y by A10,STIRL2_1:67;
        take q; thus q in SF by A11,A14;
        let h be Function of Xx,Y such that A15:h|X=H & h.k1=y;
        now let x;assume x in Xx;then
          (x in X or x in {k1}) & dom H=X by A10,XBOOLE_0:def 2,FUNCT_2:def 1;
          then (q.x=H.x & h.x=H.x) or x=k1 by A14,A15,FUNCT_1:70,TARSKI:def 1;
          hence h.x = q.x by A14,A15;
        end;
        hence thesis by FUNCT_2:18;
      end;
      consider QQ be Function of YY,SF such that 
        A13:for y st y in YY holds Q[y,QQ.y] from FUNCT_2:sch 1(A12);
      A14:SF c= rng QQ
      proof
        let y; assume A15:y in SF;
        then consider h be Function of Xx,Y such that 
          A16:y=h & h|X=H by A11;
        k1 in {k1} by TARSKI:def 1;
        then dom h=Xx & k1 in Xx by A10,FUNCT_2:def 1,XBOOLE_0:def 2;
        then h.k1 in rng h & rng h  c= Y & dom QQ=YY
          by A15,FUNCT_1:def 5,RELSET_1:12,FUNCT_2:def 1;
        then QQ.(h.k1) in rng QQ & QQ.(h.k1)=h by A13,A16,FUNCT_1:def 5;
        hence thesis by A16;
      end;
      rng QQ c= SF by RELSET_1:12;
      then A15:rng QQ=SF by A14,XBOOLE_0:def 10;
      Y\/{n}=Y by A10,ZFMISC_1:46;
      then consider h be Function of Xx,Y such that
        A16:h|X=H & h.k1=n by A10,STIRL2_1:67;
      A17:h in SF by A11,A16;
      SF c= FUNC1' by FINSUB_1:def 5;
      then reconsider QQ as Function of YY,FUNC1' by A15,A17,FUNCT_2:8;
      now let x1,x2 be set such that A18:x1 in Y & x2 in Y & QQ.x1 = QQ.x2;
        Y\/{x1}=Y by A18,ZFMISC_1:46;
        then consider h1 be Function of Xx,Y such that
          A19:h1|X=H & h1.k1=x1 by A10,STIRL2_1:67;
        Y\/{x2}=Y by A18,ZFMISC_1:46;
        then consider h2 be Function of Xx,Y such that
          A20:h2|X=H & h2.k1=x2 by A10,STIRL2_1:67;
        QQ.x1=h1 & QQ.x2=h2 by A13,A18,A19,A20;
        hence x1=x2 by A18,A19,A20;
     end;then 
   A18:QQ is one-to-one & QQ.:dom QQ= SF & dom QQ=Y' 
        by A15,FUNCT_2:25,def 1,RELAT_1:146;
      k<=n by A5,A5',XXREAL_0:2;
      then consider Mh be Matrix of n,K such that
        A19:Mh = AB +* ((B*(I +* H))| Seg k) and
        A20:for j holds (j in Seg k implies Mh.j=B.(H.j))&
                     (not j in Seg k implies Mh.j=AB.j) by A1,Th59;
      consider Path be FinSequence of K such that
        A21:len Path=k and
        A22:for Hj,j be Nat st j in Seg k & Hj=H.j holds Path.j=A*(j,Hj) and
        A23:P.H= mm $$ Path * Det Mh by A1,A8,A19,A20;
      set PA=mm "**" Path;
   A24:for j be Nat st j in Y holds 
       (P1'*QQ).j=PA*(A*(k1,j))* Det RLine(Mh,k1,Line(B,j))
      proof
        let j be Nat such that A25:j in Y;
        Y\/{j}=Y by A25,ZFMISC_1:46;
        then consider hj be Function of Xx,Y such that
          A26:hj|X=H & hj.k1=j by A10,STIRL2_1:67;
        Xx=Seg k1 by FINSEQ_1:11;
        then reconsider hj'=hj as Function of Seg k1,Y;
        consider Mhj be Matrix of n,K such that
          A27:Mhj = AB +* ((B*(I +* hj'))| Seg k1) and
          A28:for i holds (i in Seg k1 implies Mhj.i=B.(hj'.i))&
                     (not i in Seg k1 implies Mhj.i=AB.i) by A5,Th59;
        set L=Line(B,j);
        set R=RLine(Mh,k1,L);
        A29:len R=len Mh & len Mh=n & len Mhj=n & len L=width B & width B=n 
          & width Mh=n by MATRIX_1:def 8,25,LM4;
        now let i be Element of NAT such that A30:1 <=i & i <= len Mhj;
          A31:i in Seg n & k+0 < k1 by A29,A30,XREAL_1:10;
          per cases;
            suppose i <=k;
              then i in X & i<>k1 & i<=k1 & dom H=X 
                by A30,A31,FUNCT_2:def 1,XXREAL_0:2;
              then Line(R,i)=Line(Mh,i)& Line(R,i)=R.i & Line(Mh,i)=Mh.i &
                Mh.i=B.(H.i) & H.i=hj.i & i in Seg k1
                by A20,A26,A30,A31,Th28,MATRIX_2:10,FUNCT_1:70;
              hence Mhj.i=R.i by A28;end;
            suppose i>k & i<=k1;
              then i=k1 & k1 in NAT & k1 in Seg k1 by NAT_1:9,FINSEQ_1:6;
              then L=Line(R,i) & R.i=Line(R,i) & L=B.j & Mhj.i=B.(hj.k1)
                by A25,A28,A29,A31,Th28,MATRIX_2:10;
              hence Mhj.i=R.i by A26;end;
            suppose i>k & i>k1;
              then not i in Seg k1 & Line(R,i)=Line(Mh,i) & Line(R,i)=R.i & 
                Line(Mh,i)=Mh.i&not i in X by FINSEQ_1:3,A31,Th28,MATRIX_2:10;
              then Mhj.i=AB.i & R.i=AB.i by A20,A28;
              hence Mhj.i=R.i;end;
        end;
        then R=Mhj by A29,FINSEQ_1:18;
        then consider Pathj be FinSequence of K such that
          A30:len Pathj=k1 and
          A31:for m,j be Nat st j in Seg k1 & m=hj.j holds Pathj.j=A*(j,m) and
          A32:P1.hj= (mm "**" Pathj) * Det R by A1,A7,A27,A28;
        A32':QQ.j=hj & dom (P1'*QQ)=Y by A13,A25,A26,FUNCT_2:def 1;
        A33:len (Pathj|k)=k by A5',A30,FINSEQ_1:80;
        now let i be Element of NAT such that A34:1 <=i & i <= len Path;
          i in X & X=dom H by A21,A25,A34,FUNCT_2:def 1;
          then A35:H.i in rng H & rng H c= Y & H.i=hj.i 
            by A26,FUNCT_1:def 5,70,RELSET_1:12;
          then H.i in Y;
          then reconsider Hi=H.i as Nat by ORDINAL1:def 13;
          i<=k1 by A21,A34,A5',XXREAL_0:2;
          then i in X & i in Seg k1 by A21,A34;
          then Path.i=A*(i,Hi) & Pathj.i=A*(i,Hi) & (Pathj|k).i=Pathj.i
            by A21,A22,A31,A34,A35,FINSEQ_3:121;
          hence Path.i=(Pathj|X).i;
        end;
        then Path=(Pathj|X) & k1 in Seg k1 by FINSEQ_1:6,A21,A33,FINSEQ_1:18;
        then Pathj=Path^<*Pathj.k1*> & Pathj.k1=A*(k1,j) 
          by A26,A31,A30,FINSEQ_3:61;
        then mm "**" Pathj = mm.(PA,A*(k1,j)) by FINSOP_1:5
                          .= PA*(A*(k1,j)) by GROUP_1:def 1;
        hence thesis by A32,A32',A25,FUNCT_1:22;
      end;
      1+0<=k1 by XREAL_1:9;
      then A25:k1 in Seg n by A5; 
      then Mh.k1=AB.k1 & Mh.k1=Line(Mh,k1) & AB.k1=Line(AB,k1) 
        by A10,A20,MATRIX_2:10;
      then Mh=RLine(Mh,k1,Line(AB,k1)) by Th30;
      then consider SUM be FinSequence of KK such that
        A26:len SUM = n and
        A27:Det Mh = aa"**" SUM and
        A28:for j st j in Y holds SUM.j= A*(k1,j)*Det RLine(Mh,k1,Line(B,j))
          by A1,A25,Th56;
      set PS=PA*SUM;
      set PSaa=[#](PS,the_unity_wrt aa);
      A29':dom (P1'*QQ)=Y by FUNCT_2:def 1;
      then A29:(P1'*QQ)*(id Y)=P1'*QQ by RELAT_1:78;
      len PS=n by A26,LM5;
      then A30:dom PS=Y by FINSEQ_1:def 3;
      now let y such that A31:y in dom PS;
        reconsider j=y as Nat by A30,A31,ORDINAL1:def 13;
        SUM.j = A*(k1,j)*Det RLine(Mh,k1,Line(B,j)) & j is Element of NAT 
          by A28,A30,A31;
        hence PS.y =PA*(A*(k1,j)*Det RLine(Mh,k1,Line(B,j))) by A31,FVSUM_1:62
                  .=PA*(A*(k1,j))*Det RLine(Mh,k1,Line(B,j)) by
                        GROUP_1:def 4
                  .=(P1'*QQ).y by A24,A30,A31;
      end;
      then A31:PS=P1'*QQ by A29',A30,FUNCT_1:9;
      dom id Y=Y & rng id Y =Y & PSaa|(dom PS)=(P1'*QQ)
        by A31,SETWOP_2:23,RELAT_1:71;
      then aa $$ (Y',P1'*QQ) = aa $$(dom PS,PSaa) by A30,SETWOP_2:7,A29
                            .= Sum(PS) by A10,SETWOP_2:def 2
                            .= PA*Sum(SUM) by FVSUM_1:92
                            .= P.H by A23,A27;
      hence thesis by A18,SETWOP_2:8;
    end;
    then Det AB=aa $$(FinOmega(FUNC1'),P1') by A8,A10,Th58;
    hence thesis by A7,A9;
  end;::end A3
  A4:for k be Element of NAT holds P[k] from NAT_1:sch 1(A2,A3);
  set FUN=Funcs(Seg n,Seg n);
  n is Element of NAT by ORDINAL1:def 13;
  then consider P be Function of FUN,KK such that
     A5:FF[P,n] & Det(AB) = aa $$ (FinOmega(FUN),P) by A4;
  take P;
  thus for F be Function of Seg n,Seg n 
     ex Path be FinSequence of K st len Path=n & 
       (for Fj,j be Nat st j in Seg n & Fj=F.j holds Path.j=A*(j,Fj))
        & P.F= (the mult of K) $$ Path * Det (B*F)
    proof
      let F be Function of Seg n,Seg n;
      consider M be Matrix of n,K such that
        A6:M = AB +* ((B*(I +* F))| Seg n) and
        A7:for j holds (j in Seg n implies M.j=B.(F.j))&
                       (not j in Seg n implies M.j=AB.j)by A1,Th59;
      dom F=Seg n & dom I=Seg n by FUNCT_2:67;
      then A8:I+*F=F & len (B*F)=len B & len B=n & len AB=n
        by Def3,FUNCT_4:20,MATRIX_1:25;
      then (B*F)|n=B*F & (B*F)|Seg n =(B*F)|n & dom (B*F)=Seg n & dom AB=Seg n
        by FINSEQ_1:79,def 3;
      then M=B*F by A6,A8,FUNCT_4:20;
      hence thesis by A1,A5,A6,A7;
    end;
  thus thesis by A5;
end;

theorem Th61:
  for A,B be Matrix of n,K st 0 < n
    ex P be Function of Permutations n,the carrier of K st
      Det(A*B) = (the add of K) $$ (FinOmega(Permutations n),P) &
      for perm be Element of Permutations n holds
        P.perm= (the mult of K) $$ Path_matrix(perm,A) * -(Det B,perm)
proof
  let A,B be Matrix of n,K such that A1:0 < n;
  set X=Seg n;
  set F=Funcs(X,X);
  set AB=A*B;
  set aa=the add of K;
  set mm=the mult of K;
  set KK=the carrier of K;
  set P=Permutations n;
  consider SUM be Function of F,KK such that
    A2:for F be Function of X,X ex Path be FinSequence of K st
         len Path=n & 
         (for Fj,j be Nat st j in Seg n & Fj=F.j holds Path.j=A*(j,Fj)) & 
         SUM.F= mm $$ Path * Det (B*F) and
    A3:Det(AB)=aa $$ (FinOmega(F),SUM) by A1,Th60;
  A4':F is finite by FRAENKEL:16;
  then A4:F\P c= F & F\P is finite & FinOmega(F)=F 
    by FINSET_1:16,MATRIX_2:def 17;
  then reconsider FP=F\P as Element of Fin F by FINSUB_1:def 5;
  A5:P c= F
  proof
    let x; assume x in P;
    then reconsider p=x as Permutation of X by MATRIX_2:def 11;
    p is Element of F by FUNCT_2:12;
    hence thesis;
  end;
  then P is finite by A4',FINSET_1:13;
  then reconsider P'=P as Element of Fin F by A5,FINSUB_1:def 5;
  A6:now per cases;
    suppose FP={};
      then F c= P by XBOOLE_1:37;
      hence Det(AB)=aa $$ (P',SUM) by A3,A4,A5,XBOOLE_0:def 10;end;
    suppose A6:FP<>{};
      A7:SUM.:FP c= {0.K}
      proof
        let s be set;
        assume s in SUM.:FP;
        then consider x such that
          A8:x in dom SUM & x in FP & s = SUM.x by FUNCT_1:def 12;
        reconsider f=x as Function of X,X by A8,FUNCT_2:121;
        consider Path be FinSequence of K such that
          len Path=n and
          for Fj,j be Nat st j in Seg n & Fj=f.j holds Path.j=A*(j,Fj) and
          A9:SUM.f= (mm $$ Path) * Det (B*f) by A2;
        not f in P by A8,XBOOLE_0:def 4;
        then Det(B*f)=0.K by Th54;
        then SUM.f=0.K by A9,VECTSP_1:36;
        hence thesis by A8,TARSKI:def 1;
      end;
      dom SUM=F by FUNCT_2:def 1;
      then SUM.:FP<>{} by A6,RELAT_1:152;
      then A8:SUM.:FP={0.K} & aa has_a_unity & 0.K = the_unity_wrt aa 
        by A7,ZFMISC_1:39,FVSUM_1:10,9;
      then aa $$ (FP,SUM)=0.K & FP misses P & FP\/P=F\/P & F\/P=F
        by A5,SETWOP_2:10,XBOOLE_1:79,12,39;
      hence Det(AB) = aa $$ (P',SUM)+0.K by A3,A4,A8,SETWOP_2:6
                   .= aa $$ (P',SUM) by RLVECT_1:10;end;
  end;
  dom SUM=F & rng (SUM|P) c= rng SUM & rng SUM c= KK 
    by FUNCT_2:def 1,RELAT_1:99,RELSET_1:12;
  then A7:dom (SUM|P)=P & rng (SUM|P) c= KK by A5,RELAT_1:91,XBOOLE_1:1;
  then reconsider SP=SUM|P as Function of P,KK by FUNCT_2:4;
  take SP;
  P is finite by MATRIX_2:30;
  then SP*id P=SP & dom id P=P & rng id P=P & aa has_a_unity & P=FinOmega(P)
    by A7,RELAT_1:78,MATRIX_2:def 17,FVSUM_1:10,RELAT_1:71;
  hence Det(AB)=aa $$ (FinOmega(P),SP) by A6,SETWOP_2:7;
  let perm be Element of P;
  reconsider Perm=perm as Permutation of X by MATRIX_2:def 11;
  SUM.Perm=SP.Perm by A7,FUNCT_1:70;
  then consider Path be FinSequence of K such that
    A8:len Path=n and
    A9:for Fj,j be Nat st j in Seg n & Fj=Perm.j holds Path.j=A*(j,Fj) and
    A10:SP.Perm= mm $$ Path * Det (B*Perm) by A2;
  set PM=Path_matrix(perm,A);
  A11:len PM=n by MATRIX_3:def 7;
  now let i be Element of NAT such that A12:1 <= i & i <= len Path;
    i in X & X=dom Perm by A8,A12,FUNCT_2:67;
    then Perm.i in rng Perm & rng Perm c= X by FUNCT_1:def 5,RELSET_1:12;
    then Perm.i in X;
    then reconsider Pi=Perm.i as Element of NAT;
    i in X & dom PM=X by A8,A11,A12,FINSEQ_1:def 3;
    then PM.i=A*(i,Pi) & Path.i=A*(i,Pi) by A9,MATRIX_3:def 7;
    hence Path.i=PM.i;
  end;
  then Path=PM by A8,A11,FINSEQ_1:18;
  hence thesis by A10,Th45;
end;

     ::------------------------------------------::
     ::     Determinant of a product of          ::
     ::          two square matrices             ::
     ::------------------------------------------::

theorem
  for A,B be Matrix of n,K st 0 < n holds
    Det (A * B) = Det A * Det B
proof
  let A,B be Matrix of n,K such that A1:0 < n;
  set AB = A * B;
  set aa=the add of K;
  set mm=the mult of K;
  set KK=the carrier of K;
  set P=Permutations n;
  set F=FinOmega(P);
  set Path=Path_product(A);
  consider SUM be Function of P,KK such that
    A2:Det(AB) = aa $$ (FinOmega(P),SUM) and
    A3:for perm be Element of Permutations n holds
       SUM.perm = mm $$ Path_matrix(perm,A) * -(Det B,perm) by A1,Th61;
  P is finite by MATRIX_2:30;
  then A0:F=P by MATRIX_2:def 17;
  consider Gs be Function of Fin P,KK such that
    B1:Det(AB) = Gs.F and
    B2:for e being Element of KK st e is_a_unity_wrt aa holds Gs.{} = e and
    B3:for x being Element of P holds Gs.{x} = SUM.x and
    B4:for B' being Element of Fin P st B' c= F & B' <> {}
      for x being Element of P st x in F \ B' holds Gs.(B' \/ {x}) = 
      aa.(Gs.B',SUM.x) by A2,A0,SETWISEO:def 3;
  consider Ga be Function of Fin P,KK such that
    C1:Det A = Ga.F and
    C2:for e being Element of KK st e is_a_unity_wrt aa holds Ga.{} = e and
    C3:for x being Element of P holds Ga.{x} = Path.x and
    C4:for B' being Element of Fin P st B' c= F & B' <> {}
      for x being Element of P st x in F \ B' holds Ga.(B' \/ {x}) = 
      aa.(Ga.B',Path.x) by A0,SETWISEO:def 3;
  defpred S[set] means
    for B' be Element of Fin P st B'=$1 holds
    Gs.B'=(Ga.B')*Det B;
  A4:S[{}.P]
    proof
      0.K is_a_unity_wrt aa by FVSUM_1:8;
      then Gs.{}=0.K & Ga.{}=0.K by B2,C2;
      hence thesis by VECTSP_1:39;
    end;
  A5:for B' be (Element of Fin P), b be Element of P
     holds S[B'] & not b in B' implies S[B' \/ {b}]
    proof
      let B' be (Element of Fin P), b be Element of P;
      assume A6:S[B'] & not b in B';
      let Bb be Element of Fin P such that A6':Bb=B'\/{b};
      set mA=mm $$ Path_matrix(b,A);
      A7:now per cases;
        suppose b is even;
          then -(Det B,b)=Det B & -(mA,b)=mA by MATRIX_2:def 16;     
          hence SUM.b = -(mA,b)*Det B by A3
                     .= Path.b*Det B by MATRIX_3:def 8;end;
        suppose b is odd;
          then -(Det B,b)=-Det B & -(mA,b)=-mA by MATRIX_2:def 16;
          then -(-(mA,b)*(Det B)) = (-mA)*(-(Det B,b)) by VECTSP_1:41
                                 .= -(mA*-(Det B,b)) by VECTSP_1:41;
          then (mA*-(Det B,b))--(mA,b)*(Det B)=0.K by VECTSP_1:63;
          then -(mA,b)*(Det B)=mA*-(Det B,b) & -(mA,b)=Path.b 
            by VECTSP_1:66,MATRIX_3:def 8;
          hence SUM.b=Path.b*Det B by A3;end;
      end;
      per cases;
        suppose B'={};
          then Ga.Bb = Path.b & Gs.Bb = SUM.b by A6',B3,C3;
          hence thesis by A7;end;
        suppose A8:B'<>{};
          b in F\B' & B' c= P by A6,A0,XBOOLE_0:def 4,FINSUB_1:def 5;
          then Ga.Bb = aa.(Ga.B',Path.b) & Gs.Bb = aa.(Gs.B',SUM.b) 
            by A0,B4,C4,A6',A8;
          then Gs.Bb=((Ga.B')*Det B)+(Path.b*Det B) & Ga.Bb=Ga.B'+Path.b
            by A6,A7;
          hence thesis by VECTSP_1:def 18;end;
    end;
  for B be Element of Fin P holds S[B] from SETWISEO:sch 2(A4,A5);  
  hence thesis by B1,C1;
end;
