:: Exponential Function on Complex {B}anach Algebra
::  by Noboru Endou
::
:: Received April 6, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, NORMSP_1, RLVECT_1, FUNCT_1, ARYTM, ARYTM_1, ARYTM_3,
      RELAT_1, ABSVALUE, ORDINAL2, QC_LANG1, LATTICES, SEQ_1, PROB_1, SEQ_2,
      SEQM_3, SIN_COS, CAT_1, PREPOWER, SERIES_1, SQUARE_1, GROUP_1, RSSPACE3,
      LOPBAN_3, SUPINF_2, LOPBAN_4, CLOPBAN2, COMPLEX1, CFUNCT_1, XCMPLX_0,
      VECTSP_1, RELOC;
 notations RELAT_1, FUNCT_1, SUBSET_1, FUNCT_2, PRE_TOPC, STRUCT_0, ALGSTR_0,
      XCMPLX_0, XXREAL_0, XREAL_0, ORDINAL1, ARYTM_0, NUMBERS, REAL_1, NAT_1,
      NEWTON, RLVECT_1, GROUP_1, SQUARE_1, NORMSP_1, VALUED_1, SEQ_1, SEQ_2,
      SEQM_3, SERIES_1, LOPBAN_4, BINARITH, SIN_COS, COMPLEX1, BHSP_4,
      CFUNCT_1, CLVECT_1, CSSPACE3, CLOPBAN2, LOPBAN_3, CLOPBAN3;
 constructors ARYTM_0, REAL_1, SQUARE_1, NAT_1, SEQ_2, COMSEQ_1, SERIES_1,
      COMSEQ_3, BINARITH, SIN_COS, BHSP_4, LOPBAN_3, LOPBAN_4, CSSPACE3,
      CLOPBAN3, SEQ_1, VECTSP_1, VALUED_1, PARTFUN1;
 registrations ORDINAL1, RELSET_1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0,
      MEMBERED, COMPLEX1, SEQ_1, STRUCT_0, CLOPBAN2, CLOPBAN3, XBOOLE_0,
      VALUED_0, VALUED_1, FUNCT_2;
 requirements SUBSET, REAL, BOOLE, NUMERALS, ARITHM;
 definitions CLVECT_1, VALUED_1;
 theorems ABSVALUE, REAL_1, REAL_2, RLVECT_1, XCMPLX_0, XCMPLX_1, XREAL_0,
      SEQ_2, SEQM_3, COMSEQ_3, SIN_COS, SERIES_1, NAT_1, INT_1, NEWTON,
      BINARITH, FUNCT_2, SEQ_4, LOPBAN_3, CLOPBAN3, CLVECT_1, COMPLEX1,
      CSSPACE3, XREAL_1, XXREAL_0, LOPBAN_4, BHSP_4, NORMSP_1;
 schemes SEQ_1, FUNCT_1, NAT_1, FUNCT_2;

begin :: Basic Properties of Exponential Function on Complex Banach Algebra

reserve X for Complex_Banach_Algebra,
        w,z,z1,z2 for Element of X,
        k,l,m,n,n1,n2 for Element of NAT,
        seq,seq1,seq2,s,s' for sequence of X,
        rseq for Real_Sequence;

definition
  canceled;
end;

Lm1:
z* (z #N n) =z #N (n+1) &
(z #N n) *z= z #N (n+1) &
z* (z #N n) =(z #N n) *z
proof
   defpred P[Element of NAT] means z* (z #N $1)=z #N ($1 +1);
A1:z*(z #N0 ) =z * (z GeoSeq.0) by CLOPBAN3:def 13
                .=z * 1.X by CLOPBAN3:def 12
                .=z by CLOPBAN3:42;
z #N (0+1) =z GeoSeq.(0+1) by CLOPBAN3:def 13
              .=z GeoSeq.0 * z by CLOPBAN3:def 12
              .=1.X  * z by CLOPBAN3:def 12
              .=z by CLOPBAN3:42;
then A2:P[0] by A1;
A3:for k be Element of NAT st P[k] holds P[k+1]
   proof
     let k be Element of NAT such that
A4: P[k];
     thus z* (z #N (k+1)) =z * (z GeoSeq.(k+1)) by CLOPBAN3:def 13
           .= z* (z GeoSeq.(k)*z) by CLOPBAN3:def 12
           .=z* (z #N k *z ) by CLOPBAN3:def 13
           .=(z * z #N k) *z by CLOPBAN3:42
           .=z GeoSeq.(k+1) * z by A4,CLOPBAN3:def 13
           .=z GeoSeq.((k+1)+1) by CLOPBAN3:def 12
           .=z #N ((k+1)+1) by CLOPBAN3:def 13;
   end;
A5:for n be Element of NAT  holds P[n]  from NAT_1:sch 1(A2,A3);
    now let n;
     thus z * z #N n =z #N (n+1) by A5;
     thus (z #N n) *z =z GeoSeq.n * z by CLOPBAN3:def 13
          .=z GeoSeq.(n+1) by CLOPBAN3:def 12
          .=z #N (n+1) by CLOPBAN3:def 13;
     hence z * (z #Nn) =(z #N n) * z by A5;
   end;
   hence thesis;
end;

Lm2:
for z,w st z,w are_commutative holds
   w* (z #N n) = (z #N n)*w & z* (w #N n) = (w #N n)*z
proof
   let z,w such that
A1:z,w are_commutative;
   defpred P[Element of NAT] means
   w* (z #N $1) =(z #N $1)*w & z* (w #N $1) = (w #N $1)*z;
A2:(z #N0 ) = (z GeoSeq.0) by CLOPBAN3:def 13
           .= 1.X by CLOPBAN3:def 12;
A3:(w #N0 ) = (w GeoSeq.0) by CLOPBAN3:def 13
           .= 1.X by CLOPBAN3:def 12;
A4:w*(z #N0 )=w by A2,CLOPBAN3:42
             .=(z #N0 ) *w by A2,CLOPBAN3:42;
z*(w #N0 )=z by A3,CLOPBAN3:42
             .=(w #N0 ) *z by A3,CLOPBAN3:42;
then A5:P[0] by A4;
A6:for k be Element of NAT st P[k] holds P[k+1]
   proof
     let k be Element of NAT such that
A7: P[k];
A8: w*(z #N (k+1) )=w*( (z #N k) *z) by Lm1
                   .=(w*(z #N k)) *z by CLOPBAN3:42
                   .=(z #N k)*(w *z) by A7,CLOPBAN3:42
                   .=(z #N k)*(z *w) by A1,LOPBAN_4:def 1
                   .=((z #N k))*z *w by CLOPBAN3:42
                   .=(z #N (k+1) ) *w by Lm1;
 z*(w #N (k+1) )=z*( (w #N k) *w) by Lm1
                   .=(z*(w #N k)) *w by CLOPBAN3:42
                   .=(w #N k)*(z *w) by A7,CLOPBAN3:42
                   .=(w #N k)*(w *z) by A1,LOPBAN_4:def 1
                   .=(w #N k)*w *z by CLOPBAN3:42
                   .=(w #N (k+1) ) *z by Lm1;
     hence P[k+1] by A8;
   end;
    for n be Element of NAT holds P[n] from NAT_1:sch 1(A5,A6);
   hence thesis;
end;

theorem Th1:
seq1 is convergent & seq2 is convergent & lim(seq1-seq2)=0.X implies
 lim seq1 = lim seq2
proof
   assume that
A1:seq1 is convergent and
A2:seq2 is convergent and
A3:lim(seq1-seq2)=0.X;
    lim(seq1)-lim(seq2) =0.X by A1,A2,A3,CLVECT_1:122;
   then lim(seq1)-lim(seq2) + lim(seq2) =  lim(seq2) by CLOPBAN3:42;
   then lim(seq1)-(lim(seq2) - lim(seq2)) =  lim(seq2) by CLOPBAN3:42;
   then lim(seq1)-0.X = lim(seq2) by RLVECT_1:28;
   hence lim(seq1) = lim(seq2) by RLVECT_1:26;
end;

theorem Th2:
for z st for n being Element of NAT holds s.n = z holds lim s = z
proof
   let z; assume
A1:for n being Element of NAT holds s.n = z;
A2:now
     let p be Real such that
A3:  0<p;
     take k = 0;
     let n such that k<=n;
      s.n = z by A1;
     hence ||.s.n - z.|| < p by A3,CLVECT_1:108;
   end;
   then s is convergent by CLVECT_1:def 16;
   hence thesis by A2,CLVECT_1:def 18;
end;

theorem Th3:
s is convergent & s' is convergent implies s * s' is convergent
proof
   assume that
A1:s is convergent and
A2:s' is convergent;
   consider g1 be Point of X such that
A3: for p being Real st 0<p ex n st for m st n<=m holds ||.s.m-g1.||<p
      by A1,CLVECT_1:def 16;
   consider g2 be Point of X such that
A4: for p being Real st 0<p ex n st for m st n<=m holds ||.s'.m-g2.||<p
      by A2,CLVECT_1:def 16;
   take g=g1*g2;
    ||.s.|| is convergent by A1,CLVECT_1:119;
   then ||.s.|| is bounded by SEQ_2:27;
then ||.s.|| is bounded_above by SEQ_2:def 5;
   then consider R be real number such that
A5: for n holds ||.s.||.n < R by SEQ_2:def 3;
    ||. s.1 .|| =  ||. s .||.1 by CLVECT_1:def 17;
   then 0<= ||.s.||.1 by CLVECT_1:106;
then A6:0 <  R by A5;
A7:now let n;
      ||.s.n.||= ||.s.||.n by CLVECT_1:def 17;
     hence ||.s.n.||  < R by A5;
   end;
   reconsider R as Real by XREAL_0:def 1;
A8:0<=||.g2.|| by CLVECT_1:106;
then A9:0+0<||.g2.||+R by A6,XREAL_1:10;
   let p be Real;
   assume 0<p;
then A10:0<p/(||.g2.||+R) by A9,XREAL_1:141;
   then consider n1 such that
A11:for m st n1<=m holds ||.s.m-g1.||<p/(||.g2.||+R) by A3;
   consider n2 such that
A12:for m st n2<=m holds ||.s'.m-g2.||<p/(||.g2.||+R) by A4,A10;
   take n=n1+n2;
   let m such that
A13:n<=m;
A14:0<=||.s.m.|| by CLVECT_1:106;
A15:0<=||.s'.m-g2.|| by CLVECT_1:106;
    n2<=n by NAT_1:12;
   then n2<=m by A13,XXREAL_0:2;
then A16:||.s'.m-g2.||<p/(||.g2.||+R) by A12;
    n1<=n1+n2 by NAT_1:12;
   then n1<=m by A13,XXREAL_0:2;
then A17:||.s.m-g1.||<=p/(||.g2.||+R) by A11;
    ||.((s*s').m)-g.|| =||.s.m*s'.m-g1*g2.|| by CLOPBAN3:def 10
         .=||.(s.m*s'.m-s.m*g2)+(s.m*g2-g1*g2).|| by CLOPBAN3:42
         .=||.s.m*(s'.m-g2)+(s.m*g2-g1*g2).|| by CLOPBAN3:42
         .=||.s.m*(s'.m-g2)+(s.m-g1)*g2.|| by CLOPBAN3:42;
then A18:||.((s*s').m)-g.||<=
       ||.s.m*(s'.m-g2).||+||.(s.m-g1)*g2.|| by CLVECT_1:def 11;
     ||.s.m.||<R by A7;
then A19:||.s.m.||*||.s'.m-g2.||<R*(p/(||.g2.||+R)) by A14,A15,A16,XREAL_1:98;
    ||.s.m*(s'.m-g2).|| <= ||.s.m.||*||.s'.m-g2.|| by CLOPBAN3:42;
then A20:||.s.m*(s'.m-g2).||<R*(p/(||.g2.||+R)) by A19,XXREAL_0:2;
A21:||.(s.m-g1)*g2.||<=||.g2.||*||.s.m-g1.|| by CLOPBAN3:42;
    ||.g2.||*||.s.m-g1.|| <=||.g2.||*(p/(||.g2.||+R)) by A8,A17,XREAL_1:66;
then A22:||.(s.m-g1)*g2.||<=||.g2.||*(p/(||.g2.||+R)) by A21,XXREAL_0:2;
    R*(p/(||.g2.||+R))+||.g2.||*(p/(||.g2.||+R))
                 =(p/(||.g2.||+R))*(||.g2.||+R)
                .=p by A9,XCMPLX_1:88;
   then ||.s.m*(s'.m-g2).||+||.(s.m-g1)*g2.||<p by A20,A22,XREAL_1:10;
   hence ||.((s*s').m)-g.||<p by A18,XXREAL_0:2;
end;

theorem Th4:
s is convergent implies z * s is convergent
proof
   assume that
A1:s is convergent;
   consider g1 be Point of X such that
A2: for p being Real st 0<p ex n st for m st n<=m holds ||.s.m-g1.||<p
      by A1,CLVECT_1:def 16;
   take g=z*g1;
A3:0<=||.z.|| by CLVECT_1:106;
then A4:0+0< ||.z.||+1 by XREAL_1:10;
   let p be Real;
   assume 0<p;
then A5:0<p/(||.z.||+1) by A4,XREAL_1:141;
   then consider n such that
A6:for m st n<=m holds ||.s.m-g1.||<p/(||.z.||+1) by A2;
   take n;
   let m such that
A7:n<=m;
A8:||.s.m-g1.||< p/(||.z.||+1) by A6,A7;
A9:||.((z*s).m)-g.||  =||.z*s.m-z*g1.|| by CLOPBAN3:def 8
                      .=||.z*(s.m-g1).|| by CLOPBAN3:42;
A10:||.z*(s.m-g1).|| <= ||.z.||*||.s.m-g1.|| by CLOPBAN3:42;
    0<=||.s.m-g1.|| by CLVECT_1:106;
   then ||.z.||*||.s.m-g1.||<= ||.z.||*(p/(||.z.||+1)) by A3,A8,XREAL_1:68;
then A11:||.z*(s.m-g1).||<=||.z.||*(p/(||.z.||+1)) by A10,XXREAL_0:2;
    0+  ||.z.|| < ||.z.||+1 by XREAL_1:10;
then A12:||.z.||*(p/(||.z.||+1)) <  ( ||.z.||+1) *(p/(||.z.||+1)) by A3,A5,
XREAL_1:99;
    ( ||.z.||+1) *(p/(||.z.||+1)) =p by A4,XCMPLX_1:88;
   hence ||.((z*s).m)-g.|| < p by A9,A11,A12,XXREAL_0:2;
end;

theorem  Th5:
s is convergent implies s*z  is convergent
proof
   assume that
A1:s is convergent;
   consider g1 be Point of X such that
A2: for p being Real st 0<p ex n st for m st n<=m holds ||.s.m-g1.||<p
      by A1,CLVECT_1:def 16;
   take g=g1*z;
A3:0<=||.z.|| by CLVECT_1:106;
then A4:0+0< ||.z.||+1 by XREAL_1:10;
   let p be Real;
   assume 0<p;
then A5:0<p/(||.z.||+1) by A4,XREAL_1:141;
   then consider n such that
A6:for m st n<=m holds ||.s.m-g1.||<p/(||.z.||+1) by A2;
   take n;
   let m such that
A7:n<=m;
A8:||.s.m-g1.||< p/(||.z.||+1) by A6,A7;
A9:||.((s*z).m)-g.||  =||.s.m*z-g1*z.|| by CLOPBAN3:def 9
                      .=||.(s.m-g1)*z.|| by CLOPBAN3:42;
A10:||.(s.m-g1)*z.|| <=||.s.m-g1.||* ||.z.|| by CLOPBAN3:42;
    0<=||.s.m-g1.|| by CLVECT_1:106;
   then ||.s.m-g1.||* ||.z.||<=(p/(||.z.||+1))* ||.z.|| by A3,A8,XREAL_1:68;
then A11:||.(s.m-g1)*z.||<=(p/(||.z.||+1))*||.z.|| by A10,XXREAL_0:2;
    0+ ||.z.|| < ||.z.||+1 by XREAL_1:10;
then A12:(p/(||.z.||+1))*||.z.||< (p/(||.z.||+1)) * ( ||.z.||+1) by A3,A5,
XREAL_1:99
;
    (p/(||.z.||+1))* ( ||.z.||+1) = p by A4,XCMPLX_1:88;
   hence ||.((s*z).m)-g.|| < p by A9,A11,A12,XXREAL_0:2;
end;

theorem  Th6:
s is convergent implies lim (z * s) =z* lim(s)
proof
   assume
A1:s is convergent;
then A2:z*s is convergent by Th4;
   set g1=lim(s);
   set g=z*g1;
A3:0<=||.z.|| by CLVECT_1:106;
then A4:0+0< ||.z.||+1 by XREAL_1:10;
    now let p be Real;
     assume 0<p;
then A5:  0<p/(||.z.||+1) by A4,XREAL_1:141;
     then consider n such that
A6:  for m st n<=m holds ||.s.m-g1.||<p/(||.z.||+1) by A1,CLVECT_1:def 18;
     take n;
     let m such that
A7: n<=m;
A8: ||.s.m-g1.||< p/(||.z.||+1) by A6,A7;
A9: ||.((z*s).m)-g.||  =||.z*s.m-z*g1.|| by CLOPBAN3:def 8
                       .=||.z*(s.m-g1).|| by CLOPBAN3:42;
A10: ||.z*(s.m-g1).|| <= ||.z.||*||.s.m-g1.|| by CLOPBAN3:42;
      0<=||.s.m-g1.|| by CLVECT_1:106;
     then ||.z.||*||.s.m-g1.||<= ||.z.||*(p/(||.z.||+1)) by A3,A8,XREAL_1:68;
then A11: ||.z*(s.m-g1).||<=||.z.||*(p/(||.z.||+1)) by A10,XXREAL_0:2;
      0+  ||.z.|| < ||.z.||+1 by XREAL_1:10;
then A12: ||.z.||*(p/(||.z.||+1)) <  ( ||.z.||+1) *(p/(||.z.||+1)) by A3,A5,
XREAL_1:99;
      ( ||.z.||+1) *(p/(||.z.||+1)) = p by A4,XCMPLX_1:88;
     hence ||.((z*s).m)-g.|| < p by A9,A11,A12,XXREAL_0:2;
   end;
   hence thesis by A2,CLVECT_1:def 18;
end;

theorem  Th7:
s is convergent implies lim (s*z) = lim(s)*z
proof
   assume
A1:s is convergent;
then A2:s*z is convergent by Th5;
   set g1=lim(s);
   set g=g1*z;
A3:0<=||.z.|| by CLVECT_1:106;
then A4:0+0< ||.z.||+1 by XREAL_1:10;
    now let p be Real;
     assume 0<p;
then A5:  0<p/(||.z.||+1) by A4,XREAL_1:141;
     then consider n such that
A6:  for m st n<=m holds ||.s.m-g1.||<p/(||.z.||+1) by A1,CLVECT_1:def 18;
     take n;
     let m such that
A7: n<=m;
A8: ||.s.m-g1.||< p/(||.z.||+1) by A6,A7;
A9: ||.((s*z).m)-g.||  =||.s.m*z-g1*z.|| by CLOPBAN3:def 9
                       .=||.(s.m-g1)*z.|| by CLOPBAN3:42;
A10: ||.(s.m-g1)*z.|| <=||.s.m-g1.||* ||.z.|| by CLOPBAN3:42;
      0<=||.s.m-g1.|| by CLVECT_1:106;
     then ||.s.m-g1.||* ||.z.||<=(p/(||.z.||+1))* ||.z.|| by A3,A8,XREAL_1:68;
then A11: ||.(s.m-g1)*z.||<=(p/(||.z.||+1))*||.z.|| by A10,XXREAL_0:2;
      0+  ||.z.|| < ||.z.||+1 by XREAL_1:10;
then A12: (p/(||.z.||+1))*||.z.||< (p/(||.z.||+1)) * ( ||.z.||+1) by A3,A5,
XREAL_1:99;
      (p/(||.z.||+1))* ( ||.z.||+1) =p by A4,XCMPLX_1:88;
     hence ||.((s*z).m)-g.|| < p by A9,A11,A12,XXREAL_0:2;
   end;
   hence thesis by A2,CLVECT_1:def 18;
end;

theorem Th8:
s is convergent & s' is convergent implies
 lim(s*s')=(lim s)*(lim s')
proof
   assume that
A1:s is convergent and
A2:s' is convergent;
A3:s*s' is convergent by A1,A2,Th3;
   set g1=lim s;
   set g2=lim s';
   set g=g1*g2;
    ||.s.|| is convergent by A1,CLVECT_1:119;
   then ||.s.|| is bounded by SEQ_2:27;
then ||.s.|| is bounded_above by SEQ_2:def 5;
   then consider R be real number such that
A4: for n holds ||.s.||.n < R by SEQ_2:def 3;
    ||. s.1 .|| =  ||. s .||.1 by CLVECT_1:def 17;
   then 0<= ||.s.||.1 by CLVECT_1:106;
then A5:0 <  R by A4;
A6:now let n;
      ||.s.n.||= ||.s.||.n by CLVECT_1:def 17;
     hence ||.s.n.|| < R by A4;
   end;
   reconsider R  as Real by XREAL_0:def 1;
A7:0<=||.g2.|| by CLVECT_1:106;
then A8:0+0<||.g2.||+R by A5,XREAL_1:10;
    now let p be Real;
     assume 0<p;
then A9:  0<p/(||.g2.||+R) by A8,XREAL_1:141;
     then consider n1 such that
A10:  for m st n1<=m holds ||.s.m-g1.||<p/(||.g2.||+R) by A1,CLVECT_1:def 18;
     consider n2 such that
A11:  for m st n2<=m holds ||.s'.m-g2.||<p/(||.g2.||+R)
        by A2,A9,CLVECT_1:def 18;
     take n=n1+n2;
     let m such that
A12: n<=m;
A13: 0<=||.s.m.|| by CLVECT_1:106;
A14: 0<=||.s'.m-g2.|| by CLVECT_1:106;
      n2<=n by NAT_1:12;
     then n2<=m by A12,XXREAL_0:2;
then A15: ||.s'.m-g2.||<p/(||.g2.||+R) by A11;
      n1<=n1+n2 by NAT_1:12;
     then n1<=m by A12,XXREAL_0:2;
then A16: ||.s.m-g1.||<=p/(||.g2.||+R) by A10;
      ||.((s*s').m)-g.|| =||.s.m*s'.m-g1*g2.|| by CLOPBAN3:def 10
          .=||.(s.m*s'.m-s.m*g2)+(s.m*g2-g1*g2).|| by CLOPBAN3:42
         .=||.s.m*(s'.m-g2)+(s.m*g2-g1*g2).|| by CLOPBAN3:42
         .=||.s.m*(s'.m-g2)+(s.m-g1)*g2.|| by CLOPBAN3:42;
then A17: ||.((s*s').m)-g.||<=
        ||.s.m*(s'.m-g2).||+||.(s.m-g1)*g2.|| by CLVECT_1:def 11;
      ||.s.m.||<R by A6;
then A18: ||.s.m.||*||.s'.m-g2.||<R*(p/(||.g2.||+R)) by A13,A14,A15,XREAL_1:98;
      ||.s.m*(s'.m-g2).|| <= ||.s.m.||*||.s'.m-g2.|| by CLOPBAN3:42;
then A19: ||.s.m*(s'.m-g2).||<R*(p/(||.g2.||+R)) by A18,XXREAL_0:2;
A20: ||.(s.m-g1)*g2.||<=||.g2.||*||.s.m-g1.|| by CLOPBAN3:42;
      ||.g2.||*||.s.m-g1.|| <=||.g2.||*(p/(||.g2.||+R))
       by A7,A16,XREAL_1:66;
then A21: ||.(s.m-g1)*g2.||<=||.g2.||*(p/(||.g2.||+R)) by A20,XXREAL_0:2;
      R*(p/(||.g2.||+R))+||.g2.||*(p/(||.g2.||+R))
                 =(p/(||.g2.||+R))*(||.g2.||+R)
                .=p by A8,XCMPLX_1:88;
     then ||.s.m*(s'.m-g2).||+||.(s.m-g1)*g2.||<p by A19,A21,XREAL_1:10;
     hence ||.((s*s').m)-g.||<p by A17,XXREAL_0:2;
   end;
   hence thesis by A3,CLVECT_1:def 18;
end;

theorem Th9:
Partial_Sums(z * seq) = z * Partial_Sums(seq) &
Partial_Sums(seq *z ) =  Partial_Sums(seq) *z
proof
A1:Partial_Sums(z * seq) = z * Partial_Sums(seq)
   proof
     defpred P[Element of NAT] means
     Partial_Sums(z *seq).$1= (z * Partial_Sums(seq)).$1;
A2:  P[0]
     proof
       thus Partial_Sums(z*seq).0 =(z * seq).0 by BHSP_4:def 1
             .=z * seq.0 by CLOPBAN3:def 8
             .=z * Partial_Sums(seq).0 by BHSP_4:def 1
             .=(z * Partial_Sums(seq)).0 by CLOPBAN3:def 8;
     end;
A3:  now let n;
       assume
A4:    P[n];
       thus P[n+1]
       proof
         thus Partial_Sums(z * seq).(n+1)
           =Partial_Sums(z * seq).n + (z * seq).(n+1) by BHSP_4:def 1
          .=(z * Partial_Sums(seq).n )+ (z * seq).(n+1) by A4,CLOPBAN3:def 8
          .=(z * Partial_Sums(seq).n )+ (z * seq.(n+1)) by CLOPBAN3:def 8
          .= z * ( Partial_Sums(seq).n + seq.(n+1)) by CLOPBAN3:42
          .= z * ( Partial_Sums(seq).(n+1)) by BHSP_4:def 1
          .= (z * Partial_Sums(seq)).(n+1) by CLOPBAN3:def 8;
       end;
     end;
      for n holds P[n] from NAT_1:sch 1(A2,A3);
     hence thesis by FUNCT_2:113;
   end;
Partial_Sums(seq *z ) =  Partial_Sums(seq) *z
   proof
     defpred P[Element of NAT] means
     Partial_Sums(seq*z).$1= (Partial_Sums(seq)*z).$1;
A5:  P[0]
     proof
       thus Partial_Sums(seq*z).0 = (seq*z).0 by BHSP_4:def 1
        .= seq.0*z by CLOPBAN3:def 9
        .=Partial_Sums(seq).0 *z by BHSP_4:def 1
        .=(Partial_Sums(seq)*z).0 by CLOPBAN3:def 9;
     end;
A6:  now let n;
       assume
A7:    P[n];
       thus P[n+1]
       proof
         thus Partial_Sums( seq*z).(n+1)
            =Partial_Sums( seq*z).n + (seq*z).(n+1) by BHSP_4:def 1
           .=(Partial_Sums(seq).n *z )+ (seq*z).(n+1) by A7,CLOPBAN3:def 9
           .=(Partial_Sums(seq).n*z)+ (seq.(n+1)*z) by CLOPBAN3:def 9
           .= ( Partial_Sums(seq).n + seq.(n+1))*z by CLOPBAN3:42
           .=  ( Partial_Sums(seq).(n+1))*z by BHSP_4:def 1
           .= (Partial_Sums(seq)*z ).(n+1) by CLOPBAN3:def 9;
       end;
     end;
      for n holds P[n] from NAT_1:sch 1(A5,A6);
     hence thesis by FUNCT_2:113;
   end;
   hence thesis by A1;
end;

theorem  Th10:
||.Partial_Sums(seq).k.|| <= Partial_Sums(||.seq.||).k
proof
   defpred P[Element of NAT] means ||. Partial_Sums(seq).$1 .||
      <= Partial_Sums(||.seq.||).$1;
    Partial_Sums(||.seq.||).0
       = (||.seq.||).0 by SERIES_1:def 1
      .= ||. seq.0 .|| by CLVECT_1:def 17;
then A1:P[0] by BHSP_4:def 1;
A2:now let k such that
A3:  P[k];
A4:  ||. Partial_Sums(seq).(k+1) .||
       =||. Partial_Sums(seq).k + (seq).(k+1) .|| by BHSP_4:def 1;
A5:  ||. Partial_Sums(seq).k + (seq).(k+1) .||
       <= ||. Partial_Sums(seq).k.|| + ||. (seq).(k+1) .|| by CLVECT_1:def 11;
      ||. Partial_Sums(seq).k.|| + ||.(seq).(k+1).||
       <= Partial_Sums(||.seq.|| ).k + ||.(seq).(k+1).|| by A3,XREAL_1:8;
     then ||. Partial_Sums(seq).(k+1) .||
       <= Partial_Sums(||.seq.||).k + ||.seq.(k+1).|| by A4,A5,XXREAL_0:2;
     then ||. Partial_Sums(seq).(k+1) .||
       <= Partial_Sums(||.seq.||).k+||.seq.||.(k+1) by CLVECT_1:def 17;
     hence P[k+1] by SERIES_1:def 1;
   end;
    for k holds P[k] from NAT_1:sch 1(A1,A2);
   hence thesis;
end;

theorem  Th11:
(for n st n <= m holds seq1.n = seq2.n) implies
  Partial_Sums(seq1).m =Partial_Sums(seq2).m
proof
   assume
A1:for n st n <= m holds seq1.n = seq2.n;
   defpred P[Element of NAT] means $1 <= m implies
    Partial_Sums(seq1).$1=Partial_Sums(seq2).$1;
A2:P[0]
   proof
     assume
A3:  0 <= m;
     thus Partial_Sums(seq1).0=seq1.0 by BHSP_4:def 1
                         .=seq2.0 by A1,A3
                         .=Partial_Sums(seq2).0 by BHSP_4:def 1;
   end;
A4:for k be Element of NAT st P[k] holds P[k+1]
   proof
     let k be Element of NAT such that
A5:  P[k];
     assume
A6:  k+1 <= m;
      k < k+1 by XREAL_1:31;
     hence Partial_Sums(seq1).(k+1)
            =Partial_Sums(seq2).k+seq1.(k+1) by A5,A6,BHSP_4:def 1,XXREAL_0:2
              .=Partial_Sums(seq2).k+seq2.(k+1) by A1,A6
              .=Partial_Sums(seq2).(k+1) by BHSP_4:def 1;
   end;
    for k holds P[k] from NAT_1:sch 1(A2,A4);
   hence Partial_Sums(seq1).m=Partial_Sums(seq2).m;
end;

theorem  Th12:
((for n holds ||. seq.n .|| <= rseq.n) & rseq is convergent & lim(rseq)=0)
  implies seq is convergent & lim(seq)=0.X
proof
   assume that
A1:(for n holds ||. seq.n .|| <= rseq.n) and
A2:rseq is convergent and
A3:lim(rseq)=0;
    now
     let p be real number such that
A4: 0<p;
     consider n such that
A5: for m st n<=m holds abs(rseq.m-0)<p by A2,A3,A4,SEQ_2:def 7;
   now
       let m such that
A6:   n<=m;
A7:    abs(rseq.m-0)<p by A5,A6;
A8:    ||. seq.m-0.X .|| = ||. seq.m .|| by RLVECT_1:26;
A9:    ||. seq.m .|| <= rseq.m    by A1;
        rseq.m  <=  abs(rseq.m) by ABSVALUE:11;
       then ||. seq.m - 0.X .||  <=  abs(rseq.m) by A8,A9,XXREAL_0:2;
       hence ||. seq.m - 0.X .||  < p by A7,XXREAL_0:2;
     end;
     hence ex n st for m st n<=m holds ||. seq.m - (0.X) .||  < p;
   end;
then A10: for p be Real st 0<p ex n st for m st n<=m holds ||. seq.m - 0.X .||
< p;
   hence seq is convergent by CLVECT_1:def 16;
   hence thesis by A10,CLVECT_1:def 18;
end;

definition let X,z;
func z ExpSeq -> sequence of X means :Def2:
   for n holds it.n = 1r/(n!c) * (z #N n);
existence
proof
   deffunc U(Element of NAT) = 1r/(($1)!c) *(z #N $1);
   thus ex s being sequence of X st
     for n holds s.n = U(n) from FUNCT_2:sch 4;
   end;
uniqueness
proof
   let S1,S2 be sequence of X;
   assume that
A1: ( for n be Element of NAT holds S1.n = 1r/(n!c) *(z #N n) ) and
A2: ( for n be Element of NAT holds S2.n = 1r/(n!c) *(z #N n) );
    for n be Element of NAT holds S1.n = S2.n
   proof
     let n be Element of NAT;
      S1.n = 1r/(n!c) *(z #N n) by A1;
     hence thesis by A2;
   end;
   hence thesis by FUNCT_2:113;
end;
end;

scheme ExNormSpaceCASE {CNS()->non empty Complex_Banach_Algebra,
   F(Element of NAT,Element of NAT)-> Point of CNS() }: for k holds
 ex seq be sequence of CNS()
   st for n holds ((n <= k implies seq.n=F(k,n)) &
                                   (n > k implies seq.n=0.CNS()))
proof
   let k;
   defpred P[set,set] means ex n st (n=$1 &
   (n <= k implies $2=F(k,n )) &
   (n > k implies $2=0.CNS()));
A1:  now let x be set;
     assume x in NAT;
     then consider n such that
A2:  n=x;
A3:  now
       assume n <= k;
       hence CHK(n,k ) * F (k,n) =1r * F(k,n) by COMPLEX1:def 7,SIN_COS:def 2
                                .=F(k,n) by CLVECT_1:def 2;
     end;
A4:  now assume n > k;
       hence CHK(n,k) * F (k,n) =0c * F(k,n) by SIN_COS:def 2
                       .=0.CNS() by CLVECT_1:2;
     end;
     reconsider y=CHK(n,k) * F (k,n) as set;
     take y;
     thus P[x,y] by A2,A3,A4;
   end;
A5:for x,y,z be set st x in NAT & P[x,y] & P[x,z] holds y=z;
   consider f be Function such that
A6: dom f =NAT and
A7:for x be set st x in NAT holds P[x,f.x] from FUNCT_1:sch 2(A5,A1);
    now let x be set;
     assume x in NAT;
     then ex n st (n=x & (n <= k implies f.x=F(k,n)) &
                       (n > k implies f.x=0.CNS())) by A7;
     hence f.x in the carrier of CNS();
   end;
   then reconsider f as sequence of CNS() by A6,FUNCT_2:5;
   take seq=f;
   let n;
    ex l be Element of NAT st l=n &
    (l <= k implies seq.n=F(k,l)) & (l > k implies seq.n=0.CNS()) by A7;
   hence ((n <= k implies seq.n=F(k,n)) & (n > k implies seq.n=0.CNS()));
end;

definition let n,X,z,w;
  canceled;
func Expan(n,z,w) -> sequence of X means :Def4:
  for k be Element of NAT holds
   ( k <= n implies it.k=((Coef(n)).k) * (z #N k) * (w #N (n-' k)) ) &
   (n < k implies it.k=0.X);
existence
proof
   deffunc U(Element of NAT,Element of NAT)
    = ((Coef($1)).$2) * (z #N $2) * (w #N ($1 -' $2));
    for n holds
    ex seq st
     for k holds
      (k <= n implies seq.k = U(n,k)) &
      (k > n implies seq.k=0.X) from ExNormSpaceCASE;
   hence ex seq st
     for k holds
      (k <= n implies seq.k =((Coef(n)).k) * (z #N k) * (w #N (n-' k))) &
      (k > n implies seq.k=0.X);
end;
uniqueness
proof
   let seq1,seq2;
   assume that
A1:for k holds
    (k <= n implies seq1.k =((Coef(n)).k) * (z #N k) * (w #N (n-' k)))&
    (k > n implies seq1.k=0.X) and
A2:for k holds
    (k <= n implies seq2.k = ((Coef(n)).k) * (z #N k) * (w #N (n-' k)) )&
    (k > n implies seq2.k=0.X);
    now let k;
     per cases;
       suppose
A3:    k <= n;
       hence seq1.k =((Coef(n)).k) * (z #N k) * (w #N (n-' k)) by A1
                   .= seq2.k by A2,A3;
     end;
       suppose
A4:    k > n;
       hence seq1.k = 0.X by A1 .= seq2.k by A2,A4;
     end;
   end;
   hence seq1=seq2 by FUNCT_2:113;
end;
end;

definition let n,X,z,w;
func Expan_e(n,z,w) -> sequence of X means :Def5:
  for k be Element of NAT holds
   ( k <= n implies it.k=((Coef_e(n)).k) * (z #N k) * (w #N (n-' k)) ) &
   ( n < k implies it.k=0.X );
existence
proof
   deffunc U(Element of NAT,Element of NAT)
    = ((Coef_e($1)).$2) * (z #N $2) * (w #N ($1-'$2));
    for n holds
    ex seq st for k holds
     (k <= n implies seq.k = U(n,k)) &
     (k > n implies seq.k=0.X ) from ExNormSpaceCASE;
   hence ex seq st for k holds
     (k <= n implies seq.k =((Coef_e(n)).k) * (z #N k) * (w #N (n-'k)) ) &
     (k > n implies seq.k=0.X );
end;
uniqueness
proof
   let seq1,seq2;
   assume that
A1: for k holds
     (k <= n implies seq1.k =((Coef_e(n)).k) * (z #N k) * (w #N (n-' k)) ) &
     (k > n implies seq1.k=0.X ) and
A2: for k holds
     (k <= n implies seq2.k = ((Coef_e(n)).k) * (z #N k) * (w #N (n-' k)) ) &
     (k > n implies seq2.k=0.X );
    now let k;
     per cases;
     suppose
A3:    k <= n;
       hence seq1.k =((Coef_e(n)).k) * (z #N k) * (w #N (n-' k)) by A1
                   .= seq2.k by A2,A3;
     end;
     suppose
A4:    k > n;
       hence seq1.k = 0.X by A1 .= seq2.k by A2,A4;
     end;
   end;
   hence seq1=seq2 by FUNCT_2:113;
end;
end;

definition let n,X,z,w;
func Alfa(n,z,w) -> sequence of X means :Def6:
  for k be Element of NAT holds
   ( k <= n implies it.k= (z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) ) &
   ( n < k implies it.k=0.X);
existence
proof
   deffunc U(Element of NAT,Element of NAT)
    = (z ExpSeq).$2 * Partial_Sums(w ExpSeq).($1 -' $2);
    for n holds
    ex seq st for k holds
     (k <= n implies seq.k = U(n,k)) &
     (k > n implies seq.k=0.X  ) from ExNormSpaceCASE;
   hence ex seq st for k holds
     (k <= n implies seq.k =(z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) ) &
     (k > n implies seq.k=0.X );
end;
uniqueness
proof
   let seq1,seq2;
   assume that
A1: for k holds
     (k <= n implies seq1.k = (z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) ) &
     (k > n implies seq1.k=0.X ) and
A2: for k holds
     (k <= n implies seq2.k = (z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) ) &
     (k > n implies seq2.k=0.X );
    now let k;
     per cases;
     suppose
A3:    k <= n;
       hence seq1.k = (z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) by A1
                   .= seq2.k by A2,A3;
     end;
     suppose
A4:    k > n;
       hence seq1.k = 0.X by A1 .= seq2.k by A2,A4;
     end;
   end;
   hence seq1=seq2 by FUNCT_2:113;
end;
end;

definition let X,z,w,n;
func Conj(n,z,w) -> sequence of X means :Def7:
  for k be Element of NAT holds
   ( k <= n implies
      it.k= (z ExpSeq).k * (Partial_Sums(w ExpSeq).n
                           -Partial_Sums(w ExpSeq).(n-'k))) &
   ( n < k implies it.k=0.X );
existence
proof
   deffunc U(Element of NAT,Element of NAT)
    = (z ExpSeq).$2 * (Partial_Sums(w ExpSeq).$1
                          -Partial_Sums(w ExpSeq).($1-'$2));
    for n holds
    ex seq st for k holds
     (k <= n implies seq.k = U(n,k) ) &
     (k > n implies seq.k=0.X) from ExNormSpaceCASE;
   hence ex seq st for k holds
    (k <= n implies seq.k =(z ExpSeq).k * (Partial_Sums(w ExpSeq).n
                          -Partial_Sums(w ExpSeq).(n-'k)) ) &
    (k > n implies seq.k=0.X);
end;
uniqueness
proof
   let seq1,seq2;
   assume that
A1:for k holds
    (k <= n implies seq1.k =(z ExpSeq).k * (Partial_Sums(w ExpSeq).n
                          -Partial_Sums(w ExpSeq).(n-'k)) ) &
    (k > n implies seq1.k=0.X) and
A2:for k holds
    (k <= n implies seq2.k = (z ExpSeq).k * (Partial_Sums(w ExpSeq).n
                          -Partial_Sums(w ExpSeq).(n-'k)) )&
    (k > n implies seq2.k=0.X);
    now let k;
     per cases;
     suppose A3:k <= n;
       hence seq1.k =(z ExpSeq).k * (Partial_Sums(w ExpSeq).n
              -Partial_Sums(w ExpSeq).(n-'k)) by A1
           .= seq2.k by A2,A3;
     end;
     suppose A4:k > n;
       hence seq1.k = 0.X by A1 .= seq2.k by A2,A4;
     end;
   end;
   hence seq1=seq2 by FUNCT_2:113;
end;
end;

theorem Th13:
z ExpSeq.(n+1) = (1r/[*(n+1),0*] * z) * (z ExpSeq.n)  &
z ExpSeq.0=1.X  &
||.(z ExpSeq).n.|| <= (||.z.|| ExpSeq ).n
proof
A1:now
     let n be Element of NAT;
A2:    [*(n+1),0*] = n+1+0*<i> by COMPLEX1:27;
     thus (z ExpSeq).(n+1)=1r/((n+1)!c) * z #N (n+1) by Def2
               .=1r/((n+1)!c) * (z GeoSeq).(n+1)  by CLOPBAN3:def 13
               .=1r/((n+1)!c) * ((z GeoSeq).n * z) by CLOPBAN3:def 12
               .=1r/((n+1)!c) * (z #N n * z) by CLOPBAN3:def 13
               .=1r/(n!c * [*(n+1),0*])*(z #N n * z) by A2,SIN_COS:1
               .=((1r*1r)/(n!c * [*(n+1),0*]))*(z* z #N n ) by Lm1,
COMPLEX1:def 7
               .=((1r/(n!c)) * (1r/[*(n+1),0*]) )*(z* z #N n ) by XCMPLX_1:77
               .=((1r/[*(n+1),0*])*z)*((1r/(n!c)) * z #N n ) by CLOPBAN3:42
               .=((1r/[*(n+1),0*])*z)*(z ExpSeq.n) by Def2;
   end;
A3:(z ExpSeq).0=1r/(0!c) * z #N 0 by Def2
              .=1r/(0!c)*(z GeoSeq).0 by CLOPBAN3:def 13
              .=1r * 1.X by CLOPBAN3:def 12,SIN_COS:1
              .=1.X by CLVECT_1:def 2;
   defpred X[Element of NAT] means
    ||. ((z ExpSeq)).$1 .|| <=( ||.z.|| ExpSeq ).$1;
    ( ||.z.|| ExpSeq ).0 =  ((||.z.||) |^ 0) / (0!) by SIN_COS:def 9
                       .= 1/(0!) by NEWTON:9
                       .= 1/((Prod_real_n).0) by SIN_COS:def 7
                       .= 1/1 by SIN_COS:def 5
                       .= 1;
then A4:X[0] by A3,CLOPBAN3:42;
A5:for n be Element of NAT st X[n] holds X[n+1]
   proof
     let n such that
A6:  X[n];
A7:  0 <= n+1 by NAT_1:2;
      [*(n+1),0*] = n+1+0*<i> by COMPLEX1:27;
then  |.[*(n+1),0*].| = n+1 by A7,ABSVALUE:def 1;
then A8:  |. 1r/[*(n+1),0*] .| = 1/(n+1) by COMPLEX1:134,153;
A9: ||. ((z ExpSeq)).(n+1) .||
       = ||. (1r/[*(n+1),0*] *z )*(z ExpSeq.n) .|| by A1;
A10: ||. (1r/[*(n+1),0*] *z)*(z ExpSeq.n ) .||
      <= ||. (1r/[*(n+1),0*] *z) .|| * ||. (z ExpSeq.n ) .|| by CLOPBAN3:42;
A11: ||. (1r/[*(n+1),0*] *z) .||
       = 1/(n+1) *  ||.z.|| by A8,CLVECT_1:def 11;
      0<= ||. (1r/[*(n+1),0*] *z) .|| by CLVECT_1:106;
then A12: ||.(1r/[*(n+1),0*] *z).|| * ||.(z ExpSeq.n).||
       <= ||.(1r/[*(n+1),0*] *z).|| * (||.z.|| ExpSeq).n by A6,XREAL_1:66;
      1/(n+1) * ||.z.|| * (||.z.|| ExpSeq).n
       = 1/(n+1) * ( (||.z.|| ExpSeq).n * ||.z.|| )
      .= 1/(n+1) * ( ((||.z.||)|^n)/(n!)*||.z.|| ) by SIN_COS:def 9
      .= 1/(n+1) * ( ((||.z.||)|^n)*||.z.||/(n!) ) by XCMPLX_1:75
      .= 1/(n+1) * ( (||.z.||)|^(n+1)/(n!) ) by NEWTON:11
      .= ((||.z.||)|^ (n+1)) /((n)!*(n+1)) by XCMPLX_1:104
      .= ((||.z.||)|^ (n+1)) /((n+1)!) by SIN_COS:2
      .= ( ||.z.|| ExpSeq ).(n+1) by SIN_COS:def 9;
     hence X[n+1] by A9,A10,A11,A12,XXREAL_0:2;
   end;
    for n holds X[n] from NAT_1:sch 1(A4,A5);
   hence thesis by A1,A3;
end;

theorem
0 < k implies (Shift seq).k=seq.(k -' 1) by LOPBAN_4:15;

theorem Th15:
Partial_Sums(seq).k=Partial_Sums(Shift seq).k+seq.k
proof
   defpred X[Element of NAT] means
    Partial_Sums(seq).$1=Partial_Sums(Shift seq).$1+seq.$1;
    Partial_Sums(seq).0 = seq.0 by BHSP_4:def 1
       .= 0.X + seq.0 by RLVECT_1:10
       .= (Shift(seq)).0 + seq.0 by LOPBAN_4:def 5
       .= Partial_Sums(Shift(seq)).0+seq.0 by BHSP_4:def 1;
then A1:X[0];
A2:for k st X[k] holds X[k+1]
   proof
     let k such that
A3:  Partial_Sums(seq).k=Partial_Sums(Shift(seq)).k+seq.k;
     thus Partial_Sums(seq).(k+1)
        = (Partial_Sums(Shift(seq)).k+seq.k) + seq.(k+1) by A3,BHSP_4:def 1
       .=(Partial_Sums(Shift(seq)).k+(Shift(seq)).(k+1)) + seq.(k+1)
       by LOPBAN_4:def 5
       .=Partial_Sums(Shift(seq)).(k+1)+seq.(k+1) by BHSP_4:def 1;
   end;
    for k holds X[k] from NAT_1:sch 1(A1,A2);
   hence thesis;
end;

theorem Th16:
for z,w st z,w are_commutative holds
  (z+w) #N n = Partial_Sums(Expan(n,z,w)).n
proof
   let z,w such that
A1:z,w are_commutative;

A2:(z+w) #N 0 =((z+w) GeoSeq).0 by CLOPBAN3:def 13
             .=1.X by CLOPBAN3:def 12;
A3:0-'0=0-0 by BINARITH:def 3 .=0;
   defpred X[Element of NAT] means
    (z+w) #N $1 = Partial_Sums(Expan($1,z,w)).$1;
    Partial_Sums(Expan(0,z,w)).0 = Expan(0,z,w).0 by BHSP_4:def 1
                .= (Coef(0)).0 * (z #N 0) * (w #N 0) by A3,Def4
                .= 1r/(1r * 1r) * z #N 0 * w #N 0
                     by A3,COMPLEX1:def 7,SIN_COS:1,def 10
                .= z #N 0 * w #N 0 by CLVECT_1:def 2,COMPLEX1:def 7
                .= (z GeoSeq).0 * w #N 0 by CLOPBAN3:def 13
                .= (z GeoSeq).0 * (w GeoSeq).0 by CLOPBAN3:def 13
                .= 1.X * (w GeoSeq).0 by CLOPBAN3:def 12
                .= 1.X  * 1.X by CLOPBAN3:def 12
                .= 1.X by CLOPBAN3:42;
then A4:X[0] by A2;
A5:for n st X[n] holds X[n+1]
   proof
     let n such that
A6:  (z+w) #N n = Partial_Sums(Expan(n,z,w)).n;
A7:  (z+w) #N (n+1) =((z+w) GeoSeq ).(n+1) by CLOPBAN3:def 13
             .=((z+w) GeoSeq ).n * (z+w) by CLOPBAN3:def 12
             .=(Partial_Sums(Expan(n,z,w)).n) * (z+w) by A6,CLOPBAN3:def 13
             .=(Partial_Sums(Expan(n,z,w))*(z+w)) .n by CLOPBAN3:def 9
             .=(Partial_Sums(Expan(n,z,w)*(z+w))).n by Th9;
      Expan(n,z,w)*(z+w) =( Expan(n,z,w)*z)+(Expan(n,z,w)*w)
     proof
        now let k be Element of NAT;
         thus ( Expan(n,z,w)*(z+w) ).k= Expan(n,z,w).k *(z+w) by CLOPBAN3:def 9
               .= Expan(n,z,w).k * z + Expan(n,z,w).k *w by CLOPBAN3:42
               .=( Expan(n,z,w)*z).k+ Expan(n,z,w).k *w by CLOPBAN3:def 9
               .=(Expan(n,z,w)*z).k+(Expan(n,z,w)*w).k by CLOPBAN3:def 9
               .=( (Expan(n,z,w)*z)+(Expan(n,z,w)*w)).k by NORMSP_1:def 5;
       end;
       hence thesis by FUNCT_2:113;
     end;
then A8:  (z+w) #N (n+1) =( Partial_Sums( (Expan(n,z,w)*z))
        +Partial_Sums(( (Expan(n,z,w)*w)))).n by A7,CLOPBAN3:19
      .= Partial_Sums((Expan(n,z,w)*z)).n
        +Partial_Sums((Expan(n,z,w)*w)).n by NORMSP_1:def 5;
A9:  Partial_Sums(( Expan(n,z,w)*z)).n
      = Partial_Sums((Expan(n,z,w)*z)).(n+1)
     proof
A10:    Partial_Sums((Expan(n,z,w)*z)).(n+1)
        = Partial_Sums((Expan(n,z,w)*z)).n+(Expan(n,z,w)*z).(n+1)
           by BHSP_4:def 1
       .= Partial_Sums((Expan(n,z,w)*z)).n
         + Expan(n,z,w).(n+1) *z by CLOPBAN3:def 9;
        n < n+1 & n+1 is Element of NAT by XREAL_1:31;
       hence Partial_Sums(( Expan(n,z,w)*z)).(n+1)
              =Partial_Sums((Expan(n,z,w)*z)).n +0.X *z by A10,Def4
             .=Partial_Sums((Expan(n,z,w)*z)).n +0.X by CLOPBAN3:42
             .=Partial_Sums((Expan(n,z,w)*z)).n by CLOPBAN3:42;
     end;

A11: Partial_Sums(( Expan(n,z,w)*w )).n
      =Partial_Sums(( Expan(n,z,w)*w )).(n+1)
     proof
A12:   Partial_Sums((Expan(n,z,w)*w)).(n+1)
        =Partial_Sums((Expan(n,z,w)*w)).n+(Expan(n,z,w)*w).(n+1)
           by BHSP_4:def 1
       .=Partial_Sums((Expan(n,z,w)*w)).n
         + Expan(n,z,w).(n+1) *w by CLOPBAN3:def 9;
        n < n+1 & n+1 is Element of NAT by XREAL_1:31;
       hence Partial_Sums(( Expan(n,z,w)*w)).(n+1)
            =Partial_Sums((Expan(n,z,w)*w)).n +0.X *w by A12,Def4
           .=Partial_Sums((Expan(n,z,w)*w)).n +0.X by CLOPBAN3:42
           .=Partial_Sums((Expan(n,z,w)*w)).n by CLOPBAN3:42;
     end;

A13: Partial_Sums(( Expan(n,z,w)*z)).(n+1)
      =Partial_Sums(Shift(( Expan(n,z,w)*z))).(n+1)
     proof
A14:   Partial_Sums(( Expan(n,z,w)*z)).(n+1)
        =Partial_Sums(Shift(( Expan(n,z,w)*z))).(n+1) +( Expan(n,z,w)*z).(n+1)
           by Th15;
        0 +n < n+1 by XREAL_1:31;
then A15:    Expan(n,z,w).(n+1)=0.X by Def4;
       thus Partial_Sums(( Expan(n,z,w)*z)).(n+1)
           =Partial_Sums(Shift(( Expan(n,z,w)*z))).(n+1)
            + Expan(n,z,w).(n+1)*z by A14,CLOPBAN3:def 9
          .=Partial_Sums(Shift(( Expan(n,z,w)*z))).(n+1)
            +0.X by A15,CLOPBAN3:42
          .= Partial_Sums(Shift((Expan(n,z,w)*z))).(n+1) by CLOPBAN3:42;
     end;
A16: ( Expan(n,z,w)*w)+Shift(( Expan(n,z,w)*z)) =Expan(n+1,z,w)
     proof
        now let k be Element of NAT;
A17:     now assume
A18:       n+1 < k;
A19:       n < k & 1 <= k & 1 <k & 0 <k & n <k-1 & n < k-'1
           proof
              0 <= n by NAT_1:2;
then A20:         0+1 <= n+1 by XREAL_1:8;
A21:         n+1-1 < k -1 by A18,XREAL_1:11;
then A22:         n+0 < k-1+1 by XREAL_1:10;
              1 <= k by A18,A20,XXREAL_0:2;
             hence thesis by A18,A20,A21,A22,BINARITH:50,XXREAL_0:2;
           end;
            (( Expan(n,z,w)*w)+Shift(( Expan(n,z,w)*z))).k
            =(Expan(n,z,w)*w).k + (Shift((Expan(n,z,w)*z))).k by NORMSP_1:def 5
           .=(Expan(n,z,w).k*w) + (Shift(( Expan(n,z,w)*z))).k
               by CLOPBAN3:def 9
           .=(Expan(n,z,w).k*w) +  ((Expan(n,z,w)*z)).(k -' 1) by A19,
LOPBAN_4:15
           .=(Expan(n,z,w).k*w) + ((Expan(n,z,w).(k -' 1)*z)) by CLOPBAN3:def 9
           .=( 0.X*w )+ ((Expan(n,z,w).(k -' 1))*z) by A19,Def4
           .= 0.X+  ((Expan(n,z,w).(k -' 1))*z) by CLOPBAN3:42
           .=0.X + ( 0.X*z) by A19,Def4
           .=0.X + 0.X by CLOPBAN3:42
           .=0.X by CLOPBAN3:42;
           hence (( Expan(n,z,w)*w)+Shift((Expan(n,z,w)*z))).k
                =Expan(n+1,z,w).k by A18,Def4;
         end;
          now assume
A23:       k <= (n+1);
A24:       now assume
A25:         k=n+1;
             thus ((Expan(n,z,w)*w) +Shift((Expan(n,z,w)*z))).k=
                Expan(n+1,z,w).k
             proof
A26:           n < n+1 by XREAL_1:31;
A27:           n-'n= n-n by BINARITH:50 .= 0;
A28:           n+1-'(n+1)= n+1-(n+1) by BINARITH:50 .= 0;
A29:           n!c <>0 & (n+1)!c <>0 by SIN_COS:1;
A30:           (Coef(n)).n = n!c /((n!c) * 1) by A27,SIN_COS:1,def 10
                            .= 1 by A29,XCMPLX_1:60;

A31:           (Coef(n+1)).(n+1)
                 = (n+1)!c /(((n+1)!c) * 1) by A28,SIN_COS:1,def 10
                .= 1 by A29,XCMPLX_1:60;
               thus
                (( Expan(n,z,w)*w)+Shift((Expan(n,z,w)*z))).k
                = (Expan(n,z,w)*w).(n+1)+(Shift((Expan(n,z,w)*z ))).(n+1)
                    by A25,NORMSP_1:def 5
               .= (Expan(n,z,w).(n+1) *w) + (Shift((Expan(n,z,w)*z))).(n+1)
                    by CLOPBAN3:def 9
               .= (0.X*w) + (Shift(( Expan(n,z,w)*z))).(n+1) by A26,Def4
               .= (0.X) + (Shift(( Expan(n,z,w)*z))).(n+1) by CLOPBAN3:42
               .= (Shift((Expan(n,z,w)*z ))).(n+1) by CLOPBAN3:42
               .= (( Expan(n,z,w)*z)).(n) by LOPBAN_4:def 5
               .=  (Expan(n,z,w)).n *z by CLOPBAN3:def 9
               .=((Coef(n)).n * (z #N n) * (w #N (n-' n))) *z by Def4
               .=(Coef(n)).n * (z #N n) * ((w #N (n-' n)) *z) by CLOPBAN3:42
               .=(Coef(n)).n * (z #N n) * ( z*(w #N (n-' n))) by A1,Lm2
               .=(Coef(n)).n * (z #N n) *  z*(w #N (n-' n)) by CLOPBAN3:42
               .=(Coef(n)).n *( (z #N n) *  z)*(w #N (n-' n)) by CLOPBAN3:42
               .= ((Coef(n+1)).(n+1) * (z #N (n+1)) * (w #N (n-' n)))
                    by A30,A31,Lm1
               .= Expan(n+1,z,w).k by A25,A27,A28,Def4;
             end;
           end;
            now assume
A32:         k < n+1;
A33:         now assume
A34:           k=0;
               thus
                ((Expan(n,z,w)*w)+Shift(( Expan(n,z,w)*z))).k = Expan(n+1,z,w).
k
               proof
A35:             0 <= n by NAT_1:2;
A36:             0 <= n+1 by NAT_1:2;
A37:             n-'0 =n-0 by A35,BINARITH:50;
A38:             n+1-'0 =n+1-0 by A36,BINARITH:50;
A39:             n!c  <>0 & (n+1)!c  <>0 by SIN_COS:1;
A40:             (Coef(n)).0 = n!c /(1 * (n!c)) by A35,A37,SIN_COS:1,def 10
                            .= 1 by A39,XCMPLX_1:60;
A41:             (Coef(n+1)).0 = (n+1)!c /(1 * ((n+1)!c)) by A36,A38,SIN_COS:1
,def 10
                            .= 1 by A39,XCMPLX_1:60;
                 thus
                  (( Expan(n,z,w)*w)+Shift(( Expan(n,z,w)*z))).k
                  = ( Expan(n,z,w)*w).0+(Shift((Expan(n,z,w)*z))).0
                      by A34,NORMSP_1:def 5
                 .=(Expan(n,z,w).0 *w) + (Shift(( Expan(n,z,w)*z))).0
                      by CLOPBAN3:def 9
                 .=( Expan(n,z,w).0 *w ) + 0.X by LOPBAN_4:def 5
                 .=( Expan(n,z,w).0 *w) by CLOPBAN3:42
                 .= ((Coef(n)).0 * (z #N 0) * (w #N (n-'0)))*w by A35,Def4
                 .= (Coef(n)).0 *(z #N 0) *((w #N (n-' 0))*w) by CLOPBAN3:42
                 .= (Coef(n+1)).0  *(z #N 0)* (w #N ((n+1)-'0))
                     by A37,A38,A40,A41,Lm1
                 .= Expan(n+1,z,w).k by A34,A36,Def4;
               end;
             end;
              now assume
A42:             k<>0;
then A43:           0<k by NAT_1:3;
then A44:           0+1 <= k by INT_1:20;
A45:           k+1-1 <= n+1-1 by A32,INT_1:20;
                k < k+1 by XREAL_1:31;
               then k-1 <= k+1-1 by XREAL_1:11;
               then k-1 <= n by A45,XXREAL_0:2;
then A46:           k-'1 <= n by A44,BINARITH:50;
               thus
                (( Expan(n,z,w)*w)+Shift((Expan(n,z,w)*z))).k = Expan(n+1,z,w).
k
               proof
A47:             (( Expan(n,z,w)*w )+Shift(( Expan(n,z,w)*z))).k
                  =( Expan(n,z,w)*w).k+(Shift((Expan(n,z,w)*z))).k
                     by NORMSP_1:def 5
                 .=(Expan(n,z,w).k)*w + (Shift(( Expan(n,z,w)*z))).k
                     by CLOPBAN3:def 9
                 .=(Expan(n,z,w).k)*w + (Expan(n,z,w)*z).( k-'1) by A43,
LOPBAN_4:15
                 .=(Expan(n,z,w).k)*w  + ( Expan(n,z,w).(k-'1))*z
                     by CLOPBAN3:def 9
                 .=( (Coef(n)).k * (z #N k) * (w #N (n-' k)) ) *w
                   + ( Expan(n,z,w).(k-'1))*z by A45,Def4
                 .=(((Coef(n)).k * (z #N k) * (w #N (n-' k)))) *w
                   + ( ((Coef(n)).(k -' 1) * (z #N (k-'1))
                    * (w #N (n-' (k -' 1)))))*z by A46,Def4
                 .=((Coef(n)).k * ((z #N k)) * ((w #N (n-' k))*w))
                   + ( ((Coef(n)).(k -' 1)) * (z #N (k -' 1))
                    * (w #N (n-' (k-'1)))) *z by CLOPBAN3:42
                 .=((Coef(n)).k * ((z #N k)) * ((w #N ((n-' k)+1))))
                   + ( ((Coef(n)).(k -' 1)) * (z #N (k -' 1))
                    * (w #N (n-' (k-'1)))) *z by Lm1
                 .=((Coef(n)).k * ((z #N k)) * ((w #N ((n-' k)+1))))
                   +  ((Coef(n)).(k -' 1)) * (z #N (k -' 1))
                    *((w #N (n-' (k-'1))) *z) by CLOPBAN3:42
                 .=((Coef(n)).k * ((z #N k)) * ((w #N ((n-' k)+1))))
                   +  ((Coef(n)).(k -' 1)) * (z #N (k -' 1))
                    *(z*(w #N (n-' (k-'1)))) by A1,Lm2
                 .=((Coef(n)).k * ((z #N k)) * ((w #N ((n-' k)+1))))
                   +  ((Coef(n)).(k -' 1)) * (z #N (k -' 1))
                    *z*(w #N (n-' (k-'1))) by CLOPBAN3:42
                 .=((Coef(n)).k * ((z #N k)) * ((w #N ((n-' k)+1))))
                   + ((Coef(n)).(k -'1))*((z #N (k -'1))*z)*(w #N (n-'(k-'1)))
                     by CLOPBAN3:42
                 .=((Coef(n)).k * ((z #N k)) * ((w #N ((n-' k)+1))))
                   + ((Coef(n)).(k -'1))*(z #N ((k -'1)+1))*(w #N (n-'(k-'1)))
                     by Lm1;
A48:             w #N ((n-' k)+1) = w #N (n+1-'k)
                 proof
                    (n-'k)+1=n-k+1 by A45,BINARITH:50
                          .=n+1-k
                          .=(n+1-'k) by A32,BINARITH:50;
                   hence w #N ((n-' k)+1) = w #N (n+1-'k);
                 end;
A49:             w #N (n-' (k-'1)) =w #N  (n+1-'k)
                 proof
                    (n-'(k-'1))=n-(k-'1) by A46,BINARITH:50
                             .=n-(k-1) by A44,BINARITH:50
                             .=n+1-k
                             .=(n+1-'k) by A32,BINARITH:50;
                   hence w #N (n-' (k-'1))=w #N (n+1-'k);
                 end;
                  z #N ((k -' 1)+1)= z #N k
                 proof
                    (k-'1)+1 =k-1+1 by A44,BINARITH:50
                           .=k;
                   hence z #N ((k -' 1)+1)= z #N k;
                 end;
then A50:             ((Expan(n,z,w)*w)+Shift((Expan(n,z,w)*z))).k
                  =(Coef(n)).k * ((z #N k) * (w #N (n+1-'k)))
                   +  (Coef(n)).((k -' 1)) *(z #N k) *(w #N (n+1-'k))
                     by A47,A48,A49,CLOPBAN3:42
                 .=(Coef(n)).k * ((z #N k) * (w #N (n+1-'k)))
                   +  (Coef(n)).((k -' 1)) *((z #N k) *(w #N (n+1-'k)))
                     by CLOPBAN3:42
                 .= ((Coef(n)).k +(Coef(n)).(k -' 1) )
                   * ((z #N k) * (w #N ((n+1) -' k) )) by CLOPBAN3:42;
A51:             (Coef(n)).k +(Coef(n)).(k -' 1)
                  =n!c /( (k!c) * ((n-'k)!c) ) +(Coef(n)).(k-'1)
                    by A45,SIN_COS:def 10
                 .=n!c /( (k!c) * ((n-'k)!c) )
                   +n!c /(((k-'1)!c)*(((n-'(k-'1)))!c)) by A46,SIN_COS:def 10;
A52:             (n-'(k-'1)) =n-(k-'1) by A46,BINARITH:50
                        .=n-(k-1) by A44,BINARITH:50
                        .=n+1-k
                        .=n+1-'k by A32,BINARITH:50;
A53:             (((k-'1)!c) * (((n-'(k-'1)))!c)) * [*k,0*]
                   = [*k,0*] * ((k-'1)!c) * (((n-'(k-'1)))!c)
                  .=(k+0*<i>) * ((k-'1)!c) * (((n-'(k-'1)))!c) by COMPLEX1:27
                  .=(k!c ) * ((n+1-'k)!c ) by A43,A52,SIN_COS:3;

A54:             (k!c) <> 0c & (n-'k)!c <>0c & (k-'1)!c <>0c &
                  ((n-'(k-'1)))!c <>0c & ((n+1-'k))!c <>0c &
                   (n+1-k) <> 0 by A32,SIN_COS:1;
A55:      [*(n+1-k),0*] + [*k,0*] = (n+1-k)+0*<i> + [*k,0*] by COMPLEX1:27
            .= (n+1-k)+0*<i> + (k+0*<i>) by COMPLEX1:27
            .= (n+1-k+k)+(0+0)*<i>
            .= [*(n+1-k+k),0+0*] by COMPLEX1:27;
                 A56: [*n+1-k,0*] = n+1-k+0*<i> & [*k,0*] = k+0*<i> & 0 = 0+0*
<i>
                          by COMPLEX1:27;
                 then A57:             n!c /( (k!c) * ((n-'k)!c) )
                  =(n!c * [*(n+1-k),0*])/(((k!c) * ((n-'k)!c)) * (n+1-k+0*<i>))
                     by A54,XCMPLX_1:92
                 .=(n!c * [*(n+1-k),0*])/((k!c) * (((n-'k)!c) * (n+1-k+0*<i>)))
                 .=(n!c * [*(n+1-k),0*])/((k!c) * ((n+1-'k)!c))
                     by A45,SIN_COS:3;

                  n!c / (((k-'1)!c) * (((n-'(k-'1)))!c))
                   =(n!c * [*k,0*])/((k!c ) * ((n+1-'k)!c ))
                      by A42,A53,A56,XCMPLX_1:92;
                 then (Coef(n)).k +(Coef(n)).(k-'1)
                   =((n!c * [*(n+1-k),0*])+(n!c * [*k,0*]))
                      / ((k!c) * ((n+1-'k)!c)) by A51,A57,XCMPLX_1:63
             .= ( n!c * [*(n+1-k+k),0+0*] ) / ((k!c) * ((n+1-'k)!c))
                  by A55
                  .= ( n!c * (n+1-k+k+(0+0)*<i>) ) / ((k!c) * ((n+1-'k)!c))
                       by COMPLEX1:27
                  .=((n+1)!c ) / ((k!c ) * ((n+1-'k)!c )) by SIN_COS:1
                  .=(Coef(n+1)).k by A32,SIN_COS:def 10;
                 hence ((Expan(n,z,w)*w)+Shift((Expan(n,z,w)*z))).k
                   =(Coef(n+1)).k * (z #N k) * (w #N (n+1-'k) )
                      by A50,CLOPBAN3:42
                  .=Expan(n+1,z,w).k by A32,Def4;
               end;
             end;
             hence ((Expan(n,z,w)*w)+Shift((Expan(n,z,w)*z))).k
              =Expan(n+1,z,w).k by A33;
           end;
           hence ((Expan(n,z,w)*w)+Shift((Expan(n,z,w)*z))).k =Expan(n+1,z,w).k
                  by A23,A24,REAL_1:def 5;
         end;
         hence ((Expan(n,z,w)*w)+Shift((Expan(n,z,w)*z))).k =Expan(n+1,z,w).k
                by A17;
       end;
       hence thesis by FUNCT_2:113;
     end;
     thus (z+w) #N (n+1) =(Partial_Sums(( Expan(n,z,w)*w)) +
                      Partial_Sums(Shift(( Expan(n,z,w)*z)))).(n+1)
       by A8,A9,A11,A13,NORMSP_1:def 5
      .=Partial_Sums(Expan(n+1,z,w)).(n+1) by A16,CLOPBAN3:19;
   end;
    for n holds X[n] from NAT_1:sch 1(A4,A5);
   hence thesis;
end;

theorem Th17:
Expan_e(n,z,w)=(1r/(n!c)) * Expan(n,z,w)
proof
    now let k be Element of NAT;
A1:  now assume
A2:    n <k;
       hence Expan_e(n,z,w).k = 0.X by Def5
                          .=(1r/(n!c)) * 0.X by CLOPBAN3:42
                          .=(1r/(n!c)) * Expan(n,z,w).k by A2,Def4
                          .=((1r/(n!c)) * Expan(n,z,w)).k by CLVECT_1:def 15;
     end;
      now assume
A3:    k <= n;
A4:    n!c <> 0c by SIN_COS:1;
       thus
A5:    Expan_e(n,z,w).k
         = (Coef_e(n)).k * (z #N k) * (w #N (n -' k)) by A3,Def5
        .= 1r/((k!c) * ((n-'k)!c)) * (z #N k) * (w #N (n-'k))
            by A3,SIN_COS:def 11;
        1r/((k!c) * ((n-'k)!c))
         =(((n!c) * 1r)/(n!c)) /((k!c) * ((n-'k)!c)) by A4,COMPLEX1:def 7
,XCMPLX_1:60
        .= (1r/(n!c)) * (n!c) /((k!c) * ((n-'k)!c)) by XCMPLX_1:75;
       hence Expan_e(n,z,w).k
         = (1r/(n!c)) * (n!c) /((k!c) * ((n-'k)!c))
                 * ((z #N k) * (w #N (n-'k))) by A5,CLOPBAN3:42
        .= (1r/(n!c)) * ((n!c) /((k!c) * ((n-'k)!c)))
                 * ((z #N k) * (w #N (n-'k))) by XCMPLX_1:75
        .= (1r/(n!c)) * (((n!c) /((k!c) * ((n-'k)!c)))
                 * ((z #N k) * (w #N (n-'k)))) by CLOPBAN3:42
        .= (1r/(n!c)) * ((n!c) /((k!c) * ((n-'k)!c))
                 * (z #N k) * (w #N (n-'k))) by CLOPBAN3:42
        .= (1r/(n!c)) * ((Coef(n)).k * ((z #N k))
            * (w #N (n-'k))) by A3,SIN_COS:def 10
        .= (1r/(n!c)) * Expan(n,z,w).k by A3,Def4
        .= ( (1r/(n!c)) * Expan(n,z,w) ).k by CLVECT_1:def 15;
     end;
     hence Expan_e(n,z,w).k = ( (1r/(n!c)) * Expan(n,z,w) ).k by A1;
   end;
   hence Expan_e(n,z,w)=((1r/(n!c)) * Expan(n,z,w)) by FUNCT_2:113;
end;

theorem Th18:
for z,w st z,w are_commutative holds
  1r/(n!c) *((z+w) #N n)  = Partial_Sums(Expan_e(n,z,w)).n
proof
   let z,w such that
A1:z,w are_commutative;
   thus
    1r/(n!c)*((z+w) #N n)
     = 1r/(n!c)*(Partial_Sums(Expan(n,z,w)).n) by A1,Th16
    .= ((1r/(n!c)) * (Partial_Sums(Expan(n,z,w)))).n by CLVECT_1:def 15
    .= Partial_Sums((1r/(n!c)) * Expan(n,z,w)).n by CLOPBAN3:23
    .= Partial_Sums(Expan_e(n,z,w)).n by Th17;
end;

theorem Th19:
0.X ExpSeq is norm_summable & Sum(0.X ExpSeq)=1.X
proof
   defpred X[set] means Partial_Sums( ||. 0.X ExpSeq.||).$1=1;
    Partial_Sums(||.(0.X  ExpSeq).||).0
        =||. (0.X  ExpSeq) .||.0 by SERIES_1:def 1
       .=||. (0.X  ExpSeq).0 .|| by CLVECT_1:def 17
       .=||. 1.X .|| by Th13
       .=1 by CLOPBAN3:42;
then A1:X[0];
A2:for n st X[n] holds X[n+1]
   proof
     let n such that
A3:  Partial_Sums(||.0.X ExpSeq .||).n=1;
     thus Partial_Sums(||.0.X ExpSeq .||).(n+1)
         =1 + ||.0.X ExpSeq .||.(n+1) by A3,SERIES_1:def 1
        .=1 + ||.(0.X ExpSeq ).(n+1).|| by CLVECT_1:def 17
        .=1 + ||.(1r/[*(n+1),0*] * 0.X)*(0.X ExpSeq .n)  .|| by Th13
        .=1 + ||.(0.X)*(0.X ExpSeq .n)  .|| by CLOPBAN3:42
        .=1 + ||.0.X.|| by CLOPBAN3:42
        .=1 + 0 by CLOPBAN3:42
        .=1;
   end;
A4:0.X ExpSeq is norm_summable
   proof
      for n holds X[n] from NAT_1:sch 1(A1,A2);
     then Partial_Sums(||.0.X ExpSeq .||) is constant by SEQM_3:def 6;
     then Partial_Sums(||.0.X ExpSeq .||) is convergent by SEQ_4:39;
     then ||.0.X ExpSeq .|| is summable by SERIES_1:def 2;
     hence thesis by CLOPBAN3:def 4;
   end;
   defpred X[set] means Partial_Sums(0.X ExpSeq ).$1=1.X;
    Partial_Sums(0.X ExpSeq ).0
        =(0.X ExpSeq ).0 by BHSP_4:def 1
       .=1.X by Th13;
then A5:X[0];
A6:for n st X[n] holds X[n+1]
   proof
     let n such that
A7:  Partial_Sums(0.X ExpSeq ).n=1.X;
     thus Partial_Sums(0.X ExpSeq ).(n+1)
        =1.X + (0.X ExpSeq ).(n+1) by A7,BHSP_4:def 1
       .=1.X + (1r/[*(n+1),0*] *0.X)*(0.X ExpSeq .n) by Th13
       .=1.X + (0.X)*(0.X ExpSeq .n) by CLOPBAN3:42
       .=1.X + 0.X by CLOPBAN3:42
       .=1.X by CLOPBAN3:42;
   end;
    for n holds X[n] from NAT_1:sch 1(A5,A6);
   then lim(Partial_Sums(0.X ExpSeq ))=1.X by Th2;
   hence thesis by A4,CLOPBAN3:def 3;
end;

registration
  let X;
  let z be Element of X;
cluster z ExpSeq -> norm_summable;
coherence
proof
A1:ex m be Element of NAT st for n be Element of NAT st  m<=n
     holds ||.(z ExpSeq).n.|| <= (||.z.|| ExpSeq ).n
   proof
     take 0;
     thus
      for n be Element of NAT st  0<=n
       holds ||.(z ExpSeq).n.|| <= (||.z.|| ExpSeq ).n by Th13;
   end;
||.z.|| ExpSeq is summable by SIN_COS:49;
   hence z ExpSeq is norm_summable by A1,CLOPBAN3:31;
end;
end;

theorem Th20:
(z ExpSeq).0 = 1.X  & Expan(0,z,w).0 = 1.X
proof
   thus (z ExpSeq).0
     = 1r/(0!c)*(z #N 0) by Def2
    .= 1r/(0!c)*(z GeoSeq).0 by CLOPBAN3:def 13
    .= 1r * 1.X by CLOPBAN3:def 12,SIN_COS:1
    .= 1.X by CLOPBAN3:42;
A1:0-'0=0 by BINARITH:51;
   hence Expan(0,z,w).0
     = (Coef(0)).0 * (z #N 0) * (w #N 0) by Def4
    .= 1r/(1r * 1r) * z #N 0 * w #N 0 by A1,COMPLEX1:def 7,SIN_COS:1,def 10
    .= z #N 0 * w #N 0 by CLOPBAN3:42,COMPLEX1:def 7
    .= (z GeoSeq).0 * w #N 0 by CLOPBAN3:def 13
    .= (z GeoSeq).0 * (w GeoSeq).0 by CLOPBAN3:def 13
    .= 1.X  * (w GeoSeq).0 by CLOPBAN3:def 12
    .= 1.X * 1.X by CLOPBAN3:def 12
    .= 1.X by CLOPBAN3:42;
end;

theorem Th21:
l <= k implies (Alfa(k+1,z,w)).l = (Alfa(k,z,w)).l + Expan_e(k+1,z,w).l
proof
   assume
A1:l <= k;
    k < k+1 by XREAL_1:31;
then A2:l <= k+1 by A1,XXREAL_0:2;
   then (k+1-'l)=k+1-l by BINARITH:50;
then A3:(k+1-'l)=k-l+1
          .=(k-'l)+1 by A1,BINARITH:50;
then A4:(Alfa(k+1,z,w)).l
     =((z ExpSeq).l )* (Partial_Sums(w ExpSeq).((k -' l)+1)) by A2,Def6
    .=(z ExpSeq).l * (Partial_Sums(w ExpSeq).((k -' l))
       +(w ExpSeq).(((k+1)-'l))) by A3,BHSP_4:def 1
    .=((z ExpSeq).l * (Partial_Sums(w ExpSeq).((k -' l)))
       +((z ExpSeq).l * (w ExpSeq).(( (k+1) -' l)))) by CLOPBAN3:42
    .=(Alfa(k,z,w)).l+((z ExpSeq).l * (w ExpSeq).(( (k+1) -' l))) by A1,Def6;
    (z ExpSeq).l * (w ExpSeq).(((k+1)-' l))
    =1r/(l!c)*(z #N l) * (w ExpSeq).(((k+1)-' l)) by Def2
   .=1r/(l!c)*(z #N l) * (1r/(((k+1)-'l )!c)*(w #N (((k+1)-'l)))) by Def2
   .=(1r/(l!c)* (1r/(((k+1)-'l)!c))*((z #N l) *(w #N (((k+1)-'l)))))
       by CLOPBAN3:42
   .=((1r*1r)/((l!c) * (((k+1)-'l)!c) )*((z #N l) *(w #N (((k+1)-'l)))))
       by XCMPLX_1:77
   .= ((Coef_e(k+1)).l) * ((z #N l) * (w #N (((k+1)-'l)))) by A2,COMPLEX1:def 7
,SIN_COS:def 11
   .= ((Coef_e(k+1)).l) * (z #N l) * (w #N (((k+1)-'l))) by CLOPBAN3:42
   .=Expan_e(k+1,z,w).l by A2,Def5;
   hence (Alfa(k+1,z,w)).l = (Alfa(k,z,w)).l + Expan_e(k+1,z,w).l by A4;
end;

theorem Th22:
Partial_Sums((Alfa(k+1,z,w))).k
  = (Partial_Sums( Alfa(k,z,w) )).k + (Partial_Sums( Expan_e(k+1,z,w) )).k
proof
    now let l be Element of NAT;
     assume l <= k;
     hence (Alfa(k+1,z,w)).l = (Alfa(k,z,w)).l + Expan_e(k+1,z,w).l by Th21
              .= ( (Alfa(k,z,w)) + Expan_e(k+1,z,w)).l by NORMSP_1:def 5;
   end;
   hence Partial_Sums((Alfa(k+1,z,w))).k
         =Partial_Sums(((Alfa(k,z,w)) + Expan_e(k+1,z,w))).k by Th11
        .=(Partial_Sums(( Alfa(k,z,w)))
           +Partial_Sums(( Expan_e(k+1,z,w )))).k by CLOPBAN3:19
        .= (Partial_Sums(( Alfa(k,z,w)))).k
           + (Partial_Sums(( Expan_e(k+1,z,w) ))).k by NORMSP_1:def 5;
end;

theorem Th23:
(z ExpSeq).k=(Expan_e(k,z,w)).k
proof
    0 = k -k;
then A1:k-'k=0 by BINARITH:50;
   hence (Expan_e(k,z,w)).(k)=((Coef_e(k)).k) * (z #N k) * (w #N 0) by Def5
                      .=( (Coef_e(k)).k) * (z #N k) * 1.X by CLOPBAN3:43
                      .=( (Coef_e(k)).k) * (z #N k) by CLOPBAN3:42
                      .=(1r/((k!c) * 1r)) * (z #N k)
                        by A1,COMPLEX1:def 7,SIN_COS:1,def 11
                      .=(z ExpSeq).k by Def2,COMPLEX1:def 7;
end;

theorem Th24:
for z,w st z,w are_commutative holds
  Partial_Sums((z+w) ExpSeq).n = Partial_Sums(Alfa(n,z,w)).n
proof
   let z,w such that
A1: z ,w are_commutative;
A2:Partial_Sums((z+w) ExpSeq).0
     =((z+w) ExpSeq).0 by BHSP_4:def 1
    .=1.X by Th20;
   defpred X[Element of NAT] means
     Partial_Sums((z+w) ExpSeq).$1=Partial_Sums(Alfa($1,z,w)).$1;
A3:0-'0=0 by BINARITH:51;
    Partial_Sums(Alfa(0,z,w)).0
    = Alfa(0,z,w).0 by BHSP_4:def 1
   .= (z ExpSeq).0 * Partial_Sums(w ExpSeq).0 by A3,Def6
   .= (z ExpSeq).0 * (w ExpSeq).0 by BHSP_4:def 1
   .=1.X * (w ExpSeq).0 by Th20
   .=1.X * 1.X by Th20
   .= 1.X by CLOPBAN3:42;
then A4:X[0] by A2;
A5:for k st X[k] holds X[k+1]
   proof
     let k such that
A6:   Partial_Sums((z+w) ExpSeq).k=Partial_Sums(Alfa(k,z,w)).k;
     thus Partial_Sums((z+w) ExpSeq).(k+1)=Partial_Sums(Alfa(k+1,z,w)).(k+1)
     proof
A7:    Partial_Sums((Alfa(k+1,z,w))).(k+1)
        =Partial_Sums((Alfa(k+1,z,w))).k+(Alfa(k+1,z,w)).(k+1)
           by BHSP_4:def 1
       .=((Partial_Sums(Alfa(k,z,w))).k + (Partial_Sums(Expan_e(k+1,z,w))).k)
         + (Alfa(k+1,z,w)).(k+1) by Th22
       .= Partial_Sums((z+w) ExpSeq).k + ((Partial_Sums(Expan_e(k+1,z,w))).k
         + (Alfa(k+1,z,w)).(k+1)) by A6,CLOPBAN3:42;
        k+1-'(k+1)=0 by BINARITH:51;
       then (Alfa(k+1,z,w)).(k+1)
        =(z ExpSeq).(k+1) * Partial_Sums(w ExpSeq).0 by Def6
       .=(z ExpSeq).(k+1) * ((w ExpSeq).0) by BHSP_4:def 1
       .=(z ExpSeq).(k+1) * 1.X by Th20
       .=(z ExpSeq).(k+1) by CLOPBAN3:42
       .=(Expan_e(k+1,z,w)).(k+1) by Th23;
       then (Partial_Sums(( Expan_e(k+1,z,w) ))).k + (Alfa(k+1,z,w)).(k+1)
        =(Partial_Sums(( Expan_e(k+1,z,w) ))).(k+1) by BHSP_4:def 1
       .=1r/((k+1)!c) * (z+w) #N (k+1) by A1,Th18;
       hence Partial_Sums((Alfa(k+1,z,w))).(k+1)
        = Partial_Sums((z+w) ExpSeq).k + (z+w) ExpSeq.(k+1) by A7,Def2
       .= Partial_Sums((z+w) ExpSeq).(k+1) by BHSP_4:def 1;
     end;
   end;
    for n holds X[n] from NAT_1:sch 1(A4,A5);
   hence thesis;
end;

theorem Th25:
for z,w st z,w are_commutative holds
 Partial_Sums(z ExpSeq).k * Partial_Sums(w ExpSeq).k
  - Partial_Sums((z+w) ExpSeq).k
    = Partial_Sums(Conj(k,z,w)).k
proof
   let z,w such that
A1: z,w are_commutative;
A2:(Partial_Sums(z ExpSeq).k) * (Partial_Sums(w ExpSeq).k)
       -Partial_Sums((z+w) ExpSeq).k
    =Partial_Sums(z ExpSeq).k * Partial_Sums(w ExpSeq).k
       -Partial_Sums(Alfa(k,z,w)).k by A1,Th24
   .=( Partial_Sums(z ExpSeq)*  (Partial_Sums(w ExpSeq).k) ).k
       -Partial_Sums(Alfa(k,z,w)).k by CLOPBAN3:def 9
   .=(Partial_Sums((z ExpSeq)*(Partial_Sums(w ExpSeq).k))) .k
       -Partial_Sums(Alfa(k,z,w)).k by Th9
   .=(Partial_Sums( (z ExpSeq)*(Partial_Sums(w ExpSeq).k) )
       -Partial_Sums(Alfa(k,z,w))).k by NORMSP_1:def 6
   .=Partial_Sums((( (z ExpSeq)*(Partial_Sums(w ExpSeq).k)))
       -(Alfa(k,z,w))).k by CLOPBAN3:20;
    for l be Element of NAT st l <= k holds
    ((z ExpSeq)*(Partial_Sums(w ExpSeq).k) - (Alfa(k,z,w))).l =Conj(k,z,w).l
   proof
     let l be Element of NAT such that
A3:   l <= k;
     thus
      ((z ExpSeq)*( Partial_Sums(w ExpSeq).k) - (Alfa(k,z,w))).l
      =  ((z ExpSeq)*(Partial_Sums(w ExpSeq).k) ).l - (Alfa(k,z,w)).l
          by NORMSP_1:def 6
     .= ( (z ExpSeq).l )*( Partial_Sums(w ExpSeq).k) - Alfa(k,z,w).l
          by CLOPBAN3:def 9
     .=((z ExpSeq).l) * ( Partial_Sums(w ExpSeq).k)
         -((z ExpSeq).l) * ( Partial_Sums(w ExpSeq).(k-'l)) by A3,Def6
     .=((z ExpSeq).l) * ( Partial_Sums(w ExpSeq).k
         -Partial_Sums(w ExpSeq).(k-'l) ) by CLOPBAN3:42
     .=Conj(k,z,w).l by A3,Def7;
   end;
   hence thesis by A2,Th11;
end;

theorem Th26:
0 <= (||. z .|| ExpSeq).n
proof
   defpred P[Element of NAT] means 0 <= ||.z.|| |^$1;
A1:(n!) >0 by NEWTON:23;
A2:P[0] by NEWTON:9;
A3:for k st P[k] holds P[k+1]
   proof
     let k such that
A4: P[k];
A5:  ||.z.|| |^ (k+1) = ||.z.|| |^ k *  ||.z.|| by NEWTON:11;
      0 <= ||.z.|| by CLVECT_1:106;
     hence 0<= ||.z.|| |^ (k+1) by A4,A5,XREAL_1:129;
   end;
    for k holds P[k] from NAT_1:sch 1(A2,A3);
then A6:0 <= ||.z.|| |^n;
A7:||.z.|| |^n/(n!) =  ||.z.|| |^n *((n!))" by XCMPLX_0:def 9;
    0 < ((n!))" by A1,XREAL_1:124;
   then 0<= ||.z.|| |^n *((n!))" by A6,XREAL_1:129;
   hence thesis by A7,SIN_COS:def 9;
end;

theorem Th27:
||. Partial_Sums((z ExpSeq)).k .|| <= Partial_Sums(||.z.|| ExpSeq).k &
 Partial_Sums((||.z.|| ExpSeq)).k <= Sum(||.z.|| ExpSeq) &
  ||. Partial_Sums(( z ExpSeq)).k .|| <= Sum(||.z.|| ExpSeq)
proof
   defpred X[Element of NAT] means
   ||. Partial_Sums(( z ExpSeq)).$1 .|| <= Partial_Sums((||.z.|| ExpSeq)).$1;
A1:||. Partial_Sums(( z ExpSeq)).0 .||
    = ||. ((z ExpSeq)).0 .|| by BHSP_4:def 1
   .= ||. 1r/ (0!c)*(z #N 0).|| by Def2
   .= ||. 1r/(0!c)*((z GeoSeq)).0 .|| by CLOPBAN3:def 13
   .= ||. 1r * 1.X.|| by CLOPBAN3:def 12,SIN_COS:1
   .= ||. 1.X.|| by CLOPBAN3:42

   .= 1 by CLOPBAN3:42;
    Partial_Sums((||.z.|| ExpSeq)).0
    = ((||.z.|| ExpSeq)).0 by SERIES_1:def 1
   .= ((||.z.|| ) |^ 0 ) / (0!) by SIN_COS:def 9
   .= 1 by NEWTON:9,18;
then A2:X[0] by A1;
A3:for k st X[k] holds X[k+1]
   proof
     let k such that
A4:  ||. Partial_Sums(( z ExpSeq)).k .|| <= Partial_Sums((||.z.|| ExpSeq)).k;
A5:  ||. Partial_Sums(( z ExpSeq)).(k+1) .||
       =||. Partial_Sums(( z ExpSeq)).k + ((z ExpSeq)).(k+1) .||
          by BHSP_4:def 1;
A6: ||. Partial_Sums(( z ExpSeq)).k + ((z ExpSeq)).(k+1) .||
       <= ||. Partial_Sums(( z ExpSeq)).k.|| + ||. ((z ExpSeq)).(k+1) .||
          by CLOPBAN3:42;
      ||. ((z ExpSeq)).(k+1) .|| <=(||.z.|| ExpSeq).(k+1) by Th13;
     then ||. Partial_Sums(( z ExpSeq)).k.|| + ||. ((z ExpSeq)).(k+1) .||
       <= ||. Partial_Sums(( z ExpSeq)).k.|| + (||.z.|| ExpSeq).(k+1)
          by XREAL_1:9;
then A7:  ||. Partial_Sums(( z ExpSeq)).(k+1) .||
       <= ||. Partial_Sums(( z ExpSeq)).k.|| + (||.z.|| ExpSeq).(k+1)
          by A5,A6,XXREAL_0:2;
A8:  ||. Partial_Sums(( z ExpSeq)).k.|| + (||.z.|| ExpSeq).(k+1)
       <= Partial_Sums((||.z.|| ExpSeq)).k + (||.z.|| ExpSeq).(k+1)
          by A4,XREAL_1:8;
      Partial_Sums((||.z.|| ExpSeq)).k + (||.z.|| ExpSeq).(k+1)
       =Partial_Sums((||.z.|| ExpSeq)).(k+1) by SERIES_1:def 1;
     hence ||. Partial_Sums(( z ExpSeq)).(k+1) .||
       <= Partial_Sums((||.z.|| ExpSeq)).(k+1) by A7,A8,XXREAL_0:2;
   end;
A9:for k holds X[k] from NAT_1:sch 1(A2,A3);
   hence ||. Partial_Sums(( z ExpSeq)).k .||
      <= Partial_Sums((||.z.|| ExpSeq)).k;
A10:for n holds  0 <= (||.z.|| ExpSeq).n by Th26;
then A11:Partial_Sums((||.z.|| ExpSeq )) is non-decreasing by SERIES_1:19;
    ||.z.|| ExpSeq is summable by SIN_COS:49;
then A12:Partial_Sums(||.z.|| ExpSeq) is bounded_above by A10,SERIES_1:20;
   then Partial_Sums((||.z.|| ExpSeq)).k <= lim(Partial_Sums((||.z.|| ExpSeq )
))
     by A11,SEQ_4:54;
   hence Partial_Sums((||.z.|| ExpSeq)).k <= Sum(||.z.|| ExpSeq)
     by SERIES_1:def 3;
A13:now let k;
      lim(Partial_Sums((||.z.|| ExpSeq)))=Sum(||.z.|| ExpSeq)
       by SERIES_1:def 3;
     hence Partial_Sums((||.z.|| ExpSeq)).k <= Sum(||.z.|| ExpSeq)
       by A11,A12,SEQ_4:54;
   end;

A14:||. Partial_Sums(( z ExpSeq)).k .||
      <= Partial_Sums((||.z.|| ExpSeq)).k by A9;
    Partial_Sums((||.z.|| ExpSeq)).k <= Sum(||.z.|| ExpSeq) by A13;
   hence ||. Partial_Sums(( z ExpSeq)).k .|| <= Sum(||.z.|| ExpSeq)
     by A14,XXREAL_0:2;
end;

theorem Th28:
1 <= Sum(||.z.|| ExpSeq)
proof
    ||. Partial_Sums(z ExpSeq).0 .||
     =||. (z ExpSeq).0 .|| by BHSP_4:def 1
    .=||. 1.X.|| by Th13
    .=1 by CLOPBAN3:42;
   hence 1 <= Sum(||.z.|| ExpSeq) by Th27;
end;

theorem Th29:
abs((Partial_Sums(||.z.||  ExpSeq)).n) = Partial_Sums(||.z.||  ExpSeq).n &
( n <= m implies
    abs((Partial_Sums(||.z.||  ExpSeq).m-Partial_Sums(||.z.||  ExpSeq).n))
     = Partial_Sums(||.z.||  ExpSeq).m-Partial_Sums(||.z.||  ExpSeq).n )
proof
    for n holds 0 <= (||. z .|| ExpSeq).n by Th26;
   hence thesis by COMSEQ_3:9;
end;

theorem Th30:
abs(Partial_Sums(||.Conj(k,z,w).||).n)=Partial_Sums(||.Conj(k,z,w).||).n
proof
A1:now let n;
      ||.Conj(k,z,w).||.n=||.(Conj(k,z,w)).n.|| by CLVECT_1:def 17;
     hence 0 <= ||.Conj(k,z,w).||.n by CLVECT_1:106;
   end;
then A2:Partial_Sums(||.Conj(k,z,w).||) is non-decreasing by SERIES_1:19;
    0 <= n by NAT_1:2;
then A3:Partial_Sums(||.Conj(k,z,w).||).0
    <= Partial_Sums(||.Conj(k,z,w).||).n by A2,SEQM_3:12;
A4:Partial_Sums(||.Conj(k,z,w).||).0=(||.Conj(k,z,w).||).0 by SERIES_1:def 1;
    0 <= (||.Conj(k,z,w).||).0 by A1;
   hence thesis by A3,A4,ABSVALUE:def 1;
end;

theorem Th31:
for p being real number st p>0
 ex n st
  for k st n <= k holds abs(Partial_Sums(||.Conj(k,z,w).||).k) < p
proof
   let p be real number such that
A1: p>0;
   reconsider pp = p as Real by XREAL_0:def 1;
A2:1 <= Sum(||.z.|| ExpSeq) by Th28;
   then 0 * Sum(||.z.|| ExpSeq) < 4 * Sum(||.z.|| ExpSeq) by XREAL_1:70;
then A3:0/(4 * Sum(||.z.|| ExpSeq)) < p/(4 * Sum(||.z.|| ExpSeq)) by A1,
REAL_1:73;
   A4: 1 <= Sum(||.w.|| ExpSeq) by Th28;
   then 0 * Sum(||.w.|| ExpSeq) < 4 * Sum(||.w.|| ExpSeq) by XREAL_1:70;
then A5:0/(4 * Sum(||.w.|| ExpSeq)) < p/(4 * Sum(||.w.|| ExpSeq)) by A1,
REAL_1:73;
   set p1=min((pp/(4 * Sum(||.z.|| ExpSeq))),(pp/(4 * Sum(||.w.|| ExpSeq))));
A6:p1 > 0 by A3,A5,XXREAL_0:15;
A7:p1>0 & p1 <= p/(4 * Sum(||.z.|| ExpSeq)) &
    p1 <= p/(4 * Sum(||.w.|| ExpSeq)) by A3,A5,XXREAL_0:15,17;
    ( ||.z.|| ExpSeq ) is summable by SIN_COS:49;
   then Partial_Sums(( ||.z.|| ExpSeq )) is convergent by SERIES_1:def 2;
   then consider n1 such that
A8:for k,l be Element of NAT st n1 <= k & n1 <= l holds
   abs(Partial_Sums(( ||.z.|| ExpSeq )).k - Partial_Sums(( ||.z.|| ExpSeq )).l)
     < p1 by A6,COMSEQ_3:4;
    Partial_Sums(( w ExpSeq )) is convergent by CLOPBAN3:def 2;
   then for s be Real st 0<s ex n be Element of NAT
   st for m be Element of NAT st n<=m holds
    ||.Partial_Sums(( w ExpSeq )).m -Partial_Sums(( w ExpSeq )).n.||<s
      by CLOPBAN3:4;
   then Partial_Sums(( w ExpSeq )) is Cauchy_sequence_by_Norm by CLOPBAN3:5;
   then consider n2 such that
A9:for k,l be Element of NAT st n2 <= k & n2 <= l holds
     ||.Partial_Sums(( w ExpSeq )).k - Partial_Sums(( w ExpSeq )).l.||
        < p1 by A6,CSSPACE3:10;
   set n3=n1+n2;
   take n4 = n3+n3;
A10:now let n;
     let k;
      now let l be Element of NAT such that
A11:   l <= k;
A12:   ||.(z ExpSeq).l * (Partial_Sums(w ExpSeq).k
            -Partial_Sums(w ExpSeq).(k-'l)) .||
        <=||.(z ExpSeq).l.|| * ||.(Partial_Sums(w ExpSeq).k
            -Partial_Sums(w ExpSeq).(k-'l)).|| by CLOPBAN3:42;
A13:   0 <=||.(Partial_Sums(w ExpSeq).k
            -Partial_Sums(w ExpSeq).(k-'l)).|| by CLVECT_1:106;
        ||.(z ExpSeq).l.||  <= (||.z.|| ExpSeq).l by Th13;
then A14:   ||.(z ExpSeq).l.|| * ||.(Partial_Sums(w ExpSeq).k
            -Partial_Sums(w ExpSeq).(k-'l)).||
        <=(||.z.|| ExpSeq).l * ||.(Partial_Sums(w ExpSeq).k
            -Partial_Sums(w ExpSeq).(k-'l)).|| by A13,XREAL_1:66;
        ||.Conj(k,z,w).||.l
        =||.( Conj(k,z,w).l) .|| by CLVECT_1:def 17
       .=||.(z ExpSeq).l * (Partial_Sums(w ExpSeq).k
            -Partial_Sums(w ExpSeq).(k-'l)) .|| by A11,Def7;
       hence ||.Conj(k,z,w).||.l
         <=(||.z.|| ExpSeq).l * ||.(Partial_Sums(w ExpSeq).k
            -Partial_Sums(w ExpSeq).(k-'l)).|| by A12,A14,XXREAL_0:2;
     end;
     hence for l be Element of NAT st l <= k holds
       ||.Conj(k,z,w).||.l <=(||.z.|| ExpSeq).l * ||.(Partial_Sums(w ExpSeq).k
         -Partial_Sums(w ExpSeq).(k-'l)).||;
   end;

A15:now let k;
      now let l be Element of NAT;
       assume l <= k;
then A16:   ||.Conj(k,z,w).||.l
         <= (||.z.||  ExpSeq).l * ||.Partial_Sums(w ExpSeq).k
              -Partial_Sums(w ExpSeq).(k-'l).|| by A10;
A17:   ||.Partial_Sums(w ExpSeq).k -Partial_Sums(w ExpSeq).(k-'l).||
         <= ||.Partial_Sums(w ExpSeq).k .||
             + ||.Partial_Sums(w ExpSeq).(k-'l).|| by CLVECT_1:105;
        ||.Partial_Sums(w ExpSeq).k .|| <= Sum(||.w.|| ExpSeq) by Th27;
then A18:   ||.Partial_Sums(w ExpSeq).k .|| + ||.Partial_Sums(w ExpSeq).(k-'l)
.||
        <= Sum(||.w.|| ExpSeq) + ||.Partial_Sums(w ExpSeq).(k-'l).||
          by XREAL_1:8;
        ||.Partial_Sums(w ExpSeq).(k-'l).|| <= Sum(||.w.|| ExpSeq) by Th27;
then   Sum(||.w.|| ExpSeq) + ||.Partial_Sums(w ExpSeq).(k-'l).||
         <= Sum(||.w.|| ExpSeq)+ Sum(||.w.|| ExpSeq) by XREAL_1:8;
       then ||.Partial_Sums(w ExpSeq).k .|| + ||.Partial_Sums(w ExpSeq).(k-'l)
.||
         <= 2 * Sum(||.w.|| ExpSeq) by A18,XXREAL_0:2;
then A19:   ||.Partial_Sums(w ExpSeq).k -Partial_Sums(w ExpSeq).(k-'l).||
         <= 2 * Sum(||.w.|| ExpSeq) by A17,XXREAL_0:2;
        0 <= (||.z.||  ExpSeq).l by Th26;
       then (||.z.||  ExpSeq).l * ||.Partial_Sums(w ExpSeq).k
           -Partial_Sums(w ExpSeq).(k-'l).||
         <= (||.z.||  ExpSeq).l *  (2 * Sum(||.w.|| ExpSeq)) by A19,XREAL_1:66;
       hence ||.Conj(k,z,w).||.l <= (||.z.||  ExpSeq).l * (2 * Sum(||.w.||
ExpSeq))
         by A16,XXREAL_0:2;
     end;
     hence for l be Element of NAT st l <= k holds
      ||.Conj(k,z,w).||.l <= (||.z.||  ExpSeq).l * (2 * Sum(||.w.|| ExpSeq));
   end;
    now let k such that
A20: n4 <= k;
      0 <= n3 by NAT_1:2;
     then 0+n3 <= n3+n3 by XREAL_1:8;
then A21: n3 <= k by A20,XXREAL_0:2;
      0 <= n2 by NAT_1:2;
then A22: n1+0 <= n1+n2 by XREAL_1:8;
then A23: n1 <= k by A21,XXREAL_0:2;
      now let l be Element of NAT;
       assume
A24:   l <= n3;
then A25:   l <= k by A21,XXREAL_0:2;
then A26:   k-'l=k-l by BINARITH:50;
        0 <= n1 by NAT_1:2;
then A27:   0+n2 <= n1+n2 by XREAL_1:8;
A28:   n4-l <= k-l by A20,XREAL_1:11;
        n3+n3-n3 <= n3+n3-l by A24,REAL_2:106;
       then n3 <= k-l by A28,XXREAL_0:2;
then A29:   n2 <= k-'l by A26,A27,XXREAL_0:2;
        0 <= n3 by NAT_1:2;
       then 0+n3 <= n3+n3 by XREAL_1:8;
       then n2 <= n4 by A27,XXREAL_0:2;
       then n2 <= k by A20,XXREAL_0:2;
then A30:   ||.Partial_Sums(( w ExpSeq )).k - Partial_Sums(( w ExpSeq )).(k-'l)
.||
          < p1 by A9,A29;
        0 <= (||.z.||  ExpSeq).l by Th26;
then A31:    (||.z.||  ExpSeq).l * ||.Partial_Sums(( w ExpSeq )).k
          - Partial_Sums(( w ExpSeq )).(k-'l).||
        <= (||.z.||  ExpSeq).l * p1 by A30,XREAL_1:66;
        ||.Conj(k,z,w).||.l
        <=(||.z.|| ExpSeq).l * ||.(Partial_Sums(w ExpSeq).k
          -Partial_Sums(w ExpSeq).(k-'l)).|| by A10,A25;
       hence ||.Conj(k,z,w).||.l <= p1 * (||.z.||  ExpSeq).l by A31,XXREAL_0:2;
     end;
then A32: Partial_Sums(||.Conj(k,z,w).||).n3
      <= Partial_Sums(||.z.||  ExpSeq).n3 * p1 by COMSEQ_3:7;
      Partial_Sums(||.z.||  ExpSeq).n3 <= Sum(||.z.|| ExpSeq) by Th27;
     then Partial_Sums(||.z.||  ExpSeq).n3 * p1
      <= Sum(||.z.|| ExpSeq) * p1 by A6,XREAL_1:66;
then A33: Partial_Sums(||.Conj(k,z,w).||).n3
      <= Sum(||.z.|| ExpSeq) * p1 by A32,XXREAL_0:2;
A34: Sum(||.z.|| ExpSeq) * p1
     <= Sum(||.z.|| ExpSeq) * (p/(4 * Sum(||.z.|| ExpSeq))) by A2,A7,XREAL_1:66
;
A35: 0 <> Sum(||.z.|| ExpSeq) & 4 <>0 by Th28;
      Sum(||.z.|| ExpSeq) * (p/(4 * Sum(||.z.|| ExpSeq)))
       =Sum(||.z.|| ExpSeq) * (p*(4*Sum(||.z.|| ExpSeq))") by XCMPLX_0:def 9
      .=(Sum(||.z.|| ExpSeq) * p) * (4 * Sum(||.z.|| ExpSeq))"
      .=(Sum(||.z.|| ExpSeq) * p)/(4 * Sum(||.z.|| ExpSeq)) by XCMPLX_0:def 9
      .=p/4 by A35,XCMPLX_1:92;
then A36: Partial_Sums(||.Conj(k,z,w).||).n3 <= p/4 by A33,A34,XXREAL_0:2;
      0 < p/4 & 0 < p/2 by A1,SEQ_2:3;
     then 0+p/4 < p/4 + p/4 by XREAL_1:8;
then A37: Partial_Sums(||.Conj(k,z,w).||).n3 < p/2 by A36,XXREAL_0:2;
      k-'n3=k-n3 by A21,BINARITH:50;
then A38: k=(k-'n3)+n3;
      for l be Element of NAT st l <= k holds
      ||.Conj(k,z,w).||.l <= (2 * Sum(||.w.|| ExpSeq)) * (||.z.||  ExpSeq).l
        by A15;
then A39: Partial_Sums(||.Conj(k,z,w).||).(k) -Partial_Sums(||.Conj(k,z,w).||).
n3
       <= (Partial_Sums(||.z.||  ExpSeq).(k)
        -Partial_Sums(||.z.||  ExpSeq).n3) *(2 * Sum(||.w.|| ExpSeq))
          by A21,A38,COMSEQ_3:8;
      abs((Partial_Sums(||.z.||  ExpSeq).k-Partial_Sums(||.z.||  ExpSeq).n3))
      <= p1 by A8,A22,A23;
then A40: Partial_Sums(||.z.||  ExpSeq).k-Partial_Sums(||.z.||  ExpSeq).n3
      <= p1 by A21,Th29;
A41: 0 * Sum(||.w.|| ExpSeq) < 2 * Sum(||.w.|| ExpSeq) by A4,XREAL_1:70;
     then (Partial_Sums(||.z.||  ExpSeq).k -Partial_Sums(||.z.||  ExpSeq).n3)
        * (2 * Sum(||.w.|| ExpSeq))
      <= p1 * (2 * Sum(||.w.|| ExpSeq)) by A40,XREAL_1:66;
then A42: Partial_Sums(||.Conj(k,z,w).||).k-Partial_Sums(||.Conj(k,z,w).||).n3
      <= p1 * (2 * Sum(||.w.|| ExpSeq)) by A39,XXREAL_0:2;
A43: (2 * Sum(||.w.|| ExpSeq)) * p1
      <= (2 * Sum(||.w.|| ExpSeq)) * (p/(4 * Sum(||.w.|| ExpSeq)))
        by A7,A41,XREAL_1:66;
A44: 0 <> Sum(||.w.|| ExpSeq) & 4 <>0 by Th28;
      (2 * Sum(||.w.|| ExpSeq) ) * (p/(4 * Sum(||.w.|| ExpSeq)))
      =(2 * Sum(||.w.|| ExpSeq) ) * (p * (4 * Sum
         (||.w.|| ExpSeq))") by XCMPLX_0:def 9
     .=((2 * Sum(||.w.|| ExpSeq) ) * p ) * (4 * Sum(||.w.|| ExpSeq))"
     .=((2 * p ) * Sum(||.w.|| ExpSeq) ) /(4 * Sum(||.w.|| ExpSeq))
         by XCMPLX_0:def 9
     .= (2 * p )/4 by A44,XCMPLX_1:92
     .= p/2;
     then Partial_Sums(||.Conj(k,z,w).||).k -Partial_Sums(||.Conj(k,z,w).||).
n3
      <= p/2 by A42,A43,XXREAL_0:2;
     then (Partial_Sums(||.Conj(k,z,w).||).k
       -Partial_Sums(||.Conj(k,z,w).||).n3)
         +Partial_Sums(||.Conj(k,z,w).||).n3 < (p/2)+(p/2) by A37,XREAL_1:10;
     hence abs(Partial_Sums(||.Conj(k,z,w).||).k) < p by Th30;
   end;
   hence for k st n4 <= k holds abs(Partial_Sums(||.Conj(k,z,w).||).k) < p;
end;

theorem Th32:
for seq st for k holds seq.k=Partial_Sums((Conj(k,z,w))).k holds
  seq is convergent & lim(seq)=0.X
proof
   let seq such that
A1: for k holds seq.k=Partial_Sums((Conj(k,z,w))).k;
A2:now let k;
      ||.seq.k.||=||.Partial_Sums((Conj(k,z,w))).k.|| by A1;
     hence ||.seq.k.|| <= Partial_Sums(||.Conj(k,z,w).||).k by Th10;
   end;
   deffunc U(Element of NAT) = Partial_Sums(||.Conj($1,z,w).||).$1;
    ex rseq be Real_Sequence st for k holds rseq.k = U(k) from SEQ_1:sch 1;
   then consider rseq be Real_Sequence such that
A3: for k holds rseq.k=Partial_Sums(||.Conj(k,z,w).||).k;
A4:now let k;
      ||.seq.k.|| <= Partial_Sums(||.Conj(k,z,w).||).k by A2;
     hence ||.seq.k.|| <= rseq.k by A3;
   end;
    rseq is convergent & lim(rseq) =0
   proof
A5:  now let p be real number;
       assume p>0;
       then consider n such that
A6:     for k st n <= k holds abs(Partial_Sums(||.Conj(k,z,w).||).k) < p
          by Th31;
       take n;
        now let k such that
A7:      n <= k;
          abs(rseq.k-0)=abs(Partial_Sums(||.Conj(k,z,w).||).k) by A3;
         hence abs(rseq.k-0) < p by A6,A7;
       end;
       hence for k st n <= k holds abs(rseq.k-0) < p;
     end;
     hence rseq is convergent by SEQ_2:def 6;
     hence lim(rseq)=0 by A5,SEQ_2:def 7;
   end;
   hence seq is convergent & lim(seq)=0.X by A4,Th12;
end;

Lm3:
for z,w st z,w are_commutative holds
  Sum(z ExpSeq) * Sum(w ExpSeq) = Sum((z+w) ExpSeq)
proof
   let z,w such that
A1:z,w are_commutative;
   deffunc U(Element of NAT) = Partial_Sums(Conj($1,z,w)).$1;
    ex seq st
    for k holds seq.k= U(k) from FUNCT_2:sch 4;
   then consider seq be sequence of X such that
A2: for k holds seq.k=Partial_Sums(Conj(k,z,w)).k;
    now let k be Element of NAT;
     thus seq.k=Partial_Sums(Conj(k,z,w)).k by A2
         .=Partial_Sums(z ExpSeq).k * Partial_Sums(w ExpSeq).k
          -Partial_Sums((z+w) ExpSeq).k by A1,Th25
         .=(Partial_Sums(z ExpSeq) * Partial_Sums(w ExpSeq)).k
          -Partial_Sums((z+w) ExpSeq).k by CLOPBAN3:def 10
         .=(Partial_Sums(z ExpSeq) * Partial_Sums(w ExpSeq)
          -Partial_Sums((z+w) ExpSeq) ).k by NORMSP_1:def 6;
   end;
then A3:seq=Partial_Sums(z ExpSeq) * Partial_Sums(w ExpSeq)
       -Partial_Sums((z+w) ExpSeq) by FUNCT_2:113;
A4:Partial_Sums(z ExpSeq) is convergent by CLOPBAN3:def 2;
A5:Partial_Sums(w ExpSeq) is convergent by CLOPBAN3:def 2;
A6:Partial_Sums((z+w) ExpSeq) is convergent by CLOPBAN3:def 2;
A7:Partial_Sums(z ExpSeq) * Partial_Sums(w ExpSeq) is convergent &
    lim(Partial_Sums(z ExpSeq) * Partial_Sums(w ExpSeq))
     =lim(Partial_Sums(z ExpSeq)) * lim(Partial_Sums(w ExpSeq))
        by A4,A5,Th3,Th8;
A8:seq is convergent & lim(seq)=0.X by A2,Th32;
   thus Sum((z+w) ExpSeq)
     =lim(Partial_Sums((z+w) ExpSeq)) by CLOPBAN3:def 3
    .=lim(Partial_Sums(z ExpSeq)) * lim(Partial_Sums(w ExpSeq))
        by A3,A6,A7,A8,Th1
    .=Sum(z ExpSeq) * lim(Partial_Sums(w ExpSeq)) by CLOPBAN3:def 3
    .=Sum(z ExpSeq) * Sum(w ExpSeq) by CLOPBAN3:def 3;
end;

definition let X;
func exp_ X -> Function of the carrier of X, the carrier of X means :Def8:
  for z being Element of X holds it.z=Sum(z ExpSeq);
existence
proof
   deffunc U(Element of X ) = Sum($1 ExpSeq);
   consider f being Function of the carrier of X, the carrier of X such that
A1: for z be Element of X holds
   f.z = U(z) from FUNCT_2:sch 4;
   thus thesis by A1;
end;
uniqueness
proof
   let f1,f2 be Function of the carrier of X, the carrier of X;
   assume that
A2:for z be Element of X holds f1.z=Sum(z ExpSeq) and
A3:for z be Element of X holds f2.z=Sum(z ExpSeq);
    for z be Element of X holds f1.z = f2.z
   proof
     let z be Element of X;
     thus f1.z=Sum(z ExpSeq) by A2
             .=f2.z by A3;
   end;
   hence f1=f2 by FUNCT_2:113;
end;
end;

definition let X,z;
func exp z -> Element of X equals
   (exp_ X).z;
correctness;
end;

theorem
for z holds exp(z)=Sum(z ExpSeq) by Def8;

theorem Th34:
for z1,z2 st z1,z2 are_commutative holds
  exp(z1+z2)=exp(z1) *exp(z2) &
  exp(z2+z1)=exp(z2) *exp(z1) &
  exp(z1+z2)=exp(z2+z1) &
  exp(z1),exp(z2) are_commutative
proof
   let z1,z2 such that
A1:z1,z2 are_commutative;
A2:exp(z1+z2)=Sum((z1+z2) ExpSeq) by Def8
         .=Sum(z1 ExpSeq)*Sum(z2 ExpSeq) by A1,Lm3
         .=exp(z1)*Sum(z2 ExpSeq) by Def8
         .=exp(z1) *exp(z2) by Def8;
    exp(z2+z1)=Sum((z2+z1) ExpSeq) by Def8
         .=Sum(z2 ExpSeq)*Sum(z1 ExpSeq) by A1,Lm3
         .=exp(z2)*Sum(z1 ExpSeq) by Def8
         .=exp(z2) *exp(z1) by Def8;
   hence thesis by A2,LOPBAN_4:def 1;
end;

theorem
 for z1,z2 st z1,z2 are_commutative holds z1 * exp(z2)=exp(z2) * z1
proof
   let z1,z2 such that
A1:z1,z2 are_commutative;
A2:Partial_Sums( z2 ExpSeq) is convergent by CLOPBAN3:def 2;
A3:z1*(z2 ExpSeq) = (z2 ExpSeq) *z1
   proof
      now let n be Element of NAT;
       thus (z1*(z2 ExpSeq)).n
         =z1*(z2 ExpSeq).n by CLOPBAN3:def 8
        .=z1*(1r/(n!c)*(z2 #N n)) by Def2
        .=(1r/(n!c))*(z1*(z2 #N n)) by CLOPBAN3:42
        .=(1r/(n!c))*((z2 #N n)*z1) by A1,Lm2
        .=(1r/(n!c)*(z2 #N n))*z1 by CLOPBAN3:42
        .=((z2 ExpSeq).n)*z1 by Def2
        .=((z2 ExpSeq)*z1).n by CLOPBAN3:def 9;
     end;
     hence thesis by FUNCT_2:113;
   end;
   thus z1*exp(z2) =z1*Sum(z2 ExpSeq) by Def8
         .=z1*lim (Partial_Sums(z2 ExpSeq)) by CLOPBAN3:def 3
         .=lim(z1*Partial_Sums(z2 ExpSeq)) by A2,Th6
         .=lim(Partial_Sums(z1*(z2 ExpSeq))) by Th9
         .=lim(Partial_Sums(z2 ExpSeq)*z1) by A3,Th9
         .=lim(Partial_Sums(z2 ExpSeq))*z1 by A2,Th7
         .=Sum(z2 ExpSeq) *z1 by CLOPBAN3:def 3
         .=exp(z2) *z1 by Def8;
end;

theorem Th36:
exp(0.X) = 1.X
proof
    exp(0.X) =Sum( 0.X ExpSeq) by Def8
           .=1.X by Th19;
   hence thesis;
end;

theorem Th37:
exp(z)*exp(-z)= 1.X & exp(-z)*exp(z)= 1.X
proof
    z *(-z) =z *((-1r)*z) by CLOPBAN3:42
            .=(-1r)*(z*z) by CLOPBAN3:42
            .=(-1r)*z*z by CLOPBAN3:42
            .=(-z)*z by CLOPBAN3:42;
then A1:z,(-z) are_commutative by LOPBAN_4:def 1;
   hence exp(z)*exp(-z) =exp(z+(-z)) by Th34
                      .=exp(0.X) by RLVECT_1:16
                      .=1.X by Th36;
   thus exp(-z)*exp(z) =exp((-z)+z) by A1,Th34
                      .=exp(0.X) by RLVECT_1:16
                      .=1.X by Th36;
end;

theorem
 exp(z) is invertible & (exp(z))" = exp(-z) &
exp(-z) is invertible & (exp(-z))" = exp(z)
proof
A1:exp(z)*exp(-z)= 1.X & exp(-z)*exp(z)= 1.X by Th37;
   hence exp(z) is invertible by LOPBAN_3:def 8;
   hence (exp(z))" = exp(-z) by A1,LOPBAN_3:def 12;
   thus exp(-z) is invertible by A1,LOPBAN_3:def 8;
   hence (exp(-z))" = exp(z) by A1,LOPBAN_3:def 12;
end;

theorem Th39:
for z for s,t be Complex holds s*z,t*z are_commutative
proof
   let z;
   let s,t be Complex;
    (s*z) *(t*z) =(s*t)*(z*z) by CLOPBAN3:42
               .=(t*z)*(s*z) by CLOPBAN3:42;
   hence s*z,t*z are_commutative by LOPBAN_4:def 1;
end;

theorem
 for z for s,t be Complex holds
 exp(s*z)*exp(t*z) = exp((s+t)*z) &
 exp(t*z)*exp(s*z) = exp((t+s)*z) &
 exp((s+t)*z) = exp((t+s)*z) &
 exp(s*z),exp(t*z) are_commutative
proof
   let z;
   let s,t be Complex;
A1:s*z,t*z are_commutative by Th39;
   hence A2: exp(s*z)*exp(t*z) = exp(s*z+t*z) by Th34
                             .= exp((s+t)*z) by CLOPBAN3:42;
   thus A3: exp(t*z)*exp(s*z) = exp(t*z+s*z) by A1,Th34
                             .= exp((t+s)*z) by CLOPBAN3:42;
   thus exp((s+t)*z) = exp((t+s)*z);
   thus exp(s*z),exp(t*z) are_commutative by A2,A3,LOPBAN_4:def 1;
end;
