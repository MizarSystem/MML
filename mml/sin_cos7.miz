:: Formulas And Identities of Inverse Hyperbolic Functions
::  by Fuguo Ge , Xiquan Liang and Yuzhong Ding
:: 
:: Received May 24, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM, ARYTM_3, ARYTM_1, SQUARE_1, FUNCT_1, ORDINAL2, SIN_COS2,
      GROUP_1, POWER, ABSVALUE, POLYEQ_1, FUNCT_3, SIN_COS7;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XREAL_0,
      REAL_1, RELAT_1, NAT_1, SQUARE_1, PREPOWER, NEWTON, QUIN_1, COMPLEX1,
      PARTFUN1, SEQ_2, SEQM_3, SIN_COS, SEQ_1, ABSVALUE, BINOP_2, SIN_COS2,
      POWER, POLYEQ_1;
 constructors NAT_1, COMSEQ_3, REAL_1, SEQ_2, RCOMP_1, FINSEQ_4, FCONT_1,
      RFUNCT_1, FDIFF_1, SQUARE_1, PREPOWER, PARTFUN1, SIN_COS, MEMBERED,
      BINOP_2, SIN_COS2, POWER, ABSVALUE, XCMPLX_0, XREAL_0, XBOOLE_0,
      COMPLEX1, QUIN_1, POLYEQ_1, NEWTON;
 registrations XREAL_0, RELSET_1, FDIFF_1, RCOMP_1, SEQ_1, MEMBERED, NUMBERS,
      ORDINAL2, SQUARE_1, SIN_COS, SIN_COS2, ABSVALUE, QUIN_1, NEWTON,
      XCMPLX_0;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 theorems AXIOMS, REAL_1, REAL_2, SQUARE_1, ABSVALUE, SIN_COS, NEWTON,
      XCMPLX_1, QUIN_1, SIN_COS2, TAYLOR_1, POWER, JGRAPH_4, POLYEQ_1, XREAL_1,
      COMPLEX1, ASYMPT_1;

begin

reserve a,b,x,y,t,p,q for real number;

Lm1:1/1 = 1;

Lm2: number_e>0 & number_e<>1 by TAYLOR_1:11;

Lm3: x^2<1 & y<1 implies x^2*y<1
proof
  assume A1:x^2<1 & y<1;
  per cases by SQUARE_1:72;
  suppose 0=x^2;
    hence thesis;
  end;
  suppose 0<x^2;then
    y*x^2<1*x^2 by A1,REAL_1:70;
    hence thesis by A1,AXIOMS:22;
  end;
end;

theorem Th4:
  x>0 implies 1/x=x to_power (-1)
proof
  assume x>0;then
  x to_power (-1) 
  =(1/x) to_power 1 by POWER:37
 .=1/x by POWER:30;
  hence thesis;
end;

Th23:
  x>1 implies x^2>1 by SQUARE_1:59,78;

theorem Th24:
  x>1 implies ((sqrt(x^2-1))/x)^2<1
proof
   assume x>1;then
   x^2>1+0 by Th23; then
A2:x^2-1>0 by REAL_1:86;
   -1+x^2<0+x^2 by REAL_1:53;then
A3:(x^2-1)/x^2<1 by A2,REAL_2:142;
   ((sqrt(x^2-1))/x)^2=(sqrt(x^2-1))^2/x^2 by SQUARE_1:69
   .=(x^2-1)/x^2 by SQUARE_1:def 4,A2;
   hence thesis by A3;
end;

theorem Th26:
  (x/sqrt(x^2+1))^2<1
  proof
    x^2>=0 by SQUARE_1:72;then
    A2:x^2+1>=0+1 by REAL_1:55;
    A3:x^2<x^2+1 by REAL_1:69;
A6: (sqrt(x^2+1))^2=sqrt(x^2+1)*sqrt(x^2+1) by SQUARE_1:def 3
       .=sqrt((x^2+1)*(x^2+1)) by SQUARE_1:97,A2
       .=sqrt(x^2+1)^2 by SQUARE_1:def 3
       .=x^2+1 by SQUARE_1:89,A2;
A7: (x/sqrt(x^2+1))^2=(x^2)/(x^2+1) by A6,SQUARE_1:69;
    per cases by SQUARE_1:72;
      suppose x^2>0;
      hence thesis by A7,A3,REAL_2:142;
      end;
      suppose x^2=0;
     hence thesis by A7;
    end;
  end;

theorem Th27:
  sqrt(x^2+1)>0
  proof
    x^2>=0 by SQUARE_1:72;then
    x^2+1>=0+1 by REAL_1:55;
    hence thesis by SQUARE_1:93;
  end;

theorem Th28:
  sqrt(x^2+1)+x>0
proof
  A1:sqrt(x^2+1)>0 by Th27;
   per cases;
     suppose x>0;
     hence thesis by A1,REAL_1:69;
     end;
     suppose D1:x<=0;
     C1:x^2>=0 by SQUARE_1:72;
     x^2<x^2+1 by REAL_1:69;then
     sqrt(x^2)<sqrt(x^2+1) by C1,SQUARE_1:95;then
     -x<sqrt(x^2+1) by SQUARE_1:90,D1;
     hence thesis by REAL_2:112;
     end;
end;

Th29:
  1<=x implies x^2-1>=0
  proof
    assume A1:1<=x;then
    x^2>=1 by SQUARE_1:77,59;then
    1+(-1)<=x^2+(-1) by AXIOMS:24;
    hence thesis;
  end;

theorem Th31:
  y>=0 & x>=1 implies (x+1)/y>=0
proof
  assume A1: y>=0 & x>=1;then
  x+1>=1+1 by AXIOMS:24;then
  (x+1)/y>=0/y by A1,XREAL_1:74;
  hence thesis;
end;

theorem Th32:
  y>=0 & x>=1 implies (x-1)/y>=0
proof
  assume A1: y>=0 & x>=1;then
  x+(-1)>=1+(-1) by AXIOMS:24;
  hence thesis by A1,REAL_2:125;
end;

theorem Th30:
  x>=1 implies sqrt((x+1)/2)>=1
proof
  assume x>=1;then
  x+1>=1+1 by AXIOMS:24;then
  (x+1)/2>=1 by REAL_2:143;
  hence thesis by SQUARE_1:83,94;
end;

theorem Th34:
  y>=0 & x>=1 implies (x^2-1)/y>=0
proof
  assume A1:y>=0 & x>=1;then
A2:(x+1)/y>=0 by Th31;
  x^2-1>=0 by A1,Th29;
  hence thesis by A1,REAL_2:125;
end;

theorem Th33:
  x>=1 implies sqrt((x+1)/2)+sqrt((x-1)/2)>0
proof
  assume A1:x>=1;then
A2:sqrt((x+1)/2)>=1 by Th30;
  (x-1)/2>=0 by A1,Th32;then
  sqrt((x-1)/2)>=0 by SQUARE_1:94,82;then
  sqrt((x-1)/2)+1>=0+1 by AXIOMS:24;
  hence thesis by A2,AXIOMS:24;
end;

theorem Th35:
  x^2<1 implies x+1>0 & 1-x>0
proof
  assume x^2<1;then
  -1<x & x<1 by JGRAPH_4:5;
  hence thesis by JGRAPH_4:3;
end;

Th36:
  x^2<1 implies (x+1)/(1-x)>0
proof
  assume x^2<1;then
  x+1>0 & 1-x>0 by Th35;
  hence thesis by REAL_2:127;
end;

theorem Th37:
  x<>1 implies (1-x)^2>0
proof
  assume x<>1;then
  0<>1-x by XCMPLX_1:15;
  hence thesis by SQUARE_1:74;
end;

theorem Th38:
  x^2<1 implies (x^2+1)/(1-x^2)>=0
proof
  assume x^2<1;then
B1:0<1-x^2 by SQUARE_1:11;
  x^2>=0 by SQUARE_1:72;then
  x^2+1>=0+1 by AXIOMS:24;
  hence thesis by B1,REAL_2:127;
end;

theorem Th39:
  x^2<1 implies ((2*x)/(1+x^2))^2<1
proof
  assume A1:x^2<1;
   x^2>=0 by SQUARE_1:72;then
A2:4*x^2>=4*0 by AXIOMS:25;
   x^2+(-1)<1+(-1) by A1,REAL_1:67;then
   (x^2-1)*(x^2-1)>0*(x^2-1) by REAL_1:71;then
   (x^2-1)^2>0 by SQUARE_1:def 3;then
   (x^2)^2-2*(x^2)+1>0 by SQUARE_1:66;then
   (x^2)^2-2*(x^2)+1+4*x^2>0+4*x^2 by REAL_1:67;then
A3:(4*x^2)/((x^2)^2+2*(x^2)+1)<1 by A2,XREAL_1:191;
   ((2*x)/(1+x^2))^2 = (2*x)^2/(1+x^2)^2 by SQUARE_1:69
  .=2^2*x^2/(1+x^2)^2 by SQUARE_1:68
  .=2*2*x^2/(1+x^2)^2 by SQUARE_1:def 3
  .=4*x^2/((x^2)^2+2*(x^2)+1) by SQUARE_1:65;
  hence thesis by A3;
end;

Th40: 0<=x implies 1+x>0
proof
  assume A1:0<=x;then
  1+x >= 1+0 by REAL_1:55;
  hence thesis;
end;

theorem Th41:
  0<x & x<1 implies (1+x)/(1-x)>0 
proof
  assume A1:0<x & x<1;then
  x^2<x by SQUARE_1:75;then
  x^2<1 by A1,AXIOMS:22;
  hence thesis by Th36;
end;

theorem Th42:
  0<x & x<1 implies x^2<1 
proof
  assume A1:0<x & x<1;then
  x^2<x by SQUARE_1:75;
  hence thesis by A1,AXIOMS:22;
end;

Th43: 0<x & x<1 implies 0<1-x^2
proof
  assume A1:0<x & x<1;then
  x^2<x by SQUARE_1:75;then
  x^2<1 by A1,AXIOMS:22;then
  x^2-x^2<1-x^2 by REAL_1:54;
  hence thesis;
end;

theorem Th44:
  0<x & x<1 implies 1/(sqrt(1-x^2))>1 
proof
  assume A1:0<x & x<1;then
A2:0<1-x^2 by Th43;
A3:0<sqrt(1-x^2) by A2,SQUARE_1:93;
  x*x>0*x by A1,REAL_1:70;then
  0<x^2 by SQUARE_1:def 3;then
  x^2*(-1)<0*(-1) by REAL_1:71;then
  -x^2+1<0+1 by REAL_1:67;then
  sqrt(1-x^2)<1 by A2,SQUARE_1:83,95;
  hence thesis by A3,REAL_2:142;
end;

theorem Th45:
  0<x & x<1 implies (2*x)/(1-x^2)>0 
proof
  assume A1:0<x & x<1;then
A2:0*2<x*2 by REAL_1:70;
  x^2<x by A1,SQUARE_1:75;then
  x^2<1 by A1,AXIOMS:22;then
  x^2+(-x^2)<1+(-x^2) by REAL_1:67;
  hence thesis by A2,REAL_2:127;
end;

theorem Th46:
  0<x & x<1 implies 0<(1-x^2)^2 
proof
  assume A1:0<x & x<1;then
  x^2<x by SQUARE_1:75;then
  x^2<1 by A1,AXIOMS:22;then
  x^2+(-x^2)<1+(-x^2) by REAL_1:67;
  hence thesis by SQUARE_1:74;
end;

theorem Th47:
  0<x & x<1 implies ((1+x^2)/(1-x^2))>1 
proof
  assume A1:0<x & x<1;then
  x^2<x by SQUARE_1:75;then
  x^2<1 by A1,AXIOMS:22;then
A2:x^2+(-x^2)<1+(-x^2) by REAL_1:67;
  0*x<x*x by A1,REAL_1:70;then
  0<x^2 by SQUARE_1:def 3;then
  0*2<(x^2)*2 by REAL_1:70;then
  0+(-x^2)<2*(x^2)+(-x^2) by REAL_1:67;then
  -x^2+1<x^2+1 by REAL_1:67;
  hence thesis by A2,REAL_2:142;
end;

theorem Th48:
  1<x^2 implies (1/x)^2<1
  proof
    assume A1:1<x^2;then
    (1/x^2)<((x^2)/(x^2)) by REAL_1:73;then
    (1^2/x^2)<1 by A1,SQUARE_1:59,XCMPLX_1:60;
    hence thesis by SQUARE_1:69;
  end;

Th49:x>0 & x<=1 implies 1/x>=1 by Lm1,REAL_2:152;

theorem Th50:
  0<x & x<=1 implies 1-x^2>=0
  proof
    assume A1:0<x & x<=1;then
A2: x-1<=1-1 by REAL_1:49;
    per cases by A2;
    suppose x-1<0;then
    x-1+1<0+1 by REAL_1:67;
    hence thesis by A1,Th43;
    end;
    suppose x-1=0;then
    x=0+1;
    hence thesis by SQUARE_1:59;
    end;
  end;

theorem Th52:
  1<=x implies 0<x+sqrt(x^2-1)
proof
  assume A1:1<=x;then
  x^2-1>=0 by Th29;then
  0<=sqrt(x^2-1) by SQUARE_1:def 4;then
  0+x<=sqrt(x^2-1)+x by AXIOMS:24;
  hence thesis by A1;
end;

theorem Th53:
  1<=x & 1<=y implies 0<=x*sqrt(y^2-1)+y*sqrt(x^2-1)
proof
  assume A1:1<=x & 1<=y;then
  0<=x^2-1 & 0<=y^2-1 by Th29;then
  0<=sqrt(x^2-1) & 0<=sqrt(y^2-1) by SQUARE_1:def 4;then
A2:0*y<=sqrt(x^2-1)*y & 0*x<=sqrt(y^2-1)*x by A1,AXIOMS:25;then
  0+sqrt(y^2-1)*x<=sqrt(x^2-1)*y+sqrt(y^2-1)*x by AXIOMS:24;
  hence thesis by A2;
end;

theorem Th55:
  1<=x & 1<=y & abs(y)<=abs(x) implies 0<y-sqrt(y^2-1)
   proof
     assume A1:1<=x & 1<=y & abs(y)<=abs(x);then     
     1*y<=y*y by AXIOMS:25;then
     y<=y^2 by SQUARE_1:def 3;then
     1<=y^2 by A1,AXIOMS:22;then
A2:  1-1<=y^2-1 by REAL_1:92;
     -1+y^2<0+y^2 by REAL_1:67;then
     sqrt(y^2-1)<sqrt(y^2) by A2,SQUARE_1:95;then
     sqrt(y^2-1)<y by A1,SQUARE_1:89;then
     sqrt(y^2-1)-sqrt(y^2-1)<y-sqrt(y^2-1) by REAL_1:92;
     hence thesis;     
   end;

theorem Th56:
  1<=x & 1<=y & abs(y)<=abs(x) implies 0<=y*sqrt(x^2-1)-x*sqrt(y^2-1)
proof
  assume A1:1<=x & 1<=y & abs(y)<=abs(x);
  B2:(abs x)^2=x^2 & (abs y)^2=y^2 by COMPLEX1:161;
 A4:0<=x^2 & 0<=y^2 by SQUARE_1:72;
 A2:0<=x^2-1 & 0<=y^2-1 by A1,Th29;
    1*x<=x*x & 1*y<=y*y by A1,AXIOMS:25;then
    x<=x^2 & y<=y^2 by SQUARE_1:def 3;
    1<=x^2 & 1<=y^2 by A1,SQUARE_1:77,59;then
    1*(y^2-1)<=x^2*(y^2-1) by A2,AXIOMS:25;then
 A3:0<=x^2*(y^2-1) by A2;
    1<=abs(y) by A1,ABSVALUE:def 1;then
    y^2<=x^2 by A1,B2,SQUARE_1:77;then
    (-1)*x^2<=(-1)*y^2 by REAL_1:52;then
    -x^2+x^2*y^2<=-y^2+x^2*y^2 by AXIOMS:24;then
    sqrt(x^2*(y^2-1))<=sqrt(y^2*(x^2-1)) by A3,SQUARE_1:94;then
    sqrt(x^2)*sqrt(y^2-1)<=sqrt(y^2*(x^2-1)) by A2,A4,SQUARE_1:97;then
    x*sqrt(y^2-1)<=sqrt(y^2*(x^2-1)) by A1,SQUARE_1:89;then
    x*sqrt(y^2-1)<=sqrt(y^2)*sqrt(x^2-1) by A2,A4,SQUARE_1:97;then
    x*sqrt(y^2-1)<=y*sqrt(x^2-1) by A1,SQUARE_1:89;then
    x*sqrt(y^2-1)-x*sqrt(y^2-1)<=y*sqrt(x^2-1)-x*sqrt(y^2-1) by REAL_1:92;
    hence thesis;
  end;

theorem Th57:
  x^2<1 & y^2<1 implies x*y<>-1
  proof
    assume x^2<1 & y^2<1; then
B1: x^2*y^2<1 by Lm3;
    assume A3:x*y=-1;
    x*y*(x*y)=1 by A3;then
    (x*x)*(y*y)=1;then
    x^2*(y*y)=1 by SQUARE_1:def 3;
    hence contradiction by B1,SQUARE_1:def 3;
  end;

Th58:
  x^2<1 & y^2<1 implies x*y+1<>0
  proof
    assume x^2<1 & y^2<1;then
 A1:x*y<>-1 by Th57;
    assume x*y+1=0;then
    (x*y)+1-1=-1;
    hence contradiction by A1,XCMPLX_1:26;
  end;

theorem Th61:
  x^2<1 & y^2<1 implies x*y<>1
  proof
    assume x^2<1 & y^2<1;then
B1: x^2*y^2<1 by Lm3;
    assume x*y=1;then
    x*y*(x*y)=1;then
    (x*x)*(y*y)=1;then
    x^2*(y*y)=1 by SQUARE_1:def 3;
    hence contradiction by B1,SQUARE_1:def 3;
  end;

Th62:
  x^2<1 & y^2<1 implies 1-x*y<>0
  proof
    assume x^2<1 & y^2<1;then
  A1:x*y<>1 by Th61;
    assume 1-x*y=0;then
    1-x*y+x*y=0+x*y;
    hence contradiction by A1,XCMPLX_1:27;
  end;

theorem Th63:
  x<>0 implies exp(x)<>1
  proof
    assume A1:x<>0;
    assume A2:exp(x)=1;
    x=log(number_e,exp(x)) by TAYLOR_1:12
    .=0 by Lm2,A2,POWER:59;
    hence contradiction by A1;
  end;

theorem Th64:
  0<>x implies (exp x)^2-1<>0
  proof
    assume 0<>x;then
 A2:exp x<>1 by Th63;
    assume (exp x)^2-1=0;then
    (exp x)^2-1+1=0+1;then
    (exp x)^2=1 by XCMPLX_1:27;then
    (exp x)*(exp x)=1 by SQUARE_1:def 3;then
    exp x=1 or exp x= -1 by XCMPLX_1:183;
    hence contradiction by A2,SIN_COS:60;
  end;

Lm16:x^2+1>0
  proof
    x^2>=0 by SQUARE_1:72;then
    x^2+1>=0+1 by REAL_1:55;
    hence thesis;
  end;

theorem 
  Th65: 0<t implies (t^2-1)/(t^2+1)<1
  proof
    B1:0<t^2+1 by Lm16;
    assume 0<t;
    -1+t^2<1+t^2 by REAL_1:67;then
    (-1+t^2)/((1+t^2))<(1+t^2)/((1+t^2)) by B1,REAL_1:73;
    hence thesis by XCMPLX_1:60,B1;
end;

theorem 
  Th66:-1<t & t<1 implies 0<(t+1)/(1-t)
proof
  assume -1<t & t<1;then
  -1+1<t+1 & t-t<1-t by REAL_1:67,92;then
  0/(1-t)<(t+1)/(1-t) by REAL_1:73;
  hence thesis;
end;

Lm30:1<t or t< -1 implies 0<(t+1)/(t-1)
   proof
     assume A1:1<t or t < -1;
       per cases by A1;
        suppose A2:1<t;then
A3:     1+1<t+1 by REAL_1:67;
          1-1<t-1 by A2,REAL_1:92;then
          0/(t-1)<(t+1)/(t-1) by A3,REAL_1:73;
          hence thesis;
        end;
        suppose A4:t< -1;then
A5:     t+1< -1+1 by REAL_1:67;
        t-1< -1-1 by A4,REAL_1:92;then
        0/(t-1)<(t+1)/(t-1) by A5,REAL_1:74;
        hence thesis;
      end;
   end;

Lm22:sqrt(x^2+1)>x
  proof
 A2:x^2>=0 by SQUARE_1:72;
    per cases;
    suppose A1:x>=0;
      x^2+1>x^2+0 by REAL_1:67;then
      sqrt(x^2+1)>sqrt(x^2) by A2,SQUARE_1:95;
      hence thesis by A1,SQUARE_1:89;
    end;
    suppose x<0;
      hence thesis by Th27;
    end;
  end;

Lm24:sqrt(x^2+1)*sqrt(y^2+1)-x*y>0
proof
  A1:x^2+1>0 & y^2+1>0 & sqrt(x^2+1)>0 & sqrt(y^2+1)>0 by Th27,Lm16;then
   A2:(sqrt(x^2+1))*(sqrt(y^2+1))>0*sqrt(y^2+1) by REAL_1:70;
   A3:0<=x^2 & 0<=y^2by SQUARE_1:72;then
      0+y^2<=x^2+y^2 by AXIOMS:24;then
A4:   0+1<=x^2+y^2+1 by A3,AXIOMS:24;
      assume (sqrt(x^2+1)*sqrt(y^2+1))-x*y<=0;then
      (sqrt(x^2+1)*sqrt(y^2+1))+(-x*y)<=0;then
      sqrt(x^2+1)*sqrt(y^2+1)<=x*y by REAL_2:105;then
      (sqrt(x^2+1)*sqrt(y^2+1))^2<=(x*y)^2 by A2,SQUARE_1:77;then
      (sqrt(x^2+1))^2*(sqrt(y^2+1))^2<=(x*y)^2 by SQUARE_1:68;then
      (x^2+1)*(sqrt(y^2+1))^2<=(x*y)^2 by A1,SQUARE_1:def 4;then
      (x^2+1)*(y^2+1)<=(x*y)^2 by A1,SQUARE_1:def 4;then
      x^2*y^2+x^2+y^2+1<=(x*y)^2 ;then
      (x*y)^2+x^2+y^2+1<=(x*y)^2 by SQUARE_1:68;then
      (x*y)^2+x^2+y^2+1-(x*y)^2<=(x*y)^2-(x*y)^2 by REAL_1:92;
     hence contradiction by A4;
end;

Lm25:1<=y implies y+sqrt(y^2-1)>0
  proof
    assume A1:1<=y;then
    0<=y^2-1 by Th29;then
    0<=sqrt(y^2-1) by SQUARE_1:def 4;then
    0+y<=sqrt(y^2-1)+y by REAL_1:55;
    hence thesis by A1;
  end;

Lm26: 0<t implies -1<(t^2-1)/(t^2+1)
  proof
    B1:0<t^2+1 by Lm16;
    assume 0<t;then
    0<t^2 by SQUARE_1:74;then
    0*2<2*t^2 by REAL_1:70;then
    0-(t^2+1)<2*t^2-(t^2+1) by REAL_1:92;then
    (-(t^2+1))/(t^2+1)<(t^2-1)/(t^2+1) by B1,REAL_1:73;then
    -(t^2+1)/(t^2+1)<(t^2-1)/(t^2+1) by XCMPLX_1:188;
    hence thesis by B1,XCMPLX_1:60;
end;

Lm29:0<t & t<>1 implies 1<(t^2+1)/(t^2-1) or (t^2+1)/(t^2-1)< -1
     proof
       assume A1:0<t & t<>1;then
       1<t or t<1 by AXIOMS:21;then
A3:    1-1<t-1 or t-1<1-1 by REAL_1:92;
       per cases by A3;
        suppose 0<t-1;then
     A4:0+1<t-1+1 by REAL_1:67;then
        t<t^2 by SQUARE_1:76;then
        1<t^2 by A4,AXIOMS:22;then
A6:     1-1<t^2-1 by REAL_1:92;
        -1+t^2<1+t^2 by REAL_1:67;
        hence thesis by A6,REAL_2:142;
       end;
       suppose t-1<0;then
       A9:t-1+1<0+1 by REAL_1:67;then
          t^2<t by A1,SQUARE_1:75;then
          t^2<1 by A9,AXIOMS:22;then
A10:      t^2-t^2<1-t^2 by REAL_1:92;
          0<t^2 by A1,SQUARE_1:74;then
          0*2<2*t^2 by REAL_1:70;then
          0-t^2<2*t^2-t^2 by REAL_1:92;then
          -t^2+1<t^2+1 by REAL_1:67;then
          1<(t^2+1)/(1-t^2) by A10,REAL_2:142;then
          (-1)*((t^2+1)/(1-t^2))<1*(-1) by REAL_1:71;then
          ((-1)*(t^2+1))/(1-t^2)<1*(-1) by XCMPLX_1:75;then
          (-(t^2+1))/(1-t^2)<1*(-1);then
          (t^2+1)/(-(1-t^2))<1*(-1) by XCMPLX_1:193;
         hence thesis;
        end;
     end;

Lm31:0<t implies 0<2*t/(1+t^2)
proof
   assume 0<t;then
A2:0*2<2*t by REAL_1:70;
   t^2+1>0 by Lm16;then
   0/(t^2+1)<2*t/(t^2+1) by A2,REAL_1:73;
   hence thesis;   
end;

Lm32:0<t implies 2*t/(1+t^2)<=1
proof
   assume 0<t;then
A2:0*2<2*t by REAL_1:70;
   0<=(t-1)^2 by SQUARE_1:72;then
   0<=t^2-2*t+1 by SQUARE_1:66;then
   0+2*t<=t^2-2*t+1+2*t by AXIOMS:24;
   hence thesis by A2,REAL_2:143;
end;

Lm33:0<t implies 0<(1+sqrt(1+t^2))/t
proof
   assume A1:0<t;
   0<sqrt(t^2+1) by Th27;then
   0+1<(sqrt(1+t^2))+1 by REAL_1:67;then
   0/t<(1+sqrt(1+t^2))/t by A1,REAL_1:73;
   hence thesis;
end;

Lm34:0<t implies (1-sqrt(1+t^2))/t<0
proof
  assume A1:0<t;then
  0<t^2 by SQUARE_1:74;then
  0+1<t^2+1 by REAL_1:67;then
  1<sqrt(t^2+1) by SQUARE_1:83,95;then
  -sqrt(t^2+1)< -1 by REAL_1:50;then
  -sqrt(t^2+1)+1< -1+1 by REAL_1:67;then
  (1-sqrt(t^2+1))/t<0/t by A1,REAL_1:73;
  hence thesis;
end;

Lm35:0<t & t<=1 implies 0<=1-t^2 
proof
  assume A1:0<t & t<=1;
  t^2<=1 by A1,SQUARE_1:59,77;then
  t^2-t^2<=1-t^2 by REAL_1:92;
  hence thesis;
end;

Lm36:0<t & t<=1 implies 0<=4-4*t^2 
proof
  assume 0<t & t<=1;then
  0<=1-t^2 by Lm35;then
  0*4<=4*(1-t^2) by AXIOMS:25;
  hence thesis;
end;

Lm37:0<t & t<=1 implies 0<1+sqrt(1-t^2)
proof
  assume 0<t & t<=1;then
  0<=1-t^2 by Lm35;then
  0<=sqrt(1-t^2) by SQUARE_1:82,94;then
  0+1<=sqrt(1-t^2)+1 by AXIOMS:24;
  hence thesis;
end;

Lm38:0<t & t<=1 implies 0<(1+sqrt(1-t^2))/t
proof
  assume A1:0<t & t<=1;
  0<1+sqrt(1-t^2) by A1,Lm37;then
  0/t<(1+sqrt(1-t^2))/t by A1,REAL_1:73;
  hence thesis;
end;

Lm39: 0<t & t<>1 implies 2*t/(t^2-1)<>0
proof
  assume A1:0<t & t<>1;then
  A2:1<t or t<1 by AXIOMS:21;
  per cases by A2;
    suppose A3:1<t;then
     t<t^2 by SQUARE_1:76;then
     1<t^2 by A3,AXIOMS:22;then
A4:  1-1<t^2-1 by REAL_1:92;
     2*1<2*t by A3,REAL_1:70;then
     0/(t^2-1)<2*t/(t^2-1) by A4,REAL_1:73;
     hence thesis;
    end;
    suppose A5:t<1;then
     t^2<t by A1,SQUARE_1:75;then
     t^2<1 by A5,AXIOMS:22;then
A6:  t^2-1<1-1 by REAL_1:92;
     0*2<2*t by A1,REAL_1:70;then
     2*t/(t^2-1)<0/(t^2-1) by A6,REAL_1:74;
     hence thesis;
    end;
end;

Lm40:t<0 implies (1+sqrt(1+t^2))/t<0
proof
  assume A1:t<0;
  0<sqrt(t^2+1) by Th27;then
  0+1<(sqrt(1+t^2))+1 by REAL_1:67;then
  (1+sqrt(1+t^2))/t<0/t by A1,REAL_1:74;
  hence thesis;
end;

Lm41:t<0 implies 0<(1-sqrt(1+t^2))/t
proof
  assume A1:t<0;then
 0<t^2 by SQUARE_1:74;then
       0+1<t^2+1 by REAL_1:67;then
       1<sqrt(t^2+1) by SQUARE_1:83,95;then
       -sqrt(t^2+1)< -1 by REAL_1:50;then
       -sqrt(t^2+1)+1< -1+1 by REAL_1:67;then
       0/t<(1-sqrt(t^2+1))/t by A1,REAL_1:74;
       hence thesis;
     end;

begin :: Main Part

definition 
  let x be real number;
  func sinh"(x) -> real number equals :Def1:
  log(number_e,(x+sqrt(x^2+1))); 
  coherence;
end;

definition
  let x be real number;
  func cosh1"(x) -> real number equals :Def21:
  log(number_e,x+sqrt(x^2-1));
  coherence;
end;

definition
  let x be real number;
  func cosh2"(x) -> real number equals :Def22:
  -(log(number_e,x+sqrt(x^2-1)));
  coherence;
end;
  
definition
  let x be real number;
  func tanh"(x) -> real number equals :Def3:
  (1/2)*log(number_e,(1+x)/(1-x));
  coherence;
end;

definition
  let x be real number;
  func coth"(x) -> real number equals :Def4:
  (1/2)*log(number_e,(x+1)/(x-1));
  coherence;
end;

definition
  let x be real number;                  
  func sech1"(x) -> real number equals :Def51:
  log(number_e,(1+sqrt(1-x^2))/x);
  coherence;
end;

definition
  let x be real number;                   
  func sech2"(x) -> real number equals :Def52:
  -(log(number_e,(1+sqrt(1-x^2))/x));
  coherence;
end;

definition
  let x be real number;
  func csch"(x) -> real number equals :Def6:
  log(number_e,(1+sqrt(1+x^2))/x) if 0<x,
  log(number_e,(1-sqrt(1+x^2))/x) if x<0;
  correctness;
end;

theorem 
  0<=x implies sinh"(x)=cosh1"(sqrt(x^2+1))    
  proof
    x^2>=0 by SQUARE_1:72;then
    A2:x^2+1>=0+1 by REAL_1:55;
     assume A5:0<=x;
     cosh1"(sqrt(x^2+1))
    =log(number_e,sqrt(x^2+1)+sqrt((sqrt(x^2+1))^2-1)) by Def21
   .=log(number_e,sqrt(x^2+1)+sqrt(x^2+1-1)) by A2,SQUARE_1:def 4
   .=log(number_e,(sqrt(x^2+1)+x)) by A5,SQUARE_1:89;
   hence thesis by Def1;   
  end;

theorem 
  x<0 implies sinh"(x)=cosh2"(sqrt(x^2+1)) 
  proof
    assume A5:x<0;
    AA:sqrt(x^2+1)+x>0 by Th28;then
    A6:1/(sqrt(x^2+1)+x)=(sqrt(x^2+1)+x) to_power (-1) by Th4;
       x^2>=0 by SQUARE_1:72;then
    A2:x^2+1>=0+1 by REAL_1:55;
     cosh2"(sqrt(x^2+1))
    =-log(number_e,sqrt(x^2+1)+sqrt((sqrt(x^2+1))^2-1)) by Def22
   .=-log(number_e,sqrt(x^2+1)+sqrt(x^2+1-1)) by A2,SQUARE_1:def 4
   .=-log(number_e,(sqrt(x^2+1)+(-x))) by A5,SQUARE_1:90
   .=-log(number_e,(sqrt(x^2+1)-x)*(sqrt(x^2+1)+x)/(sqrt(x^2+1)+x))
     by AA,XCMPLX_1:90
   .=-log(number_e,((sqrt(x^2+1))^2-x^2)/(sqrt(x^2+1)+x)) 
     by SQUARE_1:67
   .=-log(number_e,((x^2+1)-x^2)/(sqrt(x^2+1)+x)) by A2,SQUARE_1:def 4
   .=-((-1)*log(number_e,(sqrt(x^2+1)+x))) by Lm2,AA,POWER:63,A6
   .=log(number_e,(sqrt(x^2+1)+x));
   hence thesis by Def1;           
  end;

theorem
  sinh"(x)=tanh"(x/sqrt(x^2+1))
proof
  AAA:(x/sqrt(x^2+1))^2<1 & sqrt(x^2+1)>0 & sqrt(x^2+1)+x>0 &
  x^2+1>0 by Th26,Th27,Th28,Lm16;
  set t = sqrt(x^2+1)+x;
  tanh"(x/sqrt(x^2+1))
  =(1/2)*log(number_e,(1+x/sqrt(x^2+1))/(1-x/sqrt(x^2+1))) by Def3
 .=(1/2)*log(number_e,((sqrt(x^2+1)*1+x)/sqrt(x^2+1))/(1-x/sqrt(x^2+1)))
     by XCMPLX_1:114,AAA
 .=(1/2)*log(number_e,((sqrt(x^2+1)*1+x)/sqrt(x^2+1))/((1*sqrt(x^2+1)-x)
    /sqrt(x^2+1))) by XCMPLX_1:128,AAA
 .=(1/2)*log(number_e,t/(sqrt(x^2+1)-x)) by XCMPLX_1:55,AAA
 .=(1/2)*log(number_e,t*t/((sqrt(x^2+1)-x)*t)) by XCMPLX_1:92,AAA
 .=(1/2)*log(number_e,(t*t)/((sqrt(x^2+1))^2-x^2)) by SQUARE_1:67
 .=(1/2)*log(number_e,(t*t)/((sqrt(x^2+1))*(sqrt(x^2+1))-x^2))
     by SQUARE_1:def 3
 .=(1/2)*log(number_e,(t*t)
   /((sqrt((x^2+1)*(x^2+1)))-x^2)) by SQUARE_1:97,AAA
 .=(1/2)*log(number_e,(t*t)/((sqrt((x^2+1)^2))-x^2)) by SQUARE_1:def 3
 .=(1/2)*log(number_e,(t*t)/((x^2+1)-x^2)) by SQUARE_1:89,AAA
 .=(1/2)*log(number_e,t^2) by SQUARE_1:def 3
 .=(1/2)*log(number_e,t to_power 2) by POWER:53
 .=(1/2)*(2*log(number_e,t)) by POWER:63,AAA,Lm2
 .=log(number_e,t);
 hence thesis by Def1;
end;

theorem
  x>=1 implies cosh1"(x)=sinh"(sqrt(x^2-1))
  proof
     assume A2:x>=1;then
     AA:x^2-1>=0 by Th29;
     sinh"(sqrt(x^2-1))
    =log(number_e,((sqrt(x^2-1))+sqrt((sqrt(x^2-1))^2+1))) by Def1
   .=log(number_e,((sqrt(x^2-1))+sqrt((x^2-1)+1))) by AA,SQUARE_1:def 4
   .=log(number_e,((sqrt(x^2-1))+x)) by A2,SQUARE_1:89;
   hence thesis by Def21;     
  end;

theorem
  x>1 implies cosh1"(x)=tanh"((sqrt(x^2-1))/x)
     proof
        assume MM:x>1;then
        x^2>1+0 by Th23;then
LL:     x^2-1>0 by REAL_1:86;then
LM:     sqrt(x^2-1)>0 & ((sqrt(x^2-1))/x)^2<1 & sqrt(x^2-1)+x>0
          by MM,Th24,Th52,SQUARE_1:93;
        tanh"((sqrt(x^2-1))/x)
       =(1/2)*log(number_e,(1+((sqrt(x^2-1))/x))/(1-((sqrt(x^2-1))/x)))
         by Def3
      .=(1/2)*log(number_e,((sqrt(x^2-1)+x*1)/x)/(1-(sqrt(x^2-1))/x))
         by MM,XCMPLX_1:114
      .=(1/2)*log(number_e,((sqrt(x^2-1)+x)/x)/((1*x-(sqrt(x^2-1)))/x))
         by MM,XCMPLX_1:128
      .=(1/2)*log(number_e,(sqrt(x^2-1)+x)/(x-sqrt(x^2-1))) by MM,XCMPLX_1:55
      .=(1/2)*log(number_e,((sqrt(x^2-1)+x)*(sqrt(x^2-1)+x))/
        ((x-sqrt(x^2-1))*(sqrt(x^2-1)+x))) by LM,XCMPLX_1:92
      .=(1/2)*log(number_e,((sqrt(x^2-1)+x)*(sqrt(x^2-1)+x))/
        (x^2-(sqrt(x^2-1))^2)) by SQUARE_1:67
      .=(1/2)*log(number_e,((sqrt(x^2-1)+x)*(sqrt(x^2-1)+x))/
        (x^2-(x^2-1))) by LL,SQUARE_1:def 4
      .=(1/2)*log(number_e,((sqrt(x^2-1)+x)^2)) by SQUARE_1:def 3
      .=(1/2)*log(number_e,((sqrt(x^2-1)+x) to_power 2)) by POWER:53
      .=(1/2)*(2*log(number_e,(sqrt(x^2-1)+x))) by LM,POWER:63,Lm2
      .=log(number_e,(sqrt(x^2-1)+x));
      hence thesis by Def21;
    end;

theorem 
  x>=1 implies cosh1"(x)=2*cosh1"(sqrt((x+1)/2))
   proof
     assume x>=1;then
    LL:sqrt((x+1)/2)>=1 & (x+1)/2>=0 & (x-1)/2>=0 &
       sqrt((x+1)/2)+sqrt((x-1)/2)>0 & (x^2-1)/4>=0 by Th30,Th31,Th32,
       Th33,Th34;
     2*cosh1"(sqrt((x+1)/2))
    =2*(log(number_e,(sqrt((x+1)/2))+sqrt((sqrt((x+1)/2))^2-1))) 
      by Def21 
   .=2*(log(number_e,(sqrt((x+1)/2))+sqrt(((x+1)/2)-1))) 
     by LL,SQUARE_1:def 4
   .=log(number_e,((sqrt((x+1)/2))+sqrt((x-1)/2)) to_power 2)
      by LL,Lm2,POWER:63
   .=log(number_e,((sqrt((x+1)/2))+sqrt((x-1)/2))^2) by POWER:53
   .=log(number_e,(sqrt((x+1)/2))^2+2*(sqrt((x+1)/2))*
      (sqrt((x-1)/2))+(sqrt((x-1)/2))^2) by SQUARE_1:63
   .=log(number_e,((x+1)/2)+2*(sqrt((x+1)/2))*
      (sqrt((x-1)/2))+(sqrt((x-1)/2))^2) by LL,SQUARE_1:def 4
   .=log(number_e,(x+1)/2+2*sqrt((x+1)/2)*
      sqrt((x-1)/2)+(x-1)/2) by LL,SQUARE_1:def 4
   .=log(number_e,x+2*(sqrt((x+1)/2)*sqrt((x-1)/2)))
   .=log(number_e,x+2*(sqrt(((x+1)/2)*((x-1)/2)))) by LL,SQUARE_1:97
   .=log(number_e,x+2*(sqrt(((x+1)*(x-1))/(2*2)))) by XCMPLX_1:77
   .=log(number_e,x+2*(sqrt((x^2-1)/4))) by SQUARE_1:59,67
   .=log(number_e,x+sqrt(2^2)*(sqrt((x^2-1)/4))) by SQUARE_1:89
   .=log(number_e,x+sqrt(2*2)*(sqrt((x^2-1)/4))) by SQUARE_1:def 3
   .=log(number_e,x+sqrt(4*((x^2-1)/4))) by LL,SQUARE_1:97
   .=log(number_e,x+sqrt(x^2-1));
   hence thesis by Def21;
  end;

theorem
  x>=1 implies cosh2"(x)=2*cosh2"(sqrt((x+1)/2))
  proof
    assume x>=1;then
    LL:sqrt((x+1)/2)>=1 & (x+1)/2>=0 & (x-1)/2>=0 &
       sqrt((x+1)/2)+sqrt((x-1)/2)>0 & (x^2-1)/4>=0 by Th30,Th31,Th32,
       Th33,Th34;
     2*cosh2"(sqrt((x+1)/2))
    =2*(-(log(number_e,(sqrt((x+1)/2))+sqrt((sqrt((x+1)/2))^2-1)))) 
      by Def22 
   .=2*(-(log(number_e,(sqrt((x+1)/2))+sqrt(((x+1)/2)-1))))
      by LL,SQUARE_1:def 4
   .=2*(-(log(number_e,(sqrt((x+1)/2))+sqrt((x-1)/2))))
   .=-(2*(log(number_e,(sqrt((x+1)/2))+sqrt((x-1)/2))))
   .=-(log(number_e,((sqrt((x+1)/2))+sqrt((x-1)/2)) to_power 2)) 
      by LL,Lm2,POWER:63
   .=-log(number_e,((sqrt((x+1)/2))+sqrt((x-1)/2))^2) by POWER:53
   .=-log(number_e,(sqrt((x+1)/2))^2+2*(sqrt((x+1)/2))*
      (sqrt((x-1)/2))+(sqrt((x-1)/2))^2) by SQUARE_1:63
   .=-log(number_e,((x+1)/2)+2*(sqrt((x+1)/2))*
      (sqrt((x-1)/2))+(sqrt((x-1)/2))^2) by LL,SQUARE_1:def 4
   .=-log(number_e,(x+1)/2+2*sqrt((x+1)/2)*
      sqrt((x-1)/2)+(x-1)/2) by LL,SQUARE_1:def 4
   .=-log(number_e,(x+2*(sqrt((x+1)/2)*sqrt((x-1)/2))))
   .=-log(number_e,(x+2*(sqrt(((x+1)/2)*((x-1)/2)))))
      by LL,SQUARE_1:97
   .=-log(number_e,(x+2*(sqrt(((x+1)*(x-1))/(2*2))))) by XCMPLX_1:77
   .=-log(number_e,(x+2*(sqrt((x^2-1)/4)))) by SQUARE_1:59,67
   .=-log(number_e,(x+sqrt(2^2)*(sqrt((x^2-1)/4)))) by SQUARE_1:89
   .=-log(number_e,(x+sqrt(2*2)*(sqrt((x^2-1)/4)))) by SQUARE_1:def 3
   .=-log(number_e,(x+sqrt(4*((x^2-1)/4)))) by LL,SQUARE_1:97
   .=-log(number_e,(x+sqrt(x^2-1)));
   hence thesis by Def22;
  end;

theorem
  x>=1 implies cosh1"(x)=2*sinh"(sqrt((x-1)/2))
   proof
     assume x>=1;then 
     A2:sqrt((x+1)/2)>=1 & (x+1)/2>=0 & (x-1)/2>=0 &
        sqrt((x+1)/2)+sqrt((x-1)/2)>0 & (x^2-1)/4>=0 by Th30,Th31,Th32,
       Th33,Th34;
     2*sinh"(sqrt((x-1)/2))
    =2*log(number_e,((sqrt((x-1)/2))+sqrt((sqrt((x-1)/2))^2+1))) by Def1
   .=2*log(number_e,((sqrt((x-1)/2))+sqrt(((x-1)/2)+1))) 
     by A2,SQUARE_1:def 4
   .=log(number_e,(sqrt((x-1)/2)+sqrt((x+1)/2)) to_power 2)
     by A2,Lm2,POWER:63 
   .=log(number_e,(sqrt((x-1)/2)+sqrt((x+1)/2))^2) by POWER:53
   .=log(number_e,(sqrt((x-1)/2))^2+2*(sqrt((x-1)/2))
     *(sqrt((x+1)/2))+(sqrt((x+1)/2))^2) by SQUARE_1:63
   .=log(number_e,((x-1)/2)+2*(sqrt((x-1)/2))
     *(sqrt((x+1)/2))+(sqrt((x+1)/2))^2) by A2,SQUARE_1:def 4
   .=log(number_e,(x-1)/2+2*(sqrt((x-1)/2))
     *(sqrt((x+1)/2))+(x+1)/2) by A2,SQUARE_1:def 4
   .=log(number_e,x+2*((sqrt((x-1)/2))*(sqrt((x+1)/2))))
   .=log(number_e,x+2*(sqrt(((x-1)/2)*((x+1)/2)))) by A2,SQUARE_1:97
   .=log(number_e,x+2*(sqrt(((x-1)*(x+1))/(2*2)))) by XCMPLX_1:77
   .=log(number_e,x+2*(sqrt((x^2-1^2)/4))) by SQUARE_1:67
   .=log(number_e,x+sqrt(2^2)*(sqrt((x^2-1)/4))) by SQUARE_1:59,89
   .=log(number_e,x+sqrt(2*2)*(sqrt((x^2-1)/4))) by SQUARE_1:def 3
   .=log(number_e,x+sqrt(4*((x^2-1)/4))) by A2,SQUARE_1:97
   .=log(number_e,x+sqrt(x^2-1));
   hence thesis by Def21;
  end;

theorem
  x^2<1 implies tanh"(x)=sinh"(x/sqrt(1-x^2))
   proof
   assume x^2<1;then
BB:1-x^2>0 & x+1>0 & 1-x>0 & (x+1)/(1-x)>0 by SQUARE_1:11,Th35,
   Th36;then
B1:sqrt(x+1)>0 by SQUARE_1:93;
DD:sqrt((x+1)/(1-x))=((x+1)/(1-x)) to_power (1/2) by BB,ASYMPT_1:89;
    sinh"(x/sqrt(1-x^2))
   =log(number_e,((x/sqrt(1-x^2))+sqrt((x/sqrt(1-x^2))^2+1))) by Def1
  .=log(number_e,((x/sqrt(1-x^2))+sqrt((x^2/(sqrt(1-x^2))^2)+1)))
     by SQUARE_1:69
  .=log(number_e,((x/sqrt(1-x^2))+sqrt((x^2/(1-x^2))+1)))
     by BB,SQUARE_1:def 4
  .=log(number_e,((x/sqrt(1-x^2))+sqrt((x^2+(1-x^2)*1)/(1-x^2)))) 
    by BB,XCMPLX_1:114
  .=log(number_e,((x/sqrt(1-x^2))+sqrt(1)/sqrt(1-x^2)))
     by BB,SQUARE_1:99
  .=log(number_e,((x+1)/sqrt(1-x^2))) by XCMPLX_1:63,SQUARE_1:83
  .=log(number_e,((x+1)/sqrt((1-x)*(1+x)))) by SQUARE_1:59,67
  .=log(number_e,(sqrt((x+1)^2)/sqrt((1-x)*(1+x)))) by SQUARE_1:89,BB
  .=log(number_e,(sqrt((x+1)*(x+1))/sqrt((1-x)*(1+x)))) by SQUARE_1:def 3
  .=log(number_e,((sqrt(x+1)*sqrt(x+1))/sqrt((1-x)*(1+x)))) 
    by SQUARE_1:97,BB
  .=log(number_e,((sqrt(x+1)*sqrt(x+1))/(sqrt(1-x)*sqrt(1+x)))) 
    by SQUARE_1:97,BB
  .=log(number_e,(sqrt(x+1)/sqrt(1-x))) by XCMPLX_1:92,B1
  .=log(number_e,(sqrt((x+1)/(1-x)))) by SQUARE_1:99,BB
  .=(1/2)*log(number_e,((1+x)/(1-x))) by Lm2,BB,POWER:63,DD;
  hence thesis by Def3; 
 end;

theorem
  0<x & x<1 implies tanh"(x)=cosh1"(1/(sqrt(1-x^2)))
   proof
     assume A1:0<x & x<1;then
     (1+x)/(1-x)>0 by Th41;then
     A3:sqrt((1+x)/(1-x))=((1+x)/(1-x)) to_power (1/2) by ASYMPT_1:89;
     A2:1-x>0 & 1+x>0 & x^2<1 & 1-x^2>0 & 1/(sqrt(1-x^2))>1 &  
        (1+x)/(1-x)>0 by A1,Th40,Th41,Th42,Th43,Th44,
         SQUARE_1:11; then
a2:  sqrt(1+x)>0 by SQUARE_1:93;
     A4:x^2>=0 by SQUARE_1:72;
     cosh1"(1/(sqrt(1-x^2)))
    =log(number_e,(1/(sqrt(1-x^2)))+sqrt((1/(sqrt(1-x^2)))^2-1))
      by Def21
   .=log(number_e,(1/(sqrt(1-x^2)))+sqrt((1/((sqrt(1-x^2))^2))-1))
      by SQUARE_1:59,69
   .=log(number_e,(1/(sqrt(1-x^2)))+sqrt(1/(1-x^2)-1))
     by A2,SQUARE_1:def 4
   .=log(number_e,(1/(sqrt(1-x^2)))+sqrt((1-1*(1-x^2))/(1-x^2))) 
      by A2,XCMPLX_1:127 
   .=log(number_e,(1/(sqrt(1-x^2)))+sqrt(x^2)/sqrt(1-x^2))
      by A2,SQUARE_1:99,A4
   .=log(number_e,1/(sqrt(1-x^2))+x/sqrt(1-x^2)) by A1,SQUARE_1:89
   .=log(number_e,(1+x)/sqrt(1^2-x^2)) by SQUARE_1:59,XCMPLX_1:63
   .=log(number_e,(1+x)/sqrt((1-x)*(1+x))) by SQUARE_1:67
   .=log(number_e,(1+x)/(sqrt(1-x)*sqrt(1+x))) by A2,SQUARE_1:97
   .=log(number_e,(sqrt((1+x)^2))/(sqrt(1-x)*sqrt(1+x))) 
      by A2,SQUARE_1:89
   .=log(number_e,(sqrt((1+x)*(1+x)))/(sqrt(1-x)*sqrt(1+x))) 
      by SQUARE_1:def 3
   .=log(number_e,(sqrt(1+x)*sqrt(1+x))/(sqrt(1-x)*sqrt(1+x)))
      by A2,SQUARE_1:97
   .=log(number_e,(sqrt(1+x))/(sqrt(1-x))) by XCMPLX_1:92,a2
   .=log(number_e,sqrt((1+x)/(1-x))) by A2,SQUARE_1:99
   .=(1/2)*log(number_e,((1+x)/(1-x))) by Lm2,A2,A3,POWER:63;
   hence thesis by Def3;
 end;

theorem
  x^2<1 implies tanh"(x)=(1/2)*sinh"((2*x)/(1-x^2))
  proof
    assume x^2<1;then
     AAA:(1-x^2)^2>0 & (x^2+1)/(1-x^2)>=0 & x+1>0 & 1-x>0 by Th37,Th38,
      Th35;
     (1/2)*sinh"((2*x)/(1-x^2))
    =(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt(((2*x)/(1-x^2))^2+1))) by Def1
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt(((2*x)^2/(1-x^2)^2)+1))) 
      by SQUARE_1:69
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt((2^2*x^2/(1-x^2)^2)+1))) 
     by SQUARE_1:68
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt((2*2*x^2/(1-x^2)^2)+1))) 
     by SQUARE_1:def 3
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt(((4*x^2)+((1-x^2)^2)*1)/
     ((1-x^2)^2)))) by AAA,XCMPLX_1:114
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt((4*x^2+(1^2-2*1*(x^2)+(x^2)^2))/
     ((1-x^2)^2)))) by SQUARE_1:64
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt((4*x^2+(1-2*(x^2)+(x^2)^2))/
     ((1-x^2)^2)))) by SQUARE_1:59
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt(((x^2)^2+2*(x^2)+1)/
     ((1-x^2)^2))))
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt((((x^2)+1)^2)/  
     ((1-x^2)^2)))) by SQUARE_1:65
   .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt((((x^2)+1)/(1-x^2))^2)))
      by SQUARE_1:69 
   .=(1/2)*log(number_e,((2*x)/(1-x^2)+((x^2)+1)/(1-x^2))) 
      by AAA,SQUARE_1:89
   .=(1/2)*log(number_e,(((2*x)+((x^2)+1))/(1-x^2))) by XCMPLX_1:63
   .=(1/2)*log(number_e,((x^2+2*x+1)/(1-x^2)))
   .=(1/2)*log(number_e,((x+1)^2/(1-x^2))) by SQUARE_1:65
   .=(1/2)*log(number_e,((x+1)*(x+1)/(1^2-x^2))) by SQUARE_1:59,def 3
   .=(1/2)*log(number_e,((x+1)*(x+1)/((1-x)*(1+x)))) by SQUARE_1:67
   .=(1/2)*log(number_e,((x+1)/(1-x))) by AAA,XCMPLX_1:92;
   hence thesis by Def3;
  end;

theorem 
  x>0 & x<1 implies tanh"(x)=(1/2)*cosh1"((1+x^2)/(1-x^2))
  proof
    assume x>0 & x<1;then
    A2:0<x+1 & x^2<1 & 0<1-x^2 & 0<(1-x^2)^2 &
       (2*x)/(1-x^2)>0 & ((1+x^2)/(1-x^2))>1 by Th40,Th42,
        Th43,Th45,Th46,Th47;
    (1/2)*cosh1"((1+x^2)/(1-x^2))
   =(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt(((1+x^2)/(1-x^2))^2-1)) 
     by Def21
  .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt((1+x^2)^2/(1-x^2)^2-1))
     by SQUARE_1:69
  .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt((((1+x^2)^2)-1*
    ((1-x^2)^2))/((1-x^2)^2))) by A2,XCMPLX_1:127
  .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt((((1+x^2)-(1-x^2))*
    ((1+x^2)+(1-x^2)))/(1-x^2)^2)) by SQUARE_1:67
  .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt((2*2*x^2)/(1-x^2)^2))
  .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt((2^2*x^2)/(1-x^2)^2))
     by SQUARE_1:def 3
  .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt(((2*x)^2)/((1-x^2)^2))) 
     by SQUARE_1:68
  .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt(((2*x)/(1-x^2))^2))
     by SQUARE_1:69
  .=(1/2)*log(number_e,(1+x^2)/(1-x^2)+(2*x)/(1-x^2)) 
     by A2,SQUARE_1:89 
  .=(1/2)*log(number_e,((1+x^2)+(2*x))/(1-x^2)) by XCMPLX_1:63
  .=(1/2)*log(number_e,(x^2+2*x+1)/(1-x^2))
  .=(1/2)*log(number_e,(x+1)^2/(1-x^2)) by SQUARE_1:65
  .=(1/2)*log(number_e,(x+1)*(x+1)/(1^2-x^2)) by SQUARE_1:59,def 3
  .=(1/2)*log(number_e,(x+1)*(x+1)/((1-x)*(1+x))) by SQUARE_1:67
  .=(1/2)*log(number_e,(x+1)/((1-x))) by A2,XCMPLX_1:92;
  hence thesis by Def3;
  end;

theorem 
  x^2<1 implies tanh"(x)=(1/2)*tanh"((2*x)/(1+x^2))
  proof
    assume x^2<1;then
    A2:1+x^2>0 & ((2*x)/(1+x^2))^2<1 & (x+1)/(1-x)>0 by Th39,Th36,Lm16;
    (1/2)*tanh"((2*x)/(1+x^2))
   =(1/2)*((1/2)*log(number_e,(1+((2*x)/(1+x^2)))/(1-((2*x)/(1+x^2))))) 
     by Def3 
  .=(1/2)*((1/2)*log(number_e,(((2*x)+(1+x^2)*1)/(1+x^2))/(1-(2*x)/(1+x^2))))
     by A2,XCMPLX_1:114
  .=(1/2)*((1/2)*log(number_e,((2*x+1+x^2)/(1+x^2))/((1*(1+x^2)-(2*x))
     /(1+x^2)))) by A2,XCMPLX_1:128    
  .=(1/2)*((1/2)*log(number_e,(x^2+2*x+1)/(x^2-2*x+1))) by A2,XCMPLX_1:55
  .=(1/2)*((1/2)*log(number_e,(x+1)^2/(x^2-2*1*x+1^2))) by SQUARE_1:59,65
  .=(1/2)*((1/2)*log(number_e,(x+1)^2/(1^2-2*1*x+x^2)))
  .=(1/2)*((1/2)*log(number_e,(x+1)^2/(1-x)^2)) by SQUARE_1:64
  .=(1/2)*((1/2)*log(number_e,((x+1)/(1-x))^2)) by SQUARE_1:69
  .=(1/2)*((1/2)*log(number_e,((x+1)/(1-x)) to_power 2)) by POWER:53
  .=(1/2)*((1/2)*(2*log(number_e,((x+1)/(1-x))))) by Lm2,A2,POWER:63
  .=(1/2)*log(number_e,(1+x)/(1-x));
  hence thesis by Def3;
 end;

theorem
  x^2>1 implies coth"(x)=tanh"(1/x)
  proof
    assume x^2>1;then
A2:(1/x)^2<1 & x<>0 by SQUARE_1:60,Th48;
    tanh"(1/x) =(1/2)*log(number_e,(1+(1/x))/(1-(1/x))) by Def3
  .=(1/2)*log(number_e,((1+x*1)/x)/(1-(1/x))) by A2,XCMPLX_1:114
  .=(1/2)*log(number_e,((1+x*1)/x)/((1*x-1)/x)) by A2,XCMPLX_1:128
  .=(1/2)*log(number_e,(1+x)/(x-1)) by A2,XCMPLX_1:55;
   hence thesis by Def4;
  end;

theorem
  x>0 & x<=1 implies sech1"(x)=cosh1"(1/x)
  proof
    assume A1:x>0 & x<=1;then
    A3:x^2>0 by SQUARE_1:74;
    A2:1/x>=1 & 1-x^2>=0 by A1,Th49,Th50;
    cosh1"(1/x)=log(number_e,(1/x)+sqrt((1/x)^2-1)) by Def21
  .=log(number_e,(1/x)+sqrt(1/x^2-1)) by SQUARE_1:59,69
  .=log(number_e,(1/x)+sqrt((1-1*(x^2))/x^2)) by A3,XCMPLX_1:127
  .=log(number_e,(1/x)+sqrt(1-(x^2))/sqrt(x^2)) by A2,A3,SQUARE_1:99
  .=log(number_e,(1/x)+sqrt(1-(x^2))/x) by A1,SQUARE_1:89
  .=log(number_e,(1+sqrt(1-x^2))/x) by XCMPLX_1:63;
  hence thesis by Def51; 
 end;

theorem
  x>0 & x<=1 implies sech2"(x)=cosh2"(1/x)
  proof
    assume A1:x>0 & x<=1;then
    A3:x^2>0 by SQUARE_1:74;
    A2:1/x>=1 & 1-x^2>=0 by A1,Th49,Th50;
    cosh2"(1/x) = -log(number_e,(1/x)+sqrt((1/x)^2-1)) by Def22
  .=-log(number_e,(1/x)+sqrt(1/x^2-1)) by SQUARE_1:59,69
  .=-log(number_e,(1/x)+sqrt((1-1*(x^2))/x^2)) by A3,XCMPLX_1:127
  .=-log(number_e,(1/x)+sqrt(1-(x^2))/sqrt(x^2)) by A2,A3,SQUARE_1:99
  .=-log(number_e,(1/x)+sqrt(1-(x^2))/x) by A1,SQUARE_1:89
  .=-log(number_e,(1+sqrt(1-x^2))/x) by XCMPLX_1:63;
  hence thesis by Def52; 
 end;

theorem
  x>0 implies csch"(x)=sinh"(1/x)
 proof
   assume A1:x>0;then
A2:x^2>0 by SQUARE_1:74;
A3:x^2+1>0 by Lm16;
   sinh"(1/x)=log(number_e,(1/x+sqrt((1/x)^2+1))) by Def1
 .=log(number_e,(1/x+sqrt(1/x^2+1))) by SQUARE_1:59,69
 .=log(number_e,(1/x+sqrt((1+x^2*1)/(x^2)))) by A2,XCMPLX_1:114
 .=log(number_e,(1/x+sqrt(1+x^2)/sqrt(x^2))) by A2,A3,SQUARE_1:99
 .=log(number_e,(1/x+sqrt(1+x^2)/x)) by A1,SQUARE_1:89
 .=log(number_e,(1+sqrt(1+x^2))/x) by XCMPLX_1:63;
 hence thesis by A1,Def6;
end;

theorem
  (x*y+sqrt(x^2+1)*sqrt(y^2+1))>=0 implies
  sinh"(x)+sinh"(y)=sinh"(x*sqrt(1+y^2)+y*sqrt(1+x^2))
  proof
    assume A1:(x*y+sqrt(x^2+1)*sqrt(y^2+1))>=0;
    A2:sqrt(x^2+1)+x>0 & sqrt(y^2+1)+y>0 by Th28;   
    A3:x^2+1>=0 & y^2+1>=0 by Lm16;then
    A4:(x^2+1)*(y^2+1)>=0 by REAL_2:121;
   B1:sinh"(x)+sinh"(y)
   =log(number_e,(x+sqrt(x^2+1)))+sinh"(y) by Def1
  .=log(number_e,(x+sqrt(x^2+1)))+log(number_e,(y+sqrt(y^2+1))) by Def1
  .=log(number_e,(x+sqrt(x^2+1))*(y+sqrt(y^2+1))) by A2,Lm2,POWER:61
  .=log(number_e,(x*sqrt(y^2+1)+sqrt(x^2+1)*y+(x*y+sqrt(x^2+1)*sqrt(y^2+1))))
  .=log(number_e,(x*sqrt(y^2+1)+sqrt(x^2+1)*y+
     sqrt((x*y+sqrt(x^2+1)*sqrt(y^2+1))^2))) by A1,SQUARE_1:89;      
  set t=sqrt((x*y+sqrt(x^2+1)*sqrt(y^2+1))^2);
     A5:t=sqrt((x*y+sqrt((x^2+1)*(y^2+1)))^2) by A3,SQUARE_1:97
      .=sqrt((x*y)^2+2*(x*y)*(sqrt((x^2+1)*(y^2+1)))+(sqrt((x^2+1)*(y^2+1)))^2)
         by SQUARE_1:63
      .=sqrt((x*y)^2+2*(x*y)*(sqrt((x^2+1)*(y^2+1)))+(x^2*y^2+x^2+y^2+1)) 
          by A4,SQUARE_1:def 4
      .=sqrt((x*y)^2+2*(x*y)*(sqrt((x^2+1)*(y^2+1)))+((x*y)^2+x^2+y^2+1))
         by SQUARE_1:68
      .=sqrt(2*(x*y)^2+x^2+y^2+1+2*(x*y)*(sqrt((x^2+1)*(y^2+1))));
      set p=sqrt(((x*sqrt(1+y^2))+(y*sqrt(1+x^2)))^2+1);
       p=sqrt(((x*sqrt(1+y^2))^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +(y*sqrt(1+x^2))^2)+1) by SQUARE_1:63
         .=sqrt((x^2*(sqrt(1+y^2))^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +(y*sqrt(1+x^2))^2)+1) by SQUARE_1:68
         .=sqrt((x^2*(1+y^2)+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +(y*sqrt(1+x^2))^2)+1) by SQUARE_1:def 4,A3
         .=sqrt((x^2+x^2*y^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +(y*sqrt(1+x^2))^2)+1) 
         .=sqrt((x^2+(x*y)^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +(y*sqrt(1+x^2))^2)+1) by SQUARE_1:68
         .=sqrt((x^2+(x*y)^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +y^2*(sqrt(1+x^2))^2)+1) by SQUARE_1:68 
         .=sqrt((x^2+(x*y)^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +y^2*(1+x^2))+1) by SQUARE_1:def 4,A3
         .=sqrt((x^2+(x*y)^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +y^2+x^2*y^2)+1)
         .=sqrt((x^2+(x*y)^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
            +y^2+(x*y)^2)+1) by SQUARE_1:68
         .=sqrt(x^2+2*(x*y)^2+y^2+1+2*x*y*(sqrt(1+y^2)*sqrt(1+x^2)))
         .=sqrt(x^2+2*(x*y)^2+y^2+1+2*x*y*sqrt((1+y^2)*(1+x^2)))
            by A3,SQUARE_1:97;
     hence thesis by B1,A5,Def1;
   end;

theorem
  sinh"(x)-sinh"(y)=sinh"(x*sqrt(1+y^2)-y*sqrt(1+x^2))
  proof
A1: (sqrt(x^2+1)*sqrt(y^2+1)-x*y)>=0 by Lm24;
A2: sqrt(x^2+1)+x>0 & sqrt(y^2+1)+y>0 by Th28;
A3: x^2+1>=0 & y^2+1>=0 by Lm16;
    y+0<sqrt(y^2+1) by Lm22;then
A5: sqrt(y^2+1)-y>0 by REAL_1:86;
 C1:sinh"(x)-sinh"(y)
   =log(number_e,(x+sqrt(x^2+1)))-sinh"(y) by Def1
  .=log(number_e,(x+sqrt(x^2+1)))-log(number_e,(y+sqrt(y^2+1))) by Def1
  .=log(number_e,(x+sqrt(x^2+1))/(y+sqrt(y^2+1))) by Lm2,A2,POWER:62
  .=log(number_e,((x+sqrt(x^2+1))*(sqrt(y^2+1)-y))/
       ((y+sqrt(y^2+1))*(sqrt(y^2+1)-y))) by A5,XCMPLX_1:92
  .=log(number_e,((x+sqrt(x^2+1))*(sqrt(y^2+1)-y))/
       ((sqrt(y^2+1))^2-y^2)) by SQUARE_1:67
  .=log(number_e,((x+sqrt(x^2+1))*(sqrt(y^2+1)-y))/
       ((y^2+1)-y^2)) by A3,SQUARE_1:def 4
  .=log(number_e,x*(sqrt(y^2+1))-y*(sqrt(x^2+1))+(sqrt(x^2+1))
     *(sqrt(y^2+1))-x*y);
  set t=(sqrt(x^2+1))*(sqrt(y^2+1))-x*y;
   A6:t=sqrt(((sqrt(x^2+1))*(sqrt(y^2+1))-x*y)^2) by A1,SQUARE_1:89
      .=sqrt(((sqrt(x^2+1))*(sqrt(y^2+1)))^2-2*((sqrt(x^2+1))*
        (sqrt(y^2+1)))*(x*y)+(x*y)^2) by SQUARE_1:64
      .=sqrt(((sqrt(x^2+1))^2*(sqrt(y^2+1))^2)-2*((sqrt(x^2+1))*
        (sqrt(y^2+1)))*(x*y)+(x*y)^2) by SQUARE_1:68
      .=sqrt(((x^2+1)*(sqrt(y^2+1))^2)-2*((sqrt(x^2+1))*
        (sqrt(y^2+1)))*(x*y)+(x*y)^2) by A3,SQUARE_1:def 4
      .=sqrt(((x^2+1)*(y^2+1))-2*((sqrt(x^2+1))*
        (sqrt(y^2+1)))*(x*y)+(x*y)^2) by A3,SQUARE_1:def 4
      .=sqrt(x^2*y^2+x^2+y^2+1-2*((sqrt(x^2+1))*
        (sqrt(y^2+1)))*(x*y)+(x*y)^2) 
      .=sqrt((x*y)^2+x^2+y^2+1-2*((sqrt(x^2+1))*
        (sqrt(y^2+1)))*(x*y)+(x*y)^2) by SQUARE_1:68
      .=sqrt(2*((x*y)^2)+x^2+y^2+1-2*(x*y)*((sqrt(x^2+1))*
        (sqrt(y^2+1))));
    C2:log(number_e,x*(sqrt(y^2+1))-y*(sqrt(x^2+1))+(sqrt(x^2+1))
       *(sqrt(y^2+1))-x*y)
      =log(number_e,x*(sqrt(y^2+1))-y*(sqrt(x^2+1))+t)
     .=log(number_e,(x*sqrt(y^2+1)-y*sqrt(x^2+1)+
       sqrt(x^2+y^2+2*(x*y)^2+1-2*x*y*sqrt(x^2+1)*sqrt(y^2+1)))) by A6;
      set q=sqrt((x*sqrt(1+y^2)-y*sqrt(1+x^2))^2+1);
         A7:q=sqrt((x*sqrt(1+y^2))^2-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
             +((y*sqrt(1+x^2)))^2+1) by SQUARE_1:64
          .=sqrt((x^2*(sqrt(1+y^2))^2)-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
             +((y*sqrt(1+x^2)))^2+1) by SQUARE_1:68
          .=sqrt((x^2*(1+y^2))-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
             +((y*sqrt(1+x^2)))^2+1) by A3,SQUARE_1:def 4
          .=sqrt((x^2+x^2*y^2)-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
             +(y^2*(sqrt(1+x^2))^2)+1) by SQUARE_1:68
          .=sqrt((x^2+x^2*y^2)-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
             +(y^2*(1+x^2))+1) by A3,SQUARE_1:def 4
          .=sqrt(x^2+y^2+2*(x^2*y^2)+1-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2)))             
          .=sqrt(x^2+y^2+2*(x*y)^2+1-2*x*y*sqrt(1+y^2)*sqrt(1+x^2)) 
              by SQUARE_1:68;
         log(number_e,((x*sqrt(1+y^2)-y*sqrt(1+x^2))+
             sqrt((x*sqrt(1+y^2)-y*sqrt(1+x^2))^2+1)))
         =log(number_e,(x*sqrt(1+y^2)-y*sqrt(1+x^2)+
             sqrt(x^2+y^2+2*(x*y)^2+1-2*x*y*(sqrt(1+y^2)*sqrt(1+x^2))))) by A7
        .=log(number_e,(x*sqrt(y^2+1)-y*sqrt(x^2+1)+
             sqrt(x^2+y^2+2*(x*y)^2+1-2*x*y*(sqrt(x^2+1)*sqrt(y^2+1)))));
        hence thesis by C1,C2,Def1;
     end;

theorem
  1<=x & 1<=y implies 
  cosh1"(x)+cosh1"(y)=cosh1"(x*y+sqrt((x^2-1)*(y^2-1)))   
proof
    assume A1:1<=x & 1<=y;then
    A4:x^2-1>=0 & y^2-1>=0 by Th29;then
A7:    (x^2-1)*(y^2-1)>=0*(y^2-1) by AXIOMS:25;
    A3:0<x+sqrt(x^2-1) & 0<y+sqrt(y^2-1) by A1,Th52;
    A5:0<=x*sqrt(y^2-1)+y*sqrt(x^2-1) by A1,Th53;
   B1:cosh1"(x)+cosh1"(y)
   =log(number_e,x+sqrt(x^2-1))+cosh1"(y) by Def21
  .=log(number_e,x+sqrt(x^2-1))+log(number_e,y+sqrt(y^2-1)) by Def21
  .=log(number_e,(x+sqrt(x^2-1))*(y+sqrt(y^2-1))) by Lm2,A3,POWER:61
  .=log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+(sqrt(x^2-1))*(sqrt(y^2-1))+x*y)
  .=log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+sqrt((x^2-1)*(y^2-1))+x*y)
    by A4,SQUARE_1:97;
  set t=x*sqrt(y^2-1)+y*sqrt(x^2-1);
   A6:t=sqrt((x*sqrt(y^2-1)+y*sqrt(x^2-1))^2) by A5,SQUARE_1:89
      .=sqrt((x*sqrt(y^2-1))^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         (y*sqrt(x^2-1))^2) by SQUARE_1:63
      .=sqrt(x^2*(sqrt(y^2-1))^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         (y*sqrt(x^2-1))^2) by SQUARE_1:68
      .=sqrt(x^2*(y^2-1)+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         (y*sqrt(x^2-1))^2) by A4,SQUARE_1:def 4
      .=sqrt(x^2*y^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         y^2*(sqrt(x^2-1))^2) by SQUARE_1:68
      .=sqrt(x^2*y^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         y^2*(x^2-1)) by A4,SQUARE_1:def 4
      .=sqrt(x^2*y^2+y^2*x^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))-y^2)
      .=sqrt((x*y)^2+y^2*x^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))-y^2)
         by SQUARE_1:68
      .=sqrt(((x*y)^2+(x*y)^2)-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))-y^2)
         by SQUARE_1:68
      .=sqrt(2*(x*y)^2-x^2-y^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1)));
   B2:log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+sqrt((x^2-1)*(y^2-1))+x*y)
     =log(number_e,(sqrt(2*(x*y)^2-x^2-y^2+2*x*y*(sqrt(y^2-1)
         *sqrt(x^2-1))))+sqrt((x^2-1)*(y^2-1))+x*y) by A6
    .=log(number_e,(sqrt(2*(x*y)^2-x^2-y^2+2*x*y*sqrt((y^2-1)*(x^2-1))))
       +sqrt((x^2-1)*(y^2-1))+x*y) by A4,SQUARE_1:97;
    cosh1"(x*y+sqrt((x^2-1)*(y^2-1)))
    =log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+
        sqrt((x*y+sqrt((x^2-1)*(y^2-1)))^2-1)) by Def21
   .=log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
        2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+((sqrt((x^2-1)*(y^2-1)))^2)-1)) 
        by SQUARE_1:63        
   .=log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
        2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+(x^2-1)*(y^2-1)-1))
     by A7,SQUARE_1:def 4
   .=log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
        2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+x^2*y^2-x^2-y^2))
   .=log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
        2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+(x*y)^2-x^2-y^2)) by SQUARE_1:68
   .=log(number_e,((x*y+sqrt((x^2-1)*(y^2-1))))+(sqrt(2*(x*y)^2-x^2-y^2
     +2*(x*y)*(sqrt((x^2-1)*(y^2-1))))))
   .=log(number_e,((sqrt(2*(x*y)^2-x^2-y^2+2*x*y*sqrt((y^2-1)*(x^2-1)))))
       +(sqrt((x^2-1)*(y^2-1))+x*y));
    hence thesis by B1,B2;   
  end;
  
theorem
  1<=x & 1<=y implies 
  cosh2"(x)+cosh2"(y)=cosh2"(x*y+sqrt((x^2-1)*(y^2-1)))
   proof
    assume A1:1<=x & 1<=y;then
    A4:x^2-1>=0 & y^2-1>=0 by Th29;then
A7:    (x^2-1)*(y^2-1)>=0*(y^2-1) by AXIOMS:25;
    A3:0<x+sqrt(x^2-1) & 0<y+sqrt(y^2-1) by A1,Th52;
    A5:0<=x*sqrt(y^2-1)+y*sqrt(x^2-1) by A1,Th53;
   B1:cosh2"(x)+cosh2"(y)
   =-log(number_e,x+sqrt(x^2-1))+cosh2"(y) by Def22
  .=-log(number_e,x+sqrt(x^2-1))+(-log(number_e,y+sqrt(y^2-1))) by Def22
  .=(-1)*(log(number_e,x+sqrt(x^2-1))+log(number_e,y+sqrt(y^2-1)))
  .=(-1)*log(number_e,(x+sqrt(x^2-1))*(y+sqrt(y^2-1))) by Lm2,A3,POWER:61
  .=-log(number_e,(x+sqrt(x^2-1))*(y+sqrt(y^2-1)))
  .=-log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+(sqrt(x^2-1))*(sqrt(y^2-1))+x*y)
  .=-log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+sqrt((x^2-1)*(y^2-1))+x*y)
    by A4,SQUARE_1:97;
  set t=x*sqrt(y^2-1)+y*sqrt(x^2-1);
   A6:t=sqrt((x*sqrt(y^2-1)+y*sqrt(x^2-1))^2) by A5,SQUARE_1:89
      .=sqrt((x*sqrt(y^2-1))^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         (y*sqrt(x^2-1))^2) by SQUARE_1:63
      .=sqrt(x^2*(sqrt(y^2-1))^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         (y*sqrt(x^2-1))^2) by SQUARE_1:68
      .=sqrt(x^2*(y^2-1)+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         (y*sqrt(x^2-1))^2) by A4,SQUARE_1:def 4
      .=sqrt(x^2*y^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         y^2*(sqrt(x^2-1))^2) by SQUARE_1:68
      .=sqrt(x^2*y^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
         y^2*(x^2-1)) by A4,SQUARE_1:def 4
      .=sqrt(x^2*y^2+y^2*x^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))-y^2)
      .=sqrt((x*y)^2+y^2*x^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))-y^2)
         by SQUARE_1:68   
      .=sqrt(((x*y)^2+(x*y)^2)-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))-y^2)
         by SQUARE_1:68
      .=sqrt(2*(x*y)^2-x^2-y^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1)));
   B2:-log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+sqrt((x^2-1)*(y^2-1))+x*y)
     =-log(number_e,(sqrt(2*(x*y)^2-x^2-y^2+2*x*y*(sqrt(y^2-1)
         *sqrt(x^2-1))))+sqrt((x^2-1)*(y^2-1))+x*y) by A6
    .=-log(number_e,(sqrt(2*(x*y)^2-x^2-y^2+2*x*y*sqrt((y^2-1)*(x^2-1))))
       +sqrt((x^2-1)*(y^2-1))+x*y) by A4,SQUARE_1:97;
    cosh2"(x*y+sqrt((x^2-1)*(y^2-1)))
    =-log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+
        sqrt((x*y+sqrt((x^2-1)*(y^2-1)))^2-1)) by Def22
   .=-log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
        2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+((sqrt((x^2-1)*(y^2-1)))^2)-1)) 
        by SQUARE_1:63        
   .=-log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
        2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+(x^2-1)*(y^2-1)-1))
     by A7,SQUARE_1:def 4
   .=-log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
        2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+x^2*y^2-x^2-y^2))
   .=-log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
        2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+(x*y)^2-x^2-y^2)) by SQUARE_1:68
   .=-log(number_e,((x*y+sqrt((x^2-1)*(y^2-1))))+(sqrt(2*(x*y)^2-x^2-y^2
     +2*(x*y)*(sqrt((x^2-1)*(y^2-1))))))
   .=-log(number_e,((sqrt(2*(x*y)^2-x^2-y^2+2*x*y*sqrt((y^2-1)*(x^2-1)))))
       +(sqrt((x^2-1)*(y^2-1))+x*y));
    hence thesis by B1,B2;   
  end;

theorem 
  1<=x & 1<=y & abs(y)<=abs(x) implies 
  cosh1"(x)-cosh1"(y)=cosh1"(x*y-sqrt((x^2-1)*(y^2-1)))
 proof
     assume A1:1<=x & 1<=y & abs(y)<=abs(x);then
     A2:(y-sqrt(y^2-1))>0 by Th55;
     A3:0<x+sqrt(x^2-1) & 0<y+sqrt(y^2-1) by A1,Th52;
     A4:0<=x^2-1 & 0<=y^2-1 by A1,Th29;
     A5:0<=y*sqrt(x^2-1)-x*sqrt(y^2-1) by A1,Th56;
     A7:   0*(y^2-1)<=(x^2-1)*(y^2-1) by A4,AXIOMS:25;
 B1:cosh1"(x)-cosh1"(y)
    =log(number_e,x+sqrt(x^2-1))-cosh1"(y) by Def21
   .=log(number_e,x+sqrt(x^2-1))-log(number_e,y+sqrt(y^2-1)) by Def21
   .=log(number_e,((x+sqrt(x^2-1))/(y+sqrt(y^2-1)))) by A3,Lm2,POWER:62
   .=log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
     ((y+sqrt(y^2-1))*(y-sqrt(y^2-1))))) by A2,XCMPLX_1:92
   .=log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
     (y^2-(sqrt(y^2-1))^2))) by SQUARE_1:67 
   .=log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
     (y^2-(y^2-1)))) by A4,SQUARE_1:def 4
   .=log(number_e,(x*y-x*sqrt(y^2-1)+y*sqrt(x^2-1)-sqrt(x^2-1)*
     (sqrt(y^2-1)))) 
   .=log(number_e,(x*y-x*sqrt(y^2-1)+y*sqrt(x^2-1)-sqrt((x^2-1)*(y^2-1))))
      by A4,SQUARE_1:97 
   .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+y*sqrt(x^2-1)-x*sqrt(y^2-1)));
   set t=y*sqrt(x^2-1)-x*sqrt(y^2-1);
   A8:    t=sqrt((y*sqrt(x^2-1)-x*sqrt(y^2-1))^2) by SQUARE_1:89,A5
       .=sqrt((y*sqrt(x^2-1))^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by SQUARE_1:64
       .=sqrt(y^2*(sqrt(x^2-1))^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by SQUARE_1:68
       .=sqrt(y^2*(x^2-1)-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by A4,SQUARE_1:def 4
       .=sqrt(y^2*x^2-y^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2)
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by SQUARE_1:68
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+x^2*(sqrt(y^2-1))^2) by SQUARE_1:68
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+x^2*(y^2-1)) by A4,SQUARE_1:def 4
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*(x*sqrt(y^2-1))+(x^2*y^2-x^2))
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*(x*sqrt(y^2-1))+((x*y)^2-x^2))
          by SQUARE_1:68
       .=sqrt(2*(x*y)^2-x^2-y^2-2*x*y*(sqrt(x^2-1)*sqrt(y^2-1)))
       .=sqrt(2*(x*y)^2-x^2-y^2-2*x*y*(sqrt((x^2-1)*(y^2-1))))
         by A4,SQUARE_1:97;
  B2:log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+y*sqrt(x^2-1)-x*sqrt(y^2-1)))
    =log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+t))
   .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+sqrt(2*(x*y)^2-x^2-y^2-
      2*x*y*(sqrt((x^2-1)*(y^2-1)))))) by A8;
   cosh1"(x*y-sqrt((x^2-1)*(y^2-1)))
    =log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+
     sqrt((x*y-sqrt((x^2-1)*(y^2-1)))^2-1)) by Def21
  .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
    (sqrt((x^2-1)*(y^2-1)))+(sqrt((x^2-1)*(y^2-1)))^2-1)) by SQUARE_1:64
  .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
    (sqrt((x^2-1)*(y^2-1)))+((x^2-1)*(y^2-1))-1)) by A7,SQUARE_1:def 4     
  .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
    (sqrt((x^2-1)*(y^2-1)))+x^2*y^2-x^2-y^2+1-1))
  .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
    (sqrt((x^2-1)*(y^2-1)))+(x*y)^2-x^2-y^2)) by SQUARE_1:68
  .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt(2*(x*y)^2-x^2-y^2
     -2*x*y*(sqrt((x^2-1)*(y^2-1)))));
  hence thesis by B1,B2; 
 end;
   
theorem 
  1<=x & 1<=y & abs(y)<=abs(x) implies 
  cosh2"(x)-cosh2"(y)=cosh2"(x*y-sqrt((x^2-1)*(y^2-1)))
 proof
   assume A1:1<=x & 1<=y & abs(y)<=abs(x);then
   A2:(y-sqrt(y^2-1))>0 by Th55;
     A3:0<x+sqrt(x^2-1) & 0<y+sqrt(y^2-1) by A1,Th52;
     A4:0<=x^2-1 & 0<=y^2-1 by A1,Th29;
     A5:0<=y*sqrt(x^2-1)-x*sqrt(y^2-1) by A1,Th56;
A7:     0*(y^2-1)<=(x^2-1)*(y^2-1) by A4,AXIOMS:25;
    B1:cosh2"(x)-cosh2"(y)
    =-(log(number_e,x+sqrt(x^2-1)))-cosh2"(y) by Def22
   .=-(log(number_e,x+sqrt(x^2-1)))-(-(log(number_e,y+sqrt(y^2-1)))) 
     by Def22
   .=-(log(number_e,x+sqrt(x^2-1))-log(number_e,y+sqrt(y^2-1)))
   .=-log(number_e,((x+sqrt(x^2-1))/(y+sqrt(y^2-1)))) by A3,Lm2,POWER:62
   .=-log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
     ((y+sqrt(y^2-1))*(y-sqrt(y^2-1))))) by A2,XCMPLX_1:92
   .=-log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
     (y^2-(sqrt(y^2-1))^2))) by SQUARE_1:67 
   .=-log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
     (y^2-(y^2-1)))) by A4,SQUARE_1:def 4
   .=-log(number_e,(x*y-x*sqrt(y^2-1)+y*sqrt(x^2-1)-sqrt(x^2-1)*
     (sqrt(y^2-1))))
   .=-log(number_e,(x*y-x*sqrt(y^2-1)+y*sqrt(x^2-1)-sqrt((x^2-1)*(y^2-1))))
      by A4,SQUARE_1:97
   .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+y*sqrt(x^2-1)-x*sqrt(y^2-1)));
   set t=y*sqrt(x^2-1)-x*sqrt(y^2-1);
   A8:    t=sqrt((y*sqrt(x^2-1)-x*sqrt(y^2-1))^2) by SQUARE_1:89,A5
       .=sqrt((y*sqrt(x^2-1))^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by SQUARE_1:64
       .=sqrt(y^2*(sqrt(x^2-1))^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by SQUARE_1:68
       .=sqrt(y^2*(x^2-1)-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by A4,SQUARE_1:def 4
       .=sqrt(y^2*x^2-y^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2)
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by SQUARE_1:68
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+x^2*(sqrt(y^2-1))^2) by SQUARE_1:68
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*
         (x*sqrt(y^2-1))+x^2*(y^2-1)) by A4,SQUARE_1:def 4
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*(x*sqrt(y^2-1))+(x^2*y^2-x^2))
       .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*(x*sqrt(y^2-1))+((x*y)^2-x^2))
          by SQUARE_1:68
       .=sqrt(2*(x*y)^2-x^2-y^2-2*x*y*(sqrt(x^2-1)*sqrt(y^2-1)))
       .=sqrt(2*(x*y)^2-x^2-y^2-2*x*y*(sqrt((x^2-1)*(y^2-1))))
         by A4,SQUARE_1:97;
  B2:-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+y*sqrt(x^2-1)-x*sqrt(y^2-1)))
    =-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+t))
   .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+sqrt(2*(x*y)^2-x^2-y^2-
      2*x*y*(sqrt((x^2-1)*(y^2-1)))))) by A8;
   cosh2"(x*y-sqrt((x^2-1)*(y^2-1)))
    =-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+
     sqrt((x*y-sqrt((x^2-1)*(y^2-1)))^2-1)) by Def22
  .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
    (sqrt((x^2-1)*(y^2-1)))+(sqrt((x^2-1)*(y^2-1)))^2-1)) by SQUARE_1:64
  .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
    (sqrt((x^2-1)*(y^2-1)))+((x^2-1)*(y^2-1))-1)) by A7,SQUARE_1:def 4     
  .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
    (sqrt((x^2-1)*(y^2-1)))+x^2*y^2-x^2-y^2))
  .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
    (sqrt((x^2-1)*(y^2-1)))+(x*y)^2-x^2-y^2)) by SQUARE_1:68
  .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt(2*(x*y)^2-x^2-y^2
     -2*x*y*(sqrt((x^2-1)*(y^2-1)))));
  hence thesis by B1,B2;
 end;

theorem 
  x^2<1 & y^2<1 implies tanh"(x)+tanh"(y)=tanh"((x+y)/(1+x*y))
   proof
     assume A1:x^2<1 & y^2<1;then
     A2:0<(1+x)/(1-x) & 0<(1+y)/(1-y) by Th36;
     A4:x*y+1<>0 by A1,Th58;
 B1:tanh"(x)+tanh"(y)
   =(1/2)*log(number_e,(1+x)/(1-x))+tanh"(y) by Def3
  .=(1/2)*log(number_e,(1+x)/(1-x))+((1/2)*log(number_e,(1+y)/(1-y)))
     by Def3
  .=(1/2)*(log(number_e,(1+x)/(1-x))+log(number_e,(1+y)/(1-y)))
  .=(1/2)*(log(number_e,((1+x)/(1-x))*((1+y)/(1-y)))) by A2,Lm2,POWER:61
  .=(1/2)*(log(number_e,(((1+x)*(1+y))/((1-x)*(1-y))))) by XCMPLX_1:77
  .=(1/2)*(log(number_e,((1+x+y+x*y)/(1+x*y-x-y))));
 tanh"((x+y)/(1+x*y))
   =(1/2)*log(number_e,(1+((x+y)/(1+x*y)))/(1-((x+y)/(1+x*y)))) by Def3
  .=(1/2)*log(number_e,(((x+y)+(1+x*y)*1)/(1+x*y))/(1-((x+y)/(1+x*y))))
      by A4,XCMPLX_1:114
  .=(1/2)*log(number_e,((x+y+1+x*y)/(1+x*y))/(((1*(1+x*y)-(x+y))/(1+x*y))))
      by A4,XCMPLX_1:128 
  .=(1/2)*log(number_e,(1+x+y+x*y)/(1+x*y-x-y)) by A4,XCMPLX_1:55;
 hence thesis by B1;
end;

theorem 
  x^2<1 & y^2<1 implies tanh"(x)-tanh"(y)=tanh"((x-y)/(1-x*y))
  proof
    assume A1:x^2<1 & y^2<1;then
    A2:0<(1+x)/(1-x) & 0<(1+y)/(1-y) by Th36;
    A4:1-x*y<>0 by A1,Th62;
  B1:  tanh"(x)-tanh"(y)
   =(1/2)*log(number_e,(1+x)/(1-x))-tanh"(y) by Def3
  .=(1/2)*log(number_e,(1+x)/(1-x))-(1/2)*log(number_e,(1+y)/(1-y)) by Def3
  .=(1/2)*(log(number_e,(1+x)/(1-x))-log(number_e,(1+y)/(1-y)))
  .=(1/2)*(log(number_e,((1+x)/(1-x))/((1+y)/(1-y)))) by Lm2,A2,POWER:62
  .=(1/2)*(log(number_e,((1+x)*(1-y))/((1-x)*(1+y)))) by XCMPLX_1:85
  .=(1/2)*(log(number_e,(1-y+x-x*y)/(1+y-x-x*y)));
    tanh"((x-y)/(1-x*y))
   =(1/2)*log(number_e,(1+((x-y)/(1-x*y)))/(1-((x-y)/(1-x*y)))) by Def3
  .=(1/2)*log(number_e,(((x-y)+(1-x*y)*1)/(1-x*y))/(1-((x-y)/(1-x*y))))
     by A4,XCMPLX_1:114
  .=(1/2)*log(number_e,((x-y+1-x*y)/(1-x*y))/((1*(1-x*y)-(x-y))/(1-x*y)))
     by A4,XCMPLX_1:128
  .=(1/2)*(log(number_e,(1-y+x-x*y)/(1+y-x-x*y))) by A4,XCMPLX_1:55;
   hence thesis by B1;
  end;

theorem
  0<x & ((x-1)/(x+1))^2<1 implies log(number_e,x)=2*tanh"((x-1)/(x+1))
  proof
    assume 0<x & ((x-1)/(x+1))^2<1;then
    A2:x+1>0 by XREAL_1:36;
    2*tanh"((x-1)/(x+1))
   =2*((1/2)*log(number_e,(1+((x-1)/(x+1)))/(1-((x-1)/(x+1))))) by Def3
  .=log(number_e,(((x-1)+(x+1)*1)/(x+1))/(1-(x-1)/(x+1))) by A2,XCMPLX_1:114
  .=log(number_e,((2*x)/(x+1))/((1*(x+1)-(x-1))/(x+1))) by A2,XCMPLX_1:128
  .=log(number_e,(2*x)/2) by A2, XCMPLX_1:55
  .=log(number_e,x);
  hence thesis;
  end;

theorem
  0<x & ((x^2-1)/(x^2+1))^2<1 implies log(number_e,x)=tanh"((x^2-1)/(x^2+1))
  proof
    assume A1:0<x & ((x^2-1)/(x^2+1))^2<1;
    A2:x^2+1>0 by Lm16;
    tanh"((x^2-1)/(x^2+1))
   =(1/2)*log(number_e,(1+((x^2-1)/(x^2+1)))/(1-((x^2-1)/(x^2+1)))) by Def3
  .=(1/2)*log(number_e,(((x^2-1)+(x^2+1)*1)/(x^2+1))/(1-(x^2-1)/(x^2+1)))
     by A2,XCMPLX_1:114
  .=(1/2)*log(number_e,((2*x^2)/(x^2+1))/((1*(x^2+1)-(x^2-1))/(x^2+1)))
     by A2,XCMPLX_1:128
  .=(1/2)*log(number_e,2*x^2/2) by A2,XCMPLX_1:55
  .=(1/2)*log(number_e,x to_power 2) by POWER:53
  .=(1/2)*(2*log(number_e,x)) by Lm2,A1,POWER:63
  .=log(number_e,x);
  hence thesis;
 end;

theorem
  1<x & 1<=(x^2+1)/(2*x) implies log(number_e,x)=cosh1"((x^2+1)/(2*x))
   proof
     assume A1:1<x & 1<=(x^2+1)/(2*x);
     1*2<2*x by A1,REAL_1:70;then
  A5:1<2*x by AXIOMS:22;then
  A3:1<(2*x)^2 by SQUARE_1:59,78;
     x<x^2 by A1,SQUARE_1:76;then
     1<x^2 by A1,AXIOMS:22;then
  A6:1-1<x^2-1 by REAL_1:92;then
  A4:0<(x^2-1)^2 by SQUARE_1:74;
     cosh1"((x^2+1)/(2*x))
    =log(number_e,((x^2+1)/(2*x))+sqrt(((x^2+1)/(2*x))^2-1)) by Def21
   .=log(number_e,((x^2+1)/(2*x))+sqrt(((x^2+1))^2/(2*x)^2-1)) by SQUARE_1:69
   .=log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)^2+2*x^2+1)/(2*x)^2-1)) 
     by SQUARE_1:65
   .=log(number_e,((x^2+1)/(2*x))+sqrt((((x^2)^2+2*x^2+1)-1*(2*x)^2)/(2*x)^2)) 
      by A3,XCMPLX_1:127
   .=log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)^2+2*x^2+1-(2^2*x^2))/(2*x)^2))
      by SQUARE_1:68
   .=log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)^2+2*x^2+1-(2*2*x^2))/(2*x)^2))
      by SQUARE_1:def 3
   .=log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)^2-2*x^2+1)/(2*x)^2))
   .=log(number_e,((x^2+1)/(2*x))+sqrt((((x^2)-1)^2)/(2*x)^2)) by SQUARE_1:66
   .=log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)-1)^2)/sqrt((2*x)^2)) 
      by A3,A4,SQUARE_1:99
   .=log(number_e,((x^2+1)/(2*x))+(x^2-1)/sqrt((2*x)^2)) by A6,SQUARE_1:89
   .=log(number_e,((x^2+1)/(2*x))+(x^2-1)/(2*x)) by A5,SQUARE_1:89
   .=log(number_e,((x^2+1)+(x^2-1))/(2*x)) by XCMPLX_1:63
   .=log(number_e,(2*x^2)/(2*x))
   .=log(number_e,(x^2)/x) by XCMPLX_1:92
   .=log(number_e,(x*x)/x) by SQUARE_1:def 3
   .=log(number_e,x/(x/x)) by XCMPLX_1:78 
   .=log(number_e,x/1) by XCMPLX_1:60,A1
   .=log(number_e,x);
   hence thesis;
   end;

theorem
  0<x & x<1 & 1<=(x^2+1)/(2*x) implies log(number_e,x)=cosh2"((x^2+1)/(2*x))
   proof
     assume A1:0<x & x<1 & 1<=(x^2+1)/(2*x);then
  A2:   0*2<x*2 by REAL_1:70;then
  A3:0<(2*x)^2 by SQUARE_1:74;
     x^2<x by A1,SQUARE_1:75;then
     x^2<1 by A1,AXIOMS:22;then
  A4:x^2-x^2<1-x^2 by REAL_1:92;then
  A5:0<(1-x^2)^2 by SQUARE_1:74;
  A6:(1/x)=x to_power (-1) by A1,Th4;
     cosh2"((x^2+1)/(2*x))
    =-log(number_e,((x^2+1)/(2*x))+sqrt(((x^2+1)/(2*x))^2-1)) by Def22
   .=-log(number_e,((x^2+1)/(2*x))+sqrt(((x^2+1))^2/(2*x)^2-1)) by SQUARE_1:69
   .=-log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)^2+2*x^2+1)/(2*x)^2-1)) 
     by SQUARE_1:65
   .=-log(number_e,((x^2+1)/(2*x))+sqrt((((x^2)^2+2*x^2+1)-1*(2*x)^2)/(2*x)^2)) 
      by A3,XCMPLX_1:127
   .=-log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)^2+2*x^2+1-(2^2*x^2))/(2*x)^2))
      by SQUARE_1:68
   .=-log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)^2+2*x^2+1-(2*2*x^2))/(2*x)^2))
      by SQUARE_1:def 3
   .=-log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)^2-2*x^2+1^2)/(2*x)^2))
      by SQUARE_1:59
   .=-log(number_e,((x^2+1)/(2*x))+sqrt((1^2-2*1*x^2+(x^2)^2)/(2*x)^2))
   .=-log(number_e,((x^2+1)/(2*x))+sqrt(((1-(x^2))^2)/(2*x)^2)) by SQUARE_1:64
   .=-log(number_e,((x^2+1)/(2*x))+sqrt((1-(x^2))^2)/sqrt(2*x)^2) 
      by A3,A5,SQUARE_1:99
   .=-log(number_e,((x^2+1)/(2*x))+(1-(x^2))/sqrt(2*x)^2) by A4,SQUARE_1:89
   .=-log(number_e,((x^2+1)/(2*x))+(1-x^2)/(2*x)) by A2,SQUARE_1:89
   .=-log(number_e,((x^2+1)+(1-x^2))/(2*x)) by XCMPLX_1:63
   .=-log(number_e,(2*1)/(2*x))
   .=-log(number_e,1/x) by XCMPLX_1:92
   .=-((-1)*log(number_e,x)) by POWER:63,Lm2,A1,A6
   .=log(number_e,x);
    hence thesis;
   end; 

theorem
  0<x implies log(number_e,x)=sinh"((x^2-1)/(2*x))
   proof
     assume A1:0<x;then
  A2:0*2<x*2 by REAL_1:70;then
  A3:0<(2*x)^2 by SQUARE_1:74;
     x^2>=0 by SQUARE_1:72;then
  A5:x^2+1>=0+1 by REAL_1:55;then
  A4:(x^2+1)^2>0 by SQUARE_1:74; 
     sinh"((x^2-1)/(2*x))
    =log(number_e,(((x^2-1)/(2*x))+sqrt(((x^2-1)/(2*x))^2+1))) by Def1
   .=log(number_e,(((x^2-1)/(2*x))+sqrt((x^2-1)^2/(2*x)^2+1))) 
      by SQUARE_1:69
   .=log(number_e,(((x^2-1)/(2*x))+sqrt(((x^2)^2-2*x^2+1)/(2*x)^2+1))) 
      by SQUARE_1:66
   .=log(number_e,(((x^2-1)/(2*x))+sqrt((((x^2)^2-2*x^2+1)+
        (2*x)^2*1)/(2*x)^2))) by A3,XCMPLX_1:114      
   .=log(number_e,(((x^2-1)/(2*x))+sqrt(((x^2)^2-2*x^2+1+
        (2^2*x^2))/(2*x)^2))) by SQUARE_1:68
   .=log(number_e,(((x^2-1)/(2*x))+sqrt(((x^2)^2-2*x^2+1+
        (2*2*x^2))/(2*x)^2))) by SQUARE_1:def 3
   .=log(number_e,(((x^2-1)/(2*x))+sqrt(((x^2)^2+2*x^2+1)/(2*x)^2)))
   .=log(number_e,(((x^2-1)/(2*x))+sqrt(((x^2+1)^2)/(2*x)^2))) by SQUARE_1:65
   .=log(number_e,(((x^2-1)/(2*x))+sqrt((x^2+1)^2)/sqrt((2*x)^2)))
       by A3,A4,SQUARE_1:99
   .=log(number_e,(((x^2-1)/(2*x))+(x^2+1)/sqrt((2*x)^2))) by A5,SQUARE_1:89
   .=log(number_e,((x^2-1)/(2*x)+(x^2+1)/(2*x))) by A2,SQUARE_1:89
   .=log(number_e,(((x^2-1)+(x^2+1))/(2*x))) by XCMPLX_1:63
   .=log(number_e,((2*x^2)/(2*x)))
   .=log(number_e,((x^2)/x)) by XCMPLX_1:92
   .=log(number_e,(x*x)/x) by SQUARE_1:def 3
   .=log(number_e,x/(x/x)) by XCMPLX_1:78
   .=log(number_e,x/1) by XCMPLX_1:60,A1
   .=log(number_e,x);
   hence thesis;
 end;

theorem
  y=(1/2)*(exp(x)-exp(-x)) implies x=log(number_e,(y+sqrt(y^2+1)))
  proof
    assume A1:y=(1/2)*(exp(x)-exp(-x));
    A2:exp(x)>0 by SIN_COS:60;
    A3:2*y*exp(x)=(exp(x)-1/exp(x))*exp(x) by A1,TAYLOR_1:4;
    set t=exp(x);
    2*y*t=t*t-(1/t)*t by A3;then
    2*y*t=t^2-(1/t)*t by SQUARE_1:def 3;then
    2*y*t=t^2-(t*1)/t by XCMPLX_1:75;then
    2*y*t-2*y*t=t^2-1-2*y*t by A2,XCMPLX_1:60;then
    C1:1*t^2+(-2*y)*t+(-1)=0 by XCMPLX_1:14;
    A7:delta(1,-2*y,-1)=((-2)*y)^2-4*1*(-1) by QUIN_1:def 1
         .=(-2)^2*(y^2)-4*1*(-1) by SQUARE_1:68
         .=(-2)*(-2)*(y^2)-4*1*(-1) by SQUARE_1:def 3
         .=4*(y^2)+4;
    A9:0<=y^2 by SQUARE_1:72;then
    A10:0+1<=y^2+1 by AXIOMS:24;
    4*0<=4*y^2 by A9,AXIOMS:25;then
    0+4<=4*y^2+4 by AXIOMS:24;then
    t = (-(-2*y)+sqrt delta(1,-2*y,-1))/(2*1)
        or t = (-(-2*y)-sqrt delta(1,-2*y,-1))/(2*1)
             by QUIN_1:15,A7,C1;then
    t=(2*y+sqrt(4)*sqrt(y^2+1))/2
           or t=(2*y-sqrt(4*(y^2+1)))/2 by A7,SQUARE_1:97,A10;then
    t=(2*y+2*sqrt(y^2+1))/2
           or t=(2*y-2*sqrt(y^2+1))/2 by SQUARE_1:85,97,A10;then
T1: exp(x)=y+sqrt(y^2+1) or exp(x)=y-sqrt(y^2+1);
    y<sqrt(y^2+1)+0 by Lm22;
    hence thesis by A2,T1,TAYLOR_1:12,REAL_1:84;
  end;

theorem
  y=(1/2)*(exp(x)+exp(-x)) & 1<=y implies 
  x=log(number_e,(y+sqrt(y^2-1))) or x=-log(number_e,(y+sqrt(y^2-1))) 
  proof
    assume A1:y=(1/2)*(exp(x)+exp(-x)) & 1<=y;then       
    A10:0<=(y^2)-1 by Th29;then
B1: 0*4<=((y^2)-1)*4 by AXIOMS:25;
    A12:y+sqrt(y^2-1)>0 by A1,Lm25;then
    B2:1/(y+sqrt(y^2-1))=(y+sqrt(y^2-1)) to_power (-1) by Th4;
    A2:0<exp(x)by SIN_COS:60;
    A3:2*y*exp(x)=(exp(x)+1/exp(x))*exp(x) by A1,TAYLOR_1:4;
    set t=exp(x);
    2*y*t=t*t+(1/t)*t by A3;then
    2*y*t=t^2+(1/t)*t by SQUARE_1:def 3;then
    2*y*t=t^2+(t*1)/t by XCMPLX_1:75;then
    2*y*t-2*y*t=t^2+1-2*y*t by A2,XCMPLX_1:60;then
    C1:0=1*t^2+(-2*y)*t+1 by XCMPLX_1:14;
    A7:delta(1,-2*y,1)=((-2)*y)^2-4*1*1 by QUIN_1:def 1
         .=(-2)^2*(y^2)-4*1*1 by SQUARE_1:68
         .=(-2)*(-2)*(y^2)-4*1*1 by SQUARE_1:def 3
         .=4*(y^2)-4;then
    A8:0<=delta(1,-2*y,1) by B1;
    t = (-(-2*y)+sqrt delta(1,-2*y,1))/(2*1)
        or t = (-(-2*y)-sqrt delta(1,-2*y,1))/(2*1)
              by A7,B1,C1,QUIN_1:15;then
              t=(2*y+sqrt(4)*sqrt(y^2-1))/2
           or t=(2*y-sqrt(4*(y^2-1)))/2 by A7,SQUARE_1:97,A10;then
A11:          t=(2*y+2*sqrt(y^2-1))/2
           or t=(2*y-2*sqrt(y^2-1))/2 by SQUARE_1:85,97,A10;
    log(number_e,y+sqrt(y^2-1))=x
           or log(number_e,y-sqrt(y^2-1))=x by A11,TAYLOR_1:12;then
              log(number_e,y+sqrt(y^2-1))=x 
           or log(number_e,(y-sqrt(y^2-1))*(y+sqrt(y^2-1))/
              (y+sqrt(y^2-1)))=x by A12,XCMPLX_1:90;then      
              log(number_e,y+sqrt(y^2-1)) = x 
           or log(number_e,(y^2-(sqrt(y^2-1))^2)/
              (y+sqrt(y^2-1)))=x by SQUARE_1:67;then          
              log(number_e,y+sqrt(y^2-1))=x 
           or log(number_e,(y^2-(y^2-1))/(y+sqrt(y^2-1)))=x 
       by A10,SQUARE_1:def 4;then               
              log(number_e,y+sqrt(y^2-1))=x 
           or (-1)*log(number_e,(y+sqrt(y^2-1)))=x by POWER:63,Lm2,A12,B2;          
     hence thesis;
  end;    

theorem
  y=(exp(x)-exp(-x))/(exp(x)+exp(-x)) implies
  x=(1/2)*log(number_e,(1+y)/(1-y))
 proof
  A1:0<exp(x)by SIN_COS:60;then
     0*exp(x)<exp(x)*exp(x) by REAL_1:70;then
     0<(exp x)^2 by SQUARE_1:def 3;then
A2:  0+1<(exp x)^2+1 by REAL_1:67;
  assume y=(exp(x)-exp(-x))/(exp(x)+exp(-x));then
     y=(exp(x)-1/exp(x))/(exp(x)+exp(-x)) by TAYLOR_1:4;then
     y=(exp(x)-1/exp(x))/(exp(x)+1/exp(x)) by TAYLOR_1:4;then
     y=((exp(x)*exp(x)-1)/exp(x))/(exp(x)+1/exp(x))
        by A1,XCMPLX_1:128;then
     y=((exp(x)*exp(x)-1)/exp(x))/((1+exp(x)*exp(x))/exp(x))
        by A1,XCMPLX_1:114;then
     y=(((exp x)^2-1)/exp(x))/((1+exp(x)*exp(x))/exp(x))
        by SQUARE_1:def 3;then
     y=(((exp x)^2-1)/exp(x))/((1+(exp(x))^2)/exp(x))
        by SQUARE_1:def 3;then
  A4:y=((exp x)^2-1)/(1+(exp(x))^2) by A1,XCMPLX_1:55;then
  A5:-1<y & y<1 by A1,Lm26,Th65;then
  A6:y-1<>0 by XCMPLX_1:15;
  A7:0<(y+1)/(1-y) by A5,Th66;then
  A8:sqrt((y+1)/(1-y))=((y+1)/(1-y)) to_power (1/2) by ASYMPT_1:89;
   set t=exp(x);
   1*y+t^2*y=(t^2-1)/(1+t^2)*(1+t^2) by A4;then
   y+t^2*y-(t^2-1)=t^2-1-(t^2-1) by A2,XCMPLX_1:88;then
   y+t^2*y-t^2+1=0 by XCMPLX_1:37;then
   t^2*(y-1)=0-(y+1);then
   t^2*(y-1)/(y-1)=(-(y+1))/(y-1);then
   t^2*(y-1)/(y-1)=(y+1)/(-(y-1)) by XCMPLX_1:193;then
   sqrt(t^2)=sqrt((y+1)/(1-y)) by A6,XCMPLX_1:90;then
A9:exp(x)=sqrt((y+1)/(1-y)) by A1,SQUARE_1:89;
log(number_e,((y+1)/(1-y)) to_power (1/2))=x by A9,TAYLOR_1:12,A8;
  hence thesis by Lm2,A7,POWER:63;
 end;

theorem
  y=(exp(x)+exp(-x))/(exp(x)-exp(-x)) & x<>0 implies 
  x=(1/2)*log(number_e,(y+1)/(y-1))
   proof
     A1:0<exp(x)by SIN_COS:60;
     assume A2:y=(exp(x)+exp(-x))/(exp(x)-exp(-x)) & x<>0;then
     A3:(exp(x))^2-1<>0 by Th64;
     B1:exp(x)<>1 by A2,Th63;
     y=(exp(x)+1/exp(x))/(exp(x)-exp(-x)) by A2,TAYLOR_1:4;then
     y=(exp(x)+1/exp(x))/(exp(x)-1/exp(x)) by TAYLOR_1:4;then
     y=(exp(x)+1/exp(x))/((exp(x)*exp(x)-1)/exp(x))
       by A1,XCMPLX_1:128;then
     y=((1+exp(x)*exp(x))/exp(x))/((exp(x)*exp(x)-1)/exp(x))
       by A1,XCMPLX_1:114;then
     y=((1+exp(x)*exp(x))/exp(x))/(((exp(x))^2-1)/exp(x))
                   by SQUARE_1:def 3;then
     y=((1+(exp x)^2)/exp(x))/(((exp x)^2-1)/exp(x))
                   by SQUARE_1:def 3;then
     A4:y=(1+(exp x)^2)/((exp x)^2-1) by A1,XCMPLX_1:55;
     set t=exp x;
     B2:1<y or y< -1 by A4,A1,B1,Lm29;then
A5:     1-1<y-1 or y-1< -1-1 by REAL_1:92;
     A6:0<(y+1)/(y-1) by B2,Lm30;then
     A8:sqrt((y+1)/(y-1))=((y+1)/(y-1)) to_power (1/2) by ASYMPT_1:89;
        y*(t^2-1)=1+t^2 by A3,A4,XCMPLX_1:88;then
        y*t^2-y*1=1+t^2 by XCMPLX_1:40;then
        y*t^2-y-t^2=1+t^2-t^2;then
        y*t^2-t^2*1-y=1 by XCMPLX_1:21;then
        t^2*(y-1)/(y-1)=(y+1)/(y-1) by XCMPLX_1:40;then
        sqrt(t^2)=sqrt((y+1)/(y-1)) by A5,XCMPLX_1:90;then
      A9:exp(x)=sqrt((y+1)/(y-1)) by A1,SQUARE_1:89;
        log(number_e,((y+1)/(y-1)) to_power (1/2))=x by A8,TAYLOR_1:12,A9;
     hence thesis by Lm2,A6,POWER:63;
   end;

theorem
  y=1/((exp(x)+exp(-x))/2) implies x=log(number_e,(1+sqrt(1-y^2))/y)
  or x=-log(number_e,(1+sqrt(1-y^2))/y)
  proof
   A1:0<exp(x)by SIN_COS:60;
   C5:log(number_e,exp(x))=x by TAYLOR_1:12;
   assume y=1/((exp(x)+exp(-x))/2);then
   y=(1*2)/(2*((exp(x)+exp(-x))/2)) by XCMPLX_1:92;then
   y=2/(exp(x)+1/exp(x)) by TAYLOR_1:4;then
   y=2/((1+exp(x)*exp(x))/exp(x)) by A1,XCMPLX_1:114;then
   y=2/((1+(exp(x))^2)/exp(x)) by SQUARE_1:def 3;then
   A3:y=2*(exp(x)/(1+(exp(x))^2)) by XCMPLX_1:80;
   set t=exp(x);
   C1:y=2*t/(1+t^2) by A3,XCMPLX_1:75;
   C2: 0<y & y<=1 by C1,A1,Lm31,Lm32;then
   B2:0<=1-y^2 & 0<=4-4*y^2 & 0<1+sqrt(1-y^2) & 0<(1+sqrt(1-y^2))/y
       by Lm35,Lm36,Lm37,Lm38;then
   B3:1/((1+sqrt(1-y^2))/y)=((1+sqrt(1-y^2))/y) to_power (-1) by Th4;
   1+t^2>0 by Lm16;then
   y*(1+t^2)=(2*t) by C1,XCMPLX_1:88;then
   y*1+y*t^2=2*t by XCMPLX_1:8;then
   y+y*t^2-2*t=2*t-2*t;then
D1:y+y*t^2-2*t=0;
   A6:Poly2(y,-2,y,t)=y*t^2+(-2)*t+y by POLYEQ_1:def 2;
   A7:delta(y,-2,y)
     =(-2)^2-4*y*y by QUIN_1:def 1
    .=(-2)^2-4*(y*y)
    .=(-2)^2-4*y^2 by SQUARE_1:def 3
    .=(-2)*(-2)-4*y^2 by SQUARE_1:def 3
    .=4-4*y^2;
     t=(-(-2)+sqrt delta(y,-2,y))/(2*y)
  or t=(-(-2)-sqrt delta(y,-2,y))/(2*y)
       by A6,C2,D1,B2,A7,QUIN_1:15;then
     t=(2+sqrt(4*(1-y^2)))/(2*y)
  or t=(2-sqrt(4*(1-y^2)))/(2*y) by A7;then
     t=(2+2*sqrt(1-y^2))/(2*y)
  or t=(2-2*sqrt(1-y^2))/(2*y) by B2,SQUARE_1:85,97;then
     t=(2*(1+sqrt(1-y^2)))/(2*y)
  or t=(2*(1-sqrt(1-y^2)))/(2*y);then
     t=(1+sqrt(1-y^2))/y
  or t=(1-sqrt(1-y^2))/y by XCMPLX_1:92;then
     t=(1+sqrt(1-y^2))/y
  or t=((1-sqrt(1-y^2))*(1+sqrt(1-y^2)))/(y*(1+sqrt(1-y^2)))
      by B2,XCMPLX_1:92;then
     t=(1+sqrt(1-y^2))/y
  or t=(1-(sqrt(1-y^2))^2)/(y*(1+sqrt(1-y^2))) by SQUARE_1:59,67;then
     t=(1+sqrt(1-y^2))/y
  or t=(1-(1-y^2))/(y*(1+sqrt(1-y^2))) by B2,SQUARE_1:def 4;then
     t=(1+sqrt(1-y^2))/y
  or t=(y*y)/(y*(1+sqrt(1-y^2))) by SQUARE_1:def 3;then
     t=(1+sqrt(1-y^2))/y
  or t=y/(1+sqrt(1-y^2)) by C2,XCMPLX_1:92;then
     log(number_e,(1+sqrt(1-y^2))/y)=x
  or log(number_e,((1+sqrt(1-y^2))/y) to_power (-1))=x
       by B3,C5,XCMPLX_1:57;then
     log(number_e,(1+sqrt(1-y^2))/y)=x
  or (-1)*log(number_e,((1+sqrt(1-y^2))/y))=x by Lm2,B2,POWER:63;
    hence thesis;
  end;

theorem
  y=1/((exp(x)-exp(-x))/2) & x<>0 implies x=log(number_e,(1+sqrt(1+y^2))/y)
  or x=log(number_e,(1-sqrt(1+y^2))/y)
   proof
   A1:0<exp(x)by SIN_COS:60;
      assume A2:y=1/((exp(x)-exp(-x))/2) & x<>0;then
   A3:(exp(x))^2-1<>0 by Th64;
      exp(x)<>1 by A2,Th63;then
   B2:2*exp(x)/((exp(x))^2-1)<>0 by A1,Lm39;
      y=(1*2)/(2*((exp(x)-exp(-x))/2)) by A2,XCMPLX_1:92;then
      y=2/(exp(x)-1/exp(x)) by TAYLOR_1:4;then
      y=2/((exp(x)*exp(x)-1)/exp(x)) by A1,XCMPLX_1:128;then
      y=2/(((exp(x))^2-1)/exp(x)) by SQUARE_1:def 3;then
      y=2*(exp(x)/((exp(x))^2-1)) by XCMPLX_1:80;then
   A4:y=2*exp(x)/((exp(x))^2-1) by XCMPLX_1:75;
      set t=exp x;
      y*(t^2-1)=2*t by A3,A4,XCMPLX_1:88;then
      y*t^2-y*1-2*t=2*t-2*t by XCMPLX_1:40;then
      y*t^2-y*1-2*t=0;then
      D1:y*t^2+(-2)*t+(-y)=0;
      A7:delta(y,-2,-y)=(-2)^2-4*y*(-y) by QUIN_1:def 1
              .=(-2)^2+4*(y*y)
              .=(-2)^2+4*y^2 by SQUARE_1:def 3
              .=(-2)*(-2)+4*y^2 by SQUARE_1:def 3
              .=4+4*y^2;
      C2:y<>0 by B2,A4;
               per cases by C2;
                suppose 0<y;then
            C3:(1-sqrt(1+y^2))/y<0 & 0<(1+sqrt(1+y^2))/y by Lm33,Lm34;
            D2:0<1+y^2 by Lm16;then
               0*4<4*(1+y^2) by REAL_1:70;then
               t=(2+sqrt delta(y,-2,-y))/(2*y) 
            or t=(-(-2)-sqrt delta(y,-2,-y))/(2*y) by A7,C2,D1,QUIN_1:15;then
               t=(2+sqrt(4)*sqrt(1+y^2))/(2*y) 
            or t=(2-sqrt(4*(1+y^2)))/(2*y) by A7,D2,SQUARE_1:97;then
               t=(2*(1+sqrt(1+y^2)))/(2*y) 
            or t=(2-2*sqrt(1+y^2))/(2*y) by D2,SQUARE_1:85,97;then
               t=(1+sqrt(1+y^2))/y 
            or t=(2*(1-sqrt(1+y^2)))/(2*y) by XCMPLX_1:92;
           hence thesis by TAYLOR_1:12,A1,C3,XCMPLX_1:92;
           end;
           suppose y<0;then
            C3':0<(1-sqrt(1+y^2))/y & (1+sqrt(1+y^2))/y<0 by Lm40,Lm41;
            D2':0<1+y^2 by Lm16;then
               0*4<4*(1+y^2) by REAL_1:70;then
               t=(2+sqrt delta(y,-2,-y))/(2*y)
            or t=(-(-2)-sqrt delta(y,-2,-y))/(2*y) by A7,C2,D1,QUIN_1:15;then
               t=(2+sqrt(4)*sqrt(1+y^2))/(2*y)
            or t=(2-sqrt(4*(1+y^2)))/(2*y) by A7,D2',SQUARE_1:97;then
               t=(2*(1+sqrt(1+y^2)))/(2*y) 
            or t=(2-2*sqrt(1+y^2))/(2*y) by D2',SQUARE_1:85,97;then
               t=(1+sqrt(1+y^2))/y 
            or t=(2*(1-sqrt(1+y^2)))/(2*y) by XCMPLX_1:92;then
               exp(x)=(1-sqrt(1+y^2))/y by XCMPLX_1:92,A1,C3';
           hence thesis by TAYLOR_1:12;   
           end;
      end;

theorem M1:
  cosh.(2*x)=1+2*(sinh.x)^2
  proof
    2*(sinh.x)^2=2*(1/2*(cosh.(2*x)-1)) by SIN_COS2:18;
    hence thesis;
  end;

theorem M2:
  (cosh.x)^2=1+(sinh.x)^2
  proof
    cosh.(2*x)=cosh.(x+x)
      .=(cosh.x)*(cosh.x)+(sinh.x)*(sinh.x) by SIN_COS2:20
      .=(cosh.x)^2+(sinh.x)*(sinh.x) by SQUARE_1:def 3
      .=(cosh.x)^2+(sinh.x)^2 by SQUARE_1:def 3;then
    1+2*(sinh.x)^2-(sinh.x)^2=(cosh.x)^2+(sinh.x)^2-(sinh.x)^2 by M1;then
    1+(sinh.x)^2+(sinh.x)^2-(sinh.x)^2=(cosh.x)^2+(sinh.x)^2-(sinh.x)^2;
    hence thesis by XCMPLX_1:26;
  end;

theorem M3:
  (sinh.x)^2=(cosh.x)^2-1
  proof
    (cosh.x)^2-(sinh.x)^2-1=1-1 by SIN_COS2:14;then
    (cosh.x)^2-(sinh.x)^2-1=0;then
    (cosh.x)^2-(sinh.x)^2+(sinh.x)^2-1=0+(sinh.x)^2;
    hence thesis;
  end;

theorem
  sinh(5*x)=5*sinh(x)+20*(sinh(x))|^3+16*(sinh(x))|^5
  proof
    set t = sinh.x;
    sinh(5*x) =sinh.(4*x+x) by SIN_COS2:def 2
  .=sinh.(2*(2*x))*(cosh.x)+(cosh.(4*x))*t by SIN_COS2:21
  .=2*(sinh.(2*x))*(cosh.(2*x))*(cosh.x)+(cosh.(4*x))*t by SIN_COS2:23
  .=2*(2*t*(cosh.x))*(cosh.(2*x))*(cosh.x)+(cosh.(4*x))*t by SIN_COS2:23
  .=2*2*t*((cosh.x)*(cosh.x))*(cosh.(2*x))+(cosh.(4*x))*(sinh.x)
  .=4*t*((cosh.x)^2)*(cosh.(2*x))+(cosh.(4*x))*t by SQUARE_1:def 3
  .=4*t*((cosh.x)^2)*(1+2*t^2)+(cosh.(2*(2*x)))*t by M1
  .=4*t*((cosh.x)^2)*(1+2*t^2)+(1+2*(sinh.(2*x))^2)*t by M1
  .=4*t*((cosh.x)^2)*(1+2*t^2)+(1+2*(2*t*
     (cosh.x))^2)*t by SIN_COS2:23
  .=4*t*((cosh.x)^2)*(1+2*t^2)+(1+2*((2*t)^2*
     (cosh.x)^2))*t by SQUARE_1:68
  .=4*t*((cosh.x)^2)*(1+2*t^2)+(1+2*(2^2*t^2*
     (cosh.x)^2))*t by SQUARE_1:68
  .=4*t*((cosh.x)^2)*(1+2*t^2)+(1+2*(2*2*t^2*
     (cosh.x)^2))*t by SQUARE_1:def 3
  .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*t^2*t*(cosh.x)^2
  .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*(t*t)*t*(cosh.x)^2 by SQUARE_1:def 3
  .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*(t|^1*t)*t*(cosh.x)^2 by NEWTON:10
  .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*t|^(1+1)*t*(cosh.x)^2 by NEWTON:11
  .=4*t*((cosh.x)^2)*(1+2*t^2)+sinh.x+8*t|^2*t*(cosh.x)^2
  .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*(t|^2*t)*(cosh.x)^2
  .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*(t|^(2+1))*(cosh.x)^2 by NEWTON:11
  .=4*t*((cosh.x)^2)*1+4*t*((cosh.x)^2)*2*t^2+t+8*t|^3*(cosh.x)^2
  .=4*t*(1+t^2)*1+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2 by M2
  .=4*t+4*t*t^2+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t*(t*t)+8*t*((cosh.x)^2)*
     t^2+t+8*t|^3*(cosh.x)^2 by SQUARE_1:def 3
  .=4*t+4*t*t*t+8*t*((cosh.x)^2)*t^2+sinh.x+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^1*t*t+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2 by NEWTON:10
  .=4*t+4*(t|^1*t)*t+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^(1+1)*t+8*t*((cosh.x)^2)*
     t^2+t+8*t|^3*(cosh.x)^2 by NEWTON:11
  .=4*t+4*t|^2*t+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2
  .=4*t+4*(t|^2*t)+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2
  .=4*t+4*(t|^(2+1))+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2 by NEWTON:11
  .=4*t+4*t|^3+8*t*t^2*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*t*(t*t)*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2 by SQUARE_1:def 3
  .=4*t+4*t|^3+8*t*t*t*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*t|^1*t*t*
     ((cosh.x)^2)+t+8*t|^3*(cosh.x)^2 by NEWTON:10
  .=4*t+4*t|^3+8*(t|^1*t)*t*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*(t|^(1+1))*t*
     ((cosh.x)^2)+t+8*t|^3*(cosh.x)^2 by NEWTON:11
  .=4*t+4*t|^3+8*t|^2*t*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*(t|^2*t)*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*(t|^(2+1))*
     ((cosh.x)^2)+t+8*t|^3*(cosh.x)^2 by NEWTON:11
  .=4*t+4*t|^3+8*t|^3*(1+t^2)+t+8*t|^3*(cosh.x)^2 by M2
  .=4*t+4*t|^3+8*t|^3+8*t|^3*t^2+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*t|^3+8*t|^3*(t*t)+t+8*t|^3*(cosh.x)^2 by SQUARE_1:def 3
  .=4*t+4*t|^3+8*t|^3+8*(t|^3*t)*t+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*t|^3+8*(t|^(3+1))*t+t+8*t|^3*(cosh.x)^2 by NEWTON:11
  .=4*t+4*t|^3+8*t|^3+8*t|^4*t+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*t|^3+8*(t|^4*t)+t+8*t|^3*(cosh.x)^2
  .=4*t+4*t|^3+8*t|^3+8*(t|^(4+1))+t+8*t|^3*(cosh.x)^2 by NEWTON:11
  .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3*(1+t^2) by M2
  .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^3)*(t^2)
  .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^3)*(t*t) by SQUARE_1:def 3
  .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^3*t)*t
  .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^(3+1))*t by NEWTON:11
  .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*t|^4*t
  .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^4*t)
  .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^(4+1)) by NEWTON:11
  .=4*t+t+4*t|^3+8*t|^3+8*t|^3+8*t|^5+8*t|^5
  .=5*t+20*t|^3+(8*t|^5+8*t|^5)
  .=5*sinh(x)+20*t|^3+16*t|^5 by SIN_COS2:def 2
  .=5*sinh(x)+20*(sinh x)|^3+16*t|^5 by SIN_COS2:def 2
  .=5*sinh(x)+20*(sinh x)|^3+16*(sinh x)|^5 by SIN_COS2:def 2;
   hence thesis;
  end;

theorem
  cosh(5*x)=5*cosh(x)-20*(cosh(x))|^3+16*(cosh(x))|^5
  proof
    set t = cosh.x, u = sinh.x;
    cosh(5*x) = cosh.(4*x+x) by SIN_COS2:def 4
   .=(cosh.(2*(2*x)))*t+(sinh.(4*x))*u by SIN_COS2:20
   .=(1+2*(sinh.(2*x))^2)*t+(sinh.(4*x))*u by M1
   .=(1+2*((2*u)*t)^2)*t+(sinh.(2*(2*x)))*u by SIN_COS2:23
   .=(1+2*((2*u)^2*t^2))*t+(sinh.(2*(2*x)))*u by SQUARE_1:68
   .=(1+2*((2^2*u^2)*t^2))*t+(sinh.(2*(2*x)))*u by SQUARE_1:68
   .=(1+2*((2*2*u^2)*t^2))*t+(sinh.(2*(2*x)))*u by SQUARE_1:def 3
   .=t+8*u^2*(t^2*t)+(sinh.(2*(2*x)))*u
   .=t+8*u^2*(t*t*t)+(sinh.(2*(2*x)))*u by SQUARE_1:def 3
   .=t+8*u^2*(t|^1*t*t)+(sinh.(2*(2*x)))*u by NEWTON:10
   .=t+8*u^2*(t|^(1+1)*t)+(sinh.(2*(2*x)))*u by NEWTON:11
   .=t+8*(sinh.x)^2*(t|^(2+1))+(sinh.(2*(2*x)))*u by NEWTON:11
   .=t+8*(t^2-1)*(t|^3)+(sinh.(2*(2*x)))*u by M3
   .=t+8*(t|^3*t^2)-8*t|^3+(sinh.(2*(2*x)))*u
   .=t+8*(t|^3*(t*t))-8*t|^3+(sinh.(2*(2*x)))*u by SQUARE_1:def 3
   .=t+8*(t|^3*t*t)-8*t|^3+(sinh.(2*(2*x)))*u
   .=t+8*(t|^(3+1)*t)-8*t|^3+(sinh.(2*(2*x)))*u by NEWTON:11
   .=t+8*(t|^(4+1))-8*t|^3+(sinh.(2*(2*x)))*u by NEWTON:11
   .=t+8*(t|^5)-8*t|^3+(2*(sinh.(2*x))*(cosh.(2*x)))*u by SIN_COS2:23
   .=t+8*(t|^5)-8*t|^3+(2*(2*u*t)*(cosh.(2*x)))*u by SIN_COS2:23
   .=t+8*(t|^5)-8*t|^3+4*(u*u)*t*(cosh.(2*x))
   .=t+8*(t|^5)-8*t|^3+4*u^2*t*(cosh.(2*x)) by SQUARE_1:def 3
   .=t+8*(t|^5)-8*t|^3+4*(t^2-1)*t*(cosh.(2*x)) by M3
   .=t+8*(t|^5)-8*t|^3+4*(t^2*t)*(cosh.(2*x))-4*t*(cosh.(2*x))
   .=t+8*(t|^5)-8*t|^3+4*(t*t*t)*(cosh.(2*x))-4*t*(cosh.(2*x))
      by SQUARE_1:def 3
   .=t+8*(t|^5)-8*t|^3+4*(t|^1*t*t)*(cosh.(2*x))-4*t*(cosh.(2*x))
      by NEWTON:10
   .=t+8*(t|^5)-8*t|^3+4*(t|^(1+1)*t)*(cosh.(2*x))-4*t*(cosh.(2*x))
      by NEWTON:11
   .=t+8*(t|^5)-8*t|^3+4*(t|^(2+1))
      *(cosh.(2*x))-4*t*(cosh.(2*x)) by NEWTON:11
   .=t+8*(t|^5)-8*t|^3+4*t|^3*(2*t^2-1)-4*t*(cosh.(2*x)) by SIN_COS2:23
   .=t+8*(t|^5)-8*t|^3+4*t|^3*2*t^2-4*t|^3-4*t*(cosh.(2*x))
   .=t+8*(t|^5)-8*t|^3+4*t|^3*2*(t*t)-4*t|^3-4*t*(cosh.(2*x))
      by SQUARE_1:def 3
   .=t+8*(t|^5)-8*t|^3+8*(t|^3*t*t)-4*t|^3-4*t*(cosh.(2*x))
   .=t+8*(t|^5)-8*t|^3+8*(t|^(3+1)*t)-4*t|^3-4*t*(cosh.(2*x)) by NEWTON:11
   .=t+8*(t|^5)-8*t|^3+8*(t|^(4+1))-4*t|^3-4*t*(cosh.(2*x)) by NEWTON:11
   .=t+8*(t|^5)-8*t|^3+8*(t|^5)-4*t|^3-4*t*(cosh.(2*x))
   .=t+16*(t|^5)-12*t|^3-4*t*(2*t^2-1) by SIN_COS2:23
   .=5*t+16*(t|^5)-12*t|^3-4*2*t*t^2
   .=5*t+16*(t|^5)-12*t|^3-8*t*(t*t) by SQUARE_1:def 3
   .=5*t+16*(t|^5)-12*t|^3-8*(t*t*t)
   .=5*t+16*(t|^5)-12*t|^3-8*(t|^1*t*t) by NEWTON:10
   .=5*t+16*(t|^5)-12*t|^3-8*(t|^(1+1)*t) by NEWTON:11
   .=5*t+16*(t|^5)-12*t|^3-8*(t|^(2+1)) by NEWTON:11
   .=5*t+16*(t|^5)-12*t|^3-8*t|^3
   .=5*t-20*t|^3+16*(t|^5)
   .=5*(cosh x)-20*t|^3+16*(t|^5) by SIN_COS2:def 4
   .=5*(cosh x)-20*(cosh x)|^3+16*(t|^5) by SIN_COS2:def 4
   .=5*cosh(x)-20*(cosh x)|^3+16*(cosh x)|^5 by SIN_COS2:def 4;
  hence thesis;
  end;
