:: Formulas And Identities of Inverse Hyperbolic Functions
::  by Fuguo Ge , Xiquan Liang and Yuzhong Ding
::
:: Received May 24, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies ARYTM, ARYTM_3, ARYTM_1, SQUARE_1, FUNCT_1, SIN_COS2, GROUP_1,
      POWER, ABSVALUE, FUNCT_3, SIN_COS7, SIN_COS, POLYEQ_1;
 notations ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, SQUARE_1, NEWTON,
      QUIN_1, COMPLEX1, SIN_COS, SEQ_1, BINOP_2, SIN_COS2, POWER, POLYEQ_1;
 constructors REAL_1, SQUARE_1, NAT_1, BINOP_2, SEQ_1, QUIN_1, POWER, POLYEQ_1,
      SIN_COS, SIN_COS2, VALUED_1;
 registrations XXREAL_0, XREAL_0, SQUARE_1, MEMBERED, QUIN_1, SIN_COS,
      XBOOLE_0, VALUED_0, RELSET_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions SQUARE_1;
 theorems REAL_2, SQUARE_1, ABSVALUE, SIN_COS, NEWTON, XCMPLX_1,
      QUIN_1, SIN_COS2, TAYLOR_1, POWER, POLYEQ_1, XREAL_1, COMPLEX1, ASYMPT_1,
      XXREAL_0;

begin

reserve x,y,t for real number;

Lm1: 1/1 = 1;

Lm2: number_e>0 & number_e<>1 by TAYLOR_1:11;

Lm3: x^2<1 & y<1 implies x^2*y<1
proof
  assume
A1: x^2<1 & y<1;
  per cases by XREAL_1:65;
  suppose 0=x^2;
    hence thesis;
  end;
  suppose 0<x^2;
    then y*x^2<1*x^2 by A1,XREAL_1:70;
    hence thesis by A1,XXREAL_0:2;
  end;
end;

theorem Th1:
  x>0 implies 1/x=x to_power (-1)
proof
  assume x>0;
  then x to_power (-1) =(1/x) to_power 1 by POWER:37
    .=1/x by POWER:30;
  hence thesis;
end;

theorem Th2:
  x>1 implies ((sqrt(x^2-1))/x)^2<1
proof
  assume x>1;
  then x^2>1+0 by SQUARE_1:59,78;
  then
A1: x^2-1>0 by XREAL_1:22;
A2: -1+x^2<0+x^2 by XREAL_1:8;
  ((sqrt(x^2-1))/x)^2=(sqrt(x^2-1))^2/x^2 by XCMPLX_1:77
    .=(x^2-1)/x^2 by A1,SQUARE_1:def 4;
  hence thesis by A1,A2,REAL_2:142;
end;

theorem Th3:
  (x/sqrt(x^2+1))^2<1
proof
  x^2>=0 by XREAL_1:65;
  then
A1: x^2+1>=0+1 by XREAL_1:9;
A2: x^2<x^2+1 by XREAL_1:31;
  (sqrt(x^2+1))^2=sqrt(x^2+1)^2 by A1,SQUARE_1:97
    .=x^2+1 by A1,SQUARE_1:89;
  then
A3: (x/sqrt(x^2+1))^2=(x^2)/(x^2+1) by XCMPLX_1:77;
  per cases by XREAL_1:65;
  suppose x^2>0;
    hence thesis by A2,A3,REAL_2:142;
  end;
  suppose x^2=0;
    hence thesis by A3;
  end;
end;

theorem Th4:
  sqrt(x^2+1)>0
proof
  x^2>=0 by XREAL_1:65;
  then x^2+1>=0+1 by XREAL_1:9;
  hence thesis by SQUARE_1:93;
end;

theorem Th5:
  sqrt(x^2+1)+x>0
proof
  per cases;
  suppose x>0;
    hence thesis by Th4,XREAL_1:31;
  end;
  suppose
A1: x<=0;
    x^2<x^2+1 by XREAL_1:31;
    then sqrt(x^2)<sqrt(x^2+1) by SQUARE_1:95,XREAL_1:65;
    then -x<sqrt(x^2+1) by A1,SQUARE_1:90;
    hence thesis by XREAL_1:64;
  end;
end;

Lm4: 1<=x implies x^2-1>=0
proof
  assume 1<=x;
  then x^2>=1 by SQUARE_1:59,77;
  then 1+(-1)<=x^2+(-1) by XREAL_1:8;
  hence thesis;
end;

theorem
  y>=0 & x>=1 implies (x+1)/y>=0;

theorem Th7:
  y>=0 & x>=1 implies (x-1)/y>=0
proof
  assume
A1: y>=0 & x>=1;
  then x+(-1)>=1+(-1) by XREAL_1:8;
  hence thesis by A1;
end;

theorem Th8:
  x>=1 implies sqrt((x+1)/2)>=1
proof
  assume x>=1;
  then x+1>=1+1 by XREAL_1:8;
  then (x+1)/2>=1 by REAL_2:143;
  hence thesis by SQUARE_1:83,94;
end;

theorem Th9:
  y>=0 & x>=1 implies (x^2-1)/y>=0
proof
  assume
A1: y>=0 & x>=1;
  then x^2-1>=0 by Lm4;
  hence thesis by A1;
end;

theorem Th10:
  x>=1 implies sqrt((x+1)/2)+sqrt((x-1)/2)>0
proof
  assume
A1: x>=1;
  then
A2: sqrt((x+1)/2)>=1 by Th8;
  (x-1)/2>=0 by A1,Th7;
  then sqrt((x-1)/2)>=0 by SQUARE_1:82,94;
  then sqrt((x-1)/2)+1>=0+1 by XREAL_1:8;
  hence thesis by A2,XREAL_1:8;
end;

theorem Th11:
  x^2<1 implies x+1>0 & 1-x>0
proof
  assume x^2<1;
  then -1<x & x<1 by SQUARE_1:122;
  hence thesis by XREAL_1:150,151;
end;

Lm5: x^2<1 implies (x+1)/(1-x)>0
proof
  assume x^2<1;
  then x+1>0 & 1-x>0 by Th11;
  hence thesis by REAL_2:127;
end;

theorem Th12:
  x<>1 implies (1-x)^2>0
proof
  assume x<>1;
  then 0<>1-x;
  hence thesis by SQUARE_1:74;
end;

theorem Th13:
  x^2<1 implies (x^2+1)/(1-x^2)>=0
proof
  assume x^2<1;
  then
A1: 0<1-x^2 by XREAL_1:52;
  x^2>=0 by XREAL_1:65;
  then x^2+1>=0+1 by XREAL_1:8;
  hence thesis by A1;
end;

theorem Th14:
  x^2<1 implies ((2*x)/(1+x^2))^2<1
proof
  assume
A1: x^2<1;
  x^2>=0 by XREAL_1:65;
  then
A2: 4*x^2>=4*0;
  x^2+(-1)<1+(-1) by A1,XREAL_1:10;
  then (x^2-1)*(x^2-1)>0*(x^2-1) by XREAL_1:71;
  then
A3: (x^2)^2-2*(x^2)+1+4*x^2>0+4*x^2 by XREAL_1:10;
  ((2*x)/(1+x^2))^2 = (2*x)^2/(1+x^2)^2 by XCMPLX_1:77
    .=4*x^2/((x^2)^2+2*(x^2)+1);
  hence thesis by A2,A3,XREAL_1:191;
end;

theorem Th15:
  0<x & x<1 implies (1+x)/(1-x)>0
proof
  assume
A1: 0<x & x<1;
  then x^2<x by SQUARE_1:75;
  then x^2<1 by A1,XXREAL_0:2;
  hence thesis by Lm5;
end;

theorem Th16:
  0<x & x<1 implies x^2<1
proof
  assume
A1: 0<x & x<1;
  then x^2<x by SQUARE_1:75;
  hence thesis by A1,XXREAL_0:2;
end;

Lm7: 0<x & x<1 implies 0<1-x^2
proof
  assume
A1: 0<x & x<1;
  then x^2<x by SQUARE_1:75;
  then x^2<1 by A1,XXREAL_0:2;
  then x^2-x^2<1-x^2 by XREAL_1:11;
  hence thesis;
end;

theorem Th17:
  0<x & x<1 implies 1/(sqrt(1-x^2))>1
proof
  assume
A1: 0<x & x<1;
  then
A2: 0<1-x^2 by Lm7;
  then
A3: 0<sqrt(1-x^2) by SQUARE_1:93;
  x*x>0*x by A1,XREAL_1:70;
  then x^2*(-1)<0*(-1) by XREAL_1:71;
  then -x^2+1<0+1 by XREAL_1:10;
  then sqrt(1-x^2)<1 by A2,SQUARE_1:83,95;
  hence thesis by A3,REAL_2:142;
end;

theorem Th18:
  0<x & x<1 implies (2*x)/(1-x^2)>0
proof
  assume
A1: 0<x & x<1;
  then
A2: 0*2<x*2 by XREAL_1:70;
  x^2<x by A1,SQUARE_1:75;
  then x^2<1 by A1,XXREAL_0:2;
  then x^2+(-x^2)<1+(-x^2) by XREAL_1:10;
  hence thesis by A2,REAL_2:127;
end;

theorem Th19:
  0<x & x<1 implies 0<(1-x^2)^2
proof
  assume
A1: 0<x & x<1;
  then x^2<x by SQUARE_1:75;
  then x^2<1 by A1,XXREAL_0:2;
  then x^2+(-x^2)<1+(-x^2) by XREAL_1:10;
  hence thesis by SQUARE_1:74;
end;

theorem Th20:
  0<x & x<1 implies ((1+x^2)/(1-x^2))>1
proof
  assume
A1: 0<x & x<1;
  then x^2<x by SQUARE_1:75;
  then x^2<1 by A1,XXREAL_0:2;
  then
A2: x^2+(-x^2)<1+(-x^2) by XREAL_1:10;
  0*x<x*x by A1,XREAL_1:70;
  then 0*2<(x^2)*2 by XREAL_1:70;
  then 0+(-x^2)<2*(x^2)+(-x^2) by XREAL_1:10;
  then -x^2+1<x^2+1 by XREAL_1:10;
  hence thesis by A2,REAL_2:142;
end;

theorem Th21:
  1<x^2 implies (1/x)^2<1
proof
  assume
A1: 1<x^2;
  then (1/x^2)<((x^2)/(x^2)) by XREAL_1:76;
  then (1^2/x^2)<1 by A1,XCMPLX_1:60;
  hence thesis by XCMPLX_1:77;
end;

theorem Th22:
  0<x & x<=1 implies 1-x^2>=0
proof
  assume
A1: 0<x & x<=1;
  then
A2: x-1<=1-1 by XREAL_1:11;
  per cases by A2;
  suppose x-1<0;
    then x-1+1<0+1 by XREAL_1:10;
    hence thesis by A1,Lm7;
  end;
  suppose x-1=0;
    hence thesis;
  end;
end;

theorem Th23:
  1<=x implies 0<x+sqrt(x^2-1)
proof
  assume
A1: 1<=x;
  then x^2-1>=0 by Lm4;
  then 0<=sqrt(x^2-1) by SQUARE_1:def 4;
  then 0+x<=sqrt(x^2-1)+x by XREAL_1:8;
  hence thesis by A1;
end;

theorem Th24:
  1<=x & 1<=y implies 0<=x*sqrt(y^2-1)+y*sqrt(x^2-1)
proof
  assume
A1: 1<=x & 1<=y;
  then 0<=x^2-1 & 0<=y^2-1 by Lm4;
  then 0<=sqrt(x^2-1) & 0<=sqrt(y^2-1) by SQUARE_1:def 4;
  then
A2: 0*y<=sqrt(x^2-1)*y & 0*x<=sqrt(y^2-1)*x by A1;
  thus thesis by A2;
end;

theorem Th25:
  1<=x & 1<=y & abs(y)<=abs(x) implies 0<y-sqrt(y^2-1)
proof
  assume
A1: 1<=x & 1<=y & abs(y)<=abs(x);
  then 1*y<=y*y by XREAL_1:66;
  then 1<=y^2 by A1,XXREAL_0:2;
  then
A2: 1-1<=y^2-1 by XREAL_1:15;
  -1+y^2<0+y^2 by XREAL_1:10;
  then sqrt(y^2-1)<sqrt(y^2) by A2,SQUARE_1:95;
  then sqrt(y^2-1)<y by A1,SQUARE_1:89;
  then sqrt(y^2-1)-sqrt(y^2-1)<y-sqrt(y^2-1) by XREAL_1:16;
  hence thesis;
end;

theorem Th26:
  1<=x & 1<=y & abs(y)<=abs(x) implies 0<=y*sqrt(x^2-1)-x*sqrt(y^2-1)
proof
  assume
A1: 1<=x & 1<=y & abs(y)<=abs(x);
A2: (abs x)^2=x^2 & (abs y)^2=y^2 by COMPLEX1:161;
A3: 0<=x^2 & 0<=y^2 by XREAL_1:65;
A4: 0<=x^2-1 & 0<=y^2-1 by A1,Lm4;
  1<=x^2 & 1<=y^2 by A1,SQUARE_1:59,77;
  then
A5: 1*(y^2-1)<=x^2*(y^2-1) by A4,XREAL_1:66;
  1<=abs(y) by A1,ABSVALUE:def 1;
  then y^2<=x^2 by A1,A2,SQUARE_1:77;
  then (-1)*x^2<=(-1)*y^2 by XREAL_1:67;
  then -x^2+x^2*y^2<=-y^2+x^2*y^2 by XREAL_1:8;
  then sqrt(x^2*(y^2-1))<=sqrt(y^2*(x^2-1)) by A4,A5,SQUARE_1:94;
  then sqrt(x^2)*sqrt(y^2-1)<=sqrt(y^2*(x^2-1)) by A3,A4,SQUARE_1:97;
  then x*sqrt(y^2-1)<=sqrt(y^2*(x^2-1)) by A1,SQUARE_1:89;
  then x*sqrt(y^2-1)<=sqrt(y^2)*sqrt(x^2-1) by A3,A4,SQUARE_1:97;
  then x*sqrt(y^2-1)<=y*sqrt(x^2-1) by A1,SQUARE_1:89;
  then x*sqrt(y^2-1)-x*sqrt(y^2-1)<=y*sqrt(x^2-1)-x*sqrt(y^2-1)
   by XREAL_1:15;
  hence thesis;
end;

theorem Th27:
  x^2<1 & y^2<1 implies x*y<>-1
proof
  assume
A1: x^2<1 & y^2<1;
  assume x*y=-1;
  then x*y*(x*y)=1;
  then (x*x)*(y*y)=1;
  hence contradiction by A1,Lm3;
end;

theorem Th28:
  x^2<1 & y^2<1 implies x*y<>1
proof
  assume
A1: x^2<1 & y^2<1;
  assume x*y=1;
  then x*y*(x*y)=1;
  then (x*x)*(y*y)=1;
  hence contradiction by A1,Lm3;
end;

theorem Th29:
  x<>0 implies exp_R(x)<>1
proof
  assume
A1: x<>0;
  assume
A2: exp_R(x)=1;
  x=log(number_e,exp_R(x)) by TAYLOR_1:12
    .=0 by A2,Lm2,POWER:59;
  hence contradiction by A1;
end;

theorem Th30:
  0<>x implies (exp_R x)^2-1<>0
proof
  assume
A1: 0<>x;
  assume (exp_R x)^2-1=0;
  then exp_R x=1 or exp_R x= -1 by XCMPLX_1:183;
  hence contradiction by A1,Th29,SIN_COS:60;
end;

Lm8: x^2+1>0
proof
  x^2>=0 by XREAL_1:65;
  then x^2+1>=0+1 by XREAL_1:9;
  hence thesis;
end;

theorem Th31:
  0<t implies (t^2-1)/(t^2+1)<1
proof
A1: 0<t^2+1 by Lm8;
  assume 0<t;
  -1+t^2<1+t^2 by XREAL_1:10;
  then (-1+t^2)/((1+t^2))<(1+t^2)/((1+t^2)) by A1,XREAL_1:76;
  hence thesis by A1,XCMPLX_1:60;
end;

theorem Th32:
  -1<t & t<1 implies 0<(t+1)/(1-t)
proof
  assume -1<t & t<1;
  then -1+1<t+1 & t-t<1-t by XREAL_1:16,XREAL_1:10;
  then 0/(1-t)<(t+1)/(1-t) by XREAL_1:76;
  hence thesis;
end;

Lm9: 1<t or t< -1 implies 0<(t+1)/(t-1)
proof
  assume
A1: 1<t or t < -1;
  per cases by A1;
  suppose
A2: 1<t;
    then
A3: 1+1<t+1 by XREAL_1:10;
    1-1<t-1 by A2,XREAL_1:16;
    then 0/(t-1)<(t+1)/(t-1) by A3,XREAL_1:76;
    hence thesis;
  end;
  suppose
A4: t< -1;
    then
A5: t+1< -1+1 by XREAL_1:10;
    t-1< -1-1 by A4,XREAL_1:16;
    then 0/(t-1)<(t+1)/(t-1) by A5,XREAL_1:77;
    hence thesis;
  end;
end;

Lm10: sqrt(x^2+1)>x
proof
  per cases;
  suppose
A1: x>=0;
    x^2+1>x^2+0 by XREAL_1:10;
    then sqrt(x^2+1)>sqrt(x^2) by SQUARE_1:95,XREAL_1:65;
    hence thesis by A1,SQUARE_1:89;
  end;
  suppose x<0;
    hence thesis by Th4;
  end;
end;

Lm11: sqrt(x^2+1)*sqrt(y^2+1)-x*y>0
proof
A1: x^2+1>0 & y^2+1>0 & sqrt(x^2+1)>0 & sqrt(y^2+1)>0 by Lm8,Th4;
  then
A2: (sqrt(x^2+1))*(sqrt(y^2+1))>0*sqrt(y^2+1) by XREAL_1:70;
A3: 0<=x^2 & 0<=y^2by XREAL_1:65;
A4: 0+1<=x^2+y^2+1 by A3,XREAL_1:8;
  assume (sqrt(x^2+1)*sqrt(y^2+1))-x*y<=0;
  then (sqrt(x^2+1)*sqrt(y^2+1))+(-x*y)<=0;
  then sqrt(x^2+1)*sqrt(y^2+1)<=x*y by REAL_2:105;
  then (sqrt(x^2+1)*sqrt(y^2+1))^2<=(x*y)^2 by A2,SQUARE_1:77;
  then (sqrt(x^2+1))^2*(sqrt(y^2+1))^2<=(x*y)^2;
  then (x^2+1)*(sqrt(y^2+1))^2<=(x*y)^2 by A1,SQUARE_1:def 4;
  then (x^2+1)*(y^2+1)<=(x*y)^2 by A1,SQUARE_1:def 4;
  then (x*y)^2+x^2+y^2+1-(x*y)^2<=(x*y)^2-(x*y)^2 by XREAL_1:15;
  hence contradiction by A4;
end;

Lm12: 1<=y implies y+sqrt(y^2-1)>0
proof
  assume
A1: 1<=y;
  then 0<=y^2-1 by Lm4;
  then 0<=sqrt(y^2-1) by SQUARE_1:def 4;
  then 0+y<=sqrt(y^2-1)+y by XREAL_1:9;
  hence thesis by A1;
end;

Lm13: 0<t implies -1<(t^2-1)/(t^2+1)
proof
A1: 0<t^2+1 by Lm8;
  assume 0<t;
  then 0<t^2 by SQUARE_1:74;
  then 0*2<2*t^2 by XREAL_1:70;
  then 0-(t^2+1)<2*t^2-(t^2+1) by XREAL_1:16;
  then (-(t^2+1))/(t^2+1)<(t^2-1)/(t^2+1) by A1,XREAL_1:76;
  then -(t^2+1)/(t^2+1)<(t^2-1)/(t^2+1) by XCMPLX_1:188;
  hence thesis by A1,XCMPLX_1:60;
end;

Lm14: 0<t & t<>1 implies 1<(t^2+1)/(t^2-1) or (t^2+1)/(t^2-1)< -1
proof
  assume
A1: 0<t & t<>1;
  then 1<t or t<1 by XXREAL_0:1;
  then
A2: 1-1<t-1 or t-1<1-1 by XREAL_1:16;
  per cases by A2;
  suppose 0<t-1;
    then
A3: 0+1<t-1+1 by XREAL_1:10;
    then t<t^2 by SQUARE_1:76;
    then 1<t^2 by A3,XXREAL_0:2;
    then
A4: 1-1<t^2-1 by XREAL_1:16;
    -1+t^2<1+t^2 by XREAL_1:10;
    hence thesis by A4,REAL_2:142;
  end;
  suppose t-1<0;
    then
A5: t-1+1<0+1 by XREAL_1:10;
    then t^2<t by A1,SQUARE_1:75;
    then t^2<1 by A5,XXREAL_0:2;
    then
A6: t^2-t^2<1-t^2 by XREAL_1:16;
    0<t^2 by A1,SQUARE_1:74;
    then 0*2<2*t^2 by XREAL_1:70;
    then 0-t^2<2*t^2-t^2 by XREAL_1:16;
    then -t^2+1<t^2+1 by XREAL_1:10;
    then 1<(t^2+1)/(1-t^2) by A6,REAL_2:142;
    then (-1)*((t^2+1)/(1-t^2))<1*(-1) by XREAL_1:71;
    then ((-1)*(t^2+1))/(1-t^2)<1*(-1) by XCMPLX_1:75;
    then (-(t^2+1))/(1-t^2)<1*(-1);
    then (t^2+1)/(-(1-t^2))<1*(-1) by XCMPLX_1:193;
    hence thesis;
  end;
end;

Lm15: 0<t implies 0<2*t/(1+t^2)
proof
  assume 0<t;
  then
A1: 0*2<2*t by XREAL_1:70;
  t^2+1>0 by Lm8;
  then 0/(t^2+1)<2*t/(t^2+1) by A1,XREAL_1:76;
  hence thesis;
end;

Lm16: 0<t implies 2*t/(1+t^2)<=1
proof
  assume 0<t;
  then
A1: 0*2<2*t by XREAL_1:70;
  0<=(t-1)^2 by XREAL_1:65;
  then 0+2*t<=t^2-2*t+1+2*t by XREAL_1:8;
  hence thesis by A1,REAL_2:143;
end;

Lm17: 0<t implies 0<(1+sqrt(1+t^2))/t
proof
  assume
A1: 0<t;
  0+1<(sqrt(1+t^2))+1 by Th4,XREAL_1:10;
  then 0/t<(1+sqrt(1+t^2))/t by A1,XREAL_1:76;
  hence thesis;
end;

Lm18: 0<t implies (1-sqrt(1+t^2))/t<0
proof
  assume
A1: 0<t;
  then 0+1<t^2+1 by SQUARE_1:74,XREAL_1:10;
  then 1<sqrt(t^2+1) by SQUARE_1:83,95;
  then -sqrt(t^2+1)< -1 by XREAL_1:26;
  then -sqrt(t^2+1)+1< -1+1 by XREAL_1:10;
  then (1-sqrt(t^2+1))/t<0/t by A1,XREAL_1:76;
  hence thesis;
end;

Lm19: 0<t & t<=1 implies 0<=1-t^2
proof
  assume 0<t & t<=1;
  then t^2<=1 by SQUARE_1:59,77;
  then t^2-t^2<=1-t^2 by XREAL_1:15;
  hence thesis;
end;

Lm20: 0<t & t<=1 implies 0<=4-4*t^2
proof
  assume 0<t & t<=1;
  then 0<=1-t^2 by Lm19;
  then 0*4<=4*(1-t^2);
  hence thesis;
end;

Lm21: 0<t & t<=1 implies 0<1+sqrt(1-t^2)
proof
  assume 0<t & t<=1;
  then 0<=1-t^2 by Lm19;
  then 0<=sqrt(1-t^2) by SQUARE_1:82,94;
  then 0+1<=sqrt(1-t^2)+1 by XREAL_1:8;
  hence thesis;
end;

Lm22: 0<t & t<=1 implies 0<(1+sqrt(1-t^2))/t
proof
  assume
A1: 0<t & t<=1;
  then 0<1+sqrt(1-t^2) by Lm21;
  then 0/t<(1+sqrt(1-t^2))/t by A1,XREAL_1:76;
  hence thesis;
end;

Lm23: 0<t & t<>1 implies 2*t/(t^2-1)<>0
proof
  assume
A1: 0<t & t<>1;
  per cases by A1,XXREAL_0:1;
  suppose
A2: 1<t;
    then t<t^2 by SQUARE_1:76;
    then 1<t^2 by A2,XXREAL_0:2;
    then
A3: 1-1<t^2-1 by XREAL_1:16;
    2*1<2*t by A2,XREAL_1:70;
    then 0/(t^2-1)<2*t/(t^2-1) by A3,XREAL_1:76;
    hence thesis;
  end;
  suppose
A4: t<1;
    then t^2<t by A1,SQUARE_1:75;
    then t^2<1 by A4,XXREAL_0:2;
    then
A5: t^2-1<1-1 by XREAL_1:16;
    0*2<2*t by A1,XREAL_1:70;
    then 2*t/(t^2-1)<0/(t^2-1) by A5,XREAL_1:77;
    hence thesis;
  end;
end;

Lm24: t<0 implies (1+sqrt(1+t^2))/t<0
proof
  assume
A1: t<0;
  0+1<(sqrt(1+t^2))+1 by Th4,XREAL_1:10;
  then (1+sqrt(1+t^2))/t<0/t by A1,XREAL_1:77;
  hence thesis;
end;

Lm25: t<0 implies 0<(1-sqrt(1+t^2))/t
proof
  assume
A1: t<0;
  then 0+1<t^2+1 by SQUARE_1:74,XREAL_1:10;
  then 1<sqrt(t^2+1) by SQUARE_1:83,95;
  then -sqrt(t^2+1)< -1 by XREAL_1:26;
  then -sqrt(t^2+1)+1< -1+1 by XREAL_1:10;
  then 0/t<(1-sqrt(t^2+1))/t by A1,XREAL_1:77;
  hence thesis;
end;

begin :: Main Part

definition
  let x be real number;
  func sinh"(x) -> real number equals

  log(number_e,(x+sqrt(x^2+1)));
  coherence;
end;

definition
  let x be real number;
  func cosh1"(x) -> real number equals

  log(number_e,x+sqrt(x^2-1));
  coherence;
end;

definition
  let x be real number;
  func cosh2"(x) -> real number equals

  -(log(number_e,x+sqrt(x^2-1)));
  coherence;
end;

definition
  let x be real number;
  func tanh"(x) -> real number equals

  (1/2)*log(number_e,(1+x)/(1-x));
  coherence;
end;

definition
  let x be real number;
  func coth"(x) -> real number equals

  (1/2)*log(number_e,(x+1)/(x-1));
  coherence;
end;

definition
  let x be real number;
  func sech1"(x) -> real number equals

  log(number_e,(1+sqrt(1-x^2))/x);
  coherence;
end;

definition
  let x be real number;
  func sech2"(x) -> real number equals

  -(log(number_e,(1+sqrt(1-x^2))/x));
  coherence;
end;

definition
  let x be real number;
  func csch"(x) -> real number equals
  :Def8:
  log(number_e,(1+sqrt(1+x^2))/x) if 0<x,
  log(number_e,(1-sqrt(1+x^2))/x) if x<0;
  correctness;
end;

theorem
  0<=x implies sinh"(x)=cosh1"(sqrt(x^2+1))
proof
  x^2>=0 by XREAL_1:65;
  then
A1: x^2+1>=0+1 by XREAL_1:9;
  assume
A2: 0<=x;
  cosh1"(sqrt(x^2+1))
  =log(number_e,sqrt(x^2+1)+sqrt(x^2+1-1)) by A1,SQUARE_1:def 4
    .=log(number_e,(sqrt(x^2+1)+x)) by A2,SQUARE_1:89;
  hence thesis;
end;

theorem
  x<0 implies sinh"(x)=cosh2"(sqrt(x^2+1))
proof
  assume
A1: x<0;
A2: sqrt(x^2+1)+x>0 by Th5;
A3: 1/(sqrt(x^2+1)+x)=(sqrt(x^2+1)+x) to_power (-1) by Th1,Th5;
  x^2>=0 by XREAL_1:65;
  then
A4: x^2+1>=0+1 by XREAL_1:9;
  then cosh2"(sqrt(x^2+1))
  =-log(number_e,sqrt(x^2+1)+sqrt(x^2+1-1)) by SQUARE_1:def 4
    .=-log(number_e,(sqrt(x^2+1)+(-x))) by A1,SQUARE_1:90
    .=-log(number_e,(sqrt(x^2+1)-x)*(sqrt(x^2+1)+x)/(sqrt(x^2+1)+x))
  by A2,XCMPLX_1:90
    .=-log(number_e,((sqrt(x^2+1))^2-x^2)/(sqrt(x^2+1)+x))
    .=-log(number_e,((x^2+1)-x^2)/(sqrt(x^2+1)+x)) by A4,SQUARE_1:def 4
    .=-((-1)*log(number_e,(sqrt(x^2+1)+x))) by A2,A3,Lm2,POWER:63
    .=log(number_e,(sqrt(x^2+1)+x));
  hence thesis;
end;

theorem
  sinh"(x)=tanh"(x/sqrt(x^2+1))
proof
A1: (x/sqrt(x^2+1))^2<1 & sqrt(x^2+1)>0 & sqrt(x^2+1)+x>0 &
  x^2+1>0 by Lm8,Th3,Th4,Th5;
  set t = sqrt(x^2+1)+x;
  tanh"(x/sqrt(x^2+1))
  =(1/2)*log(number_e,((sqrt(x^2+1)*1+x)/sqrt(x^2+1))/(1-x/sqrt(x^2+1)))
  by A1,XCMPLX_1:114
    .=(1/2)*log(number_e,((sqrt(x^2+1)*1+x)/sqrt(x^2+1))/((1*sqrt(x^2+1)-x)
  /sqrt(x^2+1))) by A1,XCMPLX_1:128
    .=(1/2)*log(number_e,t/(sqrt(x^2+1)-x)) by A1,XCMPLX_1:55
    .=(1/2)*log(number_e,t*t/((sqrt(x^2+1)-x)*t)) by A1,XCMPLX_1:92
    .=(1/2)*log(number_e,(t*t)/((sqrt(x^2+1))*(sqrt(x^2+1))-x^2))
    .=(1/2)*log(number_e,(t*t)/((sqrt((x^2+1)^2))-x^2)) by A1,SQUARE_1:97
    .=(1/2)*log(number_e,(t*t)/((x^2+1)-x^2)) by A1,SQUARE_1:89
    .=(1/2)*log(number_e,t^2)
    .=(1/2)*log(number_e,t to_power 2) by POWER:53
    .=(1/2)*(2*log(number_e,t)) by A1,Lm2,POWER:63
    .=log(number_e,t);
  hence thesis;
end;

theorem
  x>=1 implies cosh1"(x)=sinh"(sqrt(x^2-1))
proof
  assume
A1: x>=1;
  then x^2-1>=0 by Lm4;
  then sinh"(sqrt(x^2-1))
  =log(number_e,((sqrt(x^2-1))+sqrt((x^2-1)+1))) by SQUARE_1:def 4
    .=log(number_e,((sqrt(x^2-1))+x)) by A1,SQUARE_1:89;
  hence thesis;
end;

theorem
  x>1 implies cosh1"(x)=tanh"((sqrt(x^2-1))/x)
proof
  assume
A1: x>1;
  then x^2>1+0 by SQUARE_1:59,78;
  then
A2: x^2-1>0 by XREAL_1:22;
  then
A3: sqrt(x^2-1)>0 & ((sqrt(x^2-1))/x)^2<1 & sqrt(x^2-1)+x>0
  by A1,Th2,Th23,SQUARE_1:93;
  tanh"((sqrt(x^2-1))/x)
  =(1/2)*log(number_e,((sqrt(x^2-1)+x*1)/x)/(1-(sqrt(x^2-1))/x))
  by A1,XCMPLX_1:114
    .=(1/2)*log(number_e,((sqrt(x^2-1)+x)/x)/((1*x-(sqrt(x^2-1)))/x))
  by A1,XCMPLX_1:128
    .=(1/2)*log(number_e,(sqrt(x^2-1)+x)/(x-sqrt(x^2-1))) by A1,XCMPLX_1:55
    .=(1/2)*log(number_e,((sqrt(x^2-1)+x)*(sqrt(x^2-1)+x))/
  ((x-sqrt(x^2-1))*(sqrt(x^2-1)+x))) by A3,XCMPLX_1:92
    .=(1/2)*log(number_e,((sqrt(x^2-1)+x)*(sqrt(x^2-1)+x))/
  (x^2-(sqrt(x^2-1))^2))
    .=(1/2)*log(number_e,((sqrt(x^2-1)+x)*(sqrt(x^2-1)+x))/
  (x^2-(x^2-1))) by A2,SQUARE_1:def 4
    .=(1/2)*log(number_e,((sqrt(x^2-1)+x)^2))
    .=(1/2)*log(number_e,((sqrt(x^2-1)+x) to_power 2)) by POWER:53
    .=(1/2)*(2*log(number_e,(sqrt(x^2-1)+x))) by A3,Lm2,POWER:63
    .=log(number_e,(sqrt(x^2-1)+x));
  hence thesis;
end;

theorem
  x>=1 implies cosh1"(x)=2*cosh1"(sqrt((x+1)/2))
proof
  assume x>=1;
  then
A1: sqrt((x+1)/2)>=1 & (x+1)/2>=0 & (x-1)/2>=0 &
  sqrt((x+1)/2)+sqrt((x-1)/2)>0 & (x^2-1)/4>=0 by Th7,Th8,Th9,Th10;
  then 2*cosh1"(sqrt((x+1)/2))
  =2*(log(number_e,(sqrt((x+1)/2))+sqrt(((x+1)/2)-1))) by SQUARE_1:def 4
    .=log(number_e,((sqrt((x+1)/2))+sqrt((x-1)/2)) to_power 2)
  by A1,Lm2,POWER:63
    .=log(number_e,((sqrt((x+1)/2))+sqrt((x-1)/2))^2) by POWER:53
    .=log(number_e,(sqrt((x+1)/2))^2+2*(sqrt((x+1)/2))*
  (sqrt((x-1)/2))+(sqrt((x-1)/2))^2)
    .=log(number_e,((x+1)/2)+2*(sqrt((x+1)/2))*
  (sqrt((x-1)/2))+(sqrt((x-1)/2))^2) by A1,SQUARE_1:def 4
    .=log(number_e,(x+1)/2+2*sqrt((x+1)/2)*
  sqrt((x-1)/2)+(x-1)/2) by A1,SQUARE_1:def 4
    .=log(number_e,x+2*(sqrt((x+1)/2)*sqrt((x-1)/2)))
    .=log(number_e,x+2*(sqrt(((x+1)/2)*((x-1)/2)))) by A1,SQUARE_1:97
    .=log(number_e,x+sqrt(2^2)*(sqrt((x^2-1)/4))) by SQUARE_1:89
    .=log(number_e,x+sqrt(4*((x^2-1)/4))) by A1,SQUARE_1:97
    .=log(number_e,x+sqrt(x^2-1));
  hence thesis;
end;

theorem
  x>=1 implies cosh2"(x)=2*cosh2"(sqrt((x+1)/2))
proof
  assume x>=1;
  then
A1: sqrt((x+1)/2)>=1 & (x+1)/2>=0 & (x-1)/2>=0 &
  sqrt((x+1)/2)+sqrt((x-1)/2)>0 & (x^2-1)/4>=0 by Th7,Th8,Th9,Th10;
  then 2*cosh2"(sqrt((x+1)/2))
  =2*(-(log(number_e,(sqrt((x+1)/2))+sqrt(((x+1)/2)-1)))) by SQUARE_1:def 4
    .=-(2*(log(number_e,(sqrt((x+1)/2))+sqrt((x-1)/2))))
    .=-(log(number_e,((sqrt((x+1)/2))+sqrt((x-1)/2)) to_power 2))
  by A1,Lm2,POWER:63
    .=-log(number_e,((sqrt((x+1)/2))+sqrt((x-1)/2))^2) by POWER:53
    .=-log(number_e,(sqrt((x+1)/2))^2+2*(sqrt((x+1)/2))*
  (sqrt((x-1)/2))+(sqrt((x-1)/2))^2)
    .=-log(number_e,((x+1)/2)+2*(sqrt((x+1)/2))*
  (sqrt((x-1)/2))+(sqrt((x-1)/2))^2) by A1,SQUARE_1:def 4
    .=-log(number_e,(x+1)/2+2*sqrt((x+1)/2)*
  sqrt((x-1)/2)+(x-1)/2) by A1,SQUARE_1:def 4
    .=-log(number_e,(x+2*(sqrt((x+1)/2)*sqrt((x-1)/2))))
    .=-log(number_e,(x+2*(sqrt(((x+1)/2)*((x-1)/2))))) by A1,SQUARE_1:97
    .=-log(number_e,(x+sqrt(2^2)*(sqrt((x^2-1)/4)))) by SQUARE_1:89
    .=-log(number_e,(x+sqrt(4*((x^2-1)/4)))) by A1,SQUARE_1:97
    .=-log(number_e,(x+sqrt(x^2-1)));
  hence thesis;
end;

theorem
  x>=1 implies cosh1"(x)=2*sinh"(sqrt((x-1)/2))
proof
  assume x>=1;
  then
A1: sqrt((x+1)/2)>=1 & (x+1)/2>=0 & (x-1)/2>=0 &
  sqrt((x+1)/2)+sqrt((x-1)/2)>0 & (x^2-1)/4>=0 by Th7,Th8,Th9,Th10;
  then 2*sinh"(sqrt((x-1)/2))
  =2*log(number_e,((sqrt((x-1)/2))+sqrt(((x-1)/2)+1))) by SQUARE_1:def 4
    .=log(number_e,(sqrt((x-1)/2)+sqrt((x+1)/2)) to_power 2)
  by A1,Lm2,POWER:63
    .=log(number_e,(sqrt((x-1)/2)+sqrt((x+1)/2))^2) by POWER:53
    .=log(number_e,(sqrt((x-1)/2))^2+2*(sqrt((x-1)/2))
  *(sqrt((x+1)/2))+(sqrt((x+1)/2))^2)
    .=log(number_e,((x-1)/2)+2*(sqrt((x-1)/2))
  *(sqrt((x+1)/2))+(sqrt((x+1)/2))^2) by A1,SQUARE_1:def 4
    .=log(number_e,(x-1)/2+2*(sqrt((x-1)/2))
  *(sqrt((x+1)/2))+(x+1)/2) by A1,SQUARE_1:def 4
    .=log(number_e,x+2*((sqrt((x-1)/2))*(sqrt((x+1)/2))))
    .=log(number_e,x+2*(sqrt(((x-1)/2)*((x+1)/2)))) by A1,SQUARE_1:97
    .=log(number_e,x+sqrt(2^2)*(sqrt((x^2-1)/4))) by SQUARE_1:89
    .=log(number_e,x+sqrt(4*((x^2-1)/4))) by A1,SQUARE_1:97
    .=log(number_e,x+sqrt(x^2-1));
  hence thesis;
end;

theorem
  x^2<1 implies tanh"(x)=sinh"(x/sqrt(1-x^2))
proof
  assume x^2<1;
  then
A1: 1-x^2>0 & x+1>0 & 1-x>0 & (x+1)/(1-x)>0 by Lm5,Th11,XREAL_1:52;
  then
A2: sqrt(x+1)>0 by SQUARE_1:93;
A3: sqrt((x+1)/(1-x))=((x+1)/(1-x)) to_power (1/2) by A1,ASYMPT_1:89;
  sinh"(x/sqrt(1-x^2))
  =log(number_e,((x/sqrt(1-x^2))+sqrt((x^2/(sqrt(1-x^2))^2)+1)))
  by XCMPLX_1:77
    .=log(number_e,((x/sqrt(1-x^2))+sqrt((x^2/(1-x^2))+1)))
  by A1,SQUARE_1:def 4
    .=log(number_e,((x/sqrt(1-x^2))+sqrt((x^2+(1-x^2)*1)/(1-x^2))))
  by A1,XCMPLX_1:114
    .=log(number_e,((x/sqrt(1-x^2))+sqrt(1)/sqrt(1-x^2)))
  by A1,SQUARE_1:99
    .=log(number_e,((x+1)/sqrt((1-x)*(1+x)))) by SQUARE_1:83,XCMPLX_1:63
    .=log(number_e,(sqrt((x+1)^2)/sqrt((1-x)*(1+x)))) by A1,SQUARE_1:89
    .=log(number_e,((sqrt(x+1)*sqrt(x+1))/sqrt((1-x)*(1+x))))
  by A1,SQUARE_1:97
    .=log(number_e,((sqrt(x+1)*sqrt(x+1))/(sqrt(1-x)*sqrt(1+x))))
  by A1,SQUARE_1:97
    .=log(number_e,(sqrt(x+1)/sqrt(1-x))) by A2,XCMPLX_1:92
    .=log(number_e,(sqrt((x+1)/(1-x)))) by A1,SQUARE_1:99
    .=(1/2)*log(number_e,((1+x)/(1-x))) by A1,A3,Lm2,POWER:63;
  hence thesis;
end;

theorem
  0<x & x<1 implies tanh"(x)=cosh1"(1/(sqrt(1-x^2)))
proof
  assume
A1: 0<x & x<1;
  then (1+x)/(1-x)>0 by Th15;
  then
A2: sqrt((1+x)/(1-x))=((1+x)/(1-x)) to_power (1/2) by ASYMPT_1:89;
A3: 1-x>0 & 1+x>0 & x^2<1 & 1-x^2>0 & 1/(sqrt(1-x^2))>1 &
  (1+x)/(1-x)>0 by A1,Lm7,Th15,Th16,Th17,XREAL_1:52;
  then
A4: sqrt(1+x)>0 by SQUARE_1:93;
A5: x^2>=0 by XREAL_1:65;
  cosh1"(1/(sqrt(1-x^2)))
  =log(number_e,(1/(sqrt(1-x^2)))+sqrt((1/((sqrt(1-x^2))^2))-1))
  by SQUARE_1:59,XCMPLX_1:77
    .=log(number_e,(1/(sqrt(1-x^2)))+sqrt(1/(1-x^2)-1)) by A3,SQUARE_1:def 4
    .=log(number_e,(1/(sqrt(1-x^2)))+sqrt((1-1*(1-x^2))/(1-x^2)))
  by A3,XCMPLX_1:127
    .=log(number_e,(1/(sqrt(1-x^2)))+sqrt(x^2)/sqrt(1-x^2))
  by A3,A5,SQUARE_1:99
    .=log(number_e,1/(sqrt(1-x^2))+x/sqrt(1-x^2)) by A1,SQUARE_1:89
    .=log(number_e,(1+x)/sqrt((1-x)*(1+x))) by XCMPLX_1:63
    .=log(number_e,(1+x)/(sqrt(1-x)*sqrt(1+x))) by A3,SQUARE_1:97
    .=log(number_e,(sqrt((1+x)^2))/(sqrt(1-x)*sqrt(1+x))) by A3,SQUARE_1:89
    .=log(number_e,(sqrt(1+x)*sqrt(1+x))/(sqrt(1-x)*sqrt(1+x)))
  by A3,SQUARE_1:97
    .=log(number_e,(sqrt(1+x))/(sqrt(1-x))) by A4,XCMPLX_1:92
    .=log(number_e,sqrt((1+x)/(1-x))) by A3,SQUARE_1:99
    .=(1/2)*log(number_e,((1+x)/(1-x))) by A2,A3,Lm2,POWER:63;
  hence thesis;
end;

theorem
  x^2<1 implies tanh"(x)=(1/2)*sinh"((2*x)/(1-x^2))
proof
  assume x^2<1;
  then
A1: (1-x^2)^2>0 & (x^2+1)/(1-x^2)>=0 & x+1>0 & 1-x>0 by Th11,Th12,Th13;
  (1/2)*sinh"((2*x)/(1-x^2))
  =(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt(((2*x)^2/(1-x^2)^2)+1)))
  by XCMPLX_1:77
    .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt(((4*x^2)+((1-x^2)^2)*1)/
  ((1-x^2)^2)))) by A1,XCMPLX_1:114
    .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt((((x^2)+1)^2)/ ((1-x^2)^2))))
    .=(1/2)*log(number_e,(((2*x)/(1-x^2))+sqrt((((x^2)+1)/(1-x^2))^2)))
  by XCMPLX_1:77
    .=(1/2)*log(number_e,((2*x)/(1-x^2)+((x^2)+1)/(1-x^2)))
  by A1,SQUARE_1:89
    .=(1/2)*log(number_e,(((2*x)+((x^2)+1))/(1-x^2))) by XCMPLX_1:63
    .=(1/2)*log(number_e,((x+1)*(x+1)/((1-x)*(1+x))))
    .=(1/2)*log(number_e,((x+1)/(1-x))) by A1,XCMPLX_1:92;
  hence thesis;
end;

theorem
  x>0 & x<1 implies tanh"(x)=(1/2)*cosh1"((1+x^2)/(1-x^2))
proof
  assume x>0 & x<1;
  then
A1: 0<x+1 & x^2<1 & 0<1-x^2 & 0<(1-x^2)^2 &
  (2*x)/(1-x^2)>0 & ((1+x^2)/(1-x^2))>1 by Lm7,Th16,Th18,Th19,Th20;
  (1/2)*cosh1"((1+x^2)/(1-x^2))
  =(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt((1+x^2)^2/(1-x^2)^2-1))
  by XCMPLX_1:77
    .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt((((1+x^2)^2)-1*
  ((1-x^2)^2))/((1-x^2)^2))) by A1,XCMPLX_1:127
    .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt(((2*x)^2)/((1-x^2)^2)))
    .=(1/2)*log(number_e,((1+x^2)/(1-x^2))+sqrt(((2*x)/(1-x^2))^2))
  by XCMPLX_1:77
    .=(1/2)*log(number_e,(1+x^2)/(1-x^2)+(2*x)/(1-x^2))
  by A1,SQUARE_1:89
    .=(1/2)*log(number_e,((1+x^2)+(2*x))/(1-x^2)) by XCMPLX_1:63
    .=(1/2)*log(number_e,(x+1)*(x+1)/((1-x)*(1+x)))
    .=(1/2)*log(number_e,(x+1)/((1-x))) by A1,XCMPLX_1:92;
  hence thesis;
end;

theorem
  x^2<1 implies tanh"(x)=(1/2)*tanh"((2*x)/(1+x^2))
proof
  assume x^2<1;
  then
A1: 1+x^2>0 & ((2*x)/(1+x^2))^2<1 & (x+1)/(1-x)>0 by Lm5,Lm8,Th14;
  then (1/2)*tanh"((2*x)/(1+x^2))
  =(1/2)*((1/2)*log(number_e,(((2*x)+(1+x^2)*1)/(1+x^2))/(1-(2*x)/(1+x^2))))
  by XCMPLX_1:114
    .=(1/2)*((1/2)*log(number_e,((2*x+1+x^2)/(1+x^2))/((1*(1+x^2)-(2*x))
  /(1+x^2)))) by A1,XCMPLX_1:128
    .=(1/2)*((1/2)*log(number_e,(x+1)^2/(1-x)^2)) by A1,XCMPLX_1:55
    .=(1/2)*((1/2)*log(number_e,((x+1)/(1-x))^2)) by XCMPLX_1:77
    .=(1/2)*((1/2)*log(number_e,((x+1)/(1-x)) to_power 2)) by POWER:53
    .=(1/2)*((1/2)*(2*log(number_e,((x+1)/(1-x))))) by A1,Lm2,POWER:63
    .=(1/2)*log(number_e,(1+x)/(1-x));
  hence thesis;
end;

theorem
  x^2>1 implies coth"(x)=tanh"(1/x)
proof
  assume x^2>1;
  then
A1: (1/x)^2<1 & x<>0 by Th21;
  then tanh"(1/x) =(1/2)*log(number_e,((1+x*1)/x)/(1-(1/x))) by XCMPLX_1:114
    .=(1/2)*log(number_e,((1+x*1)/x)/((1*x-1)/x)) by A1,XCMPLX_1:128
    .=(1/2)*log(number_e,(1+x)/(x-1)) by A1,XCMPLX_1:55;
  hence thesis;
end;

theorem
  x>0 & x<=1 implies sech1"(x)=cosh1"(1/x)
proof
  assume
A1: x>0 & x<=1;
  then
A2: x^2>0 by SQUARE_1:74;
A3: 1/x>=1 & 1-x^2>=0 by A1,Lm1,Th22,REAL_2:152;
  cosh1"(1/x)=log(number_e,(1/x)+sqrt(1/x^2-1)) by SQUARE_1:59,XCMPLX_1:77
    .=log(number_e,(1/x)+sqrt((1-1*(x^2))/x^2)) by A2,XCMPLX_1:127
    .=log(number_e,(1/x)+sqrt(1-(x^2))/sqrt(x^2)) by A2,A3,SQUARE_1:99
    .=log(number_e,(1/x)+sqrt(1-(x^2))/x) by A1,SQUARE_1:89
    .=log(number_e,(1+sqrt(1-x^2))/x) by XCMPLX_1:63;
  hence thesis;
end;

theorem
  x>0 & x<=1 implies sech2"(x)=cosh2"(1/x)
proof
  assume
A1: x>0 & x<=1;
  then
A2: x^2>0 by SQUARE_1:74;
A3: 1/x>=1 & 1-x^2>=0 by A1,Lm1,Th22,REAL_2:152;
  cosh2"(1/x) =-log(number_e,(1/x)+sqrt(1/x^2-1)) by SQUARE_1:59,XCMPLX_1:77
    .=-log(number_e,(1/x)+sqrt((1-1*(x^2))/x^2)) by A2,XCMPLX_1:127
    .=-log(number_e,(1/x)+sqrt(1-(x^2))/sqrt(x^2)) by A2,A3,SQUARE_1:99
    .=-log(number_e,(1/x)+sqrt(1-(x^2))/x) by A1,SQUARE_1:89
    .=-log(number_e,(1+sqrt(1-x^2))/x) by XCMPLX_1:63;
  hence thesis;
end;

theorem
  x>0 implies csch"(x)=sinh"(1/x)
proof
  assume
A1: x>0;
  then
A2: x^2>0 by SQUARE_1:74;
  sinh"(1/x)=log(number_e,(1/x+sqrt(1/x^2+1))) by SQUARE_1:59,XCMPLX_1:77
    .=log(number_e,(1/x+sqrt((1+x^2*1)/(x^2)))) by A2,XCMPLX_1:114
    .=log(number_e,(1/x+sqrt(1+x^2)/sqrt(x^2))) by A2,SQUARE_1:99
    .=log(number_e,(1/x+sqrt(1+x^2)/x)) by A1,SQUARE_1:89
    .=log(number_e,(1+sqrt(1+x^2))/x) by XCMPLX_1:63;
  hence thesis by A1,Def8;
end;

theorem
  (x*y+sqrt(x^2+1)*sqrt(y^2+1))>=0 implies
  sinh"(x)+sinh"(y)=sinh"(x*sqrt(1+y^2)+y*sqrt(1+x^2))
proof
  assume
A1: (x*y+sqrt(x^2+1)*sqrt(y^2+1))>=0;
A2: sqrt(x^2+1)+x>0 & sqrt(y^2+1)+y>0 by Th5;
A3: x^2+1>=0 & y^2+1>=0 by Lm8;
  then
A4: (x^2+1)*(y^2+1)>=0;
A5: sinh"(x)+sinh"(y)
  =log(number_e,(x+sqrt(x^2+1))*(y+sqrt(y^2+1))) by A2,Lm2,POWER:61
    .=log(number_e,(x*sqrt(y^2+1)+sqrt(x^2+1)*y+(x*y+sqrt(x^2+1)*sqrt(y^2+1))))
    .=log(number_e,(x*sqrt(y^2+1)+sqrt(x^2+1)*y+
  sqrt((x*y+sqrt(x^2+1)*sqrt(y^2+1))^2))) by A1,SQUARE_1:89;
  set t=sqrt((x*y+sqrt(x^2+1)*sqrt(y^2+1))^2);
A6: t=sqrt((x*y+sqrt((x^2+1)*(y^2+1)))^2) by A3,SQUARE_1:97
    .=sqrt((x*y)^2+2*(x*y)*(sqrt((x^2+1)*(y^2+1)))+(sqrt((x^2+1)*(y^2+1)))^2)
    .=sqrt((x*y)^2+2*(x*y)*(sqrt((x^2+1)*(y^2+1)))+((x*y)^2+x^2+y^2+1)) by A4,
  SQUARE_1:def 4
    .=sqrt(2*(x*y)^2+x^2+y^2+1+2*(x*y)*(sqrt((x^2+1)*(y^2+1))));
  set p=sqrt(((x*sqrt(1+y^2))+(y*sqrt(1+x^2)))^2+1);
  p=sqrt((x^2*(sqrt(1+y^2))^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
  +(y*sqrt(1+x^2))^2)+1)
    .=sqrt((x^2*(1+y^2)+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
  +(y*sqrt(1+x^2))^2)+1) by A3,SQUARE_1:def 4
    .=sqrt((x^2+(x*y)^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
  +y^2*(sqrt(1+x^2))^2)+1)
    .=sqrt((x^2+(x*y)^2+2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
  +y^2*(1+x^2))+1) by A3,SQUARE_1:def 4
    .=sqrt(x^2+2*(x*y)^2+y^2+1+2*x*y*(sqrt(1+y^2)*sqrt(1+x^2)))
    .=sqrt(x^2+2*(x*y)^2+y^2+1+2*x*y*sqrt((1+y^2)*(1+x^2))) by A3,SQUARE_1:97;
  hence thesis by A5,A6;
end;

theorem
  sinh"(x)-sinh"(y)=sinh"(x*sqrt(1+y^2)-y*sqrt(1+x^2))
proof
A1: (sqrt(x^2+1)*sqrt(y^2+1)-x*y)>=0 by Lm11;
A2: sqrt(x^2+1)+x>0 & sqrt(y^2+1)+y>0 by Th5;
A3: x^2+1>=0 & y^2+1>=0 by Lm8;
  y+0<sqrt(y^2+1) by Lm10;
  then
A4: sqrt(y^2+1)-y>0 by XREAL_1:22;
A5: sinh"(x)-sinh"(y)
  =log(number_e,(x+sqrt(x^2+1))/(y+sqrt(y^2+1))) by A2,Lm2,POWER:62
    .=log(number_e,((x+sqrt(x^2+1))*(sqrt(y^2+1)-y))/
  ((y+sqrt(y^2+1))*(sqrt(y^2+1)-y))) by A4,XCMPLX_1:92
    .=log(number_e,((x+sqrt(x^2+1))*(sqrt(y^2+1)-y))/ ((sqrt(y^2+1))^2-y^2))
    .=log(number_e,((x+sqrt(x^2+1))*(sqrt(y^2+1)-y))/
  ((y^2+1)-y^2)) by A3,SQUARE_1:def 4
    .=log(number_e,x*(sqrt(y^2+1))-y*(sqrt(x^2+1))+(sqrt(x^2+1))
  *(sqrt(y^2+1))-x*y);
  set t=(sqrt(x^2+1))*(sqrt(y^2+1))-x*y;
A6: t=sqrt(((sqrt(x^2+1))*(sqrt(y^2+1))-x*y)^2) by A1,SQUARE_1:89
    .=sqrt(((sqrt(x^2+1))^2*(sqrt(y^2+1))^2)-2*((sqrt(x^2+1))*
  (sqrt(y^2+1)))*(x*y)+(x*y)^2)
    .=sqrt(((x^2+1)*(sqrt(y^2+1))^2)-2*((sqrt(x^2+1))*
  (sqrt(y^2+1)))*(x*y)+(x*y)^2) by A3,SQUARE_1:def 4
    .=sqrt(((x^2+1)*(y^2+1))-2*((sqrt(x^2+1))*
  (sqrt(y^2+1)))*(x*y)+(x*y)^2) by A3,SQUARE_1:def 4
    .=sqrt(2*((x*y)^2)+x^2+y^2+1-2*(x*y)*((sqrt(x^2+1))* (sqrt(y^2+1))));
  set q=sqrt((x*sqrt(1+y^2)-y*sqrt(1+x^2))^2+1);
  q=sqrt((x^2*(sqrt(1+y^2))^2)-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
  +((y*sqrt(1+x^2)))^2+1)
    .=sqrt((x^2*(1+y^2))-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
  +((y*sqrt(1+x^2)))^2+1) by A3,SQUARE_1:def 4
    .=sqrt((x^2+x^2*y^2)-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
  +(y^2*(sqrt(1+x^2))^2)+1)
    .=sqrt((x^2+x^2*y^2)-2*(x*sqrt(1+y^2))*(y*sqrt(1+x^2))
  +(y^2*(1+x^2))+1) by A3,SQUARE_1:def 4
    .=sqrt(x^2+y^2+2*(x*y)^2+1-2*x*y*sqrt(1+y^2)*sqrt(1+x^2));
  hence thesis by A5,A6;
end;

theorem
  1<=x & 1<=y implies cosh1"(x)+cosh1"(y)=cosh1"(x*y+sqrt((x^2-1)*(y^2-1)))
proof
  assume
A1: 1<=x & 1<=y;
  then
A2: x^2-1>=0 & y^2-1>=0 by Lm4;
  then
A3: (x^2-1)*(y^2-1)>=0*(y^2-1);
  0<x+sqrt(x^2-1) & 0<y+sqrt(y^2-1) by A1,Th23;
  then
A4: cosh1"(x)+cosh1"(y)
  =log(number_e,(x+sqrt(x^2-1))*(y+sqrt(y^2-1))) by Lm2,POWER:61
    .=log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+(sqrt(x^2-1))*(sqrt(y^2-1))+x*y)
    .=log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+sqrt((x^2-1)*(y^2-1))+x*y)
  by A2,SQUARE_1:97;
  set t=x*sqrt(y^2-1)+y*sqrt(x^2-1);
  t=sqrt((x*sqrt(y^2-1)+y*sqrt(x^2-1))^2) by A1,Th24,SQUARE_1:89
    .=sqrt(x^2*(sqrt(y^2-1))^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
  (y*sqrt(x^2-1))^2)
    .=sqrt(x^2*(y^2-1)+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
  (y*sqrt(x^2-1))^2) by A2,SQUARE_1:def 4
    .=sqrt(x^2*y^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+ y^2*(sqrt(x^2-1))^2)
    .=sqrt(x^2*y^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
  y^2*(x^2-1)) by A2,SQUARE_1:def 4
    .=sqrt(2*(x*y)^2-x^2-y^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1)));
  then
A5: log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+sqrt((x^2-1)*(y^2-1))+x*y)
  =log(number_e,(sqrt(2*(x*y)^2-x^2-y^2+2*x*y*(sqrt(y^2-1)
  *sqrt(x^2-1))))+sqrt((x^2-1)*(y^2-1))+x*y)
    .=log(number_e,(sqrt(2*(x*y)^2-x^2-y^2+2*x*y*sqrt((y^2-1)*(x^2-1))))
  +sqrt((x^2-1)*(y^2-1))+x*y) by A2,SQUARE_1:97;
  cosh1"(x*y+sqrt((x^2-1)*(y^2-1)))
  =log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
  2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+((sqrt((x^2-1)*(y^2-1)))^2)-1))
    .=log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
  2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+(x^2-1)*(y^2-1)-1)) by A3,SQUARE_1:def 4
    .=log(number_e,((sqrt(2*(x*y)^2-x^2-y^2+2*x*y*sqrt((y^2-1)*(x^2-1)))))
  +(sqrt((x^2-1)*(y^2-1))+x*y));
  hence thesis by A4,A5;
end;

theorem
  1<=x & 1<=y implies cosh2"(x)+cosh2"(y)=cosh2"(x*y+sqrt((x^2-1)*(y^2-1)))
proof
  assume
A1: 1<=x & 1<=y;
  then
A2: x^2-1>=0 & y^2-1>=0 by Lm4;
  then
A3: (x^2-1)*(y^2-1)>=0*(y^2-1);
A4: 0<x+sqrt(x^2-1) & 0<y+sqrt(y^2-1) by A1,Th23;
A5: cosh2"(x)+cosh2"(y)
  =(-1)*(log(number_e,x+sqrt(x^2-1))+log(number_e,y+sqrt(y^2-1)))
    .=(-1)*log(number_e,(x+sqrt(x^2-1))*(y+sqrt(y^2-1))) by A4,Lm2,POWER:61

.=-log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+(sqrt(x^2-1))*(sqrt(y^2-1))+x*y)
    .=-log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+sqrt((x^2-1)*(y^2-1))+x*y)
  by A2,SQUARE_1:97;
  set t=x*sqrt(y^2-1)+y*sqrt(x^2-1);
  t=sqrt((x*sqrt(y^2-1)+y*sqrt(x^2-1))^2) by A1,Th24,SQUARE_1:89
    .=sqrt(x^2*(sqrt(y^2-1))^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
  (y*sqrt(x^2-1))^2)
    .=sqrt(x^2*(y^2-1)+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
  (y*sqrt(x^2-1))^2) by A2,SQUARE_1:def 4
    .=sqrt(x^2*y^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+ y^2*(sqrt(x^2-1))^2)
    .=sqrt(x^2*y^2-x^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1))+
  y^2*(x^2-1)) by A2,SQUARE_1:def 4
    .=sqrt(2*(x*y)^2-x^2-y^2+2*(x*sqrt(y^2-1))*(y*sqrt(x^2-1)));
  then
A6: -log(number_e,x*sqrt(y^2-1)+y*sqrt(x^2-1)+sqrt((x^2-1)*(y^2-1))+x*y)
  =-log(number_e,(sqrt(2*(x*y)^2-x^2-y^2+2*x*y*(sqrt(y^2-1)
  *sqrt(x^2-1))))+sqrt((x^2-1)*(y^2-1))+x*y)
    .=-log(number_e,(sqrt(2*(x*y)^2-x^2-y^2+2*x*y*sqrt((y^2-1)*(x^2-1))))
  +sqrt((x^2-1)*(y^2-1))+x*y) by A2,SQUARE_1:97;
  cosh2"(x*y+sqrt((x^2-1)*(y^2-1)))
  =-log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
  2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+((sqrt((x^2-1)*(y^2-1)))^2)-1))
    .=-log(number_e,(x*y+sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2+
  2*(x*y)*(sqrt((x^2-1)*(y^2-1)))+(x^2-1)*(y^2-1)-1)) by A3,SQUARE_1:def 4
    .=-log(number_e,((sqrt(2*(x*y)^2-x^2-y^2+2*x*y*sqrt((y^2-1)*(x^2-1)))))
  +(sqrt((x^2-1)*(y^2-1))+x*y));
  hence thesis by A5,A6;
end;

theorem
  1<=x & 1<=y & abs(y)<=abs(x) implies
  cosh1"(x)-cosh1"(y)=cosh1"(x*y-sqrt((x^2-1)*(y^2-1)))
proof
  assume
A1: 1<=x & 1<=y & abs(y)<=abs(x);
  then
A2: (y-sqrt(y^2-1))>0 by Th25;
A3: 0<x+sqrt(x^2-1) & 0<y+sqrt(y^2-1) by A1,Th23;
A4: 0<=x^2-1 & 0<=y^2-1 by A1,Lm4;
  then
A5: 0*(y^2-1)<=(x^2-1)*(y^2-1);
A6: cosh1"(x)-cosh1"(y)
  =log(number_e,((x+sqrt(x^2-1))/(y+sqrt(y^2-1)))) by A3,Lm2,POWER:62
    .=log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
  ((y+sqrt(y^2-1))*(y-sqrt(y^2-1))))) by A2,XCMPLX_1:92
    .=log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/ (y^2-(sqrt(y^2-1))^2)))
    .=log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
  (y^2-(y^2-1)))) by A4,SQUARE_1:def 4
    .=log(number_e,(x*y-x*sqrt(y^2-1)+y*sqrt(x^2-1)-sqrt(x^2-1)*
  (sqrt(y^2-1))))
    .=log(number_e,(x*y-x*sqrt(y^2-1)+y*sqrt(x^2-1)-sqrt((x^2-1)*(y^2-1))))
  by A4,SQUARE_1:97
    .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+y*sqrt(x^2-1)-x*sqrt(y^2-1)));
  set t=y*sqrt(x^2-1)-x*sqrt(y^2-1);
A7: t=sqrt((y*sqrt(x^2-1)-x*sqrt(y^2-1))^2) by A1,Th26,SQUARE_1:89
    .=sqrt(y^2*(sqrt(x^2-1))^2-2*(y*sqrt(x^2-1))*
  (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2)
    .=sqrt(y^2*(x^2-1)-2*(y*sqrt(x^2-1))*
  (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by A4,SQUARE_1:def 4
    .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))* (x*sqrt(y^2-1))+x^2*(sqrt(y^2-1))^2)
    .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*
  (x*sqrt(y^2-1))+x^2*(y^2-1)) by A4,SQUARE_1:def 4
    .=sqrt(2*(x*y)^2-x^2-y^2-2*x*y*(sqrt(x^2-1)*sqrt(y^2-1)))
    .=sqrt(2*(x*y)^2-x^2-y^2-2*x*y*(sqrt((x^2-1)*(y^2-1)))) by A4,SQUARE_1:97;
  cosh1"(x*y-sqrt((x^2-1)*(y^2-1)))
  =log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
  (sqrt((x^2-1)*(y^2-1)))+(sqrt((x^2-1)*(y^2-1)))^2-1))
    .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
  (sqrt((x^2-1)*(y^2-1)))+((x^2-1)*(y^2-1))-1)) by A5,SQUARE_1:def 4
    .=log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt(2*(x*y)^2-x^2-y^2
  -2*x*y*(sqrt((x^2-1)*(y^2-1)))));
  hence thesis by A6,A7;
end;

theorem
  1<=x & 1<=y & abs(y)<=abs(x) implies
  cosh2"(x)-cosh2"(y)=cosh2"(x*y-sqrt((x^2-1)*(y^2-1)))
proof
  assume
A1: 1<=x & 1<=y & abs(y)<=abs(x);
  then
A2: (y-sqrt(y^2-1))>0 by Th25;
A3: 0<x+sqrt(x^2-1) & 0<y+sqrt(y^2-1) by A1,Th23;
A4: 0<=x^2-1 & 0<=y^2-1 by A1,Lm4;
  then
A5: 0*(y^2-1)<=(x^2-1)*(y^2-1);
A6: cosh2"(x)-cosh2"(y)
  =-(log(number_e,x+sqrt(x^2-1))-log(number_e,y+sqrt(y^2-1)))
    .=-log(number_e,((x+sqrt(x^2-1))/(y+sqrt(y^2-1)))) by A3,Lm2,POWER:62
    .=-log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
  ((y+sqrt(y^2-1))*(y-sqrt(y^2-1))))) by A2,XCMPLX_1:92
    .=-log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
  (y^2-(sqrt(y^2-1))^2)))
    .=-log(number_e,(((x+sqrt(x^2-1))*(y-sqrt(y^2-1)))/
  (y^2-(y^2-1)))) by A4,SQUARE_1:def 4
    .=-log(number_e,(x*y-x*sqrt(y^2-1)+y*sqrt(x^2-1)-sqrt(x^2-1)*
  (sqrt(y^2-1))))
    .=-log(number_e,(x*y-x*sqrt(y^2-1)+y*sqrt(x^2-1)-sqrt((x^2-1)*(y^2-1))))
  by A4,SQUARE_1:97
    .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1))+y*sqrt(x^2-1)-x*sqrt(y^2-1)));
  set t=y*sqrt(x^2-1)-x*sqrt(y^2-1);
A7: t=sqrt((y*sqrt(x^2-1)-x*sqrt(y^2-1))^2) by A1,Th26,SQUARE_1:89
    .=sqrt(y^2*(sqrt(x^2-1))^2-2*(y*sqrt(x^2-1))*
  (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2)
    .=sqrt(y^2*(x^2-1)-2*(y*sqrt(x^2-1))*
  (x*sqrt(y^2-1))+(x*sqrt(y^2-1))^2) by A4,SQUARE_1:def 4
    .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))* (x*sqrt(y^2-1))+x^2*(sqrt(y^2-1))^2)
    .=sqrt((x*y)^2-y^2-2*(y*sqrt(x^2-1))*
  (x*sqrt(y^2-1))+x^2*(y^2-1)) by A4,SQUARE_1:def 4
    .=sqrt(2*(x*y)^2-x^2-y^2-2*x*y*(sqrt(x^2-1)*sqrt(y^2-1)))
    .=sqrt(2*(x*y)^2-x^2-y^2-2*x*y*(sqrt((x^2-1)*(y^2-1)))) by A4,SQUARE_1:97;
  cosh2"(x*y-sqrt((x^2-1)*(y^2-1)))
  =-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
  (sqrt((x^2-1)*(y^2-1)))+(sqrt((x^2-1)*(y^2-1)))^2-1))
    .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt((x*y)^2-2*(x*y)*
  (sqrt((x^2-1)*(y^2-1)))+((x^2-1)*(y^2-1))-1)) by A5,SQUARE_1:def 4
    .=-log(number_e,(x*y-sqrt((x^2-1)*(y^2-1)))+sqrt(2*(x*y)^2-x^2-y^2
  -2*x*y*(sqrt((x^2-1)*(y^2-1)))));
  hence thesis by A6,A7;
end;

theorem
  x^2<1 & y^2<1 implies tanh"(x)+tanh"(y)=tanh"((x+y)/(1+x*y))
proof
  assume
A1: x^2<1 & y^2<1;
  then
A2: 0<(1+x)/(1-x) & 0<(1+y)/(1-y) by Lm5;
A3: x*y+1<>0 by A1,Th27;
A4: tanh"(x)+tanh"(y)
  =(1/2)*(log(number_e,(1+x)/(1-x))+log(number_e,(1+y)/(1-y)))
    .=(1/2)*(log(number_e,((1+x)/(1-x))*((1+y)/(1-y)))) by A2,Lm2,POWER:61
    .=(1/2)*(log(number_e,(((1+x)*(1+y))/((1-x)*(1-y))))) by XCMPLX_1:77
    .=(1/2)*(log(number_e,((1+x+y+x*y)/(1+x*y-x-y))));
  tanh"((x+y)/(1+x*y))
  =(1/2)*log(number_e,(((x+y)+(1+x*y)*1)/(1+x*y))/(1-((x+y)/(1+x*y))))
  by A3,XCMPLX_1:114
    .=(1/2)*log(number_e,((x+y+1+x*y)/(1+x*y))/(((1*(1+x*y)-(x+y))/(1+x*y))))
  by A3,XCMPLX_1:128
    .=(1/2)*log(number_e,(1+x+y+x*y)/(1+x*y-x-y)) by A3,XCMPLX_1:55;
  hence thesis by A4;
end;

theorem
  x^2<1 & y^2<1 implies tanh"(x)-tanh"(y)=tanh"((x-y)/(1-x*y))
proof
  assume
A1: x^2<1 & y^2<1;
  then
A2: 0<(1+x)/(1-x) & 0<(1+y)/(1-y) by Lm5;
A3: 1-x*y<>0 by A1,Th28;
A4: tanh"(x)-tanh"(y)
  =(1/2)*(log(number_e,(1+x)/(1-x))-log(number_e,(1+y)/(1-y)))
    .=(1/2)*(log(number_e,((1+x)/(1-x))/((1+y)/(1-y)))) by A2,Lm2,POWER:62
    .=(1/2)*(log(number_e,((1+x)*(1-y))/((1-x)*(1+y)))) by XCMPLX_1:85
    .=(1/2)*(log(number_e,(1-y+x-x*y)/(1+y-x-x*y)));
  tanh"((x-y)/(1-x*y))
  =(1/2)*log(number_e,(((x-y)+(1-x*y)*1)/(1-x*y))/(1-((x-y)/(1-x*y))))
  by A3,XCMPLX_1:114
    .=(1/2)*log(number_e,((x-y+1-x*y)/(1-x*y))/((1*(1-x*y)-(x-y))/(1-x*y)))
  by A3,XCMPLX_1:128
    .=(1/2)*(log(number_e,(1-y+x-x*y)/(1+y-x-x*y))) by A3,XCMPLX_1:55;
  hence thesis by A4;
end;

theorem
  0<x & ((x-1)/(x+1))^2<1 implies log(number_e,x)=2*tanh"((x-1)/(x+1))
proof
  assume 0<x & ((x-1)/(x+1))^2<1;
  then
A1: x+1>0;
  then 2*tanh"((x-1)/(x+1))
  =log(number_e,(((x-1)+(x+1)*1)/(x+1))/(1-(x-1)/(x+1))) by XCMPLX_1:114
    .=log(number_e,((2*x)/(x+1))/((1*(x+1)-(x-1))/(x+1))) by A1,XCMPLX_1:128
    .=log(number_e,(2*x)/2) by A1,XCMPLX_1:55
    .=log(number_e,x);
  hence thesis;
end;

theorem
  0<x & ((x^2-1)/(x^2+1))^2<1 implies log(number_e,x)=tanh"((x^2-1)/(x^2+1))
proof
  assume
A1: 0<x & ((x^2-1)/(x^2+1))^2<1;
A2: x^2+1>0 by Lm8;
  then tanh"((x^2-1)/(x^2+1))
  =(1/2)*log(number_e,(((x^2-1)+(x^2+1)*1)/(x^2+1))/(1-(x^2-1)/(x^2+1)))
  by XCMPLX_1:114
    .=(1/2)*log(number_e,((2*x^2)/(x^2+1))/((1*(x^2+1)-(x^2-1))/(x^2+1)))
  by A2,XCMPLX_1:128
    .=(1/2)*log(number_e,2*x^2/2) by A2,XCMPLX_1:55
    .=(1/2)*log(number_e,x to_power 2) by POWER:53
    .=(1/2)*(2*log(number_e,x)) by A1,Lm2,POWER:63
    .=log(number_e,x);
  hence thesis;
end;

theorem
  1<x & 1<=(x^2+1)/(2*x) implies log(number_e,x)=cosh1"((x^2+1)/(2*x))
proof
  assume
A1: 1<x & 1<=(x^2+1)/(2*x);
  then
A2: 1*2<2*x by XREAL_1:70;
  then 1<2*x by XXREAL_0:2;
  then
A3: 1<(2*x)^2 by SQUARE_1:59,78;
  x<x^2 by A1,SQUARE_1:76;
  then 1<x^2 by A1,XXREAL_0:2;
  then
A4: 1-1<x^2-1 by XREAL_1:16;
  then
A5: 0<(x^2-1)^2 by SQUARE_1:74;
  cosh1"((x^2+1)/(2*x))
  =log(number_e,((x^2+1)/(2*x))+sqrt(((x^2+1))^2/(2*x)^2-1)) by XCMPLX_1:77
    .=log(number_e,((x^2+1)/(2*x))+sqrt((((x^2)^2+2*x^2+1)-1*(2*x)^2)/(2*x)^2))
  by A3,XCMPLX_1:127
    .=log(number_e,((x^2+1)/(2*x))+sqrt(((x^2)-1)^2)/sqrt((2*x)^2))
  by A3,A5,SQUARE_1:99
    .=log(number_e,((x^2+1)/(2*x))+(x^2-1)/sqrt((2*x)^2)) by A4,SQUARE_1:89
    .=log(number_e,((x^2+1)/(2*x))+(x^2-1)/(2*x)) by A2,SQUARE_1:89
    .=log(number_e,((x^2+1)+(x^2-1))/(2*x)) by XCMPLX_1:63
    .=log(number_e,(2*x^2)/(2*x))
    .=log(number_e,(x*x)/x) by XCMPLX_1:92
    .=log(number_e,x/(x/x)) by XCMPLX_1:78
    .=log(number_e,x/1) by A1,XCMPLX_1:60
    .=log(number_e,x);
  hence thesis;
end;

theorem
  0<x & x<1 & 1<=(x^2+1)/(2*x) implies log(number_e,x)=cosh2"((x^2+1)/(2*x))
proof
  assume
A1: 0<x & x<1 & 1<=(x^2+1)/(2*x);
  then
A2: 0*2<x*2;
  then
A3: 0<(2*x)^2 by SQUARE_1:74;
  x^2<x by A1,SQUARE_1:75;
  then x^2<1 by A1,XXREAL_0:2;
  then
A4: x^2-x^2<1-x^2 by XREAL_1:16;
  then
A5: 0<(1-x^2)^2 by SQUARE_1:74;
A6: (1/x)=x to_power (-1) by A1,Th1;
  cosh2"((x^2+1)/(2*x))
  =-log(number_e,((x^2+1)/(2*x))+sqrt(((x^2+1))^2/(2*x)^2-1)) by XCMPLX_1:77

.=-log(number_e,((x^2+1)/(2*x))+sqrt((((x^2)^2+2*x^2+1)-1*(2*x)^2)/(2*x)^2))
  by A3,XCMPLX_1:127
    .=-log(number_e,((x^2+1)/(2*x))+sqrt((1-(x^2))^2)/sqrt(2*x)^2)
  by A3,A5,SQUARE_1:99
    .=-log(number_e,((x^2+1)/(2*x))+(1-(x^2))/sqrt(2*x)^2) by A4,SQUARE_1:89
    .=-log(number_e,((x^2+1)/(2*x))+(1-x^2)/(2*x)) by A2,SQUARE_1:89
    .=-log(number_e,((x^2+1)+(1-x^2))/(2*x)) by XCMPLX_1:63
    .=-log(number_e,(2*1)/(2*x))
    .=-log(number_e,1/x) by XCMPLX_1:92
    .=-((-1)*log(number_e,x)) by A1,A6,Lm2,POWER:63
    .=log(number_e,x);
  hence thesis;
end;

theorem
  0<x implies log(number_e,x)=sinh"((x^2-1)/(2*x))
proof
  assume
A1: 0<x;
  then
A2: 0*2<x*2 by XREAL_1:70;
  then
A3: 0<(2*x)^2 by SQUARE_1:74;
  x^2>=0 by XREAL_1:65;
  then
A4: x^2+1>=0+1 by XREAL_1:9;
  then
A5: (x^2+1)^2>0 by SQUARE_1:74;
  sinh"((x^2-1)/(2*x))
  =log(number_e,(((x^2-1)/(2*x))+sqrt((x^2-1)^2/(2*x)^2+1))) by XCMPLX_1:77
    .=log(number_e,(((x^2-1)/(2*x))+sqrt((((x^2)^2-2*x^2+1)+
  (2*x)^2*1)/(2*x)^2))) by A3,XCMPLX_1:114
    .=log(number_e,(((x^2-1)/(2*x))+sqrt((x^2+1)^2)/sqrt((2*x)^2)))
  by A3,A5,SQUARE_1:99
    .=log(number_e,(((x^2-1)/(2*x))+(x^2+1)/sqrt((2*x)^2))) by A4,SQUARE_1:89
    .=log(number_e,((x^2-1)/(2*x)+(x^2+1)/(2*x))) by A2,SQUARE_1:89
    .=log(number_e,(((x^2-1)+(x^2+1))/(2*x))) by XCMPLX_1:63
    .=log(number_e,((2*x^2)/(2*x)))
    .=log(number_e,(x*x)/x) by XCMPLX_1:92
    .=log(number_e,x/(x/x)) by XCMPLX_1:78
    .=log(number_e,x/1) by A1,XCMPLX_1:60
    .=log(number_e,x);
  hence thesis;
end;

theorem
  y=(1/2)*(exp_R(x)-exp_R(-x)) implies x=log(number_e,(y+sqrt(y^2+1)))
proof
  assume
A1: y=(1/2)*(exp_R(x)-exp_R(-x));
A2: exp_R(x)>0 by SIN_COS:60;
A3: 2*y*exp_R(x)=(exp_R(x)-1/exp_R(x))*exp_R(x) by A1,TAYLOR_1:4;
  set t=exp_R(x);
  2*y*t=t*t-(1/t)*t by A3;
  then 2*y*t=t^2-(t*1)/t by XCMPLX_1:75;
  then 2*y*t-2*y*t=t^2-1-2*y*t by A2,XCMPLX_1:60;
  then
A4: 1*t^2+(-2*y)*t+(-1)=0;
A5: delta(1,-2*y,-1)=((-2)*y)^2-4*1*(-1) by QUIN_1:def 1
    .=4*(y^2)+4;
A6: 0<=y^2 by XREAL_1:65;
  then
A7: 0+1<=y^2+1 by XREAL_1:8;
  4*0<=4*y^2 by A6;
  then 0+4<=4*y^2+4 by XREAL_1:8;
  then t = (-(-2*y)+sqrt delta(1,-2*y,-1))/(2*1)
  or t = (-(-2*y)-sqrt delta(1,-2*y,-1))/(2*1) by A4,A5,QUIN_1:15;
  then t=(2*y+sqrt(4)*sqrt(y^2+1))/2
  or t=(2*y-sqrt(4*(y^2+1)))/2 by A5,A7,SQUARE_1:97;
  then t=(2*y+2*sqrt(y^2+1))/2
  or t=(2*y-2*sqrt(y^2+1))/2 by A7,SQUARE_1:85,97;
  then
A8: exp_R(x)=y+sqrt(y^2+1) or exp_R(x)=y-sqrt(y^2+1);
  y<sqrt(y^2+1)+0 by Lm10;
  hence thesis by A2,A8,TAYLOR_1:12,XREAL_1:21;
end;

theorem
  y=(1/2)*(exp_R(x)+exp_R(-x)) & 1<=y implies
  x=log(number_e,(y+sqrt(y^2-1))) or x=-log(number_e,(y+sqrt(y^2-1)))
proof
  assume
A1: y=(1/2)*(exp_R(x)+exp_R(-x)) & 1<=y;
  then
A2: 0<=(y^2)-1 by Lm4;
  then
A3: 0*4<=((y^2)-1)*4;
A4: y+sqrt(y^2-1)>0 by A1,Lm12;
  then
A5: 1/(y+sqrt(y^2-1))=(y+sqrt(y^2-1)) to_power (-1) by Th1;
A6: 0<exp_R(x)by SIN_COS:60;
A7: 2*y*exp_R(x)=(exp_R(x)+1/exp_R(x))*exp_R(x) by A1,TAYLOR_1:4;
  set t=exp_R(x);
  2*y*t=t*t+(1/t)*t by A7;
  then 2*y*t=t^2+(t*1)/t by XCMPLX_1:75;
  then 2*y*t-2*y*t=t^2+1-2*y*t by A6,XCMPLX_1:60;
  then
A8: 0=1*t^2+(-2*y)*t+1;
A9: delta(1,-2*y,1)=((-2)*y)^2-4*1*1 by QUIN_1:def 1
    .=4*(y^2)-4;
  then t = (-(-2*y)+sqrt delta(1,-2*y,1))/(2*1)
  or t = (-(-2*y)-sqrt delta(1,-2*y,1))/(2*1) by A3,A8,QUIN_1:15;
  then t=(2*y+sqrt(4)*sqrt(y^2-1))/2
  or t=(2*y-sqrt(4*(y^2-1)))/2 by A2,A9,SQUARE_1:97;
  then t=(2*y+2*sqrt(y^2-1))/2
  or t=(2*y-2*sqrt(y^2-1))/2 by A2,SQUARE_1:85,97;
  then log(number_e,y+sqrt(y^2-1))=x
  or log(number_e,y-sqrt(y^2-1))=x by TAYLOR_1:12;
  then log(number_e,y+sqrt(y^2-1))=x
  or log(number_e,(y-sqrt(y^2-1))*(y+sqrt(y^2-1))/
  (y+sqrt(y^2-1)))=x by A4,XCMPLX_1:90;
  then log(number_e,y+sqrt(y^2-1)) = x or log(number_e,(y^2-(sqrt(y^2-1))^2)/
  (y+sqrt(y^2-1)))=x;
  then log(number_e,y+sqrt(y^2-1))=x
  or log(number_e,(y^2-(y^2-1))/(y+sqrt(y^2-1)))=x by A2,SQUARE_1:def 4;
  then log(number_e,y+sqrt(y^2-1))=x
  or (-1)*log(number_e,(y+sqrt(y^2-1)))=x by A4,A5,Lm2,POWER:63;
  hence thesis;
end;

theorem
  y=(exp_R(x)-exp_R(-x))/(exp_R(x)+exp_R(-x)) implies
  x=(1/2)*log(number_e,(1+y)/(1-y))
proof
A1: 0<exp_R(x) by SIN_COS:60;
  then 0*exp_R(x)<exp_R(x)*exp_R(x) by XREAL_1:70;
  then
A2: 0+1<(exp_R x)^2+1 by XREAL_1:10;
  assume y=(exp_R(x)-exp_R(-x))/(exp_R(x)+exp_R(-x));
  then y=(exp_R(x)-1/exp_R(x))/(exp_R(x)+exp_R(-x)) by TAYLOR_1:4;
  then y=(exp_R(x)-1/exp_R(x))/(exp_R(x)+1/exp_R(x)) by TAYLOR_1:4;
  then y=((exp_R(x)*exp_R(x)-1)/exp_R(x))/(exp_R(x)+1/exp_R(x))
  by A1,XCMPLX_1:128;
  then y=(((exp_R x)^2-1)/exp_R(x))/((1+exp_R(x)*exp_R(x))/exp_R(x)) by A1,
  XCMPLX_1:114;
  then
A3: y=((exp_R x)^2-1)/(1+(exp_R(x))^2) by A1,XCMPLX_1:55;
  then
A4: -1<y & y<1 by A1,Lm13,Th31;
A5: y-1<>0 by A3,Th31,SIN_COS:60;
A6: 0<(y+1)/(1-y) by A4,Th32;
  then
A7: sqrt((y+1)/(1-y))=((y+1)/(1-y)) to_power (1/2) by ASYMPT_1:89;
  set t=exp_R(x);
  1*y+t^2*y=(t^2-1)/(1+t^2)*(1+t^2) by A3;
  then y+t^2*y-(t^2-1)=t^2-1-(t^2-1) by A2,XCMPLX_1:88;
  then t^2*(y-1)/(y-1)=(-(y+1))/(y-1);
  then t^2*(y-1)/(y-1)=(y+1)/(-(y-1)) by XCMPLX_1:193;
  then sqrt(t^2)=sqrt((y+1)/(1-y)) by A5,XCMPLX_1:90;
  then exp_R(x)=sqrt((y+1)/(1-y)) by A1,SQUARE_1:89;
  then log(number_e,((y+1)/(1-y)) to_power (1/2))=x by A7,TAYLOR_1:12;
  hence thesis by A6,Lm2,POWER:63;
end;

theorem
  y=(exp_R(x)+exp_R(-x))/(exp_R(x)-exp_R(-x)) & x<>0 implies
  x=(1/2)*log(number_e,(y+1)/(y-1))
proof
A1: 0<exp_R(x) by SIN_COS:60;
  assume
A2: y=(exp_R(x)+exp_R(-x))/(exp_R(x)-exp_R(-x)) & x<>0;
  then
A3: exp_R(x)<>1 by Th29;
  y=(exp_R(x)+1/exp_R(x))/(exp_R(x)-exp_R(-x)) by A2,TAYLOR_1:4;
  then y=(exp_R(x)+1/exp_R(x))/(exp_R(x)-1/exp_R(x)) by TAYLOR_1:4;
  then y=(exp_R(x)+1/exp_R(x))/((exp_R(x)*exp_R(x)-1)/exp_R(x))
  by A1,XCMPLX_1:128;
  then y=((1+exp_R(x)*exp_R(x))/exp_R(x))/(((exp_R(x))^2-1)/exp_R(x)) by A1,
  XCMPLX_1:114;
  then
A4: y=(1+(exp_R x)^2)/((exp_R x)^2-1) by A1,XCMPLX_1:55;
  set t=exp_R x;
A5: 1<y or y< -1 by A3,A4,Lm14,SIN_COS:60;
  then
A6: 1-1<y-1 or y-1< -1-1 by XREAL_1:16;
A7: 0<(y+1)/(y-1) by A5,Lm9;
  then
A8: sqrt((y+1)/(y-1))=((y+1)/(y-1)) to_power (1/2) by ASYMPT_1:89;
  y*(t^2-1)=1+t^2 by A2,A4,Th30,XCMPLX_1:88;
  then t^2*(y-1)/(y-1)=(y+1)/(y-1);
  then sqrt(t^2)=sqrt((y+1)/(y-1)) by A6,XCMPLX_1:90;
  then exp_R(x)=sqrt((y+1)/(y-1)) by A1,SQUARE_1:89;
  then log(number_e,((y+1)/(y-1)) to_power (1/2))=x by A8,TAYLOR_1:12;
  hence thesis by A7,Lm2,POWER:63;
end;

theorem
  y=1/((exp_R(x)+exp_R(-x))/2) implies x=log(number_e,(1+sqrt(1-y^2))/y)
  or x=-log(number_e,(1+sqrt(1-y^2))/y)
proof
A1: 0<exp_R(x) by SIN_COS:60;
A2: log(number_e,exp_R(x))=x by TAYLOR_1:12;
  assume y=1/((exp_R(x)+exp_R(-x))/2);
  then y=(1*2)/(2*((exp_R(x)+exp_R(-x))/2)) by XCMPLX_1:92;
  then y=2/(exp_R(x)+1/exp_R(x)) by TAYLOR_1:4;
  then y=2/((1+exp_R(x)*exp_R(x))/exp_R(x)) by A1,XCMPLX_1:114;
  then
A3: y=2*(exp_R(x)/(1+(exp_R(x))^2)) by XCMPLX_1:80;
  set t=exp_R(x);
A4: y=2*t/(1+t^2) by A3,XCMPLX_1:75;
  then
A5: 0<y & y<=1 by A1,Lm15,Lm16;
  then
A6: 0<=1-y^2 & 0<=4-4*y^2 & 0<1+sqrt(1-y^2) & 0<(1+sqrt(1-y^2))/y
  by Lm19,Lm20,Lm21,Lm22;
  then
A7: 1/((1+sqrt(1-y^2))/y)=((1+sqrt(1-y^2))/y) to_power (-1) by Th1;
  1+t^2>0 by Lm8;
  then
A8: y*(1+t^2)=(2*t) by A4,XCMPLX_1:88;
A9: Polynom(y,-2,y,t)=y*t^2+(-2)*t+y by POLYEQ_1:def 2;
A10: delta(y,-2,y) =(-2)^2-4*y*y by QUIN_1:def 1
    .=4-4*y^2;
  then t=(-(-2)+sqrt delta(y,-2,y))/(2*y)
  or t=(-(-2)-sqrt delta(y,-2,y))/(2*y) by A5,A6,A8,A9,QUIN_1:15;
  then t=(2+sqrt(4*(1-y^2)))/(2*y) or t=(2-sqrt(4*(1-y^2)))/(2*y) by A10;
  then t=(2+2*sqrt(1-y^2))/(2*y)
  or t=(2-2*sqrt(1-y^2))/(2*y) by A6,SQUARE_1:85,97;
  then t=(2*(1+sqrt(1-y^2)))/(2*y) or t=(2*(1-sqrt(1-y^2)))/(2*y);
  then t=(1+sqrt(1-y^2))/y or t=(1-sqrt(1-y^2))/y by XCMPLX_1:92;
  then t=(1+sqrt(1-y^2))/y
  or t=((1-sqrt(1-y^2))*(1+sqrt(1-y^2)))/(y*(1+sqrt(1-y^2)))
  by A6,XCMPLX_1:92;
  then t=(1+sqrt(1-y^2))/y or t=(1-(sqrt(1-y^2))^2)/(y*(1+sqrt(1-y^2)));
  then t=(1+sqrt(1-y^2))/y
  or t=(1-(1-y^2))/(y*(1+sqrt(1-y^2))) by A6,SQUARE_1:def 4;
  then t=(1+sqrt(1-y^2))/y or t=y/(1+sqrt(1-y^2)) by A5,XCMPLX_1:92;
  then log(number_e,(1+sqrt(1-y^2))/y)=x
  or log(number_e,((1+sqrt(1-y^2))/y) to_power (-1))=x by A2,A7,XCMPLX_1:57;
  then log(number_e,(1+sqrt(1-y^2))/y)=x
  or (-1)*log(number_e,((1+sqrt(1-y^2))/y))=x by A6,Lm2,POWER:63;
  hence thesis;
end;

theorem
  y=1/((exp_R(x)-exp_R(-x))/2) & x<>0 implies
  x=log(number_e,(1+sqrt(1+y^2))/y) or x=log(number_e,(1-sqrt(1+y^2))/y)
proof
A1: 0<exp_R(x) by SIN_COS:60;
  assume
A2: y=1/((exp_R(x)-exp_R(-x))/2) & x<>0;
  then
A3: exp_R(x)<>1 by Th29;
  then
A4: 2*exp_R(x)/((exp_R(x))^2-1)<>0 by Lm23,SIN_COS:60;
  y=(1*2)/(2*((exp_R(x)-exp_R(-x))/2)) by A2,XCMPLX_1:92;
  then y=2/(exp_R(x)-1/exp_R(x)) by TAYLOR_1:4;
  then y=2/((exp_R(x)*exp_R(x)-1)/exp_R(x)) by A1,XCMPLX_1:128;
  then y=2*(exp_R(x)/((exp_R(x))^2-1)) by XCMPLX_1:80;
  then
A5: y=2*exp_R(x)/((exp_R(x))^2-1) by XCMPLX_1:75;
  set t=exp_R x;
  y*(t^2-1)=2*t by A2,A5,Th30,XCMPLX_1:88;
  then
A6: y*t^2+(-2)*t+(-y)=0;
A7: delta(y,-2,-y)=(-2)^2-4*y*(-y) by QUIN_1:def 1
    .=4+4*y^2;
  per cases by A4,A5;
  suppose 0<y;
    then
A8: (1-sqrt(1+y^2))/y<0 & 0<(1+sqrt(1+y^2))/y by Lm17,Lm18;
A9: 0<1+y^2 by Lm8;
    then 0*4<4*(1+y^2) by XREAL_1:70;
    then t=(2+sqrt delta(y,-2,-y))/(2*y)
    or t=(-(-2)-sqrt delta(y,-2,-y))/(2*y) by A1,A3,A5,A6,A7,Lm23,QUIN_1:15;
    then t=(2+sqrt(4)*sqrt(1+y^2))/(2*y)
    or t=(2-sqrt(4*(1+y^2)))/(2*y) by A7,A9,SQUARE_1:97;
    then t=(2*(1+sqrt(1+y^2)))/(2*y)
    or t=(2-2*sqrt(1+y^2))/(2*y) by A9,SQUARE_1:85,97;
    then t=(1+sqrt(1+y^2))/y or t=(2*(1-sqrt(1+y^2)))/(2*y) by XCMPLX_1:92;
    hence thesis by A1,A8,TAYLOR_1:12,XCMPLX_1:92;
  end;
  suppose y<0;
    then
A10: 0<(1-sqrt(1+y^2))/y & (1+sqrt(1+y^2))/y<0 by Lm24,Lm25;
A11: 0<1+y^2 by Lm8;
    then 0*4<4*(1+y^2) by XREAL_1:70;
    then t=(2+sqrt delta(y,-2,-y))/(2*y)
    or t=(-(-2)-sqrt delta(y,-2,-y))/(2*y) by A1,A3,A5,A6,A7,Lm23,QUIN_1:15;
    then t=(2+sqrt(4)*sqrt(1+y^2))/(2*y)
    or t=(2-sqrt(4*(1+y^2)))/(2*y) by A7,A11,SQUARE_1:97;
    then t=(2*(1+sqrt(1+y^2)))/(2*y)
    or t=(2-2*sqrt(1+y^2))/(2*y) by A11,SQUARE_1:85,97;
    then t=(1+sqrt(1+y^2))/y or t=(2*(1-sqrt(1+y^2)))/(2*y) by XCMPLX_1:92;
    then exp_R(x)=(1-sqrt(1+y^2))/y by A10,SIN_COS:60,XCMPLX_1:92;
    hence thesis by TAYLOR_1:12;
  end;
end;

theorem Th69:
  cosh.(2*x)=1+2*(sinh.x)^2
proof
  2*(sinh.x)^2=2*(1/2*(cosh.(2*x)-1)) by SIN_COS2:18;
  hence thesis;
end;

theorem Th70:
  (cosh.x)^2=1+(sinh.x)^2
proof
  cosh.(2*x)=cosh.(x+x)
    .=(cosh.x)^2+(sinh.x)^2 by SIN_COS2:20;
  then 1+2*(sinh.x)^2-(sinh.x)^2=(cosh.x)^2+(sinh.x)^2-(sinh.x)^2 by Th69;
  hence thesis;
end;

theorem Th71:
  (sinh.x)^2=(cosh.x)^2-1
proof
  (cosh.x)^2-(sinh.x)^2-1=1-1 by SIN_COS2:14;
  hence thesis;
end;

theorem
  sinh(5*x)=5*sinh(x)+20*(sinh(x))|^3+16*(sinh(x))|^5
proof
  set t = sinh.x;
  sinh(5*x) =sinh.(4*x+x) by SIN_COS2:def 2
    .=sinh.(2*(2*x))*(cosh.x)+(cosh.(4*x))*t by SIN_COS2:21
    .=2*(sinh.(2*x))*(cosh.(2*x))*(cosh.x)+(cosh.(4*x))*t by SIN_COS2:23
    .=2*(2*t*(cosh.x))*(cosh.(2*x))*(cosh.x)+(cosh.(4*x))*t by SIN_COS2:23
    .=4*t*((cosh.x)^2)*(cosh.(2*x))+(cosh.(4*x))*t
    .=4*t*((cosh.x)^2)*(1+2*t^2)+(cosh.(2*(2*x)))*t by Th69
    .=4*t*((cosh.x)^2)*(1+2*t^2)+(1+2*(sinh.(2*x))^2)*t by Th69
    .=4*t*((cosh.x)^2)*(1+2*t^2)+(1+2*(2*t* (cosh.x))^2)*t by SIN_COS2:23
    .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*t^2*t*(cosh.x)^2
    .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*(t|^1*t)*t*(cosh.x)^2 by NEWTON:10
    .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*t|^(1+1)*t*(cosh.x)^2 by NEWTON:11
    .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*(t|^2*t)*(cosh.x)^2
    .=4*t*((cosh.x)^2)*(1+2*t^2)+t+8*(t|^(2+1))*(cosh.x)^2 by NEWTON:11
    .=4*t*((cosh.x)^2)*1+4*t*((cosh.x)^2)*2*t^2+t+8*t|^3*(cosh.x)^2
    .=4*t*(1+t^2)*1+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2 by Th70
    .=4*t+4*t*t*t+8*t*((cosh.x)^2)*t^2+sinh.x+8*t|^3*(cosh.x)^2
    .=4*t+4*t|^1*t*t+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2 by NEWTON:10
    .=4*t+4*(t|^1*t)*t+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2
    .=4*t+4*t|^(1+1)*t+8*t*((cosh.x)^2)* t^2+t+8*t|^3*(cosh.x)^2 by NEWTON:11
    .=4*t+4*(t|^2*t)+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2
    .=4*t+4*(t|^(2+1))+8*t*((cosh.x)^2)*t^2+t+8*t|^3*(cosh.x)^2 by NEWTON:11
    .=4*t+4*t|^3+8*t*t*t*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2
    .=4*t+4*t|^3+8*t|^1*t*t* ((cosh.x)^2)+t+8*t|^3*(cosh.x)^2 by NEWTON:10
    .=4*t+4*t|^3+8*(t|^1*t)*t*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2
    .=4*t+4*t|^3+8*(t|^(1+1))*t* ((cosh.x)^2)+t+8*t|^3*(cosh.x)^2 by NEWTON:11
    .=4*t+4*t|^3+8*(t|^2*t)*((cosh.x)^2)+t+8*t|^3*(cosh.x)^2
    .=4*t+4*t|^3+8*(t|^(2+1))* ((cosh.x)^2)+t+8*t|^3*(cosh.x)^2 by NEWTON:11
    .=4*t+4*t|^3+8*t|^3*(1+t^2)+t+8*t|^3*(cosh.x)^2 by Th70
    .=4*t+4*t|^3+8*t|^3+8*(t|^3*t)*t+t+8*t|^3*(cosh.x)^2
    .=4*t+4*t|^3+8*t|^3+8*(t|^(3+1))*t+t+8*t|^3*(cosh.x)^2 by NEWTON:11
    .=4*t+4*t|^3+8*t|^3+8*(t|^4*t)+t+8*t|^3*(cosh.x)^2
    .=4*t+4*t|^3+8*t|^3+8*(t|^(4+1))+t+8*t|^3*(cosh.x)^2 by NEWTON:11
    .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3*(1+t^2) by Th70
    .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^3*t)*t
    .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^(3+1))*t by NEWTON:11
    .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^4*t)
    .=4*t+4*t|^3+8*t|^3+8*t|^5+t+8*t|^3+8*(t|^(4+1)) by NEWTON:11
    .=5*t+20*t|^3+(8*t|^5+8*t|^5)
    .=5*sinh(x)+20*t|^3+16*t|^5 by SIN_COS2:def 2
    .=5*sinh(x)+20*(sinh x)|^3+16*t|^5 by SIN_COS2:def 2
    .=5*sinh(x)+20*(sinh x)|^3+16*(sinh x)|^5 by SIN_COS2:def 2;
  hence thesis;
end;

theorem
  cosh(5*x)=5*cosh(x)-20*(cosh(x))|^3+16*(cosh(x))|^5
proof
  set t = cosh.x, u = sinh.x;
  cosh(5*x) = cosh.(4*x+x) by SIN_COS2:def 4
    .=(cosh.(2*(2*x)))*t+(sinh.(4*x))*u by SIN_COS2:20
    .=(1+2*(sinh.(2*x))^2)*t+(sinh.(4*x))*u by Th69
    .=(1+2*((2*u)*t)^2)*t+(sinh.(2*(2*x)))*u by SIN_COS2:23
    .=t+8*u^2*(t^2*t)+(sinh.(2*(2*x)))*u
    .=t+8*u^2*(t|^1*t*t)+(sinh.(2*(2*x)))*u by NEWTON:10
    .=t+8*u^2*(t|^(1+1)*t)+(sinh.(2*(2*x)))*u by NEWTON:11
    .=t+8*(sinh.x)^2*(t|^(2+1))+(sinh.(2*(2*x)))*u by NEWTON:11
    .=t+8*(t^2-1)*(t|^3)+(sinh.(2*(2*x)))*u by Th71
    .=t+8*(t|^3*t*t)-8*t|^3+(sinh.(2*(2*x)))*u
    .=t+8*(t|^(3+1)*t)-8*t|^3+(sinh.(2*(2*x)))*u by NEWTON:11
    .=t+8*(t|^(4+1))-8*t|^3+(sinh.(2*(2*x)))*u by NEWTON:11
    .=t+8*(t|^5)-8*t|^3+(2*(sinh.(2*x))*(cosh.(2*x)))*u by SIN_COS2:23
    .=t+8*(t|^5)-8*t|^3+(2*(2*u*t)*(cosh.(2*x)))*u by SIN_COS2:23
    .=t+8*(t|^5)-8*t|^3+4*u^2*t*(cosh.(2*x))
    .=t+8*(t|^5)-8*t|^3+4*(t^2-1)*t*(cosh.(2*x)) by Th71
    .=t+8*(t|^5)-8*t|^3+4*(t*t*t)*(cosh.(2*x))-4*t*(cosh.(2*x))
    .=t+8*(t|^5)-8*t|^3+4*(t|^1*t*t)*(cosh.(2*x))-4*t*(cosh.(2*x))
  by NEWTON:10
    .=t+8*(t|^5)-8*t|^3+4*(t|^(1+1)*t)*(cosh.(2*x))-4*t*(cosh.(2*x))
  by NEWTON:11
    .=t+8*(t|^5)-8*t|^3+4*(t|^(2+1))
  *(cosh.(2*x))-4*t*(cosh.(2*x)) by NEWTON:11
    .=t+8*(t|^5)-8*t|^3+4*t|^3*(2*t^2-1)-4*t*(cosh.(2*x)) by SIN_COS2:23
    .=t+8*(t|^5)-8*t|^3+8*(t|^3*t*t)-4*t|^3-4*t*(cosh.(2*x))
    .=t+8*(t|^5)-8*t|^3+8*(t|^(3+1)*t)-4*t|^3-4*t*(cosh.(2*x)) by NEWTON:11
    .=t+8*(t|^5)-8*t|^3+8*(t|^(4+1))-4*t|^3-4*t*(cosh.(2*x)) by NEWTON:11
    .=t+16*(t|^5)-12*t|^3-4*t*(2*t^2-1) by SIN_COS2:23
    .=5*t+16*(t|^5)-12*t|^3-8*(t*t*t)
    .=5*t+16*(t|^5)-12*t|^3-8*(t|^1*t*t) by NEWTON:10
    .=5*t+16*(t|^5)-12*t|^3-8*(t|^(1+1)*t) by NEWTON:11
    .=5*t+16*(t|^5)-12*t|^3-8*(t|^(2+1)) by NEWTON:11
    .=5*t-20*t|^3+16*(t|^5)
    .=5*(cosh x)-20*t|^3+16*(t|^5) by SIN_COS2:def 4
    .=5*(cosh x)-20*(cosh x)|^3+16*(t|^5) by SIN_COS2:def 4
    .=5*cosh(x)-20*(cosh x)|^3+16*(cosh x)|^5 by SIN_COS2:def 4;
  hence thesis;
end;

