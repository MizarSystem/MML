:: Trigonometric Functions and Existence of Circle Ratio
::  by Yuguang Yang and Yasunari Shidama
::
:: Received October 22, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies NUMBERS, REAL_1, XREAL_0, ORDINAL1, SUBSET_1, COMSEQ_1, SEQ_1,
      FDIFF_1, XXREAL_0, CARD_1, COMPLEX1, FUNCT_1, RELAT_1, XCMPLX_0, ARYTM_3,
      NAT_1, REALSET1, COMSEQ_3, NEWTON, ARYTM_1, VALUED_1, SERIES_1, PREPOWER,
      CARD_3, SEQ_2, ORDINAL2, SQUARE_1, VALUED_0, XXREAL_2, FUNCT_2, TARSKI,
      RCOMP_1, XXREAL_1, PARTFUN1, XBOOLE_0, FCONT_1, SIN_COS;
 notations TARSKI, XBOOLE_0, SUBSET_1, ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0,
      XREAL_0, COMPLEX1, REAL_1, RELAT_1, FUNCT_1, RELSET_1, FCONT_1, FUNCT_2,
      VALUED_1, SEQ_1, SEQ_2, FDIFF_1, SERIES_1, NAT_1, NEWTON, SQUARE_1,
      PARTFUN1, VALUED_0, RCOMP_1, NAT_D, RFUNCT_1, COMSEQ_1, COMSEQ_2,
      COMSEQ_3, PREPOWER;
 constructors PARTFUN1, FUNCT_4, ARYTM_0, REAL_1, SQUARE_1, NAT_1, RCOMP_1,
      RFUNCT_1, COMSEQ_2, RFUNCT_2, FCONT_1, FDIFF_1, PREPOWER, COMSEQ_3,
      NAT_D, SEQ_1, RECDEF_1, SEQM_3, SERIES_1, SEQ_2, VALUED_1, RELSET_1;
 registrations XBOOLE_0, ORDINAL1, RELSET_1, FUNCT_2, NUMBERS, XCMPLX_0,
      XXREAL_0, XREAL_0, SQUARE_1, NAT_1, MEMBERED, RCOMP_1, COMSEQ_2, FDIFF_1,
      NEWTON, COMSEQ_3, VALUED_0, VALUED_1, SEQ_4, FCONT_3;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions COMSEQ_3, SQUARE_1, SUBSET_1, VALUED_1, XCMPLX_0, COMPLEX1;
 theorems NAT_1, ABSVALUE, INT_1, SEQ_1, SEQ_2, SERIES_1, SEQM_3, SEQ_4,
      PREPOWER, COMPLEX1, COMSEQ_1, COMSEQ_2, SQUARE_1, COMSEQ_3, FCONT_2,
      RCOMP_1, FUNCT_1, FDIFF_1, FDIFF_2, FCONT_1, TARSKI, FUNCT_2, RFUNCT_1,
      SUBSET_1, POWER, ROLLE, NEWTON, RELAT_1, XREAL_0, XBOOLE_0, XBOOLE_1,
      XCMPLX_0, XCMPLX_1, XREAL_1, XXREAL_0, ORDINAL1, VALUED_1, XXREAL_1,
      VALUED_0;
 schemes NAT_1, SEQ_1, COMSEQ_1, CLASSES1, BINOP_2, FUNCT_2;

begin

reserve q,th,r for Real,
  a,b,p for real number,
  w,z,z1,z2 for Element of COMPLEX,
  k,l,m,n,n1,n2 for Element of NAT,
  seq,seq1,seq2,cq1 for Complex_Sequence,
  rseq,rseq1,rseq2 for Real_Sequence,
  rr for set,
  hy1 for convergent_to_0 Real_Sequence;

:: Some definitions and properties of complex sequence

definition
  let m,k be Element of NAT;
  canceled;
  func CHK(m,k) -> Element of COMPLEX equals
  :Def2:
  1 if m <= k otherwise 0;
  correctness
  proof
A1: 0c = 0;
    thus thesis by A1,COMPLEX1:def 7;
  end;
end;

registration
  let m,k be Element of NAT;
  cluster CHK(m,k) -> real;
  coherence by Def2;
end;

scheme ExComplexCASE{F(Element of NAT,Element of NAT)->Element of COMPLEX}:
  for k holds ex seq st for n holds (n <= k implies seq.n=F(k,n)) &
  (n > k implies seq.n=0)
proof
  let k;
  defpred P[set,set] means ex n st (n=$1 & (n <= k implies $2=F(k,n)) &
  (n > k implies $2=0c) );
A1: now
    let x be set;
    assume
A2: x in NAT;
    consider n such that
A3: n=x by A2;
A4: now
      assume
A5:   n <= k;
      thus CHK(n,k) * F (k,n) =1 * F(k,n) by A5,Def2
        .=F(k,n);
    end;
A6: now
      assume
A7:   n > k;
      thus CHK(n,k) * F (k,n) =0 * F(k,n) by A7,Def2
        .=0;
    end;
    reconsider y=CHK(n,k) * F (k,n) as set;
    take y;
    thus P[x,y] by A3,A4,A6;
  end;
  consider f be Function such that
A8: dom f = NAT and
A9: for x be set st x in NAT holds P[x,f.x] from CLASSES1:sch 1(A1);
A10: now
    let x be set;
    assume
A11: x in NAT;
A12: ex n st n=x & (n <= k implies f.x=F(k,n)) &
    (n > k implies f.x=0c) by A9,A11;
    thus f.x is Element of COMPLEX by A12;
  end;
  reconsider f as Complex_Sequence by A8,A10,COMSEQ_1:1;
  take seq=f;
  let n;
A13: ex l be Element of NAT st l=n &
  (l <= k implies seq.n=F(k,l)) & (l > k implies seq.n=0c) by A9;
  thus thesis by A13;
end;

scheme ExRealCASE{F(Element of NAT,Element of NAT)->real number}: for k holds
  ex rseq st for n holds (n <= k implies rseq.n=F(k,n)) &
  (n > k implies rseq.n=0)
proof
  let k;
  defpred P[set,set] means ex n st (n=$1 & (n <= k implies $2=F(k,n )) &
  (n > k implies $2=0) );
A1: now
    let x be set;
    assume
A2: x in NAT;
    consider n such that
A3: n=x by A2;
A4: now
      assume
A5:   n <= k;
      thus CHK(n,k) * F (k,n) =1 * F(k,n) by A5,Def2
        .=F(k,n);
    end;
A6: now
      assume
A7:   n > k;
      thus CHK(n,k) * F (k,n) =0 * F(k,n) by A7,Def2
        .=0;
    end;
    reconsider y=CHK(n,k) * F (k,n) as set;
    take y;
    thus P[x,y] by A3,A4,A6;
  end;
  consider f be Function such that
A8: dom f = NAT and
A9: for x be set st x in NAT holds P[x,f.x] from CLASSES1:sch 1(A1);
A10: now
    let x be set;
    assume
A11: x in NAT;
A12: ex n st n=x & (n <= k implies f.x=F(k,n)) &
    (n > k implies f.x=0) by A9,A11;
    thus f.x is real by A12;
  end;
  reconsider f as Real_Sequence by A8,A10,SEQ_1:3;
  take rseq=f;
  let n;
A13: ex l be Element of NAT st l=n &
  (l <= k implies rseq.n=F(k,l)) & (l > k implies rseq.n=0) by A9;
  thus thesis by A13;
end;

definition
  let x be Element of COMPLEX, c be complex number;
  redefine func x*c -> Element of COMPLEX;
  coherence by XCMPLX_0:def 2;
end;

definition
  canceled;
  func Prod_complex_n -> Complex_Sequence means
  :Def4:
  it.0 = 1 & for n holds it.(n+1) = it.n * (n+1);
  existence
  proof
    deffunc U(Nat,Element of COMPLEX) = $2 * ($1+1);
    consider f being Function of NAT,COMPLEX such that
A1: f.0 = 1r & for n being Nat holds f.(n+1) = U(n,f.n)
    from NAT_1:sch 12;
    take f;
    thus f.0 = 1 by A1;
    let n be Element of NAT;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let seq1,seq2;
    assume that
A2: seq1.0=1 and
A3: for n holds seq1.(n+1)=seq1.n * (n+1) and
A4: seq2.0=1 and
A5: for n holds seq2.(n+1)=seq2.n * (n+1);
    defpred X[Element of NAT] means seq1.$1 = seq2.$1;
A6: X[0] by A2,A4;
A7: for k be Element of NAT st X[k] holds X[k+1]
    proof
      let k be Element of NAT;
      assume
A8:   seq1.k =seq2.k;
      thus seq1.(k+1) = seq2.k * (k+1) by A3,A8
        .= seq2.(k+1) by A5;
    end;
A9: for n holds X[n] from NAT_1:sch 1(A6,A7);
    thus seq1 = seq2 by A9,FUNCT_2:113;
  end;
end;

definition
  func Prod_real_n -> Real_Sequence means
  :Def5:
  it.0 = 1 & for n holds it.(n+1) = it.n * (n+1);
  existence
  proof
    deffunc U(Nat,Element of REAL) = $2 * ($1+1);
    consider f being Function of NAT,REAL such that
A1: f.0 = 1 & for n being Nat holds f.(n+1) = U(n,f.n)
    from NAT_1:sch 12;
    take f;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let rseq1,rseq2;
    assume that
A2: rseq1.0=1 and
A3: for n holds rseq1.(n+1)=rseq1.n * (n+1) and
A4: rseq2.0=1 and
A5: for n holds rseq2.(n+1)=rseq2.n * (n+1);
    defpred X[Element of NAT] means rseq1.$1 = rseq2.$1;
A6: X[0] by A2,A4;
A7: for k be Element of NAT st X[k] holds X[k+1]
    proof
      let k be Element of NAT;
      assume
A8:   rseq1.k =rseq2.k;
      thus rseq1.(k+1) = rseq2.k * (k+1) by A3,A8
        .= rseq2.(k+1) by A5;
    end;
A9: for n holds X[n] from NAT_1:sch 1(A6,A7);
    thus rseq1 = rseq2 by A9,FUNCT_2:113;
  end;
end;

definition
  let n be Element of NAT;
  func n !c -> Element of COMPLEX equals
  Prod_complex_n.n;
  coherence;
end;

definition
  let n be Element of NAT;
  redefine func n! -> Real equals
  Prod_real_n.n;
  coherence;
  compatibility
  proof
    defpred X[Element of NAT] means Prod_real_n.$1 = $1!;
A1: X[0] by Def5,NEWTON:18;
A2: now
      let l be Element of NAT;
      assume
A3:   X[l];
A4:   Prod_real_n.(l+1) = Prod_real_n.l * (l+1) by Def5
        .= (l+1)! by A3,NEWTON:21;
      thus X[l+1] by A4;
    end;
A5: for k be Element of NAT holds X[k] from NAT_1:sch 1(A1,A2);
    thus thesis by A5;
  end;
end;

definition
  let z be complex number;
  deffunc U(Element of NAT) = z #N $1 /($1!c);
  func z ExpSeq -> Complex_Sequence means
  :Def8:
  for n holds it.n = z #N n / (n!c);
  existence
  proof
    thus ex s being Complex_Sequence st
    for n holds s.n = U(n) from COMSEQ_1:sch 1;
  end;
  uniqueness
  proof
    thus for s1,s2 being Complex_Sequence st
    (for x being Element of NAT holds s1.x = U(x)) &
    (for x being Element of NAT holds s2.x = U(x)) holds s1 = s2
    from BINOP_2:sch 1;
  end;
end;

definition
  let a be real number;
  deffunc U(Element of NAT) = a |^ $1 /($1!);
  func a rExpSeq -> Real_Sequence means
  :Def9:
  for n holds it.n = a |^ n /(n!);
  existence
  proof
    thus
    ex s being Real_Sequence st for n holds s.n = U(n) from SEQ_1:sch 1;
  end;
  uniqueness
  proof
    thus for s1,s2 being Real_Sequence st
    (for x being Element of NAT holds s1.x = U(x)) &
    (for x being Element of NAT holds s2.x = U(x)) holds s1 = s2
    from BINOP_2:sch 1;
  end;
end;

theorem Th1:
  0!c = 1 & n!c <> 0 & (n+1)!c = n!c * (n+1)
proof
  defpred X[Element of NAT] means $1!c <> 0;
  thus 0!c = 1 by Def4;
A1: X[0] by Def4;
A2: now
    let n;
    assume
A3: X[n];
A4: (n+1)!c = n!c * (n+1+0*<i>) by Def4;
    thus X[n+1] by A3,A4;
  end;
A5: for n holds X[n] from NAT_1:sch 1(A1,A2);
  thus n!c <> 0 by A5;
  thus thesis by Def4;
end;

canceled;

theorem Th3:
  (0 < k implies ((k-'1)!c ) * k = k!c) &
  (k <= m implies ((m-'k)!c) * (m+1-k) = (m+1-'k)!c)
proof
A1: now
    let k;
    assume
A2: 0 < k;
A3: 0+1 <= k by A2,INT_1:20;
A4: k-'1+1=k-1+1 by A3,XREAL_1:235
      .=k;
    thus k!c =(k-'1)!c * k by A4,Th1;
  end;
A5: now
    let m,k such that
A6: k <= m;
A7: m <= m+1 by XREAL_1:31;
A8: m+1-'k=m+1-k by A6,A7,XREAL_1:235,XXREAL_0:2
      .=m-k+1
      .=m-'k+1 by A6,XREAL_1:235;
    thus (m+1-'k)!c=((m-'k)!c ) * (m-'k+1+0*<i>) by A8,Th1
      .=((m-'k)!c ) * (m-k+1+0*<i>) by A6,XREAL_1:235
      .=((m-'k)!c ) * (m+1-k);
  end;
  thus thesis by A1,A5;
end;

definition
  let n be Element of NAT;
  func Coef(n) -> Complex_Sequence means
  :Def10:
  for k be Element of NAT holds
  (k <= n implies it.k = n!c /( (k!c ) * ((n-'k)!c ))) &
  (k > n implies it.k=0);
  existence
  proof
    deffunc U(Element of NAT,Element of NAT) = $1!c /( ($2!c ) * (($1-'$2)!c));
A1: for n holds ex seq st for k holds (k <= n implies seq.k = U(n,k)) &
    (k > n implies seq.k=0 ) from ExComplexCASE;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let seq1,seq2;
    assume that
A2: for k holds (k <= n implies seq1.k = n!c /( (k!c ) * ((n-'k)!c))) &
    (k > n implies seq1.k=0) and
A3: for k holds (k <= n implies seq2.k = n!c /( (k!c ) * ((n-'k)!c))) &
    (k > n implies seq2.k=0);
A4: now
      let k;
      per cases;
      suppose
A5:     k <= n;
        thus seq1.k = n!c /( (k!c ) * ((n-'k)!c)) by A2,A5
          .= seq2.k by A3,A5;
      end;
      suppose
A6:     k > n;
        thus seq1.k = 0c by A2,A6
          .= seq2.k by A3,A6;
      end;
    end;
    thus seq1=seq2 by A4,FUNCT_2:113;
  end;
end;

definition
  let n be Element of NAT;
  func Coef_e(n) -> Complex_Sequence means
  :Def11:
  for k be Element of NAT holds
  (k <= n implies it.k = 1r/((k!c ) * ((n-'k)!c ))) & (k > n implies it.k=0);
  existence
  proof
    deffunc U(Element of NAT,Element of NAT) = 1r/(($2!c ) * (($1-'$2)!c ));
A1: for n holds ex seq st for k holds (k <= n implies seq.k = U(n,k)) &
    (k > n implies seq.k=0) from ExComplexCASE;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let seq1,seq2;
    assume that
A2: for k holds (k <= n implies seq1.k = 1r/((k!c ) * ((n-'k)!c ))) &
    (k > n implies seq1.k=0) and
A3: for k holds (k <= n implies seq2.k = 1r/((k!c ) * ((n-'k)!c ))) &
    (k > n implies seq2.k=0);
A4: now
      let k;
      per cases;
      suppose
A5:     k <= n;
        thus seq1.k =1r/((k!c ) * ((n-'k)!c )) by A2,A5
          .= seq2.k by A3,A5;
      end;
      suppose
A6:     k > n;
        thus seq1.k = 0c by A2,A6
          .= seq2.k by A3,A6;
      end;
    end;
    thus seq1=seq2 by A4,FUNCT_2:113;
  end;
end;

definition
  let seq;
  func Shift seq -> Complex_Sequence means
  :Def12:
  it.0=0 & for k be Element of NAT holds it.(k+1) = seq.k;
  existence
  proof
    deffunc U(Nat,Element of COMPLEX) = seq.$1;
    consider f being Function of NAT,COMPLEX such that
A1: f.0 = 0c & for n being Nat holds f.(n+1)=U(n,f.n)
    from NAT_1:sch 12;
    take f;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let seq1,seq2;
    assume that
A2: seq1.0=0 and
A3: for n holds seq1.(n+1)=seq.n and
A4: seq2.0=0 and
A5: for n holds seq2.(n+1)=seq.n;
    defpred X[Element of NAT] means seq1.$1 = seq2.$1;
A6: X[0] by A2,A4;
A7: for k be Element of NAT st X[k] holds X[k+1]
    proof
      let k be Element of NAT;
      assume seq1.k =seq2.k;
      thus seq1.(k+1) = seq.k by A3
        .= seq2.(k+1) by A5;
    end;
A8: for n holds X[n] from NAT_1:sch 1(A6,A7);
    thus seq1 = seq2 by A8,FUNCT_2:113;
  end;
end;

definition
  let n;
  let z,w be Element of COMPLEX;
  func Expan(n,z,w) -> Complex_Sequence means
  :Def13:
  for k be Element of NAT holds ( k <= n implies
  it.k=((Coef(n)).k) * (z |^ k) * (w |^ (n-'k)) ) & (n < k implies it.k=0);
  existence
  proof
    deffunc U(Element of NAT,Element of NAT)
    = ((Coef($1)).$2) * (z #N $2) * (w #N ($1-'$2));
A1: for n holds ex seq st for k holds (k <= n implies seq.k = U(n,k)) &
    (k > n implies seq.k=0) from ExComplexCASE;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let seq1,seq2;
    assume that
A2: for k holds
    (k <= n implies seq1.k =((Coef(n)).k) * (z |^ k) * (w |^ (n-'k)))&
    (k > n implies seq1.k=0) and
A3: for k holds
    (k <= n implies seq2.k = ((Coef(n)).k) * (z |^ k) * (w |^ (n-'k)) )&
    (k > n implies seq2.k=0);
A4: now
      let k;
      per cases;
      suppose
A5:     k <= n;
        thus seq1.k =((Coef(n)).k) * (z |^ k) * (w |^ (n-'k)) by A2,A5
          .= seq2.k by A3,A5;
      end;
      suppose
A6:     k > n;
        thus seq1.k = 0c by A2,A6
          .= seq2.k by A3,A6;
      end;
    end;
    thus seq1=seq2 by A4,FUNCT_2:113;
  end;
end;

definition
  let n;
  let z,w be Element of COMPLEX;
  func Expan_e(n,z,w) -> Complex_Sequence means
  :Def14:
  for k be Element of NAT holds ( k <= n implies
  it.k=((Coef_e(n)).k) * (z |^ k) * (w |^ (n-'k)) ) & (n < k implies it.k=0);
  existence
  proof
    deffunc U(Element of NAT,Element of NAT)
    = ((Coef_e($1)).$2) * (z #N $2) * (w #N ($1-'$2));
A1: for n holds ex seq st for k holds (k <= n implies seq.k = U(n,k)) &
    (k > n implies seq.k=0) from ExComplexCASE;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let seq1,seq2;
    assume that
A2: for k holds
    (k <= n implies seq1.k =((Coef_e(n)).k) * (z |^ k) * (w |^ (n-'k)) ) &
    (k > n implies seq1.k=0) and
A3: for k holds
    (k <= n implies seq2.k = ((Coef_e(n)).k) * (z |^ k) * (w |^ (n-'k)) ) &
    (k > n implies seq2.k=0);
A4: now
      let k;
      per cases;
      suppose
A5:     k <= n;
        thus seq1.k =((Coef_e(n)).k) * (z |^ k) * (w |^ (n-'k)) by A2,A5
          .= seq2.k by A3,A5;
      end;
      suppose
A6:     k > n;
        thus seq1.k = 0c by A2,A6
          .= seq2.k by A3,A6;
      end;
    end;
    thus seq1=seq2 by A4,FUNCT_2:113;
  end;
end;

definition
  let n;
  let z,w be Element of COMPLEX;
  func Alfa(n,z,w) -> Complex_Sequence means
  :Def15:
  for k be Element of NAT holds ( k <= n implies
  it.k= (z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) ) &
  ( n < k implies it.k=0);
  existence
  proof
    deffunc U(Element of NAT,Element of NAT)
    = (z ExpSeq).$2 * Partial_Sums(w ExpSeq).($1-'$2);
A1: for n holds ex seq st for k holds (k <= n implies seq.k = U(n,k)) &
    (k > n implies seq.k=0 ) from ExComplexCASE;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let seq1,seq2;
    assume that
A2: for k holds
    (k <= n implies seq1.k = (z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) ) &
    (k > n implies seq1.k=0) and
A3: for k holds
    (k <= n implies seq2.k = (z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) )
    & (k > n implies seq2.k=0);
A4: now
      let k;
      per cases;
      suppose
A5:     k <= n;
        thus seq1.k = (z ExpSeq).k * Partial_Sums(w ExpSeq).(n-'k) by A2,A5
          .= seq2.k by A3,A5;
      end;
      suppose
A6:     k > n;
        thus seq1.k = 0c by A2,A6
          .= seq2.k by A3,A6;
      end;
    end;
    thus seq1=seq2 by A4,FUNCT_2:113;
  end;
end;

definition
  let a,b be real number;
  let n be Element of NAT;
  func Conj(n,a,b) -> Real_Sequence means
  for k be Element of NAT holds
  ( k <= n implies it.k= (a rExpSeq).k * (Partial_Sums(b rExpSeq).n
  -Partial_Sums(b rExpSeq).(n-'k))) & (n < k implies it.k=0);
  existence
  proof
    deffunc U(Element of NAT,Element of NAT)
    = (a rExpSeq).$2 * (Partial_Sums(b rExpSeq).$1
    -Partial_Sums(b rExpSeq).($1-'$2));
    A1: for n holds ex rseq st for k holds (k <= n implies rseq.k = U(n,k) ) &
    (k > n implies rseq.k=0) from ExRealCASE;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let rseq1,rseq2;
    assume that
A2: for k holds
    (k <= n implies rseq1.k =(a rExpSeq).k * (Partial_Sums(b rExpSeq).n
    -Partial_Sums(b rExpSeq).(n-'k)) ) & (k > n implies rseq1.k=0) and
A3: for k holds
    (k <= n implies rseq2.k = (a rExpSeq).k * (Partial_Sums(b rExpSeq).n
    -Partial_Sums(b rExpSeq).(n-'k)) ) & (k > n implies rseq2.k=0);
A4: now
      let k;
      per cases;
      suppose
A5:     k <= n;
        thus rseq1.k =(a rExpSeq).k * (Partial_Sums(b rExpSeq).n
        -Partial_Sums(b rExpSeq).(n-'k)) by A2,A5
          .= rseq2.k by A3,A5;
      end;
      suppose
A6:     k > n;
        thus rseq1.k = 0 by A2,A6
          .= rseq2.k by A3,A6;
      end;
    end;
    thus rseq1=rseq2 by A4,FUNCT_2:113;
  end;
end;

definition
  let z, w be Element of COMPLEX;
  let n be Element of NAT;
  func Conj(n,z,w) -> Complex_Sequence means
  :Def17:
  for k be Element of NAT holds ( k <= n implies
  it.k= (z ExpSeq).k * (Partial_Sums(w ExpSeq).n
  -Partial_Sums(w ExpSeq).(n-'k))) & (n < k implies it.k=0);
  existence
  proof
    deffunc U(Element of NAT,Element of NAT)
    = (z ExpSeq).$2 * (Partial_Sums(w ExpSeq).$1
    -Partial_Sums(w ExpSeq).($1-'$2));
A1: for n holds ex seq st for k holds (k <= n implies seq.k = U(n,k) ) &
    (k > n implies seq.k=0) from ExComplexCASE;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let seq1,seq2;
    assume that
A2: for k holds
    (k <= n implies seq1.k =(z ExpSeq).k * (Partial_Sums(w ExpSeq).n
    -Partial_Sums(w ExpSeq).(n-'k)) ) & (k > n implies seq1.k=0) and
A3: for k holds
    (k <= n implies seq2.k = (z ExpSeq).k * (Partial_Sums(w ExpSeq).n
    -Partial_Sums(w ExpSeq).(n-'k)) )& (k > n implies seq2.k=0);
A4: now
      let k;
      per cases;
      suppose
A5:     k <= n;
        thus seq1.k =(z ExpSeq).k * (Partial_Sums(w ExpSeq).n
        -Partial_Sums(w ExpSeq).(n-'k)) by A2,A5
          .= seq2.k by A3,A5;
      end;
      suppose
A6:     k > n;
        thus seq1.k = 0c by A2,A6
          .= seq2.k by A3,A6;
      end;
    end;
    thus seq1=seq2 by A4,FUNCT_2:113;
  end;
end;

Lm1: for p1,p2,g1,g2 being Element of REAL holds
(p1+g1*<i>)*(p2+g2*<i>) =(p1*p2-g1*g2+(p1*g2+p2*g1)*<i>)
& (p2+g2*<i>)*'=(p2+(-g2)*<i>)
proof
  let p1,p2,g1,g2 be Element of REAL;
  thus (p1+g1*<i>)*(p2+g2*<i>) =(p1*p2-g1*g2+(p1*g2+p2*g1)*<i>);
  thus (p2+g2*<i>)*' = p2-Im(p2+g2*<i>)*<i> by COMPLEX1:28
    .= p2-g2*<i> by COMPLEX1:28
    .=(p2+(-g2)*<i>);
end;

theorem Th4:
  for z being complex number holds
  z ExpSeq.(n+1) = z ExpSeq.n * z /(n+1+0*<i>) & z ExpSeq.0=1 &
  |.(z ExpSeq).n .| = (|.z.| rExpSeq ).n
proof
  let z be complex number;
A1: now
    let n be Element of NAT;
    thus (z ExpSeq).(n+1)=z |^ (n+1) / ((n+1) !c ) by Def8
      .=(z GeoSeq).n * z /((n+1)!c ) by COMSEQ_3:def 1
      .=z |^ n * z /(n!c * (n+1+0*<i>)) by Th1
      .=(z |^ n / (n!c ) )*( z / (n+1+0*<i>)) by XCMPLX_1:77
      .=(z |^ n / (n!c ) * z) / (n+1+0*<i>)
      .=z ExpSeq.n * z /(n+1+0*<i>) by Def8;
  end;
A2: (z ExpSeq).0=z |^ 0 /(0!c ) by Def8
    .=1 by Th1,COMSEQ_3:def 1;
  defpred X[Element of NAT] means |. ((z ExpSeq)).$1 .| =( |.z.| rExpSeq ).$1;
A3: ( |.z.| rExpSeq ).0 = ((|.z .|) |^ 0)/ (0!) by Def9
    .= 1/((Prod_real_n).0) by NEWTON:9
    .= 1/1 by Def5
    .= 1;
A4: X[0] by A2,A3,COMPLEX1:134;
A5: now
    let n such that
A6: X[n];
A7: |.(n+1+0*<i>).|=n+1 by ABSVALUE:def 1;
A8: |. ((z ExpSeq)).(n+1) .| =|. z ExpSeq.n * z /(n+1+0*<i>) .| by A1
      .=|. z ExpSeq.n * z .|/|.(n+1+0*<i>).| by COMPLEX1:153
      .=(( |.z.| rExpSeq ).n) * |.z .|/|.(n+1+0*<i>).| by A6,COMPLEX1:151
      .= (|.z.| |^ n)/(n!) * |.z .|/|.(n+1+0*<i>).| by Def9
      .=( (|.z.| |^ n) * |.z .| )/( (n!) * |.(n+1+0*<i>).| ) by XCMPLX_1:84
      .=( (|.z.| |^ n) * |.z .| )/( (n+1)! ) by A7,NEWTON:21
      .=( (|.z.| |^ (n+1)) )/( (n+1)! ) by NEWTON:11
      .=( |.z.| rExpSeq ).(n+1) by Def9;
    thus X[n+1] by A8;
  end;
A9: for n holds X[n] from NAT_1:sch 1(A4,A5);
  thus thesis by A1,A2,A9;
end;

theorem Th5:
  0 < k implies (Shift(seq)).k=seq.(k-'1)
proof
  assume
A1: 0 < k;
A2: 0+1 <= k by A1,INT_1:20;
  consider m be Nat such that
A3: m+1=k by A1,NAT_1:6;
A4: m=k-1 by A3;
A5: m in NAT by ORDINAL1:def 13;
  thus (Shift seq).k=seq.m by A3,A5,Def12
    .=seq.(k-'1) by A2,A4,XREAL_1:235;
end;

theorem Th6:
  Partial_Sums(seq).k=Partial_Sums(Shift(seq)).k+seq.k
proof
  defpred X[Element of NAT] means
  Partial_Sums(seq).$1=Partial_Sums(Shift(seq)).$1+seq.$1;
A1: Partial_Sums(seq).0=0c + seq.0 by COMSEQ_3:def 7
    .=(Shift(seq)).0 + seq.0 by Def12
    .=Partial_Sums(Shift(seq)).0+seq.0 by COMSEQ_3:def 7;
A2: X[0] by A1;
A3: for k st X[k] holds X[k+1]
  proof
    let k such that
A4: Partial_Sums(seq).k=Partial_Sums(Shift(seq)).k+seq.k;
    thus
    Partial_Sums(seq).(k+1) = (Partial_Sums(Shift(seq)).k+seq.k) + seq.(k+1)
    by A4,COMSEQ_3:def 7
      .=(Partial_Sums(Shift(seq)).k+(Shift(seq)).(k+1)) + seq.(k+1) by Def12
      .=Partial_Sums(Shift(seq)).(k+1)+seq.(k+1) by COMSEQ_3:def 7;
  end;
A5: for k holds X[k] from NAT_1:sch 1(A2,A3);
  thus thesis by A5;
end;

theorem Th7:
  (z+w) |^ n = Partial_Sums(Expan(n,z,w)).n
proof
A1: 0-'0=0 by XREAL_1:234;
  defpred X[Element of NAT] means (z+w) |^ $1 = Partial_Sums(Expan($1,z,w)).$1;
A2: Partial_Sums(Expan(0,z,w)).0 = Expan(0,z,w).0 by COMSEQ_3:def 7
    .= (Coef(0)).0 * (z |^ 0) * (w |^ 0) by A1,Def13
    .= 1/(1 * 1) * z |^ 0 * w |^ 0 by A1,Def10,Th1
    .= 1r * (w GeoSeq).0 by COMSEQ_3:def 1
    .= 1r by COMSEQ_3:def 1;
A3: X[0] by A2,COMSEQ_3:def 1;
A4: for n st X[n] holds X[n+1]
  proof
    let n such that
A5: (z+w) |^ n = Partial_Sums(Expan(n,z,w)).n;
A6: (z+w) |^ (n+1) =((z+w) GeoSeq ).n * (z+w) by COMSEQ_3:def 1
      .=((z+w) (#) Partial_Sums(Expan(n,z,w))).n by A5,VALUED_1:6
      .=(Partial_Sums((z+w) (#) Expan(n,z,w))).n by COMSEQ_3:29;
A7: now
      let k be Element of NAT;
      thus
      ((z+w) (#) Expan(n,z,w)).k=(z+w) * Expan(n,z,w).k by VALUED_1:6
        .=z * Expan(n,z,w).k+w * Expan(n,z,w).k
        .=(z (#) Expan(n,z,w)).k+w * Expan(n,z,w).k by VALUED_1:6
        .=(z (#) Expan(n,z,w)).k+(w (#) Expan(n,z,w)).k by VALUED_1:6
        .=((z (#) Expan(n,z,w))+(w (#) Expan(n,z,w))).k by VALUED_1:1;
    end;
A8: (z+w) (#) Expan(n,z,w)=(z (#) Expan(n,z,w))+(w (#) Expan(n,z,w)) by A7,
    FUNCT_2:113;
    A9: (
z+w) |^ (n+1) =( Partial_Sums(z (#) (Expan(n,z,w))) +Partial_Sums ((w (#)
    (Expan(n,z,w))))).n by A6,A8,COMSEQ_3:27
      .= Partial_Sums((z (#) Expan(n,z,w))).n
    +Partial_Sums((w (#) Expan(n,z,w))).n by VALUED_1:1;
A10: Partial_Sums((z (#) Expan(n,z,w))).(n+1)
    =Partial_Sums((z (#) Expan(n,z,w))).n+(z (#) Expan(n,z,w)).(n+1)
    by COMSEQ_3:def 7
      .=Partial_Sums((z (#) Expan(n,z,w))).n
    +z * Expan(n,z,w).(n+1) by VALUED_1:6;
A11: n < n+1 by XREAL_1:31;
A12: Expan(n,z,w).(n+1)=0c by A11,Def13;
A13: Partial_Sums((w (#) Expan(n,z,w))).(n+1)
    =Partial_Sums((w (#) Expan(n,z,w))).n+(w (#) Expan(n,z,w)).(n+1)
    by COMSEQ_3:def 7
      .=Partial_Sums((w (#) Expan(n,z,w))).n
    +w * Expan(n,z,w).(n+1) by VALUED_1:6;
A14: Partial_Sums((z (#) Expan(n,z,w))).(n+1)
    =Partial_Sums(Shift((z (#) Expan(n,z,w)))).(n+1)
    +(z (#) Expan(n,z,w)).(n+1) by Th6;
A15: 0 +n < n+1 by XREAL_1:31;
A16: Expan(n,z,w).(n+1)=0c by A15,Def13;
A17: z * Expan(n,z,w).(n+1)=0c by A16;
A18: Partial_Sums((z (#) Expan(n,z,w))).(n+1)
    =Partial_Sums(Shift((z (#) Expan(n,z,w)))).(n+1) +0c by A14,A17,
    VALUED_1:6
      .= Partial_Sums(Shift((z (#) Expan(n,z,w)))).(n+1);
A19: now
      let k be Element of NAT;
A20:  now
        assume
A21:    n+1 < k;
A22:    0+1 <= n+1 by XREAL_1:8;
A23:    n+1-1 < k -1 by A21,XREAL_1:11;
A24:    n+0 < k-1+1 by A23,XREAL_1:10;
        A25:    k-1 = k-'1 by A21,A22,XREAL_1:235,XXREAL_0:2;
A26:    ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k
        =(w (#) Expan(n,z,w)).k + (Shift((z (#) Expan(n,z,w)))).k
        by VALUED_1:1
          .=(w * Expan(n,z,w).k) + (Shift((z (#) Expan(n,z,w)))).k
        by VALUED_1:6
          .=(w * Expan(n,z,w).k) + ((z (#) Expan(n,z,w))).(k-'1) by A24,Th5
          .=(w * Expan(n,z,w).k) + (z * (Expan(n,z,w).(k-'1))) by VALUED_1:6
          .=(w * 0c )+ (z * ((Expan(n,z,w).(k-'1)))) by A24,Def13
          .=0c + (z * 0c) by A23,A25,Def13
          .=0c;
        thus ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k
        =Expan(n+1,z,w).k by A21,A26,Def13;
      end;
A27:  now
        assume
A28:    k <= (n+1);
A29:    now
          assume
A30:      k=n+1;
A31:      n < n+1 by XREAL_1:31;
A32:      n-'n= n-n by XREAL_1:235
            .= 0;
A33:      n+1-'(n+1)= n+1-(n+1) by XREAL_1:235
            .= 0;
A34:      (Coef(n)).(n) = n!c /((n!c ) * (0!c )) by A32,Def10
            .= 1 by Th1,XCMPLX_1:60;
A35:      (Coef(n+1)).(n+1) = (n+1)!c /(((n+1)!c ) * (0!c )) by A33,Def10
            .= 1 by Th1,XCMPLX_1:60;
A36:      ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k =
          (w (#) Expan(n,z,w)).(n+1)+(Shift((z (#) Expan(n,z,w)))).(n+1)
          by A30,VALUED_1:1
.=(w * Expan(n,z,w).(n+1)) + (Shift((z (#) Expan(n,z,w)))).(n+1) by VALUED_1:6
            .=( w * 0c) + (Shift((z (#) Expan(n,z,w)))).(n+1) by A31,Def13
            .=((z (#) Expan(n,z,w))).n by Def12
            .=z * (Expan(n,z,w)).n by VALUED_1:6
            .=z * ((Coef(n)).n * (z |^ n) * (w |^ (n-'n))) by Def13
            .= ((Coef(n)).n * ((z GeoSeq).n * z) * (w |^ (n-'n)))
            .= ((Coef(n+1)).(n+1) * (z |^ (n+1)) * (w |^ (n-'n)))
          by A34,A35,COMSEQ_3:def 1
            .= Expan(n+1,z,w).k by A30,A32,A33,Def13;
thus ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k=Expan(n+1,z,w) .
          k by A36;
        end;
A37:    now
          assume
A38:      k < n+1;
A39:      now
            assume
A40:        k=0;
A41:        n-'0 =n-0 by XREAL_1:235;
A42:        n+1-'0 =n+1-0 by XREAL_1:235;
A43:        (Coef(n)).0 = n!c /((0!c ) * (n!c )) by A41,Def10
              .= 1 by Th1,XCMPLX_1:60;
A44:        (Coef(n+1)).0 = (n+1)!c /((0!c ) * ((n+1)!c )) by A42,Def10
              .= 1 by Th1,XCMPLX_1:60;
            A45:        ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k =
            (w (#) Expan(n,z,w)).0+(Shift((z (#) Expan(n,z,w)))).0
            by A40,VALUED_1:1
              .=(w * Expan(n,z,w).0) + (Shift((z (#) Expan(n,z,w)))).0
            by VALUED_1:6
              .=(w * Expan(n,z,w).0) + 0c by Def12
              .=w * ((Coef n).0 * (z |^ 0) * (w |^ (n-'0))) by Def13
              .= (Coef n).0 * (z |^ 0) * (((w GeoSeq)).n * w) by A41
              .= (Coef(n+1)).0 * (z |^ 0) * (w |^ (n+1-'0))
            by A42,A43,A44,COMSEQ_3:def 1
              .= Expan(n+1,z,w).k by A40,Def13;
thus ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k =Expan(n+1,z,w).
            k by A45;
          end;
A46:      now
            assume
A47:        k<>0;
A48:        0+1 <= k by A47,INT_1:20;
A49:        k+1-1 <= n+1-1 by A38,INT_1:20;
A50:        k < k+1 by XREAL_1:31;
A51:        k-1 <= k+1-1 by A50,XREAL_1:11;
A52:        k-1 <= n by A49,A51,XXREAL_0:2;
A53:        k-'1 <= n by A48,A52,XREAL_1:235;
A54:        ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k
            =(w (#) Expan(n,z,w)).k+(Shift((z (#) Expan(n,z,w)))).k
            by VALUED_1:1
              .=(w * Expan(n,z,w).k) + (Shift((z (#) Expan(n,z,w)))).k
            by VALUED_1:6
              .=(w * Expan(n,z,w).k) + (z (#) Expan(n,z,w)).(k-'1)
            by A47,Th5
              .=(w * Expan(n,z,w).k) + (z * Expan(n,z,w).(k-'1))
            by VALUED_1:6
              .=(w * ((Coef n).k * (z |^ k) * (w |^ (n-'k))))
            + (z * Expan(n,z,w).(k-'1)) by A49,Def13
              .=(w * ((Coef n).k * (z |^ k)) * (w |^ (n-'k)))
            + (z * ((Coef n).(k-'1) * (z |^ (k-'1))
            * (w |^ (n-'(k-'1))))) by A53,Def13
              .=((Coef n).k * (w * (z |^ k) * (w |^ (n-'k)) )
            + ((Coef n).(k-'1) * ((z |^ (k-'1))
            * z * (w |^ (n-'(k-'1))))));
A55:        (n-'k)+1=n-k+1 by A49,XREAL_1:235
              .=n+1-k
              .=n+1-'k by A38,XREAL_1:235;
A56:        n-'(k-'1)=n-(k-'1) by A53,XREAL_1:235
              .=n-(k-1) by A48,XREAL_1:235
              .=n+1-k
              .=n+1-'k by A38,XREAL_1:235;
A57:        (k-'1)+1 =k-1+1 by A48,XREAL_1:235
              .=k;
            A58:        (
w |^ (n-'k)) * w = w |^ (n-'k+1) & (z |^ (k-'1)) * z = z |^ k by A57,
            COMSEQ_3:def 1;
A59:        (Coef n).k +(Coef n).(k-'1) =n!c /((k!c) * ((n-'k)!c))
            +(Coef n).(k-'1) by A49,Def10
              .=n!c /((k!c) * ((n-'k)!c))
            +n!c /(((k-'1)!c) * (((n-'(k-'1)))!c )) by A53,Def10;
A60:        ((k!c) * ((n-'k)!c)) * (n+1-k+0*<i>)
            =(k!c) * (((n-'k)!c) * (n+1-k+0*<i>));
A61:        (((k-'1)!c) * (((n-'(k-'1)))!c )) * (k+0*<i>)
            =(k+0*<i>) * ((k-'1)!c) * (((n-'(k-'1)))!c )
              .=(k!c) * ((n+1-'k)!c) by A47,A56,Th3;
A62:        (n+1-k+0*<i>) <>0c by A38;
            A63:        n !c /((k!c) * ((n-'k)!c)) =(n!c * (n+1-k+0*<i>))
            /(((k!c) * ((n-'k)!c)) * (n+1-k+0*<i>)) by A62,XCMPLX_1:92
              .=(n!c * (n+1-k+0*<i>))/((k!c) * ((n+1-'k)!c))
            by A49,A60,Th3;
A64:        n!c /(((k-'1)!c) * (((n-'(k-'1)))!c ))
            =(n!c * (k+0*<i>))/((k!c) * ((n+1-'k)!c ))
            by A47,A61,XCMPLX_1:92;
A65:        (Coef n).k +(Coef n).(k-'1)
            =( n!c * (n+1-k+k+(0+0)*<i>)) / ((k!c) * ((n+1-'k)!c))
            by A59,A63,A64
              .=((n+1)!c ) / ((k!c) * ((n+1-'k)!c)) by Th1
              .=(Coef(n+1)).k by A38,Def10;
A66:        ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k
            =(Coef(n+1)).k * (z |^ k) * (w |^ (n+1-'k)) by A54,A55,A56,A58
            ,A65
              .=Expan(n+1,z,w).k by A38,Def13;
thus ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k =Expan(n+1,z,w).
            k by A66;
          end;
          thus ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k
          =Expan(n+1,z,w).k by A39,A46;
        end;
        thus ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k
        =Expan(n+1,z,w).k by A28,A29,A37,XXREAL_0:1;
      end;
      thus ((w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w)))).k
      =Expan(n+1,z,w).k by A20,A27;
    end;
A67: (w (#) Expan(n,z,w))+Shift((z (#) Expan(n,z,w))) =Expan(n+1,z,w) by A19,
    FUNCT_2:113;
    thus (z+w) |^ (n+1) =(Partial_Sums((w (#) Expan(n,z,w))) +
    Partial_Sums(Shift((z (#) Expan(n,z,w))))).(n+1)
    by A9,A10,A12,A13,A18,VALUED_1:1
      .=Partial_Sums(Expan(n+1,z,w)).(n+1) by A67,COMSEQ_3:27;
  end;
A68: for n holds X[n] from NAT_1:sch 1(A3,A4);
  thus thesis by A68;
end;

theorem Th8:
  Expan_e(n,z,w)=(1r/(n!c )) (#) Expan(n,z,w)
proof
A1: now
    let k be Element of NAT;
A2: now
      assume
A3:   n <k;
      thus Expan_e(n,z,w).k =(1r/(n!c )) * 0c by A3,Def14
        .=(1r/(n!c )) * Expan(n,z,w).k by A3,Def13
        .=((1r/(n!c )) (#) Expan(n,z,w)).k by VALUED_1:6;
    end;
A4: now
      assume
A5:   k <= n;
A6:   Expan_e(n,z,w).k = (Coef_e(n)).k * (z |^ k) * (w |^ (n-'k)) by A5,Def14
        .= 1r/((k!c ) * ((n-'k)!c )) * (z |^ k) * (w |^ (n-'k)) by A5,Def11;
A7:   1r/((k!c ) * ((n-'k)!c ))
      =(((n!c ) * 1r )/(n!c )) /((k!c ) * ((n-'k)!c))
      by Th1,XCMPLX_1:60
        .= (1r /(n!c )) * (n!c ) /((k!c ) * ((n-'k)!c ));
      thus
      Expan_e(n,z,w).k = (1r/(n!c )) * (n!c ) /((k!c ) * ((n-'k)!c ))
      * ((z |^ k) * (w |^ (n-'k))) by A6,A7
        .= (1r/(n!c )) * ((n!c ) /((k!c ) * ((n-'k)!c ))
      * (z |^ k) * (w |^ (n-'k)))
        .= (1r/(n!c )) * ((Coef(n)).k * ((z |^ k))
      * (w |^ (n-'k))) by A5,Def10
        .= (1r/(n!c )) * Expan(n,z,w).k by A5,Def13
        .= ( (1r/(n!c )) (#) Expan(n,z,w) ).k by VALUED_1:6;
    end;
    thus Expan_e(n,z,w).k = ( (1r/(n!c )) (#) Expan(n,z,w) ).k by A2,A4;
  end;
  thus thesis by A1,FUNCT_2:113;
end;

theorem Th9:
  ((z+w) |^ n) / (n!c) = Partial_Sums(Expan_e(n,z,w)).n
proof
  thus ((z+w) |^ n)/(n!c )
  = (Partial_Sums(Expan(n,z,w)).n) * (1r/(n!c )) by Th7
    .= ((1r/(n!c )) (#) (Partial_Sums(Expan(n,z,w)))).n by VALUED_1:6
    .= Partial_Sums( (1r/(n!c )) (#) Expan(n,z,w)).n by COMSEQ_3:29
    .= Partial_Sums(Expan_e(n,z,w)).n by Th8;
end;

theorem Th10:
  (0c qua complex number) ExpSeq is absolutely_summable
  & Sum((0c qua complex number) ExpSeq)=1r
proof
  defpred X[set] means Partial_Sums(|.(0c qua complex number) ExpSeq.|).$1=1;
A1: Partial_Sums(|.((0c qua complex number) ExpSeq).|).0
  =|.((0c qua complex number) ExpSeq).|.0 by SERIES_1:def 1
    .=|. ((0c qua complex number) ExpSeq).0 .| by VALUED_1:18
    .=1 by Th4,COMPLEX1:134;
A2: X[0] by A1;
A3: for n being Nat st X[n] holds X[n+1]
  proof
    let n being Nat such that
A4: Partial_Sums(|.(0c qua complex number) ExpSeq.|).n=1;
A5: n in NAT by ORDINAL1:def 13;
    thus Partial_Sums(|.(0c qua complex number) ExpSeq.|).(n+1)
    =1 + |.(0c qua complex number) ExpSeq.|.(n+1) by A4,A5,SERIES_1:def 1
      .=1 + |.((0c qua complex number) ExpSeq).(n+1).| by VALUED_1:18
      .=1 + |.((0c qua complex number) ExpSeq.n) * 0c/(n+1+0*<i>) .| by A5,Th4
      .=1 by COMPLEX1:130;
  end;
A6: for n being Nat holds X[n] from NAT_1:sch 2(A2,A3);
A7: Partial_Sums(|.(0c qua complex number) ExpSeq.|)
  is constant by A6,VALUED_0:def 18;
A8: |.(0c qua complex number) ExpSeq.| is summable by A7,SERIES_1:def 2;
  defpred X[set] means Partial_Sums((0c qua complex number) ExpSeq).$1=1;
A9: Partial_Sums((0c qua complex number) ExpSeq).0
  =((0c qua complex number) ExpSeq).0 by COMSEQ_3:def 7
    .=1 by Th4;
A10: X[0] by A9;
A11: for n st X[n] holds X[n+1]
  proof
    let n such that
A12: Partial_Sums((0c qua complex number) ExpSeq).n=1;
    thus Partial_Sums((0c qua complex number) ExpSeq).(n+1)
    =1r + ((0c qua complex number) ExpSeq).(n+1) by A12,COMSEQ_3:def 7
      .=1r + ((0c qua complex number) ExpSeq.n) * 0c/(n+1+0*<i>) by Th4
      .=1;
  end;
A13: for n holds X[n] from NAT_1:sch 1(A10,A11);
  thus thesis by A8,A13,COMSEQ_2:10,COMSEQ_3:def 11;
end;

registration
  let z be complex number;
  cluster z ExpSeq -> absolutely_summable;
  coherence
  proof
A1: now
      assume
A2:   z<>0c;
      defpred X[set] means (z ExpSeq).$1 <> 0c;
A3:   X[0] by Th4;
A4:   for n st X[n] holds X[n+1]
      proof
        let n;
        assume
A5:     (z ExpSeq).n <> 0c;
        thus (z ExpSeq).(n+1) <> 0c
        proof
          assume
A6:       (z ExpSeq).(n+1) = 0c;
A7:       ((z ExpSeq).n * z)/(n+1+0*<i>)=0c by A6,Th4;
A8:       0c = 0c/z
            .=(z ExpSeq).n * z/z by A7,XCMPLX_1:50
            .=(z ExpSeq).n * (z/z)
            .=(z ExpSeq).n * 1 by A2,XCMPLX_1:60
            .=(z ExpSeq).n;
          thus contradiction by A5,A8;
        end;
      end;
      deffunc U(Element of NAT) = |.(z ExpSeq).|.($1+1)/|.(z ExpSeq).|.$1;
      thus
A9:   for n holds X[n] from NAT_1:sch 1(A3,A4);
A10:  ex rseq st for n holds rseq.n= U(n) from SEQ_1:sch 1;
      consider rseq such that
A11:  for n holds rseq.n=|.z ExpSeq.|.(n+1)/|.z ExpSeq.|.n by A10;
A12:  now
        let n;
        thus rseq.n =|.z ExpSeq.|.(n+1)/|.z ExpSeq.|.n by A11
          .=|.(z ExpSeq).(n+1).|/|.z ExpSeq.|.n by VALUED_1:18
          .=|.(z ExpSeq).(n+1).|/|.(z ExpSeq).n.| by VALUED_1:18
          .=|.(z ExpSeq).(n+1)/(z ExpSeq).n.| by COMPLEX1:153
          .=|.((z ExpSeq).n * z/(n+1+0*<i>)) /(z ExpSeq).n.| by Th4
          .=|.((z ExpSeq).n * (z/(n+1+0*<i>))) /(z ExpSeq).n.|
          .=|.(z/(n+1+0*<i>)) .| by A9,XCMPLX_1:90
          .=|.z.|/abs(n+1) by COMPLEX1:153
          .=|.z.|/(n+1) by ABSVALUE:def 1;
      end;
A13:  rseq is convergent & lim rseq < 1 by A12,SEQ_4:46;
      thus thesis by A9,A11,A13,COMSEQ_3:76;
    end;
    thus thesis by A1,Th10;
  end;
end;

theorem Th11:
  (z ExpSeq).0 = 1 & Expan(0,z,w).0 = 1
proof
  thus (z ExpSeq).0 = (z |^ 0)/(0!c ) by Def8
    .= 1 by Th1,COMSEQ_3:def 1;
A1: 0-'0=0 by XREAL_1:234;
  thus Expan(0,z,w).0 = (Coef(0)).0 * (z |^ 0) * (w |^ 0) by A1,Def13
    .= 1/(1 * 1) * z |^ 0 * w |^ 0 by A1,Def10,Th1
    .= 1r * (w GeoSeq).0 by COMSEQ_3:def 1
    .= 1 by COMSEQ_3:def 1;
end;

theorem Th12:
  l <= k implies (Alfa(k+1,z,w)).l = (Alfa(k,z,w)).l + Expan_e(k+1,z,w).l
proof
  assume
A1: l <= k;
A2: k < k+1 by XREAL_1:31;
A3: l <= k+1 by A1,A2,XXREAL_0:2;
A4: (k+1-'l)=k+1-l by A1,A2,XREAL_1:235,XXREAL_0:2;
A5: (k+1-'l)=k-l+1 by A4
    .=(k-'l)+1 by A1,XREAL_1:235;
A6: (Alfa(k+1,z,w)).l
  =(z ExpSeq).l * (Partial_Sums(w ExpSeq).((k-'l)+1)) by A3,A5,Def15
    .=(z ExpSeq).l * (Partial_Sums(w ExpSeq).((k-'l))
  +(w ExpSeq).((k+1-'l))) by A5,COMSEQ_3:def 7
    .=((z ExpSeq).l * (Partial_Sums(w ExpSeq).((k-'l)))
  +((z ExpSeq).l * (w ExpSeq).((k+1-'l))))
    .=(Alfa(k,z,w)).l+((z ExpSeq).l * (w ExpSeq).((k+1-'l))) by A1,Def15;
A7: (z ExpSeq).l * (w ExpSeq).((k+1-'l))
  =(z |^ l/(l!c )) * (w ExpSeq).((k+1-'l)) by Def8
    .=(z |^ l/(l!c )) * (w |^ ((k+1-'l))/(((k+1-'l))!c )) by Def8
    .=(((z |^ l) * (w |^ ((k+1-'l))) * 1r)/((l!c ) * (((k+1-'l))!c )))
  by XCMPLX_1:77
    .=((z |^ l) * (w |^ ((k+1-'l))) * ((1r/((l!c ) * (((k+1-'l))!c )))))
    .= ((Coef_e(k+1)).l) * ((z |^ l) * (w |^ ((k+1-'l)))) by A3,Def11
    .= ((Coef_e(k+1)).l) * (z |^ l) * (w |^ ((k+1-'l)))
    .=Expan_e(k+1,z,w).l by A3,Def14;
  thus thesis by A6,A7;
end;

theorem Th13:
  Partial_Sums((Alfa(k+1,z,w))).k = (Partial_Sums(( Alfa(k,z,w)))).k
  + (Partial_Sums(( Expan_e(k+1,z,w) ))).k
proof
A1: now
    let l be Element of NAT;
    assume
A2: l <= k;
    thus (Alfa(k+1,z,w)).l = (Alfa(k,z,w)).l + Expan_e(k+1,z,w).l by A2,Th12
      .= ( (Alfa(k,z,w)) + Expan_e(k+1,z,w)).l by VALUED_1:1;
  end;
  thus Partial_Sums((Alfa(k+1,z,w))).k
  =Partial_Sums(((Alfa(k,z,w)) + Expan_e(k+1,z,w))).k by A1,COMSEQ_3:35
    .=(Partial_Sums(( Alfa(k,z,w)))
  +Partial_Sums(( Expan_e(k+1,z,w )))).k by COMSEQ_3:27
    .= (Partial_Sums(( Alfa(k,z,w)))).k
  + (Partial_Sums(( Expan_e(k+1,z,w) ))).k by VALUED_1:1;
end;

theorem Th14:
  (z ExpSeq).k=(Expan_e(k,z,w)).k
proof
A1: 0 = k -k;
A2: k-'k=0 by A1,XREAL_1:235;
A3: (k-'k)!c =1 by A1,Th1,XREAL_1:235;
  thus (Expan_e(k,z,w)).(k)=((Coef_e(k)).k) * (z |^ k) * (w |^ 0) by A2,Def14
    .=( (Coef_e(k)).k) * (z |^ k) * 1r by COMSEQ_3:11
    .=(1r/((k!c ) * 1r)) * (z |^ k) by A3,Def11
    .=((z |^ k) * 1r)/(k!c )
    .=(z ExpSeq).k by Def8;
end;

theorem Th15:
  Partial_Sums((z+w) ExpSeq).n = Partial_Sums(Alfa(n,z,w)).n
proof
A1: Partial_Sums((z+w) ExpSeq).0 =((z+w) ExpSeq).0 by COMSEQ_3:def 7
    .=1 by Th11;
  defpred X[Element of NAT] means
  Partial_Sums((z+w) ExpSeq).$1=Partial_Sums(Alfa($1,z,w)).$1;
A2: 0-'0=0 by XREAL_1:234;
A3: Partial_Sums(Alfa(0,z,w)).0 = Alfa(0,z,w).0 by COMSEQ_3:def 7
    .= (z ExpSeq).0 * Partial_Sums(w ExpSeq).0 by A2,Def15
    .= (z ExpSeq).0 * (w ExpSeq).0 by COMSEQ_3:def 7
    .=1r * (w ExpSeq).0 by Th11
    .= 1 by Th11;
A4: X[0] by A1,A3;
A5: for k st X[k] holds X[k+1]
  proof
    let k such that
A6: Partial_Sums((z+w) ExpSeq).k=Partial_Sums(Alfa(k,z,w)).k;
A7: Partial_Sums((Alfa(k+1,z,w))).(k+1)
    =Partial_Sums((Alfa(k+1,z,w))).k+(Alfa(k+1,z,w)).(k+1) by COMSEQ_3:def 7
      .=((Partial_Sums(( Alfa(k,z,w) ))).k
    + (Partial_Sums(( Expan_e(k+1,z,w) ))).k )
    + (Alfa(k+1,z,w)).(k+1) by Th13
      .= Partial_Sums((z+w) ExpSeq).k
    + ((Partial_Sums(( Expan_e(k+1,z,w) ))).k
    + (Alfa(k+1,z,w)).(k+1)) by A6;
A8: k+1-'(k+1)=0 by XREAL_1:234;
A9: (Alfa(k+1,z,w)).(k+1)
    =(z ExpSeq).(k+1) * Partial_Sums(w ExpSeq).0 by A8,Def15
      .=(z ExpSeq).(k+1) * ((w ExpSeq).0) by COMSEQ_3:def 7
      .=(z ExpSeq).(k+1) * 1 by Th11
      .=(Expan_e(k+1,z,w)).(k+1) by Th14;
A10: (Partial_Sums(( Expan_e(k+1,z,w) ))).k + (Alfa(k+1,z,w)).(k+1)
    =(Partial_Sums(( Expan_e(k+1,z,w) ))).(k+1) by A9,COMSEQ_3:def 7
      .=(z+w) |^ (k+1) /((k+1)!c ) by Th9;
    A11: Partial_Sums((Alfa(k+1,z,w))).(k+1) =Partial_Sums((z+w) ExpSeq).k
    +(z+w) ExpSeq.(k+1) by A7,A10,Def8
      .=Partial_Sums((z+w) ExpSeq).(k+1) by COMSEQ_3:def 7;
    thus Partial_Sums((z+w) ExpSeq).(k+1)=Partial_Sums(Alfa(k+1,z,w)).(k+1)
    by A11;
  end;
A12: for n holds X[n] from NAT_1:sch 1(A4,A5);
  thus thesis by A12;
end;

theorem Th16:
  Partial_Sums(z ExpSeq).k * Partial_Sums(w ExpSeq).k
  -Partial_Sums((z+w) ExpSeq).k = Partial_Sums(Conj(k,z,w)).k
proof
A1: (Partial_Sums(z ExpSeq).k) * (Partial_Sums(w ExpSeq).k)
  -Partial_Sums((z+w) ExpSeq).k
  =Partial_Sums(z ExpSeq).k * Partial_Sums(w ExpSeq).k
  -Partial_Sums(Alfa(k,z,w)).k by Th15
    .= ((Partial_Sums(w ExpSeq).k) (#) Partial_Sums(z ExpSeq)).k
  -Partial_Sums(Alfa(k,z,w)).k by VALUED_1:6
    .=Partial_Sums((Partial_Sums(w ExpSeq).k) (#) (z ExpSeq)).k
  -Partial_Sums(Alfa(k,z,w)).k by COMSEQ_3:29
    .=Partial_Sums((Partial_Sums(w ExpSeq).k) (#) (z ExpSeq)).k
  +(-Partial_Sums(Alfa(k,z,w))).k by VALUED_1:8
    .=(Partial_Sums((Partial_Sums(w ExpSeq).k) (#) (z ExpSeq))
  -Partial_Sums(Alfa(k,z,w))).k by VALUED_1:1
    .=Partial_Sums((((Partial_Sums(w ExpSeq).k) (#) (z ExpSeq)))
  -(Alfa(k,z,w))).k by COMSEQ_3:28;
A2: for l be Element of NAT st l <= k holds
  ((Partial_Sums(w ExpSeq).k) (#) (z ExpSeq) - (Alfa(k,z,w))).l =Conj(k,z,w).l
  proof
    let l be Element of NAT such that
A3: l <= k;
    thus ( ( Partial_Sums(w ExpSeq).k) (#) (z ExpSeq) - (Alfa(k,z,w)) ).l
    =(( Partial_Sums(w ExpSeq).k) (#) (z ExpSeq)).l
    +(-Alfa(k,z,w)).l by VALUED_1:1
      .=(( Partial_Sums(w ExpSeq).k) (#) (z ExpSeq)).l -Alfa(k,z,w).l
    by VALUED_1:8
      .=(( Partial_Sums(w ExpSeq).k ) * ( (z ExpSeq).l ))
    -Alfa(k,z,w).l by VALUED_1:6
      .=((z ExpSeq).l) * ( Partial_Sums(w ExpSeq).k)
    -((z ExpSeq).l) * ( Partial_Sums(w ExpSeq).(k-'l)) by A3,Def15
      .=((z ExpSeq).l) * ( Partial_Sums(w ExpSeq).k
    -Partial_Sums(w ExpSeq).(k-'l) )
      .=Conj(k,z,w).l by A3,Def17;
  end;
  thus thesis by A1,A2,COMSEQ_3:35;
end;

theorem Th17:
  |. Partial_Sums((z ExpSeq)).k .| <= Partial_Sums(|.z.| rExpSeq).k &
  Partial_Sums((|.z.| rExpSeq)).k <= Sum(|.z.| rExpSeq) &
  |. Partial_Sums(( z ExpSeq)).k .| <= Sum(|.z.| rExpSeq)
proof
  defpred X[Element of NAT] means |. Partial_Sums(( z ExpSeq)).$1 .|
  <= Partial_Sums((|.z.| rExpSeq)).$1;
  A1: |.
 Partial_Sums(( z ExpSeq)).0 .| = |. ((z ExpSeq)).0 .| by COMSEQ_3:def 7
    .= |. (z |^ 0)/ (0 !c).| by Def8
    .= 1 by Th1,COMPLEX1:134,COMSEQ_3:def 1;
A2: Partial_Sums((|.z.| rExpSeq)).0 = ((|.z.| rExpSeq)).0 by SERIES_1:def 1
    .= ((|.z .|) |^ 0)/ (0!) by Def9
    .= 1 by NEWTON:9,18;
A3: X[0] by A1,A2;
A4: for k st X[k] holds X[k+1]
  proof
    let k such that
A5: |. Partial_Sums(( z ExpSeq)).k .| <= Partial_Sums((|.z.| rExpSeq)).k;
    A6: |.
 Partial_Sums(( z ExpSeq)).(k+1) .| =|. Partial_Sums(( z ExpSeq)).k + ((z
ExpSeq)).(k+1) .| & |. Partial_Sums(( z ExpSeq)).k + ((z ExpSeq)).(k+1) .| <=
    |. Partial_Sums(( z ExpSeq)).k.| + |. ((z ExpSeq)).(k+1) .| by COMPLEX1:142
    ,COMSEQ_3:def 7;
A7: |. Partial_Sums(( z ExpSeq)).(k+1) .|
    <= |. Partial_Sums(( z ExpSeq)).k.| + (|.z.| rExpSeq).(k+1) by A6,Th4;
A8: |. Partial_Sums(( z ExpSeq)).k.| + (|.z.| rExpSeq).(k+1)
    <= Partial_Sums((|.z.| rExpSeq)).k + (|.z.| rExpSeq).(k+1) by A5,XREAL_1:8;
A9: Partial_Sums((|.z.| rExpSeq)).k + (|.z.| rExpSeq).(k+1)
    =Partial_Sums((|.z.| rExpSeq)).(k+1) by SERIES_1:def 1;
    thus thesis by A7,A8,A9,XXREAL_0:2;
  end;
A10: for k holds X[k] from NAT_1:sch 1(A3,A4);
  thus
  |. Partial_Sums(( z ExpSeq)).k .| <= Partial_Sums((|.z.| rExpSeq)).k by A10;
A11: now
    let k be set such that
A12: k in NAT;
    thus |. (z ExpSeq).|.k = |. (z ExpSeq).k .| by VALUED_1:18
      .= (|.z.| rExpSeq).k by A12,Th4;
  end;
A13: |. z ExpSeq .| = |.z.| rExpSeq by A11,FUNCT_2:18;
A14: Partial_Sums((|.z.| rExpSeq)).k <= lim(Partial_Sums((|.z.| rExpSeq )))
  by A13,SEQ_4:54;
  thus Partial_Sums((|.z.| rExpSeq)).k <= Sum(|.z.| rExpSeq)
  by A14,SERIES_1:def 3;
A15: now
    let k;
    A16: lim
(Partial_Sums((|.z.| rExpSeq)))=Sum(|.z.| rExpSeq) by SERIES_1:def 3;
    thus Partial_Sums((|.z.| rExpSeq)).k <= Sum(|.z.| rExpSeq)
    by A13,A16,SEQ_4:54;
  end;
A17: |. Partial_Sums(( z ExpSeq)).k .|
  <= Partial_Sums((|.z.| rExpSeq)).k by A10;
A18: Partial_Sums((|.z.| rExpSeq)).k <= Sum(|.z.| rExpSeq) by A15;
  thus thesis
  by A17,A18,XXREAL_0:2;
end;

theorem Th18:
  1 <= Sum(|.z.| rExpSeq)
proof
A1: |. Partial_Sums(z ExpSeq).0 .|=|. (z ExpSeq).0 .| by COMSEQ_3:def 7
    .=1 by Th4,COMPLEX1:134;
  thus thesis by A1,Th17;
end;

theorem Th19:
  0 <= (|. z .| rExpSeq).n
proof
A1: (|. z .| rExpSeq).n = |.((z ExpSeq)).n .| by Th4;
  thus thesis by A1,COMPLEX1:132;
end;

theorem Th20:
  abs((Partial_Sums(|.z .| rExpSeq)).n) = Partial_Sums(|.z .| rExpSeq).n &
  (n <= m implies
  abs((Partial_Sums(|.z .| rExpSeq).m-Partial_Sums(|.z .| rExpSeq).n))
  = Partial_Sums(|.z .| rExpSeq).m-Partial_Sums(|.z .| rExpSeq).n)
proof
A1: for n holds 0 <= (|. z .| rExpSeq).n by Th19;
  thus thesis by A1,COMSEQ_3:9;
end;

theorem Th21:
  abs(Partial_Sums(|.Conj(k,z,w).|).n)=Partial_Sums(|.Conj(k,z,w).|).n
proof
A1: now
    let n;
A2: |.Conj(k,z,w).|.n=|.(Conj(k,z,w)).n.| by VALUED_1:18;
    thus 0 <= |.Conj(k,z,w).|.n by A2,COMPLEX1:132;
  end;
  A3: Partial_Sums(|.Conj(k,z,w).|).0 <= Partial_Sums(|.Conj(k,z,w).|).n &
  Partial_Sums(|.Conj(k,z,w).|).0=(|.Conj(k,z,w).|).0 by SEQM_3:12
  ,SERIES_1:def 1;
A4: 0 <= (|.Conj(k,z,w).|).0 by A1;
  thus thesis by A3,A4,ABSVALUE:def 1;
end;

theorem Th22:
  for p being real number st p>0 ex n st for k st n <= k
  holds abs(Partial_Sums(|.Conj(k,z,w).|).k) < p
proof
  let p be real number such that
A1: p>0;
  reconsider pp = p as Real by XREAL_0:def 1;
A2: 1 <= Sum(|.z.| rExpSeq) by Th18;
A3: 0 < Sum(|.w.| rExpSeq) by Th18;
  set p1=min((pp/(4 * Sum(|.z.| rExpSeq))),(pp/(4 * Sum(|.w.| rExpSeq))));
A4: p1 > 0 by A1,A2,A3,XXREAL_0:15;
A5: now
    let k be set such that
A6: k in NAT;
    thus |. (z ExpSeq).|.k = |. (z ExpSeq).k .| by VALUED_1:18
      .= (|.z.| rExpSeq).k by A6,Th4;
  end;
A7: |. z ExpSeq .| = |.z.| rExpSeq by A5,FUNCT_2:18;
  consider n1 such that
A8: for k,l be Element of NAT st n1 <= k & n1 <= l holds
  abs(Partial_Sums(( |.z.| rExpSeq )).k - Partial_Sums(( |.z.| rExpSeq )).l)
  < p1 by A4,A7,COMSEQ_3:4;
  consider n2 such that
A9: for k,l be Element of NAT st n2 <= k & n2 <= l holds
  |.Partial_Sums(( w ExpSeq )).k - Partial_Sums(( w ExpSeq )).l.|
  < p1 by A4,COMSEQ_3:47;
  set n3=n1+n2;
  take n4 = n3+n3;
A10: now
    let n;
    let k;
A11: now
      let l be Element of NAT such that
A12:  l <= k;
      thus |.Conj(k,z,w).|.l =|.( Conj(k,z,w).l) .| by VALUED_1:18
        .=|.(z ExpSeq).l * (Partial_Sums(w ExpSeq).k
      -Partial_Sums(w ExpSeq).(k-'l)) .| by A12,Def17
        .=|.(z ExpSeq).l.| * |.(Partial_Sums(w ExpSeq).k
      -Partial_Sums(w ExpSeq).(k-'l)).| by COMPLEX1:151
        .=(|.z.| rExpSeq).l * |.(Partial_Sums(w ExpSeq).k
      -Partial_Sums(w ExpSeq).(k-'l)).| by Th4;
    end;
    thus for l be Element of NAT st l <= k holds
    |.Conj(k,z,w).|.l =(|.z.| rExpSeq).l * |.(Partial_Sums(w ExpSeq).k
    -Partial_Sums(w ExpSeq).(k-'l)).| by A11;
  end;
A13: now
    let k;
A14: now
      let l be Element of NAT;
      assume
A15:  l <= k;
      A16:  |.Conj(k,z,w).|.l =(|.z .| rExpSeq).l * |.Partial_Sums(w ExpSeq ).k
      -Partial_Sums(w ExpSeq).(k-'l).| by A10,A15;
A17:  |.Partial_Sums(w ExpSeq).k .| <= Sum(|.w.| rExpSeq) by Th17;
A18:  |.Partial_Sums(w ExpSeq).k .| + |.Partial_Sums(w ExpSeq).(k-'l ).|
      <= Sum(|.w.| rExpSeq) + |.Partial_Sums(w ExpSeq).(k-'l).| by A17,
      XREAL_1:8;
A19:  |.Partial_Sums(w ExpSeq).(k-'l).| <= Sum(|.w.| rExpSeq) by Th17;
A20:  Sum(|.w.| rExpSeq) + |.Partial_Sums(w ExpSeq).(k-'l).|
      <= Sum(|.w.| rExpSeq)+ Sum(|.w.| rExpSeq) by A19,XREAL_1:8;
      A21:  |.
Partial_Sums(w ExpSeq).k -Partial_Sums(w ExpSeq).(k-'l).| <= |.Partial_Sums
(w ExpSeq).k .| + |.Partial_Sums(w ExpSeq).(k-'l).| & |.Partial_Sums(w ExpSeq)
.k .| + |.Partial_Sums(w ExpSeq).(k-'l).| <= 2 * Sum(|.w.| rExpSeq) by A18,A20
      ,COMPLEX1:143,XXREAL_0:2;
A22:  |.Partial_Sums(w ExpSeq).k -Partial_Sums(w ExpSeq).(k-'l).|
      <= 2 * Sum(|.w.| rExpSeq) by A21,XXREAL_0:2;
A23:  0 <= (|.z .| rExpSeq).l by Th19;
      thus
      |.Conj(k,z,w).|.l <= (|.z .| rExpSeq).l * (2 * Sum(|.w.| rExpSeq))
      by A16,A22,A23,XREAL_1:66;
    end;
    thus for l be Element of NAT st l <= k holds
    |.Conj(k,z,w).|.l <= (|.z .| rExpSeq).l * (2 * Sum(|.w.| rExpSeq)) by A14;
  end;
A24: now
    let k such that
A25: n4 <= k;
A26: 0+n3 <= n3+n3 by XREAL_1:8;
A27: n3 <= k by A25,A26,XXREAL_0:2;
A28: n1+0 <= n1+n2 by XREAL_1:8;
A29: n1 <= k by A27,A28,XXREAL_0:2;
A30: now
      let l be Element of NAT;
      assume
A31:  l <= n3;
A32:  k-'l=k-l by A27,A31,XREAL_1:235,XXREAL_0:2;
A33:  0+n2 <= n1+n2 by XREAL_1:8;
A34:  n4-l <= k-l by A25,XREAL_1:11;
A35:  n3+n3-n3 <= n3+n3-l by A31,XREAL_1:12;
A36:  n3 <= k-l by A34,A35,XXREAL_0:2;
A37:  n2 <= k-'l by A32,A33,A36,XXREAL_0:2;
A38:  0+n3 <= n3+n3 by XREAL_1:8;
A39:  n2 <= n4 by A33,A38,XXREAL_0:2;
A40:  n2 <= k by A25,A39,XXREAL_0:2;
      A41:  |.
Partial_Sums(( w ExpSeq )).k - Partial_Sums(( w ExpSeq )).(k -'l).|
      < p1 by A9,A37,A40;
A42:  0 <= (|.z .| rExpSeq).l by Th19;
A43:  (|.z .| rExpSeq).l * |.Partial_Sums(( w ExpSeq )).k
      - Partial_Sums(( w ExpSeq )).(k-'l).|
      <= (|.z .| rExpSeq).l * p1 by A41,A42,XREAL_1:66;
      thus |.Conj(k,z,w).|.l <= p1 * (|.z .| rExpSeq).l by A10,A27,A31,A43,
      XXREAL_0:2;
    end;
A44: Partial_Sums(|.Conj(k,z,w).|).n3
    <= Partial_Sums(|.z .| rExpSeq).n3 * p1 by A30,COMSEQ_3:7;
A45: Partial_Sums(|.z .| rExpSeq).n3 * p1
    <= Sum(|.z.| rExpSeq) * p1 by A4,Th17,XREAL_1:66;
A46: Partial_Sums(|.Conj(k,z,w).|).n3
    <= Sum(|.z.| rExpSeq) * p1 by A44,A45,XXREAL_0:2;
A47: Sum(|.z.| rExpSeq) * p1 <= Sum(|.z.| rExpSeq) * (p/(4 * Sum
    (|.z.| rExpSeq))) by A2,XREAL_1:66,XXREAL_0:17;
A48: 0 <> Sum(|.z.| rExpSeq) by Th18;
A49: Sum(|.z.| rExpSeq) * (p/(4 * Sum(|.z.| rExpSeq)))
    =(Sum(|.z.| rExpSeq) * p)/(4 * Sum(|.z.| rExpSeq))
      .=p/4 by A48,XCMPLX_1:92;
A50: Partial_Sums(|.Conj(k,z,w).|).n3 <= p/4 by A46,A47,A49,XXREAL_0:2;
A51: 0+p/4 < p/4 + p/4 by A1,XREAL_1:8;
A52: Partial_Sums(|.Conj(k,z,w).|).n3 < p/2 by A50,A51,XXREAL_0:2;
A53: k-'n3=k-n3 by A25,A26,XREAL_1:235,XXREAL_0:2;
A54: k=(k-'n3)+n3 by A53;
A55: for l be Element of NAT st l <= k holds
    |.Conj(k,z,w).|.l <= (2 * Sum(|.w.| rExpSeq)) * (|.z .| rExpSeq).l by A13;
A56: Partial_Sums(|.Conj(k,z,w).|).(k) -Partial_Sums(|.Conj(k,z,w).|). n3
    <= (Partial_Sums(|.z .| rExpSeq).(k) -Partial_Sums(|.z .| rExpSeq).n3)
    * (2 * Sum(|.w.| rExpSeq)) by A54,A55,COMSEQ_3:8;
A57: abs((Partial_Sums(|.z .| rExpSeq).k-Partial_Sums(|.z .| rExpSeq).n3))
    <= p1 by A8,A28,A29;
A58: Partial_Sums(|.z .| rExpSeq).k-Partial_Sums(|.z .| rExpSeq).n3
    <= p1 by A25,A26,A57,Th20,XXREAL_0:2;
A59: (Partial_Sums(|.z .| rExpSeq).k -Partial_Sums(|.z .| rExpSeq).n3)
    * (2 * Sum(|.w.| rExpSeq))
    <= p1 * (2 * Sum(|.w.| rExpSeq)) by A3,A58,XREAL_1:66;
A60: Partial_Sums(|.Conj(k,z,w).|).k-Partial_Sums(|.Conj(k,z,w).|).n3
    <= p1 * (2 * Sum(|.w.| rExpSeq)) by A56,A59,XXREAL_0:2;
A61: (2 * Sum(|.w.| rExpSeq)) * p1
    <= (2 * Sum(|.w.| rExpSeq)) * (p/(4 * Sum(|.w.| rExpSeq)))
    by A3,XREAL_1:66,XXREAL_0:17;
A62: 0 <> Sum(|.w.| rExpSeq) by Th18;
A63: (2 * Sum(|.w.| rExpSeq) ) * (p/(4 * Sum(|.w.| rExpSeq)))
    =((2 * p ) * Sum(|.w.| rExpSeq) ) /(4 * Sum(|.w.| rExpSeq))
      .= (p +p )/4 by A62,XCMPLX_1:92
      .= p/2;
A64: Partial_Sums(|.Conj(k,z,w).|).k-Partial_Sums(|.Conj(k,z,w).|).n3
    <= p/2 by A60,A61,A63,XXREAL_0:2;
A65: (Partial_Sums(|.Conj(k,z,w).|).k -Partial_Sums(|.Conj(k,z,w).|).n3)
    +Partial_Sums(|.Conj(k,z,w).|).n3 < (p/2)+(p/2) by A52,A64,XREAL_1:10;
    thus abs(Partial_Sums(|.Conj(k,z,w).|).k) < p by A65,Th21;
  end;
  thus thesis by A24;
end;

theorem Th23:
  (for k holds seq.k=Partial_Sums((Conj(k,z,w))).k) implies
  seq is convergent & lim seq = 0
proof
A1: now
    let seq such that
A2: for k holds seq.k=Partial_Sums((Conj(k,z,w))).k;
A3: now
      let k;
A4:   |.seq.k.|=|.Partial_Sums((Conj(k,z,w))).k.| by A2;
      thus |.seq.k.| <= Partial_Sums(|.Conj(k,z,w).|).k by A4,COMSEQ_3:30;
    end;
    deffunc U(Element of NAT) = Partial_Sums(|.Conj($1,z,w).|).$1;
    A5: ex rseq be Real_Sequence st for k holds rseq.k = U(k) from SEQ_1:sch 1;
    consider rseq be Real_Sequence such that
A6: for k holds rseq.k=Partial_Sums(|.Conj(k,z,w).|).k by A5;
A7: now
      let k;
A8:   |.seq.k.| <= Partial_Sums(|.Conj(k,z,w).|).k by A3;
      thus |.seq.k.| <= rseq.k by A6,A8;
    end;
A9: now
      let p be real number;
      assume
A10:  p>0;
      consider n such that
A11:  for k st n <= k
      holds abs(Partial_Sums(|.Conj(k,z,w).|).k) < p by A10,Th22;
      take n;
A12:  now
        let k such that
A13:    n <= k;
A14:    abs(rseq.k-0)=abs(Partial_Sums(|.Conj(k,z,w).|).k) by A6;
        thus abs(rseq.k-0) < p by A11,A13,A14;
      end;
      thus for k st n <= k holds abs(rseq.k-0) < p by A12;
    end;
A15: rseq is convergent by A9,SEQ_2:def 6;
A16: lim(rseq) =0 by A9,A15,SEQ_2:def 7;
    thus seq is convergent & lim(seq)=0c by A7,A15,A16,COMSEQ_3:48;
  end;
  thus thesis by A1;
end;

Lm2: for z,w being complex number holds
Sum(z ExpSeq) * Sum(w ExpSeq) = Sum((z+w) ExpSeq)
proof
  let z0,w0 be complex number;
  reconsider z=z0, w=w0 as Element of COMPLEX by XCMPLX_0:def 2;
  deffunc U(Element of NAT) = Partial_Sums(Conj($1,z,w)).$1;
A1: ex seq st for k holds seq.k= U(k) from COMSEQ_1:sch 1;
  consider seq such that
A2: for k holds seq.k=Partial_Sums(Conj(k,z,w)).k by A1;
A3: now
    let k;
    thus seq.k=Partial_Sums(Conj(k,z,w)).k by A2
      .=Partial_Sums(z ExpSeq).k * Partial_Sums(w ExpSeq).k
    -Partial_Sums((z+w) ExpSeq).k by Th16
      .=(Partial_Sums(z ExpSeq) (#) Partial_Sums(w ExpSeq)).k
    -Partial_Sums((z+w) ExpSeq).k by VALUED_1:5
      .=(Partial_Sums(z ExpSeq) (#) Partial_Sums(w ExpSeq)).k
    +(-Partial_Sums((z+w) ExpSeq)).k by VALUED_1:8
      .=(Partial_Sums(z ExpSeq) (#) Partial_Sums(w ExpSeq)
    -Partial_Sums((z+w) ExpSeq) ).k by VALUED_1:1;
  end;
A4: seq=Partial_Sums(z ExpSeq) (#) Partial_Sums(w ExpSeq)
  -Partial_Sums((z+w) ExpSeq) by A3,FUNCT_2:113;

  A5: Partial_Sums(z ExpSeq) (#) Partial_Sums(w ExpSeq) is convergent & lim(

Partial_Sums(z ExpSeq) (#) Partial_Sums(w ExpSeq)) =lim(Partial_Sums (z ExpSeq)
  ) * lim(Partial_Sums(w ExpSeq)) by COMSEQ_2:29,30;
A6: lim(seq)=0c by A2,Th23;
  thus thesis
  by A4,A5,A6,COMSEQ_3:10;
end;

begin :: Definition of Exponential Function on Complex

definition
  func exp -> Function of COMPLEX, COMPLEX means
  :Def18:
  for z being complex number holds it.z=Sum(z ExpSeq);
  existence
  proof
    deffunc U(Element of COMPLEX) = Sum($1 ExpSeq);
    consider f being Function of COMPLEX, COMPLEX such that
A1: for x being Element of COMPLEX holds f.x = U(x) from FUNCT_2:sch 4;
    take f;
    let z be complex number;
A2: z in COMPLEX by XCMPLX_0:def 2;
    thus thesis by A1,A2;
  end;
  uniqueness
  proof
    let f1,f2 be Function of COMPLEX, COMPLEX;
    assume
A3: for z being complex number holds f1.z=Sum(z ExpSeq);
    assume
A4: for z being complex number holds f2.z=Sum(z ExpSeq);
A5: for z being Element of COMPLEX holds f1.z = f2.z
    proof
      let z be Element of COMPLEX;
      thus f1.z=Sum(z ExpSeq) by A3
        .=f2.z by A4;
    end;
    thus f1=f2 by A5,FUNCT_2:113;
  end;
end;

definition
  let z be complex number;
  func exp z -> complex number equals
  exp.z;
  coherence;
end;

definition
  let z be complex number;
  redefine func exp z -> Element of COMPLEX;
  coherence by XCMPLX_0:def 2;
end;

theorem
  for z1, z2 being complex number holds exp(z1+z2)=exp(z1) *exp(z2)
proof
  let z1,z2 be complex number;
A1: exp(z1+z2)=Sum((z1+z2) ExpSeq) by Def18
    .=Sum(z1 ExpSeq)*Sum(z2 ExpSeq) by Lm2
    .=exp(z1)*Sum(z2 ExpSeq) by Def18
    .=exp(z1) *exp(z2) by Def18;
  thus thesis by A1;
end;

begin :: Definition of Sinus, Cosine, and Exponential Function on REAL

definition
  func sin -> Function of REAL, REAL means
  :Def20:
  for d being Element of REAL holds
  it.d = Im(Sum(d*<i> ExpSeq));
  existence
  proof
    deffunc U(Element of REAL) = Im(Sum($1*<i> ExpSeq));
    consider f being Function of REAL, REAL such that
A1: for x being Element of REAL holds f.x = U(x) from FUNCT_2:sch 4;
    take f;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f1,f2 be Function of REAL, REAL;
    assume
A2: for d being Element of REAL holds f1.d=Im(Sum(d*<i> ExpSeq));
    assume
A3: for d being Element of REAL holds f2.d=Im(Sum(d*<i> ExpSeq));
A4: for d being Element of REAL holds f1.d = f2.d
    proof
      let d be Element of REAL;
      thus f1.d=Im(Sum((d*<i>) ExpSeq)) by A2
        .=f2.d by A3;
    end;
    thus f1=f2 by A4,FUNCT_2:113;
  end;
end;

definition
  let th be real number;
  func sin th equals
  sin.th;
  coherence;
end;

registration
  let th be real number;
  cluster sin th -> real;
  coherence;
end;

definition
  let th be Real;
  redefine func sin th -> Real;
  coherence;
end;

reserve d for Real;

definition
  func cos -> Function of REAL, REAL means
  :Def22:
  for d holds it.d = Re(Sum(d*<i> ExpSeq));
  existence
  proof
    deffunc U(Element of REAL) = Re(Sum(($1*<i>) ExpSeq));
    consider f being Function of REAL, REAL such that
A1: for x being Element of REAL holds f.x = U(x) from FUNCT_2:sch 4;
    take f;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let f1,f2 be Function of REAL, REAL;
    assume
A2: for d holds f1.d=Re(Sum(d*<i> ExpSeq));
    assume
A3: for d holds f2.d=Re(Sum(d*<i> ExpSeq));
A4: for d holds f1.d=f2.d
    proof
      let d;
      thus f1.d=Re(Sum((d*<i>) ExpSeq)) by A2
        .=f2.d by A3;
    end;
    thus f1=f2 by A4,FUNCT_2:113;
  end;
end;

definition
  let th be real number;
  func cos th equals
  cos.th;
  coherence;
end;

registration
  let th be real number;
  cluster cos th -> real;
  coherence;
end;

definition
  let th be Real;
  redefine func cos th -> Real;
  coherence;
end;

canceled 2;

theorem Th27:
  dom(sin)=REAL & dom(cos)=REAL by FUNCT_2:def 1;

Lm3: Sum(th*<i> ExpSeq)=cos.(th)+(sin.th)*<i>
proof

  A1: Im
(Sum(th*<i> ExpSeq))=sin.(th) & Re(Sum(th*<i> ExpSeq))=cos.(th) by Def20
  ,Def22;
  thus thesis by A1,COMPLEX1:29;
end;

theorem
  exp(th*<i>)=cos(th)+(sin th)*<i>
proof
A1: exp(th*<i>)=Sum(th*<i> ExpSeq ) by Def18
    .=cos(th)+(sin th)*<i> by Lm3;
  thus thesis by A1;
end;

Lm4: (Sum((th*<i>) ExpSeq))*' = Sum((-(th*<i>)) ExpSeq)
proof
A1: Partial_Sums(((th*<i>) ExpSeq))*' =Partial_Sums((-(th*<i>)) ExpSeq)
  proof
A2: for n holds (((th*<i>) ExpSeq).n)*' = ((-(th*<i>)) ExpSeq).n
    proof
      defpred X[Element of NAT] means
      (((th*<i>) ExpSeq).$1)*' = ((-(th*<i>)) ExpSeq).$1;
A3:   (((th*<i>) ExpSeq).0)*' =1r by Th4,COMPLEX1:115
        .=((-(th*<i>)) ExpSeq).0 by Th4;
A4:   X[0] by A3;
A5:   for n st X[n] holds X[n+1]
      proof
        let n;
        assume
A6:     ((th*<i> ExpSeq).n)*' = ((-(th*<i>)) ExpSeq).n;
        thus (((th*<i>) ExpSeq).(n+1))*'
        =(((th*<i>) ExpSeq).n * (th*<i>)/(n+1+0*<i>))*' by Th4
          .=((th*<i> ExpSeq).n * ((th*<i>)/(n+1+0*<i>)))*'
          .=((-(th*<i>)) ExpSeq).n * ((th*<i>)/(n+1))*' by A6,COMPLEX1:121
          .=((-(th*<i>)) ExpSeq).n * ((0+th*<i>)*'/((n+1))*') by COMPLEX1:123
          .=((-(th*<i>)) ExpSeq).n * ((0+(-th)*<i>) /(n+1+0*<i>)*') by Lm1
          .=((-(th*<i>)) ExpSeq).n * ((0+(-th)*<i>)/(n+1+(-0)*<i>)) by Lm1
          .=((-(th*<i>)) ExpSeq).n * (-(th*<i>))/(n+1+0*<i>)
          .=((-(th*<i>)) ExpSeq).(n+1) by Th4;
      end;
      thus for n holds X[n] from NAT_1:sch 1(A4,A5);
    end;
    defpred X[Element of NAT] means (Partial_Sums(((th*<i>) ExpSeq))*').$1
    =Partial_Sums((-(th*<i>)) ExpSeq).$1;
A7: (Partial_Sums((th*<i>) ExpSeq)*').0
    =(Partial_Sums((th*<i>) ExpSeq).0)*' by COMSEQ_2:def 2
      .=(((th*<i>) ExpSeq).0)*' by COMSEQ_3:def 7
      .=1 by Th4,COMPLEX1:115
      .=((-(th*<i>)) ExpSeq).0 by Th4
      .=Partial_Sums((-(th*<i>)) ExpSeq).0 by COMSEQ_3:def 7;
A8: X[0] by A7;
A9: for n st X[n] holds X[n+1]
    proof
      let n such that
A10:  (Partial_Sums(((th*<i>) ExpSeq))*').n
      =Partial_Sums((-(th*<i>)) ExpSeq).n;
      thus (Partial_Sums((th*<i>) ExpSeq)*').(n+1)
      =(Partial_Sums((th*<i>) ExpSeq).(n+1))*' by COMSEQ_2:def 2
        .=(Partial_Sums((th*<i>) ExpSeq).n+((th*<i>) ExpSeq).(n+1))*'
      by COMSEQ_3:def 7
        .=(Partial_Sums((th*<i>) ExpSeq).n)*'+(((th*<i>) ExpSeq).(n+1))*'
      by COMPLEX1:118
        .=Partial_Sums((-(th*<i>)) ExpSeq).n+(((th*<i>) ExpSeq).(n+1))*'
      by A10,COMSEQ_2:def 2
        .=Partial_Sums((-(th*<i>)) ExpSeq).n+((-(th*<i>)) ExpSeq).(n+1) by A2
        .=Partial_Sums((-(th*<i>)) ExpSeq).(n+1) by COMSEQ_3:def 7;
    end;
A11: for n holds X[n] from NAT_1:sch 1(A8,A9);
    thus thesis by A11,FUNCT_2:113;
  end;
  thus thesis by A1,COMSEQ_2:12;
end;

theorem
  (exp((th*<i>)))*'=exp(-(th*<i>))
proof
A1: (exp((th*<i>)))*'=(Sum((th*<i>) ExpSeq))*' by Def18
    .=Sum((-(th*<i>)) ExpSeq) by Lm4
    .=exp(-(th*<i>)) by Def18;
  thus thesis by A1;
end;

Lm5: for th1 being real number st th = th1 holds
|.Sum(th*<i> ExpSeq ).| = 1 & abs(sin.th1) <= 1 & abs(cos.th1) <= 1
proof
  let th1 be real number such that
A1: th = th1;
A2: Sum((th*<i>) ExpSeq ) * Sum((-(th*<i>)) ExpSeq )
  =Sum(((th*<i>)+(-(th*<i>))) ExpSeq ) by Lm2
    .=1r by Th10;
A3: |.Sum((th*<i>) ExpSeq ).| * |.Sum((th*<i>) ExpSeq ).|
  =|.Sum((th*<i>) ExpSeq )*Sum((th*<i>) ExpSeq ).| by COMPLEX1:151
    .=|.Sum((th*<i>) ExpSeq ) * Sum((th*<i>) ExpSeq)*'.| by COMPLEX1:155
    .=1 by A2,Lm4,COMPLEX1:134;

  A4: |.Sum((th*<i>) ExpSeq ).| =1/|.Sum((th*<i>) ExpSeq ).| & |.Sum((th*<i>)
  ExpSeq ).| <> 0 by A3,XCMPLX_1:74;
A5: |.Sum((th*<i>) ExpSeq ).| <> -1 by COMPLEX1:132;

  thus
A6: |.Sum(th*<i> ExpSeq ).|=1 by A4,A5,XCMPLX_1:200;

  A7: abs
(sin.th)=abs(Im(Sum((th*<i>) ExpSeq ))) & abs(cos.th)=abs(Re(Sum((th*<i>)
  ExpSeq ))) by Def20,Def22;
  thus thesis by A1,A6,A7,COMSEQ_3:13;
end;

theorem
  |.exp((th*<i>)).| = 1 &
  for th being real number holds abs(sin th) <= 1 & abs(cos th) <= 1
proof
  thus |.exp((th*<i>)).| =|.Sum((th*<i>) ExpSeq ).| by Def18
    .=1 by Lm5;
  let th be real number;
A1: th is Real by XREAL_0:def 1;
  thus abs(sin(th)) <= 1 by A1,Lm5;
  thus thesis by A1,Lm5;
end;

reserve th,th1,th2 for real number;

theorem Th31:
  (cos.th)^2+(sin.th)^2=1 & (cos.th)*(cos.th)+(sin.th)*(sin.th)=1
proof
  reconsider th1=th as Real by XREAL_0:def 1;
A1: Sum((th1*<i>) ExpSeq ) * Sum((-(th1*<i>)) ExpSeq )
  =Sum(((th1*<i>)+(-(th1*<i>))) ExpSeq ) by Lm2
    .=1r by Th10;
  thus
A2: (cos.(th))^2+(sin.(th))^2
  =(Re(Sum((th1*<i>) ExpSeq )))^2+(sin.(th))^2 by Def22
    .=(Re(Sum((th1*<i>) ExpSeq )))^2+(Im(Sum((th1*<i>) ExpSeq )))^2 by Def20
    .=|.Sum((th1*<i>) ExpSeq )*Sum((th1*<i>) ExpSeq ).| by COMPLEX1:154
    .=|.Sum((th1*<i>) ExpSeq ) * Sum((th1*<i>) ExpSeq)*'.| by COMPLEX1:155
    .=1 by A1,Lm4,COMPLEX1:134;
  thus thesis by A2;
end;

theorem
  (cos(th))^2+(sin(th))^2=1 &
  (cos(th))*(cos(th))+(sin(th))*(sin(th))=1 by Th31;

theorem Th33:
  cos.0=1 & sin.0=0 & cos.(-th)=cos.th & sin.(-th)=-sin.th
proof
  thus cos.0 = Re(Sum(0*<i> ExpSeq)) by Def22
    .= 1 by Th10,COMPLEX1:15;
  thus sin.0 = Im(Sum(0*<i> ExpSeq)) by Def20
    .= 0 by Th10,COMPLEX1:15;
  reconsider th1=th as Real by XREAL_0:def 1;
  thus cos.(-th) = Re( Sum((-0+(-th1)*<i>) ExpSeq) ) by Def22
    .= Re( Sum((-(th1*<i>)) ExpSeq) )
    .= Re(Sum((th1*<i>) ExpSeq)*') by Lm4
    .= Re((cos.(th)+(sin.th)*<i>)*') by Lm3
    .= Re(cos.(th)+(-sin.th)*<i>) by Lm1
    .= cos.(th) by COMPLEX1:28;
  thus sin.(-th) = Im( Sum((-0+(-th1)*<i>) ExpSeq) ) by Def20
    .= Im( Sum((-(th1*<i>)) ExpSeq) )
    .= Im(Sum((th1*<i>) ExpSeq)*') by Lm4
    .= Im((cos.(th)+(sin.th)*<i>)*') by Lm3
    .= Im(cos.(th)+(-sin.th)*<i>) by Lm1
    .= -sin.(th) by COMPLEX1:28;
end;

theorem
  cos(0)=1 & sin(0)=0 & cos(-th)=cos(th) & sin(-th)=-sin(th) by Th33;

definition
  let th be real number;
  deffunc U(Element of NAT) = (-1)|^ $1 * (th)|^ (2*$1+1)/((2*$1+1)!);
  func th P_sin -> Real_Sequence means
  :Def24:
  for n holds it.n = (-1)|^ n * (th)|^ (2*n+1)/((2*n+1)!);
  existence
  proof
    thus
    ex s being Real_Sequence st for n holds s.n = U(n) from SEQ_1:sch 1;
  end;
  uniqueness
  proof
    thus for s1,s2 being Real_Sequence st
    (for x being Element of NAT holds s1.x = U(x)) &
    (for x being Element of NAT holds s2.x = U(x)) holds s1 = s2
    from BINOP_2:sch 1;
  end;
  deffunc U(Element of NAT) = (-1)|^ $1 * (th)|^ (2*$1)/((2*$1)!);
  func th P_cos -> Real_Sequence means
  :Def25:
  for n holds it.n = (-1)|^ n * (th)|^ (2*n)/((2*n)!);
  existence
  proof
    thus
    ex s being Real_Sequence st for n holds s.n = U(n) from SEQ_1:sch 1;
  end;
  uniqueness
  proof
    thus for s1,s2 being Real_Sequence st
    (for x being Element of NAT holds s1.x = U(x)) &
    (for x being Element of NAT holds s2.x = U(x)) holds s1 = s2
    from BINOP_2:sch 1;
  end;
end;

theorem Th35:
  for z being complex number,
  k holds z|^ (2*k) = (z|^ k)|^ 2& z|^ (2*k)= (z|^ 2)|^ k
proof
  let z be complex number, k;
  defpred X[Element of NAT] means
  z|^ (2*$1) = (z|^ $1)|^ 2 & z|^ (2*$1)= (z|^ 2)|^ $1;
A1: z|^ (2*0) =1*1 by NEWTON:9
    .= 1|^1*1 by NEWTON:10
    .= 1|^(1+1) by NEWTON:11
    .= (z|^ 0)|^2 by NEWTON:9;
A2: z|^ (2*0) = 1 by NEWTON:9
    .= (z|^ 2)|^ 0 by NEWTON:9;
A3: X[0] by A1,A2;
A4: for k st X[k] holds X[k+1]
  proof
    let k;
    assume that
A5: z|^ (2*k) = (z|^ k)|^ 2 and
A6: z|^ (2*k)= (z|^ 2)|^ k;
A7: z|^ (2*(k+1))=z GeoSeq.(2*k+1+1)
      .=z GeoSeq.(2*k+1)*z by COMSEQ_3:def 1
      .=z|^ (2*k)*z*z by COMSEQ_3:def 1;
A8: z|^ (2*(k+1)) =((z|^ k)GeoSeq.(1+1))*z*z by A5,A7
      .=((z|^ k)GeoSeq.(0+1)*(z|^ k))*z*z by COMSEQ_3:def 1
      .=((z|^ k)GeoSeq.0*(z|^ k)*(z|^ k))*z*z by COMSEQ_3:def 1
      .=((1r)*(z|^ k)*(z|^ k))*z*z by COMSEQ_3:def 1
      .=(1r)*(z GeoSeq.k*z)*((z|^ k)*z)
      .=(1r)*(z GeoSeq.(k+1))*(z GeoSeq.k*z) by COMSEQ_3:def 1
      .=(1r)*(z|^(k+1))*(z GeoSeq.(k+1)) by COMSEQ_3:def 1
      .=((z|^(k+1))GeoSeq.0)*(z|^(k+1))*(z|^(k+1)) by COMSEQ_3:def 1
      .=((z|^(k+1))GeoSeq.(0+1))*(z|^(k+1)) by COMSEQ_3:def 1
      .=(z|^(k+1))GeoSeq.(0+1+1) by COMSEQ_3:def 1
      .=(z|^(k+1))|^ 2;
A9: z|^ (2*(k+1))= (z|^ 2)|^ k*(1r*z*z) by A6,A7
      .= (z|^ 2)|^ k*(z GeoSeq.0 *z*z) by COMSEQ_3:def 1
      .=(z|^ 2)|^ k*(z GeoSeq.(0+1) *z) by COMSEQ_3:def 1
      .=(z|^ 2)|^ k*z GeoSeq.(0+1+1) by COMSEQ_3:def 1
      .= (z|^ 2)|^ (k+1) by COMSEQ_3:def 1;
    thus thesis by A8,A9;
  end;
A10: for k holds X[k] from NAT_1:sch 1(A3,A4);
  thus thesis by A10;
end;

theorem Th36:
  for th being Real holds (th*<i>) |^ (2*k)=(-1)|^ k* (th |^ (2*k))
  & (th*<i>) |^ (2*k+1)=(-1)|^ k* (th |^ (2*k+1))*<i>
proof
  let th be Real;
A1: (-1)|^ 0* (th |^ (2*0))*<i>=(-1)|^ 0* th GeoSeq.0*<i> by PREPOWER:def 1
    .=(-1)|^ 0*(1)*<i> by PREPOWER:4
    .=(-1)GeoSeq.0*<i> by PREPOWER:def 1
    .=1*<i> by PREPOWER:4;
A2: (th*<i>) |^ (2*0+1)=(th*<i>) GeoSeq.0*(th*<i>) by COMSEQ_3:def 1
    .=1*(th*<i>) by COMSEQ_3:def 1
    .=(th*<i>);
  defpred X[Element of NAT] means
  (th*<i>) |^ (2*$1)=(-1)|^ $1* (th |^ (2*$1))
  & (th*<i>) |^ (2*$1+1)=(-1)|^ $1* (th |^ (2*$1+1))*<i>;
A3: (-1)|^ 0 * (th |^ (2*0+1))*<i>=(-1)|^ 0 * (th GeoSeq.(2*0+1))*<i>
  by PREPOWER:def 1
    .= (-1)|^ 0* (th GeoSeq.0*th)*<i> by PREPOWER:4
    .=(-1)|^ 0* (1*th)*<i> by PREPOWER:4
    .= ((-1)GeoSeq.0)* th *<i> by PREPOWER:def 1
    .=(1)* th *<i> by PREPOWER:4
    .=(th*<i>);
A4: X[0] by A1,A2,A3,COMSEQ_3:def 1;
A5: for k st X[k] holds X[k+1]
  proof
    let k;
    assume that
A6: (th*<i>) |^ (2*k)=(-1)|^ k* (th |^ (2*k)) and
A7: (th*<i>) |^ (2*k+1)=(-1)|^ k* (th |^ (2*k+1))*<i>;
A8: (th*<i>) |^ (2*(k+1))= ((th*<i>) |^ 2)|^(k+1) by Th35
      .=((th*<i>) |^ 2)|^ k * ((th*<i>) |^ 2) by COMSEQ_3:def 1
      .= (-1)|^ k* (th |^ (2*k))* ((th*<i>)GeoSeq.(1+1)) by A6,Th35
      .=(-1)|^ k* (th |^ (2*k))* ((th*<i>)GeoSeq.(0+1)*(th*<i>))
    by COMSEQ_3:def 1
      .=(-1)|^ k* (th |^ (2*k))* ((th*<i>)GeoSeq.0*(th*<i>)
    *(th*<i>)) by COMSEQ_3:def 1
      .= (-1)|^ k* (th |^ (2*k))* (1r*(th*<i>)*(th*<i>))
    by COMSEQ_3:def 1
      .=(-1)|^ k*(-1)* (th |^ (2*k))*th*th
      .=(-1)GeoSeq.k*(-1)* (th |^ (2*k))*th*th by PREPOWER:def 1
      .=(-1)GeoSeq.(k+1)* (th |^ (2*k))*th*th by PREPOWER:4
      .=(-1)|^(k+1)* (th |^ (2*k))*th*th by PREPOWER:def 1
      .=(-1)|^(k+1)* (th GeoSeq.(2*k))*th*th by PREPOWER:def 1
      .= (-1)|^(k+1)* (th GeoSeq.(2*k)*th)*th
      .=(-1)|^(k+1)* (th GeoSeq.(2*k+1))*th by PREPOWER:4
      .=(-1)|^(k+1)* (th GeoSeq.(2*k+1)*th)
      .=(-1)|^(k+1)* (th GeoSeq.(2*k+1+1)) by PREPOWER:4
      .=(-1)|^(k+1)* (th |^ (2*(k+1))) by PREPOWER:def 1;
A9: (th*<i>) |^ (2*(k+1)+1)=
    (th*<i>)GeoSeq.(2*k+1+1)*(th*<i>) by COMSEQ_3:def 1
      .=(th*<i>)GeoSeq.(2*k+1)*(th*<i>)*(th*<i>) by COMSEQ_3:def 1
      .=(-1)|^ k*(-1)* (th |^ (2*k+1))*th*th*<i> by A7
      .=(-1)GeoSeq.k*(-1)* (th |^ (2*k+1))*th*th*<i> by PREPOWER:def 1
      .=(-1)GeoSeq.(k+1)* (th |^ (2*k+1))*th*th*<i> by PREPOWER:4
      .=(-1)|^ (k+1)* (th |^ (2*k+1))*th*th*<i> by PREPOWER:def 1
      .=(-1)|^ (k+1)* (th GeoSeq.(2*k+1))*th*th*<i> by PREPOWER:def 1
      .=(-1)|^ (k+1)* (th GeoSeq.(2*k+1)*th)*th*<i>
      .=(-1)|^ (k+1)* (th GeoSeq.(((2*k+1)+1)))*th*<i> by PREPOWER:4
      .=(-1)|^ (k+1)* (th GeoSeq.((2*k+1+1))*th)*<i>
      .= (-1)|^ (k+1)* (th GeoSeq.(2*(k+1)+1))*<i> by PREPOWER:4
      .=(-1)|^ (k+1)*(th |^ (2*(k+1)+1))*<i> by PREPOWER:def 1;
    thus thesis by A8,A9;
  end;
A10: for k holds X[k] from NAT_1:sch 1(A4,A5);
  thus thesis by A10;
end;

theorem Th37:
  n!c = n!
proof
  defpred X[Element of NAT] means $1!c=$1!;
A1: X[0] by Th1,NEWTON:18;
A2: for k st X[k] holds X[k+1]
  proof
    let k be Element of NAT;
    assume
A3: k!c=k!;
    thus (k+1)!c = k!*(k+1) by A3,Th1
      .=(k+1)! by NEWTON:21;
  end;
A4: for k holds X[k] from NAT_1:sch 1(A1,A2);
  thus thesis by A4;
end;

theorem Th38:
  for th being Real holds
  Partial_Sums(th P_sin).n = Partial_Sums(Im ((th*<i>) ExpSeq)).(2*n+1) &
  Partial_Sums(th P_cos).n = Partial_Sums(Re ((th*<i>) ExpSeq)).(2*n)
proof
  let th be Real;
A1: now
A2: Partial_Sums(th P_sin).0= (th P_sin).0 by SERIES_1:def 1
      .= (-1)|^ 0 * (th)|^ (2*0+1)/((2*0+1)!) by Def24;
A3: (th)|^ (2*0+1)= th GeoSeq.(0+1) by PREPOWER:def 1
      .= th GeoSeq.0 * th by PREPOWER:4
      .= 1* th by PREPOWER:4
      .= th;
A4: ((2*0+1)!)= 0! * 1 by NEWTON:21
      .= 1 by NEWTON:18;
A5: (-1)|^ 0 = (-1) GeoSeq.0 by PREPOWER:def 1
      .= 1 by PREPOWER:4;
A6: Partial_Sums(th P_cos).0= (th P_cos).0 by SERIES_1:def 1
      .=(-1)|^ 0 * (th)|^ (2*0)/((2*0)!) by Def25
      .= 1* th GeoSeq.0 / 1 by A5,NEWTON:18,PREPOWER:def 1
      .=1 by PREPOWER:4;
    A7: (
Im ((th*<i>) ExpSeq)).0= Im (((th*<i>) ExpSeq).0) & (Im ((th*<i>) ExpSeq)).
    1= Im (((th*<i>) ExpSeq).1) by COMSEQ_3:def 4;
A8: (th*<i>) = 0+th*<i>;
A9: Partial_Sums(Im ((th*<i>) ExpSeq)).(2*0+1)
    = Partial_Sums(Im ((th*<i>) ExpSeq)).0 +
    (Im ((th*<i>) ExpSeq)).1 by SERIES_1:def 1
      .= (Im ((th*<i>) ExpSeq)).0+
    (Im ((th*<i>) ExpSeq)).1 by SERIES_1:def 1
      .= 0 + Im (((th*<i>) ExpSeq).(0+1)) by A7,Th4,COMPLEX1:15
      .=0 + Im ((((th*<i>) ExpSeq).0)* (th*<i>) /(0+1+0*<i>)) by Th4
      .= Im (1* (th*<i>) /1) by Th4
      .=th by A8,COMPLEX1:28;
    defpred X[Element of NAT] means
    Partial_Sums(th P_sin).$1 = Partial_Sums(Im ((th*<i>) ExpSeq)).(2*$1+1) &
    Partial_Sums(th P_cos).$1 = Partial_Sums(Re ((th*<i>) ExpSeq)).(2*$1);
A10: Partial_Sums(Re ((th*<i>) ExpSeq)).(2*0)
    = (Re ((th*<i>) ExpSeq)).0 by SERIES_1:def 1
      .= Re (((th*<i>) ExpSeq).0) by COMSEQ_3:def 3
      .=1 by Th4,COMPLEX1:15;
A11: X[0] by A2,A3,A4,A5,A6,A9,A10;
A12: for k st X[k] holds X[k+1]
    proof
      let k be Element of NAT;
      assume that
A13:  Partial_Sums(th P_sin).k = Partial_Sums(Im ((th*<i>) ExpSeq)).(2*k+1) and
A14:  Partial_Sums(th P_cos).k = Partial_Sums(Re ((th*<i>) ExpSeq)).(2*k);
A15:  Partial_Sums(Im ((th*<i>) ExpSeq)).(2*(k+1)+1)
      = Partial_Sums(Im ((th*<i>) ExpSeq)).((2*k+1)+1) +
      (Im ((th*<i>) ExpSeq)).(2*(k+1)+1) by SERIES_1:def 1
        .= Partial_Sums(th P_sin).k +
      (Im((th*<i>) ExpSeq)).(2*(k+1))+(Im ((th*<i>) ExpSeq)).(2*(k+1)+1)
      by A13,SERIES_1:def 1
        .= Partial_Sums(th P_sin).k +
      Im (((th*<i>) ExpSeq).(2*(k+1)))+(Im((th*<i>) ExpSeq)).(2*(k+1)+1)
      by COMSEQ_3:def 4
        .=Partial_Sums(th P_sin).k +
      Im (((th*<i>) ExpSeq).(2*(k+1)))+Im(((th*<i>) ExpSeq).(2*(k+1)+1))
      by COMSEQ_3:def 4
        .= Partial_Sums(th P_sin).k +
      Im((th*<i>) |^ (2*(k+1))/((2*(k+1))!c))+
      Im(((th*<i>) ExpSeq).(2*(k+1)+1)) by Def8
        .= Partial_Sums(th P_sin).k +
      Im((th*<i>) |^ (2*(k+1))/((2*(k+1))!c))+
      Im((th*<i>) |^ (2*(k+1)+1)/((2*(k+1)+1)!c)) by Def8
        .= Partial_Sums(th P_sin).k +
      Im( (-1)|^ (k+1)* (th |^ (2*(k+1)))/ ((2*(k+1))!c))
      +Im((th*<i>) |^ (2*(k+1)+1)/((2*(k+1)+1)!c)) by Th36
        .= Partial_Sums(th P_sin).k +
      Im( (-1)|^ (k+1)* (th |^ (2*(k+1)))/ ((2*(k+1))!c))
      + Im( (-1)|^ (k+1)* (th |^ (2*(k+1)+1))*<i>/((2*(k+1)+1)!c)) by Th36
        .= Partial_Sums(th P_sin).k +
      Im( ((-1)|^ (k+1)* (th |^ (2*(k+1)))) / ((2*(k+1))!))
      + Im( (-1)|^ (k+1)* (th |^ (2*(k+1)+1))*<i>/((2*(k+1)+1)!c))
      by Th37
        .= Partial_Sums(th P_sin).k +
      Im( ((-1)|^ (k+1)* (th |^ (2*(k+1)))) / ((2*(k+1))!))
      + Im( (-1)|^ (k+1)* (th |^ (2*(k+1)+1))*<i>/((2*(k+1)+1)!))
      by Th37
        .= Partial_Sums(th P_sin).k +
      Im( ((-1)|^ (k+1)* (th |^ (2*(k+1)))) / ((2*(k+1))!c))
      + Im( (-1)|^ (k+1)* (th |^ (2*(k+1)+1))*<i>/((2*(k+1)+1)!))
      by Th37
        .= Partial_Sums(th P_sin).k +
      Im( ((-1)|^ (k+1)* (th |^ (2*(k+1)))) / ((2*(k+1))!c))
      + Im( (-1)|^ (k+1)* (th |^ (2*(k+1)+1))*<i>/((2*(k+1)+1)!c))
      by Th37
        .= Partial_Sums(th P_sin).k +
      Im( (-1)|^ (k+1)* (th |^ (2*(k+1)))/ ((2*(k+1))!))
      + Im( (-1)|^ (k+1)* (th |^ (2*(k+1)+1))*<i>/((2*(k+1)+1)!c))
      by Th37
        .= Partial_Sums(th P_sin).k +
      Im( (-1)|^ (k+1)* (th |^ (2*(k+1)))/ ((2*(k+1))!))
      + Im( (-1)|^ (k+1)* (th |^ (2*(k+1)+1))*<i>/((2*(k+1)+1)!))
      by Th37;
A16:  Partial_Sums(Im ((th*<i>) ExpSeq)).(2*(k+1)+1)
      = Partial_Sums(th P_sin).k +
      Im( (((-1)|^ (k+1)* (th |^ (2*(k+1))))/((2*(k+1))!)+0/((2*(k+1))!)*<i>))
      + Im( ((-1)|^ (k+1)* (th |^ (2*(k+1)+1))*<i>)/((2*(k+1)+1)!))
      by A15;
A17:  Partial_Sums(Im ((th*<i>) ExpSeq)).(2*(k+1)+1)
      = Partial_Sums(th P_sin).k + 0 + Im(0/((2*(k+1)+1)!)+
      ((-1)|^ (k+1)* (th |^ (2*(k+1)+1)))/((2*(k+1)+1)!)*<i>)
      by A16,COMPLEX1:28
        .= Partial_Sums(th P_sin).k + (0/((2*(k+1))!)) +
      (-1)|^ (k+1)* (th |^ (2*(k+1)+1))/((2*(k+1)+1)!) by COMPLEX1:28;
A18:  Partial_Sums(Im ((th*<i>) ExpSeq)).(2*(k+1)+1)
      = Partial_Sums(th P_sin).k + th P_sin.(k+1) by A17,Def24
        .= Partial_Sums(th P_sin).(k+1) by SERIES_1:def 1;
A19:  Partial_Sums(Re ((th*<i>) ExpSeq)).(2*(k+1))
      = Partial_Sums(Re ((th*<i>) ExpSeq)).(2*k+1)+
      (Re ((th*<i>) ExpSeq)).((2*k+1)+1) by SERIES_1:def 1
        .=Partial_Sums(th P_cos).k + (Re ((th*<i>) ExpSeq)).(2*k+1)+
      (Re ((th*<i>) ExpSeq)).((2*k+1)+1) by A14,SERIES_1:def 1
        .= Partial_Sums(th P_cos).k + Re (((th*<i>) ExpSeq).(2*k+1))+
      (Re ((th*<i>) ExpSeq)).((2*k+1)+1) by COMSEQ_3:def 3
        .= Partial_Sums(th P_cos).k + Re (((th*<i>) ExpSeq).(2*k+1))+
      Re (((th*<i>) ExpSeq).((2*k+1)+1)) by COMSEQ_3:def 3
        .=Partial_Sums(th P_cos).k + Re ((th*<i>)|^ (2*k+1)/ ((2*k+1)!c))+
      Re (((th*<i>) ExpSeq).((2*k+1)+1)) by Def8
        .= Partial_Sums(th P_cos).k + Re ((th*<i>)|^ (2*k+1)/ ((2*k+1)!c))+
      Re ((th*<i>)|^ (2*(k+1))/(((2*k+1)+1)!c)) by Def8
        .= Partial_Sums(th P_cos).k +
      Re ( (-1)|^ k* (th |^ (2*k+1))*<i>/((2*k+1)!c))+
      Re ((th*<i>)|^ (2*(k+1))/((2*(k+1))!c)) by Th36
        .= Partial_Sums(th P_cos).k +
      Re ( (-1)|^ k* (th |^ (2*k+1))*<i>/((2*k+1)!c))+
      Re ((-1)|^ (k+1)* (th |^ (2*(k+1)))/((2*(k+1))!c)) by Th36
        .= Partial_Sums(th P_cos).k +
      Re ( (-1)|^ k* (th |^ (2*k+1))*<i>/((2*k+1)!))+
      Re ((-1)|^ (k+1)* (th |^ (2*(k+1)))/((2*(k+1))!c)) by Th37
        .= Partial_Sums(th P_cos).k +
      Re ( (-1)|^ k* (th |^ (2*k+1))*<i>/((2*k+1)!))+
      Re ((-1)|^ (k+1)* (th |^ (2*(k+1)))/((2*(k+1))!)) by Th37;
A20:  Partial_Sums(Re ((th*<i>) ExpSeq)).(2*(k+1))
      = Partial_Sums(th P_cos).k +
      Re ( (0/((2*k+1)!)+((-1)|^ k* (th |^ (2*k+1)))/((2*k+1)!)*<i>)) +
      Re ((-1)|^ (k+1)* (th |^ (2*(k+1)))/((2*(k+1))!)) by A19;
A21:  Partial_Sums(Re ((th*<i>) ExpSeq)).(2*(k+1))
      = Partial_Sums(th P_cos).k +0/((2*k+1)!)+
      Re ((-1)|^ (k+1)* (th |^ (2*(k+1)))/((2*(k+1))!)+(0/((2*(k+1))!))*<i>)
      by A20,COMPLEX1:28
        .= Partial_Sums(th P_cos).k +0/((2*k+1)!)+
      (-1)|^ (k+1)* (th |^ (2*(k+1)))/((2*(k+1))!) by COMPLEX1:28;
A22:  Partial_Sums(Re ((th*<i>) ExpSeq)).(2*(k+1))
      = Partial_Sums(th P_cos).k + th P_cos.(k+1) by A21,Def25
        .= Partial_Sums(th P_cos).(k+1) by SERIES_1:def 1;
      thus thesis by A18,A22;
    end;
    thus for n holds X[n] from NAT_1:sch 1(A11,A12);
  end;
  thus thesis by A1;
end;

theorem Th39:
  for th being Real holds
  Partial_Sums(th P_sin) is convergent & Sum(th P_sin)=Im(Sum
  ((th*<i>) ExpSeq))
  &Partial_Sums(th P_cos) is convergent & Sum(th P_cos)=Re(Sum
  ((th*<i>) ExpSeq))
proof
  let th be Real;
A1: Sum(((th*<i>) ExpSeq))
  =Sum(Re ((th*<i>) ExpSeq))+Sum(Im ((th*<i>) ExpSeq))*<i> by COMSEQ_3:54;
A2: Sum(Re ((th*<i>) ExpSeq))= Re (Sum(((th*<i>) ExpSeq))) by A1,COMPLEX1:28;
A3: Sum(Im ((th*<i>) ExpSeq))=Im (Sum(((th*<i>) ExpSeq))) by A1,COMPLEX1:28;
  A4: Partial_Sums
(Re ((th*<i>) ExpSeq)) is convergent & lim Partial_Sums(Re ((th*
  <i>) ExpSeq))= Re (Sum(((th*<i>) ExpSeq))) by A2,SERIES_1:def 2,def 3;
  A5: Partial_Sums
(Im ((th*<i>) ExpSeq)) is convergent & lim Partial_Sums(Im ((th*
  <i>) ExpSeq))=Im (Sum(((th*<i>) ExpSeq))) by A3,SERIES_1:def 2,def 3;
A6: now
    let p be real number;
    assume
A7: p>0;
    consider n such that
A8: for k st n <= k holds abs(Partial_Sums(Re ((th*<i>) ExpSeq)).k-
    Re (Sum(((th*<i>) ExpSeq)))) < p by A4,A7,SEQ_2:def 7;
    take n;
A9: now
      let k such that
A10:  n <= k;
      A11:  abs
(Partial_Sums(th P_cos).k-Re (Sum(((th*<i>) ExpSeq)))) =abs(Partial_Sums(
Re ((th*<i>) ExpSeq)).(2*k)- Re (Sum(((th*<i>) ExpSeq)))) & 2*k=k+k by Th38;
      thus abs(Partial_Sums(th P_cos).k-Re (Sum(((th*<i>) ExpSeq))))
      < p by A8,A10,A11,NAT_1:12;
    end;
    thus for k st n <= k holds
    abs(Partial_Sums(th P_cos).k-Re (Sum(((th*<i>) ExpSeq)))) < p by A9;
  end;
A12: Partial_Sums(th P_cos) is convergent by A6,SEQ_2:def 6;
A13: lim(Partial_Sums(th P_cos))=Re(Sum ((th*<i>) ExpSeq)) by A6,A12,
  SEQ_2:def 7;
A14: now
    let p be real number;
    assume
A15: p>0;
    consider n such that
A16: for k st n <= k holds abs(Partial_Sums(Im ((th*<i>) ExpSeq)).k-
    Im (Sum(((th*<i>) ExpSeq)))) < p by A5,A15,SEQ_2:def 7;
    take n;
A17: now
      let k such that
A18:  n <= k;
A19:  abs(Partial_Sums(th P_sin).k-Im (Sum(((th*<i>) ExpSeq))))
      =abs(Partial_Sums(Im ((th*<i>) ExpSeq)).(2*k+1)-
      Im (Sum(((th*<i>) ExpSeq)))) by Th38;
A20:  2*k=k+k;
A21:  n <= 2*k by A18,A20,NAT_1:12;
A22:  n<2*k+1 by A21,NAT_1:13;
      thus abs(Partial_Sums(th P_sin).k-Im (Sum(((th*<i>) ExpSeq))))
      < p by A16,A19,A22;
    end;
    thus for k st n <= k holds
    abs(Partial_Sums(th P_sin).k-Im (Sum(((th*<i>) ExpSeq)))) < p by A17;
  end;
A23: Partial_Sums(th P_sin) is convergent by A14,SEQ_2:def 6;
A24: lim(Partial_Sums(th P_sin))=Im(Sum
  ((th*<i>) ExpSeq)) by A14,A23,SEQ_2:def 7;
  thus thesis by A6,A13,A14,A24,SEQ_2:def 6,SERIES_1:def 3;
end;

theorem Th40:
  cos.th=Sum(th P_cos) & sin.th=Sum(th P_sin)
proof
  reconsider th as Real by XREAL_0:def 1;
  A1: sin.th = Im(Sum((th*<i>) ExpSeq)) & cos.th=Re(Sum((th*<i>) ExpSeq)) by
  Def20,Def22;
  thus thesis by A1,Th39;
end;

theorem
  for p,th,rseq st rseq is convergent &
  lim(rseq)=th & (for n holds rseq.n >= p) holds th >= p
  by PREPOWER:2;

deffunc U(Real) = 2*$1+1;
consider bq being Real_Sequence such that
Lm6: for n holds bq.n= U(n) from SEQ_1:sch 1;
Lm7: bq is increasing sequence of NAT
proof

A1: for n,k st n<k holds bq.n<bq.k
  proof
    let n,k;
    assume
A2: n<k;
A3: 2*n<2*k by A2,XREAL_1:70;
A4: 2*n+1<2*k+1 by A3,XREAL_1:8;
A5: bq.n<2*k+1 by A4,Lm6;
    thus thesis by A5,Lm6;
  end;
A6: for n holds bq.n is Element of NAT
  proof
    let n;
A7: 2*n+1 is Element of NAT;
    thus thesis by A7,Lm6;
  end;
  thus thesis by A1,A6,SEQM_3:7,29;
end;
reconsider bq as increasing sequence of NAT by Lm7;

Lm8: for th,th1,th2,th3 be real number holds
th |^ (2*n)=(th |^ n)|^ 2 & (-1)|^(2*n)=1 & (-1)|^ (2*n+1)=-1
proof
  let th,th1,th2,th3 be real number;
  thus th |^ (2*n)=(th |^ n)|^ 2 by NEWTON:14;
  thus
A1: (-1)|^(2*n)=(1)|^(2*n) by POWER:1
    .=1 by NEWTON:15;
  thus (-1)|^ (2*n+1)=(-1)|^ (2*n)*(-1) by NEWTON:11
    .=-1 by A1;
end;

theorem Th42:
  n <= k implies n!<=k!
proof
  assume
A1: n <= k;
A2: for m holds 1<= m+1
  proof
    let m;
A3: 0+1<= m+1 by XREAL_1:8;
    thus thesis by A3;
  end;
  deffunc U(Element of NAT) = $1!;
  consider rseq such that
A4: for l holds rseq.l=U(l) from SEQ_1:sch 1;
A5: for l holds rseq.l<=rseq.(l+1)& rseq.l>0
  proof
    let l;
    defpred X[Element of NAT] means rseq.$1<=rseq.($1+1)& rseq.$1>0;
A6: rseq.(0+1)=(0+1)! by A4
      .=0!*1 by NEWTON:21
      .=1 by NEWTON:18;
A7: X[0] by A4,A6,NEWTON:18;
A8: for l st X[l] holds X[l+1]
    proof
      let l;
      assume
A9:   rseq.l<=rseq.(l+1) & rseq.l>0;
A10:  rseq.(l+1+1)= (l+1+1)! by A4
        .=(l+1)!*(l+1+1) by NEWTON:21
        .=rseq.(l+1) *(l+1+1) by A4;
      thus thesis by A2,A9,A10,XREAL_1:153;
    end;
A11: for l holds X[l] from NAT_1:sch 1(A7,A8);
    thus thesis by A11;
  end;
A12: rseq is non-decreasing by A5,SEQM_3:def 13;
A13: n!=rseq.n & k! = rseq.k by A4;
  thus thesis by A1,A12,A13,SEQM_3:12;
end;

theorem Th43:
  0<=th & th <=1 & n<=k implies th |^ k <= th |^ n
proof
  assume that
A1: 0<=th and
A2: th <=1 and
A3: n<=k;
A4: for m holds th GeoSeq.(m+1)<=th GeoSeq.m& th GeoSeq.m >=0
  proof
    let m;
    defpred X[Element of NAT] means
    th GeoSeq.($1+1)<=th GeoSeq.$1 &th GeoSeq.$1 >=0;
A5: th GeoSeq.(0+1)=th GeoSeq.0 * th by PREPOWER:4
      .= 1*th by PREPOWER:4
      .=th;
A6: X[0] by A2,A5,PREPOWER:4;
A7: for m st X[m] holds X[m+1]
    proof
      let m;
      assume that
      th GeoSeq.(m+1)<=th GeoSeq.m and
A8:   th GeoSeq.m >=0;
      A9:   th
 GeoSeq.(m+1+1)=th GeoSeq.(m+1)*th & th GeoSeq.(m+1)=th GeoSeq.(m)*th by
      PREPOWER:4;
      thus thesis by A1,A2,A8,A9,XREAL_1:155;
    end;
A10: for m holds X[m] from NAT_1:sch 1(A6,A7);
    thus thesis by A10;
  end;
A11: th GeoSeq is non-increasing by A4,SEQM_3:def 14;
  A12: th |^ k=th GeoSeq. k & th |^ n=th GeoSeq. n by PREPOWER:def 1;
  thus thesis by A3,A11,A12,SEQM_3:14;
end;

canceled;

theorem
  for th being Real holds th |^ n /(n!c)=(th|^ n) /(n!) by Th37;

theorem Th46:
  for p being real number holds Im(Sum(p ExpSeq))=0
proof
  let p be real number;
A1: for n holds (Partial_Sums(Im(p ExpSeq))).n=0
  proof
    let n;
    defpred X[Element of NAT] means (Partial_Sums(Im(p ExpSeq))).$1=0;
A2: (Partial_Sums(Im(p ExpSeq))).0=Im(p ExpSeq).0
    by SERIES_1:def 1
      .=Im(p ExpSeq.0) by COMSEQ_3:def 4
      .=0 by Th4,COMPLEX1:15;
A3: X[0] by A2;
A4: for k st X[k] holds X[k+1]
    proof
      let k;
      assume
A5:   (Partial_Sums(Im(p ExpSeq))).k=0;
A6:   (Partial_Sums(Im(p ExpSeq))).(k+1)
      =0+ (Im(p ExpSeq)).(k+1) by A5,SERIES_1:def 1
        .=Im((p ExpSeq).(k+1)) by COMSEQ_3:def 4
        .=Im(p |^ (k+1) /((k+1)!c) ) by Def8
        .=Im((p|^ (k+1))/((k+1)!)+0*<i>) by Th37
        .=0 by COMPLEX1:28;
      thus thesis by A6;
    end;
A7: for n holds X[n] from NAT_1:sch 1(A3,A4);
    thus thesis by A7;
  end;
A8: for n,m being Nat holds (Partial_Sums(Im(p ExpSeq))).n=
  (Partial_Sums(Im(p ExpSeq))).m
  proof
    let n,m be Nat;
A9: n in NAT by ORDINAL1:def 13;
    A10: m in NAT & (Partial_Sums(Im(p ExpSeq))).n=0 by A1,A9,ORDINAL1:def 13;
    thus thesis by A1,A10;
  end;
A11: lim Partial_Sums(Im(p ExpSeq))=Partial_Sums(Im(p
  ExpSeq)).0 by A8,SEQ_4:41,VALUED_0:24
    .= 0 by A1;
A12: Im(Sum(p ExpSeq))
  = Im(Sum(Re (p ExpSeq))+(Sum(Im (p ExpSeq))*<i>)) by COMSEQ_3:54
    .= Sum(Im (p ExpSeq )) by COMPLEX1:28;
  thus thesis by A11,A12,SERIES_1:def 3;
end;

theorem Th47:
  cos.1>0 & sin.1>0 & cos.1<sin.1
proof
A1: Partial_Sums(1 P_cos) is convergent by Th39;
A2: cos.1=Sum(1 P_cos) by Th40;
A3: lim ((Partial_Sums(1 P_cos))*bq )=lim(Partial_Sums(1 P_cos)) by A1,
  SEQ_4:30;
A4: lim ((Partial_Sums(1 P_cos))*bq )= cos.1 by A2,A3,SERIES_1:def 3;
A5: for n holds ((Partial_Sums(1 P_cos))*bq).n >=1/2
  proof
    let n;
    defpred X[Element of NAT] means ((Partial_Sums(1 P_cos))*bq).$1 >= 1/2;
A6: ((Partial_Sums(1 P_cos))*bq).0=(Partial_Sums(1 P_cos)).(bq.0)
    by FUNCT_2:21
      .= (Partial_Sums(1 P_cos)).(2*0+1) by Lm6
      .=(Partial_Sums(1 P_cos)).0+(1 P_cos).(0+1) by SERIES_1:def 1
      .= (1 P_cos).0 +(1 P_cos).(0+1) by SERIES_1:def 1
      .=(-1)|^ 0 * (1)|^ (2*0)/((2*0)!)+(1 P_cos).(1) by Def25
      .= (-1)|^ 0 * (1)|^ (2*0)/((2*0)!)+
    (-1)|^ 1 * (1)|^ (2*1)/((2*1)!) by Def25
      .=1* (1)|^ (2*0)/((2*0)!)+
    (-1)|^ 1 * (1)|^ (2*1)/((2*1)!) by Lm8
      .=1/1 + (-1)|^ 1 * (1)|^ (2*1)/((2*1)!) by NEWTON:9,18
      .=1+ (-1)* (1)|^ (2*1)/((2*1)!) by NEWTON:10
      .=1+ (-1)*1/((2*1)!) by NEWTON:15
      .=1+(-1)/(1! * (1+1)) by NEWTON:21
      .=1+(-1)/(0! * (0+1) *2) by NEWTON:21
      .=1/2 by NEWTON:18;
A7: X[0] by A6;
A8: for k st X[k] holds X[k+1]
    proof
      let k;
      assume
A9:   ((Partial_Sums(1 P_cos))*bq).k >= 1/2;
A10:  ((Partial_Sums(1 P_cos))*bq).(k+1)
      =(Partial_Sums(1 P_cos)).(bq.(k+1)) by FUNCT_2:21
        .=(Partial_Sums(1 P_cos)).(2*(k+1)+1) by Lm6
        .=(Partial_Sums(1 P_cos)).(2*k+1+1)+(1 P_cos).(2*(k+1)+1) by
      SERIES_1:def 1
        .=(Partial_Sums(1 P_cos)).(2*k+1) + (1 P_cos).(2*k+1+1)+
      (1 P_cos).(2*(k+1)+1) by SERIES_1:def 1
        .= (Partial_Sums(1 P_cos)).(bq.k)+ (1 P_cos).(2*k+1+1)+
      (1 P_cos).(2*(k+1)+1) by Lm6
        .=((Partial_Sums(1 P_cos))*bq).k+ (1 P_cos).(2*k+1+1)+
      (1 P_cos).(2*(k+1)+1) by FUNCT_2:21;
      A11:  ( (Partial_Sums(1 P_cos))*bq).(k+1)-((Partial_Sums(1 P_cos))*bq).k
      =(1 P_cos).(2*k+1+1)+ (1 P_cos).(2*(k+1)+1) by A10;
A12:  (1 P_cos).(2*k+1+1)=(-1)|^ (2*(k+1)) * (1)|^ (2*(2*(k+1)))/((2*(2*
      (k+1)))!) by Def25
        .= 1 * 1|^ (2*(2*(k+1)))/((2*(2*(k+1)))!) by Lm8
        .=1/((2*(2*(k+1)))!) by NEWTON:15;
A13:  (1 P_cos).(2*(k+1)+1)
      = (-1)|^ (2*(k+1)+1) * (1)|^ (2*(2*(k+1)+1))/((2*(2*(k+1)+1))!)
      by Def25
        .=(-1) * (1)|^ (2*(2*(k+1)+1))/((2*(2*(k+1)+1))!) by Lm8
        .=(-1) * 1/((2*(2*(k+1)+1))!) by NEWTON:15
        .=(-1)/((2*(2*(k+1)+1))!);
A14:  2*(2*(k+1))<2*(2*(k+1)+1) by XREAL_1:31,70;
A15:  (2*(2*(k+1)))! <= (2*(2*(k+1)+1))! by A14,Th42;
A16:  1/((2*(2*(k+1)))!) >= 1/((2*(2*(k+1)+1))!) by A15,NEWTON:23
      ,XREAL_1:87;
A17:  1/((2*(2*(k+1)))!)-1/(2*(2*(k+1)+1)!)>=0 by A16,XREAL_1:50;
A18:  ((Partial_Sums(1 P_cos))*bq).(k+1)>=((Partial_Sums(1 P_cos))*bq).
      k by A11,A12,A13,A17,XREAL_1:51;
      thus thesis by A9,A18,XXREAL_0:2;
    end;
A19: for n holds X[n] from NAT_1:sch 1(A7,A8);
    thus thesis by A19;
  end;
A20: cos.1>=1/2 by A1,A4,A5,PREPOWER:2,SEQ_4:29;
A21: Partial_Sums(1 P_sin) is convergent by Th39;
A22: sin.1=Sum(1 P_sin) by Th40;
A23: lim ((Partial_Sums(1 P_sin))*bq )=lim(Partial_Sums(1 P_sin)) by A21,
  SEQ_4:30;
A24: lim ((Partial_Sums(1 P_sin))*bq )= sin.1 by A22,A23,SERIES_1:def 3;
A25: for n holds ((Partial_Sums(1 P_sin))*bq).n >=5/6
  proof
    let n;
    defpred X[Element of NAT] means ((Partial_Sums(1 P_sin))*bq).$1 >= 5/6;
A26: ((Partial_Sums(1 P_sin))*bq).0=(Partial_Sums(1 P_sin)).(bq.0)
    by FUNCT_2:21
      .= (Partial_Sums(1 P_sin)).(2*0+1) by Lm6
      .=(Partial_Sums(1 P_sin)).0+(1 P_sin).(0+1) by SERIES_1:def 1
      .= (1 P_sin).0 +(1 P_sin).(0+1) by SERIES_1:def 1
      .=(-1)|^ 0 * (1)|^ (2*0+1)/((2*0+1)!)+(1 P_sin).(1) by Def24
      .= (-1)|^ 0 * (1)|^ (2*0+1)/((2*0+1)!)+
    (-1)|^ 1 * (1)|^ (2*1+1)/((2*1+1)!) by Def24
      .=1* (1)|^ (2*0+1)/((2*0+1)!)+
    (-1)|^ 1 * (1)|^ (2*1+1)/((2*1+1)!) by Lm8
      .=1/((0+1)!)+(-1)|^ 1 * (1)|^ (2*1+1)/((2*1+1)!) by NEWTON:10
      .=1/(0!*1)+(-1)|^ 1 * (1)|^ (2*1+1)/((2*1+1)!) by NEWTON:21
      .=1+ (-1)* (1)|^ (2*1+1)/((2*1+1)!) by NEWTON:10,18
      .=1+ (-1)*1/((2*1+1)!) by NEWTON:15
      .=1+ (-1)/((2*1)!*(2*1+1)) by NEWTON:21
      .=1+(-1)/(1! * (1+1)*3) by NEWTON:21
      .=1+(-1)/((0+1)! *(2*3))
      .=1+(-1)/(1* 1 *6) by NEWTON:18,21
      .=5/6;
A27: X[0] by A26;
A28: for k st X[k] holds X[k+1]
    proof
      let k;
      assume
A29:  ((Partial_Sums(1 P_sin))*bq).k >= 5/6;
A30:  ((Partial_Sums(1 P_sin))*bq).(k+1)
      =(Partial_Sums(1 P_sin)).(bq.(k+1)) by FUNCT_2:21
        .=(Partial_Sums(1 P_sin)).(2*(k+1)+1) by Lm6
        .=(Partial_Sums(1 P_sin)).(2*k+1+1)+(1 P_sin).(2*(k+1)+1) by
      SERIES_1:def 1
        .=(Partial_Sums(1 P_sin)).(2*k+1) + (1 P_sin).(2*k+1+1)+
      (1 P_sin).(2*(k+1)+1) by SERIES_1:def 1
        .= (Partial_Sums(1 P_sin)).(bq.k)+ (1 P_sin).(2*k+1+1)+
      (1 P_sin).(2*(k+1)+1) by Lm6
        .=((Partial_Sums(1 P_sin))*bq).k+ (1 P_sin).(2*k+1+1)+
      (1 P_sin).(2*(k+1)+1) by FUNCT_2:21;
      A31:  ( (Partial_Sums(1 P_sin))*bq).(k+1)-((Partial_Sums(1 P_sin))*bq).k
      =(1 P_sin).(2*k+1+1)+ (1 P_sin).(2*(k+1)+1) by A30;
A32:  (1 P_sin).(2*k+1+1)=(-1)|^ (2*(k+1)) * (1)|^ (2*(2*(k+1))+1)/((2*
      (2*(k+1))+1)!) by Def24
        .=(1)* (1)|^ (2*(2*(k+1))+1)/((2*(2*(k+1))+1)!) by Lm8
        .=1/((2*(2*(k+1))+1)!) by NEWTON:15;
A33:  (1 P_sin).(2*(k+1)+1)
      = (-1)|^ (2*(k+1)+1) * (1)|^ (2*(2*(k+1)+1)+1)/((2*(2*(k+1)+1)+1)!)
      by Def24
        .=(-1) * (1)|^ (2*(2*(k+1)+1)+1)/((2*(2*(k+1)+1)+1)!) by Lm8
        .=(-1) * 1/((2*(2*(k+1)+1)+1)!) by NEWTON:15
        .=(-1)/((2*(2*(k+1)+1)+1)!);
A34:  2*(2*(k+1))<2*(2*(k+1)+1) by XREAL_1:31,70;
A35:  2*(2*(k+1))+1<2*(2*(k+1)+1) +1 by A34,XREAL_1:8;
A36:  (2*(2*(k+1))+1)! <= (2*(2*(k+1)+1)+1)! by A35,Th42;
A37:  1/((2*(2*(k+1))+1)!) >= 1/((2*(2*(k+1)+1)+1)!)
      by A36,NEWTON:23,XREAL_1:87;
A38:  1/((2*(2*(k+1))+1)!)-1/((2*(2*(k+1)+1)+1)!)>=0 by A37,XREAL_1:50;
A39:  ((Partial_Sums(1 P_sin))*bq).(k+1)>=((Partial_Sums(1 P_sin))*bq)
      .k by A31,A32,A33,A38,XREAL_1:51;
      thus thesis by A29,A39,XXREAL_0:2;
    end;
A40: for n holds X[n] from NAT_1:sch 1(A27,A28);
    thus thesis by A40;
  end;
A41: sin.1>=5/6 by A21,A24,A25,PREPOWER:2,SEQ_4:29;
A42: (cos.(1))^2+(sin.(1))^2=1 by Th31;
A43: (sin.(1))^2>=(5/6)^2 by A41,SQUARE_1:77;
A44: 1-(1-(cos.(1))^2)<=1-25/36 by A42,A43,XREAL_1:12;
A45: (cos.(1))^2<25/36 by A44,XXREAL_0:2;
A46: (sin.(1))^2> (cos.(1))^2 by A43,A45,XXREAL_0:2;
A47: sqrt(cos.(1))^2<sqrt(sin.(1))^2 by A46,SQUARE_1:95,XREAL_1:65;
A48: sqrt(cos.(1))^2 = cos.1 by A20,SQUARE_1:89;
  thus thesis by A20,A41,A47,A48,SQUARE_1:89;
end;

theorem Th48:
  for th being Real holds th rExpSeq=Re(th ExpSeq)
proof
  let th be Real;
A1: for n holds (th rExpSeq).n=Re(th ExpSeq).n
  proof
    let n;
A2: Re(th ExpSeq).n= Re (th ExpSeq.n) by COMSEQ_3:def 3
      .=Re(th |^ n /(n!c+0*<i>)) by Def8
      .=Re((th|^ n) /(n!)+0*<i>) by Th37
      .=(th|^ n) /(n!) by COMPLEX1:28
      .=(th rExpSeq).n by Def9;
    thus thesis by A2;
  end;
  thus thesis by A1,FUNCT_2:113;
end;

theorem Th49:
  for th being Real holds th rExpSeq is summable &
  Sum(th rExpSeq)=Re(Sum(th ExpSeq))
proof
  let th be Real;
  A1: Sum(th ExpSeq) =Sum(Re (th ExpSeq))+(Sum(Im (th ExpSeq)))*<i> & Sum(th
  rExpSeq)=Sum(Re (th ExpSeq)) by Th48,COMSEQ_3:54;
  thus thesis by A1,Th48,COMPLEX1:28;
end;

Lm9: for z being complex number holds
z ExpSeq.1=z & z ExpSeq.0=1r & |.z |^ n.|=|.z.| |^n
proof
  let z be complex number;
A1: z in COMPLEX by XCMPLX_0:def 2;
A2: z|^1=z by NEWTON:10;
A3: z ExpSeq.1= z/(1!c) by A2,Def8
    .=z/1 by Th37,NEWTON:19
    .=z;
A4: z ExpSeq.0=z|^ 0/ (0!c) by Def8
    .=1r by A1,Th1,COMSEQ_3:11;
A5: |.z |^ n.|=|.z.| |^ n
  proof
    defpred X[Element of NAT] means |.z |^ $1.|=|.z.| |^ $1;
A6: |.z |^ 0 .|=1 by COMPLEX1:134,COMSEQ_3:def 1;
A7: X[0] by A6,NEWTON:9;
A8: for k st X[k] holds X[k+1]
    proof
      let k such that
A9:   |.z |^ k.|=|.z.| |^ k;
A10:  |.z |^ (k+1).|=|.z*(z GeoSeq.k).| by COMSEQ_3:def 1
        .=|.z.| |^ k *|.z.| by A9,COMPLEX1:151
        .=|.z.| |^(k+1) by NEWTON:11;
      thus thesis by A10;
    end;
A11: for n holds X[n] from NAT_1:sch 1(A7,A8);
    thus thesis by A11;
  end;
  thus thesis by A3,A4,A5;
end;

Lm10: for th being Real holds Sum(th ExpSeq)=Sum(th rExpSeq)
proof
  let th be Real;
A1: for n being Nat holds Im(Partial_Sums(th ExpSeq)).n=0
  proof
    defpred X[Nat] means Im(Partial_Sums(th ExpSeq)).$1=0;
A2: Im(Partial_Sums(th ExpSeq)).0
    =Im(Partial_Sums(th ExpSeq).0) by COMSEQ_3:def 4
      .=Im((th ExpSeq).0) by COMSEQ_3:def 7
      .=0 by Lm9,COMPLEX1:15;
A3: X[0] by A2;
A4: for n being Nat st X[n] holds X[n+1]
    proof
      let n being Nat such that
A5:   Im(Partial_Sums(th ExpSeq)).n=0;
A6:   n in NAT by ORDINAL1:def 13;
A7:   (th |^(n+1))/((n+1)!c) =(th|^ (n+1)) /((n+1)!) by Th37;
A8:   Im(Partial_Sums(th ExpSeq)).(n+1)
      =Im(Partial_Sums(th ExpSeq).(n+1)) by COMSEQ_3:def 4
        .=Im(Partial_Sums(th ExpSeq).n+(th ExpSeq).(n+1))
      by A6,COMSEQ_3:def 7
        .=Im(Partial_Sums(th ExpSeq).n)+Im((th ExpSeq).(n+1))
      by COMPLEX1:19
        .=0+Im((th ExpSeq).(n+1)) by A5,A6,COMSEQ_3:def 4
        .=Im((th|^ (n+1)) /((n+1)!)+0*<i>) by A7,Def8
        .=0 by COMPLEX1:28;
      thus thesis by A8;
    end;
A9: for n being Nat holds X[n] from NAT_1:sch 2(A3,A4);
    thus thesis by A9;
  end;
A10: Im(Partial_Sums(th ExpSeq)) is constant by A1,VALUED_0:def 18;
A11: lim(Im(Partial_Sums(th ExpSeq)))
  =Im(Partial_Sums(th ExpSeq)).0 by A10,SEQ_4:41
    .=0 by A1;
A12: Im(Sum(th ExpSeq)) =0 by A11,COMSEQ_3:41;
A13: Sum(th ExpSeq) =Re(Sum(th ExpSeq))+0*<i> by A12,COMPLEX1:29
    .=Sum(th rExpSeq) by Th49;
  thus thesis by A13;
end;

theorem Th50:
  for p,q being real number holds
  Sum((p+q) rExpSeq) = (Sum(p rExpSeq))*(Sum(q rExpSeq))
proof
  let p,q be real number;
  reconsider p,q as Real by XREAL_0:def 1;
A1: Sum((p+q) rExpSeq)=Re(Sum((p+q) ExpSeq)) by Th49
    .=Re((Sum(p ExpSeq)) *(Sum(q ExpSeq))) by Lm2
    .=Re((Re(Sum(p ExpSeq))+Im(Sum(p ExpSeq))*<i>)*(Sum(q ExpSeq)))
  by COMPLEX1:29
    .= Re((Re(Sum(p ExpSeq))+Im(Sum(p ExpSeq))*<i>)*
  (Re(Sum(q ExpSeq))+Im(Sum(q ExpSeq))*<i>)) by COMPLEX1:29
    .= Re((Sum(p rExpSeq)+Im(Sum(p ExpSeq))*<i>)*
  (Re(Sum(q ExpSeq))+Im(Sum(q ExpSeq))*<i>)) by Th49
    .= Re((Sum(p rExpSeq)+0*<i>)*
  (Re(Sum(q ExpSeq))+Im(Sum(q ExpSeq))*<i>)) by Th46
    .= Re(Sum(p rExpSeq)*(Sum(q rExpSeq)+Im(Sum(q ExpSeq))*<i>)) by Th49
    .= Re(Sum(p rExpSeq)*(Sum(q rExpSeq)+0*<i>)) by Th46
    .= Re((Sum(p rExpSeq))*(Sum(q rExpSeq))+0*<i>)
    .= (Sum(p rExpSeq))*(Sum(q rExpSeq)) by COMPLEX1:28;
  thus thesis by A1;
end;

definition
  func exp_R -> Function of REAL, REAL means
  :Def26:
  for d being real number holds it.d = Sum(d rExpSeq);
  existence
  proof
    deffunc U(Element of REAL) = Sum($1 rExpSeq);
    consider f being Function of REAL, REAL such that
A1: for d be Element of REAL holds f.d = U(d) from FUNCT_2:sch 4;
    take f;
    let d be real number;
A2: d in REAL by XREAL_0:def 1;
    thus thesis by A1,A2;
  end;
  uniqueness
  proof
    let f1,f2 be Function of REAL, REAL;
    assume
A3: for d being real number holds f1.d=Sum(d rExpSeq);
    assume
A4: for d being real number holds f2.d=Sum(d rExpSeq);
A5: for d being Element of REAL holds f1.d = f2.d
    proof
      let d be Element of REAL;
      thus f1.d=Sum(d rExpSeq) by A3
        .=f2.d by A4;
    end;
    thus f1=f2 by A5,FUNCT_2:113;
  end;
end;

definition
  let th be real number;
  func exp_R th equals
  exp_R.th;
  coherence;
end;

registration
  let th be real number;
  cluster exp_R th -> real;
  coherence;
end;

definition
  let th be Real;
  redefine func exp_R th -> Real;
  coherence;
end;

theorem Th51:
  dom(exp_R)=REAL by FUNCT_2:def 1;

canceled;

theorem Th53:
  for th being Real holds exp_R.th = Re(Sum(th ExpSeq))
proof
  let th be Real;
A1: exp_R.th = Sum(th rExpSeq) by Def26;
  thus thesis by A1,Th49;
end;

theorem
  for th being Real holds exp th=exp_R th
proof
  let th be Real;
  thus exp th=Sum(th ExpSeq) by Def18
    .=Sum(th rExpSeq) by Lm10
    .=exp_R th by Def26;
end;

Lm11: for p,q being real number holds exp_R.(p+q) = exp_R.p * exp_R.q
proof
  let p,q be real number;
A1: exp_R.(p+q) = Sum((p+q) rExpSeq) by Def26
    .= (Sum(p rExpSeq))*(Sum(q rExpSeq)) by Th50
    .=exp_R.p *(Sum(q rExpSeq)) by Def26
    .=exp_R.p * exp_R.q by Def26;
  thus thesis by A1;
end;

theorem
  for p,q being real number holds exp_R(p+q) = exp_R(p) * exp_R(q) by Lm11;

Lm12: exp_R.0 =1
proof
A1: exp_R.0 = Sum(0 rExpSeq) by Def26
    .=1 by Th10,Th49,COMPLEX1:15;
  thus thesis by A1;
end;

theorem
  exp_R(0) =1 by Lm12;

theorem Th57:
  th > 0 implies exp_R.th >=1
proof
  assume
A1: th >0;
A2: for n holds Partial_Sums(th rExpSeq).n>=1
  proof
    defpred X[Element of NAT] means Partial_Sums(th rExpSeq).$1>=1;
A3: Partial_Sums(th rExpSeq).0=(th rExpSeq).0 by SERIES_1:def 1
      .=th |^ 0 /(0!) by Def9
      .=1 by NEWTON:9,18;
A4: X[0] by A3;
A5: for n st X[n] holds X[n+1]
    proof
      let n;
      assume
A6:   Partial_Sums(th rExpSeq).n>=1;
A7:   Partial_Sums(th rExpSeq).(n+1)
      =Partial_Sums(th rExpSeq).n+(th rExpSeq).(n+1) by SERIES_1:def 1
        .=Partial_Sums(th rExpSeq).n+th |^(n+1)/((n+1)!) by Def9;
A8:   th |^(n+1) >0 & (n+1)!>0 by A1,NEWTON:23,PREPOWER:13;
A9:   Partial_Sums(th rExpSeq).n+th |^(n+1)/((n+1)!)>Partial_Sums(th
      rExpSeq).n by A8,XREAL_1:31;
      thus thesis by A6,A7,A9,XXREAL_0:2;
    end;
A10: for n holds X[n] from NAT_1:sch 1(A4,A5);
    thus thesis by A10;
  end;
A11: th is Real by XREAL_0:def 1;
A12: th rExpSeq is summable by A11,Th49;
A13: Partial_Sums(th rExpSeq) is convergent by A12,SERIES_1:def 2;
A14: lim(Partial_Sums(th rExpSeq))=Sum(th rExpSeq) by SERIES_1:def 3;
A15: Sum(th rExpSeq)>=1 by A2,A13,A14,PREPOWER:2;
  thus thesis by A15,Def26;
end;

theorem Th58:
  th < 0 implies 0<exp_R.th & exp_R.th <=1
proof
  assume
A1: th <0;
A2: exp_R.(-th)>=1 by A1,Th57;
A3: exp_R.(-th)*exp_R.th=exp_R.(-th+th) by Lm11
    .=1 by Lm12;
A4: exp_R.th=1/(exp_R.(-th)) by A3,XCMPLX_1:74;
  thus 0<exp_R.th by A2,A3;
  thus thesis by A2,A4,XREAL_1:213;
end;

theorem Th59:
  exp_R.th>0
proof
A1: now per cases;
    suppose
A2:   th=0;
      thus thesis by A2,Lm12;
    end;
    suppose
A3:   th<>0;
A4:   now per cases by A3;
        suppose
A5:       th<0;
          thus thesis by A5,Th58;
        end;
        suppose
A6:       th>0;
          thus thesis by A6,Th57;
        end;
      end;
      thus thesis by A4;
    end;
  end;
  thus thesis by A1;
end;

theorem
  exp_R(th)>0 by Th59;

begin :: Differential of sin, cos, and Exponential Function

definition
  let z be complex number;
  deffunc U(Element of NAT) = (z#N($1+1))/(($1+2)!c);
  func z P_dt -> Complex_Sequence means
  :Def28:
  for n holds it.n = (z|^(n+1))/((n+2)!c);
  existence
  proof
    thus ex s being Complex_Sequence st
    for n holds s.n = U(n) from COMSEQ_1:sch 1;
  end;
  uniqueness
  proof
    thus for s1,s2 being Complex_Sequence st
    (for x being Element of NAT holds s1.x = U(x)) &
    (for x being Element of NAT holds s2.x = U(x)) holds s1 = s2
    from BINOP_2:sch 1;
  end;
  deffunc U(Element of NAT) = (z#N($1))/(($1+2)!c);
  func z P_t -> Complex_Sequence means
  for n holds it.n = (z#N(n))/((n+2)!c);
  existence
  proof
    thus ex s being Complex_Sequence st
    for n holds s.n = U(n) from COMSEQ_1:sch 1;
  end;
  uniqueness
  proof
    thus for s1,s2 being Complex_Sequence st
    (for x being Element of NAT holds s1.x = U(x)) &
    (for x being Element of NAT holds s2.x = U(x)) holds s1 = s2
    from BINOP_2:sch 1;
  end;
end;

Lm13: for p being Real, z being complex number
holds Re(p*<i>*z)=-p*Im z & Im(p*<i>*z)=p*Re z
&Re(p*z)=p*Re z & Im(p*z)=p*Im z
proof
  let p being Real, z be complex number;
A1: (p*<i>*z) =p*<i>*(Re z+Im z*<i>) by COMPLEX1:29
    .=-p* Im z+(p*Re z)*<i>;
A2: (p*z) =(p*(Re z+Im z*<i>)) by COMPLEX1:29
    .=p* Re z+(p*Im z)*<i>;
  thus thesis by A1,A2,COMPLEX1:28;
end;

Lm14: for
p being real number, z being complex number
st p>0 holds Re(z/(p*<i>))=Im z /p & Im(z/(p*<i>))=-Re z /p
&|.z/p.|=|.z.|/p
proof
  let p be real number, z being complex number such that
A1: p>0;

A2: Re(0+p*<i>)=0 & Im(0+p*<i>)=p by COMPLEX1:28;
A3: Im(p+0*<i>)=0 by COMPLEX1:28;
A4: z/(p*<i>) =
  (Re z * 0 + Im z * p) / (0^2 + p^2) +
  (0 * Im z - Re z * p) / (0^2 + p^2)*<i>
  by A2,COMPLEX1:def 14
    .=(Im z * p)/(p*p)+ ((-Re z) * p)/(p*p)*<i>
    .=(Im z * p)/(p*p)+(-Re z) /p *<i> by A1,XCMPLX_1:92
    .=Im z /p+(-Re z /p)*<i> by A1,XCMPLX_1:92;
A5: |.z/p.|=|.z.|/|.p.| by COMPLEX1:153
    .=|.z.|/sqrt((p)^2) by A3,COMPLEX1:28;
  thus thesis by A1,A4,A5,COMPLEX1:28,SQUARE_1:89;
end;

theorem Th61:
  for z being complex number holds
  z P_dt is absolutely_summable
proof
  let z be complex number;
A1: ex r st for n holds Partial_Sums(|.z P_dt.|).n<r
  proof
A2: for n holds Partial_Sums(|.z P_dt.|).n<Partial_Sums(|.z ExpSeq.|).(n+1)
    proof
      let n;
A3:   Partial_Sums(|.z P_dt.|).0=|.z P_dt.|.0 by SERIES_1:def 1
        .=|.z P_dt.0 .| by VALUED_1:18
        .=|.(z|^(0+1))/((0+2)!c).| by Def28
        .=|.z/(2!c).| by NEWTON:10
        .=|.z/2.| by Th37,NEWTON:20;
A4:   Partial_Sums(|.z P_dt.|).0=|.z.|/2 by A3,Lm14;
A5:   Partial_Sums(|.z ExpSeq.|).(0+1)
      =Partial_Sums(|.z ExpSeq.|).0+|.z ExpSeq.|.(0+1) by SERIES_1:def 1
        .=|.z ExpSeq.|.0+|.z ExpSeq.|.1 by SERIES_1:def 1
        .=|.z ExpSeq.|.0+|.z ExpSeq.1 .| by VALUED_1:18
        .=|.z ExpSeq.0 .|+|.z ExpSeq.1 .| by VALUED_1:18
        .=|.z ExpSeq.0 .|+|.z .| by Lm9
        .=1+|.z .| by Lm9,COMPLEX1:134;
A6:   Partial_Sums(|.z ExpSeq.|).(0+1)-Partial_Sums(|.z P_dt.|). 0
      =1+(|.z .| - |.z.|/2) by A4,A5;
      defpred X[Element of NAT] means
      Partial_Sums(|.z P_dt.|).$1 <Partial_Sums(|.z ExpSeq.|).($1+1);
A7:   0 <= |.z.| by COMPLEX1:132;
A8:   X[0] by A6,A7,XREAL_1:49;
A9:   for n st X[n] holds X[n+1]
      proof
        let n such that
A10:    Partial_Sums(|.z P_dt.|).n <Partial_Sums(|.z ExpSeq.|).(n+1);
A11:    Partial_Sums(|.z P_dt.|).(n+1)
        =Partial_Sums(|.z P_dt.|).n+|.z P_dt.|.(n+1) by SERIES_1:def 1
          .=Partial_Sums(|.z P_dt.|).n+|.z P_dt.(n+1).| by VALUED_1:18
.=Partial_Sums(|.z P_dt.|).n+|.(z|^((n+1)+1))/(((n+1)+2)!c).| by Def28
          .=Partial_Sums(|.z P_dt.|).n+|.(z|^(n+2))/((n+3)!).| by Th37;
A12:    (n+3)!>0 by NEWTON:23;
A13:    Partial_Sums(|.z P_dt.|).(n+1)
        =Partial_Sums(|.z P_dt.|).n+|.(z#N(n+2)).|/((n+3)!) by A11,A12,Lm14;
A14:    Partial_Sums(|.z ExpSeq.|).((n+1)+1)
        = Partial_Sums(|.z ExpSeq.|).(n+1)+|.z ExpSeq.|.(n+(1+1)) by
        SERIES_1:def 1
          .=Partial_Sums(|.z ExpSeq.|).(n+1)+|.z ExpSeq.(n+2).| by VALUED_1:18
          .=Partial_Sums(|.z ExpSeq.|).(n+1)+|.(z|^(n+2))/((n+2)!c).| by Def8
          .=Partial_Sums(|.z ExpSeq.|).(n+1)+|.(z|^(n+2))/((n+2)!).|
        by Th37;
A15:    (n+2)!>0 by NEWTON:23;
A16:    Partial_Sums(|.z ExpSeq.|).((n+1)+1)
        =Partial_Sums(|.z ExpSeq.|).(n+1)+|.(z#N(n+2)).|/((n+2)!)
        by A14,A15,Lm14;
A17:    (n+2)<(n+3) by XREAL_1:8;
A18:    (n+2)!<=(n+3)! by A17,Th42;
A19:    |.(z|^(n+2)).|>=0 by COMPLEX1:132;
A20:    |.(z|^(n+2)).|/((n+2)!)>=|.(z|^(n+2)).|/((n+3)!) by A18,A19,NEWTON:23
        ,XREAL_1:120;
A21:    Partial_Sums(|.z P_dt.|).n+|.(z|^(n+2)).|/((n+3)!)<=
        Partial_Sums(|.z P_dt.|).n+|.(z|^(n+2)).|/((n+2)!) by A20,XREAL_1:8;
A22:    Partial_Sums(|.z P_dt.|).n+|.(z|^(n+2)).|/((n+2)!)<
        Partial_Sums(|.z ExpSeq.|).(n+1)+|.(z|^(n+2)).|/((n+2)!)
        by A10,XREAL_1:8;
        thus thesis by A13,A16,A21,A22,XXREAL_0:2;
      end;
A23:  for n holds X[n] from NAT_1:sch 1(A8,A9);
      thus thesis by A23;
    end;
    consider r be real number such that
A24: for n holds Partial_Sums(|.z ExpSeq.|).n<r by SEQ_2:def 3;
A25: for n holds Partial_Sums(|.z P_dt.|).n<r
    proof
      let n;
A26:  Partial_Sums(|.z P_dt.|).n<Partial_Sums(|.z ExpSeq.|). (n+1) by A2;
      thus thesis by A24,A26,XXREAL_0:2;
    end;
    take r;
    thus thesis by A25,XREAL_0:def 1;
  end;
A27: Partial_Sums(|.z P_dt.|) is bounded_above by A1,SEQ_2:def 3;
  thus thesis by A27,COMSEQ_3:62;
end;

theorem Th62:
  for z being complex number holds z*(Sum(z P_dt))=(Sum(z ExpSeq))-1r-z
proof
  let z be complex number;
A1: for z being complex number holds z (#) (z P_dt)=(z ExpSeq)^\ 2
  proof
    let z be complex number;
A2: for n holds (z(#)(z P_dt)).n=((z ExpSeq)^\ 2).n
    proof
      let n;
A3:   (z(#)(z P_dt)).n=z*(z P_dt).(n) by VALUED_1:6
        .=z*((z|^(n+1))/((n+2)!c)) by Def28;
A4:   (z(#)(z P_dt)).n= (z*(z|^(n+1)))/((n+2)!c) by A3
        .=(z|^(n+1+1))/((n+2)!c) by NEWTON:11
        .=z ExpSeq.(n+2) by Def8
        .=((z ExpSeq)^\ 2).n by NAT_1:def 3;
      thus thesis by A4;
    end;
    thus thesis by A2,FUNCT_2:113;
  end;
  A5: Sum
(z ExpSeq)= Partial_Sums(z ExpSeq).1 + Sum(z ExpSeq^\(1+1)) by COMSEQ_3:61
    .=Partial_Sums(z ExpSeq).1 + Sum(z ExpSeq^\(2));
A6: Sum(z (#) (z P_dt))=Sum(z ExpSeq) - Partial_Sums(z ExpSeq).(0+1) by A1,A5
    .=Sum(z ExpSeq)-(Partial_Sums(z ExpSeq).0+z ExpSeq.1) by COMSEQ_3:def 7
    .=Sum(z ExpSeq)-(z ExpSeq.0+z ExpSeq.1) by COMSEQ_3:def 7
    .=Sum(z ExpSeq)-(1r+z ExpSeq.1) by Lm9
    .=Sum(z ExpSeq)-(1r+z) by Lm9
    .=(Sum(z ExpSeq))-1r-z;
  reconsider BBB=z P_dt as absolutely_summable Complex_Sequence by Th61;
A7: BBB is summable;
  thus thesis by A6,A7,COMSEQ_3:57;
end;

theorem Th63:
  for p st p>0 holds ex q st q>0 &
  for z being complex number st |.z.|<q holds |.(Sum(z P_dt)).|<p
proof
A1: for z holds |.(Sum(z P_dt)).| <= Sum (|.z P_dt.|)
  proof
    let z;
A2: for k holds |.Partial_Sums(z P_dt).|.k <= Partial_Sums(|.z P_dt.|).k
    proof
      let k;
      A3:   |.
Partial_Sums(z P_dt).k.|=|.Partial_Sums(z P_dt).|.k by VALUED_1:18;
      thus thesis by A3,COMSEQ_3:30;
    end;
A4: z P_dt is absolutely_summable by Th61;
A5: z P_dt is summable by Th61,COMSEQ_3:64;
    reconsider PS = Partial_Sums(z P_dt) as convergent Complex_Sequence by A5;
A6: lim|.Partial_Sums(z P_dt).|=|.lim(Partial_Sums(z P_dt)).|
    by A5,COMSEQ_2:11;
A7: lim|.Partial_Sums(z P_dt).|<=lim(Partial_Sums(|.z P_dt.|)) by A2,A4,
    SEQ_2:32;
    thus thesis by A6,A7,SERIES_1:def 3;
  end;
A8: for z,n holds |.z P_dt.|.n<=|.z.|*|.z.| GeoSeq.n
  proof
    let z,n;
A9: |.z P_dt.|.n=|.z P_dt.n.| by VALUED_1:18
      .=|.(z|^(n+1))/((n+2)!c).| by Def28
      .=|.(z|^(n+1))/((n+2)!).| by Th37;
A10: (n+2)!>0 by NEWTON:23;
A11: |.z P_dt.|.n=|.(z#N(n+1)).|/((n+2)!) by A9,A10,Lm14
      .=|.z.| |^(n+1)/((n+2)!) by Lm9;
A12: |.z.|*|.z.| GeoSeq.n=|.z.|* (|.z.| |^ n) by PREPOWER:def 1
      .=|.z.| |^(n+1) by NEWTON:11;
    A13: (n+2)!>=1 & |.z.| |^(n+1) >=0 by Th42,COMPLEX1:132,NEWTON:18,POWER:3;
A14: |.z.| |^(n+1)/1>=|.z.| |^(n+1)/((n+2)!) by A13,XREAL_1:120;
    thus thesis by A11,A12,A14;
  end;
  let p0 be real number;
  assume
A15: p0>0;
  reconsider p = p0 as Real by XREAL_0:def 1;
  consider q such that
A16: q=p/(p+1);
A17: p+1>p by XREAL_1:31;
A18: q <1 by A15,A16,A17,XREAL_1:191;
A19: for z st |.z.|<q holds |.(Sum(z P_dt)).|<p
  proof
    let z;
    assume
A20: |.z.|<q;
A21: |.z.|<1 by A18,A20,XXREAL_0:2;
A22: abs(|.z.|)<1 by A18,A20,XXREAL_0:2;
A23: |.z.| GeoSeq is summable by A22,SERIES_1:28;
A24: Sum(|.z.| GeoSeq) = 1/(1- |.z.|) by A22,SERIES_1:28;
A25: |.z.|(#)|.z.| GeoSeq is summable by A23,SERIES_1:13;
A26: for n holds |.z P_dt.|.n<=(|.z.|(#)|.z.| GeoSeq).n
    proof
      let n;
A27:  |.z P_dt.|.n<=|.z.|*|.z.| GeoSeq.n by A8;
      thus thesis by A27,SEQ_1:13;
    end;
A28: for n holds 0<=|.z P_dt.|.n
    proof
      let n;
A29:  |.z P_dt.|.n=|.z P_dt.n.| by VALUED_1:18;
      thus thesis by A29,COMPLEX1:132;
    end;
A30: Sum(|.z P_dt.|)<=Sum(|.z.|(#)|.z.| GeoSeq) by A25,A26,A28,SERIES_1:24;
A31: Sum(|.z.|(#)|.z.| GeoSeq) =|.z.|/(1- |.z.|) by A23,A24,SERIES_1:13;
A32: |.z.|*(p+1)<(p/(p+1))*(p+1) by A15,A16,A20,XREAL_1:70;
A33: (p/(p+1))*(p+1)=p by A15,XCMPLX_1:88;
A34: p*|.z.|+|.z.|-p*|.z.|<p-p*|.z.| by A32,A33,XREAL_1:11;
A35: 1- |.z.|>0 by A21,XREAL_1:52;
A36: |.z.|/(1- |.z.|)< p*(1- |.z.|)/(1- |.z.|) by A34,A35,XREAL_1:76;
A37: |.z.|/(1- |.z.|)<p by A35,A36,XCMPLX_1:90;
A38: Sum(|.z P_dt.|)<p by A30,A31,A37,XXREAL_0:2;
A39: |.(Sum(z P_dt)).|<=Sum (|.z P_dt.|) by A1;
    thus thesis by A38,A39,XXREAL_0:2;
  end;
  take q;
  thus q>0 by A15,A16;
  let z being complex number;
A40: z in COMPLEX by XCMPLX_0:def 2;
  thus thesis by A19,A40;
end;

theorem Th64:
  for z,z1 being complex number holds
  (Sum((z1+z) ExpSeq))-(Sum(z1 ExpSeq))
  =(Sum(z1 ExpSeq))*z+z*(Sum(z P_dt))*(Sum(z1 ExpSeq))
proof
  let z,z1 be complex number;
A1: (Sum((z1+z) ExpSeq))-(Sum(z1 ExpSeq))
  =(Sum(z1 ExpSeq))*(Sum(z ExpSeq))-(Sum(z1 ExpSeq))*1r by Lm2
    .=(Sum(z1 ExpSeq))*(((Sum(z ExpSeq))-1r)-z+z)
    .=(Sum(z1 ExpSeq))* (z*(Sum(z P_dt))+z) by Th62
    .=(Sum(z1 ExpSeq))*z +z*(Sum(z P_dt))*(Sum(z1 ExpSeq));
  thus thesis by A1;
end;

theorem Th65:
  for p,q being Real holds cos.(p+q)-cos.p=
  -q*sin.p -q*Im((Sum(q*<i> P_dt))* (cos.p+(sin.p)*<i>))
proof
  let p,q be Real;
A1: cos.(p+q)-cos.p=cos.(p+q)-Re(Sum(p*<i> ExpSeq)) by Def22
    .=Re(Sum((p+q)*<i> ExpSeq))-Re(Sum(p*<i> ExpSeq)) by Def22
    .=Re(Sum((p*<i>+q*<i>) ExpSeq)-Sum(p*<i> ExpSeq)) by COMPLEX1:48
    .=Re((Sum(p*<i> ExpSeq))*(q*<i>)+q*<i>*(Sum((q*<i>) P_dt))*
  (Sum(p*<i> ExpSeq)))
  by Th64
    .=Re((cos.p+(sin.p)*<i>)*(q*<i>)
  +q*<i>*(Sum(q*<i> P_dt))*(Sum(p*<i> ExpSeq))) by Lm3
    .= Re((cos.p+(sin.p)*<i>)*(q*<i>)
  +q*<i>*(Sum(q*<i> P_dt))* (cos.p+(sin.p)*<i>)) by Lm3
    .=Re(-q*sin.p+q*cos.p*<i>)+Re(q*<i>*(Sum(q*<i> P_dt))*
  (cos.p+(sin.p)*<i>)) by COMPLEX1:19
    .=-q*sin.p+ Re(q*<i>*((Sum(q*<i> P_dt))* (cos.p+(sin.p)*<i>)))
  by COMPLEX1:28
    .=-q*sin.p -q*Im((Sum(q*<i> P_dt))* (cos.p+(sin.p)*<i>)) by Lm13;
  thus thesis by A1;
end;

theorem Th66:
  for p,q being Real holds sin.(p+q)-sin.p=
  q*cos.p+q*Re((Sum((q*<i>) P_dt))* (cos.p+sin.p*<i>))
proof
  let p,q be Real;
A1: sin.(p+q)-sin.p=sin.(p+q)-Im(Sum((p*<i>) ExpSeq)) by Def20
    .=Im(Sum(((p+q)*<i>) ExpSeq))-Im(Sum((p*<i>) ExpSeq)) by Def20
    .=Im(Sum(((p*<i>)+(q*<i>)) ExpSeq)-Sum((p*<i>) ExpSeq)) by COMPLEX1:48
    .=Im((Sum((p*<i>) ExpSeq))*(q*<i>)
  +(q*<i>)*(Sum((q*<i>) P_dt))*(Sum((p*<i>) ExpSeq))) by Th64
    .=Im((cos.p+(sin.p)*<i>)*(q*<i>)
  +(q*<i>)*(Sum((q*<i>) P_dt))*(Sum((p*<i>) ExpSeq))) by Lm3
    .= Im((cos.p+sin.p*<i>)*(q*<i>)
  +(q*<i>)*(Sum((q*<i>) P_dt))* (cos.p+(sin.p)*<i>)) by Lm3
    .=Im(-q*sin.p+(q*cos.p)*<i>)+Im((q*<i>)*(Sum((q*<i>) P_dt))
  * (cos.p+sin.p*<i>)) by COMPLEX1:19
    .=q*cos.p+Im((q*<i>)*((Sum((q*<i>) P_dt))* (cos.p+sin.p*<i>)))
  by COMPLEX1:28
    .=q*cos.p+q*Re((Sum((q*<i>) P_dt))* (cos.p+sin.p*<i>)) by Lm13;
  thus thesis by A1;
end;

theorem Th67:
  for p,q being Real holds exp_R.(p+q)-exp_R.p=
  q * (exp_R.p)+q*exp_R.p*Re((Sum(q P_dt)))
proof
  let p,q be Real;
A1: exp_R.(p+q)-exp_R.p=exp_R.(p+q)-Re(Sum(p ExpSeq)) by Th53
    .=Re(Sum((p+q) ExpSeq))-Re(Sum(p ExpSeq)) by Th53
    .=Re(Sum((p+q) ExpSeq)-Sum(p ExpSeq)) by COMPLEX1:48
    .=Re((Sum(p ExpSeq))*q
  +q*(Sum(q P_dt))*(Sum(p ExpSeq))) by Th64
    .=Re((Sum(p ExpSeq))*q)+
  Re(q*(Sum(q P_dt))*(Sum(p ExpSeq))) by COMPLEX1:19
    .=q*Re(Sum(p ExpSeq))+
  Re(q*(Sum(q P_dt))*(Sum(p ExpSeq))) by Lm13
    .=q* (exp_R.p)+ Re(q*((Sum(q P_dt))*(Sum(p ExpSeq))))
  by Th53
    .=q* (exp_R.p)+ q*Re((Sum(q P_dt))*(Sum(p ExpSeq))) by Lm13
    .=q* (exp_R.p)+ q*Re((Sum(q P_dt))*Sum(p rExpSeq)) by Lm10
    .=q* (exp_R.p)+q*Re((Sum(q P_dt))*(exp_R.p+0*<i>)) by Def26
    .=q* (exp_R.p)+q*(exp_R.p*Re((Sum(q P_dt)))) by Lm13
    .=q* (exp_R.p)+q*exp_R.p*Re((Sum(q P_dt)));
  thus thesis by A1;
end;

theorem Th68:
  cos is_differentiable_in p & diff(cos,p)=-sin.p
proof
  reconsider p as Real by XREAL_0:def 1;
  deffunc U(Element of REAL) =
  - $1* Im((Sum($1*<i> P_dt))* (cos.p+sin.p*<i>));
  consider Cr being Function of REAL, REAL such that
A1: for th be Real holds Cr.th = U(th) from FUNCT_2:sch 4;
A2: for hy1 holds
  (hy1")(#)(Cr/*hy1) is convergent & lim ((hy1")(#)(Cr/*hy1)) = 0
  proof
    let hy1;
A3: for n holds ((hy1")(#)(Cr/*hy1)).n
    =-Im((Sum((hy1.n)*<i> P_dt))* (cos.p+sin.p*<i>))
    proof
      let n;
A4:   ((hy1")(#)(Cr/*hy1)).n=(hy1".n)*((Cr/*hy1).n) by SEQ_1:12
        .=(hy1.n)"*((Cr/*hy1).n) by VALUED_1:10;
A5:   dom Cr = REAL by FUNCT_2:def 1;
A6:   rng hy1 c= dom Cr by A5;
A7:   ((hy1")(#)(Cr/*hy1)).n= (hy1.n)"*(Cr.(hy1.n)) by A4,A6,
      FUNCT_2:185
        .=(hy1.n)"*( -(hy1.n)* Im((Sum((hy1.n)*<i> P_dt))*
      (cos.p+sin.p*<i>))) by A1
        .=-(hy1.n)"*(hy1.n)* Im((Sum((hy1.n)*<i> P_dt))*(cos.p+sin.p*<i>));
A8:   hy1 is non-zero by FDIFF_1:def 1;
A9:   hy1.n<>0 by A8,SEQ_1:7;
A10:  ((hy1")(#)(Cr/*hy1)).n
      =-1*Im((Sum((hy1.n)*<i> P_dt))* (cos.p+sin.p*<i>))
      by A7,A9,XCMPLX_0:def 7
        .=-Im((Sum((hy1.n)*<i> P_dt))* (cos.p+sin.p*<i>));
      thus thesis by A10;
    end;
    deffunc U(Real) = -Im((Sum((hy1.$1)*<i> P_dt))*(cos.p+(sin.p)*<i>));
    consider rseq such that
A11: for n holds rseq.n= U(n) from SEQ_1:sch 1;
    deffunc U(Element of NAT) =
    (Sum((hy1.$1)*<i> P_dt))*(cos.p+(sin.p)*<i>);
    consider cq1 such that
A12: for n holds cq1.n=U(n) from COMSEQ_1:sch 1;
A13: for q be Real st q>0 holds ex k st for m st k<=m holds |.cq1.m-0c.|<q
    proof
      let q be Real such that
A14:  q>0;
A15:  ex k st for m st k<=m holds |.cq1.m-0c.|<q
      proof
        consider r such that
A16:    r>0 and
A17:    for z being complex number st |.z.|<r holds |.(Sum (z P_dt)).|<q
        by A14,Th63;
A18:    hy1 is convergent & lim(hy1)=0 by FDIFF_1:def 1;
        consider k such that
A19:    for m st k<=m holds abs(hy1.m-0)<r by A16,A18,SEQ_2:def 7;
A20:    now
          let m such that
A21:      k<=m;
A22:      |.cq1.m-0c.|= |.(Sum((hy1.m)*<i> P_dt))* (cos.p+(sin.p)*<i>)
          .| by A12
.= |.(Sum((hy1.m)*<i> P_dt)).|* |.cos.p+(sin.p)*<i>.| by COMPLEX1:151
            .= |.(Sum((hy1.m)*<i> P_dt)).|* |.Sum(p*<i> ExpSeq ).| by Lm3
            .=|.(Sum((hy1.m)*<i> P_dt)).|*1 by Lm5
            .=|.(Sum((hy1.m)*<i> P_dt)).|;
A23:      abs(hy1.m-0)<r by A19,A21;
A24:      (hy1.m)*<i> = 0+(hy1.m)*<i>;
          A25:      Re
((hy1.m)*<i>)=0 & Im((hy1.m)*<i>)=hy1.m by A24,COMPLEX1:28;
A26:      |.(hy1.m)*<i>.| =abs(hy1.m) by A25,COMPLEX1:158;
          thus |.cq1.m-0c.|<q by A17,A22,A23,A26;
        end;
        take k;
        thus thesis by A20;
      end;
      thus thesis by A15;
    end;
A27: cq1 is convergent by A13,COMSEQ_2:def 4;
A28: lim(cq1)=0c by A13,A27,COMSEQ_2:def 5;
A29: lim(-cq1)=-0c by A27,A28,COMSEQ_2:22;
A30: for n holds Im(-cq1).n= rseq.n
    proof
A31:  for n holds Im(-cq1).n=-Im((Sum((hy1.n)*<i> P_dt))*
      (cos.p+(sin.p)*<i>))
      proof
        let n;
A32:    Im(-cq1).n=Im((-cq1).n) by COMSEQ_3:def 4
          .=Im(-cq1.n) by VALUED_1:8
          .=Im(-(Sum((hy1.n)*<i> P_dt))* (cos.p+(sin.p)*<i>)) by A12
          .=-Im((Sum((hy1.n)*<i> P_dt))* (cos.p+(sin.p)*<i>))
        by COMPLEX1:34;
        thus thesis by A32;
      end;
      let n;
A33:  rseq.n=-Im((Sum((hy1.n)*<i> P_dt))* (cos.p+(sin.p)*<i>)) by A11;
      thus thesis by A31,A33;
    end;
A34: for n holds rseq.n=((hy1")(#)(Cr/*hy1)).n
    proof
      let n;
A35:  rseq.n=-Im((Sum((hy1.n)*<i> P_dt))* (cos.p+(sin.p)*<i>)) by A11;
      thus thesis by A3,A35;
    end;
A36: rseq=(hy1")(#)(Cr/*hy1) by A34,FUNCT_2:113;
A37: (hy1")(#)(Cr/*hy1)=Im(-cq1) by A30,A36,FUNCT_2:113;
    thus thesis by A27,A29,A37,COMPLEX1:12,COMSEQ_3:41;
  end;
  reconsider PR = Cr as REST by A2,FDIFF_1:def 3;
  deffunc U(Element of REAL) = -$1*sin.p;
  consider CL being Function of REAL, REAL such that
A38: for th be Real holds CL.th= U(th) from FUNCT_2:sch 4;
A39: ex r be Real st for q be Real holds CL.q=r*q
  proof
A40: for q be Real holds CL.q=(-sin.p)*q
    proof
      let q be Real;
A41:  CL.q= -q*sin.p by A38
        .=(-sin.p)*q;
      thus thesis by A41;
    end;
    take (-sin.p);
    thus thesis by A40;
  end;
  reconsider PL = CL as LINEAR by A39,FDIFF_1:def 4;
A42: ex N being Neighbourhood of p st N c= dom cos & for r st r in N holds
  cos.r - cos.p = PL.(r-p) + PR.(r-p)
  proof
A43: for r st r in ].p-1,p+1 .[ holds cos.r - cos.p = PL.(r-p) + PR.(r-p)
    proof
      let r;
A44:  r=p+(r-p);
A45:  cos.r - cos.p
      =-(r-p)*sin.p -(r-p)*Im((Sum((r-p)*<i> P_dt))* (cos.p+sin.p*<i>))
      by A44,Th65
        .=-(r-p)*sin.p+Cr.(r-p) by A1
        .=PL.(r-p) + PR.(r-p) by A38;
      thus thesis by A45;
    end;
    take ].p-1,p+1 .[;
    thus thesis by A43,Th27,RCOMP_1:def 7;
  end;
A46: cos is_differentiable_in p by A42,FDIFF_1:def 5;
A47: PL.1= -1*sin.p by A38
    .=-sin.p;
  thus thesis by A42,A46,A47,FDIFF_1:def 6;
end;

theorem Th69:
  sin is_differentiable_in p & diff(sin,p)=cos.p
proof
  reconsider p as Real by XREAL_0:def 1;
  deffunc U(Element of REAL)
  = $1 *Re((Sum($1*<i> P_dt))* (cos.p+(sin.p)*<i>));
  consider Cr being Function of REAL, REAL such that
A1: for th be Real holds Cr.th = U(th) from FUNCT_2:sch 4;
A2: for hy1 holds
  (hy1")(#)(Cr/*hy1) is convergent & lim ((hy1")(#)(Cr/*hy1)) = 0
  proof
    let hy1;
A3: for n holds ((hy1")(#)(Cr/*hy1)).n
    =Re((Sum(hy1.n*<i> P_dt))* (cos.p+(sin.p)*<i>))
    proof
      let n;
A4:   ((hy1")(#)(Cr/*hy1)).n=(hy1".n)*((Cr/*hy1).n) by SEQ_1:12
        .=(hy1.n)"*((Cr/*hy1).n) by VALUED_1:10;
A5:   dom Cr = REAL by FUNCT_2:def 1;
A6:   rng hy1 c= dom Cr by A5;
A7:   ((hy1")(#)(Cr/*hy1)).n= (hy1.n)"*(Cr.(hy1.n)) by A4,A6,
      FUNCT_2:185
        .=(hy1.n)"*( (hy1.n)*Re((Sum(hy1.n*<i> P_dt))*
      (cos.p+(sin.p)*<i>))) by A1
        .=(hy1.n)"*(hy1.n)* Re((Sum(hy1.n*<i> P_dt))* (cos.p+(sin.p)*<i>));
A8:   hy1 is non-zero by FDIFF_1:def 1;
A9:   hy1.n<>0 by A8,SEQ_1:7;
A10:  ((hy1")(#)(Cr/*hy1)).n
      =1*Re((Sum(hy1.n*<i> P_dt))* (cos.p+(sin.p)*<i>))
      by A7,A9,XCMPLX_0:def 7
        .=Re((Sum(hy1.n*<i> P_dt))* (cos.p+(sin.p)*<i>));
      thus thesis by A10;
    end;
    deffunc U(Real) = Re((Sum(hy1.$1*<i> P_dt))* (cos.p+(sin.p)*<i>));
    consider rseq such that
A11: for n holds rseq.n= U(n) from SEQ_1:sch 1;
    deffunc U(Element of NAT)
    = (Sum((hy1.$1)*<i> P_dt))* (cos.p+(sin.p)*<i>);
    consider cq1 such that
A12: for n holds cq1.n= U(n) from COMSEQ_1:sch 1;
A13: for q be Real st q>0 holds ex k st for m st k<=m holds |.cq1.m-0c.|<q
    proof
      let q be Real such that
A14:  q>0;
A15:  ex k st for m st k<=m holds |.cq1.m-0c.|<q
      proof
        consider r such that
A16:    r>0 and
A17:    for z being complex number st |.z.|<r holds |.(Sum (z P_dt)).|<q
        by A14,Th63;
A18:    hy1 is convergent & lim(hy1)=0 by FDIFF_1:def 1;
        consider k such that
A19:    for m st k<=m holds abs(hy1.m-0)<r by A16,A18,SEQ_2:def 7;
A20:    now
          let m such that
A21:      k<=m;
A22:      |.
          cq1.m-0c.|= |.(Sum((hy1.m)*<i> P_dt))* (cos.p+(sin.p)*<i>).|
          by A12
            .= |.(Sum((hy1.m)*<i> P_dt)).|* |.cos.p+(sin.p)*<i>.|
          by COMPLEX1:151
            .= |.(Sum((hy1.m)*<i> P_dt)).|* |.Sum(p*<i> ExpSeq ).|
          by Lm3
            .=|.(Sum((hy1.m)*<i> P_dt)).|*1 by Lm5
            .=|.(Sum((hy1.m)*<i> P_dt)).|;
A23:      abs(hy1.m-0)<r by A19,A21;
A24:      (hy1.m)*<i> = 0+(hy1.m)*<i>;
          A25:      Re
((hy1.m)*<i>)=0 & Im((hy1.m)*<i>)=hy1.m by A24,COMPLEX1:28;
A26:      |.(hy1.m)*<i>.| =abs(hy1.m) by A25,COMPLEX1:158;
          thus |.cq1.m-0c.|<q by A17,A22,A23,A26;
        end;
        take k;
        thus thesis by A20;
      end;
      thus thesis by A15;
    end;
A27: cq1 is convergent by A13,COMSEQ_2:def 4;
A28: lim(cq1)=0c by A13,A27,COMSEQ_2:def 5;
A29: for n holds Re(cq1).n= rseq.n
    proof
      let n;
A30:  Re(cq1).n=Re((cq1).n) by COMSEQ_3:def 3
        .=Re((Sum((hy1.n)*<i> P_dt))* (cos.p+(sin.p)*<i>)) by A12;
      thus thesis by A11,A30;
    end;
A31: for n holds rseq.n=((hy1")(#)(Cr/*hy1)).n
    proof
      let n;
A32:  rseq.n=Re((Sum((hy1.n)*<i> P_dt))* (cos.p+(sin.p)*<i>)) by A11;
      thus thesis by A3,A32;
    end;
A33: rseq=(hy1")(#)(Cr/*hy1) by A31,FUNCT_2:113;
A34: (hy1")(#)(Cr/*hy1)=Re(cq1) by A29,A33,FUNCT_2:113;
    thus thesis by A27,A28,A34,COMPLEX1:12,COMSEQ_3:41;
  end;
  reconsider PR = Cr as REST by A2,FDIFF_1:def 3;
  deffunc U(Element of REAL) = $1*cos.p;
  consider CL being Function of REAL, REAL such that
A35: for th be Real holds CL.th=U(th) from FUNCT_2:sch 4;
A36: for d be real number holds CL.d = d*cos.p
  proof
    let d be real number;
A37: d is Real by XREAL_0:def 1;
    thus thesis by A35,A37;
  end;
A38: ex r be Real st for q be Real holds CL.q=r*q
  proof
    take (cos.p);
    thus thesis by A36;
  end;
  reconsider PL = CL as LINEAR by A38,FDIFF_1:def 4;
A39: ex N being Neighbourhood of p st N c= dom sin & for r st r in N holds
  sin.r - sin.p = PL.(r-p) + PR.(r-p)
  proof
A40: for r st r in ].p-1,p+1 .[ holds sin.r - sin.p = PL.(r-p) + PR.(r-p)
    proof
      let r;
A41:  r=p+(r-p);
A42:  sin.r - sin.p
      =(r-p)*cos.p +(r-p)*Re((Sum((r-p)*<i> P_dt))* (cos.p+sin.p*<i>))
      by A41,Th66
        .=(r-p)*cos.p +Cr.(r-p) by A1
        .=PL.(r-p) + PR.(r-p) by A36;
      thus thesis by A42;
    end;
    take ].p-1,p+1 .[;
    thus thesis by A40,Th27,RCOMP_1:def 7;
  end;
A43: sin is_differentiable_in p by A39,FDIFF_1:def 5;
A44: PL.1= 1*cos.p by A36;
  thus thesis by A39,A43,A44,FDIFF_1:def 6;
end;

theorem Th70:
  exp_R is_differentiable_in p & diff(exp_R,p)=exp_R.p
proof
  deffunc U(Element of REAL) = $1 * exp_R.p*Re((Sum($1 P_dt)));
  consider Cr being Function of REAL, REAL such that
A1: for th be Real holds Cr.th = U(th) from FUNCT_2:sch 4;
A2: for hy1 holds
  (hy1")(#)(Cr/*hy1) is convergent & lim ((hy1")(#)(Cr/*hy1)) = 0
  proof
    let hy1;
A3: for n holds ((hy1")(#)(Cr/*hy1)).n =exp_R.p*Re((Sum((hy1.n) P_dt)))
    proof
      let n;
A4:   ((hy1")(#)(Cr/*hy1)).n=(hy1".n)*((Cr/*hy1).n) by SEQ_1:12
        .=(hy1.n)"*((Cr/*hy1).n) by VALUED_1:10;
A5:   dom Cr = REAL by FUNCT_2:def 1;
A6:   rng hy1 c= dom Cr by A5;
A7:   ((hy1")(#)(Cr/*hy1)).n= (hy1.n)"*(Cr.(hy1.n)) by A4,A6,
      FUNCT_2:185
        .=(hy1.n)"*((hy1.n)*exp_R.p*Re((Sum((hy1.n) P_dt)))) by A1
        .=(hy1.n)"*(hy1.n)* (exp_R.p*Re((Sum((hy1.n) P_dt))));
A8:   hy1 is non-zero by FDIFF_1:def 1;
A9:   hy1.n<>0 by A8,SEQ_1:7;
A10:  ((hy1")(#)(Cr/*hy1)).n
      =1*(exp_R.p*Re((Sum((hy1.n) P_dt)))) by A7,A9,XCMPLX_0:def 7
        .=exp_R.p*Re((Sum((hy1.n) P_dt)));
      thus thesis by A10;
    end;
    deffunc U(Real) = exp_R.p*Re((Sum((hy1.$1) P_dt)));
    consider rseq such that
A11: for n holds rseq.n= U(n) from SEQ_1:sch 1;
    deffunc U(Element of NAT) = Sum((hy1.$1) P_dt)*exp_R.p;
    consider cq1 such that
A12: for n holds cq1.n= U(n) from COMSEQ_1:sch 1;
A13: for q be Real st q>0 holds ex k st for m st k<=m holds |.cq1.m-0c.|<q
    proof
      let q be Real such that
A14:  q>0;
A15:  ex k st for m st k<=m holds |.cq1.m-0c.|<q
      proof
A16:    exp_R.p>0 by Th59;
        consider r such that
A17:    r>0 and
A18:    for z being complex number st |.z.|<r holds |.(Sum(z P_dt)).|<q/
        exp_R.p
        by A14,A16,Th63;
A19:    hy1 is convergent & lim(hy1)=0 by FDIFF_1:def 1;
        consider k such that
A20:    for m st k<=m holds abs(hy1.m-0)<r by A17,A19,SEQ_2:def 7;
A21:    now
          let m such that
A22:      k<=m;
A23:      |.cq1.m-0c.|= |.(Sum((hy1.m) P_dt))* exp_R.p.|
          by A12
            .= |.(Sum((hy1.m) P_dt)).|* |.exp_R.p.|
          by COMPLEX1:151
            .= |.(Sum((hy1.m) P_dt)).|*
          sqrt((Re(exp_R.p))^2 + (Im(exp_R.p))^2);
A24:      exp_R.p = exp_R.p+0*<i>;
          A25:      Re(exp_R.p)=exp_R.p & Im(exp_R.p)=0 by A24,COMPLEX1:28;
A26:      exp_R.p>0 by Th59;
A27:      |.cq1.m-0c.| =|.(Sum((hy1.m) P_dt)).|* ( exp_R.p)
          by A23,A25,A26,SQUARE_1:89;
A28:      abs(hy1.m-0)<r by A20,A22;
A29:      |.cq1.m-0c.|<q/exp_R.p * exp_R.p by A18,A26,A27,A28,
          XREAL_1:70;
          thus |.cq1.m-0c.|<q by A26,A29,XCMPLX_1:88;
        end;
        take k;
        thus thesis by A21;
      end;
      thus thesis by A15;
    end;
A30: cq1 is convergent by A13,COMSEQ_2:def 4;
A31: lim(cq1)=0c by A13,A30,COMSEQ_2:def 5;
A32: for n holds Re(cq1).n= rseq.n
    proof
A33:  for n holds Re(cq1).n=exp_R.p*Re((Sum((hy1.n) P_dt)))
      proof
        let n;
A34:    Re(cq1).n=Re(cq1.n) by COMSEQ_3:def 3
          .=Re((Sum((hy1.n) P_dt))*exp_R.p) by A12
          .=exp_R.p*Re((Sum((hy1.n) P_dt))) by Lm13;
        thus thesis by A34;
      end;
      let n;
A35:  rseq.n=exp_R.p*Re((Sum((hy1.n) P_dt))) by A11;
      thus thesis by A33,A35;
    end;
A36: for n holds rseq.n=((hy1")(#)(Cr/*hy1)).n
    proof
      let n;
A37:  rseq.n=exp_R.p*Re((Sum((hy1.n) P_dt))) by A11;
      thus thesis by A3,A37;
    end;
A38: rseq=(hy1")(#)(Cr/*hy1) by A36,FUNCT_2:113;
A39: (hy1")(#)(Cr/*hy1)=Re(cq1) by A32,A38,FUNCT_2:113;
    thus thesis by A30,A31,A39,COMPLEX1:12,COMSEQ_3:41;
  end;
  reconsider PR = Cr as REST by A2,FDIFF_1:def 3;
  deffunc U(Element of REAL) = $1* (exp_R.p);
  consider CL being Function of REAL, REAL such that
A40: for th be Real holds CL.th=U(th) from FUNCT_2:sch 4;
A41: for d be real number holds CL.d = d * (exp_R.p)
  proof
    let d be real number;
A42: d is Real by XREAL_0:def 1;
    thus thesis by A40,A42;
  end;
A43: ex r be Real st for q be Real holds CL.q=r*q
  proof
    take (exp_R.p);
    thus thesis by A41;
  end;
  reconsider PL = CL as LINEAR by A43,FDIFF_1:def 4;
A44: ex N being Neighbourhood of p st N c= dom exp_R &
  for r st r in N holds exp_R.r - exp_R.p = PL.(r-p) + PR.(r-p)
  proof
A45: for r st r in ].p-1,p+1 .[ holds exp_R.r - exp_R.p = PL.(r-p) + PR.(r-p)
    proof
      let r;
      reconsider p as Real by XREAL_0:def 1;
A46:  r=p+(r-p);
A47:  exp_R.r - exp_R.p =(r-p)* (exp_R.p)+(r-p)*exp_R.p*
      Re((Sum((r-p) P_dt))) by A46,Th67
        .=(r-p)* (exp_R.p)+Cr.(r-p) by A1
        .=PL.(r-p) + PR.(r-p) by A41;
      thus thesis by A47;
    end;
    take ].p-1,p+1 .[;
    thus thesis by A45,Th51,RCOMP_1:def 7;
  end;
A48: exp_R is_differentiable_in p by A44,FDIFF_1:def 5;
A49: PL.1= 1*exp_R.p by A41;
  thus thesis by A44,A48,A49,FDIFF_1:def 6;
end;

theorem Th71:
  exp_R is_differentiable_on REAL & diff(exp_R,th)=exp_R.th
proof
A1: [#]REAL is open Subset of REAL & REAL c=dom exp_R by FUNCT_2:def 1;
A2: for r st r in REAL holds exp_R is_differentiable_in r by Th70;
  thus thesis by A1,A2,Th70,FDIFF_1:16;
end;

theorem Th72:
  cos is_differentiable_on REAL & diff(cos,th)=-sin.th
proof
A1: [#]REAL is open Subset of REAL & REAL c=dom cos by FUNCT_2:def 1;
A2: for r st r in REAL holds cos is_differentiable_in r by Th68;
  thus thesis by A1,A2,Th68,FDIFF_1:16;
end;

theorem Th73:
  sin is_differentiable_on REAL & diff(sin,th)=cos.th
proof
A1: [#]REAL is open Subset of REAL & REAL c=dom sin by FUNCT_2:def 1;
A2: for r st r in REAL holds sin is_differentiable_in r by Th69;
  thus thesis by A1,A2,Th69,FDIFF_1:16;
end;

theorem Th74:
  th in [.0,1 .] implies 0<cos.th & cos.th>=1/2
proof
  assume
A1: th in [.0,1 .];
A2: 0<=th & th<=1 by A1,XXREAL_1:1;
A3: th is Real by XREAL_0:def 1;
A4: Partial_Sums(th P_cos) is convergent by A3,Th39;
A5: cos.th=Sum (th P_cos) by Th40;
A6: lim ((Partial_Sums(th P_cos))*bq )=lim(Partial_Sums(th P_cos)) by A4,
  SEQ_4:30;
A7: lim ((Partial_Sums(th P_cos))*bq )= cos.th by A5,A6,SERIES_1:def 3;
A8: for n holds ((Partial_Sums(th P_cos))*bq).n >=1/2
  proof
    let n;
A9: ((Partial_Sums(th P_cos))*bq).0=(Partial_Sums(th P_cos)).(bq.0)
    by FUNCT_2:21
      .= (Partial_Sums(th P_cos)).(2*0+1) by Lm6
      .= (Partial_Sums(th P_cos)).0+(th P_cos).(0+1) by SERIES_1:def 1
      .= (th P_cos).0 +(th P_cos).(0+1) by SERIES_1:def 1
      .= (-1)|^ 0 * (th)|^ (2*0)/((2*0)!)+(th P_cos).1 by Def25
      .= (-1)|^ 0 * (th)|^ (2*0)/((2*0)!)+
    (-1)|^ 1 * (th)|^ (2*1)/((2*1)!) by Def25
      .= 1* (th)|^ (2*0)/((2*0)!)+
    (-1)|^ 1 * (th)|^ (2*1)/((2*1)!) by Lm8
      .= 1/1 + (-1)|^ 1 * (th)|^ (2*1)/((2*1)!) by NEWTON:9,18
      .= 1+ (-1)* (th)|^ (2*1)/((2*1)!) by NEWTON:10
      .= 1+ (-1)*(th*th)/((2*1)!) by NEWTON:100
      .= 1-(th^2)/2 by NEWTON:20;
    defpred X[Element of NAT] means ((Partial_Sums(th P_cos))*bq).$1 >= 1/2;
A10: th^2 <= 1^2 by A2,SQUARE_1:77;
A11: 1 - 1/2 = 1/2 & (th^2)/2<=1/2 by A10,XREAL_1:74;
A12: X[0] by A9,A11,XREAL_1:12;
A13: for k st X[k] holds X[k+1]
    proof
      let k;
      assume
A14:  ((Partial_Sums(th P_cos))*bq).k >= 1/2;
A15:  ((Partial_Sums(th P_cos))*bq).(k+1)
      =(Partial_Sums(th P_cos)).(bq.(k+1)) by FUNCT_2:21
        .=(Partial_Sums(th P_cos)).(2*(k+1)+1) by Lm6
        .=(Partial_Sums(th P_cos)).(2*k+1+1)+(th P_cos).(2*(k+1)+1) by
      SERIES_1:def 1
        .=(Partial_Sums(th P_cos)).(2*k+1) + (th P_cos).(2*k+1+1)+
      (th P_cos).(2*(k+1)+1) by SERIES_1:def 1
        .= (Partial_Sums(th P_cos)).(bq.k)+ (th P_cos).(2*k+1+1)+
      (th P_cos).(2*(k+1)+1) by Lm6
        .=((Partial_Sums(th P_cos))*bq).k+ (th P_cos).(2*k+1+1)+
      (th P_cos).(2*(k+1)+1) by FUNCT_2:21;
      A16:  (
(Partial_Sums(th P_cos))*bq).(k+1)-((Partial_Sums(th P_cos))* bq).k
      =(th P_cos).(2*k+1+1)+ (th P_cos).(2*(k+1)+1) by A15;
A17:  (th P_cos).(2*k+1+1)=(-1)|^ (2*(k+1)) * (th)|^ (2*(2*(k+1)))/((2*(
      2*(k+1)))!) by Def25
        .=(1)* (th)|^ (2*(2*(k+1)))/((2*(2*(k+1)))!) by Lm8
        .=(th)|^ (2*(2*(k+1)))/((2*(2*(k+1)))!);
A18:  (th P_cos).(2*(k+1)+1)
      = (-1)|^ (2*(k+1)+1) * (th)|^ (2*(2*(k+1)+1))/((2*(2*(k+1)+1))!)
      by Def25
        .=((-1) * (th)|^ (2*(2*(k+1)+1)))/((2*(2*(k+1)+1))!) by Lm8;
A19:  2*(2*(k+1))<2*(2*(k+1)+1) by XREAL_1:31,70;
A20:  (2*(2*(k+1)))! <= (2*(2*(k+1)+1))! by A19,Th42;
A21:  (th)|^ (2*(2*(k+1)+1)) <= (th)|^ (2*(2*(k+1))) by A2,A19,Th43;
      A22:  0
 <= (th)|^ (2*(2*(k+1)+1)) & (2*(2*(k+1)+1))! >0 by NEWTON:23,POWER:3;
A23:  1/((2*(2*(k+1)+1))!)<=1/((2*(2*(k+1)))!) by A20,NEWTON:23,XREAL_1:87;
A24:  (th)|^ (2*(2*(k+1)+1))*(1/((2*(2*(k+1)+1))!))<=
      (th)|^ (2*(2*(k+1)))*(1/((2*(2*(k+1)))!)) by A21,A22,A23,XREAL_1:68;
A25:  ((th)|^ (2*(2*(k+1)))*1)/((2*(2*(k+1)))!)-
      ((th)|^ (2*(2*(k+1)+1))*1)/((2*(2*(k+1)+1))!)>=0 by A24,XREAL_1:50;
A26:  ((Partial_Sums(th P_cos))*bq).(k+1)>=((Partial_Sums(th P_cos))*bq
      ).k by A16,A17,A18,A25,XREAL_1:51;
      thus thesis by A14,A26,XXREAL_0:2;
    end;
A27: for n holds X[n] from NAT_1:sch 1(A12,A13);
    thus thesis by A27;
  end;
  thus thesis by A4,A7,A8,PREPOWER:2,SEQ_4:29;
end;

definition
  func tan -> PartFunc of REAL, REAL equals
  sin/cos;
  coherence;
  func cot -> PartFunc of REAL, REAL equals
  cos/sin;
  coherence;
end;

theorem Th75:
  [.0,1 .] c= dom tan & ].0,1 .[ c= dom tan
proof
A1: [.0,1 .] \ cos"{0} c=dom cos \ cos"{0} by Th27,XBOOLE_1:33;
A2: [.0,1 .] /\ cos"{0}={}
  proof
    assume
A3: [.0,1 .] /\ cos"{0}<>{};
    consider rr such that
A4: rr in [.0,1 .] /\ cos"{0} by A3,XBOOLE_0:def 1;
A5: rr in [.0,1 .] by A4,XBOOLE_0:def 4;
A6: rr in cos"{0} by A4,XBOOLE_0:def 4;
A7: cos.(rr) <>0 by A5,Th74;
A8: cos.(rr) in {0} by A6,FUNCT_1:def 13;
    thus contradiction by A7,A8,TARSKI:def 1;
  end;
A9: [.0,1 .] misses cos"{0} by A2,XBOOLE_0:def 7;
A10: [.0,1 .] c= dom cos \ cos"{0} by A1,A9,XBOOLE_1:83;
A11: [.0,1 .] c= dom sin /\ (dom cos \ cos"{0}) by A10,Th27,XBOOLE_1:19;
A12: [.0,1 .] c= dom tan by A11,RFUNCT_1:def 4;
A13: ].0,1 .[c=[.0,1 .] by XXREAL_1:25;
  thus thesis by A12,A13,XBOOLE_1:1;
end;

Lm15: dom(tan|[.0,1 .]) = [.0,1 .] &
for th st th in [.0,1 .] holds tan|[.0,1 .].th=tan.th
proof
A1: dom(tan|[.0,1 .])=dom tan /\ [.0,1 .] by RELAT_1:90;
A2: dom(tan|[.0,1 .]) = [.0,1 .] by A1,Th75,XBOOLE_1:28;
  thus thesis by A2,FUNCT_1:70;
end;

Lm16: tan is_differentiable_on ].0,1 .[ & for th st th in ].0,1 .[ holds
diff(tan,th)>0
proof

  A1: sin is_differentiable_on ].0,1 .[ & cos is_differentiable_on ].0,1 .[ by
  Th72,Th73,FDIFF_1:34;
A2: for th be Real st th in ].0,1 .[ holds cos.th<>0
  proof
    let th be Real such that
A3: th in ].0,1 .[;
A4: ].0,1 .[ c=[.0,1 .] by XXREAL_1:25;
    thus thesis by A3,A4,Th74;
  end;
A5: for th st th in ].0,1 .[ holds diff(tan,th)>0
  proof
    let th such that
A6: th in ].0,1 .[;

A7: th is Real & cos is_differentiable_in th by Th68,XREAL_0:def 1;
A8: sin is_differentiable_in th by Th69;
A9: cos.th<>0 by A2,A6;
A10: diff(tan,th)=(diff(sin,th)*cos.th-diff(cos,th)*sin.th)/(cos.th) ^2
    by A7,A8,A9,FDIFF_2:14
      .=(cos.th*cos.th-diff(cos,th)*sin.th)/(cos.th)^2 by Th69
      .=(cos.th*cos.th-(-sin.th)*sin.th)/(cos.th)^2 by Th68
      .=((cos.th)^2+(sin.th)*sin.th)/(cos.th)^2
      .=1/(cos.th)^2 by Th31;
A11: (cos.th)^2>0 by A9,SQUARE_1:74;
    thus thesis by A10,A11;
  end;
  thus thesis by A1,A2,A5,FDIFF_2:21;
end;

theorem Th76:
  tan|[.0,1 .] is continuous
proof
A1: for th be real number st th in dom(tan|[.0,1 .]) holds
  tan|[.0,1 .] is_continuous_in th
  proof
    let th be real number;
    assume
A2: th in dom(tan|[.0,1 .]);
A3: th in [.0,1 .] by A2,RELAT_1:86;
A4: dom sin = REAL by FUNCT_2:def 1;
A5: th in dom sin by A4,XREAL_0:def 1;
A6: th in [#]REAL by XREAL_0:def 1;
A7: sin is_differentiable_in th by A6,Th73,FDIFF_1:16;
A8: sin is_continuous_in th by A7,FDIFF_1:32;
A9: cos is_differentiable_in th by A6,Th72,FDIFF_1:16;
A10: cos is_continuous_in th by A9,FDIFF_1:32;
A11: cos.(th) <> 0 by A3,Th74;
A12: tan is_continuous_in th by A5,A8,A10,A11,FCONT_1:11;
A13: now
      let rseq;
      assume that
A14:  rng rseq c= dom (tan|[.0,1 .]) and
A15:  rseq is convergent & lim rseq = th;
A16:  rng rseq c= dom tan by A14,Lm15,Th75,XBOOLE_1:1;
A17:  tan.th = lim (tan/*rseq) by A12,A15,A16,FCONT_1:def 1;
A18:  now
        let k1 be Element of NAT;
A19:    dom (rseq) = NAT by SEQ_1:3;
A20:    rseq.k1 in rng rseq by A19,FUNCT_1:def 5;
A21:    (tan|[.0,1 .]).(rseq.k1)=tan.(rseq.k1) by A14,A20,Lm15;
A22:    (tan|[.0,1 .]).(rseq.k1)=((tan|[.0,1 .])/*rseq).k1
        by A14,FUNCT_2:185;
        thus ((tan|[.0,1 .])/*rseq).k1=(tan/*rseq).k1
        by A14,A21,A22,Lm15,Th75,FUNCT_2:185,XBOOLE_1:1;
      end;
A23:  (tan|[.0,1 .])/*rseq =tan/*rseq by A18,FUNCT_2:113;
      thus (tan|[.0,1 .])/*rseq is convergent
      & (tan|[.0,1 .]).th = lim ((tan|[.0,1 .])/*rseq)
      by A2,A12,A15,A16,A17,A23,Lm15,FCONT_1:def 1;
    end;
    thus thesis by A13,FCONT_1:def 1;
  end;
  thus thesis by A1,FCONT_1:def 2;
end;

theorem Th77:
  th1 in ].0,1 .[ & th2 in ].0,1 .[ & tan.th1=tan.th2 implies th1=th2
proof
  assume that
A1: th1 in ].0,1 .[ and
A2: th2 in ].0,1 .[ and
A3: tan.(th1)=tan.(th2);
A4: 0<th1 by A1,XXREAL_1:4;
A5: th1<1 by A1,XXREAL_1:4;
A6: 0<th2 by A2,XXREAL_1:4;
A7: th2<1 by A2,XXREAL_1:4;
  assume
A8: th1<>th2;
A9: now per cases by A8,XXREAL_0:1;
    suppose
A10:  th1<th2;
A11:  for th be Real st th in ].th1,th2.[ holds th in ].0,1 .[
      proof
        let th be Real;
        assume
A12:    th in ].th1,th2.[;
A13:    th1<th by A12,XXREAL_1:4;
A14:    th< th2 by A12,XXREAL_1:4;
A15:    th <1 by A7,A14,XXREAL_0:2;
        thus thesis by A4,A13,A15,XXREAL_1:4;
      end;
A16:  for th be Real st th in [.th1,th2.] holds th in [.0,1 .]
      proof
        let th be Real;
        assume
A17:    th in [.th1,th2.];
A18:    th1<=th by A17,XXREAL_1:1;
A19:    th<= th2 by A17,XXREAL_1:1;
A20:    th <=1 by A7,A19,XXREAL_0:2;
        thus thesis by A4,A18,A20,XXREAL_1:1;
      end;
A21:  ].th1,th2.[c=].0,1 .[ by A11,SUBSET_1:7;
A22:  tan is_differentiable_on ].th1,th2.[ by A21,Lm16,FDIFF_1:34;
      A23:  [.th1,th2.]c=[. 0,1 .] & tan|[.th1,th2.] is continuous by A16,Th76,
      FCONT_1:17,SUBSET_1:7;
      consider r such that
A24:  r in ].th1,th2.[ & diff(tan,r)=0
      by A1,A2,A3,A10,A22,A23,Th75,ROLLE:1,XBOOLE_1:1;
      take th=r;
      thus th in ].0,1 .[ & diff(tan,(th))=0 by A11,A24;
    end;
    suppose
A25:  th2<th1;
A26:  for th be Real st th in ].th2,th1 .[ holds th in ].0,1 .[
      proof
        let th be Real;
        assume
A27:    th in ].th2,th1 .[;
A28:    th2<th by A27,XXREAL_1:4;
A29:    th< th1 by A27,XXREAL_1:4;
A30:    th <1 by A5,A29,XXREAL_0:2;
        thus thesis by A6,A28,A30,XXREAL_1:4;
      end;
A31:  for th be Real st th in [.th2,th1 .] holds th in [.0,1 .]
      proof
        let th be Real;
        assume
A32:    th in [.th2,th1 .];
A33:    th2<=th by A32,XXREAL_1:1;
A34:    th<= th1 by A32,XXREAL_1:1;
A35:    th <=1 by A5,A34,XXREAL_0:2;
        thus thesis by A6,A33,A35,XXREAL_1:1;
      end;
A36:  ].th2,th1 .[c=].0,1 .[ by A26,SUBSET_1:7;
A37:  tan is_differentiable_on ].th2,th1 .[ by A36,Lm16,FDIFF_1:34;
      A38:  [.
th2,th1 .] c=[.0,1 .] & tan|[.th2,th1.] is continuous by A31,Th76,
      FCONT_1:17,SUBSET_1:7;
      consider r such that
A39:  r in ].th2,th1 .[ & diff(tan,r)=0
      by A1,A2,A3,A25,A37,A38,Th75,ROLLE:1,XBOOLE_1:1;
      take th=r;
      thus th in ].0,1 .[ & diff(tan,(th))=0 by A26,A39;
    end;
  end;
  thus thesis by A9,Lm16;
end;

Lm17: tan.0 = 0 & tan.1 >1
proof
A1: 0 in [.0,1 .] by XXREAL_1:1;
A2: tan.0=(sin.0)* (cos.0)" by A1,Th75,RFUNCT_1:def 4
    .=0 by Th33;
A3: 1 in [.0,1 .] by XXREAL_1:1;
A4: tan.1 =(sin.1)/(cos.1) by A3,Th75,RFUNCT_1:def 4;
  thus thesis by A2,A4,Th47,XREAL_1:189;
end;

begin :: Existence of Circle Ratio

definition
  func PI -> real number means
  :Def32:
  tan.(it/4) = 1 & it in ]. 0, 4 .[;
  existence
  proof
A1: [.tan.0,tan.1 .] = {r : tan.0<=r & r<= tan.1 } by RCOMP_1:def 1;
A2: [.tan.1,tan.0 .]= {} by Lm17,XXREAL_1:29;
A3: 1 in [.tan.0,tan.1 .] \/ [.tan.1,tan.0 .] by A1,A2,Lm17;
    consider th be Real such that
A4: th in [.0, 1 .] and
A5: tan.(th)=1 by A3,Th75,Th76,FCONT_2:16;
A6: 0<=th by A4,XXREAL_1:1;
A7: th <= 1 by A4,XXREAL_1:1;
A8: 0 <th by A5,A6,Lm17;
A9: th <1 by A5,A7,Lm17,XXREAL_0:1;
    take th1 =th*4;
    thus tan.(th1/4)=1 by A5;
A10: th*4<1*4 by A9,XREAL_1:70;
    thus thesis by A8,A10,XXREAL_1:4;
  end;
  uniqueness
  proof
    let th1,th2 be real number such that
A11: tan.(th1/4) = 1 and
A12: th1 in ]. 0, 4 .[ and
A13: tan.(th2/4) = 1 and
A14: th2 in ]. 0, 4 .[;
A15: 0<th1 by A12,XXREAL_1:4;
A16: th1 <4 by A12,XXREAL_1:4;
A17: th1/4 <4/4 by A16,XREAL_1:76;
A18: th1/4 in ]. 0, 1 .[ by A15,A17,XXREAL_1:4;
A19: 0<th2 by A14,XXREAL_1:4;
A20: th2 < 4 by A14,XXREAL_1:4;
A21: th2/4 <4/4 by A20,XREAL_1:76;
A22: th2/4 in ]. 0, 1 .[ by A19,A21,XXREAL_1:4;
A23: th1/4=th2/4 by A11,A13,A18,A22,Th77;
    thus thesis by A23;
  end;
end;

definition
  redefine func PI -> Real;
  coherence by XREAL_0:def 1;
end;

theorem Th78:
  sin.(PI/4) = cos.(PI/4)
proof
A1: PI in ].0, 4.[ by Def32;
A2: 0< PI by A1,XXREAL_1:4;
A3: PI < 4 by A1,XXREAL_1:4;
A4: PI/4 < 4/4 by A3,XREAL_1:76;
A5: PI/4 in ].0,1 .[ by A2,A4,XXREAL_1:4;
A6: tan.(PI/4) =1 by Def32;
A7: sin.(PI/4) * (cos.(PI/4))"=1 by A5,A6,Th75,RFUNCT_1:def 4;
  thus thesis by A7,XCMPLX_1:210;
end;

begin :: Formulas of sin and cos

theorem Th79:
  sin.(th1+th2)=(sin.(th1)) *(cos.(th2))+(cos.(th1)) * (sin.(th2)) &
  cos.(th1+th2)=(cos.(th1)) *(cos.(th2))-(sin.(th1)) * (sin.(th2))
proof
  reconsider th1,th2 as Real by XREAL_0:def 1;
A1: (th1+th2)*<i> = 0+0+(th1+th2)*<i>;
A2: Sum(th1*<i> ExpSeq ) * Sum(th2*<i> ExpSeq )
  =Sum((th1*<i>+th2*<i>) ExpSeq ) by Lm2
    .=cos.(th1+th2)+(sin.(th1+th2))*<i> by A1,Lm3;
A3: Sum((th1*<i> ExpSeq) ) * Sum((th2*<i> ExpSeq) )
  =(cos.(th1)+(sin.th1)*<i>)* Sum((th2*<i> ExpSeq) ) by Lm3
    .=(cos.(th1)+sin.(th1)*<i>)*(cos.(th2)+(sin.th2)*<i>) by Lm3
    .=((cos.(th1)) *( cos.(th2))-(sin.(th1)) * (sin.(th2))+
  ((sin.(th1)) * (cos.(th2))+(cos.(th1)) * (sin.(th2)))*<i>);
  thus thesis by A2,A3,COMPLEX1:163;
end;

theorem
  sin(th1+th2)=(sin(th1)) *(cos(th2))+(cos(th1)) * (sin(th2)) &
  cos(th1+th2)=(cos(th1)) *(cos(th2))-(sin(th1)) * (sin(th2)) by Th79;

theorem Th81:
  cos.(PI/2) = 0 & sin.(PI/2) = 1 & cos.(PI) = -1 & sin.(PI) = 0 &
  cos.(PI+PI/2) = 0 & sin.(PI+PI/2) = -1 & cos.(2 * PI) = 1 & sin.(2 * PI) =0
proof
  thus
A1: cos.(PI/2)=cos.(PI/4+PI/4)
    .=(cos.(PI/4)) * (cos.(PI/4))
  -(cos.(PI/4)) * (cos.(PI/4)) by Th78,Th79
    .=0;
  thus
A2: sin.(PI/2)=sin.(PI/4+PI/4)
    .= (cos.(PI/4)) *( cos.(PI/4))
  +(sin.(PI/4)) * (sin.(PI/4)) by Th78,Th79
    .=1 by Th31;
  thus
A3: cos.(PI)=cos.(PI/2+PI/2)
    .=0 * 0-sin.(PI/2) * sin.(PI/2) by A1,Th79
    .=-1 by A2;
  thus
A4: sin.(PI)=sin.(PI/2+PI/2)
    .=(sin.(PI/2)) *( cos.(PI/2))
  +(cos.(PI/2) )*( sin.(PI/2)) by Th79
    .=0 by A1;
  thus cos.(PI+PI/2) =(cos.(PI)) *( cos.(PI/2))
  -(sin.(PI)) * (sin.(PI/2)) by Th79
    .=0 by A1,A4;
  thus sin.(PI+PI/2) =(sin.(PI) )*( cos.(PI/2))
  +(cos.(PI)) *( sin.(PI/2) ) by Th79
    .=-1 by A1,A2,A3;
  thus cos.(2 * PI)=cos.(PI+PI)
    .=(-1 ) * (-1) -sin.(PI) * sin.(PI) by A3,Th79
    .=1 by A4;
  thus sin.(2 * PI)=sin.(PI+PI)
    .=(sin.(PI)) * (cos.(PI))+(cos.(PI)) * (sin.(PI)) by Th79
    .=0 by A4;
end;

theorem
  cos(PI/2) = 0 & sin(PI/2) = 1 & cos(PI) = -1 & sin(PI) = 0 &
  cos(PI+PI/2) = 0 & sin(PI+PI/2) = -1 &
  cos(2 * PI) = 1 & sin(2 * PI) =0 by Th81;

theorem Th83:
  sin.(th+2 * PI) = sin.th & cos.(th+2 * PI) = cos.th &
  sin.(PI/2-th) = cos.th & cos.(PI/2-th) = sin.th & sin.(PI/2+th) = cos.th &
  cos.(PI/2+th) = -sin.th & sin.(PI+th) = -sin.th & cos.(PI+th) = -cos.th
proof
  thus sin.(th+2 * PI)=sin.(th) * 1 +cos.(th) * 0 by Th79,Th81
    .=sin.th;
  thus cos.(th+2 * PI)=cos.(th) * 1 - sin.(th) * 0 by Th79,Th81
    .=cos.th;
  thus sin.(PI/2-th) =(sin.(PI/2) )* (cos.(-th))
  + (cos.(PI/2)) *( sin.(-th)) by Th79
    .=cos.(th) by Th33,Th81;
  thus cos.(PI/2-th) =(cos.(PI/2)) * (cos.(-th))
  - ( sin.(PI/2)) *( sin.(-th)) by Th79
    .=0 - 1 * (-sin.(th)) by Th33,Th81
    .=sin.(th);
  thus sin.(PI/2+th) =1 * cos.(th) + 0 * (sin.(th)) by Th79,Th81
    .=cos.(th);
  thus cos.(PI/2+th) =(cos.(PI/2)) *( cos.(th))
  -( sin.(PI/2) )*( sin.(th)) by Th79
    .=-sin.(th) by Th81;
  thus
  sin.(PI+th) =(sin.(PI)) * (cos.(th)) +( cos.(PI) )*( sin.(th)) by Th79
    .=-sin.(th) by Th81;
  thus cos.(PI+th) =(-1) * cos.(th) - 0 * sin.(th) by Th79,Th81
    .=-cos.(th);
end;

theorem
  sin(th+2 * PI) = sin(th) & cos(th+2 * PI) = cos(th) &
  sin(PI/2-th) = cos(th) & cos(PI/2-th) = sin(th) & sin(PI/2+th) = cos(th) &
  cos(PI/2+th) = -sin(th) & sin(PI+th) = -sin(th) &
  cos(PI+th) = -cos(th) by Th83;

Lm18: th in [.0,1 .] implies sin.th>=0
proof
  assume
A1: th in [.0,1 .];
A2: th<=1 by A1,XXREAL_1:1;
A3: sin.th>= sin.0
  proof
A4: now per cases by A1,XXREAL_1:1;
      suppose
A5:     th=0;
        thus thesis by A5;
      end;
      suppose
A6:     0<th;
A7:     sin|REAL is continuous by Th73,FDIFF_1:33;
A8:     sin|[.0,th.] is continuous by A7,FCONT_1:17;
        consider r be Real such that
A9:     r in ].0,th.[ and
A10:    diff(sin,r)=(sin.th-sin.0)/(th-0)
        by A6,A8,Th27,Th73,FDIFF_1:34,ROLLE:3;
A11:    r < th by A9,XXREAL_1:4;
A12:    0<r by A9,XXREAL_1:4;
A13:    r <1 by A2,A11,XXREAL_0:2;
A14:    r in [.0,1 .] by A12,A13,XXREAL_1:1;
A15:    cos.r >0 by A14,Th74;
A16:    sin.th-sin.0 >0 by A6,A10,A15,Th73;
        thus thesis by A16,XREAL_1:49;
      end;
    end;
    thus thesis by A4;
  end;
  thus thesis by A3,Th33;
end;

theorem Th85:
  th in ].0,PI/2.[ implies cos.th > 0
proof
  assume that
A1: th in ].0,PI/2.[ and
A2: cos.th <= 0;
A3: cos|REAL is continuous by Th72,FDIFF_1:33;
A4: cos|[.0,th.] is continuous by A3,FCONT_1:17;
A5: 0 < th by A1,XXREAL_1:4;
A6: th is Real by XREAL_0:def 1;
  A7: [.
cos.0,cos.th .] \/ [.cos.th,cos.0 .] = [.cos.th,cos.0 .] & 0 in [.cos.th,
  cos.0 .] by A2,Th33,XXREAL_1:1,222;
A8: ex th2 be Real st th2 in [.0,th .] & cos.th2 = 0
  by A4,A5,A6,A7,Th27,FCONT_2:16;
  consider th2 be Real such that
A9: th2 in [.0,th .]
  and 0 < th and
A10: cos.th2 = 0 by A5,A8;
A11: 0 <= th2 by A9,XXREAL_1:1;
A12: th2 <= th by A9,XXREAL_1:1;
A13: th < PI/2 by A1,XXREAL_1:4;
A14: 0<th2 by A10,A11,Th33;
A15: th2<PI/2 by A12,A13,XXREAL_0:2;
A16: th2/2 < PI/2/2 by A15,XREAL_1:76;
A17: PI in ].0, 4.[ by Def32;
A18: PI < 4 by A17,XXREAL_1:4;
A19: PI/4 < 4/4 by A18,XREAL_1:76;
A20: th2/2 < 1 by A16,A19,XXREAL_0:2;
A21: 0 = cos.(th2/2 + th2/2) by A10
    .= (cos.(th2/2))^2 - (sin.(th2/2)) * (sin.(th2/2)) by Th79
    .= ((cos.(th2/2)) - (sin.(th2/2))) * ((cos.(th2/2)) + (sin.(th2/2)));
A22: cos.(th2/2) - sin.(th2/2) = 0 or
  cos.(th2/2) + sin.(th2/2) = 0 by A21;
A23: th2/2 in ].0,1 .[ by A14,A20,XXREAL_1:4;
A24: ].0,1 .[ c= [.0,1 .] by XXREAL_1:25;
A25: cos.(th2/2) > 0 & sin.(th2/2) >= -0 by A23,A24,Lm18,Th74;
A26: 4*(th2/2) < 4*1 by A20,XREAL_1:70;
A27: (2*th2) in ].0,4.[ by A14,A26,XXREAL_1:4;
A28: (sin.(th2/2)) * (cos.(th2/2))"=1 by A22,A25,XCMPLX_0:def 7;
A29: tan.((2*th2)/4) = 1 by A23,A28,Th75,RFUNCT_1:def 4;
A30: 2*th2 =PI by A27,A29,Def32;
  thus contradiction by A1,A12,A30,XXREAL_1:4;
end;

theorem
  th in ].0,PI/2.[ implies cos(th) > 0 by Th85;

begin :: Addenda

:: from COMPLEX2, 2006.03,06, A.T.

canceled;

theorem
  sin(a-b) = sin(a)*cos(b)-cos(a)*sin(b)
proof
  thus sin(a-b) =(sin.a) *(cos.(-b))+(cos.a) * (sin.(-b)) by Th79
    .=(sin.a) *(cos.b)+(cos.a) * (sin.(-b)) by Th33
    .=(sin.a) *(cos.b)+(cos.a) * -(sin.b) by Th33
    .=(sin a) *(cos b)-(cos a) * (sin b);
end;

theorem
  cos(a-b) = cos(a)*cos(b)+sin(a)*sin(b)
proof
  thus cos(a-b) =(cos.a) *(cos.(-b))-(sin.(a)) * (sin.(-b)) by Th79
    .=(cos.a) *(cos.b)-(sin.a) * (sin.(-b)) by Th33
    .=(cos.a) *(cos.b)-(sin.a) * -(sin.b) by Th33
    .=(cos a) *(cos b)+(sin a) * (sin b);
end;

definition
  let n;
  redefine func n!c equals
  n!;
  compatibility by Th37;
end;

registration
  cluster sin -> continuous;
  coherence
  proof
    A1: dom
 sin = REAL & sin|REAL is continuous by Th73,FDIFF_1:33,FUNCT_2:def 1;
    thus thesis by A1,RELAT_1:98;
  end;
  cluster cos -> continuous;
  coherence
  proof
    A2: dom
 cos = REAL & cos|REAL is continuous by Th72,FDIFF_1:33,FUNCT_2:def 1;
    thus thesis by A2,RELAT_1:98;
  end;
  cluster exp_R -> continuous;
  coherence
  proof
    A3: dom exp_R = REAL & exp_R|REAL is continuous by Th71,FDIFF_1:33
    ,FUNCT_2:def 1;
    thus thesis by A3,RELAT_1:98;
  end;
end;

