:: Spaces of Pencils, {G}rassmann Spaces, and Generalized {V}eronese 
:: Spaces
::  by Adam Naumowicz
:: 
:: Received November 8, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies RELAT_1, ARYTM_1, BOOLE, PENCIL_1, PRE_TOPC, REALSET1, CARD_1,
      AMI_1, FINSET_1, VECTSP_1, RLSUB_1, RLSUB_2, MATRLIN, VECTSP_9, SETFAM_1,
      PENCIL_4, RLVECT_3, RLVECT_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, ZFMISC_1, XCMPLX_0, XREAL_0, NAT_1,
      SETFAM_1, REALSET1, STRUCT_0, CARD_1, FINSET_1, PRE_TOPC, PENCIL_1,
      RLVECT_1, VECTSP_1, VECTSP_4, VECTSP_5, VECTSP_7, VECTSP_9, BINARITH;
 constructors POLYNOM1, PENCIL_2, VECTSP_5, VECTSP_9, BINARITH, VECTSP_7,
      DOMAIN_1, RLVECT_2;
 registrations STRUCT_0, SUBSET_1, PENCIL_1, XREAL_0, ARYTM_3, XBOOLE_0,
      REALSET1, FINSET_1, MATRLIN, VECTSP_9, SETFAM_1;
 requirements REAL, BOOLE, SUBSET, NUMERALS, ARITHM;
 definitions TARSKI, XBOOLE_0, STRUCT_0, PENCIL_1, VECTSP_4;
 theorems XBOOLE_0, ZFMISC_1, AXIOMS, NAT_1, TARSKI, PENCIL_1, CARD_1,
      XBOOLE_1, REAL_1, CARD_2, STRUCT_0, SUBSET_1, VECTSP_5, SETFAM_1,
      VECTSP_9, AMI_5, GOBOARD9, VECTSP_4, XCMPLX_1, RLVECT_1, VECTSP_7,
      VECTSP10, VECTSP_1, EULER_1, CARD_4;
 schemes NAT_1, XBOOLE_0, SETFAM_1, SUBSET_1;

begin :: Spaces of pencils

theorem degnat:
  for k,n being Nat st 1<=k & k < n & 3 <= n holds
  k+1 < n or 2 <= k
  proof
    let k,n be Nat such that
    k: 1<=k & k < n & 3 <= n;
    assume k+1 >= n; then
    k+1 >= n & k+1 <= n by k,NAT_1:38; then
    3 <= k+1 by k,AXIOMS:21; then
    3-1 <= k+1-1 by REAL_1:49;
    hence 2 <= k;
  end;  
  
theorem kara:
  for X being finite set
  for n being Nat st n <= card X
  ex Y being Subset of X st card Y = n
  proof
    let X be finite set;
    let n be Nat;
    assume n: n <= card X;
    defpred P[Nat] means $1 <= card X implies
    ex Y being Subset of X st card Y = $1;
    z: P[0]
    proof
      assume 0<=card X;
      reconsider e={} as Subset of X by XBOOLE_1:2;
      take e;
      thus thesis by CARD_1:78;
    end;
k:  now
      let k be Nat;
      assume p: P[k];
      thus P[k+1]
      proof
        assume k+1 <= card X; then
        k: k < card X by NAT_1:38; then
        consider Y being Subset of X such that
        Y: card Y = k by p;
        not X c= Y by XBOOLE_0:def 10,Y,k; then
        consider x being set such that
        x: x in X & not x in Y by TARSKI:def 3;
        {x} c= X by ZFMISC_1:37,x; then
        reconsider Z=Y\/{x} as Subset of X by XBOOLE_1:8;
        take Z;
        thus card Z = k+1 by CARD_2:54,x,Y;
      end;
    end;
    for k being Nat holds P[k] from NAT_1:sch 1(z,k);
    hence thesis by n;    
  end;
  
theorem som:
  for F being Field
  for V being VectSp of F
  for W being Subspace of V holds W is Subspace of (Omega).V
  proof
    let F be Field;
    let V be VectSp of F;
    let W be Subspace of V;
    A: the VectSpStr of V = (Omega).V by VECTSP_4:def 4;
    hence the carrier of W c= the carrier of (Omega).V by VECTSP_4:def 2;
    thus thesis by A,VECTSP_4:def 2;
 end;  
  
theorem smo:
  for F being Field
  for V being VectSp of F
  for W being Subspace of (Omega).V holds W is Subspace of V
  proof
    let F be Field;
    let V be VectSp of F;
    let W be Subspace of (Omega).V;
    A: the VectSpStr of V = (Omega).V by VECTSP_4:def 4;
    hence the carrier of W c= the carrier of V by VECTSP_4:def 2;
    thus thesis by A,VECTSP_4:def 2;
  end;

theorem smon:
  for F being Field
  for V being VectSp of F
  for W being Subspace of V holds (Omega).W is Subspace of V
  proof
    let F be Field;
    let V be VectSp of F;
    let W be Subspace of V;
    A: the VectSpStr of W = (Omega).W by VECTSP_4:def 4;
    hence the carrier of (Omega).W c= the carrier of V by VECTSP_4:def 2;
    thus thesis by A,VECTSP_4:def 2;
  end;
  
theorem smot:
  for F being Field
  for V,W being VectSp of F holds
  (Omega).W is Subspace of V implies W is Subspace of V
  proof
    let F be Field;
    let V,W be VectSp of F;
    assume o: (Omega).W is Subspace of V;
    A: the VectSpStr of W = (Omega).W by VECTSP_4:def 4;
    hence the carrier of W c= the carrier of V by VECTSP_4:def 2,o;
    thus thesis by o,A,VECTSP_4:def 2;
  end;
  
definition
  let F be Field;
  let V be VectSp of F;
  let W1,W2 be Subspace of V;
  func segment(W1,W2) -> Subset of Subspaces(V) means :segdef:
  for W being strict Subspace of V holds W in it iff
  W1 is Subspace of W & W is Subspace of W2 if W1 is Subspace of W2
  otherwise it={};
  consistency;
  existence
  proof
    hereby assume W1 is Subspace of W2;
      deffunc A()=Subspaces(V);
      defpred P[set] means for W being Subspace of V st W=$1 holds
      W1 is Subspace of W & W is Subspace of W2;
      consider X being Subset of A() such that
      X: for x being set holds x in X iff x in A() & P[x] from SUBSET_1:sch 1;
      take X;
      let W be strict Subspace of V;
      thus W in X implies W1 is Subspace of W & W is Subspace of W2 by X;
      assume W1 is Subspace of W & W is Subspace of W2; then
      w: P[W];
      W in Subspaces(V) by VECTSP_5:def 3;
      hence W in X by X,w;
    end;
    hereby assume not W1 is Subspace of W2;
      reconsider W={} as Subset of Subspaces(V) by XBOOLE_1:2;
      take W;
      thus W={};
    end;
  end;
  uniqueness
  proof
    let S,T be Subset of Subspaces(V);
    now
      assume W1 is Subspace of W2;
      assume that
      S: for W being strict Subspace of V holds W in S iff
      W1 is Subspace of W & W is Subspace of W2 and
      T: for W being strict Subspace of V holds W in T iff
      W1 is Subspace of W & W is Subspace of W2;
      now
        let x be set;
        thus x in S implies x in T
        proof
          assume x: x in S; then
          consider x1 being strict Subspace of V such that
          x1: x1=x by VECTSP_5:def 3;
          x1 in S iff W1 is Subspace of x1 & x1 is Subspace of W2 by S;
          hence x in T by T,x,x1;
        end;
        assume x: x in T; then
        consider x1 being strict Subspace of V such that
        x1: x1=x by VECTSP_5:def 3;
        x1 in T iff W1 is Subspace of x1 & x1 is Subspace of W2 by T;
        hence x in S by x,x1,S;
      end;
      hence S=T by TARSKI:2;
    end;
    hence thesis;
  end;
end;

theorem o1:
  for F being Field
  for V being VectSp of F
  for W1,W2,W3,W4 being Subspace of V st
  W1 is Subspace of W2 & W3 is Subspace of W4 &
  (Omega).W1=(Omega).W3 & (Omega).W2=(Omega).W4
  holds segment(W1,W2) = segment(W3,W4)
  proof
    let F be Field;
    let V be VectSp of F;
    let W1,W2,W3,W4 be Subspace of V;
    assume o:
    W1 is Subspace of W2 & W3 is Subspace of W4 &
    (Omega).W1=(Omega).W3 & (Omega).W2=(Omega).W4;
    thus segment(W1,W2) c= segment(W3,W4)
    proof
      let a be set;
      assume a: a in segment(W1,W2); then
      consider A1 being strict Subspace of V such that
      A1: A1=a by VECTSP_5:def 3;
      reconsider A=a as strict Subspace of V by A1;
      W1 is Subspace of A & A is Subspace of W2 by o,a,segdef; then
      (Omega).W1 is Subspace of A & A is Subspace of (Omega).W2 by som,smon;
      then
      W3 is Subspace of A & A is Subspace of W4 by o,smot,smo; 
      hence a in segment(W3,W4) by o,segdef;
    end;
    let a be set;
    assume a: a in segment(W3,W4); then
    consider A1 being strict Subspace of V such that
    A1: A1=a by VECTSP_5:def 3;
    reconsider A=a as strict Subspace of V by A1;
    W3 is Subspace of A & A is Subspace of W4 by o,a,segdef; then
    (Omega).W3 is Subspace of A & A is Subspace of (Omega).W4 by som,smon;
    then
    W1 is Subspace of A & A is Subspace of W2 by o,smot,smo;
    hence a in segment(W1,W2) by o,segdef;
  end;
  
definition
  let F be Field;
  let V be VectSp of F;
  let W1,W2 be Subspace of V;
  func pencil(W1,W2) -> Subset of Subspaces(V) equals :pendef:
  segment(W1,W2) \ {(Omega).W1,(Omega).W2};
  coherence
  proof
    segment(W1,W2) \ {(Omega).W1,(Omega).W2} c= segment(W1,W2) by XBOOLE_1:36;
    hence segment(W1,W2) \ {(Omega).W1,(Omega).W2} is Subset of Subspaces(V)
    by XBOOLE_1:1;
  end;
end;

theorem o2:
  for F being Field
  for V being VectSp of F
  for W1,W2,W3,W4 being Subspace of V st
  W1 is Subspace of W2 & W3 is Subspace of W4 &
  (Omega).W1=(Omega).W3 & (Omega).W2=(Omega).W4
  holds pencil(W1,W2) = pencil(W3,W4)
  proof
    let F be Field;
    let V be VectSp of F;
    let W1,W2,W3,W4 be Subspace of V such that
    o: W1 is Subspace of W2 & W3 is Subspace of W4 &
    (Omega).W1=(Omega).W3 & (Omega).W2=(Omega).W4;
    thus pencil(W1,W2) = segment(W1,W2)\{(Omega).W1,(Omega).W2} by pendef
    .= segment(W3,W4)\{(Omega).W3,(Omega).W4} by o,o1
    .= pencil(W3,W4) by pendef;
  end;
  
definition
  let F be Field;
  let V be finite-dimensional VectSp of F;
  let W1,W2 be Subspace of V;
  let k be Nat;
  func pencil(W1,W2,k) -> Subset of (k Subspaces_of V) equals :kpendef:
  pencil (W1,W2) /\ (k Subspaces_of V);
  coherence by XBOOLE_1:17;
end;

theorem sub:
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat
  for W1,W2,W being Subspace of V st W in pencil(W1,W2,k)
  holds W1 is Subspace of W & W is Subspace of W2
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let k be Nat;
    let W1,W2,W be Subspace of V;
    assume W in pencil(W1,W2,k); then
    W in pencil(W1,W2)/\(k Subspaces_of V) by kpendef; then
    w: W in pencil(W1,W2) & W in k Subspaces_of V by XBOOLE_0:def 3; then
    consider X being strict Subspace of V such that
    w1: W=X & dim X=k by VECTSP_9:def 3;
    W in segment(W1,W2)\{(Omega).W1,(Omega).W2} by w,pendef; then
    wu: W in segment(W1,W2) by XBOOLE_0:def 4; then
    W1 is Subspace of W2 by segdef;
    hence W1 is Subspace of W & W is Subspace of W2 by wu,w1,segdef;
  end;

theorem o3:
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat
  for W1,W2,W3,W4 being Subspace of V st
  W1 is Subspace of W2 & W3 is Subspace of W4 &
  (Omega).W1=(Omega).W3 & (Omega).W2=(Omega).W4
  holds pencil(W1,W2,k) = pencil(W3,W4,k)
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let k be Nat;
    let W1,W2,W3,W4 be Subspace of V such that
    o: W1 is Subspace of W2 & W3 is Subspace of W4 &
    (Omega).W1=(Omega).W3 & (Omega).W2=(Omega).W4;
    thus pencil(W1,W2,k) = pencil(W1,W2)/\(k Subspaces_of V) by kpendef
    .= pencil(W3,W4)/\(k Subspaces_of V) by o,o2
    .= pencil(W3,W4,k) by kpendef;
  end;

definition
  let F be Field;
  let V be finite-dimensional VectSp of F;
  let k be Nat;
  func k Pencils_of V -> Subset-Family of (k Subspaces_of V) means :kpek:
  for X being set holds X in it iff
  ex W1,W2 being Subspace of V st
  W1 is Subspace of W2 & dim W1+1=k & dim W2=k+1 & X=pencil(W1,W2,k);
  existence
  proof
    deffunc A() = bool (k Subspaces_of V);
    defpred P[set] means ex W1,W2 being Subspace of V st
    W1 is Subspace of W2 & dim W1+1=k & dim W2=k+1 & $1=pencil(W1,W2,k);
    consider X being set such that
    X: for x being set holds x in X iff x in A() & P[x] from XBOOLE_0:sch 1;
    X c= A()
    proof
      let a be set;
      assume a in X;
      hence thesis by X;
    end; then
    reconsider X as Subset-Family of (k Subspaces_of V) by SETFAM_1:def 7;
    take X;
    let x be set;
    thus x in X implies ex W1,W2 being Subspace of V st
    W1 is Subspace of W2 & dim W1+1=k & dim W2=k+1 & x=pencil(W1,W2,k) by X;
    given W1,W2 being Subspace of V such that
    d: W1 is Subspace of W2 & dim W1+1=k & dim W2=k+1 & x=pencil(W1,W2,k);
    thus x in X by d,X;
  end;
  uniqueness
  proof
    let S,T be Subset-Family of (k Subspaces_of V) such that
    S: for X being set holds X in S iff
    ex W1,W2 being Subspace of V st
    W1 is Subspace of W2 & dim W1+1=k & dim W2=k+1 & X=pencil(W1,W2,k)
    and
    T: for X being set holds X in T iff
    ex W1,W2 being Subspace of V st
    W1 is Subspace of W2 & dim W1+1=k & dim W2=k+1 & X=pencil(W1,W2,k);
    now
      let x be set;
      thus x in S implies x in T
      proof
        assume x in S; then
        ex W1,W2 being Subspace of V st
       W1 is Subspace of W2 & dim W1+1=k & dim W2=k+1 & x=pencil(W1,W2,k) by S;
        hence x in T by T;
      end; 
      assume x in T; then
      ex W1,W2 being Subspace of V st
      W1 is Subspace of W2 & dim W1+1=k & dim W2=k+1 & x=pencil(W1,W2,k) by T;
      hence x in S by S;
    end;
    hence S=T by TARSKI:2;
  end;
end;  

theorem pemp:
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat st 1 <= k & k < dim V holds k Pencils_of V is non empty
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let k be Nat;
    assume a: 1 <= k & k < dim V;
    k+1 <= dim V by NAT_1:38,a; then
    consider W2 being strict Subspace of V such that
    W2: dim W2 = k+1 by VECTSP_9:39;
    k-'1<=k by GOBOARD9:2; then
    k-'1 <= dim W2 by W2,NAT_1:38; then
    consider W1 being strict Subspace of W2 such that
    W1: dim W1 = k-'1 by VECTSP_9:39;
    W11: dim W1+1=k by a,W1,AMI_5:4;
    reconsider W1'=W1 as Subspace of V by VECTSP_4:34;
    pencil(W1',W2,k) in k Pencils_of V by kpek,W11,W2;
    hence k Pencils_of V is non empty;
  end;
  
theorem pekcol:
  for F being Field
  for V being finite-dimensional VectSp of F
  for W1,W2,P,Q being Subspace of V
  for k being Nat st 1 <= k & k < dim V & dim W1+1=k & dim W2=k+1 &
  P in pencil(W1,W2,k) & Q in pencil(W1,W2,k) & P<>Q holds
  P/\Q = (Omega).W1 & P+Q = (Omega).W2
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let W1,W2,P0,Q0 be Subspace of V;
    let k be Nat such that
    k: 1 <= k & k < dim V & dim W1+1=k & dim W2=k+1 &
    P0 in pencil(W1,W2,k) & Q0 in pencil(W1,W2,k) & P0<>Q0;
    consider P being strict Subspace of V such that
    P: P=P0 & dim P = k by k,VECTSP_9:def 3;
    consider Q being strict Subspace of V such that
    Q: Q=Q0 & dim Q = k by k,VECTSP_9:def 3;
    P/\Q is Subspace of P by VECTSP_5:20; then
    d1: dim(P/\Q)<=k by P,VECTSP_9:29;
    W1 is Subspace of P & W1 is Subspace of Q by P,Q,k,sub; then
    wu: W1 is Subspace of (P/\Q) by VECTSP_5:24; then
    dim W1 <= dim (P/\Q) by VECTSP_9:29; then
    dp: dim W1 = dim (P/\Q) or dim (P/\Q)=k by d1,NAT_1:27,k;
    per cases by dp;
    suppose s: dim W1 = dim (P/\Q); then
      (Omega).W1 = (Omega).(P/\Q) by wu,VECTSP_9:32;      
      hence P0/\Q0 = (Omega).W1 by P,Q,VECTSP_4:def 4;
      P is Subspace of W2 & Q is Subspace of W2 by sub,P,Q,k; then
      wu: P+Q is Subspace of W2 by VECTSP_5:40;
      dim(P+Q)+dim W1-dim W1 = dim P + dim Q - dim W1 by s,VECTSP_9:36; then
      dim (P+Q) = dim W2 by P,Q,XCMPLX_1:26,k; then
      (Omega).(W2) = (Omega).(P+Q) by wu,VECTSP_9:32;
      hence P0+Q0 = (Omega).W2 by P,Q,VECTSP_4:def 4;
    end;
    suppose s: dim (P/\Q)=k;
      P/\Q is Subspace of P by VECTSP_5:20; then
      (Omega).(P/\Q) = (Omega).P by VECTSP_9:32,s,P; then
      (Omega).(P/\Q) = P by VECTSP_4:def 4; then
      pq: P/\Q = P by VECTSP_4:def 4;
      P/\Q is Subspace of Q by VECTSP_5:20; then
      (Omega).(P/\Q) = (Omega).Q by VECTSP_9:32,s,Q; then
      (Omega).(P/\Q) = Q by VECTSP_4:def 4;
      hence thesis by pq,P,Q,k,VECTSP_4:def 4;
    end;
  end;
  
theorem dim1:
  for F being Field
  for V being finite-dimensional VectSp of F
  for v being Vector of V st v <> 0.V holds dim Lin{v} = 1
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let v be Vector of V such that
    v: v <> 0.V;
    v in {v} by TARSKI:def 1; then
    v in Lin{v} by VECTSP_7:13; then
    reconsider v0=v as Vector of Lin{v} by RLVECT_1:def 1;
    v0: Lin{v0}=Lin{v} by VECTSP_9:21;
    v1: v <> 0.Lin{v} by v,VECTSP_4:19;
    (Omega).Lin{v0} = Lin{v0} by VECTSP_4:def 4;
    hence dim Lin{v} = 1 by v0,v1,VECTSP_9:34;
  end;
  
theorem plus:
  for F being Field
  for V being finite-dimensional VectSp of F
  for W being Subspace of V
  for v being Vector of V st not v in W holds dim(W+Lin{v})=dim W + 1
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let W be Subspace of V;
    let v be Vector of V such that
    v: not v in W;
    v <> 0.V by v,VECTSP_4:25; then
    dv: dim Lin{v}=1 by dim1;
    the carrier of (Omega).(W/\Lin{v}) = {0.(W/\Lin{v})}
    proof
      thus the carrier of (Omega).(W/\Lin{v}) c= {0.(W/\Lin{v})}
      proof
        let a be set;
        assume a in the carrier of (Omega).(W/\Lin{v}); then
        a in the carrier of (W/\Lin{v}) by VECTSP_4:def 4; then
        a in (the carrier of W)/\the carrier of Lin{v} by VECTSP_5:def 2; then
        a in the carrier of W &
        a in the carrier of Lin{v} by XBOOLE_0:def 3;
        then
        aw: a in W & a in Lin{v} by RLVECT_1:def 1; then
        consider e being Element of F such that
        e: a=e*v by VECTSP10:4;
        now assume
          e<>0.F; then
          v=e"*(e*v) by VECTSP_1:67;
          hence contradiction by VECTSP_4:29,v,e,aw;
        end; then
        a=0.V by e,VECTSP_1:59; then
        a = 0.(W/\Lin{v}) by VECTSP_4:19;
        hence a in {0.(W/\Lin{v})} by TARSKI:def 1;
      end;
      let a be set;
      assume a in {0.(W/\Lin{v})}; then
      a=0.(W/\Lin{v}) by TARSKI:def 1; then
      a=0.V by VECTSP_4:19; then
      a in W/\Lin{v} by VECTSP_4:25; then
      a in the carrier of (W/\Lin{v}) by RLVECT_1:def 1;
      hence a in the carrier of (Omega).(W/\Lin{v}) by VECTSP_4:def 4;
    end; then
    (Omega).(W/\Lin{v})=(0).(W/\Lin{v}) by VECTSP_4:def 3; then
    d: dim(W/\Lin{v})=0 by VECTSP_9:33;
    dim(W+Lin{v}) + dim(W/\Lin{v}) = dim W + dim Lin{v} by VECTSP_9:36; 
    hence dim(W+Lin{v})=dim W + 1 by d,dv;
  end;
    
theorem plus2:
  for F being Field
  for V being finite-dimensional VectSp of F
  for W being Subspace of V
  for v,u being Vector of V st
  not v in W & not u in W & v<>u & {v,u} is linearly-independent &
  W/\Lin{v,u}=(0).V holds dim(W+Lin{v,u})=dim W + 2
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let W be Subspace of V;
    let v,u be Vector of V such that
    v: not v in W & not u in W & v<>u & {v,u} is linearly-independent &
    W/\Lin{v,u}=(0).V;
    v in {v,u} & u in {v,u} by TARSKI:def 2; then
    v in Lin{v,u} & u in Lin{v,u} by VECTSP_7:13; then
    reconsider v1=v,u1=u as Vector of Lin{v,u} by RLVECT_1:def 1;
    consider L being Subset of Lin{v,u} such that
    L: L is linearly-independent & L={v1,u1} by v,VECTSP_9:16;
    Lin{v,u}=Lin{v1,u1} by VECTSP_9:21; then
    (Omega).Lin{v,u}=Lin{v1,u1} by VECTSP_4:def 4; then
    d: dim Lin{v,u}=2 by v,L,VECTSP_9:35;
    W/\Lin{v,u}=(0).(W/\Lin{v,u}) by v,VECTSP_4:47; then
    (Omega).(W/\Lin{v,u})=(0).(W/\Lin{v,u}) by VECTSP_4:def 4; then
    dim(W/\Lin{v,u}) = 0 by VECTSP_9:33;
    hence dim(W+Lin{v,u}) = dim(W+Lin{v,u}) + dim(W/\Lin{v,u})
    .= dim W + 2 by d,VECTSP_9:36;
  end;
  
theorem lin:
  for F being Field
  for V being finite-dimensional VectSp of F
  for W1,W2 being Subspace of V st W1 is Subspace of W2
  for k being Nat st 1 <= k & k < dim V & dim W1+1=k & dim W2=k+1
  for v being Vector of V st v in W2 & not v in W1
  holds W1+Lin{v} in pencil(W1,W2,k)
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let W1,W2 be Subspace of V such that
    W1: W1 is Subspace of W2;
    let k be Nat such that
    k: 1 <= k & k < dim V & dim W1+1=k & dim W2=k+1;
    let v be Vector of V such that
    v: v in W2 & not v in W1;
    set W=W1+Lin{v};
    d: dim W = k by v,k,plus;
    w1: W1 is Subspace of W by VECTSP_5:11;
    v in the carrier of W2 by v,RLVECT_1:def 1; then
    {v} c= the carrier of W2 by ZFMISC_1:37; then
    Lin{v} is Subspace of W2 by VECTSP_9:20; then
    W is Subspace of W2 by W1,VECTSP_5:40; then
    s: W in segment(W1,W2) by W1,segdef,w1;
    dim (Omega).W1 + 1 = k & dim (Omega).W2 = k+1 by k,VECTSP_9:31; then
    W <> (Omega).W1 & W <> (Omega).W2 by d,NAT_1:38; then
    not W in {(Omega).W1,(Omega).W2} by TARSKI:def 2; then
    W in segment(W1,W2)\{(Omega).W1,(Omega).W2} by s,XBOOLE_0:def 4; then
    p: W in pencil(W1,W2) by pendef;
    W in k Subspaces_of V by d,VECTSP_9:def 3; then
    W in pencil(W1,W2)/\(k Subspaces_of V) by p,XBOOLE_0:def 3;
    hence W1+Lin{v} in pencil(W1,W2,k) by kpendef;
  end;
  
theorem pektri:
  for F being Field
  for V being finite-dimensional VectSp of F
  for W1,W2 being Subspace of V st W1 is Subspace of W2
  for k being Nat st 1 <= k & k < dim V & dim W1+1=k & dim W2=k+1
  holds pencil(W1,W2,k) is non trivial
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let W1,W2 be Subspace of V such that
    W1: W1 is Subspace of W2;
    let k be Nat such that
    k: 1 <= k & k < dim V & dim W1+1=k & dim W2=k+1;
    reconsider U=W1 as Subspace of W2 by W1;
    consider W being Linear_Compl of U;
    ds: W2 is_the_direct_sum_of W,U by VECTSP_5:def 5; then
    dim W2 = dim U + dim W by VECTSP_9:38; then
    dim W2 + 1 = dim W + (dim U + 1); then
    k + (1 + 1)-k = dim W + k-k by k,XCMPLX_1:1; then
    dim W=2 by XCMPLX_1:26; then
    consider u,v being Vector of W such that
    va0: u <> v & {u,v} is linearly-independent &
    (Omega).W = Lin{u,v} by VECTSP_9:35;
    nov0:
    now
      assume v in Lin{u}; then
      consider a being Element of F such that
      va: v = a*u by VECTSP10:4;
      thus contradiction by va,VECTSP_7:8,va0;
    end;
    set L=Lin{u};
    ts: the VectSpStr of W2 = W+U & W /\ U = (0).W2 by ds,VECTSP_5:def 4;
    reconsider u,v as Vector of W2 by VECTSP_4:18;
    u0:u in W by RLVECT_1:def 1;
    u1: u in W2 by RLVECT_1:def 1;
    u2:
    now
      assume u in W1; then
      u in (0).W2 by u0,VECTSP_5:7,ts; then
      u=0.W2 by VECTSP_4:46; then
      u=0.W by VECTSP_4:19;
      hence contradiction by VECTSP_7:6,va0;
    end;
    v0: v in W by RLVECT_1:def 1;
    v1: v in W2 by RLVECT_1:def 1;
    v2:
    now
      assume v in W1; then
      v in (0).W2 by v0,VECTSP_5:7,ts; then
      v=0.W2 by VECTSP_4:46; then
      v=0.W by VECTSP_4:19;
      hence contradiction by VECTSP_7:6,va0;
    end;
    reconsider u1=u,v1=v as Vector of V by VECTSP_4:18;
    v in {v1} by TARSKI:def 1; then
    v in Lin{v1} by VECTSP_7:13; then
    vi: v in W1+Lin{v1} by VECTSP_5:6;
    nov: not v in Lin{u} by nov0,VECTSP10:14;
    di:
    now
      assume W1+Lin{v1} = W1+Lin{u1}; then
      consider h,j being Vector of V such that
      h: h in W1 & j in Lin{u1} & v1 = h+j by VECTSP_5:5,vi;
      consider a being Element of F such that
      a: j=a*u1 by VECTSP10:4,h;
      v1-a*u1=h+(a*u1-a*u1) by h,a,RLVECT_1:42; then
      v1-a*u1=h+0.V by RLVECT_1:28; then
      fa: v1-a*u1=h by RLVECT_1:10;
      reconsider Wx=W as Subspace of V by VECTSP_4:34;
      a*u in Wx by VECTSP_4:29,u0; then
      fal: v-a*u in Wx by VECTSP_4:31,v0;
      a*u = a*u1 by VECTSP_4:22; then
      wik: v1-a*u1 = v-a*u by VECTSP_4:24; then
      v-a*u in W/\U by fa,h,fal,VECTSP_5:7; then
      v-a*u = 0.W2 by ts,VECTSP_4:46; then
      h = 0.V by wik,fa,VECTSP_4:19; then
      v1 = j by h,RLVECT_1:10;
      hence contradiction by VECTSP10:14,h,nov;
    end;
    W1+Lin{u1} in pencil(W1,W2,k) & 
    W1+Lin{v1} in pencil(W1,W2,k) by u1,u2,k,lin,v1,v2; then
    2 c= Card pencil(W1,W2,k) by di,PENCIL_1:2;
    hence pencil(W1,W2,k) is non trivial by PENCIL_1:4;
  end;
  
definition
  let F be Field;
  let V be finite-dimensional VectSp of F;
  let k be Nat;
  func PencilSpace(V,k) -> strict TopStruct equals :PS:
  TopStruct(#k Subspaces_of V, k Pencils_of V#);
  coherence;
end;  

theorem pne:
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat st k <= dim V holds PencilSpace(V,k) is non empty
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let k be Nat such that
    k: k <= dim V;
    set S=PencilSpace(V,k);
    s: S=TopStruct(#k Subspaces_of V, k Pencils_of V#) by PS;
    k Subspaces_of V is non empty by k,VECTSP_9:40;
    hence S is non empty by STRUCT_0:def 1,s;
  end;
  
theorem pnv:
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat st 1 <= k & k < dim V holds PencilSpace(V,k) is non void
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let k be Nat such that
    k: 1 <= k & k < dim V;
    set S=PencilSpace(V,k);
    S=TopStruct(#k Subspaces_of V, k Pencils_of V#) by PS; then
    the topology of S is non empty by k,pemp;
    hence S is non void by PENCIL_1:def 4;
  end;
  
theorem pnd:
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat st 1 <= k & k < dim V & 3 <= dim V
  holds PencilSpace(V,k) is non degenerated
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let k be Nat such that
    k: 1 <= k & k < dim V & 3 <= dim V;
    set S=PencilSpace(V,k);
    s: S=TopStruct(#k Subspaces_of V, k Pencils_of V#) by PS;
    thus S is non degenerated
    proof
      now
        let B be Block of S;
        the topology of S is non empty by k,pemp,s; then
        consider W1,W2 being Subspace of V such that
        B: W1 is Subspace of W2 &
        dim W1+1=k & dim W2=k+1 & B=pencil(W1,W2,k) by kpek,s;
        W1c: the carrier of W1 c= the carrier of V by VECTSP_4:def 2;
        per cases by k,degnat;
        suppose k+1 < dim V; then
          o: (Omega).W2 <> (Omega).V by B,VECTSP_9:32;
          z: the carrier of W2 c= the carrier of V by VECTSP_4:def 2;
          now
            assume a: the carrier of V = the carrier of W2;
            s: (Omega).W2 is Subspace of V by smon;
            the carrier of V = the carrier of the VectSpStr of V; then
            c: the carrier of V = the carrier of (Omega).V by VECTSP_4:def 4;
            the carrier of W2 = the carrier of the VectSpStr of W2; then
            the carrier of W2 = the carrier of (Omega).W2 by VECTSP_4:def 4;
            hence contradiction by o,a,c,VECTSP_4:37,s;
          end; then
          not the carrier of V c= the carrier of W2 by z,XBOOLE_0:def 10;
          then consider v being set such that
          v: v in the carrier of V & not v in the carrier of W2
          by TARSKI:def 3;
          reconsider v as Vector of V by v;
          set X=W1+Lin{v};
          not v in W2 by v,RLVECT_1:def 1; then
          not v in W1 by VECTSP_4:16,B; then
          dim X = k by plus,B; then
          X: X in the carrier of S by VECTSP_9:def 3,s;
          now
            assume X in B; then
            X is Subspace of W2 by sub,B; then
            x: the carrier of X c= the carrier of W2 by VECTSP_4:def 2;
            v in {v} by TARSKI:def 1; then
            v in Lin{v} by VECTSP_7:13; then
            v in X by VECTSP_5:6; then
            v in the carrier of X by RLVECT_1:def 1;
            hence contradiction by x,v;
          end;
          hence the carrier of S <> B by X;
        end;
        suppose su: 2 <= k & k+1>=dim V; then
          k+1 <= dim V & dim V <= k+1 by k,NAT_1:38; then
          dv: dim V = k+1 by AXIOMS:21;
          consider T being Linear_Compl of W1;
          ct: the carrier of T c= the carrier of V by VECTSP_4:def 2;
          vd: V is_the_direct_sum_of W1,T by VECTSP_5:48; then
          k+1 - (dim W1 + 1) = dim W1 + dim T - (dim W1 + 1)
            by dv,VECTSP_9:38; then
          1+1 = dim T - 1+1 by B,XCMPLX_1:26; then
          consider u,v being Vector of T such that
          u: u<>v & {u,v} is linearly-independent & (Omega).T=Lin{u,v}
          by VECTSP_9:35;
          up: u <> 0.T & v <> 0.T by u,VECTSP_7:6;
          u in the carrier of T & v in the carrier of T; then
          reconsider u1=u,v1=v as Vector of V by ct;
          consider I being Basis of W1;
          reconsider I1=I as finite Subset of W1 by VECTSP_9:24;
          2-1 <= dim W1+1-1 by su,B,REAL_1:49; then
          1 <= card I1 by VECTSP_9:def 2; then
          I1 is non empty by CARD_4:17; then
          consider i being set such that
          i: i in I by XBOOLE_0:def 1;
          reconsider i as Vector of W1 by i;
          reconsider J=I1\{i} as finite Subset of V by W1c,XBOOLE_1:1;
          c: Card I = dim W1 by VECTSP_9:def 2;
          ca: card J = card I1 - card{i} by i,EULER_1:5
          .= dim W1 - 1 by CARD_1:79,c;
          jot: J c= the carrier of Lin J
          proof
            let a be set;
            assume a in J; then
            a in Lin J by VECTSP_7:13;
            hence a in the carrier of Lin J by RLVECT_1:def 1;
          end; then
          reconsider J1=J as Subset of Lin J;
          il: I is linearly-independent by VECTSP_7:def 3;
          I\{i} c= I by XBOOLE_1:36; then
          I\{i} is linearly-independent by il,VECTSP_7:2; then
          consider JJ being Subset of V such that
          JJ: JJ is linearly-independent & JJ=I\{i} by VECTSP_9:15;
          consider JJJ being Subset of Lin J such that
          JJJ: JJJ is linearly-independent & JJJ=J by JJ,jot,VECTSP_9:16;
          Lin J1 = Lin J by VECTSP_9:21; then
          J is Basis of Lin J by JJJ,VECTSP_7:def 3; then
          dd: dim Lin J = dim W1 - 1 by ca,VECTSP_9:def 2;
          p: W1/\T=(0).V by vd,VECTSP_5:def 4;
          nu:
          now
            assume u1 in W1; then
            u1 in W1 & u1 in T by RLVECT_1:def 1; then
            u1 in (0).V by p,VECTSP_5:7; then
            u1 in the carrier of (0).V by RLVECT_1:def 1; then
            u1 in {0.V} by VECTSP_4:def 3; then
            u1 = 0.V by TARSKI:def 1;
            hence contradiction by up,VECTSP_4:19;
          end;
          nv:
          now
            assume v1 in W1; then
            v1 in W1 & v1 in T by RLVECT_1:def 1; then
            v1 in (0).V by p,VECTSP_5:7; then
            v1 in the carrier of (0).V by RLVECT_1:def 1; then
            v1 in {0.V} by VECTSP_4:def 3; then
            v1 = 0.V by TARSKI:def 1;
            hence contradiction by up,VECTSP_4:19;
          end;
          lt: Lin I = the VectSpStr of W1 by VECTSP_7:def 3
          .= (Omega).W1 by VECTSP_4:def 4;
          I\{i} c= I by XBOOLE_1:36; then
          Lin (I\{i}) is Subspace of Lin I by VECTSP_7:18; then
          lin: Lin J is Subspace of W1 by VECTSP_9:21;
          n1: not u1 in Lin J by VECTSP_4:16,nu,lin;
          n2: not v1 in Lin J by VECTSP_4:16,nv,lin;
          consider Y being Subset of V such that
          Y: Y is linearly-independent & Y={u,v} by VECTSP_9:15,u;
          li: {u1,v1} is linearly-independent by Y;
          zs: (0).V is Subspace of (Lin J)/\Lin{u1,v1} by VECTSP_4:50;
          the carrier of ((Lin J)/\Lin{u1,v1}) c= the carrier of (0).V
          proof
            let a be set;
            assume a in the carrier of ((Lin J)/\Lin{u1,v1}); then
            a in (Lin J)/\Lin{u1,v1} by RLVECT_1:def 1; then
            a in Lin J & a in Lin {u1,v1} by VECTSP_5:7; then
            a in W1 & a in Lin{u,v} by lin,VECTSP_4:16,VECTSP_9:21; then
            a in W1 & a in the VectSpStr of T by u,VECTSP_4:def 4; then
            a in W1 & a in the carrier of the VectSpStr of T
            by RLVECT_1:def 1; then
            a in W1 & a in T by RLVECT_1:def 1; then
            a in W1 /\ T by VECTSP_5:7;
            hence a in the carrier of (0).V by p,RLVECT_1:def 1;
          end; then
          (Lin J)/\Lin{u1,v1} is Subspace of (0).V by VECTSP_4:35; then
          (Lin J)/\Lin{u1,v1}=(0).V by zs,VECTSP_4:33; then
          die: dim ((Lin J)+Lin{u1,v1}) = dim W1 - 1 + 2
          by li,u,plus2,dd,n1,n2; then
          l: (Lin J)+Lin{u1,v1} in the carrier of S by VECTSP_9:def 3,s,B;
          iw: i in W1 by RLVECT_1:def 1;
          now
            assume (Lin J)+Lin{u1,v1} in B; then
            wu: W1 is Subspace of (Lin J)+Lin{u1,v1} by B,sub;
            now
              assume ihaha: i in (Lin J)+Lin{u1,v1};
              reconsider IV=I as Subset of V by W1c,XBOOLE_1:1;
              IV c= the carrier of (Lin J)+Lin{u1,v1}
              proof
                let a be set;
                assume a: a in IV;
                {i} c= I by i,ZFMISC_1:37; then
                I\{i}\/{i}=I by XBOOLE_1:45; then
                ap: a in J or a in {i} by XBOOLE_0:def 2,a;
                per cases by ap;
                suppose a in J; then
                  al: a in Lin J by VECTSP_7:13; then
                  a in V by VECTSP_4:17; then
                  reconsider o=a as Vector of V by RLVECT_1:def 1;
                  o in (Lin J)+Lin{u1,v1} by VECTSP_5:6,al;
                  hence a in the carrier of (Lin J)+Lin{u1,v1}
                  by RLVECT_1:def 1;
                end;
                suppose a in {i}; then
                  a=i by TARSKI:def 1;
                  hence a in the carrier of (Lin J)+Lin{u1,v1}
                  by ihaha,RLVECT_1:def 1;
                end;
              end; then
              Lin IV is Subspace of (Lin J)+Lin{u1,v1} by VECTSP_9:20; then
              Lin I is Subspace of (Lin J)+Lin{u1,v1} by VECTSP_9:21; then
              wuj: W1 is Subspace of (Lin J)+Lin{u1,v1} by lt,smot;
              Lin{u1,v1} is Subspace of (Lin J)+Lin{u1,v1} by VECTSP_5:11;
              then W1+Lin{u1,v1} is Subspace of (Lin J)+Lin{u1,v1}
              by wuj,VECTSP_5:40; then
              fi: dim(W1+Lin{u1,v1}) <= dim((Lin J)+Lin{u1,v1})
              by VECTSP_9:29;
              zs: (0).V is Subspace of W1/\Lin{u1,v1} by VECTSP_4:50;
              the carrier of (W1/\Lin{u1,v1}) c= the carrier of (0).V
              proof
                let a be set;
                assume a in the carrier of (W1/\Lin{u1,v1}); then
                a in W1/\Lin{u1,v1} by RLVECT_1:def 1; then
                a in W1 & a in Lin {u1,v1} by VECTSP_5:7; then
                a in W1 & a in Lin {u,v} by VECTSP_9:21; then
                a in W1 & a in the VectSpStr of T by u,VECTSP_4:def 4; then
                a in W1 & a in the carrier of the VectSpStr of T
                by RLVECT_1:def 1; then
                a in W1 & a in T by RLVECT_1:def 1; then
                a in W1 /\ T by VECTSP_5:7;
                hence a in the carrier of (0).V by p,RLVECT_1:def 1;
              end; then
              W1/\Lin{u1,v1} is Subspace of (0).V by VECTSP_4:35; then
              W1/\Lin{u1,v1}=(0).V by zs,VECTSP_4:33; then
              dim (W1+Lin{u1,v1}) = dim W1 + 2 by plus2,li,nu,nv,u; then
              dim W1 + 1+1 <= dim W1 + 1 by die,fi;
              hence contradiction by NAT_1:38;
            end;
            hence contradiction by wu,iw,VECTSP_4:16;
          end;
          hence the carrier of S <> B by l;
        end;
      end; then
      not the carrier of S is Block of S;
      hence thesis by PENCIL_1:def 5;
    end;
  end;

theorem pntb:
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat st 1 <= k & k < dim V
  holds PencilSpace(V,k) is with_non_trivial_blocks
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let k be Nat such that
    k: 1 <= k & k < dim V;
    set S=PencilSpace(V,k);
    s: S=TopStruct(#k Subspaces_of V, k Pencils_of V#) by PS;
    thus S is with_non_trivial_blocks
    proof
      let X be Block of S;
      S is non void by k,pnv; then
      the topology of S is non empty by PENCIL_1:def 4; then
      consider W1,W2 being Subspace of V such that
      W: W1 is Subspace of W2 &
      dim W1+1=k & dim W2=k+1 & X=pencil(W1,W2,k) by s,kpek;
      X is non trivial by W,k,pektri;
      hence 2 c= Card X by PENCIL_1:4;
    end;
  end;
    
theorem picb:
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat st 1 <= k & k < dim V
  holds PencilSpace(V,k) is identifying_close_blocks
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let k be Nat such that
    k: 1 <= k & k < dim V;
    set S=PencilSpace(V,k);
    s: S=TopStruct(#k Subspaces_of V, k Pencils_of V#) by PS;    
    thus S is identifying_close_blocks
    proof
      let X,Y be Block of S;
      assume 2 c= Card(X/\Y); then
      consider P,Q being set such that
      P: P in X/\Y & Q in X/\Y & P<>Q by PENCIL_1:2;
      Q: P in X & P in Y & Q in X & Q in Y by P,XBOOLE_0:def 3;
      ne: S is non empty by pne,k;
      S is non void by pnv,k; then
      the topology of S is non empty by PENCIL_1:def 4; then
      XY: X in the topology of S & Y in the topology of S; then
      reconsider P,Q as Point of S by Q;
      the carrier of S is non empty by ne,STRUCT_0:def 1; then
      consider P1 being strict Subspace of V such that
      P1: P1=P & dim P1 = k by VECTSP_9:def 3,s;
      reconsider P as strict Subspace of V by P1;
      the carrier of S is non empty by ne,STRUCT_0:def 1; then
      consider Q1 being strict Subspace of V such that
      Q1: Q1=Q & dim Q1 = k by VECTSP_9:def 3,s;
      reconsider Q as strict Subspace of V by Q1;
      consider W1,W2 being Subspace of V such that
      W: W1 is Subspace of W2 &
      dim W1+1=k & dim W2=k+1 & X=pencil(W1,W2,k) by kpek,s,XY;
      consider U1,U2 being Subspace of V such that
      U: U1 is Subspace of U2 &
      dim U1+1=k & dim U2=k+1 & Y=pencil(U1,U2,k) by kpek,s,XY;
      j: (Omega).W1=P/\Q by pekcol,k,W,Q,P .= (Omega).U1 by pekcol,k,U,Q,P;
      (Omega).W2=P+Q by pekcol,k,W,Q,P .= (Omega).U2 by pekcol,k,U,Q,P;
      hence X=Y by j,W,U,o3;
    end;
  end;
    
theorem
  for F being Field
  for V being finite-dimensional VectSp of F
  for k being Nat st 1 <= k & k < dim V & 3 <= dim V
  holds PencilSpace(V,k) is PLS by pne,pnv,pnd,pntb,picb;

begin :: Grassmann spaces

definition
  let F be Field;
  let V be finite-dimensional VectSp of F;
  let m,n be Nat;
  func SubspaceSet(V,m,n) -> Subset-Family of m Subspaces_of V means :SS:
  for X being set holds X in it iff
  ex W being Subspace of V st dim W = n & X = m Subspaces_of W;
  existence
  proof
    deffunc A()=bool (m Subspaces_of V);
    defpred P[set] means
    ex W being Subspace of V st dim W = n & $1 = m Subspaces_of W;
    consider X being set such that
    X: for x being set holds x in X iff x in A() & P[x] from XBOOLE_0:sch 1;
    X c= A()
    proof
      let a be set;
      assume a in X;
      hence a in A() by X;
    end; then
    reconsider X as Subset-Family of m Subspaces_of V by SETFAM_1:def 7;
    take X;
    let x be set;
    thus x in X implies ex W being Subspace of V st
    dim W=n & x = m Subspaces_of W by X;
    given W being Subspace of V such that
    W: dim W=n & x = m Subspaces_of W;
    x c= m Subspaces_of V by VECTSP_9:42,W;
    hence x in X by X,W;
  end;
  uniqueness
  proof
    let S,T be Subset-Family of m Subspaces_of V such that
    S: for X being set holds X in S iff
    ex W being Subspace of V st dim W = n & X = m Subspaces_of W and
    T: for X being set holds X in T iff
    ex W being Subspace of V st dim W = n & X = m Subspaces_of W;
    now
      let X be set;
      thus X in S implies X in T
      proof
        assume X in S; then
        ex W being Subspace of V st dim W = n & X = m Subspaces_of W by S;
        hence X in T by T;
      end;
      assume X in T; then
      ex W being Subspace of V st dim W = n & X = m Subspaces_of W by T;
      hence X in S by S;
    end;
    hence S=T by TARSKI:2;
  end;
end;  

theorem ssne:
  for F being Field
  for V being finite-dimensional VectSp of F
  for m,n be Nat st n <= dim V holds SubspaceSet(V,m,n) is non empty
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let m,n be Nat;
    assume n <= dim V; then
    consider W being strict Subspace of V such that
    W: dim W = n by VECTSP_9:39;
    m Subspaces_of W in SubspaceSet(V,m,n) by W,SS;
    hence thesis;
end;  

theorem rowns:
  for F being Field
  for W1,W2 being finite-dimensional VectSp of F st (Omega).W1 = (Omega).W2
  for m being Nat holds m Subspaces_of W1 = m Subspaces_of W2
  proof
    let F be Field;
    let W1,W2 be finite-dimensional VectSp of F such that
    w: (Omega).W1 = (Omega).W2;
    let m be Nat;
    (Omega).W1 is Subspace of (Omega).W2 by w,VECTSP_4:32; then
    W1 is Subspace of (Omega).W2 by smot; then
    W1 is Subspace of W2 by smo;
    hence m Subspaces_of W1 c= m Subspaces_of W2 by VECTSP_9:42;
    (Omega).W2 is Subspace of (Omega).W1 by w,VECTSP_4:32; then
    W2 is Subspace of (Omega).W1 by smot; then
    W2 is Subspace of W1 by smo;
    hence m Subspaces_of W2 c= m Subspaces_of W1 by VECTSP_9:42;
  end;
  
theorem rowne:
  for F being Field
  for V being finite-dimensional VectSp of F
  for W being Subspace of V
  for m being Nat st 1<=m & m <= dim V & m Subspaces_of V c= m Subspaces_of W
  holds (Omega).V = (Omega).W
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let W be Subspace of V;
    let m be Nat such that
    m: 1<=m & m <= dim V & m Subspaces_of V c= m Subspaces_of W;
    hereby
      assume om: (Omega).V <> (Omega).W; then
      dd: dim W <> dim V by VECTSP_9:32;
      dim W <= dim V by VECTSP_9:29; then
      dw: dim W < dim V by dd,AXIOMS:21;
      per cases by AXIOMS:21,m;
      suppose md: m=dim V; then
        em: m Subspaces_of W = {} by VECTSP_9:41,dw;
        m Subspaces_of V is non empty by VECTSP_9:40,md;
        hence contradiction by m,em,XBOOLE_1:3;
      end;
      suppose s: m < dim V;
        z: the carrier of W c= the carrier of V by VECTSP_4:def 2;
        now
          assume c: the carrier of V = the carrier of W;
          (Omega).V = the VectSpStr of V &
          (Omega).W = the VectSpStr of W by VECTSP_4:def 4; then
          t: the carrier of (Omega).V = the carrier of (Omega).W by c;
          (Omega).W is strict Subspace of V by smon;
          hence contradiction by VECTSP_4:37,t,om;
        end; then
        not the carrier of V c= the carrier of W by z,XBOOLE_0:def 10; then
        consider x being set such that
        x: x in the carrier of V & not x in the carrier of W by TARSKI:def 3;
        reconsider x as Vector of V by x;
        0.V in W by VECTSP_4:25; then
        x <> 0.V by x,RLVECT_1:def 1; then
        {x} is linearly-independent by VECTSP_7:5; then
        consider I being Basis of V such that
        I: {x} c= I by VECTSP_7:27;
        II: I is linearly-independent & Lin I = the VectSpStr of V
        by VECTSP_7:def 3;
        reconsider J=I as finite Subset of V by VECTSP_9:24;
        card J = dim V by VECTSP_9:def 2; then
        consider K being Subset of J such that
        K: card K = m by s,kara;
        per cases;
        suppose xk: x in K;
          reconsider L=K as finite Subset of V by XBOOLE_1:1;
          cl: card L = m by K;
          i1_: L is linearly-independent by VECTSP_7:2,II;
          L c= the carrier of Lin L
          proof
            let a be set;
            assume a in L; then
            a in Lin L by VECTSP_7:13;
            hence a in the carrier of Lin L by RLVECT_1:def 1;
          end; then
          reconsider L'=L as Subset of Lin L;
          consider LL1 being Subset of Lin L such that
          LL1: LL1 is linearly-independent & LL1=L' by VECTSP_9:16,i1_;
          Lin L' = the VectSpStr of Lin L by VECTSP_9:21; then
          L is Basis of Lin L by VECTSP_7:def 3,LL1; then
          dim Lin L = m by cl,VECTSP_9:def 2; then
          Lin L in m Subspaces_of V by VECTSP_9:def 3; then
          consider M being strict Subspace of W such that
          M: M=Lin L & dim M = m by m,VECTSP_9:def 3;
          x in Lin L by VECTSP_7:13,xk; then
          x in W by M,VECTSP_4:17;
          hence contradiction by x,RLVECT_1:def 1;
        end;
        suppose xnk: not x in K;
          consider y being set such that
          y: y in K by XBOOLE_0:def 1,K,m,CARD_1:78;
          (K\{y})\/{x} c= the carrier of V
          proof
            let a be set;
            assume a in (K\{y})\/{x}; then
            a in (K\{y}) or a in {x} by XBOOLE_0:def 2; then
            a in K or a in {x} by XBOOLE_0:def 4;
            hence a in the carrier of V by TARSKI:def 3;
          end; then
          reconsider L=(K\{y})\/{x} as finite Subset of V;
          x in {x} by TARSKI:def 1; then
          xk: x in L by XBOOLE_0:def 2;
          not x in K\{y} by XBOOLE_0:def 4,xnk; then
          cl: card L = card(K\{y})+1 by CARD_2:54
          .= card K - card{y} + 1 by EULER_1:5,y
          .= card K - 1 + 1 by CARD_1:79
          .= m by K;
          L c= I
          proof
            let a be set;
            assume a in L; then
            a in K\{y} or a in {x} by XBOOLE_0:def 2; then
            a in K or a in {x} by XBOOLE_0:def 4;
            hence a in I by I;
          end; then
          i1_: L is linearly-independent by VECTSP_7:2,II;
          L c= the carrier of Lin L
          proof
            let a be set;
            assume a in L; then
            a in Lin L by VECTSP_7:13;
            hence a in the carrier of Lin L by RLVECT_1:def 1;
          end; then
          reconsider L'=L as Subset of Lin L;
          consider LL1 being Subset of Lin L such that
          LL1: LL1 is linearly-independent & LL1=L' by VECTSP_9:16,i1_;
          Lin L' = the VectSpStr of Lin L by VECTSP_9:21; then
          L is Basis of Lin L by VECTSP_7:def 3,LL1; then
          dim Lin L = m by cl,VECTSP_9:def 2; then
          Lin L in m Subspaces_of V by VECTSP_9:def 3; then
          consider M being strict Subspace of W such that
          M: M=Lin L & dim M = m by m,VECTSP_9:def 3;
          x in Lin L by VECTSP_7:13,xk; then
          x in W by M,VECTSP_4:17;
          hence contradiction by x,RLVECT_1:def 1;
        end;
      end;
    end;
  end;
  
definition
  let F be Field;
  let V be finite-dimensional VectSp of F;
  let m,n be Nat;
  func GrassmannSpace(V,m,n) -> strict TopStruct equals :GS:
  TopStruct(#m Subspaces_of V, SubspaceSet(V,m,n)#);
  coherence;
end;  

theorem gne:
  for F being Field
  for V being finite-dimensional VectSp of F
  for m,n being Nat st m <= dim V holds GrassmannSpace(V,m,n) is non empty
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let m,n be Nat such that
    m: m<= dim V;
    GrassmannSpace(V,m,n) =
    TopStruct(#m Subspaces_of V, SubspaceSet(V,m,n)#) by GS; then
    the carrier of GrassmannSpace(V,m,n) is non empty by VECTSP_9:40,m;
    hence GrassmannSpace(V,m,n) is non empty by STRUCT_0:def 1;
  end;
  
theorem gnv:
  for F being Field
  for V being finite-dimensional VectSp of F
  for m,n being Nat st n <= dim V holds GrassmannSpace(V,m,n) is non void
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let m,n be Nat such that
    n: n <= dim V;
    GrassmannSpace(V,m,n) =
    TopStruct(#m Subspaces_of V, SubspaceSet(V,m,n)#) by GS; then
    the topology of GrassmannSpace(V,m,n) is non empty by ssne,n;
    hence GrassmannSpace(V,m,n) is non void by PENCIL_1:def 4;
  end;
  
theorem gnd:
  for F being Field
  for V being finite-dimensional VectSp of F
  for m,n being Nat st 1 <= m & m < n & n < dim V holds
  GrassmannSpace(V,m,n) is non degenerated
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let m,n be Nat such that
    m: 1 <= m & m < n & n < dim V;
    m1: m < dim V by m,AXIOMS:22;
    set S=GrassmannSpace(V,m,n);
    s: S=TopStruct(#m Subspaces_of V, SubspaceSet(V,m,n)#) by GS;
    hereby 
      assume a: the carrier of S is Block of S;
      the topology of S is non empty by ssne,m,s; then
      consider W being Subspace of V such that
      W: dim W = n & m Subspaces_of V = m Subspaces_of W by SS,s,a;
      (Omega).V = (Omega).W by rowne,m,W,m1; then
      dim W = dim (Omega).V by VECTSP_9:31;
      hence contradiction by W,VECTSP_9:31,m;
    end;
  end;
  
theorem gntb:
  for F being Field
  for V being finite-dimensional VectSp of F
  for m,n being Nat st 1 <= m & m < n & n <= dim V holds
  GrassmannSpace(V,m,n) is with_non_trivial_blocks
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let m,n be Nat such that
    m: 1 <= m & m < n & n <= dim V;
    set S=GrassmannSpace(V,m,n);
    s: S=TopStruct(#m Subspaces_of V, SubspaceSet(V,m,n)#) by GS;
    let B be Block of S;
    the topology of S is non empty by s,ssne,m; then
    consider W being Subspace of V such that
    W: dim W = n & B = m Subspaces_of W by s,SS;
    m+1 <= n by m,NAT_1:38; then
    consider U being strict Subspace of W such that
    U: dim U = m+1 by W,VECTSP_9:39;
    consider I being Basis of U;
    reconsider I'=I as finite Subset of U by VECTSP_9:24;
    II: I is linearly-independent & Lin I=the VectSpStr of U by VECTSP_7:def 3;
    I: Card I = m+1 by U,VECTSP_9:def 2;
    1+1 <= m+1 by m,REAL_1:55; then
    2 c= Card I by CARD_1:56,I; then
    consider i,j being set such that
    i: i in I & j in I & i<>j by PENCIL_1:2;
    ii: I\{i} c= I by XBOOLE_1:36; then
    reconsider I1=I'\{i} as finite Subset of U by XBOOLE_1:1;
    ij: I\{j} c= I by XBOOLE_1:36; then
    reconsider I2=I'\{j} as finite Subset of U by XBOOLE_1:1;
    i1_:I1 is linearly-independent by VECTSP_7:2,II,ii;
    I1 c= the carrier of Lin I1
    proof
      let a be set;
      assume a in I1; then
      a in Lin I1 by VECTSP_7:13;
      hence a in the carrier of Lin I1 by RLVECT_1:def 1;
    end; then
    reconsider I1'=I1 as Subset of Lin I1;
    consider II1 being Subset of Lin I1 such that
    II1: II1 is linearly-independent & II1=I1' by VECTSP_9:16,i1_;
    Lin I1' = the VectSpStr of Lin I1 by VECTSP_9:21; then
    b: I1 is Basis of Lin I1 by VECTSP_7:def 3,II1;
    card I1 = card I' - card{i} by i,EULER_1:5
    .= m+1 - 1 by I,CARD_1:79; then
    d1: dim Lin I1 = m by b,VECTSP_9:def 2;
    Lin I1 is strict Subspace of W by VECTSP_4:34; then
    l1: Lin I1 in B by d1,W,VECTSP_9:def 3;
    i2_:I2 is linearly-independent by VECTSP_7:2,II,ij;
    I2 c= the carrier of Lin I2
    proof
      let a be set;
      assume a in I2; then
      a in Lin I2 by VECTSP_7:13;
      hence a in the carrier of Lin I2 by RLVECT_1:def 1;
    end; then
    reconsider I2'=I2 as Subset of Lin I2;
    consider II2 being Subset of Lin I2 such that
    II2: II2 is linearly-independent & II2=I2' by VECTSP_9:16,i2_;
    Lin I2' = the VectSpStr of Lin I2 by VECTSP_9:21; then
    b: I2 is Basis of Lin I2 by VECTSP_7:def 3,II2;
    card I2 = card I' - card{j} by i,EULER_1:5
    .= m+1 - 1 by I,CARD_1:79; then
    d2: dim Lin I2 = m by b,VECTSP_9:def 2;
    Lin I2 is strict Subspace of W by VECTSP_4:34; then
    l2: Lin I2 in B by d2,W,VECTSP_9:def 3;
    not j in {i} by TARSKI:def 1,i; then
    j in I1 by i,XBOOLE_0:def 4; then
    jot: j in Lin I1 by VECTSP_7:13;
    not j in Lin I2 by i,II,VECTSP_9:18;
    hence 2 c= Card B by jot,l1,l2,PENCIL_1:2;
  end;
  
theorem gicb:
  for F being Field
  for V being finite-dimensional VectSp of F
  for m being Nat st 1 <= m & m+1 <= dim V holds
  GrassmannSpace(V,m,m+1) is identifying_close_blocks
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let m be Nat such that
    m: 1 <= m & m+1 <= dim V;
    set S = GrassmannSpace(V,m,m+1);
    g: GrassmannSpace(V,m,m+1) =
    TopStruct(#m Subspaces_of V, SubspaceSet(V,m,m+1)#) by GS;
    let K,L be Block of S;
k:  the topology of S is non empty by ssne,g,m;
    consider W1 being Subspace of V such that
    W1: dim W1 = m+1 & K = m Subspaces_of W1 by g,SS,k; 
    consider W2 being Subspace of V such that
    W2: dim W2 = m+1 & L = m Subspaces_of W2 by g,SS,k; 
    assume 2 c= Card(K/\L); then
    consider x,y being set such that
    x: x in K/\L & y in K/\L & x <> y by PENCIL_1:2;
    xx: x in K & x in L & y in K & y in L by x,XBOOLE_0:def 3;
    consider X being strict Subspace of W1 such that
    X: x=X & dim X = m by xx,W1,VECTSP_9:def 3;
    consider X' being strict Subspace of W2 such that
    X': x=X' & dim X' = m by xx,W2,VECTSP_9:def 3;
    consider Y being strict Subspace of W1 such that
    Y: y=Y & dim Y = m by xx,W1,VECTSP_9:def 3;
    consider Y' being strict Subspace of W2 such that
    Y': y=Y' & dim Y' = m by xx,W2,VECTSP_9:def 3;
    reconsider x,y as strict Subspace of V by X,Y,VECTSP_4:34;
    x is Subspace of x+y by VECTSP_5:11; then
    em: m <= dim (x+y) by X,VECTSP_9:29;
    now
      assume me: dim(x+y)=m;
      reconsider x'=x as strict Subspace of x+y by VECTSP_5:11;
      (Omega).x' = (Omega).(x+y) by X,me,VECTSP_9:32; then
      x' = (Omega).(x+y) by VECTSP_4:def 4; then
      x = x+y by VECTSP_4:def 4; then
      x = y+x by VECTSP_5:9; then
      ig: y is Subspace of x by VECTSP_5:12;
      reconsider y'=y as strict Subspace of x+y by VECTSP_5:11;
      (Omega).y' = (Omega).(x+y) by Y,me,VECTSP_9:32; then
      y' = (Omega).(x+y) by VECTSP_4:def 4; then
      y = x+y by VECTSP_4:def 4; then
      x is Subspace of y by VECTSP_5:12;
      hence contradiction by VECTSP_4:33,ig,x;
    end; then
    dm: dim (x+y) > m by em,AXIOMS:21;
    x+y is Subspace of W1 by X,Y,VECTSP_5:40; then
    dim (x+y) <= m+1 by VECTSP_9:29,W1; then
    d: dim (x+y) = m+1 by dm,NAT_1:27;
    xr: X+Y=x+y by X,Y,VECTSP10:13;
    xr': X'+Y'=x+y by X',Y',VECTSP10:13;
    (Omega).(X+Y) = (Omega).W1 by xr,d,W1,VECTSP_9:32; then    
    o1: x+y = (Omega).W1 by xr,VECTSP_4:def 4;
    (Omega).(X'+Y') = (Omega).W2 by xr',d,W2,VECTSP_9:32; then    
    o2: x+y = (Omega).W2 by xr',VECTSP_4:def 4;
    thus K=L by o1,o2,rowns,W1,W2;
  end;
  
theorem
  for F being Field
  for V being finite-dimensional VectSp of F
  for m being Nat st 1 <= m & m+1 < dim V holds GrassmannSpace(V,m,m+1) is PLS
  proof
    let F be Field;
    let V be finite-dimensional VectSp of F;
    let m be Nat; assume
    k: 1 <= m & m+1 < dim V; then
    m: m <= dim V by NAT_1:38;
    set S=GrassmannSpace(V,m,m+1);
    now
      thus S is non empty by gne,m;
      thus S is non void by gnv,k;
      m < m+1 by NAT_1:38;
      hence S is non degenerated by gnd,k;
      m < m+1 by NAT_1:38;
      hence S is with_non_trivial_blocks by gntb,k;
      thus S is identifying_close_blocks by gicb,k;
    end;
    hence thesis;
  end;
  
begin :: Veronese spaces

definition
  let X be set;
  func PairSet(X) means :pairsetdef:
  for z being set holds z in it iff
  ex x,y being set st x in X & y in X & z={x,y};
  existence
  proof
    defpred P[set] means
    ex x,y being set st x in X & y in X & $1={x,y};
    consider S being set such that
    z: for z being set holds
    z in S iff z in bool X & P[z] from XBOOLE_0:sch 1;
    take S;
    let z be set;
    thus z in S implies P[z] by z;
    assume p: P[z]; then
    consider x,y being set such that
    x: x in X & y in X & z={x,y};
    z c= X by ZFMISC_1:38,x;
    hence z in S by p,z;
  end;
  uniqueness
  proof
    let p1,p2 be set such that
    p1: for z being set holds z in p1 iff
    ex x,y being set st x in X & y in X & z={x,y} and
    p2: for z being set holds z in p2 iff
    ex x,y being set st x in X & y in X & z={x,y};
    now
      let z be set;
      z in p1 iff ex x,y being set st x in X & y in X & z={x,y} by p1;
      hence z in p1 iff z in p2 by p2;
    end;    
    hence p1=p2 by TARSKI:2;    
  end;
end;

registration
  let X be non empty set;
  cluster PairSet(X) -> non empty;
  coherence
  proof
    consider x being set such that
    x: x in X by XBOOLE_0:def 1;
    {x,x} in PairSet(X) by x,pairsetdef;
    hence thesis;
  end;
end;

definition
  let t,X be set;
  func PairSet(t,X) means :pairsetdef1:
  for z being set holds z in it iff
  ex y being set st y in X & z={t,y};
  existence
  proof
    defpred P[set] means
    ex y being set st y in X & $1={t,y};
    consider S being set such that
    z: for z being set holds
    z in S iff z in bool (X\/{t}) & P[z] from XBOOLE_0:sch 1;
    take S;
    let z be set;
    thus z in S implies P[z] by z;
    assume p: P[z]; then
    consider y being set such that
    x: y in X & z={t,y};
    t in {t} by TARSKI:def 1; then
    t: t in X\/{t} by XBOOLE_0:def 2;
    y in X\/{t} by x,XBOOLE_0:def 2; then
    z c= X\/{t} by x,ZFMISC_1:38,t;
    hence z in S by p,z;    
  end;
  uniqueness
  proof
    let p1,p2 be set such that
    p1: for z being set holds z in p1 iff
    ex y being set st y in X & z={t,y} and
    p2: for z being set holds z in p2 iff
    ex y being set st y in X & z={t,y};
    now
      let z be set;
      z in p1 iff ex y being set st y in X & z={t,y} by p1;
      hence z in p1 iff z in p2 by p2;
    end;    
    hence p1=p2 by TARSKI:2;    
  end;
end;

registration
  let t be set;
  let X be non empty set;
  cluster PairSet(t,X) -> non empty;
  coherence
  proof
    consider x being set such that
    x: x in X by XBOOLE_0:def 1;
    {t,x} in PairSet(t,X) by x,pairsetdef1;
    hence thesis;
  end;
end;

registration
  let t be set;
  let X be non trivial set;
  cluster PairSet(t,X) -> non trivial;
  coherence
  proof
    2 c= Card X by PENCIL_1:4; then
    consider x,y being set such that
    x: x in X & y in X & x<>y by PENCIL_1:2;
    tt: {t,x} in PairSet(t,X) & {t,y} in PairSet(t,X) by x,pairsetdef1;
    now
      assume a: {t,x}={t,y}; then
      y=t by x,ZFMISC_1:10;
      hence contradiction by x,ZFMISC_1:10,a;
    end; then      
    2 c= Card PairSet(t,X) by PENCIL_1:2,tt;
    hence PairSet(t,X) is non trivial by PENCIL_1:4;
  end;
end;

definition
  let X be set;
  let L be Subset-Family of X;
  func PairSetFamily(L) -> Subset-Family of PairSet(X) means :pairfamdef:
  for S being set holds S in it iff
  ex t being set, l being Subset of X st t in X & l in L & S=PairSet(t,l);
  existence
  proof
    deffunc A()=PairSet(X);
    defpred P[Subset of A()] means
    ex t being set, l being Subset of X st t in X & l in L & $1=PairSet(t,l);
    consider F being Subset-Family of A() such that
    F: for B being Subset of A() holds B in F iff P[B] from SETFAM_1:sch 1;
    take F;
    let S be set;
    thus S in F implies ex t being set, l being Subset of X st
    t in X & l in L & S=PairSet(t,l) by F;
    given t being set, l being Subset of X such that
    t: t in X & l in L & S=PairSet(t,l);  
    S c= PairSet(X)
    proof
      let a be set;
      assume a in S; then
      consider y being set such that
      y: y in l & a={t,y} by pairsetdef1,t;
      thus a in PairSet(X) by pairsetdef,t,y;
    end;
    hence thesis by F,t;    
  end;
  uniqueness
  proof
    let p1,p2 be Subset-Family of PairSet(X) such that
    p1: for z being set holds z in p1 iff
    ex t being set, l being Subset of X st t in X & l in L & z=PairSet(t,l) and
    p2: for z being set holds z in p2 iff
    ex t being set, l being Subset of X st t in X & l in L & z=PairSet(t,l);
    now
      let z be set;
      z in p1 iff ex t being set, l being Subset of X st
      t in X & l in L & z=PairSet(t,l) by p1;
      hence z in p1 iff z in p2 by p2;
    end;    
    hence p1=p2 by TARSKI:2;    
  end; 
end;

registration
  let X be non empty set;
  let L be non empty Subset-Family of X;
  cluster PairSetFamily(L) -> non empty;
  coherence
  proof
    consider t being set such that
    t: t in X by XBOOLE_0:def 1;
    consider l being set such that
    l: l in L by XBOOLE_0:def 1;
    PairSet(t,l) in PairSetFamily(L) by t,l,pairfamdef;
    hence thesis;
  end;  
end;

definition
  let S be TopStruct;
  func VeroneseSpace(S) -> strict TopStruct equals :vs:
  TopStruct(#PairSet(the carrier of S), PairSetFamily(the topology of S)#);
  coherence;
end;

registration
  let S be non empty TopStruct;
  cluster VeroneseSpace(S) -> non empty;
  coherence
  proof
    VeroneseSpace(S)=TopStruct
    (#PairSet(the carrier of S), PairSetFamily(the topology of S)#) by vs;
    hence the carrier of VeroneseSpace(S) is non empty;
  end;
end;

registration
  let S be non empty non void TopStruct;
  cluster VeroneseSpace(S) -> non void;
  coherence
  proof
    VeroneseSpace(S)=TopStruct
    (#PairSet(the carrier of S), PairSetFamily(the topology of S)#) by vs;
    hence the topology of VeroneseSpace(S) is non empty;
  end;
end;

registration
  let S be non empty non void non degenerated TopStruct;
  cluster VeroneseSpace(S) -> non degenerated;
  coherence
  proof
    v: VeroneseSpace(S)=TopStruct
    (#PairSet(the carrier of S), PairSetFamily(the topology of S)#) by vs;
    n: not the carrier of S in the topology of S by PENCIL_1:def 5;
    assume the carrier of VeroneseSpace(S) is Block of VeroneseSpace(S); then
    consider t being set, l being Subset of S such that
    t: t in the carrier of S & l in the topology of S &
    PairSet(the carrier of S)=PairSet(t,l) by v,pairfamdef;
    not the carrier of S c= l by n,t,XBOOLE_0:def 10; then
    consider s being set such that
    s: s in the carrier of S & not s in l by TARSKI:def 3;
    now
      assume {t,s} in PairSet(t,l); then
      consider z being set such that
      z: z in l & {t,s}={t,z} by pairsetdef1;
      s = t by s,z,ZFMISC_1:10;
      hence contradiction by s,z,ZFMISC_1:10;
    end;
    hence contradiction by t,pairsetdef,s;
  end;
end;

registration
  let S be non empty non void with_non_trivial_blocks TopStruct;
  cluster VeroneseSpace(S) -> with_non_trivial_blocks;
  coherence
  proof
    v: VeroneseSpace(S)=TopStruct
    (#PairSet(the carrier of S), PairSetFamily(the topology of S)#) by vs;
    let L be Block of VeroneseSpace(S);
    consider t being set, l being Subset of S such that
    t: t in the carrier of S & l in the topology of S & L=PairSet(t,l)
    by v,pairfamdef;
    2 c= Card l by t,PENCIL_1:def 6; then
    reconsider l as non trivial set by PENCIL_1:4;
    PairSet(t,l) is non trivial;
    hence 2 c= Card L by t,PENCIL_1:4;
  end;
end;

registration
  let S be identifying_close_blocks TopStruct;
  cluster VeroneseSpace(S) -> identifying_close_blocks;
  coherence
  proof
    v: VeroneseSpace(S)=TopStruct
    (#PairSet(the carrier of S), PairSetFamily(the topology of S)#) by vs;
    let K,L be Block of VeroneseSpace(S);
    assume 2 c= Card (K/\L); then
    consider a,b being set such that
    a: a in K/\L & b in K/\L & a <> b by PENCIL_1:2;
    b: a in K & a in L & b in K & b in L by a,XBOOLE_0:def 3; then
kl: the topology of VeroneseSpace(S) is non empty by SUBSET_1:def 2;
    consider t being set, k being Subset of the carrier of S such that
    t: t in the carrier of S & k in the topology of S & K=PairSet(t,k)
    by v,pairfamdef,kl;
    consider s being set, l being Subset of the carrier of S such that
    s: s in the carrier of S & l in the topology of S & L=PairSet(s,l)
    by v,pairfamdef,kl;
    consider x being set such that
    x: x in k & a={t,x} by b,t,pairsetdef1;
    consider y being set such that
    y: y in k & b={t,y} by b,t,pairsetdef1;
    consider z being set such that
    z: z in l & a={s,z} by b,s,pairsetdef1;
    consider w being set such that
    w: w in l & b={s,w} by b,s,pairsetdef1;
    (t=s or t=z) & (t=s or t=w) by ZFMISC_1:10,x,y,z,w; then
    ts: t=s by a,z,w;
    now
      assume i: x<>z; then
      x=t by ts,ZFMISC_1:10,z,x;
      hence contradiction by i,x,z,ZFMISC_1:10;
    end; then
    xx: x in k & x in l by x,z;
    now
      assume i: y<>w; then
      y=t by ts,ZFMISC_1:10,w,y;
      hence contradiction by i,y,w,ZFMISC_1:10;
    end; then
    x in k/\l & y in k/\l by y,w,xx,XBOOLE_0:def 3; then
    2 c= Card (k/\l) by PENCIL_1:2,a,x,y;     
    hence K=L by PENCIL_1:def 7,ts,t,s;
  end;
end;

definition
  let S be PLS;
  redefine func VeroneseSpace(S) -> strict PLS;
  coherence;
end;
