:: Several Properties of Fields. Field Theory
::  by J\'ozef Bia{\l}as
::
:: Received September 27, 1990
:: Copyright (c) 1990 Association of Mizar Users

environ

 vocabularies VECTSP_1, REALSET2, FUNCT_1, BOOLE, BINOP_1, REALSET3, RLVECT_1,
      ARYTM_1, GROUP_1;
 notations TARSKI, ZFMISC_1, SUBSET_1, DOMAIN_1, FUNCT_1, FUNCT_2, BINOP_1,
      STRUCT_0, GROUP_1, RLVECT_1, VECTSP_1, REALSET2;
 constructors BINOP_1, REALSET2;
 registrations SUBSET_1, RELSET_1, REALSET1, STRUCT_0, REALSET2;
 requirements SUBSET, BOOLE;
 definitions BINOP_1, RLVECT_1, REALSET2, GROUP_1;
 theorems FUNCT_2, ZFMISC_1, REALSET2, XBOOLE_0, RLVECT_1, VECTSP_1;
 schemes BINOP_1;

begin

:: Properties of fields

theorem
    for F being Field holds -0.F = 0.F by RLVECT_1:25;

theorem Th2:
   for F being Field holds revf(F).(nmf(F)) = nmf(F)
proof
   let F be Field;
     nmf(F)*nmf(F) = 1.F by REALSET2:10;
   hence thesis by REALSET2:26;
end;

theorem
    for F being Field holds
   for a,b being Element of the carrier of F holds
     -(a-b) = b+-a by RLVECT_1:47;

theorem Th4:
   for F being Field holds
   for a,b being Element of (the carrier of F)\{0.F} holds
   revf(F).(omf(F).(a,revf(F).b)) = omf(F).(b,revf(F).a)
proof
   let F be Field;
   let a,b be Element of (the carrier of F)\{0.F};
     reconsider revfa = revf(F).a,  revfb = revf(F).b
      as Element of (the carrier of F)\{0.F};
A1:omf(F).(a,revf(F).b) is Element of (the carrier of F)\{0.F}
by REALSET2:28;
A2:omf(F).(b,revf(F).a) is Element of (the carrier of F)\{0.F}
by REALSET2:28;
    revfb*(b*revfa) = revfa*(b*revfb) by REALSET2:8
        .= revfa*(omf F).(b,revfb)
        .= revfa*nmf F by REALSET2:def 18
        .= revf(F).a by REALSET2:10;
    then a*revfb*(b*revfa) = a*revfa by A2,REALSET2:8
        .= (omf F).(a,revfa)
        .= nmf(F) by REALSET2:def 18;
   hence thesis by A1,A2,REALSET2:26;
end;

theorem
    for F being Field holds
   for a,b being Element of the carrier of F holds
     -(a+b) = -a + -b by RLVECT_1:45;

theorem Th6:
   for F being Field,
       a,b being Element of (the carrier of F)\{0.F} holds
   revf(F).(omf(F).(a,b)) = omf(F).(revf(F).a,revf(F).b)
proof
   let F be Field;
   let a,b be Element of (the carrier of F)\{0.F};
     reconsider revfa = revf(F).a,  revfb = revf(F).b
      as Element of (the carrier of F)\{0.F};
   thus revf(F).(omf(F).(a,b)) = revf(F).(omf(F).(a,revf(F).(revf(F).b))) by
REALSET2:27
          .= revfb*revfa by Th4
          .= revfa*revfb
          .= omf(F).(revf(F).a,revf(F).b);
end;

theorem Th7:
   for F being Field holds
   for a,b,c,d being Element of the carrier of F holds
     a-b = c-d iff a+d = b+c
proof
   let F be Field;
   let a,b,c,d be Element of the carrier of F;
   hereby assume a-b = c-d;
     then c-d+b = a+-b+b
               .= a+(b-b) by RLVECT_1:def 6
               .= a+0.F by RLVECT_1:16
               .= a by RLVECT_1:10;
    hence a+d = c+b+-d+d by RLVECT_1:def 6
             .= c+b+(d-d) by RLVECT_1:def 6
             .= c+b+0.F by RLVECT_1:16
             .= b+c by RLVECT_1:10;
   end;
   assume a+d = b+c;
     then b+c-d = a+(d-d) by RLVECT_1:def 6
               .= a+0.F by RLVECT_1:16
               .= a by RLVECT_1:10;
   hence a-b = c-d+b-b by RLVECT_1:def 6
       .= c-d+(b-b) by RLVECT_1:def 6
       .= c-d+0.F by RLVECT_1:16
       .= c-d by RLVECT_1:10;
end;

theorem Th8:
   for F being Field holds
   for a,c being Element of the carrier of F holds
   for b,d being Element of (the carrier of F)\{0.F} holds
   omf(F).(a,revf(F).b) = omf(F).(c,revf(F).d) iff omf(F).(a,d) = omf(F).(b,c)
proof
   let F be Field;
   let a,c be Element of the carrier of F;
   let b,d be Element of (the carrier of F)\{0.F};
   reconsider revfb = revf(F).b as Element of the carrier of F
                by XBOOLE_0:def 4;
A1:omf(F).(a,revf(F).b) = omf(F).(c,revf(F).d) implies
   omf(F).(a,d) = omf(F).(b,c)
   proof
     reconsider revfb = revf(F).b, revfd = revf(F).d
      as Element of (the carrier of F)\{0.F};
     reconsider crevfd = c*revfd
      as Element of the carrier of F;

      assume
   A2:omf(F).(a,revf(F).b) = omf(F).(c,revf(F).d);
   A3:a = a*1.F by REALSET2:25
       .= omf(F).(a,nmf(F))
       .= omf(F).(a,b*revfb) by REALSET2:def 18
       .= a*(revfb*b)
       .= a*revfb*b by REALSET2:23
       .= b*crevfd by A2
       .= omf(F).(b,omf(F).(c,revf(F).d));
A4:      c = c*1.F by REALSET2:25
       .= omf(F).(c,nmf(F))
       .= omf(F).(c,d*revfd) by REALSET2:def 18
       .= c*(revfd*d)
       .= c*revfd*d by REALSET2:23;
     thus omf(F).(a,d) = b*(c*revfd)*d by A3
             .= b*(c*revfd*d) by REALSET2:23
             .= omf(F).(b,c) by A4;
   end;
       omf(F).(a,d) = omf(F).(b,c) implies
   omf(F).(a,revf(F).b) = omf(F).(c,revf(F).d)
   proof
    reconsider revfd = revf(F).d, revfb = revf(F).b
     as Element of (the carrier of F)\{0.F};
    reconsider crevfd = omf(F).(c,revfd) as Element of F;
      assume
   A5:omf(F).(a,d) = omf(F).(b,c);
          a = a*1.F by REALSET2:25
       .= omf(F).(a,nmf(F))
       .= a*(d*revfd) by REALSET2:def 18
       .= a*d*revfd by REALSET2:23
       .= b*c*revfd by A5
       .= b*(c*revfd) by REALSET2:23
       .= crevfd*b;
      hence omf(F).(a,revf(F).b)
        = crevfd*b*revfb
       .= crevfd*(b*revfb) by REALSET2:23
       .= omf(F).(omf(F).(c,revf(F).d),nmf(F)) by REALSET2:def 18
       .= omf(F).(c,revfd)*1.F
       .= omf(F).(c,revf(F).d) by REALSET2:25;
   end;
   hence thesis by A1;
end;

theorem
       for F being Field holds
   for a,b being Element of the carrier of F holds
   a*b = 0.F iff a = 0.F or b = 0.F by VECTSP_1:44;

theorem
       for F being Field holds
   for a,b being Element of the carrier of F holds
   for c,d being Element of (the carrier of F)\{0.F} holds
   omf(F).(omf(F).(a,revf(F).c),omf(F).(b,revf(F).d)) =
   omf(F).(omf(F).(a,b),revf(F).(omf(F).(c,d)))
proof
   let F be Field;
   let a,b be Element of the carrier of F;
   let c,d be Element of (the carrier of F)\{0.F};
     reconsider revfc = revf(F).c, revfd = revf(F).d
      as Element of (the carrier of F)\{0.F};
       omf(F).(c,d) is Element of (the carrier of F)\{0.F} by REALSET2:28;
    then reconsider revfcd = revf(F).(c*d) as Element of F by REALSET2:28;
   thus omf(F).(omf(F).(a,revf(F).c),omf(F).(b,revf(F).d))
       = a*revfc*(b*revfd)
      .= a*(revfc*(b*revfd)) by REALSET2:23
      .= a*(b*(revfc*revfd)) by REALSET2:23
      .= omf(F).(a,omf(F).(b,revf(F).(omf(F).(c,d)))) by Th6
      .= a*(b*revfcd)
      .= a*b*revfcd by REALSET2:23
      .= omf(F).(omf(F).(a,b),revf(F).(omf(F).(c,d)));
end;

theorem
       for F being Field holds
   for a,b being Element of the carrier of F holds
   for c,d being Element of (the carrier of F)\{0.F} holds
   (the add of F).(omf(F).(a,revf(F).c),omf(F).(b,revf(F).d)) =
   omf(F).((the add of F).(omf(F).(a,d),omf(F).(b,c)),revf(F).(omf(F).(c,d)))
proof
   let F be Field;
   let a,b be Element of the carrier of F;
   let c,d be Element of (the carrier of F)\{0.F};
   reconsider revfc = revf(F).c, revfd = revf(F).d
            as Element of F by XBOOLE_0:def 4;
A1: omf(F).(c,d) is Element of (the carrier of F)\{0.F} by REALSET2:28;
A2:a = a*1.F by REALSET2:25
          .= omf(F).(a,nmf(F))
          .= a*(d*revfd) by REALSET2:def 18
          .= a*d*revfd by REALSET2:23;
     reconsider revfc = revf(F).c,  revfd = revf(F).d
      as Element of (the carrier of F)\{0.F};
A3:b = b*1.F by REALSET2:25
          .= omf(F).(b,nmf(F))
          .= b*(c*revfc) by REALSET2:def 18
          .= b*c*revfc by REALSET2:23;
A4:omf(F).(a,revf(F).c) = a*d*revfd*revfc by A2
         .= a*d*(revfd*revfc) by REALSET2:23
          .= omf(F).(omf(F).(a,d),revfc*revfd)
          .= omf(F).(omf(F).(a,d),revf(F).(omf(F).(c,d))) by Th6;
     reconsider revfcd = revf(F).(c*d)
           as Element of F by A1,REALSET2:28;
       omf(F).(b,revf(F).d) = b*c*revfc*revfd by A3
         .= b*c*(revfc*revfd) by REALSET2:23
          .= omf(F).(omf(F).(b,c),revf(F).(omf(F).(c,d))) by Th6;

   hence (the add of F).(omf(F).(a,revf(F).c),omf(F).(b,revf(F).d))
    = a*d*revfcd+b*c*revfcd by A4
   .= (a*d+b*c)*revfcd by REALSET2:3
 .= omf(F).((the add of F).(omf(F).(a,d),omf(F).(b,c)),revf(F).(omf(F).(c,d)));
end;

definition ::subtraction
   let F be Field;
   func osf(F) -> BinOp of the carrier of F means
:Def1: for x,y being Element of the carrier of F holds
        it.(x,y) = (the add of F).(x,compf(F).y);
existence
proof
  defpred P[Element of the carrier of F,Element of the carrier of F,set]
          means $3=(the add of F).($1,compf(F).$2);
 A1: for x being Element of the carrier of F
      for y being Element of the carrier of F
      ex z being Element of the carrier of F st P[x,y,z];
       ex f being BinOp of the carrier of F
   st for x being Element of the carrier of F
    for y being Element of the carrier of F holds
   P[x,y,f.(x,y)] from BINOP_1:sch 3(A1);
   then consider S being BinOp of the carrier of F such that
A2:for x,y being Element of the carrier of F holds
   S.(x,y)=(the add of F).(x,compf(F).y);
   take S;
   thus thesis by A2;
end;
uniqueness
proof
   let S1,S2 be BinOp of the carrier of F such that
A3:for x,y being Element of the carrier of F holds
        S1.(x,y) = (the add of F).(x,compf(F).y) and
A4:for x,y being Element of the carrier of F holds
        S2.(x,y) = (the add of F).(x,compf(F).y);
       now let p be set;
      assume p in [:the carrier of F,the carrier of F:];
      then consider x,y being set such that
   A5:x in the carrier of F & y in the carrier of F & p=[x,y]
   by ZFMISC_1:def 2;
      thus S1.p = S1.(x,y) by A5
          .= (the add of F).(x,compf(F).y) by A3,A5
          .= S2.(x,y) by A4,A5
          .= S2.p by A5;
   end;
   hence thesis by FUNCT_2:18;
end;
end;

canceled 2;

theorem
       for F being Field holds
   for x being Element of the carrier of F holds osf(F).(x,x) = 0.F
proof
   let F be Field;
   let x be Element of the carrier of F;
   thus osf(F).(x,x) = (the add of F).(x,compf(F).x) by Def1
               .= x-x by REALSET2:def 17
               .= 0.F by RLVECT_1:16;
end;

theorem Th15:
   for F being Field holds
   for a,b,c being Element of the carrier of F holds
   omf(F).(a,osf(F).(b,c)) = osf(F).(omf(F).(a,b),omf(F).(a,c))
proof
   let F be Field;
   let a,b,c be Element of the carrier of F;
   thus omf(F).(a,osf(F).(b,c)) = omf(F).(a,(the add of F).(b,compf(F).c))
           by Def1
        .= a*(b-c) by REALSET2:def 17
        .= a*b-a*c by REALSET2:15
        .= (the add of F).(omf(F).(a,b),compf(F).(a*c)) by REALSET2:def 17
        .= osf(F).(omf(F).(a,b),omf(F).(a,c)) by Def1;
end;

theorem
    for F being Field holds
   for a,b being Element of the carrier of F holds
   osf(F).(a,b) is Element of the carrier of F;

theorem
       for F being Field holds
   for a,b,c being Element of the carrier of F holds
   omf(F).(osf(F).(a,b),c) =
   osf(F).(omf(F).(a,c),omf(F).(b,c))
proof
   let F be Field;
   let a,b,c be Element of the carrier of F;
   thus omf(F).(osf(F).(a,b),c) =osf(F).(a,b)*c
       .= c*osf(F).(a,b)
      .= omf(F).(c,osf(F).(a,b))
      .= osf(F).(c*a,c*b) by Th15
      .= osf(F).(a*c,b*c)
      .= osf(F).(omf(F).(a,c),omf(F).(b,c));
end;

theorem
       for F being Field holds
   for a,b being Element of the carrier of F holds
   osf(F).(a,b) = compf(F).(osf(F).(b,a))
proof
   let F be Field;
   let a,b be Element of the carrier of F;
       osf(F).(a,b) = (the add of F).(a,compf(F).b) by Def1
               .= a+-b by REALSET2:def 17
               .= -(b-a) by RLVECT_1:47
               .= compf(F).(b+-a) by REALSET2:def 17
               .= compf(F).((the add of F).(b,compf(F).a)) by REALSET2:def 17;
   hence thesis by Def1;
end;

theorem
       for F being Field holds
   for a,b being Element of the carrier of F holds
   osf(F).(compf(F).a,b) = compf(F).((the add of F).(a,b))
proof
   let F be Field;
   let a,b be Element of the carrier of F;
   thus osf(F).(compf(F).a,b) = (the add of F).(compf(F).a,compf(F).b) by Def1
        .= (the add of F).(-a,compf(F).b) by REALSET2:def 17
        .= -a+-b by REALSET2:def 17
        .= -(a+b) by RLVECT_1:45
        .= compf(F).((the add of F).(a,b)) by REALSET2:def 17;
end;

theorem Th20:
   for F being Field holds
   for a,b,c,d being Element of the carrier of F holds
   osf(F).(a,b) = osf(F).(c,d) iff a+d = b+c
proof
   let F be Field;
   let a,b,c,d be Element of the carrier of F;
A1:osf(F).(a,b) = (the add of F).(a,compf(F).b) by Def1
              .= a-b by REALSET2:def 17;
       osf(F).(c,d) = (the add of F).(c,compf(F).d) by Def1
              .= c-d by REALSET2:def 17;
   hence thesis by A1,Th7;
end;

theorem
       for F being Field holds
   for a being Element of the carrier of F holds
   osf(F).(0.F,a) = compf(F).a
proof
   let F be Field;
   let a be Element of the carrier of F;
   thus osf(F).(0.F,a) = 0.F+compf(F).a by Def1
                    .= compf(F).a by REALSET2:6;
end;

theorem Th22:
   for F being Field holds
   for a being Element of the carrier of F holds
   osf(F).(a,0.F) = a
proof
   let F be Field;
   let a be Element of the carrier of F;
   thus osf(F).(a,0.F) = (the add of F).(a,compf(F).0.F) by Def1
                    .= (the add of F).(a,-0.F) by REALSET2:def 17
                    .= a+0.F by RLVECT_1:25
                    .= a by REALSET2:6;
end;

theorem
       for F being Field holds
   for a,b,c being Element of the carrier of F holds
   a+b = c iff osf(F).(c,a) = b
proof
   let F be Field;
   let a,b,c be Element of the carrier of F;
   set d=0.F;
A1: c+d = c by REALSET2:6;
       osf(F).(b,d) = b by Th22;
   hence thesis by A1,Th20;
end;

theorem
       for F being Field holds
   for a,b,c being Element of the carrier of F holds
   a+b = c iff osf(F).(c,b) = a
proof
   let F be Field;
   let a,b,c be Element of the carrier of F;
   set d=0.F;
A1: c+d = c by REALSET2:6;
 osf(F).(a,d) = a by Th22;
   hence thesis by A1,Th20;
end;

theorem
       for F being Field holds
   for a,b,c being Element of the carrier of F holds
   osf(F).(a,osf(F).(b,c)) = (the add of F).(osf(F).(a,b),c)
proof
   let F be Field;
   let a,b,c be Element of the carrier of F;
   thus osf(F).(a,osf(F).(b,c)) = osf(F).(a,(the add of F).(b,compf(F).c))
             by Def1
        .= a+compf(F).(b+compf(F).c) by Def1
        .= a+compf(F).(b+-c) by REALSET2:def 17
        .= a+-(b+-c) by REALSET2:def 17
       .= a+(-b+--c) by RLVECT_1:45
    .= a+(compf(F).b+--c) by REALSET2:def 17
    .= a+(compf(F).b+compf(F).(-c)) by REALSET2:def 17
    .= a+(compf(F).b+compf(F).(compf(F).c))
                by REALSET2:def 17
        .= a+(compf(F).b+c) by REALSET2:13
        .= a+(compf(F).b)+c by REALSET2:4
        .= (the add of F).(osf(F).(a,b),c) by Def1;
end;

theorem
       for F being Field holds
   for a,b,c being Element of the carrier of F holds
   osf(F).(a,(the add of F).(b,c)) = osf(F).(osf(F).(a,b),c)
proof
   let F be Field;
   let a,b,c be Element of the carrier of F;
   thus osf(F).(a,(the add of F).(b,c))
    = (the add of F).(a,compf(F).((the add of F).(b,c))) by Def1
    .= a-(b+c) by REALSET2:def 17
        .= a-b-c by RLVECT_1:41
        .= (the add of F).((the add of F).(a,-b),compf(F).c)
        by REALSET2:def 17
        .= (the add of F).((the add of F).(a,compf(F).b),compf(F).c)
        by REALSET2:def 17
        .= (the add of F).(osf(F).(a,b),compf(F).c) by Def1
        .= osf(F).(osf(F).(a,b),c) by Def1;
end;

definition ::division.
   let F be Field;
   func ovf(F) -> Function of [:the carrier of F,
              (the carrier of F)\{0.F}:],the carrier of F means
:Def2: for x being Element of the carrier of F,
           y being Element of (the carrier of F)\{0.F} holds
       it.(x,y) = omf(F).(x,revf(F).y);
existence
proof
  defpred P[Element of the carrier of F,
            Element of (the carrier of F)\{0.F},set]
          means $3=omf(F).($1,revf(F).$2);
    now let x be Element of the carrier of F,
             y be Element of (the carrier of F)\{0.F};
             revf(F).y is Element of the carrier of F by XBOOLE_0:def 4;
         then reconsider z = omf(F).(x,revf(F).y)
          as Element of the carrier of F
         by REALSET2:14;
         take z;
         thus z = omf(F).(x,revf(F).y);
     end;
then A1:  for x being Element of the carrier of F
 for y being Element of (the carrier of F)\{0.F}
      ex z being Element of the carrier of F st P[x,y,z];
         ex f being Function of [:the carrier of F,
        (the carrier of F)\{0.F}:],the carrier of F
     st for x being Element of the carrier of F,
            y being Element of (the carrier of F)\{0.F} holds P[x,y,f.(x,y)]
      from BINOP_1:sch 3(A1);
     hence thesis;
end;
uniqueness
proof
   let S1,S2 be Function of
    [:the carrier of F,(the carrier of F)\{0.F}:],the carrier of F
   such that
A2:for x being Element of the carrier of F,
       y being Element of (the carrier of F)\{0.F} holds
        S1.(x,y) = omf(F).(x,revf(F).y) and
A3:for x being Element of the carrier of F,
       y being Element of (the carrier of F)\{0.F} holds
        S2.(x,y) = omf(F).(x,revf(F).y);
       now let p be set;
      assume p in [:the carrier of F,(the carrier of F)\{0.F}:];
      then consider x,y being set such that
   A4:x in the carrier of F & y in (the carrier of F)\{0.F} & p=[x,y]
   by ZFMISC_1:def 2;
          S1.(x,y) = omf(F).(x,revf(F).y) by A2,A4
          .= S2.(x,y) by A3,A4;
      hence S1.p = S2.p by A4;
   end;
   hence thesis by FUNCT_2:18;
end;
end;

canceled 2;

theorem Th29:
   for F being Field holds
   for x being Element of (the carrier of F)\{0.F} holds
      ovf(F).(x,x) = nmf(F)
proof
   let F be Field;
   let x be Element of (the carrier of F)\{0.F};
   thus ovf(F).(x,x) = omf(F).(x,revf(F).x) by Def2
               .=nmf(F) by REALSET2:def 18;
end;

theorem
    for F being Field holds
   for a being Element of the carrier of F holds
   for b being Element of (the carrier of F)\{0.F} holds
   ovf(F).(a,b) is Element of the carrier of F;

theorem Th31:
   for F being Field holds
   for a,b being Element of the carrier of F holds
   for c being Element of (the carrier of F)\{0.F} holds
   omf(F).(a,ovf(F).(b,c)) =
   ovf(F).(omf(F).(a,b),c)
proof
   let F be Field;
   let a,b be Element of the carrier of F;
   let c be Element of (the carrier of F)\{0.F};
    reconsider revfc = revf(F).c as Element of F by XBOOLE_0:def 4;
   thus omf(F).(a,ovf(F).(b,c))
        = a*(b*revfc) by Def2
        .= a*b*revfc by REALSET2:23
        .= ovf(F).(omf(F).(a,b),c) by Def2;
end;

theorem
       for F being Field holds
   for a being Element of (the carrier of F)\{0.F} holds
   omf(F).(a,ovf(F).(nmf(F),a)) = nmf(F) &
   omf(F).(ovf(F).(nmf(F),a),a) = nmf(F)
proof
   let F be Field;
   let a be Element of (the carrier of F)\{0.F};
 thus A1:omf(F).(a,ovf(F).(nmf(F),a))
        = ovf(F).(a*1.F,a) by Th31
        .= ovf(F).(a,a) by REALSET2:25
        .= nmf(F) by Th29;
 thus omf(F).(ovf(F).(nmf(F),a),a) = ovf(F).(nmf(F),a)*a
     .= a*ovf(F).(nmf(F),a)
     .= nmf(F) by A1;
end;

canceled 2;

theorem
       for F being Field holds
   for a,b being Element of (the carrier of F)\{0.F} holds
   ovf(F).(a,b) = revf(F).(ovf(F).(b,a))
proof
   let F be Field;
   let a,b be Element of (the carrier of F)\{0.F};
    ovf(F).(a,b) = omf(F).(a,revf(F).b) by Def2
               .= revf(F).(omf(F).(b,revf(F).a)) by Th4;
   hence thesis by Def2;
end;

theorem
       for F being Field holds
   for a,b being Element of (the carrier of F)\{0.F} holds
   ovf(F).(revf(F).a,b) = revf(F).(omf(F).(a,b))
proof
   let F be Field;
   let a,b be Element of (the carrier of F)\{0.F};
       revf(F).a is Element of the carrier of F by XBOOLE_0:def 4;
   hence ovf(F).(revf(F).a,b) = omf(F).(revf(F).a,revf(F).b) by Def2
        .= revf(F).(omf(F).(a,b)) by Th6;
end;

theorem Th37:
   for F being Field holds
   for a,c being Element of the carrier of F holds
   for b,d being Element of (the carrier of F)\{0.F} holds
   ovf(F).(a,b) = ovf(F).(c,d) iff omf(F).(a,d) = omf(F).(b,c)
proof
   let F be Field;
   let a,c be Element of the carrier of F;
   let b,d be Element of (the carrier of F)\{0.F};
A1:ovf(F).(a,b) = omf(F).(a,revf(F).b) by Def2;
       ovf(F).(c,d) = omf(F).(c,revf(F).d) by Def2;
   hence thesis by A1,Th8;
end;

theorem
       for F being Field holds
   for a being Element of (the carrier of F)\{0.F} holds
   ovf(F).(nmf(F),a) = revf(F).a
proof
   let F be Field;
   let a be Element of (the carrier of F)\{0.F};
    reconsider revfa = revf(F).a
      as Element of (the carrier of F)\{0.F};
   thus ovf(F).(nmf(F),a) = omf(F).(nmf(F),revf(F).a) by Def2
                    .= 1.F*revfa
                    .= revf(F).a by REALSET2:10;
end;

theorem Th39:
   for F being Field holds
   for a being Element of the carrier of F holds
   ovf(F).(a,nmf(F)) = a
proof
   let F be Field;
   let a be Element of the carrier of F;
   thus ovf(F).(a,nmf(F)) = omf(F).(a,revf(F).nmf(F)) by Def2
                    .= a*1.F by Th2
                    .= a by REALSET2:25;
end;

theorem
       for F being Field holds
   for a being Element of (the carrier of F)\{0.F} holds
   for b,c being Element of the carrier of F holds
   omf(F).(a,b) = c iff ovf(F).(c,a) = b
proof
   let F be Field;
   let a be Element of (the carrier of F)\{0.F};
   let b,c be Element of the carrier of F;
   set d=nmf(F);
A1:omf(F).(c,d) =c*1.F
     .= c by REALSET2:25;
       ovf(F).(b,d) = b by Th39;
   hence thesis by A1,Th37;
end;

theorem
       for F being Field holds
   for a,c being Element of the carrier of F holds
   for b being Element of (the carrier of F)\{0.F} holds
   omf(F).(a,b) = c iff ovf(F).(c,b) = a
proof
   let F be Field;
   let a,c be Element of the carrier of F;
   let b be Element of (the carrier of F)\{0.F};
   set d=nmf(F);
A1:omf(F).(c,d) = c*1.F
       .= c by REALSET2:25;
A2:ovf(F).(a,d) = a by Th39;
    omf(F).(b,a) = b*a .= a*b
       .= omf(F).(a,b);
   hence thesis by A1,A2,Th37;
end;

theorem
       for F being Field holds
   for a being Element of the carrier of F holds
   for b,c being Element of (the carrier of F)\{0.F} holds
   ovf(F).(a,ovf(F).(b,c)) = omf(F).(ovf(F).(a,b),c)
proof
   let F be Field;
   let a be Element of the carrier of F;
   let b,c be Element of (the carrier of F)\{0.F};
A1:omf(F).(b,revf(F).c) is Element of (the carrier of F)\{0.F}
by REALSET2:28;
    reconsider revfb = revf(F).b as Element of F by XBOOLE_0:def 4;
   thus ovf(F).(a,ovf(F).(b,c)) = ovf(F).(a,omf(F).(b,revf(F).c)) by Def2
        .= omf(F).(a,revf(F).(omf(F).(b,revf(F).c))) by A1,Def2
        .= omf(F).(a,omf(F).(revf(F).b,revf(F).(revf(F).c))) by Th6
        .= a*(revfb*c) by REALSET2:27
        .= a*revfb*c by REALSET2:23
        .= omf(F).(ovf(F).(a,b),c) by Def2;
end;

theorem
       for F being Field holds
   for a being Element of the carrier of F holds
   for b,c being Element of (the carrier of F)\{0.F} holds
   ovf(F).(a,omf(F).(b,c)) = ovf(F).(ovf(F).(a,b),c)
proof
   let F be Field;
   let a be Element of the carrier of F;
   let b,c be Element of (the carrier of F)\{0.F};
   reconsider revfb = revf(F).b, revfc = revf(F).c
                as Element of F by XBOOLE_0:def 4;
omf(F).(b,c) is Element of (the carrier of F)\{0.F} by REALSET2:28;
   hence ovf(F).(a,omf(F).(b,c)) = omf(F).(a,revf(F).(omf(F).(b,c))) by Def2
        .= a*(revfb*revfc) by Th6
        .= a*revfb*revfc by REALSET2:23
        .= omf(F).(ovf(F).(a,b),revf(F).c) by Def2
        .= ovf(F).(ovf(F).(a,b),c) by Def2;
end;
