:: Several Integrability Formulas of Special Functions
::  by Cuiying Peng , Fuguo Ge and Xiquan Liang
:: 
:: Received August 28, 2007
:: Copyright (c) 2007 Association of Mizar Users

environ

 vocabularies BOOLE, INTEGRA1, ARYTM_1, REALSET1, RFUNCT_1, FUNCT_3, ORDINAL2,
      MEASURE5, RCOMP_1, INTEGRA5, FUNCOP_1, INTEGRA8, SIN_COS, PRE_TOPC,
      FUNCT_1, RELAT_1, ARYTM_3, PARTFUN1, FDIFF_1, SEQ_2, FCONT_1, SIN_COS2,
      POWER, SQUARE_1, SIN_COS4, INT_1, SEQ_1, SIN_COS6, ABSVALUE;
 notations TARSKI, XBOOLE_0, FUNCOP_1, INTEGRA1, RFUNCT_1, PSCOMP_1, RELAT_1,
      FUNCT_1, INTEGRA5, SIN_COS, SUBSET_1, PARTFUN1, BORSUK_1, MEMBERED,
      RCOMP_1, NUMBERS, FDIFF_1, TAYLOR_1, XCMPLX_0, XXREAL_0, REAL_1, FCONT_1,
      SEQ_2, RFUNCT_2, SIN_COS2, NAT_1, POWER, SQUARE_1, SEQ_1, SIN_COS6,
      COMPLEX1, FUNCT_2;
 constructors BINOP_2, FINSEQOP, INTEGRA5, SIN_COS, ARYTM_0, COMSEQ_3,
      PARTFUN1, PSCOMP_1, REAL_1, SQUARE_1, RFUNCT_1, FDIFF_1, TAYLOR_1,
      RFUNCT_2, FCONT_1, SERIES_1, SIN_COS2, NAT_1, SIN_COS6, ABSVALUE,
      NUMBERS, SEQ_1;
 registrations INTEGRA1, SEQ_1, SEQM_3, RELSET_1, MEMBERED, RCOMP_1, FDIFF_1,
      INT_1, ORDINAL1, ABSVALUE, XXREAL_0, COMPLEX1, XREAL_0, RFUNCT_1,
      NUMBERS, XBOOLE_0, FUNCOP_1;
 requirements ARITHM, SUBSET, NUMERALS, BOOLE, REAL;
 definitions TARSKI, SIN_COS, INTEGRA5;
 theorems PARTFUN1, INTEGRA1, FUNCT_1, SEQ_1, FDIFF_1, TARSKI, RCOMP_1,
      INTEGRA4, XBOOLE_0, XBOOLE_1, FUNCOP_1, INTEGRA5, FUNCT_3, SIN_COS,
      SIN_COS2, FCONT_1, FCONT_3, SUBSET_1, RFUNCT_1, SEQ_2, RFUNCT_2, IRRAT_1,
      TAYLOR_1, XCMPLX_1, XREAL_1, SQUARE_1, FDIFF_7, SIN_COS6, FDIFF_2,
      SIN_COS5, REAL_1, COMPLEX1, ABSVALUE, INTEGRA2, FUNCT_2;
 schemes NAT_1;

begin :: Preliminaries

 reserve f,f1,f2,g for PartFunc of REAL,REAL;
 reserve A for closed-interval Subset of REAL;
 reserve a,p,r,s,x,x0 for Real;
 reserve n for Element of NAT;
 reserve Z for open Subset of REAL;

Lm1: sin(PI/4) > 0
proof
  PI in ].0, 4.[ by SIN_COS:def 32;
  then A1:0 < PI & PI < 4 by RCOMP_1:47;
  then A2:0/4 < PI/4 by REAL_1:73;
  PI/4 < PI/1 by A1,XREAL_1:78;then
  2*PI*0 < PI/4 & PI/4 < PI+2*PI*0 by A2;
  hence thesis by SIN_COS6:11;
end;

Lm2: sin(-PI/4) < 0
proof
  PI in ].0, 4.[ by SIN_COS:def 32;then
A1:0 < PI & PI < 4 by RCOMP_1:47;
  then 0/4 < PI/4 by REAL_1:73;then
A3:(PI/4)* (-1) < 0*(-1) by XREAL_1:71;
  PI/4 < PI/1 by A1,XREAL_1:78;then
A4:PI*(-1) < (PI/4)*(-1) by XREAL_1:71;
  PI+2*PI*(-1) < r & r < 2*PI+2*PI*(-1) implies sin r < 0 by SIN_COS6:12;
  hence thesis by A3,A4;
end;

theorem Th1:
  sin(x + 2 * n * PI) = sin x
proof
  sin.x = sin.(2*PI*n + x) by SIN_COS2:10;
  hence thesis;
end;

theorem Th2:
  sin(x + (2*n+1)*PI) = -sin x
proof
   defpred X[Element of NAT] means sin(x + (2*$1+1)* PI) = -sin x;
A1:X[0] by SIN_COS:84;
A2:for n st X[n] holds X[n+1]
   proof let n;
   assume
A3:sin(x + (2*n+1)*PI) = -sin x;
   sin(x + (2*(n+1)+1)*PI)=sin((x + (2*n+1)*PI)+2*PI)
   .=sin(x + (2*n+1)*PI)*cos(2*PI) + cos(x + (2*n+1)*PI)*sin(2*PI)
         by SIN_COS:80
   .= -sin(x) by A3,SIN_COS:82;
   hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A1,A2);
   hence thesis;
end;

theorem Th3:
  cos(x + 2 * n* PI) = cos x
proof
  cos.x = cos.(x+2*PI*n) by SIN_COS6:10;
  hence thesis;
end;

theorem Th4:
  cos(x + (2*n+1)*PI) = -cos x
proof
   defpred X[Element of NAT] means cos(x + (2*$1+1)* PI) = -cos x;
A1:X[0] by SIN_COS:84;
A2:for n st X[n] holds X[n+1]
   proof let n;
     assume
A3:  cos(x + (2*n+1)*PI) = -cos x;
     cos(x + (2*(n+1)+1)*PI)=cos((x + (2*n+1)*PI)+2*PI)
      .= cos(x + (2*n+1)*PI)*cos(2*PI) - sin(x + (2*n+1)*PI)*sin(2*PI)
         by SIN_COS:80
      .= -cos(x) by A3,SIN_COS:82;
     hence thesis;
   end;
   for n holds X[n] from NAT_1:sch 1(A1,A2);
   hence thesis;
end;

theorem Th6:
  sin(x/2) >= 0 implies sin(x/2)=sqrt((1-cos(x))/2)
proof
    assume
A1: sin(x/2) >= 0;
    sqrt((1-cos(x))/2)=sqrt((1-cos(2*(x/2)))/2)
   .=sqrt((1-(1-2*(sin(x/2))^2))/2) by SIN_COS5:7
   .=abs(sin(x/2)) by COMPLEX1:158
   .=sin(x/2) by A1,ABSVALUE:def 1;
   hence thesis;
end;

theorem Th7:
  sin(x/2) < 0 implies sin(x/2)=-sqrt((1-cos(x))/2)
proof
    assume
A1: sin(x/2) < 0;
    sqrt((1-cos(x))/2)=sqrt((1-cos(2*(x/2)))/2)
   .=sqrt((1-(1-2*(sin(x/2))^2))/2) by SIN_COS5:7
   .=abs(sin(x/2)) by COMPLEX1:158
   .=-sin(x/2) by A1,ABSVALUE:def 1;
   hence thesis;
end;

theorem Th8:
  sin(PI/4) = sqrt(2)/2
proof
A1:sqrt 2 > 0 by SQUARE_1:93;
   sin((PI/2)/2)=sqrt((1-cos(PI/2))/2) by Lm1,Th6;then
   sin(PI/4) = 1 /sqrt 2 by SQUARE_1:83,SQUARE_1:99,SIN_COS:82
        .= (sqrt(2) * 1)/(sqrt(2)*sqrt(2)) by A1,XCMPLX_1:92
        .= (sqrt(2) * 1)/(sqrt(2))^2 by SQUARE_1:def 3;
   hence thesis by SQUARE_1:def 4;
end;

theorem Th9:
  sin(-PI/4) = -sqrt(2)/2
proof
A1:sqrt 2 > 0 by SQUARE_1:93;
A2:cos(-PI/2) = sin(PI/2-(-PI/2))by SIN_COS:84
             .= 0 by SIN_COS:82;
   sin((-PI/2)/2)=-sqrt((1-cos(-PI/2))/2) by Lm2,Th7;then
           sin(-PI/4)
         = -1 /sqrt 2 by A2,SQUARE_1:83,SQUARE_1:99
        .= -(sqrt(2) * 1)/(sqrt(2)*sqrt(2)) by A1,XCMPLX_1:92
        .= -(sqrt(2) * 1)/(sqrt(2))^2 by SQUARE_1:def 3;
   hence thesis by SQUARE_1:def 4;
end;

theorem Th10:
  [.-sqrt 2/2,sqrt 2/2.] c= ].-1,1.[
proof
    sqrt 2 < sqrt 4 by SQUARE_1:95;then
A1: sqrt 2/2 < 2/2 by XREAL_1:76,SQUARE_1:85;then
A2: sqrt 2/2*(-1) > 1*(-1) by XREAL_1:71;
    sqrt 2 > 0 by SQUARE_1:93;then
A4: sqrt 2/2 > 0 by XREAL_1:141;then
A5: sqrt 2/2 in ].-1,1.[ by A1,RCOMP_1:47;
A6: sqrt 2/2 *(-1) < 0 *(-1) by A4,XREAL_1:71;
    -sqrt 2/2 in ].-1,1.[ by A2,A6,RCOMP_1:47;
    hence thesis by A5,RCOMP_1:17;
end;

theorem Th11:
  arcsin (sqrt 2/2) = PI/4
proof
    PI in ].0, 4.[ by SIN_COS:def 32;then
A1: 0 < PI & PI < 4 by RCOMP_1:47;then
A2: PI/4 < PI/2 by XREAL_1:78;
A3: 0/4 < PI/4 by A1,XREAL_1:76;
    0/2 < PI/2 by A1,XREAL_1:76;then
    (PI/2)*(-1) < 0*(-1) by XREAL_1:71;
    hence thesis by Th8,A2,A3,SIN_COS6:70;
end;

theorem Th12:
  arcsin (-sqrt 2/2) = -PI/4
proof
    PI in ].0, 4.[ by SIN_COS:def 32;then
A1: 0 < PI & PI < 4 by RCOMP_1:47;then
    PI/4 < PI/2 by XREAL_1:78;then
A2: (PI/4)*(-1) > (PI/2)*(-1) by XREAL_1:71;
    0/4 < PI/4 by A1,XREAL_1:76;then
B2:0*(-1) > (PI/4)*(-1) by XREAL_1:71;
    0/2 < PI/2 by A1,XREAL_1:76;
    hence thesis by B2,Th9,SIN_COS6:70,A2;
end;

theorem Th13:
  cos(x/2) >=0 implies cos(x/2)=sqrt((1+cos x)/2)
proof
    assume
A1: cos(x/2) >=0;
    sqrt((1+cos(x))/2)=sqrt((1+cos(2*(x/2)))/2)
   .=sqrt((1+(2*(cos(x/2))^2-1))/2) by SIN_COS5:7
   .=abs(cos(x/2)) by COMPLEX1:158
   .= cos(x/2) by A1,ABSVALUE:def 1;
   hence thesis;
end;

Th14: cos(PI/4) > 0
proof
  PI in ].0, 4.[ by SIN_COS:def 32;then
A1:0 < PI & PI < 4 by RCOMP_1:47;then
A2:0/4 < PI/4 by REAL_1:73;
   0/2 < PI/2 by A1,REAL_1:73;then
   (PI/2)*(-1) < 0*(-1) by XREAL_1:71;then
   -PI/2 + 2*PI*0 < PI/4 & PI/4 < PI/2+2*PI*0 by A1,XREAL_1:78,A2;
   hence thesis by SIN_COS6:13;
end;

theorem Th15:
  cos (PI/4) = sqrt 2/2
proof
A1:sqrt 2 > 0 by SQUARE_1:93;
   cos ((PI/2)/2) = sqrt((1+cos(PI/2))/2) by Th14,Th13
   .= 1 /sqrt 2 by SQUARE_1:83,SQUARE_1:99,SIN_COS:82
   .= (sqrt(2) * 1)/(sqrt(2)*sqrt(2)) by A1,XCMPLX_1:92
   .= (sqrt(2) * 1)/(sqrt(2))^2 by SQUARE_1:def 3;
   hence thesis by SQUARE_1:def 4;
end;

theorem Th16:
  cos(3*PI/4) = -sqrt 2 /2
proof
  cos(3*PI/4) = cos(PI/2 + PI/4)
  .= -sqrt 2/2 by Th8,SIN_COS:84;
  hence thesis;
end;

theorem Th17:
  arccos (sqrt 2 /2) = PI/4
proof
    PI in ].0,4.[ by SIN_COS:def 32;then
A1: 0 < PI & PI < 4 by RCOMP_1:47;then
A2: PI/4 < PI/1 by XREAL_1:78;
    0/4 < PI/4 by A1,XREAL_1:76;
    hence thesis by Th15,A2,SIN_COS6:94;
end;

theorem Th18:
  arccos (-sqrt 2 /2) = 3*PI/4
proof
    PI in ].0,4.[ by SIN_COS:def 32;then
A1: 0 < PI & PI < 4 by RCOMP_1:47;then
    PI/4 >0 & PI/2 > 0 by XREAL_1:141;then
A3: 0 < PI/4 + PI/2 by XREAL_1:36;
    (3/4)*PI < 1*PI by A1,XREAL_1:70;
    hence thesis by Th16,A3,SIN_COS6:94;
end;

theorem Th19:
  sinh.1 = (number_e^2 - 1)/(2*number_e)
proof
  sinh.1=(number_e - exp_R(-1))/2 by SIN_COS2:def 1,IRRAT_1:def 7
   .=(number_e/1 - 1/number_e)/2 by IRRAT_1:def 7,TAYLOR_1:4
   .=((number_e * number_e)/(1*number_e) - 1/number_e)/2
            by XCMPLX_1:92,TAYLOR_1:11
   .=((number_e)^2/number_e - 1/number_e)/2 by SQUARE_1:def 3
   .=(number_e^2 - 1)/number_e/2 by XCMPLX_1:121
   .=(number_e^2 - 1)/(2*number_e) by XCMPLX_1:79;
   hence thesis;
end;

theorem Th20:
  cosh.0 = 1
proof
  cosh.0 = (exp_R.0+exp_R.(-0))/2 by SIN_COS2:def 3
      .= 1 by SIN_COS:56;
  hence thesis;
end;

theorem Th21:
  cosh.1 = (number_e^2 + 1)/(2*number_e)
proof
  cosh.1=(number_e + exp_R(-1))/2 by SIN_COS2:def 3,IRRAT_1:def 7
   .=(number_e/1 + 1/number_e)/2 by IRRAT_1:def 7,TAYLOR_1:4
   .=((number_e * number_e)/(1*number_e) + 1/number_e)/2
            by XCMPLX_1:92,TAYLOR_1:11
   .=((number_e)^2/number_e + 1/number_e)/2 by SQUARE_1:def 3
   .=(number_e^2 + 1)/number_e/2 by XCMPLX_1:63
   .=(number_e^2 + 1)/(2*number_e) by XCMPLX_1:79;
   hence thesis;
end;

Lm3B:
  (-f1)(#)(-f2) = f1(#)f2
proof
  (-f1)(#)(-f2) = (-1)(#)f1(#)(-f2) by RFUNCT_1:38
  .= (-1)(#)(f1(#)(-f2)) by RFUNCT_1:24
  .= f1(#)((-1)(#)(-f2)) by RFUNCT_1:25
  .= f1(#)(-(-f2)) by RFUNCT_1:38;
  hence thesis;
end;

Lm3C:
  (-f1)(#)f2 = -(f1(#)f2)
proof
  (-f1)(#)f2 = (-1)(#)f1(#)f2 by RFUNCT_1:38
  .=(-1)(#)(f1(#)f2) by RFUNCT_1:24;
   hence thesis by RFUNCT_1:38;
end;

theorem Th26:
  for L1 being LINEAR holds -L1 is LINEAR
proof
  let L1 be LINEAR;
  consider g1 being Real such that
A1: for p holds L1.p = g1*p by FDIFF_1:def 4;
A3: L1 is total by FDIFF_1:def 4;then
A4: -L1 is total by RFUNCT_1:68;
  now let p;
    thus (-L1).p = -L1.p by A3,RFUNCT_1:74
     .= -g1*p by A1
     .= (-g1)*p;
   end; hence -L1 is LINEAR by A4,FDIFF_1:def 4;
end;

theorem Th27:
  for R1 being REST holds -R1 is REST
proof
   let R1 be REST;
A1:R1 is total by FDIFF_1:def 3;
A2:-R1 is total by A1,RFUNCT_1:68;then
B1:dom R1 = REAL & dom (-R1) = REAL by A1,PARTFUN1:def 4;
Lma: for h being convergent_to_0 Real_Sequence holds
     -(R1*h) = (-R1)*h
  proof
    let h be convergent_to_0 Real_Sequence;
    rng h c= dom R1 by B1,SEQ_1:def 1;
    hence thesis by RFUNCT_2:25;
  end;
   now let h be convergent_to_0 Real_Sequence;
A3:(h")(#)(R1*h) is convergent & lim ((h")(#)(R1*h)) = 0 by FDIFF_1:def 3;
A5:(h")(#)((-R1)*h) = (h")(#)(-(R1*h)) by Lma
     .= (h")(#)((-1)(#)(R1*h)) by SEQ_1:25
     .= (-1)(#) ((h")(#)(R1*h)) by SEQ_1:27
     .= - ((h")(#)(R1*h)) by SEQ_1:25;
   hence (h")(#)((-R1)*h) is convergent by A3,SEQ_2:23;
   thus lim ((h")(#)((-R1)*h))
        = - (lim ((h")(#)((R1)*h))) by A3,A5,SEQ_2:24
       .= 0 by A3;
  end; hence -R1 is REST by A2,FDIFF_1:def 3;
end;

theorem Th28:
  for f1,x0 st f1 is_differentiable_in x0
  holds -f1 is_differentiable_in x0 & diff(-f1,x0)=-diff(f1,x0)
proof
    let f1,x0;
    assume
A1: f1 is_differentiable_in x0;
    consider N1 be Neighbourhood of x0 such that
A3: N1 c= dom f1 &
    ex L being LINEAR,R being REST st for x st x in N1 holds
    f1.x - f1.x0 = L.(x-x0) + R.(x-x0) by A1,FDIFF_1:def 5;
    consider L1 being LINEAR,R1 being REST such that
A4: for x st x in N1 holds f1.x - f1.x0 = L1.(x-x0) + R1.(x-x0) by A3;
    consider N be Neighbourhood of x0 such that
A7: N c= N1;
    reconsider L=-L1 as LINEAR by Th26;
A8: L1 is total by FDIFF_1:def 4;
    reconsider R=-R1 as REST by Th27;
A9: R1 is total by FDIFF_1:def 3;
    N c= dom f1 by A3,A7,XBOOLE_1:1;then
A11: N c= dom -f1 by SEQ_1:def 7;
A14: x0 in N by RCOMP_1:37;
A12: now let x; assume
A13: x in N; 
     hence (-f1).x - (-f1).x0 = -(f1.x) - (-f1).x0 by A11,SEQ_1:def 7
       .= -(f1.x) - -(f1.x0) by A11,A14,SEQ_1:def 7
       .= -((f1.x) -(f1.x0))
       .= -(L1.(x-x0) + R1.(x-x0)) by A4,A7,A13
       .= -L1.(x-x0) + -R1.(x-x0)
       .= L.(x-x0)+-R1.(x-x0) by A8,RFUNCT_1:74
       .= L.(x-x0)+R.(x-x0) by A9,RFUNCT_1:74;
  end;
 hence -f1 is_differentiable_in x0 by A11,FDIFF_1:def 5;
 hence diff(-f1,x0)=L.1 by A11,A12,FDIFF_1:def 6
  .=-L1.1 by A8,RFUNCT_1:74
  .=-diff(f1,x0) by A1,A3,A4,FDIFF_1:def 6;
end;

theorem Th29:
  for f1,Z st Z c= dom (-f1) & f1 is_differentiable_on Z
  holds -f1 is_differentiable_on Z &
  for x st x in Z holds ((-f1)`|Z).x = -diff(f1,x)
proof
   let f1,Z;
   assume that
A1:Z c= dom (-f1) and
A2:f1 is_differentiable_on Z;
   now let x0; assume x0 in Z;then
A5:f1 is_differentiable_in x0 by A2,FDIFF_1:16;
   thus -f1 is_differentiable_in x0 by A5,Th28;
   end;
   hence
A6: -f1 is_differentiable_on Z by A1,FDIFF_1:16;
   now let x; assume
A7: x in Z;then
A8: f1 is_differentiable_in x by A2,FDIFF_1:16;
    thus ((-f1)`|Z).x = diff((-f1),x) by A6,A7,FDIFF_1:def 8
  .= -diff(f1,x) by A8,Th28;
  end; hence thesis;
end;

LmX0: for f being Function of REAL, REAL holds dom (-f) = REAL
proof
  let f be Function of REAL, REAL;
  dom f = REAL by FUNCT_2:def 1;
  hence thesis by SEQ_1:def 7;
end;

theorem Th30:
  -sin is_differentiable_on REAL
proof
B1:dom (-sin) = REAL by LmX0;
   REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
   hence thesis by B1,Th29,SIN_COS:73;
end;

theorem Th31:
  -cos is_differentiable_in x & diff(-cos,x) = sin.x
proof
A1: cos is_differentiable_in x by SIN_COS:68;
    diff(-cos,x) = -diff(cos,x) by A1,Th28
    .= - -sin.x by SIN_COS:68;
    hence thesis by A1,Th28;
end;

theorem Th32:
  -cos is_differentiable_on REAL &
  for x st x in REAL holds diff(-cos,x)=sin.x
proof
A1: dom (-cos) = REAL by LmX0;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by Th31,A1,Th29,SIN_COS:72;
end;

theorem Th33:
  sin`|REAL = cos
proof
A1: dom cos = REAL by SIN_COS:def 22;
    (for x st x in REAL holds diff(sin,x)=cos.x) by SIN_COS:73;
    hence thesis by SIN_COS:73,FDIFF_1:def 8,A1;
end;

theorem Th34:
  cos`|REAL = -sin
proof
A2:dom (-sin) = REAL by LmX0;
A3:dom (cos`|REAL) = dom (-sin) by FDIFF_1:def 8,SIN_COS:72,A2;
   for x being Element of REAL st x in dom (cos`|REAL) holds
     (cos`|REAL).x = (-sin).x
   proof
    let x be Element of REAL;
    assume x in dom (cos`|REAL);
    (cos`|REAL).x = diff(cos,x) by SIN_COS:72,FDIFF_1:def 8
      .= -sin.x by SIN_COS:68;
    hence thesis by A2,SEQ_1:def 7;
   end;
   hence thesis by A3,PARTFUN1:34;
end;

theorem Th35:
  (-cos)`|REAL = sin
proof
  dom sin = REAL by SIN_COS:def 20;
  hence thesis by Th32,FDIFF_1:def 8;
end;

theorem Th36:
  sinh`|REAL = cosh
proof
A1: dom cosh = REAL by SIN_COS2:def 3;
    (for x st x in REAL holds diff(sinh,x)=cosh.x) by SIN_COS2:34;
    hence thesis by SIN_COS2:34,FDIFF_1:def 8,A1;
end;

theorem Th37:
  cosh`|REAL = sinh
proof
A1: dom sinh = REAL by SIN_COS2:def 1;
    (for x st x in REAL holds diff(cosh,x)=sinh.x) by SIN_COS2:35;
    hence thesis by SIN_COS2:35,FDIFF_1:def 8,A1;
end;

theorem Th38:
  exp_R`|REAL = exp_R
proof
A1: dom exp_R = REAL by SIN_COS:def 26;
    (for x st x in REAL holds diff(exp_R,x)=exp_R.x) by SIN_COS:71;
    hence thesis by A1,SIN_COS:71,FDIFF_1:def 8;
end;

theorem Th39:
  Z c= dom tan &
  (for x st x in Z holds f.x = 1/(cos.x)^2 & cos.x <> 0) implies
  tan is_differentiable_on Z &
  for x st x in Z holds ((tan)`|Z).x = 1/(cos.x)^2
proof
   assume
A1:Z c= dom tan &
   (for x st x in Z holds f.x = 1/(cos.x)^2 & cos.x <> 0);
B1:for x st x in Z holds tan is_differentiable_in x
   proof
    let x;
    assume
   A3:x in Z;
   A4:sin is_differentiable_in x by SIN_COS:69;
   A5:cos is_differentiable_in x by SIN_COS:68;
      cos.x <> 0 by A1,A3;
      hence tan is_differentiable_in x by A4,A5,FDIFF_2:14;
   end;then
A6:tan is_differentiable_on Z by A1,FDIFF_1:16;
   for x st x in Z holds ((tan)`|Z).x = 1/(cos.x)^2
   proof
    let x;
    assume
   A7:x in Z;
   A8:sin is_differentiable_in x by SIN_COS:69;
   A9:cos is_differentiable_in x by SIN_COS:68;
   A10:cos.x <> 0 by A1,A7;
      diff (tan,x) = (diff(sin,x) * cos.x - diff(cos,x)*sin.x)/(cos.x)^2
        by A8,A9,A10,FDIFF_2:14
    .=((cos.x)*cos.x - diff(cos,x)*sin.x)/(cos.x)^2 by SIN_COS:69
    .=((cos.x)*(cos.x)-(-sin.x)*(sin.x))/(cos.x)^2 by SIN_COS:68
    .=((cos.x)*(cos.x) + (sin.x)*(sin.x))/(cos.x)^2
    .=1/(cos.x)^2 by SIN_COS:31;
    hence thesis by A6,A7,FDIFF_1:def 8;
  end;
  hence thesis by A1,B1,FDIFF_1:16;
end;

theorem Th40:
  Z c= dom cot &
  (for x st x in Z holds f.x = -1/(sin.x)^2 & sin.x <> 0) implies
  cot is_differentiable_on Z &
  for x st x in Z holds (cot`|Z).x = -1/(sin.x)^2
proof
   assume
A1:Z c= dom cot &
   (for x st x in Z holds f.x = -1/(sin.x)^2 & sin.x <> 0);
B1:for x st x in Z holds cot is_differentiable_in x
   proof
    let x;
    assume
   A3:x in Z;
   A4:sin is_differentiable_in x by SIN_COS:69;
   A5:cos is_differentiable_in x by SIN_COS:68;
      sin.x <> 0 by A1,A3;
      hence cot is_differentiable_in x by A4,A5,FDIFF_2:14;
   end;then
A6:cot is_differentiable_on Z by A1,FDIFF_1:16;
   for x st x in Z holds (cot`|Z).x = -1/(sin.x)^2
   proof
    let x;
    assume
   A7:x in Z;
   A8:sin is_differentiable_in x by SIN_COS:69;
   A9:cos is_differentiable_in x by SIN_COS:68;
   A10:sin.x <> 0 by A1,A7;
       diff (cot,x)=(diff(cos,x) * sin.x - diff(sin,x)*cos.x)/(sin.x)^2
        by A8,A9,A10,FDIFF_2:14
    .=((-sin.x)*sin.x - diff(sin,x)*cos.x)/(sin.x)^2 by SIN_COS:68
    .=(-(sin.x)*(sin.x)-(cos.x)*(cos.x))/(sin.x)^2 by SIN_COS:69
    .=(-((sin.x)*(sin.x) + (cos.x)*(cos.x) ))/(sin.x)^2
    .=(-((sin.x)*(sin.x) + (cos.x)^2 ))/(sin.x)^2 by SQUARE_1:def 3
    .=(-((sin.x)^2 + (cos.x)^2 ))/(sin.x)^2 by SQUARE_1:def 3
    .=-((cos.x)^2 + (sin.x)^2)/(sin.x)^2by XCMPLX_1:188
    .=-1/(sin.x)^2 by SIN_COS:31;
    hence thesis by A6,A7,FDIFF_1:def 8;
  end;
  hence thesis by A1,B1,FDIFF_1:16;
end;

theorem Th41:
  for r being Real holds dom (REAL --> r)=REAL &
  rng (REAL --> r) c= REAL
proof
  let r be Real;
  thus dom (REAL --> r) = REAL by FUNCOP_1:19;
A1: rng (REAL --> r) c= {r} by FUNCOP_1:19;
  {r} c= REAL
  proof
    let x be set;
    assume x in {r};then
    x=r by TARSKI:def 1;
    hence x in REAL;
  end;
  hence thesis by A1,XBOOLE_1:1;
end;

definition let r be Real;
  func Cst(r) -> Function of REAL,REAL equals
    REAL --> r;
  coherence by FUNCOP_1:58;
end;

theorem Th43:
  for a,b being Real, A being closed-interval Subset of REAL holds
    chi(A,A)=(Cst(1))|A
proof let a,b be Real, A be closed-interval Subset of REAL;
   dom ((Cst(1))|A) = A &
   for x being set st x in A
     holds (x in A implies ((Cst(1))|A).x = 1)
     & (not x in A implies ((Cst(1))|A).x = 0)
   proof
A2: dom (Cst(1))=REAL by Th41;
A3: dom ((Cst(1))|A)=dom (Cst(1)) /\ A by FUNCT_1:68;
    thus dom ((Cst(1))|A)=A by A2,A3,XBOOLE_1:28;
     let x be set;
     assume B1: x in A;
      per cases;
      suppose x in A;
        x in dom ((Cst(1))|A) by A2,B1,A3,XBOOLE_0:def 3;then
        ((Cst 1)|A).x = (REAL-->1).x by FUNCT_1:68
                      .= 1 by FUNCOP_1:13,B1;
        hence thesis by B1;
      end;
      suppose not x in A;
       hence thesis by B1;
      end;
   end;
 hence chi(A,A)=(Cst(1))|A by FUNCT_3:def 3;
end;

theorem Th45:
  for a,b being Real, A being closed-interval Subset of REAL
  st A=[.a,b.] holds sup A=b & inf A=a
proof
 let a,b be Real, A be closed-interval Subset of REAL;
 assume A1: A=[.a,b.];
 A=[. inf A, sup A .] by INTEGRA1:5;
 hence sup A=b & inf A=a by A1,INTEGRA1:6;
end;

begin :: Integrability formulas of special functions

theorem
  for a,b being Real st a<=b holds
    integral(Cst(1),a,b)=b-a
proof let a,b be Real;
 assume a<=b; then
  A10: [.a,b.]=[' a,b '] by INTEGRA5:def 4;
  reconsider A=[.a,b.] as non empty closed-interval Subset of REAL by A10;
  A2: integral(Cst(1),a,b)=integral(Cst(1),A) by INTEGRA5:19;
  A7: (Cst(1))||A = chi(A,A) by Th43;
  sup A=b & inf A=a by Th45;then
  vol(A)=b-a by INTEGRA1:def 6;
 hence integral(Cst(1),a,b)=b-a by A7,A2,INTEGRA4:2;
end;

Lm3:cos is_continuous_on A
proof
   cos is_continuous_on REAL by SIN_COS:72,FDIFF_1:33;
   hence thesis by FCONT_1:17;
end;

Lm3A:cos is_integrable_on A & cos is_bounded_on A
proof
   cos is_continuous_on A by Lm3;
   hence thesis by INTEGRA5:10,11;
end;

theorem Th46:
  integral(cos,A) = sin.(sup A)-sin.(inf A)
proof
A1:cos is_integrable_on A & cos is_bounded_on A by Lm3A;
A2:dom (sin`|REAL) = REAL by FDIFF_1:def 8,SIN_COS:73;
A3:dom (sin`|REAL) = dom cos by SIN_COS:def 22,A2;
A4:for x being Element of REAL st x in dom (sin`|REAL) holds
   (sin`|REAL).x = cos.x
   proof
    let x be Element of REAL;
    assume x in dom (sin`|REAL);
    (sin`|REAL).x = diff(sin,x) by SIN_COS:73,FDIFF_1:def 8;
    hence thesis by SIN_COS:69;
   end;
   sin`|REAL = cos by A3,A4,PARTFUN1:34;
   hence thesis by A1,SIN_COS:73,INTEGRA5:13;
end;

theorem
  A = [.0,PI/2.] implies integral(cos,A) = 1
proof
    assume A=[.0,PI/2.];then
A2: sup A=PI/2 & inf A=0 by Th45;
    integral(cos,A) = 1 - sin.0 by SIN_COS:82,A2,Th46
   .= 1 - cos.(PI/2 - 0) by SIN_COS:83
   .= 1 - 0 by SIN_COS:82;
   hence thesis;
end;

theorem
  A = [.0,PI.] implies integral(cos,A) = 0
proof
    assume A=[.0,PI.];then
A2: sup A=PI & inf A=0 by Th45;
    integral(cos,A) = 0 - sin.0 by SIN_COS:81,A2,Th46
   .= 0 - sin.(0+2*PI) by SIN_COS:83
   .= 0 - 0 by SIN_COS:81;
   hence thesis;
end;

theorem
  A = [.0,PI*3/2.] implies integral(cos,A) = -1
proof
    assume A=[.0,PI*3/2.];then
A2: sup A=PI*3/2 & inf A=0 by Th45;
    integral(cos,A) = -1 - sin.0 by SIN_COS:81,A2,Th46
   .= -1 - sin.(0+2*PI) by SIN_COS:83
   .= -1 - 0 by SIN_COS:81;
   hence thesis;
end;

theorem
  A= [.0,PI*2.] implies integral(cos,A) = 0
proof
    assume A=[.0,PI*2.];then
A2: sup A=PI*2 & inf A=0 by Th45;
    integral(cos,A) = 0 - sin.0 by SIN_COS:81,A2,Th46
   .= 0 - sin.(0+2*PI) by SIN_COS:83
   .= 0 - 0 by SIN_COS:81;
   hence thesis;
end;

theorem
  A = [.2*n*PI,(2*n+1)*PI.] implies integral(cos,A) = 0
proof
    assume A=[.2*n*PI,(2*n+1)*PI.];then
A2: sup A=(2*n+1)*PI & inf A=2*n*PI by Th45;
    integral(cos,A) = sin(0+(2*n+1)*PI) - sin(0+2*n*PI) by A2,Th46
   .= -sin(0) - sin(0+2*n*PI) by Th2
   .= -sin(0) - sin(0) by Th1
   .= -sin(0+2 * PI) - sin(0) by SIN_COS:84
   .= 0 - 0 by SIN_COS:82,84;
   hence thesis;
end;

theorem
  A = [.x+2*n*PI,x+(2*n+1)*PI.] implies integral(cos,A) = -2*sin(x)
proof
    assume A=[.x+2*n*PI,x+(2*n+1)*PI.];then
A2: sup A=x+(2*n+1)*PI & inf A=x+2*n*PI by Th45;
    integral(cos,A) = sin(x+(2*n+1)*PI) - sin(x+2*n*PI) by A2,Th46
   .= -sin(x) - sin(x+2*n*PI) by Th2
   .= -sin(x) - sin(x) by Th1
   .= -2*sin(x);
   hence thesis;
end;

Lm4:-sin is_continuous_on A
proof
   -sin is_continuous_on REAL by Th30,FDIFF_1:33;
   hence thesis by FCONT_1:17;
end;

Lm4A:-sin is_integrable_on A & -sin is_bounded_on A
proof
  -sin is_continuous_on A by Lm4;
  hence thesis by INTEGRA5:10,11;
end;

theorem Th53:
  integral(-sin,A) = cos.(sup A)-cos.(inf A)
proof
A1:-sin is_integrable_on A & -sin is_bounded_on A by Lm4A;
B1:dom (-sin) = REAL by LmX0;
A5:dom (cos`|REAL) = dom (-sin) by FDIFF_1:def 8,SIN_COS:72,B1;
A6:for x being Element of REAL st x in dom (cos`|REAL) holds
   (cos`|REAL).x = (-sin).x
   proof
    let x be Element of REAL;
    assume x in dom (cos`|REAL);
    (cos`|REAL).x = diff(cos,x) by SIN_COS:72,FDIFF_1:def 8
    .= -sin.x by SIN_COS:68;
    hence thesis by B1,SEQ_1:def 7;
   end;
   cos`|REAL = -sin by A5,A6,PARTFUN1:34;
   hence thesis by A1,SIN_COS:72,INTEGRA5:13;
end;

theorem
  A = [.0,PI/2.] implies integral(-sin,A) = -1
proof
    assume A=[.0,PI/2.];then
A2: sup A=PI/2 & inf A=0 by Th45;
    integral(-sin,A) = 0 - cos.0 by SIN_COS:81,A2,Th53
   .= 0 - sin.(PI/2 - 0) by SIN_COS:83
   .= 0 - 1 by SIN_COS:81;
   hence thesis;
end;

theorem
  A = [.0,PI.] implies integral(-sin,A) = -2
proof
    assume A=[.0,PI.];then
A2: sup A=PI & inf A=0 by Th45;
    integral(-sin,A) = -1 - cos.0 by SIN_COS:81,A2,Th53
   .= -1 - sin.(PI/2 - 0) by SIN_COS:83
   .= -1 - 1 by SIN_COS:81;
   hence thesis;
end;

theorem
  A = [.0,PI*3/2.] implies integral(-sin,A) = -1
proof
    assume A=[.0,PI*3/2.];then
A2: sup A=PI*3/2 & inf A=0 by Th45;
    integral(-sin,A) = 0 - cos.0 by SIN_COS:81,A2,Th53
   .= 0 - sin.(PI/2 - 0) by SIN_COS:83
   .= 0 - 1 by SIN_COS:81;
   hence thesis;
end;

theorem
  A = [.0,PI*2.] implies integral(-sin,A) = 0
proof
   assume A=[.0,PI*2.];then
A2:sup A=PI*2 & inf A=0 by Th45;
   integral(-sin,A) = 1 - cos.0 by SIN_COS:81,A2,Th53
   .= 1 - sin.(PI/2 - 0) by SIN_COS:83
   .= 1 - 1 by SIN_COS:81;
   hence thesis;
end;

theorem
  A = [.2*n*PI,(2*n+1)*PI.] implies integral(-sin,A) = -2
proof
    assume A=[.2*n*PI,(2*n+1)*PI.];then
A2: sup A=(2*n+1)*PI & inf A=2*n*PI by Th45;
    integral(-sin,A) = cos(0+(2*n+1)*PI) - cos(0+2*n*PI) by A2,Th53
   .= -cos(0) - cos(0+2*n*PI) by Th4
   .= -cos(0) - cos(0) by Th3
   .= -cos(0+2 * PI) - cos(0) by SIN_COS:84
   .= -1 - 1 by SIN_COS:82,84;
   hence thesis;
end;

theorem
  A = [.x+2*n*PI,x+(2*n+1)*PI.] implies integral(-sin,A) = -2*cos(x)
proof
    assume A=[.x+2*n*PI,x+(2*n+1)*PI.];then
A2: sup A=x+(2*n+1)*PI & inf A=x+2*n*PI by Th45;
    integral(-sin,A) = cos(x+(2*n+1)*PI) - cos(x+2*n*PI) by A2,Th53
   .= -cos(x) - cos(x+2*n*PI) by Th4
   .= -cos(x) - cos(x) by Th3
   .= -2*cos(x);
   hence thesis;
end;

Lm5:exp_R is_continuous_on A
proof
   exp_R is_continuous_on REAL by SIN_COS:71,FDIFF_1:33;
   hence thesis by FCONT_1:17;
end;

Lm5A:
  exp_R is_integrable_on A & exp_R is_bounded_on A
proof
  exp_R is_continuous_on A by Lm5;
  hence thesis by INTEGRA5:10,11;
end;

theorem Th60:
  integral(exp_R,A) = exp_R.(sup A)-exp_R.(inf A)
proof
A1:exp_R is_integrable_on A & exp_R is_bounded_on A by Lm5A;
A2:dom (exp_R`|REAL) = REAL by FDIFF_1:def 8,SIN_COS:71;
A3:dom (exp_R`|REAL) = dom exp_R by SIN_COS:def 26,A2;
A4:for x being Element of REAL st x in dom (exp_R`|REAL) holds
   (exp_R`|REAL).x = exp_R.x
   proof
    let x be Element of REAL;
    assume x in dom (exp_R`|REAL);
    (exp_R`|REAL).x = diff(exp_R,x) by SIN_COS:71,FDIFF_1:def 8;
    hence thesis by SIN_COS:70;
   end;
   exp_R`|REAL = exp_R by A3,A4,PARTFUN1:34;
   hence thesis by A1,SIN_COS:71,INTEGRA5:13;
end;

theorem
  A = [.0,1.] implies integral(exp_R,A) = number_e -1
proof
  assume A=[.0,1.];then
  sup A=1 & inf A=0 by Th45;
  hence thesis by IRRAT_1:def 7,SIN_COS:56,Th60;
end;

Lm6:sinh is_continuous_on A
proof
  sinh is_continuous_on REAL by SIN_COS2:34,FDIFF_1:33;
  hence thesis by FCONT_1:17;
end;

Lm6A:
  sinh is_integrable_on A & sinh is_bounded_on A
proof
  sinh is_continuous_on A by Lm6;
  hence thesis by INTEGRA5:10,11;
end;

theorem The1:
  integral(sinh,A) = cosh.(sup A)-cosh.(inf A)
proof
A1:sinh is_integrable_on A & sinh is_bounded_on A by Lm6A;
A2:dom (cosh`|REAL) = REAL by FDIFF_1:def 8,SIN_COS2:35;
A3:dom (cosh`|REAL) = dom sinh by SIN_COS2:def 1,A2;
A4:for x being Element of REAL st x in dom (cosh`|REAL) holds
   (cosh`|REAL).x = sinh.x
   proof
    let x be Element of REAL;
    assume x in dom (cosh`|REAL);
    (cosh`|REAL).x = diff(cosh,x) by SIN_COS2:35,FDIFF_1:def 8;
    hence thesis by SIN_COS2:35;
   end;
   cosh`|REAL = sinh by A3,A4,PARTFUN1:34;
   hence thesis by A1,SIN_COS2:35,INTEGRA5:13;
end;

theorem
  A= [.0,1.] implies integral(sinh,A) = (number_e - 1)^2/(2*number_e)
proof
    assume A=[.0,1.];then
A2: sup A=1 & inf A=0 by Th45;
A3: 2*number_e > 0
    proof
      exp_R(1)>0 by SIN_COS:60;
      hence thesis by IRRAT_1:def 7,XREAL_1:131;
    end;
    integral(sinh,A)
    = (number_e^2 + 1)/(2*number_e) - 1 by A2,The1,Th20,Th21
   .= (number_e^2 + 1)/(2*number_e) - (2*number_e)/(2*number_e)
       by XCMPLX_1:60,A3
   .= ((number_e^2 + 1) - (2*number_e))/(2*number_e) by XCMPLX_1:121
   .= (number_e^2 - 2*number_e*1 + 1^2)/(2*number_e) by SQUARE_1:59
   .= (number_e - 1)^2/(2*number_e) by SQUARE_1:64;
   hence thesis;
end;

Lm7:cosh is_continuous_on A
proof
   cosh is_continuous_on REAL by SIN_COS2:35,FDIFF_1:33;
   hence thesis by FCONT_1:17;
end;

Lm7A:
  cosh is_integrable_on A & cosh is_bounded_on A
proof
  cosh is_continuous_on A by Lm7;
  hence thesis by INTEGRA5:10,11;
end;

theorem Th64:
  integral(cosh,A) = sinh.(sup A)-sinh.(inf A)
proof
A1:cosh is_integrable_on A & cosh is_bounded_on A by Lm7A;
A2:dom (sinh`|REAL) = REAL by FDIFF_1:def 8,SIN_COS2:34;
A3:dom (sinh`|REAL) = dom cosh by SIN_COS2:def 3,A2;
A4:for x being Element of REAL st x in dom (sinh`|REAL) holds
   (sinh`|REAL).x = cosh.x
   proof
    let x be Element of REAL;
    assume x in dom (sinh`|REAL);
    (sinh`|REAL).x = diff(sinh,x) by SIN_COS2:34,FDIFF_1:def 8;
    hence thesis by SIN_COS2:34;
   end;
   sinh`|REAL = cosh by A3,A4,PARTFUN1:34;
   hence thesis by A1,SIN_COS2:34,INTEGRA5:13;
end;

theorem
  A= [.0,1.] implies integral(cosh,A) = (number_e^2 - 1)/(2*number_e)
proof
    assume A=[.0,1.];then
A2: sup A=1 & inf A=0 by Th45;
    integral(cosh,A) = sinh.1 -0 by SIN_COS2:16,A2,Th64
   .= (number_e^2 - 1)/(2*number_e) by Th19;
   hence thesis;
end;

theorem
  A c= Z & dom tan = Z & dom tan = dom f2 &
  (for x st x in Z holds f2.x = 1/(cos.x)^2 & cos.x <> 0) &
  f2 is_continuous_on A implies
  integral(f2,A) = tan.(sup A)-tan.(inf A)
proof
   assume
A1:A c= Z & dom tan = Z & dom tan = dom f2 &
  (for x st x in Z holds f2.x = 1/(cos.x)^2 & cos.x <> 0) &
  f2 is_continuous_on A;then
B1:f2 is_integrable_on A & f2 is_bounded_on A by INTEGRA5:10,11;
A2:tan is_differentiable_on Z by A1,Th39;
A3:for x st x in dom (tan`|Z) holds
   (tan`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((tan)`|Z); then
     B2: x in Z by A2,FDIFF_1:def 8; then
     ((tan)`|Z).x = 1/(cos.x)^2 by A1,Th39
      .= f2.x by A1,B2;
     hence thesis;
   end;
   dom (tan`|Z) = dom f2 by A1,A2,FDIFF_1:def 8;then
   (tan`|Z) = f2 by A3,PARTFUN1:34;
   hence thesis by A1,A2,B1,INTEGRA5:13;
end;

theorem
  A c= Z & dom cot = Z & dom cot = dom f2 &
  (for x st x in Z holds f2.x = -1/(sin.x)^2 & sin.x <> 0) &
  f2 is_continuous_on A implies
  integral(f2,A) = cot.(sup A)-cot.(inf A)
proof
   assume
A1:A c= Z & dom cot = Z & dom cot = dom f2 &
  (for x st x in Z holds f2.x = -1/(sin.x)^2 & sin.x <> 0) &
  f2 is_continuous_on A;then
B1:f2 is_integrable_on A & f2 is_bounded_on A by INTEGRA5:10,11;
A2:cot is_differentiable_on Z by A1,Th40;
A3:for x st x in dom (cot`|Z) holds
   (cot`|Z).x = f2.x
   proof
     let x;
     assume x in dom ((cot)`|Z); then
 B2: x in Z by A2,FDIFF_1:def 8; then
     (cot`|Z).x = -1/(sin.x)^2 by A1,Th40
      .= f2.x by A1,B2;
     hence thesis;
   end;
   dom (cot`|Z) = dom f2 by A1,A2,FDIFF_1:def 8;then
   (cot`|Z) = f2 by A3,PARTFUN1:34;
   hence thesis by A1,A2,B1,INTEGRA5:13;
end;

theorem
  dom tanh = dom f2 &
  (for x st x in REAL holds f2.x = 1/(cosh.x)^2) &
  f2 is_continuous_on A implies
  integral(f2,A) = tanh.(sup A)-tanh.(inf A)
proof
   assume
A1:dom tanh = dom f2 &
  (for x st x in REAL holds f2.x = 1/(cosh.x)^2) &
  f2 is_continuous_on A;then
B1:f2 is_integrable_on A & f2 is_bounded_on A by INTEGRA5:10,11;
B2:dom tanh = REAL by SIN_COS2:def 5;
A3:for x st x in dom (tanh`|REAL) holds
   (tanh`|REAL).x = f2.x
   proof
     let x;
     assume x in dom (tanh`|REAL);
     (tanh`|REAL).x =diff(tanh,x) by SIN_COS2:36,FDIFF_1:def 8
      .= 1/(cosh.x)^2 by SIN_COS2:33
      .= f2.x by A1;
     hence thesis;
   end;
   dom ((tanh)`|REAL) = dom f2 by A1,SIN_COS2:36,FDIFF_1:def 8,B2;then
   ((tanh)`|REAL) = f2 by A3,PARTFUN1:34;
   hence thesis by SIN_COS2:36,B1,INTEGRA5:13;
end;

theorem Th40:
  A c=].-1,1.[ & dom ((arcsin)`|].-1,1.[) = dom f2 &
  (for x holds x in ].-1,1.[ & f2.x = 1/sqrt(1-x^2)) &
  f2 is_continuous_on A implies
  integral(f2,A) = arcsin.(sup A)-arcsin.(inf A)
proof
   assume
A1:A c=].-1,1.[ & dom ((arcsin)`|].-1,1.[) = dom f2 &
  (for x holds x in ].-1,1.[ & f2.x = 1/sqrt(1-x^2)) &
  f2 is_continuous_on A;then
A2:f2 is_integrable_on A & f2 is_bounded_on A by INTEGRA5:10,11;
A3:for x st x in dom ((arcsin)`|].-1,1.[) holds
   ((arcsin)`|].-1,1.[).x = f2.x
   proof
     let x;
     assume
  B1:x in dom ((arcsin)`|].-1,1.[);
  B2:dom ((arcsin)`|].-1,1.[ ) = ].-1,1.[ by SIN_COS6:84,FDIFF_1:def 8;then
  B3:-1 < x & x < 1 by B1,RCOMP_1:47;
     ((arcsin)`|].-1,1.[).x
       =diff(arcsin,x) by B1,B2,SIN_COS6:84,FDIFF_1:def 8
      .= 1 / sqrt(1-x^2) by B3,SIN_COS6:84
      .= f2.x by A1;
      hence thesis;
   end;
   (arcsin`|].-1,1.[) = f2 by A1,A3,PARTFUN1:34;
   hence thesis by A1,SIN_COS6:84,A2,INTEGRA5:13;
end;

theorem Th41:
  A c=].-1,1.[ & dom (arccos`|].-1,1.[) = dom f2 &
  (for x holds x in ].-1,1.[ & f2.x = -1/sqrt(1-x^2)) &
  f2 is_continuous_on A implies
  integral(f2,A) = arccos.(sup A)-arccos.(inf A)
proof
   assume
A1:A c=].-1,1.[ & dom ((arccos)`|].-1,1.[) = dom f2 &
   (for x holds x in ].-1,1.[ & f2.x = -1/sqrt(1-x^2)) &
   f2 is_continuous_on A;then
A2:f2 is_integrable_on A & f2 is_bounded_on A by INTEGRA5:10,11;
A3:for x st x in dom ((arccos)`|].-1,1.[) holds
   ((arccos)`|].-1,1.[).x = f2.x
   proof
     let x;
     assume
  B1:x in dom (arccos`|].-1,1.[);
  B2:dom (arccos`|].-1,1.[ ) = ].-1,1.[ by SIN_COS6:108,FDIFF_1:def 8;then
  B3:-1 < x & x < 1 by B1,RCOMP_1:47;
     ((arccos)`|].-1,1.[).x
       =diff(arccos,x) by B1,B2,SIN_COS6:108,FDIFF_1:def 8
      .= -1 / sqrt(1-x^2) by B3,SIN_COS6:108
      .= f2.x by A1;
      hence thesis;
   end;
   ((arccos)`|].-1,1.[) = f2 by A1,A3,PARTFUN1:34;
   hence thesis by A1,SIN_COS6:108,A2,INTEGRA5:13;
end;

theorem
  A = [.-sqrt 2/2,sqrt 2/2.] & dom ((arcsin)`|].-1,1.[) = dom f2 &
  (for x holds x in ].-1,1.[ & f2.x = 1/sqrt(1-x^2)) &
  f2 is_continuous_on A implies
  integral(f2,A) = PI/2
proof
    assume
A1: A = [.-sqrt 2/2,sqrt 2/2.] & dom ((arcsin)`|].-1,1.[) = dom f2 &
    (for x holds x in ].-1,1.[ & f2.x = 1/sqrt(1-x^2)) &
    f2 is_continuous_on A;
A2: sup A = sqrt 2/2 & inf A = -sqrt 2/2 by A1,Th45;
    integral(f2,A)
     = arcsin.(sqrt 2/2) - arcsin.(-sqrt 2/2) by A2,A1,Th40,Th10
    .= arcsin(sqrt 2/2) - arcsin.(-sqrt 2/2) by SIN_COS6:def 2
    .= arcsin(sqrt 2/2) - arcsin(-sqrt 2/2) by SIN_COS6:def 2
    .=(2*PI)/4 by Th11,Th12;
    hence thesis;
end;

theorem
  A = [.-sqrt 2/2,sqrt 2/2.] & dom ((arccos)`|].-1,1.[) = dom f2 &
  (for x holds x in ].-1,1.[ & f2.x = -1/sqrt(1-x^2)) &
  f2 is_continuous_on A implies
  integral(f2,A) = -PI/2
proof
    assume
A1: A = [.-sqrt 2/2,sqrt 2/2.] & dom ((arccos)`|].-1,1.[) = dom f2 &
    (for x holds x in ].-1,1.[ & f2.x = -1/sqrt(1-x^2)) &
    f2 is_continuous_on A;
A2: sup A = sqrt 2/2 & inf A = -sqrt 2/2 by A1,Th45;
    integral(f2,A)
     = arccos.(sqrt 2/2)-arccos.(-sqrt 2/2) by A2,Th41,A1,Th10
    .= arccos(sqrt 2/2) - arccos.(-sqrt 2/2) by SIN_COS6:def 4
    .= arccos(sqrt 2/2) - arccos(-sqrt 2/2) by SIN_COS6:def 4
    .=(-2*PI)/4 by Th17,Th18;
    hence thesis;
end;

theorem Th73:
  f is_differentiable_on Z & g is_differentiable_on Z & A c= Z &
  f`|Z is_integrable_on A & f`|Z is_bounded_on A & g`|Z is_integrable_on A &
  g`|Z is_bounded_on A implies
  integral(f`|Z+g`|Z,A) = f.(sup A) - f.(inf A) + g.(sup A) - g.(inf A)
proof
    assume that
A1: f is_differentiable_on Z and
A2: g is_differentiable_on Z and
A3: A c= Z and
A4: f`|Z is_integrable_on A and
A5: f`|Z is_bounded_on A and
A6: g`|Z is_integrable_on A and
A7: g`|Z is_bounded_on A;
    f is_continuous_on Z & g is_continuous_on Z by A1,A2,FDIFF_1:33;then
    f is_continuous_on A & g is_continuous_on A by A3,FCONT_1:17;
    then f is_integrable_on A & g is_integrable_on A by INTEGRA5:11;then
A10:f||A is_integrable_on A & g||A is_integrable_on A &
    (f`|Z)||A is_integrable_on A & (g`|Z)||A is_integrable_on A
     by A4,A6,INTEGRA5:def 2;
    A c= dom(f`|Z) & A c= dom(g`|Z) by A1,A2,A3,FDIFF_1:def 8;
    then (f`|Z)||A is total & (g`|Z)||A is total by INTEGRA5:6;then
A16: (f`|Z)||A is Function of A,REAL & (g`|Z)||A is Function of A,REAL
     by FUNCT_2:131;
B1: (f`|Z)||A is_bounded_on A & (g`|Z)||A is_bounded_on A
         by A5,A7,INTEGRA5:9;
    integral(f`|Z+g`|Z,A)
    = integral((f`|Z)||A+(g`|Z)||A) by INTEGRA5:5
   .= integral(f`|Z,A) + integral(g`|Z,A) by A10,A16,B1,INTEGRA1:59
   .= f.(sup A) - f.(inf A)+ integral(g`|Z,A) by A1,A3,A4,A5,INTEGRA5:13
   .= f.(sup A) - f.(inf A)+ (g.(sup A) - g.(inf A))
      by A2,A3,A6,A7,INTEGRA5:13;
   hence thesis;
end;

theorem Th74:
  f is_differentiable_on Z & g is_differentiable_on Z & A c= Z &
  f`|Z is_integrable_on A & f`|Z is_bounded_on A & g`|Z is_integrable_on A &
  g`|Z is_bounded_on A implies
  integral(f`|Z-g`|Z,A) = f.(sup A) - f.(inf A) - (g.(sup A) - g.(inf A))
proof
    assume that
A1: f is_differentiable_on Z and
A2: g is_differentiable_on Z and
A3: A c= Z and
A4: f`|Z is_integrable_on A and
A5: f`|Z is_bounded_on A and
A6: g`|Z is_integrable_on A and
A7: g`|Z is_bounded_on A;
    f is_continuous_on Z & g is_continuous_on Z by A1,A2,FDIFF_1:33;then
    f is_continuous_on A & g is_continuous_on A by A3,FCONT_1:17;
    then f is_integrable_on A & g is_integrable_on A by INTEGRA5:11;then
A10:f||A is_integrable_on A & g||A is_integrable_on A &
    (f`|Z)||A is_integrable_on A & (g`|Z)||A is_integrable_on A
     by A4,A6,INTEGRA5:def 2;
    A c= dom(f`|Z) & A c= dom(g`|Z) by A1,A2,A3,FDIFF_1:def 8;
    then (f`|Z)||A is total & (g`|Z)||A is total by INTEGRA5:6;then
A16: (f`|Z)||A is Function of A,REAL & (g`|Z)||A is Function of A,REAL
      by FUNCT_2:131;
B1: (f`|Z)||A is_bounded_on A & (g`|Z)||A is_bounded_on A
         by A5,A7,INTEGRA5:9;
    integral(f`|Z-g`|Z,A) = integral((f`|Z)||A-(g`|Z)||A) by RFUNCT_1:63
   .= integral(f`|Z,A) - integral((g`|Z)||A) by A10,A16,B1,INTEGRA2:33
   .= f.(sup A) - f.(inf A)- integral(g`|Z,A) by A1,A3,A4,A5,INTEGRA5:13
   .= f.(sup A) - f.(inf A)- (g.(sup A) - g.inf A) by A2,A3,A6,A7,INTEGRA5:13;
   hence thesis;
end;

theorem Th75:
  f is_differentiable_on Z & A c= Z &
  f`|Z is_integrable_on A & f`|Z is_bounded_on A implies
  integral((r(#)(f`|Z)),A) = r*f.(sup A) - r*f.(inf A)
proof
    assume that
A1: f is_differentiable_on Z and
A2: A c= Z and
A3: f`|Z is_integrable_on A and
A4: f`|Z is_bounded_on A;
    f is_continuous_on Z by A1,FDIFF_1:33;then
    f is_continuous_on A by A2,FCONT_1:17;
    then f is_integrable_on A by INTEGRA5:11;then
A10:f||A is_integrable_on A &
    (f`|Z)||A is_integrable_on A by A3,INTEGRA5:def 2;
    A c= dom(f`|Z) by A1,A2,FDIFF_1:def 8;
    then (f`|Z)||A is total by INTEGRA5:6;then
A16: (f`|Z)||A is Function of A,REAL by FUNCT_2:131;
B1: (f`|Z)||A is_bounded_on A by A4,INTEGRA5:9;
    integral((r(#)(f`|Z)),A) = integral (r(#)((f`|Z)||A)) by RFUNCT_1:65
    .= r * integral ((f`|Z),A) by A10,A16,B1,INTEGRA2:31
    .= r*(f.(sup A) - f.(inf A)) by A1,A2,A3,A4,INTEGRA5:13;
    hence thesis;
end;

Lm8:sin is_continuous_on A
proof
  sin is_continuous_on REAL by SIN_COS:73,FDIFF_1:33;
  hence thesis by FCONT_1:17;
end;

Lm8A:
  sin is_integrable_on A & sin is_bounded_on A
proof
  sin is_continuous_on A by Lm8;
  hence thesis by INTEGRA5:10,11;
end;

theorem Th76:
  integral(sin+cos,A) =
    (-cos).(sup A) - (-cos).(inf A) + sin.(sup A) - sin.(inf A)
proof
A1: cos is_integrable_on A & cos is_bounded_on A by Lm3A; 
A2: sin is_integrable_on A & sin is_bounded_on A by Lm8A;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A1,A2,Th32,Th33,Th35,SIN_COS:73,Th73;
end;

theorem
  A = [.0,PI/2.] implies integral(sin+cos,A) = 2
proof
    assume A = [.0,PI/2.];then
A2: sup A=PI/2 & inf A=0 by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin+cos,A)
     = (-cos).(PI/2) - (-cos).0 + sin.(PI/2) - sin.0 by A2,Th76
    .= -(cos.(PI/2)) - (-cos).0 + sin.(PI/2) - sin.0 by A3,SEQ_1:def 7
    .= -0 - (-cos.0) + 1 - sin.0 by SIN_COS:81,A3,SEQ_1:def 7
    .= - (-cos.0) + 1 - sin.(0+2 * PI) by SIN_COS:83
    .= - (-cos.(0+2*PI)) + 1 - 0 by SIN_COS:81,83
    .= 2 by SIN_COS:81;
    hence thesis;
end;

theorem
  A = [.0,PI.] implies integral(sin+cos,A) = 2
proof
    assume A = [.0,PI.];then
A2: sup A=PI & inf A=0 by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin+cos,A)
     = (-cos).(PI) - (-cos).0 + sin.(PI) - sin.0 by A2,Th76
    .= -(cos.(PI)) - (-cos).0 + sin.(PI) - sin.0 by A3,SEQ_1:def 7
    .= -(cos.(PI)) - (-cos.0) + sin.(PI) - sin.0 by A3,SEQ_1:def 7
    .= - (-cos.0) + 1 - sin.(0+2 * PI) by SIN_COS:81,83
    .= - (-cos.(0+2*PI)) + 1 - 0 by SIN_COS:81,83
    .= 2 by SIN_COS:81;
    hence thesis;
end;

theorem
  A = [.0,PI*3/2.] implies integral(sin+cos,A) = 0
proof
    assume A = [.0,PI*3/2.];then
A2: sup A=PI*3/2 & inf A=0 by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin+cos,A)
     = (-cos).(PI*3/2) - (-cos).0 + sin.(PI*3/2) - sin.0 by A2,Th76
    .= -(cos.(PI*3/2)) - (-cos).0 + sin.(PI*3/2) - sin.0 by A3,SEQ_1:def 7
    .= -(cos.(PI*3/2)) - (-cos.0) + sin.(PI*3/2) - sin.0 by A3,SEQ_1:def 7
    .= -(cos.(PI*3/2)) - (-cos.(0+2*PI)) + sin.(PI*3/2) - sin.0 by SIN_COS:83
    .= -0 +1 + -1 - 0 by SIN_COS:81,83;
    hence thesis;
end;

theorem
  A = [.0,PI*2.] implies integral(sin+cos,A) = 0
proof
    assume A = [.0,PI*2.];then
A2: sup A=PI*2 & inf A=0 by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin+cos,A)
     = (-cos).(PI*2) - (-cos).0 + sin.(PI*2) - sin.0 by A2,Th76
    .= -(cos.(PI*2)) - (-cos).0 + sin.(PI*2) - sin.0 by A3,SEQ_1:def 7
    .= -(cos.(PI*2)) - (-cos.0) + sin.(PI*2) - sin.0 by A3,SEQ_1:def 7
    .= -(cos.(PI*2)) - (-cos.(0+2*PI)) + sin.(PI*2) - sin.0 by SIN_COS:83
    .= -1+1 + 0 - 0 by SIN_COS:81,83;
    hence thesis;
end;

theorem
  A = [.2*n*PI,(2*n+1)*PI.] implies integral(sin+cos,A) = 2
proof
    assume A = [.2*n*PI,(2*n+1)*PI.];then
A2: sup A=(2*n+1)*PI & inf A=(2*n*PI) by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin+cos,A)
     = (-cos).((2*n+1)*PI) - (-cos).(2*n*PI) + sin.((2*n+1)*PI) - sin.(2*n*PI)
        by A2,Th76
    .= -(cos.((2*n+1)*PI)) - (-cos).(2*n*PI) + sin.((2*n+1)*PI) - sin.(2*n*PI)
        by A3,SEQ_1:def 7
    .= -(cos.((2*n+1)*PI)) - -(cos(0+2*n*PI)) +
       sin.((2*n+1)*PI) - sin.(0+2*n*PI) by A3,SEQ_1:def 7
    .= -(cos.((2*n+1)*PI)) - -(cos(0)) +
       sin.((2*n+1)*PI) - sin.(0+2*n*PI) by Th3
    .= -(cos.((2*n+1)*PI)) + (cos(0)) +
       sin.((2*n+1)*PI) - sin.(0+2*n*PI)
    .= -(cos.((2*n+1)*PI)) + (cos(0+2*PI)) +
       sin.((2*n+1)*PI) - sin.(0+2*n*PI) by SIN_COS:84
    .= -(cos.((2*n+1)*PI)) + 1 +
       sin.((2*n+1)*PI) - sin(0+2*n*PI) by SIN_COS:82
    .= -(cos.((2*n+1)*PI)) + 1 +
       sin.((2*n+1)*PI) - sin(0) by Th1
    .= -(cos.((2*n+1)*PI)) + 1 +
       sin.((2*n+1)*PI) - sin(0+2*PI) by SIN_COS:84
    .= -(cos(0+(2*n+1)*PI)) + 1 + sin(0+(2*n+1)*PI) by SIN_COS:82
    .= -(cos(0+(2*n+1)*PI)) + 1 + -sin(0) by Th2
    .= - -cos(0) + 1 + -sin(0) by Th4
    .= cos(0) + 1 - sin(0)
    .= cos(0+2*PI) + 1 - sin(0) by SIN_COS:84
    .= cos(0+2*PI) + 1 - sin(0+2*PI) by SIN_COS:84
    .= 2 by SIN_COS:82;
    hence thesis;
end;

theorem
  A = [.x+2*n*PI,x+(2*n+1)*PI.] implies
    integral(sin+cos,A) = 2*cos x - 2*sin x
proof
    assume A = [.x+2*n*PI,x+(2*n+1)*PI.];then
A2: sup A=x+(2*n+1)*PI & inf A=x+2*n*PI by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin+cos,A)
     = (-cos).(x+(2*n+1)*PI) - (-cos).(x+2*n*PI) +
       sin.(x+(2*n+1)*PI) - sin.(x+2*n*PI) by A2,Th76
    .= -(cos.(x+(2*n+1)*PI)) - (-cos).(x+2*n*PI) +
       sin.(x+(2*n+1)*PI) - sin.(x+2*n*PI) by A3,SEQ_1:def 7
    .= -(cos(x+(2*n+1)*PI)) - -(cos(x+2*n*PI)) +
       sin(x+(2*n+1)*PI) - sin(x+2*n*PI) by A3,SEQ_1:def 7
    .= -(-cos x) - -(cos(x+2*n*PI)) +
       sin(x+(2*n+1)*PI) - sin(x+2*n*PI) by Th4
    .= -(-cos x) - -cos x + sin(x+(2*n+1)*PI) - sin(x+2*n*PI) by Th3
    .= -(-cos x) - -cos x + -sin x - sin(x+2*n*PI) by Th2
    .= cos x + cos x  -sin x - sin x by Th1
    .= 2* cos x - 2*sin x;
    hence thesis;
end;

theorem Th83:
  integral(sinh+cosh,A) =
    cosh.(sup A) - cosh.(inf A) + sinh.(sup A) - sinh.(inf A)
proof
    cosh is_continuous_on A by Lm7;then
A1: cosh is_integrable_on A & cosh is_bounded_on A by INTEGRA5:10,11;
    sinh is_continuous_on A by Lm6;then
A2: sinh is_integrable_on A & sinh is_bounded_on A by INTEGRA5:10,11;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A1,A2,Th36,Th37,SIN_COS2:34,35,Th73;
end;

theorem
  A = [.0,1.] implies integral(sinh+cosh,A) = number_e -1
proof
    assume A = [.0,1.];then
A1: sup A=1 & inf A=0 by Th45;
A2: number_e > 0 by SIN_COS:60,IRRAT_1:def 7;
    integral(sinh+cosh,A)
    = cosh.1 - cosh.0 + sinh.1 - sinh.0 by A1,Th83
   .= (number_e^2 + 1)/(2*number_e) + (number_e^2 - 1)/(2*number_e) - 1
       by Th20,Th21,Th19,SIN_COS2:16
   .= ((number_e^2 + 1) + (number_e^2 - 1))/(2*number_e) -1 by XCMPLX_1:63
   .= (2*number_e^2)/(2*number_e) -1
   .= number_e^2/number_e -1 by XCMPLX_1:92
   .= number_e*number_e/number_e -1 by SQUARE_1:def 3
   .= number_e -1 by XCMPLX_1:90,A2;
   hence thesis;
end;

theorem Th85:
  integral(sin-cos,A) =
    (-cos).(sup A) - (-cos).(inf A) - (sin.(sup A) - sin.(inf A))
proof
A1: cos is_integrable_on A & cos is_bounded_on A by Lm3A; 
A2: sin is_integrable_on A & sin is_bounded_on A by Lm8A;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A1,A2,Th32,Th33,Th35,SIN_COS:73,Th74;
end;

theorem
  A = [.0,PI/2.] implies integral(sin-cos,A) = 0
proof
    assume A = [.0,PI/2.];then
A2: sup A=PI/2 & inf A=0 by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin-cos,A)
     = (-cos).(PI/2) - (-cos).0 - (sin.(PI/2) - sin.0) by A2,Th85
    .= -(cos.(PI/2)) - (-cos).0 - (sin.(PI/2) - sin.0) by A3,SEQ_1:def 7
    .= -0 - (-cos.0) - (1 - sin.0) by SIN_COS:81,A3,SEQ_1:def 7
    .= - (-cos.0) - (1 - sin.(0+2 * PI)) by SIN_COS:83
    .= - (-cos.(0+2*PI)) - (1 - 0) by SIN_COS:81,83
    .= 0 by SIN_COS:81;
    hence thesis;
end;

theorem
  A = [.0,PI.] implies integral(sin-cos,A) = 2
proof
    assume A = [.0,PI.];then
A2: sup A=PI & inf A=0 by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin-cos,A)
     = (-cos).(PI) - (-cos).0 - (sin.(PI) - sin.0) by A2,Th85
    .= -(cos.(PI)) - (-cos).0 - (sin.(PI) - sin.0) by A3,SEQ_1:def 7
    .= -(cos.(PI)) - (-cos.0) - (sin.(PI) - sin.0) by A3,SEQ_1:def 7
    .= - (-cos.0) + 1 - (sin.(0+2 * PI)-0) by SIN_COS:81,83
    .= - (-cos.(0+2*PI)) + 1 - 0 by SIN_COS:81,83
    .= 2 by SIN_COS:81;
    hence thesis;
end;

theorem
  A = [.0,PI*3/2.] implies integral(sin-cos,A) = 2
proof
    assume A = [.0,PI*3/2.];then
A2: sup A=PI*3/2 & inf A=0 by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin-cos,A)
     = (-cos).(PI*3/2) - (-cos).0 - (sin.(PI*3/2) - sin.0) by A2,Th85
    .= -(cos.(PI*3/2)) - (-cos).0 - (sin.(PI*3/2) - sin.0) by A3,SEQ_1:def 7
    .= -(cos.(PI*3/2)) - (-cos.0) - (sin.(PI*3/2) - sin.0) by A3,SEQ_1:def 7
    .= -(cos.(PI*3/2)) - (-cos.(0+2*PI)) - (sin.(PI*3/2) - sin.0) by SIN_COS:83
    .= -0 +1 - (-1 - 0) by SIN_COS:81,83;
    hence thesis;
end;

theorem
  A = [.0,PI*2.] implies integral(sin-cos,A) = 0
proof
    assume A = [.0,PI*2.];then
A2: sup A=PI*2 & inf A=0 by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin-cos,A)
     = (-cos).(PI*2) - (-cos).0 - (sin.(PI*2) - sin.0) by A2,Th85
    .= -(cos.(PI*2)) - (-cos).0 - (sin.(PI*2) - sin.0) by A3,SEQ_1:def 7
    .= -(cos.(PI*2)) - (-cos.0) - (sin.(PI*2) - sin.0) by A3,SEQ_1:def 7
    .= -(cos.(PI*2)) - (-cos.(0+2*PI)) - (sin.(PI*2) - sin.0) by SIN_COS:83
    .= -1+1 - (0 - 0) by SIN_COS:81,83;
    hence thesis;
end;

theorem
  A = [.2*n*PI,(2*n+1)*PI.] implies integral(sin-cos,A) = 2
proof
    assume A = [.2*n*PI,(2*n+1)*PI.];then
A2: sup A=(2*n+1)*PI & inf A=(2*n*PI) by Th45;
A3: dom (-cos) = REAL by LmX0;
     integral(sin-cos,A)
   = (-cos).((2*n+1)*PI) - (-cos).(2*n*PI) - (sin.((2*n+1)*PI) - sin.(2*n*PI))
       by A2,Th85
  .= -(cos.((2*n+1)*PI)) - (-cos).(2*n*PI) - (sin.((2*n+1)*PI) - sin.(2*n*PI))
        by A3,SEQ_1:def 7
  .= -(cos.((2*n+1)*PI)) - -(cos(0+2*n*PI)) -
       (sin.((2*n+1)*PI) - sin.(0+2*n*PI)) by A3,SEQ_1:def 7
  .= -(cos.((2*n+1)*PI)) - -(cos(0)) -
      (sin.((2*n+1)*PI) - sin.(0+2*n*PI)) by Th3
  .= -(cos.((2*n+1)*PI)) + (cos(0)) -
      (sin.((2*n+1)*PI) - sin.(0+2*n*PI))
  .= -(cos.((2*n+1)*PI)) + (cos(0+2*PI)) -
      (sin.((2*n+1)*PI) - sin.(0+2*n*PI)) by SIN_COS:84
  .= -(cos.((2*n+1)*PI)) + 1 -
      (sin.((2*n+1)*PI) - sin(0+2*n*PI)) by SIN_COS:82
  .= -(cos.((2*n+1)*PI)) + 1 -
      (sin.((2*n+1)*PI) - sin(0)) by Th1
  .= -(cos.((2*n+1)*PI)) + 1 -
      (sin.((2*n+1)*PI) - sin(0+2*PI)) by SIN_COS:84
  .= -(cos(0+(2*n+1)*PI)) + 1 - sin(0+(2*n+1)*PI) by SIN_COS:82
  .= -(cos(0+(2*n+1)*PI)) + 1 - -sin(0) by Th2
  .= - -cos(0) + 1 - -sin(0) by Th4
  .= cos(0) + 1 + sin(0)
  .= cos(0+2*PI) + 1 + sin(0) by SIN_COS:84
  .= cos(0+2*PI) + 1 + sin(0+2*PI) by SIN_COS:84
  .= 2 by SIN_COS:82;
    hence thesis;
end;

theorem
  A = [.x+2*n*PI,x+(2*n+1)*PI.] implies
    integral(sin-cos,A) = 2*cos x + 2*sin x
proof
    assume A = [.x+2*n*PI,x+(2*n+1)*PI.];then
A2: sup A=x+(2*n+1)*PI & inf A=x+2*n*PI by Th45;
A3: dom (-cos) = REAL by LmX0;
    integral(sin-cos,A)
     = (-cos).(x+(2*n+1)*PI) - (-cos).(x+2*n*PI) -
       (sin.(x+(2*n+1)*PI) - sin.(x+2*n*PI)) by A2,Th85
    .= -(cos.(x+(2*n+1)*PI)) - (-cos).(x+2*n*PI) -
       (sin.(x+(2*n+1)*PI) - sin.(x+2*n*PI)) by A3,SEQ_1:def 7
    .= -(cos(x+(2*n+1)*PI)) - -(cos(x+2*n*PI)) -
       (sin(x+(2*n+1)*PI) - sin(x+2*n*PI)) by A3,SEQ_1:def 7
    .= -(-cos x) - -(cos(x+2*n*PI)) -
       (sin(x+(2*n+1)*PI) - sin(x+2*n*PI)) by Th4
    .= -(-cos x) - -cos x -
       (sin(x+(2*n+1)*PI) - sin(x+2*n*PI)) by Th3
    .= -(-cos x) - -cos x - (-sin x - sin(x+2*n*PI)) by Th2
    .= cos x + cos x - (-sin x - sin x) by Th1
    .= 2* cos x + 2*sin x;
    hence thesis;
end;

theorem
  integral(r(#)sin,A) = r*(-cos).(sup A) - r*(-cos).(inf A)
proof
    sin is_continuous_on A by Lm8;then
A2: sin is_integrable_on A & sin is_bounded_on A by INTEGRA5:10,11;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A2,Th32,Th35,Th75;
end;

theorem
  integral(r(#)cos,A) = r*sin.(sup A) - r*sin.(inf A)
proof
A2: cos is_integrable_on A & cos is_bounded_on A by Lm3A;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A2,Th33,SIN_COS:73,Th75;
end;

theorem
  integral(r(#)sinh,A) = r*cosh.(sup A) - r*cosh.(inf A)
proof
    sinh is_continuous_on A by Lm6;then
A2: sinh is_integrable_on A & sinh is_bounded_on A by INTEGRA5:10,11;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A2,SIN_COS2:35,Th37,Th75;
end;

theorem
  integral(r(#)cosh,A) = r*sinh.(sup A) - r*sinh.(inf A)
proof
    cosh is_continuous_on A by Lm7;then
A2: cosh is_integrable_on A & cosh is_bounded_on A by INTEGRA5:10,11;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A2,SIN_COS2:34,Th36,Th75;
end;

theorem
  integral(r(#)exp_R,A) = r*exp_R.(sup A) - r*exp_R.(inf A)
proof
    exp_R is_continuous_on A by Lm5;then
A2: exp_R is_integrable_on A & exp_R is_bounded_on A by INTEGRA5:10,11;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A2,SIN_COS:71,Th38,Th75;
end;

theorem Th457:
  integral(sin(#)cos,A)
    = 1/2*(cos.(inf A)*cos.(inf A)-cos.(sup A)*cos.(sup A))
proof
    sin is_continuous_on A by Lm8;then
B3: (-cos)`|REAL is_integrable_on A & (-cos)`|REAL is_bounded_on A
      by Th35,INTEGRA5:10,11;
    -sin is_continuous_on A by Lm4;then
B5: cos`|REAL is_integrable_on A & cos`|REAL is_bounded_on A
     by Th34,INTEGRA5:10,11;
B6: dom (-cos) = REAL by LmX0;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;then
    integral(sin(#)cos,A)
    =(-cos).(sup A)*cos.(sup A)-(-cos).(inf A)*cos.(inf A)
     -integral((-cos)(#)(-sin),A)
      by Th32,SIN_COS:72,B3,B5,INTEGRA5:21,Th35,Th34
   .=(-cos).(sup A)*cos.(sup A)-(-cos).(inf A)*cos.(inf A)
    -integral(sin(#)cos,A) by Lm3B
   .=(-cos.(sup A))*cos.(sup A)-(-cos).(inf A)*cos.(inf A)
    -integral(sin(#)cos,A) by B6,SEQ_1:def 7
   .=(-cos.(sup A))*cos.(sup A)-(-cos.(inf A))*cos.(inf A)
    -integral(sin(#)cos,A) by B6,SEQ_1:def 7
   .=cos.(inf A)*cos.(inf A)-cos.(sup A)*cos.(sup A)
     -integral(sin(#)cos,A);
    hence thesis;
end;

theorem
  A = [.0,PI/2.] implies integral(sin(#)cos,A) = 1/2
proof
  assume A = [.0,PI/2.];then
  sup A=PI/2 & inf A=0 by Th45; then
  integral(sin(#)cos,A)
    =1/2*(cos.0*cos.0-cos.(PI/2)*cos.(PI/2)) by Th457
   .=1/2*(cos.(0+2*PI)*cos.0-cos.(PI/2)*cos.(PI/2)) by SIN_COS:83
   .=1/2*(1*1-0*0) by SIN_COS:81,83;
   hence thesis;
end;

theorem
  A = [.0,PI.] implies integral(sin(#)cos,A) = 0
proof
    assume A = [.0,PI.];then
    sup A=PI & inf A=0 by Th45; then
    integral(sin(#)cos,A)
    =1/2*(cos.0*cos.0-cos.PI*cos.PI) by Th457
   .=1/2*(cos.(0+2*PI)*cos.0-cos.PI*cos.PI) by SIN_COS:83
   .=1/2*(1*1-(-1)*(-1)) by SIN_COS:81,83;
   hence thesis;
end;

theorem
  A = [.0,PI*(3/2).] implies integral(sin(#)cos,A) = 1/2
proof
   assume A = [.0,PI*(3/2).];then
   sup A=PI*(3/2) & inf A=0 by Th45; then
    integral(sin(#)cos,A)
    =1/2*(cos.0*cos.0-cos.(PI*(3/2))*cos.(PI*(3/2))) by Th457
   .=1/2*(cos.(0+2*PI)*cos.0-cos.(PI*(3/2))*cos.(PI*(3/2))) by SIN_COS:83
   .=1/2*(1*1-0*0) by SIN_COS:81,83;
   hence thesis;
end;

theorem
  A = [.0,PI*2.] implies integral(sin(#)cos,A) = 0
proof
  assume A = [.0,PI*2.];then
  sup A=PI*2 & inf A=0 by Th45; then
    integral(sin(#)cos,A)
    =1/2*(cos.0*cos.0-cos.(PI*2)*cos.(PI*2)) by Th457
   .=1/2*(cos.(0+2*PI)*cos.0-cos.(PI*2)*cos.(PI*2)) by SIN_COS:83
   .=1/2*(1*1-1*1) by SIN_COS:81,83;
   hence thesis;
end;

theorem
  A = [.2*n*PI,(2*n+1)*PI.] implies integral(sin(#)cos,A) = 0
proof
    assume A = [.2*n*PI,(2*n+1)*PI.];then
    sup A=(2*n+1)*PI & inf A=2*n*PI by Th45; then
      integral(sin(#)cos,A)
    =1/2*(cos.(2*n*PI)*cos.(2*n*PI)-cos.((2*n+1)*PI)*cos.((2*n+1)*PI))
       by Th457
   .=1/2*(cos(0)*cos(0+2*n*PI)-cos.((2*n+1)*PI)*cos.((2*n+1)*PI)) by Th3
   .=1/2*(cos(0)*cos(0)-cos.((2*n+1)*PI)*cos.((2*n+1)*PI)) by Th3
   .=1/2*(cos(0+2*PI)*cos(0)-cos.((2*n+1)*PI)*cos.((2*n+1)*PI)) by SIN_COS:84
   .=1/2*(1*1-cos.((2*n+1)*PI)*cos.((2*n+1)*PI)) by SIN_COS:82,84
   .=1/2*(1*1- (-cos(0))*cos(0+(2*n+1)*PI)) by Th4
   .=1/2*(1*1- (-cos(0))*(-cos(0))) by Th4
   .=1/2*(1*1- cos(0)*cos(0))
   .=1/2*(1*1- cos(0+2*PI)*cos(0)) by SIN_COS:84
   .=1/2*(1*1- cos(0+2*PI)*cos(0+2*PI)) by SIN_COS:84;
   hence thesis by SIN_COS:82;
end;

theorem
  A = [.x+2*n*PI,x+(2*n+1)*PI.] implies integral(sin(#)cos,A) = 0
proof
    assume A = [.x+2*n*PI,x+(2*n+1)*PI.];then
    sup A=x+(2*n+1)*PI & inf A=x+2*n*PI by Th45; then
    integral(sin(#)cos,A)
    = 1/2*(cos.(x+2*n*PI)*cos.(x+2*n*PI)-cos.(x+(2*n+1)*PI)*cos.(x+(2*n+1)*PI))
         by Th457
   .= 1/2*(cos(x)*cos(x+2*n*PI)-cos(x+(2*n+1)*PI)*cos(x+(2*n+1)*PI)) by Th3
   .= 1/2*(cos(x)*cos(x)-cos(x+(2*n+1)*PI)*cos(x+(2*n+1)*PI)) by Th3
   .= 1/2*(cos(x)*cos(x)- (-cos(x))*cos(x+(2*n+1)*PI)) by Th4
   .= 1/2*(cos(x)*cos(x)- (-cos(x))*(-cos(x))) by Th4
   .= 0;
   hence thesis;
end;

Lm11:cos is_continuous_on A
proof
   cos is_continuous_on REAL by SIN_COS:72,FDIFF_1:33;
   hence thesis by FCONT_1:17;
end;

Lm11A:cos(#)cos is_integrable_on A & cos(#)cos is_bounded_on A
proof
  cos is_continuous_on A by Lm11;then
  cos(#)cos is_continuous_on A by FCONT_1:19;
  hence thesis by INTEGRA5:10,11;
end;

LmX1: for f, g being Function of REAL, REAL holds
  dom (f(#)g) = REAL & dom (f-g) = REAL & dom (f+g) = REAL
proof
  let f, g be Function of REAL, REAL;
A1:dom f = REAL & dom g = REAL by FUNCT_2:def 1;
  thus dom (f(#)g) = dom f /\ dom g by SEQ_1:def 5
    .= REAL by A1;
  thus dom (f-g) = dom f /\ dom g by SEQ_1:def 4
    .= REAL by A1;
  thus dom (f+g) = dom f /\ dom g by SEQ_1:def 3
    .= REAL by A1;
end;

theorem
  integral(sin(#)sin,A)=cos.(inf A)*sin.(inf A)-
  cos.(sup A)*sin.(sup A)+integral((cos)(#)(cos),A)
proof
    sin is_continuous_on A by Lm8;then
A1: (-cos)`|REAL is_integrable_on A & (-cos)`|REAL is_bounded_on A
     by Th35,INTEGRA5:10,11;
    cos is_continuous_on A by Lm11;then
A2: sin`|REAL is_integrable_on A & sin`|REAL is_bounded_on A
     by Th33,INTEGRA5:10,11;
A3: dom (-cos) = REAL by LmX0;
    cos(#)cos is_integrable_on A & cos(#)cos is_bounded_on A by Lm11A;then
A4: (cos(#)cos)||A is_integrable_on A &
    (cos(#)cos)||A is_bounded_on A by INTEGRA5:def 2,INTEGRA5:9;
    dom(cos(#)cos) = REAL by LmX1;then
    (cos(#)cos)||A is total by INTEGRA5:6;then
A5: (cos(#)cos)||A is Function of A,REAL by FUNCT_2:131;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;then
      integral(sin(#)sin,A)
    =(-cos).(sup A)*sin.(sup A)-(-cos).(inf A)*sin.(inf A)
     -integral((-cos)(#)(cos),A)
       by Th32,SIN_COS:73,A1,A2,INTEGRA5:21,Th33,Th35
   .=(-cos.(sup A))*sin.(sup A)-(-cos).(inf A)*sin.(inf A)
     -integral((-cos)(#)(cos),A) by A3,SEQ_1:def 7
   .=(-cos.(sup A))*sin.(sup A)-(-(cos.(inf A)))*sin.(inf A)
     -integral((-cos)(#)(cos),A) by A3,SEQ_1:def 7
   .=cos.(inf A)*sin.(inf A)-cos.(sup A)*sin.(sup A)
     -integral(-((cos)(#)(cos)),A) by Lm3C
   .=cos.(inf A)*sin.(inf A)-cos.(sup A)*sin.(sup A)
     -integral((-1)(#)((cos)(#)(cos)),A) by RFUNCT_1:38
   .=cos.(inf A)*sin.(inf A) - cos.(sup A)*sin.(sup A)
     -integral((-1)(#)(((cos)(#)(cos))||A)) by RFUNCT_1:65
   .=cos.(inf A)*sin.(inf A)-cos.(sup A)*sin.(sup A)
     -(-1)*integral((cos)(#)(cos),A) by A4,A5,INTEGRA2:31
   .=cos.(inf A)*sin.(inf A)-cos.(sup A)*sin.(sup A)
     + integral((cos)(#)(cos),A);
    hence thesis;
end;

theorem
  integral(sinh(#)sinh,A)=cosh.(sup A)*sinh.(sup A)-
  cosh.(inf A)*sinh.(inf A)-integral((cosh)(#)(cosh),A)
proof
A1: (cosh)`|REAL is_integrable_on A & (cosh)`|REAL is_bounded_on A
     by Lm6A,Th37;
A2: sinh`|REAL is_integrable_on A & sinh`|REAL is_bounded_on A
     by Lm7A,Th36;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
    hence thesis by A1,A2,INTEGRA5:21,SIN_COS2:34,35,Th36,Th37;
end;

theorem
  integral(sinh(#)cosh,A)
  =1/2*((cosh.(sup A))*cosh.(sup A)-(cosh).(inf A)*cosh.(inf A))
proof
    sinh is_continuous_on A by Lm6;then
B3: (cosh)`|REAL is_integrable_on A & (cosh)`|REAL is_bounded_on A
      by Th37,INTEGRA5:10,11;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;then
    integral(sinh(#)cosh,A)
    =(cosh.(sup A))*cosh.(sup A)-(cosh).(inf A)*cosh.(inf A)
    -integral(sinh(#)cosh,A) by SIN_COS2:35,B3,INTEGRA5:21,Th37;
    hence thesis;
end;

theorem
  integral(exp_R(#)exp_R,A)
  =1/2*((exp_R.(sup A))^2-((exp_R).(inf A))^2)
proof
    exp_R is_continuous_on A by Lm5;then
B5: exp_R`|REAL is_integrable_on A & exp_R`|REAL is_bounded_on A
      by Th38,INTEGRA5:10,11;
    REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;then
    integral(exp_R(#)exp_R,A)
    =(exp_R).(sup A)*exp_R.(sup A)-(exp_R).(inf A)*exp_R.(inf A)
     -integral((exp_R)(#)(exp_R),A) by SIN_COS:71,B5,INTEGRA5:21,Th38
   .= (exp_R.(sup A))^2 - ((exp_R).(inf A))*exp_R.(inf A)
    -integral(exp_R(#)exp_R,A) by SQUARE_1:def 3
   .= (exp_R.(sup A))^2 - ((exp_R).(inf A))^2
    -integral(exp_R(#)exp_R,A) by SQUARE_1:def 3;
    hence thesis;
end;

Lm12:sin is_continuous_on A
proof 
   sin is_continuous_on REAL by SIN_COS:73,FDIFF_1:33;
   hence thesis by FCONT_1:17;
end;

Lm13:exp_R(#)(sin + cos) is_integrable_on A &
      exp_R(#)(sin + cos) is_bounded_on A
proof
A1: exp_R is_continuous_on A by Lm5;
A2: cos is_continuous_on A by Lm11;
    sin is_continuous_on A by Lm12; then
    sin + cos is_continuous_on A by A2,FCONT_1:19;then
    exp_R(#)(sin + cos) is_continuous_on A by A1,FCONT_1:19;
    hence thesis by INTEGRA5:10,11;
end;

Lm14:exp_R(#)(cos - sin) is_integrable_on A &
     exp_R(#)(cos - sin) is_bounded_on A
proof
A1: exp_R is_continuous_on A by Lm5;
A2: cos is_continuous_on A by Lm11;
    sin is_continuous_on A by Lm12; then
    cos - sin is_continuous_on A by A2,FCONT_1:19;then
    exp_R(#)(cos - sin) is_continuous_on A by A1,FCONT_1:19;
    hence thesis by INTEGRA5:10,11;
end;

theorem
  integral(exp_R(#)(sin+cos),A) = (exp_R(#)sin).(sup A)-(exp_R(#)sin).(inf A)
proof
A1: dom (exp_R(#)sin) = REAL by LmX1;
A2: dom (sin+cos) = REAL by LmX1;
A3: dom (exp_R(#)(sin+cos)) = dom exp_R /\ dom (sin+cos) by SEQ_1:def 5
    .= REAL /\ dom (sin+cos) by SIN_COS:def 26
    .= REAL by A2;
A8: exp_R(#)(sin + cos) is_integrable_on A &
    exp_R(#)(sin + cos) is_bounded_on A by Lm13;
A9: REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
A10:(exp_R(#)sin) is_differentiable_on REAL by A9,A1,FDIFF_7:44;
A11:for x st x in dom ((exp_R(#)sin)`|REAL) holds
   ((exp_R(#)sin)`|REAL).x = (exp_R(#)(sin + cos)).x
   proof
     let x;
     assume x in dom ((exp_R(#)sin)`|REAL);
     (exp_R(#)(sin + cos)).x = (exp_R.x) * ((sin + cos).x)
       by A3,SEQ_1:def 5
         .= (exp_R.x) * (sin.x + cos.x) by A2,SEQ_1:def 3;
     hence thesis by A9,A1,FDIFF_7:44;
   end;
   dom ((exp_R(#)sin)`|REAL) = dom (exp_R(#)(sin + cos))
       by A10,A3,FDIFF_1:def 8;then
   ((exp_R(#)sin)`|REAL) = exp_R(#)(sin + cos) by A11,PARTFUN1:34;
   hence thesis by A8,A10,INTEGRA5:13;
end;

theorem
  integral(exp_R(#)(cos-sin),A) = (exp_R(#)cos).(sup A)-(exp_R(#)cos).(inf A)
proof
A1: dom (exp_R(#)cos) = REAL by LmX1;
A2: dom (cos - sin) = REAL by LmX1;
A3: dom (exp_R(#)(cos - sin)) = dom exp_R /\ dom (cos - sin) by SEQ_1:def 5
    .= REAL /\ dom (cos - sin) by SIN_COS:def 26
    .= REAL by A2;
A8: exp_R(#)(cos - sin) is_integrable_on A &
    exp_R(#)(cos - sin) is_bounded_on A by Lm14;
A9: REAL is open Subset of REAL by FCONT_3:4,SUBSET_1:def 4;
A10: (exp_R(#)cos) is_differentiable_on REAL by A9,A1,FDIFF_7:45;
A11:for x st x in dom ((exp_R(#)cos)`|REAL) holds
   ((exp_R(#)cos)`|REAL).x = (exp_R(#)(cos - sin)).x
   proof
     let x;
     assume x in dom ((exp_R(#)cos)`|REAL);
     (exp_R(#)(cos - sin)).x = (exp_R.x) * ((cos - sin).x)
        by A3,SEQ_1:def 5
         .= (exp_R.x) * (cos.x - sin.x) by A2,SEQ_1:def 4;
     hence thesis by A9,A1,FDIFF_7:45;
   end;
   dom ((exp_R(#)cos)`|REAL) = dom (exp_R(#)(cos - sin))
     by A10,A3,FDIFF_1:def 8;then
   ((exp_R(#)cos)`|REAL) = exp_R(#)(cos - sin) by A11,PARTFUN1:34;
   hence thesis by A8,A10,INTEGRA5:13;
end;
