:: Properties of the External Approximation of Jordan's Curve
::  by Artur Korni{\l}owicz
::
:: Received June 24, 1999
:: Copyright (c) 1999 Association of Mizar Users

environ

 vocabularies RELAT_2, COMPTS_1, SPPOL_1, EUCLID, TOPREAL1, FINSEQ_1, JORDAN9,
      MATRIX_1, JORDAN8, GOBOARD1, GOBOARD5, TREES_1, BOOLE, ARYTM_1, TARSKI,
      PSCOMP_1, ARYTM_3, GROUP_1, MCART_1, PRE_TOPC, GOBOARD9, TOPS_1,
      SUBSET_1, CONNSP_1, RELAT_1, JORDAN2C, LATTICES, CONNSP_3, SETFAM_1,
      TOPREAL4, PCOMPS_1, WEIERSTR, METRIC_1, JORDAN10, FINSEQ_4, ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, NUMBERS, REAL_1, NAT_1,
      NAT_D,
      BINARITH, PARTFUN1, FINSEQ_1, NEWTON, DOMAIN_1, STRUCT_0, METRIC_1,
      TBSP_1, WEIERSTR, PRE_TOPC, TOPS_1, CONNSP_1, CONNSP_3, COMPTS_1,
      PCOMPS_1, MATRIX_1, EUCLID, TOPREAL1, GOBOARD1, GOBOARD5, TOPREAL4,
      PSCOMP_1, GOBOARD9, SPPOL_1, JORDAN2C, JORDAN8, GOBRD13, TOPREAL6,
      JORDAN9, XXREAL_0;
 constructors SETFAM_1, XXREAL_0, REAL_1, NAT_1, NEWTON, REALSET1, BINARITH,
      TOPS_1, CONNSP_1, TBSP_1, TOPREAL4, JORDAN1, PSCOMP_1, WEIERSTR,
      GOBOARD9, CONNSP_3, JORDAN2C, TOPREAL6, JORDAN8, GOBRD13, JORDAN9,
      NAT_D;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, XXREAL_0, MEMBERED, FINSEQ_1,
      STRUCT_0, PRE_TOPC, COMPTS_1, PCOMPS_1, EUCLID, TOPREAL1, SPPOL_2,
      PSCOMP_1, SPRECT_1, SPRECT_2, JORDAN2C, TOPREAL6, JORDAN8, FUNCT_1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, XBOOLE_0, SUBSET_1, PSCOMP_1, CONNSP_3;
 theorems BINARITH, CONNSP_1, CONNSP_3, EUCLID, GOBOARD6, GOBOARD7,
      GOBOARD9, GOBRD11, GOBRD13, GOBRD14, JGRAPH_1, JORDAN2C, JORDAN3,
      JORDAN6, JORDAN8, JORDAN9, NEWTON, NAT_1, PRE_TOPC, REAL_2,
      SETFAM_1, SPPOL_2, SPRECT_1, SPRECT_3, SPRECT_4, SUBSET_1, TARSKI,
      TOPREAL1, TOPREAL3, TOPREAL4, TOPREAL6, TOPS_1, WEIERSTR, ZFMISC_1,
      XBOOLE_0, XBOOLE_1, XCMPLX_1, REALSET1, XREAL_1, XXREAL_0, MATRIX_1,
      XREAL_0;
 schemes NAT_1;

begin  :: Properties of the external approximation of Jordan's curve

registration
  cluster connected compact non vertical non horizontal Subset of TOP-REAL 2;
  existence
  proof
    take R^2-unit_square;
    thus thesis;
  end;
end;

reserve i, j, k, n for Element of NAT,
  P for Subset of TOP-REAL 2,
  C for connected compact non vertical non horizontal Subset of TOP-REAL 2;

theorem Th1:
  1 <= k & k+1 <= len Cage(C,n) &
  [i,j] in Indices Gauge(C,n) & [i,j+1] in Indices Gauge(C,n) &
  Cage(C,n)/.k = Gauge(C,n)*(i,j) & Cage(C,n)/.(k+1) = Gauge(C,n)*(i,j+1)
  implies i < len Gauge(C,n)
proof
  set f = Cage(C,n), G = Gauge(C,n);
  assume that
A1: 1 <= k & k+1 <= len Cage(C,n) and
A2: [i,j] in Indices Gauge(C,n) and
A3: [i,j+1] in Indices Gauge(C,n) & Cage(C,n)/.k = Gauge(C,n)*(i,j) &
  Cage(C,n)/.(k+1) = Gauge(C,n)*(i,j+1);
A4: i <= len G by A2,MATRIX_1:39;
  assume i >= len G;
  then
A5: i = len G by A4,XXREAL_0:1;
  f is_sequence_on G by JORDAN9:def 1;
  then
A6: right_cell(f,k,G) = cell(G,i,j) by A1,A2,A3,GOBRD13:23;
  len G = width G by JORDAN8:def 1;
  then j <= len G by A2,MATRIX_1:39;
  then cell(G,len G,j) misses C by JORDAN8:19;
  hence contradiction by A1,A5,A6,JORDAN9:33;
end;

theorem Th2:
  1 <= k & k+1 <= len Cage(C,n) &
  [i,j] in Indices Gauge(C,n) & [i,j+1] in Indices Gauge(C,n) &
  Cage(C,n)/.k = Gauge(C,n)*(i,j+1) & Cage(C,n)/.(k+1) = Gauge(C,n)*(i,j)
  implies i > 1
proof
  set f = Cage(C,n), G = Gauge(C,n);
  assume that
A1: 1 <= k & k+1 <= len Cage(C,n) and
A2: [i,j] in Indices Gauge(C,n) and
A3: [i,j+1] in Indices Gauge(C,n) & Cage(C,n)/.k = Gauge(C,n)*(i,j+1) &
  Cage(C,n)/.(k+1) = Gauge(C,n)*(i,j);
A4: 1 <= i by A2,MATRIX_1:39;
  assume i <= 1;
  then i = 1 by A4,XXREAL_0:1;
  then
A5: i-'1 = 0 by XREAL_1:234;
  f is_sequence_on G by JORDAN9:def 1;
  then
A6: right_cell(f,k,G) = cell(G,i-'1,j) by A1,A2,A3,GOBRD13:29;
  len G = width G by JORDAN8:def 1;
  then j <= len G by A2,MATRIX_1:39;
  then cell(G,0,j) misses C by JORDAN8:21;
  hence contradiction by A1,A5,A6,JORDAN9:33;
end;

theorem Th3:
  1 <= k & k+1 <= len Cage(C,n) &
  [i,j] in Indices Gauge(C,n) & [i+1,j] in Indices Gauge(C,n) &
  Cage(C,n)/.k = Gauge(C,n)*(i,j) & Cage(C,n)/.(k+1) = Gauge(C,n)*(i+1,j)
  implies j > 1
proof
  set f = Cage(C,n), G = Gauge(C,n);
  assume that
A1: 1 <= k & k+1 <= len Cage(C,n) and
A2: [i,j] in Indices Gauge(C,n) and
A3: [i+1,j] in Indices Gauge(C,n) & Cage(C,n)/.k = Gauge(C,n)*(i,j) &
  Cage(C,n)/.(k+1) = Gauge(C,n)*(i+1,j);
A4: 1 <= j by A2,MATRIX_1:39;
  assume j <= 1;
  then j = 1 by A4,XXREAL_0:1;
  then
A5: j-'1 = 0 by XREAL_1:234;
  f is_sequence_on G by JORDAN9:def 1;
  then
A6: right_cell(f,k,G) = cell(G,i,j-'1) by A1,A2,A3,GOBRD13:25;
  i <= len G by A2,MATRIX_1:39;
  then cell(G,i,0) misses C by JORDAN8:20;
  hence contradiction by A1,A5,A6,JORDAN9:33;
end;

theorem Th4:
  1 <= k & k+1 <= len Cage(C,n) &
  [i,j] in Indices Gauge(C,n) & [i+1,j] in Indices Gauge(C,n) &
  Cage(C,n)/.k = Gauge(C,n)*(i+1,j) & Cage(C,n)/.(k+1) = Gauge(C,n)*(i,j)
  implies j < width Gauge(C,n)
proof
  set f = Cage(C,n), G = Gauge(C,n);
  assume that
A1: 1 <= k & k+1 <= len Cage(C,n) and
A2: [i,j] in Indices Gauge(C,n) and
A3: [i+1,j] in Indices Gauge(C,n) & Cage(C,n)/.k = Gauge(C,n)*(i+1,j) &
  Cage(C,n)/.(k+1) = Gauge(C,n)*(i,j);
A4: j <= width G by A2,MATRIX_1:39;
  assume j >= width G;
  then
A5: j = width G by A4,XXREAL_0:1;
  f is_sequence_on G by JORDAN9:def 1;
  then
A6: right_cell(f,k,G) = cell(G,i,j) by A1,A2,A3,GOBRD13:27;
A7: len G = width G by JORDAN8:def 1;
  i <= len G by A2,MATRIX_1:39;
  then cell(G,i,len G) misses C by JORDAN8:18;
  hence contradiction by A1,A5,A6,A7,JORDAN9:33;
end;

theorem Th5:
  C misses L~Cage(C,n)
proof
  set f = Cage(C,n), G = Gauge(C,n);
  assume not thesis;
  then consider c being set such that
A1: c in C & c in L~f by XBOOLE_0:3;
  L~f = union { LSeg(f,i) where i is Element of NAT: 1 <= i & i+1 <= len f }
  by TOPREAL1:def 6;
  then consider Z being set such that
A2: c in Z and
A3: Z in { LSeg(f,i) where i is Element of NAT: 1 <= i & i+1 <= len f }
  by A1,TARSKI:def 4;
  consider i being Element of NAT such that
A4: Z = LSeg(f,i) and
A5: 1 <= i & i+1 <= len f by A3;
  f is_sequence_on G by JORDAN9:def 1;
  then LSeg(f,i) = left_cell(f,i,G) /\ right_cell(f,i,G) by A5,GOBRD13:30;
  then
A6: c in left_cell(f,i,G) by A2,A4,XBOOLE_0:def 4;
  left_cell(f,i,G) misses C by A5,JORDAN9:33;
  hence contradiction by A1,A6,XBOOLE_0:3;
end;

theorem Th6:
  N-bound L~Cage(C,n) = N-bound C + (N-bound C - S-bound C)/(2|^n)
proof
  set a = N-bound C, s = S-bound C, w = W-bound C,
  f = Cage(C,n), G = Gauge(C,n);
  consider i such that
A1: 1 <= i & i+1 <= len G and
A2: f/.1 = G*(i,width G) and f/.2 = G*(i+1,width G) &
  N-min C in cell(G,i,width G-'1) &
  N-min C <> G*(i,width G-'1) by JORDAN9:def 1;
A3: len G = width G by JORDAN8:def 1;
  i+0 <= i+1 by XREAL_1:8;
  then
A4: i <= len G by A1,XXREAL_0:2;
  4 <= len G by JORDAN8:13;
  then 1 <= len G by XXREAL_0:2;
  then
A5: [i,len G] in Indices G by A1,A3,A4,MATRIX_1:37;
A6: 2|^n <> 0 by NEWTON:102;
  thus N-bound L~f = (N-min L~f)`2 by EUCLID:56
    .= (f/.1)`2 by JORDAN9:34
    .= |[w+((E-bound C-w)/(2|^n))*(i-2), s+((a-s)/(2|^n))*(len G-2)]|`2
  by A2,A3,A5,JORDAN8:def 1
    .= s+((a-s)/(2|^n))*(len G-2) by EUCLID:56
    .= s+((a-s)/(2|^n))*(2|^n+3-2) by JORDAN8:def 1
    .= s+((a-s)/(2|^n))*2|^n+(a-s)/(2|^n)
    .= s+(a-s)+(a-s)/(2|^n) by A6,XCMPLX_1:88
    .= a+(a-s)/(2|^n);
end;

theorem Th7:
  i < j implies N-bound L~Cage(C,j) < N-bound L~Cage(C,i)
proof
  assume
A1: i < j;
  defpred P[Element of NAT] means
  i < $1 implies N-bound L~Cage(C,$1) < N-bound L~Cage(C,i);
A2: P[0] by NAT_1:2;
A3: for n being Element of NAT st P[n] holds P[n+1]
  proof
    let n be Element of NAT;
    assume
A4: P[n];
    assume i < n+1;
    then
A5: i <= n by NAT_1:13;
    set j = n + 1, a = N-bound C, s = S-bound C;
A6: N-bound L~Cage(C,n) = a+(a-s)/(2|^n) &
    N-bound L~Cage(C,j) = a+(a-s)/(2|^j) by Th6;
    2|^n > 0 by NEWTON:102;
    then
A7: 2|^n*2 > 0 * 2 by XREAL_1:70;
    s < a by SPRECT_1:34;
    then
A8: s - a < 0 by REAL_2:105;
    a+(a-s)/(2|^j) - (a+(a-s)/(2|^n)) = 0 + (a-s)/(2|^j) - (a-s)/(2|^n)
      .= (a-s)/(2|^n*2) - (a-s)/(2|^n)/(2/2) by NEWTON:11
      .= (a-s)/(2|^n*2) - (a-s)*2/(2|^n*2) by XCMPLX_1:85
      .= (a-s - (2*a-2*s))/(2|^n*2) by XCMPLX_1:121
      .= (s-a)/(2|^n*2);
    then 0 > a+(a-s)/(2|^j) - (a+(a-s)/(2|^n)) by A7,A8,REAL_2:128;
    then N-bound L~Cage(C,n+1) < N-bound L~Cage(C,n) by A6,XREAL_1:50;
    hence thesis by A4,A5,XXREAL_0:1,2;
  end;
  for n being Element of NAT holds P[n] from NAT_1:sch 1(A2,A3);
  hence thesis by A1;
end;

registration
  let C be connected compact non vertical non horizontal Subset of TOP-REAL 2,
  n be Element of NAT;
  cluster Cl RightComp Cage(C,n) -> compact;
  coherence by GOBRD14:42;
end;

theorem Th8:
  N-min C in RightComp Cage(C,n)
proof
  set f = Cage(C,n), G = Gauge(C,n);
A1: N-min C in C by SPRECT_1:13;
  C misses L~f by Th5;
  then C /\ L~f = {} by XBOOLE_0:def 7;
  then
A2: not N-min C in L~f by A1,XBOOLE_0:def 4;
  RightComp f misses L~f by SPRECT_3:42;
  then
A3: RightComp f /\ L~f = {} by XBOOLE_0:def 7;
  consider k being Element of NAT such that
A4: 1 <= k & k+1 <= len G and
A5: f/.1 = G*(k,width G) and
A6: f/.2 = G*(k+1,width G) and
A7: N-min C in cell(G,k,width G-'1) and
  N-min C <> G*(k,width G-'1) by JORDAN9:def 1;
A8: f is_sequence_on G by JORDAN9:def 1;
  L~f <> {};
  then
A9: 1+1 <= len f by GOBRD14:8;
A10: len G = width G by JORDAN8:def 1;
A11: 1 <= k+1 by NAT_1:11;
  then
A12: 1 <= len G by A4,XXREAL_0:2;
A13: k < len G by A4,NAT_1:13;
  then [k,len G] in Indices G & [k+1,len G] in Indices G
  by A4,A10,A11,A12,MATRIX_1:37;
  then
A14: cell(G,k,len G-'1) = right_cell(f,1,G) by A5,A6,A8,A9,A10,GOBRD13:25;
  right_cell(f,1,G) c= right_cell(f,1) by A8,A9,GOBRD13:34;
  then
A15: Int right_cell(f,1,G) c= Int right_cell(f,1) by TOPS_1:48;
  Int right_cell(f,1) c= RightComp f by GOBOARD9:def 2;
  then
A16: Int cell(G,k,len G-'1) c= RightComp f by A14,A15,XBOOLE_1:1;
  RightComp f c= RightComp f \/ L~f by XBOOLE_1:7;
  then
A17: Int cell(G,k,len G-'1) c= RightComp f \/ L~f by A16,XBOOLE_1:1;
A18: right_cell(f,1,G) is closed by A8,A9,GOBRD13:31;
A19: Fr cell(G,k,len G-'1) c= RightComp f \/ L~f
  proof
    let q be set;
    assume
A20: q in Fr cell(G,k,len G-'1);
    then reconsider s = q as Point of TOP-REAL 2;
    4 <= len G by JORDAN8:13;
    then 4 - 1 <= len G - 1 by XREAL_1:15;
    then 0 <= len G - 1 by XXREAL_0:2;
    then
A21: len G-'1 = len G - 1 by XREAL_0:def 2;
A22: len G - 1 < len G - 0 by XREAL_1:17;
    then Int cell(G,k,len G-'1) <> {} by A10,A13,A21,GOBOARD9:17;
    then consider p being set such that
A23: p in Int cell(G,k,len G-'1) by XBOOLE_0:def 1;
    reconsider p as Point of TOP-REAL 2 by A23;
    per cases;
    suppose q in L~f;
      hence thesis by XBOOLE_0:def 3;
    end;
    suppose
A24:  not q in L~f;
A25:  LSeg(p,s) c= (L~f)`
      proof
        let a be set;
        assume
A26:    a in LSeg(p,s);
        then reconsider b = a as Point of TOP-REAL 2;
        3 <= len G-'1 by GOBRD14:17;
        then
A27:    1 <= len G-'1 by XXREAL_0:2;
        then
A28:    Int cell(G,k,len G-'1) = { |[x,y]| where x, y is Real:
        G*(k,1)`1 < x & x < G*(k+1,1)`1 &
        G*(1,len G-'1)`2 < y & y < G*(1,len G-'1+1)`2 }
        by A4,A10,A13,A21,A22,GOBOARD6:29;
A29:    b = |[b`1,b`2]| by EUCLID:57;
        consider x, y being Real such that
A30:    p = |[x,y]| and
A31:    G*(k,1)`1 < x & x < G*(k+1,1)`1 &
        G*(1,len G-'1)`2 < y & y < G*(1,len G-'1+1)`2 by A23,A28;
        Fr cell(G,k,len G-'1) c= cell(G,k,len G-'1) by A8,A9,A14,GOBRD13:31
,TOPS_1:69;
        then
A32:    q in cell(G,k,len G-'1) by A20;
        cell(G,k,len G-'1) = { |[m,o]| where m, o is Real:
        G*(k,1)`1 <= m & m <= G*(k+1,1)`1 &
        G*(1,len G-'1)`2 <= o & o <= G*(1,len G-'1+1)`2 }
        by A4,A10,A13,A21,A22,A27,GOBRD11:32;
        then consider m, o being Real such that
A33:    s = |[m,o]| and
A34:    G*(k,1)`1 <= m & m <= G*(k+1,1)`1 &
        G*(1,len G-'1)`2 <= o & o <= G*(1,len G-'1+1)`2 by A32;
A35:    s`1 = m & s`2 = o & p`1 = x & p`2 = y by A30,A33,EUCLID:56;
        now per cases;
          case b = s;
            hence a in (L~f)` by A24,SUBSET_1:50;
          end;
          case
A36:        b <> s;
            now per cases by XXREAL_0:1;
              case s`1 < p`1 & s`2 < p`2;
                then s`1 < b`1 & b`1 <= p`1 & s`2 < b`2 & b`2 <= p`2
                by A26,A36,TOPREAL6:37,38;
                then G*(k,1)`1 < b`1 & b`1 < G*(k+1,1)`1 &
                G*(1,len G-'1)`2 < b`2 & b`2 < G*(1,len G-'1+1)`2
                by A31,A34,A35,XXREAL_0:2;
                hence b in Int cell(G,k,len G-'1) by A28,A29;
              end;
              case s`1 < p`1 & s`2 > p`2;
                then s`1 < b`1 & b`1 <= p`1 & p`2 <= b`2 & b`2 < s`2
                by A26,A36,TOPREAL6:37,38;
                then G*(k,1)`1 < b`1 & b`1 < G*(k+1,1)`1 &
                G*(1,len G-'1)`2 < b`2 & b`2 < G*(1,len G-'1+1)`2
                by A31,A34,A35,XXREAL_0:2;
                hence b in Int cell(G,k,len G-'1) by A28,A29;
              end;
              case s`1 < p`1 & s`2 = p`2;
                then s`1 < b`1 & b`1 <= p`1 & p`2 = b`2 & b`2 = s`2
                by A26,A36,GOBOARD7:6,TOPREAL6:37;
                then G*(k,1)`1 < b`1 & b`1 < G*(k+1,1)`1 &
                G*(1,len G-'1)`2 < b`2 & b`2 < G*(1,len G-'1+1)`2
                by A31,A34,A35,XXREAL_0:2;
                hence b in Int cell(G,k,len G-'1) by A28,A29;
              end;
              case s`1 > p`1 & s`2 < p`2;
                then s`1 > b`1 & b`1 >= p`1 & s`2 < b`2 & b`2 <= p`2
                by A26,A36,TOPREAL6:37,38;
                then G*(k,1)`1 < b`1 & b`1 < G*(k+1,1)`1 &
                G*(1,len G-'1)`2 < b`2 & b`2 < G*(1,len G-'1+1)`2
                by A31,A34,A35,XXREAL_0:2;
                hence b in Int cell(G,k,len G-'1) by A28,A29;
              end;
              case s`1 > p`1 & s`2 > p`2;
                then s`1 > b`1 & b`1 >= p`1 & s`2 > b`2 & b`2 >= p`2
                by A26,A36,TOPREAL6:37,38;
                then G*(k,1)`1 < b`1 & b`1 < G*(k+1,1)`1 &
                G*(1,len G-'1)`2 < b`2 & b`2 < G*(1,len G-'1+1)`2
                by A31,A34,A35,XXREAL_0:2;
                hence b in Int cell(G,k,len G-'1) by A28,A29;
              end;
              case s`1 > p`1 & s`2 = p`2;
                then s`1 > b`1 & b`1 >= p`1 & s`2 = b`2 & b`2 = p`2
                by A26,A36,GOBOARD7:6,TOPREAL6:37;
                then G*(k,1)`1 < b`1 & b`1 < G*(k+1,1)`1 &
                G*(1,len G-'1)`2 < b`2 & b`2 < G*(1,len G-'1+1)`2
                by A31,A34,A35,XXREAL_0:2;
                hence b in Int cell(G,k,len G-'1) by A28,A29;
              end;
              case s`1 = p`1 & s`2 > p`2;
                then s`1 = b`1 & b`1 = p`1 & s`2 > b`2 & b`2 >= p`2
                by A26,A36,GOBOARD7:5,TOPREAL6:38;
                then G*(k,1)`1 < b`1 & b`1 < G*(k+1,1)`1 &
                G*(1,len G-'1)`2 < b`2 & b`2 < G*(1,len G-'1+1)`2
                by A31,A34,A35,XXREAL_0:2;
                hence b in Int cell(G,k,len G-'1) by A28,A29;
              end;
              case s`1 = p`1 & s`2 < p`2;
                then s`1 = b`1 & b`1 = p`1 & s`2 < b`2 & b`2 <= p`2
                by A26,A36,GOBOARD7:5,TOPREAL6:38;
                then G*(k,1)`1 < b`1 & b`1 < G*(k+1,1)`1 &
                G*(1,len G-'1)`2 < b`2 & b`2 < G*(1,len G-'1+1)`2
                by A31,A34,A35,XXREAL_0:2;
                hence b in Int cell(G,k,len G-'1) by A28,A29;
              end;
              case s`1 = p`1 & s`2 = p`2;
                then p = s by TOPREAL3:11;
                then LSeg(p,s) = {p} by TOPREAL1:7;
                hence b in Int cell(G,k,len G-'1) by A23,A26,TARSKI:def 1;
              end;
            end;
            then not b in L~f by A3,A16,XBOOLE_0:def 4;
            hence a in (L~f)` by SUBSET_1:50;
          end;
        end;
        hence a in (L~f)`;
      end;
A37:  RightComp f is_a_component_of (L~f)` by GOBOARD9:def 2;
A38:  {p} c= RightComp f by A16,A23,ZFMISC_1:37;
      {p} meets LSeg(p,s)
      proof
        now
          take a = p;
          thus a in {p} & a in LSeg(p,s) by TARSKI:def 1,TOPREAL1:6;
        end;
        hence thesis by XBOOLE_0:3;
      end;
      then
A39:  LSeg(p,s) c= RightComp f by A25,A37,A38,GOBOARD9:6;
      s in LSeg(p,s) by TOPREAL1:6;
      hence thesis by A39,XBOOLE_0:def 3;
    end;
  end;
  Fr right_cell(f,1,G) = right_cell(f,1,G) \ Int right_cell(f,1,G)
  by A18,TOPS_1:77;
  then Fr right_cell(f,1,G) \/ Int right_cell(f,1,G) = right_cell(f,1,G)
  by TOPS_1:44,XBOOLE_1:45;
  then right_cell(f,1,G) c= RightComp f \/ L~f by A14,A17,A19,XBOOLE_1:8;
  hence thesis by A2,A7,A10,A14,XBOOLE_0:def 3;
end;

theorem Th9:
  C meets RightComp Cage (C,n)
proof
A1: N-min C in C by SPRECT_1:13;
  N-min C in RightComp Cage(C,n) by Th8;
  then C /\ RightComp Cage (C,n) <> {} by A1,XBOOLE_0:def 4;
  hence thesis by XBOOLE_0:def 7;
end;

theorem Th10:
  C misses LeftComp Cage(C,n)
proof
  set f = Cage(C,n);
  C misses L~f by Th5;
  then
A1: C /\ L~f = {} by XBOOLE_0:def 7;
A2: C meets RightComp f by Th9;
  assume
A3: C meets LeftComp f;
  LeftComp f is_a_component_of (L~f)` by GOBOARD9:def 1;
  then consider L being Subset of (TOP-REAL 2)|(L~f)` such that
A4: L = LeftComp f and
A5: L is_a_component_of (TOP-REAL 2)|(L~f)` by CONNSP_1:def 6;
  RightComp f is_a_component_of (L~f)` by GOBOARD9:def 2;
  then consider R being Subset of (TOP-REAL 2)|(L~f)` such that
A6: R = RightComp f and
A7: R is_a_component_of (TOP-REAL 2)|(L~f)` by CONNSP_1:def 6;
  C c= the carrier of (TOP-REAL 2)|(L~f)`
  proof
    let c be set;
    assume c in C;
    then c in the carrier of TOP-REAL 2 & not c in L~f by A1,XBOOLE_0:def 4;
    then c in (L~f)` by SUBSET_1:50;
    hence thesis by PRE_TOPC:29;
  end;
  then reconsider C1 = C as Subset of (TOP-REAL 2)|(L~f)`;
  C1 is connected by CONNSP_1:24;
  hence contradiction by A2,A3,A4,A5,A6,A7,JORDAN2C:100,SPRECT_4:7;
end;

theorem Th11:
  C c= RightComp Cage(C,n)
proof
  set f = Cage(C,n);
  let c be set;
  assume
A1: c in C;
  C misses L~f by Th5;
  then
A2: C /\ L~f = {} by XBOOLE_0:def 7;
  C misses LeftComp f by Th10;
  then C /\ LeftComp f = {} by XBOOLE_0:def 7;
  then not c in LeftComp f & not c in L~f by A1,A2,XBOOLE_0:def 4;
  hence c in RightComp f by A1,GOBRD14:28;
end;

theorem Th12:
  C c= BDD L~Cage(C,n)
proof
A1: C c= RightComp Cage(C,n) by Th11;
  RightComp Cage(C,n) c= BDD L~Cage(C,n) by GOBRD14:45,JORDAN2C:26;
  hence thesis by A1,XBOOLE_1:1;
end;

theorem Th13:
  UBD L~Cage(C,n) c= UBD C
proof
  set f = Cage(C,n);
A1: UBD C = union {B where B is Subset of TOP-REAL 2:
  B is_outside_component_of C} by JORDAN2C:def 6;
  let x be set;
  assume
A2: x in UBD L~f;
  UBD L~f = union {B where B is Subset of TOP-REAL 2:
  B is_outside_component_of L~f} by JORDAN2C:def 6;
  then consider L being set such that
A3: x in L and
A4: L in {B where B is Subset of TOP-REAL 2:
  B is_outside_component_of L~f} by A2,TARSKI:def 4;
  consider B being Subset of TOP-REAL 2 such that
A5: L = B and
A6: B is_outside_component_of L~f by A4;
  reconsider B1 = B as Subset of Euclid 2 by TOPREAL3:13;
  B is_a_component_of (L~f)` by A6,JORDAN2C:def 4;
  then consider B2 being Subset of (TOP-REAL 2)|(L~f)` such that
A7: B2 = B and
A8: B2 is_a_component_of (TOP-REAL 2)|(L~f)` by CONNSP_1:def 6;
  B2 is connected by A8,CONNSP_1:def 5;
  then
A9: B is connected by A7,CONNSP_1:24;
  not B is Bounded by A6,JORDAN2C:def 4;
  then
A10: not B1 is bounded by JORDAN2C:def 2;
  the carrier of (TOP-REAL 2)|C` = C` by PRE_TOPC:29;
  then reconsider P1 = Component_of (Down(B,C`))
  as Subset of TOP-REAL 2 by XBOOLE_1:1;
  RightComp f is_inside_component_of L~f by GOBRD14:45;
  then
A11: C c= RightComp f & B1 misses RightComp f by A6,Th11,JORDAN2C:126;
  then B1 misses C by XBOOLE_1:63;
  then
A12: P1 is_outside_component_of C by A9,A10,JORDAN2C:71;
A13: B1 /\ RightComp f = {} by A11,XBOOLE_0:def 7;
  B c= C`
  proof
    let a be set;
    assume
A14: a in B;
    then not a in C by A11,A13,XBOOLE_0:def 4;
    hence a in C` by A14,XBOOLE_0:def 5;
  end;
  then Down(B,C`) = B by XBOOLE_1:28;
  then
A15: Down(B,C`) c= Component_of Down(B,C`) by A9,CONNSP_3:1,JORDAN2C:15;
  not x in C by A3,A5,A11,A13,XBOOLE_0:def 4;
  then x in C` by A3,A5,XBOOLE_0:def 5;
  then
A16: x in B /\ C` by A3,A5,XBOOLE_0:def 4;
  P1 in {W where W is Subset of TOP-REAL 2: W is_outside_component_of C }
  by A12;
  hence x in UBD C by A1,A15,A16,TARSKI:def 4;
end;

definition
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  func UBD-Family C equals
  { UBD L~Cage(C,n) where n is Element of NAT : not contradiction };
  coherence;
  func BDD-Family C equals
  { Cl BDD L~Cage(C,n) where n is Element of NAT : not contradiction };
  coherence;
end;

definition
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  redefine func UBD-Family C -> Subset-Family of TOP-REAL 2;
  coherence
  proof
    { UBD L~Cage(C,i) where i is Element of NAT : not contradiction } c=
    bool the carrier of TOP-REAL 2
    proof
      let x be set;
      assume
      x in { UBD L~Cage(C,i) where i is Element of NAT : not contradiction };
      then ex i being Element of NAT
      st x = UBD L~Cage(C,i) & not contradiction;
      hence x in bool the carrier of TOP-REAL 2;
    end;
    hence thesis;
  end;
  redefine func BDD-Family C -> Subset-Family of TOP-REAL 2;
  coherence
  proof
    { Cl BDD L~Cage(C,i) where i is Element of NAT : not contradiction } c=
    bool the carrier of TOP-REAL 2
    proof
      let x be set;
      assume x in { Cl BDD L~Cage(C,i) where i is Element of NAT :
      not contradiction };
      then ex i being Element of NAT
      st x = Cl BDD L~Cage(C,i) & not contradiction;
      hence x in bool the carrier of TOP-REAL 2;
    end;
    hence thesis;
  end;
end;

registration
  let C be compact non vertical non horizontal Subset of TOP-REAL 2;
  cluster UBD-Family C -> non empty;
  coherence
  proof
    UBD L~Cage(C,0) in UBD-Family C;
    hence thesis;
  end;
  cluster BDD-Family C -> non empty;
  coherence
  proof
    Cl BDD L~Cage(C,0) in BDD-Family C;
    hence thesis;
  end;
end;

theorem Th14:
  union UBD-Family C = UBD C
proof
  for X being set st X in UBD-Family C holds X c= UBD C
  proof
    let X be set;
    assume X in UBD-Family C;
    then ex n st X = UBD L~Cage(C,n) & not contradiction;
    hence X c= UBD C by Th13;
  end;
  hence union UBD-Family C c= UBD C by ZFMISC_1:94;
  let x be set such that
A1: x in UBD C;
  UBD C = union {B where B is Subset of TOP-REAL 2:
  B is_outside_component_of C} by JORDAN2C:def 6;
  then consider A being set such that
A2: x in A and
A3: A in {B where B is Subset of TOP-REAL 2:
  B is_outside_component_of C} by A1,TARSKI:def 4;
  consider B being Subset of TOP-REAL 2 such that
A4: A = B and B is_outside_component_of C by A3;
  reconsider p = x as Point of TOP-REAL 2 by A2,A4;
  consider q being Point of TOP-REAL 2 such that
A5: q`2 > N-bound L~Cage(C,0) and
A6: p <> q by TOPREAL6:41;
A7: UBD L~Cage(C,0) c= UBD C by Th13;
A8: Cage(C,0)/.1 = N-min L~Cage(C,0) by JORDAN9:34;
A9: UBD L~Cage(C,0) = LeftComp Cage(C,0) by GOBRD14:46;
  q in LeftComp Cage(C,0) by A5,A8,JORDAN2C:121;
  then consider P such that
A10: P is_S-P_arc_joining p,q and
A11: P c= UBD C by A1,A6,A7,A9,TOPREAL4:30;
  consider f being FinSequence of TOP-REAL 2 such that
A12: f is_S-Seq and
A13: P = L~f and
A14: p = f/.1 and q = f/.len f by A10,TOPREAL4:def 1;
  reconsider f as being_S-Seq FinSequence of TOP-REAL 2 by A12;
A15: L~f is non empty;
  TOP-REAL 2 = TopSpaceMetr Euclid 2 by EUCLID:def 8;
  then reconsider P1 = P, C1 = C as non empty compact Subset of
  TopSpaceMetr Euclid 2 by A13,A15;
  set d = min_dist_min(P1,C1);
  UBD C is_outside_component_of C by JORDAN2C:76;
  then UBD C is_a_component_of C` by JORDAN2C:def 4;
  then C misses UBD C by JORDAN2C:125;
  then P misses C by A11,XBOOLE_1:63;
  then d > 0 by JGRAPH_1:55;
  then d/2 > 0 by REAL_2:127;
  then consider n such that 1 < n and
A16: dist(Gauge(C,n)*(1,1),Gauge(C,n)*(1,2)) < d/2 and
A17: dist(Gauge(C,n)*(1,1),Gauge(C,n)*(2,1)) < d/2 by GOBRD14:21;
  set G = Gauge(C,n), g = Cage(C,n);
  2 <= len f by REALSET1:13;
  then
A18: x in P by A13,A14,JORDAN3:34;
A19: now
    assume L~g /\ P <> {};
    then consider a being set such that
A20: a in L~g /\ P by XBOOLE_0:def 1;
    reconsider a' = a as Point of Euclid 2 by A20,TOPREAL3:13;
    reconsider A = a as Point of TOP-REAL 2 by A20;
A21: a in L~g & a in P by A20,XBOOLE_0:def 4;
    then consider i such that
A22: 1 <= i & i+1 <= len g and
A23: a in LSeg(g,i) by SPPOL_2:13;
A24: g is_sequence_on G by JORDAN9:def 1;
    then consider i1, j1, i2, j2 being Element of NAT such that
A25: [i1,j1] in Indices G and
A26: g/.i = G*(i1,j1) and
A27: [i2,j2] in Indices G and
A28: g/.(i+1) = G*(i2,j2) and
A29: i1 = i2 & j1+1 = j2 or i1+1 = i2 & j1 = j2 or
    i1 = i2+1 & j1 = j2 or i1 = i2 & j1 = j2+1 by A22,JORDAN8:6;
    right_cell(g,i,G) meets C by A22,JORDAN9:33;
    then consider c being set such that
A30: c in right_cell(g,i,G) /\ C by XBOOLE_0:4;
    reconsider c as Point of Euclid 2 by A30,TOPREAL3:13;
    reconsider c' = c as Point of TOP-REAL 2 by A30;
    c in C by A30,XBOOLE_0:def 4;
    then
A31: dist(a',c) >= d by A21,WEIERSTR:40;
    set e = E-bound C, w = W-bound C, p = N-bound C, s = S-bound C;
A32: len G = width G by JORDAN8:def 1;
    4 <= len G by JORDAN8:13;
    then
A33: 1 <= len G & 1 <= width G & 1+1 <= len G & 1+1 <= width G
    by A32,XXREAL_0:2;
    then [1,1] in Indices G & [1,1+1] in Indices G &
    [1+1,1] in Indices G by MATRIX_1:37;
    then
A34: dist(G*(1,1),G*(1,1+1)) = (p-s)/2|^n &
    dist(G*(1,1),G*(1+1,1)) = (e-w)/2|^n by GOBRD14:19,20;
A35: c in right_cell(g,i,G) by A30,XBOOLE_0:def 4;
    left_cell(g,i,G) /\ right_cell(g,i,G) = LSeg(g,i) by A22,A24,GOBRD13:30;
    then
A36: a in right_cell(g,i,G) by A23,XBOOLE_0:def 4;
    now per cases by A29;
      case
A37:    i1 = i2 & j1+1 = j2;
        then
A38:    j1+1 <= width G by A27,MATRIX_1:39;
        then
A39:    1 <= i1 & i1 < len G & 1 <= j1 & j1 < width G
        by A22,A25,A26,A27,A28,A37,Th1,MATRIX_1:39,NAT_1:13;
A40:    right_cell(g,i,G) = cell(G,i1,j1)
        by A22,A24,A25,A26,A27,A28,A37,GOBRD13:23
          .= { |[r,t]| where r, t is Real :
        G*(i1,1)`1 <= r & r <= G*(i1+1,1)`1 &
        G*(1,j1)`2 <= t & t <= G*(1,j1+1)`2 } by A39,GOBRD11:32;
        then consider r, t being Real such that
A41:    c = |[r,t]| and
A42:    G*(i1,1)`1 <= r & r <= G*(i1+1,1)`1 &
        G*(1,j1)`2 <= t & t <= G*(1,j1+1)`2 by A35;
        consider aa, ab being Real such that
A43:    a = |[aa,ab]| and
A44:    G*(i1,1)`1 <= aa & aa <= G*(i1+1,1)`1 &
        G*(1,j1)`2 <= ab & ab <= G*(1,j1+1)`2 by A36,A40;
A45:    c'`1 = r & c'`2 = t & A`1 = aa & A`2 = ab by A41,A43,EUCLID:56;
        1 <= i1+1 & i1+1 <= len G & 1 <= j1+1 by A39,NAT_1:13;
        then
A46:    [i1,1] in Indices G & [i1+1,1] in Indices G &
        [1,j1] in Indices G & [1,j1+1] in Indices G
        by A33,A38,A39,MATRIX_1:37;
        then
A47:    dist(G*(i1,1),G*(i1+1,1)) = G*(i1+1,1)`1 - G*(i1,1)`1 &
        dist(G*(1,j1),G*(1,j1+1)) = G*(1,j1+1)`2 - G*(1,j1)`2
        by GOBRD14:15,16;
        dist(G*(1,j1),G*(1,j1+1)) = (p-s)/2|^n &
        dist(G*(i1,1),G*(i1+1,1)) = (e-w)/2|^n by A46,GOBRD14:19,20;
        hence dist(A,c') <= (p-s)/2|^n + (e-w)/2|^n by A42,A44,A45,A47,
TOPREAL6:104;
      end;
      case
A48:    i1+1 = i2 & j1 = j2;
        then
A49:    i1+1 <= len G by A27,MATRIX_1:39;
        j2 > 1 by A22,A25,A26,A27,A28,A48,Th3;
        then
A50:    j2-1 > 0 by XREAL_1:52;
        then
A51:    j2-1 = j2-'1 by XREAL_0:def 2;
A52:    j2 <= width G by A27,MATRIX_1:39;
        width G - 1 < width G - 0 by XREAL_1:17;
        then
A53:    1 <= i1 & i1 < len G & 1 <= j2-'1 & j2-'1 < width G
        by A25,A49,A50,A51,A52,MATRIX_1:39,NAT_1:13,14,XREAL_1:17;
A54:    right_cell(g,i,G) = cell(G,i1,j2-'1)
        by A22,A24,A25,A26,A27,A28,A48,GOBRD13:25
          .= { |[r,t]| where r, t is Real :
        G*(i1,1)`1 <= r & r <= G*(i1+1,1)`1 &
        G*(1,j2-'1)`2 <= t & t <= G*(1,j2-'1+1)`2 } by A53,GOBRD11:32;
        then consider r, t being Real such that
A55:    c = |[r,t]| and
A56:    G*(i1,1)`1 <= r & r <= G*(i1+1,1)`1 &
        G*(1,j2-'1)`2 <= t & t <= G*(1,j2-'1+1)`2 by A35;
        consider aa, ab being Real such that
A57:    a = |[aa,ab]| and
A58:    G*(i1,1)`1 <= aa & aa <= G*(i1+1,1)`1 &
        G*(1,j2-'1)`2 <= ab & ab <= G*(1,j2-'1+1)`2 by A36,A54;
A59:    c'`1 = r & c'`2 = t & A`1 = aa & A`2 = ab by A55,A57,EUCLID:56;
A60:    1 <= i1+1 & i1+1 <= len G & j2-'1+1 <= width G by A53,NAT_1:13;
        1 <= j2-'1+1 by A53,NAT_1:13;
        then
A61:    [i1,1] in Indices G & [i1+1,1] in Indices G &
        [1,j2-'1] in Indices G & [1,j2-'1+1] in Indices G
        by A33,A53,A60,MATRIX_1:37;
        then
A62:    dist(G*(i1,1),G*(i1+1,1)) = G*(i1+1,1)`1 - G*(i1,1)`1 &
        dist(G*(1,j2-'1),G*(1,j2-'1+1)) = G*(1,j2-'1+1)`2 - G*(1,j2-'1)`2
        by GOBRD14:15,16;
        dist(G*(1,j2-'1),G*(1,j2-'1+1)) = (p-s)/2|^n &
        dist(G*(i1,1),G*(i1+1,1)) = (e-w)/2|^n by A61,GOBRD14:19,20;
        hence dist(A,c') <= (p-s)/2|^n + (e-w)/2|^n by A56,A58,A59,A62,
TOPREAL6:104;
      end;
      case
A63:    i1 = i2+1 & j1 = j2;
        then i2+1 <= len G by A25,MATRIX_1:39;
        then
A64:    1 <= i2 & i2 < len G & 1 <= j1 & j1 < width G
        by A22,A25,A26,A27,A28,A63,Th4,MATRIX_1:39,NAT_1:13;
A65:    right_cell(g,i,G) = cell(G,i2,j1)
        by A22,A24,A25,A26,A27,A28,A63,GOBRD13:27
          .= { |[r,t]| where r, t is Real :
        G*(i2,1)`1 <= r & r <= G*(i2+1,1)`1 &
        G*(1,j1)`2 <= t & t <= G*(1,j1+1)`2 } by A64,GOBRD11:32;
        then consider r, t being Real such that
A66:    c = |[r,t]| and
A67:    G*(i2,1)`1 <= r & r <= G*(i2+1,1)`1 &
        G*(1,j1)`2 <= t & t <= G*(1,j1+1)`2 by A35;
        consider aa, ab being Real such that
A68:    a = |[aa,ab]| and
A69:    G*(i2,1)`1 <= aa & aa <= G*(i2+1,1)`1 &
        G*(1,j1)`2 <= ab & ab <= G*(1,j1+1)`2 by A36,A65;
A70:    c'`1 = r & c'`2 = t & A`1 = aa & A`2 = ab by A66,A68,EUCLID:56;
A71:    1 <= i2+1 & i2+1 <= len G & j1+1 <= width G by A64,NAT_1:13;
        1 <= j1+1 by NAT_1:12;
        then
A72:    [i2,1] in Indices G & [i2+1,1] in Indices G &
        [1,j1] in Indices G & [1,j1+1] in Indices G
        by A33,A64,A71,MATRIX_1:37;
        then
A73:    dist(G*(i2,1),G*(i2+1,1)) = G*(i2+1,1)`1 - G*(i2,1)`1 &
        dist(G*(1,j1),G*(1,j1+1)) = G*(1,j1+1)`2 - G*(1,j1)`2
        by GOBRD14:15,16;
        dist(G*(1,j1),G*(1,j1+1)) = (p-s)/2|^n &
        dist(G*(i2,1),G*(i2+1,1)) = (e-w)/2|^n by A72,GOBRD14:19,20;
        hence dist(A,c') <= (p-s)/2|^n + (e-w)/2|^n by A67,A69,A70,A73,
TOPREAL6:104;
      end;
      case
A74:    i1 = i2 & j1 = j2+1;
        then
A75:    j2+1 <= width G by A25,MATRIX_1:39;
        i1 > 1 by A22,A25,A26,A27,A28,A74,Th2;
        then
A76:    i1-1 > 0 by XREAL_1:52;
        then
A77:    i1-1 = i1-'1 by XREAL_0:def 2;
A78:    i1 <= len G by A25,MATRIX_1:39;
        len G - 1 < len G - 0 by XREAL_1:17;
        then
A79:    1 <= i1-'1 & i1-'1 < len G & 1 <= j2 & j2 < width G
        by A27,A75,A76,A77,A78,MATRIX_1:39,NAT_1:13,14,XREAL_1:17;
A80:    right_cell(g,i,G) = cell(G,i1-'1,j2)
        by A22,A24,A25,A26,A27,A28,A74,GOBRD13:29
          .= { |[r,t]| where r, t is Real :
        G*(i1-'1,1)`1 <= r & r <= G*(i1-'1+1,1)`1 &
        G*(1,j2)`2 <= t & t <= G*(1,j2+1)`2 } by A79,GOBRD11:32;
        then consider r, t being Real such that
A81:    c = |[r,t]| and
A82:    G*(i1-'1,1)`1 <= r & r <= G*(i1-'1+1,1)`1 &
        G*(1,j2)`2 <= t & t <= G*(1,j2+1)`2 by A35;
        consider aa, ab being Real such that
A83:    a = |[aa,ab]| and
A84:    G*(i1-'1,1)`1 <= aa & aa <= G*(i1-'1+1,1)`1 &
        G*(1,j2)`2 <= ab & ab <= G*(1,j2+1)`2 by A36,A80;
A85:    c'`1 = r & c'`2 = t & A`1 = aa & A`2 = ab by A81,A83,EUCLID:56;
        1 <= i1-'1+1 & i1-'1+1 <= len G & 1 <= j2+1 by A79,NAT_1:13;
        then
A86:    [i1-'1,1] in Indices G & [i1-'1+1,1] in Indices G &
        [1,j2] in Indices G & [1,j2+1] in Indices G
        by A33,A75,A79,MATRIX_1:37;
        then
A87:    dist(G*(i1-'1,1),G*(i1-'1+1,1)) = G*(i1-'1+1,1)`1 - G*(i1 -'1,1)`1 &
        dist(G*(1,j2),G*(1,j2+1)) = G*(1,j2+1)`2 - G*(1,j2)`2
        by GOBRD14:15,16;
        dist(G*(1,j2),G*(1,j2+1)) = (p-s)/2|^n & dist(G*(i1-'1,1),G*
        (i1-'1+1,1)) = (e-w)/2|^n by A86,GOBRD14:19,20;
        hence dist(A,c') <= (p-s)/2|^n + (e-w)/2|^n by A82,A84,A85,A87,
TOPREAL6:104;
      end;
    end;
    then
A88: dist(a',c) <= (p-s)/2|^n + (e-w)/2|^n by TOPREAL6:def 1;
    (p-s)/2|^n + (e-w)/2|^n < d/2 + d/2 by A16,A17,A34,XREAL_1:10;
    hence contradiction by A31,A88,XXREAL_0:2;
  end;
  UBD L~g is_outside_component_of L~g by JORDAN2C:76;
  then consider E being Subset of (TOP-REAL 2)|(L~g)` such that
A89: E = UBD L~g & E is_a_component_of (TOP-REAL 2)|(L~g)` and
  not E is bounded Subset of Euclid 2 by JORDAN2C:18;
A90: P c= (L~g)`
  proof
    let a be set;
    assume
A91: a in P;
    then not a in L~g by A19,XBOOLE_0:def 4;
    hence thesis by A91,SUBSET_1:50;
  end;
A92: P is_an_arc_of p,q by A10,TOPREAL4:3;
A93: UBD L~g is_a_component_of (L~g)` by A89,CONNSP_1:def 6;
A94: g/.1 = N-min L~g by JORDAN9:34;
  0 < n or 0 = n by NAT_1:2;
  then N-bound L~(Cage(C,0)) >= N-bound L~g by Th7;
  then q`2 > N-bound L~g by A5,XXREAL_0:2;
  then q in LeftComp g by A94,JORDAN2C:121;
  then q in UBD L~g by GOBRD14:46;
  then
A95: {q} c= UBD L~g by ZFMISC_1:37;
  {q} meets P
  proof
    now
      take a = q;
      thus a in {q} & a in P by A92,TARSKI:def 1,TOPREAL1:4;
    end;
    hence thesis by XBOOLE_0:3;
  end;
  then
A96: P c= UBD L~g by A90,A92,A93,A95,GOBOARD9:6,JORDAN6:11;
  UBD L~g in UBD-Family C;
  hence x in union UBD-Family C by A18,A96,TARSKI:def 4;
end;

theorem Th15:
  C c= meet BDD-Family C
proof
  for Z being set st Z in BDD-Family C holds C c= Z
  proof
    let Z be set;
    assume Z in BDD-Family C;
    then consider k being Element of NAT such that
A1: Z = Cl BDD L~Cage(C,k);
A2: C c= BDD L~Cage(C,k) by Th12;
    BDD L~Cage(C,k) c= Cl BDD L~Cage(C,k) by PRE_TOPC:48;
    hence thesis by A1,A2,XBOOLE_1:1;
  end;
  hence thesis by SETFAM_1:6;
end;

theorem Th16:
  BDD C misses LeftComp Cage(C,n)
proof
  set f = Cage(C,n);
  assume BDD C /\ LeftComp f <> {};
  then consider x being Point of TOP-REAL 2 such that
A1: x in BDD C /\ LeftComp f by SUBSET_1:10;
A2: x in BDD C & x in LeftComp f by A1,XBOOLE_0:def 4;
  BDD C misses UBD C by JORDAN2C:28;
  then BDD C /\ UBD C = {} by XBOOLE_0:def 7;
  then not x in UBD C by A2,XBOOLE_0:def 4;
  then
A3: not x in union UBD-Family C by Th14;
  UBD L~f in
  { UBD L~Cage(C,k) where k is Element of NAT : not contradiction };
  then not x in UBD L~f by A3,TARSKI:def 4;
  hence contradiction by A2,GOBRD14:46;
end;

theorem Th17:
  BDD C c= RightComp Cage(C,n)
proof
  set f = Cage(C,n);
  let x be set;
  assume
A1: x in BDD C;
  BDD C misses UBD C by JORDAN2C:28;
  then BDD C /\ UBD C = {} by XBOOLE_0:def 7;
  then not x in UBD C by A1,XBOOLE_0:def 4;
  then
A2: not x in union UBD-Family C by Th14;
  UBD L~f in
  { UBD L~Cage(C,k) where k is Element of NAT : not contradiction };
  then
A3: not x in UBD L~Cage(C,n) by A2,TARSKI:def 4;
A4: UBD L~f = union {E where E is Subset of TOP-REAL 2:
  E is_outside_component_of L~f} by JORDAN2C:def 6;
  LeftComp f is_outside_component_of L~f by GOBRD14:44;
  then LeftComp f in {E where E is Subset of TOP-REAL 2:
  E is_outside_component_of L~f};
  then
A5: not x in LeftComp f by A3,A4,TARSKI:def 4;
A6: L~f = (Cl LeftComp f) \ LeftComp f by GOBRD14:30;
  now
    assume x in Cl LeftComp f;
    then BDD C meets LeftComp f by A1,PRE_TOPC:def 13;
    hence contradiction by Th16;
  end;
  then not x in L~f by A6,XBOOLE_0:def 5;
  hence x in RightComp f by A1,A5,GOBRD14:28;
end;

theorem Th18:
  P is_inside_component_of C implies P misses L~Cage(C,n)
proof
  set f = Cage(C,n);
  assume
A1: P is_inside_component_of C;
  assume P /\ L~f <> {};
  then consider x being Point of TOP-REAL 2 such that
A2: x in P /\ L~f by SUBSET_1:10;
A3: x in P & x in L~f by A2,XBOOLE_0:def 4;
  P c= BDD C by A1,JORDAN2C:26;
  then
A4: x in BDD C by A3;
  BDD C c= RightComp f by Th17;
  hence contradiction by A3,A4,GOBRD14:28;
end;

theorem Th19:
  BDD C misses L~Cage(C,n)
proof
A1: BDD C = union {B where B is Subset of TOP-REAL 2:
  B is_inside_component_of C} by JORDAN2C:def 5;
  assume not thesis;
  then consider x being set such that
A2: x in BDD C /\ L~Cage(C,n) by XBOOLE_0:4;
  x in BDD C by A2,XBOOLE_0:def 4;
  then consider Z being set such that
A3: x in Z & Z in {B where B is Subset of TOP-REAL 2:
  B is_inside_component_of C} by A1,TARSKI:def 4;
  consider B being Subset of TOP-REAL 2 such that
A4: Z = B & B is_inside_component_of C by A3;
  B misses L~Cage(C,n) by A4,Th18;
  then
A5: B /\ L~Cage(C,n) = {} by XBOOLE_0:def 7;
  x in L~Cage(C,n) by A2,XBOOLE_0:def 4;
  hence thesis by A3,A4,A5,XBOOLE_0:def 4;
end;

theorem Th20:
  COMPLEMENT UBD-Family C = BDD-Family C
proof
  for P being Subset of TOP-REAL 2
  holds P in BDD-Family C iff P` in UBD-Family C
  proof
    let P being Subset of TOP-REAL 2;
    thus P in BDD-Family C implies P` in UBD-Family C
    proof
      assume P in BDD-Family C;
      then consider k such that
A1:   P = Cl BDD L~Cage(C,k);
      P = Cl RightComp Cage(C,k) by A1,GOBRD14:47;
      then
A2:   P = (RightComp Cage(C,k)) \/ L~Cage(C,k) by GOBRD14:31;
A3:   L~Cage(C,k) misses LeftComp Cage(C,k) by SPRECT_3:43;
      RightComp Cage(C,k) misses LeftComp Cage(C,k) by GOBRD14:24;
      then
A4:   P misses LeftComp Cage(C,k) by A2,A3,XBOOLE_1:70;
      P` /\ (LeftComp Cage(C,k))` = (P \/ LeftComp Cage(C,k))` by XBOOLE_1:53
        .= ([#]the carrier of TOP-REAL 2)` by A2,GOBRD14:25
        .= {}the carrier of TOP-REAL 2 by XBOOLE_1:37;
      then P` misses (LeftComp Cage(C,k))` by XBOOLE_0:def 7;
      then P` = LeftComp Cage(C,k) by A4,SUBSET_1:46;
      then P` = UBD L~Cage(C,k) by GOBRD14:46;
      hence P` in UBD-Family C;
    end;
    assume P` in UBD-Family C;
    then consider k such that
A5: P` = UBD L~Cage(C,k);
A6: P` = LeftComp Cage(C,k) by A5,GOBRD14:46;
    then
A7: P` misses RightComp Cage(C,k) by GOBRD14:24;
    P` misses L~Cage(C,k) by A6,SPRECT_3:43;
    then P` misses (RightComp Cage(C,k)) \/ L~Cage(C,k) by A7,XBOOLE_1:70;
    then
A8: P` misses Cl RightComp Cage(C,k) by GOBRD14:31;
    P`` /\ (Cl RightComp Cage(C,k))`
    = (P` \/ (Cl RightComp Cage(C,k)))` by XBOOLE_1:53
      .= (P` \/ ((RightComp Cage(C,k)) \/ L~Cage(C,k)))` by GOBRD14:31
      .= ([#]the carrier of TOP-REAL 2)` by A6,GOBRD14:25
      .= {}the carrier of TOP-REAL 2 by XBOOLE_1:37;
    then P`` misses (Cl RightComp Cage(C,k))` by XBOOLE_0:def 7;
    then P`` = Cl RightComp Cage(C,k) by A8,SUBSET_1:46;
    then P = Cl BDD L~Cage(C,k) by GOBRD14:47;
    hence P in BDD-Family C;
  end;
  hence thesis by SETFAM_1:def 8;
end;

theorem
  meet BDD-Family C = C \/ BDD C
proof
  (BDD C) misses (UBD C) by JORDAN2C:28;
  then
A1: (BDD C) /\ (UBD C) = {} by XBOOLE_0:def 7;
  thus meet BDD-Family C c= C \/ BDD C
  proof
    let x be set;
    assume
A2: x in meet BDD-Family C;
    COMPLEMENT UBD-Family C = BDD-Family C by Th20;
    then ([#] the carrier of TOP-REAL 2) \ union UBD-Family C
    = meet BDD-Family C by SETFAM_1:47;
    then not x in union UBD-Family C by A2,XBOOLE_0:def 5;
    then
A3: not x in UBD C by Th14;
    per cases;
    suppose not x in C;
      then
A4:   x in C` by A2,SUBSET_1:50;
      BDD C \/ UBD C = C` by JORDAN2C:31;
      then x in BDD C by A3,A4,XBOOLE_0:def 3;
      hence x in C \/ BDD C by XBOOLE_0:def 3;
    end;
    suppose x in C;
      hence x in C \/ BDD C by XBOOLE_0:def 3;
    end;
  end;
A5: BDD C c= meet BDD-Family C
  proof
    let x be set;
    assume
A6: x in BDD C;
    then not x in UBD C by A1,XBOOLE_0:def 4;
    then
A7: not x in union UBD-Family C by Th14;
    for Y being set st Y in BDD-Family C holds x in Y
    proof
      let Y be set;
      assume Y in BDD-Family C;
      then consider n such that
A8:   Y = Cl BDD L~Cage(C,n) and not contradiction;
A9:   UBD L~Cage(C,n) = union{B where B is Subset of TOP-REAL 2:
      B is_outside_component_of L~Cage(C,n)} by JORDAN2C:def 6;
A10:  BDD L~Cage(C,n) = union{B where B is Subset of TOP-REAL 2:
      B is_inside_component_of L~Cage(C,n)} by JORDAN2C:def 5;
      UBD L~Cage(C,n) in UBD-Family C;
      then
A11:  not x in UBD L~Cage(C,n) by A7,TARSKI:def 4;
      LeftComp Cage(C,n) is_outside_component_of L~Cage(C,n) by GOBRD14:44;
      then LeftComp Cage(C,n) in {B where B is Subset of TOP-REAL 2:
      B is_outside_component_of L~Cage(C,n)};
      then
A12:  not x in LeftComp Cage(C,n) by A9,A11,TARSKI:def 4;
      BDD C misses L~Cage(C,n) by Th19;
      then BDD C /\ L~Cage(C,n) = {} by XBOOLE_0:def 7;
      then not x in L~Cage(C,n) by A6,XBOOLE_0:def 4;
      then
A13:  x in RightComp Cage(C,n) by A6,A12,GOBRD14:28;
      RightComp Cage(C,n) is_inside_component_of L~Cage(C,n) by GOBRD14:45;
      then RightComp Cage(C,n) in {B where B is Subset of TOP-REAL 2:
      B is_inside_component_of L~Cage(C,n)};
      then
A14:  x in BDD L~Cage(C,n) by A10,A13,TARSKI:def 4;
      BDD L~Cage(C,n) c= Cl BDD L~Cage(C,n) by PRE_TOPC:48;
      hence x in Y by A8,A14;
    end;
    hence x in meet BDD-Family C by SETFAM_1:def 1;
  end;
  C c= meet BDD-Family C by Th15;
  hence thesis by A5,XBOOLE_1:8;
end;

