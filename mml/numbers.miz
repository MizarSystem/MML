:: Subsets of Complex Numbers
::  by Andrzej Trybulec
::
:: Received November 7, 2003
:: Copyright (c) 2003 Association of Mizar Users

environ

 vocabularies COMPLEX1, FUNCT_2, FUNCT_1, FUNCOP_1, ARYTM_2, BOOLE, ARYTM_3,
      ORDINAL2, ARYTM, ORDINAL1, RELAT_1, RAT_1, INT_1, ORDINAL3;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, RELAT_1, FUNCT_1, FUNCT_2,
      FUNCT_4, ORDINAL1, ORDINAL2, ORDINAL3, ARYTM_3, ARYTM_2;
 constructors ARYTM_1, FRAENKEL, FUNCT_4, ORDINAL3;
 registrations XBOOLE_0, ARYTM_2, SUBSET_1, ORDINAL2, ARYTM_3, FRAENKEL,
      FUNCT_2, FUNCT_4, ORDINAL1, ZFMISC_1;
 requirements BOOLE, SUBSET, NUMERALS;
 definitions ARYTM_3, XBOOLE_0;
 theorems XBOOLE_1, ARYTM_2, ZFMISC_1, ARYTM_3, XBOOLE_0, TARSKI, ORDINAL2,
      ORDINAL3, ENUMSET1, ORDINAL1, FUNCT_2, FUNCT_4;
 schemes DOMAIN_1;

begin

Lm1: one = succ 0 by ORDINAL2:def 4 .= 1;

notation
 synonym NAT for omega;
end;

definition
 func REAL equals
:Def1: REAL+ \/ [:{0},REAL+:] \ {[0,0]};
 coherence;
end;

Lm2: REAL+ c= REAL
proof
A1: REAL+ c= REAL+ \/ [:{0},REAL+:] by XBOOLE_1:7;
    not [0,0] in REAL+ by ARYTM_2:3;
 hence thesis by A1,Def1,ZFMISC_1:40;
end;

registration
 cluster REAL -> non empty;
 coherence by Lm2,XBOOLE_1:3;
end;

definition
 func COMPLEX equals
:Def2: Funcs({0,one},REAL)
        \ { x where x is Element of Funcs({0,one},REAL): x.one = 0}
        \/ REAL;
 coherence;
 func RAT equals
:Def3: RAT+ \/ [:{0},RAT+:] \ {[0,0]};
 coherence;
 func INT equals
:Def4: NAT \/ [:{0},NAT:] \ {[0,0]};
 coherence;
 redefine func NAT -> Subset of REAL;
 coherence by Lm2,ARYTM_2:2,XBOOLE_1:1;
end;

Lm3: RAT+ c= RAT
proof
A1: RAT+ c= RAT+ \/ [:{0},RAT+:] by XBOOLE_1:7;
      not [0,0] in RAT+ by ARYTM_3:68;
 hence thesis by A1,Def3,ZFMISC_1:40;
end;

Lm4: NAT c= INT
proof
A1: NAT c= NAT \/ [:{0},NAT:] by XBOOLE_1:7;
    not [0,0] in NAT by ARYTM_3:38;
 hence thesis by A1,Def4,ZFMISC_1:40;
end;

registration
 cluster COMPLEX -> non empty;
 coherence by Def2;
 cluster RAT -> non empty;
 coherence by Lm3,XBOOLE_1:3;
 cluster INT -> non empty;
 coherence by Lm4,XBOOLE_1:3;
end;

reserve i,j,k for Element of NAT;
reserve a,b for Element of REAL;

Lm5:
 for x,y,z being set st [x,y] = {z}
  holds z = {x} & x = y
proof let x,y,z be set;
 assume [x,y] = {z};
  then {{x,y},{x}} = {z} by TARSKI:def 5;
  then A1:  {x,y} in {z} & {x} in {z} by TARSKI:def 2;
 hence
A2: z = {x} by TARSKI:def 1;
      {x,y} = z by A1,TARSKI:def 1;
 hence x = y by A2,ZFMISC_1:9;
end;

Lm6:
 not (0,one)-->(a,b) in REAL
proof set f = (0,one)-->(a,b);
A1: f = {[0,a],[one,b]} by FUNCT_4:71;
       now assume f in {[i,j]: i,j are_relative_prime & j <> {}};
     then consider i,j such that
A2:   f = [i,j] and
          i,j are_relative_prime and
          j <> {};
A3:   f = {{i,j},{i}} by A2,TARSKI:def 5;
     then A4:    {i} in f by TARSKI:def 2;
A5:  {i,j} in f by A3,TARSKI:def 2;
A6:  [0,a] = {{0,a},{0}} by TARSKI:def 5;
A7:  [one,b] = {{one,b},{one}} by TARSKI:def 5;
A8: now assume i = j;
      then {i} = {i,j} by ENUMSET1:69;
      then [i,j] = {{i}} by A2,A3,ENUMSET1:69;
      then [0,a] in {{i}} & [one,b] in {{i}} by A1,A2,TARSKI:def 2;
      then [0,a] = {i} & [one,b] = {i} by TARSKI:def 1;
     hence contradiction by ZFMISC_1:33;
    end;
    per cases by A1,A4,A5,TARSKI:def 2;
    suppose {i,j} = [0,a] & {i} = [0,a];
     hence contradiction by A8,ZFMISC_1:9;
    end;
    suppose that
A9:  {i,j} = [0,a] and
A10:  {i} = [one,b];
A11:    i = {one} by A10,Lm5;
          i in [0,a] by A9,TARSKI:def 2;
      then i = {0,a} or i = {0} by A6,TARSKI:def 2;
      then 0 in i by TARSKI:def 1,def 2;
     hence contradiction by A11,TARSKI:def 1;
    end;
    suppose that
A12:  {i,j} = [one,b] and
A13:  {i} = [0,a];
A14:    i = {0} by A13,Lm5;
          i in [one,b] by A12,TARSKI:def 2;
      then i = {one,b} or i = {one} by A7,TARSKI:def 2;
      then one in i by TARSKI:def 1,def 2;
     hence contradiction by A14,TARSKI:def 1;
    end;
    suppose {i,j} = [one,b] & {i} = [one,b];
     hence contradiction by A8,ZFMISC_1:9;
   end;
   end;
   then A15: not f in
     {[i,j]: i,j are_relative_prime & j <> {}} \ {[k,one]: not contradiction}
       by XBOOLE_0:def 4;
       now assume f in omega;
     then f is ordinal by ORDINAL1:23;
     then {} in f by A1,ORDINAL3:10;
    hence contradiction by A1,TARSKI:def 2;
   end;
   then A16: not f in RAT+ by A15,ARYTM_3:def 6,XBOOLE_0:def 2;
 set IR = { A where A is Subset of RAT+:
    for r being Element of RAT+ st r in A holds
    (for s being Element of RAT+ st s <=' r holds s in A) &
     ex s being Element of RAT+ st s in A & r < s};
     not ex x,y being set st {[0,x],[one,y]} in IR
proof given x,y being set such that
A17: {[0,x],[one,y]} in IR;
 consider A being Subset of RAT+ such that
A18: {[0,x],[one,y]} = A and
A19: for r being Element of RAT+ st r in A holds
     (for s being Element of RAT+ st s <=' r holds s in A) &
      ex s being Element of RAT+ st s in A & r < s by A17;
A20: [0,x] in A by A18,TARSKI:def 2;
      for r,s being Element of RAT+ st r in A & s <=' r holds s in A by A19;
  then consider r1,r2,r3 being Element of RAT+ such that
A21: r1 in A & r2 in A & r3 in A and
A22: r1 <> r2 & r2 <> r3 & r3 <> r1 by A20,ARYTM_3:82;
      (r1 = [0,x] or r1 = [one,y]) &
  (r2 = [0,x] or r2 = [one,y]) &
  (r3 = [0,x] or r3 = [one,y]) by A18,A21,TARSKI:def 2;
 hence contradiction by A22;
end;
  then not f in IR by A1;
  then not f in DEDEKIND_CUTS by ARYTM_2:def 1,XBOOLE_0:def 4;
  then not f in RAT+ \/ DEDEKIND_CUTS by A16,XBOOLE_0:def 2;
  then A23: not f in REAL+ by ARYTM_2:def 2,XBOOLE_0:def 4;
      now assume f in [:{{}},REAL+:];
    then consider x,y being set such that
A24:  x in {{}} and
         y in REAL+ and
A25:  f = [x,y] by ZFMISC_1:103;
        x = 0 by A24,TARSKI:def 1;
    then A26:    f = {{0,y},{0}} by A25,TARSKI:def 5;
        f = {[0,a],[one,b]} by FUNCT_4:71;
    then [one,b] in f by TARSKI:def 2;
    then A27:   [one,b] = {0} or [one,b] = {0,y} by A26,TARSKI:def 2;
   per cases by A27,TARSKI:def 5;
   suppose {{one,b},{one}} = {0};
    then 0 in {{one,b},{one}} by TARSKI:def 1;
   hence contradiction by TARSKI:def 2;
   end;
   suppose {{one,b},{one}} = {0,y};
    then 0 in {{one,b},{one}} by TARSKI:def 2;
   hence contradiction by TARSKI:def 2;
  end;
  end;
  then not f in REAL+ \/ [:{{}},REAL+:] by A23,XBOOLE_0:def 2;
 hence thesis by Def1,XBOOLE_0:def 4;
end;

theorem Th1:
 REAL c< COMPLEX
  proof
   thus REAL c= COMPLEX by Def2,XBOOLE_1:7;
A1:  dom (0,1) --> (0,1) = {0,1} by FUNCT_4:65;
A2:  rng (0,1) --> (0,1) c= {0,1} by FUNCT_4:65;
A3: REAL+ c= REAL+ \/ [:{{}},REAL+:] by XBOOLE_1:7;
      not [{},{}] in REAL+ by ARYTM_2:3;
    then A4:  REAL+ c= REAL by A3,Def1,ZFMISC_1:40;
    then {0,1} c= REAL by Lm1,ARYTM_2:21,ZFMISC_1:38;
    then rng (0,1) --> (0,1) c= REAL by A2,XBOOLE_1:1;
    then A5:   (0,1) --> (0,1) in Funcs({0,one},REAL) by A1,Lm1,FUNCT_2:def 2;
    reconsider z = 0, j = 1 as Element of REAL by A4,Lm1,ARYTM_2:21;
A6:  not (0,1) --> (z,j) in REAL by Lm1,Lm6;
    set X = { x where x is Element of Funcs({0,one},REAL): x.one = 0};
      now assume (0,1) --> (0,1) in X;
      then ex x being Element of Funcs({0,one},REAL) st
       x = (0,1) --> (0,1) & x.one = 0;
     hence contradiction by Lm1,FUNCT_4:66;
    end;
    then (0,1) --> (0,1) in Funcs({0,one},REAL) \ X by A5,XBOOLE_0:def 4;
   hence REAL <> COMPLEX by A6,Def2,XBOOLE_0:def 2;
  end;

Lm7: RAT c= REAL
 proof
     [:{0},RAT+:] c= [:{0},REAL+:] by ARYTM_2:1,ZFMISC_1:118;
   then RAT+ \/ [:{0},RAT+:] c= REAL+ \/ [:{0},REAL+:]
                                     by ARYTM_2:1,XBOOLE_1:13;
  hence thesis by Def1,Def3,XBOOLE_1:33;
 end;

reserve r,s,t for Element of RAT+;
reserve i,j,k for Element of omega;

Lm8:   :: powinien byc w ARYTM_3
 for i,j being ordinal Element of RAT+ st i in j holds i < j
 proof let i,j be ordinal Element of RAT+;
A1: i in omega & j in omega by ARYTM_3:37;
   then reconsider x = i, y = j as Element of REAL+ by ARYTM_2:2;
  assume
A2: i in j;
   then x <=' y by A1,ARYTM_2:19;
   then consider x',y' being Element of RAT+ such that
A3: x = x' & y = y' and
A4: x' <=' y' by ARYTM_2:def 5;
     i <> j by A2;
  hence i < j by A3,A4,ARYTM_3:73;
 end;

Lm9:   :: powinien byc w ARYTM_3
 for i,j being ordinal Element of RAT+ st i c= j holds i <='j
  proof let i,j be ordinal Element of RAT+;
   assume i c= j;
    then consider C being ordinal number such that
A1:   j = i+^C by ORDINAL3:30;
      C in omega by A1,ARYTM_3:1;
    then reconsider C as Element of RAT+ by ARYTM_3:34;
      j = i + C by A1,ARYTM_3:64;
   hence i <='j by ARYTM_3:def 12;
  end;

Lm10: 2 = succ 1 .= succ 0 \/ {1} by ORDINAL1:def 1
      .= 0 \/ {0} \/ {1} by ORDINAL1:def 1
      .= {0,1} by ENUMSET1:41;

Lm11:
  for i,k being natural Ordinal st i *^ i = 2 *^ k
   ex k being natural Ordinal st i = 2 *^ k
proof let i,k be natural Ordinal; assume
A1: i *^ i = 2 *^ k;
   {} in 2 by ORDINAL1:24;
 then A2: i mod^ 2 in 2 by ARYTM_3:10;
set id2 = i div^ 2;
 per cases by A2,Lm10,TARSKI:def 2;
 suppose
A3: i mod^ 2 = 0;
 take k = id2;
 thus i = k*^2+^0 by A3,ORDINAL3:78
    .= 2 *^ k by ORDINAL2:44;
 end;
 suppose i mod^ 2 = 1;
  then i = id2*^2+^1 by ORDINAL3:78;
  then A4:  i *^ i = id2*^2*^ (id2*^2+^1) +^ one *^ (id2*^2+^1) by Lm1,
ORDINAL3:54
     .= id2*^2*^ (id2*^2+^1) +^  (one *^(id2*^2)+^one *^1) by ORDINAL3:54
     .= id2*^2*^ (id2*^2+^1) +^  (one *^(id2*^2)+^ 1)by ORDINAL2:56
     .= id2*^2*^ (id2*^2+^1) +^  one *^(id2*^2)+^ 1 by ORDINAL3:33
     .= id2*^2*^ (id2*^2+^1 +^  one) +^ 1 by ORDINAL3:54
     .= id2*^ (id2*^2+^1 +^  one)*^2 +^ 1 by ORDINAL3:58;
A5: 2 divides id2*^ (id2*^2+^1 +^  one)*^2 by ARYTM_3:def 2;
    2 divides i *^ i by A1,ARYTM_3:def 2;
  then A6:  2 divides 1 by A4,A5,ARYTM_3:16;
     1 divides 2 by Lm1,ARYTM_3:14;
 hence thesis by A6,ARYTM_3:13;
end;
end;

    1 in omega;
  then reconsider 1' = 1 as Element of RAT+ by ARYTM_3:34;
    2 in omega;
  then reconsider two = 2 as ordinal Element of RAT+ by ARYTM_3:34;

Lm12:
  one + one = two
proof
    one +^ one = succ(one +^ {}) by ORDINAL2:45,def 4
    .= succ one by ORDINAL2:44
    .= two by Lm1;
 hence one + one = two by ARYTM_3:64;
end;

Lm13:
 for i being Element of RAT+ holds i + i = two *' i
 proof let i be Element of RAT+;
  thus i + i = one *' i + i by ARYTM_3:59
      .= one *' i + one *' i  by ARYTM_3:59
      .= two *' i by Lm12,ARYTM_3:63;
 end;

theorem Th2:
 RAT c< REAL
 proof
  set DD = { A where A is Subset of RAT+: r in A implies
      (for s st s <=' r holds s in A) & ex s st s in A & r < s };
    0 in omega;
  then reconsider 0' = 0 as Element of RAT+ by ARYTM_3:34;
  defpred P[Element of RAT+] means $1 *' $1 < two;
  set X = { s : P[s] };
  reconsider X as Subset of RAT+ from DOMAIN_1:sch 7;
  consider half being Element of RAT+ such that
A1: 1' = two*'half by ARYTM_3:61;
A2:    one <=' two by Lm12,ARYTM_3:def 12;
then A3: one < two by Lm1,ARYTM_3:75;
A4: for r,t st r in X & t <=' r holds t in X
   proof let r,t;
    assume r in X;
     then A5:   ex s st r = s & s *' s < two;
      assume
A6:     t <=' r;
       then A7:      t *' t <=' t *' r by ARYTM_3:90;
         t *' r <=' r *' r by A6,ARYTM_3:90;
       then t *' t <=' r *' r by A7,ARYTM_3:74;
       then t *' t < two by A5,ARYTM_3:76;
      hence t in X;
   end;
A8:  1' *' 1' = 1' by Lm1,ARYTM_3:59;
      2 = succ 1 .= 1 \/ {1} by ORDINAL1:def 1;
    then 1 c= 2 by XBOOLE_1:7;
    then A9:  1' <=' two by Lm9;
then A10: 1' < two by ARYTM_3:75;
    then A11: 1 in X by A8;
A12:  0' <=' 1' by ARYTM_3:71;
    then A13: 0 in X by A4,A11;
A14:  1' *' half = half by Lm1,ARYTM_3:59;
      1' *' half <=' two*'half by A9,ARYTM_3:90;
    then A15: half in X by A1,A4,A11,A14;
A16: now assume X = [0,0];
     then X = {{0,0}, {0}} by TARSKI:def 5
           .= {{0}, {0}} by ENUMSET1:69
           .= {{0}} by ENUMSET1:69;
    hence contradiction by A13,TARSKI:def 1;
   end;
A17: 2 *^ 2 = two *' two by ARYTM_3:65;
A18: 1*^2 = 2 by Lm1,ORDINAL2:56;
     2 = succ 1;
   then 1 in 2 by ORDINAL1:10;
   then A19: 1 *^ 2 in 2 *^ 2 by ORDINAL3:22;
A20: now assume X in {{ s: s < t}: t <> 0};
     then consider t0 being Element of RAT+ such that
A21:   X = { s: s < t0} and
A22:  t0 <> 0;
     set n = numerator t0, d = denominator t0;
A23:   now assume
A24:   t0 *' t0 <> two;
      per cases by A24,ARYTM_3:73;
      suppose t0 *' t0 < two;
       then t0 in X;
       then ex s st s = t0 & s < t0 by A21;
      hence contradiction;
      end;
      suppose
A25:      two < t0 *' t0;
       consider es being Element of RAT+ such that
A26:     two + es = t0 *' t0 or t0 *' t0 + es = two by ARYTM_3:100;
A27:     0' <=' es by ARYTM_3:71;
         now assume 0' = es;
         then two + es = two by ARYTM_3:56;
        hence contradiction by A25,A26,ARYTM_3:def 12;
       end;
       then 0' < es by A27,ARYTM_3:75;
       then consider s such that
A28:     0' < s and
A29:     s < es by ARYTM_3:101;
         now per cases;
       suppose
A30:       s < one;
A31:      s <> 0 by A28;
        then s *' s < s *' one by A30,ARYTM_3:88;
        then A32:       s *' s < s by ARYTM_3:59;
       consider t0_2_s_2 being Element of RAT+ such that
A33:     s *' s + t0_2_s_2 = t0 *' t0 or t0 *' t0 + t0_2_s_2 = s *' s
                                        by ARYTM_3:100;
          es <=' t0 *' t0 by A25,A26,ARYTM_3:def 12;
        then s < t0 *' t0 by A29,ARYTM_3:76;
        then A34:       s *' s < t0 *' t0 by A32,ARYTM_3:77;
          s *' s < es by A29,A32,ARYTM_3:77;
        then two + s *' s < two + es by ARYTM_3:83;
        then A35:      two < t0_2_s_2 by A25,A26,A33,A34,ARYTM_3:83,def 12;
          two *' t0 <> 0 by A22,ARYTM_3:86;
        then consider 2t' being Element of RAT+ such that
A36:     (two *' t0) *' 2t' = one by ARYTM_3:61;
        set x = s *' s *' 2t';
        consider t0_x being Element of RAT+ such that
A37:   x + t0_x = t0 or t0 + t0_x = x by ARYTM_3:100;
          x *' (two *' t0) = s *' s *' one by A36,ARYTM_3:58;
        then A38:      x <=' s *' s or two *' t0 <=' one by ARYTM_3:91;
A39:     now assume
A40:       t0 <=' one;
          then t0 *' t0 <=' t0 *' one by ARYTM_3:90;
          then t0 *' t0 <=' t0 by ARYTM_3:59;
          then t0 *' t0 <=' one by A40,ARYTM_3:74;
         hence contradiction by A2,A25,ARYTM_3:76;
        end;
        then A41:      one *' one < one *' t0 by ARYTM_3:88;
          one *' t0 < two *' t0 by A3,A22,ARYTM_3:88;
        then A42:      one *' one < two *' t0 by A41,ARYTM_3:77;
          s < t0 by A30,A39,ARYTM_3:77;
        then s *' s < t0 by A32,ARYTM_3:77;
        then A43:      x < t0 by A38,A42,ARYTM_3:59,76;
A44:     now assume
A45:       x = 0;
         per cases by A45,ARYTM_3:86;
         suppose s *' s = 0;
         hence contradiction by A31,ARYTM_3:86;
         end;
         suppose 2t' = 0;
         hence contradiction by A36,ARYTM_3:54;
        end;
        end;
A46:      x *' t0_x + x *' t0 + x *' x
             = x *' t0_x + x *' x + x *' t0 by ARYTM_3:57
            .= x *' t0 + x *' t0 by A37,A43,ARYTM_3:63,def 12
            .= x *' t0 *' one + x *' t0 by ARYTM_3:59
            .= x *' t0 *' one + x *' t0 *' one by ARYTM_3:59
            .= t0 *' x *' two by Lm12,ARYTM_3:63
            .= x *' (t0 *' two) by ARYTM_3:58
            .= s *' s *' one by A36,ARYTM_3:58
            .= s *' s by ARYTM_3:59;
          t0_2_s_2 + x *' x + s *' s = (t0_x + x) *' t0 + x *' x
                       by A33,A34,A37,A43,ARYTM_3:57,def 12
            .= t0_x *' (t0_x + x) + x *' t0 + x *' x
                           by A37,A43,ARYTM_3:63,def 12
            .= t0_x *' t0_x + x *' t0_x + x *' t0 + x *' x by ARYTM_3:63
            .= t0_x *' t0_x + x *' t0_x + (x *' t0 + x *' x) by ARYTM_3:57
            .= t0_x *' t0_x + (x *' t0_x + (x *' t0 + x *' x)) by ARYTM_3:57
            .= t0_x *' t0_x + s *' s by A46,ARYTM_3:57;
        then t0_x *' t0_x = t0_2_s_2 + x *' x by ARYTM_3:69;
        then A47:       t0_2_s_2 <=' t0_x *' t0_x by ARYTM_3:def 12;
A48:      t0_x <=' t0 by A37,A43,ARYTM_3:def 12;
          t0_x <> t0 by A37,A43,A44,ARYTM_3:92,def 12;
        then t0_x < t0 by A48,ARYTM_3:75;
        then t0_x in X by A21;
        then ex s st s = t0_x & s *' s < two;
       hence contradiction by A35,A47,ARYTM_3:76;
       end;
       suppose
A49:     one <=' s;
A50:     half <> one by A1,ARYTM_3:59;
          half *' two = one *' one by A1,Lm1,ARYTM_3:59;
        then half <=' one by A3,ARYTM_3:91;
        then A51:     half < one by A50,ARYTM_3:75;
A52:      half <> 0 by A1,ARYTM_3:54;
        then half *' half < half *' one by A51,ARYTM_3:88;
        then A53:       half *' half < half by ARYTM_3:59;
          half < s by A49,A51,ARYTM_3:76;
        then A54:      half < es by A29,ARYTM_3:77;
        set s = half;
       consider t0_2_s_2 being Element of RAT+ such that
A55:     s *' s + t0_2_s_2 = t0 *' t0 or t0 *' t0 + t0_2_s_2 = s *' s
                                        by ARYTM_3:100;
          es <=' t0 *' t0 by A25,A26,ARYTM_3:def 12;
        then s < t0 *' t0 by A54,ARYTM_3:76;
        then A56:      s *' s < t0 *' t0 by A53,ARYTM_3:77;
          s *' s < es by A53,A54,ARYTM_3:77;
        then two + s *' s < two + es by ARYTM_3:83;
        then A57:      two < t0_2_s_2 by A25,A26,A55,A56,ARYTM_3:83,def 12;
          two *' t0 <> 0 by A22,ARYTM_3:86;
        then consider 2t' being Element of RAT+ such that
A58:     (two *' t0) *' 2t' = one by ARYTM_3:61;
        set x = s *' s *' 2t';
        consider t0_x being Element of RAT+ such that
A59:   x + t0_x = t0 or t0 + t0_x = x by ARYTM_3:100;
          x *' (two *' t0) = s *' s *' one by A58,ARYTM_3:58;
        then A60:      x <=' s *' s or two *' t0 <=' one by ARYTM_3:91;
          one <=' two by Lm12,ARYTM_3:def 12;
        then A61: one < two by Lm1,ARYTM_3:75;
A62:     now assume
A63:       t0 <=' one;
          then t0 *' t0 <=' t0 *' one by ARYTM_3:90;
          then t0 *' t0 <=' t0 by ARYTM_3:59;
          then t0 *' t0 <=' one by A63,ARYTM_3:74;
         hence contradiction by A2,A25,ARYTM_3:76;
        end;
        then A64:      one *' one < one *' t0 by ARYTM_3:88;
          one *' t0 < two *' t0 by A22,A61,ARYTM_3:88;
        then A65:      one *' one < two *' t0 by A64,ARYTM_3:77;
          s < t0 by A51,A62,ARYTM_3:77;
        then s *' s < t0 by A53,ARYTM_3:77;
        then A66:      x < t0 by A60,A65,ARYTM_3:59,76;
A67:     now assume
A68:  x = 0;
         per cases by A68,ARYTM_3:86;
         suppose s *' s = 0;
         hence contradiction by A52,ARYTM_3:86;
         end;
         suppose 2t' = 0;
         hence contradiction by A58,ARYTM_3:54;
        end;
        end;
A69:      x *' t0_x + x *' t0 + x *' x
             = x *' t0_x + x *' x + x *' t0 by ARYTM_3:57
            .= x *' t0 + x *' t0 by A59,A66,ARYTM_3:63,def 12
            .= x *' t0 *' one + x *' t0 by ARYTM_3:59
            .= x *' t0 *' one + x *' t0 *' one by ARYTM_3:59
            .= t0 *' x *' two by Lm12,ARYTM_3:63
            .= x *' (t0 *' two) by ARYTM_3:58
            .= s *' s *' one by A58,ARYTM_3:58
            .= s *' s by ARYTM_3:59;
          t0_2_s_2 + x *' x + s *' s = t0 *' t0 + x *' x
                     by A55,A56,ARYTM_3:57,def 12
            .= t0_x *' (t0_x + x) + x *' t0 + x *' x
                         by A59,A66,ARYTM_3:63,def 12
            .= t0_x *' t0_x + x *' t0_x + x *' t0 + x *' x by ARYTM_3:63
            .= t0_x *' t0_x + x *' t0_x + (x *' t0 + x *' x) by ARYTM_3:57
            .= t0_x *' t0_x + (x *' t0_x + (x *' t0 + x *' x)) by ARYTM_3:57
            .= t0_x *' t0_x + s *' s by A69,ARYTM_3:57;
        then t0_x *' t0_x = t0_2_s_2 + x *' x by ARYTM_3:69;
        then A70:       t0_2_s_2 <=' t0_x *' t0_x by ARYTM_3:def 12;
A71:      t0_x <=' t0 by A59,A66,ARYTM_3:def 12;
          t0_x <> t0 by A59,A66,A67,ARYTM_3:92,def 12;
        then t0_x < t0 by A71,ARYTM_3:75;
        then t0_x in X by A21;
        then ex s st s = t0_x & s *' s < two;
       hence contradiction by A57,A70,ARYTM_3:76;
      end;
      end;
      hence contradiction;
     end;
     end;
       d <> 0 by ARYTM_3:41;
     then A72:    d *^ d <> {} by ORDINAL3:34;
       n/d = t0 by ARYTM_3:45;
     then two = (n *^ n)/(d *^ d) by A23,ARYTM_3:55;
     then two/1 = (n *^ n)/(d *^ d) by Lm1,ARYTM_3:46;
     then A73:    two*^(d *^ d) = 1*^(n *^ n) by A72,ARYTM_3:51
                    .= n *^ n by Lm1,ORDINAL2:56;
     then consider k being natural Ordinal such that
A74:    n = 2 *^ k by Lm11;
       two*^(d *^ d) = 2 *^ (k *^ (2 *^ k)) by A73,A74,ORDINAL3:58;
     then d *^ d = k *^ (2 *^ k)  by ORDINAL3:36
                .= 2 *^ (k *^ k)by ORDINAL3:58;
     then consider p being natural Ordinal such that
A75:    d = 2 *^ p by Lm11;
       n, d are_relative_prime by ARYTM_3:40;
    hence contradiction by A74,A75,Lm1,ARYTM_3:def 1;
   end;
A76: not X in {[0,0]} by A16,TARSKI:def 1;
  now assume two in X;
     then ex s st two = s & s *' s < two;
    hence contradiction by A17,A18,A19,Lm8;
   end;
   then X <> RAT+;
   then A77:  not X in {RAT+} by TARSKI:def 1;
     now let r;
    assume
A78:  r in X;
     then consider s such that
A79:   r = s and
A80:   s *' s < two;
    thus for t st t <=' r holds t in X by A4,A78;
    per cases;
    suppose
A81:   r = 0;
    take 1';
    thus 1' in X by A8,A10;
    thus r < 1' by A12,A81,ARYTM_3:75;
    end;
    suppose
A82:   r <> 0;
     consider rr being Element of RAT+ such that
A83:   r *' r + rr = two or two + rr = r *' r by ARYTM_3:100;
       r + r + r <> 0 by A82,ARYTM_3:70;
     then consider 3r' being Element of RAT+ such that
A84:   (r + r + r) *' 3r' = one by ARYTM_3:60;
     set eps = rr *' 3r';
A85: now assume
A86:  eps = 0;
      per cases by A86,ARYTM_3:86;
      suppose rr = 0';
       then r *' r = two by A83,ARYTM_3:56;
      hence contradiction by A79,A80;
      end;
      suppose 3r' = 0';
      hence contradiction by A84,ARYTM_3:54;
     end;
     end;
      now per cases;
    suppose that
A87:  eps < r;
     take t = r + eps;
A88:    r *' eps + eps *' r + r *' eps
          = eps *' (r + r) + r *' eps by ARYTM_3:63
         .= eps *' (r + r + r) by ARYTM_3:63
         .= rr *' one by A84,ARYTM_3:58
         .= rr by ARYTM_3:59;
       eps *' eps < r *' eps by A85,A87,ARYTM_3:88;
     then A89:    r *' eps + eps *' r + eps *' eps < r *' eps + eps *' r + r *'
eps
           by ARYTM_3:83;
       t *' t = r *' t + eps *' t by ARYTM_3:63
       .= r *' r + r *' eps + eps *' t by ARYTM_3:63
       .= r *' r + r *' eps + (eps *' r + eps *' eps) by ARYTM_3:63
       .= r *' r + (r *' eps + (eps *' r + eps *' eps)) by ARYTM_3:57
       .= r *' r + (r *' eps + eps *' r + eps *' eps) by ARYTM_3:57;
     then t *' t < two by A79,A80,A83,A88,A89,ARYTM_3:83,def 12;
    hence t in X;
       0' <=' eps by ARYTM_3:71;
     then 0' < eps by A85,ARYTM_3:75;
     then r + 0' < r + eps by ARYTM_3:83;
    hence r < t by ARYTM_3:56;
    end;
    suppose
A90:  r <=' eps;
    take t = (1' + half) *' r;
       eps *' (r + r + r) = rr *' one by A84,ARYTM_3:58
         .= rr by ARYTM_3:59;
     then A91:    r *' (r + r + r) <=' rr by A90,ARYTM_3:90;
        r *' (r + r + r) + r *' r = r *' (r + r) + r *' r + r *' r by
ARYTM_3:63
          .= r *' (r + r) + (r *' r + r *' r) by ARYTM_3:57
          .= r *' (two *' r) + (r *' r + r *' r) by Lm13
          .= r *' (two *' r) + two *' (r *' r) by Lm13
          .= two *' (r *' r) + two *' (r *' r) by ARYTM_3:58
          .= two *' (two *' (r *' r)) by Lm13
          .= two *' (two *' r *' r) by ARYTM_3:58
          .= (two *' r) *' (two *' r) by ARYTM_3:58;
     then A92:   two *' r *' (two *' r) <=' two by A79,A80,A83,A91,ARYTM_3:83
,def 12;
A93:   two *' r <> 0 by A82,ARYTM_3:86;
       1' + half <> 0 by ARYTM_3:70;
     then A94:   t <> 0 by A82,ARYTM_3:86;
       1' < two *' one by A10,ARYTM_3:59;
     then half < one by A1,ARYTM_3:90;
     then one + half < two by Lm12,ARYTM_3:83;
     then A95:    t < two *' r by A82,Lm1,ARYTM_3:88;
     then A96:   t *' t < two *' r *' t by A94,ARYTM_3:88;
       two *' r *' t < two *' r *' (two *' r) by A93,A95,ARYTM_3:88;
     then t *' t < two *' r *' (two *' r) by A96,ARYTM_3:77;
     then t *' t < two by A92,ARYTM_3:76;
    hence t in X;
A97:   0' <> half by A1,ARYTM_3:54;
       0' <=' half by ARYTM_3:71;
     then 0' < half by A97,ARYTM_3:75;
     then one + 0' < one + half by ARYTM_3:83;
     then one < one + half by ARYTM_3:56;
     then one *' r < t by A82,Lm1,ARYTM_3:88;
    hence r < t by ARYTM_3:59;
   end;
   end;
    hence ex t st t in X & r < t;
   end;
   end;
   then X in DD;
   then X in DEDEKIND_CUTS by A77,ARYTM_2:def 1,XBOOLE_0:def 4;
   then X in RAT+ \/ DEDEKIND_CUTS by XBOOLE_0:def 2;
   then X in REAL+ by A20,ARYTM_2:def 2,XBOOLE_0:def 4;
   then X in REAL+ \/ [:{0},REAL+:] by XBOOLE_0:def 2;
   then A98: X in REAL by A76,Def1,XBOOLE_0:def 4;
     now assume X in RAT;
     then A99:   X in RAT+ \/ [:{0},RAT+:] by Def3,XBOOLE_0:def 4;
    per cases by A99,XBOOLE_0:def 2;
    suppose
A100:    X in RAT+;
       now per cases by A100,ARYTM_3:def 6,XBOOLE_0:def 2;
      suppose X in
       {[i,j]: i,j are_relative_prime & j <> {}}
          \ {[k,one]: not contradiction};
       then X in {[i,j]: i,j are_relative_prime & j <> {}} by XBOOLE_0:def 4;
       then consider i,j such that
A101:     X = [i,j] and i,j are_relative_prime & j <> {};
         X = {{i,j}, {i}} by A101,TARSKI:def 5;
      hence contradiction by A13,TARSKI:def 2;
      end;
      suppose X in omega;
       then A102:     X is ordinal number by ORDINAL1:23;
         2 c= X by A11,A13,Lm10,ZFMISC_1:38;
       then A103:      not X in 2 by ORDINAL1:7;
         now per cases by A102,A103,ORDINAL1:24;
        suppose X = two;
         then half = 0 or half = 1 by A15,Lm10,TARSKI:def 2;
        hence contradiction by A1,Lm1,ARYTM_3:54,59;
        end;
        suppose two in X;
         then ex s st s = two & s *' s < two;
        hence contradiction by A17,A18,A19,Lm8;
       end;
       end;
      hence contradiction;
     end;
     end;
    hence contradiction;
    end;
    suppose X in [:{0},RAT+:];
     then consider x,y being set such that
A104:   X = [x,y] by ZFMISC_1:102;
       X = {{x,y}, {x}} by A104,TARSKI:def 5;
    hence contradiction by A13,TARSKI:def 2;
   end;
   end;
  hence thesis by A98,Lm7,XBOOLE_0:def 8;
 end;

theorem Th3:
 RAT c< COMPLEX by Th1,Th2,XBOOLE_1:56;

Lm14: INT c= RAT
 proof
     [:{0},NAT:] c= [:{0},RAT+:] by ARYTM_3:34,ZFMISC_1:118;
   then NAT \/ [:{0},NAT:] c= RAT+ \/ [:{0},RAT+:]
               by ARYTM_3:34,XBOOLE_1:13;
  hence thesis by Def3,Def4,XBOOLE_1:33;
 end;

theorem Th4:
 INT c< RAT
  proof
  1,2 are_relative_prime
     proof let c,d1,d2 be Ordinal such that
A1:    1 = c *^ d1 and
         2 = c *^ d2;
      thus c = one by A1,Lm1,ORDINAL3:41;
     end;
then A2:  [1,2] in RAT+ by Lm1,ARYTM_3:39;
A3:  not [1,2] in NAT by ARYTM_3:38;
      not 1 in {0} by TARSKI:def 1;
    then not [1,2] in [:{0},NAT:] by ZFMISC_1:106;
    then not [1,2] in NAT \/ [:{0},NAT:] by A3,XBOOLE_0:def 2;
    then INT <> RAT by A2,Def4,Lm3,XBOOLE_0:def 4;
   hence thesis by Lm14,XBOOLE_0:def 8;
  end;

theorem Th5:
 INT c< REAL by Th2,Th4,XBOOLE_1:56;

theorem Th6:
 INT c< COMPLEX by Th1,Th5,XBOOLE_1:56;

theorem Th7:
 NAT c< INT
 proof
     0 in {0} by TARSKI:def 1;
   then [0,1] in [:{0},NAT:] by ZFMISC_1:106;
   then A1:  [0,1] in NAT \/ [:{0},NAT:] by XBOOLE_0:def 2;
     [0,1] <> [0,0] by ZFMISC_1:33;
   then not [0,1] in {[0,0]} by TARSKI:def 1;
   then A2:  [0,1] in INT by A1,Def4,XBOOLE_0:def 4;
     not [0,1] in NAT by ARYTM_3:38;
  hence thesis by A2,Lm4,XBOOLE_0:def 8;
 end;

theorem Th8:
 NAT c< RAT by Th4,Th7,XBOOLE_1:56;

theorem Th9:
 NAT c< REAL by Th2,Th8,XBOOLE_1:56;

theorem Th10:
 NAT c< COMPLEX by Th1,Th9,XBOOLE_1:56;

begin :: to be canceled

theorem
   REAL c= COMPLEX by Th1,XBOOLE_0:def 8;

theorem
   RAT c= REAL by Th2,XBOOLE_0:def 8;

theorem
   RAT c= COMPLEX by Th3,XBOOLE_0:def 8;

theorem
   INT c= RAT by Th4,XBOOLE_0:def 8;

theorem
   INT c= REAL by Th5,XBOOLE_0:def 8;

theorem
   INT c= COMPLEX by Th6,XBOOLE_0:def 8;

theorem
   NAT c= INT by Th7,XBOOLE_0:def 8;

theorem
   NAT c= RAT by Th8,XBOOLE_0:def 8;

theorem
   NAT c= REAL;

theorem
   NAT c= COMPLEX by Th10,XBOOLE_0:def 8;

theorem
   REAL <> COMPLEX by Th1;

theorem
   RAT <> REAL by Th2;

theorem
   RAT <> COMPLEX by Th1,Th2;

theorem
   INT <> RAT by Th4;

theorem
   INT <> REAL by Th2,Th4;

theorem
   INT <> COMPLEX by Th1,Th5;

theorem
   NAT <> INT by Th7;

theorem
   NAT <> RAT by Th4,Th7;

theorem
   NAT <> REAL by Th2,Th8;

theorem
   NAT <> COMPLEX by Th1,Th9;
