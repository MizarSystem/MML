:: Homeomorphism between Finite Topological Spaces, Two-Dimensional Lattice
:: Spaces and a Fixed Point Theorem
::  by Masami Tanaka , Hiroshi Imura and Yatsuka Nakamura
::
:: Received July 6, 2005
:: Copyright (c) 2005 Association of Mizar Users

environ

 vocabularies FINSEQ_1, BOOLE, FUNCT_1, RELAT_1, ARYTM_1, RELAT_2, FIN_TOPO,
      SQUARE_1, FINTOPO4, FINTOPO5, INT_1, ARYTM_3, ABSVALUE, TOPS_2, SGRAPH1,
      XREAL_0, ORDERS_1, EQREL_1, COMPLEX1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0,
      NAT_1, RELAT_1, FUNCT_1, RELSET_1, EQREL_1, FUNCT_2, FINSEQ_1, STRUCT_0,
      FIN_TOPO, FINTOPO3, BINARITH, ENUMSET1, ORDERS_2, FINTOPO4, COMPLEX1,
      INT_1;
 constructors ENUMSET1, REAL_1, NAT_1, INT_1, COMPLEX1, EQREL_1, LIMFUNC1,
      BINARITH, FIN_TOPO, FINTOPO3, FINTOPO4;
 registrations XBOOLE_0, SUBSET_1, RELSET_1, XXREAL_0, XREAL_0, REAL_1, NAT_1,
      INT_1, STRUCT_0, FIN_TOPO, ORDINAL1;
 requirements REAL, NUMERALS, SUBSET, BOOLE, ARITHM;
 definitions TARSKI, FINSEQ_1, FIN_TOPO, RELAT_1;
 theorems NAT_1, NAT_2, ZFMISC_1, REAL_1, INT_1, FUNCT_1, FUNCT_2, FINSEQ_1,
      ABSVALUE, RELSET_1, XBOOLE_0, XBOOLE_1, UNIFORM1, FIN_TOPO, FINTOPO3,
      BINARITH, ENUMSET1, STRUCT_0, FINTOPO4, TARSKI, JGRAPH_6, XREAL_1,
      COMPLEX1, XXREAL_0;
 schemes NAT_1, FUNCT_2;

begin

theorem Th1:
  for X being set, Y being non empty set, f being Function of X,Y,
      A being Subset of X st f is one-to-one holds
   f".:(f.:A)=A
  proof let X be set, Y be non empty set, f be Function of X,Y,
        A be Subset of X;
    assume A1: f is one-to-one;
     dom f = X by FUNCT_2:def 1;
    hence thesis by A1,JGRAPH_6:22;
  end;

theorem
   for n being Element of NAT holds n>0 iff Seg n<>{}
  proof
    let n be Element of NAT;
    hereby assume n>0;
      then n>=0+1 by NAT_1:13;
      hence Seg n <>{} by FINSEQ_1:3;
    end;
    assume A1: Seg n<>{};
    assume n<=0;
    then n=0;
    hence contradiction by A1,FINSEQ_1:4;
  end;

definition let FT1,FT2 be RelStr, h be Function of FT1, FT2;
  pred h is_homeomorphism means
    :Def1: h is one-to-one onto &
    for x being Element of FT1 holds
      h.:U_FT x = Im(the InternalRel of FT2,h.x);
end;

theorem Th3:
  for FT1,FT2 being non empty RelStr,
      h being Function of FT1, FT2 st h is_homeomorphism
  ex g being Function of FT2, FT1 st g=h" & g is_homeomorphism
  proof let FT1,FT2 be non empty RelStr,
    h be Function of FT1, FT2;
    assume
A1:   h is_homeomorphism;
then A2: h is one-to-one onto &
    for x being Element of FT1 holds
      h.:U_FT(x)=Im(the InternalRel of FT2,h.x) by Def1;
      then A3: rng h = the carrier of FT2 by FUNCT_2:def 3;
    then reconsider g2=h" as Function of FT2, FT1 by A2,FUNCT_2:31;
A4: g2 is one-to-one by A2,FUNCT_1:62;
     rng g2=dom h by A2,FUNCT_1:55
         .=the carrier of FT1 by FUNCT_2:def 1;
    then A5: g2 is onto by FUNCT_2:def 3;
     for y being Element of FT2 holds
      g2.:U_FT(y)=Im(the InternalRel of FT1,g2.y)
    proof let y be Element of FT2;
      reconsider x = g2.y as Element of FT1;
A6:    y=h.x by A2,A3,FUNCT_1:57;
       h.:U_FT x= Im(the InternalRel of FT2,h.x) by A1,Def1;
      hence g2.:U_FT(y)=Im(the InternalRel of FT1,g2.y)
       by A2,A6,Th1;
    end;
    then g2 is_homeomorphism by A4,A5,Def1;
    hence thesis;
  end;

theorem Th4:
  for FT1,FT2 being non empty RelStr,
      h being Function of FT1, FT2,
      n being Element of NAT, x being Element of FT1, y being Element of FT2
    st h is_homeomorphism & y=h.x holds
    (for z being Element of FT1 holds z in U_FT(x,n) iff h.z in U_FT(y,n))
  proof let FT1,FT2 be non empty RelStr,
    h be Function of FT1, FT2,
    n be Element of NAT,x be Element of FT1,y be Element of FT2;
    assume A1: h is_homeomorphism & y=h.x;
     x in the carrier of FT1;
then A2: x in dom h by FUNCT_2:def 1;
A3: h is one-to-one onto & for x2 being Element of FT1 holds
      h.:U_FT(x2)=Im(the InternalRel of FT2,h.x2)
      by A1,Def1;
    let z be Element of FT1;
     z in the carrier of FT1;
      then A4: z in dom h by FUNCT_2:def 1;
      A5: now assume A6: z in U_FT(x,n);
        defpred P[Element of NAT] means
        for w being Element of FT1
          holds w in U_FT(x,$1) implies h.w in U_FT(y,$1);
         for w being Element of FT1
          holds w in U_FT(x,0) implies h.w in U_FT(y,0)
        proof let w be Element of FT1;
A7:       h.:U_FT(x)=Class(the InternalRel of FT2,h.x) by A1,Def1;
           w in the carrier of FT1;
          then A8: w in dom h by FUNCT_2:def 1;
          hereby assume w in U_FT(x,0);
            then w in U_FT x by FINTOPO3:47;
            then h.w in U_FT y by A1,A7,A8,FUNCT_1:def 12;
           hence h.w in U_FT(y,0) by FINTOPO3:47;
          end;
        end;
    then A9: P[0];
   A10: for k being Element of NAT st P[k] holds P[k+1]
        proof let k be Element of NAT;
          assume A11:P[k];
           for w being Element of FT1
            holds w in U_FT(x,k+1) implies h.w in U_FT(y,k+1)
          proof
            let w be Element of FT1;
            assume A12: w in U_FT(x,k+1);
             U_FT(x,k+1)=(U_FT(x,k))^f by FINTOPO3:48;
            then consider x3 being Element of FT1 such that
       A13: x3=w & ex y3 being Element of FT1 st
            y3 in U_FT(x,k) & x3 in U_FT y3 by A12;
            consider y2 being Element of FT1 such that
       A14: y2 in U_FT(x,k) & x3 in U_FT y2 by A13;
            reconsider q=h.y2, p=h.x3 as Element of FT2;
       A15: for w2 being Element of FT1
              holds w2 in U_FT(y2,0) implies h.w2 in U_FT(q,0)
            proof let w2 be Element of FT1;
A16:   h.:U_FT(y2)=Class(the InternalRel of FT2,h.y2)
          by A1,Def1;
               w2 in the carrier of FT1;
         then A17: w2 in dom h by FUNCT_2:def 1;
              hereby assume w2 in U_FT(y2,0);
                then w2 in U_FT y2 by FINTOPO3:47;
                then h.w2 in U_FT q by A16,A17,FUNCT_1:def 12;
               hence h.w2 in U_FT(q,0) by FINTOPO3:47;
              end;
            end;
       A18: q in U_FT(y,k) by A11,A14;
             x3 in U_FT(y2,0) by A14,FINTOPO3:47;
            then p in U_FT(q,0) by A15;
            then p in U_FT q by FINTOPO3:47;
            then p in (U_FT(y,k))^f by A18;
            hence h.w in U_FT(y,k+1) by A13,FINTOPO3:48;
          end;
          hence P[k+1];
        end;
         for k being Element of NAT holds P[k] from NAT_1:sch 1(A9,A10);
        hence h.z in U_FT(y,n) by A6;
      end;
       now assume A19: h.z in U_FT(y,n);
        defpred P[Element of NAT] means
        for w being Element of FT2
          holds w in U_FT(y,$1) implies h".w in U_FT(x,$1);
        consider g being Function of FT2, FT1 such that
A20:    g=h" & g is_homeomorphism by A1,Th3;
A21:    g is_homeomorphism & g.y=x by A1,A2,A3,A20,FUNCT_1:56;
         for w being Element of FT2 holds
          w in U_FT(y,0) implies h".w in U_FT(x,0)
        proof let w be Element of FT2;
A22:      g.:U_FT(y)=Class(the InternalRel of FT1,g.y) by A20,Def1;
           w in the carrier of FT2;
     then A23: w in dom g by FUNCT_2:def 1;
          hereby assume w in U_FT(y,0);
            then w in U_FT y by FINTOPO3:47;
            then g.w in U_FT x by A21,A22,A23,FUNCT_1:def 12;
            hence h".w in U_FT(x,0) by A20,FINTOPO3:47;
          end;
        end;
   then A24: P[0];
   A25: for k being Element of NAT st P[k] holds P[k+1]
        proof let k be Element of NAT;
          assume A26: P[k];
           for w being Element of FT2
            holds w in U_FT(y,k+1) implies h".w in U_FT(x,k+1)
          proof
            let w be Element of FT2;
            assume A27: w in U_FT(y,k+1);
             U_FT(y,k+1)=(U_FT(y,k))^f by FINTOPO3:48;
            then consider x3 being Element of FT2 such that
       A28: x3=w & ex y3 being Element of FT2 st
            y3 in U_FT(y,k) & x3 in U_FT y3 by A27;
            consider y2 being Element of FT2 such that
       A29: y2 in U_FT(y,k) & x3 in U_FT y2 by A28;
            reconsider q=g.y2, p=g.x3 as Element of FT1;
       A30: for w2 being Element of FT2
              holds w2 in U_FT(y2,0) implies h".w2 in U_FT(q,0)
            proof let w2 be Element of FT2;
A31: h".:U_FT(y2)=Class(the InternalRel of FT1,h".y2)
                 by A20,Def1;
               w2 in the carrier of FT2;
         then A32: w2 in dom g by FUNCT_2:def 1;
              hereby assume w2 in U_FT(y2,0);
                then w2 in U_FT y2 by FINTOPO3:47;
                then h".w2 in U_FT q by A20,A31,A32,FUNCT_1:def 12;
                hence h".w2 in U_FT(q,0) by FINTOPO3:47;
              end;
            end;
       A33: q in U_FT(x,k) by A20,A26,A29;
             x3 in U_FT(y2,0) by A29,FINTOPO3:47;
            then p in U_FT(q,0) by A20,A30;
            then p in U_FT q by FINTOPO3:47;
            then p in (U_FT(x,k))^f by A33;
            hence h".w in U_FT(x,k+1) by A20,A28,FINTOPO3:48;
          end;
          hence P[k+1];
        end;
         for k being Element of NAT holds P[k] from NAT_1:sch 1(A24,A25);
        then h".(h.z) in U_FT(x,n) by A19;
        hence z in U_FT(x,n) by A3,A4,FUNCT_1:56;
      end;
      hence thesis by A5;
  end;

theorem
   for FT1,FT2 being non empty RelStr,
      h being Function of FT1, FT2,
      n being Element of NAT,x being Element of FT1,y being Element of FT2
    st h is_homeomorphism & y=h.x holds
    (for v being Element of FT2 holds
    h".v in U_FT(x,n) iff v in U_FT(y,n))
  proof let FT1,FT2 be non empty RelStr,
            h be Function of FT1, FT2,
            n be Element of NAT,x be Element of FT1,y be Element of FT2;
    assume A1: h is_homeomorphism & y=h.x;
    then consider g being Function of FT2, FT1 such that
A2: g=h" & g is_homeomorphism by Th3;
     x in the carrier of FT1;
    then A3: x in dom h by FUNCT_2:def 1;
     h is one-to-one onto & for x2 being Element of FT1 holds
    h.:U_FT(x2)=Im(the InternalRel of FT2,h.x2) by A1,Def1;
      then x=g.y by A1,A2,A3,FUNCT_1:56;
    hence thesis by A2,Th4;
  end;

theorem
   for n being non zero Element of NAT,
      f being Function of FTSL1 n, FTSL1 n st f is_continuous 0 holds
   ex p being Element of FTSL1 n st f.p in U_FT(p,0)
    proof let n be non zero Element of NAT,
    f be Function of FTSL1 n, FTSL1 n;
    assume A1: f is_continuous 0;
A2: n>=0+1 by NAT_1:13;
    assume A3: for p being Element of FTSL1 n holds not f.p in U_FT(p,0);
A4: RelStr(# Seg n,Nbdl1 n #)=FTSL1 n by FINTOPO4:def 4;
A5: FTSL1 n is filled by FINTOPO4:18;
    defpred P2[Element of NAT] means $1>0 &
      for j being Element of NAT st $1<=n & j=f.$1 holds $1>j;
     now given j being Element of NAT such that
  A6: j=f.n & n<=j;
  A7: n in the carrier of FTSL1 n by A2,A4;
      then f.n in the carrier of FTSL1 n by FUNCT_2:7;
      then 1<=j & j<=n by A4,A6,FINSEQ_1:3;
  then A8: n=j by A6,XXREAL_0:1;
      reconsider p2=n as Element of FTSL1 n by A7;
       p2 in U_FT p2 by A5,FIN_TOPO:def 4;
      then p2 in U_FT(p2,0) by FINTOPO3:47;
      hence contradiction by A3,A6,A8;
    end;
then A9: for j being Element of NAT st n<=n & j=f.n holds n>j;
then A10: ex k being Element of NAT st P2[k];
     ex k being Element of NAT st P2[k] &
    for m being Element of NAT st P2[m] holds k <= m
      from NAT_1:sch 5(A10);
    then consider k being Element of NAT such that
A11: P2[k] & for m being Element of NAT st P2[m] holds k <= m;
A12: k<=n by A9,A11;
A13: 0+1<=k by A11,NAT_1:13;
then A14: k-1>=0 by XREAL_1:50;
then A15: k-1=k-'1 by BINARITH:def 3;
     k<k+1 by NAT_1:13;
then A16:k-1 < k+1-1 by XREAL_1:11;
    reconsider pk=k as Element of FTSL1 n by A4,A12,A13,FINSEQ_1:3;
     now per cases by A11,A15,A16;
      case k-'1<=0;
then A17:       k-1=0 by A14,BINARITH:def 3;
         1 in the carrier of FTSL1 n by A2,A4;
        then A18: f.1 in Seg n by A4,FUNCT_2:7;
        then reconsider j0=f.1 as Element of NAT;
         1>j0 by A2,A11,A17;
        hence contradiction by A18,FINSEQ_1:3;
      end;
      case A19: k-'1>0 &
        ex j being Element of NAT st k-'1<=n & j=f.(k-'1) & k-'1<=j;
        then consider j being Element of NAT such that
   A20: k-'1<=n & j=f.(k-'1) & k-'1<=j;
   A21: k-'1>=0+1 by A19,NAT_1:13;
   then A22: k-'1 in the carrier of FTSL1 n by A4,A20;
        then j in the carrier of FTSL1 n by A20,FUNCT_2:7;
   then A23: 1<=j & j<=n by A4,FINSEQ_1:3;
         now assume A24: k-'1=j;
          then reconsider pj=j as Element of FTSL1 n by A22;
           pj in U_FT pj by A5,FIN_TOPO:def 4;
          then f.j in U_FT(pj,0) by A20,A24,FINTOPO3:47;
          hence contradiction by A3;
        end;
        then k-'1<j by A20,REAL_1:def 5;
then A25:    k-'1+1<=j by NAT_1:13;
        reconsider pkm=k-'1 as Element of FTSL1 n by A4,A20,A21,FINSEQ_1:3;
         k-'1 in Seg n by A20,A21;
        then A26: Im(Nbdl1 n,pkm)={k-'1,max(k-'1-'1,1),min(k-'1+1,n)}
                            by FINTOPO4:def 3;
         now assume A27: k=j;
          then min(k-'1+1,n)=k-'1+1 by A15,A23,XXREAL_0:def 8;
          then k in U_FT pkm by A4,A15,A26,ENUMSET1:def 1;
          then f.(k-'1) in U_FT(pkm,0) by A20,A27,FINTOPO3:47;
          hence contradiction by A3;
        end;
        then A28: k<j by A15,A25,REAL_1:def 5;
        A29: k in the carrier of FTSL1 n by A4,A12,A13;
        then A30: f.k in Seg n by A4,FUNCT_2:7;
        then reconsider jn=f.k as Element of NAT;
        A31: Im(Nbdl1 n,jn)={jn,max(jn-'1,1),min(jn+1,n)}
                            by A30,FINTOPO4:def 3;
        A32: Im(Nbdl1 n,k)={k,max(k-'1,1),min(k+1,n)}
                            by A4,A29,FINTOPO4:def 3;
        A33: 1<=jn & jn<=n by A30,FINSEQ_1:3;
        A34: jn <k by A11,A12;
        then A35: jn+1<=k by NAT_1:13;
        reconsider pfk=jn as Element of FTSL1 n by A29,FUNCT_2:7;
        A36: f.:( U_FT(pk,0)) c= U_FT(pfk,0) by A1,FINTOPO4:def 2;
        A37: k-'1 in dom f by A22,FUNCT_2:def 1;
         (k-'1=k or k-'1=max(k-'1,1) or k-'1=min(k+1,n))
          by A21,XXREAL_0:def 9;
        then k-'1 in U_FT pk by A4,A32,ENUMSET1:def 1;
        then k-'1 in U_FT(pk,0) by FINTOPO3:47;
        then f.(k-'1) in f.:(U_FT(pk,0)) by A37,FUNCT_1:def 12;
   then A38: j in U_FT(pfk,0) by A20,A36;
   A39: jn<j by A15,A25,A34,XXREAL_0:2;
         jn-1>=0 by A33,XREAL_1:50;
        then A40: jn-1=jn-'1 by BINARITH:def 3;
         jn<jn+1 by NAT_1:13;
then A41:    jn-1<jn+1-1 by XREAL_1:11;
   A42: now per cases;
          suppose jn-'1>=1;
            hence j<> max(jn-'1,1) by A39,A40,A41,XXREAL_0:def 9;
          end;
          suppose jn-'1<1;
            hence j<> max(jn-'1,1) by A33,A39,XXREAL_0:def 9;
          end;
        end;
         now per cases;
          case jn+1<=n;
            hence j<> min(jn+1,n) by A28,A35,XXREAL_0:def 8;
          end;
          case A43: jn+1>n;
            then jn>=n by NAT_1:13;
            hence j<> min(jn+1,n) by A39,A43,XXREAL_0:def 8;
          end;
        end;
        then not j in U_FT pfk by A4,A15,A25,A31,A34,A42,ENUMSET1:def 1;
        hence contradiction by A38,FINTOPO3:47;
      end;
    end;
    hence thesis;
  end;

theorem Th7:
  for T being non empty RelStr,
      p being Element of T, k being Element of NAT st T is filled holds
    U_FT(p,k) c= U_FT(p,k+1)
  proof let T be non empty RelStr, p be Element of T,
  k be Element of NAT;
    assume A1: T is filled;
     U_FT(p,k+1) = (U_FT(p,k))^f by FINTOPO3:48;
    hence U_FT(p,k) c= U_FT(p,k+1) by A1,FINTOPO3:1;
  end;

theorem Th8:
  for T being non empty RelStr,
      p being Element of T, k being Element of NAT st T is filled holds
    U_FT(p,0) c= U_FT(p,k)
  proof let T be non empty RelStr, p be Element of T,
  k be Element of NAT;
    assume A1: T is filled;
    defpred P[Element of NAT] means U_FT(p,0) c= U_FT(p,$1);
A2: P[0];
A3: for k being Element of NAT st P[k] holds P[k+1]
    proof let k be Element of NAT;
      assume A4:P[k];
       U_FT(p,k) c= U_FT(p,k+1) by A1,Th7;
      hence P[k+1] by A4,XBOOLE_1:1;
    end;
     for i being Element of NAT holds P[i] from NAT_1:sch 1(A2,A3);
    hence U_FT(p,0) c= U_FT(p,k);
  end;

theorem Th9:
  for n being non zero Element of NAT, jn,j,k being Element of NAT,
      p being Element of FTSL1 n st p=jn holds
    j in U_FT(p,k) iff j in Seg n & abs(jn-j)<= k+1
  proof let n be non zero Element of NAT,
  jn,j,k be Element of NAT,p be Element of FTSL1 n;
    assume A1: p=jn;
A2: FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
A3: now assume A4: j in U_FT(p,k);
      defpred P[Element of NAT] means
        for j2,jn2 being Element of NAT,p2 being Element of FTSL1 n
        st jn2=p2 & j2 in U_FT(p2,$1) holds abs(jn2-j2)<=$1+1;
A5:  P[0]
      proof let j2,jn2 be Element of NAT,p2 being Element of FTSL1 n;
        assume A6: jn2=p2 & j2 in U_FT(p2,0);
    then A7: 1<=jn2 & jn2<=n by A2,FINSEQ_1:3;
A8:     j2 in U_FT p2 by A6,FINTOPO3:47;
A9:    Im(Nbdl1 n,jn2)={jn2,max(jn2-'1,1),min(jn2+1,n)}
                           by A2,A6,FINTOPO4:def 3;
        per cases by A2,A6,A8,A9,ENUMSET1:def 1;
          suppose j2=jn2;
            hence abs(jn2-j2)<=0+1 by ABSVALUE:7;
          end;
          suppose A10: j2=max(jn2-'1,1);
            per cases;
              suppose A11: jn2-'1>=1;
                then j2=jn2-'1 by A10,XXREAL_0:def 9;
                then j2=jn2-1 by A11,BINARITH:57;
                hence abs(jn2-j2)<=0+1 by ABSVALUE:def 1;
              end;
              suppose A12: jn2-'1<1;
                then A13: j2=1 by A10,XXREAL_0:def 9;
                 jn2-'1<0+1 by A12;
                then jn2-'1<=0 by NAT_1:13;
                then jn2-'1=0;
                then jn2<=1 by BINARITH:54;
                then 1-jn2>=0 by XREAL_1:50;
                then A14: abs(j2-jn2)=1-jn2 by A13,ABSVALUE:def 1;
                 1<=1+jn2 by NAT_1:12;
                then 1-jn2<=1+jn2-jn2 by XREAL_1:11;
                hence abs(jn2-j2)<=0+1 by A14,UNIFORM1:13;
              end;
          end;
          suppose A15: j2=min(jn2+1,n);
            per cases;
              suppose jn2+1<=n;
                then j2=jn2+1 by A15,XXREAL_0:def 8;
                then abs(j2-jn2)=1 by ABSVALUE:def 1;
                hence abs(jn2-j2)<=0+1 by UNIFORM1:13;
              end;
              suppose A16: jn2+1>n;
                then A17: j2=n by A15,XXREAL_0:def 8;
                 jn2>=n by A16,NAT_1:13;
                then jn2=n by A7,XXREAL_0:1;
                hence abs(jn2-j2)<=0+1 by A17,ABSVALUE:7;
              end;
          end;
      end;
      A18: for i2 being Element of NAT st P[i2] holds P[i2+1]
      proof let i2 be Element of NAT;
        assume A19:P[i2];
        let j3,jn3 be Element of NAT,p2 being Element of FTSL1 n;
        assume A20: jn3=p2 & j3 in U_FT(p2,i2+1);
         U_FT(p2,i2+1)=(U_FT(p2,i2))^f by FINTOPO3:48
                    .= {x where x is Element of FTSL1 n:
        ex y being Element of FTSL1 n st
          y in U_FT(p2,i2) & x in U_FT y};
        then consider x being Element of FTSL1 n such that
   A21: x=j3 & ex y being Element of FTSL1 n st
        y in U_FT(p2,i2) & x in U_FT y by A20;
        consider y being Element of FTSL1 n such that
   A22: y in U_FT(p2,i2) & x in U_FT y by A21;
         y in Seg n by A2;
        then reconsider iy=y as Element of NAT;
        A23: abs(jn3-iy)<=i2+1 by A19,A20,A22;
         x in U_FT(y,0) by A22,FINTOPO3:47;
        then abs(iy-j3)<=1 by A5,A21;
        then A24: abs(jn3-iy)+abs(iy-j3)<=i2+1+1 by A23,XREAL_1:9;
         abs(jn3-iy+(iy-j3))<=abs(jn3-iy)+abs(iy-j3) by COMPLEX1:142;
        hence abs(jn3-j3)<=i2+1+1 by A24,XXREAL_0:2;
      end;
       for i3 being Element of NAT holds P[i3] from NAT_1:sch 1(A5,A18);
      hence j in Seg n & abs(jn-j)<= k+1 by A1,A2,A4;
    end;
     now assume A25: j in Seg n & abs(jn-j)<= k+1;
      defpred P[Element of NAT] means
        for j2,jn2 being Element of NAT,p2 being Element of FTSL1 n st
       jn2=p2 & j2 in Seg n & abs(jn2-j2)<= $1+1 holds j2 in U_FT(p2,$1);
A26:  P[0]
      proof let j2,jn2 be Element of NAT,p2 be Element of FTSL1 n;
        assume A27: jn2=p2 & j2 in Seg n & abs(jn2-j2)<= 0+1;
        then A28: 1<=j2 & j2<=n by FINSEQ_1:3;
         now per cases;
          case A29: jn2-j2>=0;
            A30: jn2-j2<= 1 by A27,ABSVALUE:def 1;
            A31: jn2-j2=jn2-'j2 by A29,BINARITH:def 3;
            then jn2-'j2>=0+1 or jn2-'j2=0 by A29,NAT_1:13;
then A32:        jn2-j2=1 or jn2-'j2=0 by A30,A31,XXREAL_0:1;
            per cases by A31,A32;
              suppose A33: jn2-1=j2;
                then jn2-1=jn2-'1 by BINARITH:def 3;
                hence j2=jn2 or j2=max(jn2-'1,1) or j2=min(jn2+1,n)
                        by A28,A33,XXREAL_0:def 9;
              end;
              suppose jn2=j2;
                hence j2=jn2 or j2=max(jn2-'1,1) or j2=min(jn2+1,n);
              end;
          end;
          case A34: jn2-j2<0;
            then -(jn2-j2)<= 1 by A27,ABSVALUE:def 1;
then A35:        j2-jn2+jn2<=1+jn2 by XREAL_1:9;
             jn2-j2+j2<0+j2 by A34,XREAL_1:10;
            then jn2+1<=j2 by NAT_1:13;
            then jn2+1=j2 by A35,XXREAL_0:1;
            hence j2=jn2 or j2=max(jn2-'1,1) or j2=min(jn2+1,n)
                            by A28,XXREAL_0:def 8;
          end;
        end;
        then j2 in {jn2,max(jn2-'1,1),min(jn2+1,n)} by ENUMSET1:def 1;
        then j2 in U_FT p2 by A2,A27,FINTOPO4:def 3;
        hence j2 in U_FT(p2,0) by FINTOPO3:47;
      end;
      A36: for jj being Element of NAT st P[jj] holds P[jj+1]
      proof let jj be Element of NAT;
        assume A37:P[jj];
        let j2,jn2 be Element of NAT,p2 be Element of FTSL1 n;
        assume A38: jn2=p2 & j2 in Seg n & abs(jn2-j2)<= jj+1+1;
        then reconsider x0=j2 as Element of FTSL1 n by A2;
        A39: 1<=j2 & j2<=n by A38,FINSEQ_1:3;
        A40: 1<=jn2 & jn2<=n by A2,A38,FINSEQ_1:3;
A41:    now per cases;
          suppose A42:jn2-j2>=0;
            per cases by A42;
              suppose A43: jn2-j2=0;
then A44:            abs(jn2-j2)<=jj+1 by ABSVALUE:def 1;
                 (FTSL1 n) is filled by FINTOPO4:18;
                then p2 in U_FT(p2,jj) & x0 in U_FT p2
                  by A37,A38,A43,A44,FIN_TOPO:def 4;
                hence ex y being Element of FTSL1 n st
                  y in U_FT(p2,jj) & x0 in U_FT y;
              end;
              suppose A45: jn2-j2>0;
                then jn2-j2=jn2-'j2 by BINARITH:def 3;
                then A46: jn2-j2>=0+1 by A45,NAT_1:13;
then A47:            jn2-j2+j2>=1+j2 by XREAL_1:9;
                      j2<j2+1 by NAT_1:13;
                then A48: jn2-(j2+1)<jn2-j2 by REAL_1:92;
                 abs(jn2-j2)=jn2-j2 by A45,ABSVALUE:def 1;
                then A49: jn2-(j2+1)<jj+1+1 by A38,A48,XXREAL_0:2;
                A50: jn2-j2-1>=1-1 by A46,XREAL_1:11;
                then jn2-'(j2+1)=jn2-(j2+1) by BINARITH:def 3;
                then A51: jn2-(j2+1)<=jj+1 by A49,NAT_1:13;
                A52: n>=j2+1 by A40,A47,XXREAL_0:2;
                 1<=j2+1 by A39,NAT_1:13;
                then A53: j2+1 in Seg n by A52;
                then reconsider yj2=j2+1 as Element of FTSL1 n by A2;
A54:            abs(jn2-(j2+1))<=jj+1 by A50,A51,ABSVALUE:def 1;
                 abs(j2+1-j2)=1 by ABSVALUE:def 1;
                then x0 in U_FT(yj2,0) by A26,A38;
                then yj2 in U_FT(p2,jj) & x0 in U_FT yj2
                      by A37,A38,A53,A54,FINTOPO3:47;
                hence ex y being Element of FTSL1 n st
                  y in U_FT(p2,jj) & x0 in U_FT y;
              end;
          end;
          suppose jn2-j2<0;
            then A55: jn2-j2+j2<0+j2 by XREAL_1:8;
            then jn2+1<=j2 by NAT_1:13;
            then A56: jn2+1-1<=j2-1 by XREAL_1:11;
            then A57: j2-1-jn2>=0 by XREAL_1:50;
             j2-1>=0 by A39,XREAL_1:50;
            then A58: j2-1=j2-'1 by BINARITH:def 3;
            then A59: j2-'1>=1 by A40,A56,XXREAL_0:2;
             j2<j2+1 by NAT_1:13;
            then j2-1<j2+1-1 by XREAL_1:11;
            then j2-'1<n by A39,A58,XXREAL_0:2;
            then A60: j2-'1 in Seg n by A59;
            then reconsider pj21=j2-'1 as Element of FTSL1 n by A2;
            A61: abs((j2-'1)-jn2)=(j2-'1)-jn2 by A57,A58,ABSVALUE:def 1;
            A62: j2-jn2>0 by A55,XREAL_1:52;
             abs(jn2-j2)=abs(j2-jn2) by UNIFORM1:13
                      .=1+((j2-'1)-jn2) by A58,A62,ABSVALUE:def 1
                      .=1+abs(jn2-(j2-'1)) by A61,UNIFORM1:13;
then A63:        abs(jn2-(j2-'1))<=jj+1 by A38,XREAL_1:8;
             abs(j2-'1-j2)=abs(j2-(j2-'1)) by UNIFORM1:13
                        .=1 by A58,ABSVALUE:def 1;
            then x0 in U_FT(pj21,0) by A26,A38;
            then pj21 in U_FT(p2,jj) & x0 in U_FT pj21
                   by A37,A38,A60,A63,FINTOPO3:47;
            hence ex y being Element of FTSL1 n st
              y in U_FT(p2,jj) & x0 in U_FT y;
          end;
        end;
         U_FT(p2,jj+1)=(U_FT(p2,jj))^f by FINTOPO3:48
                    .= {x where x is Element of FTSL1 n:
        ex y being Element of FTSL1 n st
          y in U_FT(p2,jj) & x in U_FT y};
        hence j2 in U_FT(p2,jj+1) by A41;
      end;
       for ii being Element of NAT holds P[ii] from NAT_1:sch 1(A26,A36);
      hence j in U_FT(p,k) by A1,A25;
    end;
    hence thesis by A3;
  end;

:: Fixed Point Theorem
theorem
   for kc,km being Element of NAT, n being non zero Element of NAT,
      f being Function of FTSL1 n, FTSL1 n st
    f is_continuous kc & km=[/ (kc/2) \] holds
    ex p being Element of FTSL1 n st f.p in U_FT(p,km)
  proof let kc,km be Element of NAT, n be non zero Element of NAT,
    f be Function of FTSL1 n, FTSL1 n;
    assume that A1:f is_continuous kc and A2: km=[/ (kc/2) \];
A3: n>=0+1 by NAT_1:13;
    assume A4: for p being Element of FTSL1 n holds
      not f.p in U_FT(p,km);
  A5: RelStr(# Seg n,Nbdl1 n #)=FTSL1 n by FINTOPO4:def 4;
  A6: FTSL1 n is filled by FINTOPO4:18;
      defpred P2[Element of NAT] means $1>0 &
        for j being Element of NAT st $1<=n & j=f.$1 holds $1>j;
       now given j being Element of NAT such that
    A7: j=f.n & n<=j;
    A8: n in the carrier of FTSL1 n by A3,A5;
        then f.n in the carrier of FTSL1 n by FUNCT_2:7;
        then 1<=j & j<=n by A5,A7,FINSEQ_1:3;
    then A9: n=j by A7,XXREAL_0:1;
        reconsider p2=n as Element of FTSL1 n by A8;
         p2 in U_FT p2 by A6,FIN_TOPO:def 4;
   then A10: p2 in U_FT(p2,0) by FINTOPO3:47;
         U_FT(p2,0) c= U_FT(p2,km) by A6,Th8;
        hence contradiction by A4,A7,A9,A10;
      end;
      then A11: for j being Element of NAT st n<=n & j=f.n holds n>j;
      then A12: ex k being Element of NAT st P2[k];
       ex k being Element of NAT st P2[k] &
      for m being Element of NAT st P2[m] holds k <= m
                      from NAT_1:sch 5(A12);
      then consider k being Element of NAT such that
 A13: P2[k] & for m being Element of NAT st P2[m] holds k <= m;
      A14: k<=n by A11,A13;
      A15: 0+1<=k by A13,NAT_1:13;
then A16:  k-1>=0 by XREAL_1:50;
      then A17: k-1=k-'1 by BINARITH:def 3;
       k<k+1 by NAT_1:13;
then A18:  k-1 < k+1-1 by XREAL_1:11;
      reconsider pk=k as Element of FTSL1 n by A5,A14,A15,FINSEQ_1:3;
      per cases by A13,A17,A18;
        suppose k-'1<=0;
then A19:      k-1=0 by A16,BINARITH:def 3;
           1 in the carrier of FTSL1 n by A3,A5;
          then A20: f.1 in Seg n by A5,FUNCT_2:7;
          then reconsider j0=f.1 as Element of NAT;
           1>j0 by A3,A13,A19;
          hence contradiction by A20,FINSEQ_1:3;
        end;
        suppose A21: k-'1>0 &
          ex j being Element of NAT st k-'1<=n & j=f.(k-'1) & k-'1<=j;
          then consider j being Element of NAT such that
            A22: k-'1<=n & j=f.(k-'1) & k-'1<=j;
          A23: k-'1>=0+1 by A21,NAT_1:13;
          then reconsider pkm=k-'1 as Element of FTSL1 n by A5,A22,FINSEQ_1:3;
          A24: k-'1 in the carrier of FTSL1 n by A5,A22,A23;
          then A25: j in the carrier of FTSL1 n by A22,FUNCT_2:7;
           now assume A26: k-'1=j;
            then reconsider pj=j as Element of FTSL1 n by A24;
             pj in U_FT pj by A6,FIN_TOPO:def 4;
            then A27: pj in U_FT(pj,0) by FINTOPO3:47;
             U_FT(pj,0) c= U_FT(pj,km) by A6,Th8;
            hence contradiction by A4,A22,A26,A27;
          end;
          then k-'1<j by A22,REAL_1:def 5;
then A28:      k-'1+1<=j by NAT_1:13;
          then j-k>=0 by A17,XREAL_1:50;
          then A29: j-k=j-'k by BINARITH:def 3;
          A30: k in the carrier of FTSL1 n by A5,A14,A15;
               then f.k in Seg n by A5,FUNCT_2:7;
          then reconsider jn=f.k as Element of NAT;
          A31: Im(Nbdl1 n,k)={k,max(k-'1,1),min(k+1,n)}
                        by A5,A30,FINTOPO4:def 3;
          A32: jn <k by A13,A14;
          then A33: k-jn>0 by XREAL_1:52;
          then A34: k-jn=k-'jn by BINARITH:def 3;
           not j in U_FT(pkm,km) by A4,A22;
          then not abs((k-'1)-j)<=km+1 by A5,A25,Th9;
          then abs(j-(k-'1))>km+1 by UNIFORM1:13;
          then j-'k+1>km+1 by A17,A29,ABSVALUE:def 1;
          then A35: j-k+1>=km+1+1 by A29,NAT_1:13;
          A36: jn in Seg n by A5,A30,FUNCT_2:7;
           not jn in U_FT(pk,km) by A4;
          then not abs(k-jn)<=km+1 by A36,Th9;
          then k-jn>km+1 by A33,ABSVALUE:def 1;
          then k-jn>=km+1+1 by A34,NAT_1:13;
          then k-jn+(j-k+1)>= km+1+1+(km+1+1) by A35,XREAL_1:9;
          then j-jn+1>= km+1+1+(km+1)+1;
          then j-jn>= km+1+1+(km+1) by XREAL_1:8;
          then j-jn-1>= km+1+1+km+1-1 by XREAL_1:11;
then A37:      (j-jn-1)/2>= ((2*km)+2)/2 by REAL_1:73;
           [/ (kc/2) \] >= kc/2 by INT_1:def 5;
          then [/ (kc/2) \]+2/2 >= kc/2 +2/2 by XREAL_1:9;
          then (j-jn-1)/2 >= kc/2+2/2 by A2,A37,XXREAL_0:2;
then A38:      (j-jn-1)/2*2>=(kc/2+2/2)*2 by XREAL_1:66;
          reconsider pfk=jn as Element of FTSL1 n by A30,FUNCT_2:7;
          A39: f.:(U_FT(pk,0)) c= U_FT(pfk,kc) by A1,FINTOPO4:def 2;
          A40: k-'1 in dom f by A24,FUNCT_2:def 1;
           k-'1=max(k-'1,1) by A23,XXREAL_0:def 9;
          then k-'1 in U_FT pk by A5,A31,ENUMSET1:def 1;
          then k-'1 in U_FT(pk,0) by FINTOPO3:47;
then A41:      f.(k-'1) in f.:( U_FT(pk,0)) by A40,FUNCT_1:def 12;
           jn<j by A17,A28,A32,XXREAL_0:2;
          then j-jn>=0 by XREAL_1:50;
          then A42: abs(j-jn)=j-jn by ABSVALUE:def 1;
          A43: abs(jn-j)=abs(j-jn) by UNIFORM1:13;
           kc <kc+2 by XREAL_1:31;
          then j-jn-1> kc by A38,XXREAL_0:2;
          then j-jn-1+1>kc+1 by XREAL_1:8;
          hence contradiction by A22,A39,A41,A42,A43,Th9;
        end;
  end;

definition let A,B be set;
 let R be Relation of A,B, x be set;
 redefine func Im(R,x) -> Subset of B;
 coherence
  proof
     Im(R,x) = R.:{x};
   hence thesis;
  end;
end;

:: 2-dimensional linear FT_Str
definition let n,m be Element of NAT;
  func Nbdl2(n,m) -> Relation of [:Seg n, Seg m:] means
  :Def2: for x being set st x in [:Seg n, Seg m:] holds
    for i,j being Element of NAT st x=[i,j] holds
      Im(it,x) = [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):];
  existence
  proof
    defpred P[set,set] means for i,j being Element of NAT st $1=[i,j] holds
    $2= [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):];
    A1: for x being set st x in [:Seg n, Seg m:] ex y being set
      st y in bool [:Seg n, Seg m:] & P[x,y]
    proof let x be set;
      assume x in [:Seg n, Seg m:];
      then consider u,y being set such that
        A2: u in Seg n & y in Seg m & x = [u,y] by ZFMISC_1:def 2;
      reconsider i=u, j=y as Element of NAT by A2;
      set y3= [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):];
A3:   y3 c= [:Seg n, Seg m:]
      proof let z be set;assume z in y3;
        then ex x4,y4 being set st
          x4 in Im(Nbdl1 n,i) & y4 in Im(Nbdl1 m,j)
          & z = [x4,y4] by ZFMISC_1:def 2;
       hence z in [:Seg n, Seg m:] by ZFMISC_1:def 2;
      end;
       for i4,j4 being Element of NAT st x=[i4,j4] holds
        y3= [:Im(Nbdl1 n,i4), Im(Nbdl1 m,j4):]
      proof let i4,j4 be Element of NAT;
        assume x=[i4,j4]; then i4=u & j4=y by A2,ZFMISC_1:33;
        hence y3= [:Im(Nbdl1 n,i4), Im(Nbdl1 m,j4):];
      end;
      hence ex y2 being set st y2 in bool [:Seg n, Seg m:] & P[x,y2] by A3;
    end;
    consider f being Function of [:Seg n, Seg m:],bool [:Seg n, Seg m:]
      such that
A4:     for x being set st x in [:Seg n, Seg m:] holds P[x,f.x]
      from FUNCT_2:sch 1(A1);
     consider R being Relation of [:Seg n, Seg m:] such that
A5:   for i being set st i in [:Seg n, Seg m:] holds Im(R,i) = f.i
        by FUNCT_2:170;
    take R;
    let x be set such that
A6:  x in [:Seg n, Seg m:];
    let i,j be Element of NAT such that
A7:  x=[i,j];
    thus Im(R,x) = f.x by A5,A6
      .= [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):] by A4,A6,A7;
  end;
  uniqueness
  proof let f1,f2 be Relation of [:Seg n, Seg m:];
    assume
A8: ( for x being set st x in [:Seg n, Seg m:]
      holds for i,j being Element of NAT st x=[i,j] holds
      Im(f1,x) = [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):]) &
      ( for x being set st x in [:Seg n, Seg m:]
      holds for i,j being Element of NAT st x=[i,j] holds
      Im(f2,x) = [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):]);
     for x being set st x in [:Seg n, Seg m:] holds Im(f1,x)=Im(f2,x)
    proof let x be set;
      assume A9: x in [:Seg n, Seg m:];
      then consider u,y being set such that
A10:  u in Seg n & y in Seg m & x=[u,y] by ZFMISC_1:def 2;
      reconsider i=u, j=y as Element of NAT by A10;
       Im(f1,x)= [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):] by A8,A9,A10;
      hence Im(f1,x)=Im(f2,x) by A8,A9,A10;
    end;
    hence f1=f2 by RELSET_1:54;
  end;
end;

definition let n,m be Element of NAT;
  func FTSL2(n,m) -> strict RelStr equals
   RelStr(# [:Seg n, Seg m:], Nbdl2(n,m) #);
  coherence;
end;

registration let n,m be non zero Element of NAT;
  cluster FTSL2(n,m) -> non empty;
  coherence
  proof
A1: 0+1<=m by NAT_1:13;
     0+1<=n by NAT_1:13;
    then n in Seg n & m in Seg m by A1;
    then [n,m] in [:Seg n, Seg m:] by ZFMISC_1:def 2;
    hence thesis by STRUCT_0:def 1;
  end;
end;

theorem
   for n,m being non zero Element of NAT holds FTSL2(n,m) is filled
  proof let n,m be non zero Element of NAT;
     for x being Element of FTSL2(n,m) holds x in U_FT x
    proof let x be Element of FTSL2(n,m);
      consider u,y being set such that
      A1: u in Seg n & y in Seg m & x=[u,y] by ZFMISC_1:def 2;
      reconsider i=u, j=y as Element of NAT by A1;
A2:   FTSL1 m = RelStr(# Seg m,Nbdl1 m #) by FINTOPO4:def 4;
A3:   FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
      then reconsider pi=i as Element of FTSL1 n by A1;
      reconsider pj=j as Element of FTSL1 m by A1,A2;
       FTSL1 n is filled by FINTOPO4:18;
then A4:   i in U_FT pi by FIN_TOPO:def 4;
       FTSL1 m is filled by FINTOPO4:18;
      then j in U_FT pj by FIN_TOPO:def 4;
      then x in [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):] by A1,A2,A3,A4,ZFMISC_1:def 2
;
      hence x in U_FT x by A1,Def2;
    end;
    hence thesis by FIN_TOPO:def 4;
  end;

theorem
   for n,m being non zero Element of NAT holds FTSL2(n,m) is symmetric
  proof let n,m be non zero Element of NAT;
     for x, y being Element of FTSL2(n,m) holds
      y in U_FT x implies x in U_FT y
    proof let x, y be Element of FTSL2(n,m);
      assume A1: y in U_FT x;
      consider xu,xv being set such that
  A2: xu in Seg n & xv in Seg m & x=[xu,xv] by ZFMISC_1:def 2;
      reconsider i=xu, j=xv as Element of NAT by A2;
  A3: FTSL1 m = RelStr(# Seg m,Nbdl1 m #) by FINTOPO4:def 4;
  A4: FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
      then reconsider pi=i as Element of FTSL1 n by A2;
      reconsider pj=j as Element of FTSL1 m by A2,A3;
      consider yu,yv being set such that
      A5: yu in Seg n & yv in Seg m & y=[yu,yv] by ZFMISC_1:def 2;
      reconsider i2=yu, j2=yv as Element of NAT by A5;
      reconsider pi2=i2 as Element of FTSL1 n by A4,A5;
      reconsider pj2=j2 as Element of FTSL1 m by A3,A5;
       y in [:Im(Nbdl1 n,i), Im(Nbdl1 m,j):] by A1,A2,Def2;
      then consider y1,y2 being set such that
 A6: y1 in Class(Nbdl1 n,i) & y2 in Class(Nbdl1 m,j) & y=[y1,y2]
                                 by ZFMISC_1:def 2;
 A7: pi2 in U_FT pi by A4,A5,A6,ZFMISC_1:33;
       FTSL1 n is symmetric by FINTOPO4:19;
then A8:   pi in U_FT pi2 by A7,FIN_TOPO:def 15;
 A9: j2 in U_FT pj by A3,A5,A6,ZFMISC_1:33;
       FTSL1 m is symmetric by FINTOPO4:19;
      then j in U_FT pj2 by A9,FIN_TOPO:def 15;
      then x in [:Im(Nbdl1 n,i2), Im(Nbdl1 m,j2):] by A2,A3,A4,A8,
ZFMISC_1:def 2;
      hence x in U_FT y by A5,Def2;
    end;
    hence thesis by FIN_TOPO:def 15;
  end;

theorem
   for n being non zero Element of NAT
  ex h being Function of FTSL2(n,1), FTSL1 n st h is_homeomorphism
  proof let n be non zero Element of NAT;
    defpred P[set,set] means [$2,1]=$1;
A1: FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
    A2: for x be set st x in the carrier of FTSL2(n,1)
    ex y be set st y in the carrier of FTSL1 n & P[x,y]
    proof let x be set;
      assume x in the carrier of FTSL2(n,1);
      then consider u,v being set such that
        A3: u in Seg n & v in Seg 1 & x= [u,v] by ZFMISC_1:def 2;
      reconsider nu=u,nv=v as Element of NAT by A3;
       1<=nv & nv<=1 by A3,FINSEQ_1:3;
      then A4: P[x,nu] by A3,XXREAL_0:1;
       FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
      hence ex y be set st y in the carrier of FTSL1 n & P[x,y] by A3,A4;
    end;
     ex f being Function of FTSL2(n,1), FTSL1 n
       st for x be set st x in the carrier of FTSL2(n,1) holds P[x,f.x]
               from FUNCT_2:sch 1(A2);
    then consider f being Function of FTSL2(n,1), FTSL1 n such that
A5: for x be set st x in the carrier of FTSL2(n,1) holds P[x,f.x];
     for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2
      holds x1=x2
    proof let x1,x2 be set;
      assume A6: x1 in dom f & x2 in dom f & f.x1=f.x2;
      then [f.x1,1]=x1 by A5;
      hence x1=x2 by A5,A6;
    end;
then A7: f is one-to-one by FUNCT_1:def 8;
A8: the carrier of FTSL1 n c= rng f
    proof let x be set;
      assume
A9:     x in the carrier of FTSL1 n;
      set z=[x,1];
       1 in Seg 1;
 then A10: z in the carrier of FTSL2(n,1) by A1,A9,ZFMISC_1:def 2;
 then A11: z in dom f by FUNCT_2:def 1;
       [f.z,1]=z by A5,A10;
      then f.z=x by ZFMISC_1:33;
      hence x in rng f by A11,FUNCT_1:def 5;
    end;
    then rng f= the carrier of FTSL1 n by XBOOLE_0:def 10;
then A12: f is onto by FUNCT_2:def 3;
    set FT1=FTSL2(n,1),FT2= FTSL1 n;
     for x being Element of FT1 holds
      f.:U_FT(x)=Im(the InternalRel of FT2,f.x)
    proof let x be Element of FT1;
      consider u,v being set such that
 A13: u in Seg n & v in Seg 1 & x= [u,v] by ZFMISC_1:def 2;
 A14: f.:U_FT x c= Im(the InternalRel of FT2,f.x)
      proof let y be set;
        assume y in f.:U_FT x;
        then consider x2 being set such that
   A15: x2 in dom f & x2 in Im(Nbdl2(n,1),x) & y=f.x2 by FUNCT_1:def 12;
   A16: Im(Nbdl2(n,1),x) =[:Im(Nbdl1 n,u),Im(Nbdl1 1,v):] by A13,Def2;
         x=[f.x,1] by A5;
   then A17: u=f.x & v=1 by A13,ZFMISC_1:33;
         x2=[f.x2,1] by A5,A15;
        hence y in Im(the InternalRel of FT2,f.x)
        by A1,A15,A16,A17,ZFMISC_1:106;
      end;
       Im(the InternalRel of FT2,f.x) c= f.:U_FT x
      proof let y be set;
        assume A18: y in Im(the InternalRel of FT2,f.x);
         Im(Nbdl1 n,f.x) c= rng f by A1,A8,XBOOLE_1:1;
        then consider x3 being set such that
   A19: x3 in dom f & y=f.x3 by A1,A18,FUNCT_1:def 5;
        set u2=f.x3,v2=1;
A20:    x3=[f.x3,1] by A5,A19;
        reconsider nv=v as Element of NAT by A13;
         1 <= nv & nv <= 1 by A13,FINSEQ_1:3;
   then A21: nv = 1 by XXREAL_0:1;
         Im(Nbdl1 1,v) = {nv, max(nv-'1,1),min(nv+1,1)} by A13,FINTOPO4:def 3
                   .= {1,max(0,1),min(2,1)} by A21,NAT_2:10
                   .= {1,1,min(2,1)} by XXREAL_0:def 9
                   .= {1, min(2,1)} by ENUMSET1:70
                   .= {1, 1} by XXREAL_0:def 8
                   .= {1} by ENUMSET1:69;
   then A22: v2 in Im(Nbdl1 1,v) by ZFMISC_1:37;
         x=[f.x,1] by A5;
        then u2 in Im(Nbdl1 n,u) by A1,A13,A18,A19,ZFMISC_1:33;
   then A23: [u2,v2] in [:Im(Nbdl1 n,u),Im(Nbdl1 1,v):]
                     by A22,ZFMISC_1:def 2;
         Im(Nbdl2(n,1),x) = [:Im(Nbdl1 n,u),Im(Nbdl1 1,v):]
                     by A13,Def2;
        hence y in f.:U_FT x by A19,A20,A23,FUNCT_1:def 12;
      end;
      hence f.:U_FT(x)=Im(the InternalRel of FT2,f.x)
                        by A14,XBOOLE_0:def 10;
    end;
    then f is_homeomorphism by A7,A12,Def1;
    hence thesis;
  end;

:: 2-dimensional small FT_Str
definition let n,m be Element of NAT;
  func Nbds2(n,m) -> Relation of [:Seg n, Seg m:] means
  :Def4: for x being set st x in [:Seg n, Seg m:]
    holds for i,j being Element of NAT st x=[i,j] holds
    Im(it,x) = [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,i),{j}:];
  existence
  proof
    defpred P[set,set] means for i,j being Element of NAT st $1=[i,j] holds
    $2= [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,i),{j}:];
A1: for x being set st x in [:Seg n, Seg m:] ex y being set
      st y in bool [:Seg n, Seg m:] & P[x,y]
    proof let x be set;
      assume x in [:Seg n, Seg m:];
      then consider u,y being set such that
  A2: u in Seg n & y in Seg m & x = [u,y] by ZFMISC_1:def 2;
      reconsider i=u, j=y as Element of NAT by A2;
      set y3= [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,u),{j}:];
A3:   y3 c= [:Seg n, Seg m:]
      proof let z be set;
        assume
A4:       z in y3;
        per cases by A4,XBOOLE_0:def 2;
          suppose z in [:{i}, Im(Nbdl1 m,j):];
            then consider x4,y4 being set such that
        A5: x4 in {i} & y4 in Im(Nbdl1 m,j) & z=[x4,y4] by ZFMISC_1:def 2;
            x4 = i by A5,TARSKI:def 1;
            hence z in [:Seg n, Seg m:] by A2,A5,ZFMISC_1:def 2;
          end;
          suppose z in [:Im(Nbdl1 n,u),{j}:];
            then consider x4,y4 being set such that
            A6: x4 in Im(Nbdl1 n,u) & y4 in {j} & z=[x4,y4] by ZFMISC_1:def 2;
             y4 in Seg m by A2,A6,TARSKI:def 1;
            hence z in [:Seg n, Seg m:] by A6,ZFMISC_1:def 2;
          end;
       end;
       for i4,j4 being Element of NAT st x=[i4,j4] holds
        y3= [:{i4}, Im(Nbdl1 m,j4):] \/ [:Im(Nbdl1 n,i4),{j4}:]
      proof let i4,j4 be Element of NAT;
        assume x=[i4,j4];
        then i4=u & j4=y by A2,ZFMISC_1:33;
        hence y3= [:{i4}, Im(Nbdl1 m,j4):] \/ [:Im(Nbdl1 n,i4),{j4}:];
      end;
      hence ex y2 being set st y2 in bool [:Seg n, Seg m:] & P[x,y2] by A3;
    end;
    consider f being Function of [:Seg n, Seg m:],bool [:Seg n, Seg m:]
      such that
A7:  for x being set st x in [:Seg n, Seg m:] holds P[x,f.x]
      from FUNCT_2:sch 1(A1);
    consider R being Relation of [:Seg n, Seg m:] such that
A8:  for i being set st i in [:Seg n, Seg m:] holds Im(R,i) = f.i
      by FUNCT_2:170;
    take R;
    let x be set such that
A9:  x in [:Seg n, Seg m:];
    let i,j be Element of NAT such that
A10:  x=[i,j];
    thus Im(R,x) = f.x by A8,A9
      .= [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,i),{j}:] by A7,A9,A10;
  end;
  uniqueness
  proof let f1,f2 be Relation of [:Seg n, Seg m:];
    assume
  A11: ( for x being set st x in [:Seg n, Seg m:]
      holds (for i,j being Element of NAT st x=[i,j] holds
      Im(f1,x) = [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,i),{j}:])) &
      for x being set st x in [:Seg n, Seg m:]
      holds (for i,j being Element of NAT st x=[i,j] holds
      Im(f2,x) = [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,i),{j}:]);
     for x being set st x in [:Seg n, Seg m:] holds Im(f1,x)=Im(f2,x)
    proof let x be set;
      assume A12: x in [:Seg n, Seg m:];
      then consider u,y being set such that
A13:  u in Seg n & y in Seg m & x=[u,y] by ZFMISC_1:def 2;
      reconsider i=u, j=y as Element of NAT by A13;
       Im(f1,x)= [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,u),{j}:]
      by A11,A12,A13;
      hence Im(f1,x)=Im(f2,x) by A11,A12,A13;
    end;
    hence f1=f2 by RELSET_1:54;
  end;
end;

definition let n,m be Element of NAT;
  func FTSS2(n,m) -> strict RelStr equals
   RelStr(# [:Seg n, Seg m:], Nbds2(n,m) #);
  coherence;
end;

registration let n,m be non zero Element of NAT;
  cluster FTSS2(n,m) -> non empty;
  coherence
  proof
A1: 0+1<=m by NAT_1:13;
     0+1<=n by NAT_1:13;
    then n in Seg n & m in Seg m by A1;
    then [n,m] in [:Seg n, Seg m:] by ZFMISC_1:def 2;
    hence thesis by STRUCT_0:def 1;
  end;
end;

theorem
   for n,m being non zero Element of NAT holds FTSS2(n,m) is filled
  proof let n,m be non zero Element of NAT;
     for x being Element of FTSS2(n,m) holds x in U_FT x
    proof let x be Element of FTSS2(n,m);
      consider u,y being set such that
A1:   u in Seg n & y in Seg m & x=[u,y] by ZFMISC_1:def 2;
      reconsider i=u, j=y as Element of NAT by A1;
A2:   FTSL1 m = RelStr(# Seg m,Nbdl1 m #) by FINTOPO4:def 4;
   FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
      then reconsider pi=i as Element of FTSL1 n by A1;
      reconsider pj=j as Element of FTSL1 m by A1,A2;
       FTSL1 m is filled by FINTOPO4:18;
then A3:   j in U_FT pj by FIN_TOPO:def 4;
       i in {i} by ZFMISC_1:37;
      then x in [:{i}, Im(Nbdl1 m,j):] by A1,A2,A3,ZFMISC_1:def 2;
      then x in [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,u), {j}:]
        by XBOOLE_0:def 2;
      hence x in U_FT x by A1,Def4;
    end;
    hence FTSS2(n,m) is filled by FIN_TOPO:def 4;
  end;

theorem
   for n,m being non zero Element of NAT holds FTSS2(n,m) is symmetric
  proof let n,m be non zero Element of NAT;
     for x, y being Element of FTSS2(n,m) holds
      y in U_FT x implies x in U_FT y
    proof let x, y be Element of FTSS2(n,m);
      assume A1: y in U_FT x;
      consider xu,xv being set such that
  A2: xu in Seg n & xv in Seg m & x=[xu,xv] by ZFMISC_1:def 2;
      reconsider i=xu, j=xv as Element of NAT by A2;
A3:   FTSL1 m = RelStr(# Seg m,Nbdl1 m #) by FINTOPO4:def 4;
A4:   FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
      then reconsider pi=i as Element of FTSL1 n by A2;
      reconsider pj=j as Element of FTSL1 m by A2,A3;
      consider yu,yv being set such that
  A5: yu in Seg n & yv in Seg m & y=[yu,yv] by ZFMISC_1:def 2;
      reconsider i2=yu, j2=yv as Element of NAT by A5;
      reconsider pi2=i2 as Element of FTSL1 n by A4,A5;
      reconsider pj2=j2 as Element of FTSL1 m by A3,A5;
A6:   y in [:{i}, Im(Nbdl1 m,j):] \/ [:Im(Nbdl1 n,i),{j}:]
                                         by A1,A2,Def4;
       now per cases by A6,XBOOLE_0:def 2;
        case y in [:{i}, Im(Nbdl1 m,j):];
          then consider y1,y2 being set such that
           A7: y1 in {i} & y2 in Class(Nbdl1 m,j) & y=[y1,y2]
                                          by ZFMISC_1:def 2;
          A8: y1 = i by A7,TARSKI:def 1;
          A9: pj2 in U_FT pj by A3,A5,A7,ZFMISC_1:33;
           FTSL1 m is symmetric by FINTOPO4:19;
then A10:      pj in U_FT pj2 by A9,FIN_TOPO:def 15;
           i in {i2} by A5,A7,A8,ZFMISC_1:33;
          hence x in [:{i2}, Im(Nbdl1 m,j2):] by A2,A3,A10,ZFMISC_1:def 2;
        end;
        case y in [:Im(Nbdl1 n,i),{j}:];
          then consider y1,y2 being set such that
      A11: y1 in Class(Nbdl1 n,i) & y2 in {j} & y=[y1,y2] by ZFMISC_1:def 2;
      A12: y2 = j by A11,TARSKI:def 1;
      A13: pi2 in U_FT pi by A4,A5,A11,ZFMISC_1:33;
           FTSL1 n is symmetric by FINTOPO4:19;
then A14:      pi in U_FT pi2 by A13,FIN_TOPO:def 15;
           j in {j2} by A5,A11,A12,ZFMISC_1:33;
          hence x in [:Im(Nbdl1 n,i2), {j2}:] by A2,A4,A14,ZFMISC_1:def 2;
        end;
     end;
     then x in [:{i2}, Im(Nbdl1 m,j2):] \/ [:Im(Nbdl1 n,i2), {j2}:]
                              by XBOOLE_0:def 2;
     hence x in U_FT y by A5,Def4;
   end;
   hence thesis by FIN_TOPO:def 15;
 end;

theorem
   for n being non zero Element of NAT
   ex h being Function of FTSS2(n,1), FTSL1 n st h is_homeomorphism
  proof let n be non zero Element of NAT;
    defpred P[set,set] means [$2,1]=$1;
A1: FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
A2: for x be set st x in the carrier of FTSS2(n,1)
    ex y be set st y in the carrier of FTSL1 n & P[x,y]
    proof let x be set;
      assume x in the carrier of FTSS2(n,1);
      then consider u,v being set such that
  A3: u in Seg n & v in Seg 1 & x= [u,v] by ZFMISC_1:def 2;
      reconsider nu=u,nv=v as Element of NAT by A3;
       1<=nv & nv<=1 by A3,FINSEQ_1:3;
  then A4: P[x,nu] by A3,XXREAL_0:1;
       FTSL1 n = RelStr(# Seg n,Nbdl1 n #) by FINTOPO4:def 4;
      hence thesis by A3,A4;
    end;
     ex f being Function of FTSS2(n,1), FTSL1 n
       st for x be set st x in the carrier of FTSS2(n,1) holds P[x,f.x]
               from FUNCT_2:sch 1(A2);
    then consider f being Function of FTSS2(n,1), FTSL1 n such that
A5: for x be set st x in the carrier of FTSS2(n,1) holds P[x,f.x];
     for x1,x2 being set st x1 in dom f & x2 in dom f & f.x1=f.x2
      holds x1=x2
    proof let x1,x2 be set;
      assume A6: x1 in dom f & x2 in dom f & f.x1=f.x2;
      then [f.x1,1]=x1 by A5;
      hence x1=x2 by A5,A6;
    end;
then A7: f is one-to-one by FUNCT_1:def 8;
A8: the carrier of FTSL1 n c= rng f
    proof let x be set;
      assume
A9:     x in the carrier of FTSL1 n;
      set z=[x,1];
       1 in Seg 1;
 then A10: z in the carrier of FTSS2(n,1) by A1,A9,ZFMISC_1:def 2;
 then A11: z in dom f by FUNCT_2:def 1;
       [f.z,1]=z by A5,A10;
      then f.z=x by ZFMISC_1:33;
      hence x in rng f by A11,FUNCT_1:def 5;
    end;
    then rng f= the carrier of FTSL1 n by XBOOLE_0:def 10;
then A12: f is onto by FUNCT_2:def 3;
    set FT1=FTSS2(n,1),FT2= FTSL1 n;
     for x being Element of FT1 holds
      f.:U_FT x=Im(the InternalRel of FT2,f.x)
    proof let x be Element of FT1;
      consider u,v being set such that
 A13: u in Seg n & v in Seg 1 & x= [u,v] by ZFMISC_1:def 2;
 A14: f.:U_FT x c= Im(the InternalRel of FT2,f.x)
      proof let y be set;
        assume y in f.:U_FT x;
        then consider x2 being set such that
A15:    x2 in dom f & x2 in Im(Nbds2(n,1),x) & y=f.x2
             by FUNCT_1:def 12;
A16:    Im(Nbds2(n,1),x) = [:{u}, Im(Nbdl1 1,v):] \/ [:Im(Nbdl1 n,u),{v}:]
                        by A13,Def4;
        consider u2,v2 being set such that
A17:    u2 in Seg n & v2 in Seg 1 & x2= [u2,v2] by A15,ZFMISC_1:def 2;
A18:    x=[f.x,1] by A5;
         x2=[f.x2,1] by A5,A15;
then A19:    u2=f.x2 & v2=1 by A17,ZFMISC_1:33;
         now per cases by A15,A16,A17,XBOOLE_0:def 2;
          suppose [u2,v2] in [:{u}, Im(Nbdl1 1,v):];
then A20:        u2 in {u} by ZFMISC_1:106;
            reconsider pu=u as Element of FTSL1 n by A1,A13;
             (FTSL1 n) is filled by FINTOPO4:18;
            then u in U_FT pu by FIN_TOPO:def 4;
            hence u2 in Class(Nbdl1 n,u) by A1,A20,TARSKI:def 1;
          end;
          suppose [u2,v2] in [:Im(Nbdl1 n,u),{v}:];
           hence u2 in Class(Nbdl1 n,u) by ZFMISC_1:106;
          end;
        end;
        hence y in Im(the InternalRel of FT2,f.x)
         by A1,A13,A15,A18,A19,ZFMISC_1:33;
      end;
       Im(the InternalRel of FT2,f.x) c= f.:U_FT x
      proof let y be set;
        assume A21: y in Im(the InternalRel of FT2,f.x);
         Im(Nbdl1 n,f.x) c= rng f by A1,A8,XBOOLE_1:1;
        then consider x3 being set such that
   A22: x3 in dom f & y=f.x3 by A1,A21,FUNCT_1:def 5;
        set u2=f.x3,v2=1;
A23:    x3=[f.x3,1] by A5,A22;
        reconsider nv=v as Element of NAT by A13;
         1 <= nv & nv <= 1 by A13,FINSEQ_1:3;
   then A24: nv = 1 by XXREAL_0:1;
  A25: Im(Nbdl1 1,v) = {nv, max(nv-'1,1),min(nv+1,1)} by A13,FINTOPO4:def 3
                   .= {1,max(0,1),min(2,1)} by A24,NAT_2:10
                   .= {1,1,min(2,1)} by XXREAL_0:def 9
                   .= {1, min(2,1)} by ENUMSET1:70
                   .= {1, 1} by XXREAL_0:def 8
                   .= {1} by ENUMSET1:69;
        then A26: v2 in Im(Nbdl1 1,v) by ZFMISC_1:37;
         x=[f.x,1] by A5;
        then u2 in Im(Nbdl1 n,u) by A1,A13,A21,A22,ZFMISC_1:33;
   then A27: [u2,v2] in [:Im(Nbdl1 n,u), Im(Nbdl1 1,v):] by A26,ZFMISC_1:def 2;
        set X=Im(Nbdl1 n,u), Y=Im(Nbdl1 1,v);
  A28: [u2,v2] in [:X,{v}:] \/ [:{u},Y:] by A24,A25,A27,XBOOLE_0:def 2;
         Im(Nbds2(n,1),x) = [:{u}, Y:] \/ [:X,{v}:] by A13,Def4;
       hence y in f.:U_FT x by A22,A23,A28,FUNCT_1:def 12;
      end;
      hence f.:U_FT(x)=Im(the InternalRel of FT2,f.x)
                        by A14,XBOOLE_0:def 10;
    end;
    then f is_homeomorphism by A7,A12,Def1;
    hence thesis;
  end;
