:: On the Boundary and Derivative of a Set
::  by Adam Grabowski
:: 
:: Received November 8, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, SETFAM_1, SUBSET_1, BOOLE, TARSKI, FINSET_1, TOPGEN_1,
      TOPS_1, NATTRA_1, RLVECT_3, CARD_1, ARYTM, URYSOHN1, TOPMETR, ORDINAL1,
      RAT_1, BORSUK_5, CARD_4;
 notations TARSKI, XBOOLE_0, SUBSET_1, CARD_1, ORDINAL1, NUMBERS, CARD_4,
      FINSET_1, STRUCT_0, GROUP_1, PRE_TOPC, TOPS_1, TDLAT_3, YELLOW_8,
      URYSOHN1, TOPMETR, BORSUK_5, ORDERS_4;
 constructors FUNCT_3, NAT_1, FINSEQ_1, PRE_TOPC, XREAL_0, MEMBERED, TOPS_1,
      TOPS_2, CONNSP_1, TEX_2, TDLAT_3, REALSET2, YELLOW_8, CARD_1, CANTOR_1,
      ORDINAL2, WAYBEL23, URYSOHN1, COMPTS_1, TOPMETR, RAT_1, BORSUK_5, REAL_1,
      NUMBERS, CARD_4, ORDERS_4, GROUP_1, PCOMPS_1, BINARITH, BORSUK_3;
 registrations STRUCT_0, MEMBERED, TOPS_1, TEX_4, TEX_1, YELLOW13, CARD_1,
      WAYBEL12, BORSUK_3;
 requirements NUMERALS, BOOLE, SUBSET;
 definitions TARSKI, XBOOLE_0;
 theorems TARSKI, ZFMISC_1, PRE_TOPC, TOPS_1, XBOOLE_0, XBOOLE_1, SUBSET_1,
      TOPS_3, TDLAT_3, YELLOW_8, FRECHET, FRECHET2, BORSUK_5, TOPMETR, NUMBERS,
      CARD_4, ORDERS_4, GROUP_1;
 schemes PRE_TOPC, ORDINAL1;

begin :: Preliminaries

LemPos:
  for T being empty TopSpace,
      A being Subset of T holds
    A = {};

definition let T be set, A be Subset of T, B be set; :: move to SUBSET_1
  redefine func A \ B -> Subset of T;
  coherence
  proof
    A \ B c= A by XBOOLE_1:36;
    hence thesis by XBOOLE_1:1;
  end;
end;

theorem Lem1:
  for T being 1-sorted,
      A, B being Subset of T holds A meets B` iff A \ B <> {}
  proof
    let T be 1-sorted,
        A, B be Subset of T;
    hereby
      assume A meets B`; then
      A /\ B` <> {} by XBOOLE_0:def 7;
      hence A \ B <> {} by SUBSET_1:32;
    end;
    assume A \ B <> {};
    then A /\ B` <> {} by SUBSET_1:32;
    hence thesis by XBOOLE_0:def 7;
  end;

theorem Cosik:
  for T being 1-sorted holds
    T is countable iff [#]T is countable
  proof
    let T be 1-sorted;
    hereby
      assume T is countable; then
      the carrier of T is countable by ORDERS_4:def 2;
      hence [#]T is countable by PRE_TOPC:def 3;
    end;
    assume [#]T is countable; then
      the carrier of T is countable by PRE_TOPC:def 3;
    hence thesis by ORDERS_4:def 2;
  end;

theorem
  for T being 1-sorted holds
    T is countable iff Card [#]T <=` alef 0
  proof
    let T be 1-sorted;
    hereby
      assume T is countable; then
      [#]T is countable by Cosik;
      hence Card [#]T <=` alef 0 by CARD_4:def 1;
    end;
    assume Card [#]T <=` alef 0; then
    [#]T is countable by CARD_4:def 1;
    hence thesis by Cosik;
  end;

registration let T be finite 1-sorted;
  cluster [#]T -> finite;
  coherence by PRE_TOPC:def 3;
end;

registration
  cluster finite -> countable 1-sorted;
  coherence
  proof
    let T be 1-sorted;
    assume T is finite; then
    the carrier of T is finite by GROUP_1:def 14; then
    the carrier of T is countable by CARD_4:43;
    hence thesis by ORDERS_4:def 2;
  end;
end;

registration
  cluster countable non empty 1-sorted;
  existence
  proof
    consider T being finite non empty 1-sorted;
    take T;
    thus thesis;
  end;
  cluster countable non empty TopSpace;
  existence
  proof
    consider T being finite non empty TopSpace;
    take T;
    thus thesis;
  end;
end;

registration let T be countable 1-sorted;
  cluster [#]T -> countable;
  coherence
  proof
    the carrier of T = [#]T by PRE_TOPC:def 3;
    hence thesis by ORDERS_4:def 2;
  end;
end;

registration
  cluster being_T1 non empty TopSpace;
  existence
  proof
    consider T being discrete non empty TopSpace;
    take T;
    thus thesis;
  end;
end;

begin :: Boundary of a Subset

theorem SUBSET_1'28:
  for T being TopStruct,
      A being Subset of T holds
    A \/ [#]T = [#]T
  proof
    let T be TopStruct,
        A be Subset of T;
    A \/ [#]T = A \/ (the carrier of T) by PRE_TOPC:def 3
             .= the carrier of T by XBOOLE_1:12
             .= [#]T by PRE_TOPC:def 3;
    hence thesis;
  end;

theorem Pik1:
  for T being TopSpace,
      A being Subset of T holds
    Int A = (Cl A`)`
  proof
    let T be TopSpace,
        A be Subset of T;
    Int A = (Cl A```)` by TDLAT_3:4
         .= (Cl A`)`;
    hence thesis;
  end;

:: Collective Boundary

definition let T be TopSpace,
               F be Subset-Family of T;
  func Fr F -> Subset-Family of T means :ColFr:
    for A being Subset of T holds A in it iff
      ex B being Subset of T st A = Fr B & B in F;
  existence
  proof
    defpred P[set] means
    ex W being Subset of T st $1 = Fr W & W in F;
    thus ex S be Subset-Family of T st
    for Z being Subset of T holds Z in S iff P[Z] from PRE_TOPC:sch 1;
  end;
  uniqueness
  proof
    let H, G be Subset-Family of T;
    defpred P[set] means
    ex W being Subset of T st $1 = Fr W & W in F;
    assume that
A1: for Z being Subset of T holds Z in H iff P[Z] and
A2: for X being Subset of T holds X in G iff P[X];
    H = G
    proof
     now
      let Z be set;
      assume A3: Z in H;
      then reconsider Z'=Z as Subset of T;
      P[Z] by A1,A3;
      hence Z in G by A2;
     end;
     then A4: H c= G by TARSKI:def 3;
      now
      let X be set;
      assume A5: X in G;
      then reconsider X'=X as Subset of T;
      P[X'] by A2,A5;
      hence X in H by A1;
     end;
     then G c= H by TARSKI:def 3;
     hence thesis by A4,XBOOLE_0:def 10;
    end;
   hence thesis;
  end;
end;

theorem
  for T being TopSpace,
      F being Subset-Family of T st
    F = {} holds Fr F = {}
  proof
    let T be TopSpace,
        F be Subset-Family of T;
    assume
A0: F = {};
    assume Fr F <> {}; then
    consider x being set such that
A1: x in Fr F by XBOOLE_0:def 1;
    consider B being Subset of T such that
A2: x = Fr B & B in F by A1,ColFr;
    thus thesis by A2,A0;
  end;

theorem
  for T being TopSpace,
      F being Subset-Family of T,
      A being Subset of T st
    F = { A } holds Fr F = { Fr A }
  proof
    let T be TopSpace,
        F be Subset-Family of T,
        A be Subset of T;
    assume
A1: F = { A }; then
B1: A in F by TARSKI:def 1;
    thus Fr F c= { Fr A }
    proof
      let x be set;
      assume
C1:   x in Fr F; then
      reconsider B = x as Subset of T;
      consider C being Subset of T such that
C2:   B = Fr C & C in F by C1,ColFr;
      C = A by C2,A1,TARSKI:def 1;
      hence thesis by C2,TARSKI:def 1;
    end;
    let x be set;
    assume x in { Fr A }; then
    x = Fr A by TARSKI:def 1;
    hence thesis by B1,ColFr;
  end;

theorem U21:
  for T being TopSpace,
      F, G being Subset-Family of T st
    F c= G holds Fr F c= Fr G
  proof
    let T be TopSpace,
        F, G be Subset-Family of T;
    assume
A0: F c= G;
    Fr F c= Fr G
    proof
      let x be set;
      assume
A1:   x in Fr F; then
      reconsider A = x as Subset of T;
      consider B being Subset of T such that
A2:   A = Fr B & B in F by A1,ColFr;
      thus x in Fr G by A0,A2,ColFr;
    end;
    hence thesis;
  end;

theorem
  for T being TopSpace,
      F, G being Subset-Family of T holds
    Fr (F \/ G) = Fr F \/ Fr G
  proof
    let T be TopSpace,
        F, G be Subset-Family of T;
    thus Fr (F \/ G) c= Fr F \/ Fr G
    proof
      let x be set;
      assume
A1:   x in Fr (F \/ G); then
      reconsider A = x as Subset of T;
      consider B being Subset of T such that
A2:   A = Fr B & B in F \/ G by A1,ColFr;
      per cases by A2,XBOOLE_0:def 2;
      suppose B in F; then
        A in Fr F by A2,ColFr;
        hence thesis by XBOOLE_0:def 2;
      end;
      suppose B in G; then
        A in Fr G by A2,ColFr;
        hence thesis by XBOOLE_0:def 2;
      end;
    end;
    F c= F \/ G by XBOOLE_1:7; then
B1: Fr F c= Fr (F \/ G) by U21;
    G c= F \/ G by XBOOLE_1:7; then
    Fr G c= Fr (F \/ G) by U21;
    hence Fr F \/ Fr G c= Fr (F \/ G) by B1,XBOOLE_1:8;
  end;

theorem ThX: :: 1.3.0.
  for T being TopStruct,
      A being Subset of T holds
    Fr A = Cl A \ Int A
  proof
    let T be TopStruct,
        A be Subset of T;
    Fr A = Cl A /\ (Cl A`)`` by TOPS_1:def 2
        .= Cl A \ (Cl A`)` by SUBSET_1:32
        .= Cl A \ Int A by TOPS_1:def 1;
    hence thesis;
  end;

:: 1.3.1. Characterization of Fr via basis

theorem TOPS_1'61:
  for T being non empty TopSpace,
      A being Subset of T,
      p being Point of T holds
      p in Fr A iff
   (for U being Subset of T st U is open & p in U holds
     A meets U & U \ A <> {})
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        p be Point of T;
    hereby
      assume
A1:   p in Fr A;
      let U be Subset of T;
      assume U is open & p in U; then
      A meets U & U meets A` by A1,TOPS_1:61;
      hence A meets U & U \ A <> {} by Lem1;
    end;
    assume
B1: for U being Subset of T st U is open & p in U holds
    A meets U & U \ A <> {};
    for U being Subset of T st U is open & p in U holds
    A meets U & U meets A`
    proof
      let U be Subset of T;
      assume U is open & p in U; then
      A meets U & U \ A <> {} by B1;
      hence thesis by Lem1;
    end;
    hence thesis by TOPS_1:61;
  end;

theorem
  for T being non empty TopSpace,
      A being Subset of T,
      p being Point of T holds
    p in Fr A iff
      (for B being Basis of p,
         U being Subset of T st U in B holds A meets U & U \ A <> {})
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        p be Point of T;
    hereby
      assume
A1:   p in Fr A;
      let B be Basis of p,
          U be Subset of T;
      assume U in B; then
      U is open & p in U by YELLOW_8:21;
      hence A meets U & U \ A <> {} by A1,TOPS_1'61;
    end;
    assume
B1: for B being Basis of p,
        U being Subset of T st U in B holds A meets U & U \ A <> {};
    for U being Subset of T st U is open & p in U holds
      A meets U & U meets A`
    proof
      let U be Subset of T;
      assume
T1:   U is open & p in U;
      consider B being Basis of p;
      consider V being Subset of T such that
T2:   V in B & V c= U by T1,YELLOW_8:def 2;
T3:   A meets V & V \ A <> {} by B1,T2;
      V \ A c= U \ A by T2,XBOOLE_1:33; then
      U \ A <> {} by T3,XBOOLE_1:3;
      hence thesis by T3,Lem1,T2,XBOOLE_1:63;
    end;
    hence thesis by TOPS_1:61;
  end;

theorem
  for T being non empty TopSpace,
      A being Subset of T,
      p being Point of T holds
     p in Fr A iff
   (ex B being Basis of p st
     for U being Subset of T st U in B holds A meets U & U \ A <> {})
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        p be Point of T;
    hereby
      assume
A1:   p in Fr A;
      consider B being Basis of p;
      take B;
      let U be Subset of T;
      assume U in B; then
      U is open & p in U by YELLOW_8:21;
      hence A meets U & U \ A <> {} by A1,TOPS_1'61;
    end;
    given B being Basis of p such that
B1: for U being Subset of T st U in B holds A meets U & U \ A <> {};
    for U being Subset of T st U is open & p in U holds
      A meets U & U meets A`
    proof
      let U be Subset of T;
      assume U is open & p in U; then
      consider V being Subset of T such that
T2:   V in B & V c= U by YELLOW_8:def 2;
T3:   A meets V & V \ A <> {} by B1,T2;
      V \ A c= U \ A by T2,XBOOLE_1:33; then
      U \ A <> {} by T3,XBOOLE_1:3;
      hence thesis by T3,Lem1,T2,XBOOLE_1:63;
    end;
    hence thesis by TOPS_1:61;
  end;

theorem :: Theorem 1.3.2. (d)
  for T being TopSpace,
      A, B being Subset of T holds
    Fr (A /\ B) c= (Cl A /\ Fr B) \/ (Fr A /\ Cl B)
  proof
    let T be TopSpace,
        A, B be Subset of T;
A2: Fr (A /\ B) = Cl (A /\ B) /\ Cl (A /\ B)` by TOPS_1:def 2
               .= Cl (A /\ B) /\ Cl (A` \/ B`) by SUBSET_1:30
               .= Cl (A /\ B) /\ ((Cl A`) \/ Cl B`) by PRE_TOPC:50;
A1: Cl (A /\ B) c= Cl A /\ Cl B by PRE_TOPC:51;
    Cl A /\ Cl B /\ ((Cl A`) \/ Cl B`) =
      (Cl A /\ Cl B /\ (Cl A`)) \/ (Cl A /\ Cl B /\ Cl B`) by XBOOLE_1:23
      .= (Cl A /\ Cl A` /\ Cl B) \/ (Cl A /\ Cl B /\ Cl B`) by XBOOLE_1:16
      .= (Fr A /\ Cl B) \/ (Cl A /\ Cl B /\ Cl B`) by TOPS_1:def 2
      .= (Fr A /\ Cl B) \/ (Cl A /\ (Cl B /\ Cl B`)) by XBOOLE_1:16
      .= (Fr A /\ Cl B) \/ (Cl A /\ Fr B) by TOPS_1:def 2;
    hence thesis by A1,A2,XBOOLE_1:26;
  end;

theorem :: Theorem 1.3.2. (f)
  for T being TopSpace,
      A being Subset of T holds
    the carrier of T = Int A \/ Fr A \/ Int A`
  proof
    let T be TopSpace,
        A be Subset of T;
    Int A \/ Fr A \/ Int A` = Int A \/ Int A` \/ Fr A by XBOOLE_1:4
      .= Int A \/ Int A` \/ (Cl A /\ Cl A`) by TOPS_1:def 2
      .= (Int A \/ Int A` \/ Cl A) /\ (Int A \/ Int A` \/ Cl A`)
        by XBOOLE_1:24
      .= (Int A \/ (Cl A)` \/ Cl A) /\ (Int A \/ Int A` \/ Cl A`)
        by TDLAT_3:4
      .= (Int A \/ ((Cl A)` \/ Cl A)) /\ (Int A \/ Int A` \/ Cl A`)
          by XBOOLE_1:4
      .= (Int A \/ [#]T) /\ (Int A \/ Int A` \/ Cl A`) by PRE_TOPC:18
      .= (Int A \/ [#]T) /\ (Int A \/ Int A` \/ (Int A)`) by TDLAT_3:3
      .= (Int A \/ [#]T) /\ (Int A \/ (Int A)` \/ Int A`) by XBOOLE_1:4
      .= (Int A \/ [#]T) /\ ([#]T \/ Int A`) by PRE_TOPC:18
      .= [#]T /\ ([#]T \/ Int A`) by SUBSET_1'28
      .= [#]T /\ [#]T by SUBSET_1'28
      .= [#]T;
    hence thesis by PRE_TOPC:def 3;
  end;

theorem Th132k: :: Theorem 1.3.2. (k)
  for T being TopSpace,
      A being Subset of T holds
    A is open closed iff Fr A = {}
  proof
    let T be TopSpace,
        A be Subset of T;
    hereby assume
A1:   A is open closed; then
A2:   Int A = A by TOPS_1:55;
      Fr A = A \ Int A by A1,TOPS_1:77
          .= {} by A2,XBOOLE_1:37;
      hence Fr A = {};
    end;
    assume
B1: Fr A = {};
B3: A c= Cl A by PRE_TOPC:48;
    Fr A = Cl A \ Int A by ThX; then
B2: Cl A c= Int A by B1,XBOOLE_1:37;
B4: Int A c= A by TOPS_1:44; then
    Cl A c= A by B2,XBOOLE_1:1; then
    Cl A = A by XBOOLE_0:def 10,B3;
    hence thesis by B2,B4,XBOOLE_0:def 10;
  end;

begin :: Accumulation Points and Derivative of a Set

:: 1.3.2. Accumulation Points

definition let T be TopStruct, A be Subset of T, x be set;
  pred x is_an_accumulation_point_of A means :AcuDef:
    x in Cl (A \ {x});
end;

theorem AcuTh1:
  for T being TopSpace,
      A being Subset of T,
      x being set st
  x is_an_accumulation_point_of A holds x is Point of T
  proof
    let T be TopSpace,
        A be Subset of T,
        x be set;
    assume x is_an_accumulation_point_of A; then
    x in Cl (A \ {x}) by AcuDef;
    hence thesis;
  end;

:: Derivative of a Set

definition let T be TopStruct, A be Subset of T;
  func Der A -> Subset of T means :DerDef:
    for x being set st x in the carrier of T holds
      x in it iff x is_an_accumulation_point_of A;
  existence
  proof
    defpred P[set] means
      $1 is_an_accumulation_point_of A;
    consider D being Subset of T such that
A1: for x being set st x in the carrier of T holds x in D iff P[x]
      from PRE_TOPC:sch 2;
    take D;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let A1, A2 be Subset of T;
    defpred P[set] means $1 is_an_accumulation_point_of A;
    assume that
A1: (for x being set st x in the carrier of T holds
      x in A1 iff P[x]) and
A2: (for x being set st x in the carrier of T holds
      x in A2 iff P[x]);
A3: A1 c= A2
    proof
      let x be set;
      assume
B1:   x in A1;
      P[x] by B1,A1;
      hence thesis by A2,B1;
    end;
    A2 c= A1
    proof
      let x be set;
      assume
B1:   x in A2; then
      P[x] by A2;
      hence thesis by A1,B1;
    end;
    hence thesis by A3,XBOOLE_0:def 10;
  end;
end;

:: 1.3.3. Characterizations of a Derivative

theorem Th0:
  for T being non empty TopSpace,
      A being Subset of T,
      x being set holds
    x in Der A iff x is_an_accumulation_point_of A
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        x be set;
    thus x in Der A implies
      x is_an_accumulation_point_of A by DerDef;
    assume
A2: x is_an_accumulation_point_of A; then
    x is Point of T by AcuTh1;
    hence thesis by A2,DerDef;
  end;

theorem Th1:
  for T being non empty TopSpace,
      A being Subset of T,
      x being Point of T holds
    x in Der A iff
      for U being open Subset of T st x in U
       ex y being Point of T st y in A /\ U & x <> y
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        x be Point of T;
    hereby assume x in Der A; then
A1:   x is_an_accumulation_point_of A by Th0;
      let U be open Subset of T;
      assume
A3:   x in U;
      x in Cl (A \ {x}) by A1,AcuDef; then
      A \ {x} meets U by A3,PRE_TOPC:54; then
      consider y being set such that
A4:   y in A \ {x} & y in U by XBOOLE_0:3;
      reconsider y as Point of T by A4;
      take y;
      y in A & y <> x by A4,ZFMISC_1:64;
      hence y in A /\ U & x <> y by A4,XBOOLE_0:def 3;
    end;
    assume
Z0: for U being open Subset of T st x in U
     ex y being Point of T st y in A /\ U & x <> y;
    for G being Subset of T st G is open holds
    x in G implies A \ {x} meets G
    proof
      let G be Subset of T;
      assume
Z1:   G is open;
      assume x in G; then
      consider y being Point of T such that
Z2:   y in A /\ G & x <> y by Z0,Z1;
Z3:   y in G by Z2,XBOOLE_0:def 3;
      y in A by Z2, XBOOLE_0:def 3; then
      y in A \ {x} by Z2,ZFMISC_1:64;
      hence thesis by Z3,XBOOLE_0:3;
    end; then
    x in Cl (A \ {x}) by PRE_TOPC:54; then
    x is_an_accumulation_point_of A by AcuDef;
    hence thesis by Th0;
  end;

theorem
  for T being non empty TopSpace,
      A being Subset of T,
      x being Point of T holds
    x in Der A iff
      for B being Basis of x,
          U being Subset of T st U in B
       ex y being Point of T st y in A /\ U & x <> y
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        x be Point of T;
    hereby assume x in Der A; then
A1:   x is_an_accumulation_point_of A by Th0;
      let B be Basis of x,
          U be Subset of T;
      assume
A3:   U in B;
A2:   x in Cl (A \ {x}) by A1,AcuDef;
      U is open & x in U by A3,YELLOW_8:21; then
      A \ {x} meets U by A2,PRE_TOPC:54; then
      consider y being set such that
A4:   y in A \ {x} & y in U by XBOOLE_0:3;
      reconsider y as Point of T by A4;
      take y;
      y in A & y <> x by A4,ZFMISC_1:64;
      hence y in A /\ U & x <> y by A4,XBOOLE_0:def 3;
    end;
    assume
Z0: for B being Basis of x, U being Subset of T st U in B
     ex y being Point of T st y in A /\ U & x <> y;
    for G being Subset of T st G is open holds
    x in G implies A \ {x} meets G
    proof
      let G be Subset of T;
      assume
Z1:   G is open;
      consider B being Basis of x;
      assume x in G; then
      consider V being Subset of T such that
ZZ:   V in B & V c= G by Z1,YELLOW_8:22;
      consider y being Point of T such that
ZA:   y in A /\ V & x <> y by Z0,ZZ;
      A /\ V c= A /\ G by ZZ,XBOOLE_1:26; then
      consider y being Point of T such that
Z2:   y in A /\ G & x <> y by ZA;
Z3:   y in G by Z2,XBOOLE_0:def 3;
      y in A by Z2, XBOOLE_0:def 3; then
      y in A \ {x} by Z2,ZFMISC_1:64;
      hence thesis by Z3,XBOOLE_0:3;
    end; then
    x in Cl (A \ {x}) by PRE_TOPC:54; then
    x is_an_accumulation_point_of A by AcuDef;
    hence thesis by Th0;
  end;

theorem
  for T being non empty TopSpace,
      A being Subset of T,
      x being Point of T holds
    x in Der A iff
      ex B being Basis of x st
        for U being Subset of T st U in B
         ex y being Point of T st y in A /\ U & x <> y
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        x be Point of T;
    hereby assume x in Der A; then
A1:   x is_an_accumulation_point_of A by Th0;
      consider B being Basis of x;
      take B;
      let U be Subset of T;
      assume
A3:   U in B;
A2:   x in Cl (A \ {x}) by A1,AcuDef;
      U is open & x in U by A3,YELLOW_8:21; then
      A \ {x} meets U by A2,PRE_TOPC:54; then
      consider y being set such that
A4:   y in A \ {x} & y in U by XBOOLE_0:3;
      reconsider y as Point of T by A4;
      take y;
      y in A & y <> x by A4,ZFMISC_1:64;
      hence y in A /\ U & x <> y by A4,XBOOLE_0:def 3;
    end;
    given B being Basis of x such that
Z0: for U being Subset of T st U in B
     ex y being Point of T st y in A /\ U & x <> y;
    for G being Subset of T st G is open holds
    x in G implies A \ {x} meets G
    proof
      let G be Subset of T;
      assume
Z1:   G is open;
      assume x in G; then
      consider V being Subset of T such that
ZZ:   V in B & V c= G by Z1,YELLOW_8:22;
      consider y being Point of T such that
ZA:   y in A /\ V & x <> y by Z0,ZZ;
      A /\ V c= A /\ G by ZZ,XBOOLE_1:26; then
      consider y being Point of T such that
Z2:   y in A /\ G & x <> y by ZA;
Z3:   y in G by Z2,XBOOLE_0:def 3;
      y in A by Z2, XBOOLE_0:def 3; then
      y in A \ {x} by Z2,ZFMISC_1:64;
      hence thesis by Z3,XBOOLE_0:3;
    end; then
    x in Cl (A \ {x}) by PRE_TOPC:54; then
    x is_an_accumulation_point_of A by AcuDef;
    hence thesis by Th0;
  end;

begin :: 1.3.3. Isolated Points

definition let T be TopSpace,
               A be Subset of T,
               x be set;
  pred x is_isolated_in A means :IsoAcu:
    x in A & not x is_an_accumulation_point_of A;
end;

theorem
  for T being non empty TopSpace,
      A being Subset of T,
      p being set holds
    p in A \ Der A iff p is_isolated_in A
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        p be set;
    hereby
      assume p in A \ Der A; then
A1:   p in A & not p in Der A by XBOOLE_0:def 4; then
      not p is_an_accumulation_point_of A by Th0;
      hence p is_isolated_in A by A1,IsoAcu;
    end;
    assume p is_isolated_in A; then
A2: p in A & not p is_an_accumulation_point_of A by IsoAcu; then
    not p in Der A by Th0;
    hence thesis by A2,XBOOLE_0:def 4;
  end;

theorem Cashmir:
  for T being non empty TopSpace,
      A being Subset of T,
      p being Point of T holds
    p is_an_accumulation_point_of A iff
      for U being open Subset of T st p in U
       ex q being Point of T st q <> p & q in A & q in U
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        p be Point of T;
    hereby assume
      p is_an_accumulation_point_of A; then
A1:   p in Der A by Th0;
      let U be open Subset of T;
      assume p in U; then
      consider q being Point of T such that
A2:   q in A /\ U & p <> q by A1,Th1;
      take q;
      thus q <> p & q in A & q in U by A2,XBOOLE_0:def 3;
    end;
    assume
B1: for U being open Subset of T st p in U
     ex q being Point of T st q <> p & q in A & q in U;
    for U being open Subset of T st p in U
     ex y being Point of T st y in A /\ U & p <> y
    proof
      let U be open Subset of T;
      assume p in U; then
      consider q being Point of T such that
B3:   q <> p & q in A & q in U by B1;
      take q;
      thus thesis by B3,XBOOLE_0:def 3;
    end; then
    p in Der A by Th1;
    hence thesis by Th0;
  end;

theorem ThIso:
  for T being non empty TopSpace,
      A being Subset of T,
      p being Point of T holds
    p is_isolated_in A iff
      ex G being open Subset of T st G /\ A = {p}
  proof
    let T be non empty TopSpace,
        A be Subset of T,
        p be Point of T;
    hereby
      assume p is_isolated_in A; then
A0:   p in A & not p is_an_accumulation_point_of A by IsoAcu; then
      consider U being open Subset of T such that
A1:   p in U & not ex q being Point of T st q <> p & q in A & q in U
        by Cashmir;
      take U;
      U /\ A = {p}
      proof
        thus U /\ A c= {p}
        proof
          let x be set;
          assume
a1:       x in U /\ A; then
AA:       x in U & x in A by XBOOLE_0:def 3;
          reconsider x' = x as Point of T by a1;
          x = p by AA,A1;
          hence thesis by TARSKI:def 1;
        end;
        let x be set;
        assume x in {p}; then
        x = p by TARSKI:def 1;
        hence thesis by A0,A1,XBOOLE_0:def 3;
      end;
      hence U /\ A = {p};
    end;
    given G being open Subset of T such that
B1: G /\ A = {p};
B3: p in G /\ A by B1,TARSKI:def 1; then
B2: p in A by XBOOLE_0:def 3;
    ex U being open Subset of T st
    p in U & not ex q being Point of T st q <> p & q in A & q in U
    proof
      take G;
      for q being Point of T holds q = p or not q in A or not q in G
      proof
        given q being Point of T such that
C2:     q <> p & q in A & q in G;
        q in A /\ G by C2,XBOOLE_0:def 3;
        hence thesis by C2,B1,TARSKI:def 1;
      end;
      hence thesis by B3,XBOOLE_0:def 3;
    end; then
    not p is_an_accumulation_point_of A by Cashmir;
    hence thesis by B2,IsoAcu;
  end;

definition let T be TopSpace,
               p be Point of T;
  attr p is isolated means :IsolDef:
    p is_isolated_in [#]T;
end;

theorem
  for T being non empty TopSpace,
      p being Point of T holds
    p is isolated iff {p} is open
  proof
    let T be non empty TopSpace,
        p be Point of T;
    hereby
      assume p is isolated; then
      p is_isolated_in [#]T by IsolDef; then
      consider G being open Subset of T such that
A1:   G /\ [#]T = {p} by ThIso;
      thus {p} is open by PRE_TOPC:15,A1;
    end;
    assume
B1: {p} is open;
    {p} /\ [#]T = {p} by PRE_TOPC:15; then
    p is_isolated_in [#]T by B1,ThIso;
    hence thesis by IsolDef;
  end;

begin :: Derivative of a Subset-Family

definition let T be TopSpace,
               F be Subset-Family of T;
  func Der F -> Subset-Family of T means :ColDer:
    for A being Subset of T holds A in it iff
      ex B being Subset of T st A = Der B & B in F;
  existence
  proof
    defpred P[set] means
    ex W being Subset of T st $1 = Der W & W in F;
    thus ex S be Subset-Family of T st
    for Z being Subset of T holds Z in S iff P[Z] from PRE_TOPC:sch 1;
  end;
  uniqueness
  proof
    let H, G be Subset-Family of T;
    defpred P[set] means
    ex W being Subset of T st $1 = Der W & W in F;
    assume that
A1: for Z being Subset of T holds Z in H iff P[Z] and
A2: for X being Subset of T holds X in G iff P[X];
    H = G
    proof
      now
        let Z be set;
        assume
A3:     Z in H;
        then reconsider Z'=Z as Subset of T;
        P[Z] by A1,A3;
        hence Z in G by A2;
      end; then
A4:   H c= G by TARSKI:def 3;
      now
        let X be set;
        assume
A5:     X in G;
        then reconsider X' = X as Subset of T;
        P[X'] by A2,A5;
        hence X in H by A1;
      end;
      then G c= H by TARSKI:def 3;
      hence thesis by A4,XBOOLE_0:def 10;
    end;
    hence thesis;
  end;
end;

 reserve T for non empty TopSpace,
         A, B for Subset of T,
         F, G for Subset-Family of T,
         x for set,
         p for Point of T;

theorem
  F = {} implies Der F = {}
  proof
    assume
A0: F = {};
    assume Der F <> {}; then
    consider x being set such that
A1: x in Der F by XBOOLE_0:def 1;
    consider B being Subset of T such that
A2: x = Der B & B in F by A1,ColDer;
    thus thesis by A2,A0;
  end;

theorem
  F = { A } implies Der F = { Der A }
  proof
    assume
A1: F = { A }; then
B1: A in F by TARSKI:def 1;
    thus Der F c= { Der A }
    proof
      let x be set;
      assume
C1:   x in Der F; then
      reconsider B = x as Subset of T;
      consider C being Subset of T such that
C2:   B = Der C & C in F by C1,ColDer;
      C = A by C2,A1,TARSKI:def 1;
      hence thesis by C2,TARSKI:def 1;
    end;
    let x be set;
    assume
D1: x in { Der A }; then
A2: x = Der A by TARSKI:def 1;
    reconsider B = x as Subset of T by D1,TARSKI:def 1;
    thus thesis by A2,B1,ColDer;
  end;

theorem U21:
  F c= G implies Der F c= Der G
  proof
    assume
A0: F c= G;
    Der F c= Der G
    proof
      let x be set;
      assume
A1:   x in Der F; then
      reconsider A = x as Subset of T;
      consider B being Subset of T such that
A2:   A = Der B & B in F by A1,ColDer;
      thus x in Der G by A0,A2,ColDer;
    end;
    hence thesis;
  end;

theorem
  Der (F \/ G) = Der F \/ Der G
  proof
    thus Der (F \/ G) c= Der F \/ Der G
    proof
      let x be set;
      assume
A1:   x in Der (F \/ G); then
      reconsider A = x as Subset of T;
      consider B being Subset of T such that
A2:   A = Der B & B in F \/ G by A1,ColDer;
      per cases by A2,XBOOLE_0:def 2;
      suppose B in F; then
        A in Der F by A2,ColDer;
        hence thesis by XBOOLE_0:def 2;
      end;
      suppose B in G; then
        A in Der G by A2,ColDer;
        hence thesis by XBOOLE_0:def 2;
      end;
    end;
    F c= F \/ G by XBOOLE_1:7; then
B1: Der F c= Der (F \/ G) by U21;
    G c= F \/ G by XBOOLE_1:7; then
    Der G c= Der (F \/ G) by U21;
    hence Der F \/ Der G c= Der (F \/ G) by B1,XBOOLE_1:8;
  end;

:: 1.3.4. Properties of a Derivative of a Set

theorem Lem1:
  for T being non empty TopSpace,
      A being Subset of T holds
    Der A c= Cl A
  proof
    let T be non empty TopSpace,
        A be Subset of T;
    let x be set;
    assume
A1: x in Der A;
    reconsider x' = x as Point of T by A1;
    for G being Subset of T st G is open holds
    x' in G implies A meets G
    proof
      let G be Subset of T;
      assume
A4:   G is open;
      assume x' in G; then
      consider y being Point of T such that
A3:   y in A /\ G & x <> y by A1,A4,Th1;
      thus thesis by A3,XBOOLE_0:4;
    end;
    hence thesis by PRE_TOPC:54;
  end;

theorem Th134a: :: Theorem 1.3.4. (a)
  for T being TopSpace,
      A being Subset of T holds
    Cl A = A \/ Der A
  proof
    let T be TopSpace,
        A be Subset of T;
    per cases;
      suppose
V1:   T is non empty;
      thus Cl A c= A \/ Der A
      proof
        let x be set;
        assume
T1:     x in Cl A; then
        reconsider x' = x as Point of T;
        per cases;
        suppose x in A;
          hence thesis by XBOOLE_0:def 2;
        end;
        suppose
A1:       not x in A;
          for U being open Subset of T st x' in U
           ex y being Point of T st y in A /\ U & x' <> y
          proof
            let U be open Subset of T;
            assume x' in U; then
            A meets U by T1,PRE_TOPC:54; then
            consider y being set such that
Z2:         y in A & y in U by XBOOLE_0:3;
            reconsider y as Point of T by Z2;
            take y;
            thus thesis by A1,Z2,XBOOLE_0:def 3;
          end; then
          x' in Der A by Th1,V1;
          hence thesis by XBOOLE_0:def 2;
        end;
      end;
      let x be set;
      assume
B1:   x in A \/ Der A;
BB:   Der A c= Cl A by Lem1,V1;
      per cases by B1,XBOOLE_0:def 2;
      suppose
B2:     x in A;
        A c= Cl A by PRE_TOPC:48;
        hence thesis by B2;
      end;
      suppose x in Der A;
        hence thesis by BB;
      end;
    end;
    suppose T0: T is empty;
      Cl A = {} by T0,LemPos
          .= {} \/ {}
          .= A \/ Der A by T0,LemPos;
      hence thesis;
    end;
  end;

theorem Th134b: :: Theorem 1.3.4. (b)
  for T being non empty TopSpace,
      A, B being Subset of T st
    A c= B holds Der A c= Der B
  proof
    let T be non empty TopSpace,
        A, B be Subset of T;
    assume
A0: A c= B;
    let x be set;
    assume
AA: x in Der A; then
    reconsider x' = x as Point of T;
    for U being open Subset of T st x' in U
     ex y being Point of T st y in B /\ U & x' <> y
    proof
      let U be open Subset of T;
      assume x' in U; then
      consider y being Point of T such that
A2:   y in A /\ U & x' <> y by AA,Th1;
      A /\ U c= B /\ U by A0,XBOOLE_1:26;
      hence thesis by A2;
    end;
    hence thesis by Th1;
  end;

theorem Th134c: :: Theorem 1.3.4. (c)
  for T being non empty TopSpace,
      A, B being Subset of T holds
    Der (A \/ B) = Der A \/ Der B
  proof
    let T be non empty TopSpace,
        A, B be Subset of T;
    thus Der (A \/ B) c= Der A \/ Der B
    proof
      let x be set;
      assume x in Der (A \/ B); then
      x is_an_accumulation_point_of A \/ B by Th0; then
P1:   x in Cl ((A \/ B) \ {x}) by AcuDef;
      (A \/ B) \ {x} = (A \ {x}) \/ (B \ {x}) by XBOOLE_1:42; then
      Cl ((A \/ B) \ {x}) = Cl (A \ {x}) \/ Cl (B \ {x}) by PRE_TOPC:50; then
      x in Cl (A \ {x}) or x in Cl (B \ {x}) by P1,XBOOLE_0:def 2; then
      x is_an_accumulation_point_of A or
        x is_an_accumulation_point_of B by AcuDef; then
      x in Der A or x in Der B by Th0;
      hence thesis by XBOOLE_0:def 2;
    end;
    let x be set;
    assume x in Der A \/ Der B; then
    x in Der A or x in Der B by XBOOLE_0:def 2; then
xx: x is_an_accumulation_point_of A or
      x is_an_accumulation_point_of B by Th0;
    x in Cl ((A \/ B) \ {x})
    proof
      A c= A \/ B by XBOOLE_1:7; then
      A \ {x} c= (A \/ B) \ {x} by XBOOLE_1:33; then
XY:   Cl (A \ {x}) c= Cl ((A \/ B) \ {x}) by PRE_TOPC:49;
      B c= A \/ B by XBOOLE_1:7; then
      B \ {x} c= (A \/ B) \ {x} by XBOOLE_1:33; then
xy:   Cl (B \ {x}) c= Cl ((A \/ B) \ {x}) by PRE_TOPC:49;
      per cases by xx,AcuDef;
      suppose x in Cl (A \ {x});
        hence thesis by XY;
      end;
      suppose x in Cl (B \ {x});
        hence thesis by xy;
      end;
    end; then
    x is_an_accumulation_point_of A \/ B by AcuDef;
    hence thesis by Th0;
  end;

theorem Add:
  for T being non empty TopSpace,
      A being Subset of T st T is being_T1 holds
    Der Der A c= Der A
  proof
    let T be non empty TopSpace,
        A be Subset of T;
    assume
XX: T is being_T1;
    let x be set;
    assume
A0: x in Der Der A; then
    reconsider x' = x as Point of T;
    assume
DD: not x in Der A;
    consider G being open Subset of T such that
D1: x' in G & not ex y being Point of T st y in A /\ G & x' <> y by DD,Th1;
    consider y being Point of T such that
H1: y in Der A /\ G & x <> y by D1,Th1,A0;
H2: y in Der A & y in G by H1,XBOOLE_0:def 3;
H3: y in Der A \ {x} by ZFMISC_1:64,H1,H2;
f1: G misses A \ {x}
    proof
      assume G meets A \ {x}; then
      consider g being set such that
R1:   g in G & g in A \ {x} by XBOOLE_0:3;
      g in A by R1,XBOOLE_0:def 4; then
      g in A /\ G by R1,XBOOLE_0:def 3; then
      x' = g by D1;
      hence thesis by R1,ZFMISC_1:64;
    end;
    consider q being set such that
A1: q in G & q in Der A \ {x} by H2,H3;
    reconsider q as Point of T by A1;
A2: not q in {x} by A1,XBOOLE_0:def 4;
J1: q in Der A by A1,XBOOLE_0:def 4;
TA: q in G \ {x} by A1,A2,XBOOLE_0:def 4;
    Cl {x'} = {x'} by XX,FRECHET2:47; then
J2: G \ {x'} is open by FRECHET:4;
    set U = G \ {x'};
    consider y being Point of T such that
J3: y in A /\ U & q <> y by Th1,J1,TA,J2;
J4: y in A & y in U by J3,XBOOLE_0:def 3; then
    y in A \ {q} by J3,ZFMISC_1:64; then
    consider f being set such that
Z1: f in G \ {x'} & f in A \ {q} by J4;
Z2: f in G & f <> x' by Z1,ZFMISC_1:64;
    f in A by Z1,ZFMISC_1:64; then
    f in A \ {x'} by Z2,ZFMISC_1:64;
    hence thesis by f1,Z2,XBOOLE_0:3;
  end;

theorem Adda:
  for T being TopSpace,
      A being Subset of T st T is being_T1 holds
    Cl Der A = Der A
  proof
    let T be TopSpace,
        A be Subset of T;
    assume
XX: T is being_T1;
    per cases;
    suppose
YY:   T is non empty;
X1:   Cl Der A = Der A \/ Der Der A by Th134a;
      Der Der A c= Der A by YY,Add,XX; then
X2:   Cl Der A c= Der A \/ Der A by X1,XBOOLE_1:9;
      Der A c= Cl Der A by PRE_TOPC:48;
      hence thesis by XBOOLE_0:def 10,X2;
    end;
    suppose
U0:   T is empty; then
      Cl Der A = {} by LemPos;
      hence thesis by LemPos,U0;
    end;
  end;

registration let T be being_T1 non empty TopSpace,
                 A be Subset of T;
  cluster Der A -> closed;
  coherence
  proof
    Der A = Cl Der A by Adda;
    hence thesis;
  end;
end;

theorem Th134d: :: Theorem 1.3.4. d)
  for T being non empty TopSpace,
      F being Subset-Family of T holds
    union Der F c= Der union F
  proof
    let T be non empty TopSpace,
        F be Subset-Family of T;
    let x be set;
    assume x in union Der F; then
    consider Y being set such that
A1: x in Y & Y in Der F by TARSKI:def 4;
    reconsider Y as Subset of T by A1;
    consider B being Subset of T such that
A2: Y = Der B & B in F by A1,ColDer;
    B c= union F by ZFMISC_1:92,A2; then
    Der B c= Der union F by Th134b;
    hence thesis by A1,A2;
  end;

theorem
  A c= B & x is_an_accumulation_point_of A implies
    x is_an_accumulation_point_of B
  proof
    assume A c= B; then
A1: Der A c= Der B by Th134b;
    assume x is_an_accumulation_point_of A; then
    x in Der A by Th0;
    hence thesis by Th0,A1;
  end;

begin :: 1.3.4. Density-in-itself

definition let T be TopSpace,
               A be Subset of T;
  attr A is dense-in-itself means :Def3:
    A c= Der A;
end;

definition let T be non empty TopSpace;
  attr T is dense-in-itself means
    [#]T is dense-in-itself;
end;

theorem ClDenI:
  T is being_T1 & A is dense-in-itself implies
    Cl A is dense-in-itself
  proof
    assume
X1: T is being_T1;
    assume A is dense-in-itself; then
    A c= Der A by Def3; then
A1: Der A = A \/ Der A by XBOOLE_1:12
         .= Cl A by Th134a;
A2: Der Der A c= Der A by X1,Add;
    Der Cl A = Der (A \/ Der A) by Th134a
            .= Der A \/ Der Der A by Th134c
            .= Cl A by A1,A2,XBOOLE_1:12;
    hence thesis by Def3;
  end;

definition let T be TopSpace,
               F be Subset-Family of T;
  attr F is dense-in-itself means :ColDenI:
    for A being Subset of T st A in F holds
      A is dense-in-itself;
end;

theorem LemUniDer:
  for F being Subset-Family of T st
      F is dense-in-itself holds
    union F c= union Der F
  proof
    let F be Subset-Family of T;
    assume
A0: F is dense-in-itself;
    thus union F c= union Der F
    proof
      let x be set;
      assume x in union F; then
      consider A being set such that
A1:   x in A & A in F by TARSKI:def 4;
      reconsider A as Subset of T by A1;
      A is dense-in-itself by ColDenI,A0,A1; then
a2:   A c= Der A by Def3;
      Der A in Der F by ColDer,A1;
      hence thesis by a2,TARSKI:def 4,A1;
    end;
  end;

theorem Clos1:
  F is dense-in-itself implies
    union F is dense-in-itself
  proof
    assume F is dense-in-itself; then
A2: union F c= union Der F by LemUniDer;
    union Der F c= Der union F by Th134d; then
    union F c= Der union F by A2,XBOOLE_1:1;
    hence thesis by Def3;
  end;

theorem
  Fr {}T = {}
  proof
    Fr {}T = Cl {}T /\ Cl ({}T)` by TOPS_1:def 2
          .= {} /\ Cl ({}T)`;
    hence thesis;
  end;

registration let T be TopSpace,
                 A be open closed Subset of T;
  cluster Fr A -> empty;
  coherence by Th132k;
end;

registration
  let T be non empty non discrete TopSpace;
  cluster non open Subset of T;
  existence by TDLAT_3:17;
  cluster non closed Subset of T;
  existence by TDLAT_3:18;
end;

registration let T be non empty non discrete TopSpace,
                 A be non open Subset of T;
  cluster Fr A -> non empty;
  coherence by Th132k;
end;

registration let T be non empty non discrete TopSpace,
                 A be non closed Subset of T;
  cluster Fr A -> non empty;
  coherence by Th132k;
end;

begin :: Perfect Sets

definition let T be TopSpace,
               A be Subset of T;
  attr A is perfect means :DefPer:
    A is closed dense-in-itself;
end;

registration let T be TopSpace;
  cluster perfect -> closed dense-in-itself Subset of T;
  coherence by DefPer;
  cluster closed dense-in-itself -> perfect Subset of T;
  coherence by DefPer;
end;

theorem Czy:
  for T being TopSpace holds
    Der {}T = {}T
  proof
    let T be TopSpace;
    per cases;
    suppose T is non empty; then
      reconsider T' = T as non empty TopSpace;
      Cl {}T' is empty; then
      {} = Cl {}T
        .= {}T \/ Der {}T by Th134a
        .= Der {}T;
      hence thesis;
    end;
    suppose T is empty;
      hence thesis by LemPos;
    end;
  end;

ThDenDer:
  for T being TopSpace,
      A being Subset of T st A is closed dense-in-itself holds
    Der A = A
  proof
    let T be TopSpace,
        A be Subset of T;
    assume
A1: A is closed dense-in-itself; then
    A = Cl A by PRE_TOPC:52
     .= Der A \/ A by Th134a;
    hence Der A c= A by XBOOLE_1:7;
    thus A c= Der A by A1,Def3;
  end;

theorem Czy1:
  for T being TopSpace,
      A being Subset of T holds
    A is perfect iff Der A = A
  proof
    let T be TopSpace,
        A be Subset of T;
    hereby
      assume A is perfect; then
      A is dense-in-itself closed by DefPer;
      hence Der A = A by ThDenDer;
    end;
    assume
A0: Der A = A; then
A1: A is dense-in-itself by Def3;
    Cl A = Der A \/ A by Th134a
        .= A by A0;
    hence thesis by A1,DefPer;
  end;

theorem Czy2:
  for T being TopSpace holds
    {}T is perfect
  proof
    let T be TopSpace;
    Der {}T = {}T by Czy;
    hence thesis by Czy1;
  end;

registration let T be TopSpace;
  cluster empty -> perfect Subset of T;
  coherence
  proof
    let A be Subset of T;
    assume A is empty; then
    A = {}T;
    hence thesis by Czy2;
  end;
end;

registration let T be TopSpace;
  cluster perfect Subset of T;
  existence
  proof
    take {}T;
    thus thesis;
  end;
end;

begin :: Scattered Subsets

definition let T be TopSpace,
               A be Subset of T;
  attr A is scattered means :ScatDef:
    not ex B being Subset of T st
      B is non empty & B c= A & B is dense-in-itself;
end;

registration let T be non empty TopSpace;
  cluster non empty scattered -> non dense-in-itself Subset of T;
  coherence by ScatDef;
  cluster dense-in-itself non empty -> non scattered Subset of T;
  coherence by ScatDef;
end;

theorem EScat:
  for T being TopSpace holds
    {}T is scattered
  proof
    let T be TopSpace;
    {}T is scattered
    proof
      assume {}T is not scattered; then
      consider B being Subset of T such that
A1:   B is non empty & B c= {}T & B is dense-in-itself by ScatDef;
      thus thesis by A1,XBOOLE_1:3;
    end;
    hence thesis;
  end;

registration let T be TopSpace;
  cluster empty -> scattered Subset of T;
  coherence
  proof
    let A be Subset of T;
    assume A is empty; then
    A = {}T;
    hence thesis by EScat;
  end;
end;

theorem
  for T being non empty TopSpace st T is being_T1 holds
    ex A, B being Subset of T st
      A \/ B = [#]T & A misses B & A is perfect & B is scattered
  proof
    let T be non empty TopSpace;
    assume
A0: T is being_T1;
    defpred P[Subset of T] means $1 is dense-in-itself;
    consider CC being Subset-Family of T such that
A1: for A being Subset of T holds A in CC iff P[A] from PRE_TOPC:sch 1;
    set C = union CC;
    CC is dense-in-itself by A1,ColDenI; then
B2: C is dense-in-itself by Clos1; then
    Cl C is dense-in-itself by A0,ClDenI; then
    Cl C in CC by A1; then
BB: Cl C c= C by ZFMISC_1:92;
    C c= Cl C by PRE_TOPC:48; then
    Cl C = C by BB,XBOOLE_0:def 10; then
B1: C is perfect by B2,DefPer;
    not ex B being Subset of T st
      B is non empty & B c= C` & B is dense-in-itself
    proof
      given B being Subset of T such that
C1:   B is non empty & B c= C` & B is dense-in-itself;
C2:   B misses union CC by C1,SUBSET_1:43;
      B in CC by C1,A1; then
      B c= union CC by ZFMISC_1:92;
      hence thesis by C1,C2,XBOOLE_1:69;
    end; then
B2: C` is scattered by ScatDef;
B3: [#]T = C \/ C` by PRE_TOPC:18;
    C misses C` by SUBSET_1:26;
    hence thesis by B1,B2,B3;
  end;

registration let T be discrete TopSpace,
                 A be Subset of T;
  cluster Fr A -> empty;
  coherence
  proof
    A is open closed by TDLAT_3:18,17;
    hence thesis by Th132k;
  end;
end;

registration let T be discrete TopSpace;
  cluster -> closed open Subset of T;
  coherence by TDLAT_3:18,17;
end;

theorem Der0:
  for T being discrete TopSpace,
      A being Subset of T holds
    Der A = {}
  proof
    let T be discrete TopSpace,
        A be Subset of T;
    per cases;
    suppose
A2:   T is non empty;
      assume Der A <> {}; then
      consider x being set such that
A1:   x in Der A by XBOOLE_0:def 1;
      x is_an_accumulation_point_of A by A2,A1,Th0; then
      x in Cl (A \ {x}) by AcuDef; then
      x in A \ {x} by PRE_TOPC:52;
      hence thesis by ZFMISC_1:64;
    end;
    suppose T is empty;
      hence thesis by LemPos;
    end;
  end;

registration let T be discrete non empty TopSpace,
                 A be Subset of T;
  cluster Der A -> empty;
  coherence by Der0;
end;

theorem
  for T being discrete non empty TopSpace,
      A being Subset of T st
    A is dense holds A = [#]T
  proof
    let T be discrete non empty TopSpace,
        A be Subset of T;
    assume
A1: A is dense;
    A = the carrier of T by A1,TOPS_3:19;
    hence thesis by PRE_TOPC:def 3;
  end;

begin :: Density of a Topological Space and Separable Spaces

:: 1.3.6. Density of a Topological Space

definition let T be TopSpace;
  func density T -> cardinal number means :DenDef:
    (ex A being Subset of T st A is dense & it = Card A) &
    for B being Subset of T st B is dense holds it <=` Card B;
  existence
  proof
    defpred P[ordinal number] means
    ex A being Subset of T st A is dense & $1 = Card A;
    set B0 = [#]T;
A1: B0 is dense by TOPS_3:16;
    Card B0 is ordinal; then
A2: ex A being ordinal number st P[A] by A1;
    consider A being ordinal number such that
A3: P[A] and
A4: for B being ordinal number st P[B] holds A c= B
      from ORDINAL1:sch 1(A2);
    consider B1 being Subset of T such that
A5: B1 is dense & A = Card B1 by A3;
    take Card B1;
    thus thesis by A4,A5;
  end;
  uniqueness
  proof
    let M1, M2 be cardinal number;
    assume
    (ex B being Subset of T st B is dense & M1 = Card B) &
    (for B being Subset of T st B is dense holds M1 <=` Card B) &
    (ex B being Subset of T st B is dense & M2 = Card B) &
    for B being Subset of T st B is dense holds M2 <=` Card B;
    hence M1 c= M2 & M2 c= M1;
  end;
end;

:: Separable Spaces

definition let T be TopSpace;
  attr T is separable means :OsrDef:
    density T <=` alef 0;
end;

theorem ThSep:
  for T being countable TopSpace holds
    T is separable
  proof
    let T be countable TopSpace;
A1: [#]T is dense by TOPS_3:16;
A2: Card [#]T <=` alef 0 by CARD_4:def 1;
    density T <=` Card [#]T by A1,DenDef; then
    density T <=` alef 0 by A2,XBOOLE_1:1;
    hence thesis by OsrDef;
  end;

registration
  cluster countable -> separable TopSpace;
  coherence by ThSep;
end;

begin :: Exercise 1.3.E.

theorem
  for A being Subset of R^1 st A = RAT holds A` = IRRAT
    by BORSUK_5:def 3,BORSUK_5:13,PRE_TOPC:17;

Lem3: RAT = REAL \ IRRAT
  proof
    REAL \ IRRAT = REAL /\ RAT by XBOOLE_1:48,BORSUK_5:def 3;
    hence thesis by NUMBERS:12,XBOOLE_1:28;
  end;

theorem
  for A being Subset of R^1 st A = IRRAT holds A` = RAT
    by Lem3,BORSUK_5:13,PRE_TOPC:17;

theorem
  for A being Subset of R^1 st A = RAT holds
    Int A = {}
  proof
    let A be Subset of R^1;
    assume A = RAT; then
    A` = IRRAT by BORSUK_5:def 3,BORSUK_5:13,PRE_TOPC:17; then
    Cl A` = the carrier of R^1 by BORSUK_5:51
         .= [#]R^1 by PRE_TOPC:def 3; then
    (Cl A`)` = {}R^1 by TOPS_1:8;
    hence thesis by Pik1;
  end;

theorem
  for A being Subset of R^1 st A = IRRAT holds
    Int A = {}
  proof
    let A be Subset of R^1;
    assume A = IRRAT; then
    A` = RAT by Lem3,BORSUK_5:13,PRE_TOPC:17; then
    Cl A` = the carrier of R^1 by BORSUK_5:35
         .= [#]R^1 by PRE_TOPC:def 3; then
    (Cl A`)` = {}R^1 by TOPS_1:8;
    hence thesis by Pik1;
  end;

theorem Kil1:
  for A being Subset of R^1 st A = RAT holds
    A is dense
  proof
    let A be Subset of R^1;
    assume A = RAT; then
    Cl A = the carrier of R^1 by BORSUK_5:35;
    hence thesis by TOPS_3:def 2;
  end;

theorem Kil5:
  for A being Subset of R^1 st A = IRRAT holds
    A is dense
  proof
    let A be Subset of R^1;
    assume A = IRRAT; then
    Cl A = the carrier of R^1 by BORSUK_5:51;
    hence thesis by TOPS_3:def 2;
  end;

theorem Kil3:
  for A being Subset of R^1 st A = RAT holds
    A is boundary
  proof
    let A be Subset of R^1;
    assume
A1: A = RAT;
    A` = IRRAT by A1,BORSUK_5:def 3,BORSUK_5:13,PRE_TOPC:17; then
    A` is dense by Kil5;
    hence thesis by TOPS_1:def 4;
  end;

theorem Kil2:
  for A being Subset of R^1 st A = IRRAT holds
    A is boundary
  proof
    let A be Subset of R^1;
    assume
A1: A = IRRAT;
    A` = RAT by Lem3,A1,BORSUK_5:13,PRE_TOPC:17; then
    A` is dense by Kil1;
    hence thesis by TOPS_1:def 4;
  end;

theorem Kil4:
  for A being Subset of R^1 st A = REAL holds
    A is non boundary
  proof
    let A be Subset of R^1;
    assume
A1: A = REAL; then
    Int A = A by TOPS_1:43,BORSUK_5:13;
    hence thesis by A1,TOPS_3:def 1;
  end;

theorem
  ex A, B being Subset of R^1 st
     A is boundary & B is boundary & A \/ B is non boundary
  proof
    reconsider A = RAT as Subset of R^1 by NUMBERS:12,TOPMETR:24;
    reconsider B = IRRAT as Subset of R^1 by TOPMETR:24;
    take A,B;
    A \/ B = RAT \/ REAL by XBOOLE_1:39,BORSUK_5:def 3
          .= REAL by NUMBERS:12,XBOOLE_1:12;
    hence thesis by Kil3,Kil2,Kil4;
  end;
