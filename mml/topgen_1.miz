:: On the Boundary and Derivative of a Set
::  by Adam Grabowski
::
:: Received November 8, 2004
:: Copyright (c) 2004 Association of Mizar Users

environ

 vocabularies PRE_TOPC, SETFAM_1, SUBSET_1, BOOLE, TARSKI, FINSET_1, TOPGEN_1,
      TOPS_1, NATTRA_1, RLVECT_3, CARD_1, ARYTM, TOPMETR, ORDINAL1, RAT_1,
      BORSUK_5, CARD_4, ORDINAL2, COMPTS_1;
 notations TARSKI, XBOOLE_0, SUBSET_1, SETFAM_1, CARD_1, ORDINAL1, NUMBERS,
      CARD_3, FINSET_1, DOMAIN_1, STRUCT_0, PRE_TOPC, TOPS_1, TDLAT_3,
      YELLOW_8, URYSOHN1, TOPMETR, BORSUK_5, ORDERS_4;
 constructors SETFAM_1, CARD_3, TOPS_1, COMPTS_1, URYSOHN1, TDLAT_3, TOPMETR,
      YELLOW_8, BORSUK_5, ORDERS_4;
 registrations XBOOLE_0, SUBSET_1, NUMBERS, CARD_1, STRUCT_0, TOPS_1, TEX_1,
      TEX_4, YELLOW13, BORSUK_3;
 requirements BOOLE, SUBSET;
 definitions TARSKI, XBOOLE_0, SUBSET_1, STRUCT_0;
 theorems TARSKI, ZFMISC_1, PRE_TOPC, TOPS_1, XBOOLE_0, XBOOLE_1, SUBSET_1,
      TOPS_3, TDLAT_3, YELLOW_8, FRECHET, BORSUK_5, TOPMETR, NUMBERS, CARD_4,
      ORDERS_4, CARD_3;
 schemes ORDINAL1, SUBSET_1;

begin :: Preliminaries

Lm1: for T being empty TopSpace, A being Subset of T holds A = {};

theorem Th1:
  for T being 1-sorted,
  A, B being Subset of T holds A meets B` iff A \ B <> {}
proof
  let T be 1-sorted, A, B be Subset of T;
  hereby
    assume A meets B`;
    then A /\ B` <> {} by XBOOLE_0:def 7;
    hence A \ B <> {} by SUBSET_1:32;
  end;
  assume A \ B <> {};
  then A /\ B` <> {} by SUBSET_1:32;
  hence thesis by XBOOLE_0:def 7;
end;

canceled;

theorem
  for T being 1-sorted holds T is countable iff card [#]T c= omega
proof
  let T be 1-sorted;
  hereby
    assume T is countable;
    then [#]T is countable by ORDERS_4:def 2;
    hence card [#]T c= omega by CARD_3:def 15;
  end;
  assume card [#]T c= omega;
  then [#]T is countable by CARD_3:def 15;
  hence thesis by ORDERS_4:def 2;
end;

registration
  let T be finite 1-sorted;
  cluster [#]T -> finite;
  coherence;
end;

registration
  cluster finite -> countable 1-sorted;
  coherence
  proof
    let T be 1-sorted;
    assume T is finite;
    then the carrier of T is finite;
    then the carrier of T is countable by CARD_4:43;
    hence thesis by ORDERS_4:def 2;
  end;
end;

registration
  cluster countable non empty 1-sorted;
  existence
  proof
    consider T being finite non empty 1-sorted;
    take T;
    thus thesis;
  end;
  cluster countable non empty TopSpace;
  existence
  proof
    consider T being finite non empty TopSpace;
    take T;
    thus thesis;
  end;
end;

registration
  let T be countable 1-sorted;
  cluster [#]T -> countable;
  coherence by ORDERS_4:def 2;
end;

registration
  cluster T_1 non empty TopSpace;
  existence
  proof
    consider T being discrete non empty TopSpace;
    take T;
    thus thesis;
  end;
end;

begin

canceled;

theorem Th5:
  for T being TopSpace, A being Subset of T holds Int A = (Cl A`)`
proof
  let T be TopSpace, A be Subset of T;
  Int A = (Cl A```)` by TDLAT_3:4
    .= (Cl A`)`;
  hence thesis;
end;

:: Collective Boundary

definition
  let T be TopSpace, F be Subset-Family of T;
  func Fr F -> Subset-Family of T means
  :Def1:
  for A being Subset of T holds A in it iff
  ex B being Subset of T st A = Fr B & B in F;
  existence
  proof
    defpred P[set] means ex W being Subset of T st $1 = Fr W & W in F;
    thus ex S be Subset-Family of T st
    for Z being Subset of T holds Z in S iff P[Z] from SUBSET_1:sch 3;
  end;
  uniqueness
  proof
    let H, G be Subset-Family of T;
    defpred P[set] means ex W being Subset of T st $1 = Fr W & W in F;
    assume that
A1: for Z being Subset of T holds Z in H iff P[Z] and
A2: for X being Subset of T holds X in G iff P[X];
    H = G
    proof
      now
        let Z be set;
        assume
A3:     Z in H;
        P[Z] by A1,A3;
        hence Z in G by A2;
      end;
      then
A4:   H c= G by TARSKI:def 3;
      now
        let X be set;
        assume
A5:     X in G;
        then reconsider X'=X as Subset of T;
        P[X'] by A2,A5;
        hence X in H by A1;
      end;
      then G c= H by TARSKI:def 3;
      hence thesis by A4,XBOOLE_0:def 10;
    end;
    hence thesis;
  end;
end;

theorem
  for T being TopSpace, F being Subset-Family of T st F = {} holds Fr F = {}
proof
  let T be TopSpace, F be Subset-Family of T;
  assume
A1: F = {};
  assume Fr F <> {};
  then consider x being set such that
A2: x in Fr F by XBOOLE_0:def 1;
  consider B being Subset of T such that
A3: x = Fr B & B in F by A2,Def1;
  thus thesis by A1,A3;
end;

theorem
  for T being TopSpace, F being Subset-Family of T, A being Subset of T st
  F = { A } holds Fr F = { Fr A }
proof
  let T be TopSpace, F be Subset-Family of T, A be Subset of T;
  assume
A1: F = { A };
  then
A2: A in F by TARSKI:def 1;
  thus Fr F c= { Fr A }
  proof
    let x be set;
    assume
A3: x in Fr F;
    then reconsider B = x as Subset of T;
    consider C being Subset of T such that
A4: B = Fr C & C in F by A3,Def1;
    C = A by A1,A4,TARSKI:def 1;
    hence thesis by A4,TARSKI:def 1;
  end;
  let x be set;
  assume x in { Fr A };
  then x = Fr A by TARSKI:def 1;
  hence thesis by A2,Def1;
end;

theorem Th8:
  for T being TopSpace, F, G being Subset-Family of T st
  F c= G holds Fr F c= Fr G
proof
  let T be TopSpace, F, G be Subset-Family of T;
  assume
A1: F c= G;
  Fr F c= Fr G
  proof
    let x be set;
    assume
A2: x in Fr F;
    then reconsider A = x as Subset of T;
    consider B being Subset of T such that
A3: A = Fr B & B in F by A2,Def1;
    thus x in Fr G by A1,A3,Def1;
  end;
  hence thesis;
end;

theorem
  for T being TopSpace, F, G being Subset-Family of T holds
  Fr (F \/ G) = Fr F \/ Fr G
proof
  let T be TopSpace, F, G be Subset-Family of T;
  thus Fr (F \/ G) c= Fr F \/ Fr G
  proof
    let x be set;
    assume
A1: x in Fr (F \/ G);
    then reconsider A = x as Subset of T;
    consider B being Subset of T such that
A2: A = Fr B & B in F \/ G by A1,Def1;
    per cases by A2,XBOOLE_0:def 3;
    suppose B in F;
      then A in Fr F by A2,Def1;
      hence thesis by XBOOLE_0:def 3;
    end;
    suppose B in G;
      then A in Fr G by A2,Def1;
      hence thesis by XBOOLE_0:def 3;
    end;
  end;
A3: Fr F c= Fr (F \/ G) by Th8,XBOOLE_1:7;
  Fr G c= Fr (F \/ G) by Th8,XBOOLE_1:7;
  hence Fr F \/ Fr G c= Fr (F \/ G) by A3,XBOOLE_1:8;
end;

theorem Th10: :: 1.3.0.
  for T being TopStruct, A being Subset of T holds Fr A = Cl A \ Int A
proof
  let T be TopStruct, A be Subset of T;
  Fr A = Cl A /\ (Cl A`)`` by TOPS_1:def 2
    .= Cl A \ (Cl A`)` by SUBSET_1:32
    .= Cl A \ Int A by TOPS_1:def 1;
  hence thesis;
end;

:: 1.3.1. Characterization of Fr via basis

theorem Th11:
  for T being non empty TopSpace, A being Subset of T,
  p being Point of T holds p in Fr A iff
  (for U being Subset of T st U is open & p in U holds
  A meets U & U \ A <> {})
proof
  let T be non empty TopSpace, A be Subset of T, p be Point of T;
  hereby
    assume
A1: p in Fr A;
    let U be Subset of T;
    assume U is open & p in U;
    then A meets U & U meets A` by A1,TOPS_1:61;
    hence A meets U & U \ A <> {} by Th1;
  end;
  assume
A2: for U being Subset of T st U is open & p in U holds
  A meets U & U \ A <> {};
  for U being Subset of T st U is open & p in U holds A meets U & U meets A`
  proof
    let U be Subset of T;
    assume U is open & p in U;
    then A meets U & U \ A <> {} by A2;
    hence thesis by Th1;
  end;
  hence thesis by TOPS_1:61;
end;

theorem
  for T being non empty TopSpace, A being Subset of T,
  p being Point of T holds p in Fr A iff (for B being Basis of p,
  U being Subset of T st U in B holds A meets U & U \ A <> {})
proof
  let T be non empty TopSpace, A be Subset of T, p be Point of T;
  hereby
    assume
A1: p in Fr A;
    let B be Basis of p, U be Subset of T;
    assume U in B;
    then U is open & p in U by YELLOW_8:21;
    hence A meets U & U \ A <> {} by A1,Th11;
  end;
  assume
A2: for B being Basis of p,
  U being Subset of T st U in B holds A meets U & U \ A <> {};
  for U being Subset of T st U is open & p in U holds A meets U & U meets A`
  proof
    let U be Subset of T;
    assume
A3: U is open & p in U;
    consider B being Basis of p;
    consider V being Subset of T such that
A4: V in B & V c= U by A3,YELLOW_8:def 2;
A5: A meets V & V \ A <> {} by A2,A4;
    then U \ A <> {} by A4,XBOOLE_1:3,33;
    hence thesis by A4,A5,Th1,XBOOLE_1:63;
  end;
  hence thesis by TOPS_1:61;
end;

theorem
  for T being non empty TopSpace, A being Subset of T,
  p being Point of T holds p in Fr A iff (ex B being Basis of p st
  for U being Subset of T st U in B holds A meets U & U \ A <> {})
proof
  let T be non empty TopSpace, A be Subset of T, p be Point of T;
  hereby
    assume
A1: p in Fr A;
    consider B being Basis of p;
    take B;
    let U be Subset of T;
    assume U in B;
    then U is open & p in U by YELLOW_8:21;
    hence A meets U & U \ A <> {} by A1,Th11;
  end;
  given B being Basis of p such that
A2: for U being Subset of T st U in B holds A meets U & U \ A <> {};
  for U being Subset of T st U is open & p in U holds A meets U & U meets A`
  proof
    let U be Subset of T;
    assume U is open & p in U;
    then consider V being Subset of T such that
A3: V in B & V c= U by YELLOW_8:def 2;
A4: A meets V & V \ A <> {} by A2,A3;
    then U \ A <> {} by A3,XBOOLE_1:3,33;
    hence thesis by A3,A4,Th1,XBOOLE_1:63;
  end;
  hence thesis by TOPS_1:61;
end;

theorem :: Theorem 1.3.2. (d)
  for T being TopSpace, A, B being Subset of T holds
  Fr (A /\ B) c= (Cl A /\ Fr B) \/ (Fr A /\ Cl B)
proof
  let T be TopSpace, A, B be Subset of T;
A1: Fr (A /\ B) = Cl (A /\ B) /\ Cl (A /\ B)` by TOPS_1:def 2
    .= Cl (A /\ B) /\ Cl (A` \/ B`) by XBOOLE_1:54
    .= Cl (A /\ B) /\ ((Cl A`) \/ Cl B`) by PRE_TOPC:50;
  Cl A /\ Cl B /\ ((Cl A`) \/ Cl B`) =
  (Cl A /\ Cl B /\ (Cl A`)) \/ (Cl A /\ Cl B /\ Cl B`) by XBOOLE_1:23
    .= (Cl A /\ Cl A` /\ Cl B) \/ (Cl A /\ Cl B /\ Cl B`) by XBOOLE_1:16
    .= (Fr A /\ Cl B) \/ (Cl A /\ Cl B /\ Cl B`) by TOPS_1:def 2
    .= (Fr A /\ Cl B) \/ (Cl A /\ (Cl B /\ Cl B`)) by XBOOLE_1:16
    .= (Fr A /\ Cl B) \/ (Cl A /\ Fr B) by TOPS_1:def 2;
  hence thesis by A1,PRE_TOPC:51,XBOOLE_1:26;
end;

theorem :: Theorem 1.3.2. (f)
  for T being TopSpace, A being Subset of T holds
  the carrier of T = Int A \/ Fr A \/ Int A`
proof
  let T be TopSpace, A be Subset of T;
  Int A \/ Fr A \/ Int A` = Int A \/ Int A` \/ Fr A by XBOOLE_1:4
    .= Int A \/ Int A` \/ (Cl A /\ Cl A`) by TOPS_1:def 2
    .= (Int A \/ Int A` \/ Cl A) /\ (Int A \/ Int A` \/ Cl A`) by XBOOLE_1:24
    .= (Int A \/ (Cl A)` \/ Cl A) /\ (Int A \/ Int A` \/ Cl A`) by TDLAT_3:4
    .= (Int A \/ ((Cl A)` \/ Cl A)) /\ (Int A \/ Int A` \/ Cl A`)
  by XBOOLE_1:4
    .= (Int A \/ [#]T) /\ (Int A \/ Int A` \/ Cl A`) by PRE_TOPC:18
    .= (Int A \/ [#]T) /\ (Int A \/ Int A` \/ (Int A)`) by TDLAT_3:3
    .= (Int A \/ [#]T) /\ (Int A \/ (Int A)` \/ Int A`) by XBOOLE_1:4
    .= (Int A \/ [#]T) /\ ([#]T \/ Int A`) by PRE_TOPC:18
    .= [#]T /\ ([#]T \/ Int A`) by XBOOLE_1:12
    .= [#]T /\ [#]T by XBOOLE_1:12
    .= [#]T;
  hence thesis;
end;

theorem Th16: :: Theorem 1.3.2. (k)
  for T being TopSpace, A being Subset of T holds
  A is open closed iff Fr A = {}
proof
  let T be TopSpace, A be Subset of T;
  hereby
    assume
A1: A is open closed;
    then
A2: Int A = A by TOPS_1:55;
    Fr A = A \ Int A by A1,TOPS_1:77
      .= {} by A2,XBOOLE_1:37;
    hence Fr A = {};
  end;
  assume
A3: Fr A = {};
A4: A c= Cl A by PRE_TOPC:48;
  Fr A = Cl A \ Int A by Th10;
  then
A5: Cl A c= Int A by A3,XBOOLE_1:37;
A6: Int A c= A by TOPS_1:44;
  then Cl A c= A by A5,XBOOLE_1:1;
  then Cl A = A by A4,XBOOLE_0:def 10;
  hence thesis by A5,A6,XBOOLE_0:def 10;
end;

begin :: Accumulation Points and Derivative of a Set

:: 1.3.2. Accumulation Points

definition
  let T be TopStruct, A be Subset of T, x be set;
  pred x is_an_accumulation_point_of A means
  :Def2:
  x in Cl (A \ {x});
end;

theorem Th17:
  for T being TopSpace, A being Subset of T, x being set st
  x is_an_accumulation_point_of A holds x is Point of T
proof
  let T be TopSpace, A be Subset of T, x be set;
  assume x is_an_accumulation_point_of A;
  then x in Cl (A \ {x}) by Def2;
  hence thesis;
end;

:: Derivative of a Set

definition
  let T be TopStruct, A be Subset of T;
  func Der A -> Subset of T means
  :Def3:
  for x being set st x in the carrier of T holds
  x in it iff x is_an_accumulation_point_of A;
  existence
  proof
    defpred P[set] means $1 is_an_accumulation_point_of A;
    consider D being Subset of T such that
A1: for x being set holds x in D iff x in the carrier of T & P[x]
    from SUBSET_1:sch 1;
    take D;
    thus thesis by A1;
  end;
  uniqueness
  proof
    let A1, A2 be Subset of T;
    defpred P[set] means $1 is_an_accumulation_point_of A;
    assume that
A2: (for x being set st x in the carrier of T holds x in A1 iff P[x]) and
A3: (for x being set st x in the carrier of T holds x in A2 iff P[x]);
A4: A1 c= A2
    proof
      let x be set;
      assume
A5:   x in A1;
      then P[x] by A2;
      hence thesis by A3,A5;
    end;
    A2 c= A1
    proof
      let x be set;
      assume
A6:   x in A2;
      then P[x] by A3;
      hence thesis by A2,A6;
    end;
    hence thesis by A4,XBOOLE_0:def 10;
  end;
end;

:: 1.3.3. Characterizations of a Derivative

theorem Th18:
  for T being non empty TopSpace, A being Subset of T, x being set holds
  x in Der A iff x is_an_accumulation_point_of A
proof
  let T be non empty TopSpace, A be Subset of T, x be set;
  thus x in Der A implies x is_an_accumulation_point_of A by Def3;
  assume
A1: x is_an_accumulation_point_of A;
  then x is Point of T by Th17;
  hence thesis by A1,Def3;
end;

theorem Th19:
  for T being non empty TopSpace, A being Subset of T,
  x being Point of T holds x in Der A iff
  for U being open Subset of T st x in U
  ex y being Point of T st y in A /\ U & x <> y
proof
  let T be non empty TopSpace, A be Subset of T, x be Point of T;
  hereby
    assume x in Der A;
    then
A1: x is_an_accumulation_point_of A by Th18;
    let U be open Subset of T;
    assume
A2: x in U;
    x in Cl (A \ {x}) by A1,Def2;
    then A \ {x} meets U by A2,PRE_TOPC:54;
    then consider y being set such that
A3: y in A \ {x} & y in U by XBOOLE_0:3;
    reconsider y as Point of T by A3;
    take y;
    y in A & y <> x by A3,ZFMISC_1:64;
    hence y in A /\ U & x <> y by A3,XBOOLE_0:def 4;
  end;
  assume
A4: for U being open Subset of T st x in U
  ex y being Point of T st y in A /\ U & x <> y;
  for G being Subset of T st G is open holds x in G implies A \ {x} meets G
  proof
    let G be Subset of T;
    assume
A5: G is open;
    assume x in G;
    then consider y being Point of T such that
A6: y in A /\ G & x <> y by A4,A5;
A7: y in G by A6,XBOOLE_0:def 4;
    y in A by A6,XBOOLE_0:def 4;
    then y in A \ {x} by A6,ZFMISC_1:64;
    hence thesis by A7,XBOOLE_0:3;
  end;
  then x in Cl (A \ {x}) by PRE_TOPC:54;
  then x is_an_accumulation_point_of A by Def2;
  hence thesis by Th18;
end;

theorem
  for T being non empty TopSpace, A being Subset of T,
  x being Point of T holds x in Der A iff for B being Basis of x,
  U being Subset of T st U in B ex y being Point of T st y in A /\ U & x <> y
proof
  let T be non empty TopSpace, A be Subset of T, x be Point of T;
  hereby
    assume x in Der A;
    then
A1: x is_an_accumulation_point_of A by Th18;
    let B be Basis of x, U be Subset of T;
    assume
A2: U in B;
A3: x in Cl (A \ {x}) by A1,Def2;
    U is open & x in U by A2,YELLOW_8:21;
    then A \ {x} meets U by A3,PRE_TOPC:54;
    then consider y being set such that
A4: y in A \ {x} & y in U by XBOOLE_0:3;
    reconsider y as Point of T by A4;
    take y;
    y in A & y <> x by A4,ZFMISC_1:64;
    hence y in A /\ U & x <> y by A4,XBOOLE_0:def 4;
  end;
  assume
A5: for B being Basis of x, U being Subset of T st U in B
  ex y being Point of T st y in A /\ U & x <> y;
  for G being Subset of T st G is open holds x in G implies A \ {x} meets G
  proof
    let G be Subset of T;
    assume
A6: G is open;
    consider B being Basis of x;
    assume x in G;
    then consider V being Subset of T such that
A7: V in B & V c= G by A6,YELLOW_8:22;
    consider y being Point of T such that
A8: y in A /\ V & x <> y by A5,A7;
    A /\ V c= A /\ G by A7,XBOOLE_1:26;
    then consider y being Point of T such that
A9: y in A /\ G & x <> y by A8;
A10: y in G by A9,XBOOLE_0:def 4;
    y in A by A9,XBOOLE_0:def 4;
    then y in A \ {x} by A9,ZFMISC_1:64;
    hence thesis by A10,XBOOLE_0:3;
  end;
  then x in Cl (A \ {x}) by PRE_TOPC:54;
  then x is_an_accumulation_point_of A by Def2;
  hence thesis by Th18;
end;

theorem
  for T being non empty TopSpace, A being Subset of T,
  x being Point of T holds x in Der A iff ex B being Basis of x st
  for U being Subset of T st U in B
  ex y being Point of T st y in A /\ U & x <> y
proof
  let T be non empty TopSpace, A be Subset of T, x be Point of T;
  hereby
    assume x in Der A;
    then
A1: x is_an_accumulation_point_of A by Th18;
    consider B being Basis of x;
    take B;
    let U be Subset of T;
    assume
A2: U in B;
A3: x in Cl (A \ {x}) by A1,Def2;
    U is open & x in U by A2,YELLOW_8:21;
    then A \ {x} meets U by A3,PRE_TOPC:54;
    then consider y being set such that
A4: y in A \ {x} & y in U by XBOOLE_0:3;
    reconsider y as Point of T by A4;
    take y;
    y in A & y <> x by A4,ZFMISC_1:64;
    hence y in A /\ U & x <> y by A4,XBOOLE_0:def 4;
  end;
  given B being Basis of x such that
A5: for U being Subset of T st U in B
  ex y being Point of T st y in A /\ U & x <> y;
  for G being Subset of T st G is open holds x in G implies A \ {x} meets G
  proof
    let G be Subset of T;
    assume
A6: G is open;
    assume x in G;
    then consider V being Subset of T such that
A7: V in B & V c= G by A6,YELLOW_8:22;
    consider y being Point of T such that
A8: y in A /\ V & x <> y by A5,A7;
    A /\ V c= A /\ G by A7,XBOOLE_1:26;
    then consider y being Point of T such that
A9: y in A /\ G & x <> y by A8;
A10: y in G by A9,XBOOLE_0:def 4;
    y in A by A9,XBOOLE_0:def 4;
    then y in A \ {x} by A9,ZFMISC_1:64;
    hence thesis by A10,XBOOLE_0:3;
  end;
  then x in Cl (A \ {x}) by PRE_TOPC:54;
  then x is_an_accumulation_point_of A by Def2;
  hence thesis by Th18;
end;

begin :: 1.3.3. Isolated Points

definition
  let T be TopSpace, A be Subset of T, x be set;
  pred x is_isolated_in A means
  :Def4:
  x in A & not x is_an_accumulation_point_of A;
end;

theorem
  for T being non empty TopSpace, A being Subset of T, p being set holds
  p in A \ Der A iff p is_isolated_in A
proof
  let T be non empty TopSpace, A be Subset of T, p be set;
  hereby
    assume p in A \ Der A;
    then
A1: p in A & not p in Der A by XBOOLE_0:def 5;
    then not p is_an_accumulation_point_of A by Th18;
    hence p is_isolated_in A by A1,Def4;
  end;
  assume p is_isolated_in A;
  then
A2: p in A & not p is_an_accumulation_point_of A by Def4;
  then not p in Der A by Th18;
  hence thesis by A2,XBOOLE_0:def 5;
end;

theorem Th23:
  for T being non empty TopSpace, A being Subset of T,
  p being Point of T holds p is_an_accumulation_point_of A iff
  for U being open Subset of T st p in U
  ex q being Point of T st q <> p & q in A & q in U
proof
  let T be non empty TopSpace, A be Subset of T, p be Point of T;
  hereby
    assume p is_an_accumulation_point_of A;
    then
A1: p in Der A by Th18;
    let U be open Subset of T;
    assume p in U;
    then consider q being Point of T such that
A2: q in A /\ U & p <> q by A1,Th19;
    take q;
    thus q <> p & q in A & q in U by A2,XBOOLE_0:def 4;
  end;
  assume
A3: for U being open Subset of T st p in U
  ex q being Point of T st q <> p & q in A & q in U;
  for U being open Subset of T st p in U
  ex y being Point of T st y in A /\ U & p <> y
  proof
    let U be open Subset of T;
    assume p in U;
    then consider q being Point of T such that
A4: q <> p & q in A & q in U by A3;
    take q;
    thus thesis by A4,XBOOLE_0:def 4;
  end;
  then p in Der A by Th19;
  hence thesis by Th18;
end;

theorem Th24:
  for T being non empty TopSpace, A being Subset of T,
  p being Point of T holds p is_isolated_in A iff
  ex G being open Subset of T st G /\ A = {p}
proof
  let T be non empty TopSpace, A be Subset of T, p be Point of T;
  hereby
    assume p is_isolated_in A;
    then
A1: p in A & not p is_an_accumulation_point_of A by Def4;
    then consider U being open Subset of T such that
A2: p in U & not ex q being Point of T st q <> p & q in A & q in U by Th23;
    take U;
    U /\ A = {p}
    proof
      thus U /\ A c= {p}
      proof
        let x be set;
        assume x in U /\ A;
        then
A4:     x in U & x in A by XBOOLE_0:def 4;
        x = p by A2,A4;
        hence thesis by TARSKI:def 1;
      end;
      let x be set;
      assume x in {p};
      then x = p by TARSKI:def 1;
      hence thesis by A1,A2,XBOOLE_0:def 4;
    end;
    hence U /\ A = {p};
  end;
  given G being open Subset of T such that
A5: G /\ A = {p};
A6: p in G /\ A by A5,TARSKI:def 1;
  then
A7: p in A by XBOOLE_0:def 4;
  ex U being open Subset of T st
  p in U & not ex q being Point of T st q <> p & q in A & q in U
  proof
    take G;
    for q being Point of T holds q = p or not q in A or not q in G
    proof
      given q being Point of T such that
A8:   q <> p & q in A & q in G;
      q in A /\ G by A8,XBOOLE_0:def 4;
      hence thesis by A5,A8,TARSKI:def 1;
    end;
    hence thesis by A6,XBOOLE_0:def 4;
  end;
  then not p is_an_accumulation_point_of A by Th23;
  hence thesis by A7,Def4;
end;

definition
  let T be TopSpace, p be Point of T;
  attr p is isolated means
  :Def5:
  p is_isolated_in [#]T;
end;

theorem
  for T being non empty TopSpace, p being Point of T holds
  p is isolated iff {p} is open
proof
  let T be non empty TopSpace, p be Point of T;
  hereby
    assume p is isolated;
    then p is_isolated_in [#]T by Def5;
    then consider G being open Subset of T such that
A1: G /\ [#]T = {p} by Th24;
    thus {p} is open by A1,XBOOLE_1:28;
  end;
  assume
A2: {p} is open;
  {p} /\ [#]T = {p} by XBOOLE_1:28;
  then p is_isolated_in [#]T by A2,Th24;
  hence thesis by Def5;
end;

begin :: Derivative of a Subset-Family

definition
  let T be TopSpace, F be Subset-Family of T;
  func Der F -> Subset-Family of T means
  :Def6:
  for A being Subset of T holds A in it iff
  ex B being Subset of T st A = Der B & B in F;
  existence
  proof
    defpred P[set] means ex W being Subset of T st $1 = Der W & W in F;
    thus ex S be Subset-Family of T st
    for Z being Subset of T holds Z in S iff P[Z] from SUBSET_1:sch 3;
  end;
  uniqueness
  proof
    let H, G be Subset-Family of T;
    defpred P[set] means ex W being Subset of T st $1 = Der W & W in F;
    assume that
A1: for Z being Subset of T holds Z in H iff P[Z] and
A2: for X being Subset of T holds X in G iff P[X];
    H = G
    proof
      now
        let Z be set;
        assume
A3:     Z in H;
        P[Z] by A1,A3;
        hence Z in G by A2;
      end;
      then
A4:   H c= G by TARSKI:def 3;
      now
        let X be set;
        assume
A5:     X in G;
        then reconsider X' = X as Subset of T;
        P[X'] by A2,A5;
        hence X in H by A1;
      end;
      then G c= H by TARSKI:def 3;
      hence thesis by A4,XBOOLE_0:def 10;
    end;
    hence thesis;
  end;
end;

reserve T for non empty TopSpace,
  A, B for Subset of T,
  F, G for Subset-Family of T,
  x for set;

theorem
  F = {} implies Der F = {}
proof
  assume
A1: F = {};
  assume Der F <> {};
  then consider x being set such that
A2: x in Der F by XBOOLE_0:def 1;
  consider B being Subset of T such that
A3: x = Der B & B in F by A2,Def6;
  thus thesis by A1,A3;
end;

theorem
  F = { A } implies Der F = { Der A }
proof
  assume
A1: F = { A };
  then
A2: A in F by TARSKI:def 1;
  thus Der F c= { Der A }
  proof
    let x be set;
    assume
A3: x in Der F;
    then reconsider B = x as Subset of T;
    consider C being Subset of T such that
A4: B = Der C & C in F by A3,Def6;
    C = A by A1,A4,TARSKI:def 1;
    hence thesis by A4,TARSKI:def 1;
  end;
  let x be set;
  assume x in { Der A };
  then x = Der A by TARSKI:def 1;
  hence thesis by A2,Def6;
end;

theorem Th28:
  F c= G implies Der F c= Der G
proof
  assume
A1: F c= G;
  Der F c= Der G
  proof
    let x be set;
    assume
A2: x in Der F;
    then reconsider A = x as Subset of T;
    consider B being Subset of T such that
A3: A = Der B & B in F by A2,Def6;
    thus x in Der G by A1,A3,Def6;
  end;
  hence thesis;
end;

theorem
  Der (F \/ G) = Der F \/ Der G
proof
  thus Der (F \/ G) c= Der F \/ Der G
  proof
    let x be set;
    assume
A1: x in Der (F \/ G);
    then reconsider A = x as Subset of T;
    consider B being Subset of T such that
A2: A = Der B & B in F \/ G by A1,Def6;
    per cases by A2,XBOOLE_0:def 3;
    suppose B in F;
      then A in Der F by A2,Def6;
      hence thesis by XBOOLE_0:def 3;
    end;
    suppose B in G;
      then A in Der G by A2,Def6;
      hence thesis by XBOOLE_0:def 3;
    end;
  end;
A3: Der F c= Der (F \/ G) by Th28,XBOOLE_1:7;
  Der G c= Der (F \/ G) by Th28,XBOOLE_1:7;
  hence Der F \/ Der G c= Der (F \/ G) by A3,XBOOLE_1:8;
end;

:: 1.3.4. Properties of a Derivative of a Set

theorem Th30:
  for T being non empty TopSpace, A being Subset of T holds Der A c= Cl A
proof
  let T be non empty TopSpace, A be Subset of T;
  let x be set;
  assume
A1: x in Der A;
  then reconsider x' = x as Point of T;
  for G being Subset of T st G is open holds x' in G implies A meets G
  proof
    let G be Subset of T;
    assume
A2: G is open;
    assume x' in G;
    then consider y being Point of T such that
A3: y in A /\ G & x <> y by A1,A2,Th19;
    thus thesis by A3,XBOOLE_0:4;
  end;
  hence thesis by PRE_TOPC:54;
end;

theorem Th31: :: Theorem 1.3.4. (a)
  for T being TopSpace, A being Subset of T holds Cl A = A \/ Der A
proof
  let T be TopSpace, A be Subset of T;
  per cases;
  suppose
A1: T is non empty;
    thus Cl A c= A \/ Der A
    proof
      let x be set;
      assume
A2:   x in Cl A;
      then reconsider x' = x as Point of T;
      per cases;
      suppose x in A;
        hence thesis by XBOOLE_0:def 3;
      end;
      suppose
A3:     not x in A;
        for U being open Subset of T st x' in U
        ex y being Point of T st y in A /\ U & x' <> y
        proof
          let U be open Subset of T;
          assume x' in U;
          then A meets U by A2,PRE_TOPC:54;
          then consider y being set such that
A4:       y in A & y in U by XBOOLE_0:3;
          reconsider y as Point of T by A4;
          take y;
          thus thesis by A3,A4,XBOOLE_0:def 4;
        end;
        then x' in Der A by A1,Th19;
        hence thesis by XBOOLE_0:def 3;
      end;
    end;
    let x be set;
    assume
A5: x in A \/ Der A;
A6: Der A c= Cl A by A1,Th30;
    per cases by A5,XBOOLE_0:def 3;
    suppose
A7:   x in A;
      A c= Cl A by PRE_TOPC:48;
      hence thesis by A7;
    end;
    suppose x in Der A;
      hence thesis by A6;
    end;
  end;
  suppose
A8: T is empty;
    then Cl A = {} \/ {}
      .= A \/ Der A by A8;
    hence thesis;
  end;
end;

theorem Th32: :: Theorem 1.3.4. (b)
  for T being non empty TopSpace, A, B being Subset of T st
  A c= B holds Der A c= Der B
proof
  let T be non empty TopSpace, A, B be Subset of T;
  assume
A1: A c= B;
  let x be set;
  assume
A2: x in Der A;
  then reconsider x' = x as Point of T;
  for U being open Subset of T st x' in U
  ex y being Point of T st y in B /\ U & x' <> y
  proof
    let U be open Subset of T;
    assume x' in U;
    then consider y being Point of T such that
A3: y in A /\ U & x' <> y by A2,Th19;
    A /\ U c= B /\ U by A1,XBOOLE_1:26;
    hence thesis by A3;
  end;
  hence thesis by Th19;
end;

theorem Th33: :: Theorem 1.3.4. (c)
  for T being non empty TopSpace, A, B being Subset of T holds
  Der (A \/ B) = Der A \/ Der B
proof
  let T be non empty TopSpace, A, B be Subset of T;
  thus Der (A \/ B) c= Der A \/ Der B
  proof
    let x be set;
    assume x in Der (A \/ B);
    then x is_an_accumulation_point_of A \/ B by Th18;
    then
A1: x in Cl ((A \/ B) \ {x}) by Def2;
    (A \/ B) \ {x} = (A \ {x}) \/ (B \ {x}) by XBOOLE_1:42;
    then Cl ((A \/ B) \ {x}) = Cl (A \ {x}) \/ Cl (B \ {x}) by PRE_TOPC:50;
    then x in Cl (A \ {x}) or x in Cl (B \ {x}) by A1,XBOOLE_0:def 3;
    then x is_an_accumulation_point_of A or
    x is_an_accumulation_point_of B by Def2;
    then x in Der A or x in Der B by Th18;
    hence thesis by XBOOLE_0:def 3;
  end;
  let x be set;
  assume x in Der A \/ Der B;
  then x in Der A or x in Der B by XBOOLE_0:def 3;
  then
A2: x is_an_accumulation_point_of A or
  x is_an_accumulation_point_of B by Th18;
  x in Cl ((A \/ B) \ {x})
  proof
    A \ {x} c= (A \/ B) \ {x} by XBOOLE_1:7,33;
    then
A3: Cl (A \ {x}) c= Cl ((A \/ B) \ {x}) by PRE_TOPC:49;
    B \ {x} c= (A \/ B) \ {x} by XBOOLE_1:7,33;
    then
A4: Cl (B \ {x}) c= Cl ((A \/ B) \ {x}) by PRE_TOPC:49;
    per cases by A2,Def2;
    suppose x in Cl (A \ {x});
      hence thesis by A3;
    end;
    suppose x in Cl (B \ {x});
      hence thesis by A4;
    end;
  end;
  then x is_an_accumulation_point_of A \/ B by Def2;
  hence thesis by Th18;
end;

theorem Th34:
  for T being non empty TopSpace, A being Subset of T st T is T_1 holds
  Der Der A c= Der A
proof
  let T be non empty TopSpace, A be Subset of T;
  assume
A1: T is T_1;
  let x be set;
  assume
A2: x in Der Der A;
  then reconsider x' = x as Point of T;
  assume not x in Der A;
  then consider G being open Subset of T such that
A3: x' in G & not ex y being Point of T st y in A /\ G & x' <> y by Th19;
  consider y being Point of T such that
A4: y in Der A /\ G & x <> y by A2,A3,Th19;
A5: y in Der A & y in G by A4,XBOOLE_0:def 4;
  then
A6: y in Der A \ {x} by A4,ZFMISC_1:64;
A7: G misses A \ {x}
  proof
    assume G meets A \ {x};
    then consider g being set such that
A8: g in G & g in A \ {x} by XBOOLE_0:3;
    g in A by A8,XBOOLE_0:def 5;
    then g in A /\ G by A8,XBOOLE_0:def 4;
    then x' = g by A3;
    hence thesis by A8,ZFMISC_1:64;
  end;
  consider q being set such that
A9: q in G & q in Der A \ {x} by A5,A6;
  reconsider q as Point of T by A9;
A10: not q in {x} by A9,XBOOLE_0:def 5;
A11: q in Der A by A9,XBOOLE_0:def 5;
A12: q in G \ {x} by A9,A10,XBOOLE_0:def 5;
  Cl {x'} = {x'} by A1,YELLOW_8:35;
  then
A13: G \ {x'} is open by FRECHET:4;
  set U = G \ {x'};
  consider y being Point of T such that
A14: y in A /\ U & q <> y by A11,A12,A13,Th19;
A15: y in A & y in U by A14,XBOOLE_0:def 4;
  then y in A \ {q} by A14,ZFMISC_1:64;
  then consider f being set such that
A16: f in G \ {x'} & f in A \ {q} by A15;
A17: f in G & f <> x' by A16,ZFMISC_1:64;
  f in A by A16,ZFMISC_1:64;
  then f in A \ {x'} by A17,ZFMISC_1:64;
  hence thesis by A7,A17,XBOOLE_0:3;
end;

theorem Th35:
  for T being TopSpace, A being Subset of T st T is T_1 holds
  Cl Der A = Der A
proof
  let T be TopSpace, A be Subset of T;
  assume
A1: T is T_1;
  per cases;
  suppose
A2: T is non empty;
    Cl Der A = Der A \/ Der Der A by Th31;
    then
A3: Cl Der A c= Der A \/ Der A by A1,A2,Th34,XBOOLE_1:9;
    Der A c= Cl Der A by PRE_TOPC:48;
    hence thesis by A3,XBOOLE_0:def 10;
  end;
  suppose
A4: T is empty;
    then Cl Der A = {};
    hence thesis by A4;
  end;
end;

registration
  let T be T_1 non empty TopSpace, A be Subset of T;
  cluster Der A -> closed;
  coherence
  proof
    Der A = Cl Der A by Th35;
    hence thesis;
  end;
end;

theorem Th36: :: Theorem 1.3.4. d)
  for T being non empty TopSpace, F being Subset-Family of T holds
  union Der F c= Der union F
proof
  let T be non empty TopSpace, F be Subset-Family of T;
  let x be set;
  assume x in union Der F;
  then consider Y being set such that
A1: x in Y & Y in Der F by TARSKI:def 4;
  reconsider Y as Subset of T by A1;
  consider B being Subset of T such that
A2: Y = Der B & B in F by A1,Def6;
  Der B c= Der union F by A2,Th32,ZFMISC_1:92;
  hence thesis by A1,A2;
end;

theorem
  A c= B & x is_an_accumulation_point_of A implies
  x is_an_accumulation_point_of B
proof
  assume A c= B;
  then
A1: Der A c= Der B by Th32;
  assume x is_an_accumulation_point_of A;
  then x in Der A by Th18;
  hence thesis by A1,Th18;
end;

begin :: 1.3.4. Density-in-itself

definition
  let T be TopSpace, A be Subset of T;
  attr A is dense-in-itself means
  :Def7:
  A c= Der A;
end;

definition
  let T be non empty TopSpace;
  attr T is dense-in-itself means

  [#]T is dense-in-itself;
end;

theorem Th38:
  T is T_1 & A is dense-in-itself implies Cl A is dense-in-itself
proof
  assume
A1: T is T_1;
  assume A is dense-in-itself;
  then A c= Der A by Def7;
  then
A2: Der A = A \/ Der A by XBOOLE_1:12
    .= Cl A by Th31;
  Der Cl A = Der (A \/ Der A) by Th31
    .= Der A \/ Der Der A by Th33
    .= Cl A by A1,A2,Th34,XBOOLE_1:12;
  hence thesis by Def7;
end;

definition
  let T be TopSpace, F be Subset-Family of T;
  attr F is dense-in-itself means
  :Def9:
  for A being Subset of T st A in F holds A is dense-in-itself;
end;

theorem Th39:
  for F being Subset-Family of T st F is dense-in-itself holds
  union F c= union Der F
proof
  let F be Subset-Family of T;
  assume
A1: F is dense-in-itself;
  thus union F c= union Der F
  proof
    let x be set;
    assume x in union F;
    then consider A being set such that
A2: x in A & A in F by TARSKI:def 4;
    reconsider A as Subset of T by A2;
    A is dense-in-itself by A1,A2,Def9;
    then
A3: A c= Der A by Def7;
    Der A in Der F by A2,Def6;
    hence thesis by A2,A3,TARSKI:def 4;
  end;
end;

theorem Th40:
  F is dense-in-itself implies union F is dense-in-itself
proof
  assume F is dense-in-itself;
  then
A1: union F c= union Der F by Th39;
  union Der F c= Der union F by Th36;
  then union F c= Der union F by A1,XBOOLE_1:1;
  hence thesis by Def7;
end;

theorem
  Fr {}T = {}
proof
  Fr {}T = Cl {}T /\ Cl ({}T)` by TOPS_1:def 2
    .= {} /\ Cl ({}T)`;
  hence thesis;
end;

registration
  let T be TopSpace, A be open closed Subset of T;
  cluster Fr A -> empty;
  coherence by Th16;
end;

registration
  let T be non empty non discrete TopSpace;
  cluster non open Subset of T;
  existence by TDLAT_3:17;
  cluster non closed Subset of T;
  existence by TDLAT_3:18;
end;

registration
  let T be non empty non discrete TopSpace, A be non open Subset of T;
  cluster Fr A -> non empty;
  coherence by Th16;
end;

registration
  let T be non empty non discrete TopSpace, A be non closed Subset of T;
  cluster Fr A -> non empty;
  coherence by Th16;
end;

begin :: Perfect Sets

definition
  let T be TopSpace, A be Subset of T;
  attr A is perfect means
  :Def10:
  A is closed dense-in-itself;
end;

registration
  let T be TopSpace;
  cluster perfect -> closed dense-in-itself Subset of T;
  coherence by Def10;
  cluster closed dense-in-itself -> perfect Subset of T;
  coherence by Def10;
end;

theorem Th42:
  for T being TopSpace holds Der {}T = {}T
proof
  let T be TopSpace;
  per cases;
  suppose T is non empty;
    then reconsider T' = T as non empty TopSpace;
    Cl {}T' is empty;
    then {} = {}T \/ Der {}T by Th31
      .= Der {}T;
    hence thesis;
  end;
  suppose T is empty;
    hence thesis by Lm1;
  end;
end;

Lm2: for T being TopSpace,
A being Subset of T st A is closed dense-in-itself holds Der A = A
proof
  let T be TopSpace, A be Subset of T;
  assume
A1: A is closed dense-in-itself;
  then A = Cl A by PRE_TOPC:52
    .= Der A \/ A by Th31;
  hence Der A c= A by XBOOLE_1:7;
  thus A c= Der A by A1,Def7;
end;

theorem Th43:
  for T being TopSpace, A being Subset of T holds A is perfect iff Der A = A
proof
  let T be TopSpace, A be Subset of T;
  thus A is perfect implies Der A = A by Lm2;
  assume
A1: Der A = A;
  then
A2: A is dense-in-itself by Def7;
  Cl A = Der A \/ A by Th31
    .= A by A1;
  hence thesis by A2;
end;

theorem Th44:
  for T being TopSpace holds {}T is perfect
proof
  let T be TopSpace;
  Der {}T = {}T by Th42;
  hence thesis by Th43;
end;

registration
  let T be TopSpace;
  cluster empty -> perfect Subset of T;
  coherence
  proof
    let A be Subset of T;
    assume A is empty;
    then A = {}T;
    hence thesis by Th44;
  end;
end;

registration
  let T be TopSpace;
  cluster perfect Subset of T;
  existence
  proof
    take {}T;
    thus thesis;
  end;
end;

begin :: Scattered Subsets

definition
  let T be TopSpace, A be Subset of T;
  attr A is scattered means
  :Def11:
  not ex B being Subset of T st
  B is non empty & B c= A & B is dense-in-itself;
end;

registration
  let T be non empty TopSpace;
  cluster non empty scattered -> non dense-in-itself Subset of T;
  coherence by Def11;
  cluster dense-in-itself non empty -> non scattered Subset of T;
  coherence;
end;

theorem Th45:
  for T being TopSpace holds {}T is scattered
proof
  let T be TopSpace;
  {}T is scattered
  proof
    assume {}T is not scattered;
    then consider B being Subset of T such that
A1: B is non empty & B c= {}T & B is dense-in-itself by Def11;
    thus thesis by A1;
  end;
  hence thesis;
end;

registration
  let T be TopSpace;
  cluster empty -> scattered Subset of T;
  coherence
  proof
    let A be Subset of T;
    assume A is empty;
    then A = {}T;
    hence thesis by Th45;
  end;
end;

theorem
  for T being non empty TopSpace st T is T_1 holds
  ex A, B being Subset of T st
  A \/ B = [#]T & A misses B & A is perfect & B is scattered
proof
  let T be non empty TopSpace;
  assume
A1: T is T_1;
  defpred P[Subset of T] means $1 is dense-in-itself;
  consider CC being Subset-Family of T such that
A2: for A being Subset of T holds A in CC iff P[A] from SUBSET_1:sch 3;
  set C = union CC;
A3: CC is dense-in-itself by A2,Def9;
  then
A4: C is dense-in-itself by Th40;
  Cl C is dense-in-itself by A1,A3,Th38,Th40;
  then Cl C in CC by A2;
  then
A5: Cl C c= C by ZFMISC_1:92;
  C c= Cl C by PRE_TOPC:48;
  then Cl C = C by A5,XBOOLE_0:def 10;
  then
A6: C is perfect by A4;
  not ex B being Subset of T st
  B is non empty & B c= C` & B is dense-in-itself
  proof
    given B being Subset of T such that
A7: B is non empty & B c= C` & B is dense-in-itself;
A8: B misses union CC by A7,SUBSET_1:43;
    B in CC by A2,A7;
    hence thesis by A7,A8,XBOOLE_1:69,ZFMISC_1:92;
  end;
  then
A9: C` is scattered by Def11;
A10: [#]T = C \/ C` by PRE_TOPC:18;
  C misses C` by XBOOLE_1:79;
  hence thesis by A6,A9,A10;
end;

registration
  let T be discrete TopSpace, A be Subset of T;
  cluster Fr A -> empty;
  coherence
  proof
    A is open closed by TDLAT_3:17,18;
    hence thesis;
  end;
end;

registration
  let T be discrete TopSpace;
  cluster -> closed open Subset of T;
  coherence by TDLAT_3:17,18;
end;

theorem Th47:
  for T being discrete TopSpace, A being Subset of T holds Der A = {}
proof
  let T be discrete TopSpace, A be Subset of T;
  per cases;
  suppose
A1: T is non empty;
    assume Der A <> {};
    then consider x being set such that
A2: x in Der A by XBOOLE_0:def 1;
    x is_an_accumulation_point_of A by A1,A2,Th18;
    then x in Cl (A \ {x}) by Def2;
    then x in A \ {x} by PRE_TOPC:52;
    hence thesis by ZFMISC_1:64;
  end;
  suppose T is empty;
    hence thesis;
  end;
end;

registration
  let T be discrete non empty TopSpace, A be Subset of T;
  cluster Der A -> empty;
  coherence by Th47;
end;

begin :: Density of a Topological Space and Separable Spaces

:: 1.3.6. Density of a Topological Space

definition
  let T be TopSpace;
  func density T -> cardinal number means
  :Def12:
  (ex A being Subset of T st A is dense & it = card A) &
  for B being Subset of T st B is dense holds it c= card B;
  existence
  proof
    defpred P[ordinal number] means
    ex A being Subset of T st A is dense & $1 = card A;
    set B0 = [#]T;
    card B0 is ordinal;
    then
A1: ex A being ordinal number st P[A];
    consider A being ordinal number such that
A2: P[A] and
A3: for B being ordinal number st P[B] holds A c= B from ORDINAL1:sch 1(A1);
    consider B1 being Subset of T such that
A4: B1 is dense & A = card B1 by A2;
    take card B1;
    thus thesis by A3,A4;
  end;
  uniqueness
  proof
    let M1, M2 be cardinal number;
    assume (ex B being Subset of T st B is dense & M1 = card B) &
    (for B being Subset of T st B is dense holds M1 c= card B) &
    (ex B being Subset of T st B is dense & M2 = card B) &
    for B being Subset of T st B is dense holds M2 c= card B;
    hence M1 c= M2 & M2 c= M1;
  end;
end;

:: Separable Spaces

definition
  let T be TopSpace;
  attr T is separable means
  :Def13:
  density T c= omega;
end;

canceled;

theorem Th49:
  for T being countable TopSpace holds T is separable
proof
  let T be countable TopSpace;
A1: card [#]T c= omega by CARD_3:def 15;
  density T c= card [#]T by Def12;
  then density T c= omega by A1,XBOOLE_1:1;
  hence thesis by Def13;
end;

registration
  cluster countable -> separable TopSpace;
  coherence by Th49;
end;

begin :: Exercise 1.3.E.

theorem
  for A being Subset of R^1 st A = RAT holds A` = IRRAT
  by BORSUK_5:def 3,TOPMETR:24;

Lm3: RAT = REAL \ IRRAT
proof
  REAL \ IRRAT = REAL /\ RAT by BORSUK_5:def 3,XBOOLE_1:48;
  hence thesis by NUMBERS:12,XBOOLE_1:28;
end;

theorem
  for A being Subset of R^1 st A = IRRAT holds A` = RAT by Lm3,TOPMETR:24;

theorem
  for A being Subset of R^1 st A = RAT holds Int A = {}
proof
  let A be Subset of R^1;
  assume A = RAT;
  then Cl A` = [#]R^1 by BORSUK_5:51,def 3,TOPMETR:24;
  then (Cl A`)` = {}R^1 by XBOOLE_1:37;
  hence thesis by Th5;
end;

theorem
  for A being Subset of R^1 st A = IRRAT holds Int A = {}
proof
  let A be Subset of R^1;
  assume A = IRRAT;
  then Cl A` = [#]R^1 by Lm3,BORSUK_5:35,TOPMETR:24;
  then (Cl A`)` = {}R^1 by XBOOLE_1:37;
  hence thesis by Th5;
end;

theorem Th54:
  for A being Subset of R^1 st A = RAT holds A is dense
proof
  let A be Subset of R^1;
  assume A = RAT;
  then Cl A = the carrier of R^1 by BORSUK_5:35;
  hence thesis by TOPS_3:def 2;
end;

theorem Th55:
  for A being Subset of R^1 st A = IRRAT holds A is dense
proof
  let A be Subset of R^1;
  assume A = IRRAT;
  then Cl A = the carrier of R^1 by BORSUK_5:51;
  hence thesis by TOPS_3:def 2;
end;

theorem Th56:
  for A being Subset of R^1 st A = RAT holds A is boundary
proof
  let A be Subset of R^1;
  assume A = RAT;
  then A` is dense by Th55,BORSUK_5:def 3,TOPMETR:24;
  hence thesis by TOPS_1:def 4;
end;

theorem Th57:
  for A being Subset of R^1 st A = IRRAT holds A is boundary
proof
  let A be Subset of R^1;
  assume A = IRRAT;
  then A` is dense by Lm3,Th54,TOPMETR:24;
  hence thesis by TOPS_1:def 4;
end;

theorem Th58:
  for A being Subset of R^1 st A = REAL holds A is non boundary
proof
  let A be Subset of R^1;
  assume
A1: A = REAL;
  [#]R^1 = REAL by TOPMETR:24;
  then Int A = A by A1,TOPS_1:43;
  hence thesis by A1;
end;

theorem
  ex A, B being Subset of R^1 st
  A is boundary & B is boundary & A \/ B is non boundary
proof
  reconsider A = RAT as Subset of R^1 by NUMBERS:12,TOPMETR:24;
  reconsider B = IRRAT as Subset of R^1 by TOPMETR:24;
  take A,B;
  A \/ B = RAT \/ REAL by BORSUK_5:def 3,XBOOLE_1:39
    .= REAL by NUMBERS:12,XBOOLE_1:12;
  hence thesis by Th56,Th57,Th58;
end;

