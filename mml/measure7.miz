:: The One-Dimensional Lebesgue Measure As an Example of a
:: Formalization in the Mizar Language of the Classical Definition
:: of a Mathematical Object
::  by J\'ozef Bia{\l}as
::
:: Received February 4, 1995
:: Copyright (c) 1995 Association of Mizar Users

environ

 vocabularies FUNCT_1, SUPINF_1, RLVECT_1, SUPINF_2, ORDINAL2, RELAT_1, TARSKI,
      MEASURE5, FUNCT_2, BOOLE, COMPLEX1, FUNCT_3, MCART_1, MEASURE4, MEASURE1,
      MEASURE7, FUNCOP_1, PROB_1, NAT_1;
 notations TARSKI, XBOOLE_0, ZFMISC_1, SUBSET_1, SETFAM_1, MCART_1, DOMAIN_1,
      NUMBERS, XCMPLX_0, XXREAL_0, REAL_1, RELAT_1, FUNCT_1, RELSET_1,
      PARTFUN1, FUNCT_2, FUNCOP_1, NAT_1, NAT_D, PROB_1, SUPINF_1, SUPINF_2,
      MEASURE1, MEASURE3, MEASURE4, MEASURE5;
 constructors PARTFUN1, DOMAIN_1, FUNCOP_1, FRAENKEL, NAT_1, MEMBERED,
      MEASURE3, MEASURE4, MEASURE5, NAT_D;
 registrations SUBSET_1, SUPINF_1, MEASURE4, RELSET_1, ARYTM_3, FRAENKEL,
      FUNCT_2, MEMBERED, ORDINAL2, NUMBERS, RELAT_1, ORDINAL1, NAT_1;
 requirements NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, SUPINF_1, MEASURE4, XBOOLE_0;
 theorems TARSKI, SUPINF_1, SUPINF_2, MEASURE1, FUNCT_1, MEASURE4, MEASURE5,
      MEASURE6, ZFMISC_1, FUNCT_2, NAT_1, MCART_1, XBOOLE_0, XBOOLE_1,
      FUNCOP_1, XXREAL_0, ORDINAL1;
 schemes FUNCT_2, NAT_1, BINOP_2, SUBSET_1, XBOOLE_0;

begin :: Some theorems about series of R_eal numbers

theorem Th1:
   for F being Function of NAT,ExtREAL st
   for n being Element of NAT holds F.n = 0. holds SUM(F) = 0.
proof
   let F be Function of NAT,ExtREAL;
   assume
A1:for n being Element of NAT holds F.n = 0.;
    defpred P[Element of NAT] means Ser(F).$1 = 0.;
      Ser(F).0 = F.0 by SUPINF_2:63
                .= 0. by A1;
   then A2: P[0];
   A3: for t being Element of NAT st P[t] holds P[t+1]
         proof
            let t be Element of NAT;
            assume Ser(F).t = 0.;
            hence Ser(F).(t+1) = 0. + F.(t+1) by SUPINF_2:63
                        .= F.(t+1) by SUPINF_2:18
                        .= 0. by A1;
         end;
   A4:for s being Element of NAT holds P[s] from NAT_1:sch 1(A2,A3);
   A5:sup {0.} = 0. by SUPINF_1:85;
         rng Ser(F) = {0.}
      proof
      A6:dom Ser(F) = NAT & rng Ser(F) c= ExtREAL by FUNCT_2:def 1;
       thus rng Ser(F) c= {0.}
         proof
            let x be set;
            assume x in rng Ser(F);
            then consider s being set such that
         A7:s in dom Ser(F) & x = Ser(F).s by FUNCT_1:def 5;
            reconsider s as Element of NAT by A7;
            Ser(F).s = 0. by A4;
            hence thesis by A7,TARSKI:def 1;
         end;
         let x be set;
         assume x in {0.};
         then x = 0. by TARSKI:def 1;
         then x = Ser(F).0 by A4;
         hence thesis by A6,FUNCT_1:def 5;
      end;
   hence thesis by A5,SUPINF_2:def 23;
end;

theorem Th2:
   for F being Function of NAT,ExtREAL st F is nonnegative holds
   for n being Element of NAT holds F.n <= Ser(F).n
proof
   let F be Function of NAT,ExtREAL;
   assume
A1:F is nonnegative;
   let n be Element of NAT;
   per cases;
   suppose n = 0;
    hence thesis by SUPINF_2:63;
   end;
   suppose n <> 0;
    then consider k being Nat such that
    A2:n = k + 1 by NAT_1:6;
    reconsider k as Element of NAT by ORDINAL1:def 13;
         A3:Ser(F).n = Ser(F).k + F.n by A2,SUPINF_2:63;
            0. <= Ser(F).k & F.n <= F.n & 0. <= F.n by A1,SUPINF_2:58,
59;
         then not ((0. = +infty & F.n = -infty) or (0. = -infty & F.n = +infty
)) &
         not ((Ser(F).k = +infty & F.n = -infty) or (Ser(F).k = -infty & F.n =
+infty)) &
         0. <= Ser(F).k & F.n <= F.n by XXREAL_0:12,SUPINF_2:def 1;
   then 0. + F.n <= Ser(F).n by A3,SUPINF_2:14;
  hence thesis by SUPINF_2:18;
end;
end;

theorem Th3:
   for F,G,H being Function of NAT,ExtREAL st
   G is nonnegative & H is nonnegative holds
   (for n being Element of NAT holds F.n = G.n + H.n ) implies
   (for n being Element of NAT holds (Ser F).n = (Ser G).n + (Ser H).n )
proof
   let F,G,H be Function of NAT,ExtREAL;
   assume
A1:G is nonnegative & H is nonnegative;
   assume
A2:for n being Element of NAT holds F.n = G.n + H.n;
     defpred P[Element of NAT] means (Ser F).$1 = (Ser G).$1 + (Ser H).$1;
       (Ser F).0 = F.0 & (Ser G).0 = G.0 & (Ser H).0 = H.0 by SUPINF_2:63;
     then A3: P[0] by A2;
   A4: for k being Element of NAT st P[k] holds P[k+1]
      proof
         let k be Element of NAT;
         assume
      A5:(Ser F).k = (Ser G).k + (Ser H).k;
         A6:F.(k+1) = G.(k+1) + H.(k+1) by A2;
            A7: Ser(G).(k + 1) = Ser(G).k + G.(k + 1) &
            Ser(H).(k + 1) = Ser(H).k + H.(k + 1) by SUPINF_2:63;
      set n = k+1;
         0. <= G.n & 0. <= H.n by A1,SUPINF_2:58;
      then 0. + 0. <= G.n + H.n by MEASURE1:4;
      then 0. <= G.n + H.n by SUPINF_2:18;
    then A8: 0. <= F.(k+1) by A2;
         A9:0. <= (Ser G).k & 0. <= (Ser H).k & 0. <= (Ser G).(k+1)
                 by A1,SUPINF_2:59;
         A10:0. <= H.(k+1) & 0. <= G.(k+1) by A1,SUPINF_2:58;
               (Ser G).k + (Ser H).k + F.(k + 1)
                 = ((Ser G).k + F.(k+1)) + (Ser H).k by A8,A9,MEASURE4:1
                .= (((Ser G).k + G.(k+1)) + H.(k+1)) + (Ser H).k
                   by A6,A9,A10,MEASURE4:1
                .= (Ser G).(k+1) + (Ser H).(k+1) by A7,A9,A10,MEASURE4:1;
            hence thesis by A5,SUPINF_2:63;
      end;
      thus for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A4);
end;

theorem Th4:
   for F,G,H being Function of NAT,ExtREAL st
   for n being Element of NAT holds F.n = G.n + H.n holds
   G is nonnegative & H is nonnegative implies
   SUM(F) <= SUM(G) + SUM(H)
proof
   let F,G,H be Function of NAT,ExtREAL;
   assume
A1:for n being Element of NAT holds F.n = G.n + H.n;
      assume
   A2:G is nonnegative & H is nonnegative;
      defpred P[Element of NAT] means Ser(F).$1 = Ser(G).$1 + Ser(H).$1;
      A3: P[0]
         proof
            Ser(F).0 = F.0 & Ser(G).0 = G.0 & Ser(H).0 = H.0
            by SUPINF_2:63;
            hence thesis by A1;
         end;
      A4: for k being Element of NAT st P[k] holds P[k+1]
         proof
            let k be Element of NAT;
            assume
         A5:Ser(F).k = Ser(G).k + Ser(H).k;
            A6:Ser(F).(k+1) = Ser(F).k + F.(k+1) &
            Ser(G).(k+1) = Ser(G).k + G.(k+1) &
            Ser(H).(k+1) = Ser(H).k + H.(k+1) by SUPINF_2:63;
            0. <= G.(k+1) & 0. <= H.(k+1) by A2,SUPINF_2:58;
         then 0. + 0. <= G.(k+1) + H.(k+1) by MEASURE1:4;
         then 0. <= G.(k+1) + H.(k+1) by SUPINF_2:18;
         then A7: 0. <= F.(k+1) by A1;
        A8:0. <= Ser(G).k & 0. <= G.(k+1) &
            0. <= Ser(H).k & 0. <= H.(k+1) & 0. <= Ser(H).(k+1)
            by A2,SUPINF_2:58,59;
        hence Ser(F).(k+1) = Ser(G).k + (Ser(H).k + F.(k+1))
                     by A5,A6,A7,MEASURE4:1
             .= Ser(G).k + (Ser(H).k + (G.(k+1) + H.(k+1))) by A1
             .= Ser(G).k + ((Ser(H).k + H.(k+1)) + G.(k+1))
                by A8,MEASURE4:1
             .= Ser(G).(k+1) + Ser(H).(k+1) by A6,A8,MEASURE4:1;
         end;
   A9:for n being Element of NAT holds P[n] from NAT_1:sch 1(A3,A4);
         SUM(G) + SUM(H) is majorant of rng Ser(F)
      proof
         let x be R_eal;
         assume
      A10:x in rng Ser(F);
            dom Ser(F) = NAT by FUNCT_2:def 1;
         then consider n being set such that
      A11:n in NAT & x = Ser(F).n by A10,FUNCT_1:def 5;
         reconsider n as Element of NAT by A11;
               Ser(G).n in rng Ser(G) & Ser(H).n in rng Ser(H)
            by FUNCT_2:6;
            then Ser(G).n <= sup(rng Ser(G)) & Ser(H).n <= sup(rng Ser(H))
            by SUPINF_1:76;
          then A12:Ser(G).n <= SUM(G) & Ser(H).n <= SUM(H) by SUPINF_2:def 23
;
            0. <= Ser(G).n & 0. <= Ser(H).n by A2,SUPINF_2:59;
         then Ser(G).n + Ser(H).n <= SUM(G) + SUM(H) by A12,MEASURE1:4;
       hence thesis by A9,A11;
      end;
       then sup(rng Ser(F)) <= SUM(G) + SUM(H) by SUPINF_1:def 16;
      hence thesis by SUPINF_2:def 23;
end;

canceled;

theorem Th6:
   for F,G being Function of NAT,ExtREAL holds
   (F is nonnegative & for n being Element of NAT holds F.n <= G.n) implies
   for n being Element of NAT holds (Ser(F)).n <= SUM(G)
proof
   let F,G be Function of NAT,ExtREAL;
   assume that
A1:F is nonnegative and
A2: for n being Element of NAT holds F.n <= G.n;
      let n be Element of NAT;
   A3:SUM(G) = sup(rng Ser(G)) by SUPINF_2:def 23;
   set y = Ser(G).n;
      dom Ser(G) = NAT & rng Ser(G) c= ExtREAL by FUNCT_2:def 1;
   then y in rng Ser(G) & Ser(F).n <= y by A1,A2,FUNCT_1:def 5,SUPINF_2:61;
  hence thesis by A3,SUPINF_1:97;
end;

theorem Th7:
   for F being Function of NAT,ExtREAL holds
   F is nonnegative implies
   for n being Element of NAT holds (Ser(F)).n <= SUM(F)
proof
  let F be Function of NAT,ExtREAL;
      for n being Element of NAT holds F.n <= F.n;
  hence thesis by Th6;
end;

definition
   let S be non empty Subset of NAT;
   let H be Function of S,NAT;
   let n be Element of S;
redefine func H.n -> Element of NAT;
correctness
proof
   H.n is Element of NAT;
   hence thesis;
end;
end;

definition
   let S be non empty set;
   let H be Function of S,ExtREAL;
func On H -> Function of NAT,ExtREAL means
:Def1:for n being Element of NAT holds
              (n in S implies it.n = H.n) &
              (not n in S implies it.n = 0.);
existence
proof
   defpred P[Element of NAT,Element of ExtREAL] means
        ($1 in S implies $2 = H.$1) &
        (not $1 in S implies $2 = 0.);
A1:for n being Element of NAT holds
   ex y being Element of ExtREAL st P[n,y]
   proof
      let n be Element of NAT;
      per cases;
      suppose n in S;
         then reconsider n as Element of S;
         take H.n;
         thus thesis;
      end;
      suppose
      A2:not n in S;
         take 0.;
         thus thesis by A2;
   end;
   end;
   consider GG being Function of NAT,ExtREAL such that
A3:for n being Element of NAT holds P[n,GG.n] from FUNCT_2:sch 3(A1);
   take GG;
   thus thesis by A3;
end;
uniqueness
proof
   let G1,G2 be Function of NAT,ExtREAL such that
A4:for n being Element of NAT holds
   ((n in S implies G1.n = H.n) &
   ((not n in S) implies G1.n = 0.)) and
A5:for n being Element of NAT holds
   ((n in S implies G2.n = H.n) &
   ((not n in S) implies G2.n = 0.));
    for n being set st n in NAT holds G1.n = G2.n
   proof
      let n be set;
      assume n in NAT;
      then reconsider n as Element of NAT;
      per cases;
      suppose
      A6:n in S;
      then G1.n = H.n by A4;
      hence thesis by A5,A6;
      end;
      suppose
      A7:not n in S;
      then G1.n = 0. by A4;
      hence thesis by A5,A7;
   end;
   end;
   hence thesis by FUNCT_2:18;
end;
end;

theorem Th8:
   for X being non empty set
   for G being Function of X,ExtREAL st G is nonnegative holds
   On G is nonnegative
proof let X be non empty set;
   let G be Function of X,ExtREAL;
   assume
A1:G is nonnegative;
      for n being Element of NAT holds 0. <= (On G).n
   proof
      let n be Element of NAT;
      per cases;
      suppose
      A2:n in X;
      then (On G).n = G.n by Def1;
      hence thesis by A1,A2,SUPINF_2:58;
      end;
      suppose not n in X;
         hence thesis by Def1;
   end;
   end;
   hence thesis by SUPINF_2:58;
end;

theorem Th9:
   for F being Function of NAT,ExtREAL st F is nonnegative holds
   for n,k being Element of NAT st n <= k holds Ser(F).n <= Ser(F).k
proof
   let F be Function of NAT,ExtREAL;
   assume
A1:F is nonnegative;
      let n,k be Element of NAT;
      assume n <= k;
      then consider s being Nat such that
      A2:k = n + s by NAT_1:10;
      reconsider s as Element of NAT by ORDINAL1:def 13;
      A3:k = n + s by A2;
   defpred P[Element of NAT] means Ser(F).n <= Ser(F).(n+$1);
   A4: P[0];
   A5: for k being Element of NAT st P[k] holds P[k+1]
      proof
         let k be Element of NAT;
         assume
      A6:Ser(F).n <= Ser(F).(n+k);
            Ser(F).(n+k) <= Ser(F).((n+k)+1) by A1,SUPINF_2:59;
       hence thesis by A6,XXREAL_0:2;
      end;
        for s being Element of NAT holds P[s] from NAT_1:sch 1(A4,A5);
    hence thesis by A3;
end;

theorem Th10:
   for k being Element of NAT holds
   for F being Function of NAT,ExtREAL holds
   ((for n being Element of NAT st n <> k holds F.n = 0.) implies
   ((for n being Element of NAT st n < k holds Ser(F).n = 0.) &
   (for n being Element of NAT st k <= n holds Ser(F).n = F.k)))
proof
   let k be Element of NAT;
   let F be Function of NAT,ExtREAL;
   assume
A1:for n being Element of NAT st n <> k holds F.n = 0.;
    defpred P[Element of NAT] means $1 < k implies Ser(F).$1 = 0.;
   A2: P[0]
      proof
         assume 0 < k;
      then F.0 = 0. by A1;
         hence thesis by SUPINF_2:63;
      end;
   A3: for n being Element of NAT st P[n] holds P[n+1]
      proof
         let n be Element of NAT;
         assume
      A4:n < k implies Ser(F).n = 0.;
         assume
      A5:n+1 < k;
A6:   n <= n+1 by NAT_1:11;
         A7:F.(n+1) = 0. by A1,A5;
            Ser(F).(n+1) = Ser(F).n + F.(n+1) by SUPINF_2:63
                     .= 0. by A4,A5,A6,A7,XXREAL_0:2,SUPINF_2:18;
         hence thesis;
      end;
A8:for n being Element of NAT holds P[n] from NAT_1:sch 1(A2,A3);
   defpred P[Element of NAT] means k <= $1 implies Ser(F).$1 = F.k;
   A9: P[0]
      proof
         assume k <= 0;
         then k <= 0 & 0 <= k by NAT_1:2;
      then F.0 = F.k by XXREAL_0:1;
         hence thesis by SUPINF_2:63;
      end;
   A10: for n being Element of NAT st P[n] holds P[n+1]
      proof
         let n be Element of NAT;
         assume
      A11:k <= n implies Ser(F).n = F.k;
        assume A12:k <= n+1;
        per cases by A12,NAT_1:8;
           suppose
           A13:k <= n;
              then k <> n+1 by NAT_1:13;
           then A14:F.(n+1) = 0. by A1;
                 Ser(F).(n+1) = Ser(F).n + F.(n+1) by SUPINF_2:63
                          .= F.k by A11,A13,A14,SUPINF_2:18;
              hence thesis;
           end;
           suppose
           A15:k = n + 1;
              then n < k by NAT_1:13;
           then A16:Ser(F).n = 0. by A8;
                 Ser(F).(n+1) = Ser(F).n + F.(n+1) by SUPINF_2:63
                          .= F.k by A15,A16,SUPINF_2:18;
              hence thesis;
      end;
      end;
      for n being Element of NAT holds P[n] from NAT_1:sch 1(A9,A10);
   hence thesis by A8;
end;

theorem Th11:
   for G being Function of NAT,ExtREAL st G is nonnegative holds
   for S being non empty Subset of NAT holds
   for H being Function of S,NAT st H is one-to-one holds
   SUM(On(G*H)) <= SUM(G)
proof
   let G be Function of NAT,ExtREAL;
   assume
A1:G is nonnegative;
   let S be non empty Subset of NAT;
   let H be Function of S,NAT;
   assume A2: H is one-to-one;
      defpred P[Element of NAT] means ex m being Element of NAT st
        for F being Function of NAT,ExtREAL st F is nonnegative holds
          Ser(On(F*H)).$1 <= Ser(F).m;
      A3: P[0]
         proof
            per cases;
            suppose
            A4:0 in S;
               then reconsider m = H.0 as Element of NAT by FUNCT_2:7;
               take m;
                  let F be Function of NAT,ExtREAL;
                  assume
               A5:F is nonnegative;
                  Ser(On(F*H)).0 = (On(F*H)).0 by SUPINF_2:63;
                  then Ser(On(F*H)).0 = (F*H).0 by A4,Def1
                       .= F.(H.0) by A4,FUNCT_2:21;
                  hence thesis by A5,Th2;
            end;
            suppose
           A6:not 0 in S;
               take m = 0;
                  let F be Function of NAT,ExtREAL;
                  assume
               A7:F is nonnegative;
                  Ser(On(F*H)).0 = (On(F*H)).0 by SUPINF_2:63;
                  then A8:Ser(On(F*H)).0 = 0. by A6,Def1;
              A9:0. <= F.0 by A7,SUPINF_2:58;
                     F.0 <= Ser(F).m by A7,Th2;
                  hence thesis by A8,A9,XXREAL_0:2;
         end;
         end;
      A10: for k being Element of NAT st P[k] holds P[k+1]
         proof
            let k be Element of NAT;
            given m0 being Element of NAT such that
         A11:for F being Function of NAT,ExtREAL st F is nonnegative holds
            Ser(On(F*H)).k <= Ser(F).m0;
            per cases;
            suppose
            A12:k+1 in S;
               then reconsider m1 = H.(k+1) as Element of NAT by FUNCT_2:7;
               set m = m0 + m1;
          A13: m0 <= m by NAT_1:11;
          A14: m1 <= m by NAT_1:11;
               take m;
                  let F be Function of NAT,ExtREAL;
                  assume
               A15:F is nonnegative;
                  defpred QQ0[Element of NAT,Element of ExtREAL] means
                       (($1 = H.(k+1) implies $2 = F.$1) &
                       ($1 <> H.(k+1) implies $2 = 0.));
               A16:for n being Element of NAT holds
                  ex y being Element of ExtREAL st QQ0[n,y]
                  proof
                     let n be Element of NAT;
                     per cases;
                     suppose
                     A17:n = H.(k+1);
                        take F.n;
                        thus thesis by A17;
                     end;
                     suppose
                     A18:n <> H.(k+1);
                        take 0.;
                        thus thesis by A18;
                  end;
                  end;
                  consider G0 being Function of NAT,ExtREAL such that
               A19:for n being Element of NAT holds
                  QQ0[n,G0.n] from FUNCT_2:sch 3(A16);
                     for n being Element of NAT holds 0. <= G0.n
                  proof
                     let n be Element of NAT;
                     per cases;
                        suppose n = H.(k+1);
                        then G0.n = F.n by A19;
                           hence thesis by A15,SUPINF_2:58;
                        end;
                        suppose not n = H.(k+1);hence thesis by A19;
                  end;
                  end;
              then A20:G0 is nonnegative by SUPINF_2:58;
                  defpred QQ1[Element of NAT,Element of ExtREAL] means
                       (($1 <> H.(k+1) implies $2 = F.$1) &
                       ($1 = H.(k+1) implies $2 = 0.));
               A21:for n being Element of NAT holds
                  ex y being Element of ExtREAL st QQ1[n,y]
                  proof
                     let n be Element of NAT;
                     per cases;
                     suppose
                     A22:n <> H.(k+1);
                        take F.n;
                        thus thesis by A22;
                     end;
                     suppose
                     A23:n = H.(k+1);
                        take 0.;
                        thus thesis by A23;
                  end;
                  end;
                  consider G1 being Function of NAT,ExtREAL such that
               A24:for n being Element of NAT holds
                  QQ1[n,G1.n] from FUNCT_2:sch 3(A21);
                     for n being Element of NAT holds 0. <= G1.n
                  proof
                     let n be Element of NAT;
                     per cases;
                        suppose n <> H.(k+1);
                        then G1.n = F.n by A24;
                           hence thesis by A15,SUPINF_2:58;
                        end;
                        suppose n = H.(k+1);hence thesis by A24;
                  end;
                  end;
              then A25:G1 is nonnegative by SUPINF_2:58;
                  reconsider GG0 = On(G0*H) as Function of NAT,ExtREAL;
                     G0*H is nonnegative by A20,MEASURE1:54;
                  then A26:GG0 is nonnegative by Th8;
                      reconsider n = k+1 as Element of S by A12;
A27:                GG0.n = (G0*H).n by Def1
                          .= G0.(H.n) by FUNCT_2:21
                          .= F.(H.n) by A19;
              A28:for n being Element of NAT holds
                   n <> k+1 implies GG0.n = 0.
                  proof
                     let n be Element of NAT;
                        assume
                     A29:n <> k+1;
                        per cases;
                           suppose
                           A30:n in S;
                           then A31:GG0.n = (G0*H).n by Def1
                                .= G0.(H.n) by A30,FUNCT_2:21;
                              reconsider n as Element of S by A30;
                                 not H.n = H.(k+1) by A2,A12,A29,FUNCT_2:25;
                            hence thesis by A19,A31;
                           end;
                           suppose not n in S;
                              hence thesis by Def1;
                  end;
                  end;
                  set GG1 = On(G1*H);
                     G1*H is nonnegative by A25,MEASURE1:54;
                  then A32:GG1 is nonnegative by Th8;
              A33: GG1.(k+1) = (G1*H).n by Def1
                         .= G1.(H.n) by FUNCT_2:21
                         .= 0. by A24;
              A34:for n being Element of NAT holds
                   n <> k+1 & n in S implies GG1.n = F.(H.n)
                  proof
                     let n be Element of NAT;
                        assume
                     A35:n <> k+1 & n in S;
                     then A36:GG1.n = (G1*H).n by Def1
                            .= G1.(H.n) by A35,FUNCT_2:21;
                        reconsider n as Element of S by A35;
                           not H.n = H.(k+1) by A2,A12,A35,FUNCT_2:25;
                        hence thesis by A24,A36;
                  end;
               A37:for n being Element of NAT holds On(F*H).n = GG0.n + GG1.n
                  proof
                     let n be Element of NAT;
                     per cases;
                     suppose
                     A38:n = k+1;
                        then On(F*H).n = (F*H).n by A12,Def1
                                 .= F.(H.n) by A12,A38,FUNCT_2:21
                                 .= GG0.n + GG1.n by A27,A33,A38,SUPINF_2:18;
                        hence thesis;
                     end;
                     suppose
                     A39:n <> k+1;
                           now
                        per cases;
                           suppose
                          A40:n in S;
                          A41:GG0.n = 0. by A28,A39;
                          A42:GG1.n = F.(H.n) by A34,A39,A40;
                                 On(F*H).n = (F*H).n by A40,Def1
                                     .= F.(H.n) by A40,FUNCT_2:21
                                     .= GG0.n + GG1.n by A41,A42,SUPINF_2:18;
                              hence thesis;
                           end;
                           suppose
                          A43:not n in S;
                          A44:GG0.n = 0. by A28,A39;
                          A45:GG1.n = 0. by A43,Def1;
                                 On(F*H).n = 0. by A43,Def1
                                     .= GG0.n + GG1.n by A44,A45,SUPINF_2:18;
                              hence thesis;
                        end;
                        end;
                        hence thesis;
                  end;
                  end;
                     A46:(Ser GG0).(k+1) = F.(H.(k+1)) by A27,A28,Th10;
                     A47:(Ser G0).m = G0.(H.(k+1)) by A14,A19,Th10;
                     set v = H.n;
A48:             G0.v = F.v by A19;
A49:                 (Ser GG1).(k+1) = (Ser GG1).k + GG1.(k+1) by SUPINF_2:63
                                 .= (Ser On(G1*H)).k by A33,SUPINF_2:18;
                 A50:(Ser On(G1*H)).k <= Ser(G1).m0 by A11,A25;
                        Ser(G1).m0 <= Ser(G1).m by A13,A25,Th9;
                 then A51:(Ser GG1).(k+1) <= (Ser G1).m by A49,A50,XXREAL_0:2
;
A52:                  0. <= (Ser GG0).(k+1) &
                    0. <= (Ser GG1).(k+1) by A26,A32,SUPINF_2:59;
                  for m being Element of NAT holds
               F.m = G0.m + G1.m
                  proof
                     let m be Element of NAT;
                     per cases;
                     suppose
                    A53:m = H.(k+1);
                    then A54:G0.m = F.m by A19;
                       G1.m = 0.by A24,A53;
                        hence thesis by A54,SUPINF_2:18;
                     end;
                     suppose
                    A55:m <> H.(k+1);
                    then A56:G0.m = 0. by A19;
                       G1.m = F.m by A24,A55;
                   hence thesis by A56,SUPINF_2:18;
                  end;
                  end;
                then (Ser On(F*H)).(k+1) = (Ser GG0).(k+1) + (Ser GG1).(k+1) &
                 (Ser F).m = (Ser G0).m + (Ser G1).m
                   by A20,A25,A26,A32,A37,Th3;
               hence thesis by A46,A47,A48,A51,A52,MEASURE1:4;
            end;
            suppose
            A57:not k+1 in S;
               take m0;
                  let F be Function of NAT,ExtREAL;
                  assume
               A58:F is nonnegative;
               A59:On(F*H).(k+1) = 0. by A57,Def1;
              Ser(On(F*H)).(k+1) = Ser(On(F*H)).k + (On(F*H)).(k+1) by
SUPINF_2:63
                                    .= Ser(On(F*H)).k by A59,SUPINF_2:18;
                  hence thesis by A11,A58;
         end;
         end;
   A60:for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A10);
         for x being R_eal st x in rng Ser(On(G*H)) holds
      (ex y being R_eal st y in rng Ser(G) & x <= y)
      proof
         let x be R_eal;
         assume
      A61:x in rng Ser(On(G*H));
            ex y being R_eal st y in rng Ser(G) & x <= y
         proof
            consider n being set such that
         A62:n in dom Ser(On(G*H)) & x = Ser(On(G*H)).n by A61,FUNCT_1:def 5;
            reconsider n as Element of NAT by A62;
            consider m being Element of NAT such that
         A63:for F being Function of NAT,ExtREAL st F is nonnegative holds
            Ser(On(F*H)).n <= Ser(F).m by A60;
            take Ser(G).m;
               dom Ser(G) = NAT & m in NAT by FUNCT_2:def 1;
          hence thesis by A1,A62,A63,FUNCT_1:def 5;
         end;
         hence thesis;
      end;
   then A64:sup(rng Ser(On(G*H))) <= sup(rng Ser(G)) by SUPINF_1:99;
      SUM(G) = sup(rng Ser(G)) by SUPINF_2:def 23;
      hence thesis by A64,SUPINF_2:def 23;
end;

theorem Th12:
   for F,G being Function of NAT,ExtREAL st
   F is nonnegative & G is nonnegative holds
   for S being non empty Subset of NAT holds
   for H being Function of S,NAT st H is one-to-one holds
   (for k being Element of NAT holds ((k in S implies F.k = (G*H).k) &
   ((not k in S) implies F.k = 0.))) implies SUM(F) <= SUM(G)
proof
   let F,G be Function of NAT,ExtREAL;
   assume
A1:F is nonnegative & G is nonnegative;
      let S be non empty Subset of NAT;
      let H be Function of S,NAT;
      assume
   A2:H is one-to-one;
      assume for k being Element of NAT holds ((k in S implies F.k = (G*H).k) &
         ((not k in S) implies F.k = 0.));
         then F = On(G*H) by Def1;
         hence thesis by A1,A2,Th11;
end;

::
::                       Outside Lebesque measure
::

Lm1: REAL c= REAL;
   then consider G0 being Function of NAT, bool REAL such that
Lm2: rng G0 = {REAL} by MEASURE1:34;
       REAL in {REAL} by TARSKI:def 1;
    then Lm3: REAL c= union rng G0 by Lm2,ZFMISC_1:92;
Lm4: for n being Element of NAT holds G0.n is Interval
      proof
         let n be Element of NAT;
           G0.n in {REAL} by Lm2,FUNCT_2:6; hence thesis
           by MEASURE6:35,TARSKI:def 1;
      end;

definition
   let A be Subset of REAL;
   mode Interval_Covering of A -> Function of NAT, bool REAL means
:Def2:A c= union(rng it) & for n being Element of NAT holds it.n is Interval;
existence
proof
   take G0;
  thus thesis by Lm3,Lm4,XBOOLE_1:1;
end;
end;

definition
   let A be Subset of REAL;
   let F be Interval_Covering of A;
   let n be Element of NAT;
redefine func F.n ->Interval;
correctness by Def2;
end;

definition
   let F be Function of NAT,bool REAL;
   mode Interval_Covering of F -> Function of NAT,Funcs(NAT,bool REAL) means
:Def3:for n being Element of NAT holds it.n is Interval_Covering of F.n;
existence
proof
   A1:for n being Element of NAT holds G0 is Interval_Covering of F.n
      proof
         let n be Element of NAT;
             F.n c= union rng G0 by Lm3,XBOOLE_1:1;
         hence thesis by Def2,Lm4;
      end;
      reconsider G = G0 as Element of Funcs(NAT,bool REAL) by FUNCT_2:11;
      reconsider H = NAT --> G as Function of NAT,Funcs(NAT,bool REAL)
             by FUNCOP_1:57;
   A2:for n being Element of NAT holds H.n = G by FUNCOP_1:13;
   A3:for n being Element of NAT holds H.n is Interval_Covering of F.n
      proof
         let n be Element of NAT;
            H.n = G by A2;
         hence thesis by A1;
      end;
      take H;
      thus thesis by A3;
end;
end;

definition
   let A be Subset of REAL;
   let F be Interval_Covering of A;
  deffunc F(Element of NAT) = vol(F.$1);
   func F vol -> Function of NAT,ExtREAL means
:Def4:for n being Element of NAT holds it.n = vol(F.n);
existence
 proof
  thus ex G being Function of NAT,ExtREAL st
   for n being Element of NAT holds G.n = F(n) from FUNCT_2:sch 4;
 end;
uniqueness
 proof
   thus for G1,G2 being Function of NAT,ExtREAL st
    (for n being Element of NAT holds G1.n = F(n)) &
    (for n being Element of NAT holds G2.n = F(n))
   holds G1=G2 from BINOP_2:sch 1;
 end;
end;

theorem Th13:
   for A being Subset of REAL holds
   for F being Interval_Covering of A holds
   (F vol) is nonnegative
proof
   let A be Subset of REAL;
   let F be Interval_Covering of A;
      (F vol) is nonnegative
   proof
     for n being Element of NAT holds 0. <= (F vol).n
      proof
         let n be Element of NAT;
         (F vol).n = vol(F.n) by Def4;
         hence thesis by MEASURE5:63;
      end;
      hence thesis by SUPINF_2:58;
   end;
   hence thesis;
end;

definition
   let F be Function of NAT, bool REAL;
   let H be Interval_Covering of F;
   let n be Element of NAT;
redefine func H.n -> Interval_Covering of F.n;
correctness by Def3;
end;

definition
   let F be Function of NAT, bool REAL;
   let G be Interval_Covering of F;
   func G vol -> Function of NAT,Funcs(NAT,ExtREAL) means
   for n being Element of NAT holds it.n = (G.n) vol;
existence
proof
 defpred P[Element of NAT,Element of Funcs(NAT,ExtREAL)] means $2 = (G.$1) vol;
A1:for n being Element of NAT holds
   ex y being Element of Funcs(NAT,ExtREAL) st P[n,y]
   proof
      let n be Element of NAT;
         (G.n) vol is Element of Funcs(NAT,ExtREAL) by FUNCT_2:11;
    hence thesis;
   end;
      ex G0 being Function of NAT,Funcs(NAT,ExtREAL) st
   for n being Element of NAT holds P[n,G0.n] from FUNCT_2:sch 3(A1);
 hence thesis;
end;
uniqueness
 proof
  deffunc F(Element of NAT) = (G.$1) vol;
   thus
     for F1,F2 be Function of NAT,Funcs(NAT,ExtREAL) st
      (for n being Element of NAT holds F1.n = F(n)) &
      (for n being Element of NAT holds F2.n = F(n))
     holds F1 = F2 from BINOP_2:sch 1;
  end;
end;

definition
   let A be Subset of REAL;
   let F be Interval_Covering of A;
   func vol F -> R_eal equals
 SUM(F vol);
correctness;
end;

definition
   let F be Function of NAT,(bool REAL);
   let G be Interval_Covering of F;
   func vol(G) -> Function of NAT,ExtREAL means
:Def7:for n being Element of NAT holds it.n = vol(G.n);
existence
proof
   defpred P[Element of NAT,Element of ExtREAL] means
        $2 = vol(G.$1);
A1:for n being Element of NAT holds
   ex y being Element of ExtREAL st P[n,y];
   consider G0 being Function of NAT,ExtREAL such that
A2:for n being Element of NAT holds P[n,G0.n]
   from FUNCT_2:sch 3(A1);
   take G0;
   thus thesis by A2;
end;
uniqueness
 proof
   deffunc F(Element of NAT) = vol(G.$1);
   thus for F1,F2 being Function of NAT,ExtREAL st
    (for n being Element of NAT holds F1.n = F(n)) &
    (for n being Element of NAT holds F2.n = F(n))
   holds F1 = F2 from BINOP_2:sch 1;
 end;
end;

theorem Th14:
   for F being Function of NAT,(bool REAL) holds
   for G being Interval_Covering of F holds
   for n being Element of NAT holds 0. <= (vol(G)).n
proof
   let F be Function of NAT, bool REAL;
   let G be Interval_Covering of F;
   let n be Element of NAT;
A1:(vol(G)).n = vol(G.n) by Def7;
       for k being Element of NAT holds 0. <= ((G.n) vol).k
    proof
       let k be Element of NAT;
       0. <= vol((G.n).k) by MEASURE5:63;
       hence thesis by Def4;
    end;
  then ((G.n) vol) is nonnegative by SUPINF_2:58;
 hence thesis by A1,MEASURE6:2;
end;

definition
   let A be Subset of REAL;
 defpred P[set] means ex F being Interval_Covering of A st $1 = vol(F);
   func Svc(A) -> Subset of ExtREAL means
:Def8:for x being R_eal holds x in it iff
       ex F being Interval_Covering of A st x = vol(F);
existence
proof
   consider D being set such that
A1: for x being set holds x in D iff x in ExtREAL & P[x] from XBOOLE_0:sch 1;
       for z being set holds z in D implies z in ExtREAL by A1;
    then reconsider D as Subset of ExtREAL by TARSKI:def 3;
   take D;
   thus thesis by A1;
end;
uniqueness
 proof
   thus for D1,D2 being Subset of ExtREAL st
     (for x being R_eal holds x in D1 iff P[x]) &
     (for x being R_eal holds x in D2 iff P[x])
    holds D1 = D2 from SUBSET_1:sch 2;
 end;
end;

registration
   let A be Subset of REAL;
   cluster Svc(A) -> non empty;
coherence
proof
   defpred P[set] means ex F being Interval_Covering of A st $1 = vol(F);
   consider D being set such that
A1:for x being set holds x in D iff x in ExtREAL & P[x] from XBOOLE_0:sch 1;
  consider F0 being Function of NAT, bool REAL such that
A2: rng F0 = {REAL,{}} & F0.0 = REAL &
    for n being Element of NAT st 0 < n holds F0.n = {} by Lm1,MEASURE1:40;
A3:   union{REAL,{}} = REAL \/ {} by ZFMISC_1:93;
    for n being Element of NAT holds F0.n is Interval
      proof
         let n be Element of NAT;
            n = 0 or 0 < n by NAT_1:3;
       hence thesis by A2,MEASURE6:35;
      end;
   then reconsider F0 as Interval_Covering of A by A2,A3,Def2;
       vol F0 in D by A1;
   hence thesis by Def8;
end;
end;

definition
   let A be Subset of REAL;
   func COMPLEX(A) -> Element of ExtREAL equals
 inf(Svc(A));
correctness;
end;

definition
   func OS_Meas -> Function of bool REAL,ExtREAL means
:Def10:for A being Subset of REAL holds it.A = inf(Svc(A));
existence
proof
   consider G being Function of bool REAL,ExtREAL such that
A1:for A being Subset of REAL holds G.A = COMPLEX(A) from FUNCT_2:sch 4;
A2:for A being Subset of REAL holds G.A = inf(Svc(A))
   proof
      let A be Subset of REAL;
         G.A = COMPLEX(A) by A1;
      hence thesis;
   end;
   take G;
   thus thesis by A2;
end;
uniqueness
 proof
   deffunc F(Subset of REAL) = inf(Svc($1));
   thus for F1,F2 being Function of bool REAL,ExtREAL st
    (for A being Subset of REAL holds F1.A = F(A)) &
    (for A being Subset of REAL holds F2.A = F(A))
    holds F1 = F2 from BINOP_2:sch 1;
 end;
end;

definition
   let H be Function of NAT,[:NAT,NAT:];
 redefine func pr1(H) -> Function of NAT,NAT means
    for n being Element of NAT holds
       ex s being Element of NAT st H.n = [it.n,s];
 coherence
  proof
   thus pr1 H is Function of NAT,NAT;
  end;
 compatibility
  proof let IT be Function of NAT,NAT;
A1:  dom pr1 H = dom H by MCART_1:def 12;
    A2:  dom pr1 H = NAT by FUNCT_2:def 1;
   hereby assume
A3:   IT = pr1 H;
    let n be Element of NAT;
A4:   IT.n = (H.n)`1 by A1,A2,A3,MCART_1:def 12;
     reconsider s = (H.n)`2 as Element of NAT;
    take s;
    thus H.n = [IT.n,s] by A4,MCART_1:24;
   end;
   assume
A5:  for n being Element of NAT holds
       ex s being Element of NAT st H.n = [IT.n,s];
A6:   for x being set st x in dom H holds IT.x = (H.x)`1
    proof let x be set;
     assume x in dom H;
      then reconsider n = x as Element of NAT;
         ex s being Element of NAT st H.n = [IT.n,s] by A5;
     hence IT.x = (H.x)`1 by MCART_1:7;
    end;
       dom IT = dom H by A1,A2,FUNCT_2:def 1;
   hence IT = pr1 H by A6,MCART_1:def 12;
  end;
end;

definition
   let H be Function of NAT,[:NAT,NAT:];
 redefine func pr2(H) -> Function of NAT,NAT means
:Def12:for n being Element of NAT holds
       H.n = [pr1(H).n,it.n];
 coherence
  proof
   thus pr2 H is Function of NAT,NAT;
  end;
 compatibility
  proof let IT be Function of NAT,NAT;
A1:  dom pr2 H = dom H by MCART_1:def 13;
    A2:  dom pr2 H = NAT by FUNCT_2:def 1;
   hereby assume
A3:   IT = pr2 H;
    let n be Element of NAT;
A4:   IT.n = (H.n)`2 by A1,A2,A3,MCART_1:def 13;
        (pr1 H).n = (H.n)`1 by A1,A2,MCART_1:def 12;
    hence H.n = [(pr1 H).n,IT.n] by A4,MCART_1:24;
   end;
   assume
A5:  for n being Element of NAT holds H.n = [(pr1 H).n,IT.n];
A6:   for x being set st x in dom H holds IT.x = (H.x)`2
    proof let x be set;
     assume x in dom H;
      then reconsider n = x as Element of NAT;
         H.n = [(pr1 H).n,IT.n] by A5;
     hence IT.x = (H.x)`2 by MCART_1:7;
    end;
       dom IT = dom H by A1,A2,FUNCT_2:def 1;
   hence IT = pr2 H by A6,MCART_1:def 13;
  end;
end;

definition
   let F be Function of NAT,bool REAL;
   let G be Interval_Covering of F;
   let H be Function of NAT,[:NAT,NAT:] such that
A1: rng H = [:NAT,NAT:];
func On(G,H) -> Interval_Covering of union rng F means
:Def13:for n being Element of NAT holds
              it.n = (G.(pr1(H).n)).(pr2(H).n);
existence
proof
   defpred P[Element of NAT,Subset of REAL] means
        $2 = (G.(pr1(H).$1)).(pr2(H).$1);
A2:for n being Element of NAT holds
   ex y being Subset of REAL st P[n,y];
   consider GG being Function of NAT,bool REAL such that
A3:for n being Element of NAT holds P[n,GG.n] from FUNCT_2:sch 3(A2);
      GG is Interval_Covering of union rng F
   proof
     A4:union rng F c= union(rng GG)
         proof
            let x be set;
            assume x in union rng F;
            then consider A being set such that
         A5:x in A & A in rng F by TARSKI:def 4;
            consider n1 being set such that
         A6:n1 in dom F & A = F.n1 by A5,FUNCT_1:def 5;
            reconsider n1 as Element of NAT by A6;
               F.n1 c= union rng(G.n1) by Def2;
            then consider AA being set such that
         A7:x in AA & AA in rng(G.n1) by A5,A6,TARSKI:def 4;
            consider n2 being set such that
        A8:n2 in dom(G.n1) & AA = (G.n1).n2 by A7,FUNCT_1:def 5;
            reconsider n2 as Element of NAT by A8;
               [n1,n2] in rng H by A1;
            then consider n being set such that
        A9:n in dom(H) & [n1,n2] = H.n by FUNCT_1:def 5;
            reconsider n as Element of NAT by A9;
               [pr1(H).n,pr2(H).n] = [n1,n2] by A9,Def12;
            then pr1(H).n = n1 & pr2(H).n = n2 by ZFMISC_1:33;
            then x in GG.n & GG.n in rng GG by A3,A7,A8,FUNCT_2:6;
          hence thesis by TARSKI:def 4;
         end;
         for n being Element of NAT holds GG.n is Interval
      proof
            let n be Element of NAT;
            GG.n = (G.(pr1(H).n)).(pr2(H).n) by A3;
            hence thesis;
      end;
      hence thesis by A4,Def2;
   end;
   then reconsider GG as Interval_Covering of union rng F;
   take GG;
   thus thesis by A3;
end;
uniqueness
proof
   let G1,G2 be Interval_Covering of union rng F such that
A10:for n being Element of NAT holds G1.n = (G.(pr1(H).n)).(pr2(H).n) and
A11:for n being Element of NAT holds G2.n = (G.(pr1(H).n)).(pr2(H).n);
  for n being set st n in NAT holds G1.n = G2.n
   proof
      let n be set;
      assume n in NAT;
      then reconsider n as Element of NAT;
      G1.n = (G.(pr1(H).n)).(pr2(H).n) by A10;
   hence thesis by A11;
   end;
   hence thesis by FUNCT_2:18;
end;
end;

   reconsider D = NAT --> {} as Function of NAT,bool REAL by FUNCOP_1:57;
Lm5: for n being Element of NAT holds D.n = {} by FUNCOP_1:13;

theorem Th15:
   for H being Function of NAT,[:NAT,NAT:] st
   H is one-to-one & rng H = [:NAT,NAT:] holds
   for k being Element of NAT holds
   ex m being Element of NAT st
   for F being Function of NAT,bool REAL holds
   for G being Interval_Covering of F holds
   Ser((On(G,H)) vol).k <= Ser(vol(G)).m
proof
   let H be Function of NAT,[:NAT,NAT:];
   assume that
A1:H is one-to-one and
A2: rng H = [:NAT,NAT:];
   reconsider y = D as Element of Funcs(NAT,bool REAL) by FUNCT_2:11;
   defpred P[Element of NAT] means
      ex m being Element of NAT st
       for F being Function of NAT,bool REAL holds
        for G being Interval_Covering of F holds
         Ser((On(G,H)) vol).($1) <= Ser(vol(G)).m;
   A3:  P[0]
      proof
       take m = pr1(H).0;
            let F be Function of NAT,bool REAL;
            let G be Interval_Covering of F;
            reconsider GG = On(G,H) as Interval_Covering of union rng F;
         A4:Ser(GG vol).0 = (GG vol).0 by SUPINF_2:63;
               (GG vol).0 = vol(GG.0) &
            ((G.(pr1(H).0)) vol).(pr2(H).0) = vol((G.(pr1(H).0)).(pr2(H).0))
            by Def4;
        then A5:(GG vol).0 <= ((G.(pr1(H).0)) vol).(pr2(H).0) by A2,Def13;
          ((G.(pr1(H).0))) vol is nonnegative by Th13;
            then (GG vol).0 <= SUM((G.(pr1(H).0)) vol)
            by A5,MEASURE6:3;
            then (GG vol).0 <= vol(G.(pr1(H).0));
        then A6:(GG vol).0 <= (vol(G)).(pr1(H).0) by Def7;
               for n being Element of NAT holds 0. <= (vol(G)).n by Th14;
            then vol(G) is nonnegative by SUPINF_2:58;
            then (vol(G)).m <= Ser(vol(G)).m by Th2;
           hence thesis by A4,A6,XXREAL_0:2;
      end;
   A7: for k being Element of NAT st P[k] holds P[k+1]
      proof
         let k be Element of NAT;
         given m0 being Element of NAT such that
      A8: for F being Function of NAT,bool REAL holds
         for G being Interval_Covering of F holds
         Ser((On(G,H)) vol).k <= Ser(vol(G)).m0;
         take m = m0 + pr1(H).(k+1);
            let F be Function of NAT,bool REAL;
            let G be Interval_Covering of F;
            defpred QQ0[Element of NAT,Element of Funcs(NAT,bool REAL)] means
                 (($1 = pr1(H).(k+1) implies
                 for m being Element of NAT holds $2.m = (G.$1).m) &
                 ($1 <> pr1(H).(k+1) implies
                 for m being Element of NAT holds $2.m = {}));
        A9:for n being Element of NAT holds
            ex y being Element of Funcs(NAT,bool REAL) st QQ0[n,y]
            proof
               let n be Element of NAT;
               per cases;
                  suppose
                 A10:n = pr1(H).(k+1);
                     reconsider y = G.n as Element of Funcs(NAT,bool REAL)
                        by FUNCT_2:11;
                     take y;
                     thus thesis by A10;
                  end;
                  suppose
                 A11:n <> pr1(H).(k+1);
                     take y;
                     thus thesis by Lm5,A11;
            end;
            end;
            consider G0 being Function of NAT,Funcs(NAT,bool REAL) such that
        A12:for n being Element of NAT holds
            QQ0[n,G0.n] from FUNCT_2:sch 3(A9);
            defpred QQ1[Element of NAT,Element of Funcs(NAT,bool REAL)] means
                 (($1 <> pr1(H).(k+1) implies
                 for m being Element of NAT holds $2.m = (G.$1).m) &
                 ($1 = pr1(H).(k+1) implies
                 for m being Element of NAT holds $2.m = {}));
        A13:for n being Element of NAT holds
            ex y being Element of Funcs(NAT,bool REAL) st QQ1[n,y]
            proof
               let n be Element of NAT;
               per cases;
                  suppose
                 A14:n <> pr1(H).(k+1);
                     reconsider y = G.n as Element of Funcs(NAT,bool REAL)
                         by FUNCT_2:11;
                     take y;
                     thus thesis by A14;
                  end;
                  suppose
                 A15:n = pr1(H).(k+1);
                     take y;
                     thus thesis by Lm5,A15;
            end;
            end;
            consider G1 being Function of NAT,Funcs(NAT,bool REAL) such that
        A16:for n being Element of NAT holds
            QQ1[n,G1.n] from FUNCT_2:sch 3(A13);
               for n being Element of NAT holds
            G0.n is Interval_Covering of D.n
            proof
               let n be Element of NAT;
               consider f0 being Function such that
            A17:G0.n = f0 & dom f0 = NAT & rng f0 c= bool REAL
               by FUNCT_2:def 2;
               reconsider f0 as Function of NAT,bool REAL by A17,FUNCT_2:4;
                  D.n = {} by Lm5;
            then A18:D.n c= union(rng f0) by XBOOLE_1:2;
                  for s being Element of NAT holds f0.s is Interval
               proof
                  let s be Element of NAT;
                  per cases;
                     suppose n = pr1(H).(k+1);
                     then f0.s = (G.n).s by A12,A17;
                     hence thesis;
                     end;
                     suppose n <> pr1(H).(k+1); hence thesis by A12,A17;
               end;
               end;
               then reconsider f0 as Interval_Covering of D.n
               by A18,Def2;
                  G0.n = f0 by A17;
               hence thesis;
            end;
            then reconsider G0 as Interval_Covering of D by Def3;
            set GG0 = On(G0,H);
               for n being Element of NAT holds
            G1.n is Interval_Covering of D.n
            proof
               let n be Element of NAT;
               consider f0 being Function such that
            A19:G1.n = f0 & dom f0 = NAT & rng f0 c= bool REAL
               by FUNCT_2:def 2;
               reconsider f0 as Function of NAT,bool REAL by A19,FUNCT_2:4;
                  D.n = {} by Lm5;
            then A20:D.n c= union(rng f0) by XBOOLE_1:2;
                  for s being Element of NAT holds f0.s is Interval
               proof
                  let s be Element of NAT;
                  per cases;
                     suppose n <> pr1(H).(k+1);
                     then f0.s = (G.n).s by A16,A19;
                        hence thesis;
                     end;
                     suppose n = pr1(H).(k+1); hence thesis by A16,A19;
               end;
               end;
               then reconsider f0 as Interval_Covering of D.n by A20,Def2;
                  G1.n = f0 by A19;
               hence thesis;
            end;
            then reconsider G1 as Interval_Covering of D by Def3;
            set GG1 = On(G1,H);
         A21:for n being Element of NAT
          holds (vol(G)).n = (vol(G0)).n + (vol(G1)).n
            proof
               let n be Element of NAT;
            A22:(vol(G)).n = vol(G.n) &
               (vol(G0)).n = vol(G0.n) &
               (vol(G1)).n = vol(G1.n) by Def7;
               vol(G.n) = vol(G0.n) + vol(G1.n)
               proof
               A23:(G.n) vol is nonnegative &
                   (G0.n) vol is nonnegative by Th13;
                  per cases;
                     suppose
                    A24:n = pr1(H).(k+1);
                    A25:SUM((G.n) vol) = SUM((G0.n) vol) + SUM((G1.n) vol)
                        proof
                       for s being Element of NAT holds ((G1.n) vol).s = 0.
                        proof
                           let s be Element of NAT;
                          vol((G1.n).s) = 0. by A16,A24,MEASURE5:60;
                           hence thesis by Def4;
                        end;
                     then A26:SUM((G1.n) vol) = 0. by Th1;
                              for s being Element of NAT holds
                           ((G0.n) vol).s <= ((G.n) vol).s
                           proof
                              let s be Element of NAT;
                                 ((G0.n) vol).s = vol((G0.n).s) by Def4
                                        .= vol((G.n).s) by A12,A24
                                        .= ((G.n) vol).s by Def4;
                              hence thesis;
                           end;
                       then A27:SUM((G0.n) vol) <= SUM((G.n) vol)
                           by A23,SUPINF_2:62;
                              for s being Element of NAT holds
                           ((G.n) vol).s <= ((G0.n) vol).s
                           proof
                              let s be Element of NAT;
                                 ((G0.n) vol).s = vol((G0.n).s) by Def4
                                        .= vol((G.n).s) by A12,A24
                                        .= ((G.n) vol).s by Def4;
                              hence thesis;
                           end;
                           then SUM((G.n) vol) <= SUM((G0.n) vol)
                           by A23,SUPINF_2:62;
                       then SUM((G.n) vol) = SUM((G0.n) vol)
                                            by A27,XXREAL_0:1;
                            hence thesis by A26,SUPINF_2:18;
                        end;
                        thus thesis by A25;
                     end;
                     suppose
                    A28:n <> pr1(H).(k+1);
                    A29:for s being Element of NAT holds
                        ((G1.n) vol).s = ((G.n) vol).s
                        proof
                           let s be Element of NAT;
                              ((G1.n) vol).s = vol((G1.n).s) &
                           ((G.n) vol).s = vol((G.n).s) by Def4;
                           hence thesis by A16,A28;
                        end;
                    A30:for s being Element of NAT holds ((G0.n) vol).s = 0.
                        proof
                           let s be Element of NAT;
                          vol((G0.n).s) = 0. by A12,A28,MEASURE5:60;
                           hence thesis by Def4;
                        end;
                    A31:SUM((G.n) vol) = SUM((G0.n) vol) + SUM((G1.n) vol)
                        proof
                       A32:(G.n) vol is nonnegative &
                           (G1.n) vol is nonnegative by Th13;
                       A33:SUM((G0.n) vol) = 0. by A30,Th1;
                              for s being Element of NAT holds
                           ((G1.n) vol).s <= ((G.n) vol).s by A29;
                       then A34:SUM((G1.n) vol) <= SUM((G.n) vol)
                           by A32,SUPINF_2:62;
                              for s being Element of NAT holds
                           ((G.n) vol).s <= ((G1.n) vol).s by A29;
                           then SUM((G.n) vol) <= SUM((G1.n) vol)
                           by A23,SUPINF_2:62;
                           then SUM((G.n) vol) = SUM((G1.n) vol)
                           by A34,XXREAL_0:1;
                            hence thesis by A33,SUPINF_2:18;
                        end;
                        thus thesis by A31;
               end;
               end;
               hence thesis by A22;
            end;
A35:          GG0 vol is nonnegative by Th13;
A36:       GG1 vol is nonnegative by Th13;
           A37:(Ser(GG0 vol)).(k+1) <= SUM(GG0 vol) by A35,Th7;
A38:   G0.(pr1(H).(k+1)) vol is nonnegative by Th13;
            set N0 = {s where s is Element of NAT : pr1(H).(k+1) = pr1(H).s};
A39:                N0 c= NAT
                  proof
                   let s1 be set;
                   assume s1 in N0;
                    then ex s being Element of NAT
                     st s = s1 & pr1(H).(k+1) = pr1(H).s;
                   hence thesis;
                  end;
                     k+1 in N0;
                  then reconsider N0 as non empty Subset of NAT by A39;
                 defpred SSS[Element of N0,Element of NAT] means
                      $2 = pr2(H).$1;
                 A40:for s being Element of N0 holds
                    ex y being Element of NAT st SSS[s,y];
                    consider SOS being Function of N0,NAT such that
                A41:for s being Element of N0 holds
                    SSS[s,SOS.s] from FUNCT_2:sch 3(A40);
                        for s1,s2 being set st
                     s1 in N0 & s2 in N0 & SOS.s1 = SOS.s2 holds s1 = s2
                     proof
                        let s1,s2 be set;
                        assume
                    A42:s1 in N0 & s2 in N0 & SOS.s1 = SOS.s2;
                        then reconsider s1,s2 as Element of NAT;
                        consider s11 being Element of NAT such that
                   A43:s11 = s1 & pr1(H).(k+1) = pr1(H).s11 by A42;
                        consider s22 being Element of NAT such that
                   A44:s22 = s2 & pr1(H).(k+1) = pr1(H).s22 by A42;
A45:                    SOS.s1 = pr2(H).s1 & SOS.s2 = pr2(H).s2 by A41,A42;
                           H.s1 = [pr1(H).s1,pr2(H).s1] &
                        H.s2 = [pr1(H).s2,pr2(H).s2] by Def12;
                      hence thesis by A1,A42,A43,A44,A45,FUNCT_2:25;
                     end;
                then A46: SOS is one-to-one by FUNCT_2:25;
                     for s being Element of NAT holds ((s in N0 implies
                  (GG0 vol).s = ((G0.(pr1(H).(k+1)) vol)*SOS).s) &
                  (not s in N0 implies (GG0 vol).s = 0.))
                  proof
                     let s be Element of NAT;
                   thus s in N0 implies
                     (GG0 vol).s = ((G0.(pr1(H).(k+1)) vol)*SOS).s
                     proof
                        assume
                    A47:s in N0;
                        then consider s1 being Element of NAT such that
                    A48:s1 = s & pr1(H).(k+1) = pr1(H).s1;
                    A49:pr2(H).s =SOS.s by A41,A47;
                           (GG0 vol).s = vol(GG0.s) by Def4
                                   .= vol((G0.(pr1(H).(k+1))).(pr2(H).s))
                                      by A2,A48,Def13
                                   .= (G0.(pr1(H).(k+1)) vol).(SOS.s)
                                      by A49,Def4
                                   .= ((G0.(pr1(H).(k+1)) vol)*SOS).s
                                      by A47,FUNCT_2:21;
                        hence thesis;
                     end;
                   assume not s in N0;
                    then A50:not pr1(H).(k+1) = pr1(H).s;
                       (GG0 vol).s = vol(GG0.s) by Def4
                               .= vol((G0.(pr1(H).s)).(pr2(H).s)) by A2,Def13
                               .= 0. by A12,A50,MEASURE5:60;
                   hence thesis;
                  end;
                then A51:SUM(GG0 vol) <= SUM(G0.(pr1(H).(k+1)) vol)
                   by A35,A38,A46,Th12;
              A52:SUM(G0.(pr1(H).(k+1)) vol) = vol(G0.(pr1(H).(k+1)))
                                   .= (vol(G0)).(pr1(H).(k+1)) by Def7;
                     for s being Element of NAT holds 0. <= (vol(G0)).s
                      by Th14;
              then A53:vol(G0) is nonnegative by SUPINF_2:58;
              then A54:(vol(G0)).(pr1(H).(k+1)) <= (Ser(vol(G0))).(pr1(H).(k+1
))
                  by Th2;
                 (Ser vol(G0)).(pr1(H).(k+1)) <= (Ser vol(G0)).m
                  by A53,SUPINF_2:60;
                then A55:SUM(G0.(pr1(H).(k+1)) vol) <= (Ser vol(G0)).m
                           by A52,A54,XXREAL_0:2;
                  (Ser(GG0 vol)).(k+1) <= SUM(G0.(pr1(H).(k+1)) vol)
               by A37,A51,XXREAL_0:2;
              then A56:(Ser (GG0 vol)).(k+1) <= (Ser vol(G0)).m by A55,
XXREAL_0:2;
           A57:(Ser(GG1 vol)).k <= (Ser vol(G1)).m0 by A8;
                   GG1.(k+1) = (G1.(pr1(H).(k+1))).(pr2(H).(k+1)) by A2,Def13
                          .= {} by A16;
                then A58: (GG1 vol).(k+1) = 0. by Def4,MEASURE5:60;
                  (Ser (GG1 vol)).(k+1) = (Ser (GG1 vol)).k + (GG1 vol).(k+1)
                  by SUPINF_2:63;
               then A59:             (Ser (GG1 vol)).(k+1) = (Ser (GG1 vol)).k
                 by A58,SUPINF_2:18;
                  for s being Element of NAT holds 0. <= (vol(G1)).s by Th14;
               then vol(G1) is nonnegative by SUPINF_2:58;
               then (Ser vol(G1)).m0 <= (Ser vol(G1)).m by SUPINF_2:60;
              then A60:(Ser (GG1 vol)).(k+1) <= (Ser vol(G1)).m by A57,A59,
XXREAL_0:2;
                  GG0 vol is nonnegative &
               GG1 vol is nonnegative by Th13;
then A61:          0. <= (Ser (GG0 vol)).(k+1) &
               0. <= (Ser (GG1 vol)).(k+1) by SUPINF_2:59;
                  for s being Element of NAT holds 0. <= (vol(G0)).s by Th14;
            then A62:vol(G0) is nonnegative by SUPINF_2:58;
                  for s being Element of NAT holds 0. <= (vol(G1)).s by Th14;
               then vol(G1) is nonnegative by SUPINF_2:58;
            then A63: (Ser vol(G)).m = (Ser vol(G0)).m + (Ser vol(G1)).m
             by A21,A62,Th3;
             for n being Element of NAT
              holds ((On(G,H)) vol).n = (GG0 vol).n + (GG1 vol).
n
            proof
               let n be Element of NAT;
            A64:((On(G,H)) vol).n = vol((On(G,H)).n) &
               (GG0 vol).n = vol(GG0.n) &
               (GG1 vol).n = vol(GG1.n) by Def4;
               then A65:((On(G,H)) vol).n = vol((G.(pr1(H).n)).(pr2(H).n))
                   by A2,Def13;
               per cases;
                  suppose
                 A66:pr1(H).n = pr1(H).(k+1);
A67:              GG0.n = (G0.(pr1(H).n)).(pr2(H).n) by A2,Def13
                          .= (G.(pr1(H).n)).(pr2(H).n) by A12,A66;
                    GG1.n = (G1.(pr1(H).n)).(pr2(H).n) by A2,Def13
                          .= {} by A16,A66;
                 hence thesis by A64,A65,A67,MEASURE5:60,SUPINF_2:18;
                  end;
                  suppose
                 A68:pr1(H).n <> pr1(H).(k+1);
A69:              GG1.n = (G1.(pr1(H).n)).(pr2(H).n) by A2,Def13
                          .= (G.(pr1(H).n)).(pr2(H).n) by A16,A68;
                    GG0.n = (G0.(pr1(H).n)).(pr2(H).n) by A2,Def13
                          .= {} by A12,A68;
                 hence thesis by A64,A65,A69,MEASURE5:60,SUPINF_2:18;
            end;
            end;
            then (Ser (On(G,H) vol)).(k+1) =
            (Ser (GG0 vol)).(k+1) + (Ser (GG1 vol)).(k+1)
                by A35,A36,Th3;
            hence thesis by A56,A60,A61,A63,MEASURE1:4;
      end;
  thus for k being Element of NAT holds P[k] from NAT_1:sch 1(A3,A7);
end;

theorem Th16:
   for F being Function of NAT,bool REAL holds
   for G being Interval_Covering of F holds
   inf Svc(union rng F) <= SUM(vol(G))
proof
   let F be Function of NAT,bool REAL;
   let G be Interval_Covering of F;
      consider H being Function of NAT,[:NAT,NAT:] such that
   A1:H is one-to-one & dom H = NAT & rng H = [:NAT,NAT:] by MEASURE6:1;
      set GG = On(G,H);
      set Q = vol(GG);
            SUM(GG vol) = sup(rng Ser(GG vol)) by SUPINF_2:def 23;
      then A2:vol(GG) = sup(rng Ser(GG vol));
      A3:SUM(vol(G)) = sup(rng Ser(vol(G))) by SUPINF_2:def 23;
            for x being R_eal st x in rng Ser(GG vol)
          ex y being R_eal st y in rng Ser(vol(G)) & x <= y
         proof
            let x be R_eal;
            assume x in rng Ser(GG vol);
             then consider n being set such that
           A4:n in dom Ser(GG vol) & x = Ser(GG vol).n by FUNCT_1:def 5;
               reconsider n as Element of NAT by A4;
               consider m being Element of NAT such that
           A5:for F being Function of NAT,bool REAL holds
               for G be Interval_Covering of F holds
               Ser((On(G,H)) vol).n <= Ser(vol(G)).m by A1,Th15;
               take Ser(vol(G)).m;
                  dom Ser(vol(G)) = NAT & m in NAT by FUNCT_2:def 1;
             hence thesis by A4,A5,FUNCT_1:def 5;
         end;
     then A6:   vol(GG) <= SUM(vol(G)) by A2,A3,SUPINF_1:99;
       Q in Svc(union rng F) by Def8;
    then inf Svc(union rng F) <= Q by SUPINF_1:79;
   hence thesis by A6,XXREAL_0:2;
end;

theorem Th17:
   OS_Meas is C_Measure of REAL
proof
A1:for A being Subset of REAL holds
   for F being Interval_Covering of A holds
   F vol is nonnegative
   proof
      let A be Subset of REAL;
      let F be Interval_Covering of A;
         for n being Element of NAT holds 0. <= (F vol).n
      proof
         let n be Element of NAT;
         (F vol).n = vol(F.n) by Def4;
         hence thesis by MEASURE5:63;
      end; hence thesis by SUPINF_2:58;
   end;
         A2: for A being Subset of REAL holds
         0.<= OS_Meas.A
         proof
            let A be Subset of REAL;
         A3:OS_Meas.A = inf(Svc(A)) by Def10;
               0. is minorant of Svc(A)
            proof
               let x be R_eal;
               assume x in Svc(A);
               then consider F being Interval_Covering of A such that
            A4:x = vol(F) by Def8;
               A5:F vol is nonnegative by A1;
               A6:SUM(F vol) = sup(rng Ser(F vol)) by SUPINF_2:def 23;
                  set y = Ser(F vol).0;
                 y in rng Ser(F vol) & 0. <= y by A5,FUNCT_2:6,SUPINF_2:59;
                  then 0. <= y & y <= sup(rng Ser(F vol)) by SUPINF_1:76;
               then 0. <= SUM(F vol) by A6,XXREAL_0:2;
               hence thesis by A4;
            end;
           hence thesis by A3,SUPINF_1:def 17;
         end;
     hence OS_Meas is nonnegative by MEASURE1:def 4;
      set G = D;
      A7:{} c= union(rng G) by XBOOLE_1:2;
            for n being Element of NAT holds G.n is Interval by Lm5;
         then reconsider G as Interval_Covering of {} by A7,Def2;
      A8:G vol is nonnegative by A1;
A9:   for r being Element of NAT holds 0 <= r implies (G vol).r = 0.
         proof
            let n be Element of NAT;
               vol(G.n) = vol({}) by Lm5;
            hence thesis by Def4,MEASURE5:60;
         end;
            vol(G) = Ser(G vol).0 by A8,A9,SUPINF_2:67
               .= (G vol).0 by SUPINF_2:63
               .= 0. by A9;
      then A10:0. in Svc({}) by Def8;
            0. is minorant of Svc {}
         proof
            let x be R_eal;
            assume
         A11:x in Svc({});
               0. <= OS_Meas.{} by A2;
            then 0. <= inf Svc({}) & inf Svc({}) <= x by A11,Def10,
SUPINF_1:79;
            hence thesis by XXREAL_0:2;
         end;
         then inf Svc {} = 0. by A10,SUPINF_1:81;
    hence OS_Meas.{} = 0. by Def10;
         let A,B be Subset of REAL;
         assume
     A12:A c= B;
A13:    Svc(B) c= Svc(A)
         proof
            let x be set;
            assume
         A14:x in Svc(B);
            then reconsider x as R_eal;
            consider F being Interval_Covering of B such that
         A15:x = vol(F) by A14,Def8;
A16:           B c= union(rng F) &
               for n being Element of NAT holds F.n is Interval by Def2;
               then A c= union(rng F) by A12,XBOOLE_1:1;
            then reconsider G = F as Interval_Covering of A by A16,Def2;
               x = vol(G)
            proof
A17:             for n being Element of NAT holds (G vol).n <= (F vol).n
               proof
                  let n be Element of NAT;
                     (G vol).n = vol(G.n) by Def4
                           .= (F vol).n by Def4;
                  hence thesis;
               end;
                  G vol is nonnegative by A1;
           then A18:SUM(G vol) <= SUM(F vol) by A17,SUPINF_2:62;
A19:             for n being Element of NAT holds (F vol).n <= (G vol).n
               proof
                  let n be Element of NAT;
                     (G vol).n = vol(G.n) by Def4
                           .= (F vol).n by Def4;
                  hence thesis;
               end;
             F vol is nonnegative by A1;
          then A20:      SUM(F vol) <= SUM(G vol) by A19,SUPINF_2:62;
             thus thesis by A15,A18,A20,XXREAL_0:1;
            end;
          hence thesis by Def8;
         end;
            OS_Meas.A = inf Svc(A) & OS_Meas.B = inf Svc(B) by Def10;
         hence OS_Meas.A <= OS_Meas.B by A13,SUPINF_1:94;
         let F be Function of NAT,bool REAL;
         per cases;
         suppose
        A21:for n being Element of NAT holds Svc(F.n) <> {+infty};
         A22:SUM(OS_Meas*F) = sup(rng Ser(OS_Meas*F)) by SUPINF_2:def 23;
           A23:for n being Element of NAT holds 0. <= (OS_Meas*F).n
               proof
                  let n be Element of NAT;
                 (OS_Meas*F).n = OS_Meas.(F.n) by FUNCT_2:21;
                  hence thesis by A2;
               end;
           then A24:OS_Meas*F is nonnegative by SUPINF_2:58;
               inf Svc(union rng F) <= sup(rng Ser(OS_Meas*F))
            proof
               assume
            A25:not inf Svc(union rng F) <= sup(rng Ser(OS_Meas*F));
               set y = inf Svc(union rng F), x = sup(rng Ser(OS_Meas*F));
              0. <= (OS_Meas*F).0 by A23;
               then A26:0. <= Ser(OS_Meas*F).0 by SUPINF_2:63;
                  Ser(OS_Meas*F).0 in rng Ser(OS_Meas*F) by FUNCT_2:6;
               then Ser(OS_Meas*F).0 <= x by SUPINF_1:76;
           then 0. <= x by A26,XXREAL_0:2;
               then consider eps being R_eal such that
           A27:0. < eps & x + eps < y & eps in REAL by A25,MEASURE6:25;
               consider E being Function of NAT,ExtREAL such that
           A28:(for n being Element of NAT holds 0. < E.n) & SUM(E) < eps
               by A27,MEASURE6:31;
               defpred P[Element of NAT,Element of Funcs(NAT,bool REAL)]
               means
                 ex F0 being Interval_Covering of F.$1 st
                 ($2 = F0 & (vol(F0) < inf Svc(F.$1) + E.$1));
             A29:for n being Element of NAT holds
               ex F0 being Element of Funcs(NAT,bool REAL) st P[n,F0]
               proof
                  let n be Element of NAT;
A30:        OS_Meas.(F.n) = inf Svc(F.n) & 0. <= OS_Meas.(F.n)
               by A2,Def10;
               Svc(F.n) <> {+infty} by A21;
            then not Svc(F.n) c= {+infty} by ZFMISC_1:39;
            then Svc(F.n) \ {+infty} <> {} by XBOOLE_1:37;
               then consider x being set such that
            A31:x in Svc(F.n) \ {+infty} by XBOOLE_0:def 1;
               reconsider x as R_eal by A31;
               A32: x in Svc(F.n) & not x in {+infty} by A31,XBOOLE_0:def 4;
            then A33:x in Svc(F.n) & x <> +infty by TARSKI:def 1;
            A34:inf Svc(F.n) <= x by A32,SUPINF_1:79;
                  x <= +infty by XXREAL_0:4;
               then x < +infty by A33,XXREAL_0:1;
                 then 0. is Real & 0. <= inf Svc(F.n) & inf Svc(F.n) <
+infty
                  by A30,A34,XXREAL_0:2,SUPINF_2:def 1;
                  then 0. < E.n & inf Svc(F.n) is Real by A28,MEASURE6:17;
                 then consider S being R_eal such that
              A35: S in Svc(F.n) & S < inf Svc(F.n) + E.n by MEASURE6:32;
                  consider FS being Interval_Covering of F.n such that
              A36:S = vol(FS) by A35,Def8;
                  reconsider FS as Element of Funcs(NAT,bool REAL)
                     by FUNCT_2:11;
                  take FS;
                  thus thesis by A35,A36;
               end;
               consider FF being Function of NAT,Funcs(NAT,bool REAL)
               such that
             A37:for n being Element of NAT holds P[n,FF.n]
               from FUNCT_2:sch 3(A29);
                for n being Element of NAT
                 holds FF.n is Interval_Covering of F.n
               proof
                  let n be Element of NAT;
                  consider F0 being Interval_Covering of F.n such that
               A38:F0 = FF.n & vol(F0) < inf Svc(F.n) + E.n by A37;
                  thus thesis by A38;
               end;
               then reconsider FF as Interval_Covering of F by Def3;
                     for n being Element of NAT holds 0. <= E.n by A28;
              then A39:E is nonnegative by SUPINF_2:58;
                  defpred P1[Element of NAT,Element of ExtREAL] means
                       $2 = (OS_Meas*F).$1 + E.$1;
              A40:for n being Element of NAT holds
                  ex y being Element of ExtREAL st P1[n,y];
                  consider G0 being Function of NAT,ExtREAL such that
              A41:for n being Element of NAT holds P1[n,G0.n]
                  from FUNCT_2:sch 3(A40);
              A42:SUM(G0) <= SUM(OS_Meas*F) + SUM(E) by A24,A39,A41,Th4;
              A43:for n being Element of NAT holds
                  (vol(FF)).n <= G0.n
                  proof
                     let n be Element of NAT;
                  consider F0 being Interval_Covering of F.n such that
               A44:F0 = FF.n & vol(F0) < inf Svc(F.n) + E.n by A37;
                  (OS_Meas*F).n = OS_Meas.(F.n) by FUNCT_2:21;
               then vol(FF.n) < (OS_Meas*F).n + E.n by A44,Def10;
                   then (vol(FF)).n < (OS_Meas*F).n + E.n by Def7;
                     hence thesis by A41;
                  end;
                     for n being Element of NAT holds 0. <= (vol(FF)).n
                      by Th14;
                  then vol(FF) is nonnegative by SUPINF_2:58;
              then A45:SUM(vol(FF)) <= SUM(G0) by A43,SUPINF_2:62;
                0. <= SUM(OS_Meas*F) & 0. <= SUM(E) by A24,A39,MEASURE6:2;
                  then SUM(OS_Meas*F) + SUM(E) <= SUM(OS_Meas*F) + eps
                  by A28,MEASURE1:4;
                  then SUM(G0) <= SUM(OS_Meas*F) + eps by A42,XXREAL_0:2;
               then A46:SUM (vol(FF)) <= SUM (OS_Meas*F) + eps by A45,
XXREAL_0:2;
                  y <= SUM vol(FF) by Th16;
              hence thesis by A22,A27,A46,XXREAL_0:2;
            end;
            hence thesis by A22,Def10;
         end;
         suppose ex n being Element of NAT st Svc(F.n) = {+infty};
          then consider n being Element of NAT such that
            A47:Svc(F.n) = {+infty};
               inf {+infty} = +infty by SUPINF_1:86; then
               OS_Meas.(F.n) = +infty by A47,Def10;
            then A48: (OS_Meas*F).n = +infty by FUNCT_2:21;
               for n being Element of NAT holds 0. <= (OS_Meas*F).n
            proof
               let n be Element of NAT;
               (OS_Meas*F).n = OS_Meas.(F.n) by FUNCT_2:21;
               hence thesis by A2;
            end;
            then OS_Meas*F is nonnegative by SUPINF_2:58;
            then SUM(OS_Meas*F) = +infty by A48,SUPINF_2:64;
            hence thesis by XXREAL_0:4;
end;
end;

definition
  redefine func OS_Meas -> C_Measure of REAL;
correctness by Th17;
end;

definition
  func Lmi_sigmaFIELD -> SigmaField of REAL equals
  sigma_Field(OS_Meas);
correctness;
end;

definition
  func L_mi -> sigma_Measure of Lmi_sigmaFIELD equals
 sigma_Meas(OS_Meas);
correctness;
end;

