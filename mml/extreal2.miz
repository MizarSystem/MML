:: Some Properties of Extended Real Numbers Operations: absolute
:: value, min and max
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received September 15, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies SUPINF_1, RLVECT_1, ARYTM, MEASURE6, GROUP_1, ARYTM_1, ARYTM_3,
      COMPLEX1, ABSVALUE, SQUARE_1;
 notations ORDINAL1, NUMBERS, XCMPLX_0, COMPLEX1, XXREAL_0, XREAL_0, REAL_1,
      SUPINF_1, SUPINF_2, MEASURE6, EXTREAL1, MESFUNC1;
 constructors REAL_1, COMPLEX1, SUPINF_2, MEASURE6, EXTREAL1, MESFUNC1,
      VALUED_1, PARTFUN1, SEQ_1;
 registrations NUMBERS, XXREAL_0, XREAL_0, MEMBERED, XBOOLE_0, VALUED_1,
      FUNCT_2;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions SUPINF_2, VALUED_1;
 theorems MEASURE6, REAL_2, SUPINF_2, AXIOMS, XREAL_0, EXTREAL1, MESFUNC1,
      ABSVALUE, XCMPLX_1, COMPLEX1, XREAL_1, XXREAL_0;

begin :: Preliminaries

reserve x,y,w,z for R_eal,
  a,b for Real;

canceled;

theorem :: extension of AXIOMS:19
  x <> +infty & x <> -infty implies ex y st x + y = 0
proof
  assume x <> +infty & x <> -infty;
  then reconsider a = x as Real by XXREAL_0:14;
  reconsider b = -a as Real;
A1: a + b = 0;
A2: R_EAL b = b by MEASURE6:def 1;
  take R_EAL b;
  thus thesis by A1,A2,SUPINF_2:1;
end;

theorem  :: extension of AXIOMS:20
  x <> +infty & x <> -infty & x <> 0 implies ex y st x*y = 1.
proof
  assume
A1: x <> +infty & x <> -infty & x <> 0;
  then reconsider a = x as Real by XXREAL_0:14;
  consider b being real number such that
A2: a * b = 1 by A1,AXIOMS:20;
  reconsider b as Real by XREAL_0:def 1;
A3: R_EAL b = b by MEASURE6:def 1;
  take R_EAL b;
  thus thesis by A2,A3,EXTREAL1:13,MESFUNC1:def 8;
end;

theorem Th4:
  1. * x = x & R_EAL 1 * x = x
proof
A1: R_EAL 1 = 1. by MEASURE6:def 1,MESFUNC1:def 8;
  per cases by XXREAL_0:14;
  suppose x is Real;
    then reconsider a = x as Real;
    1. * x = 1 * a by EXTREAL1:13,MESFUNC1:def 8;
    hence thesis by MEASURE6:def 1,MESFUNC1:def 8;
  end;
  suppose x = +infty;
    hence thesis by A1,EXTREAL1:def 1,MESFUNC1:def 8;
  end;
  suppose x = -infty;
    hence thesis by A1,EXTREAL1:def 1,MESFUNC1:def 8;
  end;
end;

theorem  :: extension of REAL_1:25
  0. - x = -x
proof
  per cases;
  suppose
A1: x = +infty;
    then 0. - x = -infty by SUPINF_2:6;
    hence thesis by A1,SUPINF_2:def 3;
  end;
  suppose x = -infty;
    hence thesis by SUPINF_2:4,7;
  end;
  suppose x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    0. - x = 0 - a by SUPINF_2:5
      .= -a;
    hence thesis by SUPINF_2:3;
  end;
end;

canceled;

theorem Th7:
  0 <= x & 0 <= y implies 0 <= x + y
proof
  assume
A1: (0 <= x & 0 <= y);
  per cases;
  suppose x = +infty;
    hence thesis by A1,SUPINF_2:def 2;
  end;
  suppose x <> +infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
    now per cases;
      suppose y = +infty;
        hence thesis by A1,SUPINF_2:def 2;
      end;
      suppose y <> +infty;
        then reconsider b = y as Real by A1,XXREAL_0:14;
        0 + 0 <= a + b by A1,XREAL_1:9;
        hence thesis by SUPINF_2:1;
      end;
    end;
    hence thesis;
  end;
end;

canceled;

theorem Th9:
  x <= 0 & y <= 0 implies x + y <= 0
proof
  assume
A1: x <= 0 & y <= 0;
  per cases;
  suppose x = -infty;
    hence thesis by A1,SUPINF_2:def 2;
  end;
  suppose x <> -infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
    now per cases;
      suppose y = -infty;
        hence thesis by A1,SUPINF_2:def 2;
      end;
      suppose y <> -infty;
        then reconsider b = y as Real by A1,XXREAL_0:14;
        a + b <= 0 + 0 by A1,XREAL_1:9;
        hence thesis by SUPINF_2:1;
      end;
    end;
    hence thesis;
  end;
end;

canceled;

theorem
  z <> +infty & z <> -infty & x + z = y implies x = y - z
proof
  assume that
A1: z <> +infty and
A2: z <> -infty and
A3: x + z = y;
  per cases;
  suppose
A4: x = +infty;
    then x + z = +infty by A2,SUPINF_2:def 2;
    hence thesis by A1,A3,A4,SUPINF_2:6;
  end;
  suppose
A5: x = -infty;
    then x + z = -infty by A1,SUPINF_2:def 2;
    hence thesis by A2,A3,A5,SUPINF_2:7;
  end;
  suppose x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    reconsider c = z as Real by A1,A2,XXREAL_0:14;
    y = a + c by A3,SUPINF_2:1;
    then y - z = a + c - c by SUPINF_2:5;
    hence thesis;
  end;
end;

theorem  :: extension of REAL_1:34
  x <> +infty & x <> -infty & x <> 0 implies x*(1./x) = 1. & (1./x)*x = 1.
proof
  assume
A1: x <> +infty & x <> -infty & x <> 0;
  then reconsider a = x as Real by XXREAL_0:14;
  1./x = 1/a by A1,EXTREAL1:32,MESFUNC1:def 8;
  then x * (1. / x) = a * (1/a) by EXTREAL1:13
    .= 1 by A1,XCMPLX_1:107;
  hence thesis by MESFUNC1:def 8;
end;

Lm1: +infty + -infty = 0. by SUPINF_2:def 2,XXREAL_0:8;
0. = 0;
then

Lm2: ex a being Real st 0. = a & -0. = - a by SUPINF_2:def 3;

Lm3: -+infty = -infty by SUPINF_2:def 3;

theorem  :: extension of REAL_1:36
  x - x = 0
proof
  per cases by XXREAL_0:14;
  suppose x in REAL;
    then reconsider a = x as Real;
    x - x = a - a by SUPINF_2:5
      .= 0;
    hence thesis;
  end;
  suppose x = +infty;
    hence x - x = 0 by Lm1,SUPINF_2:def 3;
  end;
  suppose x = -infty;
    hence x - x = 0 by Lm1,SUPINF_2:def 3,XXREAL_0:11;
  end;
end;

Lm4: -(+infty + -infty) = +infty - +infty by Lm1,Lm2,SUPINF_2:def 3
  .= --infty - +infty by SUPINF_2:def 3,XXREAL_0:11;

Lm5: x in REAL implies -(x + +infty) = -+infty + -x
proof
  assume
A1: x in REAL;
A2: -+infty = -infty by EXTREAL1:4;
A3: x + +infty = +infty by A1,SUPINF_2:def 2,XXREAL_0:11;
  -x <> +infty & -x <> -infty by A1,EXTREAL1:4,XXREAL_0:8,11;
  hence thesis by A2,A3,SUPINF_2:def 2;
end;

Lm6: x in REAL implies -(x + -infty) = --infty + -x
proof
  assume
A1: x in REAL;
A2: --infty = +infty by EXTREAL1:4;
A3: x + -infty = -infty by A1,SUPINF_2:def 2,XXREAL_0:8;
  -x <> +infty & -x <> -infty by A1,EXTREAL1:4,XXREAL_0:8,11;
  hence thesis by A2,A3,SUPINF_2:def 2;
end;

theorem Th14:  :: extension of REAL_2:6
  -(x + y) = -y - x
proof per cases by XXREAL_0:14;
  suppose
A1: x = +infty & y = +infty;
    hence -(x + y) = -+infty by SUPINF_2:def 2
      .= -y - x by A1,Lm3,SUPINF_2:def 2;
  end;
  suppose x = +infty & y = -infty;
    hence thesis by Lm4;
  end;
  suppose x = +infty & y in REAL;
    hence thesis by Lm5;
  end;
  suppose x = -infty & y = +infty;
    hence thesis by Lm4;
  end;
  suppose
A2: x = -infty & y = -infty;
    hence -(x + y) = --infty by SUPINF_2:def 2
      .= -y - x by A2,SUPINF_2:4,def 2;
  end;
  suppose x = -infty & y in REAL;
    hence thesis by Lm6;
  end;
  suppose x in REAL & y = +infty;
    hence thesis by Lm5;
  end;
  suppose x in REAL & y = -infty;
    hence thesis by Lm6;
  end;
  suppose x in REAL & y in REAL;
    then reconsider a=x, b=y as Real;
A3: x + y = a + b & -x = -a & -y = -b by SUPINF_2:1,3;
    then -(x + y) = -(a + b) by SUPINF_2:3
      .= -a + - b;
    hence thesis by A3,SUPINF_2:1;
  end;
end;

theorem Th15:  :: extension of REAL_2:8
  -(x - y) = -x + y & -(x - y) = y - x
proof
  -(x - y) = -(-y) - x by Th14;
  hence thesis;
end;

theorem  :: extension of REAL_2:9
  -(-x + y) = x - y & -(-x + y) = x + -y
proof
  -(-x + y) = -(-x) - y by Th14
    .= x - y;
  hence thesis;
end;

theorem Th17:
  (x = +infty & 0 < y & y < +infty) or (x = -infty & y < 0 & -infty < y)
  implies x / y = +infty
proof
  assume
A1: (x=+infty & 0<y & y<+infty) or (x=-infty & y<0 & -infty<y);
  per cases by A1;
  suppose x=+infty & 0<y & y<+infty;
    hence thesis by EXTREAL1:def 2;
  end;
  suppose x=-infty & y<0 & -infty<y;
    hence thesis by EXTREAL1:def 2;
  end;
end;

theorem Th18:
  (x = +infty & y < 0 & -infty < y) or (x = -infty & 0 < y & y < +infty)
  implies x / y = -infty
proof
  assume
A1: (x=+infty & y<0 & -infty<y) or (x=-infty & 0<y & y<+infty);
  per cases by A1;
  suppose x=+infty & y<0 & -infty<y;
    hence thesis by EXTREAL1:def 2;
  end;
  suppose x=-infty & 0<y & y<+infty;
    hence thesis by EXTREAL1:def 2;
  end;
end;

theorem Th19:  :: extension of REAL_2:62
  -infty < y & y < +infty & y <> 0 implies x * y / y = x & x * (y / y) = x
proof
  assume that
A1: -infty < y and
A2: y < +infty and
A3: y <> 0;
  reconsider b = y as Real by A1,A2,XXREAL_0:14;
A4: x * y / y = x
  proof
    per cases;
    suppose
A5:   x = +infty;
      now per cases by A3;
        suppose
A6:       0 < y;
          then x * y = +infty by A5,EXTREAL1:def 1;
          hence thesis by A2,A5,A6,Th17;
        end;
        suppose
A7:       y < 0;
          then x * y = -infty by A5,EXTREAL1:def 1;
          hence thesis by A1,A5,A7,Th17;
        end;
      end;
      hence thesis;
    end;
    suppose
A8:   x = -infty;
      now per cases by A3;
        suppose
A9:       0 <y;
          then x * y = -infty by A8,EXTREAL1:def 1;
          hence thesis by A2,A8,A9,Th18;
        end;
        suppose
A10:      y < 0;
          then x * y = +infty by A8,EXTREAL1:def 1;
          hence thesis by A1,A8,A10,Th18;
        end;
      end;
      hence thesis;
    end;
    suppose x <> +infty & x <> -infty;
      then reconsider a = x as Real by XXREAL_0:14;
      x * y = a * b by EXTREAL1:13;
      then (x * y)/y = (a * b)/b by A3,EXTREAL1:32
        .= a by A3,XCMPLX_1:90;
      hence thesis;
    end;
  end;
  y / y = 1. by A1,A2,A3,EXTREAL1:34,MESFUNC1:def 8;
  hence thesis by A4,Th4;
end;

theorem
  1. < +infty & -infty < 1. by MESFUNC1:def 8,XXREAL_0:9;

theorem
  x = +infty or x = -infty implies (for y st y in REAL holds x + y <> 0)
proof
  assume
A1: x = +infty or x = -infty;
  given y such that
A2: y in REAL & x + y = 0;
A3: y <> -infty & y <> +infty by A2,XXREAL_0:9,12;
  per cases by A1;
  suppose x = +infty;
    hence contradiction by A2,A3,SUPINF_2:def 2;
  end;
  suppose x = -infty;
    hence contradiction by A2,A3,SUPINF_2:def 2;
  end;
end;

theorem
  x = +infty or x = -infty implies for y holds x * y <> 1.
proof
  assume
A1: x = +infty or x = -infty;
  given y such that
A2: x * y = 1.;
  per cases;
  suppose y = 0;
    hence contradiction by A2,EXTREAL1:def 1,MESFUNC1:def 8;
  end;
  suppose
A3: 0 < y;
    now per cases by A1;
      suppose x = +infty;
        hence contradiction by A2,A3,EXTREAL1:def 1,MESFUNC1:def 8;
      end;
      suppose x = -infty;
        hence contradiction by A2,A3,EXTREAL1:def 1,MESFUNC1:def 8;
      end;
    end;
    hence thesis;
  end;
  suppose
A4: y < 0;
    now per cases by A1;
      suppose x = +infty;
        hence contradiction by A2,A4,EXTREAL1:def 1,MESFUNC1:def 8;
      end;
      suppose x = -infty;
        hence contradiction by A2,A4,EXTREAL1:def 1,MESFUNC1:def 8;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th23:
  not ((x = +infty & y = -infty) or (x = -infty & y = +infty)) &
  x + y < +infty implies x <> +infty & y <> +infty
proof
  assume that
A1: not ((x=+infty & y=-infty) or (x=-infty & y=+infty)) and
A2: x + y < +infty;
  assume
A3: x = +infty or y = +infty;
  per cases by A3;
  suppose x = +infty;
    hence contradiction by A1,A2,SUPINF_2:def 2;
  end;
  suppose y = +infty;
    hence contradiction by A1,A2,SUPINF_2:def 2;
  end;
end;

theorem Th24:
  not ((x = +infty & y = -infty) or (x = -infty & y = +infty)) &
  -infty < x + y implies x <> -infty & y <> -infty
proof
  assume that
A1: not ((x=+infty & y=-infty) or (x=-infty & y=+infty)) and
A2: -infty < x + y;
  assume
A3: x = -infty or y = -infty;
  per cases by A3;
  suppose x = -infty;
    hence contradiction by A1,A2,SUPINF_2:def 2;
  end;
  suppose y = -infty;
    hence contradiction by A1,A2,SUPINF_2:def 2;
  end;
end;

theorem Th25:
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty)) &
  x - y < +infty implies x <> +infty & y <> -infty
proof
  assume that
A1: not ((x=+infty & y=+infty) or (x=-infty & y=-infty)) and
A2: x - y < +infty;
  assume
A3: x = +infty or y = -infty;
  per cases by A3;
  suppose x = +infty;
    hence contradiction by A1,A2,SUPINF_2:6;
  end;
  suppose y = -infty;
    hence contradiction by A1,A2,SUPINF_2:7;
  end;
end;

theorem Th26:
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty)) &
  -infty < x - y implies x <> -infty & y <> +infty
proof
  assume that
A1: not ((x=+infty & y=+infty) or (x=-infty & y=-infty)) and
A2: -infty < x - y;
  assume
A3: x = -infty or y = +infty;
  per cases by A3;
  suppose x = -infty;
    hence contradiction by A1,A2,SUPINF_2:7;
  end;
  suppose y = +infty;
    hence contradiction by A1,A2,SUPINF_2:6;
  end;
end;

theorem
  not ((x = +infty & y = -infty) or (x = -infty & y = +infty)) &
  x + y < z implies x <> +infty & y <> +infty & z <> -infty & x < z - y
proof
  assume that
A1: not ((x=+infty & y=-infty) or (x=-infty & y=+infty)) and
A2: x + y < z;
  per cases;
  suppose
A3: z = +infty;
    then x<>+infty & y<>+infty & z<>-infty by A1,A2,Th23;
    then x < +infty & z - y = +infty by A3,SUPINF_2:6,XXREAL_0:4;
    hence thesis by A1,A2,A3,Th23;
  end;
  suppose
A4: z <> +infty;
    x + y < +infty by A2,XXREAL_0:2,4;
    then
A5: x <> +infty & y <> +infty by A1,Th23;
A6: -infty <= x + y by XXREAL_0:5;
    then reconsider c = z as Real by A2,A4,XXREAL_0:14;
    now per cases;
      suppose
A7:     y = -infty;
        then x < +infty by A1,XXREAL_0:4;
        hence thesis by A2,A6,A7,SUPINF_2:7;
      end;
      suppose y <> -infty;
        then reconsider b = y as Real by A5,XXREAL_0:14;
        now per cases;
          suppose
A8:         x = -infty;
            z - y = c - b by SUPINF_2:5;
            hence thesis by A8,XXREAL_0:12;
          end;
          suppose x <> -infty;
            then reconsider a = x as Real by A5,XXREAL_0:14;
A9:         x + y = a + b & z = c by SUPINF_2:1;
            consider p,q being Real;
            a < c - b by A2,A9,XREAL_1:22;
            hence thesis by SUPINF_2:5;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  not ((z = +infty & y = +infty) or (z = -infty & y = -infty)) &
  x < z - y implies x <> +infty & y <> +infty & z <> -infty & x + y < z
proof
  assume that
A1: not ((z=+infty & y=+infty) or (z=-infty & y=-infty)) and
A2: x < z - y;
  per cases;
  suppose
A3: x = -infty;
    then
A4: x<>+infty & y<>+infty & z<>-infty by A1,A2,Th26;
    -infty <= z by XXREAL_0:5;
    then -infty < z & x + y = -infty by A3,A4,SUPINF_2:def 2,XXREAL_0:1;
    hence thesis by A1,A2,A3,Th26;
  end;
  suppose
A5: x <> -infty;
    -infty <= x by XXREAL_0:5;
    then -infty < z - y by A2,XXREAL_0:2;
    then
A6: z <> -infty & y <> +infty by A1,Th26;
A7: z - y <= +infty by XXREAL_0:4;
    then reconsider a = x as Real by A2,A5,XXREAL_0:14;
    now per cases;
      suppose
A8:     y = -infty;
        then z <> -infty & -infty <= z by A1,XXREAL_0:5;
        then -infty < z by XXREAL_0:1;
        hence thesis by A2,A7,A8,SUPINF_2:def 2;
      end;
      suppose y <> -infty;
        then reconsider b = y as Real by A6,XXREAL_0:14;
        now per cases;
          suppose
A9:         z = +infty;
            x + y = a + b by SUPINF_2:1;
            hence thesis by A9,XXREAL_0:9;
          end;
          suppose z <> +infty;
            then reconsider c = z as Real by A6,XXREAL_0:14;
A10:        x = a & z - y = c - b by SUPINF_2:5;
            consider p,q being Real;
            a + b < c by A2,A10,XREAL_1:22;
            hence thesis by SUPINF_2:1;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty)) &
  x - y < z implies x <> +infty & y <> -infty & z <> -infty & x < z + y
proof
  assume that
A1: not ((x=+infty & y=+infty) or (x=-infty & y=-infty)) and
A2: x - y < z;
  per cases;
  suppose
A3: z = +infty;
    then x<>+infty & y<>-infty & z<>-infty by A1,A2,Th25;
    then x < +infty & z + y = +infty by A3,SUPINF_2:def 2,XXREAL_0:4;
    hence thesis by A1,A2,A3,Th25;
  end;
  suppose
A4: z <> +infty;
    x - y < +infty by A2,XXREAL_0:2,4;
    then
A5: x <> +infty & y <> -infty by A1,Th25;
A6: -infty <= x - y by XXREAL_0:5;
    then reconsider c = z as Real by A2,A4,XXREAL_0:14;
    now per cases;
      suppose
A7:     y = +infty;
        then x < +infty by A1,XXREAL_0:4;
        hence thesis by A2,A6,A7,SUPINF_2:def 2;
      end;
      suppose y <> +infty;
        then reconsider b = y as Real by A5,XXREAL_0:14;
        now per cases;
          suppose
A8:         x = -infty;
            z + y = c + b by SUPINF_2:1;
            hence thesis by A8,XXREAL_0:12;
          end;
          suppose x <> -infty;
            then reconsider a = x as Real by A5,XXREAL_0:14;
A9:         x - y = a - b & z = c by SUPINF_2:5;
            consider p,q being Real;
            a < c + b by A2,A9,XREAL_1:21;
            hence thesis by SUPINF_2:1;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  not ((z = +infty & y = -infty) or (z = -infty & y = +infty)) &
  x < z + y implies x <> +infty & y <> -infty & z <> -infty & x - y < z
proof
  assume that
A1: not ((z=+infty & y=-infty) or (z=-infty & y=+infty)) and
A2: x < z + y;
  per cases;
  suppose
A3: x = -infty;
    then
A4: x<>+infty & y<>-infty & z<>-infty by A1,A2,Th24;
    -infty <= z by XXREAL_0:5;
    then -infty < z & x - y = -infty by A3,A4,SUPINF_2:7,XXREAL_0:1;
    hence thesis by A1,A2,A3,Th24;
  end;
  suppose
A5: x <> -infty;
    -infty <= x by XXREAL_0:5;
    then -infty < z + y by A2,XXREAL_0:2;
    then
A6: z <> -infty & y <> -infty by A1,Th24;
A7: z + y <= +infty by XXREAL_0:4;
    then reconsider a = x as Real by A2,A5,XXREAL_0:14;
    now per cases;
      suppose
A8:     y = +infty;
        then z <> -infty & -infty <= z by A1,XXREAL_0:5;
        then -infty < z by XXREAL_0:1;
        hence thesis by A2,A7,A8,SUPINF_2:6;
      end;
      suppose y <> +infty;
        then reconsider b = y as Real by A6,XXREAL_0:14;
        now per cases;
          suppose
A9:         z = +infty;
            x - y = a - b by SUPINF_2:5;
            hence thesis by A9,XXREAL_0:9;
          end;
          suppose z <> +infty;
            then reconsider c = z as Real by A6,XXREAL_0:14;
A10:        x = a & z + y = c + b by SUPINF_2:1;
            consider p,q being Real;
            a - b < c by A2,A10,XREAL_1:21;
            hence thesis by SUPINF_2:5;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  not ((x = +infty & y = -infty) or (x = -infty & y = +infty) or
  (y = +infty & z = +infty) or (y = -infty & z = -infty)) &
  x + y <= z implies y <> +infty & x <= z - y
proof
  assume that
A1: not ((x=+infty & y=-infty) or (x=-infty & y=+infty) or
  (y = +infty & z = +infty) or (y = -infty & z = -infty)) and
A2: x + y <= z;
  thus y <> +infty
  proof
    assume
A3: y = +infty;
    then x + y = +infty by A1,SUPINF_2:def 2;
    hence contradiction by A1,A2,A3,XXREAL_0:4;
  end;
  per cases;
  suppose z = +infty;
    then z - y = +infty by A1,SUPINF_2:6;
    hence thesis by XXREAL_0:4;
  end;
  suppose
A4: z <> +infty;
    then x + y < +infty by A2,XXREAL_0:2,4;
    then
A5: x <> +infty & y <> +infty by A1,Th23;
    now per cases;
      suppose x = -infty;
        hence thesis by XXREAL_0:5;
      end;
      suppose x <> -infty;
        then reconsider a = x as Real by A5,XXREAL_0:14;
        now per cases;
          suppose y = -infty;
            then z - y = +infty by A1,SUPINF_2:7;
            hence thesis by XXREAL_0:4;
          end;
          suppose y <> -infty;
            then reconsider b = y as Real by A5,XXREAL_0:14;
A6:         x + y = a + b by SUPINF_2:1;
            then z <> -infty by A2,XXREAL_0:12;
            then reconsider c = z as Real by A4,XXREAL_0:14;
            z = c;
            then consider p,q being Real such that
A7:         p = x + y & q = z & p <= q by A2,A6;
            a <= c - b by A6,A7,XREAL_1:21;
            hence thesis by SUPINF_2:5;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  not (x = +infty & y = -infty) & not (x = -infty & y = +infty) &
  not (y = +infty & z = +infty) & x <= z - y implies y <> +infty & x + y <= z
proof
  assume that
A1: not (x = +infty & y = -infty) and
A2: not (x = -infty & y = +infty) and
A3: not (y = +infty & z = +infty) and
A4: x <= z - y; thus
A5: y <> +infty
  proof
    assume
A6: y = +infty;
    then z - y = -infty by A3,SUPINF_2:6;
    hence contradiction by A2,A4,A6,XXREAL_0:6;
  end;
  per cases by A5;
  suppose y = -infty;
    then x + y = -infty by A1,SUPINF_2:def 2;
    hence thesis by XXREAL_0:5;
  end;
  suppose
A7: y <> +infty & y <> -infty;
    then
A8: y is Real by XXREAL_0:14;
    - +infty = -infty & - -infty = +infty by SUPINF_2:4;
    then -y <> -infty & -y <> +infty by A7;
    then z - y + y = z + (-y + y) by A7,EXTREAL1:8
      .= z + 0. by EXTREAL1:9
      .= z by SUPINF_2:18;
    hence thesis by A4,A8,MEASURE6:11;
  end;
end;

theorem
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty) or
  (y = +infty & z = -infty) or (y = -infty & z = +infty)) &
  x - y <= z implies y <> -infty & x <= z + y
proof
  assume that
A1: not ((x=+infty & y=+infty) or (x=-infty & y=-infty) or
  (y = +infty & z = -infty) or (y = -infty & z = +infty)) and
A2: x - y <= z;
  thus y <> -infty
  proof
    assume
A3: y = -infty;
    then x - y = +infty by A1,SUPINF_2:7;
    hence contradiction by A1,A2,A3,XXREAL_0:4;
  end;
  per cases;
  suppose z = +infty;
    then z + y = +infty by A1,SUPINF_2:def 2;
    hence thesis by XXREAL_0:4;
  end;
  suppose
A4: z <> +infty;
    then x - y < +infty by A2,XXREAL_0:2,4;
    then
A5: x <> +infty & y <> -infty by A1,Th25;
    now per cases;
      suppose x = -infty;
        hence thesis by XXREAL_0:5;
      end;
      suppose x <> -infty;
        then reconsider a = x as Real by A5,XXREAL_0:14;
        now per cases;
          suppose y = +infty;
            then z + y = +infty by A1,SUPINF_2:def 2;
            hence thesis by XXREAL_0:4;
          end;
          suppose y <> +infty;
            then reconsider b = y as Real by A5,XXREAL_0:14;
A6:         x - y = a - b by SUPINF_2:5;
            then z <> -infty by A2,XXREAL_0:12;
            then reconsider c = z as Real by A4,XXREAL_0:14;
            z = c;
            then consider p,q being Real such that
A7:         p = x - y & q = z & p <= q by A2,A6;
            a <= c + b by A6,A7,XREAL_1:22;
            hence thesis by SUPINF_2:1;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th34:
  not (x = +infty & y = +infty) & not (x = -infty & y = -infty) &
  not (y = -infty & z = +infty) & x <= z + y implies y <> -infty & x - y <= z
proof
  assume that
A1: not(x=+infty & y=+infty) and
A2: not(x=-infty & y=-infty) and
A3: not(y = -infty & z = +infty) and
A4: x <= z + y; thus
A5: y <> -infty
  proof
    assume
A6: y = -infty;
    then z + y = -infty by A3,SUPINF_2:def 2;
    hence contradiction by A2,A4,A6,XXREAL_0:6;
  end;
  per cases by A5;
  suppose y = +infty;
    then x - y = -infty by A1,SUPINF_2:6;
    hence thesis by XXREAL_0:5;
  end;
  suppose
A7: y <> +infty & y <> -infty;
    then
A8: y is Real by XXREAL_0:14;
    y - y = 0 by EXTREAL1:9;
    then z + y - y = z + 0. by A7,EXTREAL1:11
      .= z by SUPINF_2:18;
    hence thesis by A4,A8,MEASURE6:11;
  end;
end;

canceled 5;

theorem  :: extension of REAL_1:27
  not (x = +infty & y = +infty) & not (x = -infty & y = -infty) &
  not (y = +infty & z = -infty) & not (y = -infty & z = +infty) &
  not (x = +infty & z = +infty) & not (x = -infty & z = -infty)
  implies x - y - z = x - (y + z)
proof
  assume that
A1: not(x=+infty & y=+infty) and
A2: not(x=-infty & y=-infty) and
A3: not (y = +infty & z = -infty) and
A4: not (y = -infty & z = +infty) and
A5: not (x = +infty & z = +infty) and
A6: not (x = -infty & z = -infty);
  per cases;
  suppose
A7: x = +infty;
    then x - y = +infty by A1,SUPINF_2:6;
    then
A8: x - y - z = +infty by A5,A7,SUPINF_2:6;
    y + z <> +infty by A1,A5,A7,SUPINF_2:8;
    hence thesis by A7,A8,SUPINF_2:6;
  end;
  suppose
A9: x = -infty;
    then x - y = -infty by A2,SUPINF_2:7;
    then
A10: x - y - z = -infty by A6,A9,SUPINF_2:7;
    y + z <> -infty by A2,A6,A9,SUPINF_2:9;
    hence thesis by A9,A10,SUPINF_2:7;
  end;
  suppose
A11: x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    now per cases;
      suppose
A12:    y = +infty;
        then x - y = -infty & y + z = +infty by A3,A11,SUPINF_2:6,def 2;
        hence thesis by A3,A12,SUPINF_2:7;
      end;
      suppose
A13:    y = -infty;
        then x - y = +infty & y + z = -infty by A4,A11,SUPINF_2:7,def 2;
        hence thesis by A4,A13,SUPINF_2:6;
      end;
      suppose y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
A14:    x - y = a - b by SUPINF_2:5;
        now per cases;
          suppose z = +infty;
            then x - y - z = -infty & y + z = +infty by A14,SUPINF_2:6,def 2;
            hence thesis by A11,SUPINF_2:6;
          end;
          suppose z = -infty;
            then x - y - z = +infty & y + z = -infty by A14,SUPINF_2:7,def 2;
            hence thesis by A11,SUPINF_2:7;
          end;
          suppose z <> +infty & z <> -infty;
            then reconsider c = z as Real by XXREAL_0:14;
A15:        x - y - z = a - b - c & y + z = b + c by A14,SUPINF_2:1,5;
            then x - (y + z) = a - (b + c) by SUPINF_2:5;
            hence thesis by A15;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem  :: extension of REAL_1:28
  not (x = +infty & y = +infty) & not (x = -infty & y = -infty) &
  not (y = +infty & z = +infty) & not (y = -infty & z = -infty) &
  not (x = +infty & z = -infty) & not (x = -infty & z = +infty)
  implies x - y + z = x - (y - z)
proof
  assume that
A1: not(x=+infty & y=+infty) and
A2: not(x=-infty & y=-infty) and
A3: not (y = +infty & z = +infty) and
A4: not (y = -infty & z = -infty) and
A5: not (x = +infty & z = -infty) and
A6: not (x = -infty & z = +infty);
  per cases;
  suppose
A7: x = +infty;
    then x - y = +infty by A1,SUPINF_2:6;
    then
A8: x - y + z = +infty by A5,A7,SUPINF_2:def 2;
    y - z <> +infty by A1,A5,A7,SUPINF_2:10;
    hence thesis by A7,A8,SUPINF_2:6;
  end;
  suppose
A9: x = -infty;
    then x - y = -infty by A2,SUPINF_2:7;
    then
A10: x - y + z = -infty by A6,A9,SUPINF_2:def 2;
    y - z <> -infty by A2,A6,A9,SUPINF_2:11;
    hence thesis by A9,A10,SUPINF_2:7;
  end;
  suppose
A11: x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    now per cases;
      suppose
A12:    y = +infty;
        then x - y = -infty & y - z = +infty by A3,A11,SUPINF_2:6;
        hence thesis by A3,A12,SUPINF_2:def 2;
      end;
      suppose
A13:    y = -infty;
        then x - y = +infty & y - z = -infty by A4,A11,SUPINF_2:7;
        hence thesis by A4,A13,SUPINF_2:def 2;
      end;
      suppose y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
A14:    x - y = a - b by SUPINF_2:5;
        now per cases;
          suppose z = +infty;
            then x - y + z = +infty & y - z = -infty by A14,SUPINF_2:6,def 2;
            hence thesis by A11,SUPINF_2:7;
          end;
          suppose z = -infty;
            then x - y + z = -infty & y - z = +infty by A14,SUPINF_2:7,def 2;
            hence thesis by A11,SUPINF_2:6;
          end;
          suppose z <> +infty & z <> -infty;
            then reconsider c = z as Real by XXREAL_0:14;
A15:        x - y + z = a - b + c & y - z = b - c by A14,SUPINF_2:1,5;
            then x - (y - z) = a - (b - c) by SUPINF_2:5;
            hence thesis by A15;
          end;
        end;
        hence thesis;
      end;
    end;
    hence thesis;
  end;
end;

theorem
  x * y <> +infty & x * y <> -infty implies x is Real or y is Real
proof
  assume
A1: x * y <> +infty & x * y <> -infty;
  assume
A2: not (x is Real or y is Real);
  per cases by A2,XXREAL_0:14;
  suppose x = +infty & y = +infty;
    hence contradiction by A1,EXTREAL1:def 1;
  end;
  suppose x = +infty & y = -infty;
    hence contradiction by A1,EXTREAL1:def 1;
  end;
  suppose x = -infty & y = +infty;
    hence contradiction by A1,EXTREAL1:def 1;
  end;
  suppose x = -infty & y = -infty;
    hence contradiction by A1,EXTREAL1:def 1;
  end;
end;

theorem Th43:  :: extension of EXTREAL1:21
  (0 < x & 0 < y) or (x < 0 & y < 0) iff 0 < x * y
proof
  0 < x * y implies (0 < x & 0 < y) or (x < 0 & y < 0)
  proof
    assume
A1: 0 < x * y;
    assume
A2: not ((0 < x & 0 < y) or (x < 0 & y < 0));
    per cases by A2;
    suppose x <= 0 & 0 <= x;
      then x = 0;
      hence contradiction by A1,EXTREAL1:def 1;
    end;
    suppose
A3:   x <= 0 & 0 <= y;
      now per cases by A3;
        suppose x = 0;
          hence contradiction by A1,EXTREAL1:def 1;
        end;
        suppose
A4:       x < 0;
          now per cases by A3;
            suppose y = 0;
              hence contradiction by A1,EXTREAL1:def 1;
            end;
            suppose 0 < y;
              hence contradiction by A1,A4,EXTREAL1:21;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    suppose
A5:   0 <= x & y <= 0;
      now per cases by A5;
        suppose x = 0;
          hence contradiction by A1,EXTREAL1:def 1;
        end;
        suppose
A6:       0 < x;
          now per cases by A5;
            suppose y = 0;
              hence contradiction by A1,EXTREAL1:def 1;
            end;
            suppose y < 0;
              hence contradiction by A1,A6,EXTREAL1:21;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    suppose y <= 0 & 0 <= y;
      then y = 0;
      hence contradiction by A1,EXTREAL1:def 1;
    end;
  end;
  hence thesis by EXTREAL1:20;
end;

theorem Th44:  :: extension of EXTREAL1:22
  (0 < x & y < 0) or (x < 0 & 0 < y) iff x * y < 0
proof
  x * y < 0 implies (0 < x & y < 0) or (x < 0 & 0 < y)
  proof
    assume
A1: x * y < 0;
    assume
A2: not ((0 < x & y < 0) or (x < 0 & 0 < y));
    per cases by A2;
    suppose x <= 0 & 0 <= x;
      then x = 0;
      hence contradiction by A1,EXTREAL1:def 1;
    end;
    suppose
A3:   x <= 0 & y <= 0;
      now per cases by A3;
        suppose x = 0;
          hence contradiction by A1,EXTREAL1:def 1;
        end;
        suppose
A4:       x < 0;
          now per cases by A3;
            suppose y = 0;
              hence contradiction by A1,EXTREAL1:def 1;
            end;
            suppose y < 0;
              hence contradiction by A1,A4,EXTREAL1:20;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    suppose
A5:   0 <= x & 0 <= y;
      now per cases by A5;
        suppose x = 0;
          hence contradiction by A1,EXTREAL1:def 1;
        end;
        suppose
A6:       0 < x;
          now per cases by A5;
            suppose y = 0;
              hence contradiction by A1,EXTREAL1:def 1;
            end;
            suppose 0 < y;
              hence contradiction by A1,A6,EXTREAL1:20;
            end;
          end;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
    suppose y <= 0 & 0 <= y;
      then y = 0;
      hence contradiction by A1,EXTREAL1:def 1;
    end;
  end;
  hence thesis by EXTREAL1:21;
end;

theorem   :: extension of REAL_2:121
  ((0 <= x or 0 < x) & (0 <= y or 0 < y)) or
  ((x <= 0 or x < 0) & (y <= 0 or y < 0)) iff 0 <= x * y by Th44;

theorem  :: extension of REAL_2:123
  ((x <= 0 or x < 0) & (0 <= y or 0 < y)) or
  ((0 <= x or 0 < x) & (y <= 0 or y < 0)) iff x * y <= 0 by Th43;

theorem Th47:
  (x <= -y implies y <= -x) & (-x <= y implies -y <= x)
proof
  x <= -y implies --y <= -x by SUPINF_2:16;
  hence x <= -y implies y <= -x;
  -x <= y implies -y <= --x by SUPINF_2:16;
  hence thesis;
end;

begin :: Basic properties of absolute value for extended real numbers

canceled;

theorem Th49:
  x = a implies |.x.| = abs(a)
proof
  assume
A1: x = a;
  per cases;
  suppose
A2: 0 <= x;
    then |.x.| = a by A1,EXTREAL1:def 3;
    hence thesis by A1,A2,ABSVALUE:def 1;
  end;
  suppose
A3: not 0 <= x;
    then |.x.| = -x by EXTREAL1:37
      .= -a by A1,SUPINF_2:3;
    hence thesis by A1,A3,ABSVALUE:def 1;
  end;
end;

theorem
  |.x.| = x or |.x.| = -x by EXTREAL1:def 3;

theorem Th51:  :: extension of ABSVALUE:5
  0 <= |.x.|
proof
  per cases;
  suppose 0 <= x;
    hence thesis by EXTREAL1:def 3;
  end;
  suppose
A1: not (0 <= x);
    then |.x.| = -x by EXTREAL1:def 3;
    hence thesis by A1,EXTREAL1:25;
  end;
end;

theorem Th52:  :: extension of ABSVALUE:6
  x <> 0 implies 0 < |.x.|
proof
  assume
A1: x <> 0;
  per cases;
  suppose 0 <= x;
    hence thesis by A1,EXTREAL1:def 3;
  end;
  suppose
A2: not (0 <= x);
    then 0 < -x by EXTREAL1:25;
    hence thesis by A2,EXTREAL1:def 3;
  end;
end;

theorem  :: extension of ABSVALUE:7
  x = 0 iff |.x.| = 0 by Th52,EXTREAL1:def 3;

theorem  :: extension of ABSVALUE:9
  |.x.| = -x & x <> 0 implies x < 0
proof
  assume
A1: |.x.| = -x & x <> 0;
  assume
A2: not x < 0;
  then -x = x by A1,EXTREAL1:def 3;
  hence contradiction by A1,A2,EXTREAL1:25;
end;

theorem Th55:
  x <= 0 implies |.x.| = -x
proof
  assume
A1: x <= 0;
  per cases by A1;
  suppose x < 0;
    hence thesis by EXTREAL1:def 3;
  end;
  suppose x = 0;
    hence thesis by EXTREAL1:24,def 3;
  end;
end;

theorem  :: extension of ABSVALUE:10
  |.x * y.| = |.x.| * |.y.|
proof
  per cases;
  suppose x = 0;
    then x * y = 0 & |.x.| = 0 by EXTREAL1:22,def 3;
    then |.x * y.| = 0 & |.x.| * |.y.| = 0 by EXTREAL1:22,def 3;
    hence thesis;
  end;
  suppose
A1: 0 < x;
    now per cases;
      suppose y = 0;
        then x * y = 0 & |.y.| = 0 by EXTREAL1:22,def 3;
        then |.x * y.| = 0 & |.x.| * |.y.| = 0 by EXTREAL1:22,def 3;
        hence thesis;
      end;
      suppose 0 < y;
        then 0 <= x * y & |.x.| = x & |.y.| = y by A1,EXTREAL1:20,def 3;
        hence thesis by EXTREAL1:def 3;
      end;
      suppose
A2:     y < 0;
        then |.x.| = x & |.y.| = -y by A1,EXTREAL1:def 3;
        then
A3:     |.x.| * |.y.| = -(x * y) by EXTREAL1:26;
        x * y < 0 by A1,A2,EXTREAL1:21;
        hence thesis by A3,EXTREAL1:def 3;
      end;
    end;
    hence thesis;
  end;
  suppose
A4: x < 0;
    now per cases;
      suppose y = 0;
        then x * y = 0 & |.y.| = 0 by EXTREAL1:22,def 3;
        then |.x * y.| = 0 & |.x.| * |.y.| = 0 by EXTREAL1:22,def 3;
        hence thesis;
      end;
      suppose 0 < y;
        then not 0 <= x * y & |.y.| = y by A4,EXTREAL1:21,def 3;

then |.x * y.| = -(x * y) & |.x.| * |.y.| = (-x) * y by A4,EXTREAL1:def 3;
        hence thesis by EXTREAL1:26;
      end;
      suppose y < 0;
        then
A5:     0 <= x * y & |.y.| = -y by A4,EXTREAL1:20,def 3;

then |.x * y.| = x * y & |.x.| * |.y.| = (-x) * (-y) by A4,EXTREAL1:def 3;
        then |.x.| * |.y.| = -(x * (-y)) by EXTREAL1:26
          .= -(-(x * y)) by
        EXTREAL1:26;
        hence thesis by A5,EXTREAL1:def 3;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th57:  :: extension of ABSVALUE:11
  -|.x.| <= x & x <= |.x.|
proof
  per cases;
  suppose
A1: 0 <= x;
    0 <= |.x.| by Th51;
    hence thesis by A1,EXTREAL1:24,def 3,SUPINF_2:16;
  end;
  suppose
A2: not 0 <= x;
    then |.x.|=-x by EXTREAL1:def 3;
    hence thesis by A2,EXTREAL1:25;
  end;
end;

theorem Th58:
  |.x.| < y implies -y < x & x < y
proof
  assume
A1: |.x.| < y;
  per cases;
  suppose
A2: 0 <= x;
    then x < y by A1,EXTREAL1:def 3;
    hence thesis by A2,Th47,EXTREAL1:24;
  end;
  suppose
A3: not 0 <= x;
    then |.x.|=-x by EXTREAL1:def 3;
    hence thesis by A1,A3,Th47,EXTREAL1:25;
  end;
end;

theorem Th59:
  -y < x & x < y implies 0 < y & |.x.| < y
proof
  assume
A1: -y < x & x < y;
  per cases;
  suppose 0 <= x;
    hence thesis by A1,EXTREAL1:def 3;
  end;
  suppose
A2: not 0 <= x;
    then 0 < -x & -x < y by A1,Th47,EXTREAL1:25;
    hence thesis by A2,EXTREAL1:def 3;
  end;
end;

theorem Th60: :: extension of ABSVALUE:12
  -y <= x & x <= y iff |.x.| <= y
proof
A1: -y <= x & x <= y implies |.x.| <= y
  proof
    assume
A2: -y <= x & x <= y;
    per cases;
    suppose 0 <= x;
      hence thesis by A2,EXTREAL1:def 3;
    end;
    suppose
A3:   not 0 <= x;
      then 0 < -x & -x <= y by A2,Th47,EXTREAL1:25;
      hence thesis by A3,EXTREAL1:def 3;
    end;
  end;
  |.x.| <= y implies -y <= x & x <= y
  proof
    assume
A4: |.x.| <= y;
    per cases by A4,XXREAL_0:1;
    suppose |.x.| = y;
      hence thesis by Th57;
    end;
    suppose |.x.| < y;
      hence thesis by Th58;
    end;
  end;
  hence thesis by A1;
end;

theorem Th61:  :: extension of ABSVALUE:13
  not ((x = +infty & y = -infty) or (x = -infty & y = +infty)) implies
  |.x + y.| <= |.x.| + |.y.|
proof
  assume
A1: not ((x = +infty & y = -infty) or (x = -infty & y = +infty));
A2: -|.x.| <= x & x <= |.x.| & -|.y.| <= y & y <= |.y.| by Th57;
  per cases;
  suppose
A3: x = +infty;
    then +infty <= |.x.| & y <> -infty & -infty <= y by A1,Th57,XXREAL_0:5;
    then
A4: |.x.| = +infty & -infty < y by XXREAL_0:1,4;
    now per cases;
      suppose
A5:     y = +infty;
        then |.y.| = +infty by A2,XXREAL_0:4;
        then -|.y.| <> +infty by SUPINF_2:def 3;
        then
A6:     -|.x.|+(-|.y.|) <= x + y by A2,A3,A5,SUPINF_2:14;
        -|.x.|-|.y.| = -(|.x.|+|.y.|) by Th14;
        hence thesis by A3,A4,A5,A6,Th60;
      end;
      suppose
A7:     y <> +infty;
        then
A8:     x + y <= |.x.| + |.y.| by A2,A4,SUPINF_2:14;
A9:     -|.x.| <> +infty by A4,SUPINF_2:def 3;
        -|.y.| <> +infty by A2,A7,XXREAL_0:4;
        then
A10:    -|.x.|+(-|.y.|) <= x + y by A1,A2,A9,SUPINF_2:14;
        -|.x.|-|.y.| = -(|.x.|+|.y.|) by Th14;
        hence thesis by A8,A10,Th60;
      end;
    end;
    hence thesis;
  end;
  suppose
A11: x = -infty;
A12: |.x.| <> -infty by A2,SUPINF_2:4;
    now per cases;
      suppose
A13:    y = -infty;
        then -|.y.| <= -infty by Th57;
        then
A14:    -|.y.| = -infty by XXREAL_0:6;
A15:    x + y <= |.x.| + |.y.| by A2,A11,A13,SUPINF_2:4,14;
        -|.x.|-|.y.| = -(|.x.|+|.y.|) by Th14;
        hence thesis by A11,A13,A14,A15,Th60;
      end;
      suppose
A16:    y <> -infty;
        -infty <= y by XXREAL_0:5;
        then -infty < y by A16,XXREAL_0:1;
        then
A17:    x + y <= |.x.| + |.y.| by A1,A2,A12,SUPINF_2:14;
        -|.y.| <> +infty by A2,SUPINF_2:4,17;
        then
A18:    -|.x.|+(-|.y.|) <= x + y by A1,A2,A11,SUPINF_2:14;
        -|.x.|-|.y.| = -(|.x.|+|.y.|) by Th14;
        hence thesis by A17,A18,Th60;
      end;
    end;
    hence thesis;
  end;
  suppose
A19: x <> +infty & x <> -infty;
    then -x <> +infty & -x <> -infty by EXTREAL1:4;
    then
A20: |.x.| <> +infty & |.x.| <> -infty by A19,EXTREAL1:def 3;
    then
A21: x + y <= |.x.| + |.y.| by A2,A19,SUPINF_2:14;
    -|.x.| <> +infty & -|.x.| <> -infty by A20,EXTREAL1:4;
    then
A22: -|.x.|+(-|.y.|) <= x + y by A2,A19,SUPINF_2:14;
    -|.x.|-|.y.| = -(|.x.|+|.y.|) by Th14;
    hence thesis by A21,A22,Th60;
  end;
end;

theorem Th62:  :: extension of ABSVALUE:14
  -infty < x & x < +infty & x <> 0 implies |.x.|*|. 1./x .| = 1.
proof
  assume that
A1: -infty < x and
A2: x < +infty and
A3: x <> 0;
  reconsider a = x as Real by A1,A2,XXREAL_0:14;
A4: 1./x = 1/a by A3,EXTREAL1:32,MESFUNC1:def 8;
  per cases;
  suppose
A5: 0 <= x;
    then
A6: |.x.| = a by EXTREAL1:def 3;
    0 < 1/a by A3,A5,REAL_2:127;
    then |. 1./x .| = 1/a by A4,EXTREAL1:36;
    then |.x.|*|. 1./x .| = a * (1/a) by A6,EXTREAL1:13;
    hence thesis by A3,MESFUNC1:def 8,XCMPLX_1:107;
  end;
  suppose
A7: not 0 <= x;
    then
A8: |.x.| = -x by EXTREAL1:def 3
      .= -a by SUPINF_2:3;
    1/a < 0 by A7,REAL_2:149;
    then |. 1./x .| = -(1./x) by A4,EXTREAL1:37
      .= -(1/a) by A4,SUPINF_2:3;
    then |.x.|*|. 1./x .| = (-a) * (-(1/a)) by A8,EXTREAL1:13
      .= a * (1/a);
    hence thesis by A7,MESFUNC1:def 8,XCMPLX_1:107;
  end;
end;

theorem
  x = +infty or x = -infty implies |.x.|*|. 1./x .| = 0
proof
  assume x = +infty or x = -infty;
  then 1./x = 0 by EXTREAL1:33,MESFUNC1:def 8;
  then |. 1./x .| = 0 by EXTREAL1:def 3;
  hence thesis by EXTREAL1:22;
end;

theorem  :: extension of ABSVALUE:15
  x <> 0 implies |. 1./x .| = 1./|.x.|
proof
  assume
A1: x <> 0;
  per cases;
  suppose
A2: x = +infty;
    then 1./x = 0 by EXTREAL1:33,MESFUNC1:def 8;
    then
A3: |. 1./x .| = 0 by EXTREAL1:def 3;
    |.x.| = +infty by A2,EXTREAL1:36;
    hence thesis by A3,EXTREAL1:33,MESFUNC1:def 8;
  end;
  suppose
A4: x = -infty;
    then 1./x = 0 by EXTREAL1:33,MESFUNC1:def 8;
    then
A5: |. 1./x .| = 0 by EXTREAL1:def 3;
    |.x.| = +infty by A4,EXTREAL1:37,SUPINF_2:4;
    hence thesis by A5,EXTREAL1:33,MESFUNC1:def 8;
  end;
  suppose
A6: x <> +infty & x <> -infty;
    -infty <= x & x <= +infty by XXREAL_0:4,5;
    then
A7: -infty < x & x < +infty by A6,XXREAL_0:1;
    then
A8: |. 1./x .|*|.x.| = 1. by A1,Th62;
    -(+infty) < x & 0 < |.x.| by A1,A7,Th52,SUPINF_2:def 3;
    then |.x.| <> 0 & -infty < |.x.| & |.x.| < +infty by A7,Th59;
    hence thesis by A8,Th19;
  end;
end;

theorem  :: extension of ABSVALUE:16
  not((x=-infty or x=+infty) & (y=-infty or y=+infty)) & y<>0 implies
  |.x/y.| = |.x.|/|.y.|
proof
  assume that
A1: not((x=-infty or x=+infty) & (y=-infty or y=+infty)) and
A2: y<>0;
  per cases;
  suppose
A3: x = +infty;
    then y is Real by A1,XXREAL_0:14;
    then
A4: -infty < y & y < +infty by XXREAL_0:9,12;
A5: |.x.| = +infty by A3,EXTREAL1:36;
    now per cases by A2;
      suppose
A6:     0 < y;
        then
A7:     |.y.| = y by EXTREAL1:36;
        x / y = +infty by A3,A4,A6,Th17;
        then |.x/y.| = +infty by EXTREAL1:36;
        hence thesis by A4,A5,A6,A7,Th17;
      end;
      suppose
A8:     y < 0;
        then |.y.| = -y by EXTREAL1:37;
        then
A9:     |.y.| < +infty by A4,SUPINF_2:4,17;
A10:    0 < |.y.| by A2,Th52;
        x / y = -infty by A3,A4,A8,Th18;
        then |.x/y.| = +infty by EXTREAL1:37,SUPINF_2:4;
        hence thesis by A5,A9,A10,Th17;
      end;
    end;
    hence thesis;
  end;
  suppose
A11: x = -infty;
    then y is Real by A1,XXREAL_0:14;
    then
A12: -infty < y & y < +infty by XXREAL_0:9,12;
A13: |.x.| = +infty by A11,EXTREAL1:37,SUPINF_2:4;
    now per cases by A2;
      suppose
A14:    0 < y;
        then
A15:    |.y.| = y by EXTREAL1:36;
        x / y = -infty by A11,A12,A14,Th18;
        then |.x/y.| = +infty by EXTREAL1:37,SUPINF_2:4;
        hence thesis by A12,A13,A14,A15,Th17;
      end;
      suppose
A16:    y < 0;
        then |.y.| = -y by EXTREAL1:37;
        then
A17:    |.y.| < +infty by A12,SUPINF_2:4,17;
A18:    0 < |.y.| by A2,Th52;
        x / y = +infty by A11,A12,A16,Th17;
        then |.x/y.| = +infty by EXTREAL1:36;
        hence thesis by A13,A17,A18,Th17;
      end;
    end;
    hence thesis;
  end;
  suppose
A19: x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    x is Real by A19,XXREAL_0:14;
    then -infty < x & x < +infty by XXREAL_0:9,12;
    then -(+infty) < x & x < +infty by EXTREAL1:4;
    then
A20: |.x.| < +infty by Th59;
A21: -infty < 0 & 0 <= |.x.| by Th51;
    now per cases;
      suppose
A22:    y = +infty;
        then x / y = 0 by A19,EXTREAL1:33;
        then
A23:    |.x/y.| = 0 by EXTREAL1:def 3;
        |.y.| = +infty by A22,EXTREAL1:36;
        hence thesis by A20,A21,A23,EXTREAL1:33;
      end;
      suppose
A24:    y = -infty;
        then x / y = 0 by A19,EXTREAL1:33;
        then
A25:    |.x/y.| = 0 by EXTREAL1:def 3;
        |.y.| = +infty by A24,EXTREAL1:37,SUPINF_2:4;
        hence thesis by A20,A21,A25,EXTREAL1:33;
      end;
      suppose y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        x / y = a / b by A2,EXTREAL1:32;
        then
A26:    |.x/y.| = abs(a/b) by Th49;
A27:    0 <> |.y.| by A2,Th52;
        |.x.| = abs(a) & |.y.| = abs b by Th49;
        then |.x.|/|.y.| = abs(a)/abs(b) by A27,EXTREAL1:32;
        hence thesis by A26,COMPLEX1:153;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th66:  :: extension of ABSVALUE:17
  |.x.| = |.-x.|
proof
  per cases;
  suppose
A1: 0 < x;
    then -x < 0 by EXTREAL1:25;
    then |.-x.| = -(-x) by EXTREAL1:37
      .= x;
    hence thesis by A1,EXTREAL1:36;
  end;
  suppose
A2: x < 0;
    then
A3: |.x.| = -x by EXTREAL1:37;
    0 < -x by A2,EXTREAL1:25;
    hence thesis by A3,EXTREAL1:36;
  end;
  suppose x = 0;
    hence thesis by EXTREAL1:24;
  end;
end;

theorem Th67:
  |.+infty.| = +infty & |.-infty.| = +infty
proof
  thus |.+infty.| = +infty by EXTREAL1:36;
  --infty = +infty by EXTREAL1:4;
  hence |.-infty.| = +infty by EXTREAL1:37;
end;

theorem Th68:  :: extension of ABSVALUE:18
  x is Real or y is Real implies |.x.|-|.y.| <= |.x-y.|
proof
  assume
A1: x is Real or y is Real;
  per cases by A1;
  suppose
A2: y is Real;
    then reconsider b = y as Real;
A3: |.y.| = abs b by Th49;
    (x - y) + y = x by A2,MEASURE6:8;
    then |.x.| <= |.x-y.| + |.y.| by A2,Th61;
    hence thesis by A3,Th34;
  end;
  suppose x is Real;
    then reconsider a = x as Real;
A4: |.x.| = abs a by Th49;
    now per cases;
      suppose y = +infty or y = -infty;
        then |.x.|-|.y.| < 0 & 0 <= |.x-y.| by A4,Th51,Th67,SUPINF_2:6;
        hence thesis;
      end;
      suppose y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        x - y = a - b by SUPINF_2:5;
        then
A5:     |.y.| = abs b & |.x-y.| = abs(a-b) by Th49;
        then |.x.|-|.y.| = abs(a)-abs(b) by A4,SUPINF_2:5;
        hence thesis by A5,COMPLEX1:145;
      end;
    end;
    hence thesis;
  end;
end;

theorem  :: extension of ABSVALUE:19
  not((x = +infty & y = +infty) or (x = -infty & y = -infty)) implies
  |.x-y.| <= |.x.| + |.y.|
proof
  assume
A1: not((x = +infty & y = +infty) or (x = -infty & y = -infty));
  per cases;
  suppose
A2: x = +infty;
    then x - y = +infty by A1,SUPINF_2:6;
    then
A3: |.x-y.| = +infty & |.x.| = +infty by A2,EXTREAL1:36;
    -infty < 0 & 0 <= |.y.| by Th51;
    hence thesis by A3,SUPINF_2:def 2;
  end;
  suppose
A4: x = -infty;
    then
A5: x - y = -infty by A1,SUPINF_2:7;
    -infty < 0 & 0 <= |.y.| by Th51;
    hence thesis by A4,A5,Th67,SUPINF_2:def 2;
  end;
  suppose
A6: x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    now per cases;
      suppose
A7:     y = +infty;
        then
A8:     x - y = -infty by A6,SUPINF_2:6;
        -infty < 0 & 0 <= |.x.| by Th51;
        hence thesis by A7,A8,Th67,SUPINF_2:def 2;
      end;
      suppose
A9:     y = -infty;
        then
A10:    x - y = +infty by A6,SUPINF_2:7;
        -infty < 0 & 0 <= |.x.| by Th51;
        hence thesis by A9,A10,Th67,SUPINF_2:def 2;
      end;
      suppose y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        x - y = a - b by SUPINF_2:5;
        then
A11:    |.x-y.| = abs(a-b) & |.x.|=abs a & |.y.|=abs b by Th49;
        then |.x.|+|.y.|=abs(a)+abs(b) by SUPINF_2:1;
        hence thesis by A11,COMPLEX1:143;
      end;
    end;
    hence thesis;
  end;
end;

theorem  :: extension of ABSVALUE:20
  |.|.x.|.| = |.x.|
proof
  0 <= |.x.| by Th51;
  hence thesis by EXTREAL1:def 3;
end;

theorem  :: extension of ABSVALUE:21
  not((x = +infty & y = -infty) or (x = -infty & y = +infty)) &
  |.x.| <= z & |.y.| <= w implies |.x+y.| <= z + w
proof
  assume that
A1: not((x=+infty & y=-infty) or (x=-infty & y=+infty)) and
A2: |.x.| <= z and
A3: |.y.| <= w;
A4: 0 <= |.x.| & 0 <= |.y.| by Th51;
  0 <= z & 0 <= w & -infty < 0 by A2,A3,Th51;
  then -z < -(-infty) & -w < -(-infty) by SUPINF_2:17;
  then
A5: -z < +infty & -w < +infty by EXTREAL1:4;
A6: -z <= x & x <= z & -w <= y & y <= w by A2,A3,Th60;
  then
A7: x + y <= z + w by A1,A2,A3,A4,SUPINF_2:14;
A8: -z + -w <= x + y by A1,A5,A6,SUPINF_2:14;
  -z -w = -(z + w) by Th14;
  hence thesis by A7,A8,Th60;
end;

theorem  :: extension of ABSVALUE:22
  x is Real or y is Real implies |.|.x.|-|.y.|.| <= |.x-y.|
proof
  assume
A1: x is Real or y is Real;
  then
A2: |.x.|-|.y.| <= |.x-y.| by Th68;
A3: |.y.|-|.x.| <= |.y-x.| by A1,Th68;
  y - x = -(x - y) by Th15;
  then
A4: |.y-x.| = |.x-y.| by Th66;
  |.y.|-|.x.| = -(|.x.|-|.y.|) by Th15;
  then -|.x-y.| <= -(-(|.x.|-|.y.|)) by A3,A4,SUPINF_2:16;
  hence thesis by A2,Th60;
end;

theorem  :: extension of ABSVALUE:24
  0 <= x * y implies |.x+y.| = |.x.|+|.y.|
proof
  assume
A1: 0 <= x * y;
  per cases by A1,Th44;
  suppose
A2: (0 <= x or 0 < x) & (0 <= y or 0 < y);
    then
A3: |.x.| = x & |.y.| = y by EXTREAL1:def 3;
    0 <= x + y by A2,Th7;
    hence thesis by A3,EXTREAL1:def 3;
  end;
  suppose
A4: (x <= 0 or x < 0) & (y <= 0 or y < 0);
    then
A5: |.x.| = -x & |.y.| = -y by Th55;
    |.x+y.| = -(x + y) by A4,Th9,Th55
      .= -x -y by Th14;
    hence thesis by A5;
  end;
end;

begin

canceled 6;

theorem  :: extension of SQUARE_1:34
  x <> +infty & y <> +infty &
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty))
  implies min(x,y) = (x + y - |.x - y.|) / R_EAL 2
proof
  assume that
A1: x <> +infty & y <> +infty and
A2: not ((x = +infty & y = +infty) or (x = -infty & y = -infty));
  per cases;
  suppose
A3: x = -infty;
    then
A4: min(x,y) = -infty by XXREAL_0:44;
A5: x + y = -infty by A1,A3,SUPINF_2:def 2;
    x - y = -infty by A2,A3,SUPINF_2:7;
    then
A6: x + y - |.x - y.| = -infty by A5,Th67,SUPINF_2:7;
    0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
    then 0 < R_EAL 2 & R_EAL 2 < +infty by XXREAL_0:9;
    hence thesis by A4,A6,Th18;
  end;
  suppose x <> -infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
    now per cases;
      suppose
A7:     y = -infty;
        then
A8:     min(x,y) = -infty by XXREAL_0:44;
A9:     x + y = -infty by A1,A7,SUPINF_2:def 2;
        x - y = +infty by A2,A7,SUPINF_2:7;
        then
A10:    x + y - |.x - y.| = -infty by A9,Th67,SUPINF_2:7;
        0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
        then 0 < R_EAL 2 & R_EAL 2 < +infty by XXREAL_0:9;
        hence thesis by A8,A10,Th18;
      end;
      suppose y <> -infty;
        then reconsider b = y as Real by A1,XXREAL_0:14;
A11:    x + y = a + b by SUPINF_2:1;
        x - y = a - b by SUPINF_2:5;
        then |.x - y.| = abs(a - b) by Th49;
        then
A12:    x + y - |.x - y.| = a + b - abs(a - b) by A11,SUPINF_2:5;
        0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
        then (x + y - |.x - y.|) / R_EAL 2 = (a+b-abs(a-b))/2
        by A12,EXTREAL1:32;
        hence thesis by COMPLEX1:159;
      end;
    end;
    hence thesis;
  end;
end;

canceled 10;

theorem  :: extension of SQUARE_1:45
  x <> -infty & y <> -infty &
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty))
  implies max(x,y) = (x + y + |.x - y.|) / R_EAL 2
proof
  assume that
A1: x <> -infty & y <> -infty and
A2: not ((x = +infty & y = +infty) or (x = -infty & y = -infty));
  per cases;
  suppose
A3: x = +infty;
    then
A4: max(x,y) = +infty by XXREAL_0:41;
A5: x + y = +infty by A1,A3,SUPINF_2:def 2;
    x - y = +infty by A2,A3,SUPINF_2:6;
    then
A6: x + y + |.x - y.| = +infty by A5,Th67,SUPINF_2:def 2;
    0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
    then 0 < R_EAL 2 & R_EAL 2 < +infty by XXREAL_0:9;
    hence thesis by A4,A6,Th17;
  end;
  suppose x <> +infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
    now per cases;
      suppose
A7:     y = +infty;
        then
A8:     max(x,y) = +infty by XXREAL_0:41;
A9:     x + y = +infty by A1,A7,SUPINF_2:def 2;
        x - y = -infty by A2,A7,SUPINF_2:6;
        then
A10:    x + y + |.x - y.| = +infty by A9,Th67,SUPINF_2:def 2;
        0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
        then 0 < R_EAL 2 & R_EAL 2 < +infty by XXREAL_0:9;
        hence thesis by A8,A10,Th17;
      end;
      suppose y <> +infty;
        then reconsider b = y as Real by A1,XXREAL_0:14;
A11:    x + y = a + b by SUPINF_2:1;
        x - y = a - b by SUPINF_2:5;
        then |.x - y.| = abs(a - b) by Th49;
        then
A12:    x + y + |.x - y.| = a + b + abs(a - b) by A11,SUPINF_2:1;
        0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
        then (x + y + |.x - y.|) / R_EAL 2 = (a+b+abs(a-b))/2
        by A12,EXTREAL1:32;
        hence thesis by COMPLEX1:160;
      end;
    end;
    hence thesis;
  end;
end;

definition
  let x,y be R_eal;
  redefine
  func max(x,y) -> R_eal;
  coherence by XXREAL_0:def 1;
  func min(x,y) -> R_eal;
  coherence by XXREAL_0:def 1;
end;

canceled 8;

theorem  :: extension of SQUARE_1:53
  min(x,y) + max(x,y)= x + y
proof
  per cases;
  suppose x <= y;
    then min(x,y) = x & max(x,y) = y by XXREAL_0:def 8,def 9;
    hence thesis;
  end;
  suppose not (x <= y);
    then min(x,y) = y & max(x,y) = x by XXREAL_0:def 8,def 9;
    hence thesis;
  end;
end;

begin ::Addenda

:: missing, 2007.07.20, A.T.

theorem Th101:
  |.x.| = +infty implies x = +infty or x = -infty
proof
  assume
A1: |.x.| = +infty;
  |.x.| = x or -|.x.| = --x by EXTREAL1:def 3;
  hence x = +infty or x = -infty by A1,SUPINF_2:4;
end;

theorem
  |.x.| < +infty iff x in REAL
proof
  thus |.x.| < +infty implies x in REAL by Th67,XXREAL_0:14;
  assume
A1: x in REAL;
  assume |.x.| >= +infty;
  then x = +infty or x = -infty by Th101,XXREAL_0:4;
  hence contradiction by A1,XXREAL_0:9,12;
end;

