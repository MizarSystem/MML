:: Some Properties of Extended Real Numbers Operations: absolute
:: value, min and max
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received September 15, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies SUPINF_1, ARYTM, MEASURE6, GROUP_1, ARYTM_1, ARYTM_3, COMPLEX1,
      ABSVALUE, SQUARE_1;
 notations ORDINAL1, NUMBERS, XCMPLX_0, XXREAL_0, XREAL_0, XXREAL_3, COMPLEX1,
      REAL_1, SUPINF_1, SUPINF_2, MEASURE6, EXTREAL1, MESFUNC1;
 constructors REAL_1, COMPLEX1, SUPINF_2, MEASURE6, EXTREAL1, MESFUNC1,
      SUPINF_1;
 registrations NUMBERS, XXREAL_0, XREAL_0, MEMBERED, XBOOLE_0, XXREAL_3;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions SUPINF_2, XXREAL_3;
 theorems MEASURE6, SUPINF_2, EXTREAL1, MESFUNC1, ABSVALUE, XCMPLX_1, COMPLEX1,
      XREAL_1, XXREAL_0, XXREAL_3;

begin :: Preliminaries

reserve x,y,w,z for R_eal,
  a,b for Real;

begin :: Basic properties of absolute value for extended real numbers

canceled 48;

theorem Th49:
  x = a implies |.x.| = abs(a)
proof
  assume
A1: x = a;
  per cases;
  suppose
A2: 0 <= x;
    then |.x.| = a by A1,EXTREAL1:def 3;
    hence thesis by A1,A2,ABSVALUE:def 1;
  end;
  suppose
A3: not 0 <= x;
    then |.x.| = -x by EXTREAL1:37
      .= -a by A1,SUPINF_2:3;
    hence thesis by A1,A3,ABSVALUE:def 1;
  end;
end;

theorem
  |.x.| = x or |.x.| = -x by EXTREAL1:def 3;

theorem Th51:  :: extension of ABSVALUE:5
  0 <= |.x.|
proof
  per cases;
  suppose 0 <= x;
    hence thesis by EXTREAL1:def 3;
  end;
  suppose
A1: not (0 <= x);
    then |.x.| = -x by EXTREAL1:def 3;
    hence thesis by A1;
  end;
end;

theorem Th52:  :: extension of ABSVALUE:6
  x <> 0 implies 0 < |.x.|
proof
  assume
A1: x <> 0;
  per cases;
  suppose 0 <= x;
    hence thesis by A1,EXTREAL1:def 3;
  end;
  suppose
A2: not (0 <= x);
    then 0 < -x;
    hence thesis by A2,EXTREAL1:def 3;
  end;
end;

theorem  :: extension of ABSVALUE:7
  x = 0 iff |.x.| = 0 by Th52,EXTREAL1:def 3;

theorem  :: extension of ABSVALUE:9
  |.x.| = -x & x <> 0 implies x < 0 by EXTREAL1:def 3;

theorem Th55:
  x <= 0 implies |.x.| = -x
proof
  assume
A1: x <= 0;
  per cases by A1;
  suppose x < 0;
    hence thesis by EXTREAL1:def 3;
  end;
  suppose
S: x = 0;
     then -0 = -x;
    hence thesis by S,EXTREAL1:def 3;
  end;
end;

theorem  :: extension of ABSVALUE:10
  |.x * y.| = |.x.| * |.y.|
proof
  per cases;
  suppose x = 0;
    then x * y = 0 & |.x.| = 0 by EXTREAL1:def 3;
    then |.x * y.| = 0 & |.x.| * |.y.| = 0 by EXTREAL1:def 3;
    hence thesis;
  end;
  suppose
A1: 0 < x;
    now per cases;
      suppose y = 0;
        then x * y = 0 & |.y.| = 0 by EXTREAL1:def 3;
        then |.x * y.| = 0 & |.x.| * |.y.| = 0 by EXTREAL1:def 3;
        hence thesis;
      end;
      suppose 0 < y;
        then 0 <= x * y & |.x.| = x & |.y.| = y by A1,EXTREAL1:def 3;
        hence thesis by EXTREAL1:def 3;
      end;
      suppose
A2:     y < 0;
        then |.x.| = x & |.y.| = -y by A1,EXTREAL1:def 3;
        then
        |.x.| * |.y.| = -(x * y) by XXREAL_3:103;
        hence thesis by A1,A2,EXTREAL1:def 3;
      end;
    end;
    hence thesis;
  end;
  suppose
A4: x < 0;
    now per cases;
      suppose y = 0;
        then x * y = 0 & |.y.| = 0 by EXTREAL1:def 3;
        then |.x * y.| = 0 & |.x.| * |.y.| = 0 by EXTREAL1:def 3;
        hence thesis;
      end;
      suppose 0 < y;
        then not 0 <= x * y & |.y.| = y by A4,EXTREAL1:def 3;

then |.x * y.| = -(x * y) & |.x.| * |.y.| = (-x) * y by A4,EXTREAL1:def 3;
        hence thesis by XXREAL_3:103;
      end;
      suppose
S:     y < 0;
        then
        0 <= x * y & |.y.| = -y by A4,EXTREAL1:def 3;

then |.x * y.| = x * y & |.x.| * |.y.| = (-x) * (-y) by A4,EXTREAL1:def 3;
        then |.x.| * |.y.| = -(x * (-y)) by XXREAL_3:103
          .= -(-(x * y)) by XXREAL_3:103;
        hence thesis by S,A4,EXTREAL1:def 3;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th57:  :: extension of ABSVALUE:11
  -|.x.| <= x & x <= |.x.|
proof
  per cases;
  suppose
A1: 0 <= x;
    0 <= |.x.| by Th51;
    hence thesis by A1,EXTREAL1:def 3;
  end;
  suppose
A2: not 0 <= x;
    then |.x.|=-x by EXTREAL1:def 3;
    hence thesis by A2;
  end;
end;

theorem Th58:
  |.x.| < y implies -y < x & x < y
proof
  assume
A1: |.x.| < y;
  per cases;
  suppose
A2: 0 <= x;
    then x < y by A1,EXTREAL1:def 3;
    hence thesis by A2;
  end;
  suppose
A3: not 0 <= x;
    then |.x.|=-x by EXTREAL1:def 3;
    hence thesis by A1,A3,XXREAL_3:71;
  end;
end;

theorem Th59:
  -y < x & x < y implies 0 < y & |.x.| < y
proof
  assume
A1: -y < x & x < y;
  per cases;
  suppose 0 <= x;
    hence thesis by A1,EXTREAL1:def 3;
  end;
  suppose
A2: not 0 <= x;
    then 0 < -x & -x < y by A1,XXREAL_3:71;
    hence thesis by A2,EXTREAL1:def 3;
  end;
end;

theorem Th60: :: extension of ABSVALUE:12
  -y <= x & x <= y iff |.x.| <= y
proof
A1: -y <= x & x <= y implies |.x.| <= y
  proof
    assume
A2: -y <= x & x <= y;
    per cases;
    suppose 0 <= x;
      hence thesis by A2,EXTREAL1:def 3;
    end;
    suppose
A3:   not 0 <= x;
      then 0 < -x & -x <= y by A2,XXREAL_3:71;
      hence thesis by A3,EXTREAL1:def 3;
    end;
  end;
  |.x.| <= y implies -y <= x & x <= y
  proof
    assume
A4: |.x.| <= y;
    per cases by A4,XXREAL_0:1;
    suppose |.x.| = y;
      hence thesis by Th57;
    end;
    suppose |.x.| < y;
      hence thesis by Th58;
    end;
  end;
  hence thesis by A1;
end;

theorem Th61:  :: extension of ABSVALUE:13
  not ((x = +infty & y = -infty) or (x = -infty & y = +infty)) implies
  |.x + y.| <= |.x.| + |.y.|
proof
  assume
A1: not ((x = +infty & y = -infty) or (x = -infty & y = +infty));
A2: -|.x.| <= x & x <= |.x.| & -|.y.| <= y & y <= |.y.| by Th57;
  per cases;
  suppose
A3: x = +infty;
    then +infty <= |.x.| & y <> -infty & -infty <= y by A1,Th57,XXREAL_0:5;
    then
A4: |.x.| = +infty & -infty < y by XXREAL_0:1,4;
    now per cases;
      suppose
A5:     y = +infty;
        -|.x.|-|.y.| = -(|.x.|+|.y.|) by XXREAL_3:26;
        hence thesis by A3,A4,A5,Th60;
      end;
      suppose
        y <> +infty;
A8:     x + y <= |.x.| + |.y.| by A2,XXREAL_3:38;
A10:    -|.x.|+(-|.y.|) <= x + y by A2,XXREAL_3:38;
        -|.x.|-|.y.| = -(|.x.|+|.y.|) by XXREAL_3:26;
        hence thesis by A8,A10,Th60;
      end;
    end;
    hence thesis;
  end;
  suppose
A11: x = -infty;
    now per cases;
      suppose
A13:    y = -infty;
        then
A14:    -|.y.| = -infty by Th57,XXREAL_0:6;
A15:    x + y <= |.x.| + |.y.| by A2,XXREAL_3:38;
        -|.x.|-|.y.| = -(|.x.|+|.y.|) by XXREAL_3:26;
        hence thesis by A11,A13,A14,A15,Th60;
      end;
      suppose
        y <> -infty;
A17:    x + y <= |.x.| + |.y.| by A2,XXREAL_3:38;
A18:    -|.x.|+(-|.y.|) <= x + y by A2,XXREAL_3:38;
        -|.x.|-|.y.| = -(|.x.|+|.y.|) by XXREAL_3:26;
        hence thesis by A17,A18,Th60;
      end;
    end;
    hence thesis;
  end;
  suppose
     x <> +infty & x <> -infty;
A21: x + y <= |.x.| + |.y.| by A2,XXREAL_3:38;
A22: -|.x.|+(-|.y.|) <= x + y by A2,XXREAL_3:38;
    -|.x.|-|.y.| = -(|.x.|+|.y.|) by XXREAL_3:26;
    hence thesis by A21,A22,Th60;
  end;
end;

theorem Th62:  :: extension of ABSVALUE:14
  -infty < x & x < +infty & x <> 0 implies |.x.|*|. 1./x .| = 1.
proof
  assume that
A1: -infty < x and
A2: x < +infty and
A3: x <> 0;
  reconsider a = x as Real by A1,A2,XXREAL_0:14;
A4: 1./x = 1/a by EXTREAL1:32,MESFUNC1:def 8;
  per cases;
  suppose
A5: 0 <= x;
    then
A6: |.x.| = a by EXTREAL1:def 3;
    |. 1./x .| = 1/a by A4,A3,A5,EXTREAL1:36,XREAL_1:141;
    then |.x.|*|. 1./x .| = a * (1/a) by A6,EXTREAL1:13;
    hence thesis by A3,MESFUNC1:def 8,XCMPLX_1:107;
  end;
  suppose
A7: not 0 <= x;
    then
A8: |.x.| = -x by EXTREAL1:def 3
      .= -a by SUPINF_2:3;
    |. 1./x .| = -(1./x) by A4,A7,EXTREAL1:37,XREAL_1:144
      .= -(1/a) by A4,SUPINF_2:3;
    then |.x.|*|. 1./x .| = (-a) * (-(1/a)) by A8,EXTREAL1:13
      .= a * (1/a);
    hence thesis by A7,MESFUNC1:def 8,XCMPLX_1:107;
  end;
end;

theorem
  x = +infty or x = -infty implies |.x.|*|. 1./x .| = 0
proof
  assume x = +infty or x = -infty;
  then |. 1./x .| = 0 by EXTREAL1:def 3;
  hence thesis;
end;

theorem  :: extension of ABSVALUE:15
  x <> 0 implies |. 1./x .| = 1./|.x.|
proof
  assume
A1: x <> 0;
  per cases;
  suppose
A2: x = +infty;
    then |. 1./x .| = 0 by EXTREAL1:def 3;
    hence thesis by A2,EXTREAL1:36;
  end;
  suppose
A4: x = -infty;
    then
A5: |. 1./x .| = 0 by EXTREAL1:def 3;
    |.x.| = +infty by A4,EXTREAL1:37,XXREAL_3:5;
    hence thesis by A5;
  end;
  suppose
A6: x <> +infty & x <> -infty;
    -infty <= x & x <= +infty by XXREAL_0:4,5;
    then
A7: -infty < x & x < +infty by A6,XXREAL_0:1;
    then
A8: |. 1./x .|*|.x.| = 1. by A1,Th62;
    -(+infty) < x & 0 < |.x.| by A1,A7,Th52,XXREAL_3:def 3;
    then |.x.| <> 0 & -infty < |.x.| & |.x.| < +infty by A7,Th59;
    hence thesis by A8,XXREAL_3:99;
  end;
end;

theorem  :: extension of ABSVALUE:16
  not((x=-infty or x=+infty) & (y=-infty or y=+infty)) & y<>0 implies
  |.x/y.| = |.x.|/|.y.|
proof
  assume that
A1: not((x=-infty or x=+infty) & (y=-infty or y=+infty)) and
A2: y<>0;
  per cases;
  suppose
A3: x = +infty;
    then y is Real by A1,XXREAL_0:14;
    then
A4: -infty < y & y < +infty by XXREAL_0:9,12;
    now per cases by A2;
      suppose
A6:     0 < y;
        then
A7:     |.y.| = y by EXTREAL1:36;
        x / y = +infty by A3,A4,A6,XXREAL_3:94;
        then
X:        |.x/y.| = +infty by EXTREAL1:36;
         |.x.| = +infty by A3,EXTREAL1:36;
        hence thesis by A4,A6,A7,X,XXREAL_3:94;
      end;
      suppose
A8:     y < 0;
        then |.y.| = -y by EXTREAL1:37;
        then
A9:     |.y.| < +infty by A4,XXREAL_3:5,40;
A10:    0 < |.y.| by A2,Th52;
        x / y = -infty by A3,A4,A8,XXREAL_3:96;
        then
X:       |.x/y.| = +infty by EXTREAL1:37,XXREAL_3:5;
         |.x.| = +infty by A3,EXTREAL1:36;
        hence thesis by A9,A10,X,XXREAL_3:94;
      end;
    end;
    hence thesis;
  end;
  suppose
A11: x = -infty;
    then y is Real by A1,XXREAL_0:14;
    then
A12: -infty < y & y < +infty by XXREAL_0:9,12;
    now per cases by A2;
      suppose
A14:    0 < y;
        then
A15:    |.y.| = y by EXTREAL1:36;
        x / y = -infty by A11,A12,A14,XXREAL_3:97;
        then
X:       |.x/y.| = +infty by EXTREAL1:37,XXREAL_3:5;
         |.x.| = +infty by A11,EXTREAL1:37,XXREAL_3:5;
        hence thesis by A12,A14,A15,X,XXREAL_3:94;
      end;
      suppose
A16:    y < 0;
        then |.y.| = -y by EXTREAL1:37;
        then
A17:    |.y.| < +infty by A12,XXREAL_3:5,40;
A18:    0 < |.y.| by A2,Th52;
        x / y = +infty by A11,A12,A16,XXREAL_3:95;
        then
X:       |.x/y.| = +infty by EXTREAL1:36;
         |.x.| = +infty by A11,EXTREAL1:37,XXREAL_3:5;
        hence thesis by X,A17,A18,XXREAL_3:94;
      end;
    end;
    hence thesis;
  end;
  suppose x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    now per cases;
      suppose
A22:    y = +infty;
        then
A23:    |.x/y.| = 0 by EXTREAL1:def 3;
        |.y.| = +infty by A22,EXTREAL1:36;
        hence thesis by A23;
      end;
      suppose
A24:    y = -infty;
        then
A25:    |.x/y.| = 0 by EXTREAL1:def 3;
        |.y.| = +infty by A24,EXTREAL1:37,XXREAL_3:5;
        hence thesis by A25;
      end;
      suppose y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        x / y = a / b by EXTREAL1:32;
        then
A26:    |.x/y.| = abs(a/b) by Th49;
        |.x.| = abs(a) & |.y.| = abs b by Th49;
        then |.x.|/|.y.| = abs(a)/abs(b) by EXTREAL1:32;
        hence thesis by A26,COMPLEX1:153;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th66:  :: extension of ABSVALUE:17
  |.x.| = |.-x.|
proof
  per cases;
  suppose
A1: 0 < x;
    then |.-x.| = -(-x) by EXTREAL1:37
      .= x;
    hence thesis by A1,EXTREAL1:36;
  end;
  suppose
A2: x < 0;
    then
    |.x.| = -x by EXTREAL1:37;
    hence thesis by A2,EXTREAL1:36;
  end;
  suppose x = 0;
    hence thesis;
  end;
end;

theorem Th67:
  |.+infty.| = +infty & |.-infty.| = +infty
proof
  thus |.+infty.| = +infty by EXTREAL1:36;
  --infty = +infty by XXREAL_3:23;
  hence thesis by EXTREAL1:37;
end;

theorem Th68:  :: extension of ABSVALUE:18
  x is Real or y is Real implies |.x.|-|.y.| <= |.x-y.|
proof
  assume
A1: x is Real or y is Real;
  per cases by A1;
  suppose
A2: y is Real;
    then reconsider b = y as Real;
A3: |.y.| = abs b by Th49;
    (x - y) + y = x by A2,XXREAL_3:22;
    then |.x.| <= |.x-y.| + |.y.| by A2,Th61;
    hence thesis by A3,XXREAL_3:45;
  end;
  suppose x is Real;
    then reconsider a = x as Real;
A4: |.x.| = abs a by Th49;
    now per cases;
      suppose y = +infty or y = -infty;
        then |.x.|-|.y.| < 0 & 0 <= |.x-y.| by A4,Th51,Th67,XXREAL_3:13;
        hence thesis;
      end;
      suppose y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        x - y = a - b by SUPINF_2:5;
        then
A5:     |.y.| = abs b & |.x-y.| = abs(a-b) by Th49;
        then |.x.|-|.y.| = abs(a)-abs(b) by A4,SUPINF_2:5;
        hence thesis by A5,COMPLEX1:145;
      end;
    end;
    hence thesis;
  end;
end;

theorem  :: extension of ABSVALUE:19
  not((x = +infty & y = +infty) or (x = -infty & y = -infty)) implies
  |.x-y.| <= |.x.| + |.y.|
proof
  assume
A1: not((x = +infty & y = +infty) or (x = -infty & y = -infty));
  per cases;
  suppose
A2: x = +infty;
    then x - y = +infty by A1,XXREAL_3:13;
    then
A3: |.x-y.| = +infty & |.x.| = +infty by A2,EXTREAL1:36;
    -infty < 0 & 0 <= |.y.| by Th51;
    hence thesis by A3,XXREAL_3:def 2;
  end;
  suppose
A4: x = -infty;
    then
A5: x - y = -infty by A1,XXREAL_3:14;
    -infty < 0 & 0 <= |.y.| by Th51;
    hence thesis by A4,A5,Th67,XXREAL_3:def 2;
  end;
  suppose
A6: x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    now per cases;
      suppose
A7:     y = +infty;
        then
A8:     x - y = -infty by A6,XXREAL_3:13;
        -infty < 0 & 0 <= |.x.| by Th51;
        hence thesis by A7,A8,Th67,XXREAL_3:def 2;
      end;
      suppose
A9:     y = -infty;
        then
A10:    x - y = +infty by A6,XXREAL_3:14;
        -infty < 0 & 0 <= |.x.| by Th51;
        hence thesis by A9,A10,Th67,XXREAL_3:def 2;
      end;
      suppose y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        x - y = a - b by SUPINF_2:5;
        then
A11:    |.x-y.| = abs(a-b) & |.x.|=abs a & |.y.|=abs b by Th49;
        then |.x.|+|.y.|=abs(a)+abs(b) by SUPINF_2:1;
        hence thesis by A11,COMPLEX1:143;
      end;
    end;
    hence thesis;
  end;
end;

theorem  :: extension of ABSVALUE:20
  |.|.x.|.| = |.x.|
proof
  0 <= |.x.| by Th51;
  hence thesis by EXTREAL1:def 3;
end;

theorem  :: extension of ABSVALUE:21
  |.x.| <= z & |.y.| <= w implies |.x+y.| <= z + w
proof
  assume that
A2: |.x.| <= z and
A3: |.y.| <= w;
A6: -z <= x & x <= z & -w <= y & y <= w by A2,A3,Th60;
  then
A7: x + y <= z + w by XXREAL_3:38;
A8: -z + -w <= x + y by A6,XXREAL_3:38;
  -z -w = -(z + w) by XXREAL_3:26;
  hence thesis by A7,A8,Th60;
end;

theorem  :: extension of ABSVALUE:22
  x is Real or y is Real implies |.|.x.|-|.y.|.| <= |.x-y.|
proof
  assume
A1: x is Real or y is Real;
  then
A2: |.x.|-|.y.| <= |.x-y.| by Th68;
A3: |.y.|-|.x.| <= |.y-x.| by A1,Th68;
  y - x = -(x - y) by XXREAL_3:27;
  then
A4: |.y-x.| = |.x-y.| by Th66;
  |.y.|-|.x.| = -(|.x.|-|.y.|) by XXREAL_3:27;
  then -|.x-y.| <= -(-(|.x.|-|.y.|)) by A3,A4,XXREAL_3:40;
  hence thesis by A2,Th60;
end;

theorem  :: extension of ABSVALUE:24
  0 <= x * y implies |.x+y.| = |.x.|+|.y.|
proof
  assume
A1: 0 <= x * y;
  per cases by A1;
  suppose
A2: (0 <= x or 0 < x) & (0 <= y or 0 < y);
    then
    |.x.| = x & |.y.| = y by EXTREAL1:def 3;
    hence thesis by A2,EXTREAL1:def 3;
  end;
  suppose
A4: (x <= 0 or x < 0) & (y <= 0 or y < 0);
    then
A5: |.x.| = -x & |.y.| = -y by Th55;
    |.x+y.| = -(x + y) by A4,Th55
      .= -x -y by XXREAL_3:26;
    hence thesis by A5;
  end;
end;

begin

canceled 6;

theorem  :: extension of SQUARE_1:34
  x <> +infty & y <> +infty &
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty))
  implies min(x,y) = (x + y - |.x - y.|) / R_EAL 2
proof
  assume that
A1: x <> +infty & y <> +infty and
A2: not ((x = +infty & y = +infty) or (x = -infty & y = -infty));
  per cases;
  suppose
A3: x = -infty;
    then
A4: min(x,y) = -infty by XXREAL_0:44;
A5: x + y = -infty by A1,A3,XXREAL_3:def 2;
    x - y = -infty by A2,A3,XXREAL_3:14;
    then
A6: x + y - |.x - y.| = -infty by A5,Th67,XXREAL_3:14;
    0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
    then 0 < R_EAL 2 & R_EAL 2 < +infty by XXREAL_0:9;
    hence thesis by A4,A6,XXREAL_3:97;
  end;
  suppose x <> -infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
    now per cases;
      suppose
A7:     y = -infty;
        then
A8:     min(x,y) = -infty by XXREAL_0:44;
A9:     x + y = -infty by A1,A7,XXREAL_3:def 2;
        x - y = +infty by A2,A7,XXREAL_3:14;
        then
A10:    x + y - |.x - y.| = -infty by A9,Th67,XXREAL_3:14;
        0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
        then 0 < R_EAL 2 & R_EAL 2 < +infty by XXREAL_0:9;
        hence thesis by A8,A10,XXREAL_3:97;
      end;
      suppose y <> -infty;
        then reconsider b = y as Real by A1,XXREAL_0:14;
A11:    x + y = a + b by SUPINF_2:1;
        x - y = a - b by SUPINF_2:5;
        then |.x - y.| = abs(a - b) by Th49;
        then
A12:    x + y - |.x - y.| = a + b - abs(a - b) by A11,SUPINF_2:5;
        0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
        then (x + y - |.x - y.|) / R_EAL 2 = (a+b-abs(a-b))/2
        by A12,EXTREAL1:32;
        hence thesis by COMPLEX1:159;
      end;
    end;
    hence thesis;
  end;
end;

canceled 10;

theorem  :: extension of SQUARE_1:45
  x <> -infty & y <> -infty &
  not ((x = +infty & y = +infty) or (x = -infty & y = -infty))
  implies max(x,y) = (x + y + |.x - y.|) / R_EAL 2
proof
  assume that
A1: x <> -infty & y <> -infty and
A2: not ((x = +infty & y = +infty) or (x = -infty & y = -infty));
  per cases;
  suppose
A3: x = +infty;
    then
A4: max(x,y) = +infty by XXREAL_0:41;
A5: x + y = +infty by A1,A3,XXREAL_3:def 2;
    x - y = +infty by A2,A3,XXREAL_3:13;
    then
A6: x + y + |.x - y.| = +infty by A5,Th67,XXREAL_3:def 2;
    0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
    then 0 < R_EAL 2 & R_EAL 2 < +infty by XXREAL_0:9;
    hence thesis by A4,A6,XXREAL_3:94;
  end;
  suppose x <> +infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
    now per cases;
      suppose
A7:     y = +infty;
        then
A8:     max(x,y) = +infty by XXREAL_0:41;
A9:     x + y = +infty by A1,A7,XXREAL_3:def 2;
        x - y = -infty by A2,A7,XXREAL_3:13;
        then
A10:    x + y + |.x - y.| = +infty by A9,Th67,XXREAL_3:def 2;
        0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
        then 0 < R_EAL 2 & R_EAL 2 < +infty by XXREAL_0:9;
        hence thesis by A8,A10,XXREAL_3:94;
      end;
      suppose y <> +infty;
        then reconsider b = y as Real by A1,XXREAL_0:14;
A11:    x + y = a + b by SUPINF_2:1;
        x - y = a - b by SUPINF_2:5;
        then |.x - y.| = abs(a - b) by Th49;
        then
A12:    x + y + |.x - y.| = a + b + abs(a - b) by A11,SUPINF_2:1;
        0 < 2 & R_EAL 2 = 2 by MEASURE6:def 1;
        then (x + y + |.x - y.|) / R_EAL 2 = (a+b+abs(a-b))/2
        by A12,EXTREAL1:32;
        hence thesis by COMPLEX1:160;
      end;
    end;
    hence thesis;
  end;
end;

definition
  let x,y be R_eal;
  redefine
  func max(x,y) -> R_eal;
  coherence by XXREAL_0:def 1;
  func min(x,y) -> R_eal;
  coherence by XXREAL_0:def 1;
end;

canceled 8;

theorem  :: extension of SQUARE_1:53
  min(x,y) + max(x,y)= x + y
proof
  per cases;
  suppose x <= y;
    then min(x,y) = x & max(x,y) = y by XXREAL_0:def 9,def 10;
    hence thesis;
  end;
  suppose not (x <= y);
    then min(x,y) = y & max(x,y) = x by XXREAL_0:def 9,def 10;
    hence thesis;
  end;
end;

begin ::Addenda

:: missing, 2007.07.20, A.T.

theorem Th101:
  |.x.| = +infty implies x = +infty or x = -infty
proof
  assume
A1: |.x.| = +infty;
  |.x.| = x or -|.x.| = --x by EXTREAL1:def 3;
  hence thesis by A1,XXREAL_3:5;
end;

theorem
  |.x.| < +infty iff x in REAL
proof
  thus |.x.| < +infty implies x in REAL by Th67,XXREAL_0:14;
  assume
A1: x in REAL;
  assume |.x.| >= +infty;
  hence contradiction by A1,Th101,XXREAL_0:4;
end;

