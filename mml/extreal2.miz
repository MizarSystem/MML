:: Some Properties of Extended Real Numbers Operations: absolute
:: value, min and max
::  by Noboru Endou , Katsumi Wasaki and Yasunari Shidama
::
:: Received September 15, 2000
:: Copyright (c) 2000 Association of Mizar Users

environ

 vocabularies NUMBERS, SUPINF_1, REAL_1, COMPLEX1, CARD_1, XXREAL_0, ARYTM_1,
      ARYTM_3, RELAT_1, MESFUNC1, MEASURE6;
 notations NUMBERS, XXREAL_0, COMPLEX1, REAL_1, SUPINF_1, SUPINF_2, MEASURE6,
      EXTREAL1, MESFUNC1;
 constructors REAL_1, COMPLEX1, SUPINF_2, MEASURE6, EXTREAL1, MESFUNC1,
      SUPINF_1, BINOP_2;
 registrations NUMBERS, XXREAL_0, XREAL_0, MEMBERED, XBOOLE_0, XXREAL_3;
 requirements NUMERALS, REAL, SUBSET, BOOLE, ARITHM;
 definitions SUPINF_2, XXREAL_3;
 theorems MEASURE6, SUPINF_2, EXTREAL1, MESFUNC1, ABSVALUE, XCMPLX_1, COMPLEX1,
      XREAL_1, XXREAL_0, XXREAL_3;

begin :: Preliminaries

reserve x,y,w,z for R_eal,
  a for Real;

begin :: Basic properties of absolute value for extended real numbers

canceled 48;

theorem Th49:
  x = a implies |.x.| = abs(a)
proof
  assume
A1: x = a;
  per cases;
  suppose
A2: 0 <= x;
    then |.x.| = a by A1,EXTREAL1:def 3;
    hence thesis by A1,A2,ABSVALUE:def 1;
  end;
  suppose
A3: not 0 <= x;
    then |.x.| = -x by EXTREAL1:37
      .= -a by A1,SUPINF_2:3;
    hence thesis by A1,A3,ABSVALUE:def 1;
  end;
end;

theorem
  |.x.| = x or |.x.| = -x by EXTREAL1:def 3;

theorem Th51: :: extension of ABSVALUE:5
  0 <= |.x.|
proof
  per cases;
  suppose
    0 <= x;
    hence thesis by EXTREAL1:def 3;
  end;
  suppose
A1: not 0 <= x;
    then |.x.| = -x by EXTREAL1:def 3;
    hence thesis by A1;
  end;
end;

theorem Th52: :: extension of ABSVALUE:6
  x <> 0 implies 0 < |.x.|
proof
  assume
A1: x <> 0;
  per cases;
  suppose
    0 <= x;
    hence thesis by A1,EXTREAL1:def 3;
  end;
  suppose
A2: not 0 <= x;
    then 0 < -x;
    hence thesis by A2,EXTREAL1:def 3;
  end;
end;

theorem :: extension of ABSVALUE:7
  x = 0 iff |.x.| = 0 by Th52,EXTREAL1:def 3;

theorem :: extension of ABSVALUE:9
  |.x.| = -x & x <> 0 implies x < 0 by EXTREAL1:def 3;

theorem Th55:
  x <= 0 implies |.x.| = -x
proof
  assume
A1: x <= 0;
  per cases by A1;
  suppose
    x < 0;
    hence thesis by EXTREAL1:def 3;
  end;
  suppose
A2: x = 0;
    then -0 = -x;
    hence thesis by A2,EXTREAL1:def 3;
  end;
end;

theorem :: extension of ABSVALUE:10
  |.x * y.| = |.x.| * |.y.|
proof
  per cases;
  suppose
    x = 0;
    then |.x.| = 0 & |.x * y.| = 0 by EXTREAL1:def 3;
    hence thesis;
  end;
  suppose
A1: 0 < x;
    now
      per cases;
      suppose
        y = 0;
        then |.y.| = 0 & |.x * y.| = 0 by EXTREAL1:def 3;
        hence thesis;
      end;
      suppose
A2:     0 < y;
        then
A3:     |.y.| = y by EXTREAL1:def 3;
        |.x.| = x by A1,EXTREAL1:def 3;
        hence thesis by A1,A2,A3,EXTREAL1:def 3;
      end;
      suppose
A4:     y < 0;
        then
A5:     |.y.| = -y by EXTREAL1:def 3;
        |.x.| = x by A1,EXTREAL1:def 3;
        then |.x.| * |.y.| = -(x * y) by A5,XXREAL_3:103;
        hence thesis by A1,A4,EXTREAL1:def 3;
      end;
    end;
    hence thesis;
  end;
  suppose
A6: x < 0;
    now
      per cases;
      suppose
        y = 0;
        then |.y.| = 0 & |.x * y.| = 0 by EXTREAL1:def 3;
        hence thesis;
      end;
      suppose
A7:     0 < y;
        then |.y.| = y by EXTREAL1:def 3;
        then
A8:     |.x.| * |.y.| = (-x) * y by A6,EXTREAL1:def 3;
        |.x * y.| = -(x * y) by A6,A7,EXTREAL1:def 3;
        hence thesis by A8,XXREAL_3:103;
      end;
      suppose
A9:     y < 0;
        then |.y.| = -y by EXTREAL1:def 3;
        then |.x.| * |.y.| = (-x) * (-y) by A6,EXTREAL1:def 3;
        then |.x.| * |.y.| = -(x * (-y)) by XXREAL_3:103
          .= -(-(x * y)) by XXREAL_3:103;
        hence thesis by A6,A9,EXTREAL1:def 3;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th57: :: extension of ABSVALUE:11
  -|.x.| <= x & x <= |.x.|
proof
  per cases;
  suppose
A1: 0 <= x;
    0 <= |.x.| by Th51;
    hence thesis by A1,EXTREAL1:def 3;
  end;
  suppose
A2: not 0 <= x;
    then |.x.|=-x by EXTREAL1:def 3;
    hence thesis by A2;
  end;
end;

theorem Th58:
  |.x.| < y implies -y < x & x < y
proof
  assume
A1: |.x.| < y;
  per cases;
  suppose
A2: 0 <= x;
    then x < y by A1,EXTREAL1:def 3;
    hence thesis by A2;
  end;
  suppose
A3: not 0 <= x;
    then |.x.|=-x by EXTREAL1:def 3;
    hence thesis by A1,A3,XXREAL_3:71;
  end;
end;

theorem Th59:
  -y < x & x < y implies 0 < y & |.x.| < y
proof
  assume that
A1: -y < x and
A2: x < y;
  per cases;
  suppose
    0 <= x;
    hence thesis by A2,EXTREAL1:def 3;
  end;
  suppose
A3: not 0 <= x;
    -x < y by A1,XXREAL_3:71;
    hence thesis by A3,EXTREAL1:def 3;
  end;
end;

theorem Th60: :: extension of ABSVALUE:12
  -y <= x & x <= y iff |.x.| <= y
proof
A1: -y <= x & x <= y implies |.x.| <= y
  proof
    assume that
A2: -y <= x and
A3: x <= y;
    per cases;
    suppose
      0 <= x;
      hence thesis by A3,EXTREAL1:def 3;
    end;
    suppose
A4:   not 0 <= x;
      -x <= y by A2,XXREAL_3:71;
      hence thesis by A4,EXTREAL1:def 3;
    end;
  end;
  |.x.| <= y implies -y <= x & x <= y
  proof
    assume
A5: |.x.| <= y;
    per cases by A5,XXREAL_0:1;
    suppose
      |.x.| = y;
      hence thesis by Th57;
    end;
    suppose
      |.x.| < y;
      hence thesis by Th58;
    end;
  end;
  hence thesis by A1;
end;

theorem Th61: :: extension of ABSVALUE:13
  |.x + y.| <= |.x.| + |.y.|
proof
A1: -|.x.| <= x & -|.y.| <= y by Th57;
A2: x <= |.x.| & y <= |.y.| by Th57;
A6:     -|.x.|-|.y.| = -(|.x.|+|.y.|) by XXREAL_3:26;
  x + y <= |.x.| + |.y.| & -|.x.|+(-|.y.|) <= x + y by A1,A2,XXREAL_3:38;
  hence thesis by A6,Th60;
end;

theorem Th62:
  -infty < x & x < +infty & x <> 0 implies |.x.|*|. 1./x .| = 1.
proof
  assume that
A1: -infty < x & x < +infty and
A2: x <> 0;
  reconsider a = x as Real by A1,XXREAL_0:14;
A3: 1./x = 1/a by EXTREAL1:32,MESFUNC1:def 8;
  per cases;
  suppose
    0 <= x;
    then |.x.| = a & |. 1./x .| = 1/a by A3,
    EXTREAL1:def 3;
    then |.x.|*|. 1./x .| = a * (1/a) by EXTREAL1:13;
    hence thesis by A2,MESFUNC1:def 8,XCMPLX_1:107;
  end;
  suppose
A4: not 0 <= x;
    then
A5: |.x.| = -x by EXTREAL1:def 3
      .= -a by SUPINF_2:3;
    |. 1./x .| = -(1./x) by A3,A4,EXTREAL1:37,XREAL_1:144
      .= -(1/a) by A3,SUPINF_2:3;
    then |.x.|*|. 1./x .| = (-a) * (-(1/a)) by A5,EXTREAL1:13
      .= a * (1/a);
    hence thesis by A4,MESFUNC1:def 8,XCMPLX_1:107;
  end;
end;

theorem
  x = +infty or x = -infty implies |.x.|*|. 1./x .| = 0
proof
  assume x = +infty or x = -infty;
  then |. 1./x .| = 0 by EXTREAL1:def 3;
  hence thesis;
end;

theorem :: extension of ABSVALUE:15
  x <> 0 implies |. 1./x .| = 1./|.x.|
proof
  assume
A1: x <> 0;
  per cases;
  suppose
A2: x = +infty;
    then |. 1./x .| = 0 by EXTREAL1:def 3;
    hence thesis by A2,EXTREAL1:36;
  end;
  suppose
    x = -infty;
    then |. 1./x .| = 0 & |.x.| = +infty by EXTREAL1:def 3,XXREAL_3:5;
    hence thesis;
  end;
  suppose
A3: x <> +infty & x <> -infty;
A4: 0 < |.x.| by A1,Th52;
A5: x < +infty by A3,XXREAL_0:4;
    -infty <= x by XXREAL_0:5;
    then
A6: -infty < x by A3,XXREAL_0:1;
    then -(+infty) < x by XXREAL_3:def 3;
    then
A7: |.x.| < +infty by A5,Th59;
    |. 1./x .|*|.x.| = 1. by A1,A6,A5,Th62;
    hence thesis by A4,A7,XXREAL_3:99;
  end;
end;

theorem :: extension of ABSVALUE:16
  not((x=-infty or x=+infty) & (y=-infty or y=+infty)) & y<>0 implies |.
  x/y.| = |.x.|/|.y.|
proof
  assume that
A1: not((x=-infty or x=+infty) & (y=-infty or y=+infty)) and
A2: y<>0;
  per cases;
  suppose
A3: x = +infty;
    then
A4: y < +infty by A1,XXREAL_0:9,14;
A5: -infty < y by A1,A3,XXREAL_0:12,14;
    now
      per cases by A2;
      suppose
A6:     0 < y;
        then x / y = +infty by A3,A4,XXREAL_3:94;
        then
A7:     |.x/y.| = +infty by EXTREAL1:36;
A8:     |.x.| = +infty by A3,EXTREAL1:36;
        |.y.| = y by A6,EXTREAL1:36;
        hence thesis by A4,A6,A7,A8,XXREAL_3:94;
      end;
      suppose
A9:     y < 0;
        then |.y.| = -y by EXTREAL1:37;
        then
A10:    |.y.| < +infty by A5,XXREAL_3:5,40;
        x / y = -infty by A3,A5,A9,XXREAL_3:96;
        then
A11:    |.x/y.| = +infty by EXTREAL1:37,XXREAL_3:5;
        0 < |.y.| & |.x.| = +infty by A2,A3,Th52,EXTREAL1:36;
        hence thesis by A10,A11,XXREAL_3:94;
      end;
    end;
    hence thesis;
  end;
  suppose
A12: x = -infty;
    then
A13: y < +infty by A1,XXREAL_0:9,14;
A14: -infty < y by A1,A12,XXREAL_0:12,14;
    now
      per cases by A2;
      suppose
A15:    0 < y;
        then x / y = -infty by A12,A13,XXREAL_3:97;
        then
A16:    |.x/y.| = +infty by EXTREAL1:37,XXREAL_3:5;
A17:    |.x.| = +infty by A12,EXTREAL1:37,XXREAL_3:5;
        |.y.| = y by A15,EXTREAL1:36;
        hence thesis by A13,A15,A16,A17,XXREAL_3:94;
      end;
      suppose
A18:    y < 0;
        then |.y.| = -y by EXTREAL1:37;
        then
A19:    |.y.| < +infty by A14,XXREAL_3:5,40;
        x / y = +infty by A12,A14,A18,XXREAL_3:95;
        then
A20:    |.x/y.| = +infty by EXTREAL1:36;
        0 < |.y.| & |.x.| = +infty by A2,A12,Th52,EXTREAL1:37,XXREAL_3:5;
        hence thesis by A19,A20,XXREAL_3:94;
      end;
    end;
    hence thesis;
  end;
  suppose
    x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    now
      per cases;
      suppose
        y = +infty;
        then |.x/y.| = 0 & |.y.| = +infty by EXTREAL1:def 3;
        hence thesis;
      end;
      suppose
        y = -infty;
        then |.x/y.| = 0 & |.y.| = +infty by EXTREAL1:def 3,XXREAL_3:5;
        hence thesis;
      end;
      suppose
        y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        |.x.| = abs(a) & |.y.| = abs b by Th49;
        then
A21:    |.x.|/|.y.| = abs(a)/abs(b) by EXTREAL1:32;
        x / y = a / b by EXTREAL1:32;
        then |.x/y.| = abs(a/b) by Th49;
        hence thesis by A21,COMPLEX1:153;
      end;
    end;
    hence thesis;
  end;
end;

theorem Th66: :: extension of ABSVALUE:17
  |.x.| = |.-x.|
proof
  per cases;
  suppose
A1: 0 < x;
    then |.-x.| = -(-x) by EXTREAL1:37
      .= x;
    hence thesis by A1,EXTREAL1:36;
  end;
  suppose
A2: x < 0;
    then |.x.| = -x by EXTREAL1:37;
    hence thesis by A2,EXTREAL1:36;
  end;
  suppose
    x = 0;
    hence thesis;
  end;
end;

theorem Th67:
  |.+infty.| = +infty & |.-infty.| = +infty
proof
  thus |.+infty.| = +infty by EXTREAL1:36;
  --infty = +infty by XXREAL_3:23;
  hence thesis by EXTREAL1:37;
end;

theorem Th68: :: extension of ABSVALUE:18
  x is Real or y is Real implies |.x.|-|.y.| <= |.x-y.|
proof
  assume
A1: x is Real or y is Real;
  per cases by A1;
  suppose
A2: y is Real;
    then (x - y) + y = x by XXREAL_3:22;
    then
A3: |.x.| <= |.x-y.| + |.y.| by Th61;
    reconsider b = y as Real by A2;
    |.y.| = abs b by Th49;
    hence thesis by A3,XXREAL_3:45;
  end;
  suppose
    x is Real;
    then reconsider a = x as Real;
A4: |.x.| = abs a by Th49;
    now
      per cases;
      suppose
        y = +infty or y = -infty;
        then |.x.|-|.y.| < 0 by A4,Th67,XXREAL_3:13;
        hence thesis by Th51;
      end;
      suppose
        y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        x - y = a - b by SUPINF_2:5;
        then
A5:     |.x-y.| = abs(a-b) by Th49;
        |.y.| = abs b by Th49;
        then |.x.|-|.y.| = abs(a)-abs(b) by A4,SUPINF_2:5;
        hence thesis by A5,COMPLEX1:145;
      end;
    end;
    hence thesis;
  end;
end;

theorem :: extension of ABSVALUE:19
  not( x = +infty & y = +infty or x = -infty & y = -infty ) implies |.x-
  y.| <= |.x.| + |.y.|
proof
  assume
A1: not( x = +infty & y = +infty or x = -infty & y = -infty );
  per cases;
  suppose
A2: x = +infty;
    then x - y = +infty by A1,XXREAL_3:13;
    then
A3: |.x-y.| = +infty by EXTREAL1:36;
A4: 0 <= |.y.| by Th51;
    |.x.| = +infty by A2,EXTREAL1:36;
    hence thesis by A3,A4,XXREAL_3:def 2;
  end;
  suppose
A5: x = -infty;
A6: 0 <= |.y.| by Th51;
    x - y = -infty by A1,A5,XXREAL_3:14;
    hence thesis by A5,A6,Th67,XXREAL_3:def 2;
  end;
  suppose
A7: x <> +infty & x <> -infty;
    then reconsider a = x as Real by XXREAL_0:14;
    now
      per cases;
      suppose
A8:     y = +infty;
A9:     0 <= |.x.| by Th51;
        x - y = -infty by A7,A8,XXREAL_3:13;
        hence thesis by A8,A9,Th67,XXREAL_3:def 2;
      end;
      suppose
A10:    y = -infty;
A11:    0 <= |.x.| by Th51;
        x - y = +infty by A7,A10,XXREAL_3:14;
        hence thesis by A10,A11,Th67,XXREAL_3:def 2;
      end;
      suppose
        y <> +infty & y <> -infty;
        then reconsider b = y as Real by XXREAL_0:14;
        |.x.|=abs a & |.y.|=abs b by Th49;
        then
A12:    |.x.|+|.y.|=abs(a)+abs(b) by SUPINF_2:1;
        x - y = a - b by SUPINF_2:5;
        then |.x-y.| = abs(a-b) by Th49;
        hence thesis by A12,COMPLEX1:143;
      end;
    end;
    hence thesis;
  end;
end;

theorem :: extension of ABSVALUE:20
  |.|.x.|.| = |.x.|
proof
  0 <= |.x.| by Th51;
  hence thesis by EXTREAL1:def 3;
end;

theorem :: extension of ABSVALUE:21
  |.x.| <= z & |.y.| <= w implies |.x+y.| <= z + w
proof
  assume
A1: |.x.| <= z & |.y.| <= w;
  then -z <= x & -w <= y by Th60;
  then
A2: -z + -w <= x + y by XXREAL_3:38;
  x <= z & y <= w by A1,Th60;
  then
A3: x + y <= z + w by XXREAL_3:38;
  -z -w = -(z + w) by XXREAL_3:26;
  hence thesis by A3,A2,Th60;
end;

theorem :: extension of ABSVALUE:22
  x is Real or y is Real implies |.|.x.|-|.y.|.| <= |.x-y.|
proof
A1: |.y.|-|.x.| = -(|.x.|-|.y.|) by XXREAL_3:27;
  assume
A2: x is Real or y is Real;
  then
A3: |.x.|-|.y.| <= |.x-y.| by Th68;
  y - x = -(x - y) by XXREAL_3:27;
  then
A4: |.y-x.| = |.x-y.| by Th66;
  |.y.|-|.x.| <= |.y-x.| by A2,Th68;
  then -|.x-y.| <= -(-(|.x.|-|.y.|)) by A4,A1,XXREAL_3:40;
  hence thesis by A3,Th60;
end;

theorem :: extension of ABSVALUE:24
  0 <= x * y implies |.x+y.| = |.x.|+|.y.|
proof
  assume
A1: 0 <= x * y;
  per cases by A1;
  suppose
A2: (0 <= x or 0 < x) & (0 <= y or 0 < y);
    then |.x.| = x & |.y.| = y by EXTREAL1:def 3;
    hence thesis by A2,EXTREAL1:def 3;
  end;
  suppose
A3: (x <= 0 or x < 0) & (y <= 0 or y < 0);
    then
A4: |.x+y.| = -(x + y) by Th55
      .= -x -y by XXREAL_3:26;
    |.x.| = -x by A3,Th55;
    hence thesis by A3,A4,Th55;
  end;
end;

begin

canceled 6;

theorem :: extension of SQUARE_1:34
  x <> +infty & y <> +infty & not ( x = +infty & y = +infty or x =
  -infty & y = -infty ) implies min(x,y) = (x + y - |.x - y.|) / R_EAL 2
proof
  assume that
A1: x <> +infty and
A2: y <> +infty and
A3: not ( x = +infty & y = +infty or x = -infty & y = -infty );
  per cases;
  suppose
A4: x = -infty;
    then x + y = -infty & x - y = -infty by A2,A3,XXREAL_3:14,def 2;
    then
A5: x + y - |.x - y.| = -infty by Th67,XXREAL_3:14;
A6: min(x,y) = -infty by A4,XXREAL_0:44;
 R_EAL 2 = 2 by MEASURE6:def 1;
    hence thesis by A6,A5,XXREAL_3:97;
  end;
  suppose
    x <> -infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
    now
      per cases;
      suppose
A8:     y = -infty;
        then x + y = -infty & x - y = +infty by A1,A3,XXREAL_3:14,def 2;
        then
A9:     x + y - |.x - y.| = -infty by Th67,XXREAL_3:14;
A10:    min(x,y) = -infty by A8,XXREAL_0:44;
    R_EAL 2 = 2 by MEASURE6:def 1;
        hence thesis by A10,A9,XXREAL_3:97;
      end;
      suppose
        y <> -infty;
        then reconsider b = y as Real by A2,XXREAL_0:14;
        x - y = a - b by SUPINF_2:5;
        then x + y = a + b & |.x - y.| = abs(a - b) by Th49,SUPINF_2:1;
        then
A12:    x + y - |.x - y.| = a + b - abs(a - b) by SUPINF_2:5;
        R_EAL 2 = 2 by MEASURE6:def 1;
        then (x + y - |.x - y.|) / R_EAL 2 = (a+b-abs(a-b))/2 by A12,
        EXTREAL1:32;
        hence thesis by COMPLEX1:159;
      end;
    end;
    hence thesis;
  end;
end;

canceled 10;

theorem :: extension of SQUARE_1:45
  x <> -infty & y <> -infty & not ( x = +infty & y = +infty or x =
  -infty & y = -infty ) implies max(x,y) = (x + y + |.x - y.|) / R_EAL 2
proof
  assume that
A1: x <> -infty and
A2: y <> -infty and
A3: not ( x = +infty & y = +infty or x = -infty & y = -infty );
  per cases;
  suppose
A4: x = +infty;
    then x + y = +infty & x - y = +infty by A2,A3,XXREAL_3:13,def 2;
    then
A5: x + y + |.x - y.| = +infty by Th67,XXREAL_3:def 2;
A6: max(x,y) = +infty by A4,XXREAL_0:41;
 R_EAL 2 = 2 by MEASURE6:def 1;
    hence thesis by A6,A5,XXREAL_3:94;
  end;
  suppose
    x <> +infty;
    then reconsider a = x as Real by A1,XXREAL_0:14;
    now
      per cases;
      suppose
A8:     y = +infty;
        then x + y = +infty & x - y = -infty by A1,A3,XXREAL_3:13,def 2;
        then
A9:     x + y + |.x - y.| = +infty by Th67,XXREAL_3:def 2;
A10:    max(x,y) = +infty by A8,XXREAL_0:41;
    R_EAL 2 = 2 by MEASURE6:def 1;
        hence thesis by A10,A9,XXREAL_3:94;
      end;
      suppose
        y <> +infty;
        then reconsider b = y as Real by A2,XXREAL_0:14;
        x - y = a - b by SUPINF_2:5;
        then x + y = a + b & |.x - y.| = abs(a - b) by Th49,SUPINF_2:1;
        then
A12:    x + y + |.x - y.| = a + b + abs(a - b) by SUPINF_2:1;
        R_EAL 2 = 2 by MEASURE6:def 1;
        then (x + y + |.x - y.|) / R_EAL 2 = (a+b+abs(a-b))/2 by A12,
        EXTREAL1:32;
        hence thesis by COMPLEX1:160;
      end;
    end;
    hence thesis;
  end;
end;

definition
  let x,y be R_eal;
  redefine func max(x,y) -> R_eal;
  coherence by XXREAL_0:def 1;
  redefine func min(x,y) -> R_eal;
  coherence by XXREAL_0:def 1;
end;

canceled 8;

theorem :: extension of SQUARE_1:53
  min(x,y) + max(x,y)= x + y
proof
  per cases;
  suppose
A1: x <= y;
    then min(x,y) = x by XXREAL_0:def 9;
    hence thesis by A1,XXREAL_0:def 10;
  end;
  suppose
A2: not x <= y;
    then min(x,y) = y by XXREAL_0:def 9;
    hence thesis by A2,XXREAL_0:def 10;
  end;
end;

begin ::Addenda
:: missing, 2007.07.20, A.T.

theorem Th101:
  |.x.| = +infty implies x = +infty or x = -infty
proof
A1: |.x.| = x or -|.x.| = --x by EXTREAL1:def 3;
  assume |.x.| = +infty;
  hence thesis by A1,XXREAL_3:5;
end;

theorem
  |.x.| < +infty iff x in REAL
proof
  thus |.x.| < +infty implies x in REAL by Th67,XXREAL_0:14;
  assume
A1: x in REAL;
  assume |.x.| >= +infty;
  hence contradiction by A1,Th101,XXREAL_0:4;
end;
