:: Euler's {T}heorem and Small {F}ermat's Theorem
::  by Yoshinori Fujisawa , Yasushi Fuwa and Hidetaka Shimizu
::
:: Received June 10, 1998
:: Copyright (c) 1998 Association of Mizar Users

environ

 vocabularies INT_1, FINSEQ_1, INT_2, ARYTM_3, ABSVALUE, ARYTM_1, NAT_1,
      RELAT_1, FUNCT_1, RFINSEQ, SQUARE_1, BOOLE, CARD_1, EULER_1, FINSET_1,
      FILTER_0, CARD_3, GROUP_1, ARYTM;
 notations TARSKI, XBOOLE_0, SUBSET_1, NUMBERS, XCMPLX_0, XXREAL_0, CARD_1,
      RELAT_1, FUNCT_1, RECDEF_1, INT_1, INT_2, FINSET_1, NAT_1, NAT_D, NEWTON,
      EULER_1, CLASSES1, RFINSEQ, RVSUM_1, FINSEQ_1, FINSEQ_3, TREES_4,
      WSIERP_1;
 constructors REAL_1, SQUARE_1, NAT_1, NAT_D, NEWTON, RFINSEQ, WSIERP_1,
      EULER_1, SEQ_1, RECDEF_1, CLASSES1;
 registrations FUNCT_1, ORDINAL1, RELSET_1, NUMBERS, XXREAL_0, XREAL_0, NAT_1,
      INT_1, FINSEQ_1, SEQM_3, NEWTON, XBOOLE_0, VALUED_0, FINSET_1;
 requirements REAL, NUMERALS, BOOLE, SUBSET, ARITHM;
 definitions TARSKI, FUNCT_1, XBOOLE_0, RELAT_1;
 theorems INT_2, FINSEQ_1, NAT_1, ABSVALUE, INT_1, FUNCT_1, EULER_1, PREPOWER,
      RFINSEQ, FINSEQ_2, RVSUM_1, FINSEQ_3, NEWTON, RELAT_1, XBOOLE_1,
      XCMPLX_1, XREAL_1, FINSEQ_5, XXREAL_0, ORDINAL1, NAT_D, CLASSES1;
 schemes NAT_1, FINSEQ_1;

begin :: Preliminary

reserve a,b,m,x,n,l,xi,xj for Nat,
  t,z for Integer,
  f,F for FinSequence of NAT;

Lm1: t < 1 iff t <= 0
proof
  t < 1 implies t <= 0
  proof
    assume
A1: t < 1;
    assume
A2: t > 0;
    then reconsider t as Element of NAT by INT_1:16;
    t >= 1 by A2,NAT_1:14;
    hence contradiction by A1;
  end;
  hence thesis;
end;

Lm2: a <> 0 implies a-1 >= 0
proof
  assume a <> 0;
  then a >= 1 by NAT_1:14;
  then a-1 >= 1-1 by XREAL_1:11;
  hence thesis;
end;

Lm3: 1 gcd z = 1
proof
  thus 1 gcd z = abs 1 gcd abs(z) by INT_2:51
    .= 1 gcd abs z by ABSVALUE:def 1
    .= 1 by NEWTON:64;
end;

::  Very useful theorem

theorem
  a,(b qua Integer) are_relative_prime iff a,b are_relative_prime;

theorem Th2:
  m > 1 & m*t >= 1 implies t >= 1
proof
  assume
A1: m > 1 & m*t >= 1;
  assume t < 1;
  then t <= 0 by Lm1;
  hence contradiction by A1;
end;

Lm4: m > 1 & 1-m <= z & z <= m-1 & m divides z implies z = 0
proof
  assume
A1: m > 1 & 1-m <= z & z <= m-1 & m divides z;
  assume
A2: z <> 0;
  consider t being Integer such that
A3: z = m*t by A1,INT_1:def 9;
A4: t <> 0 by A2,A3;
  now per cases by A4;
    suppose
A5:   t > 0;
      then reconsider t as Element of NAT by INT_1:16;
      m*t >= m by A5,NAT_1:14,NEWTON:45;
      then m*t+1 > m by NAT_1:13;
      hence contradiction by A1,A3,XREAL_1:21;
    end;
    suppose
A6:   t < 0;
      1 <= m*t+m*1 by A1,A3,XREAL_1:22;
      then
A7:   1 <= m*(t+1);
      reconsider r = (t+1) as Integer;
      r >= 1 by A1,A7,Th2;
      then 1-1 <= t by XREAL_1:22;
      hence contradiction by A6;
    end;
  end;
  hence contradiction;
end;

canceled 2;

theorem Th5:
  a <> 0 & b <> 0 & m <> 0 & a,m are_relative_prime & b,m are_relative_prime
  implies m,a*b mod m are_relative_prime
proof
  assume
A1: a <> 0 & b <> 0 & m <> 0 &
  a,m are_relative_prime & b,m are_relative_prime;
  then
A2: a*b,m are_relative_prime by EULER_1:15;
  consider t being Nat such that
A3: a*b = m*t+(a*b mod m) and (a*b mod m) < m by A1,NAT_D:def 2;
  a*b <> a*0 by A1,XCMPLX_1:5;
  then
A4: a*b+(-t)*m gcd m = a*b gcd m by A1,EULER_1:17;
  a*b gcd m = 1 by A2,INT_2:def 4;
  hence thesis by A3,A4,INT_2:def 4;
end;

theorem Th6:
  m > 1 & b <> 0 &
  m,n are_relative_prime & a,m are_relative_prime & n = a*b mod m
  implies m,b are_relative_prime
proof
  assume
A1: m > 1 & b <> 0 & m,n are_relative_prime
  & a,m are_relative_prime & n = a*b mod m;
  assume not m,b are_relative_prime;
  then
A2: m gcd b <> 1 by INT_2:def 4;
  set k = m gcd b;
A3: k divides m & k divides b by NAT_D:def 5;
  then consider km being Nat such that
A4: m = k*km by NAT_D:def 3;
A5: k <> 0 & km <> 0 by A1,A4;
  reconsider km as Element of NAT by ORDINAL1:def 13;
  consider kb being Nat such that
A6: b = k*kb by A3,NAT_D:def 3;
  consider p being Nat such that
A7: a*(k*kb) = (k*km)*p+(a*(k*kb) mod (k*km)) &
  (a*(k*kb) mod (k*km)) < k*km by A1,A4,NAT_D:def 2;
A8: (a*(k*kb) mod (k*km)) = k*((a*kb)-(km*p)) by A7;
  set tm = (a*kb)-(km*p);
A9: tm = 0 implies m gcd n <> 1 by A1,A4,A6,A8,NEWTON:65;
A10: k <> -1 by NAT_1:2;
  tm <> 0 implies m gcd n <> 1
  proof
    assume tm <> 0;
    then m gcd n = k*(km gcd tm) by A1,A4,A5,A6,A8,EULER_1:16;
    hence thesis by A2,A10,INT_1:22;
  end;
  hence contradiction by A1,A9,INT_2:def 4;
end;

theorem Th7:
  (m mod n) mod n = m mod n
proof
  per cases;
  suppose n <> 0;
    then ex t being Nat st m = n*t+(m mod n) & m mod n < n by NAT_D:def 2;
    hence thesis by NAT_D:24;
  end;
  suppose
A1: n = 0;
    hence (m mod n) mod n = 0 by NAT_D:def 2
      .= m mod n by A1,NAT_D:def 2;
  end;
end;

theorem
  (l+m) mod n = ((l mod n)+(m mod n)) mod n
proof
  thus (l+m) mod n = ((l mod n)+m) mod n by NAT_D:22
    .= ((l mod n)+(m mod n)) mod n by NAT_D:23;
end;

theorem Th9:
  (l*m) mod n = (l*(m mod n)) mod n
proof
  per cases;
  suppose n <> 0;
    then consider t being Nat such that
A1: m = n*t+(m mod n) & (m mod n)<n by NAT_D:def 2;
    (l*m) mod n =((l*t)*n+l*(m mod n)) mod n by A1;
    hence thesis by NAT_D:21;
  end;
  suppose
A2: n = 0;
    hence (l*m) mod n = 0 by NAT_D:def 2
      .= (l*(m mod n)) mod n by A2,NAT_D:def 2;
  end;
end;

theorem
  (l*m) mod n = ((l mod n)*m) mod n by Th9;

theorem Th11:
  (l*m) mod n = ((l mod n)*(m mod n)) mod n
proof
  (l*m) mod n = (l*(m mod n)) mod n by Th9;
  hence thesis by Th9;
end;

begin :: Function of Element of NAT*(FinSequence of NAT)

definition
  let f,a;
  redefine func a*f -> FinSequence of NAT;
  coherence
  proof
    rng(a*f) c= NAT
    proof
      let x be set;
      assume x in rng (a*f);
      then consider xx being set such that
A1:   xx in dom (a*f) & x = (a*f).xx by FUNCT_1:def 5;
      reconsider xx as Element of NAT by A1;
      (a*f).xx = a*(f.xx) by RVSUM_1:66;
      hence thesis by A1;
    end;
    hence thesis by FINSEQ_1:def 4;
  end;
end;

canceled 13;

theorem Th25:
  for R1,R2 be FinSequence of NAT
  st R1,R2 are_fiberwise_equipotent holds Product R1 = Product R2
proof
  defpred P[Nat] means
  for f,g be FinSequence of NAT st f,g are_fiberwise_equipotent & len f = $1
  holds Product f = Product g;
A1: P[0]
  proof
    let f,g be FinSequence of NAT;
    assume f,g are_fiberwise_equipotent & len f = 0;
    then len g = 0 & f = <*>NAT by FINSEQ_1:32,RFINSEQ:16;
    hence thesis by FINSEQ_1:32;
  end;
A2: for n st P[n] holds P[n+1]
  proof
    let n;
    assume
A3: P[n];
    let f,g be FinSequence of NAT;
    assume
A4: f,g are_fiberwise_equipotent & len f = n+1;
    then
A5: rng f = rng g & len f = len g by CLASSES1:83,RFINSEQ:16;
    0+1<=n+1 by NAT_1:13;
    then
A6: n+1 in dom f by A4,FINSEQ_3:27;
    reconsider a = f.(n+1) as Element of NAT;
    a in rng g by A5,A6,FUNCT_1:def 5;
    then consider m being Nat such that
A7: m in dom g & g.m = a by FINSEQ_2:11;
    reconsider m as Element of NAT by ORDINAL1:def 13;
A8: g = (g|m)^(g/^m) by RFINSEQ:21;
A9: 1<=m & m<=len g by A7,FINSEQ_3:27;
    then max(0,m-1) = m-1 by FINSEQ_2:4;
    then reconsider m1 = m-1 as Element of NAT by FINSEQ_2:5;
    set gg = g/^m, gm = g|m;
A10: len gm = m by A9,FINSEQ_1:80;
A11: m = m1+1;
    m in Seg m by A9,FINSEQ_1:3;
    then gm.m = a & m in dom g by A7,RFINSEQ:19;
    then
A12: gm = (gm|m1)^<*a*> by A10,A11,RFINSEQ:20;
    m1<=m by A11,NAT_1:11;
    then
A13: Seg m1 c= Seg m by FINSEQ_1:7;
A14: gm|m1 = gm|(Seg m1) by FINSEQ_1:def 15
      .= (g|(Seg m))|(Seg m1) by FINSEQ_1:def 15
      .= g|((Seg m)/\(Seg m1)) by RELAT_1:100
      .= g|(Seg m1) by A13,XBOOLE_1:28
      .= g|m1 by FINSEQ_1:def 15;
    set fn = f|n;
A15: len fn = n by A4,FINSEQ_1:80,NAT_1:11;
A16: f = fn ^ <*a*> by A4,RFINSEQ:20;
    now
      let x be set;
      card Coim(f,x) = card Coim(g,x) by A4,CLASSES1:def 9;
      then card(fn"{x})+card(<*a*>"{x}) = card(((g|m1)^<*a*>^(g/^m))"{x})
      by A8,A12,A14,A16,FINSEQ_3:63
        .= card(((g|m1)^<*a*>)"{x})+card((g/^m)"{x}) by FINSEQ_3:63
        .= card((g|m1)"{x})+card(<*a*>"{x})+card((g/^m)"{x}) by FINSEQ_3:63
        .= card((g|m1)"{x})+card((g/^m)"{x})+card(<*a*>"{x})
        .= card(((g|m1)^(g/^m))"{x})+card(<*a*>"{x}) by FINSEQ_3:63;
      hence card Coim(fn,x) = card Coim((g|m1)^(g/^m),x);
    end;
    then fn, (g|m1)^(g/^m) are_fiberwise_equipotent by CLASSES1:def 9;
    then Product fn = Product((g|m1)^gg) by A3,A15;
    hence Product f = Product((g|m1)^gg)*Product <*a*> by A16,RVSUM_1:127
      .= Product(g|m1)*Product gg*Product <*a*> by RVSUM_1:127
      .= Product(g|m1)*Product <*a*>*Product gg
      .= Product gm*Product gg by A12,A14,RVSUM_1:127
      .= Product g by A8,RVSUM_1:127;
  end;
A17: for n holds P[n] from NAT_1:sch 2(A1,A2);
  let R1,R2 be FinSequence of NAT;
  assume
A18: R1,R2 are_fiberwise_equipotent;
  len R1 = len R1;
  hence thesis by A17,A18;
end;

begin :: Modulus for Finite Sequence of Natural

::  Function of (FinSequence of NAT) mod Element of NAT

definition
  let f be FinSequence of NAT,m be Nat;
  func f mod m -> FinSequence of NAT means
  :Def1:
  len(it) = len(f) & for i being Nat st i in dom f holds it.i = (f.i) mod m;
  existence
  proof
    deffunc F(Nat) = (f.$1) mod m;
    consider g be FinSequence such that
A1: len g = len f and
A2: for k being Nat st k in dom g holds g.k = F(k) from FINSEQ_1:sch 2;
    rng g c= NAT
    proof
      let x be set;
      assume x in rng g;
      then consider y being set such that
A3:   y in dom g & x = g.y by FUNCT_1:def 5;
      reconsider y as Element of NAT by A3;
      g.y = (f.y) mod m by A2,A3;
      hence thesis by A3;
    end;
    then reconsider g as FinSequence of NAT by FINSEQ_1:def 4;
    take g;
    thus len g = len f by A1;
    let k be Nat;
    assume k in dom f;
    then k in dom g by A1,FINSEQ_3:31;
    hence thesis by A2;
  end;
  uniqueness
  proof
    let fa,fb be FinSequence of NAT such that
A5: len(f) = len(fa) &
    for i being Nat st i in dom f holds fa.i = (f.i) mod m and
A6: len(f) = len(fb) & for i being Nat st i in dom f holds fb.i = (f.i) mod m;
    now
      let X be set such that
A7:   dom f = X;
A8:   dom f = Seg len f by FINSEQ_1:def 3
        .= dom fa by A5,FINSEQ_1:def 3;
A9:   dom f = Seg len f by FINSEQ_1:def 3
        .= dom fb by A6,FINSEQ_1:def 3;
      for i being Nat st i in X holds fa.i = fb.i
      proof
        given j being Nat such that
A10:    j in X & fa.j <> fb.j;
        j in X & fa.j <> (f.j) mod m by A6,A7,A10;
        hence contradiction by A5,A7;
      end;
      hence thesis by A7,A8,A9,FINSEQ_1:17;
    end;
    hence thesis;
  end;
end;

theorem
  for f be FinSequence of NAT st m <> 0 holds
  (Product(f mod m)) mod m = (Product f) mod m
proof
  defpred P[Nat] means
  for f be FinSequence of NAT st m <> 0 & len f = $1 holds
  (Product(f mod m)) mod m = (Product f) mod m;
A1: P[0]
  proof
    let f be FinSequence of NAT;
    assume
A2: m <> 0 & len f=0;
    then
A3: f = <*> NAT by FINSEQ_1:32;
    len (f mod m) = 0 by A2,Def1;
    hence thesis by A3,FINSEQ_1:32;
  end;
A4: for n st P[n] holds P[n+1]
  proof
    let n;
    assume
A5: P[n];
    let f be FinSequence of NAT;
    assume
A6: m <> 0 & len f = n+1;
    reconsider fn = f|n as FinSequence of NAT;
A7: n <= len f by A6,NAT_1:11;
A8: len fn = n by A6,FINSEQ_1:80,NAT_1:11;
A9: (Product f) mod m = (Product (fn ^ <* f.(n+1) *>)) mod m by A6,RFINSEQ:20
      .= ((Product fn)*f.(n+1)) mod m by RVSUM_1:126;
A10: len (f mod m) = n+1 by A6,Def1;
A11: (f mod m)|n = fn mod m
    proof
A12:  len((f mod m)|n) = n by A10,FINSEQ_1:80,NAT_1:11;
      then
A13:  len((f mod m)|n) = len(fn mod m) by A8,Def1;
      for i being Nat st 1 <= i & i <= len ((f mod m)|n) holds
      ((f mod m)|n).i = (fn mod m).i
      proof
        let i be Nat;
        assume
A14:    1 <= i & i <= len ((f mod m)|n);
        then
A15:    i in Seg n by A12,FINSEQ_1:3;
        then
A16:    ((f mod m)|Seg n).i = (f mod m).i by FUNCT_1:72;
        i <= len f by A7,A12,A14,XXREAL_0:2;
        then
A17:    i in dom f by A14,FINSEQ_3:27;
A18:    (f|n).i = ((f|Seg n).i) by FINSEQ_1:def 15;
        i in dom fn by A8,A12,A14,FINSEQ_3:27;
        then (fn mod m).i = fn.i mod m by Def1
          .= f.i mod m by A15,A18,FUNCT_1:72
          .= (f mod m).i by A17,Def1;
        hence thesis by A16,FINSEQ_1:def 15;
      end;
      hence thesis by A13,FINSEQ_1:18;
    end;
    0+1<=n+1 by NAT_1:13;
    then n+1 in dom f by A6,FINSEQ_3:27;
    then (f mod m).(n+1) = f.(n+1) mod m by Def1;
    then f mod m = (fn mod m) ^ <* f.(n+1) mod m *> by A10,A11,RFINSEQ:20;
    then Product (f mod m) mod m
    = ((Product (fn mod m))*(f.(n+1) mod m)) mod m by RVSUM_1:126
      .= (((Product (fn mod m)) mod m)*((f.(n+1) mod m) mod m)) mod m by Th11
      .= (((Product fn) mod m)*((f.(n+1) mod m) mod m)) mod m by A5,A6,A8
      .= (((Product fn) mod m)*(f.(n+1) mod m)) mod m by Th7;
    hence thesis by A9,Th11;
  end;
A19: for n holds P[n] from NAT_1:sch 2(A1,A4);
  let f be FinSequence of NAT;
  assume
A20: m <> 0;
  len f is Element of NAT;
  hence thesis by A19,A20;
end;

theorem Th27:
  a <> 0 & m > 1 & n <> 0 &
  a*n mod m = n mod m & m,n are_relative_prime implies a mod m = 1
proof
  assume
A1: a <> 0 & m > 1 & n <> 0 & a*n mod m = n mod m & m,n are_relative_prime;
  then consider k1 be Nat such that
A2: a*n = m*k1+(a*n mod m) and (a*n mod m) < m by NAT_D:def 2;
  consider k2 be Nat such that
A3: n = m*k2+(n mod m) and n mod m < m by A1,NAT_D:def 2;
  (a-1)*n = m*(k1-k2) by A1,A2,A3;
  then
A4: m divides (a-1)*n by INT_1:def 9;
  reconsider t = (a-1),m as Integer;
  m divides t by A1,A4,INT_2:40;
  then consider tt be Integer such that
A5: (a-1) = m*tt by INT_1:def 9;
A6: a = m*tt+1 by A5;
  a-1 >= 0 by A1,Lm2;
  then tt >= 0 by A1,A5,XREAL_1:160;
  then reconsider tt,m as Element of NAT by INT_1:16;
  a mod m = (((m*tt) mod m)+1) mod m by A6,NAT_D:22
    .= (0+1) mod m by NAT_D:13
    .= 1 by A1,NAT_D:24;
  hence thesis;
end;

theorem
  (F mod m) mod m = F mod m
proof
A1: dom ((F mod m) mod m) = Seg(len((F mod m) mod m)) by FINSEQ_1:def 3
    .= Seg(len(F mod m)) by Def1
    .= Seg(len F) by Def1
    .= dom F by FINSEQ_1:def 3;
A2: dom (F mod m) = Seg(len(F mod m)) by FINSEQ_1:def 3
    .= Seg(len F) by Def1
    .= dom F by FINSEQ_1:def 3;
  for x being set st x in dom F holds ((F mod m) mod m).x = (F mod m).x
  proof
    let x be set;
    assume
A3: x in dom F;
    then reconsider x as Element of NAT;
    ((F mod m) mod m).x = ((F mod m).x) mod m by A2,A3,Def1
      .= (F.x mod m) mod m by A3,Def1
      .= F.x mod m by Th7
      .= (F mod m).x by A3,Def1;
    hence thesis;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

theorem
  (a*(F mod m)) mod m = (a*F) mod m
proof
A1: dom ((a*(F mod m)) mod m)
  = Seg(len((a*(F mod m)) mod m)) by FINSEQ_1:def 3
    .= Seg(len(a*(F mod m))) by Def1
    .= Seg(len(F mod m)) by NEWTON:6
    .= Seg(len F) by Def1
    .= dom F by FINSEQ_1:def 3;
A2: dom (a*(F mod m)) = Seg(len(a*(F mod m))) by FINSEQ_1:def 3
    .= Seg(len(F mod m)) by NEWTON:6
    .= Seg(len F) by Def1
    .= dom F by FINSEQ_1:def 3;
A3: dom ((a*F) mod m) = Seg(len((a*F) mod m)) by FINSEQ_1:def 3
    .= Seg(len(a*F)) by Def1
    .= Seg(len F) by NEWTON:6
    .= dom F by FINSEQ_1:def 3;
A4: dom (a*F) = Seg(len(a*F)) by FINSEQ_1:def 3
    .= Seg(len F) by NEWTON:6
    .= dom F by FINSEQ_1:def 3;
  for x being set st x in dom F holds
  ((a*(F mod m)) mod m).x = ((a*F) mod m).x
  proof
    let x be set;
    assume
A5: x in dom F;
    then reconsider x as Element of NAT;
    ((a*(F mod m)) mod m).x = (a*(F mod m)).x mod m by A2,A5,Def1
      .= (a*(F mod m).x ) mod m by RVSUM_1:66
      .= (a*(F.x mod m)) mod m by A5,Def1
      .= (a*F.x) mod m by Th9
      .= (a*F).x mod m by RVSUM_1:66
      .= ((a*F) mod m).x by A4,A5,Def1;
    hence thesis;
  end;
  hence thesis by A1,A3,FUNCT_1:9;
end;

theorem
  for F,G being FinSequence of NAT holds (F ^ G) mod m = (F mod m) ^ (G mod m)
proof
  let F,G be FinSequence of NAT;
A1: dom ((F ^ G) mod m) = Seg(len((F ^ G) mod m)) by FINSEQ_1:def 3
    .= Seg(len(F ^ G)) by Def1
    .= dom (F ^ G) by FINSEQ_1:def 3;
A2: dom ((F mod m) ^ (G mod m))
  = Seg(len((F mod m) ^ (G mod m))) by FINSEQ_1:def 3
    .= Seg(len(F mod m)+len(G mod m)) by FINSEQ_1:35
    .= Seg(len(F)+len(G mod m)) by Def1
    .= Seg(len(F)+len(G)) by Def1
    .= Seg(len(F ^ G)) by FINSEQ_1:35
    .= dom (F ^ G) by FINSEQ_1:def 3;
A3: dom (F mod m) = Seg(len(F mod m)) by FINSEQ_1:def 3
    .= Seg(len F) by Def1
    .= dom F by FINSEQ_1:def 3;
A4: dom (G mod m) = Seg(len(G mod m)) by FINSEQ_1:def 3
    .= Seg(len G) by Def1
    .= dom G by FINSEQ_1:def 3;
  for x being set st x in dom (F ^ G)
  holds ((F ^ G) mod m).x = ((F mod m) ^ (G mod m)).x
  proof
    let x be set;
    assume
A5: x in dom (F ^ G);
    then reconsider x as Element of NAT;
    now per cases by A5,FINSEQ_1:38;
      suppose
A6:     x in dom F;
A7:     ((F ^ G) mod m).x = (F ^ G).x mod m by A5,Def1
          .= F.x mod m by A6,FINSEQ_1:def 7;
        ((F mod m) ^ (G mod m)).x = (F mod m).x by A3,A6,FINSEQ_1:def 7
          .= F.x mod m by A6,Def1;
        hence ((F ^ G) mod m).x = ((F mod m) ^ (G mod m)).x by A7;
      end;
      suppose ex n being Nat st n in dom G & x=len F+n;
        then consider n being Element of NAT such that
A8:     n in dom G & x=len F+n;
A9:     len (F mod m) = len F by Def1;
A10:    ((F ^ G) mod m).x = (F ^ G).x mod m by A5,Def1
          .= G.n mod m by A8,FINSEQ_1:def 7;
        ((F mod m) ^ (G mod m)).x = (G mod m).n by A4,A8,A9,FINSEQ_1:def 7
          .= G.n mod m by A8,Def1;
        hence ((F ^ G) mod m).x = ((F mod m) ^ (G mod m)).x by A10;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

theorem
  for F,G being FinSequence of NAT holds
  (a*(F ^ G)) mod m = ((a*F) mod m) ^ ((a*G) mod m)
proof
  let F,G be FinSequence of NAT;
  reconsider FG = F ^ G as FinSequence of NAT;
A1: dom ((a*(F ^ G)) mod m) = Seg(len((a*FG) mod m)) by FINSEQ_1:def 3
    .= Seg(len(a*FG)) by Def1
    .= Seg(len(FG)) by NEWTON:6
    .= dom (F ^ G) by FINSEQ_1:def 3;
A2: dom (((a*F) mod m) ^ ((a*G) mod m))
  = Seg(len(((a*F) mod m) ^ ((a*G) mod m))) by FINSEQ_1:def 3
    .= Seg(len((a*F) mod m)+len((a*G) mod m)) by FINSEQ_1:35
    .= Seg(len(a*F)+len((a*G) mod m)) by Def1
    .= Seg(len(a*F)+len(a*G)) by Def1
    .= Seg(len(F)+len(a*G)) by NEWTON:6
    .= Seg(len(F)+len(G)) by NEWTON:6
    .= Seg(len(F ^ G)) by FINSEQ_1:35
    .= dom(F ^ G) by FINSEQ_1:def 3;
A3: dom (a*(F ^ G)) = Seg(len(a*FG)) by FINSEQ_1:def 3
    .= Seg(len(FG)) by NEWTON:6
    .= dom (F ^ G) by FINSEQ_1:def 3;
A4: dom (a*F) = Seg(len(a*F)) by FINSEQ_1:def 3
    .= Seg(len F) by NEWTON:6
    .= dom F by FINSEQ_1:def 3;
A5: dom ((a*F) mod m) = Seg(len((a*F) mod m)) by FINSEQ_1:def 3
    .= Seg(len (a*F)) by Def1
    .= Seg(len(F)) by NEWTON:6
    .= dom F by FINSEQ_1:def 3;
A6: dom (a*G) = Seg(len(a*G)) by FINSEQ_1:def 3
    .= Seg(len G) by NEWTON:6
    .= dom G by FINSEQ_1:def 3;
A7: dom ((a*G) mod m) = Seg(len((a*G) mod m)) by FINSEQ_1:def 3
    .= Seg(len (a*G)) by Def1
    .= Seg(len(G)) by NEWTON:6
    .= dom G by FINSEQ_1:def 3;
  for x being set st x in dom (F ^ G)
  holds ((a*(F ^ G)) mod m).x = (((a*F) mod m) ^ ((a*G) mod m)).x
  proof
    let x be set;
    assume
A8: x in dom (F ^ G);
    reconsider H = F ^ G as FinSequence of NAT;
    reconsider x as Element of NAT by A8;
    now per cases by A8,FINSEQ_1:38;
      suppose
A9:     x in dom F;
A10:    ((a*(F ^ G)) mod m).x = ((a*(F ^ G)).x) mod m by A3,A8,Def1
          .= (a*H.x) mod m by RVSUM_1:66
          .= (a*(F.x)) mod m by A9,FINSEQ_1:def 7;
        (((a*F) mod m) ^ ((a*G) mod m)).x
        = ((a*F) mod m).x by A5,A9,FINSEQ_1:def 7
          .= ((a*F).x) mod m by A4,A9,Def1
          .= (a*(F.x)) mod m by RVSUM_1:66;
        hence ((a*(F ^ G)) mod m).x = (((a*F) mod m) ^ ((a*G) mod m)).x by A10;
      end;
      suppose ex n being Nat st n in dom G & x=len F+n;
        then consider n being Element of NAT such that
A11:    n in dom G & x=len F+n;
A12:    len ((a*F) mod m) = len(a*F) by Def1
          .= len F by NEWTON:6;
A13:    ((a*(F ^ G)) mod m).x = ((a*(F ^ G)).x) mod m by A3,A8,Def1
          .= (a*H.x) mod m by RVSUM_1:66
          .= (a*G.n) mod m by A11,FINSEQ_1:def 7;
        (((a*F) mod m) ^ ((a*G) mod m)).x = ((a*G) mod m).n by A7,A11,A12,
FINSEQ_1:def 7
          .= ((a*G).n) mod m by A6,A11,Def1
          .= (a*G.n) mod m by RVSUM_1:66;
        hence ((a*(F ^ G)) mod m).x = (((a*F) mod m) ^ ((a*G) mod m)).x by A13;
      end;
    end;
    hence thesis;
  end;
  hence thesis by A1,A2,FUNCT_1:9;
end;

::  Function of (Element of NAT) |^ (Element of NAT)

theorem
  a <> 0 & m <> 0 & a,m are_relative_prime implies
  for b holds a |^ b,m are_relative_prime
proof
  assume
A1: a <> 0 & m <> 0 & a,m are_relative_prime;
A2: (a |^ 0) gcd m = 1 gcd m by NEWTON:9;
  defpred P[Nat] means (a |^ $1),m are_relative_prime;
  m gcd 1 = 1 by NEWTON:64;
  then
A3: P[0] by A2,INT_2:def 4;
A4: for b holds P[b] implies P[b+1]
  proof
    let b;
    assume
A5: (a |^ b),m are_relative_prime;
A6: (a |^ (b+1)) = (a |^ b)*(a |^ 1) by NEWTON:13
      .= (a |^ b)*a by NEWTON:10;
    a |^ b <> 0 by A1,PREPOWER:12;
    hence thesis by A1,A5,A6,EULER_1:15;
  end;
  for b holds P[b] from NAT_1:sch 2(A3,A4);
  hence thesis;
end;

begin :: Euler's Theorem and Small Fermat's Theorem

theorem Th33:
  a <> 0 & m > 1 & a,m are_relative_prime implies (a |^ Euler m) mod m = 1
proof
  assume
A1: a <> 0 & m > 1 & a,m are_relative_prime;
  set X = {k where k is Element of NAT :
  m,k are_relative_prime & k >= 1 & k <= m};
  set Y = {l where l is Element of NAT :
  ex u being Element of NAT st l = a*u mod m & u in X};

::Begin Euler's Lemma1
A2: x in X implies a*x mod m in X
  proof
    assume x in X;
    then consider t being Element of NAT such that
A3: t = x and
A4: m,t are_relative_prime and
A5: t >= 1 & t <= m;
A6: a*t,m are_relative_prime by A1,A4,A5,EULER_1:15;
A7: m,a*t mod m are_relative_prime by A1,A4,A5,Th5;
    a*t mod m <> 0
    proof
      assume a*t mod m = 0;
      then consider s being Nat such that
A8:   a*t = m*s+0 and 0 < m by A1,NAT_D:def 2;
      s gcd 1 = 1 by NEWTON:64;
      then m*s gcd m*1 = m by EULER_1:6;
      hence contradiction by A1,A6,A8,INT_2:def 4;
    end;
    then
A9: a*t mod m >= 1 by NAT_1:14;
    a*t mod m <= m by A1,NAT_D:1;
    hence thesis by A3,A7,A9;
  end;

::End of Euler's Lemma1
::Begin Euler's Lemma2
A10: xi in X & xj in X & xi <> xj implies a*xi mod m <> a*xj mod m
  proof
    assume
A11: xi in X & xj in X & xi <> xj;
    assume
A12: a*xi mod m = a*xj mod m;
    set tm = a*xi mod m,sm = a*xj mod m;
    consider t being Element of NAT such that
A13: t = xi and m,t are_relative_prime and
A14: t >= 1 & t <= m by A11;
A15: tm = (a*t)-m*((a*t) div m) by A1,A13,NEWTON:77;
    consider s being Element of NAT such that
A16: s = xj and m,s are_relative_prime and
A17: s >= 1 & s <= m by A11;
    sm = (a*s)-m*((a*s) div m) by A1,A16,NEWTON:77;
    then 0 = a*(t-s)-m*(((a*t) div m)-((a*s) div m)) by A12,A15;
    then
A18: m divides a*(t-s) by INT_1:def 9;
    reconsider m,c = (t-s) as Integer;
    1-m <= c & c <= m-1 by A14,A17,XREAL_1:15;
    then t-s = 0 by A1,A18,Lm4,INT_2:40;
    hence contradiction by A11,A13,A16;
  end;

::End of Euler's Lemma2
::Begin Euler's Lemma3
A19: X = Y
  proof
    thus X c= Y
    proof
      let x be set;
      assume x in X;
      then consider xx being Element of NAT such that
A20:  x = xx and
A21:  m,xx are_relative_prime and
A22:  xx >= 1 & xx <= m;
      xx <> m by A1,A21,EULER_1:2;
      then xx < m by A22,XXREAL_0:1;
      then
A23:  xx mod m = xx by NAT_D:24;
      consider i,j being Integer such that
A24:  i*a+j*m = xx by A1,EULER_1:11;
A25:  i = (i div m)*m+(i mod m) by A1,NEWTON:80;
A26:  i mod m <> 0
      proof
        assume i mod m = 0;
        then
A27:    0 = i-(i div m)*m by A1,INT_1:def 8;
        then xx = m*((i div m)*a+j) by A24;
        then
A28:    ((i div m)*a+j) <> 0 & 1 <> 0 by A22;
        m gcd xx = m*1 gcd m*((i div m)*a+j) by A24,A27
          .= m*(1 gcd ((i div m)*a+j)) by A1,A28,EULER_1:16
          .= m*1 by Lm3
          .= m;
        hence contradiction by A1,A21,INT_2:def 4;
      end;
      reconsider im = i mod m as Element of NAT by INT_1:16,NEWTON:78;
A29:  im >= 1 by A26,NAT_1:14;
A30:  im <= m by A1,NEWTON:79;
      reconsider ij = (i mod m)*a+(((i div m)*a)+j)*m as Element of NAT
      by A24,A25;
A31:  ij mod m = im*a mod m by EULER_1:13;
      then m,im are_relative_prime by A1,A21,A23,A24,A25,A26,Th6;
      then im in X by A29,A30;
      hence thesis by A20,A23,A24,A25,A31;
    end;
    let y be set;
    assume y in Y;
    then consider yy being Element of NAT such that
A32: y = yy and
A33: ex u being Element of NAT st yy = a*u mod m & u in X;
    consider uu being Element of NAT such that
A34: yy = a*uu mod m and
A35: uu in X by A33;
    thus thesis by A2,A32,A34,A35;
  end;

::End of Euler'S Lemma3
  reconsider FX = Sgm X as FinSequence of NAT;
  reconsider FYY = (a*FX) as FinSequence of NAT;
  reconsider FY = FYY mod m as FinSequence of NAT;

::Begin Euler's Lemma4
A36: X c= Seg m
  proof
    let x be set;
    assume x in X;
    then consider xx being Element of NAT such that
A37: x = xx and m,xx are_relative_prime and
A38: xx >= 1 & xx <= m;
    thus thesis by A37,A38,FINSEQ_1:3;
  end;

::End of Euler's Lemma4
  then reconsider X as finite set;
A39: len(FX) = card X by A36,FINSEQ_3:44
    .= Euler m by EULER_1:def 1;
A40: dom FY = Seg(len FY) by FINSEQ_1:def 3
    .= Seg(len FYY) by Def1
    .= Seg(len FX) by NEWTON:6;
  then
A41: dom FX = dom FY by FINSEQ_1:def 3;
  dom FYY = Seg(len FYY) by FINSEQ_1:def 3
    .= Seg(len FX) by NEWTON:6;
  then
A42: dom FX = dom FYY by FINSEQ_1:def 3;

::Begin Euler's Lemma5
A43: rng FY = Y
  proof
    thus rng FY c= Y
    proof
      let x be set;
      assume x in rng FY;
      then consider y being set such that
A44:  y in dom FY & x = FY.y by FUNCT_1:def 5;
      reconsider y as Element of NAT by A44;
      y in dom FX by A40,A44,FINSEQ_1:def 3;
      then
A45:  FY.y = (FYY.y) mod m by A42,Def1
        .= (a*FX.y) mod m by RVSUM_1:66;
      FX.y in rng FX by A41,A44,FUNCT_1:def 5;
      then FX.y in X by A36,FINSEQ_1:def 13;
      hence thesis by A44,A45;
    end;
    let y be set;
    assume y in Y;
    then consider yy being Element of NAT such that
A46: y = yy and
A47: ex u being Element of NAT st yy = a*u mod m & u in X;
    consider uu being Element of NAT such that
A48: yy = a*uu mod m and
A49: uu in X by A47;
    uu in rng FX by A36,A49,FINSEQ_1:def 13;
    then consider xx being set such that
A50: xx in dom FX & uu = FX.xx by FUNCT_1:def 5;
    reconsider xx as Element of NAT by A50;
    FY.xx = (FYY.xx) mod m by A42,A50,Def1
      .= y by A46,A48,A50,RVSUM_1:66;
    hence thesis by A41,A50,FUNCT_1:def 5;
  end;

::End of Euler's Lemma5
::Begin Euler's Lemma6
A51: FY is one-to-one
  proof
    let x1,x2 be set;
    assume
A52: x1 in dom FY & x2 in dom FY & FY.x1 = FY.x2;
    then
A53: x1 in dom FX & x2 in dom FX & FY.x1 = FY.x2 by A40,FINSEQ_1:def 3;
    reconsider x1 as Element of NAT by A52;
    reconsider x2 as Element of NAT by A52;
A54: FY.x1 = (FYY.x1) mod m by A42,A53,Def1
      .= (a*FX.x1) mod m by RVSUM_1:66;
A55: FY.x2 = (FYY.x2) mod m by A42,A53,Def1
      .= (a*FX.x2) mod m by RVSUM_1:66;
A56: FX.x1 in rng FX by A41,A52,FUNCT_1:def 5;
A57: FX.x2 in rng FX by A41,A52,FUNCT_1:def 5;
A58: FX is one-to-one by A36,FINSEQ_3:99;
    not(FX.x1 in X & FX.x2 in X & FX.x1 <> FX.x2) by A10,A52,A54,A55;
    hence thesis by A36,A53,A56,A57,A58,FINSEQ_1:def 13,FUNCT_1:def 8;
  end;

::End of Euler's Lemma6
::Begin Euler's Lemma7
  for x be set holds card Coim(FX,x) = card Coim(FY,x)
  proof
    let x be set;
    now per cases;
      suppose
A59:    x in X;
        then
A60:    x in rng FX by A36,FINSEQ_1:def 13;
        FX is one-to-one by A36,FINSEQ_3:99;
        then
A61:    len(FX-{x}) = len(FX)-1 by A60,FINSEQ_3:97;
A62:    len(FX-{x})-len(FX-{x})
        = len(FX)-card(FX"{x})-len(FX-{x}) by FINSEQ_3:66;
A63:    len(FY-{x}) = len(FY)-1 by A19,A43,A51,A59,FINSEQ_3:97;
        len(FY-{x}) = len(FY)-card(FY"{x}) by FINSEQ_3:66;
        hence thesis by A61,A62,A63;
      end;
      suppose
A64:    not(x in X);
        then
A65:    not(x in rng FX) by A36,FINSEQ_1:def 13;
        FY"{x} = {} by A19,A43,A64,FUNCT_1:142;
        hence thesis by A65,FUNCT_1:142;
      end;
    end;
    hence thesis;
  end;

::End of Euler's Lemma7
  then
A66: FX,FY are_fiberwise_equipotent by CLASSES1:def 9;
  then
A67: (Product FX) mod m = (Product FY) mod m by Th25;
A68: len(FX) = len(FY) by A66,RFINSEQ:16;

::Begin Euler's Lemma8
A69: (Product FY) mod m = (a |^ len(FY)*Product FX) mod m
  proof
    defpred P[Nat] means $1 <= len(FY) implies
    (Product (FY|$1)) mod m = (a |^ len(FY|$1)*Product (FX|$1)) mod m;
A70: P[0]
    proof
      0 <= len FY;
      then len (FY|0) = 0 by FINSEQ_1:80;
      hence thesis by NEWTON:9,RVSUM_1:124;
    end;
A71: for n st P[n] holds P[n+1]
    proof
      let n;
      now per cases;
        suppose
A72:      n+1 <= len FY;
          assume
A73:      P[n];
A74:      n <= n+1 by NAT_1:11;
          then
A75:      len(FY|n) = n by A72,FINSEQ_1:80,XXREAL_0:2;
A76:      len (FY|(n+1)) = n+1 by A72,FINSEQ_1:80;
          then
A77:      FY|(n+1) = (FY|(n+1))|n ^ <* (FY|(n+1)).(n+1) *> by RFINSEQ:20;
          0+1 <= n+1 by NAT_1:13;
          then
A78:      n+1 in dom FY by A72,FINSEQ_3:27;
A79:      (FY|(n+1))|n = FY|n by A74,FINSEQ_5:80;
A80:      (FY|(n+1)).(n+1) = FY.(n+1) by FINSEQ_3:121;
          len (FX|(n+1)) = n+1 by A68,A72,FINSEQ_1:80;
          then
A81:      FX|(n+1) = (FX|(n+1))|n ^ <* (FX|(n+1)).(n+1) *> by RFINSEQ:20;
A82:      (FX|(n+1))|n = FX|n by A74,FINSEQ_5:80;
A83:      (FX|(n+1)).(n+1) = FX.(n+1) by FINSEQ_3:121;
          (Product(FY|(n+1))) mod m = (Product(FY|n)*FY.(n+1)) mod m
          by A77,A79,A80,RVSUM_1:126
            .= (((a |^ len(FY|n)*Product(FX|n)) mod m)
          *(FY.(n+1) mod m)) mod m by A72,A73,A74,Th11,XXREAL_0:2
            .= (((a |^ len(FY|n)*Product(FX|n)) mod m)
          *((FYY.(n+1) mod m) mod m)) mod m by A41,A42,A78,Def1
            .= (((a |^ len(FY|n)*Product(FX|n)) mod m)
          *((a*FX).(n+1) mod m)) mod m by Th7
            .= ((a |^ len(FY|n)*Product(FX|n))*(a*FX).(n+1)) mod m by Th11
            .= (a |^ n*Product (FX|n))*(a*(FX.(n+1))) mod m by A75,RVSUM_1:66
            .= (a |^ n*a)*(Product(FX|n)*(FX.(n+1))) mod m
            .= (a |^ len(FY|(n+1)))*(Product(FX|n)*(FX.(n+1))) mod m
          by A76,NEWTON:11
            .= (a |^ len(FY|(n+1)))*(Product(FX|(n+1))) mod m by A81,A82,A83,
RVSUM_1:126;
          hence P[n+1];
        end;
        suppose n+1 > len FY;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
A84: for n holds P[n] from NAT_1:sch 2(A70,A71);
    FY|len FY = FY|(Seg len FY) by FINSEQ_1:def 15;
    then
A85: FY = FY|len FY by FINSEQ_2:23;
    FX|len FX = FX|(Seg len FX) by FINSEQ_1:def 15;
    then FX = FX|len FY by A68,FINSEQ_2:23;
    hence thesis by A84,A85;
  end;

::End of Euler's Lemma8
A86: len FY = Euler m by A39,A66,RFINSEQ:16;
A87: a |^ Euler m <> 0 by A1,PREPOWER:12;

::Begin Euler's Lemma9
A88: Product FX <> 0
  proof
    assume Product FX = 0;
    then consider k being Nat such that
A89: k in dom FX and
A90: FX.k = 0 by RVSUM_1:133;
A91: rng FX = X by A36,FINSEQ_1:def 13;
    FX.k in rng FX by A89,FUNCT_1:def 5;
    then consider p being Element of NAT such that
A92: FX.k = p and m,p are_relative_prime and
A93: p >= 1 & p <= m by A91;
    thus contradiction by A90,A92,A93;
  end;

::End of Euler's Lemma9
::Begin Euler's Lemma10
  (Product FX),m are_relative_prime
  proof
    defpred P[Nat] means
    $1 <= len FX implies (Product (FX|$1)),m are_relative_prime;
A94: P[0]
    proof
      (Product (FX|0)) gcd m = 1 by NEWTON:64,RVSUM_1:124;
      hence thesis by INT_2:def 4;
    end;
A95: for n st P[n] holds P[n+1]
    proof
      let n;
      now per cases;
        suppose
A96:      len FX >= n+1;
          assume
A97:      P[n];
A98:      n <= n+1 by NAT_1:11;
          reconsider FF = (FX|(n+1)) as FinSequence of NAT;
          reconsider ff = FF.(n+1) as Element of NAT;
          len FF = n+1 by A96,FINSEQ_1:80;
          then
A99:      FF = (FF|n) ^ <*ff*> by RFINSEQ:20;
A100:     rng FX = X by A36,FINSEQ_1:def 13;
A101:     (FX|(n+1)).(n+1) = FX.(n+1) by FINSEQ_3:121;
          0+1 <= (n+1) by XREAL_1:8;
          then n+1 in dom FX by A96,FINSEQ_3:27;
          then (FX|(n+1)).(n+1) in rng FX by A101,FUNCT_1:def 5;
          then consider p being Element of NAT such that
A102:     ff = p and
A103:     m,p are_relative_prime and p >= 1 & p <= m by A100;
          reconsider a = (Product (FF|n)),b = ff,m' = m as Integer;
          a,m' are_relative_prime by A96,A97,A98,FINSEQ_5:80,XXREAL_0:2;
          then a*b,m' are_relative_prime by A102,A103,INT_2:41;
          hence P[n+1] by A99,RVSUM_1:126;
        end;
        suppose len FX < n+1;
          hence thesis;
        end;
      end;
      hence thesis;
    end;
A104: for n holds P[n] from NAT_1:sch 2(A94,A95);
    FX|len(FX) = FX by FINSEQ_1:79;
    hence thesis by A104;
  end;
  hence thesis by A1,A67,A69,A86,A87,A88,Th27;
end;

::End of Euler's Theorem

theorem ::Small Fermat's theorem
  a <> 0 & m is prime & a,m are_relative_prime implies
  (a |^ m) mod m = a mod m
proof
  assume
A1: a<>0 & m is prime & a,m are_relative_prime;
  then
A2: m > 1 by INT_2:def 5;
  then m-1 > 1-1 by XREAL_1:11;
  then reconsider mm = m-1 as Element of NAT by INT_1:16;
A3: mm+1 = m;
  Euler m = m-1 by A1,EULER_1:21;
  then ((a |^ mm) mod m)*a = 1*a by A1,A2,Th33;
  then ((a |^ mm)*a) mod m = a mod m by Th9;
  hence thesis by A3,NEWTON:11;
end;

